PDS71    TITLE 'P D S  --  PDS COMMAND PROCESSOR               6/10/85'
*   DATA SET PDS AT LEVEL 7.1 ON   6/10/85  (ABL -- THIS VERSION)     *
*   DATA SET PDS AT LEVEL 7 AS OF  1/15/85  (SDS -- MEMLIST)          *
*   DATA SET PDS AT LEVEL 6 AS OF  7/16/83  (ABL -- MVS/XA VERSION)   *
*   DATA SET PDS AT LEVEL 5 AS OF  7/08/82  (ABL -- MEMBER GROUPS)    *
*   DATA SET PDS AT LEVEL 4 AS OF  9/30/80  (ABL -- MVS ONLY VERSION) *
*   DATA SET PDS AT LEVEL 3 AS OF  1/28/80  (ABL -- MVS VERSION)      *
*   DATA SET PDS AT LEVEL 2 AS OF 11/14/77  (ABL -- MVT VERSION)      *
*   DATA SET PDS AT LEVEL 1 AS OF  6/12/75  (UCLA VERSION)            *
*   DATA SET PDS AT LEVEL 0 AS OF  3/20/72  (FIREMAN'S FUND VERSION)  *
*                                                                     *
***                                                                   *
*** BEFORE ASSEMBLING, THE COPY CODE IN PDSGEN71 SHOULD BE MODIFIED   *
*** TO REFLECT RESOURCES AVAILABLE AT YOUR INSTALLATION.              *
***                                                                   *
*** NOTE: PDS CAN BE LINKED INTO SYS1.LPALIB TO REDUCE EACH PDS       *
***       USER'S SWAP LOAD; HOWEVER, YOU SHOULD ALSO KEEP A COPY      *
***       OF PDS IN YOUR LINK LIST (WITH THE SAME OR A DIFFERENT      *
***       MEMBER NAME) AS A BACKUP IN CASE YOUR LPALIB IS REPLACED    *
***       IN A FUTURE SYSTEM.                                         *
***                                                                   *
         TITLE 'P D S  --  PDS SUPPORT REPRESENTATIVE          6/10/85'
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
*                                                                     *
*                                                                     *
* PLEASE REPORT ANY PROBLEMS, ENHANCEMENTS, SUGGESTIONS OR COMMENTS   *
* CONCERNING THE PDS COMMAND TO BRUCE LELAND AT:                      *
*                                                                     *
*     HITACHI AMERICA, LTD.     OR         6084 CRIMSON DRIVE         *
*     2210 O'TOOLE AVENUE                  SAN JOSE, CALIF 95120      *
*     SAN JOSE, CALIF. 95131                                          *
*     (408) 435-2143                                                  *
*                                                                     *
*                                                                     *
*                                                                     *
*  IF AN ERROR OR ABEND CONDITION PERSISTS IN PDS, TRY THE            *
*  FOLLOWING STEPS FOR PROBLEM RESOLUTION:                            *
*                                                                     *
*                                                                     *
*   1.  CONTACT THE IBM SUPPORT CENTER FOR KNOWN MVS PROBLEMS         *
*                                                                     *
*                                                                     *
*   2.  REPORT THE PROBLEM (WHETHER RESOLVED OR NOT) TO BRUCE LELAND. *
*       PLEASE INCLUDE APPROPRIATE DOCUMENTATION FOR UNRESOLVED       *
*       PROBLEMS AS FOLLOWS:                                          *
*                                                                     *
*       A.  PLEASE USE AN UNLABELLED, 1600 OR 6250 BPI TAPE WITH      *
*           AS MANY FILES AS REQUIRED FOR DOCUMENTATION.              *
*                                                                     *
*       B.  INDICATE IF YOU HAVE SPF, ISPF VERSION 1 OR 2 AND IF      *
*           THE PROBLEM INVOLVES THE SPF INTERFACE CODE IN PDS.       *
*                                                                     *
*       C.  IF POSSIBLE, DUPLICATE THE ERROR ON PAPER BY EXECUTING    *
*           PDS IN BACKGROUND MODE.                                   *
*                                                                     *
*       D.  NOTE YOUR MVS OPERATING SYSTEM LEVEL (FOR EXAMPLE,        *
*           SP 1.3.3 AT 8402).                                        *
*                                                                     *
*       E.  NOTE THE PDS LEVEL (OR BETTER YET, UNLOAD A COPY OF THE   *
*           PDS SOURCE CODE INSTALLED AT YOUR INSTALLATION).          *
*                                                                     *
*       F.  UNLOAD A COPY OF THE PDS LOAD MODULE USING IEBCOPY.       *
*                                                                     *
*       G.  UNLOAD THE DATA SET IN QUESTION WITH IEBCOPY OR AN        *
*           APPROPRIATE UTILITY.                                      *
*                                                                     *
*       H.  BE SURE TO INCLUDE YOUR NAME AND PHONE NUMBER SO THAT     *
*           ANY RESULTS OR QUESTIONS CAN BE REFERRED BACK TO YOU.     *
*                                                                     *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         TITLE 'P D S  --  PDS COMMAND PURPOSE                 6/10/85'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* TITLE -      P D S     --    TSO PDS COMMAND PROCESSOR              *
*                                                                     *
* FUNCTION -   PROVIDE A TSO USER WITH THE CAPABILITY                 *
*              TO MANIPULATE A PARTITIONED DATA SET                   *
*                                                                     *
* OPERATION -  ACCEPT FROM THE TSO USER THE NAME OF A PARTITIONED     *
*              DATA SET AND PERFORM ANY OF SEVERAL SUBCOMMANDS        *
*                                                                     *
* WRITTEN    - BY TOM SPRINGER AT FIREMAN'S FUND (NOW WITH IBM)       *
* MODIFIED   - BY WILLIAM FINKELSTEIN, WITH CITIBANK IN LOS ANGELES   *
* MODIFIED   - BY STEVEN SMITH, WITH SECURITY PACIFIC NATIONAL BANK   *
* MAINTAINED - BY BRUCE LELAND, WITH HITACHI AMERICA, LTD.            *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'P D S  --  PDS COMMAND CHANGE DOCUMENTATION    6/10/85'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  CHANGE SECTION:                                                    *
*                                                                     *
****VER 2.0: ADDED FORMATTING OPTIONS FOR LIST;                    ****
*            ADDED EDIT, FIND AND SUBMIT SUBCOMMANDS.                 *
*                                                                     *
****VER 2.2: CORRECTED AN ERROR IN LIST AND FIND FOR DATA SETS     ****
*       WITH ASA OR MACHINE CARRIAGE CONTROLS.                        *
*                                                                     *
****VER 2.3: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY DINESH      ****
*       DATTANI FROM PRUDENTIAL INSURANCE CO. OF AMERICA, TORONTO     *
*       1.  OUTPUT THE PROGRAM OPTION LIST IN SORTED ORDER.           *
*       2.  ADDED SEVERAL EDIT DESCRIPTIVE QUALIFIERS.                *
*                                                                     *
****VER 3.0: UPDATED THE PDS COMMAND FOR MVS USE BASED ON A        ****
*       VERSION OF THE PDS COMMAND ON THE CBT MODS TAPE.              *
*       1.  ADDED EXEC (EXPLICIT AND IMPLIED) AND HELP SUBCOMMANDS.   *
*       2.  A SINGLE CHARACTER SUBCOMMAND NAME IS NOT AMBIGUOUS.      *
*       3.  SUPPORT WAS ADDED IN THE FOLLOWING AREAS:                 *
*           -- SSI INFORMATION DISPLAY                                *
*           -- APF DATA CAN BE LISTED AND MODIFIED                    *
*           -- HEXADECIMAL MEMBER NAMES ALLOWED AS INPUT              *
*           -- HEXADECIMAL OUTPUT ADDED - DISPLAY, PATTERN, MEMBERS   *
*           -- $PUTGET, $PUTLINE AND MESSAGE MACROS WERE ADDED        *
*           -- ATTRIB LISTS ALL ALIASES FOR A GIVEN MODULE            *
*       4.  SEVERAL SOURCES OF 0C7'S WERE CORRECTED FOR IDR DATA.     *
*       5.  THE MEMBER NAME VALIDITY CHECK ROUTINE HAD TWO BUGS:      *
*           --  THE MEMBER NAME LENGTH USED WAS THE INPUT NAME        *
*               LENGTH (INCORRECT FOR HEXADECIMAL NAMES)              *
*           --  PROMPTING FOR INVALID MEMBER NAMES WAS INCORRECT      *
*       6.  THE ATTRIB SUBCOMMAND SOMETIMES LISTED SSI INFORMATION    *
*           FOR NON-LOAD MODULES WHICH DID NOT HAVE ANY SSI DATA.     *
*                                                                     *
****VER 3.1: SEVERAL GENERAL CHANGES WERE MADE                     ****
*       1.  THE PATTERN SUBCOMMAND WAS ADDED.                         *
*       2.  DEFAULT MEMBER NAMES ARE ALLOWED FOR ATTRIB, BROWSE,      *
*           DELETE, DIRENTRY, DSPRINT, EDIT, FSE, FIND, HISTORY, IF,  *
*           LIST, MAP, MEMBERS, OUTCOPY, PRINTOFF, REVIEW, SPFEDIT,   *
*           SUBMIT AND TSOLIST SUBCOMMANDS.                           *
*       3.  PDS WAS ADDED AS AN ALIAS FOR THE CHANGE SUBCOMMAND.      *
*       4.  ALIAS SUBCOMMAND IS NOT PREVENTED FROM WORKING IF THE     *
*           APF DATA LENGTH IS INVALID (APF=0 IS USED).               *
*       5.  EDIT PREVIOUSLY USED THE PREVIOUS PDL FOR DEFAULT         *
*           MEMBERS; NOW THE EDIT WORK AREA IS SAVED BEFORE EACH      *
*           SUBCOMMAND SCAN.                                          *
*                                                                     *
****VER 4.0: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY SEYMOUR     ****
*        METZ FROM COMNET IN WASHINGTON, D.C.:                        *
*        1.  REPLACED IBM/360 INSTRUCTIONS WITH THEIR IBM/370         *
*            EQUIVALENTS (ICM, STCM, MVCL, ...)                       *
*        2.  REPLACED BCR INSTRUCTIONS WITH EQUIVALENT MNEMONICS.     *
*        3.  TSO SERVICE ROUTINE ADDRESSES ARE NOW OBTAINED FROM      *
*            THE CVT INSTEAD OF VIA LOAD/DELETE CODE.                 *
*                                                                     *
****VER 4.1: SEVERAL ADDITIONAL CHANGES WERE MADE TO TAKE          ****
*        ADVANTAGE OF MVS PROGRAM LOGIC AND SERVICE ROUTINES:         *
*        1.  USED THE DAIRFAIL MESSAGE ROUTINE INSTEAD OF INTERNALLY  *
*            GENERATING MESSAGES AS SUGGESTED BY SEYMOUR METZ.        *
*        2.  USED THE GENERAL FAIL MESSAGE ROUTINE FOR PARSE ERRORS.  *
*        3.  USED THE TSO DEFAULT ROUTINE TO FULLY QUALIFY DSNAMES.   *
*        4.  CHANGED THE ALLOCATION STRATEGY TO "PERMANENTLY"         *
*            ALLOCATE DATA SETS SO THAT EXTERNAL PDS SUBCOMMANDS      *
*            CAN USE AN UNCATALOGED DATA SET AFTER IT IS ALLOCATED    *
*            BY PDS.  THIS CHANGE WAS SUGGESTED BY JOE RAREY          *
*            WITH MARTIN-MARRIETA IN FLORIDA.                         *
*                                                                     *
****VER 4.2: AFTER THE DAIRFAIL CHANGES INDICATED ABOVE, THE       ****
*            VOLUME PARAMETER FOR UNCATALOGED DATA SETS DID NOT WORK. *
*                                                                     *
****VER 4.3: CORRECTED ONE SOURCE OF 0C4'S IN LISTING HISTORY      ****
*            DATA AS REPORTED BY ARNIE CASINGHINO FROM CBT.           *
*                                                                     *
****VER 4.4: ADDED THE RESTORE SUBCOMMAND TO RESURRECT DELETED     ****
*            PDS MEMBERS.  ALSO, CLEANED UP SOME PDS CODE AS          *
*            SUGGESTED BY SEYMOUR METZ FROM COMNET AND ADDED A        *
*            SYSPARM ASSEMBLY PARAMETER FOR GENERATING PDS.           *
*                                                                     *
****VER 4.5: ADDED THE FOLLOWING SUBCOMMANDS                       ****
*                                                                     *
*            TSOLIST: A SUBCOMMAND TO LINK TO THE TSO LIST COMMAND    *
*            AS SUGGESTED BY SEYMOUR METZ.                            *
*                                                                     *
*            PRINTOFF: A SUBCOMMAND TO LINK TO THE PRINTOFF COMMAND   *
*            AS SUGGESTED BY DAVID FILSINGER OF FACSO (SHARE CODE CB) *
*                                                                     *
*            DELETE:  NEW, PREFERRED ALIAS FOR THE SCRATCH SUBCOMMAND *
*                                                                     *
****VER 5.0: ADDED ANOTHER BASE REGISTER AND MADE SEVERAL          ****
*            SUBCOMMAND CHANGES:                                      *
*                                                                     *
*        ATTRIB, BROWSE, DELETE, DIRENTRY, DSPRINT, EDIT, FIND, FSE,  *
*        HISTORY, IF, LIST, MAP, MEMBERS, OUTCOPY, PRINTOFF, REVIEW,  *
*        SPFEDIT, SUBMIT AND TSOLIST:                                 *
*          1.  ADDED THE * NOTATION (MEANS USE PREVIOUS MEMBER NAME)  *
*          2.  ADDED MEMBER NAME GROUPS (RANGES AND PATTERNS)         *
*          3.  ENTER "HELP GROUP" FOR MORE INFORMATION                *
*                                                                     *
*        BROWSE AND SPFEDIT: ADDED SUBCOMMANDS WHICH MAKE PDS A SPF   *
*        DIALOG SO IT CAN USE BROWSE AND EDIT SERVICE FOR A MEMBER.   *
*        HELP ON FULL SCREEN, SPF AND ISPF INTERFACES WAS PROVIDED    *
*        BY MIKE LOOS WITH DELUXE CHECK PRINTERS IN MINNESOTA.        *
*                                                                     *
*        ATTRIB:                                                      *
*          1.  FORMAT AND DISPLAY ANY SPF STATISTICS IN SPF FORMAT.   *
*          2.  DISPLAY ENTRY POINT ADDRESS, MODULE SIZE AND THE LAST  *
*              LINKAGE-EDIT DATE FOR LOAD MODULES.                    *
*          3.  FIXED A STORAGE OVERLAY FOR MEMBERS WITH MANY ALIASES. *
*          4.  ADDED THE PAGE AND NOPAGE KEYWORDS.                    *
*                                                                     *
*        ALIAS:                                                       *
*          1.  NOW WORKS FOR SCATTER LOADED MEMBERS                   *
*          2.  IF A MODULE IS CREATED BY THE OS/VS LINKAGE EDITOR     *
*              AND HAS SSI INFORMATION, THE ALIAS WILL ALSO.          *
*          3.  AN ALIAS CAN NOW BE ASSIGNED TO AN ALIAS MEMBER.       *
*          4.  FIXED A BUG: AN ASSIGNED ENTRY POINT OF ZERO           *
*              SOMETIMES DID NOT GET THE ENTRY POINT ZERO FLAG SET.   *
*                                                                     *
*        EDIT: ALLOWS EDITING OF NEW MEMBERS.                         *
*                                                                     *
*        RESTORE: NOW OPERATES ON LOAD MODULES.                       *
*                                                                     *
*        ATTRIB AND MAP: IF NO MAIN MEMBER NAME CAN BE FOUND FOR AN   *
*        ALIAS IN A LOAD LIBRARY, THE NAME STORED IN THE DIRECTORY    *
*        ENTRY AS THE MAIN ENTRY IS DISPLAYED.                        *
*                                                                     *
*        FIND: ADDED FIRST/NOFIRST KEYWORDS AND HEXADECIMAL STRINGS.  *
*                                                                     *
*        FIND AND LIST:                                               *
*          1.  ADDED SUPPORT FOR RECORD FORMAT U DATA SETS.           *
*          2.  ADDED SUPPORT FOR NON-PARTITIONED DATA SETS.           *
*          3.  ADDED LBLOCK, LDUMP, BLOCK AND DUMP FORMATS.           *
*          4.  ADDED SKIPREC, MAXIN, MAXOUT, SKIPCOL AND MAXLEN.      *
*          5.  NUM FORMAT CHECKS FOR DATA SAVED BY SPF EDIT.          *
*          6.  NUM FORMAT CHECKS FOR NUMERIC LINE NUMBERS AND         *
*              SWITCHES TO NONUM MODE FOR THE REMAINDER OF A          *
*              MEMBER IF AN INVALID LINE NUMBER IS ENCOUNTERED.       *
*                                                                     *
*        PATTERN AND DISPLAY:  UNPRINTABLE LETTERS ARE DISPLAYED AS   *
*        PERIODS.  ALSO, HEXADECIMAL/CHARACTER DISPLAYS ARE FORCED    *
*        FOR ANY MEMBER NAME WHICH IS NOT VALID.  NOTE: VALID MEMBER  *
*        NAMES CONTAIN UPPER-CASE ALPHANUMERIC CHARACTERS (WHICH      *
*        INCLUDES #, $ AND @) AND THE FIRST CHARACTER OF A VALID      *
*        MEMBER NAME IS UPPER-CASE ALPHABETIC.                        *
*                                                                     *
*        CHANGE AND PDS:                                              *
*          1.  IF A DSNAME IS NOT ENTERED WITH THE SUBCOMMAND,        *
*              THE PREVIOUSLY USED DATA SET IS ASSUMED.               *
*          2.  ADDED THE SHR AND OLD DATA SET KEYWORDS.               *
*                                                                     *
*        DIRENTRY: SUBCOMMAND WAS ADDED TO DUMP A DIRECTORY ENTRY.    *
*                                                                     *
*        MEMBERS: SUBCOMMAND WAS ADDED TO DISPLAY A MEMBER GROUP.     *
*                                                                     *
*        RESTORE, LIST, MAP, HISTORY AND ALIAS: FIXED A BUG; THE      *
*        NEWEXTENT SUBROUTINE ALLOWED THE USE OF ONE TOO MANY EXTENTS.*
*                                                                     *
****VER 5.1:  MADE SOME CODE CLEANUP CHANGES;                      ****
*                                                                     *
*        DELETED THE READ OF THE JFCB FOR THE INPUT DATA SET.         *
*                                                                     *
*        EXCP'S ARE NOW ALL IN AN EXCP SUBROUTINE; SET AND READ       *
*        SECTORS ARE PERFORMED FOR ALL DISK DRIVES EXCEPT 2314'S.     *
*                                                                     *
*                                                                     *
*        AN OBTAIN IS ALWAYS PERFORMED ONCE FOR EACH INPUT DATA       *
*        SET (NOT WHEN NEEDED AS BEFORE IN RESTORE AND USAGE).        *
*                                                                     *
*        PDS BLDL'S ARE PERFORMED BEFORE INVOKING THE PROCESSING      *
*        SUBROUTINE.  IN ADDITION, PDS MAINTAINS THE STATUS OF THE    *
*        CURRENT BLDL BUT NO LONGER ATTEMPTS TO AVOID BLDL'S.         *
*                                                                     *
*        ADDED A RESTORE SUBCOMMAND TEST FACILITY:  AN EXISTING       *
*        MEMBER MAY BE "RESTORED" IF THE RESTORE MEMBER NAME BEGINS   *
*        WITH "TEMP".  USE THE DIRENTRY SUBCOMMAND TO COMPARE THEIR   *
*        DIRECTORY ENTRIES (REMEMBER TO DELETE THE TEMP MEMBER SINCE  *
*        YOU HAVE AN APPARENT ALIAS -- NEITHER HAS AN ALIAS BIT SET). *
*                                                                     *
****VER 5.2:  ADDED SUPPORT FOR TRACK OVERFLOW RECORDS AS WELL     ****
*        AS THE FOLLOWING OTHER CHANGES:                              *
*                                                                     *
*        CHANGE AND PDS: IF A VOLUME NAME IS SUPPLIED, SYSALLDA IS    *
*        USED AS THE UNIT NAME; OTHERWISE, THE UNIT NAME FROM THE     *
*        PSCB WOULD BE USED (WHICH MIGHT NOT COVER THE VOLUME TO BE   *
*        ALLOCATED).  REPORTED BY BILL COWAN WITH THE COCA-COLA       *
*        COMPANY IN ATLANTA, GEORGIA.                                 *
*                                                                     *
*        DELETE: ALLOWS MEMBER GROUPS AND DEFAULT MEMBER NAMES AS     *
*        REQUESTED BY ARNIE CASINGHINO FROM CBT.                      *
*                                                                     *
*        ATTENTION HANDLING IMPROVEMENTS -- TWO ATTENTIONS IN A ROW   *
*        ARE GENERALLY REQUIRED TO TERMINATE PDS; ALL SUBCOMMANDS     *
*        NOW RECOGNIZE ATTENTION INTERRUPTS TO SOME DEGREE:           *
*        A.  PDS ATTENTION HANDLING IS NOW DOCUMENTED IN PDS'S HELP   *
*            (ENTER "HELP ATTN" FOR MORE INFORMATION).                *
*        B.  FIXED A BUG: A NULL RESPONSE TO "ENTER OPTION" AFTER     *
*            AN ATTENTION DID NOT ALLOW THE SUBCOMMAND TO CONTINUE.   *
*        C.  THE INPUT ROUTINE NOW ALSO CHECKS FOR ATTENTION -- THIS  *
*            ELIMINATES WAITING UNTIL A MESSAGE IS OUTPUT AS BEFORE.  *
*                                                                     *
*        OPEN: FIXED ABEND 013-D0 FOR RECFM=FS OR FBS.  THIS PROBLEM  *
*        WAS REPORTED BY JAMES BERRY FROM WESTLAKE VILLAGE, CALIF.    *
*                                                                     *
*        ALIAS, RENAME AND RESTORE: CHANGE THE DEFAULT MEMBER NAME AS *
*        SUGGESTED BY JOHN SULLIVAN AT LOMA LINDA UNIVERSITY, CALIF.  *
*                                                                     *
****VER 5.3:  CHANGED THE CSECT STRUCTURE OF PDS AND MADE THE      ****
*        FOLLOWING OTHER CHANGES:                                     *
*                                                                     *
*        EDIT AND BROWSE:                                             *
*        A.  THE INITIALIZATION ADDRESS FOR PDS PASSED TO ISPF IS     *
*            NOW CONVERTED TO DECIMAL BEFORE THE CALL AND CONVERTED   *
*            BACK TO BINARY ON THE RECURSIVE ENTRY IN PDS DUE TO THE  *
*            POTENTIAL PROBLEM OF A X'5D' (A RIGHT PARENTHESIS) IN    *
*            THE ADDRESS.  THIS WAS REPORTED BY SEYMOUR METZ, U.S.    *
*            DEPARTMENT OF STATE.                                     *
*        B.  A PARAMETER LIST PASSED TO INITIALIZE SPF OR ISPF MUST   *
*            NOT BE REUSED BY THE CALLING PROGRAM SINCE SPF USES IT   *
*            LATER IF IT SPLITS THE USER'S SCREEN.  THIS PROBLEM WAS  *
*            RESOLVED BY MIKE LOOS WITH DELUXE CHECK PRINTERS.        *
*        BROWSE: THE ALLOCATED FILE IS NOW FREED BEFORE EACH CALL     *
*        TO SPF SINCE THE DATA SET COULD BE UPDATED IN A SUBSEQUENT   *
*        SPLIT SCREEN.                                                *
*                                                                     *
*        EDIT AND TSOEDIT: ADDED NEW AND OLD KEYWORDS TO ALLOW BLDL   *
*        CHECKING OF MEMBER NAMES BEFORE ENTERING EITHER EDITOR.      *
*                                                                     *
*        END: THE RETURN CODE ASSIGNED BY THE PROGRAM IS NOW SAVED    *
*        TEMPORARILY IN THE PROGRAM'S STORAGE; IN VERSION 5.2 THE     *
*        RETURN CODE IS CHANGED DURING A CALL TO FREE THE FILE AND    *
*        A STACK DELETE IS INCORRECTLY ISSUED AT PDS TERMATION.       *
*                                                                     *
*        CHANGE AND PDS: THE DATA SET ALLOCATION STATUS IS DISPLAYED  *
*        AFTER EACH DATA SET IS ALLOCATED.                            *
*                                                                     *
*        ATTRIB:                                                      *
*          1.  ADDED ALIASINFO AND NOALIASINFO KEYWORDS               *
*          2.  ADDED LKEDDATE AND NOLKEDDATE KEYWORDS                 *
*          3.  A LIST OF ATTRIBUTES TO BE ASSIGNED TO A MODULE        *
*              CAN NO LONGER BE PLACED IN PARENTHESES.                *
*                                                                     *
*        UNDOCUMENTED SUBCOMMANDS:                                    *
*          1. COMPRESS OPERAND "SHR" -- EVALUATE THIS BEFORE USING    *
*          2. KLEAR (OR K -- CLEARS 3270 DISPLAY SCREENS)             *
*          3. W (DUMMY SUBCOMMAND FOR EXECUTING PDS UNDER TEST)       *
*                                                                     *
*                                                                     *
*                                                                     *
*        IF: ADDED SUBCOMMAND TO TEST MEMBERS FOR GIVEN ATTRIBUTES    *
*        AND PERFORM CONDITIONAL PROCESSING AS SUGGESTED BY JIM BERRY *
*        FROM WESTLAKE VILLAGE, CALIFORNIA:                           *
*                                                                     *
*        FIND: ADDED THEN AND ELSE CONDITIONAL PROCESSING             *
*        CAPABILITIES AS IMPLEMENTED IN THE IF SUBCOMMAND.            *
*                                                                     *
*        MAP: DELETED THE SMAP SUBCOMMAND AND MADE SEVERAL CHANGES -  *
*           1.  ADDED FULL, SHORT AND ENTRY KEYWORDS                  *
*           2.  ADDED MODULE NAME FILTERING                           *
*                                                                     *
*        HISTORY: ADDED TRANSLATOR IDENTIFICATION MESSAGES AND        *
*        MADE SEVERAL ADDITIONAL CHANGES -                            *
*           1.  ADDED MODULE NAME FILTERING                           *
*           2.  ADDED TRAN(PARTNAME)                                  *
*           3.  ADDED ZAP(PARTNAME)                                   *
*           4.  ADDED USER(PARTNAME)                                  *
*           5.  FIXED A BUG - IF A IDR USER RECORD HAD CONTINUED      *
*               HEADER DATA, ITS DATE WAS NOT INITIALIZED.            *
*                                                                     *
*        MEMBER PROTECTION: THE DELETE, EDIT, FIXPDS, FSE, SPF AND    *
*        RENAME SUBCOMMANDS ISSUE SPF-STYLE ENQUEUE CHECKS BEFORE     *
*        UPDATING THE ALLOCATED DATA SET.  THIS ENQUEUE CHECK CANNOT  *
*        DETECT A USER FROM ANOTHER CPU UPDATING THE MEMBER THROUGH   *
*        SHARED DASD (THIS IS ALSO AN SPF PROBLEM).                   *
*                                                                     *
*                                                                     *
*                                                                     *
*        DATA SET PROTECTION: THE PDS GENERATION OPTIONS OF OLD, SHR, *
*        AND OPER HAVE BEEN REMOVED -- THE DATA SET'S INTEGRITY CAN   *
*        BE INSURED BY USING DISPOSITION "OLD" ON THE CHANGE OR PDS   *
*        SUBCOMMAND.  ALSO, SEE THE FOLLOWING NOTES ON SHR UPDATES    *
*        AND MEMBER PROTECTION.                                       *
*                                                                     *
*        SHR UPDATES: PDS NOW USES SPF RESERVE LOGIC TO PROTECT THE   *
*        INTEGRITY OF DATA SETS ALLOCATED AS SHR DURING ANY UPDATES.  *
*        THIS CHANGE AFFECTS THOSE PDS SUBCOMMANDS WHICH UPDATE THE   *
*        DATA SET BEING PROCESSED (ALIAS, ATTRIB WITH ATTRIBUTE       *
*        CHANGES, DELETE, FIXPDS, RENAME, RESTORE AND VERIFY          *
*        SUBCOMMANDS) AND WAS SUGGESTED BY ARNIE CASINGHINO WITH CBT. *
*        ENTER "HELP UPDATE" FOR MORE INFORMATION.                    *
*                                                                     *
*        ESTAE PROCESSING: PDS CAN NOW RECOVER FROM ABENDS (EXCEPT    *
*        IN THE SPFEDIT AND BROWSE SUBCOMMANDS).  ENTER "HELP ABEND"  *
*        FOR MORE INFORMATION.                                        *
*                                                                     *
*        RESTORE: ADDED CONTROL KEYWORDS AS SUGGESTED BY              *
*        GEORGE GOFFE WITH AMDAHL CORPORATION IN SUNNYVALE, CALIF.    *
*                                                                     *
*        EXTERNAL SUBCOMMANDS: SUBCOMMANDS EXTERNAL TO PDS ARE NOW    *
*        ATTACHED INSTEAD OF LINKED TO.  THIS WAS SUGGESTED BY        *
*        SEYMOUR METZ WITH THE STATE DEPARTMENT.  THIS CHANGE MAKES   *
*        THE FOLLOWING IMPROVEMENTS POSSIBLE:                         *
*           1.  PDS CAN RECOVER FROM ABENDS IN THESE SUBCOMMANDS      *
*           2.  PDS CAN FREE ALLOCATED FILES AND MOST OTHER RESOURCES *
*               OBTAINED BY THESE SUBCOMMANDS                         *
*           3.  PDS CAN PERFORM ATTENTION PROCESSING IN BEHALF OF     *
*               THESE SUBCOMMANDS                                     *
*                                                                     *
*        ATTRIB: ANY APPARENT ALIASES FOR A MEMBER ARE DISPLAYED (AS  *
*        WELL AS ACTUAL ALIASES) DURING THE ALIAS SEARCH FOR A MEMBER.*
*                                                                     *
*        ATTRIB AND MAP: ALL CORRESPONDING MAIN MEMBERS FOR AN ALIAS  *
*        MEMBER ARE DISPLAYED (NOT JUST THE FIRST ONE AS BEFORE).     *
*                                                                     *
*        BACKGROUND USE OF PDS: A BLANK LINE IS OUTPUT BETWEEN EACH   *
*        SUBCOMMAND FOR READABILITY; ALSO, THE PROGRAM CIRCUMVENTS    *
*        PROMPTS FOR YES/NO DECISIONS BY SIMULATING YES RESPONSES.    *
*                                                                     *
*        TSSO SUPPORT:  A SINGLE SUBCOMMAND MAY NOW BE ENTERED ON THE *
*        PRIMARY COMMAND LINE; PDS WILL PROCESS THE SUBCOMMAND AND    *
*        TERMINATE.  THIS WAS SUGGESTED BY ARNIE CASINGHINO WITH CBT. *
*                                                                     *
*        FIXPDS:  ADDED SUBCOMMAND TO MODIFY THE ALLOCATED DATA SET.  *
*        THE EXPANDDIR OPTION WAS DERIVED FROM FILE 288 OF THE CBT    *
*        TAPE AS SUGGESTED BY ARNIE CASINGHINO WITH CBT.              *
*                                                                     *
*        NON-PARTITIONED DATA SETS:  PARSE DATA FOR THE MEMBER NAME   *
*        POSITITION IS NO LONGER ENTERED. ALSO, SUPPORT HAS BEEN      *
*        ADDED FOR NON-PARTITIONED DATA SETS FOR BROWSE, EDIT,        *
*        DSPRINT, FIND, FSE, LIST, PRINTOFF, SPFEDIT, SUBMIT, SUBLIST,*
*        REVIEW, TSOLIST, UPDATE AND VERIFY.                          *
*                                                                     *
****VER 6.0:  MADE THE FOLLOWING CHANGES:                          ****
*                                                                     *
*        SPF AND ISPF USERS: PDS CAN BE INVOKED AS A SPF DIALOG USING *
*        THE PROVIDED SPF PANEL AND MESSAGE MEMBER.  THIS CHANGE      *
*        WAS SUGGESTED BY JAMES W. BERRY FROM WESTLAKE VILLAGE, CAL.  *
*                                                                     *
*        RENAME: IF A NON-ALIAS LOAD MODULE IS RENAMED, ADDITIONAL    *
*        CODE IN RENAME WILL UPDATE THE CHANGED NAME POINTERS IN      *
*        ANY ASSOCIATED ALIAS DIRECTORY ENTRIES.                      *
*                                                                     *
*        VERIFY: ADDED A SUBCOMMAND TO CHECK A DATA SET FOR           *
*        VALIDITY ERRORS.                                             *
*                                                                     *
*        IF: ADDED SEVERAL MEMBER VALIDITY CHECKS IN SUPPORT OF       *
*        THE NEW VERIFY SUBCOMMAND.                                   *
*                                                                     *
*        FROM ARNIE CASINGHINO (CBT):                                 *
*        A.  MODIFIED THE SCREEN CLEAR TPUT FOR KLEAR TO SUPPORT      *
*            3278-5 TERMINALS UNDER ACF/TCAM.                         *
*        B.  MODIFIED RESERVE LOGIC FOR BATCH EXECUTIONS OF PDS       *
*            TO HOLD RESERVES UNTIL A NEW DATA SET IS SELECTED.       *
*        C.  ADDED KEYWORDS TO THE RESTORE SUBCOMMAND TO ALLOW        *
*            MULTIPLE MEMBER RESTORATIONS WITH AND WITHOUT PROMPTING. *
*        D.  MODIFIED FIXPDS TO ADD AN PADDING DIRECTORY BLOCK IF     *
*            A TRACK WILL BE FILLED WITH DIRECTORY BLOCKS.  THIS      *
*            IS DONE TO ENSURE THAT THE END OF FILE MARK IS ON THE    *
*            FOLLOWING TRACK AND IT AVOIDS IEBCOPY ABENDS.            *
*        E.  MODIFIED RENAME TO WORK ON A RANGE OF MEMBERS.           *
*                                                                     *
*        FROM GEORGE GOFFE (AMDAHL CORPORATION):                      *
*        A.  ADDED A SWAP OPERAND TO THE RENAME SUBCOMMAND TO         *
*            ALLOW TWO MEMBERS TO EXCHANGE MEMBER NAMES.              *
*        B.  THE PATTERN SUBCOMMAND MAY BE ENTERED WITHOUT OPERANDS   *
*            AND THE PREVIOUS PATTERN OPERAND(S) WILL BE USED.        *
*        C.  ADDED THE RECALL SUBCOMMAND TO DISPLAY OR REISSUE        *
*            AND DISPLAY THE PREVIOUSLY ENTERED SUBCOMMAND.           *
*                                                                     *
*        FROM JOHN SULLIVAN (LOMA LINDA UNIVERSITY MEDICAL CENTER):   *
*        A.  ADDED A NONE OPERAND FOR THE ATTRIB SUBCOMMAND TO        *
*            CHANGE ALL ATTRIBUTES OF A MODULE TO DEFAULT VALUES.     *
*        B.  CHANGED ALL MODULE SIZE DISPLAYS TO OUTPUT THE NEXT      *
*            HIGHER 1K BOUNDARY RATHER THAN THE CLOSEST 1K BOUNDARY.  *
*        C.  MODIFIED ATTRIB TO ALLOW DC ATTRIBUTE CHANGES.           *
*                                                                     *
*        HISTORY:  CORRECTED TO LIST HISTORY DATA IN DESCENDING ORDER *
*        IN EACH GROUP BY LAST CHANGE DATE, ADDED LINKAGE-EDITOR NAME *
*        FILTERING AND CHANGED SUBCOMMAND DEFAULTS.                   *
*                                                                     *
*        USE OF PDS CLISTS OR STACKED INPUT: THE PROGRAM CIRCUMVENTS  *
*        PROMPTS FOR YES/NO DECISIONS BY ASSUMING YES RESPONSES.      *
*                                                                     *
*        IF, VERIFY, ATTRIB: MADE CHANGES TO CHECK FOR MUTUALLY       *
*        EXCLUSIVE LOAD MODULE ATTRIBUTES (FOR EXAMPLE, OVERLAY       *
*        ATTRIBUTE WITH THE REENTRANT ATTRIBUTE).                     *
*                                                                     *
*        MVS/XA SUPPORT - MADE CHANGES FOR THE FOLLOWING SUBCOMMANDS: *
*        A.  IF: ADDED SELECTION BY RMODE AND AMODE                   *
*        B.  ATTRIB: ADDED RMODE AND AMODE ATTRIBUTE SUPPORT:         *
*              1.  ADDED RMODE AND AMODE CHANGE CAPABILITY            *
*              2.  ADDED RMODE AND AMODE DISPLAY CAPABILITY FOR       *
*                  MODULES WHOSE RMODE AND AMODE ARE NOT BOTH 24      *
*              3.  IF RMODE OR AMODE IS CHANGED FOR A NON-ALIAS       *
*                  MODULE, ADDITIONAL CODE IN ATTRIB WILL UPDATE      *
*                  THE RMODE AND THE MAIN MODULE AMODE VALUE FOR      *
*                  ANY ASSOCIATED ALIAS DIRECTORY ENTRIES.            *
*        C.  RESTORE: ADDED CODE TO SET THE RMODE AND AMODE FOR       *
*            A MODULE BASED ON INFORMATION FROM ITS ESD ENTRIES       *
*        D.  ALIAS: ADDED CODE TO SET THE RMODE AND AMODE BASED ON    *
*            ESD INFORMATION AND THE ALIAS BASE ENTRY'S ATTRIBUTES    *
*        E.  MAP: ADDED CODE TO DISPLAY RMODE AND AMODE FOR CSECTS    *
*            WHOSE RMODE AND AMODE ARE NOT BOTH 24                    *
*                                                                     *
*        OUTCOPY: ADDED A NEW SUBCOMMAND TO PRODUCE IEBCOPY CONTROL   *
*        STATEMENTS WHICH CAN BE USED TO SELECTIVELY COPY PDS MEMBERS *
*                                                                     *
*        FROM WALT FARRELL WITH RAINER NATIONAL BANK IN SEATTLE:      *
*        A.  THE "ENTER OPTION" PROMPT MESSAGE HAS BEEN EXPANDED      *
*            TO INCLUDE THE DSNAME AND VOLUME SERIAL NAME.            *
*        B.  ATTRIB FOR NON-LOAD MEMBERS, BROWSE, DELETE, FIND, FSE,  *
*            HISTORY, IF, MAP, OUTCOPY, PRINTOFF, REVIEW, AND EDIT    *
*            SUBCOMMANDS:  THE MEMBER NAME IDENTIFICATION FOR MEMBER  *
*            GROUPS IS NORMALLY SUPPRESSED.  FOR THE FIND, HISTORY,   *
*            IF, MAP AND REPLACE SUBCOMMANDS, HOWEVER, THE HEADER IS  *
*            WRITTEN IF ANY OUTPUT IS GENERATED FOR A MEMBER.         *
*                                                                     *
*        SPFEDIT AND BROWSE: A DIALOG ERROR NO LONGER TERMINATES THE  *
*        PDS COMMAND, HOWEVER, THE PDS COMMAND WILL NOT ATTEMPT TO    *
*        REUSE THE SPF INTERFACE AFTER THE FIRST SUCH DIALOG ERROR.   *
*                                                                     *
*        ATTENTION PROCESSING: STAX PROCESSING HAS BEEN MODIFIED --   *
*        TWO ATTENTIONS SHOULD NOW TERMINATE A PDS PROGRAM LOOP.      *
*                                                                     *
*        FIXPDS:  MODIFIED TO PREVENT A RESET OR EXPANDED DIRECTORY   *
*        FROM OVERFLOWING THE FIRST DISK EXTENT.                      *
*                                                                     *
*        FROM RICHARD MCCARTY WITH HITACHI AMERICA IN SAN JOSE:       *
*        IN FIXPDS EXPAND, IF THE LAST ALLOCATED DIRECTORY BLOCK WAS  *
*        COMPLETELY FULL, PDS COULD NOT ADD ANY DIRECTORY BLOCKS.     *
*                                                                     *
*        DELETE: ADDED THE ALIAS OPERAND                              *
*                                                                     *
*                                                                     *
*                                                                     *
*        FROM BEN MOORE (FEDERAL EXPRESS):                            *
*        A.  IF, ATTRIB, VERIFY: CHECKS FOR THE DIRECTORY             *
*            RLD/CONTROL COUNT NOT MATCHING THE FIRST RLD ENTRY.      *
*        B.  IF: ADDED THE RLDZERO AND NORLDZERO KEYWORDS.            *
*                                                                     *
*                                                                     *
*                                                                     *
****VER 6.1:  MADE SEVERAL MODIFICATIONS:                          ****
*                                                                     *
*        FIND AND LIST:                                               *
*        A.  CHANGED LOGIC TO SUPPORT OUTPUT OF CSECT INFORMATION     *
*            AS SUGGESTED BY BEN MOORE WITH FEDERAL EXPRESS.          *
*        B.  ADDED A SECOND DISPLACEMENT FIELD FOR LBLOCK, LDUMP      *
*            BLOCK AND DUMP OUTPUT FORMATS.                           *
*        C.  CHANGED SPELLING OF MULTILINE TO LBLOCK.                 *
*        D.  CHANGED SPELLING OF LINEDUMP TO LDUMP.                   *
*        E.  DEFAULT OUTPUT FORMAT FOR LOAD MODULES IS LDUMP.         *
*        F.  ADDED MODULE AND OFFSET OPERANDS FOR LBLOCK AND LDUMP.   *
*                                                                     *
*        FROM JOHN SULLIVAN (LOMA LINDA MEDICAL CENTER):              *
*        A.  MODIFIED HISTORY TO LIST SPF STATISTICS (LIKE THE        *
*            ATTRIB SUBCOMMAND) FOR NON-LOAD MEMBERS.                 *
*        B.  ADDED THE CONTROL SUBCOMMAND.                            *
*        C.  ADDED THE LIKE KEYWORD ON THE RESTORE SUBCOMMAND.        *
*        D.  CHANGED RESTORE SCAN TO ALSO CHECK THE PROVIDED TTR      *
*            TO SEE IF IT IS A DELETED MEMBER.                        *
*                                                                     *
*        PROGRAM INPUT PROCESSING: DOUBLE BUFFERING HAS BEEN ADDED.   *
*                                                                     *
*        DELETE: REWRITTEN TO TAKE ADVANTAGE OF DOUBLE BUFFERING.     *
*                                                                     *
*        PDS DIALOG ENTRY: CODE FOR LOCATING THE TSO ECT HAS BEEN     *
*        ADDED AS PROVIDED BY MIKE LOOS WITH DELUXE CHECK PRINTERS.   *
*                                                                     *
*        TSO SERVICE ROUTINE ADDRESSES: SINCE SOME INSTALLATIONS      *
*        MAY MOVE ROUTINES LIKE IKJPARS OUT OF LPALIB, PDS HAS BEEN   *
*        MODIFIED TO TEST FOR AN ACTUAL ADDRESS IN THE CVT AND        *
*        TO LOAD AND DELETE ANY REQUIRED MODULES AS SUGGESTED BY      *
*        JERRY MORRIS WITH GTE DATA SERVICES IN TAMPA, FLORIDA.       *
*                                                                     *
*        FIXPDS: BLOCKSIZE VALUES FOR DATA KEYWORDS SUCH AS ASM,      *
*        CLIST, CNTL, LOAD OR OBJECT ARE BASED ON THE DEVICE TYPE.    *
*                                                                     *
*        TSO TIME SUBCOMMAND: IF TIME IS REQUESTED, PDS LINKS TO      *
*        IKJEFT25 INSTEAD OF TIME AS SUGGESTED BY MARK HUTCHINSON     *
*        WITH BURLINGTON INDUSTRIES IN GREENSBORO, NORTH CAROLINA.    *
*                                                                     *
*        REVIEW: SUBCOMMAND ADDED TO LINK TO THE REVIEW COMMAND       *
*        FROM THE CBT TAPE, FILE 300 (AIR FORCE TSO MODIFICATIONS).   *
*                                                                     *
*        EDIT, FSE, PRINTOFF, REVIEW, SUBMIT AND TSOLIST: PDS CHECKS  *
*        FOR VALID MEMBER NAMES BEFORE LINKING TO THE COMMAND.        *
*                                                                     *
*        IF: FIXED A MINOR PROBLEM WITH AUTHORIZED MODULE CHECKING;   *
*        ALSO, ADDED THE APFERR AND NOAPFERR KEYWORDS.                *
*                                                                     *
*        DISPLAY AND PATTERN: ADDED MEMBER GROUP SYNTAX AS AN         *
*        ALTERNATE WAY TO SPECIFY MEMBER NAMES.                       *
*                                                                     *
*        VERIFY: ADDED THE UPDATE AND NOUPDATE KEYWORDS.              *
*                                                                     *
*      ERRORS NOTED BY ARNOLD CASINGHINO AT CBT:                      *
*        HISTORY: ALWAYS DISPLAYED LINKAGE-EDITOR MOD LEVEL AS ZERO.  *
*        MAP: SOMETIMES DISPLAYED AMODE24 AND RMODE24 ATTRIBUTES.     *
*        EDIT, FSE, PRINTOFF, REVIEW, SUBMIT AND TSOLIST:             *
*        INTRODUCED A PROBLEM WITH SINGLE CHARACTER MEMBER NAMES.     *
*                                                                     *
****VER 6.2:  MADE SEVERAL MODIFICATIONS:                          ****
*                                                                     *
*        CONSOLIDATED ALL MESSAGES AND ADDED MESSAGE IDENTIFIERS      *
*        FOR USE BY THE TSO MESSAGE FORMATTING MODULE (IKJEFF18).     *
*                                                                     *
*        ADDED AN EXTENDED HELP (.?) FACILITY TO HELP EXPLAIN         *
*        ERRORS DETECTED DURING PDS PROCESSING.                       *
*                                                                     *
*        SUBCOMMANDS ADDED:                                           *
*        COMPARE: ATTACHES BILL GODFREY'S COMPARE PREPROCESSOR FROM   *
*                 JIM MARSHALL'S TSO UTILITIES.                       *
*        COMPRESS: FOR PDS COMPRESS FUNCTIONS.                        *
*        DSNAME: DISPLAYS THE CURRENT ALLOCATION AS SUGGESTED BY      *
*                DAVID FILSINGER AND JOHN SULLIVAN.                   *
*        DSPRINT: AS SUGGESTED BY WILLIAM SMITH IN PALO ALTO, CALIF.  *
*                                                                     *
*        OUTCOPY:                                                     *
*        A.  ECHO MODE IS NOW A DEFAULT.                              *
*        B.  ALIAS OPERAND WAS ADDED.                                 *
*                                                                     *
*        CONTROL: ADDED THE MSGS AND TESTREAD OPERANDS AND            *
*        DELETED THE UPDATEALIAS/NOUPDATEALIAS OPERANDS.              *
*                                                                     *
*        ALLOCATION/UNALLOCATION: REWRITTEN TO USE SVC 99 INSTEAD     *
*        OF DAIR.                                                     *
*                                                                     *
*      FROM STEVE SMITH WITH SECURITY PACIFIC BANK IN GLENDALE, CAL.  *
*        DISPLAY: THE SECOND OPERAND IS MADE OPTIONAL.                *
*        ISPF: ADDED SUBCOMMAND FOR DIRECT ENTRY INTO SPF.            *
*        INVALID SUBCOMMAND MESSAGE: ADDED FOR SUBCOMMAND RENAMES.    *
*        SUBCOMMAND TABLE: MOVED INTO THE COPY LIBRARY.               *
*                                                                     *
*      FROM MIKE LOOS WITH DELUXE CHECK PRINTERS:                     *
*        ALLOCATION: ADDED SUPPORT FOR ALIAS DATA SET NAMES.          *
*        IF: ADDED CREATED AND CHANGED KEYWORDS.                      *
*                                                                     *
*      FROM SAM LEPORE WITH WELLS FARGO BANK IN SAN FRANCISCO:        *
*        SPF HELP: ADDED A HELP SCREEN FOR THE PDS ENTRY PANEL.       *
*        RECALL: ADDED NULLS SO CHARACTER INSERT ON 3270'S WILL WORK. *
*        EXTENDED HELP: USED .? INSTEAD OF ! FOR THE KEY SYMBOL.      *
*        ALLOCATION: ADDED * NOTATION FOR THE DATA SET NAME POSITION. *
*                                                                     *
*      FROM GRAY MADDRY AT THE COOPER GROUP IN APEX, NORTH CAROLINA:  *
*        MEMBER IN USE MESSAGE: MODIFIED TO INCLUDE THE NAME OF THE   *
*        USER MODIFYING THE MEMBER.                                   *
*                                                                     *
*      FROM MARC SCHARE WITH BELL LABORATORIES IN PISCATAWAY, NJ:     *
*        ALLOCATION: ADDED SUPPORT FOR FILE(FILENAME).                *
*        ESTAE EXIT: FIXED AN S0C4 ABEND FOR MVS/XA 2.1.2.            *
*                                                                     *
*      FROM JOHN SULLIVAN WITH LOMA LINDA UNIVERSITY MEDICAL CENTER:  *
*        VERIFY: FIXED A BUG WHICH CAUSED S0C4 ABENDS WHEN PROCESSING *
*        DATA SETS WITH LARGE DIRECTORIES.                            *
*                                                                     *
*      FROM KEVIN MC CARTHY WITH MC DONALD'S CORPORATION IN CHICAGO:  *
*        IMPLIED CLIST (%): FIXED A BUG WHICH REQUIRED CLISTS TO      *
*        HAVE A PROC STATEMENT.                                       *
*                                                                     *
*      FROM ARNOLD CASINGHINO WITH CBT:                               *
*        SUBCOMMANDS WITH DEFAULT MEMBERS: ADDED MEMBER LIST SUPPORT. *
*        SCREEN UTILIZATION: SUBCOMMANDS WHICH OUTPUT VARIABLE        *
*        AMOUNTS OF DATA ON AN OUTPUT LINE (SUCH AS THE DISPLAY       *
*        SUBCOMMAND) USE THE FULL TERMINAL LINE WIDTH.                *
*                                                                     *
*      FROM MICHAEL EMIGH FROM UNITED AIR LINES (LISTM COMMAND):      *
*        DATA SET OPEN: ADDED A DIRECTORY COUNTING CCW CHAIN.         *
*                                                                     *
*      FROM DAVID SAGER WITH JOHNS HOPKINS UNIVERSITY/APL:            *
*        COMMAND SECURITY: ADDED A SAMPLE RACF TSO VALIDATION EXIT.   *
*        VALIDATION: ADDED TSO COMMAND AND SUBCOMMAND AUTHORIZATION   *
*        EXITS SIMILAR TO TSO COMMAND EXITS FROM APL LABORATORIES.    *
*        PROGRAM GENERATION: ADDED INSTALLATION SUBCOMMAND CONTROLS.  *
*                                                                     *
*      FROM DICK HINTON WITH LOCKHEED AIRCRAFT, BURBANK, CALIF.       *
*        ESTAE EXIT: FIXED AN S0C6 ABEND FOR MVS/XA 2.1.2.            *
*                                                                     *
*        IF/FIND WITH THEN OR ELSE OPERANDS: MESSAGE SUPPRESSION      *
*        HAS BEEN ADDED FOR SUBCOMMANDS WHICH DISPLAY MEMBER NAMES.   *
*                                                                     *
*        INPUT BUFFERING:                                             *
*          SINGLE - NAME CHANGED FROM NODOUBLE; ONE BLOCK PER EXCP.   *
*          DOUBLE - CHANGED TO GET TWO BLOCKS PER EXCP.               *
*          MULTIPLE - ADDED TO GET A FULL TRACK PER EXCP.             *
*                                                                     *
*        OPTIONS: REVISED TO SUPPORT THE DELETION OF ANY SUBCOMMAND   *
*        DURING THE PROGRAM GENERATION PROCESS.                       *
*                                                                     *
*        USAGE: REWRITTEN TO ADD MORE DATA SET INFORMATION.           *
*                                                                     *
*        MEMBER BLDL: PERFORMED FOR EACH SUBCOMMAND WITH A SIMPLE     *
*        MEMBER NAME SO THAT PDS WILL BE AWARE OF CHANGES TO THE      *
*        DATA SET FROM OTHER ADDRESS SPACES.                          *
*                                                                     *
*                                                                     *
****VER 6.3:  ADDED DIALOG DISPLAY MODE:                           ****
*                                                                     *
*      FROM STEVE SMITH WITH SECURITY PACIFIC BANK IN GLENDALE, CAL.  *
*                                                                     *
*                                                              SS JUL84
*        ADDED SUPPORT OF DIALOG DISPLAY MODE USING            SS JUL84
*        A DIALOG TABLE TO DISPLAY PDS OUTPUT                  SS JUL84
*                                                              SS JUL84
*                                                                     *
*        FROM BARB COUTURE WITH DELUXE CHECK PRINTERS IN MINNESOTA:   *
*        PROFILE NOPREFIX CAUSED A S0C4 FOR DSNAME ALLOCATION AND     *
*        FILE(FILENAME) ALLOCATION DID NOT WORK IF PROFILE NOPREFIX.  *
*                                                                     *
*        FROM JIM MELNYK WITH GREAT-WEST LIFE ASSURANCE CO. IN        *
*        WINNIPEG, CANADA:  PDS DATA.SET VOL(XXX) OLD DID NOT WORK    *
*                       BUT PDS DATA.SET OLD VOL(XXX) DID WORK.       *
*                                                                     *
*        FROM FRANK PAJERSKI WITH ATARI INCORPORATED IN SUNNYVALE:    *
*        CERTAIN FOCUS LOAD MODULES GOT S0C7 ABENDS USING HISTORY.    *
*                                                                     *
*        FROM ARNOLD CASINGHINO WITH CBT: WHEN PDS IS INVOKED AS A    *
*        SUBCOMMAND, IT USES THE PRIMARY COMMAND NAME (LIKE EDIT);    *
*        THIS PREVENTED SPFEDIT IN PDS UNDER EDIT FROM WORKING.       *
*                                                                     *
*        FOR THE STAX EXIT FROM RICHARD SCHAFER AT RICE UNIVERSITY:   *
*          1.  TCLEARQ SHOULD BE ISSUED TO SYNCRONIZE INPUTS.         *
*          2.  STATUS STOP SHOULD BE ISSUED FOR ANY SUBTASKS.         *
*          3.  STACK DELETE=ALL IS ISSUED TO FLUSH THE CURRENT STACK. *
*          4.  THE PUTGET IN THE ATTENTION ROUTINE SHOULD ACCEPT      *
*              TERMINAL INPUT ONLY.                                   *
*                                                                     *
*        FOR THE ESTAE EXIT FROM RICHARD SCHAFER AT RICE UNIVERSITY:  *
*          1.  STACK DELETE=ALL IS ISSUED TO FLUSH THE CURRENT STACK. *
*                                                                     *
*        FROM SAM LEPORE WITH WELLS FARGO BANK IN SAN FRANCISCO:      *
*        THE PDS200I MESSAGE WAS REDESIGNED TO MAKE IT MORE USEFUL.   *
*                                                                     *
*        FROM MARC SCHARE WITH BELL LABORATORIES IN PISCATAWAY, NJ:   *
*        IF AND HISTORY: ADDED A "SYSMOD" ALIAS FOR THE USER KEYWORD. *
*                                                                     *
*        FROM BEN MOORE (FEDERAL EXPRESS):                            *
*        FIND: ASIS AND CAPS KEYWORDS HAVE BEEN ADDED TO ALLOW        *
*        THE SPECIFICATION OF LOWER-CASE SEARCH STRINGS.              *
*                                                                     *
*        FROM BILL SMITH WITH SYNTEX: ADDED CONTROLS TO SPECIFY       *
*        THE NUMBER OF MEMBERS IN THE DATA SET TO AVOID SB37          *
*        ABENDS IN THE COMPRESS SUBCOMMAND.                           *
*                                                                     *
*        BACKGROUND USE OF PDS: CONTROL NORECOVER IS MADE A DEFAULT   *
*        TO PREVENT INADVERTENT RECOVERY AFTER AN ERROR.              *
*                                                                     *
*        FROM JIM MELNYK WITH GREAT-WEST LIFE ASSURANCE CO. IN        *
*        WINNIPEG, CANADA:  CHANGED THE RESERVE/DEQ MAJOR NAME FROM   *
*        SPFDSN TO SPFEDIT (AS IN PTF UZ65671).                       *
*                                                                     *
*        VERIFY: ADDED A MAXIMUM PHYSICAL BLKSIZE MESSAGE.            *
*                                                                     *
*        FROM SAM LEPORE WITH WELLS FARGO BANK IN SAN FRANCISCO -     *
*        COMPARE, DSPRINT, PRINTOFF, REVIEW, SUBMIT AND TSOLIST:      *
*        PERMIT CLIST INVOCATION INSTEAD OF A TSO COMMAND.            *
*                                                                     *
*        SPFEDIT, BROWSE, ISPF AND ISPMODE: AFTER A RETURN COMMAND    *
*        IN SPF, SUBSEQUENT SPF SUBCOMMANDS WERE TERMINATED WITHOUT   *
*        PROCESSING.  NOW, THEY ARE REINVOKED AFTER A RETURN KEY IF   *
*        &RETURNX IS YES.                                             *
*                                                                     *
*    SESSION MANAGER PROBLEMS:                                        *
*        FROM MARSHALL WERNICK WITH ARCO PETROLEUM PRODUCTS IN        *
*        LOS ANGELES: USING SESSION MANAGER, THE ENTRY TO PDS         *
*        VIA THE SPF PDS PANEL CAUSED AN S0C4 ABEND.                  *
*                                                                     *
*        FROM BILL SMITH WITH SYNTEX: USING SESSION MANAGER, PDS      *
*        LINE MODE OUTPUT FOLLOWING SPFEDIT OR BROWSE SUBCOMMANDS     *
*        WERE NOT CAPTURED BY SESSION MANAGER.                        *
*                                                                     *
*        FROM CHARLES HOFFMAN WITH GTE LABORATORIES IN                *
*        WALTHAM, MASS: USING SESSION MANAGER, RESTORE WITH PROMPT    *
*        DID DOUBLE PROMPTING AND INCORRECT "INVALID SUBCOMMAND"      *
*        MESSAGES OCCURED FOR FOLLOWING PDS SUBCOMMANDS.              *
*                                                                     *
*        FROM JAMES BERRY AT WESTLAKE VILLAGE, CALIF:  WITH           *
*        SESSION MANAGER, THE SCREEN CLEAR FUNCTION (SUBCOMMAND K)    *
*        CLEARS THE SESSION MANAGER CONTROL CHARACTERS; NOW IF THE    *
*        SESSION MANAGER TMP IS IN USE, ONLY A BLANK LINE IS OUTPUT.  *
*                                                                     *
*                                                                     *
****VER 7.0:  ADDED MEMLIST SUBCOMMAND:                            ****
*                                                                     *
*      FROM STEVE SMITH WITH SECURITY PACIFIC BANK IN GLENDALE, CAL.  *
*                                                                     *
*                                                              SS SEP84
*        ADDED SUPPORT OF MEMLIST WITH LINE MODE SUBCOMMANDS   SS SEP84
*                                                              SS SEP84
*                                                                     *
*        FROM SAM LEPORE WITH WELLS FARGO:                            *
*          1.  VERIFY APPLIES TO DATA SETS AND INDIVIDUAL MEMBERS.    *
*          2.  MEMLIST OR ISPMODE ON THE PRIMARY COMMAND LINE         *
*              DETERMINES THE INITIAL PROGRAM PROCESSING MODE.        *
*          3.  THE DEFAULT MEMBER GROUP IS FORMATTED ON THE           *
*              PDS300A MESSAGE (FIRST SPECIFICATION FOR A LIST).      *
*                                                                     *
*        PDS ENTRY PANEL: CHANGED TO ALLOW LINE/ISPMODE/MEMLIST       *
*        ENTRY MODES.                                                 *
*                                                                     *
*        DEFAULT MEMBER GROUP: NOT MODIFIED BY PDS LINE COMMANDS.     *
*                                                                     *
*        ISPMODE: THE ENTERED SUBCOMMAND IS ECHOED IN UPPER AND       *
*        LOWER CASE.                                                  *
*                                                                     *
*        IF AND FIND: ADDED THEN(VERIFY) AND ELSE(VERIFY)             *
*        IF AND FIND: ADDED THEN(MEMLIST) AND ELSE(MEMLIST)           *
*                                                                     *
*        CONTROL: OPERANDS CALLED CPULOOP, ABEND, OUTLOOP AND MSGS    *
*        ARE NOW TESTCPULOOP, TESTABEND, TESTOUTLOOP AND TESTMSGS.    *
*                                                                     *
*        ISPMODE: CHKMAX, CMDMAX AND TBLMAX DIALOG CONTROL OPERANDS   *
*        HAVE BEEN ADDED AS SUGGESTED BY STEVE SMITH WITH SPNB.       *
*                                                                     *
*        COMPRESS: CHANGED TO ALLOCATE COMPRESS WORK SPACE BASED      *
*        ON THE NUMBER OF DIRECTORY BLOCKS IN USE AS SUGGESTED BY     *
*        JOHN SULLIVAN WITH THE LOMA LINDA MEDICAL CENTER.            *
*                                                                     *
*        TSO SUBCOMMAND:                                              *
*          1.  CHANGED TO INVOKE CLISTS AS SUGGESTED BY               *
*              SAM LEPORE AND JOHN SULLIVAN.                          *
*          2.  CHANGED TO OPTIONALLY INVOKE TSOEXEC FOR TSO/E REL 2   *
*              AS SUGGESTED BY MIKE LOOS WITH DELUXE CHECK PRINTERS.  *
*                                                                     *
*        CONTROL: ADDED OPERANDS TO PROVIDE A HARDCOPY OR DATA SET    *
*        LOG AS SUGGESTED BY BILL SMITH WITH SYNTEX.                  *
*                                                                     *
*        TIME SUBCOMMAND: ADDED AS SUGGESTED BY ARNOLD CASINGHINO.    *
*                                                                     *
*        COMPARE: MODIFIED THE TSO COMMAND TO RETURN OUTPUT LINES     *
*        TO THE PDS PUTLINE ROUTINE FOR SESSION DISPLAY AND LOGGING.  *
*                                                                     *
*        ATTRIB AND IF: ADDED SSI CHANGE AND TESTING CAPABILITY.      *
*                                                                     *
*        FIXPDS CHANGES:                                              *
*          1.  ALLOW FIXPDS USE FOR SEQUENTIAL DATA SETS.             *
*          2.  ADDED THE MAXSPACE OPERAND TO ALLOW RESTORING OF       *
*              MEMBERS AFTER A COMPRESS OF THE DATA SET AS SUGGESTED  *
*              BY JOHN SULLIVAN WITH THE LOMA LINDA MEDICAL CENTER.   *
*          3.  ADDED THE RELEASE, RELEXTENT AND RELSAVE OPERANDS.     *
*          4.  REDUCED THE DEFAULT BLOCKSIZES FOR 3350, 3375 AND      *
*              3380 DISKS.                                            *
*                                                                     *
*        SPFEDIT: ADDED THE MACRO KEYWORD FOR ISPF V2 AS SUGGESTED    *
*        BY MARC SCHARE WITH BELL LABORATORIES IN PISCATAWAY, NJ.     *
*                                                                     *
*        3278-5 TERMINALS: IF ISPMODE OR SESSION LOGGING IS ON, PDS   *
*        OUTPUT LINES ARE LIMITED TO 80 CHARACTERS AS SUGGESTED BY    *
*        ARNOLD CASINGHINO WITH CBT.                                  *
*                                                                     *
*        IF: FIXED A PROBLEM WITH CHANGED AND CREATED KEYWORDS FOR    *
*        LOAD MEMBER CHECKING AS REPORTED BY JOHN SULLIVAN AT LOMA    *
*        LINDA (CHECKING ACROSS A YEAR BOUNDARY WAS INCORRECT).       *
*                                                                     *
*        TSOEDIT AND EDIT: SUBCOMMAND EDIT WAS RENAMED AS TSOEDIT     *
*        AND SPFEDIT WAS RENAMED AS EDIT AS SUGGESTED BY STEVE SMITH. *
*                                                                     *
*                                                                     *
****VER 7.1:  MADE SEVERAL MODIFICATIONS:                          ****
*                                                                     *
*        LIST AND FIND: OUTPUT OF CSECT INFORMATION FAILED USING      *
*        MVS/XA FOR LARGE LOAD MODULES SUCH AS THE APL INTERPRETOR    *
*        AS REPORTED BY JIM MELNYK WITH THE GREAT-WEST LIFE ASSURANCE *
*        COMPANY IN WINNIPEG, CANADA.                                 *
*                                                                     *
*        RESTORE: CHANGED KEYWORD DEFAULTS AND THE TTR INPUT FROM     *
*        A POSITIONAL TO AN OPTIONAL KEYWORD PARAMETER AS SUGGESTED   *
*        BY MARC SCHARE WITH BELL LABORATORIES IN PISCATAWAY, NJ.     *
*                                                                     *
*        FROM BOB ZIMMERMAN WITH CNA INSURANCE IN CHICAGO:            *
*          A.  EDIT: ADDED PROFILE AND SETPROF KEYWORDS FOR ISPF V2.  *
*          B.  ADDED DIALOG SUPPORT FOR PDS WHEN IT IS CALLED BY      *
*              ISPF V2 WITH THE TSO COMMAND OR OPTION 6.              *
*          C.  IF: ADDED THE HASALIAS AND NOHASALIAS KEYWORDS.        *
*          D.  PROVIDED ANOTHER WAY TO LOCATE THE ECT CONTROL BLOCK.  *
*          E.  SUBLIST: ADDED A SUBCOMMAND TO DEFINE MEMBER SUBGROUPS.*
*          F.  RECALL: ADDED THE CHANGE OPERAND.                      *
*          G.  ATTRIB: ADDED AN OPERAND TO CHANGE THE ENTRY POINT     *
*              OF LOAD MODULES.                                       *
*          H.  MAP: FIXED AN ERROR WITH THE ENTRY KEYWORD             *
*          I.  EDIT, BROWSE, ISPF: DELETED CONTROL REFRESH AND        *
*              LINE MODE SWITCH IF INVOKED UNDER ISPMODE.             *
*          J.  PRINTOFF, SUBMIT, ..: ADDED SM CALL TO INVOKE SESSION  *
*              MANAGER FOR LINE MODE OUTPUTS IF UNDER ISPMODE.        *
*          K.  ISPMODE RECALL: FIXED A CURSOR POSITIONING ERROR.      *
*          L.  RESTORE: ADDED MODULE AND FIND KEYWORDS.               *
*          L.  ISPMODE AND MEMLIST: ADDED A SUBCOMMAND CONTINUATION   *
*              FEATURE WHICH IS ACTIVATED BY A SUBCOMMAND ENDING WITH+*
*                                                                     *
*        MEMLIST/ISPMODE OPTION 7: CHANGED TO PRINT THE CURRENT TABLE *
*        AS PROGRAMMED BY BOB ZIMMERMAN WITH CNA INSURANCE IN CHICAGO.*
*                                                                     *
*        FROM JOHN SULLIVAN WITH LOMA LINDA MEDICAL CENTER:           *
*          A.  CHANGED THE GRP= TEXT ON THE PDS300A MESSAGE TO MEM=   *
*                                                                     *
*        ISPMODE OUTPUT: MODIFIED TO SUPPORT "PROFILE NOMSGID" AS     *
*        SUGGESTED BY KEVIN MC CARTHY WITH MC DONALD'S CORPORATION    *
*        IN CHICAGO.                                                  *
*                                                                     *
*        FROM SAM LEPORE WITH WELLS FARGO BANK IN SAN FRANCISCO:      *
*          A.  RENAME: ADDED THE REPBY KEYWORD.                       *
*          B.  REPLACE: ADDED A STRING REPLACEMENT SUBCOMMAND.        *
*                                                                     *
*        MAP: ADDED A DISPLAY OF UNRESOLVED AND WEAK UNRESOLVED       *
*        EXTERNAL REFERENCES AS SUGGESTED BY DENICE ROBERG WITH       *
*        NORTHERN TRUST COMPANY IN CHICAGO.                           *
*                                                                     *
*        FROM BILL SMITH WITH SYNTEX CORPORATION:                     *
*          A.  CHANGE AND PDS: ADDED THE VOLSET KEYWORD               *
*          B.  MEMLIST: ALLOWED ATTENTIONS FOR EDIT AND BROWSE        *
*          C.  VPSPRINT: ADDED AS A SUBCOMMAND                        *
*          D.  FIND, IF, MAP, HISTORY, REPLACE: GENERATE A MESSAGE    *
*              IF NO DATA IS FOUND FOR A SUBCOMMAND.                  *
*          E.  HISTORY: MESSAGE PDS250I WAS ADDED TO DISPLAY          *
*              COBOL COMPILE OPTIONS AS ORIGINALLY PROGRAMMED         *
*              BY REX WIDMER WITH TWA IN KANSAS CITY.                 *
*          F.  OUTCOPY: ADDED THE IEBUPDTE/NOIEBUPDTE OPERANDS.       *
*          G.  MAP: ADDED RELINK OPTION TO GENERATE JCL AND LKED      *
*              CONTROL STATEMENTS (OVERLAY PROCESSING TAKEN FROM      *
*              ARON EISENPRESS'S VERSION OF THE OVERLAY TREE          *
*              PROCESSOR V1.4 -- AN IBM TYPE 3 PROGRAM).              *
*                                                                     *
*        HISTORY: IF THE MODULE KEYWORD IS USED FOR A MEMBER GROUP,   *
*        ONLY MEMBERS WITH MODULE MATCHES WILL HAVE ANY DATA LISTED.  *
*                                                                     *
*        IF: CHANGED AND CREATED KEYWORDS - ADDED A MM/DD/YY DATE     *
*        FORMAT IN ADDITION TO THE YYDDD FORMAT.                      *
*                                                                     *
*        FIXPDS: ADDED OPTCD AND NOOPTCD OPERANDS TO ALLOW PDS TO     *
*        MODIFY THE OPTCD FIELD IN A DATA SET'S DCB AS SUGGESTED      *
*        BY ARNOLD CASINGHINO WITH CONNECTICUT BANK AND TRUST.        *
*                                                                     *
*        COMPRESS: ADDED IEBCOPY AUTHORIZATION VIA THE USE OF         *
*        IKJEFTSR AS SUGGESTED BY MIKE LOOS WITH DELUXE CHECK.        *
*                                                                     *
*        HISTORY: ADDED A MESSAGE FOR ZAP RECORDS WITH UNMATCHED      *
*        CSECT NAMES (?UNKNOWN) -- THESE WERE PREVIOUSLY IGNORED.     *
*                                                                     *
*        CONTROL: ADDED A TRANSLATOR KEYWORD TO CONTROL HISTORY       *
*        OUTPUTS AS SUGGESTED BY HOWARD DEAN WITH SYNTEX CORPORATION. *
*                                                                     *
*        COMPARE: ALLOWED USE WITH SEQUENTIAL DATA SETS AND ALLOWED   *
*        EITHER OLD OR NEW NAMES TO REFER TO A DIFFERENT DATA SET.    *
*                                                                     *
*        SPF COMMAND TABLE ENTRY TO PDS WITH NO DATA SET NAME:        *
*        ABEND 0C4 WAS FIXED - NOTED BY RON LEBAUBE WITH MONSANTO.    *
*                                                                     *
*        MEMBERS: PDS165I MESSAGE WAS ADDED TO SUPPORT SUBLIST.       *
*                                                                     *
*        DELETE:                                                      *
*          A.  THE ORDER OF MEMBER DELETES WAS CHANGED TO USE         *
*              INVERSE COLLATING SEQUENCE FOR I/O EFFICIENCY.         *
*          B.  MEMBER LIST SUPPORT WAS ADDED IN SUPPORT OF SUBLIST.   *
*                                                                     *
*        TSOMON ABEND IN ISPF V2: PDS WAS CHANGED TO RESTORE ITS      *
*        COMMAND BUFFER AS NOTED BY KEITH MOE AT AMDAHL CORPORATION.  *
*                                                                     *
*        EXCP: ADDED A TTR CHECK TRAP FOR READ MULTIPLE LOGICAL       *
*        FAILURES (PDS893E ERROR MESSAGE).                            *
*                                                                     *
*        EXCP: ADDED LOGIC TO AVOID A MVCL (44K MOVE) INSTRUCTION     *
*        EXCEPT WHEN A NEW TTR IS SELECTED.                           *
*                                                                     *
*        FIND AND REPLACE: SHORTENED THE SEARCH PATH LENGTH.          *
*                                                                     *
*        FROM STEVE SMITH WITH SECURITY PACIFIC BANK:                 *
*          A.  MODIFIED THE &CONDRNG INSTALLATION OPTION TO MAKE      *
*              MEMBER RANGE  AAA:  IDENTICAL TO ENTERING  AAA:AAA     *
*          B.  MEMLIST: MODIFIED CURSOR SUPPORT TO REMAIN AT THE      *
*              LAST ENTERED LINE COMMAND.                             *
*          C.  PDSM711R MESSAGE ADDED TO PDSM71 (RESPONSE REQUIRED).  *
*          D.  COMMAND LINE ATTRIB AND SPF: ADD TO THE MEMBER LIST.   *
*          E.  MEMLIST: CLEARING OF ERROR STATUS WAS IMPROVED.        *
*          F.  MEMLIST: ADDED LOAD MODULE LINKAGE EDIT DATES.         *
*          G.  RELEASE: ADDED VOLUME(SERIAL) PARAMETER IN TSO COMMAND *
*              (ALSO, NOTE ENABLE/DISABLE ZAP AT LABEL FIXDCB70).     *
*          H.  ISPMODE: ADDED A ISPMODE TABLE FIND FUNCTION.          *
*          I.  RECALL: MODIFIED IN ISPMODE TO ADD SEVERAL PARAMETERS. *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'P D S  --  PDS MACROS                          6/10/85'
         MACRO
&NAME    ENTER &TYPE
         LCLC  &LABEL
&LABEL   SETC  'IHB'.'&SYSNDX'
         AIF   ('&TYPE' EQ 'VALCHECK').VALCK
         AIF   ('&TYPE' EQ 'ATTNEXIT').ATTN
         AIF   ('&TYPE' EQ '').MAIN
         MNOTE 8,'INVALID TYPE ''&TYPE'''
         MEXIT
.MAIN    ANOP
&NAME    CSECT
         SAVE  (14,12),,*
         LCLA  &R
&R       SETA  0
.EQU     ANOP
R&R      EQU   &R
&R       SETA  &R+1
         AIF   (&R LT 16).EQU
         LR    R9,R15
         USING &NAME,R9,R10,R11,R12
         LA    R12,2048
         LA    R10,2048(R9,R12)
         LA    R11,2048(R10,R12)
         LA    R12,2048(R11,R12)
         MEXIT
.VALCK   ANOP
&NAME    CSECT
         SAVE  (14,12)
         LR    R8,R15             BASE REGISTER ADDRESS
         USING &NAME,R8           BASE REGISTER NOTIFICATION
         L     R7,4(,R1)          WORK AREA ADDRESS
         LM    R9,R12,BASES       RESTORE BASE REGISTERS
         L     R6,0(R1)           PDE ADDRESS
         LA    R15,VALSAVE
         AGO   .CHAIN
.ATTN    ANOP
&NAME    SAVE  (14,12)
         L     R7,8(,R1)
         LM    R9,R12,BASES
         LA    R15,ATTNSAVE
.CHAIN   ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         SPACE 1
         MEND
         SPACE 1
         MACRO TSMSG   -   OUTPUT A STATUS OR ERROR MESSAGE WITH MSGID
&NAME    TSMSG &NUM
         AIF   ('&NUM' EQ '(1)' OR '&NUM' EQ '(R1)').CALL
&NAME    LA    R1,&NUM
         BAL   R14,TSMSGRT
         MEXIT
.CALL    ANOP
&NAME    BAL   R14,TSMSGRT
         MEND
         SPACE 2
         MACRO
&L       $T    &Z,&PF,&RTN,&O,&PCL,&P,&PP,&CR,&HD
         LCLC  &NAME
&NAME    SETC  '&Z'.' '
&L       DC    0F'0',CL8'&NAME'                *** SUBCOMMAND NAME
         DC    X'&PF',AL3(&RTN)                *** OFF, SUBCOMMAND ADDR
         DC    AL1(&O),AL3(&PCL)               *** FLAGS, PCL ADDRESS
         DC    AL1(&P),AL3(&PP)                *** FLAGS, PP ADDRESS
         DC    CL8'&CR'                        *** ATTACHED TSO COMMAND
         DC    AL2(&HD-ONIX),XL2'FFFF'         *** HELP, AVAILABLE
.EXIT    MEND
         SPACE 2
         MACRO
&L       $TL   &F,&F2,&TB,&P,&P2,&PMID                         SS NOV84
&L       DC    AL1(&F),AL1(&F2)                *** FLAGS 1 AND 2
         DC    AL2(&TB-$TBL)                   *** TABLE OFFSET
         DC    AL1(&P),AL1(&P2)                *** PROC1,PROC2 SS NOV84
         DC    AL2(&PMID)                      *** MSGID       SS NOV84
         MEND
         SPACE 2
         MACRO
&L       $TX   &F,&F2,&TB,&P,&P2,&PMID                         SS NOV84
&L       DC    AL1(&F+#X),AL1(&F2)             *** FLAGS 1 AND 2
         DC    AL2(&TB-ISP$TX)                                 SS SEP84
         DC    AL1(&P),AL1(&P2)                *** PROC1,PROC2 SS NOV84
         DC    AL2(&PMID)                      *** MSGID       SS NOV84
         MEND
         SPACE 2
         MACRO                                                 SS NOV84
&L       $TC   &F,&F2,&TB,&P,&P2,&PMID                         SS NOV84
&L       DC    AL1(&F),AL1(&F2+#C)             *** FLAGS 1     SS NOV84
         DC    CL2&TB                                          SS NOV84
         DC    AL1(&P),AL1(&P2)                *** PROC1,PROC2 SS NOV84
         DC    AL2(&PMID)                      *** MSGID       SS NOV84
         MEND
         SPACE 2
         MACRO
&NAME    EXIT  &LV=
         AIF   ('&LV' EQ '').A0
&NAME    LR    R2,R13                         ADDR OF THIS SAVE AREA
         L     R13,4(,R13)
         AGO   .A1
.A0      ANOP
&NAME    L     R13,4(,R13)
.A1      STM   R15,R1,16(R13)                 RETURN REGS 15, 0, 1
         AIF   ('&LV' EQ '').A2 NOT DYNAMIC STORAGE
         FREEMAIN R,LV=&LV,A=(R2)
.A2      ANOP
         RETURN (14,12),T
         MEND
         SPACE 2
         MACRO
&NAME    MSG   &TEXT
         LCLA  &A
&A       SETA  K'&TEXT-2+4
&NAME    DC    AL2(&A,0),C&TEXT              NORMAL MESSAGE
         MEND
         SPACE 3
         MACRO $PUTGET -  OUTPUT A DATA LINE AND GET RESPONSE
&NAME   $PUTGET  &LINE,&ATTN=
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
&NAME    LA    R1,&LINE
         BAL   R14,$PUTGET
         AGO   .ATTN
.R       ANOP
&NAME    LR    R1,&LINE(1)
         BAL   R14,$PUTGET
         AGO   .ATTN
.CALL    ANOP
&NAME    BAL   R14,$PUTGET
.ATTN    ANOP
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                         EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                             IGNORE ATTENTIONS
.END     MEND
         SPACE 2
         MACRO $PUTLINE -  OUTPUT A DATA LINE
&NAME   $PUTLINE &LINE,&ATTN=
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
&NAME    LA    R1,&LINE                      ADDRESS OF DATA LINE
         BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
         AGO   .ATTN
.R       ANOP
&NAME    LR    R1,&LINE(1)                   ADDRESS OF DATA LINE
         BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
         AGO   .ATTN
.CALL    ANOP
&NAME    BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
.ATTN    ANOP
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                         EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                             IGNORE ATTENTIONS
.END     MEND
         SPACE 2
         MACRO MESSAGE - OUTPUT AN ERROR/STATUS MESSAGE
&NAME    MESSAGE &MSG1,&MSG2
         AIF   ('&MSG1' EQ '(1)' OR '&MSG1' EQ '(R1)').MSGDS
         AIF   ('&MSG1'(1,1) EQ '(').RMSG1
         AIF   ('&MSG1' EQ 'MSGTEXT1' AND '&MSG2' EQ '').NOR1
&NAME    LA    R1,&MSG1
         AGO   .MSG2
.NOR1    ANOP
&NAME    BAL   R14,MESSTXT1
         MEXIT
.RMSG1   ANOP
&NAME    LR    R1,&MSG1(1)
         AGO   .MSG2
.MSGDS   ANOP
         AIF   ('&MSG2' NE '').SECOND
&NAME    BAL   R14,MESSAGE0
         MEXIT
.SECOND  ANOP
&NAME    DS    0H
.MSG2    ANOP
         AIF   ('&MSG2' EQ '').NOMSG2 NO SECONDARY MESSAGE
         AIF   ('&MSG2' EQ '(0)' OR '&MSG2' EQ '(R0)').CALL
         AIF   ('&MSG2'(1,1) EQ '(').RMSG2
         LA    R0,&MSG2
         AGO   .CALL
.RMSG2   ANOP
         LR    R0,&MSG2(1)
.CALL    ANOP
         BAL   R14,MESSAGE
         AGO   .END
.NOMSG2  ANOP
         BAL   R14,MESSAGE0
.END     MEND
         SPACE 3
         MACRO
&L       COND  &OPRAND,&OPRAT
         AIF   ('&L' EQ '').EXIT
         &OPRAND &OPRAT
.EXIT    MEND
         SPACE 2
         MACRO
&L       CONDIKJ &OPRAT
         AIF   ('&L' EQ '').EXIT
         IKJNAME &OPRAT
.EXIT    MEND
         COPY  PDSGEN71   ***  ESTABLISH THE PDS GLOBAL VALUES SS AUG84
         SPACE 2
&CBRO    SETC  'BROWSE'   ***  SPF BROWSE SERVICE NAME
&CEDI    SETC  'EDIT'     ***  SPF EDIT SERVICE NAME
         SPACE 1
         AIF   ('&MVSLEV' EQ 'MVS038').MVSOK
         AIF   ('&MVSLEV' EQ 'MVS12X').MVSOK
         AIF   ('&MVSLEV' EQ 'MVS13X').MVSOK
         SPACE 3
         MNOTE 12,'*** MVSLEV -- MVS LEVEL &MVSLEV IS INVALID ***'
         SPACE 2
         AGO   .NOGEN
.MVSOK   ANOP
         AIF   ('&CISP' EQ '').NOSPF
         AIF   ('&CISP' EQ 'SPF').SMNOTE
         AIF   ('&CISP' EQ 'ISPF').SMNOTE
         AIF   ('&CISP' EQ 'ISPFV2').SMNOTE
         SPACE 3
         MNOTE 12,'*** CISP -- SPF CONTROL &CISP IS INVALID ***'
         SPACE 2
         AGO   .NOGEN
.NOSPF   ANOP
&CISP    SETC  'NO SPF'
&NBRO    SETC  ''          *** NULLIFY THE SPF SUBCOMMANDS -- NO SPF
&NEDI    SETC  ''          *** NULLIFY THE SPF SUBCOMMANDS -- NO SPF
&NISP    SETC  ''          *** NULLIFY THE SPF SUBCOMMANDS -- NO SPF
&NSPF    SETC  ''          *** NULLIFY THE SPF SUBCOMMANDS -- NO SPF
.SMNOTE  ANOP
         SPACE 3
         MNOTE *,'&CISP INTERFACE CODE WILL BE GENERATED'
         SPACE 2
.NOERR   ANOP
         TITLE 'P D S  --  PDS PROGRAM ENTRY                   6/10/85'
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
***                                                                 ***
***    HISTORICAL SECTION:                                          ***
***                                                                 ***
***                                                                 ***
***     VERSION   LAST MOD   SIZE   IN K   # SUBS   SOURCE   HELP   ***
***     =======   ========   ====   ====   ======   ======   ====   ***
***                                                                 ***
***      PDS30    1/28/80    47A0    18K     19      4851     512   ***
***                                                                 ***
***      PDS45    8/20/81    45A8    18K     22      5037     517   ***
***                                                                 ***
***      PDS52   11/08/82    6090    25K     25      6253    1123   ***
***                                                                 ***
***      PDS53    4/06/83    9610    38K     28      8978    1502   ***
***                                                                 ***
***      PDS60    9/30/83    CF28    52K     32     12068    1997   ***
***                                                                 ***
***      PDS61    2/06/84    E948    59K     34     13496    2270   ***
***                                                                 ***
***      PDS62    7/31/84   11F58    72K     39     16062    3676   ***
***                                                                 ***
***      PDS63   10/08/84   12DB0    76K     40     17122    3782   ***
***                                                                 ***
***      PDS70    1/20/85   16CD8    92K     41     18185    4112   ***
***                                                                 ***
***      PDS71    6/10/85   1B6E0   110K     44     20190    4675   ***
***                                                                 ***
***                                                                 ***
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         SPACE 5
PDS71    ENTER
         WXTRN VTSOCMD,VSUBCMD      OPTIONAL PDS EXIT ROUTINES
         USING WORKAREA,R7          WORK AREA DSECT
         SPACE 1
         LA    R0,0                 POSSIBLE ZAP LOCATION
         L     R8,=A(PDSINIT)       ADDRESS OF INITIALIZATION CSECT
         BR    R8
         SPACE 1
ABEND0C1 DC    H'0'                 ADDED FOR TEST FUNCTION
         SPACE 1
KLEAR    LA    R2,NEWCMD            ADDED FOR TEST FUNCTION
         B     CLEAR                ADDED FOR TEST FUNCTION
         SPACE 2
PDSINIT  CSECT
         USING *,R8
         LR    R6,R1                ADDRESS OF CPPL
         L     R1,0(,R1)            POINT TO PASSED PARAMETER LIST
         CLC   2(2,R1),=C'++'       SPF ADDRESS MARKER FROM PDS?
         BNE   GETMAIN              NO, NOT AN ENTRY FROM SPF
         CVB   R7,4(,R1)            WORK AREA POINTER
         L     R0,PDSENTRY          NUMBER OF ENTRIES
         A     R0,=F'1'
         ST    R0,PDSENTRY
         C     R0,=F'1'             FIRST SPF ENTRY?
         BH    RETURN2              NO, END STATEMENT
         LA    R15,SPFMAIN          SPF REGISTER SAVE AREA
         ST    R15,8(,R13)
         ST    R13,4(,R15)          CHAIN TO SECONDARY SAVEAREA
         LR    R13,R15
         MVI   ISPFTEXT+1,12        LENGTH OF COUNT AND "ISPF    "
         NI    FLAGSFF,FF-FSPFERR   SPF ERROR OFF
         OI    FLAGSFF,FSPFCALL     SPF CALL ACTIVE
         MVC   STAEPARM(4),STAELIST   RESET THE ESTAE EXIT
         L     R2,=A(STAEEXIT)        ESTAE EXIT ADDRESS
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)
         B     RESTART0               REALLOCATE THE DATA FILES
         SPACE 3
GETMAIN  L     R5,WORKSIZE
         LA    R0,0(,R5)                CLEAR THE TOP BYTE
         GETMAIN RU,LV=(0),SP=1,BNDRY=PAGE
         LR    R7,R1                    WORK AREA ADDRESS
         LR    R4,R1                    CLEAR WORK AREA
         SR    R1,R1                    NULL SOURCE
         MVCL  R4,R0                    FILL WITH PAD 0 FROM R1(0:7)
         SPACE 1
         L     R1,0(,R6)                POINT TO PASSED PARAMETER LIST
         CLI   2(R1),X'40'              INVOKED AS AN ISPF PGM()?
         BNH   INITADDR                 NO, BRANCH
*
***   PDS HAS BEEN INVOKED AS AN ISPF PGM()  -- TO ALLOW THE USE OF
*     NORMAL TSO SERVICES, A CPPL MUST BE BUILT.
*     A.  GETMAIN AN AREA FOR CREATED CONTROL BLOCKS IN SUBPOOL 78
*         (THE SUBPOOL TSO USES FOR ECT I/O SERVICE AND WORK AREAS)
*     B.  COPY IN THE SYSTEM UPT AND PSCB ADDRESSES
*     C.  CREATE A TSO COMMAND BUFFER FROM THE INPUT PARAMETERS
*     D.  CREATE AN ECT AREA
*         1.  THE ECT POINTS TO AN I/O SERVICE AREA
*         2.  THE I/O SERVICE AREA POINTS TO AN I/O WORK AREA WHICH
*             IS INITIALLY 32 BYTES LONG.
*     E.  A METHOD OF LOCATING THE ACTUAL TSO ECT HAS BEEN DEVELOPED.
*         IN A FOLLOWING RELEASE OF PDS, THE CREATED ECT AREA WILL
*         BE DELETED; FOR NOW, THE LOCATED ECT IS ACTUALLY USED.
*     F.  AT PROGRAM END, CLEAN UP THE OBTAINED AREA
*         1.  IF THE I/O WORK AREA WAS FREED AND EXPANDED, RELEASE
*             THE NEW I/O WORK AREA.
*         2.  RELEASE THE OBTAINED SUBPOOL 78 CONTROL BLOCK AREA.
         SPACE 1
         OI    FLAGSFF,FSPFDIAL+FSPFOPT6  SET APPROPRIATE SPF FLAGS
         MVI   SUBPDIAL,78              GETMAIN SUBPOOL
         LA    R0,16+96+64+16+32        GETMAIN AREA SIZE
         ICM   R0,B'1000',SUBPDIAL      GETMAIN SUBPOOL
         GETMAIN R,LV=(0)
         XC    0(16+96+64+16+32,R1),0(R1)
         STCM  R1,B'0111',SUBPDIAL+1    CREATED CPPL START
         LA    R2,16(,R1)               CREATED CBUF START
         LA    R3,96(,R2)               CREATED ECT START
         LA    R4,64(,R3)               CREATED I/O SERVICE AREA
         LA    R5,16(,R4)               CREATED I/O WORK AREA
         ST    R2,CPPLCBUF-CPPL(,R1)    START OF CBUF
         ST    R3,CPPLECT-CPPL(,R1)     START OF ECT
         MVI   0(R5),X'80'              MARK END OF THE LIST
         ST    R5,0(,R4)                FIRST I/O WORK ELEMENT
         ST    R5,4(,R4)                LAST USED I/O WORK ELEMENT
         MVC   8(2,R4),=H'32'           INITIAL I/O WORK AREA LENGTH
         L     R5,16                    --> CVT
         L     R5,0(,R5)                --> TCB WORDS
         L     R5,4(,R5)                --> TCB
         L     R5,180(,R5)              --> JSCB
         L     R5,264(,R5)              --> PSCB
         ST    R5,CPPLPSCB-CPPL(,R1)
         L     R5,52(,R5)               --> UPT
         ST    R5,CPPLUPT-CPPL(,R1)
         MVC   ECTPCMD-ECT(8,R3),BLANKS CLEAR THE PRIMARY COMMAND
         MVC   ECTSCMD-ECT(8,R3),BLANKS CLEAR THE SECONDARY COMMAND
         ST    R4,ECTIOWA-ECT(,R3)      START OF THE I/O SERVICE AREA
         L     R15,0(,R6)               START OF THE PARM AREA
         LR    R6,R1                    POINT TO THE CREATED CPPL
         LH    R5,0(,R15)               PARM LENGTH
         LA    R1,4(,R5)
         STH   R1,0(,R2)                CBUF STRING LENGTH
         MVC   4(92,R2),BLANK128        CLEAR THE CBUF AREA
         BCTR  R5,0                     MACHINE STRING LENGTH
         MVC   4(*-*,R2),2(R15)         <<EXECUTED>>
         EX    R5,*-6                   MOVE IN THE PARM STRING
         LA    R15,4(,R2)               PREPARE FOR STRING SCAN
         LR    R14,R15
         SPACE 1
         LA    R15,1(,R15)              FIND
         CLI   0(R15),X'40'                 FIRST
         BNE   *-8                               BLANK
         SPACE 1
         LR    R5,R15
         SR    R5,R14                   LENGTH OF PDS PROGRAM NAME
         BCTR  R5,0                     MACHINE LENGTH
         MVC   ECTPCMD-ECT(*-*,R3),4(R2)  <<EXECUTED>>
         EX    R5,*-6                   MOVE IN THE PDS PROGRAM NAME
         SPACE 1
         LA    R15,1(,R15)              FIND
         CLI   0(R15),X'40'                 NEXT
         BE    *-8                              NON-BLANK
         SPACE 1
         SR    R15,R14
         STH   R15,2(,R2)               OFFSET TO OPERANDS
         MVI   ECTSWS2-ECT(R3),ECTDEFCS DEFAULT DELETE CHARACTERS USED
         LOAD  EP=ISPLINK               ADDRESS OF SPF INTERFACE MODULE
         ST    R0,ISPLINK               SAVE ADDRESS FOR LATER
         SPACE 3
         NOP   INITADDR                   ZAP FOR TESTING
         L     R2,X'21C'               ** SEARCH TCB'S METHOD
         L     R2,X'7C'(,R2)              --> FIRST JSTCB
         L     R2,X'88'(,R2)              --> DAUGHTER TCB
         L     R2,X'50'(,R2)              --> IKJEFT01 REGISTER 8
         L     R2,X'2C'(,R2)              --> ECT
         B     FECTUPD                    ZAP FOR TESTING
         SPACE 1
         L     R1,SUBPDIAL             ** USE THE PSCBRLGB AREA
         L     R2,CPPLPSCB-CPPL(,R1)       --> PSCB
         L     R2,PSCBRLGB-PSCB(,R2)      --> RELOGON BUFFER
         L     R2,X'100'(,R2)             --> ECT
         SPACE 1
FECTUPD  L     R1,SUBPDIAL             ** UPDATE THE ECT ADDRESS
         ST    R2,CPPLECT-CPPL(,R1)       NEW ECT POINTER
         MVC   ECTPCMD-ECT(8+8,R2),ECTPCMD-ECT(R3)
         SPACE 3
INITADDR LM    R2,R4,CPPLUPT-CPPL(R6)   INITIALIZE ADDRUPT,
         L     R5,CPPLCBUF-CPPL(R6)       ADDRPSCB, ADDRECT,
         STM   R2,R6,ADDRUPT              ADDRCBUF AND ADDRCPPL
         ST    R5,ADDRCBU2              SAVE ORIGINAL CBUF ADDR
         MVC   PDSNAME,ECTPCMD-ECT(R4)  SAVE THE PDS COMMAND NAME
         CLI   ECTSCMD-ECT(R4),X'40'      IS PDS A SUBCOMMAND?
         BE    INITADD2                   NO, BRANCH
         CLC   ECTSCMD-ECT(3,R4),=C'GO '  GO (UNDER TEST)?
         BE    INITADD2                   YES, BRANCH
         CLC   ECTSCMD-ECT(4,R4),=C'END ' RUN (UNDER TEST)?
         BE    INITADD2                   YES, BRANCH
         MVC   PDSNAME,ECTSCMD-ECT(R4)    SAVE THE PDS COMMAND NAME
         SPACE 1
*  SET DEFAULTS FOR CONTROL --
INITADD2 OI    FLAGSGG,&CONGLBL         SET DEFAULT GLOBAL VARIABLES
         OI    FLAGSJJ,&CONADEF         SET DEFAULT ALLOCATION MSG
         TM    ECTSWS-ECT(R4),ECTBKGRD  BACKGROUND MODE?
         BNO   INITADD3                 NO, BRANCH
         OI    FLAGSEE,FBKGRND          YES, SET A FLAG FOR LATER
         OI    FLAGSGG,FRECVCON         YES, NO RECOVERY IS A DEFAULT
         SPACE 1
INITADD3 STM   R9,R12,BASES             SAVE BASE REGISTERS
         MVC   BLDLLIST,BLDLPARM        BLDL NUMBER OF MEMBERS & LENGTH
         ST    R7,8(,R13)               CHAIN
         ST    R13,4(,R7)                    SAVE
         LR    R13,R7                            AREAS
         SPACE 1
         L     R5,WORKBUFF
         LA    R0,0(,R5)                CLEAR THE TOP BYTE
         GETMAIN RU,LV=(0),SP=1,BNDRY=PAGE
         L     R0,=F'32768'             MAXIMUM BLOCKSIZE
         ST    R1,IOBUFF1               INPUT BUFFER 1
         AR    R1,R0
         ST    R1,IOBUFF2               INPUT BUFFER 2
         AR    R1,R0
         ST    R1,IODIR1                FIRST DIR IOAREA
         AR    R1,R0
         ST    R1,IODIR2                SECOND DIR IOAREA
         SPACE 1
         LA    R1,2048(,R7)
         LA    R1,2048(,R1)             POINT TO DMEMAREA (MEMBER LIST)
         ST    R1,PMEMMIN               FIRST ELEMENT OF THE LIST
         ST    R1,PMEMMAX               LAST ELEMENT OF THE LIST
         LA    R0,TSMSGWRK-WORKAREA-4096(R1)  POINT TO TSMSGWRK
         ST    R0,MTEXTRCT              SAVE EXTRACT ADDRESS
         MVI   MTEXTRCT,124             MAXIMUM TEXT LENGTH
         LA    R1,24(,R1)               SECOND MEMBER LIST POINTER
         ST    R1,MMEMMIN               FIRST ELEMENT OF THE LIST
         ST    R1,MMEMCURR              CURRENT ELEMENT OF THE LIST
         ST    R1,MMEMMAX               LAST ELEMENT OF THE LIST
         GTSIZE ,                       TERMINAL PAGESIZE/LINESIZE
         STM   R0,R1,PAGESIZE           LINES/SCREEN
*        ST    R1,LINESIZE              CHARACTERS/LINE
         SPACE 1
         MVC   STAEPARM(4),STAELIST     SET A ESTAE EXIT
         L     R2,=A(STAEEXIT)          ESTAE EXIT ADDRESS
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)
         SPACE 1
         LA    R14,MTCSECTP            START OF MESSAGE DESCRIPTOR
         L     R15,ADDRCPPL            START OF THE CPPL
         LA    R0,ATTNECB              ECB TO USE
         STM   R14,R0,MTPLPTR          INITIALIZE IKJEFF02 FIELDS
         OI    MTHIGH,X'80'            STANDARD LINKAGE
         OI    MTSW1,MTPUTLSW+MTJOBISW MESSAGE -- PUTLINE AND COMPRESS
         MVI   MTMSGID,C'L'            MESSAGE IDENTIFIER (FIRST BYTE)
         LA    R1,INSERT#1             ADDRESS OF INSERT 1
         ST    R1,MTADDR-1             START OF INSERT INFORMATION
         MVI   MTHIGHL,8               LENGTH OF THE STANDARD INSERT
         LA    R1,INSERT#2             ADDRESS OF INSERT 2
         ST    R1,MTADDR+4-1           START OF INSERT INFORMATION
         MVI   MTHIGHL+4,8             LENGTH OF THE STANDARD INSERT
         MVI   VOLSET,C'*'             INITIALIZE VOLSET
         MVI   FIXPDSD+4,X'80'         INITIALIZE WRITE DECB
         TITLE 'P D S  --  PDS SERVICE ROUTINE ADDRESSES       6/10/85'
*
*    LOCATE THE TSO SERVICE ROUTINES AND THE DISK ADDRESS CONVERTERS
*
         LA    R2,ADDRPUTL              FIRST SERVICE ROUTINE ADDRESS
         LA    R3,MODULEN               FIRST LOAD ADDRESS
         SPACE 1
SERVLOOP L     R1,CVTPTR                CVT ADDRESS
         AH    R1,0(,R3)                OFFSET IN THE CVT
         ICM   R0,B'1111',0(R1)         ANY PRELOADED ADDRESS?
         BNZ   SERVCVTA                 YES, BRANCH
***      XC    PARMLIST(12),PARMLIST    CLEAR THE PARMLIST AREA
         LOAD  EPLOC=2(R3)  ***,SF=(E,PARMLIST)
         MVI   0(R2),X'D6'              MARK TO DELETE LATER
         SPACE 1
SERVCVTA STCM  R0,B'0111',1(R2)         UPDATE THE ADDRESS
         LA    R2,4(,R2)                NEXT SERVICE ADDRESS
         LA    R3,10(,R3)               NEXT MODULES ADDRESS
         CLI   0(R3),X'FF'              END OF MODULES?
         BNE   SERVLOOP                 NO, BRANCH
         SPACE 2
         L     R2,ADDRPSCB              ADDRESS OF THE PSCB
         MVC   PARMLIST(4),ENQSTEP      SET OPTION BITS FOR ENQ
         ENQ   (SPFUSER,PSCBUSER-PSCB(R2),E,7,STEP),RET=TEST,          X
               MF=(E,PARMLIST)          CHECK FOR SPF ENQ
         SPACE 1
         LTR   R15,R15                  SPF ALREADY INITIALIZED?
         BZ    PRIM00                   NO, BRANCH
         XI    FLAGSFF,FSPFOPT6         YES, INVERT THE OPTION 6 FLAG
         TM    FLAGSFF,FSPFDIAL         DIALOG ALREADY?
         BO    *+8                      YES, BRANCH
         BAL   R2,CLEAR1                NO, WAKE UP SESSION MANAGER
         TITLE 'P D S  --  PDS PRIMARY SUBCOMMAND CHECK        6/10/85'
*
*     GET THE DATA SET NAME AND ANY SUBCOMMAND FROM THE COMMAND LINE
*
         SPACE 1
PRIM00   L     R2,ADDRCBUF         COMMAND BUFFER
         LH    R3,0(,R2)           TOTAL COMMAND LENGTH
         LR    R1,R3               TOTAL COMMAND LENGTH
         BCTR  R1,0                MACHINE LENGTH
         LA    R14,FIRST4K         BASE FOR RECALLST
         MVC   RECALLST-FIRST4K(*-*,R14),0(R2) <<EXECUTED>>
         EX    R1,*-6              SAVE PREVIOUS SUBCOMMAND
         XC    RECALLST-FIRST4K(2,R14),=F'0'
         SPACE 1
         LH    R4,2(,R2)           OFFSET TO OPERANDS
         LA    R4,4(,R4)           PLUS COUNT BYTES
         LA    R2,0(R2,R4)         START OF OPERANDS
         SR    R3,R4               BYTES LEFT TO SCAN - ANY?
         BNP   PRIM90              NO, BRANCH
         SPACE 1
PRIM10   OI    0(R2),X'40'         UPPER-CASE THE OPERANDS
         CLI   0(R2),X'40'         BLANK?
         BE    PRIM20              YES, BRANCH
         LA    R2,1(,R2)           NO, TRY THE NEXT ONE
         BCT   R3,PRIM10
         B     PRIM90              NOT FOUND, BRANCH
         SPACE 1
PRIM20   OI    0(R2),X'40'         UPPER-CASE THE OPERANDS
         LA    R5,PRIMOPTN-2       PDS VALID KEYWORD TABLE START -2
         CLI   0(R2),X'40'         BLANK?
         BNE   PRIM30              NO, BRANCH
         LA    R2,1(,R2)           YES, TRY THE NEXT ONE
         BCT   R3,PRIM20
         B     PRIM90              NOT FOUND, BRANCH
         SPACE 1
PRIM30   LA    R5,2(,R5)           NEXT VALID KEYWORD ENTRY
         CLI   0(R5),X'FF'         END OF KEYWORD TABLE?
         BE    PRIM40              YES, FOUND A SUBCOMMAND
         CLC   0(1,R2),0(R5)       FIRST CHARACTER MATCH?
         BNE   PRIM30              NO, CONTINUE LOOPING
         SPACE 1
         C     R3,=F'1'            ANOTHER CHARACTER?
         BNH   PRIM90              NO, NO SUBCOMMAND ADDED
         OI    1(R2),X'40'         UPPER-CASE THE NEXT CHARACTER
         CLC   1(1,R2),1(R5)       MATCH SECOND CHARACTER TOO?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         CLI   1(R2),X'40'         SECOND CHARACTER A BLANK?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         CLI   1(R2),C'('          PARENTHESIS FOLLOWING V (FOR VOL)?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         EJECT
PRIM40   LA    R0,256              COUNT OF SUBCOMMAND CHARACTERS
         ICM   R0,B'1000',=X'01'   SUBPOOL 1
         GETMAIN R,LV=(0)
         MVI   0(R1),X'40'         BLANK THE
         MVC   1(255,R1),0(R1)              SUBCOMMAND STORAGE
         XC    0(4,R1),0(R1)       CLEAR THE FIRST WORD
         MVI   0(R1),1             LENGTH=1*256
         ST    R1,SUBCMNDP         SAVE THE STORAGE POINTER
         SPACE 1
PRIM42   OC    4(1,R1),0(R2)       MOVE AND UPPER-CASE THE CHARACTER
         MVI   0(R2),X'40'         BLANK THE COMMAND LINE CHARACTER
         LA    R1,1(,R1)
         LA    R2,1(,R2)
         BCT   R3,PRIM42
         SPACE 3
PRIM90   ICM   R1,B'1111',SUBCMNDP ADDRESS OF SUBCOMMAND?
         BZ    PRIM92              NO - ZERO, BRANCH
         CLC   4(4,R1),$ISM        ISPMODE?
         BE    PRIM98              YES, BRANCH
         CLC   4(4,R1),$MML        MEMLIST?
         BE    PRIM98              YES, BRANCH
PRIM92   TM    FLAGSFF,FSPFDIAL    DIALOG ALREADY?
         BNO   PRIM96              NO, DO PROGRAM IDENTIFIER
         LA    R0,=CL8'CONTROL'    FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'    SECOND PARAMETER
         LA    R2,=CL8'SM     '    THIRD PARAMETER
         LA    R3,=F'1'            FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2      SAVE ADDRESSES
         OI    MSGTEXT2+12,X'80'   LAST
         LA    R1,MSGTEXT2             PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15             LET ISPF KNOW
         SPACE 2
PRIM96   MVC   INSERT#1(8),PDSNAME
         TSMSG L100$1
         MESSAGE MSGBLANK          ONE BLANK LINE
         SPACE 2
PRIM98   MVC   ##SUBCOM(PTW),$CHA  PCL FOR DATA SET NAME
         L     R15,=A(PARSE)       PARSE ADDRESS
         BALR  R14,R15             CALL PARSE
         B     EXIT12N             ERROR EXIT
         SPACE 2
         ICM   R1,B'1111',SUBCMNDP    ADDRESS OF SUBCOMMAND?
         ST    R1,ADDRCBUF
         BZ    PRIM99                 NO - ZERO, BRANCH
         OI    FLAGSBB,FCMD+FONESHOT  YES, INDICATE ONE SUBCOMMAND ONLY
         B     RESTART0                    AND SKIP THE PROGRAM MESSAGE
         SPACE 1
PRIM99   L     R1,$DSN+8           ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD         EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE   FLAG TO CONTINUE
         B     RESTART0
         DROP  R8
PDS71    CSECT
         TITLE 'P D S  --  PDS DATA SET INITIALIZATION         6/10/85'
RESTART0 DS    0H                  ** ALLOCATE OR REALLOCATE DATA SET
         SPACE 1
         MVC   DSPALLOC,DSPREQST   SET INITIAL DISPOSITION SHR/OLD
         SPACE 2
         L     R15,=A(ALLOCATE)    ALLOCATION ROUTINE ADDRESS
         XC    RECOVER,RECOVER     CANNOT RECOVER - NOT INITIALIZED YET
         BALR  R14,R15             ALLOCATE THE DATA SET
         B     EXIT12N             ALLOCATION UNSUCCESSFUL
         SPACE 1
RESTART2 MVC   INDCB(LEXCPDCB),EXCPDCB MOVE PATTERN DCB
         MVC   STOWDCB(LSAMDCB),SAMDCB ALSO FOR STOW OPERATIONS
         MVC   DCBDDNAM-IHADCB+INDCB,DDNAME
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVC   DCBDSORG-IHADCB+STOWDCB(1),DSORG
         TM    DSORG,DS1DSGPO                    DSORG=PO?
         BO    *+8                               YES, BRANCH
         MVI   DCBDSORG-IHADCB+STOWDCB,DS1DSGPS  NO, USE DSORG=PS
         SPACE 2
RESTART4 MVI   OPENLIST,X'00'      ** REOPEN THE DATA SET          **
         MVI   OPENLIST+4,X'80'
         XC    RECOVER,RECOVER     CANNOT RECOVER - NOT INITIALIZED YET
         XC    BLKSI(2),BLKSI
         OPEN  (STOWDCB,INPUT,INDCB,INPUT),MF=(E,OPENLIST)
         SPACE 1
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)       OPENED FOR DCB EXIT ONLY
         SPACE 1
         TM    DCBOFLGS-IHADCB+INDCB,DCBOFOPN  DCB OPEN?
         BNO   EXIT12O                         NO, QUIT
         SPACE 2
         NI    FLAGSCC,FF-FBLDLOK              BLDL IS INVALID NOW
         EJECT
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         MVC   NUMEXT+1(1),DEBNMEXT(R1)        NUMBER OF D.A. EXTENTS
         L     R4,DEBUCBAD(,R1)                ADDRESS OF FIRST UCB
         ST    R4,UCBADDR                      ADDRESS OF FIRST UCB
         MVC   BYTEUCB+1(1),UCBTYP+3(R4)       SAVE THE UCB DEVICE TYPE
         CLI   VOLALLOC,X'40'                  ANY VOLUME DISCOVERED?
         BNE   RESTART7                        YES, OBTAIN IS DONE
         MVC   VOLALLOC,UCBVOLI(R4)            SAVE VOLUME NAME USED
         L     R14,OBTAIN                      OBTAIN FLAGS
         LA    R15,DSNAME                      ADDRESS OF DSNAME
         LA    R0,VOLALLOC                     VOLUME SERIAL
         LA    R1,DS1FMTID                     OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST                 SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                       GET THE FORMAT 1 DSCB
         LTR   R15,R15                         SUCESSFUL OBTAIN?
         BZ    RESTART7                        YES, BRANCH
         SPACE 1
*     IF THE DSNAME IS AN ALIAS ENTRY IN A VSAM CATALOG, A LOCATE
*     WILL PUT THE TRUE NAME IN THE DSNAME FIELD -- ISSUE A LOCATE,
*     AND TRY THE OBTAIN AGAIN.
         MVC   WORKTBL(44),DSNAME              COPY DSNAME
         L     R14,LOCATE
         LA    R15,WORKTBL                     ACTUAL DSNAME
         SR    R0,R0                           NO CVOL VALUE
         LA    R1,MSGTEXT1                     LOCATE WORK AREA
         STM   R14,R1,PARMLIST                 SAVE CAMLIST PARAMETERS
         LOCATE PARMLIST
         LTR   15,15                           SUCCESSFUL LOCATE?
         BNZ   RESTART5                        NO, ERROR
         SPACE 1
         L     R14,OBTAIN                      OBTAIN FLAGS
         LA    R15,WORKTBL                     ACTUAL DSNAME
         LA    R0,VOLALLOC                     VOLUME SERIAL
         LA    R1,DS1FMTID                     OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST                 SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                       GET THE FORMAT 1 DSCB
         SPACE 1
         LTR   R15,R15                         SUCCESSFUL?
         BZ    RESTART7                        YES, BRANCH
         SPACE 1
RESTART5 TSMSG L830                     NO DSCB FOUND MESSAGE
         BAL   R2,SHUTSTW2              DELETE DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET
RESTART6 $PUTGET PDS381A,ATTN=EXIT12N   DATA SET RECOVERY MESSAGE
         SPACE 2
         TM    FLAGSBB,FNULL            USER WANT ANOTHER DATA SET?
         BO    RESTART6                 NULL REPLY, ASK AGAIN
         SPACE 1
         MVC   DSNLEN(SAVLEN),SAVDSN+1  RESET THE DATA SET NAME
         MVC   FULLWORD(4),##ADRCMD     SAVE CONTINUATION INFORMATION
         MVC   ##SUBCOM(PTW),$CHA       PCL FOR SUBCOMMAND NAME
         L     R15,=A(PARSE)            PARSE ADDRESS
         BALR  R14,R15                  INVOKE PARSE
         B     EXIT12N                  EXIT - PARSE FAILURE
         MVC   ##ADRCMD(4),FULLWORD     CONTINUATION INFORMATION
         B     RESTART0                 RETRY ALLOCATION
         SPACE 2
RESTART7 STM   R14,R12,12(R13)                 SAVE REGISTERS
         ICM   R0,B'0111',DS1LSTAR             DS1LSTAR --> MBBCCHHR
         SLL   R0,8                            MOVE TO HIGH POSTION
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         LA    R2,LSTARMBB                     RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BALR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SPACE 2
         LA    R0,IOECB                        ECB ADDRESS
         ST    R0,IOBECB                       SET INTO IOB
         LA    R0,INDCB                        DCB ADDRESSS
         ST    R0,IOBDCB                       SET INTO DCB
         MVI   IOB,X'C2'                       SET IOB FLAGS
         SPACE 2
         XC    TOTUSEDX(2+2),TOTUSEDX          CLEAR TOTUSEDX/TOTALLOX
         TM    DSORG,DS1DSGPO                  PARTITIONED DATA SET?
         BNO   RESTART9                        NO, BRANCH
         SPACE 1
         L     R14,=X'01080100'                R=1, K=08, DD=256
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         ST    R0,DIRTRACK                     DIRECTORY BLOCKS/TRACK
         CLC   DS1LSTAR(3),ZERO                NULL DATA SET?
         BE    RESTART9                        YES, BRANCH
         SPACE 1
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         LA    R0,1*256                        TTR=000001
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         LA    R2,IOBSEEK                      RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BALR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SPACE 1
         LA    R1,CCW0                         START OF CHANNEL PROGRAM
         ST    R1,IOBCCW
         MVC   CCW0(7*8),CCWDIRR               CCW'S TO READ DIRECTORY
         MVC   WORKTBL(8),IOBSEEK              INITIALIZE BOTH CCHHR'S
         MVC   WORKTBL+8(8),WORKTBL            INITIALIZE BOTH CCHHR'S
         LA    R1,WORKTBL+3                    USED CCHHR
         STCM  R1,B'0111',CCW1+1
         LA    R1,CCW1                         TIC TARGET 1
         STCM  R1,B'0111',CCW3+1
         LA    R1,WORKTBL+8+3                  TOTAL CCHHR
         STCM  R1,B'0111',CCW4+1
         LA    R1,CCW4                         TIC TARGET 2
         STCM  R1,B'0111',CCW6+1
         SPACE 2
         MVI   IOECB,0
         EXCP  IOB
         WAIT  ECB=IOECB
         MVI   IOB,X'C2'                       SET IOB FLAGS
         NI    DCBIFLG-IHADCB+INDCB,X'3B'      RESET DCB ERROR FLAG
         SPACE 1
         STM   R2,R12,28(R13)                  CONVERT CCHHR TO TTR
         LA    R2,WORKTBL
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRDL  R0,16                           00TT RXXX
         SRL   R1,24                           00TT 000R
         MH    R0,DIRTRACK+2
         AR    R0,R1
         STH   R0,TOTUSEDX                     USED DIR. BLOCKS
         SPACE 1
         STM   R2,R12,28(R13)                  CONVERT CCHHR TO TTR
         LA    R2,WORKTBL+8
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRDL  R0,16                           00TT RXXX
         SRL   R1,24                           00TT 000R
         MH    R0,DIRTRACK+2
         AR    R0,R1
         BCTR  R0,0
         STH   R0,TOTALLOX                     ALLOCATED DIR. BLOCKS
         SPACE 2
RESTART9 MVC   CCWS(LCCWINIT),CCWINIT          INITIALIZE CCWS
         SPACE 1
         LA    R0,IOBSEEK+3
         LA    R1,CCW1A
         STCM  R0,B'0111',CCW1A+1              ***
         STCM  R1,B'0111',CCW2A+1              *** -- INIT. CCW'S
         STCM  R0,B'0111',CCW3A+1              ***
         SPACE 1
         LA    R1,SNO
         LA    R2,CCW1
         STCM  R1,B'0111',CCW0+1               ***
         STCM  R0,B'0111',CCW1+1               ***
         STCM  R2,B'0111',CCW2+1               ***  -- READ CCW'S
         STCM  R0,B'0111',CCW5+1               ***
         STCM  R1,B'0111',CCW6+1               ***
         SPACE 2
         LA    R1,CCW0                         FIRST CCW (SET SECTOR)
         ST    R1,EXCPCCWI                     SAVE FOR EXCP ROUTINE
         LH    R15,BYTEUCB                     UCBBYTE
         MH    R15,=H'9'                       INDEX INTO UNIT TABLE
         LA    R15,UNITTBL(R15)                DEVICE UNIT TYPE
         NI    FLAGSEE,FF-FFULLTRK-FDOUBCON
         CLI   0(R15),C'M'                     READ MULTIPLE?
         BE    SETBUFF                         YES, BRANCH
         OI    FLAGSEE,FFULLTRK
         CLI   0(R15),C'D'                     DOUBLE?
         BE    SETBUFF                         YES, BRANCH
         OI    FLAGSEE,FDOUBCON                NO, MUST BE SINGLE
         SPACE 1
SETBUFF  CLI   UCBTYP+3(R4),X'08'              2314 DISK?
         BNE   NOT2314                         NO, BRANCH
         ST    R2,EXCPCCWI                     YES, DO NOT READ SECTOR
         XI    CCW5+4,X'40'                    DO NOT CHAIN CCW5
         SPACE 1
NOT2314  MVI   DSORG+1,0                       CLEAR SECOND DSORG BYTE
         TM    DSORG,DS1DSGIS+DS1DSGPS+DS1DSGDA+DS1DSGPO  DSORG KNOWN?
         BM    *+10                                       YES, BRANCH
         MVC   DSORG(2),DS1DSORG                          NO, USE LABEL
         SPACE 1
         LA    R14,FIRST4K
         LA    R14,MSGENTDS-FIRST4K(R14)       POINT TO ENTER OPTIONS
         ST    R14,MSGENTDA                    SAVE FOR LATER
         MVC   0(115,R14),PDS300A              " ENTER OPTIONS -- ..."
         LA    R1,25+7(R14)                    WHERE TO ADD DSNAME
         MVC   0(44,R1),DSNAME                 ADD DSNAME
         AH    R1,DSNLEN                       POINT TO DSNAME END
         MVC   0(9,R1),=C',VOL=SER='           ADD ",VOL=SER="
         MVC   9(6,R1),VOLALLOC                ADD VOLUME SERIAL
         TM    DSORG,DS1DSGPO                  PARTITIONED?
         BNO   *+10                            NO, BRANCH
         MVC   9+6+2(4,R1),=C'MEM='            DEFAULT GROUP NAME
         LA    R1,9+6+2+4(,R1)                 WHERE TO ADD MEM=...
         ST    R1,MSGENTPT                     WHERE TO ADD MEM=...
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 1
         MVC   LABLRECL(2),LRECL               SAVE THE DCB LRECL FIELD
         TM    FLAGSCC,RECFMF                  FIXED FORMAT?
         BNO   NEWSTAX                         NO, BRANCH
         TM    RECFM,DCBRECBR                  RECFM=FB?
         BO    NEWSTAX                         YES, BRANCH
         MVC   LRECL(2),BLKSI                  RECFM=F ONLY
         SPACE 1
NEWSTAX  LA    R0,NEWCMD              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),PDSNAME   RESET PDS COMMAND NAME
         SPACE 1
         STAX  ATTNEXIT,USADDR=(R7),REPLACE=YES,IBUF=0,OBUF=0,         X
               MF=(E,STAXPARM)
         SPACE 1
         ICM   R8,B'1111',##ADRCMD    SUBCOMMAND TO BE CONTINUED?
         BNM   NEWCMD                 NO, BRANCH
         NI    ##ADRCM#,FF-CONTINUE           TURN OFF CALL FLAG
         BALR  R2,R8                  YES, BRANCH
         TITLE 'P D S  --  PDS NEW SUBCOMMAND PROCESSING       6/10/85'
*
*  GET THE NEXT SUBCOMMAND
*
         SPACE 1
NEWCMD   MVI   STARTTR+3,0             CLEAR THE READ ADDRESS
         BAL   R2,SHUTSTOW             CLOSE STOW; RELEASE ANY ENQUEUES
         SPACE 1
         SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT     ANY TEMPORARY STORAGE?
         MVI   SUBPOOLT,0              CLEAR THE SUBPOOL CHARACTER
         BZ    NEWCMD10                NO, LINK TO THE SUBCOMMAND
         FREEMAIN R,SP=(0)             YES, RELEASE IT
         SPACE 1
NEWCMD10 MVI   MTHIGHL,8               LENGTH OF STANDARD INSERT 1
         MVI   MTHIGHL+4,8             LENGTH OF STANDARD INSERT 2
         TM    FLAGSAA,FMEM#MEM        MEMBER GROUP IN PROGRESS?
         BO    MEMSGET                 YES, BRANCH
         SPACE 2
         TM    PMEMMIN,X'80'           MEMBER LIST ACTIVE?
         BNO   NEWCMD20                NO, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   NEWCMD20                NO, MEMBER NAME NOT NEEDED
         TM    ##ADRPC#,@G             MEMBER GROUP ALLOWED?
         BNO   NEWCMD20                NO, BRANCH
         L     R1,PMEMCURR             CURRENT MEMBER LIST ELEMENT
         TM    0(R1),X'80'             ACTIVE ELEMENT?
         BNO   NEWCMD20                NO, BRANCH
         NI    FLAGSAA,FOPTIONS        PRESERVE THE OPTIONS FLAG
         OC    FLAGSAA,4(R1)           RESET CONTROL FLAGS
         MVC   LMEMBER1+1(19),5(R1)    RESET MEMBER NAMES AND LENGTH
         L     R1,0(,R1)               NEXT LIST ELEMENT
         ST    R1,PMEMCURR             SAVE FOR NEXT LIST ITERATION
         MVC   ##SUBCOM(#LSUB),#SUBHOLD  RESET SUBCOMMAND CHANGED DATA
         TM    FLAGSAA,FMEM#MEM        MEMBER GROUP REQUESTED?
         BO    CMDVER                  YES, BRANCH
         SPACE 1
         TM    ##ADRPA#,$W             DELAY MEMBER MESSAGE?
         BNO   NEWCMD14                NO, BRANCH
         MVC   DELAYMSG(38),MSGMEMQ
         MVC   DELAYMSG+7(8),##SUBCOM
         MVC   DELAYMSG+7+8+1(8),MEMBER1
         TR    DELAYMSG+7+8+1(8),TRLINE
         OI    FLAGSAA,FDELAYM         MESSAGE IS AVAILABLE
         B     CMDVER
         SPACE 1
NEWCMD14 TM    ##ADRPA#,$D             SUPPRESS DEFAULT MESSAGE?
         BO    CMDVER                  YES, BRANCH
         MESSAGE MSGBLANK
         MVC   INSERT#1(8),MEMBER1
         TSMSG L002$1                  WHO IS BEING PROCESSED
         B     CMDVER
         SPACE 1
NEWCMD20 MVI   FLAGSAA,0               CLEAR THE MAJOR CONTROL FLAGS
         MVI   FLAGSII,0               CLEAR ALL FLAGSII FLAGS
         MVC   PMEMCURR(4),PMEMMIN     RESET THE CURRENT POINTER
         SPACE 1
         TM    FLAGSBB,FCMD            COMMAND BUFFER AVAILABLE?
         BO    HAVECMD                 YES, BRANCH
         SPACE 1
         TM    FLAGSBB,FONESHOT        ONE SUBCOMMAND ONLY?
         BO    RETURN                  YES, ALL DONE
         SPACE 1
         TM    FLAGSEE,FBKGRND         BACKGROUND MODE?
         BNO   NEWCMD30                NO, BRANCH
         MESSAGE MSGBLANK              YES, OUTPUT A SEPARATOR LINE
         SPACE 8
NEWCMD30 TM    FLAGSEE,FMEMGRP         MEMGRP USED TO DEFINE A NEW GRP?
         BNO   NEWCMD32                NO, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   NEWCMD32                NO, MEMBER NAME NOT NEEDED
         MVC   LMEMBER1(2+8+8+2),MMEMBERD+1  RESET THE MEMBER NAME
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEMRANG-FMEM#MEM
         OC    FLAGSAA(1),MMEMBERD     RESET THE MEMBER FLAG
         XC    MMEMMIN(12),PMEMMIN     INTERCHANGE
         XC    PMEMMIN(12),MMEMMIN                THE
         XC    MMEMMIN(12),PMEMMIN                   MEMBER LISTS
         MVC   MEMBERD(1+2+8+8+2),MMEMBERD    RESET THE DEFAULT NAME
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         XI    FLAGSEE,FMEMGRP         MEMGRP USED NOW
         SPACE 2
NEWCMD32 TM    FLAGSGG,FOUTSOME           ANY OUTPUT FOR SUBCOMMAND?
         BO    NEWCMD38                   YES, BRANCH
         TM    FLAGSGG,FOUTSUB            SUBCOMMAND GAIN CONTROL?
         BNO   NEWCMD38                   NO, BRANCH
         LA    R1,L241
         CLC   $FIN(8),##SUBCOM           FIND SUBCOMMAND?
         BE    NEWCMD36                   YES, BRANCH
*        LA    R1,L241
         CLC   $REP(8),##SUBCOM           REPLACE SUBCOMMAND?
         BE    NEWCMD36                   YES, BRANCH
         LA    R1,L242
         CLC   $MAP(8),##SUBCOM           MAP SUBCOMMAND?
         BE    NEWCMD36                   YES, BRANCH
         LA    R1,L243
         CLC   $HIS(8),##SUBCOM           HISTORY SUBCOMMAND?
         BE    NEWCMD36                   YES, BRANCH
         LA    R1,L244
         CLC   $IFX(8),##SUBCOM           IF SUBCOMMAND?
         BNE   NEWCMD38                   NO, BRANCH
NEWCMD36 TSMSG (R1)
         SPACE 2
NEWCMD38 NI    FLAGSGG,FF-FOUTSOME-FOUTSUB  NO OUTPUT FOR SUBCOMMAND
         MVI   ##SUBCOM,0              RESET THE SUBCOMMAND NAME
         MVI   ATTNECB,2+1             FLAG -- CAN BE ATTENTIONED
         L     R1,MSGENTDA             POINT TO ENTER OPTIONS MESSAGE
        $PUTGET (R1),ATTN=NEWCMD40     FIRST TRY FOR A COMMAND
         B     HAVECMD                 GOT A COMMAND
         SPACE 2
NEWCMD40 MVI   ATTNECB,2+1             FLAG -- CAN BE ATTENTIONED
         L     R1,MSGENTDA             POINT TO ENTER OPTIONS MESSAGE
        $PUTGET (R1),ATTN=EXIT8N       SECOND TRY FOR A COMMAND
         B     HAVECMD                 GOT A COMMAND
         SPACE 2
NEWCMD50 BAL   R2,FREEPDL              FREE THE PARSE PDL
         TITLE 'P D S  --  PDS SUBCOMMAND INVOKE SCAN          6/10/85'
*
*        SET UP FOR SUBCOMMAND SCAN
*
         SPACE 1
HAVECMD  NI    FLAGSBB,FF-FCMD
         TM    FLAGSEE,FMEMGRP         SUBLIST FOR A NEW GROUP?
         BNO   HAVECMD2                NO, BRANCH
         MVC   LMEMBER1(2+8+8+2),MMEMBERD+1  RESET THE MEMBER NAME
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEMRANG-FMEM#MEM
         OC    FLAGSAA(1),MMEMBERD     RESET THE MEMBER FLAG
         XC    MMEMMIN(12),PMEMMIN     INTERCHANGE
         XC    PMEMMIN(12),MMEMMIN                THE
         XC    MMEMMIN(12),PMEMMIN                   MEMBER LISTS
         MVC   MEMBERD(1+2+8+8+2),MMEMBERD    RESET THE DEFAULT NAME
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 1
HAVECMD2 NI    FLAGSEE,FF-FMEMGRP      SUBLIST NOT USED YET
         LA    R15,PARMLIST
         USING CSPL,R15
         SPACE 1
         L     R1,ADDRUPT              ADDRESS OF THE UPT
         L     R2,ADDRECT              ADDRESS OF THE ECT
         LA    R3,ATTNECB              ADDRESS OF THE ATTENTION ECB
         LA    R4,ZERO                 ADDRESS OF A ZERO FLAG AREA
         LA    R5,SCANANSR             ADDRESS OF THE RESULT AREA
         L     R6,ADDRCBUF             ADDRESS OF THE SUBCOMMAND
         STM   R1,R6,CSPLUPT           SETUP CSPLUPT, CSPLECT, CSPLECB,
         DROP  R15                           CSPLFLG, CSPLOA & CSPLCBUF
         SPACE 1
         LA    R14,FIRST4K
         MVC   MSGTEXT1(256),RECALLST-FIRST4K(R14) SAVE SUBCOMMAND
         CLC   0(2,R6),=H'4'                    NULL INPUT?
         BE    HAVECMD8                         YES, IGNORE SUBCOMMAND
         MVC   RECALLST-FIRST4K(256,R14),0(R6)  NO, RESET SUBCOMMAND
         SPACE 2
         CLI   4(R6),C'?'              QUESTION ?
         BE    HAVECMD3                YES, BRANCH
         CLI   4(R6),C'.'              PERIOD (FOR EXTENDED HELP)?
         BNE   HAVECMD6                NO, BRANCH
         CLI   5(R6),C'?'              QUESTION (FOR EXTENDED HELP)?
         BNE   HAVECMD6                NO, BRANCH
         SPACE 1
HAVECMD3 LA    R1,L520                 ** EXTENDED HELP IS REQUESTED
         MVC   RECALLST-FIRST4K(256,R14),MSGTEXT1  RESET SUBCOMMAND
         OC    ERRORMSG(3*5),ERRORMSG  ANY ERRORS NOTED?
         BZ    MSGNEW                  NO, "NO INFORMATION AVAILABLE"
         MVC   0(12,R6),MSGHELP        'H MS MS(' AND 4 BYTE LENGTH
         LA    R2,ERRORMSG-3           START OF ERROR MESSAGES-3
         LA    R0,3*5/3                MAXIMUM NUMBER OF MESSAGES
         LH    R4,0(,R6)               CURRENT LENGTH
         SPACE 1
HAVECMD4 LA    R3,0(R4,R6)             POINT TO CURRENT BYTE
         LA    R2,3(,R2)               NEXT MESSAGE
         MVC   0(3,R3),PDS300A+4       'PDSNNN,' (AFTER FORMATTING)
         MVC   3(3,R3),0(R2)           MESSAGE IDENTIFICATION
         MVI   6(R3),C','              SEPARATING COMMA
         LA    R4,7(,R4)               ADD TO LENGTH
         STH   R4,0(,R6)               UPDATE LENGTH
         CLI   3(R2),X'00'             ANOTHER MESSAGE?
         BE    *+8                     NO, BRANCH
         BCT   R0,HAVECMD4             REPEAT FOR ALL MESSAGES
         MVI   6(R3),C')'              FINAL MATCHING PARENTHESIS
**       MESSAGE (R6)                  DELETE ** FOR TESTING
         SPACE 2
HAVECMD6 XC    ERRORMSG(3*5),ERRORMSG  CLEAR PREVIOUS ERROR MESSAGES
         SPACE 1
HAVECMD8 MVI   ATTNECB,0               CLEAR ECB
         LA    R1,PARMLIST             PARAMETER LIST START
         L     R15,ADDRSCAN            IKJSCAN ADDRESS
         BALR  R14,R15                 INVOKE COMMAND SCAN
         SPACE 1
         LA    R15,SCANANSR            ADDR OF RESULTS
         USING CSOA,R15
         SPACE 1
         TM    CSOAFLG,CSOANOC         EMPTY BUFFER?
         BO    NEWCMD30                YES, STILL NEED COMMAND
         SPACE 1
         TM    CSOAFLG,CSOAEXEC        IMPLICIT EXEC?
         BO    EXECPER                 YES, BRANCH
         SPACE 2
         TM    CSOAFLG,CSOAVWP+CSOAVNP VALID COMMAND?
         BNZ   HAVECMD9                YES, BRANCH
         LA    R1,L877                 INVALID COMMAND MESSAGE
         CLI   4(R6),X'6E'             RESHOW COMMAND?
         BNE   MSGNEW                  NO, ERROR MESSAGE
         MVI   ATTNECB,2+1             FLAG -- CAN BE ATTENTIONED
        $PUTGET MSGBLANK,ATTN=NEWCMD40 FIRST TRY FOR A COMMAND
         B     HAVECMD                 GOT A COMMAND
         SPACE 2
HAVECMD9 TM    CSOAFLG,CSOAVWP         ANY OPERAND?
         BNO   *+8                     NO, BRANCH
         OI    FLAGSAA,FOPTIONS        YES, NOTE FOR LATER
         TITLE 'P D S  --  PDS SUBCOMMAND TABLE SCAN           6/10/85'
*
*   COMMAND SCAN - IN THE CASE OF A POTENTIALLY AMBIGUOUS ENTRY
*                  (E.G. 'A ') THE FIRST MATCH IN THE COMMAND TABLE
*                  (ATTR IN THIS CASE) IS USED.
*
         L     R2,CSOACNM              ADDR OF COMMAND NAME
         LH    R3,CSOALNM              LENGTH OF NAME
         BCTR  R3,0                    MACHINE LENGTH
         SPACE 2
         LA    R1,$TBL                 START OF COMMAND SCAN
         SPACE 2
         CLC   0(*-*,R1),0(R2)         <<EXECUTED>>
CMDSCAN  EX    R3,*-6                  THIS SUBCOMMAND?
         BNE   CMDSCAN2                NO, BRANCH
         SPACE 1
         MVC   ##SUBCOM(PTW),0(R1)     INITIALIZE ##SUBCOM, ##ADRCMD,
*                                        ##ADRPCL AND ##ADRPAR
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BO    CMDFND                  YES, BRANCH
         TM    ##ADRPC#,@S             SUPPORTED IF NOT PARTITIONED?
         BO    CMDFND2                 YES, BRANCH
         SPACE 1
CMDSCAN2 LA    R1,PTW(R1)              NEXT ENTRY IN TABLE
         CLI   0(R1),X'FF'             END OF TABLE?
         BNE   CMDSCAN                 NO, CONTINUE
         SPACE 1
         SR    R2,R2
         L     R8,$OPT+8               OPTIONS SUBCOMMAND START
         BR    R8
         SPACE 1
CMDSCAN4 LA    R15,SCANANSR            INVALID SUBCOMMAND MESSAGE
         LH    R3,CSOALNM              LENGTH OF NAME
         BCTR  R3,0                    MACHINE LENGTH
         SR    R2,R2
         L     R8,$OPT+8               OPTIONS SUBCOMMAND START
         BR    R8
         DROP  R15
         TITLE 'P D S  --  PDS SUBCOMMAND INITIAL PROCESSING   6/10/85'
CMDFND   NI    ##ADRPC#,FF-@S          PARTITIONED, TURN OFF OTHER FLAG
         SPACE 2
CMDFND2  MVC   ##ANSWER(LISUBS),ISUBS  INITIALIZE THE PDL SAVE AREA
         L     R14,ADDRECT
         MVC   ECTSCMD-ECT(8,R14),##SUBCOM  RESET PDS SUBCOMMAND NAME
         NI    ECTSWS2-ECT(R14),FF-X'04'    PROMPT HELP IS ENABLED
         CLC   0(8,R1),$REC            RECALL SUBCOMMAND?
         BNE   CMDCHECK                NO, BRANCH
         LA    R14,FIRST4K
         MVC   RECALLST-FIRST4K(256,R14),MSGTEXT1  YES, USE PREVIOUS
         LH    R1,RECALLST-FIRST4K(R14)  LENGTH OF OLD SUBCOMMAND
         BCTR  R1,0                    MACHINE LENGTH
         XC    MSGTEXT1(256),MSGTEXT1  CLEAR THE MESSAGE AREA
         MVC   MSGTEXT1(*-*),RECALLST-FIRST4K(R14) <<EXECUTED>>
         EX    R1,*-6                  MOVE IN MESSAGE
         CLI   MSGTEXT1+1,79+4         A FULL LINE (80 -1)?
         BH    *+8                     YES, BRANCH
         MVI   MSGTEXT1+1,79+4         NO, CHANGE LENGTH TO 80
         LA    R1,MSGTEXT1+1(R1)       POINT TO POSITION AFTER TEXT
         MVI   0(R1),X'40'             ADD ONE TRAILING BLANK
         LH    R1,2(,R6)               OFFSET TO OPERANDS
         LA    R1,4(R1,R6)             POINT TO OPERANDS
         TM    FLAGSAA,FOPTIONS        ANY OPERANDS?
         BNO   CMDFND4                 NO, BRANCH
         OI    0(R1),X'40'             UPPER CASE THE OPERAND
         CLI   0(R1),C'C'              CHANGE OPERAND?
         BNE   CMDFND4                 NO, BRANCH
         MVC   MSGTEXT1(132),RECALLDS-FIRST4K(R14)  MOVE IN DSNAME
         SPACE 2
CMDFND4  MESSAGE MSGTEXT1              DISPLAY THE OLD SUBCOMMAND
         LH    R1,2(,R6)               OFFSET TO OPERANDS
         LA    R1,4(R1,R6)             POINT TO OPERANDS
         TM    FLAGSAA,FOPTIONS        ANY OPERANDS?
         BNO   NEWCMD                  NO, BRANCH
         OI    0(R1),X'40'             UPPER CASE THE OPERAND
         CLI   0(R1),C'E'              ENTER OPERAND?
         BNE   NEWCMD                  NO, BRANCH
         NI    FLAGSAA,FF-FOPTIONS     TURN OFF THE OPERAND FLAG
         LA    R14,FIRST4K
         MVC   PUTGETBF(256),RECALLST-FIRST4K(R14)  CHANGE TO PREVIOUS
         B     HAVECMD                 SCAN AND PARSE THIS AGAIN
         SPACE 2
CMDCHECK TM    ##ADRPC#,@N       IGNORE OPTIONS?
         BO    CMDVER            YES, BRANCH
         SPACE 2
         CLC   ##SUBCOM(8),$PAT  PATTERN SUBCOMMAND?
         BNE   CMDCHK2           NO, BRANCH
         TM    FLAGSAA,FOPTIONS  ANY OPERAND SPECIFIED?
         BO    CMDPARSE          YES, USE THE SPECIFIED DATA
         CLI   MEMBERP,X'00'     ANY DEFAULT PATTERN YET?
         BE    CMDPARSE          NO, FORCE A CALL TO PARSE
         SPACE 1
         MVC   LMEMBER1(2+8+8+2),MEMBERP+1  RESET DEFAULT PATTERN
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERP   RESTORE THE DEFAULT FLAGS
         B     CMDVER
         SPACE 2
CMDCHK2  TM    ##ADRPC#,@G       MEMBER NAME DEFAULT (AND GROUP)?
         BNO   CMDREQD           NO, CHECK IF OPERANDS REQUIRED
         TM    FLAGSAA,FOPTIONS  ANY OPERAND SPECIFIED?
         BO    CMDPARSE          YES, USE THE SPECIFIED DATA
         SPACE 2
         TM    ##ADRPC#,@S       NON-PARTITIONED DATA SET?
         BO    CMDVER            YES, BRANCH
         CLI   MEMBERD,X'00'     ANY DEFAULT MEMBER NAME YET?
         BE    CMDPARSE          NO, FORCE A CALL TO PARSE
         SPACE 1
         MVC   LMEMBER1(2+8+8+2),MEMBERD+1  RESET DEFAULT MEMBER NAMES
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERD   RESTORE THE DEFAULT FLAGS
         MVC   PMEMCURR(4),PMEMMIN  RESET THE FIRST MEMBER POINTER
         TM    FLAGSAA,FMEM#MEM  MEMBER GROUP REQUESTED?
         BO    CMDVER            YES, BRANCH
         SPACE 1
         TM    ##ADRPA#,$W       DELAY MEMBER MESSAGE?
         BO    CMDPARS2          YES, BRANCH
         SPACE 1
         TM    ##ADRPA#,$D       SUPPRESS DEFAULT MESSAGE?
         BO    CMDVER            YES, BRANCH
         SPACE 1
         TM    PMEMMIN,X'80'     ACTIVE MEMBER LIST?
         BNO   CMDCHK6           NO, BRANCH
         SPACE 1
         CLC   ##SUBCOM(8),$MEM  MEMBERS SUBCOMMAND?
         BE    CMDVER            YES, BRANCH
         SPACE 1
         CLC   ##SUBCOM(8),$DEL  DELETE SUBCOMMAND?
         BE    CMDVER            YES, BRANCH
         SPACE 1
CMDCHK6  MVC   INSERT#1(8),MEMBER1
         TSMSG L002$1            TELL THE USER WHO IS BEING PROCESSED
         B     CMDVER            CALL THE COMMAND
         SPACE 3
CMDREQD  TM    ##ADRPC#,@R       OPERANDS REQUIRED?
         BO    CMDPARSE          YES, BRANCH
         TM    FLAGSAA,FOPTIONS  ANY OPERANDS SPECIFIED?
         BZ    CMDVER            NO, BRANCH
         SPACE 2
CMDPARSE OI    FLAGSAA,FOPTIONS  NOW THERE ARE OPERANDS
         L     R1,ADDRCBUF       START OF SUBCOMMAND BUFFER
         MVC   ##HELOFF(2),2(R1) SAVE THE OFFSET
         L     R15,=A(PARSE)     PARSE ADDRESS
         BALR  R14,R15           GET THE COMMAND OPERANDS
         B     NEWCMD            INVALID PARSE, IGNORE
         L     R1,ADDRCBUF       START OF SUBCOMMAND BUFFER
         MVC   2(2,R1),##HELOFF  RESET THE OFFSET
         SPACE 1
         TM    ##ADRPA#,$W       DELAY MEMBER MESSAGE?
         BNO   CMDVER            NO, BRANCH
CMDPARS2 MVC   DELAYMSG(38),MSGMEMQ
         MVC   DELAYMSG+7(8),##SUBCOM
         MVC   DELAYMSG+7+8+1(8),MEMBER1
         TR    DELAYMSG+7+8+1(8),TRLINE
         OI    FLAGSAA,FDELAYM         MESSAGE IS AVAILABLE
         B     CMDVER
         SPACE 3
CMDVER   ICM   R15,B'1111',=V(VSUBCMD)  VERIFY THE SUBCOMMAND?
         BZ    CALLCMD                  NO, BRANCH
         OI    FLAGSJJ,FSUBCMD          YES, SET A ESTAE FLAG
         L     R1,ADDRCBUF              COMMAND BUFFER ADDRESS
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         MVC   MSGTEXT1(16),ADDRTEXT    MOVE ARGUMENT LIST
         LA    R1,##SUBCOM              COMMAND NAME
         CLC   ##SUBCOM(8),$ATT         ATTRIB SUBCOMMAND?
         BNE   CMDVER2                  NO, BRANCH
         TM    #OPTOPT,X'F0'            ATTRIBUTE CHANGES?
         BNO   CMDVER2                  NO, BRANCH
         LA    R1,=CL8'ATTRMOD'         YES, CHANGE THE COMMAND NAME
CMDVER2  CLC   ##SUBCOM(8),$VER         VERIFY SUBCOMMAND?
         BNE   CMDVER4                  NO, BRANCH
         TM    #VERUPDT,2               NOUPDATE?
         BO    CMDVER4                  YES, BRANCH
         TM    DSORG,DS1DSGPO           PARTITIONED DATA SET?
         BNO   CMDVER4                  NO, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?
         BNZ   CMDVER4                    NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP?
         BO    CMDVER4                    YES, BRANCH
         LA    R1,=CL8'VERMOD'            NO, CHANGE THE COMMAND NAME
         SPACE 1
CMDVER4  ST    R1,MSGTEXT1+16           FOR VERIFICATION ROUTINE
         LA    R1,DSNAME                DSNAME
         ST    R1,MSGTEXT1+20           FOR VERIFICATION ROUTINE
         OI    MSGTEXT1+20,X'80'        MARK END OF LIST
         LA    R1,MSGTEXT1              START OF ARGUMENT LIST
         BALR  R14,R15                  CALL VERIFICATION ROUTINE
         NI    FLAGSJJ,FF-FSUBCMD       RESET THE ESTAE FLAG
         MVC   INSERT#1(8),##SUBCOM     COMMAND NAME
         LA    R1,L920$1                ASSUME NOT AUTHORIZED
         LTR   R15,R15                  CORRECT?
         BNZ   MSGNEWXX                 YES, BRANCH
         SPACE 2
         CLC   ##SUBCOM(8),$FIN         FIND SUBCOMMAND?
         BE    CMDVER6                  YES, BRANCH
         CLC   ##SUBCOM(8),$IFX         IF SUBCOMMAND?
         BNE   CALLCMD                  NO, BRANCH
         SPACE 1
CMDVER6  L     R15,=V(VSUBCMD)          VERIFY THE SUBCOMMAND
         ICM   R1,B'1111',#ACTIONT      THEN ACTION?
         BZ    CMDVER8                  NO, BRANCH
         OI    FLAGSJJ,FSUBCMD          YES, SET A ESTAE FLAG
         ST    R1,MSGTEXT1+16           FOR VERIFICATION ROUTINE
         LA    R1,MSGTEXT1              START OF ARGUMENT LIST
         BALR  R14,R15                  CALL VERIFICATION ROUTINE
         NI    FLAGSJJ,FF-FSUBCMD       RESET THE ESTAE FLAG
         ICM   R1,B'1111',#ACTIONT      THEN ACTION
         MVC   INSERT#1(8),0(R1)        COMMAND NAME
         LA    R1,L920$1                ASSUME NOT AUTHORIZED
         LTR   R15,R15                  CORRECT?
         BNZ   MSGNEWXX                 YES, BRANCH
         SPACE 2
CMDVER8  L     R15,=V(VSUBCMD)          VERIFY THE SUBCOMMAND
         ICM   R1,B'1111',#ACTIONE      ELSE ACTION?
         BZ    CALLCMD                  NO, BRANCH
         OI    FLAGSJJ,FSUBCMD          YES, SET A ESTAE FLAG
         ST    R1,MSGTEXT1+16           FOR VERIFICATION ROUTINE
         LA    R1,MSGTEXT1              START OF ARGUMENT LIST
         BALR  R14,R15                  CALL VERIFICATION ROUTINE
         NI    FLAGSJJ,FF-FSUBCMD       RESET THE ESTAE FLAG
         ICM   R1,B'1111',#ACTIONE      ELSE ACTION
         MVC   INSERT#1(8),0(R1)        COMMAND NAME
         LA    R1,L920$1                ASSUME NOT AUTHORIZED
         LTR   R15,R15                  CORRECT?
         BNZ   MSGNEWXX                 YES, BRANCH
         TITLE 'P D S  --  PDS SUBCOMMAND BLDL CHECKING        6/10/85'
CALLCMD  MVC   #SUBHOLD(#LSUB),##SUBCOM   SAVE SUBCOMAND CHANGED DATA
         TM    ##ADRPC#,@S         NON-PARTITIONED DATA SET?
         BO    CALLCMD9            YES, BRANCH
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP DESIRED?
         BO    CALLCMD8            YES, BRANCH
         TM    ##ADRPC#,@C         CHANGE DIRECTORY AND NO BLDL?
         BNO   CALLCMD2            NO, BRANCH
         MVC   DIRNAME,MEMBER1     GET MEMBER NAME
         NI    FLAGSCC,FF-FBLDLOK  DIRECTORY WILL BE CHANGED
         SPACE 2
CALLCMD2 TM    ##ADRPC#,@B         BLDL REQUIRED?
         BNO   CALLCMD8            NO, BRANCH
***      TM    FLAGSCC,FBLDLOK     BLDL STILL VALID?
***      BNO   CALLCMD4            NO, BRANCH
***      TM    FLAGSAA,FOPTIONS    ANY MEMBER NAME SPECIFIED?
***      BO    CALLCMD4            YES, DO A BLDL THEN
***      CLC   DIRNAME(8),MEMBER1  STILL THIS MEMBER NAME?
***      BE    CALLCMD8            YES, NO NEED TO BLDL
         SPACE 2
CALLCMD4 XC    DIRUSER,DIRUSER     CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1     ORIGINAL MEMBER NAME
         NI    FLAGSCC,FF-FBLDLOK  ASSUME MEMBER NOT FOUND
         SPACE 2
         BLDL  INDCB,BLDLLIST      LOCATE DIRECTORY ENTRY
         B     *+4(R15)            PROCESS RETURN CODE
         B     CALLCMD6              00 - SUCCESSFUL
         B     NOMEMBER              04 - MEMBER NOT FOUND
         B     IOERROR               08 - I/O ERROR
         SPACE 2
CALLCMD6 OI    FLAGSCC,FBLDLOK     MEMBER FOUND
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         LA    R14,DIRNAME                CURRENT MEMBER NAME ADDRESS
         ST    R14,DIRPTRS                UPDATE IF NO GROUP
         SPACE 2
CALLCMD8 TM    FLAGSAA,FMEM#MEM           MEMBER GROUP TO START?
         BNO   CALLCMDZ                   NO, BRANCH
         NI    FLAGSCC,FF-FBLDLOK         YES, FORCE A BLDL NEXT TIME
         SPACE 1
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ANY MEMBER NAMES?
         BNZ   MEMSINIT                   YES, BRANCH
         TM    ##ADRPC#,@I                BROWSE, EDIT OR VERIFY?
         BNO   MEMSINIT                   NO, BRANCH
         SPACE 2
CALLCMD9 NI    FLAGSAA,FF-FMEM#MEM        TURN OFF MEMBER GROUPS
         B     CALLCMDZ                   CALL THE SUBCOMMAND
         TITLE 'P D S  --  PDS SUBCOMMAND MEMBER GROUPS        6/10/85'
MEMSINIT MVC   #SUBHOLD(#LSUB),##SUBCOM SAVE SUBCOMMAND CHANGED DATA
         XC    DIRNAME(8),DIRNAME       CLEAR THE MEMBER NAME
         OI    FLAGSII,FDIRGRP          INPUT FOR MEMBER GROUP
         TM    FLAGSAA,FMEMRANG         MEMBER RANGE?
         BO    DISPLAY                  YES, BRANCH
         B     PATTERN                  NO, MEMBER PATTERN
         SPACE 1
MEMSGET  MVC   DIRPTRS(12),DIRPTRS2     RESTORE THE DIRPTRS
         MVC   CURMBB(8),CURDIR         RESTORE THE DIRECTORY ADDRESS
         OI    FLAGSII,FDIRGRP          INPUT FOR MEMBER GROUP
         B     DISPLAY2                 GET NEXT MEMBER
         SPACE 1
MEMSNEXT NI    FLAGSII,FF-FDIRGRP       MEMBER GROUP INPUT IS COMPLETE
         OI    FLAGSBB,FRANPAT          MEMBERS EXIST IN THIS RANGE
         MVC   DIRPTRS2(12),DIRPTRS     SAVE THE DIRPTRS
         MVC   CURDIR(8),CURMBB         SAVE THE DIRECTORY ADDRESS
         MVC   ##SUBCOM(#LSUB),#SUBHOLD RESET CHANGED INFORMATION
         L     R14,DIRPTRS              START OF CURRENT MEMBER NAME
         CLC   DIRNAME(8),0(R14)        NAME PROCESSED BEFORE?
         BNL   MEMSGET                  YES, BRANCH AND IGNORE
         XC    DIRNAME(74),DIRNAME      CLEAR THE CURRENT ENTRY
         IC    R15,MEMFLAG              DIRECTORY FLAG BYTE
         N     R15,=XL4'0000001F'       NUMBER OF HALFWORDS
         LA    R15,11(R15,R15)          MACHINE LENGTH OF ENTRY
         MVC   DIRNAME(*-*),0(R14)      <<EXECUTED>>
         EX    R15,*-6                  MOVE IN THE CURRENT ENTRY
         SPACE 1
         MVC   MSGTEXT1,MSGMEMQ         SUBCOMMAND AND MEMBER MESSAGE
         MVC   MSGTEXT1+7(8),##SUBCOM
         MVC   MSGTEXT1+7+8+1(8),DIRNAME
         TR    MSGTEXT1+7+8+1(8),TRLINE
         TM    ##ADRPC#,@W              HEADER DELAYED SUBCOMMAND?
         BNO   MEMSMSG                  NO, BRANCH
         SPACE 1
MEMSKIP  MVC   DELAYMSG(38),MSGTEXT1    SAVE FOR THEN/ELSE ACTION
         OI    FLAGSAA,FDELAYM          SUBCOMMAND IS AVAILABLE
         B     CALLCMDZ                 CALL THE SUBCOMMAND
         SPACE 2
MEMSMSG  MESSAGE MSGBLANK
         MESSAGE MSGTEXT1
         SPACE 3
CALLCMDZ L     R8,##ADRCMD              SUBCOMMAND ROUTINE START
         LA    R2,NEWCMD                SUBCOMMAND TERMINATION ADDRESS
         BR    R8                       LINK TO THE SUBCOMMAND
         TITLE 'P D S  --  PDS DISPLAY, PATTERN                6/10/85'
***********************************************************************
***      DISPLAY SUBCOMMAND                                         ***
***                                                                 ***
***      PATTERN SUBCOMMAND    ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
DISPLAY  OI    FLAGSAA,FMEMRANG          **  MEMBER RANGE SELECTION
         TM    FLAGSAA,FMEMBER1+FMEMBER2 DISPLAY RANGE?
         BNO   DISPLAYA                  NO, NO RANGE CHECK
         LH    R14,LMEMBER1
         CH    R14,LMEMBER2              DETERMINE MIN NAME LENGTH
         BNH   *+8
         LH    R14,LMEMBER2
         CLC   MEMBER1(*-*),MEMBER2      <<EXECUTED>>
         EX    R14,*-6                   VALID DISPLAY RANGE?
         LA    R1,L700                   INVALID RANGE MESSAGE
         BH    MSGNEWXX                  YES, BRANCH
         SPACE 1
PATTERN  DS    0H                        **  MEMBER PATTERN SELECTION
         SPACE 1
DISPLAYA LA    R1,80                     ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON            ISPMODE ACTIVE?
         BO    DISPLAY0                  YES, BRANCH
         TM    CONTOPTN,1                ANY LOG RECORDING?
         BO    DISPLAY0                  YES, BRANCH
         GTSIZE ,                        TERMINAL SIZE
         CH    R1,=H'120'                120 OR LESS BYTES?
         BL    *+8                       YES, BRANCH
         LH    R1,=H'120'                NO, USE 120 BYTES
DISPLAY0 ST    R1,LINESIZE               CHARACTERS/LINE
         NI    FLAGSBB,FF-FEXIST-FLINESET-FRANPAT
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 1
DISPLAY1 L     R4,LINESIZE               GET TERMINAL LINE SIZE
         LA    R4,MSGTEXT1+3-11(R4)      END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4             START OF LINE
         MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         SPACE 1
DISPLAY2 BAL   R14,READDIR               GET NEXT DIRECTORY ENTRY
         B     DISPLAY6                  LAST MEMBER IN DIRECTORY
         SPACE 1
         OI    FLAGSBB,FEXIST            MEMBER EXISTS FLAG
         TM    FLAGSAA,FMEMRANG          MEMBER NAME RANGE?
         BNO   PATTERN1                  NO, BRANCH
         TM    FLAGSAA,FMEMBER1          START ENTRY SPECIFIED?
         BZ    DISPLAY3                  NO, BRANCH
         LH    R15,LMEMBER1              LENGTH-1 OF MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6
         BH    DISPLAY2                  NOT WANTED, GET NEXT
         XI    FLAGSAA,FMEMBER1
         SPACE 2
         AIF   ('&CONDRNG' EQ 'N').NRNG
         TM    FLAGSAA,FMEMBER2          LAST ENTRY SPECIFIED?
         BO    DISPLAY3                  YES, BRANCH
         CLC   $DIS(8),##SUBCOM          DISPLAY SUBCOMMAND? ***TEST
         BNE   DISPLAY3                  NO, BRANCH          ***TEST
         STH   R15,LMEMBER2              LENGTH-1 OF SECOND NAME
         MVC   MEMBER2(*-*),MEMBER1      <<EXECUTED>>
         EX    R15,*-6                   DUPLICATE FIRST MEMBER NAME
         OI    FLAGSAA,FMEMBER2          SET SECOND ENTRY HERE
         SPACE 2
.NRNG    ANOP
DISPLAY3 TM    FLAGSAA,FMEMBER2          LAST ENTRY SPECIFIED?
         BZ    DISPLAY4                  NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2
         CLC   MEMBER2(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6                   PAST END?
         BL    DISPLAY6                  YES, END OF DISPLAY
         B     DISPLAY4                  NO, DISPLAY THIS MEMBER NAME
         SPACE 1
PATTERN1 LH    R15,LMEMBER1              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER1        <<EXECUTED>>
PATTERN2 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    PATTERN3                  YES, CHECK FOR ANOTHER PATTERN
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN2               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
PATTERN3 TM    FLAGSAA,FMEMBER2          A SECOND PATTERN SPECIFIED?
         BZ    DISPLAY4                  NO, DISPLAY THE MEMBER NAME
         LH    R15,LMEMBER2              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER2        <<EXECUTED>>
PATTERN4 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    DISPLAY4                  YES, DISPLAY THE MEMBER NAME
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN4               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
DISPLAYT TRT   MEMNAME(*-*),TRTMEM       <<EXECUTED>>
DISPLAY4 TM    FLAGSAA,FMEM#MEM          MEMBER NAME GROUP DESIRED?
         BO    MEMSNEXT                  YES, FOUND A MEMBER
         LA    R1,7                      MEMBER NAME MACHINE LENGTH
         LA    R14,MEMNAME+8             END OF MEMBER NAME +1
DISPLAYB BCTR  R14,0                     SCAN
         CLI   0(R14),X'40'                  BACKWARDS
         BNE   *+8                                    FOR A
         BCT   R1,DISPLAYB                                 NON-BLANK
         CLI   MEMNAME,C'0'              FIRST DIGIT NUMERIC?
         BNL   *+12                      YES, FORCE A HEX DISPLAY
         EX    R1,DISPLAYT               ANY INVALID OR UNPRINTABLE?
         BZ    DISPLAY8                  NO, BRANCH
         TM    FLAGSBB,FLINESET          LINE IN PROGRESS?
         BZ    DISPLAY9                  NO, SKIP THE PUTLINE
         MESSAGE MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         SPACE 2
DISPLAY9 MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         UNPK  MSGTEXT1+4(9),MEMNAME(5)  FIRST HALF OF NAME
         UNPK  MSGTEXT1+12(9),MEMNAME+4(5) SECOND HALF OF NAME
         TR    MSGTEXT1+4(16),TRTABLE    FINISH TRANSLATION
         MVC   MSGTEXT1+20(8),BLANKS     CLEAN UP GARBAGE
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    *+10                      NO, SKIP -A
         MVC   MSGTEXT1+20(2),=C'-A'     YES, SET -A
         MVI   MSGTEXT1+24,C'*'
         MVC   MSGTEXT1+25(8),MEMNAME    MEMBER NAME FROM DIRECTORY
         TR    MSGTEXT1+25(8),TRLINE     TRANSLATE TO PRINTABLE
         MVI   MSGTEXT1+33,C'*'
         OI    FLAGSBB,FLINESET+FRANPAT  OUTPUT AND MEMBERS EXIST NOW
         B     DISPLAY5
         SPACE 1
DISPLAY8 MVC   0(8,R5),MEMNAME
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    DISPLAY7                  NO, BRANCH
         MVC   8(2,R5),=C'-A'
DISPLAY7 OI    FLAGSBB,FLINESET+FRANPAT
         LA    R5,10+2(R5)
         CR    R5,R4                     LINE FULL?
         BL    DISPLAY2                  NO, CONTINUE
         SPACE 1
DISPLAY5 MESSAGE MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         B     DISPLAY1                  RETURN
         SPACE 2
DISPLAY6 TM    FLAGSBB,FLINESET          OUTPUT LINE IN PROGRESS?
         BNO   DISPLAYX                  NO, BRANCH
         LA    R1,MSGTEXT1
         TM    FLAGSAA,FMEM#MEM          MEMBER GROUP IN PROGRESS?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DISPLAYX TM    FLAGSBB,FRANPAT           MEMBER IN RANGE?
         BO    DISPLAYZ                  YES, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE ANY MEMBER GROUP
         TM    FLAGSBB,FEXIST            NO, BUT ANY IN DIRECTORY?
         LA    R1,L400
         BZ    MSGNEW                    NO, ** EMPTY DIRECTORY **
         LA    R1,L401                   ASSUME NONE IN RANGE
         TM    FLAGSAA,FMEMRANG MEMBER   CORRECT?
         BO    MSGNEW                    YES, BRANCH
         LA    R1,L402                   NO, NONE MATCHING PATTERN
         B     MSGNEW
         SPACE 2
DISPLAYZ TM    FLAGSAA,FMEM#MEM          MEMBER GROUP?
         BNO   NEWCMD                    NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE THE MEMBER GROUP
         B     NEWCMD
         TITLE 'P D S  --  PDS EXEC                            6/10/85'
***********************************************************************
***      EXEC SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
EXECPER  L     R2,ADDRCBUF          EXEC (%NAME -- IMPLIED FORM)
         XC    2(2,R2),2(R2)        CLEAR OFFSET
         L     R1,ADDRECT                  ECT ADDRESS
         OI    ECTSWS-ECT(R1),ECTNOPD      ASSUME NO OPERAND
         TM    CSOAFLG-CSOA(R15),CSOAVWP   ANY OPERAND?
         BNO   *+8                         NO, BRANCH
         NI    ECTSWS-ECT(R1),FF-ECTNOPD   YES, ZAP THE NO OPERAND FLAG
         SPACE 1
EXEC     L     R2,ADDRCBUF          EXEC (EXEC NAME -- EXPLICIT FORM)
         LA    R1,L732              ASSUME ISPMODE IS ACTIVE
         TM    SPFLAG0,SPFDON       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         L     R1,ADDRCPPL
         ST    R2,0(,R1)
         SPACE 1
         LA    R3,$EXE+20           TSO EXEC CLIST PROCESSOR
         BAL   R2,ATTACH            GO ATTACH IT
         B     NEWSTAX
         TITLE 'P D S  --  PDS ALIAS                           6/10/85'
***********************************************************************
***      ALIAS SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
ALIAS    CSECT
         USING *,R8
         TM    SPFLAG2,SPFPOST            LINE COMMAND?
         BNO   ALIAS0                     NO, BRANCH
         LA    R1,FIRST4K                 GET ADDRESSABILITY
         MVC   MEMLNMSV-FIRST4K(8,R1),MEMBER2
         B     ALIAS1                     NO, BRANCH
         SPACE 2
ALIAS0   MVC   MEMBERD+1(2),LMEMBER2      CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2     CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1           ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'           NO MEMBER LIST NOW
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
ALIAS1   MVC   DIRNAME,MEMBER2            SET ALIAS NAME
         MVI   ENTRYPT,C'?'               ENTRY POINT NOT KNOWN YET
         TM    DIRFLAG,X'80'              MODULE A CURRENT ALIAS?
         BZ    ALIAS2                     NO, BRANCH
         TM    FLAGSCC,RECFMU             LOAD MODULE?
         BO    ALIAS3                     YES, BRANCH
         B     ALIAS4                     NO, BRANCH
         SPACE 1
ALIAS2   TM    FLAGSCC,RECFMU             LOAD MODULE?
         BZ    ALIAS4                     NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG VS LKED & APF DATA PRESENT?
         BZ    ALIAS2Y                    NO, BRANCH
         SPACE 1
         LA    R2,DIRAPF                  POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BZ    *+8                        NO, BRANCH
         LA    R2,DIRAPF3                 YES, POINT TO APF DATA
         LA    R4,11(,R2)                 POINT TO ALIAS APF DATA
         TM    DIRATTR2,DIR2SSI           SSI PRESENT?
         BZ    ALIAS2B                    NO, BRANCH
         SPACE 1
         LA    R2,1(,R2)                  ROUND TO HALFWORD
         N     R2,=F'-2'                  FFFFFFFE MASK
         LA    R4,1(,R4)                  ROUND TO HALFWORD
         N     R4,=F'-2'                  FFFFFFFE MASK
         MVC   0(4,R4),0(R2)              MOVE SSI INFORMATION
         LA    R4,4(,R4)                  MOVE PAST SSI INFORMATION
         LA    R2,4(,R2)                  POINT TO APF DATA
         SPACE 1
ALIAS2B  CLI   0(R2),1                    IS APF LENGTH OK?
         BE    ALIAS2D                    YES, BRANCH
         TSMSG L710                       BAD APF INFORMATION FORMAT
         LA    R2,ZERO                    SET APF=0
         SPACE 1
ALIAS2D  MVI   0(R4),1                    SET PROPER LENGTH
         MVC   1(1,R4),1(R2)              MOVE IN APF DATA
         SPACE 3
ALIAS2Y  LA    R4,DIREP                   POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND2-DIRUSER+1)/2   NUMBER OF HALFWORDS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   ALIAS2Z                    NO, BRANCH
         LA    R4,DIREPSC                 POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND3-DIRUSER+1)/2   NUMBER OF HALFWORDS
         SPACE 2
ALIAS2Z  MVC   0(3,R4),DIREPA             MOVE IN MAIN MODULE ENTRY
         MVC   3(8,R4),MEMBER1            ADD MAIN MODULE NAME
         TM    DIRATTR2,DIR2SSI           SSI INFORMATION?
         BNO   *+8                        NO, BRANCH
         LA    R5,2(,R5)                  YES, TWO MORE HALFWORDS
         NI    DIRFLAG,X'E0'              TURN OFF HALFWORD COUNT
         OI    DIRFLAG,*-*                <<EXECUTED>>
         EX    R5,*-4                     MOVE IN NUMBER OF HALFWORDS
         SPACE 1
ALIAS3   TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ALIAS3S                    YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    DIRATTR3,DIRAM24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    DIRATTR3,DIRAM31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS3S  L     R15,=A(READESD)            SCAN ESD FOR MEMBER NAME
         BALR  R14,R15                    MEMBER NAME IN ESD ENTRIES?
         B     ALIAS4                     NO, PSEUDO ENTRY
         SPACE 1
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ALIAS4                     YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS4   OI    DIRFLAG,X'80'              SET ALIAS FLAG
         BAL   R2,OPENSTOW                OPEN STOW DCB; ENQUEUES
         B     NEWCMD                     COULD NOT OPEN -- ERROR
         STOW  STOWDCB,DIRNAME,A          ADD ALIAS TO DIRECTORY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     ALIAS5                     00 - SUCCESSFUL
         B     MEMEXIST                   04 - MEMBER ALREADY EXISTS
         EX    0,*                        08 - SHOULD NOT HAPPEN - ADD
         B     FULLDIR                    12 - DIRECTORY IS FULL
         B     IOERROR                    16 - I/O ERROR IN DIRECTORY
         SPACE 3
ALIAS5   TSMSG L010                       MSG - ALIAS ASSIGNED
         TM    FLAGSCC,RECFMU             MUST WE GIVE ENTRY POINT?
         BZ    NEWCMD                     NO, BRANCH
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         TSMSG (R1)
         B     NEWCMD
         TITLE 'P D S  --  PDS ATTRIB                          6/10/85'
***********************************************************************
***      ATTRIB SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
ATTRIB   CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    ATTR020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTATH
         MVI   MTHIGHL,72
         TM    ##ADRCM#+#LSUB,FATTRHDR  HEADER WRITTEN YET?
         BO    ATTR000                  YES, BRANCH
         TSMSG L230$1
         OI    ##ADRCM#+#LSUB,FATTRHDR  MARK FOR NEXT CALL
         SPACE 1
ATTR000  MVC   INSERT#1(72),BLANK128
         CLI   #SSIOPT,0               ANY SSI CHANGE?
         BE    ATTR006                 NO, BRANCH
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR002                 YES, BRANCH
         NI    DIRFLAG,FF-X'1F'        TURN OFF SSI DATA
         XC    DIRSTART(4),DIRSTART    CLEAR THE SSI FIELD
         B     ATTR006
         SPACE 1
ATTR002  MVC   DIRSTART(4),#SSITEXT    ADD THE CURRENT SSI DATA
         NI    DIRFLAG,FF-X'1F'        TURNS OFF ANY HALFWORDS
         OI    DIRFLAG,X'02'           ADDS IN TWO HALFWORDS
         SPACE 1
ATTR006  MVC   INSERT#1+1(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   ATTR010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   ATTR010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   ATTR010             NO, BRANCH
         SPACE 1
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         B     ATTR012                 NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
ATTR010  CLC   ZERO,0(R2)              ZERO?
         BE    ATTR012                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR012                 YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         SPACE 1
ATTR012  TSMSG L230$1                  OUTPUT STATISTICS
         MVI   MTHIGHL,8               RESET THE INSERT LENGTH
         CLI   #OPTOPT,FF              ANY ILLEGAL CHANGES?
         BNE   ATTR014                 NO, BRANCH
         TSMSG L701                    NOT A LOAD MODULE
ATTR014  CLI   #OPTOPT,X'F0'           ANY SSI CHANGE?
         BE    ATTR240                 YES, BRANCH
         B     ATTR600                 CHECK FOR ALIAS
         SPACE 3
ATTR020  TM    FLAGSAA,FHEAD           BLANK SEPARATOR WRITTEN YET?
         BNO   ATTR022                 NO, BRANCH
         MESSAGE MSGBLANK              NOW SEPARATORS CAN BE WRITTEN
         SPACE 1
ATTR022  OI    FLAGSAA,FHEAD
         MVI   RLDCOUNT,X'FF'          ASSUME NO RLD/CONTROL DATA
         MVI   LKEDDATE,X'FF'          ASSUME NO LKED DATE
         CLI   #RLDERR,X'01'           "RLDFIX" OPERAND?
         BE    ATTR024                 YES, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE"?
         BE    ATTR030                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    ATTR024                 YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR030                 YES, BRANCH
ATTR024  L     R15,=A(READIDR)         FIRST RLD COUNT, LKED DATA
         BALR  R14,R15                 IDR DATA AVAILABLE?
         MVI   LKEDDATE,X'FF'          NO, ERROR
         SPACE 1
ATTR030  TM    #OPTOPT,X'F0'           ANY CHANGED ATTRIBUTES?
         BNO   ATTR250                 NO, BRANCH
         SPACE 1
         CLI   #RLDERR,X'01'           "RLDFIX"?
         BNE   ATTR038                 NO, BRANCH
         TM    DIRATTR2,DIRAOSLE       OS/VS LINKAGE-EDITOR?
         BNO   ATTR034                 NO, BRANCH
         CLI   RLDCOUNT,X'FF'          ANY RLD/CONTROL DATA FOUND?
         BE    ATTR034                 NO, BRANCH
         MVC   DIRATTR4(1),RLDCOUNT    MODIFY THE DIRECTORY ENTRY
         B     ATTR038
ATTR034  TSMSG L723                    NO CHANGE IS POSSIBLE
         SPACE 2
ATTR038  CLI   #AUTHOPT,X'01'          CHANGE APF PROCESSING?
         BL    ATTR060                 NO, BRANCH
         BE    ATTR040                 YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE APF INFORMATION?
         BNO   ATTR060                     NO, BRANCH
         SPACE 1
ATTR040  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE APF INFORMATION?
         BNO   ATTR050                     NO, BRANCH
         LA    R2,DIRAPF                   YES, POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               SCATTER SIZE
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR044                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR044  LA    R2,11(,R2)              ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI        SSI?
         BNO   *+12                    NO, BRANCH
         LA    R2,5(,R2)               ADD 4 AND ROUND TO HALFWORD
         N     R2,=F'-2'
         CLI   0(R2),1                 LENGTH RIGHT?
         BNE   ATTR050                 NO, BRANCH
         MVI   1(R2),1                 ASSUME TURN AUTHORIZATION ON
         CLI   #AUTHOPT,X'01'          CORRECT?
         BE    ATTR060                 YES, BRANCH
         MVI   1(R2),0                 NO, TURN OFF
         B     ATTR060
         SPACE 3
ATTR050  TSMSG L722                        INVALID APF DATA LENGTH
         SPACE 2
ATTR060  CLI   #PAGEOPT,X'01'              CHANGE PAGE ALIGNMENT?
         BL    ATTR100                     NO, BRANCH
         BE    ATTR070                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE PAGE INFO?
         BNO   ATTR100                     NO, BRANCH
         SPACE 1
ATTR070  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE PAGE INFORMATION?
         BNO   ATTR080                     NO, BRANCH
         OI    DIRATTR2,DIR2PAGA           ASSUME TURN ON PAGE BOUNDARY
         CLI   #PAGEOPT,X'01'              CORRECT?
         BE    ATTR100                     YES, BRANCH
         NI    DIRATTR2,FF-DIR2PAGA        NO, TURN OFF PAGE BOUNDARY
         B     ATTR100
         SPACE 1
ATTR080  TSMSG L726                    PAGE ALIGNMENT CANNOT BE CHANGED
         SPACE 2
ATTR100  CLI   #RMODE,1                    ANY RMODE CHANGE?
         BL    ATTR120                     NO, BRANCH
         BH    ATTR110                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR120                     NO, BRANCH
         SPACE 1
ATTR110  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR150                     NO, BRANCH
         NI    DIRATTR3,FF-DIRRMANY        SET RMODE=24
         CLI   #RMODE,1                    CORRECT?
         BE    ATTR120                     YES, BRANCH
         OI    DIRATTR3,DIRRMANY           NO, TURN ON RMODE ANY
         SPACE 2
ATTR120  CLI   #AMODE,1                    ANY AMODE CHANGE?
         BL    ATTR160                     NO, BRANCH
         BH    ATTR130                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR160                     NO, BRANCH
         SPACE 1
ATTR130  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR150                     NO, BRANCH
         TM    DIRFLAG,X'80'               ALIAS ENTRY?
         BO    ATTR140                     YES, BRANCH
         OI    DIRATTR3,DIRAM24+DIRAM31    MAIN ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAM31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAM31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAM24            SET AMODE=31
         B     ATTR160
         SPACE 1
ATTR140  OI    DIRATTR3,DIRAA24+DIRAA31    ALIAS ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAA31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAA31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAA24            SET AMODE=31
         B     ATTR160
         SPACE 1
ATTR150  TSMSG (R1)                        AMODE/RMODE MESSAGE
         SPACE 2
ATTR160  OC    DIRATTR,#ATTRYES        ADD POSITIVE ATTRIBUTES
         NC    DIRATTR,#ATTRNO         REMOVE NEGATIVE ATTRIBUTES
         SPACE 1
*
*   RESTRICTION: BOTH RENT AND REUS MUST BE SPECIFIED FOR REENTRANT
         TM    DIRATTR,ATTRRENT        WAS REENTRABLE SPECIFIED?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR,ATTRREUS        YES, ALSO FORCE REUSABLE
         SPACE 1
ATTR180  CLI   #SSIOPT,0               SSI(HEXDATA) OR NOSSI?
         BE    ATTR220                 NO, BRANCH
         SR    R1,R1                   SSI INDICATOR
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR182                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR182  LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         LR    R3,R2                   START OF DATA BEFORE ANY SSI
         LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR184                 YES, BRANCH
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR188                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR188                 YES, NO SSI
         B     ATTR186                 NO, POINT TO SSI
         SPACE 1
ATTR184  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR188                 NO, NO SSI
         SPACE 1
ATTR186  LR    R1,R2                   POINT TO SSI
ATTR188  LR    R15,R2                  SSI POSITION
         SR    R15,R3                  OFFSET FROM LAST DATA TO SSI
         LA    R15,2(,R15)             ADJUSTMENT AMOUNT
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR200                 YES, BRANCH
         SPACE 1
*** NOSSI
         LTR   R1,R1                   ANY SSI?
         BZ    ATTR220                 NO, DONE
         ICM   R0,B'0011',4(R2)        APF BYTES
         XC    0(8,R3),0(R3)           CLEAR FOLLOWING BYTES
         STCM  R0,B'0011',0(R3)        MOVE APF BEFORE THE SSI
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         NI    DIRATTR2,FF-DIR2SSI     NO SSI PRESENT
         LA    R3,2+1(,R3)             2 FOR APF AND 1 FOR ROUNDING
         B     ATTR214
         SPACE 2
ATTR200  LTR   R1,R1                   SSI ALREADY?
         BZ    ATTR210                 NO, BRANCH
*** SSI(HEXTEXT) WITH EXISTING SSI INFORMATION
         MVC   0(4,R2),#SSITEXT        UPDATE THE SSI DATA
         B     ATTR220
         SPACE 2
*** SSI(HEXTEXT) WITH NO PREVIOUS SSI INFORMATION
ATTR210  ICM   R0,B'0011',0(R3)        APF BYTES
         MVC   0(4,R2),#SSITEXT        MOVE IN ADDED SSI DATA
         STCM  R0,B'0011',4(R2)        RESET THE APF INFORMATION
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR2,DIR2SSI        SSI PRESENT
         LA    R3,4+2(,R2)             4 FOR SSI AND 2 FOR APF
         SPACE 1
ATTR214  LA    R0,DIRSTART             FIRST BYTE AFTER DIRFLAG
         SR    R3,R0                   LENGTH OF ADDED DATA
         SRL   R3,1                    IN HALFWORDS
         NI    DIRFLAG,FF-X'1F'        REMOVE LENGTH BITS
         IC    R0,DIRFLAG              PICK UP ALIAS FLAG, TTR COUNT
         OR    R0,R3                   ADD IN NEW LENGTH IN HALFWORDS
         STC   R0,DIRFLAG              UPDATE DIRECTORY FLAG ENTRY
         SPACE 2
ATTR220  CLI   #MODTXT,X'41'              ANY ENTRY(EXTNAME)?
         BL    ATTR240                    NO, BRANCH
         MVC   #USERTXT(8),DIRNAME        SAVE THE CURRENT MEMBER NAME
         MVC   DIRNAME(8),#MODTXT         NAME TO SEARCH FOR IN ESD
         SPACE 1
         L     R15,=A(READESD)            SCAN ESD
         BALR  R14,R15                    EXTERNAL NAME IN ESD ENTRIES?
         NOP   0                          NO, IGNORE
         CLC   ENTRYPT(8),#MODTXT         NAME MATCH?
         BE    ATTR222                    YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         MVC   INSERT#1(8),#MODTXT        EXTERNAL NAME
         TSMSG L705$1
         B     ATTR240
         SPACE 1
ATTR222  MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ATTR240                    YES, BRANCH
         OC    #RMODE(2),#RMODE           RMODE/AMODE CHANGES ALREADY?
         BNZ   ATTR240                    YES, BRANCH
         TM    DIRATTR2,DIRAOSLE          CAN CHANGE AMODE?
         BNO   ATTR240                    NO, BRANCH
         OI    #RMODE,1                   MARK RMODE/AMODE CHANGE
         SPACE 1
         TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ATTR224                    YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY       SET RMODE=24
         TM    #ALIAESD,RMODEANY          CORRECT?
         BZ    *+8                        YES, BRANCH
         OI    DIRATTR3,DIRRMANY          NO, TURN ON RMODE ANY
         SPACE 2
         NI    DIRATTR3,FF-DIRAM24-DIRAM31  MAIN ENTRY -- AMODE=24+31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM31           YES, SET THE DIRECTORY
         B     ATTR240
         SPACE 1
ATTR224  NI    DIRATTR3,FF-DIRAA24-DIRAA31  ALIAS ENTRY -- AMODE=24+31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 2
ATTR240  BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR
         STOW  STOWDCB,DIRNAME,R       REPLACE OPTION
         LR    R5,R15                  SAVE RETURN CODE
***      BAL   R2,SHUTSTOW             *** CLOSE STOW AFTER NEWCMD
         SPACE 1
         B     *+4(R5)                 PROCESS RETURN CODE
         B     ATTR250                   00 - SUCCESSFUL
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
ATTR250  TM    FLAGSCC,RECFMU          LOAD LIBRARY?
         BNO   ATTR600                 NO, CHECK FOR ALIASES
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(128),BLANK128
         LA    R1,INSERT#2-1           FIRST COMMA ADDRESS (HIDDEN)
         MVC   INSERT#2+1(4),=C'NONE'  IF NO SPECIAL ATTRIBUTES
         LA    R14,TBLATTR-12          ATTRIBUTES TABLE
         LH    R2,DIRATTR              GET MODULE ATTRIBUTES
         SPACE 2
ATTR300  LA    R14,12(R14)
         CLI   0(R14),X'FF'            END OF TABLE?
         BE    ATTR310                 YES, BRANCH
         SPACE 1
         LH    R15,0(R14)              GET ATTRIBUTE FLAGS
         TM    2(R14),X'80'            POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'                MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'                MASK FOR NEG (FOR BNZ) INST
         NR    R15,R2                  CHECK FOR ATTRIBUTE PRESENT
         NOP   ATTR300                 <<BZ OR BNZ>>
         EX    R3,*-4                  BRANCH IF NO DISPLAY ATTRIBUTE
         SPACE 1
         MVC   0(6,R1),ATTRCOMM        COMMA AND FOLLOWING BLANKS
         IC    R15,2(R14)              LENGTH-1 OF ATTRIBUTE
         LA    R3,X'7F'                MASK FOR 7 BITS
         NR    R15,R3
         MVC   2(*-*,R1),3(R14)        <<EXECUTED>>
         EX    R15,*-6
         LA    R1,3(R15,R1)            JUMP POINTER
         B     ATTR300
         SPACE 2
ATTR310  MVI   MTHIGHL+4,127
         TSMSG L020$2                  ATTRIBUTES MSG
         MVI   MTHIGHL+4,8
         SPACE 2
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR314                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR314  LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR330                 YES, BRANCH
         TSMSG L022                    NON-VS LINKAGE EDITOR MESSAGE
         B     ATTR340
         SPACE 1
ATTR330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR360                 NO, SKIP SSI PROCESSING
         SPACE 1
ATTR340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR360                 YES, NO SSI
         SPACE 1
         UNPK  INSERT#1(9),0(5,R2)
         TR    INSERT#1(8),TRTABLE
         TSMSG L025$1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTR360  TM    FLAGSCC,RECFMU          LOAD MODULE LIB?
         BZ    ATTR600                 NO, BRANCH
         SPACE 1
         MVI   MODESAVE,0
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTR540                     NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    ATTR400                     NO, BRANCH
         MVC   INSERT#1(8),BLANKS          RMODE FILLER
         MVC   INSERT#2(8),BLANKS          AMODE FILLER
         MVC   INSERT#1(3),=C'24 '         MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#1(3),=C'ANY'         MOVE IN THE RMODE TEXT
         MVC   INSERT#2(3),=C'24 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'ANY'         MOVE IN THE AMODE TEXT
         TSMSG L120$2
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTR400                     NO, BRANCH
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    ATTR370                     YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    ATTR400                     YES, BRANCH
         LA    R1,=C'24 '                  AMODE IS 24
ATTR370  MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         TSMSG L880$1                      RMODE ANY/AMODE CONFLICT
         SPACE 2
ATTR400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTR500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTR500                 YES, BRANCH
         TSMSG L826
         SPACE 2
ATTR500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTR510                 NO, BRANCH
         TSMSG L024                    PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTR510  LA    R1,L720                 ASSUME NO APF DATA
         TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   ATTR530                 NO, BRANCH
         SPACE 1
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTR530                 NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTR530                 YES, BRANCH
         BL    ATTR540                 NO, APF=0 (NO MESSAGE NEEDED)
         LA    R1,L023                 APF DATA IS TOO LARGE
         SPACE 1
ATTR530  TSMSG (R1)
         SPACE 2
ATTR540  TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANK FILL
         MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REENTRANT
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   ATTR542                         NO, BRANCH
         TSMSG L884$1                          YES, ERROR
ATTR542  MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REUSABLE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   ATTR543                         NO, BRANCH
         TSMSG L884$1                          YES, ERROR
ATTR543  MVC   INSERT#1(4),REFRTXT+3           OVERLAY AND REFRESHABLE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   ATTR544                         NO, BRANCH
         TSMSG L884$1                          YES, ERROR
ATTR544  MVC   INSERT#1(4),SCTRTXT+3           OVERLAY AND SCATTER
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   ATTR545                         NO, BRANCH
         TSMSG L884$1                          YES, ERROR
ATTR545  TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         TM    MODESAVE,DIRRMANY               CORRECT?
         BNO   ATTR546                         NO, BRANCH
         TSMSG L884$1                          YES, ERROR
ATTR546  MVI   INSERT#1,C'A'                   AMODE ANY
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    ATTR550                         YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    ATTR550                         YES, BRANCH
         MVC   INSERT#1+6(3),=C'31 '           NO, AMODE 31
         TSMSG L884$1                          YES, ERROR
         SPACE 1
ATTR550  TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   ATTR560                         NO, BRANCH
         TSMSG L882                            YES, ERROR
         SPACE 1
ATTR560  TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   ATTR570                         NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    ATTR570                         YES, BRANCH
         TSMSG L881                            ERROR MESSAGE
         SPACE 1
ATTR570  TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   ATTR590                         NO, BRANCH
         TSMSG L883                            YES, ERROR
         SPACE 2
ATTR590  UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         TSMSG L102$1                 NO ENTRY POINT MESSAGE
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         TSMSG L104$2
         SPACE 2
         CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTR600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTR600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR600                 YES, BRANCH
         SPACE 1
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         TSMSG L106$1
         SPACE 3
ATTR600  TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTR630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTR630                 NO, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM24
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTR610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTR630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTR610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTR610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM24
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTR610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM24
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         TSMSG L092$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTR610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTR630  NI    FLAGSBB,FF-FLINESET
         CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
         BE    ATTR640                 YES, BRANCH
         BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD                  YES, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BO    ATTR640                 YES, BRANCH
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTR640  MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTR900                 NO, FIND ALL ALIASES
         XC    INSERT#1(8),INSERT#1    NO MAIN(S) FOUND
         SPACE 2
*  LIST ANY CORRESPONDING MAIN MODULE NAMES
         SPACE 1
ATTR800  BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTR810            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTR800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTR800            NO, BRANCH
         MVC   INSERT#1(8),MEMNAME
         TSMSG L066$1
         B     ATTR800
         SPACE 1
ATTR810  OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, BRANCH
         TSMSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 2
*  LIST ANY ALIASES FOR THE MODULE
         SPACE 1
ATTR900  BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     ATTR940              LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   ATTR900              NO, BRANCH
         TM    MEMFLAG,X'80'        AN ALIAS?
         BO    ATTR910              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME   DID I FIND MYSELF?
         BE    ATTR900              YES, BRANCH
         MVC   INSERT#1(8),MEMNAME
         TSMSG L864$1               THIS IS AN APPARENT ALIAS MEMBER
         B     ATTR900
         SPACE 1
ATTR910  LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BO    ATTR920              YES, BRANCH
         OI    FLAGSBB,FLINESET     LINE NOW IN PROGRESS
         LA    R1,80                ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON       ISPMODE ACTIVE?
         BO    ATTR912              YES, BRANCH
         TM    CONTOPTN,1           ANY LOG RECORDING?
         BO    ATTR912              YES, BRANCH
         GTSIZE ,                   TERMINAL SIZE
         CH    R1,=H'120'           120 OR LESS BYTES?
         BL    *+8                  YES, BRANCH
         LH    R1,=H'120'           NO, USE 120 BYTES
ATTR912  SH    R1,=H'7'             LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE          CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGALIAS),MSGALIAS
         LA    R5,MSGLINE+4+L'MSGALIAS
         MVC   FULLWORD(3),L160$1   MESSAGE IDENTIFIER
         SPACE 1
ATTR920  CLI   0(R2),X'40'          SCAN FOR
         BNE   *+8                          LAST
         BCT   R2,ATTR920                       NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R5,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   ATTR930
         LA    R1,MSGLINE+4
         SR    R5,R1
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3         INDENT FOR BLANKS
         SPACE 1
ATTR930  MVI   0(R5),X'40'
         MVC   1(8,R5),MEMNAME
         TR    1(8,R5),TRLINE
         LA    R1,2(R2,R5)
         MVI   0(R1),C','
         LA    R5,3(R2,R5)
         B     ATTR900
         SPACE 2
ATTR940  TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BZ    NEWCMD               NO, EXIT
         LA    R1,MSGLINE+5
         SR    R5,R1
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         B     NEWCMD
ATTRCOMM DC    C',     '         COMMA AND FOLLOWING BLANKS
MSGALIAS DC    C'ALIASES FOR THIS MEMBER ARE:'
         SPACE 2
         TITLE 'P D S -- PDS BROWSE/EDIT/ISPF/ISPMODE/MEMLIST  6/10/85'
***********************************************************************
***     BROWSE SUBCOMMAND      ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***     EDIT SUBCOMMAND        ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***     ISPF SUBCOMMAND        ADDED BY STEVE SMITH  -- MAY,  1984  ***
***                                                                 ***
***     ISPMODE SUBCOMMAND     ADDED BY STEVE SMITH  -- AUG., 1984  ***
***                                                                 ***
***     MEMLIST SUBCOMMAND     ADDED BY STEVE SMITH  -- SEPT, 1984  ***
***********************************************************************
*
         SPACE 1
BROWSE   CSECT
         USING *,R8
         AIF ('&CISP' EQ 'ISPFV2').NSPF150
         LA    R1,L730                  ASSUME ALREADY UNDER SPF
         TM    FLAGSFF,FSPFOPT6         CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
.NSPF150 ANOP
         OI    ##ADRPA#,$A+$D           SET TO CANCEL STAX EXIT
         LA    R1,L731                  ASSUME SPF DID NOT INITIALIZE
         TM    FLAGSFF,FSPFERR          CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
         MVC   MSGTEXT1(8),##SUBCAL     SPF SERVICE REQUESTED
         CLI   #SPFOLD,X'01'            "NEW" SPECIFIED?
         BNE   SPFMEMOK                 NO, BRANCH
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    SPFMEMOK                 YES, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     SPFMEMOK                   04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
SPFMEMOK TM    ##ADRPC#,@I             BROWSE OR EDIT?
         BNO   SPFINIT                 NO, ALLOW IT
         LA    R1,DIRNAME-1            START OF MEMBER NAME
         LA    R0,8                    MAXIMUM MEMBER NAME
         SPACE 1
SPFMEM2  LA    R1,1(,R1)               SCAN
         CLI   0(R1),C'.'                  FOR
         BE    BADMEMB                        FIRST
         BCT   R0,SPFMEM2                          PERIOD
         CLI   DIRNAME,X'EF'           NUMERIC FIRST CHARACTER?
         BH    BADMEMB                 YES, INVALID
         SPACE 1
SPFMEM4  CLC   ##SUBCOM(8),$EDI        EDIT SUBCOMMAND?
         BE    SPFMEM5                 YES, BRANCH
         CLC   ##SUBCOM(8),$SPF        SPFEDIT SUBCOMMAND?
         BNE   SPFINIT                 NO, ALLOW IT
SPFMEM5  MVC   DSNMEMQ(8),DIRNAME      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         LA    R1,L702                 ASSUME THIS IS A LOAD LIBRARY
         TM    FLAGSCC,RECFMU          CORRECT?
         BO    MSGNEWXX                YES, BRANCH
         SPACE 2
SPFINIT  TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF ENVIRONMENT INITIALIZED?
         BM    SPFLINK                    YES, CALL SPF EDIT/BROWSE
         BAL   R2,SPFRECUR                NO, INVOKE PDS AS A DIALOG
         SPACE 3
SPFLINK  ICM   R15,B'1111',ISPLINK      ISPLINK ADDRESS KNOWN?
         BNZ   SPFINTRF                 YES, BRANCH
         OI    FLAGSFF,FSPFERR          SPF ERROR
         MVI   ##ADRCM#,CONTINUE+EDITOR+FABEND
         LOAD  EP=ISPLINK               ADDRESS OF SPF INTERFACE MODULE
         ST    R0,ISPLINK               SAVE ADDRESS FOR LATER
         XI    FLAGSFF,FSPFERR          NOT AN SPF ERROR NOW
         MVI   ##ADRCM#,0
SPFINTRF DS    0H                                              SS JUL84
         STFSMODE ON,INITIAL=YES       ENTER FULLSCREEN MODE
         CLC   ##SUBCOM(8),$ISM        ISPF DISPLAY MODE SUBCD SS JUL84
         BE    SPFSETM                 YES, ALLOW IT           SS JUL84
         CLC   ##SUBCOM(8),$MML        MEMLIST SUBCOMMAND?    ABL OCT84
         BE    MEMLIST                 YES, ALLOW IT          ABL OCT84
         BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS JUL84
         SPACE 2
         XC    RECOVER,RECOVER            NO ESTAE RECOVER ATTEMPTED
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'REFRESH'           THIRD PARAMETER
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'           LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BO    *+6                        YES, BRANCH
         BALR  R14,R15                    NO, LET SPF KNOW
         SPACE 1
         TM    ##ADRPA#,$P                SELECT PANEL SUPPORT SS NOV84
         BO    SPFP1                      GO PROCESS           SS NOV84
         BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS NOV84
         CLC   ##SUBCOM(8),$ISP           ISPF SUBCOMMAND?
         BNE   SPFNOMEN                   NO, BRANCH
         LA    R0,SPFP0SEL                FIRST PARAMETER
         LA    R1,29                      16 CHARACTERS
         ST    R1,FULLWORD
         LA    R1,FULLWORD                SECOND PARAMETER
         LA    R2,MSGTEXT1                START OF STRING BYTES
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'           MARK END OF LIST (THIRD ITEM)
         MVC   0(33,R2),ISPPANEL          ADD PANEL(ISR@PRIM) ...
         OC    #COMMDSZ(2),#COMMDSZ       ANY PARM DATA?
         BZ    SPFCALL                    NO, BRANCH
         MVC   33(80,R2),MSGLINE          ADD THE PARM DATA
         LA    R1,34                      LENGTH OF PARAMETER STRING
         AH    R1,#COMMDSZ                LENGTH OF PARAMETER STRING
         ST    R1,FULLWORD                UPDATE THE PARAMETER LENGTH
         AR    R2,R1                      POINT TO LAST BYTE +1
         BCTR  R2,0                       POINT TO LAST BYTE
         MVI   0(R2),C')'                 LAST PAREN
         B     SPFCALL
         SPACE 2
SPFNOMEN MVI   MSGTEXT1+8,C''''           ADD A QUOTE
         MVC   MSGTEXT1+8+1(44),DSNAME    ADD THE DATA SET NAME
         LH    R15,DSNLEN                 DSNAME ACTUAL LENGTH
         LA    R2,MSGTEXT1+8(R15)         POINT TO CURRENT BYTE
         TM    DSORG,DS1DSGPO             NON-PARTITIONED DATA SET?
         BZ    SPFALLM                    YES, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS DESIRED?
         BNZ   SPFMEMBR                   NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP IN PROGRESS?
         BNO   SPFALLM                    NO, BRANCH
         SPACE 1
SPFMEMBR MVI   1(R2),C'('               MEMBER NAME PARENTHESIS
         MVC   2(8,R2),DIRNAME          ADD MEMBER NAME
         MVI   10(R2),X'40'             INSURE SCAN STOPS
         LA    R2,1(,R2)                SCAN
         CLI   0(R2),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R2),C')'               ADD FINAL PARENTHESIS
         SPACE 2
SPFALLM  MVI   1(R2),C''''              ADD FINAL QUOTE
         MVI   2(R2),X'40'              ADD FINAL BLANK
         LA    R0,MSGTEXT1              FIRST PARAMETER
         LA    R1,MSGTEXT1+8            SECOND PARAMETER
         LA    R2,VOLUME                THIRD PARAMETER
         LA    R3,PASSWORD              FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2           SAVE PARAMETER ADDRESSES
         MVI   MSGTEXT2+12,X'80'        MARK END OF LIST (FOUR ITEMS)
         CLC   ##SUBCOM(8),$BRO         BROWSE SUBCOMMAND?
         BNE   SPFALLZ                  NO, BRANCH
         SPACE 1
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,SPFPERRO              SECOND PARAMETER
         LA    R2,SPFPRETU              THIRD PARAMETER
         STM   R0,R2,MSGTEXT2+24        SAVE ADDRESSES
         OI    MSGTEXT2+24+08,X'80'     LAST
         LA    R1,MSGTEXT2+24               PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15
         SPACE 1
         LA    R1,MSGTEXT2              PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15                  SPF INTERFACE ROUTINE
         LR    R3,R15                   SAVE ANY ERROR INDICATION
         SPACE 1
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,SPFPERRO              SECOND PARAMETER
         LA    R2,SPFPCANC              THIRD PARAMETER
         STM   R0,R2,MSGTEXT2+24        SAVE ADDRESSES
         OI    MSGTEXT2+24+08,X'80'     LAST
         LA    R1,MSGTEXT2+24               PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R3,R3                    ANY ERROR?
         BZ    SPFRET00                 NO, BRANCH
         TSMSG L733
         B     SPFRET00
         SPACE 1
SPFALLZ  CLI   #SPFPROF,0               ANY PROFILE NAME?
         BNE   SPFMORE2                 YES, BRANCH
         MVC   #SPFPROF,SETPROF         INITIALIZE WITH DEFAULT
SPFMORE  CLC   #SPFMACR(1),#SPFPROF     ANY MACRO OR PROFILE?
         BNE   SPFMORE2                 YES,  BRANCH
         CLI   #SPFPROF,0               ANY PROFILE NAME?
         BE    SPFCALL                  NO, BRANCH
         SPACE 1
SPFMORE2 SR    R4,R4                    FIFTH PARAMETER
         SR    R5,R5                    ASSUME NO MACRO NAME
         CLI   #SPFMACR,0               CORRECT?
         BE    *+8                      NO, BRANCH
         LA    R5,#SPFMACR              SIXTH PARAMETER
         SR    R6,R6                    ASSUME NO PROFILE NAME?
         CLI   #SPFPROF,0               CORRECT?
         BE    *+8                      NO, BRANCH
         LA    R6,#SPFPROF              SEVENTH PARAMETER
         STM   R0,R6,MSGTEXT2           SAVE PARAMETER ADDRESSES
         MVI   MSGTEXT2+24,X'80'        MARK END OF LIST (SEVEN ITEMS)
         SPACE 1
SPFCALL  LA    R1,MSGTEXT2              PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15                  SPF INTERFACE ROUTINE
SPFRET00 DS    0H
         AIF ('&RETURNX' EQ 'NO').RETURNX
         CLI   FLAGSSS,0                ANY RETURN TRAP SET?
         BE    SPFRET30                 NO, BRANCH
         CLC   ##SUBCOM(8),$ISP         ISPF SUBCOMMAND?
         BNE   SPFRET20                 NO, BRANCH
         MVI   FLAGSSS,0                YES, TRAP IS RESET
         B     SPFRET30
         SPACE 1
SPFRET20 CLC   ##SUBCOM(1),FLAGSSS      THIS SERVICE AGAIN?
         MVI   FLAGSSS,0
         BNE   SPFCALL                  NO, CALL IT AGAIN
         SPACE 1
SPFRET30 TM    SPFLAG0,SPFDON           ISPF DIALOG TABLES?
         BNO   SPFRET35                 NO, BRANCH
         CLI   PDSENTRY+3,0             ENTRY TO PDS AS A DIALOG?
         BE    SPFRET60                 YES, DO NOT RE-INVOKE
SPFRET35 CLC   ##SUBCOM(8),$ISP         ISPF SUBCOMMAND?
         BNE   SPFRET40                 NO, BRANCH
         CH    R15,=H'4'                YES, RETURN OR EXIT OPTION?
         BNE   SPFRET60                      NO, BRANCH
         MVI   FLAGSSS,C'Z'                  YES, SET RE-INVOKE TRAP
         B     SPFRET80
         SPACE 1
SPFRET40 LA    R1,SPFVDEF               VDEF
         LA    R2,SPFZVERB              ZVERB
         LA    R3,MSGTEXT2+50           WHERE TO PLACE OUTPUT
         LA    R4,SPFCHAR               MOVE FUNCTION
         L     R15,SPFL8                LENGTH 8
         ST    R15,FULLWORD
         LA    R5,FULLWORD              USE AS PARM
         STM   R1,R5,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+16,X'80'     END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R15,R15                  DOES ZVERB EXIST?
         BNZ   SPFRET50                 NO, BRANCH
         SPACE 1
         LA    R1,SPFVGET               VGET
         LA    R2,SPFZVERB              ZVERB
         LA    R3,SPFSHARE              SHARED
         STM   R1,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+8,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R15,R15                  DOES ZVERB EXIST?
         BNZ   SPFRET50                 NO, BRANCH
         SPACE 1
         CLC   MSGTEXT2+50(3),SPFPRETU  RETURN KEY?
         BNE   SPFRET50                 NO, BRANCH
         MVC   FLAGSSS(1),##SUBCOM      SET THE TRAP CHARACTER
SPFRET50 LA    R2,SPFVDEL               VDELETE
         LA    R3,SPFZVERB              ZVERB
         STM   R2,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+4,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         B     SPFRET80
SPFVDEF  DC    CL8'VDEFINE'
SPFVDEL  DC    CL8'VDELETE'
SPFZVERB DC    CL8'(ZVERB)'
SPFL8    DC    F'8'
SPFCHAR  DC    CL8'CHAR'
SPFVGET  DC    CL8'VGET'
SPFSHARE DC    CL8'SHARED'
         SPACE 1
SPFRET60 MVI   FLAGSSS,0                RESET THE TRAP CHARACTER
         SPACE 1
SPFRET80 DS    0H
.RETURNX ANOP
         LA    R1,FIRST4K
         MVI   SPFPANPR-FIRST4K(R1),X'88' MARK UNKNOWN PANEL NAME
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'SM'                THIRD PARAMETER
         LA    R3,=F'1'                   FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+12,X'80'          LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BO    *+6                        YES, BRANCH
         BALR  R14,R15                    NO, LET SPF KNOW
*        BAL   R2,CLEAR                 TURN OFF FULL SCREEN MODE
         NI    ##ADRPA#,FF-$A-$D        DO NOT IGNORE ATTENTION NOW
         SPACE 1
         ESTAE 0                        CANCEL THE ESTAE ADDRESS
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRESS
         L     R2,=A(STAEEXIT)          ESTAE EXIT ADDRESS
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)
         MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
         SPACE 1
         MVI   ##ADRCM#,EDITOR          NO REPEATED WARNING MESSAGE
         L     R1,X'21C'                --> CURRENT TCB
         L     R1,X'00C'(,R1)           --> TIOT
         LA    R1,24(,R1)               TIOENTRY
SPFNTIOT CLI   0(R1),0                  END OF TIOT?
         BE    RESTART0                 YES, REALLOCATE AND OPEN AGAIN
         CLC   4(8,R1),DDNAME           THIS DDNAME?
         BE    RESTART2                 YES, JUST OPEN AGAIN
         SR    R15,R15
         IC    R15,0(,R1)
         LA    R1,0(R15,R1)
         B     SPFNTIOT
SPFSETM  DS    0H                       DO FUNCTION            SS JUL84
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?       ABL OCT84
         BO    SPFSETMM                 YES, BRANCH           ABL OCT84
         OI    SPFLAG0,SPFDON           ISPMODE ACTIVE NOW     SS JUL84
         NI    FLAGSBB,FF-FONESHOT      ALWAYS CONTINUE       ABL OCT84
         TM    SPFLAG0,SPFDSUSP         ISPMODE SUSPENDED?    ABL OCT84
         BO    SPFSETMM                 YES, BRANCH           ABL OCT84
         NI    SPFLAG0,255-SPFDSUSP     NO LONGER SUSPENDED   ABL OCT84
         OI    SPFLAG3,SPFCHN           FORCE MSG TO BE LOGGED SS SEP84
         MVC   INSERT#1(8),PDSNAME                            ABL OCT84
         TSMSG L100$1                                         ABL OCT84
         MESSAGE MSGBLANK               ONE BLANK LINE        ABL OCT84
         L     R15,$DSN+8               DO A "DSN" SUBCOMMAND ABL OCT84
         BR    R15                                            ABL OCT84
         SPACE 3
SPFSETMM NI    SPFLAG0,255-SPFDSUSP     SET SUSPEND MODE OFF   SS JUL84
         NI    SPFLAG3,255-SPFCLIST     END CLIST INTERCEPT    SS NOV84
         B     NEWCMD                                         ABL OCT84
         SPACE 3
*                                                              SS NOV84
*    SELECT PANEL SUPPORT INTERFACE                            SS NOV84
*                                                              SS NOV84
SPFP1    DS    0H                                              SS NOV84
         ST    R6,SPFSAV6                                      SS NOV84
         LA    R6,FIRST4K                                      SS NOV84
         USING FIRST4K,R6                                      SS NOV84
         TM    ##ADRPA#,$J                MEMBERS PRESENT      SS NOV84
         BO    SPFP10                     PROCESS WITHOUT      SS NOV84
         TM    DSORG,DS1DSGPO             NON-PARTITIONED DATA SET?
         BZ    SPFP10                     YES, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS DESIRED?
         BNZ   SPFP5                      NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP IN PROGRESS?
         BNO   SPFP10                     NO, BRANCH
SPFP5    MVC   SPFMEMB,DIRNAME            SET CURRENT MEMBER   SS NOV84
         B     SPFP20                                          SS NOV84
SPFP10   MVI   SPFMEMB,C' '                                    SS NOV84
         MVC   SPFMEMB+1(7),SPFMEMB       BLANK                SS NOV84
SPFP20   DS    0H                                              SS NOV84
         LH    R1,#COMMDSZ                GET ADDED DATA LEN   SS NOV84
         STH   R1,SPFZLEN                 SAVE LEN (EVEN ZERO) SS NOV84
         LTR   R1,R1                      IS IT ZERO           SS NOV84
         BZ    SPFP22                     NO DATA              SS NOV84
         LA    R1,MSGLINE                 GET ADDR             SS NOV84
         ST    R1,SPFZDATA                SAVE ADDR ZCMD DATA  SS NOV84
SPFP22   DS    0H                                              SS NOV84
         MVC   SPFPSTK2,##SUBCAL          COPY PANEL ID        SS NOV84
         L     R6,SPFSAV6                                      SS NOV84
         DROP  R6                                              SS NOV84
         MVI   SPFLAG1,SPFPAND            CODE                 SS NOV84
         L     R15,=V(ISPDSPY)                                 SS NOV84
         BALR  R14,R15                                         SS NOV84
         NOP   0                                               SS NOV84
         CLC   0(4,R1),SPFP0END           END KEY              SS NOV84
         BE    SPFP30                     YES, BRANCH         ABL DEC84
         LR    R2,R1                      3RD PARAMETER        SS NOV84
         LA    R0,SPFP0SEL                FIRST PARAMETER      SS NOV84
         LA    R1,250                     BUFFER 250 CHARS     SS NOV84
         ST    R1,FULLWORD                                     SS NOV84
         LA    R1,FULLWORD                SECOND PARAMETER     SS NOV84
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES       SS NOV84
         OI    MSGTEXT2+8,X'80'           MARK END OF LIST     SS NOV84
SPFP30   BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS NOV84
         B     SPFCALL                                         SS NOV84
SPFP0END DC    C'END '                                         SS NOV84
SPFP0SEL DC    CL8'SELECT'
SPFPERRO DC    C'ERRORS '
SPFPRETU DC    C'RETURN '
SPFPCANC DC    C'CANCEL '
         SPACE 3                                               SS NOV84
         COPY  MEML71                   INCLUDE MEMLIST CODE   SS NOV84
         TITLE 'P D S  --  PDS COMPARE                        6/10/85'
***********************************************************************
***      COMPARE SUBCOMMAND    ADDED BY BRUCE LELAND --  MAY, 1984  ***
***********************************************************************
*
         SPACE 1
COMPARE  CSECT
         USING *,R8
         SPACE 1
*
*  MSGLINE
*      OFFSET    FIELD      DESCRIPTION
*     --------  ---------  -------------
*     0-99      OPERANDS   ANY OPERANDS AFTER THE NAMES
MSGDS1   EQU   MSGLINE+100,64
*     +0,56     DSNAME1    THE FIRST DATA SET NAME STRING
*     +61,1     FLAG       X'80' IF MEMBER ONLY
*     +62,2     LSTR1      LENGTH OF STRING
*                         
MSGDS2   EQU   MSGLINE+164,64
*     +0,56     DSNAME2    THE SECOND DATA SET NAME STRING
*     +61,1     FLAG       X'80' IF MEMBER ONLY
*     +62,2     LSTR2      LENGTH OF STRING
*
*
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MSGDS1          FIRST PART OF DSNAME1
         CLI   MSGDS1+63,8             MORE THAN 8 DIGITS?
         BH    COMPA10                 YES, BRANCH
         CLI   MSGDS1,C''''            QUOTED?
         BE    COMPA10                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA10                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA02  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA10                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA10                 YES, BRANCH
         BCT   R0,COMPA02              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFPOST         LINE COMMAND?
         BO    COMPA07                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS1+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA07  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA10                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
COMPA10  MVC   DIRNAME,MSGDS2          FIRST PART OF DSNAME2
         CLI   MSGDS2+63,8             MORE THAN 8 DIGITS?
         BH    COMPA20                 YES, BRANCH
         CLI   MSGDS2,C''''            QUOTED?
         BE    COMPA20                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA20                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA12  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA20                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA20                 YES, BRANCH
         BCT   R0,COMPA12              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFPOST         LINE COMMAND?
         BO    COMPA17                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS2+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA17  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA20                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
COMPA20  MVC   DIRNAME(8),MSGDS1       FIRST MEMBER NAME
         MVC   MSGTEXT1+4(8),##SUBCAL  PROCESSOR TO ATTACH
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         SPACE 2
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,MSGTEXT1+13(R15)     POINT TO CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
         TM    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA22                 YES, BRANCH
         LA    R3,MSGTEXT1+13          POINT TO CURRENT BYTE
         MVC   0(57,R3),MSGDS1         ADD THE FIRST DATA SET NAME
         AH    R3,MSGDS1+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA27
         SPACE 1
COMPA22  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA22                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 2
COMPA27  MVC   DIRNAME(8),MSGDS2       SECOND MEMBER NAME
         TM    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA29                 YES, BRANCH
         MVC   0(57,R3),MSGDS2         ADD THE SECOND DATA SET NAME
         AH    R3,MSGDS2+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA37
         SPACE 2
COMPA29  MVI   0(R3),C''''             ADD A QUOTE
         MVC   1(44,R3),DSNAME         ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,0(R3,R15)            CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
COMPA30  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA30                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 1
COMPA37  MVC   0(100,R3),MSGLINE       ADD ANY OTHER DATA
         AH    R3,#COMMDSZ             ADD LENGTH OF KEYWORD DATA
         SPACE 2
         LA    R2,MSGTEXT1             START OF COMMAND TEXT
         SR    R3,R2                   LENGTH OF COMMAND
         SLL   R3,16                   CLEAR BOTTOM TWO BYTES
         STCM  R3,B'1111',0(R2)        SAVE STRING TOTAL LENGTH
*        MESSAGE (R2)                  DELETE * TO OUTPUT COMMAND
         ST    R2,ADDRTEXT             COMMAND ADDRESS
         ST    R2,ADDRCBUF             COMMAND ADDRESS
         LA    R1,ADDRTEXT             COMMAND ADDRESS
         CLI   ##SUBCAL,C'%'           CLIST CALL?
         BE    EXEC                    YES, IMPLIED CLIST
         MVI   3(R2),9                 OPERAND OFFSET
         SPACE 1
         LA    R3,##SUBCAL             PROCESSOR TO ATTACH
         AIF ('&CC296' EQ 'YES').YESC296
COMPAZAP NOP   COMPA39                 DO NOT LINK TO COMPARE
         AGO   .NOTC296
.YESC296 ANOP
COMPAZAP B     COMPA39                 LINK TO COMPARE
.NOTC296 ANOP
***  THE FOLLOWING INTERFACE IS FOR COMPARE FROM THE CBT TAPE, FILE 300
         BAL   R2,ATTACH               ATTACH THIS COMMAND PROCESSOR
         B     NEWCMD
         SPACE 1
***  EITHER THE ABOVE OR THE FOLLOWING INTERFACE MAY BE
***  USED BY THE COMPARE COMMAND FROM THE CBT TAPE, FILE 296
COMPA39  LA    R15,WORKTBL           ***  START OF PARAMETER LIST
         MVC   0(12,R15),COMPA40     ***  SAVE RE-ENTRY CODE
         LA    R0,COMPA50            ***  START OF WRITE SUBROUTINE
         ST    R0,12(,R15)           ***  OPERAND OFFSET
         ST    R7,16(,R15)           ***  SAVE GETMAIN ADDRESS
         MVC   4(4,R2),COMPRAST      ***  SET COMPARE FLAGS
         ST    R15,8(,R2)            ***  SAVE REENTRY LIST ADDRESS
         LA    R0,NEWSTAX            ***  ABEND RECOVERY
         ST    R0,RECOVER            ***
         SPACE 2
         XC    PARMLIST(8),PARMLIST       *** INITIALIZE LINK LIST
         LINK  EPLOC=((3)),SF=(E,PARMLIST)
         B     NEWCMD
         SPACE 1
COMPA40  ST    R1,20(,R15)           ***  SAVE PARAMETER ADDRESS
         L     R15,12(,R15)          ***  RE-ENTRY POINT
         BAL   R1,0(,R15)            ***  LINK TO SUBROUTINE
         SPACE 3
COMPA50  SAVE  (14,12)               ***
         LR    R8,R15                ***  BASE REGISTER ADDRESS
         USING COMPA50,R8            ***  BASE REGISTER NOTIFICATION
         L     R7,4(,R1)             ***  WORK AREA ADDRESS
         L     R1,8(,R1)             ***  OUTPUT MESSAGE START
         LM    R9,R12,BASES          ***  RESTORE BASE REGISTERS
         LA    R15,VALSAVE           ***  SAVE AREA FOR THIS ROUTINE
         ST    R13,4(,R15)           ***
         ST    R15,8(,R13)           ***  CHAIN SAVE AREAS
         LR    R13,R15               ***
         MVC   MSGTEXT2+4(80),1(R1)  ***
         LA    R1,79+4               ***  LENGTH
         SLL   R1,16                 ***  CLEAR OUT OTHER BYTES
         ST    R1,MSGTEXT2           ***  SET IN THE MESSAGE
         LA    R1,MSGTEXT2           ***  MESSAGE ADDRESS
        $PUTLINE (R1),ATTN=COMPA58   ***  OUTPUT THIS MESSAGE
         SR    R15,R15               ***  SUCCESSFUL OUTPUT
         B     COMPA59               ***  RETURN
COMPA58  LA    R15,4                 ***  SET REGISTER R15
COMPA59  L     R13,4(,R13)           ***  UNCHAIN SAVE AREAS
         ST    R15,16(R13)           ***  SAVE RETURN CODE
         RETURN (14,12)              ***  RETURN
COMPRAST DC    C'****'
         TITLE 'P D S  --  PDS COMPRESS                        6/10/85'
***********************************************************************
***      COMPRESS SUBCOMMAND   ADDED BY BRUCE LELAND -- APR., 1984  ***
***********************************************************************
*
         SPACE 1
COMPRESS CSECT
         USING *,R8
         SPACE 1
         MVI   VOLALLOC,X'40'             READ THE DSCB AGAIN
         CLI   DSPALLOC,ALLOOLD           EXCLUSIVE USE ALREADY?
         BE    CMXSYSDD                   YES, BRANCH
*  ALLOCATE THE DATA SET TO COMPRESS AS "OLD"
         BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         MVI   DSPALLOC,ALLOOLD           WANT EXCLUSIVE USE NOW
         L     R15,=A(ALLOCATE)           ALLOCATION ROUTINE ADDRESS
         BALR  R14,R15                    ALLOCATE THE DATA SET?
         B     RESTART0                   NO, ERROR
         SPACE 1
         L     R8,##ADRCMD                REESTABLISH THE BASE REGISTER
         CLI   DSPALLOC,ALLOOLD           OLD ALLOCATION NOW?
         BE    CMXSYSDD                   YES, BRANCH
         CLI   #CMXSHR,1                  SHR COMPRESS DESIRED?
         BNE   RESTART2                   NO, QUIT
         SPACE 2
*  ALLOCATE COMPRESS SYSIN FILE
CMXSYSDD XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYSI(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYSI                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(20),CMXSYSIN    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         MVC   #CMXSYSI(8),#CMXSYSI+6     MOVE DOWN THE DATA SET NAME
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   CMXERROR                   NO, BRANCH
         SPACE 3
*  ALLOCATE COMPRESS SYSPRINT DATA SET
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYSP(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYSP                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(4),CMXSYSIN     ASSUME DUMMY TEXT IS DESIRED
         CLI   #CMXFLAG,3                 NOLIST?
         BNE   *+8                        NO, BRANCH
         LA    R5,4(,R5)                  YES, USE THE DUMMY TEXT
         MVC   S99TUPTR+4(40),CMXSYSPR    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),CMXPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         AR    R15,R15                    MAXIMUM MEMBERS*2
         SR    R14,R14
         D     R14,=F'50'                 50 MEMBERS/PAGE
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXSYSP(8),#CMXSYSP+6     MOVE DOWN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   CMXERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COMPRESS SYSUT3 DATA SET
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(24),CMXSYSUT    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),CMXPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         MH    R15,=H'105'
         SR    R14,R14
         D     R14,=F'100'                TOTAL SPACE IN BLOCKS
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXSYS3(8),#CMXSYS3+6     MOVE DOWN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BZ    CMXSTAX                    YES, BRANCH
         SPACE 2
CMXERROR ST    R15,DAIRRC                 SAVE RETURN CODE
         BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         LA    R14,M99RB                  POINTER TO SVC 99 BLOCK
         LA    R15,DAIRRC                 POINTER TO RETURN CODE
         LA    R0,ADDRFF02                POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)           POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL                POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP             INITIALIZE DFDAPLP, DFDRCP,
*                                           DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                    PROBLEM WITH IKJEFF18?
         BZ    CMXRETRN                   NO, BRANCH
         SPACE 1
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         TSMSG L835$1
         B     CMXRETRN
         SPACE 3
CMXSTAX  TSMSG L480                       COMPRESS IN PROGRESS MESSAGE
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         SPACE 1
         XC    WORKTBL,WORKTBL            CLEAR THE TEMPORARY TABLE
         STAX  ATTNNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(PDS480W,PDS480WL),MF=(E,WORKTBL)
         LA    R1,ZERO                    NO FIRST PARAMETER
         ST    R1,#CMXLIST
         LA    R1,#CMXDDNA                SECOND PARAMETER
         ST    R1,#CMXLIST+4
         OI    #CMXLIST+4,X'80'           MARK END OF LIST
         XC    #CMXDDNA(82),#CMXDDNA      SYSIN DDNAME
         MVI   #CMXDDNA+1,80              LENGTH OF DDNAME LIST
         MVC   #CMXDDIN,#CMXSYSI          SYSIN DDNAME
         MVC   #CMXDDPR,#CMXSYSP          SYSPRINT DDNAME
         MVC   #CMXDDT1,DDNAME            SYSUT1 DDNAME
         MVC   #CMXDDT2,DDNAME            SYSUT2 DDNAME
         MVC   #CMXDDT3,#CMXSYS3          SYSUT3 DDNAME
         BAL   R6,RESERVE                 RESERVE DEVICE IF REQUIRED
         XC    MSGTEXT2+40(28+20),MSGTEXT2+40
         LA    R14,MSGTEXT2+40+28         POINT TO FLAGS
         LA    R15,CMXCOPY                POINT TO CALLED PROGRAM
         LA    R0,MSGTEXT2+40+28+4        POINT TO PROGRAM NAME LENGTH
         LA    R1,MSGTEXT2+40+28+8        POINT TO RETURN CODE
         LA    R2,MSGTEXT2+40+28+12       POINT TO REASON CODE
         LA    R3,MSGTEXT2+40+28+16       POINT TO ABEND CODE
         LA    R4,#CMXLIST                POINT TO PARAMETER LIST
         STM   R14,R4,MSGTEXT2+40         UPDATE IKJEFTSR PARM LIST
         OI    MSGTEXT2+40+24,X'80'       INDICATE THE END OF THE LIST
         MVI   MSGTEXT2+40+28+3,X'02'     A PROGRAM IS BEING INVOKED
         MVI   MSGTEXT2+40+28+4+3,8       PROGRAM NAME LENGTH IS 8
         SPACE 1
COMPRZAP LA    R0,3239  **TO BE ZAPPED    PASSWORD
         SVC   247      **TO BE ZAPPED    GET AUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),CMXEFTSR       INVOKE IKJEFTSR?
         BNE   COMPNLNK                   NO, BRANCH
         LA    R1,MSGTEXT2+40             POINT TO THE PARAMETER LIST
         SPACE 2
COMPNLNK BAL   R2,ATTACH                  SPFCOPY/IEBCOPY/$PDSFAST
COMPLINK SR    R0,R0                      UNAUTHORIZATION CODE
         SVC   247      **TO BE ZAPPED    GET UNAUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         SPACE 3
         BAL   R2,DEQ                     RELEASE ANY RESERVES
         BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         CLC   ##SUBCAL(8),CMXEFTSR       INVOKE IKJEFTSR?
         BNE   CMXTSROK                   NO, BRANCH
         MVC   SUBSECB+3(1),MSGTEXT2+40+28+11 COPY FINAL RETURN CODE
         CLI   SUBSECB+3,8                RC:8
         BL    CMXTSROK                   LOW, VALID COMPRESS
         BE    *+12                       EQUAL, ATTENTION
         CLI   SUBSECB+3,X'FF'            RETURN CODE = FF?
         BNE   CMXTSROK                   NO, BRANCH
         TSMSG L980                       ATTENTION WITH IKJEFTSR
         B     CMXRETRN
         SPACE 1
CMXTSROK CLI   SUBSECB+3,12               RETURN CODE > 12?
         BH    CMXNOPEN                   YES, BRANCH
         CLI   #CMXFLAG,3                 NOLIST REQUESTED?
         BNE   CMXOPEN                    NO, BRANCH
CMXNOPEN MVI   MTHIGHL,3
         SR    R15,R15
         ICM   R15,B'0001',SUBSECB+3
         CVD   R15,DOUBLE
         MVC   INSERT#1-1(4),=X'40202120'
         ED    INSERT#1-1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         TSMSG L171$1
         B     CMXRETRN
         SPACE 3
CMXOPEN  MVC   STOWDCB(CMXDL),CMXDCB      GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+STOWDCB(8),#CMXSYSP
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB),MF=(E,OPENLIST)      OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  SUCCESSFUL OPEN?
         BO    CMXMSGIN                       YES, BRANCH
         TSMSG L865                           NO STATUS AVAILABLE
         B     CMXRETRN                       EXIT
         SPACE 3
*  PROCESS SYSPRINT RECORDS
         SPACE 1
CMXMSGIN GET   STOWDCB                    GET LIST RECORD
         CLI   1(R1),X'40'                HEADER LINE?
         BE    CMXMSGIN                   YES, IGNORE
         CLI   #CMXFLAG,2                 FULL LIST REQUESTED?
         BE    CMXMSGON                   YES, OUTPUT
         LA    R3,CMXEXCL-6               START OF EXCLUSION TABLE -6
CMXMSGCK LA    R3,6(,R3)                  NEXT MESSAGE ID
         CLC   1(6,R1),0(R3)              MESSAGE ID TO BE EXCLUDED?
         BE    CMXMSGIN                   YES, IGNORE
         CLI   0(R3),FF                   END OF TABLE?
         BNE   CMXMSGCK                   NO, LOOP
         SPACE 3
CMXMSGON MVC   MSGTEXT1(136),MSGBL132     BLANK THE MESSAGE LINE
         CLC   1(6,R1),CMXIEB14           UNUSED TRACKS MESSAGE?
         BNE   *+8                        NO, BRANCH
         MVI   MSGTEXT1+1,64              YES, TRUNCATE THE MESSAGE
         MVC   MSGTEXT1+4(120),1(R1)      ADD THE IEBCOPY MESSAGE
         MESSAGE MSGTEXT1                 OUTPUT THE MESSAGE
         B     CMXMSGIN
         SPACE 3
CMXEXCL  DC    C'IEB152'           IEB152I MEMBERNAME COMPRESSED-WAS AL
         DC    C'IEB154'           IEB154I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB167'           IEB167I FOLLOWING MEMBER(S) COPIED F
         DC    C'IEB161'           IEB161I COMPRESS TO BE DONE USING IN
         DC    C'PDSFAS'           PDSFAST   ... HEADER LINE
         DC    C'PDF00 '           PDF00  START EXECUTION
         DC    C'PDF06 '           PDF06   INPUT DATASET:  ...
         DC    C'PDF09 '           PDF09  MEMBER ........ - MOVED ...
         DC    X'FF'
         SPACE 2
*** THE FOLLOWING MESSAGES ARE ALSO CANDIDATES FOR SUPPRESSION:
CMXIEB14 DC    C'IEB144'           IEB144I THERE ARE XXXXXXX UNUSED TRA
***      DC    C'IEB147'           IEB147I END OF JOB X WAS HIGHEST RET
***      DC    C'IEB149'           IEB149I THERE ARE XXXXXXX UNUSED DIR
***      DC    C'IEB153'           IEB153I ALL MEMBERS COMPRESSED-ALL W
         DC    X'FF'
         SPACE 2
CMXMDONE DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)  CLOSE THE SYSPRINT FILE.
         SPACE 2
CMXRETRN MESSAGE MSGBLANK
         CLI   #CMXSYSI,X'41'             ANY SYSIN DDNAME?
         BL    CMXFREEP                   NO, BRANCH
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYSI-6              TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),CMXDDNAM       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         OI    S99TUPTR,X'80'             MARK END OF THE TEXT LIST
         MVI   S99TUKEY+1,1               CONVERT TO DDNAME REQUEST
         SPACE 1
         DYNALLOC
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
CMXFREEP CLI   #CMXSYSP,X'41'             ANY SYSPRINT ALLOCATED?
         BL    CMXFREE3                   NO, BRANCH
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYSP-6              TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),CMXDDNAM       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         MVI   S99TUKEY+1,1               CONVERT TO DDNAME REQUEST
         LA    R0,CMXUNALC                UNALLOCATE EVEN IF "PERM"
         ST    R0,S99TUPTR+4              ADD THIS ELEMENT TOO
         OI    S99TUPTR+4,X'80'           MARK END OF THE TEXT LIST
         SPACE 1
         DYNALLOC
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
CMXFREE3 CLI   #CMXSYSP,X'41'             ANY SYSUT3 ALLOCATED?
         BL    RESTART0                   NO, BRANCH
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYS3-6              TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),CMXDDNAM       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         OI    S99TUPTR,X'80'             MARK END OF THE TEXT LIST
         MVI   S99TUKEY+1,1               CONVERT DDNAME REQUEST
         SPACE 1
         DYNALLOC
         SPACE 1
         DROP  R1,R3,R4,R5
         B     RESTART0
         SPACE 3
*  COMPRESS CONSTANTS
         PRINT NOGEN
CMXDCB   DCB   DSORG=PS,DDNAME=CMX,MACRF=GL,EODAD=CMXMDONE
CMXDL    EQU   *-CMXDCB
         PRINT GEN
         SPACE 2
CMXEFTSR DC    CL8'IKJEFTSR'
CMXCOPY  DC    CL8'IEBCOPY'
CMXDDNAM DC    X'0055000100084040404040404040'
CMXDUMMY DC    X'00240000'                       DUMMY DATA SET
CMXBLKSI DC    X'0030000100020050'               DCB=BLKSIZE=80
CMXNEW   DC    X'00040001000104'                 DISP=(NEW)
CMXDEL   DC    X'00050001000104'                 DISP=(,DELETE)
CMXCDEL  DC    X'00060001000104'                 DISP=(,,DELETE)
CMXPERM  DC    X'00520000'                       PERMANENT ALLOCATION
CMXUNALC DC    X'00070000'                       UNALLOCATE PERMANENT
         SPACE 2
         SPACE 2
CMXPRTFI DC    X'000A00010003',AL3(*-*)          SPACE=(,NNN)
CMXPRTSE DC    X'000B00010003',AL3(*-*)          SPACE=(,(,NNN))
CMXPRTIN DC    X'00090001000300181B'             SPACE=(6171)
CMXUT3IN DC    X'000900010003000050'             SPACE=(80)
         SPACE 1
CMXPRTRE DC    X'004900010001',XL1'94'           DCB=RECFM=FBA
CMXPRTLR DC    X'004200010002',AL2(121)          DCB=LRECL=121
CMXPRTBL DC    X'003000010002',AL2(6171)         DCB=BLKSIZE=6171
         SPACE 2
CMXSYSIN DC    A(CMXDUMMY,CMXBLKSI,CMXNEW,CMXDEL),X'80',AL3(CMXCDEL)
CMXSYSUT DC    A(CMXPRTFI,CMXPRTSE,CMXUT3IN)
         DC    A(CMXNEW,CMXDEL),X'80',AL3(CMXCDEL)
CMXSYSPR DC    A(CMXPRTFI,CMXPRTSE,CMXPRTIN)
         DC    A(CMXPRTRE,CMXPRTLR,CMXPRTBL)
         DC    A(CMXNEW,CMXDEL,CMXCDEL),X'80',AL3(CMXPERM)
         TITLE 'P D S  --  PDS CONTROL                         6/10/85'
***********************************************************************
***      CONTROL SUBCOMMAND    ADDED BY BRUCE LELAND -- OCT., 1983  ***
***********************************************************************
*
         SPACE 1
CONTROL  CSECT
         USING *,R8
         AIF ('&CISP' EQ 'NO SPF').NSPF200
         TM    FLAGSEE,FBKGRND               BACKGROUND MODE?
         BO    CONT004                       YES, BRANCH
         TM    FLAGSFF,FSPFOPT6+FSPFERR+FSPFCALL+FSPFDIAL  CHANGE SPF?
         BNZ   CONT004                                     NO, BRANCH
         TM    #OUT+10,X'01'                 SYSOUT/DSNAME?
         BNO   CONT004                       NO, BRANCH
         BAL   R2,SPFRECUR                   INVOKE PDS AS A DIALOG
         SPACE 1
.NSPF200 ANOP
         SPACE 2
CONT004  CLI   #OUT+10,0                     SYSOUT/NOSYSOUT/DSN/NODSN?
         BE    CONT010                       NO, BRANCH
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN?
         BZ    CONT010                       NO, BRANCH
*** CLOSE THE SYSOUT/DSNAME DATA SET
         NI    FLAGSFF,FF-FLOGWRT         TURN OFF RECORD FLAG
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         TSMSG L081
         SPACE 2
         CLI   CONTOPTN,X'03'             DISK LOG OPEN?
         BNE   CONT010                    NO, BRANCH
         LH    R1,DSNLEN                  DSNAME LENGTH
         BCTR  R1,0                       MACHINE LENGTH
         LA    R15,FIRST4K
         CLC   DSNAME(*-*),CONTDSN-FIRST4K(R15)
         EX    R1,*-6                     THIS DATA SET?
         BNE   CONT010                    NO, BRANCH
         SPACE 1
         MVI   ##ADRCM#,CONTINUE          FLAG TO CONTINUE
         MVI   VOLALLOC,X'40'             READ THE DSCB AGAIN
         BAL   R2,CLOSEIT                 CLOSE THE DCB
         B     RESTART2                   OPEN THE DATA SET AGAIN
         SPACE 2
CONT010  MESSAGE MSGBLANK          ONE BLANK LINE
         MVC   INSERT#1(8),PDSNAME
         TSMSG L100$1
         MESSAGE MSGBLANK          ONE BLANK LINE
         SPACE 1
         CLI   #OUT+0,1            ANY CONTROL OPERAND?
         BL    CONT020             NO, BRANCH
         BH    CONT012             YES, THE NO FORM
         NI    FLAGSGG,FF-FALINCON YES FORM
         B     CONT020
CONT012  OI    FLAGSGG,FALINCON    NO FORM
         SPACE 1
CONT020  CLI   #OUT+1,1            ANY CONTROL OPERAND?
         BL    CONT030             NO, BRANCH
         BH    CONT022             YES, THE NO FORM
         NI    FLAGSGG,FF-FTRANCON YES FORM
         B     CONT030
CONT022  OI    FLAGSGG,FTRANCON    NO FORM
         SPACE 1
CONT030  CLI   #OUT+2,1            ANY CONTROL OPERAND?
         BL    CONT040             NO, BRANCH
         BH    CONT032             YES, THE NO FORM
         NI    FLAGSGG,FF-FLKEDCON YES FORM
         B     CONT040
CONT032  OI    FLAGSGG,FLKEDCON    NO FORM
         SPACE 1
CONT040  CLI   #OUT+3,1            ANY CONTROL OPERAND?
         BL    CONT050             NO, BRANCH
         BH    CONT042             YES, THE NO FORM
         NI    FLAGSGG,FF-FPROMCON YES FORM
         B     CONT050
CONT042  OI    FLAGSGG,FPROMCON    NO FORM
         SPACE 1
CONT050  CLI   #OUT+4,1            ANY CONTROL OPERAND?
         BL    CONT060             NO, BRANCH
         BH    CONT052             YES, THE NO FORM
         NI    FLAGSGG,FF-FRECVCON YES FORM
         B     CONT060
CONT052  OI    FLAGSGG,FRECVCON    NO FORM
         SPACE 1
CONT060  CLI   #OUT+5,1            ANY CONTROL OPERAND?
         BL    CONT100             NO, BRANCH
         CLI   #OUT+5,2            ANY CONTROL OPERAND?
         BE    CONT062             YES, THE NO FORM
         NI    FLAGSEE,FF-FDOUBCON-FFULLTRK  YES FORM
         XI    FLAGSEE,FDOUBCON    DOUBLE BUFFER FORM
         CLI   #OUT+5,1            CONTROL DOUBLE?
         BE    CONT100             YES, BRANCH
         XI    FLAGSEE,FDOUBCON    NO, FULLTRACK REQUEST
         B     CONT100
CONT062  OI    FLAGSEE,FDOUBCON+FFULLTRK     NO FORM
         SPACE 2
CONT100  MVI   MTHIGHL,100
         MVC   INSERT#1(128),BLANK128
         LA    R1,INSERT#1-2       START OF GLOBAL TEXT
         SPACE 1
         TM    FLAGSGG,FALINCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CALINCON,R1),CALINCON
         LA    R1,L'CALINCON(R1)
         SPACE 1
         TM    FLAGSGG,FLKEDCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON,R1),CLKEDCON
         LA    R1,L'CLKEDCON(R1)
         SPACE 1
         TM    FLAGSGG,FPROMCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CPROMCON,R1),CPROMCON
         LA    R1,L'CPROMCON(R1)
         SPACE 1
         TM    FLAGSGG,FRECVCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CRECVCON,R1),CRECVCON
         LA    R1,L'CRECVCON(R1)
         SPACE 1
         TM    FLAGSGG,FTRANCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CTRANCON,R1),CTRANCON
         LA    R1,L'CTRANCON(R1)
         SPACE 2
         TM    FLAGSGG,FALINCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CALINCON-2,R1),CALINCON+2
         LA    R1,L'CALINCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FLKEDCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON-2,R1),CLKEDCON+2
         LA    R1,L'CLKEDCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FPROMCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CPROMCON-2,R1),CPROMCON+2
         LA    R1,L'CPROMCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FRECVCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CRECVCON-2,R1),CRECVCON+2
         LA    R1,L'CRECVCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FTRANCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CTRANCON-2,R1),CTRANCON+2
         LA    R1,L'CTRANCON-2(R1)
         SPACE 1
         MVC   0(8,R1),BLANKS      DELETE THE LAST COMMA
         TSMSG L030$1
         SPACE 2
         MVC   INSERT#1(120),BLANK128
         CLI   #OUT+13,X'02'              NODEST?
         BE    *+12                       YES, BRANCH
         CLI   #OUT+15,0                  ANY DEST?
         BE    CONT112                    NO, BRANCH
         MVC   CONTDEST(2),#OUT+14        DEST LENGTH
         MVC   CONTDEST+2(8),#OUT+30      DEST
         SPACE 1
CONT112  CLI   #OUT+12,X'02'              NOFORM?
         BE    *+12                       YES, BRANCH
         CLI   #OUT+17,0                  ANY FORM?
         BE    CONT114                    NO, BRANCH
         MVC   CONTFORM(2),#OUT+16        DEST LENGTH
         MVC   CONTFORM+2(4),#OUT+38      DEST
         SPACE 1
CONT114  CLI   #OUT+10,0                  ANY SWITCHED LOG?
         BE    CONT116                    NO, BRANCH
         MVC   CONTOPTN(1),#OUT+10        CURRENT OPTION
         CLI   CONTOPTN,X'02'             NOSYSOUT?
         BNE   *+8                        NO, BRANCH
         MVI   CONTOPTN,0                 YES, CONVERT TO NULL
         CLI   CONTOPTN,X'04'             NODSNAME?
         BNE   *+8                        NO, BRANCH
         MVI   CONTOPTN,0                 YES, CONVERT TO NULL
         SPACE 2
CONT116  CLI   CONTOPTN,X'03'             DSNAME?
         BNE   CONT124                    NO, BRANCH
         LA    R14,FIRST4K                BASE REGISTER
         LA    R14,CONTDSN-FIRST4K(,R14)  POINT TO THE CONTDSN
         CLI   #OUT+19,0                  ANY DSNAME?
         BE    CONT120                    NO, USE PREVIOUS
         MVC   0(60,R14),BLANK128         BLANK CONTDSN
         MVC   0(44,R14),#OUT+44          DSNAME
         LH    R1,#OUT+18                 DSNAME LENGTH
         LA    R15,0(R1,R14)              POINT TO START OF MEMBER
         CLI   #OUT+21,0                  ANY MEMBER?
         BE    CONT118                    NO, BRANCH
         MVI   0(R15),C'('                BEGINNING (
         MVC   1(8,R15),#OUT+88           MEMBER NAME
         AH    R15,#OUT+20                MEMBER LENGTH
         MVI   1(R15),C')'                ENDING )
         LA    R15,2(,R15)                CURRENT LENGTH OF NAME
         SPACE 1
CONT118  MVI   0(R15),C')'                TO MATCH DSN( ...
         MVC   2(3,R15),CLOGSHR           SHR
         CLI   #OUT+11,X'02'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGMOD           MOD
         CLI   #OUT+11,X'03'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGNEW           NEW
         CLI   #OUT+11,X'04'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGOLD           OLD
         SPACE 1
CONT120  MVC   INSERT#1(7),CLOGDSN        DSNAME KEYWORD
         MVC   INSERT#1+7(60),0(R14)
         B     CONT129
         SPACE 2
CONT124  LA    R15,INSERT#1
         CLI   CONTOPTN,0                 ANY CURRENT OPTIONS?
         BH    CONT126                    YES, BRANCH
         MVC   0(9,R15),CLOGNDSN          ADD NODSNAME,
         LA    R15,10(,R15)
         MVC   0(9,R15),CLOGNSYS          ADD NOSYSOUT,
         LA    R15,10(,R15)
         B     CONT127
         SPACE 1
CONT126  MVC   0(7,R15),CLOGSYS           ADD SYSOUT(
         MVC   7(1,R15),CONTSYS           ADD SYSOUT CLASS
         MVC   8(2,R15),CLOGCOMM          ADD ),
         LA    R15,11(,R15)
         SPACE 1
CONT127  MVC   0(7,R15),CLOGNFOR          ADD NOFORM,
         CLI   CONTFORM+1,0               ANY FORM?
         BNE   *+12                       YES, BRANCH
         LA    R15,8(,R15)
         B     CONT128
         MVC   0(5,R15),CLOGFOR           ADD FORM(
         MVC   5(4,R15),CONTFORM+2        ADD FORM NAME
         LH    R1,CONTFORM                LENGTH
         LA    R15,5(R1,R15)
         MVC   0(2,R15),CLOGCOMM          ADD ),
         LA    R15,3(R15)
         SPACE 1
CONT128  MVC   0(6,R15),CLOGNDES          ADD NODEST,
         CLI   CONTDEST+1,0               ANY DEST?
         BE    CONT129                    NO, BRANCH
         MVC   0(5,R15),CLOGDES           ADD DEST(
         MVC   5(8,R15),CONTDEST+2        ADD DEST NAME
         LH    R1,CONTDEST                LENGTH
         LA    R15,5(R1,R15)
         MVI   0(R15),C')'                ADD )
         SPACE 2
CONT129  DS    0H
         TSMSG L030$1
         MVC   INSERT#1(120),BLANK128
         MVC   INSERT#1(8),CSINGCON       ASSUME SINGLE BUFFERING
         TM    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL SINGLE?
         BO    CONT130                    YES, BRANCH
         MVC   INSERT#1(8),CDOUBCON       ASSUME DOUBLE BUFFERING
         TM    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL DOUBLE?
         BM    CONT130                    YES, BRANCH
         MVC   INSERT#1(8),CMULTCON       READ MULTIPLE
         SPACE 2
CONT130  MVI   MTHIGHL,8
         TSMSG L031$1
         MESSAGE MSGBLANK                 ONE BLANK LINE
         SPACE 2
CONT134  TM    CONTOPTN,1                    ANY LOG RECORDING?
         BNO   CONT200                       NO, BRANCH
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN ALREADY
         BO    CONT200                       YES, BRANCH
***  ALLOCATE THE SYSOUT/DATA SET
         NI    FLAGSFF,FF-FLOGWRT            TURN OFF RECORD FLAG
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'CONDDNAM,R3),CONDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDSORG,R3),CONDSORG         SPECIFY DSORG TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDSORG(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFREE,R3),CONFREE           SPECIFY FREE TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFREE(,R3)                 START OF NEXT TEXT
         SPACE 1
CONT140  CLI   CONTOPTN,X'03'                    DSNAME ALLOCATION?
         BNE   CONT150                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDSNAM,R3),CONDSNAM         SPECIFY DSNAME TEXT
         MVC   6(44,R3),#OUT+44                  SPECIFY DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+88,X'41'                     ANY MEMBER?
         BL    CONT141                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONMEMBR,R3),CONMEMBR         SPECIFY MEMBER TEXT
         MVC   6(8,R3),#OUT+88                   SPECIFY MEMBER
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONMEMBR(,R3)                START OF NEXT TEXT
         SPACE 1
CONT141  CLI   #OUT+96,X'41'                     ANY PASSWORD?
         BL    CONT142                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONPASSW,R3),CONPASSW         SPECIFY PASSWORD TEXT
         MVC   6(8,R3),#OUT+96                   SPECIFY PASSWORD
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONPASSW(,R3)                START OF NEXT TEXT
         SPACE 1
CONT142  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONALLOC,R3),CONALLOC         SPECIFY DISP TEXT
         CLI   #OUT+11,2                         SHR?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONOSHR                     SPECIFY DISPOSITION
         CLI   #OUT+11,3                         MOD?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONOMOD                     SPECIFY DISPOSITION
         CLI   #OUT+11,4                         NEW?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONONEW                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+11,3                         MOD OR NEW?
         BL    CONT149                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFIRST,R3),CONFIRST         SPECIFY PRIMARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFIRST(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONSECON,R3),CONSECON         SPECIFY SECONDARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONSECON(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONTRK,R3),CONTRK             SPECIFY TRACK ALLOC
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONTRK(,R3)                  START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+88,X'41'                     ANY MEMBER NAME?
         BL    CONT149                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDIR,R3),CONDIR             SPECIFY DIRECTORY BLKS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDIR(,R3)                  START OF NEXT TEXT
         SPACE 1
CONT149  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONNORM,R3),CONNORM           SPECIFY DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONANORM,R3),CONANORM         SPECIFY DISP=(,,KEEP)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         B     CONT159
         SPACE 1
CONT150  CLI   CONTFORM+1,0                      ANY FORM?
         BZ    CONT152                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFORMS,R3),CONFORMS         SPECIFY FORMS TEXT
         MVC   6(4,R3),CONTFORM+2                SPECIFY FORM NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFORMS(,R3)                START OF NEXT TEXT
         SPACE 1
CONT152  CLI   CONTDEST+1,0                      ANY DEST?
         BZ    CONT154                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDESTS,R3),CONDESTS         SPECIFY DEST TEXT
         MVC   6(8,R3),CONTDEST+2                SPECIFY DESTINATION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDESTS(,R3)                START OF NEXT TEXT
         SPACE 1
CONT154  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONSYSOU,R3),CONSYSOU         SPECIFY SYSOUT TEXT
         MVC   6(1,R3),CONTSYS                   SPECIFY SYSOUT CLASS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SPACE 1
CONT159  OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   DDNAMEH,WORKTBL+6        SAVE DDNAME
         LTR   R15,R15                  SUCCESSFUL?
         BZ    CONT164                  YES, BRANCH
         SPACE 2
         LA    R14,M99RB                POINTER TO SVC 99 BLOCK
         ST    R15,DAIRRC               RETURN CODE
         LA    R15,DAIRRC               POINTER TO RETURN CODE
         LA    R0,ADDRFF02              POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)         POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL              POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP           INITIALIZE DFDAPLP, DFDRCP,
*                                         DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                  PROBLEM WITH IKJEFF18?
         BZ    CONT162                  NO, BRANCH
         SPACE 2
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         TSMSG L835$1
         SPACE 1
CONT162  MVI   CONTOPTN,0               TURN OFF LOGGING
         NI    FLAGSFF,FF-FLOGWRT       TURN OFF LOGGING
         B     CONT200
         SPACE 2
CONT164  CLI   CONTOPTN,X'03'           DSNAME ALLOCATION?
         BNE   CONT180                  NO, BRANCH
         LA    R1,L781$1                ASSUME A LOGICAL ERROR
         CLI   WORKTBL+14+6,DS1DSGPO    PARTITIONED?
         BNE   CONT166                  NO, BRANCH
         CLI   #OUT+88,X'41'            ANY MEMBER NAME?
         BL    CONT190                  NO, ERROR
         B     CONT180
         SPACE 1
CONT166  CLI   WORKTBL+14+6,DS1DSGPS    SEQUENTIAL?
         BNE   CONT190                  NO, ERROR
         CLI   #OUT+88,X'40'            ANY MEMBER NAME?
         BH    CONT190                  YES, ERROR
         SPACE 2
*** OPEN THE SYSOUT/DATA SET
CONT180  MVC   LOGDCB(LLOGDCB),LOGSDCB       CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+LOGDCB(8),DDNAMEH
         MVI   OPENLIST,X'80'
         OPEN  (LOGDCB,(OUTPUT)),MF=(E,OPENLIST)
         LA    R1,L780$1
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN ALREADY
         BNO   CONT190                       NO, ERROR
         TM    FLAGSFF,FLOGWRT               OPEN WITH GOOD VALUES?
         BO    CONT200                       YES, BRANCH
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         LA    R1,L781$1                     ERROR MESSAGE
CONT190  MVI   CONTOPTN,0                    TURN OFF ANY LOGGING
         MVC   INSERT#1(8),CTEXTLOG          LOGCOPY
         B     MSGNEW
         SPACE 2
CONT200  CLI   #OUT+7,1            CPULOOP?
         BNE   CONT300             NO, BRANCH
         TSMSG L032                YES, LOOP MESSAGE
         B     *                   LOOP
         SPACE 1
CONT300  CLI   #OUT+7,2            ABEND?
         BNE   CONT400             NO, BRANCH
         TSMSG L033                YES, ABEND MESSAGE
         B     ABEND0C1            ABEND 0C1 AT +2C
         SPACE 1
CONT400  CLI   #OUT+7,3            OUTLOOP?
         BNE   CONT500             NO, BRANCH
         TSMSG L034                YES, LOOP MESSAGE
         B     CONT400             LOOP
         SPACE 1
CONT500  CLI   #OUT+7,4            MESSAGES?
         BNE   CONT600             NO, BRANCH
         MVC   INSERT#1(8),CTEXTP1
         MVC   INSERT#2(8),CTEXTP2
         LA    R4,L000             FIRST INFORMATIONAL MESSAGE
CONT510  LR    R1,R4               POINT TO IT
         TSMSG (R1)                OUTPUT IT
         LA    R4,3(,R4)           NEXT MESSAGE
         CLI   0(R4),C'3'          DONE?
         BNH   CONT510             NO, BRANCH
         LA    R5,CONLIST          FIRST ACTION MESSAGE
CONT520  L     R1,0(,R5)           POINT TO IT
         MESSAGE (R1)              OUTPUT IT
         LA    R5,4(,R5)           NEXT MESSAGE
         CLI   0(R5),X'FF'         DONE?
         BNE   CONT520             NO, BRANCH
CONT530  LR    R1,R4               NEXT WARNING/ERROR MESSAGE
         TSMSG (R1)                OUTPUT IT
         LA    R4,3(,R4)           NEXT MESSAGE
         CLI   0(R4),C'9'          DONE?
         BNH   CONT530             NO, BRANCH
         SPACE 1
CONT600  CLI   #OUT+7,5            IOSTATS DESIRED?
         BNE   CONT700             NO, BRANCH
         LH    R1,IODONE           ** INPUT ROUTINE ENTRIES
         LA    R14,MSGCOA1
         BAL   R2,CONT900
         LH    R1,IODONEW          ** NUMBER OF TTR CHANGES
         LA    R14,MSGCOA2
         BAL   R2,CONT900
         LH    R1,IOLOGUSE         ** LOGICAL INPUTS PERFORMED
         LA    R14,MSGCOA3
         BAL   R2,CONT900
         LH    R1,IOPHYTRK         ** PHYSICAL TRACKS READ
         LA    R14,MSGCOA4
         BAL   R2,CONT900
         XC    IODONE(8),IODONE
         SPACE 1
CONT700  CLI   #OUT+7,6            TESTREAD?
         BNE   NEWCMD              NO, BRANCH
         MVC   #OUT+20(1),FLAGSEE         SAVE THE INPUT FLAGS
         MESSAGE MSGBLANK                 ONE BLANK LINE
         MVC   INSERT#1(8),CSINGCON       SINGLE BUFFERING
         TSMSG L031$1
         OI    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL SINGLE
         BAL   R2,CONT710                 READ THE FIRST RECORD
         SPACE 1
         MVC   INSERT#1(8),CDOUBCON       DOUBLE BUFFERING
         TSMSG L031$1
         XI    FLAGSEE,FDOUBCON           CONTROL DOUBLE
         BAL   R2,CONT710                 READ THE FIRST RECORD
         SPACE 1
         MVC   INSERT#1(8),CMULTCON       DOUBLE BUFFERING
         TSMSG L031$1
         NI    FLAGSEE,FF-FFULLTRK-FDOUBCON
         BAL   R2,CONT710                 READ THE FIRST RECORD
         MVC   FLAGSEE(1),#OUT+20         RESET THE INPUT FLAGS
         B     NEWCMD
         SPACE 2
CONT710  MVI   STARTTR+2,1                SET TTR TO 1
         L     R15,=A(EXCP)               INPUT IT
         BALR  R14,R15
         CVD   R15,DOUBLE                 CONVERT RETURN CODE
         MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1-2(4),=X'40212020' EDIT CHARACTERS
         ED    INSERT#1-2(4),DOUBLE+6     CONVERT TO DISPLAY
         STM   R2,R12,28(R13)             CONVERT CCHHR TO TTR
         LA    R2,IOBSEEK                 NEXT MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,FULLWORD                TTR OF CURRENT DIRECTORY EOF
         UNPK  INSERT#2(7),FULLWORD(4)
         TR    INSERT#2(6),TRTABLE
         MVC   INSERT#2+6(2),BLANKS
         TSMSG L035$2
         MESSAGE MSGBLANK                 ONE BLANK LINE
         BR    R2
         SPACE 2
CONT900  MVC   MSGTEXT1,0(R14)
         MVC   MSGTEXT1+4(7),MSGCOAM
         LTR   R1,R1
         BZR   R2
         CVD   R1,DOUBLE
         ED    MSGTEXT1+4(7),DOUBLE+5
         MESSAGE MSGTEXT1
         BR    R2
         SPACE 4
         USING IHADCB,R1
*** DCB OPEN EXIT
CONT980  CLC   DCBDSORG(2),ZERO              ANY DSORG?
         BNE   *+8                           YES, BRANCH
         OI    DCBDSORG,DS1DSGPS             NO, USE DSORG=PS
         TM    DCBDSORG,DS1DSGPS             DSORG=PS?
         BNOR  R14                           NO, ERROR
         CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+8                           YES, BRANCH
         MVI   DCBRECFM,DCBRECF+DCBRECBR     NO, USE RECFM=FB
         TM    DCBRECFM,DCBRECF+DCBRECV      RECFM=F. OR RECFM=V.?
         BNMR  R14                           NO, ERROR
         LA    R0,80                         DEFAULT LRECL
         TM    DCBRECFM,DCBRECV              RECFM=V.?
         BNO   *+8                           NO, BRANCH
         LA    R0,255                        DEFAULT LRECL FOR VARIABLE
         CLC   DCBLRECL(2),ZERO              ANY LRECL?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBLRECL                   NO, USE LRECL=80
         TM    DCBRECFM,DCBRECV              RECFM=V.?
         BO    CONT988                       YES, BRANCH
         CH    R0,DCBLRECL                   LRECL=80?
         BNER  R14                           NO, ERROR
CONT988  LA    R0,3120                       DEFAULT BLKSIZE
         CLC   DCBBLKSI(2),ZERO              ANY BLKSIZE?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBBLKSI                   NO, USE BLKSIZE=3120
         OI    FLAGSFF,FLOGWRT               ALL ATTRIBUTES ARE CORRECT
         BR    R14
         DROP  R1
         SPACE 2
         PRINT NOGEN
LOGSDCB  DCB   DSORG=PS,DDNAME=PDSLOG,MACRF=(PM),EXLST=CONT990
LLOGDCB  EQU   *-LOGSDCB
         PRINT GEN
         SPACE 2
CONT990  DC    0F'0',X'85',AL3(CONT980)      OPEN EXIT ONLY
         PRINT NOGEN
MSGCOA1  MSG   ' 12,345 INPUT ROUTINE ENTRIES'
         ORG   MSGCOA1+4
MSGCOAM  DC    X'4020206B202120'
         ORG   ,
MSGCOA2  MSG   ' 12,345 TTR CHANGES'
MSGCOA3  MSG   ' 12,345 LOGICAL INPUTS PERFORMED'
MSGCOA4  MSG   ' 12,345 PHYSICAL TRACKS READ'
         PRINT GEN
CALINCON DC    C'NOALIASINFO, '
CLKEDCON DC    C'NOLKEDDATE, '
CPROMCON DC    C'NOPROMPT, '
CRECVCON DC    C'NORECOVER, '
CTRANCON DC    C'NOTRANSLATOR, '
CSINGCON DC    CL8'SINGLE'
CDOUBCON DC    CL8'DOUBLE'
CMULTCON DC    CL8'MULTIPLE'
CTEXTP1  DC    CL8'<PARM#1>'
CTEXTP2  DC    CL8'<PARM#2>'
CTEXTLOG DC    CL8'LOGCOPY'
CLOGOLD  DC    CL3'OLD'
CLOGMOD  DC    CL3'MOD'
CLOGSHR  DC    CL3'SHR'
CLOGNEW  DC    CL3'NEW'
CLOGDSN  DC    CL7'DSNAME('
CLOGNDSN DC    CL9'NODSNAME,'
CLOGSYS  DC    CL7'SYSOUT('
CLOGNSYS DC    CL9'NOSYSOUT,'
CLOGFOR  DC    CL5'FORM('
CLOGNFOR DC    CL7'NOFORM,'
CLOGDES  DC    CL5'DEST('
CLOGNDES DC    CL6'NODEST'
CLOGCOMM DC    CL2'),'
CONLIST  DC    A(PDS300A)
         DC    A(PDS380A,PDS381A,PDS382A,PDS383A,PDS384A,PDS385A)
         DC    A(PDS386A,PDS390A,PDS391A,PDS392A,PDS393A,PDS394A),X'FF'
         SPACE 2
CONDDNAM DC    X'0055000100084040404040404040'     RETURN DDNAME
CONDSORG DC    X'0057000100024040'                 RETURN DSORG
CONFREE  DC    X'001C0000'                         FREE=CLOSE
CONMEMBR DC    X'0003000100084040404040404040'     DSNAME
CONPASSW DC    X'0050000100084040404040404040'     PASSWORD
CONDSNAM DC    X'00020001002C'                     DSNAME
CONALLOC DC    X'00040001000101'                   DISP=(_)
CONNORM  DC    X'00050001000102'                   DISP=(,CATLG)
CONANORM DC    X'00060001000108'                   DISP=(,,KEEP)
CONTRK   DC    X'00070000'                         SPACE=(TRK,
CONFIRST DC    X'000A00010003000001'               SPACE=(,1
CONSECON DC    X'000B00010003000004'               SPACE=(,,4
CONDIR   DC    X'000C00010003000005'               SPACE=(,,,5)
CONOOLD  EQU   X'01'
CONOMOD  EQU   X'02'
CONONEW  EQU   X'04'
CONOSHR  EQU   X'08'
CONSYSOU DC    X'001800010001C1'                   SYSOUT=A
CONFORMS DC    X'001A00010004C4F1F0F1'             SYSOUT=(,,D101)
CONDESTS DC    X'005800010008D5F1F3F040404040'     DEST=R130
         TITLE 'P D S  --  PDS DELETE, MEMBERS                 6/10/85'
***********************************************************************
***    DELETE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- OCT., 1983  ***
***                                                                 ***
***    MEMBERS SUBCOMMAND   MODIFIED BY BRUCE LELAND -- JUNE, 1985  ***
***********************************************************************
*
         SPACE 1
DELETE   CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO            CORRECT?
         BZ    MSGNEW                    YES, BRANCH
         SPACE 2
         CLC   ##SUBCOM(8),$MEM          MEMBERS SUBCOMMAND?
         BNE   *+8                       NO, BRANCH
         OI    #MEMFLAG,X'80'            YES, SET A FLAG
         SPACE 2
         NI    FLAGSCC,FF-FBLDLOK        BLDL IS INVALIDATED
         TM    FLAGSII,FSINGLE+FMEMBERS  IF OR FIND THEN(DEL/MEM)?
         BZ    DELE010                   NO, BRANCH
         TM    #MEMFLAG,X'80'            MEMBERS?
         BO    NEWCMD                    YES, DONE
         MVC   IODIR1+4+12+00(8),CURDIR  RESET READ ADDRESS
         MVI   IODIR1+4+12+08,X'08'      RESET KEY LENGTH
         MVC   IODIR1+4+12+09(2),=H'256' RESET DATA LENGTH
         MVI   IODIR1+4+12+11,0          RESET SECTOR ADDRESS TO ZERO
         MVI   IOECB,X'7F'               LAST READ WAS OK
         LA    R4,DIRNAME                DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)                POSITION FOR ONE ENTRY
         B     DELE100                   PROMPT BY DEFAULT
         SPACE 1
DELE010  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    DELE200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE200               YES, BRANCH
         OI    #DELFLAG,#DELONE      ONLY A SINGLE MEMBER
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATES TOO?
         BO    DELE200               YES, BRANCH
         SPACE 1
         LA    R4,DIRNAME            DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         SPACE 1
         TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BNO   DELE090               NO, BRANCH
         TM    FLAGSAA,FMEMSTAR      ASTERISK NOTATION?
         BNO   NEWCMD                NO, BRANCH
         MVC   INSERT#1(8),MEMBER1
         TSMSG L002$1                WHO IS BEING PROCESSED
         B     NEWCMD
         SPACE 2
DELE090  TM    FLAGSAA,FMEMSTAR      ASTERISK NOTATION?
         BO    DELE110               YES, NO PROMPTING
         TM    FLAGSAA,FOPTIONS      ANY MEMBER NAME?
         BO    DELE110               YES, NO PROMPTING
         SPACE 2
DELE100  LA    R1,PDS393A            DELETE SINGLE MEMBER
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO -- DONE
         SPACE 1
DELE110  MVC   DSNMEMQ(8),DIRNAME    MEMBER NAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         B     NEWCMD                YES, IGNORE THE COMMAND
         B     DELE900               NO, DELETE IT
         SPACE 3
DELE200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
         LA    R1,80                 ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON        ISPMODE ACTIVE?
         BO    DELE202               YES, BRANCH
         TM    CONTOPTN,1            ANY LOG RECORDING?
         BO    DELE202               YES, BRANCH
         GTSIZE ,                    TERMINAL SIZE
         CH    R1,=H'120'            120 OR LESS BYTES?
         BL    *+8                   YES, BRANCH
         LH    R1,=H'120'            NO, USE 120 BYTES
DELE202  SH    R1,=H'7'              LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE           CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGDELET),MSGDELET
         LA    R6,MSGLINE+4+L'MSGDELET
         MVC   FULLWORD(3),L162$1    MEMBERS TO BE DELETED
         NI    FLAGSBB,FF-FLINESET   NO LINE IN PROGRESS
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BNO   DELE210               NO, BRANCH
         MVC   MSGLINE+4(L'MSGMEMBS),MSGMEMBS
         LA    R6,MSGLINE+4+L'MSGMEMBS
         MVC   FULLWORD(3),L165$1    MEMBERS ARE:
         SPACE 1
DELE210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   DELE220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     DELE500
         SPACE 1
DELE220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
DELE300  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   DELE500               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   DELE605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     DELE315                   00 - SUCCESSFUL
         B     DELE310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
DELE310  MVC   INSERT#1(8),DIRNAME
         TSMSG L853$1                MSG - (MEMBER) NOT FOUND
         B     DELE300
         SPACE 1
DELE315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     DELE500
         SPACE 1
DELE319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
DELE320  BAL   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     DELE600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE400               YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMRANG      MEMBER NAME RANGE?
         BNO   DELE340               NO, BRANCH
         TM    FLAGSAA,FMEMBER1      START ENTRY SPECIFIED?
         BNO   DELE330               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER1          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               ABOVE THIS VALUE?
         BH    DELE300               YES, BRANCH
         XI    FLAGSAA,FMEMBER1      FIRST NAME IS SATISFIED
         SPACE 2
DELE330  TM    FLAGSAA,FMEMBER2      END ENTRY SPECIFIED?
         BNO   DELE500               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER2(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               BELOW THIS VALUE?
         BL    DELE600               YES, BRANCH
         B     DELE500
         SPACE 3
DELE340  LH    R15,LMEMBER1          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER1    <<EXECUTED>>
DELE350  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DELE360               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DELE350            MAYBE, CHECK ALL OTHER POSITIONS
         B     DELE300               NO, IGNORE THIS ENTRY
         SPACE 2
DELE360  TM    FLAGSAA,FMEMBER2      A SECOND PATTERN TOO?
         BNO   DELE500               NO, USE THIS ENTRY
         LH    R15,LMEMBER2          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER2    <<EXECUTED>>
DELE370  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DELE500               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DELE370            MAYBE, CHECK ALL OTHER POSITIONS
         B     DELE300               NO, IGNORE THIS ENTRY
         SPACE 3
DELE400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE410+4             SKIP FIRST ADD
DELE410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   DELE410               NO, BRANCH
         SPACE 3
         L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE430+4             SKIP FIRST ADD
DELE430  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE500               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    DELE300               YES, IGNORE THIS ENTRY
         B     DELE430               NO, CONTINUE SEARCHING
         SPACE 3
DELE500  TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BO    DELE505               YES, BRANCH
         MVC   DSNMEMQ(8),MEMNAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         OI    #DELFLAG,#DELENQ      YES, MARK TO EXIT LATER
         SPACE 1
DELE505  LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET      LINE IN PROGRESS?
         BO    DELE510               YES, BRANCH
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
DELE510  CLI   0(R2),X'40'           SCAN
         BNE   *+8                       FOR LAST
         BCT   R2,DELE510                        NON-BLANK
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE          FIT ON THIS OUTPUT LINE?
         BNH   DELE520               YES, BRANCH
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R6,MSGLINE+3          INDENT FOR BLANKS
         SPACE 1
DELE520  MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE        MAKE PRINTABLE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','            ADD A COMMA
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   DELE210               NO, BRANCH
         TM    #DELFLAG,#DELONE      A SINGLE MEMBER?
         BNO   DELE300               NO, BRANCH
         SPACE 3
DELE600  TM    #DELFLAG,#DELASSP     ASSOCIATED MEMBERS?
         BO    DELE605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE300               YES, BRANCH
         SPACE 1
DELE605  TM    FLAGSBB,FLINESET      ANY MEMBERS FOUND?
         BNO   DELE610               NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MESSAGE MSGBLANK
         SPACE 1
DELE610  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE STARTED?
         BO    DELE700               YES, BRANCH
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATE MEMBERS TOO?
         BNO   DELE700               NO, BRANCH
         OI    #DELFLAG,#DELASSP     YES, ASSOCIATES PHASE STARTED
         NI    #DELFLAG,FF-#DELONE   WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         NI    FLAGSBB,FF-FLINESET   NEW OUTPUT LINE
         MVC   MSGLINE+4(L'MSGDELAS),MSGDELAS
         LA    R6,MSGLINE+4+L'MSGDELAS
         MVC   FULLWORD(3),L163$1    MEMBERS TO BE DELETED
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     DELE300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
DELE700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         TM    #DELFLAG,#DELENQ      ANY MEMBERS IN USE?
         BO    NEWCMD                YES, BRANCH
         SPACE 1
         TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BO    NEWCMD                YES, BRANCH
         SPACE 1
         LA    R1,PDS394A            DELETE PROMPT MESSAGE
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO, QUIT
         SPACE 2
DELE900  BAL   R2,OPENSTOW           OPEN STOW DCB; ENQUEUE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 1
         AGO   .TESTORD              ***TEST MODIFY DELETE ORDER
         L     R3,#MEMPTR            START OF MEMBER LIST
         B     DELE910+4             SKIP FIRST ADD
DELE910  LA    R3,16(,R3)
         CR    R3,R4
         BNL   DELE980
.TESTORD ANOP
         LR    R3,R4                 END OF MEMBER LIST
         LA    R6,16                 DECREMENT AMOUNT
DELE910  SR    R3,R6
         CLM   R3,B'0111',#MEMPTR+1
         BL    DELE980
         STOW  STOWDCB,0(R3),D       ISSUE STOW WITH DELETE OPTION
         SPACE 1
         B     *+4(R15)              PROCESS RETURN CODE
         B     DELE940                 00 - SUCCESSFUL
         EX    0,*                     04 - SHOULD NOT OCCUR
         B     DELE960                 08 - MEMBER NOT FOUND
         B     IOERROR                 12 - I/O ERROR IN DIRECTORY
         SPACE 1
DELE940  MVC   INSERT#1(8),0(R3)
         TSMSG L040$1
         B     DELE910
         SPACE 1
DELE960  MVC   INSERT#1(8),0(R3)
         TSMSG L853$1
         B     DELE910
         SPACE 1
DELE980  BAL   R2,SHUTSTOW            CLOSE FOR: IF * THEN(DELETE)
         B     NEWCMD
         SPACE 2
#DELASSP EQU     X'80'            DELETE: ASSOCIATES PHASE OF DELETE
#DELONE  EQU     X'10'            DELETE: DELETE A SINGLE MEMBER
#DELENQ  EQU     X'08'            DELETE: A DELETE MEMBER IS "IN USE"
#DELASS2 EQU     X'01'            DELETE: DELETE ASSOCIATES TOO
#MEMBLKE EQU     X'08'            BLKSIZE TOO LARGE
#MEMDIVE EQU     X'04'            BLKSIZE/LRECL NOT INTEGRAL
#MEMLRLE EQU     X'02'            LRECL TOO LARGE
#MEMLRL0 EQU     X'01'            LRECL NEGATIVE OR ZERO
MSGDELET DC    C'MEMBERS TO BE DELETED ARE:'
MSGMEMBS DC    C'MEMBERS ARE:'
MSGDELAS DC    C'ASSOCIATED MEMBERS TO BE DELETED ARE:'
         TITLE 'P D S  --  PDS DSNAME                          6/10/85'
***********************************************************************
***      DSNAME  SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
DSNAMES  CSECT
         BALR  R6,0
         USING *,R6
         MVC   INSERT#1(80),BLANK128    BLANK THE FIRST LINE
         MVI   MTHIGHL,80               80 CHARACTER MESSAGE
         TM    FLAGSJJ,FDSNMSG          MSG ALLOCATION?
         BO    DSNMSG                   YES, BRANCH
         TM    FLAGSJJ,FDSNTSO          TSO ALLOCATION?
         BO    DSNTSO                   YES, BRANCH
         SPACE 1
DSNJCL   MVC   INSERT#1(20),DSNJDSN     ADD "//12345678  DD  DSN="
         MVC   INSERT#1+2(8),DDNAME     MOVE IN THE DDNAME
         LA    R1,INSERT#1+20           DSNAME START POSITION
         MVC   0(44,R1),DSNAME          MOVE IN THE DSNAME
         AH    R1,DSNLEN                LENGTH OF THE DSNAME
         MVC   0(15,R1),DSNJDSND        ADD ",DISP=SHR,UNIT="
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   6(3,R1),=C'OLD'          NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         LA    R1,15(,R1)               START OF UNIT DATA
         MVC   0(8,R1),1(R15)           ADD TO THE MESSAGE
         MVI   8(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R1),C','               ADD A TERMINATOR AND CONTINUE
         TSMSG L220$1
         MVC   INSERT#1(80),BLANK128    BLANK THE SECOND LINE
         MVC   INSERT#1(20),DSNJDCT     "// DCB=(RECFM=, ..."
         IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+15           MOVE INTO MESSAGE
         LA    R1,INSERT#1+16
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    DSN010                   YES, NO LRECL
         MVC   0(7,R1),DSNJDCTL         MOVE IN LRECL TEXT
         LA    R1,7(R1)
         LH    R4,LABLRECL
         BAL   R14,DSNCONV
         SPACE 1
DSN010   MVC   0(9,R1),DSNJDCTB
         LA    R1,9(R1)
         LH    R4,BLKSI
         BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0001',DS1KEYL       ANY KEY LENGTH?
         BZ    DSN020                   NO, BRANCH
         MVC   0(8,R1),DSNJDCTK         ADD ,KEYLEN=
         LA    R1,8(R1)
         BAL   R14,DSNCONV
         SPACE 1
         MVC   0(5,R1),DSNJDCTR         ADD ,RKP=
         LA    R1,5(R1)
         SR    R4,R4
         ICM   R4,B'0011',DS1RKP        RELATIVE KEY POSITION
         BAL   R14,DSNCONV
         SPACE 1
DSN020   TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN024                   NO, BRANCH
         MVC   0(7,R1),DSNJDCTO         ADD ,OPTCD=
         LA    R1,7(R1)
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
         SPACE 1
DSN024   TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN030                   NO, BRANCH
         MVC   0(7,R1),DSNJDCTD         ADD ,DSORG=
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   7(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   7(2,R1),DSNLVS
         LA    R1,9(,R1)                SKIP OVER DSORG TEXT
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN030                   NO, BRANCH
         MVI   0(R1),C'U'               ADD U
         LA    R1,1(,R1)                SKIP OVER U
         SPACE 1
DSN030   MVC   0(10,R1),DSNJDCTV        ADD ),VOL=SER=
         MVC   10(6,R1),VOLALLOC        ADD THE SERIAL NUMBER
         MVI   10+6(R1),C','
         TSMSG L220$1
         SPACE 1
         MVC   INSERT#1(80),DSNJSPA     //  SPACE= ...
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         LA    R1,INSERT#1+DSNJSPAP     PRIMARY SPACE
         LH    R4,DSNTOTAL              TOTAL FREE SPACE
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   DSN050                   NO, BRANCH
         MVC   INSERT#1+11(3),=C'CYL'   YES, MARK AS CYLINDER ALLOC.
         LH    R0,DEVTTRKS              TRACKS/CYLINDER
         LR    R5,R0
         BCTR  R5,0                     TRACKS/CYLINDER -1
         AR    R5,R4                    TOTAL+TRK/CYL -1
         SR    R4,R4
         DR    R4,R0                    CEIL(TOTAL+TRK/CYL-1) / TRK/CYL
         LR    R4,R5                    RESULTING QUOTIENT
DSN050   BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN070                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN060                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN060                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN060   MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         BAL   R14,DSNCONV
         SPACE 1
DSN070   TM    DSORG,DS1DSGPO           PARTITIONED DATA SET?
         BNO   DSN080                   NO, BRANCH
         MVI   0(R1),C','               SEPARATOR
         CLC   DS1SCALO+1(3),ZERO       SECONDARY ALLOCATION?
         BNE   *+8                      YES, BRANCH
         LA    R1,1(,R1)                NO, ADD ANOTHER COMMA
         MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         BAL   R14,DSNCONV
         SPACE 2
DSN080   MVI   0(R1),C')'               PAIRED PARENTHESIS 1
         MVI   1(R1),C')'               PAIRED PARENTHESIS 2
         TM    DS1SCALO,X'01'           CYLINDER ROUNDING?
         BNO   *+10                     NO, BRANCH
         MVC   1(8,R1),DSNLROUN         YES, MARK IT
         SPACE 1
         LA    R1,INSERT#1+DSNJSPAT     WHERE TO STORE NUMBER FREE
         LH    R4,DSNEMPTY              TOTAL FREE TRACKS
         BAL   R14,DSNCONV
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   DSN090                   NO, BRANCH
         MVC   0(10,R1),DSNLFREE        FREE DIRECTORY BLOCKS
         LA    R1,10(,R1)               WHERE TO STORE NUMBER FREE
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         BAL   R14,DSNCONV
DSN090   MVI   0(R1),C'*'               TERMINATOR
         MVI   1(R1),C'/'                         CHARACTERS
         TSMSG L220$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         MESSAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNTSO   MVC   INSERT#1(22),DSNTDSN     ADD "ALLOC F(1234567)  DA('"
         MVC   INSERT#1+8(8),DDNAME     MOVE IN THE DDNAME
         LA    R1,INSERT#1+8            DDNAME START POSITION
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R1),C')'               ADD TERMINATOR
         LA    R1,INSERT#1+22           DSNAME START POSITION
         MVC   0(44,R1),DSNAME          MOVE IN THE DSNAME
         AH    R1,DSNLEN                LENGTH OF THE DSNAME
         MVC   0(12,R1),DSNTDSND        ADD "') SHR UNIT("
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   3(3,R1),=C'OLD'          NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         LA    R1,12(,R1)               START OF UNIT DATA
         MVC   0(8,R1),1(R15)           ADD TO THE MESSAGE
         MVI   8(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVC   0(3,R1),DSNLPAR          ADD A TERMINATOR AND CONTINUE
         TSMSG L210$1
         MVC   INSERT#1(80),BLANK128    BLANK THE SECOND LINE
         MVC   INSERT#1(08),DSNTDCT     "  RECFM("
         IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+8            MOVE INTO MESSAGE
         LA    R1,INSERT#1+9
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'B'
         LA    R1,2(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'S'
         LA    R1,2(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'T'
         LA    R1,2(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'A'
         LA    R1,2(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'M'
         LA    R1,2(R1)
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    DSN310                   YES, NO LRECL
         MVC   0(8,R1),DSNTDCTL         MOVE IN LRECL TEXT
         LA    R1,8(R1)
         LH    R4,LABLRECL
         BAL   R14,DSNCONV
         SPACE 1
DSN310   MVC   0(10,R1),DSNTDCTB
         LA    R1,10(R1)
         LH    R4,BLKSI
         BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0001',DS1KEYL       ANY KEY LENGTH?
         BZ    DSN320                   NO, BRANCH
         MVC   0(9,R1),DSNTDCTK         ADD ,KEYLEN=
         LA    R1,9(R1)
         BAL   R14,DSNCONV
         SPACE 1
         MVC   0(6,R1),DSNTDCTR         ADD ,RKP=
         LA    R1,6(R1)
         SR    R4,R4
         ICM   R4,B'0011',DS1RKP        RELATIVE KEY POSITION
         BAL   R14,DSNCONV
         SPACE 1
DSN320   TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN324                   NO, BRANCH
         MVC   0(8,R1),DSNTDCTO         ADD OPTCD(
         LA    R1,8(R1)
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
         SPACE 1
DSN324   TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN330                   NO, BRANCH
         MVC   0(8,R1),DSNTDCTD         ADD ) DSORG(
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   8(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   8(2,R1),DSNLVS
         LA    R1,10(,R1)               SKIP OVER DSORG TEXT
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN330                   NO, BRANCH
         MVI   0(R1),C'U'               ADD U
         LA    R1,1(,R1)                SKIP OVER U
         SPACE 1
DSN330   MVC   0(9,R1),DSNTDCTV         ADD ) VOLUME(
         LA    R1,9(,R1)                START OF UNIT DATA
         MVC   0(6,R1),VOLALLOC         ADD THE SERIAL NUMBER
         MVI   6(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVC   0(3,R1),DSNLPAR          ADD A TERMINATOR AND CONTINUE
         TSMSG L210$1
         SPACE 1
         MVC   INSERT#1(80),DSNTSPA     "  TRK SPACE(N ..."
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         LA    R1,INSERT#1+DSNTSPAP     PRIMARY SPACE
         LH    R4,DSNTOTAL              TOTAL FREE SPACE
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   DSN350                   NO, BRANCH
         MVC   INSERT#1+2(3),=C'CYL'    YES, MARK AS CYLINDER ALLOC.
         LH    R0,DEVTTRKS              TRACKS/CYLINDER
         LR    R5,R0
         BCTR  R5,0                     TRACKS/CYLINDER -1
         AR    R5,R4                    TOTAL+TRK/CYL -1
         SR    R4,R4
         DR    R4,R0                    CEIL(TOTAL+TRK/CYL-1) / TRK/CYL
         LR    R4,R5                    RESULTING QUOTIENT
DSN350   BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN370                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN360                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN360                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN360   MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         BAL   R14,DSNCONV
         SPACE 1
DSN370   MVI   0(R1),C')'               PAIRED PARENTHESIS 1
         LA    R1,1(,R1)
         TM    DSORG,DS1DSGPO           PARTITIONED DATA SET?
         BNO   DSN380                   NO, BRANCH
         MVC   1(4,R1),DSNLDIR          SEPARATOR
         LA    R1,5(,R1)
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         BAL   R14,DSNCONV
         MVI   0(R1),C')'               FINAL PARENTHESIS
         SPACE 2
DSN380   LA    R1,INSERT#1+DSNTSPAT     WHERE TO STORE NUMBER FREE
         LH    R4,DSNEMPTY              TOTAL FREE TRACKS
         BAL   R14,DSNCONV
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   DSN390                   NO, BRANCH
         MVC   0(10,R1),DSNLFREE        FREE DIRECTORY BLOCKS
         LA    R1,10(,R1)               WHERE TO STORE NUMBER FREE
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         BAL   R14,DSNCONV
DSN390   MVI   0(R1),C'*'               TERMINATOR
         MVI   1(R1),C'/'                         CHARACTERS
         TSMSG L210$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         MESSAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNMSG   MVC   INSERT#1(80),DSNM200H       ADD HEADER
         TM    OPTCD,X'A4'                 ANY OPTCD CHARACTERS?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#1+10(3),DSNLOPT      ADD OPT
         TM    DSORG,DS1DSGPO              PARTITIONED?
         BO    *+10                        YES, BRANCH
         MVC   INSERT#1+63(7),DSNLDSO      NO, CHANGE THE HEADER
         TSMSG L200$1
         MVC   INSERT#1(80),DSNM200E    ADD DATA LINE
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   INSERT#1+0(3),=C'OLD'    NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         MVC   INSERT#1+05(8),1(R15)    DEVICE TYPE TEXT
         TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN650                   NO, BRANCH
         LA    R1,INSERT#1+10
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
DSN650   IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+14           MOVE INTO MESSAGE
         LA    R1,INSERT#1+15
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         LH    R4,LABLRECL
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   *+6                      NO, BRANCH
         SR    R4,R4                    YES, LRECL IS ZERO
         CVD   R4,DOUBLE                CONVERT TO DECIMAL
         ED    INSERT#1+19(6),DOUBLE+5  CONVERT TO DISPLAY
         LH    R4,BLKSI
         CVD   R4,DOUBLE                CONVERT TO DECIMAL
         ED    INSERT#1+27(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         CVD   R14,DOUBLE
         ED    INSERT#1+33(4),DOUBLE+6  CONVERT TO DISPLAY
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         CVD   R0,DOUBLE
         ED    INSERT#1+38(6),DOUBLE+5  CONVERT TO DISPLAY
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         CVD   R0,DOUBLE
         ED    INSERT#1+46(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN660                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN660                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN660                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN660   CVD   R4,DOUBLE
         ED    INSERT#1+52(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   *+10                     NO, BRANCH
         MVC   INSERT#1+59(3),=C'CYL'   YES, MARK AS CYLINDER ALLOC.
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN680                   NO, BRANCH
         LA    R1,INSERT#1+63           WHERE TO ADD DSORG DATA
         MVC   0(8,R1),BLANKS           BLANK THE EDIT DATA
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   0(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   0(2,R1),DSNLVS
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN690                   NO, BRANCH
         MVI   2(R1),C'U'               ADD U
         B     DSN690
         SPACE 1
DSN680   LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         CVD   R4,DOUBLE
         ED    INSERT#1+64(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
DSN690   TSMSG L200$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         MESSAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNCONV  CVD   R4,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R4,DOUBLE-1
         LA    R4,1(R4)
         CLI   0(R4),C'0'
         BE    *-8
         OI    DOUBLE+4,X'F0'
DSNMOVE  MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         TM    1(R4),X'F0'
         LA    R4,1(R4)
         BO    DSNMOVE
         BR    R14
         SPACE 1
DSNTDSN  DC    C'ALLOC F(1234567)  DA('''') SHR UNIT('
DSNTDSND EQU   DSNTDSN+22,12
         SPACE 1
DSNTDCT  DC    C'  RECFM() LRECL() BLKSIZE() DSORG(**) KEYLEN() RKP() OX
               PTCD() VOLUME('
DSNTDCTL EQU   DSNTDCT+8,8
DSNTDCTB EQU   DSNTDCT+8+8,10
DSNTDCTD EQU   DSNTDCT+8+8+10,10
DSNTDCTK EQU   DSNTDCT+8+8+10+10,9
DSNTDCTR EQU   DSNTDCT+8+8+10+10+9,6
DSNTDCTO EQU   DSNTDCT+8+8+10+10+9+6,8
DSNTDCTV EQU   DSNTDCT+8+8+10+10+9+6+8,9
         SPACE 1
DSNTSPA  DC    CL80'  TRK SPACE(N                            /*FREE TRKX
               =N'
DSNTSPAP EQU   12,4
DSNTSPAT EQU   52,7
         SPACE 2
DSNJDSN  DC    C'//12345678  DD  DSN=,DISP=SHR,UNIT='
DSNJDSND EQU   DSNJDSN+20,15
         SPACE 1
DSNJDCT  DC    C'//  DCB=(RECFM=,LRECL=,BLKSIZE=,DSORG=**,KEYLEN=,RKP=,X
               OPTCD=),VOL=SER='
DSNJDCTL EQU   DSNJDCT+15,7
DSNJDCTB EQU   DSNJDCT+15+7,9
DSNJDCTD EQU   DSNJDCT+15+7+9,9
DSNJDCTK EQU   DSNJDCT+15+7+9+9,8
DSNJDCTR EQU   DSNJDCT+15+7+9+9+8,5
DSNJDCTO EQU   DSNJDCT+15+7+9+9+8+5,7
DSNJDCTV EQU   DSNJDCT+15+7+9+9+8+5+7,10
         SPACE 1
DSNJSPA  DC    CL80'//  SPACE=(TRK,(N                        /*FREE TRKX
               =N'
DSNJSPAP EQU   16,4
DSNJSPAT EQU   52,7
         SPACE 2
DSNM200H DC    CL80'DISP UNIT     RECFM LRECL BLKSIZE   ALLOCTRK FREETRX
               K SECONDARY FREEDIR'                   123X 12345
DSNM200E DC    CL80'SHR                                  X             X
                       TRK        '
         ORG   DSNM200E+19
         DC    X'402020202120'
         ORG   DSNM200E+27
         DC    X'402020202120'
         ORG   DSNM200E+33
         DC    X'40202120'
         ORG   DSNM200E+38
         DC    X'402020202120'
         ORG   DSNM200E+46
         DC    X'402020202120'
         ORG   DSNM200E+52
         DC    X'402020202120'
         ORG   DSNM200E+64
         DC    X'402020202120'
         ORG   ,
DSNLFREE DC    C',FREE DIR='
DSNLROUN DC    C',,ROUND)'
DSNLDSO  DC    CL7'DSORG'
DSNLDIR  DC    C'DIR('
DSNLOPT  DC    C'OPT'
DSNLPAR  DC    C') -'
DSNLVS   DC    C'VS'
DSORGTYP DC    C'**DAPS??IS'              DSORG=.. TEXT
RECFMTYP DC    C'*FV?U'                   RECFM=. TEXT
         DROP  R6
         TITLE 'P D S  --  PDS FIXPDS                          6/10/85'
***********************************************************************
***      FIXPDS SUBCOMMAND     ADDED BY BRUCE LELAND -- NOV., 1982  ***
***********************************************************************
*
         SPACE 1
FIXPDS   CSECT
         USING *,R8
         SPACE 1
         ICM   R1,B'1111',#RECFM       NEW DCB=RECFM SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #RECFM+3(1),RECFM       NO, USE PREVIOUS RECFM
         ICM   R2,B'1111',#LRECL       NEW DCB=LRECL SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #LRECL+2(2),LABLRECL    NO, USE THE LRECL FROM THE DCB
         TM    #RECFM+3,X'C0'          RECFM=U..?
         BNO   *+10                    NO, BRANCH
         XC    #LRECL,#LRECL           YES, USE LRECL=0
         ICM   R3,B'1111',#BLKSI       NEW DCB=BLKSIZE SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #BLKSI+2(2),BLKSI       NO, USE PREVIOUS BLOCKSIZE
         SPACE 2
         TM    #RECFM+3,DCBRECF        RECFM=F OR RECFM=U?
         BNO   FIXPDS10                NO, BRANCH
         TM    #RECFM+3,DCBRECV        RECFM=U?
         BO    FIXPDS10                YES, BRANCH
         SPACE 1
         L     R15,#BLKSI              REQUESTED BLOCKSIZE
         SR    R14,R14
         ICM   R4,B'1111',#LRECL       ANY LRECL?
         BZ    FIXPDS10                NO, AVOID DIVIDE ERROR
         DR    R14,R4                  CHECK LRECL'S PER BLOCK
         LTR   R14,R14                 INTEGRAL NUMBER?
         BZ    FIXPDS10                YES, BRANCH
         TSMSG L482                    NO, DIVIDE ERROR MESSAGE
         SPACE 2
FIXPDS10 TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXPDS50                NO, BRANCH
         CLI   #FIXOPT,X'02'           DCB?
         BL    FIXPDS50                YES, BRANCH
         SPACE 1
         MVC   ##SUBCOM(8),$USA        TEMPORARY SUBCOMMAND IDENTIFIER
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         BAL   R14,READDIR             NUMBER OF DIRECTORY BLOCKS
         MVC   ##SUBCOM(8),$FIX        RESET THE SUBCOMMAND NAME
         SPACE 1
         ICM   R3,B'1111',TOTMEMR      REAL (2 BYTES) AND ALIASES (2)
         LH    R6,TOTBLOCK             NUMBER OF DIRECTORY BLOCKS
         LTR   R6,R6                   ANY CURRENT DIRECTORY BLOCKS?
         BP    FIXPDS20                YES, BRANCH
         LA    R1,L752                 ASSUME RESET OR EXPAND & NOCOUNT
         CLC   #NEWDIR,ZERO            CORRECT?
         BE    MSGNEW                  YES, ERROR EXIT
         SPACE 1
FIXPDS20 CLI   #FIXOPT,X'02'           EXPANDDIR?
         BE    FIXPDS30                YES, BRANCH
         ICM   R1,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         ST    R6,#NEWDIR              NO, USE CURRENT NUMBER
         SR    R6,R6                   CLEAR CURRENT DIRECTORY NUMBER
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BZ    FIXPDS30                NO, A WARNING IS NOT NEEDED
         TSMSG L451                    YES, ALL MEMBERS WILL BE LOST
         SPACE 1
FIXPDS30 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#EOF1TTR             TTR OF CURRENT DIRECTORY EOF
         SPACE 1
         ICM   R4,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         LA    R4,50                   NO, ADD 50 TO THE CURRENT NUMBER
         AR    R4,R6                   DESIRED + CURRENT DIR. BLOCKS
         ST    R4,#NEWDIR              UPDATE (NOW TOTAL # OF BLOCKS)
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BNZ   *+6                     YES, BRANCH
         SR    R6,R6                   NO, CLEAR DIRECTORY COUNT
         SPACE 1
         LR    R1,R4
         SR    R1,R6                   NUMBER OF BLOCKS TO ADD
         ST    R1,#NEWDIR              UPDATE (NOW NUMBER TO ADD)
         SPACE 1
         L     R14,=X'01080100'        R=1, K=08, DD=256 BYTES
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         ST    R0,#DIRBTRK             SAVE DIRECTORY BLOCKS PER TRACK
         SRDA  R4,32                   TOTAL BLOCKS
         DR    R4,R0                   TOTAL / ENTRIES PER TRACK
         STH   R5,#EOF2TT              TRACK OF NEW DIRECTORY EOF
         L     R2,DCBDEBAD-IHADCB+INDCB  DEB ADDRESS
         LA    R1,L870                 ASSUME MULTI-EXTENT DIRECTORY
         CLC   32+14(2,R2),ZERO        NULL DATA SET?
         BE    FIXPDS31                YES, ALLOW THE ALLOCATION
         CH    R5,32+14(,R2)           CORRECT?
         BNL   MSGNEW                  YES, ERROR
         SPACE 1
FIXPDS31 LTR   R4,R4                   PARTIAL TRACK USED?
         BP    FIXPDS32                YES, BRANCH
         TSMSG L450                    NO, WARNING MESSAGE
         L     R1,#NEWDIR              ONE ADDITIONAL
         A     R1,=F'1'                       DIRECTORY
         ST    R1,#NEWDIR                           BLOCK USED
         SPACE 2
FIXPDS32 LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         USING DIRLINK,R3              NO, BRANCH
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
FIXPDS40 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     FIXPDS50                LAST MEMBER, BRANCH
         SPACE 1
         CLI   #FIXOPT,X'03'           RESET DIRECTORY?
         BE    FIXPDS42                YES, BRANCH
         CLC   #EOF2TT(2),MEMTTR       IN THE NEW DIRECTORY AREA?
         BL    FIXPDS40                NO, BRANCH
         MVI   SUBPOOLT,21             MARK TEMPORARY SUBPOOL IN USE
         LA    R0,DIREND-DIRLINK       GET ANOTHER DIRECTORY ENTRY
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,DIRLINK              CHAIN TO THE
         LR    R3,R1                               NEXT ENTRY
         L     R14,DIRPTRS             ACTUAL DIRECTORY ENTRY
         MVC   DIRNAME(74),0(R14)      ADD THE DIRECTORY INFORMATION
         MVC   INSERT#1(8),DIRNAME
         TSMSG L050$1
         SPACE 2
FIXPDS42 MVC   DSNMEMQ(8),MEMNAME      MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     FIXPDS40                YES, OUTPUT WARNING MESSAGE ONLY
         B     FIXPDS40                REPEAT FOR ALL MEMBERS
         SPACE 2
FIXPDS50 MESSAGE MSGBLANK              ONE SPACING LINE
         L     R15,$DSN+8
         BALR  R2,R15                  DISPLAY ALLOCATION STATUS
         LA    R1,PDS392A              CORRECT DATA SET
         BAL   R2,YESNO                RESPONSE "YES" OR "NO"?
         B     NEWCMD                  "NO", QUIT
         SPACE 1
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXDCB                  NO, BRANCH
         MVI   #DIRFIX,X'F9'           HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),=C'FIXPDS' TEMPORARY STOW AND DELETE NAME
         BAL   R6,RESERVE              RESERVE/ENQUE AS REQUIRED
         L     R4,#NEWDIR              DIRECTORY BLOCKS DESIRED
         CLI   #FIXOPT,X'02'           WHICH PROCESSING MODE?
         BL    FIXDCB                    DCB       -- CHANGE DCB ONLY
***      BE    EXPAND                    EXPANDDIR -- ADD TO DIRECTORY
         BH    FIXRES                    RESETDIR  -- REWRITE DIRECTORY
         SPACE 3
EXPAND   ICM   R1,B'1111',TOTMEMR      ANY MAIN OR ALIAS MEMBERS?
         BZ    FIXRES                  NO, USE RESETDIR INSTEAD
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAL   R2,SHUTSTW2             CLOSE AND FREE THE STOW DCB
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT
         SPACE 1
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         LA    R1,L831                           NO OPEN ERROR MSG
         BNO   MSGNEW                            NO, QUIT
         CLC   #MEMPTR(4),ZERO         ANY MEMBERS TO MOVE?
         BE    EXPAND80                NO, BRANCH
         LA    R0,80                   FIRST RECORD IS 80 BYTES LONG
         SPACE 2
*  ONE OR MORE MEMBERS MUST BE MOVED
*    WRITE GARBAGE RECORDS TO OCCUPY SPACE UP TO THE NEW DIRECTORY EOF
EXPAND02 STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,IOBUFF1              START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         LH    R0,BLKSI                MAXIMUM OUTPUT RECORD LENGTH
         CLM   R1,B'1100',#EOF2TT      PAST THE NEW DIRECTORY EOF?
         BNH   EXPAND02                NO, BRANCH
         ST    R1,#FIXTTRO             HIGHEST OUTPUT TTR
         LA    R3,#MEMPTR
         SPACE 1
EXPAND12 ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    EXPAND20                END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   EXPAND18                NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
EXPAND14 ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    EXPAND18                      NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    EXPAND14                      YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   EXPAND14                      NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    EXPAND16                      YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    EXPAND14                      YES, BRANCH
         SPACE 1
EXPAND16 MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
EXPAND18 TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    EXPAND12                      NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     EXPAND12
         SPACE 3
EXPAND20 LA    R3,#MEMPTR
EXPAND22 ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    EXPAND80                NO, BRANCH
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         TSMSG L051$1
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   EXPAND22                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND24 EX    R5,*-4                  ANOTHER TTR?
         BZ    EXPAND30                NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    EXPAND26                NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         L     R15,=A(EXCP)            INPUT IT
         BALR  R14,R15
         LA    R1,L751                 ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
EXPAND26 LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,EXPAND24
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
EXPAND30 MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         SPACE 2
EXPAND32 L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND34                  00 - GOOD READ
         B     EXPAND60                  04 - END OF MEMBER
         B     EXPAND60                  08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
EXPAND34 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND50                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND36 EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    EXPAND38                NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    EXPAND40                YES, BRANCH
EXPAND38 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,EXPAND36
         B     EXPAND50                NOT THIS RECORD, BRANCH
         SPACE 2
EXPAND40 SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,L750                 ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
EXPAND42 TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,EXPAND42             MOVE AND CHECK ALL ENTRIES
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
EXPAND50 L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND32                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND52 EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   EXPAND54                NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   EXPAND54                NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND54 EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   EXPAND58                NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
EXPAND56 CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,EXPAND56
EXPAND58 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,EXPAND52
         B     EXPAND32               MOVE ALL MEMBERS
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
EXPAND60 LA    R1,L871                 ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   EXPAND62                NO, BRANCH
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         LA    R1,L833
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,L832
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
EXPAND62 TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO MAIN)?
         BNO   EXPAND70                NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND70                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
EXPAND70 LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
EXPAND72 MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND74                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 1
EXPAND74 ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    EXPAND22                       END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   EXPAND74                       NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    EXPAND74                       YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
EXPAND76 EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   EXPAND72                       NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,EXPAND76
         B     EXPAND72
         DROP  R3
         SPACE 3
*  WRITE THE ADDED DIRECTORY RECORDS
EXPAND80 L     R6,#NEWDIR                     DIRECTORY BLOCKS TO ADD
         LA    R0,CCW1                        FIRST CCW
         ST    R0,IOBCCW                               TO USE
         MVI   CCW3,X'1D'                     COMMAND FOR WRITE C,K,D
         LA    R0,MSGTEXT1+3                  STORAGE FOR
         STCM  R0,B'0111',CCW3+1                         WRITE ADDRESS
         NI    CCW3+4,FF-X'40'                LAST CCW IN CHAIN
         MVC   CCW3+6(2),=H'8'                DATA LENGTH IS EIGHT
         XC    MSGTEXT1(20),MSGTEXT1          CLEAR THE WRITE ADDRESS
         MVI   MSGTEXT1+8,8                   KEY IS EIGHT BYTES
         MVI   MSGTEXT1+9,1                   DATA IS 1*256 BYTES
         MVI   OPENLIST,X'80'
         CLOSE (INDCB),MF=(E,OPENLIST)
         OPEN  (INDCB,UPDAT),MF=(E,OPENLIST)
         SPACE 2
EXPAND82 L     R0,#EOF1TTR                    POINT TO LAST RECORD
         STM   R15,R12,12+4(R13)              CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)     DEB ADDRESS
         LA    R2,MSGTEXT1                    RETURN CCHHR
         L     R15,ADDRCNVT                   TTR TO MBBCCHHR CONVERT
         LR    R3,R13                         SAVE AREA ADDRESS
         BALR  R14,R15
         LR    R13,R3                         SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)              RESTORE REGISTERS
         MVC   IOBSEEK(8),MSGTEXT1
         SR    R1,R1
         IC    R1,IOBSEEK+7
         S     R1,=F'1'                       REDUCE INITIAL TTR
         STC   R1,IOBSEEK+7
         MVI   IOECB,0
         EXCP  IOB                            WRITE THIS RECORD
         WAIT  ECB=IOECB                      WAIT FOR I/O COMPLETION
         LA    R1,L836                        I/O ERROR IN DIRECTORY
         CLI   IOECB,X'7F'                    SUCCESSFUL WRITE?
         BNE   EXIT8M                         NO, ERROR
         SR    R1,R1
         IC    R1,#EOF1TTR+2                  INCREMENT
         A     R1,=F'1'                                R OF
         STC   R1,#EOF1TTR+2                               TTR
         C     R1,#DIRBTRK                    FIT ON THIS TRACK?
         BNH   EXPAND84                       YES, BRANCH
         LH    R1,#EOF1TTR                    INCREMENT
         A     R1,=F'1'                                TT OF
         STH   R1,#EOF1TTR                                  TTR
         MVI   #EOF1TTR+2,1                   RESET R OF TTR
         SPACE 1
EXPAND84 S     R6,=F'1'                       MORE BLOCKS?
         BP    EXPAND82                       YES, BRANCH
         SPACE 2
*  WRITE THE DATA SET END-OF-FILE MARKER
         OC    MSGTEXT1+8(3),MSGTEXT1+8       END-OF-FILE WRITTEN?
         BZ    FIXDCB                         YES, BRANCH
         XC    MSGTEXT1+8(3),MSGTEXT1+8       SET KEY, DATA LENGTH TO 0
         B     EXPAND82                       ONE MORE LOOP
         SPACE 3
FIXRES   BAL   R2,CLOSEIT                        CLOSE INPUT DATA SET
         LA    R0,CHANGE2                        ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   BAMDCB(LDIRDCB),DIRDCB            COPY PROTTYPE DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB,DDNAME     CURRENT DDNAME
         MVI   OPENLIST,X'80'                    END OF LIST MARKER
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  DIRECTORY OPEN?
         BNO   EXIT12O                       NO, QUIT
         MVC   MSGTEXT1(18),KEYDATA          MOVE IN THE KEY AND DATA
         XC    MSGTEXT1+18(256),MSGTEXT1+18  FOLLOWED BY ZEROES
         SPACE 1
FIXRES30 WRITE FIXPDSD,SF,BAMDCB,MSGTEXT1,'S',MF=E
         CHECK FIXPDSD                 CHECK THIS OUTPUT OPERATION
         XC    MSGTEXT1(18),MSGTEXT1   ZERO THE KEY AND DATA PORTION
         S     R4,=F'1'                SUFFICIENT BLOCKS WRITTEN?
         BP    FIXRES30                NO, BRANCH
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         SPACE 3
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN        MARK FOR DCB OPEN EXIT USE
         SPACE 2
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 1
         STOW  STOWDCB,#DIRFIX,A       ADD A NULL MEMBER
         LA    R1,L832                 NOT ADDED MESSAGE
         LTR   R15,R15                 ADDED?
         BP    EXIT8M                  NO, QUIT
         STOW  STOWDCB,#DIRFIX,D       DELETE THE DUMMY MEMBER
         LA    R1,L833                 NOT DELETED MESSAGE
         LTR   R15,R15                 DELETED?
         BP    EXIT8M                  NO, QUIT
         B     FIXDCB20                YES, CLOSE AND END
         SPACE 3
FIXDCB   BAL   R2,CLOSEIT              CLOSE THE INPUT DCB
         TM    DSORG,DS1DSGPO          PARTITIONED DATA SET?
         BO    FIXDCB10                YES, BRANCH
         LA    R0,RESTART0             ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAL   R2,DEALLDCB             FREE THE DATA SET
         MVI   DSPALLOC,CONOMOD        ASSUME MOD -- NO CHANGE
         CLI   #FIXOPT,X'03'           CORRECT?
         BNE   *+8                     NO, BRANCH
         MVI   DSPALLOC,CONOOLD        YES, OPEN FOR RESET PROCESSING
         MVI   ##ADRCM#,EDITOR         NO REPEATED WARNING MESSAGE
         L     R15,=A(ALLOCATE)        ALLOCATION ROUTINE
         BALR  R14,R15                 GOOD ALLOCATION?
         B     RESTART0                NO, ERROR
         SPACE 1
         L     R8,##ADRCMD             REESTABLISH THE BASE REGISTER
         CLI   DSPALLOC,CONOSHR        REQUESTED ALLOCATION?
         BE    RESTART2                NO, OLD ALLOCATION FAILED
         SPACE 1
         LA    R0,CHANGE2              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVI   DCBDSORG-IHADCB+STOWDCB,DS1DSGPS  SET FOR SEQUENTIAL
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         B     FIXDCB20
         SPACE 1
FIXDCB10 LA    R0,CHANGE2              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         BAL   R6,RESERVE
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 2
FIXDCB20 CLI   #FIXOPT2,4                      MAXSPACE?
         BNE   FIXDCB30                        NO, BRANCH
         L     R1,DCBDEBAD-IHADCB+STOWDCB      GET DEB ADDRESS
         LH    R14,NUMEXT                      NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)                   ADD TRACKS IN EXTENT
         LA    R1,16(,R1)                      NEXT EXTENT
         BCT   R14,*-8                         BRANCH FOR ALL EXTENTS
         S     R0,=F'1'                        MAX 00TT OF TTR VALID?
         BM    FIXDCB30                        NO, BRANCH
         SLL   R0,8                            0TTX
         A     R0,=F'1'                        0TT1 (R OF TTR IS 1)
         SLL   R0,8                            TT10 -- NEW DS1LSTAR
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         L     R1,DCBDEBAD-IHADCB+STOWDCB      DEB ADDRESS
         LA    R2,DCBFDAD-IHADCB+STOWDCB       RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BALR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         XC    DCBTRBAL-IHADCB+STOWDCB(2),DCBTRBAL-IHADCB+STOWDCB
         SPACE 2
FIXDCB30 CLOSE (STOWDCB),MF=(E,OPENLIST)
         MESSAGE MSGBLANK              ONE SPACING LINE
         L     R1,$DSN+8               ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD             EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE+EDITOR  CONTINUE AND NO WARNING
         CLI   #FIXOPT2,0              RELEASE/RELSAVE/RELEXT/MAXSPACE?
         BE    FIXDCB90                NO, BRANCH
         CLI   #FIXOPT2,4              MAXSPACE?
         BE    FIXDCB90                YES, BRANCH
         MVC   MSGTEXT1+4(8),##SUBCAL  RELEASE COMMAND NAME
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DSNAME
         LH    R15,DSNLEN              LENGTH OF DSNAME
         LA    R3,MSGTEXT1+14(R15)     WHERE ADDITIONAL TEXT STARTS
         MVC   0(22,R3),BLANK128       BLANK IT FIRST
         MVI   0(R3),C''''             ADD A FINAL QUOTE
         LA    R4,14+22(,R15)          LENGTH OF COMMAND BUFFER
         SLL   R4,16
         ST    R4,MSGTEXT1             UPDATE LENGTH FIELD
         SPACE 1
         CLI   #FIXOPT2,1              RELEASE ALL?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(3,R3),FIXLEXT         ADD EXT FOR EXTENTS
         CLI   #FIXOPT2,3              RELEASE EXTENTS ONLY?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(4,R3),FIXLSPA         ADD SPA( FOR SPACE(
         L     R1,#FIXRES              TRACKS TO RESERVE
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'          CONVERT SIGN
         UNPK  2+4(5,R3),DOUBLE+5(3)   CONVERT TO PRINTABLE
         MVI   2+4+5(R3),C')'          ENDING )
         SPACE 1
         AIF ('&CF296' EQ 'YES').YESF296
FIXDCB70 B     FIXDCB80                ***ZAP (NOP ) TO ADD V(SERIAL)
         AGO   .NOTF296
.YESF296 ANOP
FIXDCB70 NOP   FIXDCB80                ***ZAP (B ) TO BYPASS V(SERIAL)
.NOTF296 ANOP
         MVI   2+4+7(R3),C'V'          V OF VOLUME
         MVI   2+4+8(R3),C'('          BEGINNING (
         MVC   2+4+8+1(6,R3),VOLALLOC  VOLUME ALLOCATED
         MVI   2+4+8+1+6(R3),C')'      ENDING )
         SPACE 1
FIXDCB80 LA    R1,MSGTEXT1             START OF COMMAND BUFFER
         ST    R1,ADDRTEXT             COMMAND ADDRESS
         MVI   3(R1),9                 OFFSET TO OPERANDS
         LA    R1,ADDRTEXT             COMMAND ADDRESS
         LA    R3,##SUBCAL             RELEASE COMMAND PROCESSOR
         BAL   R2,ATTACH               CALL THE COMMAND
         SPACE 2
FIXDCB90 BAL   R2,DEALLDCB             FREE THE DATA SET
         B     RESTART0                REALLOCATE AND START OVER
FIXLEXT  DC    C'EXT'
FIXLSPA  DC    C'SPA('
         TITLE 'P D S  --  PDS HELP                            6/10/85'
***********************************************************************
***      HELP  SUBCOMMAND      ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
HELP     CSECT
         USING *,R8
         MVC   DDNAMEH,DDNAME        SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP        DDNAME TO FREE
         BAL   R2,DEALLOCZ           FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH        RESTORE ACTUAL DDNAME
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),PDSNAME   SET PDS COMMAND NAME
         MVC   ECTSCMD-ECT(8,R14),HELPLHEL  SET PDS SUBCOMMAND NAME
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?
         BNO   *+8                      NO, BRANCH
         MVI   ECTSCMD-ECT+4(R14),C'X'  YES, HELPX (FOR NO LINE MODE)
         L     R2,ADDRCBUF           INPUT BUFFER ADDRESS
         L     R1,ADDRCPPL
         USING CPPL,R1               ACTUAL ADDRESS
         ST    R2,CPPLCBUF           SET COMMAND ADDRESS
         DROP  R1
         LA    R3,##SUBCAL           TSO HELP COMMAND
         BAL   R2,ATTACH             GO ATTACH IT
         B     NEWSTAX               NEXT COMMAND
HELPLHEL DC    CL8'HELP'
         SPACE 2
*  NOTE: THE SYSHELP FILE IS FREED BEFORE THE HELP
*        CALL IN CASE A PCF EXIT ALREADY REQUESTED HELP.
         TITLE 'P D S  --  PDS HISTORY                         6/10/85'
***********************************************************************
***      HISTORY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
HISTORY  CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         OI    FLAGSGG,FOUTSUB     SUBCOMMAND GAINED CONTROL
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    HIST020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTATH
         MVI   MTHIGHL,72
         TM    FLAGSAA,FHEAD       HEADER WRITTEN YET?
         BO    HIST000             YES, BRANCH
         TSMSG L230$1
         OI    FLAGSAA,FHEAD       NOW IT HAS BEEN WRITTEN
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         SPACE 1
HIST000  MVC   INSERT#1(72),BLANK128
         MVC   INSERT#1+1(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   HIST010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   HIST010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   HIST010             NO, BRANCH
         SPACE 1
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         LA    R1,L230$1
         B     MSGNEW                  NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
HIST010  LA    R1,L230$1
         CLC   ZERO,0(R2)              ZERO?
         BE    MSGNEW                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MSGNEW                  YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         B     MSGNEW
         SPACE 3
HIST020  OC    #ZAPOPT(4),#ZAPOPT        ANY ZAP, USER, TRAN OR LKED?
         BNZ   HIST021                   YES, BRANCH
         OC    #ZAPOPT(4),=X'01010101'   NO, DEFAULT TO DO THEM ALL
         TM    FLAGSGG,FTRANCON          TRANSLATOR DATA OFF?
         BZ    HIST021                   NO, BRANCH
         MVI   #TRANOPT,X'02'            YES, NO TRANSLATOR DATA
         SPACE 1
HIST021  L     R15,=A(READESD)           FORMAT THE ESD DATA
         BALR  R14,R15                   ANY ESD DATA?
         B     *+8                       NO, BRANCH
         B     HIST024                   YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FDELAYM     DELAYED MESSAGE AVAILABLE?
         BNO   HIST022             NO, BRANCH
         TM    PMEMMIN,X'80'       MEMBER LIST?
         BO    *+12                YES, BRANCH
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP?
         BNO   HIST022             NO, BRANCH
         CLI   #MODLEN,0           ANY FILTERING?
         BH    NEWCMD              NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE DELAYMSG
         XI    FLAGSAA,FDELAYM     MESSAGE WAS WRITTEN
         SPACE 1
HIST022  LA    R1,MSGTEXT1         MESSAGE IDENTIFIER
         B     MSGNEW
         SPACE 2
HIST024  L     R15,=A(READIDR)     FORMAT THE IDR DATA
         BALR  R14,R15             IS IDR DATA AVAILABLE?
         B     NOHIST              NO, MESSAGE AND QUIT
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         B     HISTTRN$            YES, CONTINUE
         SPACE 1
NOHIST   SR    R6,R6
         BAL   R4,HISTHDR
         LA    R1,L460             NO HISTORY AVAILABLE
         B     MSGNEW
         SPACE 3
HISTHDR  TM    FLAGSAA,FDELAYM     ANY DELAYED HEADER?
         BNO   HISTHDR2            NO, BRANCH
         XI    FLAGSAA,FDELAYM     MESSAGE WAS WRITTEN
         MESSAGE MSGBLANK
         MESSAGE DELAYMSG
HISTHDR2 TM    FLAGSCC,F1IDR       FIRST IDR RECORD?
         BZR   R4                  NO, RETURN
         NI    FLAGSCC,FF-F1IDR
         LTR   R1,R6               ANY SECOND HEADER?
         BZR   R4                  NO, RETURN
         TSMSG (R1)                INDIVIDUAL HISTORY TITLE
         BR    R4
         EJECT
*
*        FORMAT TRANSLATOR IDR DATA ENTRIES
*
HISTTRN$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         CLI   #TRANOPT,X'02'      NO TRANSLATOR DATA?
         BE    HISTCOB$            YES, BRANCH
         SPACE 2
HISTTRN  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOB$            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTTRN             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTTRN1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTTRN             YES, BRANCH
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTTRN1            NO, SKIP THIS
         CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTTRN1            NO, BRANCH
         SPACE 1
         LA    R6,L060
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(8),ESDNAME
         MVC   MSGTEXT1+12(9),DATEMASK
         ED    MSGTEXT1+12(9),IDRDATE
         MVC   MSGTEXT1+24(10),IDRTDATA   TRANSLATOR 1
         UNPK  DOUBLE(5),IDRTDATA+10(3)   VVMM PACKED
         MVI   MSGTEXT1+36,C'V'
         MVC   MSGTEXT1+36+1(2),DOUBLE    VV
         MVI   MSGTEXT1+40,C'M'
         MVC   MSGTEXT1+40+1(2),DOUBLE+2  MM
         CLI   IDRLDATA,15                TWO TRANSLATORS?
         BL    HISTTRN3                   NO, BRANCH
         SPACE 1
         OI    IDRTDATA+27+2,X'0F'        FIX THE SIGN FIELD
         LA    R15,FULLWORD               OUTPUT LOCATION
         LA    R1,IDRTDATA+27             SECOND YYDDD
         BAL   R14,CONVDATE               CONVERT TO MMDDYY FORMAT
         MVC   MSGTEXT1+48(9),DATEMASK
         ED    MSGTEXT1+48(9),FULLWORD
         MVC   MSGTEXT1+60(10),IDRTDATA+15  TRANSLATOR 2
         UNPK  DOUBLE(5),IDRTDATA+25(3)     VVMM PACKED
         MVI   MSGTEXT1+72,C'V'
         MVC   MSGTEXT1+72+1(2),DOUBLE      VV
         MVI   MSGTEXT1+76,C'M'
         MVC   MSGTEXT1+76+1(2),DOUBLE+2    MM
         SPACE 1
HISTTRN3 MESSAGE MSGTEXT1
         B     HISTTRN1            NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 3
*
*        FORMAT SPECIFIC ATTRIBUTES FOR COBOL CSECTS
*
HISTCOB$ LA    R3,#IDRPTR            SCAN IDR CHAIN
         USING IDRENTRY,R3
         LA    R2,#ESDPTR            START OF ESD CHAIN
         SPACE 2
HISTCOBA ICM   R3,B'1111',IDRLINK    END OF IDR CHAIN?
         BZ    HISTCOBB              YES, BRANCH
         CLI   IDRTYPE,IDRTRAN       TRANSLATOR ENTRY?
         BNE   HISTCOBA              NO, BRANCH
         CLC   IDRTDATA(6),HISTCTR1  COBOL TRANSLATOR ID?
         BNE   HISTCOBA              NO, BRANCH
         B     HISTCOBC              YES, COBOL -- BRANCH
         SPACE 2
         USING ESDENTRY,R2
HISTCOBB ICM   R2,B'1111',ESDLINK    END OF ESD CHAIN?
         BZ    HISTZAP$              YES, BRANCH
         CLI   ESDTYPE,CODESD        CSECT ENTRY?
         BNE   HISTCOBB              NO, SKIP THIS
         CLC   ESDNAME(3),HISTLILB   COBOL ESD ENTRY NAME?
         BNE   HISTCOBB              NO, BRANCH
         SPACE 2
HISTCOBC LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         TM    DIRATTR,ATTREXEC    EXECUTABLE?
         BNO   HISTZAP$            NO, IGNORE COBOL ATTRIBUTES
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,HISTCABE         RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD  EPLOC=DIRNAME,DCB=INDCB
         LR    R4,R0               ENTRY POINT ADDRESS OF MODULE
         ICM   R1,B'0111',DIREPA   ENTRY POINT OFFSET OF MODULE
         LA    R1,0(,R1)           CLEAR THE TOP BYTE
         SR    R4,R1               START OF MODULE
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         B     HISTCOB
         SPACE 1
HISTCABE LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         B     HISTZAP$            LOAD DID NOT WORK
         SPACE 2
HISTCOB  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOBZ            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTCOB             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTCOB1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTCOBZ            YES, DONE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTCOB1            NO, SKIP THIS
         SPACE 1
         SR    R5,R5
         ICM   R5,B'0111',ESDADDR         OFFSET TO THIS CSECT
         SR    R15,R15
         ICM   R15,B'0111',ESDLEN         LENGTH OF CSECT
         CH    R15,HISTL450               POSSIBLY COBOL?
         BL    HISTCOB1                   NO, BRANCH
         AR    R5,R4                      ADDRESS OF CSECT START
         LR    R1,R5
         AR    R15,R5                     ADDRESS OF CSECT END
         CLC   0(2,R5),HISTLX07           PADDED NOP ENTRY POINT?
         BNE   HISTCEP                    NO, BRANCH
         CLC   14(8,R5),BLANK128          PADDED ENTRY POINT?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,22(,R5)                 GET THE REAL POINTER
         B     HISTCGOT
HISTCEP  CLC   12(8,R5),BLANK128          ENTRY POINT CODING?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,20(,R5)                 GET THE REAL POINTER
HISTCGOT LA    R5,0(,R5)
         CR    R5,R1                      IN THIS CSECT?
         BL    HISTCOB1                   NO, BRANCH
         CR    R5,R15                     IN THIS CSECT?
         BH    HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCGT2 LA    R1,HISTCVS                 ASSUME VS COMPILER
         CLC   20(4,R5),HISTCVSR          CORRECT?
         BE    HISTCMSG                   YES, BRANCH
         LA    R1,HISTCV4                 ASSUME V4 COMPILER
         CLC   20(4,R5),HISTCANS          CORRECT?
         BNE   HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCMSG MVC   INSERT#1(70),HISTCHDR      INITIALIZE THE HEADER LINE
         MVI   MTHIGHL,70                 70 CHARACTER MESSAGE
         TM    FLAGSCC,F1IDR              A COBOL HEADER WRITTEN?
         BNO   HISTCCOM                   NO, BRANCH
         XI    FLAGSCC,F1IDR              HEADER HAS BEEN WRITTEN
         TM    FLAGSAA,FDELAYM            ANY DELAYED HEADER?
         BNO   HISTCMS2                   NO, BRANCH
         XI    FLAGSAA,FDELAYM            MESSAGE WAS WRITTEN
         MESSAGE MSGBLANK
         MESSAGE DELAYMSG
HISTCMS2 TSMSG L250$1
         MVC   INSERT#1(70),HISTCUND      INITIALIZE THE HEADER LINE
         TSMSG L250$1
         B     HISTCGT2                   FIND THE COMPILER TYPE AGAIN
         SPACE 1
HISTCCOM MVC   INSERT#1+00(8),ESDNAME     SET THE CSECT NAME
         MVC   INSERT#1+09(3),0(R1)       SET THE COMPILER NAME
         L     R6,60(,R5)                 TGT POINTER
         CLC   X'1BC'(8,R6),HISTCOUT      SYSOUT?
         BE    HISTCBIT                   YES, BRANCH
         TSMSG L500
         B     HISTCOB1
         SPACE 1
HISTCBIT TM    X'48'(R6),X'08'            SYMDUMP?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+52(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'04'            FLOW?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+19(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'02'            STATE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+24(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'49'(R6),X'10'            TEST?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+30(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'80'            RESIDENT?
         BZ    HISTCRS2                   YES, BRANCH (INVERSE TEST)
         MVC   INSERT#1+41(03),BLANK128   NO, CLEAR THE DOC
         OI    #TRANOPT,X'80'             MARK NORES FOR LATER
         B     HISTCEN0
HISTCRS2 OI    #TRANOPT,X'40'             MARK RES FOR LATER
         SPACE 1
HISTCEN0 TM    X'4A'(R6),X'40'            ENDJOB?
         BO    HISTCEN2                   YES, BRANCH
         MVC   INSERT#1+45(06),BLANK128   NO, CLEAR THE DOC
         B     HISTCOBJ
HISTCEN2 OI    #TRANOPT,X'20'             MARK ENDJOB FOR LATER
         SPACE 1
HISTCOBJ MVC   INSERT#1+57(03),BLANK128   CLEAR "OBJ"
         TM    X'4A'(R6),X'20'            OBJ370?
         BNO   *+10                       NO, BRANCH
         MVC   INSERT#1+57(03),HISTC370   YES, ADD "370"
         SPACE 1
         MVC   INSERT#1+61(10),BLANK128   CLEAR "OPTIMIZE "
         TM    X'48'(R6),X'01'            COBOL OPTIMIZED?
         BNO   HISTCCPX                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCOPT   YES, ADD "COBOL"
         B     HISTCTWO
         SPACE 1
HISTCCPX CLC   X'48'(5,R5),HISTCCAP       CAPEX?
         BNE   HISTCTWO                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCCAP   YES, ADD "CAPEX"
         L     R5,X'38'(,R5)              PGT POINTER
         SH    R5,=H'8'
         ICM   R5,B'1111',0(R5)           DTECT POSSIBLE?
         BZ    HISTCTWO                   NO, BRANCH
         AR    R5,R6                      POINT TO OPTION BYTE
         TM    0(R5),X'60'                XCOUNT OR PFLOW?
         BZ    HISTCTWO                   YES, BRANCH
         MVC   INSERT#1+61(09),HISTCC$D   NO, ADD "CAP/DTECT"
         SPACE 1
HISTCTWO CLC   INSERT#1(3),HISTCVS        VS COMPILER?
         BE    HISTCFIN                   NO, BRANCH
         TM    X'4A'(R6),X'08'            COUNT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+13(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'10'            TRACE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+35(05),BLANK128   NO, CLEAR THE DOC
HISTCFIN TSMSG L250$1
         B     HISTCOB1
         SPACE 1
HISTCOBZ DELETE EPLOC=DIRNAME
         B     HISTZAP$
         SPACE 2
HISTCTR1 DC    C'5740CB'
HISTLILB DC    C'ILB'                     COBOL SUBROUTINE NAME
HISTCHDR DC    C'CSECT    VER COUNT FLOW STATE TEST TRACE '
         DC    C'RES ENDJOB SYMD OBJ OPTIMIZE  '
HISTCUND DC    C'-----    --- ----- ---- ----- ---- ----- '
         DC    C'--- ------ ---- --- --------  '
HISTCVS  DC    C'VS '
HISTCV4  DC    C'V4 '
HISTCANS DC    C'ANS4'
HISTCVSR DC    C'VSR1'
HISTCOUT DC    CL8'SYSOUT'
HISTC370 DC    C'370'
HISTCOPT DC    C'COBOL'
HISTCCAP DC    C'CAPEX'
HISTCC$D DC    C'CAP/DTECT'
         EJECT
*
*        FORMAT AMASPZAP IDR DATA ENTRIES
*
HISTZAP$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         MVI   MTHIGHL,8
         TM    #TRANOPT,X'80'+X'40'+X'20'  ENDJOB WITH RES AND NORES?
         BNO   HISTZAP                     NO, BRANCH
         TSMSG L502
         SPACE 1
HISTZAP  ICM   R3,B'1111',IDRLINK  END OF ESD CHAIN?
         BZ    HISTUSR$            YES, BRANCH
         CLI   IDRTYPE,IDRZAP      AMASPZAP ENTRY?
         BNE   HISTZAP             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
         SPACE 1
HISTZAP1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTZAP3            YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTZAP1            NO, SKIP THIS
         SPACE 1
         CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTZAP1            NO, BRANCH
         SPACE 1
HISTZAP2 LA    R6,L061
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         MESSAGE MSGTEXT1
         B     HISTZAP             NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 1
HISTZAP3 CLI   #MODLEN,0           ANY MODULE NAME SELECTION?
         BH    HISTZAP             YES, CANNOT DETERMINE MISSING ESD'S
         LA    R2,HISTZAP4-4       MESSAGE IF CSECT NAME UNKNOWN
         B     HISTZAP2            NEXT IDR DATA RECORD THIS ESD ENTRY
HISTZAP4 DC    C'?UNKNOWN'         UNKNOWN CSECT NAME
         EJECT
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE 1
HISTUSR$ LA    R3,#IDRPTR
         OI    FLAGSCC,F1IDR
         SPACE 1
HISTUSR  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTLKD$            YES, BRANCH
         CLI   IDRTYPE,IDRUSER     USER IDR DATA RECORD?
         BNE   HISTUSR             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          ADDRESS OF ESD CHAIN
         SPACE 1
HISTUSR1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTUSR             YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTUSR1            NO, BRANCH
         CLC   ESDID,IDRESDID      WANTED ESD RECORD?
         BNE   HISTUSR1            NO, BRANCH
         SPACE 1
         LA    R6,L062
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         ICM   R1,B'0001',IDRLDATA ZERO BYTES?
         BZ    HISTUSRS            YES, SKIP THE MOVE (LENGTH ZERO)
         SPACE 1
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(*-*),IDRDATA  <<EXEC>>
         EX    R1,*-6
         SPACE 1
HISTUSRS MESSAGE MSGTEXT1
         B     HISTUSR1
         DROP  R2,R3
         EJECT
*
*        FORMAT THE LINKAGE-EDITOR IDR DATA RECORD
*
HISTLKD$ CLI   #LKEDOPT,X'01'           "LKED" REQUESTED?
         BNE   NEWCMD                   NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN      ANY LKED NAME CHECK?
         BZ    HISTLKDG                 NO, BRANCH
         BCTR  R1,0                     MACHINE LENGTH
         CLC   #LKEDTXT(*-*),LKEDNAME   <<EXECUTED>>
         EX    R1,*-6                   THIS LINKAGE EDITOR?
         BNE   NEWCMD                   NO, BRANCH
HISTLKDG SR    R6,R6
         BAL   R4,HISTHDR
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTHIGHL+4,18                   LENGTH OF THIS INSERT
         MVC   INSERT#2(10),LKEDNAME
         MVC   INSERT#2+10(2),HISTLV
         UNPK  INSERT#2+10+2(3),LKEDVVMM(2)
         MVC   INSERT#2+10+2+2(2),HISTLM
         UNPK  INSERT#2+10+2+2+2(3),LKEDVVMM+1(2)
         SPACE 1
         TSMSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTHIGHL+4,8                    LENGTH OF STANDARD INSERT
         B     NEWCMD
         TITLE 'P D S  --  PDS HISTORY IDR SCAN SUBROUTINE     6/10/85'
*
*        IDR SCAN SUBROUTINE
*
         SPACE 3
READIDR  STM   R14,R12,ESDIDRSV    SAVE REGISTERS
         LR    R8,R15              BASE FOR THIS SUBROUTINE
         USING READIDR,R8
         NI    FLAGSCC,255-FIDR    IDR DATA DOES NOT EXIST YET
         LA    R6,IDRNORML         BRANCH ADDRESS IF NOT CONTINUED
         LA    R2,#IDRPTR          BASE FOR IDR LIST
         USING IDRENTRY,R2
         MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
IDREXCP  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     IDREXCP1              00 - GOOD READ
         B     LASTIDR               04 - END OF MEMBER
         B     LASTIDR               08 - END OF DATA SET
         B     NEWCMD                12 - I/O ERROR
         SPACE 1
IDREXCP1 LR    R15,R0              START OF RECORD
         TM    0(R15),X'50'        TEST SYM OR SCATTER RECORD?
         BM    IDREXCP             YES, SKIP IT
         SPACE 1
         CLI   0(R15),X'20'        CESD RECORD?
         BE    IDREXCP             YES, SKIP
         SPACE 1
         CLI   0(R15),X'80'        IDR RECORD?
         BNE   LASTIDR             NO, BRANCH
         SPACE 1
         LA    R3,3(,R15)          START OF IDR DATA
         SR    R5,R5
         IC    R5,1(,R15)          BYTE COUNT THIS RECORD
         LA    R5,0(R5,R15)        END OF BUFFER ADDRESS
         BR    R6                  BRANCH TO IDRNORML OR AS CONTINUED
         SPACE 2
IDRNORML TM    2(R15),IDRLKED      LINKAGE EDITOR IDR RECORD?
         BO    LKEDIDR             YES, BRANCH
         TM    2(R15),IDRTRAN      TRANSLATOR IDR RECORD?
         BO    TRANIDR             YES, BRANCH
         TM    2(R15),IDRZAP       AMASPZAP IDR RECORD?
         BO    ZAPIDR              YES, BRANCH
         TM    2(R15),IDRUSER      USER-SUPPLIED IDR RECORD?
         BNO   NEXTIDR             NO, NEXT RECORD
         EJECT
*  USER-SUPPLIED IDR RECORDS:
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
*   0 - 1    2 - 4            5               6 - 6+COUNT          *
*   ESDID    IDENTIFY DATE    COUNT (0-40)    IDRDATA              *
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
         SPACE 1
USERIDR  CLI   #USEROPT,0          "USER"?
         BE    NEXTIDR             NO, BRANCH
         BAL   R14,GETIDR          GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1               ADDRESS OF NEW RECORD
         SPACE 2
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH MOVED
         CH    R4,=H'5'            CONTINUED USER HEADER?
         BNL   USER20              NO, BRANCH
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R4,*-6              MOVE IN DATA
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         LA    R14,IDRUPREF+1(R4)  TARGET OF MOVE
         LA    R15,5-1(,R3)        LENGTH BYTE ADDRESS-1 IF NOT SPLIT
         SR    R15,R4              AMOUNT WE ALREADY HAVE
         SR    R1,R1
         IC    R1,0(,R15)          LENGTH BYTE
         LA    R15,6-2(,R1)        MACHINE LENGTH OF RECORD -1
         SR    R15,R4              MACHINE LENGTH TO MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         B     USER30
         SPACE 2
USER20   SR    R15,R15
         IC    R15,5(,R3)          LENGTH OF USER DATA AREA
         LA    R15,5(,R15)         MACHINE LENGTH FOR MOVE
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         SPACE 1
USER30   LA    R4,1(,R15)          ACTUAL RECORD LENGTH
         MVI   IDRTYPE,IDRUSER     USER IDR DATA IDENTIFIER
         MVC   IDRESDID,IDRUPREF   ADD THE CSECT ID
         LA    R1,IDRUPREF+2       YYDDD IDR RECORD CREATED
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SR    R15,R15
         IC    R15,IDRUPREF+5      LENGTH OF USER DATA AREA
         BCTR  R15,0
         STC   R15,IDRLDATA
         SPACE 2
USER40   BXLE  R3,R4,USERIDR
         EJECT
         SR    R3,R5
         BCTR  R3,0                ACTUAL REMAINING LENGTH
         LTR   R4,R3               CONTINUED DATA PORTION?
         BNP   NEXTIDR             NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRDATA+1(R14)  END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R4,1(,R4)           ACTUAL LENGTH OF LAST PORTION
         B     USER40
         SPACE 4
*  AMASPZAP IDR RECORDS:
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
*  0 - 1    2 - 4       5 - 12                                      *
*  ESDID    ZAP DATE    IDRDATA OR USER IDENTIFICATION              *
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
         SPACE 1
ZAPIDR   CLI   #ZAPOPT,X'01'       "ZAP"?
         BNE   NEXTIDR             NO, BRANCH
         SR    R4,R4
         NI    0(R3),X'3F'
         IC    R4,0(R3)            GET COUNT OF AMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R4,1(R4)            JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  BAL   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         MVC   IDRESDID,0(R3)      MOVE ESDID TO IDR RECORD
         LA    R1,2(R3)            ADDRESS OF DATE OF RECORD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SPACE 1
         MVC   IDRDATA,5(R3)       MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8          SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP      INDICATE AMASPZAP ENTRY
         LA    R3,13(R3)           JUMP DATA ADDRESS
         SPACE 1
ZAPIDR1  BCT   R4,ZAPIDR2          IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR             READ NEXT IDR RECORD
         EJECT
*  TRANSLATOR IDR RECORDS:
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*  0 - 1   2 - 3   N - N+1            0    (0 IS 1 TRANSLATOR)  
*  ESDID   ...     X'80'+LAST ESDID   FLAG (1 IS 2 TRANSLATORS) 
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
*    1 - 10        11 - 12    13 -15       16 - 30                 *
*    TRANSLATOR    VVMM       TRANS DATE   OPTIONAL, SAME AS 1-15  *
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
         SPACE 1
TRANIDR  CLI   #TRANOPT,0          NOTRAN?
         BE    NEXTIDR             YES, BRANCH
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH OK?
         BM    NEXTIDR             NO, DONE WITH THIS RECORD
         BAL   R14,GETIDR
         ST    R1,IDRLINK          CHAIN ON A
         LR    R2,R1                         NEW IDR RECORD
         SPACE 1
         MVI   IDRTYPE,IDRTRAN     TRANSLATOR TYPE RECORD
         LTR   R4,R4               MACHINE LENGTH OK?
         BP    TRAN20              YES, NOT SPLIT
         MVC   IDRESDID(1),0(R3)   NO, ONLY ONE ESDID BYTE
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         MVC   IDRESDID+1(1),0(R3) SECOND BYTE OF ESDID
         BCTR  R3,0                BACK UP THE INPUT POINTER BY ONE
         B     TRAN30
         SPACE 2
TRAN20   MVC   IDRESDID(2),0(R3)   ESDID FIELD
         SPACE 1
TRAN30   MVC   IDRUPREF(2),IDRESDID  ORIGINAL ESDID
         NI    IDRESDID,FF-X'80'   TURN OFF THE HIGH-ORDER BIT
         TM    IDRUPREF,X'80'      LAST ESDID?
         BO    TRAN50              YES, BRANCH
         LA    R4,2                ESDID WIDTH
         BXLE  R3,R4,TRANIDR       CONTINUE FOR ALL ESDID'S
         B     NEXTIDR             GET THE NEXT RECORD
         SPACE 1
TRAN50   LA    R3,2(,R3)
         LR    R4,R5
         SR    R4,R3               FLAG BYTE INPUT?
         BNM   TRAN60              YES, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
TRAN60   LA    R1,14               ASSUME ONE TRANSLATOR
         CLI   0(R3),0             CORRECT?
         BE    *+8                 YES, BRANCH
         LA    R1,29               NO, TWO TRANSLATORS
         MVC   IDRTDATA(30),1(R3)  MOVE IN THE MAXIMUM LENGTH
         STC   R1,IDRLDATA         MACHINE LENGTH
         LA    R4,2(,R1)           ACTUAL SEGMENT LENGTH
TRAN70   BXLE  R3,R4,TRAN80
         EJECT
         LR    R4,R3
         SR    R4,R5               BYTES LEFT +1
         S     R4,=F'1'            ANY ACTUAL REMAINING BYTES?
         BNP   TRAN80              NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRTDATA+1(R14) END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R3,1(R3,R4)         POSITION FOR THE NEXT SEGMENT
         SPACE 2
TRAN80   LA    R1,IDRTDATA+12      ADDRESS OF CREATION YYDDD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         LA    R1,#IDRPTR          SCAN IDR CHAIN
         SPACE 1
TRAN90   ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)
         BZ    TRANIDR                        DONE, BRANCH
         CLI   IDRTYPE-IDRENTRY(R1),IDRTRAN   TRANSLATOR TYPE?
         BNE   TRAN90                         NO, BRANCH
         CLI   IDRDATE-IDRENTRY(R1),0         ANY DATE YET?
         BNE   TRAN90                         YES, BRANCH
         MVC   IDRDATE-IDRENTRY(3+6+1+30,R1),IDRDATE
         B     TRAN90
         DROP  R2
         SPACE 3
*  LINKAGE EDITOR IDR RECORDS:
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
*  0 - 9                  10 - 11    12 - 14                        *
*  LINKAGE EDITOR NAME    VVMM       LINKAGE EDIT DATE              *
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
         SPACE 1
LKEDIDR  OI    FLAGSCC,FIDR
         LA    R1,12(R3)           ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         MVC   LKEDNAME(10),0(R3)  LINKAGE EDITOR NAME
         MVC   LKEDVVMM(2),10(R3)  VV.MM IN HEX
         SPACE 2
NEXTIDR  LA    R6,IDRNORML         NOT A CONTINUED RECORD
         SPACE 1
IDRCONT  B     IDREXCP             CONTINUED RECORD
         EJECT
LASTIDR  MVC   RLDCOUNT(1),3(R15)    SAVE THE FIRST RLD/CONTROL AMOUNT
         OC    #ZAPLEN(3),#ZAPLEN    ANY IDR FILTERING?
         BZ    LASTIDRZ              NO, BRANCH
         SPACE 2
         USING IDRENTRY,R3
         SR    R4,R4
         LA    R3,#IDRPTR            SCAN IDR CHAIN
IDRFIL10 LR    R2,R3                 PREVIOUS ENTRY
         ICM   R3,B'0111',IDRLINK+1  CURRENT ENTRY?
         BZ    LASTIDRZ              NO, BRANCH
         ICM   R4,B'0111',IDRLINK+1  NEXT ENTRY
         SPACE 2
         SR    R15,R15               COMPARE STRING LENGTH
         CLI   IDRTYPE,IDRTRAN       IDR TYPE:
         BH    IDRFIL40                USER, BRANCH
         BL    IDRFIL30                ZAP, BRANCH
         LA    R1,#TRANTXT             TRANSLATOR
         SPACE 1
         ICM   R15,B'0001',#TRANLEN  ANY TRANSLATOR FILTERING?
         BZ    IDRFIL10              NO, BRANCH
         CLI   IDRLDATA,15           ONE TRANSLATOR ONLY?
         BL    IDRFIL60              YES, BRANCH
         BCTR  R15,0                 COMPARE MACHINE LENGTH
         CLC   IDRTDATA+15(*-*),#TRANTXT  <<EXECUTED>>
         EX    R15,*-6                    THIS PL/S TRANSLATOR?
         BE    IDRFIL10                   YES, BRANCH
         IC    R15,#TRANLEN          COMPARE STRING LENGTH (AGAIN)
         B     IDRFIL60              CHECK THE FIRST TRANSLATOR TOO
         SPACE 2
IDRFIL30 IC    R15,#ZAPLEN           ZAP IDR RECORD
         LA    R1,#ZAPTXT
         B     IDRFIL60
         SPACE 2
IDRFIL40 IC    R15,#USERLEN          USER IDR RECORD
         LA    R1,#USERTXT
         SPACE 2
IDRFIL60 S     R15,=F'1'             VALID MACHINE LENGTH?
         BM    IDRFIL10              NO, BRANCH
         SPACE 1
         CLC   IDRDATA(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6               PARTIAL NAME MATCH?
         BE    IDRFIL10              YES, BRANCH
         SPACE 1
         LR    R3,R2                 NO, UNCHAIN THE
         ST    R4,IDRLINK                           CURRENT ENTRY
         B     IDRFIL10
         EJECT
LASTIDRZ LA    R3,#IDRPTR          PERFORM AN IDR RECORD SORT
         SPACE 2
SORTIDR  ICM   R3,B'1111',IDRLINK
         BZ    SORTIDRZ
         LR    R1,R3
         SPACE 1
SORTIDR2 ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)    GET THE NEXT ENTRY
         BZ    SORTIDR
         SPACE 1
         ICM   R0,B'1110',IDRDATE   ==> SORT BY DATE (DESCENDING ORDER)
         CLM   R0,B'0010',IDRDATE-IDRENTRY+2(R1)  THIRD BYTE:THIRD BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'1000',IDRDATE-IDRENTRY+0(R1)  FIRST BYTE:FIRST BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'0100',IDRDATE-IDRENTRY+1(R1)  SECONDBYTE:SECONDBYTE
         BNL   SORTIDR2                           HIGH/EQUAL - BRANCH
         SPACE 1
SORTIDR3 XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         B     SORTIDR2
         SPACE 1
SORTIDRZ LM    R14,R12,ESDIDRSV    RESTORE REGISTERS
         TM    FLAGSCC,FIDR        ANY IDR RECORDS?
         BZR   R14                 NO, RETURN AT +0
         B     4(,R14)             YES, RETURN AT +4
         DROP  R3
         SPACE 2
GETIDR   CLC   ##SUBCOM(8),$ATT      ATTRIB SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         CLC   ##SUBCOM(8),$MML      MEMLIST SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         MVI   SUBPOOLT,21
         LA    R0,LENIDR
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENIDR,R1),0(R1)
         OI    FLAGSCC,FIDR          IDR DATA EXISTS
         BR    R14
HISTL450 DC    H'450'
HISTLX07 DC    X'0700'
HISTLV   DC    C' V'
HISTLM   DC    C' M'
         TITLE 'P D S  --  PDS IF                        6/10/85'
***********************************************************************
***      IF SUBCOMMAND         ADDED BY BRUCE LELAND -- JAN., 1983  ***
***********************************************************************
*
         SPACE 1
IF       CSECT
         USING *,R8
         OI    FLAGSGG,FOUTSUB        SUBCOMMAND GAINED CONTROL
         OC    #ACTIONT(8),#ACTIONT   ANY THEN OR ELSE?
         BNZ   *+10                   YES, BRANCH
         MVC   #ACTIONT(4),IFLA$ATT   DEFAULT THEN ACTION IS ATTRIB
         SPACE 1
         TM    #OPTOPT,X'80'        PARSE INVOKED?
         BNO   IF996                NO, DONE
         CLC   #ATTRLO(3),DIRTTR    IN TTR LOWER BOUND?
         BH    IF997                NO, DO ANY ELSE
         CLC   #ATTRHI(3),DIRTTR    IN TTR UPPER BOUND?
         BL    IF997                NO, DO ANY ELSE
         SPACE 1
         TM    FLAGSCC,RECFMU       A LOAD LIBRARY?
         BO    IF006                YES, BRANCH
         LA    R1,L701              ASSUME LINKEDIT ATTRIBUTES
         TM    #OPTOPT,X'01'        CORRECT?
         BO    MSGNEWXX             YES, ERROR
         B     IF016
         SPACE 2
IF006    MVC   MODESAVE(1),DIRATTR3 RMODE/AMODE BYTE
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   MODESAVE,0           NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,MODESAVE
         SRA   R15,2                AMODE FOR ALIAS
         TM    DIRFLAG,X'80'        ALIAS?
         BNO   *+8                  NO, BRANCH
         STC   R15,MODESAVE         YES, USE ALIAS AMODE BITS
         NI    MODESAVE,DIRAM24+DIRAM31 TURN OFF OTHER BITS
         SPACE 3
IF016    CLI   #RMODE,X'01'         ANY RMODE SELECTION?
         BL    IF036                NO, BRANCH
         MVC   DOUBLE(1),DIRATTR3   RMODE/AMODE BYTE
         NI    DOUBLE,DIRRMANY      TURN OFF OTHER BITS
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   DOUBLE,0             NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,#RMODE
         SLA   R15,2                #RMODE*4
         B     *(R15)               CHECK FOR:
         B     IF020                  RMODE=24
         B     IF030                  RMODE=ANY
         B     IF030                  NO RMODE=24
         B     IF020                  NO RMODE=ANY
IF020    CLI   DOUBLE,0             RMODE=24?
         BNE   IF997                NO, DO ANY ELSE
         B     IF036                YES, BRANCH
IF030    CLI   DOUBLE,0             RMODE=24?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF036    CLI   #AMODE,X'01'         ANY AMODE SELECTION?
         BL    IF102                NO, BRANCH
         SR    R15,R15
         IC    R15,#AMODE
         SLA   R15,2                #AMODE*4
         B     *(R15)               CHECK FOR:
         B     IF040                  AMODE=24
         B     IF050                  AMODE=31
         B     IF060                  AMODE=ANY
         B     IF070                  NO AMODE=24
         B     IF080                  NO AMODE=31
         B     IF090                  NO AMODE=ANY
IF040    CLI   MODESAVE,DIRAM24     AMODE=24?
         BH    IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF050    CLI   MODESAVE,DIRAM31     AMODE=31?
         BNE   IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF060    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BNE   IF997                     NO, DO ANY ELSE
         B     IF102                     YES, BRANCH
IF070    CLI   MODESAVE,DIRAM24     AMODE=24?
         BNH   IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF080    CLI   MODESAVE,DIRAM31     AMODE=31?
         BE    IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF090    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BE    IF997                     YES, DO ANY ELSE
         SPACE 3
IF102    CLI   #ALIAOPT,X'01'       "ALIAS" OR "NOALIAS"?
         BL    IF106                NEITHER, BRANCH
         BH    IF104                "NOALIAS", BRANCH
         TM    DIRFLAG,X'80'        "ALIAS" -- CORRECT?
         BO    IF106                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF104    TM    DIRFLAG,X'80'        ALIAS?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF106    CLI   #VSLKED,X'01'        "VSLKED" OR "NOVSLKED"?
         BL    IF110                NEITHER, BRANCH
         BH    IF108                "NOVSLKED", BRANCH
         TM    DIRATTR2,DIRAOSLE    "VSLKED" -- CORRECT?
         BO    IF110                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF108    TM    DIRATTR2,DIRAOSLE    VSLKED?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF110    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"?
         BL    IF113                NEITHER, BRANCH
         LA    R2,ZERO              ASSUME NOT AUTHORIZED
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF111                       NO, NOT AUTHORIZED
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF111                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF111    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"
         BH    IF112                "NOAUTH", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              "AUTH" -- CORRECT?
         BH    IF113                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF112    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF113                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              AUTHORIZED?
         BH    IF997                YES, DO ANY ELSE
         SPACE 3
IF113    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"?
         BL    IF116                NEITHER, BRANCH
         LA    R2,=X'01010101'      ASSUME NO APF ERROR
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF114                       NO, NO APF ERROR
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF114                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF114    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"
         BH    IF115                "NOAPFERR", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BE    IF997                YES, DO ANY ELSE
         B     IF116                NO, BRANCH
IF115    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, DO ANY ELSE
         SPACE 3
IF116    CLI   #PAGEOPT,X'01'       "PAGE" OR "NOPAGE"?
         BL    IF120                NEITHER, BRANCH
         BH    IF118                "NOPAGE", BRANCH
         TM    DIRATTR2,DIR2PAGA    "PAGE" -- CORRECT?
         BO    IF120                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF118    TM    DIRATTR2,DIR2PAGA    PAGE?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF120    CLI   #SSIOPT,X'01'        "SSI" OR "NOSSI"?
         BL    IF130                NEITHER, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF122                YES, BRANCH
         LA    R2,ZERO              ASSUME NO SSI
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BO    IF124                YES, NO SSI INFORMATION
         LA    R2,DIRUSER           POINT TO SSI INFORMATION
         B     IF124                CHECK FOR ANY SSI
IF122    LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BNO   IF124                NO, BRANCH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF128                NO, BRANCH
IF124    LA    R2,1(,R2)            ROUND TO START
         N     R2,=F'-2'                          OF NEXT HALFWORD
         CLC   ZERO,0(R2)           ZERO?
         BE    IF128                YES, NO SSI
         CLC   =F'-1',0(R2)         ALL ONES?
         BE    IF128                YES, NO SSI
IF126    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BNE   IF997                NO, BRANCH
         OC    #SSITEXT(4),#SSITEXT ANY SSI SEARCH?
         BZ    IF130                NO, BRANCH
         CLC   0(4,R2),#SSITEXT     THIS SSI?
         BE    IF130                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF128    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF130    TM    FLAGSCC,RECFMU       RECFM=U?
         BNO   IF140                NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#ATTRYES  ANY YES+0 ATTRIBUTES?
         BZ    IF132                NO, BRANCH
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF132    ICM   R1,B'0001',#ATTRYES+1 ANY YES+1 ATTRIBUTES?
         BZ    IF134                NO, BRANCH
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF134    ICM   R1,B'0001',#ATTRNO   NO+0 ATTRIBUTES
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 1
         ICM   R1,B'0001',#ATTRNO+1 NO+1 ATTRIBUTES
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 3
IF140    CLI   #USERIDO,X'01'       USERID, NOUSERID OR NOTUSERID?
         BL    IF144                NO, BRANCH
         LA    R1,L702              ASSUME DATA SET IS A LOAD MODULE
         TM    FLAGSCC,RECFMU       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BNO   IF143                NO, BRANCH
         CLI   #USERIDO,X'02'       "NOUSERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SR    R1,R1
         ICM   R1,B'0001',#USERIDL  ANY USERID FILTERING?
         BP    IF141                YES, BRANCH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF144                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
         SPACE 1
IF141    BCTR  R1,0                 MACHINE LENGTH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BNE   IF142                NO, BRANCH
         EX    R1,IF142             PARTIAL USERID MATCH?
         BNE   IF997                NO, DO ANY ELSE
         B     IF144                YES, BRANCH
         SPACE 1
IF142    CLC   #USERTXT(*-*),DIRSPFID <<EXECUTED>>
         EX    R1,*-6               PARTIAL USERID MATCH?
         BE    IF997                YES, DO ANY ELSE
         B     IF144                NO, BRANCH
         SPACE 1
IF143    CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF144    TM    #OPTOPT,X'02'        NEED ANY MAP OR HISTORY DATA?
         BNO   IF156                NO, BRANCH
         MVI   RLDCOUNT,X'FF'       ASSUME NO RLD/CONTROL DATA FOUND
         SPACE 1
         L     R15,=A(READESD)
         BALR  R14,R15              ANY ESD DATA?
         NOP   0                    NO, IGNORE
         SPACE 1
         ICM   R2,B'1110',#ZAPOPT       SAVE ZAP/USER/TRAN
         MVC   #ZAPOPT(3),=X'01010101'  CHECK ZAP/USER/TRAN
         MVC   LKEDDATE+2,X'FF'         NO LINKAGE-EDIT DATE FOUND
         L     R15,=A(READIDR)
         BALR  R14,R15              ANY IDR DATA?
         B     IF145                NO, BRANCH
         SPACE 2
         MVI   #LKEDOPT,0           ASSUME LINKAGE-EDITOR FOUND
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN  ANY LKED NAME CHECK?
         BZ    IF145                NO, BRANCH
         BCTR  R1,0                 MACHINE COMPARE LENGTH
         CLC   LKEDNAME(*-*),#LKEDTXT  <<EXECUTED>>
         EX    R1,*-6               THIS PARTIAL LKED NAME?
         BE    IF145                YES, BRANCH
         MVI   #LKEDOPT,1           NO, LINKAGE-EDITOR NOT FOUND
         SPACE 1
IF145    OC    #IFCREIF(6),#IFCREIF        ANY CREATION DATE RANGE?
         BZ    IF146                       NO, CONTINUE
         ICM   R1,B'0100',LKEDDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',LKEDDATE         MMDD OF MMDDYY
         CLM   R1,B'0111',#IFCREIF         DATA VALUE < FIRST YYMMDD?
         BL    IF997                       YES, DO ANY ELSE
         CLM   R1,B'0111',#IFCREIF+3       DATA VALUE > SECOND YYMMDD?
         BH    IF997                       YES, DO ANY ELSE
         SPACE 2
IF146    STCM  R2,B'1110',#ZAPOPT   RESTORE ZAP/USER/TRAN
         USING ESDENTRY,R2
         LA    R2,#ESDPTR           SCAN ESD CHAIN
         SPACE 1
IF148    ICM   R2,B'1111',ESDLINK   ANY MORE ESD ENTRIES?
         BZ    IF154                NO, BRANCH
         CLI   ESDTYPE,CODESD       CSECT ESD ENTRY?
         BNE   IF148                NO, BRANCH
         SPACE 1
         USING IDRENTRY,R3
         LA    R3,#IDRPTR           SCAN IDR CHAIN
         SPACE 1
IF150    ICM   R3,B'1111',IDRLINK   ANY MORE IDR ENTRIES?
         BZ    IF148                NO, BRANCH
         CLC   ESDID,IDRESDID       THIS CSECT IDENTIFIER?
         BNE   IF150                NO, BRANCH
         SPACE 1
         MVI   #MODOPT,0            YES, MODULE FOUND
         CLI   IDRTYPE,IDRTRAN      ZAP RECORD?
         BNL   IF152                NO, BRANCH
         MVI   #ZAPOPT,0            YES, ZAP RECORDS MATCH
         OC    #IFMODIF(6),#IFMODIF       ANY MODIFICATION CHECK?
         BE    IF152                      NO, BRANCH
         ICM   R1,B'0100',IDRDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',IDRDATE         MMDD OF MMDDYY
         CLM   R1,B'0111',#IFMODIF        DATA VALUE < FIRST YYMMDD?
         BL    IF152                      YES, NO MATCH
         CLM   R1,B'0111',#IFMODIF+3      DATA VALUE > SECOND YYMMDD?
         BH    IF152                      YES, NO MATCH
         XC    #IFMODIF(6),#IFMODIF       A MODIFICATION RECORD MATCHES
         SPACE 1
IF152    CLI   IDRTYPE,IDRTRAN      IDR RECORD TYPE:
         BNH   *+8
         MVI   #USEROPT,0              **USER
         BNE   *+8
         MVI   #TRANOPT,0              **TRANSLATOR
         B     IF150
         DROP  R2,R3
IF154    OC    #IFMODIF(6),#IFMODIF ANY CHANGES MATCH?
         BNZ   IF997                NO, DO ANY ELSE CONDITIONS
         SPACE 1
IF156    OC    #ZAPOPT(5),#ZAPOPT   ZAP/USER/TRAN/LKED/MODULE FOUND?
         BNZ   IF997                NO, DO ANY ELSE
         SPACE 3
         TM    DIRATTR2,DIRAOSLE    OS/VS LINKAGE EDITOR?
         BO    *+10                 YES, BRANCH
         MVC   RLDCOUNT(1),DIRATTR4 NO, WANT MATCHING RLD/CONTROL
         CLI   #RLDERR,X'01'        "RLDERR" OR "NORLDERR"
         BL    IF160                NEITHER, BRANCH
         BH    IF158                "NORLDERR", BRANCH
         CLC   DIRATTR4(1),RLDCOUNT "RLDERR" -- CORRECT?
         BNE   IF160                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF158    CLC   DIRATTR4(1),RLDCOUNT "NORLDERR" -- CORRECT?
         BNE   IF997                YES, DO ANY ELSE
         SPACE 3
IF160    NI    #IFABOVE,FF-X'01'    ASSUME SIZE IS NOT AVAILABLE
         TM    #IFABOVE,X'80'       ANY ABOVE CODED?
         BO    IF164                YES, BRANCH
         TM    #IFBELOW,X'80'       ANY BELOW CODED?
         BZ    IF170                NO, BRANCH
         SPACE 1
IF164    SR    R1,R1
         ICM   R1,B'0111',DIRSTORE  MODULE SIZE
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF168                YES, BRANCH
         TM    DIRFLAG,X'0F'        SPF STATISTICS PRESENT?
         BNO   IF180                NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ   RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   IF180                NO, BRANCH
         CLI   DIRSPFCD,0           00 OF OTHER 00YYDDDF ZERO?
         BNZ   IF180                NO, BRANCH
         LH    R1,DIRSPFSI
         SPACE 1
IF168    OI    #IFABOVE,X'01'       SIZE IS AVAILABLE
         ST    R1,#IFLEN            LENGTH OF THE MEMBER
         SPACE 1
IF170    TM    #OPTOPT,X'04'        NEED TO READ THE MEMBER?
         BNO   IF300                NO, BRANCH
         SPACE 1
IF180    SR    R6,R6                COUNT OF INPUT LOGICAL RECORDS
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BNL   IF242                YES, DONE
         OI    ##ADRPA#,$Q          QUIET MODE READ IS REQUIRED
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         SPACE 3
IF204    L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)             PROCESS RETURN CODE
         B     IF208                  00 - SUCCESSFUL READ
         B     IF240                  04 - END OF MEMBER
         B     IF240                  08 - END OF DATA SET
         B     IF242                  12 - I/O ERROR
         SPACE 2
IF208    LR    R3,R0                BUFFER START
         L     R5,LS                BLOCK LENGTH
         CH    R5,BLKSI             BLKSIZE VALID?
         BNH   IF220                YES, BRANCH
         OI    #OPTOPT,X'20'        NO, INDICATE A BLOCKSIZE ERROR
         SPACE 1
IF220    TM    FLAGSCC,RECFMF       FIXED FORMAT?
         BNO   IF222                NO, BRANCH
         LH    R14,LRECL            CURRENT LRECL
         LR    R0,R5                BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14               BLKSIZE/LRECL
         LTR   R0,R0                ANY REMAINDER?
         BZ    IF222                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (BLKSIZE/LRECL)
         SPACE 1
IF222    LR    R1,R5
         AR    R5,R3                END OF BUFFER
         BCTR  R5,0                 END OF BUFFER -1
         TM    FLAGSCC,RECFMF       RECFM=F?
         LH    R4,LRECL
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         LR    R4,R1
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, ASSUME RECFM=U
         AH    R3,=H'4'             SKIP BLOCK LENGTH WORD
         B     IF226
         SPACE 3
IF224    BXLE  R3,R4,IF226          GET THE NEXT LOGICAL RECORD
         B     IF204                END OF BLOCK, BRANCH
         SPACE 2
IF226    TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)     RECORD LENGTH +4
         LR    R14,R4               RECORD LENGTH +4
         AH    R3,=H'4'             SKIP RECORD LENGTH WORD
         SH    R4,=H'4'             RECORD LENGTH VALID?
         BNM   IF228                YES, BRANCH
         SR    R4,R4                NO, USE LENGTH 0
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (SHORT RECORD)
         B     IF224                SKIP THE RECORD
         SPACE 2
IF228    LA    R6,1(,R6)            ANOTHER OUTPUT RECORD
         CH    R14,LRECL            VALID LRECL VALUE?
         BNH   IF224                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (LONG RECORD)
         B     IF224
         SPACE 2
IF240    TM    DIRFLAG,DIR2TTR+DIR1TTR    ANY USER TTR?
         BZ    IF244                      NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',DIRSTART        TTR ACTUALLY INPUT?
         BNH   IF242                      NO, I/O ERROR
         TM    DIRFLAG,DIR2TTR            A SECOND TTR TOO?
         BZ    IF244                      NO, BRANCH
         CLM   R0,B'1110',DIRNOTE         TTR ACTUALLY INPUT?
         BH    IF244                      YES, BRANCH
         SPACE 1
IF242    OI    #OPTOPT,X'40'        PERMANENT I/O ERROR
         SPACE 2
IF244    LTR   R6,R6                ANY RECORDS?
         BNZ   IF246                YES, BRANCH
         OI    #OPTOPT,X'08'        NO, MARK AS NULL
         SPACE 3
IF246    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE READ
         CLI   #IOERROR,X'01'       "IOERROR" OR "NOIOERROR"?
         BL    IF260                NEITHER, BRANCH
         BH    IF248                "NOIOERROR", BRANCH
         TM    #OPTOPT,X'40'        "IOERROR" -- CORRECT?
         BO    IF260                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF248    TM    #OPTOPT,X'40'        IOERROR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF260    CLI   #BLOCKER,X'01'       "BLOCKERR" OR "NOBLOCKERR"?
         BL    IF266                NEITHER, BRANCH
         BH    IF262                "NOBLOCKERR", BRANCH
         TM    #OPTOPT,X'20'        "BLOCKERR" -- CORRECT?
         BO    IF266                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF262    TM    #OPTOPT,X'20'        BLOCKERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF266    CLI   #LRECLER,X'01'       "LRECLERR" OR "NOLRECLERR"?
         BL    IF280                NEITHER, BRANCH
         BH    IF268                "NOLRECLERR", BRANCH
         TM    #OPTOPT,X'10'        "LRECLERR" -- CORRECT?
         BO    IF280                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF268    TM    #OPTOPT,X'10'        LRECLERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF280    CLI   #NULL,X'01'          "NULL" OR "NONULL"?
         BL    IF300                NEITHER, BRANCH
         BH    IF282                "NONULL", BRANCH
         TM    #OPTOPT,X'08'        "NULL" -- CORRECT?
         BO    IF300                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF282    TM    #OPTOPT,X'08'        NULL?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF300    TM    #IFABOVE,X'01'         SIZE AVAILABLE?
         BNO   *+8                    NO, BRANCH
         L     R6,#IFLEN              YES, USE THAT LENGTH
         SPACE 1
         TM    #IFABOVE,X'80'         ABOVE CHECK?
         BNO   IF320                  NO, BRANCH
         CLM   R6,B'0111',#IFABOVE+1  ABOVE THE MINIMUM VALUE?
         BNH   IF997                  NO, DO ANY ELSE
         SPACE 1
IF320    TM    #IFBELOW,X'80'         BELOW CHECK?
         BNO   IF360                  NO, BRANCH
         CLM   R6,B'0111',#IFBELOW+1  BELOW THE MAXIMUM VALUE?
         BNL   IF997                  NO, DO ANY ELSE
         SPACE 3
IF360    CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NO, BRANCH
         SPACE 1
         SR    R3,R3                RESULT REGISTER
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),DIRNAME
         SPACE 1
IF382    BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,IF382                                  NON-BLANK
         SPACE 1
         CLI   DIRNAME,C'0'        NUMERIC FIRST CHARACTER?
         BL    *+8                 NO, BRANCH
         LA    R3,1                YES, NON-STANDARD NAME
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         BZ    *+8                 YES, BRANCH
         LA    R3,1                NO, NON-STANDARD NAME
         SPACE 1
         CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NEITHER, BRANCH
         BH    IF384                "NONAMEERR", BRANCH
         LTR   R3,R3                "NAMEERR" -- CORRECT?
         BP    IF390                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF384    LTR   R3,R3                NAMEERR?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK EACH MODULE FOR LINKAGE EDIT ATTRIBUTE CONFLICTS IF REQUIRED
IF390    CLI   #LKEDERR,X'01'              CONFLICT CHECK REQUESTED?
         BL    IF400                       NO, BRANCH
         SPACE 1
         TM    DIRATTR,ATTRRENT            REENTRANT?
         BNO   IF392                       NO, BRANCH
         TM    DIRATTR,ATTRREUS            REUSABLE?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF392    TM    DIRATTR+1,ATTRSYMS+ATTRNE   TEST AND NOT EDIT
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTRREUS+ATTRSCTR   REUSABLE AND SCATTER
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTROVLY            OVERLAY ATTRIBUTE?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR,ATTRRENT++ATTRREUS+ATTRSCTR  RENT/REUS/SCTR?
         BNZ   IF399                                YES, CONFLICT
         TM    DIRATTR+1,ATTRREFR          REFRESHABLE?
         BO    IF399                       YES, CONFLICT FOUND
         TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 24?
         BNZ   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF394    TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF397                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   IF397                       NO, BRANCH
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 31?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF397    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BE    IF997                       YES, DO ANY ELSE
         B     IF400                       NO, NONE FOUND
         SPACE 1
IF399    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BNE   IF997                       NO, DO ANY ELSE
         SPACE 3
* LOAD EACH MODULE IF REQUIRED
IF400    CLI   #LOADERR,X'01'      LOAD CHECK REQUESTED?
         BL    IF420               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,IF402            RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         B     IF404
         SPACE 1
IF402    LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   RECOVER,1           LOAD DID NOT WORK
         SPACE 1
IF404    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE LOAD
         CLI   #LOADERR,X'01'       "LOADERR" OR "NOLOADERR"?
         BL    IF420                NEITHER, BRANCH
         BH    IF406                "NOLOADERR", BRANCH
         CLI   RECOVER,1            "LOADERR" -- CORRECT?
         BE    IF420                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF406    CLI   RECOVER,1            LOAD ERROR?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
IF420    CLI   #SPFEDIT,X'01'      INUSE CHECK REQUESTED?
         BL    IF440               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE SPF USE CHECK
         SR    R3,R3               ASSUME NOT IN USE
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         LA    R3,1                YES, MARK AS SUCH
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE
         SPACE 1
         CLI   #SPFEDIT,X'01'       "SPFEDIT" OR "NOSPFEDIT"?
         BL    IF440                NEITHER, BRANCH
         BH    IF422                "NOSPFEDIT", BRANCH
         LTR   R3,R3                "SPFEDIT" -- CORRECT?
         BP    IF440                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF422    LTR   R3,R3                SPFEDIT?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF440    CLI   #AALIAS,X'01'        APPARENT ALIAS MEMBER CHECK?
         BL    IF450                NO, BRANCH
         SR    R3,R3                ASSUME NOT AN APPARENT ALIAS
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF444                YES, CANNOT BE AN APPARENT ALIAS
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF442    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF444                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF442                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF442                NO, BRANCH
         CLC   DIRNAME,MEMNAME      SAME MEMBER?
         BE    IF442                YES, BRANCH
         LA    R3,1                 AN APPARENT ALIAS MEMBER
         SPACE 1
IF444    CLI   #AALIAS,X'01'        "APPARENTALIAS" OR "NOAPPAREN.."?
         BL    IF450                NEITHER, BRANCH
         BH    IF446                "NOAPPARENTALIAS", BRANCH
         LTR   R3,R3                "APPARENTALIAS" -- CORRECT?
         BP    IF450                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF446    LTR   R3,R3                APPARENT ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF450    CLI   #HASALIA,X'01'       HAS ALIAS MEMBER CHECK?
         BL    IF460                NO, BRANCH
         SR    R3,R3                ASSUME IT HAS NO ALIASES
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF454                YES, CANNOT HAVE ALIASES
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF452    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF454                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BNO   IF452                NO, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF452                NO, BRANCH
         LA    R3,1                 YES, HAS AN ALIAS
         SPACE 1
IF454    CLI   #HASALIA,X'01'       "HASALIAS" OR "NOHASALIAS"?
         BL    IF460                NEITHER, BRANCH
         BH    IF456                "NOHASALIAS", BRANCH
         LTR   R3,R3                "HASALIAS" -- CORRECT?
         BP    IF460                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF456    LTR   R3,R3                HAS ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF460    CLI   #ORPHAN,X'01'        ORPHAN MEMBER CHECK?
         BL    IF470                NO, BRANCH
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BNO   IF463                NO, CANNOT BE AN ORPHAN
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         LA    R3,1                 ASSUME IT IS AN ORPHAN
         SPACE 1
IF462    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF464                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF462                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF462                NO, BRANCH
IF463    SR    R3,R3                NOT AN ORPHAN
         SPACE 1
IF464    CLI   #ORPHAN,X'01'        "ORPHAN" OR "NOORPHAN"?
         BL    IF470                NEITHER, BRANCH
         BH    IF466                "NOORPHAN", BRANCH
         LTR   R3,R3                "ORPHAN" -- CORRECT?
         BP    IF470                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF466    LTR   R3,R3                ORPHAN?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF470    CLI   #RLD0,X'01'          "RLDZERO" OR "NORLDZERO"
         BL    IF476                NEITHER, BRANCH
         BH    IF472                "NORLDZERO", BRANCH
         CLI   DIRATTR4,0           "RLDZERO" -- CORRECT?
         BE    IF476                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF472    CLI   DIRATTR4,0           "NORLDZERO" -- CORRECT?
         BE    IF997                YES, DO ANY ELSE
         SPACE 1
IF476    TM    FLAGSCC,RECFMU            LOAD MODULE?
         BO    IF490                     YES, BRANCH
         CLI   #IFCREIF+2,0              ANY CREATION DATE?
         BE    IF480                     NO, CHECK FOR MODIFICATION
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         CLC   #IFCREIF(3),DIRSPFCR+1    HIGHER THAN DIRECTORY?
         BH    IF997                     YES, DO ANY ELSE
         CLC   #IFCREIF+3(3),DIRSPFCR+1  LOWER THAN DIRECTORY?
         BL    IF997                     YES, DO ANY ELSE
         SPACE 1
IF480    CLI   #IFMODIF+2,0              ANY MODIFICATION DATE?
         BE    IF490                     NO, CONTINUE
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         CLC   #IFMODIF(3),DIRSPFCD+1    HIGHER THAN DIRECTORY?
         BH    IF997                     YES, DO ANY ELSE
         CLC   #IFMODIF+3(3),DIRSPFCD+1  LOWER THAN DIRECTORY?
         BL    IF997                     YES, DO ANY ELSE
         SPACE 3
IF490    DS    0H
         SPACE 1
IF996    MVC   DELAYMSG+25(4),=C'THEN'
         L     R2,#ACTIONT          THEN ACTION
         B     IF998
         SPACE 1
IF997    MVC   DELAYMSG+25(4),=C'ELSE'
         L     R2,#ACTIONE          ELSE ACTION
         SPACE 1
IF998    LTR   R2,R2                ANY ASSOCIATED ACTION?
         BNP   NEWCMD               NO, BRANCH
         TM    ##ADRPA#-##SUBCOM(R2),$D  SUPRESS DEFAULT MESSAGE?
         BO    IF999                     YES, BRANCH
         SPACE 1
         MESSAGE MSGBLANK
         TM    FLAGSAA,FDELAYM      ANY HEADER MESSAGE?
         BNO   IF999                NO, BRANCH
         XI    FLAGSAA,FDELAYM      NO MORE MESSAGE
         MVC   DELAYMSG+30(8),0(R2) SUBCOMMAND NAME
         MESSAGE DELAYMSG           OUTPUT THE HEADER MESSAGE
         SPACE 1
IF999    MVC   ##ANSWER(LISUBS),ISUBS INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(PTW),0(R2)    CHANGE THE SUBCOMMAND
         OI    FLAGSGG,FOUTSOME       A MATCH WAS MADE
         B     CALLCMDZ               CALL THE SECONDARY SUBCOMMAND
IFLA$ATT DC    A($ATT)                DEFAULT THEN ACTION
         TITLE 'P D S  --  PDS LIST, DIRENTRY, FIND, REPLACE   6/10/85'
***********************************************************************
***      LIST SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      DIRENTRY SUBCOMMAND   ADDED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      FIND SUBCOMMAND    MODIFIED BY BRUCE LELAND -- APR,  1985  ***
***                                                                 ***
***      REPLACE SUBCOMMAND    ADDED BY BRUCE LELAND -- APR,  1985  ***
***********************************************************************
*
LIST     CSECT
         USING *,R8
         MVI   ##HELOFF,C'L'       LIST SUBCOMMAND
         B     LIST010
*
         SPACE 1
DIRENTRY L     R8,$LIS+8           BASE REGISTER FOR LIST
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, ERROR
         SPACE 1
         MVI   ##HELOFF,C'D'       DIRENTRY SUBCOMMAND
         IC    R1,DIRFLAG
         N     R1,=XL4'0000001F'   NUMBER OF HALFWORDS
         LA    R5,12(R1,R1)        LENGTH IN BYTES
         LA    R6,DIRNAME          START OF DIRECTORY ENTRY
         LA    R4,16               DUMP FORMAT, WIDTH=16
         MVI   MTHIGHL+4,2
         MVC   MSGTEXT1(3),L143$2  DIRECTORY DUMP HEADER
         MVC   INSERT#1(8),DIRNAME
         CVD   R5,DOUBLE
         MVC   INSERT#2-2(4),=X'40202120'
         ED    INSERT#2-2(4),DOUBLE+6
         AR    R5,R6
         BCTR  R5,0
         LA    R3,LIST600          PROCESSING ROUTINE
         BR    R3
         SPACE 2
*
FIND     L     R8,$LIS+8           BASE REGISTER FOR LIST
         MVI   ##HELOFF,C'F'       FIND SUBCOMMAND
         MVI   ##HELOFF+3,C'D'     NO OUTPUT YET
         B     LIST008
         SPACE 2
*
REPLACE  L     R8,$LIS+8           BASE REGISTER FOR LIST
         MVI   ##HELOFF,C'R'       REPLACE SUBCOMMAND
         MVI   ##HELOFF+3,C'D'     NO OUTPUT YET
         MVC   DSNMEMQ(8),DIRNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         B     NEWCMD              YES, OUTPUT AN IN-USE MESSAGE
         SPACE 1
         CLI   REPLTH+1,0          REPLACEMENT STRING EVER CODED?
         BNE   LIST002             YES, BRANCH
         LA    R1,L772             NO, CHECK WHICH MESSAGE TO OUTPUT
         CLI   FINDLTH+1,0         SEARCH STRING EVER CODED?
         BNE   MSGNEWXX            YES, TERMINATE THIS GROUP
         SPACE 1
LIST002  MVC   FLAGSDD(1),HFLAGSDD INITIALIZE FLAGSDD
         TM    DSORG,DS1DSGPO        DSORG=PO?
         BNO   LIST004               NO, BRANCH
         TM    FLAGSCC,RECFMU        LOAD MODULE?
         BNO   LIST004               NO, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BM    LIST004                          YES, BRANCH
         MVI   FLAGSDD,LDUMP                    NO, USE LDUMP
LIST004  TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BNM   LIST006                          NO, BRANCH
         LA    R1,L773
         CLC   FINDLTH(2),REPLTH   EQUAL LENGTH STRINGS?
         BNE   MSGNEWXX            NO, TERMINATE THIS GROUP
         SPACE 1
LIST006  TM    #REPFLAG,X'01'      REPLACE SPECIFIED?
         BNO   LIST008             NO, BRANCH
         BAL   R2,SHUTSTW2         CLOSE AND FREE THE STOW DCB
         SPACE 1
LIST008  LA    R1,L770
         CLI   FINDLTH+1,0         STRING EVER CODED?
         BZ    MSGNEWXX            NO, TERMINATE THIS GROUP
         OI    FLAGSGG,FOUTSUB     SUBCOMMAND GAINED CONTROL
         XC    WORKTBL,WORKTBL     RESET THE TRT TABLE
         SR    R15,R15
         IC    R15,FINDSTR         FIRST CHARACTER OF NEW STRING
         LA    R1,WORKTBL(R15)     ADDRESS OF NEW TRT BYTE
         MVI   0(R1),X'77'         SET TRT BYTE TO NON-ZERO
         SPACE 2
LIST010  MVC   FLAGSDD(1),HFLAGSDD INITIALIZE FLAGSDD
         OC    #MAXIN,#MAXIN       ANY INPUT DESIRED?
         BZ    LIST910             NO, BRANCH
         SPACE 2
         TM    DSORG,DS1DSGPO        DSORG=PO?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSCC,RECFMU        LOAD MODULE?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BM    LIST020                          YES, BRANCH
         MVI   FLAGSDD,LDUMP                    NO, USE LDUMP
LIST020  TM    FLAGSDD,LBLOCK+LDUMP  LBLOCK OR LDUMP FORMAT?
         BZ    LIST090               NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD LIST
         L     R15,=A(READESD)     ESD SYMBOL ROUTINE
         BALR  R14,R15             ANY SYMBOLS?
         B     *+8                 NO, "NO EXTERNAL SYMBOLS"
         B     LIST030             YES, BRANCH
         SPACE 2
         MVC   INSERT#1(8),DIRNAME MEMBER NAME IN ERROR
         TSMSG L704$1              OUTPUT AN ERROR MESSAGE
         B     LIST090             CONTINUE, BUT NO CSECT IDENTIFIERS
         SPACE 2
         USING ESDENTRY,R2
*  LOOPS TO SET MAXIMUM LENGTH FIELDS FOR LABEL REFERENCE ENTRIES
*  AND TO ASSOCIATE ENTRY RECORDS WITH THE APPROPRIATE CSECT NAMES
LIST030  OI    #LISTFLG,X'01'      CSECT OPERATION FLAG
         ICM   R2,B'1111',ESDLINK  ANY MORE ENTRIES?
         BZ    LIST060             NO, BRANCH
         MVI   ESDSEG#,0           CLEAR THE SEGMENT NUMBER
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   LIST030             NO, BRANCH
         LR    R1,R2
         SPACE 1
LIST040  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BZ    LIST060
         CLI   ESDTYPE-ESDENTRY(R1),CODELR  ASSOCIATED EXTERNAL LABEL?
         BNE   LIST030                      NO, BRANCH
         MVI   ESDSEG#-ESDENTRY(R1),0       CLEAR THE SEGMENT NUMBER
         ST    R2,ESDCSECT-ESDENTRY(R1)     SAVE POINTER TO NAME
         SR    R14,R14
         LH    R0,ESDID                     RELATIVE ESDID OF CSECT
         STH   R0,ESDID-ESDENTRY(R1)        SAVE FOR ENTRY
         L     R0,ESDADDR-1                 BASE START ADDRESS
         ST    R0,ESDMAIN-ESDENTRY(R1)      SAVE MAIN OFFSET
         A     R0,ESDLEN-1                  BASE MAXIMUM REFERENCE
         ICM   R14,B'0111',ESDADDR-ESDENTRY(R1)
         SR    R0,R14
         ST    R0,ESDLEN-ESDENTRY-1(R1)     MAXIMUM SYMBOL LENGTH
         B     LIST040
         SPACE 2
*  LOOP TO FILTER THE SYMBOL NAMES
LIST060  LA    R2,#ESDPTR          START OF ESD LIST
         SR    R3,R3
         ICM   R3,B'0001',#MODLEN  ANY FILTERING?
         BZ    LIST090             NO, BRANCH
         BCTR  R3,0                MACHINE LENGTH
         SPACE 1
LIST070  LR    R1,R2
         ICM   R2,B'1111',ESDLINK  END OF LIST?
         BZ    LIST090             YES, BRANCH
         CLC   ESDNAME(*-*),#MODTXT  <<EXECUTED>>
         EX    R3,*-6              MATCH THE PARTIAL NAME?
         BE    LIST070             YES, BRANCH
         MVC   ESDLINK-ESDENTRY(4,R1),ESDLINK
         LR    R2,R1                    DROP THIS SYMBOL
         B     LIST070
         SPACE 1
LIST090  MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
LIST100  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     LIST120                    00 - GOOD READ
         B     LIST110                    04 - END OF MEMBER
         B     LIST900                    08 - END OF DATA SET
         B     LIST910                    12 - I/O ERROR
         SPACE 1
LIST110  TM    DSORG,DS1DSGIS           ISAM DATA SET?
         BNO   LIST900                  NO, BRANCH
         TSMSG L005                     YES, OUTPUT A MESSAGE
         B     LIST100                  CONTINUE
         SPACE 1
LIST120  NI    #LISTFLG,FF-X'20'        TURN OFF THE CSECT DATA FLAG
         L     R5,LS                    CURRENT LENGTH
         LR    R6,R0                    START OF BUFFER
         LR    R1,R5                    SAVE BLOCKSIZE FOR LATER
         LM    R14,R15,#LISTN0          SAVE IF NOT PHYSICAL BLOCKS
         L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R5                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         TM    FLAGSDD,BLOCK            BLOCK OUTPUT DESIRED?
         BO    LIST400                  YES, BRANCH
         TM    FLAGSDD,DUMP             DUMP OUTPUT DESIRED?
         BO    LIST420                  YES, BRANCH
         STM   R14,R15,#LISTN0          NOT PHYSICAL BLOCKS
         AR    R5,R6                    ADDRESS OF END OF BUFFER
         BCTR  R5,0                     END OF BUFFER ADDRESS
         LA    R3,LIST200               ASSUME FIXED OR UNDEFINED
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL                 GET RECORD LENGTH
         BOR   R3                       YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1                    BLOCK LENGTH
         BOR   R3                       YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNOR  R3                       NO, PROCESS LIKE RECFM=U
         SPACE 1                        SET END OF BUFFER ADDRESS
         AH    R6,=H'4'                 RECFM=V; START OF LRECL'S
         LA    R3,LIST150               VARIABLE LEN RECORD PROCESSOR
         SPACE 2
LIST150  SR    R4,R4
         ICM   R4,B'0011',0(R6)         RECORD LENGTH +4
         AH    R6,=H'4'                 SKIP OVER HEADER
         SH    R4,=H'4'                 SUBTRACT HEADER LENGTH
         BP    LIST200                  BRANCH IF RECORD EXISTS
         SR    R4,R4                    NO, RESET LENGTH TO ZERO
         SPACE 1
LIST160  LR    R0,R4                    PREVIOUS RECORD LENGTH
         A     R0,#LISTN1               OFFSET IN CURRENT RECORD
         ST    R0,#LISTN1               UPDATE
         ICM   R0,B'1111',#MAXFIND      DONE WITH FIND/REPLACE?
         BP    LIST166                  NO, BRANCH
         TM    #REPFLAG,X'01'           WRITE SPECIFIED?
         BNO   NEWCMD                   NO, BRANCH
         TM    #REPFLAG,LISTUPDE        UPDATE ERROR?
         BO    NEWCMD                   YES, BRANCH
         TM    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK?
         BO    LIST170                  YES, BRANCH
         B     NEWCMD
         SPACE 1
LIST166  BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         CLI   ##HELOFF,C'D'            DIRENTRY SUBCOMMAND?
         BE    NEWCMD                   YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BZ    LIST170                  NO, GET A NEW BLOCK
         LM    R3,R6,#LFSAVE4           RESTORE R3-R6
         BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         SPACE 3
*  REWRITE ANY UPDATED RECORDS FOR REPLACE
LIST170  CLI   ##HELOFF,C'R'            REPLACE SUBCOMMAND?
         BNE   LIST100                  NO, BRANCH
         TM    #REPFLAG,X'01'           WRITE SPECIFIED?
         BNO   LIST100                  NO, BRANCH
         TM    #REPFLAG,LISTUPDE        UPDATE ERROR?
         BO    LIST198                  YES, BRANCH
         TM    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK?
         BNO   LIST198                  YES, BRANCH
         XI    #REPFLAG,LISTUPDR        NO DATA UPDATED NOW
         SPACE 1
         TM    #REPFLAG,LISTUPDO        OPEN FOR UPDATE?
         BO    LIST190                  YES, BRANCH
         OI    #REPFLAG,LISTUPDO        OPEN FOR UPDATE NOW
         SPACE 1
         OI    FLAGSCC,FUPDATE          MARK FOR STOW OPEN ROUTINE
         BAL   R2,OPENSTOW              OPEN OUTPUT DCB AND RESERVE
         B     NEWCMD                   COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE OBTAINED
         LH    R0,=H'32760'             MAXIMUM BUFFER SIZE
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#LISTBUF              POINTER TO BUFFER AREA
         SPACE 1
* UPDATE IDR RECORDS FOR LOAD MODULES IF REQUIRED
*  IDR FOR SUPERZAP TAKES THE FOLLOWING FORM:
*        BYTE  0.....'80' FOR ID
*        BYTE  1.....LENGTH OF IDR (USUALLY X'FA' = 250.)
*        BYTE  2.....WHO'S IDR IT IS (X'01' FOR SUPERZAP AND PDS)
*        BYTE  3.....NUMBER OF 13-BYTE IDR DATA ENTRIES (BITS 2-7)
*        BYTES 4-16, 17-29,... IDR DATA ENTRY AS FOLLOWS:
*              BYTES 0-1 --- ESD ID IN BINARY (FIRST IF NOT KNOWN)
*              BYTES 2-4 --- DATE ('YYDDDF')
*              BYTES 5-12 -- INFO ('USERID' WHERE USERID=TSO USERID)
         SPACE 2
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   LIST190                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   LIST190                 NO, BRANCH
         ICM   R0,B'0111',DIRTTR       TTR TO START WITH
         SLL   R0,8                    Z OF TTRZ IS ZERO
         ST    R0,DOUBLE               SAVE TTRZ TEMPORARILY
         SPACE 1
         LA    R0,DOUBLE               POINT TO TTRZ VALUE
         POINT STOWDCB,(0)             POINT TO BLOCK TO BE UPDATED
         SPACE 1
LIST174  LH    R0,=H'32760'            MAXIMUM BUFFER SIZE
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,#LISTBUF             START OF BUFFER
         SPACE 1
         READ  FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
         LR    R1,R2
         TM    0(R1),X'01'              CONTROL RECORD OF SOME KIND?
         BO    LIST190                  YES, CANNOT FIND THE ZAP IDR
         SPACE 1
         CLI   0(R1),X'20'              CESD?
         BNE   LIST180                  NO, BRANCH
         OC    #LISTIDU(2),#LISTIDU     ANY ESDID?
         BNZ   LIST174                  YES, BRANCH
         LH    R3,4(R2)                 FIRST ESDID
LIST178  LA    R2,16(,R2)               NEXT ESD ENTRY
         LA    R3,1(,R3)                NEXT ESDID
         CLI   0(R2),0                  CSECT ENTRY?
         BNE   LIST178                  NO, BRANCH
         OC    5(3,R2),5(R2)            VALID LENGTH?
         BZ    LIST178                  NO, BRANCH
         BCTR  R3,0                     LESS ONE
         STH   R3,#LISTIDU              UPDATE THE ESDID
         B     LIST174
         SPACE 1
LIST180  CLI   0(R1),X'40'              TEST SYMBOLS?
         BE    LIST174                  YES, READ ANOTHER
         CLI   0(R1),X'80'              IDR?
         BNE   LIST190                  NO, BYPASS IDR MARKING
         TM    2(R1),X'01'              YES - BUT DONE BY SUPERZAP?
         BNO   LIST174                  NO - FORGET THIS ONE
         SR    R2,R2                    CLEAR WORK REG
         IC    R2,3(R1)                 PICK UP # OF IDR DATA ENTRIES
         LR    R4,R2                    SAVE IT FOR LATER
         N     R2,=XL4'0000003F'        WE JUST WANT BITS 2-7
         MH    R2,=H'13'                EACH ENTRY IS 13 BYTES LONG
         LA    R2,3(R2)                 IDR HDR IS 3 BYTES LONG
*  SO NOW R2 HAS AN OFFSET TO THE NEXT AVAILABLE POSITION IN THE IDR
         CLM   R2,B'0001',1(R1)         AT END OF THE IDR RECORD?
         BH    LIST174                  YES, BRANCH
         AR    R2,R1                    NEXT IDR POSITION
         LA    R4,1(R4)                 INCREMENT # OF IDR ENTRIES...
         STC   R4,3(R1)                 AND UPDATE
         MVC   1(2,R2),#LISTIDU         MOVE IN ESDID
         TIME  ,                        GET THE DATE
         STCM  R1,B'0111',3(R2)         SAVE YYDDDF IN IDR DATA FIELD
         L     R1,ADDRPSCB              ADDRESS OF THE PSCB
         MVC   6(7,R2),PSCBUSER-PSCB(R1)  LOGON USERID
         MVI   6+7(R2),X'40'              FOLLOWING BLANK
         SPACE 1
         L     R2,#LISTBUF             START OF BUFFER
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
LIST190  LH    R1,#REPCNT
         LA    R1,1(,R1)
         STH   R1,#REPCNT
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         ST    R0,DOUBLE                  SAVE TTRZ TEMPORARILY
         SPACE 1
         POINT STOWDCB,DOUBLE             POINT TO BLOCK TO BE UPDATED
         SPACE 1
         L     R0,LS                      RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB UPDATE IN THE DCB
         L     R2,#LISTBUF                START OF BUFFER
         READ  FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
         L     R2,EXCPBUFF                EXCHANGE FOR THE UPDATED DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
LIST198  CLI   OPENLIST,X'7C'                    REPLACE TERMINATION?
         BNE   LIST100                           NO, BRANCH
         MVI   MTHIGHL,L'MSGBLKU-1
         MVC   INSERT#1-1(L'MSGBLKU),MSGBLKU
         LH    R0,#REPCNT
         CVD   R0,DOUBLE
         ED    INSERT#1-1(10),DOUBLE+4
         TSMSG L145$1
         BAL   R2,SHUTSTW2                       FORCE STOW DCB CLOSE
         B     NEWCMD
         SPACE 3
LIST200  L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R4                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         STM   R3,R6,#LFSAVE4             SAVE FOR LBLOCK/LDUMP
         L     R0,#SKIPREC
         S     R0,=F'1'                   SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST160                    YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                   SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                    YES, BRANCH
LIST210  LR    R1,R4                      LENGTH OF DATA AREA
         TM    FLAGSDD,LBLOCK             LBLOCK?
         BO    LIST400                    YES, BRANCH
         TM    FLAGSDD,LDUMP              LDUMP?
         BO    LIST420                    YES, BRANCH
         LA    R0,8                       WIDTH OF LINE NUMBER FIELD
         LR    R15,R6                     MOVE START POSITION
         LR    R14,R15                    LINE NUMBER FOR RECFM=V OR U
         A     R15,#SKIPCOL               ADJUST START FOR START COLUMN
         S     R1,#SKIPCOL                ADJUST LEN FOR START COLUMN
         TM    FLAGSDD,NONUM+DONONUM      NONUM FORMAT?
         BNZ   LIST330                    YES, BRANCH
         AR    R15,R0                     ADJUST START FOR RECFM=V OR U
         SR    R1,R0                      ADJUST LENGTH, RECFM=V U OR F
         TM    FLAGSCC,RECFMF             RECFM=F?
         BNO   LIST230                    NO, BRANCH
         SR    R15,R0                     CORRECT DATA START POINTER
         LA    R14,0(R4,R6)               END OF LINE
         SR    R14,R0                     START OF LINE NUMBER
LIST230  TM    FLAGSDD,SNUM               SNUM FORMAT?
         BO    LIST330                    YES, BRANCH
         MVC   MSGLINE+4(8),0(R14)        NUM FORMAT -- GET LINE NUMBER
         NC    MSGLINE+4(8),LISTL000      DROP NUMERIC PART
         CLC   MSGLINE+4(8),LISTL000      ZONE DIGITS FOR ALL BYTES?
         BE    LIST240                    YES, BRANCH
         OI    FLAGSDD,DONONUM            CONTINUE WITH NONUM FORMAT
         B     LIST210
         SPACE 1
LIST240  MVC   MSGLINE+4(6),0(R14)        SPF LINE NUMBER FIELD
         TM    DIRFLAG,X'0F'              SPF ATTRIBUTES STORED?
         BO    LIST320                    YES, BRANCH
         LA    R0,5                       MAXIMUM OF 5 DIGITS TO BLANK
         LA    R2,MSGLINE+3               START OF FIELD TO BLANK
         MVC   MSGLINE+4(6),2(R14)        LINE NUMBER FIELD
LIST310  LA    R2,1(,R2)
         CLI   0(R2),C'0'                 BLANK LEADING ZEROES IN THE
         BNE   LIST320                       LINE NUMBER FIELD
         MVI   0(R2),X'40'
         BCT   R0,LIST310                 DO UP TO 5 DIGITS
LIST320  LA    R0,249                     MAXIMUM ALLOWED DATA LENGTH
         MVI   MSGLINE+10,X'40'           BLANK AFTER LINE NUMBER
         LA    R14,MSGLINE+11             WHERE TO MOVE DATA
         B     LIST340
         SPACE 2
LIST330  LA    R0,256                   MAXIMUM LRECL TO DISPLAY
         LA    R14,MSGLINE+4            WHERE TO MOVE OUTPUT DATA
         SPACE 1
LIST340  CR    R1,R0                    LENGTH>MAXIMUM ALLOWED?
         BL    *+6                      NO, BRANCH
         LR    R1,R0                    YES, USE MAXIMUM
         S     R1,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST360                  NO, BRANCH
         STM   R14,R1,#LUSAVE4          SAVE R14, R15, R0, R1
***      MVC   0(*-*,R14),0(R15)        <<EXECUTED>>
***      EX    R1,*-6                   MOVE DATA INTO LINE
         SPACE 1
LIST360  LR    R2,R1                    MACHINE LENGTH
         LA    R1,MSGLINE+4             START OF DATA
         CR    R14,R1                   NUM MODE?
         BE    *+8                      NO, BRANCH
         AH    R2,=H'7'                 YES, ACCOUNT FOR LINE NO, BLANK
         A     R2,=F'1'                 ACTUAL DATA LENGTH
         C     R2,#MAXLEN               LENGTH>#MAXLEN?
         BL    *+8                      NO, BRANCH
         L     R2,#MAXLEN               YES, USE #MAXLEN INSTEAD
         S     R2,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST160                  LENGTH=0, BRANCH
         LA    R0,5(,R2)                ACTUAL LINE LENGTH + HALFWORDS
         SLL   R0,16
         ST    R0,MSGLINE               CREATE OUTPUT LINE HEADER
         L     R1,#LUSAVE4+4            START OF DATA
         L     R2,#LUSAVE4+12           MACHINE LENGTH
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         LA    R14,1(R1,R2)             END OF DATA
         ST    R14,#LUSAVE4+8           END OF DATA
         MVI   ##HELOFF+1,C'N'          N OF FN.D OR RN.D
         CLI   ##HELOFF,C'F'            FIND OPERATION?
         BE    LIST370                  YES, BRANCH
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BNE   LIST399                  NO, LIST -- BRANCH
         SPACE 1
LIST370  L     R14,#LUSAVE4+8           END OF DATA
         LH    R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         LH    R15,FINDLTH              STRING LENGTH
         ICM   R0,B'1111',#MAXFIND      DONE WITH FIND/REPLACE?
         BNP   LIST398                  YES, BRANCH
         BCTR  R15,0                    MACHINE LENGTH
         EX    R2,LISTTRT               FIRST CHARACTER FOUND?
         BZ    LIST398                  NO, NOT IN STRING, QUIT
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BNL   LIST398                  NO, QUIT
         CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         EX    R15,*-6                  ENTIRE STRING FOUND?
         BNE   LIST397                  NO, TRY THE DATA AGAIN
         SPACE 2
**       IF RECFM=F, LRECL=80 AND NUM OR SNUM FORMAT,
**       THEN DO NOT USE THE CONTINUATION COLUMN (COLUMN 72)
         LR    R0,R2
         TM    FLAGSCC,RECFMF           RECFM=F?
         BNO   LIST374                  NO, BRANCH
         CLI   LRECL+1,80               LRECL=80?
         BNE   LIST374                  NO, BRANCH
         TM    FLAGSDD,NONUM            NONUM FORMAT SPECIFIED?
         BO    LIST374                  YES, BRANCH
         S     R0,=F'1'                 DROP THE CONTINUATION COLUMN
         CR    R15,R0                   POSSIBLY IN STRING?
         BNL   LIST397                  NO, BRANCH
LIST374  L     R15,#MAXFIND             FOUND
         S     R15,=F'1'                     ANOTHER
         ST    R15,#MAXFIND                         ONE
         SPACE 1
*** ADDED FOR  REPLACE MEMBER /BEFORE/AFTER/ . . . .
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BE    LIST376                  YES, BRANCH
         MVI   ##HELOFF+1,C'F'          NO, FIND
         AH    R1,FINDLTH               POINT AFTER THE STRING
         BCTR  R1,0                     POINT AT END OF STRING
         B     LIST397
         SPACE 1
LIST376  AR    R0,R1                    LAST MOVABLE BYTE
         LR    R15,R1                   REPLACEMENT LENGTH
         AH    R15,REPLTH               TARGET OF MOVE
         LR    R2,R1                    REPLACEMENT LENGTH
         AH    R2,FINDLTH               SOURCE OF MOVE
         CR    R15,R2                   REPLACE LENGTH:FIND LENGTH
         BL    LIST390                    LOW: REDUCE THE STRING
         BE    LIST394                    EQUAL: REPLACE THE STRING
*                                         HIGH:  EXPAND THE STRING
LIST380  CR    R2,R15                   COMPLETED?
         BE    LIST394                  YES, BRANCH
         LR    R14,R2                   START A NEW SCAN
         BCTR  R14,0                    LESS ONE
LIST382  LA    R14,1(,R14)              NEXT BYTE
         CR    R14,R0                   POSSIBLY ANY BLANKS?
         BNL   LIST388                  NO, BRANCH
         CLI   0(R14),X'40'             BLANK?
         BNE   LIST382                  NO, BRANCH
         CLI   1(R14),X'40'             FOLLOWED BY ANOTHER BLANK?
         BNE   LIST382                  NO, BRANCH
         SPACE 1
LIST384  BCTR  R14,0
         MVC   1(1,R14),0(R14)          MOVE THE PREVIOUS CHARACTER
         CR    R14,R2                   DONE WITH THE MOVE?
         BH    LIST384                  NO, BRANCH
         LA    R2,1(,R2)                NEXT TARGET BYTE
         B     LIST380                  REPLACE THE STRING
         SPACE 1
LIST388  BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         TSMSG L774                     ERROR MESSAGE
         OI    #REPFLAG,LISTUPDE        MARK AN UPDATE ERROR
         B     LIST399
         SPACE 2
LIST390  CR    R15,R2                   COMPLETED?
         BE    LIST394                  YES, BRANCH
         MVI   0(R15),X'40'             MOVE IN A BLANK
         CR    R2,R0                    CAN IT BE MOVED?
         BNL   *+10                     NO, BRANCH
         MVC   0(1,R15),0(R2)           YES, MOVE IT DOWN
         LA    R15,1(,R15)              NEXT TARGET BYTE
         CLI   0(R2),X'40'              FOUND FIRST BLANK?
         BE    LIST390                  YES, BRANCH
         CR    R2,R0                    NEXT SOURCE BYTE VALID?
         BNL   LIST390                  NO, LOOP
         LA    R2,1(,R2)                YES, INCREMENT
         B     LIST390
         SPACE 1
LIST394  LH    R15,REPLTH               ACTUAL LENGTH OF STRING
         BCTR  R15,0                    MACHINE LENGTH OF STRING
         MVC   0(*-*,R1),REPLSTR        <<EXECUTED>> -- MOVE IN STRING
         EX    R15,*-6                  MOVE IN THE REPLACEMENT
         AR    R1,R15                   POSITION AFTER REPLACED STRING
         MVI   ##HELOFF+1,C'R'          REPLACED DATA
         OI    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK
         L     R2,#LUSAVE4+8            END OF DATA
         LA    R0,1(,R1)                TRY NEXT CHARACTER
         SR    R2,R0                    MACHINE LENGTH LEFT
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         SPACE 2
LIST397  LA    R1,1(,R1)                TRY NEXT CHARACTER
         L     R2,#LUSAVE4+8            END OF DATA
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   LIST370                  NO, KEEP SEARCHING
         SPACE 2
LIST398  CLI   ##HELOFF+1,C'N'          UNSUCCESSFUL FIND OR REPLACE?
         BE    LIST160                  YES, BRANCH
         SPACE 1
LIST399  L     R14,#LUSAVE4+0           WHERE OUTPUT GOES
         L     R15,#LUSAVE4+4           DATA START
         LH    R1,#LUSAVE4+14           ORIGINAL MACHINE LENGTH
         MVC   0(*-*,R14),0(R15)        <<EXECUTED>>
         EX    R1,*-6                   MOVE DATA INTO LINE
         B     LIST730                  OUTPUT THIS LINE
         SPACE 3
LIST400  LA    R4,64                    BLOCK FORMAT, WIDTH=64
         B     LIST430                  FORMAT HEADER
         SPACE 1
LIST420  LA    R4,16                    DUMP FORMAT, WIDTH=16
         SPACE 1
LIST430  LA    R3,LIST600               PROCESSING ROUTINE
         L     R0,#SKIPCOL              CHARACTERS TO SKIP
         ST    R0,#LISTN2               UPDATE BLOCK OFFSET
         A     R0,#LISTN1               CURRENT DATA
         ST    R0,#LISTN1                           DISPLACEMENT
         MVI   MTHIGHL,5
         MVI   MTHIGHL+4,L'MSGDUMP-1
         MVC   MSGTEXT1(3),L140$2
         MVC   INSERT#1(5),LISTLBLO
         MVC   INSERT#2-1(40),MSGDUMP   HEADER FOR BLOCK OR DUMP
         NI    FLAGSDD,FF-OVERLAP       TURN OFF THE OVERLAP INDICATOR
         LR    R5,R1                    DATA LENGTH
         C     R5,#MAXLEN               RESTRICT DATA LENGTH?
         BL    *+8                      NO, BRANCH
         L     R5,#MAXLEN               YES, USE MAX LENGTH
         AR    R5,R6                    COMPUTE NEW END-OF-BLOCK
         BCTR  R5,0                     COMPUTE NEW END-OF-BLOCK -1
         A     R6,#SKIPCOL              ADJUST FOR START COLUMN
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST450                  YES, BRANCH
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         CVD   R0,DOUBLE
         ED    INSERT#2-1(10),DOUBLE+4
         L     R0,#SKIPREC
         S     R0,=F'1'                 SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST170                  YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                 SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                  YES, BRANCH
         CVD   R1,DOUBLE                  RECORD LENGTH
         ED    INSERT#2+MSGDUMPL-MSGDUMP-1(7),DOUBLE+5
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         ST    R0,DOUBLE                SAVE RESULTING TTR
         UNPK  INSERT#2+MSGDUMPT-MSGDUMP-1(7),DOUBLE(4)
         TR    INSERT#2+MSGDUMPT-MSGDUMP-1(6),TRTABLE
         MVI   INSERT#2+MSGDUMPT-MSGDUMP+6-1,X'40'
         TM    FLAGSDD,DUMP
         BNOR  R3
         MVC   INSERT#1(5),LISTLDUM
         BR    R3
         SPACE 2
LIST450  TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BNOR  R3                       NO, BRANCH
         OI    #LISTFLG,X'08'           NEED A HEADER CHANGE
         ST    R4,#LISTWD               WIDTH OF EACH LINE
         L     R6,EXCPBUFF              BUFFER START
         L     R5,LS                    BUFFER LENGTH
         AR    R5,R6                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    #LISTFLG,X'40'           ANY CSECT ADDRESS YET?
         BO    LIST460                  YES, BRANCH
         TM    0(R6),X'F0'              TEST, SCATTER, CESD OR IDR?
         BM    LIST170                  YES, BRANCH
LIST460  TM    #LISTFLG,X'C0'           ANY CURRENT ESD RECORD?
         BO    LIST470                  YES, THIS IS A DATA RECORD
         CLI   8(R6),X'06'              AN ESD RECORD?
         BNE   LIST170                  NO, BRANCH
         OI    #LISTFLG,X'C0'           FLAGS FOR ESD RECORD INPUT
         MVI   #LISTVIR,00              CLEAR THE HIGH-ORDER DIGIT
         MVC   #LISTVIR+1(3),9(R6)      SAVE THE OFFSET ADDRESS
         LH    R15,6(R6)                OFFSET TO ESDID
         LA    R5,16(R15,R6)            FIRST ESDID FOR THIS BLOCK
         LH    R6,4(R6)                 ESDID ENTRIES *4
         SRL   R6,2                     ESDID ENTRIES
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         SPACE 2
*  LOOP TO MARK ENTRIES FOR THE NEXT PHYSICAL BLOCK
LIST464  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST170                  NO, DONE
         MVI   ESDIDIN,0                TURN OFF FIRST
         LR    R14,R5                   START OF ESDID LIST
         LR    R15,R6                   NUMBER OF ESDID ENTRIES
         SPACE 1
LIST468  CLC   0(2,R14),ESDID           THIS ENTRY?
         BNE   *+8                      NO, BRANCH
         MVI   ESDIDIN,1                YES, MARK IT
         LA    R14,4(,R14)              NEXT ESDID ENTRY
         BCT   R15,LIST468              MARK ALL ENTRIES THAT MATCH
         B     LIST464                  CHECK ALL ESD ENTRIES
         SPACE 3
LIST470  XI    #LISTFLG,X'A0'           OFF CURRENT ESD; ON CSECT DATA
         L     R0,#LISTVIR              BLOCK OFFSET
         L     R14,#LISTOFF             OFFSET
         ST    R14,#LISTN2              BLOCK OFFSET
         A     R14,#LISTN1              DATA
         ST    R14,#LISTN1                  DISPLACEMENT
         ICM   R14,B'1111',#LISTOFF     ANY OFFSET?
         BP    *+6                      YES, BRANCH
         LR    R14,R0                   NO, USE THE BLOCK OFFSET
         SR    R6,R0
         ST    R6,#LISTVIR              VIRTUAL BUFFER START ADDRESS
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         AR    R6,R14                   OFFSET --> ACTUAL ADDRESS
         SPACE 2
*  LOOP TO FIND A START ENTRY
LIST480  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST170                  NO, DONE
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR      START ADDRESS OF SYMBOL
         LR    R15,R14                  CSECT START OFFSET
         A     R14,#LISTVIR             OFFSET --> ADDR OF CSECT START
         A     R15,ESDLEN-1             CSECT END OFFSET
         A     R15,#LISTVIR             OFFSET --> ADDRESS OF CSECT END
         CR    R6,R15                   CURRENT:CSECT END
         BNL   LIST480                    HIGH OR EQUAL, LOOP FOR NEXT
         CR    R5,R14                   END:CSECT START
         BL    LIST480                    LOW, LOOP FOR NEXT
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST480                  NO, LOOP FOR NEXT
         MVC   #LISTID(2),ESDID         YES, MARK THE CURRENT ESDID
         ST    R2,#LISTENT              SAVE THIS ENTRY START
         LR    R1,R2
         SPACE 1
LIST490  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)  NEXT ENTRY?
         BZR   R3                               NO, DONE
         CLC   #LISTID(2),ESDID-ESDENTRY(R1)    SAME ESDID ON NEXT ONE?
         BNER  R3                               NO, DONE
         ICM   R15,B'0111',ESDADDR-ESDENTRY(R1) NEXT ENTRY START
         A     R15,#LISTVIR                     OFFSET --> NEXT START
         CR    R14,R15                          CSECT START:NEXT ENTRY
         BE    LIST490                            SAME, IGNORE ENTRY
         CR    R15,R6                           NEXT START:CURRENT
         BNH   LIST480                            NOT ABOVE, USE NEXT
         BR    R3
         SPACE 3
LIST600  BAL   R14,LIST800              PERFORM ANY CSECT POSITIONING
         LR    R2,R4                    DATA LENGTH
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    LIST160                  NO, BRANCH
         CR    R2,R14
         BNH   *+8
         LA    R2,1(,R14)               DATA LENGTH FOR THIS SEGMENT
         BCTR  R2,0                     MACHINE LENGTH FOR THE SEGMENT
         ST    R2,#LUSAVE4+8            SAVE FOR REPLACE
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BE    LIST644                  YES, BRANCH
         CLI   ##HELOFF,C'F'            FIND OPERATION?
         BNE   LIST690                  NO, LIST -- BRANCH
LIST644  XC    #LUSAVE4(8),#LUSAVE4
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LH    R15,FINDLTH              STRING LENGTH
         ST    R14,#LUSAVE4             END OF DISPLAYED PART
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    MACHINE LENGTH OF STRING
         CR    R14,R5                   BEYOND RECORD END?
         BL    LIST660                  NO, BRANCH
         LR    R14,R5                   YES, USE END OF RECORD -1
         SPACE 1
LIST660  ICM   R0,B'1111',#MAXFIND      ANY MORE TO FIND?
         BNP   LIST670                  NO, BRANCH
         EX    R2,LISTTRT               FIRST CHARACTER FOUND?
         BZ    LIST670                  NO, NOT IN STRING
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BH    LIST670                  NOT IN STRING
         CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         EX    R15,*-6                  ENTIRE STRING FOUND?
         BNE   LIST666                  NO, BRANCH
         ST    R1,#LUSAVE4+4            YES, SAVE THE LAST ADDRESS
         L     R0,#MAXFIND              FOUND
         S     R0,=F'1'                      ONE
         ST    R0,#MAXFIND                      MORE
         CLI   ##HELOFF,C'R'            REPLACE SUBCOMMAND?
         BNE   LIST664                  NO, BRANCH
         MVC   0(*-*,R1),REPLSTR        <<EXECUTED>> -- REPLACE STRING
         EX    R15,*-6                  MOVE IN THE REPLACEMENT
         OI    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK
         SPACE 1
LIST664  AR    R1,R15                   SKIP OVER THE REPLACED DATA
         OC    #LISTIDU(2),#LISTIDU     ANY ESDID YET?
         BNZ   LIST666                  YES, BRANCH
         MVC   #LISTIDU(2),#LISTID      NO, SAVE THIS ONE
LIST666  LA    R1,1(,R1)                NO, TRY NEXT CHARACTER
         L     R2,#LUSAVE4              LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   LIST660                  NO, CONTINUE SEARCHING
         SPACE 2
LIST670  ICM   R1,B'1111',#LUSAVE4+4    ANY STRING FOUND?
         BZ    LIST680                  NO, BRANCH
         SR    R1,R6                    DISPLACEMENT INTO DISPLAY
         AH    R1,FINDLTH               LENGTH AT END OF COMPARE
         CR    R1,R4                    BEYOND DISPLAYED LENGTH?
         BNH   LIST684                  NO, BRANCH
         OI    FLAGSDD,OVERLAP          YES, MARK AS AN OVERLAP
         B     LIST690
         SPACE 1
LIST680  TM    FLAGSDD,OVERLAP          NO STRING FOUND -- OVERLAP SET?
         BO    LIST684                  YES, BRANCH
         L     R2,#LUSAVE4+8            MACHINE LENGTH OF THIS SEGMENT
         L     R1,#LISTN2               CSECT
         LA    R1,1(R1,R2)                   BYTE
         ST    R1,#LISTN2                        DISPLACEMENT
         B     LIST160
         SPACE 1
LIST684  NI    FLAGSDD,FF-OVERLAP       CLEAR THE OVERLAP FLAG
         B     LIST690
         SPACE 1
LISTTRT  TRT   0(*-*,R1),WORKTBL        <<EXECUTED>> -- FIND FIRST BYTE
         SPACE 2
LIST690  L     R2,#LUSAVE4+8            MACHINE LENGTH
         LA    R0,20(,R2)               LINE OUTPUT LENGTH
         LA    R1,MSGLINE+17            OUTPUT DATA START ADDRESS
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BE    LIST691                  YES, BRANCH
         LA    R15,MSGLINE              OUTPUT START FOR OFFSETS
         TM    FLAGSDD,LBLOCK+BLOCK     ANY BLOCK FORMAT?
         BM    LIST692                  YES, BRANCH
LIST691  LA    R0,62(,R2)               LINE OUTPUT LENGTH
         LA    R1,MSGLINE+57            OUTPUT CHARACTER DUMP ADDRESS
         LA    R15,MSGLINE+2            OUTPUT START FOR OFFSETS
         MVC   MSGLINE(80),MSGBL132     CLEAR THE OUTPUT LINE
         SPACE 2
LIST692  SLL   R0,16
         ST    R0,MSGLINE               OUTPUT LENGTH
         MVI   0(R1),C'*'               FIRST *
         MVC   1(*-*,R1),0(R6)          <<EXECUTED>>
         EX    R2,*-6                   MOVE IN THE DATA
         LA    R1,2(R1,R2)              POSITION OF FINAL ASTERISK
         MVI   0(R1),C'*'               FINAL *
         SPACE 1
         UNPK  4(7,R15),#LISTN1+1(4)    UNPACK ADDRESS
         TR    4(6,R15),TRTABLE         CONVERT TO HEXADECIMAL
         MVI   10(R15),X'40'            BLANK THE FOLLOWING BYTE
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BNE   *+10                     NO, BRANCH
         MVC   4(6,R15),BLANKS          YES, BLANK THIS FIELD
         UNPK  DOUBLE(7),#LISTN2+1(4)   UNPACK ADDRESS
         TR    DOUBLE(7),TRTABLE        CONVERT TO HEXADECIMAL
         MVC   11(4,R15),DOUBLE+2       MOVE IN ADDRESS
         MVI   15(R15),X'40'            BLANK THE FOLLOWING BYTE
         CLI   DOUBLE+1,C'0'            LESS THAN 64K?
         BE    *+10                     YES, BRANCH
         MVC   11(5,R15),DOUBLE+1       MOVE IN ADDRESS
         MVI   16(R15),X'40'            BLANK ANOTHER BYTE
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BE    LIST694                  YES, BRANCH
         TM    FLAGSDD,LBLOCK+BLOCK     ANY BLOCK FORMAT?
         BM    LIST700                  YES, BRANCH
LIST694  LA    R15,MSGLINE+11           WHERE TO PUT OUTPUT
         LR    R1,R6                    CURRENT DATA POINTER
         LA    R14,1(,R2)               NUMBER OF BYTES TO FORMAT
         SR    R0,R0                    CURRENT FORMATTED CHARACTER
         SPACE 2
LIST696  MVC   DOUBLE(1),0(R1)          AVOID 0C5 FOR LAST BUFFER BYTE
         UNPK  8(3,R15),DOUBLE(2)       UNPACK DATA
         TR    8(2,R15),TRTABLE         CONVERT TO HEXADECIMAL
         MVI   10(R15),X'40'            CLEAR FOLLOWING BYTE
         LA    R15,2(,R15)              INCR INTO PRINT AREA
         A     R1,=F'1'                 INCREMENT INTO DATA
         A     R0,=F'1'                 NEXT FORMATTED CHARACTER
         ST    R0,DOUBLE
         TM    DOUBLE+3,X'03'           4, 8, 12, 16, ...?
         BNZ   LIST698                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
         TM    DOUBLE+3,X'07'           8, 16, 24, 32, ...?
         BNZ   LIST698                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
LIST698  BCT   R14,LIST696              GO FORMAT
         SPACE 3
LIST700  L     R2,#LUSAVE4+8            MACHINE LENGTH OF THIS SEGMENT
         L     R1,#LISTN2               CSECT
         LA    R1,1(R1,R2)                   BYTE
         ST    R1,#LISTN2                        DISPLACEMENT
         CLI   INSERT#1,X'FF'           ANY HEADER?
         BE    LIST760                  NO, BRANCH
         CLI   ##HELOFF,C'D'            DIRENTRY SUBCOMMAND?
         BE    LIST720                  YES, BRANCH
         ICM   R0,B'1111',#MAXOUT       ANY MORE OUTPUT?
         BNP   LIST910                  NO, ALL DONE
         CLI   ##HELOFF+3,C'Y'          ANY OUTPUT YET?
         BNE   LIST710                  NO, BRANCH
         LA    R15,MSGBLANK
         BAL   R14,LIST770              OUTPUT A BLANK LINE
LIST710  TM    #LISTFLG,X'01'           CSECT OUTPUT?
         BO    LIST720                  YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST730                  YES, BRANCH
LIST720  LA    R15,MSGTEXT1             OUTPUT THE HEADER LINE
         BAL   R14,LIST770
LIST730  MVI   INSERT#1,X'FF'           TURN OFF THE HEADER
         OI    FLAGSGG,FOUTSOME         SOME OUTPUT WAS GENERATED
         L     R0,#MAXOUT               DECREMENT
         S     R0,=F'1'                          THE #MAXOUT
         ST    R0,#MAXOUT                                   COUNTER
         BM    LIST910                  NEGATIVE, ALL DONE
LIST760  TR    MSGLINE+4(256),TRLINE    UNPRINTABLES --> PERIODS
         LA    R15,MSGLINE              OUTPUT THE NEXT LINE
         BAL   R14,LIST770              OUTPUT THE NEXT LINE
         TM    ##ADRPA#,$F              FINDLIST MODIFICATION?
         BNO   *+8                      NO, BRANCH
         MVI   ##HELOFF,C'L'            YES, NOW LIST THE REST
         B     LIST160                  YES, CONTINUE
         SPACE 1
LIST770  STM   R14,R15,#LFSAVE2         SAVE REGISTERS 14 AND 15
         CLI   ##HELOFF+3,C'D'          FIND AND NO OUTPUT YET?
         BNE   LIST780                  NO, BRANCH
         SPACE 1
         LA    R1,=C'THEN'
         ICM   R2,B'1111',#ACTIONT      ANY THEN ACTION?
         BP    LIST980                  YES, BRANCH
         SPACE 1
         ICM   R0,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    NEWCMD                   YES, IGNORE THIS MEMBER
         SPACE 1
         BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         SPACE 1
LIST780  L     R1,#LFSAVE2+4            POINT TO MESSAGE
         MVI   ##HELOFF+3,C'Y'          OUTPUT WAS GENERATED
         TSMSG (R1)                     OUTPUT THE MESSAGE
         L     R14,#LFSAVE2             RESTORE REGISTER 14
         BR    R14
         SPACE 3
LIST800  TM    #LISTFLG,X'60'           CSECT DATA?
         BNOR  R14                      NO, RETURN
         TM    #LISTFLG,X'08'           NEED ADJUSTMENT?
         BNO   LIST830                  NO, BRANCH
         XI    #LISTFLG,X'08'           RESET THE ADJUSTMENT FLAG
LIST804  L     R4,#LISTWD               DISPLAY CHARACTER WIDTH
         ICM   R2,B'1111',#LISTENT      END OF CSECT ENTRIES?
         BZ    LIST170                  YES, BRANCH
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST170                  NO, GET THE NEXT INPUT BLOCK
         SPACE 1
*  ADJUST TO BEGINNING BOUNDARY
         SR    R0,R0
         ICM   R0,B'0111',ESDADDR       CURRENT OFFSET
         A     R0,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         CR    R0,R6                    VALID ADJUSTMENT?
         BNH   *+6                      NO, BRANCH
         LR    R6,R0                    YES, POSITION TO SYMBOL START
         LR    R1,R6
         BCTR  R1,0
         CR    R1,R5                    IN THIS DATA RECORD?
         BNL   LIST170                  NO, IGNORE THIS RECORD
         XC    #LISTOFF(4),#LISTOFF     MODULE OFFSET WAS HONORED
         MVC   #LISTID(2),ESDID         SAVE THE CURRENT ESD ID
         MVI   MTHIGHL,39
         MVC   MSGTEXT1(3),L141$1
         MVC   INSERT#1(39),MSGLDUMP    "AT 123456  CSECT 12345678 .."
         UNPK  INSERT#1(7),ESDADDR(4)
         TR    INSERT#1(6),TRTABLE      OFFSET OF CSECT OR ENTRY
         MVI   INSERT#1+6,X'40'
         MVC   INSERT#1+14(8),ESDNAME   PRIVATE, COMMON OR CSECT NAME
         UNPK  INSERT#1+31(7),ESDLEN(4)
         TR    INSERT#1+31(6),TRTABLE   LENGTH OF CSECT
         MVI   INSERT#1+31+6,X'40'
         LR    R0,R6
         S     R0,#LISTVIR              ACTUAL ADDRESS --> OFFSET
         ST    R0,#LISTN1               OFFSET IN LOAD MODULE
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR       ENTRY ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         CLI   ESDTYPE,CODESD           CSECT ENTRY?
         BE    LIST810                  YES, BRANCH
         TM    ESDTYPE,CODEPC           PRIVATE CODE OR OVERLAY?
         BO    LIST810                  YES, BRANCH
         L     R15,ESDCSECT             ADDRESS OF MAIN ENTRY
         MVC   INSERT#1+14(8),4(R15)    CSECT NAME
         MVC   INSERT#1+24(6),LISTLENT   ENTRY WITHIN A CSECT
         MVC   INSERT#1+31(8),ESDNAME
         AR    R0,R1
         ICM   R1,B'0111',ESDMAIN+1     CSECT START ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         SPACE 2
LIST810  ICM   R0,B'0111',ESDADDR       ENTRY ADDRESS
         LR    R15,R0                   SAVE THE ENTRY ADDRESS
         A     R0,ESDLEN-1              LAST ADDRESS TO BE ACCESSED
LIST814  LTR   R2,R2                    AT END OF LIST?
         BZ    LIST820                  YES, BRANCH
         ICM   R2,B'1111',ESDLINK       ANOTHER ESD ENTRY?
         ST    R2,#LISTENT
         BZ    LIST820                  NO, BRANCH
         CLC   #LISTID(2),ESDID         ENTRY FOR THIS CSECT?
         BNE   LIST820                  NO, USE THE CURRENT ENTRY
         CLM   R15,B'0111',ESDADDR      START:NEXT START
         BNL   LIST814                    HIGH OR EQUAL, DISCARD IT
         CLM   R0,B'0111',ESDADDR       END:NEXT START
         BNH   LIST820                    LOW OR EQUAL, USE THIS LIMIT
         ICM   R0,B'0111',ESDADDR         HIGH, USE THE LOWER LIMIT
         SPACE 1
LIST820  LR    R1,R0                    CSECT OR ENTRY END
         SR    R1,R15                   CSECT OR ENTRY LENGTH
         C     R1,#MAXLEN               ABOVE MAXIMUM CSECT LENGTH?
         BL    *+8                      NO, BRANCH
         L     R1,#MAXLEN               YES, USE THE REQUESTED MAXIMUM
         AR    R1,R15                   END OF CSECT OR ENTRY
         A     R1,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         ST    R1,#LISTE1               SAVE END ADDRESS
         SPACE 2
*  CHECK FOR END OF CSECT OR EXTERNAL LABEL
LIST830  LR    R1,R4                    DATA LENGTH
         LR    R15,R5
         SR    R15,R6                   MACHINE LENGTH VALID?
         BM    LIST170                  NO, IGNORE THIS RECORD
         CR    R1,R15
         BNH   *+8
         LA    R1,1(,R15)               R1=MIN(16 OR 64,LENGTH LEFT)
         AR    R1,R6                    END OF THIS LINE
         C     R1,#LISTE1               END ON THIS LINE?
         BLR   R14                      NO, RETURN
         OI    #LISTFLG,X'08'           NEW HEADER IS NEEDED
         L     R4,#LISTE1               MAXIMUM ADDRESS OF CSECT
         SR    R4,R6                    DISPLAY LENGTH VALID?
         BPR   R14                      YES, BRANCH
         LTR   R2,R2                    AT END OF LIST?
         BZ    LIST170                  YES, IGNORE THIS MEMBER
         B     LIST804                  NO, TRY THE NEXT CSECT
         SPACE 3
LIST900  MVI   ##HELOFF+2,X'EF'         END OF FILE ENCOUNTERED
LIST910  CLI   ##HELOFF+3,C'D'          FIND AND NO OUTPUT?
         BNE   LIST920                  NO, BRANCH
         LA    R1,=C'ELSE'
         ICM   R2,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    LIST980                  YES, BRANCH
         TM    FLAGSAA,FMEM#MEM         MEMBER GROUP IN PROGRESS?
         BO    NEWCMD                   YES, DONE
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   LIST940                  NO, BRANCH
         TM    PMEMMIN,X'80'            MEMBER LIST AND NO OUTPUT?
         BO    NEWCMD                   YES, BRANCH
         BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         B     LIST940
         SPACE 1
LIST920  TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP SEGMENT FORMAT?
         BZ    LIST940                         NO, BRANCH
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         SPACE 1
LIST940  CLI   ##HELOFF+2,X'EF'         END OF FILE ENCOUNTERED?
         BNE   NEWCMD                   NO, DONE
         MVI   MTHIGHL,L'MSGBLK-1
         LA    R1,L142$1
         MVC   INSERT#1-1(34),MSGBLK
         L     R0,#BLKCNT
         CVD   R0,DOUBLE
         ED    INSERT#1-1(10),DOUBLE+4
         TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BO    LIST960                  YES, BRANCH
         TM    FLAGSDD,DUMP+BLOCK
         BNZ   LIST960
         MVC   INSERT#1+MSGBLKL-MSGBLK-1(6),LISTLLIN
LIST960  TM    DSORG,DS1DSGPO
         BO    MSGNEW
         MVC   INSERT#1+MSGBLKS-MSGBLK-1(8),LISTLDAT
         B     MSGNEW
         SPACE 2
LIST980  MVC   DELAYMSG+25(4),0(R1)     ADD "THEN" OR "ELSE"
         TM    ##ADRPA#-##SUBCOM(R2),$D SUPPRESS DEFAULT MESSAGE?
         BO    LIST990                  YES, BRANCH
         SPACE 1
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         TM    FLAGSAA,FDELAYM          ANY MESSAGE TO OUTPUT?
         BNO   LIST990                  NO, BRANCH
         XI    FLAGSAA,FDELAYM          NO MORE MESSAGE
         MVC   DELAYMSG+30(8),0(R2)     SUBCOMMAND TO EXECUTE
         MESSAGE DELAYMSG               IDENTIFICATION LINE
         SPACE 1
LIST990  MVC   ##ANSWER(LISUBS),ISUBS   INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(PTW),0(R2)      CHANGE THE SUBCOMMAND
         MVI   MTHIGHL,8
         MVI   MTHIGHL+4,8
         B     CALLCMDZ                 CALL THE SECONDARY SUBCOMMAND
         SPACE 3
* OUTPUT A FIND OR REPLACE HEADER MESSAGE IF REQUIRED
LIST996  TM    DSORG,DS1DSGPO           PARTITIONED?
         BNOR  R2                       NO, NO MESSAGE
         TM    FLAGSAA,FDELAYM          ANY HEADER MESSAGE?
         BNOR  R2                       NO, NO MESSAGE
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         MESSAGE DELAYMSG               OUTPUT THE HEADER LINE
         NI    FLAGSAA,FF-FDELAYM       TURN OFF THE HEADER LINE
         BR    R2
LISTUPDE EQU   X'80'                    #REPFLAG -- UPDATE ERROR
LISTUPDO EQU   X'40'                    #REPFLAG -- UPDATE OPEN
LISTUPDR EQU   X'20'                    #REPFLAG -- UPDATE THIS BLOCK
         DROP  R2
         TITLE 'P D S  --  PDS MAP                             6/10/85'
***********************************************************************
***      MAP SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
MAP      CSECT
         USING *,R8
         LA    R1,L530          ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         LA    R1,L701          ASSUME NOT A LOAD LIBRARY
         TM    FLAGSCC,RECFMU   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         OI    FLAGSGG,FOUTSUB  SUBCOMMAND GAINED CONTROL
         MVI   ENTRYPT,C'?'     NO ENTRY POINT YET
         SPACE 1
         L     R15,=A(READESD)  FORMAT THE ESD DATA
         BALR  R14,R15          ANY ESD DATA?
         B     *+8              NO, BRANCH
         B     MAP04            YES, BRANCH
         SPACE 1
MAPNOESD TM    FLAGSAA,FDELAYM  DELAYED MESSAGE AVAILABLE?
         BNO   MAP02            NO, BRANCH
         TM    PMEMMIN,X'80'    MEMBER LIST?
         BO    *+12             YES, BRANCH
         TM    FLAGSAA,FMEM#MEM MEMBER GROUP?
         BNO   MAP02            NO, BRANCH
         CLI   #MODLEN,0        ANY FILTERING?
         BH    NEWCMD           NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE DELAYMSG
         XI    FLAGSAA,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 1
MAP02    LA    R1,MSGTEXT1               MESSAGE IDENTIFIER
         TSMSG (R1)
         B     MAPNOMSG
         SPACE 1
MAP04    LA    R4,#ESDPTR       ESD TABLE CHAIN POINTER
         OI    FLAGSGG,FOUTSOME SOME OUTPUT WAS GENERATED
         USING ESDENTRY,R4      BASE FOR TABLE
         TM    FLAGSAA,FDELAYM  ANY DELAYED MESSAGE AVAILABLE?
         BNO   MAPESD           NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE DELAYMSG
         XI    FLAGSAA,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 2
MAPESD   MVC   MSGTEXT1(136),MSGBL132
         ICM   R4,B'1111',ESDLINK    END OF CHAIN?
         BZ    MAPLAST               YES, BRANCH
         SPACE 1
         CLI   #MAPOPT,X'04'         RELINK FORMAT?
         BE    MAP600                YES, BRANCH
         CLI   ESDTYPE,CODELR        EXTERNAL REFERENCE?
         BE    MAPLR                 YES, BRANCH
         CLI   ESDTYPE,CODEER        $UNRESOLVED EXTERNAL REFERENCE?
         BE    MAPER                 YES, BRANCH
         CLI   ESDTYPE,CODEWK        $UNRESOLVED WEAK EXTERNAL REF?
         BE    MAPWK                 YES, BRANCH
         SPACE 1
         CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+4(8),ESDNAME
         UNPK  MSGTEXT1+14(7),ESDADDR(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVI   MSGTEXT1+20,X'40'     CLEAR GARBAGE BYTE
         UNPK  MSGTEXT1+22(7),ESDLEN(4)
         TR    MSGTEXT1+22(6),TRTABLE
         MVI   MSGTEXT1+28,X'40'     CLEAR GARBAGE BYTE
         SPACE 1
         TM    DIRATTR,ATTROVLY      OVERLAY PROGRAM?
         BZ    MAPNOVLY              NO, BRANCH
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
         B     MAPPRINT
         SPACE 1
MAPNOVLY CLI   ESDXAFLG,AMODE24          RMODE 24 AND AMODE 24?
         BNH   MAPPRINT                  NO, BRANCH
         MVC   MSGTEXT1+30(5),=C'RMODE'
         MVC   MSGTEXT1+36(3),=C'ANY'
         TM    ESDXAFLG,RMODEANY         RMODE ANY?
         BO    *+10                      YES, BRANCH
         MVC   MSGTEXT1+36(3),=C'24 '    RMODE 24
         MVC   MSGTEXT1+40(5),MAPLAMOD
         MVC   MSGTEXT1+46(3),=C'24 '
         TM    ESDXAFLG,AMODE31          AMODE 31?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'31 '    AMODE 31
         TM    ESDXAFLG,AMODE24+AMODE31  AMODE ANY?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'ANY'    AMODE ANY
         B     MAPPRINT
         SPACE 2
MAPWK    MVC   INSERT#1(8),ESDNAME
         TSMSG L441$1
         B     MAPESD
         SPACE 2
MAPER    MVC   INSERT#1(8),ESDNAME
         TSMSG L442$1
         B     MAPESD
         SPACE 1
MAPLR    CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+56(8),ESDNAME
         UNPK  MSGTEXT1+66(7),ESDADDR(4)
         TR    MSGTEXT1+66(6),TRTABLE
         MVI   MSGTEXT1+66+6,X'40'   CLEAR GARBAGE BYTE
         SPACE 1
MAPPRINT MESSAGE MSGTEXT1
         B     MAPESD
         SPACE 2
MAPLAST  MESSAGE MSGBLANK
MAPNOMSG UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         TSMSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         TSMSG L104$2
         SPACE 1
         TM    DIRFLAG,X'80'      IS MODULE AN ALIAS?
         BZ    NEWCMD             NO, NO ALIAS INFORMATION
         CLI   #MAPOPT,X'01'      SHORT OR ENTRY POINT MAP?
         BH    NEWCMD             YES, DONE
         TM    FLAGSGG,FALINCON   NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD             YES, DONE
         EJECT
*        FIND THE CORRESPONDING MAIN MODULE
         MVI   STARTTR+2,X'01'    TTR=000001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1  NO MAIN(S) FOUND
         SPACE 1
MAPAREAL BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAPNREAL           LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    MAPAREAL           YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAPAREAL           NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         TSMSG L066$1
         B     MAPAREAL
         SPACE 2
MAPNREAL OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, QUIT
         TSMSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 1
MAP600   MVC   MSGTEXT1+4(L'MAPLLKED),MAPLLKED   //LKED  EXEC PGM=IEWL,
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   MSGTEXT1+4(L'MAPLPARM),MAPLPARM   //      PARM='NCAL, ..
         LA    R1,MSGTEXT1+4+L'MAPLPARM
         SPACE 1
         TM    DIRATTR,ATTRRENT                  REENTRANT?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+00               YES, ADD ,RENT
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRREUS                  REUSABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+05               YES, ADD ,REUS
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRREFR                REFRESHABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+10               YES, ADD ,REFR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+15               YES, ADD ,OVLY
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRTEST                  TEST SYMBOLS?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+20               YES, ADD ,TEST
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRSCTR                  SCATTER LOADED?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+25               YES, ADD ,SCTR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRLOAD                  ONLY LOADABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+30               YES, ADD ,OL
         LA    R1,3(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRNE                  NOT EDITABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+33               YES, ADD ,NE
         LA    R1,3(,R1)
         SPACE 1
         MVI   0(R1),C''''                       ADD FINAL QUOTE
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSUT1),MAPLSUT1   //SYSUT1
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSPRT),MAPLSPRT   //SYSPRINT
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSMOD),MAPLSMOD   //SYSLMOD
         MVC   MSGTEXT1+4+L'MAPLSMOD(44),DSNAME  ADD DSNAME
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSLIN),MAPLSLIN   //SYSLIN
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLINCL),MAPLINCL   ADD "INCLUDE SYSLMOD("
         MVC   MSGTEXT1+4+L'MAPLINCL(8),DIRNAME  ADD MEMBER NAME
         LA    R1,MSGTEXT1+4+L'MAPLINCL+8
         CLI   0(R1),X'40'
         BNE   *+8
         BCT   R1,*-8
         MVI   1(R1),C')'
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY MODULE?
         BO    MAP700                            YES, BRANCH
         SPACE 3
         LA    R4,#ESDPTR
         SPACE 1
MAP610   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+60                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLORD           ADD " ORDER"
         SPACE 1
MAP620   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP670                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP630                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP630                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP620                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP620                            NO, BRANCH
         B     MAP640                            NO, BRANCH
         SPACE 1
MAP630   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP620                            YES, BRANCH
         SPACE 1
MAP640   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         TM    ESDADDR+1,X'0F'                   POSSIBLE 4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         TM    ESDADDR+2,X'FF'                   4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         MVC   1(3,R14),MAPLPAG                  YES, ADD "(P)"
         LA    R14,3(,R14)
         SPACE 1
MAP660   MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP620                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP610
         SPACE 2
MAP670   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP800                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP800
         SPACE 2
MAP700   MVC   STARTTR(3),DIRSTART               ADDRESS OF NOTELIST
         L     R15,=A(EXCP)
         BALR  R14,R15
         LA    R1,L751                           NOTELIST NOT FOUND
         LTR   R15,R15                           ANY ERROR?
         BP    MSGNEW                            YES, BRANCH
         SPACE 1
         LR    R2,R0                             START OF RECORD
         LR    R5,R0                             START OF RECORD
         LA    R14,24(,R2)                       FIRST SEGMENT
         LA    R0,4                              INCREMENT
         LR    R1,R2
         A     R1,LS                             LAST SEGMENT
         SR    R1,R0                             FOR BXLE
         SR    R15,R15
         SPACE 1
MAP710   LA    R15,1(,R15)                       NEXT SEGMENT #
         STC   R15,3(,R14)                       STORE IN SEGTAB
         BXLE  R14,R0,MAP710                     DO ALL ENTRIES
         SPACE 2
         MVI   0(R14),X'FF'                      MARK END OF TABLE
         MVC   FULLWORD,=F'1'                    REGION = 1
         LA    R2,24(,R5)                        BEGIN SEGMENT 1
         LA    R6,8(,R5)                         BEGIN REGION 1
         SPACE 1
MAP720   SR    R1,R1
         IC    R1,0(,R6)                         # OF LAST SEGMENT
         LTR   R1,R1                             END OF STRUCTURE?
         BZ    MAP800                            YES, BRANCH
         SLL   R1,2                              SEGMENT * 4
         LA    R3,20(R1,R5)                      LAST SEGMENT
         SPACE 2
MAP724   LA    R4,#ESDPTR                        START OF ESD ELEMENTS
         SPACE 1
MAP730   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(6),MAPLINS           ADD " INSERT"
         SPACE 1
MAP732   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP742                            NO, BRANCH
         CLC   ESDSEG#(1),3(R2)                  THIS SEGMENT?
         BNE   MAP732                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP734                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP734                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP732                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP732                            NO, BRANCH
         B     MAP740
         SPACE 1
MAP734   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP732                            YES, BRANCH
         SPACE 1
MAP740   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP732                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP730
         SPACE 1
MAP742   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP750                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP750   CLI   4(R2),X'FF'                       END OF SEGTAB?
         BE    MAP800                            YES, BRANCH
         CR    R2,R3                             END OF REGION?
         BE    MAP764                            YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD " REGION"
         MVC   MSGTEXT1+4+1+7+1+6(1),FULLWORD+3  ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         SPACE 1
         SR    R1,R1
         ICM   R1,B'0001',4(R2)                  ANY PREVIOUS SEGMENT?
         BZ    MAP760                            NO, BRANCH
         MVC   MSGTEXT1+4+1+7+1(6),MAPLOVLY      ADD " OVLY"
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+4+1+7+1+4(3),DOUBLE+6(2) ADD SEGMENT NUMBER
MAP760   MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
MAP764   LA    R0,4
         LR    R1,R3
         BXLE  R2,R0,MAP724                      DO EACH SEGMENT
         SPACE 2
         LA    R6,2(,R6)
         CLI   0(R6),X'00'                       ANY SEGMENTS IN RGN?
         BE    MAP800                            NO, BRANCH
         L     R1,FULLWORD
         LA    R0,4
         CR    R1,R0                             FOURTH REGION DONE?
         BNL   MAP800                            YES, BRANCH
         LA    R1,1(,R1)                         NEXT REGION
         ST    R1,FULLWORD
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         STC   R1,MSGTEXT1+4+1+7+1+6             ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         MVC   MSGTEXT1+4+1+7+1+6+1(8),MAPLREG   ADD "(REGION)"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP720
         SPACE 2
MAP800   LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    MAP802                  YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
MAP802   LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   MAP804                  NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   MAP820                  NO, SKIP SSI PROCESSING
         SPACE 1
MAP804   LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    MAP820                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MAP820                  YES, NO SSI
         SPACE 1
         MVC   MSGTEXT1+5(6),MAPLSET   ADD " SETSSI"
         UNPK  MSGTEXT1+5+6+2(9),0(5,R2)
         TR    MSGTEXT1+5+6+2(8),TRTABLE
         MVI   MSGTEXT1+5+6+2+8,X'40'
         LA    R2,4(,R2)               ADD SSI SIZE
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP820   TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    MAP860                      NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    MAP840                      NO, BRANCH
         MVC   DOUBLE(8),BLANKS            RMODE/AMODE FILLER
         MVC   DOUBLE(3),=C'24 '           MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE(3),=C'ANY'           MOVE IN THE RMODE TEXT
         MVC   DOUBLE+4(3),=C'24 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'ANY'         MOVE IN THE AMODE TEXT
         MVC   MSGTEXT1+4(L'MAPLMODE),MAPLMODE
         LA    R1,MSGTEXT1+4+L'MAPLMODE
         MVC   0(3,R1),DOUBLE              ADD 24/ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVC   2(L'MAPLAMO,R1),MAPLAMO     ADD "),AMODE("
         LA    R1,2+L'MAPLAMO(,R1)
         MVC   0(3,R1),DOUBLE+4            ADD 24/31 OR ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVI   2(R1),C')'                  ADD FINAL PARENTHESIS
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP840   TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   MAP860                  NO, BRANCH
         SPACE 1
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   MAP860                  NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BL    MAP860                  NO, BRANCH
         MVC   MSGTEXT1+4(L'MAPLAUTH),MAPLAUTH
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP860   MVC   MSGTEXT1+4(L'MAPLENTR),MAPLENTR
         CLI   ENTRYPT,C'?'           ANY ENTRY POINT?
         BE    *+10                   NO, BRANCH
         MVC   MSGTEXT1+4+L'MAPLENTR-8(8),ENTRYPT
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   ENTRYPT(8),DIRNAME      DEFAULT MODULE NAME
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 2
MAP880   LA    R2,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R3,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLALIA          ADD " ALIAS"
         SPACE 1
MAP890   BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAP910             LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAP890             NO, BRANCH
         TM    MEMFLAG,X'80'      ALIAS ENTRY FOUND?
         BO    MAP900             YES, BRANCH
         TM    DIRFLAG,X'80'      ALIAS ORIGINALLY?
         BNO   MAP890             NO, BRANCH
         MVC   ENTRYPT(8),MEMNAME SAVE REAL MEMBER NAME
         B     MAP890
         SPACE 1
MAP900   MVC   1(8,R2),MEMNAME                   ADD THE ALIAS NAME
         LA    R14,8(,R2)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R2,1(,R14)
         CR    R2,R3                             END OF CARD?
         BNH   MAP890                            NO, BRANCH
         SPACE 2
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP880
         SPACE 2
MAP910   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP920                            NO, BRANCH
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         MESSAGE MSGTEXT1
         SPACE 1
MAP920   MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+5(L'MAPLNAME),MAPLNAME
         MVC   MSGTEXT1+5+8(8),ENTRYPT
         LA    R1,MSGTEXT1+5+8+8
         SPACE 1
         CLI   0(R1),X'40'                       BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R1,*-8
         SPACE 1
         MVC   1(3,R1),MAPLREP
         SPACE 1
         MESSAGE MSGTEXT1
         B     NEWCMD
MAPLLKED DC    C'//LKED   EXEC  PGM=IEWL,'
MAPLPARM DC    C'//       PARM=''NCAL,MAP,LIST,LET'
MAPLATTR DC    C',RENT,REUS,REFR,OVLY,TEST,SCTR,OL,NE'
MAPLSUT1 DC    C'//SYSUT1   DD  UNIT=SYSDA,SPACE=(1024,(200,20))'
MAPLSPRT DC    C'//SYSPRINT DD  SYSOUT=*'
MAPLSMOD DC    C'//SYSLMOD  DD  DISP=SHR,DSN='
MAPLSLIN DC    C'//SYSLIN   DD  *'
MAPLINCL DC    C' INCLUDE SYSLMOD('
MAPLAUTH DC    C' SETCODE AC(1)'
MAPLENTR DC    C' ENTRY   ????????'
MAPLMODE DC    C' MODE    RMODE('
MAPLAMO  DC    C'),AMODE('
MAPLNAME DC    C'NAME'
MAPLSET  DC    C'SETSSI'
MAPLALIA DC    C'ALIAS'
MAPLORD  DC    C'ORDER'
MAPLPAG  DC    C'(P)'
MAPLREP  DC    C'(R)'
MAPLREG  DC    C'(REGION)'
MAPLOVER DC    C'OVERLAY'
MAPLOVLY DC    C'OVLY'
MAPLINS  DC    C'INSERT'
         DROP  R4
         SPACE 3
*
*        ESD SCAN SUBROUTINE
*
         SPACE 2
READESD  STM   R14,R12,ESDIDRSV   SAVE REGISTERS FOR RETURN
         LR    R8,R15             ENTRY POINT ADDRESS
         USING READESD,R8         SUBROUTINE BASE REGISTER
         LA    R2,#ESDPTR         ROOT OF ESD CHAIN
         MVC   STARTTR(3),DIRTTR  FIRST TTR
         SPACE 1
ESDEXCP  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)           PROCESS RETURN CODE
         B     ESDEXCP1             00 - GOOD READ
         B     ESDLAST              04 - END OF MEMBER
         B     ESDLAST              08 - END OF DATA SET
         B     NEWCMD               12 - I/O ERROR
         SPACE 1
ESDEXCP1 LR    R3,R0              START OF RECORD
         CLI   0(R3),X'40'        TEST SYM RECORD?
         BE    ESDEXCP            YES, SKIP RECORD
         CLI   0(R3),X'20'        CESD RECORD?
         BNE   ESDLAST            NO, BRANCH
         SPACE 1
         LH    R6,4(,R3)          RELATIVE # OF 1ST ESD ID
         LH    R5,6(,R3)          LENGTH OF DATA IN BUFFER
         LA    R3,8(,R3)          START OF ESD DATA
         AR    R5,R3
         LA    R4,16              LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
         USING ESDNAME,R3
ESDSCAN  IC    R0,ESDTYPE
         LA    R1,CODESEG         CHECK FOR SEGTAB/ENTAB
         TM    DIRATTR,ATTROVLY   OVERLAY MODULE?
         BO    *+8                YES, BRANCH
         NI    ESDXAFLG,RMODEANY+AMODE24+AMODE31
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   #MAPOPT,X'03'      ENTRY POINT ONLY?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ATT   ATTRIBUTE ENTRY(NAME) OPERAND?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ALI   ALIAS SEARCH REQUEST?
         BNE   ESDMAP             NO, BRANCH
ESDSCAN2 CLI   ESDTYPE,CODESD     VALID EXTERNAL SYMBOL?
         BE    ESDCHK             YES, BRANCH
         CLI   ESDTYPE,CODELR     ANOTHER VALID ENTRY
         BNE   NEXTESD            NO, GET THE NEXT ESD
         SPACE 1
ESDCHK   TM    DIRATTR,ATTROVLY   OVERLAY ATTRIBUTE?
         BNO   ESDCHK1            NO, BRANCH
         CLI   ESDSEG#,1          SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD            NO, IGNORE
ESDCHK1  CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT ADDRESS?
         BNE   ESDCHK2            NO, BRANCH
         MVC   ENTRYPT,ESDNAME    YES, ENTRY POINT NAME IF NO ESD MATCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    ESDUSE3            YES, BRANCH
ESDCHK2  CLC   ESDNAME,DIRNAME    THIS NAME?
         BNE   NEXTESD            NO, BRANCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    NEXTESD            YES, BRANCH
         IC    R0,ESDXAFLG        GET MVS/XA ESD FLAGS
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR GET SYMBOL OFFSET
         ST    R6,ESDIDRSV+4      RETURN ESDID IN REG 15
         STM   R0,R1,ESDIDRSV+4+4 RETURN ESD FLAGS, OFFSET IN REGS 0,1
         MVC   ENTRYPT,ESDNAME    USE THIS ESDNAME FOR THE ENTRY POINT
         B     ESDOUT             FOUND THE SYMBOL
         SPACE 1
ESDMAP   CR    R0,R1              THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1            YES, BRANCH
         CLI   ESDTYPE,CODESD     SD ENTRY?
         BE    ESDUSE2            YES, BRANCH
         CLI   ESDTYPE,CODECM     COMMON BLOCK CODE?
         BE    ESDUSE0            YES, BRANCH
         CLC   ##SUBCOM(8),$MAP   MAP SUBCOMMAND?
         BNE   ESDMAP2            NO, BRANCH
         CLI   ESDTYPE,CODEER     $UNRESOLVED EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BE    ESDMAP2            YES, BRANCH
         CLI   ESDTYPE,CODEWK     $UNRESOLVED WEAK EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         SPACE 1
ESDMAP2  CLI   ESDTYPE,CODELR     LR ENTRY?
         BNE   ESDMAPPC           NO, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BL    ESDUSE2            NO, BRANCH
         CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT?
         BE    ESDUSE2            NO, BRANCH
ESDMAPPC CLI   ESDTYPE,CODEPC     PC ENTRY?
         BNE   NEXTESD            NO, BRANCH
         MVC   ESDNAME,MAPL$PRI        PRIVATE CODE
         B     ESDUSE2
         SPACE 1
ESDUSE0  CLC   ESDNAME(8),BLANKS                   BLANK COMMON?
         BNE   ESDUSE2                             NO, BRANCH
         MVC   ESDNAME(8),MAPL$BLK                 YES, USE $BLKCOM
         B     ESDUSE2
         SPACE 1
ESDUSE1  STC   R0,ESDTYPE                          RESTORE OVERLAY TYPE
         MVC   ESDNAME(8),MAPL$SEG                 ASSUME SEGMENT TABLE
         OC    ESDADDR(3),ESDADDR                  CORRECT?
         BZ    ESDUSE2                             YES, BRANCH
         MVC   ESDNAME(8),MAPL$ENT                 NO, ENTRY TABLE
ESDUSE2  SR    R1,R1
         CLC   ##SUBCOM(8),$LIS                    LIST SUBCOMMAND?
         BE    ESDUSE3                             YES, BRANCH
         ICM   R1,B'0001',#MODLEN                  ANY MODULE FILTER?
         BZ    ESDUSE3                             NO, BRANCH
         BCTR  R1,0                                MACHINE LENGTH
         CLC   ESDNAME(*-*),#MODTXT                <<EXECUTED>>
         EX    R1,*-6                              THIS PARTIAL NAME?
         BNE   NEXTESD                             NO, BRANCH
ESDUSE3  BAL   R14,GETESD
         ST    R1,ESDLINK-ESDENTRY(R2)             SAVE LINK POINTER
         STH   R6,ESDID-ESDENTRY(R1)               SAVE ESD ID
         MVC   ESDNAME-ESDENTRY(LENESD1,R1),0(R3)  SAVE ESD ENTRY
         LR    R2,R1                               CHAIN TO NEXT ONE
NEXTESD  LA    R6,1(R6)
         BXLE  R3,R4,ESDSCAN
         B     ESDEXCP            GET NEXT ESD RECORD
         DROP  R3
         SPACE 3
         USING ESDENTRY,R2
ESDLAST  MVC   MSGTEXT1(3),L740
         L     R14,ESDIDRSV
         LA    R2,#ESDPTR
         OC    #ESDPTR,#ESDPTR    ANY ESD DATA AVAILABLE?
         BNZ   ESDSORT            YES, BRANCH
         CLI   #MODLEN,0          ANY ESD FILTERING?
         BH    ESDEXIT            YES, BRANCH
         MVC   MSGTEXT1(3),L703
         B     ESDEXIT
         SPACE 1
ESDSORT  L     R2,ESDLINK
         ICM   R1,B'1111',ESDLINK END OF ESD ENTRIES?
         BZ    ESDOUT             YES, BRANCH
         SPACE 1
ESDSORT2 TM    DIRATTR,ATTROVLY             OVERLAY ATTRIBUTE?
         BNO   ESDNOSEG
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE 1
ESDNOSEG CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE 1
ESDSWAP  XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE 1
ESDSORT3 ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BNZ   ESDSORT2
         B     ESDSORT
         DROP  R2
         SPACE 2
GETESD   MVI   SUBPOOLT,21
         LA    R0,LENESD
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 1
ESDOUT   L     R14,ESDIDRSV
         LA    R14,4(,R14)         ADJUST EXIT ADDRESS
ESDEXIT  LM    R15,R12,ESDIDRSV+4
         BR    R14                 EXIT
MAPLAMOD DC    C'AMODE'
MAPL$BLK DC    CL8'$BLKCOM'
MAPL$PRI DC    CL8'$PRIVATE'
MAPL$SEG DC    CL8'$SEGTAB'
MAPL$ENT DC    CL8'$ENTAB'
         TITLE 'P D S  --  PDS OPTIONS                         6/10/85'
***********************************************************************
***      OPTIONS SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 2
OPTIONS  CSECT
         USING *,R8
ONIX     LTR   R2,R2                  RETURN=0?
         BZ    OPT200                 YES, BRANCH
         TSMSG L071
         SPACE 1
         LA    R3,$TBL-PTW            POINT TO FIRST TABLE ENTRY -WIDTH
         LA    R4,ONIX                START OF THIS CSECT
         SPACE 1
OPT120   LA    R3,PTW(,R3)            NEXT TABLE ENTRY
         CLI   0(R3),X'FF'            END OF TABLE?
         BE    OPT300                 YES, EXIT
         CLI   0(R3),X'40'            DISABLED ENTRY?
         BE    OPT120                 YES, BRANCH
         SR    R1,R1
         ICM   R1,B'0011',28(R3)      ANY OPTION ENTRY?
         BZ    OPT120                 NO, BRANCH
         TM    12(R3),@S              DEFINED FOR SEQUENTIAL DATA SET?
         BO    OPT140                 YES, BRANCH
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   OPT120                 NO, BRANCH
         SPACE 1
OPT140   AR    R1,R4                  ADDRESS OF OPTION DATA
         MVC   MSGTEXT1(80),0(R1)     OPTION LINE
         MVC   MSGTEXT1+4(8),0(R3)    CHANGE TO SUBCOMMAND NAME USED
         MESSAGE MSGTEXT1             OUTPUT THIS MESSAGE
         B     OPT120
         SPACE 3
OPT200   L     R2,0(,R15)             START OF SUBCOMMAND
         MVC   INSERT#1(8),0(R2)      NO, BRANCH
         LA    R3,1(,R3)              ACTUAL LENGTH OF SUBCOMMAND
         STC   R3,MTHIGHL             SAVE LENGTH
         TM    FLAGSBB,FONESHOT       ONE SUBCOMMAND ONLY?
         BNO   OPT201                 NO, BRANCH
         CLC   $MML(4),0(R2)          MEMLIST SUBCOMMAND?
         BNE   OPT201                 NO, BRANCH
         TM    DSORG,DS1DSGPO         PARTITIONED?
         BO    OPT201                 YES, BRANCH
         L     R6,ADDRCBUF            BUFFER ADDRESS
         MVC   4(8,R6),$ISM           SUBCOMMAND CHANGED TO ISPMODE
         LA    R1,8+4                 LENGTH IS 8
         SLL   R1,16                  SHIFT CORRECTLY
         ST    R1,0(,R6)              UPDATE LENGTH TOO
         XI    FLAGSBB,FONESHOT       TURN OFF ONESHOT
         OI    FLAGSBB,FCMD           INDICATE SUBCOMMAND AVAILABLE
         B     NEWCMD
         SPACE 1
OPT201   TSMSG L771$1                 INVALID COMMAND
         TM    FLAGSEE,FBKGRND        BACKGROUND EXECUTION?
         BO    *+8                    YES, BRANCH
         NI    FLAGSBB,FF-FONESHOT    NO, ERROR -- DO NOT TERMINATE
         LA    R1,80                  ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON         ISPMODE ACTIVE?
         BO    OPT202                 YES, BRANCH
         TM    CONTOPTN,1             ANY LOG RECORDING?
         BO    OPT202                 YES, BRANCH
         GTSIZE ,                     TERMINAL SIZE
         CH    R1,=H'120'             120 OR LESS BYTES?
         BL    *+8                    YES, BRANCH
         LH    R1,=H'120'             NO, USE 120 BYTES
OPT202   MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(12),OPTLSUB
         LA    R3,$TBL-PTW            POINT TO FIRST TABLE ENTRY -WIDTH
         LA    R4,MSGTEXT1+4(R1)      END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4+13       START OF LINE
         SPACE 1
OPT220   LA    R3,PTW(,R3)            NEXT TABLE ENTRY
         CLI   0(R3),X'FF'            END OF TABLE?
         BE    OPT280                 YES, EXIT
         CLI   0(R3),X'40'            DISABLED ENTRY?
         BE    OPT220                 YES, BRANCH
         CLC   28(2,R3),ZERO          ANY OPTION ENTRY?
         BZ    OPT220                 NO, BRANCH
         TM    12(R3),@S              DEFINED FOR SEQUENTIAL DATA SET?
         BO    OPT240                 YES, BRANCH
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   OPT220                 NO, BRANCH
         SPACE 1
OPT240   LA    R1,9(,R5)              POTENTIAL ENTRY POSITION
         CR    R1,R4                  LINE FULL?
         BL    OPT260                 NO, CONTINUE
         MESSAGE MSGTEXT1             OUTPUT THIS PART OF THE MESSAGE
         LA    R5,MSGTEXT1+4+13       START OF LINE
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
OPT260   MVC   0(8,R5),0(R3)          ADD THE SUBCOMMAND NAME
         LA    R5,9(,R5)              POSITION FOR NEXT ENTRY
         B     OPT220
         SPACE 1
OPT280   MESSAGE MSGTEXT1
         SPACE 2
OPT300   MESSAGE MSGBLANK
         B     NEWCMD
OPTLSUB  DC    C'SUBCOMMANDS:'
         SPACE 1
         PRINT NOGEN
OATT     MSG   'ATTRIB   - LIST ATTRIBUTES OF A MEMBER'
OALI     MSG   'ALIAS    - ASSIGN AN ALIAS TO A MEMBER'
OBRO     MSG   'BROWSE   - BROWSE USING SPF'
OCHA     MSG   'CHANGE   - SELECT A NEW DATA SET'
OCAR     MSG   'COMPARE  - COMPARE TWO MEMBERS FROM THE DATA SET'
OCMX     MSG   'COMPRESS - COMPRESS IN PLACE OF THE DATA SET'
OCON     MSG   'CONTROL  - SET PROGRAM CONTROLS'
ODIS     MSG   'DISPLAY  - DISPLAY DIRECTORY MEMBER NAMES'
ODIR     MSG   'DIRENTRY - DUMP A DIRECTORY ENTRY'
ODEL     MSG   'DELETE   - DELETE A MEMBER'
ODSN     MSG   'DSNAME   - DISPLAY THE CURRENT ALLOCATION'
ODSP     MSG   'DSPRINT  - PRINT A HARDCOPY WITH DSPRINT'
OEDI     MSG   'EDIT     - EDIT USING SPF'
OEND     MSG   'END      - STOP THE PROGRAM'
OEXE     MSG   'EXEC     - EXECUTE PDS SUBCOMMANDS FROM A CLIST'
OFIN     MSG   'FIND     - LIST LINES CONTAINING A STRING'
OFIX     MSG   'FIXPDS   - MODIFY DATA SET ATTRIBUTES'
OFSE     MSG   'FSE      - EDIT USING FSE'
OHEL     MSG   'HELP     - DISPLAY PDS SUBCOMMAND HELP'
OHIS     MSG   'HISTORY  - LIST HISTORY OF A LOAD MODULE'
OIFX     MSG   'IF       - CONDITIONALLY EXECUTE A PDS SUBCOMMAND'
OISP     MSG   'ISPF     - INVOKE ISPF PRIMARY PANEL'
OISM     MSG   'ISPMODE  - SET ISPF DIALOG DISPLAY MODE'       SS JUL84
OLIS     MSG   'LIST     - DISPLAY A MEMBER'
OMAP     MSG   'MAP      - MAP A LOAD MODULE'
OMEM     MSG   'MEMBERS  - DISPLAY A MEMBER GROUP'
OMML     MSG   'MEMLIST  - DISPLAY A MEMBER GROUP IN ISP TABLE'
OOPT     MSG   'OPTIONS  - DISPLAY THIS LIST OF SUBCOMMANDS'
OOUT     MSG   'OUTCOPY  - OUTPUT IEBCOPY MEMBER SELECT STATEMENTS'
OPAT     MSG   'PATTERN  - DISPLAY DIRECTORY NAMES MATCHING A PATTERN'
OPRI     MSG   'PRINTOFF - PRINT A HARDCOPY'
OREC     MSG   'RECALL   - DISPLAY THE PREVIOUS SUBCOMMAND'
OREN     MSG   'RENAME   - RENAME A MEMBER'
OREP     MSG   'REPLACE  - CHANGE LINES CONTAINING A STRING'
ORES     MSG   'RESTORE  - RESURRECT A PREVIOUSLY DELETED MEMBER'
OREV     MSG   'REVIEW   - BROWSE DATA'
OSUB     MSG   'SUBMIT   - SUBMIT JCL'
OSUL     MSG   'SUBLIST  - DEFINE A MEMBER LIST SUBGROUP'
OTSO     MSG   'TSO      - INVOKE A TSO COMMAND'
OTSE     MSG   'TSOEDIT  - EDIT USING TSO EDIT'
OTSL     MSG   'TSOLIST  - LIST DATA WITH TSO LIST'
OUSA     MSG   'USAGE    - LIST DATA SET STATISTICS'
*OUT1    MSG   'UT1      - UTILITY PANEL #1 (NON-MEMBER)'      SS NOV84
*OUT2    MSG   'UT2      - UTILITY PANEL #2 (WITH MEMBERS)'    SS NOV84
OVER     MSG   'VERIFY   - VALIDITY CHECK THE DATA SET'
OVPS     MSG   'VPSPRINT - PRINT A HARDCOPY WITH VPSPRINT'
         DC    X'FF'
         PRINT GEN
         TITLE 'P D S  --  PDS OUTCOPY                         6/10/85'
***********************************************************************
***      OUT SUBCOMMAND        ADDED BY BRUCE LELAND -- JULY, 1983  ***
***********************************************************************
*
         SPACE 1
OUTCOPY  CSECT
         USING *,R8
         LA    R1,L530                       ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO                CORRECT?
         BZ    MSGNEW                        YES, BRANCH
         TM    #OUT,1                        CLOSE FORCED?
         BO    OUT84                         YES, BRANCH
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT ALREADY OPEN?
         BO    OUT10                         YES, BRANCH
         AIF ('&CISP' EQ 'NO SPF').NSPF250
         TM    FLAGSEE,FBKGRND               BACKGROUND MODE?
         BO    OUT04                         YES, BRANCH
         TM    FLAGSFF,FSPFOPT6+FSPFERR+FSPFCALL+FSPFDIAL  CHANGE SPF?
         BNZ   OUT04                                       NO, BRANCH
         BAL   R2,SPFRECUR                   INVOKE PDS AS A DIALOG
         SPACE 1
.NSPF250 ANOP
OUT04    MVC   QAMDCB(LQSAMDCB),QSAMDCB      CONSTRUCT A DCB
         MVI   OPENLIST,X'80'                END OF LIST
         OPEN  (QAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         MVC   INSERT#1(8),OTEXTOUT          OUTCOPY
         LA    R1,L780$1                     ASSUME OPEN ERROR
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT OPEN?
         BNO   MSGNEWXX                      NO, ERROR
         CLI   #OUT,1                        CORRECT DCB ATTRIBUTES?
         BNE   OUT84                         NO, BRANCH
         SPACE 2
OUT10    MVI   #OUT,X'40'
         LA    R15,FIRST4K
         LA    R15,SAVEOUT-FIRST4K(,R15)     SAVE TEXT AREA
         CLI   #OUTOPTN,1                    ANY TEXT?
         BNE   *+10                          NO, BRANCH
         MVC   0(40,R15),MSGLINE             YES, SAVE THE DEFAULT TEXT
         CLI   #OUTOPTN,2                    NOTEXT?
         BNE   *+8                           NO, BRANCH
         MVI   0(R15),0                      YES, RESET DEFAULT TEXT
         MVC   MSGLINE(40),0(R15)            SET THE TEXT OPERAND
         B     OUT14
         SPACE 1
OUT12    CLI   #OUTOPTN,1                    ANY TEXT?
         BE    OUT14                         YES, BRANCH
         MVI   MSGLINE,0                     NO, RESET THE TEXT
         SPACE 1
OUT14    TM    FLAGSEE,FOUTASS               ALIASES ALSO DESIRED?
         BNO   *+8                           NO, BRANCH
         MVI   STARTTR+2,X'01'               YES, SET THE START TTR
         L     R2,RECOVER                    PREVIOUS RECOVERY ADDRESS
         LA    R1,OUT80                      RESUME ADDRESS
         ST    R1,RECOVER                    IN-LINE RECOVERY
         STM   R2,R8,#OUTREGS                SAVE REGISTERS
         TM    FLAGSFF,FCHANGE               CHANGED DATA SET?
         BNZ   OUT50                         NO, BRANCH
         OI    FLAGSFF,FCHANGE               YES, GOT A COPY STATEMENT
         TM    FLAGSFF,FIEBUPDT              IEBUPDTE FORMAT?
         BO    OUT50                         YES, BRANCH
         MVC   #OUT,OUTCOPYS                 " COPY OUTPUT=OUTDD,IN..."
         SR    R1,R1                         MACHINE LENGTH
         LA    R3,DSNAME-2
         AH    R3,DSNLEN
         LA    R0,8                          MAX LOOPS +1
         CH    R0,DSNLEN                     MIN (8, NAME LENGTH)
         BH    *+8
         LH    R0,DSNLEN
OUT30    S     R0,=F'1'                      ONE CHARACTER?
         BZ    OUT40                         YES, BRANCH
         CLI   0(R3),C'.'                    PERIOD?
         BE    OUT40                         YES, BRANCH
         LA    R1,1(,R1)                     MACHINE LENGTH
         BCT   R3,OUT30                      BACK UP ONE BYTE
         SPACE 1
OUT40    MVC   #OUT+33(*-*),1(R3)            <<EXECUTED>>
         EX    R1,*-6                        MOVE LOW-LEVEL QUALIFIER
         PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         LA    R3,OUTMEMBS                   START OF DUPLICATE LIST
         TM    FLAGSFF,FNOECHO               OUTPUT TO THE TERMINAL?
         BNZ   OUT42                         NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE 1
OUT42    ICM   R3,B'0111',1(R3)              NEXT DUPLICATE STRUCTURE
         BZ    OUT50                         END, BRANCH
         MVI   6(R3),0                       CLEAR THE
         MVI   7(R3),1                                CURRENT COUNT
         B     OUT42                         LOOP FOR ALL STRUCTURES
         SPACE 1
OUT50    LA    R4,OUTMEMBS                   ROOT OF DUP STRUCTURES
OUT54    LR    R3,R4
         ICM   R4,B'0111',1(R4)              NEXT LIST ENTRY?
         BNZ   OUT60                         YES, BRANCH
         LA    R5,256                        256 -1 MEMBERS
         LR    R0,R5
         SLL   R0,3                          ELEMENT WIDTH IS 8
         MVI   OUTMEMBS,22                   SUBPOOL IS 22
         ICM   R0,B'1000',OUTMEMBS           GET THE SUBPOOL
         GETMAIN R,LV=(0)
         LR    R4,R1
         STCM  R4,B'0111',1(R3)              CHAIN TO LAST STUCTURE
         XC    0(8,R4),0(R4)                 NULL LINK POINTERS
         STH   R5,4(,R4)                     MAXIMUM ELEMENTS/STRUCTURE
         MVI   7(R4),1                       ONE ITEM CURRENTLY
         B     OUT70
         SPACE 2
OUT60    LH    R5,6(R4)                      NUMBER OF ELEMENTS
         LR    R1,R4
OUT64    S     R5,=F'1'                      ANY MORE ITEMS?
         BZ    OUT70                         NO, BRANCH
         LA    R1,8(,R1)                     NEXT ELEMENT
         CLC   0(8,R1),DIRNAME               THIS ELEMENT?
         BNE   OUT64                         NO, BRANCH
         B     OUT74                         YES, IGNORE DUPLICATE
         SPACE 1
OUT70    CLC   6(2,R4),4(R4)                 CURRENT:MAXIMUM
         BE    OUT54                         EQUAL - GET NEXT STRUCTURE
         MVC   8(8,R1),DIRNAME               ADD THIS ITEM
         LH    R5,6(R4)                      ADD
         LA    R5,1(,R5)                        1 TO
         STH   R5,6(R4)                             NUMBER OF ELEMENTS
         MVC   #OUT,OUTSEL                   "          S M=12345678  "
         MVC   #OUT+14(8),DIRNAME            MEMBER NAME
         LA    R1,#OUT+14                    POINT TO EQUALS
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         CLI   MSGLINE,0                     ANY TEXT?
         BE    OUT71                         NO, BRANCH
         MVC   #OUT+24(40),MSGLINE           YES, ADD THE TEXT
         B     OUT73
         SPACE 1
OUT71    TM    FLAGSFF,FIEBUPDT              IEBUPDTE FORMAT?
         BNO   OUT72                         NO, BRANCH
         MVC   #OUT(13),OUTSELUP             ADD "./  ADD  NAME"
         TM    DIRFLAG,X'0F'                 SPF STATISTICS PRESENT?
         BO    OUT73                         YES, BRANCH
         TM    FLAGSCC,RECFMU                LOAD MODULE?
         BO    OUT73                         YES, BRANCH
         SPACE 1
         CLC   ZERO,DIRUSER                  ZERO?
         BE    OUT73                         YES, NO SSI
         CLC   =F'-1',DIRUSER                FFFFFFFF?
         BE    OUT73                         YES, NO SSI
         SPACE 1
         MVC   0(5,R1),OUTSSI                ADD ,SSI=
         UNPK  5(9,R1),DIRUSER(5)
         TR    5(8,R1),TRTABLE
         MVI   5+8(R1),X'40'
         B     OUT73
         SPACE 1
OUT72    MVI   1(R1),C','                    ADD BLANK AND COMMA
         MVC   2(8,R1),DIRNAME               MEMBER NAME AGAIN
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         MVI   0(R1),C')'                    ADD ONE PARENTHESIS
         MVI   1(R1),C')'                    ADD SECOND PARENTHESIS
OUT73    PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         TM    FLAGSFF,FNOECHO               OUTPUT TO THE TERMINAL?
         BNZ   OUT74                         NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE 1
OUT74    TM    FLAGSEE,FOUTASS               ASSOCIATE MEMBERS DESIRED?
         BNO   OUT76                         NO, BRANCH
         BAL   R14,READDIR                   GET NEXT DIRECTORY MEMBER
         B     OUT76                         LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR                 TTR MATCH?
         BNE   OUT74                         NO, BRANCH
         MVC   DIRNAME(8),MEMNAME            SUBSTITUTE THE MEMBER NAME
         B     OUT50                         ADD ALIAS TO THE OUTPUT
         SPACE 2
OUT76    ST    R2,RECOVER                    RESET THE RECOVERY ADDRESS
         B     NEWCMD                        TERMINATE
         SPACE 3
OUT80    LM    R2,R8,#OUTREGS                RESET REGISTERS
         ST    R2,RECOVER                    RESTORE RECOVERY ADDRESS
         SPACE 1
OUT84    MVI   OPENLIST,X'80'                END OF LIST
         CLOSE (QAMDCB),MF=(E,OPENLIST)      CLOSE OUT DATA SET
         NI    FLAGSFF,FF-FCHANGE            NEED A COPY STATEMENT
         LA    R1,L781$1                     OUT DCB IS NOT FB, 80
         MVC   INSERT#1(8),OTEXTOUT          OUTCOPY
         CLI   #OUT,0                        ERROR CLOSE?
         BE    MSGNEWXX                      YES, OUTPUT A MESSAGE
         LA    R1,L080                       DATA SET IS NOW CLOSED
         B     MSGNEWXX                      NOW CLOSED
         SPACE 4
         USING IHADCB,R1
OUT90    CLC   DCBDSORG(2),ZERO              ANY DSORG?
         BNE   *+8                           YES, BRANCH
         OI    DCBDSORG,DS1DSGPS             NO, USE DSORG=PS
         TM    DCBDSORG,DS1DSGPS             DSORG=PS?
         BNOR  R14                           NO, ERROR
         CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+8                           YES, BRANCH
         MVI   DCBRECFM,DCBRECF+DCBRECBR     NO, USE RECFM=FB
         TM    DCBRECFM,DCBRECF              RECFM=F.?
         BNOR  R14                           NO, ERROR
         LA    R0,80                         DEFAULT LRECL
         CLC   DCBLRECL(2),ZERO              ANY LRECL?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBLRECL                   NO, USE LRECL=80
         CH    R0,DCBLRECL                   LRECL=80?
         BNER  R14                           NO, ERROR
         LA    R0,3120                       DEFAULT BLKSIZE
         CLC   DCBBLKSI(2),ZERO              ANY BLKSIZE?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBBLKSI                   NO, USE BLKSIZE=3120
         MVI   #OUT,1                        ALL ATTRIBUTES ARE CORRECT
         BR    R14
         DROP  R1
         SPACE 2
         PRINT NOGEN
QSAMDCB  DCB   DSORG=PS,DDNAME=PDSOUT,MACRF=(PM),EXLST=OUT99
LQSAMDCB EQU   *-QSAMDCB
         PRINT GEN
         SPACE 2
OUT99    DC    0F'0',X'85',AL3(OUT90)        OPEN EXIT ONLY
OUTCOPYS DC    CL80'          COPY OUTDD=OUTPUT,INDD=1'
OUTSEL   DC    CL80'          S M=12345678            '
OUTSELUP DC    CL13'./  ADD  NAME'
OUTSSI   DC    C',SSI='
OTEXTOUT DC    CL8'OUTCOPY'
         TITLE 'P D S  --  PDS PRINTOFF AND ITS ALIASES        6/10/85'
***********************************************************************
***    PRINTOFF SUBCOMMAND (AND ITS ALIASES)                        ***
***********************************************************************
*
         SPACE 1
*   SUPPORTED SUBCOMMANDS: DSPRINT, PRINTOFF, REVIEW, SUBMIT,
*                          TSOLIST, VPSPRINT, &XS1, &XS2, &XS3
PRINTOFF CSECT
         USING *,R8
         MVC   MSGTEXT1+4(8),##SUBCAL   PROCESSOR TO ATTACH
         MVI   MSGTEXT1+12,X'40'        ADD A BLANK
         MVI   MSGTEXT1+13,C''''        ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R4,14(,R15)              LENGTH OF "PRINTOFF'"
         LA    R3,MSGTEXT1+13(R15)      POINT TO CURRENT BYTE -1
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    PRINT6                   YES, BRANCH
         SPACE 1
         MVI   1(R3),C'('               MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R4,2+8(,R4)              LENGTH OF MEMBER +1
         LA    R2,8                     MAXIMUM MACHINE LENGTH+1
PRINT3   BCTR  R3,0                     SCAN
         BCTR  R4,0                         BACKWARDS
         CLI   0(R3),X'40'                           FOR
         BNE   *+8                                       FIRST
         BCT   R2,PRINT3                                      NON-BLANK
         SPACE 1
         BCTR  R2,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R2,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
PRINT6   MVI   1(R3),C''''              ADD A QUOTE
         MVI   2(R3),X'40'              ADD A BLANK
         LA    R4,3(,R4)                ACCOUNT FOR ")' "
         LA    R2,MSGLINE               START OF ANY ADDED TEXT
         SR    R2,R4                    WHERE TO START MESSAGE
         LR    R14,R4                   CURRENT MESSAGE LENGTH
         BCTR  R14,0                    MESSAGE MACHINE LENGTH
         MVC   0(*-*,R2),MSGTEXT1       <<EXECUTED>>
         EX    R14,*-6                  MOVE IN MESSAGE TEXT
         SPACE 1
PRINT9   AH    R4,#COMMDSZ              ADD REMAINING LENGTH
         SLL   R4,16                    CLEAR BOTTOM TWO BYTES
         STCM  R4,B'1111',0(R2)         SAVE STRING TOTAL LENGTH
         SPACE 1
         LA    R3,##SUBCAL              PROCESSOR TO ATTACH
*        MESSAGE (R2)                   DELETE * TO OUTPUT COMMAND
         ST    R2,ADDRTEXT              COMMAND ADDRESS
         ST    R2,ADDRCBUF              COMMAND ADDRESS
         CLI   ##SUBCAL,C'%'            IMPLIED CLIST?
         BE    EXEC                     YES, BRANCH
         MVI   3(R2),9                  POINT TO OPERAND OFFSET
         SPACE 1
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'SM'                THIRD PARAMETER
         LA    R3,=F'0'                   FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+12,X'80'          LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BNO   *+6                        NO, BRANCH
         BALR  R14,R15                    YES, LET SPF KNOW
         SPACE 1
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),##SUBCAL  PROCESSOR COMMAND NAME
         LA    R1,ADDRTEXT              ADDRESS OF THE CPPL
         LA    R3,##SUBCAL              PROCESSOR TO ATTACH
         BAL   R2,ATTACH                CLEAR THE ATTENTION ECB
         B     NEWSTAX                  DO THE NEXT COMMAND
         TITLE 'P D S  --  PDS RENAME                          6/10/85'
***********************************************************************
***      RENAME SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
RENAME   CSECT
         USING *,R8
         CLI   #RENRANG,1              RENAME GROUP?
         BE    RENAME30                YES, BRANCH
         SPACE 1
         TM    SPFLAG2,SPFPOST            LINE COMMAND?
         BNO   RENAME3                    NO, BRANCH
         LA    R1,FIRST4K                 GET ADDRESSABILITY
         MVC   MEMLNMSV-FIRST4K(8,R1),MEMBER2
         B     RENAME6                    NO, BRANCH
         SPACE 1
RENAME3  MVC   MEMBERD+1(2),LMEMBER2   CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         CLI   #RENREP,0               ANY REPBY NAME?
         BE    RENAME6                 NO, BRANCH
         MVC   MEMBERD+1(2),LMEMBER1   YES, SAVE NEW DEFAULT NAME
         MVC   MEMBERD+1+2(8),MEMBER1  YES, SAVE NEW DEFAULT NAME
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 1
RENAME6  MVC   DSNMEMQ(8),MEMBER1      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         LA    R6,NEWCMD               ASSUME NOT A SWAP REQUEST
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BNO   RENAME20                NO, BRANCH
         SPACE 1
*** RENAME WITH SWAP OPERAND *** *** *** *** *** *** *** *** *** *** **
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME10                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
RENAME10 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     NEWCMD
         SPACE 3
*** RENAME USING THE REPBY PARAMETER *** *** *** *** *** *** *** *** **
RENAME20 CLI   #RENREP,0               ANY REPBY OPERAND?
         BE    RENAME26                NO, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#RENREP      MEMBER NAME FOR AN ERROR MESSAGE
         BLDL  INDCB,BLDLLIST          CHECK FOR MEMBER PRESENCE
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME22                  00 - FOUND THE MEMBER
         B     NOMEMBER                  04 - MEMBER WAS NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
RENAME22 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME24                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 1
RENAME24 BAL   R6,RENAME90             UPDATE ANY ALIAS ENTRIES
         MVC   MEMBER2(8),MEMBER1      SWAP MEMBER NAMES
         MVC   MEMBER1(8),#RENREP      SWAP MEMBER NAMES
         LA    R6,NEWCMD               EXIT ADDRESS
         SPACE 3
*** RENAME WITH NO OPERANDS  *** *** *** *** *** *** *** *** *** *** **
RENAME26 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP OPERAND ** *** *** *** *** *** *** *** *** *** **
RENAME30 NI    FLAGSBB,FF-FLINESET     NO OUTPUT IN PROGRESS YET
         MVI   SUBPOOLT,21             TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'         READ THE DIRECTORY
         LA    R5,256/2                256 MEMBERS INITIALLY
         SPACE 1
RENAME40 SLL   R5,1                    MEMBERS*2 FOR EACH LOOP
         SR    R2,R2
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                    MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)              ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1                   NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR      ANY PREVIOUS MEMBERS?
         BZ    RENAME42                NO, BRANCH
         SRL   R3,1                    YES, USE SECOND HALF OF TABLE
         LR    R14,R4                  NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                   MEMBERS*8 IS OLD TABLE SIZE
         LR    R2,R15                  DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0                  PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R2                   LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
RENAME42 ST    R4,#MEMPTR              MEMBER TABLE BASE
         AR    R4,R2                   WHERE TO ADD MEMBERS
         SPACE 3
RENAME44 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     RENAME52                END OF MEMBERS, BRANCH
         SPACE 1
         LH    R1,LMEMBER1             MEMBER NAME LENGTH
         CLC   MEMNAME(*-*),MEMBER1    <<EXECUTED>>
         EX    R1,*-6                  MEMBER:GROUP
         BL    RENAME44                  LOW, LOOP
         BH    RENAME52                  HIGH, DONE WITH FIRST PHASE
         MVC   DSNMEMQ(8),MEMNAME        EQUAL, CHECK FOR ENQUEUED
         BAL   R2,ENQMTEST
         NOP   0                       OUTPUT AN ERROR MESSAGE ONLY
         SPACE 2
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BO    RENAME46                YES, BRANCH
         OI    FLAGSBB,FLINESET        LINE NOW IN PROGRESS
         LA    R1,80                   ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON          ISPMODE ACTIVE?
         BO    RENAME45                YES, BRANCH
         TM    CONTOPTN,1              ANY LOG RECORDING?
         BO    RENAME45                YES, BRANCH
         GTSIZE ,                      TERMINAL SIZE
         CH    R1,=H'120'              120 OR LESS BYTES?
         BL    *+8                     YES, BRANCH
         LH    R1,=H'120'              NO, USE 120 BYTES
RENAME45 SH    R1,=H'7'                LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE             CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRENAM),MSGRENAM
         LA    R6,MSGLINE+4+L'MSGRENAM
         MVC   FULLWORD(3),L161$1
         SPACE 1
RENAME46 CLI   0(R2),X'40'             SCAN FOR
         BNE   *+8                             LAST
         BCT   R2,RENAME46                         NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   RENAME50
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R6,MSGLINE+3            INDENT FOR BLANKS
         SPACE 1
RENAME50 MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME        CURRENT MEMBER NAME
         LA    R4,16(,R4)              NEXT MEMBER NAME
         S     R3,=F'1'                ANY HOLD POSITIONS LEFT?
         BP    RENAME44                YES, BRANCH
         B     RENAME40                NO, REALLOCATE
         SPACE 2
RENAME52 LA    R1,L401                 ASSUME NO MEMBERS FOUND
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BZ    MSGNEW                  NO, EXIT
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MESSAGE MSGBLANK
         SPACE 1
         LA    R1,PDS391A              VERIFY THAT YOU WISH TO RENAME..
         BAL   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, BRANCH
         SPACE 2
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUE
         B     NEWCMD                  OPEN FAILED -- ERROR
         SPACE 3
         L     R3,#MEMPTR              FIRST MEMBER POINTER
         LA    R6,RENAME60             WHERE TO GO
         MVI   #RENDEFT,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         MVC   #RENDEFT+1(2+8+8+2),LMEMBER1  LENGTH1, MEMBER1, M2, L2
         SPACE 1
RENAME60 MVC   MEMBERD(1+2+8+8+2),#RENDEFT    FLAG, LEN1, MEM1, M2, L2
         MVC   MEMBERD+1(2),#RENDEFT+1+2+8+8  REPEAT LEN2 FOR LEN1
         MVC   MEMBERD+1+2(8),#RENDEFT+1+2+8  REPEAT MEM2 FOR MEM1
         NI    PMEMMIN,FF-X'80'               NO MEMBER LIST NOW
         CR    R3,R4                   DONE?
         BE    NEWCMD                  YES, BRANCH
         MVC   DIRNAME(16),0(R3)       CURRENT MEMBER NAME
         LA    R3,16(,R3)              NEXT MEMBER NAME
         MVC   MEMBER1(8),DIRNAME
         MVC   MEMBER2(8),DIRNAME
         LH    R1,LMEMBER2
         MVC   MEMBER2(*-*),#RENDEFT+1+2+8  <<EXECUTED>>
         EX    R1,*-6                       MOVE IN FIRST FEW BYTES
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BO    RENAME70                YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         EX    0,*                       08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP AND SWAP OPERAND   *** *** *** *** *** *** *** **
RENAME70 XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         SPACE 1
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         LTR   R15,R15                 ANY PROBLEM?
         BNZ   IOERROR                 YES, ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME80                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
RENAME80 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     RENAME60                PROCESS OTHER MEMBERS IN GROUP
         SPACE 2
** UPDATE ANY ALIAS ENTRIES FOR LOAD MODULES
RENAME90 MVC   INSERT#1(8),MEMBER1
         TSMSG L090$1
         SPACE 1
         TM    FLAGSCC,RECFMU          LOAD MODULE?
         BNOR  R6                      NO, BRANCH
         TM    DIRFLAG,DIRALIAS        AN ALIAS?
         BOR   R6                      YES, BRANCH
         SPACE 1
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
RENAME95 BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     0(R6)                   LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   RENAME95                NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   RENAME95                NO, CONTINUE SEARCHING
         SPACE 1
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         LA    R14,DIRREAL             CORRESPONDING MAIN ENTRY NAME
         TM    DIRATTR,ATTRSCTR        SCATTER LOADED?
         BNO   *+8                     NO, BRANCH
         LA    R14,DIRREALS            YES, USE THIS NAME
         CLC   0(8,R14),MEMBER2        ALIAS NAME POINTER=MEMBER NAME?
         BE    RENAME95                YES, BRANCH
         SPACE 1
         MVC   0(8,R14),MEMBER2        CHANGE THE MEMBER POINTER
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         TSMSG L065$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     RENAME95                CONTINUE FOR ALL ALIASES
MSGRENAM DC    C'MEMBERS TO BE RENAMED ARE:'
         TITLE 'P D S  --  PDS RESTORE                         6/10/85'
***********************************************************************
***   RESTORE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***********************************************************************
*
         SPACE 1
RESTORE  CSECT
         USING *,R8
         ICM   R1,B'1111',MEMBER2       TTR TO BE USED
         CLI   LMEMBER2+1,1             COMPARE LENGTH:1
         BH    REST010                    HIGH (DO NOT ADJUST)
         SRL   R1,8
         BE    REST010                    EQUAL (ADJUSTED ONE TIME)
         SRL   R1,8                       LOW (ADJUSTED TWICE)
         SPACE 1
REST010  STCM  R1,B'1111',MEMBER2       SAVE THE UPDATED ADDRESS
         STCM  R1,B'1111',DIRTTR        SAVE THE UPDATED ADDRESS
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST020                  NO, BRANCH
         MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         MVI   #RESTDIR+9,ATTREXEC      EXECUTABLE
         MVI   #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         MVI   #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST020  CLI   #RESTLIK,0               ANY LIKE OPERAND?
         BE    REST080                  NO, BRANCH
         XC    DIRNAME(74),DIRNAME
         MVC   DIRNAME,#RESTLIK         LIKE NAME
         BLDL  INDCB,BLDLLIST           GET MEMBER DIRECTORY
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST030                    00 - FOUND
         B     NOMEMBER                   04 - NOT FOUND
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
REST030  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  BLDL ADDS THINGS
         MVC   #RESTDIR,DIRFLAG         SAVE USER DATA
         NI    #RESTDIR,FF-X'80'        TURN OFF ANY ALIAS BIT
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST080                  NO, BRANCH
         XC    #RESTDIR,#RESTDIR        CLEAR THE USER DATA
         LA    R2,DIRAPF                POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR         SCATTER LOAD?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, POSITION OVER FIELDS
         TM    DIRFLAG,X'80'            ALIAS?
         BO    *+12                     YES, BRANCH
         CLI   8(R2),0                  CONVERTED ALIAS ENTRY?
         BE    *+8                      NO, BRANCH
         LA    R2,11(,R2)               ADD ALIAS LENGTH
         TM    DIRATTR2,DIRAOSLE        O/S MVS LINKAGE EDITOR?
         BNO   REST040                  NO, BRANCH
         TM    DIRATTR2,DIR2SSI         ANY SSI FIELD?
         BNO   REST050                  NO, BRANCH
REST040  LA    R1,1(,R2)                ADD FOR ROUNDING TO HALFWORD
         N     R1,=F'-2'                ROUND TO HALFWORD
         CLC   ZERO,0(R1)               ZERO?
         BE    REST050                  YES, BRANCH
         CLC   =F'-1',0(R1)             HIGH VALUES?
         BE    REST050                  YES, BRANCH
         MVC   #RESTSSI,0(R1)           SAVE THE SSI VALUE
         LA    R2,4(,R1)                POSITION AFTER SSI
         SPACE 1
REST050  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  VS LKED AND APF DATA GOOD?
         BO    REST060                     YES, BRANCH
         XC    DIRATTR2(3),DIRATTR2        NO, CLEAR NEW FLAGS
         B     REST070
         SPACE 1
REST060  CLI   0(R2),X'01'              VALID APF LENGTH?
         BNE   REST070                  NO, BRANCH
         MVC   #RESTAPF,1(R2)           YES, SAVE APF DATA
         SPACE 1
REST070  MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         NI    DIRATTR,X'CA'            RENT, REUS, ONLY LOAD, EXEC.
         NI    DIRATTR+1,X'09'          NOT EDIT, REFR
         MVC   #RESTDIR+9(2),DIRATTR    COPY INTO THE HOLD AREA
         OI    #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         NI    DIRATTR2,DIR2PAGA        PAGE BOUNDARY FLAG
         NI    DIRATTR2+1,X'1F'         RMODE, AMODE FLAGS
         MVC   #RESTDIR+19(2),DIRATTR2  COPY INTO THE HOLD AREA
         OI    #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST080  MVC   DIRNAME,MEMBER1          MEMBER NAME TO BE RESTORED
         MVC   DIRTTR(3),MEMBER2        TTR OF MEMBER TO BE RESTORED
         SPACE 1
         LA    R1,L873
         CLC   DIRTTR(3),DS1LSTAR       IN DATA SET BOUNDS?
         BNL   MSGNEW                   NO, ERROR
         SPACE 1
         TM    #RESFLAG,#RESFREP+#RESFDIS  REPEAT, DISPLAY OR PROMPT?
         BZ    REST803                     NO, BRANCH
         SPACE 1
         OI    FLAGSII,FNOTDOUB         FORCE SINGLE OR MULTIPLE BUFFER
         ICM   R1,B'1110',DIRTTR        T T R X  ZERO?
         BZ    REST210                  YES, BRANCH
         SRL   R1,8                     0 T T R
         S     R1,=F'1'                 0 T T R - 1 = 0?
         BZ    REST210                  YES, BRANCH
         SLL   R1,8                     T T R 0 (WHERE R IS REDUCED)
         STCM  R1,B'1110',DIRTTR        T T R
         CLI   MEMBER2+2,X'01'          R OF TTR 1 OR LESS?
         BH    REST210                  YES, BRANCH
         SRL   R1,16                    0 0 T T
         BCTR  R1,0                     0 0 T T - 1
         SLL   R1,16                    T T 0 0 (WHERE TT IS REDUCED)
         STCM  R1,B'1100',DIRTTR        T T  UPDATED
         MVI   DIRTTR+2,X'01'           RESET R TO 1 OF PREVIOUS TRACK
         SPACE 1
REST210  TM    #RESFLAG,#RESFREP        REPEAT?
         BZ    REST220                  NO, BRANCH
         SPACE 2
         LA    R1,7                     MACHINE LENGTH
         LA    R2,DIRNAME+7             LAST CHARACTER
         SPACE 1
         CLI   0(R2),X'40'              BLANK?
         BNE   *+10                     NO, BRANCH
         BCTR  R2,0                     PREVIOUS CHARACTER
         BCT   R1,*-10                  TRY ALL BYTES
         SPACE 1
         ST    R1,#RESTLEN                          NAME LENGTH
         SPACE 3
REST220  MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE IN USE
         LA    R5,1024                  1024 MEMBERS INITIALLY
         SPACE 1
REST240  LR    R0,R5
         SLL   R0,2                     MEMBERS*4 FOR TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         ST    R1,#MEMPTR               START OF TTR LIST
         LR    R4,R1
         XC    0(4,R4),0(R4)            DUMMY FIRST ELEMENT
         LR    R3,R5                    NUMBER OF ELEMENTS
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST242  BAL   R14,READDIR              GET THE NEXT MEMBER
         B     REST246                  END OF DIRECTORY, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR START<TTR OF THIS MEMBER?
         BNL   REST242                  NO, IGNORE
         S     R3,=F'1'                 WILL THIS FIT?
         BNP   REST244                  NO, BRANCH
         LA    R4,4(,R4)                NEXT TTR TABLE POSITION
         MVC   0(3,R4),MEMTTR           SAVE THE MEMBER START TTR
         MVI   3(R4),0                  CLEAR THE END FLAG
         B     REST242                  GET THE NEXT ENTRY
         SPACE 1
REST244  SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL TO FREE
         FREEMAIN R,SP=(0)
         SLL   R5,1                     DOUBLE THE NUMBER OF ENTRIES
         B     REST240
         SPACE 2
REST246  MVI   3(R4),X'FF'              MARK THE END OF THE TTR LIST
         BAL   R2,CLEAR                 CLEAR THE SCREEN
REST247  TM    #RESFLAG,#RESFREP        REPEAT RESTORE?
         BNO   REST248                  NO, BRANCH
         L     R1,#RESTSEQ              THIS MEMBER NUMBER
         LA    R1,1(,R1)
         ST    R1,#RESTSEQ              NEW MEMBER NUMBER
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'           CORRECT SIGN
         L     R1,#RESTLEN              MACHINE LENGTH
         UNPK  DIRNAME(8),DOUBLE(8)     CONVERT THE SEQUENTIAL NAME
         MVC   DIRNAME(*-*),MEMBER1     <<EXECUTED>>
         EX    R1,*-6                   ADD THE CODED NAME PORTION
         B     REST260                  SKIP THIS CLEAR
         SPACE 1
REST248  BAL   R2,CLEAR                 CLEAR THE SCREEN
         SPACE 3
REST260  MESSAGE MSGBLANK               ONE BLANK LINE
         MVC   STARTTR(3),DIRTTR        FIRST DISK ADDRESS TO READ
         MVI   #RESTERR+1,3             ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 2
REST262  L     R15,=A(EXCP)
         BALR  R14,R15
         LA    R1,L006                  ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST262                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
*  PERMANENT I/O ERROR
*    A.  IF TOO MANY ERRORS FOR THIS MEMBER, TERMINATE
*    B.  SIMULATE AN END OF MEMBER CONDITION
*    C.  CONTINUE ON THE NEXT TRACK
REST263  LA    R1,L804                  ASSUME TOO MANY ERRORS
         LH    R0,#RESTERR
         S     R0,=F'1'                 CORRECT?
         BM    MSGNEW                   YES, BRANCH
         STH   R0,#RESTERR
         TSMSG L801
         STM   R2,R12,28(R13)
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRL   R0,16                    TT OF LAST INPUT TTRN
         A     R0,=F'1'
         STH   R0,STARTTR               TT OF NEXT TRACK
         MVI   STARTTR+2,X'01'          R OF TTR
         SPACE 2
*  END OF MEMBER, GET THE NEXT DATA BLOCK
REST264  L     R15,=A(EXCP)
         BALR  R14,R15
         LA    R1,L006                  ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST266                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST266  STM   R2,R12,28(R13)           CONVERT MBBCCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         STCM  R0,B'1110',DIRTTR        TTR OF THIS MEMBER
         CLM   R0,B'1110',MEMBER2       AT OR AFTER THE START POINT?
         BL    REST262                  NO, BRANCH
         SPACE 1
         L     R4,#MEMPTR               START OF TTR TABLE
REST268  CLC   DIRTTR(3),0(R4)          IN THE TABLE?
         BE    REST262                  YES, IGNORE THIS MEMBER
         CLI   3(R4),X'FF'              END OF TTR TABLE?
         LA    R4,4(,R4)
         BNE   REST268                  NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   REST410                  NO, BRANCH
         CLI   #RESTNAM,X'40'           ANY MODULE NAME?
         BH    REST430                  YES, BRANCH
         SPACE 1
REST410  CLI   #RESTSTR+1,0             ANY STRING?
         BNE   REST490                  NO, BRANCH
         B     REST710
         SPACE 2
REST420  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST430                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST430  L     R3,EXCPBUFF              START OF RECORD
         CLI   0(R3),X'80'              END OF ESD, FIRST IDR?
         BE    REST262                  YES, BRANCH
         CLI   0(R3),X'20'              ESD RECORD?
         BNE   REST420                  NO, BRANCH
         SPACE 1
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 2
         USING ESDNAME,R3
REST440  TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST450                  NO, BRANCH
         CLC   ESDNAME(8),#RESTNAM      THIS MODULE NAME?
         BE    REST470                  YES, BRANCH
REST450  LA    R3,16(,R3)               NEXT ESD
         BCT   R4,REST440               DO ALL ESD SUBENTRIES
         B     REST420                  DO ALL ESD RECORDS
         DROP  R3
         SPACE 2
REST470  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         CLI   #RESTSTR+1,0             ANY STRING TOO?
         BE    REST590                  NO, BRANCH
         L     R15,=A(EXCP)
         BALR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST490  XC    WORKTBL,WORKTBL
         SR    R15,R15
         IC    R15,#RESTSTR+2           FIRST SEARCH CHARACTER
         LA    R1,WORKTBL(R15)
         MVI   0(R1),X'77'              SET SEARCH CHARACTER OF STRING
         B     REST510                  SKIP FIRST READ
         SPACE 2
REST500  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST510                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST510  L     R6,EXCPBUFF              START OF RECORD
         L     R1,LS                    LENGTH OF RECORD
         LA    R4,256                   MAXIMUM LENGTH FOR TRT
         LR    R5,R1
         AR    R5,R6                    END OF RECORD
         BCTR  R5,0                     END OF RECORD -1
         B     REST540
         SPACE 1
REST520  BXLE  R6,R4,REST540            GET NEXT DATA SEGMENT
         B     REST500                  GET NEXT RECORD
         SPACE 1
REST540  LR    R2,R4                    EXECUTE LENGTH +1
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    REST520                  NO, BRANCH
         SPACE 1
         CR    R2,R14                   LONGER THAN RECORD LEFT?
         BNH   *+8                      NO, BRANCH
         LA    R2,1(,R14)               YES, USE REMAINING LENGTH
         BCTR  R2,0                     EXECUTE LENGTH
         SPACE 1
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LR    R3,R14
         SPACE 1
         LH    R15,#RESTSTR             STRING LENGTH
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    EXECUTE LENGTH
         CR    R14,R5                   BEYOND RECORD LENGTH?
         BL    *+6                      NO, BRANCH
         LR    R14,R5                   YES, USE RECORD END -1
         SPACE 2
REST550  EX    R2,REST560               FIRST BYTE OF STRING FOUND?
         BZ    REST520                  NO, BRANCH
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   IN THIS SEGMENT?
         BH    REST520                  NO, BRANCH
         CLC   0(*-*,R1),#RESTSTR+2     <<EXECUTED>>
         EX    R15,*-6                  ENTIRE STRING PRESENT?
         BE    REST590                  YES, BRANCH
         LA    R1,1(,R1)                NO, TRY NEXT BYTE
         LR    R2,R3                    LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   REST550                  NO, KEEP SEARCHING
         B     REST520                  YES, TRY NEXT SEGMENT
         SPACE 1
REST560  TRT   0(*-*,R1),WORKTBL        <<EXECUTED>>
         SPACE 2
REST590  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         L     R15,=A(EXCP)
         BALR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST710  UNPK  INSERT#1(7),DIRTTR(4)    TTR OF THIS DELETED MEMBER
         TR    INSERT#1(6),TRTABLE      MAKE PRINTABLE
         MVC   INSERT#1+6(2),BLANKS
         TSMSG L101$1
         TM    #RESFLAG,#RESFDIS        DISPLAY OPTION?
         BNO   REST790                  NO, BRANCH
         MESSAGE MSGBLANK
         SR    R5,R5                    LINE IN PROGRESS POINTER
         SR    R6,R6                    OUTPUT COUNTER
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST736                  YES, BRANCH
         B     REST762                  NO, MUST BE V OR F
         SPACE 3
*  RECORD FORMAT U (LOAD MODULE)
REST730  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST736                    00 - SUCCESSFUL READ
         B     REST732                    04 - END OF MEMBER
         B     REST732                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST732  LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST734                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         B     REST790
         SPACE 1
REST734  TSMSG L805                     NO CESD OR LKED IDR RECORDS
         MVC   STARTTR(3),DIRTTR        RE-READ THE MEMBER
         B     REST760                  OUTPUT WITH CHARACTER OUTPUT
         SPACE 2
REST736  L     R3,EXCPBUFF              BUFFER ADDRESS
         CLI   0(R3),X'20'              CESD RECORD?
         BNE   REST750                  NO, CHECK FOR LINKAGE-EDIT IDR
         CH    R6,=H'5'                 NEED TO BUILD MORE CSECT LINES?
         BNL   REST730                  NO, BRANCH
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     LENGTH/16=NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 1
         USING ESDNAME,R3
REST738  TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST746                  NO, BRANCH
         CLI   ESDNAME,X'40'            BLANK CSECT NAME?
         BE    REST746                  YES, IGNORE
         LA    R2,ESDNAME+7
         LTR   R5,R5                    LINE IN PROGRESS?
         BP    REST740                  YES, BRANCH
         LA    R1,80                    ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?
         BO    REST739                  YES, BRANCH
         TM    CONTOPTN,1               ANY LOG RECORDING?
         BO    REST739                  YES, BRANCH
         GTSIZE ,                       TERMINAL SIZE
         CH    R1,=H'120'               120 OR LESS BYTES?
         BL    *+8                      YES, BRANCH
         LH    R1,=H'120'               NO, USE 120 BYTES
REST739  TM    FLAGSCC,RECFMU           LOAD LIBRARY?
         BNO   *+8                      NO, BRANCH
         SH    R1,=H'7'                 YES, LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE              CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRESTO),MSGRESTO
         LA    R5,MSGLINE+4+L'MSGRESTO
         MVC   FULLWORD(3),L164$1
         SPACE 2
REST740  CLI   0(R2),X'40'              SCAN BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R2,REST740                                    NON-BLANK
         SPACE 1
         LA    R0,ESDNAME
         SR    R2,R0                    MACHINE LENGTH OF NAME
         LA    R15,2(R2,R5)             CURRENT + NAME-1 + BLANK + 1
         LA    R1,MSGLINE
         SR    R15,R1                   DISPLACEMENT INTO THE LINE
         C     R15,LINESIZE             PAST END OF LINE?
         BNH   REST744                  NO, BRANCH
         SPACE 1
         LA    R6,1(,R6)                ONE MORE OUTPUT LINE
         CH    R6,=H'5'                 ENOUGH LINES?
         BL    REST742                  NO, BRANCH
         MVC   0(4,R5),RESTLDOT         INDICATE MORE CSECTS EXIST
         LA    R5,5(,R5)                INCREMENT TO END +1
         B     REST730
         SPACE 2
REST742  LA    R1,MSGLINE+4
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3             INCREMENT TO END
         SPACE 1
REST744  MVI   0(R5),X'40'              BLANK BEFORE CSECT NAME
         MVC   1(8,R5),ESDNAME          CSECT NAME
         TR    1(8,R5),TRLINE           MAKE PRINTABLE
         LA    R5,2(R2,R5)              POSITION TO FOLLOWING COMMA
         MVI   0(R5),C','               ADD THE COMMA
         LA    R5,1(,R5)                POSITION AFTER THE COMMA
         SPACE 1
REST746  LA    R3,16(,R3)               NEXT ESD ENTRY
         BCT   R4,REST738               LOOP FOR ALL ENTRIES
         B     REST730                  GET THE NEXT RECORD
         DROP  R3
         SPACE 3
REST750  CLI   0(R3),X'80'              IDR RECORD?
         BNE   REST730                  NO, CONTINUE INPUT
         TM    2(R3),IDRLKED            LINKAGE EDITOR IDR RECORD?
         BNO   REST730                  NO, CONTINUE INPUT
         LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST752                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    LENGTH OF MESSAGE SO FAR
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         TSMSG (R1)
         MVI   MTHIGHL,8
         SPACE 1
REST752  MESSAGE MSGBLANK
         LA    R1,12+3(,R3)             ADDRESS OF YYDDD FORMAT DATE
         LA    R15,LKEDDATE             ADDRESS OF YYMMDD FORMAT OUTPUT
         BAL   R14,CONVDATE             CONVERT TO MMDDYY FORMAT
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         TSMSG L106$1
         B     REST790                  PROMPT FOR RESTORE
         SPACE 2
*  RECORD FORMAT V, F OR U
REST760  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST762                    00 - SUCCESSFUL READ
         B     REST778                    04 - END OF MEMBER
         B     REST778                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST762  L     R5,LS                    BLOCK LENGTH
         L     R2,EXCPBUFF              BUFFER START
         LR    R1,R5
         AR    R5,R2                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, ASSUME RECFM=U
         AH    R2,=H'4'                 SKIP BLOCK LENGTH WORD
         B     REST766
         SPACE 2
REST764  BXLE  R2,R4,REST766            GET THE NEXT LOGICAL RECORD
         B     REST760                  END OF BLOCK, BRANCH
         SPACE 2
REST766  TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R2)         RECORD LENGTH +4
         AH    R2,=H'4'                 SKIP RECORD LENGTH WORD
         SH    R4,=H'4'                 RECORD LENGTH VALID?
         BP    REST768                  YES, BRANCH
         SR    R4,R4                    NO, USE LENGTH ZERO
         B     REST764                  IGNORE THE RECORD
         SPACE 1
REST768  LA    R6,1(,R6)                ANOTHER OUTPUT RECORD
         CH    R6,=H'5'                 ENOUGH OUTPUT?
         BH    REST778                  YES, BRANCH
         MVC   INSERT#1(8),BLANKS       BLANK
         LA    R0,240(,R6)              PRINTABLE COUNT CHARACTER
         STC   R0,INSERT#1
         TSMSG L144$1
         LR    R1,R4                    CURRENT RECORD LENGTH
         CH    R1,=H'256'
         BNH   *+8
         LH    R1,=H'256'               MAXIMUM DISPLAY LENGTH
         BCTR  R1,0                     MACHINE LENGTH
         MVC   MSGTEXT1+4(*-*),0(R2)    <<EXECUTED>>
         EX    R1,*-6                   MOVE IN THE DISPLAY TEXT
         TR    MSGTEXT1+4(256),TRLINE   CONVERT TO PRINTABLES
         LA    R1,4+1(,R1)              MESSAGE LENGTH
         CH    R4,=H'256'               TRUNCATED RECORD MESSAGE?
         BNH   *+14                     NO, BRANCH
         MVC   MSGTEXT1+4+256(4),RESTLDOT  INDICATE THAT MORE EXISTS
         LA    R1,4(,R1)                ADD TO THE MESSAGE LENGTH
         STH   R1,MSGTEXT1
         MESSAGE MSGTEXT1
         C     R4,LINESIZE              LRECL EXACTLY MATCH LINESIZE?
         BE    REST764                  YES, A BLANK LINE ALREADY (+1)
         MESSAGE MSGBLANK
         B     REST764
         SPACE 1
REST778  TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST260                  YES, INVALID LOAD MODULE
REST790  MESSAGE MSGBLANK
         TM    #RESFLAG,#RESFPRO        PROMPT OPTION?
         BO    REST794                  YES, BRANCH
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BNZ   REST805                         YES, BRANCH
REST794  TM    FLAGSEE,FBKGRND+FNOTTERM INPUT FROM THE TERMINAL?
         BNZ   REST805                  NO, RESTORE THIS MEMBER
         LA    R1,PDS390A               CORRECT MEMBER MESSAGE
         BAL   R2,YESNO1                RESPONSE "YES" OR "NO"?
         B     REST248                  NO, CONTINUE SEARCHING
         BAL   R2,CLEAR                 YES, CLEAR THE SCREEN
         B     REST805                       NO DIRECTORY CHECK NEEDED
         SPACE 3
REST803  MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST804  BAL   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     REST805                  LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR'S MATCH?
         BNE   REST804                  NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         LA    R1,L802$1                MEMBER ALREADY EXISTS AT TTR
         TM    MEMFLAG,X'80'            AN ALIAS?
         BNO   *+8                      NO, BRANCH
         LA    R1,L190$1                AN ALIAS ALREADY EXISTS AT TTR
         TSMSG (R1)
         SPACE 1
         TM    MEMFLAG,X'80'            ALIAS ENTRY?
         BO    REST804                  YES, CONTINUE SEARCHING
         SPACE 1
         CLC   RESTLTMP,DIRNAME         ALLOW IT ANYWAY?
         BNE   NEWCMD                   NO, BRANCH
         SPACE 2
REST805  TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BNO   REST896             NO, STOW AND QUIT
         XC    DIRUSER,DIRUSER     CLEAR THE ADDITIONAL FIELDS
         MVC   DIRFLAG(33),#RESTDIR  INITIALIZE THE USER AREA
         SPACE 1
         MVI   #RESTESD,AMODE24    AMODE 24 IS THE DEFAULT
         MVI   #RESTMOD,RMODEANY   SET RMODE ANY SUMMARY START
         XC    #BLKSI(4),#BLKSI    CLEAR THE MAXIMUM MEMBER BLOCKSIZE
         LA    R2,REST810          WHERE-TO-GO
         MVC   STARTTR(3),DIRTTR   START ADDRESS
         SPACE 3
REST806  L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST808                    00 - GOOD READ
         B     REST890                    04 - END OF MEMBER
         B     REST890                    08 - END OF DATA SET
         B     NEWCMD                     12 - I/O ERROR
         SPACE 2
REST808  L     R5,LS                    CURRENT LENGTH
         LR    R4,R0                    START OF BUFFER
         C     R5,#BLKSI                BLKSIZE>MAX?
         BNHR  R2                       NO, BRANCH
         ST    R5,#BLKSI                YES, SAVE NEW MAXIMUM
         BR    R2                       DECODE THE RECORD
         SPACE 3
REST810  MVI   ENTRYPT,C'?'             NO ENTRY POINT NAME YET
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BNE   REST812                  NO, BRANCH
         OI    DIRATTR,ATTRTEST         TEST SYM FLAG
         OI    DIRATTR+1,ATTRSYMS       SYMBOLS ARE AVAILABLE
         SPACE 1
REST812  LA    R2,REST812               WHERE-TO-GO NEXT
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BE    REST806                  YES, GET NEXT RECORD
         SPACE 1
         CLI   0(R4),X'20'              CESD RECORD?
         BNE   REST820                  NO, BRANCH
         SPACE 1
         LH    R6,4(,R4)                CURRENT ESDID
         LA    R3,8(,R4)                START OF ESD DATA
         USING ESDNAME,R3
         LH    R5,6(,R4)                LENGTH OF DATA IN BUFFER
         AR    R5,R3                    END OF DATA
         LA    R4,16                    LENGTH OF ONE ENTRY
         SR    R5,R4                    END OF DATA -(ONE ENTRY LENGTH)
         SPACE 3
REST813  IC    R0,ESDTYPE
         LA    R1,CODESEG          CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   ESDTYPE,CODESD      VALID EXTERNAL SYMBOL?
         BE    REST814             YES, BRANCH
         CLI   ESDTYPE,CODELR      EXTERNAL REFERENCE?
         BNE   REST817             NO, BRANCH
         SPACE 1
REST814  CLC   ESDNAME,DIRNAME     THIS MEMBER NAME?
         BNE   REST815             NO, BRANCH
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR  ENTRY ADDRESS ZERO?
         BZ    *+8                  YES, BRANCH
         NI    DIRATTR+1,FF-ATTREP0 NO, INSURE ATTR FLAG OFF
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
         SPACE 1
REST815  OC    ESDADDR(3),ESDADDR  ZERO OFFSET?
         BNZ   REST817             NO, BRANCH
         CLI   ENTRYPT,C'?'        ANY SYMBOL NAME YET?
         BNE   REST817             YES, BRANCH
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
REST817  CR    R0,R1               THIS SEGTAB/ENTAB ENTRY?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODESD      EXTERNAL DEFINITION?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODEPC      PRIVATE CODE?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODECM      COMMON STORAGE?
         BNE   REST819             NO, BRANCH
         SPACE 1
REST818  L     R1,ESDADDR-1        RELATIVE OFFSET
         L     R14,ESDLEN-1        LENGTH OF ESD ENTRY
         LA    R14,7(R1,R14)       MAXIMUM DISPLACEMENT +7
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         CLM   R14,B'0111',DIRSTORE SMALLER THAN PREVIOUS HIGH?
         BL    *+8                 YES, BRANCH
         STCM  R14,B'0111',DIRSTORE NO, UPDATE MODULE LENGTH
         NC    #RESTMOD,ESDXAFLG   SUMMARIZE ALL RMODE ENTRIES
         OC    ESDADDR,ESDADDR     ORIGIN AT LOCATION ZERO?
         BNZ   REST819             NO, BRANCH
         OC    ESDLEN,ESDLEN       NULL LENGTH BLOCK?
         BZ    REST819             YES, BRANCH
         STCM  R6,B'0011',DIRSCET  SAVE THE FIRST TEXT ESDID
         SPACE 1
REST819  A     R6,=F'1'            NEXT ESDID
         BXLE  R3,R4,REST813
         B     REST806             GET NEXT RECORD
         SPACE 3
         DROP  R3
REST820  LA    R2,REST820          WHERE-TO-GO NEXT
         CLI   0(R4),X'80'         IDR RECORD?
         BE    REST806             YES, GET NEXT RECORD
         SPACE 1
         LA    R2,REST830          WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'         FILLER RECORD?
         BNE   REST830             NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD  NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT    ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST830  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST831             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST831  TM    DIRATTR+1,ATTRNRLD  NO RLD MARKED?
         BNO   REST833             NO, BRANCH
         CLM   R5,B'0111',DIRSTORE FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST833             YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 1
REST833  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLI   0(R4),X'10'         SCATTER LOADED?
         BE    REST860             YES, BRANCH
         STCM  R0,B'1110',DIRSTART RESULT TTR
         STCM  R5,B'0011',DIRTEXTL SAVE LENGTH OF FIRST TEXT RECORD
         LA    R2,REST806          WHERE-TO-GO NEXT TIME
         CLI   0(R4),X'00'         OVERLAY DEFINITION RECORD?
         BNER  R2                  NO, LOOP UNTIL END OF MEMBER
         LA    R2,REST840          WHERE-TO-GO NEXT
         OI    DIRATTR,ATTROVLY    OVERLAY MODULE
         XI    DIRFLAG,X'40'+X'20' TWO USER TTR'S NOW
         LA    R14,7(,R5)
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         OC    DIREPA(3),DIREPA    NON-ZERO ENTRY POINT?
         BNZ   REST806             YES, BRANCH
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         NI    DIRATTR+1,FF-ATTREP0  INSURE ATTR FLAG OFF
         MVI   ENTRYPT,C'?'        ENTRY POINT ADDRESS IS NOT KNOWN
         MVI   #RESTESD,AMODE24    AMODE 24 AGAIN
         MVI   #RESTMOD,0          RMODE 24 FOR SUMMARY
         B     REST806
         SPACE 2
REST840  SRL   R5,2                LENGTH/4
         STC   R5,DIRNOTE#         NUMBER OF ENTRIES
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRNOTE  SAVE OVERLAY TABLE TTR
         B     REST806             CONTINUE UNTIL END-OF-MEMBER
         SPACE 1
REST860  STCM  R0,B'1110',DIRNOTE  START OF SCATTER LIST TABLE
         LA    R2,REST863          WHERE-TO-GO NEXT
         MVI   DIRFLAG,X'40'+16    2 TEXT TTRS, 16 HALFWORDS
         OI    DIRATTR,ATTRSCTR    SCATTER LOADED MODULE
         SR    R3,R3               ACCUMULATE LENGTH OF OVERLAY RECORDS
         SPACE 1
REST863  CLI   0(R4),X'10'           SCATTER DEFINITION RECORD?
         BNE   REST865               NO, BRANCH
         AR    R3,R5                 ACCUMULATE SCATTER RECORD LENGTH
         SH    R3,=H'4'              LESS THE HEADER AMOUNT
         B     REST806               CONTINUE FOR ALL SCATTER RECORDS
         SPACE 1
REST865  LA    R2,REST867            WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'           FILLER RECORD?
         BNE   REST867               NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD    NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT      ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST867  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST872             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST872  TM    DIRATTR+1,ATTRNRLD    NO RLD MARKED?
         BNO   REST876               NO, BRANCH
         CLM   R5,B'0111',DIRSTORE   FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST876               YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT   NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 2
REST876  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRSTART   START OF TEXT
         STCM  R5,B'0011',DIRTEXTL   LENGTH OF FIRST TEXT RECORD
         LA    R6,2(R6,R6)           (#CSECTS+1)*2+2
         SRL   R6,2                  ((#CSECTS+1)*2+2)/4
         SLL   R6,2                  ROUNDED TO NEXT FULLWORD*2
         STCM  R6,B'0011',DIRSCTL    SAVE LENGTH OF TRANSLATE TABLE
         SR    R3,R6
         STCM  R3,B'0011',DIRSCLL    SAVE LENGTH OF SCATTER LIST
         LA    R2,REST806            WHERE-TO-GO NEXT TIME
         BR    R2                    SCAN UNTIL END-OF-MEMBER
         SPACE 1
REST890  LA    R1,L800
         OC    DIRSTART(3),DIRSTART     ANY TEXT TTR?
         BZ    REST898                  NO, ERROR
         SPACE 1
         LA    R1,L703
         OC    DIRSTORE(3),DIRSTORE     STORAGE SIZE AVAILABLE?
         BZ    REST898                  NO, ERROR
         SPACE 1
         CLC   #BLKSI+2(2),RESTL1K      LARGEST BLOCK=1024?
         BNE   *+8                      NO, PROBABLY NOT DC
         NI    DIRATTR+1,FF-ATTRNODC    YES, TURN OFF NO DC
         SPACE 1
         LA    R2,DIRAPF                APF FLAG
         TM    DIRATTR,ATTRSCTR         SCATTER LOADED?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, SKIP OVER SCATTER DATA
         OC    #RESTSSI,#RESTSSI        ANY SSI DATA?
         BZ    REST893                  NO, BRANCH
         LA    R2,1(,R2)                PREPARE FOR HALFWORD ROUNDING
         N     R2,=F'-2'                ROUND TO HALFWORD
         OI    DIRATTR2,DIR2SSI         SSI DATA IS PRESENT
         OI    DIRFLAG,X'02'            ADD TWO MORE HALFWORDS
         MVC   0(4,R2),#RESTSSI         ADD THE SSI DATA
         LA    R2,4(,R2)                POSITION OVER SSI DATA
         SPACE 1
REST893  MVI   0(R2),X'01'              APF LENGTH IS 1
         MVC   1(1,R2),#RESTAPF         ADD APF DATA
         CLI   #RESTLIK,0               ANY LIKE MEMBER?
         BH    REST896                  YES, BRANCH (RMODE, AMODE SET)
         SPACE 1
         TM    #RESTMOD,RMODEANY        ALL CSECTS RMODE ANY?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRRMANY        YES, SET RMODE ANY
         TM    #RESTESD,AMODE24         LOW-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM24         YES, SET AMODE 24 ON
         TM    #RESTESD,AMODE31         HIGH-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM31         YES, SET AMODE 31 ON
         SPACE 1
REST896  BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     NEWCMD              COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A   ADD AT THE GIVEN TTR
         LR    R5,R15              SAVE RETURN CODE
         BAL   R2,SHUTSTOW         CLOSE STOW, RELEASE ENQUEUES
         SPACE 1
         B     *+4(R5)             PROCESS RETURN CODE
         B     REST897               00 - ADDED CORRECTLY
         B     MEMEXIST              04 - NAME ALREADY IN THE DIRECTORY
         EX    0,*                   08 - SHOULD NOT HAPPEN WITH ADD
         B     FULLDIR               12 - DIRECTORY IS ALREADY FULL
         B     IOERROR               16 - I/O ERROR IN THE DIRECTORY
         SPACE 1
REST897  MVC   INSERT#1(8),DIRNAME   RESTORED MEMBER
         TSMSG L091$1
         SPACE 1
         TM    SPFLAG2,SPFPOST            LINE COMMAND?
         BO    REST89D                    YES, BRANCH
         SPACE 1
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1          DEFAULT MEMBER1
         MVC   MEMBERD+1+2+8(8),LMEMBER1+2          DEFAULT MEMBER2
         MVC   MEMBERD+1+2+8+8(2),LMEMBER1          DEFAULT LENGTH
         MVI   MEMBERD,FMEMBER1                     ASSUME ONE MEMBER
         NI    PMEMMIN,FF-X'80'                     NO MEMBER LIST NOW
         TM    #RESFLAG,#RESFREP                    REPEAT?
         BZ    *+8                                  NO, BRANCH
         OI    MEMBERD,FMEMBER2+FMEMRANG+FMEM#MEM   MEMBER RANGE
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
REST89D  TM    FLAGSCC,RECFMU         LOAD LIBRARY?
         BNO   REST899                NO, DONE WITH THIS MEMBER
         SPACE 1
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         TSMSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         TSMSG L104$2
         B     REST899
         SPACE 1
REST898  TSMSG (R1)
         SPACE 2
REST899  TM    #RESFLAG,#RESFREP   REPEAT OPTION?
         BNO   NEWCMD              NO, DONE
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BZ    REST247                         NO, BRANCH
         TM    #RESFLAG,#RESFPRO   PROMPT OPTION?
         BO    REST247             YES, PROCESS THE NEXT DELETED MEMBER
         MESSAGE MSGBLANK          ONE BLANK LINE
         MESSAGE MSGBLANK          ONE BLANK LINE
         B     REST247             PROCESS THE NEXT DELETED MEMBER
         SPACE 2
#RESFREP EQU     X'01'            RESTORE: REPEAT
#RESFDIS EQU     X'02'            RESTORE: DISPLAY
#RESFPRO EQU     X'04'            RESTORE: PROMPT
RESTLFF8 DC    0F'0',X'FFFFFFF8'
RESTLTEM DC    C'TEMP'
RESTL1K  DC    H'1024'
RESTLDOT DC    C' ...'
RESTLTMP DC    C'TEMP'
MSGRESTO DC    C'CSECTS ARE:'
         TITLE 'P D S  --  PDS SUBLIST                         6/10/85'
***********************************************************************
***      SUBLIST SUBCOMMAND - ADDED BY BRUCE LELAND 4/14/85         ***
***********************************************************************
*
         SPACE 1
SUBLIST  CSECT
         USING *,R8
         LA    R1,L530                    ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO             CORRECT?
         BZ    MSGNEW                     YES, BRANCH
         SPACE 1
         TM    FLAGSEE,FMEMGRP            MEMBER GROUP DEFINE START?
         BO    SUBL100                    YES, BRANCH
         SPACE 1
         OI    FLAGSEE,FMEMGRP            FIRST SUBLIST ELEMENT
         MVC   MMEMMAX(4),MMEMMIN         POINT TO THE FIRST ELEMENT
         XC    MMEMBERD(1+2+8+8+2),MMEMBERD  CLEAR THE MEMBER NAME
         MVC   MMEMBERD+1+2(8),DIRNAME    MEMBER NAME
         LA    R1,DIRNAME+7               END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
SUBL050  CLI   0(R1),X'40'                BLANK?
         BNE   SUBL060                    NO, BRANCH
         BCTR  R1,0                       PREVIOUS CHARACTER
         BCT   R0,SUBL050                 CHECK ALL BYTES
SUBL060  STCM  R0,B'1100',MMEMBERD+1      SAVE THE MACHINE LENGTH
         MVI   MMEMBERD,FMEMBER1          A SINGLE MEMBER NAME
         NI    MMEMMIN,FF-X'80'           MEMBER LIST IS INACTIVE
         B     NEWCMD
         SPACE 2
SUBL100  L     R1,MMEMMAX                 LAST ACTIVE POINTER
         OC    1(3,R1),1(R1)              ANY SLOT ENTRIES?
         BNZ   SUBL180                    YES, BRANCH
         LA    R0,42*24                   42 MEMBERS, 24 WIDE EACH
         ICM   R0,B'1000',=AL1(24)        ASSUME SPF IS INITIALIZED
         STCM  R0,B'1000',SUBLIST2        MARK FOR LATER RELEASE
         TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF INITIALIZED?
         BM    SUBL110                    YES, BRANCH
         AIF   ('&CISP' NE 'ISPFV2').NSPFMEM
         TM    FLAGSFF,FSPFOPT6           CALLED FROM OPTION 6?
         BO    SUBL110                    YES, BRANCH
.NSPFMEM ANOP
         ICM   R0,B'1000',=AL1(23)        SPF IS NOT INITIALIZED
         STCM  R0,B'1000',SUBLIST1        MARK FOR LATER RELEASE
         MVI   SUBLIST2,0                 CLEAR THE EARLIER MARK
SUBL110  GETMAIN R,LV=(0)                 GET MORE ELEMENT STORAGE
         LR    R15,R1                     START OF TABLE
         L     R1,MMEMMAX                 CURRENT ELEMENT
         LA    R0,42-1                    NUMBER OF ELEMENTS -1
SUBL141  LA    R15,24(,R15)               NEXT ELEMENT POINTER
         ST    R15,0(,R1)                 POINT TO THE NEXT ELEMENT
         LR    R1,R15                     NEXT ELEMENT
         BCT   R0,SUBL141                 DO 41 ELEMENTS
         XC    0(4,R1),0(R1)              CLEAR THE LAST POINTER
         SPACE 2
SUBL180  L     R1,MMEMMAX                 CURRENT ADDED ELEMENT
         L     R15,0(,R1)                 NEXT POINTER
         NI    0(R15),FF-X'80'            NOT AN ACTIVE ELEMENT
         ST    R15,MMEMMAX                NEXT ELEMENT TO ADD
         OI    0(R1),X'80'                ACTIVE ELEMENT
         OI    MMEMMIN,X'80'              MEMBER LIST IS ACTIVE
         XC    4(2+8+8+2,R1),4(R1)        CLEAR THE MEMBER NAME
         MVC   4+2(8,R1),DIRNAME          MEMBER NAME
         LA    R15,DIRNAME+7              END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
SUBL250  CLI   0(R15),X'40'               BLANK?
         BNE   SUBL260                    NO, BRANCH
         BCTR  R15,0                      PREVIOUS CHARACTER
         BCT   R0,SUBL250                 CHECK ALL BYTES
SUBL260  STC   R0,5(R1)                   SAVE THE MACHINE LENGTH
         MVI   4(R1),FMEMBER1             A SINGLE MEMBER NAME
         B     NEWCMD
         TITLE 'P D S  --  PDS TIME                            6/10/85'
***********************************************************************
***      TIME  SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
TIME     CSECT
         USING *,R8
         MVC   MSGTEXT2(132),MSGBL132    BLANK LINE
         MVC   MSGTEXT2+4(14),TIMETBL    EDIT MASK
         TIME  DEC
         CL    R0,TIMEL12A                 MORNING?
         BL    *+12                      YES, BRANCH
         MVI   MSGTEXT2+14,C'P'          NO, SET TO PM
         SL    R0,TIMEL12A               NO, SUBTRACT 12 HOURS
         ST    R0,TIMEWRKW               ADJUSTED TIME
         TM    TIMEWRKW,X'08'            ADJUSTMENT ERROR (20 OR 21)?
         BZ    *+8                       NO, BRANCH
         NI    TIMEWRKW,X'09'            YES, CONVERT TO 8 OR 9 (PM)
         CLI   TIMEWRKW,X'00'            ZERO HOURS?
         BNE   *+8                       NO, BRANCH
         MVI   TIMEWRKW,X'12'            YES, CONVERT ZERO TO 12
         ED    MSGTEXT2+4(9),TIMEWRKW    FINISH TIME PROCESSING
         MVC   MSGTEXT2+34(25),DATEPC    TIME CONSTANT
         ST    R1,TIMEWRKW               SAVE 00YYDDDF DATE WORD
         UNPK  MSGTEXT2+44(5),TIMEWRKW+1(3) MOVE IN "YY"
         MVC   MSGTEXT2+51(2),MSGTEXT2+44 (YY.
         MVC   MSGTEXT2+54(3),MSGTEXT2+46 DDD)
         OI    MSGTEXT2+56,X'F0'         FIX THE ZONE
         MVC   MSGTEXT2+46(3),BLANKS     BLANK THE NEXT THREE BYTES
         LH    R1,TIMEWRKW+2
         SRL   R1,12                     D..  (HUNDREDS)
         M     R0,TIMEL10
         IC    R15,TIMEWRKW+2
         N     R15,=F'15'
         AR    R1,R15                    .D.  (TENS)
         M     R0,TIMEL10
         IC    R15,TIMEWRKW+3
         SRL   R15,4
         AR    R1,R15                    ..D  (UNITS)
         PACK  TIMEDOUB(8),MSGTEXT2+44(2)
         CVB   R15,TIMEDOUB              CONVERT YEAR TO BINARY
         LR    R0,R15
         N     R15,TIMELFFC              YEAR/4*4  (INTEGER DIVISION)
         CR    R0,R15                    LEAP YEAR?
         BE    NOADJUST                  YES, NO ADJUSTMENT NEEDED
         CH    R1,CUMDAYS+4              BEFORE FEB 30?
         BL    NOADJUST                  YES, NO ADJUSTMENT NEEDED
         AH    R1,=H'1'                  ADD 1 TO THE JULIAN DATE
NOADJUST SR    R15,R15
         AH    R15,=H'2'
         CH    R1,CUMDAYS(R15)
         BH    *-8
         SH    R1,CUMDAYS-2(R15)
         LR    R14,R15
         SRL   R15,1                     MONTH IN BINARY
         LA    R14,0(R14,R15)
         LA    R14,MONTHS-3(R14)
         MVC   MSGTEXT2+34(3),0(R14)     MONTH TEXT
         AR    R15,R15
         LA    R15,CONVTBL(R15)
         MVC   TIMEDOUB+4(2),0(R15)      MOVE IN MONTH
         LR    R15,R1                    DAY OF MONTH IN BINARY
         AR    R15,R15
         LA    R15,CONVTBL(R15)
         MVC   MSGTEXT2+38(2),0(R15)     MOVE IN DAY OF MONTH
         MVC   TIMEDOUB(4),MSGTEXT2+42   MOVE IN '19XX'
         MVC   TIMEDOUB+6(2),MSGTEXT2+38 MOVE IN DAY OF MONTH
         BAL   R14,WEEKDAY               FIND THE DAY OF WEEK
         L     R15,TIMEWRKW
         SLL   R15,4
         SRL   R15,28
         MH    R15,=H'10'
         LA    R15,DAY@WEEK(R15)
         MVC   MSGTEXT2+22(10),0(R15)
         MVI   MSGTEXT2+1,79
         MESSAGE MSGTEXT2
         B     NEWCMD
         SPACE 4
DAY@WEEK DC    CL10'SUNDAY',CL10'MONDAY',CL10'TUESDAY',CL10'WEDNESDAY'
         DC    CL10'THURSDAY',CL10'FRIDAY',CL10'SATURDAY'
         SPACE 2
CONVTBL  DC    CL32' 0 1 2 3 4 5 6 7 8 9101112131415'
         DC    CL32'16171819202122232425262728293031'
         SPACE 2
TIMETBL  DC    XL14'4021204B20204B202040C1D44040'
         SPACE 2
DATEPC   DC    CL26'ABC EF, 19GH    (12.456)'
         SPACE 2
CUMDAYS  DC    H'0,31,60,91,121,152,182,213,244,274,305,335,32000'
         SPACE 2
MONTHS   DC    CL36'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*     THIS REENTRANT SUBROUTINE DETERMINES THE DAY OF THE             *
*   WEEK FOR ANY GREGORIAN DATE FROM OCTOBER 15,1582, THROUGH         *
*   FEBRUARY 28, 4000.                                                *
*                                                                     *
*         NOTE:  DATE IS THE ARGUMENT AND DAY IS THE ANSWER.          *
*           THEY HAVE THE FOLLOWING FORMATS, RESPECTIVELY -           *
*                                                                     *
*TIMEDOUB DS   0ZL8     ARGUMENT                                      *
*TIMEINYR DS   ZL4      YEAR - ANY VALUE 1582-4000                    *
*TIMEINMT DS   ZL2      MONTH - ANY VALUE 01-12                       *
*TIMEINDY DS   ZL2      DAY OF MONTH - ANY VALUE 01-31                *
*TIMEWRKW DS   ZL1      DAY OF WEEK -  0 (SUNDAY) - 6 (SATURDAY)      *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
WEEKDAY  ST    R14,R14SAVE
         XC    TIMEDDAT(8),TIMEDDAT
         PACK  TIMEDECC,TIMEINDY
         CVB   R1,TIMEDDAT
         PACK  TIMEDECM,TIMEINMT
         CVB   R15,TIMEDDAT
         IC    R15,FFTBL-1(R15)      FIND FUDGE FACTOR
         AR    R1,R15                   AND APPLY IT TO CORRECTION
         PACK  TIMEDECC,TIMEINYR
         SP    TIMEDECC,TIMELP3      ADJUST YEAR IF JANUARY OR FEBRUARY
         OI    TIMEYRSG,15           LEGITIMIZE SIGN FOR ZAP
         ZAP   TIMEDECC,TIMEDECY
         CVB   R15,TIMEDDAT
         SR    R14,R14
         LA    R0,3                  USED TO FIND CENTURY MOD 4
         D     R14,=F'100'           FIND CENTURY
         NR    R15,R0                CENTURIES MOD 4
         NR    R0,R14                THIS MANY COMMON YEARS
         SRA   R14,2                   AND
         AR    R15,R14                   THIS MANY LEAP YEARS
         MH    R15,=H'5'
         AR    R1,R15
         AR    R1,R0
         SR    R1-1,R1-1             PREPARE TO DIVIDE
         D     R1-1,TIMEL7
         STC   R1-1,TIMEWRKW
         OI    TIMEWRKW,X'F0'        ADD A ZONE FIELD
         L     R14,R14SAVE
         BR    R14                   AND RETURN
         SPACE 1
FFTBL    DC    FL1'0,3,2,5,0,3,5,1,4,6,2,4'  FUDGE FACTORS
TIMEL10  DC    F'10'
TIMEL7   DC    F'7'
TIMEL12A DC    X'12000000'
TIMELFFC DC    X'FFFFFFFC'
TIMELP3  DC    P'3'
         TITLE 'P D S  --  PDS TSO                             6/10/85'
***********************************************************************
***      TSO   SUBCOMMAND      ADDED BY BRUCE LELAND -- AUG., 1982  ***
***********************************************************************
*
         SPACE 1
TSO      CSECT
         USING *,R8
         XC    MSGTEXT2(32),MSGTEXT2
TSO000   L     R1,ADDRCBUF                 POINT TO THE COMMAND BUFFER
         ST    R1,ADDRTEXT
         MVC   MSGTEXT2(4),0(R1)           COMMAND BUFFER LENGTHS
         LA    R1,PARMLIST                 USE THE SAME PARAMETER LIST
         L     R15,ADDRSCAN                SCAN START ADDRESS
         BALR  R14,R15                     INVOKE COMMAND SCAN
         SPACE 1
         LA    R5,SCANANSR                       ADDR OF ANSWER AREA
         TM    CSOAFLG-CSOA(R5),CSOANOC          EMPTY BUFFER?
         BO    TSO090                            YES, NO TSO COMMAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP+CSOAVNP  VALID COMMAND?
         LA    R1,L874                           ASSUME NOT
         BZ    MSGNEW                            NO, BRANCH
         L     R2,CSOACNM-CSOA(,R5)              ADDR OF COMMAND NAME
         LH    R3,CSOALNM-CSOA(,R5)              LENGTH OF NAME
         BCTR  R3,0                              MACHINE LENGTH
         SPACE 1
         MVC   DIRNAME,BLANKS              INITIALIZE WITH BLANKS
         MVC   DIRNAME(*-*),0(R2)          <<EXECUTED>>
         EX    R3,*-6                      MOVE IN THE COMMAND NAME
         SPACE 1
         L     R1,ADDRCBUF                 COMMAND BUFFER ADDRESS
         ST    R1,ADDRTEXT                 COMMAND ADDRESS
         SPACE 1
TSO005   CLC   DIRNAME(5),TSOLTIME         TIME REQUEST?
         BNE   *+10                        NO, BRANCH
         MVC   DIRNAME(8),TSOLIKJ          YES, USE IKJEFT25 INSTEAD
         SPACE 1
         CLC   DIRNAME(2),TSOLH            HELP REQUEST?
         BE    TSO010                      YES, BRANCH
         CLC   DIRNAME(5),TSOLHEL          HELP REQUEST?
         BNE   TSO020                      NO, BRANCH
TSO010   MVC   DDNAMEH,DDNAME              SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP              DDNAME TO FREE
         BAL   R2,DEALLOCZ                 FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH              RESTORE ACTUAL DDNAME
         SPACE 1
TSO020   ICM   R15,B'1111',=V(VTSOCMD)     VERIFY THE TSO COMMAND?
         BZ    TSO024                      NO, BRANCH
         OI    FLAGSJJ,FTSOCMD             YES, SET A ESTAE FLAG
         MVC   MSGTEXT1(16),ADDRTEXT       MOVE ARGUMENT LIST
         LA    R2,DIRNAME                  COMMAND NAME
         ST    R2,MSGTEXT1+16              FOR VERIFICATION ROUTINE
         OI    MSGTEXT1+16,X'80'           MARK END OF LIST
         LA    R1,MSGTEXT1                 START OF ARGUMENT LIST
         BALR  R14,R15                     CALL VERIFICATION ROUTINE
         NI    FLAGSJJ,FF-FTSOCMD          RESET THE ESTAE FLAG
         LTR   R15,R15                     AUTHORIZED?
         BZ    TSO024                      YES, BRANCH
         MVC   INSERT#1(8),DIRNAME         COMMAND NAME
         TSMSG L910$1                      NOT AUTHORIZED
         B     TSO090                      BYPASS THIS COMMAND
         SPACE 1
TSO024   L     R4,ADDRECT                  ECT ADDRESS
         OI    ECTSWS-ECT(R4),ECTNOPD      ASSUME NO OPERAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP    ANY OPERAND?
         BNO   *+8                         NO, BRANCH
         NI    ECTSWS-ECT(R4),FF-ECTNOPD   YES, ZAP THE NO OPERAND FLAG
         TM    CSOAFLG-CSOA(R5),CSOAEXEC   IMPLIED CLIST?
         BO    TSO030                      YES, BRANCH
         SPACE 2
*** MODULE NOT AN IMPLIED CLIST, TRY FOR A LINKLIST LIBRARY
         BLDL  0,BLDLLIST                  CHECK FOR A LOAD MODULE
         LTR   R15,R15                     IN THE LINKLIST?
         BZ    TSO040                      YES, ATTACH IT
         L     R15,CVTPTR                  NEED TO ALSO CHECK LPALIB
         L     R15,CVTLPDIA-CVTMAP(R15)    POINT TO LINK PACK DIRECTORY
         SPACE 2
*** MODULE NOT IN LINKLIST, TRY THE LPALIB DIRECTORY (IN STORAGE)
TSO026   CLC   8(8,R15),DIRNAME            THIS MODULE NAME?
         BE    TSO040                      YES, FOUND
         CLI   8(R15),X'FF'                END OF DIRECTORY?
         LA    R15,40(,R15)
         BNE   TSO026                      NO, BRANCH
         SPACE 2
*** MODULE NOT FOUND ANYWHERE, TRY IT AS AN IMPLIED CLIST
TSO030   MVC   DIRNAME(8),$EXE+20          INVOKE CLIST PROCESSOR
         L     R15,ADDRCBUF                COMMAND LINE
         MVC   0(4,R15),MSGTEXT2           RESET THE OPERAND OFFSET
*        NI    ECTSWS-ECT(R4),FF-ECTNOPD   ZAP THE NO OPERAND FLAG
         SR    R1,R1
         ICM   R1,B'0011',2(R15)           OFFSET TO OPERAND
         BZ    TSO040                      NULL RETURN
         STCM  R1,B'1100',2(R15)           CLEAR THE OFFSET
TSO034   LA    R15,1(,R15)                 NEXT BYTE
         MVI   3(R15),X'40'                CLEAR THE BYTE
         BCT   R1,TSO034                   REPEAT FOR ALL CHARACTERS
         SPACE 1
*** MODULE WAS FOUND, WE CAN ATTACH IT DIRECTLY
TSO040   MVC   ECTPCMD-ECT(8,R4),DIRNAME   COMMAND NAME
         MVC   ECTSCMD-ECT(8,R4),BLANKS    NO SUBCOMMAND
         SPACE 1
         CLI   ##SUBCAL,X'40'              TSOEXEC-TYPE PROCESSOR?
         BE    TSO050                      NO, BRANCH
         CLC   ECTPCMD-ECT(8,R4),$EXE+20   CLIST PROCESSOR?
         BE    TSO050                      YES, BRANCH
         LA    R3,##SUBCAL                 PDS EXTERNAL SUBCOMMAND
         NI    ECTSWS-ECT(R4),FF-ECTNOPD   ZAP THE NO OPERAND FLAG
         MVC   ECTPCMD-ECT(8,R4),##SUBCAL  COMMAND NAME IS TSOEXEC
         L     R15,ADDRCBUF                COMMAND BUFFER ADDRESS
         MVC   0(4,R15),MSGTEXT2           RESTORE THE COMMAND OFFSETS
         MVC   DIRNAME(8),##SUBCAL         INVOKE TSOEXEC
         SPACE 1
TSO050   L     R1,ADDRTEXT                 CPPL START
*        MESSAGE (R1)                      DELETE * FOR OUTPUT
         LA    R1,ADDRTEXT                 CPPL START
         MVI   ATTNECB,0                   CLEAR ATTENTION ECB
         LA    R3,DIRNAME                  PDS EXTERNAL SUBCOMMAND
         BAL   R2,ATTACH                   GO ATTACH IT
         SPACE 1
TSO090   ICM   R1,B'1111',MSGTEXT2+4+4     ANY GETLINE BUFFER?
         BZ    TSO092                      NO, BRANCH
         LH    R0,0(,R1)                   LENGTH
         ICM   R0,B'1000',=X'01'           SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
TSO092   L     R4,ADDRECT
         L     R1,ECTIOWA-ECT(R4)          --> I/O WORK AREA
         L     R1,0(,R1)                   --> ELEMENT ON STACK
         TM    0(R1),X'40'                 FROM CLIST?
         BZ    NEWSTAX                     NO, BRANCH
         SPACE 1
         LA    R1,MSGTEXT2+12              I/O PARM LIST OFR GETLINE
         L     R2,ADDRUPT                  UPT FOR GETLINE
         L     R3,ADDRECT                  ECT FOR GETLINE
         L     R4,MSGTEXT2+12+16           ECB FOR GETLINE
         L     R15,ADDRGETL                ADDRESS OF GETLINE
         MVC   MSGTEXT2+4(TSO099),TSO098   INITIALIZE GETLINE PARMS
         GETLINE PARM=MSGTEXT2+4,UPT=(R2),ECT=(R3),ECB=(R4),          XX
               ENTRY=(15),MF=(E,(1))
         CH    R15,=H'16'                  END OF INPUT?
         BE    NEWSTAX                     YES, BRANCH
         L     R1,MSGTEXT2+4+4             NEXT COMMAND
         ST    R1,ADDRCBUF
         SPACE 2
         LA    R15,PARMLIST
         USING CSPL,R15
         SPACE 1
         L     R1,ADDRUPT              ADDRESS OF THE UPT
         L     R2,ADDRECT              ADDRESS OF THE ECT
         LA    R3,ATTNECB              ADDRESS OF THE ATTENTION ECB
         LA    R4,ZERO                 ADDRESS OF A ZERO FLAG AREA
         LA    R5,SCANANSR             ADDRESS OF THE RESULT AREA
         L     R6,ADDRCBUF             ADDRESS OF THE SUBCOMMAND
         STM   R1,R6,CSPLUPT           SETUP CSPLUPT, CSPLECT, CSPLECB,
         DROP  R15                           CSPLFLG, CSPLOA & CSPLCBUF
         B     TSO000
         SPACE 1
TSO098   GETLINE MF=L
TSO099   EQU   *-TSO098
* MSGTEXT2+00,4   COMMAND BUFFER START HALFWORDS
* MSGTEXT2+04,8   GETLINE I/O AREA
* MSGTEXT2+12,16  GETLINE I/O PARM LIST
* MSGTEXT2+28,4   GETLINE ECB
TSOLTIME DC    C'TIME '
TSOLIKJ  DC    C'IKJEFT25'
TSOLH    DC    C'H '
TSOLHEL  DC    C'HELP '
         TITLE 'P D S  --  PDS TSOEDIT AND FSE                 6/10/85'
***********************************************************************
***      TSOEDIT AND FSE SUBCOMMANDS                                ***
***********************************************************************
*
         SPACE 1
TSOEDIT  CSECT
         USING *,R8
         MVC   DSNMEMQ(8),DIRNAME       MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST              MEMBER IN USE?
         NOP   0                        IF YES, OUTPUT A WARNING ONLY
         CLI   #EDITOLD,X'01'           "NEW" SPECIFIED?
         BNE   TSOED5                   NO, BRANCH
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    TSOED5                   YES, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     TSOED5                     04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
TSOED5   MVC   MSGTEXT2+4(8),##SUBCOM   MOVE IN "EDIT" OR "FSE"
         MVI   MSGTEXT2+12,C''''        ADD A QUOTE
         MVC   MSGTEXT2+13(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R3,MSGTEXT2+12(R15)      POINTER TO CURRENT BYTE
         MVI   1(R3),C'('               MEMBER NAME PAREN
         SR    R5,R5                    LENGTH OF LAST QUALIFIER
         LR    R14,R3                   BACKWARD SCAN POINTER
         SPACE 1
TSOED6   BCTR  R14,0                    SCAN
         LA    R5,1(,R5)                    TO
         CLI   0(R14),C'.'                    PREVIOUS
         BNE   TSOED6                                 PERIOD
         MVC   MSGTEXT1+4(9),1(R14)
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    TSOED8                   YES, BRANCH
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R1,8                     MAXIMUM MACHINE LENGTH+1
         SPACE 1
TSOED7   BCTR  R3,0                     SCAN
         CLI   0(R3),X'40'                  BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R1,TSOED7                                      NON-BLANK
         SPACE 1
         BCTR  R1,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R1,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
TSOED8   MVI   1(R3),C''''              ADD A QUOTE
         MVI   2(R3),X'40'              ADD A BLANK
         LA    R3,3(,R3)
         CLI   #EDITNON,1               NONUM CODED?
         BNE   TSOED9                   NO, BRANCH
         SPACE 2
         MVC   0(6,R3),TSOELNON         YES
         LA    R3,6(,R3)
TSOED9   CLI   #EDITOLD,1               "NEW" SPECIFIED?
         BNE   TSOED10                  NO, BRANCH
         MVC   0(4,R3),TSOELNEW         YES
         LA    R3,4(,R3)
TSOED10  CLI   #EDITASI,1               ASIS CODED?
         BNE   TSOED11                  NO, BRANCH
         MVC   0(5,R3),TSOELASI         YES
         LA    R3,5(,R3)
TSOED11  ICM   R15,B'1111',#EDITTYP     ANY DATA TYPE CODED?
         BZ    TSOED13                  NO, BRANCH
         MH    R15,=H'7'                INDEX  =  OPTIONUM * 7
         LA    R15,EDITTYPE-7(R15)      POINT TO THE PROPER OPTION
         B     TSOED20                  DONE WITH FORMATTING
         SPACE 1
TSOED13  BCTR  R5,0                     MACHINE QUALIFIER LENGTH
         LA    R15,EDITTYPE-7
TSOED15  LA    R15,7(,R15)              TABLE WIDTH IS 7
         CLC   PLIF,0(R15)              END OF DESCRIPTIVE QUALIFIERS?
         BE    TSOED17                  YES, USE "DATA" OR "GOFORT"
         CLC   0(*-*,R15),MSGTEXT1+4    <<EXECUTED>>
         EX    R5,*-6                   THIS QUALIFIER?
         BNE   TSOED15                  NO, TRY THE NEXT ONE
         B     TSOED20                  YES, USE THIS QUALIFIER
         SPACE 1
TSOED17  LA    R15,EDITTYPE             NOT FOUND, ASSUME "DATA"
         CLC   TSOELFOR,MSGTEXT1+4      FORT QUALIFIER?
         BNE   TSOED20
         LA    R15,GOFORT               YES, USE "GOFORT" (EDIT RULES)
         SPACE 1
TSOED20  MVC   0(7,R3),0(R15)           MOVE IN DATA TYPE
         LA    R1,MSGTEXT2
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LA    R3,7(,R3)
         SR    R3,R1                    COMMAND LENGTH
         SLL   R3,16
         ST    R3,MSGTEXT2              SAVE LENGTH OF STRING
*        MESSAGE MSGTEXT2               DELETE * TO OUTPUT THE COMMAND
         MVI   MSGTEXT2+3,8             OPERAND OFFSET
         BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE THE EDIT
         MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
         LA    R0,RESTART2              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         SPACE 1
         L     R4,ADDRECT
         MVC   ECTPCMD-ECT(8,R4),##SUBCAL      COMMAND IS EDIT OR FSE
         NI    ECTSWS-ECT(R4),FF-ECTNOPD       WE ALWAYS HAVE OPERANDS
         LA    R1,ADDRTEXT                     THE CPPL FOR EDIT/FSE
         LA    R3,##SUBCAL                     TSO EDIT/FSE COMMAND
         BAL   R2,ATTACH2                      GO ATTACH IT
         MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
         SPACE 1
         MVI   ##ADRCM#,EDITOR          NO REPEATED WARNING MESSAGE
         L     R1,X'21C'                --> CURRENT TCB
         L     R1,X'00C'(,R1)           --> TIOT
         LA    R1,24(,R1)               TIOENTRY
TSOED97  CLI   0(R1),0                  END OF TIOT?
         BE    RESTART0                 YES, REALLOCATE AND OPEN AGAIN
         CLC   4(8,R1),DDNAME           THIS DDNAME?
         BE    RESTART2                 YES, JUST OPEN AGAIN
         SR    R15,R15
         IC    R15,0(,R1)
         LA    R1,0(R15,R1)
         B     TSOED97
TSOELNON DC    C'NONUM '
TSOELNEW DC    C'NEW '
TSOELASI DC    C'ASIS '
TSOELFOR DC    C'FORT('
         SPACE 2
**  THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH "PCLTYPE"
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
EDITTYPE DC    CL7'DATA'
** INSERT INSTALLATION-ADDED EDIT DATA TYPES HERE AND IN "PCLTYPE"
         DC    CL7'LIST'      ADDED LOCALLY AS A VALID DATA SET TYPE
         DC    CL7'VSBASIC'   NEW VS BASIC
         DC    CL7'BASIC'
         DC    CL7'IPLI'
         DC    CL7'TEXT'
         DC    CL7'COBOL'
         DC    CL7'ASM'
         DC    CL7'CLIST'
         DC    CL7'CNTL'
         DC    CL7'PLI'
* END OF VALID DATA SET TYPES
PLIF     DC    CL7'PLIF'
         DC    CL7'FORTE'    *
         DC    CL7'FORTG'    *
         DC    CL7'FORTGI'   *  -- DATA TYPE .FORT
         DC    CL7'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
GOFORT   DC    CL7'GOFORT'   *
         TITLE 'P D S  --  PDS USAGE                           6/10/85'
***********************************************************************
***      USAGE SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
USAGE    CSECT
         USING *,R8
         L     R15,$DSN+8
         BALR  R2,R15                   FORMAT THE ALLOCATION STATUS
         SPACE 1
         MVC   INSERT#1(66),MSG180H
         MVI   MTHIGHL,56
         CLI   DS1SYSCD+8,X'FF'        ASM2 IDENTIFIER?
         BNE   *+8                     NO, BRANCH
         MVI   MTHIGHL,66              YES, USE 66 CHARACTERS
         TSMSG L180$1
         MVC   INSERT#1(66),BLANK128
         SR    R15,R15
         MVC   INSERT#1+10(7),USAL000
         ICM   R15,B'0111',DS1CREDT    DATA SET CREATION DATE NULL?
         BZ    US070                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+08(9),USAL4020
         ED    INSERT#1+08(9),MSGTEXT1
         SPACE 1
US070    SR    R15,R15
         MVC   INSERT#1+19(7),USAL000
         ICM   R15,B'0111',DS1EXPDT    DATA SET EXPIRATION DATE NULL?
         BZ    US080                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+17(9),USAL4020
         ED    INSERT#1+17(9),MSGTEXT1
         SPACE 1
US080    SR    R15,R15
         MVC   INSERT#1+29(7),USAL000
         ICM   R15,B'0111',DS1REFD     LAST OPEN DATE NULL?
         BZ    US090                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+27(9),USAL4020
         ED    INSERT#1+27(9),MSGTEXT1
         SPACE 1
US090    MVC   INSERT#1+38(3),USALYES  ASSUME UPDATED
         TM    DS1DSIND,DS1DSCHA       CORRECT?
         BO    *+10                    YES, BRANCH
         MVC   INSERT#1+38(3),USALNO   NO, RESET THE DATA
         SPACE 1
         MVC   INSERT#1+47(4),=C'NONE' ASSUME NO SECURITY
         TM    DS1DSIND,X'40'+X'10'+X'04'  CORRECT?
         BZ    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALRACF ASSUME RACF DEFINED
         TM    DS1DSIND,X'40'          CORRECT?
         BO    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALREAD ASSUME READ PROTECTED
         TM    DS1DSIND,X'10'+X'04'    CORRECT?
         BM    US100                   YES, BRANCH
         MVC   INSERT#1+47(5),USALWRIT   MUST BE WRITE PROTECTED
         SPACE 1
US100    MVC   INSERT#1+57(8),DS1SYSCD USERID IF ANY
         SPACE 1
         TSMSG L180$1
         MESSAGE MSGBLANK
         SPACE 2
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         SPACE 1
USEXTNT  AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,USEXTNT              IF MORE EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 2
         MVC   INSERT#1(120),BLANK128
         MVI   MTHIGHL,120
         L     R4,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT               NUMBER OF EXTENTS
         LA    R2,INSERT#1-2           POSITION FOR FIRST DATA
         SPACE 1
US120    LH    R1,32+14(,R4)           TRACKS IN THIS EXTENT
         CVD   R1,DOUBLE
         MVC   MSGTEXT1(6),=X'402020202120'
         ED    MSGTEXT1(6),DOUBLE+5
         LA    R1,MSGTEXT1-1
         LA    R5,6
         SPACE 1
US130    LA    R1,1(,R1)               SCAN
         BCTR  R5,0                        FOR
         CLI   0(R1),X'40'                    FIRST
         BE    US130                               NON-BLANK
         SPACE 1
         MVI   0(R2),C','
         MVC   2(*-*,R2),0(R1)        <<EXECUTED>>
         EX    R5,*-6                 MOVE IN THE LENGTH TEXT
         LA    R2,2+1(R2,R5)          POSITION FOR NEXT COMMA
         LA    R4,16(,R4)             NEXT EXTENT START
         BCT   R3,US120               DO ALL EXTENTS
         SPACE 1
         TSMSG L181$1
         MESSAGE MSGBLANK
         SPACE 2
US200    MVI   MTHIGHL,38
         MVC   INSERT#1(38),MSG182H
         TSMSG L182$1
         MVC   INSERT#1(38),MSG182E
         SPACE 2
         LH    R1,DSNTOTAL            TOTAL DATA SET TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+11(6),DOUBLE+5
         SPACE 1
         LH    R1,DSNTOTAL            TOTAL DATA SET TRACKS
         SH    R1,DSNEMPTY            TOTAL DATA SET USED TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(6),DOUBLE+5
         SPACE 1
         LH    R1,DSNEMPTY            TOTAL DATA SET FREE TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+23(6),DOUBLE+5
         SPACE 1
         LH    R1,NUMEXT              TOTAL DATA SET EXTENTS
         CVD   R1,DOUBLE
         ED    INSERT#1+34(4),DOUBLE+6
         SPACE 1
         TSMSG L182$1
         MESSAGE MSGBLANK
         SPACE 2
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   US300                  NO, BRANCH
         MVI   STARTTR+2,X'01'        TTR=000001 (START OF DIRECTORY)
         SPACE 1
         BAL   R14,READDIR            SCAN THROUGH THE DIRECTORY
         SPACE 1
         MVI   MTHIGHL,60
         MVC   INSERT#1(60),MSG183H
         TSMSG L183$1
         MVC   INSERT#1(60),MSG183E
         SPACE 1
         LH    R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+11(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTUSED             TOTAL USED DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         SH    R1,TOTUSED             TOTAL FREE DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+23(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTTRKS             TOTAL TRACKS FOR DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+31(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTMEMR             TOTAL REAL ENTRIES
         AH    R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+40(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+49(6),DOUBLE+5
         SPACE 1
         TSMSG L183$1
         MESSAGE MSGBLANK
         SPACE 2
US300    TM    FLAGSAA,FOPTIONS       ANY OPTION?
         BNO   NEWCMD                 NO, BRANCH
         MVI   MTHIGHL,70
         MVC   INSERT#1(70),MSG184H
         TSMSG L184$1
         MVC   INSERT#1(70),MSG184U
         TSMSG L184$1
         MVC   INSERT#1(70),BLANK128
         L     R2,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT
         SR    R4,R4                  EXTENT NUMBER
         SR    R5,R5                  BEGINNING TT OF TTR
         SPACE 1
US310    CVD   R4,DOUBLE              EXTENT
         A     R4,=F'1'
         MVC   INSERT#1+2(4),=X'40202120'
         ED    INSERT#1+2(4),DOUBLE+6
         L     R1,32(,R2)             UCB ADDRESS
         MVC   INSERT#1+8(3),13(R1)
         LH    R6,32+14(,R2)          TRACKS IN THIS EXTENT
         LR    R0,R5
         AR    R5,R6                  START OF NEXT EXTENT
         LA    R15,2                  LOOPS
         LA    R14,INSERT#1+16
         SPACE 1
US320    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US320
         SPACE 1
         CLI   INSERT#1+24,C'.'       SECOND TTR VALUE?
         BE    US330                  YES, BRANCH
         LA    R14,INSERT#1+22
         LA    R15,2
         LTR   R0,R5                  ANY DATA?
         BZ    US320                  NO, BRANCH
         BCTR  R0,0                   YES, REDUCE BY ONE
         B     US320
         SPACE 1
US330    MVI   INSERT#1+18,X'40'      CLEAR FIRST EXTRA PERIOD
         MVI   INSERT#1+24,X'40'      CLEAR SECOND EXTRA PERIOD
         CVD   R6,DOUBLE              TRACKS
         MVC   INSERT#1+26(6),=X'402020202120'
         ED    INSERT#1+26(6),DOUBLE+5
         SPACE 2
         ICM   R0,B'1111',38(R2)      FIRST DEVICE CC.CC.HH.HH
         LA    R15,4                  LOOPS
         LA    R14,INSERT#1+43
         SPACE 1
US420    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US420
         SPACE 1
         CLI   INSERT#1+57,C'.'       SECOND TTR VALUE?
         BE    US430                  YES, BRANCH
         LA    R14,INSERT#1+55
         ICM   R0,B'1111',42(R2)      SECOND CC.CC.HH.HH FIGURE
         LA    R15,4
         B     US420
         SPACE 1
US430    MVI   INSERT#1+45,X'40'
         MVI   INSERT#1+57,X'40'
         MVC   INSERT#1+59(3),=C'CYL' ASSUME CYLINDER BOUNDARY
         CLC   40(2,R2),ZERO          FIRST HH.HH=0?
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         LH    R1,44(,R2)
         LA    R1,1(,R1)
         CH    R1,DEVTTRKS            END HH.HH=MAX/DEVICE
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         SPACE 1
         TSMSG L184$1
         LA    R2,16(,R2)             NEXT EXTENT
         BCT   R3,US310               DO ALL EXTENTS
         SPACE 2
         MESSAGE MSGBLANK
         B     NEWCMD
         SPACE 3
MSG180H  DC    CL66'DATA SET: CREATED  EXPIRES  LAST USE  UPDATED  SECUX
               RITY  ASM2ID'
MSG182H  DC    CL38'TRACKS: ALLOCATED  USED  FREE  EXTENTS'
MSG182E  DC    CL38'                                      '
         ORG   MSG182E+11
         DC    X'402020202120'
         ORG   MSG182E+17
         DC    X'402020202120'
         ORG   MSG182E+23
         DC    X'402020202120'
         ORG   MSG182E+34
         DC    X'40202120'
         ORG   ,
MSG183H  DC    CL60'DIRECTORY: BLOCKS  USED  FREE  TRACKS  MEMBERS  ALIX
               ASES'
MSG183E  DC    CL60'                                            '
         ORG   MSG183E+11
         DC    X'402020202120'
         ORG   MSG183E+17
         DC    X'402020202120'
         ORG   MSG183E+23
         DC    X'402020202120'
         ORG   MSG183E+31
         DC    X'402020202120'
         ORG   MSG183E+40
         DC    X'402020202120'
         ORG   MSG183E+49
         DC    X'402020202120'
         ORG   ,
MSG184H  DC    CL70'EXTENT  UCB  LO TT-HI TT  TRACKS     LOW CCHH-HIGH X
               CCHH    BOUNDARY '
MSG184U  DC    CL70'------  ---  ----- -----  ------  ----------- -----X
               ------  -------- '
USAL000  DC    C'0/00/00'
USAL4020 DC    X'402021612020612020'
USALYES  DC    C'YES'
USALNO   DC    C'NO '
USALTRK  DC    C'TRK'
USALRACF DC    C'RACF'
USALREAD DC    C'READ'
USALWRIT DC    C'WRITE'
         PRINT GEN
         TITLE 'P D S  --  PDS VERIFY                    6/10/85'
***********************************************************************
***      VERIFY SUBCOMMAND     ADDED BY BRUCE LELAND --  MAY, 1983  ***
***********************************************************************
*
         SPACE 1
VERIFY   CSECT
         USING *,R8
         TM    FLAGSCC,RECFMF      FIXED FORMAT RECORDS?
         BNO   VERP00              NO, BRANCH
         LH    R15,BLKSI           BLKSIZE
         SR    R14,R14
         LH    R1,LRECL            LRECL
         LTR   R1,R1               ANY LRECL?
         BZ    VERP00              NO, AVOID DIVIDE ERROR
         DR    R14,R1              BLKSIZE/LRECL
         LTR   R14,R14             INTEGRAL NUMBER OF LRECL RECORDS?
         BZ    VERP00              YES, BRANCH
         TSMSG L482                NO, ERROR
         SPACE 2
VERP00   TM    DSORG,DS1DSGPO      PARTITIONED DATA SET?
         BNO   VERS00              NO, PERFORM OTHER GROUP OF CHECKS
         SPACE 1
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?
         BNZ   VERP02                     NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP?
         BNO   VERP03                     NO, BRANCH
         SPACE 2
* SINGLE MEMBER CHECK IS DESIRED
VERP02   OI    #VERUPDT,4+2               SET FLAG FOR LATER
         SR    R1,R1                      NO MESSAGE YET
         BAL   R2,VERMBR                  OUTPUT MEMBER IDENTIFIER
         LA    R4,#VERR4                  POINT TO A WORK AREA
         ST    R4,#MEMPTR                 ADD AT END OF THE TABLE
         SR    R3,R3                      NO TABLE ENTRIES
         B     VERP18                     SKIP INITIALIZATION
         SPACE 2
VERP03   LA    R1,L875             ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO    CORRECT?
         BE    MSGNEW              YES, BRANCH
         TM    #VERUPDT,2          NOUPDATE SPECIFIED?
         BO    VERP09              YES, DO NOT OPEN STOW
         BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     VERP08              COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   MSGTEXT2,X'F9'          HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),=C'FIXPDS' TEMPORARY STOW AND DELETE NAME
         MVI   MSGTEXT2+11,B'1001111'  ALIAS/MAX LENGTH ENTRY
         STOW  STOWDCB,MSGTEXT2,A      ADD THIS MEMBER
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     VERP06                    00 - SUCCESSFUL
         B     VERP06                    04 - MEMBER ALREADY EXISTS
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     VERP04                    12 - FULL DIRECTORY
         TSMSG L836                      16 - I/O ERROR IN DIRECTORY
         B     VERP08
         SPACE 1
VERP04   TSMSG L855                    DIRECTORY IS FULL
         B     VERP08                    12 - FULL DIRECTORY
         SPACE 2
VERP06   STOW  STOWDCB,MSGTEXT2,D      DELETE THIS MEMBER
         SPACE 1
         LTR   R15,R15                 ANY ERROR?
         BNP   VERP08                  NO, BRANCH
         TSMSG L836                    YES, I/O ERROR IN DIRECTORY
VERP08   BAL   R2,SHUTSTOW         CLOSE THE STOW DCB; DEQUEUE
         SPACE 2
VERP09   MVI   SUBPOOLT,21         TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'     READ THE DIRECTORY
         LA    R5,1024/2           1024 MEMBERS INITIALLY
         SPACE 1
VERP10   SLL   R5,1                MEMBERS*2 FOR EACH LOOP
         SR    R6,R6
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)          ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1               NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR  ANY PREVIOUS MEMBERS?
         BZ    VERP12              NO, BRANCH
         SRL   R3,1                YES, USE SECOND HALF OF TABLE
         LR    R14,R4              NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3               MEMBERS*8 IS OLD TABLE SIZE
         LR    R6,R15              DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0              PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R6               LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
VERP12   ST    R4,#MEMPTR          MEMBER TABLE BASE
         AR    R4,R6               WHERE TO ADD MEMBERS
         SPACE 2
VERP14   BAL   R14,READDIR         GET THE NEXT MEMBER
         B     VERP50              END OF MEMBERS, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         SPACE 3
* CHECK FOR STANDARD MEMBER NAMES IF REQUIRED
         TM    #VERSTND,X'02'      STANDARD NAME CHECK?
         BNZ   VERP18              NO, BRANCH
         SPACE 1
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),MEMNAME
         SPACE 1
VERP16   BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERP16                                 NON-BLANK
         SPACE 1
         SR    R1,R1               NO MESSAGE YET
         LA    R2,VERP18           EXIT ADDRESS
         CLI   MEMNAME,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR              YES, NON-STANDARD
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         LA    R1,0
         LA    R2,VERP18
         BNZ   VERMBR              NO, BRANCH
         SPACE 3
* CHECK FOR CONFLICTING LOAD MODULE ATTRIBUTES
VERP18   L     R14,DIRPTRS                 CURRENT MEMBER
         MVC   DIRNAME(12),0(R14)          MOVE IN NAME, FIRST TTR
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BZ    VERP28                      NO, BRANCH
         MVC   DIRNAME(40),0(R14)          MOVE IN NAME, LKED FLAGS
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BNO   VERP20                      NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BZ    VERP20                      NO, BRANCH
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    CORRECT?
         BO    VERP19                      YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    VERP20                      YES, BRANCH
         LA    R1,=C'24 '                  AMODE MUST BE 24
VERP19   MVI   MTHIGHL,3                   MESSAGE LENGTH
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         LA    R1,L880$1                   RMODE ANY/AMODE CONFLICT
         BAL   R2,VERMBR                   OUTPUT AN ERROR MESSAGE
         SPACE 3
VERP20   TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   VERP22                          NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    VERP22                          YES, BRANCH
         LA    R1,L881                         RENT AND NOT REUS
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
VERP22   LA    R1,L882                         TEST AND NOT EDIT
         TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         LA    R1,L883                         REUSABLE AND SCATTER
         TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANKS
         MVC   INSERT#1(4),RENTTXT+3           OVERLAY AND REENTRANT
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REUSABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),REFRTXT+3           OVERLAY AND REFRESHABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),SCTRTXT+3           OVERLAY AND SCATTER
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR3,DIRRMANY               RMODE ANY?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVI   INSERT#1,C'A'                   AMODE ANY
         SR    R1,R1
         IC    R1,DIRATTR3                     AMODE BITS
         TM    DIRFLAG,X'80'                   ALIAS?
         BNO   *+8                             NO, BRANCH
         SRL   R1,2                            YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                     SAVE CURRENT AMODE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    VERP24                          YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    *+10                            YES, BRANCH
         MVC   INSERT#1+6(3),=C'31 '           NO, AMODE 31
         BAL   R2,VERMBR                       ERROR MESSAGE
         SPACE 3
* LOAD EACH MEMBER IF REQUIRED AND THIS IS A LOAD LIBRARY
VERP24   TM    #VERLOAD,X'02'      LOAD CHECK REQUESTED?
         BNZ   VERP28              NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BZ    VERP28              NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,VERP26           RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,#SAVEREG      SAVE REGISTERS
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         B     VERP28
         SPACE 1
VERP26   LM    R2,R8,#SAVEREG      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   MTHIGHL,4           FOUR CHARACTERS FROM ABEND MESSAGE
*        MVC   INSERT#1(4),INSERT#1
         LA    R1,L998$1           "ABEND SNNN LOADING THIS MODULE"
         BAL   R2,VERMBR           OUTPUT AN ABENDED MESSAGE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
VERP28   OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   DSNMEMQ(8),DIRNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         BAL   R2,VERMBR           YES, OUTPUT AN IN-USE MESSAGE
         SPACE 3
* CHECK THAT THE PDS DIRECTORY MEMBERS ARE IN CORRECT COLLATING ORDER
VERP30   NI    ##ADRPA#,FF-$Q      LEAVE QUIET MODE PROCESSING
         CLC   #VERMBR(8),DIRNAME  PREVIOUS:CURRENT
         BH    VERP32                HIGH  -- ERROR, OUT OF ORDER
         MVC   #VERMBR(8),DIRNAME
         BL    VERP34                LOW   -- VALID
         LA    R1,L824               EQUAL -- ERROR, DUPLICATE
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 1
VERP32   LA    R1,L825             MEMBER OUT OF SEQUENCE
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 3
* SELECT THE HIGHEST TTR POINTERS FOR THIS MEMBER
VERP34   MVC   0(8+4,R4),DIRNAME   MOVE IN THE NAME, FIRST TTR
         XC    12(4,R4),12(R4)     CLEAR THE SECOND TTR ADDRESS
         TM    MEMTTR+3,DIR1TTR+DIR2TTR  ANY OTHER TTR'S?
         BZ    VERP35                    NO, BRANCH
         SPACE 1
         MVC   12(3,R4),DIRNAME+12 ADD FIRST USER TTR
         TM    MEMTTR+3,DIR2TTR    SECOND TTR?
         BZ    VERP35              NO, BRANCH
         SPACE 1
         CLC   12(3,R4),DIRNAME+16 FIRST TTR > SECOND TTR?
         BH    *+10                YES, BRANCH
         MVC   12(3,R4),DIRNAME+16 NO, USE SECOND TTR
         SPACE 1
VERP35   NI    11(R4),X'80'        SAVE ONLY THE ALIAS BIT
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         LA    R1,L872
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         B     VERP38
         SPACE 1
VERP36   CLC   12(3,R4),DS1LSTAR   IN DATA SET BOUNDS?
         BNH   VERP37              YES, BRANCH
         LA    R1,L873
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         SPACE 1
VERP37   MVI   15(R4),X'FF'        ASSUME NOT AN OS/VS LINKAGE-EDITOR
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP38              NO, BRANCH
         TM    DIRATTR2,DIRAOSLE   OS/VS LINKAGE-EDITOR?
         BNO   VERP38              NO, BRANCH
         NI    DIRATTR3,DIRRMANY+DIRAM31+DIRAM24
         OC    11(1,R4),DIRATTR3   SAVE THE RMODE/AMODE CHARACTER
         MVC   15(1,R4),DIRATTR4   SAVE THE RLD/CONTROL COUNT
         SPACE 3
* PERFORM AN INSERTION SORT OF THIS ENTRY
VERP38   MVC   DIRNAME(16),0(R4)   SAVE ENTRY FOR INSERTION SORT
         LR    R14,R4              LAST SORT POSITION
         L     R1,#MEMPTR          FIRST SORT POSTION
         SH    R1,=H'16'           ADJUST FOR NEXT LA
         SPACE 1
VERP40   LA    R1,16(,R1)          NEXT TABLE ENTRY
         CLC   8(4,R1),DIRTTR      TABLE TTR:NEW TTR
         BL    VERP40                LOW   --  ITERATE
         BH    VERP41                HIGH  --  INSERT
         CLC   0(8,R1),DIRNAME       EQUAL --  TABLE NAME:NEW NAME
         BL    VERP40                            LOW   -- ITERATE
         BE    VERP42                            EQUAL -- IN PLACE
         SPACE 1
VERP41   SH    R14,=H'16'          PREVIOUS TABLE ENTRY
         MVC   16(16,R14),0(R14)   SLIDE DOWN TO MAKE ROOM
         CR    R14,R1              DONE WITH SHIFTING?
         BH    VERP41              NO, BRANCH
         MVC   0(16,R1),DIRNAME    INSERT NEW ENTRY TO ITS POSITION
         SPACE 2
VERP42   LA    R4,16(,R4)          NEXT TABLE ENTRY
         S     R3,=F'1'            ANY MORE ENTRIES FIT?
         BP    VERP14              YES, BRANCH
         BZ    VERP10              NO, REALLOCATE THE MEMBER TABLE
         SPACE 3
* SINGLE MEMBER -- CHECK DIRECTORY ATTRIBUTES
         ST    R4,#MEMLAST         UPDATE LAST MEMBER POINTER
         MVI   STARTTR+2,X'01'     TTR=00001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1
         SPACE 1
VERP44   BAL   R14,READDIR         GET NEXT MEMBER
         B     VERP48              LAST MEMBER, BRANCH
         SPACE 1
         CLC   DIRTTR,MEMTTR       TTR MATCH?
         BNE   VERP44              NO, BRANCH
         TM    DIRFLAG,X'80'       FIRST THE MAIN ENTRY?
         BZ    VERP45              YES, BRANCH
         TM    MEMFLAG,X'80'       SECOND AN ALIAS?
         BO    VERP44              YES, IGNORE
         SPACE 1
* FOUND A REAL CORRESPONDING ENTRY
         MVC   INSERT#1(8),MEMNAME REAL ENTRY FOR THE ALIAS
         TSMSG L066$1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, DONE
         LA    R14,DIRREAL
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R14,DIRREALS
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         CLC   0(8,R14),MEMNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, MESSAGE
         B     VERP47
         SPACE 3
* FOUND AN ALIAS CORRESPONDING ENTRY
VERP45   MVC   INSERT#1(8),MEMNAME
         TM    MEMFLAG,X'80'       AN ALIAS?
         BO    VERP46              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME  FIND MYSELF?
         BE    VERP44              YES, BRANCH
         TSMSG L864$1              FOUND AN APPARENT ALIAS
         B     VERP73
         SPACE 1
VERP46   TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, BRANCH
         L     R3,DIRPTRS
         LA    R14,DIRREAL-DIRNAME(R3)
         TM    DIRATTR-DIRNAME(R3),ATTRSCTR
         BNO   *+8
         LA    R14,DIRREALS-DIRNAME(R3)
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L827$2
         CLC   0(8,R14),DIRNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, MESSAGE
         SPACE 1
VERP47   L     R3,DIRPTRS
         LA    R1,L823$1           RLD/CONTROL MISMATCH
         CLC   DIRATTR4(1),DIRATTR4-DIRNAME(R3)
         BE    *+8
         BAL   R2,VERMBR
         SPACE 1
         MVI   DOUBLE+0,DIRRMANY
         MVI   DOUBLE+1,DIRRMANY
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L821$1           RMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, ERROR
         SPACE 1
         MVI   DOUBLE+0,DIRAM31+DIRAM24
         MVI   DOUBLE+1,DIRAM31+DIRAM24
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L822$1           AMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, ERROR
         B     VERP44
         SPACE 1
* END OF MEMBERS
VERP48   TM    DIRNAME,X'80'       ALIAS FIRST MEMBER?
         BNO   VERP73              NO, BRANCH
         CLI   INSERT#1,0          ANY MAIN ENTRY FOUND?
         BNE   VERP73              YES, BRANCH
         TSMSG L860                ORPHAN MEMBER
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP73              NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         TSMSG L861$1
         B     VERP73
         SPACE 3
* DIRECTORY TTR SORT COMPLETE -- CHECK FOR ORPHAN MEMBERS, APPARENT
* ALIAS MEMBERS AND ALIAS MEMBERS WITH INCORRECT MAIN MEMBER POINTERS
VERP50   LA    R1,L400
         ST    R4,#MEMLAST         SAVE THE END POINTER
         ICM   R3,B'1111',#MEMPTR  FIRST MEMBER
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         OI    #VERSTND,X'02'      NO MORE MEMBER NAME CHECKING
         LR    R5,R3
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BO    VERP54              YES, BRANCH
         B     VERP58+4
         SPACE 1
VERP52   LA    R3,16(,R5)          NEXT TABLE ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R3,R4               DONE?
         BE    VERP66              YES, BRANCH
         LR    R5,R3
         SPACE 1
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BNO   VERP58+4            NO, BRANCH
VERP54   LA    R1,L860             YES, THIS IS AN ORPHAN
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         NI    8+3(R3),X'FF'-X'80' TURN OFF THE ALIAS BIT (ORPHAN)
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP52              NO, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,0(R3)       MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         TSMSG L861$1
         B     VERP52
         SPACE 2
VERP58   LA    R5,16(,R5)          NEXT POTENTIAL ALIAS ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R5,R4               DONE?
         BE    VERP66              YES, BRANCH
         CLC   8(3,R3),16+8(R5)    TTR FOR AN ALIAS?
         BNE   VERP52              NO, BRANCH
         TM    16+8+3(R5),X'80'    ALIAS FLAG SET?
         BNO   VERP64              NO, APPARENT ALIAS
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP58              NO, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,16(R5)      MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         LA    R1,DIRREAL          NAME OF CORRESPONDING MAIN
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRREALS         YES, USE THIS NAME INSTEAD
         SPACE 1
         CLC   0(8,R1),0(R3)       CORRECT NAME RECORDED?
         BE    VERP60              YES, BRANCH
         MVI   MTHIGHL,8           MEMBER NAME 1
         MVI   MTHIGHL+4,8         MEMBER NAME 2
         MVC   INSERT#1(8),0(R3)
         MVC   INSERT#2(8),0(R1)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         MVC   MEMNAME(8),16(R5)
         BAL   R2,VERMBR
         SPACE 1
VERP60   CLC   15(1,R3),16+15(R5)  MATCHING RLD/CONTROL COUNTS?
         BE    VERP62              YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L823$1           NO, RLD/CONTROL MISMATCH
         BAL   R2,VERMBR
         SPACE 1
VERP62   MVI   DOUBLE,DIRRMANY       BIT TO TEST
         MVI   DOUBLE+1,DIRRMANY     BIT TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS RMODE BIT
         NC    DOUBLE+1(1),11(R3)    MAIN RMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    RMODE BITS EQUAL?
         BE    VERP63                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L821$1             NO, RMODE DOES NOT MATCH
         BAL   R2,VERMBR
         SPACE 1
VERP63   MVI   DOUBLE,DIRAM31+DIRAM24    BITS TO TEST
         MVI   DOUBLE+1,DIRAM31+DIRAM24  BITS TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS AMODE BITS
         NC    DOUBLE+1(1),11(R3)    MAIN AMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    AMODE BITS EQUAL?
         BE    VERP58                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L822$1             NO, AMODE DOES NOT MATCH
         BAL   R2,VERMBR
         B     VERP58
         SPACE 1
VERP64   MVC   INSERT#1(8),16(R5)  MEMBER NAME INSERTED
         MVC   MEMNAME(8),0(R3)
         LA    R1,L864$1
         BAL   R2,VERMBR
         OI    16+8+3(R5),X'80'    TURN ON ALIAS BIT (APPARENT ALIAS)
         B     VERP58
         SPACE 3
* END OF DIRECTORY CHECK, NOW PERFORM ANY INPUT CHECK
VERP66   MVI   STARTTR+2,X'01'     TTR=000001 (START OF DIRECTORY)
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    VERP73              YES, BRANCH
         SPACE 1
VERP68   L     R15,=A(EXCP)        READ THE DIRECTORY RECORDS
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP70                00 - SUCCESSFUL READ
         B     VERP72                04 - END OF MEMBER
         B     VERP72                08 - END OF DATA SET
         B     IOERROR               12 - I/O ERROR
         SPACE 2
VERP70   L     R0,LS               LENGTH OF RECORD (DD OF RKDD)
         ICM   R0,B'0100',=X'08'   KEY LENGTH OF RECORD (K OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         B     VERP68              READ ALL DIRECTORY BLOCKS
         SPACE 1
VERP72   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         CLI   CURMBB,0            MULTI-EXTENT DIRECTORY?
         BE    VERP73              NO, BRANCH
         MESSAGE MSGBLANK          YES, ERROR MESSAGE
         MESSAGE MSGBLANK
         TSMSG L870
         SPACE 1
VERP73   ICM   R6,B'1111',#MEMPTR  FIRST MEMBER
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BNO   *+8                 NO, BRANCH
         NI    8+3(R6),FF-X'80'    YES, READ THIS MEMBER
         CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERP99              YES, BRANCH
         B     VERP74+4
         SPACE 2
VERP74   LA    R6,16(,R6)          NEXT TABLE ENTRY
         MVI   RLDCOUNT,X'FF'      ASSUME NO RLD/CONTROL IS FOUND
         C     R6,#MEMLAST         DONE?
         BE    VERP97              YES, BRANCH
         TM    8+3(R6),X'80'       ALIAS ENTRY?
         BO    VERP74              YES, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         MVC   MEMNAME(8),0(R6)    INITIALIZE THE MEMBER NAME
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         LA    R5,3                ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 1
         CLC   8(3,R6),DS1LSTAR    BEYOND END OF DATA MARKER?
         BNL   VERP90              YES, DONE
         MVC   STARTTR(3),8(R6)    NO, FIRST DISK ADDRESS TO READ
         SPACE 3
VERP76   L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP80                00 - SUCCESSFUL READ
         B     VERP92                04 - END OF MEMBER
         B     VERP90                08 - END OF DATA SET
         B     VERP78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF MEMBER
VERP78   LA    R1,L991$1           OUTPUT THE I/O ERROR MESSAGE
         MVI   MTHIGHL,6           LENGTH OF THE TTR INSERT
         BAL   R2,VERMBR
         MVI   MTHIGHL,8           RESET TO EIGHT
         B     VERP92
         SPACE 2
VERP80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         LR    R0,R5               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERP81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L812$1           BLKSIZE ERROR
         BAL   R2,VERMBR
         SPACE 1
VERP81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERP82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERP82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERP82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L814$1           BLKSIZE/LRECL ERROR
         BAL   R2,VERMBR
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERP82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERP88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERP87              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERP86
         SPACE 3
VERP84   BXLE  R3,R4,VERP86        GET THE NEXT LOGICAL RECORD
         B     VERP76              END OF BLOCK, BRANCH
         SPACE 2
VERP86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BNM   VERP88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERP76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTHIGHL,1           LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         LA    R1,L811$1           LRECL TOO SMALL
         BAL   R2,VERMBR
         B     VERP76
         SPACE 2
VERP87   CLI   RLDCOUNT,X'FF'      ANY RLD/CONTROL RECORD YET?
         BNE   VERP88              YES, BRANCH
         TM    0(R3),X'F0'         TEST, ESD, IDR OR SCATTER RECORD?
         BM    VERP88              YES, BRANCH
         MVC   RLDCOUNT(1),3(R3)   NO, FIRST RLD/CONTROL ENTRY
         SPACE 2
VERP88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERP84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERP84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L813$1           LRECL TOO LARGE
         BAL   R2,VERMBR
         B     VERP84
         SPACE 2
VERP90   MVI   #MEMCNT,X'FF'              END OF DATA SET
         SPACE 2
VERP92   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OC    12(3,R6),12(R6)            ANY USER TTR?
         BZ    VERP93                     NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',12(R6)          TTR ACTUALLY INPUT?
         BH    VERP93                     YES, BRANCH
         LA    R1,L871                    NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 1
VERP93   OC    #MEMCNT+1(3),#MEMCNT+1  ANY RECORDS?
         BNZ   VERP94                  YES, BRANCH
         LA    R1,L510                 NO, NULL MEMBER WARNING
         BAL   R2,VERMBR
         SPACE 2
VERP94   CLI   15(R6),X'FF'        OS/VS LINKAGE EDITOR?
         BE    VERP96              NO, BRANCH
         CLC   15(1,R6),RLDCOUNT   RLD/CONTROL COUNTS MATCH?
         BE    VERP96              YES, BRANCH
         LA    R1,L826             NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 2
VERP96   CLI   #MEMCNT,X'FF'       END OF DATA SET?
         BNE   VERP74              NO, BRANCH
         MESSAGE MSGBLANK
         TSMSG L006                YES, STATUS MESSAGE
         SPACE 2
VERP97   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         MESSAGE MSGBLANK
         MVI   MTHIGHL,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L110$1
         SPACE 1
VERP98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERP98B             NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L113$1
         SPACE 1
VERP98B  TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         LH    R0,DS1LSTAR         GET TT OF LAST TRACK USED
         S     R0,#VERCTRK         LESS THE AMOUNT AFTER THE COMPRESS
         BNM   *+6                 ZERO OR MORE, BRANCH
         SR    R0,R0               NEGATIVE, ERROR (USE ZERO)
         CVD   R0,DOUBLE           NUMBER OF WHOLE COMPRESSED TRACKS
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L114$1
         B     VERP99B
         SPACE 2
VERP99   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         MESSAGE MSGBLANK
         SPACE 1
VERP99B  MVI   MTHIGHL,10          TEN BYTES OF INSERTED MESSAGE
         L     R1,#MEMLAST         LAST MEMBER POINTER
         L     R2,#MEMPTR          START OF MEMBER LIST
         LA    R2,0(,R2)           CLEAR HIGH-ORDER DIGIT
         SR    R1,R2               (END+1 - START) * 16
         SRL   R1,4                DIVIDE BY 16
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L115$1
         MESSAGE MSGBLANK
         B     NEWCMD
         SPACE 2
VERMBR   ST    R2,#SAVEREG         SAVE REGISTER
         ST    R1,MSGTEXT1         SAVE FOR LATER OUTPUT
         LA    R1,MEMNAME          ASSUME DATA SET CHECK
         TM    #VERUPDT,4+2        MEMBER NAME CHECK?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRNAME          YES, USE DIRECTORY NAME
         CLC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED?
         BE    VERMBR20            YES, BRANCH
         MVC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED
         SPACE 1
         MVC   MSGTEXT2(136),MSGBL132
         MVC   MSGTEXT2+4(2),VERLSTAR
         MVC   MSGTEXT2+4+3(8),##SUBCOM
         TM    DSORG,DS1DSGPO      PARTITIONED?
         BNO   VERMBR10            NO, BRANCH
         MESSAGE MSGBLANK          ONE BLANK LINE
         MVC   MSGTEXT2+4+3+8+1(8),#VERMEM
         TR    MSGTEXT2+4+3+8+1(8),TRLINE
         MVC   DOUBLE(8),#VERMEM
         LA    R1,7
         LA    R14,DOUBLE+8
         SPACE 1
VERMBR02 BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERMBR02                               NON-BLANK
         SPACE 1
         CLI   #VERMEM,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR04            YES, NON-STANDARD
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              VALID CHARACTERS?
         BZ    VERMBR10            YES, BRANCH
         SPACE 1
VERMBR04 MVC   MSGTEXT2+4+3+8+1+8+2(2),=C'X'''
         UNPK  MSGTEXT2+4+3+8+1+8+2+2(9),#VERMEM(5)
         UNPK  MSGTEXT2+4+3+8+1+8+2+2+8(9),#VERMEM+4(5)
         TR    MSGTEXT2+4+3+8+1+8+2+2(8+8),TRTABLE
         MVI   MSGTEXT2+4+3+8+1+8+2+2+8+8,C''''
         TM    #VERSTND,X'02'          STANDARD NAME CHECKING?
         BNZ   VERMBR10                NO, BRANCH
         MVC   MSGTEXT2+48(24),VERLNONS
         MESSAGE MSGTEXT2          MEMBER HEADER MESSAGE
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 1
VERMBR10 MESSAGE MSGTEXT2          MEMBER HEADER MESSAGE
         SPACE 1
VERMBR20 L     R1,MSGTEXT1         MESSAGE IDENTIFIER OR MESSAGE
         LTR   R1,R1               ANY MESSAGE?
         BZ    VERMBR30            NO, BRANCH
         TSMSG (R1)
VERMBR30 MVI   MTHIGHL,8           RESET THE MESSAGE LENGTH
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 2
VERCOMP  ST    R0,#VERCREG
         SPACE 1
VERCOMP2 L     R0,#VERCREG
         L     R1,#VERCREC
         A     R1,=F'1'            CURRENT COMPRESS RECORD
         ST    R1,#VERCREC
         ICM   R0,B'1000',#VERCREC+3  RECORD NUMBER IN RKDD (R OF RKDD)
         SPACE 1
         TRKCALC FUNCTN=TRKBAL,TYPE=BYTEUCB+1,REGSAVE=YES,             X
               BALANCE=#VERCBAL+2,RKDD=(0),MF=(E,PARMLIST)
         SPACE 1
         LTR   R15,R15             FIT ON CURRENT TRACK?
         BZ    VERCOMPX            YES, BRANCH
         SPACE 1
         L     R1,#VERCTRK
         A     R1,=F'1'            START A NEW TRACK IN OUTPUT
         ST    R1,#VERCTRK
         MVI   #VERCREC+3,0        RECORD ZERO
         B     VERCOMP2            TRY AGAIN
         SPACE 1
VERCOMPX ST    R0,#VERCBAL         SAVE THE TRACK BALANCE
         BR    R2
         EJECT
VERS00   OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   MEMNAME(8),BLANKS   INITIALIZE THE MEMBER NAME
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         B     *+8                 YES, BRANCH
         B     VERS40              NO, BRANCH
         MVC   INSERT#1(8),INSERT#2  JOBNAME
         TSMSG L851$1
         SPACE 2
VERS40   CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERS99              YES, BRANCH
         SPACE 2
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         MVI   STARTTR+2,X'01'     TTR=000001 (START OF DATA SET)
         SPACE 2
VERS76   L     R15,=A(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERS80                00 - SUCCESSFUL READ
         B     VERS90                04 - END OF FILE
         B     VERS92                08 - END OF DATA SET
         B     VERS78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF DATA SET
VERS78   MVI   MTHIGHL,6           LENGTH OF THE TTR INSERT
*        MVC   INSERT#1(6),INSERT#1        FIRST PARAMETER
         TSMSG L991$1                      OUTPUT AN ERROR MESSAGE
         MVI   MTHIGHL,8                   RESET TO EIGHT
         B     VERS97
         SPACE 2
VERS80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERS81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         TSMSG L812$1
         SPACE 1
VERS81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERS82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERS82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERS82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         TSMSG L814$1
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERS82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERS86
         SPACE 3
VERS84   BXLE  R3,R4,VERS86        GET THE NEXT LOGICAL RECORD
         B     VERS76              END OF BLOCK, BRANCH
         SPACE 2
VERS86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BP    VERS88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERS76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTHIGHL,1           LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         TSMSG L811$1
         MVI   MTHIGHL,8                   RESET TO EIGHT
         B     VERS76              YES, BRANCH
         SPACE 2
VERS88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERS84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERS84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERL2020
         ED    INSERT#1(7),DOUBLE+5
         TSMSG L813$1
         B     VERS84
         SPACE 2
VERS90   MESSAGE MSGBLANK
         TSMSG L005                       END OF FILE
         MESSAGE MSGBLANK
         B     VERS76                     LOOP
         SPACE 2
VERS92   MESSAGE MSGBLANK
         TSMSG L006                       END OF DATA SET
         SPACE 2
VERS97   MVI   MTHIGHL,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L110$1
         SPACE 1
VERS98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERS99              NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERL206B
         ED    INSERT#1(10),DOUBLE+4
         TSMSG L113$1
         B     NEWCMD
         SPACE 2
VERS99   MESSAGE MSGBLANK
         LA    R1,L116
         B     MSGNEW
VERL2020 DC    X'4020206B20212040'
VERL206B DC    X'40206B2020206B202120'
VERLSTAR DC    C'**'
VERLNONS DC    CL24'NON-STANDARD MEMBER NAME'
         COPY  DIALOG71
PDS71    CSECT
         TITLE 'P D S  --  PDS CHANGE                          6/10/85'
***********************************************************************
***      CHANGE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
CHANGE   L     R1,$DSN+8                ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         SPACE 2
CHANGE2  NI    FLAGSFF,FF-FCHANGE       DATA SET WAS CHANGED
         LA    R0,RESTART0              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         OI    SPFLAG3,SPFCHN           SIGNAL ISPMODE         SS SEP84
         BAL   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET
         TM    FLAGSAA,FOPTIONS         ANY OPERANDS?
         BO    RESTART0                 YES, BRANCH
         SPACE 2
         OI    FLAGSBB,FNOTFILE         NO FILE NAME NOW
         CLI   SAVDSN+1+2,0             ANY OTHER DSNAME?
         BE    RESTART0                 NO, DONE
         SPACE 2
         XC    SAVDSN+1(SAVLEN),DSNLEN  EXCHANGE
         XC    DSNLEN(SAVLEN),SAVDSN+1          OLD AND NEW
         XC    SAVDSN+1(SAVLEN),DSNLEN                     DSNAMES
         MVC   DSPALLOC,SAVDSN          GET PREVIOUS DISPOSITION
         MVC   SAVDSN(1),DSPREQST       SAVE REQUESTED DISPOSITION
         MVC   DSPREQST,DSPALLOC        GET PREVIOUS DISPOSITION
         B     RESTART0                 BRANCH TO REALLOCATE
         TITLE 'P D S  --  PDS KLEAR                           6/10/85'
***********************************************************************
***      KLEAR   SUBCOMMAND  (SCREEN CLEAR FOR 3270 SCREENS)        ***
***********************************************************************
*
         SPACE 1
CLEAR    ICM   R0,B'1111',PAGESIZE 3270 DISPLAY SCREEN?
         BZ    CLEAR2              NO, JUST WRITE A BLANK LINE
         TM    SPFLAG0,SPFDON      ISPMODE ON?
         BO    CLEAR2              YES, JUST WRITE A BLANK LINE
         L     R1,16               --> CVT
         L     R1,0(,R1)           --> TCB WORDS
         L     R1,8(,R1)           --> CURRENT USER ASCB
         L     R1,X'6C'(,R1)       --> ASXB
         L     R1,X'14'(,R1)       --> LWA
         CLC   X'94'+1(3,R1),=F'0' SESSION MANAGER TMP ACTIVE?
         BNE   CLEAR2              YES, JUST WRITE A BLANK LINE
         AIF ('&CONVTAM' EQ 'TCAMONLY').NCONV10
         STFSMODE ON               TURN ON FULLSCREEN MODE
.NCONV10 ANOP
         SPACE 1
         AIF ('&CONVTAM' EQ 'VTAMONLY').NCONV20
         LA    R0,12               SET SIZE FOR TPUT
         LA    R1,=X'F1115D7E114040133C404000'
         ICM   R1,B'1000',=X'03'   FULLSCR OPTION BITS
         TPUT  (1),(0),R           FULL SCREEN TPUT TO CLEAR THE SCREEN
.NCONV20 ANOP
         SPACE 1
CLEAR1   TCLEARQ INPUT             CLEAR INPUT QUEUE FOR SYNCRONIZATION
         AIF ('&CONVTAM' EQ 'TCAMONLY').NCONV30
         STLINENO LINE=1,MODE=OFF  TURN OFF FULL SCREEN MODE
.NCONV30 ANOP
         SPACE 1
         STFSMODE OFF              TURN OFF FULL SCREEN MODE
         STTMPMD  OFF              WAKE UP SESSION MANAGER
         BR    R2
         SPACE 1
CLEAR2   MESSAGE MSGBLANK          WRITE A BLANK LINE ONLY
         BR    R2
         TITLE 'P D S  --  PDS HEXC -- HEXADECIMAL CONVERSION  6/10/85'
*
*        ROUTINE TO HEXADECIMAL INPUT TO BINARY
*
         SPACE 1
*R5          START OF STRING
*R1          LENGTH OF STRING
*MSGTEXT1    PROMPTING MESSAGE FOR ANY ERROR
*MSGTEXT2+0  LENGTH OF RESULT
*MSGTEXT2+1  MAXIMUM LENGTH OF RESULT
*MSGTEXT2+4  RESULTANT STRING
*R0          RESULT IN BINARY (UP TO FOUR BYTES)
*R2          RETURN REGISTER
         SPACE 1
HEXC     LA    R3,1(,R1)            STRING LENGTH +1
         N     R3,=F'-2'            NEXT EVEN NUMBER
         SR    R15,R15              ZERO
         LA    R4,MSGTEXT2+4        WHERE OUTPUT GOES
         LA    R0,2                 PAIRWISE COUNTER
         BCTR  R5,0                 PREPARE FOR THE FIRST ADD
         CR    R1,R3                EVEN LENGTH ORIGINALLY?
         SRL   R3,1
         STC   R3,MSGTEXT2          (STRING LENGTH +1)/2
         BNE   HEXC20               NO, BRANCH
         SPACE 1
HEXC10   LA    R5,1(,R5)            NEXT CHARACTER
         IC    R15,0(,R5)           GET A BYTE TO CONVERT
         CLI   0(R5),C'0'
         BL    *+12                 0-9?
         CLI   0(R5),C'9'
         BNH   HEXC20               YES, BRANCH
         SPACE 1
         LA    R15,9(,R15)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'           BELOW AN A?
         BL    HEXC50               YES, INVALID
         CLI   0(R5),C'F'           ABOVE AN F?
         BH    HEXC50               YES, INVALID
         SPACE 1
HEXC20   SLL   R15,28               SHIFT
         SLDL  R14,4                GET A GOOD DIGIT
         BCT   R0,HEXC10            DO THE NEXT DIGIT OF A PAIR
         LA    R0,2                 PAIRWISE COUNTER
         STC   R14,0(,R4)           SAVE AN OUTPUT CHARACTER
         LA    R4,1(,R4)            ANOTHER OUTPUT CHARACTER
         BCT   R3,HEXC10            DO ALL DIGITS
         SPACE 2
         CLC   MSGTEXT2(1),MSGTEXT2+1  TOO MANY DIGITS?
         BH    HEXC40                  YES, INVALID
         ICM   R0,B'1111',MSGTEXT2+4   RESULT REGISTER
         CLI   MSGTEXT2,3              LENGTH:3
         BH    4(R2)                     ABOVE, DONE
         SRL   R0,8
         BE    4(R2)                     EQUAL, RETURN
         SRL   R0,8
         CLI   MSGTEXT2,1              LENGTH:1
         BH    4(R2)                     ABOVE, DONE
         SRL   R0,8
         B     4(R2)                     EQUAL, RETURN
         SPACE 3
HEXC40   TSMSG L840                 STRING IS TOO LONG
         B     HEXC60
         SPACE 1
HEXC50   TSMSG L841                 INVALID HEXADECIMAL DIGITS
         SPACE 1
HEXC60  $PUTGET MSGTEXT1,ATTN=0(R2) SECOND PART OF MESSAGE
         L     R5,ADDRCBUF          ENTERED DATA
         LH    R1,0(,R5)            LENGTH OF STRING
         LA    R5,4(,R5)            START OF DATA IN THE STRING
         SH    R1,=H'5'             VALID MACHINE LENGTH?
         LA    R3,0(R5,R1)          LAST BYTE OF INPUT STRING
         BZ    HEXC90               MAYBE, CHECK FURTHER
         BNP   HEXC60               NO, NULL STRING -- PROMPT AGAIN
         SPACE 1
HEXC70   CLI   0(R3),X'40'          SCAN
         BNE   HEXC80                   BACKWARDS
         BCTR  R3,0                              FOR A
         BCT   R1,HEXC70                               NON-BLANK
         B     HEXC90               POSSIBLE STRING -- CHECK
         SPACE 1
HEXC80   CLI   0(R5),X'40'          SCAN
         BNE   HEXC90                   FORWARDS
         LA    R5,1(,R5)                        FOR A
         BCT   R1,HEXC80                              NON-BLANK
         SPACE 1
HEXC90   CLI   0(R5),X'40'          REAL STRING?
         BE    HEXC60               NO, PROMPT AGAIN
         MVI   MSGTEXT2+4,X'40'     BLANKS FOR
         MVC   MSGTEXT2+5(40),MSGTEXT2+4      UPPER-CASING INPUTS
         OC    0(*-*,R5),MSGTEXT2+4 <<EXECUTED>>
         EX    R1,*-6               UPPER-CASE THE INPUT DATA
         LA    R1,1(,R1)            ACTUAL STRING LENGTH
         B     HEXC                 PARSE THIS NEW STRING
         TITLE 'P D S  --  PDS YES/NO PROMPTING ROUTINE        6/10/85'
*
*        ROUTINE TO PROMPT FOR A YES OR NO ANSWER
*
         SPACE 1
YESNO    TM    FLAGSGG,FPROMCON          PROMPTING TURNED OFF?
         BO    4(R2)                     YES, SIMULATE A YES RESPONSE
YESNO1   TM    FLAGSEE,FBKGRND+FNOTTERM  INPUT FROM THE TERMINAL?
         BNZ   4(R2)                     NO, SIMULATE A YES RESPONSE
         TM    FLAGSBB,FONESHOT          ONE SUBCOMMAND ONLY?
         BO    4(R2)                     YES, ALL DONE
         ST    R1,YESNOSV                PROMPTING MESSAGE ADDRESS
         SPACE 1
YESNO2   L     R1,YESNOSV              PROMPTING MESSAGE ADDRESS
        $PUTGET (R1),ATTN=NEWCMD       PROMPT FOR DESIRED ACTION
         TM    FLAGSEE,FNOTTERM        INPUT FROM THE TERMINAL?
         BZ    YESNO4                  YES, BRANCH
         OI    FLAGSBB,FCMD            NO, PDS SUBCOMMAND AVAILABLE
         B     4(R2)                   SIMULATE A YES RESPONSE
         SPACE 1
YESNO4   TM    FLAGSBB,FNULL           NULL REPLY?
         BO    YESNO2                  YES, TRY AGAIN
         L     R1,ADDRCBUF
         LH    R14,0(,R1)              TOTAL NUMBER OF BYTES IN MESSAGE
         SH    R14,=H'4'               LESS LENGTH OF COUNT FIELDS
         SPACE 1
YESNO6   CLI   4(R1),X'40'             BLANK?
         BNE   YESNO8                  NO, BRANCH
         LA    R1,1(,R1)               TRY THE NEXT BYTE
         BCT   R14,YESNO6              ANY NON-BLANK?
         B     YESNO2                  NO, TRY AGAIN
         SPACE 1
YESNO8   OI    4(R1),X'40'             UPPER-CASE THE RESULT
         CLI   4(R1),C'N'              CHECK REPLY
         BER   R2                      "N", RETURN AT +0
         SPACE 1
         CLI   4(R1),C'Y'              VALID REPLY?
         BNE   YESNO2                  NO, ASK AGAIN
         B     4(,R2)                  "Y", RETURN AT +4
         TITLE 'P D S  --  PDS DATE CONVERSION ROUTINE         6/10/85'
*
*        DATE CONVERSION ROUTINE -- CHANGES FORMAT YYDDDF TO MMDDYY
*
         SPACE 1
CONVDATE ST    R14,R14SAVE
         MVC   DAYTABLE,DAYMONTH
         XC    DOUBLE,DOUBLE
         MVC   0(3,R15),=X'456799' INVALID DATE IS 45/67/99
         SR    R14,R14             SETUP FOR LEAP YEAR TEST
         IC    R14,0(,R1)
         SLL   R14,4
         ST    R14,DOUBLE+4
         OI    DOUBLE+7,X'0F'
         LR    R14,R1
         CVB   R0,DOUBLE           GET YEAR IN BINARY
         SR    R1,R1
         SRDL  R0,2                SHIFT OFF REMAINDER OF YEAR/4
         LTR   R1,R1               THIS A LEAP YEAR?
         BNZ   *+8                 NO, BRANCH
         MVI   DAYTABLE+1,29       YES, FEB HAS 29 DAYS
         SPACE 1
         MVC   2(1,R15),0(R14)     YEAR RECORD CREATED
         MVC   DOUBLE+6(2),1(R14)
         TM    DOUBLE+7,X'0C'      VALID SIGN?
         BNO   DAYSKIP             NO, DON'T 0C7
         CVB   R1,DOUBLE           GET RELATIVE DAYS IN BINARY
         SPACE 1
         LA    R14,DAYTABLE
         SR    R0,R0
DAYLOOP  IC    R0,0(R14)           DAYS THIS MONTH
         SR    R1,R0               WITHIN THIS MONTH?
         BNP   DAYNOW
         LA    R14,1(R14)
         B     DAYLOOP
         SPACE 1
DAYNOW   AR    R1,R0               GET DAY OF THE MONTH
         LA    R0,10
         MR    R0,R0
         CVD   R1,DOUBLE
         MVC   1(1,R15),DOUBLE+6
         LA    R1,DAYTABLE-1
         SR    R14,R1
         LA    R1,10
         MR    R0,R14
         CVD   R1,DOUBLE
         MVC   0(1,R15),DOUBLE+6
         SPACE 1
DAYSKIP  L     R14,R14SAVE
         BR    R14
         TITLE 'P D S  --  PDS GROUP DISPLAY ROUTINE           6/10/85'
*
*        GROUP NAME DISPLAY -- UPDATES THE PDS300A MESSAGE
*
         SPACE 1
DEFGROUP TM    DSORG,DS1DSGPO                  PARTITIONED?
         BNOR  R14                             NO, RETURN
         CLI   MEMBERD,0                       ANY DEFAULT YET?
         BER   R14                             NO, RETURN
         ST    R14,R14SAVE                     RETURN ADDRESS
         LA    R0,17                           MAX CHARACTERS
         L     R1,MSGENTPT                     WHERE TO START OUTPUT
         MVC   0(17,R1),BLANK128               BLANK THE OUTPUT
         TM    PMEMMIN,X'80'                   ACTIVE MEMBER LIST?
         BNO   DEFGR10                         NO, BRANCH
         MVI   0(R1),C'('                      START OF MEMBER LIST
         LA    R1,1(,R1)                       NEXT CHARACTER
         BCTR  R0,0                            ONE LESS CHARACTER
         SPACE 1
DEFGR10  MVI   0(R1),C':'                      ASSUME COLON SEPARATOR
         TM    MEMBERD,FMEMBER1+FMEMBER2       ANY MEMBERS?
         BZ    DEFGR90                         NO, ASSUME ALL MEMBERS
         MVC   0(8,R1),MEMBERD+1+2             ASSUME A SINGLE MEMBER
         TM    MEMBERD,FMEM#MEM                MEMBER GROUP?
         BNO   DEFGR90                         NO, DONE
         SPACE 1
         SR    R14,R14
         ICM   R14,B'0011',MEMBERD+1           MACHINE LENGTH OF MEMBER
         BNZ   DEFGR20                         MORE THAN ZERO, BRANCH
         CLI   0(R1),X'00'                     NULL FIRST OPERAND?
         BNE   DEFGR20                         NO, BRANCH
         TM    MEMBERD,FMEMRANG                RANGE?
         BO    DEFGR30                         YES, BRANCH
         SPACE 1
DEFGR20  AH    R14,=H'1'                       ACTUAL LENGTH
         SR    R0,R14                          OK?
         BNP   DEFGR90                         YES, BRANCH
         AR    R1,R14                          NEXT CHARACTER START
         SPACE 1
DEFGR30  MVI   0(R1),C':'                      ASSUME RANGE
         TM    MEMBERD,FMEMRANG                CORRECT?
         BO    *+8                             YES, BRANCH
         MVI   0(R1),C'/'                      NO, PATTERN
         TM    MEMBERD,FMEMBER2                ANY MEMBER 2?
         BNO   DEFGR90                         NO, BRANCH
         MVC   1(8,R1),MEMBERD+1+2+8           ADD THE SECOND NAME
         SPACE 1
DEFGR90  L     R1,MSGENTPT                     WHERE TO START OUTPUT
         TR    0(17,R1),TRLINE                 TRANSLATE TO PRINTABLE
         L     R14,R14SAVE                     RETURN ADDRESS
         BR    R14
         TITLE 'P D S  --  PDS PARSE CLEANUP ROUTINE           6/10/85'
*
*        PARSE CLEANUP ROUTINE
*
         SPACE 1
FREEPDL  IKJRLSA ADDRANSR            RELEASE THE STORAGE
         SPACE 1
         XC    ADDRANSR,ADDRANSR
         BR    R2
         SPACE 2
PACKMAX  LH    R15,4(,R1)             NUMBER OF DIGITS
         S     R15,=F'1'              ANY CODED?
         BM    4(R14)                 NO, RETURN AT +4
         L     R1,0(,R1)              POINT TO INPUT STRING
         EX    R15,PACKNUMB           PACK ALL THE DIGITS
         CVB   R1,DOUBLE              CONVERT TO BINARY
         BR    R14                    RETURN AT +0
PACKNUMB PACK  DOUBLE(8),0(*-*,R1)    <<EXECUTED>>
         TITLE 'P D S  --  INITIALIZATION OF SPF ENVIRONMENT   6/10/85'
*
*        CALL SPF; REQUEST SPF TO CALL PDS RECURSIVELY AS A DIALOG
*
SPFRECUR DS    0H
         AIF ('&CISP' NE 'ISPFV2').NSPF350
         TM    FLAGSFF,FSPFOPT6         UNDER SPF AS A DIALOG?
         BOR   R2                       YES, BRANCH
.NSPF350 ANOP
         OI    FLAGSFF,FSPFERR          SPF ERROR FLAG
         MVI   ##ADRCM#,CONTINUE+EDITOR+FABEND
         BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE SPF
         BAL   R2,DEALLDCB              AND FREE THE DATA SET
         MVC   ISPFTEXT+4(L'ISPF),ISPF        ISPF   PGM(PDS45678) P...
         MVC   ISPFTEXT+ISPFPGM+4(8),PDSNAME  PDS COMMAND NAME
         CVD   R7,ISPFTEXT+4+ISPFPARM         ADD STORAGE ADDRESS
         LA    R1,L'ISPF+4              LENGTH OF STRING PASSED TO SPF
         SLL   R1,16                    CLEAR BOTTOM TWO BYTES
         ST    R1,ISPFTEXT              SAVE STRING TOTAL LENGTH
*        MESSAGE ISPFTEXT               DELETE * TO OUTPUT COMMAND
         MVI   ISPFTEXT+3,8             OFFSET OF PGM(... PARAMETER
         LA    R1,ISPFTEXT              START OF PARAMETER LIST
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LA    R1,ADDRTEXT              START OF CPPL PARAMETERS
         ATTACH EPLOC=ISPF,ECB=SPFECB,SZERO=NO,SHSPV=78,              XX
               SF=(E,ATTACHS1)
         ST    R1,SPFTCB                TCB ADDRESS FOR THE SUBTASK
         WAIT  ECB=SPFECB               WAIT FOR COMPLETION
         DETACH SPFTCB                  REMOVE THIS SUBTASK
         NI    FLAGSFF,FF-FSPFCALL      SPF FLAG TURNED OFF
         SR    R5,R5
         CLC   ##SUBCOM(8),$END         END SUBCOMMAND?
         BE    RETURN8                  YES, DONE
         OI    FLAGSFF,FSPFERR          SPF ERROR
         B     RESTART0                 REALLOCATE THE FILE AND REOPEN
         TITLE 'P D S  --  PDS LOGGING OF SESSION DATA         6/10/85'
         SPACE 1
LOGDATA  L     R0,RECOVER                   CURRENT RECOVERY ADDRESS
         LA    R15,FIRST4K                  SECOND WORK AREA BASE
         STM   R14,R8,LOGREGS-FIRST4K(R15)  SAVE ENTRY REGISTERS
         LA    R0,LOGD90                    LOCAL RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   MSGLAST(80),BLANK128         BLANK
         LH    R14,0(,R1)                   LENGTH OF MESSAGE
         SH    R14,=H'5'                    MACHINE LENGTH VALID?
         BM    LOGD96                       NO, DO NOT LOG
         CH    R14,=H'79'
         BNH   *+8                          LINE LENGTH=MIN(80,ACTUAL)
         LH    R14,=H'79'
         MVC   MSGLAST(*-*),4(R1)          <<EXECUTED>>
         EX    R14,*-6                     MOVE IN THE PROMPT
         LA    R0,MSGLAST                      OUTPUT THE MESSAGE
         TM    DCBRECFM-IHADCB+LOGDCB,DCBRECV  RECFM=V?
         BNO   LOGD40                          NO, BRANCH
         CLC   0(2,R1),DCBLRECL-IHADCB+LOGDCB  LRECL OK?
         BNH   *+10                            YES, BRANCH
         MVC   0(2,R1),DCBLRECL-IHADCB+LOGDCB  NO, REDUCE LRECL
         LR    R0,R1
         SPACE 1
LOGD40   NI    FLAGSFF,FF-FLOGWRT           TURN OFF FOR AN ERROR
         PUT   LOGDCB,(0)
         OI    FLAGSFF,FLOGWRT              TURN ON -- IT IS OK
         B     LOGD96
         SPACE 1
LOGD90   LA    R15,FIRST4K        RECOVERY: SECOND WORK AREA BASE
         LM    R14,R8,LOGREGS-FIRST4K(R15)  RESET ENTRY REGISTERS
         MVI   CONTOPTN,0                   TURN OFF RECORDING
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         TSMSG L081                         NOTE THE CLOSE
***TEST  OI    ATTNECB,X'40'                MARK AS AN ATTENTION
LOGD96   LA    R15,FIRST4K                  SECOND WORK AREA BASE
         LM    R14,R1,LOGREGS-FIRST4K(R15)  RESET CHANGED REGISTERS
         ST    R0,RECOVER                   RESET THE RECOVERY ADDRESS
         BR    R14
         TITLE 'P D S  --  PDS ATTACH EXTERNAL SUBCOMMANDS     6/10/85'
*
*        ATTACH EXTERNAL PDS SUBCOMMANDS
*
         SPACE 3
ATTACH   LA    R0,NEWSTAX                 ABEND RECOVERY (EXCEPT EDIT)
         ST    R0,RECOVER
         SPACE 1
ATTACH2  OI    ##ADRCM#,EXTERNAL          AN EXTERNAL SUBCOMMAND
         MVI   SUBSECB,0                  CLEAR THE ATTENTION ECB
         SPACE 2
         L     R4,=A(STAEEXIT)            POINT TO THE ESTAE EXIT
         ATTACH EPLOC=(3),ECB=SUBSECB,SZERO=NO,SHSPV=78,              XX
               TERM=YES,ESTAI=((4),(7)),SF=(E,ATTACHS2)
         SPACE 2
         ST    R1,SUBSTCB                 SUBTASK TCB ADDRESS
         WAIT  ECB=SUBSECB                WAIT FOR COMPLETION
         SPACE 2
         NI    ##ADRCM#,FF-EXTERNAL       BACK IN PDS NOW
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                MARK ITS DATA SETS FOR REUSE
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBRI           REMOVE IN-USE VERB
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,M99TUNIT
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO TEXT UNITS
         OI    S99TUPTR,S99TUPLN          MARK END OF LIST
         MVI   S99TUKEY+1,1               KEY=1
         MVI   S99TUNUM+1,1               NUMBER=1
         MVI   S99TULNG+1,4               LENGTH OF TEXT=4
         MVC   S99TUPAR(4),SUBSTCB        SUBTASK TCB ADDRESS
         SPACE 1
         DYNALLOC
         SPACE 2
         ICM   R1,B'1111',SUBSTCB         NEED TO BE DETACHED?
         BZR   R2                         NO, DONE
         MVC   FULLWORD(4),SUBSTCB        PREPARE FOR THE XC
         XC    SUBSTCB(4),SUBSTCB         DO NOT DETACH AGAIN
         DETACH FULLWORD                  REMOVE THIS SUBTASK
         BR    R2                         DONE
         DROP  R1,R3,R4,R5
         TITLE 'P D S  --  PDS ATTENTION EXIT ROUTINE          6/10/85'
***
***      ATTENTION EXIT ROUTINE
***
         SPACE 1
ATTNEXIT ENTER ATTNEXIT
         MVC   PMEMHOLD(4),PMEMCURR      SAVE MEMBER LIST POSITION
         MVC   PMEMHOLD(1),FLAGSAA       SAVE MEMBER GROUP FLAG
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE ANY MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX       TERMINATE ANY MEMBER LIST
         TM    ATTNECB,2+1             PARSE OR "ENTER OPTIONS"?
         BO    ATTNPOST                YES, "ENTER OPTIONS" -- BRANCH
         SPACE 2
         TM    ##ADRPA#,$A+$D          BROWSE, ISPF OR EDIT?
         BNO   ATTN$A                  NO, BRANCH
         STAX  ,                       CANCEL THE ATTENTION EXIT
         OI    ##ADRPA#,$F                                     SOFTATTN
         B     ATTNDONE
         SPACE 2
ATTN$A   TM    ##ADRPA#,$A             ATTENTIONS ACCEPTED?
         BZ    ATTNPTGT                YES, BRANCH
         MVI   ATTNECB,0               CLEAR THE ECB
         TSMSG L481
         B     ATTNDONE
         SPACE 2
ATTNPTGT TM    ATTNECB,X'40'           PREVIOUS ATTENTION CLEARED?
         BNZ   ATTNLOOP                NO, BRANCH
         TCLEARQ INPUT                 SYNCRONIZE INPUT QUEUE AGAIN
         OI    FLAGSII,FATTNPRO        FLAG FOR PROMPT INPUT ONLY
         L     R2,R14PTGT              $PUTGET IS NOT RECURSIVE
         L     R1,MSGENTDA             POINT TO ENTER OPTIONS MESSAGE
        $PUTGET (R1)                   PROMPT WITH "ENTER OPTIONS"
         ST    R2,R14PTGT              $PUTGET IS NOT RECURSIVE
         TM    FLAGSBB,FNULL           NULL LINE ENTERED?
         BO    ATTNBACK                YES, UNDO ATTENTION
         OI    FLAGSBB,FCMD            NO, INDICATE COMMAND AVAILABLE
         B     ATTNPOST
ATTNBACK MVC   PMEMCURR(4),PMEMHOLD    RESET MEMBER LIST POSTION
         TM    PMEMHOLD,FMEM#MEM       MEMBER GROUP IN PROGRESS?
         BNO   ATTNDONE                NO, BRANCH
         OI    FLAGSAA,FMEM#MEM        RESET ANY MEMBER GROUP
         B     ATTNDONE
         SPACE 2
ATTNPOST LA    R1,ATTNECB              ASSUME THIS IS INTERNAL TO PDS
         TM    ##ADRCM#,EXTERNAL       CORRECT?
         BZ    ATTNPOS2                YES, BRANCH
         L     R2,SUBSTCB              SUBTASK TCB ADDRESS
         STATUS STOP,TCB=(2)           STOP THE SUBTASK
         LA    R1,SUBSECB              SUBCOMMAND ECB
ATTNPOS2 POST  (1)                     NOTIFY OF ATTENTION ENTERED
         XC    PARMLIST(28),PARMLIST   CLEAR THE STACK PARAMETER LIST
         LA    R1,PARMLIST
         L     R14,ADDRUPT
         L     R15,ADDRECT
         LA    R0,PARMLIST+24
         STM   R14,R0,IOPLUPT-IOPL(R1) FOR IOPLUPT, IOPLECT & IOPLECB
         L     R15,ADDRSTCK            STACK ROUTINE START
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1)),ENTRY=(15)
         SPACE 1
ATTNDONE SR    R15,R15                 ATTENTION PROCESSED
         EXIT
         SPACE 1
ATTNLOOP MVI   ATTNECB,0               CLEAR THE ATTENTION ECB FOR NOW
         TSMSG L470                    POSSIBLE LOOP MESSAGE
         OI    ##ADRPA#,$Q             NO ABEND MSG, QUIET MODE
         DC    H'0'                    *** S0C1 ABEND TO RESTART PDS **
         SPACE 1
ATTNNULL BR    14                      JUST RETURN TO THE RETURN
         TITLE 'P D S  --  PDS ESTAE EXIT ROUTINE              6/10/85'
STAEEXIT CSECT
         USING *,R15                    TEMPORARY BASE
         CH    R0,STAECD                WORK AREA PROVIDED?
         BNE   STAE10                   YES, BRANCH
         SR    R15,R15                  NO RETRY TO BE ATTEMPTED
         BR    R14                      RETURN
         DROP  R15
STAECD   DC    H'12'
         SPACE 1
STAE10   STM   R14,R12,12(R13)          SAVE REGISTERS
         LR    R5,R1                    SDWA POINTER
         USING SDWA,R5
         L     R7,SDWAPARM              WORK AREA ADDRESS
         LR    R8,R15                   BASE REGISTER
         USING STAEEXIT,R8
         LA    R15,STAESAVE             STAE SAVE AREA
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         LM    R9,R12,BASES
         MVC   INSERT#1,MSGABE          ABEND MESSAGE
         L     R0,SDWAABCC              ABEND CODE
         SRDL  R0,12                    SHIFT USER ABEND TO R1
         ST    R0,FULLWORD              SYSTEM ABEND CODE
         UNPK  INSERT#1(5),FULLWORD+2(3)
         TR    INSERT#1(4),TRTABLE
         MVI   INSERT#1,C'S'
         MVI   INSERT#1+4,X'40'
         SRL   R1,20                    USER ABEND
         CVD   R1,DOUBLE
         UNPK  INSERT#1+MSGABEUS(5),DOUBLE+5(3)
         MVI   INSERT#1+MSGABEUS+4,X'40'
         MVC   INSERT#1+MSGABEPG(8),SDWANAME  PROGRAM NAME
         TR    INSERT#1+MSGABEPG(8),TRLINE    MAKE DISPLAYABLE
         SR    R0,R0
         SR    R1,R1
         ICM   R0,B'0111',SDWANXTP            NEXT INSTRUCTION
         ICM   R1,B'0111',SDWAEPA+1           ENTRY POINT ADDRESS
         SPACE 1
         TM    FLAGSJJ,FTSOCMD                TSOCMD VERIFICATION?
         BNO   STAE12                         NO, BRANCH
         L     R1,=V(VTSOCMD)                 YES, ACTUAL START
         MVC   INSERT#1+MSGABEPG(8),=CL8'VTSOCMD'
         SPACE 1
STAE12   TM    FLAGSJJ,FSUBCMD                SUBCMD VERIFICATION?
         BNO   STAE14                         NO, BRANCH
         L     R1,=V(VSUBCMD)                 YES, ACTUAL START
         MVC   INSERT#1+MSGABEPG(8),=CL8'VSUBCMD'
         SPACE 1
STAE14   NI    FLAGSJJ,FF-FTSOCMD-FSUBCMD     RESET ESTAE FLAGS
         SR    R0,R1                          OFFSET TO ERROR POSITIVE?
         BP    STAE16                         YES, BRANCH
         AR    R0,R1                          NO, USE ERROR LOCATION
         SR    R1,R1
         SPACE 1
STAE16   CLI   INSERT#1+MSGABEPG,C'.'         UNPRINTABLE NAME?
         BNE   STAE20                         NO, BRANCH
         MVC   INSERT#1+MSGABEAT-1(27),INSERT#1+MSGABEAT-2
         AR    R0,R1                          YES, USE ERROR LOCATION
         SPACE 1
STAE20   ST    R0,FULLWORD
         UNPK  INSERT#1+MSGABEAT(7),FULLWORD+1(4)
         TR    INSERT#1+MSGABEAT(6),TRTABLE
         MVI   INSERT#1+MSGABEAT+6,X'40'
         CLC   INSERT#1+2(2),=C'3E'           S*3E ABEND?
         BE    STAE60                         YES, IGNORE THIS ABEND
         TM    ##ADRPA#,$Q                    QUIET MODE?
         BO    STAE30                         YES, BRANCH
         L     R2,MTADDR-1                    SAVE CURRENT PARM#1
         LA    R1,INSERT#1                    START OF THIS INSERT
         ST    R1,MTADDR-1                    TELL IKJEFF02
         MVI   MTHIGHL,MSGABEND-MSGABE        LENGTH FOR THIS MESSAGE
         TSMSG L999$1                         ABEND ERROR MESSAGE
         ST    R2,MTADDR-1                    RESET CURRENT PARM#1
         SPACE 1
         TCLEARQ INPUT                        CLEAR ANY INPUT
         XC    PARMLIST(28),PARMLIST          CLEAR THE STACK
         LA    R1,PARMLIST                    AREA FOR STACK PARM LIST
         SPACE 1
         L     R2,ADDRUPT
         L     R3,ADDRECT
         LA    R4,PARMLIST+24
         STM   R2,R4,IOPLUPT-IOPL(R1)         IOPLUPT, IOPLECT, IOPLECB
         SPACE 2
         L     R15,ADDRSTCK                   STACK ROUTINE
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1)),ENTRY=(15)
         SPACE 1
         NI    FLAGSAA,FF-FMEM#MEM            TURN OFF ANY MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX      TERMINATE ANY MEMBER LIST
         SPACE 2
STAE30   LR    R1,R5                    SDWA START
         STM   R0,R15,SDWASRSV          RESET THE RETURN REGISTERS
         LR    R0,R7                    MAIN SAVE AREA ADDRESS
         TM    FLAGSFF,FSPFCALL         SPF INITIALIZED?
         BNO   *+8                      NO, BRANCH
         LA    R0,SPFMAIN               SAVEAREA WHEN PDS IS A DIALOGUE
         ST    R0,SDWASR13              RESET REGISTER 13
         SR    R2,R2
         ICM   R2,B'0111',RECOVER+1     ANY ABEND RECOVERY ADDRESS?
         BZ    STAE60                   NO, BRANCH
         TM    ##ADRPA#,$Q              QUIET MODE?
         BO    STAE32                   YES, BRANCH
         TM    FLAGSGG,FRECVCON         STAE RECOVERY DISALLOWED?
         BO    STAE60                   YES, BRANCH
         SPACE 1
STAE32   TM    ##ADRCM#,EXTERNAL        EXTERNAL SUBCOMMAND?
         BO    STAE40                   YES, BRANCH
         TM    ##ADRCM#,CONTINUE+FABEND CONTINUE THROUGH RECOVERY?
         BO    STAE35                   YES, BRANCH
         NI    ##ADRCM#,FF-CONTINUE     TURN OFF ANY CONTINUATION
STAE35   LR    R1,R5                    PDS ABENDED !!!
         L     R13,4(,R13)
         SETRP REGS=(14,12),RC=4,RETADDR=(2),RETREGS=YES,FRESDWA=YES
         SPACE 2
STAE40   POST  SUBSECB                  NOTIFY OF ATTENTION ENTERED
         SPACE 1
STAE60   NI    FLAGSAA,FF-FMEM#MEM      TURN OFF ANY MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX      TERMINATE ANY MEMBER LIST
         L     R13,4(,R13)
         LR    R1,R5                    SDWA POINTER
         SETRP REGS=(14,12),RC=0        NO RECOVERY
         DROP  R8
PDS71    CSECT
         TITLE 'P D S  --  PDS MEMBER IN USE CHECKING          6/10/85'
*
*        MEMBER IN USE CHECKS
*
         SPACE 2
ENQMTEST MVC   PARMLIST(4),ENQTEST     INITIALIZE ENQUEUE PARAMTERS
         ENQ   (SPFEDIT,DSNAME,E,52,SYSTEMS),RET=TEST,MF=(E,PARMLIST)
         SR    R1,R1
         LTR   R15,R15                 MEMBER IN USE?
         BZ    4(,R2)                  NO, SKIP ONE INSTRUCTION
         SPACE 1
         AIF ('&MVSLEV' LT 'MVS13X').NMVS13A
         XC    PARMLIST(48),PARMLIST
         GQSCAN AREA=(WORKTBL,296),RESNAME=(SPFEDIT,DSNAME,52),        X
               REQLIM=1,SCOPE=ALL,MF=(E,PARMLIST)
         SR    R1,R1
         CH    R15,=H'4'               ALLOCATED?
         BE    4(,R2)                  NO, RETURN
         LA    R1,L862                 ERROR?
         BH    MSGNEWXX                YES, TERMINATE
         LH    R14,WORKTBL+RIBVLEN-RIB         VARIABLE SECTION LENGTH
         LA    R14,WORKTBL+RIBEND-RIB(R14)     START OF RIB ENTRY
         MVC   INSERT#2(8),RIBEJBNM-RIBE(R14)  JOBNAME
         AGO   .GOTQCB
.NMVS13A ANOP
         MVC   INSERT#2(8),=CL8'????????'  NAME IF NOT ON SP 1.3 SYSTEM
         L     R14,16                  --> CVT
         LA    R14,640(,R14)           --> FIRST QCB
***TEST  LA    R14,TESTQCB  ***TEST    --> TEST STRUCTURE
         SPACE 1
ENQ2     ICM   R14,B'0111',1(R14)      ANY MORE MAJORS?
         BZ    ENQDONE                 NO, USE ????????
         CLC   16(8,R14),SPFEDIT       SPFEDIT MAJOR?
         BNE   ENQ2                    NO, CONTINUE LOOKING
         SPACE 1
         LA    R14,8(,R14)             POINT TO START OF MINOR LIST
ENQ4     ICM   R14,B'0111',1(R14)      ANY MORE MINORS?
         BZ    ENQDONE                 NO, USE ????????
         CLI   16(R14),52              SAME LENGTH?
         BNE   ENQ4                    NO, NO MATCH
         CLC   DSNAME(52),20(R14)      THE SAME MINOR?
         BNE   ENQ4                    NO, BRANCH
         SPACE 1
         L     R1,8(,R14)              FIRST (AND ONLY) QEL
         MVC   INSERT#2-2(2),14(R1)    ASID VALUE
         L     R1,16                   --> CVT
         L     R14,556(,R1)            --> ASVT
         LA    R15,520(,R14)           --> ENTRY ADDRESS -4
         L     R0,516(,R14)            NUMBER OF ASID'S
         SPACE 1
ENQ6     LA    R15,4(,R15)             NEXT ENTRY
         TM    0(R15),X'80'            IN USE?
         BO    ENQ8                    NO, BRANCH
         L     R1,0(,R15)              --> ASCB
         CLC   INSERT#2-2(2),36(R1)    THIS ASCB?
         BE    ENQ10                   YES, BRANCH
ENQ8     BCT   R0,ENQ6                 CHECK ALL ACTIVE ASID'S
         B     ENQDONE                 NOT FOUND, USE ????????
         SPACE 1
ENQ10    ICM   R15,B'1111',172(R1)     INITIATED PROGRAM JOBNAME
         BNP   ENQ12                   NO, BRANCH
         MVC   INSERT#2(8),0(R15)      YES, GET THE JOBNAME
         B     ENQDONE
ENQ12    ICM   R15,B'1111',176(R1)     JOBNAME FOR START/MOUNT/LOGON
         BNP   ENQ14                   NO, CANNOT LOCATE
         MVC   INSERT#2(8),0(R15)      YES, GET THE JOBNAME
         B     ENQDONE
ENQ14    MVC   INSERT#2(8),=CL8'SYSTEM'
         ICM   R15,B'1111',56(R1)      CSCB?
         BNP   ENQDONE                 NO, BRANCH
         MVC   INSERT#2(8),8(R15)      YES, GET THE JOBNAME
         SPACE 3
         AGO   .GOTQCB      ***TEST
         B     ENQDONE
         LA    R14,TESTQCB             --> TEST STRUCTURE
TESTQCB  DC    A(TESTQCB2)
TESTQCB2 DC    A(TESTQCB3),A(0),A(MINOR1),A(0),CL8'SYSDSN'
TESTQCB3 DC    A(00000000),A(0),A(MINOR2),A(0),CL8'SPFEDIT'
MINOR1   DC    CL2'NOT USED'
MINOR2   DC    A(MINOR3),A(0),A(TESTQEL1),A(0),AL1(52),AL3(0)
         DC    CL44'HABL.LIB.CNTL',CL8'XXXXXXXX'
MINOR3   DC    A(0),A(0),A(TESTQEL2),A(0),AL1(52),AL3(0)
         DC    CL44'HABL.LIB.CNTL',CL8'PDS71'
TESTQEL1 DC    A(0),A(0),A(0),AL2(0),AL2(1)
TESTQEL2 DC    A(0),A(0),A(0),AL2(0),AL2(2)
         SPACE 3
.GOTQCB  ANOP
ENQDONE  DS    0H
         MVI   MTHIGHL,8               EIGHT CHARACTER MEMBER NAME
         MVI   MTHIGHL+4,8             EIGHT CHARACTER JOBNAME
         MVC   INSERT#1(8),DSNMEMQ     MEMBER IN USE
         LA    R1,L850$2               ERROR MESSAGE
         TM    ##ADRPA#,$Q             QUIET MODE CHECK?
         BOR   R2                      YES, RETURN
         TSMSG (R1)                    YES, MESSAGE
         BR    R2                                  AND ERROR RETURN
         TITLE 'P D S  --  PDS STANDARD MESSAGES               6/10/85'
*
*        SUBCOMMAND MESSAGE SUBROUTINES
*
         SPACE 3
NOMEMBER LA    R1,L853$1              MSG - (MEMBER) NOT FOUND
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
BADMEMB  LA    R1,L854$1              MSG - (MEMBER) IS AN INVALID NAME
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
MEMEXIST LA    R1,L852$1              MSG - (MEMBER) EXISTS
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
INVABBR  LA    R1,L930$1              MSG - (SUBCOM.) INVALID ABBREV.
         MVC   INSERT#1(8),##SUBCOM
         LA    R3,1(,R3)              ACTUAL SUBCOMMAND NAME LENGTH
         STC   R3,MTHIGHL
         B     MSGNEW
         SPACE 3
IOERROR  LA    R1,L836                MSG - I/O ERROR IN DIRECTORY
         B     MSGNEWXX
         SPACE 3
FULLDIR  LA    R1,L855                MSG - DIRECTORY IS FULL
         SPACE 2
MSGNEWXX NI    FLAGSAA,FF-FMEM#MEM       TURN OFF ANY MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX       TERMINATE ANY MEMBER LIST
         SPACE 2
MSGNEW   TSMSG (R1)                   OUTPUT THE MESSAGE REFERRED TO
         B     NEWCMD                   AND GET A NEW SUBCOMMAND
         TITLE 'P D S  --  PDS IKJEFF02 SETUP PROCESSING       6/10/85'
*
*        IKJEFF02 SETUP PROCESSING
*
         SPACE 1
TSMSGRT  CLI   0(R1),C'0'               IKJEFF02 CALL?
         BL    MESSAGE0                 NO, BRANCH
         ST    R14,R14PUTT              SAVE RETURN ADDRESS
         MVC   MTMSGID+1(3),0(R1)       MESSAGE IDENTIFIER
         TR    INSERT#1(8),TRLINE       TRANSLATE ANY MEMBER NAME
         CLI   0(R1),C'4'               WARNING OR ERROR MESSAGE?
         BL    TSMSGRT5                 NO, BRANCH
         LA    R0,3*5/3                 NUMBER OF MESSAGES TO SAVE
         LA    R15,ERRORMSG-3           START OF MESSAGES -3
         SPACE 1
TSMSGRT2 LA    R15,3(,R15)              NEXT SAVED MESSAGE ID
         CLC   0(3,R15),0(R1)           THIS MESSAGE?
         BE    TSMSGRT5                 YES, BRANCH
         CLI   0(R15),X'00'             EMPTY SLOT?
         BNE   TSMSGRT3                 NO, BRANCH
         MVC   0(3,R15),0(R1)           YES, SAVE THIS MESSAGE ID
         B     TSMSGRT5
TSMSGRT3 BCT   R0,TSMSGRT2              CONTINUE FOR ALL POSITIONS
         MVC   ERRORMSG(3*5),ERRORMSG+3 SLIDE UP BY ONE ENTRY
         MVC   ERRORMSG+3*5-3(3),0(R1)  SAVE THIS NEW ENTRY
         SPACE 1
TSMSGRT5 LA    R15,PARTADDR-4           PARTITIONED SEARCH LIST -4
         L     R1,PARTADDR              INITIALIZE FOR THE LOOP
TSMSGRT7 LA    R15,4(,R15)              NEXT LIST ENTRY
         LR    R0,R1                    SAVE FOR OVERRUN
         L     R1,0(,R15)               POINTER IN MSGCSECT
         CLI   0(R1),X'40'              END OF MSGCSECT?
         BE    TSMSGRT9                 YES, BRANCH
         CLC   1(3,R1),MTMSGID+1        IN THIS PARTITION ELEMENT?
         BNH   TSMSGRT7                 NO, BRANCH
         SPACE 1
TSMSGRT9 ST    R0,MTCSECTP              START OF MESSAGE CSECT
         LA    R1,MTPLPTR               START OF PARAMETER LIST
         L     R15,ADDRFF02             START OF IKJEFF02
         BALR  R14,R15                  FORMAT THIS MESSAGE
         SPACE 1
         L     R14,R14PUTT              LOAD RETURN ADDRESS
         L     R1,MTEXTRCT              ADDRESS OF EXTRACTED MESSAGE
         CLC   4(3,R1),=C'PDS'          PDS ROUTINE MESSAGE?
         BNE   MESSAGE0                 NO, BRANCH
         TM    SPFLAG0,SPFDON           SPF DISPLAY MODE ON    SS SEP84
         BO    MESSAGE0                 YES, WANT MSGIDS       SS SEP84
         L     R15,ADDRUPT              ADDRESS OF THE UPT
         TM    UPTSWS-UPT(R15),UPTMID   MESSAGE ID DESIRED?
         BO    MESSAGE0                 YES, BRANCH
         LH    R0,0(,R1)                CURRENT LENGTH
         SH    R0,=H'8'                 LESS EIGHT
         SLL   R0,16                    SHIFT TO CORRECT POSITION
         ST    R0,8(,R1)                STORE OVER MESSAGE-ID
         LA    R1,8(,R1)                POINT TO NEW LENGTH WORD
         B     MESSAGE0
         TITLE 'P D S  --  PDS MESSAGE/PUTLINE PROCESSING      6/10/85'
*
*        MESSAGE PROCESSING (MULTILEVEL MESSAGES HAVE BEEN DISABLED)
*
         SPACE 1
MESSTXT1 LA    R1,MSGTEXT1              OUTPUT MSGTEXT1
         SPACE 1
MESSAGE0 ST    R14,R14PUTM              SAVE LINKAGE REGISTER
        $PUTLINE (R1),ATTN=NEWCMD       DEFAULT INTERRUPT ADDRESS
         L     R14,R14PUTM              RESTORE LINKAGE REGISTER
         BR    R14                      RETURN
         SPACE 3
*
*        PUTLINE PROCESSING
*
         SPACE 1
$PUTLINE TM    ATTNECB,X'40'          ATTN OCCUR?
         BOR   R14                    YES, IMMEDIATE RETURN
         ST    R14,R14PUTL
         TM    FLAGSFF,FLOGWRT        LOG RECORDING?
         BNO   PUTLINE1               NO, BRANCH
         ST    R14,R14PUTL2           SECONDARY PUTLINE RETURN ADDRESS
         L     R14,R14PUTM
         ST    R14,R14PUTL2+4
         BAL   R14,LOGDATA            YES, LINK TO LOG ROUTINE
         L     R14,R14PUTL2+4
         ST    R14,R14PUTM            LINKAGE FOR MESSAGE0
         L     R14,R14PUTL2
         ST    R14,R14PUTL
PUTLINE1 DS    0H
         SPACE 1
         TM    SPFLAG0,SPFDON         DIALOG DISPLAY MODE      SS JUL84
         BZ    SPFNO1                 NO                       SS JUL84
         MVI   SPFLAG1,SPFPUTL        FUNCTION TYPE            SS JUL84
         L     R15,=V(ISPDSPY)                                 SS JUL84
         TM    ##ADRCM#,EXTERNAL      EXTERNAL SUBCOMMAND?    ABL NOV84
         BNOR  R15                    NO, CALL ISPMODE        ABL NOV84
         CLC   4(6,R1),=C'PDS999'     PDS ABEND MESSAGE?      ABL NOV84
         BNER  R15                    NO, CALL ISPMODE        ABL NOV84
*** ISPMODE CANNOT ISSUE THIS ERROR MESSAGE ???               ABL NOV84
SPFNO1   DS    0H                                              SS JUL84
         SPACE 1
         LR    R0,R1                  SAVE DATA LINE POINTER
         LA    R1,PARMLIST
         USING IOPL,R1
         L     R14,ADDRUPT
         L     R15,ADDRECT
         STM   R14,R15,IOPLUPT       INITIALIZE IOPLUPT AND IOPLECT
         SPACE 1
         LA    R15,ATTNECB
         ST    R15,IOPLECB           INITIALIZE IOPLECB
         DROP  R1
         SPACE 1
         L     R15,ADDRPUTL
         SPACE 2
         PUTLINE PARM=PUTLPARM,ENTRY=(15),MF=(E,(1)),                  X
               OUTPUT=((0),TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE 2
         L     R14,R14PUTL           RETURN ADDRESS (+0 OR +4)
         TM    ATTNECB,X'40'         ATTN OCCUR?
         BOR   R14                   YES, RETURN AT +0
         SPACE 2
         CH    R15,=H'4'             PUTLINE ERROR SIGNALLED?
         BNH   4(R14)                NO, RETURN AT +4
         SPACE 2
         LA    R1,PUTLINER           ERROR MESSAGE
         LA    R0,L'PUTLINER         ERROR MESSAGE LENGTH
         TPUT  (1),(0),R             PUTLINE FAILED
         SPACE 2
         L     R1,PUTLPARM+4         START OF OUTPUT MESSAGE
         LA    R1,4(,R1)             LESS LENGTH CODE
         LA    R0,79                 FIRST 79 BYTES
         TPUT  (1),(0),R             INVALID OUTPUT TEXT
         SPACE 2
         NOP   NEWCMD                LOCATION FOR TEST COMMAND
         DC    H'0'                  FORCE AN ABEND CLEANUP
         SPACE 2
PUTLINER DC    C'*** PUTLINE FAILED, RC > 4; DATA WAS:'
         TITLE 'P D S  --  PDS PUTGET PROCESSING               6/10/85'
*
*        PUTGET PROCESSING
*
         SPACE 1
$PUTGET  TM    ATTNECB,X'40'         ATTN OCCUR?
         BOR   R14                   YES, IMMEDIATE RETURN
         LA    R0,1                  ONLY ONE SEGMENT
         STM   R0,R1,PUTOLD1
         ST    R14,R14PTGT
         TM    FLAGSFF,FLOGWRT       LOG RECORDING?
         BNO   PUTGT01               NO, BRANCH
         ST    R14,R14PUTL2+8
         BAL   R14,LOGDATA           YES, LINK TO LOG ROUTINE
         L     R14,R14PUTL2+8
         ST    R14,R14PTGT
         L     R1,PUTOLD1+4          PROMPT MESSAGE
PUTGT01  DS    0H
         SPACE 1
         TM    SPFLAG0,SPFDON         DIALOG DISPLAY MODE      SS JUL84
         BZ    SPFNO2                 NO                       SS JUL84
         MVI   SPFLAG1,SPFPTGET       FUNCTION TYPE            SS JUL84
         L     R15,=V(ISPDSPY)                                 SS JUL84
         BR    R15                                             SS JUL84
SPFNO2   DS    0H                                              SS JUL84
         NI    FLAGSBB,FF-FNULL
         SPACE 1
         LA    R0,1                  ONLY ONE SEGMENT
         STM   R0,R1,PUTOLD1
         SPACE 2
PUTGT02  LA    R1,PARMLIST
         L     R14,ADDRUPT
         L     R15,ADDRECT
         LA    R0,ATTNECB
         STM   R14,R0,IOPLUPT-IOPL(R1)  INITIALIZE IOPLUPT, IOPLECT,
*                                                  AND IOPLECB
         L     R15,ADDRPTGT
         LA    R14,PTGTPARM             LOAD PARM ADDRESS
         MVI   00(R14),X'10'            SINGLE LEVEL
         MVI   01(R14),X'80'            MODE MESSAGE
         MVI   08(R14),X'00'            PUTGET
         MVI   10(R14),X'80'            TGET EDIT
         LA    R0,PUTOLD1               ADDRESS OF OUTPUT
         ST    R0,4(,R14)
         SPACE 1
         TM    FLAGSII,FATTNPRO         ATTENTION PROCESSING?
         BNO   PUTGT04                  NO, BRANCH
         MVI   08(R14),X'10'            TERM PROCESSING
         NI    FLAGSII,FF-FATTNPRO      NOT ATTENTION PROCESSING
         SPACE 1
PUTGT04  PUTGET PARM=PTGTPARM,MF=(E,(1)),ENTRY=(15),                   X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         STC   R15,PARMLIST          SAVE RETURN CODE
         NI    FLAGSEE,FF-FNOTTERM   ASSUME INPUT WAS FROM THE TERMINAL
         CLI   PARMLIST,4            CORRECT?
         BNE   *+8                   YES, BRANCH
         OI    FLAGSEE,FNOTTERM      NO, NOT FROM THE TERMINAL
         SPACE 1
         CLI   PARMLIST,12           PUTGET STATUS:
         BH    EXIT8N                  ERROR -- BRANCH
         BL    PUTGT10                 SUCCESSFUL, STACK OR ATTENTION
         L     R1,ADDRECT              STACK, 2ND-LEVEL MSG., NO PAUSE
         OI    ECTMSGF-ECT(R1),X'80' IGNORE SECOND LEVEL MESSAGES AND
         B     PUTGT02               GET THE DATA FROM THE STACK
         SPACE 3
PUTGT10  SR    R15,R15               ASSUME RETURN AT +0
         TM    ATTNECB,X'40'         ATTN OCCUR?
         BO    PUTGT80               YES, BRANCH
         SPACE 1
         LA    R1,PTGTPARM           PUTGET PARM AREA START
         L     R1,PGPBIBUF-PGPB(R1)  INPUT BUFFER ADDRESS
         SPACE 2
         LH    R14,0(,R1)            SUBCOMMAND LENGTH
         CH    R14,=H'256'           MORE THAN PDS'S LIMIT?
         BNH   *+8                   NO, BRANCH
         LH    R14,=H'256'           YES, USE THE CURRENT LIMIT
         BCTR  R14,0                 MACHINE LENGTH OF THIS LINE
         MVC   PUTGETBF(*-*),0(R1)   <<EXECUTED>>
         EX    R14,*-6               SAVE THE ENTERED LINE
         SPACE 2
         LH    R0,0(R1)              BUFFER LENGTH
         ICM   R0,B'1000',=X'01'     SUBPOOL IS 1
         SPACE 1
         FREEMAIN R,LV=(0),A=(1)     RELEASE THE INPUT BUFFER
         SPACE 2
         LA    R1,PUTGETBF           ADDRESS OF INPUT LINE
         ST    R1,ADDRCBUF           SAVE THE LINE ADDRESS
         SPACE 1
         TM    FLAGSFF,FLOGWRT       LOG RECORDING?
         BNO   PUTGT70               NO, BRANCH
         L     R14,R14PTGT
         ST    R14,R14PUTL2+12
         L     R1,ADDRCBUF           USER RESPONSE
         BAL   R14,LOGDATA           YES, LINK TO LOG ROUTINE
         L     R14,R14PUTL2+12
         ST    R14,R14PTGT
         SPACE 2
PUTGT70  LA    R15,4                 ASSUME RETURN AT +4
         CH    R15,PUTGETBF          A NULL LINE?
         BL    PUTGT80               NO, BRANCH
         OI    FLAGSBB,FNULL         YES, INDICATE A NULL LINE
         SPACE 2
PUTGT80  L     R14,R14PTGT
         B     0(R14,R15)
         TITLE 'P D S  --  PDS DATA SET ALLOCATION             6/10/85'
*
*        DATA SET ALLOCATION SUBROUTINE
*
         SPACE 2
ALLOCATE CSECT
         USING *,R8
         LR    R8,R15
         ST    R14,R14SAVE             SAVE LINKAGE REGISTER
         SPACE 2
DYNA010  NI    FLAGSCC,FF-FPERMDS      CLEAR PERMANENT STATUS
         SPACE 2
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'ALLDDNAM,R3),ALLDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLDSORG,R3),ALLDSORG         SPECIFY DSORG TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLDSORG(,R3)                START OF NEXT TEXT
         SPACE 1
         TM    FLAGSBB,FNOTFILE                  FILENAME ENTERED?
         BO    DYNA080                           NO, BRANCH
         SPACE 1
         MVI   S99VERB,S99VRBIN                  REQUEST INFORMATION
         MVI   WORKTBL+1,X'01'                   SPECIFY DDNAME KEY
         MVC   WORKTBL+6(8),DDNAMEH              SPECIFY DDNAME
         MVC   DDNAME(8),DDNAMEH                 SAVE DDNAME
         MVI   WORKTBL+6+8+1,X'0A'               REQUEST DSORG
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLDSNAM,R3),ALLDSNAM         SPECIFY DSNAME TEXT
         MVI   1(R3),X'05'                       REQUEST DSNAME KEY
         MVC   6(44,R3),BLANK128                 BLANK DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         SPACE 1
         DYNALLOC
         SPACE 1
         LTR   R15,R15                           ANY PROBLEM?
         BNZ   DYNA400                           YES, BRANCH
         SPACE 1
         MVC   DSNLEN(46),4(R3)                  GET DSN AND LENGTH
         MVC   DSORG(2),WORKTBL+20               SAVE RETURNED DSORG
         MVC   VOLUME(8),BLANKS
         MVI   VOLALLOC,X'40'                    VOLUME IS NOT KNOWN
         L     R1,X'21C'                         --> CURRENT TCB
         L     R1,X'00C'(,R1)                    --> TIOT
         LA    R15,24                            OFFSET TO TIOENTRY
         SPACE 1
DYNA060  LA    R1,0(R15,R1)                      NEXT ENTRY
         CLI   0(R1),0                           END OF TIOT?
         BE    DYNA200                           YES, BRANCH
         SR    R15,R15
         IC    R15,0(,R1)
         CLC   4(8,R1),DDNAME                    THIS DDNAME?
         BNE   DYNA060                           NO, BRANCH
         SPACE 1
         L     R14,16(,R1)                       POINT TO UCB
         TM    18(R14),X'20'                     DASD?
         BO    DYNA070                           YES, BRANCH
         TSMSG L843                              NO, ERROR MESSAGE
         B     DYNA440
         SPACE 1
DYNA070  MVC   VOLUME(6),28(R14)                 SAVE VOLUME NAME
         MVC   VOLALLOC(6),28(R14)               SAVE VOLUME NAME
         LA    R1,0(R15,R1)                      NEXT ENTRY
         CLC   4(8,R1),BLANKS                    CONCATENATED ENTRY?
         BNE   DYNA200                           NO, BRANCH
         MVI   MTHIGHL,8
         MVC   INSERT#1(8),DDNAME                DDNAME IN ERROR
         TSMSG L844$1
         B     DYNA440
         SPACE 3
DYNA080  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLVOLUM,R3),ALLVOLUM         SPECIFY VOLUME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R0,L'ALLVOLUM(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   VOLUME,X'40'                      ANY VOLUME SPECIFIED?
         BE    DYNA100                           NO, BRANCH
         MVI   1(R3),X'10'                       CONVERT TO SPECIFY
         MVC   6(6,R3),VOLUME                    SPECIFY VOLUME
         LA    R3,L'ALLVOLUM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLUNIT,R3),ALLUNIT           SPECIFY UNIT TEXT
         MVC   6(8,R3),ALLOLSYS                  USE "SYSALLDA"
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R0,L'ALLUNIT(,R3)                 START OF NEXT TEXT
         SPACE 1
DYNA100  LR    R3,R0                             NEXT TEXT POINTER
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLPERM,R3),ALLPERM           SPECIFY PERMANENT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLPERM(,R3)                 START OF NEXT TEXT
         SPACE 1
         CLI   PASSWORD,X'41'                    ANY PASSWORD?
         BL    DYNA140                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLPASSW,R3),ALLPASSW         SPECIFY PASSWORD TEXT
         MVC   6(8,R3),PASSWORD                  SPECIFY DDNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLPASSW(,R3)                START OF NEXT TEXT
         SPACE 1
DYNA140  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLDSNAM,R3),ALLDSNAM         SPECIFY DSNAME TEXT
         MVC   4(46,R3),DSNLEN                   SPECIFY DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLALLOC,R3),ALLALLOC         SPECIFY DISP TEXT
         MVC   6(1,R3),DSPALLOC                  SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLNORM,R3),ALLNORM           SPECIFY NORMAL TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'ALLNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'ALLANORM,R3),ALLANORM         SPECIFY ABNORMAL TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         SPACE 1
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         LTR   R15,R15                  SUCCESSFUL?
         BNZ   DYNA400                  NO, BRANCH
         MVC   DDNAME,WORKTBL+6         SAVE DDNAME
         MVC   DSORG(2),WORKTBL+20      SAVE DSORG
         MVC   VOLALLOC(6),VOLUME       SET THE VOLUME SPECIFIED
         CLI   VOLUME,X'40'             ANY VOLUME?
         BNE   *+10                     YES, BRANCH
         MVC   VOLALLOC(6),WORKTBL+28   GET THE ALLOCATED VOLUME NAME
         SPACE 1
         CLC   DDNAME(3),ALLOL000         TEMPORARY TYPE DDNAME?
         BNE   DYNA200                    NO, BRANCH
         NC    WORKTBL+6(8),ALLOL000      MASK WITH "SYS00000"
         CLC   WORKTBL+6(8),ALLOL000      TEMPORARY TYPE DDNAME?
         BE    *+8                        YES, BRANCH
DYNA200  OI    FLAGSCC,FPERMDS            NO, MARK AS "PERMANENT"
         SPACE 1
         DEVTYPE DDNAME,DEVTYPE,DEVTAB  GET DEVICE DEPENDENT DATA
         SPACE 1
         L     R14,OBTAIN               OBTAIN FLAGS
         LA    R15,DSNAME               ADDRESS OF DSNAME
         LA    R0,VOLALLOC              VOLUME SERIAL
         LA    R1,DS1FMTID              OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                GET THE FORMAT 1 DSCB
         SPACE 1
         LTR   R15,R15                  SUCESSFUL OBTAIN?
         BZ    DYNA260                  YES, BRANCH
         SPACE 1
*     IF THE DSNAME IS AN ALIAS ENTRY IN A VSAM CATALOG, A LOCATE
*     WILL PUT THE TRUE NAME IN THE DSNAME FIELD -- ISSUE A LOCATE,
*     AND TRY THE OBTAIN AGAIN.
         MVC   WORKTBL(44),DSNAME       COPY DSNAME TO ANOTHER AREA
         L     R14,LOCATE
         LA    R15,WORKTBL              ACTUAL DSNAME
         SR    R0,R0                    NO CVOL VALUE
         LA    R1,MSGTEXT1              LOCATE WORK AREA
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAMETERS
         LOCATE PARMLIST
         LTR   15,15                    SUCCESSFUL LOCATE?
         BNZ   DYNA240                  NO, ERROR
         SPACE 1
         L     R14,OBTAIN               OBTAIN FLAGS
         LA    R15,WORKTBL              ACTUAL DSNAME
         LA    R0,VOLALLOC              VOLUME SERIAL
         LA    R1,DS1FMTID              OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST          SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                GET THE FORMAT 1 DSCB
         SPACE 1
         LTR   R15,R15                  SUCCESSFUL OBTAIN?
         BZ    DYNA260                  YES, BRANCH
DYNA240  MVI   VOLALLOC,X'40'           NO, SET BACK TO BLANK
         SPACE 2
DYNA260  TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DYNA300                  YES, BRANCH
         SPACE 2
         LA    R1,X'100'                TTR=1
         ST    R1,DIRTTR                SAVE FOR LATTER
         TM    ##ADRCM#,EDITOR          EDIT, BROWSE, TSOEDIT OR FSE?
         BO    DYNA300                  YES, BRANCH
         TSMSG L530                     OUTPUT A WARNING MESSAGE
         SPACE 2
DYNA300  LA    R14,FIRST4K
         LA    R14,RECALLDS-FIRST4K(R14)  POINT TO THE DSNAME SAVE
         MVC   0(132,R14),MSGBL132
         MVC   4(8,R14),$CHA              "CHANGE"
         MVI   4+9(R14),C''''             QUOTE
         MVC   4+10(44,R14),DSNAME        DATA SET NAME
         LH    R1,DSNLEN                  LENGTH
         LA    R14,4+10(R1,R14)           POINT TO END OF NAME
         MVI   0(R14),C''''               FINAL QUOTE
         CLI   VOLUME,X'41'               ANY VOLUME?
         BL    DYNA310                    NO, BRANCH
         MVC   2(4,R14),DYNAVOL           ADD "VOL("
         MVC   2+4(6,R14),VOLUME          ADD VOLUME
         MVI   2+4+6(R14),C')'            ADD FINAL )
         LA    R14,2+4+6(R14)             POINT TO ) -1
         SPACE 1
DYNA310  CLI   DSPALLOC,ALLOOLD           OLD ALLOCATION?
         BNE   *+10                       NO, BRANCH
         MVC   2(3,R14),DYNAOLD           YES, ADD OLD
         SPACE 1
         XC    6(40,R14),6(R14)           INSERT FINAL NULLS
         SPACE 1
         L     R14,R14SAVE              RETURN AT
         B     4(R14)                            +4 (SUCCESSFUL)
         SPACE 2
DYNA400  CLC   DSNAMEH(3),=C'END '      WAS "END" ENTERED BY THE USER?
         BNE   DYNA410                  NO, BRANCH
         TM    DSNAMEH+3,X'FF'-X'40'    WITH A FOLLOWING BLANK OR NULL?
         BZ    DYNA900                  YES, TERMINATE THE PROGRAM
         SPACE 1
DYNA410  LA    R14,M99RB                POINTER TO SVC 99 BLOCK
         ST    R15,DAIRRC               RETURN CODE
         LA    R15,DAIRRC               POINTER TO RETURN CODE
         LA    R0,ADDRFF02              POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)         POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL              POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP           INITIALIZE DFDAPLP, DFDRCP,
*                                         DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                  PROBLEM WITH IKJEFF18?
         BZ    DYNA440                  NO, BRANCH
         SPACE 2
DYNA420  CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         TSMSG L835$1
         SPACE 1
DYNA440  TM    FLAGSBB,FONESHOT         ONE SUBCOMMAND ONLY?
         BO    DYNA900                  YES, TERMINATE
         CLI   DSPREQST,ALLOOLD         DISP=OLD REQUESTED?
         BE    DYNA500                  YES, ATTEMPT RETRY
         CLI   DSPALLOC,ALLOMOD         MOD ALLOCATION ATTEMPT?
         BE    DYNA444                  YES, BRANCH
         CLI   DSPALLOC,ALLOOLD         OLD ALLOCATION ATTEMPT?
         BNE   DYNA500                  NO, REALLOCATE
         SPACE 1
DYNA444  MVI   DSPALLOC,ALLOSHR         YES, RETRY WITH DISP=SHR
         NI    FLAGSAA,FF-FMEM#MEM      TURN OFF MEMBER GROUPS
         NI    ##ADRCM#,FF-CONTINUE     TURN OFF THE CONTINUED FLAG
         B     DYNA010
         SPACE 2
DYNA500  CLI   VOLSET,C'*'              ANY VOLSET IN EFFECT?
         BE    DYNA600                  NO, BRANCH
         CLI   VOLSET,X'40'             ANY VOLSET IN EFFECT?
         BNH   DYNA600                  NO, BRANCH
         MVI   MTHIGHL,8
         MVC   INSERT#1(8),VOLSET       CURRENT VOLSET VALUE
         TSMSG L410$1                   ISSUE A WARNING MESSAGE
         SPACE 2
DYNA600 $PUTGET PDS381A,ATTN=DYNA900    DATA SET RECOVERY ROUTINE
         SPACE 2
         TM    FLAGSBB,FNULL            USER WANT ANOTHER DATA SET?
         BO    DYNA500                  NULL REPLY, ASK AGAIN
         SPACE 1
         MVC   DSNLEN(SAVLEN),SAVDSN+1  RESET THE DATA SET NAME
         MVC   FULLWORD(4),##ADRCMD     SAVE CONTINUATION INFORMATION
         MVC   ##SUBCOM(PTW),$CHA       PCL FOR SUBCOMMAND NAME
         L     R15,=A(PARSE)            PARSE ADDRESS
         BALR  R14,R15                  INVOKE PARSE
         B     DYNA900                  EXIT - PARSE FAILURE
         MVC   ##ADRCMD(4),FULLWORD     CONTINUATION INFORMATION
         B     DYNA010                  RETRY ALLOCATION
         SPACE 2
DYNA900  L     R14,R14SAVE              ERROR EXIT - NOT ALLOCATED
         BR    R14
ALLDDNAM DC    X'0055000100084040404040404040'
ALLDSORG DC    X'0057000100020000'
ALLVOLUM DC    X'005D00010006404040404040'
ALLUNIT  DC    X'0015000100084040404040404040'
ALLPERM  DC    X'00520000'
ALLPASSW DC    X'0050000100084040404040404040'
ALLDSNAM DC    X'00020001002C'
ALLALLOC DC    X'00040001000108'
ALLNORM  DC    X'00050001000108'
ALLANORM DC    X'00060001000108'
ALLOOLD  EQU   X'01'
ALLOMOD  EQU   X'02'
ALLOSHR  EQU   X'08'
ALLOLSYS DC    C'SYSALLDA'
ALLOL000 DC    C'SYS00000'
DYNAVOL  DC    C'VOL('
DYNAOLD  DC    C'OLD'
PDS71    CSECT
         TITLE 'P D S  --  PDS DATA SET UNALLOCATION           6/10/85'
*
*        FREE DATA SET NO LONGER REQUIRED
*
         SPACE 2
DEALLDCB MVI   VOLALLOC,X'40'     ** CONDITIONAL FREE OF FILE
         TM    FLAGSCC,FPERMDS    DATA SET PERMANENTLY ALLOCATED?
         BOR   R2                 YES, SKIP EXPLICIT FREE
         SPACE 2
DEALLOCZ CLC   DDNAME(8),SYSPROC        SYSPROC DDNAME?
         BER   R2                       YES, QUIT
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBUN         UNALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL               START OF TEXT
         SPACE 1
         MVC   0(L'UNADDNAM,R3),UNADDNAM
         MVC   6(8,R3),DDNAME           SPECIFY DDNAME
         ST    R3,S99TUPTR              POINT TO TEXT UNIT
         LA    R3,L'UNADDNAM(,R3)       START OF NEXT TEXT
         SPACE 1
         CLC   DDNAME(8),SYSHELP        SYSHELP DDNAME?
         BE    UNA100                   YES, BRANCH
         SPACE 1
         LA    R5,4(,R5)                NEXT POINTER ELEMENT
         MVC   0(L'UNAPERM,R3),UNAPERM  UNALLOC "PERM" TOO
         ST    R3,S99TUPTR              POINT TO TEXT UNIT
         LA    R3,L'ALLDSORG(,R3)       START OF NEXT TEXT
         SPACE 1
UNA100   OI    S99TUPTR,X'80'           MARK END OF LIST
         SPACE 1
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         LTR   R15,R15                  SUCCESSFUL?
         BNZR  R2                       NO, RETURN
         BR    R2                       RETURN
         SPACE 1
UNADDNAM DC    X'0001000100084040404040404040'
UNAPERM  DC    X'00070000'
         TITLE 'P D S  --  PDS OPEN/CLOSE STOW ROUTINES        6/10/85'
*
*        OPEN/CLOSE STOW SUBROUTINES
*
         SPACE 3
OPENSTOW NI    FLAGSCC,FF-FBLDLOK                BLDL IS INVALID NOW
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BO    4(R2)                             YES, EXIT
         SPACE 1
         MVI   OPENLIST,X'80'
         TM    FLAGSCC,FUPDATE                   UPDATE DESIRED?
         BNO   OPENSTW4                          NO, BRANCH
         OPEN  (STOWDCB,UPDAT),MF=(E,OPENLIST)   OPEN IT FOR UPDATE NOW
         B     OPENSTW8                          CHECK IF GOOD OPEN
         SPACE 1
OPENSTW4 BAL   R6,RESERVE                        RESERVE THE DEVICE
***TEST  OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT NOW
         OPEN  (STOWDCB,UPDAT),MF=(E,OPENLIST)   OPEN IT FOR UPDATE NOW
         SPACE 1
OPENSTW8 TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BO    4(R2)                             YES, BRANCH
         SPACE 1
         TSMSG L831                              STOW DID NOT OPEN
         NI    FLAGSCC,FF-FUPDATE                OPEN FAILED
         BR    R2                                ERROR RETURN
         EJECT
*
*        OPEN/CLOSE STOW SUBROUTINES
*
         SPACE 3
SHUTSTOW TM    FLAGSCC,FUPDATE                   UPDATE CLOSE?
         BO    SHUTSTW2                          YES, BRANCH
         TM    FLAGSEE,FBKGRND                   BACKGROUND MODE?
         BOR   R2                                YES, RESERVE IS OK
         SPACE 2
SHUTSTW2 MVI   OPENLIST,X'80'
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BNO   DEQ                               NO, BRANCH
         SPACE 1
         CLI   DSPALLOC,ALLOOLD                  EXCLUSIVE USE?
         BE    DEQ                               YES, BRANCH
         CLI   DSPALLOC,ALLOMOD                  EXCLUSIVE USE?
         BE    DEQ                               YES, BRANCH
         SPACE 1
         TM    FLAGSCC,FUPDATE                   UPDATE CLOSE?
         BNO   SHUTSTW6                          NO, BRANCH
         XI    FLAGSCC,FUPDATE                   UPDATE CLOSE NOW
         MVI   OPENLIST,X'7C'                    MARK TERMINATION
         L     R8,$LIS+8                         UPDATE BASE REGISTER
         L     R15,=A(LIST170)                   FINAL UPDATE ROUTINE
         BALR  R14,R15
         SPACE 2
SHUTSTW6 TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BNO   DEQ                               NO, BRANCH
         CLOSE (STOWDCB),MF=(E,OPENLIST)         CLOSE IT NOW
         B     DEQ
         TITLE 'P D S  --  PDS RESERVE ROUTINE                 6/10/85'
*
*        UCB RESERVE ROUTINE
*
         SPACE 1
RESERVE  CLI   DSPALLOC,ALLOOLD            EXCLUSIVE USE?
         BER   R6                          YES, RETURN
         CLI   DSPALLOC,ALLOMOD            EXCLUSIVE USE?
         BER   R6                          YES, RETURN
         SPACE 1
         CLI   RESERVET,C'R'               ALREADY RESERVED?
         BER   R6                          YES, RETURN
         MVI   RESERVET,C'R'               MARK AS RESERVED
         SPACE 2
         MVC   PARMLIST(4),RESERVEL        LIST FORM OF RESERVE
         RESERVE (SPFEDIT,DSNAME,E,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,  XX
               MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNOR  R6                          NO, RETURN
         SPACE 2
         L     R1,UCBADDR                  UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   RESERVE2                    NO, BRANCH
         MVC   PARMLIST(4),RESERVEL        LIST FORM OF RESERVE
         RESERVE (SYSIEWLP,DSNAME,E,44,SYSTEMS),RET=HAVE,UCB=UCBADDR, XX
               MF=(E,PARMLIST)
         BR    R6
         SPACE 5
RESERVE2 MVC   PARMLIST(4),ENQHAVE         LIST FORM OF RESERVE
         ENQ   (SYSIEWLP,DSNAME,E,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
         BR    R6
         TITLE 'P D S  --  PDS DEQ ROUTINE                     6/10/85'
*
*        UCB DEQ ROUTINE
*
         SPACE 1
DEQ      CLI   DSPALLOC,ALLOOLD            EXCLUSIVE USE?
         BER   R2                          YES, RETURN
         CLI   DSPALLOC,ALLOMOD            EXCLUSIVE USE?
         BER   R2                          YES, RETURN
         SPACE 1
         CLI   RESERVET,C'R'               RESERVED?
         BNER  R2                          NO, RETURN
         MVI   RESERVET,0                  NO LONGER RESERVED
         SPACE 2
         MVC   PARMLIST(4),DEQSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (SPFEDIT,DSNAME,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,       X
               MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNOR  R2                          NO, RETURN
         SPACE 2
         L     R1,UCBADDR                  UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   DEQ2                        NO, BRANCH
         MVC   PARMLIST(4),DEQSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (SYSIEWLP,DSNAME,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,      X
               MF=(E,PARMLIST)
         BR    R2
         SPACE 5
DEQ2     MVC   PARMLIST(4),DEQSYS          LIST FORM, DEQ "SYSTEM"
         DEQ   (SYSIEWLP,DSNAME,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
         BR    R2
         TITLE 'P D S  --  PDS OPEN EXIT                       6/10/85'
         USING IHADCB,R1
OPENEXIT TM    ##ADRCM#,FIXOPEN                FIXPDS DCB MODIFICATION?
         BNO   OPENEXT2                        NO, BRANCH
         MVC   DCBRECFM(1),#RECFM+3            NEW RECORD FORMAT
         MVC   DCBLRECL(2),#LRECL+2            NEW LRECL
         MVC   DCBBLKSI(2),#BLKSI+2            NEW BLKSIZE
         MVI   DCBKEYLE,0                      RESET THE KEY LENGTH
         CLI   #OPTCD,1                        ANY OPTCD CHANGE?
         BL    OPENEXT2                        NO, BRANCH
         NI    DCBOPTCD,255-X'A4'              TURN OFF AFFECTED BITS
         OC    DCBOPTCD(1),#OPTCD+3            TURN ON DESIRED BITS
         SPACE 1
OPENEXT2 MVC   RECFM(1),DCBRECFM               SAVE RECORD FORMAT BYTE
         NI    FLAGSCC,FF-RECFMU-RECFMV-RECFMF CLEAR TEST BITS
         XI    FLAGSCC,RECFMU                  ASSUME RECFM=U
         TM    DCBRECFM,DCBRECU                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMU+RECFMV           ASSUME RECFM=V
         TM    DCBRECFM,DCBRECV                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMV+RECFMF           ASSUME RECFM=F
         NI    DCBRECFM,FF-DCBRECSB            DROP ANY S OF RECFM=FBS
         TM    DCBRECFM,DCBRECF                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMV                  NO, INDICATE UNKNOWN
         SPACE 1
OPENEXT4 CLC   BLKSI(2),ZERO                   HAVE ANY LRECL?
         BNER  R14                             YES, RETURN
         MVC   LRECL,DCBLRECL                  SAVE LRECL
         MVC   BLKSI,DCBBLKSI                  SAVE BLOCKSIZE
         MVC   OPTCD,DCBOPTCD                  SAVE OPTCD
         BR    R14
         DROP  R1
         TITLE 'P D S  --  PDS DIRECTORY READ SUBROUTINE       6/10/85'
*
*        DIRECTORY READ SUBROUTINE
*
         SPACE 1
READDIR  ST    R14,R14READD
         NI    FLAGSEE,FF-FDIREND      END OF DIRECTORY TO COME YET
         CLI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)?
         BNE   DEBLOCK                 NO, BRANCH
         SPACE 1
         XC    TOTBLOCK(LTOT),TOTBLOCK CLEAR THE TOTALS AREA
         LA    R1,L875                 ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO        CORRECT?
         BE    READDIRE                YES, ERROR EXIT
         SPACE 2
READDIR1 L     R15,=A(EXCP)        READ THE DIRECTORY RECORDS
         BALR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     READDIR2                 00 - GOOD READ
         B     READDIR4                 04 - END OF MEMBER
         B     READDIR4                 08 - END OF DATA SET
         B     IOERROR                  12 - I/O ERROR IN DIRECTORY
         SPACE 2
READDIR2 LA    R1,L876                 ASSUME INVALID DIRECTORY BLOCK
         CLC   LS+2(2),=H'256'         CORRECT?
         BNE   READDIRE                YES, ERROR EXIT
         LH    R14,TOTTRKS
         LA    R14,1(,R14)
         CLC   TOTCCHH,CURMBB+3        SWITCH TRACKS?
         BE    *+8                     NO, BRANCH
         STH   R14,TOTTRKS             YES, UPDATE TRACK COUNT
         MVC   TOTCCHH,CURMBB+3        NEW CCHH QUANTITY
         SPACE 1
         LH    R14,TOTBLOCK
         LA    R14,1(R14)
         STH   R14,TOTBLOCK
         TM    FLAGSEE,FDIREND         USAGE AND LAST MEMBER FOUND?
         BO    READDIR1                YES, BRANCH
         SPACE 1
         LH    R14,TOTUSED
         LA    R14,1(R14)
         STH   R14,TOTUSED
         MVI   TOTTHISM+1,0
         SPACE 1
         LR    R15,R0                   BUFFER ADDRESS
         LA    R0,2
         LH    R1,0(,R15)
         BCTR  R1,0
         LA    R1,0(R1,R15)
         STM   R15,R1,DIRPTRS
         SPACE 8
DEBLOCK  LM    R15,R1,DIRPTRS
         BXH   R15,R0,READDIR1         INDEX TO NEXT MEMBER NAME
         STM   R15,R1,DIRPTRS
         SPACE 1
         CLI   0(R15),X'FF'            LAST MEMBER?
         BE    READDIR3                YES
         SPACE 1
         MVC   MEMNAME(12),0(R15)      SAVE FIRST 12 BYTES OF ENTRY
         SPACE 1
         LH    R0,TOTTHISM
         A     R0,=F'1'
         STH   R0,TOTTHISM
         CH    R0,TOTMAXM
         BNH   *+8
         STH   R0,TOTMAXM              MAX DIRECTORY ENTRIES IN A BLOCK
         SPACE 1
         IC    R14,11(,R15)
         N     R14,=XL4'0000001F'
         LA    R0,12(R14,R14)
         STM   R15,R1,DIRPTRS
         SPACE 2
         TM    MEMFLAG,X'80'           ALIAS?
         LA    R14,TOTMEMR             ASSUME NOT
         BZ    *+8                     CORRECT?
         LA    R14,TOTMEMA             NO, ALIAS
         SPACE 1
         LH    R15,0(,R14)
         LA    R15,1(,R15)
         STH   R15,0(,R14)
         SPACE 1
         CLC   ##SUBCOM(8),$USA        USAGE SUBCOMMAND?
         BE    DEBLOCK                 YES, CONTINUE TO DEBLOCK
         LA    R15,4                   NO, GET THE EXIT OFFSET
         B     READDIR5
         SPACE 2
READDIR3 OI    FLAGSEE,FDIREND         FLAG LAST MEMBER FOUND
         CLC   ##SUBCOM(8),$USA        USAGE SUBCOMMAND?
         BE    READDIR1                YES, CONTINUE READING
         B     READDIR4                NO, EXIT WITH LAST MEMBER
         SPACE 2
READDIRE TSMSG (R1)                    DIRECTORY ERROR
         MVI   STARTTR+2,X'00'         RESET THE START TTR
         SPACE 2
READDIR4 SR    R15,R15                 END OF DIRECTORY OR LAST MEMBER
         SPACE 1
READDIR5 L     R14,R14READD
         B     0(R14,R15)
         TITLE 'P D S  --  PDS EXCP READ SUBROUTINE            6/10/85'
*
*        EXCP SUBROUTINE
*
         SPACE 1
EXCP     CSECT
         USING *,R8
         ST    R14,R14EXCP
         ST    R8,R8EXCP
         LR    R8,R15
         LH    R1,IODONE                    ANOTHER
         LA    R1,1(,R1)                           INPUT
         STH   R1,IODONE                                ENTRY
         LA    R1,IODIRCUR                  ASSUME DIRECTORY BLOCKS
         TM    FLAGSII,FDIRGRP              DIRECTORY READ?
         BO    *+8                          YES, BRANCH
         LA    R1,IOBUFCUR                  NO, I/O BUFFER CONTROL
         ST    R1,ICONTROL
         ICM   R0,B'1111',STARTTR           NEW TTR ADDRESS?
         BNZ   EXCPNTTR                     YES, BRANCH
         TM    FLAGSII,FDIRGRP              DIR. GROUP TO BE INPUT?
         BNO   EXCPCONT                     NO, BRANCH
         MVC   IOBSEEK(LIOBDIR),IODIR1+4+12 RESTORE INPUT STATUS
         B     EXCPCONT
         SPACE 2
EXCPNTTR LH    R1,IODONEW                   ANOTHER
         LA    R1,1(,R1)                           TTR
         STH   R1,IODONEW                             CHANGE
         L     R0,STARTTR
         SRL   R0,8
         BCTR  R0,0                         REDUCE INITIAL TTR BY ONE
         SLL   R0,8
         SPACE 1
         STM   R15,R12,12+4(R13)            CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)   DEB ADDRESS
         LA    R2,IOBSEEK                   RETURN CCHHR
         L     R15,ADDRCNVT                 TTR TO MBBCCHHR CONVERTER
         LR    R3,R13                       SAVE ADDRESS OF SAVE AREA
         BALR  R14,R15
         LR    R13,R3                       RESTORE SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)            RESTORE REGISTERS
         SPACE 1
         XC    STARTTR,STARTTR              ZERO THE TTR
         CLC   IOBSEEK(8),LSTARMBB          LESS THAN DS1LSTAR?
         BL    EXCP1                        YES, BRANCH
         TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCP1                        YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGIS               ISAM DATA SET?
         BO    EXCP1                        YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGPO               PARTITIONED DATA SET?
         BNO   EXCPEND8                     NO, NO WARNING MESSAGE
         TSMSG L872                         OUTPUT A WARNING MESSAGE
         B     EXCPEND8                     TERMINATE I/O FOR THIS
         SPACE 1
EXCP1    L     R1,INDCB+(DCBDEBAD-IHADCB)   DEB ADDRESS
         LA    R1,16(,R1)                   CURRENT EXTENT POINTER - 16
         SR    R15,R15
EXCP2    LA    R1,16(,R1)                   NEXT EXTENT POINTER
         CLM   R15,B'0001',IOBSEEK          THIS EXTENT?
         LA    R15,1(,R15)
         BNE   EXCP2                        NO, BRANCH
         B     EXCPINIT                     YES, DO THE FIRST READ
         SPACE 2
*    INITIALIZE CCHHR, SECTOR NUMBER AND READ IN THE LENGTH OF RECORD 1
EXCPXTNT SR    R1,R1                        AFTER THE FIRST EXTENT
         IC    R1,IOBSEEK
         A     R1,=F'1'
         STC   R1,IOBSEEK                   INCREMENT EXTENT NUMBER
         CH    R1,NUMEXT                    WITHIN THE EXTENTS?
         BNL   EXCPEND8                     NO, NO FINAL EOF RECORDED
         L     R1,CUREXT
         LA    R1,16(,R1)                   CURRENT EXTENT ENTRY
         MVC   IOBSEEK+3(4),6(R1)           INITIAL CCHH
         MVI   IOBSEEK+7,0                  INITIAL R=0
         SPACE 3
EXCPINIT ST    R1,CUREXT                    UPDATE FOR LATER
         MVI   SNO,0                        SET SECTOR NUMBER=ZERO
         L     R1,ICONTROL                  CURRENT BUFFER START
         MVI   0(R1),X'40'                  MARK AS A NEW ADDRESS
         MVI   4(R1),X'FE'                  INVALIDATE THE EXTENT
         SPACE 1
EXCPREST LA    1,CCW1A
         ST    1,IOBCCW                     CHANNEL PROGRAM ADDRESS
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         MVC   IOBCCW,EXCPCCWI              CHANNEL PROGRAM ADDRESS
         CLI   IOECB,X'7F'                  GOOD READ?
         BNE   EXCPERRA                     NO, INITIAL READ FAILED
         SPACE 2
EXCPCONT TM    ATTNECB,X'40'                ATTENTION SIGNALLED?
         BO    NEWCMD                       YES, BRANCH
         SPACE 1
         MVC   CURMBB(8),IOBSEEK            DISK ADDRESS OF THIS RECORD
         MVC   LS+2(2),DATALN               BLOCK LENGTH FOR THIS READ
         CLI   IOECB,X'42'                  EXTENT VIOLATION LAST TIME?
         BE    EXCPXTNT                     YES, DO THE NEXT EXTENT
         CLI   IOECB,X'7F'                  PREVIOUS READ OK?
         BNE   EXCPREST                     NO, RETRY WITH OTHER EXCP
         SPACE 3
*   CHECK FOR READ MULTIPLE
EXCPEXCP TM    FLAGSEE,FDOUBCON+FFULLTRK    MULTIPLE DESIRED?
         BNZ   EXCPDOUB                     NO, BRANCH
         TM    FLAGSII,FSINGLE              SINGLE BUFFERING REQUIRED?
         BO    EXCPDOUB                     YES, BRANCH
         TM    RECFM,DCBRECTO               TRACK OVERFLOW DATA SET?
         BO    EXCPDOUB                     YES, BRANCH
         L     R1,ICONTROL                  CONTROL WORDS ADDRESS
         CLI   0(R1),X'40'                  DEGRADED BUFFERING?
         BL    EXCPDOUB                     YES, BRANCH
         LH    R14,IOLOGUSE                 COUNT OF SAVED I/O'S
         LA    R14,1(,R14)
         CLI   4(R1),X'FE'                  NEW TTR?
         BE    EXCPTRK                      YES, BRANCH
         L     R15,0(,R1)                   CURRENT DATA ADDRESS
         CLC   4(4,R1),0(R15)               ANY LOGICAL DATA RECORDS?
         BE    EXCPLOG                      YES, BRANCH
         SPACE 1
EXCPTRK  MVI   CCW3,X'5E'              READ MULTIPLE COUNT, KEY, DATA
         L     R14,IOBUFF1                  STANDARD BUFFER ADDRESS
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+8                          NO, BRANCH
         L     R14,IODIR1                   DIRECTORY BUFFER ADDRESS
         STCM  R14,B'0111',CCW3+1           UPDATE READ BUFFER ADDRESS
         ST    R14,EXCPBUFF                 EXCP BUFFER ADDRESS
         SR    R15,R15
         ICM   R15,B'0011',DEVTMAXT         ZERO FILL AND LENGTH
         STCM  R15,B'0011',CCW3+6           UPDATE READ LENGTH
         SPACE 1
         L     R1,ICONTROL                  CONTROL WORDS ADDRESS
         CLI   4(R1),X'FE'                  NEW TTR?
         BNE   EXCPTRK1                     NO, BYPASS MVCL
         SR    R0,R0                        NULL SOURCE
         SR    R1,R1                        NULL SOURCE
         MVCL  R14,R0                       FILL WITH PAD 0 FROM R1
         SPACE 1
EXCPTRK1 SR    R0,R0
         IC    R0,IOBSEEK+7
         BCTR  R0,0
         STC   R0,IOBSEEK+7                 REDUCE IOBSEEK BY ONE
         MVI   SNO,0                        SECTOR ZERO
         MVC   CCW4(8),CCWTIC
         LA    R1,CCW5                      USE A TIC
         STCM  R1,B'0111',CCW4+1
         MVI   IOECB,0                      CLEAR IOECB
         EXCP  IOB                          INITIATE TRACK I/O
         WAIT  ECB=IOECB                    WAIT FOR COMPLETION
         LH    R14,IOPHYTRK
         LA    R14,1(,R14)                  COUNT OF PHYSICAL TRACKS
         STH   R14,IOPHYTRK
         L     R1,ICONTROL                  CONTROL WORDS ADDRESS
         L     R15,EXCPBUFF                 START OF DATA
         ST    R15,0(,R1)                   SAVE FOR LATER
         MVC   4(4,R1),0(R15)               SAVE CCHH OF CCHHRKDL
         LH    R14,IOLOGUSE                 COUNT OF SAVED I/O'S
         MVC   0(1,R1),IOECB                SAVE THE ECB
         MVI   IOECB,X'7F'                  ASSUME GOOD READ
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+10                         NO, BRANCH
         MVC   IODIR1+4+12(LIOBDIR),IOBSEEK SAVE IOBSEEK INFORMATION
         SPACE 1
         CLI   0(R1),X'42'                  NEW EXTENT (NEXT TRACK)?
         BE    EXCPLOG                      YES, BRANCH
         CLI   0(R1),X'7F'                  GOOD READ?
         BNE   EXCPTRK6                     NO, BRANCH
EXCPTRK2 CLC   IOBSEEK+3(4),CURMBB+3        NEXT CCHH EQUAL THIS CCHH?
         BNE   EXCPLOG                      NO, SUCCESSFUL READ
         MVC   IOBSEEK(8),CURMBB            RESTORE THE DISK ADDRESS
         MVI   0(R1),0                      DEGRADED I/O BUFFERING
         L     R14,0(,R1)                   START OF INPUT RECORD
         MVC   IOBSEEK+8(3),5(R14)          RESET KDD FIELDS
         TM    ##ADRPA#,$Q                  QUIET MODE?
         BO    EXCPDOUB                     YES, BRANCH
         TSMSG L893
         B     EXCPDOUB                     DO NORMAL INPUTS
         SPACE 1
EXCPTRK6 MVC   IOBSEEK(8),CURMBB            RESTORE THE DISK ADDRESS
         MVI   0(R1),0                      DEGRADED I/O BUFFERING
         TM    ##ADRPA#,$Q                  QUIET MODE?
         BO    EXCPDOUB                     YES, BRANCH
         TSMSG L892
         B     EXCPDOUB                     DO NORMAL INPUTS
         SPACE 2
EXCPLOG  STH   R14,IOLOGUSE                 COUNT OF SAVED I/O'S
         L     R14,0(,R1)                   START OF NEXT RECORD
         MVC   CURMBB+3(5),0(R14)           CURRENT MBBCCHHR
         SR    R15,R15
         IC    R15,5(,R14)                  KEY LENGTH
         SR    R0,R0
         ICM   R0,B'0011',6(R14)            DATA LENGTH
         ST    R0,LS
         LA    R14,8(R14,R15)               START OF ACTUAL DATA
         ST    R14,EXCPBUFF
         AR    R14,R0
         STCM  R14,B'0111',1(R1)            NEXT DATA ADDRESS
         MVI   IOECB,X'7F'                  ASSUME NORMAL INPUT
         CLC   0(4,R14),4(R1)               SAME TRACK ADDRESS?
         BE    *+10                         YES, BRANCH
         MVC   IOECB(1),0(R1)               RESET THE IOECB
         B     EXCPFIN
         SPACE 3
* CHECK FOR DOUBLE BUFFERING
EXCPDOUB MVC   CCW3(8),CCWREAD              READ KEY AND DATA
         L     R1,IOBUFF1                   STANDARD BUFFER ADDRESS
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+8                          NO, BRANCH
         L     R1,IODIR1                    DIRECTORY BUFFER ADDRESS
         MVC   0(8,R1),IOBSEEK+3            SAVE THE CCHHRKDD DATA
         LA    R1,8(,R1)                    SKIP PAST CCHHRKDD BYTES
         STCM  R1,B'0111',CCW3+1            UPDATE READ BUFFER ADDRESS
         TM    RECFM,DCBRECTO               TRACK OVERFLOW DATA SET?
         BO    EXCPSING                     YES, BRANCH
         TM    FLAGSEE,FDOUBCON+FFULLTRK    CONTROL SINGLE?
         BO    EXCPSING                     YES, BRANCH
         TM    FLAGSII,FSINGLE+FNOTDOUB     THEN/ELSE(DELETE), RESTORE?
         BNZ   EXCPSING                     YES, BRANCH
         MVC   CCW4(8),CCWREAD              INITIALIZE SECOND READ CCW
         MVI   CCW4,X'9E'                   MT READ COUNT, KEY, DATA
         L     R14,IODIR2                   ASSUME DIRECTORY INPUT
         TM    FLAGSII,FDIRGRP              CORRECT?
         BO    *+8                          YES, BRANCH
         L     R14,IOBUFF2                  NO, NORMAL INPUT
         STCM  R14,B'0111',CCW4+1
         SPACE 1
         L     R1,ICONTROL
         CLI   0(R1),X'3F'                  GOOD READ?
         BE    EXCPSEC                      YES, BRANCH
         CLI   0(R1),X'02'                  NEW EXTENT NEXT TIME?
         BE    EXCPSEC                      YES, BRANCH
         SPACE 1
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+10                         NO, BRANCH
         MVC   IODIR1+4+12(LIOBDIR),IOBSEEK SAVE IOBSEEK INFORMATION
         SPACE 1
         L     R1,ICONTROL
         MVC   0(1,R1),IOECB                SAVE THE ECB
         NI    0(R1),FF-X'40'               REDUCE BY X'40'
         MVI   IOECB,X'7F'                  ASSUME A GOOD READ
         L     R14,CCW3                     LOAD FIRST BUFFER ADDRESS
         SH    R14,=H'8'                    BACK UP TO CCHHRKDD
         CLI   0(R1),X'3F'                  GOOD READ?
         BE    EXCPSEC2                     YES, BRANCH
         LA    R15,CCW5                     IF SECOND FAILED
         CLI   0(R1),X'02'                  NEW EXTENT?
         BNE   EXCPDOU2                     NO, BRANCH
         C     R15,IOBCSW                   FAILED ON:
         BH    EXCPXTNT                       FIRST, BRANCH
         BE    EXCPSEC1                       SECOND, MARK NEXT INPUT
         B     EXCPSEC2                       THIRD, MARK AFTER NEXT
         SPACE 1
EXCPDOU2 TM    IOBCSW+4,X'01'               X'41', END OF FILE?
         BNO   EXCPDOU4                     NO, BRANCH
         MVI   0(R1),X'3F'                  YES, READ NEXT RECORD
         C     R15,IOBCSW                   FAILED ON FIRST?
         BNH   EXCPSEC2                     NO, BRANCH
         MVI   0(R1),0                      READ THE NEXT BLOCK
         MVC   IOBSEEK+3(5),0(R14)          CURRENT MBBCCHHR
         SR    R0,R0
         IC    R0,IOBSEEK+7                 R OF MBBCCHHR
         AH    R0,=H'1'                     NEXT R
         STC   R0,IOBSEEK+7                 UPDATE FOR NEXT READ
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   EXCPSEC2                     NO, BRANCH
         MVC   IODIR1+4+12(8),IOBSEEK       SAVE IOBSEEK INFORMATION
         B     EXCPSEC2
         SPACE 1
EXCPDOU4 MVI   0(R1),0                      READ THE NEXT BLOCK
         MVC   IOBSEEK+3(5),0(R14)          CURRENT MBBCCHHR
         SR    R0,R0
         IC    R0,IOBSEEK+7                 R OF MBBCCHHR
         AH    R0,=H'1'                     NEXT R
         STC   R0,IOBSEEK+7                 UPDATE FOR NEXT READ
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+10                         NO, BRANCH
         MVC   IODIR1+4+12(8),IOBSEEK       SAVE IOBSEEK INFORMATION
         SPACE 1
         C     R15,IOBCSW                   FAILED ON FIRST?
         BNH   EXCPSEC2                     NO, BRANCH
         MVI   IOECB,X'41'                  PERMANENT I/O ERROR
         MVC   IOBSEEK(8),CURMBB            RESET THE SEEK ADDRESS
         B     EXCPERR
         SPACE 2
EXCPSEC  LH    R14,IOLOGUSE
         LA    R14,1(,R14)
         STH   R14,IOLOGUSE
         L     R14,CCW4                     LOAD SECOND BUFFER ADDRESS
EXCPSEC1 MVC   IOECB(1),0(R1)               RESET THE ECB
         OI    IOECB,X'40'                  ADD X'40'
         MVI   0(R1),0                      READ THE NEXT BLOCK
         SPACE 1
EXCPSEC2 MVC   CURMBB+3(5),0(R14)           CURRENT MBBCCHHR
         SR    R15,R15
         IC    R15,5(,R14)                  KEY LENGTH
         SR    R0,R0
         ICM   R0,B'0011',6(R14)            DATA LENGTH
         ST    R0,LS
         LA    R14,8(R14,R15)               START OF ACTUAL DATA
         ST    R14,EXCPBUFF
         B     EXCPFIN
         SPACE 3
*  IF ALL ELSE FAILS, TRY SINGLE BUFFERING
EXCPSING MVC   CCW4(8),CCWTIC               USE A TIC
         LA    R1,CCW5
         STCM  R1,B'0111',CCW4+1            WHERE TO TIC
         SPACE 1
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP?
         BNO   *+10                         NO, BRANCH
         MVC   IODIR1+4+12(LIOBDIR),IOBSEEK SAVE IOBSEEK INFORMATION
         SPACE 1
         L     R1,ICONTROL
         MVI   0(R1),0
         L     R14,CCW3                     POINT TO OUTPUT BUFFER
         SH    R14,=H'8'                    BACK UP TO CCHHRKDD
         CLI   IOECB,X'41'                  X'42' OR X'7F'?
         BH    EXCPSIN2                     YES, BRANCH
         TM    IOBCSW+4,X'01'               X'41', END OF FILE?
         BNO   EXCPERR                      NO, BRANCH
         SPACE 1
EXCPSIN2 MVC   CURMBB+3(5),0(R14)           CURRENT MBBCCHHR
         SR    R15,R15
         IC    R15,5(,R14)                  KEY LENGTH
         SR    R0,R0
         ICM   R0,B'0011',6(R14)            DATA LENGTH
         ST    R0,LS
         LA    R14,8(R14,R15)               START OF ACTUAL DATA
         ST    R14,EXCPBUFF
         SPACE 1
         TM    RECFM,DCBRECTO               RECORD OVERFLOW DATA SET?
         BNO   EXCPFIN                      NO, BRANCH
         CLI   IOBSEEK+7,2                  THIS R OF CCHHR=2?
         BNE   EXCPFIN                      NO, BRANCH
         CLI   CURMBB+7,1                   THIS R OF CCHHR=1?
         BE    EXCPFIN                      YES, BRANCH
         SPACE 1
*  TRACK OVERFLOW RECORDS READ OK: HOWEVER, THE LENGTH
*       IS SET TO THE FIRST SEGMENT OF A SPLIT RECORD.
         AR    R14,R0
         ICM   R0,B'0011',6(R14)            LENGTH OF SECOND SEGMENT
         L     R1,LS                        LENGTH OF FIRST SEGMENT
         AR    R1,R0
         ST    R1,LS                        COMBINED RECORD LENGTH
         LA    R1,8(,R14)                   START OF SECOND SEGMENT
         LR    R15,R14                      END OF FIRST SEGMENT
         LR    R14,R0                       LENGTH TO MOVE
         MVCL  R14,R0                       SLIDE DOWN BY 8 BYTES
         L     R0,LS
         SPACE 3
EXCPFIN  LTR   R0,R0                        END OF FILE?
         BZ    EXCPFIN2                     YES, BRANCH
         CLI   IOECB,X'42'                  EXTENT PROBLEM (NEXT TIME)?
         BE    EXCPEND0                     YES, QUIT
EXCPFIN2 TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCPFIN4                     YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGIS               ISAM DATA SET?
         BO    EXCPFIN4                     YES, IGNORE DS1LSTAR
         CLC   CURMBB(8),LSTARMBB           CURRENT:DS1LSTAR
         BH    EXCPEND8                       HIGH, END OF DATA SET
         BL    EXCPFIN4                       LOW, GOOD READ
         TM    DSORG,DS1DSGPO                 EQUAL, PARTITIONED?
         BO    EXCPEND8                              YES, END OF DATA
         SPACE 1
EXCPFIN4 LTR   R0,R0                        END OF MEMBER OR DATA SET?
         BP    EXCPEND0                     NO, BRANCH
         TM    DSORG,DS1DSGPS+DS1DSGDA      DSORG=PS OR DA?
         BM    EXCPEND8                     YES, END OF DATA SET
         TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCPEND8                     YES, END OF DATA SET
         B     EXCPEND4                     END OF MEMBER
         SPACE 3
EXCPERRA SR    R0,R0                        FIRST READ OF EXTENT ERROR
         IC    R0,IOBSEEK+7
         A     R0,=F'1'
         STC   R0,IOBSEEK+7                 ADD 1 TO R OF MBBCCHHR
         TM    ##ADRPA#,$Q                  QUIET MODE I/O?
         BO    EXCPERR+6                    YES, BRANCH
         MESSAGE MSGBLANK
         TSMSG L990                         INITIALIZATION READ FAILED
         B     EXCPERR+6
         SPACE 1
EXCPERR  MVC   IOBSEEK(8),CURMBB            CURRENT MBBCCHHR
         STM   R2,R12,28(R13)               PERMANENT I/O ERROR
         LA    R2,IOBSEEK
         L     R1,INDCB+(DCBDEBAD-IHADCB)   CONVERT MBBCCHHR TO TTR
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         STCM  R0,B'1110',MSGTEXT1+20       TTR OF THIS ERROR
         UNPK  INSERT#1(7),MSGTEXT1+20(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         LA    R15,12                   *** PERMANENT I/O ERROR
         TM    ##ADRPA#,$Q                  QUIET MODE I/O?
         BO    EXCPENDZ                     YES, BRANCH
         MESSAGE MSGBLANK
         MVC   INSERT#1+8(1),MTHIGHL        PRESERVE CURRENT LENGTH
         MVI   MTHIGHL,6                    LENGTH OF THE TTR INSERT
         TSMSG L991$1
         MVC   MTHIGHL(1),INSERT#1+8        RESET MESSAGE LENGTH
         LA    R15,12                   *** PERMANENT I/O ERROR
         B     EXCPENDZ
         SPACE 5
EXCPEND8 LA    R15,8                    *** END OF DATA SET
         B     EXCPENDZ
EXCPEND4 LA    R15,4                    *** END OF MEMBER OR ISAM AREA
         B     EXCPENDZ
EXCPEND0 SR    R15,R15                  *** SUCCESSFUL READ
         L     R0,EXCPBUFF              EXCP BUFFER ADDRESS
EXCPENDZ L     R8,R8EXCP
         L     R14,R14EXCP
         BR    R14
PDS71    CSECT
         TITLE 'P D S  --  PDS CLOSE DCB ROUTINE               6/10/85'
*
*        CLOSE DCB'S
*
CLOSEIT  MVI   FLAGSII,0               CLEAR THE I/O FLAGS
         SPACE 1
         MVI   OPENLIST,0
         MVI   OPENLIST+4,X'80'
         CLOSE (INDCB,,STOWDCB),MF=(E,OPENLIST)
         MVC   BLKSI(2),ZERO           CLEAR THE BLOCKSIZE
         SPACE 1
         BR    R2
         TITLE 'P D S  --  PDS PARSE PROCESSING ROUTINE        6/10/85'
*
*        PARSE INTERFACE SUBROUTINE
*
         SPACE 1
PARSE    CSECT
         USING *,R8
         ST    R8,R8PARSE
         ST    R14,R14PARSE
         LR    R8,R15
         CLC   ##ADRPCL+1(3),$CHA+13    PARSE FOR NEW DATA SET?
         BNE   PARSE2                   NO, BRANCH
         STAX  ,                        YES, CANCEL ANY PREVIOUS STAX
PARSE2   BAL   R2,FREEPDL               FREE ANY PREVIOUS PARSE STORAGE
         SPACE 1
         LA    R15,PARMPARS             AREA FOR PARSE PARAMETERS
         USING PPL,R15                  BASE FOR PARSE PARAMETER LIST
         SPACE 1
         L     R1,ADDRUPT
         L     R2,ADDRECT
         LA    R3,ATTNECB
         L     R4,##ADRPCL
         LA    R5,ADDRANSR
         L     R6,ADDRCBUF
***      LA    R7,MAINSAVE
         SPACE 1
         TM    ##ADRPC#,@S+@G       NON-PARTITIONED AND MEMBER NEEDED?
         BNO   PARSE4               NO, BRANCH
         SPACE 1
*    THE FOLLOWING 6 LINES OF CODE ADD A MEMBER PARAMETER WHEN
*      REQUIRED FOR NON-PARTITIONED DATA SET SUBCOMMAND PARSE.
         LH    R14,2(,R6)           OFFSET FOR OPERANDS
         A     R14,=F'-2'           LESS TWO
         STH   R14,2(,R6)           UPDATE THE OFFSET
         LA    R14,0(R6,R14)        POINT TO THE NEW OPERAND
         MVI   4(R14),C'Z'          DUMMY MEMBER NAME
         MVI   5(R14),X'40'         SEPARATOR CHARACTER
         SPACE 1
PARSE4   STM   R1,R7,PPLUPT         INITIALIZE PPLUPT, PPLECT, PPLECB
*                                   PPLPCL, PPLANS, PPLCBUF AND PPLUWA
         DROP  R15
         LR    R1,R15
         MVI   ATTNECB,1            FLAG -- CAN BE ATTENTIONED
         SPACE 2
         L     R15,ADDRPARS         INVOKE
         BALR  R14,R15                    PARSE
         STC   R15,R14PARSE
         MVI   ATTNECB,0            CLEAR ATTENTION ECB
         EJECT
         L     R6,ADDRANSR             PARSE RESULT BASE REGISTER
         LA    R1,PARSECHK             PARSE CLEANUP ROUTINE
         ST    R1,PARSNEXT             SAVE FOR EXIT VECTOR
         MVC   PARSPOST(6),PARSEXIT    SET UP THE CODE VECTOR
         LTR   R15,R15                 GOOD PARSE?
         BNZ   PARSEXIT                NO, BRANCH
         SPACE 1
         ICM   R8,B'0111',##ADRPAR+1   ANY PROCESSING ROUTINE?
         BNZR  R8                      YES, BRANCH
         SPACE 3
PARSEXIT L     R8,PARSNEXT             LOAD THE REENTRY ADDRESS
         BR    R8                      LINK TO THE ROUTINE
         SPACE 3
         USING *,R8
*** PARSE POST PROCESSING OF OPERANDS IS COMPLETE
PARSECHK SR    R15,R15
         IC    R15,R14PARSE
         SPACE 1
         LA    R14,MAXPARSE         RETURN CODE LIMIT
         CR    R15,R14              RETURN CODE WITHIN LIMITS?
         BH    PARSEBAD             NO, ERROR
         SPACE 1
         LA    R3,4                 OFFSET OF RETURN IF SUCCESSFUL
         B     *+4(R15)             PROCESS RETURN CODE
PARSERET B     PARSEOK                00 - SUCCESSFUL
         B     EXIT8N                 04 - PARSE UNABLE TO PROMPT
         B     PARSERR                08 - USER ENTERED ATTENTION
         B     PARSEBAD               12 - INVALID PARAMETERS
         B     PARSEBAD               16 - PARSE INTERNAL FAILURE
         B     EXIT8N                 20 - VALIDITY CHECK ERROR
MAXPARSE EQU   *-PARSERET
         SPACE 2
PARSEBAD LA    R1,GFPARMS
         ST    R1,GFPARMP           SAVE ADDRESS OF PARMLIST
         L     R0,ADDRFF02          ADDRESS OF IKJEFF02
         STM   R15,R0,GFRCODE       SAVE GFRCODE AND GF02PTR
         LA    R1,GFPARSE
         STH   R1,GFCALLID          SAVE CALLER-ID FOR PARSE
         L     R0,ADDRCPPL          CPPL
         LA    R1,ATTNECB           ATTENTION ECB
         STM   R0,R1,GFCPPLP        SAVE GFCPPLP AND GFECBP
         MVI   ATTNECB,0            CLEAR THE ECB
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)
         CLI   R14PARSE,X'4'        USER ENTER ATTENTION?
         BE    PARSERR              YES, BRANCH -- IGNORE COMMAND
         LTR   R15,R15              ERROR MESSAGE OUTPUT?
         BZ    EXIT8N               YES, BRANCH
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         LA    R1,L834$1
         B     EXIT8M
         SPACE 1
PARSERR  SR    R3,R3                RETURN AT OFFSET 0
         SPACE 1
PARSEOK  BAL   R2,FREEPDL           FREE PARSE STORAGE
         L     R8,R8PARSE           ORIGINAL BASE REGISTER
         L     R14,R14PARSE         EXIT BASE ADDRESS
         B     0(R3,R14)            EXIT FROM PARSE AT +0 OR +4
         USING PDLATTR,R6
         USING *,R8
ZATTRIB  MVC   #ALIAOPT(1),INFOALIA+1     SAVE ALIASINFO/NOALIASINFO
         MVC   #LKEDOPT(1),INFOLKED+1     SAVE LKEDDATE/NOLKEDDATE
         MVC   #RLDERR(1),LKEDRLDF+1      SAVE RLDFIX/NORLDFIX
         MVC   #SSIOPT(1),LKEDSSIK+1      SAVE SSI/NOSSI
         CLI   LKEDNONE+1,X'01'           NONE ATTRIBUTE USED?
         BNE   ZATTR10                    NO, BRANCH
         MVI   #AUTHOPT,2                 SIMULATE "NOAUTH"
         MVI   #PAGEOPT,2                 SIMULATE "NOPAGE"
         MVI   #RMODE,1                   SIMULATE "RMODE24"
         MVI   #AMODE,1                   SIMULATE "AMODE24"
         OI    #ATTRNO,ATTRREUS+ATTRRENT+ATTRLOAD  "NO" REUS/RENT/LOAD
         OI    #ATTRNO+1,ATTRREFR+ATTRNE  "NOREFR" AND "EDIT"
         OI    #ATTRYES,ATTREXEC          "EXEC"
         OI    #ATTRYES+1,ATTRNODC        "NODC"
         SPACE 2
ZATTR10  ICM   R1,B'0001',LKEDAUTH+1    AUTH/NOAUTH
         BZ    *+8
         STCM  R1,B'0001',#AUTHOPT
         ICM   R1,B'0001',LKEDPAGE+1    PAGE/NOPAGE
         BZ    *+8
         STCM  R1,B'0001',#PAGEOPT
         ICM   R1,B'0001',LKEDRMOD+1    RMODE24/RMODEANY
         BZ    *+8
         STCM  R1,B'0001',#RMODE
         ICM   R1,B'0001',LKEDAMOD+1    AMODE24/AMODE31/AMODEANY
         BZ    *+8
         STCM  R1,B'0001',#AMODE
         SPACE 2
ZATTR30  CLI   LKEDREUS+1,X'01'       REUSABLE ATTRIBUTE STATUS:
         BL    ZATTR34                  NONE, BRANCH
         BH    ZATTR32                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRREUS        "REUS"
         NI    #ATTRNO,FF-ATTRREUS      MAKE SURE NEGATIVE IS OFF
         B     ZATTR34
ZATTR32  OI    #ATTRNO,ATTRREUS         "NOREUS"
         SPACE 1
ZATTR34  CLI   LKEDREFR+1,X'01'       REFRESHABLE ATTRIBUTE STATUS:
         BL    ZATTR40                  NONE, BRANCH
         BH    ZATTR36                  "NO" FORM, BRANCH
         OI    #ATTRYES+1,ATTRREFR      "REFR"
         NI    #ATTRNO+1,FF-ATTRREFR    MAKE SURE NEGATIVE IS OFF
         B     ZATTR40
ZATTR36  OI    #ATTRNO+1,ATTRREFR       "NOREFR"
         SPACE 1
ZATTR40  CLI   LKEDRENT+1,X'01'       REENTRANT ATTRIBUTE STATUS:
         BL    ZATTR44                  NONE, BRANCH
         BH    ZATTR42                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRRENT        "RENT"
         NI    #ATTRNO,FF-ATTRRENT      MAKE SURE NEGATIVE IS OFF
         B     ZATTR44
ZATTR42  OI    #ATTRNO,ATTRRENT         "NORENT"
         SPACE 1
ZATTR44  CLI   LKEDEXEC+1,X'01'       EXECUTABLE ATTRIBUTE STATUS:
         BL    ZATTR50                  NONE, BRANCH
         BH    ZATTR46                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTREXEC        "EXEC"
         NI    #ATTRNO,FF-ATTREXEC      MAKE SURE NEGATIVE IS OFF
         B     ZATTR50
ZATTR46  OI    #ATTRNO,ATTREXEC         "NOEXEC"
         SPACE 1
ZATTR50  CLI   LKEDLOAD+1,X'01'       LOADABLE ATTRIBUTE STATUS:
         BL    ZATTR54                  NONE, BRANCH
         BH    ZATTR52                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRLOAD        "LOAD"
         NI    #ATTRNO,FF-ATTRLOAD      MAKE SURE NOLOAD IS OFF
         B     ZATTR54
ZATTR52  OI    #ATTRNO,ATTRLOAD         "NOLOAD"
         SPACE 1
ZATTR54  CLI   LKEDEDIT+1,X'01'       NOT EDITABLE ATTRIBUTE STATUS:
         BL    ZATTR60                  NONE, BRANCH
         BH    ZATTR56                  "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNE        "EDIT"  -- BIT MUST BE REVERSED
         B     ZATTR60
ZATTR56  OI    #ATTRYES+1,ATTRNE        "NOEDIT"
         NI    #ATTRNO+1,FF-ATTRNE     MAKE SURE EDIT IS OFF
         SPACE 1
ZATTR60  CLI   LKEDDC+1,X'01'         DC ATTRIBUTE STATUS:
         BL    ZATTR70                  NONE, BRANCH
         BH    ZATTR62                  "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNODC       "DC"
         NI    #ATTRYES+1,FF-ATTRNODC   MAKE SURE NODC IS OFF
         B     ZATTR70
ZATTR62  OI    #ATTRYES+1,ATTRNODC      "NODC"
         SPACE 1
ZATTR70  TM    LKEDSSI+6,X'80'      STRING CODED?
         BNO   ZATTR80              NO, DONE
         L     R5,LKEDSSI           NEW STRING ADDRESS
         LH    R1,LKEDSSI+4         LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZATTR80              YES, DONE
         MVI   MSGTEXT2+1,4         MAXIMUM OF 4 DIGITS
         MVC   MSGTEXT1,PDS385A     SSI MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     NEWCMD50             ATTENTION -- IGNORE THE REST
         STCM  R0,B'1111',#SSITEXT  SAVE THE RESULTING TTR
         SPACE 2
ZATTR80  MVC   #MODTXT(8),BLANKS      BLANK THE MODULE AREA
         LH    R15,AENTRYS+4          LENGTH OF BLOCK/DUMP STRING
         S     R15,=F'1'              ANY STRING?
         BM    ZATTR99                NO, BRANCH
         MVI   #OPTOPT,X'FF'          SET A FLAG FOR LATER
         L     R1,AENTRYS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         SPACE 2
ZATTR99  OC    #ZAPOPT(14),#ZAPOPT    ANY LINKAGE EDITOR ATTRIBUTES?
         BZ    *+8                    NO, BRANCH
         MVI   #OPTOPT,FF             YES, SET A FLAG FOR LATER
         CLI   #SSIOPT,0              ANY SSI CHANGE?
         BE    *+8                    NO, BRANCH
         OI    #OPTOPT,X'F0'          YES, SET A FLAG FOR LATER
         XC    #ATTRNO(2),=F'-1'      INVERT THE #ATTRNO BITS
         B     PARSPOST
         SPACE 2
         USING PDLCOMPA,R6
         USING *,R8
ZCOMPARE MVC   MSGDS1(64),BLANK128  CLEAR THE DATA SET NAME AREA
         LH    R2,PCLCAR1+4         LENGTH OF STRING
         STH   R2,MSGDS1+62         SAVE LENGTH USED
         BCTR  R2,0                 MACHINE LENGTH
         L     R1,PCLCAR1           NEW STRING ADDRESS
         MVC   MSGDS1(*-*),0(R1)    <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         SPACE 1
         MVC   MSGDS2(64),BLANK128  CLEAR THE DATA SET NAME AREA
         LH    R2,PCLCAR2+4         LENGTH OF STRING
         STH   R2,MSGDS2+62         SAVE LENGTH USED
         BCTR  R2,0                 MACHINE LENGTH
         L     R1,PCLCAR2           NEW STRING ADDRESS
         MVC   MSGDS2(*-*),0(R1)    <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         SPACE 1
         TM    PCLCOSTR+6,X'80'     ANY STRING CODED?
         BNO   PARSPOST             NO, BRANCH
         LH    R2,PCLCOSTR+4        LENGTH OF STRING
         CH    R2,=H'100'           LENGTH(STRING)>=100?
         BL    *+8                  NO, BRANCH
         LH    R2,=H'100'           YES, USE 100 AS A MAXIMUM LENGTH
         STH   R2,#COMMDSZ          SAVE LENGTH USED
         BCTR  R2,0                 MACHINE LENGTH
         MVC   MSGLINE(100),BLANK128  BLANK THE TEXT AREA
         L     R1,PCLCOSTR          NEW STRING ADDRESS
         MVC   MSGLINE(*-*),0(R1)   <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         B     PARSPOST
         SPACE 2
         USING PDLCOMPR,R6
         USING *,R8
ZCOMPRES MVC   #CMXFLAG(1),CMXLIST+1     SAVE SUMMARY/LIST/NOLIST
         MVC   #CMXSHR(1),CMXSHARE+1     SAVE SHR
         B     PARSPOST
         SPACE 2
         USING PDLCONT,R6
         USING *,R8
ZCONTROL MVC   #OUT+0(1),PALINCON+1
         MVC   #OUT+1(1),PTRANCON+1
         MVC   #OUT+2(1),PLKEDCON+1
         MVC   #OUT+3(1),PPROMCON+1
         MVC   #OUT+4(1),PRECVCON+1
         MVC   #OUT+5(1),PDOUBCON+1
         MVC   #OUT+7(1),PIMMCON+1
         MVC   #OUT+10(1),CLOGTYPE+1      SYSOUT/NOSYSOUT/DSNAME/NODSN
         MVC   #OUT+11(1),CLOGDISP+1      OLD/SHR/MOD/NEW
         MVC   #OUT+12(1),CLOGFORM+1      FORM/NOFORM
         MVC   #OUT+13(1),CLOGDEST+1      DEST/NODEST
         MVC   #OUT+30(80),BLANK128       CLEAR THE OPERANDS
         LH    R15,CDESTT+4               LENGTH OF STRING
         STH   R15,#OUT+14                14,15
         S     R15,=F'1'                  ANY DATA?
         BM    ZCONT010                   NO, BRANCH
         L     R1,CDESTT
         MVC   #OUT+30(*-*),0(R1)         <<EXECUTED>> 30-37
         EX    R15,*-6                    MOVE IT IN
         SPACE 1
ZCONT010 LH    R15,CFORMT+4               LENGTH OF STRING
         STH   R15,#OUT+16                16,17
         S     R15,=F'1'                  ANY DATA?
         BM    ZCONT020                   NO, BRANCH
         L     R1,CFORMT
         MVC   #OUT+38(*-*),0(R1)         <<EXECUTED>> 38-41
         EX    R15,*-6                    MOVE IT IN
         SPACE 1
ZCONT020 LH    R15,CSYSOUTT               LENGTH OF STRING
         S     R15,=F'1'                  ANY DATA?
         BM    ZCONT030                   NO, BRANCH
         L     R1,CSYSOUTT
         MVC   CONTSYS(1),0(R1)           <<EXECUTED>>
         SPACE 1
ZCONT030 L     R15,CDSNAMET               ADDRESS OF DSNAME
         LH    R14,CDSNAMET+4             LENGTH OF DSNAME
         STH   R14,#OUT+18                18,19
         S     R14,=F'1'                  ANY DATA?
         BM    PARSPOST                   NO, BRANCH
         MVC   #OUT+44(*-*),0(R15)        <<EXECUTED>> 44-87
         EX    R14,*-6                    MOVE IT IN
         SPACE 1
         TM    CDSNAMET+14,X'80'          ANY MEMBER?
         BZ    ZCONT034                   NO, BRANCH
         L     R15,CDSNAMET+8             ADDRESS OF DSNAME
         LH    R14,CDSNAMET+12            LENGTH OF MEMBER
         STH   R14,#OUT+20                20,21
         S     R14,=F'1'                  ANY DATA?
         BM    PARSPOST                   NO, BRANCH
         MVC   #OUT+88(*-*),0(R15)        <<EXECUTED>> 88-95
         EX    R14,*-6                    MOVE IT IN
         SPACE 1
ZCONT034 TM    CDSNAMET+22,X'80'          ANY PASSWORD?
         BZ    PARSPOST                   NO, BRANCH
         L     R15,CDSNAMET+16            ADDRESS OF DSNAME
         LH    R14,CDSNAMET+20            LENGTH OF MEMBER
         STH   R14,#OUT+22                22,23
         S     R14,=F'1'                  ANY DATA?
         BM    PARSPOST                   NO, BRANCH
         MVC   #OUT+96(*-*),0(R15)        <<EXECUTED>> 96-103
         EX    R14,*-6                    MOVE IT IN
         B     PARSPOST
         SPACE 2
         USING PDLDELET,R6
         USING *,R8
ZDELETE  MVC   #DELFLAG(1),PCLDELAL+1
         B     PARSPOST
         SPACE 2
         USING PDLDSNA,R6
         USING *,R8
ZDSNAMES NI    FLAGSJJ,FF-FDSNTSO-FDSNJCL  CLEAR THE FLAGS
         OC    FLAGSJJ(1),PCLDSN+1         SET THE FALL FLAGS
         B     PARSPOST
         SPACE 2
         USING PDLEDIT,R6
         USING *,R8
ZEDIT    MVC   #SPFOLD(1),SPFNEWOP+1       SAVE ANY OLD/NEW OPTION
         CLI   SPFNEWOP+1,X'02'            "OLD"?
         BNE   *+8                         NO, BRANCH
         OI    ##ADRPC#,@B                 YES, MUST EXIST
         SPACE 1
         TM    SPFMACRO+6,X'80'            ANY MACRO NAME?
         BNO   ZEDIT10                     NO, BRANCH
         LH    R15,SPFMACRO+4              LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    ZEDIT10                     NO, BRANCH
         MVC   #SPFMACR,BLANKS             INITIALIZE THE MACRO NAME
         L     R1,SPFMACRO                 MACRO NAME
         MVC   #SPFMACR(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         SPACE 1
ZEDIT10  TM    SPFPRSET+6,X'80'            ANY SETPROF NAME?
         BNO   ZEDIT20                     NO, BRANCH
         LH    R15,SPFPRSET+4              LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    ZEDIT20                     NO, BRANCH
         L     R1,SPFPRSET                 PROFILE NAME
         MVC   SETPROF(8),BLANKS           INITIALIZE SETPROF
         CLI   0(R1),C'*'                  NULLIFY PARAMETER?
         BE    ZEDIT20                     YES, BRANCH
         MVC   SETPROF(*-*),0(R1)          <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         SPACE 1
ZEDIT20  TM    SPFPROFL+6,X'80'            ANY PROFILE NAME?
         BNO   PARSPOST                    NO, BRANCH
         LH    R15,SPFPROFL+4              LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    PARSPOST                    NO, BRANCH
         MVC   #SPFPROF,BLANKS             INITIALIZE WITH BLANKS
         L     R1,SPFPROFL                 PROFILE NAME
         MVC   #SPFPROF(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         B     PARSPOST
         SPACE 2
         USING PDLFIND,R6
         USING *,R8
ZFIND    TM    FINDSTRG+6,X'80'     STRING CODED?
         BNO   PARSPOST             NO, ASSUME OK
         L     R5,FINDSTRG          NEW STRING ADDRESS
         LH    R2,FINDSTRG+4        LENGTH OF STRING
ZFINTEST LTR   R2,R2                NULL STRING CODED?
         BNP   ZFINCHK              YES, CHECK FORMAT
         BCTR  R5,0                 BACK UP ONE
         OI    0(R5),X'40'          UPPER CASE THE DELIMITER
         CLI   0(R5),C'X'           POSSIBLE HEX STRING?
         BNE   ZFINCHAR             NO, BRANCH
         LA    R1,L841              BAD HEX STRING MESSAGE
         LA    R3,1(,R2)            STRING LENGTH +1
         N     R3,=F'-2'            NEXT EVEN NUMBER
         SR    R15,R15              ZERO
         LA    R4,MSGTEXT2+4        WHERE OUTPUT GOES
         LA    R0,2                 PAIRWISE COUNTER
         CR    R2,R3                EVEN LENGTH ORIGINALLY?
         SRL   R3,1                 (STRING LENGTH +1)/2
         ST    R3,MSGTEXT2          (STRING LENGTH +1)/2
         BNE   ZFINHEX2             NO, BRANCH
         SPACE 1
ZFINHEX1 LA    R5,1(,R5)            NEXT CHARACTER
         OI    0(R5),X'40'          UPPER CASE THE HEX CHARACTER
         IC    R15,0(,R5)           GET A BYTE TO CONVERT
         CLI   0(R5),C'0'
         BL    *+12                 0-9?
         CLI   0(R5),C'9'
         BNH   ZFINHEX2             YES, BRANCH
         LA    R15,9(,R15)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'           BELOW AN A?
         BL    ZFINNEWS             YES, INVALID
         CLI   0(R5),C'F'           ABOVE AN F?
         BH    ZFINNEWS             YES, INVALID
         SPACE 1
ZFINHEX2 SLL   R15,28               SHIFT
         SLDL  R14,4                GET A GOOD DIGIT
         BCT   R0,ZFINHEX1          DO THE NEXT DIGIT OF A PAIR
         LA    R0,2                 PAIRWISE COUNTER
         STC   R14,0(,R4)           SAVE AN OUTPUT CHARACTER
         LA    R4,1(,R4)            ANOTHER OUTPUT CHARACTER
         BCT   R3,ZFINHEX1          DO ALL DIGITS
         SPACE 1
         LA    R5,MSGTEXT2+4-1      START OF CONVERTED STRING -1
         L     R2,MSGTEXT2          LENGTH OF CONVERTED STRING
         CH    R2,=H'32'            VALID LENGTH?
         BH    ZFINNEWS             NO, BRANCH
         STH   R2,FINDLTH           SAVE ACTUAL LENGTH
         BCTR  R2,0                 MACHINE LENGTH
         MVC   FINDSTR(*-*),1(R5)   <<EXECUTED>>
         EX    R2,*-6               SAVE THE NEW STRING
         B     ZFINCHK
         SPACE 2
ZFINCHAR LA    R1,L842              INVALID LENGTH MESSAGE
         CH    R2,=H'32'            VALID LENGTH?
         BH    ZFINNEWS             NO, BRANCH
         STH   R2,FINDLTH           SAVE ACTUAL LENGTH
         BCTR  R2,0                 MACHINE LENGTH
         MVC   FINDSTR(*-*),1(R5)   <<EXECUTED>>
         EX    R2,*-6               SAVE THE NEW STRING
         CLI   FINDASIS+1,1            ASIS REQUESTED?
         BE    ZFINCHK                 YES, BRANCH
         OC    FINDSTR(32),BLANK128    NO, UPPER CASE THE DATA
         B     ZFINCHK
         SPACE 2
ZFINNEWS TSMSG (R1)                 INVALID LENGTH STRING -- REENTER
ZFINNEW0 LA    R2,NEWCMD            EXIT ADDRESS FOR ATTENTION
        $PUTGET PDS380A,ATTN=NEWCMD50  SECOND PART OF MESSAGE
         L     R5,ADDRCBUF          ENTERED DATA
         LH    R2,0(,R5)            LENGTH OF STRING
         SH    R2,=H'5'             VALID MACHINE LENGTH?
         LA    R1,4(R5,R2)          LAST BYTE OF INPUT STRING
         BZ    ZFINNEW3             MAYBE, CHECK FURTHER
         BNP   ZFINNEW0             NO, NULL STRING -- PROMPT AGAIN
         SPACE 1
ZFINNEW1 CLI   0(R1),X'40'          SCAN
         BNE   ZFINNEW2                 BACKWARDS
         BCTR  R1,0                              FOR A
         BCT   R2,ZFINNEW1                             NON-BLANK
         B     ZFINNEW3             POSSIBLE STRING -- CHECK
         SPACE 1
ZFINNEW2 CLI   4(R5),X'40'          SCAN
         BNE   ZFINNEW3                 FORWARDS
         LA    R5,1(,R5)                        FOR A
         BCT   R2,ZFINNEW2                            NON-BLANK
         SPACE 1
ZFINNEW3 CLI   4(R5),X'40'          REAL STRING?
         BE    ZFINNEW0             NO, PROMPT AGAIN
         CLI   FINDASIS+1,1         ASIS REQUESTED?
         BE    ZFINNEW4             YES, BRANCH
         OC    4(*-*,R5),BLANK128   <<EXECUTED>>
         EX    R2,*-6               UPPER-CASE DATA AND DELIMITERS
         SPACE 1
ZFINNEW4 OI    4(R5),X'40'          UPPER-CASE THE FIRST DELIMITER
         MVC   0(1,R5),0(R1)        ENDING CHARACTER
         OI    0(R5),X'40'          UPPER-CASE THE LAST DELIMITER
         CLC   0(1,R5),4(R5)        SAME DELIMITERS?
         BNE   *+6                  NO, BRANCH
         BCTR  R2,0                 YES, ALLOW FOR LAST DELIMITER
         LA    R5,5(,R5)            FIRST CHARACTER OF STRING
         B     ZFINTEST             PARSE THIS NEW STRING
         SPACE 2
ZFINCHK  CLI   FINDLIST+1,1           FINDLIST SPECIFIED?
         BNE   ZFINCHK1               NO, BRANCH
         OI    ##ADRPA#,$F            YES, TURN ON THE FLAG
         SPACE 1
ZFINCHK1 CLI   FINDFMT+1,0            WAS A FORMAT SPECIFIED?
         BE    ZFINCHK2               NO, BRANCH
         NI    HFLAGSDD,FF-NONUM-SNUM-BLOCK-DUMP-LBLOCK-LDUMP
         LH    R15,FINDFMT            FORMAT REQUESTED
         A     R15,=F'-2'             LESS 2 -- NEGATIVE?
         BM    ZFINCHK2               YES, BRANCH
         LA    R1,1                   SHIFT VALUE
         SLL   R1,0(R15)              SHIFT INTO POSITION
         OI    HFLAGSDD,*-*           <<EXECUTED>>
         EX    R1,*-4                 MOVE INTO FLAG BYTE
         SPACE 1
ZFINCHK2 LA    R1,FMAXLENC            MAXLEN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXLEN             SAVE RESULT
         SPACE 1
         LA    R1,FMAXINC             MAXIN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXIN              SAVE RESULT
         SPACE 1
         LA    R1,FMAXOUTC            MAXOUT
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXOUT             SAVE RESULT
         SPACE 1
         LA    R1,FMAXFIND            MAXFIND
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXFIND            SAVE RESULT
         SPACE 1
         LA    R1,FSKIPRC             SKIPREC
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPREC            SAVE RESULT
         SPACE 1
         LA    R1,FSKIPCC             SKIPCOL
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPCOL            SAVE RESULT
         SPACE 1
         LH    R1,FTHENCC             THEN ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         THEN ACTION POINTER
         ST    R1,#ACTIONT            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONT    SET POSSIBLE FSINGLE FLAG
         OC    SPFLAG3(1),#ACTIONT    SET POSSIBLE INVALID    ABL OCT84
         SPACE 1
ZFINCHK3 LH    R1,FELSECC             ELSE ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         ELSE ACTION POINTER
         ST    R1,#ACTIONE            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONE    SET POSSIBLE FSINGLE FLAG
         OC    SPFLAG3(1),#ACTIONE    SET POSSIBLE INVALID    ABL OCT84
         SPACE 1
         TM    FINDOFF+6,X'80'        OFFSET CODED?
         BNO   ZFINCHK4               NO, DONE
         L     R5,FINDOFF             OFFSET ADDRESS
         LH    R1,FINDOFF+4           LENGTH OF OFFSET
         LTR   R1,R1                  NULL STRING CODED?
         BNP   ZFINCHK4               YES, DONE
         MVI   MSGTEXT2+1,3           MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,PDS384A       OFFSET INVALID MESSAGE
         BAL   R2,HEXC                CONVERT TO BINARY
         B     NEWCMD50               ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#LISTOFF+1  SAVE THE RESULTING OFFSET
         SPACE 1
ZFINCHK4 LH    R15,FDUMPCS+4          LENGTH OF BLOCK/DUMP STRING
         STC   R15,#MODLEN
         S     R15,=F'1'              ANY STRING?
         BM    PARSPOST               NO, BRANCH
         L     R1,FDUMPCS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         B     PARSPOST
         SPACE 2
         USING PDLFIX,R6
         USING *,R8
ZFIXPDS  MVC   #FIXOPT(1),PCLFIXOP+1  SAVE FIXPDS MAIN KEYWORD OPTION
         MVC   #FIXOPT2(1),PCLRELOP+1 SAVE FIXPDS RELEASE/RELSAVE/MAXSP
         MVC   #OPTCD(1),ROPTCD+1     SAVE OPTCD/NOOPTCD
         SPACE 1
         SR    R1,R1
         ICM   R1,B'0001',RTYPE+1   ANY GENERAL DATA TYPE?
         BZ    ZFIX010              NO, BRANCH
         MH    R1,=H'22'            INDEX VALUE
         LA    R3,RTABLE-22-22(R1)  CURRENT ENTRY -22
         MVC   #RECFM+3(1),22+1(R3) RECFM
         MVC   #LRECL+2(2),22+2(R3) LRECL
         IC    R1,BYTEUCB+1         DEVICE INDEX
         LA    R0,X'0000000E'       MAXIMUM DEFINED INDEX
         CR    R1,R0                ABOVE 3380?
         BNH   *+6                  NO, BRANCH
         LR    R1,R0                YES, USE 3380 DATA
         AR    R1,R1                DEVICE INDEX * 2 (FOR TABLE ENTRY)
         LA    R3,22+4-12(R3,R1)    POINT TO DEVICE BLOCKSIZE
         MVC   #BLKSI+2(2),0(R3)    BLKSIZE
         SPACE 1
ZFIX010  LH    R1,RRECFMZI          RECORD FORMAT INDEX (0 IF NONE)
         LA    R1,RECFMVAL(R1)      INDEX TO CORRESPONDING DCBRECFM
         CLI   0(R1),X'00'          DEFAULT RECFM VALUE?
         BE    *+10                 YES, BRANCH
         MVC   #RECFM+3(1),0(R1)    NO, NEW RECORD FORMAT
         SPACE 1
         LH    R1,ROPTCDZI          OPTCD VALUE (0 IF NONE)
         LA    R1,OPTCDVAL(R1)      INDEX TO CORRESPONDING DCBOPTCD
         CLI   0(R1),X'00'          DEFAULT RECFM VALUE?
         BE    *+10                 YES, BRANCH
         MVC   #OPTCD+3(1),0(R1)    NO, NEW OPTCD VALUE
         SPACE 1
         LA    R1,RDIRZI            NUMBER OF DIRECTORY BLOCKS
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#NEWDIR           SAVE RESULT IF CODED
         SPACE 1
         LA    R1,RLRECLZI          LRECL DESIRED
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#LRECL            SAVE RESULT IF CODED
         SPACE 1
         LA    R1,RBLKSIZI          BLOCKSIZE DESIRED
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#BLKSI            SAVE RESULT IF CODED
         SPACE 1
         LA    R1,RRESV             RESERVE COUNT
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#FIXRES           SAVE RESULT IF CODED
         B     PARSPOST
         SPACE 2
******           23051 23052  2314  3330  3340  3350  3375 33301  3380
******           ***** *****  ****  ****  ****  ****  **** *****  ****
RTABLE   DC  XL2'C0',AL2(0)      ...LOAD
         DC  AL2(14136,14660,+7294,13030,+8368,19069,19069,13030,19069)
         DC  XL2'90',AL2(80)     ...OBJECT
         DC  AL2(+3200,+2720,+2240,+3120,+2640,+2960,+3200,+3120,+3120)
         DC  XL2'50',AL2(255)    ...CLIST
         DC  AL2(+4424,+4754,+2296,+4252,+1964,+6232,+6816,+4252,+6356)
         DC  XL2'90',AL2(80)     ...CNTL
         DC  AL2(+6800,+7200,+3520,+6400,+4080,+6160,+6800,+6400,+6320)
         DC  XL2'90',AL2(80)     ...ASM
         DC  AL2(+6800,+7200,+3520,+6400,+4080,+6160,+6800,+6400,+6320)
         SPACE 2
RECFMVAL DC    X'00',X'80',X'90',X'A0',X'B0',X'84',X'94',X'A4',X'B4'
         DC    X'82',X'92',X'A2',X'B2'  RECFM(F) THROUGH RECFM(FBTM)
         DC          X'40',X'50',X'60',X'70',X'44',X'54',X'64',X'74'
         DC    X'42',X'52',X'62',X'72'  RECFM(V) THROUGH RECFM(VBTM)
         DC          X'C0',X'E0',X'C4',X'C2',X'E4',X'E2'
         SPACE 2
OPTCDVAL DC    X'00',X'80',X'A0',X'84',X'A4',X'A4'
         DC          X'20',X'24',X'A0',X'A4',X'A4'
         DC          X'04',X'24',X'84',X'A4',X'A4'
         SPACE 2
         DS    0H
         USING PDLHIST,R6
         USING *,R8
ZHISTORY MVC   #ZAPOPT(1),PCLHZAPK+1    SAVE THE ZAP OPTION
         MVC   #USEROPT(1),PCLHUSRK+1   SAVE THE USER OPTION
         MVC   #TRANOPT(1),PCLHTRNK+1   SAVE THE TRAN OPTION
         MVC   #LKEDOPT(1),PCLHLKDK+1   SAVE THE LKED OPTION
         SPACE 1
         TM    PCLHMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   ZHIST10                NO, BRANCH
         LH    R15,PCLHMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST10                NO, BRANCH
         L     R1,PCLHMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 3
ZHIST10  TM    PCLHTRN+6,X'80'        ANY TRANSLATOR NAME FILTERING?
         BNO   ZHIST20                NO, BRANCH
         LH    R15,PCLHTRN+4          LENGTH OF NAME
         STC   R15,#TRANLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST20                NO, BRANCH
         L     R1,PCLHTRN             PARTIAL TRANSLATOR NAME
         MVC   #TRANTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST20  TM    PCLHZAP+6,X'80'        ANY ZAP IDRDATA NAME FILTERING?
         BNO   ZHIST30                NO, BRANCH
         LH    R15,PCLHZAP+4          LENGTH OF NAME
         STC   R15,#ZAPLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST30                NO, BRANCH
         L     R1,PCLHZAP             PARTIAL IDRDATA DATA
         MVC   #ZAPTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST30  TM    PCLHUSR+6,X'80'        ANY USER IDRDATA FILTERING?
         BNO   ZHIST40                NO, BRANCH
         LH    R15,PCLHUSR+4          LENGTH OF NAME
         STC   R15,#USERLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST40                NO, BRANCH
         L     R1,PCLHUSR             PARTIAL IDRDATA DATA
         MVC   #USERTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST40  TM    PCLHLKD+6,X'80'        ANY LKED IDRDATA FILTERING?
         BNO   PARSPOST               NO, BRANCH
         LH    R15,PCLHLKD+4          LENGTH OF NAME
         STC   R15,#LKEDLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    PARSPOST               NO, BRANCH
         L     R1,PCLHLKD             PARTIAL IDRDATA DATA
         MVC   #LKEDTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         B     PARSPOST
         SPACE 2
         USING PDLIFTTR,R6
         USING *,R8
ZIF      MVC   #ALIAOPT(1),IFLKALIA+1   ALIAS/NOALIAS
         MVC   #SSIOPT(1),IFLKSSI+1     SSI/NOSSI
         MVC   #AUTHOPT(1),IFLKAUTH+1   AUTH/NOAUTH
         MVC   #PAGEOPT(1),IFLKPAGE+1   PAGE/NOPAGE
         MVC   #VSLKED(1),IFLKVSLE+1    VSLKED/NOVSLKED
         MVC   #RMODE(1),IFRMODE+1      RMODE24/RMODEANY
         MVC   #AMODE(1),IFAMODE+1      AMODE24/AMODE31/AMODEANY/NOA...
         MVC   #USERIDO(1),IFTUSERK+1   USERID/NOUSERID/NOTUSERID
         MVC   #LOADERR(1),IFLOADER+1   LOADERR/NOLOADERR
         MVC   #LKEDERR(1),IFLKEDER+1   LKEDERR/NOLKEDERR
         MVC   #IOERROR(1),IFIOERR+1    IOERR/NOIOERR
         MVC   #BLOCKER(1),IFBLKERR+1   BLOCKERR/NOBLOCKERR
         MVC   #LRECLER(1),IFLRECLE+1   LRECLERR/NOLRECLERR
         MVC   #NULL(1),IFNULL+1        NULL/NONULL
         MVC   #AALIAS(1),IFAALIAS+1    APPARENTALIAS/NOAPPARENTALIAS
         MVC   #ORPHAN(1),IFORPHAN+1    ORPHAN/NOORPHAN
         MVC   #SPFEDIT(1),IFSPFEDT+1   SPFEDIT/NOSPFEDIT
         MVC   #NAMEERR(1),IFNAMERR+1   NAMEERR/NONAMEERR
         MVC   #HASALIA(1),IFHASALI+1   HASALIAS/NOHASALIAS
         MVC   #ATTRHI(3),=F'-1'        INITIALIZE THE SECOND TTR VALUE
         SPACE 1
ZIF380   CLI   IFLKREUS+1,X'01'       REUSABLE ATTRIBUTE STATUS:
         BL    ZIF384                   NONE, BRANCH
         BH    ZIF382                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRREUS        "REUS"
         B     ZIF384
ZIF382   OI    #ATTRNO,ATTRREUS         "NOREUS"
         SPACE 1
ZIF384   CLI   IFLKREFR+1,X'01'       REFRESHABLE ATTRIBUTE STATUS:
         BL    ZIF400                   NONE, BRANCH
         BH    ZIF386                   "NO" FORM, BRANCH
         OI    #ATTRYES+1,ATTRREFR      "REFR"
         B     ZIF400
ZIF386   OI    #ATTRNO+1,ATTRREFR       "NOREFR"
         SPACE 1
ZIF400   CLI   IFLKRENT+1,X'01'       REENTRANT ATTRIBUTE STATUS:
         BL    ZIF404                   NONE, BRANCH
         BH    ZIF402                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRRENT        "RENT"
         B     ZIF404
ZIF402   OI    #ATTRNO,ATTRRENT         "NORENT"
         SPACE 1
ZIF404   CLI   IFLKEXEC+1,X'01'       EXECUTABLE ATTRIBUTE STATUS:
         BL    ZIF420                   NONE, BRANCH
         BH    ZIF406                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTREXEC        "EXEC"
         B     ZIF420
ZIF406   OI    #ATTRNO,ATTREXEC         "NOEXEC"
         SPACE 1
ZIF420   CLI   IFLKLOAD+1,X'01'       LOADABLE ATTRIBUTE STATUS:
         BL    ZIF424                   NONE, BRANCH
         BH    ZIF422                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRLOAD        "LOAD"
         B     ZIF424
ZIF422   OI    #ATTRNO,ATTRLOAD         "NOLOAD"
         SPACE 1
ZIF424   CLI   IFLKEDIT+1,X'01'       NOT EDITABLE ATTRIBUTE STATUS:
         BL    ZIF440                   NONE, BRANCH
         BH    ZIF426                   "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNE        "EDIT"  -- BIT MUST BE REVERSED
         B     ZIF440
ZIF426   OI    #ATTRYES+1,ATTRNE        "NOEDIT"
         SPACE 1
ZIF440   CLI   IFLKLKED+1,X'01'       LINKAGE EDITOR ATTRIBUTE:
         BL    ZIF444                   NEITHER, BRANCH
         BH    ZIF442                   NULL FORM, BRANCH
         OI    #ATTRYES+1,ATTRFLEV      "FLEVEL"
         B     ZIF444
ZIF442   OI    #ATTRNO+1,ATTRFLEV       "NOFLEVEL"
         SPACE 1
ZIF444   CLI   IFLKOVER+1,X'01'       OVERLAY ATTRIBUTE STATUS:
         BL    ZIF460                   NONE, BRANCH
         BH    ZIF446                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTROVLY        "OVERLAY"
         B     ZIF460
ZIF446   OI    #ATTRNO,ATTROVLY         "NOOVERLAY"
         SPACE 1
ZIF460   CLI   IFLKTEST+1,X'01'       TEST ATTRIBUTE STATUS:
         BL    ZIF464                   NONE, BRANCH
         BH    ZIF462                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRTEST        "TEST"
         B     ZIF464
ZIF462   OI    #ATTRNO,ATTRTEST         "NOTEST"
         SPACE 1
ZIF464   CLI   IFLKSCTR+1,X'01'       SCATTER ATTRIBUTE STATUS:
         BL    ZIF480                   NONE, BRANCH
         BH    ZIF466                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRSCTR        "SCTR"
         B     ZIF480
ZIF466   OI    #ATTRNO,ATTRSCTR         "NOSCTR"
         SPACE 1
ZIF480   CLI   IFLKDC+1,X'01'         DOWNWARD COMPATIBLE ATTRIBUTE:
         BL    ZIF484                   NONE, BRANCH
         BH    ZIF482                   "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNODC       "DC" -- BIT IS REVERSED
         B     ZIF484
ZIF482   OI    #ATTRYES+1,ATTRNODC      "NODC"
         SPACE 1
ZIF484   TM    IFTUSER+6,X'80'        ANY USERID NAME FILTERING?
         BNO   ZIF500                 NO, BRANCH
         LH    R15,IFTUSER+4          LENGTH OF USERID NAME
         STC   R15,#USERIDL           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF500                 NO, BRANCH
         L     R1,IFTUSER             PARTIAL USERID NAME
         MVC   #USERTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF500   MVC   #ZAPOPT(1),IFTLZAPK+1   SAVE ZAP/NOZAP
         MVC   #USEROPT(1),IFTLUSRK+1  SAVE USER/NOUSER
         MVC   #TRANOPT(1),IFTLTRNK+1  SAVE TRAN/NOTRAN
         MVC   #LKEDOPT(1),IFTLKEDK+1  SAVE LKED/NOLKED
         MVC   #MODOPT(1),IFTLMODK+1   SAVE MODULE/NOMODULE
         MVC   #RLDERR(1),IFRLDERR+1   SAVE RLDERR/NORLDERR
         MVC   #RLD0(1),IFRLD0+1       SAVE RLDZERO/NORLDZERO
         MVC   #APFERR(1),IFAPFERR+1   SAVE APFERR/NOAPFERR
         SPACE 1
         TM    IFTLKED+6,X'80'        ANY LINKAGE EDITOR FILTERING?
         BNO   ZIF502                 NO, BRANCH
         LH    R15,IFTLKED+4          LENGTH OF LKED NAME
         STC   R15,#LKEDLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF502                 NO, BRANCH
         L     R1,IFTLKED             PARTIAL MODULE NAME
         MVC   #LKEDTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF502   TM    IFTLMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   ZIF504                 NO, BRANCH
         LH    R15,IFTLMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF504                 NO, BRANCH
         L     R1,IFTLMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF504   TM    IFTLTRN+6,X'80'        ANY TRANSLATOR NAME FILTERING?
         BNO   ZIF506                 NO, BRANCH
         LH    R15,IFTLTRN+4          LENGTH OF NAME
         STC   R15,#TRANLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF506                 NO, BRANCH
         L     R1,IFTLTRN             PARTIAL TRANSLATOR NAME
         MVC   #TRANTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF506   TM    IFTLZAP+6,X'80'        ANY ZAP IDRDATA NAME FILTERING?
         BNO   ZIF508                 NO, BRANCH
         LH    R15,IFTLZAP+4          LENGTH OF NAME
         STC   R15,#ZAPLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF508                 NO, BRANCH
         L     R1,IFTLZAP             PARTIAL IDRDATA DATA
         MVC   #ZAPTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF508   TM    IFTLUSR+6,X'80'        ANY USER IDRDATA FILTERING?
         BNO   ZIF540                 NO, BRANCH
         LH    R15,IFTLUSR+4          LENGTH OF NAME
         STC   R15,#USERLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF540                 NO, BRANCH
         L     R1,IFTLUSR             PARTIAL IDRDATA DATA
         MVC   #USERTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 3
ZIF540   TM    IFTTTR+6,X'80'       STRING CODED?
         BNO   ZIF560               NO, DONE
         L     R5,IFTTTR            NEW STRING ADDRESS
         LH    R1,IFTTTR+4          LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZIF560               YES, DONE
         MVI   MSGTEXT2+1,3         MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,PDS382A     FIRST TTR MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     NEWCMD50             ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#ATTRLO   SAVE THE RESULTING TTR
         SPACE 2
         TM    IFTTTR+8+6,X'80'     STRING CODED?
         BNO   ZIF560               NO, DONE
         L     R5,IFTTTR+8          NEW STRING ADDRESS
         LH    R1,IFTTTR+8+4        LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZIF560               YES, DONE
         MVC   MSGTEXT1,PDS383A     SECOND TTR MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     NEWCMD50             ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#ATTRHI   SAVE THE RESULTING TTR
         SPACE 2
ZIF560   MVI   #OPTOPT,X'80'          PARSE WAS CALLED FOR IF
         LA    R1,IFABOSUB            ABOVE(COUNT)
         BAL   R14,PACKMAX            ANY RESULT?
         B     ZIF600                 YES, BRANCH
         B     ZIF620                 NO, BRANCH
ZIF600   ST    R1,#IFABOVE            SAVE RESULT
         OI    #IFABOVE,X'80'         MARK AS ENTERED
         SPACE 1
ZIF620   LA    R1,IFBELSUB            BELOW(COUNT)
         BAL   R14,PACKMAX            ANY RESULT?
         B     ZIF640                 YES, BRANCH
         B     ZIF700                 NO, BRANCH
ZIF640   ST    R1,#IFBELOW            SAVE RESULT
         OI    #IFBELOW,X'80'         MARK AS ENTERED
         SPACE 1
ZIF700   LA    R1,IFMODSUB              CHANGED(DATE:
         BAL   R14,PACKMAX              ANY RESULT?
         B     ZIF720                   YES, BRANCH
         B     ZIF740                   NO, BRANCH
ZIF720   MVC   #IFMODIF(3),DOUBLE+5     SAVE RESULT
         MVC   #IFMODIF+3(3),DOUBLE+5   IF SECOND IS MISSING, USE FIRST
         SPACE 1
ZIF740   LA    R1,IFMODSUB+8            CHANGED(:DATE
         BAL   R14,PACKMAX              ANY RESULT?
         B     ZIF760                   YES, BRANCH
         B     ZIF800                   NO, BRANCH
ZIF760   MVC   #IFMODIF+3(3),DOUBLE+5   SAVE RESULT
         OI    #IFMODIF+2,X'0F'         IF FIRST IS MISSING, USE 00000F
         SPACE 1
ZIF800   LA    R1,IFCRESUB              CREATED(DATE:
         BAL   R14,PACKMAX              ANY RESULT?
         B     ZIF820                   YES, BRANCH
         B     ZIF840                   NO, BRANCH
ZIF820   MVC   #IFCREIF(3),DOUBLE+5     SAVE RESULT
         MVC   #IFCREIF+3(3),DOUBLE+5   IF SECOND IS MISSING, USE FIRST
         SPACE 1
ZIF840   LA    R1,IFCRESUB+8            CREATED(:DATE
         BAL   R14,PACKMAX              ANY RESULT?
         B     ZIF860                   YES, BRANCH
         B     ZIF900                   NO, BRANCH
ZIF860   MVC   #IFCREIF+3(3),DOUBLE+5   SAVE RESULT
         OI    #IFCREIF+2,X'0F'         IF FIRST IS MISSING, USE 00000F
         SPACE 1
ZIF900   TM    IFTSSI+6,X'80'       STRING CODED?
         BNO   ZIF980               NO, DONE
         L     R5,IFTSSI            NEW STRING ADDRESS
         LH    R1,IFTSSI+4          LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZIF980               YES, DONE
         MVI   MSGTEXT2+1,4         MAXIMUM OF 4 DIGITS
         MVC   MSGTEXT1,PDS385A     SSI MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     NEWCMD50             ATTENTION -- IGNORE THE REST
         STCM  R0,B'1111',#SSITEXT  SAVE THE RESULTING TTR
         SPACE 2
ZIF980   TM    FLAGSCC,RECFMU         LOAD MODULE?
         BNO   ZIF990                 NO, BRANCH
         OC    #IFCREIF(6),#IFCREIF   ANY CREATION DATE CHECK?
         BZ    ZIF984                 NO, BRANCH
         OI    #OPTOPT,X'02'          YES, SET A FLAG FOR LATER
         MVC   FULLWORD(3),#IFCREIF   FIRST FIELD
         LA    R1,FULLWORD
         LA    R15,#IFCREIF
         BAL   R14,CONVDATE           CONVERT YYDDDF TO MMDDYY
         ICM   R1,B'0100',#IFCREIF+2  GET YY
         ICM   R1,B'0011',#IFCREIF    GET MMDD
         STCM  R1,B'0111',#IFCREIF    SAVE AS YYMMDD
         MVC   FULLWORD(3),#IFCREIF+3 GET SECOND FIELD
         LA    R1,FULLWORD
         LA    R15,#IFCREIF+3
         BAL   R14,CONVDATE           CONVERT YYDDDF TO MMDDYY
         ICM   R1,B'0100',#IFCREIF+5  GET YY
         ICM   R1,B'0011',#IFCREIF+3  GET MMDD
         STCM  R1,B'0111',#IFCREIF+3  SAVE AS YYMMDD
         SPACE 1
ZIF984   OC    #IFMODIF(6),#IFMODIF   ANY MODIFICATION DATE CHECK?
         BZ    ZIF990                 NO, BRANCH
         OI    #OPTOPT,X'02'          YES, SET A FLAG FOR LATER
         MVC   FULLWORD(3),#IFMODIF   FIRST FIELD
         LA    R1,FULLWORD
         LA    R15,#IFMODIF
         BAL   R14,CONVDATE           CONVERT YYDDDF TO MMDDYY
         ICM   R1,B'0100',#IFMODIF+2  GET YY
         ICM   R1,B'0011',#IFMODIF    GET MMDD
         STCM  R1,B'0111',#IFMODIF    SAVE AS YYMMDD
         MVC   FULLWORD(3),#IFMODIF+3 SECOND FIELD
         LA    R1,FULLWORD
         LA    R15,#IFMODIF+3
         BAL   R14,CONVDATE           CONVERT YYDDDF TO MMDDYY
         ICM   R1,B'0100',#IFMODIF+5  GET YY
         ICM   R1,B'0011',#IFMODIF+3  GET MMDD
         STCM  R1,B'0111',#IFMODIF+3  SAVE AS YYMMDD
         SPACE 1
ZIF990   L     R6,ADDRANSR            RESET THE PDL BASE ADDRESS
         OC    #ATTRYES(13),#ATTRYES  ANY LINKAGE EDITOR ATTRIBUTES?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'01'          YES, SET A FLAG FOR LATER
         OC    #ZAPOPT(6),#ZAPOPT     ZAP/USER/TRAN/LKED/MODULE/RLDERR?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'02'+X'01'    YES, SET A FLAG FOR LATER
         OC    #IOERROR(4),#IOERROR   IOERROR/BLOCKERR/LRECLERR/NULL?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'04'          YES, SET A FLAG FOR LATER
         SPACE 1
         LH    R1,IFTHENCC            THEN ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         THEN ACTION POINTER
         ST    R1,#ACTIONT            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONT    SET POSSIBLE FSINGLE FLAG
         OC    SPFLAG3(1),#ACTIONT    SET POSSIBLE INVALID    ABL OCT84
         SPACE 1
         LH    R1,IFELSECC            ELSE ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         ELSE ACTION POINTER
         ST    R1,#ACTIONE            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONE    SET POSSIBLE FSINGLE FLAG
         OC    SPFLAG3(1),#ACTIONE    SET POSSIBLE INVALID    ABL OCT84
         B     PARSPOST
         SPACE 2
THENTBL  DC    A(0)                        NO ASSOCIATED THEN/ELSE
&NATT    COND  DC,A($ATT)                  ATTRIB
&NBRO    COND  DC,A($BRO)                  BROWSE
&NDIR    COND  DC,A($DIR)                  DIRECTORY
     AIF ('&NDEL' EQ '').NDEL120
         DC    AL1(FSINGLE),AL3($DEL)      DELETE
.NDEL120 ANOP
&NEDI    COND  DC,A($EDI)                  EDIT
&NEND    COND  DC,A($END)                  END
&NFIN    COND  DC,A($FIN)                  FIND
&NFSE    COND  DC,A($FSE)                  FSE
&NHIS    COND  DC,A($HIS)                  HISTORY
&NLIS    COND  DC,A($LIS)                  LIST
&NMAP    COND  DC,A($MAP)                  MAP
     AIF ('&NMEM' EQ '').NMEM120
         DC    AL1(FMEMBERS),AL3($MEM)     MEMBERS
.NMEM120 ANOP
&NMML    COND  DC,A($MML)                  MEMLIST
&NOUT    COND  DC,A($OUT)                  OUTCOPY
&NPRI    COND  DC,A($PRI)                  PRINTOFF
&NREV    COND  DC,A($REV)                  REVIEW
&NSPF    COND  DC,A($SPF)                  SPFEDIT
&NSUB    COND  DC,A($SUB)                  SUBMIT
&NSUL    COND  DC,A($SUL)                  SUBLIST
&NTSE    COND  DC,A($TSE)                  TSOEDIT
&NTSL    COND  DC,A($TSL)                  TSOLIST
&NVER    COND  DC,A($VER)                  VERIFY
         SPACE 2
         USING PDLISPF,R6
         USING *,R8
ZISPF    TM    PCLISSTR+6,X'80'     STRING CODED?
         BNO   PARSPOST             NO, DONE
         LH    R2,PCLISSTR+4        LENGTH OF STRING
         LTR   R2,R2                NULL STRING CODED?
         BZ    PARSPOST             YES, DONE
         CH    R2,=H'256'           LENGTH(STRING)>=256?
         BL    *+8                  NO, BRANCH
         LH    R2,=H'256'           YES, USE 256 AS A MAXIMUM LENGTH
         STH   R2,#COMMDSZ          SAVE ACTUAL LENGTH
         L     R1,PCLISSTR          NEW STRING ADDRESS
         BCTR  R2,0                 MACHINE LENGTH
         MVC   MSGLINE(*-*),0(R1)   <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         B     PARSPOST
         SPACE 2
         USING PDLISPM,R6
         USING *,R8
ZISPM    LA    R2,FIRST4K
         LA    R1,ISCHKMAX                     CHKMAX(COUNT)
         BAL   R14,PACKMAX                     ANY RESULT?
         STCM  R1,B'0011',SPFCKSZ-FIRST4K(R2)  YES, SAVE
         SPACE 1
         LA    R1,ISCMDMAX                    CMDMAX(COUNT)
         BAL   R14,PACKMAX                    ANY RESULT?
         STCM  R1,B'0011',SPFMAX-FIRST4K(R2)  YES, SAVE
         SPACE 1
         LH    R1,SPFMAX-FIRST4K(R2)          CMDMAX(COUNT)
         S     R1,=F'1'                       VALID?
         BNP   ZISPM010                       NO, BRANCH
         CH    R1,SPFCKSZ-FIRST4K(R2)         BELOW CHECKPOINT SIZE?
         BNL   ZISPM010                       NO, BRANCH
         STH   R1,SPFCKSZ-FIRST4K(R2)         SET NEW CHECKPOINT SIZE
         SPACE 1
ZISPM010 LA    R1,ISTBLMAX                     TBLMAX(COUNT)
         BAL   R14,PACKMAX                     ANY RESULT?
         STCM  R1,B'0011',SPFSIZE-FIRST4K(R2)  YES, SAVE
         B     PARSPOST
         SPACE 2
         USING PDLLIST,R6
         USING *,R8
ZLIST    CLI   LISTFMT+1,0            WAS A FORMAT SPECIFIED?
         BE    ZLISCHK2               NO, QUIT
         NI    HFLAGSDD,FF-NONUM-SNUM-BLOCK-DUMP-LBLOCK-LDUMP
         LH    R15,LISTFMT            FORMAT REQUESTED
         A     R15,=F'-2'             LESS 2 -- NEGATIVE?
         BM    ZLISCHK2               YES, BRANCH
         LA    R1,1                   SHIFT VALUE
         SLL   R1,0(R15)              SHIFT INTO POSITION
         OI    HFLAGSDD,*-*           <<EXECUTED>>
         EX    R1,*-4                 OR INTO FLAG BYTE
         SPACE 1
ZLISCHK2 LA    R1,LMAXLENC            MAXLEN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXLEN             SAVE RESULT
         SPACE 1
         LA    R1,LMAXINC             MAXIN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXIN              SAVE RESULT
         SPACE 1
         LA    R1,LMAXOUTC            MAXOUT
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXOUT             SAVE RESULT
         SPACE 1
         LA    R1,LSKIPRC             SKIPREC
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPREC            SAVE RESULT
         SPACE 1
         LA    R1,LSKIPCC             SKIPCOL
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPCOL            SAVE RESULT
         SPACE 1
         TM    LISTOFF+6,X'80'        OFFSET CODED?
         BNO   ZLISCHK3               NO, DONE
         L     R5,LISTOFF             OFFSET ADDRESS
         LH    R1,LISTOFF+4           LENGTH OF OFFSET
         LTR   R1,R1                  NULL STRING CODED?
         BNP   ZLISCHK3               YES, DONE
         MVI   MSGTEXT2+1,3           MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,PDS384A       OFFSET INVALID MESSAGE
         BAL   R2,HEXC                CONVERT TO BINARY
         B     NEWCMD50               ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#LISTOFF+1  SAVE THE RESULTING OFFSET
         SPACE 1
ZLISCHK3 LH    R15,LDUMPCS+4          LENGTH OF BLOCK/DUMP STRING
         STC   R15,#MODLEN
         S     R15,=F'1'              ANY STRING?
         BM    PARSPOST               NO, BRANCH
         L     R1,LDUMPCS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         B     PARSPOST
         SPACE 2
         USING PDLMAP,R6
         USING *,R8
ZMAP     MVC   #MAPOPT(1),PCLMAPOP+1  SAVE ANY PROCESSING OPTIONS
         TM    PCLMMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   PARSPOST               NO, BRANCH
         LH    R15,PCLMMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    PARSPOST               NO, BRANCH
         L     R1,PCLMMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         B     PARSPOST
         SPACE 2
         USING PDLMEML,R6
         USING *,R8
ZMEMLIST CLI   MEMLRES+1,1              RESET?
         BNE   *+8                      NO, BRANCH
         OI    SPFLAG3,SPFCHN           YES, SIGNAL ISPMODE
         MVC   #LKEDOPT(1),MEMLLKD+1    SAVE LKEDDATE/NOLKEDDATE
         B     PARSPOST
         SPACE 2
         USING PDLOUT,R6
         USING *,R8
ZOUTCOPY MVC   #OUTOPTN(1),OUTOPTN+1     SAVE TEXT/NOTEXT
         CLI   OUTCLOS+1,1               CLOSE REQUESTED?
         BNE   *+8                       NO, BRANCH
         MVI   #OUT,1                    YES, FLAG FOR LATER
         CLI   OUTECHO+1,1               ECHO/NOECHO?
         BL    ZOUT010                   NO, BRANCH
         OI    FLAGSFF,FNOECHO           ASSUME NO ECHO IS DESIRED
         CLI   OUTECHO+1,2               CORRECT?
         BE    ZOUT010                   YES, BRANCH
         NI    FLAGSFF,FF-FNOECHO        NO, ECHO IS DESIRED
         SPACE 1
ZOUT010  CLI   #OUTOPTN,1                TEXT('STRING')?
         BNE   ZOUT020                   NO, BRANCH
         MVC   MSGLINE(40),BLANK128      CLEAR INITIALLY
         LH    R2,OUTTEXT+4              LENGTH OF STRING
         LTR   R2,R2                     NULL STRING CODED?
         BZ    ZOUT020                   YES, DONE
         CH    R2,=H'40'                 LENGTH(STRING)>=40?
         BL    *+8                       NO, BRANCH
         LH    R2,=H'40'                 YES, USE 40 INSTEAD
         L     R1,OUTTEXT                NEW STRING ADDRESS
         S     R2,=F'1'                  GOOD STRING TO MOVE?
         BM    ZOUT020                   NO, SKIP THE MOVE
         MVC   MSGLINE(*-*),0(R1)        <<EXECUTED>>
         EX    R2,*-6                    MOVE IN THE TEXT
         SPACE 1
ZOUT020  CLI   OUTASS+1,1                ALIAS/NOALIAS?
         BL    ZOUT030                   NO, BRANCH
         OI    FLAGSEE,FOUTASS           ASSUME ALIAS IS DESIRED
         CLI   OUTASS+1,1                CORRECT?
         BE    ZOUT030                   YES, BRANCH
         NI    FLAGSEE,FF-FOUTASS        NO, NOALIAS IS DESIRED
         SPACE 1
ZOUT030  CLI   OUTIEB+1,1                IEBUPDAT/NOIEBUPDAT
         BL    PARSPOST                  NO, BRANCH
         OI    FLAGSFF,FIEBUPDT          ASSUME IEBUPDAT IS DESIRED
         CLI   OUTIEB+1,1                CORRECT?
         BE    PARSPOST                  YES, BRANCH
         NI    FLAGSFF,FF-FIEBUPDT       NO, NOIEBUPDAT IS DESIRED
         B     PARSPOST
         SPACE 2
         USING PDLPRINT,R6
         USING *,R8
ZPRINTOF TM    PCLPRSTR+6,X'80'     STRING CODED?
         BNO   PARSPOST             NO, DONE
         LH    R2,PCLPRSTR+4        LENGTH OF STRING
         LTR   R2,R2                NULL STRING CODED?
         BZ    PARSPOST             YES, DONE
         CH    R2,=H'40'            LENGTH(STRING)>=40?
         BL    *+8                  NO, BRANCH
         LH    R2,=H'40'            YES, USE 40 AS A MAXIMUM LENGTH
         STH   R2,#COMMDSZ          SAVE ACTUAL LENGTH
         MVC   MSGLINE(40),BLANK128 CLEAR INITIALLY
         L     R1,PCLPRSTR          NEW STRING ADDRESS
         BCTR  R2,0                 MACHINE LENGTH
         MVC   MSGLINE(*-*),0(R1)   <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         B     PARSPOST
         SPACE 2
         USING PDLRENAM,R6
         USING *,R8
ZRENAME  MVC   #RENSWAP(1),PCLRENSW+1    SAVE SWAP/NOSWAP
         MVC   #RENRANG(1),PCLRENGP+1    SAVE GROUP/NOGROUP
         CLI   #RENRANG,1                GROUP OPTION?
         BNE   ZRENAM10                  NO, BRANCH
         NI    ##ADRPC#,FF-@B            YES, NO BLDL REQUIRED
         SPACE 1
ZRENAM10 TM    PCLREPBY+6,X'80'          ANY RENAME REPBY OPERAND?
         BNO   PARSPOST                  NO, BRANCH
         LH    R15,PCLREPBY+4            LENGTH OF NAME
         S     R15,=F'1'                 MACHINE LENGTH VALID?
         BM    PARSPOST                  NO, BRANCH
         MVC   #RENREP,BLANKS            INITIALIZE THE MEMBER NAME
         L     R1,PCLREPBY               REPBY NAME
         MVC   #RENREP(*-*),0(R1)        <<EXECUTED>>
         EX    R15,*-6                   MOVE INTO STORAGE
         B     PARSPOST
         SPACE 2
         USING PDLREPL,R6
         USING *,R8
ZREPLACE TM    REPLSTR1+6,X'80'     STRING CODED?
         BNO   PARSPOST             NO, ASSUME OK
         MVC   #REPFLAG(1),REPLREP+1  SAVE WRITE/NOWRITE OPTION
         L     R5,REPLSTR1          NEW STRING ADDRESS
         LH    R2,REPLSTR1+4        LENGTH OF STRING
         LA    R1,FINDLTH           FIRST STRING ADDRESS
         ST    R1,#LFSAVE4          SAVE FOR LATER
         LA    R1,PDS380A           GENERAL PROMPT MESSAGE
         ST    R1,#LFSAVE4+4        SAVE FOR LATER
ZREPTEST LTR   R2,R2                NULL STRING CODED?
         BNP   ZREPCHK              YES, CHECK FORMAT
         BCTR  R5,0                 BACK UP ONE
         OI    0(R5),X'40'          UPPER CASE THE DELIMITER
         CLI   0(R5),C'X'           POSSIBLE HEX STRING?
         BNE   ZREPCHAR             NO, BRANCH
         LA    R1,L841              BAD HEX STRING MESSAGE
         LA    R3,1(,R2)            STRING LENGTH +1
         N     R3,=F'-2'            NEXT EVEN NUMBER
         SR    R15,R15              ZERO
         LA    R4,MSGTEXT2+4        WHERE OUTPUT GOES
         LA    R0,2                 PAIRWISE COUNTER
         CR    R2,R3                EVEN LENGTH ORIGINALLY?
         SRL   R3,1                 (STRING LENGTH +1)/2
         ST    R3,MSGTEXT2          (STRING LENGTH +1)/2
         BNE   ZREPHEX2             NO, BRANCH
         SPACE 1
ZREPHEX1 LA    R5,1(,R5)            NEXT CHARACTER
         OI    0(R5),X'40'          UPPER CASE THE HEX CHARACTER
         IC    R15,0(,R5)           GET A BYTE TO CONVERT
         CLI   0(R5),C'0'
         BL    *+12                 0-9?
         CLI   0(R5),C'9'
         BNH   ZREPHEX2             YES, BRANCH
         LA    R15,9(,R15)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'           BELOW AN A?
         BL    ZREPNEWS             YES, INVALID
         CLI   0(R5),C'F'           ABOVE AN F?
         BH    ZREPNEWS             YES, INVALID
         SPACE 1
ZREPHEX2 SLL   R15,28               SHIFT
         SLDL  R14,4                GET A GOOD DIGIT
         BCT   R0,ZREPHEX1          DO THE NEXT DIGIT OF A PAIR
         LA    R0,2                 PAIRWISE COUNTER
         STC   R14,0(,R4)           SAVE AN OUTPUT CHARACTER
         LA    R4,1(,R4)            ANOTHER OUTPUT CHARACTER
         BCT   R3,ZREPHEX1          DO ALL DIGITS
         SPACE 1
         LA    R5,MSGTEXT2+4-1      START OF CONVERTED STRING -1
         L     R2,MSGTEXT2          LENGTH OF CONVERTED STRING
         CH    R2,=H'32'            VALID LENGTH?
         BH    ZREPNEWS             NO, BRANCH
         L     R3,#LFSAVE4          STRING POINTER
         STH   R2,0(,R3)            SAVE ACTUAL LENGTH
         BCTR  R2,0                 MACHINE LENGTH
         MVC   2(*-*,R3),1(R5)      <<EXECUTED>>
         EX    R2,*-6               SAVE THE NEW STRING
         B     ZREPCHK
         SPACE 2
ZREPCHAR LA    R1,L842              INVALID LENGTH MESSAGE
         CH    R2,=H'32'            VALID LENGTH?
         BH    ZREPNEWS             NO, BRANCH
         L     R3,#LFSAVE4          STRING POINTER
         STH   R2,0(,R3)            SAVE ACTUAL LENGTH
         BCTR  R2,0                 MACHINE LENGTH
         MVC   2(*-*,R3),1(R5)      <<EXECUTED>>
         EX    R2,*-6               SAVE THE NEW STRING
         CLI   REPLASIS+1,1         ASIS REQUESTED?
         BE    ZREPCHK              YES, BRANCH
         OC    2(32,R3),BLANK128    NO, UPPER CASE THE DATA
         B     ZREPCHK
         SPACE 2
ZREPNEWS TSMSG (R1)                 INVALID LENGTH STRING -- REENTER
ZREPNEW0 LA    R2,NEWCMD            EXIT ADDRESS FOR ATTENTION
         L     R1,#LFSAVE4+4        GENERAL PROMPT MESSAGE
        $PUTGET (1),ATTN=NEWCMD50   SECOND PART OF MESSAGE
         L     R5,ADDRCBUF          ENTERED DATA
         LH    R2,0(,R5)            LENGTH OF STRING
         SH    R2,=H'5'             VALID MACHINE LENGTH?
         LA    R1,4(R5,R2)          LAST BYTE OF INPUT STRING
         BZ    ZREPNEW3             MAYBE, CHECK FURTHER
         BNP   ZREPNEW0             NO, NULL STRING -- PROMPT AGAIN
         SPACE 1
ZREPNEW1 CLI   0(R1),X'40'          SCAN
         BNE   ZREPNEW2                 BACKWARDS
         BCTR  R1,0                              FOR A
         BCT   R2,ZREPNEW1                             NON-BLANK
         B     ZREPNEW3             POSSIBLE STRING -- CHECK
         SPACE 1
ZREPNEW2 CLI   4(R5),X'40'          SCAN
         BNE   ZREPNEW3                 FORWARDS
         LA    R5,1(,R5)                        FOR A
         BCT   R2,ZREPNEW2                            NON-BLANK
         SPACE 1
ZREPNEW3 CLI   4(R5),X'40'          REAL STRING?
         BE    ZREPNEW0             NO, PROMPT AGAIN
         CLI   REPLASIS+1,1         ASIS REQUESTED?
         BE    ZREPNEW4             YES, BRANCH
         OC    4(*-*,R5),BLANK128   <<EXECUTED>>
         EX    R2,*-6               UPPER-CASE DATA AND DELIMITERS
         SPACE 1
ZREPNEW4 OI    4(R5),X'40'          UPPER-CASE THE FIRST DELIMITER
         MVC   0(1,R5),0(R1)        ENDING CHARACTER
         OI    0(R5),X'40'          UPPER-CASE THE LAST DELIMITER
         CLC   0(1,R5),4(R5)        SAME DELIMITERS?
         BNE   *+6                  NO, BRANCH
         BCTR  R2,0                 YES, ALLOW FOR LAST DELIMITER
         LA    R5,5(,R5)            FIRST CHARACTER OF STRING
         B     ZREPTEST             PARSE THIS NEW STRING
         SPACE 2
ZREPCHK  TM    #LFSAVE4,X'80'         SECOND PASS?
         BO    ZREPCHK1               YES, BRANCH
         TM    REPLSTR2+6,X'80'       STRING CODED?
         BNO   PARSPOST               NO, ASSUME OK
         L     R5,REPLSTR2            NEW STRING ADDRESS
         LH    R2,REPLSTR2+4          LENGTH OF STRING
         LA    R1,REPLTH              SECOND STRING ADDRESS
         ST    R1,#LFSAVE4            SAVE FOR LATER
         LA    R1,PDS386A             GENERAL PROMPT MESSAGE
         ST    R1,#LFSAVE4+4          SAVE FOR LATER
         OI    #LFSAVE4,X'80'         MARK AS SECOND PASS
         B     ZREPTEST
         SPACE 1
ZREPCHK1 CLI   REPLFMT+1,0            WAS A FORMAT SPECIFIED?
         BE    ZREPCHK2               NO, BRANCH
         NI    HFLAGSDD,FF-NONUM-SNUM-BLOCK-DUMP-LBLOCK-LDUMP
         LH    R15,REPLFMT            FORMAT REQUESTED
         A     R15,=F'-2'             LESS 2 -- NEGATIVE?
         BM    ZREPCHK2               YES, BRANCH
         LA    R1,1                   SHIFT VALUE
         SLL   R1,0(R15)              SHIFT INTO POSITION
         OI    HFLAGSDD,*-*           <<EXECUTED>>
         EX    R1,*-4                 MOVE INTO FLAG BYTE
         SPACE 1
ZREPCHK2 LA    R1,RMAXLENC            MAXLEN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXLEN             SAVE RESULT
         SPACE 1
         LA    R1,RMAXINC             MAXIN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXIN              SAVE RESULT
         SPACE 1
         LA    R1,RMAXOUTC            MAXOUT
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXOUT             SAVE RESULT
         SPACE 1
         LA    R1,RMAXFIND            MAXFIND
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXFIND            SAVE RESULT
         SPACE 1
         LA    R1,RSKIPRC             SKIPREC
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPREC            SAVE RESULT
         SPACE 1
         LA    R1,RSKIPCC             SKIPCOL
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPCOL            SAVE RESULT
         SPACE 1
         TM    REPLOFF+6,X'80'        OFFSET CODED?
         BNO   ZREPCHK4               NO, DONE
         L     R5,REPLOFF             OFFSET ADDRESS
         LH    R1,REPLOFF+4           LENGTH OF OFFSET
         LTR   R1,R1                  NULL STRING CODED?
         BNP   ZREPCHK4               YES, DONE
         MVI   MSGTEXT2+1,3           MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,PDS384A       OFFSET INVALID MESSAGE
         BAL   R2,HEXC                CONVERT TO BINARY
         B     NEWCMD50               ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#LISTOFF+1  SAVE THE RESULTING OFFSET
         SPACE 1
ZREPCHK4 LH    R15,RDUMPCS+4          LENGTH OF BLOCK/DUMP STRING
         STC   R15,#MODLEN
         S     R15,=F'1'              ANY STRING?
         BM    PARSPOST               NO, BRANCH
         L     R1,RDUMPCS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         B     PARSPOST
         SPACE 2
         USING PDLRESTO,R6
         USING *,R8
ZRESTORE OI    #RESFLAG,#RESFDIS+#RESFPRO  DEFAULT OPERANDS
         TM    FLAGSGG,FPROMCON            PROMPTING OFF?
         BNO   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFPRO*4         YES, SET A FLAG
         CLI   PCLRSREP+1,X'01'            REPEAT?
         BNE   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFREP           YES, SET REPEAT FLAG
         SPACE 1
         CLI   PCLRSDIS+1,X'02'            NODISPLAY?
         BNE   *+8                         NO, BRANCH
         NI    #RESFLAG,FF-#RESFDIS        YES, TURN IT OFF
         CLI   PCLRSPRO+1,X'02'            NOPROMPT?
         BNE   *+8                         NO, BRANCH
         XI    #RESFLAG,#RESFPRO*2+#RESFPRO  YES, SET FLAGS
         SPACE 1
         LH    R2,PCLRFIND+4             LENGTH OF STRING
         LTR   R2,R2                     NULL STRING CODED?
         BZ    ZREST010                  YES, DONE
         CH    R2,=H'32'                 LENGTH(STRING)>=40?
         BL    *+8                       NO, BRANCH
         LH    R2,=H'32'                 YES, USE 40 INSTEAD
         STH   R2,#RESTSTR               SAVE STRING LENGTH
         L     R1,PCLRFIND               NEW STRING ADDRESS
         S     R2,=F'1'                  GOOD STRING TO MOVE?
         BM    ZREST010                  NO, SKIP THE MOVE
         MVC   #RESTSTR+2(*-*),0(R1)     <<EXECUTED>>
         EX    R2,*-6                    MOVE IN THE TEXT
         SPACE 1
ZREST010 TM    PCLRMOD+6,X'80'             ANY RESTORE MODULE OPERAND?
         BNO   ZREST020                    NO, BRANCH
         LH    R15,PCLRMOD+4               LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    ZREST020                    NO, BRANCH
         MVC   #RESTNAM,BLANKS             INITIALIZE THE MEMBER NAME
         L     R1,PCLRMOD                  START OF NAME
         MVC   #RESTNAM(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         SPACE 1
ZREST020 TM    PCLREST+6,X'80'             ANY RESTORE LIKE OPERAND?
         BNO   PARSPOST                    NO, BRANCH
         LH    R15,PCLREST+4               LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    PARSPOST                    NO, BRANCH
         MVC   #RESTLIK,BLANKS             INITIALIZE THE MEMBER NAME
         L     R1,PCLREST                  START OF NAME
         MVC   #RESTLIK(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         B     PARSPOST
         SPACE 2
         USING PDLTSOED,R6
         USING *,R8
ZTSOEDIT MVC   #EDITOLD(1),PCLEDOP+1   SAVE SPECIFIED OLD/NEW OPTION
         CLI   PCLEDOP+1,X'02'         "OLD"?
         BNE   ZTSOEDI2                NO, BRANCH
         OI    ##ADRPC#,@B             YES, MUST EXIST
ZTSOEDI2 MVC   #EDITNON(1),PCLNON+1    NONUM OPTION
         MVC   #EDITASI(1),PCLASIS+1   ASIS OPTION
         MVC   #EDITTYP+2(2),PCLTYPE   EDIT TYPE FIELD
         B     PARSPOST
         SPACE 2
         USING PDLVERIF,R6
         USING *,R8
ZVERIFY  MVC   #VERLOAD(1),VERLOAD+1    SAVE LOAD/NOLOAD OPTION
         MVC   #VERSTND(1),VERNAME+1    SAVE NAME/NONAME OPTION
         MVC   #VERREAD(1),VERINPUT+1   SAVE INPUT/NOINPUT OPTION
         MVC   #VERUPDT(1),VERUPDT+1    SAVE UPDATE/NOUPDATE OPTION
         MVC   #VERSTAT(1),VERSTATS+1   SAVE STATS/NOSTATS OPTION
         B     PARSPOST
         SPACE 2
         USING MAINPDL,R6
         USING *,R8
ZMAINPDL TM    VOLSETID+6,X'80'     ANY NAME REQUESTED?
         BNO   MAINPDL1             NO, BRANCH
         LH    R15,VOLSETID+4       LENGTH OF NAME
         S     R15,=F'1'            VALID?
         BM    MAINPDL1             NO, BRANCH
         MVC   VOLSET,BLANKS        INITIALIZE VOLUME NAME
         L     R1,VOLSETID          ADDRESS OF NAME
         MVC   VOLSET(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6              MOVE IN THE NAME
         SPACE 1
MAINPDL1 MVC   VOLUME,VOLSET        INITIALIZE VOLUME NAME
         TM    VOLNAME+6,X'80'      ANY NAME REQUESTED?
         BNO   MAINPDL2             NO, BRANCH
         LH    R15,VOLNAME+4        LENGTH OF NAME
         S     R15,=F'1'            VALID?
         BM    MAINPDL2             NO, BRANCH
         MVC   VOLUME,BLANKS        INITIALIZE VOLUME NAME
         L     R1,VOLNAME           ADDRESS OF NAME
         MVC   VOLUME(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6              MOVE IN THE NAME
         SPACE 1
MAINPDL2 CLI   VOLUME,C'*'          NULLIFY VALUE?
         BNE   *+10                 NO, BRANCH
         MVC   VOLUME,BLANKS        YES, CLEAR VOLUME NAME
         MVI   DSPALLOC,ALLOSHR     ASSUME DISP=SHR
         MVI   DSPREQST,ALLOSHR     ASSUME DISP=SHR
         CLI   OPTDISP+1,1          CORRECT?
         BNE   MAINPDL4             NO, BRANCH
         MVI   DSPALLOC,ALLOOLD     DISP=OLD
         MVI   DSPREQST,ALLOOLD     DISP=OLD
         DROP  R6
         SPACE 1
MAINPDL4 TM    FLAGSBB,FQUOTE       QUOTED DATA SET NAME?
         BO    PARSPOST             YES, BRANCH
         CLI   VOLUME,X'40'         ANY VOLUME NAME SPECIFIED?
         BNE   PARSPOST             YES, ASSUME NO CATALOG USE
*  NEED TO FULLY QUALIFY THE DATA SET NAME
         USING DFPL,R6
         LA    R6,PARMLIST          START OF DFPL PARAMETER LIST
         L     R1,ADDRUPT
         L     R2,ADDRECT
         LA    R3,ATTNECB
         LA    R4,PARMLIST+16
         STM   R1,R4,DFPLUPT        INITIALIZE DFPLUPT, DFPLECT,
*                                              DFPLECB AND DFPLDFPB
         USING DFPB,R6
         LR    R6,R4
         LA    R1,DSNLEN            DSNAME LENGTH, DSNAME
         L     R2,ADDRPSCB          PSCB
         LA    R3,DOUBLE            BLANK DEFAULT QUALIFIER
         SR    R4,R4                NO USER CATALOG
         LA    R5,PASSWORD
         STM   R1,R5,DFPBDSN        INITIALIZE DFPBDSN, DFPBPSCB,
*                                     DFPBQUAL, DFBCAT AND DFPBPSWD
         MVI   DFPBCNTL,DFPBRET     ADD LOW-LEVEL QUALIFIER
         MVI   DFPBCODE,DFPB04      ENTRY CODE X'04'
         MVC   DOUBLE(8),BLANKS     BLANK DEFAULT QUALIFIER
         DROP  R6
         SPACE 1
         LA    R1,PARMLIST          ADDRESS OF PARM LIST
         L     R15,ADDRDEFT         DEFAULT ROUTINE ADDRESS
         BALR  R14,R15
         B     PARSPOST
         TITLE 'P D S  --  PDS PARSE VALIDITY CHECKING         6/10/85'
*
*        CREATED/CHANGED VALIDITY CHECK SUBROUTINE
*
         SPACE 1
VRTDATE  ENTER VALCHECK
         SPACE 1
         TM    8+6(R6),X'80'      SECOND PARAMETER PRESENT?
         BO    VRTDCHK0           YES, CONTINUE
         L     R1,0(,R6)          ADDR OF FIRST PARAMETER
         LH    R0,4(,R6)          LENGTH OF FIRST PARAMETER
VRTDATE2 CLI   0(R1),C':'         SEPARATOR CHARACTER?
         BE    VRTDATE4           YES, FOUND
         LA    R1,1(,R1)          NEXT CHARACTER
         BCT   R0,VRTDATE2        CHECK ALL CHARACTERS
         B     VRTDCHK0 '         ADDR OF FIRST PARAMETER
VRTDATE4 LH    R14,4(,R6)         LENGTH OF FIRST PARAMETER
         SR    R14,R0             LENGTH OF FIRST PART OF RANGE VALID?
         BZ    VRTDCHK0           NO, BRANCH
         STH   R14,4(,R6)         UPDATE THE FIRST FIELD LENGTH
         LA    R1,1(,R1)          POINT AFTER THE :
         ST    R1,8(,R6)          START OF SECOND PARAMETER
         S     R0,=F'1'           LENGTH VALID?
         BNP   VRTDCHK0           NO, BRANCH
         STH   R0,8+4(,R6)        UPDATE THE SECOND FIELD LENGTH
         OI    8+6(R6),X'80'      SECOND PARAMETER IS PRESENT
VRTDCHK0 L     R1,0(,R6)          ADDR OF FIRST PARAMETER
         CLI   5(R6),5            NUMBER OF DIGITS:
         BL    VRTDBAD              0-4, ERROR
         BE    VRTDCHK1             5, YYDDD
         CLI   5(R6),8
         BH    VRTDBAD              6-8, MM/DD/YY
         SR    R0,R0
         IC    R0,5(R6)           NUMBER OF CHARACTERS
         BAL   R3,VRTDRTN         CONVERT TO YYDDD
         LA    R1,DELAYMSG        TEMPORARY FIRST ADDRESS
         MVC   0(5,R1),DAYTABLE+3 MOVE IN YYDDD
         SPACE 1
VRTDCHK1 LR    R5,R1              SAVE R1 POINTER FOR LATER
         CLC   2(3,R5),VRTDL366   >366?
         BH    VRTDBAD            YES, ERROR
         CLC   2(3,R5),VRTDL000   ZEROR DAYS?
         BE    VRTDBAD            YES, ERROR
         TM    8+6(R6),X'80'      SECOND PARAMETER PRESENT?
         BNO   VRTDOK             NO, BRANCH
         L     R1,8(,R6)          ADDR OF SECOND PARAMETER
         CLI   8+5(R6),5          NUMBER OF DIGITS:
         BL    VRTDBAD              0-4, ERROR
         BE    VRTDCHK2             5, YYDDD
         CLI   8+5(R6),8
         BH    VRTDBAD              6-8, MM/DD/YY
         SR    R0,R0
         IC    R0,8+5(R6)         NUMBER OF CHARACTERS
         BAL   R3,VRTDRTN         CONVERT TO YYDDD
         LA    R1,DELAYMSG+5      TEMPORARY SECOND ADDRESS
         MVC   0(5,R1),DAYTABLE+3 MOVE IN YYDDD
         SPACE 1
VRTDCHK2 CLC   2(3,R1),VRTDL366   >366?
         BH    VRTDBAD            YES, ERROR
         CLC   2(3,R1),VRTDL000   ZEROR DAYS?
         BE    VRTDBAD            YES, ERROR
         CLC   0(5,R5),0(R1)      FIRST:SECOND RELATIONSHIP VALID?
         BH    VRTDBAD            NO, ERROR
         SPACE 1
VRTDOK   SR    R15,R15            VALID -- RESET TRANSLATED RESULTS
         L     R2,0(,R6)          FIRST PARAMETER ADDRESS
         MVC   0(5,R2),0(R5)      SUBSTITUTE THE TRANSLATED RESULT
         MVI   5(R6),5            SET THE LENGTH FOR LATER
         TM    8+6(R6),X'80'      SECOND PARAMETER PRESENT?
         BNO   VRTDTERM           NO, BRANCH
         L     R2,8(,R6)          SECOND PARAMETER ADDRESS
         MVC   0(5,R2),0(R1)      SUBSTITUTE THE TRANSLATED RESULT
         MVI   8+5(R6),5          SET THE LENGTH FOR LATER
         B     VRTDTERM
VRTDL000 DC    C'000'
VRTDL366 DC    C'366'
         SPACE 2
* SUBROUTINE TO CONVERT MM/DD/YY TO YYDDD
VRTDRTN  BAL   R2,VRTDNUM         CONVERT MM TO BINARY
         LTR   R4,R14             MM ZERO?
         BZ    VRTDBAD            YES, ERROR
         CH    R4,=H'12'          MONTH>12?
         BH    VRTDBAD            YES, ERROR
         BAL   R2,VRTDSLA         CONVERT /DD TO BINARY
         LTR   R14,R14            DD ZERO?
         BZ    VRTDBAD            YES, ERROR
         ST    R14,DOUBLE         SAVE DAYS FOR LATER
         BAL   R2,VRTDSLA         CONVERT /YY TO BINARY
         LTR   R0,R0              ALL DIGITS USED?
         BNZ   VRTDBAD            NO, ERROR
         MVC   DAYTABLE,DAYMONTH  DAYS/MONTH
         LR    R15,R14
         SRL   R15,2
         SLL   R15,2
         CR    R15,R14            LEAP YEAR?
         BNE   *+8                NO, BRANCH
         MVI   DAYTABLE+1,29      YES, FEBRUARY HAS 29 DAYS
         SR    R0,R0
         SR    R15,R15
         LA    R1,DAYTABLE
         SPACE 1
VRTDLOOP IC    R0,0(,R1)          NUMBER OF DAYS FOR A MONTH
         S     R4,=F'1'           ANY MORE MONTHS?
         BNP   VRTDOUT            NO, BRANCH
         AR    R15,R0             ADD THIS MONTH
         LA    R1,1(,R1)          NEXT MONTH
         B     VRTDLOOP
         SPACE 1
VRTDOUT  L     R1,DOUBLE          DAYS IN THE FINAL MONTH
         CR    R1,R0              TOO MANY?
         BH    VRTDBAD            YES, BRANCH
         AR    R15,R1             JULIAN DAYS
         MH    R14,=H'1000'       YY000
         AR    R15,R14            YYDDD
         CVD   R15,DOUBLE
         MVC   DAYTABLE(8),VRTDLEDI
         ED    DAYTABLE(8),DOUBLE+4
         BR    R3
VRTDLEDI DC    X'4020212020202020'
         SPACE 1
* SUBROUTINE TO CONVERT /NN TO BINARY
VRTDSLA  CLI   0(R1),C'/'         SLASH DIVIDER?
         BNE   VRTDBAD            NO, ERROR
         LA    R1,1(,R1)          YES, SKIP IT
         BCTR  R0,0               REDUCE STRING LENGTH
         SPACE 1
* SUBROUTINE TO CONVERT NN TO BINARY
VRTDNUM  TM    0(R1),X'F0'        A NUMERIC DIGIT?
         BNO   VRTDBAD            NO, ERROR
         IC    R14,0(,R1)         FIRST DIGIT
         BCTR  R0,0
         LA    R1,1(,R1)
         N     R14,=F'15'         FIRST DIGIT IN BINARY
         CLI   0(R1),C'/'         SLASH DIVIDER?
         BER   R2                 YES, RETURN
         MH    R14,=H'10'
         TM    0(R1),X'F0'        A NUMERIC DIGIT?
         BNO   VRTDBAD            NO, ERROR
         IC    R15,0(,R1)         FIRST DIGIT
         BCTR  R0,0
         LA    R1,1(,R1)
         N     R15,=F'15'         SECOND DIGIT IN BINARY
         AR    R14,R15            RESULT IN BINARY
         BR    R2
VRTDBAD  LA    R15,4              INVALID, PROMPT FOR ANOTHER DATE
VRTDTERM EXIT
         EJECT
*
*        INPUT DATA SET VALIDITY CHECK EXIT
*
         SPACE 3
VRTDSN   ENTER VALCHECK
         OI    FLAGSBB,FQUOTE+FNOTFILE  ASSUME QUOTED AND NOT FILE(...
         MVC   SAVDSN+1(SAVLEN),DSNLEN  SAVE PREVIOUS DSNAME
         MVC   SAVDSN(1),DSPREQST       SAVE DISPOSITION FOR LATER
         MVC   PASSWORD,BLANKS
         SPACE 1
         L     R15,0(R6)                ADDR OF DSNAME
         CLI   0(R15),C'*'              ASTERISK DSNAME?
         BE    NOFILE                   YES, BRANCH
         MVC   DSNAME(L'DSNAME),BLANK128
         LH    R14,4(R6)                LENGTH OF DSNAME
         STH   R14,DSNLEN
         BCTR  R14,0
         MVC   DSNAME(*-*),0(R15)       <<EXECUTED>>
         EX    R14,*-6                  MOVE DSNAME TO AREA
         L     R1,ADDRUPT               ADDRESS OF THE UPT
         SR    R14,R14
         IC    R14,UPTPREFL-UPT(R1)     LENGTH OF USERID
         MVC   DSNAMEH(4),DSNAME        FIRST FOUR BYTES OF DSNAME
         LTR   R14,R14                  VALID LENGTH?
         BZ    *+6                      NO, BRANCH
         BCTR  R14,0                    MACHINE LENGTH
         CLC   DSNAME(*-*),UPTPREFX-UPT(R1) <<EXECUTED>>
         EX    R14,*-6                      START WITH USERID?
         LR    R1,R15                       ADDRESS OF DSNAME
         BNE   *+14                         NO, BRANCH
         LA    R1,2(R14,R15)                DATA AFTER PERIOD
         MVC   DSNAMEH(4),0(R1)         FIRST FOUR BYTES OF DSNAME
         SPACE 1
         OI    FLAGSBB,FQUOTE+FNOTFILE  ASSUME QUOTED AND NOT FILE(...
         TM    6(R6),X'40'              QUOTED?
         BO    NOFILE                   YES, BRANCH
         XI    FLAGSBB,FQUOTE           NO, RESET THE BIT
         TM    14(R6),X'80'             FILE(FILENAME) SUPPLIED?
         BZ    NOMEMB                   NO, BRANCH
         CLC   0(1,R1),VRTDSFIL         CORRECT FILE IDENTIFIER?
         BNE   NOFILE                   NO, BRANCH
         CLI   1(R1),C'('               AND A (?
         BE    GOTFILE                  YES, BRANCH
         TM    1(R1),FF-X'40'           AND A BLANK OR NULL?
         BZ    GOTFILE                  YES, BRANCH
         CLC   0(2,R1),VRTDSFIL         CORRECT FILE IDENTIFIER?
         BNE   NOFILE                   NO, BRANCH
         CLI   2(R1),C'('               AND A (?
         BE    GOTFILE                  YES, BRANCH
         TM    2(R1),FF-X'40'           AND A BLANK OR NULL?
         BZ    GOTFILE                  YES, BRANCH
         CLC   0(3,R1),VRTDSFIL         CORRECT FILE IDENTIFIER?
         BNE   NOFILE                   NO, BRANCH
         CLI   3(R1),C'('               AND A (?
         BE    GOTFILE                  YES, BRANCH
         TM    3(R1),FF-X'40'           AND A BLANK OR NULL?
         BZ    GOTFILE                  YES, BRANCH
         CLC   0(4,R1),VRTDSFIL         CORRECT FILE IDENTIFIER?
         BNE   NOFILE                   NO, BRANCH
         CLI   4(R1),C'('               AND A (?
         BE    GOTFILE                  YES, BRANCH
         TM    4(R1),FF-X'40'           AND A BLANK OR NULL?
         BNZ   NOFILE                   NO, BRANCH
GOTFILE  XI    FLAGSBB,FNOTFILE         FILE(FILENAME) SUPPLIED
         L     R15,8(,R6)               POINT TO FILENAME
         LH    R14,12(,R6)              LENGTH OF MEMBER NAME
         BCTR  R14,0
         MVC   DDNAMEH(8),BLANKS        FILENAME AREA
         MVC   DDNAMEH(*-*),0(R15)      <<EXECUTED>>
         EX    R14,*-6                  MOVE FILENAME
         B     NOMEMB
         SPACE 2
NOFILE   TM    14(R6),X'80'             MEMBER SUPPLIED?
         BZ    NOMEMB                   NO, BRANCH
         SPACE 1
         L     R15,8(,R6)               POINTER TO MEMBER NAME
         LH    R14,12(,R6)              LENGTH OF MEMBER NAME
         BCTR  R14,0
         MVC   MEMBERD+3(8),BLANKS      MEMBER NAME DEFAULT AREA
         MVC   MEMBERD+3(*-*),0(R15)    <<EXECUTED>>
         EX    R14,*-6                  MOVE MEMBER TO AREA
         STCM  R14,B'0011',MEMBERD+1    MACHINE LENGTH OF MEMBER NAME
         MVI   MEMBERD,FMEMBER1         JUST ONE MEMBER NAME DEFAULT
         SPACE 2
NOMEMB   TM    22(R6),X'80'             PASSWORD SUPPLIED?
         BZ    VRTDSN99                 NO, BRANCH
         SPACE 1
         L     R15,16(,R6)              POINTER TO PASSWORD
         LH    R14,20(,R6)              PASSWORD LENGTH
         BCTR  R14,0
         MVC   PASSWORD(*-*),0(R15)     <<EXECUTED>>
         EX    R14,*-6                  MOVE PASSWORD TO AREA
         SPACE 2
VRTDSN99 SR    R15,R15
         EXIT
VRTDSFIL DC    C'FILE'
         EJECT
*
*        MEMBER NAME VALIDITY CHECKING
*
         SPACE 3
VRTMEMBR ENTER VALCHECK
         SR    R15,R15            ASSUME NOT PARTITIONED
         TM    ##ADRPC#,@S        CORRECT?
         BO    VRTMEMX            YES, EXIT
         L     R15,0(R6)          ADDR OF MEMBER NAME
         LH    R14,4(R6)          LENGTH OF NAME
         TM    ##ADRPC#,@G        NEW DEFAULT MEMBER NAME?
         BNO   VRTMEMS            NO, BRANCH
         MVI   FLAGSAA,FOPTIONS   SET THE OPTIONS FLAG
         SPACE 1
VRTMEMS  BCTR  R14,0              EXECUTE LENGTH
         OI    FLAGSAA,FMEMBER2
         TM    FLAGSAA,FMEMBER1   FIRST MEMBER PROCESSED?
         LA    R6,MEMBER2
         LA    R2,LMEMBER2
         BO    MEM2ND             YES, DO THE SECOND MEMBER NAME
         XC    0(10,R6),0(R6)     NO, CLEAR MEMBER2, LMEMBER2
         XI    FLAGSAA,FMEMBER1+FMEMBER2
         LA    R6,MEMBER1
         LA    R2,LMEMBER1
MEM2ND   MVC   0(8,R6),BLANKS     BLANK THE MEMBER NAME
         SPACE 1
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER NAME?
         BO    VRTSOUT            YES, BRANCH
         TM    ##ADRPC#,@G        DEFAULT AND MEMBER GROUPS ALLOWED?
         BO    VRTM190            YES, BRANCH
         TM    ##ADRPA#,$M        PATTERN OR DISPLAY SUBCOMMAND?
         BNO   VRTSOUT            NO, BRANCH
VRTM190  LA    R0,1(,R14)         ACTUAL MEMBER NAME LENGTH
         LR    R5,R15             START OF MEMBER NAME
         LTR   R14,R14            SINGLE CHARACTER NAME?
         BNZ   VRTM210            NO, BRANCH
         CLI   0(R5),C'/'         MEMBER PATTERN REQUEST?
         BE    VRTBAD             YES, INVALID
         CLI   0(R5),C'*'         MEMBER PLACE-HOLDER?
         BE    VRTM200            YES, BRANCH
         CLI   0(R5),C':'         MEMBER RANGE REQUEST?
         BNE   VRTSOUT            NO, BRANCH
         NI    FLAGSAA,FF-FMEMBER1        ENTIRE DIRECTORY DESIRED
         OI    FLAGSAA,FMEM#MEM+FMEMRANG  MEMBER RANGE GROUP DESIRED
         B     VRTGOOD
         SPACE 2
VRTM200  OI    FLAGSAA,FMEMSTAR   MEMBER NAME STAR (*) NOTATION
         CLI   MEMBERD,X'00'      ANY MEMBER DEFAULT YET?
         BE    VRTSOUT            NO, JUST USE *
         NI    FLAGSAA,FF-FOPTIONS    SET FLAGS FOR NO OPERAND
         MVC   LMEMBER1(2+8+8+2),MEMBERD+1
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERD    RESET THE FLAG BITS
         B     VRTGOOD
         SPACE 1
VRTM210  CLI   0(R5),C':'         MEMBER RANGE DESIRED?
         BE    VRTM220            YES, BRANCH
         CLI   0(R5),C'/'         MEMBER PATTERN DESIRED?
         BE    VRTM222            YES, BRANCH
         LA    R5,1(,R5)          NEXT MEMBER CHARACTER
         BCT   R0,VRTM210         CHECK ALL MEMBER CHARACTERS
         B     VRTSOUT            NOT FOUND -- NORMAL MEMBER NAME
         SPACE 1
VRTM220  OI    FLAGSAA,FMEMRANG   MEMBER RANGE DESIRED
VRTM222  OI    FLAGSAA,FMEM#MEM   MEMBER GROUP DESIRED
         CR    R15,R5             FIRST CHARACTER OF NAME?
         BNE   VRTM230            NO, BRANCH
         LA    R15,1(,R15)        SKIP OVER : OR /
         XC    LMEMBER2,LMEMBER2  NO MEMBER 2 YET
         TM    FLAGSAA,FMEMRANG   MEMBER RANGE DESIRED?
         BNO   VRTM240            NO, BRANCH
         MVI   LMEMBER1+1,0       MACHINE LENGTH IS ZERO
         MVI   MEMBER1,X'00'      DATA IS X'00'
         B     VRTMEMS            CONTINE WITH MEMBER2
         SPACE 1
VRTM230  S     R0,=F'1'           ACTUAL LENGTH OF MEMBER2
         STH   R0,LMEMBER2        SAVE FOR LATER
         SR    R14,R0             ACTUAL LENGTH OF MEMBER1
         LA    R5,1(,R5)          SKIP OVER : OR +
         ST    R5,FULLWORD        SAVE FOR LATTER
         SPACE 1
VRTM240  BCTR  R14,0              REDUCE MACHINE LENGTH BY ONE
         SPACE 2
VRTSOUT  STH   R14,0(,R2)         STASH LENGTH
         SPACE 1
         LA    R5,0(R14,R15)      POINT TO LAST CHARACTER OF MEMBER
         LM    R0,R1,BLANKS       HEX MEMBER NAME PADDING
         SPACE 1
         CLC   =C'X''',0(R15)     BEGINS WITH X'?
         BE    VRTHEX             YES, MUST BE A HEX MEMBER NAME
         SPACE 1
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER?
         BNO   MEMLEN             NO, BRANCH
         CLC   ##SUBCOM(8),$RES   RESTORE SUBCOMMAND?
         BNE   MEMLEN             NO, BRANCH
         BCTR  R15,0              BACK UP R15
         SR    R0,R0              ZERO MEMBER NAME PADDING
         SR    R1,R1              ZERO MEMBER NAME PADDING
         B     VRTNULL            CHECK FOR A NULL
         SPACE 2
MEMLEN   CLI   1(R2),7            COMPARE LENGTH > 7?
         BNH   VRTCHAR            NO, BRANCH
         B     VRTBAD             YES, INVALID MEMBER NAME
         SPACE 2
VRTHEX   CLI   0(R5),C''''        A FINAL QUOTE?
         BNE   VRTBAD             NO, INVALID HEXADECIMAL
         BCTR  R5,0               POINT TO PRECEDING CHARACTER
         LA    R15,1(,R15)        POINT TO FIRST QUOTE
         SPACE 1
VRTNULL  CR    R5,R15             NULL HEX STRING?
         BNH   VRTBAD             YES, INVALID SYNTAX
         SR    R4,R4              COUNT OF DIGITS
         L     R14,=F'-1'         BXH INCREMENT
         SPACE 1
VRTHLOOP IC    R3,0(,R5)          NEXT CHARACTER (REVERSE SCAN)
         CLI   0(R5),C'0'
         BL    *+12               0-9?
         CLI   0(R5),C'9'
         BNH   VRT0TOF            YES, BRANCH
         LA    R3,9(,R3)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'
         BL    VRTBAD             A-F?
         CLI   0(R5),C'F'
         BH    VRTBAD             NO, BRANCH
         SPACE 1
VRT0TOF  SLL   R3,28              GET DIGIT IN TOP DIGIT
         SRDL  R0,4               CREATE ROOM IN R0,R1
         OR    R0,R3              ADD TO R0,R1
         LA    R4,1(,R4)          ANOTHER DIGIT
         BXH   R5,R14,VRTHLOOP    DO ALL DIGITS
         SPACE 1
         LA    R14,1(,R4)         DIGITS+1
         SRL   R14,1              (DIGITS+1)/2    -- TRUNCATED
         BCTR  R14,0              MACHINE COMPARE LENGTH
         STH   R14,0(,R2)         MACHINE LENGTH AFTER CONVERSION
         LA    R14,2(R14,R14)     (COMPARE LENGTH+1)*2
         CR    R14,R4             EVEN NUMBER OF DIGITS?
         BE    *+8                YES, BRANCH
         SRDL  R0,4               NO, ADJUST BY ONE MORE DIGIT
         SPACE 1
         STM   R0,R1,DOUBLE       SAVE CREATED MEMBER NAME
         LA    R14,7              FULL LENGTH MEMBER NAME
         LA    R15,DOUBLE         START OF MOVE ADDRESS
         SPACE 2
VRTCHAR  MVC   0(*-*,R6),0(R15)   <<EXECUTED>>
         EX    R14,*-6            MOVE IN MEMBER NAME
         SPACE 3
         TM    FLAGSAA,FMEM#MEM   MEMBER GROUP PROCESSING?
         BNO   VRTGOOD            NO, BRANCH
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER PROCESSED?
         BO    VRTGOOD            YES, BRANCH
         LH    R14,LMEMBER2       ACTUAL LENGTH OF MEMBER2
         L     R15,FULLWORD       START OF MEMBER2
         LTR   R14,R14            ANY MEMBER2?
         BP    VRTMEMS            YES, BRANCH
         SPACE 1
         AIF   ('&CONDRNG' EQ 'N').NMRNG  OPTION TO MAKE AA: = AA:AA
         TM    FLAGSAA,FMEMRANG   MEMBER RANGE?
         BZ    VRTGOOD            NO, BRANCH
         OI    FLAGSAA,FMEMBER2   INDICATE SECOND MEMBER NAME PROVIDED
         MVC   MEMBER2,MEMBER1    MEMBER NAMES ARE THE SAME
         MVC   LMEMBER2,LMEMBER1  MEMBER NAME LENGTHS ARE THE SAME
.NMRNG   ANOP
         SPACE 1
VRTGOOD  SR    R15,R15
         TM    ##ADRPC#,@G        NEW DEFAULT MEMBER NAME?
         BNO   VRTGOOD2           NO, BRANCH
         TM    FLAGSII,FMEMFRST   FIRST MEMBER OF A LIST?
         BZ    VRTGOOD0           YES, BRANCH
         L     R1,PMEMMAX                 LAST ACTIVE POINTER
         OC    1(3,R1),1(R1)              ANY SLOT ENTRIES?
         BNZ   VRTADD80                   YES, BRANCH
         LA    R0,42*24                   42 MEMBERS, 24 WIDE EACH
         ICM   R0,B'1000',=AL1(24)        ASSUME SPF IS INITIALIZED
         STCM  R0,B'1000',SUBLIST2        MARK FOR LATER RELEASE
         TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF INITIALIZED?
         BM    VRTADD10                   YES, BRANCH
         AIF   ('&CISP' NE 'ISPFV2').NSPFVRT
         TM    FLAGSFF,FSPFOPT6           CALLED FROM OPTION 6?
         BO    VRTADD10                   YES, BRANCH
.NSPFVRT ANOP
         ICM   R0,B'1000',=AL1(23)        SPF IS NOT INITIALIZED
         STCM  R0,B'1000',SUBLIST1        MARK FOR LATER RELEASE
         MVI   SUBLIST2,0                 CLEAR THE EARLIER MARK
VRTADD10 GETMAIN R,LV=(0)                 GET MORE ELEMENT STORAGE
         LR    R15,R1                     START OF TABLE
         L     R1,PMEMMAX                 CURRENT ELEMENT
         LA    R0,42-1                    NUMBER OF ELEMENTS -1
VRTADD41 LA    R15,24(,R15)               NEXT ELEMENT POINTER
         ST    R15,0(,R1)                 POINT TO THE NEXT ELEMENT
         LR    R1,R15                     NEXT ELEMENT
         BCT   R0,VRTADD41                DO 41 ELEMENTS
         XC    0(4,R1),0(R1)              CLEAR THE LAST POINTER
         SPACE 2
VRTADD80 L     R1,PMEMMAX                 CURRENT ADDED ELEMENT
         L     R15,0(,R1)                 NEXT POINTER
         NI    0(R15),FF-X'80'            NOT AN ACTIVE ELEMENT
         ST    R15,PMEMMAX                NEXT ELEMENT TO ADD
         OI    0(R1),X'80'                ACTIVE ELEMENT
         OI    PMEMMIN,X'80'              MEMBER LIST IS ACTIVE
         SR    R15,R15
         MVC   4(20,R1),LMEMBER1  LMEMBER1,MEMBER1,MEMBER2,LMEMBER2
         MVC   4(1,R1),FLAGSAA    SAVE FLAGSAA TOO
         NI    4(R1),FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         MVC   LMEMBER1(20),MEMBERD+1  RESTORE THE FIRST ELEMENT
         NI    FLAGSAA,FF-FMEM#MEM-FMEMBER1-FMEMBER2-FMEMRANG
         OC    FLAGSAA(1),MEMBERD
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         B     VRTMEMX
         SPACE 1
VRTGOOD0 OI    FLAGSII,FMEMFRST   FIRST LIST ELEMENT
         TM    FLAGSAA,FMEMSTAR   ASTERISK NOTATION?
         BO    VRTGOOD1           YES, BRANCH
         MVC   PMEMMAX(4),PMEMMIN NO, POINT TO THE FIRST ELEMENT
         NI    PMEMMIN,FF-X'80'           MEMBER LIST IS INACTIVE
         SPACE 1
VRTGOOD1 TM    SPFLAG2,SPFPOST            LINE COMMAND?
         BO    VRTMEMX                    YES, TERMINATE
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1  SAVE CURRENT MEMBER NAMES
         MVC   MEMBERD(1),FLAGSAA           SAVE CURRENT FLAGS
         NI    MEMBERD,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         B     VRTMEMX            TERMINATE
         SPACE 2
VRTGOOD2 TM    ##ADRPA#,$M        DISPLAY OR PATTERN SUBCOMMAND?
         BNO   VRTGOOD4           NO, BRANCH
         TM    FLAGSAA,FMEM#MEM   MEMBER GROUP TO DISPLAY?
         BNO   VRTGOOD4           NO, BRANCH
         XI    FLAGSAA,FMEM#MEM   TURN OFF THE MEMBER GROUP FLAG
         L     R1,$DIS+8          ASSUME DISPLAY
         TM    FLAGSAA,FMEMRANG   CORRECT?
         BO    *+8                YES, BRANCH
         L     R1,$PAT+8          NO, PATTERN
         ST    R1,##ADRCMD        UPDATE THE PROCESSING ADDRESS
         SPACE 2
VRTGOOD4 CLC   ##SUBCOM(8),$PAT   NEW DEFAULT PATTERN?
         BNE   VRTMEMX            NO, BRANCH
         MVC   MEMBERP+1(2+8+8+2),LMEMBER1  SAVE CURRENT MEMBER NAMES
         MVC   MEMBERP(1),FLAGSAA           SAVE CURRENT FLAGS
         NI    MEMBERP,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         B     VRTMEMX            TERMINATE
         SPACE 2
VRTBAD   LA    R15,4               INVALID MEMBER NAME -- TRY AGAIN
         TM    FLAGSAA,FMEMBER2    PROCESSING MEMBER2?
         BO    *+8                 YES, BRANCH
         NI    FLAGSAA,FF-FMEMBER1 NO, SAY NO MEMBER1 YET
         NI    FLAGSAA,FF-FMEMBER2 ALWAYS SAY NOT MEMBER2 NOW
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP REQUEST?
         BNO   VRTMEMX             NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM-FMEMBER1-FMEMBER2-FMEMRANG
VRTMEMX  EXIT
         SPACE 2
         DROP  R8
PDS71    CSECT
         TITLE 'P D S  --  PDS END                             6/10/85'
EXIT12O  LA    R1,L831                  DATA SET DID NOT OPEN
EXIT12M  TSMSG (R1)                     OUTPUT THE ERROR MESSAGE
EXIT12N  MVI   RETURNCD+3,12            ERROR CODE 12 (NO CLOSE & FREE)
         MVC   ##SUBCOM(8),$END         SIMULATE AN END SUBCOMMAND
         B     RETURN2
         SPACE 1
EXIT8M   TSMSG (R1)                     OUTPUT THE ERROR MESSAGE
EXIT8N   MVI   RETURNCD+3,8             ERROR CODE 8
         MVC   ##SUBCOM(8),$END         SIMULATE AN END SUBCOMMAND
         SPACE 1
RETURN   TM    FLAGSSS,C'1'             RETURN SEEN AN END BEFORE?
         BO    RETURN1                  YES, BRANCH
         OI    FLAGSSS,X'01'            CONVERT 2 --> 3;  4 --> 5
         CLI   FLAGSSS,C'0'             RETURN TRAP SET?
         BH    NEWCMD                   YES, BRANCH
         SPACE 1
RETURN1  BAL   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET TOO
         TM    SPFLAG0,SPFTABLE       DIALOG TABLE UNCLEAN     SS JUL84
         BZ    SPFNO3                 NO                       SS JUL84
         MVI   SPFLAG1,SPFCLEAN       FUNCTION TYPE            SS JUL84
         L     R15,=V(ISPDSPY)                                 SS JUL84
         BALR  R14,R15                                         SS JUL84
         NOP   0                      IGNORE RETURN 0          SS JUL84
SPFNO3   DS    0H                                              SS JUL84
         SPACE 1
RETURN2  OI    FLAGSGG,FRECVCON         STAE RECOVERY DISALLOWED
         ICM   R5,B'1111',RETURNCD      NORMAL EXIT?
         BZ    RETURN4                  YES, BRANCH
         SPACE 2
         XC    PARMLIST(6*4),PARMLIST   CLEAR STACK PARAMETER AREA
         LA    R1,PARMLIST              AREA FOR STACK PARM LIST
         USING IOPL,R1
         SPACE 1
         L     R2,ADDRUPT
         L     R3,ADDRECT
         LA    R4,ATTNECB
         STM   R2,R4,IOPLUPT            INITIALIZE IOPLUPT, IOPLECT,
         DROP  R1
         SPACE 2
         MVI   ATTNECB,0
         L     R15,ADDRSTCK             STACK ROUTINE
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1)),ENTRY=(15)
         SPACE 2
         TCLEARQ INPUT                  CLEAR INPUT BUFFERS
         SPACE 2
RETURN4  STAX  ,                        CANCEL ANY OUTSTANDING STAX
         ESTAE 0                        CANCEL THE ESTAE EXIT
         SPACE 2
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10' OUT OPEN?
         BNO   RETURN5                      NO, BRANCH
         MVI   OPENLIST,X'80'               END OF LIST
         CLOSE (QAMDCB),MF=(E,OPENLIST)     CLOSE THE OUT DATA SET
         SPACE 1
RETURN5  MVI   CONTOPTN,0                   TURN OFF ANY LOGGING
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10' LOG OPEN?
         BNO   RETURN6                      NO, BRANCH
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         SPACE 2
RETURN6  SR    R0,R0
         ICM   R0,B'1000',SUBLIST2      ANY SUBPOOL 24 DATA?
         BZ    RETURN7                  NO, BRANCH
         FREEMAIN R,SP=(0)
         SPACE 2
RETURN7  TM    FLAGSFF,FSPFCALL         TERMINATE SPF?
         BNO   RETURN8                  NO, BRANCH
         NI    FLAGSFF,FF-FSPFCALL      YES, TERMINATE SPF TOO
         MVC   ##SUBCOM(8),$END         SIMULATE END SUBCOMMAND
         LA    R1,RETURN8               END COMMAND FINAL CODE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         L     R13,4(,R13)              PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)          SAVE REGISTERS
         LA    R15,4                    RC=4 -- SIMULATES RETURN KEY
         BR    R14                      RETURN TO SPF
         SPACE 2
RETURN8  TM    FLAGSFF,FSPFDIAL         PDS CALLED BY SPF AS A DIALOG?
         BNO   RETURN12                 NO, BRANCH
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'         SECOND PARAMETER
         LA    R2,=CL8'REFRESH'         THIRD PARAMETER
         STM   R0,R2,MSGTEXT2           SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'         LAST
         LA    R1,MSGTEXT2                  PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15                  LET SPF KNOW
         SPACE 1
         L     R3,SUBPDIAL              START OF GETMAINED AREA
         CLC   16+96+64+8(2,R3),=H'32'  EXPANDED I/O WORK AREA?
         BE    RETURN10                 NO, BRANCH
         L     R1,16+96+64(,R3)         START OF NEW I/O WORK AREA
         LH    R0,16+96+64+8(,R3)       SIZE OF NEW I/O WORK AREA
         ICM   R0,B'1000',SUBPDIAL      SUBPOOL OF NEW I/O WORK AREA
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
RETURN10 LA    R0,16+96+64+16           LENGTH OF CREATED AREAS
         CLC   16+96+64+8(2,R3),=H'32'  I/O WORK AREA RELEASE TOO?
         BNE   *+8                      NO, BRANCH
         LA    R0,16+96+64+16+32        LENGTH OF CREATED AREAS
         ICM   R0,B'1000',SUBPDIAL      SUBPOOL OF CREATED WORK AREAS
         FREEMAIN R,LV=(0),A=(3)
         SPACE 2
RETURN12 SR    R0,R0
         ICM   R0,B'1000',OUTMEMBS      ANY SUBPOOL 22 DATA?
         BZ    RETURN14                 NO, BRANCH
         FREEMAIN R,SP=(0)
         SPACE 2
RETURN14 SR    R0,R0
         ICM   R0,B'1000',SUBLIST1      ANY SUBPOOL 23 DATA?
         BZ    RETURN20                 NO, BRANCH
         FREEMAIN R,SP=(0)
         SPACE 2
RETURN20 L     R1,IOBUFF1               START OF BUFFER AREA
         L     R0,WORKBUFF
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
         LA    R2,ADDRPUTL              FIRST SERVICE ROUTINE ADDRESS
         LA    R3,MODULEN               FIRST LOAD ADDRESS
         SPACE 1
RETURN30 CLI   0(R2),X'D6'              DELETE REQUIRED?
         BNE   RETURN40                 NO, BRANCH
         DELETE  EPLOC=2(R3)
RETURN40 LA    R2,4(,R2)                NEXT SERVICE ADDRESS
         LA    R3,10(,R3)               NEXT MODULES ADDRESS
         CLI   0(R3),X'FF'              END OF MODULES?
         BNE   RETURN30                 NO, BRANCH
         SPACE 2
         L     R1,ADDRCPPL                   ORIGINAL CPPL ADDRESS
         MVC   CPPLCBUF-CPPL(4,R1),ADDRCBU2  RESTORE ORIGINAL CBUF
         LR    R13,R7                   ADDRESS OF THE FIRST SAVE AREA
         LR    R15,R5                   RETURN CODE
         L     R0,WORKSIZE
         SPACE 1
         EXIT  LV=(0)
         TITLE 'P D S  --  PDS PARSE DEFINITIONS               6/10/85'
*
*        P A R S E   C O N T R O L   L I S T
*
         PRINT NOGEN
         SPACE 3
PCLMAIN  IKJPARM DSECT=MAINPDL
INPUTDSN IKJPOSIT DSTHING,USID,VALIDCK=VRTDSN,                         $
               PROMPT='NAME OF DATA SET',                              $
               HELP=('NAME OF THE DATA SET TO BE PROCESSED')
OPTVOL   IKJKEYWD
         IKJNAME 'V',ALIAS=('VO','VOL','VOLUME'),SUBFLD=VOLNAM
OPTSET   IKJKEYWD
         IKJNAME 'VOLSET',SUBFLD=VOLSETS
OPTDISP  IKJKEYWD
         IKJNAME 'OLD'
         IKJNAME 'SHR'
VOLNAM   IKJSUBF
VOLNAME  IKJIDENT 'VOLUME NAME',ASTERISK,MAXLNTH=6,                    $
               FIRST=ALPHANUM,OTHER=ALPHANUM,PROMPT='VOLUME NAME',     $
               HELP=('NAME OF THE VOLUME WHERE THE DATA SET RESIDES')
VOLSETS  IKJSUBF
VOLSETID IKJIDENT 'DEFAULT VOLUME NAME',ASTERISK,MAXLNTH=6,            $
               FIRST=ALPHANUM,OTHER=ALPHANUM,                          $
               PROMPT='DEFAULT VOLUME NAME',                           $
               HELP=('DEFAULT NAME FOR THE VOLUME PARAMETER')
         IKJENDP
PRIMOPTN DC    CL2'OL'      "OLD"
         DC    CL2'SH'      "SHR"
         DC    CL2'VO'      "VOLUME" OR "VOLSET"
         DC    X'FF'        END OF MAIN COMMAND TABLE
         SPACE 3
PCLALIAS IKJPARM DSECT=PDLALIAS
PCLAL1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='CURRENT MEMBER NAME',VALIDCK=VRTMEMBR,          $
               HELP=('NAME OF THE MEMBER TO BE ASSIGNED AN ALIAS')
PCLAL2   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='ALIAS NAME',VALIDCK=VRTMEMBR,                   $
               HELP=('NAME TO BE ASSIGNED AS AN ALIAS')
         IKJENDP
         SPACE 2
PCLATTR  IKJPARM DSECT=PDLATTR
PCLAT1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER WHOSE ATTRIBUTES ARE TO BE LIS$
               TED'),VALIDCK=VRTMEMBR
LKEDNONE IKJKEYWD
         IKJNAME  'NONE'
LKEDREUS IKJKEYWD
         IKJNAME  'REUS'
         IKJNAME  'NOREUS'
LKEDREFR IKJKEYWD
         IKJNAME  'REFR'
         IKJNAME  'NOREFR'
LKEDRENT IKJKEYWD
         IKJNAME  'RENT'
         IKJNAME  'NORENT'
LKEDEXEC IKJKEYWD
         IKJNAME  'EXEC'
         IKJNAME  'NOEXEC'
LKEDLOAD IKJKEYWD
         IKJNAME  'LOADONLY'
         IKJNAME  'NOLOADONLY'
LKEDEDIT IKJKEYWD
         IKJNAME  'EDIT'
         IKJNAME  'NOEDIT'
LKEDDC   IKJKEYWD
         IKJNAME  'DC'
         IKJNAME  'NODC'
LKEDAUTH IKJKEYWD
         IKJNAME  'AUTH'
         IKJNAME  'NOAUTH'
LKEDPAGE IKJKEYWD
         IKJNAME  'PAGE'
         IKJNAME  'NOPAGE'
LKEDRLDF IKJKEYWD
         IKJNAME  'RLDFIX'
         IKJNAME  'NORLDFIX'
LKEDRMOD IKJKEYWD
         IKJNAME  'RMODE24'
         IKJNAME  'RMODEANY'
LKEDAMOD IKJKEYWD
         IKJNAME  'AMODE24'
         IKJNAME  'AMODE31'
         IKJNAME  'AMODEANY'
LKEDSSIK IKJKEYWD
         IKJNAME  'SSI',SUBFLD=LKEDSSIS
         IKJNAME  'NOSSI'
LKEDENTR IKJKEYWD
         IKJNAME  'ENTRY',SUBFLD=AENTRY
INFOALIA IKJKEYWD
         IKJNAME  'A',ALIAS=('ALIASINFO')
         IKJNAME  'NOA',ALIAS=('NOALIASINFO')
INFOLKED IKJKEYWD
         IKJNAME  'LKEDDATE'
         IKJNAME  'NOLKEDDATE'
AENTRY   IKJSUBF
AENTRYS  IKJIDENT 'EXTERNAL NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,       $
               PROMPT='THE EXTERNAL NAME OF THE NEW ENTRY POINT',      $
               HELP=('ONE TO EIGHT BYTES EXTERNAL ENTRY POINT NAME')
LKEDSSIS IKJSUBF
LKEDSSI  IKJIDENT 'SSI DATA',FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=8,  $
               PROMPT='THE NEW OR CHANGED SSI INFORMATION',            $
               HELP=('ONE TO EIGHT BYTES OF HEXADECIMAL SSI DATA')
         IKJENDP
         SPACE 2
PCLBROWS IKJPARM DSECT=PDLBROWS
PCLBR1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER TO BE BROWSED',VALIDCK=VRTMEMBR, $
               HELP=('NAME OF THE MEMBER FOR THE SPF BROWSE COMMAND')
         IKJENDP
         SPACE 2
PCLCOMPA IKJPARM DSECT=PDLCOMPA
PCLCAR1  IKJIDENT 'DSNAME OR MEMBER NAME',FIRST=ANY,OTHER=ANY,         $
               MAXLNTH=56,                                             $
               PROMPT='NAME OF THE ORIGINAL DATA SET OR MEMBER',       $
               HELP=('NAME OF THE ORIGINAL DATA SET OR MEMBER')
PCLCAR2  IKJIDENT 'DSNAME OR MEMBER NAME',FIRST=ANY,OTHER=ANY,         $
               MAXLNTH=56,                                             $
               PROMPT='NAME OF THE CHANGED DATA SET OR MEMBER',        $
               HELP=('NAME OF THE CHANGED DATA SET OR MEMBER')
PCLCO2   IKJPOSIT SPACE
PCLCOSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLCOMPR IKJPARM DSECT=PDLCOMPR
CMXLIST  IKJKEYWD
         IKJNAME  'S',ALIAS=('SUMMARY')
         IKJNAME  'LIST'
         IKJNAME  'NOLIST'
CMXSHARE IKJKEYWD
         IKJNAME  'SHR'
         IKJENDP
         SPACE 2
PCLCONT  IKJPARM DSECT=PDLCONT
PALINCON IKJKEYWD
         IKJNAME  'ALIASINFO'
         IKJNAME  'NOALIASINFO'
PLKEDCON IKJKEYWD
         IKJNAME  'LKEDDATE'
         IKJNAME  'NOLKEDDATE'
PTRANCON IKJKEYWD
         IKJNAME  'T',ALIAS=('TRANSLATOR')
         IKJNAME  'NOTRANSLATOR'
PPROMCON IKJKEYWD
         IKJNAME  'PROMPT'
         IKJNAME  'NOPROMPT'
PRECVCON IKJKEYWD
         IKJNAME  'RECOVER'
         IKJNAME  'NORECOVER'
PDOUBCON IKJKEYWD
         IKJNAME  'D',ALIAS=('DOUBLE')
         IKJNAME  'S',ALIAS=('SINGLE')
         IKJNAME  'M',ALIAS=('MULTIPLE')
CLOGTYPE IKJKEYWD
         IKJNAME  'SYSOUT',SUBFLD=CSYSOUTO
         IKJNAME  'NOS',ALIAS=('NOSYSOUT')
         IKJNAME  'DSNAME',SUBFLD=CDSNAMEO
         IKJNAME  'NODSNAME'
CLOGDISP IKJKEYWD
         IKJNAME  'OLD'
         IKJNAME  'SHR'
         IKJNAME  'MOD'
         IKJNAME  'NEW'
CLOGFORM IKJKEYWD
         IKJNAME  'FORMS',SUBFLD=CFORMO
         IKJNAME  'NOFORMS'
CLOGDEST IKJKEYWD
         IKJNAME  'DEST',SUBFLD=CDESTO
         IKJNAME  'NODEST'
PIMMCON  IKJKEYWD
         IKJNAME  'TESTCPUOOP'
         IKJNAME  'TESTABEND'
         IKJNAME  'TESTOUTLOOP'
         IKJNAME  'TESTMSGS'
         IKJNAME  'IOSTATS'
         IKJNAME  'TESTREAD'
CDESTO   IKJSUBF
CDESTT   IKJIDENT 'DESTINATION',FIRST=ALPHANUM,OTHER=ALPHANUM,         $
               MAXLNTH=8,PROMPT='DESTINATION NAME',                    $
               HELP=('ONE TO EIGHT CHARACTER SYSOUT DESTINATION')
CFORMO   IKJSUBF
CFORMT   IKJIDENT 'FORMS NAME',FIRST=ALPHANUM,OTHER=ALPHANUM,          $
               MAXLNTH=4,PROMPT='FORMS NAME',                          $
               HELP=('ONE TO FOUR CHARACTER FORMS NAME')
CDSNAMEO IKJSUBF
CDSNAMET IKJPOSIT DSNAME,USID,PROMPT='OUTPUT DATA SET NAME',           $
               HELP=('THE NAME OF THE SESSION OUTPUT DATA SET')
CSYSOUTO IKJSUBF
CSYSOUTT IKJIDENT 'SYSOUT CLASS',FIRST=ALPHANUM,                       $
               MAXLNTH=1,PROMPT='SYSOUT CLASS NAME',                   $
               HELP=('ONE CHARACTER SYSOUT CLASS NAME')
         IKJENDP
         SPACE 2
PCLDELET IKJPARM DSECT=PDLDELET
PCLS1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER TO BE DELETED',VALIDCK=VRTMEMBR, $
               HELP=('NAME OF THE MEMBER TO BE SCRATCHED')
PCLDELAL IKJKEYWD
         IKJNAME  'ALIAS'
         IKJNAME  'NOALIAS'
         IKJENDP
         SPACE 2
PCLDIR   IKJPARM DSECT=PDLDIR
PCLDIR1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,HELP=('NAME OF $
               THE MEMBER WHOSE DIRECTORY ENTRY IS TO BE DUMPED')
         IKJENDP
         SPACE 2
PCLDSNA  IKJPARM DSECT=PDLDSNA
PCLDSN   IKJKEYWD
         IKJNAME   'JCL',ALIAS=('BATCH')
         IKJNAME   'T',ALIAS=('TSO')
         IKJNAME   'MSG',ALIAS=('TABLE','TBL')
         IKJENDP
         SPACE 2
PCLDSPLY IKJPARM DSECT=PDL1MEM
PCLD1    IKJIDENT 'MEMBER NAME',MAXLNTH=39,                            $
               FIRST=ANY,OTHER=ANY,VALIDCK=VRTMEMBR
PCLD2    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ANY,OTHER=ANY,VALIDCK=VRTMEMBR
         IKJENDP
         SPACE 2
PCLEDIT  IKJPARM DSECT=PDLEDIT
PCLED1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER TO BE EDITED',VALIDCK=VRTMEMBR,  $
               HELP=('NAME OF THE MEMBER FOR THE SPF EDIT COMMAND')
SPFNEWOP IKJKEYWD
         IKJNAME  'NEW'
         IKJNAME  'OLD'
SPFMAC   IKJKEYWD
         IKJNAME  'MACRO',SUBFLD=SPFMACR
SPFPRO   IKJKEYWD
         IKJNAME  'PROFILE',SUBFLD=SPFPROF
SPFPRS   IKJKEYWD
         IKJNAME  'SETPROF',SUBFLD=SPFPRSE
SPFMACR  IKJSUBF
SPFMACRO IKJIDENT 'MACRO NAME',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=8,   $
               PROMPT='MACRO NAME',HELP=('NAME OF A EDIT MACRO')
SPFPROF  IKJSUBF
SPFPROFL IKJIDENT 'PROFILE NAME',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=8, $
               PROMPT='PROFILE NAME',HELP=('NAME OF A EDIT PROFILE')
SPFPRSE  IKJSUBF
SPFPRSET IKJIDENT 'DEFAULT PROFILE',ASTERISK,MAXLNTH=8,                $
               FIRST=ALPHANUM,OTHER=ALPHANUM,PROMPT='DEFAULT PROFILE', $
               HELP=('NAME OF A DEFAULT EDIT PROFILE')
         IKJENDP
         SPACE 2
PCLFIND  IKJPARM DSECT=PDLFIND
PCLFIND1 IKJIDENT 'MEMBER NAME',MAXLNTH=39,FIRST=ANY,OTHER=ANY,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,               $
               HELP=('NAME OF THE MEMBER TO BE SEARCHED')
FINDDLM1 IKJPOSIT DELIMITER
FINDSTRG IKJPOSIT STRING,SQSTRING,ASIS
FINDFMT  IKJKEYWD
         IKJNAME  'NUM'                   FLAGSDD=X'00'
         IKJNAME  'NONUM'                 FLAGSDD=X'01'
         IKJNAME  'SNUM'                  FLAGSDD=X'02'
         IKJNAME  'LBLOCK'                FLAGSDD=X'04'
         IKJNAME  'LDUMP'                 FLAGSDD=X'08'
         IKJNAME  'BLOCK'                 FLAGSDD=X'10'
         IKJNAME  'DUMP'                  FLAGSDD=X'20'
FINDSEC  IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=FDUMPC
FINDOFFT IKJKEYWD
         IKJNAME  'OFFSET',SUBFLD=FINDOFFS
FINDLIST IKJKEYWD
         IKJNAME  'FIRST'
         IKJNAME  'NOFIRST'
FINDASIS IKJKEYWD
         IKJNAME  'ASIS'
         IKJNAME  'CAPS'
FMAXLEN  IKJKEYWD
         IKJNAME  'MAXLEN',SUBFLD=FMAXLENS
FMAXIN   IKJKEYWD
         IKJNAME  'MAXIN',SUBFLD=FMAXINS
FMAXOUT  IKJKEYWD
         IKJNAME  'MAXOUT',SUBFLD=FMAXOUTS
FMAXFIN  IKJKEYWD
         IKJNAME  'MAXFIND',SUBFLD=FMAXFINS
FSKIPR   IKJKEYWD
         IKJNAME  'SKIPREC',SUBFLD=FSKIPRS
FSKIPC   IKJKEYWD
         IKJNAME  'SKIPCOL',SUBFLD=FSKIPCS
FTHENC   IKJKEYWD
         IKJNAME  'THEN',SUBFLD=FTHENCS
FELSEC   IKJKEYWD
         IKJNAME  'ELSE',SUBFLD=FELSECS
FDUMPC   IKJSUBF
FDUMPCS  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
FINDOFFS IKJSUBF
FINDOFF  IKJIDENT 'MODULE OFFSET',FIRST=ALPHANUM,OTHER=ALPHANUM,       $
               MAXLNTH=6,PROMPT='HEXADECIMAL OFFSET',HELP=('A ONE TO SI$
               X DIGIT HEXADECIMAL OFFSET INTO THE LOAD MODULE')
FMAXLENS IKJSUBF
FMAXLENC IKJIDENT 'MAXIMUM LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM LENGTH',MAXLNTH=5,                      $
               HELP=('MAXIMUM LENGTH OF EACH INPUT RECORD')
FMAXINS  IKJSUBF
FMAXINC  IKJIDENT 'MAXIMUM INPUT',FIRST=NUMERIC,OTHER=NUMERIC,         $
               PROMPT='MAXIMUM INPUT COUNT',MAXLNTH=7,                 $
               HELP=('MAXIMUM NUMBER OF RECORDS TO INPUT')
FMAXOUTS IKJSUBF
FMAXOUTC IKJIDENT 'MAXIMUM OUTPUT',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM OUTPUT RECORDS',MAXLNTH=7,              $
               HELP=('MAXIMUM NUMBER OF LINES OR PARTIAL RECORDS TO DIS$
               PLAY')
FMAXFINS IKJSUBF
FMAXFIND IKJIDENT 'MAXIMUM FIND STRINGS',FIRST=NUMERIC,OTHER=NUMERIC,  $
               PROMPT='MAXIMUM FIND STRINGS',MAXLNTH=7,                $
               HELP=('MAXIMUM NUMBER OF STRINGS TO FIND BEFORE TERMINAT$
               ING')
FSKIPRS  IKJSUBF
FSKIPRC  IKJIDENT 'SKIP COUNT',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='RECORDS TO SKIP',MAXLNTH=7,                     $
               HELP=('NUMBER OF INPUT RECORDS TO IGNORE')
FSKIPCS  IKJSUBF
FSKIPCC  IKJIDENT 'SKIP COLUMNS',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='COLUMNS TO SKIP',MAXLNTH=5,                     $
               HELP=('THE NUMBER OF BEGINNING COLUMNS TO IGNORE')
FTHENCS  IKJSUBF
FTHENCC  IKJKEYWD
&NATT    CONDIKJ '&NATT'
&NBRO    CONDIKJ '&NBRO'
&NDIR    CONDIKJ '&NDIR'
&NDEL    CONDIKJ '&NDEL'
&NEDI    CONDIKJ '&NEDI'
&NEND    CONDIKJ '&NEND'
&NFIN    CONDIKJ '&NFIN'
&NFSE    CONDIKJ '&NFSE'
&NHIS    CONDIKJ '&NHIS'
&NLIS    CONDIKJ '&NLIS'
&NMAP    CONDIKJ '&NMAP'
&NMEM    CONDIKJ '&NMEM'
&NMML    CONDIKJ '&NMML'
&NOUT    CONDIKJ '&NOUT'
&NPRI    CONDIKJ '&NPRI'
&NREV    CONDIKJ '&NREV'
&NSPF    CONDIKJ '&NSPF'
&NSUB    CONDIKJ '&NSUB'
&NSUL    CONDIKJ '&NSUL'
&NTSE    CONDIKJ '&NTSE'
&NTSL    CONDIKJ '&NTSL'
&NVER    CONDIKJ '&NVER'
         SPACE 1
FELSECS  IKJSUBF
FELSECC  IKJKEYWD
&NATT    CONDIKJ '&NATT'
&NBRO    CONDIKJ '&NBRO'
&NDIR    CONDIKJ '&NDIR'
&NDEL    CONDIKJ '&NDEL'
&NEDI    CONDIKJ '&NEDI'
&NEND    CONDIKJ '&NEND'
&NFIN    CONDIKJ '&NFIN'
&NFSE    CONDIKJ '&NFSE'
&NHIS    CONDIKJ '&NHIS'
&NLIS    CONDIKJ '&NLIS'
&NMAP    CONDIKJ '&NMAP'
&NMEM    CONDIKJ '&NMEM'
&NMML    CONDIKJ '&NMML'
&NOUT    CONDIKJ '&NOUT'
&NPRI    CONDIKJ '&NPRI'
&NREV    CONDIKJ '&NREV'
&NSPF    CONDIKJ '&NSPF'
&NSUB    CONDIKJ '&NSUB'
&NSUL    CONDIKJ '&NSUL'
&NTSE    CONDIKJ '&NTSE'
&NTSL    CONDIKJ '&NTSL'
&NVER    CONDIKJ '&NVER'
         IKJENDP
         SPACE 2
PCLFIX   IKJPARM DSECT=PDLFIX
PCLFIXOP IKJKEYWD
         IKJNAME  'DCB'
         IKJNAME  'EXPANDDIR',SUBFLD=RDIRZ
         IKJNAME  'RESETDIR',SUBFLD=RDIRZ
PCLRELOP IKJKEYWD
         IKJNAME  'RELEASE'
         IKJNAME  'RELSAVE',SUBFLD=RRES
         IKJNAME  'RELEXTENT'
         IKJNAME  'MAXSPACE'
RTYPE    IKJKEYWD
         IKJNAME  'LOAD'
         IKJNAME  'OBJECT'
         IKJNAME  'CLIST'
         IKJNAME  'CNTL'
         IKJNAME  'ASM'
RLRECL   IKJKEYWD
         IKJNAME  'LRECL',SUBFLD=RLRECLZ
RBLKSI   IKJKEYWD
         IKJNAME  'BLKSIZE',SUBFLD=RBLKSIZ,                            $
               ALIAS=('BLOCKSIZE','B','BL')
RRECFM   IKJKEYWD
         IKJNAME  'RECFM',SUBFLD=RRECFMZ
ROPTCD   IKJKEYWD
         IKJNAME  'OPTCD',SUBFLD=ROPTCDZ
         IKJNAME  'NOOPTCD'
RDIRZ    IKJSUBF
RDIRZI   IKJIDENT 'DIRECTORY BLOCKS',FIRST=NUMERIC,OTHER=NUMERIC,      $
               HELP=('NUMBER OF DIRECTORY BLOCKS'),MAXLNTH=5
RRES     IKJSUBF
RRESV    IKJIDENT 'RESERVE TRACKS',FIRST=NUMERIC,OTHER=NUMERIC,        $
               MAXLNTH=5,PROMPT='NUMBER OF TRACKS',                    $
               HELP=('NUMBER OF TRACKS TO RESERVE FOR LATER USE')
RLRECLZ  IKJSUBF
RLRECLZI IKJIDENT 'LOGICAL RECORD LENGTH',FIRST=NUMERIC,OTHER=NUMERIC, $
               PROMPT='LOGICAL RECORD LENGTH',MAXLNTH=5,               $
               HELP=('DATA SET LOGICAL RECORD LENGTH DESIRED')
RBLKSIZ  IKJSUBF
RBLKSIZI IKJIDENT 'BLOCK SIZE',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='BLOCK SIZE',MAXLNTH=5,                          $
               HELP=('DATA SET BLOCKSIZE DESIRED')
RRECFMZ  IKJSUBF
RRECFMZI IKJKEYWD
         IKJNAME  'F'       X'80'     B'1000 0000'
         IKJNAME  'FB'      X'90'     B'1001 0000'
         IKJNAME  'FT'      X'A0'     B'1010 0000'
         IKJNAME  'FBT'     X'B0'     B'1011 0000'
         IKJNAME  'FA'      X'84'     B'1000 0100'
         IKJNAME  'FBA'     X'94'     B'1001 0100'
         IKJNAME  'FTA'     X'A4'     B'1010 0100'
         IKJNAME  'FBTA'    X'B4'     B'1011 0100'
         IKJNAME  'FM'      X'82'     B'1000 0010'
         IKJNAME  'FBM'     X'92'     B'1001 0010'
         IKJNAME  'FTM'     X'A2'     B'1010 0010'
         IKJNAME  'FBTM'    X'B2'     B'1011 0010'
         IKJNAME  'V'       X'40'     B'0100 0000'
         IKJNAME  'VB'      X'50'     B'0101 0000'
         IKJNAME  'VT'      X'60'     B'0110 0000'
         IKJNAME  'VBT'     X'70'     B'0111 0000'
         IKJNAME  'VA'      X'44'     B'0100 0100'
         IKJNAME  'VBA'     X'54'     B'0101 0100'
         IKJNAME  'VTA'     X'64'     B'0110 0100'
         IKJNAME  'VBTA'    X'74'     B'0111 0100'
         IKJNAME  'VM'      X'42'     B'0100 0010'
         IKJNAME  'VBM'     X'52'     B'0101 0010'
         IKJNAME  'VTM'     X'62'     B'0110 0010'
         IKJNAME  'VBTM'    X'72'     B'0111 0010'
         IKJNAME  'U'       X'C0'     B'1100 0000'
         IKJNAME  'UT'      X'E0'     B'1110 0000'
         IKJNAME  'UA'      X'C4'     B'1100 0100'
         IKJNAME  'UM'      X'C2'     B'1100 0010'
         IKJNAME  'UTA'     X'E4'     B'1110 0100'
         IKJNAME  'UTM'     X'E2'     B'1110 0010'
ROPTCDZ  IKJSUBF
ROPTCDZI IKJKEYWD
         IKJNAME  'W'       X'80'
         IKJNAME  'WC'      X'A0'
         IKJNAME  'WS'      X'84'
         IKJNAME  'WCZ'     X'A4'
         IKJNAME  'WZC'     X'A4'
         IKJNAME  'C'       X'20'
         IKJNAME  'CS'      X'24'
         IKJNAME  'CW'      X'A0'
         IKJNAME  'CWZ'     X'A4'
         IKJNAME  'CZW'     X'A4'
         IKJNAME  'Z'       X'04'
         IKJNAME  'ZC'      X'24'
         IKJNAME  'ZW'      X'84'
         IKJNAME  'ZWC'     X'A4'
         IKJNAME  'ZCW'     X'A4'
         IKJENDP
         SPACE 2
PCLHIST  IKJPARM DSECT=PDLHIST
PCLH1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,HELP=('NAME OF $
               THE LOAD MODULE WHOSE HISTORY IS TO BE LISTED')
PCLHTRNK IKJKEYWD
         IKJNAME  'TRAN',SUBFLD=PCLHTRNS
PCLHZAPK IKJKEYWD
         IKJNAME  'ZAP',SUBFLD=PCLHZAPS
PCLHUSRK IKJKEYWD
         IKJNAME  'USER',SUBFLD=PCLHUSRS
         IKJNAME  'SYSMOD',SUBFLD=PCLHUSRS
PCLHLKDK IKJKEYWD
         IKJNAME  'LKED',SUBFLD=PCLHLKDS
PCLHMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=PCLHMODS
PCLHMODS IKJSUBF
PCLHMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
PCLHUSRS IKJSUBF
PCLHUSR  IKJIDENT 'IDR USER DATA',FIRST=ANY,OTHER=ANY,MAXLNTH=8,       $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF IDR USER DATA')
PCLHLKDS IKJSUBF
PCLHLKD  IKJIDENT 'LINKAGE EDITOR',FIRST=ANY,OTHER=ANY,                $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A LINKA$
               GE EDITOR NAME')
PCLHZAPS IKJSUBF
PCLHZAP  IKJIDENT 'ZAP IDRDATA NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,    $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF ZAP IDRDATA')
PCLHTRNS IKJSUBF
PCLHTRN  IKJIDENT 'TRANSLATOR NAME',FIRST=ANY,OTHER=ANY,               $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A TRANS$
               LATOR (ASSEMBLER OR COMPILER) NAME')
         IKJENDP
         SPACE 2
PCLIFTTR IKJPARM DSECT=PDLIFTTR
PCLIFT1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER WHOSE ATTRIBUTES ARE TO BE CHE$
               CKED'),VALIDCK=VRTMEMBR
IFLKREUS IKJKEYWD
         IKJNAME  'REUS'
         IKJNAME  'NOREUS'
IFLKREFR IKJKEYWD
         IKJNAME  'REFR'
         IKJNAME  'NOREFR'
IFLKRENT IKJKEYWD
         IKJNAME  'RENT'
         IKJNAME  'NORENT'
IFLKEXEC IKJKEYWD
         IKJNAME  'EXEC'
         IKJNAME  'NOEXEC'
IFLKLOAD IKJKEYWD
         IKJNAME  'LOADONLY'
         IKJNAME  'NOLOADONLY'
IFLKEDIT IKJKEYWD
         IKJNAME  'EDIT'
         IKJNAME  'NOEDIT'
IFLKAUTH IKJKEYWD
         IKJNAME  'AUTH'
         IKJNAME  'NOAUTH'
IFLKPAGE IKJKEYWD
         IKJNAME  'PAGE'
         IKJNAME  'NOPAGE'
IFLKOVER IKJKEYWD
         IKJNAME  'OVERLAY'
         IKJNAME  'NOOVERLAY'
IFLKTEST IKJKEYWD
         IKJNAME  'TEST'
         IKJNAME  'NOTEST'
IFRLD0   IKJKEYWD
         IKJNAME  'RLDZERO'
         IKJNAME  'NORLDZERO'
IFAPFERR IKJKEYWD
         IKJNAME  'APFERR'
         IKJNAME  'NOAPFERR'
IFRLDERR IKJKEYWD
         IKJNAME  'RLDERR'
         IKJNAME  'NORLDERR'
IFLKSCTR IKJKEYWD
         IKJNAME  'SCTR'
         IKJNAME  'NOSCTR'
IFLKLKED IKJKEYWD
         IKJNAME  'FLEVEL'
         IKJNAME  'NOFLEVEL'
IFLKVSLE IKJKEYWD
         IKJNAME  'VSLKED'
         IKJNAME  'NOVSLKED'
IFLKDC   IKJKEYWD
         IKJNAME  'DC'
         IKJNAME  'NODC'
IFLKALIA IKJKEYWD
         IKJNAME  'ALIAS'
         IKJNAME  'NOALIAS'
IFLOADER IKJKEYWD
         IKJNAME  'LOADERR'
         IKJNAME  'NOLOADERR'
IFLKEDER IKJKEYWD
         IKJNAME  'LKEDERR'
         IKJNAME  'NOLKEDERR'
IFIOERR  IKJKEYWD
         IKJNAME  'IOERR'
         IKJNAME  'NOIOERR'
IFBLKERR IKJKEYWD
         IKJNAME  'BLOCKERR'
         IKJNAME  'NOBLOCKERR'
IFLRECLE IKJKEYWD
         IKJNAME  'LRECLERR'
         IKJNAME  'NOLRECLERR'
IFNULL   IKJKEYWD
         IKJNAME  'NULL'
         IKJNAME  'NONULL'
IFAALIAS IKJKEYWD
         IKJNAME  'APPARENTALIAS'
         IKJNAME  'NOAPPARENTALIAS'
IFORPHAN IKJKEYWD
         IKJNAME  'ORPHAN'
         IKJNAME  'NOORPHAN'
IFHASALI IKJKEYWD
         IKJNAME  'HASALIAS'
         IKJNAME  'NOHASALIAS'
IFSPFEDT IKJKEYWD
         IKJNAME  'SPFEDIT'
         IKJNAME  'NOSPFEDIT'
IFNAMERR IKJKEYWD
         IKJNAME  'NAMEERR'
         IKJNAME  'NONAMEERR'
IFRLDF   IKJKEYWD
         IKJNAME  'RLDFIX'
         IKJNAME  'NORLDFIX'
IFRMODE  IKJKEYWD
         IKJNAME  'RMODE24'
         IKJNAME  'RMODEANY'
         IKJNAME  'NORMODE24'
         IKJNAME  'NORMODEANY'
IFAMODE  IKJKEYWD
         IKJNAME  'AMODE24'
         IKJNAME  'AMODE31'
         IKJNAME  'AMODEANY'
         IKJNAME  'NOAMODE24'
         IKJNAME  'NOAMODE31'
         IKJNAME  'NOAMODEANY'
IFLKSSI  IKJKEYWD
         IKJNAME  'SSI',SUBFLD=IFTSSIS
         IKJNAME  'NOSSI'
IFTUSERK IKJKEYWD
         IKJNAME  'USERID',SUBFLD=IFTUSERS
         IKJNAME  'NOUSERID'
         IKJNAME  'NOTUSERID',SUBFLD=IFTUSERS
IFTHENC  IKJKEYWD
         IKJNAME  'THEN',SUBFLD=IFTHENCS
IFELSEC  IKJKEYWD
         IKJNAME  'ELSE',SUBFLD=IFELSECS
IFTLTRNK IKJKEYWD
         IKJNAME  'TRAN',SUBFLD=IFTLTRNS
IFTLZAPK IKJKEYWD
         IKJNAME  'ZAP',SUBFLD=IFTLZAPS
IFTLUSRK IKJKEYWD
         IKJNAME  'USER',SUBFLD=IFTLUSRS
         IKJNAME  'SYSMOD',SUBFLD=IFTLUSRS
IFTLMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=IFTLMODS
IFTLKEDK IKJKEYWD
         IKJNAME  'LKED',SUBFLD=IFTLKEDS
IFTTTRK  IKJKEYWD
         IKJNAME  'TTR',SUBFLD=IFTTTRS
IFABOKY  IKJKEYWD
         IKJNAME  'ABOVE',SUBFLD=IFABOSB
IFBELKY  IKJKEYWD
         IKJNAME  'BELOW',SUBFLD=IFBELSB
IFCREKY  IKJKEYWD
         IKJNAME  'CREATED',SUBFLD=IFCRESB
IFMODKY  IKJKEYWD
         IKJNAME  'CHANGED',SUBFLD=IFMODSB
IFMODSB  IKJSUBF
IFMODSUB IKJIDENT 'MODIFICATION DATE RANGE',                           $
               RANGE,FIRST=ANY,OTHER=ANY,CHAR,                         $
               PROMPT='DATE CHANGED',MAXLNTH=17,VALIDCK=VRTDATE,       $
               HELP=('THE MODIFICATION DATE RANGE TO SEARCH -- 6/10/85:$
               10/5/85')
IFCRESB  IKJSUBF
IFCRESUB IKJIDENT 'CREATION DATE RANGE',                               $
               RANGE,FIRST=ALPHANUM,OTHER=ANY,                         $
               PROMPT='DATE CREATED',MAXLNTH=17,VALIDCK=VRTDATE,       $
               HELP=('THE CREATION DATE RANGE TO SEARCH -- 6/10/85:10/5$
               /85')
IFABOSB  IKJSUBF
IFABOSUB IKJIDENT 'MINIMUM SIZE',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='MINIMUM SIZE',MAXLNTH=9,                        $
               HELP=('MINIMUM SIZE OF A DESIRED MEMBER')
IFBELSB  IKJSUBF
IFBELSUB IKJIDENT 'MAXIMUM SIZE',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='MAXIMUM SIZE',MAXLNTH=9,                        $
               HELP=('MAXIMUM SIZE OF A DESIRED MEMBER')
IFTTTRS  IKJSUBF
IFTTTR   IKJIDENT 'TTR RANGE',RANGE,FIRST=ALPHANUM,OTHER=ALPHANUM,     $
               MAXLNTH=6,PROMPT='HEXADECIMAL TTR',HELP=('THE LOW:HIGH H$
               ADECIMAL TTR ADDRESS RANGE OF THE MEMBER ENTRIES')
IFTLKEDS IKJSUBF
IFTLKED  IKJIDENT 'LINKAGE EDITOR',FIRST=ANY,OTHER=ANY,                $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A LINKA$
               GE EDITOR NAME')
IFTLMODS IKJSUBF
IFTLMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
IFTLUSRS IKJSUBF
IFTLUSR  IKJIDENT 'IDR USER DATA',FIRST=ANY,OTHER=ANY,MAXLNTH=8,       $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF IDR USER DATA')
IFTLZAPS IKJSUBF
IFTLZAP  IKJIDENT 'ZAP IDRDATA NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,    $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF ZAP IDRDATA')
IFTLTRNS IKJSUBF
IFTLTRN  IKJIDENT 'TRANSLATOR NAME',FIRST=ANY,OTHER=ANY,               $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A TRANS$
               LATOR (ASSEMBLER OR COMPILER) NAME')
IFTUSERS IKJSUBF
IFTUSER  IKJIDENT 'TSO USERID',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=7,   $
               HELP=('FIRST ONE TO SEVEN CHARACTERS OF A TSO USERID')
IFTSSIS  IKJSUBF
IFTSSI   IKJIDENT 'SSI DATA',FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=8,  $
               HELP=('ONE TO EIGHT BYTES OF HEXADECIMAL SSI DATA')
IFTHENCS IKJSUBF
IFTHENCC IKJKEYWD
&NATT    CONDIKJ '&NATT'
&NBRO    CONDIKJ '&NBRO'
&NDIR    CONDIKJ '&NDIR'
&NDEL    CONDIKJ '&NDEL'
&NEDI    CONDIKJ '&NEDI'
&NEND    CONDIKJ '&NEND'
&NFIN    CONDIKJ '&NFIN'
&NFSE    CONDIKJ '&NFSE'
&NHIS    CONDIKJ '&NHIS'
&NLIS    CONDIKJ '&NLIS'
&NMAP    CONDIKJ '&NMAP'
&NMEM    CONDIKJ '&NMEM'
&NMML    CONDIKJ '&NMML'
&NOUT    CONDIKJ '&NOUT'
&NPRI    CONDIKJ '&NPRI'
&NREV    CONDIKJ '&NREV'
&NSPF    CONDIKJ '&NSPF'
&NSUB    CONDIKJ '&NSUB'
&NSUL    CONDIKJ '&NSUL'
&NTSE    CONDIKJ '&NTSE'
&NTSL    CONDIKJ '&NTSL'
&NVER    CONDIKJ '&NVER'
         SPACE 1
IFELSECS IKJSUBF
IFELSECC IKJKEYWD
&NATT    CONDIKJ '&NATT'
&NBRO    CONDIKJ '&NBRO'
&NDIR    CONDIKJ '&NDIR'
&NDEL    CONDIKJ '&NDEL'
&NEDI    CONDIKJ '&NEDI'
&NEND    CONDIKJ '&NEND'
&NFIN    CONDIKJ '&NFIN'
&NFSE    CONDIKJ '&NFSE'
&NHIS    CONDIKJ '&NHIS'
&NLIS    CONDIKJ '&NLIS'
&NMAP    CONDIKJ '&NMAP'
&NMEM    CONDIKJ '&NMEM'
&NMML    CONDIKJ '&NMML'
&NOUT    CONDIKJ '&NOUT'
&NPRI    CONDIKJ '&NPRI'
&NREV    CONDIKJ '&NREV'
&NSPF    CONDIKJ '&NSPF'
&NSUB    CONDIKJ '&NSUB'
&NSUL    CONDIKJ '&NSUL'
&NTSE    CONDIKJ '&NTSE'
&NTSL    CONDIKJ '&NTSL'
&NVER    CONDIKJ '&NVER'
         IKJENDP
         SPACE 2
PCLISPF  IKJPARM DSECT=PDLISPF
PCLISSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLISPM  IKJPARM DSECT=PDLISPM
IKCHKMAX IKJKEYWD
         IKJNAME  'CHKMAX',SUBFLD=ISCHKMA
IKCMDMAX IKJKEYWD
         IKJNAME  'CMDMAX',SUBFLD=ISCMDMA
IKTBLMAX IKJKEYWD
         IKJNAME  'TBLMAX',SUBFLD=ISTBLMA
ISCHKMA  IKJSUBF
ISCHKMAX IKJIDENT 'CHECKPOINT SIZE',FIRST=NUMERIC,OTHER=NUMERIC,       $
               PROMPT='CHECKPOINT SIZE',MAXLNTH=5,                     $
               HELP=('NUMBER OF OUTPUT LINES BETWEEN CHECKPOINTS')
ISCMDMA  IKJSUBF
ISCMDMAX IKJIDENT 'LINES FOR A COMMAND',FIRST=NUMERIC,OTHER=NUMERIC,   $
               PROMPT='MAXIMUM COMMAND OUTPUT LINES',MAXLNTH=5,        $
               HELP=('MAXIMUM OUTPUT LINES FOR ONE COMMAND')
ISTBLMA  IKJSUBF
ISTBLMAX IKJIDENT 'DIALOG TABLE SIZE',FIRST=NUMERIC,OTHER=NUMERIC,     $
               PROMPT='DIALOG TABLE SIZE',MAXLNTH=5,                   $
               HELP=('MAXIMUM NUMBER OF LINES IN THE DIALOG TABLE')
         IKJENDP
         SPACE 2
PCLLIST  IKJPARM DSECT=PDLLIST
PCLL1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER TO BE LISTED'),               $
               VALIDCK=VRTMEMBR
LISTFMT  IKJKEYWD
         IKJNAME  'NUM'                   FLAGSDD=X'00'
         IKJNAME  'NONUM'                 FLAGSDD=X'01'
         IKJNAME  'SNUM'                  FLAGSDD=X'02'
         IKJNAME  'LBLOCK'                FLAGSDD=X'04'
         IKJNAME  'LDUMP'                 FLAGSDD=X'08'
         IKJNAME  'BLOCK'                 FLAGSDD=X'10'
         IKJNAME  'DUMP'                  FLAGSDD=X'20'
LISTSEC  IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=LDUMPC
LISTOFFT IKJKEYWD
         IKJNAME  'OFFSET',SUBFLD=LISTOFFS
LMAXLEN  IKJKEYWD
         IKJNAME  'MAXLEN',SUBFLD=LMAXLENS
LMAXIN   IKJKEYWD
         IKJNAME  'MAXIN',SUBFLD=LMAXINS
LMAXOUT  IKJKEYWD
         IKJNAME  'MAXOUT',SUBFLD=LMAXOUTS
LSKIPR   IKJKEYWD
         IKJNAME  'SKIPREC',SUBFLD=LSKIPRS
LSKIPC   IKJKEYWD
         IKJNAME  'SKIPCOL',SUBFLD=LSKIPCS
LDUMPC   IKJSUBF
LDUMPCS  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
LISTOFFS IKJSUBF
LISTOFF  IKJIDENT 'MODULE OFFSET',FIRST=ALPHANUM,OTHER=ALPHANUM,       $
               MAXLNTH=6,PROMPT='HEXADECIMAL OFFSET',HELP=('A ONE TO SI$
               X DIGIT HEXADECIMAL OFFSET INTO THE LOAD MODULE')
LMAXLENS IKJSUBF
LMAXLENC IKJIDENT 'MAXIMUM LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM LENGTH',MAXLNTH=5,                      $
               HELP=('MAXIMUM LENGTH OF EACH INPUT RECORD')
LMAXINS  IKJSUBF
LMAXINC  IKJIDENT 'MAXIMUM INPUT',FIRST=NUMERIC,OTHER=NUMERIC,         $
               PROMPT='MAXIMUM INPUT COUNT',MAXLNTH=7,                 $
               HELP=('MAXIMUM NUMBER OF RECORDS TO INPUT')
LMAXOUTS IKJSUBF
LMAXOUTC IKJIDENT 'MAXIMUM OUTPUT',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM OUTPUT RECORDS',MAXLNTH=7,              $
               HELP=('MAXIMUM NUMBER OF RECORDS TO DISPLAY')
LSKIPRS  IKJSUBF
LSKIPRC  IKJIDENT 'SKIP COUNT',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='RECORDS TO SKIP',MAXLNTH=7,                     $
               HELP=('NUMBER OF INPUT RECORDS TO IGNORE')
LSKIPCS  IKJSUBF
LSKIPCC  IKJIDENT 'SKIP COLUMNS',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='COLUMNS TO SKIP',MAXLNTH=5,                     $
               HELP=('THE NUMBER OF BEGINNING COLUMNS TO IGNORE')
         IKJENDP
         SPACE 2
PCLMAP   IKJPARM DSECT=PDLMAP
PCLM1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER TO BE MAPPED',                   $
               HELP=('NAME OF THE MEMBER WHOSE LOAD MODULE MAP IS TO BE$
                PRODUCED'),VALIDCK=VRTMEMBR
PCLMAPOP IKJKEYWD
         IKJNAME  'FULL'
         IKJNAME  'SHORT'
         IKJNAME  'ENTRY'
         IKJNAME  'RELINK'
PCLMMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=PCLMMODS
PCLMMODS IKJSUBF
PCLMMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
         IKJENDP
         SPACE 2
PCLMEMB  IKJPARM DSECT=PDLMEMB
PCLMEM   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='MEMBER GROUP',VALIDCK=VRTMEMBR,HELP=('THE MEMBER$
                GROUP TO BE DISPLAYED')
         IKJENDP
         SPACE 2
PCLMEML  IKJPARM DSECT=PDLMEML
PCLMEMZ  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='MEMBER GROUP',VALIDCK=VRTMEMBR,HELP=('THE MEMBER$
                GROUP TO BE DISPLAYED')
MEMLRES  IKJKEYWD
         IKJNAME  'RESET'
         IKJNAME  'NORESET'
MEMLLKD  IKJKEYWD
         IKJNAME  'LKEDDATE'
         IKJNAME  'NOLKEDDATE'
         IKJENDP
         SPACE 2
PCLOUT   IKJPARM DSECT=PDLOUT
PCLOUT1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,HELP=('NAME OF $
               THE MEMBER TO OUTPUT TO FILE(PDSOUT)')
OUTCLOS  IKJKEYWD
         IKJNAME  'CLOSE'
         IKJNAME  'NOCLOSE'
OUTECHO  IKJKEYWD
         IKJNAME  'ECHO'
         IKJNAME  'NOECHO'
OUTASS   IKJKEYWD
         IKJNAME  'ALIAS'
         IKJNAME  'NOALIAS'
OUTIEB   IKJKEYWD
         IKJNAME  'IEBUPDTE'
         IKJNAME  'NOIEBUPDTE'
OUTOPTN  IKJKEYWD
         IKJNAME  'TEXT',SUBFLD=OUTTEX
         IKJNAME  'NOTEXT'
OUTTEX   IKJSUBF
OUTTEXT  IKJPOSIT QSTRING,PROMPT='OUTPUT IDENTIFIER',                  $
               HELP=('AN IDENTIFICATION STRING FOR OUTPUT SELECT STATEM$
               ENTS')
         IKJENDP
         SPACE 2
PCLPATTR IKJPARM DSECT=PDL2MEM
PCLPAT1  IKJIDENT 'PATTERN NAME',MAXLNTH=39,VALIDCK=VRTMEMBR,LIST,     $
               HELP=('PATTERN CHARACTERS FOR A DIRECTORY SEARCH'),     $
               FIRST=ANY,OTHER=ANY,PROMPT='PATTERN NAME'
PCLPAT2  IKJIDENT 'PATTERN NAME',MAXLNTH=19,VALIDCK=VRTMEMBR,          $
               FIRST=ANY,OTHER=ANY
         IKJENDP
         SPACE 2
PCLPRINT IKJPARM DSECT=PDLPRINT
PCLPR1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,               $
               HELP=('NAME OF THE MEMBER FOR THE COMMAND')
PCLPR2   IKJPOSIT SPACE
PCLPRSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLRENAM IKJPARM DSECT=PDLRENAM
PCLREN1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='CURRENT MEMBER NAME',VALIDCK=VRTMEMBR,          $
               HELP=('NAME OF THE MEMBER TO BE RENAMED')
PCLREN2  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='NEW MEMBER NAME',VALIDCK=VRTMEMBR,              $
               HELP=('A NEW NAME FOR THE MEMBER')
PCLRENSW IKJKEYWD
         IKJNAME  'SWAP'
         IKJNAME  'NOSWAP'
PCLRENGP IKJKEYWD
         IKJNAME  'GROUP'
         IKJNAME  'NOGROUP'
PCLREP   IKJKEYWD
         IKJNAME  'REPBY',SUBFLD=PCLREPB
PCLREPB  IKJSUBF
PCLREPBY IKJIDENT 'REPLACEMENT MEMBER',FIRST=ALPHA,OTHER=ALPHANUM,     $
               MAXLNTH=8,PROMPT='REPLACEMENT MEMBER NAME',             $
               HELP=('NAME OF REPLACEMENT MEMBER')
         IKJENDP
         SPACE 2
PCLREPL  IKJPARM DSECT=PDLREPL
PCLREPLT IKJIDENT 'MEMBER NAME',MAXLNTH=39,FIRST=ANY,OTHER=ANY,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,               $
               HELP=('NAME OF THE MEMBER TO BE CHANGED')
REPLDLM1 IKJPOSIT DELIMITER
REPLSTR1 IKJPOSIT STRING,SQSTRING,ASIS
REPLSTR2 IKJPOSIT STRING,SQSTRING,ASIS
REPLFMT  IKJKEYWD
         IKJNAME  'NUM'                   FLAGSDD=X'00'
         IKJNAME  'NONUM'                 FLAGSDD=X'01'
         IKJNAME  'SNUM'                  FLAGSDD=X'02'
         IKJNAME  'LBLOCK'                FLAGSDD=X'04'
         IKJNAME  'LDUMP'                 FLAGSDD=X'08'
         IKJNAME  'BLOCK'                 FLAGSDD=X'10'
         IKJNAME  'DUMP'                  FLAGSDD=X'20'
REPLSEC  IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=RDUMPC
REPLOFFT IKJKEYWD
         IKJNAME  'OFFSET',SUBFLD=REPLOFFS
REPLREP  IKJKEYWD
         IKJNAME  'WRITE'
         IKJNAME  'NOWRITE'
REPLASIS IKJKEYWD
         IKJNAME  'ASIS'
         IKJNAME  'CAPS'
RMAXLEN  IKJKEYWD
         IKJNAME  'MAXLEN',SUBFLD=RMAXLENS
RMAXIN   IKJKEYWD
         IKJNAME  'MAXIN',SUBFLD=RMAXINS
RMAXOUT  IKJKEYWD
         IKJNAME  'MAXOUT',SUBFLD=RMAXOUTS
RMAXFIN  IKJKEYWD
         IKJNAME  'MAXFIND',SUBFLD=RMAXFINS
RSKIPR   IKJKEYWD
         IKJNAME  'SKIPREC',SUBFLD=RSKIPRS
RSKIPC   IKJKEYWD
         IKJNAME  'SKIPCOL',SUBFLD=RSKIPCS
RDUMPC   IKJSUBF
RDUMPCS  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
REPLOFFS IKJSUBF
REPLOFF  IKJIDENT 'MODULE OFFSET',FIRST=ALPHANUM,OTHER=ALPHANUM,       $
               MAXLNTH=6,PROMPT='HEXADECIMAL OFFSET',HELP=('A ONE TO SI$
               X DIGIT HEXADECIMAL OFFSET INTO THE LOAD MODULE')
RMAXLENS IKJSUBF
RMAXLENC IKJIDENT 'MAXIMUM LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM LENGTH',MAXLNTH=5,                      $
               HELP=('MAXIMUM LENGTH OF EACH INPUT RECORD')
RMAXINS  IKJSUBF
RMAXINC  IKJIDENT 'MAXIMUM INPUT',FIRST=NUMERIC,OTHER=NUMERIC,         $
               PROMPT='MAXIMUM INPUT COUNT',MAXLNTH=7,                 $
               HELP=('MAXIMUM NUMBER OF RECORDS TO INPUT')
RMAXOUTS IKJSUBF
RMAXOUTC IKJIDENT 'MAXIMUM OUTPUT',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM OUTPUT RECORDS',MAXLNTH=7,              $
               HELP=('MAXIMUM NUMBER OF LINES OR PARTIAL RECORDS TO DIS$
               PLAY')
RMAXFINS IKJSUBF
RMAXFIND IKJIDENT 'MAXIMUM FIND STRINGS',FIRST=NUMERIC,OTHER=NUMERIC,  $
               PROMPT='MAXIMUM FIND STRINGS',MAXLNTH=7,                $
               HELP=('MAXIMUM NUMBER OF STRINGS TO FIND AND UPDATE')
RSKIPRS  IKJSUBF
RSKIPRC  IKJIDENT 'SKIP COUNT',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='RECORDS TO SKIP',MAXLNTH=7,                     $
               HELP=('NUMBER OF INPUT RECORDS TO IGNORE')
RSKIPCS  IKJSUBF
RSKIPCC  IKJIDENT 'SKIP COLUMNS',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='COLUMNS TO SKIP',MAXLNTH=5,                     $
               HELP=('THE NUMBER OF BEGINNING COLUMNS TO IGNORE')
         IKJENDP
         SPACE 2
PCLRESTO IKJPARM DSECT=PDLRESTO
PCLRSNAM IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='NAME OF MEMBER TO RESTORE',VALIDCK=VRTMEMBR,    $
               HELP=('NAME OF THE MEMBER TO BE RESURRECTED')
PCLRSREP IKJKEYWD
         IKJNAME  'REPEAT'
         IKJNAME  'NOREPEAT'
PCLRSDIS IKJKEYWD
         IKJNAME  'DISPLAY'
         IKJNAME  'NODISPLAY'
PCLRSPRO IKJKEYWD
         IKJNAME  'PROMPT'
         IKJNAME  'NOPROMPT'
PCLRESTK IKJKEYWD
         IKJNAME  'LIKE',SUBFLD=PCLRESTS
PCLTTR   IKJKEYWD
         IKJNAME  'TTR',SUBFLD=PCLTTRK
PCLRFI   IKJKEYWD
         IKJNAME  'FIND',SUBFLD=PCLRFIN
PCLRMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=PCLRMODS
PCLRMODS IKJSUBF
PCLRMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
PCLRFIN  IKJSUBF
PCLRFIND IKJPOSIT QSTRING,PROMPT='SEARCH STRING',                      $
               HELP=('A SEARCH STRING TO FIND IN RESTORE CANDIDATES')
PCLTTRK  IKJSUBF
PCLTTRKY IKJIDENT 'TTR ADDRESS',FIRST=ALPHANUM,OTHER=ALPHANUM,         $
               PROMPT='HEXADECIMAL TTR',MAXLNTH=6,VALIDCK=VRTMEMBR,    $
               HELP=('THE START ADDRESS OF THE MEMBER TO BE RESTORED')
PCLRESTS IKJSUBF
PCLREST  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=8,  $
               PROMPT='MEMBER NAME',HELP=('NAME OF A MODEL MEMBER')
         IKJENDP
         SPACE 2
PCLSUBL  IKJPARM DSECT=PDLSUBL
PCLSUBH  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='MEMBER GROUP',VALIDCK=VRTMEMBR,HELP=('THE MEMBER$
                GROUP TO BE DEFINED')
         IKJENDP
         SPACE 2
PCLTSOED IKJPARM DSECT=PDLTSOED
PCLTSOE1 IKJIDENT 'MEMBER NAME',MAXLNTH=39,FIRST=ANY,OTHER=ANY,LIST,   $
               PROMPT='NAME OF MEMBER TO EDIT',VALIDCK=VRTMEMBR,       $
               HELP=('NAME OF THE MEMBER FOR THE TSO EDIT COMMAND')
PCLNON   IKJKEYWD
         IKJNAME  'NONUM'
PCLASIS  IKJKEYWD
         IKJNAME  'ASIS'
PCLEDOP  IKJKEYWD
         IKJNAME  'NEW'
         IKJNAME  'OLD'
         SPACE 1
**    THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH "EDITTYPE"
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
PCLTYPE  IKJKEYWD
         IKJNAME  'DATA'
** INSERT INSTALLATION-ADDED DATA SET TYPES HERE AND IN "EDITTYPE"
         IKJNAME  'LIST'        ADDED LOCALLY AS A EDIT DATA SET TYPE
         IKJNAME  'VSBASIC'     NEW VS BASIC
         IKJNAME  'BASIC'
         IKJNAME  'IPLI'
         IKJNAME  'TEXT'
         IKJNAME  'COBOL'
         IKJNAME  'ASM'
         IKJNAME  'CLIST'
         IKJNAME  'CNTL'
         IKJNAME  'PLI'          $  -- DATA TYPE .PLI
* END OF VALID DATA SET TYPES
         IKJNAME  'PLIF'         $        (DEFAULT EDIT TYPE IS PLI)
         IKJNAME  'FORTE'    *
         IKJNAME  'FORTG'    *
         IKJNAME  'FORTGI'   *  -- DATA TYPE .FORT
         IKJNAME  'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
         IKJNAME  'GOFORT'   *
         IKJENDP
         SPACE 2
PCLVERIF IKJPARM DSECT=PDLVERIF
PCLVERMM IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,LIST,   $
               PROMPT='NAME OF MEMBER',VALIDCK=VRTMEMBR,HELP=('NAME OF $
               THE MEMBER TO VERIFIED')
VERLOAD  IKJKEYWD
         IKJNAME  'LOAD'
         IKJNAME  'NOLOAD'
VERINPUT IKJKEYWD
         IKJNAME  'INPUT',ALIAS=('READ')
         IKJNAME  'NOINPUT',ALIAS=('NOREAD')
VERNAME  IKJKEYWD
         IKJNAME  'NAME'
         IKJNAME  'NONAME'
VERUPDT  IKJKEYWD
         IKJNAME  'UPDATE'
         IKJNAME  'NOUPDATE'
VERSTATS IKJKEYWD
         IKJNAME  'STATS'
         IKJNAME  'NOSTATS'
         IKJENDP
         TITLE 'P D S  --  PDS CONSTANTS                       6/10/85'
WORKSIZE DC    0F'0',AL1(1),AL3(LENWORK)           DYNAMIC STORAGE SIZE
WORKBUFF DC    0F'0',AL1(1),AL3(32768*2+47968)     INPUT BUFFER SIZE
BLANKS   DC    0F'0',8X'40'
         SPACE 2
FF       EQU   X'FF'        IMMEDIATE FLAG MASK (FOR NI INSTRUCTIONS)
DEBNMEXT EQU   16           OFFSET IN DEB FOR NUMBER OF EXTENTS
DEBUCBAD EQU   32           OFFSET IN DEB FOR UCB ADDRESS
UCBTYP   EQU   16           OFFSET IN UCB FOR UNIT TYPE WORD
UCBRR    EQU   X'20'        UCB IS ON A SHARED DASD DEVICE
UCBVOLI  EQU   28           OFFSET IN UCB FOR DISK VOLUME NAME
         SPACE 3
EXCPDCB  DCB   DEVD=DA,DDNAME=X,MACRF=E,EXLST=EXLST
LEXCPDCB EQU   *-EXCPDCB
SAMDCB   DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
LSAMDCB  EQU   *-SAMDCB
DIRDCB   DCB   DSORG=PS,RECFM=FB,LRECL=256,BLKSIZE=256,DDNAME=X,       X
               MACRF=(W),KEYLEN=8,DEVD=DA
LDIRDCB  EQU   *-DIRDCB
EXLST    DC    X'85',AL3(OPENEXIT)
         PRINT GEN
         SPACE 2
BLDLPARM DC    AL2(1,74)
         SPACE 2
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
TRTABLE  EQU   *-X'F0'
HEXTBL   DC    C'0123456789ABCDEF'
         SPACE 2
DATEMASK DC    X'402120612020612020'
         SPACE 2
SYSHELP  DC    CL8'SYSHELP'               FOR DDNAME FREE IN HELP
SYSPROC  DC    CL8'SYSPROC'               FOR NO DDNAME FREE IN DEALLOC
SPFUSER  DC    CL8'SPFUSER'               FOR SPF OPTION 6 ENQ TEST
SPFEDIT  DC    CL8'SPFEDIT'               FOR SPF MEMBER ENQ TEST
SYSIEWLP DC    CL8'SYSIEWLP'              FOR LINKAGE-EDITOR RESERVE
         AIF ('&CISP' EQ 'ISPF' OR '&CISP' EQ 'ISPFV2').FORISPF
ISPF     DC    C'ISPF    PGM(12345678)    PARM(++12345678)             X
                    '      COULD ZAP TO CHANGE "NEWAPPL(ISR)" OR "TEST"
ISPPANEL DC    CL33'PANEL(ISP@PRIM)              OPT('
         AGO   .FORBOTH
.FORISPF ANOP
ISPF     DC    C'ISPF    PGM(12345678)    PARM(++12345678) NEWAPPL(ISR)X
                    '      COULD ZAP TO CHANGE "NEWAPPL(ISR)" OR "TEST"
ISPPANEL DC    CL33'PANEL(ISR@PRIM) NEWAPPL(ISR) OPT('
.FORBOTH ANOP
ISPFPGM  EQU   12,8                       DISPLACEMENT TO PROGRAM NAME
ISPFPARM EQU   32,8                       DISPLACEMENT TO PARM PASSED
         EJECT
ISUBS    DC    33F'0'           INITIAL PDL AREA
         ORG   *-20
ZERO     DC    F'0'
         DC    F'99999'         INITIALIZE #MAXLEN
         DC    3F'9999999'      INITIALIZE #MAXFIND, #MAXIN, #MAXOUT
LISUBS   EQU   *-ISUBS          INITIALIZATION LENGTH
         SPACE 2
*  NOTE - THESE CCWS ARE USED FOR DATA READS
CCWINIT  CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
         CCW   X'08',*-8-(*-8),X'60',1          TIC
         CCW   X'92',IOBSEEK-IOBSEEK+3,X'20',8  MT READ NEXT COUNT
*
         CCW   X'23',SNO-SNO,X'60',1            SET SECTOR
         CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
         CCW   X'08',*-8-(*-8),X'60',1          TIC
CCWREAD  CCW   X'0E',*-*,X'60',32768            READ KEY AND DATA
CCWTIC   CCW   X'08',*+8-(*+8),X'60',1          TIC (OR MT READ CKD)
         CCW   X'92',IOBSEEK-IOBSEEK+3,X'60',8  MT READ NEXT COUNT
         CCW   X'22',SNO-SNO,X'20',1            READ SECTOR NUMBER
         SPACE 1
*  NOTE - THESE CCWS ARE USED FOR DIRECTORY COUNTS
CCWDIRR  CCW   X'1A',0,X'70',5                  READ HOME ADDRESS
         CCW   X'92',IOBSEEK+3-IOBSEEK,X'60',5  MT READ NEXT COUNT
         CCW   X'A9',KEYDATA,X'60',8            SEARCH KEY EQUAL
         CCW   X'08',*-16-(*-16),X'60',1        TIC
         CCW   X'92',WORKTBL+13-WORKTBL,X'60',5 MT READ NEXT COUNT
         CCW   X'86',0,X'70',1                  READ DATA
         CCW   X'08',*-16-(*-16),X'20',1        TIC
         SPACE 2
KEYDATA  DC    XL8'FFFFFFFFFFFFFFFF',XL2'000E',XL8'FFFFFFFFFFFFFFFF'
         EJECT
UNITTBL  DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL1'&DB23051',CL8'&UN23051 '
         DC    CL1'&DB23052',CL8'&UN23052 '
         DC    CL1'&DB2314 ',CL8'&UN2314  '
         DC    CL1'&DB3330 ',CL8'&UN3330  '
         DC    CL1'&DB3340 ',CL8'&UN3340  '
         DC    CL1'&DB3350 ',CL8'&UN3350  '
         DC    CL1'&DB3375 ',CL8'&UN3375  '
         DC    CL1'&DB33301',CL8'&UN33301 '
         DC    CL1'&DB3380 ',CL8'&UN3380  '
         DC    CL9'M????    '
         SPACE 3
OBTAIN   CAMLST SEARCH,0,0,0
         ORG   OBTAIN+4
         SPACE 3
LOCATE   CAMLST NAME,0,,0
         ORG   LOCATE+4
         SPACE 3
ENQSTEP  ENQ   (*-*,*-*,E,7,STEP),RET=TEST,MF=L
         ORG   ENQSTEP+4
         SPACE 3
ENQTEST  ENQ   (*-*,*-*,E,*-*,SYSTEMS),RET=TEST,MF=L
         ORG   ENQTEST+4
         SPACE 3
ENQHAVE  ENQ   (*-*,*-*,E,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   ENQHAVE+4
         SPACE 7
RESERVEL RESERVE (*-*,*-*,E,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   RESERVEL+4
         SPACE 3
DEQSYSS  DEQ   (*-*,*-*,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   DEQSYSS+4
         SPACE 3
DEQSYS   DEQ   (*-*,*-*,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   DEQSYS+4
         SPACE 3
STAELIST ESTAE *-*,PARAM=*-*,MF=L
         ORG   STAELIST+4
         EJECT
***BITS FOR DIRATTR+0:
ATTRRENT EQU   X'80'          REENTRANT
ATTRREUS EQU   X'40'          REUSABLE
ATTROVLY EQU   X'20'          OVERLAY STRUCTURE
ATTRTEST EQU   X'10'          TEST SYMBOLS ARE AVAILABLE
ATTRLOAD EQU   X'08'          ONLY LOADABLE
ATTRSCTR EQU   X'04'          SCATTER LOADED
ATTREXEC EQU   X'02'          EXECUTABLE
ATTR1TXT EQU   X'01'          ONE TEXT RECORD ONLY AND NO RLD ITEMS
         SPACE 1
***BITS FOR DIRATTR+1:
ATTRNODC EQU   X'80'          NOT DOWNWARD-COMPATABLE
ATTRZORG EQU   X'40'          FIRST TEXT BLOCK HAS A ZERO ORIGIN
ATTREP0  EQU   X'20'          ENTRY POINT IS AT OFFSET ZERO
ATTRNRLD EQU   X'10'          NO RLD ITEMS
ATTRNE   EQU   X'08'          CANNOT BE REPROCESSED BY LINKAGE-EDITOR
ATTRSYMS EQU   X'04'          CONTAINS TEST SYMBOLS
ATTRFLEV EQU   X'02'          CREATED BY F LEVEL LINKAGE-EDITOR
ATTRREFR EQU   X'01'          REFRESHABLE
         SPACE 3
TBLATTR  DS    0H                                    PDS PROCESSING
*                                                    ==============
         DC    AL1(ATTROVLY,0,00006),CL9'OVERLAY'       LIST ONLY
         DC    AL1(ATTRTEST,0,00003),CL9'TEST'          LIST ONLY
         DC    AL1(0,ATTRFLEV,128+6),CL9'E-LEVEL'       LIST ONLY
SCTRTXT  DC    AL1(ATTRSCTR,0,00003),CL9'SCTR'          LIST ONLY
RENTTXT  DC    AL1(ATTRRENT,0,00003),CL9'RENT'       CHANGE AND LIST
REUSTXT  DC    AL1(ATTRREUS,0,00003),CL9'REUS'       CHANGE AND LIST
REFRTXT  DC    AL1(0,ATTRREFR,00003),CL9'REFR'       CHANGE AND LIST
         DC    AL1(ATTRLOAD,0,00008),CL9'LOAD ONLY'  CHANGE AND LIST
         DC    AL1(0,ATTRNE+0,00007),CL9'NOT EDIT'   CHANGE AND LIST
         DC    AL1(ATTREXEC,0,128+7),CL9'NOT EXEC'   CHANGE AND LIST
         DC    AL1(0,ATTRNODC,128+1),CL9'DC'         CHANGE AND LIST
         DC    X'FFFF'
         SPACE 3
MODULEN  DC    AL2(CVTPUTL-CVTMAP),CL8'IKJPUTL '
         DC    AL2(CVTPARS-CVTMAP),CL8'IKJPARS '
         DC    AL2(CVTPTGT-CVTMAP),CL8'IKJPTGT '
         DC    AL2(CVTGETL-CVTMAP),CL8'IKJGETL '
         DC    AL2(CVTSCAN-CVTMAP),CL8'IKJSCAN '
         DC    AL2(CVTSTCK-CVTMAP),CL8'IKJSTCK '
         DC    AL2(CVTEHDEF-CVTMAP),CL8'IKJEHDEF'
         DC    AL2(CVTEFF02-CVTMAP),CL8'IKJEFF02'
         DC    AL2(CVTPCNVT-CVTMAP),CL8'CVTPCNVT'
         DC    AL2(CVTPRLTV-CVTMAP),CL8'CVTPRLTV'
         DC    X'FFFF'
         PRINT NOGEN
         TITLE 'P D S  --  PDS SUBCOMMAND TABLE                6/10/85'
         COPY  PDSTBL71                                        SS AUG84
         TITLE 'P D S  --  PDS PROGRAM MESSAGES                6/10/85'
         PRINT NOGEN
MSGBLANK MSG   '  '
MSGHELP  MSG   'H MS MS('
PDS480W  DC    C'PDS480W COMPRESS MAY NOT BE INTERRUPTED'
PDS480WL EQU   *-PDS480W
         SPACE 2
SPFSTATH DC    CL72'SPF STATS:  VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'
MSGMEMQ  MSG   '**                                '
MSGBL132 MSG   '                                                       X
                                                                       X
                                    '  -- 132 BYTES
BLANK128 EQU   MSGBL132+4,128
MSGABE   DC    C'S123 U1234 AT +123456 IN PROGRAM 12345678'
MSGABEUS EQU   6                 USER ERROR CODE
MSGABEAT EQU   MSGABEUS+9        OFFSET TO ERROR
MSGABEPG EQU   MSGABEAT+18       PROGRAM NAME
MSGABEND EQU   *                 END OF MSGABE MESSAGE
         SPACE 2
MSGBLKU  DC    C' 1,234,567 BLOCKS UPDATED'
         ORG   MSGBLKU
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
         SPACE 1
MSGDUMP  DC    C' 1,234,567   LENGTH 12,345   TTR 123456 '
         ORG   MSGDUMP
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   MSGDUMP+19
MSGDUMPL DC    X'4020206B202120'         EDIT PATTERN
MSGDUMPT EQU   MSGDUMP+33,6              TTR OUTPUT
         ORG   ,
MSGLDUMP DC    C'123456  CSECT 12345678  LENGTH 123456  '
*              C'1234567890123456789012345678901234567890
MSGBLK   DC    C' 1,234,567 BLOCKS IN THIS MEMBER  '
         ORG   MSGBLK
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGBLKL  EQU   MSGBLK+11,6               " LINES"
MSGBLKS  EQU   MSGBLK+26,7               "DATA SET"
LISTL000 DC    C'00000000'
LISTLBLO DC    C'BLOCK'
LISTLDUM DC    C'DUMP '
LISTLENT DC    C' ENTRY'
LISTLLIN DC    C' LINES'
LISTLDAT DC    C'DATA SET'
         EJECT
** PROMPTING MESSAGES
PDS300A  MSG   'PDS300A ENTER OPTION -- DSN=...                        X
                                                                      '
*BYTE COUNT:   ' 234567 10 234567 20 234567 30 234567 40 234567 50 2345
*DS300A  MSG   'PDS300A ENTER OPTION -- DSN=123456789012345678901234567
*              89012345678901234,VOL=SER=123456  MEM=12345678:12345678'
*BYTE COUNT:   67 60 234567 70 234567 80 234567 90 23456 100 23456 110'
*
*FOR DIALOG:   '- DSN=12345678901234567890123456789012345678901234,VOL=
*              SER=123456  MEM=12345678'
*
PDS380A  MSG   'PDS380A REENTER THE SEARCH STRING WITH DELIMITERS:'
PDS381A  MSG   'PDS381A REENTER THE DATA SET NAME, VOLUME AND DISPOSITIX
               ON'
PDS382A  MSG   'PDS382A REENTER THE FIRST TTR:'
PDS383A  MSG   'PDS383A REENTER THE SECOND TTR:'
PDS384A  MSG   'PDS384A REENTER THE HEX OFFSET:'
PDS385A  MSG   'PDS385A REENTER THE SSI DATA:'
PDS386A  MSG  'PDS386A REENTER THE REPLACEMENT STRING WITH DELIMITERS:'
         SPACE 2
** YES/NO PROMPTS
PDS390A  MSG   'PDS390A SHOULD THIS MEMBER BE RESTORED (Y/N) ?'
PDS391A  MSG   'PDS391A SHOULD THESE MEMBERS BE RENAMED (Y/N) ?'
PDS392A  MSG   'PDS392A SHOULD THIS DATA SET BE MODIFIED (Y/N) ?'
PDS393A  MSG   'PDS393A SHOULD THIS MEMBER BE DELETED (Y/N) ?'
PDS394A  MSG   'PDS394A SHOULD ALL OF THESE MEMBERS BE DELETED (Y/N) ?'
         TITLE 'P D S  --  PDS LITERALS                        6/10/85'
         LTORG
         TITLE 'P D S  --  PDS TRANSLATE TABLES                6/10/85'
PATCH    B     PATCH
         DC    18H'0'
         SPACE 3
***  TRANSLATE AND TEST TABLE TO FIND INVALID MEMBER NAMES
TRTMEM   DC    256X'FF'           ASSUME ALL INVALID FIRST
         ORG   TRTMEM+C'#'
         DC    1X'00'             # IS OK
         ORG   TRTMEM+C'$'
         DC    1X'00'             $ IS OK
         ORG   TRTMEM+C'@'
         DC    1X'00'             @ IS OK
         ORG   TRTMEM+C'A'
         DC    9X'00'             ABCDEFGHI  ARE OK
         ORG   TRTMEM+C'J'
         DC    9X'00'             JKLMNOPQR  ARE OK
         ORG   TRTMEM+C'S'
         DC    8X'00'             STUVWXYZ  ARE OK
         ORG   TRTMEM+C'0'
         DC    10X'00'            0123456789  ARE OK
         ORG   ,
         SPACE 3
***  TRANSLATE TABLE TO CONVERT UNPRINTABLES TO PERIODS
TRLINE   DC    256C'.'            ASSUME ALL UNPRINTABLE FIRST
         ORG   TRLINE+C' '
         DC    1X'40'             BLANK IS OK
         ORG   TRLINE+C''
         DC    C'.<(+&&'        .<(+&  ARE OK
         ORG   TRLINE+C'!'
         DC    C'!$*);^-/'        !$*);^-/  ARE OK
         ORG   TRLINE+C','
         DC    C',%_>?'           ,%_>?  ARE OK
         ORG   TRLINE+C':'
         DC    C':#@''="'         :#@'="  ARE OK
         ORG   TRLINE+X'81'       LOWER-CASE ABCDEFGHI
         DC    X'818283848586878889'
         ORG   TRLINE+X'91'       LOWER-CASE JKLMONPQR
         DC    X'919293949596979899'
         ORG   TRLINE+X'A2'       LOWER-CASE STUVWXYZ
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   TRLINE+C'A'
         DC    C'ABCDEFGHI'       ABCDEFGHI  ARE OK
         ORG   TRLINE+C'J'
         DC    C'JKLMNOPQR'       JKLMNOPQR  ARE OK
         ORG   TRLINE+C'S'
         DC    C'STUVWXYZ'        STUVWXYZ  ARE OK
         ORG   TRLINE+C'0'
         DC    C'0123456789'      0123456789  ARE OK
         ORG   ,
         TITLE 'P D S  --  PDS MESSAGE LABELS/MESSAGE CSECT    6/10/85'
L000     DS    0H
L001     DC    C'001'
L002$1   DC    C'002'
L005     DC    C'005'
L006     DC    C'006'
L010     DC    C'010'
L020$2   DC    C'020'
L021     DC    C'021'
L022     DC    C'022'
L023     DC    C'023'
L024     DC    C'024'
L025$1   DC    C'025'
L030$1   DC    C'030'
L031$1   DC    C'031'
L032     DC    C'032'
L033     DC    C'033'
L034     DC    C'034'
L035$2   DC    C'035'
L040$1   DC    C'040'
L050$1   DC    C'050'
L051$1   DC    C'051'
L060     DC    C'060'
L061     DC    C'061'
L062     DC    C'062'
L064$2   DC    C'064'
L065$1   DC    C'065'
L066$1   DC    C'066'
L071     DC    C'071'
L080     DC    C'080'
L081     DC    C'081'
L090$1   DC    C'090'
L091$1   DC    C'091'
L092$1   DC    C'092'
L100$1   DC    C'100'
L101$1   DC    C'101'
L102$1   DC    C'102'
L103$2   DC    C'103'
L104$2   DC    C'104'
L106$1   DC    C'106'
L110$1   DC    C'110'
L111$1   DC    C'111'
L112$1   DC    C'112'
L113$1   DC    C'113'
L114$1   DC    C'114'
L115$1   DC    C'115'
L116     DC    C'116'
L120$2   DC    C'120'
L140$2   DC    C'140'
L141$1   DC    C'141'
L142$1   DC    C'142'
L143$2   DC    C'143'
L144$1   DC    C'144'
L145$1   DC    C'145'
L160$1   DC    C'160'
L161$1   DC    C'161'
L162$1   DC    C'162'
L163$1   DC    C'163'
L164$1   DC    C'164'
L165$1   DC    C'165'
L171$1   DC    C'171'
L180$1   DC    C'180'
L181$1   DC    C'181'
L182$1   DC    C'182'
L183$1   DC    C'183'
L184$1   DC    C'184'
L190$1   DC    C'190'
L200$1   DC    C'200'
L210$1   DC    C'210'
L220$1   DC    C'220'
L230$1   DC    C'230'
L241     DC    C'241'
L242     DC    C'242'
L243     DC    C'243'
L244     DC    C'244'
L250$1   DC    C'250'
L400     DC    C'400'
L401     DC    C'401'
L402     DC    C'402'
L410$1   DC    C'410'
L441$1   DC    C'441'
L442$1   DC    C'442'
L450     DC    C'450'
L451     DC    C'451'
L460     DC    C'460'
L470     DC    C'470'
L480     DC    C'480'
L481     DC    C'481'
L482     DC    C'482'
L500     DC    C'500'
L502     DC    C'502'
L510     DC    C'510'
L520     DC    C'520'
L530     DC    C'530'
L700     DC    C'700'
L701     DC    C'701'
L702     DC    C'702'
L703     DC    C'703'
L704$1   DC    C'704'
L705$1   DC    C'705'
L710     DC    C'710'
L720     DC    C'720'
L721     DC    C'721'
L722     DC    C'722'
L723     DC    C'723'
L724     DC    C'724'
L726     DC    C'726'
L730     DC    C'730'
L731     DC    C'731'
L732     DC    C'732'
L733     DC    C'733'
L740     DC    C'740'
L750     DC    C'750'
L751     DC    C'751'
L752     DC    C'752'
L770     DC    C'770'
L771$1   DC    C'771'
L772     DC    C'772'
L773     DC    C'773'
L774     DC    C'774'
L780$1   DC    C'780'
L781$1   DC    C'781'
L800     DC    C'800'
L801     DC    C'801'
L802$1   DC    C'802'
L804     DC    C'804'
L805     DC    C'805'
L811$1   DC    C'811'
L812$1   DC    C'812'
L813$1   DC    C'813'
L814$1   DC    C'814'
L820$2   DC    C'820'
L821$1   DC    C'821'
L822$1   DC    C'822'
L823$1   DC    C'823'
L824     DC    C'824'
L825     DC    C'825'
L826     DC    C'826'
L827$2   DC    C'827'
L830     DC    C'830'
L831     DC    C'831'
L832     DC    C'832'
L833     DC    C'833'
L834$1   DC    C'834'
L835$1   DC    C'835'
L836     DC    C'836'
L840     DC    C'840'
L841     DC    C'841'
L842     DC    C'842'
L843     DC    C'843'
L844$1   DC    C'844'
L850$2   DC    C'850'
L851$1   DC    C'851'
L852$1   DC    C'852'
L853$1   DC    C'853'
L854$1   DC    C'854'
L855     DC    C'855'
L860     DC    C'860'
L861$1   DC    C'861'
L862     DC    C'862'
L864$1   DC    C'864'
L865     DC    C'865'
L870     DC    C'870'
L871     DC    C'871'
L872     DC    C'872'
L873     DC    C'873'
L874     DC    C'874'
L875     DC    C'875'
L876     DC    C'876'
L877     DC    C'877'
L880$1   DC    C'880'
L881     DC    C'881'
L882     DC    C'882'
L883     DC    C'883'
L884$1   DC    C'884'
L892     DC    C'892'
L893     DC    C'893'
L910$1   DC    C'910'
L920$1   DC    C'920'
L930$1   DC    C'930'
L980     DC    C'980'
L990     DC    C'990'
L991$1   DC    C'991'
L998$1   DC    C'998'
L999$1   DC    C'999'
         DC    X'FF'             LIST TERMINATOR
         SPACE 5
PARTADDR DC    A(PROBE01)        PROBE POINTS FOR THE MESSAGE CSECT
         DC    A(PROBE02)          (FOR A PARTITIONED SEARCH)
         DC    A(PROBE03)
         DC    A(PROBE04)
         DC    A(PROBE05)
         DC    A(PROBE06)
         DC    A(PROBE07)
         DC    A(PROBE08)
         DC    A(PROBE09)
         DC    A(PROBE10)
         DC    A(PROBE11)
         DC    A(PROBE12)
         DC    A(PROBE99)
         SPACE 2
MSGCSECT CSECT
** INFORMATIONAL MESSAGES
PROBE01  DS    0C          PLACE BEFORE THE FIRST MESSAGE OF THE CSECT
         IKJTSMSG ('PDS001I TESTMESSAGES -- ALL NUMBERED MESSAGES FOLLOX
               W:'),L001
         IKJTSMSG ('PDS002I DEFAULT MEMBER IS ',),L002
         IKJTSMSG ('PDS005I END OF FILE'),L005
         IKJTSMSG ('PDS006I END OF DATA SET'),L006
         IKJTSMSG ('PDS010I THE ALIAS HAS BEEN ASSIGNED'),L010
         IKJTSMSG ('PDS020I ',,' ATTRIBUTES ARE:',),L020
         IKJTSMSG ('PDS021I APF AUTHORIZED'),L021
         IKJTSMSG ('PDS022I NOT APF AUTHORIZED, OBSOLETE LINKAGE EDITORX
               '),L022
         IKJTSMSG ('PDS023I APF AUTHORIZED, APF DATA VALUE GREATER THANX
                1'),L023
         IKJTSMSG ('PDS024I PAGE ALIGNMENT IS REQUIRED'),L024
         IKJTSMSG ('PDS025I SSI INFORMATION: ',),L025
         IKJTSMSG ('PDS030I GLOBAL OPERANDS: ',),L030
         IKJTSMSG ('PDS031I INPUT BUFFERING: ',),L031
PROBE02  DS    0C
         IKJTSMSG ('PDS032I TESTCPULOOP -- CPU LOOP FOLLOWS:'),L032
         IKJTSMSG ('PDS033I TESTABEND -- SYSTEM 0C1 FOLLOWS:'),L033
         IKJTSMSG ('PDS034I TESTOUTLOOP -- THIS DATA REPEATS'),L034
         IKJTSMSG ('PDS035I TESTREAD -- EXCP RETURN CODE=',,'; NEXT TTRX
                ADDRESS IS ',),L035
         IKJTSMSG ('PDS040I ',,' HAS BEEN DELETED'),L040
         IKJTSMSG ('PDS050I ',,' WILL BE MOVED'),L050
         IKJTSMSG ('PDS051I ',,' IS BEING MOVED'),L051
         IKJTSMSG ('PDS060I TRANSLATOR HISTORY BY CSECT -'),L060
         IKJTSMSG ('PDS061I AMASPZAP UPDATE HISTORY BY CSECT -'),L061
         IKJTSMSG ('PDS062I USER-SUPPLIED UPDATE HISTORY BY CSECT -'), X
               L062
         IKJTSMSG ('PDS064I LAST LINK-EDITED ON ',,'  BY LKED ',),L064
         IKJTSMSG ('PDS065I MAIN MEMBER NAME UPDATED FOR MEMBER: ',),  X
               L065
         IKJTSMSG ('PDS066I MEMBER IS AN ALIAS FOR: ',),L066
         IKJTSMSG ('PDS071I THE FOLLOWING OPTIONS ARE AVAILABLE:'),L071
PROBE03  DS    0C
         IKJTSMSG ('PDS080I PDSOUT DCB IS CLOSED'),L080
         IKJTSMSG ('PDS080I LOGCOPY DCB IS CLOSED'),L081
         IKJTSMSG ('PDS090I ',,' HAS BEEN RENAMED'),L090
         IKJTSMSG ('PDS091I ',,' HAS BEEN RESTORED'),L091
         IKJTSMSG ('PDS092I AMODE/RMODE INFORMATION UPDATED FOR MEMBER:X
                ',),L092
         IKJTSMSG ('PDS100I ',,' -- VERSION 7.1    '),L100
         IKJTSMSG ('PDS101I DELETED MEMBER FOUND AT TTR: ',),L101
         IKJTSMSG ('PDS102I ENTRY POINT AT ',),L102
         IKJTSMSG ('PDS103I ENTRY POINT AT ',,'  --  ',),L103
         IKJTSMSG ('PDS104I MODULE LENGTH  ',,'  -- ',),L104
         IKJTSMSG ('PDS106I LAST LINK-EDITED ON ',),L106
         IKJTSMSG ('PDS110I',,' LOGICAL RECORDS WERE INPUT'),L110
         IKJTSMSG ('PDS111I',,' PHYSICAL BLOCKS WERE INPUT'),L111
         IKJTSMSG ('PDS112I',,' CHARACTERS IN THE LARGEST PHYSICAL BLOCX
               K'),L112
         IKJTSMSG ('PDS113I',,' CHARACTERS PER AVERAGE PHYSICAL BLOCK')X
               ,L113
         IKJTSMSG ('PDS114I',,' TRACKS COULD BE REGAINED BY COMPRESSINGX
                THIS DATA SET'),L114
         IKJTSMSG ('PDS115I',,' MEMBERS WERE CHECKED'),L115
PROBE04  DS    0C
         IKJTSMSG ('PDS116I DATA SET WAS CHECKED'),L116
         IKJTSMSG ('PDS120I MVS/XA RESIDENCE MODE IS ',,' -- ADDRESSINGX
                MODE IS ',),L120
         IKJTSMSG ('PDS140I ',,' RECORD',),L140
         IKJTSMSG ('PDS141I AT ',),L141
         IKJTSMSG ('PDS142I ',),L142
         IKJTSMSG ('PDS143I ',,' DIRECTORY ENTRY, LENGTH=',),L143
         IKJTSMSG ('PDS144I DATA LINE ',,':'),L144
         IKJTSMSG ('PDS145I ',),L145
         IKJTSMSG ('PDS160I ',),L160
         IKJTSMSG ('PDS161I ',),L161
         IKJTSMSG ('PDS162I ',),L162
         IKJTSMSG ('PDS163I ',),L163
         IKJTSMSG ('PDS164I ',),L164
         IKJTSMSG ('PDS165I ',),L165
         IKJTSMSG ('         ',),L169
         IKJTSMSG ('PDS171I COMPRESS HAS COMPLETED; RC',),L171
PROBE05  DS    0C
         IKJTSMSG ('PDS180I ',),L180
         IKJTSMSG ('PDS181I EXTENTS IN TRACKS: ',),L181
         IKJTSMSG ('PDS182I ',),L182
         IKJTSMSG ('PDS183I ',),L183
         IKJTSMSG ('PDS184I ',),L184
         IKJTSMSG ('PDS190I AN ALIAS NAMED ',,' IS ALREADY AT THIS TTR'X
               ),L190
         IKJTSMSG ('PDS200I ',),L200
         IKJTSMSG ('PDS210I ',),L210
         IKJTSMSG ('PDS220I ',),L220
         IKJTSMSG ('PDS230I ',),L230
         IKJTSMSG ('PDS241I NO MATCHING DATA WAS FOUND'),L241
         IKJTSMSG ('PDS242I NO MATCHING EXTERNAL SYMBOLS WERE FOUND'),LX
               242
         IKJTSMSG ('PDS243I NO MATCHING HISTORY DATA WAS FOUND'),L243
         IKJTSMSG ('PDS244I NO MATCHING ATTRIBUTES WERE FOUND'),L244
         IKJTSMSG ('PDS250I ',),L250
         SPACE 2
** WARNING MESSAGES
         IKJTSMSG ('PDS400W NO MEMBERS ARE IN THE DATA SET'),L400
         IKJTSMSG ('PDS401W NO MEMBER NAMES ARE IN THIS RANGE'),L401
         IKJTSMSG ('PDS402W NO MEMBER NAMES MATCH THIS PATTERN'),L402
         IKJTSMSG ('PDS410W VOLSET(',,') IS STILL IN EFFECT'),L410
         IKJTSMSG ('PDS441W ',,' (WEAK)'),L441
PROBE06  DS    0C
         IKJTSMSG ('PDS442W ',,' (MISSING)'),L442
         IKJTSMSG ('PDS450W ONE EXTRA PADDING DIRECTORY BLOCK WILL BE AX
               DDED'),L450
         IKJTSMSG ('PDS451W ALL MEMBERS IN THIS PDS WILL BE LOST'),    X
               L451
         IKJTSMSG ('PDS460W NO HISTORY DATA IS AVAILABLE'),L460
         IKJTSMSG ('PDS470W THE PROGRAM IS PROBABLY IN A LOOP'),L470
         IKJTSMSG ('PDS480W COMPRESS IS IN PROGRESS'),L480
         IKJTSMSG ('PDS480W COMPRESS MAY NOT BE INTERRUPTED'),L481
         IKJTSMSG ('PDS482W BLOCKSIZE SHOULD BE EVENLY DIVISIBLE BY THEX
                DCB LRECL'),L482
         IKJTSMSG ('PDS500W NO COBOL TASK GLOBAL TABLE WAS FOUND'),L500
         IKJTSMSG ('PDS502W THIS MODULE WAS COMPILED WITH ENDJOB AND MIX
               XED RES AND NORES OPTIONS'),L502
         IKJTSMSG ('PDS510W THIS IS A NULL MEMBER'),L510
         IKJTSMSG ('PDS520W NO INFORMATION IS AVAILABLE'),L520
         IKJTSMSG ('PDS530W THIS DATA SET IS NOT PARTITIONED'),L530
         SPACE 2
** ERROR MESSAGES
         IKJTSMSG ('PDS700E THIS RANGE OF NAMES IS INVALID'),L700
         IKJTSMSG ('PDS701E THIS DATA SET IS NOT A LOAD LIBRARY'),L701
         IKJTSMSG ('PDS702E THIS DATA SET IS A LOAD LIBRARY'),L702
         IKJTSMSG ('PDS703E THIS MODULE HAS NO EXTERNAL SYMBOLS'),L703
         IKJTSMSG ('PDS704E MODULE ',,' HAS NO EXTERNAL SYMBOLS'),L704
         IKJTSMSG ('PDS705E EXTERNAL SYMBOL ',,' WAS NOT FOUND'),L705
PROBE07  DS    0C
         IKJTSMSG ('PDS710E INVALID APF INFORMATION FORMAT; MEMBER IS AX
               SSUMED NOT AUTHORIZED'),L710
         IKJTSMSG ('PDS720E NOT APF AUTHORIZED; THE APF DATA IS MISSINGX
               '),L720
         IKJTSMSG ('PDS721E NOT APF AUTHORIZED; THE APF DATA IS THE WROX
               NG LENGTH'),L721
         IKJTSMSG ('PDS722E THE APF DATA CAN NOT BE CHANGED'),L722
         IKJTSMSG ('PDS723E THE RLD/CONTROL COUNT CAN NOT BE CHANGED'),X
               L723
         IKJTSMSG ('PDS724E RMODE AND AMODE CAN NOT BE CHANGED -- OBSOLX
               ETE LINKAGE-EDITOR'),L724
         IKJTSMSG ('PDS726E PAGE ALIGNMENT CAN NOT BE CHANGED -- OBSOLEX
               TE LINKAGE-EDITOR'),L726
         IKJTSMSG ('PDS730E THIS SUBCOMMAND IS NOT OPERATIONAL; YOU AREX
                ALREADY UNDER SPF'),L730
         IKJTSMSG ('PDS731E SPF IS NOT OPERATIONAL NOW'),L731
         IKJTSMSG ('PDS732E CLIST PROCESSING IS NOT SUPPORTED FROM ISPMX
               ODE OR MEMLIST'),L732
         IKJTSMSG ('PDS733E BROWSE FAILED -- NULL MEMBER OR I/O ERROR')X
               ,L733
         IKJTSMSG ('PDS740E THIS MODULE HAS NO MATCHING EXTERNAL SYMBOLX
               S'),L740
         IKJTSMSG ('PDS750E A REQUIRED NOTELIST POINTER WAS NOT FOUND IX
               N THIS MEMBER'),L750
         IKJTSMSG ('PDS751E A NOTELIST RECORD COULD NOT BE FOUND FOR THX
               IS MEMBER'),L751
         IKJTSMSG ('PDS752E THE NUMBER OF DESIRED DIRECTORY BLOCKS MUSTX
                BE SPECIFIED'),L752
         IKJTSMSG ('PDS770E A SEARCH STRING IS REQUIRED'),L770
PROBE08  DS    0C
         IKJTSMSG ('PDS771E ',,' IS AN INVALID SUBCOMMAND; ENTER "HELP"X
                OR "OPTIONS" FOR HELP'),L771
         IKJTSMSG ('PDS772E A REPLACEMENT STRING IS REQUIRED'),L772
         IKJTSMSG ('PDS773E EQUAL LENGTH STRINGS ARE REQUIRED FOR BLOCKX
                AND DUMP UPDATES'),L773
         IKJTSMSG ('PDS774E REPLACE TERMINATED; CHARACTER EXPANSION FAIX
               LED'),L774
         IKJTSMSG ('PDS780E ',,' DCB OPEN ERROR -- REALLOCATE AND TRY AX
               GAIN'),L780
         IKJTSMSG ('PDS781E ',,' DCB ATTRIBUTES CONFLICT -- USE SEQUENTX
               IAL WITH RECFM=FB AND LRECL=80'),L781
         IKJTSMSG ('PDS800E NO LOAD MODULE TEXT WAS FOUND FOR MEMBER'),X
               L800
         IKJTSMSG ('PDS801E END OF MEMBER SIMULATED -- REMAINDER OF TRAX
               CK SKIPPED'),L801
         IKJTSMSG ('PDS802E A MAIN MEMBER NAMED ',,' IS ALREADY AT THISX
                TTR'),L802
         IKJTSMSG ('PDS804E RESTORE ABANDONED'),L804
         IKJTSMSG ('PDS805E INVALID LOAD MODULE DATA'),L805
         IKJTSMSG ('PDS811E RECORD LENGTH',,' IS INVALID'),L811
         IKJTSMSG ('PDS812E BLOCK LENGTH OF',,' EXCEEDS THE DATA SET DCX
               B BLKSIZE'),L812
         IKJTSMSG ('PDS813E RECORD LENGTH OF',,' EXCEEDS THE MAXIMUM DCX
               B LRECL'),L813
         IKJTSMSG ('PDS814E BLOCK LENGTH OF',,' IS NOT DIVISIBLE BY THEX
                DCB LRECL'),L814
         IKJTSMSG ('PDS820E THIS MEMBER IS AN ALIAS FOR ',,' BUT IT POIX
               NTS TO ',),L820
PROBE09  DS    0C
         IKJTSMSG ('PDS821E RMODE ENTRY DOES NOT CORRESPOND WITH MEMBERX
                ',),L821
         IKJTSMSG ('PDS822E MAIN AMODE ENTRY DOES NOT CORRESPOND WITH MX
               EMBER ',),L822
         IKJTSMSG ('PDS823E RLD/CONTROL COUNT DOES NOT CORRESPOND WITH X
               MEMBER ',),L823
         IKJTSMSG ('PDS824E DUPLICATE MEMBER NAME -- IGNORED'),L824
         IKJTSMSG ('PDS825E MEMBER NAME IS OUT OF SEQUENCE -- IGNORED')X
               ,L825
         IKJTSMSG ('PDS826E THE DIRECTORY RLD/CONTROL COUNT DOES NOT MAX
               TCH THE FIRST RLD ENTRY'),L826
         IKJTSMSG ('PDS827E ',,' IS AN ALIAS FOR THIS MEMBER BUT IT POIX
               NTS TO ',),L827
         IKJTSMSG ('PDS830E OBTAIN ERROR; DSCB NOT ON VOLUME'),L830
         IKJTSMSG ('PDS831E UNABLE TO OPEN DATA SET'),L831
         IKJTSMSG ('PDS832E DUMMY MEMBER ADD FAILED, RC=8'),L832
         IKJTSMSG ('PDS833E DUMMY MEMBER DELETE FAILED, RC=8'),L833
         IKJTSMSG ('PDS834E ERROR IN GENERAL FAIL SERVICE ROUTINE;  R15X
               ',),L834
         IKJTSMSG ('PDS835E ERROR IN DAIRFAIL SERVICE ROUTINE;  R15',),X
               L835
         IKJTSMSG ('PDS836E I/O ERROR IN DIRECTORY'),L836
PROBE10  DS    0C
         IKJTSMSG ('PDS840E INVALID HEXADECIMAL STRING LENGTH'),L840
         IKJTSMSG ('PDS841E INVALID HEXADECIMAL CHARACTERS'),L841
         IKJTSMSG ('PDS842E THIS STRING IS TOO LONG'),L842
         IKJTSMSG ('PDS843E ONLY DISK DATA SETS ARE SUPPORTED'),L843
         IKJTSMSG ('PDS844E FILE(',,') HAS CONCATENATED DATA SETS -- ONX
               LY INDIVIDUAL DATA SETS ARE SUPPORTED'),L844
         IKJTSMSG ('PDS850E ',,' IS BEING UPDATED BY ',),L850
         IKJTSMSG ('PDS851E THIS DATA SET IS BEING UPDATED BY ',),L851
         IKJTSMSG ('PDS852E ',,' ALREADY EXISTS'),L852
         IKJTSMSG ('PDS853E ',,' NOT FOUND'),L853
         IKJTSMSG ('PDS854E ',,' IS AN INVALID MEMBER NAME'),L854
         IKJTSMSG ('PDS855E THE DATA SET DIRECTORY IS FULL AND MEMBERS X
               CANNOT BE ADDED'),L855
         IKJTSMSG ('PDS860E MEMBER IS AN ALIAS BUT NO MAIN MEMBER EXISTX
               S'),L860
PROBE11  DS    0C
         IKJTSMSG ('PDS861E THE ALIAS DIRECTORY ENTRY NOTES THE MAIN ENX
               TRY NAME AS ',),L861
         IKJTSMSG ('PDS862E ERROR IN GQSCAN DATA'),L862
         IKJTSMSG ('PDS864E ',,' IS AN APPARENT ALIAS FOR THIS MEMBER')X
               ,L864
         IKJTSMSG ('PDS865E THE IEBCOPY OUTPUT CAN NOT BE OPENED'),L865
         IKJTSMSG ('PDS870E PDS DIRECTORIES MUST BE CONTAINED IN ONE EXX
               TENT'),L870
         IKJTSMSG ('PDS871E A TTR FOR THIS MEMBER WAS NOT FOUND'),L871
         IKJTSMSG ('PDS872E THIS MEMBER IS AFTER THE DATA SET END-OF-FIX
               LE'),L872
         IKJTSMSG ('PDS873E TTR IS BEYOND THE USED PORTION OF THE DATA X
               SET'),L873
         IKJTSMSG ('PDS874E TSO COMMAND NAME IS INVALID'),L874
         IKJTSMSG ('PDS875E THIS DATA SET HAS NO DIRECTORY BLOCKS'),   X
               L875
         IKJTSMSG ('PDS876E A DIRECTORY RECORD HAS AN INCORRECT LENGTH X
               (NOT 256 CHARACTERS)'),L876
         IKJTSMSG ('PDS877E INVALID SUBCOMMAND NAME'),L877
PROBE12  DS    0C
         IKJTSMSG ('PDS880E RESIDENCE MODE ANY AND ADDRESSING MODE ',, X
               ' ARE INCOMPATIBLE'),L880
         IKJTSMSG ('PDS881E REENTRANT ATTRIBUTE CONFLICTS WITH NOT REUSX
               ABLE'),L881
         IKJTSMSG ('PDS882E TEST ATTRIBUTE CONFLICTS WITH NOT EDIT'),  X
               L882
         IKJTSMSG ('PDS883E REUSABLE ATTRIBUTE CONFLICTS WITH SCATTER')X
               ,L883
         IKJTSMSG ('PDS884E OVERLAY ATTRIBUTE CONFLICTS WITH ',),L884
         IKJTSMSG ('PDS892E READ MULTIPLE FAILED'),L892
         IKJTSMSG ('PDS893E READ MULTIPLE ERROR -- NEXT TTR IS INCORRECX
               T'),L893
         IKJTSMSG ('PDS910E TSO COMMAND ',,' IS NOT ALLOWED'),L910
         IKJTSMSG ('PDS920E SUBCOMMAND ',,' IS NOT ALLOWED FOR THIS DATX
               A SET'),L920
         IKJTSMSG ('PDS930E ',,' IS AN INVALID SUBCOMMAND ABBREVIATION'X
               ),L930
         IKJTSMSG ('PDS980E COMPRESS WAS INTERRUPTED'),L980
         IKJTSMSG ('PDS990E EXTENT INITIALIZATION READ FAILED'),L990
         IKJTSMSG ('PDS991E PERMANENT I/O ERROR AT TTR=',),L991
         IKJTSMSG ('PDS998E ABEND ',,' LOADING THIS MODULE'),L998
         IKJTSMSG ('PDS999E ABEND ',),L999
         PRINT GEN
          SPACE 1
** END OF MESSAGES
PROBE99  DS    0C               MUST BE THE LAST PROBE LABEL
         IKJTSMSG
         PRINT NOGEN
         COPY  PDSDSECT
         TITLE 'P D S  --  PDS ESD, IDR DSECTS                 6/10/85'
*
*        ESD TABLE FORMAT
*
ESDENTRY DSECT
ESDLINK  DS    F
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINITION
CODEER   EQU   X'02'          ENTERNAL REFERENCE ($UNRESOLVED)
CODELR   EQU   X'03'          LABEL REFERENCE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODECM   EQU   X'05'          COMMON BLOCK
CODEPR   EQU   X'06'          PSEUDO REGISTER
CODENL   EQU   X'07'          NULL
CODEWK   EQU   X'0A'          WEAK EXTERNAL ($UNRESOLVED WEAK)
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDXAFLG EQU   ESDSEG#        FLAGS FOR MVS/XA
RMODEANY EQU   X'04'          RMODE=ANY
AMODE24  EQU   X'01'          AMODE=24
AMODE31  EQU   X'02'          AMODE=31
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
ESDID    DS    XL2            RELATIVE ESD ID #
ESDIDIN  DS    XL2            ESD IS IN THIS PHYSICAL RECORD
ESDCSECT DS    XL4            LIST ONLY -- POINTER TO CSECT ENTRY
ESDMAIN  DS    XL4            LIST ONLY -- ENTRY POINT OF CSECT ENTRY
LENESD2  EQU   *-ESDNAME      LENGTH OF ESD DATA
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY
         SPACE 3
*
*        IDR TABLE FORMAT
*
IDRENTRY DSECT
IDRLINK  DS    A
IDRSTART EQU   *
IDRESDID DS    XL2
IDRTYPE  DS    X
IDRZAP   EQU   X'01'
IDRLKED  EQU   X'02'
IDRTRAN  EQU   X'04'
IDRUSER  EQU   X'08'
IDRDATE  DS    XL3
IDRLDATA DS    X
IDRUPREF DS    XL6            USER RECORD PREFIX
IDRZDATA DS    0CL8
IDRTDATA DS    0CL30
IDRDATA  DS    CL40
LENIDR1  EQU   *-IDRSTART
LENIDR   EQU   *-IDRENTRY
         TITLE 'P D S  --  PDS SDWA DSECT                      6/10/85'
         AIF ('&MVSLEV' EQ 'MVS038').MVS038
         IHASDWA  VRAMAP=NO
         AGO   .SDWADEF
.MVS038  ANOP
         IHASDWA
.SDWADEF ANOP
         TITLE 'P D S  --  PDS SVC 99 PARAMETER LIST           6/10/85'
         IEFZB4D0
         AIF ('&MVSLEV' LT 'MVS13X').NMVS13C
         TITLE 'P D S  --  RIB, RIBE DEFINITION                6/10/85'
         ISGRIB
.NMVS13C ANOP
         TITLE 'P D S  --  PDS PPL, CSPL, CSOA DSECTS          6/10/85'
         IKJPPL
         SPACE 3
         IKJCSPL
         SPACE 2
         IKJCSOA
         TITLE 'P D S  --  PDS IOPL, PGPB DSECTS               6/10/85'
         IKJIOPL
         SPACE 5
         IKJPGPB
**       TITLE 'P D S  --  PDS GTPB DSECT                      6/10/85'
**       IKJGTPB
         TITLE 'P D S  --  PDS UPT DSECT                       6/10/85'
         IKJUPT
         TITLE 'P D S  --  PDS PSCB DSECT                      6/10/85'
         IKJPSCB
         TITLE 'P D S  --  PDS ECT DSECT                       6/10/85'
         IKJECT
         TITLE 'P D S  --  PDS CPPL DSECT                      6/10/85'
         IKJCPPL
         TITLE 'P D S  --  PDS DAPL DSECT                      6/10/85'
         IKJDAPL
**       TITLE 'P D S  --  PDS DAIR00 DSECT                    6/10/85'
**       IKJDAP00
**       TITLE 'P D S  --  PDS DAIR04 DSECT                    6/10/85'
**       IKJDAP04
**       TITLE 'P D S  --  PDS DAIR08 DSECT                    6/10/85'
**       IKJDAP08
**       TITLE 'P D S  --  PDS DAIR18 DSECT                    6/10/85'
**       IKJDAP18
**       SPACE 2
**       IKJDAP0C
**0CDDN  DS    XL8 START OF DDNAME LIST
**       TITLE 'P D S  --  PDS DAIR10 DSECT                    6/10/85'
**       IKJDAP10
**       TITLE 'P D S  --  PDS DAIR14 DSECT                    6/10/85'
**       IKJDAP14
**       TITLE 'P D S  --  PDS DAIR1C DSECT                    6/10/85'
**       IKJDAP1C
**       TITLE 'P D S  --  PDS DAIR24 DSECT                    6/10/85'
**       IKJDAP24
**       TITLE 'P D S  --  PDS DAIR28 DSECT                    6/10/85'
**       IKJDAP28
**       TITLE 'P D S  --  PDS DAIR2C DSECT                    6/10/85'
**       IKJDAP2C
**       TITLE 'P D S  --  PDS DAIR30 DSECT                    6/10/85'
**       IKJDAP30
         TITLE 'P D S  --  PDS DFPL AND DFPB DSECTS            6/10/85'
         IKJDFPL
         SPACE 3
         IKJDFPB
         TITLE 'P D S  --  PDS DCBD AND CVT DSECTS             6/10/85'
         PRINT NOGEN
         DCBD  DSORG=PO
         SPACE 5
         CVT   DSECT=YES
         SPACE 5
.NOGEN   END  PDS71
