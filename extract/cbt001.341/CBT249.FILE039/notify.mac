         TITLE 'JOB/STEP NOTIFICATION ROUTINE'
***********************************************************************
*                                                                     *
*        THIS ROUTINE WILL SEND THE USER A PRE-SPECIFIED MESSAGE      *
*        UPON INVOKATION.  THIS IS DEPENDENT UPON THE USE OF THE      *
*        'NOTIFY=' KEYWORD OF THE JOB CARD.  IF THE USER IS NOT       *
*        LOGED ON AT THE TIME, THE MESSAGE WILL BE SAVED IN THE       *
*        BRODCAST DATASET.                                            *
*                                                                     *
*        IT IS SUGGESTED THAT CONDITION CODE TESTING BE USED TO       *
*        PROVIDE A MEANS FOR INTELLIGENT NOTIFICATION.                *
*                                                                     *
*        NOTE: THIS ROUTINE MUST HAVE AUTHCODE=1 AND RESIDE IN        *
*              AN AUTHORIZED LIBRARY (LNKLST).                        *
*                                                                     *
***********************************************************************
         EJECT
NOTIFY   $PROLOG
         L     R1,0(R1)       GET ADDRESS OF PARM
         LH    R15,0(R1)      GET SIZE FOR MOVE
         LTR   R15,R15        ANY PARM?
         BZ    EXIT           NO, NO-OP
         CH    R15,=H'71'     LARGER THAN MAX?
         BNH   AROUND         NO, CONTINUE
         LA    R15,=H'71'     OVERRIDE SIZE
AROUND   BCTR  R15,R0         DECREMENT FOR EXECUTE
         EX    R15,MOVE       MOVE DATA TO SEND COMMAND
         L     R1,16          GET THE CVT ADDRESS
         L     R15,0(R1)      GET THE OLD/NEW POINTERS
         L     R15,12(R15)    GET OUR ASCB ADDRESS
         SR    R0,R0          CLEAR R0
         ICM   R0,3,36(R15)   PICK UP ASID FROM ASCB
         SLL   R0,2           MULTIPLY BY 4
         L     R1,296(R1)     GET JES CONTROL TABLE ADDR
         L     R1,24(R1)      GET JES SSCT
         L     R1,16(R1)      GET JES SSVT
         L     R15,388(R1)    GET JES ASVT
         AR    R15,R0         ADD OFFSET TO ADDRESS OF ASVT
         L     R15,0(R15)     GET INITIATORS SJB ADDRESS
         ICM   R15,15,84(R15) GET INITIATOR'S DEPENDENT SJB
         BZ    EXIT           IF ZERO THIS IS NOT BATCH
         L     R15,96(R15)    GET JOBS JCT ADDRESS
         CLI   340(R15),C' '  WAS NOTIFY REQUESTED?
         BE    EXIT           NO, NO ONE TO INFORM
         MVC   SENDUSER(7),340(R15) MOVE THE NOTIFY-ID
         LA    R15,7          SET LOOP CONTROL
         LA    R1,SENDUSER    SET FIELD START
LOOP     CLI   0(R1),C' '     TRAILING BLANK FOUND?
         BE    SEND           YES, CONTINUE
         LA    R1,1(R1)       BUMP TO NEXT CHAR
         BCT   R15,LOOP       CHECK NEXT CHARACTER
SEND     MVI   0(R1),C')'     PLACE FINAL RIGHT PAREN
         MODESET KEY=ZERO     GET KEY ZERO FOR SVC 34
         SR    R0,R0          SVC 34 PARM
         LA    R1,SENDCMD     SVC 34 CMD POINTER
         SVC   34             ISSUE COMMAND
         MODESET KEY=NZERO    BACK TO PROBLEM KEY
EXIT     SR    R15,R15        CLEAR THE RETCODE
         $EPILOG
MOVE     MVC   SENDDATA(0),2(R1) MOVE SEND COMMAND DATA
         DS    0F
SENDCMD  DC    AL2(SENDEND-*,0),C'SEND '''
SENDDATA DC    CL71' ',C''',LOGON,USER=('
SENDUSER DC    CL9' '
SENDEND  EQU   *
         DC    CL8' '         PADDING
         END
