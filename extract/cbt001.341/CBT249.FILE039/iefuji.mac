         TITLE 'IEFUJI JOB INITIATION SMF EXIT..ALIAS(IGC00XXX)'
***********************************************************************
*                                                                     *
*        THIS MODULE WILL SEND A 'STARTED' MESSAGE TO JES2 REMOTE     *
*        TERMINALS (IF SOURCE WAS A REMOTE) AND WILL ALSO SEND A      *
*        'STARTED' MESSAGE TO LOGGED-ON TSO USERS IF THEY SPECIFIED   *
*        NOTIFY ON THEIR JOB STATEMENT.                               *
*                                                                     *
*          NOTE: THIS ROUTINE IS LINKEDITED AS AN SVC (TYPE 3)        *
*                TO BE INCLUDE VIA AN 'MLPA' AT IPL TIME.             *
*                IT IS BEST TO LINK WITH THE NAME OF IEFUJI           *
*                WITH AN ALIAS OF THE USED SVC (E.G. IGC00XXX).       *
*                ALSO, SINCE THIS ROUTINE MUST BE REENTRANT, NO       *
*                IN-LINE MACRO EXPANSIONS MAY BE USED (E.G. DO        *
*                NOT USE A MACRO THAT GENERATES AN ADCON).            *
*                                                                     *
***********************************************************************
         EJECT
IEFUJI   CSECT
         $REGS
         USING *,R6           INFORM THE ASSEMBLER OF OUR BASE REG
         LR    R12,R1         SAVE PARM LIST POINTER
         LA    R0,WORKEND-WORK SIZE FOR GETMAIN
         BAL   R1,*+4         INDICATE GETMAIN
         SVC   10             GET A WORKAREA
         MVC   0(WORKEND-WORK,R1),RETCODE MOVE CONSTANTS TO WORKAREA
         LR    R9,R1          COPY TO USING REG
         USING WORK,R9        ADDRESSABILITY TO GOTTEN STORAGE
         USING COM,R11        BASE FOR COMMON EXIT PARM AREA
         USING PARMS,R12      BASE FOR INPUT REG 1
*
*        BECAUSE WE ARE AN SVC, THERE IS NO NEED TO SAVE THE CALLERS
*        REGISTERS (THE SYSTEM HAS ALREADY SAVED THEM).  ALSO,
*        CERTAIN REGISTERS HAVE BEEN PRIMED FOR US AS FOLLOWS:
*
*         0 = UNPREDICTABLE
*         1 = ADDRESS OF ENTRY PARMS
*         2 = UNPREDICTABLE
*         3 = CVT ADDRESS
*         4 = TCB ADDRESS(CURRENT)
*         5 = SVRB ADDRESS (OURS)
*         6 = ENTRY POINT (OUR BASE REG)
*         7 = ASCB ADDRESS (CURRENT)
*         8 = UNPREDICTABLE    (ADDRESS OF JOBS JES2 JCT)
*         9 = UNPREDICTABLE
*        10 = UNPREDICTABLE    (RETURN POINT ADDRESS -R14-)
*        11 = UNPREDICTABLE    (ADDRESS OF COMMON EXIT PARM AREA)
*        12 = UNPREDICTABLE    (ADDRESS OF ENTRY PARMS)
*        13 = SAME AS WHEN SVC WAS ISSUED (AT ENTRY TO EXIT)
*        14 = RETURN REGISTER (TO SVC RETURN HANDLER)
*        15 = SAME AS WHEN SVC WAS ISSUED (AT ENTRY TO EXIT)
*
         L     R11,PRMCOM     GET POINTER TO COMMON EXIT PARM LIST
         XC    COMUSER,COMUSER CLEAR IN CASE OF RE-START
***********************************************************************
*
*          LOCATE THE JOB'S JCT TO GET THE RMT-ID AND NOTIFY-ID
*
***********************************************************************
         SR    R0,R0          CLEAR R0
         ICM   R0,3,36(R7)    PICK UP ASID FROM ASCB
         SLL   R0,2           MULTIPLY BY 4
         L     R1,296(R3)     GET JES CONTROL TABLE ADDR
         L     R1,24(R1)      GET JES SSCT
         L     R1,16(R1)      GET JES SSVT
         L     R15,388(R1)    GET JES ASVT
         AR    R15,R0         ADD OFFSET TO ADDRESS OF ASVT
         L     R15,0(R15)     GET INITIATORS SJB ADDRESS
         ICM   R15,15,84(R15) GET INITIATOR'S DEPENDENT SJB
         BZ    EXIT           IF ZERO THIS IS NOT BATCH
         L     R8,96(R15)     GET JOBS JCT ADDRESS
         EJECT
***********************************************************************
*
*          BUILD THE REMOTE 'STARTED' MESSAGE IF READ FROM
*          A REMOTE READER
*
***********************************************************************
         CLI   192(R8),C'R'   IS THIS A JOB FROM A REMOTE?
         BNE   SKIPRMT        NO, NO ONE TO INFORM
         CLI   193(R8),C'1'   CHECK IF NUMERIC REMOTE ID
         BL    SKIPRMT        NO, IT AIN'T
         MVC   COMUSER+2(2),193(R8) SAVE RMT# FOR ENDED MSG(IEFACTRT)
         CLI   COMUSER+3,C'0' SECOND CHARACTER ALSO NUMERIC?
         BNL   DOUBLE         YES, SKIP BLANK INSERTION
         MVI   COMUSER+3,C' ' CLEAR PERIOD IN RMT ID
DOUBLE   MVC   RMTCMD+4(2),COMUSER+2 ADD THE REMOTE ID TO MESSAGE
         MVC   RMTNAME(8),COMJN MOVE JOBNAME TO MESSAGE
         MVC   RMTSID(4),COMSID ADD THE SYSTEM ID
         SR    R0,R0          CLEAR FOR SVC 34
         LA    R1,RMT         SET SVC 34 PARM POINTER
         SVC   34             ISSUE $DMR STARTED MESSAGE
SKIPRMT  EQU   *
         SPACE 3
***********************************************************************
*
*          CHECK IF NOTIFY WAS SPECIFIED FOR THIS JOB AND IF
*          SO, SEND THE USER A 'STARTED' MESSAGE VIA 'TPUT'
*
***********************************************************************
         CLI   340(R8),X'00'  WAS NOTIFY REQUESTED?
         BE    SKIPTPUT       NO, NO ONE TO INFORM
         MVC   TPUTUSER(7),340(R8) MOVE THE NOTIFY-ID
         MVC   TPUTNUM(8),148(R8) MOVE THE JOB NUMBER TO MSG
         MVC   TPUTNAME(8),COMJN ADD THE JOBNAME
         LA    R1,TPUTNAME    ADDRESS OF NAME FIELD
         LA    R15,9          SET LOOP CONTROL
TPUTLOOP CLI   0(R1),C' '     END OF JOB NAME?
         BE    TPUTMOVE       YES, COMPLETE THE MSG
         LA    R1,1(R1)       BUMP TO NEXT CHAR
         BCT   R15,TPUTLOOP   CHECK NEXT CHARACTER
TPUTMOVE MVC   1(8,R1),=C'STARTED ' ADD THE STARTED SUFFIX
         MVC   10(4,R1),COMSID ADD THE SYSTEM ID
         MVC   15(6,R1),=C'CN(00)' ADD CONSOLE ID (INTERNAL)
         CNOP  0,4            FULLWORD ALIGNED
         BAL   R1,*+8         AROUND CONSTANT
         DC    X'40000000'    USERID MASK FOR TPUT
         L     R1,0(R1)       PICK UP MASK
         LA    R15,TPUTDATA   ADDRESS OF MSG DATA
         OR    R1,R15         COMPLETE TPUT PARM
         LA    R0,TPUTUSER-TPUTDATA GET SIZE FOR TPUT MSG
         LA    R15,TPUTUSER   ADDRESS OF USERID
         SVC   93             INFORM USER
SKIPTPUT EQU   *
         EJECT
EXIT     LA    R0,WORKEND-WORK    SIZE FOR FREEMAIN
         LR    R1,R9              ADDRESS OF STORAGE
         SVC   10                 FREE THE WORKAREA
         SR    R15,R15            SET RETURN CODE
         BR    R14                PASS REGS FROM EXIT EXTENSION MODULE
         SPACE 3
         LTORG
         EJECT
***********************************************************************
*
*          THE FOLLOWING CONSTANTS ARE MOVED TO GOTTEN STORAGE
*          TO MAINTAIN RE-ENTRANCY.  IF ANY MODIFICATIONS ARE
*          MADE, INSURE THAT THIS SECTION IMMEDIATELY PRECEDES
*          THE FOLLOWING DSECTS.
*
***********************************************************************
         DS    0D
WORK     EQU   *
RETCODE  DC    F'0'
*
RMT      DC    AL2(RMTEND-*,0)                 SVC 34 PARMS FIELD
RMTCMD   DC    C'$DMRXX, '''                   $DMR MESSAGE
RMTNAME  DC    CL8'JOBNAME',C' STARTED '
RMTSID   DC    C'SSSS'''
RMTEND   DC    CL8' '      PADDING
*
TPUTDATA DC    C'$HASP165 '                    TPUT MESSAGE
TPUTNUM  DC    C'JOB XXXX  '
TPUTNAME DC    C'JJJJJJJJ                          '
TPUTUSER DC    C'UUUUUUU '
*
WORKEND  EQU   *                  END OF WORKAREA
***********************************************************************
         EJECT
*              REG 1 ENTRY MAPPING DSECT
PARMS    DSECT
PRMCOM   DS    A              ADDRESS OF COMMON EXIT PARM
PRMJBNP  DS    A              ADDRESS OF JOB NAME IN EBCDIC(8)
PRMPNP   DS    A              ADDRESS OF THE PROGRAMMER NAME(20)
PRMJTTP  DS    A              ADDRESS OF JOB (TCB) TIME(3)
PRMJACTP DS    A              ADDRESS OF JOB ACCOUNTING INFO(V)
PRMSTTP  DS    A              ADDRESS OF STEP (TCB) TIME(3)
PRMSACTP DS    A              ADDRESS OF STEP ACCOUNTING INFO(V)
PRMFLAGP DS    A              ADDRESS OF FL
PRMCCP   DS    A              ADDRESS OF COMPLETION CODE
PRMSMFP  DS    A              POINTER TO SMF RECORD
*              DSECT MAPPING COMMON EXIT PARM LIST
COM      DSECT
COMJN    DS    CL8            JOB NAME
COMRTIME DS    XL4            READER START TIME
COMRDATE DS    PL4            READER START DATE
COMSID   DS    CL4            SYSTEM ID FROM SMF PRM
COMUID   DS    CL8            USER IDENTIFICATION
COMSTNO  DS    XL1            CURRENT STEP NUMBER
COMSMFOP DS    XL1            SMF OPTIONS IN EFFECT
COMRST   DS    XL1            RESTART FLAGS
COMCLS   DS    CL1            JOB CLASS
COMUSER  DS    XL4            USER EXIT COMMUNICATION FIELD
         END
