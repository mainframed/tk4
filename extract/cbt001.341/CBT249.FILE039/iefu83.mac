IEFU83   TITLE 'SMF WRITE RECORD SELECT ROUTINE'
***********************************************************************
*                                                                     *
*             MODULE NAME = IEFU83                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  SMF WRITE INTERFACE TO SELECT       *
*                SMF RECORDS TO BE WRITTEN                            *
*                                                                     *
*             FUNCTION =                                              *
*                ALLOWS OR DISALLOWS CERTAIN RECORDS TO BE WRITTEN    *
*                TO THE SMF DATASET THEREBY MINIMIZING CONTENTION     *
*                                                                     *
*                OPERATION =                                          *
*                   EXTRACT THE RECORD ID FROM EACH RECORD AND        *
*                   LOAD A RETURN CODE ASSOCIATED WITH IT WITHIN      *
*                   A TABLE.                                          *
*                                                                     *
*                ATTRIBUTES = KEY 0, REENTRANT,                       *
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          *
*                                                                     *
*             INPUT = REG1 POINTS TO FULLWORD ADDRESS OF THE SMF      *
*                     RECORD TO BE WRITTEN.                           *
*                                                                     *
*             SINCE THIS ROUTINE IS NOT NORMALLY INCLUDE IN THE       *
*             SYSTEM, IT MAY SIMPLY BE PLACED INTO THE 'MLPA' LIST    *
*             TO CAUSE ACTIVATION (NO SYSTEM DEPENDENCIES EXIST).     *
*                                                                     *
***********************************************************************
IEFU83   CSECT
         SAVE  (14,12),,*          SAVE REGISTERS IN PASS SAVE AREA
         BALR  R12,R0              SET BASE REGISTER, R12
         USING *,R12               INFORM THE ASSEMBLER
         L     R1,0(R1)            LOAD R1 WITH SMF REC ADDRESS
         SR    R15,R15             CLEAR R15 FOR SUBSEQUENT IC'S
         IC    R15,5(R1)           LOAD R15 WITH RECORD TYPE
         IC    R15,RCTAB(R15)      LOAD R15 WITH RETURN CODE (00 OR 04)
         RETURN (14,12),T,RC=(15)  RETURN TO INVOKING MODULE
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
*
*        THE FOLLOWING IS THE RETURN CODE TABLE.  IT IS INDEXED
*        BY THE RECORD TYPE (NUMBER) TO EXTRACT THE RETURN CODE.
*        RETURN CODE (R15) = 0 TO WRITE THE RECORD
*                            4 TO SKIP THE RECORD
*
RCTAB    DS    0XL256
         DC    040XL1'00'          WRITE 000-039
         DC    001XL1'04'          SKIP  040
         DC    217XL1'00'          WRITE 041-255
         SPACE 1
         END
