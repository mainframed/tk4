DU       CSECT
*
*      THIS TSO COMMAND WILL DISPLAY DISK DEVICE STATUS AND
*      AVAILABLE FREE SPACE BY DEVICE TO A TSO TERMINAL.
*
*
         $REGS
         SAVE  (14,12)        SAVE REGISTERS
         LR    R12,R15        ESTABLISH REG 12 AS BASE
         USING DU,R12         INFORM ASSEMBLER
START1   L     R2,16          LOAD PNTR TO CVT
         L     R2,40(R2)      LOAD PNTR TO UCB LOOKUP TABLE
         SR    R6,R6          CLEAR REG 6
*
* CLEAR THE SCREEN
         LA    0,3            GET TPUT OPTION BYTE
         SLL   0,24           SHIFT TO HIGH ORDER
         LA    1,=X'F1115D7E114040133C404000'
         OR    1,0            COPY TPUT OPTION
         LA    0,12           SET SIZE FOR TPUT
         SVC   93             ISSUE TPUT SVC
*
         TPUT  HDR1,78        WRITE HEADER LINE
         TPUT  BLANX,78       WRITE BLANK LINE
CHEK     CLC   0(2,R2),=X'0000' NULL ENTRY?
         BE    REPRIME        YES, IGNORE IT
         CLC   0(2,R2),=X'FFFF' END OF LIST
         BE    EOJ            YES, EXIT
         LH    R4,0(R2)       LOAD PNTR TO UNIT CONTROL BLOCK
         CLC   18(1,R4),=X'20' SEE IF UCB IS FOR DASD
         BNE   REPRIME        NO, IGNORE IT
         CR    R4,R6          UCB ADDR
         BNH   REPRIME        NOT HIGH
         LR    R6,R4          SAVE DASD UCB ADDR IN R6
         TM    3(R4),X'80'    IS DEVICE ONLINE
         BZ    REPRIME        NO, IGNORE IT
         TM    3(R4),X'50'    PENDING OFFLINE/UNLOAD
         BNZ   REPRIME        YES, IGNORE IT
         TM    35(R4),X'80'   MOUNT PENDING
         BNZ   REPRIME        YES, IGNORE IT
         CLI   28(R4),X'00'   ANY VOLUME INFORMATION?
         BE    REPRIME        NO, VOLUME NOT VERIFIED YET
         MVC   CLASS,=CL6'3330-1' INDICATE 3330 MOD 11
         CLI   19(R4),X'0D'   IS IT A MODEL 11 3330?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'3330' INDICATE A MODEL 1 3330
         CLI   19(R4),X'09'   IS IT A MODEL 1 3330?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'3350' INDICATE A 3350
         CLI   19(R4),X'0B'   IS IT A 3350?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'2314' INDICATE 2314/2319
         CLI   19(R4),X'08'   IS IT A 2314/2319?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'2305-1' INDICATE A DRUM MODEL 1
         CLI   19(R4),X'06'   IS IT A 2305 MOD 1?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'2305-2' INDICATE A DRUM MODEL 2
         CLI   19(R4),X'07'   IS IT A MODEL 2 DRUM?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6'MSSC' INDICATE THE MSS CONTROL
         CLI   19(R4),X'42'   IS IT THE MSS?
         BE    CHKSTATS       YES, CHECK STATUS
         MVC   CLASS,=CL6' '  UNKNOWN DEVICE TYPE
CHKSTATS TM    17(R4),X'08'   IS THIS A VIRTUAL VOLUME?
         BZ    NOTVIRT        NO, CONTINUE
         MVI   CLASS+4,C'V'  INDICATE VIRTUAL DEVICE
NOTVIRT  MVI   STATUS,C'O'    INDICATE DEVICE UNALLOCATED
         TM    3(R4),X'08'    IS IT ALLOCATED
         BNO   *+8            YES, DONT RESET STATUS
         MVI   STATUS,C'A'    NO, INDICATE DEVICE ALLOCATED
         MVC   ATTR,=C'PRV'   INDICATE PRIVATE
         TM    34(R4),X'10'   IS IT PRIVATE
         BO    DOSVC          YES, CONTINUE
         MVC   ATTR,=C'PUB'   INDICATE PUBLIC
         TM    34(R4),X'08'   IS IT PUBLIC
         BO    DOSVC          YES, CONTINUE
         MVC   ATTR,=C'STG'   INDICATE STORAGE
         TM    34(R4),X'04'   IS IT STORAGE
         BO    DOSVC          YES, CONTINUE
         MVC   ATTR,=C'   '   CLEAR INDICATOR
DOSVC    LA    0,0(0,R4)      LOAD PNTR TO UCB INTO REG 0
         LA    R1,MSGAREA     LOAD PNTR TO MSGAREA INTO REG 1
         SVC   78             GET DATA FOR THIS DASD
         LTR   R15,R15        TEST REG 15 FOR ZERO
         BNZ   REPRIME        ERROR, CHECK NEXT DEVICE
CNTNU    MVC   VOLUME,28(R4)  MOVE VOLUME SERIAL NUMBER
         MVC   ADDR,13(R4)    MOVE DEVICE ADDRESS
         ICM   R11,1,35(R4)   OPEN DCBS
         N     R11,=X'0000007F' TURN OFF UCBMOUNT BIT, IF ON
         CVD   R11,DBWRD      CHANGE TO PACKED DECIMAL
         UNPK  DCBS,DBWRD+6(2)  MOVE TO DETAIL LINE
         OI    DCBS+2,X'F0'   CHANGE SIGN
         ICM   R11,1,38(R4)   NUMBER OF CURRENT USERS
         CVD   R11,DBWRD      CHANGE TO PACKED DECIMAL
         UNPK  USRS,DBWRD+6(2)  MOVE TO DETAIL LINE
         OI    USRS+2,X'F0'   CHANGE SIGN
         MVC   DTLCYL,FREECYL      MOVE
         MVC   DTLTRK,FREETRK      DETAIL
         MVC   DTLXTNT,FREEXTNT    TO
         MVC   DTLCYLG,CONTGCYL    OUTPUT
         MVC   DTLTRKG,CONTGTRK    BUFFER
         CP    LINECNT,=P'23' WITHIN TUBE SIZE   (SPF)
         BL    NEXTLINE       YES, WRITE RECORD
         ZAP   LINECNT,=P'2'  RESET LINE COUNT
         TPUT  HDR1,78        WRITE HEADER LINE
         TPUT  BLANX,78       WRITE BLANK LINE
NEXTLINE AP    LINECNT,=P'1'  BUMP LINE COUNT
         TPUT  DTLLNE,78      WRITE DETAIL RECORD
REPRIME  LA    R2,2(R2)       ADVANCE 1 HALFWORD IN LIST
         B     CHEK           CONTINUE
EOJ      RETURN (14,12),RC=0 RESTORE REGS/RETURN
         DS    0F
LINECNT  DC    PL2'02'        INITIAL LINE COUNT = PAGE-HDRS + CMD
HDR1     DS    0CL078
         DC    C'DEV TYPE   STATUS   SER    DCBS USR        CYLS TRKS  .
               XTNT  LGST CONTIG                                '
BLANX    DS    0CL078
         DC    CL078' '
*
*        THE FOLLOWING IS THE TPUT MESSAGE DETAIL
*
DBWRD    DC    D'0'
DTLLNE   DS    0CL078
ADDR     DC    CL3' ',C' '    DEVICE ADDRESS
CLASS    DC    CL6' ',C' '    DEVICE TYPE (CLASS)
STATUS   DC    C' '           STATUS (OPEN/ALLOCATED)
         DC    CL1' ',C'('    PADDING-LEFT PAREN
ATTR     DC    CL3' '         MOUNT ATTRIBUTE
         DC    C')',CL2' '    RIGHT PAREN-PADDING
VOLUME   DS    CL6            VOLUME SERIAL NUMBER
         DC    CL2' '         PADDING
DCBS     DC    CL3' '         OPEN DCBS
         DC    CL1' '         PADDING
USRS     DC    CL3' '         OPEN DCBS
         DC    CL8' '         PADDING
DTLCYL   DS    CL4            TOTAL FREE CYLINDERS
         DC    CL1' '         PADDING
DTLTRK   DS    CL4            TOTAL FREE TRACKS
         DC    CL2' '         PADDING
DTLXTNT  DS    CL4            NUMBER OF EXTENTS
         DC    CL2' '         PADDING
DTLCYLG  DS    CL4            LARGEST CONTIG CYLS
         DC    CL1' '         PADDING
DTLTRKG  DS    CL4            LARGEST CONTIG ADDITIONAL TRACKS
         DC    CL10' '        PADDING
*
*        THE FOLLOWING IS THE SVC 78 PARAMETER LIST
*
MSGAREA  DS    0CL30
         DS    CL6            CONSTANT - 'SPACE='
FREECYL  DS    CL4            TOTAL NO. FREE CYLINDERS
         DS    CL1            CONSTANT - ','
FREETRK  DS    CL4            TOTAL NO. FREE TRACKS OUTSIDE CYLS
         DS    CL1            CONSTANT - ','
FREEXTNT DS    CL4            TOTAL NO. FREE EXTENTS
         DS    CL1            CONSTANT - '/'
CONTGCYL DS    CL4            LARGEST CONTIGUOUS FREE AREA
         DS    CL1             IN CYLS AND TRKS
CONTGTRK DS    CL4
         END
