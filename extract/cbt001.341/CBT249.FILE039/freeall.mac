***********************************************************************
*
*        F R E E A L L   - TSO COMMAND PROCESSOR (MVS)
*
*  FUNCTION:  FREES ALL DYNAMICALLY ALLOCATED DATA SETS THAT ARE
*             NOT IN USE (EXCEPT DDNAME SYSPROC)
*
*  DATE:      8/01/76
*
***********************************************************************
         EJECT
FREEALL  CSECT
         REGEQU
         STM   R14,R12,12(R13)  SAVE REGS
         BALR  R12,0
         USING *,R12
         LR    R8,R1
         USING CPPL,R8
         GETMAIN R,LV=LDYNAMIC
         LR    R11,R1
         USING WORKAREA,R11
         XC    WORKAREA(LDYNAMIC),WORKAREA
         LA    R9,SAVEAREA
         ST    R13,SAVEAREA+4
         ST    R9,8(,R13)
         LR    R13,R9
         L     R1,CPPLUPT
         L     R2,CPPLECT
         LA    R3,ECB
         L     R4,CPPLPSCB
         LA    R5,DAPB
         DROP  R8
         STM   R1,R5,DAPL
         EJECT
         L     R1,16      CVT ADDR
         L     R8,0(R1)    TCB-TCB ADDR
         L     R8,4(,R8)  TCB ADDR
         L     R2,180(R8)  JOB STEP CONTROL BLOCK ADDRESS
         L     R9,320(R2)   DSAB QUE DISCRIPTOR BLOCK
         L     R9,12(R9)    POINT TO FIRST DSAB
         B     INNER        BRANCH TO INNER LOOP
*        CHECK EACH DSAB FOR TO SEE IF IT CAN BE FREE'D
         USING DSAB,R9
LOOP     L     R9,DSABFCHN  GET NEXT DSAB
INNER    LTR   R9,R9        LAST ONE?
         BZ    RETURN       YES, EXIT
         CLC   DSABOPCT,=H'0' IS DATASET OPEN?
         BNE   LOOP         YES, SKIP THIS DATASET
         TM    DSABFLG1,DSABDALC FREEABLE?
         BZ    LOOP         NO, GET NEXT DSAB
         TM    DSABFLG1,DSABNUSE IS DATASET IN USE?
         BO    LOOP         YES, CANT FREE IT
         TM    DSABFLG3,DSABCATL+DSABJSCT IS CAT/JSTEPCAT?
         BNZ   LOOP         YES, CANT FREE IT
*        CHECK DD NAME TO SEE IF IT CAN BE FREE'D
         L     R15,DSABTIOT GET TIOT ENTRY POINTER
         CLI   4(R15),X'00' CHECK DDNAME FOR VALIDITY  ?
         BE    LOOP         NOT ALPHA, IGNORE IT
         CLC   =CL8'SYSPROC',4(R15)  CHECK FOR DDNAME OF "SYSPROC"
         BE    LOOP         YES, GET NEXT DSAB
         CLC   =CL8'SYSTERM',4(R15)  CHECK FOR DDNAME OF "SYSPRINT"
         BE    LOOP         YES, GET NEXT DSAB
         CLC   =CL8'SYSPRINT',4(R15) CHECK FOR DDNAME OF "SYSPRINT"
         BE    LOOP         YES, GET NEXT DSAB
         CLC   =CL8'SYSIN',4(R15)    CHECK FOR DDNAME OF "SYSIN"
         BE    LOOP         YES, GET NEXT DSAB
         CLC   =CL8' ',4(R15) CHECK FOR CONCATINATIONS
         BE    LOOP         YES, GET NEXT DSAB
*        LOAD NEXT DSAB NOW SINCE THIS ONE WILL BE FREE'D
         L     R9,DSABFCHN  GET NEXT DSAB
         MVC   DAPB(DAPB18L),DAPB18C INITIALIZE DAPB
         LA    R8,DAPB  MAKE DAIR 18 ADDRESSABLE
         USING DAPB18,R8
         MVC   DA18DDN,4(R15)    MOVE IN DDNAME
         LA    R1,DAPL  ADDRESS OF DAPL
         LINK  EP=IKJDAIR  AND FREE DSE
         B     *+4(R15)  TEST RC
         B     INNER            CHECK NEXT DSAB
         DC    20Y(0)  OTHERS ABEND
RETURN   LR    R1,R13  DYNAMIC AREA ADDRESS IN R1
         LA    R0,LDYNAMIC  SIZE OF AREA
         L     R13,4(,R13)  PREV SAVE AREA
         LA    R1,0(,R1)  CLEAR HI ORDER
         SVC   10  FREEMAIN
         LM    14,12,12(13)  RESTORE REGS
         LA    15,0  RETURN CODE
         MVI   12(13),X'FF'  INDICATE RETURN
         BR    14  RETURN
         EJECT
DAPB18C  DS    0F
         DC    X'0018'  DA18CD
         DC    H'0'  DA18ELG
         DC    H'0'  DA18DARC
         DC    H'0'  DA18CTRC
         DC    A(0)  DA18PDSN
         DC    CL8' '  DA18DDN
         DC    CL8' '  DA18MNM
         DC    CL2' '  DA18SCLS
         DC    X'00'  DA18DSP2
         DC    X'10'  DA18CTL  -  RLSE PERM ALLOC
         DC    CL8' '  DA18JBNM
DAPB18L  EQU   *-DAPB18C  LENGTH
         EJECT
WORKAREA DSECT
SAVEAREA DS    18F
DAPB     DS    0F
         ORG   DAPB+DAPB18L
         DS    0D
DAPL     DS    5F
ECB      DS    F
         DS    0D
LDYNAMIC EQU   *-WORKAREA
         SPACE 3
         IKJDAP18
         EJECT
         IKJCPPL
         EJECT
         IHADSAB
         SPACE 5
         END
