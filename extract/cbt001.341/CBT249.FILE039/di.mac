***********************************************************************
*                                                                     *
*        THIS TSO COMMAND WILL DISPLAY JES2 BATCH INITIATORS,         *
*        CLASSES ASSIGNED, AND ACTIVE JOBS UNDER THE INITS.           *
*                                                                     *
*        CHAINS THROUGH CVT , JESCT BLOCK, SSCT BLOCK,                *
*        SSVT JES2 BLOCKS AND PIT JES2 BLOCKS TO OBTAIN               *
*        INITIATOR CLASSES.                                           *
*        THEN GET SJB FROM PIT TO GET JOBNAMES, USED CLASS.           *
*                                                                     *
*        PROGRAM MUST BE ASSEMBLED WITH INPUT LIBRARY DSN             *
*           CONCATENATING 'SYS1.HASPSRC' SINCE JES2 DSECTS ARE        *
*              CONTAINED THEREIN.  THIS MEANS IF JES2 MAINTENANCE     *
*                IS APPLIED, NORMALLY ONLY A RE-ASSEMBLY WILL         *
*                   BE ALL THAT IS REQUIRED.                          *
*                                                                     *
*       NOTE, USE OF THIS ROUTINE IS LIMITED TO THOSE USERS WITH      *
*       OPERATOR AUTHORITY                                            *
*                                                                     *
*       AUGUST 1979,                                                  *
*                                                                     *
***********************************************************************
         EJECT
         TITLE 'DI -- TSO DISPLAY INITIATORS'
DI       $PROLOG
         USING CVTDSECT,R1        SETUP CVT BASE
         USING JESCT,R2           SETUP JESCT BASE
         USING SSCT,R3            SETUP SSCT BASE
         USING SSVT,R4            SETUP SSVT BASE
         USING PITDSECT,R5        SETUP PIT BASE
         USING SJBDSECT,R11       SETUP SJB BASE
         LINK  EP=CLEAR           CLEAR SCREEN
         TPUT  SLINE,L'SLINE      PRINT HEADING LINE 1
         TPUT  ALINE,L'ALINE      PRINT HEADING LINE 2
         TPUT  BLANKS,L'BLANKS    PRINT BLANK LINE
         L     R1,16              GET CVT POINTER
         L     R1,0(R1)           OLD/NEW POINTER
         L     R1,4(R1)           OUR TCB POINTER
         L     R15,12(R1)         OUR TIOT POINTER
*        CLI   0(R15),C'S'        IS THIS SYSTEMS USER?
*        BNE   NOTAUTH            NO, NOT AUTHORIZED
         L     R1,180(R1)         JSCB POINTER
         L     R1,264(R1)         PSCB POINTER
         TM    16(R1),X'80'       DO WE HAVE OPER ABILITY?
         BO    CONTINUE           YES, CONTINUE
NOTAUTH  TPUT  LLINE,L'LLINE      MSG- NOT AUTH USER
         B     EXIT               EXIT
CONTINUE L     R1,16              GET CVT
         L     R2,296(R1)         GET JESCT    (JES CONTROL TABLE)
         L     R3,JESSSCT         GET FIRST JES SSCT (SSCVT)
NEXTSSCT L     R4,SSCTSSVT        GET JES SSVT
         L     R5,$SVPIT          GET PIT ADDR--5.302 JES LOGIC+MAINT
NEXTPIT  EQU   *                  NOW READY TO WRITE
         MVC   IDENT(L'PLINE),BLANKS CLEAR DETAIL LINE
         MVI   IDENTP,C'I'        RESET 'I' IDENTITY
         TM    PITSTAT,PITHOLDA   PIT DRAINED $PI ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'DRAINED '  MOVE IN DRAINED STATUS
         TM    PITSTAT,PITHOLD1   PIT DRAINED $P IN ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'DRAINED '  MOVE IN DRAINED STATUS
         TM    PITSTAT,PITHALTA   PIT HALTED  $Z I ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'HALTED  '  MOVE IN HALTED  STATUS
         TM    PITSTAT,PITHALT1   PIT HALTED  $Z IN ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'HALTED  '  MOVE IN HALTED  STATUS
         TM    PITSTAT,PITINIT    OS INIT EXISTS FOR PIT?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'INACTIVE'  MOVE IN INACTIVE
         TM    PITSTAT,PITBUSY    PIT BUSY ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'ACTIVE  '  MOVE IN ACTIVE
         TM    PITSTAT,PITIDLE    PIT IDLE ?
         BNO   *+10               IF NOT CONTINUE
         MVC   STAT,=C'IDLE    '  MOVE IN IDLE
         CLC   STAT,=8C' '        STATUS NOT AVAILABLE ?
         BNE   *+10               IF NOT CONTINUE
         MVC   STAT,=C'UNAVAIL '  MOVE IN STATUS NOT AVAIL.
         CLI   PITPATID,C' '      NULL 1ST CHAR?
         BNE   IDENTOK            NO, CONTINUE
         MVC   IDENT(1),PITPATID+1 MOVE IN ID---FORMAT CL1
         B     IDENTSKP           SKIP NEXT MOVE
IDENTOK  MVC   IDENT,PITPATID     MOVE ID IN---FORMAT CL2
IDENTSKP MVC   INITNAM,PITCLASS   MOVE THE INIT CLASSES TO OUTPUT
*
*    &MAXCLAS PARAMETER ON GEN LIMITS MAX CLASSES, ABOVE MAY NEED
*    TO BE CHANGED IF OTHER THAN 8 CLASSES.
*
         TM    PITSTAT,PITBUSY    PIT BUSY ?
         BNO   PRINT              IF NOT CONTINUE
         L     R11,PITSJB         GET SJB ADDRESS
         MVC   JOBNAME,SJBJOBNM   GET JOBNAME FROM JOB CARD
         MVC   UCLASS,SJBJCLAS    GET USED CLASS
PRINT    CLI   LSW,C' '           CHECK PRINT SW FOR LEFT SIDE
         BE    MOVELEFT           MOVE IN LEFT PRINT AREA
         CLI   LSW,C'1'           CHECK PRINT SW FOR RIGHT SIDE
         BE    MOVERIGT           MOVE IN RIGHT PRINT AREA
ERROR    TPUT  ELINE,L'ELINE      WRITE ERROR LINE
         B     FINAL              END IF ERROR
PUTIT    TPUT  CLINE,L'CLINE      WRITE MESSAGE LINE
         MVC   LSW,=C' '          RESET PRINT SWITCH
         B     NEXTIT             GET NEXT PIT
FINAL    CLI   LSW,C' '           CHECK IF LAST DATA FILLED
         BE    EXIT               IF DONE, DON'T WRITE
         CLI   LSW,C'1'           CHECK IF LEFT ONLY FILLED
         BE    SPRIGHT            GO TO SPACE-RIGHT
LASTWR   TPUT  CLINE,L'CLINE      WRITE LAST LINE NOW
EXIT     SR    R15,R15            CLEAR RETURN CODE
         $EPILOG
MOVELEFT MVC   LSW,=C'1'          SET LEFT PRINT FLAG
         MVC   CLEFT,PLINE        MOVE IN LEFT SIDE
NEXTIT   L     R5,PITNEXT         GET NEXT PIT---REFER 5.200 JES2
         LTR   R5,R5              CHK R5 FOR VALID ADDR
         BZ    FINAL              END PROCESS FOR SSCT
         B     NEXTPIT            GO BACK
SPRIGHT  MVC   CRIGHT,FLINE       MOVE SPACES TO RIGHT
         B     LASTWR             GO TO LAST WRITE
MOVERIGT MVC   LSW,=C'2'          SET FULL PRINT FLAG
         MVC   CRIGHT,PLINE       MOVE IN RIGHT SIDE
         B     PUTIT              GO TO WRITE LINE
         EJECT
LSW      DC    C' '               LINE PRINT SWITCH
*
* LSW ' ' IS NO FILLED, '1' IS LEFT FILLED, '2' IS BOTH FILLED
*
PLINE    DS    0CL40
IDENTP   DC    CL1'I'
IDENT    DC    CL2' ',CL1' '
INITNAM  DC    CL8' ',CL1' '
STAT     DC    CL8' ',CL3' '
JOBNAME  DC    CL8' ',CL1' '
UCLASS   DC    CL1' ',CL11' ' (PAD)
CLINE    DS    0CL80
CLEFT    DC    CL40' '
CRIGHT   DC    CL40' '
SLINE    DS    0CL80
         DC    C'---------------------------  DISPLAY INI'
         DC    C'TIATORS  ------------------------------',CL1' '
ALINE    DS    0CL80
         DC    C'ID  CLASSES  STAT       JOBNAME  C   ',CL3' '
         DC    C'ID  CLASSES  STAT       JOBNAME  C   ',CL3' '
BLANKS   DC    CL80' '
FLINE    DC    CL40' '
ELINE    DC    CL80'ERROR IN MESSAGE ROUTINE'
ILINE    DC    CL80'NO SSVT CONTROL BLOCK FOUND'
JLINE    DC    CL80'NO SSCT CONTROL BLOCK FOUND'
LLINE    DC    CL80'USER NOT AUTHORIZED'
         PRINT OFF
         $CVT               GEN $CVT
         $JESCT             GEN $JESCT
         $SSCT              GEN $SSCT
         $SVT               GEN $SVT
         $PIT               GEN $PIT
         $SJB               GEN $SJB
         END
