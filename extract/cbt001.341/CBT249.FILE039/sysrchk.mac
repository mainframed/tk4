         TITLE 'BATCH JOB CONDITION CODE CHECKING ROUTINE'
**********************************************************************
*
*       THIS ROUTINE WILL PLACE A MESSAGE UPON THE OPERATOR'S
*       CONSOLE TO INFORM AS TO WHETHER A JOB HAS BEEN RUN
*       NORMALLY OR ABNORMALLY.  ADDITIONALLY, IF AN ABEND HAS
*       OCCURRED, A SPECIAL MESSAGE IS DISPLAYED.
*
*       EACH JOB TO BE CHECKED MUST INCLUDE A FINAL EXECUTE CARD
*       TO EXECUTE A 'RUNCHECK' PROC AS DEFINED BELOW.
*
*  EXAMPLE RUNCHECK PROC FOLLOWS:
*
*  //RUNCHECK   PROC
*  //GOOD       EXEC   PGM=SYSRCHK,COND=(0,LT),PARM='GOOD'
*  //BAD        EXEC   PGM=SYSRCHK,COND=(4,EQ,GOOD),PARM='BAD'
*  //ABORT      EXEC   PGM=SYSRCHK,COND=ONLY,PARM='RUN'
*
**********************************************************************
SYSRCHK  $PROLOG
         L     R1,0(R1)            ADDRESS OF PARM DATA
         LH    R2,0(R1)            PARM LENGTH
         LTR   R2,R2               ANY PARM PASSED?
         BNZ   CHECK1              YES, CHECK FOR VALID PARM
PARMBAD  WTO   'RUNCHECK - INVALID/NO PARM SPECIFIED',ROUTCDE=(11,15)
         ABEND 999                 STOP ANY FURTHUR PROCESSING
CHECK1   CLC   =C'GOOD',2(R1)      GOOD RUN?
         BNE   CHECK2              NO, NEXT TEST
         OI    OPTION,GOOD         SET GOOD RUN OPTION
         B     NORMAL              RETURN WITHOUT MESSAGE
CHECK2   CLC   =C'BAD',2(R1)       BAD RUN?
         BNE   CHECK3              NO, NEXT TEST
         OI    OPTION,BAD          SET BAD RUN OPTION
         B     PARMOK              GET TIOT POINTER
CHECK3   CLC   =C'ABORT',2(R1) ABORT OPTION?
         BNE   PARMBAD             NO, ERROR
         OI    OPTION,ABORT        SET ABORT OPTION
PARMOK   L     R11,16              GET CVTPOINTER
         L     R11,0(R11)          TCB DOUBLEWORD POINTER
         L     R11,4(R11)          CURRENT TCB POINTER
         L     R11,12(R11)         TIOT POINTER
         MVC   WTO1+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         MVC   WTO2+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         MVC   WTO3+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         WTO   '*********************************',ROUTCDE=(11,15)
         TM    OPTION,GOOD         GOOD RUN?
         BNO   TEST                NO, LONG FORMAT MESSAGE
WTO1     WTO   '*****   JJJJJJJJ GOOD-RUN   *****',ROUTCDE=(11,15)
         B     SKIP                SKIP TO LAST
TEST     WTO   '*****                       *****',ROUTCDE=(11,15)
         TM    OPTION,BAD          BAD RUN?
         BNO   WTO3                NO, MUST BE ABORT
WTO2     WTO   '*****   JJJJJJJJ BAD-RUN    *****',ROUTCDE=(11,15)
         B     LAST                SKIP NEXT MSG
WTO3     WTO   '*****   JJJJJJJJ ABORTED    *****',ROUTCDE=(11,15)
LAST     WTO   '*****                       *****',ROUTCDE=(11,15)
SKIP     WTO   '*********************************',ROUTCDE=(11,15)
NORMAL   LA    R15,4               SET RETURN CODE OF 4
         $EPILOG
OPTION   DC    X'00'               REQUEST OPTION
GOOD     EQU   X'01'               GOOD RUN REQUEST
BAD      EQU   X'02'               BAD RUN REQUEST
ABORT    EQU   X'04'               ABORT REQUEST
         END
