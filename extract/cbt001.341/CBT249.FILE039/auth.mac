USERSVC  EQU   XXX <========= USER DEFINDED 'AUTH' SVC NUMBER
         PRINT NOGEN
***********************************************************************
*
*        THIS TSO COMMAND PROCESSOR WILL INVOKE INSTALLATION
*        DEFINED AUTHORIZED PROGRAMS INTERACTIVELY BY CALLING
*        A USER WRITTEN SVC AND USING MODESET TO AUTHORIZE THE
*        USER.  AFTER THE INVOCATION, THE USER WILL BE RETURNED
*        TO NON-AUTHORIZED STATE, AND RETURNED TO THE CONTROL
*        OF THE TMP.  CARE MUST BE TAKEN TO INSURE THAT THE
*        NON-RESTRICTED (ALLOWABLE) MODULES TABLE IS UPDATED
*        IF CERTAIN PROGRAMS CAUSE OVERHEAD OR SECURITY
*        VIOLATIONS.
*
*        ALIAS XEQ
*
***********************************************************************
AUTH     CSECT
         REGEQU
         USING *,R12          PROVIDE ADDRESSABILITY
         SAVE  (14,12)        SAVE TMP REGS
         LR    R12,R15        COPY ENTRY POINT ADDRESS
         LR    R2,R13         COPY PREVIOUS SAVE AREA POINTER
         LR    R11,R1         SAVE CPPL ADDRESS
         USING CPPL,R11       CPPL ADDRESSABILITY
         LA    R0,@SIZE       SET SIZE FOR GETMAIN
         GETMAIN R,LV=(0)     GET A WORKAREA
         LR    R13,R1         COPY GOTTEN ADDRESS
         USING @WORK,R13      INFORM ASSEMBLER
         XC    @WORK(@CLSZ),@WORK  CLEAR GOTTEN STORAGE
         ST    R2,@SAVE+4     SAVE CALLERS SAVE POINTER
         ST    R13,8(R2)      CHAIN PREVIOUS SAVE AREA
         LA    R15,@PARMSZ    POINTER TO PARM INFO AREA
         ST    R15,@PARMP     SAVE IN PARM POINTER
         OI    @PARMP,X'80'   SET THE VL BIT
         MVC   @BLDLLST(@TCB-@BLDLLST),BLDLLIST MOVE MODELS
         MVC   @PPLUPT,CPPLUPT  MOVE USER PROFILE TABLE
         MVC   @PPLECT,CPPLECT  MOVE ENVIRONMENT CONTROL TAB
         LA    R1,@ECB        ADDRESS OF ECB
         ST    R1,@PPLECB     PROVIDE DUMB ECB
         L     R1,IKJADCON    ADDRESS OF IKJPARM CSECT
         ST    R1,@PPLPCL     AS PARSE CONTROL LIST
         LA    R1,@ANSWER     PARSE ANSWER AREA
         ST    R1,@PPLANS     ADDRESS FOR PARSE RETURN
         MVC   @PPLCBUF,CPPLCBUF  MOVE COMMAND BUF POINTER
         L     R1,CPPLCBUF    GET POINTER TO COMMAND BUFFER
         CLC   4(4,R1),=C'AUTH'  ARE WE CALLED FOR AUTHORIZATION?
         BE    PARSEIT        YES, CONTINUE
         OI    @FLAG,@XEQ     NO, FLAG IT AS SUCH
         DROP  R11            DROP ADDRESSABILITY
PARSEIT  LA    R1,@PPLUPT     ADDRESS OF PARSE PARM LIST
         LINK  EP=IKJPARS     CALL TSO COMMAND PARSE
         L     R11,@ANSWER    GET POINTER TO PARSE ANSWER
         USING IKJPARMD,R11   GAIN ADDRESSABILITY TO ANSWER
         L     R1,MODNAME     GET POINTER TO MODULE NAME
         TM    MODNAME+6,X'80'  WAS MODULE NAME GIVEN?
         BZ    RLSA           NO, IGNORE REQUEST
         LH    R4,MODNAME+4   GET SIZE OF MODULE NAME
         BCTR  R4,R0          DECREMENT FOR EXECUTE
         EX    R4,MOVENAME    MOVE TO WORK AREA
         OC    KEYWD(2),KEYWD DOES USER WANT RETCODE MESSAGE?
         BZ    CHKQSTR        NO, CONTINUE
         OI    @FLAG,@MSG     SET MESSAGE FLAG
CHKQSTR  TM    QSTRING+6,X'80'  DID USER PROVIDE A PARM
         BZ    COMMON         NO, USE DEFAULT
         LA    R1,@PARMSZ     POINTER TO SHORT PARM AREA
         L     R4,QSTRING     POINTER TO PARSE PARM ANSWER
         LH    R5,QSTRING+4   GET SIZE OF PARM
         CH    R5,=H'100'     CHECK MAX SIZE OF PARM (BATCH)
         BNH   PARMNORM       SHORT PARM, FAST PATH (SKIP GETMAIN)
         OI    @FLAG,@GM      SET GETMAIN FLAG
         LA    R0,2(R5)       GET SIZE PLUS 2 (LENGTH)
         BAL   R1,*+4         INDICATE GETMAIN
         SVC   10             GET PARM STORAGE
         STCM  R1,7,@PARMP+1  SAVE IN PARM POINTER
PARMNORM STH   R5,0(R1)       SET SIZE OF PARM
         BCTR  R5,R0          DECREMENT FOR EXECUTE
PARMLOOP CH    R5,=H'256'     TOO MUCH TO MOVE?
         BL    LASTMOVE       NO, MOVE IT
         MVC   2(256,R1),0(R4) MOVE QUOTED STRING TO PARM AREA
         LA    R1,256(R1)     BUMP MOVE TO POINTER
         LA    R4,256(R4)     BUMP MOVE FROM POINTER
         SH    R5,=H'256'     DECREMENT REMAINING SIZE
         BM    COMMON         ALL DONE, SKIP LAST MOVE
         B     PARMLOOP       BACK FOR NEXT SEGMENT
LASTMOVE EX    R5,MOVEPARM    MOVE PARM TO GOTTEN STORAGE
COMMON   TM    @FLAG,@XEQ     IS THIS XEQ REQ (NON-AUTH)?
         BO    LOOKUP         NO, SKIP CHECK FOR OPER ABILITY
         L     R1,16          GET CVT POINTER
         L     R1,0(R1)       OLD/NEW POINTER
         L     R1,4(R1)       OUR TCB POINTER
         L     R15,12(R1)     OUR TIOT POINTER
*        CLI   0(R15),C'S'    IS THIS SYSTEMS USER?
*        BNE   INFORM         NO, INFORM USER OF RESTRICTION
         L     R1,180(R1)     JSCB POINTER
         L     R1,264(R1)     PSCB POINTER
         TM    16(R1),X'80'   DO WE HAVE OPER ABILITY?
         BO    LOCATE         YES, CONTINUE
INFORM   TPUT  ERR1,60        INFORM USER OF RESTICTION
         LA    R15,16         GET CONDITION CODE
         ST    R15,@ECB       SET CONDITION CODE
         B     RLSA           RETURN TO USER
LOOKUP   LA    R1,AUTHTBL     GET POINTER TO AUTHORIZED MODULES
LOOKMORE CLI   0(R1),X'00'    END OF TABLE?
         BE    LOCATE         YES, CONTINUE NON-AUTHORIZED
         CLC   0(8,R1),@MODNAME  IS REQUESTED NAME AUTHORIZED?
         BE    FORCE          YES, FORCE AUTHORIZATION
         LA    R1,8(R1)       BUMP TO NEXT ENTRY
         B     LOOKMORE       CHECK NEXT ENTRY
FORCE    NI    @FLAG,X'FF'-@XEQ FORCE AN AUTHORIZED ATTACH
LOCATE   BLDL  0,@BLDLLST     ISSUE BLDL TO LOCATE MODULE
         LTR   R15,R15        DOES IT EXIST?
         BZ    MODFOUND       YES, CONTINUE
         ST    R15,@ECB       SET CONDITION CODE
         TPUT  ERR2,60        INFORM USER OF ERROR
         B     RLSA           RETURN TO TMP
MODFOUND EQU   *
         L     R2,16          GET CVT POINTER
         CLI   116(R2),X'13'  IS THIS MVS SYSTEM?
         BNE   ATTACH         NO, SKIP MVS SVC
         TM    @FLAG,@XEQ     IS THIS NON-AUTH REQUEST?
         BO    ATTACH         YES, SKIP AUTH SVC CALL
         LA    R1,PASSWORD    SET AUTH PARM
         SVC   USERSVC        CALL AUTHORIZATION SVC
ATTACH   LA    R1,@PARMP      POINT TO PARM FIELD
         ATTACH  DE=@MODNAME,ECB=@ECB,SF=(E,@ATTACH)  ATTACH
         ST    R1,@TCB        SAVE TCB ADDRESS
         WAIT  ECB=@ECB       WAIT FOR COMPLETION
         TM    @FLAG,@GM      WAS GETMAIN DONE FOR PARM AREA?
         BZ    CHKMSG         NO, CHECK IF MESSAGE IS TO BE DONE
         LH    R0,@PARMSZ     GET SIZE FOR FREEMAIN
         L     R1,@PARMP      GET AREA FOR FREEMAIN
         LA    R1,0(R1)       INDICATE FREEMAIN
         SVC   10             FREE GOTTEN PARM AREA
CHKMSG   TM    @FLAG,@MSG     SHOULD WE INFORM USER?
         BZ    DETACH         NO, IGNORE RETCODE MSG
         MVC   @PARM(L'ERR0),ERR0 MOVE MODEL MESSAGE
         MVC   @PARM(8),@MODNAME MOVE MODULE NAME
         UNPK  @PARM+19(7),@ECB+1(4) UNPACK FOR CONVERT
         TR    @PARM+19(6),TRTBL-240 MAKE PRINTABLE
         MVI   @PARM+25,C' '  CLEAR RESIDUAL
         LA    R0,60          GET SIZE OF MESSAGE
         LA    R1,@PARM       GET ADDRESS OF MESSAGE
         SVC   93             ISSUE TPUT SVC
DETACH   DETACH @TCB          DO THE DUMB DETACH
         L     R2,16          GET CVT POINTER
         CLI   116(R2),X'13'  IS THIS MVS SYSTEM?
         BNE   RLSA           NO, SKIP USER SVC
         TM    @FLAG,@XEQ     IS THIS NON-AUTH REQUEST
         BO    RLSA           YES, IGNORE AUTH SVC
         BAL   R1,*+4         VALIDATE PARM REG
         SVC   USERSVC        RELINQUISH AUTHORIZATION
RLSA     IKJRLSA @ANSWER      FREE PARSE STORAGE
         DROP  R11            DROP ADDRESSABILITY
EXIT     L     R10,@ECB       GET RETURN CODE
         LR    R1,R13         GET ADDRESS TO FREE
         LA    R0,@SIZE       SIZE OF DATA TO FREE
         L     R13,@SAVE+4    GET PREVIOUS SAVE AREA
         FREEMAIN  R,LV=(0),A=(1)  FREE GOTTEN STORAGE
         LR    R15,R10        SET UP RETCODE REG
         RETURN  (14,12),RC=(15) RETURN TO CALLER
ERR0     DC    CL60'XXXXXXXX - RETCODE XXXXXX '
ERR1     DC    CL60'MODULE NOT AUTHORIZED FOR YOUR USE'
ERR2     DC    CL60'REQUESTED MODULE NOT FOUND'
         EJECT
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
MOVENAME MVC   @MODNAME(1),0(R1)  MOVE NAME TO LINK EP
MOVEPARM MVC   2(0,R1),0(R4)  MOVE QUOTED STRING TO PARM AREA
         DS    0F             FULLWORD ALIGNMENT
BLDLLIST DC    H'1',H'58',CL8' ',XL52'00'  BLDL LIST MODEL
ATTLIST  ATTACH  DE=*,ECB=*,SF=L  ATTACH LIST MODEL
PASSWORD DC    CL4'AUTH'      SUPER SVC PARM
BLANKS   DC    CL8' '         BLANKS FOR FILL
TRTBL    DC    C'0123456789ABCDEF' TRANS TABLE
AUTHTBL  DC    CL8'IEBCOPY'   COPY CAN BE USED BY ANYONE
         DC    CL8'IEFBR14'   NO-OP ALWAYS AVAILABLE
         DC    X'00'          END OF TABLE
IKJADCON DC    V(AUTHPCL)     ADDRESS OF PARSE PDL/PCL
AUTHPCL  IKJPARM
MODNAME  IKJIDENT  'PROGRAM NAME',MAXLNTH=8,OTHER=ALPHANUM,            X
               PROMPT='NAME OF PROGRAM TO BE ATTACHED'
QSTRING  IKJPOSIT  QSTRING    USER PARM STRING
KEYWD    IKJKEYWD
         IKJNAME  'RETCODE',  RETURN CODE KEYWORD                      X
               ALIAS=('RETURNCODE','COMPCODE','CODE')
         IKJENDP
         LTORG
         EJECT
***********************************************************************
*
*        WORK AREA DSECT AND TSO DSECTS
*
***********************************************************************
@WORK    DSECT
@SAVE    DS    18F            REG SAVE AREA
@BLDLLST DS    2H             BLDL PARM LIST
@MODNAME DS    CL8            BLDL DE NAME
         DS    XL52           BLDL PADDING
@ATTACH  ATTACH  DE=*,ECB=*,SF=L  ATTACH PARM LIST
@TCB     DS    F              ADDRESS OF SUBTASK TCB
@ECB     DS    F              PARSE/ATTACH ECB
@ANSWER  DS    F              PARSE ANSWER AREA
@PPLUPT  DS    F              PARSE PARM LIST UPT
@PPLECT  DS    F              PARSE PARM LIST ECT
@PPLECB  DS    F              PARSE PARM LIST ECB
@PPLPCL  DS    F              PARSE PARM LIST PCL
@PPLANS  DS    F              PARSE PARM LIST ANSWER ->
@PPLCBUF DS    F              PARSE PARM LIST COMMAND ->
@FLAG    DS    XL1            OPTION FLAGS
@XEQ     EQU   X'01'          XEQ COMMAND (NON-AUTH)
@MSG     EQU   X'02'          RETCODE MESSAGE REQUESTED
@GM      EQU   X'04'          GETMAIN DONE FOR PARM STORAGE
@PARMP   DS    F              PARM POINTER
*                             POINTS AT PARMZ FOR NO/SHORT PARM
*                             POINTS AT GOTTEN STORAGE FOR LONG PARM
@PARMSZ  DS    H              PARM SIZE
@CLSZ    EQU   *-@WORK        SIZE TO BE CLEARED
@PARM    DS    CL256          MESSAGE AREA
@SIZE    EQU   *-@WORK        SIZE OF WORKAREA
         IKJCPPL
         END
