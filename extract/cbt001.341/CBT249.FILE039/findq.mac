***********************************************************************
*
*        NOTE: --------- MVS VERSION OF FINDQ ----------
*
*
*        THIS ROUTINE RUNS THE 'SYSDSN' MAJOR QCB CHAIN
*        AND SEARCHES FOR A CONFLICT BETWEEN 'ANY' NAME OR
*        A REQUESTED NAME (IF SPECIFIED VIA PARM VALUE).  FOR
*        EACH ENTRY FOUND (MATCH/CONFLICT), EITHER A TPUT OR WTO
*        IS ISSUED INDICATING THE STATUS AND OWNER OF THE
*        DATASET NAME.  IF A PARM VALUE IS GIVEN, IT NEED NOT
*        BE A FULLY QUALIFIED NAME, SINCE THIS ROUTINE WILL
*        SUPPORT GENERIC SEARCHES.
*
*        PARMS:  (NULL)   SEARCH FOR ANY AND ALL CONFLICTS
*                         WITHIN EVERY MAJOR NAME.
*                DSNAME   SEARCH FOR ALL ENQUEUES FOR THE
*                         DATASET NAME (OR SUFFIX) WITHIN THE
*                         'SYSDSN' MAJOR ONLY.
*                *        DISPLAY 'ALL' MAJOR AND MINOR NAMES
*                         THAT ARE CURRENTLY ENQUEUED UPON.
*                ^        DISPLAY 'ALL' MAJOR AND MINOR NAMES
*                         WITH THE EXCEPTION OF THE 'SYSDSN'
*                         MAJOR (ALL BUT DSNAME ENQUEUES).
*
***********************************************************************
         EJECT
FINDQ    $PROLOG
         L     R10,0(R1)           PARM POINTER
         L     R15,16              GET CVT
         L     R15,0(R15)          OLD/NEW POINTERS
         L     R15,12(R15)         MY ASCB POINTER
         ICM   R15,7,61(R15)       GET POINTER TO TSB
         BZ    BATCH               NO TSB, MUST BE BATCH JOB
         OI    SWITCH,CMD          INDICATE TSO COMMAND
         LR    R2,R1               SAVE CPPL POINTER
         USING CPPL,R2             PROVIDE ADDRESSABILITY
         MVC   CMDUPT,CPPLUPT      COPY UPT ADDRESS
         MVC   CMDECT,CPPLECT      COPY ECT ADDRESS
         MVC   CMDCBUF,CPPLCBUF    COPY ADDRESS OF COMMAND BUF
         LA    R1,CMDUPT           PROVIDE LIST FOR PARSE
         LINK  EP=IKJPARS          PARSE COMMAND
         L     R2,CMDANSR          GET POINTER TO ANSWER AREA
         USING IKJPARMD,R2         PROVIDE ADDRESSABILITY
         TM    CMDOPT+6,X'80'      WAS A NAME PROVIDED?
         BZ    RLSA                NO, ASSUME CONFLICT SEARCH
         L     R1,CMDOPT           GET POINTER TO DSNAME
         LH    R2,CMDOPT+4         GET SIZE OF DSNAME
         STH   R2,CMDWORK          SAVE SIZE FOR COMMON ROUTINE
         BCTR  R2,R0               DECREMENT FOR EXECUTE
         EX    R2,CMDMOVE          MOVE TO WORK AREA
RLSA     LA    R10,CMDWORK         SIMULATE BATCH PARM
         IKJRLSA CMDANSR           FREE PARSE WORK AREA
BATCH    LH    R2,0(R10)           PARM SIZE
         OI    SWITCH,CONF         ASSUME CONFLICT SEARCH
         LTR   R2,R2               ANY PARM?
         BZ    GETCVT              NO, CHECK FOR CONFLICTS
         NI    SWITCH,X'FF'-CONF   RESET CONFLICT SWITCH
         LA    R1,BADPARM          INVALID LENGTH
         CH    R2,=H'44'           VALID SIZE?
         BH    MSG                 NO, INFORM
         LA    R1,NOTFOUND         NOT FOUND ERROR
         BCTR  R2,R0               DECREMENT FOR EXECUTE
         EX    R2,MOVEPARM         MOVE TO WORK AREA
         EX    R2,OR               INSURE UPPER CASE
         CLI   PARMNAME,C'*'       NORMAL 'ALL' REQUEST?
         BE    SETALL              NO, CONTINUE
         CLI   PARMNAME,C'^'       SPECIAL 'ALL' REQUEST?
         BNE   GETCVT              NO, CONTINUE
         OI    SWITCH,NODS         SET SPECIAL OPTION
         B     GETCVT              CONTINUE
SETALL   OI    SWITCH,ALL          SET SPECIAL OPTION
GETCVT   L     R5,16               CVT
         L     R5,640(R5)          QCB HEAD
         B     FINDMAJ@            SKIP COMMON CODE
FINDMAJ  L     R5,0(R5)            NEXT MAJOR
FINDMAJ@ LTR   R5,R5               END OF CHAIN?
         BZ    CHECK               YES, ALL DONE
         TM    SWITCH,CONF+ALL     CONFLICT SEARCH OR 'ALL' REQ'D
         BNZ   GETMIN              YES, PROCESS ALL MAJORS
         TM    SWITCH,NODS         SPECIAL 'ALL' OPTION?
         BZ    CHKNORM             NO, CHECK FOR SYSDSN MAJOR
         CLC   16(8,R5),=CL8'SYSDSN'  IS THIS THE DSNAME MAJOR?
         BE    FINDMAJ             YES, IGNORE IT
         CLC   16(8,R5),=CL8'SYSVSAM' IS THIS THE VSAM DSNAME MAJOR?
         BE    FINDMAJ             YES, IGNORE IT
         B     GETMIN              NO, PROCESS IT
CHKNORM  CLC   16(8,R5),=CL8'SYSDSN'  IS THIS THE DSNAME MAJOR?
         BNE   FINDMAJ             NO, TRY AGAIN
GETMIN   L     R3,8(R5)            GET MINOR QCB
         MVC   MAJORNM(8),16(R5)   MOVE MAJOR NAME TO MSG
         B     FINDMIN@            SKIP COMMON
FINDMIN  L     R3,0(R3)            GET MINOR QCB
FINDMIN@ LTR   R3,R3               VALID QCB?
         BZ    FINDMAJ             NO, END OF CHAIN
         TM    SWITCH,ALL+NODS     IS THIS AN ALL REQUEST?
         BNZ   CONTINUE            YES, PRINT ALL
         L     R9,8(R3)            GET 1ST QEL
         TM    SWITCH,CONF         CONFLICT SEARCH?
         BO    CHKCONF             YES, CHECK FOR CONFLICTS
         EX    R2,COMPARE          CHECK FOR SPECIFIED NAME/SUFFIX
         BNE   FINDMIN             NO, GET NEXT ONE
         B     CONTINUE            YES, PRINT IT
CHKCONF  CLC   0(4,R9),=F'0'       IS THERE MORE THAN ONE USER?
         BZ    FINDMIN             NO, COULD NOT BE A CONFLICT
NEXTCONF TM    12(R9),X'80'        SHARED REQUEST?
         BZ    CONTINUE            NO, THEN THERE MUST BE A CONFLICT
         ICM   R9,15,0(R9)         GET NEXT QEL
         BZ    FINDMIN             NO MORE QELS, NO CONFLICT
         B     NEXTCONF            CHECK NEXT QEL
CONTINUE OI    SWITCH,FOUND        SET FOUND INDICATOR
         MVI   DSNAME,C' '         CLEAR
         MVC   DSNAME+1(51),DSNAME  LINE
         SR    R15,R15             CLEAR FOR IC
         IC    R15,16(R3)          GET SIZE OF QCB DSNAME
         CH    R15,=H'52'          CHECK FOR MAX SIZE
         BNH   SIZEOK              WITHIN LIMITS, OK
         LA    R15,52              RESET TO MAX
SIZEOK   BCTR  R15,R0              DECREMENT FOR EXECUTE
         EX    R15,QCBMOVE         MOVE NAME TO BUFFER
         CLI   DSNAME,X'C0'        IS IT PRINTABLE ?
         BH    PRNTABLE            YES - OK
         CH    R15,=H'6'           WITHIN LIMITS FOR UNPACK ?
         BNH   HEXOK               YES - OK
         LA    R15,6               NO - SET TO MAX
HEXOK    LA    R15,1(R15)          ADD 1 TO SIZE
         LR    R14,R15             COPY SIZE
         LA    R14,0(R15,R14)      DOUBLE
         SLL   R15,28              ISOLATE THE LENGTH
         SLDL  R14,4               MERGE R14,R15
         MVC   DSNAME,BLANKS       CLEAR IT
         EX    R14,UNPK
         SRL   R14,4               ISOLATE OUPUT SIZE
         LA    R15,DSNAME+1(R14)   FIND LAST CHARACTER
         BCTR  R14,R0              DECR FOR LENGTH OF GOOD DATA
         EX    R14,TRANS           TRANSLATE TO PRINTABLE
         MVI   0(R15),C''''        EDNING QUOTE
         MVI   DSNAME,C''''        LEADING QUOTE TO INDICATE HEX
PRNTABLE L     R9,8(R3)            GET FIRST QEL
         B     FIRSTQEL            CHECK CHAIN
FINDQEL  L     R9,0(R9)            GET QEL
FIRSTQEL LTR   R9,R9               END OF CHAIN?
         BZ    FINDMIN             YES, GET NEXT NAME
         MVC   DISP+2(3),=CL3'SHR' SET DEFAULT DISP
         TM    12(R9),X'80'        SHARED REQUEST?
         BO    NOTOLD              SKIP OLD SETTING
         MVC   DISP+2(3),=CL3'OLD' SET OLD DISP
NOTOLD   LH    R6,14(R9)           GET USER ASID
         L     R1,16               CVT POINTER
         L     R1,556(R1)          ASVT ADDRESS
         L     R15,512(R1)         PICK UP MAXUSER VALUE
         LTR   R15,R15             ANY USERS?
         BZ    INVALID             NO, INFORM USER OF ERROR
         LA    R1,528(R1)          BUMP TO FIRST VALID ASVT ENTRY
NEXTUSER TM    0(R1),X'80'         FREE ASID?
         BO    NEXTASID            YES, IGNORE IT
         L     R7,0(R1)            PICK UP ASCB POINTER
         CH    R6,36(R7)           EQUAL ASID'S
         BE    SETUSER             YES, POINT TO USER NAME
NEXTASID A     R1,=F'4'            BUMP TO NEXT ASCB
         BCT   R15,NEXTUSER        POINT TO NEXT ASCB
INVALID  LA    R4,UNKNOWN          INDICATE UNKNOWN USER
         B     MOVEJN              MOVE IT IN
SETUSER  L     R4,172(R7)          GET INITIATED JOB NAME
         LTR   R4,R4               IS THERE A POINTER
         BNZ   MOVEJN              YES, CONTINUE
         L     R4,176(R7)          NO, GET START,LOGON,MOUNT
MOVEJN   MVC   JOBNAME(8),0(R4)    SAVE JOB NAME
         LA    R1,DISP             GET OUTPUT LINE ADDR
         BAL   R14,WRITE           WRITE OUT MSG
         B     FINDQEL             GET NEXT QEL
CHECK    TM    SWITCH,FOUND        ANY FOUND?
         BO    EXIT                YES, RETURN
         LA    R1,NOCONFF          SET DATA POINTER
         TM    SWITCH,CONF         WAS THIS A CONFLICT SEARCH?
         BO    MSG                 YES, WRITE OUT MESSAGE
         LA    R1,NOTFOUND         SET MSG POINTER
MSG      BAL   R14,WRITE           WRITE IT OUT
EXIT     SR    R15,R15             CLEAR RET CODE
         $EPILOG
WRITE    LH    R15,0(R1)           SET R15=L'MESSAGE
         LA    R1,2(R1)            SET R1=A(MESSAGE)
         BCTR  R15,R0              DECREMENT R15 FOR OC/MVC EX LENGTH
         EX    R15,*+8             EXECUTE OC TO UPPER CASE
         B     *+10                BRANCH AROUND EXECUTED OC
         OC    0(0,R1),BLANKS      INSURE UPPER CASE DATA
         TM    SWITCH,CMD          IS THIS TSO USER?
         BO    TPUT                YES, USE TPUT SVC TO TERMINAL
         EX    R15,*+8             EXECUTE MVC TO WTO DATA AREA
         B     *+10                BRANCH AROUND EXECUTED OC
         MVC   WTO+8(0),0(R1)      MOVE MSG TO WTO BUFFER
WTO      WTO   '                                                       C
                                                     '
         MVC   WTO+8(77),BLANKS    CLEAR WTO BUFFER
         BR    R14                 RETURN TO CALLER
TPUT     LA    R15,1(R15)          SET R15=L'MESSAGE
         TPUT  (1),(15)            WRITE TO TERMINAL
         BR    R14                 RETURN TO CALLER
MOVEPARM MVC   PARMNAME(1),2(R10)  MOVE DSNAME
QCBMOVE  MVC   DSNAME(1),20(R3)    MOVE DSNAME
COMPARE  CLC   20(1,R3),PARMNAME   COMPARE DSNAMES
OR       OC    PARMNAME(1),BLANKS   UPPER CASE OR
CMDMOVE  MVC   CMDWORK+2(1),0(R1)   MOVE TO WORK
TRANS    TR    DSNAME+1(0),TRTBL-240
UNPK     UNPK  DSNAME+1(0),20(0,R3)  UNPAK FOR TRANSLATE
SWITCH   DC    X'00'               STATUS SWITCHES
FOUND    EQU   X'80'               FOUND INDICATOR
CMD      EQU   X'40'               TSO COMMAND INDICATOR
ALL      EQU   X'20'               LIST ALL SPECIAL REQUEST
NODS     EQU   X'10'               SPECIAL NO 'SYSDSN' ALL REQUEST
CONF     EQU   X'08'               SEARCH FOR CONFLICTS REQEST
TRTBL    DC    C'0123456789ABCDEF'
NOCONFF  DC    H'18',CL18'NO CONFLICTS FOUND'
BADPARM  DC    H'20',CL20'INVALID DSNAME GIVEN'
NOTFOUND DC    H'09',CL09'NOT FOUND'
UNKNOWN  DC    H'08',CL08'*??????*'
PARMNAME DC    CL52' '             USER REQUEST
DISP     DC    H'77',CL7'    BY '  DISP AREA
JOBNAME  DC    CL9'12345678'       JOBNAME
MAJORNM  DC    CL8' ',C' '         MAJOR NAME
DSNAME   DC    CL52' '             DSNAME AREA
BLANKS   DC    CL77' '             BLANKS
ECB      DC    F'0'                DUMB ECB FOR PARSE
CMDANSR  DC    F'0'                PARSE ANSWER AREA POINTER
CMDUPT   DC    F'0'                USER PROFILE TABLE POINTER
CMDECT   DC    F'0'                ENVIRONMENT CONTROL TABLE
CMDECB   DC    A(ECB)              ECB POINTER
CMDPCL   DC    A(FINDQPCL)         ADDRESS OF IKJPARM
CMDANS   DC    A(CMDANSR)          PLACE TO PUT ANSWER
CMDCBUF  DC    F'0'                POINTER TO COMMAND BUFFER
CMDWORK  DC    X'0000',CL60' '     TSO COMMAND WORK AREA
FINDQPCL IKJPARM
CMDOPT   IKJPOSIT STRING
         IKJENDP
         IKJCPPL
         END
