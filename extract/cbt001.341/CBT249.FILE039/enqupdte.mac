         PRINT NOGEN
         TITLE 'ENQUPDTE - ENQ LIBRARY FOR IEBUPDTE
ENQUPDTE $PROLOG
         SPACE 3
*              ENQUPDTE - INTERFACE FOR IEBUPDTE
*                   TO ENQ THE UPDATED LIBRARY
*                   ONLY FOR THE DURATION OF THE UPDATE PHASE
*                   1-1-75
         SPACE 3
         LR    R3,R1          SAVE R1 - PARM POINTER
         RDJFCB (SYSUT2)
*                             GET JFCB FOR ENQ
         MVC   HLDDSN,JFCBHLD      STORE DATA SET NAME
         ENQ   (MAJDSN,HLDDSN,E,,SYSTEM)
*                             ENQ UPDATE DATASET
         LR    R1,R3          RESTORE R1 - PARM POINTER
         LINK  EP=IEBUPDTE
*                             PASS CONTROL TO IEBUPDTE
         LR    R3,R15         SAVE RETURN CODE
         DEQ   (MAJDSN,HLDDSN,,SYSTEM)
*                             DEQ UPDATED DATASET
         LR    R15,R3         RESTORE RETURN CODE
         $EPILOG
         EJECT
HLDDSN   DC    CL44' '             HOLD DSN AREA
MAJDSN   DC    C'ENQUPDTE'         MAJOR QNAME
         SPACE 3
SYSUT2   DCB   DDNAME=SYSUT2,EXLST=EXITA,MACRF=(PM),DSORG=PS
EXITA    DS    0F                  EXIT LIST
         DC    X'07'               GET JFCB
         DC    AL3(JFCBHLD)
         SPACE 3
JFCBHLD  DC    176C' '             HOLD JFCB AREA
         END
