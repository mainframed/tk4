***********************************************************************
*
*        THIS IS AN EXAMPLE OF THE REQUIRED CODE TO SUBMIT A JOB
*        TO THE JES2 INTERNAL READER.  THE SUBMISSION IS DONE USING
*        JES2 NATIVE MODE ACCESS (VSAM ACB) ALLOWING A USER TO
*        GET THE JES2 JOB NUMBER (THIS IS THE ONLY WAY TO GET IT).
*
***********************************************************************
SUBJES2  $PROLOG
         OPEN  (INDCB,(INPUT))  OPEN INPUT FILE
         TM    INDCB+48,X'10'   DID INPUT FILE OPEN?
         BZ    EXIT             NO, PASS RETURN CODE AND EXIT
         OPEN  (JES2)           OPEN JES2 ACB
         LTR   R15,R15          DID ACB OPEN?
         BZ    GETREC           YES, CONTINUE
         ABEND 1,DUMP           GET DUMP FOR DEBUGGING
GETREC   GET   INDCB,BUF        GET INPUT RECORD
         MVI   RPL,X'C0'        INIT RPL ID
         PUT   RPL=RPL          WRITE IT TO JES2
         LTR   R15,R15          WAS WRITE SUCCESSFUL?
         BZ    GETREC           YES, CONTINUE
ABEND2   ABEND 2,DUMP           NO, GET DUMP FOR DEBUGGING
EODAD    MVC   BUF,NULL         MOVE JES TRAILOR RECORD
         PUT   RPL=RPL          WRITE TRAILOR RECORD
         LTR   R15,R15          WAS WRITE OK?
         BNZ   ABEND2           NO, ABEND
         MVC   BUF,EOF          MOVE JES TRAILOR RECORD
         PUT   RPL=RPL          WRITE TRAILOR RECORD
         LTR   R15,R15          WAS WRITE OK?
         BNZ   ABEND2           NO, ABEND
         ENDREQ RPL=RPL         JOB SUBMITTED, GET JOB NUMBER
         LTR   R15,R15          WAS ENDREQ OK?
         BZ    WRITEID          YES, GET AND WRITE JES JOB-ID
         ABEND 3,DUMP           NO, GET DUMP FOR DEBUGGING
WRITEID  MVC   WTO+8(8),RPL+60  MOVE JOB-ID
WTO      WTO   'XXXXXXXX ------ SUBMITTED',ROUTCDE=11
         CLOSE (JES2)           CLOSE THE JES2 ACB
         LTR   R15,R15          DID ACB CLOSE OK?
         BZ    CLOSEOK          YES, CONTINUE
         ABEND 4,DUMP           NO, GET DUMP FOR DEBUGGING
CLOSEOK  CLOSE (INDCB)          CLOSE INPUT DCB
         XC    RETCODE,RETCODE  CLEAR RETURN CODE
EXIT     L     R15,RETCODE      LOAD RETURN CODE
         $EPILOG
RETCODE  DC    F'16'            RETURN CODE PRIMED (INDD MISSING)
BUF      DC    CL80' '          INPUT/OUTPUT BUFFER
EOF      DC    CL80'/*EOF '     JES2 END-OF-FILE RECORD
NULL     DC    CL80'// '        NULL JCL RECORD
INDCB    DCB   DDNAME=INTSUB,DSORG=PS,MACRF=GM,EODAD=EODAD
JES2     ACB   DDNAME=JES2,MACRF=(ADR,SEQ,OUT)
RPL      RPL   ACB=JES2,RECLEN=80,OPTCD=(ADR,SEQ,SYN,NUP),             X
               AREA=BUF,AREALEN=80
         END
