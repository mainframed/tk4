         PRINT NOGEN
***********************************************************************
*
*        THIS TSO COMMAND PROCESSOR WILL ISSUE A MESSAGE INDICATING
*        THE STATE (STATUS) A NAMED DATASET AND PASS A RETURN
*        CODE INDICATING SAME (SIMILAR TO THE LISTDS COMMAND).
*        THIS COMMAND HOWEVER, WILL PASS A REURN CODE ALLOWING
*        A CLIST TO QUERY A DATASET'S STATE (STATUS) PRIOR TO
*        ISSUING DELETE OR ALLOCATE COMMANDS.
*
*        AN OPTIONAL PARAMETER (RETCODE) ALLOWS THE USE OF THE
*        RETURN CODE FEATURE WITHOUT ANY MESSAGES BEING ISSUED.
*
*        'STATE' AS USED HERE IMPLIES 'STATUS' OF A DATASET.
*        THE STATUS (EXISTS OR DOES NOT EXIST) MAY BE PASSED TO A
*        CLIST VIA RETURN CODES AS FOLLOWS:
*
*        RETURN CODE  0 = THE DATASET IS CATALOGED AND EXISTS
*                     4 = THE DATASET IS CATALOGED, BUT DOESN'T EXIST
*                     8 = THE DATASET IS NOT CATALOGED
*
*        EXAMPLES:
*           STATE JCL.CNTL RETCODE     PASS THE RETURN CODE ONLY
*
*           STATE JCL.CNTL             PASS RETURN CODE AND PROVIDE
*                                      A MESSAGE
*
*           STATE 'USER.JCL.CNTL'      SAME AS ABOVE
*
***********************************************************************
         EJECT
STATE    CSECT
         REGEQU
         USING *,R12          PROVIDE ADDRESSABILITY
         SAVE  (14,12)        SAVE TMP REGS
         LR    R12,R15        COPY ENTRY POINT ADDRESS
         LR    R2,R13         COPY PREVIOUS SAVE AREA POINTER
         LR    R11,R1         SAVE CPPL ADDRESS
         USING CPPL,R11       CPPL ADDRESSABILITY
         LA    R0,@WORKEND-@WORK SIZE FOR GETMAIN
         GETMAIN R,LV=(0)     GET A WORKAREA
         LR    R13,R1         COPY GOTTEN ADDRESS
         USING @WORK,R13      INFORM ASSEMBLER
         XC    @WORK(@SIZE),@WORK  CLEAR GOTTEN STORAGE
         ST    R2,@SAVE+4     SAVE CALLERS SAVE POINTER
         ST    R13,8(R2)      CHAIN PREVIOUS SAVE AREA
         EJECT
         MVC   @OBTAIN(MOVELEN),OBTAIN MOVE MODELS
         LA    R15,@DSN       ADDRESS OF DSNAME
         ST    R15,@OBTAIN+4  PROVIDE ADDRESS
         ST    R15,@LOCATE+4  PROVIDE ADDRESS
         LA    R15,@VOLSER    ADDRESS OF VOLSER
         ST    R15,@OBTAIN+8  PROVIDE ADDRESS
         LA    R15,@WORKA     ADDRESS OF WORKAREA
         ST    R15,@OBTAIN+12 PROVIDE ADDRESS
         ST    R15,@LOCATE+12 PROVIDE ADDRESS
         MVC   @PPLUPT,CPPLUPT  MOVE USER PROFILE TABLE
         MVC   @PPLECT,CPPLECT  MOVE ENVIRONMENT CONTROL TAB
         LA    R1,@ECB        ADDRESS OF ECB
         ST    R1,@PPLECB     PROVIDE DUMB ECB
         L     R1,IKJADCON    ADDRESS OF IKJPARM CSECT
         ST    R1,@PPLPCL     AS PARSE CONTROL LIST
         LA    R1,@ANSWER     PARSE ANSWER AREA
         ST    R1,@PPLANS     ADDRESS FOR PARSE RETURN
         MVC   @PPLCBUF,CPPLCBUF  MOVE COMMAND BUF POINTER
         DROP  R11            DROP ADDRESSABILITY
         LA    R1,@PPLUPT     ADDRESS OF PARSE PARM LIST
         LINK  EP=IKJPARS     CALL TSO COMMAND PARSE
         L     R11,@ANSWER    GET POINTER TO PARSE ANSWER
         USING IKJPARMD,R11   GAIN ADDRESSABILITY TO ANSWER
         L     R1,DSNAME      GET POINTER TO DSNAME
         LH    R4,DSNAME+4    GET SIZE OF DSNAME
         BCTR  R4,R0          DECREMENT FOR EXECUTE
         EX    R4,MOVENAME    MOVE TO WORK AREA
         OC    OPT(2),OPT     WAS 'RETCODE' REQUESTED?
         BZ    NORET          NO, SKIP MESSAGE
         OI    @FLAG,@RET     SIGNAL RETCODE ONLY
NORET    MVI   @RETCODE+3,8   SET RETURN CODE DEFAULT
         LOCATE @LOCATE       TRY TO LOCATE THE NAMED DATASET
         LTR   R15,R15        FOUND?
         BNZ   RLSA           NO, EXIT WITH RETCODE 8
         MVC   @VOLSER,@WORKA+6 MOVE THE VOLSER FOR OBTAIN
         OBTAIN @OBTAIN       SEE IF THE DATASET IS ON THE VOLUME
         MVI   @RETCODE+3,4   SET RETURN CODE DEFAULT
         LTR   R15,R15        WAS DATASET FOUND?
         BNZ   RLSA           NO, EXIT WITH RETCODE 4
         MVI   @RETCODE+3,0   SET ZERO RETURN CODE
*
RLSA     IKJRLSA @ANSWER      FREE PARSE STORAGE
         DROP  R11            DROP ADDRESSABILITY
         TM    @FLAG,@RET     RETURN CODE ONLY?
         BO    EXIT           YES, RETURN
         MVC   @MSG(32),MSG0  ASSUME NORMAL RETURN
         MVC   @MSG+23(6),@VOLSER MODIFY MESSAGE
         CLI   @RETCODE+3,0   ZERO RETURN CODE?
         BE    TPUT           YES, CONTINUE
         MVC   @MSG(22),MSG4  ASSUME RETCODE 4
         CLI   @RETCODE+3,4   ZERO RETURN CODE?
         BE    TPUT           YES, CONTINUE
         MVC   @MSG(32),MSG8  MUST BE RETURN CODE 8
TPUT     LA    R0,76          SET SIZE FOR TPUT
         LA    R1,@MSG        SET DATA FOR TPUT
         SVC   93             ISSUE MESSAGE
EXIT     L     R10,@RETCODE   GET RETURN CODE
         LR    R1,R13         GET ADDRESS TO FREE
         LA    R0,@WORKEND-@WORK SIZE FOR FREEMAIN
         L     R13,@SAVE+4    GET PREVIOUS SAVE AREA
         FREEMAIN  R,LV=(0),A=(1)  FREE GOTTEN STORAGE
         LR    R15,R10        SET UP RETCODE REG
         RETURN  (14,12),RC=(15) RETURN TO CALLER
         EJECT
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
MOVENAME MVC   @DSN(0),0(R1) MOVE NAME TO WORK AREA
* ------ RELOCATED CONSTANTS ----------------------------------------
         DS    0D             DOUBLEWORD ALIGNMENT
OBTAIN   CAMLST SEARCH,*,*,*  OBTAIN CAM LIST
LOCATE   CAMLST NAME,*,,*     LOCATE CAM LIST
DSN      DC    CL44' '        DSNAME FOR LOCATE/OBTAIN
VOLSER   DC    CL6' '         VOLUME SERIAL FOR OBTAIN
MOVELEN  EQU   *-OBTAIN       SIZE FOR RELOCATE MOVE
* ------ END OF RELOCATE SECTION ------------------------------------
MSG0     DC    CL32'CATALOGED AND FOUND ON VVVVVV   ' 32+44=76
MSG4     DC    CL32'CATALOGED - BUT NOT ON'
MSG8     DC    CL32'NOT CATALOGED ...............   '
IKJADCON DC    V(STATEPCL)    ADDRESS OF PARSE PDL/PCL
STATEPCL IKJPARM
DSNAME   IKJPOSIT DSNAME,USID,PROMPT='DATASET TO LOCATE'
OPT      IKJKEYWD
         IKJNAME 'RETCODE',ALIAS=('CODE','CONDCODE')
         IKJENDP
         LTORG
         EJECT
***********************************************************************
*
*        WORK AREA DSECT AND TSO DSECTS
*
***********************************************************************
@WORK    DSECT
@SAVE    DS    18F            REG SAVE AREA
@MSG     EQU   *              MESSAGE AREA
@OBTAIN  CAMLST SEARCH,*,*,*
@LOCATE  CAMLST NAME,*,,*
@DSN     DS    CL44           DSNAME FOR LOCATE/OBTAIN
@VOLSER  DS    CL6            VOLUME SERIAL FOR OBTAIN
@RETCODE DS    F              RETURN CODE AREA
@ECB     DS    F              PARSE/ATTACH ECB
@ANSWER  DS    F              PARSE ANSWER AREA
@PPLUPT  DS    F              PARSE PARM LIST UPT
@PPLECT  DS    F              PARSE PARM LIST ECT
@PPLECB  DS    F              PARSE PARM LIST ECB
@PPLPCL  DS    F              PARSE PARM LIST PCL
@PPLANS  DS    F              PARSE PARM LIST ANSWER ->
@PPLCBUF DS    F              PARSE PARM LIST COMMAND ->
@FLAG    DS    X              OPTION FLAG
@RET     EQU   X'01'          NO MESSAGE REQUESTED (ONLY RETCODE)
@SIZE    EQU   *-@WORK        SIZE OF WORKAREA
         DS    0D             DOUBLE WORD ALIGN
@WORKA   DS    265C           LOCATE/OBTAIN WORKAREA
@WORKEND EQU   *              END OF WORKAREA
         IKJCPPL
         END
