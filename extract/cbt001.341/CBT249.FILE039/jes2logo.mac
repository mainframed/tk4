***********************************************************************
*
*        THIS PROGRAM WILL GENERATE JES2 'NEWS FACILITY' CONTROL
*        CARDS FOR A USER 'LOGO'.
*
*        INPUT  CONSISTS OF A FILE (LRECL 132) CONTAINING RECORDS
*               AS THEY ARE TO APPEAR ON THE JES2 SYSOUT SEPARATOR.
*               (THE 1ST CHARACTER OF EACH LINE MAY BE AN ASA
*               CHARACTER IF PAGINATION IS DESIRED OR MAY BE DATA).
*
*        OUTPUT WILL CONTAIN RECORDS (LRECL 80) FOR INCLUSION IN
*               THE JES2 STARTUP PARAMETERS.  EACH RECORD WILL
*               CONTAIN THE 'NEWS' PREFIX AND CONTINUATION (IF REQ'D)
*               IS SUPPORTED.
*
*        REQUIRED DD NAMES:
*
*               SYSUT1 - INPUT FILE WITH LRECL=132
*               SYSUT2 - OUTPUT FILE WITH LRECL=80
*
***********************************************************************
JES2LOGO $PROLOG
NEXT     BAL   R14,GET          GET A RECORD
         LR    R2,R1            COPY ADDRESS OF INPUT RECORD
         MVC   BUF,0(R2)        MOVE 1ST HALF OF RECORD
         CLC   66(66,R2),=CL66' ' ANYTHING LEFT IN RECORD?
         BE    WRITE            NO, ONLY ONE RECORD TO WRITE
         MVI   CONT,C'-'        YES, SIGNAL CONTINUATION
         LA    R1,REC           ADDR OF OUTPUT RECORD
         BAL   R14,PUT          WRITE RECORD
         MVI   CONT,C' '        RESET CONTINUATION
         MVC   BUF(66),66(R2)   MOVE 2ND HALF OF RECORD
WRITE    LA    R1,REC           ADDR OF OUTPUT RECORD
         BAL   R14,PUT          WRITE RECORD
         B     NEXT             GET NEXT RECORD
EODAD    CLOSE (PUTDCB)         CLOSE OUTPUT FILE
         SR    R15,R15          CLEAR RETCODE
         $EPILOG
*
REC      DC    C'NEWS '         JES2 CONTROL WORD           05
BUF      DC    CL66' '          USER DATA FIELD (ASA+65)    66
CONT     DC    C' '             CONTINUATION FIELD          01
         DC    CL8' '           SEQUENCE NUMBER FIELD       08
*
*                                                   TOTAL = 80
GET      SYSGET DDNAME=SYSUT1,LRECL=132
PUT      SYSPUT DDNAME=SYSUT2,LRECL=80
         END
