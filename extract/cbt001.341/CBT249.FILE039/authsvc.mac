AUTHSVC  CSECT
***********************************************************************
*
*     THIS USER SVC (255 DECIMAL) WILL PROVIDE MODESET
*     AUTHORIZATION UPON INVOCATION.  IT IS THEN THE USER'S
*     RESPONSIBILITY TO ISSUE A MODESET MACRO IN ORDER TO
*     GAIN SUPERVISOR STATE OR KEY ZERO.  REGISTER ONE MUST
*     POINT TO THE CHARACTERS 'AUTH' FOR MODESET CAPABILITY,
*     AND CAN POINT TO ANYTHING ELSE TO RELINQUISH THE
*     MODESET CAPABILITY.
*
*     FOR TIME SHARING USERS ONLY, THE ISSUER OF THE SVC (CALLER)
*     MUST RESIDE IN THE LINK PACK AREA (OR MLPA).  THIS IS TO
*     INSURE THAT TSO USERS DO NOT ATTEMPT TO VIOLATE OUR SECURITY
*     BY INVOKING EITHER DIRECTLY OR UNDER TEST.
*
***********************************************************************
         USING *,R6                ENTRY BASE IS REG 6
         CLC   0(4,R1),=CL4'AUTH'  IS THIS AN 'AUTH' REQUEST?
         BNE   RESET               NO, RESET USER AUTH
 B SET   SECURITY NOT REQUIRED, ALLOW ALL AUTH REQUESTS
         SLR   R15,R15             CLEAR WORK REGISTER
         LR    R0,R15              CLEAR WORK REGISTER
         ICM   R15,7,61(R7)        GET ADDRESS OF TSB IF ANY
         LA    R15,0(,R15)
         BZ    SET                 NO TSB, CONTINUE (BATCH JOB)
         L     R8,12(R4)           ADDRESS OF TIOT
         CLI   0(R8),C'S'          IS THIS A 'SYSTEMS' USER?
         BE    SET                 YES, MUST BE TESTING SOMETHING NEW
         CLI   0(R8),C'O'          IS THIS A 'OPERATIONS' USER?
         BE    SET                 YES, MUST BE VALID
         ICM   R15,7,29(R5)        GET PREVIOUS RB POINTER
         ICM   R0,7,21(R15)        GET ADDRESS OF ISSUANCE OF SVC
         CL    R0,416(R3)          CHECK ADDRESS OF SVC ISSUANCE
         BH    SET                 VALID REQUEST, CONTINUE
         LA    R0,256              SET SIZE FOR GETMAIN
         BAL   R1,*+4              SET GETMAIN OPTION
         SVC   10                  GET STORAGE FOR WRITE TO OPERATOR
         LR    R2,R1               COPY GOTTEN STORAGE ADDRESS
         MVC   0(WTOEND-WTO,R2),WTO MOVE COMMON STORAGE
         USING WTO,R2              PROVIDE ADDRESSABILITY
         MVC   JOBN(8),0(R8)       MOVE USER-ID TO MSG
         LA    R1,WTO              SET WRITE TO OPERATOR PARM
         SVC   35                  INFORM OPERATOR OF VIOLATOR
         LA    R0,256              SET LENGTH FOR FREEMAIN
         LR    R1,R2               SET ADDRESS FOR FREEMAIN
         SVC   10                  FREE GOTTEN STORAGE
         DROP  R2
         B     RESET               FORCE USER UNAUTHORIZATION
SET      L     R8,180(R4)          GET POINTER TO JSCB
         OI    236(R8),X'01'       PROVIDE AUTHORIZATION
         BR    R14                 RETURN
RESET    L     R8,180(R4)          GET POINTER TO JSCB
         NI    236(R8),X'FE'       RESET AUTH BIT
         BR    R14                 RETURN TO SYSTEM
         DS    0D                  DOUBLEWORD ALIGNMENT
WTO      DC    H'84',X'8000'       WRITE TO OPERATOR PARM
         DC    C'TSO SECURITY VIOLATION - USER='
JOBN     DC    CL8' '              USER-ID OF VIOLATOR
         DC    CL100' ------- NOTIFY SYSTEMS PROGRAMMING - ASAP'
         DC    X'C0800000'         ROUT CODES
         DC    X'40000000'         DESC CODES
WTOEND   EQU   *
         REGEQU
         END
