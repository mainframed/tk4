***********************************************************************
*
*        THIS IS THE TSO SUBMIT EXIT ROUTINE (IKJEFF10).
*        IT MUST BE REENTRANT, REFRESHABLE, AND REUSEABLE.
*        IT'S FUNCTION IS TO CHECK ONLY THE JOB CARDS USED
*        FOR SUBMITTED JOBS FOR THE MSGCLASS PARAMETER, AND
*        IF NOT FOUND, TO ADD A DEFAULT ONE SPECIFYING
*        CLASS 'A'.  THIS IS REQUIRED SINCE THE DEFAULT CLASS
*        OF 'Z' IS A DUMMY CLASS IN THIS INSTALLATION.
*        IF THE USER PROVIDES A MSGCLASS PARM, IT IS ASSUMED
*        THAT HE KNOWS WHAT HE IS DOING AND NO CHANGES ARE MADE.
*        IN ORDER TO ADD A MSGCLASS CARD, COLUMN 71 OF THE JOB
*        CARD MUST BE BLANK SINCE A COMMA MUST BE ADDED TO
*        CONTINUE THE CARD.  IF COLUMN 71 IS NON-BLANK, A MESSAGE
*        IS WRITTEN TO THE USER AND THE SUBMIT REQUEST IS
*        CANCELLED.
*
***********************************************************************
IKJEFF10 CSECT
         REGEQU
         USING *,R12               GAIN ADDRESSABILITY
         SAVE  (14,12)             SAVE SUBMITS REGS
         LR    R12,R15             SET BASE REG
         LR    R10,R13             COPY SAVE AREA POINTER
        SPACE 3
* USE REG 2 FOR BASE OF SUBMIT DSECT
         L     R2,0(R1)            POINTER TO EXIT LIST ADDRESS
         USING IEEXITL,R2          EXIT LIST BASE
         L     R3,IESUBCTP         GET POINTER TO JCL SWITCH BYTES
         USING IESUBCTD,R3         INFORM ASSEMBLER
         LA    R0,IKJDSIZE         SIZE OF DSECT WORK AREA
         GETMAIN R,LV=(0)          GET DSECT WORKAREA
         LR    R13,R1              COPY TO WORKING REG
         USING IKJDWORK,R13        INFORM ASSEMBLER
         XC    IKJDWORK(IKJDSIZE),IKJDWORK  CLEAR WORK AREA
         ST    R13,8(R10)          FORWARD CHAIN OF SAVE AREA
         ST    R10,4(R13)          BACKWARD CHAIN OF AREAS
         SPACE 3
* TEST TO SEE IF WE PROVIDED A MSG AREA FOR SUBMIT
         L     R1,IEMSGP           GET MSG POINTER IF ANY
         LTR   R1,R1               DID WE ISSUE A MESSAGE?
         BNZ   IKJFREE             YES, FREE MSG CORE
         SPACE 3
* NO MSG ISSUED, SEE IF A CONTINUATION CARD SHOULD BE ADDED
         L     R4,IECARDP          GET POINTER TO CARD IMAGE
         LTR   R4,R4               MUST I ADD CONTINUATION?
         BZ    ADDCONT             YES, SO GO DO IT
         SPACE 3
* NONE OF THE ABOVE, MUST BE A NEW OR CONTINUED JOB CARD
         LA    R15,70              SET LIMIT FOR SCAN OF JOB CARD
         LA    R5,70(R4)           POINTER TO END OF CARD
         SPACE 3
* LOCATE LAST CHARACTER OF LAST PARAMETER
LOOPA    CLI   0(R5),C' '          BLANK CHARACTER?
         BNE   CHKCOMMA            NO, CHECK FOR CONTINUED CARD
         BCTR  R5,R0               BUMP TO PREVIOUS CHAR
         BCT   R15,LOOPA           CHECK THIS CHARACTER
         B     IKJERROR            BAD CARD, SHOULD NOT OCCUR
         SPACE 3
* SAVE ADDRESS OF FIRST BLANK FOLLOWING LAST CHAR FOR COMMA INSERT
CHKCOMMA LA    R10,1(R5)           SAVE PLACE TO PUT COMMA IN CASE
         CLI   0(R5),C','          IS THIS A CONTINUED CARD?
         BNE   SCANMC              NO, FIND MESSAGE CLASS
         SPACE 3
* INDICATE THAT THIS IS A CONTINUED CARD
         OI    IKJDBITS,IKJDCONT   INDICATE CARD CONTINUED
SCANMC   S     R5,MCSIZE           DECREMENT BY SIZE OF
         S     R15,MCSIZE          MSGCLASS PARAMETER
         BNP   NOFIND              CAN NOT BE THERE
         SPACE 3
* SCAN REMAINDER OF CARD FOR PRESENCE OF A MSGCLASS PARM
LOOPB    CLC   0(9,R5),MCLASSEQ    MESSAGE CLASS PARAMETER?
         BE    IKJDONE             YES, NORMAL RETURN (USER SPECIFIED)
         BCTR  R5,R0               DECREMENT TO PREVIOUS CHAR
         BCT   R15,LOOPB           BACK TO CHECK NEXT
         SPACE 3
* NOT FOUND ON THIS CARD, TEST IF A CONTINUATION IS EXPECTED
NOFIND   TM    IKJDBITS,IKJDCONT   WAS THIS CARD CONTINUED?
         BO    IKJNORM             YES, LET IT GO AND CHECK NEXT ONE
         SPACE 3
* NOT FOUND, AND NOT CONTINUED. ADD IT IF WE CAN
         CLI   70(R4),C' '         IS COL 71 A BLANK
         BNE   IKJERROR            NO, CANNOT CONTINUE CARD
         SPACE 3
* CARD CAN BE CONTINUED, INFORM SUBMIT TO RETURN FOR IT
         MVI   0(R10),C','         ADD COMMA INDICATING A CONTINUATION
         LA    R10,4               SET RETURN CODE FOR INSERT FUNCTION
         B     IKJEXIT             RETURN TO SUBMIT
         SPACE 3
* PREVIOUS CARD WAS CONTINUED BY US, ADD THE CONTINUATION CARD NOW
ADDCONT  LA    R4,ADDCARD          ADDRESS OF CARD ADDITION
         ST    R4,IECARDP          GIVE IT TO SUBMIT
         SPACE 3
* NORMAL RETURN FROM HERE, RETCODE = 0
IKJDONE  NI    IETAKEEX,X'FF'-IETJOB   PREVENT RETURN
IKJNORM  SR    R10,R10             CLEAR RET CODE FOR NORMAL PROCESSING
IKJEXIT  LR    R1,R13              COPY GOTTEN CORE ADDRESS
         LA    R0,IKJDSIZE         SIZE OF WORKAREA FOR FREEMAIN
         L     R13,4(R13)          GET PREVIOUS SAVE POINTER
         FREEMAIN  R,LV=(0),A=(1)  FREE WORK AREA
         LR    R15,R10             COPY RETCODE TO RETREG
         RETURN  (14,12),RC=(15)   RETURN TO SUBMIT
         SPACE 3
* AN ERROR HAS BEEN FOUND, PROVIDE AN AREA AND MSG FOR SUBMIT
IKJERROR LA    R0,IKJMSIZE         SIZE OF MESSAGE
         GETMAIN R,LV=(0)          GET STORAGE FOR MSG AREA
         ST    R1,IEMSGP           PASS TO SUBMIT
         MVC   0(IKJMSIZE,R1),IKJMSG  MOVE MSG TO GOTTEN CORE
         LA    R10,8               INDICATE MESSAGE FOR USER
         B     IKJEXIT             RETURN TO SUBMIT
         SPACE 3
* MSG AREA PROVIDED PREVIOUSLY, FREE THE AREA AND CANCEL SUBMIT REQ.
IKJFREE  LA    R0,IKJMSIZE         SIZE OF AREA TO FREE
         FREEMAIN  R,LV=(0),A=(1)  FREE MSG AREA PREVIOUSLY GOTTEN
         LA    R10,16              TERMINATE THIS SUBMISSION
         B     IKJEXIT             RETURN TO SUBMIT
         EJECT
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
MCSIZE   DC    F'9'                SIZE OF MSGCLASS= PARM
MCLASSEQ DC    C'MSGCLASS='        COMPARE DATA
ADDCARD  DC    CL80'// MSGCLASS=A     SUBMIT ADDED CONTINUATION CARD'
IKJMSG   DC    AL2(IKJMSIZE)       SIZE OF MESSAGE
         DC    C'UNABLE TO ADD MSGCLASS PARAMETER (COL 71 NON-BLANK)'
IKJMSIZE EQU   *-IKJMSG            SIZE OF ERROR MESSAGE
         EJECT
***********************************************************************
*
*        DSECTS
*
***********************************************************************
IKJDWORK DSECT
IKJDSAVE DS    18F                 REG SAVE AREA
IKJDBITS DS    B                   STATUS FLAG BYTE
IKJDCONT EQU   X'01'               INDICATES THIS CARD IS CONTINUED
IKJDSIZE EQU   *-IKJDSAVE          LENGTH OF DSECT WORKAREA
         IKJEFFIE  IETYPE=SUBMIT   MAPPING DSECT FOR SUBMIT PARMS
         END
