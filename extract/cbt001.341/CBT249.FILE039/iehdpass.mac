         TITLE 'IEHDPASS USER SECURITY CHECK MOD'
*
*          REPLACE CSECT 'IEHDPASS' IN MODULE 'IEHDASDR' WITH
*          THIS MOD TO ELIMINATE VSAM HAZARDS.
*
*          INSTEAD OF ZAPPING IEHDPASS IN THE SYSTEM TO A NO-OP
*          IT IS FELT THAT AT LEAST SOME SECURITY SHOULD EXIST
*          AND THAT IS WHAT THIS ROUTINE ATTEMPTS TO DO.
*
*          FOR EACH OPERATION THAT AFFECTS A DISK TYPE DEVICE
*          (OUTPUT) VERIFY WITH THE OPERATOR SO AS TO AVOID USER
*          ERRORS WITH 'NO' CHECKING.
*
IEHDPASS CSECT
         SAVE  (14,12)
         LR    11,15
         USING IEHDPASS,11
         ST    13,SAVEAREA+4
         LA    7,SAVEAREA
         ST    7,8(13)
         LA    13,SAVEAREA
         L     5,16           LOAD CVT PTR ADDR
         L     5,0(5)         NEXT/CURRENT TCB PTR
         L     5,4(5)         CURRENT TCB PTR
         L     5,12(5)        TIOT PTR
         MVC   WTO+39(8),0(5) MOVE JOB NAME
         MVC   WTO+48(8),8(5) MOVE STEP NAME
         LA    5,24(5)        1ST DD ENTRY
         SR    6,6            CLEAR FOR IC
NEXTENT  IC    6,0(5)         GET SIZE OF ENTRY
         LTR   6,6            END OF TIOT?
         BZ    CONTINUE       YES, MUST BE O.K.
         CLC   4(8,5),8(10)   TODD NAME?
         BE    FOUND          YES, CHECK UCB
         AR    5,6            BUMP TO NEXT ENTRY
         B     NEXTENT        CHECK NEXT ENTRY
FOUND    ICM   6,3,18(5)      GET UCB POINTER
         CLI   18(6),X'20'    DIRECT ACCESS OUTPUT?
         BNE   CONTINUE       NO, MUST BE O.K.
         MVC   WTO+65(6),28(6) MOVE VOLID TO MSG
REWRITE  XC    WAITECB,WAITECB
         MVI   MSGFLD,C' '
         LA    10,MSGFLD
WTO      WTOR  'SYS0001A IEHDASDR SECURITY CHK JJJJJJJJ.SSSSSSSS FOR VOX
               L-XXXXXX (U/N)',(10),1,WAITECB,ROUTCDE=(9)
         WAIT  ECB=WAITECB
         CLI   MSGFLD,C'U'
         BE    CONTINUE
         CLI   MSGFLD,C'N'
         BE    ERROR
         B     REWRITE
CONTINUE L     13,4(13)
         RETURN (14,12),,RC=0
ERROR    L     13,4(13)
         RETURN (14,12),,RC=12
WAITECB  DC    F'0'
MSGFLD   DS    C' '
SAVEAREA DC    18F'0'
         END
