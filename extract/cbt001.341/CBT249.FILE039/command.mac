         TITLE 'SYSTEM COMMAND SCHEDULING ROUTINE '
***********************************************************************
*
*        THIS ROUTINE WILL ALLOW A BATCH JOB TO ISSUE OS OPERATOR
*        COMMANDS.  THIS FUNCTION IS USEFUL TO OPERATIONS IN
*        EXECUTING CERTAIN FUNCTIONS AS SETTING CONSOLE ROUTE CODES
*        JES2/HASP INIT CLASSES ETC.
*
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
COMMAND  CSECT
         USING *,R12               INFORM ASSEMBLER OF BASE
         SAVE  (14,12)             SAVE CALLERS REGS
         LR    R12,R15             SET BASE REG
         LA    R15,SAVE            GET SAVE AREA ADDRESS
         ST    R15,8(R13)          FORWARD POINTER
         ST    R13,4(R15)          BACKWARD POINTER
         LR    R13,R15             SET NEW SAVE POINTER
         L     R1,0(R1)            GET PARM POINTER
         LH    R2,0(R1)            GET PARM SIZE
         LA    R15,8               SET INVALID RETURN CODE
         LTR   R2,R2               ANY PARM?
         BZ    EXIT                NO, RETURN
         BCTR  R2,R0               DECREMENT FOR EXECUTE
         EX    R2,MOVE             MOVE COMMAND TO CIB
         MODESET KEY=ZERO,MODE=SUP  GET KEY ZERO AND AUTH
         LM    R0,R1,MGCRREGS      GET SVC 34 REGS
         SVC   34                  SCHEDULE COMMAND
         MODESET KEY=NZERO,MODE=PROB  RELEASE AUTHORIZATION
EXIT     L     R13,SAVE+4          GET PREV SAVE ADDRESS
         RETURN  (14,12),RC=(15)   RETURN TO CALLER
  SPACE
SAVE     DC    18F'0'              SAVE AREA
MOVE     MVC   CIB+4(1),2(R1)      MOVE COMMAND TO CIB
MGCRREGS DC    F'0'                REG 0 FOR SVC 34
         DC    A(CIB)              REG 1 FOR SVC 34
CIB      DC    AL2(98)             MAXIMUM LENGHT OF COMMAND
         DC    H'0'                SVC 34 PADDING
         DC    CL100' '            COMMAND BUFFER
         LTORG
         END
