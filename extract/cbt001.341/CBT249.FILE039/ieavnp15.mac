*
*        NOTE: ---------- MVS ONLY ------------
*
*        REPLACES A LIKE NAMED SYSTEM SUPPLIED MODULE IN
*        'SYS1.NUCLEUS' (ONE OF THE IPL RIM'S).
*
*        THIS MODULE MAY ELIMINATE THE NEED FOR PRESRES ENTRIES
*        IN PARMLIB IF CERTAIN VOLUME ATTRIBUTES CAN BE DETERMINED
*        VIA VOLUME ID.  THIS MODULE DEFAULTS TO ANY PACK MOUNTED
*        AS BEING PRIVATE, WITH THE EXCEPTION OF A PACK WITH SIX
*        DUPLICATE NUMERIC CHARACTERS BEING STORAGE.
*
*        CHANGES CAN BE MADE SO THAT IT WILL SUPPORT YOUR STANDARDS.
*
         PRINT NOGEN
IEAVNP15 CSECT
         USING *,R12               INFORM ASSEMBLER OF BASE
         SAVE  (14,12)             SAVE ENTRY REGS
         LR    R12,R15             GET ENTRY BASE
* NO SAVE AREA REQUIRED IN THIS VERSION, SINCE NO SYSTEM
* FUCTIONS WILL BE CALLED.
         L     R2,16               GET CVT POINTER
         L     R2,40(R2)           GET UCB LU TABLE POINTER
         B     GETFIRST            CHECK FIRST ENTRY
GETNEXT  LA    R2,2(R2)            UPDATE TO NEXT UCB
GETFIRST LH    R1,0(R2)            GET UCB POINTER
         LTR   R1,R1               ANY UCB ADDR?
         BZ    GETNEXT             NON-VALID UCB, GET NEXT ONE
         BM    ALLDONE             END OF LIST, EXIT
         TM    18(R1),X'20'        DIRECT ACCESS?
         BNO   GETNEXT             NO, GET NEXT ONE
         TM    3(R1),X'80'         IS IT ONLINE?
         BZ    GETNEXT             NO, GET NEXT UCB
         TM    3(R1),X'40'         CHANGE FLAG ON?
         BO    GETNEXT             YES, IT'S GOING OFFLINE
         CLC   28(6,R1),HEXZEROS   BINARY ZEROS?
         BE    GETNEXT             YES, SKIP IT
         TM    3(R1),X'04'         IS IT PERM/RES?
         BO    RESIDENT            YES, DON'T FORCE RESERVED
         OI    3(R1),X'20'         FORCE RESERVED FOR ALL DISKS
RESIDENT TM    28(R1),C'0'         NUMERIC FIRST CHAR?
         BNO   PRIVATE             NO, MUST BE PRIVATE
         LA    R3,5                SET LOOP COUNT
         LA    R4,28(R1)           POINT TO FIRST CHARACTER
CHECKNUM CLC   0(1,R4),1(R4)       NEXT CHAR SAME AS THIS ONE?
         BNE   PRIVATE             NO, MUST BE PRIVATE
         LA    R4,1(R4)            POINT TO NEXT CHARACTER
         BCT   R3,CHECKNUM         CHECK NEXT ONE
         OI    34(R1),X'04'        FORCE STORAGE
         B     GETNEXT             GET NEXT UCB
PRIVATE  OI    34(R1),X'10'        FORCE PRIVATE
         B     GETNEXT             GET NEXT UCB
ALLDONE  SR    R15,R15             CLEAR RET CODE
         RETURN (14,12),RC=15      RETURN TO IPL INITIALIZATION
HEXZEROS DC    XL6'000000000000'   COMPARAND
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
R10      EQU       10
R11      EQU       11
R12      EQU       12
R13      EQU       13
R14      EQU       14
R15      EQU       15
         END
