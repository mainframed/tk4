*----------------------------------------------------------------------
*
*                   JAN ,1979
*
*        THIS ROUTINE WILL PROVIDE A REPORT DETAILING THE EXTENTS
*        AND PERCENT USED FOR VSAM DATASET 'CLUSTERS' BY DATASET
*        NAME.
*
*        REQUIRED JCL:
*
*  //VSAMLCLS JOB (ACCT),PGMRNAME,CLASS=A,MSGCLASS=A
*  //VSAMLCLS EXEC PGM=VSAMLCLS
*  //IDCOUT   DD   DUMMY
*  //IDCIN    DD   SPACE=(TRK,(1,1)),UNIT=SYSDA,
*  //             DCB=(LRECL=80,RECFM=FB,BLKSIZE=80)
*  //SYSPRINT DD   SPACE=(CYL,(5,1)),UNIT=SYSDA
*  //PRINT    DD   SYSOUT=*
*  //SYSIN    DD   *
*   LISTC CAT(CATALOG.NAME.CONTAINING.CLUSTER.ENTRIES)
*
*----------------------------------------------------------------------
         EJECT
*----------------------------------------------------------------------
*
*        GET STORAGE FOR DYNAMIC ALLOCATION CONTROL BLOCK
*
*----------------------------------------------------------------------
VSAMLCLS $PROLOG
ENTRY010 DS    0H
         OPEN  (PRINT,(OUTPUT))
         PUT   PRINT,HD1          PRINT HEADING
         L     R0,RBSIZE          GET STORAGE FOR BUILDING
         GETMAIN R,LV=(0)         *   THE DYNAMIC ALOC REQUEST BLOCK
         ST    R1,REQBLK          SAVE ADDRESS OF STORAGE
EXIT010  EQU   *
         B     ENTRY020
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        INVOKE IDCAMS TO DO A LISTCAT
*
*----------------------------------------------------------------------
ENTRY020 DS    0H
         LINK  EP=IDCAMS,PARAM=H0,VL=1
         LTR   R15,R15       OK ?
         BNZ   ERROR2        NO - ERROR
         OPEN  SYSPRINT      YES - OPEN SYSPRINT FOR INPUT
EXIT020  EQU   *
         B     ENTRY030
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        ALLOCATE A CLUSTER AND LINK TO IDCAMS AND DO A VERIFY
*
*----------------------------------------------------------------------
ENTRY030 DS    0H
         GET   SYSPRINT    LISTCAT OUTPUT
         CLC   5(7,R1),=C'CLUSTER'    CLUSTER ENTRY ?
         BNE   ENTRY030       NO - GET NEXT RECORD
         XR    R3,R3
         ICM   R3,3,0(R1)      LENGTH OF THIS RECORD (RDW)
         SH    R3,=H'21'       MINUS GARBAGE DATA = LNGTH OF CLUS NAME
         LA    R4,21(R1)       POINT TO CLUSTER NAME
*
*        THE FOLLOWING COMPARES ARE INSTALLATION DEPENDENT
*        IN THAT THE 'NAME' OR 'QUALIFIER' FOR VSAM CATALOGS
*        MUST BE UPDATED (SPACE INFO FOR CATALOGS DOES NOT WORK).
*
         CLC   0(8,R4),=C'SYSCATLG'   CATALOG ENTRY ?
         BE    ENTRY030      YES - SKIP IT
         CLC   0(9,R4),=C'PRODCAT.Z'  UNUSED ENTRY ?
         BE    ENTRY030      YES - SKIP IT
         CH    R3,=H'32'     LONG ENOUGH TO BE NEXT CHECK ?
         BL    AAA030        NO - SKIP CHECK (0C4 ABEND IF TOO LONG)
         CLC   0(32,R4),=C'PRODCAT.ACCOUNTS.I60902.YEARMSTR'
         BE    ENTRY030      YES - SKIP IT
*
AAA030   EQU   *
         SH    R3,=H'1'      DECR FOR EX
         EX    R3,MVCVER     MOVE TO VERIFY CONTROL CARD
         LA    R3,1(R3)      BACK TO REAL LENGTH FOR ALLOCATION
         LA    R1,IDCNAME(R3)
         MVI   0(R1),C')'
         OPEN  (IDCIN,(OUTPUT))
         PUT   IDCIN,IDCREC  WRITE VERIFY CONTROL CARD
         MVC   IDCNAME,=75C' '  CLEAR NAME FIELD
         CLOSE IDCIN
         FREEPOOL IDCIN      FREE BUFFERS
         LINK  EP=IDCAMS,PARAM=(H0,ALTDD),VL=1
         BAL   R14,ENTRY910    ALLOCATE THE CLUSTER
EXIT030  EQU   *
         B     ENTRY040
MVCVER   MVC   IDCNAME(0),0(R4)  MOVE CLUSTER NAME TO VER RECORD
IDCREC   DS    0CL80
         DC    C' VFY DS('
IDCNAME  DC    CL75' '
ALTDD    DS    0F
         DC    H'48'
         DC    XL32'00'
         DC    CL8'IDCIN'
         DC    CL8'IDCOUT'
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        OPEN CLUSTER - RETREIVE CATALOG INFO
*
*----------------------------------------------------------------------
ENTRY040 DS    0H
         OPEN  SYSUT99
         LTR   R15,R15   IS IT OPEN ?
         BNZ   ERROR3    NO - SKIP IT
         SHOWCB ACB=SYSUT99,AREA=CATDATA,LENGTH=12,OBJECT=DATA,        X
               FIELDS=(ENDRBA,HALCRBA,NEXT)
         LTR   R15,R15   OK ?
         BNZ   ERROR4
         CLOSE SYSUT99
         BAL   R14,ENTRY920    DE-ALLOCATE CLUSTER
EXIT040  EQU   *
         B     ENTRY050
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        CALCULATE % FREE SPACE
*
*----------------------------------------------------------------------
ENTRY050 DS    0H
         XR    R0,R0
         L     R1,ENDRBA      HI-USED-RBA
         XR    R6,R6
         L     R7,HALCRBA     HI-ALLOCATED-RBA
         L     R15,=F'100'    WORK WITH PER-CENT
         DR    R6,R15         * DIVIDE THE HI ALLOC BY 100 FIRST
         C     R7,=F'0'       ZERO DIVISOR FOR FINDING RATIO ?
         BNE   AAA050         NO - DIVIDE IT
         LA    R1,0           YES - SET QUOTIENT TO ZERO
         B     BBB050         SKIP DIVISION
AAA050   EQU   *
         DR    R0,R7          PERCENT USED FOR THIS CLUSTER
BBB050   EQU   *
         CVD   R1,DBWRD       QUOTIENT
         MVC   PRPERCNT,=X'40202120'
         ED    PRPERCNT,DBWRD+6
         MVI   PRPC,C'%'
EXIT050  EQU   *
         B     ENTRY060
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        CALCULATE NUMBER OF CYLINDERS
*
*----------------------------------------------------------------------
ENTRY060 DS    0H
         XR    R6,R6
         L     R7,HALCRBA     HI-ALLOCATED-RBA
         L     R15,=F'4096'   AVERAGE CI SIZE
*                             * 4096 WILL WORK FOR CISZ OF 2048, 6144
         DR    R6,R15         (BYTES)/(BYTES/CI) = # OF CI'S   (R7)
         XR    R6,R6          CLEAR REMAINDER, IF ANY
         L     R15,=F'120'    120 CI'S PER CYL (3350)
         DR    R6,R15         NUMBER OF CYLINDERS ALLOCATED
         CVD   R7,DBWRD       CONVERT CYLS TO DECIMAL
         MVC   PRCYL,=X'40202120'
         ED    PRCYL,DBWRD+6
EXIT060  EQU   *
         B     ENTRY070
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        CALCULATE EXTENTS USED
*
*----------------------------------------------------------------------
ENTRY070 DS    0H
         L     R1,NEXT        NUMBER OF EXTENTS
         CVD   R1,DBWRD
         MVC   PRNEXTNT,=X'40202120'
         ED    PRNEXTNT,DBWRD+6
         PUT   PRINT,PRLINE
         MVC   PRLINE,=72C' '
EXIT070  EQU   *
         B     ENTRY030
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        ALLOC A CLUSTER AS SHR
*
*----------------------------------------------------------------------
ENTRY910 DS    0H
         ST    R14,ST910          SAVE RETURN ADDRESS
         L     R11,REQBLK         ADDRESS OF REQUEST BLOCK
         USING S99RBP,R11         REQ BLK POINTER DSECT
         LA    R10,S99RBP+4       ADDRESSABILITY OF RB DSECT
         USING S99RB,R10          RB DSECT
         ST    R10,S99RBPTR       MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND  TURN ON HOB IN RBPTR
         XC    S99RB(RBLEN),S99RB  CLEAR RB
         MVI   S99RBLN,RBLEN      PUT LEN IN ITS LENGTH FIELD
         MVI   S99VERB,S99VRBAL   SET VERB CODE TO ALLOCATE
         LA    R9,S99RB+RBLEN     POINT BEYOND RB (1ST TEXT PTR)
         USING S99TUPL,R9         ADDRESSABILITY OF TEXT UNIT POINTERS
         ST    R9,S99TXTPP        INIT TEXT PTR IN RB
         LA    R8,S99TUPL+16      POINT BEYOND LAST TXT PTR (1ST TXT
*                                  UNIT)
         USING S99TUNIT,R8        ADDRESSABILITY OF TEXT UNITS
* DDNAME
         ST    R8,S99TUPTR        1ST PTR TO 1ST UNIT
         LA    R1,DALDDNAM        KEY FOR DDNAME
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0007'  MOVE 7 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(7),=C'SYSUT99'  DDNAME
         LA    R8,13(R8)          BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* DSNAME
         ST    R8,S99TUPTR        2ND PTR TO 2ND UNIT
         LA    R1,DALDSNAM        KEY FOR DSNAME
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         STCM  R3,3,S99TULNG      STORE LENGTH OF CLUSTER NAME
         SH    R3,=H'1'           DECR FOR EXECUTE
         EX    R3,MVC1            MOVE CLUSTER NAME FOR DYNALOC
         EX    R3,MVC2            MOVE CLUSTER NAME TO PRINT LINE
         LA    R8,7(R3,R8)        BUMP TO NEXT TXT UNIT
         LA    R9,4(R9)           BUMP TO NEXT TXT UNIT PTR
* DATA SET STATUS - SHR
         ST    R8,S99TUPTR        4TH PTR TO 4TH UNIT
         OI    S99TUPTR,S99TUPLN  TURN ON HOB TO INIDICATE LAST PTR
         LA    R1,DALSTATS        KEY FOR DSN STATUS
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0001'  MOVE 1 TO TXT UNIT LNGTH FLD
         MVI   S99TUPAR,X'08'     INDICATE SHR DATA SET
********
         LR    R1,R11             ADDR OF REQUEST BLOCK
         DYNALLOC
         ST    R15,RC             SAVE RETURN CODE
         LTR   R15,R15            RETURN CODE ZERO ?
         BNZ   ERROR1             NO - DECODE ERROR
*        QTPUT 'ALLOCATED - SHR'  FOR DEBUGGING
EXIT910  EQU   *
         L     R14,ST910
         BR    R14
ST910    DC    F'0'
         LTORG
MVC1     MVC   S99TUPAR(0),0(R4)
MVC2     MVC   PRCLUSTR(0),0(R4)
         EJECT
*----------------------------------------------------------------------
*
*        DE-ALLOC A CLUSTER
*
*----------------------------------------------------------------------
ENTRY920 DS    0H
         ST    R14,ST920          SAVE RETURN ADDRESS
         L     R11,REQBLK         ADDRESS OF REQUEST BLOCK
         USING S99RBP,R11         REQ BLK POINTER DSECT
         LA    R10,S99RBP+4       ADDRESSABILITY OF RB DSECT
         USING S99RB,R10          RB DSECT
         ST    R10,S99RBPTR       MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND  TURN ON HOB IN RBPTR
         XC    S99RB(RBLEN),S99RB  CLEAR RB
         MVI   S99RBLN,RBLEN      PUT LEN IN ITS LENGTH FIELD
         MVI   S99VERB,S99VRBUN   SET VERB CODE TO UN-ALLOCATE
         LA    R9,S99RB+RBLEN     POINT BEYOND RB (1ST TEXT PTR)
         USING S99TUPL,R9         ADDRESSABILITY OF TEXT UNIT POINTERS
         ST    R9,S99TXTPP        INIT TEXT PTR IN RB
         LA    R8,S99TUPL+4       POINT BEYOND LAST TXT PTR (1ST TXT
*                                  UNIT)
         USING S99TUNIT,R8        ADDRESSABILITY OF TEXT UNITS
* DDNAME
         ST    R8,S99TUPTR        1ST PTR TO 1ST UNIT
         OI    S99TUPTR,S99TUPLN  TURN ON HOB TO INIDICATE LAST PTR
         LA    R1,DALDDNAM        KEY FOR DDNAME
         STH   R1,S99TUKEY        PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=X'0001'  MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=X'0007'  MOVE 7 TO TXT UNIT LNGTH FLD
         MVC   S99TUPAR(7),=C'SYSUT99'  DDNAME
********
         LR    R1,R11             ADDR OF REQUEST BLOCK
         DYNALLOC
         ST    R15,RC             SAVE RETURN CODE
         LTR   R15,R15            RETURN CODE ZERO ?
         BNZ   ERROR1             NO - DECODE ERROR
*        QTPUT 'DE-ALLOCATED'     FOR DEBUGGING
EXIT920  EQU   *
         L     R14,ST920
         BR    R14
ST920    DC    F'0'
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        END OF JOB
*
*----------------------------------------------------------------------
EOJ      DS    0H
         CLOSE (PRINT,,SYSPRINT)
         L     R0,RBSIZE
         L     R1,REQBLK
         FREEMAIN R,LV=(0),A=(1)
RETURN   L     R15,RC             PICK UP RETURN CODE
         $EPILOG
         EJECT
*----------------------------------------------------------------------
*
*        ERROR ROUTINES
*
*----------------------------------------------------------------------
ERROR1   DS    0H
         UNPK  DYNERR,S99ERROR(3)  1 MORE THAN FIELD SIZE
         MVI   DYNERR+4,X'40'   BLANK OUT GARBAGE
         TR    DYNERR(4),TRTBL-240    TRANSLATE
         MVC   PRPERCNT(27),EMSG
         PUT   PRINT,PRLINE
         MVC   PRLINE,=72C' '
         B     ENTRY030
EMSG     DC    C'ALLOC FAILED - '
         DC    CL6'CODE: '
DYNERR   DC    CL5' '
         DC    20C' '
TRTBL    DC    C'0123456789ABCDEF'
DBWRD    DC    D'0'
*----------------------------------------------------------------------
ERROR2   DS    0H
         ABEND 2,DUMP
*----------------------------------------------------------------------
ERROR3   DS    0H
         CLOSE SYSUT99    MAKE SURE ITS CLOSED
         BAL   R14,ENTRY920   DE-ALLOCATE IT
         MVC   PRPERCNT(11),=C'OPEN FAILED'
         PUT   PRINT,PRLINE
         MVC   PRLINE,=72C' '
         B     ENTRY030
*----------------------------------------------------------------------
ERROR4   DS    0H
         ABEND 4,DUMP
         EJECT
*----------------------------------------------------------------------
*
*        STORAGE AREAS
*
*----------------------------------------------------------------------
REQBLK   DC    F'0'
RBSIZE   DC    F'512'
RC       DC    F'0'
H0       DC    H'0'
*----------------------------------------------------------------------
CATDATA  DS    0F
ENDRBA   DC    F'0'
HALCRBA  DC    F'0'
NEXT     DC    F'0'
*----------------------------------------------------------------------
HD1      DS    0CL72
         DC    C'1'
         DC    CL44'CLUSTER NAME'
         DC    C' '
         DC    CL5' USED'
         DC    CL3' '
         DC    CL4'CYLS'
         DC    CL3' '
         DC    CL4' EXT'
         DC    CL20' '
*----------------------------------------------------------------------
PRLINE   DS    0CL72
CCC      DC    C' '
PRCLUSTR DC    CL44' '
         DC    C' '
PRPERCNT DC    CL4' '
PRPC     DC    C' '
         DC    CL3' '
PRCYL    DC    CL4' '
         DC    CL3' '
PRNEXTNT DC    CL4' '
         DC    20C' '
*----------------------------------------------------------------------
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=GL,EODAD=EOJ
         EJECT
IDCIN    DCB   DDNAME=IDCIN,DSORG=PS,MACRF=PM
         EJECT
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=PM,LRECL=72,RECFM=FB,       X
               BLKSIZE=72
         EJECT
SYSUT99  ACB   MACRF=(KEY,DIR,IN)
         EJECT
*----------------------------------------------------------------------
*
*        DSECTS
*
*----------------------------------------------------------------------
         IEFZB4D0
         EJECT
         IEFZB4D2
RBLEN    EQU   (S99RBEND-S99RB)
         END
