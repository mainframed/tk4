         TITLE 'BATCH JOB COND CODE CHECKING AND SUBMISSION ROUTINE'
**********************************************************************
*
*       THIS ROUTINE WILL PLACE A MESSAGE UPON THE OPERATOR'S
*       CONSOLE TO INFORM AS TO WHETHER A JOB HAS BEEN RUN
*       ABNORMALLY (BAD COND CODE) OR ABENDED.
*
*       ADDITIONALLY, IF JOB NAMES ARE SPECIFIED AS EXTRA PARM INFO
*       THE SPECIFIED JOB(S) WILL BE SUBMITTED TO THE INTERNAL
*       READER (IF ALL CONDITION TESTS ARE SATISFIED).
*
*       EACH JOB TO BE CHECKED MUST INCLUDE A FINAL EXECUTE CARD
*       TO EXECUTE A 'RUNCHECK' PROC AS DEFINED BELOW.
*
*  EXAMPLE RUNCHECK PROC FOLLOWS:
*
*  //RUNCHECK   PROC   JOB=
*  //GOOD       EXEC   PGM=SYSRCHK,COND=(0,LT),PARM='GOOD &JOB'
*  //SYSPRINT   DD     DUMMY
*  //PANDD1     DD     DSN=SYS1.PANJCL,DISP=SHR
*  //PANDD2     DD     SYSOUT=(A,INTRDR),DCB=BUFNO=1
*  //SYSIN      DD     SPACE=(TRK,(1,1)),UNIT=SYSDA
*  //BAD        EXEC   PGM=SYSRCHK,COND=(4,EQ,S1),PARM='BAD'
*  //ABORT      EXEC   PGM=SYSRCHK,COND=ONLY,PARM='ABORT'
*
**********************************************************************
SYSRCHK  CSECT
RUNCHECK $PROLOG
         L     R1,0(R1)            ADDRESS OF PARM DATA
         LH    R2,0(R1)            PARM LENGTH
         LTR   R2,R2               ANY PARM PASSED?
         BNZ   CHECK1              YES, CHECK FOR VALID PARM
PARMBAD  WTO   'RUNCHECK - INVALID/NO PARM SPECIFIED',ROUTCDE=(2,11)
         ABEND 999                 STOP ANY FURTHUR PROCESSING
CHECK1   CLC   =C'GOOD ',2(R1)     GOOD RUN?
         BNE   CHECK2              NO, NEXT TEST
         OI    OPTION,GOOD         SET GOOD RUN OPTION
         LA    R5,1(R1,R2)         GET END OF PARM ADDRESS
         SH    R2,=H'5'            REDUCE PARM COUNT
         BNP   EXIT                RETURN IF NO ADDITIONAL PARM
         LA    R3,7(R1)            GET START OF JOBNAME FIELD
         LA    R4,1                GET INCREMENT FACTOR
NEXTJOB  LR    R1,R3               SAVE FOR LATER MOVE
JOBLOOP  CLI   0(R3),C','          SEPARATOR CHARACTER?
         BE    MOVENAME            YES, BUILD SUBMIT JOBNAME
         BXLE  R3,R4,JOBLOOP       CHECK NEXT CHARACTER
MOVENAME LR    R15,R3              COPY ENDING ADDRESS
         LA    R3,1(R3)            BUMP PAST COMMA
         SR    R15,R1              GET SIZE FOR MVC
         BCTR  R15,R0              DECREMENT FOR EXECUTE
         BP    BUILD               VALID JOBNAME, CONTINUE
         CR    R3,R5               END OF PARM INFO?
         BL    NEXTJOB             NO, GET NEXT JOB
         B     CHKCLOSE            YES, INSURE DCB CLOSE
BUILD    EX    R15,NAMEMOVE        MOVE NAME TO PAN RECORD
         LA    R1,PANREC           SET PAN RECORD ADDRESS
         BAL   R14,PUT             WRITE PAN RECORD
         MVC   PANREC+15(10),=CL10' ' CLEAR JOBNAME FIELD
         CR    R3,R5               END OF PARM INFO?
         BL    NEXTJOB             NO, GET NEXT JOB
CHKCLOSE TM    PUTDCB+48,X'10'     IS DCB OPEN?
         BZ    EXIT                NO, DON'T GO TO PAN
         CLOSE (PUTDCB)            YES, CLOSE PANVALET DCB
         LA    R1,NULLPARM         SET UP DUMMY PAN PARM
         LINK  EP=PAN#1            LINK TO PANVALET
         B     EXIT                SET RETCODE AND EXIT
CHECK2   CLC   =C'BAD',2(R1)       BAD RUN?
         BNE   CHECK3              NO, NEXT TEST
         OI    OPTION,BAD          SET BAD RUN OPTION
         B     PARMOK              GET TIOT POINTER
CHECK3   CLC   =C'ABORT',2(R1) ABORT OPTION?
         BNE   PARMBAD             NO, ERROR
         OI    OPTION,ABORT        SET ABORT OPTION
PARMOK   L     R11,16              GET CVTPOINTER
         L     R11,0(R11)          TCB DOUBLEWORD POINTER
         L     R11,4(R11)          CURRENT TCB POINTER
         L     R11,12(R11)         TIOT POINTER
         MVC   WTO1+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         MVC   WTO2+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         MVC   WTO3+16(8),0(R11)   MOVE JOBNAME TO MESSAGE
         WTO   '*********************************',ROUTCDE=(2,11)
         TM    OPTION,GOOD         GOOD RUN?
         BNO   TEST                NO, LONG FORMAT MESSAGE
WTO1     WTO   '*****   JJJJJJJJ GOOD-RUN   *****',ROUTCDE=(2,11)
         B     SKIP                SKIP TO LAST
TEST     WTO   '*****                       *****',ROUTCDE=(2,11)
         TM    OPTION,BAD          BAD RUN?
         BNO   WTO3                NO, MUST BE ABORT
WTO2     WTO   '*****   JJJJJJJJ BAD-RUN    *****',ROUTCDE=(2,11)
         B     LAST                SKIP NEXT MSG
WTO3     WTO   '*****   JJJJJJJJ ABORTED    *****',ROUTCDE=(2,11)
LAST     WTO   '*****                       *****',ROUTCDE=(2,11)
SKIP     WTO   '*********************************',ROUTCDE=(2,11)
EXIT     LA    R15,4               SET RETURN CODE OF 4
         $EPILOG
NAMEMOVE MVC   PANREC+15(0),0(R1)  MODIFY PAN RECORD
PANREC   DC    CL80'++WRITE WORK,EX              '
NULLPARM DC    A(*+4),H'0'         NULL PARM FOR PANVALET
PUT      SYSPUT DDNAME=SYSIN       PANVALET INPUT FILE
OPTION   DC    X'00'               REQUEST OPTION
GOOD     EQU   X'01'               GOOD RUN REQUEST
BAD      EQU   X'02'               BAD RUN REQUEST
ABORT    EQU   X'04'               ABORT REQUEST
         END
