         PRINT NOGEN
*----------------------------------------------------------------------
*
*
*
*        THIS PROGRAM SEARCHES A SYSOUT TAPE (SYSUT1) FOR A GIVEN
*        JOB AND WRITES THAT JOB TO AN OUTPUT FILE(SYSUT2).
*        THE JOBNAME OR JOB NUMBER ARE PASSED AS PARM INFORMATION.
*        IF "JOBNAME" IS USED, THEN THE FIRST JOB FOUND WITH THAT
*        NAME WILL BE WRITTEN TO THE OUTPUT DATASET.  IF JOB "NUMBER"
*        IS USED, ONLY THAT SPECIFIC JOB WILL BE COPIED REGARDLESS
*        OF JOB NAME.
*
*        DIFFERENTIATION IS BY THE FIRST CHARACTER OF THE PARM:
*        IF NON-NUMERIC, THEN IT IS A JOB NAME
*        IF NUMERIC, THEN IT IS A JOB NUMBER
*
*        PARM='J12345'       WILL CAUSE THE FIRST JOB WITH THAT NAME
*                            TO BE COPIED.
*        PARM='509'          WILL CAUSE JOB 509 TO BE COPIED.
*
*----------------------------------------------------------------------
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZATION AND PARM ISOLATION
*
*----------------------------------------------------------------------
GENJOB   $PROLOG R12              ENTRY LINKAGE
         L     R1,0(R1)           GET PARM POINTER
         LH    R2,0(R1)           GET PARM SIZE
         CH    R2,=H'0'           ANY PARM?
         BE    RETURN             NO, RETURN TO CALLER
         CH    R2,=H'8'           VALID SIZE?
         BH    RETURN             NO, RETURN TO CALLER
         CLI   2(R1),C'0'         CHECK IF NUMERIC FIRST CHAR
         BL    MOVENAME           NOT NUMERIC, MOVE JOB NAME
         OI    SWITCH,NUM         INDICATE SEARCH OPTION
         CH    R2,=H'4'           CHECK LIMIT FOR JOB NUMBER
         BH    RETURN             HIGH, ERROR
         LA    R15,4              GET LIMIT FOR NUMBER
         SR    R15,R2             GET DIFFERENCE
         LA    R15,JOBNAME(R15)   GET OFFSET
         BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVE1           MOVE PARM TO JOBNAME FIELD
         LA    R2,3               SET TO COMPARE COUNT
         B     OPEN               SKIP TO START
MOVENAME BCTR  R2,0               DECREMENT FOR EXECUTE
         EX    R2,MOVE2           MOVE PARM TO JOBNAME FIELD
         EJECT
*----------------------------------------------------------------------
*
*        INITIALIZATION AND PARM ISOLATION
*
*----------------------------------------------------------------------
OPEN     OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT)) OPEN FILES
SKIP     GET   SYSUT1             GET INPUT RECORD
         CLC   20(9,R1),STARTED   COMPARE FOR JOB START
         BNE   SKIP               NO, SKIP RECORD
         TM    SWITCH,NUM         CHECK SEARCH OPTION
         BZ    NORMAL             NOT NUMERIC, NORMAL SEARCH
         EX    R2,COMP1           COMPARE FOR SELECTED JOB NUMBER
         BE    FOUND              YES, COPY IT
         B     SKIP               NO, SKIP RECORD
NORMAL   EX    R2,COMP2           COMPARE FOR SELECTED JOB NAME
         BNE   SKIP               NO, SKIP RECORD
FOUND    OI    SWITCH,DATA        INDICATE DATA WAS FOUND
WRITE    LA    R0,1(R1)           SKIP ASA/MACHINE CONTROL CHARS
         PUT   SYSUT2,(0)         WRITE OUTPUT RECORD
         GET   SYSUT1             NEXT RECORD
         CLC   20(9,R1),STARTED   COMPARE FOR NEXT JOB START
         BNE   WRITE              NO, CONTINUE
EODAD    CLOSE (SYSUT1,,SYSUT2)   CLOSE FILES
         TM    SWITCH,DATA        DID WE FIND THE JOB?
         BZ    RETURN             NO, PASS RETCODE AS IS
         XC    RETCODE,RETCODE    CLEAR RETURN CODE
RETURN   L     R15,RETCODE        LOAD RETURN CODE
         $EPILOG ,                RETURN TO CALLER
         EJECT
*----------------------------------------------------------------------
*
*        CONSTANTS AND WORKAREAS
*
*----------------------------------------------------------------------
MOVE1    MVC   0(0,R15),2(R1)     EXECUTED MOVE OF JOB NUMBER
MOVE2    MVC   JOBNAME(0),2(R1)   EXECUTED MOVE OF JOB NAME
COMP1    CLC   14(0,R1),JOBNAME   EXECUTED COMPARE FOR JOB NUMBER
COMP2    CLC   29(0,R1),JOBNAME   EXECUTED COMPARE FOR JOB NAME
SWITCH   DC    X'00'              OPTION SWITCH
NUM      EQU   X'01'              REQUEST FOR JOB NUMBER SEARCH
DATA     EQU   X'02'              SEARCH SUCCESSFUL
STARTED  DC    CL9'$HASP373'      HASP STARTED MESSAGE
JOBNAME  DC    CL8' '             JOBNAME FIELD (PARM)
RETCODE  DC    F'16'              RETURN CODE (PRIMED FOR ERROR)
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=EODAD,            X
               LRECL=133,RECFM=FBM
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,                        X
               LRECL=132,RECFM=FB,BLKSIZE=6072
         END
