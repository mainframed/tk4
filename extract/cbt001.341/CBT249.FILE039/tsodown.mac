*
*        THIS ROUTINE WHEN STARTED BY THE OPERATOR WILL ALLOW A
*        GRACEFULL SHUTDOWN OF TSO/TCAM.  LOGGED ON USERS ARE
*        SENT MESSAGES AT PREDETERMINED INTERVALS (TOTAL OF FIVE
*        MINUTES) INFORMING THEM OF THE TIME REMAINING TO SHUTDOWN.
*
*        IF THE OPTIONAL PARM OF 'QUICK' IS GIVEN, THEN ONLY ONE
*        NOTICE IS SENT TO TIME-SHARING USERS (TOTAL OF 30 SECONDS)
*        PRIOR TO SHUTDOWN.
*
*        ONCE TSO HAS BEEN SHUTDOWN, A 'Z TP' COMMAND IS ISSUED
*        TO STOP TCAM.
*
*        THIS ROUTINE IS DEPENDENT ON THE TASK NAME OF 'TCAM' TO
*        TERMINATE TIME SHARING.  IT MUST ALSO BE LINKED AS
*        AUTHORIZED TO ALLOW FOR SVC-34 PROCESSING.
*
*        BOB BECKER MAY, 1979
*
         EJECT
*----------------------------------------------------------------------
*
*        CHECK FOR TCAM ACTIVE
*
*----------------------------------------------------------------------
TSODOWN  $PROLOG
ENTRY003 DS    0H
         LR    R3,R1              SAVE PARM
         L     R2,16              CVT ADDR
         TM    240(R2),X'80'      IS TCAM ACTIVE ?
         BZ    RETURN             NO - SKIP SHUTDOWN
EXIT003  EQU   *
         B     ENTRY005
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        CHECK FOR PARM
*
*----------------------------------------------------------------------
ENTRY005 DS    0H
         MODESET KEY=ZERO,MODE=SUP  GET KEY ZERO AND AUTH
         L     R1,0(R3)           PARM ADDR
         SR    R2,R2              CLEAR FOR ICM
         ICM   R2,3,0(R1)         PARM LENGTH
         BZ    EXIT005            NO PARM - 5 MINUTE WARNING ROUTINE
         CH    R2,=H'5'           CORRECT LENGTH ?
         BNE   EXIT005            NO - DEFAULT
         CLC   2(5,R1),=C'QUICK'  CORRECT KEYWORD
         BNE   EXIT005            NO - DEFAULT
         MVC   MSG,QMSG
         LM    R0,R1,MGCRREGS     GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         MVC   TIME,=CL8'00003000'  30 SECONDS
         STIMER  WAIT,DINTVL=TIME  WAIT REQUESTED LENGTH OF TIME
         B     CCC010
EXIT005  EQU   *
         B     ENTRY010
         LTORG
         EJECT
*----------------------------------------------------------------------
*
*        FIVE WARNING MESSAGES AT 1 MINUTE INTVLS
*
*----------------------------------------------------------------------
ENTRY010 DS    0H
         LA    R10,5
LOOP010A EQU   *
         TIME  DEC
         STCM  R0,8,HH
         UNPK  HOURS,HH
         MVC   MSG+51(2),HOURS
         STCM  R0,4,MM
         UNPK  MINUTES,MM
         MVC   MSG+54(2),MINUTES
NOP010   NOP   BBB010
         MVI   NOP010+1,X'F0'     SET TO BR
         AP    MM,=P'50'          ADD 5.0 MINUTES
         CP    MM,=P'600'         PAST 60.0 MINUTES ?
         BL    AAA010             NO - OK
         AP    HH,=P'10'          ADD 1.0 HOURS
         SP    MM,=P'600'         SUB 60.0 MINUTES
AAA010   EQU   *
         UNPK  MINUTES,MM
         MVC   MSG+27(2),HOURS
         MVC   MSG+30(2),MINUTES
BBB010   EQU   *
         LM    R0,R1,MGCRREGS     GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         STIMER  WAIT,DINTVL=TIME  WAIT 1 MINUTE
         BCT   R10,LOOP010A
CCC010   EQU   *
         MVC   MSG,=CL100'F TCAM,TS=STOP'
         MVC   TIME,=CL8'00003000'  30 SECONDS
         LM    R0,R1,MGCRREGS     GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         STIMER  WAIT,DINTVL=TIME  WAIT REQUESTED LENGTH OF TIME
         MVC   MSG,=CL100'Z TP,QUICK'
         LM    R0,R1,MGCRREGS     GET SVC 34 REGS
         SVC   34                 SCHEDULE COMMAND
         MODESET KEY=NZERO,MODE=PROB  RELEASE AUTHORIZATION
RETURN   DS    0H
         L     R13,4(R13)
         @RETURN RC=0
         DS    0D                 HHMMSSTH
TIME     DC    CL8'00010000'      DEFAULT OF 1 MINUTE
MGCRREGS DC    F'0'               REG 0 FOR SVC 34
         DC    A(CIB)             REG 1 FOR SVC 34
CIB      DC    AL2(98)            MAXIMUM LENGHT OF COMMAND
         DC    H'0'               SVC 34 PADDING
MSG      DC    CL100'SEND ''TSO WILL SHUTDOWN AT //.// - CURRENT TIME IX
               S //.//'' '
QMSG     DC    CL100'SEND ''TSO IS SHUTTING DOWN IN 20 SECONDS'' '
HH       DC    PL2'0'
MM       DC    PL2'0'
HOURS    DC    CL3' '
MINUTES  DC    CL3' '
         LTORG
         END
