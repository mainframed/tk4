DEBE     TITLE 'OPERATING SYSTEM/370 MULTIFUNCTION UTILITY - VERSION 3'
DEBE     CSECT
         PRINT NOGEN
         EJECT
***********************************************************************
*
*        OOOOO  SSSSS  DDDD   EEEEE  BBBB   EEEEE     III  III
*        O   O  S      D   D  E      B   B  E          I    I
*        O   O  SSSSS  D   D  EEE    BBBB   EEE   ***  I    I
*        O   O      S  D   D  E      B   B  E          I    I
*        OOOOO  SSSSS  DDDD   EEEEE  BBBB   EEEEE     III  III
*
*        MILWAUKEE MODIFICATION TO 1ST NATNL BANK OF CHICAGO'S
*
***********************************************************************
*
*  THE PURPOSE OF OSDEBE IS TO PROVIDE THE USER OF 370 OPERATING
*  SYSTEM THE FLEXIBILITY OF THE ORIGINAL DEBE.  FLEXIBILITY CAN
*  ONLY BE OBTAINED BY THE ELIMINATION OF DD CARDS.  ALL FUNCTIONS
*  WHICH ARE AVAILABLE WITH BPS DEBE ARE PROVIDED IN OSDEBE.
*  IN ADDITION, MANY NEW FEATURES HAVE BEEN ADDED; FOR A COMPLETE
*  DESCRIPTION, SEE THE OSDEBE PROGRAM MANUAL.
*
***********************************************************************
*
*  CORE REQUIREMENTS - ABOUT 48K (K=1024), 32K OF WHICH IS USED
*  AS AN I/O AREA FOR TAPE OPERATIONS.
*
*  ERROR RECOVERY - THE OPERATING SYSTEM PERFORMS ALL STANDARD
*  ERROR RECOVERY.  IF IT CANNOT RECOVER FROM AN ERROR, OSDEBE
*  WILL NOT EVEN TRY.
*
*  ALL REPLIES TO OSDEBE ARE OF THE WTOR FORMAT,
*          REPLY 00,'TP'
*  WHENEVER ANY INFORMATION IS REQUESTED BY OSDEBE, A REPLY OF
*  'EJ' WILL TERMINATE THE SUBROUTINE OF OSDEBE.  A REPLY OF 'EJ'
*  IN RESPONSE TO THE REQUEST FOR THE PROGRAM ID WILL TERMINATE
*  OSDEBE AND CONTROL IS RETURNED TO THE OPERATING SYSTEM.
*
*  ONCE A ROUTINE IS RUNNING IT MAY BE TERMINATED VIA A REPLY
*  OF 'CANX' TO AN OUTSTANDING WTOR.  PREVIOUSLY OSDEBE HAD TO BE
*  CANCELLED ENTIRELY IN ORDER TO TERMINATE A ROUTINE ONCE IT HAD
*  STARTED, AND THEN BE RESCHEDULED TO CONTINUE.
*
***********************************************************************
*
*  THIS REPRESENTS A MAJOR REVISION TO 360D00.1.015, WRITTEN
*  BY JOHN W. JUHLMAN OF IBM CINCINNATI.
*
***********************************************************************
         EJECT
*
*  INITIALIZATION
*
         USING *,12,9              INFORM ASSEMBLER OF BASE
         SAVE  (14,12)             SAVE ENTRY REGS
         LR    12,15               INITIALIZE BASE REG
         LR    9,15                INITIALIZE 2ND BASE
         A     9,K4096             BUMP TO NEXT SLOT
         LR    15,13               SAVE PREVIOUS SAVE POINTER
         CNOP  0,4                 FULLWORD ALIGNMENT
         BAL   13,AROUND           BRANCH TO CHAIN AND SET SAVE
         DC    18F'0'              SAVE AREA
K4096    DC    F'4096'             NEXT SLOT ADDRESS
AROUND   ST    13,8(15)            CHAIN FORWARD
         ST    15,4(13)            CHAIN BACKWARD
         SPACE 5
         L     3,16                GET CVT POINTER
         L     2,0(3)              OLD/NEW POINTERS
         L     3,4(2)              CURRENT TCB POINTER
*  SET PROTECT KEY FROM TCB FOR THE DEB'S
         OC    INDEB+24(1),28(3)
         OC    OUTDEB+24(1),28(3)
*  SET TCB ADDRESS FOR THE DEB'S
         ST    3,INDEB
         ST    3,OUTDEB
    SPACE 3
* THE FOLLOWING MACRO EXPANSION WILL ALLOW DEBE TO RUN IN A VS
* SYSTEM BY USING A PROTECT KEY OF ZERO TO BYPASS VALIDITY CHECKING.
         MODESET KEY=ZERO,MODE=SUP
         EJECT
         SR    11,11               CLEAR REG 11 TO INDICATE NO RTN
         B     INITIAL             GO TO ISSUE INITIAL WTOR
CHECKREP OC    COMM(1),BLANKS      UPPER CASE
         CLC   COMM(1),=C'C'       IS THIS A ROUTINE CANCEL REQUEST
         BNE   INITIAL             IF NOT, RE-ISSUE WTOR
         SR    11,11               IF SO, INDICATE NO RETURN
INITIAL  XC    COMMECB(8),COMMECB  CLEAR ECB FOR WTOR
         WTOR  'DEBE: REPLY ''C'' TO TERMINATE ANY ROUTINE',           X
               COMM,4,COMMECB,ROUTCDE=(2,3,7)
         LTR   11,11               IS ROUTINE RETURN INDICATED?
         BCR   7,11                IF SO RETURN TO ROUTINE
         SPACE 4
*
*  MAIN PROGRAM
*  ALL ROUTINES RETURN HERE WHEN THEY ARE FINISHED.
*
ASKAGN   EQU   *
         NI    INIOB,X'3F'         TURN OFF CHAIN FLAGS IN IOB'S
         NI    OUTIOB,X'3F'
         NI    TD3+1,X'0F'         RESET SWITCHES SET IN TP ROUTINE
         NI    TD6+1,X'0F'
         WTOR  'DEBE: ENT PROG ID - XX',ID,2,REPLYECB,ROUTCDE=(2,3,7)
         BAL   11,WAITANS          GO WAIT FOR A REPLY
         OC    ID(2),BLANKS
         EJECT
         CLC   ID(2),=C'EJ'        IS SHE DONE?
         BE    EOJ                 YES, RETURN TO THE OPERATING SYSTEM
         CLC   ID(2),=C'BF'        BACK SPACE FILE
         BE    BF
         CLC   ID(2),=C'BS'        BACK SPACE RECORD
         BE    BS
         CLC   ID(2),=C'BT'        BLOCK CARDS TO TAPE
         BE    BT
         CLC   ID(2),=C'CC'        CARD TO CARD (80-80)
         BE    CC
         CLC   ID(2),=C'CP'        CARD TO PRINT
         BE    CP
         CLC   ID(2),=C'CT'        CARD TO TAPE (80-80)
         BE    CT
         CLC   ID(2),=C'DP'        DEBLOCK TAPE TO PRINT
         BE    DP
         CLC   ID(2),=C'KC'        COUNT CARDS
         BE    KC
         CLC   ID(2),=C'KR'        COUNT RECORDS (TAPE)
         BE    KR
         CLC   ID(2),=C'RU'        REWIND/UNLOAD (TAPE)
         BE    RU
         CLC   ID(2),=C'RW'        REWIND (TAPE)
         BE    RW
         CLC   ID(2),=C'SF'        SPACE FILE (TAPE)
         BE    SF
         CLC   ID(2),=C'SR'        SPACE RECORD (TAPE)
         BE    SR
         CLC   ID(2),=C'TC'        TAPE TO CARD (DEBLOCKED)
         BE    TC
         CLC   ID(2),=C'TD'        TAPE DUMP TO PRINT (HEX LIST)
         BE    TD
         CLC   ID(2),=C'TM'        TAPE MARK TAPE (MULTIPLE)
         BE    TM
         CLC   ID(2),=C'TP'        TAPE TO PRINT
         BE    TP
         CLC   ID(2),=C'TT'        TAPE TO TAPE COPY
         BE    TT
         CLC   ID(2),=C'WT'        WRITE TAPE MARK (SINGLE)
         BE    WT
         CLC   ID(2),=C'XP'        REASSIGN PUNCH DEVICE
         BE    XP
         CLC   ID(2),=C'XR'        REASSIGN READER DEVICE
         BE    XR
         CLC   ID(2),=C'XS'        REASSIGN PRINTER DEVICE
         BE    XS
         WTO   'DEBE: BAD ID - TRY AGAIN',ROUTCDE=(2,3,7)
         B     ASKAGN
         SPACE 3
EOJ      MODESET KEY=NZERO,MODE=PROB RETURN TO PP MODE
         L     13,4(0,13)
         RETURN  (14,12),RC=0
         EJECT
*
*  SUBPROGRAMS CALLED BY ID
*
*  BACKSPACE A FILE ON TAPE
         SPACE 1
BF       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),BFCCW   MOVE IN THE BF CCW
         B     SFEXCP
         SPACE 1
*  FORWARD SPACE A FILE ON TAPE
         SPACE 1
SF       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),SFCCW   MOVE IN SF CCW
SFEXCP   EQU   *
         BAL   11,MULTGET
SFLOOP   EQU   *
         BAL   11,IOOUT            GO EXECUTE THE CCW
         CLI   OUTECB,X'7F'        WAS EVERYTHING OK
         BNE   TAPERR
         BCT   10,SFLOOP           TRY AGAIN
         B     ASKAGN              ALL DONE
MULTGET  EQU   *
         ST    11,SV11             SAVE 11
         WTOR  'DEBE: NR FILES TO BE PROCESSED - XXXX',                +
               TDAREA,4,REPLYECB,ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN              NEW ROUTINE
         OI    TDAREA+3,X'F0'
         PACK  TDWK,TDAREA(4)      PACK NUMBER
         CVB   10,TDWK             MAKE NUMBER BINARY
         L     11,SV11
         BR    11                  RETURN
         EJECT
*  BACKSPACE NNNN RECORDS ON TAPE
         SPACE 1
BS       EQU   *
         SPACE 1
*  SKIP NNNN RECORDS ON TAPE
         SPACE 1
SR       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         WTOR  'DEBE: NR REC TO BE SKIPPED - XXXX',TDAREA,4,REPLYECB,  X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS          GO WAIT FOR A REPLY
         OC    TDAREA(2),BLANKS
         CLC   TDAREA(2),=C'EJ'    END THIS ROUTINE
         BE    ASKAGN              YES, EXIT THIS ROUTINE
         OI    TDAREA+3,X'F0'      MAKE A GOOD SIGN FOR CVB
         PACK  TDWK,TDAREA(4)
         CVB   2,TDWK              PUT NUMBER OF REC'S IN REG 2
         CLC   ID(2),=C'SR'
         BE    SREX
BSEX     MVC   OUTCCW+8(8),BSCCW   SET UP BS CCW
BSAGN    EQU   *
         BAL   11,IOOUT            EXECUTE THE CCW
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    BCT6                YES, DO AGAIN TILL COUNT = 0
         B     TAPERR              NO, END THIS ROUTINE
BCT6     EQU   *
         BCT   2,BSAGN             DECREMENT THE COUNT AND DO AGAIN
         B     ASKAGN              ALL FINISHED WITH THIS ROUTINE
SREX     MVC   OUTCCW+8(8),SRCCW   SET UP SR CCW
         B     BSAGN
         EJECT
*  CARD TO CARD REPRODUCE
         SPACE 1
CC       EQU   *
         MVI   INMODSTK,X'41'      SET UP READER'S DCB
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         MVI   OUTMDSTK,X'42'
         MVI   OUTDEVT,X'43'
         MVC   D1(3),PUNCH1        USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD          STORE UCB ADDRESS IN DEB
         MVC   INCCW(8),CP1CCW     SET UP READ CCW
         MVC   OUTCCW(8),S2CCW
CCLOOP   EQU   *
         BAL   11,IOIN             READ A CARD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    CC1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF
         BC    1,PCHDN
         B     RDRERROR            READER ERROR, END THIS ROUTINE
CC1      EQU   *
         BAL   11,IOOUT            PUNCH A CARD
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    CCLOOP              YES, LOOP
*
PCHERROR WTO   'DEBE: PUNCH ERROR - EOJ',ROUTCDE=(2,3,7)
PCHDN    EQU   *
         MVC   OUTCCW(8),ROCCW
         LA    11,ASKAGN
         B     IOOUT
         EJECT
*  CARD TO PRINTER - 80/80 LIST
         SPACE 1
CP       EQU   *
*  SET UP READER'S DCB AND DEB
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         BAL   11,OPENPRNT         OPEN SYSPRINT IF NOT OPEN
CP2      EQU   *
         BAL   11,SKIPTO1          SKIP TO CHANNEL 1
*  SET UP READER CCW
CPGO     MVC   INCCW(8),CP1CCW     SET UP READ CCW
CPLOOP   EQU   *
         BAL   11,IOIN             READ A CARD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    CP1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF?
         BC    1,TDEOJ
RDRERROR WTO   'DEBE: READER ERROR - EOJ',ROUTCDE=(2,3,7)
         B     ASKAGN
CP1      EQU   *
         MVC   TDOUTAR(80),TDAREA  CLEAR UPPER PRINT LINE AFTER
         MVC   TDOUTAR+80(52),=CL52' '  MOVING DATA TO PRINT LINE
         BAL   11,TDPRINT          PRINT A CARD
         B     CPLOOP              LOOP
         EJECT
*  CARD TO TAPE
         SPACE 1
CT       EQU   *
*  SET UP READER'S DCB AND DEB
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1       USE STANDARD ADDRESS
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         MVC   INCCW(8),CP1CCW     SET UP READ CCW
*  SET UP TAPE'S DCB AND DEB
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),CTCCW   SET UP WRITE CCW
         SR    2,2                 CLEAR A COUNTER
CTLOOP   EQU   *
         BAL   11,IOIN             READ A CARD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    CT1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF
         BC    1,CTEOJ
         B     RDRERROR            READER ERROR, END THIS ROUTINE
CT1      EQU   *
         LA    2,1(2)              INCREMENT A COUNTER
         BAL   11,IOOUT            WRITE TO TAPE
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    CTLOOP              YES, LOOP
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
         B     TAPERR
CTEOJ    EQU   *
         BAL   11,WTM
         B     ASKAGN
         EJECT
* LIST TAPE (DEBLOCKED) ON PRINTER
         SPACE 1
DP       EQU   *
         BAL   11,OPENPRNT         OPEN PRINTER
         BAL   11,SKIPTO1          RESTORE CARRIAGE
         BAL   10,TAPINSET         GET A TAPE
         MVC   INCCW+8(8),TDINCCW
         BAL   11,TDPRINT          PRINT HEADER
         BAL   11,SPACE1
         WTOR  'DEBE: ENT LRECL - XXXX',TDAREA,4,REPLYECB,             X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS    SHIFT TO UPPER CASE
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN
         OI    TDAREA+3,X'F0'      MAKE A BINARY NUMBER
         PACK  TDWK,TDAREA(4)
         CVB   4,TDWK
DPLOOP1  EQU   *
         SR    2,2
         BAL   11,IOIN             READ A TAPE RECORD
         CLI   INECB,X'7F'         OK?
         BE    DP1
         TM    INSTAT+4,X'01'      EOF?
         BC    1,TDEOJ
         B     TAPERROR
DP1      EQU   *
         LH    10,INSTAT+6         RES COUNT
         N     10,=F'65535'        CLEAR HIGH ORDER
         LH    3,TDINCCW+6         REQUESTED
         N     3,=F'65535'         CLEAR HIGH ORDER
         SR    3,10                GET LENGTH
DPLOOP2  EQU   *
         LA    10,TDAREA
         AR    10,2
         MVC   TDOUTAR(132),BLANKS
         LR    5,4
         BCTR  5,0                 DECREMENT GR5
         EX    5,DPMVC             *MVC TDOUTAR(L),0(10)
         BAL   11,TDPRINT          PRINT LINE
         AR    2,4                 UP POINTER
         CR    2,3                 DONE?
         BL    DPLOOP2
         B     DPLOOP1
DPMVC    MVC   TDOUTAR(0),0(10)
         EJECT
* BLOCK CARDIMAGE ON TAPE
         SPACE 1
BT       EQU   *
         MVI   INMODSTK,X'41'      PRIME CONTROL BLOCKS
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD
         MVC   INCCW(8),CP1CCW
         BAL   10,TPOUTSET         GET OUTPUT TAPE
         MVC   OUTCCW+8(8),TTCCWOUT
         WTOR  'DEBE: ENT BLK FACTOR - XXXX',TDAREA,4,REPLYECB,        X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    TDAREA(4),BLANKS
         CLC   TDAREA(2),=C'EJ'
         BE    ASKAGN
         OI    TDAREA+3,X'F0'
         PACK  TDWK,TDAREA(4)
         CVB   2,TDWK              PUT IN GR2
         MH    2,=H'80'
BTLOOP1  EQU   *
         SR    4,4
         LA    3,TDAREA
BTLOOP2  EQU   *
         ST    3,TDWK
         MVC   INCCW+1(3),TDWK+1
         BAL   11,IOIN             READ A CARD
         CLI   INECB,X'7F'         OPERATION OK?
         BE    BT1
         TM    INSTAT+4,X'01'      EOF?
         BC    1,BTEOJ
         B     RDRERROR
BT1      EQU   *
         LA    3,80(3)             MOVE POINTER
         LA    4,80(4)             INCR COUNTER
         CR    2,4
         BNE   BTLOOP2             TRY AGAIN
         STH   4,OUTCCW+14         COUNT FIELD
         BAL   11,IOOUT            WRITE TAPE
         CLI   OUTECB,X'7F'        OK?
         BNE   TAPERR
         B     BTLOOP1             PRAY WE GET OUT OF THIS LOOP!
BTEOJ    EQU   *
         LTR   4,4
         BZ    CTEOJ
         STH   4,OUTCCW+14
         BAL   11,IOOUT
         CLI   OUTECB,X'7F'        OPERATION OK?
         BNE   TAPERR
         B     CTEOJ               WRITE T/M
         EJECT
*  TAPE TO CARD, DEBLOCKING
         SPACE 1
TC       EQU   *
         BAL   10,TAPINSET         SET UP TAPE
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         MVI   OUTDEVT,X'43'
         MVI   OUTMDSTK,X'42'
         MVC   D1(3),PUNCH1
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD
         MVC   OUTCCW(8),DBCCW     PRIME CCW
DBLOOP1  EQU   *
         SR    2,2
         BAL   11,IOIN             READ A RECORD
         CLI   INECB,X'7F'         OK?
         BE    DB1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF?
         BC    1,PCHDN
         B     TAPERROR
DB1      EQU   *
         LH    10,INSTAT+6         GET RES CNT
         N     10,=F'65535'        CLEAR HIGH ORDER
         LH    3,TDINCCW+6         GET START CNT
         N     3,=F'65535'         CLEAR HIGH ORDER
         SR    3,10                GET LENGTH
DBLOOP2  LA    10,TDAREA
         AR    10,2
         MVC   TDAREA2(80),0(10)
         BAL   11,IOOUT            PUNCH CARD
         CLI   OUTECB,X'7F'        OK?
         BNE   PCHERROR            NO, BRANCH
         LA    2,80(2)             MOVE POINTER
         CR    2,3                 DONE?
         BL    DBLOOP2             NO, GO AGAIN
         B     DBLOOP1             NEXT RECORD
         EJECT
*  PRINT A TAPE
         SPACE 1
TP       EQU   *
*  TP USES THE TD ROUTINE WITHOUT THE HEX-TO-CHARACTER CONVERSION
         OI    TD3+1,X'F0'         SET SWITCHES IN TD
         OI    TD6+1,X'F0'
         SPACE 1
*  PRINT A TAPE IN HEX UNLESS SWITCHES SET BY TP
         SPACE 1
TD       EQU   *
         BAL   11,OPENPRNT         OPEN SYSPRINT IF NECESSARY
         BAL   11,SKIPTO1          SKIP TO CHANNEL 1
*  SET UP TAPE'S DCB AND DEB
         BAL   10,TAPINSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         BAL   11,TDPRINT          PRINT THE HEADING LINE
         BAL   11,SPACE1           SPACE A LINE
         SP    RECCNT(3),RECCNT(3)  CLEAR THE RECORD COUNTER
TDLOOP   EQU   *
         BAL   11,IOIN             READ A RECORD
         CLI   INECB,X'7F'         EVERYTHING OK
         BE    TD1                 YES, BRANCH
         TM    INSTAT+4,X'01'      EOF
         BC    1,TDEOJ             YES, GO END ROUTINE
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
         B     TAPERROR
TD1      LH    2,INSTAT+6          GET RESIDUAL COUNT
         N     2,=F'65535'         CLEAR HIGH ORDER
         LH    3,TDINCCW+6         GET BEGINING COUNT
         N     3,=F'65535'         CLEAR HIGH ORDER
         SR    3,2                 GET BYTES READ IN
         CVD   3,TDWK
         MVI   TDOUTAR,C' '        CLEAR PRINT LINE
         MVC   TDOUTAR+1(131),TDOUTAR
         MVC   TDOUTAR+102(23),RECHDG  SET UP LINE INFORMATION
         ED    TDOUTAR+119(6),TDWK+5
         AP    RECCNT(3),COND1(1)  INCREMENT RECORD COUNTER
         ED    TDOUTAR+105(6),RECCNT
         LA    2,TDAREA            GET READY FOR HEX CONVERSION
         LA    5,TDAREA
         LA    7,100
TD6      BC    0,TD7               SET TO BRANCH BY TP ROUTINE
*  R2 POINTS TO THE INPUT AREA
*  R5 POINTS TO THE OUTPUT AREA
         L     5,=A(TDAREA2)
         LA    7,50
         BAL   14,UNPK             GO CONVERT 50 BYTES TO 100 HEX
TD7      EQU   *
         CR    3,7                 SHORT LINE TO BE PRINTED
         BL    TD3                 YES, GO DO THE MOVE
         MVC   TDOUTAR(100),0(5)   ELSE DO 100 BYTE MOVE
TDPR     EQU   *
         BAL   11,TDPRINT          PRINT A LINE
         MVI   TDOUTAR,C' '        CLEAR OUTPUT AREA
         MVC   TDOUTAR+1(131),TDOUTAR
         SR    3,7                 DECREMENT BLKSIZE
         BC    12,TD9              BRANCH IF BLOCK FINISHED
         AR    2,7                 ELSE, INCREMENT COUNTERS
         AR    5,7
         B     TD6                 AND LOOP
TD3      BC    0,TD4               SET TO BRANCH BY TP ROUTINE
         LR    11,3                GET REMAINDER OF DATA IN 11
         AR    11,3                DOUBLE IT BECAUSE IT'S UNPACKED
         BCTR  11,0                DECREMENT ONE FOR THE MOVE
         EX    11,MVCOML
         B     TDPR                GO PRINT SHORT LINE
TD4      EQU   *
         BCTR  3,0
         EX    3,MVCOML
         B     TDPR                GO PRINT SHORT LINE
MVCOML   MVC   TDOUTAR(0),0(5)     COMMON MOVE INSTRUCTION
UNPK     EQU   *                   CONVERT 50 BYTES TO 100 BYTES HEX
         UNPK  0(15,5),0(8,2)
         UNPK  14(15,5),7(8,2)
         UNPK  28(15,5),14(8,2)
         UNPK  42(15,5),21(8,2)
         UNPK  56(15,5),28(8,2)
         UNPK  70(15,5),35(8,2)
         UNPK  84(15,5),42(8,2)
         UNPK  98(3,5),49(2,2)
         TR    0(100,5),TDPTABLE-240
         BR    14
TD9      EQU   *
         BAL   11,SPACE1           SPACE A LINE AFTER WHOLE BLOCKED
         B     TDLOOP
TDEOJ    EQU   *
         BAL   11,SKIPTO1
         B     ASKAGN
         EJECT
*  TAPE TO TAPE COPY
         SPACE 1
TT       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         BAL   10,TAPINSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   INCCW+8(8),TDINCCW  SET UP READ CCW
         BAL   11,MULTGET          GET FACTOR
TTLOOP   EQU   *
         BAL   11,IOIN             READ A BLOCK
         CLI   INECB,X'7F'
         BE    TT1                 EVERYTHING OK, BRANCH
         TM    INSTAT+4,X'01'
         BC    1,TTEST
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
TAPERROR EQU   *
         WTO   'DEBE: INPUT TAPE ERROR - EOJ',ROUTCDE=(2,3,7)
         B     ASKAGN
TT1      LH    2,INSTAT+6          GET RESIDUAL COUNT
         N     2,=F'65535'         CLEAR HIGH ORDER
         LH    3,TDINCCW+6         GET BEGINNING COUNT
         N     3,=F'65535'         CLEAR HIGH ORDER
         SR    3,2                 COMPUTE BYTES READ IN
         STH   3,TTCCWOUT+6        SET UP WRITE CCW
         MVC   OUTCCW+8(8),TTCCWOUT
         BAL   11,IOOUT            WRITE A BLOCK
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BE    TTLOOP              YES, LOOP
         SPACE 1
*  IF OS CAN'T RECOVER FROM THE ERROR, I SURELY WON'T TRY
TAPERR   EQU   *
         WTO   'DEBE: OUTPUT TAPE ERROR - EOJ',ROUTCDE=(2,3,7)
         B     ASKAGN
TTEST    EQU   *
         LA    11,TTLOOP
         BCT   10,WTM
         B     CTEOJ
         EJECT
RU       EQU   *
         BAL   10,TPOUTSET
         MVC   OUTCCW+8(8),RUCCW
         BAL   11,IOOUT
         B     ASKAGN
         SPACE 3
RW       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         MVC   OUTCCW+8(8),RWCCW   SET UP REWIND CCW
         BAL   11,IOOUT            EXECUTE THE REWIND
         B     ASKAGN
         SPACE 3
WT       EQU   *
         BAL   10,TPOUTSET         DETERMINE WHICH TAPE UNIT AND SET UP
         B     CTEOJ               GO WRITE A TAPE MARK
         SPACE 3
TM       EQU   *
         BAL   10,TPOUTSET
         BAL   11,MULTGET
TM1      EQU   *
         BAL   11,WTM
         BCT   10,TM1
         B     ASKAGN
         SPACE 3
KR       EQU   *
         BAL   10,TAPINSET
         MVC   INCCW+8(8),TDINCCW
         B     KNTR
         SPACE 3
KC       EQU   *
         MVI   INMODSTK,X'41'
         MVI   INDEVT,X'43'
         MVC   D1(3),READER1
         BAL   14,UCBLOKUP
         ST    2,INDEBMOD
         MVC   INCCW(8),CP1CCW
         B     KNTR
         EJECT
XP       EQU   *
         WTOR  'DEBE: ENT NEW PUNCH ADDR - XXX',HOLD,3,REPLYECB,       X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    HOLD(3),BLANKS
         CLC   HOLD(2),=C'EJ'
         BE    ASKAGN
         MVC   PUNCH1(3),HOLD
         B     ASKAGN
         SPACE 3
XR       EQU   *
         WTOR  'DEBE: ENT NEW READER ADDR - XXX',HOLD,3,REPLYECB,      X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    HOLD(3),BLANKS
         CLC   HOLD(2),=C'EJ'
         BE    ASKAGN
         MVC   READER1(3),HOLD
         B     ASKAGN
         SPACE 3
XS       EQU   *
         WTOR  'DEBE: ENT NEW PRINT ADDR - XXX',HOLD,3,REPLYECB,       X
               ROUTCDE=(2,3,7)
         BAL   11,WAITANS
         OC    HOLD(3),BLANKS
         CLC   HOLD(2),=C'EJ'
         BE    ASKAGN
         MVC   PRINT1(3),HOLD
         B     ASKAGN
         EJECT
*
*  SUBROUTINES USED BY THE SUBPROGRAMS CALLED BY ID
*
*  OPEN PRINT FILE
         SPACE 1
OPENPRNT EQU   *
         MVI   OUTMDSTK,X'09'      PRIME DCB
         MVI   OUTDEVT,X'48'
         MVC   D1(3),PRINT1        PRINTER ADDRESS
         BAL   14,UCBLOKUP
         ST    2,OUTDEBMD          STORE IN DEB
         BR    11
         SPACE 3
*  COMMON PRINT ROUTINE
         SPACE 1
TDPRINT  EQU   *
         ST    11,SV11
         MVC   OUTCCW(8),PTCCW
         BAL   11,IOOUT            PRINT LINE
         CLI   OUTECB,X'7F'        PRINT OK?
         BNE   PTERROR             NO, BRANCH
         LH    1,C                 UPDATE LINE COUNTER
         LA    0,1(1)
         STH   0,C
         CH    0,CLIMIT            END OF PAGE?
         LA    11,*+8
         BH    SKIPTO1
         L     11,SV11
         SPACE 1
*  SKIP PRINTER TO CHANNEL 1
         SPACE 1
         BR    11
SKIPTO1  EQU   *
         ST    11,SAV11            SAVE RETURN ADCON
         MVC   OUTCCW(8),SKCCW
         BAL   11,IOOUT            SKIP TO CHANNEL 1
         CLI   OUTECB,X'7F'        SKIP OK?
         BNE   PTERROR             NO, BRANCH
         SR    0,0                 CLEAR COUNTER
         STH   0,C
         L     11,SAV11
         BR    11                  RETURN
         SPACE 1
*  SPACE PRINTER ONE LINE
         SPACE 1
SPACE1   EQU   *
         ST    11,SV11
         MVC   OUTCCW(8),SPCCW
         BAL   11,IOOUT            SKIP LINE
         CLI   OUTECB,X'7F'        SKIP OK?
         BNE   PTERROR
         LH    1,C                 UPDATE LINE COUNT
         LA    0,1(1)
         STH   0,C
         CH    0,CLIMIT            END OF PAGE
         LA    11,*+8
         BH    SKIPTO1
         L     11,SV11
         BR    11                  EXIT ROUTINE
PTERROR  EQU   *
         WTO   'DEBE: PRINT ERROR - EOJ',ROUTCDE=(2,3,7)
         B     ASKAGN
         SPACE 3
*  RECORD COUNTER
         SPACE 1
KNTR     EQU   *
         SR    2,2
KNTLOOP  EQU   *
         BAL   11,IOIN
         CLI   INECB,X'7F'
         BE    KNT1
         TM    INSTAT+4,X'01'
         BC    1,KNTDN
         CLC   ID(2),=C'KC'
         BE    RDRERROR
         B     TAPERROR
KNT1     EQU   *
         LA    2,1(2)
         B     KNTLOOP
KNTDN    EQU   *
         MVC   COUNT(6),=X'402020202020'
         CVD   2,TDWK
         ED    COUNT(6),TDWK+5
         LA    1,MESSAGE
         SVC   35
         B     ASKAGN
         EJECT
*  ROUTINE TO CONVERT MM INTO A SET MODE COMMAND
         SPACE 1
CONVRTMM EQU   *
         STM   2,3,SAVEUM          SAVE WORK REGISTERS
         IC    2,MM+1              GET SECOND BYTE OF MM
         TM    MM+1,X'F0'          IS IT NUMERIC
         BC    1,MMMM1             YES, BRANCH
         AH    2,=H'9'             CONVERT ALPHA TO NUMERIC
MMMM1    SRDL  2,4                 SAVE IT
         IC    2,MM                GET FIRST BYTE OF MM
         TM    MM,X'F0'            IS IT NUMERIC
         BC    1,MMMM2             YES, BRANCH
         AH    2,=H'9'             CONVERT ALPHA TO NUMERIC
MMMM2    SLDL  2,4
         STC   2,MM                PUT MM  BACK IN HEX
         LM    2,3,SAVEUM          RESTORE REGISTERS
         BR    14
SAVEUM   DS    2F
         SPACE 3
*  ROUTINE TO GET UCB ADDRESS WHEN GIVEN DEVICE ADDRESS
         SPACE 1
UCBLOKUP EQU   *
         ST    11,SAV11
UCBLOK   EQU   *
         MVC   BDUN(3),D1
         MVC   UNNUM(3),D1
         L     2,16                GET CVT ADDRESS
         L     3,40(2)             GET LUT
         BCTR  3,0                 DECREMENT
         BCTR  3,0                 DECREMENT
NEXTUCB  LA    3,2(3)              GET NEXT POINTER IN LUT
         SR    2,2                 CLEAR FOR ICM
         ICM   2,3,0(3)            GET NEXT UCB POINTER
         BZ    NEXTUCB             INVALID, GET NEXT
         CH    2,HALFX             END OF THE LUT
         BE    BADEVICE            YES, NOT FOUND
         CLC   13(3,2),D1          RIGHT UCB?
         BNE   NEXTUCB             NO, GET NEXT
         TM    3(2),X'08'          ALLOCATED?
         BC    1,ALOCK             YES; BRANCH
RETRN    EQU   *
         L     11,SAV11
         BR    14
         SPACE 3
*  ROUTINE TO ASK FOR THE INPUT TAPE
         SPACE 1
TAPINSET EQU   *
         WTOR  'DEBE: INPUT TAPE - MMXXX',MM,5,REPLYECB,ROUTCDE=(2,3,7)
         BAL   11,WAITAPE          WAIT FOR A REPLY AND SET UP MODE
         MVC   INTRTCH(1),MM       SET UP TAPE'S DCB
         MVI   INDEVT,X'81'
         CLI   18(2),X'80'         IS IT A TAPE (2 POINTS TO UCB)
         BNE   TPINBD
         ST    2,INDEBMOD          STORE UCB ADDRESS IN DEB
         MVC   INDEBMOD(1),MM      PUT SET MODE CMD IN DEB
         OI    INIOB,X'40'         SET ON CHAINING
         MVC   INCCW(1),MM         PUT SET MODE IN CCW
         MVC   INCCW+1(7),=X'00000060000001'
         BR    10
TPINBD   EQU   *
         WTO   'DEBE: NOT A TAPE',ROUTCDE=(2,3,7)
         B     TAPINSET
         SPACE 3
*  ROUTINE TO ASK FOR THE OUTPUT TAPE
         SPACE 1
TPOUTSET EQU   *
         WTOR  'DEBE: OUTPUT TAPE - MMXXX',MM,5,REPLYECB,              X
               ROUTCDE=(2,3,7)
         BAL   11,WAITAPE          WAIT FOR REPLY  AND SET UP MM
         MVC   OUTTRTCH(1),MM
         MVI   OUTDEVT,X'81'
         CLI   18(2),X'80'         IS IT A TAPE
         BNE   TPOUTBD
         ST    2,OUTDEBMD          STORE UCB ADDRESS IN DEB
         MVC   OUTDEBMD(1),MM
         OI    OUTIOB,X'40'        SET ON CHAINING FLAG
         MVC   OUTCCW(1),MM        BUILD SET MODE COMMAND
         MVC   OUTCCW+1(7),=X'00000060000001'
         BR    10
TPOUTBD  EQU   *
         WTO   'DEBE: NOT A TAPE',ROUTCDE=(2,3,7)
         B     TPOUTSET
         SPACE 3
*  COMMON I/O ROUTINE FOR OUTPUT
         SPACE 1
IOOUT    EQU   *
         XC    OUTECB(4),OUTECB
         EXCP  OUTIOB
         WAIT  ECB=OUTECB
         NI    OUTDCB+44,X'3F'
         TM    COMMECB,X'40'       HAS COMMUNICATIONS ECB
*                                      BEEN POSTED YET?
         BCR   12,11               IF NOT, RETURN ON 11
         B     CHECKREP            ELSE GO CHECK REPLY
         SPACE 3
*  COMMON I/O ROUTINE FOR INPUT
         SPACE 1
IOIN     EQU   *
         XC    INECB(4),INECB
         EXCP  INIOB
         WAIT  ECB=INECB
         NI    INDCB+44,X'3F'
         TM    COMMECB,X'40'       IS COMM ECB POSTED?
         BCR   12,11               IF NOT, CONTINUE
         B     CHECKREP            ELSE, GO CHECK REPLY
         SPACE 3
WAITANS  EQU   *
         WAITR ECB=REPLYECB
         XC    REPLYECB(4),REPLYECB
         BR    11
         SPACE 3
*  ROUTINE TO WAIT ON THE REPLY AND CONVERT THE MM INTO A SET MODE
WAITAPE  EQU   *
         MVC   MM(5),BLANKS        CLEAR REPLY AREA
         WAIT  ECB=REPLYECB
         XC    REPLYECB(4),REPLYECB
         OC    MM(5),=C'     '     MAKE ALL CAPS
         CLC   MM(2),=C'EJ'
         BE    ASKAGN
         SR    15,15               CLEAR FOR COUNT
         LA    1,MM                SET REPLY POINTER
CHKRPLY  CLI   0(1),C' '           VALID CHARACTER
         BE    ENDRPLY             NO, CHECK NUMBER OBTAINED
         LA    15,1(15)            BUMP COUNT
         LA    1,1(1)              BUMP TO NEXT CHAR
         B     CHKRPLY             CHECK NEXT CHAR
ENDRPLY  CH    15,=H'5'            ALL FIVE REPLIED?
         BE    INITMM              YES, INITIALIZE MM
         CH    15,=H'3'            ONLY DEVICE GIVEN?
         BE    MMADJ               YES, DEFAULT TO NO MODE
         WTOR  'DEBE: INVALID REPLY, TRY AGAIN',MM,5,REPLYECB,         X
               ROUTCDE=(2,3,7)
         B     WAITAPE
MMADJ    MVC   REPLYECB(3),MM      MOVE TO WORK AREA
         MVC   MM+2(3),REPLYECB    BACK TO REPLY AREA
         MVC   MM(2),=C'00'        DEFAULT TO NO MODESET
         XC    REPLYECB(4),REPLYECB
INITMM   MVC   TDOUTAR(5),MM       GET MMXXX FOR PRINTING ON LISTING
         MVI   TDOUTAR+5,C' '
         MVC   TDOUTAR+6(126),TDOUTAR+5
         MVC   TDOUTAR+5(24),=CL24' - MMXXX FOR INPUT TAPE.'
         MVC   TDOUTAR+50(47),=CL47'*** THIS IS NOT A DATA RECORD FROM X
               THE TAPE ***'
         BAL   14,CONVRTMM
         OI    MM,X'03'            SET MODE COMMAND NOW IN MM
         MVC   D1(3),XXX
         BAL   14,UCBLOKUP
         BR    11
         SPACE 3
*  ROUTINE TO WRITE A TAPEMARK
WTM      EQU   *
         ST    11,SV11
         MVC   OUTCCW+8(8),WTCCW   SET UP WTM CCW
         BAL   11,IOOUT            WRITE A TAPE MARK
         CLI   OUTECB,X'7F'        EVERYTHING OK
         BNE   TAPERR              NO, BRANCH
         L     11,SV11
         BR    11
         SPACE 3
*  UNIT ALLOCATED ROUTINE
ALOCK    EQU   *
         LA    1,WTORMSG
         SVC   35                  ISSUE WTOR
         BAL   11,WAITANS
         OC    HOLD(3),BLANKS
         CLI   HOLD,C'U'
         BE    RETRN
         CLC   HOLD(2),=C'EJ'      NEW TASK?
         BE    ASKAGN
         MVC   D1(3),HOLD
         B     UCBLOK
         SPACE  3
*  NO UCB FOUND
BADEVICE EQU   *
         LA    1,BDWTOR
         SVC   35                  ISSUE WTOR
         BAL   11,WAITANS
         OC    D1(3),BLANKS
         CLC   D1(2),=C'EJ'
         BE    ASKAGN
         B     UCBLOK              TRY AGAIN
         SPACE 1
APPEND   BR    14
         EJECT
*  SEE THE OS SYSTEM CONTROL BLOCKS MANUAL FOR THE FORMATS OF THE
*  FOLLOWING DCB'S, DEB'S, ECB'S, AND IOB'S.
*  DCB FOR INPUT DATA SET
         SPACE 1                       Î
INDCB    DS    0F
         DC    4F'0'
INTRTCH  EQU   *
INMODSTK EQU   *
         DC    X'00'
INDEVT   DC    X'00'
INDENS   DC    X'00'
         DC    X'00'
         DC    5F'0'
         DC    H'0'
         DC    BL2'1101000000001000'
         DC    A(INDEB)
         DC    X'10000000'
         DC    5F'0'
         SPACE 1                       Î
*  DEB FOR INPUT DATA SET
         SPACE 1                       Î
INIOVEC  EQU   *
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    3F'0'
         DC    X'06000000'
INDEB    DS    0F
         DC    F'0'
         DC    X'04'
         DC    AL3(OUTDEB)
         DC    X'C0000000'
         DC    X'30000000'
         DC    2F'0'
         DC    X'0F'
         DC    AL3(INDCB)
         DC    X'02'
         DC    AL3(INIOVEC)
INDEBMOD DC    X'00'
INDEBUCB DC    X'000000'
         DC    F'0'
         SPACE 1                       9
*  ECB FOR INPUT DATA SET
         SPACE 1                       9
INECB    DC    F'0'
         SPACE 1
*  IOB FOR INPUT DATA SET
         SPACE 1
INIOB    DS    0F
         DC    X'0200'
INSENS   DC    H'0'
         DC    X'7F'
         DC    AL3(INECB)
INSTAT   DC    2F'0'
         DC    A(INCCW)
         DC    A(INDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
INDASD   DC    X'00'
INSEEK   DC    XL7'00'
         SPACE 1
*  CCW'S FOR INPUT DATA SET
         SPACE 1
INCCW    DS    5D
         SPACE 3
*  DCB FOR OUTPUT DATA SET
         SPACE 1
OUTDCB   DS    0F
         DC    4F'0'
OUTTRTCH EQU   *
OUTMDSTK EQU   *
         DC    X'00'
OUTDEVT  DC    X'00'
OUTDENS  DC    X'00'
         DC    X'00'
         DC    5F'0'
         DC    H'0'
         DC    BL2'1101000000001000'
         DC    A(OUTDEB)
         DC    X'10000000'
         DC    5F'0'
         SPACE 1
*  DEB FOR OUTPUT DATA SET
         SPACE 1
OUTIOVEC EQU   *
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    A(APPEND)
         DC    3F'0'
         DC    X'06000000'
OUTDEB   DS    0F
         DC    F'0'
         DC    X'17000000'
         DC    X'C0000000'
         DC    X'30000000'
         DC    2F'0'
         DC    X'0F'
         DC    AL3(OUTDCB)
         DC    X'02'
         DC    AL3(OUTIOVEC)
OUTDEBMD DC    X'00'
OUTDBUCB DC    X'000000'
         DC    F'0'
         SPACE 1
*  ECB FOR OUTPUT DATA SET
         SPACE 1
OUTECB   DC    F'0'
         SPACE 1
*  IOB FOR OUTPUT DATA SET
         SPACE 1
OUTIOB   DS    0F
         DC    XL2'0200'
OUTSENS  DC    H'0'
         DC    X'7F'
         DC    AL3(OUTECB)
OUTSTAT  DC    2F'0'
         DC    A(OUTCCW)
         DC    A(OUTDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
OUTDASD  DC    X'00'
OUTSEEK  DC    XL7'00'
         SPACE 1
*  CCW'S FOR OUTPUT DATA SET
         SPACE 1
OUTCCW   DS    5D
         SPACE 3
*  CONSTANTS AND CCW'S      * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
HOLD     DS    D
READER1  DC    CL3'00C'
PUNCH1   DC    CL3'00D'
PRINT1   DC    CL3'00E'
         SPACE 3
BFCCW    CCW   X'2F',SFCCW,X'20',1
BSCCW    CCW   X'27',BSCCW,X'20',1
CP1CCW   CCW   2,TDAREA,X'20',80
CP2CCW   CCW   9,TDAREA,X'20',80
CTCCW    CCW   1,TDAREA,X'20',80
DBCCW    CCW   X'41',TDAREA2,X'20',80
PTCCW    CCW   9,TDOUTAR,X'20',132
ROCCW    CCW   X'41',BLANKS,X'20',80
RUCCW    CCW   15,RUCCW,X'20',1
RWCCW    CCW   7,RWCCW,X'20',1
SFCCW    CCW   X'3F',SFCCW,X'20',1
SKCCW    CCW   X'8B',BLANKS,X'20',1
SPCCW    CCW   11,BLANKS,X'20',1
SRCCW    CCW   X'37',BSCCW,X'20',1
S2CCW    CCW   X'41',TDAREA,X'20',80
TDINCCW  CCW   2,TDAREA,X'20',32768
TTCCWOUT CCW   1,TDAREA,X'20',32768
WTCCW    CCW   31,TDAREA,X'20',1
         SPACE 3
OPENSW   DC    X'00'
BLANKS   DC    132C' '
         SPACE 1
TDWK     DS    D
SAVEAREA DC    18F'0'
TDPTABLE DC    C'0123456789ABCDEF'
         DS    0F
MESSAGE  DC    AL2(THERE-*)        MESSAGE LENGTH
         DC    AL2(0)
         DC    C'DEBE: '
COUNT    DC    C'NUMBER '
         DC    C'RECORDS PROCESSED'
THERE    EQU   *
WTORMSG  DS    0F
         DC    AL1(3)
         DC    AL3(HOLD)
         DC    A(REPLYECB)
         DC    AL2(28)
         DC    AL2(0)
         DC    C'DEBE: UNIT '
UNNUM    DS    CL3
         DC    C' ALLOCATED'
BDWTOR   DS    0F
         DC    AL1(3)
         DC    AL3(D1)
         DC    A(REPLYECB)
         DC    AL2(26)
         DC    AL2(0)
         DC    C'DEBE: UNIT '
BDUN     DC    C'XXX INVALID'
D1       DC    C'0'
D2       DC    C'0'
D3       DC    C'0'
ID       DC    C'00'
MM       DC    C'00'
XXX      DC    C'000'
XXXPAD   DC    C' '
OF       DC    F'15'
COMMECB  DC    2F'0'
COMM     EQU   COMMECB+4
REPLYECB DC    F'0'
TDA      DS    CL1
SAV11    DS    F
TDOUTAR  DS    CL132
RECCNT   DC    X'00000F'
COND1    DC    X'1F'
RECHDG   DC    C'REC '
         DC    X'2020202020'
         DC    C', LENGTH '
         DC    X'2020202020'
C        DC    H'0'
CLIMIT   DC    H'58'
HALFX    DC    X'FFFF'             HALFWORD MASK
SV11     DS    F
         SPACE 3
         LTORG
         SPACE 3
TDAREA2  DS    CL101
TDAREA   DS    CL32768
         END
