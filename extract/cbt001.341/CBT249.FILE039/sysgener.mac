*
*        THIS ROUTINE IS NEARLY THE EQUIVELENT OF 'IEBGENER'
*        WITH THE FOLLOWING EXCEPTIONS.
*
*        SYSPRINT AND SYSIN ARE NOT REQUIRED
*        EXITS AND SPECIAL PROCESSING IS NOT ALLOWED
*        SYSUT2 DCB INFORMATION IS NOT REQUIRED
*        SYSUT2 DEFAULT IS 80-6080 (BUT ONLY FOR NEW DATASETS
*               OR NON-LABELED TAPES)
*
*        PARM INFORMATION IS OPTIONAL.  IF A PARM OF 'COPY' IS
*        USED, THEN INPUT DCB INFORMATION IS 'COPIED' TO THE
*        OUTPUT DCB.
*
SYSGENER $PROLOG
         PRINT NOGEN
         L     R2,0(R1)    GET PARM POINTER
         BAL   R14,IN      GET FIRST RECORD (OPEN FILE)
         LH    R3,0(R2)    GET PARM SIZE
         LTR   R3,R3       ANY PARM?
         BZ    BEGIN       NO, CONTINUE
         CH    R3,=H'4'    VALID SIZE?
         BNE   ERROR       NO, INFORM USER
         OC    2(4,R2),=CL4' ' MAKE UPPER CASE
         CLC   2(4,R2),=C'COPY' VALID PARM?
         BNE   ERROR       NO, INFORM USER
         MVC   OUTDCB+36(1),OUTDCB+36 RECFM
         MVC   OUTDCB+82(2),OUTDCB+82 LRECL
         MVC   OUTDCB+62(2),OUTDCB+62 BLKSIZE
         B     BEGIN       CONTINUE
ERROR    ABEND 0           ABEND (SAFEST WAY)
BACK     BAL   R14,IN      GET INPUT RECORD
BEGIN    BAL   R14,OUT     WRITE RECORD
         B     BACK        GET NEXT RECORD
EODAD    CLOSE (OUTDCB)    CLOSE IN CASE WE WERE NOT ATTACHED
         SR    R15,R15     CLEAR RETCODE
         $EPILOG ,         RETURN TO CALLER
IN       SYSGET DDNAME=SYSUT1
OUT      SYSPUT DDNAME=SYSUT2,BLKSIZE=6080
         END
