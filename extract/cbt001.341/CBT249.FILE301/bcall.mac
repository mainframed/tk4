         TITLE 'BCALL TSO COMMAND PROCESSOR'
**********************************************************************
* FUNCTION    -  INVOKE A LINKLIST PROGRAM                           *
*                                                                    *
* AUTHOR      -  C. LYONS                                            *
*                                                                    *
* DATE        -  2/01/80                                             *
*                                                                    *
* VERSION     -  1.0 FOR MVS 3.8 AND MVS/SE2                         *
*                                                                    *
* INPUT       -  R1->CPPL                                            *
*                                                                    *
* OUTPUT      -  NONE                                                *
*                                                                    *
* ATTRIB      -  RE-ENTRANT                                          *
*                                                                    *
* REGS        -  R12 - BASE                                          *
*                R13 - SA                                            *
*                R4  - WORK AREA                                     *
*                                                                    *
* LOGIC       -                                                      *
*                1. ENTRANCE CONVENTIONS                             *
*                2. PARSE OPERAND                                    *
*                3. TRY TO LOCATE DESIRED MODULE                     *
*                4. GET AUTHORIZATION VIA SVC248                     *
*                5. LINK TO DESIRED MODULE WITH NULL PARMS           *
*                6. RELEASE AUTHORIZATION VIA SVC248                 *
*                7. EXIT CONVENTIONS                                 *
*                                                                    *
*  MACROS     -  FULLSAVE,REGS,GETMAIN,LINK,TPUT,IKJRLSA,FREEMAIN    *
*                IKJPARM,IKJIDENT,IKJENDP                            *
*                                                                    *
*  DSECTS     -  IKJCPPL,IEFTIOT1,IKJTCB,IHAPSA,IKJPSCB,WORK,PDL     *
*                                                                    *
*  ERRORS     -  PARSE ERROR; MODULE NOT FOUND; BAD CPPL             *
*                                                                    *
*  PRIVILEGES -  PROBLEM PROGRAM                                     *
*                                                                    *
*  INSTALL    -  ASSEMBLE AND LINK AUTHSVC INTO PLPA                 *
*                CHANGE SVC # ON SETAUTH MACRO                       *
*                ASSEMBLE BCALL                                      *
*                LINK BCALL RENT REUS REFR                           *
*                PLACE BCALL IN LINKLIST LIBRARY OR PLPA             *
**********************************************************************
         SPACE 3
         MACRO
&LABEL   SETAUTH &DIR
         AIF   ('&DIR' EQ 'ON').ON
&LABEL   SR    R0,R0
         AGO   .SVC
.ON      ANOP
&LABEL   LA    R0,1
.SVC     SVC   248
         MEND
         SPACE 3
BCALL    CSECT
         REGS  R
         FULLSAVE RENT=YES
         LR    R3,R1                   SAVE CPPL ->
         USING CPPL,R3
         USING PSA,R0
         L     R1,PSATOLD              MY TCB
         DROP  R0
         USING TCB,R1
         L     R1,TCBTIO               TIOT
         USING TIOT,R1
         L     R2,CPPLPSCB             PSCB
         USING PSCB,R2
         CLC   PSCBUSER,TIOCNJOB       VERIFY VALID CPPL
         BNE   BADCPPL
         DROP  R1,R2
         GETMAIN R,LV=WORKLEN          GET WORK AREA
         LR    R4,R1
         USING WORK,R4
         XC    WORK(WORKLEN),WORK      CLEAR WORK AREA
         LA    R5,PARSPARM
         USING PPL,R5
*
*  SETUP PARSE PARAMETER LIST FOR IKJPARS
*
         MVC   PPLUPT,CPPLUPT           ->UPT
         MVC   PPLECT,CPPLECT           ->ECT
         MVC   PPLCBUF,CPPLCBUF         ->CIB
         DROP  R3
         LA    R1,ECB
         ST    R1,PPLECB                ->ECB
         L     R1,=A(PCL)
         ST    R1,PPLPCL                ->PCL
         LA    R1,ANSWER
         ST    R1,PPLANS                ->PDL
*
*  PARSE OPERAND ON BCALL COMMAND
*
         CALLTSSR EP=IKJPARS,MF=(E,(R5))
         DROP  R5
         L     R3,ANSWER                GET -> PDL
         LTR   R3,R3
         BM    BADPARSE                 EXIT IF PARSE UNSUCCESSFUL
         USING PDL,R3
         MVC   BLDL(BLDLEN),BLDLIST     MOVE BLDL PROTOTYPE
         L     R1,MODPTR                GET MODULE NAME ->
         LH    R2,MODLEN                GET L'MODULE NAME
         DROP  R3
         BCTR  R2,0
         EX    R2,*+4                   MODULE NAME TO BLDL LIST
         MVC   EPNAME(*-*),0(R1)
         LA    R2,BLDL                  ->BLDL LIST
*
* TRY TO LOCATE DESIRED MODULE
*
         BLDL  0,(R2)
         BXH   R15,R15,BADFIND          ERROR IF UNSUCCESSFUL
         LA    R2,EPNAME
         LA    R3,NULLPARM              SETUP PARMS FOR LINK
         ST    R3,PARMPTR
         MVI   PARMPTR,X'80'
*
* LINK TO DESIRED MODULE WITH NULL PARMS
*
         SETAUTH ON                     GET AUTH (IN CASE IEBCOPY)
         LINK  DE=(R2),MF=(E,PARMPTR),SF=(E,LINKPARM)
         SETAUTH OFF                    RELEASE AUTH
         B     EXIT
BADCPPL  TPUT  CPPLERR,60
         B     EXIT2
BADPARSE TPUT  PARSERR,60
         B     EXIT1
BADFIND  TPUT  FINDERR,60
EXIT     IKJRLSA  ANSWER                FREE PARSE STORAGE
EXIT1    FREEMAIN R,LV=WORKLEN,A=(R4)   FREE WORK AREA
EXIT2    FULLRTRN RENT=YES
*
*  PARSE PARAMETER CONTROL LIST
*
PCL      IKJPARM DSECT=PDL
MODULE   IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ALPHA,                 X
               OTHER=ALPHANUM,PROMPT='MODULE NAME'
         IKJENDP
*
*  CONSTANTS
*
BLDLIST  DC    H'1'                     BLDL PROTOTYPE
         DC    H'60'                    ENTRY LENGTH
         DC    CL8' '                   NAME
         DC    CL52' '                  ENTRY
BLDLEN   EQU   *-BLDLIST
PARSERR  DC    CL60'TSDTSO01-I-BCALL TERMINATED, PARSE ERROR'
FINDERR  DC    CL60'TSDTSO02-I-BCALL TERMINATED, MODULE NOT FOUND'
CPPLERR  DC    CL60'TSDTSO03-I-BCALL TERMINATED, BAD CPPL'
         LTORG
*
* DSECTS
*
        IKJCPPL                         CPPL
        CVT PREFIX=NO,DSECT=YES         CVT
        IKJPPL                          PPL
TIOT    DSECT
        IEFTIOT1                        TIOT
        IKJTCB                          TCB
        IHAPSA                          PSA
        IKJPSCB                         PSCB
*
WORK      DSECT                         WORK AREA
LINKPARM  DS   A                        LINK SF
PARSPARM  DS   7A                       PPL
ECB       DS   F                        ECB
ANSWER    DS   F                        PDL->
PARMPTR   DS   F                        PARM->
NULLPARM  DS   H                        H'0'
BLDL      DS   32H                      BLDL PARMS
EPNAME    EQU  BLDL+4                   NAME PART OF BLDL
WORKLEN   EQU  *-WORK                   WORK AREA SIZE
*
PDL       DSECT                         PDL CONTINUATION
          ORG  MODULE
MODPTR    DS   F                        ->MODULE NAME
MODLEN    DS   H                        L'MODULE NAME
          ORG
          END
