         TITLE 'DUSER COMMAND - DOCUMENTATION'
********************************************************************
*  NAME       -   DUSER                                            *
*                                                                  *
*  AUTHOR     -   C. LYONS                                         *
*                                                                  *
*  FUNCTION   -   DISPLAY TSO USERS                                *
*                                                                  *
*  INPUT      -   NONE                                             *
*                                                                  *
*  OUTPUT     -   USERID ACCOUNT PROCNAME LOGTIME  AUTH  LINE TERM *
*                 $XYZ   1234    IKJACCNT 14:20:12 O A S 041  R#7  *
*                                                                  *
*  ERRORS     -   IMMEDIATE EXIT IF TSDCVT NOT PRESENT             *
*                                                                  *
*  SYSTEM     -   MVS 3.8 AND MVS/SE2                              *
*                                                                  *
*  EXITS      -   NONE                                             *
*                                                                  *
*  DATE       -   07/28/80                                         *
*                                                                  *
*  SMP FMID   -   EBB1102 (BASE CONTROL PROGRAM)                   *
*                                                                  *
*  TARGET LIB -   TSD.LINKLIB                                      *
*                                                                  *
*  MACROS     -   FULLSAVE,GETMAIN,TPUT,ENQ,DEQ,FREEMAIN,FULLRTRN  *
*                                                                  *
*  DSECTS     -   CVT,TSDASVT,TSDASCB,WORK,IKJPSCB,IKJTCB,IHAPSA   *
*                 IEZJSCB                                          *
*                                                                  *
*  REGISTERS  -   R12 = BASE; R3 -> WORK AREA                      *
*                                                                  *
*  LANGUAGE   -   OS/VS LEVEL ASSEMBLER                            *
*                                                                  *
*  PRIVILEGES -   PROBLEM PROGRAM                                  *
*                                                                  *
*  ATTRIBUTES -   RENT,REFR                                        *
*                                                                  *
*  LOGIC      -   1. ENTRANCE CONVENTIONS                          *
*                 2. SERIALIZE TSDASVT                             *
*                 3. EXTRACT INFORMAION FROM TSDASCB FOR EACH USER *
*                 4. TPUT INFORMATION APPROPRIATE WITH CALLERS     *
*                    AUTHORITY                                     *
*                 5. RELEASE TSDASVT                               *
*                 6. EXIT                                          *
*                                                                  *
*  NOTES      -   DUSER DEPENDS ON THE TSDASCB WHICH IS            *
*                 CONSTRUCTED BY A FRONT-END TO IKJEFT01,          *
*                 IBM'S TMP.  THE TSDASCB IS POINTED TO            *
*                 FROM THE TSDASVT WHICH IS PART OF THE            *
*                 TSDCVT BUILT BY AN IEFU83 OR IEFU84 SMF EXIT.    *
*                                                                  *
*                 CVT                                              *
*                 CVTUSER -> TSDCVT                                *
*                            TSDASVT                               *
*                               --->   TSDASCB FOR USER X          *
*                               --->   TSDASCB FOR USER Y          *
*                                                                  *
*                                                                  *
*  INSTALL    -  INSTALL TMP FRONT-END (IN MEMBER STEPLIB)         *
*                CHANGE MAXUSER CONSTANT - MUST ALSO BE CHANGED    *
*                                          IN TMP FRONT-END AND    *
*                                          MUST BE CONSISTENT WITH *
*                                          IEFU84                  *
*                ASSEMBLE DUSER                                    *
*                LINK DUSER RENT REUS REFR                         *
*                PLACE DUSER INTO LINKLIST LIBRARY OR PLPA         *
*                                                                  *
********************************************************************
         TITLE 'TSO DUSER COMMAND'
DUSER    CSECT
MAXUSER  EQU   100                 MAXIMUM ADDRESS SPACES
         REGS  R                   REGISTER EQUATES
         FULLSAVE RENT=YES         ENTRANCE CONVENTIONS
         GETMAIN R,LV=WORKLEN
         LR    R3,R1               SAVE WORK AREA ADDRESS
         USING WORK,R3
         L     R1,CVTPTR           CVT
         USING CVT,R1
         L     R4,CVTUSER          IF TSDCVT NOT PRESENT, EXIT
         DROP  R1
         LTR   R4,R4
         BNZ   SETAUTH
         TPUT  CVTERROR,40
         B     EXIT
SETAUTH  MVI   AUTHFLG,X'00'       INITIALIZE AUTHORITY FLAG
         USING PSA,R0
         L     R1,PSATOLD          TCB
         USING TCB,R1
         L     R1,TCBJSCB          JSCB
         USING IEZJSCB,R1
         L     R1,JSCBPSCB
         USING PSCB,R1             PSCB
         MVC   UID,PSCBUSER        SAVE CALLER'S USERID
         TM    PSCBATR1,PSCBCTRL   IF CALLER DOES NOT HAVE
         BZ    REGUSER                OPER PRIVILEGES, THEN
         MVI   AUTHFLG,X'FF'          DON'T SHOW ACCOUNT #
*                                     OR OTHER USER'S AUTHORITY
         DROP  R0,R1
REGUSER  TPUT  HEADING,74          SEND HEADING
         USING TSDCVT,R4
         LA    R5,MAXUSER          MAXIMUM ADDRESS SPACES
         SR    R6,R6               TSO USER COUNT
         ENQ   MF=(E,ENQPARM)      SERIALIZE TSDASVT
         LA    R7,TSDASVT          TSDASVT
         DROP  R4
ASCBLOOP MVI   TERMLINE,C' '
         MVC   TERMLINE+1(L'TERMLINE-1),TERMLINE
         L     R8,0(0,R7)          TSDASCB
         LTR   R8,R8
         BZ    NEXTASCB
         USING TSDASCB,R8
         CLC   ASCBNAME,CBNAME     VERIFY TSDASCB
         BNE   NEXTASCB
         MVC   USER,USERID         USERID
         MVC   PROC,PROCNAME       LOGON PROCEDURE
         MVC   TERM,TERMNAME       TERMINAL NAME
         MVC   LOGT-1(9),EDMSK
         ED    LOGT-1(9),LOGTIME   SAVE LOGON TIME
         CLI   AUTHFLG,X'FF'       IF AUTHORIZED CALLER,
         BE    MOVEACCT               SHOW ACCOUNT NUMBER
         XR    R1,R1
         TRT   USER,TRANTAB
         LA    R15,USER            GET USERID LENGTH
         SR    R1,R15
         SR    R15,R15
         IC    R15,UID+7           IF NON-AUTHORIZED CALLER,
         CR    R1,R15                 ONLY SHOW ACCOUNT NUMBER,
         BNE   GETLINE                & AUTHORITY FOR HIS ID.
         BCTR  R1,0
         EX    R1,COMPUID
         BNE   GETLINE
MOVEACCT MVC   ACCT,ACCOUNT        ACCOUNT NUMBER
         LA    R15,AUTH
         MVC   AUTH,BLANKS
         TM    USERAUTH,X'80'
         BZ    NOTOPER             DETERMINE USER'S AUTHORITY
         MVI   0(R15),C'O'
         LA    R15,2(0,R15)          X'80' = OPER
NOTOPER  TM    USERAUTH,X'40'
         BZ    NOTACCT               X'40' = ACCOUNT
         MVI   0(R15),C'A'
         LA    R15,2(0,R15)          X'20' = SUBMIT
NOTACCT  TM    USERAUTH,X'20'
         BZ    GETLINE
         MVI   0(R15),C'S'
GETLINE  MVC   DBWORD+5(2),LINEADDR  TRANSLATE LINE ADDRESS
         UNPK  DBWORD(4),DBWORD+5(3)
         TR    DBWORD(3),TRTAB-C'0'
         MVC   LINEADR,DBWORD
         LA    R1,TERMLINE
         LA    R0,L'TERMLINE
         LA    R6,1(0,R6)            BUMP USER COUNT
         TPUT  (1),(0),R             DISPLAY USER
NEXTASCB LA    R7,4(0,R7)            NEXT TSDASVT SLOT
         BCT   R5,ASCBLOOP
         DROP  R8
         DEQ   MF=(E,DEQPARM)        RELEASE TSDASVT
         CVD   R6,DBWORD             CONVERT USER COUNT
         MVC   TOTMSG,USER#
         MVC   UCOUNT,EDMSK1
         ED    UCOUNT,DBWORD+6
         LA    R1,TOTMSG
         LA    R0,L'TOTMSG
         TPUT  (1),(0),R             DISPLAY TOTALS LINE
EXIT     FREEMAIN R,LV=WORKLEN,A=(R3)
         FULLRTRN RENT=YES,RC=0
         TITLE 'DUSER - CONSTANTS'
         DS    0H
COMPUID  CLC   UID(*-*),USER
TRANTAB  DC    (C' ')X'0',C' ',(255-C' ')X'0'
ENQPARM  ENQ   (QNAME,RNAME,E,7,SYSTEM),MF=L
DEQPARM  DEQ   (QNAME,RNAME,7,SYSTEM),MF=L
CBNAME   DC    CL8'TSDASCB'
QNAME    DC    CL8'TSDASVT'
RNAME    DC    CL7'TSDASCB'
HEADING  DC    CL10'USERID'          HEADING
         DC    CL18'ACCOUNT NUMBER'
         DC    CL11'PROCNAME'
         DC    CL9'LOGON'
         DC    CL7'AUTH'
         DC    CL6'LINE'
         DC    CL13'TERMINAL'
USER#    DC    CL25' XXX USER(S) LOGGED ON'
EDMSK    DC    X'4021207A20207A2020' LOGON TIME MASK
EDMSK1   DC    X'40202120'           USER COUNT MASK
BLANKS   DC    CL8' '                BLANKS CONSTANT
TRTAB    DC    C'0123456789ABCDEF'   LINE ADDRESS TRANSLATE TABLE
CVTERROR DC    CL40'TSDCVT NOT FOUND, COMMAND TERMINATED'
         TITLE 'DUSER - SYSTEM DSECTS'
         PRINT NOGEN
         CVT   DSECT=YES,PREFIX=NO   CVT
         IHAPSA                      PSA
         IKJTCB                      TCB
         IEZJSCB                     JSCB
         IKJPSCB                     PSCB
         PRINT GEN
         TITLE 'DUSER - USER DSECTS'
         TSDCVT                      TSDCVT
         SPACE 2
         TSDASCB                     TSDASCB
         SPACE 2
WORK     DSECT                       WORK AREA
DBWORD   DS    D                     SCRATCH PAD
UID      DS    CL8                   USERID & L'USERID
AUTHFLG  DS    X                     CALLER AUTHORITY FLAG
TERMLINE DS    CL74                  DETAIL LINE
         ORG   TERMLINE
USER     DS    CL8                   USERID
         DS    CL2
ACCT     DS    CL16                  ACCOUNT NUMBER
         DS    CL2
PROC     DS    CL8                   LOGON PROCEDURE
         DS    CL2
LOGT     DS    CL8                   LOGON TIME
         DS    CL2
AUTH     DS    CL5                   USER AUTHORITY
         DS    CL2
LINEADR  DS    CL3                   LINE ADDRESS
         DS    CL3
TERM     DS    CL8                   TERMINAL NAME
         ORG
TOTMSG   DS    CL25                  TOTALS MESSAGE
         ORG   TOTMSG
UCOUNT   DS    CL4
         ORG
WORKLEN  EQU   *-WORK                WORK AREA SIZE
         END   DUSER
