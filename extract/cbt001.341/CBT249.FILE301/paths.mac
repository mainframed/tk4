         TITLE 'PATHS - DISPLAY OFFLINE CHANNEL PATHS'
*********************************************************************
*                                                                   *
*  NAME      -      PATHS COMMAND                                   *
*                                                                   *
*  FUNCTION  -      DISPLAY OFFLINE CHANNEL PATHS                   *
*                                                                   *
*  AUTHOR    -      C. LYONS                                        *
*                                                                   *
*  SYSTEM    -      MVS 3.8 AND MVS/SE2                             *
*                                                                   *
*  DATE      -      08/27/80                                        *
*                                                                   *
*  ATTRIB    -      NON-REUS                                        *
*                                                                   *
*  ENTRY     -      PATHS VIA TMP ATTACH                            *
*                                                                   *
*  EXITS     -      NONE                                            *
*                                                                   *
*  INPUT     -      R1->CPPL (NOT USED)                             *
*                                                                   *
*  OUTPUT    -      CUU OFFLINE ON CPU XX                           *
*                                                                   *
*  TABLES    -      NONE                                            *
*                                                                   *
*  MACROS    -      FULLSAVE,FULLRTRN,TPUT                          *
*                                                                   *
*  DSECTS    -      CVT,LCH,CSD,IEFUCBOB                            *
*                                                                   *
*  SUPPORT   -      16 CHANNELS                                     *
*                   MP CONFIGURATION                                *
*                   CHANNEL ROTATE TCH PATH TABLE ALTERATION        *
*                                                                   *
*  PRIVILEGES-      PROBLEM PROGRAM                                 *
*                                                                   *
*  LOGIC     -      SCAN UCBS EXAMINING FIELDS INDICATING           *
*                   PATHS OFFLINE. USE TCH PATH TABLE TO            *
*                   DETERMINE PRIMARY AND ALTERNATE CHANNEL         *
*                   NUMBER.                                         *
*                                                                   *
*  INSTALL   -      ASSEMBLE PATHS                                  *
*                   LINK PATHS                                      *
*                   PLACE PATHS IN LINKLIST LIBRARY                 *
*                                                                   *
*********************************************************************
PATHS    CSECT
         REGS  R
         FULLSAVE RENT=NO
         L     R2,CVTPTR               CVT
         USING CVT,R2
         L     R2,CVTILK2              UCB LOOKUP TABLE
         DROP  R2
         SR    R3,R3
UCBSCAN  LH    R4,0(0,R2)              UCB
         LTR   R4,R4
         BZ    NEXTUCB                 CHECK IF END OF TABLE
         BM    EXIT
         USING UCBCMSEG,R4
         TM    UCBSTAT,UCBONLI         ONLINE DEVICE?
         BZ    NEXTUCB
         CR    R4,R3                   ALTERNATE PATH IN LOOKUP TBL?
         BNH   NEXTUCB
         LR    R3,R4
         TM    UCBCHM,UCBPPA           PRIMARY PATH AVAILABLE?
         BZ    SECPATH
         SR    R15,R15                 CPU #
         SR    R1,R1                   DISPL IN TCH PATH TABLE
         BAL   R10,UNITMSG
SECPATH  TM    UCBFL5,UCBALTPH         ALTERNATES FOR DEVICE?
         BZ    TRYMP
         TM    UCBCHM,UCBSPA           SECONDARY PATH AVAILABLE?
         BZ    TRYMP
         SR    R15,R15                 CPU #
         LA    R1,2                    DISPL IN TCH PATH TABLE
         BAL   R10,UNITMSG
TRYMP    L     R1,CVTPTR               CVT
         USING CVT,R1
         L     R1,CVTCSD               CSD
         USING CSD,R1
         TM    CSDCPUAL,CPU1           CPU 1 ACTIVE?
         DROP  R1
         BZ    NEXTUCB
         TM    UCBCHM,UCBPPB           PRIMARY PATH AVAILABLE?
         BZ    SECMP
         LA    R15,1                   CPU #
         SR    R1,R1                   DISPL IN TCH PATH TABLE
         BAL   R10,UNITMSG
SECMP    TM    UCBFL5,UCBALTPH         ALTERNATES FOR DEVICE?
         BZ    NEXTUCB
         TM    UCBCHM,UCBSPB           SECONDARY PATH AVAILABLE?
         BZ    NEXTUCB
         LA    R15,1                   CPU #
         LA    R1,2                    DISPL IN TCH PATH TABLE
         BAL   R10,UNITMSG
NEXTUCB  LA    R2,2(0,R2)              NEXT ENTRY IN UCB LOOKUP TBL
         B     UCBSCAN
         TITLE 'PATHS - MESSAGE SUBROUTINE'
UNITMSG  L     R5,CVTPTR               CVT
         USING CVT,R5
         L     R5,CVTILCH              LCH
         USING LCH,R5
         SR    R6,R6
         IC    R6,UCBLCI               LCH INDEX
         SLL   R6,5                    LCH DISPL
         LA    R5,0(R6,R5)             LCH ENTRY
         L     R6,LCHTCH               TCH PATH TABLE
         DROP  R5
         LA    R6,0(R1,R6)             PROPER SLOT
         MVC   CHAN,0(R6)              CHANNEL NUMBER
         TR    CHAN,HEX
         MVC   CUU,UCBNAME+1           CU & DEVICE
         DROP  R4
         STC   R15,CPU                 CPU #
         OI    CPU,X'F0'
         TPUT  CHAN,20                 SEND TERMINAL MESSAGE
         MVI   PRTFLG,X'FF'            INDICATE SOMETHING PRINTED
         BR    R10
EXIT     CLI   PRTFLG,X'FF'            ANYTHING PRINTED?
         BE    FINI
         TPUT  NONEOFF,30              NO, TELL USER NO OFFLINE PATHS
FINI     FULLRTRN RENT=NO              EXIT
         TITLE 'PATHS - CONSTANTS'
NONEOFF  DC    CL30'NO OFFLINE CHANNEL PATHS FOUND'
PRTFLG   DC    X'00'                   PRINT FLAG
HEX      DC    C'0123456789ABCEDF'     TRANSLATE TABLE
CHAN     DS    CL1                     TERMINAL MESSAGE:
CUU      DS    CL2                       CCU OFFLINE ON CPU X
         DC    C' OFFLINE ON CPU '
CPU      DS    CL1
         TITLE 'PATHS - DSECTS'
         CVT   DSECT=YES,PREFIX=NO     CVT
         SPACE 3
LCH      DSECT                         LCH
         ORG   LCH+16
LCHTCH   DS    A
         SPACE 3
         IEFUCBOB                      UCB
         SPACE 3
CSD      DSECT                         CSD
         ORG   CSD+8
CSDCPUAL DS    XL2
CPU1     EQU   X'40'
         END   PATHS
