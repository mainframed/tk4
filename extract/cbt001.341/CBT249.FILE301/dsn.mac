         TITLE 'DSN COMMAND DOCUMENTATION'
**********************************************************************
*                                                                    *
*  NAME       -    DSN                                               *
*                                                                    *
*  FUNCTION   -    DISPLAY USERS ALLOCATING A SPECIFIC DATASET       *
*                                                                    *
*  SYNTAX     -    DSN 'DSNAME'                                      *
*                                                                    *
*  RESTRAINTS -    NO MEMBER NAME OR PASSWORD WITH DSNAME            *
*                                                                    *
*  AUTHOR     -    C. LYONS                                          *
*                                                                    *
*  VERSION    -    2.0 FOR MVS/SE2  (MVS 3.8, SEE COMMENTS)          *
*                                                                    *
*  DATE       -    12/11/80                                          *
*                                                                    *
*  INPUT      -    R1->CPPL                                          *
*                                                                    *
*  OUTPUT     -    TEST.DSN.EXAMPLE   USER      TYPE    STATUS       *
*                                     ----      ----    ------       *
*                                     $SPRCTL   TSU      EXC         *
*                                     PAYROLL   JOB      SHR         *
*                                     INIT      INIT     SHR         *
*                                     TCAM      STC      EXC         *
*                                     MASTER    SYS      SHR         *
*                                                                    *
*  ATTRIB     -    REENTRANT                                         *
*                                                                    *
*  REGS       -    R12 - BASE                                        *
*                  R13 - SAVE AREA                                   *
*                  R4  - WORK AREA                                   *
*                                                                    *
*  LOGIC      -                                                      *
*                  1. ENTRANCE CONVENTIONS                           *
*                  2. OBTAIN WORK AREA                               *
*                  3. PARSE OPERAND                                  *
*                  4. DEFER ATTENTION INTERRUPTS                     *
*                  5. SETUP ESTAE ENVIRONMENT                        *
*                  6. GET AUTHORIZATION                              *
*                  7. MODSET KEY=ZERO,MODE=SUP                       *
*                  8. OBTAIN LOCAL AND CMSEQDQ LOCKS                 *
*                  9. SCAN ENQ CONTROL BLOCKS                        *
*                 10. RELEASE LOCKS                                  *
*                 11. RETURN TO USER PROTECT KEY                     *
*                 12. RELEASE AUTHORIZATION                          *
*                 13. CANCEL ESTAE                                   *
*                 14. ALLOW ATTENTIONS                               *
*                 15. OUTPUT RESULTS TO TERMINAL                     *
*                 16. EXIT CONVENTIONS                               *
*                                                                    *
* MACROS      -   REGS       -  REGISTER EQUATES                     *
*                 FULLSAVE   -  ENTRANCE CONVENTIONS                 *
*                 GETMAIN    -  ACQUIRE VIRTUAL MEMORY               *
*                 STAX       -  SET ATTENTION STATUS                 *
*                 ESTAE      -  ESTABLISH RECOVERY ENVIRONMENT       *
*                 MODESET    -  ACQUIRE PRIVILEGED STATUS            *
*                 SETLOCK    -  ACQUIRE/RELEASE LOCKS                *
*                 IKJRLSA    -  RELEASE PARSE WORK AREA              *
*                 FREEMAIN   -  RELEASE VIRTUAL MEMORY               *
*                 FULLRTRN   -  EXIT CONVENTIONS                     *
*                 IKJPARM    -  SETUP PARSE PCL                      *
*                 IKJPOSIT   -  SPECIFY POSITIONAL PARAMETER         *
*                 IKJENDP    -  END PARSE PCL                        *
*                 CALLTSSR   -  INVOKE TSO SERVICE ROUTINE           *
*                 TPUT       -  TERMINAL OUTPUT                      *
*                 AUTHON     -  GET APF AUTHORIZATION                *
*                 AUTHOFF    -  REMOVE APF AUTHORIZATION             *
*                                                                    *
* DSECTS      -   CB            DSECT NAME                           *
*                 --            ----- ----                           *
*                 CPPL          IKJCPPL                              *
*                 PPL           IKJPPL                               *
*                 TCB           IKJTCB                               *
*                 PSA           IHAPSA                               *
*                 CVT           CVT DSECT=YES,PREFIX=NO              *
*                 QCB           IHAQCB                               *
*                 QEL           IHAQEL                               *
*                 ASCB          IHAASCB                              *
*                 CSCB          CSCB (IEECHAIN MACRO)                *
*                 ASVT          IHAASVT                              *
*                                                                    *
* ERRORS      -   SEE 'DSN - ERROR MESSAGES' SECTION                 *
*                                                                    *
* PRIVILEGES  -   PKO, SUPR STATE, APF AUTHORIZED                    *
*                                                                    *
* NOTES       -   1. PROGRAM CAUSES MODIFICATION OF JSCBAUTH BIT     *
*                    TO ACHIEVE AUTHORIZATION.                       *
*                                                                    *
*                 2. SYMBOL 'QELMAX' REPRESENTS MAXIMUM NUMBER OF    *
*                    USERS DISPLAYED. IF THIS SYMBOL IS ALTERED TO   *
*                    A LARGER NUMBER, CODE CHANGES MAY BE REQUIRED.  *
*                                                                    *
* DEPENDENCIES-   ENQUEUE CONTROL BLOCKS AND SERIALIZATION LOCKS     *
*                 FOR MVS/SE2 SYSTEM                                 *
*                 AUTHSVC TO GET APF AUTHORIZATION                   *
*                                                                    *
* INSTALL     -   CHANGE SVC # IN AUTHON AND AUTHOFF MACROS          *
*                 ASSEMBLE DSN                                       *
*                 LINK DSN RENT REUS REFR                            *
*                 PLACE DSN IN LINKLIST LIBRARY OR PLPA              *
**********************************************************************
         TITLE 'DSN COMMAND - MACROS'
         MACRO
&LABEL   AUTHON &SVC=248
&LABEL   LA    0,1 .                   SET AUTH
         SVC   &SVC .                  ISSUE SPECIAL SVC
         MEND
         SPACE 3
         MACRO
&LABEL   AUTHOFF &SVC=248
&LABEL   SR    0,0 .                   REMOVE AUTH
         SVC   &SVC .                  ISSUE SPECIAL SVC
         MEND
         TITLE 'DSN COMMAND - EQUATED SYMBOLS'
DSNCMD   CSECT
         SPACE 3
QELMAX   EQU   20                      MAXIMUM USERS DISPLAYED
BLANK    EQU   C' '                    BLANK CONSTANT
JOBTYPE  EQU   X'0'                    TYPE=INITIATED JOB
INITYPE  EQU   X'1'                    TYPE=WAITING INITIATOR
STCTYPE  EQU   X'2'                    TYPE=STARTED TASK
TSUTYPE  EQU   X'3'                    TYPE=TSO USER
SYSTYPE  EQU   X'4'                    TYPE=SYSTEM TASK
ERRLEN   EQU   60                      LENGTH OF ERROR MSG
NULL     EQU   X'0'                    NULL VALUE
OUTLEN   EQU   72                      LENGTH OF OUTPUT LINE
MEMFOUND EQU   X'80'                   MEMBER NAME PRESENT
PASFOUND EQU   X'80'                   PASSWORD PRESENT
QELWLEN  EQU   10                      DETAIL LINE WORK AREA LENGTH
NOENQ    EQU   X'FF'                   NO USERS ALLOCATED FLAG
ENQON    EQU   X'0'                    USERS ALLOCATED FLAG
         TITLE 'DSN COMMAND - INITIALIZATION'
         REGS R
         FULLSAVE RENT=YES
         LR   R3,R1                    SAVE CPPL ADDRESS
         USING CPPL,R3
         GETMAIN R,LV=WORKLEN          GET WORK AREA
         LR   R4,R1                    SAVE WORK AREA ADDRESS
         USING WORK,R4
         XC   QELWORK(QELMAX*QELWLEN),QELWORK   CLEAR DISPLAY AREA
         MVI  DSN,BLANK                     CLEAR DSN
         MVC  DSN+1(L'DSN-1),DSN
         MVI  ENQFLAG,ENQON                 CLEAR ENQ FLAG
         TITLE 'DSN COMMAND - PARSE OPERAND'
         XC   PARSE(PARSELEN),PARSE    CLEAR PPL
         LA   R5,PARSPARM              R5->PPL
         USING PPL,R5
         MVC  PPLUPT,CPPLUPT           UPT ADDRESS
         MVC  PPLECT,CPPLECT           ECT ADDRESS
         MVC  PPLCBUF,CPPLCBUF         CIB ADDRESS
         DROP R3
         LA   R1,PARSECB
         ST   R1,PPLECB                ECB ADDRESS
         L    R1,=A(PCL)
         ST   R1,PPLPCL                PCL ADDRESS
         LA   R1,PARSANS
         ST   R1,PPLANS                PDL ADDRESS
         CALLTSSR EP=IKJPARS,MF=(E,(R5))
         DROP R5
         TITLE 'DSN COMMAND - VALIDATE OPERAND'
         L    R5,PARSANS               GET PDL ADDRESS
         LTR  R5,R5
         BP   GOODPARS                 PARSE SUCCESSFUL?
         LA   R9,PARSERR
         BAL  R10,PUTRTN               NO, THEN ERROR
         B    EXIT
GOODPARS EQU  *
         USING PDL,R5
         TM    PASSFLG,PASFOUND        PASSWORD SPECIFIED?
         BZ    NOPASS
         LA    R9,PASSERR              YES, THEN ERROR
         BAL   R10,PUTRTN
         B     EXIT
NOPASS   TM    MEMFLG,MEMFOUND         MEMBER SPECIFIED?
         BZ    QCBSETUP
         LA    R9,MEMERR               YES, THEN ERROR
         BAL   R10,PUTRTN
         B     EXIT
         DROP  R5
         TITLE 'DSN COMMAND - OBTAIN ENQUEUE LOCKS'
QCBSETUP EQU   *
         STAX  DEFER=YES               DEFER ATTENTIONS
         BXLE  R15,R15,GOODSTAX
         LA    R9,STAXERR              STAX OK?
         BAL   R10,PUTRTN              YES, CONTINUE
         B     EXIT
GOODSTAX ESTAE ESTAEX                  SETUP RECOVERY
         BXLE  R15,R15,ESTAEOK
         LA    R9,ESTAERR              ESTAE OK?
         BAL   R10,PUTRTN              YES, CONTINUE
         STAX  DEFER=NO
         B     EXIT
ESTAEOK  AUTHON                        GET AUTHORIZATION
         MODESET KEY=ZERO,MODE=SUP     GET PK0,SUPR STATE
         SETLOCK OBTAIN,TYPE=LOCAL,REGS=USE,MODE=UNCOND,               $
               RELATED=('GET LOCAL LOCK')
*
*  FOR MVS/SE2 A NEW CMS LOCK (CMSEQDQ) IS PROVIDED FOR ENQ/DEQ.
*  IF THIS COMMAND MUST RUN IN AN MVS 3.8 ENVIRONMENT, CHANGE
*  THE SETLOCKS TO TYPE=CMS.
*
         SETLOCK OBTAIN,TYPE=CMSEQDQ,REGS=USE,MODE=UNCOND,             $
               RELATED=('GET CMS LOCK')
         TITLE 'DSN COMMAND - SCAN ENQUEUE CONTROL BLOCKS'
         L     R1,CVTPTR
         USING CVT,R1
         L     R5,CVTFQCB              GET FIRST QCB ADDRESS
         DROP  R1
         L     R8,PARSANS              GET A(PDL)
         USING PDL,R8
         USING MAJ,R5
         LTR   R5,R5                   IF NONE, THEN EXIT
         BZ    NOENQUE
CHKMAJ   CLC   MAJNAME,SYSDSN          SYSDSN MAJOR QCB?
         BE    MAJFOUND
         L     R5,MAJNMAJ              NO, TRY NEXT MAJOR QCB
         LTR   R5,R5
         BNZ   CHKMAJ
NOENQUE  MVI   ENQFLAG,NOENQ           IF DATASET NOT ALLOCATED,
         B     PRIVOFF                    SET FLAG, AND EXIT
MAJFOUND L     R5,MAJFMIN              SYSDSN MAJOR QCB WAS FOUND,
         USING MIN,R5
         L     R6,DSNPTR                SEARCH FOR DSN MINOR
         SR    R2,R2
CHKMIN   IC    R2,MINNAMEL             LENGTH OF RNAME MATCH
         CLM   R2,1,DSNLEN+1                  LENGTH OF DSN?
         DROP  R8
         BE    LENGTHOK
NEXTMIN  L     R5,MINNMIN              NO, TRY NEXT MINOR QCB
         LTR   R5,R5
         BZ    NOENQUE
         B     CHKMIN                  IF LENGTH MATCHES, SEE
LENGTHOK BCTR  R2,0                       IF RNAMES ARE EQUAL
         EX    R2,COMPNAME
         BNE   NEXTMIN
MINFOUND EQU   *                       A MINOR QCB WAS FOUND!
         L     R5,MINFQEL
         USING QEL,R5                  COLLECT QELS
         LA    R6,QELMAX               MAXIMUM OF 'QELMAX'
         LA    R8,QELWORK
         USING QELWORK,R8
QELLOOP  MVC   QELFLG,QELQFLGS         SAVE TYPE OF ENQ
         SR    R1,R1
         ICM   R1,3,QELASID            GET ASID
         BCTR  R1,0                    GET INDEX TO ASVT
         SLL   R1,2                    GET DISPL TO ASVT
         L     R2,CVTPTR               GET CVT
         USING CVT,R2
         L     R2,CVTASVT              GET ASVT
         USING ASVT,R2
         LA    R2,ASVTENTY             GET ASCB POINTER SECTION
         L     R2,0(R1,R2)             GET ->ASCB
         USING ASCB,R2
         SR    R1,R1
         L     R3,ASCBJBNI             GET JOBNAME PTR
         CR    R1,R3
         BE    NOTJOB                  IF 0, NOT INITIATED JOB
         MVC   QELNAME,0(R3)           SAVE JOBNAME
         MVI   QELTYPE,JOBTYPE         INDICATE INITIATED JOB
         B     NEXTQEL
NOTJOB   L     R3,ASCBJBNS             GET JOBNAME PTR
         MVC   QELNAME,0(R3)           SAVE JOBNAME
         C     R1,ASCBCSCB             MASTER SCHEDULER?
         BNE   NOMASTER
         MVI   QELTYPE,SYSTYPE         YES, INDICATE 'SYSTEM'
         B     NEXTQEL
NOMASTER L     R2,ASCBCSCB             GET CSCB
         USING CSCB,R2
         CLI   CHTRKID,CHTSID          TSU?
         BNE   NOTTSO
         MVI   QELTYPE,TSUTYPE         YES, INDICATE TSO USER
         B     NEXTQEL
NOTTSO   CLI   CHTRKID,CHINITID        INITIATOR?
         BNE   NOTINIT
         MVI   QELTYPE,INITYPE         YES, INDICATE WAITING INIT
         B     NEXTQEL
NOTINIT  MVI   QELTYPE,STCTYPE         MUST BE STC!
NEXTQEL  L     R5,QELNQEL
         LTR   R5,R5                   IF QELS EXHAUSTED,
         BZ    PRIVOFF                    THEN PRINT RESULTS
         LA    R8,QELWLEN(0,R8)
         BCT   R6,QELLOOP              LOOP THROUGH QELS
         DROP  R2,R5,R8
         TITLE 'DSN COMMAND - RELEASE ENQUEUE LOCKS'
PRIVOFF  SETLOCK RELEASE,TYPE=CMSEQDQ,REGS=USE,                        $
               RELATED=('RELEASE CMS LOCK')
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          $
               RELATED=('RELEASE LOCAL LOCK')
         USING PSA,R0
         L     R2,PSATOLD
         USING TCB,R2
         MODESET EXTKEY=TCB,WORKREG=15
         DROP  R0,R2
         MODESET MODE=PROB
         AUTHOFF
         ESTAE   0
         STAX    DEFER=NO
         TITLE 'DSN COMMAND - TERMINAL OUTPUT ROUTINE'
         CLI   ENQFLAG,ENQON           ANY USERS ALLOCATED?
         BE    TERMIO                  YES, PRINT RESULTS
         LA    R9,RESERR
         BAL   R10,PUTRTN              NO, EXIT
         B     EXIT
TERMIO   L     R8,PARSANS              GET PDL
         USING PDL,R8
         L     R5,DSNPTR               GET ADDRESS OF DSN
         LH    R6,DSNLEN               GET LENGTH OF DSN
         DROP  R8
         BCTR  R6,0
         EX    R6,*+4                  MOVE DATASET NAME
         MVC   DSN(*-*),0(R5)
         MVC   HEADING,HEAD            SETUP HEADING
         LA    R2,DSN
         TPUT  (R2),OUTLEN             WRITE HEADING
         MVC   HEADING,ULINE           SETUP UNDERLINE
         MVI   DSN,BLANK
         MVC   DSN+1(L'DSN-1),DSN      CLEAR DSN FIELD
         LA    R2,DSN
         TPUT  (R2),OUTLEN             WRITE UNDERLINE
         LA    R8,QELMAX               MAXIMUM USERS
         LA    R6,QELWORK
         USING QELWORK,R6
PRTLOOP  CLI   QELNAME,NULL            MORE DETAIL?
         BE    EXIT                    NO, EXIT
         MVI   HEADING,BLANK           CLEAR DETAIL LINE
         MVC   HEADING+1(L'HEADING-1),HEADING
         MVC   USER,QELNAME            MOVE JOBNAME
         MVC   STATUS+1(3),=C'SHR'
         TM    QELFLG,QELSHARE         MOVE TYPE OF ENQ
         BO    NEXTMOVE
         MVC   STATUS+1(3),=C'EXC'
NEXTMOVE SR    R1,R1
         IC    R1,QELTYPE              GET TYPE OF USER INDEX
         MH    R1,=H'6'                CONVERT TO DISPLACEMENT
         EX    R0,MOVETYPE(R1)         MOVE USER TYPE
         LA    R2,DSN
         TPUT  (R2),OUTLEN
         LA    R6,QELWLEN(0,R6)        NEXT DETAIL LINE
         BCT   R8,PRTLOOP
         TITLE  'DSN COMMAND - EXIT ROUTINE'
EXIT     IKJRLSA PARSANS               RELEASE PARSE WORK SPACE
         FREEMAIN R,LV=WORKLEN,A=(R4)  RELEASE WORK AREA
         FULLRTRN RENT=YES
         TITLE 'DSN COMMAND - ERROR MESSAGE ROUTINE'
PUTRTN   TPUT  (R9),ERRLEN
         BR   R10
         TITLE 'DSN COMMAND - ESTAE EXIT'
ESTAEX   DS   0H
         AUTHOFF                       REMOVE AUTHORIZATION
         STAX DEFER=NO                 ALLOW ATTENTIONS
         SR   R15,R15                  PERMIT ABEND
         BR   R14
         TITLE 'PARSE PARAMETER CONTROL LIST'
PCL      IKJPARM DSECT=PDL
DSNAME   IKJPOSIT DSNAME,PROMPT='DATA SET NAME',USID
         IKJENDP
         TITLE 'DSN COMMAND - CONSTANTS, EXECUTED INSTRUCTIONS'
SYSDSN   DC    CL8'SYSDSN'             SYSDSN MAJOR NAME
HEAD     DC    CL28' - USER       TYPE   STATUS'
ULINE    DC    CL28'   ----       ----   ------'
         DS    0H
         USING MIN,R5
COMPNAME CLC  MINNAME(*-*),0(R6)       COMPARE DSN TO RNAME
         DROP R5
*                                      MOVE TYPE OF USER
MOVETYPE MVC  TYPE(3),=C'JOB'          0 = INITIATED JOB
         MVC  TYPE(4),=C'INIT'         1 = WAITING INITIATOR
         MVC  TYPE(3),=C'STC'          2 = STARTED TASK
         MVC  TYPE(3),=C'TSU'          3 = TSO USER
         MVC  TYPE(3),=C'SYS'          4 = SYSTEM TASK (MASTER SCHED.)
         TITLE 'DSN COMMAND - LITERALS'
         LTORG
         TITLE 'DSN COMMAND - ERROR MESSAGES'
PASSERR  DC   CL60'DSN TERMINATED - PASSWORD SPECIFIED WITH DSNAME'
MEMERR   DC   CL60'DSN TERMINATED - MEMBER SPECIFIED WITH DSNAME'
PARSERR  DC   CL60'DSN TERMINATED - PARSE FAILURE, NOTIFY TSD'
STAXERR  DC   CL60'DSN TERMINATED - STAX FAILURE, NOTIFY TSD'
ESTAERR  DC   CL60'DSN TERMINATED - ESTAE FAILURE, NOTIFY TSD'
RESERR   DC   CL60'DSN COMMAND COMPLETED - NO USERS ALLOCATED'
         TITLE 'PARSE DESCRIPTOR ENTRY'
PDL      DSECT
         ORG  DSNAME
DSNPTR   DS   F                        A(DSN)
DSNLEN   DS   H                        L'DSN
DSNFLGS  DS   X                        FLAGS
DSNRES   DS   X                        RESERVED
MEMPTR   DS   F                        A(MEMBER)
MEMLEN   DS   H                        L'MEMBER
MEMFLG   DS   X                        FLAGS
MEMRES   DS   X                        RESERVED
PASSPTR  DS   F                        A(PASSWORD)
PASSLEN  DS   H                        L'PASSWORD
PASSFLG  DS   X                        FLAGS
         TITLE 'DSN COMMAND - WORK AREA DSECT'
WORK     DSECT
PARSE    EQU  *
PARSPARM DS   7A                       PPL
PARSECB  DS   F                        PARSE ECB
PARSANS  DS   A                        A(PDL)
PARSELEN EQU  *-PARSE                  L'PARSE AREA
ENQFLAG  DS   X                        ENQUEUE FLAG
DSN      DS   CL44                     DATASET NAME
HEADING  DS   CL28                     OUTPUT HEADING
         ORG  HEADING
         DS   3X
USER     DS   CL8                      USER NAME
         DS   3X
TYPE     DS   CL4                      TYPE OF USER
         DS   3X
STATUS   DS   CL3                      TYPE OF ENQUEUE
         ORG
QELWORK  DS   (QELMAX)CL(QELWLEN)      HOLDS QEL INFORMATION
         ORG  QELWORK
QELFLG   DS   X                        TYPE OF ENQ
QELTYPE  DS   X                        TYPE OF REQUESTOR
QELNAME  DS   CL8                      JOBNAME
         ORG
WORKLEN  EQU  *-WORK                   LENGTH OF WORK AREA
         TITLE 'COMMAND PROCESSOR PARAMETER LIST'
         IKJCPPL
         TITLE 'PARSE PARAMETER CONTROL LIST'
         IKJPPL
         TITLE 'TASK CONTROL BLOCK'
         IKJTCB
         TITLE 'PREFIXED SAVE AREA'
         IHAPSA
         TITLE 'COMMUNICATIONS VECTOR TABLE'
         CVT DSECT=YES,PREFIX=NO
         TITLE 'MAJOR/MINOR QUEUE CONTROL BLOCK'
         IHAQCB
         TITLE 'QUEUE ELEMENT'
         IHAQEL
         TITLE 'ADDRESS SPACE CONTROL BLOCK'
         IHAASCB
         TITLE 'COMMAND SCHEDULING CONTROL BLOCK'
CSCB     DSECT
         IEECHAIN
         TITLE 'ADDRESS SPACE VECTOR TABLE'
         IHAASVT
         END
