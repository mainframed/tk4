         TITLE 'DALLOC - LIST DYNAMIC DATASET ALLOCATIONS'
**********************************************************************
*                                                                    *
*   NAME      -   DALLOC                                             *
*                                                                    *
*   AUTHOR    -   C. LYONS                                           *
*                                                                    *
*   FUNCTION  -   LIST DYNAMICALLY ALLOCATED DATASETS                *
*                                                                    *
*   OUTPUT    -   DSNAME   DDNAME   DISP   VOLSER   CUU              *
*                                                                    *
*   ATTRIB    -   RENT                                               *
*                                                                    *
*   SYSTEM    -   MVS 3.8 AND MVS/SE2                                *
*                                                                    *
*   DATE      -   12/09/80                                           *
*                                                                    *
*   MACROS    -   FULLSAVE,GETMAIN,FREEMAIN,TPUT,FULLRTRN            *
*                                                                    *
*   DSECTS    -   WORK,IHAPSA,IKJTCB,IEZJSCB,IHADSAB,QDB,TIOT        *
*                 IEFJFCBN,IEFUCBOB                                  *
*                                                                    *
*   REGISTERS -   R12=BASE; R13->SAVE AREA; R3->DSAB; R4->TIOT;      *
*                 R5->UCB; R6->JFCB                                  *
*                                                                    *
*   LANGUAGE  -   VS ASSEMBLER                                       *
*                                                                    *
*   PRIVILEGE -   PROBLEM PROGRAM                                    *
*                                                                    *
*   LOGIC     -   1. ENTRANCE CONVENTIONS                            *
*                 2. FIND DSAB CHAIN                                 *
*                 3. SCAN DSAB CHAIN, EXTRACTING INFORMATION:        *
*                                                                    *
*                         DSNAME  FROM  JFCB                         *
*                         DDNAME  FROM  TIOT                         *
*                         DISP    FROM  JFCB                         *
*                         VOLSER  FROM  UCB                          *
*                         CUU     FROM  UCB                          *
*                                                                    *
*                 4. EXIT CONVENTIONS                                *
*                                                                    *
*  INSTALL    -   ASSEMBLE DALLOC                                    *
*                 LINK DALLOC RENT REUS REFR                         *
*                 PLACE DALLOC IN LINKLIST LIBRARY OR PLPA           *
**********************************************************************
         TITLE 'DALLOC - INITIALIZATION'
DALLOC   CSECT
         FULLSAVE RENT=YES             RE-ENTRANT CONVENTIONS
         REGS  R                       REGISTER EQUATES
         GETMAIN R,LV=WORKLEN          GET WORK AREA
         LR    R10,R1                  SAVE ADDRESS
         USING WORK,R10
         MVI   ALLOCMSG,C' '           CLEAR ALLOCATION MESSAGE
         MVC   ALLOCMSG+1(L'ALLOCMSG-1),ALLOCMSG
         MVI   HEADING,X'0'            CLEAR PRINT HEADING INDICATOR
         TITLE 'DALLOC - FIND DSAB CHAIN'
*
*   PSA->TCB->JSCB->QDB->DSAB->JFCB->TIOT->UCB
*
         USING PSA,R0
         L     R1,PSATOLD              TCB
         USING TCB,R1
         L     R1,TCBJSCB              JSCB
         USING IEZJSCB,R1
         L     R1,JSCDSABQ             QDB
         USING QDB,R1
         L     R2,QDBNELMS             ARE THERE ANY ALLOC DATASETS?
         LTR   R2,R2
         BNZ   GETDSAB
         TPUT  NOALLOC,40              NO, TELL USER AND EXIT
         B     EXIT
GETDSAB  L     R3,QDBFELMP             DSAB
         DROP  R1
         TITLE 'DALLOC - SCAN DSAB CHAIN'
         USING DSAB,R3
DSABLOOP TM    DSABFLG1,DSABDALC       DYNAMICALLY ALLOCATED?
         BZ    NEXTDSAB
         CLI   HEADING,PRTHEAD         HAS THE HEADING BEEN PRINTED?
         MVI   HEADING,HEADPRT         SAY IT HAS NOW
         BNE   GETTIOT                 AND PRINT IT IF NECESSARY
         LA    R0,L'TITLEMSG
         LA    R1,TITLEMSG
         TPUT  (1),(0),R
         LA    R0,L'TITLEMSG
         LA    R1,UNDERLNE
         TPUT  (1),(0),R
GETTIOT  L     R4,DSABTIOT             TIOT
         USING TIOELNGH,R4
         ICM   R5,7,TIOEFSRT           UCB
         USING UCBCMSEG,R5
         ICM   R6,7,TIOEJFCB           JFCB
         LA    R6,16(0,R6)             GET PAST PREFIX
         USING INFMJFCB,R6
         MVC   DSNAME,JFCBDSNM         SAVE DSNAME
         MVC   DDNAME,TIOEDDNM         SAVE DDNAME
         CLC   DSNAME(9),NULLFILE      DUMMY DATASET?
         BE    BLANKOUT
SYSOUT   CLC   DSNAME(3),=C'JES2'      SYSOUT DATASET?
         BNE   REGDSN
BLANKOUT MVC   VOLSER,HYPHENS
         MVC   CUU,HYPHENS
         B     SETDISP
REGDSN   CLI   JFCBELNM,C' '           MEMBER NAME PRESENT?
         BE    NOMEMBER
         TRT   DSNAME(35),BLANK        YES, SAVE IT
         MVI   0(R1),C'('
         MVC   1(8,R1),JFCBELNM        DSN(MEMBER)
         TRT   2(8,R1),BLANK
         MVI   0(R1),C')'
NOMEMBER MVC   VOLSER,UCBVOLI          SAVE VOLSER
         MVC   CUU,UCBNAME             SAVE UNITNAME
SETDISP  TM    JFCBIND2,JFCSHARE       DISP=SHR?
         BZ    TRYNEW
         MVC   DISP,SHRDSN             YES, SAY DISP=SHR
         B     TERMOUT
TRYNEW   TM    JFCBIND2,JFCNEW         DISP=NEW?
         BNO   TRYMOD
         MVC   DISP,NEWDSN             YES, SAY DISP=NEW
         B     TERMOUT
TRYMOD   TM    JFCBIND2,JFCMOD         DISP=MOD?
         BZ    TRYOLD
         MVC   DISP,MODDSN             YES, SAY DISP=MOD
         B     TERMOUT
TRYOLD   TM    JFCBIND2,JFCOLD         DISP=OLD?
         BZ    TERMOUT
         MVC   DISP,OLDDSN             YES, SAY DISP=OLD
TERMOUT  LA    R1,ALLOCMSG
         LA    R0,L'ALLOCMSG
         TPUT  (1),(0),R               OUT TERMINAL MESSAGE
NEXTDSAB L     R3,DSABFCHN             FORWARD DSAB CHAIN
         LTR   R3,R3
         BNZ   DSABLOOP                LOOP THROUGH DSABS
         TITLE 'DALLOC - EXIT'
EXIT     FREEMAIN R,LV=WORKLEN,A=(R10)
         FULLRTRN RENT=YES
         TITLE 'DALLOC CONSTANTS'
HYPHENS  DC    C'--------'
NULLFILE DC    CL9'NULLFILE '
BLANK    DC    (C' ')X'0',C' ',(255-C' ')X'0'
NEWDSN   DC    CL4'NEW'
OLDDSN   DC    CL4'OLD'
SHRDSN   DC    CL4'SHR'
MODDSN   DC    CL4'MOD'
NOALLOC  DC    CL40'NO DYNAMICALLY ALLOCATED DATASETS'
TITLEMSG DS    0CL73
         DC    CL6'DSNAME'
         DC    CL40' '
         DC    CL6'DDNAME'
         DC    CL4' '
         DC    CL4'DISP'
         DC    CL2' '
         DC    CL6'VOLSER'
         DC    CL2' '
         DC    CL3'CUU'
UNDERLNE DC    6C'-',CL40' ',6C'-',CL4' ',4C'-',CL2' ',6C'-'
         DC    CL2' ',3C'-'
         TITLE 'DALLOC - DSECTS'
WORK     DSECT
ALLOCMSG DS   0CL73
DSNAME   DS   CL44
         DS   CL2
DDNAME   DS   CL8
         DS   CL2
DISP     DS   CL4
         DS   CL2
VOLSER   DS   CL6
         DS   CL2
CUU      DS   CL3
HEADING  DS   X
PRTHEAD  EQU  X'00'
HEADPRT  EQU  X'FF'
WORKLEN  EQU  *-WORK
         PRINT NOGEN
         IHAPSA
         IKJTCB
         IEZJSCB
         IHADSAB
         IEFJFCBN
         IEFUCBOB
TIOT     DSECT
         IEFTIOT1
QDB      DSECT
QDBQDB   DS    CL4
QDBATTR  DS    XL2
QDBRV001 DS    XL2
QDBNELMS DS    F
QDBFELMP DS    A
         END
