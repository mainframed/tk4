         TITLE 'LISTV COMMAND - DOCUMENTATION'
**********************************************************************
*  NAME        -   LISTV                                             *
*                                                                    *
*  FUNCTION    -   LIST VOLUME SPACE STATISTICS                      *
*                                                                    *
*  SYNTAX      -   LISTV 'SEARCH INDEX'  OR LISTV                    *
*                                                                    *
*  AUTHOR      -   C. LYONS  (ADAPTED FROM OS/MVT SHARE MOD)         *
*                                                                    *
*  VERSION     -   1.0 FOR MVS 3.8 AND MVS/SE2                       *
*                                                                    *
*  DATE        -   6/1/80                                            *
*                                                                    *
*  INPUT       -   R1->CPPL                                          *
*                                                                    *
*  OUTPUT      -   VOLUME  DEV  USE  ---- FREE ----  LARGEST         *
*                   NAME   ADR  CNT CYL TRK NUM DSCB CYL/TRK         *
*                  TONE01  14B  014 040 309 149 0812 007 004         *
*                                                                    *
*  ATTRIB      -   REENTRANT                                         *
*                                                                    *
*  REGS        -   R12 - BASE                                        *
*                  R13 - SAVE AREA                                   *
*                  R4  - WORK AREA                                   *
*                  R10 - PDL                                         *
*                                                                    *
*  LOGIC       -                                                     *
*                  1. ENTRANCE CONVENTIONS                           *
*                  2. GETMAIN WORK AREA                              *
*                  3. BUILD PARSE PARAMETER LIST                     *
*                  4. INVOKE IKJPARS TO PARSE OPERAND                *
*                  5. SCAN UCBS FOR SEARCH INDEX MATCH (DEFAULT=ALL) *
*                  6. EXTRACT INFORMATION FROM UCB                   *
*                  7. GET SPACE INFORMATION VIA LSPACE SVC           *
*                  8. BUILD CAMLST AND PSEUDO DEB                    *
*                  9. INVOKE RESIDENT CONVERT ROUTINE                *
*                 10. OBTAIN FMT 4 DSCB & EXTRACT INFORMATION        *
*                 11. PRINT DETAIL LINE TO TERMINAL                  *
*                 12. IF MORE UCBS, GO TO 6                          *
*                 13. EXIT CONVENTIONS                               *
*                                                                    *
*  MACROS      -   REGS      -   REGISTER EQUATES                    *
*                  FULLSAVE  -   ENTRANCE CONVENTIONS                *
*                  GETMAIN   -   ACQUIRE VIRTUAL MEMORY              *
*                  CALLTSSR  -   INVOKE TSO SERVICE ROUTINE          *
*                  TPUT      -   TERMINAL OUTPUT                     *
*                  OBTAIN    -   READ DSCB                           *
*                  IKJRLSA   -   RELEASE PARSE WORK AREA             *
*                  FREEMAIN  -   FREE VIRTUAL MEMORY                 *
*                  FULLRTRN  -   EXIT CONVENTIONS                    *
*                  IKJPARM   -   SETUP PARSE PCL                     *
*                  IKJIDENT  -   SPECIFY POSITION PARAMETER          *
*                  IKJENDP   -   END PARSE PCL                       *
*                                                                    *
*  DSECTS      -   CB OR AREA    DSECT NAME                          *
*                  --            ----- ----                          *
*                  CPPL          IKJCPPL                             *
*                  CVT           CVT DSECT=YES,PREFIX=NO             *
*                  UCB           IEFUCBOB                            *
*                  PPL           IKJPPL                              *
*                  PDL           PDL                                 *
*                  WORK AREA     WORK                                *
*                  MESSAGE AREA  LSPACE                              *
*                  PRINT LINE    PRTLINE                             *
*                  DEB           IEZDEB                              *
*                  FMT4 DSCB     IECSDSL1 (4)                        *
*                                                                    *
*  ERRORS      -   PARSE FAILURE                                     *
*                  LSPACE FAILURE                                    *
*                  OBTAIN FAILURE                                    *
*                                                                    *
*  PRIVILEGES  -   PROBLEM PROGRAM                                   *
*                                                                    *
*  DEPENDENCIES-   INTERFACE TO RESIDENT CONVERT ROUTINE             *
*                  LSPACE PARAMETERS                                 *
*                  OBTAIN PARAMETERS                                 *
*                  VARIOUS MVS CONTROL BLOCKS                        *
*                                                                    *
*  INSTALL     -   ASSEMBLE LISTV                                    *
*                  LINK LISTV RENT REUS REFR                         *
*                  PLACE LISTV IN LINKLIST LIBRARY OR PLPA           *
**********************************************************************
         TITLE 'LISTV - INITIALIZATION AND OPERAND PARSE'
LISTV    CSECT
         FULLSAVE RENT=YES                  ENTRANCE CONVENTIONS
         REGS R                             REGISTER EQUATES
         LR    R3,R1                        SAVE CPPL ADDRESS
         USING CPPL,R3
         GETMAIN R,LV=WORKLEN               GET WORK AREA
         LR    R4,R1                        SAVE WORK AREA ADDRESS
         USING WORK,R4
         MVI   PRTFLAG,X'00'                CLEAR PRINT FLAG
         LA    R5,PARSPARM                  GET PPL ADDRESS
         USING PPL,R5
         XC    PARSE(PARSELEN),PARSE        INITIALIZE PPL
         MVC   PPLUPT,CPPLUPT               UPT
         MVC   PPLECT,CPPLECT               ECT
         MVC   PPLCBUF,CPPLCBUF             CIB
         DROP  R3
         LA    R1,PARSECB                   ECB
         ST    R1,PPLECB
         L     R1,=A(PCL)                   PCL
         ST    R1,PPLPCL
         LA    R1,PARSANS                   PDL
         ST    R1,PPLANS
         CALLTSSR EP=IKJPARS,MF=(E,(R5))    CALL PARSE
         DROP  R5
         L     R10,PARSANS                  OK?
         USING PDL,R10
         LTR   R10,R10
         BP    GOODPARS                     YES, CONTINUE
         LA    R1,=CL40'LISTV TERMINATED - PARSE ERROR'
         LA    R0,40
         TPUT  (1),(0),R                    OUTPUT ERROR MESSAGE
         B     RETURN
GOODPARS LA    R1,=CL40'VOLUME DEV USE  ---- FREE ----  LARGEST'
         LA    R0,40
         TPUT  (1),(0),R                    OUTPUT HEADING
         LA    R1,=CL40' NAME  ADR CNT CYL TRK NUM DSCB CYL/TRK'
         LA    R0,40
         TPUT  (1),(0),R                    OUTPUT HEADING
         TITLE 'EXTRACT UCB INFORMATION'
AGAIN    L     R1,CVTPTR                    CVT ADDRESS
         USING CVT,R1
         L     R6,CVTILK2                   UCB LOOKUP TABLE
         DROP  R1
         SR    R7,R7                        CLEAR BACK PTR
LOOP     LH    R8,0(0,R6)                   NEXT UCB FROM TABLE
         LTR   R8,R8                        TEST ENTRY TYPE
         BM    EXIT                         MINUS, STOP SCANNING
         BP    GOTUCB                       IF NON-ZERO GO ON
NXTUCB   LA    R6,2(0,R6)                   NEXT ONE
         B     LOOP                         CONTINUE SCANNING
         USING UCBCMSEG,R8
GOTUCB   TM    UCBTBYT3,UCB3DACC            DIRECT ACCESS?
         BZ    NXTUCB                       NO, OMIT
         CR    R8,R7                        ALTERNATE CHANNEL?
         BNH   NXTUCB                       IF SO SKIP IT
         LR    R7,R8                        SAVE THIS ONE AS DONE
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   NXTUCB                       NO, IGNORE DEVICE
         CLC   UCBTBYT3(2),=X'2005'         DATA CELL?
         BE    NXTUCB                       IF SO SKIP IT
         TM    UCBVOLI,X'FF'                ANY VOLUME MOUNTED?
         BZ    NXTUCB                       NO, IGNORE IT
         TM    VOLFLAG,VOLFOUND             SEARCH INDEX SPECIFIED?
         BZ    USEUCB                       NO, USE UCB
         L     R2,VOLPTR                    GET INDEX ADDRESS
         LH    R1,VOLLEN                    GET INDEX LENGTH
         BCTR  R1,0
         EX    R1,COMPVOL                   INDEX SATISFIED?
         BNE   NXTUCB
         BE    USEUCB                       YES, USE UCB
COMPVOL  CLC   0(*-*,R2),UCBVOLI
USEUCB   MVI   LINE,C' '                    CLEAR THE LINE
         MVC   LINE+1(39),LINE
         LA    R2,LINE                      R2->PRINT LINE
         USING PRTLINE,R2
         MVC   VOLNAME,UCBVOLI              PRINT VOLUME NAME
         MVC   UNITADDR,UCBNAME             GET UNIT ADDRESS
         SR    R1,R1                        CLEAR A REG
         IC    R1,UCBUSER                   GET USE COUNT
         CVD   R1,DBLW                      CONVERT TO PRINT
         UNPK  USECNT,DBLW+6(2)             PLACE IT ON THE LINE
         OI    USECNT+2,X'F0'               FIX SIGN BITS
         DROP  R2
         TITLE 'EXTRACT SPACE INFORMATION USING LSPACE SVC'
         LR    R0,R8                        POINT TO UCB
         LA    R1,DBLW                      MONITOR SPACE MESSAGE AREA
*  R1 -> MONITOR SPACE MESSAGE AREA
*  R0 -> UCB, HIGH ORDER BYTE INDICATES NO SMF RECORD (TYPE 19)
         SVC   78                           GET SPACE INFO
         LTR   R15,R15                      DID IT WORK?
         BZ    SPCOK                        YES
VTOCERR  MVC   LINE+15(25),=CL25'ERROR IN VTOC'
PUTIT    LA    R1,LINE
         LA    R0,40
         TPUT  (1),(0),R                    PRINT DETAIL OR ERROR
         MVI   PRTFLAG,X'FF'                SAY WE HAVE DONE SOME
         B     NXTUCB                       NOW GO ON
*
*  LSPACE RETURNS ITS INFORMATION IN THE FORM:
*        SPACE=CCCC,TTTT,AAAA/EEEE,KKKK
*  WHERE
*        CCCC = TOTAL FREE CYLINDERS
*        TTTT = TOTAL FREE TRACKS
*        AAAA = TOTAL FREE AREAS
*        EEEE = NUMBER OF CYLINDERS IN LARGEST FREE EXTENT
*        KKKK = NUMBER OF TRACKS IN LARGEST FREE EXTENT
*
SPCOK    LA    R1,DBLW                      R1->MONITOR SPACE MSG AREA
         USING LSPACE,R1
         LA    R2,LINE                      R2->PRINT LINE
         USING PRTLINE,R2
         MVC   TOTCYL,CYLTOTAL+1            CYL (TOTAL)
         MVC   TOTTRK,TRKTOTAL+1            TRK (TOTAL)
         MVC   TOTAREA,AREATOTL+1           NUM (OF AREAS)
         MVC   LARGECYL,CYLEXT+1            CYL (LARGEST)
         MVC   LARGETRK,TRKEXT+1            TRK (LARGEST)
         DROP  R1,R2
         TITLE 'EXTRACT FMT 4 INFORMATION USING OBTAIN'
*
*  BUILD CAMLST SEEK,CCHHR,VOLUME,RETURN
*
         MVC   CAML(4),=AL1(192,128,0,0)    MOVE IN OPTIONS
         MVC   CAMV(6),UCBVOLI              VOLUME NAME
         LA    R0,CAMA                      CCHHR PTR
         LA    R1,CAMV                      VOLUME PTR
         LA    R2,CAMW                      WORKAREA PTR
         STM   R0,R2,CAML+4                 SET UP CAMLIST
*
*  BUILD PSEUDO DEB FOR CONVERT ROUTINE
*
         XC    CAMW(60),CAMW                CLEAR DEB
         LA    R2,CAMW                      R2->DEB
         USING DEBBASIC,R2
         MVI   DEBNMEXT,X'01'               INDICATE 1 EXTENT
         MVI   DEBEXSCL,X'04'               SCALING FACTOR
         ST    R8,DEBSUCBA                  UCB ADDR
         LA    R2,CAMW+32                   R2->DEB DASD SECTION
         USING DEBDASD,R2
         MVC   DEBENDCC,=X'FFFF'            END CYL OF EXT
         MVC   DEBENDHH,=X'FFFF'            END TRK OF EXT
         MVC   DEBNMTRK,=X'7FFF'            # TRACKS IN EXT
         DROP  R2
         STM   R0,R15,CAMW+60               SAVE REGS
*
*  INVOKE CVTPCNVT ROUTINE (PART OF IGC0018) TO CONVERT THE
*  VTOC TTR0 (FROM THE UCB) TO ITS MBBCCHHR FOR OBTAIN
*
*  REGISTER SETUP:
*                   R0 =  TTR0
*                   R1 -> DEB PTR
*                   R2 -> RETURN AREA (CONTAINS MBBCCHHR)
*                R3-R8 =  UNUSED BY CONVERSION ROUTINE
*               R9-R13 =  DESTROYED BY CONVERISON ROUTINE
*                  R14 =  RETURN PTR
*                  R15 =  ENTRY POINT
*
         LA    R5,CAMW+60                   POINT TO REG SAVE AREAS
         LA    R1,CAMW                      DEB LOCATION
         MVC   CAMW(4),UCBVTOC              ALIGN TTR0
         DROP  R8
         L     R0,CAMW                      SETUP R0 FOR CONVERT
         LR    R2,R1                        MBBCCHHR TO R2
         L     R15,CVTPTR                   CVT
         USING CVT,R15
         L     R15,CVTPCNVT                 TTR CONVERT ROUTINE
         DROP  R15
         BALR  R14,R15                      CONVERT IT
         LM    R0,R14,0(R5)                 RESTORE REGS
         LTR   R15,R15                      ERROR?
         BNZ   VTOCERR                      YES, SHOW NOTHING
         MVC   CAMA(5),CAMW+3               CCHHR TO CAMLST
*
*  OBTAIN THE FORMAT 4 DSCB (DESCRIBES THE VTOC)
*
         OBTAIN CAML                        GET FORMAT 4 DSCB
         LTR   R15,R15                      DID IT WORK?
         BNZ   VTOCERR                      NO, GIVE UP
         LA    R5,CAMW                      R5->FMT 4 DSCB
         USING DSCB4,R5
         LH    R1,DS4DSREC                  GET NUMBER OF FMT0S
         CVD   R1,DBLW                      CONVERT IT
         LA    R2,LINE                      R2->PRINT LINE
         USING PRTLINE,R2
         UNPK  FMT0S,DBLW+5(3)              AND MOVE TO THE LINE
         MVI   FMT0S,C' '                   DON'T NEED HIGH BYTE
         OI    FMT0S+4,X'F0'                FIX SIGN
         MVI   DIRFFLG,C' '                 CLEAR DIRF FLAG INDICATOR
         TM    DS4VTOCI,DS4DIRF             IS DIRF FLAG ON?
         DROP  R5
         BZ    *+8                          NO
         MVI   DIRFFLG,C'*'                 YES, DISPLAY FLAG
         DROP  R2
         B     PUTIT                        NOW GO ON
         TITLE 'EXIT CONVENTIONS'
EXIT     TM    PRTFLAG,X'FF'                ANY VOLUMES SHOWN
         BO    RETURN
         LA    R1,=CL40'NO VOLUMES FOUND FOR INDEX GIVEN'
         LA    R0,40
         TPUT  (1),(0),R
RETURN   IKJRLSA PARSANS                    FREE PDL
         FREEMAIN R,LV=WORKLEN,A=(R4)       FREE WORK AREA
         DROP  R4,R10
         FULLRTRN RENT=YES                  EXIT
         TITLE 'LISTV - LITERALS'
         LTORG
         TITLE 'LISTV - PARSE PARAMETER CONTROL LIST'
PCL      IKJPARM DSECT=PDL
VOL      IKJIDENT 'VOLUME SEARCH INDEX',OTHER=ANY,MAXLNTH=6            $
               FIRST=ANY
         IKJENDP
         TITLE 'COMMUNICATION VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'UNIT CONTROL BLOCK'
         IEFUCBOB
         TITLE 'COMMAND PROCESSOR PARAMETER LIST'
         IKJCPPL
         TITLE 'LISTV WORK AREA'
WORK     DSECT
PARSE    EQU   *
PARSPARM DS    7A                           PPL
PARSECB  DS    F                            ECB
PARSANS  DS    A                            PDL
PARSELEN EQU   *-PARSE
PRTFLAG  DS    X                            PRINTING FLAG
LINE     DS    CL40                         DETAIL LINE
         DS    0D
DBLW     DS    4D                           LSPACE RETURN AREA
CAML     DS    4F                           CAMLST
CAMA     DS    2F
CAMV     DS    2F
         DS    0D
CAMW     DS    350C                         OBTAIN RETURN AREA
*                                           REG SAVE AREA
*                                           PSEUDO DEB
WORKLEN  EQU   *-WORK                       LENGTH OF WORK AREA
         TITLE 'PARSE PARAMETER LIST'
         IKJPPL
         TITLE 'IKJIDENT PDE'
PDL      DSECT
         ORG   VOL
VOLPTR   DS    A                            ADDRESS OF SEARCH INDEX
VOLLEN   DS    H                            LENGTH OF SEARCH INDEX
VOLFLAG  DS    X                            FLAGS
VOLFOUND EQU   X'80'                        INDEX PRESENT
         TITLE 'DATA EXTENT BLOCK'
         IEZDEB
         TITLE 'FMT4 DSCB'
DSCB4    DSECT
         ORG   DSCB4+44                     GET PAST KEY (44X'04')
         IECSDSL1 (4)
         TITLE 'MONITOR SPACE MESSAGE AREA'
LSPACE   DSECT
SPACEMSG DS    CL6                          'SPACE='
CYLTOTAL DS    CL4                          TOTAL FREE CYLINDERS
COMMA1   DS    CL1
TRKTOTAL DS    CL4                          TOTAL FREE TRACKS
COMMA2   DS    CL1
AREATOTL DS    CL4                          TOTAL FREE AREAS
SLASH    DS    CL1
CYLEXT   DS    CL4                          CYLS IN LARGEST FREE EXTENT
COMMA3   DS    CL1
TRKEXT   DS    CL4                          TRKS IN LARGEST FREE EXTENT
         TITLE 'PRINT LINE FORMAT'
PRTLINE  DSECT
VOLNAME  DS   CL6                           VOLUME NAME
DIRFFLG  DS   C                             DIRF FLAG
UNITADDR DS   CL3                           UNIT ADDRESS
         ORG  PRTLINE+11
USECNT   DS   CL3                           # OF CURRENT ALLOCATIONS
         DS   C
TOTCYL   DS   CL3                           TOTAL FREE CYLINDERS
         DS   C
TOTTRK   DS   CL3                           TOTAL FREE TRACKS
         DS   C
TOTAREA  DS   CL3                           TOTAL FREE AREAS
FMT0S    DS   CL5                           # FREE DSCBS
         DS   C
LARGECYL DS   CL3                           # CYLS IN LARGEST FREE EXT
         DS   C
LARGETRK DS   CL3                           # TRKS IN LARGEST FREE EXT
         END
