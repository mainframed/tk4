         TITLE 'FREEALL COMMAND DOCUMENTATION'
**********************************************************************
*                                                                    *
*  NAME       -    FREEALL                                           *
*                                                                    *
*  AUTHOR     -    C. LYONS (ADAPTED FROM SHARE MODIFICATION)        *
*                            THIS CP SHOULD NOT GET 0C4'S            *
*                                                                    *
*  FUNCTION   -    FREE ALL DYNAMICALLY ALLOCATED DATASETS           *
*                                                                    *
*  ENTRY      -    FREEALL VIA ATTACH FROM IKJEFT02 (TMP)            *
*                                                                    *
*  INPUT      -    R1-> STANDARD CPPL                                *
*                                                                    *
*  OUTPUT     -    DDNAME - DSNAME - ACTION CODE                     *
*                                                                    *
*  ATTRIBUTES -    REENTRANT, REFRESHABLE                            *
*                                                                    *
*  ERRORS     -    ^0 RC FROM DAIR INTERFACE INDICATING SVC 99       *
*                  WOULD NOT UNALLOCATE A PARTICULAR DATASET         *
*                                                                    *
*  SYSTEM     -    MVS 3.8 WITH SU64 AND MVS/SE2                     *
*                                                                    *
*  EXITS      -    NONE                                              *
*                                                                    *
*  DATE       -    07/01/80                                          *
*                                                                    *
*  SMP FMID   -    EBB1102 (BASE CONTROL PROGRAM)                    *
*                                                                    *
*  TARGET LIB -    TSD.LINKLIB(FREEALL) WITH ALIAS FREEA             *
*                                                                    *
*  MACROS     -    REGS,FULLSAVE,SAVE,GETMAIN,LINK,TPUT,FREEMAIN,    *
*                  FULLRTRN,RETURN                                   *
*                                                                    *
*  DSECTS     -    WORK,QDB,CVT,IKJTCB,IEZJSCB,IHADSAB,IHAPSA,       *
*                  IEFJFCBN,IEFTIOT1,IKJDAP18,IKJCPPL,LISTMAP        *
*                                                                    *
*  REGISTERS  -    R12 = BASE; R11 -> WORK AREA; R13 -> SAVE AREA    *
*                                                                    *
*                                                                    *
*  LANGUAGE   -    OS/VS LEVEL ASSEMBLER                             *
*                                                                    *
*  PRIVILEGES -    PROBLEM PROGRAM                                   *
*                                                                    *
*  LOGIC      -    1. ENTRANCE CONVENTIONS                           *
*                  2. GETMAIN WORK SPACE                             *
*                  3. INITIALIZE DAIR PARAMETER LIST                 *
*                  4. BUILD UNALLOCATION CANDIDATE LIST FROM DSABS   *
*                  5. INVOKE DAIR INTERFACE TO UNALLOCATE DATASETS   *
*                  6. REPORT TO TERMINAL USER                        *
*                  7. EXIT CONVENTIONS                               *
*                                                                    *
*                                                                    *
*                                                                    *
*  INSTALL     -   ASSEMBLE FREEALL                                  *
*                  LINK FREEALL RENT REUS REFR                       *
*                  PLACE FREEALL IN LINKLIST LIBRARY OR PLPA         *
*                                                                    *
**********************************************************************
         TITLE 'FREEALL COMMAND - MVS 3.8 TSO'
FREEALL  CSECT
         REGS  R                       REGISTER EQUATES
MAXDSN   EQU   30                      MAXIMUM # UNALLOCATIONS
         FULLSAVE RENT=YES             ENTRANCE CONVENTIONS
         LR    R3,R1                   SAVE CPPL POINTER
         USING CPPL,R3                 ADDRESS CPPL
         GETMAIN R,LV=WORKLEN          GET WORK SPACE
         LR    R11,R1                  SAVE WORK SPACE POINTER
         USING WORK,R11                ADDRESS WORK SPACE AREA
         SPACE 2
*
*  INITIALIZE DAIR PARAMETER LIST
*
         L     R4,CPPLUPT              USER PROFILE TABLE
         L     R5,CPPLECT              ENVIRONMENT CONTROL TABLE
         LA    R6,ECB                  EVENT CONTROL BLOCK
         L     R7,CPPLPSCB             PROTECTED STEP CONTROL BLOCK
         LA    R8,DAPB                 DAIR PARAMETER BLOCK
         STM   R4,R8,DAPL              SETUP DAIR PARAMETER LIST
         XC    ECB,ECB                 INITIALIZE ECB
         DROP  R3                      DROP CPPL ADDRESSABILITY
         SPACE 2
*
*  BUILD CANDIDATE LIST FOR UNALLOCATION FROM DSABS
*       PSA->TCB->JSCB->QDB->DSAB->TIOT->JFCB
*
         USING PSA,R0
         L     R2,PSATOLD              TCB
         USING TCB,R2
         L     R2,TCBJSCB              JSCB
         USING IEZJSCB,R2
         L     R2,JSCDSABQ             QDB
         USING QDB,R2
         L     R1,QDBNELMS             IF # ELEMENTS=0 EXIT
         LTR   R1,R1
         BZ    EXIT
         L     R8,QDBFELMP             GET FIRST DSAB
         DROP  R2
         USING DSAB,R8
         LA    R4,MAXDSN               MAXIMUM CANDIDATES
         SR    R5,R5                   CANDIDATE COUNTER
         LA    R6,CLIST                POINT TO CANDIDATE LIST
         USING LISTMAP,R6
DSABLOOP TM    DSABFLG1,DSABDALC       DYNAMICALLY ALLOCATED?
         BZ    NEXTDSAB                NO, OMIT
         TM    DSABFLG1,DSABNUSE       IN USE?
         BO    NEXTDSAB                YES, OMIT
         TM    DSABFLG1,DSABDCAT       DYNAMICALLY CONCATENATED?
         BO    NEXTDSAB                YES, OMIT
         L     R3,DSABTIOT             TIOT
         USING TIOELNGH,R3
         MVC   DDNAME,TIOEDDNM         SAVE DDNAME
         ICM   R7,7,TIOEJFCB           JFCB
         LA    R7,16(0,R7)             SKIP PREFIX
         USING INFMJFCB,R7
         MVC   DSNAME,JFCBDSNM         SAVE DSNAME
         CLI   JFCBELNM,C' '           MEMBER NAME PRESENT?
         BE    NOMEMBER
         TRT   DSNAME(35),BLANK        INSERT '(MEMBER NAME)'
         MVI   0(R1),C'('
         MVC   1(8,R1),JFCBELNM
         TRT   2(8,R1),BLANK
         MVI   0(R1),C')'
NOMEMBER LA    R5,1(0,R5)              BUMP CANDIDATE COUNTER
         LA    R6,L'CLIST(0,R6)        NEXT POSITION IN LIST
NEXTDSAB L     R8,DSABFCHN             GET NEXT DSAB
         LTR   R8,R8
         BZ    DSABDONE                IF NO MORE, INVOKE DAIR
         BCT   R4,DSABLOOP             KEEP SCANNING TILL 'MAXDSN'
         DROP  R0,R3,R6,R7,R8
         SPACE 2
*
*  INVOKE DYNALLOC VIA DAIR INTERFACE TO UNALLOCATE DATASETS
*
DSABDONE LTR   R5,R5                   ANY CANDIDATES?
         BZ    EXIT                    NO, EXIT
         LA    R3,CLIST                POINT TO CANDIDATE LIST
         USING LISTMAP,R3
UNALLOC  MVC   DAPB(DAPBLEN),DAPBCONS  SETUP DAPB
         LA    R4,DAPB
         USING DAPB18,R4               ADDRESS DAPB
         MVC   DA18DDN,DDNAME          FINISH DAPB WITH DDNAME
         LA    R1,DAPL
         LINK  EP=IKJDAIR              INVOKE DAIR
         MVC   SEP1,HYPHEN
         MVC   SEP2,HYPHEN             BEAUTIFY OUTPUT MESSAGE
         BXH   R15,R15,DAIRFAIL        IF RC^0 THEN ERROR
         MVC   ACTCODE,FREEMSG
         B     TPUT                    MOVE APPROPRIATE ACTION CODE
DAIRFAIL MVC   ACTCODE,ERRMSG
TPUT     LR    R1,R3                   POINT TO MESSAGE
         LA    R0,L'CLIST              GET MESSAGE LENGTH
         TPUT  (1),(0),R               SEND MESSAGE
         LA    R3,L'CLIST(0,R3)        NEXT CANDIDATE
         BCT   R5,UNALLOC              NEXT UNALLOCATION
         DROP  R3,R4
         B     RETURN                  EXIT
EXIT     LA    R1,NOFREE               SETUP TPUT
         LA    R0,L'NOFREE
         TPUT  (1),(0),R               TELL USER NO CANDIDATES EXIST
RETURN   FREEMAIN R,LV=WORKLEN,A=(R11) RELEASE WORK SPACE
         FULLRTRN RENT=YES             EXIT CONVENTIONS
         DROP  R11,R12
         TITLE 'FREEALL COMMAND - CONSTANTS'
BLANK    DC    (C' ')X'0',C' ',(255-C' ')X'0'
ERRMSG   DC    CL15'NOT UNALLOCATED'   UNALLOC ERROR MESSAGE
FREEMSG  DC    CL15'UNALLOCATED'       UNALLOC SUCCESS MESSAGE
NOFREE   DC    CL30'NO CANDIDATES FOR UNALLOCATION'
HYPHEN   DC    CL3' - '                SEPARATOR CONSTANTS
DAPBCONS DS    0F                      DAPB CONSTANTS
         DC    X'0018'                 DA18CD
         DC    H'0'                    DA18FLG
         DC    H'0'                    DA18DARC
         DC    H'0'                    DA18CTRC
         DC    A(0)                    DA18PDSN
         DC    CL8' '                  DA18DDN
         DC    CL8' '                  DA18MNM
         DC    CL2' '                  DA18SCLS
         DC    X'00'                   DA18DSP2
         DC    X'10'                   DA18CTL
         DC    CL8' '                  DA18JBNM
DAPBLEN  EQU   *-DAPBCONS              L'DAPB
         TITLE 'FREEALL COMMAND - DSECTS'
WORK     DSECT
DAPB     DS    0F
         ORG   DAPB+DAPBLEN            RESERVE DAPB SPACE
ECB      DS    F                       DAIR ECB
DAPL     DS    5F                      DAPL
CLIST    DS    (MAXDSN)CL73            CANDIDATE LIST
WORKLEN  EQU   *-WORK                  LENGTH OF WORK SPACE
         SPACE 2
LISTMAP  DSECT                         CANDIDATE LIST
DDNAME   DS    CL8                     DDNAME - DSN - ACTION CODE
SEP1     DS    CL3
DSNAME   DS    CL44
SEP2     DS    CL3
ACTCODE  DS    CL15
         SPACE 2
QDB      DSECT                         QUEUE DESCRIPTOR BLOCK
QDBQDB   DS    CL4                     ACRONYM
QDBATTR  DS    XL2                     QUEUE ATTRIBUTES
QDBRV001 DS    XL2                     RESERVED
QDBNELMS DS    F                       # QUEUE ELEMENTS
QDBFELMP DS    A                       POINTER TO FIRST ELEMENT
         SPACE 2
         PRINT NOGEN
         CVT   DSECT=YES,PREFIX=NO     CVT
         IKJTCB                        TCB
         IEZJSCB                       JSCB
         IHADSAB                       DSAB
         IHAPSA                        PSA
         IEFJFCBN                      JFCB
         IKJDAP18                      DAPB
         IKJCPPL                       CPPL
TIOT     DSECT                         TIOT
         IEFTIOT1
         END   FREEALL
