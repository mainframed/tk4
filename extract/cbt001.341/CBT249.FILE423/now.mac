         MACRO
.**********************************************************************
.*                                                                    *
.*  COPYRIGHT (C) 1978 BY JEFFREY C. VANDOREN.  ALL RIGHTS            *
.*  RESERVED EXCEPT:                                                  *
.*                                                                    *
.*  PARTICULAR LICENSE IS GRANTED TO DISTRIBUTE THIS MACRO FREE       *
.*  OF CHARGE, BUT IT MUST NOT BE SOLD.  A SERVICE CHARGE FOR         *
.*  HANDLING A COLLECTION OF SOFTWARE OF WHICH THIS IS A PART,        *
.*  ON THE OTHER HAND, IS APPROPRIATE.                                *
.*                                                                    *
.*                                  BROIDO COMPUTER CONSULTING        *
.*                                  104 MILLS STREET                  *
.*                                  MORRISTOWN, N.J.  07960           *
.*                                                                    *
.**********************************************************************
&NAME    NOW   &T,&X,&F
         LCLA  &A
* JOHNSON AND JOHNSON NOW MACRO                             CHANGE 1.0
.* AUTHOR: J. VAN DOREN
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  AIF   ('&X' EQ '').STD
         AIF   ('&X' EQ 'STD').STD
         AIF   ('&X' EQ 'NOTIME').NT
         AIF   ('&X' EQ 'NODAY').ND
         AIF   ('&X' EQ 'NEITHER').N
         AIF   ('&X' EQ 'SLASH').S
         AIF   ('&X' EQ 'SNOTIME').SNT
         AIF   ('&X' EQ 'SNODAY').SND
         AIF   ('&X' EQ 'SONLY').SO
         MNOTE 16,'FORMAT (PARM 2) IF SPECIFIED MUST BE STD (DEFAULT), X
               NEITHER, NOTIME, NODAY, SLASH, SNOTIME, SNODAY, OR SONLYX
               FOUND: &X'
         MEXIT
.STD     ANOP
&A       SETA  &A+1
         AGO   .FCHK
.NT      ANOP
&A       SETA  &A+8
         AGO   .STD
.ND      ANOP
&A       SETA  &A+2
         AGO   .STD
.N       ANOP
&A       SETA  &A+10
         AGO   .STD
.S       ANOP
&A       SETA  &A+4
         AGO   .FCHK
.SNT     ANOP
&A       SETA  &A+8
         AGO   .S
.SND     ANOP
&A       SETA  &A+2
         AGO   .S
.SO      ANOP
&A       SETA  &A+10
         AGO   .S
.FCHK    AIF   ('&F' EQ '').FOK
         AIF   ('&F' EQ '(15)').F
         AIF   ('&F' EQ '(R15)').F
         L     2,=A(&F)            LOAD REGISTER 2 WITH ADDR OF DATE.
.F       ANOP
&A       SETA  &A+128
.FOK     AIF   ('&T' EQ '').R1
         AIF   ('&T' EQ '(1)').R1
         AIF   ('&T' EQ '(R1)').R1
         L     1,=A(&T)                LOAD RECV ADDR
.R1      ANOP
         LA    0,&A                    LOAD PARAMETER REG
         L     15,=V(MERKNOW)      POINT TO NOW SUBROUTINE.
         BALR  14,15               CALL NOW SUBROUTINE.
         MEND
