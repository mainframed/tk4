         MACRO
&NAME    PROLOG &A,&LV=80,&SA=SAVEAREA,&BASE=12,&REGS=,                X
               &PARM=11,&WORK=IKXWORK
.*                                                                    *
.*       G B L A    C O P Y  G R O U P                                *
.*                                                                    *
         GBLA  &DP(33)
         GBLA  &GCT
         GBLA  &GCOUNT
         GBLA  &LVNUM
         GBLA  &REG
.*                                                                    *
.*       G B L B    C O P Y  G R O U P                                *
.*                                                                    *
         GBLB  &DBLE
         GBLB  &LIST
         GBLB  &DBUG
         GBLB  &EQU
         GBLB  &WDSW
         GBLB  &RENT
         GBLB  &PLG
         GBLB  &ONETM
         GBLB  &DASHS
         GBLB  &PWRK
         GBLB  &RCD
.*                                                                    *
.*       G B L C    C O P Y  G R O U P                                *
.*                                                                    *
         GBLC  &WD(33)
         GBLC  &NAM
         GBLC  &DNAM
         GBLC  &ONAM
         GBLC  &INAM
         GBLC  &RNAM
         GBLC  &NDX
         GBLC  &CSECT
         GBLC  &DSECT
         LCLA  &RNM
         LCLA  &RCT
         LCLA  &RLST
         LCLC  &CNM
         LCLA  &RDSP
&RCT     SETA  0
&RDSP    SETA  0
         AIF   (&LIST).LIST
         AGO   .LISTX
.LIST    ANOP
         EJECT
*                                                                     *
*                                                                     *
*        P R O L O G    M A C R O                                     *
*                                                                     *
*                                                                     *
         SPACE 4
*                                                                     *
*        FUNCTION:                                                    *
*                   CREATES INITIAL CODING (CSECT) FOR AN             *
*                   ASSEMBLY LANGUAGE MODULE:                         *
*                        1.   CSECT STATEMENT                         *
*                        2.   REGISTER SAVING                         *
*                        3.   ADDRESSABILITY                          *
*                        4.   NEW SAVE AREA  (GETMAIN)                *
*                                                                     *
*        FORMAT:                                                      *
*                                                                     *
*              NAME  PROLOG  TYPE,LV=NN                               *
*         OR                                                          *
*              NAME  PROLOG  TYPE,REGS=X,PARM=Y                       *
*                                                                     *
*                                                                     *
*                                                                     *
*                  -  WHERE  -                                        *
*                                                                     *
*                        NAME  IS THE CSECT NAME                      *
*                                                                     *
*                        TYPE  IS  R                                  *
*                                  RENT                               *
*                                  REENTRANT                          *
*                              FOR REENTERABLE CODE                   *
*                                                                     *
*                              OR  S                                  *
*                                  STD                                *
*                                  REUS                               *
*                                  BLANK                              *
*                              FOR REUSABLE CODE                      *
*                                                                     *
*                        LV    IS  NUMBER OF BYTES IN GETMAIN         *
*                                  GENERATED FOR REENTRANT            *
*                                  MODULES                            *
*                                                                     *
*                                                                     *
*                                                                     *
*                        REGS  IS  THE NUMBER OF REGISTERS TO BE      *
*                                  USED FOR ADDRESSABILITY IN         *
*                                  RE-ENTRANT MODULES.                *
*                                                                     *
*                        PARM  IS  THE REGISTER TO BE USED            *
*                                  TO SAVE R1 IN - RE-ENTRANT         *
*                                  MODULE CODE.                       *
*                                                                     *
*                                                                     *
*              NOTE THAT THIS MACRO MUST BE USED WITH THE             *
*              - EPILOG -  MACRO                                      *
*                                                                     *
         MEXIT
.LISTX   ANOP
&LVNUM   SETA  &LV
&RENT    SETB  0
&CSECT   SETC  '&NAME'
         AIF   ('&NAME' EQ '').MG1
         AIF   (&PLG).MG4
&PLG     SETB  1
         EQUREGS
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*         O S    S T A N D A R D    E N T R Y    L I N K A G E        *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         AIF   ('&A' EQ '').NORENT
         AIF   ('&A' EQ 'R').RENT
         AIF   ('&A' EQ 'S').NORENT
         AIF   ('&A' EQ 'RENT').RENT
         AIF   ('&A' EQ 'REUS').NORENT
         AIF   ('&A' EQ 'STD').NORENT
         AIF   ('&A' EQ 'REENTRANT').RENT
         AGO   .MG3
.NORENT  ANOP
         SPACE 4
&NAME    CSECT
         USING *,15
         STM   14,12,12(13) .          SAVE ENTRY REGISTERS
         BAL   12,X&SYSNDX .           POINT TO SAVE AREA
&SA      DC    18F'0' .                SAVE AREA FOR CSECT
DOUBLE   DC    D'0' .                  DOUBLEWORD WORK AREA
X&SYSNDX EQU   *
         ST    13,4(12) .              FORWARD CHAIN SAVE AREAS
         ST    12,8(13) .               BACKWARD CHAIN SAVE AREAS
         LR    13,12
         USING &SA.,13
         DROP  15
&RNM     SETA  13
&CNM     SETC  '&SA.'
         AGO   .END
.RENT    ANOP
&RENT    SETB  1
         AIF   ('&WORK' EQ '').MG1
&DSECT   SETC  '&WORK'
         SPACE 2
****         ****
*   REENTRANT   *
****         ****
         SPACE 4
&NAME    CSECT
         USING *,15
         STM   14,12,12(13) .          SAVE ENTRY REGISTERS
         LR    &BASE,15
         USING &NAME,&BASE
&RNM     SETA  &BASE
&CNM     SETC  '&NAME.'
         DROP  15
         GETMAIN R,LV=&LV
         ST    13,4(1) .               FORWARD CHAIN SAVEAREAS
         ST    1,8(13) .               BACKWARD CHAIN SAVE AREAS
         LR    13,1
         L     &PARM,4(13)
         LM    15,1,16(&PARM.)
         AGO   .END
.END     ANOP
         AIF   ('&REGS' EQ '').DFRG
&RCT     SETA  &REGS
.DFRG    ANOP
         AIF   (&RCT LT 1).EXRG
         AIF   (&RCT LT 9).RGEN
         MNOTE 9,'MAXIMUM OF 8 BASE REGISTERS  EXCEEDED'
         MEXIT
.RGEN    ANOP
.*       GENERATION OF ADDITIONAL BASE REGISTERS                     *.
         AIF   (&RCT EQ 0).EXRG
&RLST    SETA  &RNM
&RNM     SETA  &RNM-1
&RCT     SETA  &RCT-1
&RDSP    SETA  &RDSP+4096
         LR    &RNM.,&RLST
         LA    &RNM.,2048(&RNM.)
         LA    &RNM.,2048(&RNM.)
         USING &CNM.+&RDSP.,&RNM
         AGO   .RGEN
.EXRG    ANOP
         SPACE 4
         MEXIT
.MG1     ANOP
         MNOTE 9,'NAME FIELD MISSING'
         MEXIT
.MG2     ANOP
         MNOTE 9,'NAME ENTRY MUST BE SEVEN CHARACTERS OR LESS'
         MEXIT
.MG3     ANOP
         MNOTE 9,'INVALID MODE PARAMETER SPECIFIED'
         MEXIT
.MG4     ANOP
         MNOTE 9,'AN EPILOG MACRO MUST APPEAR BETWEEN TWO SUCCESSIVE'
         MNOTE 9,'PROLOG MACRO CALLS'
         MEND
