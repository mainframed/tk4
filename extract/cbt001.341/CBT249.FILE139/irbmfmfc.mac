SYNCHRON TITLE 'RMF SYNCHRONIZER MODULE'
*                                                                    *
*                                                                    *
*         R M F   S T A R T U P   S Y N C H R O N I Z E R            *
*                                                                    *
*                                                                    *
IRBMFMFC PROLOG R,LV=128
         USING IKXWORK,R13
         ST    R1,PARMADDR
         L     R1,0(R1)
*        CLC   0(2,R1),=H'4'
         B     NQT
QUIT     ABEND 13,DUMP
         EJECT
NQT      DS    0H
*        MVC   CARD(80),BLANKS
         OPEN  (SYNC,INPUT)
         GET   SYNC,CARD
CLOSE    CLOSE SYNC
*        CLC   CARD(80),BLANKS
*        BE    DEFAULT
         OC    CARD(8),FOXES
*
*
*
DEFAULT  EQU   *
         PACK  DOUBLE(8),CARD(4)
         CVB   R4,DOUBLE
         ST    R4,SYNCT
         C     R4,=F'59'
         BH    QUIT
DOTIME   EQU   *
         TIME  DEC
         SRL   R0,16
         SLL   R0,4
         ST    R0,TWORK
         MVI   TWORK,X'00'
         MVI   TWORK+1,X'00'
         OI    TWORK+3,X'0C'
         NI    TWORK+2,X'0F'
         ZAP   DOUBLE(8),=P'0'
         MVC   DOUBLE+4(4),TWORK
         CVB   R6,DOUBLE
         C     R6,=F'59'
         BH QUIT
         EJECT
*
*        R6    = CURRENT MINUTE
*
*        R4    = SYNCH MINUTE
*
         LTR   R4,R4
         BNZ   DOTIMES
         LA    R4,60
         SR    R4,R6
         B     WAITFOR
DOTIMES  EQU   *
         SR    R14,R14
         LA    R15,60
         D     R14,R4
         LTR   R15,R15
         BNZ   ABSOLUTE
         LR    R8,R4
LOOP1    EQU   *
         CR    R4,R6
         BNL   WAITCOMP
         AR    R4,R8
         B     LOOP1
WAITCOMP EQU   *
         SR    R4,R6
         B     WAITFOR
ABSOLUTE EQU   *
         CR    R4,R6
         BNL   ABSOMIN
         A     R4,=F'60'
ABSOMIN  EQU   *
         SR    R4,R6
         B     WAITFOR
WAITFOR  EQU   *
         LTR   R4,R4
         BZ    OUTSYNC
         MH    R4,=H'60'
         MH    R4,=H'100'
         ST    R4,BINTIM
         STIMER WAIT,BINTVL=BINTIM
         EJECT
*                                                                     *
*                                                                     *
* R M F    C O N T R O L    R O U T I N E   I N V O K E D   H E R E   *
*                                                                     *
*                                                                     *
         SPACE 5
         XC    TASKECB(4),TASKECB
         SR    R0,R0
         L     R1,PARMADDR
         ATTACH EP=ERBMFMFC,ECB=TASKECB
         ST     R1,TCBADDR
         WAIT ECB=TASKECB
         DETACH  TCBADDR
*
         SR    R15,R15
OUTSYNC  EPILOG RC=(15)
         EJECT
TASKECB  DC    F'0'
SYNC     DCB   DDNAME=SYNC,MACRF=(GM),LRECL=80,DSORG=PS,EODAD=CLOSE
FOXES    DC    8X'F0'
BLANKS   DC    CL133' '
CARD     DC    CL80'0030 '
         EJECT
IKXWORK  DSECT
         DS    18F
         DS    2F
TCBADDR  DS    F
DOUBLE   DS    D
TWORK    DS    F
PARMADDR DS    F
SYNCT    DS    F
BINTIM   DS    F
         END
