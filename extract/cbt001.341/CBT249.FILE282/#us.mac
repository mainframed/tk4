***********************************************************************
*                                                                     *
*              .US (UNDERSCORE) --- CONTROL                           *
*                                                                     *
***********************************************************************
         SPACE
US      $ENTER ,
        $CALL  USARG               ISOLATE ARG TO UNDERSCORE
         BC    BOMIT,US01          BRANCH IF NONE
        $CALL  USOP                UNDERSCORE OPERAND
         LA    R15,WRETNR          PROCESS THE RESULT
         B     US02                GO TO RETURN
US01     DS    0H
         SR    R15,R15             SET NO OPERAND RETURN CODE
US02     DS    0H
        $EXIT  RC=(R15)            AND RETURN TO CALLER
         SPACE
USARG   $ENTER ,
         L     TEMP,ARGSTART       ADDR OF BLANK DELIMITER
         LA    TEMP,1(,TEMP)       BEGINNING OF OPERAND LINE
         LA    R1,BUFF2            START OF LINE
         SR    R1,TEMP             NEGATIVE LENGTH TO CHOP
         AH    R1,BUFF2LGZ         NEW LENGTH
         BNP   USARG01             BRANCH IF NOTHING THERE
         STH   R1,BUFF2LGZ         SET NEW LENGTH
         EX    R1,USARGMVC         MOVE OVER THE REMAINDER
        $CC    POS                 SET RETURN CC
         B     USARG02             GO TO RETURN
USARG01  DS    0H
        $CC    OMIT                SET NO OPERAND CC
USARG02  DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
USARGMVC MVC   BUFF2(*-*),0(TEMP)  MOVE OVER ARGUMENT LINE
         SPACE
USOP    $ENTER ,
         MVI   UDFLAG,UDON         ASSUME CHARS TO BE UNDERSCORED
         LH    TEMP,BUFF2LGZ       LENGTH OF INPUT TEXT LINE
         LTR   TEMP,TEMP           ANY DATA PRESENT
         BNP   USOP08              BRANCH IF NONE
         LA    R0,B2LG             MAX OUTPUT LENGTH ALLOWED
         L     X3,BUFF3AD          WORK BUFFER ADDRESS
         SR    TEMP,TEMP           START AT THE BEGINNING
USOP01   DS    0H
         LA    R1,BUFF2(TEMP)      NEXT CHAR OF LINE
         CLI   UDINCCHR,C' '       IS NO INCLUDE CHAR SPECIFIED .Q
         BE    USOP03              BRANCH IF YES
         CLC   UDINCCHR,0(R1)      IS THIS THE INCLUDE CHAR .Q
         BNE   USOP03              BRANCH IF NOT
         CLC   UDINCCHR,UDIGNCHR   INCLUDE AND IGNORE CHARS THE SAME .Q
         BE    USOP02              BRANCH IF YES
         MVI   UDFLAG,UDON         TURN INCLUDE FLAG ON
         B     USOP04              REMOVE ESCAPE CHAR
USOP02   DS    0H
         XI    UDFLAG,UDON         REVERSE SWITCH
         B     USOP04              REMOVE ESCAPE CHAR
USOP03   DS    0H
         CLI   UDIGNCHR,C' '       IS NO IGNORE CHAR SPECIFIED .Q
         BE    USOP05              BRANCH IF YES
         CLC   UDIGNCHR,0(R1)      IS THIS THE IGNORE CHAR .Q
         BNE   USOP05              BRANCH IF NOT
         MVI   UDFLAG,UDOFF        TURN IGNORE FLAG ON
USOP04   DS    0H
         LH    COUNT,BUFF2LGZ      LENGTH OF INPUT LINE
         LA    COUNT,BUFF2(COUNT)  LAST CHARACTER PLUS ONE
         SR    COUNT,R1            LENGTH OF REMAINDER
         EX    COUNT,USOPDE        DELETE THE ESCAPE CHAR
         LH    COUNT,BUFF2LGZ      GET FORMER LENGTH
         S     COUNT,F1            LESS ONE
         BM    USOP08              RETURN IF NOTHING LEFT
         STH   COUNT,BUFF2LGZ      SET NEW LENGTH
         B     USOP01              DO NEXT CHARACTER
         SPACE
USOP05   DS    0H
         CLI   UDFLAG,UDOFF        ARE UNDERSCORES TO BE IGNORED .Q
         BE    USOP06              BRANCH IF YES
         SR    COUNT,COUNT         ZERO WORK REGISTER
         IC    COUNT,0(,R1)        LAST CHAR OF LINE
         A     COUNT,UDTABAD       POINT TO UNDERSCORE DEFINITION
         CLI   0(COUNT),UDOFF      DON'T UNDERSCORE THIS CHAR .Q
         BE    USOP06              BRANCH AROUND IF YES
         LH    COUNT,BUFF2LGZ      LENGTH OF CURRENT BUFFER
         LA    COUNT,2(,COUNT)     BUMP BY TWO
         CR    COUNT,R0            RESULT TOO BIG .Q
         BH    USOP07              BRANCH OUT IF YES
         STH   COUNT,BUFF2LGZ      UPDATE WITH NEW LENGTH
         LA    COUNT,BUFF2-3(COUNT)     LAST CHAR OF BUFFER
         SR    COUNT,R1            REMAINDER LENGTH-1
         EX    COUNT,USOPSV        SAVE THE REMAINDER
         MVI   1(R1),X'16'         ENTER BACKSPACE
         MVI   2(R1),C'_'          ENTER UNDERSCORE
         EX    COUNT,USOPRE        RESTORE THE REMAINDER
         LA    TEMP,2(,TEMP)       BUMP OFFSET BY TWO NEW CHARS
USOP06   DS    0H
         LA    TEMP,1(,TEMP)       BUMP OFFSET BY ONE
         CH    TEMP,BUFF2LGZ       AT THE END .Q
         BL    USOP01              BRANCH BACK IF NOT
         B     USOP08              GO TO RETURN
USOP07   DS    0H
         MVI   EFLAG044,ERRWARN    GENERATED LINE TOO LONG
USOP08   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
USOPSV   MVC   4(*-*,X3),1(R1)     SAVE BUFFER REMAINDER
USOPRE   MVC   3(*-*,R1),4(X3)     RESTORE BUFFER REMAINDER
USOPDE   MVC   0(*-*,R1),1(R1)     DELETE UNDERSCORE ESCAPE CHAR
