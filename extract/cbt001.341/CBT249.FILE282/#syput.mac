***********************************************************************
*                                                                     *
*              'SYPUT' --- QSAM PUT A RECORD                          *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1) IOCB ADDRESS
*                      (R0) DATA POINTER TO RDW
         SPACE
SYPUT   $ENTER ,
         LR    R2,R1               IOCB ADDRESS
         LR    R1,R0               POINT TO DATA
         TM    IOCRECFM-IOCB(R2),X'C0'  RECFM 'U' .Q
         BO    SYPUT01             BRANCH IF YES
         TM    IOCRECFM-IOCB(R2),X'40'  RECFM 'V' .Q
         BO    SYPUT01             BRANCH IF YES
         L     R3,IOCBUSER-IOCB(,R2)    WORK BUFFER ADDR
         LTR   R3,R3               DOES IT EXIST .Q
         BZ    SYPUT00             BRANCH IF NOT
         SPACE
         LH    R4,IOCLRECL-IOCB(,R2)    LENGTH OF RECORD
         BCTR  R4,0                LENGTH-1
         BCTR  R4,0                LENGTH-2
         MVI   4(R3),C' '          BLANK FIRST CHARACTER
         EX    R4,SYPUTMVC         DO ALL CHARACTERS
         LH    R4,0(,R1)           DATA LENGTH INCL RDW
         BCTR  R4,0                LENGTH-1
         EX    R4,SYPUTMVD         MOVE DATA TO TEMP
         LR    R1,R3               POINT TO TEMP BUFFER
SYPUT00  DS    0H
         LA    R1,4(,R1)           POINT TO DATA
         LH    R0,IOCLRECL-IOCB(,R2)    FILE LRECL
         B     SYPUT03             GO TO PUT
         SPACE
SYPUT01  DS    0H
         LH    R0,0(,R1)           RECORD LENGTH
         LA    R1,4(,R1)           POINT TO DATA
         S     R0,=F'4'            DECREMENT LENGTH BY RDW LENGTH
         CH    R0,IOCLRECL-IOCB(,R2)    TOO LONG .Q
         BNH   SYPUT02             BRANCH IF NOT
         LH    R0,IOCLRECL-IOCB(,R2)    TAKE MAXIMUM LENGTH
SYPUT02  DS    0H
         SPACE
SYPUT03  DS    0H
         L     R15,=V(SYSINT)      SYSTEM DEPENDENT ROUTINE
         BAL   R14,52(,R15)        GO DO THE PUT
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
SYPUTMVC MVC   5(*-*,R3),4(R3)     BLANK WORK BUFFER
SYPUTMVD MVC   0(*-*,R3),0(R1)     ENTER RDW AND DATA
         SPACE
         LTORG ,
