***********************************************************************
*                                                                     *
*        INTERNAL FUNCTION 'SYMUSL' -- LOCATE REFERENCE NAME          *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER  --  LA    R1,SRAREA
*                     $CALL  SYMUSL
*
*        ON EXIT   --  CC=BPOS  SYMBOL FOUND (COUNT=ADDR)
*                      CC=BNEG  SYMBOL NOT FOUND (COUNT=
*                                          LAST ADDR IN CHAIN)
         SPACE
SYMUSL  $ENTER ,
         LR    X1,R1               ENTRY POINTER
         USING SRAREA,X1           AND IT LOOKS LIKE THIS
         LA    X2,SRANCH           TRAILING POINTER
         L     COUNT,SRANCH        START OF REFERENCE CHAIN
         LTR   COUNT,COUNT         DOES REFERENCE CHAIN EXIST .Q
         BZ    SUSL08              BRANCH IF NOT
         SPACE
         L     R1,RNTBSRCH         COUNT OF REF NAME SEARCHES
         A     R1,F1               PLUS ONE
         ST    R1,RNTBSRCH         UPDATE WITH NEW TOTAL
         LA    R1,ASRCNT           NUMBER OF LAST REF POINTERS
         SR    R15,R15             START OF LOOKASIDE TABLE
SUSL01   DS    0H
         USING RND,COUNT           REF TABLE ADDRESSABILITY
         L     COUNT,SRLAST(R15)   TRIAL ENTRY
         LTR   COUNT,COUNT         ANY ENTRY .Q
         BZ    SUSL02              BRANCH IF NOT
         CLC   RNSYM,SRNM          THIS ENTRY .Q
         BE    SUSL03              IF SO, QUIT
         LA    R15,4(,R15)         NEXT ENTRY ADDRESS
         BCT   R1,SUSL01           TRY THEM ALL
         SR    COUNT,COUNT         WE FAILED
         S     R15,F4              BACKUP TO LAST TRIAL ENTRY
SUSL02   DS    0H
         L     R1,RNTBFAIL         GET LOOKASIDE FAILURE COUNT
         A     R1,F1               PLUS ONE
         ST    R1,RNTBFAIL         UPDATE LOOKASIDE FAILURE COUNT
SUSL03   DS    0H
         S     R15,F4              REARRANGE POINTERS
         BM    SUSL04              BRANCH OUT WHEN DONE
         L     R1,SRLAST(R15)      GET ENTRY TO PUSH DOWN
         ST    R1,SRLAST+4(R15)    PUSH IT
         B     SUSL03              GO UNTIL EXAUSTED
         SPACE
SUSL04   DS    0H
         LTR   COUNT,COUNT         WERE WE SUCCESSFUL .Q
         BNZ   SUSL09              BRANCH IF YES
         LA    COUNT,SRANCH        START PTR TO REF CHAIN
         L     R1,SRNM             FOR FASTER FOUR CHAR COMPARES
         B     SUSL06              ENTER AT CHECK FOR END
         SPACE
SUSL05   DS    0H
         C     R1,RNSYM            COULD IT BE THIS ENTRY
         BE    SUSL07              BRANCH IF YES
SUSL06   DS    0H
         LA    X2,RNSCHN           REMEMBER THE TRAILER
         L     COUNT,RNSCHN        NEXT POINTER TO TRY
         LTR   COUNT,COUNT         AT THE CHAIN END .Q
         BNZ   SUSL05              BRANCH IF NOT
         B     SUSL08              WE HAVE FAILED
SUSL07   DS    0H
         CLC   RNSYM+4(L'RNSYM-4),SRNM+4     CHECK TRAILER INFO
         BE    SUSL09              BRANCH IF ALL EQUAL
         B     SUSL06              NO, CHECK FOR END
         SPACE
SUSL08   DS    0H
         LR    COUNT,X2            RETURN TRAILING POINTER
        $CC    NEG                 FAILURE RETURN CODE
         B     SUSL10              GO TO RETURN
         SPACE
SUSL09   DS    0H
         ST    COUNT,SRLAST        RETAIN SUCCESSFUL POINTER IN STACK
        $CC    POS                 SUCCESSFUL RETURN
         SPACE
SUSL10   DS    0H
        $EXIT  KEEP=(COUNT),RC=0   RETURN TO CALLER
         DROP  COUNT,X1
