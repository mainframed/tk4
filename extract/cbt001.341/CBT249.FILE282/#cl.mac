***********************************************************************
*                                                                     *
*              .CL (COLUMN LENGTH) --- CONTROL                        *
*                                                                     *
***********************************************************************
         SPACE
CL      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETNUM              GET NUMBER OPERAND
         BC    BDNO,CLE005         ERROR IF INVALID
         BC    BOMIT,CL01          BRANCH IF OMITTED
         CLI   NUMSIGN,C' '        WAS OPERAND UNSIGNED .Q
         BE    CL02                BRANCH IF YES
         A     COUNT,CLLZ          MAKE RELATIVE CHANGE
         B     CL02                GO CHECK AND ENTER
         SPACE
CL01     DS    0H
         SR    COUNT,COUNT         ZERO DEFAULT VALUE
CL02     DS    0H
         LTR   COUNT,COUNT         REST FOR ZERO
         BZ    CL03                SPECIAL CASE IF YES
         CH    COUNT,PICNT         TEST GREATER THAN PARAGRAPH INDENT
         BNH   CLE014              ERROR IF NOT
         C     COUNT,DEFLL+TAB3MIN LESS THAN MINIMUM .Q
         BL    CLE014              ERROR IF YES
         C     COUNT,DEFLL+TAB3MAX BEYOND MAX LENGTH .Q
         BH    CLE013              ERROR IF YES
         ST    COUNT,CLLZ          SET COLUMN LENGTH
         ST    COUNT,CLLZSAVE      AND SET IT TWICE
         B     CL04                GO TO RETURN
         SPACE
CL03     DS    0H
         ST    COUNT,CLLZSAVE      ZERO SAVED VALUE
         L     COUNT,LLZ           GET CURRENT LINE LENGTH
         ST    COUNT,CLLZ          SET AS COLUMN LENGTH
         SPACE
CL04     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
CLE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     CL04                GO TO RETURN
CLE013   DS    0H
         MVI   EFLAG013,ERRSET     OPERAND TOO BIG
         B     CL04                GO TO RETURN
CLE014   DS    0H
         MVI   EFLAG014,ERRSET     OPERAND TOO SMALL
         B     CL04                GO TO RETURN
