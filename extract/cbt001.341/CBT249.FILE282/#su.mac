***********************************************************************
*                                                                     *
*              .SU (SUBSTITUTE SYMBOL) --- CONTROL                    *
*                                                                     *
***********************************************************************
         SPACE
SU      $ENTER ,
         L     X3,ARGSTART         REMEMBER WHERE IT ALL STARTED
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BOMIT,SU02          BRANCH IF NULL, ASSUME ONE
         BC    BPOS,SU03           AFFIRMATIVE IS FOREVER
         BC    BNEG,SU04           NEGATIVE FINISHES IT
         CLI   YNRESULT,YNUP       IS ARGUMENT 'UPPER' .Q
         BE    SU06                BRANCH IF YES
         CLI   YNRESULT,YNNOUP     IS ARGUMENT 'NOUPPER' .Q
         BE    SU07                BRANCH IF YES
         CLI   YNRESULT,YNTRON     IS ARGUMENT 'TRACEON' .Q
         BE    SU08                BRANCH IF YES
         CLI   YNRESULT,YNTROFF    IS ARGUMENT 'TRACEOFF' .Q
         BE    SU09                BRANCH IF YES
         SPACE
         ST    COUNT,ARGSTART      RESET START OF SCAN
        $CALL  GETNUM              IT IS PROBABLY NUMERIC
         BC    BDNO,SU01           BRANCH IF INVALID
         BC    BNEG,SU04           NEGATIVE IS GOOD AS ZERO
         B     SU05                ENTER VALUE FOR SUBSEQUENT .UR
         SPACE
SU01     DS    0H
         ST    X3,ARGSTART         ORIGINAL 'LINE' STARTED HERE
        $CALL  USARG               MOVE LINE TO BEGINNING
         LA    COUNT,1             ONE LINE SUBSTITUTE
         STH   COUNT,URCNT         ENTER .UR VALUE
         LA    R15,WRETSU          PROCESS THE RESULT
         B     SU11                GO TO RETURN
         SPACE
SU02     DS    0H
         LA    COUNT,1             ONE LINE BY DEFAULT
         B     SU05                ENTER VALUE FOR .UR
SU03     DS    0H
         LH    COUNT,H32767        LARGE POSITIVE NUMBER
         B     SU05                ENTER VALUE FOR .UR
SU04     DS    0H
         SR    COUNT,COUNT         ZERO VALUE
SU05     DS    0H
         LR    X1,COUNT            REMEMBER VALUE
         BAL   RETURN,SU12         ANY MORE ARGUMENTS .Q
         BC    BPOS,SU01           YES, THEN A 'LINE' ARGUMENT
         STH   X1,URCNT            ENTER .UR VALUE
         B     SU10                BRANCH TO RETURN
         SPACE
SU06     DS    0H
         BAL   RETURN,SU12         ANY MORE ARGUMENTS .Q
         BC    BPOS,SU01           YES, THEN A 'LINE' ARGUMENT
        $OFF   SWITCHS,NUPSWS      CONVERT REF NAMES TO UPPER CASE
         B     SU10                BRANCH TO RETURN
SU07     DS    0H
         BAL   RETURN,SU12         ANY MORE ARGUMENTS .Q
         BC    BPOS,SU01           YES, THEN A 'LINE' ARGUMENT
        $ON    SWITCHS,NUPSWS      DON'T CONVERT REF NAMES TO UPPER
         B     SU10                BRANCH TO RETURN
         SPACE
SU08     DS    0H
         BAL   RETURN,SU12         ANY MORE ARGUMENTS .Q
         BC    BPOS,SU01           YES, THEN A 'LINE' ARGUMENT
        $ON    SWITCHS,UTSWS       USE REFERENCE TRACE ON
         B     SU10                BRANCH TO RETURN
SU09     DS    0H
         BAL   RETURN,SU12         ANY MORE ARGUMENTS .Q
         BC    BPOS,SU01           YES, THEN 'LINE' ARGUMENT
        $OFF   SWITCHS,UTSWS       USE REFERENCE TRACE OFF
         B     SU10                BRANCH TO RETURN
         SPACE
SU10     DS    0H
         SR    R15,R15             NO MORE DATA TO PROCESS
SU11     DS    0H
        $EXIT  RC=(R15)            AND GET NEXT LINE
         SPACE
SU12     DS    0H
        $SAVE  ,                   SAVE CALLER'S REGISTERS
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET ONE MORE ARGUMENT (IF ANY)
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
