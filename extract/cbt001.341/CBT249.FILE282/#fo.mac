***********************************************************************
*                                                                     *
*              .FO (FORMAT) --- CONTROL                               *
*              .FI (FILL) --- CONTROL                                 *
*                                                                     *
***********************************************************************
         SPACE
FO      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $CALL  GETYESNO            GET AND TEST ARGUMENT
         BC    BPOS+BOMIT,FO01     BRANCH IF OMITTED OR POSITIVE
         BC    BNEG,FO02           BRANCH IF NEGATIVE
         CLI   YNRESULT,YNHALF     'HALF JUSTIFY' FORMAT .Q
         BE    FO00                BRANCH IF YES
         CLI   YNRESULT,YNLEFT     'LEFT' FORMAT .Q
         BE    FO03                BRANCH IF YES
         CLI   YNRESULT,YNRGHT     'RIGHT' FORMAT .Q
         BE    FO04                BRANCH IF YES
         CLI   YNRESULT,YNCNTR     'CENTRE' FORMAT .Q
         BE    FO05                BRANCH IF YES
         CLI   YNRESULT,YNISDE     'INSIDE' FORMAT .Q
         BE    FO06                BRANCH IF YES
         CLI   YNRESULT,YNOSDE     'OUTSIDE' FORMAT .Q
         BE    FO07                BRANCH IF YES
         MVI   EFLAG005,ERRSET     ELSE INVALID ARGUMENT
         B     FO10                GO TO FINAL EXIT
         SPACE
FO00     DS    0H
         MVI   JUVAL,JUVHALF       HALF JUSTIFY LEFT AND RIGHT
         B     FO08                GO TO COMMON EXIT
FO01     DS    0H
         MVI   JUVAL,JUVPOS        JUSTIFY LEFT AND RIGHT
         B     FO08                GO TO COMMON EXIT
FO02     DS    0H
         MVI   JUVAL,JUVNEG        NO JUSTIFICATION
        $ON    SWITCHS,NFSWS       AND NO CONCATENATION
         B     FO09                GO TO EXIT
FO03     DS    0H
         MVI   JUVAL,JUVLEFT       LEFT JUSTIFY
         B     FO08                GO TO COMMON EXIT
FO04     DS    0H
         MVI   JUVAL,JUVRGHT       RIGHT JUSTIFY
         B     FO08                GO TO COMMON EXIT
FO05     DS    0H
         MVI   JUVAL,JUVCNTR       CENTRE JUSTIFY
         B     FO08                GO TO COMMON EXIT
FO06     DS    0H
         MVI   JUVAL,JUVISDE       INSIDE JUSTIFY
         B     FO08                GO TO COMMON EXIT
FO07     DS    0H
         MVI   JUVAL,JUVOSDE       OUTSIDE JUSTIFY
         B     FO08                GO TO COMMON EXIT
         SPACE
FO08     DS    0H
        $OFF   SWITCHS,NFSWS       TURN OFF 'NO CONCATENATE'
FO09     DS    0H
         MVC   CECNT,F0            CLEAR CE/LA/RA COUNT
FO10     DS    0H
        $EXIT  RC=0                AND CONTINUE
