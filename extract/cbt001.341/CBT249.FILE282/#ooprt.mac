***********************************************************************
*                                                                     *
*              'OOPRT' INTERNAL ROUTINE --- PROCESS OUTPUT OVERLAY    *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY --  (R0) -> RDW OF OUTPUT LINE
         SPACE
OOPRT   $ENTER ,
OOPRT01  DS    0H
         L     X1,OOANCH           OUTPUT OVERLAY ELEMENT ANCHOR
         LTR   X1,X1               ANYTHING THERE .Q
         BZ    OOPRT09             GO TO RETURN IF NOT
         USING OODSECT,X1          TELL THE ASSEMBLER
        $IFON  TTYPE,TONLINE,OOPRT02    BRANCH IF ONLINE
         LR    R1,R0               OUTPUT BUFFER
         CLI   4(R1),SUPRSPAC      PRINTER OVERLAY LINE .Q
         BE    OOPRT09             YES, GO TO RETURN
OOPRT02  DS    0H
         L     COUNT,OOQCNT        OVERLAY COUNT
         S     COUNT,F1            LESS ONE
         BNM   OOPRT03             BRANCH IF WE CAN DO IT
         LA    R1,OOANCH-OOQFCHN+OODSECT   PTR TO HEAD OF CHAIN
        $CALL  OVREMOV             REMOVE FIRST ENTRY
         B     OOPRT01             AND TRY AGAIN
         SPACE
OOPRT03  DS    0H
         ST    COUNT,OOQCNT        UPDATE OUTPUT OVERLAY COUNT
         LH    COUNT,OOQRDW        LENGTH OF OVERLAY BUFFER
         LTR   COUNT,COUNT         ANYTHING THERE .Q
         BNP   ABOO01              BRANCH TO QUIT IF NOTHING
         LR    R1,R0               OUTPUT BUFFER
        $IFON  TTYPE,TONLINE,OOPRT04    BRANCH IF ONLINE
         LA    COUNT,1(,COUNT)     PLUS CARRIAGE CONTROL
OOPRT04  DS    0H
         LA    COUNT,4(,COUNT)     PLUS RDW LENGTH
         AH    COUNT,ADJCNT        PLUS CURRENT ADJUST VALUE
         MVC   SAVEH0,0(R1)        ALIGN CURRENT RECORD LENGTH
         CH    COUNT,SAVEH0        IS OVERLAY LONGER .Q
         BNH   OOPRT05             BRANCH IF NOT
         STH   COUNT,SAVEH0        ALIGN LONGER RECORD LENGTH
         MVC   0(2,R1),SAVEH0      NEW LENGTH SAME AS OVERLAY
OOPRT05  DS    0H
         LA    R1,3(,R1)           POINT TO OUTPUT DATA-1
        $IFON  TTYPE,TONLINE,OOPRT06    BRANCH IF ONLINE
         LA    R1,1(,R1)           PLUS CARRIAGE CONTROL
OOPRT06  DS    0H
         AH    R1,ADJCNT           PLUS ADJUST VALUE
         LH    COUNT,OOQRDW        LENGTH OF OVERLAY
         AR    R1,COUNT            END OF OUTPUT STRING-1
OOPRT07  DS    0H
         CLI   0(R1),C' '          BLANK IN OUTPUT LINE .Q
         BNE   OOPRT08             BRANCH IF NOT
         IC    TEMP,OOQDATA-1(COUNT)    GET OVERLAY CHARACTER
         STC   TEMP,0(,R1)         PUT IN OUTPUT LINE
        $IFOFF SWITCHS,TRSWS,OOPRT08    BRANCH IF NO TRANSLATE
         L     TEMP,TRANADO        ADDRESS OF OUTPUT TRANSLATE TABLE
         TR    0(1,R1),0(TEMP)     CONVERT SINGLE CHARACTER
OOPRT08  DS    0H
         BCTR  R1,0                BACKUP OUTPUT POINTER
         BCT   COUNT,OOPRT07       DO AS REQUIRED
         L     COUNT,OOQCNT        GET CURRENT COUNT LEFT
         LTR   COUNT,COUNT         TEST IT
         BP    OOPRT09             BRANCH IF SOME LEFT
         LA    R1,OOANCH-OOQFCHN+OODSECT     PTR TO ANCHOR ELEMENT
        $CALL  OVREMOV             REMOVE CURRENT ENTRY
         SPACE
OOPRT09  DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         DROP  X1
         SPACE
ABOO01   DS    0H
        $LOGERR 1012               NO OUTPUT OVERLAY DATA
