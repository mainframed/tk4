         MACRO
&LABEL  $MSGSECT &A,&B,&DOC=NOGEN,&TYPE=
         LCLA  &N,&I
         LCLC  &C
&N       SETA  61
         AIF   ('&TYPE' EQ 'FLAG').FLAG01
         AIF   ('&TYPE' EQ 'MSG').MSG01
         AIF   ('&A' NE '' AND '&B' NE '').TEXT01
         MNOTE 8,'$MSGSECT -- INVALID ARGUMENTS.'
         MEXIT
.FLAG01  ANOP
ERRFLGS  DC    XL(&N+3)'00'        ERROR FLAGS
&I       SETA  0
.FLAG02  ANOP
&C       SETC  '&I'
         AIF   (&I GE 100).FLAG04
         AIF   (&I GE 10).FLAG03
&C       SETC  '0&C'
.FLAG03  ANOP
&C       SETC  '0&C'
.FLAG04  ANOP
EFLAG&C  EQU   ERRFLGS+&I
&I       SETA  &I+1
         AIF   (&I LE &N).FLAG02
EFLAG998 EQU   ERRFLGS+&N+1
EFLAG999 EQU   ERRFLGS+&N+2
         MEXIT
.*
.*
.MSG01   ANOP
         PUSH  PRINT
         PRINT &DOC
***********************************************************************
*                                                                     *
*              ERROR MESSAGES                                         *
*                                                                     *
***********************************************************************
         SPACE
&LABEL   DS    0H
         SPACE
*        THE START OF MESSAGE AREA CONTAINS A TABLE OF TWO BYTE
*        OFFSETS, CORRESPONDING TO BYTE OFFSETS IN 'ERRFLGS'.
*
*        THESE OFFSETS POINT TO A BYTE CONTAINING LENGTH-1
*        OF MESSAGE TEXT THAT FOLLOWS IMMEDIATELY.
         SPACE
&I       SETA 0
.MSG02   ANOP
&C       SETC  '&I'
         AIF   (&I GE 100).MSG04
         AIF   (&I GE 10).MSG03
&C       SETC  '0&C'
.MSG03   ANOP
&C       SETC  '0&C'
.MSG04   ANOP
         DC    AL2(ERR&C.M-&LABEL)      ERROR OFFSET
&I       SETA  &I+1
         AIF   (&I LE &N).MSG02
         DC    AL2(ERR998M-&LABEL)      OFFSET OF LOGIC ERROR
         DC    AL2(ERR999M-&LABEL)      OFFSET OF SPIE ERROR
.*
        $MSGSECT 000,'UNUSED'
        $MSGSECT 001,'UNRECOGNIZABLE CODES IN PARM FIELD'
        $MSGSECT 002,'OUTPUT FILE (SYSPRINT) CANNOT BE OPENED'
        $MSGSECT 003,'OMITTED CONTROL WORD PARAMETER'
        $MSGSECT 004,'UNDEFINED CONTROL WORD'
        $MSGSECT 005,'ILLEGAL CONTROL WORD PARAMETER VALUE'
        $MSGSECT 006,'OMITTED CONTROL WORD TERMINATION'
        $MSGSECT 007,'TOO MANY REFERENCE NAMES'
        $MSGSECT 008,'REFERENCE NAME OR ARGUMENT TOO LONG'
        $MSGSECT 009,'LOOP IN REFERENCE NAME SUBSTITUTION'
        $MSGSECT 010,'OUTPUT LINE HAS ZERO WIDTH'
        $MSGSECT 011,'SAVE/RESTORE STATUS STACK ERROR'
        $MSGSECT 012,'ILLEGAL PLACEMENT OF CONTROL WORD'
        $MSGSECT 013,'CONTROL WORD PARAMETER TOO BIG'
        $MSGSECT 014,'CONTROL WORD PARAMETER TOO SMALL'
        $MSGSECT 015,'MISMATCHED DELIMITER ON STRING'
        $MSGSECT 016,'HANGING INDENT INVALID'
        $MSGSECT 017,'MACRO/REMOTE NAME UNDEFINED'
        $MSGSECT 018,'INSUFFICIENT STORAGE FOR MACRO/REMOTE'
        $MSGSECT 019,'TOO MANY RECORDS IN ONE MACRO/REMOTE'
        $MSGSECT 020,'EXCESSIVE NUMBER OF LINE ELEMENTS'
        $MSGSECT 021,'BACKSPACE BEFORE COLUMN ONE'
        $MSGSECT 022,'.IF NESTING TOO DEEP'
        $MSGSECT 023,'.TH OR .EL NOT AFTER .IF'
        $MSGSECT 024,'WORKING STORAGE EXHAUSTED'
        $MSGSECT 025,'DUPLICATE LABEL'
        $MSGSECT 026,'FORWARD .GO TARGET NOT FOUND'
        $MSGSECT 027,'BACKWARD .GO TARGET NOT FOUND'
        $MSGSECT 028,'INCORRECT PLACEMENT OF .DO OR .EN'
        $MSGSECT 029,'POSITIONAL PARAMETER ASSUMED'
        $MSGSECT 030,'HYPHEN DELETE WORD NOT FOUND'
        $MSGSECT 031,'INVALID WORD TO HYPHENATE'
        $MSGSECT 032,'TOO MANY HYPHEN BREAK POINTS'
        $MSGSECT 033,'HYPHEN WORD TOO LONG'
        $MSGSECT 034,'TOO MANY QUEUED LINES FOR '
        $MSGSECT 035,'CONTROL WORD PARAMETER SHOULD BE NUMERIC'
        $MSGSECT 036,'UNUSED'
        $MSGSECT 037,'HEADNOTE TOO LONG FOR PAGE'
        $MSGSECT 038,'INVALID SUBSCRIPT'
        $MSGSECT 039,'INVALID SUBSTRING'
        $MSGSECT 040,'INVALID CONDITION OPERATOR IN .IF'
        $MSGSECT 041,'.HM + .HS GREATER THAN .TM'
        $MSGSECT 042,'.FM + .FS GREATER THAN .BM'
        $MSGSECT 043,'ILLEGAL CONTROL WORD WITHIN KEEP OR FOOTNOTE'
        $MSGSECT 044,'GENERATED INPUT LINE TOO LONG'
        $MSGSECT 045,'.TM + .BM TOO BIG FOR .PL'
        $MSGSECT 046,'UNUSED'
        $MSGSECT 047,'UNUSED'
        $MSGSECT 048,'UNUSED'
        $MSGSECT 049,'UNUSED'
        $MSGSECT 050,'.ER '
        $MSGSECT 051,'FILE NOT FOUND'
        $MSGSECT 052,'FILE NOT PARTITIONED, MEMBER NAME IGNORED'
        $MSGSECT 053,'BUFFER STORAGE UNAVAILABLE, FILE SKIPPED'
        $MSGSECT 054,'RECORD FORMAT NOT SUPPORTED, FILE SKIPPED'
        $MSGSECT 055,'INVALID INPUT RECORD, FILE SKIPPED'
        $MSGSECT 056,'I/O ERROR'
        $MSGSECT 057,'SCRIPT -- SYSTERM FAILED TO OPEN'
        $MSGSECT 058,'UNABLE TO OPEN WORK FILE'
        $MSGSECT 059,'UNUSED'
        $MSGSECT 060,'UNUSED'
        $MSGSECT 061,'UNUSED'
        $MSGSECT 998,'A LOGIC ERROR HAS BEEN DETECTED'
        $MSGSECT 999,'A PROGRAM CHECK HAS OCCURRED'
         POP   PRINT
         MEXIT
.*
.*
.TEXT01  ANOP
&C       SETC  '&A'.'*  '
&I       SETA  K'&B+K'&C-2
         SPACE
ERR&A.M  DC    AL1(&I),C'&C',C&B,C'.'
         MEND
