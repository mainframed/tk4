***********************************************************************
*                                                                     *
*              .CD (COLUMN DEFINITION) --- CONTROL                    *
*                                                                     *
***********************************************************************
         SPACE
CD      $ENTER ,
         CLI   ICORFLG,ICORNONE    IN 'NO KEEP' .Q
         BNE   CDE043              ERROR IF NOT
        $CALL  PRINTNB             CLEAR PRINT LINE
         L     COUNT,CLLZSAVE      COLUMN LENGTH VALUE
         LTR   COUNT,COUNT         HAS IT BEEN SET .Q
         BZ    CD01                BRANCH IF NOT
         ST    COUNT,CLLZ          SET COLUMN LENGTH
         SPACE
CD01     DS    0H
         CLI   ICOLFLG,ICOLNONE    IN NO COLUMN MODE .Q
         BE    CD01A               BRANCH IF YES
         CLI   ICOLFLG,ICOLSC      IN SINGLE COLUMN MODE .Q
         BNE   CD02                BRANCH IF NOT
         MVI   ICOLFLG,ICOLMC      BACK TO MULTI-COLUMN
         SR    COUNT,COUNT         ZERO WORK REG
         ST    COUNT,SCCOUNT       ZERO SINGLE COLUMN NEST
CD01A    DS    0H
         MVC   CDPLCT,PLCT         SAVE LINES REMAINING IN COLUMN
         SPACE
CD02     DS    0H
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BNE   CD03                BRANCH IF NOT
        $CALL  COLPRT              PRINT COLUMNS TO THIS POINT
         SPACE
CD03     DS    0H
        $CALL  GETNUM              GET NUMBER OF COLUMNS
         BC    BOMIT,CD07          BRANCH IF NONE
         BC    BDNO,CDE005         BRANCH IF INVALID
         CLI   NUMSIGN,C' '        WAS IT UNSIGNED .Q
         BE    CD04                BRANCH IF YES
         A     COUNT,CDCOUNT       ADJUST FORMER VALUE
CD04     DS    0H
         LTR   COUNT,COUNT         TOO SMALL .Q
         BNP   CDE014              ERROR IF YES
         LA    R0,CDMAXC           MAXIMUM ALLOWED
         CR    COUNT,R0            TOO BIG .Q
         BH    CDE013              ERROR IF YES
         ST    COUNT,CDCOUNT       SET NEW VALUE
         SR    X1,X1               KEEP LAST OPERAND VALUE
         SR    X2,X2               OPERAND COUNTER
         LA    X3,CDVALC           TARGET OFFSET RESULTS
         SPACE
CD05     DS    0H
        $CALL  GETNUM              GET OPERAND
         BC    BOMIT,CD07          BRANCH IF DONE
         BC    BDNO,CDE005         BRANCH IF INVALID
         LA    X2,1(,X2)           BUMP OPERAND NUMBER
         LA    R0,CDMAXC           MAXIMUM ALLOWED
         CR    X2,R0               TOO BIG .Q
         BH    CDE013              ERROR IF YES
         CLI   NUMSIGN,C' '        UNSIGNED OPERAND .Q
         BE    CD06                BRANCH IF YES
         AR    COUNT,X1            RELATIVE TO LAST
CD06     DS    0H
         LTR   COUNT,COUNT         TOO SMALL .Q
         BM    CDE014              ERROR IF YES
         C     COUNT,DEFLL+TAB3MAX TOO BIG .Q
         BH    CDE013              ERROR IF YES
         STC   COUNT,0(,X3)        ENTER RESULT
         LA    X3,1(,X3)           NEXT COLUMN TARGET
         LR    X1,COUNT            FORMER VALUE
         B     CD05                DO IT AGAIN
         SPACE
CD07     DS    0H
         LA    COUNT,1             TEST ONLY ONE COLUMN
         C     COUNT,CDCOUNT       ONLY ONE COLUMN .Q
         BNE   CD08                BRANCH IF NOT
         CLI   CDVALC,X'00'        STARTING AT THE BEGINNING .Q
         BNE   CD08                BRANCH IF NOT
         MVI   ICOLFLG,ICOLNONE    NORMAL AGAIN
         B     CD09                GO TO RETURN
         SPACE
CD08     DS    0H
         MVI   ICOLFLG,ICOLMC      MULTI-COLUMN OUTPUT
         LA    COUNT,1             FIRST COLUMN
         ST    COUNT,CDCURR        PROCESS FIRST COLUMN FIRST
         MVC   CDPLCT,PLCT         SAVE LINES REMAINING FOR COLUMN
         SPACE
CD09     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
CDE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID OPERAND
         B     CD09                GO TO RETURN
CDE013   DS    0H
         MVI   EFLAG013,ERRSET     TOO BIG OR TOO MANY
         B     CD09                GO TO RETURN
CDE014   DS    0H
         MVI   EFLAG014,ERRSET     TOO SMALL
         B     CD09                GO TO RETURN
CDE043   DS    0H
         MVI   EFLAG043,ERRSET     INVALID CONTROL WORD IN KEEP
         B     CD09                GO TO RETURN
