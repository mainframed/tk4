***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'PRINT' --- PRINT LINE FROM OUTLINK  *
*                                                                     *
***********************************************************************
         SPACE
PRINT   $ENTER ,
         OC    OLDCOUNT,OLDCOUNT   IS PRINT BUFFER EMPTY .Q
         BZ    PROF7               YES, RETURN
        $IFON  SWITCHS,HDPSWS,XPRT05    BRANCH IF HEADING/FOOTING
         CLI   ICORFLG,ICORNONE    ARE WE FORMATTING INTO CORE .Q
         BNE   XPRT02              BRANCH IF YES
        $IFON  SWITCHS,FNOCT,XPRT02     BRANCH IF 'DONT COUNT' LINE
PRDEC    DS    0H
        $OFF   SWITCHS,OOSWS       REENABLE OUTPUT OVERLAY FOR WIDOW
         LH    COUNT,PLCT          GET LINE COUNTER
         CH    COUNT,RMTRIG        IS THIS THE REMOTE TRIGGER .Q
         BNE   PRDEC01             IF NOT, SKIP
         LTR   R1,COUNT            ANY BOTTOM TITLE INFORMATION .Q
         BZ    PRDEC01             BRANCH IF NOT
        $CALL  RMIMBED             YES, GO IMBED REMOTE SEQUENCE
         B     PRDEC               CHECK FOR ANOTHER REMOTE RIGHT NOW
PRDEC01  DS    0H
         BCTR  COUNT,0             DECREMENT LINE BY 1
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    COUNT,R1            AT BOTTOM OF PAGE .Q
         BNL   WIDOW               NO, CHECK IF WIDOW
PRDEC02  DS    0H
         CLI   ICOLFLG,ICOLMC      MULTI-COLUMN OUTPUT .Q
         BNE   PRDEC04             BRANCH IF NOT
         L     COUNT,CDCURR        CURRENT OUTPUT COLUMN
         C     COUNT,CDCOUNT       IN THE LAST COLUMN .Q
         BL    PRDEC03             BRANCH IF NOT
        $CALL  PAGEREAL            A REAL PAGE EJECT NOW PLEASE
         B     PRDEC               AND TRY AGAIN
PRDEC03  DS    0H
        $CALL  COLNEW              DO A COLUMN EJECT
         B     PRDEC               AND TRY AGAIN
         SPACE
PRDEC04  DS    0H
        $CALL  PAGE                TERMINATE CURRENT PAGE/COLUMN NOW
         B     PRDEC               AND TRY AGAIN
         SPACE
FRSTLNE  DS    0H
*  THIS IS FOR FIRST TEXT LINE TO BE PRINTED AFTER A BREAK
        $OFF   SWITCHS,FLNESWS     NOT FIRST LINE
         SH    COUNT,DSCNT         LESS LINE SPACING FACTOR
         S     COUNT,F1            LESS CURRENT LINE
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    COUNT,R1            NEED AT LEAST THREE LINES LEFT
         BH    WIDOW02             BRANCH IF OK
         B     WIDOW01             ELSE GO TERMINATE CURRENT PAGE NOW
         SPACE
FRSTSET  DS    0H
*  THIS IS ENTERED WHEN A BREAK IS ENCOUNTERED
        $ON    SWITCHS,FLNESWS     NEXT LINE IS FIRST AFTER BREAK
         B     XPRT01              AND CONTINUE
         SPACE
WIDOW    DS    0H                                               *SOCOL*
        $IFOFF SWITCHS,WDSWS,XPRT01     BRANCH IF NO WIDOW REQUIRED
        $IFON  SWITCHS,HDPSWS,XPRT01    BRANCH IF IN HEADING/FOOTING
         L     TEMP,PTELLNG        LENGTH LEFT TO PROCESS
         LTR   TEMP,TEMP           LAST LINE OF PARAGRAPH .Q
         BZ    FRSTSET             YES, NO PROBLEM
         SH    COUNT,DSCNT         LESS LINE SPACING FACTOR
        $IFON  SWITCHS,FLNESWS,FRSTLNE  BRANCH IF FIRST LINE
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    COUNT,R1            LAST TEXT LINE ON PAGE .Q
         BH    WIDOW02             BRANCH IF NOT
         L     TEMP,CLLZ           GET LINE/COLUMN LENGTH
         SRL   TEMP,2              DIVIDE BY FOUR
         A     TEMP,CLLZ           1.25 TIMES LINE/COLUMN LENGTH
         C     TEMP,PTELLNG        IF 1.25 * LL IN LINKAREA, THAT'S
         BL    WIDOW02             TWO LINES ON NEXT PAGE, NO EJECT
WIDOW01  DS    0H
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY FOR SPACE(S)
         B     PRDEC02             AND CAUSE PAGE EJECT
         SPACE
WIDOW02  DS    0H
         LH    COUNT,PLCT          GET CURRENT PAGE LENGTH
         BCTR  COUNT,0             RESTORE LINE COUNT
         SPACE
XPRT01   DS    0H
         STH   COUNT,PLCT          SET REMAINDER OF PAGE COUNT
         SPACE
XPRT02   DS    0H
        $CALL  PRSKSPC             ADD ANY CONDITIONAL SPACE/SKIP
XPRT05   DS    0H
        $IFOFF SWITCHS,JUADSWS,XPRT06   BRANCH IF NO ADJUSTMENT
         LA    R1,LINKPARM         PARM OF AREA TO ADJUST
        $CALL  ADJUST              DO REQUIRED LINE JUSTIFICATION
XPRT06   DS    0H
         L     R1,OLDFIRST         SET POINTER TO FIRST ELEMENT
         USING LINKELEM,R1         AND TELL THE ASSEMBLER
         SR    TEMP,TEMP           CLEAR WORK REGISTER
         IC    TEMP,LINKMULT       GET MULTIPLIER
         AH    TEMP,ADJCNT         CENTER IF REQUIRED
         STC   TEMP,LINKMULT       AND RESET
         L     COUNT,NOCHARS       GET LINE SIZE
         AH    COUNT,ADJCNT        PLUS ADJUSTMENT
         ST    COUNT,NOCHARS       SET NEW LINE SIZE
         DROP  R1
         SPACE
        $OFF   SWITCHS,FOVLY       NO PRINTER OVERLAY NEEDED YET
        $IFOFF SWITCHS,FNOCT,PROF       BRANCH IF 'DONT COUNT' LINE
        $ON    SWITCHS,FOVLY       IF YES, PRETEND OVERLAY LINE
         SPACE
PROF     DS    0H
         L     X3,OUTPARM          OUTPUT RECORD
         S     X3,F4               POINT AT RDW
         MVC   5(L'BUFF1,X3),BLANKS     BLANK THE BUFFER
         SPACE
        $IFON  TTYPE,TONLINE,PRTYP BRANCH IF ONLINE
         ST    COUNT,NOCHARS       RESTORE LINE LENGTH
         LA    R1,LINKPARM         SET POINTER TO LINE PARAMETERS
        $CALL  LKGETP              STRING OUT LINE FOR PRINTER
         L     TEMP,NOCHARS        SETTING LINE LENGTH
         LA    TEMP,4(TEMP)
         STH   TEMP,0(,X3)         STORE IT AT BEGINNING OF LINE (RDW)
         MVI   4(X3),C' '          SET FOR SINGLE SPACE
        $IFOFF SWITCHS,FOVLY,PROF1 BRANCH IF OVERLAY NOT NEEDED
         MVI   4(X3),SUPRSPAC      YES, SET FOR NO SPACE
PROF1    DS    0H
         LR    R0,X3               SET RECORD POINTER FOR OFFLINE
         B     PRBOTH              ..
         SPACE
PRTYP    DS    0H
         LA    R1,LINKPARM         SET POINTER TO LINE PARAMETERS
        $IFOFF TTYPE,TSCREEN,PRTYP01    BRANCH IF NOT A CRT
        $CALL  LKGETP              FOR A CRT, STRING OUT LIKE PRINTER
         B     PRTYP02             AND CONTINUE
         SPACE
PRTYP01  DS    0H
        $CALL  LKGETT              STRING OUT LINE FOR TYPEWRITER
PRTYP02  DS    0H
         L     TEMP,NOCHARS        GET LENGTH
         LA    TEMP,3(,TEMP)       INCREMENT FOR LENGTH OF RDW
         MVC   1(4,X3),F0          ZERO REC DESC WORD FIELD
         STC   TEMP,2(,X3)         STORE LINE LENGTH
         LA    R0,1(,X3)           SET RECORD POINTER FOR ONLINE
         SPACE
PRBOTH   DS    0H
         CLI   ICORFLG,ICORNONE    ARE WE FORMATTING INTO CORE .Q
         BNE   PRBOTH1             BRANCH IF YES
        $IFOFF SWITCHS,TRSWS,PRBOTH1    BRANCH IF NO TRANSLATE
         L     R1,TRANADO          ADDRESS OF OUTPUT TRANSLATE TABLE
         L     TEMP,NOCHARS        LENGTH OF ACTUAL SCRIPT LINE
         BCTR  TEMP,0              LENGTH LESS ONE FOR SS
         EX    TEMP,TRANSLAT       TRANSLATE LINE
         SPACE
PRBOTH1  DS    0H
        $IFON  SWITCHS,HDPSWS,NONUM     BRANCH IF HEADING/FOOTING
        $IFOFF SWITCHS,NUSWS,NONUM BRANCH IF NO LINE NUMBERING
         L     R1,DEFNUM+TAB3DEF   GET NUMBER COLUMN OFFSET
         LTR   R1,R1               IS IT POSITIVE .Q
         BNP   NONUM               NO, NO NUMBERS THEN
        $CALL  PRNUM               ENTER NUMBER/FILE
         SPACE
NONUM    DS    0H
        $CALL  PRRCADD             ADD ANY REVISION CODES
         SPACE
         LH    X1,DARKVAL          GET DARK OUTPUT VALUE
PROFDA   DS    0H
         CLI   ICORFLG,ICORNONE    ARE WE FORMATTING INTO CORE .Q
         BNE   NONUM1              BRANCH IF YES
        $CALL  IOPRINT             NO, PRINT THE LINE
         B     PROF2               SKIP FOOTNOTE SAVE
NONUM1   DS    0H
        $CALL  FNSAVE              QUEUE FOOTNOTE LINE
         SPACE
PROF2    DS    0H
        $IFON  TTYPE,TONLINE,PROF2A     BRANCH IF ONLINE
        $ON    SWITCHS,FOVLY       OVERLAID IF DARK
        $ON    SWITCHS,OOSWS       DISABLE OUTPUT OVERLAY, DONE ALREADY
         LR    R1,R0               POINTER TO RECORD RDW
         MVI   4(R1),SUPRSPAC      OVERLAY CARRIAGE CONTROL
         BCT   X1,PROFDA           DARK OUTPUT AS REQUIRED
PROF2A   DS    0H
        $OFF   SWITCHS,OOSWS       REENABLE OUTPUT OVERLAY
         CLC   OLDCOUNT,F0         ANY LEFT FROM PRINTER OUTPUT .Q
         BE    PROF3               IF NOT, DONE WITH LINE
        $ON    SWITCHS,FOVLY       YES, NOTE FOR NEXT TIME THROUGH
         B     PROF                DO NEXT LAYER ON PRINTER OR CRT
         SPACE
PROF3    DS    0H
        $OFF   SWITCHS,FOVLY       NO MORE OVERLAID LINES
         LH    TEMP,INDL           ADJUST INDENT FACTOR
         AH    TEMP,OFFLI          ..
         STH   TEMP,INDL           ..
         STH   TEMP,RMARGIN        SET RIGHT MARGIN
         XC    UNDL(2),UNDL        RESET UNDENT
         XC    OFFLI,OFFLI         RESET OFFSET
         XC    OFUNCNT,OFUNCNT     RESET OFFSET/UNDENT MARGIN
        $IFON  SWITCHS,HDPSWS,PROF7     NO LINE SPACING IN HEADINGS
         LH    COUNT,DSCNT         GET LINE SPACING FACTOR
         LTR   COUNT,COUNT         ANY LINE SPACING PRESENT .Q
         BNP   PROF7               BRANCH IF NONE PRESENT
         CLI   ICORFLG,ICORNONE    ARE WE FORMATTING INTO CORE .Q
         BNE   PROF5               YES, DO IT ONE BLANK LINE AT A TIME
         LH    TEMP,PLCT           LINES LEFT ON THIS PAGE
         SH    TEMP,BMMRG          LESS BOTTOM MARGIN
         SH    TEMP,FNSEP          LESS FOOTNOTE MARGIN
         BNP   PROF7               BRANCH IF NONE LEFT
         CR    COUNT,TEMP          COMPARE NEEDED TO AVAILABLE
         BNH   PROF4               BRANCH IF OK
         LR    COUNT,TEMP          DO THE LESSER
PROF4    DS    0H
        $ON    SKSPFLG,SKSPSK      THIS IS A SKIP
        $CALL  SPEX                DO EXECUTION OF REAL SPACING
         B     PROF7               GO TO RETURN
PROF5    DS    0H
        $ON    SKSPFLG,SKSPSK      THIS IS A SKIP
         MVI   PRNTRCC,SINGLSP     SINGLE SPACE CARRIAGE CONTROL
         LA    R0,BLNKLINE         POINT TO BLANK LINE RDW
PROF6    DS    0H
        $CALL  FNSAVE              SAVE BLANK LINE
         BCT   COUNT,PROF6         LOOP TILL DONE
         MVI   SKSPFLG,X'00'       CLEAR SPACING FLAGS
         SPACE
PROF7    DS    0H
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              IF ZERO, ONLY SINGLE SPACE
         SPACE
TRANSLAT TR    5(*-*,X3),0(R1)     EXECUTED TRANSLATE
