***********************************************************************
*                                                                     *
*              VTRAPS ROUTINE --- REPLACE WITH CALLER'S TRAP ADDRS    *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  COMPARM3 IS ADDR OF OVERRIDING VCONS
*
*        ON EXIT   --  CC = BPOS  IF OK
*                      CC = BNEG  IF NO ROUTINE MATCH
         SPACE
VTRAPS  $ENTER ,
         L     TEMP,COMPARM3       CALLER'S OVERRIDING VCON LIST
         LA    TEMP,0(,TEMP)       CLEAR HIGH BYTE
         LTR   TEMP,TEMP           ANY LIST PRESENT .Q
         BZ    VTR05               BRANCH TO SUCCESS IF NOT
VTR01    DS    0H
         L     COUNT,0(,TEMP)      NEXT ROUTINE ENTRY ADDR
         LA    COUNT,0(,COUNT)     CLEAR HIGH BYTE
         LTR   COUNT,COUNT         ANYTHING THERE .Q
         BZ    VTR04               BRANCH IF NOT
         SPACE
         L     X3,VSCRICVT         COMMUNICATION VECTOR TABLE
         USING SCRICVT,X3          AND TELL THE ASSEMBLER
         LA    X1,VFSTATS          START OF ROUTINE ADDRS
         LA    X2,L'VFSTATS        ENTRY LENGTH
         LA    X3,VFSTATE          END OF ROUTINE ADDRS
         DROP  X3
VTR02    DS    0H
         L     R1,0(,X1)           NEXT CANDIDATE
         LTR   R1,R1               ANYTHING THERE .Q
         BNP   VTR03               BRANCH IF NOT
         USING SCVTDSCT,R1         CANDIDATE AREA
         CLC   SCVTCSCT,SCVTCSCT-SCVTDSCT(COUNT)  SAME NAME .Q
         DROP  R1
         BNE   VTR03               BRANCH IF NOT
         XC    1(3,X1),1(TEMP)     INTERCHANGE
         XC    1(3,TEMP),1(X1)     ENTRY
         XC    1(3,X1),1(TEMP)     ADDRESSES
         B     VTR04               GO CHECK FOR END
VTR03    DS    0H
         BXLE  X1,X2,VTR02         CHECK THEM ALL
        $CC    NEG                 ERROR CONDITION CODE
         B     VTR06               GO TO RETURN
         SPACE
VTR04    DS    0H
         TM    0(TEMP),X'80'       END OF USER'S LIST .Q
         LA    TEMP,4(,TEMP)       NEXT USER ADDRESS
         BNO   VTR01               BRANCH IF NOT LAST
VTR05    DS    0H
        $CC    POS                 SUCCESSFUL CONDITION CODE
         SPACE
VTR06    DS    0H
        $EXIT  RC=0                RETURN TO CALLER
