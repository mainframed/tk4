***********************************************************************
*                                                                     *
*              HYSCN  ---  SCAN HYPHENATION WORD                      *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  WORD IN INPUT LINE BUFFER
*        ON EXIT   --  CC = BDNO   IF SOME ERROR
*                      CC = BPOS   IF SUCCESSFUL
*        IF SUCCESSFUL HYPIND(4)  CONTAINS BREAK OFFSETS
*                      HYWKWORD(8) CONTAINS RESULTING WORD
*                      (R1) CONTAINS FIRST CHAR IN INTERNAL FORM
         SPACE
HYSCN   $ENTER ,
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              GET HYPHENATED WORD
         BC    BOMIT,HYEOMIT       ERROR IF OMITTED
         C     TEMP,HYMAXWRD       MAXIMUM WORD LENGTH
         BNL   HYETLONG            INVALID IF GREATER
         L     R1,HYPRATAD         ADDR PRATT FREQUENCY TABLE
         MVC   HYDCWORD,BLANKS     BLANK TARGET AREA
         EX    TEMP,HYSMVC         MOVE RESULT TO WORK AREA
         TR    HYDCWORD,0(R1)      TRANSLATE TO INTERNAL FORM
         SPACE
         XC    HYPLAST,HYPLAST     CLEAR POSITION OF LAST HYPHEN
         MVI   HYPIND,X'FF'        INITIALIZE HYPHEN OFFSETS
         MVC   HYPIND+1(L'HYPIND-1),HYPIND   ALL OF THEM
         LA    R1,HYDCWORD         START OF STRING
         LA    X1,HYWKWORD         START STRIPPED WORD AREA
         LA    X2,HYPIND           START OF HYPHEN BREAK FIELD
         SR    COUNT,COUNT         ZERO CHARACTER COUNT
         MVI   HYALPHSW,X'00'      CLEAR ALPHA SWITCH
         MVI   HYWKWORD,L'HYALPHA  INTERNAL BLANK TO TARGET
         MVC   HYWKWORD+1(L'HYWKWORD-1),HYWKWORD  ALL OF IT
         SPACE
HYALPHCH EQU   X'01'                    ALPHA CHARACTER SWITCH
HYS01    DS    0H
         CLI   0(R1),X'1C'         TEST FOR HYPHEN CHARACTER
         BE    HYS03               BRANCH IF EQUAL
         BH    HYS05               BRANCH IF BLANK
HYS02    DS    0H
         MVC   0(1,X1),0(R1)       MOVE CHAR TO STRIPPED AREA
         LA    COUNT,1(,COUNT)     BUMP CHARACTER COUNT
         LA    X1,1(,X1)           BUMP STRIPPED AREA PTR
        $ON    HYALPHSW,HYALPHCH   TURN ON CHARACTER SWITCH
         B     HYS04               GO INCREMENT PTRS
         SPACE
HYSMVC   MVC   HYDCWORD(*-*),0(COUNT)   TO WORK AREA
         SPACE
HYS03    DS    0H
        $IFOFF HYALPHSW,HYALPHCH,HYEINVLD    ERROR IF NO ALPHA FIRST
        $OFF   HYALPHSW,HYALPHCH   RESET PRECEEDING ALPHA SWITCH
         LA    R0,HYPIND+L'HYPIND-1     LAST BREAK POINT+1 ALLOWED
         CR    X2,R0               TOO MANY HYPHENS IN WORD .Q
         BNL   HYETMANY            BRANCH IF TOO MANY
         S     COUNT,HYPLAST       LENGTH OF CURRENT SYLLABLE
         C     COUNT,F15           SYLLABLE TOO LONG .Q
         BNL   HYETLONG            BRANCH IF OVER MAX
         STC   COUNT,0(,X2)        SYLLABLE LENGTH IN HYPHEN FIELD
         A     COUNT,HYPLAST       RESTORE NUMBER OF CHARS SO FAR
         ST    COUNT,HYPLAST       AND SAVE IT
         LA    X2,1(,X2)           BUMP HYPHEN INDICATION POS'N
         SPACE
HYS04    DS    0H
         LA    R1,1(,R1)           BUMP INPUT POINTER
         B     HYS01               GET NEXT CHARACTER
         SPACE
HYS05    DS    0H
         CLI   0(R1),X'20'         INVALID CHAR IN INPUT WORD .Q
         BH    HYEINVLD            BRANCH IF YES
         MVI   0(X2),X'0F'         SET HYPHEN END INDICATION
         PACK  HYPIND,HYPIND       PACK HYPHEN INDICATION FIELD
         MVC   HYPIND(4),HYPIND+4  LEFT ADJUST RETURN VALUE
         SPACE
*        THE FOLLOWING ROUTINE DECOMPOSES ALL WORDS GREATER THAN
*        13 CHARACTERS IN LENGTH, DOWN TO THE MAXIMUM.
         SPACE
         BCTR  COUNT,0             GET LENGTH-1
         STH   COUNT,NUMCHR        SET LENGTH-1 OF WORD STRING
        $CALL  DCOMPOZ             DECOMPOSE THE WORD
        $CC    POS                 INDICATE SUCCESS
         B     HYSRET              AND GO TO TERMINATION
         SPACE
HYEOMIT  DS    0H
         MVI   EFLAG003,ERRSET     OMITTED OPERAND
         B     HYSERR              ERROR RETURN
HYEINVLD DS    0H
         MVI   EFLAG031,ERRSET     INVALID HYPHENATION WORD
         B     HYSERR              ERROR RETURN
HYETMANY DS    0H
         MVI   EFLAG032,ERRSET     TOO MANY BREAK POINTS
         B     HYSERR              ERROR RETURN
HYETLONG DS    0H
         MVI   EFLAG033,ERRSET     HYPHEN WORD TOO LONG
         B     HYSERR              ERROR RETURN
HYSERR   DS    0H
        $CC    DNO                 ERROR CONDITION CODE
         SPACE
HYSRET   DS    0H
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
