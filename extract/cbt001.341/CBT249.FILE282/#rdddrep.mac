***********************************************************************
*                                                                     *
*              'RDDDREP'  ---  REPLACEMENT FILENAME ROUTINE           *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1)  POINTS TO FILENAME
*
*        ON EXIT   --  (R1)  POINTS TO REPLACEMENT FILENAME
*                      CC = BPOS  IF REPLACEMENT FOUND
*                      CC = BNEG  IF NO REPLACEMENT FOUND
         SPACE
RDDDREP $ENTER ,
         L     X1,COMREPAD         POINTER TO REPLACEMENT DDNAME LIST
         LTR   X1,X1               ANY SUCH LIST .Q
         BZ    RDREP02             BRANCH IF NOT
         LH    COUNT,0(,X1)        NUMBER OF NAME PAIRS IN LIST
         LTR   COUNT,COUNT         ANY PRESENT .Q
         BNP   RDREP02             BRANCH IF NONE
         SPACE
         LA    X1,2(,X1)           ADDR OF FIRST DDNAME PAIR
RDREP01  DS    0H
         CLC   0(8,X1),0(R1)       OLD NAME IN LIST .Q
         BE    RDREP03             BRANCH IF YES
         LA    X1,16(,X1)          ADDR NEXT PAIR IN LIST
         BCT   COUNT,RDREP01       GO BACK FOR ANOTHER CHECK
RDREP02  DS    0H
        $CC    NEG                 NO REPLACEMENT FOUND
         B     RDREP04             GO TO EXIT
RDREP03  DS    0H
         LA    R1,8(,X1)           NEW NAME IN LIST
        $CC    POS                 INDICATE SUCCESS
         SPACE
RDREP04  DS    0H
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
