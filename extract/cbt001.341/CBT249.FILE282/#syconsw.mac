***********************************************************************
*                                                                     *
*              'SYCONSW' --- WRITE TO ONLINE USER                     *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R0) LENGTH TO PUT
*                  --  (R1) POINTER TO DATA
         SPACE
SYCONSW $ENTER ,
         L     R2,X'10'            GET CVT POINTER
         TM    7(R2),X'01'         IS THIS CMS .Q
         BO    SYCW01              BRANCH IF YES
         TPUT  (1),(0)             DISPLAY STRING
         B     SYCW05              AND RETURN
         SPACE
SYCW01   DS    0H
         LR    R3,R1               DATA POINTER
         LR    R2,R0               LENGTH LEFT
         LR    R4,R0               TOTAL LENGTH
SYCW02   DS    0H
         C     R2,SYCWCMSX         MAX PARTIAL RECORD LENGTH
         BNH   SYCW03              BRANCH IF ALL FITS
         L     R0,SYCWCMSX         MAX AT ONE TIME
         WRTERM (R3),(0),EDIT=NO   FIRST PART BUT NO C/R
         S     R2,SYCWCMSX         DECREMENT LENGTH LEFT
         A     R3,SYCWCMSX         BUMP OUTPUT POINTER
         B     SYCW02              AND DO IT AGAIN
SYCW03   DS    0H
         WRTERM (R3),(R2)          PUT REST WITH C/R
         LR    R2,R4               TOTAL LENGTH
         C     R2,SYCWCMSX         DID IT FIT IN ONE .Q
         BNH   SYCW05              BRANCH IF YES
         SRL   R2,3                DIVIDE BY EIGHT
         LA    R1,SYCWIDLL         MAXIMUM IDLES
         CR    R2,R1               TOO MANY .Q
         BNH   SYCW04              BRANCH IF NOT
         LR    R2,R1               TAKE MAXIMUM INSTEAD
SYCW04   DS    0H
         WRTERM SYCWIDL,(R2),EDIT=NO    AND WAIT SOME
         SPACE
SYCW05   DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
         SPACE
SYCWCMSX DC    F'130'              MAX CMS TERMINAL LINE LENGTH
SYCWIDL  DC    32X'17'             MAXIMUM EXTRA IDLES
SYCWIDLL EQU   *-SYCWIDL           AND ITS LENGTH
