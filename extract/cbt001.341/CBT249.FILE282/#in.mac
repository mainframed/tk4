***********************************************************************
*                                                                     *
*              .IN (INDENT) --- CONTROL                               *
*                                                                     *
***********************************************************************
         SPACE
IN      $ENTER ,
        $CALL  PRINTNB             CLEAR OUT PRINT BUFFER
        $CALL  GETYESNO            LOCATE FIRST ARGUMENT
         BC    BOMIT,IN01          BRANCH TO DEFAULT IF OMITTED
         CLI   YNRESULT,YNASTK     IS FIRST A '*' PLACE HOLDER .Q
         BE    IN03                BRANCH IF YES
         ST    COUNT,ARGSTART      ELSE RESCAN FOR NUMERIC
        $CALL  GETNUM              GET AMOUNT OF INDENTION
         BC    BDNO,INE005         ERROR IF INVALID
         CLI   NUMSIGN,C' '        WAS OPERAND SIGNED .Q
         BE    IN02                BRANCH IF NOT
         AH    COUNT,INDL          MODIFY EXISTING INDENT VALUE
         AH    COUNT,OFFLI         PLUS UNTRIGGERED OFFSET (IF ANY)
         SH    COUNT,OFFL          AND MODIFY WITH OFFSET VALUE
         BNM   IN02                BRANCH NOT MINUS
IN01     DS    0H
         SR    COUNT,COUNT         DEFAULT VALUE OF ZERO
IN02     DS    0H
         C     COUNT,LLZ           COMPARE WITH LINE LENGTH
         BH    INE013              TOO BIG IF GREATER
         STH   COUNT,INDL          SET INDENT VALUE
         SH    COUNT,UNDL          CALCULATE CURRENT MARGIN
         BM    INE014              TEST FOR ILLEGAL
         STH   COUNT,RMARGIN       SET MARGIN (EFFECTIVE INDENT)
         SR    R0,R0               CLEAR WORK REGISTER
         STH   R0,OFFL             CLEAR OFFSET VALUE
         STH   R0,OFFLI            CLEAR NEXT OFFSET VALUE
         STH   R0,OFUNCNT          CLEAR MARGIN VALUE FOR FORMAT
         SPACE
IN03     DS    0H
        $CALL  GETYESNO            SCAN SECOND ARGUMENT
         BC    BOMIT,IN05          GO TO DEFAULT IF OMITTED
         CLI   YNRESULT,YNASTK     '*' MEANS LEAVE OPERAND ALONE .Q
         BNE   IN04                BRANCH IF NOT
         LH    COUNT,INDENTR       GET CURRENT VALUE
         B     IN06                GO CHECK FURTHER
IN04     DS    0H
         ST    COUNT,ARGSTART      RESCAN FOR NUMERIC
        $CALL  GETNUM              GET SECOND NUMERIC OPERAND
         BC    BDNO,INE005         ERROR IF INVALID
         CLI   NUMSIGN,C' '        WAS ARGUMENT SIGNED .Q
         BE    IN06                BRANCH IF NOT
         AH    COUNT,INDENTR       MAKE RELATIVE CHANGE
         B     IN06                CHECK FURTHER
IN05     DS    0H
         SR    COUNT,COUNT         DEFAULT CLEARS RIGHT INDENT
IN06     DS    0H
         XC    INDENTR,INDENTR     CLEAR RIGHT INDENT IN CASE OF ERROR
         L     R1,LLZ              GET CURRENT LINE LENGTH
         SH    R1,INDL             LESS INDENT FROM LEFT
         AR    R1,COUNT            PLUS INDENT TO RIGHT
         C     R1,DEFLL+TAB3MIN    COMPARE WITH MIN ALLOWED
         BL    INE014              BRANCH IF TOO SMALL
         LR    R1,COUNT            INDENT FROM RIGHT
         A     R1,LLZ              PLUS LINE LENGTH
         C     R1,DEFLL+TAB3MAX    COMPARE WITH MAX ALLOWED
         BH    INE013              BRANCH IF TOO BIG
         STH   COUNT,INDENTR       SET NEW RIGHT INDENT VALUE
         SPACE
         L     X1,OLDCOUNT         COUNT OF LINKED CHARACTER BLOCKS
         LTR   X1,X1               ANY PRESENT .Q
         BZ    IN07                BRANCH IF NOT
         L     X1,NOCHARS          NUMBER OF CHARACTERS NOW FORMATTED
         LTR   X1,X1               WAS IMPLIED BREAK SUCCESSFUL .Q
         BZ    IN07                BRANCH IF YES
         SPACE
         L     TEMP,OLDFIRST       FIRST LINK ELEMENT
         USING LINKELEM,TEMP       AND TELL THE ASSEMBLER
         SR    X2,X2               ZERO WORK REGISTER
         IC    X2,LINKMULT         INITIAL DUMMY BLANK MULT FACTOR
         LH    R1,INDL             NEW INDENT VALUE
         LA    R1,1(R1,X1)         NEW TOTAL LENGTH PLUS DUMMY
         SR    R1,X2               LESS OLD INDENT VALUE
         L     R0,LLZ              CURRENT LINE LENGTH
         AH    R0,INDENTR          PLUS RIGHT INDENT VALUE
         CR    R1,R0               NEW VALUE FIT ON CURRENT LINE .Q
         BH    IN07                BRANCH TO RETURN IF NOT
         ST    R1,NOCHARS          SET NEW LINE LENGTH
         SR    R1,X1               NEW LINE LENGTH CHANGE
         AR    X2,R1               ALTERATION ON FIRST DUMMY BLANK
         STC   X2,LINKMULT         UPDATE LEADING DUMMY BLANK COUNT
         DROP  TEMP
         SPACE
IN07     DS    0H
         B     IN08                COMMON EXIT
         SPACE
INE005   DS    0H
         MVI   EFLAG005,ERRSET     INVALID ARGUMENT
         B     IN08                COMMON EXIT
INE013   DS    0H
         MVI   EFLAG013,ERRSET     ARGUMENT TOO LARGE
         B     IN08                COMMON EXIT
INE014   DS    0H
         MVI   EFLAG014,ERRSET     ARGUMENT TOO SMALL
         B     IN08                COMMON EXIT
         SPACE
IN08     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
