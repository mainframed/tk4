*                             ********
* *            SOURCE BOOK *** DEUH02 *** RETURN ROUTINES          1-1
*                             ********
         SPACE 3
*****                                                             *****
*                         REJECTED WORD ROUTINE                       *
*****                                                             *****
HREJ     MVI   GSTATUS,X'00'       CLEAR ALL HYPHEN INDICATORS
         MVC   GSTATUS+1(L'GSTATUS-1),GSTATUS
         LA    REGLNK,GSTATUS      THIS IS WHERE NO DATA LIES
         LNR   REGLNK,REGLNK       INDICATE ROUTINE GONE WRONG
         B     HYPRETRN            AND GO TO FINAL RETURN
         SPACE
*****                                                             *****
*                            SUCCESSFUL WORD ROUTINE                  *
*****                                                             *****
HYPEXIT  DS    0H
         LA    REGLNK,GSTATUS      SUCCESSFUL RETURN DATA HERE
HYPRETRN DS    0H
        $EXIT  RC=(R15)            AND RETURN TO OUR CALLER
         SPACE
*****                                                             *****
*                         LAST TO FIT & ALL POINTS RETURN             *
*****                                                             *****
LTFRETR  L     REGWK2,ALTF         INIT.SCAN AT L-T-F CHAR
         B     RTNSCN
HRETRN   L     REGWK2,AEOW         INIT.SCAN AT END OF WORD
RTNSCN   S     REGWK2,HFLDAD       GENERATE RELATIVE ADDRESS
         LA    REGWK1,GSTATUS      GET ADDRESS OF GSTATUS
         LA    REG1,1                AND
         SR    REGWK1,REG1               BACK UP ONE POSITION
         AR    REGWK1,REGWK2        COMBINE REL.& ACTUAL FOR SCAN START
RTNSCN1  TM    0(REGWK1),X'30'     TEST FOR YES HYPHEN IN STATUS AREA
         BO    HITHYP               & BRANCH WHEN HYPHEN FOUND
         BCT   REGWK1,*+4          SCAN TO LEFT FOR HYPHEN
         BCT   REGWK2,RTNSCN1       DECREMENT COUNT FOR CHARACTER
         B     HREJ                 REJECT IF NO YES POINTS FOUND
HITHYP   LA    REG1,0
         C     REG1,ALTF           TEST FOR L-T-F REQ'D.
         BE    HRETRN1              IF NOT BRANCH
         MVC   RESTORE+1(1),0(REGWK1)   SAVE YES HYPHEN INDICATION
RTNSCN2  MVI   GSTATUS,X'00'                  CLEAR ALL INDICATORS
         MVC   GSTATUS+1(L'GSTATUS-1),GSTATUS  *
RESTORE  OI    0(REGWK1),X'30'          RESTORE YES HYPHEN INDICATOR
HRETRN1  B     HYPEXIT              GO TO FINAL EXIT
NOPOS    TM    SCNDSW,X'80'   TEST FOR YES FIELDS & IF YES GO TO FOUND
         BO    FOUND               ELSE FALL THRU TO NOHYP
NOHYP    L     REG3,ATEOW               TEST FOR TYPE OF EOW
         LTR   REG3,REG3                 & IF NOT TEOW
         BZ    HRETRN                   GOTO RETURN
         LA    REGWK3,0            ELSE INIT. HYFCOM POINTER
         LA    REGWK0,30           *RESET TO LIMIT OF WORD SEGMENT
         L     REG4,HFLDAD         *RESET COUNT OF CHARS.REMAINING
         LA    REG4,64(REG4)       *
         S     REG4,ATEOW          *
         L     REG3,ATEOW          RESET TO GET NEXT WORD SEGMENT
         STH   REGWK3,SCNDSW       RESET NON-ALPHA & ALPHA SCAN SWTCHS
         ST    REGWK3,ATEOW             RESET TEOW ADDRESS
         STH   REGWK3,MRGCNT
         B     NULL                 SWITCHES & RETURN
FOUND    DS    0H
         L     REGWK1,BGWDSG      START MERGE OF CHAR.STATUS BYTES FROM
         LA    REGWK2,ASTATUS  ASTATUS BACK TO GSTATUS
         LH    REGWK3,MRGCNT  SET FOR CONTROL OF MERGE LOOP
MRGA2G   TM    0(REGWK1),X'03' TEST GSTATUS FOR ALPHA / NON-ALPHA
         BZ    IGSTAT           IF NOT ALPHA MOVE TO NEXT STATUS BYTE
         MVC   0(1,REGWK1),0(REGWK2) MOVE STATUS BYTE FROM ASTATUS
         LA    REGWK2,1(REGWK2)       TO GSTATUS & ACCESS NEXT ASTATUS
         BCT   REGWK3,IGSTAT           BYTE  & DECREMENT CHAR.COUNT
*                                  FALL THROUGH AT END OF TRANSFER
         L     REGWK2,HFLDAD        & TEST FOR L-T-F MODE
         TM    0(REGWK2),X'FF'
         BM    LTFRETR             IF L-T-F MODE GO TO L-T-F RETURN
         LH    REGWK2,LSTCHROF     GET LENGTH OF INPUT
         B     NOHYP               ELSE TO NOHYP FOR EOW TEST
IGSTAT   LA    REGWK1,1(REGWK1)    BUMP TO NEXT GSTATUS BYTE
         B     MRGA2G               & CONTINUE MERGE OF ASTATUS TO
*                                      GSTATUS
