***********************************************************************
*                                                                     *
*              'SYPUTOP' --- QSAM OPEN FOR OUTPUT                     *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY  --  (R1)  IODEF ADDRESS
*
*        ON EXIT   --  (R1)  IOCB ADDRESS
*                      CC = BPOS  IF SUCCESSFUL
*                         = BNEG  IF UNSUCCESSFUL
         SPACE
SYPUTOP $ENTER ,
         LR    R2,R1               ADDRESS OF IODEF SECTION OF IOCB
         SR    R1,R1               CLEAR OPEN CODE REGISTER
         IC    R1,SYOPOPEN         OPEN CODE
         L     R15,=V(SYSINT)      SYSTEM DEPENDENT ROUTINE
         BAL   R14,32(,R15)        DO THE OPEN
         SPACE
         LTR   R15,R15             TEST SUCCESS
         BZ    SYOPQ01             BRANCH IF YES
         SR    R1,R1               RETURN NO IOCB POINTER
        $CC    NEG                 SET ERROR CONDITION
         B     SYOPQ04             GO TO RETURN
         SPACE
SYOPQ01  DS    0H
         TM    IOCRECFM-IOCB(R2),X'C0'  RECFM 'U' .Q
         BO    SYOPQ03             BRANCH IF YES
         TM    IOCRECFM-IOCB(R2),X'40'  RECFM 'V' .Q
         BO    SYOPQ03             BRANCH IF YES
         LH    R1,IOCLRECL-IOCB(,R2)    FILE RECORD LENGTH
         LA    R1,4(,R1)           PLUS LENGTH OF DUMMY RDW
        $CALL  GETCELL             GET A WORK BUFFER
         BC    BPOS,SYOPQ02        BRANCH IF SUCCESSFUL
         MVI   EFLAG024,ERRSET     NO STORAGE AVAILABLE
SYOPQ02  DS    0H
         ST    R3,IOCBUSER-IOCB(,R2)    SAVE WORK BUFFER ADDRESS
         SPACE
SYOPQ03  DS    0H
         LA    R1,0(,R2)           RETURN IOCB ADDRESS
        $CC    POS                 SUCCESSFUL RETURN
         SPACE
SYOPQ04  DS    0H
        $EXIT  KEEP=(R1),RC=0      RETURN TO CALLER
         SPACE
SYOPOPEN OPEN  (*-*,(OUTPUT,DISP)),MF=L  LIST FORM OF OPEN
         SPACE
         LTORG ,
