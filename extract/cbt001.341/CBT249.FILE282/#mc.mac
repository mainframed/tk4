***********************************************************************
*                                                                     *
*              .MC (MULTIPLE COLUMN) --- CONTROL                      *
*                                                                     *
***********************************************************************
         SPACE
MC      $ENTER ,
         CLI   ICORFLG,ICORNONE    'NO KEEP' IN PROGRESS .Q
         BNE   MCE043              ERROR IF NOT
         L     R1,SCCOUNT          GET SINGLE COLUMN NEST COUNT
         S     R1,F1               LESS ONE
         BM    MC02                BRANCH IF NONE
         ST    R1,SCCOUNT          UPDATE COUNT LEFT
         BNZ   MC02                BRANCH IF SOME LEFT
         SPACE
        $CALL  PRINTNB             CLEAR PRINT BUFFER
         L     COUNT,CLLZSAVE      SAVED COLUMN LENGTH
         LTR   COUNT,COUNT         WAS IT SPECIFIED .Q
         BZ    MC01                BRANCH IF NOT
         ST    COUNT,CLLZ          AND RESET SAME
MC01     DS    0H
         CLI   ICOLFLG,ICOLSC      IN SINGLE COLUMN MODE .Q
         BNE   MC02                BRANCH IF NOT
         MVI   ICOLFLG,ICOLMC      RESTORE MULTI-COLUMN OUTPUT
         LA    COUNT,1             FIRST COLUMN
         ST    COUNT,CDCURR        PROCESS FIRST COLUMN FIRST
         MVC   CDPLCT,PLCT         SAVE LINES REMAINING FOR COLUMN
MC02     DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
MCE043   DS    0H
         MVI   EFLAG043,ERRSET     INVALID CONTROL WORD WITHIN KEEP
         B     MC02                GO TO RETURN
