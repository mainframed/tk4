***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'SPACER' --- SKIP LINES (LESS PAGE)  *
*                                                                     *
***********************************************************************
         SPACE
SPACER  $ENTER ,
         LTR   COUNT,COUNT         TEST FOR VALID > 0
         BM    ABSPACE             NOT VALID
         BZ    SPACERET            IF 0, IGNORE
         L     X3,DEFLNUM+TAB3DEF  SAVE LINENUM VALUE
         MVC   DEFLNUM+TAB3DEF,F0  DISABLE LINENUM FOR SPACES
SPACOO01 DS    0H
         MVI   SKSPFLG2,X'00'      CLEAR CONDITIONAL FLAGS
         XC    SKSPCCNT,SKSPCCNT   CLEAR CONDITIONAL COUNT
         LA    X1,2                ASSUME NO OVERLAYS
         CLI   ICOLFLG,ICOLMC      IN MULTI-COLUMN MODE .Q
         BE    SPACOO02            BRANCH IF YES
        $IFON  SWITCHS,OOSWS,SPACOO03   BRANCH IF NO OVERLAY
         L     R1,OOANCH           OUTPUT OVERLAY BUFFER
         LTR   R1,R1               ANYTHING TO OVERLAY .Q
         BNZ   SPACOO02            BRANCH IF YES
         L     R1,BXPTR            BOX BUILDING POINTER
         LTR   R1,R1               ANYTHING PRESENT .Q
         BZ    SPACOO03            BRANCH IF NOT
SPACOO02 DS    0H
         SR    X1,X1               ONE LINE AT A TIME ONLY
         B     SPACPR              PRINT AS IF OFFLINE
SPACOO03 DS    0H
        $IFNONE SWITCHS,(CMSWS,TSOWS),SPACPR CHECK FOR TERMINALS
        $IFON  TTYPE,TONLINE,SPACTE     GO DIFFERENTLY IF ONLINE
         SPACE
SPACPR   DS    0H
         LA    TEMP,1(,X1)         GET MAXIMUM PRINTER SPACE
         CR    COUNT,TEMP          MORE THAN THAT TO DO
         BH    SPACPR01            BRANCH IF YES
         LR    TEMP,COUNT          SET FEWER
SPACPR01 DS    0H
         LH    R1,PLZ              LINES ON A PAGE
         SH    R1,PLCT             LESS LINES LEFT ON THIS PAGE
         C     R1,DEFFFTO+TAB3DEF  PAST TOP OF FORM LINE .Q
         BH    SPACPR02            BRANCH IF YES
         AR    R1,COUNT            WHERE WE WILL BE
         C     R1,DEFFFTO+TAB3DEF  PAST TOP OF FORM NOW .Q
         BL    SPACPR02            BRANCH IF NOT
         LA    TEMP,1              ONE AT A TIME FOR CHANNEL SKIP
SPACPR02 DS    0H
         LH    R1,PLCT             GET LINES LEFT ON PAGE
         SR    R1,TEMP             LESS NUMBER WE DO NOW
         STH   R1,PLCT             UPDATE LINES LEFT ON PAGE
         SR    COUNT,TEMP          LESS NUMBER WE DO NOW
         IC    TEMP,SPACTABL(TEMP) GET ASA CARRIAGE CONTROL
         STC   TEMP,PRNTRCC        INSERT ASA CARRIAGE CONTROL
         LA    R0,BLNKLINE         POINT TO BLANK LINE
        $CALL  IOPRINT             INVOKE OUTPUT ROUTINE
         LTR   X1,X1               TEST OUTPUT OVERLAY SITUATION
         BNZ   SPACPR03            BRANCH IF NO OVERLAY
         MVC   BLNKDATA,BLANKS     BLANK ALL OF DUMMY LINE
         LA    R0,BLNKRDWL         LENGTH OF THIS DUMMY BLANK LINE
         STH   R0,BLNKLINE         RESET LENGTH IN RDW
SPACPR03 DS    0H
         LTR   COUNT,COUNT         ANY LINES LEFT .Q
         BNP   SPACEFIN            BRANCH IF NOT
         LTR   X1,X1               TEST OUTPUT OVERLAY SITUATION
         BNZ   SPACPR              BRANCH IF NO OVERLAY TO OFFLINE
         B     SPACOO01            BRANCH TO TEST AGAIN
         SPACE
SPACTE   DS    0H
         LH    R1,PLCT             GET LINES LEFT ON PAGE
         SR    R1,COUNT            LESS NUMBER OF LINES WE NOW SPACE
         STH   R1,PLCT             SET REMAINDER ON CURRENT PAGE
        $IFON  SWITCHS,NPSWS,SPACEFIN   NO SPACE IF NO PRINT MODE
        $CALL  SPACEIM             DO IMMEDIATE ONLINE SPACING
         SPACE
SPACEFIN DS    0H
         ST    X3,DEFLNUM+TAB3DEF  RESTORE LINENUM VALUE
SPACERET DS    0H
        $OFF   SWITCHS,OOSWS       REENABLE OUTPUT OVERLAY
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
ABSPACE  DS    0H
        $LOGERR 1001               NEGATIVE SPACING ERROR
