SRO010   CSECT
*---------------------------------------------------------------------*
*   SRO010 --- A SUBROUTINE TO DETERMINE THE AMOUNT OF FREE SPACE     *
*   REMAINING ON A SPECIFIED DASD VOLUME.  CALLING SEQUENCE:          *
*                 CALL SRO010 ( IA, N )                               *
*   WHERE 'IA' = AN ARRAY AT LEAST 28-BYTES LONG.  FOR EACH VOLUME    *
*                TO BE SCANNED FOR FREE SPACE, 'IA' MUST CONTAIN A    *
*                28-BYTE WORK AREA AS DESCRIBED BELOW.  THE FIRST     *
*                SIX BYTES OF EACH WORK AREA MUST CONTAIN THE VOLSER  *
*                OF THE VOLUME TO BE SCANNED.  'IA' SHOULD BE 'N'*28  *
*                BYTES LONG.                                          *
*          'N' = THE NUMBER OF VOLUMES (AND HENCE WORK AREAS) IN 'IA' *
*                                                                     *
*   THE FORMAT OF EACH 28-BYTE VOLUME WORK AREA IS AS FOLLOWS:        *
*     DISP      LENGTH      FORMAT      DESCRIPTION                   *
*     ----      ------      ------      ----------------------------- *
*       0          6        EBCDIC      VOLSER OF DASD VOLUME ON      *
*                                       WHICH FREE SPACE IS TO BE     *
*                                       DETERMINED (PROVIDED BY THE   *
*                                       CALLING PROGRAM).             *
*       6          2        BINARY      RETURN CODE (SEE BELOW).      *
*       8          4        BINARY      NUMBER OF EXTENTS OF FREE     *
*                                       SPACE REMAINING ON THE VOLUME.*
*      12          4        BINARY      TOTAL NUMBER OF FREE CYLINDERS*
*                                       REMAINING ON THE VOLUME.      *
*      16          4        BINARY      TOTAL NUMBER OF FREE TRACKS   *
*                                       IN ADDITION TO THE FREE CYLIN-*
*                                       DERS ABOVE REMAINING ON THE   *
*                                       VOLUME.                       *
*      20          4        BINARY      NUMBER OF CYLINDERS IN THE    *
*                                       LARGEST FREE EXTENT REMAINING *
*                                       ON THE VOLUME.                *
*      24          4        BINARY      NUMBER OF TRACKS IN ADDITION  *
*                                       TO THE CYLINDERS IN THE       *
*                                       LARGEST FREE EXTENT REMAINING *
*                                       ON THE VOLUME.                *
*                                                                     *
*   RETURN CODE PROVIDED IN THE CALLER'S WORK AREA IS:                *
*        X'0000':  FUNCTION WAS COMPLETED SUCCESSFULLY.               *
*        X'0004':  REQUESTED VOLUME WAS NOT MOUNTED.  NO MOUNT REQUEST*
*                  WILL BE ISSUED TO THE OPERATOR'S CONSOLE.          *
*        X'0008':  AN ERROR OF UNKNOWN TYPE WAS ENCOUNTERED READING   *
*                  THE FORMAT 4 DSCB FOR THE VOLUME.                  *
*        X'000C':  AN ERROR OF UNKNOWN TYPE WAS ENCOUNTERED READING   *
*                  ONE OF THE FORMAT 5 DSCB'S ON THE VOLUME.          *
*   FOR ERRORS X'0008' AND X'000C', REPORT THE PROBLEM TO THE SYSTEMS *
*   PROGRAMMERS AT ONCE.                                              *
*   WRITTEN BY:  B. H. AUDET                          08 JUNE 1978    *
*---------------------------------------------------------------------*
         TITLE 'SRO010 -- ROUTINE TO FIND FREE SPACE ON A DASD VOLUME'
         SAVE  (14,12),,*              SAVE INPUT REGISTERS
         LR    12,15                   LOAD BASE REGISTER
         USING SRO010,12               ESTABLISH ADDRESSABILITY
         LR    11,13
         LA    13,SAVEAREA             R13<--A(SAVEAREA)
         ST    11,4(13)                FORWARD CHAIN SAVE AREAS
         ST    13,8(11)                REVERSE CHAIN SAVE AREAS
         LM    2,3,0(1)                R2<--A(IA)  R3<--A(N)
         L     3,0(3)                  R3<--N
         ST    2,IADDR                 SAVE ADDRESS OF IA
         ST    3,VOLCOUNT              SAVE VOLUME COUNT
         C     3,=F'0'                 IS VOLUME COUNT POSITIVE ?
         BNH   RETURN                  IF NOT, RETURN
         SPACE 3
*
*        BEGIN VOLUME PROCESSING.  THE WORD 'SAVE1' CONTAINS THE
*        POINTER TO THE NEXT WORK AREA ADDRESS.
*
GETVOL   L     11,IADDR                R11<--A(NEXT WORK AREA)
         USING WORKAREA,11             ESTABLISH ADDRESSABILITY
         XC    6(22,11),6(11)          INITIALIZE RESULTS
         MVC   VOLSER(6),0(11)         GET VOLSER
*
*        GET FORMAT 4 DSCB FOR THE SPECIFIED VOLUME
*
         OBTAIN BYNAME                 GET FORMAT 4 DSCB
TRAP1    LTR   15,15                   DID IT WORK ?
         BZ    OK4                     IF SO, PROCEED
         C     15,=F'4'                VOLUME NOT MOUNTED ?
         BE    SETCODE                 IF NOT MOUNTED, RETURN
         LA    15,8                    ELSE INDICATE UNKNOW ERROR
         B     SETCODE                 ...READING FMT 4 DSCB
OK4      CLI   CAMWORK,X'F4'           MAKE SURE IT'S A FMT 4 DSCB
         BE    LOCDSCB5                IF SO, BUILD CCHHR FOR FMT 5
         LA    15,8                    ELSE INDICATE UNKNOWN ERROR
         B     SETCODE                 ...READING FMT 4 DSCB
LOCDSCB5 IC    2,CAMWORK+100           R2<--R OF CCHHR(DSCB4)
         LA    2,1(2)                  BUILD CCHHR FOR DSCB5
         STC   2,CAMWORK+100           SAVE NEW RECORD NUMBER
*   NOTE:  THE ABOVE 3 INSTRUCTIONS COMPUTE THE ADDRESS (CCHHR) OF
*   THE FIRST FMT 5 DSCB BY ADDING 1 TO THE CCHHR OF THE FMT 4 DSCB.
*   THE FIRST FMT 5 DSCB SHOULD ALWAYS IMMEDIATELY FOLLOW THE FMT 4
*   DSCB, ELSE THE VTOC IS MESSED UP.  IF THE NEXT RECORD PAST THE
*   FMT 4 DSCB RESIDES ON ANOTHER TRACK, A NO-RECORD-FOUND CONDITION
*   WILL OCCUR.  THIS INDICATES THAT THE VTOC IS ALSO PROBABLY MESSED
*   UP.
         MVC   CCHHR(5),CAMWORK+96     SAVE NEW CCHHR
OBTAIN5  OBTAIN BYADDR                 GET FMT 5 DSCB
TRAP2    LTR   15,15                   DID IT WORK ?
         BZ    OK5                     IF SO, PROCEED
         LA    15,12                   ELSE INDICATE UNKNOWN ERROR
         B     SETCODE                 ...READING FMT 5 DSCB
OK5      CLI   CAMWORK+44,X'F5'        MAKE SURE IT'S A FMT 5 DSCB
         BZ    MOVEEXTS                IF OK, GO MOVE EXTENT INFO
         LA    15,12                   ELSE INDICATE UNKNOWN ERROR
         B     SETCODE                 ...READING FMT 5 DSCB
MOVEEXTS MVC   EXTWORK(40),CAMWORK+4   COPY FIRST GROUP OF EXTENTS
         MVC   EXTWORK+40(90),CAMWORK+45  AND REMAINING EXTENTS
         LA    3,26                    # EXTENTS THIS DSCB5
         LA    4,EXTWORK               R4<--A(FIRST EXTENT DESCRIPTION)
EXTLOOP  CLC   0(2,4),=X'0000'         IS TTR ZERO ?
         BE    NEXTINCR                GO GET NEXT EXTENT
         LM    5,9,EXTENTS             LOAD TOTALS SO FAR
         MVC   WORK(5),0(4)            COPY EXTENT DESC TO WORK AREA
         LA    5,1(5)                  INCREMENT EXTENT COUNT
         AH    6,WORK+2                ACCUMULATE CYLS
         SR    1,1                     R1<--0
         IC    1,WORK+4                R1<--# TRACKS
         AR    7,1                     ACCUMULATE TRKS
         LH    10,WORK+2               R10<--# CYLINDERS
         CR    10,8                    LARGEST SO FAR ?
         BH    NEWMAX                  SET NEW MAXIMUM
         BL    NEXTXTNT                GO DO NEXT EXTENT
         SR    0,0                     ZERO OUT ZERO
         IC    0,WORK+4                R0<--# TRACKS
         CR    0,9                     LARGEST SO FAR ?
         BNH   NEXTXTNT                GO PROCESS NEXT EXTENT
NEWMAX   LH    8,WORK+2                R8<--NEW LARGEST CYL
         SR    9,9
         IC    9,WORK+4                R9<--NEW LARGEST TRKS
NEXTXTNT STM   5,9,EXTENTS
NEXTINCR LA    4,5(4)                  R4<--A(NEXT EXTENT)
         BCT   3,EXTLOOP               GO PROCESS MORE
         MVC   CCHHR(5),CAMWORK+135    GET ADDRESS OF NEXT DSCB5
         CLC   CCHHR(5),=X'0000000000' IS IT ZERO ?
         BNE   OBTAIN5                 IF NOT, GO GET NEXT
         SR    15,15                   INDICATE ALL WORKED OKAY
SETCODE  STH   15,RETNCODE             SET RETURN CODE IN CALLER'S
         L     11,IADDR                R11<--A(CURRENT WORK AREA)
         LA    11,28(11)               UPDATE WORK AREA ADDRESS
         ST    11,IADDR                SAVE UPDATED PARM LIST POINTER
         L     1,VOLCOUNT              R1<--VOLUME COUNT
         BCTR  1,0                     DECREMENT VOLUME COUNT
         ST    1,VOLCOUNT              SAVE NEW VOLUME COUNT
         C     1,=F'0'                 FINISHED YET ?
         BH    GETVOL                  GO PROCESS NEXT VOLUME
RETURN   L     13,SAVEAREA+4           ...WORK AREA, RESTORE R13.
         ST    15,16(13)               SET RETURN CODE IN CALLER'S
         LM    14,12,12(13)            ...SAVE AREA, RESTORE REGS.
         BR    14                      RETURN TO CALLER
SAVEAREA DS    9D                      GENERAL REGISTER SAVE AREA
WORK     DS    D                       SCRATCH PAD
CCHHR    DS    D                       DSCB ADDRESS FOR OBTAIN BYADDR
IADDR    DS    F                       INPUT REGISTER 1
VOLCOUNT DS    F                       VOLUME COUNT
DSN      DC    44XL1'04'               DSNAME FOR FMT 4 DSCB
CAMWORK  DS    35F                     OUTPUT AREA FOR OBTAIN
EXTWORK  DS    35F                     WORK AREA FOR EXTENT DESC'S
BYNAME   CAMLST SEARCH,DSN,VOLSER,CAMWORK
BYADDR   CAMLST SEEK,CCHHR,VOLSER,CAMWORK
VOLSER   DS    CL6                     VOLSER OF VOLUME TO BE READ
         LTORG
WORKAREA DSECT                         USER WORK AREA MAP
VOLUME   DS    3H                      USER-SUPPLIED VOLSER
RETNCODE DS    H                       RETURN CODE
EXTENTS  DS    F                       # FREE EXTENTS
CYLS     DS    F                       TOTAL # FREE CYLINDERS
TRKS     DS    F                       TOTAL # FREE TRACKS
MAXCYLS  DS    F                       # CYLINDERS IN LARGEST EXTENT
MAXTRKS  DS    F                       # TRAKCS IN LARGEST EXTENT
         END   SRO010
