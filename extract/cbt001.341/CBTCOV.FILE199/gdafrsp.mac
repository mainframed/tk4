         TITLE 'GDAFRSP -- ROUTINE TO FIND FREE SPACE ON A DASD VOLUME'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  GDAFRSP                                            *
*                                                                     *
*   TYPE:  SUBROUTINE                                                 *
*                                                                     *
*   FUNCTION:  TO DETERMINE THE AMOUNT OF FREE SPACE REMAINING ON     *
*              A DIRECT ACCESS VOLUME                                 *
*                                                                     *
*   CALLING SEQUENCE:   CALL GDAFRSP                                  *
*                                                                     *
*   ARGUMENTS:  NONE                                                  *
*                                                                     *
*   INPUT REGISTERS:                                                  *
*                                                                     *
*         R10 = A(WORK AREA)                                          *
*         R13 = A(SAVE AREA)                                          *
*         R14 = A(RETURN)                                             *
*         R15 = A(ENTRY POINT)                                        *
*                                                                     *
*   OPERATION:                                                        *
*                                                                     *
*   ON ENTRY, THE VOLSER OF THE VOLUME TO BE EXAMINED IS ASSUMED      *
*   TO BE IN "VOLSER" IN THE WORK AREA.  THE FORMAT 5 DSCB CHAIN      *
*   OF THE VOLUME IS READ AND THE FREE SPACE COMPUTED.                *
*                                                                     *
*   ON RETURN, THE FOLLOWING TWO FIELDS OF THE WORK AREA WILL BE      *
*   FILLED IN WITH THE SIZE OF THE LARGEST FREE EXTENT ON THE         *
*   VOLUME:                                                           *
*                                                                     *
*          MAXCYLS = THE NUMBER OF CYLINDERS IN THE LARGEST FREE      *
*                    EXTENT ON THE VOLUME                             *
*                                                                     *
*          MAXTRKS = THE NUMBER OF TRACKS, IN ADDITION TO CYLINDERS,  *
*                    IN THE LARGEST FREE EXTENT ON THE VOLUME         *
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*          0 = THE VOLUME WAS SUCCESSFULLY READ AND "MAXCYLS" AND     *
*              "MAXTRKS" WERE UPDATED TO REFLECT THE SIZE OF THE      *
*              LARGEST FREE EXTENT ON THE VOLUME.                     *
*                                                                     *
*          4 = THE VOLUME WAS NOT ONLINE                              *
*                                                                     *
*          8 = AN I/O ERROR WAS ENCOUNTERED READING THE VTOC.         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
*
*   DEFINE REGISTER EQUATES
*
R0       EQU   0                       GENERAL REGISTER 0
R1       EQU   1                       GENERAL REGISTER 1
R2       EQU   2                       GENERAL REGISTER 2
R3       EQU   3                       GENERAL REGISTER 3
R4       EQU   4                       GENERAL REGISTER 4
R5       EQU   5                       GENERAL REGISTER 5
R6       EQU   6                       GENERAL REGISTER 6
R7       EQU   7                       GENERAL REGISTER 7
R8       EQU   8                       GENERAL REGISTER 8
R9       EQU   9                       GENERAL REGISTER 9
R10      EQU   10                      GENERAL REGISTER 10
R11      EQU   11                      GENERAL REGISTER 11
R12      EQU   12                      GENERAL REGISTER 12
R13      EQU   13                      GENERAL REGISTER 13
R14      EQU   14                      GENERAL REGISTER 14
R15      EQU   15                      GENERAL REGISTER 15
         SPACE 3
*
*   INITIALIZATION:  SAVE THE INPUT REGISTERS, LOAD THE BASE REGISTER,
*   ESTABLISH PROGRAM AND WORK AREA ADDRESSABILITY AND CHAIN THE SAVE
*   AREAS.
*
GDAFRSP  CSECT
         SAVE  (14,12),,*              SAVE INPUT REGISTERS
         LR    R12,R15                 LOAD BASE REGISTER
         USING GDAFRSP,R12             ESTABLISH ADDRESSABILITY
         LR    R11,R10                 R11<--A(WORK AREA)
         USING WORKAREA,R11            COMMON ADDRESSABILITY
         LR    R10,R13                 R10<--A(CALLER'S SAVE AREA)
         LA    R13,72(R13)             R13<--A(NEXT SAVE AREA IN STACK)
         ST    R10,4(R13)              FORWARD CHAIN SAVE AREAS
         ST    R13,8(R10)              REVERSE CHAIN SAVE AREAS
         XC    EXTENTS(20),EXTENTS     ZERO OUT RESULTS
         SPACE 3
*
*   GET FORMAT 4 DSCB FOR THE SPECIFIED VOLUME
*
GETVOL   EQU   *
*        OBTAIN BYNAME                 GET FORMAT 4 DSCB
         XC    CAMLST(16),CAMLST      +CLEAR THE CAMLST CNTL BLOCK
         MVI   CAMLST,193             +SET OPTION BYTE
         LA    R1,DSN                 +R1<--A(DSNAME STORAGE AREA)
         ST    R1,CAMLST+4            +SAVE IT IN CAMLST CNTL BLOCK
         LA    R1,VOLSER              +R1<--A(VOLSER STORAGE AREA)
         ST    R1,CAMLST+8            +SAVE IT IN CAMLST CNTL BLOCK
         LA    R1,CAMWORK             +R1<--A(CAMLST WORK AREA)
         ST    R1,CAMLST+12           +SAVE IT IN CAMLST CNTL BLOCK
         LA    R1,CAMLST              +R1<--A(CAMLST CNTL BLOCK)
         SVC   27                     +ISSUE OBTAIN BYNAME
T1       LTR   R15,R15                 DID IT WORK ?
         BZ    OK4                     IF SO, PROCEED
         C     R15,=F'4'               VOLUME NOT MOUNTED ?
         BE    RETURN                  IF NOT MOUNTED, RETURN
         LA    R15,8                   ELSE INDICATE UNKNOW ERROR
         B     RETURN                  ...READING FMT 4 DSCB
OK4      CLI   CAMWORK,X'F4'           MAKE SURE IT'S A FMT 4 DSCB
         BE    LOCDSCB5                IF SO, BUILD CCHHR FOR FMT 5
         LA    R15,8                   ELSE INDICATE UNKNOWN ERROR
         B     RETURN                  ...READING FMT 4 DSCB
LOCDSCB5 IC    R2,CAMWORK+100          R2<--R OF CCHHR(DSCB4)
         LA    R2,1(R2)                BUILD CCHHR FOR DSCB5
         STC   R2,CAMWORK+100          SAVE NEW RECORD NUMBER
*
*   NOTE:  THE ABOVE 3 INSTRUCTIONS COMPUTE THE ADDRESS (CCHHR) OF
*   THE FIRST FMT 5 DSCB BY ADDING 1 TO THE CCHHR OF THE FMT 4 DSCB.
*   THE FIRST FMT 5 DSCB SHOULD ALWAYS IMMEDIATELY FOLLOW THE FMT 4
*   DSCB, ELSE THE VTOC IS MESSED UP.  IF THE NEXT RECORD PAST THE
*   FMT 4 DSCB RESIDES ON ANOTHER TRACK, A NO-RECORD-FOUND CONDITION
*   WILL OCCUR.  THIS INDICATES THAT THE VTOC IS ALSO PROBABLY MESSED
*   UP.
*
         MVC   CCHHR(5),CAMWORK+96     SAVE NEW CCHHR
OBTAIN5  EQU   *
*        OBTAIN BYADDR                 GET FMT 5 DSCB
         MVI   CAMLST,192             +SET 1-ST OPTION BYTE
         MVI   CAMLST+1,128           +SET 2-ND OPTION BYTE
         LA    R1,CCHHR               +R1<--A(CCHHR STORAGE AREA)
         ST    R1,CAMLST+4            +SAVE IT IN CAMLST CNTL BLOCK
         LA    R1,CAMLST              +R1<--A(CAMLST CNTL BLOCK)
         SVC   27                     +ISSUE OBTAIN BYADDR
TRAP2    LTR   R15,R15                 DID IT WORK ?
         BZ    OK5                     IF SO, PROCEED
         LA    R15,12                  ELSE INDICATE UNKNOWN ERROR
         B     RETURN                  ...READING FMT 5 DSCB
OK5      CLI   CAMWORK+44,X'F5'        MAKE SURE IT'S A FMT 5 DSCB
         BZ    MOVEEXTS                IF OK, GO MOVE EXTENT INFO
         LA    R15,12                  ELSE INDICATE UNKNOWN ERROR
         B     RETURN                  ...READING FMT 5 DSCB
MOVEEXTS MVC   EXTWORK(40),CAMWORK+4   COPY FIRST GROUP OF EXTENTS
         MVC   EXTWORK+40(90),CAMWORK+45  AND REMAINING EXTENTS
         LA    R3,26                   # EXTENTS THIS DSCB5
         LA    R4,EXTWORK              R4<--A(FIRST EXTENT DESCR.)
EXTLOOP  CLC   0(2,R4),=X'0000'        IS TTR ZERO ?
         BE    NEXTINCR                GO GET NEXT EXTENT
         LM    R5,R9,EXTENTS           LOAD TOTALS SO FAR
         MVC   WORK(5),0(R4)           COPY EXTENT DESC TO WORK AREA
         LA    R5,1(R5)                INCREMENT EXTENT COUNT
         AH    R6,WORK+2               ACCUMULATE CYLS
         SR    R1,R1                   R1<--0
         IC    R1,WORK+4               R1<--# TRACKS
         AR    R7,R1                   ACCUMULATE TRKS
         LH    R10,WORK+2              R10<--# CYLINDERS
         CR    R10,R8                  LARGEST SO FAR ?
         BH    NEWMAX                  SET NEW MAXIMUM
         BL    NEXTXTNT                GO DO NEXT EXTENT
         SR    R0,R0                   ZERO OUT ZERO
         IC    R0,WORK+4               R0<--# TRACKS
         CR    R0,R9                   LARGEST SO FAR ?
         BNH   NEXTXTNT                GO PROCESS NEXT EXTENT
NEWMAX   LH    R8,WORK+2               R8<--NEW LARGEST CYL
         SR    R9,R9
         IC    R9,WORK+4               R9<--NEW LARGEST TRKS
NEXTXTNT STM   R5,R9,EXTENTS
NEXTINCR LA    R4,5(R4)                R4<--A(NEXT EXTENT)
         BCT   R3,EXTLOOP              GO PROCESS MORE
         MVC   CCHHR(5),CAMWORK+135    GET ADDRESS OF NEXT DSCB5
         CLC   CCHHR(5),=X'0000000000' IS IT ZERO ?
         BNE   OBTAIN5                 IF NOT, GO GET NEXT
         SLR   R15,R15                 CLEAR THE RETURN CODE
RETURN   L     R13,4(R13)              ...WORK AREA, RESTORE R13.
         ST    R15,16(R13)             SET RETURN CODE IN CALLER'S
         LM    R14,R12,12(R13)         ...SAVE AREA, RESTORE REGS.
         BR    R14                     RETURN TO CALLER
DSN      DC    44XL1'04'               VTOC DATASET NAME
         LTORG
         SPACE 3
*
*   WORK AREA DSECT
*
         DSECT
         $PROFWA
         END   GDAFRSP
