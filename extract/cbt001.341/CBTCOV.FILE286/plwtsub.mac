*****************************  PLWTSUB   ******************************
*                                                                     *
* NAME - PLWTSUB (FORTRAN SUBROUTINE).                                *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: THE DIFFERENCE BETWEEN TWO TIMES;               *
*         WHEN PROVIDED WITH: TWO TIMES, EACH IN THE FORM: HHMMSSTTT  *
*         (TTT = THOUSANDTHS OF SECONDS).                             *
*                                                                     *
* ENTERED  FROM - FORTRAN PROGRAMS.                                   *
*                 PLWTSUB MAY ALSO BE USED BY PL1 PROGRAMS, IN WHICH  *
*                 CASE THE TWO PARAMETERS PASSED SHOULD HAVE THE      *
*                 ATTRIBUTE: CHAR(9).                                 *
*                                                                     *
* INPUT.                                                              *
*         T1,T2  - TWO ZONED DECIMAL NUMBERS, EACH IN THE FORM:       *
*                  HHMMSSTTT.                                         *
*                                                                     *
* OUTPUT.                                                             *
*         T1     - THE DIFFERENCE T2-T1 (ALL NUMBERS BEING TREATED    *
*                  AS TIMES IN THE AFOREMENTIONED FORMAT), WHICH      *
*                  REPLACES THE FIRST PARAMETER PASSED.               *
*                                                                     *
* EXAMPLE.                                                            *
*         CALL PLWTSUB(T1,T2)                                         *
*                                                                     *
*         BEFORE CALL: T1=163520687, T2=192143291;                    *
*         AFTER CALL: T1=024622604.                                   *
*                                                                     *
* EXTERNAL  REFERENCES - NONE.                                        *
*                                                                     *
* MACROS  REFERENCED - NONE.                                          *
*                                                                     *
* TABLES  REFERENCED - NONE.                                          *
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES - NONE.                                                    *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         NONE.                                                       *
*                                                                     *
* ENVIRONMENT - FORTRAN PROGRAMS (ALSO PL1 AS DESCRIBED ABOVE).       *
*                                                                     *
* RESTRICTIONS - NONE.                                                *
*                                                                     *
*****************************  PLWTSUB   ******************************
PLWTSUB  CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SVAR+4
         LR    10,13
         LA    13,SVAR
         ST    13,8(10)
         L     2,0(1)
         L     2,0(2)              ADDR OF FIRST FIELD
         L     3,4(1)
         L     3,0(3)              ADDR OF SECOND FIELD
         LA    4,2
         B     OVER
MOVE     MVC   AREA(8),NEWAREA     MOVE SECOND TIME INTO AREA
         LR    3,2
OVER     MVC   SPARE(9),0(3)       MOVE CHARS INTO SPARE
         NI    SPARE+1,X'CF'       TO MAKE HOURS
         NI    SPARE+3,X'CF'          MINUTES  SECONDS AND
         NI    SPARE+5,X'CF'             1/1000*SECONDS
         NI    SPARE+8,X'CF'                SEPARATELY MOVEABLE
         PACK  SPACE,SPARE(2)
         ZAP   NEWAREA,SPACE       HOURS TO NEWAREA
         MP    NEWAREA,SIXTY       CONVERT TO MINUTES
         PACK  SPACE,SPARE+2(2)
         AP    NEWAREA,SPACE       ADD MINUTES
         MP    NEWAREA,SIXTY       CONVERT TO SECONDS
         PACK  SPACE,SPARE+4(2)
         AP    NEWAREA,SPACE       ADD SECONDS
         MP    NEWAREA,THOUS       CONVERT TO 1000*SECONDS
         PACK  SPACE,SPARE+6(3)
         AP    NEWAREA,SPACE       ADD 1000*SECONDS
         BCT   4,MOVE
         SP    AREA,NEWAREA        SUBTRACT FIRST TIME FROM SECOND TIME
         CP    AREA,NOUGHT         HAS TIME FOR JOB PASSED MIDNIGHT?
         BC    11,ON               NO,  BRANCH TO ON
         AP    AREA,TWFRHRS        YES,  ADD 24 HOURS
ON       DP    AREA,THOUS          1/1000*SECONDS
         UNPK  SPARE+6(3),AREA+5(3)
         OI    SPARE+8,X'F0'
         ZAP   AREA,AREA(5)
         DP    AREA,SIXTY          TO OBTAIN SECONDS
         UNPK  SPARE+4(2),AREA+6(2)
         OI    SPARE+5,X'F0'
         DP    AREA(6),SIXTY       TO OBTAIN MINUTES
         UNPK  SPARE+2(2),AREA+4(2)
         OI    SPARE+3,X'F0'
         UNPK  SPARE(2),AREA+2(2)
         OI    SPARE+1,X'F0'
         MVC   0(9,2),SPARE        MOVE TIME DIFF INTO OUTPUT AREA
         L     13,SVAR+4
         RETURN (14,12)
SPACE    DS    PL2
AREA     DS    PL8
SVAR     DS    18F
SPARE    DS    CL9
SIXTY    DC    P'60'
THOUS    DC    P'1000'
NEWAREA  DS    PL8
NOUGHT   DC    P'0'
TWFRHRS  DC    P'86400000'
         END
