*****************************  PLWPLIO   ******************************
*                                                                     *
* NAME - PLWPLIO (SUBROUTINE)..                                       *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: YEAR AND IOCS DAY;                              *
*         WHEN PROVIDED WITH: PL1 DATE.                               *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         YYMMDD - PL1 DATE.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         YY1    - YEAR,                                              *
*         DDD    - IOCS DAY.                                          *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWPLIO ENTRY(CHAR(6),FIXED(2),FIXED(3));               *
*         CALL PLWPLIO(YYMMDD,YY1,DDD);                               *
*                                                                     *
*         BEFORE CALL: YYMMDD='670503';                               *
*         AFTER CALL: YY1=67, DDD=123.                                *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF INVALID INPUT PARAMETERS ARE DETECTED, THE OUTPUT        *
*         PARAMETERS ARE SET TO ZERO (FIXED DEC).                     *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWPLIO   ******************************
PLWPLIO  CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SVAR+4
         LR    10,13
         LA    13,SVAR
         ST    13,8(10)
         L     2,0(1)
         L     2,0(2)              ADDR OF PL1 DATE
         L     3,4(1)              ADDR OF YEAR
         L     4,8(1)              ADDR OF IOCS DAY
         MVC   EAR,0(2)            MOVE PL1 DATE TO EAR
         NI    EAR+5,X'CF'         TO MAKE YEAR
         NI    EAR+3,X'CF'            MONTH AND DAY
         NI    EAR+1,X'CF'               SEPARATELY MOVEABLE
         PACK  AREA+6(2),EAR+4(2)
         CVB   5,AREA              DAY OF MONTH TO GR5
         SR    7,7
         LA    2,12
         CR    5,7
         BC    13,AB
         CH    5,TABLE+2
         BC    2,AB
         PACK  AREA+6(2),EAR+2(2)
         CVB   6,AREA              MONTH TO GR6
         CR    6,7
         BC    13,AB
         CR    6,2
         BC    2,AB
         PACK  AREA+6(2),EAR(2)
         CVB   8,AREA              YEAR TO GR8
         LTR   8,8                 CHECK FOR INVALID YEAR
         BM    AB
         SRDA  8,32
         LA    10,4
         DR    8,10                YEAR/4 IN GR9 REMAINDER IN GR8
         CR    8,7                 LEAP YEAR
         BC    7,OVER              NO, BRANCH TO OVER
         LA    8,1(8)              PUT 1 IN GR8
         CR    6,8                 IS MONTH JAN?
         BC    13,OVER             YES, BRANCH TO OVER
         LR    7,8                 NO, PUT 1 IN GR7
         AR    8,8                 PUT 2 IN GR8
         CR    6,8                 IS MONTH JAN OR FEB?
         BC    13,OVER             YES, BRANCH TO OVER
         LA    5,1(5)              NO, ADD ONE TO DAY
OVER     MVC   0(2,3),AREA+6       MOVE YEAR TO OUTPUT AREA
CONT     CR    6,2                 TO FIND THE
         BC    8,OUT                  IOCS DAY FOR
         BCT   2,CONT                    THE GIVEN
OUT      AR    2,2                          MONTH
         LA    6,2
         SR    2,6
         LA    10,TABLE
         AR    10,2
         AH    5,0(10)             ADD MONTH TO DAY FOR IOCS DAY
         AH    7,2(10)
         CR    5,7
         BC    2,AB
         CVD   5,AREA
         MVC   0(2,4),AREA+6       MOVE IOCS DAY TO OUTPUT AREA
         B     RET
AB       MVC   0(2,4),NAUT
         MVC   0(2,3),NAUT
RET      L     13,SVAR+4
         RETURN (14,12)
EAR      DS    CL6
         DS    0D
SVAR     DS    18F
AREA     DC    PL8'0'
NAUT     DC    PL2'0'
TABLE    DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
         END
