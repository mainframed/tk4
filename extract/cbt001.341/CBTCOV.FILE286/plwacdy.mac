*****************************  PLWACDY   ******************************
*                                                                     *
* NAME - PLWACDY (SUBROUTINE).                                        *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO RETURN: DAY OF WEEK TYPE 1 (1=SUN), DAY OF MONTH AND     *
*                    MONTH OF YEAR OF LAST DAY OF A/C PERIOD;         *
*         WHEN PROVIDED WITH: A/C PERIOD AND A/C YEAR.                *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         PP1    - A/C PERIOD,                                        *
*         YY2    - A/C YEAR.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         D1     - DAY OF WEEK (1=SUN),                               *
*         DD     - DAY OF MONTH,                                      *
*         MM     - MONTH OF YEAR.                                     *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWACDY                                                 *
*                ENTRY(FIXED(2),FIXED(2),FIXED(1),FIXED(2),FIXED(2)); *
*         CALL PLWACDY(PP1,YY2,D1,DD,MM);                             *
*                                                                     *
*         BEFORE CALL: PP1=5, YY2=64;                                 *
*         AFTER CALL: D1=7, DD=29, MM=8.                              *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF INVALID INPUT PARAMETERS ARE DETECTED, THE OUTPUT        *
*         PARAMETERS ARE SET TO ZERO (FIXED DEC).                     *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWACDY   ******************************
PLWACDY  CSECT
         STM   14,12,12(13)
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
         BALR  RB,0
         USING *,RB
         ST    RD,SVAR+4
         LR    RA,RD
         LA    RD,SVAR
         ST    RD,8(RA)
         LM    R6,RA,0(R1)         ADDRS OF A/C PERIOD,A/C YEAR,
*                               A/C DAY,DAY OF MONTH AND MONTH NUMBER
         LA    RE,1
         ZAP   AREA,0(2,R7)
         CVB   R2,AREA             A/C YEAR
         CR    R2,RE               INVALID YEAR?
         BL    ABE                 YES - ERROR CONDITION
         ZAP   AREA,0(2,R6)
         CVB   R6,AREA             A/C PERIOD
         CR    R6,RE               IS A/C PERIOD<1?
         BL    ABE                 YES - ERROR CONDITION
         C     R6,TWELVE           IS A/C PERIOD 12?
         BH    ABE                 GREATER THAN 12 - ERROR CONDITION
         BE    SORT                EQUAL - BRANCH TO SORT
         LR    R7,R2
         LA    R4,4
         SRDA  R2,32
         DR    R2,R4               YEAR/4 IN GR3 - REMAINDER IN GR2
         AR    R3,R7               YEAR+YEAR/4
         LA    R3,92(R3)           YEAR+YEAR/4+DAY TO APRIL 1ST
         SR    R2,R2
         LA    R5,7
         DR    R2,R5               A/C DAY FOR APRIL 1ST IN GR2
         SR    R6,RE
         SLL   R6,1
         LR    R3,R5
         SR    R3,R2
         AR    R3,RE
         CR    R2,R4               IS DAY 4?
         BL    DAYS
         AR    R3,R5
DAYS     LA    R3,21(R3)           NUMBER OF DAYS IN 1ST PERIOD
         LA    RC,PERIODS
         AR    RC,R6
         AH    R3,0(RC)            DAY IN A/C YEAR
         LA    R4,11
         LA    RC,TABLE
         LA    RC,20(RC)
COMPARE  CH    R3,0(RC)
         BH    MONTHNO
         S     RC,TWO
         SR    R4,RE
         B     COMPARE
MONTHNO  SH    R3,0(RC)
         LA    R4,3(R4)
         C     R4,TWELVE
         BNH   MONFOUND
         S     R4,TWELVE
MONFOUND LA    R2,7
         B     OVER
ABE      MVI   0(R8),X'0C'         RETURNED VALUES
         MVC   0(2,R9),NAUT           FOR INVALID INPUT
         MVC   0(2,RA),NAUT              TO THIS ROUTINE
         B     RET
SORT     AR    R2,RE
         LR    R7,R2
         LA    R4,4
         SRDA  R2,32
         DR    R2,R4               YEAR/4 IN GR3 - REMAINDER IN GR2
         AR    R3,R7               YEAR+YEAR/4
         LA    R3,91(R3)           YEAR+YEAR/4+DAY TO MARCH 31ST
         SR    R2,R2
         LA    R5,7
         DR    R2,R5               A/C DAY FOR MARCH 31ST IN GR2
         CR    R2,RE
         BNL   CONT
         AR    R2,R5
CONT     LA    R3,31
         LA    R4,3
OVER     CVD   R2,AREA
         MVC   0(1,R8),AREA+7      MOVE A/C DAY TO OUTPUT
         CVD   R3,AREA
         MVC   0(2,R9),AREA+6      MOVE DATE OF MONTH TO OUTPUT
         CVD   R4,AREA
         MVC   0(2,RA),AREA+6      MOVE MONTH NUMBER TO OUTPUT
RET      L     RD,SVAR+4
         RETURN (14,12)
AREA     DS    D
SVAR     DS    18F
TABLE    DC    H'0,30,61,91,122,153,183,214,244,275,306'
PERIODS  DC    H'0,28,63,91,119,154,182,210,245,273,301'
TWO      DC    F'2'
TWELVE   DC    F'12'
NAUT     DC    PL2'0'
         END
