*****************************  PLWACPD   ******************************
*                                                                     *
* NAME - PLWACPD (SUBROUTINE).                                        *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: A/C PERIOD;                                     *
*         WHEN PROVIDED WITH: YEAR AND IOCS DAY.                      *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         YY1    - YEAR,                                              *
*         DDD    - IOCS DAY.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         PP1    - A/C PERIOD.                                        *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWACPD ENTRY(FIXED(2),FIXED(3),FIXED(2));              *
*         CALL PLWACPD(YY1,DDD,PP1);                                  *
*                                                                     *
*         BEFORE CALL: YY1=69, DDD=124;                               *
*         AFTER CALL: PP1=2.                                          *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF INVALID INPUT PARAMETERS ARE DETECTED, THE OUTPUT        *
*         PARAMETER IS SET TO ZERO (FIXED DEC).                       *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWACPD   ******************************
PLWACPD  CSECT
         STM   14,12,12(13)
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
         BALR  RB,0
         USING *,RB
         ST    RD,SVAR+4
         LR    RA,RD
         LA    RD,SVAR
         ST    RD,8(RA)
         LM    R8,RA,0(R1)         ADDRS OF IOCS DATE AND A/C PERIOD
         LA    RE,1
         ZAP   AREA,0(2,R8)
         CVB   R2,AREA             YEAR IN GR2
         CR    R2,RE
         BL    ABE                 YEAR LESS THAN 1
         LA    R3,99
         CR    R2,R3
         BH    ABE                 YEAR GREATER THAN 99
         ZAP   AREA,0(2,R9)
         CVB   R6,AREA             IOCS DAY IN GR6
         CR    R6,RE
         BL    ABE                 DAY LESS THAN 1
         LA    R8,90
         LR    R7,R2
         SRDA  R2,32
         LA    R4,4
         DR    R2,R4               YR/4 IN GR3,REMAINDER IN GR2
         CR    R2,RE               IS THIS YEAR A LEAP YEAR?
         BNL   NOLEAP              NO - BRANCH
         LA    R8,1(R8)            YES - ADD 1 TO DAY FOR MARCH 31ST
NOLEAP   CR    R6,R8               IS A/C YEAR THIS YEAR OR LAST YEAR?
         BH    THISYEAR            THIS YEAR - BRANCH
         LA    R9,275
         AR    R6,R9               DAY IN A/C YEAR
         SR    R7,RE               YEAR =LAST YEAR
         LR    R2,R7
         SRDA  R2,32
         DR    R2,R4
         B     CONTINUE
THISYEAR SR    R6,R8
CONTINUE AR    R3,R7               YEAR+YEAR/4
         LA    R3,92(R3)           YEAR+YEAR/4+DAY TO APRIL 1ST
         SR    R2,R2
         LA    R5,7
         DR    R2,R5               A/C DAY FOR APRIL 1ST IN GR2
         LR    R3,R5
         SR    R3,R2
         AR    R3,RE
         CR    R2,R4               IS DAY 4?
         BL    DAYS
         AR    R3,R5
DAYS     LA    R3,21(R3)           NUMBER OF DAYS IN 1ST PERIOD
         LR    R2,RE
         LA    R4,PERIODS
COMPARE  CR    R6,R3               TO FIND IN WHICH
         BNH   OVER                   PERIOD THE
         AH    R3,0(R4)                  GIVEN DAY IS
         LA    R4,2(R4)
         AR    R2,RE
         B     COMPARE
ABE      MVC   0(2,RA),NAUT
         B     RET
OVER     CVD   R2,AREA
         MVC   0(2,RA),AREA+6
RET      L     RD,SVAR+4
         RETURN (14,12)
AREA     DS    D
SVAR     DS    18F
NAUT     DC    PL2'0'
PERIODS  DC    H'28,35,28,28,35,28,28,35,28,28,40'
         END
