 PLMACPD:PROC(Y,D,P);
 /****************************  PLMACPD   ******************************
 *                                                                     *
 * NAME - PLMACPD.                                                     *
 *                                                                     *
 * DATE OF LAST MODIFICATION - 78032.                                  *
 *                                                                     *
 * AUTHORS - ANDY KNIGHT.                                              *
 *                                                                     *
 * FUNCTION.                                                           *
 *         TO RETURN THE A/C PERIOD IN WHICH A GIVEN DAY FALLS WHEN    *
 *         SUPPLIED WITH AN 'IOCS' DATE (I.E. YY,DDD).                 *
 *                                                                     *
 * BASIC  FLOW.                                                        *
 *         1. IF THE YEAR IS A LEAP YEAR AND THE NUMBER OF DAYS IS     *
 *            GREATER THAN 59, THEN 1 IS SUBTRACTED FROM THE NUMBER OF *
 *            DAYS.  THIS HAS THE EFFECT OF REDUCING THE NUMBER OF     *
 *            DAYS TO WHAT WOULD BE THE CORRESPONDING NUMBER IF THE    *
 *            YEAR WERE NOT A LEAP YEAR, EXCEPT FOR THE NUMBER         *
 *            CORRESPONDING TO 29TH FEB. WHICH IS REDUCED TO THAT      *
 *            CORRESPONDING TO 28TH FEB.                               *
 *         2. THE NUMBER OF DAYS IS VALIDATED TO TEST WHETHER IT IS IN *
 *            THE RANGE 1 TO 365.  IF IT IS NOT AN ERROR IS SIGNALLED. *
 *         3. A HARDCODE BINARY SEARCH IS THEN PERFORMED TO LOCATE THE *
 *            A/C MONTH IN WHICH THE DAY FALLS.                        *
 *                                                                     *
 * ENTERED  FROM - ANY PL1 OPTIMISER PROGRAM.                          *
 *                                                                     *
 * INPUT.                                                              *
 *         Y (FIXED DEC(2)) - THE YEAR,                                *
 *         D (FIXED DEC(3)) - THE 'IOCS' DAY.                          *
 *                                                                     *
 * OUTPUT.                                                             *
 *         P (FIXED DEC(2)) - THE A/C PERIOD.                          *
 *                                                                     *
 * EXTERNAL  REFERENCES.                                               *
 *         NONE.                                                       *
 *                                                                     *
 * MACROS  REFERENCED.                                                 *
 *         NONE.                                                       *
 *                                                                     *
 * TABLES  REFERENCED.                                                 *
 *         NONE.                                                       *
 *                                                                     *
 * EXITS.                                                              *
 *         NORMAL EXIT TO CALLING PROCEDURE.                           *
 *                                                                     *
 * MESSAGES.                                                           *
 *         NONE.                                                       *
 *                                                                     *
 * ERROR  HANDLING.                                                    *
 *         IF AN INVALID NUMBER OF DAYS IS ENTERED THE VALUES (Y,D,0)  *
 *         ARE RETURNED TO THE CALLING PROCEDURE.                      *
 *         RETURNED TO THE CALLING PROCEDURE.                          *
 *                                                                     *
 * ENVIRONMENT - PL/1 OPTIMISER PROGRAMS (VER 1, REL 3.0, PTF 63).     *
 *                                                                     *
 * RESTRICTIONS - MAY ONLY BE ENTERED FROM PL/1 OPTIMISER PROGRAMS.    *
 *                                                                     *
 *****************************  PLMACPD   *****************************/
 DCL MOD BUILTIN;
 DCL Y   FIXED DEC(2);
 DCL P   FIXED DEC(2);
 DCL D   FIXED DEC(3);
           ON ERROR BEGIN;
                    P=0;
                    GOTO END;
                    END;
           IF MOD(Y,4)=0
           THEN IF D>59
                THEN D=D-1;
           IF D<1 × D>365
           THEN SIGNAL ERROR;
           IF D<182
           THEN IF D<91
                THEN SELECT;
                     WHEN (D<32) P=10;
                     WHEN (D<60) P=11;
                     OTHER P=12;
                     END;
                ELSE SELECT;
                     WHEN (D<121) P=1;
                     WHEN (D<152) P=2;
                     OTHER P=3;
                     END;
           ELSE IF D<274
                THEN SELECT;
                     WHEN (D<213) P=4;
                     WHEN (D<244) P=5;
                     OTHER P=6;
                     END;
                ELSE SELECT;
                     WHEN (D<305) P=7;
                     WHEN (D<335) P=8;
                     OTHER P=9;
                     END;
 END:      END PLMACPD;
