 PLMACDY:PROC(ACMONTH,ACYEAR,ACDAY,CALDAY,CALMONTH);
 /****************************  PLMACDY   ******************************
 *                                                                     *
 * NAME - PLMACDY.                                                     *
 *                                                                     *
 * DATE OF LAST MODIFICATION - 78032.                                  *
 *                                                                     *
 * AUTHORS - ANDY KNIGHT.                                              *
 *                                                                     *
 * FUNCTION.                                                           *
 *         TO RETURN THE ACCOUNTING DAY (I.E. DAY OF THE WEEK:         *
 *         MON - 1, TUE - 2, ETC.), DATE AND CALENDAR MONTH OF THE     *
 *         LAST DAY OF THE ACCOUNTING MONTH, WHEN SUPPLIED WITH AN     *
 *         ACCOUNTING MONTH AND AN ACCOUNTING YEAR.                    *
 *                                                                     *
 * BASIC  FLOW.                                                        *
 *         1. THE ACCOUNTING MONTH IS VALIDATED TO TEST WHETHER IT IS  *
 *            IN THE RANGE 1 TO 12.  IF IT IS NOT AN ERROR IS          *
 *            SIGNALLED.                                               *
 *         2. THE CALENDAR MONTH IS DERIVED FROM THE ACCOUNTING MONTH. *
 *         3. THE DATE OF THE LAST DAY OF THE CALENDAR MONTH IS LOOKED *
 *            UP BY USING THE VALUE OF THE ACCOUNTING MONTH.  AT THIS  *
 *            STAGE THE DATE OBTAINED FOR FEBRUARY IS ALWAYS THE 28TH. *
 *         4. ADJUSTMENTS ARE MADE TO TAKE INTO ACCOUNT LEAP-YEARS.    *
 *            AT THIS STAGE FEBRUARY'S DATE WILL BE CORRECTED IF       *
 *            NECESSARY.                                               *
 *         5. THE ACCOUNTING DAY IS CALCULATED.                        *
 *                                                                     *
 * ENTERED  FROM - ANY PL/1 OPTIMISER PROGRAM.                         *
 *                                                                     *
 * INPUT.                                                              *
 *         ACMONTH  (FIXED DEC(2)) - THE ACCOUNTING MONTH,             *
 *         ACYEAR   (FIXED DEC(2)) - THE ACCOUNTING YEAR.              *
 *                                                                     *
 * OUTPUT.                                                             *
 *         ACDAY    (FIXED DEC(1)) - THE DAY OF THE WEEK,              *
 *         CALDAY   (FIXED DEC(2)) - THE DATE OF THE LAST DAY OF THE   *
 *                                   MONTH,                            *
 *         CALMONTH (FIXED DEC(2)) - THE CALENDAR MONTH.               *
 *                                                                     *
 * EXTERNAL  REFERENCES.                                               *
 *         NONE.                                                       *
 *                                                                     *
 * MACROS  REFERENCED.                                                 *
 *         NONE.                                                       *
 *                                                                     *
 * TABLES  REFERENCED.                                                 *
 *         A     - USED TO LOOK UP THE DATE OF THE LAST DAY OF THE     *
 *                 MONTH.                                              *
 *         MTHDY - USED TO CALCULATE THE DAY OF THE WEEK OF THE LAST   *
 *                 DAY OF THE MONTH.                                   *
 *                                                                     *
 * EXITS.                                                              *
 *         NORMAL EXIT TO CALLING PROCEDURE.                           *
 *                                                                     *
 * MESSAGES.                                                           *
 *         NONE.                                                       *
 *                                                                     *
 * ERROR  HANDLING.                                                    *
 *         IF AN INVALID ACCOUNTING MONTH IS ENTERED THE VALUES        *
 *         (ACMONTH,ACYEAR,0,0,0) ARE RETURNED TO THE CALLING          *
 *         PROCEDURE.                                                  *
 *                                                                     *
 * ENVIRONMENT - PL/1 OPTIMISER PROGRAMS (VER 1, REL 3.0, PTF 63).     *
 *                                                                     *
 * RESTRICTIONS - MAY ONLY BE ENTERED FROM PL/1 OPTIMISER PROGRAMS.    *
 *                                                                     *
 *****************************  PLMACDY   *****************************/
 DCL MOD       BUILTIN;
 DCL ACMONTH   FIXED(2);
 DCL ACYEAR    FIXED(2);
 DCL CALDAY    FIXED(2);
 DCL CALMONTH  FIXED(2);
 DCL ACDAY     FIXED(1);
 DCL AM        FIXED(1);
 DCL Y         FIXED(2);
 DCL A(12)     FIXED(2) STATIC
               INIT(31,28,31,30,31,30,31,31,30,31,30,31);
 DCL MTHDY(12) FIXED(1) STATIC INIT(3,3,6,1,4,6,2,5,0,3,5,1);
 ON ERROR BEGIN;
          CALMONTH=0;
          CALDAY=0;
          ACDAY=0;
          GOTO END;
          END;
 IF ACMONTH<1 × ACMONTH>12
 THEN SIGNAL ERROR;
 CALMONTH=MOD(ACMONTH+2,12)+1;
 AM=1;
 Y=ACYEAR;
 CALDAY=A(CALMONTH);
 IF CALMONTH<4
 THEN DO;
      Y=Y+1;
      IF MOD(Y,4)=0
      THEN IF CALMONTH=2
           THEN CALDAY=29;
           ELSE IF CALMONTH=1
                THEN AM=2;
      END;
 ACDAY=MOD(MTHDY(CALMONTH)+Y*5/4-AM,7)+1;
 END:END PLMACDY;
