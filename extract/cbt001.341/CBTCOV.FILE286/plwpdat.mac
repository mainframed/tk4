*****************************  PLWPDAT   ******************************
*                                                                     *
* NAME - PLWPDAT (FUNCTION).                                          *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: DAY OF WEEK, DAY OF MONTH, MONTH AND YEAR       *
*                     IN CHARACTER FORM;                              *
*         WHEN PROVIDED WITH: PL1 DATE.                               *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         YYMMDD - PL1 DATE.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         DAY OF WEEK, DAY OF MONTH, MONTH AND YEAR (IN CHARACTER     *
*         FORM).                                                      *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWPDAT ENTRY(CHAR(6)) RETURNS(CHAR(29)VAR);            *
*         DCL X CHAR(29)VAR;                                          *
*         X=PLWPDAT(YYMMDD);                                          *
*                                                                     *
*         BEFORE CALL: YYMMDD='781123';                               *
*         AFTER CALL: X='THURSDAY 23RD NOVEMBER 1978'.                *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF AN INVALID PARAMETER IS DETECTED, THE 29-BYTE CHARACTER  *
*         STRING RETURNED IS SET TO 'X'.                              *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWPDAT   ******************************
PLWPDAT  CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SVAR+4
         LR    10,13
         LA    13,SVAR
         ST    13,8(10)
         L     9,0(1)
         L     9,0(9)              ADDR OF PL/1 DATE
         L     5,4(1)              ADDR OF DV FOR CHARACTER STRING
         MVC   LENGTH(31),SPACE
         MVC   YEAR+2(2),0(9)
         MVC   SPARE(6),0(9)
         NI    SPARE+5,X'CF'       TO MAKE YEAR
         NI    SPARE+3,X'CF'          MONTH AND DAY
         NI    SPARE+1,X'CF'             SEPARATELY MOVEABLE
         PACK  AREA,SPARE(2)
         LA    10,1
         CVB   2,AREA              YEAR
         CR    2,10
         BC    4,AB
         PACK  AREA,SPARE+2(2)
         CVB   4,AREA              MONTH
         CR    4,10
         BC    4,AB
         CH    4,OFFSET+4
         BC    11,AB
         LA    14,4
         LR    6,2
         SRDA  2,32
         DR    2,14
         AR    6,3                 YEAR+YEAR/4
         SR    4,10
         ST    4,MONTH
         CR    2,10                LEAP YEAR?
         BC    11,NEXT             NO, BRANCH TO NEXT
         CR    4,10                YES, IS MONTH JAN OR FEB?
         BC    2,NEXT              NO, BRANCH TO NEXT
         SR    6,10                YES, SUBTRACT 1 TO FIND A/C DAY
NEXT     PACK  AREA,SPARE+4(2)
         AR    4,4
         LA    3,TABLE
         AR    3,4
         AH    6,0(3)
         CVB   4,AREA              DAY OF MONTH
         CR    4,10
         BC    4,AB
         CH    4,TABLE+2
         BC    2,AB
         AR    6,4
         LA    8,7
         SRDA  6,32
         DR    6,8
         SR    2,2
         CR    6,8                 TO CHECK
         BC    13,OVER                THAT THE
         SR    6,8                       DAY NUMBER
OVER     CR    6,2                          LIES
         BC    2,ON                            BETWEEN
         AR    6,8                                1 AND 7
ON       LA    8,OFFSET
         SR    6,10                TO FIND
         AR    6,6                    WHICH DAY
         AR    8,6                       IT IS
         LA    2,WEEKDAYS                   AND HOW
         AH    2,0(8)                          MANY
         LH    6,2(8)                             CHARACTERS
         SH    6,0(8)                                IN IT
         SR    6,10                NUMBER OF CHARACTERS-1 TO BE MOVED
         LA    7,OUTPUT            ADDR OF FIRST AVAILABLE OUTPUT BYTE
         EX    6,MOVE              MOVE DAY NAME INTO OUTPUT AREA
         AR    7,6                 UPDATE NEXT AVAILABLE BYTE
         LA    7,2(7)                 IN OUTPUT AREA
         LR    6,10
         LA    2,4(9)
         CLI   4(9),X'F0'
         BC    7,AFTER
         SR    6,6
         AR    2,10
AFTER    EX    6,MOVE              MOVE DAY INTO OUTPUT AREA
         AR    7,6
         AR    7,10
         LR    6,10
         CLI   4(9),X'F1'
         BC    8,FORTH
         CLI   5(9),X'F1'
         BC    8,FIRST
         BC    4,FORTH
         CLI   5(9),X'F3'
         BC    4,SECOND
         BC    8,THIRD
FORTH    LA    2,TH
         B     CHAR
FIRST    LA    2,ST
         B     CHAR
SECOND   LA    2,ND
         B     CHAR
THIRD    LA    2,RD
CHAR     EX    6,MOVE              MOVE ST,ND,RD OR TH INTO AREA
         LA    7,3(7)              ADDR OF NEXT AVAILABLE OUTPUT AREA
         L     3,MONTH
         AR    3,3
         LA    2,MONTHS
         LA    4,MNOFSET
         AR    4,3
         AH    2,0(4)
         LH    6,2(4)
         SH    6,0(4)
         SR    6,10
         EX    6,MOVE              MOVE MONTH NAME INTO OUTPUT AREA
         AR    7,6
         LA    7,2(7)
         LA    2,YEAR
         LA    6,3
         EX    6,MOVE              MOVE YEAR INTO OUTPUT AREA
         LA    7,4(7)
         LA    8,OUTPUT
         SR    7,8
LOAD     STH   7,LENGTH
         L     5,0(5)
         MVC   0(31,5),LENGTH
         B     FINISH
AB       LR    7,10
         MVI   OUTPUT,C'X'
         B     LOAD
MOVE     MVC   0(0,7),0(2)
FINISH   L     13,SVAR+4
         LM    14,12,12(13)
         BR    14
SPARE    DS    CL6
         DS    0D
SVAR     DS    18F
AREA     DC    PL8'0'
MONTH    DS    F
SPACE    DC    C' '
LENGTH   DS    H
OUTPUT   DS    CL29
TABLE    DC    H'0,31,59,90,120,151,181,212,243,273,304,334'
OFFSET   DC    H'0,6,13,22,30,36,44,50'
WEEKDAYS DC    C'MONDAYTUESDAYWEDNESDAYTHURSDAYFRIDAYSATURDAYSUNDAY'
MNOFSET  DC    H'0,7,15,20,25,28,32,36,42,51,58,66,74'
MONTHS   DC    C'JANUARYFEBRUARYMARCHAPRILMAYJUNEJULYAUGUSTSEPTEMBER'
         DC    C'OCTOBERNOVEMBERDECEMBER'
YEAR     DC    C'19  '
ST       DC    C'ST'
ND       DC    C'ND'
RD       DC    C'RD'
TH       DC    C'TH'
         END
