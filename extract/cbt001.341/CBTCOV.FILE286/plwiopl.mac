*****************************  PLWIOPL   ******************************
*                                                                     *
* NAME - PLWIOPL (SUBROUTINE).                                        *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: PL1 FORMAT DATE;                                *
*         WHEN PROVIDED WITH: YEAR AND IOCS DATE.                     *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         YY1    - YEAR,                                              *
*         DDD    - IOCS DAY.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         YYMMDD - PL1 DATE.                                          *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWIOPL ENTRY(FIXED(2),FIXED(3),CHAR(6));               *
*         CALL PLWIOPL(YY1,DDD,YYMMDD);                               *
*                                                                     *
*         BEFORE CALL: YY1=67, DDD=123;                               *
*         AFTER CALL: YYMMDD='670503'.                                *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF INVALID INPUT PARAMETERS ARE DETECTED, THE OUTPUT        *
*         PARAMETER IS SET TO: 'ERROR '.                              *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWIOPL   ******************************
//S6      EXEC CASAL,SYSLMOD='SYS1.PLWLIB(PLWIOPL)'
//A.SYSIN   DD *
PLWIOPL  CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SVAR+4
         LR    10,13
         LA    13,SVAR
         ST    13,8(10)
         LM    2,4,0(1)           ADDRS OF YEAR,IOCS DAY,DV FOR PL1DATE
         L     4,0(4)              ADDR OF PL1 DATE
         ZAP   AREA,0(2,2)
         CVB   6,AREA              YEAR IN GR6
         SRDA  6,32
         LA    8,1
         LTR   7,7                 CHECK FOR INVALID YEAR
         BM    ERR
         LA    5,4
         DR    6,5                 YEAR/4 IN GR7 REMAINDER IN GR6
         MVC   AREA+6(2),0(3)
         CVB   5,AREA              IOCS DAY IN GR5
         CH    6,TABLE             LEAP YEAR?
         BC    7,ON                NO BRANCH TO ON
         C     5,SIXTY             YES, IS DAY>60?
         BC    4,ON                LESS THAN, BRANCH TO ON
         BC    8,FEB29             EQUAL TO, BRANCH TO FEB29
         SR    5,8                 GREATER THAN, SUBT 1 FROM IOCS DAY
ON       LA    9,12                TO FIND TO
         LA    10,TABLE+22            WHICH
         LA    3,2                       MONTH
COUNT    CH    5,0(10)                      THE
         BC    2,OVER                          IOCS
         SR    10,3                               DAY
         BCT   9,COUNT                               REFERS
OVER     CH    5,TABLE+24          IS DAY ILLEGAL?
         BC    2,ERR               YES, BRANCH TO ERR
         CR    5,8
         BC    4,ERR
         SH    5,0(10)             NO, OBTAIN DAY OF MONTH
OUT      ZAP   SPARE,0(2,2)        MOVE YEAR INTO SPARE
         MP    SPARE,HUND          MULTIPLY YEAR BY 100
         CVD   9,AREA              MONTH TO AREA
         AP    SPARE,AREA          ADD MONTH TO 100*YEAR
         MP    SPARE,HUND          MULTIPLY BY 100
         CVD   5,AREA              DAY TO AREA
         AP    SPARE,AREA          ADD DAY TO 100*(MONTH+100*YEAR)
         UNPK  AREA,SPARE
         OI    AREA+7,X'F0'        CHANGE TO CHARACTER FIELD
         MVC   0(6,4),AREA+2       MOVE PL1 DATE TO OUTPUT
         B     RET
FEB29    LA    9,2                 DATE IS FEB 29TH
         SH    5,TABLE+2
         B     OUT
ERR      MVC   0(6,4),=C'ERROR '   MOVE 'ERROR' TO OUTPUT
RET      L     13,SVAR+4
         RETURN (14,12)
TABLE    DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
         DS    0D
SVAR     DS    18F
AREA     DC    PL8'0'
SIXTY    DC    F'60'
HUND     DC    P'100'
SPARE    DC    PL5'0'
         END
