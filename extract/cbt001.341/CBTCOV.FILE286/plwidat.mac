*****************************  PLWIDAT   ******************************
*                                                                     *
* NAME - PLWIDAT (FUNCTION).                                          *
*                                                                     *
* DATE OF LAST MODIFICATION - ?.                                      *?
*                                                                     *
* AUTHORS - ?.                                                        *?
*                                                                     *
* FUNCTION.                                                           *
*         TO PROVIDE: DAY OF WEEK, DAY OF MONTH, MONTH AND YEAR, IN   *
*                     CHARACTER FORM;                                 *
*         WHEN PROVIDED WITH: YEAR AND IOCS DAY.                      *
*                                                                     *
* ENTERED  FROM - PL1 PROGRAMS.                                       *
*                                                                     *
* INPUT.                                                              *
*         YY1    - YEAR,                                              *
*         DDD    - IOCS DAY.                                          *
*                                                                     *
* OUTPUT.                                                             *
*         A CHARACTER STRING WITH ATTRIBUTES: 'CHAR(29) VAR', WHICH   *
*         CONTAINS DAY OF WEEK, DAY OF MONTH, CALENDER MONTH AND      *
*         YEAR.                                                       *
*                                                                     *
* EXAMPLE.                                                            *
*         DCL PLWIDAT ENTRY(FIXED(2),FIXED(3))                        *
*                     RETURNS(CHAR(29)VAR);                           *
*         DCL X CHAR(29)VAR;                                          *
*         X=PLWIDAT(YY1,DDD);                                         *
*                                                                     *
*         BEFORE CALL: YY1=67, DDD=123;                               *
*         AFTER CALL: X='WEDNESDAY 3RD MAY 1967'.                     *
*                                                                     *
* EXTERNAL  REFERENCES.                                               *
*         NONE.                                                       *?
*                                                                     *
* MACROS  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* TABLES  REFERENCED.                                                 *
*         NONE.                                                       *?
*                                                                     *
* EXITS.                                                              *
*         RETURNS TO CALLING PROGRAM.                                 *
*                                                                     *
* MESSAGES.                                                           *
*         NONE.                                                       *
*                                                                     *
* ERROR  HANDLING.                                                    *
*         IF INVALID INPUT PARAMETERS ARE DETECTED, THE VARYING       *
*         CHARACTER STRING TO BE RETURNED IS SET TO 'X'.              *
*                                                                     *
* ENVIRONMENT - PL1 PROGRAMS.                                         *
*                                                                     *
* RESTRICTIONS.                                                       *
*         NONE.                                                       *
*                                                                     *
*****************************  PLWIDAT   ******************************
PLWIDAT  CSECT
         STM   14,12,12(13)
         BALR  11,0
         USING *,11
         ST    13,SVAR+4
         LR    10,13
         LA    13,SVAR
         ST    13,8(10)
         L     9,0(1)              ADDR OF YEAR
         L     10,4(1)             ADDR OF IOCS DAY
         L     8,8(1)              ADDR OF DV FOR CHARACTER STRING
         MVC   LENGTH(31),SPARE
         ZAP   AREA,0(2,9)
         UNPK  YEAR+2(2),AREA+6(2)
         OI    YEAR+3,X'F0'
         CVB   2,AREA              YEAR
         MVC   AREA+6(2),0(10)
         CVB   4,AREA              DAY OF YEAR
         LA    10,1
         CR    2,10
         BC    4,AB
         CR    4,10
         BC    4,AB
         C     4,THSXSX
         BC    2,AB
         LR    6,4
         AR    6,2                 YEAR+DAY OF YEAR
         LA    5,4
         SRDA  2,32
         DR    2,5                 YEAR/4   GR2 CONTAINS REMAINDER
         AR    6,3                 YEAR+MOD(YEAR/4)+DAY OF YEAR
         SRDA  6,32
         LA    14,7
         DR    6,14                DIVIDE BY SEVEN
         CR    2,10
         BC    11,ON
         SR    6,10                DAY NUMBER
ON       CR    6,10
         BC    11,OVER
         AR    6,14
OVER     CR    6,14
         BC    13,THEN
         SR    6,14
THEN     LA    9,OFFSET
         SR    6,10                TO FIND
         AR    6,6                    WHICH DAY
         AR    9,6                       IT IS
         LR    3,2
         LA    2,WEEKDAYS                   AND HOW
         AH    2,0(9)                          MANY
         LH    6,2(9)                             CHARACTERS
         SH    6,0(9)                                IN IT
         SR    6,10                NUMBER OF CHARACTERS-1 TO BE MOVED
         LA    7,OUTPUT            ADDR OF FIRST AVAILABLE OUTPUT BYTE
         EX    6,MOVE              MOVE DAY NAME INTO OUTPUT
         AR    7,6                 UPDATE NEXT AVAILABLE BYTE
         LA    7,2(7)                 IN OUTPUT AREA
         LA    14,TABLE
         LA    6,12
         CR    3,10                IS THIS A LEAP YEAR?
         BC    11,NEXT             NO, BRANCH TO NEXT
         C     4,SIXTY             YES, COMPARE DAY OF YEAR WITH SIXTY
         BC    8,FEB29             EQUAL, GO TO FEB29
         BC    4,NEXT              LESS, GO TO NEXT
         SR    4,10                MORE, SUBTRACT ONE FROM DAY OF YEAR
NEXT     CH    4,0(14)             TO FIND
         BC    2,OUT                  MONTH
         LA    14,2(14)                  NUMBER
         BCT   6,NEXT
FEB29    SH    4,TABLE+20
         LA    6,2
         B     DAY
OUT      SR    6,10
         AR    6,6
         SH    4,0(14)
DAY      CVD   4,AREA              DAY OF MONTH
         ST    6,MONTH             STORE MONTH-ONE*2
         UNPK  AREA+6(2),AREA+6(2)
         SR    6,6
         OI    AREA+7,X'F0'
         CLI   AREA+6,X'F0'
         BC    8,AFTER
         AR    6,10
AFTER    LA    2,AREA+7
         SR    2,6
         EX    6,MOVE
         CLI   AREA+6,X'F1'
         BC    8,FORTH
         CLI   AREA+7,X'F1'
         BC    8,FIRST
         BC    4,FORTH
         CLI   AREA+7,X'F3'
         BC    4,SECOND
         BC    8,THIRD
FORTH    LA    2,TH
         B     CHAR
FIRST    LA    2,ST
         B     CHAR
SECOND   LA    2,ND
         B     CHAR
THIRD    LA    2,RD
CHAR     AR    7,6
         AR    7,10
         LR    6,10
         EX    6,MOVE              MOVE ST,ND,RD OR TH INTO AREA
         LA    7,3(7)              ADDR OF NEXT AVAILABLE OUTPUT AREA
         L     3,MONTH
         LA    2,MONTHS
         LA    4,MNOFSET
         AR    4,3
         AH    2,0(4)
         LH    6,2(4)
         SH    6,0(4)
         SR    6,10
         EX    6,MOVE              MOVE MONTH NAME INTO OUTPUT AREA
         AR    7,6
         LA    7,2(7)
         LA    2,YEAR
         LA    6,3
         EX    6,MOVE              MOVE YEAR INTO OUTPUT AREA
         LA    7,4(7)
         LA    14,OUTPUT
         SR    7,14
LOAD     STH   7,LENGTH
         L     8,0(8)
         MVC   0(31,8),LENGTH
         B     FINISH
AB       LR    7,10
         MVI   OUTPUT,C'X'
         B     LOAD
MOVE     MVC   0(0,7),0(2)
FINISH   L     13,SVAR+4
         LM    14,12,12(13)
         BR    14
SPARE    DC    C' '
LENGTH   DS    H
OUTPUT   DS    CL29
         DS    0D
SVAR     DS    18F
AREA     DC    PL8'0'
MONTH    DS    F
THSXSX   DC    F'366'
SIXTY    DC    F'60'
TABLE    DC    H'334,304,273,243,212,181,151,120,90,59,31'
OFFSET   DC    H'0,6,13,22,30,36,44,50'
WEEKDAYS DC    C'MONDAYTUESDAYWEDNESDAYTHURSDAYFRIDAYSATURDAYSUNDAY'
MNOFSET  DC    H'0,7,15,20,25,28,32,36,42,51,58,66,74'
MONTHS   DC    C'JANUARYFEBRUARYMARCHAPRILMAYJUNEJULYAUGUSTSEPTEMBER'
         DC    C'OCTOBERNOVEMBERDECEMBER'
YEAR     DC    C'19  '
ST       DC    C'ST'
ND       DC    C'ND'
RD       DC    C'RD'
TH       DC    C'TH'
         END
