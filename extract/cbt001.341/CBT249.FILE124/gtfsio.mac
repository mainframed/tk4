OPTION PS=60;
TITLE GTF SIO TRACE ANALYSIS;
RUN;
DATA DIRECTRY(KEEP=MEMBER TT R TRACK CYL1)
     ALIAS   (KEEP=MEMBER TT R TRACK CYL1)
     EXTENTS (KEEP=CYL_MIN CYL_MAX)
     DATASET (KEEP=DSNAME VOLSER);
 LENGTH DSCB $ 140 JFCB $ 176 DSNAME $ 44. VOLSER $ 6;
 FORMAT TT R CYL1 HEX4. ;
 FORMAT CC HH CYL_LOW CYL_HIGH $HEX4. ;
 RETAIN START ;
  *---- PDS MUST REFER TO A PARTITIONED DATA SET;
      INFILE PDS RECFM=U BLKSIZE=256 LRECL=256 DSCB=DSCB JFCB=JFCB;
      COL=3;
      INPUT COUNT PIB2. @; IF COUNT<14 THEN STOP;
      IF _N_ = 1 THEN DO;
*       GET DSNAME AND VOLSER;
        DSNAME = SUBSTR(JFCB,1,44);
        VOLSER = SUBSTR(JFCB,119,6);
        OUTPUT DATASET;
        FILE LOG; PUT DSNAME= VOLSER=;
*       GET LOWER LIMIT OF EXTENT;
         CC = SUBSTR(DSCB,64,2);
         HH = SUBSTR(DSCB,66,2);
         CCX = PUT(CC,HEX4.);
         HHX = PUT(HH,HEX4.);
         CC1 = INPUT(CCX,HEX4.);
         HH1 = INPUT(HHX,HEX4.);
         START=CC1*30+HH1;
         CYL_LOW=CC;
         CYL_MIN=CC1;
         FILE LOG;
         PUT CC= HH= CC1= HH1= START= CYL_LOW=;
*       GET UPPER LIMIT OF EXTENT;
         CC = SUBSTR(DSCB,68,2);
         HH = SUBSTR(DSCB,70,2);
         CCX = PUT(CC,HEX4.);
         HHX = PUT(HH,HEX4.);
         CC1 = INPUT(CCX,HEX4.);
         HH1 = INPUT(HHX,HEX4.);
         CYL_HIGH=CC;
         CYL_MAX=CC1;
         FILE LOG;
         PUT CC= HH= CC1= HH1= CYL_HIGH=;
         OUTPUT EXTENTS;
         END;
      ELSE DO;
         END;
      DO WHILE(COL<COUNT);
         INPUT @COL MEMBER $8. TT PIB2. R PIB1. IND PIB1. @;
         IF MEMBER>'99999999' THEN STOP;
         ALIAS=' '; IF IND>128 THEN ALIAS='*';
         COL=COL+12+2*MOD(IND,32);
         TRACK =TT + START;
         CYL1=INT(TRACK/30);
         IF ALIAS = '*' THEN DO;
            OUTPUT ALIAS;
            END;
         ELSE DO;
            OUTPUT DIRECTRY;
            END;
      END;
RUN;
DATA _NULL_;SET DATASET;
FILE TITLE;
PUT @1 'TITLE2 ' DSNAME ' ON ' VOLSER '   ;' ;
RUN;
%INCLUDE TITLE;
RUN;
DATA _NULL_;SET EXTENTS;
FILE MACROS;
PUT @1 'MACRO EXTENT'/
    @1 'IF CYL < ' CYL_MIN ' THEN DELETE;' /
    @1 'IF CYL > ' CYL_MAX ' THEN DELETE;' /
    @1 '%' ;
RUN;
PAGE;
%INCLUDE MACROS ;
RUN;
MACRO GTF_TIME
*  THERE ARE 21914 DAYS BETWEEN JAN 1,1900 AND JAN 1,1960
*  THERE ARE 86400 SECONDS IN A DAY
*  THERE ARE 1E6 MICRO SECONDS IN A SECOND;
FORMAT DATE WEEKDATE27. TIME TIME16.6;
*          21914*86400; *SECONDS BETWEEN JAN 1, 1900 AND JAN 1,1960;
*          1893369600;
*    EIGHT BYTES DIVIDED BY 4096 FOR BIT 51 =1 MICROSECONDS;
*    AND DIVIDED BY 1E6 TO CONVERT TO SECONDS (SAS TIME ALSO);
   TIME=TOD/(4096*1E6);
   GTFTIME=TIME-1893369600;
   DATE=DATEPART(GTFTIME);
   TIME=TIMEPART(GTFTIME);
%
DATA GTFSIO (KEEP=SIO_COND CYL HEAD RECORD TRACK TOD JOBNAME);
LENGTH DEFAULT=4;
INFILE GTFDATA RECFM=VBS ;
INPUT @1  RECID   PIB1. @11 EVENTID PIB2. @;
IF RECID   ^= 255   THEN DELETE;
IF EVENTID ^= 20736 THEN DELETE;  * 20736 = 5100X ;
INPUT @2 FMTID PIB1.
      @3 TOD PIB8.
      @17 CPUID PIB2.
      @19 JOBNAME $8.
      @39 SIO_COND PIB1.
      @40 DEVICE PIB2.
      @46 STATUS PIB2.
      @48 MODULE PIB1.
      @49 BIN PIB2.
      @51 CYL PIB2.
      @53 HEAD PIB2.
      @54 RECORD PIB1.
      @;
EXTENT ;
GTF_TIME;
TRACK = CYL * 30 + HEAD ;
OUTPUT GTFSIO;
RUN;
PAGE;
PROC MEANS DATA=GTFSIO NOPRINT MIN MAX;
VAR TOD;
OUTPUT OUT=DATES MIN=TOD1 MAX=TOD2;
RUN;
DATA DATES(KEEP=S_DATE E_DATE S_TIME E_TIME TIME1 TIME2);SET DATES;
FORMAT TIME1 TIME2 DATETIME17. ;
TOD=TOD1;GTF_TIME;S_DATE=DATE;S_TIME=TIME;
TOD=TOD2;GTF_TIME;E_DATE=DATE;E_TIME=TIME;
TIME1=DHMS(S_DATE,0,0,S_TIME);TIME2=DHMS(E_DATE,0,0,E_TIME);
FILE TITLE;PUT @1 'TITLE3 ' TIME1 '- ' TIME2 ';';
RUN;
PAGE;
%INCLUDE TITLE ;
RUN;
PROC FREQ DATA=GTFSIO;TABLES JOBNAME;
RUN;
PROC FREQ DATA=GTFSIO;TABLES CYL;
TITLE4  USAGE BY CYLINDER ADDRESS;
RUN;
PROC FREQ DATA=GTFSIO;TABLES TRACK;
TITLE4  USAGE BY TRACK ADDRESS;
RUN;
PROC SORT DATA=GTFSIO;BY TRACK CYL HEAD RECORD;
RUN;
PROC MEANS DATA=GTFSIO NOPRINT N;OUTPUT OUT=USAGE N=SIOS;VAR SIO_COND;
BY TRACK CYL HEAD RECORD;
RUN;
PAGE;
PROC SORT DATA=DIRECTRY;BY TT R;
RUN;
PROC SORT DATA=ALIAS   ;BY TT R;
RUN;
DATA ALIAS;SET ALIAS;RENAME MEMBER=ALIAS;
RUN;
DATA MEMBERS;MERGE DIRECTRY ALIAS;BY TT R;
RUN;
PROC SORT DATA=MEMBERS;BY MEMBER;
RUN;
PROC TRANSPOSE DATA=MEMBERS PREFIX=ALIAS OUT=MEMS1;BY MEMBER;VAR ALIAS;
RUN;
PROC SORT DATA=DIRECTRY;BY MEMBER;
RUN;
DATA MEMS2;MERGE DIRECTRY MEMS1;BY MEMBER;
RUN;
PROC SORT DATA=MEMS2;BY TRACK;RUN;
RUN;
DATA COUNTS(DROP=_NAME_ );
  MERGE USAGE MEMS2;BY TRACK;
RUN;
PROC PRINT DATA=COUNTS;ID MEMBER SIOS TRACK CYL CYL1 HEAD RECORD TT R;
TITLE4  USAGE BY TRACK LOCATION;
RUN;
DATA COUNTS;SET COUNTS;
IF SIOS = . THEN DELETE;
RUN;
PROC SORT DATA=COUNTS;BY DESCENDING SIOS;
RUN;
PROC PRINT DATA=COUNTS;ID MEMBER SIOS TRACK CYL CYL1 HEAD RECORD TT R;
TITLE4  USAGE BY SIO COUNTS;
RUN;
