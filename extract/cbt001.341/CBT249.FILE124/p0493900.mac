P4939    TITLE 'P0493900 - GTF SEEK ANAL. BE CYLINDER'
P0493900 CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
LINKX    EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
LINK     EQU   12
BASE     EQU   11
         PRINT ON,NOGEN
DEVENTRY EQU   10
         USING DEVTAB,DEVENTRY
         USING *,BASE
         SAVE (14,12)
         LR    BASE,R15
         LA    R10,SAVEAREA
         ST    R10,8(R13)
         ST    R13,SAVEAREA+4
         LR    R13,R10
         B     BEGIN
SAVEAREA DC    18F'-1'
BEGIN    DS    0H
         BAL   LINK,INIT
         LA    R8,HEADING2+5
         LA    R9,128(R8)      SET LIMIT ON HEADING SIZE
         BAL   LINK,READCARD
         BAL   LINK,READTAPE
         BAL   LINK,SUMMARY
         BAL   LINK,OUTPUT
         BAL   LINK,TOTAL
         BAL   LINK,CLOSE
         L    R13,SAVEAREA+4
         RETURN (14,12),RC=0
         EJECT
INIT     DS    0H
         OPEN  (GTF,(INPUT))
         OPEN  (CARD,(INPUT))
         OPEN  (PRINT,(OUTPUT))
         GETMAIN R,LV=800               GET STORAGE FOR DEVICE TABLE
         ST    R1,DEVTABPT
         LR    R2,R1
         LA    R3,800
         SR    R4,R4
         SR    R5,R5
         MVCL  R2,R4                   CLEAR DEVICE TABLE
         LR    DEVENTRY,R1
         MVC   794(2,R1),=X'FFFF'    FLAG LAST TABLE ENTRY
         BR    LINK
         EJECT
READCARD DS    0H
         LA    R8,8(R8)                POINT TO NEXT HEADING SLOT
         CR    R8,R9      TEST FOR HEADING TOO BIG
         BNL   EOFCARD     REJECT MORE CARDS
         GET   CARD
         LR    R7,R1
         GETMAIN R,LV=3236             GET STORAGE FOR DEVICE SEEK DATA
         LR    R2,R1
         LA    R3,3236
         SR    R4,R4
         SR    R5,R5
         MVCL  R2,R4                   CLEAR SEEK TABLE
         MVI   3232(R1),X'FF'          FLAG TO INIT LAST SEEK VALUE
         MVC   0(3,R8),0(R7)         MOVE DEVICE TO HEADING
         TR    0(80,R7),DEVTRANS      TRANSLATE LETTERS
NEXTDEV  DS    0H
         PACK  PACKWORK(2),0(3,R7)
         LH    R0,PACKWORK
         SRL   R0,4
         STH   R0,DEVADDR
         ST    R1,DEVSEEKP
         LA    DEVENTRY,8(DEVENTRY)
         CLC   DEVADDR+2(2),=X'FFFF'
         BE    EOFCARD             REJECT MORE CARDS
         CLI   4(R7),C' '
         BE    READCARD
         LA    R7,4(R7)
         B     NEXTDEV
EOFCARD  DS    0H
         BR    LINK
         EJECT
READTAPE DS    0H
         GET   GTF,GTFREC
         CLI   FORMATID,X'00'          CHECK FOR COMP IO,SIO
         BNE   READTAPE
         CLC   EVENTID(2),=X'5100'     CHECK FOR SIO RECORD
         BNE   READTAPE
         TM    SIOCC,X'30'          TEST FOR GOOD SIO
         BNZ   READTAPE
         L     DEVENTRY,DEVTABPT
         LA    R4,SIOSEEK
         LM    R2,R3,0(R4)
         SRDL  R2,8                    ISOLATE
         SRL   R3,16                        CYLINDER
         SLL   R3,2                MULT BY 4
COMPDEV  DS    0H
         CLC   DEVADDR(2),SIODEVAD
         BE    STORECYL
         LA    DEVENTRY,8(DEVENTRY)
         CLC   DEVADDR(2),=X'0000'
         BE    READTAPE
         B     COMPDEV
STORECYL DS    0H
         L      R2,DEVSEEKP
         L     R4,0(R2,R3)
         LA    R4,1(R4)
         ST    R4,0(R2,R3)
         B     READTAPE
EOFGTF   DS    0H
         BR    LINK
         EJECT
CLOSE    DS    0H
         CLOSE (GTF)
         CLOSE (GTF)
         CLOSE (CARD)
         CLOSE (PRINT)
         BR    LINK
         EJECT
OUTPUT   DS    0H
         PUT   PRINT,PAGETOP
         PUT   PRINT,HEADING1
         PUT   PRINT,HEADING2
         SR    R4,R4
         ZAP   PRINTCYL(2),=PL1'0'
OUTPUT1  DS    0H
         BAL   LINKX,EDITCYL
         CP    PRINTCYL(2),=PL2'808' LAST CYL
         BE    OUTPUT5
         AP    PRINTCYL(2),=PL1'1'
         L     DEVENTRY,DEVTABPT
         LA    R1,DETAIL+8
         SR    R0,R0
OUTPUT2  DS    0H
         C     R0,DEVSEEKP
         BE    OUTPUT3
         CLC   DEVADDR(2),=X'0000'
         BE    OUTPUT4
         L     R0,DEVSEEKP
         MVC   EDITWORK,MASK
         L     R2,DEVSEEKP
         L     R3,0(R2,R4)
         CVD   R3,PACKWORK
         ED    EDITWORK,PACKWORK+4
         MVC   0(8,R1),EDITWORK+2
         LA    R1,8(R1)
OUTPUT3  DS    0H
         LA    DEVENTRY,8(DEVENTRY)
         B     OUTPUT2
OUTPUT4  DS    0H
         PUT   PRINT,DETAIL
         LA    R4,4(R4)         POINT TO NEXT CYLINDER
         B     OUTPUT1
OUTPUT5  DS    0H
         BR    LINK
         EJECT
TOTAL    DS    0H
         PUT   PRINT,SPACE2
         SR    R0,R0               ZERO OLD TABLE POINTER
         MVC   DETAIL+1(7),=C'TOT D!   '
         L     DEVENTRY,DEVTABPT
         LA    R1,DETAIL+8
TOTAL1   DS    0H
         CLC   DEVADDR(2),=X'0000'
         BE    TOTAL3
         SR    R4,R4               ZERO TOTAL COUNTER
         L     R2,DEVSEEKP         POINT TO SEEK TABLE
         LA    R3,808              LOAD NUMBER OF CYLINDERS
         LR    R5,R2               TEMP SAVE OF SEEK POINTER
         CR    R2,R0               COMPARE FOR SAME DEVICE
         BNE   TOTAL2
         LA    DEVENTRY,8(DEVENTRY)         POINT TO NEXT DEVICE
         B     TOTAL1
TOTAL2   DS    0H
         A     R4,0(R2)            ADD COUNT
         LA    R2,4(R2)            POINT TO NEXT CYLINDER
         BCT   R3,TOTAL2
         CVD   R4,PACKWORK
         MVC   EDITWORK,MASK
         ED    EDITWORK,PACKWORK+4
         MVC   0(8,R1),EDITWORK+2
         LR    R0,R5               SAVE SEEK TABLE POINTER
         LA    R1,8(R1)            POINT TO NEXT OUTPUT LOCATION
         LA    DEVENTRY,8(DEVENTRY)  POINT TO NEXT DEVICE
         B     TOTAL1
TOTAL3   DS    0H
         PUT   PRINT,DETAIL
         BR    LINK
         EJECT
SUMMARY  DS    0H
         PUT   PRINT,PAGETOP
         PUT   PRINT,HEADING1
         PUT   PRINT,HEADING2
         SR    R4,R4
         ZAP   PRINTCYL(2),=PL1'0'
SUMMARY1 DS    0H
         BAL   LINKX,EDITCYL
         CP    PRINTCYL(2),=PL2'808' LAST CYL
         BE   SUMMARY6
         AP    PRINTCYL(2),=PL1'8'
         L     DEVENTRY,DEVTABPT
         LA    R1,DETAIL+8
         SR    R0,R0
         SR    R8,R8
SUMMARY2 DS    0H
         LA    R9,8
         SR    R3,R3
         C     R0,DEVSEEKP
         BE   SUMMARY4
         L     R0,DEVSEEKP
         CLC   DEVADDR(2),=X'0000'
         BE   SUMMARY5
         MVC   EDITWORK,MASK
         L     R2,DEVSEEKP
SUMMARY3 DS    0H
         A     R3,0(R2,R4)
         LA    R2,4(R2)            POINT TO NEXT CYLINDER IN GROUP
         BCT   R9,SUMMARY3
         CVD   R3,PACKWORK
         ED    EDITWORK,PACKWORK+4
         MVC   0(8,R1),EDITWORK+2
         LA    R1,8(R1)
SUMMARY4 DS    0H
         LA    DEVENTRY,8(DEVENTRY)
         B    SUMMARY2
SUMMARY5 DS    0H
         PUT   PRINT,DETAIL
         LA    R4,32(R4)        POINT TO NEXT CYLINDER
         B    SUMMARY1
SUMMARY6 DS    0H
         BR    LINK
         EJECT
EDITCYL  DS    0H
         UNPK  DETAIL+1(3),PRINTCYL(2)
         OI    DETAIL+3,X'F0'
         MVI   DETAIL+4,C'-'
         ZAP   PACKWORK(8),PRINTCYL(2)   DEVELOP
         CVB   R0,PACKWORK                 HEX DIGITS
         SLL   R0,4                PROVIDE DUMMY SIGN
         ST    R0,PACKWORK
         UNPK  DETAIL+5(3),PACKWORK+2(2)
         OI    DETAIL+7,X'F0'
         TR    DETAIL+5(3),DEVTRANS     TRANSLATE LETTERS
         BR    LINKX
         EJECT
GTF      DCB   DDNAME=GTF,DSORG=PS,EODAD=EOFGTF,MACRF=(GM)
CARD     DCB   DDNAME=SYSIN,DSORG=PS,EODAD=EOFCARD,MACRF=(GL)
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FBM
PACKWORK DS    D
EDITWORK DS    CL10
PRINTCYL DC    PL2'0'
SPACE2   DC    X'13'
         DC    132C' '
PAGETOP  DC    X'8B'               SKIP TO CHANNEL 1
         DC    132C' '
HEADING1 DS    0CL133
         DC    X'09'
         DC    C'   DISTRIBUTION OF SIO''S BY CYLINDER OF OCCURANCE   '
         DC    80C' '
HEADING2 DS    0CL133
         DC    X'09'
         DC    C'CYL '
         DC    128C' '
DETAIL   DS    0CL133
         DC    X'09'
         DC    132C' '
MASK     DC    X'40206B2020206B202120'
DEVTRANS DC    64X'00'
         DC    X'40'
         DC    128X'00'
         DC    X'FAFBFCFDFEFF'
         DC    41X'00'
         DC    C'0123456789'
         DC    C'ABCDEF'
         LTORG
         EJECT
GTFREC   DS    0D
         DS    2C
         DS    2C
RECORDID DS    C
FORMATID DS    C
TIMESTMP DS    CL8
EVENTID  DS    CL2
ASCB     DS    CL4
CPUID    DS    CL2
SIODATA  DS    0CL43
SIOJOBNM  DS    CL8
SIOCCWAD DS    CL8
SIOPURGE DS    CL4
SIOCC    DS    CL1
SIODEVAD DS    CL2
SIOCAW   DS    CL4
SIOSTAT  DS    CL2
SIOSEEK  DS    CL8
SIOFLAGS DS    CL6
         ORG   SIODATA
IODATA   DS    0CL53
IOJOBNM  DS    CL8
IOPSW    DS    CL8
IORQETCB DS    CL4
IOCSW    DS    CL8
IOCCWAD  DS    CL8
IOSENSE  DS    CL2
IODEVAD  DS    CL2
IOPURGE  DS    CL4
IOFLAGS  DS    CL9
         ORG
         DS    CL500
DEVTABPT DS    F
DEVTAB   DSECT
DEVADDR  DS    H
         DS    H
DEVSEEKP DS    F
         END   P0493900
