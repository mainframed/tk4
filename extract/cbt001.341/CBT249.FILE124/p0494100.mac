P4941    TITLE 'P0494100 - GTF SEEK MAP BY DATA SET / VOLUME'
P0494100 CSECT
BASE     EQU   11
LINK     EQU   12
LINKX    EQU   10
LOW      EQU   4
HIGH     EQU   5
MID      EQU   6
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
         PRINT ON,NOGEN
         USING TABLE,R3
         USING *,BASE
         SAVE  (14,12)
         LR    BASE,15
         LA    10,SAVEAREA
         ST    10,8(13)
         ST    13,SAVEAREA+4
         LR    13,10
         B     BEGIN
SAVEAREA DC    18F'-1'
BEGIN    DS    0H
         BAL   LINK,INIT
         BAL   LINK,READCARD
         BAL   LINK,INITTABL
         BAL   LINK,READGTF
         BAL   LINK,OUTPUT
         BAL   LINK,CLOSE
         L     13,SAVEAREA+4
         RETURN (14,12),RC=0
         EJECT
INIT     DS    0H
         OPEN  (GTF,(INPUT))
         OPEN  (VTOC,(INPUT))
         OPEN  (CARD,(INPUT))
         OPEN  (PRINT,(OUTPUT))
         BR    LINK
         EJECT
READCARD DS    0H
         GET   CARD                     READ DEVICE CARD
         MVC   HEADDEV(3),0(1)          MOVE DEVICE TO HEADING
         TR    0(80,1),DEVTRANS         TRANSLATE LETTERS
         SR    R2,R2
         LA    R3,4
NEXTDEV  DS    0H
         PACK  PACKWORK(2),0(3,1)
         LH    R6,PACKWORK
         SRL   R6,4
         STH   R6,DEVADDR(R2)
         LA    R2,2(R2)                 INCREMENT DEVICE POINTER
         LA    1,4(1)                   POINT TO NEXT ENTRY ON CARD
         BCT   R3,NEXTDEV
         GET   CARD                READ VOL SER IF PRESENT
         MVC   HEADSER(6),0(1)
EOFCARD  DS    0H
         BR    LINK
         EJECT
INITTABL DS    0H
         SR    R2,R2                    INITIALZE VTOC COUNTER
         MVI   VTOCPASS,C'1'            INDICATE FIRST PASS OF VTOC
INITTAB1 DS    0H
         BAL   LINKX,GETVTOC
         LA    R2,1(R2)
         B     INITTAB1
INITTAB2 DS    0H
         MVI   VTOCPASS,C'2'            INDICATE SECOND PASS OF VTOC
         CLOSE (VTOC)
         OPEN  (VTOC,(INPUT))
         SLL   R2,4                     CALCULATE TABLE SIZE
         LA    R2,16(R2)
         LR    R0,R2                    SET UP FOR GETMAIN
         GETMAIN R,LV=(0)
         ST    0,TABSIZE
         ST    1,TABPTR
         LR    R2,1
         LR    R3,0
         SR    R4,R4
         SR    R5,R5
         MVCL  R2,R4
         LR    R2,0
         L     R2,TABPTR
INITTAB3 DS    0H
         BAL   LINKX,GETVTOC
         TR    43(9,1),DEVTRANS         TRANSLATE LETTERS IN HIGH CC.HH
         PACK  PACKWORK(4),43(4,1)      EXTRACT CC
         L     R6,PACKWORK
         SRL   R6,4
         STH   R6,0(R2)
         PACK  PACKWORK(4),48(4,1)      EXTRACT HH
         L     R6,PACKWORK
         SRL   R6,4
         STH   R6,2(R2)
         LA    R2,16(R2)                POINT TO NEXT TABLE SLOT
         B     INITTAB3
INITTAB4 DS    0H
         MVC   0(4,R2),=X'FFFFFFFF'     SET HIGH CCHH AT TABLE END
         CLOSE (VTOC)
         BR    LINK
         EJECT
GETVTOC  DS    0H
         MVI   VTOCREC,C' '        BLANK INPUT
         MVC   VTOCREC+1(136),VTOCREC             AREA
         GET   VTOC,VTOCREC
         LA    1,VTOCREC           CALLER ASSUMED GET LOCATE
         CLI   VTOCREC+36,C'.'     EXTRACT
         BNE   GETVTOC                RECORDS
         CLI   VTOCREC+47,C'.'           WITH
         BNE   GETVTOC                      VTOC ENTRIES
         BR    LINKX
         EJECT
EOFVTOC  DS    0H
         CLI   VTOCPASS,C'1'
         BE    INITTAB2
         CLI   VTOCPASS,C'2'
         BE    INITTAB4
         B     OUTPUTX
         EJECT
READGTF  DS    0H
         GET   GTF,GTFREC
         CLI   FORMATID,X'00'           CHECK FOR COMP IO,SIO
         BNE   READGTF
         CLC   EVENTID(2),=X'5100'      CHECK FOR SIO RECORD
         BNE   READGTF
         TM    SIOCC,X'30'              TEST FOR GOOD SIO
         BNZ   READGTF
         LA    R1,DEVADDR
READGTF1 DS    0H
         CLC   0(2,R1),SIODEVAD         CHECK FOR MATCH ON DEVICE
         BE    READGTF2
         LA    R1,2(R1)
         CLC   0(2,R1),=X'0000'
         BE    READGTF
         B     READGTF1
READGTF2 DS    0H
         LA    R1,SIOSEEK
         LM    R2,R3,0(R1)
         SRDL  R2,8                     ISOLATE CCHH IN R3
         CLI   FIRSTIO,C'Y'
         BE    READGTF8
         LR    R2,R3               SET R2 FOR BINARY SEARCH
         L     R3,TABPTR
READGTF3 DS    0H
         BAL   LINKX,BINSRCH
READGTF4 DS    0H
         L     R1,SIOCOUNT
         LA    R1,1(R1)
         ST    R1,SIOCOUNT
         LR    R0,R2
         SRL   R0,16
         CH    R0,OLDCCHH               COMPARE FOR PHYSICAL SEEK
         BE    READGTF7
         L     R1,TOTSEEKS
         LA    R1,1(R1)
         ST    R1,TOTSEEKS
         L     R0,OLDCCHH
         ST    R2,OLDCCHH
         C     R0,ENDCCHH               COMPARE
         BH    READGTF5                         FOR
         S     R3,=F'16'                            SEEK
         C     R0,ENDCCHH                                FROM
         LA    R3,16(R3)                                      OTHER
         BH    READGTF6                                             DS
READGTF5 DS    0H
         L     R1,EXTSEEKS
         LA    R1,1(R1)
         ST    R1,EXTSEEKS
         B     READGTF7
READGTF6 DS    0H
         L     R1,INTSEEKS
         LA    R1,1(R1)
         ST    R1,INTSEEKS
READGTF7 DS    0H
         B     READGTF
READGTF8 DS    0H
         MVI   FIRSTIO,C'N'
         ST    R3,OLDCCHH
         B     READGTF
EOFGTF   DS    0H
         BR    LINK
         EJECT
BINSRCH  DS    0H
         L     HIGH,TABSIZE             INITIALZE
         S     HIGH,=F'16'                        HIGH POINTER
         SR    LOW,LOW                  INITIALZE LOW POINTER
*  R3  POINTS TO START OF TABLE
*  R2  CONTAINS CCHH OF CURRENT SIO
BINSRCH1 DS    0H
         LR    MID,HIGH                 CALCULATE
         AR    MID,LOW                            NEW
         SRL   MID,5                                  MID-POINT
         SLL   MID,4                    TABLE ENTRIES ARE 16 BYTES
         C     R2,0(R3,MID)
         BH    BINSRCH2                 MOVE LOW POINTER
         LTR   MID,MID
         BZ    BINSRCH4                 HIT ON FIRST TABLE ENTRY
         S     MID,=F'16'
         C     R2,0(R3,MID)
         BH    BINSRCH3                 HIT ON CURRENT INTERVAL
         LR    HIGH,MID                 SET NEW HIGH POINTER
         B     BINSRCH1
BINSRCH2 DS    0H
         CR    LOW,MID
         BE    BINSRCH3
         LR    LOW,MID
         B     BINSRCH1
BINSRCH3 DS    0H
         LA    MID,16(MID)              RESET TO CURRENT INTERVAL
BINSRCH4 DS    0H
         LA    R3,0(R3,MID)             LOAD HIT POINTER INTO R3
         BR    LINKX
         EJECT
OUTPUT   DS    0H
         BAL   LINKX,OUTHEAD
         OPEN  (VTOC,(INPUT))
         MVI   VTOCPASS,C'3'       INDICATE 3RD PASS OF VTOC
         OPEN  (GTF,(INPUT))
         L     R3,TABPTR
         SR    R4,R4                    COUNTER FOR SIO
         SR    R5,R5                    COUNTER FOR EXTERNAL SEEKS
         SR    R6,R6                    LINE COUNTER
         SR    R7,R7               COUNTER FOR INTERNAL SEEKS
OUTPUT1  DS    0H
         BAL   LINKX,GETVTOC
         MVC   DETVTOC(78),32(1)
         PACK  PACKWORK(4),DETVTOC+23(4)
         AP    TOTTRACK(4),PACKWORK(4)
         A     R4,SIOCOUNT
         A     R5,EXTSEEKS
         A     R7,INTSEEKS
         L     R0,SIOCOUNT
         CVD   R0,PACKWORK
         MVC   DETSIO(10),MASK
         ED    DETSIO(10),PACKWORK+4
         L     R0,EXTSEEKS
         CVD   R0,PACKWORK
         MVC   DETESEEK(10),MASK
         ED    DETESEEK(10),PACKWORK+4
         L     R0,INTSEEKS
         CVD   R0,PACKWORK
         MVC   DETISEEK(10),MASK
         ED    DETISEEK(10),PACKWORK+4
         A     R0,EXTSEEKS
         CVD   R0,PACKWORK
         MVC   DETTSEEK(10),MASK
         ED    DETTSEEK(10),PACKWORK+4
         PUT   PRINT,DETAIL
         LA    R3,16(R3)           POINT TO NEXT TABLE ENTRY
         LA    R6,1(R6)
         C     R6,=F'55'
         BL    OUTPUT1
         SR    R6,R6
         BAL   LINKX,OUTHEAD
         B     OUTPUT1
OUTPUTX  DS    0H
         PUT   PRINT,SPACE
         MVC   TOTPTRK(10),MASK
         ED    TOTPTRK(10),TOTTRACK
         MVC   TOTPSIO(10),MASK
         CVD   R4,PACKWORK
         ED    TOTPSIO(10),PACKWORK+4
         MVC   TOTPESEK(10),MASK
         CVD   R5,PACKWORK
         ED    TOTPESEK(10),PACKWORK+4
         MVC   TOTPISEK(10),MASK
         CVD   R7,PACKWORK
         ED    TOTPISEK(10),PACKWORK+4
         L     R0,TOTSEEKS
         CVD   R0,PACKWORK
         MVC   TOTPSEEK(10),MASK
         ED    TOTPSEEK(10),PACKWORK+4
         PUT   PRINT,TOTALS
         BR    LINK
         EJECT
OUTHEAD  DS    0H
         PUT   PRINT,TOP
         MVC   HEADPAGE(4),MASK1
         ED    HEADPAGE(4),PAGE
         PUT   PRINT,HEADING1
         PUT   PRINT,HEADING2
         AP    PAGE(2),=PL1'1'
         BR    LINKX
         EJECT
CLOSE    DS    0H
         CLOSE (GTF)
         CLOSE (VTOC)
         CLOSE (CARD)
         CLOSE (PRINT)
         BR    LINK
         EJECT
GTF      DCB   DDNAME=GTF,DSORG=PS,EODAD=EOFGTF,MACRF=(GM)
VTOC     DCB   DDNAME=VTOC,DSORG=PS,EODAD=EOFVTOC,MACRF=(GM)
CARD     DCB   DDNAME=CARD,DSORG=PS,EODAD=EOFCARD,MACRF=(GL)
PRINT    DCB   DDNAME=PRINT,DSORG=PS,MACRF=(PM),LRECL=133,RECFM=FBM
         EJECT
DEVADDR  DC    5H'0'
         SPACE 2
DEVTRANS DC    64X'00'
         DC    X'40'
         DC    128X'00'
         DC    X'FAFBFCFDFEFF'
         DC    41X'00'
         DC    C'0123456789'
         DC    6X'00'
         SPACE 2
FIRSTIO  DC    C'Y'
MASK     DC    X'40206B2020206B202120'
MASK1    DC    X'40202020'
OLDCCHH  DC    F'-1'
PACKWORK DS    D
PAGE     DC    PL2'1'
TOTTRACK DC    PL4'0'
TABPTR   DS    F
TABSIZE  DS    F
TOTSEEKS DC    F'0'
VTOCPASS DC    C'0'
         LTORG
         EJECT
TOP      DC    X'8B'
         DC    132C' '
         SPACE 2
SPACE    DC    X'0B'
         DC    132C' '
         SPACE 2
HEADING1 DC    X'09'
         DC    C'START IO AND SEEK DISTRIBUTION BY DATA SET FOR '
HEADDEV  DC    C'   '
         DC    C'     VOL=SER='
HEADSER  DC    C'SCRTCH'
         DC    55C' '
         DC    C'PAGE'
HEADPAGE DC    C'    '
         SPACE 2
HEADING2 DC    X'11'
         DC    C'START EXT   END EXT  TRACKS  EXTS '
         DC    C'DATA SET NAME                                 '
         DC    C'START IO  EXT SEEK  INT SEEK  TOT SEEK'
         DC    14C' '
         SPACE 2
DETAIL   DC    X'09'
DETVTOC  DC    78C' '
DETSIO   DC    10C' '
DETESEEK DC    10C' '
DETISEEK DC    10C' '
DETTSEEK DC    10C' '
         DC    14C' '
         SPACE 2
TOTALS   DC    X'09'
         DC    17C' '
TOTPTRK  DC    10C' '
         DC    51C' '
TOTPSIO  DC    10C' '
TOTPESEK DC    10C' '
TOTPISEK DC    10C' '
TOTPSEEK DC    10C' '
         DC    14C' '
         EJECT
GTFREC   DS    0D
         DS    2C
         DS    2C
RECORDID DS    C
FORMATID DS    C
TIMESTMP DS    CL8
EVENTID  DS    CL2
ASCB     DS    CL4
CPUID    DS    CL2
SIODATA  DS    0CL43
SIOJOBNM DS    CL8
SIOCCWAD DS    CL8
SIOPURGE DS    CL4
SIOCC    DS    CL1
SIODEVAD DS    CL2
SIOCAW   DS    CL4
SIOSTAT  DS    CL2
SIOSEEK  DS    CL8
SIOFLAGS DS    CL6
         DS    CL500
         SPACE 3
VTOCREC  DS    0D
         DS    CL137
         EJECT
TABLE    DSECT
ENDCCHH  DS    4C
SIOCOUNT DS    4C
EXTSEEKS DS    4C
INTSEEKS DS    4C
         END   P0494100
