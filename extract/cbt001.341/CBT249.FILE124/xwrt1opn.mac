XWRT     TITLE 'WRITE DSN AND PDS RECORDS SUBROUTINE'
* STATUS   VERSION 1.0 22 AUGUST 1973.
         SPACE
* AUTHOR   P. L. DENNISON, LONDON LIFE INSURANCE COMPANY.
         SPACE
* FUNCTION/OPERATION   RECEIVES THE VOLUMN AND FORMAT 1 DSCB FOR
*        EACH DATA SET PROCESSED BY VTOCLIST AND WRITES DSN RECO
*        IF DDNAME DSNFILE IS PRESENT IN THE TIOT. RECEIVES THE
*        PDS DIRECTORY ENTRY OF EACH MEMBER FOR EACH PDS PROCESS
*        BY VTOCLIST (IF PARM=PDS) AND WRITES PDS RECORDS IF DDN
*        PDSFILE IS PRESENT IN THE TIOT.
         SPACE
* ENTRY POINTS   ENTER AT 'XWRT1OPN' WITH STD CALL PASSING ADDRE
*        OF TIOT TO CHECK TIOT FOD DDNAMES AND OPEN FILES IF PRE
*        ENTER AT 'XWRT2DSN' WITH STD CALL PASSING ADDRESS OF VO
*        SERIAL AND FORMAT1 DSCB TO WRITE DSN RECORD.
*        ENTER AT 'XWRT3PDS' WITH STD CALL PASSING ADDRESS OF PD
*        DIRECTORY ENTRY TO WRITE PDS RECORD.
*        ENTER AT 'XWRT4CLO' WITH STD CALL TO CLOSE FILES IF OPE
         SPACE
* INPUT   NO INPUT FILES.
         SPACE
* OUTPUT   DSN AND PDS FILES WRITTEN.
         SPACE
* DATA SETS   THE OUTPUT DATA SETS ARE SEQUENTIAL, ARRANGED FOR
*        BLOCKED FORMAT F RECORDS. THE DDNAMES ARE 'DSNFILE' AND
*        'PDSFILE'.
         SPACE
* EXTERNAL ROUTINES   NONE.
         SPACE
* EXITS - NORMAL   RETURNS VIA REG 14 WITH RETURN CODE 0 IN ALL
         SPACE
* EXITS - ERROR   NONE.
         SPACE
* TABLES/WORK AREAS   THE WORK AREAS TO BUILD THE OUTPUT RECORDS
*        JUST BEFORE THE DUMMY SECTIONS USED TO DESCRIBE THE
*        PARAMETER LISTS AT THE END OF THE PROGRAM.
         SPACE
* ATTRIBUTES   SERRIALLY RESUABLE.
         SPACE
* NOTES   NONE.
         EJECT
XWRT1OPN CSECT
         ENTRY XWRT2DSN,XWRT3PDS,XWRT4CLO
         SPACE
* REGISTER ASSIGNMENTS
R1       EQU   1                       PARM POINTER
R10      EQU   10                      WORK REGISTER
R11      EQU   11                      WORK REGISTER
R12      EQU   12                      BASE REGISTER
R13      EQU   13                      SAVEAREA POINTER
R14      EQU   14                      BAL REGISTER
R15      EQU   15                      TEMP BASE REGISTER
OPENBIT  EQU  X'10'                    DCB IS OPEN (IN DCBOFLGS)
         EJECT
* CHECK TIOT FOR DD CARDS AND OPEN REQUIRED FILES
*
         USING XWRT1OPN,R12
         SAVE  (14,12),,*
         LR    R12,R15                 BASE
         BAL   R14,SETUP               SAVEAREA STUFF
         L     R11,0(R1)               TIOT DSECT BASE
         USING TIOT1,R11
         LA    R11,TIOENTRY            TIOT ENTRY BASE
         DROP  R11
         USING TIOENTRY,R11
NEWENTRY EQU   *
         CLI   TIOELNGH,X'00'          LAST ENTRY
         BE    ENDTIOT
         CLC   TIOEDDNM,=C'DSNFILE '   DDNAME DSNFILE
         BE    DSNFOUND
         CLC   TIOEDDNM,=C'PDSFILE '   DDNAME PDSFILE
         BE    PDSFOUND
NEXTENTR EQU   *
         SR    R1,R1
         IC    R1,TIOELNGH             LENGTH OF ENTRY
         AR    R11,R1                  NEXT TIOT ENTRY BASE
         B     NEWENTRY
         DROP  R11
         SPACE
DSNFOUND EQU   *
         OPEN  (DSNFILE,(OUTPUT))
         B     NEXTENTR                LOOP
         SPACE
PDSFOUND EQU   *
         OPEN  (PDSFILE,(OUTPUT))
         B     NEXTENTR                LOOP
         SPACE
ENDTIOT  EQU   *
         B     RETURN                  EXIT
         EJECT
* WRITE DSN RECORD IF REQUIRED
*
         USING *,R15
XWRT2DSN EQU   *
         SAVE  (14,12),,XWRT2DSN
         B     XDSN01                  AROUND BASE VALUE
ABASE1   DC    A(XWRT1OPN)
XDSN01   EQU   *
         L     R12,ABASE1              BASE
         DROP  R15
         BAL   R14,SETUP               SAVEAREA STUFF
         USING VOLDSECT,R11
         USING DSCB1,R10
         L     R11,0(R1)               PICK UP
         L     R10,4(R1)                PARM FIELDS
         MVC   VOLUMN,VOLSER           STORE VOLUMN
         MVC   DSNAME,DS1DSNAM         STORE DSN
         TM    DSNFILE+48,OPENBIT      FILE OPEN
         BZ    RETURN                  NO
         MVC   DSNVOL,VOLSER           BUILD
         MVC   DSNDSCB,DSCB1            RECORD
         PUT   DSNFILE,DSNREC          WRITE RECORD
         B     RETURN
         EJECT
* WRITE PDS RECORD IF REQUIRED
*
         USING *,R15
XWRT3PDS EQU   *
         SAVE  (14,12),,XWRT3PDS
         B     XPDS01                  AROUND BASE CONSTANT
ABASE2   DC    A(XWRT1OPN)
XPDS01   EQU   *
         L     R12,ABASE2              BASE
         DROP  R15
         BAL   R14,SETUP               SAVEAREA STUFF
         TM    PDSFILE+48,OPENBIT      FILE OPEN
         BZ    RETURN                  NO
         USING PDSENTRY,R11            PICK UP
         L     R11,0(R1)                PARM
         MVC   PDSVOL,VOLUMN           BUILD
         MVC   PDSDSN,DSNAME            RECORD
         MVC   PDSPDE,PDSINFO
         PUT   PDSFILE,PDSREC          WRITE RECORD
         B     RETURN                  EXIT
         EJECT
* CLOSE FILES
*
         USING *,R15
XWRT4CLO EQU   *
         SAVE  (14,12),,XWRT4CLO
         B     XCLO01                  AROUND BASE CONSTANT
ABASE3   DC    A(XWRT1OPN)
XCLO01   EQU   *
         L     R12,ABASE3              BASE
         DROP  R15
         BAL   R14,SETUP               SAVEAREA STUFF
         TM    DSNFILE+48,OPENBIT      FILE OPEN
         BZ    XCLO02                  NO
         CLOSE DSNFILE
XCLO02   EQU   *
         TM    PDSFILE+48,OPENBIT      FILE OPEN
         BZ    RETURN                  NO
         CLOSE PDSFILE
         B     RETURN                  EXIT
         EJECT
RETURN   EQU   *
         L     R13,SAVEAREA+4          RESTORE SAVEAREA POINTER
         RETURN (14,12),,RC=0
         SPACE
SETUP    EQU   *                       SAVE AREA STUFF
         XC    SAVEAREA(72),SAVEAREA   CLEAR SAVEAREA
         ST    R13,SAVEAREA+4          PREVIOUS SAVEAREA ADDR
         LR    R15,R13                 STORE POINTER
         LA    R13,SAVEAREA
         ST    R13,8(R15)              NEXT SAVEAREA ADDR
         BR    R14
         EJECT
DSNFILE  DCB   DDNAME=DSNFILE,DEVD=DA,DSORG=PS,LRECL=DSNLENG,          X
               MACRF=PM,RECFM=FB
         SPACE
PDSFILE  DCB   DDNAME=PDSFILE,DEVD=DA,DSORG=PS,LRECL=PDSLENG,          X
               MACRF=PM,RECFM=FB
         SPACE
SAVEAREA DS    18F                     REGISTER SAVE AREA
DSNREC   EQU   *                       WORKAREA FOR DSN RECORD
DSNVOL   DS    CL6                     VOLUMN SERIAL
DSNDSCB  DS    CL156                   DATA SET CONTROL BLOCK 1
DSNLENG  EQU   *-DSNREC                RECORD LENGTH
         SPACE
PDSREC   EQU   *                       WORKAREA FOR PDS RECORD
PDSVOL   DS    CL6                     VOLUMN SERIAL
PDSDSN   DS    CL44                    DATA SET NAME
PDSPDE   DS    CL74                    PARTITIONED DIRECTORY ENT
PDSLENG  EQU   *-PDSREC                RECORD LENGTH
         SPACE
VOLUMN   DC    CL6' '                  VOLUMN SAVE AREA
DSNAME   DC    CL44' '                 DSN SAVE AREA
         SPACE
         LTORG
         EJECT
* DUMMY SECTIONS
         SPACE
VOL      DSECT
VOLDSECT EQU  *
VOLSER   DS    CL6
         SPACE
DSCB     DSECT
DSCB1    EQU  *
DS1DSNAM DS    CL44
         SPACE
PDS      DSECT
PDSENTRY EQU  *
PDSINFO  DS   CL74
         SPACE
TIOT     DSECT
TIOT1    EQU  *
         DS   6F
TIOENTRY EQU  *
TIOELNGH DS   CL1
         DS   CL3
TIOEDDNM DS   2F
         END
