//GUYAZAP   JOB (U32050,17F),GUY,CLASS=E,MSGCLASS=N,NOTIFY=GUY
/*ROUTE PRINT LOCAL
// EXEC ASMFC
BLGI2TRW CSECT
**
****    FOR USE AFTER UZ52795, 6, & 7 ONLY
**
         USING BLGI2TRW+X'1C',12
         USING WRKAREA,11
         PRINT GEN,DATA
*
**       BRANCH TO PATCH AREA
*
         ORG   BLGI2TRW+X'05AC'
         B     PATCH                 WAS  X'5840B054'
RETURN   DS    0H
*
**       LENGTH OF DYNAMIC AREA TO GET
*
         ORG   BLGI2TRW+X'0684'
         DC    A(WKLEN)              WAS  X'00000120'
*
**       THE PATCH
*
         ORG   BLGI2TRW+X'06C0'
*
*  ON ENTRY REGISTER USAGE
*           13  SAVE AREA
*           12  BASE REGISTER
*           11  DYNAMIC WORK AREA
*            6  LENGTH OF DATA IN INPUT BUFFER
*            5  -> SSIB
*            4  -> TBUF
*        ALL OTHER REGISTERS FREE
*
*     WE WILL USE THE FOLLOWING REGISTERS FOR WORK
*            1  WORK
*            2  -> TSO ENVR SUB SECTION    (-> SSIBMSEN)
*            3  -> TGET BUFFER  TO BE FILLED IN
*           14  WORK
*           15  WORK
*
PATCH    DS    0H
         L     4,X'54'(0,11)       ACCESS TBUF (OVERLAID INST)
         LTR   6,6                 IS TGET LENGTH ZERO
         BNZ   RETURN              IF NOT THEN TUBE EXISTS
         L     1,4(0,5)            CYCLE THROUGH  5->SSIB 1->MVS
         L     2,0(0,1)              BLOCKS TO GET  1->TSO
         L     3,8(0,2)                INPUT BUFFER OF SCREEN  1->TGET
*                                                                BUFFER
         MVC   0(11,3),A           MOVE IN QUIT RESPONSE
         LA    6,11                SET LENGTH OF RESPONSE
         ICM   15,15,16(4)         GET OUTPUT BUF ADDR 4->TBUF
         BZ    RETURN              IF NONE THEN EXIT
*
**       NOW CHECK IF THE PANEL NAME IS IN THE TABLE
**       IF IT IS THEN DONT PRINT IT, JUST DO GETLINE
**             (IF WE COULD ACCESS THE MFB THE FOLLOWING
**              TWO INSTRUCTIONS COULD BE ELIMINATED
*
         CLC   30(8,15),ENTER      IS THIS BLG0ENTR
         BE    GET                 IF SO THEN CONSIDER IN TABLE
         LA    14,TABLE            POINT AT START OF TABLE
LOOP     CLI   0(14),0             END OF TABLE
         BE    PRINT               NOT IN TABLE SO PRINT
         CLC   22(8,15),0(14)      CHECK NAME
         BE    GET                 IF FOUND GO DO GETLINE
         LA    14,8(0,14)          BUMP TO NEXT ONE
         B     LOOP                AND TRY AGAIN
*
**       INSERT PRINT COMMAND INTO BUFFER
*
PRINT    LA    6,23                SET LENGTH OF PRINT
         MVC   7(16,3),D           MOVE IN PRINT COMMAND
         B     RETURN              ALL DONE
*
**       INFO/MGMT MUST GET NEXT STRING FROM USER SO DO GETLINE
*
GET      DS    0H
         L     1,4(0,2)            NOW R1->CPPL
         L     14,4(0,1)           GET UPT ADDR
         L     15,12(0,1)          GET ECT ADDR
         LA    1,IOPL              GET ADDR OF IOPL
         ST    14,0(0,1)           SAVE ADDR OF UPT
         ST    15,4(0,1)           SAVE ADDR OF ECT
         LA    14,ECB              GET ADDR OF ECB
         ST    14,8(0,1)           SAVE ADDR OF ECB
         LA    14,GTPB             GET ADDR OF GETLINE P BLOCK
         ST    14,12(0,1)          SAVE ADDR OF GTPB
         XC    GTPB(8),GTPB        CLEAR BLOCK
         MVI   GTPB+2,X'80'        SET FLAG
         GETLINE MF=(E,(1))        GO AND ASK ON SYSTSIN
         CH    15,RC               SEE IF END OF CLIST
         BE    GET                 IF SO REISSUE GETLINE
         C     15,MARC             CHECK RETURN CODE
         BH    RETURN              RETURN THE QUIT STILL IN BUFFER
         L     15,GTPB+4           GET ADDR OF BUFFER
         LH    1,0(0,15)           GET LENGTH OF BUFFER
         S     1,MARC              CHECK LENGTH <= 4
         BNP   RETURN              IF SO THEN RETURN THE ;QUIT
         BCTR  1,0                 GET MACHINE LENGHT
         EX    1,MOVEIT
         LA    6,7(0,1)            GET PROPER LENGTH
GTLNEOF  LR    1,15                MOVE IN ADDR
         LH    0,0(0,1)            GET LENGTH
         ICM   0,8,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)   FREE BUFFER
         B     RETURN
MOVEIT   MVC   6(0,3),4(15)        MOVE DATA
RC       DC    H'16'
MARC     DC    F'4'
A        DC    X'7D5CF7115CF6',C';QUIT'
D        DC    C'PR S U,2,INFORPT'
SUBPOOL  DC    X'01'
*
**       THIS TABLE OF PANEL NAMES IS THOSE THAT WILL NOT BE PRINTED
**       INSTEAD A GETLINE WILL BE ISSUED
*
ENTER    DC    C'BLG0ENTR'       **MAIN MENU**
TABLE    DC    C'BLG1M234'       **PRINT COMPLETED**
         DC    C'BLG1M212'       **REPORT COMPLETED**
         DC    C'BLG1M410'       **PURGE VERIFICATION**
         DC    XL8'00'             LAST ENTRY IN TABLE
WRKAREA  DSECT
         ORG   *+X'120'
IOPL     DS    4F
GTPB     DS    2F
ECB      DS    F
WKLEN    EQU   *-WRKAREA
         END
