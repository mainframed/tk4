.he ABE, T. Smith, Appendix A

Appendix A, PGM Subcommand                                             @









                       A B E  E d i t o r


                         T .  S m i t h

                       A P P E N D I X  A

                    P G M  S U B C O M M A N D

This appendix describes programs written for use with the PGM
subcommand of ABE. Such programs, when invoked via the PGM subcommand,
have considerable power in what they can do.  Following is a list of
some of the things that can be done:

    a.  All records in all currently edited data sets are accessible
        to the invoked program.  They can be changed or deleted, and
        new records can be created and added.

    b.  Other ABE subcommands can be created and stacked for later
        execution.

    c.  Panel displays (see $display and $store under the RUN
        subcommand) can be accessed.  The program can assign data
        to a panel for subsequent display, and it also has
        access to any data entered on a display from the terminal.

This appendix will describe how to write in PLI a program to be
invoked by PGM.  Following is a complete sample program:

    1  % INCLUDE ABESUB;
    2  ABESUB(SAMPLE);
    3  % INCLUDE ABESUBA;
    4  MSGDATA='MESSAGE FROM SAMPLE PROGRAM';
    5  RHRC=23;
    6  END;

The above sample program, when invoked via PGM, will cause the
message "MESSAGE FROM SAMPLE PROGRAM" to be printed in the message
area of the standard ABE display, and will set the return code
to 23.  Symbolic variable &LASTCC will be set to this value,
and can be interrogated in a RUN, for example (see also the help
documentation on SSE regarding symbolic variables).

Lines 1 thru 3 of the above sample program should be included at the
start of any program to be invoked via PGM.  The name in parentheses
in line 2 can be any name you want except 'MAIN'.  To compile and link
the program, the following PLI and LINK commands should be used:

    PLI SAMPLE M LIB('SYS2.MACLIB' 'x75826.abe.pli') OBJ CS(60)
    LINK (SAMPLE *) LOAD(TEST(SAMPLE)) LIB('SYS2.PLIBASE') PLIB PR(*)

When the linkage-editor prompts you for control statements, enter
the following line:

     ENTRY SAMPLE

The above ENTRY statement must begin with a blank.  The name 'SAMPLE'
should match the name assigned to @PROCNAME in the source program.


GENERAL FUNCTIONS AND VARIABLES YOU MAY REFERENCE                      @

This subsection gives specific documentation on functions and
variables you may use in your PGM load module.  You may want to
initially skip over this section, and continue on with the section
titled "SOME SAMPLE APPLICATIONS", referring back to this section for
detailed information where needed.

     FUNCTIONS

 PROMPT ENTRY(CHAR(80) VAR) RETURNS(CHAR(240) VAR);
     Will prompt the user at the terminal by sending the message
     you specify as the sole argument, and will return the response
     entered by the user at the terminal.
 PUTSLOT ENTRY(PTR,ptr,PTR)
  CALL #PUTSLOT(RHPTR,RHHDRPT,MYPTR);
     Deletes the record pointed to by the 3rd argument.  The first
     two arguments refer to variables internal to SSE.  They are
     declared automatically for you by including ABESUB (see above).
 CMD ENTRY(CHAR(*) VAR,PTR,PTR) RETURNS(FIXED BIN(15,0));
  I=#CMD('0 BL 1',RHPTR,RHHDRPT);
     Issues any abe subcommand.  In the sample statement above, the
     '0 bl 1' invokes the abe BLANK subcommand to insert a blank
     line at the top of the data set.  The other arguments refer
     to standard abe variables internal to abe declared for you by
     including ABESUB.
 PRED ENTRY(PTR,PTR,PTR) RETURNS(PTR);
  I=#PRED(RHHDRPT,RHPTR,MYPTR);
     Given a pointer to a record in the currently edited data set
     (MYPTR), it will return a pointer to the preceding record in
     the data set, or RHPTR if the record pointed to by the 3rd
     argument (MYPTR) is the first record in the data set.
 SUCC ENTRY(PTR,PTR,PTR) RETURNS(PTR);
  I=#SUCC(RHHDRPT,RHPTR,MYPTR);
     Given a pointer to a record in the currently edited data set
     (MYPTR), it will return a pointer to the succeeding record in
     the data set, or RHPTR if the record pointed to by the 3rd
     argument (MYPTR) is the last record in the data set.
 SYMC ENTRY(CHAR(*)) RETURNS(CHAR(256) VAR);
  STR=#SYMC('&PFK');
     Given a string containing a symbolic variable name as an
     argument, it will return the replacement value for that
     symbolic variable.
 SET ENTRY(BIT(1),CHAR(8),CHAR(256) VAR);
  CALL #SET('0'B,'&XYZ','HOW NOW BROWN COW');
     Sets the replacement value for the specified variable.  In the
     sample above, symbolic variable &XYZ as assigned the replacement
     value 'HOW NOW BROWN COW'.  Always specify '0'b as the first
     argument.
 REP ENTRY;
  DCL 1 REPBUF,
    2 BUFLEN FIXED BIN(15,0),
    2 OFFSET FIXED BIN(15,0),
    2 BF CHAR(250);
  BF='PFK=&PFK UID=&SYSUID';
  OFFSET=0; /* ALWAYS 0 */
  BUFLEN=LENGTH('PFK=&PFK UID=&SYSUID')+4;
  CALL #REP(REPBUF);
     Called to perform symbolic substitution on a character string.
     On return from #REP, bf will contain the character data with
     symbolic substitution having been done, and buflen will be
     adjusted if the substitution changed the length of the
     character data.


VARIABLES YOU MAY REFERENCE                                            @

NOTE: In declaring your own variables, don't begin their names
 ith the characters "RH".  Any new variables added to the ABE
 ontrol blocks will preceded with "RH".

PARY() CHAR(256) VAR
  The first element in this array will contain the first operand
  after the program name specified on the PGM subcommand, PARY(2)
  the second operand, etc.
REC
  This is a structure which contains, among other things, the data
  from one record in the currently edited data set.  See ABEREC
  in X75826.ABE.PLI for a complete description of this structure.
  It contains two pointer fields which put it on a doubly linked list
  containing all of the records for the currently edited data set.
  You should not reference these linked-list pointer fields to
  sequentially process records in the data set.  Instead, you
  should use the #PRED and #SUCC entry points described previously.
  The reason for this is that, in the future, this doubly linked
  list structure may be altered so that the next sequential record
  may not be in memory.
RECHDR
  This based structure is the main control block for a given data
  set currently being edited.  Among other things, it contains the
  head and tail pointers for the doubly-linked list of records
  for the data set.  When your PGM program receives control,
  the pointer named RHPTR will point to the RECHDR structure for the
  currently edited data set.  See ABEREC in X75826.ABE.PLI.
RHHDR
  This based structure is the main control block for the editor.
  It performs a function analogous to the CVT for the operating
  system.  When your PGM program receives control, the pointer
  named RHHDRPT will point to the RHHDR structure.  See ABERHHDR
  in X75826.ABE.PLI.
RHRC
  You should set this to a return code indicating the success or
  failure of your program.  RHRC=0 means successful execution, and
  RHRC>=8 means failure.

SOME SAMPLE APPLICATIONS                                               @

What follows is a series of sample programs designed to do functions
you might commonly want to do.  All of these programs have been
successfully executed.

***CAUTION*** As you become familiar with the internals of ABE, you
will find variables and structures other than the ones shown in this
Appendix which contain information you might want access to.  DO
SO AT YOUR OWN RISK.  Development of ABE is far from over.  In fact,
one of the distinct advantages of this editor is that it is easily
modified to respond to new requirements.  ANY STRUCTURES AND VARIABLES
NOT MENTIONED IN THIS APPENDIX ARE SUBJECT TO CHANGE WITHOUT NOTICE.


EXECUTE ANY OTHER ABE SUBCOMMAND WITH RETURN OF CONTROL                @

  1  % INCLUDE ABESUB;
  2  ABESUB(SAMPLE);
  3  % INCLUDE ABESUBA;
  4  DCL MYCMD CHAR(256) VAR;
  5  MYCMD='COPY 1 2 3'; /* COULD BE ANY SSE SUBCOMMAND */
  6  I=#CMD(MYCMD, /* could also be a literal */
  7    RHPTR,RHHDRPT); /* variables defined in ABESUB */
  8  PUT STRING(MSGDATA) LIST('RC=',I,'CMD=',MYCMD);
  9  RHRC=0;
 10  END;

Upon return from #CMD, variable I will be set to the return code from
the invoked command.  Line 7 loads the message area of the standard
ABE full screen display.

STACKING ANY OTHER ABE SUBCOMMANDS FOR EXECUTION                       @

  1  % INCLUDE ABESUB;
  2  ABESUB(SAMPLE);
  3  % INCLUDE ABESUBA;
  4  CMDDATA='COPY 1 2 3'; /* COULD BE ANY SSE SUBCOMMAND */
  5  RHRC=0;
  6  END;

Anything assigned to variable CMDDATA will be executed after your
program has returned control to ABE.


SETTING ALL RECORDS IN THE CURRENTLY EDITED DATA SET TO ASTERISKS      @

  1  % INCLUDE ABESUB;
  2  ABESUB(SAMPLE);
  3  % INCLUDE ABESUBA;
   4 DCL MYPTR PTR;
   5 MYPTR=NULL;
   6 DO MYPTR=#SUCC(RHHDRPT,RHPTR,MYPTR)
   7   REPEAT #SUCC(RHHDRPT,RHPTR,MYPTR)
   8   WHILE(MYPTR^=HDPTR);
   9   MYPTR->RECSTR=REPEAT('*',myptr->reclen-1);
  10   END;
  11 RHRC=0;
  12 END;

#SUCC returns a pointer to the next sequence record after the one
pointed to by the third argument (in this casy MYPTR).  If the third
argument is NULL, it will return the pointer to the first record in
the data set.


FINDING AND DELETING LAST RECORD WITH THE STRING 'XYZ'                 @

   1  % INCLUDE ABESUB;
   2  ABESUB(SAMPLE);
   3  % INCLUDE ABESUBA;
   4 DCL MYPTR PTR;
   5 MYPTR=NULL;
   6 DO MYPTR=#PRED(RHHDRPT,RHPTR,MYPTR)
   7   REPEAT #PRED(RHHDRPT,RHPTR,MYPTR)
   8   WHILE(MYPTR^=HDPTR)
   9   UNTIL(INDEX(MYPTR->RECSTR,'XYZ')^=0);
  10   END;
  11 IF MYPTR^=HDPTR
  12   THEN CALL #PUTSLOT(RHPTR,RHHDRPT,MYPTR);
  13     /* #PUTSLOT DELETES RECORD POINTED TO BY 3RD ARGUMENT */
  14 RHRC=0;
  15 END;

#PRED returns a pointer to the prior sequence record after the one
pointed to by the third argement (in this casy MYPTR).  If the third
argument is NULL, it will return the pointer to the last record in the
data set.


ADD A NEW RECORD AFTER THE CURRENT ONE                                 @

   1  % INCLUDE ABESUB;
   2  ABESUB(SAMPLE);
   3  % INCLUDE ABESUBA;
   4 DCL MYPTR PTR;
   5 DCL MYCMD CHAR(80) VAR;
   6 MYPTR=RECPTR; /* RECPTR IS THE CURRENT RECORD POINTER */
   7 IF MYPTR=HDPTR
   8   THEN MYCMD='0 BL 1';
   9     /* IF RECPTR=HDPTR, CURRENT RECORD POINTER IS SET TO POINT
  10     PRIOR TO THE FIRST RECORD IN THE FILE. */
  11   ELSE PUT STRING(MYCMD) EDIT(MYPTR->RECSEQ,' bl 1')
  12       (P'ZZZZZZZ9',A);
  13      /* RECSEQ CONTAINS SEQUENCE NUMBER ASSOCIATED WITH RECORD */
  14 I=#CMD(MYCMD,RHPTR,RHHDRPT);
  15 IF I^=0
  16   THEN RHRC=I; /* SOMETHING BAD HAS HAPPENED */
  17   ELSE DO;
  18     MYPTR=MYPTR->NXTPTR;
  19       /* MYPTR NOW POINTS TO BLANK RECORD */
  20     MYPTR->RECSTR='TEXT TO BE STORED IN ADDED RECORD';
  21     RHRC=0;
  22     END;
  23 END;


LOCATE A SECOND CURRENTLY EDITED DATA SET                              @
WHICH HAS SYMBOLIC NAME 'SECOND' ASSOCIATED WITH IT.  FOR EACH LINE IN
'SECOND', ISSUE A DELETE SUBCOMMAND FOR THE LINE IN THE FIRST DATA SET
WITH A MATCHING SEQUENCE NUMBER.

   1  % INCLUDE ABESUB;
   2  ABESUB(SAMPLE);
   3  % INCLUDE ABESUBA;
   4 DCL (MYPTR,TRH) PTR;
   5 DCL MYCMD CHAR(80) VAR;
   6 DO TRH=NXTRHEL REPEAT
   7   REPEAT TRH->RHNXT
   8   WHILE(TRH^=NULL)
   9   UNTIL(TRH->RHNAME='SECOND');
  10   END;
  11 IF TRH=NULL
  12   THEN DO;
  13     MSGDATA='SECOND DATA SET NOT FOUND';
  14     RHRC=16;
  15     END;
  16   ELSE DO;
  17     MYPTR=NULL;
  18     DO MYPTR=#SUCC(RHHDRPT,TRH,MYPTR)
  19       REPEAT #SUCC(RHHDRPT,TRH,MYPTR)
  20       WHILE(MYPTR^=TRH->HDPTR);
  21       PUT STRING(MYCMD) EDIT(MYPTR->RECSEQ,' del')
  22         (P'ZZZZZZZ9',a);
  23       I=#CMD(MYCMD,RHPTR,SUBENT,RHHDRPT);
  24       END;
  25     RHRC=0;
  26     END;
  27 END;


HOW TO RECEIVE AND PROCESS OPERANDS.                                   @

SYNTAX FOR THE COMMAND WOULD BE -

    PGM TEST.LOAD(SAMPLE) <L1> <L2> LO HI ROTATE(<CNT>)

<L1> AND <L1> specify the range of line numbers to which the
command is to be applied.  The keywords LO and HI
indicate whether the specified lines are to be
translated to lower or upper case ('a' to 'z' to 'A' to 'Z')
or vice versa.  The parenthesized number associated with the
ROTATE keyword tells how many positions the nonblank data in each
line is to be rotated (e.g. ROTATE(1) for 'xyz' would produce 'zxy').

      1  % INCLUDE ABESUB;
      2  ABESUB(SAMPLE) pars(11000);
      3  % INCLUDE ABESUBA;
      4 DCL KEYTABLE(3) CHAR(80) VAR STATIC INIT('LO','HI','ROTATE');
      5 DCL INDX FIXED BIN(15,0);
      6 DCL OPER CHAR(80) VAR;
      7 DCL ROTATE FIXED BIN(15,0) INIT(0);
      8 DCL (LOSW,HISW) BIT(1) INIT('0'B);
      9 DCL SETCAPS ENTRY;
     10 DCL MYCMD CHAR(256) VAR;
     11 DCL MYPTR PTR;
     12 IF NXTHDR=HDPTR
     13   THEN RETURN; /* NO RECORDS TO PROCESS */
     14 L1=PARY(1); /* L1=LINE # TO START WITH */
     15 L2=PARY(2); /* L2=LINE # TO STOP WITH */
     16 DO I=3 TO hbound(pary,1);
     17   IF PARY(I)^=''
     18     THEN DO;
     19       CALL SETCAPS(PARY(I));
     20       CALL #SPARS(PARY(I),KEYTABLE,OPER,INDX);
     21       SELECT(INDX);
     22         WHEN(0) DO;
     23           MSGDATA='INVALID OPERAND='××PARY(I);
     24           RHRC=16;
     25           RETURN;
     26           END;
     27         WHEN(1) LOSW='1'B; /* LO KEYWORD */
     28         WHEN(2) HISW='1'B; /* HI KEYWORD */
     29         WHEN(3) ROTATE=OPER; /* ROTATE KEYWORD */
     30         OTHERWISE;
     31         END;
     32       END;
     33   END;
     34 PUT STRING(MYCMD) EDIT('MD ',L1)(A,P'ZZZZZZZ9');
     35 I=#CMD(MYCMD,RHPTR,SUBENT,RHHDRPT);
     36 DO MYPTR=RECPTR REPEAT #SUCC(RHHDRPT,RHPTR,MYPTR)
     37   WHILE(MYPTR^=HDPTR);
     38   IF MYPTR->RECSEQ>L2
     39     THEN LEAVE;
     40   IF MYPTR->RECSTR^=''
     41     THEN DO;
     42       IF LOSW
     43         THEN MYPTR->RECSTR=TRANSLATE(MYPTR->RECSTR,
     44           'abcdefghijklmnopqrstuvwxyz',
     45           'ABCDEFGHIJKLMNOPQRSTUVWXYZ');
     46       IF HISW
     47         THEN MYPTR->RECSTR=TRANSLATE(MYPTR->RECSTR,
     48           'ABCDEFGHIJKLMNOPQRSTUVWXYZ',
     49           'abcdefghijklmnopqrstuvwxyz');
     50       IF ROTATE^=0
     51         THEN DO;
     52           L=LENGTH(MYPTR->RECSTR);
     53           IF L<=ROTATE
     54             THEN R=MOD(ROTATE,L);
     55             ELSE R=ROTATE;
     56           MYPTR->RECSTR=SUBSTR(MYPTR->RECSTR,L-R+1,R)××
     57             SUBSTR(MYPTR->RECSTR,1,L-R);
     58           END;
     59       END;
     60   END;
     61 END SAMPLE;

PROGRAM USING THE FULL SCREEN OPTIONS OF ABE                           @
TO DO SIMPLE MAINTENANCE ON A PERSONNEL FILE

Using the PDF keyword of SSE, a panel named 'PERSONAL'
was defined that looks like the following:

    @SAMPLE EMPLOYEE FILE MAINTENANCE DISPLAY

    @MESSAGE AREA:@

    @DELETE CODE (Y=YES):&X@

     EMPLOYEE NUMBER (SEARCH KEY):&XXXXXXXX@

     EMPLOYEE NAME:  &XXXXXXXXXXXXXXXXXXXXXXXXXXXX@

     EMPLOYEE ADDR:  &XXXXXXXXXXXXXXXXXXXXXXXXXXXX@

     DATE OF BIRTH:  &xxxxxx@


The sample program below can be used to find and change or delete
records having a specified employee number, or to add entirely
new records.  The file will be maintained in employee number sequence.
numeric fields will be verified, and the user will be prompted to
correct invalid data.  If the user hits enter without changing any
field on the display, the next record in the file will be displayed.


   1  % INCLUDE ABESUB;
   2  ABESUB(SAMPLE);
   3  % INCLUDE ABESUBA;
   4  DCL NEW CHAR(80) INIT(''),
   5   1 NEWDF BASED(NEWPTR),
   6    2 DELCH CHAR(1),
   7    2 EMPNO CHAR(8),
   8    2 NAME CHAR(28),
   9    2 ADRS CHAR(28),
  10    2 DTE CHAR(6);
  11 DCL PFK CHAR(5);
  12 NEWPTR=ADDR(NEW);
  13 DO UNTIL(PFK^='00000'); /* DO UNTIL USER HITS ANY PFK */
  14   CALL DISPLAY;
  15   IF BAD('00007',EMPNO) × BAD('00013',DTE)
  16     THEN CALL REDISPLAY;
  17   IF NEW^=''
  18     THEN DO; /* USER CHANGED A FIELD */
  19       CALL MOVE_DISPLAY;
  20       CALL MOVE_DATA;
  21       CALL DELETE_EMPLOYEE;
  22       END;
  23   END;
  24 DISPLAY: PROC; /* DISPLAY CURRENT RECORD AND SCROLL */
  25   DO UNTIL(NEW^='' × PFK^='00000');
  26     IF RECPTR^=HDPTR /* IS THERE A CURRENT RECORD? */
  27       THEN I=#CMD('$DISPLAY PERSONAL NOBREAK',
  28         RHPTR,RHHDRPT);
  29       ELSE I=#CMD('$FULLSCR PERSONAL',RHPTR,RHHDRPT);
  30     I=#CMD('$CLEAR PERSONAL',RHPTR,RHHDRPT);
  31     PFK=#SYMC('&PFK');
  32     DELCH=#SYMC('&F00005');
  33     CALL SETCAPS(DELCH);
  34     EMPNO=#SYMC('&F00007');
  35     NAME=#SYMC('&F00009');
  36     ADRS=#SYMC('&F00011');
  37     DTE=#SYMC('&F00013');
  38     IF NEW='' /* DID USER CHANGE A FIELD? */
  39       THEN I=#CMD('DOWN 1',RHPTR,RHHDRPT);
  30     END;
  41   IF EMPNO='' & RECPTR^=HDPTR
  42     THEN EMPNO=SUBSTR(RECSTR,2,8);
  43   END DISPLAY;
  44 BAD: PROC(FLDNO,VAL) RETURNS(BIT(1)); /* VERIFY NUMERICS */
  45   DCL FLDNO CHAR(5);
  46   DCL VAL CHAR(*);
  47   IF VERIFY(VAL,' 0123456789')=0
  48     THEN RETURN('0'B);
  49   I=#CMD('$SETO PERSONAL '××FLDNO××' '''××VAL××'''',
  50     RHPTR,RHHDRPT);
  51   RETURN('1'B);
  52   END BAD;
  53 REDISPLAY: PROC; /* REDISPLAY FIELDS WITH INVALID DATA */
  54   DCL VAL CHAR(28) VAR;
  55   I=#CMD('$SETO PERSONAL 3 ''DATA SHOULD BE NUMERIC''',
  56     RHPTR,RHHDRPT);
  57   I=#CMD('$FULLSCR PERSONAL',RHPTR,RHHDRPT);
  58   I=#CMD('$CLEAR PERSONAL',RHPTR,RHHDRPT);
  59   VAL=#SYMC('&F00007');
  50   IF VAL^=''
  51     THEN EMPNO=VAL;
  52   VAL=#SYMC('&F00009');
  53   IF VAL^=''
  54     THEN NAME=VAL;
  55   VAL=#SYMC('&F00011');
  56   IF VAL^=''
  57     THEN ADRS=VAL;
  58   VAL=#SYMC('&F00013');
  59   IF VAL^=''
  60     THEN DTE=VAL;
  61   END REDISPLAY;
  62 MOVE_DISPLAY: PROC; /* CHANGE CURRENT RECORD POINTER */
  63   DCL MYPTR PTR;
  64   DCL MYCMD CHAR(80) VAR;
  65   IF RECPTR^=HDPTR
  66     THEN IF SUBSTR(RECSTR,2,8)=EMPNO
  67       THEN RETURN;
  68   I=#CMD('FIND '''××EMPNO××''' STC(2) OPER(>=)',
  69     RHPTR,RHHDRPT);
  70   IF RHRC^=0 /* WILL BE NONZERO IF EMPNO NOT FOUND */
  71     THEN MYPTR=#PRED(RHHDRPT,RHPTR,NULL);
  72     ELSE IF SUBSTR(RECSTR,2,8)=EMPNO
  73       THEN RETURN;
  74       ELSE MYPTR=#PRED(RHHDRPT,RHPTR,MYPTR);
  75   IF MYPTR=HDPTR
  76     THEN DO; /* NEW RECORD GOES AT TOP */
  77       I=#CMD('0 BL 1',RHPTR,RHHDRPT);
  78       I=#CMD('TOP',RHPTR,RHHDRPT);
  79       END;
  80     ELSE DO;
  81       PUT STRING(MYCMD) EDIT(MYPTR->RECSEQ,'bl 1')
  82         (P'ZZZZZZZ9',A);
  83       I=#CMD(MYCMD,RHPTR,RHHDRPT);
  84       PUT STRING(MYCMD) EDIT('V ',MYPTR->RECSEQ+1)
  85         (A,P'ZZZZZZZ9');
  86       I=#CMD(MYCMD,RHPTR,RHHDRPT);
  87       END;
  88   END MOVE_DISPLAY;
  89 MOVE_DATA: PROC; /* UPDATE RECORD WITH NEW DATA */
  90   DCL NEWREC CHAR(80);
  91   DCL RP PTR;
  92   RP=ADDR(NEWREC);
  93   NEWREC=RECSTR;
  94   IF EMPNO^=''
  95     THEN RP->EMPNO=EMPNO;
  96   IF NAME^=''
  97     THEN RP->NAME=NAME;
  98   IF ADRS^=''
  99     THEN RP->ADRS=ADRS;
 100   IF DTE^=''
 101     THEN RP->DTE=DTE;
 102   RECSTR=NEWREC;
 103   END MOVE_DATA;
 104 DELETE_EMPLOYEE: PROC; /* DELETE EMPNO RECORD */
 105   DCL MYCMD CHAR(80) VAR;
 106   IF DELCH^='Y'
 107     THEN RETURN;
 108   PUT STRING(MYCMD) EDIT(RECSEQ,' del')(P'ZZZZZZZ9',a);
 109   I=#CMD(MYCMD,RHPTR,RHHDRPT);
 100   END DELETE_EMPLOYEE;
 111   END SAMPLE;


SAMPLE PROGRAM TO EDIT A VSAM KSDS FILE.                               @

As you may know, PL/I places restrictions on file processing in
fetched procedures (see PL/I Language Reference on FETCH).  One
if these restrictions is that any files processed must be passed
to the fetched procedure as parameters.  A number of file variables are
passed as parameters to any program invoked via the PGM subcommand:

   RH_VSAM_1 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAM1 FILE ENV(VSAM) */
   RH_VSAM_2 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAM2 FILE ENV(VSAM) */
   RH_VSAM_3 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAM3 FILE ENV(VSAM) */
   RH_VSAM_GENKEY_1 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK1 FILE ENV(VSAM GENKEY) */
   RH_VSAM_GENKEY_2 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK2 FILE ENV(VSAM GENKEY) */
   RH_VSAM_GENKEY_3 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK3 FILE ENV(VSAM GENKEY) */
   RH_VSAM_GENKEY_4 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK4 FILE ENV(VSAM GENKEY) */
   RH_VSAM_GENKEY_5 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK5 FILE ENV(VSAM GENKEY) */
   RH_VSAM_GENKEY_6 FILE,
   /* ASSIGNED FILE CONSTANT DCL VSAMGK6 FILE ENV(VSAM GENKEY) */
   RH_REGIONAL_ONE_1 FILE,
   /* ASSIGNED FILE CONSTANT DCL REGONE1 FILE ENV(REGIONAL(1)) */
   RH_ISAM_1 FILE,
   /* ASSIGNED FILE CONSTANT DCL ISAM1 FILE ENV(INDEXED) */
   RH_SEQ_1 FILE,
   /* ASSIGNED FILE CONSTANT DCL SEQ1 FILE ENV(CONSECUTIVE) */
   RH_SEQ_2 FILE,
   /* ASSIGNED FILE CONSTANT DCL SEQ2 FILE ENV(CONSECUTIVE) */
   RH_SEQ_3 FILE,
   /* ASSIGNED FILE CONSTANT DCL SEQ3 FILE ENV(CONSECUTIVE) */


Following is a sample program to perform file maintenance on a
VSAM ksds file.  It uses one of the file_variables above, namely
RH_VSAM_GENKEY_1, to support the display, addition, changing, and
deleting of records from the VSAM file:

  /* VSAMTEST */
  % INCLUDE ABESUB;
  ABESUB(vsamtst);
  % INCLUDE ABESUBA;
  DCL INREC CHAR(80);
  % DCL VSAMFLE CHAR;
  % VSAMFLE='RH_VSAM_GENKEY_1';
  DCL MSG CHAR(80) VAR INIT('');
  DCL MYKEY CHAR(9) VAR INIT('');
  DCL SUFFIX ENTRY(CHAR(*), CHAR(*))
    RETURNS(FIXED BIN(15,0));
  DCL PFK CHAR(5);
  DCL OPENSW BIT(1) INIT('1'B);
  I=#CMD('0 BL 1',RHPTR,RHHDRPT);
  I=#CMD('TOP',RHPTR,RHHDRPT);
  ON KEY(VSAMFLE) BEGIN;
    MSG='KEY NOT FOUND';
    MYKEY='++++++'; /* DISPLAY EXISTENT RECORD */
    INREC=SUBSTR(INREC,1,MIN(LENGTH(INREC),9));
    END;
  ON ENDFILE(VSAMFLE) BEGIN;
    MSG='ENDFILE CONDITION RAISED';
    CLOSE FILE(VSAMFLE);
    OPENSW='1'B;
    END;
  DO FOREVER=1 REPEAT FOREVER;
    IF OPENSW
      THEN DO;
        OPEN FILE(VSAMFLE) UPDATE KEYED TITLE('VSAMTEST');
        OPENSW='0'B;
        END;
    SELECT(MYKEY);
      WHEN('','@@@@@@')
        READ FILE(VSAMFLE) INTO(INREC);
      WHEN('------')
        INREC='';
      WHEN('++++++');
      OTHERWISE
        READ FILE(VSAMFLE) INTO(INREC) KEY(MYKEY);
      END;
    MYKEY=SUBSTR(INREC,1,SUFFIX(SUBSTR(INREC,1,9),' '));
    RECSTR=INREC;
    I=#CMD('$SETO VSAMTEST 1 '''××MSG××'''',RHPTR,RHHDRPT);
    MSG='';
    I=#CMD('$DISPLAY VSAMTEST NOBREAK LINE(1)',RHPTR,RHHDRPT);
    I=#CMD('$STORE VSAMTEST NOBREAK MASK LINE(1)',
      RHPTR,RHHDRPT);
    INREC=RECSTR;
    MYKEY=SUBSTR(INREC,1,SUFFIX(SUBSTR(INREC,1,9),' '));
    PFK=#SYMC('&PFK');
    SELECT(PFK);
      WHEN('00000') /* HE HIT ENTER KEY */
        MYKEY='++++++'; /* LEAVE INREC AS IS */
      WHEN('00001') DO; /* ADD */
        WRITE FILE(VSAMFLE) FROM(INREC) KEYFROM(MYKEY);
        MSG='ADDED';
        MYKEY='++++++'; /* LEAVE INREC AS IS */
        END;
      WHEN('00002') DO; /* DELETE */
        DELETE FILE(VSAMFLE) KEY(MYKEY);
        MSG='DELETED';
        MYKEY='------'; /* SET INREC TO BLANKS */
        END;
      WHEN('00003') DO; /* UPDATE */
        REWRITE FILE(VSAMFLE) FROM(INREC) KEY(MYKEY);
        MSG='CHANGED';
        MYKEY='++++++'; /* LEAVE INREC AS IS */
        END;
      WHEN('00004') /* NEXT RECORD */
        MYKEY='@@@@@@'; /* GET NEXT SEQUENTIAL RECORD */
      WHEN('00005') /* LOCATE */
        INREC='######'; /* GET RECORD BY KEY */
      WHEN('00006') DO; /* TOP */
        CLOSE FILE(VSAMFLE);
        OPENSW = '1'B;
        MYKEY='@@@@@@'; /* GET NEXT SEQUENTIAL RECORD */
        END;
      WHEN('00007') DO; /* EOJ */
        CLOSE FILE(VSAMFLE);
        RHRC=0;
        CMDDATA='END N';
        RETURN;
        END;
      WHEN('00008') DO; /* HALT */
        HALT;
        I=23; /* TO GIVE OPTIMIZER ONE EXECUTABLE STMT IN DO-GROUP */
        END;
      OTHERWISE; /* TO SATISFY COMPILER */
      END; /* SELECT */
    END; /* FOREVER LOOP */
  END VSAMTST;

FILE ACCESS MODULES                                                    @

The FAM keyword on the ABE command allows you to specify the name of
a module and a string-of-anything to be passed to that module, e.g.
"abe xyz fam(mymod read only etc)."  In this example, the module
name is MYMOD, and string-of-anything is "read only etc.".

FAM modules receive control with a standard parameter list as follows:

     PTR=RHFAMEP(RHHDRPT,RHPTR,PARMPTR,CODE);

 here -

RHHDRPT is the equivalent to the CVT in ABE, and is documented
  elsewhere in this appendix
RHPTR points to the control block representing this data set, and is
  also documented elsewhere in this appendix.
PARMPTR is a pointer whose contents depend on the value in CODE.
  When CODE=0, PARMPTR points at a structure documented in the
  member ABEFAM in X75826.ABE.PLI.
PTR is a pointer variable that should be set by the FAM module.
  according to the value in CODE.
CODE=0 indicates initial entry.  PARMPTR points at a structure
  containing startup values as documented in the source program for
  member ABEFAM.  The FAM module can return any pointer value it
  chooses, because it will be ignored.
CODE=1 indicates the FAM module is to return the next successive
  record in the data set.  PARMPTR should contain the pointer value
  returned on the most recent prior invocation of the FAM module.
  If PARMPTR=RHPTR the first record in the data set is to be
  returned.  When there are no more records, the FAM module should
  return RHPTR.
CODE=2 indicates the FAM module is to return the record preceding
  the one most recently accessed in the data set.  PARMPTR should
  contain the pointer value returned on the most recent prior
  invocation of the FAM module.  If PARMPTR=RHPTR the last record
  in the data set is to be returned.  When there are no more
  preceding records, the FAM module should return RHPTR.
CODE=3 indicates SAVE.  Any pointer value returned is ignored, and
  PARMPTR will be null.
CODE=4 indicates END.
CODE=5 indicates it is time to display the data set at the terminal
  and obtain any user commands.
CODE=6 indicates the REFRESH subcommand has been entered for this
  data set.

