         TITLE     'IEFUTL - MVS'
IEFUTL   CSECT
***********************************************************************
*
*        PROGRAM: IEFUTL.
*        INSTALLATION: U. S. SENATE COMPUTER CENTER.
*        AUTHOR: D. HALBIG.
*        DATE WRITTEN: AUGUST 1979.
*        FUNCTION: PERMIT EXTENSI ONS OF CPU (TCB) TIME USED BY A
*              BATCH JOB.  EXTENSIONS ARE GRANTED ONLY TO NON-DEVEL-
*              OPMENT JOBS; DEVELOPMENT JOBS (USU DESIGNATED SUCH BY
*              RUN TYPE CODE 'T') ARE PERMITTED NO EXTENSION -
*              (322 ABEND)
*              THESE CONDITIONAL EXTENSIONS APPLY ONLY TO JOB & STEP
*              LEVEL CPU TIME; WAIT TIME LIMIT EXTENSIONS ARE
*              ALWAYS GRANTED.
*              REGARDLESS OF THE TYPE OF EXTENSION, A WARNING
*              MESSAGE IS ISSUED BEFORE THE EXTENSION IS GRANTED.
*
*              09/10/80 - SILENT EXTENSION FOR PDS UPDATE UTILITES
*              CERTAIN UTILITES (IEBUPDTE, IEBCOPY, LINKAGE EDITOR)
*              CAN MODIFY PDS'S - IF THEY ABEND WHILE UPDATING
*              A PDS DIRECTORY, THE DIRECTORY MAY VERY LIKELY BE
*              DESTROYED.  THIS MODIFICATION ENSURES SUCH UTILITES
*              ARE PERMITTED TO COMPLETE NORMALLY BEFORE ABEND 322
*              IS SIGNALLED.
*              10/29/80 - UTL GETMAIN - SINCE IEFUTL MAY GAIN
*              CONTROL ASYNCHRONOUSLY FROM OTHER SMF EXITS (IEFU83,
*              IEFUSO) IT CANNOT USE THE IEFUJI-GETMAINED AREA FOR
*              SAVEAREA CHAINING WITHOUT RUNNING THE RISK OF S/A
*              CHAIN CONFUSION. THIS MODIFICATION ENSURES IEFUTL
*              DOES NOT MESS WITH THE IEFUJI-GETMAINED AREA.
*
*              11/23/81(DGH) - INSTALL BYPASS CODE; FEATURE PERMITS
*                ALL SMF EXITS TO BE BYPASSED IF BYPASS FLAG IS SET.
*                BYPASS FLAG IS SET BY SMF EXIT IEFUJV (SEE EXIT
*                IEFUJV FOR FURTHER DETAILS).
*        REGISTER USAGE:
*              R15                ENTRY PT/RETURN CODE
*              R14                RETURN ADDRESS
*              R13                SAVEAREA / G/M AREA
*              R12                BASE REGISTER FOR EXIT
*              R11                COMMON EXIT PARAMETER AREA
*              R10                G/M AREA FROM IEFUJI (EXITCORE)
*              R9-R0              WORK REGS
*
***********************************************************************
         REGEQU
         SAVE      (14,12),,IEFUTL-&SYSDATE-&SYSTIME
         LR        R12,R15        ESTAB. BASE REG.
         USING     IEFUTL,R12     TELL ASSEMBLER
         L         R11,0(,R1)     HOLD ONTO PARM PTR
         LA        R11,0(,R11)    CLEAR HI-BYTE
         USING     COMMEXIT,R11
         SR        R15,R15      SET RC=0
         TM        CEPIDFLG,CEPIDF05  IS BYPASS FLAG SET?
         BO        UTLFEXIT      BIF YES
         LR        R9,R0          HOLD ONTO ENTRY CODE
         LR        R8,R13         HOLD ONTO CALLER'S S/A PTR FOR A SEC
         L         R10,CEPUSER     GET POINTER TO IEFUJI-GETMAINED AREA
         LA        R10,0(,R10)     CLEAR HI-BYTE
         TM        CEPRSTOP,CEPRST04 WARM START?
         BZ        UTLUJIOK       BIF UJI-G/M AREA OK
         SLR       R10,R10        MAKE SURE WE DON'T USE IT IF NOT
UTLUJIOK EQU       *
         USING     EXITCORE,R10
         LA        R0,UTLPOOL     GET IEFUTL GETMAIN SUBPOOL #
         SLL       R0,24          SHIFT TO HI-BYTE
         LA        R1,UTLSIZE
         OR        R0,R1          COMBINE THEM
         GETMAIN   R,LV=(0)
         LR        R13,R1
         ST        R13,8(,R8)     COMPLETE CHAINING
         ST        R8,4(,R13)
         USING     UTLCORE,R13     MAKE AREA ADDRESSABLE
         SR        R15,R15        SET RC=0 INITIALLY
         MVI       WARNMSG,X'00'  ZERO OUT FORMATTING AREA
         MVC       WARNMSG+1(WARNLGTH),WARNMSG
         LA        R1,74          LENGTH OF TEXT
         STH       R1,WML1LGTH
         STH       R1,WML2LGTH
         MVC       WMMCSFLG,=X'8040'
         MVC       WMDESCOD,=X'0400'
         MVC       WMROUTCD,=X'8000' ROUTCDE=1
         LA        R1,2           # OF LINES
         STC       R1,WM#LINES
         MVC       WML1TYPE,=X'2000'
         MVC       WML2TYPE,=X'3000'
         B         UTLWTG(R9)     USE ENTRY CODE FOR WTG TABLE
UTLWTG   EQU       *
         B         UTLJOB         EC=0: JOB CPU TIME LIMIT EXPIRED
         B         UTLSTEP        EC=4: STEP CPU TIME LIMIT EXPIRED
         B         UTLWAIT        EC=8: CONT. WAIT LIMIT EXPIRED
         SPACE     1
UTLJOB   EQU       *
***********************************************************************
*        JOB-LEVEL CPU TIME LIMIT EXPIRED
***********************************************************************
         LTR       R10,R10         IS UJI AREA AVAIL?
         BZ        UTLJOB1         BIF NOT
         CLC       ECPGMNAM,=C'IEBUPDTE' IS IT A PGM WHICH
         BE        UTLJPDS         MODIFIES A PDS DIRECTORY?
         CLC       ECPGMNAM,=C'IEWL    '   IF IT IS,
         BE        UTLJPDS         BRANCH
         CLC       ECPGMNAM,=C'IEBCOPY '
         BE        UTLJPDS
         CLC       ECPGMNAM,=C'SRJOLOAD'
         BE        UTLJPDS
         CLC       ECPGMNAM,=C'SRJOEXTR'
         BE        UTLJPDS
         TM        CEPIDFLG,CEPIDF01 'TEST' JOB?
         BO        UTLEXIT        BIF YES
UTLJOB1  EQU       *
         MVC       WML1TEXT,UTLMSG1A
         MVC       WML1TEXT+12(8),CEPJOBN JOBNAME
         MVC       WML2TEXT,UTLMSG2
         LA        R1,WARNMSG
         SLR       R0,R0        CLEAR FOR MLWTO
         WTO       MF=(E,(1))
         LA        R15,8          PERMIT EXTENSION (IN SECONDS)
         L         R1,=A(3*60)    EXTEND FOR 3 MINUTES
         B         UTLEXIT
UTLJPDS  EQU       *
         LA        R15,8       SILENTLY EXTEND CPU TIME
         L         R1,=A(10)   TO PREVENT PDS DIRECTORY DAMAMGE
         B         UTLEXIT
         SPACE     1
UTLSTEP  EQU       *
***********************************************************************
*        STEP LEVEL CPU TIME LIMIT EXPIRED
***********************************************************************
        LTR       R10,R10         IS UJI AREA AVAIL?
        BZ        UTLSTEP1        BIF NOT
         CLC       ECPGMNAM,=C'IEBUPDTE' IS IT A PROGRAM WHICH
         BE        UTLSPDS         MIGHT MODIFY A PDS DIRECTORY?
         CLC       ECPGMNAM,=C'IEWL    '   IF YES, BRANCH
         BE        UTLSPDS
         CLC       ECPGMNAM,=C'IEBCOPY '
         BE        UTLSPDS
         CLC       ECPGMNAM,=C'SRJOLOAD'
         BE        UTLSPDS
         CLC       ECPGMNAM,=C'SRJOEXTR'
         BE        UTLSPDS
         TM        CEPIDFLG,CEPIDF01 'TEST' JOB?
         BO        UTLEXIT        BIF YES
UTLSTEP1 EQU       *
         MVC       WML1TEXT,UTLMSG1B
         MVC       WML1TEXT+12(8),CEPJOBN JOBNAME
         MVC       WML2TEXT,UTLMSG2
         LA        R1,WARNMSG
         SLR       R0,R0       CLEAR R0 FOR MLWTO
         WTO       MF=(E,(1))
         LA        R15,8          PERMIT EXTENSION (IN SECONDS)
         L         R1,=A(3*60)
         B         UTLEXIT
UTLSPDS  EQU       *
         LA        R15,8    QUIETLY EXTEND CPU TIME TO
         L         R1,=A(10)  PREVENT PDS DIRECTORY DAMAGE
         B         UTLEXIT
         SPACE     1
UTLWAIT  EQU       *
***********************************************************************
*        CONTINUOUS WAIT TIME LIMIT EXPIRED
***********************************************************************
         TM        CEPSMFOP,CEPSMF07  TSO SESSION?
         BO        UTLTSO             BIF YES
         MVC       WML1TEXT,UTLMSG1C 'CONT WAIT TIME' MSG
         MVC       WML1TEXT+12(8),CEPJOBN JOBNAME
         MVC       WML2TEXT,UTLMSG2
         LA        R1,WARNMSG
         SLR       R0,R0        CLEAR FOR MLWTO
         WTO       MF=(E,(1))
         LA        R15,8          PERMIT EXTENSION (IN SECONDS)
         L         R1,=A(3*60)    3 MINUTE EXTENSION
         B         UTLEXIT
UTLTSO   EQU       *
         LA        R15,8      QUIETLY EXTEND FOR 30 MINUTES
         L         R1,=A(30*60)
         B         UTLEXIT
         SPACE     1
UTLEXIT  EQU       *
         LR        R2,R1          HOLD ONTO THE EXTENSION VALUE (IF ANY
         LR        R3,R15         HOLD ONTO RETURN CODE FOR A SEC
         L         R8,UTLSAVE+4   GET S/A POINTER
         LA        R0,UTLSIZE     SIZE OF GETMAIN
         LA        R1,UTLPOOL     POOL FOR FREEMAIN
         SLL       R1,24          SHIFT TO HI-BYTE
         OR        R0,R1          PUT THEM TOGETHER
         LR        R1,R13         POINTER TO G/M AREA
         FREEMAIN  R,LV=(0),A=(1)
         LR        R13,R8         GET S/A POINTER
         LR        R1,R2          GET EXTENSION VALUE
         LR        R15,R3         GET RETURN CODE
         DROP      R13
UTLLEAVE EQU       *
         L         R14,12(,R13)   GET BRANCH BACK ADDRESS
UTLFEXIT EQU       *
         RETURN    (2,12),RC=(15)
         LTORG
         TITLE     'IEFUTL - MESSAGES'
UTLMSG1A DC        CL70'IEFUTL 01 I XXXXXXXX JOB CPU TIME LIMIT EXPIREDX
               . ADD 3 MINUTES TO TASK'
UTLMSG1B DC        CL70'IEFUTL 03 I XXXXXXXX STEP CPU TIME LIMIT EXPIREX
               D.ADD 3 MINUTES TO TASK'
UTLMSG1C DC        CL70'IEFUTL 04 I XXXXXXXX CONTINUOUS WAIT LIMIT HAS X
               EXPIRED'
UTLMSG2  DC        CL70'IEFUTL 02 I INTERVENTION MAY BE REQUIRED'
         TITLE     'IEFUTL - COMMON EXIT PARAMETER AREA'
         COMMEXIT
         TITLE     'IEFUTL - G/M AREA PASSED FROM IEFUJI'
         EXITCORE
         ECFOLLOW
         TITLE      'IEFUTL - GETMAINED AREA'
         UTLCORE
WARNMSG  DS        0D
***********************************************************************
*
*        WARNING MSG FORMATTING AREA
*
***********************************************************************
WML1LGTH DS        AL2            1ST LINE TEXT LENGTH
WMMCSFLG DS        XL2            MCSFLAGS (=X'8040')
WML1TEXT DS        CL70           1ST LINE TEXT
WMDESCOD DS        XL2            DESC. CODES (=X'0400')
WMROUTCD DS        XL2            ROUT CODES (=X'8000')
WML1TYPE DS        XL2            1ST LINE LINE TYPE (=X'2000')
         DS        XL1            (RESERVED)
WM#LINES DS        AL1            TOTAL # OF LINES (=AL1(3))
WML2LGTH DS        AL2            2ND LINE TEXT LENGTH
WML2TYPE DS        XL2            2ND LINE LINE TYPE (=X'3000')
WML2TEXT DS        CL70           2ND LINE TEXT
WARNLGTH EQU       *-WARNMSG
         END
