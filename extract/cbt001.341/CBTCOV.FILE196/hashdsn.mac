         MACRO
         HASHDSN
HASHDSN  DS        0H
*********************************************************************
*
*        COMPRESS A DSN INTO 4 BYTES BY HASH-CODING IT.
*        ALGORITHM:
*              USING A 'TR'ANSLATE INSTRUCTION, CONVERT EVERY
*              CHARACTER (UP TO THE 1ST BLANK CHARACTER) TO
*              A PRIME NUMBER.  MULTIPLY EACH PRIME NUMBER IN
*              EACH SLOT BY THE RELATIVE SLOT #, AND SUM FOR
*              ALL SLOTS.
*        EXAMPLE:
*              DSN:MYNAME
*    CHARS IN NAME:  M     Y    N   A    M    E
*   ASSOC. PRIME #: 53   107   59   7   53   19
*           SLOT #:    1     2    3   4    5    6
*      COMPUTATION: 53*1+107*2+59*3+7*4+53*5+19*6=851
*
*        CALLING SEQUENCE:
*              ROUTINE IS ENTERED VIA 'BAL R8,HASHRTN'.  R1 MUST POINT
*              TO A DSNAME (UP TO 44 CHARS).  UPON LEAVING THE ROUTINE,
*              R15 WILL CONTAIN THE HASHED VALUE OF THE DSNAME.
*
*        CAUTIONS:
*              THIS ROUTINE SHOULD BE ASSUMED TO USE R0-R8 & R15.
*              THIS ROUTINE DESTROYS THE DSNAME FIELD WHOSE ADDR
*              IS PASSED IN R1.  THEREFORE THE FIELD MUST BE:
*                  .NON-FETCH PROTECTED;
*                  .'DISPOSABLE'.
*              THE DSNAME FIELD MUST CONTAIN AT LEAST ONE TRAILING
*              BLANK
*
**********************************************************************
         LA        R15,0          SET RESULT REG TO ZERO
         LR        R3,R1          HOLD ONTO DSN POINTER
         LA        R1,44(,R1)     SET 'STOP' VALUE FOR 'TRT' INSTR
         TRT       0(44,R3),HASHTAB LOOK FOR FIRST BLANK
         SR        R1,R3          LENGTH OF DSN IS IN R1
         BNP       HASHEXIT       BIF NULL DSN
         BCTR      R1,0           LENGTH->LENGTH CODE
         EX        R1,HASHTR      CONVERT CHARS TO PRIME #'S.
         LR        R2,R3          A(DSN)
         BCTR      R2,0           A(DSN)-1
         LA        R3,1(,R1)      LENGTH CODE -> LENGTH
HASHLOOP EQU       *
         SLR       R1,R1          CLEAR R1
         IC        R1,0(R3,R2)    GET PRIME #
         MR        R0,R3          PRIME * REL. SLOT LOCATION
         AR        R15,R1         ADD PRODUCT TO RESULT REG
         BCT       R3,HASHLOOP    LOOP UNTIL NO MORE
HASHEXIT EQU       *
         BR        R8             RETURN TO CALLER
HASHTR   TR        0(0,R3),HASHPRIM
HASHTAB  DC        64XL1'00',XL1'01',191XL1'00'
HASHPRIM DC        75XL1'00',AL1(1),15XL1'00',AL1(2)
         DC        31XL1'00',AL1(3),AL1(5),69XL1'00'
         DC        AL1(7),AL1(11),AL1(13),AL1(17),AL1(19),AL1(23)
         DC        AL1(29),AL1(31),AL1(37),7XL1'00'
         DC        AL1(41),AL1(43),AL1(47),AL1(53),AL1(59)
         DC        AL1(61),AL1(67),AL1(71),AL1(73),8XL1'00'
         DC        AL1(79),AL1(83),AL1(89),AL1(97),AL1(101)
         DC        AL1(103),AL1(107),AL1(109),6XL1'00'
         DC        AL1(113),AL1(127),AL1(131),AL1(137),AL1(139)
         DC        AL1(149),AL1(151),AL1(157),AL1(163),AL1(167)
         DC        7XL1'00'
         MEND
