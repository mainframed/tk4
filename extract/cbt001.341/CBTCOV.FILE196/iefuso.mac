         TITLE     'IEFUSO - MVS'
IEFUSO   CSECT
***********************************************************************
*
*        PROGRAM: IEFUSO.
*        INSTALLATION: U. S. SENATE COMPUTER CENTER
*        AUTHOR: D. HALBIG:
*        DATE WRITTEN: AUGUST 1979.
*        FUNCTION: PERMIT EXTENSIONS ON THE # OF SYSOUT LINES ISSUED
*              BY A DD STATEMENT CONTROLLED BY THE 'OUTLIM' KEYWORD.
*              EXTENSIONS ARE GRANTED ONLY TO NON-DEVELOPMENT JOBS;
*              DEVELOPMENT JOBS (USU. DESIGNATED SUCH BY RUN TYPE CODE
*              'T') ARE PERMITTED NO EXTENSION - (722 ABEND).
*
*         10/29/80 - IEFUSO GETMAIN LOGIC - SINCE OTHER SMF
*         EXITS (IEFUTL) MAY GAIN CONTROL ASYNCRONOUS FROM
*         IEFUSO, IEFUSO CANNOT USE THE IEFUJI-GETMAINED AREA
*        WITHOUT RUNNING THE RISK OF S/A CHAIN CONFUSION.
*        THIS MODIFICATION ENSURES IEFUSO DOES NOT MESS
*        WITH THE IEFUJI-GETMAINED AREA.
*
*        11/23/81(DGH) - INSTALL BYPASS CODE; FEATURE PERMITS
*          ALL SMF EXITS TO BE BYPASSED IF BYPASS FLAG IS SET.
*          BYPASS FLAG IS SET BY SMF EXIT IEFUJV (FOR FURTHER
*          INFO, SEE SMF EXIT IEFUJV).
*
*        REGISTER USE:
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS
*              R13                SAVEAREA/G-M AREA
*              R12                BASE REGISTER FOR EXIT
*              R11                PARM LIST PASSED TO IEFUSO
*              R10                COMMON EXIT PARAMETER AREA
*              R9-R0              WORK REGS
*
***********************************************************************
         REGEQU
         SAVE      (14,12),,IEFUSO-&SYSDATE-&SYSTIME
         LR        R12,R15        ESTAB. BASE REG.
         USING     IEFUSO,R12     TELL ASSEMBLER
         LR        R11,R1         HOLD ONTO PARM PTR
         USING     USOMAP,R11     GIVE IT ADDRESSABILITY
         L         R10,USOCEPA    GET PTR TO COMM EXIT PARM LIST
         LA        R10,0(,R10)    CLEAR HI-BYTE
         USING     COMMEXIT,R10   GIVE AREA ADDRESSABILITY
         SR        R15,R15        SET RC=0
         TM        CEPIDFLG,CEPIDF05 IS BYPASS FLAG SET?
         BO        USOFEXIT       BIF YES
         LR        R9,R13         HOLD ONTO CALLER'S S/A PTR
         LA        R0,USOSIZE     GETMAIN AREA SIZE
         LA        R1,USOPOOL     GETMAIN AREA SUBPOOL #
         SLL       R1,24          PUT IN HI-BYTE
         OR        R0,R1          PUT THEM TOGETHER
         GETMAIN   R,LV=(0)
         LR        R13,R1
         ST        R13,8(,R9)
         ST        R9,4(,R13)
         USING     USOCORE,R13   MAKE AREA ADDRESSALBE
         LA        R15,0          SET RC=0 INITIALLY
         SLR       R1,R1
         TM        CEPIDFLG,CEPIDF01 DEVELOPMENT JOB?
         BO        USOEXIT        BIF YES
         MVI       WARNMSG,X'00'  ZERO OUT FORMATTING AREA
         MVC       WARNMSG+1(WARNLGTH),WARNMSG
         LA        R1,74          LENGTH OF TEXT
         STH       R1,WML1LGTH
         STH       R1,WML2LGTH
         MVC       WMMCSFLG,=X'8040'
         MVC       WMDESCOD,=X'0400'
         MVC       WMROUTCD,=X'8000' ROUTCDE=1
         LA        R1,2           # OF LINES
         STC       R1,WM#LINES
         MVC       WML1TYPE,=X'2000'
         MVC       WML2TYPE,=X'3000'
         MVC       WML1TEXT,USOMSG1 'OUTLIM VALUE EXCEEDED' MSG
         MVC       WML1TEXT+12(8),CEPJOBN
         MVC       WML2TEXT,USOMSG2
         LA        R1,WARNMSG
         SLR       R0,R0         CLEAR R0 FOR MLWTO
         WTO       MF=(E,(1))
         LA        R15,4          SET RC=4 (PERMIT EXTENSION)
         L         R1,=A(6000)    EXTEND LIMIT 6000 LINES
         SPACE     1
USOEXIT  EQU       *
         LR        R2,R1      SAVE EXTENSION VALUE
         LR        R3,R15     SAVE RET CODE
         L         R9,USOCORE+4 SAVE PTR TO CALLER'S S/A
         LA        R0,USOSIZE FREEMAIN SIZE
         LA        R1,USOPOOL FREEMAIN SUBPOOL
         SLL       R1,24      MOVE TO HI-BYTE
         OR        R0,R1      PUT THEM TOGETHER
         LR        R1,R13 FREEMAIN ADDR
         FREEMAIN  R,LV=(0),A=(1)
         LR        R1,R2
         LR        R15,R3
         LR        R13,R9
         L         R14,12(,R13) BRANCH-BACK ADDR
USOFEXIT EQU       *
         RETURN    (2,12),RC=(15)
         LTORG
         TITLE     'IEFUSO - MESSAGES'
USOMSG1  DC        CL70'IEFUSO 01 I XXXXXXXX AN ADDITIONAL 6000 LINES AX
               SSIGNED TO THIS TASK'
USOMSG2  DC        CL70'IEFUSO 03 I OPERATOR INTERVENTION MAY BE REQUIRX
               ED'
         TITLE     'IEFUSO - LAYOUT OF AREA PASSED TO IEFUSO'
         USOMAP
         TITLE     'IEFUSO - COMMON EXIT PARAMETER AREA'
         COMMEXIT
         TITLE     'IEFUSO - G/M AREA PASSED FROM IEFUJI'
         EXITCORE
         ECFOLLOW
         TITLE      'IEFUSO - GETMAINED AREA FOR IEFUSO'
         USOCORE
WARNMSG  DS        0D
***********************************************************************
*
*        WARNING MSG FORMATTING AREA
*
***********************************************************************
WML1LGTH DS        AL2            1ST LINE TEXT LENGTH
WMMCSFLG DS        XL2            MCSFLAGS (=X'8040')
WML1TEXT DS        CL70           1ST LINE TEXT
WMDESCOD DS        XL2            DESC. CODES (=X'0400')
WMROUTCD DS        XL2            ROUT CODES (=X'8000')
WML1TYPE DS        XL2            1ST LINE LINE TYPE (=X'2000')
         DS        XL1            (RESERVED)
WM#LINES DS        AL1            TOTAL # OF LINES (=AL1(3))
WML2LGTH DS        AL2            2ND LINE TEXT LENGTH
WML2TYPE DS        XL2            2ND LINE LINE TYPE (=X'3000')
WML2TEXT DS        CL70           2ND LINE TEXT
WARNLGTH EQU       *-WARNMSG
         END
