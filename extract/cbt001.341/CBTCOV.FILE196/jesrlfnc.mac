         MACRO
         JESRLFNC
JESRLFNC DS        0H
*********************************************************************
*
*        THIS FUNCTION ISSUES A '$AJ' COMMAND TO RELEASE A
*        JOB PREVIOUSLY HELD.  NO CONFIRMATION IS MADE AS TO
*        THE SUCCESS OF THE RELEASE, THEREFORE NO RETURN CODE
*        IS GIVEN BY THIS ROUTINE.  THE JES JOB # OF THE JOB
*        TO BE RELEASED MUST BE IN R2 BEFORE CALLING THIS FUNCTION.
*
*        INSTALLED ON: 2/6/80; BY: DGH.
*        MODIFIED ON: 6/11/81; BY: DGH - CLEAR R0 BEFORE
*           CALLING SVC 34. ??????????????
*
*        NOTES:
*              THIS FUNCTION USES TWO WORK AREAS: CMDWORK - CONTAINS
*              COMMAND TO BE ISSUED & LENGTH OF THAT COMMAND; DOUBLE -
*              USED TO CONVERT THE BINARY-CODED JES JOB # TO
*              DISPLAY FORMAT. THE PROGRAM WHICH USES THIS FUNCTION
*              MUST BE AUTHORIZED.
*
*********************************************************************
         CVD       R2,DOUBLE
         SRP       DOUBLE,1,0     MULT BY 10
         XC    CMDWORK,CMDWORK BLANK OUT COMMAND AREA
         LA        R1,L'CMDWORK   GET LENGTH OF COMMAND AREA
         STH       R1,CMDLGTH     PUT LENGTH IN COMMMAND AREA
         MVC       CMDAREA+0(3),=C'$A ' BUILD '$AJXXX' COMMAND
         MVC       CMDAREA+3(4),=X'21202020'
         ED        CMDAREA+2(5),DOUBLE+5 PUT JES JOB # IN CMD AREA
         MVI       CMDAREA+2,C'J'
         SR        R0,R0          CLEAR REG FOR SVC 34
         LA        R1,CMDWORK
         SVC       34             ISSUE COMMAND
         BR        R8
         MEND
