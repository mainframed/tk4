         TITLE     'CPWTOR - MAIN PROGRAM'
CPWTOR   CSECT
**********************************************************************
*
*        PROGRAM NAME: CPWTOR.
*        AUTHOR: D. HALBIG.
*        INSTALLATION: U.S. SENATE COMPUTER CENTER.
*        DATE WRITTEN: NOVEMBER 1979.
*        FUNCTION: TO PROVIDE CONTROL STATEMENTS TO THE IV-TUNE
*        CONFIGURATION PROCESSOR PROGRAM.  THE CPWTOR PROGRAM
*        ISSUES 'WTOR'S FROM WHICH RESPONSE TEXT IS BUILT.  THE
*        RESPONSE TEXT IS WRITTEN TO A FIXED-LENGTH 80-CHARACTER
*        LRECL SEQUENTIAL DATA SET.  NO CHECKING IS MADE FOR THE
*        CORRECTNESS OF CONFIGURATION PROCESSOR CONTROL STATEMENTS.
*
*        REGISTER                 USAGE
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS/WORK REG
*              R13                SAVE AREA/WORK AREA
*              R12                PROGRAM BASE REGISTER
*              R11-R0             WORK REGISTERS
*
**********************************************************************
         REGEQU
         SAVE      (14,12),,CPWTOR-&SYSDATE
         LR        R12,R15        ESTAB. BASE REGISTER
         USING     CPWTOR,R12     TELL ASSEMBLER
         L         R0,GETMAINL    GET 'GETMAIN' PARMS
         GETMAIN   R,LV=(0)
         LR        R2,R1
         L         R3,GETMAINL    ZERO OUT GETMAINED AREA
         SR        R4,R4
         LR        R5,R4
         MVCL      R2,R4
         ST        R1,8(,R13)     TELL CALLER OF OUR S/A
         ST        R13,4(,R1)      PUT CALLER'S S/A PTR IN OUR S/A
         LR        R13,R1         ESTAB. NEW S/A PTR
         USING     WORKAREA,R13   TELL ASSEMBLER
         MVC       OUTDCB,PATRNDCB   MOVE PATTERN DCB TO WORK AREA
         MVI       OPENLIST,X'80'    SIGNAL END OF LIST
         OPEN      (OUTDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM        OUTDCB+DCBOFLGS-IHADCB,DCBOFOPN OPEN OK?
         BO        WTORLOOP       BIF YES
         WTO       'CPWTOR-02-W - OPEN FAILED FOR OUTPUT DATA SET',    X
               ROUTCDE=2,DESC=6
         B         EOFRTN
         SPACE     2
WTORLOOP EQU       *
         XC        REPLYECB,REPLYECB   CLEAR ECB
         MVI       REPLYTXT,C' '  BLANK OUT REPLY AREA
         MVC       REPLYTXT+1(L'REPLYTXT-1),REPLYTXT
         LA        R2,REPLYTXT
         LA        R3,60          REPLY LENGTH
         LA        R4,REPLYECB    REPLY ECB
         MVC       WTORLIST(WTORLGTH),PTRNWTOR MOVE IN PATTERN WTOR
         WTOR      'CPWTOR-01-I - ENTER CONTROL CARD TEXT',            X
               (2),(3),(4),ROUTCDE=(2),MF=(E,WTORLIST)
         WAIT      1,ECB=(4),LONG=YES
         CLC       REPLYTXT(2),=C'U '  IS THE REPLY A 'U'?
         BE        EOFRTN         BIF YES (END-OF-FILE)
LGTHOK   EQU       *
         BCTR      R3,0           LENGTH->LENGTH CODE
         MVI       OUTAREA,C' ' BLANK OUTPUT AREA LINE
         MVC       OUTAREA+1(L'OUTAREA-1),OUTAREA
         EX        R3,MOVEAREA    MOVE TEXT TO OUTPUT AREA
         TR        OUTAREA,TRANTAB CONVERT NON-PRINT TO BLANKS
         PUT       OUTDCB,OUTAREA
         B         WTORLOOP       CONTINUE WITH LOOP
         SPACE     2
EOFRTN   EQU       *
         MVI       CLOSLIST,X'80' SET END-OF-LIST
         CLOSE     (OUTDCB),MF=(E,CLOSLIST)
         L         R11,SAVEAREA+4
         LR        R1,R13         SET UP FREEMAIN PARMS
         L         R0,GETMAINL
         FREEMAIN  R,LV=(0),A=(1)
         LR        R13,R11
         RETURN    (14,12),RC=0
         TITLE     'CPWTOR - PROGRAM CONSTANTS'
**********************************************************************
*
*        PROGRAM CONSTANTS
*
*********************************************************************
MOVEAREA MVC       OUTAREA(0),REPLYTXT     PATTERN MVC INSTR
GETMAINL DC        A(WORKEND-WORKAREA) GETMAIN PARMS FOR WORK AREA
PATRNDCB DCB       DDNAME=OUTPUT,LRECL=80,BLKSIZE=6160,DSORG=PS,       X
               MACRF=(PM),RECFM=FB
PTRNWTOR WTOR      'CPWTOR-01-I - ENTER CONTROL CARD TEXT',            X
               ROUTCDE=(2),MF=L
ENDPTRNW EQU       *
TRANTAB  DC        74C' ',X'4A4B4C4D4E4F50',9C' '
         DC        X'5A5B5C5D5E5F6061',8C' '
         DC        X'6A6B6C6D6E6F',10C' '
         DC        X'7A7B7C7D7E7F',65C' '
         DC        C'ABCDEFGHI',7C' '
         DC        C'JKLMNOPQR',8C' '
         DC        C'STUVWXYZ',6C' '
         DC        C'0123456789',6C' '
         LTORG
         TITLE     'CPWTOR - WORKAREA'
*********************************************************************
*
*        WORK AREA
*
********************************************************************
WORKAREA DSECT
SAVEAREA DS        18F
OUTDCB   DS        CL96
REPLYTXT DS        0F,CL100
REPLYECB DS        F              ECB FOR WTOR
OUTAREA  DS        CL80
WTORLIST WTOR      'CPWTOR-01-I - ENTER CONTROL CARD TEXT',ROUTCDE=2,  X
               MF=L
WTORLGTH EQU       *-WTORLIST
OPENLIST DS        F              AREA TO STOW OPEN PARMS
CLOSLIST DS        F              DITTO, BUT FOR CLOSE
WORKEND  DS        0D
         TITLE     'CPWTOR - DSECTS AND OTHER LAYOUTS'
         IHADCB    DSORG=(PS),DEVD=(DA)
         END
