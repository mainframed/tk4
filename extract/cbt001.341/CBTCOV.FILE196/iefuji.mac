         TITLE     'IEFUJI - LINKAGE CONTROL TABLE (LCT)'
IEFLOT   DSECT
         IEFALLCT
         TITLE     'IEFUJI - JOB CONTROL TABLE (JCT)'
IEFJCT   DSECT
         IEFAJCTB
         TITLE     'IEFUJI - DCB LAYOUT'
         DCBD      DEVD=(DA),DSORG=(PS)
         TITLE     'I E F U J I   -   IOS DATA EXTENT BLOCK (DEB)'
         IEZDEB
         TITLE     'IEFUJI - PREFIX STORAGE AREA (PSA)'
         IHAPSA
         TITLE     'IEFUJI - CVT LAYOUT'
CVTMAP   CVT       DSECT=YES
         TITLE     'IEFUJI - SYSTEM MGMT CONTROL AREA (SMCA)'
         IEESMCA
         TITLE     'IEFUJI - ADDRESS SPACE VECTOR TABLE (ASVT)'
         IHAASVT
         TITLE     'IEFUJI - ADDRESS SPACE CONTROL BLOCK(ASCB)'
         IHAASCB
         TITLE     'IEFUJI - TIMER SUPERVISION WORK AREA(TPC)'
         IEAVVTPC
         TITLE     'IEFUJI - DIE WORK AREA (INCL. TQE)'
         DIEWORK
         TITLE     'IEFUJI - SRM USER CONTROL BLOCK (OUCB)'
         IRAOUCB
         AIF       ('&SYSPARM' EQ 'NONSE' OR '&SYSPARM' EQ 'SE2' OR    *
               '&SYSPARM' EQ 'SP10' OR '&SYSPARM' EQ 'SP11').PRESP3A
         AIF       ('&SYSPARM' EQ 'SP13' OR '&SYSPARM' EQ 'SP12').SP3A
         MNOTE     12,'IEFUJI - INVALID SYSPARM FIELD IN ASSEMBLY'
         AGO       .RESUMEA
.SP3A    ANOP
         TITLE     'IEFUJI - RESOURCE INFORMATION BLOCK(RIB)'
         ISGRIB
         AGO       .RESUMEA
.PRESP3A ANOP
         TITLE     'IEFUJI - QUEUE CONTROL BLOCK (QCB)'
         IHAQCB
         TITLE     'IEFUJI - QUEUE ELEMENT (QEL)'
         IHAQEL
.RESUMEA ANOP
         TITLE     'IEFUJI - TASK CONTROL BLOCK (TCB)'
         IKJTCB
         TITLE     'IEFUJI - SUBSYSTEM OPTIONS BLOCK (SSOB)'
         IEFJSSOB  (CS)
         TITLE     'IEFUJI - SUBSYSTEM IDENTIFICATION BLOCK (SSIB)'
         IEFJSSIB
         TITLE     'IEFUJI - REQUEUER CROSS-MEMORY COMMUNICATION AREA'
         CROSSMEM
IEFUJI   CSECT
         TITLE 'IEFUJI - JOB ENTRY SUBSYSTEM CONTROL TABLE (JESCT)'
         $JESCT     LIST=NO
         TITLE     'IEFUJI - SUBSYSTEM CVT(SSCVT)'
         $SSCT     LIST=NO
         TITLE     'IEFUJI - SUBSYSTEM VECTOR TABLE(SSVT)'
         $SVT      DOC=NO
         TITLE     'IEFUJI - SUBSYSTEM JOB BLOCK(SJB)'
         $SJB      DOC=NO
         TITLE     'IEFUJI - MAIN ROUTINE'
*********************************************************************
*
*        PROGRAM-ID: IEFUJI
*        DATE WRITTEN: DECEMBER 1979.
*        AUTHOR: D. HALBIG
*        INSTALLATION: U.S. SENATE COMPUTER CENTER.
*        FUNCTION: TO GET-MAIN AN AREA TO BE USED BY ALL SUBSEQUENT
*        SMF EXITS ASSOCIATED WITH A JOB (IEFUSO, IEFUTL, IEFACTRT).
*        EXIT EXTENSIVELY MODIFIED DEC. 1979 BY DGH TO INCLUDE
*        DSNAME ENQUEUE CONFLICT REQUEUE FUNCTION.  PARALLEL CODE
*        ADDED TO IEFACTRT TO COMPLETE FEATURE.  FEATURE RELIES
*        ON FACT THAT LCT (LINKAGE CONTROL TABLE) ADDRESS IS
*        PASSED TO THE SUBROUTINE IN R10.  THIS FACT IS NOTED IN
*        IEFSMFIE.  ONE OF THE FIELDS IN THE LCT, 'LCTPARM1',
*        CONTAINS THE ADDRESS OF AN ENQ PARAMETER LIST BUILT
*        BY 'IEFSD161'&'IEFDSLST'.  THIS PROGRAM USES THIS PARM LIST BY
*        ISSUING AN 'ENQ RET=TEST'. (AFTER USE, LIST IS RESET TO BE
*        USED BY    'IEFSD102').  IF NON-ZERO RETURN CODE IS RETURNED
*        IN R15, THE PROGRAM CHAINS THRU THE ENQ LIST LOOKING FOR THE
*        DSNAME INVOLVED IN THE CONFLICT.  USING THE QCB & QEL CHAINS
*        THE TCB & ASID OF THE OWNER IS DISCOVERED, ALONG WITH THE
*        DSNAME INVOLVED IN THE CONFLICT.  IF THE RESOURCE REQUESTOR
*        IS FOUND TO BE A SYSTEM TASK OR A TSO SESSION, THIS EXIT
*        GIVES UP CONTROL, SINCE STANDARD MVS HANDLING OF THESE
*        SITUATIONS IS ADEQUATE.  IF THE RESOURCE REQUESTOR IS
*        A BATCH JOB, BUT THE RESOURCE OWNER IS NOT, THE REQUESTOR
*        IS REQUEUED WITHOUT BEING SCHEDULED FOR AUTO-RELEASE.
*        IF THE RESOURCE OWNER IS A TSO SESSION, A MESSAGE IS SENT
*        TO THE TSO USER TELLING HIM THE JOB HAS BEEN REQUEUED &
*        FREE THE DATA SET.  IF BOTH THE REQUESTOR & THE OWNER ARE
*        BATCH JOBS, THE REQUESTOR IS SCHEDULED FOR AUTO-RELEASE
*        AND THEN IS REQUEUED AND PUT ON HOLD.
*
*        MODIFIED ON 1/9/81; BY: DGH - DCM WORK AREA
*        IS CHECKED FOR VALUE '*UNKNOWN' - IF DCM IS
*        ACTIVE & A CONFLICT EXISTS, BUT DCM IS UNABLE
*        TO DETERMINE THE PLAYERS BECAUSE THE CONFLICT
*        IS RELIEVED TOO QUICKLY, THE VALUE '*UNKNOWN'
*        IS PLACED IN THE WORK AREA.  IN THIS CASE, IEFUJI
*        EXITS, TAKING NO ACTION.
*
*       MODIFIED ON 1/9/81 BY DGH - REVISE LCT DISCOVERY CHAIN.
*       IEFSMFIE WAS REWRITTEN BETWEEN SE1 & SE2 - R10, THE
*       PREVIOUS OWNER OF THE LCT ADDRESS, IS NOT VALID.  THE
*       SAVE AREA FOR THE CALLER OF IEFSMFIE (IEFSD101) CONTAINS
*       THE LCT ADDRESS.  THIS FACT IS INDEPENDENT OF MVS LEVEL
*       (NSE 3.7,NSE 3.8,SE,SE1,SE2); NOTE, HOWEVER, THAT
*       THE SAVE AREA CHAINING BACK TO THIS ROUTINE IS INVALID
*       PRIOR TO MVS/SE.
*
*       11/23/81 - INSERT BYPASS CODE;
*         FEATURE PERMITS ALL SMF EXITS TO BE BYPASSED IF SMF EXITS
*         ARE SUSPECTED OF CAUSING PROBLEMS.  FLAG USED TO SIGNAL
*         BYPASS IS SET BY IEFUJV IN THE FLAG BYTE OF THE USERID
*         FIELD IN THE COMMON EXIT PARAMETER AREA.
*
*        2MAR82(DGH) - REVISE TECHNIQUE FOR OBTAINING SELECTION
*         PRIORITY: PREVIOUSLY, SELECTION PRIORITY PASSED TO IEFUJI
*         BY SMF WAS USED.  THIS PRIORITY REFLECTED ONLY THE READER
*         (ORIGINAL) PRIORITY OF THE JOB.  IF THE JOB'S PRIORITY WAS
*         RESET BY THE OPERATOR, A REQUEUED JOB COULD NOT TAKE
*         ADVANTAGE OF THIS NEW PRIORITY.  PRIORITY IS NOW OBTAINED
*         FROM A USER-DEFINED FIELD IN THE JMR. THE FIELD, CEPIDPTY,
*         IS SET BY IEFUJV(ENTRY CODE 32).
*
*        31MAR82(DGH) - REVISE DSN CONFLICT HANDLING FOR TSO SESSIONS;
*         VICTIM IS NOW AUTOMATICALLY RELEASED WHEN TSO USER SIGNS OFF.
*         CONVERT STIMER LOGIC TO 'SETDIE'/'PURGDIE'; FIX MISC BUGS.
*
*        04APR82(DGH) - ADD STUB ROUTINE FOR TMS ACCOUNTING; MAY
*         BE FILLED IN LATER
*
*        12APR82(DGH) - ADD SUPPORT FOR SP1.3 - SYSPARM(SP13) MUST BE
*         USED DURING ASSEMBLY TO INVOKE SUPPORT.
*
*        02JUL82(ABS) - ADDED CODE TO ALLOW CONSISTANT S322_S ON
*         DIFFERENT SPEED MACHINES.  IN INITRTN DETERMINE IF SPEED
*         NORMALIZATION IS NECESSARY (BATCH ONLY, NOT RUNNING ON
*         SLOWEST MACHINE, AND TIMED (TIME .NE. 1440.))  MULTIPLY
*         JCTJMRTL BY 100 THEN DIVIDE BY SPEED COEF. (WHICH HAS
*         ALREADY BEEN MULTIPLIED BY 100.)  SPEED COEF. IS SPEED
*         DIFFERENTIAL PLUS 10% TIMES 100.
*
*        13AUG82(ABS) - SAVED A(LCT) IN EXITCORE FOR USI
*
*        02SEP82(ABS) - TO PUT THIS MESS INTO PRODUCTION, WE WILL
*         FIRST REPORT ADJUSTED TIMES WITHOUT CHANGING TIME LIMITS.
*         ALL WORK IN THIS CSECT MUST BE DONE EXCEPT RE-SETTING
*         JCTJMRTL.  THE STICKEM (STCM) HAS BEEN TURNED TO A
*         COMMENT.
*
*        08OCT82(ABS) - CHANGE REPORTING BASE MACHINE TIMES TO
*         ENFORCING BASE MACHINE TIMES.  STICKEM BY UN-COMMENTING
*         THE STCM
*
*        08NOV82(ABS) - DELETE CODE EXECUTED IF MSI NOT OPERATING THAT
*         DETERMINES IF OWNER OF RESOURCE IS A STARTED TASK, AND
*         SUBSEQUENT AUTO-DA-FE.  NOT NECESSARY.
*         - DOUBLE WAIT TIME AFTER $EJ____;$HJ____.  TIMING PROBLEM.
*         WHILE UNDER HEAVY LOAD, RC=4 CAUSED CANCELLATION BEFORE
*         ABOVE JES COMMANDS (EXECUTED VIA SVC 34) TOOK EFFECT.
*
*        21DEC82(ABS) - CHANGED DELAY TO 3 SECONDS AFTER
*         $EJ____;$HJ____.  HOPE THAT THIS IS SUFFICIENT.
*
*        07JAN83(ABS) - 3 SECOND DELAY ABOVE CREATED A NEW PROBLEM.
*         WITH HEAVY REQUEUEING ACTIVITY FROM DSN CONFLICTS REQUEUER
*         RESOURCE CAN BECOME UNAVAILABLE FOR A WHILE.  30 SECOND
*         TIMER FOR SETDIE EXPIRES AND REQUEUER APPEARS NOT AVAILABLE.
*         CONSEQUENTLY, $HJ____;$EJ____ ARE NOT ISSUED.  SINCE THE
*         PURPOSE OF THE TIMER ON THE SETDIE IS TO ALLOW A SOFT FAILURE
*         SHOULD THE REQUEUER BREAK, I AM NOW CHANGING THE DELAY TO
*         2 MINUTES.  SEE ALSO IEFACTRT.
*
*
*        REGISTER                 FUNCTION
*              R15                ENTRY POINT/RETURN CODE
*              R14                RETURN ADDRESS/WORK REG
*              R13                SAVE AREA/WORK AREA
*              R12                PROGRAM BASE REGISTER
*              R11                ROUTINE BRANCH REGISTER
*              R10                ROUTINE BRANCH BACK ADDRESS
*              R9                 ADDR OF LCT
*              R8                 FUNCTION BRANCH BACK REGISTER
*              R7-R0              WORK REGISTERS
*
**********************************************************************
         REGEQU
         SAVE      (14,12),,IEFUJI-&SYSDATE-&SYSTIME
         LR        R12,R15        ESTAB. BASE REG. FOR PROGRAM
         USING     IEFUJI,R12     TELL ASSEMBLER
         LR        R9,R1          ADDR OF IEFUJI ENTRY PARM LIST
         USING      UJIMAP,R9      TELL ASSEMBLER
         L          R8,UJICEPA     GET ADDR OF COMM. EXIT PARM AREA
         USING      COMMEXIT,R8    TELL ASSEMBLER
         SR         R15,R15        SET RETURN CODE TO ZERO
         TM        CEPIDFLG,CEPIDF05  BYPASS FLAG SET?
         BO         UJIFEXIT       BIF YES
         L         R0,=A(ECSUBPOL) GET SUBPOOL # FOR GETMAIN
         SLL       R0,24          PUT SUBPOOL IN HI-ORDER BYTE
         L         R1,=A(ECSIZE)  GET SIZE OF AREA TO BE GET-MAINED
         TM        CEPSMFOP,CEPSMF07   TSO SESSION?
         BZ        UJINOTSO         BIF NOT TSO
         L         R1,=A(ECTSOSIZ)  SIZE OF TSO-RELATED GETMAIN
UJINOTSO EQU       *
         OR        R0,R1          PUT SUBPOOL & SIZE TOGETHER
         LR        R6,R0          HOLD ONTO THIS FOR A SEC
         LR        R3,R1          HOLD ONTO THIS FOR A SEC
         GETMAIN   R,LV=(0)
         LR        R2,R1
         LA        R4,0
         LA        R5,0
         MVCL      R2,R4          ZERO OUT GETMAINED AREA
         ST        R13,4(,R1)     COMPLETE S/A CHAINING
         ST        R1,8(,R13)
         LR        R13,R1
         USING     EXITCORE,R13
         LA        R13,0(,R13)    CLEAR HI-BYTE OF 13
         ST        R13,CEPUSER    STOW IN CEPA FOR OTHER EXITS
         ST        R8,CEPAADDR    HOLD ONTO CEPA ADDRESS
         ST        R9,UJIPADDR    HOLD ONTO UJI PARMS ADDRESS
         DROP      R8,R9
         LR        R9,R10  IF NOT MSE20, R10 HAS LCT POINTER
         L         R1,16(0,0)       GET CVT ADDR
         USING     CVTMAP,R1        MAKE IT ADDRESSABLE
         L         R1,CVTIHASU
         TM        9(R1),X'20'      MVS/SE-II?
         DROP      R1
         BZ        UJINSE           BIF NOT MVS/SE-II
         L         R9,SAVEAREA+4    GET IEFSMFIE'S S/A POINTER
         L         R9,4(,R9)        GET IEFSD101'S S/A POINTER
         L         R9,24(,R9)       GET R1 (LCT POINTER)
UJINSE   EQU       *
         ST        R9,ECLCTADR    SAVE LCT FOR LATER (INCL USI...)  ABS
         USING     IEFLOT,R9      GIVE IT ADDRESSABILITY
         ST        R6,GETPARM     STOW GETMAIN PARMS FOR IEFACTRT
         L         R11,=A(INITRTN) DO REMAINDER OF INITIALIZATION
         BALR      R10,R11        USING 'INITRTN'
         TM        ERRORSW,ERRINIT  ERROR IN INIT RTN?
         BO        UJIERROR       BIF YES
         L         R1,CEPAADDR    GET ADDR OF COMM EXIT PARM AREA
         USING     COMMEXIT,R1    MAKE IT ADDRESSABLE
         TM        CEPIDFLG,CEPIDF02 BATCH JOB?
         BZ        UJIEXIT        BIF NOT: LEAVE NOW
         DROP      R1
         L         R1,LCTPARM1    GET ADDR OF DSN ENQ LIST
         LTR       R1,R1          IS THERE AN ENQ LIST?
         BZ        UJITMS         BIF NO; JUST DO TMS ACCOUNTING
         L         R1,LCTJCTAD    GET ADDR OF JCT
         USING     IEFJCT,R1      GIVE IT ADDRESSABILITY
         TM        JCTJSTAT,INCMSTS HAS JOB FAILED ANYWAY?
         BO        UJIEXIT        BIF YES
         DROP      R1
         L         R11,=A(ENQRTN)
         BALR      R10,R11        PERFORM ENQ TEST FUNCTION
         TM        ERRORSW,ERRENQ ERROR IN ENQ FUNCTION?
         BO        UJIERROR       BIF NOT
UJITMS   EQU       *
         L         R11,=A(TMSRTN) GET ADDR OF TMS ACCOUNTING ROUTINE
         BALR      R10,R11        PERFORM TMS ACCT ROUTINE
         TM        ERRORSW,ERRTMS ERROR IN TMS ACCT ROUTINE?
         BZ        UJIEXIT        BIF NOT
*
UJIERROR EQU       *
         LA        R15,4          OTHERWISE, CANCEL JOB
         ST        R15,RETCODE
         B         ERRORRTN
UJIEXIT  EQU       *
         L         R15,RETCODE
         L         R2,SAVEAREA+4  GET ADDR OF CALLER'S S/A
UJILEAVE EQU       *
         LR        R13,R2         GET CALLER'S S/A PTR
UJIFEXIT EQU       *
         RETURN    (14,12),RC=(15) 'BYE
         TITLE     'IEFUJI - ERROR FUNCTION'
ERRORRTN DS        0H
*********************************************************************
*
*        ENTERED FOR ALL TYPES OF ERRORS; AT ENTRY, R14 IS
*        EXPECTED TO CONTAIN AN ADDRESS, WHICH, WHEN R12 IS
*        SUBTRACTED FROM IT, WILL POINT TO THE RELATIVE
*        OFFSET AT WHICH THE ERROR WAS DETECTED.
*
********************************************************************
         STM       R0,R15,SNAPREGS HOLD FOR DIAGNOSTICS
         WTO       'IEFUJI-01-W - FATAL ERROR-CALL SYSTEMS PROGRAMMING'X
               ,ROUTCDE=1,DESC=2
         LA        R13,0(,R13)    CLEAR HI-BYTE
         ST        R13,SNAPSTR1   SET UP 'SNAP' LIST
         LA        R1,WORKEND-EXITCORE(,R13)
         ST        R1,SNAPEND1
         LA        R9,0(,R9)      LCT ADDRESS
         ST        R9,SNAPSTR2
         LA        R1,IEFEND-IEFLOT(,R9) END OF LCT
         ST        R1,SNAPEND2
         OI        SNAPEND2,X'80' SET END OF LIST
         L         R1,LCTPARM1    GET ADDR OF ENQ PARM LIST
         LA        R1,0(,R1)      CLEAR HI-BYTE
         LTR       R1,R1          IS THERE AN ENQ PARM LIST?
         BZ        ERROPEN        BIF NOT
         MVI       SNAPEND2,X'00' REMOVE END-OF-LIST INDICATOR
         ST        R1,SNAPSTR3
         L         R2,LCTPARM2    GET SUBPOOL & LENGTH OF LIST
         LA        R1,0(R2,R1)    ADD LENGTH TO ENQ LIST ADDR
         ST        R1,SNAPEND3
         OI        SNAPEND3,X'80' END-OF-LIST INDIC.
ERROPEN  EQU       *
***********************************************************************
*
*     FORCE 'S0C1' ABEND INSTEAD OF ISSUING 'SNAP'
*
*     DATE: 10/3/80; MAINTAINER: DGH.
*
***********************************************************************
         DC        X'0000'
         MVI       OPENLIST,X'80' SET END-OF-LIST FOR 'OPEN'
         OPEN      (SNAPDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM        SNAPDCB+(DCBOFLGS-IHADCB),DCBOFOPN OPEN OK?
         BO        ERROPNOK       BIF YES
         WTO       'IEFUJI-02-I - OPEN FAILED FOR SNAPDUMP DD STATEMENTX
               ',ROUTCDE=2,DESC=6
         B         ERRORXIT       NOW LEAVE
ERROPNOK EQU       *
         SNAP      DCB=SNAPDCB,LIST=SNAPLIST,SDATA=(LSQA,SWA,CB),      X
               PDATA=(ALL),MF=(E,SNAPMACR)
         LTR       R15,R15        SNAP OK?
         BZ        ERRSNPOK       BIF YES
         WTO       'IEFUJI-03-I - SNAP OF DIAGNOSTIC AREAS FAILED',    X
               ROUTCDE=2,DESC=6
         B         ERRORXIT
ERRSNPOK EQU       *
         MVI       CLOSLIST,X'80' END-OF-LIST FOR 'CLOSE'
         CLOSE     (SNAPDCB),MF=(E,CLOSLIST)
ERRORXIT EQU       *
         LA        R15,4          SET RC=4
         ST        R15,RETCODE
         MVI       ENQMACRO+2,X'41' SET OPTION FLAGS
         MVC       ENQQNAME,=C'REQUEUER' DO DEQ JUST IN CASE
         MVC       ENQRNAME(8),=C'RESOURCE'
         OI        ENQMACRO,X'C0' MARK END-OF-LIST
         DEQ     (ENQQNAME,ENQRNAME,8,SYSTEM),RET=HAVE,MF=(E,ENQMACRO)
         B         UJIEXIT
         TITLE     'IEFUJI - FUNCTION TO ESTAB. DISABLED INTERR. RTN'
         SETDIE
         TITLE     'IEFUJI - FUNCTION TO CLEAN UP AFTER SETDIE'
         PURGEDIE
         TITLE     'IEFUJI - DSNAME HASHING FUNCTION'
         HASHDSN
         TITLE     'IEFUJI - CONSTANTS'
         DS        0D             FORCE ALIGNMENT
REQNAME  DC        CL8'REQUEUER'      REQUEUER QNAME VALUE
PATDCB   DCB       DSORG=PS,MACRF=(W),LRECL=125,                       X
               BLKSIZE=882,DDNAME=SNAPDUMP
PATDCBL  EQU       *-PATDCB
PATSNAP  SNAP      DCB=0,LIST=0,SDATA=(LSQA,SWA,CB),PDATA=(ALL),       X
               MF=L
PATSNAPL EQU       *-PATSNAP
         LTORG
         TITLE     'IEFUJI - INITIALIZATION ROUTINE'
INITRTN  DS        0H
**********************************************************************
*
*        INITIALIZATION ROUTINE
*
*********************************************************************
         USING     INITRTN,R11    GIVE IT ADDRESSABILITY
         MVC       SNAPDCB(PATDCBL),PATDCB MOVE PATTERN SNAP DCB
         MVC       SNAPMACR(PATSNAPL),PATSNAP
         XC        RETCODE,RETCODE SET RC=0 INITIALLY
         L         R2,16          PTR TO CVT
         USING     CVTMAP,R2         MAKE IT ADDRESSABLE
         L         R2,CVTJESCT    CVT -> JESCT
         USING     JESCT,R2
         L         R2,JESSSCT     JESCT -> SSCVT
         USING     SSCT,R2
         L         R2,SSCTSSVT    SSCVT -> SSVT
         USING     SSVT,R2
         L         R2,$SVHAVT     ADDR SPACE VECTOR TABLE
         L         R3,PSAAOLD-PSA     PSAAOLD -> OUR ASCB
         USING     ASCB,R3
         LH        R3,ASCBASID    OUR ASID
         SLL       R3,2           MULT BY 4
         AR        R2,R3          => (ASID * 4) + $SVHAVT = SJB ADDR
         L         R2,0(,R2)      GET SJB POINTER FROM VECTOR TABLE
         USING     SJBDSECT,R2
         L         R1,CEPAADDR    GET POINTER TO COMM EXIT PARM AREA
         USING     COMMEXIT,R1    MAKE IT ADDRESSABLE
INITNEXT EQU       *
         CLC       CEPJOBN(8),SJBJOBNM
         BE        INITJOBN   BIF WE FOUND OUR JOBNAME
         ICM       R2,7,SJBSJB+1    IF NOT, CHAIN FORWARD
         BNZ       INITNEXT   PROCEED ONLY IF VALID PTR
         BAL       R14,ERRORRTN   OTHERWISE, ERROR
INITJOBN EQU       *
         CLC       =CL8'        ',CEPUSRID  DID IEFUJV ALREADY DO THIS?
         BNE       INITJCLS         BIF YES
         PACK      WORK01,SJBJOBID+3(5)  GET DISPLAY FORM OF JES ID #
         CVB       R4,WORK01
         OI        CEPIDFLG,CEPIDF02 ASSUME JOB IS BATCH
         CLC       =CL3'JOB',SJBJOBID BATCH JOB?
         BE        INITJES#       BIF YES
         CLC       =CL3'TSU',SJBJOBID TSO SESSION?
         BNE       INITSTC        BIF NOT; ASSUME STC
         XI        CEPIDFLG,CEPIDF02+CEPIDF03 TURN OFF BATCH & ON TSO
         A         R4,=A(20000)   ADD 20000 FOR TSO SESSION JES#
         B         INITJES#
INITSTC  EQU       *
         XI        CEPIDFLG,CEPIDF02+CEPIDF04 TURN OFF BATCH & ON STC
         A         R4,=A(10000)   ADD 10000 FOR SYSTEM TASK JES#
INITJES# EQU       *
         STH       R4,CEPIDJES  STOW BINARY FORM OF JES JOB #
INITJCLS EQU       *
         MVC       CEPIDCLS(1),SJBJCLAS  PUT IN INITIATED CLASS
         MVC       CEPIDPTY,SJBPRIO  UPDATED JOB SELECTION PRIORITY
         EJECT
*                                                             ABS070282
         CLC   SJBJOBID(3),=CL3'JOB' IS THIS A BATCH JOB ??           "
         BNE   INITEXIT      FINI IF NOT BATCH                        "
*                                                                     "
INITBATC EQU   *             BATCH ONLY___                            "
*                            ___NORMALIZE JOB CPU TIMES TO            "
*                            ___SLOWEST MACHINE FOR S322_S            "
*                                                                     "
         STIDP CPUID         WHO AM I ??                              "
         XC    CPUCMPR,CPUCMPR CLEAR AREA FOR VERSION-MODEL COMPARISON"
         MVC   ECCPUSPD,DEC100 START WITH BASE SPEED (UNITY * 100)    "
         MVC   CPUMDL,CPUMODEL PUT MODEL IN PLACE                     "
         MVC   CPUVER,CPUVERS  PUT VERSION IN PLACE                   "
         CLC   CPUCMPR,CPUBASE NORMALIZATION REQUIRED ??              "
         BE    INITEXIT      B IF NOT REQUIRED (WE ARE SLOWEST)       "
*                                                                     "
         DROP  R1            ASSEMBLER KNOWS, NOW I DO TOO            "
         L     R1,LCTJCTAD   R1 ---> A(JCT)                           "
         USING IEFJCT,R1     MAKE JCT ADDRESSABLE                     "
         L     R7,JCTJMRTL   R7 ---> C(JOB TIME LIMIT IN SECONDS/100) "
         SRA   R7,8             ---> C(TIME LIMIT ONLY)               "
         CL    R7,HOURS24    TIME=1440 ?  NO TIMING ?                 "
         BNL   INITEXIT      B IF NO TIMING                           "
*                                                                     "
*                  FIND THE TABLE ENTRY                               "
*                       TO MATCH THE CPU MODEL                        "
         L     R6,=A(CPUSPEED)    R6 ---> A(SPEED TABLE)              "
         LA    R3,CPUSRCH-CPUSPEED R3 ---> L(TABLE TO BE SEARCHED)    "
         L     R4,CPUCMPR         R4 ---> C(CPU VERSION-MODEL NUMBER) "
         LA    R5,8               R5 ---> L(ONE TABLE ENTRY)          "
         LNR   R5,R5              R5 ---> NEG. OF L(ONE TABLE ENTRY)  "
*                                                                     "
INITSCAN BXH   R3,R5,INITCHEK                                         "
*                                 NOT IN TABLE --- ERROR              "
EXECUTE  EX    0,EXECUTE          S'0C3' HERE                         "
*        BAL   R14,INITERR        KILL                                "
INITCHEK C     R4,0(R3,R6)        IS THIS CORRECT TABLE ENTRY ?       "
         BNE   INITSCAN           IF NOT, CONTINUE SEARCHING TABLE    "
*                                 FROM BOTTOM UP                      "
*                                 CONTINUE IN LINE IF ENTRY FOUND     "
         LA    R3,4(R3,R6)        R3 ---> A(SPEED COEF. (*100) FOR    "
*                                 THIS MACHINE                        "
         XR    R6,R6              R6 ---> C(ZERO)                     "
         M     R6,DEC100          R6 & R7 ---> C(JOB TIME LIMIT NOW   "
*                                 IN SECONDS)                         "
*                                 WHEN MULT BY 100 WILL NOT EXCEED    "
*                                 8640000 * 100 = X'337F9800'         "
         D     R6,0(R3)           DIVIDE BY COEF. TO GET              "
*                                 NORMALIZED SPEED IN SECONDS/100     "
         SLA   R7,8               POSITION FOR STUFFING               "
         STCM  R7,B'1110',JCTJMRTL STUFF IT ---> ALL FIXED            "
         MVC   ECCPUSPD,0(R3)     SAVE CPU COEFF * 100 FOR USI & ACTRT"
*                                                                     "
*                                                             ABS070282
         DROP      R1,R2,R3
         EJECT
INITEXIT EQU       *
         BR        R10            RETURN TO CALLER
         SPACE 3
INITERR  EQU   *             HELP                             ABS070282
         OI    ERRORSW,ERRINIT   THERES TROUBLE IN RIVER CITY         "
         B     INITEXIT                                       ABS070282
         EJECT
*                                                            ABS070282×
*---------------------------------------------------------------------×
*                                                                     ×
*               CPU SPEED NORMALIZAION TABLE                          ×
*                                                                     ×
*               ALLOWS S322_S TO BE CONSISTANT                        ×
*                                                                     ×
*               FORMAT:    00       XX    XXXX  XXXXXXXX              ×
*                        ZERO  VERSION   MODEL  RELATIVE              ×
*                               NUMBER  NUMBER     SPEED              ×
*                                                DIVISOR              ×
*                                                                     ×
*               MODEL NUMBER AND VERSION NUMBER ARE RESULTS OF        ×
*               STIDP (STORE CPU ID) INSTRUCTION AND ARE MODEL        ×
*               DEPENDANT.  DOCUMENTATION (SUCH AS IT IS) IS          ×
*               FOUND IN MAINFRAMES FUNCTIONAL CHARACTERISTICS        ×
*               MANUALS.  RELATIVE SPEEDS ARE FROM IDC                ×
*               AND PUBLISHED IN COMPUTERWORLD.  THE FOLLOWING        ×
*               IS THE COMPUTATION USED TO GET THE DIVISOR.           ×
*                                                                     ×
*                SPEED OF EXECUTION MACHINE * 91                      ×
*               --------------------------------- = COEF.             ×
*                SPEED OF BASE MACHINE                                ×
*                                                                     ×
*               91 IS INVERSE OF 1.1 * 100 USED TO ADD 10%            ×
*                                                                     ×
*               USED SPEED FIGURES AS FOLLOWS:                        ×
SP3081K  EQU   (6750/18) 675/1.8 BECAUSE DIADIC                       ×
SP3081D  EQU   (4650/18)                                              ×
SP3083J  EQU   370                                                    ×
SP3083B  EQU   277                                                    ×
SP3083E  EQU   185                                                    ×
SP3033AP EQU   (4050/17) 405/1.7 BECAUSE AP-MP                        ×
SP3033U  EQU   237                                                    ×
SP01683  EQU   118                                                    ×
SP01681  EQU   109                                                    ×
SP01583  EQU   45                                                     ×
*P5880   EQU   (10850/18) 1085/1.8 BECAUSE MP                         ×
*P5860   EQU   620                                                    ×
*PV/8    EQU   310                                                    ×
*PV/7    EQU   255                                                    ×
*                                                                     ×
I11      EQU   91    INVERSE OF 1.1*100 FOR ADDING 10%                ×
SPBASE   EQU   SP01583 158-3 IS BASE MACHINE                          ×
         EJECT                                                        ×
         CNOP  0,8                                                    ×
CPUSPEED EQU   *                                                      ×
CPU3081K DC    X'00',C'K',X'3081',A((SP3081K*I11)/SPBASE)             ×
CPU3081D DC    X'00',C'D',X'3081',A((SP3081D*I11)/SPBASE)             ×
CPU3083J DC    X'00',C'J',X'3083',A((SP3083J*I11)/SPBASE)             ×
CPU3083B DC    X'00',C'B',X'3083',A((SP3083B*I11)/SPBASE)             ×
CPU3083E DC    X'00',C'E',X'3083',A((SP3083E*I11)/SPBASE)             ×
CPU3033  DC    X'00',X'00',X'3033',A((SP3033U*I11)/SPBASE)            ×
*PU16881 DC    X'00',X'81',X'3062',A((SP01683*I11)/SPBASE)            ×
CPU16801 DC    X'00',X'01',X'0168',A((SP01683*I11)/SPBASE)            ×
*PU16800 DC    X'00',X'00',X'3062',A((SP01681*I11)/SPBASE)            ×
CPU41583 DC    X'00',X'04',X'0158',A((SPBASE*100)/SPBASE)             ×
*                                    ONE 0158 PROCESSOR HAS VERSION 03×
*                                    OTHER HAS VERSION 04.            ×
*                                    BOTH ARE SAME SPEED.             ×
CPUSRCH  EQU   *                     SEARCH BEGINS HERE GOING UP      ×
CPU31583 DC    X'00',X'03',X'0158',A((SPBASE*100)/SPBASE) NO 10% FUDGE×
*                                    FOR ARITHMETIC CONVENIENCE       ×
*                                    ALL RELATIVE SPEED DIVISORS ARE  ×
*                                    100 TIMES THEIR INTRINSIC VALUE  ×
*                                                                     ×
         ORG   *-8                                                    ×
CPUBASE  EQU   *                     SLOWEST CPU WHICH OTHER MACHINES ×
         ORG   ,                     WILL BE NORMALIZED TO            ×
*                              NOTE: BASE (SLOWEST) MACHINE GOES      ×
*                                    BETWEEN CPUSRCH AND CPUBASE      ×
*                                                                     ×
*---------------------------------------------------------------------×
*                                                                     ×
DEC100   DC    1F'100'               FOR DECIMAL CONVENIENCE          ×
HOURS24  DC    A(24*60*60*100)       1440 MINUTES IN HUNDREDTHS       ×
*                                    OF SECONDS                       ×
*---------------------------------------------------------------------×
*                                                            ABS070282×
         LTORG
         DROP      R11
         TITLE     'IEFUJI - ENQUEUE TEST AND REQUEUE RTN'
ENQRTN   DS        0H
**********************************************************************
*
*        ENQUEUE TEST & JOB REQUEUE ROUTINE
*
*********************************************************************
         USING     ENQRTN,R11
         LA        R15,0          SET RC=0 INITIALLY
         ST        R15,RETCODE
         XC        ENQECB,ENQECB  CLEAR OUT ECB
         MVI       ENQMACRO,X'C0' SET END-OF-LIST
         MVI       ENQMACRO+2,X'44' SET OPTIONS TO ECB=&SCOPE=SYSTEMS
         MVI       ENQMACRO+3,X'00' SET RC= FIELD TO ZERO
         MVC       ENQRNAME(8),=C'RESOURCE' DO ENQ TO SERIALIZE
         MVC       ENQQNAME(8),=C'REQUEUER'
         ENQ       (ENQQNAME,ENQRNAME,E,8,SYSTEM),ECB=ENQECB,          X
               MF=(E,ENQMACRO)
         LTR       R15,R15        DO WE HAVE RESOURCE?
         BZ        ENQDOENQ       BIF YES; WE HAVE RESOURCE
         LA        R2,60          SET COUNTER TO 60
         SR        R1,R1
         IC        R1,ENQMACRO+3  GET RC VALUE
         B         ENQWTG0(R1)    USE FOR WTG TABLE
ENQWTG0  EQU       *
         BAL       R14,ENQERROR   RC=0; UNEXPECTED
         B         ENQWAIT        RC=4; WAIT FOR ECB POST
         BAL       R14,ENQERROR   RC=8; ALREADY HAVE RES.
         BAL       R14,ENQERROR   RC=12; UNEXPECTED
         BAL       R14,ENQERROR   RC=16; UNEXPECTED
         BAL       R14,ENQERROR   RC=20; PREV. REQ. MADE
ENQWAIT  EQU       *
         L         R1,=A(120)     WE WILL WAIT UP TO 2 MINUTES FOR OK
         BAL       R8,SETDIE      SET UP SAFETY VALVE INTERVAL
         B         ENQSDOK1       RC=0; DIE ESTABLISHED OK
         B         ENQFAIL1       RC=4; FAILED TO ESTABLISH DIE
ENQFAIL1 EQU       *
         BAL       R8,PURGEDIE
         BAL       R14,ENQERROR   SIGNAL ERROR
*
ENQSDOK1 EQU       *
         ST        R1,ENQHTQE     HOLD ONTO TQE(ET AL) WORK AREA PTR
         USING     TQE,R1         MAKE TQE WORK AREA ADDRESSABLE
         LA        R1,DIEECB      GET ADDR OF TIMER POP ECB
         DROP      R1
         ST        R1,ECBLIST1    STOW FOR WAIT MULTIPLE
         LA        R1,ENQECB
         ST        R1,ECBLIST2
         MVI       ECBLIST2,X'80' END-OF-LIST
         WAIT      1,ECBLIST=ECBLIST0  WAIT ON ENQ & TIMER POP
         TM        ENQECB,X'40'   DO WE HAVE THE RESOURCE NOW?
         BO        ENQHAVIT       BIF YES; DO JOB'S DSNAMES NOW
         WTO       'IEFUJI-04-I - REQUEUER SYSTEM SUSPENDED IN LONG-TERX
               M WAIT',ROUTCDE=2,DESC=6
         L         R1,ENQHTQE     GET ADDR OF TQE
         BAL       R8,PURGEDIE    GET RID OF DIE & ASSOC. STORAGE
         B         ENQDEQ          NOW LEAVE QUIETLY
ENQHAVIT EQU       *
         L         R1,ENQHTQE
         BAL       R8,PURGEDIE
ENQDOENQ EQU       *
********************************************************************
*        SEE IF REQUEUER TASK IS ACTIVE; IF NOT, END QUIETLY
********************************************************************
         AIF    ('&SYSPARM' NE 'SP13' AND '&SYSPARM' NE 'SP12').PRESP3B
         L         R3,GETPARM     GET CURRENT SIZE OF WORK AREA
         LA        R3,0(,R3)      CLEAR HI-BYTE
         LA        R2,WORKEND-EXITCORE GET SIZE OF AREA IN ACTIVE USE
         SR        R3,R2          CALC SIZE OF AREA NOT IN USE
         SRL       R3,3           ROUND DOWN TO NEXT DOUBLEWORD SIZE
         SLL       R3,3
         LA        R2,WORKEND+7   FIND NEXT DOUBLEWORD BOUNDARY
         SRL       R2,3
         SLL       R2,3
         ST        R2,RIBADDR     STOW FOR USE LATER
         GQSCAN    AREA=((R2),(R3)),                                   *
               SCOPE=LOCAL,                                            *
               REQLIM=MAX,                                             *
               RESNAME=(REQNAME),                                      *
               MF=(E,ENQGQSCN)
         B         ENQWTG1(R15)   USE R15 FOR WTG TABLE
ENQWTG1  EQU       *
         B         ENQFNDRB       RC=0; FIND REQUEUER ANCHOR
         BAL       R14,ENQERROR   RC=4; NO RNAME VALUES-MUST BE 1ORMORE
         BAL       R14,ENQERROR   RC=8; GQSCAN REPLY AREA TOO SMALL
         BAL       R14,ENQERROR   RC=12; ABNORMAL CONDITION
         BAL       R14,ENQERROR   RC=16; INVALID SYSNAME VALUE
ENQFNDRB EQU       *
         LR        R2,R0          PICK APART VALUES RETURNED BY GQSCAN
         SLL       R2,16          EXTRACT LENGTH OF AN RIBE
         SRL       R2,16
         LR        R3,R0
         SRL       R3,16          EXTRACT LENGTH OF RIB FIXED PORTION
         L         R5,16(0,0)     GET ADDR OF CVT
         USING     CVTMAP,R5      MAKE IT ADDRESSABLE
         L         R5,CVTSMCA     GET ADDR OF SMCA
         USING     SMCABASE,R5
         L         R4,RIBADDR
ENQCHK01 EQU       *
         USING     RIB,R4
         L         R7,RIBNRIBE    GET # OF RIBE'S FOR THIS RIB
         LH        R0,RIBVLEN     GET SIZE OF VARIABLE RIB PORTION
         AR        R4,R3          BUMP TO VARIABLE PORTION
         USING     RIBVAR,R4      MAKE IT ADDRESSABLE
         CLC       SMCASID(4),RIBRNAME IS THIS THE CORRECT RIB FOR US?
         BE        ENQANCH1       BIF YES
         SR        R6,R6          OTHERWISE, BUMP TO NEXT RIB
         MR        R6,R2          COMPUTE AREA TAKEN BY CURRENT RIBE'S
         AR        R4,R7          ADD SIZE TAKEN BY RIBE'S
         AR        R4,R0          ADD SIZE TAKEN BY VARIABLE PORTION
         BCT       R1,ENQCHK01    LOOP UNTIL ALL RIB'S ARE CHECKED
         B         ENQDEQ         OTHERWISE, LEAVE QUIETLY IF UNFOUND
*
ENQANCH1 EQU       *
         L         R1,RIBRNAME+4  GET POINTER TO CSA-BASED POINTER
         ST        R1,ENQANCHR    STOW FOR USE LATER
         DROP      R4,R5
         AGO       .RESUMEB
.PRESP3B ANOP
         L         R1,16(0,0)     GET CVT ADDRESS
         USING     CVTMAP,R1      MAKE IT ADDRESSABLE
         L         R1,CVTFQCB     GET ADDR OF QCB CHAIN BEGIN
         USING     QCB,R1         MAKE IT ADDRESSABLE
ENQLOOP7 EQU       *
         CLC       MAJNAME(8),=C'REQUEUER' DO WE HAVE QNAME?
         BE        ENQRQHIT       BIF YES; LOOK FOR RNAME
         L         R1,MAJNMAJ     OTHERWISE, LOOK FURTHER
         LTR       R1,R1          ARE THERE ANY MORE?
         BNZ       ENQLOOP7       BIF YES; LOOK FURTHER
         BAL       R14,ENQERROR   ERROR; AT LEAST QNAME WILL BE PRESENT
ENQRQHIT EQU       *
         L         R1,MAJFMIN     GET A(1ST MINOR) FOR REQUEUER
         USING     MIN,R1
         L         R2,16(0,0)     GET CVT ADDRESS
         USING     CVTMAP,R2      MAKE IT ADDRESSABLE
         L         R2,CVTSMCA     GET ADDR OF SMCA
         USING     SMCABASE,R2    MAKE IT ADDRESSABLE
ENQLOOP8 EQU       *
********************************************************************
*        FIND ANCHOR PROVIDED BY REQUEUER TASK
********************************************************************
         CLC       MINNAME(4),SMCASID  ANCHOR POINTER FOR OUR CPU?
         BE        ENQANCH1       BIF EQUAL; WE HAVE ANCHOR
         L         R1,MINNMIN     GET NEXT MINOR NAME
         LTR       R1,R1          ARE THERE NO MORE?
         BNZ       ENQLOOP8       BIF THERE ARE MORE
         B         ENQDEQ         IF NO HITS, REQUEUER INACTIVE -
ENQANCH1 EQU       *
***********************************************************************
*        THE MINOR NAME ENQ'ED ON BY THE REQUEUER TASK
*        CONTAINS THE SMF SYSTEM ID AND THE ADDR OF
*        AN AREA SET ASIDE IN THE CSA BY THE REQUEUER
*        TASK FOR CROSS-ADDRESS SPACE COMMUNICATION
***********************************************************************
         L         R1,MINNAME+4   GET ADDR OF THIS ANCHOR POINTER
         ST        R1,ENQANCHR
         DROP      R1,R2
.RESUMEB ANOP
         L         R1,LCTPARM1    GET PTR TO ENQ PARM LIST
         LA        R2,12(,R1)     SKIP PAST PART OF ENQ LIST HDR
ENQLOOP1 EQU       *
         LA        R2,12(,R2)     ADVANCE TO NEXT LIST ENTRY
         OI        2(R2),X'03'    CHANGE REQUEST TO 'RET=TEST'
         MVI       3(R2),X'00'    SET RETURN CODE TO ZERO
         TM        0(R2),X'80'    IS THIS END OF LIST?
         BZ        ENQLOOP1       BIF NOT; CONTINUE
         LA        R5,24(,R1)     SKIP PAST UNUSED PORTION OF PARM
         LA        R15,0          SET RC=0
         ST        R15,RETCODE
         LM        R2,R3,=C'DCMGLOBL'  PUT IN DCM PASSWORD
         LA        R4,DCMWORK     GET ADDR OF DCM WORK AREA
         ENQ       (),MF=(E,(5))  ENQ RET=TEST
         LTR       R15,R15        ARE DSN'S AVAILABLE?
         BZ        ENQCLNUP       BIF YES; NO DSN'S FLAGGED
         SPACE     2
         AIF    ('&SYSPARM' NE 'SP13' AND '&SYSPARM' NE 'SP12').PRESP3C
ENQOWNER EQU       *
***********************************************************************
*        ASK GQSCAN FOR INFO ON RESOURCE HOLDER
*           . ASID
*           . JOBNAME/SESSION NAME/TASK NAME
***********************************************************************
         L         R1,LCTPARM1    GET ENQ PARM LIST POINTER FROM LCT
         LA        R2,12(,R1)     SKIP PAST PART OF HEADER
ENQLOOP2 EQU       *
         LA        R2,12(,R2)     SKIP TO NEXT ENTRY IN ENQ PARM LIST
         CLI       3(R2),X'00'    WAS THIS DSN IN CONFLICT?
         BNE       ENQHIT         BIF YES; NON-ZERO RETURN CODE
         TM        0(R2),X'80'    END OF ENQ PARM LIST?
         BZ        ENQLOOP2       BIF NOT; KEEP ON TRUCKIN'
         BAL       R14,ENQERROR   OTHERWISE, R15 LIED TO US.
ENQHIT   EQU       *
         L         R3,8(,R2)      GET ADDR OF RNAME
         SLR       R4,R4
         IC        R4,1(,R2)      GET LENGTH OF RNAME
         S         R4,=F'1'       LENGTH -> LENGTH CODE
         BNM       ENQDSOK        BIF LENGTH CODE IS NON-NEGATIVE
         BAL       R14,ENQERROR
ENQDSOK  EQU       *
         MVI       WORKDSN,C' '   BLANK OUT RECEIVING AREA
         MVC       WORKDSN+1(L'WORKDSN-1),WORKDSN
         EX        R4,ENQMVC      MOVE RNAME (DSNAME) TO WORK AREAS
         MVC       ENQDSN,WORKDSN
         CLI       DCMOWNJ#,X'00' DID DCM DO OUR WORK FOR US?
         BNE       ENQDCM00       BIF YES; DATA IS IN PARM AREA
         LA        R4,1(,R4)      RESTORE LENGTH FROM LENGTH CODE
         L         R2,4(,R2)      GET ADDR OF QNAME ('SYSDSN')
         L         R6,GETPARM     GET TOTAL WORK AREA LENGTH
         LA        R6,0(,R6)
         LA        R5,WORKEND-EXITCORE GET AMT IN ACTIVE USE
         SR        R6,R5          COMPUTE SIZE OF AREA LEFT OVER
         SRL       R6,3           ROUND DOWN TO NEXT DOUBLEWORD
         SLL       R6,3
         LA        R5,WORKEND+7   GET ADDR OF CURRENT ACTIVE AREA END
         SRL       R5,3           ROUND DOWN TO NEXT DOUBLEWORD
         SLL       R5,3
         ST        R5,RIBADDR     WE'LL NEED THIS LATER
         GQSCAN    AREA=((R5),(R6)),                                   *
               SCOPE=LOCAL,                                            *
               REQLIM=MAX,                                             *
               RESNAME=((R2),(R3),(R4)),                               *
               MF=(E,ENQGQSCN)
         B     ENQWTG2(R15)       USE RETURN CODE FOR WTG TABLE
ENQWTG2  EQU       *
         B         ENQQNHIT       RC=0;GOT A HIT ON OUR QNAME (DSNAME)
         B         ENQCLNUP       RC=4; NOTHING FOUND-CONFLICT RESOLVED
         BAL       R14,ENQERROR   RC=8; RESPONSE AREA IS TOO SMALL
         BAL       R14,ENQERROR   RC=12; ABNORMAL CONDITION
         BAL       R14,ENQERROR   RC=16; SYSNAME VALUE IS INVALID
*
ENQMVC   MVC       WORKDSN(0),0(R3) PATTERN MVC INSTRUCTION
*
ENQQNHIT EQU       *
         LR        R2,R0          PICK APART PARMS PASSED FROM GQSCAN
         SLL       R2,16
         SRL       R2,16          GET LENGTH OF AN RIBE
         LR        R3,R0
         SRL       R3,16          GET LENGTH OF RIB FIXED PORTION
         L         R4,RIBADDR     GET ADDR OF 1ST RIB
         USING     RIB,R4         MAKE FIXED PORTION ADDRESSABLE
         L         R5,RIBNRIBE    GET # OF RIBE'S ASSOC WITH THIS RIB
         AH        R4,RIBVLEN     ADD SIZE OF VAR-LENGTH PORTION
         AR        R4,R3          GET POINTER TO 1ST RIBE
         USING     RIBE,R4        MAKE RIBE ADDRESSABLE
ENQRBTST EQU       *
         TM        RIBESFLG,RIBESTAT DOES THIS RIBE OWN RESOURCE?
         BO        ENQRBHIT       BIF YES; WE HAVE CORRECT RIBE
         LA        R4,0(R2,R4)    BOUNCE TO NEXT RIBE
         BCT       R5,ENQRBTST    LOOP UNTIL NO MORE RIBE'S
         BAL       R14,ENQERROR   WADDYA MEAN, NOBODY OWNS IT?
ENQRBHIT EQU       *
         MVC       OWNASID(2),RIBEASID MOVE IN ASID OF OWNER
         DROP      R4
         L         R1,16(0,0)     GET CVT ADDRESS
         USING     CVTMAP,R1      MAKE IT ADDRESSABLE
         L         R1,CVTASVT     GET ADDR OF ADDRESS SPACE VECTOR TAB
         USING     ASVT,R1        MAKE IT ADDRESSABLE
         LA        R2,ASVTFRST
         DROP      R1
         LH        R1,OWNASID     GET OWNER'S ASID
         SLL       R1,2           MULT BY 4 FOR OFFSET
         AR        R2,R1          CALC ADD OF OUR ASCB'S
         TM        0(R2),X'80'    IS THIS ASCB IN USE?
         BZ        ENQASDOK       BIF YES
         BAL       R14,ENQERROR   BIF NOT; ERROR
ENQASDOK EQU       *
         L         R1,0(,R2)      GET ASCB ADDR OF OWNER
         ST        R1,OWNASCBA    HOLD ONTO IT FOR LATER
         B         ENQMYWAY       MUST NOW COMPLETE DISCOVERY OURSELVES
         AGO       .RESUMEC
.PRESP3C ANOP
ENQLOCLK SETLOCK   OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,             X
               RELATED=(ENQLOCRL,'RELEASE LOCAL LOCK')
ENQSETLK SETLOCK   OBTAIN,TYPE=CMS,MODE=UNCOND,REGS=USE,               X
               RELATED=(ENQFRELK,'RELEASE CMS LOCK')
ENQOWNER EQU       *
**********************************************************************
*        THE FOLLOWING CODE EXTRACTS INFORMATION ON THE OWNER
*        OF THE RESOURCE, INCLUDING:
*            .JOBNAME/TASKNAME
*            .JOB TYPE (STC/TSU/JOB)
**********************************************************************
         L         R1,LCTPARM1    GET ENQ PARM LIST
         LA        R2,12(,R1)     SKIP PAST 1ST PORTION OF LIST
ENQLOOP2 EQU       *
*********************************************************************
*        LOOK FOR DSNAME INVOLVED IN CONTENTION
*********************************************************************
         LA        R2,12(,R2)     BOUNCE TO NEXT ENQ LIST ENTRY
         CLI       3(R2),X'00'    ZERO RETURN CODE?
         BNE       ENQHIT         BIF WE FOUND OFFENDER
         TM        0(R2),X'80'    END-OF-LIST?
         BZ        ENQLOOP2       BIF NOT; CONTINUE SEARCH
         BAL       R14,ENQRLSLK   ERROR: RELEASE CMS LOCK BEFORE EXIT
ENQHIT   EQU       *
********************************************************************
*        GET QNAME (SYSDSN) ADDR, RNAME (DSN) ADDR, RNAME LENGTH &
*        BEGINNING-OF-QCB CHAIN POINTER
*******************************************************************
         L         R3,8(,R2)      GET ADDR OF RNAME
         SLR       R4,R4
         IC        R4,1(,R2)      GET LENGTH OF RNAME (DSNAME)
         S         R4,=F'1'       LENGTH -> LENGTH CODE
         BNM       ENQDSNOK       BIF DSN LENGTH NOT INVALID
         BAL       R14,ENQRLSLK   ERROR: RELEASE CMS LOCK BEFORE EXIT
ENQDSNOK EQU       *
         CLI       DCMOWNJ#,X'00'  IS MSI-DCM ACTIVE?
         BNE       ENQRNHIT        BIF YES
         L         R2,4(,R2)      GET ADDR OF QNAME
         L         R1,16(0,0)     GET ADDR OF CVT
         USING     CVTMAP,R1      GIVE IT ADDRESSABILITY
         L         R1,CVTFQCB     GET FIRST QCB ON CHAIN
         USING     QCB,R1
ENQLOOP3 EQU       *
********************************************************************
*        FIND MAJOR QCB, OR QNAME (SYSDSN), WHICH MATCHES OURS
********************************************************************
         CLC       MAJNAME,0(R2)  IS THIS THE CORRECT QNAME?
         BE        ENQQNHIT       BIF YES; ON TO OTHER THINGS
         L         R1,MAJNMAJ     GET ADDR OF NEXT MAJOR
         LTR       R1,R1          END OF MAJOR QCB NAME LIST?
         BNZ       ENQLOOP3       BIF NOT; CONTINUE W/ NEXT
         BAL       R14,ENQRLSLK   ERROR: RELEASE CMS LOCK BEFORE EXIT
ENQQNHIT EQU       *
         L         R1,MAJFMIN     GET A(1ST MINOR) FOR THIS MAJOR
         USING     MIN,R1         GIVE MINOR QCB ADDRESSABILITY
ENQLOOP4 EQU       *
*********************************************************************
*        LOOK FOR MATCH ON RNAME (DSNAME)
*********************************************************************
         EX        R4,ENQCLCMN    COMPARE MINOR NAMES
         BE        ENQRNHIT       BIF WE GOT A HIT ON RNAME
         L         R1,MINNMIN     GET ADDR OF NEXT MINOR (QNAME)
         LTR       R1,R1          END OF LIST?
         BNZ       ENQLOOP4       BIF NOT; CONTINUE LOOPING
         BAL       R14,ENQRLSLK   ERROR: RELEASE CMS LOCK BEFORE EXIT
ENQCLCMN CLC       MINNAME(0),0(R3) PATTERN CLC INSTR
ENQMVC   MVC       WORKDSN(0),0(R3)
ENQRNHIT EQU       *
*********************************************************************
*        GET QEL ASSOCIATED WITH RNAME; GET ASSOC. ASID
*        ASCB ADDRESS, TCB ADDRESS FOR TCB UNDER THAT ASCB, &
*        JOBNAME/TASK NAME (IF STARTED TASK)
*********************************************************************
         MVI       WORKDSN,C' '   BLANK OUT WORK DSN
         MVC       WORKDSN+1(L'WORKDSN-1),WORKDSN
         EX        R4,ENQMVC      MOVE DSNAME TO OUR WORK AREA
         MVC       ENQDSN,WORKDSN HOLD FOR LATER
         CLI       DCMOWNJ#,X'00'  IS MSI-DCM ACTIVE?
         BNE       ENQFRELK        BIF YES
         L         R1,MINFQEL     GET PTR TO 1ST(OWNING) QEL
         USING     QEL,R1
         TM        QELTCB,QELXLIST TCB OR LIST QEL ADDR?
         BZ        ENQQELOK       BIF TCB
         L         R1,QELLQEL     IF LIST QEL, POINT TO QEL PROPER
ENQQELOK EQU       *
         MVC       OWNASID(2),QELASID   GET ASID OF RESOURCE HOLDER
         DROP      R1
         L         R1,16(0,0)     GET CVT ADDRESS
         USING     CVTMAP,R1      MAKE IT ADDRESSABLE
         L         R1,CVTASVT     GET ADDR OF ADDR SPACE VECTOR TABLE
         USING     ASVT,R1        MAKE ASVT ADDRESSABLE
         LA        R2,ASVTFRST    GET ADDR OF ADDR SPACE STACK
         LH        R1,OWNASID     TO OBTAIN OFFSET INTO TABLE
         SLL       R1,2           MULT BY 4
         AR        R2,R1          CALC ADDR OF OWNER'S ENTRY
         TM        0(R2),X'80'    OWNER'S ENTRY SHOULD BE ASSIGNED
         BZ        ENQASDOK       BIF ENTRY IS ASSIGNED
         BAL       R14,ENQRLSLK   ERROR: RELEASE CMS LOCK BEFORE EXIT
ENQRLSLK EQU       *
*********************************************************************
*        AN ERROR HAS BEEN DETECTED WHILE SEARCHING QCB CHAINS:
*        HOLD ONTO REGS DESTROYED BY 'SETLOCK RELEASE', THEN
*        RELEASE THE CMS LOCK BEFORE CONTINUING WITH ERROR SIGNALLING
********************************************************************
         LR        R5,R14         HOLD ONTO ERROR ADDRESS LOCATOR
         LR        R6,R1          HOLD ONTO POINTER TO RELEVANT DATA
ENQERRLK SETLOCK   RELEASE,TYPE=CMS,REGS=USE,RELATED=(ENQSETLK,'ERROR')
ENQELCRL SETLOCK   RELEASE,TYPE=LOCAL,REGS=USE,RELATED=(ENQLOCLK,'A')
         BAL       R14,ENQERROR   CONTINUE W/ NORMAL ERROR SIGNALLING
ENQASDOK EQU       *
         L         R1,0(,R2)      GET ADDR OF OWNER'S ASCB
         ST        R1,OWNASCBA    STOW OWNER'S ASCB ADDR
ENQFRELK SETLOCK   RELEASE,TYPE=CMS,REGS=USE,RELATED=(ENQSETLK,'FREE')
ENQLOCRL SETLOCK   RELEASE,TYPE=LOCAL,REGS=USE,RELATED=(ENQLOCLK,'B')
         CLI       DCMOWNJ#,X'00' DID DCM PLACE INFO IN AREA?
         BE        ENQMYWAY       BIF NOT; WE MUST DO IT OURSELVES
.RESUMEC ANOP
ENQDCM00 EQU       *
**********************************************************************
*        'DCM' A FACILITY PROVIDED BY THE 'MULTIPLE-SYSTEM-INTEGRITY'
*        PACKAGE OF ALLEN SERVICES, PROVIDES INFORMATION ON THE
*        JOB NAMES AND JES IDS OF THE OWNERS AND REQUESTORS OF A DSNAME
*        THE CODE WHICH IMMEDIATELY FOLLOWS USES THE INFORMATION
*        PROVIDED BY THE DCM FACILITY, IF AVAILABLE.  IF DCM (MSI)
*        IS NOT IN THE SYSTEM, THE REQUEUER REVERTS BACK TO ITS
*        OWN METHOD OF DISCOVERING THE JOBNAMES AND JESIDS OF
*        JOBS INVOLVED IN A DSNAME CONFLICT.
**********************************************************************
         CLC       DCMOWNJN,=C'*UNKNOWN'   DCM GOT IN TOO LATE?
         BE        ENQCLNUP BIF YES: DCM ENTERED AFTER CONFLICT OK
         MVC       OWNJOBNM,DCMOWNJN MOVE CONFLICTOR'S JOBNAME
         MVC       REQJOBNM,DCMVICJN MOVE VICTIM'S JOBNAME
         CLI       DCMOWNJ#,C'J' IS CONFLICTOR A BATCH JOB?
         BNE       ENQDCM01       BIF NOT
         MVI       OWNJOBTP,OWNJOB MARK CONFLICTOR AS A BATCH JOB
         B         ENQDCM04       GO DOWN TO PROCESS JES JOB #
ENQDCM01 EQU       *
         CLI       DCMOWNJ#,C'T'  IS CONFLICTOR A TSO SESSION?
         BNE       ENQDCM02       BIF NOT; TRY SYSTEM TASK
         MVI       OWNJOBTP,OWNTSU MARK CONFLICTOR AS A TSO SESSION
         B         ENQDCM04       GO DOWN TO PROCESS JES JOB #
ENQDCM02 EQU       *
         CLI       DCMOWNJ#,C'S'  IS CONFLICTOR A SYSTEM TASK?
         BE        ENQDCM03       BIF YES
         BAL       R14,ENQERROR   OTHERWISE, ERROR
ENQDCM03 EQU       *
         MVI       OWNJOBTP,OWNSTC MARK CONFLICTOR AS A SYSTEM TASK
ENQDCM04 EQU       *
         CLI       DCMOWNJ#+7,C'0' DID DCM PROVIDE JES JOB #
*          (DCM WILL NOT IF CONFLICTOR IS RUNNING UNDER SECONDARY JES)
         BL        ENQDCM05     BIF CONFLICTOR'S JES JOB # NOT PRESENT
         PACK      WORK01,DCMOWNJ#+4(4) CNVRT JES JOB #
         CVB       R3,WORK01
         STH       R3,OWNJES#
ENQDCM05 EQU       *
         CLI       DCMVICJ#+7,C'0' DID DCM PROVIDE VICTIM' JES #
         BL        ENQDCM06       BIF NOT
         PACK      WORK01,DCMVICJ#+4(4)
         CVB       R3,WORK01
         STH       R3,REQJES#
ENQDCM06 EQU       *
         B         ENQPROC        NOW PROCESS CONFLICTOR/VICTIM
ENQMYWAY EQU       *
**********************************************************************
*        IF THE MSI FACILITY CALLED DCM PROVIDES NO INFORMATION ON
*        THE JOBS INVOLVED IN A DSNAME CONFLICT, THEN THE REQUEUER
*        MUST DISCOVER SUCH INFORMATION ON ITS OWN.  THE CODE BELOW
*        PERFORMS THIS FUNCTION
*********************************************************************
         L         R1,OWNASCBA
         USING     ASCB,R1        MAKE IT ADDRESSABLE
         L         R2,ASCBOUCB    GET ADDR SRM CONTROL BLOCK
         USING     OUCB,R2        GIVE IT ADDRESSABILITY
         TM        OUCBYFL,OUCBLOG IS THIS A TSO USER?
         DROP      R2
         BZ        ENQTRSTC       BIF NOT; OWNER IS NOT A TSO SESSION
**********************************************************************
*        OWNER IS IDENTIFIED AS A TSO SESSION;
*********************************************************************
         OI        OWNJOBTP,OWNTSU  MARK OWNER AS A TSO SESSION
         L         R2,ASCBJBNS    GET TSO USER ID ADDR
         DROP      R1
         MVC       OWNJOBNM(8),0(R2)
         B         ENQSSI         NOW TALK TO JES ABOUT THIS GUY
*
ENQTRSTC EQU       *
         L         R1,OWNASCBA    GET ADDR OF RES. OWNER'S ASCB
         USING     ASCB,R1        GIVE IT ADDRESSABILITY
         L         R2,ASCBJBNI    GET ADDR OF POSS. JOBNAME
         LTR       R2,R2          IS THIS A BATCH JOB?
         BNZ       ENQTRJOB       BIF OWNER IS A BATCH JOB
*
         L         R2,ASCBJBNS    GET ADDR OF STC NAME
         OI        OWNJOBTP,OWNSTC  MARK OWNER AS A SYSTEM TASK
         MVC       OWNJOBNM(8),0(R2)  GET NAME OF SYSTEM TASK
         B         ENQSSI
*
ENQTRJOB EQU       *
         OI        OWNJOBTP,OWNJOB RESOURCE HOLDER MUST BE A BATCH JOB
         L         R2,ASCBJBNI    ADDR OF JOBNAME
         DROP      R1
         MVC       OWNJOBNM(8),0(R2) GET NAME OF BATCH JOB
*********************************************************************
*        THE JES JOB # OF THE RESOURCE OWNER (CONFLICTOR)
*        MUST BE DETERMINED. USING THE SUBSYSTEM INTERFACE (DOCUMENTED
*        IN SPL:MVS DIAGNOSTIC TECHNIQUES, OF ALL PLACES) A STATUS
*        REQUEST IS PUT TOGETHER USING THE OWNER'S JOBNAME.
*        AFTER THE SUBSYSTEM (JES2/JES3) RETURNS CONTROL, THE
*        REPLY LIST IS SCANNED LOOKING FOR A JOB CURRENTLY EXECUTING;
*        THAT IS THE RESOURCE OWNER.  A CAUTION TO DEVELOPERS/
*        MAINTAINERS: THE REPLY AREA IS A FIXED LENGTH (ALBEIT SOMEWHAT
*        LARGE) - IF THERE ARE A LARGE NUMBER OF JOBS IN THE SYSTEM
*        WITH THE SAME NAME AS THE RESOURCE OWNER, THE SUBSYSTEM
*        INTERFACE MAY WELL CRAP OUT ON YOU.
*********************************************************************
ENQSSI   EQU       *
         MVC       ENQSSOB+(SSOBID-SSOB)(4),=C'SSOB' FORMAT SSOB
         LA        R1,SSOBHSIZ
         STH       R1,ENQSSOB+(SSOBLEN-SSOB)
         LA        R1,SSOBSTAT    GET 'JOB STATUS' FUNCTION CODE
         STH       R1,ENQSSOB+(SSOBFUNC-SSOB)
         LA        R1,ENQSSOB+(SSOBGN-SSOB) ADDR OF FUNC. EXTENSION
         ST        R1,ENQSSOB+(SSOBINDV-SSOB)
         L         R1,GETPARM      GET TOTAL LENGTH OF WORK AREA
         LA        R1,0(,R1)      CLEAR HI-BYTE
         LA        R2,WORKEND-EXITCORE GET AMT OF WORK AREA IN USE
         SR        R1,R2          CALC AMT OF AREA NOT IN USE
         BNM       ENQWRKOK       WORK AREA IS OK (NON-NEG REMAINDER)
         BAL       R14,ENQERROR   OTHERWISE, SIGNAL ERROR
ENQWRKOK EQU       *
         SRL       R1,4           ROUND DOWN TO NEXT 16-BYTE BOUNDARY
         SLL       R1,4
         LA        R2,SSCSELSZ(,R1)  GET LENGTH OF EXTENSION
         STH       R2,ENQSSOB+(SSCSLEN-SSOB) STOW IT
         LA        R2,SSCSELSZ(,R1) GET LENGTH OF ELEMENT AREA ONLY
         STH       R1,ENQSSOB+(SSCSDIMP-SSOB) STOW IT
         MVC       ENQSSOB+(SSCSJOBN-SSOB)(8),OWNJOBNM
         MVC       ENQSSOB+(SSCSJOBI-SSOB)(8),=C'        '
         LA        R1,ENQSSOB
         ST        R1,ENQSSLST
         OI        ENQSSLST,X'80' END-OF-LIST
         LA        R1,ENQSSLST
         IEFSSREQ
         L         R1,ENQSSOB+(SSOBRETN-SSOB) SUBSYSTEM RETURN CODE
         LTR       R1,R1          RC=0 (OK)?
         BZ        ENQSTOK        BIF SUBSYSTEM REQUEST OK
         BAL       R14,ENQERROR   OTHERWISE, ERROR
ENQSTOK  EQU       *
         LA        R3,ENQSSOB+(SSCSARAY-SSOB)
         LH        R1,ENQSSOB+(SSCSDIMR-SSOB) GET SIZE ACT. USED
         LA        R2,0           START SEARCH @ TABLE BEGIN
         LA        R0,SSCSELSZ   LENGTH OF ONE ELEMENT
         SR        R1,R0          REDUCE W/A LENGTH BY 1 FOR BXLE
         BNM       ENQSBROK       BIF L'(AREA RETURNED) NOT = 0
         BAL       R14,ENQERROR   OTHERWISE, ERROR
ENQSBROK EQU       *
         LA        R4,0(R2,R3)    CALC ADDR OF ELEMENT
         TM        SSCSFLG1-SSCSARAY(R4),SSCSJACT RES.HOLDER EXECUTING?
         BO        ENQEXCJB       NOW HAVE EXEC JOB W/CORRECT NAME
         BXLE      R2,R0,ENQSBROK KEEP LOOKING UNTIL YOU FIND IT
         BAL       R14,ENQERROR IF WE FALL THRU, RES. OWNER NOT EXECL
ENQEXCJB EQU       *
         PACK      WORK01(8),(SSCSARID-SSCSARAY+3)(5,R4) CONV JES JOB#
         CVB       R1,WORK01
         STH       R1,OWNJES#
         SPACE     2
ENQPROC  EQU       *
**********************************************************************
*        AT THIS POINT, EITHER DCM OR OUR OWN CODE HAS PROVIDED
*        BASIC INFORMATION ON THE CONFLICTOR (RESOURCE OWNER) AND
*        THE VICTIM (RESOURCE REQUESTOR).  ADDITIONAL INFO ON THE
*        VICTIM IS EXTRACTED FROM PARM LISTS PASSED TO THE IEFUJI
*        EXIT.
**********************************************************************
         LH        R1,OWNJES#     GET JES JOB #
         TM        OWNJOBTP,OWNJOB IS OWNER A BATCH JOB?
         BO        ENQWHORU       BIF YES; GET INFO ON OURSELVES
         TM        OWNJOBTP,OWNTSU IS OWNER A TSO SESSION?
         BZ        ENQISSTC       IF NOT, MUST BE STC
         A         R1,=A(20000)   CREATE JOB # USED FOR TSO USERS
         B         ENQWHORU       NOW GET INFO ON OURSELVES
ENQISSTC EQU       *
         A         R1,=A(10000)   CREATE JOB # USED FOR SYSTEM TASKS
ENQWHORU EQU       *
         STH       R1,OWNJES#     STOW (MODIFIED) JES JOB #
**********************************************************************
*        GET INFO ON RESOURCE REQUESTOR (OURSELVES)
**********************************************************************
         L         R1,16(0,0)     GET CVT ADDR
         USING     CVTMAP,R1      MAKE IT ADDRESSABLE
         L         R1,CVTTCBP     GET NEXT/CURR TCB/ASCB PTRS
         L         R1,12(,R1)     GET CURR (OUR) ASCB ADDR
         ST        R1,REQASCBA    STOW IT
         DROP      R1
         L         R1,CEPAADDR    GET COMM EXIT PARM AREA PTR
         USING     COMMEXIT,R1 MAKE IT ADDRESSABLE
         MVC       REQJOBNM,CEPJOBN JOBNAME
         MVC       REQRDRTM,CEPRDRTM READER START TIME
         MVC       REQRDRDT,CEPRDRDT READER START DATE
         MVC       REQPRTY,CEPIDPTY  HASP JOB SELECTION PRIORITY
         CLI       DCMVICJ#+7,C'0'  DID DCM PROVIDE INFO?
         BNL       ENQHVVIC         BIF YES
         MVC       REQJES#,CEPIDJES OTHERWISE, USE OUR OWN
ENQHVVIC EQU       *
         TM        OWNJOBTP,OWNSTC   IS OWNER AN STC?
         BO        ENQREQUE          IF SO, JUST REQUEUE (DON'T LOG IT)
         TM        OWNJOBTP,OWNTSU   IS OWNER A TSO SESSION?
         BZ        ENQCALLR          BIF NOT; REQUEUE W/O TSO MESSAGE
         MVC       CMDWORK(PATSND1L),PATSND1 'JOB REQUEUE' MESSAGE
         MVC       CMDWORK+12(8),REQJOBNM
         MVC       CMDWORK+53(8),OWNJOBNM
         LA        R1,CMDWORK+53     LOOK FOR FIRST BLANK
         LA        R2,1
         LA        R3,CMDWORK+60  STOP ADDR FOR BXLE
ENQLOOP5 EQU       *
         CLI       0(R1),C' '     FOUND A BLANK YET?
         BE        ENQOWNBK       BIF WE HAVE BLANK IN OWNER NAME
         BXLE      R1,R2,ENQLOOP5
ENQOWNBK EQU       *
         MVI       0(R1),C')'     PUT IN ')' OVER BLANK
         MVC       1(7,R1),=C'        ' BLANK THE REST
         LA        R1,CMDWORK
         SR        R0,R0
         SVC       34             TELL TSO USER
         MVC       CMDWORK(PATSND2L),PATSND2 'PLS FREE' MESSAGE
         MVC       CMDWORK+20(44),WORKDSN  MOVE DATA SET NAME INTO MSG
         MVC       CMDWORK+72(8),OWNJOBNM  MOVE TSO SESSION NAME
         LA        R1,CMDWORK+72  FIND LOCATION TO PLACE
         LA        R2,1           CLOSE PAREN
         LA        R3,CMDWORK+79  STOP ADDR FOR BXLE
ENQLOOP6 EQU       *
         CLI       0(R1),C' '     TRAILING BLANK?
         BE        ENQOWNBL       BIF YES
         BXLE      R1,R2,ENQLOOP6
ENQOWNBL EQU       *
         MVI       0(R1),C')'     CLOSE PAREN
         MVI       1(R1),C' '     BLANK AFTER CLOSE PAREN
         LA        R1,CMDWORK
         SR        R0,R0
         SVC       34
ENQCALLR EQU       *
         L         R1,ENQANCHR    GET ADDR OF ANCHR PTR
         USING     CROSSMEM,R1    MAKE IT ADDRESSABLE
         XC        CRMEXECB,CRMEXECB CLEAR THE ECB
         MVC       CRMEXASC,REQASCBA THIS EXIT'S ASCB ADDR
         MVC       CRMVCRTM,REQRDRTM VICTIM'S READER START TIME
         MVC       CRMVCRDT,REQRDRDT VICTIM'S READER START DATE
         SR        R2,R2
         IC        R2,REQPRTY     GET VICTIM'S JOB SEL. PRTY
         STH       R2,CRMVCPTY
         MVC       CRMVCJES,REQJES#  VICTIM'S JES JOB NUMBER
         MVC       CRMVCNAM,REQJOBNM PUT VICTIM'S JOBNAME IN CSA
         MVC       CRMONJES,OWNJES#  OWNER'S (CONFLICTOR'S) JES JOB #
         MVC       CRMONNAM,OWNJOBNM PUT OWNER'S JOBNAME IN CSA
         MVI       CRMFLAGS,CRMUJI   THIS IS IEFUJI CALLING
**********************************************************************
*        USING THE POINTER FROM THE REQUEUER/SID LIST OF ENQ,
*        THE AREA IN CSA SET ASIDE BY THE REQUEUER TASK IS FOUND.
*        INFORMATION NEEDED BY THE REQUEUER AUTO-RELEASE FACILITY
*        IS PLACED INTO THIS AREA.  A CROSS-MEMORY POST IS DONE
*        TO 'WAKE UP' THE REQUEUER TASK TO PROCESS THE REQUEST.
*        THIS TASK (IEFUJI) ISSUES A  'WAIT' MACRO TO AWAIT
*        COMPLETION OF PROCESSING BY THE REQUEUER TASK. A CAUTION TO
*        TO DEVELOPERS/MAINTAINERS: IF THE REQUEUER TASK GETS HUNG IN
*        A 'WAIT' STATE, THE TASK REQUESTING ITS SERVICES AT THAT
*        TIME IS ALSO HUNG (IEFUJI, IEFACTRT).  ONLY ONE TASK
*        WILL GET IN TROUBLE, AND OTHERS WILL BE ABLE TO RUN AROUND IT,
*        BUT NO PROVISION HAS BEEN MADE TO AUTOMATICALLY BREAK THIS
*        PROBLEM.  THE OPERATOR/SYSTEMS PROGRAMMER MUST INTERVENE.
*        AFTER IEFUJI IS POSTED BY THE REQUEUER TASK, 'CRMFLAGS'
*        IS CHECKED FOR ERRORS.
*        IF EVERYTHING IS NORMAL, THE REQUEUER TASK WILL HAVE
*        UPDATED THE RESOURCE CONTROL RECORD. IEFUJI MUST THEN
*        ISSUE AN '$HJXXXX;EJXXXX' SEQUENCE TO HOLD & REQUEUE JOB
*        THEN A RETURN CODE IS SET TO FORCE THIS JOB OUT OF
*        THE INITIATOR.
*********************************************************************
         DROP      R1
         LA        R1,WORKDSN     HASH-CODE CONFLICT DSNAME
         BAL       R8,HASHDSN
         L         R3,ENQANCHR    GET ADDR OF ANCHOR POINTER
         USING     CROSSMEM,R3    MAKE IT ADDRESSABLE
         ST        R15,CRMHDSN    STOW HASH-CODED DSNAME
         L         R4,16(0,0)     GET CVT ADDRESS
         USING     CVTMAP,R4
         L         R2,CRMRQASC    GET ADDR OF REQUEUER TASK'S ASCB
         POST      CRMRQECB,ASCB=(2),ERRET=CVTBRET,MF=(E,ENQPOST)
         WAIT      1,ECB=CRMEXECB WAIT FOR REQUEUER TO FINISH
         TM        CRMFLAGS,CRMRQERR WAS THERE AN ERROR?
         DROP      R3
         BZ        ENQREQUE       BIF NOT; REQUEUE THIS JOB
         WTO       'IEFUJI-05-I - ERROR IN REQUEUER TASK',             X
               ROUTCDE=2,DESC=6
ENQREQUE EQU       *
         MVC      CMDWORK(PATRQEL),PATREQUE MOVE IN PATTERN FOR REQUEUE
         LH        R1,REQJES#     GET VICTIM'S JES JOB #
         CVD       R1,WORK01
         ED        CMDWORK+6(5),WORK01+5
         ED        CMDWORK+13(5),WORK01+5
         MVI       CMDWORK+6,C'J'
         MVI       CMDWORK+13,C'J'
         SR        R0,R0          NO CONSOLE ID
         LA        R1,CMDWORK
         SVC       34
         STIMER    WAIT,BINTVL=ENQ3SEC WAIT 3 SEC FOR JES CMD TO TAKE
         LA        R15,4          SET RC=4 (CANCEL JOB)
         ST        R15,RETCODE
         MVC       WTOMSG1(PATWTO1L),PATWTO1 'REQUEUED' MSG
         MVC       WTOMSG1+18(8),REQJOBNM
         MVC       WTOMSG1+44(8),OWNJOBNM
         MVC       WTOMSG1+63(44),ENQDSN
         WTO       ,MF=(E,WTOMSG1)
         TM        OWNJOBTP,OWNSTC IS OWNER AN STC?
         BZ        ENQCLNUP        BIF NOT
         WTO       'IEFUJI-09-I - NOT ELIGIBLE FOR AUTO-RELEASE',      X
               ROUTCDE=2,DESC=6
*
ENQCLNUP EQU       *
         L         R1,LCTPARM1    GET ADDR OF ENQ PARM HEADER
         LA        R2,12(,R1)     SKIP PAST PART OF HEADER
ENQLOOP9 EQU       *
*********************************************************************
*        RESET ENQ LIST OPTION FLAGS TO RET=NONE,ECB=XXXX &
*        RESET RETURN CODES TO ZERO
*********************************************************************
         LA        R2,12(,R2)     SKIP TO NEXT LIST ENTRY
         NI        2(R2),X'FC'    CHANGE TO 'RET=NONE,ECB='
         MVI       3(R2),X'00'    SET RC=0
         TM        0(R2),X'80'    END OF LIST?
         BZ        ENQLOOP9       BIF NOT; CONTINUE TO LOOP
ENQDEQ   EQU       *
         MVC       ENQRNAME(8),=C'RESOURCE'
         DEQ      (ENQQNAME,ENQRNAME,8,SYSTEM),RET=HAVE,MF=(E,ENQMACRO)
ENQEXIT  EQU       *
         BR        R10            'BYE
         SPACE     1
ENQERROR EQU       *
         OI        ERRORSW,ERRENQ
         B         ENQEXIT
ENQ3SEC  DC        A(03*100)      3 SECOND  (IN 100TH OF SECONDS)
PATREQUE DC        AL2(PATRQEL),AL2(0),C'$H',X'2120202020'
         DC        C';E',X'2120202020',C'    '   'HOLD' & 'REQUEUE'
PATRQEL  EQU       *-PATREQUE     LENGTH OF THIS MESS
PATWTO1  WTO       'IEFUJI-08-I - XXXXXXXX REQUEUED BECAUSE XXXXXXXX HOX
               LDS DSN XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',  X
               ROUTCDE=2,DESC=6,MF=L
PATWTO1L EQU       *-PATWTO1
PATSND1  DC        AL2(PATSND1L),AL2(0)
         DC       C'SE ''JOB XXXXXXXX HAS BEEN REQUEUED && HELD'',USER=X
               (XXXXXXXX  '
PATSND1L EQU       *-PATSND1
PATSND2  DC        AL2(PATSND2L),AL2(0)
         DC       C'SE ''PLEASE FREE XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
               XXXXXXXXXX'',USER=(XXXXXXXX  '
PATSND2L EQU       *-PATSND2      CALC LENGTH OF THIS AREA
         LTORG
         DROP      R9,R12         MOMENTARILY DROP ADDRESSABILITY
         DROP      R11            NO LONGER NEED R11, EITHER
         TITLE     'IEFUJI - TMS(UCC-ONE) ACCOUNTING ROUTINE'
TMSRTN   DS        0H
***********************************************************************
*
*        ROUTINE TO INSERT JOB ACCOUNTING INFO INTO TMS (UCC-ONE)
*        CONTROL RECORD.
*
***********************************************************************
         USING     TMSRTN,R11     MAKE ROUTINE ADDRESSABLE
*
*
*
*
*
*
*
*
*
*
TMSRTNX  EQU       *
         BR        R10            RETURN TO MAIN ROUTINE
*
         LTORG
         DROP      R11            NO LONGER NEED R11
         TITLE     'IEFUJI - LAYOUT OF PARM LIST PASSED TO IEFUJI'
         UJIMAP
         TITLE     'IEFUJI - LAYOUT OF COMMON EXIT PARAMETER AREA'
         COMMEXIT
         TITLE     'IEFUJI - GETMAINED AREA USED BY ALL EXITS'
         EXITCORE
RETCODE  DS        A              RETURN CODE FOR CALLER
CEPAADDR DS        A              ADDR OF COMMON EXIT PARM AREA
UJIPADDR DS        A              ADDR OF PARM LIST PASSED TO UJI
SNAPREGS DS        16F            GEN REGISTER DIAGNOSTIC AREA
SNAPMACR SNAP      PDATA=(ALL),LIST=0,DCB=0,SDATA=(LSQA,SWA,CB),MF=L
SNAPLIST EQU       *              LIST OF AREAS TO BE SNAPPED
SNAPSTR1 DS        A
SNAPEND1 DS        A
SNAPSTR2 DS        A
SNAPEND2 DS        A
SNAPSTR3 DS        A
SNAPEND3 DS        A
SNAPDCB  DS        CL96           DCB USED FOR SNAP DISPLAYS
OPENLIST DS        A              LIST USED FOR OPEN MACRO
CLOSLIST DS        A              LIST USED FOR CLOSE MACRO
ERRORSW  DS        XL1            BYTE USED FOR ERROR INDICATORS
ERRINIT  EQU   X'01'              ERROR IN INTIALIZATION RTN
ERRENQ   EQU   X'02'              ERROR IN ENQ ROUTINE
ERRTMS   EQU   X'04'              ERROR IN TMS ACCT ROUTINE
WTOMSG1  WTO       'IEFUJI-08-I - XXXXXXXX REQUEUED BECAUSE XXXXXXXX HOX
               LDS DSN XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',  X
               ROUTCDE=2,DESC=6,MF=L
ENQMACRO ENQ       (),ECB=0,MF=L
WORK01   DS        D        DOUBLE WORD FOR PACKED DEC. OPERATIONS
WORKDSN  DS        CL44           HOLD AREA FOR CONTENTION DSN
ENQDSN   DS        CL44           HOLD DSN FOR MSGS
ENQECB   DS        A              ECB USED BY ENQ 'REQUEUER'/'RESOURCE'
ECBLIST0 DS        0D             ECB LIST FOR WAIT MULTIPLE
ECBLIST1 DS        A              1ST ECB
ECBLIST2 DS        A              2ND ECB
ENQHTQE  DS        A              TQE POINTER HOLD AREA
ENQANCHR DS        A              ADDR OF REQUEUER TASK CSA ANCHR
ENQHSDSN DS        A              HOLD AREA FOR HASH-ENCODED DSNAME
ENQQNAME DS        CL8            QNAME FOR REQUEUER TASK
ENQRNAME DS        CL8            RNAME FOR REQUEUER TASK
ENQSSLST DS        A              ADDR OF SSOBLIST
ENQPOST  POST      0,ASCB=0,ERRET=0,MF=L
DCMWORK  DS        0CL32          MSI DCM FACILITY WORK AREA
DCMOWNJN DS        CL8            CONFLICTOR'S JOBNAME
DCMOWNJ# DS        CL8            CONFLICTOR'S JES ID
DCMVICJN DS        CL8            VICTIM'S JOBNAME
DCMVICJ# DS        CL8            VICTIM'S JES ID
OWNJOBNM DS        D              JOBNAME OF RESOURCE OWNER
OWNASCBA DS        A              ADDR OF RESOURCE OWNER'S ASCB
OWNASID  DS        H              ASID OF RESOURCE OWNER
OWNJES#  DS        H              BINARY-CODED JES JOB # FOR RES. OWNER
OWNJOBTP DS        XL1            RESOURCE OWNER JOB TYPE
OWNJOB   EQU   X'01'              RESOURCE OWNER IS A BATCH JOB
OWNTSU   EQU   X'02'              RESOURCE OWNER IS A TSO SESSION
OWNSTC   EQU   X'04'              RESOURCE OWNER IS A STARTED TASK
REQJOBNM DS        D              JOBNAME OF VICTIM
REQASCBA DS        A              THIS EXIT'S ASCB ADDR
REQJES#  DS        H              BINARY-CODED JES JOB # FOR VICTIM
REQPRTY  DS        BL1            VICTIM'S JOB SEL. PRTY
REQRDRTM DS        F              VICTIM'S READER START TIME
REQRDRDT DS        PL4            VICTIM'S READER START DATE
CMDWORK  DS        0D
CMDLGTH  DS        H              LENGTH FIELD FOR COMMANDS
         DS        H              RESERVED
CMDTEXT  DS        CL80           TEXT AREA FOR COMMANDS
         AIF       ('&SYSPARM' NE 'SP13').RESUMED
RIBADDR  DS        A              POINTER TO GQSCAN WORK AREA
ENQGQSCN GQSCAN    AREA=(),                                            *
               SCOPE=LOCAL,                                            *
               RESNAME=(),                                             *
               MF=L
.RESUMED ANOP
ENQSSOB  DS        0D,CL(SSOBLEN2) AREA FOR SSOB & SSOB FUNC EXT
*
CPUID    DS    0D            STORE CPU ID OPERAND             ABS070282
CPUVERS  DS    BL1           WHAT ??                                  "
CPUIDN   DS    BL3           SERIAL NUMBER                            "
CPUMODEL DS    H             MODEL NUMBER (EG X'0158')                "
CPUMCELL DS    H             L(MCEL) MAX. FOR THIS MODEL              "
*                                                                     "
CPUCMPR  DS    0F            AREA FOR MODEL-VERSION COMPARE           "
         DS    BL1           ZERO                                     "
CPUVER   DS    BL1           VERSION NUMBER                           "
CPUMDL   DS    BL2           MODEL NUMBER                             "
*                                                                     "
*                                                             ABS070282
WORKEND  EQU       *
         ECFOLLOW
         END
