         MACRO
         PURGEDIE
PURGEDIE DS        0H
***********************************************************************
*
*        THIS FUNCTION CLEANS UP ANY DISABLED INTERRUPT EXITS
*        LEFT PENDING & FREEMAINS THE AREA IN SQA USED FOR THE
*        DIE & ITS WORK AREAS.  UPON ENTRY TO THE FUNCTION, R1
*        IS ASSUMED TO POINT TO THE TQE AND THE ASSOCIATED BAGGAGE:
*        (DIE ROUTINE & DIE ROUTINE WORK AREA).  SEE 'SETDIE'
*        FOR THE LAYOUT OF THIS AREA & HOW ADDRESSES & LENGTHS FOR
*        THE FREEMAIN MAY BE FOUND
*
***********************************************************************
         L         R4,16(,0)      GET CVT ADDRESS
         USING     CVT,R4
         L         R4,CVTQTD00    GET ADDR OF TQE DEQUEUE ROUTINE
         DROP      R4
         LR        R3,R13         SAVE R13 (DESTROYED BY TQE DEQUEUE)
         LR        R5,R1          SAVE R1 (DESTROYED BY SETLOCK)
         SETLOCK   OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=USE,              X
               RELATED=(RELEASE,'RELEASE DISPATCHER LOCK')
         LR        R1,R5          GET TQE POINTER FOR TQE DEQUEUE
         BALR      R2,R4          EXECUTE TQE DEQUEUE
         LR        R13,R3         RESTORE R13 AFTER TQE DEQUEUE
         SETLOCK   RELEASE,TYPE=DISP,REGS=USE,                         X
               RELATED=(OBTAIN,'OBTAIN DISPATCHER LOCK')
         LR        R1,R5          GET TQE (AND WORK AREA) FOR FREEMAIN
         USING     TQE,R1         MAKE TQE & FOLLOWING WORK AREA ADDR
         L         R0,DIEASIZE
         DROP      R1
         FREEMAIN  R,LV=(0),A=(1) FREEMAIN CHUNK OF SQA USED FOR TQE
         BR        R8             RETURN
         MEND
