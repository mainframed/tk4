         MACRO
         JESCKFNC
JESCKFNC DS        0H
********************************************************************
*
*        THIS FUNCTION CHECKS THE JES JOB QUEUE FOR THE CURRENT
*        STATUS OF A JOB.  THE ARGUMENT SUPPLIED IS THE JES
*        JOB #.  JES JOB # MUST BE IN R2 & JOBNAME ADDR IN R3.
*        RESULTS ARE RETURNED IN TWO FORMATS:
*              . AS RETURN CODES, IMPLEMENTED AS BRANCH BACK ADDRS;
*              . FOR RETURN CODE = 0, AS AN SSOB PARAMETER LIST
*
*        RETURN ADDR              MEANING
*              R8+0               REQUESTED JOB FOUND ON JES QUEUE -
*                                 SPECIFIC INFO IS IN SSOB PARM AREA
*              R8+4               REQUESTED JOB NOT FOUND
*              R8+8               LOGIC ERROR IN THIS FUNCTION
*
*        NOTES:
*        THIS ROUTINE USES SEVERAL MACROS & WORK AREAS:
*              . MACROS - CVT (CVTMAP), JES CONTROL TABLE (JESCT),
*              SSOB (SSOB (CS)), JES INTERFACE (IEFSSREQ)
*              . WORK AREAS - DOUBLE, SSOBAREA
*
*        INSTALLED ON: 2/6/80; BY: DGH.
*        MODIFIED ON: 04/04/82; BY: DGH - CHECK FOR JOB # RANGES
*         ASSOCIATED WITH TSO SESSIONS (20000 - 29999), SYSTEM TASKS
*         (10000 - 19999), AND BATCH JOBS (00001 - 99999); ADJUST THE
*         SSOB STATUS PARM LIST ACCORDINGLY.
*
********************************************************************
         XC        SSOBAREA(SSOBLEN2),SSOBAREA CLEAR SSOB AREA
         MVC       SSOBAREA+(SSOBID-SSOB)(4),=C'SSOB' MOVE IN ID
         LA        R1,SSOBHSIZ    SET SSOB HEADER SIZE
         STH       R1,SSOBAREA+(SSOBLEN-SSOB)
         LA        R1,SSOBSTAT    WE WANT JOB STATUS FUNCTION
         STH       R1,SSOBAREA+(SSOBFUNC-SSOB)
         LA        R1,SSOBAREA+(SSCSBGN-SSOB) GET ADDR OF FUNC SUFFIX
         ST        R1,SSOBAREA+(SSOBINDV-SSOB) STOW IT
         LA        R1,SSCSIZE     SIZE OF CANCEL/STATUS SUFFIX
         STH       R1,SSOBAREA+(SSCSLEN-SSOB) STOW IT
         MVC       SSOBAREA+(SSCSJOBN-SSOB)(8),0(R3) JOBNAME
         C         R2,=A(20000)   TSO USER?
         BL        JSCKNTSO       BIF NOT; TRY SYSTEM TASK
         S         R2,=A(20000)   CONVERT TO BASE JES #
         MVC       SSOBAREA+(SSCSJOBI-SSOB)(8),=X'E3E2E44021202020'
         B         JSCKPARM       BRANCH TO COMPLETE PARM LIST
JSCKNTSO EQU       *
         C         R2,=A(10000)   SYSTEM TASK?
         BL        JSCKNSTC       BIF NOT; ASSUME BATCH JOB
         S         R2,=A(10000)   CONVERT TO BASE JES #
         MVC       SSOBAREA+(SSCSJOBI-SSOB)(8),=X'E2E3C34021202020'
         B         JSCKPARM       BRANCH TO COMPLETE PARM LIST
JSCKNSTC EQU       *
         MVC       SSOBAREA+(SSCSJOBI-SSOB)(8),=X'D1D6C24021202020'
JSCKPARM EQU       *
         CVD       R2,DOUBLE      CONVERT JES JOB # TO DISPLAY
         SRP       DOUBLE,1,0
         ED        SSOBAREA+(SSCSJOBI-SSOB)+3(5),DOUBLE+5
         LA        R1,SSCSELSZ    SIZE OF ONE ELEMENT
         STH       R1,SSOBAREA+(SSCSDIMP-SSOB) FOR RETURNED INFO
         LA        R1,SSOBAREA
         ST        R1,SSOBADDR    BUILD PARM LIST
         LA        R1,SSOBADDR
         OI        SSOBADDR,X'80' SET END-OF-LIST
         IEFSSREQ
         L         R1,SSOBAREA+(SSOBRETN-SSOB) GET RET. CODE
         LTR       R1,R1          RC=0?
         BZR       R8             BIF YES; INFO IN SSOBAREA
         C         R1,=F'4'       RC=4? (JOB NOT FOUND)
         BE        4(,R8)         BIF YES; RC=4 TO CALLER
         C         R1,=F'8'       JOBNAME/JOBID MISMATCH?
         BE        4(,R8)         BIF YES; RC=4 TO CALLER
         B         8(,R8)         OTHERWISE, RC=8: LOGIC ERROR
         MEND
