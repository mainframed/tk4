VTAM     TITLE 'VTAM BUFFER USAGE REPORT - M. MAHONEY'
VTAMBUF  CSECT
*********************************************************************
*                                                                   *
*   THIS PROGRAM WILL PRINT THE VTAM BUFFER STATISTICS RECORD       *
*   WRITTEN TO SMF BY THE RMF INTERVAL EXIT. WE CHOSE TO USE        *
*   RECORD TYPE 78 TO CONTAIN THE BUFFER VALUES.          AS WITH   *
*   ALL SHARE GOODIES, NO WARRANTY IS EXPRESSED OR IMPLIED IN THE   *
*   DISTRIBUTION OF THIS CODE. EACH USER SHOULD EVALUATE ITS        *
*   USEFULNESS IN HIS/HER OWN ENVIRONMENT.                          *
*                                                                   *
*                                                                   *
*                    MIKE MAHONEY                                   *
*                    GTE DATA SERVICES                              *
*                    P.O. BOX 6027                                  *
*                    ERIE, PA.  16512                               *
*                    (814) 868-4821 EXT 267                         *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
         STM   R14,R12,12(R13)     SAVE CALLER'S REGS
         BALR  R10,0
         USING *,R10               BASE
         USING SMF78,R4
         USING SMFPOOL,R5
         LA    R12,SAVEAREA
         ST    R12,8(R13)          FORWARD CHAIN
         ST    R13,4(R12)          BACK CHAIN
         LR    R13,R12             PUT IN SAVE AREA ADDRESS
         OPEN  (SMFIN,(INPUT),NCPPRT,(OUTPUT))
LINK1    EQU   *
         GET   SMFIN
         LR    R4,R1               GET ADDRESS OF INCOMING RECORD
         CLI   SMF78RTY,78         RECORD ID FOR BUFFERS RECORD?
         BNE   LINK1               IF NOT GO GET ANOTHER
*
         LA    R5,SMFPOOLS+L'SMFPOOLS  POINT TO BUFFER POOLS
         MVI   OUTPUT,C'0'         SET DOUBLE SPACE
         MVC   DATE,SMF78DTE       PUT DATE IN WORKAREA
         ZAP   DAYS,SMF78DTE+2(2)  SEPARATE DAYS
         MVO   DATE,DATE(2)        GET YEAR WITHOUT DAYS
         UNPK  OUTPUT+6(3),DATE+2(2) PUT YEAR ON PRINT LINE
         MVI   OUTPUT+6,C'/'
         DP    DATE,=P'4'          CHECK FOR LEAP YEAR
         CP    DATE+3(1),=P'0'     IF REMAINDER IS ZERO THEN LEAP YEAR
         BNE   LOOK                NOT LEAP YEAR
*
         CP    DAYS,=P'60'         IF LT 60 IN LEAP YEAR DAYS O.K.
         BL    LOOK                JAN OR FEB
*
         BE    FEB29               IF EQ 60 THEN FEB 29
*
         SP    DAYS,=P'1'          MAKE LEAP YEAR NOT LEAP YEAR
LOOK     LA    R11,TABLE           POINT TO DAYS TABLE
LOOK0    CLI   0(R11),X'FF'        END OF TABLE
         BE    NODATE              DATE INVALID
*
         CP    DAYS,4(2,R11)       IF LT USE CURRENT
         BNH   LOOK1               PROCESS ENTRY FOUND
*
         LA    R11,4(R11)          GET NEXT TABLE ENTRY
         B     LOOK0               CHECK NEXT
*
LOOK1    SP    DAYS,0(2,R11)       DEDUCT END OF PREV MONTH
         UNPK  OUTPUT+3(3),DAYS+2(2) PUT DAYS ON PRINT LINE
         OI    OUTPUT+5,X'F0'      CLEAR NUMERIC SIGN
         MVI   OUTPUT+3,C'/'
         MVC   OUTPUT+1(2),6(R11)  PUT IN MONTH
NODATE   EQU   *
         L     R7,SMF78TME         GET TIME OF DAY
         SR    R6,R6               CLEAR REG
         D     R6,=F'360000'       GET TOD IN HOURS AND TENTHS
         CVD   R7,DOUBLE           GET HOURS TO DECIMAL
         MP    DOUBLE,=P'100'      MOVE OVER TWO DIGITS
         ZAP   TIME,DOUBLE+5(3)    PUT IN HOURS
         LR    R7,R6
         SR    R6,R6
         D     R6,=F'6000'         GET MINUTES
         CVD   R7,DOUBLE           MAKE DECIMAL
         AP    TIME,DOUBLE+5(3)    PUT IN MINUTES
         MVC   OUTPUT+9(7),TIMEMASK
         ED    OUTPUT+9(7),TIME
         LA    R3,11               NUMBER OF BUFFER POOLS
LINK4    EQU   *
         MVC   OUTPUT+21(4),POOLID PUT POOLID ON PRINT LINE
         LA    R7,4                COUNT OF BINARY FIELDS TO PRINT
         LA    R8,BUFSZ            POINT TO FIRST BINARY NO
         LA    R9,OUTPUT+25        RECEIVING FIELD
LINK5    LH    R6,0(R8)            GET A BINARY FIELD
         CVD   R6,DOUBLE           MAKE IT DECIMAL
         MVC   0(12,R9),MASK       PUT IN EDIT MASK
         ED    0(12,R9),DOUBLE+3   PUT IN VALUE
         LA    R8,2(R8)            GET NEXT BINARY VALUE
         LA    R9,12(R9)           GET NEXT OUTPUT AREA
         BCT   R7,LINK5            IF MORE LEFT GO FORMAT THEM
*
         LA    R7,3                COUNT OF BINARY FIELDS TO PRINT
LINK6    L     R6,0(R8)            GET A BINARY FIELD
         CVD   R6,DOUBLE           MAKE IT DECIMAL
         MVC   0(12,R9),MASK       PUT IN EDIT MASK
         ED    0(12,R9),DOUBLE+3   PUT IN VALUE
         LA    R8,4(R8)            GET NEXT BINARY VALUE
         LA    R9,12(R9)           GET NEXT OUTPUT AREA
         BCT   R7,LINK6            IF MORE LEFT GO FORMAT THEM
*
         CP    LINES,=P'55'        LINES PER PAGE
         BL    PRINT
*
         AP    PAGES,=P'1'
         MVC   HEAD1+125(4),=X'40202120'
         ED    HEAD1+125(4),PAGES
         MVC   HEAD2+50(4),SMF78SID PUT SMF ID IN HEADING
         PUT   NCPPRT,HEAD1
         PUT   NCPPRT,HEAD2
         PUT   NCPPRT,HEAD3
         ZAP   LINES,=P'6'
PRINT    PUT   NCPPRT,OUTPUT       WRITE RECORD TO PRINTER
         MVC   OUTPUT,OUTPUT-1
         AP    LINES,=P'1'         COUNT LINES PRINTED
         LA    R5,L'SMFPOOLS(R5)
         BCT   R3,LINK4            LOOP IF MORE POOLS
*
         AP    LINES,=P'1'
         B     LINK1               GO GET ANOTHER RECORD
*
FEB29    MVC   OUTPUT+1(5),=C'02/29'
         B     NODATE
*
ENDSMF   CLOSE (SMFIN,,NCPPRT)
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*        C O N S T A N T S   A N D   W O R K A R E A S                *
*                                                                     *
*                                                                     *
***********************************************************************
         REGS
SAVEAREA DC    18F'0'
DOUBLE   DC    D'0'
TABLE    DC    PL2'0',C'00'
         DC    PL2'31',C'01'
         DC    PL2'59',C'02'
         DC    PL2'90',C'03'
         DC    PL2'120',C'04'
         DC    PL2'151',C'05'
         DC    PL2'181',C'06'
         DC    PL2'212',C'07'
         DC    PL2'243',C'08'
         DC    PL2'273',C'09'
         DC    PL2'304',C'10'
         DC    PL2'334',C'11'
         DC    PL2'365',C'12'
         DC    X'FF'
DATE     DC    F'0'
DAYS     DC    F'0'
TIME     DC    PL3'0'
LINES    DC    PL2'56'
PAGES    DC    PL2'0'
TIMEMASK DC    X'402120207A2020'
MASK     DC    X'402020206B2020206B202120'
         DC    C' '
OUTPUT   DC    CL133'0'
HEAD1    DC    CL50'1'
         DC    CL70'G T E   D A T A   S E R V I C E S'
         DC    CL13'PAGE'
HEAD2    DC    CL55'0'
         DC    CL78'VTAM BUFFER USAGE REPORT'
HEAD3    DC    CL12'0 DATE'
         DC    CL5'TIME'
         DC    CL9'    POOL'
         DC    CL2' '
         DC    CL9'BUFF SIZE'
         DC    CL3' '
         DC    CL9'BUFF  NO.'
         DC    CL3' '
         DC    CL9'BUFF SLPT'
         DC    CL3' '
         DC    CL9'MAX QUEUE'
         DC    CL3' '
         DC    CL9'MAX  USED'
         DC    CL3' '
         DC    CL9'EXP COUNT'
         DC    CL3' '
         DC    CL9'MAX BUFFS'
         DC    CL3' '
         DC    CL9'         '
         DC    CL3' '
         DC    CL9'         '
SMFIN    DCB   DSORG=PS,MACRF=(GL),BFTEK=A,EODAD=ENDSMF,DDNAME=SMF,    X
               LRECL=8192
NCPPRT   DCB   DSORG=PS,MACRF=(PM),DDNAME=NCPPRT,                      X
               RECFM=FA,BLKSIZE=133,LRECL=133
         LTORG
SMF78    DSECT
SMF78LEN DS    CL4
SMF78FLG DS    CL1
SMF78RTY DS    CL1                 RECORD TYPE
SMF78TME DS    CL4                 TIME RECORD MOVED TO SMF
SMF78DTE DS    CL4                 DATE RECORD MOVED TO SMF
SMF78SID DS    CL4                 SMF SYSTEM ID
SMFPOOLS DS    12CL24              INTELLIGENT CONTROLLER NAME
SMFPOOL  DSECT
POOLID   DS    F                   POOL IDENTIFIER
BUFSZ    DS    H                   SIZE OF EACH BUFFER
BUFNO    DS    H                   NUMBER OF BUFFERS
BUFSP    DS    H                   SLOW POINT
MAXQ     DS    H                   MAX NO OF REQUESTS QUEUED
MAXUSE   DS    F                   MAX BUFFERS USED
EXPCT    DS    F                   NUMBER OF TIMES EXPANDED
MAXBUF   DS    F                   MAX BUFFERS GOTTEN
         END
