FDUC     TITLE 'RMF INTERVAL RECORD FOR VTAM BUFFERS - M. MAHONEY'
ERBMFDUC CSECT
*********************************************************************
*                                                                   *
*   THIS EXIT WAS WRITTEN FOR USE WITH ACF/VTAM REL 2. AS WITH      *
*   ALL SHARE GOODIES, NO WARRANTY IS EXPRESSED OR IMPLIED IN THE   *
*   DISTRIBUTION OF THIS CODE. EACH USER SHOULD EVALUATE ITS        *
*   USEFULNESS IN HIS/HER OWN ENVIRONMENT.                          *
*                                                                   *
*                                                                   *
*                    MIKE MAHONEY                                   *
*                    GTE DATA SERVICES                              *
*                    P.O. BOX 6027                                  *
*                    ERIE, PA.  16512                               *
*                    (814) 868-4821 EXT 267                         *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
         USING *,R15
         B     START
SAVE     DC    18F'0'
         DC    C'ERBMFDUC &SYSDATE'
START    STM   R14,R12,12(R13)
         LA    R12,SAVE
         ST    R12,8(R13)
         ST    R13,4(R12)
         LR    R13,R12
         LR    R10,R15
         USING ERBMFDUC,R10
         DROP  R15
         L     R1,4(R1)            GET PARM POINTER
         CLI   3(R1),4             IF ENTRY REASON IS 4 ...
         BE    EXIT                THEN INIT IN PROGRESS
*
         L     R2,X'408'           ADDRESS OF ATCVT IN PSA
         LTR   R2,R2               CHECK FOR VTAM ACTIVE.....
         BZ    EXIT                IF NOT GET OUT
*
         L     R0,GMCTL            GET SUBPOOL ID AND SIZE
         GETMAIN R,LV=(0)
         LTR   R15,R15
         BNZ   EXIT
*
         LR    R9,R1               PUT ADDRESS OF GOTTEN AREA INTO R9
         USING SMFREC,R9           DSECT FOR SMF RECORD
         LA    R6,SMFPOOLS         BEGINNING OF POOL DATA
         USING SMFPOOL,R6          DSECT FOR EACH POOL
         MVC   SMFHDR,RECID        SMF RECORD ID AND LENGTH
         L     R3,X'358'(R2)       POINTER TO BPDTY
         LA    R8,12               THERE ARE 12 BUFFER POOLS
         LA    R2,X'C0'(R3)        STEP OVER HEADER
         CLC   0(4,R2),=C'SMS1'    FIRST BUFFER IS SMS1
         BNE   LINK2               IF IT ISN'T WE'RE
*                                  ... NOT WHERE WE SHOULD BE
LINK1    L     R7,8(R2)            ADDRESS OF BPCB
         C     R3,X'20'(R7)        THIS WORD SHOULD POINT
         BNE   LINK2               ... TO THE BPDTY
*                                  IF NOT GET OUT
         MVC   POOLID,0(R2)        ID OF POOL
         MVC   BUFSZ,X'28'(R7)     SIZE OF EACH BUFFER
         MVC   BUFNO,X'2A'(R7)     NUMBER OF BUFFERS
         MVC   BUFSP,X'2C'(R7)     SLOW POINT
         MVC   MAXQ,X'30'(R7)      MAX REQUESTS QUEUED
         MVC   MAXUSE,X'34'(R7)    HWM OF BUFFERS USED
         MVC   EXPCT,X'84'(R7)     TIMES EXPANDED
         MVC   MAXBUF,X'88'(R7)    HWM BUFFERS
         LA    R6,L'SMFPOOLS(R6)
         LA    R2,16(R2)           GET NEXT BPENT
         BCT   R8,LINK1            IF MORE POOLS TO GET LOOP
*
         SMFWTM (R9)
         LTR   R15,R15             CHECK FOR GOOD WRITE
         BNZ   LINK2               IF NOT DON'T SET CLEAR BIT
*
         OI    40(R3),X'40'        SET BIT IN BPDTY TO CLEAR
LINK2    EQU   *
         LR    R1,R9
         L     R0,GMCTL
         FREEMAIN R,LV=(0),A=(1)
EXIT     L     R13,4(R13)
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*         C O N S T A N T S   A N D   W O R K A R E A S               *
*                                                                     *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         DS    0F
GMCTL    DC    AL1(245),AL3(18+12*L'SMFPOOLS)
         DS    0H
RECID    DC    AL2(18+12*L'SMFPOOLS),H'0',H'78',3XL4'00'
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*         D U M M Y   S E C T I O N S   F O R   S M F   R E C O R D   *
*                                                                     *
*                                                                     *
***********************************************************************
SMFREC   DSECT
SMFHDR   DS    CL18
SMFPOOLS DS    12CL24
SMFPOOL  DSECT
POOLID   DS    F                   BUFFER POOL ID
BUFSZ    DS    H                   SIZE OF EACH BUFFER
BUFNO    DS    H                   NUMBER OF BUFFERS
BUFSP    DS    H                   BUFFER SLOW POINT
MAXQ     DS    H                   MAX REQUESTS QUEUED
MAXUSE   DS    F                   HWM OF BUFFERS USED
EXPCT    DS    F                   NUMBER OF TIMES EXPANDED
MAXBUF   DS    F                   HWM BUFFERS GOTTEN
         END
