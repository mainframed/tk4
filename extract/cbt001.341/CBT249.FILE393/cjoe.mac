CJOE     TITLE 'QUEUE COMMAND - DUMP A JOE IN HEX'                ONL01
CJOE     QSTART ,                  START CJOE CSECT ASSEMBLY      ONL01
         GBLB  &QSP           MVS/SP OPTION
         GBLB  &QSP133        MVS/SP 1.3.3 OPTION                 UF046
         AIF   (&QSP).SPOK
         QTILT 'JOE COMMAND ONLY SUPPORT UNDER SP VERSION OF QUEUE'
.SPOK    ANOP
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE                                    *
*                                                                     *
***********************************************************************
FINDJQE  L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL HEXDUMP TO DUMP THE JOES                                     *
*                                                                     *
***********************************************************************
         L     R10,QVCKPT     BASE FOR CKPT WORK AREA
         USING QCKPT,R10      ADDRESSING FOR IT
         L     R1,QCJQEA      ADDRESS OF JQE
         USING JQEDSECT,R1    ADDRESSING FOR JQE
         SR    R2,R2          CLEAR WORK REG
         ICM   R2,7,JQEJOEB   OFFSET TO FIRST WORK JOE
         BNZ   JOELOOP        CONTINUE IF ANY
         QTILT 'JOB HAS NO JOES'
         DROP  R1             DROP JQE ADDRESSING
         USING JOEDSECT,R2    ADDRESSING FOR JOE
         SPACE 1
JOELOOP  A     R2,QCJOTA      ADD TO GET ACTUAL ADDRESS
         LA    R1,MSGJWORK    WORK JOE MESSAGE
         ST    R1,QDMSGA      SAVE MESSAGE ADDRESS
         LA    R1,L'MSGJWORK  LENGTH OF MESSAGE
         STH   R1,QDMLNG      SAVE FOR DISPLAY ROUTINE
         L     R15,=V(DISPLAY) ADDRESS OF DISPLAY ROUTINE
         BALR  R14,R15        LINK TO IT
         LR    R1,R2          POINTER TO JOE TO DUMP
         LA    R0,JOE1END-JOEDSECT ACTUAL LENGTH OF WORK JOE
         L     R15,=V(HEXDUMP) ADDR OF HEXDUMP MODULE
         BALR  R14,R15        GO TO IT
         SPACE 1
         LA    R1,MSGJCHAR    CHAR JOE MESSAGE
         ST    R1,QDMSGA      SAVE MESSAGE ADDRESS
         LA    R1,L'MSGJCHAR  LENGTH OF MESSAGE
         STH   R1,QDMLNG      SAVE FOR DISPLAY ROUTINE
         L     R15,=V(DISPLAY) ADDRESS OF DISPLAY ROUTINE
         BALR  R14,R15        LINK TO IT
         ICM   R1,7,JOECHARB  POINTER TO CHARACTERISTICS JOE
         A     R1,QCJOTA      ADD TO GET ACTUAL ADDRESS
         LA    R0,JOE2END-JOEDSECT ACTUAL LENGTH OF CHAR JOE
         L     R15,=V(HEXDUMP) ADDR OF HEXDUMP MODULE
         BALR  R14,R15        GO TO IT
         SPACE 1
         AIF   (&QSP133).SP133A                                   UF046
         LA    R1,MSGJCKPT    CKPT JOE MESSAGE
         ST    R1,QDMSGA      SAVE MESSAGE ADDRESS
         LA    R1,L'MSGJCKPT  LENGTH OF MESSAGE
         STH   R1,QDMLNG      SAVE FOR DISPLAY ROUTINE
         ICM   R1,7,JOECKPTB  POINTER TO CKPT JOE
         BZ    NEXTWORK       NONE, GET NEXT WORK JOB FOR THIS JOB
         L     R15,=V(DISPLAY) ADDRESS OF DISPLAY ROUTINE
         BALR  R14,R15        LINK TO IT
         A     R1,QCJOTA      ADD TO GET ACTUAL ADDRESS
         LA    R0,JOE3END-JOEDSECT ACTUAL LENGTH OF CKPT JOE
         L     R15,=V(HEXDUMP) ADDR OF HEXDUMP MODULE
         BALR  R14,R15        GO TO IT
.SP133A  ANOP  ,                                                  UF046
         SPACE 1
NEXTWORK ICM   R2,7,JOEJQNXB  GET NEXT WORK JOE FOR THIS JOB
         BNZ   JOELOOP          AND DUMP IT
         QSTOP
MSGJWORK DC    C'*** WORK JOE ***'
MSGJCHAR DC    C'*** CHAR JOE ***'
MSGJCKPT DC    C'*** CKPT JOE ***'
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
CJOE     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
        $JOE
        $JQE
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
