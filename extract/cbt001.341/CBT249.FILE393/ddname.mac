DDNAME   TITLE 'QUEUE COMMAND - LIST DDNAMES AND DSIDS FOR A JOB' ONL01
DDNAME   QSTART ,                  START DDNAME CSECT ASSEMBLY    ONL01
         GBLB  &QSP                SP V1.3.X OPTION               RNB12
         GBLB  &QSP133             SP V1.3.3 OPTION               ONL11
         GBLB  &QJTIP              JTIP OPTION                    UF046
         SPACE 2                                                  ONL11
******************************************************************ONL11
*                                                                 ONL11
*   LIST DDNAME'S AND DSID'S FOR A JOB                            ONL11
*                                                                 ONL11
*   SYNTAX:  DD <JOBNAME>                                         ONL11
*                                                                 ONL11
*   NOTE:  THE SP 1.3.3 VERSION OF THE DD COMMAND IS JUST A       ONL11
*          SUBSET OF THE PDDB COMMAND.                            ONL11
*                                                                 ONL11
******************************************************************ONL11
         SPACE 1                                                  ONL11
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         USING WORK,R13
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ROUTINE TO FIND JOB
         BALR  R14,R15        GO TO IT
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB                           ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QADD              PERFORM JOB AUTHORIZATION      ONL02
         SPACE 2                                                  ONL02
         AIF   (&QSP133).C29DD                                    ONL11
***********************************************************************
*                                                                     *
*   DETERMINE JOB TYPE (BATCH OR TSO)                                 *
*                                                                     *
***********************************************************************
         USING JCTDSECT,R1    BASE REG FOR JCT                    ONL16
         USING PDBDSECT,R2    BASE REG FOR PDDB
         L     R1,QCJCTA      A(JCT)
         CLI   QPARM2,C'S'                                        RNB11
         BE    DDTSO                                              RNB11
         CLC   JCTJOBID(3),=CL3'TSU' ?/TSO USER
         BE    DDTSO          YES. GO PROCESS
         CLC   JCTJOBID(3),=CL3'STC' ?/STARTED TASK
         BE    DDTSO          YES. GO PROCESS
***********************************************************************
*                                                                     *
*   LOCATE PDDB NUMBER 5                                              *
*                                                                     *
***********************************************************************
DDJOB    L     R3,QCIOTA      LOAD BASE REG
         SH    R3,=AL2(IOTSTART-IOTDSECT) BACK UP THE POINTER     UF046
         USING IOTDSECT,R3    BASE REG FOR IOT                    UF046
         LR    R4,R3          BASE OF IOT
         CLC   IOTID,=CL4'IOT'  WAS AN IOT READ FROM SPOOL ?      ONL03
         BNE   BADIOT         ERROR IF NO                         ONL03
         CLC   QPJOBID,IOTJBKEY  IS THE IOT'S JOB KEY VALID ?     ONL03
         BNE   BADIOT         ERROR IF NO                         ONL03
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB
         LR    R2,R3          BASE OF IOT
         A     R2,QCPDDB1     OFFSET TO FIRST PDDB IN IOT
FINDDS   CLC   =H'5',PDBDSKEY IS THIS THE DATASET?
         BE    FOUNDDS        YES. CONTINUE.
         LA    R2,PDBLENG(R2) NO. LOOK AT NEXT PDDB.
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB?
         BL    FINDDS         NO. TRY AGAIN.
         QTILT '*** JOB DOES NOT HAVE DD TABLE ***'
FOUNDDS  L     R4,PDBMTTR     DISK ADDR OF FIRST BLOCK
         L     R5,QCBLKA      ADDR OF DATASET BLOCK IOAREA
         MVC   QDMSG,QBLANK   BLANK OUT THE MESSAGE AREA
         B     FIRST          PROCESS DATASET
***********************************************************************
*                                                                     *
*   PROCESS DATASET                                                   *
*                                                                     *
***********************************************************************
NEXTBLK  L     R4,0(R5)       DISK ADDR OF NEXT BLOCK
FIRST    LTR   R4,R4          IS THE DISK ADDR ZERO?
         BZ    END            YES. END OF DATASET.
         BAL   R8,READ        READ A BLOCK
         CLC   QPJOBID,4(R5)  DOES THE JOBID MATCH?
         BNE   END            NO. END OF DATASET.
         CLC   =H'5',8(R5)    IS THE DSID 5?
         BNE   END            NO. END OF DATASET.
         LA    R4,10(R5)      ADDR OF FIRST RECORD IN BLOCK
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
NEXTREC  CLI   0(R4),X'FF'    IS LENGTH BYTE FF?
         BE    NEXTBLK        YES. END OF BLOCK.
         TM    1(R4),X'10'    IS THIS A SPANNED RECORD?
         BO    SPAN           YES. SKIP IT.
         SR    R6,R6          ZERO OUT REG
         IC    R6,0(R4)       INSERT LENGTH
         TM    5(R4),2        IS THIS AN EXEC RECORD?
         BO    EXEC           YES. PROCESS IT.
         TM    5(R4),4        IS THIS A DD RECORD?
         BO    DD             YES. PROCESS IT.
SKIPREC  LA    R4,3(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPAN     LH    R6,2(R4)       LENGTH OF SEGMENT
         TM    1(R4),X'08'    IS THIS THE FIRST SEGMENT?
         BO    SPANFRST       YES. USE HEADER LENGTH OF 6.
         LA    R4,4(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPANFRST LA    R4,6(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
END      QSTOP
***********************************************************************
*                                                                     *
*   PROCESS AN EXEC RECORD                                            *
*                                                                     *
***********************************************************************
EXEC     MVC   STEPNAME,QBLANK BLANK OUT THE STEPNAME
         CLI   7(R4),X'94'    IS THERE A STEPNAME?
         BNE   SKIPREC        NO. SKIP THE REST.
         SR    R1,R1          ZERO OUT R1
         IC    R1,9(R4)       LENGTH OF STEPNAME
         SH    R1,=H'1'       DECREMENT BY 1
         BM    SKIPREC        STEPNAME WAS ZERO LENGTH.
         EX    R1,MVCSTEP     MOVE THE STEPNAME
         B     SKIPREC        CONTINUE PROCESSING
***********************************************************************
*                                                                     *
*   PROCESS DD RECORDS                                                *
*                                                                     *
***********************************************************************
DD       TM    6(R4),X'30'    IS THIS A SYSIN OR SYSOUT DD?
         BZ    SKIPREC        NO. SKIP THE RECORD.
         MVC   DDN,QBLANK     BLANK OUT THE DDNAME
         MVC   DSID,QBLANK    BLANK OUT THE DSID
         MVC   DSRECCT,QBLANK BLANK OUT THE DS RECORD COUNT
         MVC   DSCLASS,QBLANK BLANK OUT THE DS CLASS
         LA    R7,7(R4)       ADDR OF FIRST KEY
         LR    R8,R6          REMAINING LENGTH OF RECORD
         SR    R15,15         ZERO OUT R15
         SR    R14,R14        ZERO OUT R14
         SR    R1,R1          ZERO OUT R1
TRYFLD   CLI   0(R7),X'6E'    IS THIS THE DDNAME?
         BE    DDKEY          YES. PROCESS IT.
         CLI   0(R7),X'4A'    IS THIS THE DSNAME?
         BNE   NEXTFLD        NO. GET NEXT FIELD
         CLC   3(3,R7),=C'JES' YES. IS THIS TRULY A JES2 SYSIN/OUT DS?
         BE    DSKEY          YES. PROCESS IT.
NEXTFLD  IC    R1,1(R7)       NUMBER OF SUBFIELDS
         LA    R7,2(R7)       UPDATE LOCATION
         SH    R8,=H'2'       REMAINING COUNT
         SR    R8,R1          REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
         LTR   R1,R1          ARE THERE ANY SUBFIELDS?
         BZ    TRYFLD         NO. TRY NEXT FIELD.
LOOPFLD  TM    0(R7),X'80'    IS THIS A SUB-SUB-FIELD
         BZ    NOSUB          NO. CONTINUE.
         NI    0(R7),X'7F'    CLEAR THE HEX 80 BIT
         IC    R14,0(R7)      NUMBER OF SUB-SUB-FIELDS
         LA    R7,1(R7)       UPDATE LOCATION
         SH    R8,=H'1'       REMAINING COUNT
         SR    R8,R14         REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
         AR    R1,R14         INCREASE NUMBER OF SUBFIELDS
         B     YESSUB         DECREMENT AND TRY AGAIN
NOSUB    IC    R15,0(R7)      SUBFIELD LENGTH
         LA    R7,1(R15,R7)   ADD TO LOCATION
         SR    R8,R15         REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
YESSUB   BCT   R1,LOOPFLD     DO NEXT SUBFIELD
         B     TRYFLD         TRY NEXT FIELD
DDKEY    IC    R1,2(R7)       LENGTH OF DDNAME
         LTR   R1,R1          IS THE LENGTH ZERO?
         BZ    NEXTFLD        YES. SKIP THE FIELD.
         BCTR  R1,0           DECREMENT BY 1
         EX    R1,MVCDDN      MOVE THE DDNAME
         B     NEXTFLD        PROCESS NEXT FIELD
DSKEY    MVC   DSID+1(3),20(R7)  MOVE THE DSID
         L     R15,=V(FINDPDDB) ADDR OF FINDPDDB MODULE
         BALR  R14,R15        GO TO IT
         MVC   QDHLINE,HEADING MOVE IN HEADING
         LA    R1,QDMSG       ADDR OF MESSAGE LINE
         ST    R1,QDMSGA      STORE IN MESSAGE ADDR
         MVC   QDMLNG,=H'80'  MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        GO TO IT
         B     SKIPREC        PROCESS NEXT RECORD
***********************************************************************
*                                                                     *
*   PROCESS DD TSU                                                    *
*                                                                     *
***********************************************************************
DDTSO    L     R5,QCBLKA      ADDR OF DATASET BLOCK IOAREA
         LR    R3,R5          BASE OF IOAREA
         SH    R3,=AL2(IOTSTART-IOTDSECT) BACK UP POINTER         UF046
         L     R4,JCTSPIOT    ADDR OF FIRST SPIN IOT
TSO010   LTR   R4,R4          IS IOT ADDR ZERO?
         BZ    DDJOB          YES, GO READ REGULAR IOT'S
         BAL   R8,READ        READ IOT
         LR    R4,R3          BASE OF IOT
         CLC   IOTID,=CL4'IOT'  WAS AN IOT READ FROM SPOOL ?      ONL03
         BNE   BADIOT         ERROR IF NO                         ONL03
         CLC   QPJOBID,IOTJBKEY  IS THE IOT'S JOB KEY VALID ?     ONL03
         BNE   BADIOT         ERROR IF NO                         ONL03
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB
         LR    R2,R3          BASE OF IOT
         A     R2,QCPDDB1     OFFSET TO FIRST PDDB IN IOT
TSO020   CLI   PDBFLAG1,X'00' IS THIS PDDB VALID
         BE    TSO040         NO, GET NEXT IOT
         MVC   QDMSG,QBLANK   BLANK MESSAGE LINE
         MVC   DDN,=CL8'SPIN-DS'      MOVE IN DDNAME
         SR    R0,R0          CLEAR REG 0
         LH    R0,PDBDSKEY    CONVERT
         CVD   R0,CONVERT        DATA SET
         MVC   DSID,ED4               ID  TO
         ED    DSID,CONVERT+6             ZERO
         L     R0,PDBRECCT    CONVERT
         CVD   R0,CONVERT        RECORD
         MVC   DSRECCT,ED8          COUNT TO
         ED    DSRECCT,CONVERT+4        CHARACTER
         MVC   DSCLASS,PDBCLASS  MOVE IN SYSOUT CLASS
         AIF   (&QSP).RNB12A                                      RNB12
         TM    PDBFLAG1,PDB1PSO  HAS DATA SET BEEN PRINTED
         BO    TSO030         NO
         MVC   MESSAGE,PRTMSG INDICATE DATA SET PRINTED
.RNB12A  ANOP
TSO030   MVC   QDHLINE,HEADING MOVE IN HEADING
         LA    R1,QDMSG       ADDR OF MESSAGE LINE
         ST    R1,QDMSGA      STORE IN MESSAGE ADDR
         MVC   QDMLNG,=H'80'  MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        GO TO IT
         LA    R2,PDBLENG(R2) LOOK AT NEXT PDDB
         CR    R2,R4          HAVE WE GONE PAST THE LAST PDDB
         BL    TSO020         NO, TRY AGAIN
TSO040   L     R4,IOTIOTTR    DISK ADDR OF NEXT IOT
         B     TSO010         GO SEARCH THE NEXT IOT
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK      STORE DISK ADDR
         LR    R1,R5          IOAREA ADDRESS
         L     R15,=V(READSPC) ADDR OF ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         BR    R8             RETURN TO CALLER
         SPACE 3                                                  ONL03
BADIOT   QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       ONL03
         SPACE 2                                                  ONL03
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
MVCSTEP  MVC   STEPNAME(1),10(R4)
MVCDDN   MVC   DDN(1),3(R7)
HEADING  DC    CL80'STEPNAME    DDNAME      DSID      LINES   CLASS'
ED4      DC    X'40202120'
ED5      DC    X'4020202120'
ED8      DC    X'4020202020202120'
PRTMSG   DC    CL15'ALREADY PRINTED'
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
DDNAME   CSECT ,                                                  UF023
JCT      EQU   0
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $BUFFER ,                                                UF046
         $TAB
         $JCT
         $PDDB
         $IOT
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDMSG
STEPNAME DS    CL8
         DS    CL4
DDN      DS    CL8 END OF DATA. LAST REC #'
         DS    CL4
DSID     DS    CL4
         DS    CL4
DSRECCT  DS    CL8
         DS    CL4
DSCLASS  DS    CL1
         DS    CL6
MESSAGE  DS    CL15
WORK     DSECT
         DS    CL72
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         AGO   .END                                               ONL11
.C29DD   ANOP  ,                                                  ONL11
***********************************************************************
*                                                                     *
*   FIND ALL PDDBS FOR THIS JOB WITH A DSID OF 101 OR GRATER          *
*                                                                     *
***********************************************************************
         USING PDBDSECT,R2        BASE REG FOR PDDB
         MVC   QDHLINE,HEADING    SET HEADING LINE
         MVI   SWITCH,0           CLEAR FLAG BYTE
         L     R3,QCIOTA          LOAD BASE REG
         LR    R5,R3              IOAREA FOR READ IOT BLOCK
         SH    R3,=AL2(IOTSTART-IOTDSECT) ADJUST FOR HEADER       UF046
         USING IOTDSECT,R3        BASE REG FOR IOT                UF046
         AIF   (NOT &QSP133).C29A1                                UF046
         L     R1,QCJQEA          ADDRESS OF JQE                  UF046
         USING JQEDSECT,R1        SET TEMP ADDRESSING             UF046
         TM    JQEFLAG3,QUESYSDS  JQETRAK POINT TO IOT?           UF046
         BZ    READJCT            NO, SKIP THIS CODE              UF046
         OI    SWITCH,1            SKIP SPIN IOT'S                UF046
         L     R4,JQETRAK          ADDRESS OF FIRST IOT           UF046
         B     READIOT             READ FIRST IOT                 UF046
READJCT  DS    0H                                                 UF046
.C29A1   ANOP  ,                                                  UF046
         USING JCTDSECT,R1        SET TEMP ADDRESSING             ONL16
         L     R1,QCJCTA          POINT TO JCT
         L     R4,JCTIOT          FIRST IOT ADDRESS
         DROP  R1                 DROP TEMP ADDRESSING
READIOT  DS    0H                                                 UF046
         BAL   R8,READ            READ THE IOT
         SPACE 1
NEXTIOT  LR    R4,R3              BASE OF IOT
         CLC   IOTID,=CL4'IOT'    WAS AN IOT READ FROM SPOOL ?    ONL03
         BNE   BADIOT             ERROR IF NO                     ONL03
         CLC   QPJOBID,IOTJBKEY   IS THE IOT'S JOB KEY VALID ?    ONL03
         BNE   BADIOT             ERROR IF NO                     ONL03
         A     R4,IOTPDDBP        OFFSET BEYOND LAST PDDB
         LR    R2,R3              BASE OF IOT
         AIF   (NOT &QSP133).C29A                                 UF046
         A     R2,IOTPDDB            ADD OFFSET TO FIRST PDDB     UF046
         AGO   .C29B                                              UF046
.C29A    ANOP  ,                                                  UF046
         A     R2,QCPDDB1         OFFSET TO FIRST PDDB IN IOT
.C29B    ANOP  ,                                                  UF046
         SPACE 1                                                  ONL11
PDDBLOOP LH    R0,PDBDSKEY        GET THE DSID
         C     R0,=F'101'         IS DSID .GE. 101?               ONL11
         BL    NEXTPDDB           IF NOT, IGNORE THIS PDDB        ONL11
         SPACE 1                                                  ONL11
         MVC   QDMSG,QBLANK       BLANK WORK LINE AREA            ONL11
         LTR   R0,R0              TEST FOR NULL PDDB#
         BZ    NEXTPDDB           SKIP IF SO
         CVD   R0,CONVERT         CONVERT TO DECIMAL
         MVC   DSID-4(8),ED8      MOVE EDIT PATTERN TO DISPLAY
         ED    DSID-4(8),CONVERT+4  EDIT THE DSID
         L     R0,PDBRECCT        GET THE RECORD COUNT
         CVD   R0,CONVERT         CONVERT TO DECIMAL
         MVC   DSRECCT,ED8        MOVE EDIT PATTERN TO DISPLAY
         ED    DSRECCT,CONVERT+4  EDIT THE RECORD COUNT
         MVC   DSCLASS,PDBCLASS   MOVE PDBCLASS TO DISPLAY
         AIF   (NOT &QSP133).C29C                                 UF046
         MVC   DS#PROC,PDBPNAME   SET PDDB PROC NAME              UF046
         MVC   DS#STEP,PDBSNAME   SET PDDB STEP NAME              UF046
         MVC   DS#DDNM,PDBDDNAM   SET PDDB DD NAME                UF046
         AGO   .JTIP1              SKIP JTIP CODE                 UF046
.C29C    ANOP                                                     UF046
         AIF   (NOT &QJTIP).JTIP1
         MVC   DS#PROC,PDB#PROC   SET JTIP PROC NAME
         MVC   DS#STEP,PDB#STEP   SET JTIP STEP NAME
         MVC   DS#DDNM,PDB#DDN    SET JTIP DD NAME
.JTIP1   ANOP
         MVC   QDMLNG,=H'80'      SET THE LENGTH
         LA    R0,QDMSG           POINT TO MESSAGE
         ST    R0,QDMSGA          SET IN AREA
         L     R15,=V(DISPLAY)    POINT TO DISPLAY ROUTINE
         BALR  R14,R15            LINK TO IT
NEXTPDDB LA    R2,PDBLENG(R2)     POINT TO NEXT PDDB
         CR    R2,R4              HAVE WE GONE PAST THE LAST PDDB
         BL    PDDBLOOP           NO. KEEP TRYING
         L     R4,IOTIOTTR        DISK ADDR OF NEXT IOT
SPIN     LTR   R4,R4              IS THERE ANOTHER IOT?
         BZ    SPINIOT            NO. TRY THE SPIN IOT.
         BAL   R8,READ            READ THE IOT
         B     NEXTIOT            SEARCH THE NEXT IOT
         USING JCTDSECT,R1        BASE REG FOR JCT                ONL16
SPINIOT  TM    SWITCH,1           DID WE SEARCH THE SPINIOT ALREADY
         BO    STOP               YES, DONE
         OI    SWITCH,1           SET SWITCH
         L     R1,QCJCTA          LOAD BASE REG
         L     R4,JCTSPIOT        DISK ADDR OF SPIN IOT
         B     SPIN               SEARCH THE SPIN IOT CHAIN
         DROP  R1
STOP     QSTOP                    GO BACK TO CALLER
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK          STORE DISK ADDR
         LR    R1,R5              IOAREA ADDRESS
         L     R15,=V(READSPC)    ADDR OF ROUTINE TO READ HASPACE
         BALR  R14,R15            GO TO IT
         BR    R8                 RETURN TO CALLER
ED8      DC    X'4020202020202120'
BADIOT   QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       ONL03
HEADING  DC    CL80' '
         ORG   HEADING            POINT TO START OF AREA
         DC    CL4' '                                             ONL11
         DC    C'DSID'                                            ONL11
         DC    CL2' '                                             ONL11
         DC    C'RECORDS'                                         ONL11
         DC    CL2' '                                             ONL11
         DC    C'CLASS'                                           ONL11
         AIF   (NOT (&QJTIP OR &QSP133)).JTIP2                    ONL11
         DC    CL2' '                                             ONL11
         DC    C'PROCNAME'                                        ONL11
         DC    CL2' '                                             ONL11
         DC    C'STEPNAME'                                        ONL11
         DC    CL2' '                                             ONL11
         DC    C'DDNAME'                                          ONL11
.JTIP2   ANOP                                                     ONL11
         ORG   ,                  BACK TO NORMAL ADDRESSING
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
DDNAME   CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
JCT      EQU   0
         $BUFFER ,                                                UF046
         $TAB
         $JCT
         $JQE                                                     UF046
         $PDDB
         $IOT
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDMSG
         DS    CL4                                                ONL11
DSID     DS    CL4                                                ONL11
         DS    CL1                                                ONL11
DSRECCT  DS    CL8                                                ONL11
         DS    CL4                                                ONL11
DSCLASS  DS    CL1                                                ONL11
         AIF   (NOT (&QJTIP OR &QSP133)).JTIP3                    ONL11
         DS    CL4                                                ONL11
DS#PROC  DS    CL8                                                ONL11
         DS    CL2                                                ONL11
DS#STEP  DS    CL8                                                ONL11
         DS    CL2                                                ONL11
DS#DDNM  DS    CL8                                                ONL11
.JTIP3   ANOP                                                     ONL11
WORK     DSECT
         DS    CL72
SWITCH   DS    C
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
.END     ANOP  ,                                                  ONL11
         END   ,                                                  ONL11
