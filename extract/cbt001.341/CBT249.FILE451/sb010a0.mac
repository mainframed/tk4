++USERMOD(SB010A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     ALLOW CERTAIN USERS ALL BDT AND SOME JES3 CMDS     */.
++VER(Z038) FMID(HBD1102)
            PRE(SB005A2 SB001B0 SB005A0
                              )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++JCLIN.
//LINK  EXEC PGM=IEWL,PARM='XREF,LIST,LET,NCAL,SIZE=(768K,100K),RENT
//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(1,1))
//AOSBA    DD DISP=SHR,DSN=SYS1.AOSBA
//SYSLMOD  DD DISP=SHR,DSN=SYS1.LPALIB
//SYSPRINT DD SYSOUT=A
//SYSLIN   DD *
 INCLUDE AOSBA(BDTUX10)             * INCLUDE ORIGINAL MODULE
  ORDER          BDTLP
  ORDER          BDTGRSC
  ENTRY          BDTLP
 NAME    BDTLP                      * LOAD MODULE NAME
/*
++SRC    (BDTUX10 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ FUNCTION:  BDTUX10 - COMMAND PASSWORD PROCESSING EXIT       @SB010A0
*@                      ATHORIZED ALL COMMAND AND JES3 COMMANDS@SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ DEPENDENCIES:                                               @SB010A0
*@             ACF2 RESOURCE RULES RBDTALLCMDS AND RBDTJESCMDS @SB010A0
*@             ONLY ENTERED IF PARAMETER ENTERED AFTER         @SB010A0
*@             PRIMARY COMMAND VERB                            @SB010A0
*@ RESTRICTIONS:                                               @SB010A0
*@             N/A                                             @SB010A0
*@                                                             @SB010A0
*@ REGISTER CONVENTIONS:                                       @SB010A0
*@                                                             @SB010A0
*@                  REGISTER 0 = WORK REGISTER                 @SB010A0
*@                  REGISTER 1 = POINTER TO PARAMETER LIST     @SB010A0
*@                  REGISTER 2 = SAVED PTR TO PARAMETER LIST   @SB010A0
*@                  REGISTER 3 = WORK REGISTER                 @SB010A0
*@                  REGISTER 4 = GETMAINED AREA                @SB010A0
*@                  REGISTER 5 = WORK REG                      @SB010A0
*@                  REGISTER 6 = WORK                          @SB010A0
*@                  REGISTER 7 = WORK                          @SB010A0
*@                  REGISTER 8 = MODULE BASE                   @SB010A0
*@                  REGISTER 9 = BSID BASE                     @SB010A0
*@                  REGISTER 10= MODULE BASE                   @SB010A0
*@                  REGISTER 11= NOT IN USE                    @SB010A0
*@                  REGISTER 13= SAVE AREA SET UP BY ASAVE     @SB010A0
*@                  REGISTER 14= RETURN ADDRESS                @SB010A0
*@                  REGISTER 15= ENTRY POINT/RETURN CODE       @SB010A0
*@ ENTRY POINTS:                                               @SB010A0
*@             BDTUX10                                         @SB010A0
*@                                                             @SB010A0
*@ ENTRY PURPOSE:                                              @SB010A0
*@             N/A                                             @SB010A0
*@                                                             @SB010A0
*@ ENTRY LINKAGE:                                              @SB010A0
*@             BALR R14,15   FROM BDTLP                        @SB010A0
*@ INPUT:                                                      @SB010A0
*@             R13 -> SAVE AREA                                @SB010A0
*@             R1  -> PARAMETER LIST WHICH CONTAINS            @SB010A0
*@                    ADDRESS(PARAMETER)                       @SB010A0
*@                    PARAMETER LENGTH                         @SB010A0
*@                    ADDRESS(BSID)                            @SB010A0
*@                                                             @SB010A0
*@ EXIT:                                                       @SB010A0
*@             BR   R14                                        @SB010A0
*@                                                             @SB010A0
*@ OUTPUT:                                                     @SB010A0
*@             IF AUTHORIZED,FLAGS IN XOID INDICATING SO       @SB010A0
*@             BSIDXRU1 EQUATED TO UX10FLGA                    @SB010A0
*@ EXTERNAL ROUTINES:                                          @SB010A0
*@             N/A                                             @SB010A0
*@                                                             @SB010A0
*@ CONTROL BLOCKS:                                             @SB010A0
*@             BDTDBSID         R/W   ACCVT    R/O             @SB010A0
*@             BDTDGSD          R/W   CVT      R/O             @SB010A0
*@             IHAASCB          R/O   IHAPSA   R/O             @SB010A0
*@             IHASDWA          R/W   IKJTCB   R/O             @SB010A0
*@             IHASDWA          R/W   IKJTCB   R/O             @SB010A0
*@             ACGRSRC          R/W                            @SB010A0
*@                                                             @SB010A0
*@ EXECUTABLE MACROS:                                          @SB010A0
*@             SETRP      ACFINCVT                             @SB010A0
*@             GETMAIN    ACFSVC                               @SB010A0
*@             FREEMAIN                                        @SB010A0
*@                                                             @SB010A0
*@ CHANGE ACTIVITY                                             @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB010A0
*END OF SPECIFICATIONS*****************************************@SB010A0
BDTUX10  START   0                                             @SB010A0
         ACCVT                                                 @SB010A0
         BDTDBSID                                              @SB010A0
         BDTDGSD                                               @SB010A0
         CVT    DSECT=YES                                      @SB010A0
         IHAASCB                                               @SB010A0
         IHAPSA                                                @SB010A0
         IHASDWA                                               @SB010A0
         IKJTCB LIST=YES                                       @SB010A0
         BDTDREG                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        USER EQUATES                                          @SB010A0
*--------------------------------------------------------------@SB010A0
RCAUTHRZ EQU   0                   RETURN CODE 0 = AUTHORIZED  @SB010A0
RCFAILTR EQU   4                   RETURN CODE 4 = UNAUTHORIZED@SB010A0
RCRETRY  EQU   4                   RETURN CODE 4 = RETRY       @SB010A0
RCNORTRY EQU   0                   RETURN CODE 0 = NO RETRY    @SB010A0
ZERO     EQU   0                   USE TO INITIALIZE STORAGE   @SB010A0
*--------------------------------------------------------------@SB010A0
*        USER DSECT FOR WORK AREA                              @SB010A0
*                                                              @SB010A0
*        WORK AREA IS PASSED TO ESTAE MACRO AS USER PARAMETER L@SB010A0
*        THIS IS NECESSARY SO THAT, IF AN ABEND OCCURS, ADDRESS@SB010A0
*        BILITY TO THE AREA CAN BE REESTABLISHED.  DSECT CONTAI@SB010A0
*        FIELDS WHICH ARE NEEDED IN ESTAE RECOVERY ROUTINE, RET@SB010A0
*        ROUTINE OR COMMON EXIT FROM MODULE.                   @SB010A0
*                                                              @SB010A0
*        NOTE:  IF ADDITIONAL WORK AREA IS REQUIRED, ADD IT TO @SB010A0
*        DSECT (PREFERABLY BETWEEN UXESTLEN AND UXMAPLEN), SO  @SB010A0
*        ADDITIONAL GETMAINS ARE NOT NECESSARY.                @SB010A0
*--------------------------------------------------------------@SB010A0
UXPARMAP DSECT                     MAP OF USER PARAMETER LIST  @SB010A0
UXWORKID DS    F                   CONTAINS 'WORK' FOR IDENTIFI@SB010A0
UXTVTADD DS    F                   ADDRESS OF TVT,SAVED FOR RET@SB010A0
UXGSDADD DS    F                   ADDRESS OF GSD,SAVED FOR ABE@SB010A0
UXBASE   DS    F                   MOD BASE ADR,SAVED FOR RETRY@SB010A0
UXBASE2  DS    F                   MOD BASE2 ADDR              @SB010A0
UXTOKEN  DS    F                   TOKEN USED IN ESTAE         @SB010A0
RSCR14SV DS    F                   R14 SAVE FOR SUBROUTINE     @SB010A0
          ACGRSRC DSECT=NO         GENERAL RESOURCE VALIDATION @SB010A0
ACFSVCRC DS    X                   RETURN CODE                 @SB010A0
UXESTFLG DS    B                   INDICATES ESTAE STATUS      @SB010A0
UXESTSUC EQU   BIT0                ESTAE ESTABLISHED SUCCESSFUL@SB010A0
UXESTREP EQU   BIT1                ESTAE RECOVERY ALREADY ENTER@SB010A0
UXESTAPM DS    0F                  ESTAE MACRO PARAMETER LIST  @SB010A0
         ESTAE MF=L                                            @SB010A0
UXESTLEN EQU   *-UXESTAPM          LENGTH OF ESTAE LIST        @SB010A0
*************END OF STANDARD GETMAIN AREA*********             @SB010A0
UXPARMAD DS    F                  SAVE ADDRESS OF PARM LIST    @SB010A0
UXSAVE   DS    18F                                             @SB010A0
UXMAPLEN EQU   *-UXPARMAP          LENGTH OF THIS MAPPING      @SB010A0
*--------------------------------------------------------------@SB010A0
*        DSECT FOR PARAMETER LIST FROM CALLER                  @SB010A0
*                                                              @SB010A0
*        PARAMETER LIST (12 BYTES)                             @SB010A0
*--------------------------------------------------------------@SB010A0
UXPARMIN DSECT                     PARAMETER LIST PASSED BY CAL@SB010A0
UXPASSAD DS    A                   ADDRESS OF DUMMY PASSWORD   @SB010A0
UXPASSLN DS    A                   LENGTH OF DUMMY PASSWORD    @SB010A0
UXBSIDAD DS    A                   ADDRESS OF BSID             @SB010A0
BDTUX10  CSECT                                                 @SB010A0
*--------------------------------------------------------------@SB010A0
*        INVOKE BDTDMOD TO IDENTIFY MODULE                     @SB010A0
*                                                              @SB010A0
*        NOTE THAT CONSTANTS GENERATED BY THIS MACRO ARE ALSO U@SB010A0
*        IN ESTAE RECOVERY ROUTINE TO UPDATE THE SDWA.         @SB010A0
*--------------------------------------------------------------@SB010A0
*        BDTDMOD LABELS=YES                                    @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
         B     ENDCATCH-*(,R15)    BR AROUND ENTRY INFORMATION @SB010A0
         DC    AL1(ENDCATCH-*-1)   LENGTH OF ENTRY INFORMATION @SB010A0
MDNUX10  DC    CL8'BDTUX10'        MODULE NAME                 @SB010A0
MDLUX10  DC    CL8'       '        LABEL NAME                  @SB010A0
MDRUX10  DC    CL9' HBD1102 '      BDT RELEASE OR PTF NUMBER   @SB010A0
MDDUX10  DC    CL8'&SYSDATE'       ASSEMBLY DATE               @SB010A0
MDTUX10  DC    CL6'-&SYSTIME'      ASSEMBLY TIME               @SB010A0
         SPLEVEL SET=1                                         @SB010A0
ENDCATCH DS    0H                                              @SB010A0
*        ESTABLISH ADDRESSABILITY                              @SB010A0
*                                                              @SB010A0
*        REGISTERS ARE NOT SAVED BECAUSE THE CALLING PROGRAM   @SB010A0
*  SAVES THEM VIA BDTXUEX.                                     @SB010A0
*--------------------------------------------------------------@SB010A0
UXINIT   DS    0H                                              @SB010A0
         SAVE  (14,12)             SAVE THE REGISTERS          @SB010A0
         LR    R10,R15             SET BASE REGISTER           @SB010A0
         USING BDTUX10,R10,R8      ESTABLISH ADDRESSABILITY    @SB010A0
         LA    R8,1(R10)           2ND BASE                    @SB010A0
         LA    R8,4095(R8)         2ND BASE                    @SB010A0
         USING UXPARMIN,R1         ESTABLISH ADDRESSAB. TO PARM@SB010A0
         L     R9,UXBSIDAD         SET BASE FOR BSID           @SB010A0
         USING BSID,R9             ESTABLISH ADDRESSAB. TO BSID@SB010A0
         LR    R2,R1               SAVE ADDRESS OF PARMLIST    @SB010A0
         DROP  R1                                              @SB010A0
*--------------------------------------------------------------@SB010A0
*        GETMAIN WORK AREA                                     @SB010A0
*                                                              @SB010A0
*        IF GETMAIN IS UNSUCCESSFUL, SET REGISTER 4 TO ZERO TO @SB010A0
*        INDICATE FAILURE, SKIP ESTAE SETUP AND BEGIN          @SB010A0
*        MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER RECOVERY @SB010A0
*        ROUTINE ESTABLISHED FOR CALLER.                       @SB010A0
*--------------------------------------------------------------@SB010A0
         LA    R4,UXMAPLEN         LENGTH OF AREA NEEDED       @SB010A0
         GETMAIN RC,LV=(R4)        GET STORAGE FOR PARAMETER LI@SB010A0
         LTR   R15,R15             WAS STORAGE OBTAINED?       @SB010A0
         BZ    UXGETOK             YES, INITIALIZE STORAGE     @SB010A0
         XR    R4,R4               NO, INDICATE GETMAIN FAILURE@SB010A0
         B     UXRTONLY            RETURN                      @SB010A0
UXGETOK  DS    0H                                              @SB010A0
         LR    R4,R1               SET BASE FOR PARM LIST      @SB010A0
         USING UXPARMAP,R4         ESTABLISH ADDRESSABILITY TO @SB010A0
         LA    R5,UXMAPLEN         LENGTH OF AREA TO ZERO      @SB010A0
         SR    R7,R7               PAD AND LENGTH OF ZERO      @SB010A0
         MVCL  R4,R6               ZERO THE AREA               @SB010A0
         LR    R4,R1               RELOAD R4                   @SB010A0
         MVC   UXWORKID,UXWKCONS   SET IDENTIFICATION          @SB010A0
         ST    R13,UXSAVE+4        SAVE OLD SAVE AREA ADDR     @SB010A0
         LA    R11,UXSAVE          NEW SAVE AREA ADDR          @SB010A0
         ST    R11,8(R13)          SAVE NEW SAVE AREA          @SB010A0
         LR    R13,R11                                         @SB010A0
*--------------------------------------------------------------@SB010A0
*        ESTABLISH AN ESTAE                                    @SB010A0
*                                                              @SB010A0
*        1. BUILD USER PARAMETER LIST.                         @SB010A0
*        2. INITIALIZE ESTAE MACRO LIST;  ISSUE ESTAE.         @SB010A0
*        3. IF ESTAE SUCCESSFUL, SET BIT 0 IN 'ESTAE SUCCESS FL@SB010A0
*           TO 1.                                              @SB010A0
*        4. IF ESTAE IS UNSUCCESSFUL, LEAVE BIT 0 IN 'ESTAE SUC@SB010A0
*           FLAG' EQUAL TO ZERO.  (FLAG CONTAINS ALL ZEROES AS @SB010A0
*           RESULT OF STORAGE INITIALIZATION AFTER GETMAIN.)   @SB010A0
*           BEGIN MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER @SB010A0
*           RECOVERY ESTABLISHED FOR CALLER.                   @SB010A0
*--------------------------------------------------------------@SB010A0
         USING PSA,R0              ESTABLISH ADDRESSAB. TO PSA @SB010A0
         L     R3,PSATOLD          SET BASE FOR TCB            @SB010A0
         USING TCB,R3              ESTABLISH ADDRESSAB. TO TCB @SB010A0
         L     R3,TCBBDT           SET BASE FOR GSD            @SB010A0
         USING GSDSTART,R3         ESTABLISH ADDRESSAB. TO GSD @SB010A0
         CLC   GSDID,UXGSDID       IS THERE A GSD?             @SB010A0
         BNE   UXNOESTA            NO, SKIP ESTAE SET UP       @SB010A0
         ST    R3,UXGSDADD         STORE ADDRESS OF GSD IN PARM@SB010A0
         ST    R10,UXBASE          STORE MODULE BASE FOR RETRY @SB010A0
         ST    R8,UXBASE2          STORE SECOND BASE           @SB010A0
*                                  MOVE ESTAE MODEL OVER STORAG@SB010A0
         MVC   UXESTAPM(UXESTLEN),ESTMODEL                     @SB010A0
         ESTAE UXESTAE,CT,PARAM=(R4),RECORD=YES,               @SB010A0+
               TOKEN=UXTOKEN,MF=(E,UXESTAPM)                   @SB010A0
         LTR   R15,R15             WAS ESTAE SUCCESSFUL?       @SB010A0
         BNZ   UXNOESTA            NO, DO NOT SET FLAG         @SB010A0
         OI    UXESTFLG,UXESTSUC   SET FLAG SUCCESSFUL  EXTAEL @SB010A0
UXNOESTA DS    0H                                              @SB010A0
         DROP  R0,R3                                           @SB010A0
*--------------------------------------------------------------@SB010A0
*        MAINLINE ROUTINE                                      @SB010A0
*                                                              @SB010A0
*                                                              @SB010A0
*                                                              @SB010A0
*--------------------------------------------------------------@SB010A0
UXMAIN   DS    0H                                              @SB010A0
         LA    R1,ALLCMDSP           ALLCMDS PARAMETER LIST    @SB010A0
         BAL   R14,ACF2VALD          GO VALIDATE ALL CMDS USE  @SB010A0
         B     UX10JESC           +0 INVALID GO VALIDATE JES3  @SB010A0
         OI    UX10AFLG,UX10BDTA  +4 SET AUTH FOR ALL BDT CMDS @SB010A0
UX10JESC DS    0H                                              @SB010A0
         LA    R1,JESCMDSP           JESCMDS PARAMETER LIST    @SB010A0
         BAL   R14,ACF2VALD          GO VALIDATE JES CMDS USE  @SB010A0
         B     UX10DUN1           +0 INVALID ALL DONE          @SB010A0
         OI    UX10AFLG,UX10JESA  +4 SET AUTH FOR JES CMDS     @SB010A0
         WTO   'AUTHORIZED FOR JES3 COMMANDS'                  @SB010A0
         B     UX10DUN1              RETURN                    @SB010A0
ALLCMDSP DC    X'6',CL7'ALLCMDS'     ALLCMDS RESOURCE NAME     @SB010A0
JESCMDSP DC    X'6',CL7'JESCMDS'     JESCMDS RESOURCE NAME     @SB010A0
UX10AFLG EQU   BSIDXRU1,1,C'X'       FLAG TO SEND UX31 AND UX25@SB010A0
UX10BDTA EQU   X'01'                 AUTHORIZED FOR ALL BDT CMD@SB010A0
UX10JESA EQU   X'02'                 AUTHORIZED FOR JES CMDS   @SB010A0
         EJECT ,                                               @SB010A0
         TITLE 'BDT USER EXIT BDTUX10  - RESOURCE VALIDATION ' @SB010A0
ACF2VALD DS    0H                      DO ACF2 VALIDATION      @SB010A0
*--------------------------------------------------------------@SB010A0
         ST    R14,RSCR14SV            SAVE R14                @SB010A0
*      BUILD RESOURCE NAME FIRST                               @SB010A0
         OI    ACGFCN,X'04'                                    @SB010A0
         OI    ACGSFCN,ACGSINP     VALIDATE RESOURCE RULE      @SB010A0
         OI    ACGFLGS,ACGFADRQ    USE ACUCB                   @SB010A0
         MVC   ACGRTYPE,=C'RBDT'   RESOURCE TYPE               @SB010A0
         MVI   ACGRNAME,C' '                     CLEAR ACGRNAME@SB010A0
         MVC   ACGRNAME+1(L'ACGRNAME-1),ACGRNAME CLEAR ACGRNAME@SB010A0
         IC    R15,0(R1)                EXECUTABLE LENGTH      @SB010A0
         EX    R15,EXRSRC               MOVE IN REQUEST RSRC NM@SB010A0
         ACFINCVT R5,NONE=UX10DUN1      GET ACF CVT ADDRESS    @SB010A0
         USING ACCVT,R5                                        @SB010A0
         ACFSVC ACGRSRC,TYPE=A,CVT=HAVE,NONE=UX10DUN1          @SB010A0
         DROP  R5                                              @SB010A0
         L     R14,RSCR14SV             RESTORE RETURN CODE    @SB010A0
         STC   R15,ACFSVCRC             SAVE SVC RETURN CODE   @SB010A0
         CLI   ACFSVCRC,0               CK SVC RETURN CODE     @SB010A0
         BE    4(R14)                   +4 AUTHORIZED          @SB010A0
         BR    R14                      +0 NOT AUTHORIZED      @SB010A0
EXRSRC   MVC   ACGRNAME(0),1(R1)        OBJECT OF EXECUTE      @SB010A0
         EJECT ,                                               @SB010A0
         TITLE 'BDTUX10 GENERAL CODE'                          @SB010A0
UX10DUN1 DS    0H                                              @SB010A0
         SR    R15,R15               OKAY THE COMMAND          @SB010A0
*        TO RETURN TO CALLER OF MODULE (I.E., NORMAL EXIT AND  @SB010A0
*        EXIT AFTER ESTAE RETRY ROUTINE).                      @SB010A0
*                                                              @SB010A0
*        REGISTER 4 AND UXESTFLG (SUCCESSFUL ESTAE FLAG) ARE   @SB010A0
* TESTED TO DETERMINE WHETHER TO CANCEL ESTAE AND INVOKE FREEMA@SB010A0
*                                                              @SB010A0
*        1.  IF REG 4 IS ZERO, GETMAIN WAS UNSUCCESSFUL AND THE@SB010A0
*            IS NO ESTAE TO CANCEL.                            @SB010A0
*        2.  IF REG 4 IS NOT ZERO, TEST FOR ESTAE.             @SB010A0
*            A.  IF UXESTSUC IS OFF, THERE IS NO ESTAE TO CANCE@SB010A0
*                DO FREEMAIN AND RETURN.                       @SB010A0
*            B.  IF UXESTSUC IS ON, CANCEL ESTAE, FREEMAIN AND @SB010A0
*                RETURN.                                       @SB010A0
*--------------------------------------------------------------@SB010A0
UXRETURN DS    0H                                              @SB010A0
         LR    R3,R15              TEMPORARILY SAVE RETURN CODE@SB010A0
         LTR   R4,R4               WAS GETMAIN SUCCESSFUL?     @SB010A0
         BZ    UXRTONLY            NO, THEN NO ESTAE TO CANCEL,@SB010A0
         TM    UXESTFLG,UXESTSUC   WAS ESTAE ESTABLISHED?      @SB010A0
         BNO   UXRTFREE      NO, SKIP ESTAE CANCEL, DO FREEMAIN@SB010A0
         ESTAE 0,TOKEN=UXTOKEN     CANCEL ESTAE                @SB010A0
UXRTFREE DS    0H                                              @SB010A0
         L     R13,UXSAVE+4        POINT TO OLD SAVE AREA      @SB010A0
         FREEMAIN  RC,LV=UXMAPLEN,A=(R4)  FREE THE STORAGE     @SB010A0
UXRTONLY DS    0H                                              @SB010A0
         LR    R15,R3              RESTORE RETURN CODE         @SB010A0
         L     R14,12(R13)         RESTORE RETURN ADDRESS      @SB010A0
         LM    R0,R12,20(R13)      RESTORE REMAINING REGS      @SB010A0
         BR    R14                 RETURN                      @SB010A0
*--------------------------------------------------------------@SB010A0
*        ESTAE RECOVERY EXIT                                   @SB010A0
*                                                              @SB010A0
*        THIS ROUTINE SAVES ABEND INFORMATION IN THE SDWA AND S@SB010A0
*        THE RETURN CODE FOR RTM INDICATING RETRY.  THIS ESTAE @SB010A0
*        RECOVERY ROUTINE IS USED IN CONJUNCTION WITH THE RETRY@SB010A0
*        ROUTINE, UXRETRY.  IF THIS RECOVERY ROUTINE IS CHANGED@SB010A0
*        CARE MUST BE TAKEN TO ENSURE THAT ASSUMPTIONS MADE BY @SB010A0
*        UXRETRY (E.G., REGISTER CONVENTIONS) ARE STILL VALID. @SB010A0
*                                                              @SB010A0
*        THIS ROUTINE DOES NOT RETRY WHEN IT IS ENTERED FOR THE@SB010A0
*        SECOND TIME.  THIS PREVENTS A LOOP CAUSED BY A RECURSI@SB010A0
*        ERROR.                                                @SB010A0
*                                                              @SB010A0
*        REGISTER INTERFACE TO RECOVERY ROUTINE (UXESTAE) DEPEN@SB010A0
*        ON WHETHER OR NOT RTM OBTAINED SDWA.  NORMALLY, AN SDW@SB010A0
*        WILL BE OBTAINED.                                     @SB010A0
*                                                              @SB010A0
*        TWO REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLES@SB010A0
*        SDWA STATUS:                                          @SB010A0
*               REGISTER 14 - RETURN ADDRESS                   @SB010A0
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RECO@SB010A0
*                             ROUTINE                          @SB010A0
*                                                              @SB010A0
*        IF SDWA WAS OBTAINED, REGISTERS UPON ENTRY TO UXESTAE @SB010A0
*               REGISTER 0  - A CODE INDICATING THE TYPE OF I/O@SB010A0
*                             PROCESSING PERFORMED             @SB010A0
*                             0  - ACTIVE I/O HAS BEEN QUIESCED@SB010A0
*                                  IS RESTORABLE               @SB010A0
*                             4  - ACTIVE I/O HAS BEEN HALTED A@SB010A0
*                                  NOT RESTORABLE              @SB010A0
*                             8  - NO ACTIVE I/O AT ABEND TIME @SB010A0
*                             16 - NO I/O PROCESSING WAS PERFOR@SB010A0
*               REGISTER 1  - ADDRESS OF SDWA                  @SB010A0
*               REGISTER 13 - SAVE AREA ADDRESS (72 BYTES)     @SB010A0
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB010A0
*               UNPREDICTABLE.                                 @SB010A0
*                                                              @SB010A0
*        IF SDWA WAS NOT OBTAINED, REGISTERS UPON ENTRY ARE:   @SB010A0
*               REGISTER 0  - A DECIMAL 12 INDICATING NO SDWA  @SB010A0
*               REGISTER 1  - ABEND COMPLETION CODE            @SB010A0
*               REGISTER 2  - ADDRESS OF USER-SUPPLIED PARAMETE@SB010A0
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB010A0
*               UNPREDICTABLE.                                 @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
UXESTAE  DS    0H                                              @SB010A0
         PUSH  USING               SAVE CURRENT ADDRESSABILITY @SB010A0
         DROP  ,                                               @SB010A0
         USING *,R15               SET BASE FOR ESTAE ROUTINE  @SB010A0
         CH    R0,=H'12'           ESTAE WORK AREA,SDWA AVAILAB@SB010A0
         BNE   UXSDWA              YES, SDWA AVAILABLE, TAKE BR@SB010A0
         SPACE 1                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        IF SDWA NOT PROVIDED, DETERMINE WHETHER TO RETRY.     @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB010A0
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB010A0
         BNO   UXRETRY1            YES, SET UP RETRY           @SB010A0
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB010A0
         LA    R15,RCNORTRY        DO NOT RETRY                @SB010A0
         BR    R14                 RETURN TO RTM               @SB010A0
         SPACE 1                                               @SB010A0
UXRETRY1 DS    0H                                              @SB010A0
         OI    UXESTFLG,UXESTREP   SET FLAG MARKING FIRST USE  @SB010A0
         LA    R0,UXRETRY          ADDRESS OF RETRY ROUTINE    @SB010A0
         LA    R15,RCRETRY         SET RETRY RETURN CODE FOR RT@SB010A0
         BR    R14                 RETURN TO RTM               @SB010A0
         SPACE 1                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        IF SDWA PROVIDED, UPDATE SDWA AND DETERMINE WHETHER TO@SB010A0
*        RETRY.                                                @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
UXSDWA   DS    0H                                              @SB010A0
         STM   R14,R12,12(R13)     SAVE REGS WHERE INDICATED BY@SB010A0
         LR    R4,R1               ESTABLISH ADDRESSABILITY . .@SB010A0
         USING SDWA,R4                  . . . TO SDWA          @SB010A0
         L     R2,SDWAPARM         LOAD ADDRESS OF PARM LIST   @SB010A0
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB010A0
         L     R10,UXBASE          CHANGE BASE REGISTER . . .  @SB010A0
         LA    R8,2048(R10)                                    @SB010A0
         LA    R8,2048(R8)                                     @SB010A0
         USING BDTUX10,R10,R8           . . . FOR THIS ROUTINE @SB010A0
         DROP  R15                                             @SB010A0
         SPACE 1                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        UPDATE THE SDWA                                       @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
         MVC   SDWAMODN(8),UXMODN  SET NAME OF LOAD MODULE     @SB010A0
         MVC   SDWACSCT(8),MDNUX10 SET NAME OF CSECT           @SB010A0
         MVC   SDWAREXN(8),MDNUX10 SET CSECT WITH RECOVERY ROUT@SB010A0
         L     R3,SDWAXPAD         LOAD SDWA EXTENSION POINTERS@SB010A0
         USING SDWAPTRS,R3         ESTABLISH ADDRESSABILITY    @SB010A0
         L     R3,SDWASRVP         LOAD RECORDABLE EXTENSION AD@SB010A0
         USING SDWARC1,R3          ESTABLISH ADDRESSABILITY    @SB010A0
         MVC   SDWACID(5),UXACID   SET COMPONENT ID            @SB010A0
         MVC   SDWASC(23),UXASC    SET NAME OF SUBCOMPONENT    @SB010A0
         MVC   SDWAMDAT(8),MDDUX10 SET DATE OF ASSEMBLY        @SB010A0
         MVC   SDWAMVRS(8),MDRUX10 SET PTF OR PRODUCT NUMBER   @SB010A0
         MVC   SDWARRL(8),UXRRL    IDENTIFY ROUTINE UPDATING SD@SB010A0
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB010A0
         BNO   UXRETRY2            YES, SET UP RETRY           @SB010A0
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB010A0
         SETRP RC=0,WKAREA=(R4)    DO NOT RETRY                @SB010A0
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB010A0
         BR    R14                 RETURN TO RTM               @SB010A0
         SPACE 1                                               @SB010A0
UXRETRY2 DS    0H                                              @SB010A0
         OI    UXESTFLG,UXESTREP   MARK FIRST USE              @SB010A0
         SETRP RC=4,RETADDR=UXRETRY,FRESDWA=YES,               @SB010A0*
               WKAREA=(R4)                                     @SB010A0
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB010A0
         BR    R14                 RETURN TO RTM               @SB010A0
         SPACE 1                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        POP THE USING STACK HERE TO RE-ESTABLISH USINGS IN EFF@SB010A0
*        PRIOR TO ESTAE RECOVERY ROUTINE.                      @SB010A0
*--------------------------------------------------------------@SB010A0
         SPACE 1                                               @SB010A0
         POP   USING                                           @SB010A0
         EJECT                                                 @SB010A0
         SPACE 1                                               @SB010A0
*--------------------------------------------------------------@SB010A0
*        RETRY ROUTINE FOR ESTAE                               @SB010A0
*                                                              @SB010A0
*        THIS ROUTINE IS USED IN CONJUNCTION WITH THE ESTAE REC@SB010A0
*        ROUTINE, UXESTAE.  THE RETRY ROUTINE 'FAILS' THE BDT  @SB010A0
*        REQUEST BY SETTING THE RETURN CODE TO 4 AND RETURNING @SB010A0
*        THE CALLING MODULE.  BEFORE BRANCHING TO THE COMMON EX@SB010A0
*        FOR THE MODULE, THE RETRY ROUTINE REESTABLISHES ADDRES@SB010A0
*        BILITY TO THE TVT.  (ADDRESSABILITY TO THE USER PARAME@SB010A0
*        LIST WAS REESTABLISHED BY THE 'POP USING' AT THE END O@SB010A0
*        THE ESTAE RECOVERY ROUTINE.  PARAMETER LIST IS BASED O@SB010A0
*        REGISTER 4.)                                          @SB010A0
*                                                              @SB010A0
*        THE REGISTER INTERFACE TO THE RETRY ROUTINE (UXRETRY) @SB010A0
*        DEPENDS ON THE CODING OF THE 'SETRP' MACRO IN THE RECO@SB010A0
*        ROUTINE (UXESTAE) AND ON WHETHER OR NOT RTM OBTAINED A@SB010A0
*        SDWA.  NORMALLY, AN SDWA WILL HAVE BEEN OBTAINED BUT T@SB010A0
*        THE REGISTER INTERFACE WHEN THERE WAS AN SDWA IS SET U@SB010A0
*        AS DESCRIBED BELOW ONLY BECAUSE THE 'SETRP' SPECIFIED @SB010A0
*        FREE SDWA & NO REGISTER UPDATE.  IF THESE PARAMETERS A@SB010A0
*        CHANGED ON THE 'SETRP', THE RETRY ROUTINE MUST ALSO BE@SB010A0
*        CHANGED.                                              @SB010A0
*                                                              @SB010A0
*        THE VALUE IN REGISTER 0 DEPENDS UPON WHETHER OR NOT RT@SB010A0
*        OBTAINED AN SDWA:                                     @SB010A0
*               REGISTER 0  - DECIMAL 20 IF SDWA WAS OBTAINED  @SB010A0
*                           - DECIMAL 12 IF SDWA WAS NOT OBTAIN@SB010A0
*                                                              @SB010A0
*        FOUR REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLE@SB010A0
*        SDWA STATUS:                                          @SB010A0
*               REGISTER 1  - ADDRESS OF USER PARAMETER LIST   @SB010A0
*                             ESTABLISHED USING ESTAE MACRO    @SB010A0
*               REGISTER 2  - POINTER TO PIRL IF I/O WAS QUIESC@SB010A0
*                             IS RESTORABLE; OTHERWISE ZERO    @SB010A0
*               REGISTER 14 - ADDRESS OF SUPERVISOR-ASSIGNED EX@SB010A0
*                             LINKAGE (SVC3)                   @SB010A0
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RETR@SB010A0
*                             ROUTINE                          @SB010A0
*               REGISTERS BESIDES 0,1,2,14 & 15 ARE UNPREDICTAB@SB010A0
*--------------------------------------------------------------@SB010A0
UXRETRY  DS    0H                                              @SB010A0
         LR    R4,R1               SET BASE REGISTER FOR PARAME@SB010A0
         L     R10,UXBASE          RESTORE MODULE BASE FOR EXIT@SB010A0
         L     R8,UXBASE2          SECOND BASE                 @SB010A0
         L     R12,UXTVTADD        RESTORE BASE FOR TVT        @SB010A0
         LA    R15,RCFAILTR        SET RETURN CODE FAILING BDT @SB010A0
         B     UXRETURN            RETURN TO CALLER            @SB010A0
*--------------------------------------------------------------@SB010A0
*        MODEL FOR ESTAE MACRO (LIST FORM). USED TO INITIALIZE @SB010A0
*        ACTUAL LIST REFERENCED IN EXECUTE FORM OF ESTAE MACRO.@SB010A0
*        DO NOT STORE INTO THIS AREA.                          @SB010A0
*--------------------------------------------------------------@SB010A0
ESTMODEL ESTAE MF=L                'MODEL' FOR ESTAE PARM LIST @SB010A0
*-------------------------------------------------------------*@SB010A0
*        CONSTANTS,LITERAL POOL, PATCH AREA                   *@SB010A0
*-------------------------------------------------------------*@SB010A0
UXGSDID  DC    CL4'GSD '            IDENTIFICATION FOR GSD     @SB010A0
UXWKCONS DC    CL4'WORK'            IDENTIFICATION FOR WORK ARE@SB010A0
UXMODN   DC    CL8'BDTUX10'         LOAD MODULE NAME           @SB010A0
UXRRL    DC    CL8'UXESTAE'         RECOVERY ROUTINE UPDATING S@SB010A0
UXACID   DC    CL5'30201'           COMPONENT ID               @SB010A0
UXASC    DC    CL23'BDT - USER EXIT BDTUX10'   DESCRIPTION     @SB010A0
         LTORG                                                 @SB010A0
         END   ,                                               @SB010A0
++SRCUPD (BDTUX25 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX25
         EJECT                                                 @SB010A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ FUNCTION:   AUTHORIZED CERTAIN TSO USERS TO ISSUE ALL       @SB010A0
*@             BDT COMMANDS AND JES3 COMMANDS VIA BDT          @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ DEPENDENCIES:                                               @SB010A0
*@             SB010A0 - BDTUX10 - SET AUTHORIZATION FLAGS     @SB010A0
*@             SB005A2 - BASE COMMAND AUTHORIZATON CODE        @SB010A0
*@                                                             @SB010A0
*@ CONTROL BLOCKS:                                             @SB010A0
*@             BDTDBSID  R/O                                   @SB010A0
*@             BDTXRU1 EQUATED TO UX10FLGA                     @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ CHANGE ACTIVITY                                             @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB010A0
*------------------------------------------------------------- @SB010A0
*      CHECK FLAGS SET IN UX10 THAT AUTHORIZE WHETHER THE      @SB010A0
*      USER IS AUTHORIZED TO ENTER ALL BDT CMDS AND JES3 CMDS. @SB010A0
*------------------------------------------------------------- @SB010A0
UX10FLGA EQU   BSIDXRU1,1,C'X'         1ST BYTE XOID USER FIELD@SB010A0
UX10BDTA EQU   X'01'                   UX10 AUTHRZD FOR BDT CMD@SB010A0
UX10JESA EQU   X'02'                   UX10 AUTHRZD FOR JES CMD@SB010A0
*                                                              @SB010A0
         TM    UX10FLGA,UX10BDTA       AUHTORIZED FOR ALL BDT? @SB010A0
         BO    UX25AUTH                SET AS AUTHORIZED       @SB010A0
         TM    UX10FLGA,UX10JESA       AUHTORIZED FOR JES CMD? @SB010A0
         BO    UX25AUTH                SET AS AUTHORIZED       @SB010A0
++SRCUPD (BDTUX31 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX31
         EJECT                                                 @SB010A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ FUNCTION:    AUTHORIZE SOME TSO USERS TO INQUIRE UPON       @SB010A0
*@              AND MODIFY ALL JOBS IN THE QUEUE               @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@ DEPENDENCIES:                                               @SB010A0
*@             SB010A0 - BDTUX10 - SETS AUTHORIZATION FLAGS    @SB010A0
*@                                                             @SB010A0
*@ INPUT:                                                      @SB010A0
*@             BSIDXRU1 EQUATED TO UX10FLGA                    @SB010A0
*@             UX10BDTA - USER AUTHORIZED FOR ALL COMMANDS     @SB010A0
*@ CONTROL BLOCKS:                                             @SB010A0
*@             BDTDBSID R/   BDTDXOID R/O                      @SB010A0
*@ CHANGE ACTIVITY                                             @SB010A0
*@                                                             @SB010A0
*@                                                             @SB010A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB010A0
UX10FLGA EQU   CMDXRU1,1,C'X'      AUTHORIZATION FLAG          @SB010A0
UX10BDTA EQU   X'01'               AUTHRZ FOR ALL BDT CMDS     @SB010A0
UX10JESA EQU   X'02'               AUTHRZ FOR JES3 CMDS        @SB010A0
         TM    UX10FLGA,UX10BDTA   AUTHORIZED FOR ALL CMDS?    @SB010A0
         BO    UXMAIN              YES, DON'T ISSUE MESSGE     @SB010A0
         TM    UX10FLGA,UX10BDTA   AUTHORIZED FOR ALL CMDS?    @SB010A0
         BO    UXBDT04             YES, DISPLAY THE JOB        @SB010A0
