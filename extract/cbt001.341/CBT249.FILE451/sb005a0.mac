++USERMOD(SB005A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     BDTUX31 BASE USER EXIT                             */.
++VER(Z038) FMID(HBD1102)
        /* END OF ++VER */  .
++JCLIN.
//LINK  EXEC PGM=IEWL,PARM='XREF,LIST,LET,NCAL,SIZE=(768K,100K),RENT'
//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(1,1))
//AOSBA    DD DISP=SHR,DSN=SYS1.AOSBA
//SYSLMOD  DD DISP=SHR,DSN=SYS1.JES3LIB
//SYSPRINT DD SYSOUT=A
//SYSLIN   DD *
 INCLUDE AOSBA(BDTUX31)             * INCLUDE ORIGINAL MODULE
 NAME    BDTUX31                    * LOAD MODULE NAME
/*
++SRC    (BDTUX31 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
BDTUX31  TITLE 'BDT SAMPLE AUTHORIZATION EXIT - RESPONSE AUTHORIZATION'
*START OF SPECIFICATIONS***********************************************
*                                                                     *
*    $MOD(BDTUX31)            PROD(BDT)                               *
*                                                                     *
*    MODULE-NAME: BDTUX31                                             *
*                                                                     *
*    DESCRIPTIVE-NAME: BDT COMMAND RESPONSE AUTHORIZATION EXIT -      *
*                      SAMPLE                                         *
*                                                                     *
*    COPYRIGHT: 5665-302 (C) COPYRIGHT IBM CORP., 1983                *
*               LICENSED MATERIAL PROGRAM                             *
*               PROPERTY OF IBM                                       *
*               REFER TO COPYRIGHT INSTRUCTIONS                       *
*               FORM NUMBER G120-2083                                 *
*                                                                     *
*    STATUS:  OS/VS2 HBD1102                                          *
*                                                                     *
*    FUNCTION:  BDTUX31 IS CALLED FROM ANY INQUIRY/MODIFY             *
*               COMMAND PROCESSOR TO VERIFY THAT A USER IS AUTHORIZED *
*               TO RECEIVE COMMAND RESPONSES.                         *
*                                                                     *
*    OPERATION: BDTUX31 ALLOWS THE INSTALLATION TO EXAMINE FIELDS     *
*               IN THE COMMAND XOID, TRANSACTION XOID, AND THE        *
*               JOB CONTROL TABLE.                                    *
*                                                                     *
*               THE SAMPLE EXIT WILL AUTHORIZE COMMANDS (RC=0)        *
*               IF:                                                   *
*                                                                     *
*                *  FROM AN MCS OR JES CONSOLE.                       *
*                                                                     *
*               THE SAMPLE EXIT WILL AUTHORIZE COMMANDS (RC=4)        *
*               IF:                                                   *
*                                                                     *
*                *  TSO SUBMITTED TRANSACTIONS                 @SB005A0
*                                                                     *
*               IF NONE OF THE ABOVE, COMMAND RESPONSE IS NOT         *
*               AUTHORIZED (RC = 8).                                  *
*                                                                     *
*               THE SAMPLE EXIT WILL REJECT COMMANDS (RC12)           *
*               IF:                                                   *
*                                                                     *
*                *  COMMAND ORIGIN IS BATCH JOB.                      *
*                                                                     *
*    RECOVERY-OPERATION:                                              *
*                                                                     *
*        AN ESTAE IS ESTABLISHED TO HANDLE MOST ABEND CONDITIONS      *
*        WHICH CAN ARISE DURING THIS EXIT.  BECAUSE TERM=NO IS        *
*        USED ON THE ESTAE MACRO INVOCATION, SOME ABEND CONDITIONS    *
*        WILL NOT RESULT IN THE SCHEDULING OF THIS MODULE'S ESTAE.    *
*        EXAMPLES ARE CANCEL BY OPERATOR AND FORCED LOGOFF.  SEE      *
*        PUBLICATIONS DESCRIBING ESTAE MACRO FOR FURTHER INFORMATION. *
*                                                                     *
*        NORMALLY, WHEN AN ABEND OCCURS, THIS MODULE IS REENTERED     *
*        AT THE RECOVERY ROUTINE, UXESTAE.  THE ABEND ENVIRONMENT     *
*        IS SAVED IN THE GSD & THE SDWA, IF AVAILABLE.  THE MODULE    *
*        IS THEN REENTERED AT THE RETRY ROUTINE, UXRETRY.  UXRETRY    *
*        SETS THE RETURN CODE SO THE BDT REQUEST IS UNAUTHORIZED AND  *
*        THEN RETURNS TO THE CALLER.  THE RETRY ROUTINE WILL NOT      *
*        BE ENTERED IF THE ESTAE HAS BEEN EXECUTED BEFORE (MARKED     *
*        BY A FLAG).  THIS PREVENTS RECURSIVE ERRORS FROM CAUSING     *
*        A LOOP.  INSTEAD, IT WILL PERCOLATE.                         *
*                                                                     *
*    NOTES:                                                           *
*                                                                     *
*        DEPENDENCIES:  NONE                                          *
*                                                                     *
*        RESTRICTIONS:  NONE                                          *
*                                                                     *
*        REGISTER-CONVENTIONS:  SEE BDTDREG MACRO                     *
*                                                                     *
*        REGISTERS-SAVED:  REGISTERS 12,13 & 14. (MODULE IN-          *
*                   VOKED VIA BDTXUEX MACRO WHICH SAVES ALL           *
*                   REGISTERS.)                                       *
*                                                                     *
*        REGISTER-USAGE:                                              *
*                   REGISTER 0 = WORK REGISTER                        *
*                   REGISTER 1 = POINTER TO PARAMETER LIST            *
*                   REGISTER 2 = WORK REGISTER                        *
*                   REGISTER 3 = WORK REGISTER                        *
*                   REGISTER 4 = WORK REGISTER                        *
*                   REGISTER 5 = WORK REGISTER                        *
*                   REGISTER 6 = WORK REGISTER                        *
*                   REGISTER 7 = TRANSACTION XOID BASE                *
*                   REGISTER 8 = COMMAND XOID BASE                    *
*                   REGISTER 9 = JCT  BASE                            *
*                   REGISTER 10= MODULE BASE                          *
*                   REGISTER 11= NOT IN USE                           *
*                   REGISTER 12= TVT BASE                             *
*                   REGISTER 13= SAVE AREA SET UP BY ASAVE PROCESS    *
*                   REGISTER 14= RETURN ADDRESS                       *
*                   REGISTER 15= ENTRY POINT/RETURN CODE              *
*                                                                     *
*        REGISTERS-RESTORED:  REGISTERS 12, 13 & 14 TO ESTAB-         *
*                   LISH ADDRESSABILITY TO ASAVE ROUTINE              *
*                   WHICH RESTORES OTHER REGISTERS AND                *
*                   RETURNS TO CALLER.                                *
*                                                                     *
*        PATCH-LABEL:  PTCHUX31                                       *
*                                                                     *
*    MODULE-TYPE:  PROCEDURE                                          *
*                                                                     *
*        PROCESSOR:  ASSEMBLER-H                                      *
*                                                                     *
*        MODULE-SIZE:                                                 *
*                                                                     *
*        ATTRIBUTES:                                                  *
*            STATE:  SUPERVISOR                                       *
*            KEY:    8                                                *
*            TYPE:   REENTRANT                                        *
*            AMODE:  24                                               *
*            RMODE:  24                                               *
*            MODE:   TASK                                             *
*                                                                     *
*    ENTRY POINT:  BDTUX31                                            *
*                                                                     *
*        PURPOSE:  BEGINNING OF CONTROL SECTION                       *
*                                                                     *
*        LINKAGE:  BDTUX31 IS CALLED BY BDTIQDV ON BEHALF OF A        *
*                  COMMAND PROCESSING MODULE.                         *
*                                                                     *
*            ENTRY-REGISTERS:                                         *
*                REGISTER 1  - POINTER TO PARAMETER LIST              *
*                REGISTER 12 - POINTER TO THE TVT                     *
*                REGISTER 13 - POINTER TO SAVE AREA                   *
*                REGISTER 14 - RETURN ADDRESS                         *
*                REGISTER 15 - BASE REGISTER                          *
*                                                                     *
*    INPUT:  PARAMETER LIST CONTAINING:                               *
*                                                                     *
*            1)  ADDRESS OF COMMAND XOID                              *
*            2)  ADDRESS OF TRANSACTION XOID                          *
*            3)  LENGTH OF COMMAND TEXT                               *
*            4)  ADDRESS OF COMMAND TEXT                              *
*            5)  ADDRESS OF JCT                                       *
*            6)  LENGTH OF MESSAGE AREA                               *
*            7)  ADDRESS OF MESSAGE AREA                              *
*            8)  ADDRESS OF ONE WORD USER FIELD                       *
*                                                                     *
*    OUTPUT: RETURN CODE IN REGISTER 15                               *
*                                                                     *
*    EXIT-NORMAL:  RETURN TO CALLER VIA BR R14                        *
*                                                                     *
*        EXIT REGISTERS:  REGISTER 12 = POINTER TO THE TVT            *
*                         REGISTER 13 = POINTER TO SAVE AREA          *
*                         REGISTER 14 = RETURN ADDRESS                *
*                         REGISTER 15 = RETURN CODE 0 OR 4            *
*                                                                     *
*    EXIT-ERROR:  ESTAE EXIT RECEIVES CONTROL TO HANDLE ABEND, SETS   *
*        UP RETRY ROUTINE.  RETRY 'FAILS' THE BDT REQUEST BY SETTING  *
*        RETURN CODE.  WILL NOT RETRY IF ESTAE ROUTINE ENTERED FOR    *
*        SECOND TIME.                                                 *
*                                                                     *
*        EXIT REGISTERS:  REGISTER 12 = POINTER TO THE TVT            *
*                         REGISTER 13 = POINTER TO SAVE AREA          *
*                         REGISTER 14 = RETURN ADDRESS                *
*                         REGISTER 15 = RETURN CODE 8                 *
*                                                                     *
*    EXIT-USER:  NONE                                                 *
*                                                                     *
*    EXTERNAL REFERENCES:                                             *
*                                                                     *
*        ROUTINES:  NONE                                              *
*        DATA AREAS:  SEE CONTROL BLOCKS                              *
*        CONTROL BLOCKS:    BDTDTVT  READ ONLY                        *
*                           IHAPSA   READ ONLY                        *
*                           IKJTCB   READ ONLY                        *
*                           BDTDGSD  READ/WRITE                       *
*                           IHASDWA  READ/WRITE                       *
*                                                                     *
*    TABLES:  SEE CONTROL BLOCKS                                      *
*                                                                     *
*    BDT EXECUTABLE MACROS: BDTDMOD                                   *
*                           BDTXASRV                                  *
*                                                                     *
*    MVS EXECUTABLE-MACROS: ESTAE                                     *
*                           SETRP                                     *
*                           GETMAIN                                   *
*                           FREEMAIN                                  *
*                                                                     *
*    BDT NON-EXECUTABLE MACROS:                                       *
*                           BDTDGSD                                   *
*                           BDTDJCT                                   *
*                           BDTDREG                                   *
*                           BDTDXOID                                  *
*                           BDTDTVT                                   *
*                                                                     *
*    MVS NON-EXECUTABLE MACROS:                                       *
*                           IHASDWA                                   *
*                           IKJTCB                                    *
*                           IHAPSA                                    *
*                                                                     *
*    ABEND-CODES: NONE                                                *
*                                                                     *
*    CHANGE-ACTIVITY:                                                 *
*                                                                     *
*      $L0 =                                                          *
*                                                                     *
*END OF SPECIFICATIONS*************************************************
         EJECT
BDTUX31  START
         TITLE 'BDTDJCT - BDT JOB CONTROL TABLE'
         BDTDJCT
         TITLE 'BDTDXOID - TRANSACTION ORIGIN ID(COMMAND)'
CMDXOID1 DSECT
         BDTDXOID CMD
         TITLE 'BDTDXOID - TRANSACTION ORIGIN ID(TRANSACTION)'
TRNXOID1 DSECT
         BDTDXOID TRN
         TITLE 'BDTDTVT - TRANSFER VECTOR TABLE'
         BDTDTVT
         TITLE 'BDTDGSD - GENERALIZED SUBTASK DIRECTORY PREFIX'
         BDTDGSD
         TITLE 'IHASDWA - SYSTEM DIAGNOSTIC WORK AREA'
         IHASDWA
         TITLE 'IHAPSA - PREFIXED SAVE AREA'
         IHAPSA
         TITLE 'IKJTCB - TASK CONTROL BLOCK'
         IKJTCB LIST=YES
         TITLE 'BDTDREG - SYMBOLIC EQUATES FOR REGISTERS'
         BDTDREG
         TITLE 'BDTUX31 - USER EQUATES AND WORKING STORAGE DSECT'
*--------------------------------------------------------------------*
*        USER EQUATES                                                *
*--------------------------------------------------------------------*
         SPACE 1
RC0      EQU   0                   RETURN CODE 0  = AUTHORIZED
RC4      EQU   4                   RETURN CODE 4  = AUTHORIZED
RC8      EQU   8                   RETURN CODE 8  = UNAUTHORIZED
RC12     EQU   12                  RETURN CODE 12 = UNAUTHORIZED
ZERO     EQU   0                   USE TO INITIALIZE STORAGE
         SPACE 1
*--------------------------------------------------------------------*
*        USER DSECT FOR WORK AREA                                    *
*                                                                    *
*        WORK AREA IS PASSED TO ESTAE MACRO AS USER PARAMETER LIST.  *
*        THIS IS NECESSARY SO THAT, IF AN ABEND OCCURS, ADDRESSA-    *
*        BILITY TO THE AREA CAN BE REESTABLISHED.  DSECT CONTAINS    *
*        FIELDS WHICH ARE NEEDED IN ESTAE RECOVERY ROUTINE, RETRY    *
*        ROUTINE OR COMMON EXIT FROM MODULE.                         *
*                                                                    *
*        NOTE:  IF ADDITIONAL WORK AREA IS REQUIRED, ADD IT TO THIS  *
*        DSECT (PREFERABLY BETWEEN UXESTLEN AND UXMAPLEN), SO        *
*        ADDITIONAL GETMAINS ARE NOT NECESSARY.                      *
*--------------------------------------------------------------------*
         SPACE 1
UXPARMAP DSECT                     MAP OF USER PARAMETER LIST
         SPACE 1
UXWORKID DS    F                   CONTAINS 'WORK' FOR IDENTIFICATION
UXTVTADD DS    F                   ADDRESS OF TVT,SAVED FOR RETRY
UXGSDADD DS    F                   ADDRESS OF GSD,SAVED FOR ABEND
UXSAVADD DS    F                   SAVE AREA (REG 13)
UXRTNADD DS    F                   RETURN ADDRESS (REG 14)
UXBASE   DS    F                   MODULE BASE ADDR,SAVED FOR RETRY
UXTOKEN  DS    F                   TOKEN USED IN ESTAE
UXESTFLG DS    B                   INDICATES ESTAE STATUS
UXESTSUC EQU   BIT0                ESTAE ESTABLISHED SUCCESSFULLY
UXESTREP EQU   BIT1                ESTAE RECOVERY ALREADY ENTERED
UXESTAPM DS    0F                  ESTAE MACRO PARAMETER LIST
         ESTAE MF=L
UXESTLEN EQU   *-UXESTAPM          LENGTH OF ESTAE LIST
UXMAPLEN EQU   *-UXPARMAP          LENGTH OF THIS MAPPING
         SPACE 1
*--------------------------------------------------------------------*
*        DSECT FOR PARAMETER LIST PASSED FORM CALLER                 *
*--------------------------------------------------------------------*
         SPACE 1
UXPARMIN DSECT                     PARAMETER LIST PASSED FROM CALLER
UXCXOID  DS    A                   ADDRESS OF COMMAND XOID
UXXXOID  DS    A                   ADDRESS OF XACTION XOID
UXCMDLEN DS    A                   COMMAND LENGTH
UXCMDTXT DS    A                   ADDRESS OF COMMAND
UXJCT    DS    A                   ADDRESS OF JCT,ELSE ZERO
UXMSGLEN DS    A                   MSG FIELD LENGTH
UXMSGFLD DS    A                   ADDRESS OF MESSAGE FIELD
UXUSER   DS    A                   ADDRESS OF ONE WORD USER AREA
         SPACE 1
*--------------------------------------------------------------------*
*        DSECT FOR USER AREA PASSED FROM CALLER                      *
*--------------------------------------------------------------------*
         SPACE 1
UXWORK   DSECT                     ONE WORD WORK AREA PASSED
UXWORK1  DS    X                   BYTE 1
         SPACE 1
UXREASON EQU   UXWORK1,*-UXWORK1,C'X'    REASON CODE
UXRSN$IA EQU   0                   I A          COMMAND
UXRSN$ID EQU   4                   I DSN=       COMMAND
UXRSN$IJ EQU   8                   I J=         COMMAND
UXRSN$IP EQU   12                  I P=         COMMAND
UXRSN$IQ EQU   16                  I Q          COMMAND
UXRSN$IN EQU   20                  I NET        COMMAND
UXRSN$FJ EQU   128                 F J=         COMMAND
UXRSN$FN EQU   132                 F NET        COMMAND
UXRSNEND EQU   252                 LAST CALL ON COMMAND
         SPACE 1
UXWORK2  DS    X                   BYTE 2
UXRC04   EQU   BIT0                RC 4 WAS ISSUED
UXMSGOFF EQU   BIT0                NO USER EXIT MESSAGES TO BE ISSUED
UXRC08   EQU   BIT1                RC 8 WAS ISSUED
         SPACE 1
UXWORK3  DS    X                   BYTE 3
UXWORK4  DS    X                   BYTE 4
         SPACE 1
         TITLE 'BDTUX31 --  AUTHORIZATION EXIT - SAMPLE'
BDTUX31  CSECT
         SPACE 1
         B     ENDCATCH-*(,R15)    BR AROUND ENTRY INFORMATION
         DC    AL1(ENDCATCH-*-1)   LENGTH OF ENTRY INFORMATION
MDNUX31  DC    CL8'BDTUX31'        MODULE NAME
MDLUX31  DC    CL8'       '        LABEL NAME
MDRUX31  DC    CL9' HBD1102 '      BDT RELEASE OR PTF NUMBER
MDDUX31  DC    CL8'&SYSDATE'       ASSEMBLY DATE
MDTUX31  DC    CL6'-&SYSTIME'      ASSEMBLY TIME
         SPLEVEL SET=1
ENDCATCH DS    0H
         SPACE 1
*--------------------------------------------------------------------*
*        ESTABLISH ADDRESSABILITY                                    *
*                                                                    *
*        REGISTERS ARE NOT SAVED HERE BECAUSE THE CALLING PROGRAM    *
*        SAVES THEM VIA BDTXUEX.                                     *
*--------------------------------------------------------------------*
         SPACE 1
UXINIT   DS    0H
         LR    R10,R15             SET BASE REGISTER
         USING BDTUX31,R10         ESTABLISH ADDRESSABILITY
         SPACE 1
         LR    R2,R1               SET BASE REGISTER
         USING UXPARMIN,R2         ESTABLISH ADDRESSABILITY TO PARM
         SPACE 1
         L     R11,UXUSER          SET BASE FOR WORK AREA
         USING UXWORK,R11          ESTABLISH ADDRESSABILITY TO WORK
         STC   R0,UXREASON         SAVE REASON CODE PASSED
         SPACE 1
         L     R9,UXJCT            SET BASE FOR JCT
         USING JCTSTART,R9         ESTABLISH ADDRESSABILITY TO JCT
         SPACE 1
         L     R8,UXCXOID          SET BASE FOR COMMAND XOID
         USING CMDXOID1,R8         ESTABLISH ADDRESSABILITY TO XOID
         SPACE 1
         L     R7,UXXXOID          SET BASE FOR TRANSACTION XOID
         USING TRNXOID1,R7         ESTABLISH ADDRESSABILITY TO XOID
         SPACE 1
*--------------------------------------------------------------------*
*        GETMAIN WORK AREA                                           *
*                                                                    *
*        IF GETMAIN IS UNSUCCESSFUL, SET REGISTER 4 TO ZERO TO       *
*        INDICATE FAILURE, SKIP ESTAE SETUP AND BEGIN                *
*                                                                    *
*        MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER RECOVERY       *
*        ROUTINE ESTABLISHED FOR CALLER.                             *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R4,UXMAPLEN         LENGTH OF AREA NEEDED
         GETMAIN RC,LV=(R4)        GET STORAGE FOR PARAMETER LIST
         LTR   R15,R15             WAS STORAGE OBTAINED?
         BZ    UXGETOK             YES, INITIALIZE STORAGE
         XR    R4,R4               NO, INDICATE GETMAIN FAILURE
         B     UXMAIN              SKIP ESTAE SETUP
UXGETOK  DS    0H
         LR    R4,R1               SET BASE FOR PARM LIST
         USING UXPARMAP,R4         ESTABLISH ADDRESSABILITY TO PARMS
         MVI   UXPARMAP,ZERO       SET PAD CHARACTER TO ZERO
*                                  INITIALIZE STORAGE TO ZEROES
         MVC   UXPARMAP+1(UXMAPLEN),UXPARMAP
         MVC   UXWORKID,UXWKCONS   SET IDENTIFICATION TO 'WORK'
         SPACE 1
*---------------------------------------------------------------------*
*        ESTABLISH AN ESTAE                                           *
*                                                                     *
*        1. BUILD USER PARAMETER LIST.                                *
*        2. INITIALIZE ESTAE MACRO LIST;  ISSUE ESTAE.                *
*        3. IF ESTAE SUCCESSFUL, SET BIT 0 IN 'ESTAE SUCCESS FLAG'    *
*           TO 1.                                                     *
*        4. IF ESTAE IS UNSUCCESSFUL, LEAVE BIT 0 IN 'ESTAE SUCCESS   *
*           FLAG' EQUAL TO ZERO.  (FLAG CONTAINS ALL ZEROES AS A      *
*           RESULT OF STORAGE INITIALIZATION AFTER GETMAIN.)          *
*           BEGIN MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER        *
*           RECOVERY ESTABLISHED FOR CALLER.                          *
*---------------------------------------------------------------------*
         SPACE 1
         ST    R12,UXTVTADD        SAVE TVT ADDRESS AS PARAMETER
         ST    R13,UXSAVADD        SAVE SAVE-AREA ADDRESS
         ST    R14,UXRTNADD        SAVE RETURN ADDRESS
         USING PSA,R0              ESTABLISH ADDRESSABILITY TO PSA
         L     R3,PSATOLD          SET BASE FOR TCB
         USING TCB,R3              ESTABLISH ADDRESSABILITY TO TCB
         L     R3,TCBBDT           SET BASE FOR GSD
         USING GSDSTART,R3         ESTABLISH ADDRESSABILITY TO GSD
         CLC   GSDID,UXGSDID       IS THERE A GSD?
         BNE   UXNOESTA            NO, SKIP ESTAE SET UP
         ST    R3,UXGSDADD         STORE ADDRESS OF GSD IN PARM LIST
         ST    R10,UXBASE          STORE MODULE BASE FOR RETRY
*                                  MOVE ESTAE MODEL OVER STORAGE AREA
         MVC   UXESTAPM(UXESTLEN),ESTMODEL
         ESTAE UXESTAE,CT,PARAM=(R4),RECORD=YES,                       +
               TOKEN=UXTOKEN,MF=(E,UXESTAPM)
         LTR   R15,R15             WAS ESTAE SUCCESSFUL?
         BNZ   UXNOESTA            NO, DO NOT SET FLAG
         OI    UXESTFLG,UXESTSUC   SET FLAG MARKING SUCCESSFUL ESTAE
UXNOESTA DS    0H
         DROP  R0,R3
         EJECT
         SPACE 1
*---------------------------------------------------------------------*
*        PRINT HEADER MESSAGE FOR RESTRICTED USERS                    *
*                                                                     *
*        IF COMMAND IS FROM A TSO USER,                               *
*        DISPLAY A MESSAGE ON FIRST CALL TO INFORM HIM                *
*        THAT SOME RESPONSES ARE NOT AUTHORIZED FOR DISPLAY.          *
*---------------------------------------------------------------------*
         SPACE 1
UXHEADER DS    0H
         CLI   UXWORK2,X'00'       COMPARE WORK FLAG FOR ZERO
         BNE   UXMAIN              IF NOT ZERO, NOT FIRST CALL
         SPACE 1
         CLI   CMDXTYP,CMDTSO      SOURCE IS TSO TERMINAL?
         BNE   UXMAIN              NO,  CONTINUE
         SPACE 1
*        LA    R1,UXMSG01          YES, MOVE HEADER MESSAGE    @SB005A0
*        BAL   R3,UXMESSG          TO MESSAGE AREA             @SB005A0
         SPACE 1
*---------------------------------------------------------------------*
*        TEST BDT COMMANDS HERE                                       *
*                                                                     *
*        THE SAMPLE BELOW TREATS BDT COMMANDS FROM JES OR MCS         *
*        CONSOLES AS AUTHORIZED.  IF NOT FROM JES OR MCS, COMMAND     *
*        XOID IS COMPARED TO TRANSACTION XOID. IF EQUAL, THEN         *
*        COMMAND IS AUTHORIZED.                                       *
*                                                                     *
*        REPLACE THIS SECTION WITH INSTALLATION-DEFINED               *
*        AUTHORIZATION TESTS.                                         *
*---------------------------------------------------------------------*
         SPACE 1
UXMAIN   DS    0H
         TM    UXREASON,UXRSNEND   ENTRY FOR LAST CALL
         BO    UXCLEAN             YES - CLEAN UP AND EXIT
         SPACE 1
         CLI   CMDXTYP,CMDJES      SOURCE IS JES CONSOLE?
         BE    UXBDT00             YES, BRANCH
         SPACE 1
         CLI   CMDXTYP,CMDMCS      SOURCE IS MCS CONSOLE?
         BE    UXBDT00             YES, BRANCH
         SPACE 1
         CLI   CMDXTYP,CMDBTCH     SOURCE IS BATCH JOB?
         BE    UXBDT12             YES, REJECT
         SPACE 1
         CLI   CMDXTYP,CMDTSO      SOURCE IS TSO TERMINAL?
         BE    UXCMDTSO            YES, COMPARE XOIDS
         SPACE 1
         B     UXBDT12             NO , REJECT
UXCMDTSO DS    0H
         B     UXBDT04             ATHORIZE ALL                @SB005A0
         CLC   CMDXOID,TRNXOID     COMPARE XOIDS
         BE    UXBDT04             IF EQUAL, AUTHORIZE
         B     UXBDT08             IF NOT, REJECT THIS ITEM
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE RETURN CODES AND SET WORK FLAGS                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
UXBDT00  DS    0H
         LA    R15,RC0             COMMAND RESPONSE IS UNAUTHORIZED
         B     UXRETURN            RETURN TO CALLER
         SPACE 1
UXBDT04  DS    0H
         OI    UXWORK2,UXRC04      INDICATE A  COMMAND
         LA    R15,RC4             RESPONSE IS AUTHORIZED
         B     UXRETURN            RETURN TO CALLER
         SPACE 1
UXBDT08  DS    0H
         OI    UXWORK2,UXRC08      INDICATE A  COMMAND
         LA    R15,RC8             RESPONSE IS UNAUTHORIZED
         B     UXRETURN            RETURN TO CALLER
         SPACE 1
UXBDT12  DS    0H
         LA    R15,RC12            COMMAND IS REJECTED
         LA    R1,UXMSG03          GET ADDRESS OF MESSAGE TO ISSUE
         BAL   R3,UXMESSG          AND MOVE IT TO MESSAGE AREA
         B     UXRETURN            RETURN TO CALLER
         SPACE 1
*--------------------------------------------------------------------*
*        CLEAN UP AFTER LAST CALL PROCESSING                         *
*--------------------------------------------------------------------*
         SPACE 1
UXCLEAN  DS    0H
         TM    UXWORK2,UXMSGOFF    NO MESSAGE TO BE ISSUED?
         BO    UXRETURN            YES - SIMPLY RETURN
         SPACE 1
         LA    R1,UXMSG02          NO  - GET ITS ADDRESS
         BAL   R3,UXMESSG          RETURN TO CALLER
         SPACE 1
*--------------------------------------------------------------------*
*        COMMON EXIT ROUTINE - THIS EXIT ROUTINE IS ALWAYS USED      *
*        TO RETURN TO CALLER OF MODULE (I.E., NORMAL EXIT AND        *
*        EXIT AFTER ESTAE RETRY ROUTINE).                            *
*                                                                    *
*        REGISTER 4 AND UXESTFLG (SUCCESSFUL ESTAE FLAG) ARE TESTED  *
*        TO DETERMINE WHETHER TO CANCEL ESTAE AND INVOKE FREEMAIN.   *
*                                                                    *
*        1.  IF REG 4 IS ZERO, GETMAIN WAS UNSUCCESSFUL AND THERE    *
*            IS NO ESTAE TO CANCEL.  REGISTERS 12, 13 & 14           *
*            COULD NOT HAVE BEEN DESTROYED IN ESTAE ROUTINE.         *
*        2.  IF REG 4 IS NOT ZERO, SAVE ADDRESSES FOR                *
*            RESTORATION. TEST FOR SUCCESSFUL ESTAE.                 *
*            A.  IF UXESTSUC IS OFF, THERE IS NO ESTAE TO CANCEL.    *
*                DO FREEMAIN, RESTORE REGISTERS & RETURN.            *
*            B.  IF UXESTSUC IS ON, CANCEL ESTAE, FREEMAIN,          *
*                RESTORE REGISTERS & RETURN.                         *
*--------------------------------------------------------------------*
         SPACE 1
UXRETURN DS    0H
         LTR   R4,R4               WAS GETMAIN SUCCESSFUL?
         BZ    UXRTONLY            NO, THEN NO ESTAE TO CANCEL, RETURN
         LR    R3,R15              TEMPORARILY SAVE RETURN CODE
         L     R12,UXTVTADD        RESTORE BASE FOR TVT
         L     R5,UXRTNADD         SAVE RETURN ADDRESS
         L     R6,UXSAVADD         SAVE SAVE-AREA ADDRESS
         TM    UXESTFLG,UXESTSUC   WAS ESTAE ESTABLISHED?
         BNO   UXRTFREE            NO, SKIP ESTAE CANCEL, DO FREEMAIN
         ESTAE 0,TOKEN=UXTOKEN     CANCEL ESTAE
UXRTFREE DS    0H
         FREEMAIN  RC,LV=UXMAPLEN,A=(R4)  FREE THE STORAGE
         LR    R13,R6              RESTORE SAVE AREA ADDRESS
         LR    R14,R5              RESTORE RETURN ADDRESS
         LR    R15,R3              RESTORE RETURN CODE
UXRTONLY DS    0H
         BR    R14                 RETURN TO CALLER
         EJECT
         SPACE 1
*---------------------------------------------------------------------*
*        MESSAGE SUBROUTINE                                           *
*                                                                     *
*        THIS SUBROUTINE MOVE A MESSAGE FROM A USER                   *
*        DATA AREA TO THE BDT SUPPLIED MESSAGE BUFFER.                *
*                                                                     *
*        INPUTS:                                                      *
*        R1 = ADDRESS OF MESSAGE TO BE MOVED                          *
*        R3 = ADDRESS OF RETURN TO CALLER                             *
*---------------------------------------------------------------------*
         SPACE 1
UXMESSG  DS    0H
         SLR   R6,R6               INITIALIZE WORK REGISTER
         IC    R6,0(R1)            GET LENGTH OF MESSAGE
         L     R5,UXMSGLEN         GET LENGTH OF MESSAGE AREA
         BCTR  R5,R0               ADJUST ITS LENGTH
         SPACE 1
         CR    R6,R5               MESSAGE > THAN MAX ALLOWED
         BH    UXMOVE              YES -
         SPACE 1
         LR    R5,R6               USE MAX LENGTH
UXMOVE   DS    0H
         L     R6,UXMSGFLD         GET ADDRESS OF MESSAGE AREA
         EX    R5,MOVEMSG          MOVE ERROR MESSAGE INTO IT
         BR    R3                  RETURN TO CALLER
         SPACE 1
*--------------------------------------------------------------------*
*        EXECUTE INSTRUCTION:                                        *
*        MOVE MESSAGE TO PASSED AREA.                                *
*--------------------------------------------------------------------*
         SPACE 1
MOVEMSG  MVC   0(0,R6),0(R1)       MOVE ERROR MESSAGE
         EJECT
         SPACE 1
*---------------------------------------------------------------------*
*        ESTAE RECOVERY EXIT                                          *
*                                                                     *
*        THIS ROUTINE SAVES ABEND INFORMATION IN THE GSD AND SDWA     *
*        AND SETS RETURN CODE FOR RTM INDICATING RETRY.  THIS ESTAE   *
*        RECOVERY ROUTINE IS USED IN CONJUNCTION WITH THE RETRY       *
*        ROUTINE, UXRETRY.  IF THIS RECOVERY ROUTINE IS CHANGED       *
*        CARE MUST BE TAKEN TO ENSURE THAT ASSUMPTIONS MADE BY        *
*        UXRETRY (E.G., REGISTER CONVENTIONS) ARE STILL VALID.        *
*                                                                     *
*        THIS ROUTINE DOES NOT RETRY WHEN IT IS ENTERED FOR THE       *
*        SECOND TIME.  THIS PREVENTS A LOOP CAUSED BY A RECURSIVE     *
*        ERROR.                                                       *
*                                                                     *
*        REGISTER INTERFACE TO RECOVERY ROUTINE (UXESTAE) DEPENDS     *
*        ON WHETHER OR NOT RTM OBTAINED SDWA.  NORMALLY, AN SDWA      *
*        WILL BE OBTAINED.                                            *
*                                                                     *
*        TWO REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLESS OF   *
*        SDWA STATUS:                                                 *
*               REGISTER 14 - RETURN ADDRESS                          *
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RECOVERY   *
*                             ROUTINE                                 *
*                                                                     *
*        IF SDWA WAS OBTAINED, REGISTERS UPON ENTRY TO UXESTAE ARE:   *
*               REGISTER 0  - A CODE INDICATING THE TYPE OF I/O       *
*                             PROCESSING PERFORMED                    *
*                             0  - ACTIVE I/O HAS BEEN QUIESCED AND   *
*                                  IS RESTORABLE                      *
*                             4  - ACTIVE I/O HAS BEEN HALTED AND IS  *
*                                  NOT RESTORABLE                     *
*                             8  - NO ACTIVE I/O AT ABEND TIME        *
*                             16 - NO I/O PROCESSING WAS PERFORMED    *
*               REGISTER 1  - ADDRESS OF SDWA                         *
*               REGISTER 13 - SAVE AREA ADDRESS (72 BYTES)            *
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 ARE    *
*               UNPREDICTABLE.                                        *
*                                                                     *
*        IF SDWA WAS NOT OBTAINED, REGISTERS UPON ENTRY ARE:          *
*               REGISTER 0  - A DECIMAL 12 INDICATING NO SDWA         *
*               REGISTER 1  - ABEND COMPLETION CODE                   *
*               REGISTER 2  - ADDRESS OF USER-SUPPLIED PARAMETER LIST *
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 ARE    *
*               UNPREDICTABLE.                                        *
*---------------------------------------------------------------------*
         SPACE 1
UXESTAE  DS    0H
         PUSH  USING               SAVE CURRENT ADDRESSABILITY
         DROP
         USING *,R15               SET BASE FOR ESTAE ROUTINE
         CH    R0,=H'12'           ESTAE WORK AREA,SDWA AVAILABLE?
         BNE   UXSDWA              YES, SDWA AVAILABLE, TAKE BRANCH
         SPACE 1
*---------------------------------------------------------------------*
*        IF SDWA NOT PROVIDED, GET GSD ADDRESS FROM PARAMETER LIST    *
*        BASED ON R2 (PASSED BY ESTAE), SAVE ABEND CODE IN GSD.  DO   *
*        NOT USE MACRO BDTXASRV TO UPDATE GSD BECAUSE IT RELIES ON    *
*        THE SDWA.  DETERMINE WHETHER TO RETRY.                       *
*---------------------------------------------------------------------*
         SPACE 1
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIST
         L     R3,UXGSDADD         RESTORE GSD BASE ADDRESS
         USING GSDSTART,R3         ESTABLISH BASE FOR GSD
         ST    R1,GSDABCC          SAVE ABEND CODE IN GSD
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?
         BNO   UXRETRY1            YES, SET UP RETRY
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE
         LA    R15,RC0             DO NOT RETRY
         BR    R14                 RETURN TO RTM
         SPACE 1
UXRETRY1 DS    0H
         OI    UXESTFLG,UXESTREP   SET FLAG MARKING FIRST USE
         LA    R0,UXRETRY          ADDRESS OF RETRY ROUTINE
         LA    R15,RC4             SET RETRY RETURN CODE FOR RTM
         BR    R14                 RETURN TO RTM
         SPACE 1
*---------------------------------------------------------------------*
*        IF SDWA PROVIDED, GET GSD ADDRESS FROM PARAMETER LIST BASED  *
*        ON SDWAPARM, RECORD ABEND ENVIRONMENT IN GSD AND SDWA.       *
*        DETERMINE WHETHER TO RETRY.                                  *
*---------------------------------------------------------------------*
         SPACE 1
UXSDWA   DS    0H
         STM   R14,R12,12(R13)     SAVE REGS WHERE INDICATED BY RTM
         LR    R4,R1               ESTABLISH ADDRESSABILITY . . .
         USING SDWA,R4                  . . . TO SDWA
         L     R2,SDWAPARM         LOAD ADDRESS OF PARM LIST
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIST
         L     R10,UXBASE          CHANGE BASE REGISTER . . .
         USING BDTUX31,R10              . . . FOR THIS ROUTINE
         DROP  R15
         L     R3,UXGSDADD         RESTORE GSD BASE ADDRESS
         USING GSDSTART,R3         ESTABLISH BASE FOR GSD
         SPACE 1
*---------------------------------------------------------------------*
*        UPDATE THE GSD                                               *
*                                                                     *
*        BDTXASRV PROVIDES COMMON ABEND SERVICES INCLUDING UPDATING   *
*        THE GSD. (NOTE: THIS SERVICE SAVES AND RESTORES THE CALLER'S *
*        REGISTERS USING AN AREA IN THE GSD.)                         *
*---------------------------------------------------------------------*
         SPACE 1
         BDTXASRV TYPE=EXIT,GSD=(R3),SDWA=(R4),                        +
               RCVMOD=BDTUX25,RCVLABEL=UXESTAE
         DROP  R3
         SPACE 1
*---------------------------------------------------------------------*
*        UPDATE THE SDWA                                              *
*---------------------------------------------------------------------*
         SPACE 1
         MVC   SDWAMODN(8),UXMODN  SET NAME OF LOAD MODULE
         MVC   SDWACSCT(8),MDNUX31 SET NAME OF CSECT
         MVC   SDWAREXN(8),MDNUX31 SET CSECT WITH RECOVERY ROUTINE
         L     R3,SDWAXPAD         LOAD SDWA EXTENSION POINTERS ADDR
         USING SDWAPTRS,R3         ESTABLISH ADDRESSABILITY
         L     R3,SDWASRVP         LOAD RECORDABLE EXTENSION ADDRESS
         USING SDWARC1,R3          ESTABLISH ADDRESSABILITY
         MVC   SDWACID(5),UXACID   SET COMPONENT ID
         MVC   SDWASC(23),UXASC    SET NAME OF SUBCOMPONENT
         MVC   SDWAMDAT(8),MDDUX31 SET DATE OF ASSEMBLY
         MVC   SDWAMVRS(8),MDRUX31 SET PTF OR PRODUCT NUMBER
         MVC   SDWARRL(8),UXRRL    IDENTIFY ROUTINE UPDATING SDWA
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?
         BNO   UXRETRY2            YES, SET UP RETRY
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE
         SETRP RC=0,WKAREA=(R4)    DO NOT RETRY
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN TO RTM
         SPACE 1
UXRETRY2 DS    0H
         OI    UXESTFLG,UXESTREP   MARK FIRST USE
         SETRP RC=4,RETADDR=UXRETRY,FRESDWA=YES,                       +
               WKAREA=(R4)
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BR    R14                 RETURN TO RTM
         SPACE 1
*---------------------------------------------------------------------*
*        POP THE USING STACK HERE TO RE-ESTABLISH USINGS IN EFFECT    *
*        PRIOR TO ESTAE RECOVERY ROUTINE.                             *
*---------------------------------------------------------------------*
         SPACE 1
         POP   USING
         EJECT
         SPACE 1
*--------------------------------------------------------------------*
*        RETRY ROUTINE FOR ESTAE                                     *
*                                                                    *
*        THIS ROUTINE IS USED IN CONJUNCTION WITH THE ESTAE RECOVERY *
*        ROUTINE, UXESTAE.  THE RETRY ROUTINE 'FAILS' THE BDT        *
*        REQUEST BY SETTING THE RETURN CODE TO 4 AND RETURNING TO    *
*        THE CALLING MODULE. (ADDRESSABILITY TO THE USER PARAMETER   *
*        LIST WAS REESTABLISHED BY THE 'POP USING' AT THE END OF     *
*        THE ESTAE RECOVERY ROUTINE.  PARAMETER LIST IS BASED ON     *
*        REGISTER 4.)                                                *
*                                                                    *
*        THE REGISTER INTERFACE TO THE RETRY ROUTINE (UXRETRY)       *
*        DEPENDS ON THE CODING OF THE 'SETRP' MACRO IN THE RECOVERY  *
*        ROUTINE (UXESTAE) AND ON WHETHER OR NOT RTM OBTAINED AN     *
*        SDWA.  NORMALLY, AN SDWA WILL HAVE BEEN OBTAINED BUT THE    *
*        THE REGISTER INTERFACE WHEN THERE WAS AN SDWA IS SET UP     *
*        AS DESCRIBED BELOW ONLY BECAUSE THE 'SETRP' SPECIFIED       *
*        FREE SDWA & NO REGISTER UPDATE.  IF THESE PARAMETERS ARE    *
*        CHANGED ON THE 'SETRP', THE RETRY ROUTINE MUST ALSO BE      *
*        CHANGED.                                                    *
*                                                                    *
*        THE VALUE IN REGISTER 0 DEPENDS UPON WHETHER OR NOT RTM     *
*        OBTAINED AN SDWA:                                           *
*               REGISTER 0  - DECIMAL 20 IF SDWA WAS OBTAINED        *
*                           - DECIMAL 12 IF SDWA WAS NOT OBTAINED    *
*                                                                    *
*        FOUR REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLESS    *
*        SDWA STATUS:                                                *
*               REGISTER 1  - ADDRESS OF USER PARAMETER LIST         *
*                             ESTABLISHED USING ESTAE MACRO          *
*               REGISTER 2  - POINTER TO PIRL IF I/O WAS QUIESCED &  *
*                             IS RESTORABLE; OTHERWISE ZERO          *
*               REGISTER 14 - ADDRESS OF SUPERVISOR-ASSIGNED EXIT    *
*                             LINKAGE (SVC3)                         *
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RETRY     *
*                             ROUTINE                                *
*               REGISTERS BESIDES 0,1,2,14 & 15 ARE UNPREDICTABLE.   *
*--------------------------------------------------------------------*
         SPACE 1
UXRETRY  DS    0H
         LR    R4,R1               SET BASE REGISTER FOR PARAMETERS
         L     R10,UXBASE          RESTORE MODULE BASE FOR EXIT ROUTN
         LA    R15,RC8             SET RETURN CODE FAILING BDT REQUEST
         B     UXRETURN            RETURN TO CALLER
         EJECT
         SPACE 1
*--------------------------------------------------------------------*
*        MODEL FOR ESTAE MACRO (LIST FORM). USED TO INITIALIZE       *
*        ACTUAL LIST REFERENCED IN EXECUTE FORM OF ESTAE MACRO.      *
*        DO NOT STORE INTO THIS AREA.                                *
*--------------------------------------------------------------------*
         SPACE 1
ESTMODEL ESTAE MF=L                'MODEL' FOR ESTAE PARM LIST
         SPACE 1
*-------------------------------------------------------------*
*        MESSAGES                                             *
*-------------------------------------------------------------*
         SPACE 1
UXMSG01  DC    AL1(UXMSG01X-*-1)
         DC    C'UX31001 BDT COMMAND PROCESSING IS FOR YOUR '
         DC    C'USER-ID ONLY '
UXMSG01X EQU   *
         SPACE 1
UXMSG02  DC    AL1(UXMSG02X-*-1)
         DC    C'UX31002 NO JOBS FOUND FOR YOUR USER-ID '
UXMSG02X EQU   *
         SPACE 1
UXMSG03  DC    AL1(UXMSG03X-*-1)
         DC    C'UX31003 COMMAND FAILED BDTUX31 AUTHORIZATION - '
         DC    C'REJECTED'
UXMSG03X EQU   *
         SPACE 1
*-------------------------------------------------------------*
*        CONSTANTS,LITERAL POOL, PATCH AREA                   *
*-------------------------------------------------------------*
         SPACE 1
UXGSDID  DC    CL4'GSD '            IDENTIFICATION FOR GSD
UXWKCONS DC    CL4'WORK'            IDENTIFICATION FOR WORK AREA
UXMODN   DC    CL8'BDTUX31'         LOAD MODULE NAME
UXRRL    DC    CL8'UXESTAE'         RECOVERY ROUTINE UPDATING SDWA
UXACID   DC    CL5'30201'           COMPONENT ID
UXASC    DC    CL23'BDT - USER EXIT BDTUX25'   DESCRIPTION
         SPACE 1
         LTORG
         SPACE 1
*-------------------------------------------------------------*
*        PATCH AREA                                           *
*-------------------------------------------------------------*
         DC    0H'0',C'PTCH'
         PRINT DATA
PTCHUX31 DC    26S(*)
         ORG   *-52
         DC    ((*+195-BDTUX31)/400*4)S(*)
         PRINT NODATA
         ORG   ,
APARNUM  DC    CL7'       '
         END
