++USERMOD(SB004A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*                                                        */.
++VER(Z038) FMID(HBD1102)
            PRE(SB001A0 SB003A0
                UZ73936       )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRCUPD (BDTUX19 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX19
         EJECT                                                 @SB004A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@ FUNCTION:   RETRIEVE ACCOUNT LITERAL AND ADD TEXT UNIT      @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@ DEPENDENCIES:                                               @SB004A0
*@             BDTDBSID - ACCTNO ALREADY RETRIEVED FLAGE       @SB004A0
*@                                                             @SB004A0
*@ REGISTER CONVENTIONS:                                       @SB004A0
*@                         R2   INPUT PARAMETER LIST           @SB004A0
*@                         R4   GETMAINE AREA                  @SB004A0
*@                         R5   WORK AREA                      @SB004A0
*@                         R7   WORK AREA                      @SB004A0
*@                         R9   BSID                           @SB004A0
*@ OUTPUT:                                                     @SB004A0
*@             MJD WITH TEXT UNIT FOR ACCOUNT NUMBER           @SB004A0
*@                                                             @SB004A0
*@ EXTERNAL ROUTINES:                                          @SB004A0
*@             FINDTU                                          @SB004A0
*@                                                             @SB004A0
*@ CONTROL BLOCKS:                                             @SB004A0
*@             CVT   R/O    IEZJSCB  R/O                       @SB004A0
*@             IEFAJCTB R/O BDTDDATU R/O                       @SB004A0
*@             BDTDBSID R/W BDTDMJD  R/W                       @SB004A0
*@ MESSAGES:                                                   @SB004A0
*@             BDTR1905                                        @SB004A0
*@                                                             @SB004A0
*@ ABEND CODES:                                                @SB004A0
*@             N/A                                             @SB004A0
*@                                                             @SB004A0
*@ CHANGE ACTIVITY                                             @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB004A0
         CVT    DSECT=YES                                      @SB004A0
         IEFAJCTB                                              @SB004A0
         IEZJSCB                                               @SB004A0
*------------------------------------------------------------- @SB004A0
*    RETRIEVE 52 BYTE ACCOUNTING FIELD FOR OS ACT              @SB004A0
*                                                              @SB004A0
*    USER IS NOT ALLOWED TO SPECIFY THE ACCT PARAMETER.        @SB004A0
*    A FLAG, BSIDACCT, IS SET WHEN THE ACCOUNTING FIELD IS     @SB004A0
*    RETRIEVED.  IF THE ACCT TEXT UNIT IS FOUND BEFORE THE     @SB004A0
*    FLAG IS SET, THEN THE USER ATTEMPTED TO SPECIFY THE       @SB004A0
*    ACCT PARAMETER, SO THE TRANSACTION IS FLUSHED WITH A      @SB004A0
*    MSG.                                                      @SB004A0
*------------------------------------------------------------- @SB004A0
BSIDACCT EQU   X'01'               ACCT NO ALREAD RETRIEVE     @SB004A0
         TM    BSIDUFLG,BSIDACCT    ACCTNO ALREADY RETRIEVED?  @SB004A0
         BO    UX19DUN2             DONE WITH ACCT CODE        @SB004A0
         LA    R0,BTUACCT         SEARRCH FOR ACCOUNT FIELD    @SB004A0
         ICM   R0,B'1000',=X'F0'  SET GENERIC KEYWORD          @SB004A0
         BAL   R6,FINDTU          DID USER SUPPLY USERID       @SB004A0
         B     UX19ACRT           GO RETRIEVE ACCTNO           @SB004A0
         B     UX19ACUS           USER SPECIFIED ACCTNO. ERROR @SB004A0
*--------------------------------------------------------------@SB004A0
*     RETRIEVE ACCOUNT NUMBER AND BUILD TEXT UNIT,SET BSIDACCT @SB004A0
*--------------------------------------------------------------@SB004A0
UX19ACRT DS    0H                                              @SB004A0
         USING CVT,R1                                          @SB004A0
         L     R1,CVTPTR          POINT TO THE CVT             @SB004A0
         L     R1,CVTTCBP         POINTER TO TCB               @SB004A0
         DROP  R1                                              @SB004A0
         L     R1,0(R1)           USERS TCB                    @SB004A0
         USING TCB,R1                                          @SB004A0
         L     R1,TCBJSCB         JSCB                         @SB004A0
         USING IEZJSCB,R1                                      @SB004A0
         L     R1,JSCBJCT         JCT-X'10'                    @SB004A0
         LA    R1,X'10'(R1)       JCT                          @SB004A0
         USING INJMJCT,R1                                      @SB004A0
         ICM   R1,B'0111',JCTACTAD ACT-X'10'                   @SB004A0
         LA    R1,X'10'(R1)      STEP PAST PREFIX              @SB004A0
         USING IEFAACTB,R1                                     @SB004A0
         L     R7,NEXTSLOT        POINT TO NEXT AVAILABLE BYTE @SB004A0
         USING DATUNIT,R7                                      @SB004A0
         LA    R0,DATUPAR+52      END OF NEW TEXT UNIT         @SB004A0
         C     R0,BSIDENDA        AT END OF BSID?              @SB004A0
         BH    UXBSEXCD           YES, BSID EXECEEDED          @SB004A0
         MVC   DATUNUM,=AL2(1)    ONLY 1 PARAMETER IN TEXT UNIT@SB004A0
         MVC   DATULNG,=H'52'     PARAMETER 8 BYTES IN LENGTH  @SB004A0
         MVI   DATUFLG,0          ZERO THE FLAG                @SB004A0
         LA    R5,BTUACCT         SET TEXT UNIT KEY            @SB004A0
         STCM  R5,B'0011',DATUKEY                              @SB004A0
         OI    DATUFLG,DATUNDA    SET NOT DYNALLOC TU          @SB004A0
         MVC   DATUPAR(52),ACTACCNT+1 MOVE IN ACCT FIELD       @SB004A0
         DROP  R1                                              @SB004A0
         LA    R5,DATUPAR+52       PREPARE TO UPDATE           @SB004A0
         ST    R5,NEXTSLOT         NEXT AVAILABLE TEXTUNIT     @SB004A0
         L     R2,UXPARMAD         ADDRESS OF PARM PASSED      @SB004A0
         USING UXPARMIN,R2                                     @SB004A0
         MVC   UXNXTMJD,NEXTSLOT   MOVE BACK TO PARMLIST       @SB004A0
         DROP  R2,R7                                           @SB004A0
         OI    BSIDUFLG,BSIDACCT  SET ACCTNO RETRIEVED         @SB004A0
         B     UX19DUN2           ACCTNO CODE DONE             @SB004A0
*--------------------------------------------------------------@SB004A0
*     USER SPECIFIED ACCT PARM, FAIL WITH AN ERROR             @SB004A0
*--------------------------------------------------------------@SB004A0
UX19ACUS DS    0H                                              @SB004A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB004A0
         USING UXPARMIN,R2                                     @SB004A0
         MVC   UXUSRMSG,MSG5ADDR MOVE ERR MSG                  @SB004A0
         LA    R15,RCFAILTR       INDICATE FAIL THE TRANSACTION@SB004A0
         B     UXRETURN           ERROR , RETURN TO CALLER     @SB004A0
         DROP  R2                                              @SB004A0
MSG5ADDR DC    AL4(UX19MSG5)                                   @SB004A0
UX19MSG5 DC    AL1(MSG5END-MSG5STRT-1)                         @SB004A0
MSG5STRT DC    C'BDTR1905 THE ACCT PARAMETER CANNOT BE '       @SB004A0
         DC    C'SPECIFIED BY THE USER.'                       @SB004A0
MSG5END  EQU   *                                               @SB004A0
UX19DUN2 DS    0H                                              @SB004A0
