++USERMOD(SB001E0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     UX27 BASE CODE                                     */.
++VER(Z038) FMID(HBD1102)
        /* END OF ++VER */  .
++JCLIN.
//LINK  EXEC PGM=IEWL,PARM='XREF,LIST,LET,NCAL,SIZE=(768K,100K),RENT'
//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(1,1))
//AOSBA    DD DISP=SHR,DSN=SYS1.AOSBA
//SYSLMOD  DD DISP=SHR,DSN=SYS1.JES3LIB
//SYSPRINT DD SYSOUT=A
//SYSLIN   DD *
 INCLUDE AOSBA(BDTUX27)             * INCLUDE ORIGINAL MODULE
 NAME    BDTUX27                    * LOAD MODULE NAME
/*
++SRC    (BDTUX27 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB001E1
*@                                                             @SB001E1
*@                                                             @SB001E1
*@ FUNCTION:    BDTUX27 - BASE CODE                            @SB001E1
*@                                                             @SB001E1
*@                                                             @SB001E1
*@                                                             @SB001E1
*@ DEPENDENCIES:                                               @SB001E1
*@                                                             @SB001E1
*@                                                             @SB001E1
*@ RESTRICTIONS:                                               @SB001E1
*@             N/A                                             @SB001E1
*@                                                             @SB001E1
*@ REGISTER CONVENTIONS:  R1 - PARMETER LIST INPUT             @SB001E1
*@                        R4 - GETMAINED AREA                  @SB001E1
*@                        R8 - SECOND BASE                     @SB001E1
*@                        R9 - MJD  ADDRESS                    @SB001E1
*@                        R10- FIRST BASE                      @SB001E1
*@                        R12- TVTABLE                         @SB001E1
*@                        R13- SAVE AREA                       @SB001E1
*@                        R14- RETURN ADDRESS                  @SB001E1
*@                        R15- ENTRY ADDRESS                   @SB001E1
*@ ENTRY POINTS:                                               @SB001E1
*@             N/A                                             @SB001E1
*@                                                             @SB001E1
*@ ENTRY PURPOSE:                                              @SB001E1
*@             N/A                                             @SB001E1
*@                                                             @SB001E1
*@ ENTRY LINKAGE:                                              @SB001E1
*@             BDTXCALL                                        @SB001E1
*@                                                             @SB001E1
*@ INPUT:                                                      @SB001E1
*@             R1 - PARAMETER LIST                             @SB001E1
*@                                                             @SB001E1
*@ EXIT:                                                       @SB001E1
*@             N/A                                             @SB001E1
*@ OUTPUT:                                                     @SB001E1
*@             R15- 0 AUTHORIZED                               @SB001E1
*@                  4 UNAUTHORIZED                             @SB001E1
*@                                                             @SB001E1
*@ EXTERNAL ROUTINES:                                          @SB001E1
*@             N/A                                             @SB001E1
*@                                                             @SB001E1
*@ CONTROL BLOCKS:                                             @SB001E1
*@                          BDTDMJD  READ ONLY                 @SB001E1
*@                          BDTDTVT  READ ONLY                 @SB001E1
*@                          IHAPSA   READ ONLY                 @SB001E1
*@                          IKJTCB   READ ONLY                 @SB001E1
*@                          IHASDWA  READ/WRITE                @SB001E1
*@                                                             @SB001E1
*@ EXECUTABLE MACROS:                                          @SB001E1
*@           ESTAE                                             @SB001E1
*@           FREEMAIN                                          @SB001E1
*@           GETMAIN                                           @SB001E1
*@           SETRP                                             @SB001E1
*@                          BDTXRTRN                           @SB001E1
*@                          BDTXASRV                           @SB001E1
*@ MESSAGES:                                                   @SB001E1
*@             MSG ID'S                                        @SB001E1
*@                                                             @SB001E1
*@ ABEND CODES:                                                @SB001E1
*@             N/A                                             @SB001E1
*@                                                             @SB001E1
*@ CHANGE ACTIVITY                                             @SB001E1
*@                                                             @SB001E1
*@                                                             @SB001E1
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB001E1
BDTUX27  START   0                                             @SB001E1
         BDTDGSD                                               @SB001E1
         BDTDMJD                                               @SB001E1
BTUUSR   EQU   511                 USERID IN BSID              @SB001E1
         BDTDTVT                                               @SB001E1
         IHASDWA                                               @SB001E1
         IHAPSA                                                @SB001E1
         IKJTCB LIST=YES                                       @SB001E1
         BDTDREG                                               @SB001E1
*--------------------------------------------------------------@SB001E1
*        USER EQUATES                                          @SB001E1
*--------------------------------------------------------------@SB001E1
RCAUTHRZ EQU   0                   RETURN CODE 0 = AUTHORIZED  @SB001E1
RCFAILTR EQU   4                   RETURN CODE 4 = UNAUTHORIZED@SB001E1
RCRETRY  EQU   4                   RETURN CODE 4 = RETRY       @SB001E1
RCNORTRY EQU   0                   RETURN CODE 0 = NO RETRY    @SB001E1
ZERO     EQU   0                   USE TO INITIALIZE STORAGE   @SB001E1
*--------------------------------------------------------------@SB001E1
*        USER DSECT FOR WORK AREA                              @SB001E1
*                                                              @SB001E1
*        WORK AREA IS PASSED TO ESTAE MACRO AS USER PARAMETER L@SB001E1
*        THIS IS NECESSARY SO THAT, IF AN ABEND OCCURS, ADDRESS@SB001E1
*        BILITY TO THE AREA CAN BE REESTABLISHED.  DSECT CONTAI@SB001E1
*        FIELDS WHICH ARE NEEDED IN ESTAE RECOVERY ROUTINE, RET@SB001E1
*        ROUTINE OR COMMON EXIT FROM MODULE.                   @SB001E1
*                                                              @SB001E1
*        NOTE:  IF ADDITIONAL WORK AREA IS REQUIRED, ADD IT TO @SB001E1
*        DSECT (PREFERABLY BETWEEN UXESTLEN AND UXMAPLEN), SO  @SB001E1
*        ADDITIONAL GETMAINS ARE NOT NECESSARY.                @SB001E1
*--------------------------------------------------------------@SB001E1
UXPARMAP DSECT                     MAP OF USER PARAMETER LIST  @SB001E1
UXWORKID DS    F                   CONTAINS 'WORK' FOR IDENTIFI@SB001E1
UXTVTADD DS    F                   ADDRESS OF TVT,SAVED FOR RET@SB001E1
UXGSDADD DS    F                   ADDRESS OF GSD,SAVED FOR ABE@SB001E1
UXBASE   DS    F                   MOD BASE ADR,SAVED FOR RETRY@SB001E1
UXTOKEN  DS    F                   TOKEN USED IN ESTAE         @SB001E1
UXESTFLG DS    B                   INDICATES ESTAE STATUS      @SB001E1
UXESTSUC EQU   BIT0                ESTAE ESTABLISHED SUCCESSFUL@SB001E1
UXESTREP EQU   BIT1                ESTAE RECOVERY ALREADY ENTER@SB001E1
UXESTAPM DS    0F                  ESTAE MACRO PARAMETER LIST  @SB001E1
         ESTAE MF=L                                            @SB001E1
UXESTLEN EQU   *-UXESTAPM          LENGTH OF ESTAE LIST        @SB001E1
UXMAPLEN EQU   *-UXPARMAP          LENGTH OF THIS MAPPING      @SB001E1
*--------------------------------------------------------------@SB001E1
*        DSECT FOR PARAMETER LIST FROM CALLER                  @SB001E1
*                                                              @SB001E1
*        PARAMETER LIST (4 BYTES) CONTAINS ADDRESS OF MJD      @SB001E1
*--------------------------------------------------------------@SB001E1
UXPARMIN DSECT                     PARAMETER LIST PASSED BY CAL@SB001E1
UXMJDADR DS    A                   ADDRESS OF MJD              @SB001E1
BDTUX27  CSECT                                                 @SB001E1
*--------------------------------------------------------------@SB001E1
*        INVOKE BDTDMOD TO IDENTIFY MODULE                     @SB001E1
*                                                              @SB001E1
*        NOTE THAT CONSTANTS GENERATED BY THIS MACRO ARE ALSO U@SB001E1
*        IN ESTAE RECOVERY ROUTINE TO UPDATE THE SDWA.         @SB001E1
*--------------------------------------------------------------@SB001E1
*        BDTDMOD LABELS=YES                                    @SB001E1
         B     ENDCATCH-*(,R15)    BR AROUND ENTRY INFORMATION @SB001E1
         DC    AL1(ENDCATCH-*-1)   LENGTH OF ENTRY INFORMATION @SB001E1
MDNUX27  DC    CL8'BDTUX27'        MODULE NAME                 @SB001E1
MDLUX27  DC    CL8'       '        LABEL NAME                  @SB001E1
MDRUX27  DC    CL9' HBD1102 '      BDT RELEASE OR PTF NUMBER   @SB001E1
MDDUX27  DC    CL8'&SYSDATE'       ASSEMBLY DATE               @SB001E1
MDTUX27  DC    CL6'-&SYSTIME'      ASSEMBLY TIME               @SB001E1
         SPLEVEL SET=1                                         @SB001E1
ENDCATCH DS    0H                                              @SB001E1
*--------------------------------------------------------------@SB001E1
*        ESTABLISH ADDRESSABILITY                              @SB001E1
*                                                              @SB001E1
*        REGISTERS ARE NOT SAVED BECAUSE THE CALLING PROGRAM   @SB001E1
*  SAVES THEM VIA BDTXUEX.                                     @SB001E1
*--------------------------------------------------------------@SB001E1
UXINIT   DS    0H                                              @SB001E1
         LR    R10,R15             SET BASE REGISTER           @SB001E1
         LA    R8,2048(R10)        SET BASE REGISTER           @SB001E1
         LA    R8,2048(R8)         SET BASE REGISTER           @SB001E1
         USING BDTUX27,R10,R8      ESTABLISH ADDRESSABILITY    @SB001E1
         USING UXPARMIN,R1         ESTABLISH ADDRESSAB. TO PARM@SB001E1
         L     R9,UXMJDADR         SET BASE FOR MJDD           @SB001E1
         USING MJDSTART,R9         ESTABLISH ADDRESSAB. TO BSID@SB001E1
         DROP  R1                                              @SB001E1
*--------------------------------------------------------------@SB001E1
*        GETMAIN WORK AREA                                     @SB001E1
*                                                              @SB001E1
*        IF GETMAIN IS UNSUCCESSFUL, SET REGISTER 4 TO ZERO TO @SB001E1
*        INDICATE FAILURE, SKIP ESTAE SETUP AND BEGIN          @SB001E1
*        MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER RECOVERY @SB001E1
*        ROUTINE ESTABLISHED FOR CALLER.                       @SB001E1
*--------------------------------------------------------------@SB001E1
         LA    R4,UXMAPLEN         LENGTH OF AREA NEEDED       @SB001E1
         GETMAIN RC,LV=(R4)        GET STORAGE FOR PARAMETER LI@SB001E1
         LTR   R15,R15             WAS STORAGE OBTAINED?       @SB001E1
         BZ    UXGETOK             YES, INITIALIZE STORAGE     @SB001E1
         XR    R4,R4               NO, INDICATE GETMAIN FAILURE@SB001E1
         B     UXMAIN              SKIP ESTAE SETUP            @SB001E1
UXGETOK  DS    0H                                              @SB001E1
         LR    R4,R1               SET BASE FOR PARM LIST      @SB001E1
         USING UXPARMAP,R4         ESTABLISH ADDRESSABILITY TO @SB001E1
         LA    R5,UXMAPLEN         LENGTH OF AREA TO ZERO      @SB001E1
         XR    R7,R7               LENGTH AND PAD OF ZERO      @SB001E1
         MVCL  R4,R6               ZERO OUT THE AREA           @SB001E1
         MVC   UXWORKID,UXWKCONS   SET IDENTIFICATION TO 'WORK'@SB001E1
*--------------------------------------------------------------@SB001E1
*        ESTABLISH AN ESTAE                                    @SB001E1
*                                                              @SB001E1
*        1. BUILD USER PARAMETER LIST.                         @SB001E1
*        2. INITIALIZE ESTAE MACRO LIST;  ISSUE ESTAE.         @SB001E1
*        3. IF ESTAE SUCCESSFUL, SET BIT 0 IN 'ESTAE SUCCESS FL@SB001E1
*           TO 1.                                              @SB001E1
*        4. IF ESTAE IS UNSUCCESSFUL, LEAVE BIT 0 IN 'ESTAE SUC@SB001E1
*           FLAG' EQUAL TO ZERO.  (FLAG CONTAINS ALL ZEROES AS @SB001E1
*           RESULT OF STORAGE INITIALIZATION AFTER GETMAIN.)   @SB001E1
*           BEGIN MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER @SB001E1
*           RECOVERY ESTABLISHED FOR CALLER.                   @SB001E1
*--------------------------------------------------------------@SB001E1
         ST    R12,UXTVTADD        SAVE TVT ADDRESS AS PARAMETE@SB001E1
         USING PSA,R0              ESTABLISH ADDRESSAB. TO PSA @SB001E1
         L     R3,PSATOLD          SET BASE FOR TCB            @SB001E1
         USING TCB,R3              ESTABLISH ADDRESSAB. TO TCB @SB001E1
         L     R3,TCBBDT           SET BASE FOR GSD            @SB001E1
         USING GSDSTART,R3         ESTABLISH ADDRESSAB. TO GSD @SB001E1
         CLC   GSDID,UXGSDID       IS THERE A GSD?             @SB001E1
         BNE   UXNOESTA            NO, SKIP ESTAE SET UP       @SB001E1
         ST    R3,UXGSDADD         STORE ADDRESS OF GSD IN PARM@SB001E1
         ST    R10,UXBASE          STORE MODULE BASE FOR RETRY @SB001E1
*                                  MOVE ESTAE MODEL OVER STORAG@SB001E1
         MVC   UXESTAPM(UXESTLEN),ESTMODEL                     @SB001E1
         ESTAE UXESTAE,CT,PARAM=(R4),RECORD=YES,               @SB001E1X
               TOKEN=UXTOKEN,MF=(E,UXESTAPM)                   @SB001E1
         LTR   R15,R15             WAS ESTAE SUCCESSFUL?       @SB001E1
         BNZ   UXNOESTA            NO, DO NOT SET FLAG         @SB001E1
         OI    UXESTFLG,UXESTSUC   SET FLAG SUCDESSFULE EXTAEL @SB001E1
UXNOESTA DS    0H                                              @SB001E1
         DROP  R0,R3                                           @SB001E1
*--------------------------------------------------------------@SB001E1
*        MAINLINE ROUTINE                                      @SB001E1
*                                                              @SB001E1
*                                                              @SB001E1
*                                                              @SB001E1
*--------------------------------------------------------------@SB001E1
UXMAIN   DS    0H                                              @SB001E1
*--------------------------------------------------------------@SB001E1
*        COMMON EXIT ROUTINE - THIS EXIT ROUTINE IS ALWAYS USED@SB001E1
*        TO RETURN TO CALLER OF MODULE (I.E., NORMAL EXIT AND  @SB001E1
*        EXIT AFTER ESTAE RETRY ROUTINE).                      @SB001E1
*                                                              @SB001E1
*        REGISTER 4 AND UXESTFLG (SUCCESSFUL ESTAE FLAG) ARE   @SB001E1
* TESTED TO DETERMINE WHETHER TO CANCEL ESTAE AND INVOKE FREEM @SB001E1
*                                                              @SB001E1
*        1.  IF REG 4 IS ZERO, GETMAIN WAS UNSUCCESSFUL AND THE@SB001E1
*            IS NO ESTAE TO CANCEL.                            @SB001E1
*        2.  IF REG 4 IS NOT ZERO, TEST FOR ESTAE.             @SB001E1
*            A.  IF UXESTSUC IS OFF, THERE IS NO ESTAE TO CANCE@SB001E1
*                DO FREEMAIN AND RETURN.                       @SB001E1
*            B.  IF UXESTSUC IS ON, CANCEL ESTAE, FREEMAIN AND @SB001E1
*                RETURN.                                       @SB001E1
*--------------------------------------------------------------@SB001E1
UXRETURN DS    0H                                              @SB001E1
         LR    R3,R15              TEMPORARILY SAVE RETURN CODE@SB001E1
         LTR   R4,R4               WAS GETMAIN SUCCESSFUL?     @SB001E1
         BZ    UXRTONLY            NO, THEN NO ESTAE TO CANCEL,@SB001E1
         TM    UXESTFLG,UXESTSUC   WAS ESTAE ESTABLISHED?      @SB001E1
         BNO   UXRTFREE      NO, SKIP ESTAE CANCEL, DO FREEMAIN@SB001E1
         ESTAE 0,TOKEN=UXTOKEN     CANCEL ESTAE                @SB001E1
UXRTFREE DS    0H                                              @SB001E1
         FREEMAIN  RC,LV=UXMAPLEN,A=(R4)  FREE THE STORAGE     @SB001E1
UXRTONLY DS    0H                                              @SB001E1
         LR    R15,R3              RESTORE RETURN CODE         @SB001E1
*        BDTXRTRN                  RETURN TO CALLING MODULE    @SB001E1
         L     R14,TVTASVRT                                    @SB001E1
         BR    R14                                             @SB001E1
*--------------------------------------------------------------@SB001E1
*        ESTAE RECOVERY EXIT                                   @SB001E1
*                                                              @SB001E1
*        THIS ROUTINE SAVES ABEND INFORMATION IN THE GSD &SDWAD@SB001E1
*        AND SETS RETURN CODE FOR RTM INDICATING RETRY.  THIS E@SB001E1
*        RECOVERY ROUTINE IS USED IN CONJUNCTION WITH THE RETRY@SB001E1
*        ROUTINE, UXRETRY.  IF THIS RECOVERY ROUTINE IS CHANGED@SB001E1
*        CARE MUST BE TAKEN TO ENSURE THAT ASSUMPTIONS MADE BY @SB001E1
*        UXRETRY (E.G., REGISTER CONVENTIONS) ARE STILL VALID. @SB001E1
*                                                              @SB001E1
*        THIS ROUTINE DOES NOT RETRY WHEN IT IS ENTERED FOR THE@SB001E1
*        SECOND TIME.  THIS PREVENTS A LOOP CAUSED BY A RECURSI@SB001E1
*        ERROR.                                                @SB001E1
*                                                              @SB001E1
*        REGISTER INTERFACE TO RECOVERY ROUTINE (UXESTAE) DEPEN@SB001E1
*        ON WHETHER OR NOT RTM OBTAINED SDWA.  NORMALLY, AN SDW@SB001E1
*        WILL BE OBTAINED.                                     @SB001E1
*                                                              @SB001E1
*        TWO REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLES@SB001E1
*        SDWA STATUS:                                          @SB001E1
*               REGISTER 14 - RETURN ADDRESS                   @SB001E1
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RECO@SB001E1
*                             ROUTINE                          @SB001E1
*                                                              @SB001E1
*        IF SDWA WAS OBTAINED, REGISTERS UPON ENTRY TO UXESTAE @SB001E1
*               REGISTER 0  - A CODE INDICATING THE TYPE OF I/O@SB001E1
*                             PROCESSING PERFORMED             @SB001E1
*                             0  - ACTIVE I/O HAS BEEN QUIESCED@SB001E1
*                                  IS RESTORABLE               @SB001E1
*                             4  - ACTIVE I/O HAS BEEN HALTED  @SB001E1
*                                  NOT RESTORABLE              @SB001E1
*                             8  - NO ACTIVE I/O AT ABEND TIME @SB001E1
*                             16 - NO I/O PROCESSING WAS PERFOR@SB001E1
*               REGISTER 1  - ADDRESS OF SDWA                  @SB001E1
*               REGISTER 13 - SAVE AREA ADDRESS (72 BYTES)     @SB001E1
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB001E1
*               UNPREDICTABLE.                                 @SB001E1
*                                                              @SB001E1
*        IF SDWA WAS NOT OBTAINED, REGISTERS UPON ENTRY ARE:   @SB001E1
*               REGISTER 0  - A DECIMAL 12 INDICATING NO SDWA  @SB001E1
*               REGISTER 1  - ABEND COMPLETION CODE            @SB001E1
*               REGISTER 2  - ADDRESS OF USER-SUPPLIED PARAMETE@SB001E1
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB001E1
*               UNPREDICTABLE.                                 @SB001E1
*--------------------------------------------------------------@SB001E1
UXESTAE  DS    0H                                              @SB001E1
         PUSH  USING               SAVE CURRENT ADDRESSABILITY @SB001E1
         DROP  ,                                               @SB001E1
         USING *,R15               SET BASE FOR ESTAE ROUTINE  @SB001E1
         CH    R0,=H'12'           ESTAE WORK AREA,SDWA AVAILAB@SB001E1
         BNE   UXSDWA              YES, SDWA AVAILABLE, TAKE BR@SB001E1
*--------------------------------------------------------------@SB001E1
*        IF SDWA NOT PROVIDED, GET GSD ADDRESS FROM PARAMETER L@SB001E1
*        BASED ON R2 (PASSED BY ESTAE), SAVE ABEND CODE IN GSD.@SB001E1
*        NOT USE MACRO BDTXASRV TO UPDATE GSD BECAUSE IT RELIES@SB001E1
*        THE SDWA.  DETERMINE WHETHER TO RETRY.                @SB001E1
*--------------------------------------------------------------@SB001E1
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB001E1
         L     R3,UXGSDADD         RESTORE GSD BASE ADDRESS    @SB001E1
         USING GSDSTART,R3         ESTABLISH BASE FOR GSD      @SB001E1
         ST    R1,GSDABCC          SAVE ABEND CODE IN GSD      @SB001E1
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB001E1
         BNO   UXRETRY1            YES, SET UP RETRY           @SB001E1
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB001E1
         LA    R15,RCNORTRY        DO NOT RETRY                @SB001E1
         BR    R14                 RETURN TO RTM               @SB001E1
UXRETRY1 DS    0H                                              @SB001E1
         OI    UXESTFLG,UXESTREP   SET FLAG MARKING FIRST USE  @SB001E1
         LA    R0,UXRETRY          ADDRESS OF RETRY ROUTINE    @SB001E1
         LA    R15,RCRETRY         SET RETRY RETURN CODE FOR RT@SB001E1
         BR    R14                 RETURN TO RTM               @SB001E1
*--------------------------------------------------------------@SB001E1
*        IF SDWA PROVIDED, GET GSD ADDRESS FROM PARAMETER LIST @SB001E1
*        ON SDWAPARM, RECORD ABEND ENVIRONMENT IN GSD AND SDWA.@SB001E1
*        DETERMINE WHETHER TO RETRY.                           @SB001E1
*--------------------------------------------------------------@SB001E1
UXSDWA   DS    0H                                              @SB001E1
         STM   R14,R12,12(R13)     SAVE REGS WHERE INDICATED BY@SB001E1
         LR    R4,R1               ESTABLISH ADDRESSABILITY . .@SB001E1
         USING SDWA,R4                  . . . TO SDWA          @SB001E1
         L     R2,SDWAPARM         LOAD ADDRESS OF PARM LIST   @SB001E1
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB001E1
         L     R10,UXBASE          CHANGE BASE REGISTER . . .  @SB001E1
         LA    R8,2048(R10)        CHANGE BASE REGISTER . . .  @SB001E1
         LA    R8,2048(R8)         CHANGE BASE REGISTER . . .  @SB001E1
         USING BDTUX27,R10,R8           . . . FOR THIS ROUTINE @SB001E1
         DROP  R15                                             @SB001E1
         L     R3,UXGSDADD         RESTORE GSD BASE ADDRESS    @SB001E1
         USING GSDSTART,R3         ESTABLISH BASE FOR GSD      @SB001E1
*--------------------------------------------------------------@SB001E1
*        UPDATE THE GSD                                        @SB001E1
*                                                              @SB001E1
*        BDTXASRV PROVIDES COMMON ABEND SERVICES INCLUDING UPDT@SB001E1
*        THE GSD. (NOTE: THIS SERVICE SAVES AND RESTORES THE C @SB001E1
*        REGISTERS USING AN AREA IN THE GSD.)                  @SB001E1
*--------------------------------------------------------------@SB001E1
         BDTXASRV TYPE=EXIT,GSD=(R3),SDWA=(R4),                @SB001E1+
               RCVMOD=BDTUX27,RCVLABEL=UXESTAE                 @SB001E1
         DROP  R3                                              @SB001E1
*--------------------------------------------------------------@SB001E1
*        UPDATE THE SDWA                                       @SB001E1
*--------------------------------------------------------------@SB001E1
         MVC   SDWAMODN(8),UXMODN  SET NAME OF LOAD MODULE     @SB001E1
         MVC   SDWACSCT(8),MDNUX27 SET NAME OF CSECT           @SB001E1
         MVC   SDWAREXN(8),MDNUX27 SET CSECT WITH RECOVERY ROUT@SB001E1
         L     R3,SDWAXPAD         LOAD SDWA EXTENSION POINTERS@SB001E1
         USING SDWAPTRS,R3         ESTABLISH ADDRESSABILITY    @SB001E1
         L     R3,SDWASRVP         LOAD RECORDABLE EXTENSION AD@SB001E1
         USING SDWARC1,R3          ESTABLISH ADDRESSABILITY    @SB001E1
         MVC   SDWACID(5),UXACID   SET COMPONENT ID            @SB001E1
         MVC   SDWASC(23),UXASC    SET NAME OF SUBCOMPONENT    @SB001E1
         MVC   SDWAMDAT(8),MDDUX27 SET DATE OF ASSEMBLY        @SB001E1
         MVC   SDWAMVRS(8),MDRUX27 SET PTF OR PRODUCT NUMBER   @SB001E1
         MVC   SDWARRL(8),UXRRL    IDENTIFY ROUTINE UPDATING SD@SB001E1
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB001E1
         BNO   UXRETRY2            YES, SET UP RETRY           @SB001E1
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB001E1
         SETRP RC=0,WKAREA=(R4)    DO NOT RETRY                @SB001E1
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB001E1
         BR    R14                 RETURN TO RTM               @SB001E1
UXRETRY2 DS    0H                                              @SB001E1
         OI    UXESTFLG,UXESTREP   MARK FIRST USE              @SB001E1
         SETRP RC=4,RETADDR=UXRETRY,FRESDWA=YES,               @SB001E1+
               WKAREA=(R4)                                     @SB001E1
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB001E1
         BR    R14                 RETURN TO RTM               @SB001E1
*--------------------------------------------------------------@SB001E1
*        POP THE USING STACK HERE TO RE-ESTABLISH USINGS IN EFF@SB001E1
*        PRIOR TO ESTAE RECOVERY ROUTINE.                      @SB001E1
*--------------------------------------------------------------@SB001E1
         POP   USING                                           @SB001E1
*--------------------------------------------------------------@SB001E1
*        RETRY ROUTINE FOR ESTAE                               @SB001E1
*                                                              @SB001E1
*        THIS ROUTINE IS USED IN CONJUNCTION WITH THE ESTAE REC@SB001E1
*        ROUTINE, UXESTAE.  THE RETRY ROUTINE 'FAILS' THE BDT  @SB001E1
*        REQUEST BY SETTING THE RETURN CODE TO 4 AND RETURNING @SB001E1
*        THE CALLING MODULE.  BEFORE BRANCHING TO THE COMMON EX@SB001E1
*        FOR THE MODULE, THE RETRY ROUTINE REESTABLISHES ADDRES@SB001E1
*        BILITY TO THE TVT.  (ADDRESSABILITY TO THE USER PARAME@SB001E1
*        LIST WAS REESTABLISHED BY THE 'POP USING' AT THE END O@SB001E1
*        THE ESTAE RECOVERY ROUTINE.  PARAMETER LIST IS BASED O@SB001E1
*        REGISTER 4.)                                          @SB001E1
*                                                              @SB001E1
*        THE REGISTER INTERFACE TO THE RETRY ROUTINE (UXRETRY) @SB001E1
*        DEPENDS ON THE CODING OF THE 'SETRP' MACRO IN THE RECO@SB001E1
*        ROUTINE (UXESTAE) AND ON WHETHER OR NOT RTM OBTAINED  @SB001E1
*        SDWA.  NORMALLY, AN SDWA WILL HAVE BEEN OBTAINED BUT T@SB001E1
*        THE REGISTER INTERFACE WHEN THERE WAS AN SDWA IS SET U@SB001E1
*        AS DESCRIBED BELOW ONLY BECAUSE THE 'SETRP' SPECIFIED @SB001E1
*        FREE SDWA & NO REGISTER UPDATE.  IF THESE PARAMETERS  @SB001E1
*        CHANGED ON THE 'SETRP', THE RETRY ROUTINE MUST ALSO BE@SB001E1
*        CHANGED.                                              @SB001E1
*                                                              @SB001E1
*        THE VALUE IN REGISTER 0 DEPENDS UPON WHETHER OR NOT RT@SB001E1
*        OBTAINED AN SDWA:                                     @SB001E1
*               REGISTER 0  - DECIMAL 20 IF SDWA WAS OBTAINED  @SB001E1
*                           - DECIMAL 12 IF SDWA WAS NOT OBTAIN@SB001E1
*                                                              @SB001E1
*        FOUR REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLE@SB001E1
*        SDWA STATUS:                                          @SB001E1
*               REGISTER 1  - ADDRESS OF USER PARAMETER LIST   @SB001E1
*                             ESTABLISHED USING ESTAE MACRO    @SB001E1
*               REGISTER 2  - POINTER TO PIRL IF I/O WAS QUIESC@SB001E1
*                             IS RESTORABLE; OTHERWISE ZERO    @SB001E1
*               REGISTER 14 - ADDRESS OF SUPERVISOR-ASSIGNED EX@SB001E1
*                             LINKAGE (SVC3)                   @SB001E1
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RETR@SB001E1
*                             ROUTINE                          @SB001E1
*               REGISTERS BESIDES 0,1,2,14 & 15 ARE UNPREDICTAB@SB001E1
*--------------------------------------------------------------@SB001E1
UXRETRY  DS    0H                                              @SB001E1
         LR    R4,R1               SET BASE REGISTER FOR PARAME@SB001E1
         L     R10,UXBASE          RESTORE MODULE BASE FOR EXIT@SB001E1
         LA    R8,2048(R10)        RESTORE MODULE BASE FOR EXIT@SB001E1
         LA    R8,2048(R8)         RESTORE MODULE BASE FOR EXIT@SB001E1
         L     R12,UXTVTADD        RESTORE BASE FOR TVT        @SB001E1
         LA    R15,RCFAILTR        SET RETURN CODE FAILING BDT @SB001E1
         B     UXRETURN            RETURN TO CALLER            @SB001E1
*--------------------------------------------------------------@SB001E1
*        MODEL FOR ESTAE MACRO (LIST FORM). USED TO INITIALIZE @SB001E1
*        ACTUAL LIST REFERENCED IN EXECUTE FORM OF ESTAE MACRO.@SB001E1
*        DO NOT STORE INTO THIS AREA.                          @SB001E1
*--------------------------------------------------------------@SB001E1
ESTMODEL ESTAE MF=L                'MODEL' FOR ESTAE PARM LIST @SB001E1
*-------------------------------------------------------------*@SB001E1
*        CONSTANTS,LITERAL POOL, PATCH AREA                   *@SB001E1
*-------------------------------------------------------------*@SB001E1
UXGSDID  DC    CL4'GSD '            IDENTIFICATION FOR GSD     @SB001E1
UXWKCONS DC    CL4'WORK'            IDENTIFICATION FOR WORK ARE@SB001E1
UXMODN   DC    CL8'BDTUX27'         LOAD MODULE NAME           @SB001E1
UXRRL    DC    CL8'UXESTAE'         RECOVERY ROUTINE UPDATING S@SB001E1
UXACID   DC    CL5'30201'           COMPONENT ID               @SB001E1
UXASC    DC    CL23'BDT - USER EXIT BDTUX27'   DESCRIPTION     @SB001E1
         LTORG                                                 @SB001E1
         END   ,                                               @SB001E1
