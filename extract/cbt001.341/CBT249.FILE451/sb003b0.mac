++USERMOD(SB003B0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     ACF2 DATASET VALIDATION                            */.
++VER(Z038) FMID(HBD1102)
            PRE(SB001E0
                              )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRCUPD (BDTUX27 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX27
         EJECT                                                 @SB003B0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB003B0
*@                                                             @SB003B0
*@                                                             @SB003B0
*@ FUNCTION:  ACF2 DATASET VALIDATION                          @SB003B0
*@             1. ACF2 USER VALIDATION TO RETURN UID STRING    @SB003B0
*@             2. DATASET/VOLUME VALIDATION OF FROM AND TO     @SB003B0
*@                DATASETS BASED ON FLAGS UXFRFLAG AND UXTOFLAG@SB003B0
*@                                                             @SB003B0
*@                                                             @SB003B0
*@ DEPENDENCIES:                                               @SB003B0
*@             ACF2    SB001E0                                 @SB003B0
*@                                                             @SB003B0
*@ RESTRICTIONS:                                               @SB003B0
*@                                                             @SB003B0
*@                                                             @SB003B0
*@ REGISTER CONVENTIONS:                                       @SB003B0
*@                        R4 - GETMAINED AREA                  @SB003B0
*@                        R5 - ACF2 CVT                        @SB003B0
*@                        R8 - SECOND BASE                     @SB003B0
*@                        R9 - MJD                             @SB003B0
*@                        R10- BASE                            @SB003B0
*@                        R12- TVT                             @SB003B0
*@                        R13- SAVE AREA SET UP BY ASAVE       @SB003B0
*@                        R14- RETURN ADDRESS AND DEST OF MOVE @SB003B0
*@                        R15- ENTRY POINT AND LENGTH OF MOVE  @SB003B0
*@ ENTRY POINTS:                                               @SB003B0
*@             N/A                                             @SB003B0
*@                                                             @SB003B0
*@ ENTRY PURPOSE:                                              @SB003B0
*@             N/A                                             @SB003B0
*@                                                             @SB003B0
*@ ENTRY LINKAGE:                                              @SB003B0
*@             N/A                                             @SB003B0
*@                                                             @SB003B0
*@ INPUT:                                                      @SB003B0
*@             R1 -> MJD                                       @SB003B0
*@                                                             @SB003B0
*@ RETURN CODES:                                               @SB003B0
*@                                                             @SB003B0
*@RCACF08  EQU   8   ACF2 USER VALIDATION - FAILED             @SB003B0
*@RCACF12  EQU   12  ACF2 FROM DATASET READ ACCESS - FAILED    @SB003B0
*@RCACF16  EQU   16  ACF2 FROM DATASET ALLLOC. ACCESS - FAILED @SB003B0
*@RCACF20  EQU   20  ACF2 FROM DATASET NL      ACCESS - FAILED @SB003B0
*@RCACF24  EQU   24  ACF2 FROM DATASET BYPASS LABEL   - FAILED @SB003B0
*@RCACF28  EQU   28  ACF2 FROM DATASET BYPASS TMS     - FAILED @SB003B0
*@RCACF32  EQU   32  ACF2 TO   DATASET WRITE ACCESS   - FAILED @SB003B0
*@RCACF36  EQU   36  ACF2 TO   DATASET ALLLOC. ACCESS - FAILED @SB003B0
*@RCACF40  EQU   40  ACF2 TO   DATASET NL      ACCESS - FAILED @SB003B0
*@RCACF44  EQU   44  ACF2 TO   DATASET BYPASS LABEL   - FAILED @SB003B0
*@RCACF48  EQU   48  ACF2 TO   DATASET BYPASS TMS     - FAILED @SB003B0
*@                                                             @SB003B0
*@                                                             @SB003B0
*@ OUTPUT:                                                     @SB003B0
*@             FIRST SECTION - ACF2 VALIDATION REQUIREMENTS    @SB003B0
*@             SECOND SECTION- VALIDATED DATASET ACCESS        @SB003B0
*@                             RETURN CODE IDENTIFYING ACF2    @SB003B0
*@                             VALIDATION TYPE OF REJECT.      @SB003B0
*@                                                             @SB003B0
*@ EXTERNAL ROUTINES:                                          @SB003B0
*@             N/A                                             @SB003B0
*@                                                             @SB003B0
*@ CONTROL BLOCKS:                                             @SB003B0
*@             ##ACFBK3  R/W                                   @SB003B0
*@             ACFDSV    R/W                                   @SB003B0
*@             ACFUCB    R/W                                   @SB003B0
*@             BDTDBTUK  R/W                                   @SB003B0
*@             BDTDDATU  R/W                                   @SB003B0
*@             BDTDMJD   R/O                                   @SB003B0
*@             BDTDTVT   R/O                                   @SB003B0
*@             CAMLST    R/W                                   @SB003B0
*@             IEFZB4D2  R/O                                   @SB003B0
*@ EXECUTABLE MACROS:                                          @SB003B0
*@             ACFINCVT                                        @SB003B0
*@             ACFSVC                                          @SB003B0
*@             BDTXTUAM                                        @SB003B0
*@             LOCATE                                          @SB003B0
*@                                                             @SB003B0
*@ MESSAGES:                                                   @SB003B0
*@             MSG ID'S                                        @SB003B0
*@                                                             @SB003B0
*@ ABEND CODES:                                                @SB003B0
*@             N/A                                             @SB003B0
*@                                                             @SB003B0
*@ CHANGE ACTIVITY                                             @SB003B0
*@                                                             @SB003B0
*@                                                             @SB003B0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - ACF2 CVT MAP'         @SB003B0
         ACCVT  ,                  ACF CVT DEFINITIONS         @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - BDTDDATU'             @SB003B0
         BDTDDATU                                              @SB003B0
         IEFZB4D2                                              @SB003B0
RCACF2FL EQU   8                   RETURN CODE 8 =ACF2 FAILED  @SB003B0
* ACF2 VALIDATION RETURN CODES                                 @SB003B0
RCACF08  EQU   8   ACF2 USER VALIDATION - FAILED               @SB003B0
RCACF12  EQU   12  ACF2 FROM DATASET READ ACCESS - FAILED      @SB003B0
RCACF16  EQU   16  ACF2 FROM DATASET ALLLOC. ACCESS - FAILED   @SB003B0
RCACF20  EQU   20  ACF2 FROM DATASET NL      ACCESS - FAILED   @SB003B0
RCACF24  EQU   24  ACF2 FROM DATASET BYPASS LABEL   - FAILED   @SB003B0
RCACF28  EQU   28  ACF2 FROM DATASET BYPASS TMS     - FAILED   @SB003B0
RCACF32  EQU   32  ACF2 TO   DATASET WRITE ACCESS   - FAILED   @SB003B0
RCACF36  EQU   36  ACF2 TO   DATASET ALLLOC. ACCESS - FAILED   @SB003B0
RCACF40  EQU   40  ACF2 TO   DATASET NL      ACCESS - FAILED   @SB003B0
RCACF44  EQU   44  ACF2 TO   DATASET BYPASS LABEL   - FAILED   @SB003B0
RCACF48  EQU   48  ACF2 TO   DATASET BYPASS TMS     - FAILED   @SB003B0
*                                                              @SB003B0
UX27USER DS    CL8                 USERID                      @SB003B0
*--------------------------------------------------------------@SB003B0
*   FROM SIDE ACF2 VALIDATION INFORMATION                      @SB003B0
*--------------------------------------------------------------@SB003B0
UXFRFLAG DS    X                   VALIDATION FLAGS            @SB003B0
UXFRVALD EQU   X'01'               VALIDATE FROM DATASET(READ) @SB003B0
UXFRALLC EQU   X'02'               VALIDATE FOR ALLOC AUTH.    @SB003B0
UXFRNL   EQU   X'04'               VALIDATE FOR NL PROCESSING  @SB003B0
UXFRBLP  EQU   X'08'               VALIDATE FOR BYPASS LABEL   @SB003B0
UXFRBTMS EQU   X'10'               VALIDATE BYPASS TMS         @SB003B0
UXFRDSN  DS    CL44                DATASET NAME                @SB003B0
UXFRVOLS DS    CL6                 VOLSER                      @SB003B0
*--------------------------------------------------------------@SB003B0
*   TO   SIDE ACF2 VALIDATION INFORMATION                      @SB003B0
*--------------------------------------------------------------@SB003B0
UXTOFLAG DS    X                   VALIDATION FLAGS            @SB003B0
UXTOVALD EQU   X'01'               VALIDATE TO DATASET (WRITE) @SB003B0
UXTOALLC EQU   X'02'               VALIDATE FOR ALLOC AUTH.    @SB003B0
UXTONL   EQU   X'04'               VALIDATE FOR NL PROCESSING  @SB003B0
UXTOBLP  EQU   X'08'               VALIDATE FOR BYPASS LABEL   @SB003B0
UXTOBTMS EQU   X'10'               VALIDATE BYPASS TMS         @SB003B0
UXTODSN  DS    CL44                DATASET NAME                @SB003B0
UXTOVOLS DS    CL6                 VOLSER                      @SB003B0
*--------------------------------------------------------------@SB003B0
*    LOCATE CONTROL BLOCKS                                     @SB003B0
*--------------------------------------------------------------@SB003B0
UXCAMLST CAMLST NAME,UX27DSN,,UX27LOCA                         @SB003B0
UX27DSN  DS     CL44               DATASET NAME FOR LOCATE     @SB003B0
         DS     0D                                             @SB003B0
UX27LOCA DS     CL265              AREA TO PLACE LOCATE INFO   @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - ##ACFBK3 (ACFBLOCK)'  @SB003B0
         ##ACFBK3  DSECT=NO        ACF WORK AREA DEFINITIONS   @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - ACUCB'             '  @SB003B0
         ACUCB     DSECT=NO                                    @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - ACDSB'             '  @SB003B0
         ACDSV     DSECT=NO                                    @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - VALIDATION WORK AREA' @SB003B0
R14SAVE  DS    F                   BAL REG SAVE AREA           @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  -             '         @SB003B0
         LR    R4,R1               RELOAD ADDRESS DESTRY MVCL  @SB003B0
*    RETREIVE REQUIRED DATA FOR ACF2 VALIDATION                @SB003B0
*    AND SET FLAGS IN PARMAREA TO INDICATE THE TYPES OF        @SB003B0
*    VALIDATION.                                               @SB003B0
*--------------------------------------------------------------@SB003B0
*    RETRIEVE THE USERID                                       @SB003B0
*--------------------------------------------------------------@SB003B0
         USING DATUNIT,R1             R1 FROM BDTXTUAM         @SB003B0
         BDTXTUAM START=(R9),KEY=BTUUSR,EOD=NOUSERID,DATU=NO   @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF MOVE          @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         LA    R14,UX27USER            DEST OF MOVE            @SB003B0
         MVC   UX27USER,=CL44' '       BLANK THE FIELD         @SB003B0
         EX    R15,MOVEPARM                                    @SB003B0
*--------------------------------------------------------------@SB003B0
*        RETRIEVE FROM DATASET INFORMATION                     @SB003B0
*--------------------------------------------------------------@SB003B0
*        IS FROM LOC = TO THIS NODE'S SYSID                    @SB003B0
*--------------------------------------------------------------@SB003B0
         CLC   MJDFRLOC,TVTSYSID      FROM DSN ON THIS NODE?   @SB003B0
         BNE   UXRETVTO               NO, GO CHECK TO DSN      @SB003B0
*--------------------------------------------------------------@SB003B0
*     RETRIEVE DSN AND MOVE TO PARM AREA                       @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALDSNAM,TYPE=FROM,           @SB003B0X
               EOD=NOFRMDSN,DATU=YES                           @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF PARM          @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         LA    R14,UXFRDSN             MOVE TO PARM AREA       @SB003B0
         MVC   UXFRDSN,=CL44' '        PRE- BLANK              @SB003B0
         EX    R15,MOVEPARM                                    @SB003B0
*--------------------------------------------------------------@SB003B0
*    RETRIEVE VOLSER. IF NOT FOUND DO LOCATE                   @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALVLSER,TYPE=FROM,           @SB003B0X
               EOD=NOFRMVOL,DATU=YES                           @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF MOVE          @SB003B0
         LA    R14,UXFRVOLS            MOVE DESTINATION        @SB003B0
         MVC   UXFRVOLS,=CL44' '       PRE- BLANK              @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         EX    R15,MOVEPARM            MOVE THE VOLSER         @SB003B0
         B     FRDETDSP                GO DETERMINE THE DISP   @SB003B0
NOFRMVOL DS    0H                                              @SB003B0
*--------------------------------------------------------------@SB003B0
*   VOLSER NOT SPECIFIED, CHECK IF VOLREF SPECIFIED            @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALVLRDS,TYPE=FROM,           @SB003B0X
               EOD=NOFRVLRF,DATU=YES                           @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF MOVE          @SB003B0
         LA    R14,UX27DSN             USE VOLREF FOR LOCATE   @SB003B0
         MVC   UX27DSN,=CL44' '        PRE - BLANK             @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         EX    R15,MOVEPARM            MOVE DSNAME             @SB003B0
         B     UX27FLOC                GO DO LOCATE            @SB003B0
NOFRVLRF DS    0H                                              @SB003B0
         MVC   UX27DSN,UXFRDSN         SET UP FOR LOCATE       @SB003B0
UX27FLOC DS    0H                      LOCATE (FROM)           @SB003B0
         MVC   UXCAMLST(CAMLNGTH),DUMCAM MOVE IN CAMLIST FLAGS @SB003B0
         LA    R1,UX27DSN              ADDRESS OF DSN          @SB003B0
         ST    R1,UXCAMLST+4           TO CAMLST               @SB003B0
         LA    R1,UX27LOCA             ADDRESS OF LOC AREA     @SB003B0
         ST    R1,UXCAMLST+12          TO CAMLST               @SB003B0
         LOCATE UXCAMLST               DO THE LOCATE           @SB003B0
         LTR   R15,R15                 RETURN CODE             @SB003B0
         BNZ   FRMLOCFA                LOCATE FAILED. BRANCH   @SB003B0
         MVC   UXFRVOLS,UX27LOCA+6     USE THE FIRST VOL SER   @SB003B0
*--------------------------------------------------------------@SB003B0
*      IF DATASET DISP OF DELETE SPECIFIED, ACF2 ALLOC AUTH    @SB003B0
*      NEEDED.                                                 @SB003B0
*--------------------------------------------------------------@SB003B0
FRDETDSP DS    0H                                              @SB003B0
         OI    UXFRFLAG,UXFRVALD       ACF2 VALD. FOR FROM DSN @SB003B0
         BDTXTUAM START=(R9),KEY=DALNDISP,DATU=YES,            @SB003B0X
               TYPE=FROM,EOD=FRCDISP                           @SB003B0
         TM     DATUPAR,BTUDELET       NORMAL DISP OF DELETE?  @SB003B0
         BZ     FRCDISP                NO, CHECK COND. DISP    @SB003B0
         OI     UXFRFLAG,UXFRALLC      SET VALD. ALLOC AUTH    @SB003B0
         B      FRLABELP               GO DETERMINE LABEL PROC @SB003B0
FRCDISP  DS     0H                     CHECK COND DISP         @SB003B0
         BDTXTUAM START=(R9),KEY=DALCDISP,DATU=YES,            @SB003B0X
               TYPE=FROM,EOD=FRLABELP                          @SB003B0
         TM    DATUPAR,BTUDELET         COND DISP OF DELETE?   @SB003B0
         BZ    FRLABELP                 NO, GO CHECK LABEL PROC@SB003B0
         OI    UXFRFLAG,UXFRALLC        SET VALD ALLOC AUTH    @SB003B0
*--------------------------------------------------------------@SB003B0
*     NL AND BLP TAPE PROCESSING AUTHORIZATION REQUIRED?       @SB003B0
*--------------------------------------------------------------@SB003B0
FRLABELP DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALLABEL,DATU=YES,            @SB003B0X
               TYPE=FROM,EOD=FROMBTMS                          @SB003B0
         TM    DATUPAR,BTUNL           NL SPECIFIED?           @SB003B0
         BZ    FROMBLP                 NO, GO CHECK BLP        @SB003B0
         OI    UXFRFLAG,UXFRNL         SET NL PROCESSING       @SB003B0
         B     FROMBTMS                GO CHECK BYPASS TMS     @SB003B0
FROMBLP  DS    0H                                              @SB003B0
         TM    DATUPAR,BTUBLP          BLP SPECFIED?           @SB003B0
         BZ    FROMBTMS                NO, GO CHECK BYPASS TMS @SB003B0
         OI    UXFRFLAG,UXFRBLP        SET BLP PROCESSING      @SB003B0
*--------------------------------------------------------------@SB003B0
*      BYPASS TMS TAPE PROCESSING AUTHORIZATION REQUIRED?      @SB003B0
*--------------------------------------------------------------@SB003B0
FROMBTMS DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALEXPDT,DATU=YES,            @SB003B0X
               TYPE=FROM,EOD=UXFRCOMP                          @SB003B0
         CLC   DATUPAR(5),=C'98000'    BYPASS TMS SPECIFIED?   @SB003B0
         BNE   UXFRCOMP                NO,FROM SIDE COMPLETE   @SB003B0
         OI    UXFRFLAG,UXFRBTMS       SET BYPASS TMS PROCESS. @SB003B0
UXFRCOMP DS    0H                      FROM SIDE COMPLETE      @SB003B0
         EJECT                                                 @SB003B0
*--------------------------------------------------------------@SB003B0
*      DETERMINE ACF2 VALIDATION REQUIRED FOR TO DATASET       @SB003B0
*      IF ANY                                                  @SB003B0
*--------------------------------------------------------------@SB003B0
UXRETVTO DS    0H                                              @SB003B0
*--------------------------------------------------------------@SB003B0
*        RETRIEVE TO   DATASET INFORMATION                     @SB003B0
*--------------------------------------------------------------@SB003B0
*        IS TO   LOC = TO THIS NODE'S SYSID                    @SB003B0
*--------------------------------------------------------------@SB003B0
         CLC   MJDTOLOC,TVTSYSID       TO DSN ON THIS NODE?    @SB003B0
         BNE   ACF2VALD                NO, GO DO ACF2 VALD.    @SB003B0
*--------------------------------------------------------------@SB003B0
*     RETRIEVE DSN AND MOVE TO PARM AREA                       @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALDSNAM,TYPE=TO,             @SB003B0X
               EOD=NOTODSN,DATU=YES                            @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF PARM          @SB003B0
         LA    R14,UXTODSN             MOVE TO PARM AREA       @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         MVC   UXTODSN,=CL44' '        PRE- BLANK              @SB003B0
         EX    R15,MOVEPARM                                    @SB003B0
*--------------------------------------------------------------@SB003B0
*    RETRIEVE VOLSER. IF NOT FOUND DO LOCATE                   @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALVLSER,TYPE=TO,             @SB003B0X
               EOD=NOTOVOL,DATU=YES                            @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF MOVE          @SB003B0
         LA    R14,UXTOVOLS            MOVE DESTINATION        @SB003B0
         MVC   UXTOVOLS,=CL44' '       PRE- BLANK              @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         EX    R15,MOVEPARM            MOVE THE VOLSER         @SB003B0
         B     TODETSTA                GO DETERMINE THE STATUS @SB003B0
NOTOVOL  DS    0H                                              @SB003B0
*--------------------------------------------------------------@SB003B0
*   VOLSER NOT SPECIFIED, CHECK IF VOLREF SPECIFIED            @SB003B0
*--------------------------------------------------------------@SB003B0
         BDTXTUAM START=(R9),KEY=DALVLRDS,TYPE=TO,             @SB003B0X
               EOD=NOTOVLRF,DATU=YES                           @SB003B0
         SLR   R15,R15                                         @SB003B0
         ICM   R15,B'0011',DATULNG     LENGTH OF MOVE          @SB003B0
         LA    R14,UX27DSN             USE VOLREF FOR LOCATE   @SB003B0
         MVC   UX27DSN,=CL44' '        PRE - BLANK             @SB003B0
         BCTR  R15,0                   DECREMENT FOR MOVE      @SB003B0
         EX    R15,MOVEPARM            MOVE DSNAME             @SB003B0
         B     UX27TLOC                GO DO LOCATE            @SB003B0
NOTOVLRF DS    0H                                              @SB003B0
         MVC   UX27DSN,UXTODSN         SET UP FOR LOCATE       @SB003B0
UX27TLOC DS    0H                      LOCATE (TO)             @SB003B0
         LA    R1,UX27DSN              ADDRESS OF DSN          @SB003B0
         ST    R1,UXCAMLST+4           TO CAMLST               @SB003B0
         LA    R1,UX27LOCA             ADDRESS OF LOC AREA     @SB003B0
         ST    R1,UXCAMLST+12          TO CAMLST               @SB003B0
         LOCATE UXCAMLST               DO THE LOCATE           @SB003B0
         LTR   R15,R15                 RETURN CODE             @SB003B0
         BNZ   TOLOCFA                 LOCATE FAILED. BRANCH   @SB003B0
         MVC   UXTOVOLS,UX27LOCA+6     USE THE FIRST VOL SER   @SB003B0
*--------------------------------------------------------------@SB003B0
*     IF TO DATASET IS NEW, ALLOC AUTHORITY IS NEEDED          @SB003B0
*--------------------------------------------------------------@SB003B0
TODETSTA DS    0H                                              @SB003B0
         OI    UXTOFLAG,UXTOVALD      VALIDATE TO DSN          @SB003B0
         BDTXTUAM START=(R9),KEY=DALSTATS,TYPE=TO,             @SB003B0X
               EOD=TODETDSP,DATU=YES                           @SB003B0
         TM    DATUPAR,BTUNEW          IS THIS A NEW DS?       @SB003B0
         BZ    TODETDSP                NO, GO CHECK DISP       @SB003B0
         OI    UXTOFLAG,UXTOALLC       SET ALLOC AUTH REQUIRED @SB003B0
*--------------------------------------------------------------@SB003B0
*     IF A NORMAL OR CONDITIONAL DISP OF DELETE IS SPECIFIED   @SB003B0
*     ALLOCATE AUTHORITY IS ALSO REQUIRED                      @SB003B0
*--------------------------------------------------------------@SB003B0
TODETDSP DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALNDISP,TYPE=TO,             @SB003B0X
               EOD=NOTONDSP,DATU=YES                           @SB003B0
         TM    DATUPAR,BTUDELET         DELETE SPECIFIED?      @SB003B0
         BZ    NOTONDSP                 NO, CHECK COND DISP    @SB003B0
         OI    UXTOFLAG,UXTOALLC        SET ALLOC AUTH REQUIRED@SB003B0
         B     TOLABELP                 GO CHECK LABEL PROCESS.@SB003B0
NOTONDSP DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALCDISP,TYPE=TO,             @SB003B0X
               EOD=TOLABELP,DATU=YES                           @SB003B0
         TM    DATUPAR,BTUDELET          DELETE SPECIFED?      @SB003B0
         BZ    TOLABELP                  NO, GO CHECK LABEL PR @SB003B0
         OI    UXTOFLAG,UXTOALLC         SET ALLOC AUTH REQUIRE@SB003B0
*--------------------------------------------------------------@SB003B0
*     NL AND BLP TAPE PROCESSING AUTHORIZATION REQUIRED?       @SB003B0
*--------------------------------------------------------------@SB003B0
TOLABELP DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALLABEL,DATU=YES,            @SB003B0X
               TYPE=TO,EOD=TOBTMS                              @SB003B0
         TM    DATUPAR,BTUNL           NL SPECIFIED?           @SB003B0
         BZ    TOBLP                   NO, GO CHECK BLP        @SB003B0
         OI    UXTOFLAG,UXTONL         SET NL PROCESSING       @SB003B0
         B     TOBTMS                  GO CHECK BYPASS TMS     @SB003B0
TOBLP    DS    0H                                              @SB003B0
         TM    DATUPAR,BTUBLP          BLP SPECFIED?           @SB003B0
         BZ    TOBTMS                  NO, GO CHECK BYPASS TMS @SB003B0
         OI    UXTOFLAG,UXTOBLP        SET BLP PROCESSING      @SB003B0
*--------------------------------------------------------------@SB003B0
*      BYPASS TMS TAPE PROCESSING AUTHORIZATION REQUIRED?      @SB003B0
*--------------------------------------------------------------@SB003B0
TOBTMS   DS    0H                                              @SB003B0
         BDTXTUAM START=(R9),KEY=DALEXPDT,DATU=YES,            @SB003B0X
               TYPE=TO,EOD=UXTOCOMP                            @SB003B0
         CLC   DATUPAR(5),=C'98000'    BYPASS TMS SPECIFIED?   @SB003B0
         BNE   UXTOCOMP                NO,FROM SIDE COMPLETE   @SB003B0
         OI    UXTOFLAG,UXTOBTMS       SET BYPASS TMS PROCESS. @SB003B0
UXTOCOMP DS    0H                      FROM SIDE COMPLETE      @SB003B0
         B     ACF2VALD                                        @SB003B0
         EJECT                                                 @SB003B0
*--------------------------------------------------------------@SB003B0
*     THE FOLLOWING ERROR RETURN (NO USERID) IS FATAL.         @SB003B0
*     FAIL THE TRANSACTION                                     @SB003B0
*--------------------------------------------------------------@SB003B0
NOUSERID DS    0H                                              @SB003B0
         LA    R15,RCFAILTR            FAIL THE TRANSACTION    @SB003B0
         B     SB003DUN                EXIT                    @SB003B0
*--------------------------------------------------------------@SB003B0
*     FOLLOWING ERROR RETURNS INDICATE THERE IS AN ERROR IN    @SB003B0
*     IN THE TRANSACTION.  THE TRANSACTION IS VALIDATED        @SB003B0
*     BECAUSE BDT WILL FAIL THE TRANSACTION, EITHER BEFORE     @SB003B0
*     OR AFTER BDTUX27 WITH A DESCRIPTIVE ERROR MESSAGE        @SB003B0
*------------------------------------------------------------- @SB003B0
NOFRMDSN DS    0H                      NO FROM DATASET SPECIFY @SB003B0
FRMLOCFA DS    0H                      LOCATE FAILED FROM DSN  @SB003B0
NOTODSN  DS    0H                      NO TO DATASET SPECIFY   @SB003B0
TOLOCFA  DS    0H                      LOCATE FAILED TO DSN    @SB003B0
         LA    R15,RCAUTHRZ            SET AS VALID            @SB003B0
         B     SB003DUN                LET BDT FAIL            @SB003B0
MOVEPARM MVC   0(0,R14),DATUPAR        EX MOVE PARAMETER       @SB003B0
DUMCAM   CAMLST NAME,DUMBY,,DUMBY      DUMMY  CAMLST           @SB003B0
CAMLNGTH EQU *-DUMCAM                  LENGTH OF DUMMY         @SB003B0
DUMBY    DS    0C                      DUMMY LABEL             @SB003B0
         EJECT                                                 @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  - ACF2 VALIDATION     ' @SB003B0
ACF2VALD DS    0H                      DO ACF2 VALIDATION      @SB003B0
*--------------------------------------------------------------@SB003B0
*    INSERT ACF2 DATASET VALIDATION CODE HERE                  @SB003B0
*    ALL REQUIRED DATA IS IN DATA AREA                         @SB003B0
*    USE   LA   R15,RCACF2FL    IF ACF2 FAILED                 @SB003B0
*          B    SB003DUN        EXIT                           @SB003B0
*    USE   LA   R15,RCAUTHRZ    IF ACCESS VALIDATED            @SB003B0
*          B    SB003DUN                                       @SB003B0
*--------------------------------------------------------------@SB003B0
***************************************************************@SB003B0
* ISSUE ACF2 VALIDATION INFO CALL TO RETURN USER-ID STRING    *@SB003B0
***************************************************************@SB003B0
         MVC   ACVLID,UX27USER    USER-ID / LID                @SB003B0
         MVC   ACFBLKID,ACFACFID   ACF BLOCK ID                @SB003B0
         OI    ACFJ3FLG,ACFJ3LID   LID ENTERED                 @SB003B0
         MVC   ACVSRCE,ACVLID      MOVE LID TO SOURCE          @SB003B0
         MVC   ACVSLID,ACVLID      MOVE LID TO SUBMITTER LID   @SB003B0
         MVC   ACVJOBV,=CL8'MVSBDT' JOBNAME                    @SB003B0
         MVI   ACFSVCRC,0          ENSURE RC=0                 @SB003B0
         MVI   ACVFCN,1            SET ACF SVC FUNCTION        @SB003B0
         MVI   ACVSFCN,ACVSINFO    SUB-FUNC TO INFO. ONLY      @SB003B0
         MVI   ACVUIDL+3,24        SET UID STRING LENGTH       @SB003B0
***************************************************************@SB003B0
*                   FINAL PARMLIST SETUP                       @SB003B0
***************************************************************@SB003B0
         LA    R0,ACFLIDAD         GET LID BUFFER ADDR OR 0    @SB003B0
         ST    R0,ACVRECB          PUT IN PARMLIST             @SB003B0
         LA    R0,ACFLIDLN         LID LENGTH OR ZERO          @SB003B0
         ST    R0,ACVRECL          PUT IN PARMLIST             @SB003B0
         LA    R0,ACFMTEXT         MESSAGE BUFFER ADDR         @SB003B0
         ST    R0,ACVMSG           PUT IN PARMLIST             @SB003B0
         LA    R0,ACFAUIDB         GET UID BUFF ADDRESS        @SB003B0
         ST    R0,ACVUIDB          PUT IN PARMLIST             @SB003B0
************************************************************** @SB003B0
*                   FIND ACF2 CVT                              @SB003B0
***************************************************************@SB003B0
*        NI    ACVRFLG,FF-ACVRMSG  ENSURE MSG FLAG RESET       @SB003B0
         ACFINCVT R5,NONE=USRNOCVT GET CVT ADDR TO R4          @SB003B0
         USING ACCVT,R5            SET FOR ADDRESSING          @SB003B0
************************************************************** @SB003B0
*                   ISSUE ACF2 VALIDATION SVC                  @SB003B0
***************************************************************@SB003B0
         ACFSVC ACVALD,CVT=HAVE    ISSUE VALIDATION CALL       @SB003B0
         DROP  R5                  DROP CVT ADDRESSING         @SB003B0
         STC   R15,ACFSVCRC        SAVE THE SVC RETURN CODE    @SB003B0
         CLI   ACFSVCRC,0          CK SVC RETURN CODE          @SB003B0
         BE    USRZERO             ALL IS OK, EXIT WITH ZERO   @SB003B0
         CLI   ACFSVCRC,4          LID VALIDATION 0 OR 4?      @SB003B0
         BNH   USRFOUR             YES, LID FILE IS OK.        @SB003B0
         B     USRGFOUR            RETURN CODE GREATER THAN 4  @SB003B0
***************************************************************@SB003B0
* ACF2 USER VALIDATION RETURN CODE PROCESSING                 *@SB003B0
***************************************************************@SB003B0
USRZERO  DS    0H                                              @SB003B0
         B     DSNSTART            USER OK. GO VALIDATE DSN    @SB003B0
USRNOCVT DS    0H                                              @SB003B0
         B     USRERR1                                         @SB003B0
USRFOUR  DS    0H                                              @SB003B0
         B     USRERR1                                         @SB003B0
USRGFOUR DS    0H                                              @SB003B0
         B     USRERR1                                         @SB003B0
USRERR1  LA    R15,RCACF08      ACF2 USER VALIDATION ERROR RC  @SB003B0
         B     SB003DUN         EXIT                           @SB003B0
         EJECT                                                 @SB003B0
***************************************************************@SB003B0
* VALIDATE DATASET                                            *@SB003B0
***************************************************************@SB003B0
***************************************************************@SB003B0
* VALIDATE FROM DATASET FOR READ AUTHORITY                    *@SB003B0
***************************************************************@SB003B0
DSNSTART DS    0H                                              @SB003B0
         BAL   R14,DSNSET          SETUP ACUCB ACDSV           @SB003B0
         TM    UXFRFLAG,UXFRVALD   VALIDATE READ REQUEST ?     @SB003B0
         BZ    DSNFRNOR            BR NO                       @SB003B0
* SETUP REMAINDER OF ACUCB ACDSV                               @SB003B0
         LA    R1,UXFRDSN          POINT TO DSNAME (FROM DSN)  @SB003B0
         ST    R1,ACFSPDSN         POINT TO DSNAME             @SB003B0
         LA    R1,UXFRVOLS         POINT TO VOLSER (FROM VOLS) @SB003B0
         ST    R1,ACFSPVOL         POINT TO VOLSER             @SB003B0
         MVI   ACFSPAC1,ACFSPAIN   VALIDATE READ ACCESS        @SB003B0
         BAL   R14,DSNVAL          VALIDATE READ ACCESS        @SB003B0
         CLI   ACFSVCRC,0          RETURN CODE ZERO            @SB003B0
         BE    DSNFRROK            BR YES                      @SB003B0
* ERROR RETURN  CODE FOR READ REQUEST OF FROM DATASET          @SB003B0
         LA    R15,RCACF12         FROM DSN READ REQ - RET CDE @SB003B0
         B     SB003DUN            EXIT FROM ACF2 VALIDATION   @SB003B0
DSNFRROK DS    0H                  FRON DSN READ  OK           @SB003B0
         B     DSNNEXT1            GO TEST NEXT                @SB003B0
DSNFRNOR DS    0H                                              @SB003B0
***************************************************************@SB003B0
* VALIDATE FROM DATASET FOR ALLOC. AUTHORITY                  *@SB003B0
***************************************************************@SB003B0
DSNNEXT1 DS    0H                                              @SB003B0
         TM    UXFRFLAG,UXFRALLC   VALIDATE ALLOC. REQUEST ?   @SB003B0
         BZ    DSNFRNOA            BR NO                       @SB003B0
* SETUP REMAINDER OF ACUCB ACDSV                               @SB003B0
         LA    R1,UXFRDSN          POINT TO DSNAME (FROM DSN)  @SB003B0
         ST    R1,ACFSPDSN         POINT TO DSNAME             @SB003B0
         LA    R1,UXFRVOLS         POINT TO VOLSER (FROM VOLS) @SB003B0
         ST    R1,ACFSPVOL         POINT TO VOLSER             @SB003B0
         MVI   ACFSPAC1,ACFSPAOT   VALIDATE ALLOC. ACCESS      @SB003B0
         BAL   R14,DSNVAL          VALIDATE ALLOC. ACCESS      @SB003B0
         CLI   ACFSVCRC,0          RETURN CODE ZERO            @SB003B0
         BE    DSNFRAOK            BR YES                      @SB003B0
* ERROR RETURN  CODE FOR ALLC REQUEST OF FROM DATASET          @SB003B0
         LA    R15,RCACF16         FROM DSN ALLC REQ - RET CDE @SB003B0
         B     SB003DUN            EXIT FROM ACF2 VALIDATION   @SB003B0
DSNFRAOK DS    0H                  FROM DSN ALLOC OK           @SB003B0
         B     DSNNEXT2            GO TEST NEXT                @SB003B0
DSNFRNOA DS    0H                                              @SB003B0
***************************************************************@SB003B0
* VALIDATE TO   DATASET FOR WRITE  AUTHORITY                  *@SB003B0
***************************************************************@SB003B0
DSNNEXT2 DS    0H                                              @SB003B0
         TM    UXTOFLAG,UXTOVALD   VALIDATE WRITE  REQUEST ?   @SB003B0
         BZ    DSNTONOW            BR NO                       @SB003B0
* SETUP REMAINDER OF ACUCB ACDSV                               @SB003B0
         LA    R1,UXTODSN          POINT TO DSNAME (TO   DSN)  @SB003B0
         ST    R1,ACFSPDSN         POINT TO DSNAME             @SB003B0
         LA    R1,UXTOVOLS         POINT TO VOLSER (TO   VOLS) @SB003B0
         ST    R1,ACFSPVOL         POINT TO VOLSER             @SB003B0
         MVI   ACFSPAC1,ACFSPAOU   VALIDATE WRITE  ACCESS      @SB003B0
         BAL   R14,DSNVAL          VALIDATE WRITE  ACCESS      @SB003B0
         CLI   ACFSVCRC,0          RETURN CODE ZERO            @SB003B0
         BE    DSNTOWOK            BR YES                      @SB003B0
* ERROR RETURN  CODE FOR WRITE REQUEST OF TO  DATASET          @SB003B0
         LA    R15,RCACF32         FROM DSN ALLC REQ - RET CDE @SB003B0
         B     SB003DUN            EXIT FROM ACF2 VALIDATION   @SB003B0
DSNTOWOK DS    0H                  FROM DSN ALLOC OK           @SB003B0
         B     DSNNEXT3            GO TEST NEXT                @SB003B0
DSNTONOW DS    0H                                              @SB003B0
***************************************************************@SB003B0
* VALIDATE TO   DATASET FOR ALLOC. AUTHORITY                  *@SB003B0
***************************************************************@SB003B0
DSNNEXT3 DS    0H                                              @SB003B0
         TM    UXTOFLAG,UXTOALLC   VALIDATE ALLOC. REQUEST ?   @SB003B0
         BZ    DSNTONOA            BR NO                       @SB003B0
* SETUP REMAINDER OF ACUCB ACDSV                               @SB003B0
         LA    R1,UXTODSN          POINT TO DSNAME (TO   DSN)  @SB003B0
         ST    R1,ACFSPDSN         POINT TO DSNAME             @SB003B0
         LA    R1,UXTOVOLS         POINT TO VOLSER (TO   VOLS) @SB003B0
         ST    R1,ACFSPVOL         POINT TO VOLSER             @SB003B0
         MVI   ACFSPAC1,ACFSPAOT   VALIDATE ALLOC. ACCESS      @SB003B0
         BAL   R14,DSNVAL          VALIDATE ALLOC. ACCESS      @SB003B0
         CLI   ACFSVCRC,0          RETURN CODE ZERO            @SB003B0
         BE    DSNTOAOK            BR YES                      @SB003B0
* ERROR RETURN  CODE FOR ALLC REQUEST OF TO   DATASET          @SB003B0
         LA    R15,RCACF36         TO   DSN ALLC REQ - RET CDE @SB003B0
         B     SB003DUN            EXIT FROM ACF2 VALIDATION   @SB003B0
DSNTOAOK DS    0H                  FROM DSN ALLOC OK           @SB003B0
         B     DSNNEXT4            GO TEST NEXT                @SB003B0
DSNTONOA DS    0H                                              @SB003B0
DSNNEXT4 DS    0H                                              @SB003B0
DSNLAST  DS    0H                  ALL ACF2 TESTS ARE OK       @SB003B0
         LA    R15,RCAUTHRZ        AUTHORIZED RETURN CODE      @SB003B0
         B     SB003DUN            EXIT FROM ACF2 VALIDATION   @SB003B0
         EJECT                                                 @SB003B0
***************************************************************@SB003B0
* SETUP THE ACUCB   CONTROL BLOCK                             *@SB003B0
***************************************************************@SB003B0
DSNSET   DS    0H                                              @SB003B0
         ST    R14,R14SAVE         SAVE RETURN LOCATION        @SB003B0
         MVC   ACULID,UX27USER     USER ID / LID               @SB003B0
         LA    R1,ACFAUIDB         UID BUFFER ADDR             @SB003B0
         ST    R1,ACUUIDP          UID BUFFER ADDR             @SB003B0
         LA    R1,LIDLID           LID BUFFER ADDR             @SB003B0
         ST    R1,ACULRECP         LID BUFFER ADDR             @SB003B0
         MVI   ACUSIND,ACUSVS2     SMF INDICATOR               @SB003B0
         MVC   ACUSCPU,TVTCPUID    CPUID                       @SB003B0
         MVC   ACUSJOBN,=CL8'MVSBDT' JOBNAME                   @SB003B0
         TIME    BIN               TIME/DATE                   @SB003B0
         STCM  R0,R15,ACUSRTIM     TIME                        @SB003B0
         STCM  R1,R15,ACUSRDAT     DATE                        @SB003B0
         MVC   ACUSMTIM,ACUSRTIM   TIME                        @SB003B0
         MVC   ACUSMDAT,ACUSRDAT   DATE                        @SB003B0
*        PACK  RCFDDBL,WTRDJNO     JOB NUMBER EBCDIC           @SB003B0
*        CVB   R1,RCFDDBL          JOB NUMBER BINARY           @SB003B0
*        STH   R1,RCFDDBL          JOB NUMBER BINARY           @SB003B0
*        MVC   ACUSUIF(2),RCFDDBL  SMF USER ID                 @SB003B0
         MVC   ACUJESID,=CL8'BDT'  IDENTIFIER                  @SB003B0
         MVC   ACUPGM,=CL8'BDT'                                @SB003B0
         MVC   ACUSTEP,=CL8'BDT'   STEPNAME. - IDENTIFIER      @SB003B0
         MVC   ACUTIDNT,=CL8'BDT'  SOURCE                      @SB003B0
         MVI   ACUT2FLG,ACUT2JOB   *************************** @SB003B0
         MVC   ACUSLIB,=CL44'SYS1.LINKLIB'                     @SB003B0
         MVC   ACUSLVOL,=CL8' '                                @SB003B0
         MVC   ACUSPGM,ACUPGM                                  @SB003B0
         MVC   ACUSKEY,=CL8' '     INITIALIZE                  @SB003B0
***************************************************************@SB003B0
* SETUP THE ACDSV   CONTROL BLOCK                             *@SB003B0
***************************************************************@SB003B0
         MVI   ACFSPREQ,ACFSPRAC   VALIDATE DSN & VOL=SER      @SB003B0
         OI    ACFSPREQ,ACFSPRFF   RETURN ERROR MESSAGE        @SB003B0
         MVI   ACFSPID1,ACFSPIEX   EXTERNAL CALL               @SB003B0
         LA    R1,ACUCB            POINT TO ACUCB              @SB003B0
         ST    R1,ACFSPAUB         POINT TO ACUCB              @SB003B0
         LA    R1,ACFMTEXT         POINT TO ERROR MSG BUFFER   @SB003B0
         ST    R1,ACFSPMSG         POINT TO ERROR MSG BUFF     @SB003B0
         L     R14,R14SAVE         RETURN LOCATION             @SB003B0
         BR    R14                 RETURN                      @SB003B0
         EJECT                                                 @SB003B0
***************************************************************@SB003B0
*  FIND ACF2 CVT                                              *@SB003B0
***************************************************************@SB003B0
DSNVAL   DS    0H                                              @SB003B0
         ST    R14,R14SAVE         SAVE RETURN LOCATION        @SB003B0
         MVI   ACFSVCRC,0          CLEAR ACF2 RETURN CODE      @SB003B0
        ACFINCVT R5,NONE=DSNNOCVT                              @SB003B0
         USING ACCVT,R5            ADDRESSABILITY              @SB003B0
***************************************************************@SB003B0
* VALIDATE DATASET                                            *@SB003B0
***************************************************************@SB003B0
         ACFSVC  ACDSV,TYPE=S,CVT=HAVE,NONE=DSNNOACD           @SB003B0
         STC   R15,ACFSVCRC        SAVE RETURN CODE            @SB003B0
         MVI   ACFSPREQ,ACFSPRCL   CLEANUP                     @SB003B0
***************************************************************@SB003B0
* CLEANUP DATASET VALIDATION REQUEST                          *@SB003B0
***************************************************************@SB003B0
         ACFSVC  ACDSV,TYPE=S,CVT=HAVE,NONE=DSNNOACD           @SB003B0
         DROP  R5                                              @SB003B0
DSNRET   L     R14,R14SAVE         RETURN LOCATION             @SB003B0
         BR    R14                 RETURN                      @SB003B0
DSNNOCVT DS    0H  NONE RETURN FROM ACFINCVT                   @SB003B0
         MVI   ACFSVCRC,4          SET RETURN CODE TO 4        @SB003B0
         B     DSNRET                                          @SB003B0
DSNNOACD DS    0H  NONE RETURN FROM ACFSVC                     @SB003B0
         MVI   ACFSVCRC,4          SET RETURN CODE TO 4        @SB003B0
         B     DSNRET                                          @SB003B0
         EJECT                                                 @SB003B0
***************************************************************@SB003B0
* C O N S T A N T S                                           *@SB003B0
***************************************************************@SB003B0
ACFACFID DC    CL6'ACFBLK'         BLOCK ID                    @SB003B0
         TITLE 'BDT USER EXIT BDTUX27  -             '         @SB003B0
         B     SB003DUN                EXIT ACF2 VALIDATION    @SB003B0
SB003DUN DS    0H                      ACF2 VALIDATION CODE EX @SB003B0
         EJECT                                                 @SB003B0
