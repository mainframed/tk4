++USERMOD(SB008A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     PRIORITY LIMITING BY ACF2                          */.
++VER(Z038) FMID(HBD1102)
            PRE(SB011A0
                SB011A0
                              )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRCUPD (BDTUX19 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX19
         EJECT                                                 @SB008A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB008A0
*@                                                             @SB008A0
*@                                                             @SB008A0
*@ FUNCTION:  PRIORITY LIMITING                                @SB008A0
*@            USING ACF2                                       @SB008A0
*@             THE USER'S PRIORITY LIMIT IS SAVED ON THE FIRST @SB008A0
*@             CALL, AND IS PLUGGED IN THE MJD THE SECOND      @SB008A0
*@             CALL IF IT IS LESS THAT THE SPECIFIED PRIORITY  @SB008A0
*@ DEPENDENCIES:                                               @SB008A0
*@             ACF2 MOD WHICH PLACES PRIORITY LIMIT IN LIDREC  @SB008A0
*@                                                             @SB008A0
*@ OUTPUT:                                                     @SB008A0
*@             FIRST CALL BSIDMAXP (BSIDRSU1+1) PRIORITY LIMIT @SB008A0
*@             SECOND CALL MJDPRTY CONTAINS PRIORITY LIMIT IF  @SB008A0
*@             LESS THAN THE SPECIFIED PRIORITY                @SB008A0
*@ CONTROL BLOCKS:                                             @SB008A0
*@             LIDREC   R/O ACCVT   R/O  ACUCB  R/O            @SB008A0
*@             BDTDBSID R/O BDTDMJD R/O                        @SB008A0
*@                                                             @SB008A0
*@ EXECUTABLE MACROS:                                          @SB008A0
*@             ACFINCVT   ACFGUCB                              @SB008A0
*@                                                             @SB008A0
*@ CHANGE ACTIVITY                                             @SB008A0
*@                                                             @SB008A0
*@                                                             @SB008A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB008A0
         EJECT ,                                               @SB008A0
*--------------------------------------------------------------@SB008A0
*      SB008 - PRIORITY LIMITING                               @SB008A0
*              SET PRIORITY TO LOWER OF SUPPLIED VALUE         @SB008A0
*              OR USER'S MAXIMUM PRIORITY ALLOWED              @SB008A0
*--------------------------------------------------------------@SB008A0
         TM    UXCALLER,UXBDTCLL       CALLED FROM BDT A.S.?   @SB008A0
         BO    UXSETPTY                YES, SET PRIORITY       @SB008A0
*--------------------------------------------------------------@SB008A0
*        IN USER'S ADDRESS SPACE, EXTRACT MAX ALLOWED PRIORITY @SB008A0
*        AND SAVE IN BSID                                      @SB008A0
*--------------------------------------------------------------@SB008A0
         ACFINCVT R7,NONE=X19NOUCB                             @SB008A0
         USING  ACCVT,R7                                       @SB008A0
         ACFGUCB R5,NONE=X19NOUCB,INLINE=YES,SYS=AOS2 F2 CB    @SB008A0
         DROP   R7                                             @SB008A0
         USING  ACUCB,R5                                       @SB008A0
         L      R5,ACULRECP        POINT TO LID REC            @SB008A0
         USING  LIDREC,R5                                      @SB008A0
BSIDMAXP EQU    BSIDRSU1+1,1,C'X'  SAVE MAX PRIORITY FOR USER  @SB008A0
         TM     LIDI2FLG,LIDI2F2   ANY PRIORITY ALLOWED        @SB008A0
         BNO    UXMVPRTY           NO, MOVE PRIORITY           @SB008A0
         MVI    BSIDMAXP,14        SET MAX AT 14               @SB008A0
         B      UX19DUN4           DONE                        @SB008A0
UXMVPRTY MVC    BSIDMAXP,LIDLPRTY  MOVE MAX TO BSID FOR LATER  @SB008A0
         B      UX19DUN4           DONE THIS CALL              @SB008A0
         DROP   R5                                             @SB008A0
*--------------------------------------------------------------@SB008A0
*        UX19 CALLED FROM BDT A.S.                             @SB008A0
*        MOVE SMALLER OF SPECIFIED OR MAX ALLOWED PRTY TO MJD  @SB008A0
*--------------------------------------------------------------@SB008A0
UXSETPTY DS    0H                                              @SB008A0
         LA    R7,BSIDDATA             POINT TO MJD            @SB008A0
         USING MJDSTART,R7                                     @SB008A0
         CLC   MJDPRTY,=X'FF'          NOT SPECIFIED?          @SB008A0
         BNE   UXBSIDNF                NO, NOT FF'S            @SB008A0
         MVI   MJDPRTY,X'04'           SET DEFAULT             @SB008A0
UXBSIDNF DS    0H                                              @SB008A0
         CLC   BSIDMAXP,MJDPRTY        IS MAX GE SPECIFIED PRTY@SB008A0
         BNL   UX19DUN4                YES, DONE               @SB008A0
         MVC   MJDPRTY,BSIDMAXP        MOVE IN MAX ALLOWED PRTY@SB008A0
         B     UX19DUN4                DONE                    @SB008A0
         DROP  R7                                              @SB008A0
UX19DUN4 DS    0H                                              @SB008A0
