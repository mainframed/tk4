++USERMOD(SJ210A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     USERID FOR BDT SUBMITTED JOBS                      */.
++VER(Z038) FMID(HJS2327)
            PRE(
                TT83362
                TT83362 UZ54964 UZ55684 UZ56647 UZ57971
                UZ58551 UZ59152 UZ59215 UZ59246 UZ60451
                UZ61177 UZ61215 UZ64973 UZ64999 UZ65185
                UZ66263 UZ66433 UZ66902 UZ68120 UZ69069
                UZ74202
                              )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRCUPD (IATOSDR )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=IATOSDR
         EJECT                                                 @SJ210A0
*@@ START --- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SJ210A0
*@                                                             @SJ210A0
*@                                                             @SJ210A0
*@ FUNCTION:  PROGAGATE USERID TO MVS/BDT SUBMITTED JOBS       @SJ210A0
*@            MOVE USERID FROM JDSDEST FIELD TO ACF2 FIELD     @SJ210A0
*@            FOR SUBMITTORS USERID                            @SJ210A0
*@                                                             @SJ210A0
*@ DEPENDENCIES:                                               @SJ210A0
*@             BDTUX19 - CREATES DYNAMIC TEXT UNIT CONTAINING  @SJ210A0
*@                       USERID SO USERID IS PLACED IN THE     @SJ210A0
*@                       DEST FIELD                            @SJ210A0
*@             OSDR009 - COPY MACRO                            @SJ210A0
*@ RESTRICTIONS:                                               @SJ210A0
*@             NONE                                            @SJ210A0
*@                                                             @SJ210A0
*@ REGISTER CONVENTIONS:  R3 - JDS                             @SJ210A0
*@                        R6 - STEPS THROUGH JDS               @SJ210A0
*@                        R14- ACF2 WSP                        @SJ210A0
*@                                                             @SJ210A0
*@ OUTPUT:     SUBMITTING USERID FILLED IN- ACFIWSLD           @SJ210A0
*@             JDSDEST RESET TO LOCAL                          @SJ210A0
*@             SOURCE SET TO BDT                               @SJ210A0
*@             ACFIWSFL SET TO ACFIWSFD ONLY                   @SJ210A0
*@ CONTROL BLOCKS:                                             @SJ210A0
*@             IATYJDS R/W                              .      @SJ210A0
*@             ACFINWSP R/W                                    @SJ210A0
*@                                                             @SJ210A0
*@ CHANGE ACTIVITY                                             @SJ210A0
*@                                                             @SJ210A0
*@                                                             @SJ210A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SJ210A0
         COPY  OSDR009             GET BDT USERID INTRDR CODE  @SJ210A0
++MAC    (OSDR009 )
  ASSEM(      /* START OF ASSEMBLE NAMES */
 IATOSDR
    )         /* END OF ASSEMBLE NAMES   */
   SYSLIB(JES3MAC )
  DISTLIB(AJES3MAC)
    /* UPDATE FOLLOWS */ .
*--------------------------------------------------------------@SJ210A0
*  PROPAGATE USERID TO MVS/BDT SPAWNED JOBS                    @SJ210A0
*  MUST IMMEDIATELY FOLLOW COPY FOR ##OSDR01                   @SJ210A0
*--------------------------------------------------------------@SJ210A0
         USING ACFINWSP,R14                                    @SJ210A0
         CLC   0(6,R6),#ACFCINT          IS THIS INTRDR?       @SJ210A0
         BC    NE,NOSJ210                NO, EXIT              @SJ210A0
         CLC   ACFIWSID,#ACF1JID         IS THIS THE ACF2 DATA?@SJ210A0
         BC    NE,NOSJ210                NO, EXIT              @SJ210A0
         CLC   ACFIWSLD,SJ210STC         FROM PAYROLL BDT?     @SJ210A0
         BE    NOSJ210                   YES, DONT PROPAGATE   @SJ210A0
         CLC   ACFIWSPG,SJ210MOD         SUBMITTED FROM BDT?   @SJ210A0
         BC    NE,NOSJ210                NO, EXIT              @SJ210A0
         TM    ACFIWSFL,ACFIWSFP+ACFIWSFS FROM AUTHORIZED STC? @SJ210A0
         BC    NALLON,NOSJ210            NO, EXIT              @SJ210A0
         MVC   ACFIWSLD,JDSDEST          MOVE IN USERID        @SJ210A0
         MVC   SJ210DST(8,R3),SJ210LOC   SET DEST (ORIG) LOCAL @SJ210A0
         MVC   ACFIWSRC,SJ210SRC         SET SOURCE TO BDT     @SJ210A0
         NI    ACFIWSFL,ACFIWSFD         SET LID PRESENT       @SJ210A0
         B     NOSJ210                  ALL DONE               @SJ210A0
         DROP  R14                                             @SJ210A0
SJ210MOD DC    CL8'BDTGSC1'              MODULE NAME FOR BDT   @SJ210A0
SJ210LOC DC    CL8'LOCAL'                USED TO RESET DEST    @SJ210A0
SJ210SRC DC    CL8'BDT'                  SOURCE                @SJ210A0
SJ210STC DC    CL8'BDTP'                 PAYROLL BDT STC       @SJ210A0
SJ210DST EQU   JDSDEST-JDSENTRY          DISPLACEMENT TO JDSDST@SJ210A0
NOSJ210  DS    0H                                              @SJ210A0
