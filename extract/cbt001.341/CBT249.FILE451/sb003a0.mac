++USERMOD(SB003A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*                                                        */.
++VER(Z038) FMID(HBD1102)
            PRE(
                SB001A0 SB001A0 SB001A0
                UZ73936
                              )         /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRCUPD (BDTUX19 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
./ CHANGE NAME=BDTUX19
         EJECT                                                 @SB003A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@ FUNCTION:  RETRIEVE USERID AND CREATE A TEXT UNIT           @SB003A0
*@            WITH KEY BTUUSR                                  @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@ DEPENDENCIES:                                               @SB003A0
*@             BDTDTUK -DEFINE TEXT UNIT KEY BTUUSR 511        @SB003A0
*@             BDTDBSID-USERID ALREADY RETRIEVED FLAG          @SB003A0
*@ RESTRICTIONS:                                               @SB003A0
*@             N/A                                             @SB003A0
*@                                                             @SB003A0
*@ REGISTER CONVENTIONS:                                       @SB003A0
*@                       R2   INPUT PARAMETER ADDRESS          @SB003A0
*@                       R5   WORK ADDRESS                     @SB003A0
*@                       R7   WORK ADDRESS                     @SB003A0
*@                       R9   BSID ADDRESS                     @SB003A0
*@ INPUT:                                                      @SB003A0
*@                       R9   BSID ADDRESS                     @SB003A0
*@                       R4   GETMAINED AREA                   @SB003A0
*@ CONTROL BLOCKS:                                             @SB003A0
*@             ACCVT R/O      ACFASVT R/O                      @SB003A0
*@             ACUCB R/O      LIDREC  R/O                      @SB003A0
*@             BDTDBSID  R/O  BDTDTUK R/O                      @SB003A0
*@             IHAPSA    R/O  IHAASCB R/O                      @SB003A0
*@ EXECUTABLE MACROS:                                          @SB003A0
*@             ACFGUCB        ACFINCVT                         @SB003A0
*@             BDTXTUAM                                        @SB003A0
*@ MESSAGES:                                                   @SB003A0
*@             BDTR1905   BDTR1907  BDTR1909                   @SB003A0
*@                                                             @SB003A0
*@ ABEND CODES:                                                @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@ CHANGE ACTIVITY                                             @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB003A0
         LIDREC                                                @SB003A0
*--------------------------------------------------------------@SB003A0
*    FOR TSO AND BATCH SUBMITTALS , RETRIEVE  USERID           @SB003A0
*--------------------------------------------------------------@SB003A0
         TM     BSIDUFLG,BSIDVLDU USERID ALREADY RETRIEVED     @SB003A0
         BO     UX19PRED          USERID ALREADY RETRIEVED     @SB003A0
         USING PSA,R0                                          @SB003A0
         L     R5,PSAANEW          POINT TO ASCB               @SB003A0
         USING ASCB,R5                                         @SB003A0
         ICM   R7,B'1111',ASCBTSB  IS IT FROM TSO?             @SB003A0
         BNE   UX19TSO             TSO, BRANCH                 @SB003A0
         ICM   R7,B'1111',ASCBJBNI IS IT BATCH?                @SB003A0
         BZ    UX19CONS            NO, FROM MCS OR JES3 CONSOLE@SB003A0
         DROP  R5                                              @SB003A0
UX19TSO  DS    0H                  HAS USERID BEEN OBTAINED?   @SB003A0
*--------------------------------------------------------------@SB003A0
*    USERID FROM TSO SESSION OR BATCH JOB HAS NOT BEEN         @SB003A0
*    RETRIEVED.  ACCESS IT AND BUILD A TEXT UNIT               @SB003A0
*--------------------------------------------------------------@SB003A0
UX19NOTD DS    0H                                              @SB003A0
         ACFINCVT R7,NONE=X19NOUCB                             @SB003A0
         USING  ACCVT,R7                                       @SB003A0
         ACFGUCB R5,NONE=X19NOUCB,INLINE=YES,SYS=AOS2 F2 CB    @SB003A0
         DROP   R7                                             @SB003A0
         USING  ACUCB,R5                                       @SB003A0
*        ACULID CONTAINS USERID                                @SB003A0
         MVC    UXUSERID,ACULID    SAVE FOR LATER USE          @SB003A0
*--------------------------------------------------------------@SB003A0
*    TEMP CODE TO LIMIT USE OF BDT SYSTEM BY DEPT GROUP        @SB003A0
*--------------------------------------------------------------@SB003A0
         L      R5,ACULRECP        POINT TO LID REC            @SB003A0
         USING  LIDREC,R5                                      @SB003A0
         LA     R7,U19DPTGP        TABLE OF VALID DEPT'S       @SB003A0
U19CHKGP CLC    =XL6'FF',0(R7)     END OF TABLE?               @SB003A0
         BE     U19INVGP           YES, INVALID GROUP          @SB003A0
         CLC    LIDGROUP(6),0(R7)  VALID GROUP?                @SB003A0
         BE     U19VLDGP           YES, VALID GROUP            @SB003A0
         LA     R7,6(,R7)          POINT TO NEXT GROUP/DEPT    @SB003A0
         B      U19CHKGP           GO CHECK NEXT GROUP         @SB003A0
         DROP   R5                                             @SB003A0
U19INVGP DS     0H                                             @SB003A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXUSRMSG,MSG9ADDR  MOVE ADDRESS OF ERR MSG      @SB003A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB003A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB003A0
         DROP  R2                                              @SB003A0
MSG9ADDR DC    AL4(UX19MSG9)                                   @SB003A0
UX19MSG9 DC    AL1(MSG9END-MSG9STRT-1)                         @SB003A0
MSG9STRT DC    C'BDTR1909 TRANSACTION REJECTED, NOT '          @SB003A0
         DC    C' AUTHORIZED BDT USER.'                        @SB003A0
MSG9END  EQU   *                                               @SB003A0
U19DPTGP DS    0H                                              @SB003A0
         DC    CL6'630205'        JES GROUP WCC                @SB003A0
         DC    CL6'712565'        JES GROUP SWCC               @SB003A0
         DC    CL6'000305'        JES GROUP ECC                @SB003A0
         DC    XL6'FF'            TERMINATOR                   @SB003A0
U19VLDGP DS    0H                 VALID USER CONTINUE          @SB003A0
         L     R7,NEXTSLOT        POINT TO NEXT AVAILABLE BYTE @SB003A0
         USING DATUNIT,R7                                      @SB003A0
         LA    R0,DATUPAR+8       END OF NEW TEXT UNIT         @SB003A0
         C     R0,BSIDENDA        AT END OF BSID?              @SB003A0
         BH    UXBSEXCD           YES, BSID EXECEEDED          @SB003A0
         MVC   DATUNUM,=AL2(1)    ONLY 1 PARAMETER IN TEXT UNIT@SB003A0
         MVC   DATULNG,=H'8'      PARAMETER 8 BYTES IN LENGTH  @SB003A0
         MVI   DATUFLG,0          ZERO THE FLAG                @SB003A0
         LA    R5,BTUUSR          SET TEXT UNIT KEY            @SB003A0
         STCM  R5,B'0011',DATUKEY                              @SB003A0
         OI    DATUFLG,DATUNDA    SET NOT DYNALLOC TU          @SB003A0
         MVC   DATUPAR(8),UXUSERID MOVE IN USERID              @SB003A0
         LA    R5,DATUPAR+8        PREPARE TO UPDATE           @SB003A0
         ST    R5,NEXTSLOT         NEXT AVAILABLE TEXTUNIT     @SB003A0
         L     R2,UXPARMAD         ADDRESS OF PARM PASSED      @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXNXTMJD,NEXTSLOT   MOVE BACK TO PARMLIST       @SB003A0
         DROP  R2,R7                                           @SB003A0
         OI     BSIDUFLG,BSIDVLDU  SET USERID RTRIEVED         @SB003A0
         B     UX19DUN1            DONE WITH USERID CODE       @SB003A0
UX19PRED DS    0H                  USERID ALREADY RETRIEVED    @SB003A0
         B     UX19DUN1            DONE WITH USERID CODE       @SB003A0
*------------------------------------------------------------  @SB003A0
*     GET ACF2 ACUCB ERROR RETURN                              @SB003A0
*     FAIL TRANSACTION AND ASK FOR RESUBMITTAL LATER           @SB003A0
*------------------------------------------------------------  @SB003A0
X19NOUCB DS    0H                 ACF2 UNAVAILABLE             @SB003A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXUSRMSG,MSG6ADDR  MOVE ADDRESS OF ERR MSG      @SB003A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB003A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB003A0
         DROP  R2                                              @SB003A0
MSG6ADDR DC    AL4(UX19MSG6)                                   @SB003A0
UX19MSG6 DC    AL1(MSG6END-MSG6STRT-1)                         @SB003A0
MSG6STRT DC    C'BDTR1906 ACF2 UNAVAILABLE AT PRESENT, '       @SB003A0
         DC    C'RESUBMIT LATER.'                              @SB003A0
MSG6END  EQU   *                                               @SB003A0
*------------------------------------------------------------  @SB003A0
*        CONSOLE SUBMITTED TRANSACTION                         @SB003A0
*        FAIL THE TRANSACTION WITH A MESSAGE                   @SB003A0
*------------------------------------------------------------  @SB003A0
UX19CONS DS    0H                  CONSOLE SUBMITTAL           @SB003A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXUSRMSG,MSG7ADDR  MOVE ADDRESS OF ERR MSG      @SB003A0
         LA    R15,RCFAILTR                                    @SB003A0
         B     UXRETURN           DONE WITH USERID CODE        @SB003A0
         DROP  R2                                              @SB003A0
MSG7ADDR DC    AL4(UX19MSG7)                                   @SB003A0
UX19MSG7 DC    AL1(MSG7END-MSG7STRT-1)                         @SB003A0
MSG7STRT DC    C'BDTR1907 TRANSACTIONS CANNOT BE SUBMITTED '   @SB003A0
         DC    C'FROM MCS OR JES3 CONSOLES.'                   @SB003A0
MSG7END  EQU   *                                               @SB003A0
         EJECT                                                 @SB003A0
*------------------------------------------------------------- @SB003A0
*    USERID PROCESSING DONE WITH NO ERRORS, CONTINUE           @SB003A0
*------------------------------------------------------------- @SB003A0
UX19DUN1 DS    0H                                              @SB003A0
BTUUSR   EQU   511                 USERID                      @SB003A0
BSIDVLDU EQU   X'02'               USERID ALREADY RETRIEVED    @SB003A0
