++USERMOD(SB011A0)  /*  ROCKWELL INTERNATIONAL USERMOD */
   /*     REPLACE WITH RENUM UX19. PRTY GDG INTRDR CMDAUTH   */.
++VER(Z038) FMID(HBD1102)
            PRE(SB001A0 SB002A0 SB003A0 SB003A2
                SB004A0 SB007A0 )       /*  END OF PREREQS  */
        /* END OF ++VER */  .
++SRC    (BDTUX19 )
   SYSLIB(JES3SRC )
  DISTLIB(AJES3SRC)
    /* UPDATE FOLLOWS */ .
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB001A0
*@                                                             @SB001A0
*@                                                             @SB001A0
*@ FUNCTION:  BDTUX19 - LANGUAGE PROCESSOR EXIT                @SB001A0
*@                                                             @SB001A0
*@                                                             @SB001A0
*@                                                             @SB001A0
*@ DEPENDENCIES:                                               @SB001A0
*@             N/A                                             @SB001A0
*@                                                             @SB001A0
*@ RESTRICTIONS:                                               @SB001A0
*@             N/A                                             @SB001A0
*@                                                             @SB001A0
*@ REGISTER CONVENTIONS:                                       @SB001A0
*@                                                             @SB001A0
*@                  REGISTER 0 = WORK REGISTER                 @SB001A0
*@                  REGISTER 1 = POINTER TO PARAMETER LIST     @SB001A0
*@                  REGISTER 2 = WORK REGISTER                 @SB001A0
*@                  REGISTER 3 = WORK REGISTER                 @SB001A0
*@                  REGISTER 4 = GETMAINED AREA                @SB001A0
*@                  REGISTER 5 = WORK REG                      @SB001A0
*@                  REGISTER 6 = BALR REGISTER                 @SB001A0
*@                  REGISTER 7 = ACCVT                         @SB001A0
*@                  REGISTER 8 = MODULE BASE                   @SB001A0
*@                  REGISTER 9 = BSID BASE                     @SB001A0
*@                  REGISTER 10= MODULE BASE                   @SB001A0
*@                  REGISTER 11= NOT IN USE                    @SB001A0
*@                  REGISTER 13= SAVE AREA SET UP BY ASAVE     @SB001A0
*@                  REGISTER 14= RETURN ADDRESS                @SB001A0
*@                  REGISTER 15= ENTRY POINT/RETURN CODE       @SB001A0
*@ ENTRY POINTS:                                               @SB001A0
*@             BDTUX19                                         @SB001A0
*@                                                             @SB001A0
*@ ENTRY PURPOSE:                                              @SB001A0
*@             N/A                                             @SB001A0
*@                                                             @SB001A0
*@ ENTRY LINKAGE:                                              @SB001A0
*@             BALR R14,15   FROM BDTLP                        @SB001A0
*@ INPUT:                                                      @SB001A0
*@             R13 -> SAVE AREA                                @SB001A0
*@             R1  -> PARAMETER LIST WHICH CONTAINS            @SB001A0
*@                    ADDRESS OF BSID                          @SB001A0
*@                    NEXT AVAILABLE BYTE IN MJD               @SB001A0
*@                    ADDRESS MSG TO RETURN TO CALLER          @SB001A0
*@                                                             @SB001A0
*@ EXIT:                                                       @SB001A0
*@             BR   R14                                        @SB001A0
*@                                                             @SB001A0
*@ OUTPUT:                                                     @SB001A0
*@             UPDATED MJD OR ERROR MESSAGE                    @SB001A0
*@                                                             @SB001A0
*@ EXTERNAL ROUTINES:                                          @SB001A0
*@             N/A                                             @SB001A0
*@                                                             @SB001A0
*@ CONTROL BLOCKS:                                             @SB001A0
*@             BDTDBSID         R/W                            @SB001A0
*@             BDTDMJD          R/W                            @SB001A0
*@             BDTDGSD          R/W                            @SB001A0
*@                                                             @SB001A0
*@ EXECUTABLE MACROS:                                          @SB001A0
*@             SETRP                                           @SB001A0
*@             GETMAIN                                         @SB001A0
*@             FREEMAIN                                        @SB001A0
*@                                                             @SB001A0
*@ CHANGE ACTIVITY                                             @SB001A0
*@       ADD OPTFLAG INPUT PARAMETER WHICH DETERMINES WHO      @SB001A1
*@       CALLED BDTUX19                                        @SB001A1
*@       CHANGE FINDTU ROUTINE TO START WHERE INPUT REGISTER   @SB001A1
*@       R1 POINTS INSTEAD OF ALWAYS AT THE BEGINNING OF MJD   @SB001A1
*@                                                             @SB001A1
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB001A0
*END OF SPECIFICATIONS*****************************************@SB001A0
         EJECT                                                 @SB002A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@ FUNCTION:  1.  EXTRACT ACF2 USERID FOR TSO AND BATCH        @SB002A0
*@                SUBMITTALS.  COMPARE THE USERID TO A TABLE   @SB002A0
*@                OF AUTHORIZED USERIDS. FAIL IN NOT IN TABLE  @SB002A0
*@            2.  PREVENT CONSOLE SUBMITTALS.                  @SB002A0
*@ DEPENDENCIES:                                               @SB002A0
*@             ACF2                                            @SB002A0
*@                                                             @SB002A0
*@ RESTRICTIONS:                                               @SB002A0
*@             N/A                                             @SB002A0
*@                                                             @SB002A0
*@ REGISTER CONVENTIONS:                                       @SB002A0
*@                                                             @SB002A0
*@ INPUT:                                                      @SB002A0
*@             PARAMETER LIST 3 FULLWORDS                      @SB002A0
*@             1. ADDRESS OF BSID                              @SB002A0
*@             2. ADDRESS OF NEXT AVAILABLE BYTE IN MJD        @SB002A0
*@             3. ADDRESS OF NEXT USER MSG.                    @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@ OUTPUT:                                                     @SB002A0
*@             1. VALIDATED TRANSACTION BY USERID AGAINST      @SB002A0
*@                INCORE TABLE                                 @SB002A0
*@             2. FAILED TRANSACTION IF NOT TSO OR BATCH       @SB002A0
*@                OR IF NOT VALID USERID                       @SB002A0
*@ EXTERNAL ROUTINES:                                          @SB002A0
*@             N/A                                             @SB002A0
*@                                                             @SB002A0
*@ CONTROL BLOCKS:                                             @SB002A0
*@             IHAPSA   R/O                                    @SB002A0
*@             IHAASCB  R/O                                    @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@ EXECUTABLE MACROS:                                          @SB002A0
*@             ACFGUCB                                         @SB002A0
*@             ACFINCVT                                        @SB002A0
*@ MESSAGES:                                                   @SB002A0
*@             BDTP1901                                        @SB002A0
*@             BDTP1902                                        @SB002A0
*@             BDTP1903                                        @SB002A0
*@             BDTP1904                                        @SB002A0
*@                                                             @SB002A0
*@ ABEND CODES:                                                @SB002A0
*@             N/A                                             @SB002A0
*@                                                             @SB002A0
*@ CHANGE ACTIVITY                                             @SB002A0
*@                                                             @SB002A0
*@                                                             @SB002A0
*@@ END ----- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB002A0
         EJECT                                                 @SB003A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@ FUNCTION:  RETRIEVE USERID AND CREATE A TEXT UNIT           @SB003A0
*@            WITH KEY BTUUSR                                  @SB003A0
*@                                                             @SB003A0
*@            CREATE DALSUSER TEXT UNIT CONTAINING USERID      @SB003A3
*@            TO PASS USERID TO JES3 FOR BDT SUBMITTED JOBS    @SB003A3
*@                                                             @SB003A0
*@ DEPENDENCIES:                                               @SB003A0
*@             BDTDTUK -DEFINE TEXT UNIT KEY BTUUSR 511        @SB003A0
*@             BDTDBSID-USERID ALREADY RETRIEVED FLAG          @SB003A0
*@ RESTRICTIONS:                                               @SB003A0
*@             N/A                                             @SB003A0
*@                                                             @SB003A0
*@ REGISTER CONVENTIONS:                                       @SB003A0
*@                       R2   INPUT PARAMETER ADDRESS          @SB003A0
*@                       R5   WORK ADDRESS                     @SB003A0
*@                       R7   WORK ADDRESS                     @SB003A0
*@                       R9   BSID ADDRESS                     @SB003A0
*@ INPUT:                                                      @SB003A0
*@                       R9   BSID ADDRESS                     @SB003A0
*@                       R4   GETMAINED AREA                   @SB003A0
*@ CONTROL BLOCKS:                                             @SB003A0
*@             ACCVT R/O      ACFASVT R/O                      @SB003A0
*@             ACUCB R/O      LIDREC  R/O                      @SB003A0
*@             BDTDBSID  R/O  BDTDTUK R/O                      @SB003A0
*@             IHAPSA    R/O  IHAASCB R/O                      @SB003A0
*@             IEFZB4D2  R/O                                   @SB003A3
*@ EXECUTABLE MACROS:                                          @SB003A0
*@             ACFGUCB        ACFINCVT                         @SB003A0
*@             BDTXTUAM                                        @SB003A0
*@ MESSAGES:                                                   @SB003A0
*@             BDTR1905   BDTR1907  BDTR1909                   @SB003A0
*@                                                             @SB003A0
*@ ABEND CODES:                                                @SB003A0
*@                                                             @SB003A0
*@                                                             @SB003A0
*@ CHANGE ACTIVITY                                             @SB003A0
*@    SB003A3    ADD DALSUSER TO PASS USERID TO JES3           @SB003A3
*@                                                             @SB003A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB003A0
         EJECT                                                 @SB004A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@ FUNCTION:   RETRIEVE ACCOUNT LITERAL AND ADD TEXT UNIT      @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@ DEPENDENCIES:                                               @SB004A0
*@             BDTDBSID - ACCTNO ALREADY RETRIEVED FLAGE       @SB004A0
*@                                                             @SB004A0
*@ REGISTER CONVENTIONS:                                       @SB004A0
*@                         R2   INPUT PARAMETER LIST           @SB004A0
*@                         R4   GETMAINE AREA                  @SB004A0
*@                         R5   WORK AREA                      @SB004A0
*@                         R7   WORK AREA                      @SB004A0
*@                         R9   BSID                           @SB004A0
*@ OUTPUT:                                                     @SB004A0
*@             MJD WITH TEXT UNIT FOR ACCOUNT NUMBER           @SB004A0
*@                                                             @SB004A0
*@ EXTERNAL ROUTINES:                                          @SB004A0
*@             FINDTU                                          @SB004A0
*@                                                             @SB004A0
*@ CONTROL BLOCKS:                                             @SB004A0
*@             CVT   R/O    IEZJSCB  R/O                       @SB004A0
*@             IEFAJCTB R/O BDTDDATU R/O                       @SB004A0
*@             BDTDBSID R/W BDTDMJD  R/W                       @SB004A0
*@ MESSAGES:                                                   @SB004A0
*@             BDTR1905                                        @SB004A0
*@                                                             @SB004A0
*@ ABEND CODES:                                                @SB004A0
*@             N/A                                             @SB004A0
*@                                                             @SB004A0
*@ CHANGE ACTIVITY                                             @SB004A0
*@                                                             @SB004A0
*@                                                             @SB004A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB004A0
         EJECT                                                 @SB011A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB011A0
*@ FUNCTION:   CHANGE REMOTE LOC'S TO SECONDARY LOC'S          @SB011A0
*@             BY REPLACING 3RD CHARACTER WITH AN R.           @SB011A0
*@             DETERMINE IF REMOTE BY COMPARING TO SYSNAME     @SB011A0
*@                                                             @SB011A0
*@ DEPENDENCIES: SYSNAME MUST BE S#NODE FORMAT                 @SB011A0
*@                                                             @SB011A0
*@ CONTROL BLOCKS:   CVT R/O  BDTDMJD R/W                      @SB011A0
*@                                                             @SB011A0
*@ CHANGE ACTIVITY                                             @SB011A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB011A0
         EJECT                                                 @SB005A2
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB005A2
*@                                                             @SB005A2
*@                                                             @SB005A2
*@ FUNCTION:  MOVE USERID TO XOID FOR TRANSACTIONS TO          @SB005A2
*@            PROVIDE OWNERSHIP OF THE TRANSACTION.            @SB005A2
*@            USER EXIT  31 USES THE USERID IN THE XOID        @SB005A2
*@            TO DETERMINE WHETHER A TSO USER CAN INQUIRE      @SB005A2
*@            UPON OR MODIFY A TRANSACTIONS                    @SB005A2
*@ DEPENDENCIES:                                               @SB005A2
*@             SB003 EXTRACTS THE USERID.                      @SB005A2
*@                                                             @SB005A2
*@ RESTRICTIONS:                                               @SB005A2
*@             BDT I Q * COMMAND WILL NOT WORK AFTER THIS MOD  @SB005A2
*@             BDT I Q WORKS LIKE BDT I Q *                    @SB005A2
*@ OUTPUT:                                                     @SB005A2
*@             BSIDXRU1  +  BSIDXRU2 CONTAINS THE USERID       @SB005A2
*@                                                             @SB005A2
*@ CONTROL BLOCKS:                                             @SB005A2
*@             BDTDBSID    R/W                                 @SB005A2
*@ CHANGE ACTIVITY                                             @SB005A2
*@                                                             @SB005A2
*@                                                             @SB005A2
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB005A2
         EJECT                                                 @SB009A0
*@@ START --- ROCKWELL INTERNATIONAL BDT  MOD PROLOG --------- @SB009A0
*@                                                             @SB009A0
*@                                                             @SB009A0
*@ FUNCTION:  DISALLOW RELATIVE GENERATION GROUP USEAGE BY     @SB009A0
*@            BDT DUE TO GDG WEAKNESSES IN THE PRODUCT.        @SB009A0
*@                                                             @SB009A0
*@ EXTERNAL ROUTINES:                                          @SB009A0
*@             FINDTU                                          @SB009A0
*@                                                             @SB009A0
*@ CONTROL BLOCKS:                                             @SB009A0
*@             BDTDMJD  R/O BDTDDATU  R/O                      @SB009A0
*@                                                             @SB009A0
*@ MESSAGES:                                                   @SB009A0
*@             BDTR1910                                        @SB009A0
*@                                                             @SB009A0
*@ CHANGE ACTIVITY                                             @SB009A0
*@                                                             @SB009A0
*@                                                             @SB009A0
*@@ END ----- ROCKWELL INTERNATIONAL JES3 MOD PROLOG --------- @SB009A0
BDTUX19  START   0                                             @SB001A0
         ACCVT                                                 @SB002A0
         ACFASVT                                               @SB002A0
         ACUCB                                                 @SB002A0
         BDTDBSID                                              @SB001A0
BSIDUFLG EQU   BSIDRSU1,1,C'X'     USER FLAG                   @SB001A0
BTUUSR   EQU   511                 USERID                      @SB003A0
BSIDVLDU EQU   X'02'               USERID ALREADY RETRIEVED    @SB003A0
BSIDLOC  EQU   X'04'               LOC, 2ND PASS THRU UX19     @SB003A0
BSIDACCT EQU   X'01'               ACCT NO ALREAD RETRIEVE     @SB004A0
         BDTDDATU                                              @SB001A0
         BDTDGSD                                               @SB001A0
         BDTDMJD                                               @SB001A0
         CVT    DSECT=YES                                      @SB004A0
         IEFAJCTB                                              @SB004A0
         IEFZB4D2                                              @SB003A3
         IEZJSCB                                               @SB004A0
         IHAASCB                                               @SB002A0
         IHASDWA                                               @SB001A0
         IHAPSA                                                @SB001A0
         IKJTCB LIST=YES                                       @SB001A0
         LIDREC                                                @SB003A0
         BDTDREG                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        USER EQUATES                                          @SB001A0
*--------------------------------------------------------------@SB001A0
RCAUTHRZ EQU   0                   RETURN CODE 0 = AUTHORIZED  @SB001A0
RCFAILTR EQU   4                   RETURN CODE 4 = UNAUTHORIZED@SB001A0
RCRETRY  EQU   4                   RETURN CODE 4 = RETRY       @SB001A0
RCNORTRY EQU   0                   RETURN CODE 0 = NO RETRY    @SB001A0
ZERO     EQU   0                   USE TO INITIALIZE STORAGE   @SB001A0
*--------------------------------------------------------------@SB001A0
*        USER DSECT FOR WORK AREA                              @SB001A0
*                                                              @SB001A0
*        WORK AREA IS PASSED TO ESTAE MACRO AS USER PARAMETER L@SB001A0
*        THIS IS NECESSARY SO THAT, IF AN ABEND OCCURS, ADDRESS@SB001A0
*        BILITY TO THE AREA CAN BE REESTABLISHED.  DSECT CONTAI@SB001A0
*        FIELDS WHICH ARE NEEDED IN ESTAE RECOVERY ROUTINE, RET@SB001A0
*        ROUTINE OR COMMON EXIT FROM MODULE.                   @SB001A0
*                                                              @SB001A0
*        NOTE:  IF ADDITIONAL WORK AREA IS REQUIRED, ADD IT TO @SB001A0
*        DSECT (PREFERABLY BETWEEN UXESTLEN AND UXMAPLEN), SO  @SB001A0
*        ADDITIONAL GETMAINS ARE NOT NECESSARY.                @SB001A0
*--------------------------------------------------------------@SB001A0
UXPARMAP DSECT                     MAP OF USER PARAMETER LIST  @SB001A0
UXWORKID DS    F                   CONTAINS 'WORK' FOR IDENTIFI@SB001A0
UXTVTADD DS    F                   ADDRESS OF TVT,SAVED FOR RET@SB001A0
UXGSDADD DS    F                   ADDRESS OF GSD,SAVED FOR ABE@SB001A0
UXBASE   DS    F                   MOD BASE ADR,SAVED FOR RETRY@SB001A0
UXBASE2  DS    F                   MOD BASE2 ADDR              @SB001A0
UXTOKEN  DS    F                   TOKEN USED IN ESTAE         @SB001A0
UXESTFLG DS    B                   INDICATES ESTAE STATUS      @SB001A0
UXESTSUC EQU   BIT0                ESTAE ESTABLISHED SUCCESSFUL@SB001A0
UXESTREP EQU   BIT1                ESTAE RECOVERY ALREADY ENTER@SB001A0
UXESTAPM DS    0F                  ESTAE MACRO PARAMETER LIST  @SB001A0
         ESTAE MF=L                                            @SB001A0
UXESTLEN EQU   *-UXESTAPM          LENGTH OF ESTAE LIST        @SB001A0
*************END OF STANDARD GETMAIN AREA*********             @SB001A0
UXPARMAD DS    F                  SAVE ADDRESS OF PARM LIST    @SB001A0
UXSAVE   DS    18F                                             @SB001A0
UXUSERID DS    CL8                USERID FROM ACFGUCB          @SB002A0
NEXTSLOT DS    F                  ADDRESS OF NEXT BYTE IN MJD  @SB001A0
BSIDENDA DS    F                  END ADDRESS OF BSID          @SB001A0
FINDTUSR DS    F                  SAVE RETURN FROM FINDTU      @SB001A0
UXCALLER DS    X                  UX19 CALLER FLAG             @SB001A1
UXBDTCLL EQU   X'80'              UX19 CALLED FROM BDT A.S.    @SB001A1
UXMAPLEN EQU   *-UXPARMAP          LENGTH OF THIS MAPPING      @SB001A0
*--------------------------------------------------------------@SB001A0
*        DSECT FOR PARAMETER LIST FROM CALLER                  @SB001A0
*                                                              @SB001A0
*        PARAMETER LIST (12 BYTES)                             @SB001A0
*--------------------------------------------------------------@SB001A0
UXPARMIN DSECT                     PARAMETER LIST PASSED BY CAL@SB001A0
UXBSIDAD DS    A                   ADDRESS OF BSID             @SB001A0
UXNXTMJD DS    A                   NEXT AVAILABLE BYTE IN MJD  @SB001A0
UXUSRMSG DS    A                   ADDRESS OF USER MSG         @SB001A0
UXOPTFLG DS    A                   ADDRESS OF FLAG             @SB001A1
BDTUX19  CSECT                                                 @SB001A0
*--------------------------------------------------------------@SB001A0
*        INVOKE BDTDMOD TO IDENTIFY MODULE                     @SB001A0
*                                                              @SB001A0
*        NOTE THAT CONSTANTS GENERATED BY THIS MACRO ARE ALSO U@SB001A0
*        IN ESTAE RECOVERY ROUTINE TO UPDATE THE SDWA.         @SB001A0
*--------------------------------------------------------------@SB001A0
*        BDTDMOD LABELS=YES                                    @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
         B     ENDCATCH-*(,R15)    BR AROUND ENTRY INFORMATION @SB001A0
         DC    AL1(ENDCATCH-*-1)   LENGTH OF ENTRY INFORMATION @SB001A0
MDNUX19  DC    CL8'BDTUX19'        MODULE NAME                 @SB001A0
MDLUX19  DC    CL8'       '        LABEL NAME                  @SB001A0
MDRUX19  DC    CL9' HBD1102 '      BDT RELEASE OR PTF NUMBER   @SB001A0
MDDUX19  DC    CL8'&SYSDATE'       ASSEMBLY DATE               @SB001A0
MDTUX19  DC    CL6'-&SYSTIME'      ASSEMBLY TIME               @SB001A0
         SPLEVEL SET=1                                         @SB001A0
ENDCATCH DS    0H                                              @SB001A0
*        ESTABLISH ADDRESSABILITY                              @SB001A0
*                                                              @SB001A0
*        REGISTERS ARE NOT SAVED BECAUSE THE CALLING PROGRAM   @SB001A0
*  SAVES THEM VIA BDTXUEX.                                     @SB001A0
*--------------------------------------------------------------@SB001A0
UXINIT   DS    0H                                              @SB001A0
         SAVE  (14,12)             SAVE THE REGISTERS          @SB001A0
         LR    R10,R15             SET BASE REGISTER           @SB001A0
         USING BDTUX19,R10,R8      ESTABLISH ADDRESSABILITY    @SB001A0
         LA    R8,1(R10)           2ND BASE                    @SB001A0
         LA    R8,4095(R8)         2ND BASE                    @SB001A0
         USING UXPARMIN,R1         ESTABLISH ADDRESSAB. TO PARM@SB001A0
         L     R9,UXBSIDAD         SET BASE FOR BSID           @SB001A0
         USING BSID,R9             ESTABLISH ADDRESSAB. TO BSID@SB001A0
         LR    R2,R1               SAVE ADDRESS OF PARMLIST    @SB001A0
         DROP  R1                                              @SB001A0
*--------------------------------------------------------------@SB001A0
*        GETMAIN WORK AREA                                     @SB001A0
*                                                              @SB001A0
*        IF GETMAIN IS UNSUCCESSFUL, SET REGISTER 4 TO ZERO TO @SB001A0
*        INDICATE FAILURE, SKIP ESTAE SETUP AND BEGIN          @SB001A0
*        MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER RECOVERY @SB001A0
*        ROUTINE ESTABLISHED FOR CALLER.                       @SB001A0
*--------------------------------------------------------------@SB001A0
         LA    R4,UXMAPLEN         LENGTH OF AREA NEEDED       @SB001A0
         GETMAIN RC,LV=(R4)        GET STORAGE FOR PARAMETER LI@SB001A0
         LTR   R15,R15             WAS STORAGE OBTAINED?       @SB001A0
         BZ    UXGETOK             YES, INITIALIZE STORAGE     @SB001A0
         XR    R4,R4               NO, INDICATE GETMAIN FAILURE@SB001A0
         B     UXRTONLY            RETURN                      @SB001A0
UXGETOK  DS    0H                                              @SB001A0
         LR    R4,R1               SET BASE FOR PARM LIST      @SB001A0
         USING UXPARMAP,R4         ESTABLISH ADDRESSABILITY TO @SB001A0
         MVI   UXPARMAP,ZERO       SET PAD CHARACTER TO ZERO   @SB001A0
*                                  INITIALIZE STORAGE TO ZEROES@SB001A0
         MVC   UXPARMAP+1(UXMAPLEN-1),UXPARMAP                 @SB001A0
         MVC   UXWORKID,UXWKCONS   SET IDENTIFICATION TO 'WORK'@SB001A0
         ST    R13,UXSAVE+4        SAVE OLD SAVE AREA ADDR     @SB001A0
         LA    R11,UXSAVE          NEW SAVE AREA ADDR          @SB001A0
         ST    R11,8(R13)          SAVE NEW SAVE AREA          @SB001A0
         LR    R13,R11                                         @SB001A0
*--------------------------------------------------------------@SB001A0
*        ESTABLISH AN ESTAE                                    @SB001A0
*                                                              @SB001A0
*        1. BUILD USER PARAMETER LIST.                         @SB001A0
*        2. INITIALIZE ESTAE MACRO LIST;  ISSUE ESTAE.         @SB001A0
*        3. IF ESTAE SUCCESSFUL, SET BIT 0 IN 'ESTAE SUCCESS FL@SB001A0
*           TO 1.                                              @SB001A0
*        4. IF ESTAE IS UNSUCCESSFUL, LEAVE BIT 0 IN 'ESTAE SUC@SB001A0
*           FLAG' EQUAL TO ZERO.  (FLAG CONTAINS ALL ZEROES AS @SB001A0
*           RESULT OF STORAGE INITIALIZATION AFTER GETMAIN.)   @SB001A0
*           BEGIN MAINLINE ROUTINE - MODULE WILL EXECUTE UNDER @SB001A0
*           RECOVERY ESTABLISHED FOR CALLER.                   @SB001A0
*--------------------------------------------------------------@SB001A0
         USING PSA,R0              ESTABLISH ADDRESSAB. TO PSA @SB001A0
         L     R3,PSATOLD          SET BASE FOR TCB            @SB001A0
         USING TCB,R3              ESTABLISH ADDRESSAB. TO TCB @SB001A0
         L     R3,TCBBDT           SET BASE FOR GSD            @SB001A0
         USING GSDSTART,R3         ESTABLISH ADDRESSAB. TO GSD @SB001A0
         CLC   GSDID,UXGSDID       IS THERE A GSD?             @SB001A0
         BNE   UXNOESTA            NO, SKIP ESTAE SET UP       @SB001A0
         ST    R3,UXGSDADD         STORE ADDRESS OF GSD IN PARM@SB001A0
         ST    R10,UXBASE          STORE MODULE BASE FOR RETRY @SB001A0
         ST    R8,UXBASE2          STORE SECOND BASE           @SB001A0
*                                  MOVE ESTAE MODEL OVER STORAG@SB001A0
         MVC   UXESTAPM(UXESTLEN),ESTMODEL                     @SB001A0
         ESTAE UXESTAE,CT,PARAM=(R4),RECORD=YES,               @SB001A0+
               TOKEN=UXTOKEN,MF=(E,UXESTAPM)                   @SB001A0
         LTR   R15,R15             WAS ESTAE SUCCESSFUL?       @SB001A0
         BNZ   UXNOESTA            NO, DO NOT SET FLAG         @SB001A0
         OI    UXESTFLG,UXESTSUC   SET FLAG SUCDESSFULE EXTAEL @SB001A0
UXNOESTA DS    0H                                              @SB001A0
         DROP  R0,R3                                           @SB001A0
*--------------------------------------------------------------@SB001A0
*        MAINLINE ROUTINE                                      @SB001A0
*                                                              @SB001A0
*                                                              @SB001A0
*                                                              @SB001A0
*--------------------------------------------------------------@SB001A0
UXMAIN   DS    0H                                              @SB001A0
         USING UXPARMIN,R2                                     @SB001A0
         MVC   NEXTSLOT,UXNXTMJD       MOVE NEXT BYTE TO GETMAI@SB001A0
         LH    R0,BSIDTOTL             LENGTH OF BSID          @SB001A0
         AR    R0,R9                   END OF BSID             @SB001A0
         ST    R0,BSIDENDA             SAVE BSID END ADDRESS   @SB001A0
         L     R3,UXOPTFLG         ADDRESS OF OPTFLAG          @SB001A1
         MVC   UXCALLER,0(R3)      SAVE CALLER FLAG            @SB001A1
         DROP  R2                                              @SB001A0
         ST    R2,UXPARMAD         SAVE ADDRESS OF PARMLIST    @SB001A0
*--------------------------------------------------------------@SB002A0
*    SEPARATE REQUEST FOR PAYROLL BDT AND PROD BDT             @SB002A0
*--------------------------------------------------------------@SB002A0
         CLI  BSIDXBSI,X'00'       SYSTEM NOT SPECIFIED        @SB002A0
         BE   UX19DFSY             DEFAULT SYSTEM              @SB002A1
         LA   R2,BSIDXBSI          BEGINNING OF SYSTEM         @SB002A0
         LA   R3,8                 LENGTH OF SYSTEM            @SB002A0
UX19BCTB CLI  0(R2),C'P'           IS IT THE PAYROLL SYSTEM    @SB002A0
         BNE  UX19BCTR             NO, GO DECREMENT COUNT      @SB002A0
         C    R3,=F'1'             IS IT THE LAST CHAR?        @SB002A0
         BE   UX19PAYR             YES, PAYROLL BDT            @SB002A0
         CLI  1(R2),C' '           IS THE NEXT CHARACTER BLANK @SB002A0
         BE   UX19PAYR             YES, PAYROLL BDT            @SB002A0
UX19BCTR LA   R2,1(,R2)            INCREMENT R2                @SB002A0
         BCT  R3,UX19BCTB          GO TO BEGIN BCT LOOP        @SB002A0
         B    UX19PRDB             PRODUCTION BDT              @SB002A0
UX19PAYR DS   0H                                               @SB002A0
         TM    BSIDUFLG,BSIDVLDU   USERID ALREADY VALIDATED.   @SB002A0
         BO    UXRETURN            YES, EXIT                   @SB002A0
*--------------------------------------------------------------@SB002A0
*    FOR TSO AND BATCH SUBMITTALS , RETRIEVE  USERID           @SB002A0
*    FIRST DISALLOW MCS AND JES3 SUBMITTALS                    @SB002A0
*--------------------------------------------------------------@SB002A0
         USING PSA,R0                                          @SB002A0
         L     R5,PSAANEW          POINT TO ASCB               @SB002A0
         USING ASCB,R5                                         @SB002A0
         ICM   R7,B'1111',ASCBTSB  IS IT FROM TSO?             @SB002A0
         BNE   XX19TSO             TSO, BRANCH                 @SB002A0
         ICM   R7,B'1111',ASCBJBNI IS IT BATCH?                @SB002A0
         BZ    XX19CONS            NO, FROM MCS OR JES3 CONSOLE@SB002A0
         DROP  R5                                              @SB002A0
XX19TSO  DS    0H                  HAS USERID BEEN OBTAINED?   @SB002A0
*--------------------------------------------------------------@SB002A0
*    ACCESS USERID FROM ACF2 CONTROL BLOCK                     @SB002A0
*--------------------------------------------------------------@SB002A0
XX19NOTD DS    0H                                              @SB002A0
         ACFINCVT R7,NONE=U19NOUCB                             @SB002A0
         USING  ACCVT,R7                                       @SB002A0
         ACFGUCB R5,NONE=U19NOUCB,INLINE=YES,SYS=AOS2          @SB002A0
         DROP   R7                                             @SB002A0
         USING  ACUCB,R5                                       @SB002A0
*        ACULID CONTAINS USERID                                @SB002A0
         MVC    UXUSERID,ACULID    SAVE FOR LATER USE          @SB002A0
         DROP   R5                                             @SB002A0
         LA    R5,USERTBL         POINT TO FIRST VALID USERID  @SB002A0
USERLP   DS    0H                 LOOP ON USERIDS              @SB002A0
         CLC   0(8,R5),=XL8'FF'   END OF TABLE?                @SB002A0
         BE    USERINVD           YES, USER IS INVALID         @SB002A0
         CLC   UXUSERID,0(R5)     USERIDS MATCH?               @SB002A0
         BE    USERVALD           YES, USER IS VALID           @SB002A0
         LA    R5,8(R5)           BUMP TO NEXT ENTRY           @SB002A0
         B     USERLP             CONTINUE LOOPING             @SB002A0
*--------------------------------------------------------------@SB002A0
*      USER IS VALID                                           @SB002A0
*      NO ACTION REQUIRED                                      @SB002A0
*--------------------------------------------------------------@SB002A0
USERVALD DS    0H                                              @SB002A0
         OI    BSIDUFLG,BSIDVLDU   SET USERID VALIDATED.       @SB002A0
         LA    R15,RCAUTHRZ        SET AS AUTHORIZED           @SB002A0
         B     UXRETURN            ADDRESS OF PARM PASSED      @SB002A0
*--------------------------------------------------------------@SB002A0
*      USER IS INVALID                                         @SB002A0
*      REJECTION IDICATED BY PLACING MSG ADDRESS IN PARM       @SB002A0
*--------------------------------------------------------------@SB002A0
USERINVD DS    0H                                              @SB002A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB002A0
         USING UXPARMIN,R2                                     @SB002A0
         MVC   UXUSRMSG,MSG1ADDR  ADDRESS OF ERR MSG           @SB002A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB002A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB002A0
         DROP  R2                                              @SB002A0
MSG1ADDR DC    AL4(UX19MSG1)                                   @SB002A0
UX19MSG1 DC    AL1(MSG1END-MSG1STRT-1)                         @SB002A0
MSG1STRT DC    C'BDTP1901 UNAUTHORIZED USER OF PAYROLL '       @SB002A0
         DC    C'BDT SYSTEM. TRANSACTION FAILED.'              @SB002A0
MSG1END  EQU   *                                               @SB002A0
*------------------------------------------------------------  @SB002A0
*        CONSOLE SUBMITTED TRANSACTION                         @SB002A0
*        FAIL THE TRANSACTION WITH A MESSAGE                   @SB002A0
*------------------------------------------------------------  @SB002A0
XX19CONS DS    0H                  CONSOLE SUBMITTAL           @SB002A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB002A0
         USING UXPARMIN,R2                                     @SB002A0
         MVC   UXUSRMSG,MSG2ADDR  ADDRESS OF ERROR MESSAGE     @SB002A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB002A0
         B     UXRETURN           DONE WITH USERID CODE        @SB002A0
         DROP  R2                                              @SB002A0
MSG2ADDR DC    AL4(UX19MSG2)                                   @SB002A0
UX19MSG2 DC    AL1(MSG2END-MSG2STRT-1)                         @SB002A0
MSG2STRT DC    C'BDTP1902 TRANSACTIONS CANNOT BE SUBMITTED '   @SB002A0
         DC    C'FROM MCS OR JES3 CONSOLES.'                   @SB002A0
MSG2END  EQU   *                                               @SB002A0
*------------------------------------------------------------  @SB002A0
*     GET ACF2 ACUCB ERROR RETURN                              @SB002A0
*     FAIL TRANSACTION AND ASK FOR RESUBMITTAL LATER           @SB002A0
*------------------------------------------------------------  @SB002A0
U19NOUCB DS    0H                 ACF2 UNAVAILABLE             @SB002A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB002A0
         USING UXPARMIN,R2                                     @SB002A0
         MVC   UXUSRMSG,MSG3ADDR  ADDRESS OF ERROR MSG         @SB002A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB002A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB002A0
         DROP  R2                                              @SB002A0
MSG3ADDR DC    AL4(UX19MSG3)                                   @SB002A0
UX19MSG3 DC    AL1(MSG3END-MSG3STRT-1)                         @SB002A0
MSG3STRT DC    C'BDTP1903 ACF2 UNAVAILABLE AT PRESENT, '       @SB002A0
         DC    C'RESUBMIT LATER.'                              @SB002A0
MSG3END  EQU   *                                               @SB002A0
*------------------------------------------------------------- @SB002A0
*  TABLE OF 8 BYTE VALID PAYROLL USERIDS                       @SB002A0
*--------------------------------------------------------------@SB002A0
USERTBL  DS    0F                                              @SB002A0
         DC    CL8'YAC030A'            PAYROLL                 @SB002A0
         DC    CL8'$A1149'             PAYROLL                 @SB002A0
         DC    CL8'$A2023'             PAYROLL                 @SB002A0
         DC    CL8'YABDT0'             CNTRLS TEST             @SB002A0
         DC    CL8'$A2389'             GRANT                   @SB002A0
         DC    CL8'$A1001'             BILL                    @SB002A0
         DC    CL8'$A1238'             LIONEL                  @SB002A0
         DC    CL8'$A1089'             CINDY                   @SB002A0
         DC    CL8'$A1035'             AL                      @SB002A0
         DC    CL8'$A1019'             DOUG                    @SB002A0
         DC    CL8'ASYSJDR'            DISK READER             @SB002A0
         DC    CL8'$A1050'             PAUL                    @SB002A0
         DC    CL8'$A1070'             DALE                    @SB002A0
         DC    XL8'FF'             END OF TABLE                @SB002A0
*--------------------------------------------------------------@SB002A0
*    STANDARD BDT                                              @SB002A0
*--------------------------------------------------------------@SB002A0
UX19DFSY DS    0H                                              @SB002A0
UX19PRDB DS    0H                                              @SB002A0
*--------------------------------------------------------------@SB003A0
*    FOR TSO AND BATCH SUBMITTALS , RETRIEVE  USERID           @SB003A0
*--------------------------------------------------------------@SB003A0
         TM     BSIDUFLG,BSIDVLDU USERID ALREADY RETRIEVED     @SB003A0
         BO     UX19PRED          USERID ALREADY RETRIEVED     @SB003A0
         USING PSA,R0                                          @SB003A0
         L     R5,PSAANEW          POINT TO ASCB               @SB003A0
         USING ASCB,R5                                         @SB003A0
         ICM   R7,B'1111',ASCBTSB  IS IT FROM TSO?             @SB003A0
         BNE   UX19TSO             TSO, BRANCH                 @SB003A0
         ICM   R7,B'1111',ASCBJBNI IS IT BATCH?                @SB003A0
         BZ    UX19CONS            NO, FROM MCS OR JES3 CONSOLE@SB003A0
         DROP  R5                                              @SB003A0
UX19TSO  DS    0H                  HAS USERID BEEN OBTAINED?   @SB003A0
*--------------------------------------------------------------@SB003A0
*    USERID FROM TSO SESSION OR BATCH JOB HAS NOT BEEN         @SB003A0
*    RETRIEVED.  ACCESS IT AND BUILD A TEXT UNIT               @SB003A0
*--------------------------------------------------------------@SB003A0
UX19NOTD DS    0H                                              @SB003A0
         ACFINCVT R7,NONE=X19NOUCB                             @SB003A0
         USING  ACCVT,R7                                       @SB003A0
         ACFGUCB R5,NONE=X19NOUCB,INLINE=YES,SYS=AOS2 F2 CB    @SB003A0
         DROP   R7                                             @SB003A0
         USING  ACUCB,R5                                       @SB003A0
*        ACULID CONTAINS USERID                                @SB003A0
         MVC    UXUSERID,ACULID    SAVE FOR LATER USE          @SB003A0
         MVC    BSIDXRU1(8),ACULID    USERID FOR UX31          @SB005A2
U19VLDGP DS    0H                 VALID USER CONTINUE          @SB003A0
         L     R7,NEXTSLOT        POINT TO NEXT AVAILABLE BYTE @SB003A0
         USING DATUNIT,R7                                      @SB003A0
         LA    R0,DATUPAR+8       END OF NEW TEXT UNIT         @SB003A0
         C     R0,BSIDENDA        AT END OF BSID?              @SB003A0
         BH    UXBSEXCD           YES, BSID EXECEEDED          @SB003A0
         MVC   DATUNUM,=AL2(1)    ONLY 1 PARAMETER IN TEXT UNIT@SB003A0
         MVC   DATULNG,=H'8'      PARAMETER 8 BYTES IN LENGTH  @SB003A0
         MVI   DATUFLG,0          ZERO THE FLAG                @SB003A0
         LA    R5,BTUUSR          SET TEXT UNIT KEY            @SB003A0
         STCM  R5,B'0011',DATUKEY                              @SB003A0
         OI    DATUFLG,DATUNDA    SET NOT DYNALLOC TU          @SB003A0
         MVC   DATUPAR(8),UXUSERID MOVE IN USERID              @SB003A0
         LA    R5,DATUPAR+8        PREPARE TO UPDATE           @SB003A0
         ST    R5,NEXTSLOT         NEXT AVAILABLE TEXTUNIT     @SB003A0
         L     R2,UXPARMAD         ADDRESS OF PARM PASSED      @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXNXTMJD,NEXTSLOT   MOVE BACK TO PARMLIST       @SB003A0
         DROP  R2,R7                                           @SB003A0
         OI     BSIDUFLG,BSIDVLDU  SET USERID RTRIEVED         @SB003A0
         B     UX19DUN1            DONE WITH USERID CODE       @SB003A0
UX19PRED DS    0H                  USERID ALREADY RETRIEVED    @SB003A0
*--------------------------------------------------------------@SB003A3
*           FOR INTRDR READER TRANSACTIONS ONLY                @SB003A3
*           CREATE A TEXT UNIT DALSUSER CONTAINING USERID      @SB003A3
*           TO PASS THE USERID TO JES3 FOR THE SUBMITTED JOB   @SB003A3
*--------------------------------------------------------------@SB003A3
         LA    R0,DALSPGNM         TEXT UNIT FOR WRITER NAME   @SB003A3
         LA    R1,BSIDDATA         START BEGINNING OF MJD      @SB003A3
         BAL   R6,FINDTU           WAS IT SPECIFIED            @SB003A3
         B     UX19DUN1    RC=0    NO, ALL DONE                @SB003A3
UX19IRDR DS    0H          RC=4    YES, INTRDR TRANSACTION     @SB003A3
         LA    R0,BTUUSR           USERID TEXT UNIT            @SB003A3
         LA    R1,BSIDDATA         START BEGINNING OF MJD      @SB003A3
         BAL   R6,FINDTU           WAS IT SPECIFIED            @SB003A3
         B     UX19DUN1   RC=0     NO,NEVER OCCURS  (RC=0)     @SB003A3
         USING DATUNIT,R1                                      @SB003A3
         LA    R3,DATUPAR RC=4     POINT TO THE USERID         @SB003A3
         DROP  R1                                              @SB003A3
         L     R7,NEXTSLOT        POINT TO NEXT AVAILABLE BYTE @SB003A3
         USING DATUNIT,R7                                      @SB003A3
         LA    R0,DATUPAR+8       END OF NEW TEXT UNIT         @SB003A3
         C     R0,BSIDENDA        AT END OF BSID?              @SB003A3
         BH    UXBSEXCD           YES, BSID EXECEEDED          @SB003A3
         MVC   DATUNUM,=AL2(1)    ONLY 1 PARAMETER IN TEXT UNIT@SB003A3
         MVC   DATULNG,=H'8'      PARAMETER 8 BYTES IN LENGTH  @SB003A3
         MVI   DATUFLG,0          ZERO THE FLAG                @SB003A3
         LA    R5,DALSUSER        SET TEXT UNIT KEY            @SB003A3
         STCM  R5,B'0011',DATUKEY                              @SB003A3
         OI    DATUFLG,DATUDST     SET DYNAMIC TO SIDE         @SB003A3
         MVC   DATUPAR(8),0(R3)    MOVE IN USERID              @SB003A3
         LA    R5,DATUPAR+8        PREPARE TO UPDATE           @SB003A3
         ST    R5,NEXTSLOT         NEXT AVAILABLE TEXTUNIT     @SB003A3
         L     R2,UXPARMAD         ADDRESS OF PARM PASSED      @SB003A3
         USING UXPARMIN,R2                                     @SB003A3
         MVC   UXNXTMJD,NEXTSLOT   MOVE BACK TO PARMLIST       @SB003A3
         DROP  R2,R7                                           @SB003A3
         B     UX19DUN1            DONE WITH USERID CODE       @SB003A0
*------------------------------------------------------------  @SB003A0
*     GET ACF2 ACUCB ERROR RETURN                              @SB003A0
*     FAIL TRANSACTION AND ASK FOR RESUBMITTAL LATER           @SB003A0
*------------------------------------------------------------  @SB003A0
X19NOUCB DS    0H                 ACF2 UNAVAILABLE             @SB003A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXUSRMSG,MSG6ADDR  MOVE ADDRESS OF ERR MSG      @SB003A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB003A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB003A0
         DROP  R2                                              @SB003A0
MSG6ADDR DC    AL4(UX19MSG6)                                   @SB003A0
UX19MSG6 DC    AL1(MSG6END-MSG6STRT-1)                         @SB003A0
MSG6STRT DC    C'BDTR1906 ACF2 UNAVAILABLE AT PRESENT, '       @SB003A0
         DC    C'RESUBMIT LATER.'                              @SB003A0
MSG6END  EQU   *                                               @SB003A0
*------------------------------------------------------------  @SB003A0
*        CONSOLE SUBMITTED TRANSACTION                         @SB003A0
*        FAIL THE TRANSACTION WITH A MESSAGE                   @SB003A0
*------------------------------------------------------------  @SB003A0
UX19CONS DS    0H                  CONSOLE SUBMITTAL           @SB003A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB003A0
         USING UXPARMIN,R2                                     @SB003A0
         MVC   UXUSRMSG,MSG7ADDR  MOVE ADDRESS OF ERR MSG      @SB003A0
         LA    R15,RCFAILTR                                    @SB003A0
         B     UXRETURN           DONE WITH USERID CODE        @SB003A0
         DROP  R2                                              @SB003A0
MSG7ADDR DC    AL4(UX19MSG7)                                   @SB003A0
UX19MSG7 DC    AL1(MSG7END-MSG7STRT-1)                         @SB003A0
MSG7STRT DC    C'BDTR1907 TRANSACTIONS CANNOT BE SUBMITTED '   @SB003A0
         DC    C'FROM MCS OR JES3 CONSOLES.'                   @SB003A0
MSG7END  EQU   *                                               @SB003A0
         EJECT                                                 @SB003A0
*------------------------------------------------------------- @SB003A0
*    USERID PROCESSING DONE WITH NO ERRORS, CONTINUE           @SB003A0
*------------------------------------------------------------- @SB003A0
UX19DUN1 DS    0H                                              @SB003A0
*------------------------------------------------------------- @SB004A0
*    RETRIEVE 52 BYTE ACCOUNTING FIELD FOR OS ACT              @SB004A0
*                                                              @SB004A0
*    USER IS NOT ALLOWED TO SPECIFY THE ACCT PARAMETER.        @SB004A0
*    A FLAG, BSIDACCT, IS SET WHEN THE ACCOUNTING FIELD IS     @SB004A0
*    RETRIEVED.  IF THE ACCT TEXT UNIT IS FOUND BEFORE THE     @SB004A0
*    FLAG IS SET, THEN THE USER ATTEMPTED TO SPECIFY THE       @SB004A0
*    ACCT PARAMETER, SO THE TRANSACTION IS FLUSHED WITH A      @SB004A0
*    MSG.                                                      @SB004A0
*------------------------------------------------------------- @SB004A0
         TM    BSIDUFLG,BSIDACCT    ACCTNO ALREADY RETRIEVED?  @SB004A0
         BO    UX19DUN2             DONE WITH ACCT CODE        @SB004A0
         LA    R0,BTUACCT         SEARRCH FOR ACCOUNT FIELD    @SB004A0
         ICM   R0,B'1000',=X'F0'  SET GENERIC KEYWORD          @SB004A0
         LA    R1,BSIDDATA        START AT BEGINNING OF MJD    @SB004A1
         BAL   R6,FINDTU          DID USER SUPPLY USERID       @SB004A0
         B     UX19ACRT           GO RETRIEVE ACCTNO           @SB004A0
         B     UX19ACUS           USER SPECIFIED ACCTNO. ERROR @SB004A0
*--------------------------------------------------------------@SB004A0
*     RETRIEVE ACCOUNT NUMBER AND BUILD TEXT UNIT,SET BSIDACCT @SB004A0
*--------------------------------------------------------------@SB004A0
UX19ACRT DS    0H                                              @SB004A0
         USING CVT,R1                                          @SB004A0
         L     R1,CVTPTR          POINT TO THE CVT             @SB004A0
         L     R1,CVTTCBP         POINTER TO TCB               @SB004A0
         DROP  R1                                              @SB004A0
         L     R1,0(R1)           USERS TCB                    @SB004A0
         USING TCB,R1                                          @SB004A0
         L     R1,TCBJSCB         JSCB                         @SB004A0
         USING IEZJSCB,R1                                      @SB004A0
         L     R1,JSCBJCT         JCT-X'10'                    @SB004A0
         LA    R1,X'10'(R1)       JCT                          @SB004A0
         USING INJMJCT,R1                                      @SB004A0
         ICM   R1,B'0111',JCTACTAD ACT-X'10'                   @SB004A0
         LA    R1,X'10'(R1)      STEP PAST PREFIX              @SB004A0
         USING IEFAACTB,R1                                     @SB004A0
         L     R7,NEXTSLOT        POINT TO NEXT AVAILABLE BYTE @SB004A0
         USING DATUNIT,R7                                      @SB004A0
         LA    R0,DATUPAR+52      END OF NEW TEXT UNIT         @SB004A0
         C     R0,BSIDENDA        AT END OF BSID?              @SB004A0
         BH    UXBSEXCD           YES, BSID EXECEEDED          @SB004A0
         MVC   DATUNUM,=AL2(1)    ONLY 1 PARAMETER IN TEXT UNIT@SB004A0
         MVC   DATULNG,=H'52'     PARAMETER 8 BYTES IN LENGTH  @SB004A0
         MVI   DATUFLG,0          ZERO THE FLAG                @SB004A0
         LA    R5,BTUACCT         SET TEXT UNIT KEY            @SB004A0
         STCM  R5,B'0011',DATUKEY                              @SB004A0
         OI    DATUFLG,DATUNDA    SET NOT DYNALLOC TU          @SB004A0
         MVC   DATUPAR(52),ACTACCNT+1 MOVE IN ACCT FIELD       @SB004A0
         DROP  R1                                              @SB004A0
         LA    R5,DATUPAR+52       PREPARE TO UPDATE           @SB004A0
         ST    R5,NEXTSLOT         NEXT AVAILABLE TEXTUNIT     @SB004A0
         L     R2,UXPARMAD         ADDRESS OF PARM PASSED      @SB004A0
         USING UXPARMIN,R2                                     @SB004A0
         MVC   UXNXTMJD,NEXTSLOT   MOVE BACK TO PARMLIST       @SB004A0
         DROP  R2,R7                                           @SB004A0
         OI    BSIDUFLG,BSIDACCT  SET ACCTNO RETRIEVED         @SB004A0
         B     UX19DUN2           ACCTNO CODE DONE             @SB004A0
*--------------------------------------------------------------@SB004A0
*     USER SPECIFIED ACCT PARM, FAIL WITH AN ERROR             @SB004A0
*--------------------------------------------------------------@SB004A0
UX19ACUS DS    0H                                              @SB004A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB004A0
         USING UXPARMIN,R2                                     @SB004A0
         MVC   UXUSRMSG,MSG5ADDR MOVE ERR MSG                  @SB004A0
         LA    R15,RCFAILTR       INDICATE FAIL THE TRANSACTION@SB004A0
         B     UXRETURN           ERROR , RETURN TO CALLER     @SB004A0
         DROP  R2                                              @SB004A0
MSG5ADDR DC    AL4(UX19MSG5)                                   @SB004A0
UX19MSG5 DC    AL1(MSG5END-MSG5STRT-1)                         @SB004A0
MSG5STRT DC    C'BDTR1905 THE ACCT PARAMETER CANNOT BE '       @SB004A0
         DC    C'SPECIFIED BY THE USER.'                       @SB004A0
MSG5END  EQU   *                                               @SB004A0
UX19DUN2 DS    0H                                              @SB004A0
*--------------------------------------------------------------@SB011A0
*     CHANGE LOC TO REMOTE NODE NAME IF NOT EQUAL TO HOME NODE @SB011A0
*--------------------------------------------------------------@SB011A0
         TM    BSIDUFLG,BSIDLOC    ALREADY BEEN THROUGH ONCE   @SB011A0
         BO    UX19BDAD            YES,RUNNING IN BDT AD SPACE @SB011A0
         OI    BSIDUFLG,BSIDLOC    SET ALREADY BEEN THROUGH    @SB011A0
         B     UX19DUN3                                        @SB011A0
UX19BDAD DS    0H                                              @SB011A0
         L     R5,FLCCVT          POINT TO CVT C               @SB011A0
         USING CVT,R5                                          @SB011A0
         LA    R7,BSIDDATA         POINT TO START OF TEXT      @SB011A0
         USING MJDSTART,R7                                     @SB011A0
         CLC   CVTSNAME+2(6),MJDFRLOC IS LOC HERE?             @SB011A0
         BE    UX19NOTR           YES,NOT REMOTE               @SB011A0
         MVI   MJDFRLOC+2,C'R'    SET AS REMOTE                @SB011A0
UX19NOTR DS    0H                                              @SB011A0
         CLC   CVTSNAME+2(6),MJDTOLOC IS LOC HERE?             @SB011A0
         BE    UX19DUN3            YES, ALL DUN                @SB011A0
         MVI   MJDTOLOC+2,C'R'     SET AS REMOTE               @SB011A0
         DROP  R5,R7                                           @SB011A0
UX19DUN3 DS    0H                                              @SB011A0
         EJECT ,                                               @SB009A0
*--------------------------------------------------------------@SB009A0
*        DISALLOW GDG USEAGE                                   @SB009A0
*        IF MEMBER NAME STARTS  WITH + - 0, DISALLOW WITH MSG  @SB009A0
*--------------------------------------------------------------@SB009A0
UX19GDG  DS    0H                                              @SB009A0
         LA    R1,BSIDDATA             START BEGINNING OF MJD  @SB009A0
UX19FGDG LA    R0,DALMEMBR             MEMBER NAME TEXT UNIT   @SB009A0
         BAL   R6,FINDTU               GET MEMBER TEXT UNIT    @SB009A0
         B     UX19DUN5                NOT FOUND , ALL DONE    @SB009A0
         USING DATUNIT,R1                                      @SB009A0
         CLI   DATUPAR,C'+'            GDG SPECIFIED?          @SB009A0
         BE    UX19GDGE                YES, GDG ERROR          @SB009A0
         CLI   DATUPAR,C'-'            GDG SPECIFIED?          @SB009A0
         BE    UX19GDGE                YES, GDG ERROR          @SB009A0
         CLI   DATUPAR,C'0'            GDG SPECIFIED?          @SB009A0
         BE    UX19GDGE                YES, GDG ERROR          @SB009A0
         B     UX19FGDG                GO FIND 2ND MBR(R1 SET) @SB009A0
         DROP  R1                                              @SB009A0
UX19GDGE DS    0H                                              @SB009A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB009A0
         USING UXPARMIN,R2                                     @SB009A0
         MVC   UXUSRMSG,MSGAADDR MOVE ERR MSG                  @SB009A0
         LA    R15,RCFAILTR       INDICATE FAIL THE TRANSACTION@SB009A0
         B     UXRETURN           ERROR , RETURN TO CALLER     @SB009A0
         DROP  R2                                              @SB009A0
MSGAADDR DC    AL4(UX19MSGA)                                   @SB009A0
UX19MSGA DC    AL1(MSGAEND-MSGASTRT-1)                         @SB009A0
MSGASTRT DC    C'BDTR1910 GDG''S ARE NOT SUPPORTED. '          @SB009A0
         DC    C'TRANSACTION FAILED.'                          @SB009A0
MSGAEND  EQU   *                                               @SB009A0
UX19DUN5 DS    0H                                              @SB009A0
         EJECT ,                                               @SB009A0
         LA    R15,0                  OKAY                     @SB009A0
*--------------------------------------------------------------@SB009A0
*        TO RETURN TO CALLER OF MODULE (I.E., NORMAL EXIT AND  @SB001A0
*        EXIT AFTER ESTAE RETRY ROUTINE).                      @SB001A0
*                                                              @SB001A0
*        REGISTER 4 AND UXESTFLG (SUCCESSFUL ESTAE FLAG) ARE   @SB001A0
* TESTED TO DETERMINE WHETHER TO CANCEL ESTAE AND INVOKE FREEMA@SB001A0
*                                                              @SB001A0
*        1.  IF REG 4 IS ZERO, GETMAIN WAS UNSUCCESSFUL AND THE@SB001A0
*            IS NO ESTAE TO CANCEL.                            @SB001A0
*        2.  IF REG 4 IS NOT ZERO, TEST FOR ESTAE.             @SB001A0
*            A.  IF UXESTSUC IS OFF, THERE IS NO ESTAE TO CANCE@SB001A0
*                DO FREEMAIN AND RETURN.                       @SB001A0
*            B.  IF UXESTSUC IS ON, CANCEL ESTAE, FREEMAIN AND @SB001A0
*                RETURN.                                       @SB001A0
*--------------------------------------------------------------@SB001A0
UXRETURN DS    0H                                              @SB001A0
         LR    R3,R15              TEMPORARILY SAVE RETURN CODE@SB001A0
         LTR   R4,R4               WAS GETMAIN SUCCESSFUL?     @SB001A0
         BZ    UXRTONLY            NO, THEN NO ESTAE TO CANCEL,@SB001A0
         TM    UXESTFLG,UXESTSUC   WAS ESTAE ESTABLISHED?      @SB001A0
         BNO   UXRTFREE      NO, SKIP ESTAE CANCEL, DO FREEMAIN@SB001A0
         ESTAE 0,TOKEN=UXTOKEN     CANCEL ESTAE                @SB001A0
UXRTFREE DS    0H                                              @SB001A0
         L     R13,UXSAVE+4        POINT TO OLD SAVE AREA      @SB001A0
         FREEMAIN  RC,LV=UXMAPLEN,A=(R4)  FREE THE STORAGE     @SB001A0
UXRTONLY DS    0H                                              @SB001A0
         LR    R15,R3              RESTORE RETURN CODE         @SB001A0
         L     R14,12(R13)         RESTORE RETURN ADDRESS      @SB001A0
         LM    R0,R12,20(R13)      RESTORE REMAINING REGS      @SB001A0
         BR    R14                 RETURN                      @SB001A0
*--------------------------------------------------------------@SB001A0
*        ESTAE RECOVERY EXIT                                   @SB001A0
*                                                              @SB001A0
*        THIS ROUTINE SAVES ABEND INFORMATION IN THE SDWA AND S@SB001A0
*        THE RETURN CODE FOR RTM INDICATING RETRY.  THIS ESTAE @SB001A0
*        RECOVERY ROUTINE IS USED IN CONJUNCTION WITH THE RETRY@SB001A0
*        ROUTINE, UXRETRY.  IF THIS RECOVERY ROUTINE IS CHANGED@SB001A0
*        CARE MUST BE TAKEN TO ENSURE THAT ASSUMPTIONS MADE BY @SB001A0
*        UXRETRY (E.G., REGISTER CONVENTIONS) ARE STILL VALID. @SB001A0
*                                                              @SB001A0
*        THIS ROUTINE DOES NOT RETRY WHEN IT IS ENTERED FOR THE@SB001A0
*        SECOND TIME.  THIS PREVENTS A LOOP CAUSED BY A RECURSI@SB001A0
*        ERROR.                                                @SB001A0
*                                                              @SB001A0
*        REGISTER INTERFACE TO RECOVERY ROUTINE (UXESTAE) DEPEN@SB001A0
*        ON WHETHER OR NOT RTM OBTAINED SDWA.  NORMALLY, AN SDW@SB001A0
*        WILL BE OBTAINED.                                     @SB001A0
*                                                              @SB001A0
*        TWO REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLES@SB001A0
*        SDWA STATUS:                                          @SB001A0
*               REGISTER 14 - RETURN ADDRESS                   @SB001A0
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RECO@SB001A0
*                             ROUTINE                          @SB001A0
*                                                              @SB001A0
*        IF SDWA WAS OBTAINED, REGISTERS UPON ENTRY TO UXESTAE @SB001A0
*               REGISTER 0  - A CODE INDICATING THE TYPE OF I/O@SB001A0
*                             PROCESSING PERFORMED             @SB001A0
*                             0  - ACTIVE I/O HAS BEEN QUIESCED@SB001A0
*                                  IS RESTORABLE               @SB001A0
*                             4  - ACTIVE I/O HAS BEEN HALTED A@SB001A0
*                                  NOT RESTORABLE              @SB001A0
*                             8  - NO ACTIVE I/O AT ABEND TIME @SB001A0
*                             16 - NO I/O PROCESSING WAS PERFOR@SB001A0
*               REGISTER 1  - ADDRESS OF SDWA                  @SB001A0
*               REGISTER 13 - SAVE AREA ADDRESS (72 BYTES)     @SB001A0
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB001A0
*               UNPREDICTABLE.                                 @SB001A0
*                                                              @SB001A0
*        IF SDWA WAS NOT OBTAINED, REGISTERS UPON ENTRY ARE:   @SB001A0
*               REGISTER 0  - A DECIMAL 12 INDICATING NO SDWA  @SB001A0
*               REGISTER 1  - ABEND COMPLETION CODE            @SB001A0
*               REGISTER 2  - ADDRESS OF USER-SUPPLIED PARAMETE@SB001A0
*               OTHER REGISTERS, WITH THE EXCEPTION OF 14 & 15 @SB001A0
*               UNPREDICTABLE.                                 @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
UXESTAE  DS    0H                                              @SB001A0
         PUSH  USING               SAVE CURRENT ADDRESSABILITY @SB001A0
         DROP  ,                                               @SB001A0
         USING *,R15               SET BASE FOR ESTAE ROUTINE  @SB001A0
         CH    R0,=H'12'           ESTAE WORK AREA,SDWA AVAILAB@SB001A0
         BNE   UXSDWA              YES, SDWA AVAILABLE, TAKE BR@SB001A0
         SPACE 1                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        IF SDWA NOT PROVIDED, DETERMINE WHETHER TO RETRY.     @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB001A0
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB001A0
         BNO   UXRETRY1            YES, SET UP RETRY           @SB001A0
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB001A0
         LA    R15,RCNORTRY        DO NOT RETRY                @SB001A0
         BR    R14                 RETURN TO RTM               @SB001A0
         SPACE 1                                               @SB001A0
UXRETRY1 DS    0H                                              @SB001A0
         OI    UXESTFLG,UXESTREP   SET FLAG MARKING FIRST USE  @SB001A0
         LA    R0,UXRETRY          ADDRESS OF RETRY ROUTINE    @SB001A0
         LA    R15,RCRETRY         SET RETRY RETURN CODE FOR RT@SB001A0
         BR    R14                 RETURN TO RTM               @SB001A0
         SPACE 1                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        IF SDWA PROVIDED, UPDATE SDWA AND DETERMINE WHETHER TO@SB001A0
*        RETRY.                                                @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
UXSDWA   DS    0H                                              @SB001A0
         STM   R14,R12,12(R13)     SAVE REGS WHERE INDICATED BY@SB001A0
         LR    R4,R1               ESTABLISH ADDRESSABILITY . .@SB001A0
         USING SDWA,R4                  . . . TO SDWA          @SB001A0
         L     R2,SDWAPARM         LOAD ADDRESS OF PARM LIST   @SB001A0
         USING UXPARMAP,R2         ESTABLISH BASE FOR PARAM LIS@SB001A0
         L     R10,UXBASE          CHANGE BASE REGISTER . . .  @SB001A0
         LA    R8,2048(R10)                                    @SB001A0
         LA    R8,2048(R8)                                     @SB001A0
         USING BDTUX19,R10,R8           . . . FOR THIS ROUTINE @SB001A0
         DROP  R15                                             @SB001A0
         SPACE 1                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        UPDATE THE SDWA                                       @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
         MVC   SDWAMODN(8),UXMODN  SET NAME OF LOAD MODULE     @SB001A0
         MVC   SDWACSCT(8),MDNUX19 SET NAME OF CSECT           @SB001A0
         MVC   SDWAREXN(8),MDNUX19 SET CSECT WITH RECOVERY ROUT@SB001A0
         L     R3,SDWAXPAD         LOAD SDWA EXTENSION POINTERS@SB001A0
         USING SDWAPTRS,R3         ESTABLISH ADDRESSABILITY    @SB001A0
         L     R3,SDWASRVP         LOAD RECORDABLE EXTENSION AD@SB001A0
         USING SDWARC1,R3          ESTABLISH ADDRESSABILITY    @SB001A0
         MVC   SDWACID(5),UXACID   SET COMPONENT ID            @SB001A0
         MVC   SDWASC(23),UXASC    SET NAME OF SUBCOMPONENT    @SB001A0
         MVC   SDWAMDAT(8),MDDUX19 SET DATE OF ASSEMBLY        @SB001A0
         MVC   SDWAMVRS(8),MDRUX19 SET PTF OR PRODUCT NUMBER   @SB001A0
         MVC   SDWARRL(8),UXRRL    IDENTIFY ROUTINE UPDATING SD@SB001A0
         TM    UXESTFLG,UXESTREP   FIRST TIME THRU ESTAE?      @SB001A0
         BNO   UXRETRY2            YES, SET UP RETRY           @SB001A0
         FREEMAIN RC,LV=UXMAPLEN,A=(R2)  NO, FREE STORAGE      @SB001A0
         SETRP RC=0,WKAREA=(R4)    DO NOT RETRY                @SB001A0
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB001A0
         BR    R14                 RETURN TO RTM               @SB001A0
         SPACE 1                                               @SB001A0
UXRETRY2 DS    0H                                              @SB001A0
         OI    UXESTFLG,UXESTREP   MARK FIRST USE              @SB001A0
         SETRP RC=4,RETADDR=UXRETRY,FRESDWA=YES,               @SB001A0*
               WKAREA=(R4)                                     @SB001A0
         LM    R14,R12,12(R13)     RESTORE REGISTERS           @SB001A0
         BR    R14                 RETURN TO RTM               @SB001A0
         SPACE 1                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        POP THE USING STACK HERE TO RE-ESTABLISH USINGS IN EFF@SB001A0
*        PRIOR TO ESTAE RECOVERY ROUTINE.                      @SB001A0
*--------------------------------------------------------------@SB001A0
         SPACE 1                                               @SB001A0
         POP   USING                                           @SB001A0
         EJECT                                                 @SB001A0
         SPACE 1                                               @SB001A0
*--------------------------------------------------------------@SB001A0
*        RETRY ROUTINE FOR ESTAE                               @SB001A0
*                                                              @SB001A0
*        THIS ROUTINE IS USED IN CONJUNCTION WITH THE ESTAE REC@SB001A0
*        ROUTINE, UXESTAE.  THE RETRY ROUTINE 'FAILS' THE BDT  @SB001A0
*        REQUEST BY SETTING THE RETURN CODE TO 4 AND RETURNING @SB001A0
*        THE CALLING MODULE.  BEFORE BRANCHING TO THE COMMON EX@SB001A0
*        FOR THE MODULE, THE RETRY ROUTINE REESTABLISHES ADDRES@SB001A0
*        BILITY TO THE TVT.  (ADDRESSABILITY TO THE USER PARAME@SB001A0
*        LIST WAS REESTABLISHED BY THE 'POP USING' AT THE END O@SB001A0
*        THE ESTAE RECOVERY ROUTINE.  PARAMETER LIST IS BASED O@SB001A0
*        REGISTER 4.)                                          @SB001A0
*                                                              @SB001A0
*        THE REGISTER INTERFACE TO THE RETRY ROUTINE (UXRETRY) @SB001A0
*        DEPENDS ON THE CODING OF THE 'SETRP' MACRO IN THE RECO@SB001A0
*        ROUTINE (UXESTAE) AND ON WHETHER OR NOT RTM OBTAINED A@SB001A0
*        SDWA.  NORMALLY, AN SDWA WILL HAVE BEEN OBTAINED BUT T@SB001A0
*        THE REGISTER INTERFACE WHEN THERE WAS AN SDWA IS SET U@SB001A0
*        AS DESCRIBED BELOW ONLY BECAUSE THE 'SETRP' SPECIFIED @SB001A0
*        FREE SDWA & NO REGISTER UPDATE.  IF THESE PARAMETERS A@SB001A0
*        CHANGED ON THE 'SETRP', THE RETRY ROUTINE MUST ALSO BE@SB001A0
*        CHANGED.                                              @SB001A0
*                                                              @SB001A0
*        THE VALUE IN REGISTER 0 DEPENDS UPON WHETHER OR NOT RT@SB001A0
*        OBTAINED AN SDWA:                                     @SB001A0
*               REGISTER 0  - DECIMAL 20 IF SDWA WAS OBTAINED  @SB001A0
*                           - DECIMAL 12 IF SDWA WAS NOT OBTAIN@SB001A0
*                                                              @SB001A0
*        FOUR REGISTERS ARE ALWAYS SET THE SAME BY RTM REGARDLE@SB001A0
*        SDWA STATUS:                                          @SB001A0
*               REGISTER 1  - ADDRESS OF USER PARAMETER LIST   @SB001A0
*                             ESTABLISHED USING ESTAE MACRO    @SB001A0
*               REGISTER 2  - POINTER TO PIRL IF I/O WAS QUIESC@SB001A0
*                             IS RESTORABLE; OTHERWISE ZERO    @SB001A0
*               REGISTER 14 - ADDRESS OF SUPERVISOR-ASSIGNED EX@SB001A0
*                             LINKAGE (SVC3)                   @SB001A0
*               REGISTER 15 - ENTRY POINT ADDRESS OF ESTAE RETR@SB001A0
*                             ROUTINE                          @SB001A0
*               REGISTERS BESIDES 0,1,2,14 & 15 ARE UNPREDICTAB@SB001A0
*--------------------------------------------------------------@SB001A0
UXRETRY  DS    0H                                              @SB001A0
         LR    R4,R1               SET BASE REGISTER FOR PARAME@SB001A0
         L     R10,UXBASE          RESTORE MODULE BASE FOR EXIT@SB001A0
         L     R8,UXBASE2          SECOND BASE                 @SB001A0
         L     R12,UXTVTADD        RESTORE BASE FOR TVT        @SB001A0
         LA    R15,RCFAILTR        SET RETURN CODE FAILING BDT @SB001A0
         B     UXRETURN            RETURN TO CALLER            @SB001A0
*--------------------------------------------------------------@SB001A0
*        MODEL FOR ESTAE MACRO (LIST FORM). USED TO INITIALIZE @SB001A0
*        ACTUAL LIST REFERENCED IN EXECUTE FORM OF ESTAE MACRO.@SB001A0
*        DO NOT STORE INTO THIS AREA.                          @SB001A0
*--------------------------------------------------------------@SB001A0
ESTMODEL ESTAE MF=L                'MODEL' FOR ESTAE PARM LIST @SB001A0
*------------------------------------------------------------- @SB001A0
*   ROUTINES COPIED FROM BDTLP AND MODIFIED                    @SB001A0
*--------------------------------------------------------------@SB001A0
*   FINDTU - FIND A TEXT UNIT IN THE BSID                      @SB001A0
*   FOUND -  RETURN +4, R1 -> DATUNIT                          @SB001A0
* NOTFOUND-  RETURN +0                                         @SB001A0
*--------------------------------------------------------------@SB001A0
FINDTU   DS    0H                                              @SB001A0
         PUSH  USING               SAVE CURRENT USING STATUS   @SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        SET UP TO PERFORM A SCAN OF MJD TEXT UNITS           *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
         ST    R6,FINDTUSR         SAVE RETURN ADDRESS         @SB001A0
         LR    R6,R0               PUT TYPE INTO R6            @SB001A0
         SRL   R6,24               PUT TYPE IN LOW BYTE        @SB001A0
*        LA    R1,BSIDDATA         POINT TO START OF TEXT      @SB001A0
         USING MJDSTART,R1                                     @SB001A0
         CLC   MJDSTART(3),=C'MJD' STARTING AT BEGINNING OF MJD@SB001A1
         BNE   FINDT200            NO, SKIP PAST THIS TEXT UNIT@SB001A1
         AH    R1,MJDFXDLN         UNITS IN THE MJD            @SB001A0
         DROP  R1                                              @SB001A0
         SLR   R15,R15             CLEAR R15 TO REC THE TEXT   @SB001A0
*                                  UNIT PARAMETER COUNT        @SB001A0
         SLR   R14,R14             CLEAR R14 TO REC THE TEXT   @SB001A0
*                                  UNIT PARAMETER LENGTH       @SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        TEST TEXT UNIT FOR KEY AND TYPE BEING SOUGHT         *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
FINDT100 DS    0H                                              @SB001A0
         C     R1,NEXTSLOT         END OF EXISTING TEXT UNITS? @SB001A0
         BNL   FINDT600            YES-                        @SB001A0
         USING DATUNIT,R1         NO- ESTABLISH ADDRESSABILITY @SB001A0
         CLM   R0,B'0011',DATUKEY DOES THIS TEXT UNIT'S KEY    @SB001A0
*                                  MATCH THAT BEING SOUGHT?    @SB001A0
         BE    FINDT500            YES, BRANCH                 @SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        NO MATCH ON THIS TEXT UNIT                           *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
FINDT200 DS    0H                                              @SB001A0
         CLC   DATUNUM,=H'0'      DOES THIS TEXT UNIT HAVE ANY @SB001A0
*                                  PARAMETER(S)?               @SB001A0
         BNE   FINDT300            YES-                        @SB001A0
         LA    R1,DATUENT         NO- PNT TO THE NEXT TEXT UNIT@SB001A0
         B     FINDT100            CONTINUE SEARCHING-         @SB001A0
         SPACE 1                                               @SB001A0
FINDT300 DS    0H                                              @SB001A0
         ICM   R15,B'0011',DATUNUM OBT THE NUM OF PARAMS ENT-  @SB001A0
*                            RIES CONTAINED IN THIS TEXT UNIT  @SB001A0
         PUSH  USING         ESTABLISH ADDRESSABILITY TO THEM  @SB001A0
         LA    R1,DATUENT    POINT TO THE NEXT TEXT UNIT       @SB001A0
         USING DATUENT,R1    ESTABLISH ADDRESSABILITY TO IT    @SB001A0
         SPACE 1                                               @SB001A0
FINDT400 DS    0H                                              @SB001A0
         ICM   R14,B'0011',DATULNG OBTAIN LENGTH OF THIS TEXT  @SB001A0
*                                  UNIT PARAMETER              @SB001A0
         LA    R1,DATUPAR(R14)    POINT TO NEXT TEXT UNIT      @SB001A0
*                                 PARAMETER ENTRY              @SB001A0
         BCT   R15,FINDT400        ANY MORE PARAM ENTRIES? YES-@SB001A0
         B     FINDT100            NO-                         @SB001A0
         POP   USING              RESTORE USING STATUS         @SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        TEST TEXT UNIT FOR BEING EITHER A BDT TEXT UNIT OR   *@SB001A0
*        A DYNAMIC ALLOCATION TEXT UNIT                       *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
FINDT500 DS    0H                                              @SB001A0
         SPACE 1                                               @SB001A0
*        USING TUDENTRY,R7        ESTABLISH ADDRESSABILITY     @SB001A0
*        TM    DATUFLG,DATUNDA    DYNAMIC ALLOCATION TEXT UNIT @SB001A0
*                                 FOUND ?                      @SB001A0
*        BZ    FINDT525           YES  -                       @SB001A0
         SPACE 1                                               @SB001A0
*        TM    TUDFLAG1,TUDDATU   NO - IS THE KEYWORD TO BEING @SB001A0
*                                 SOUGHT A DATU ?              @SB001A0
*        BO    FINDT200           YES -                        @SB001A0
         B     FINDT550           NO  - TEXT UNIT FOUND        @SB001A0
         SPACE 1                                               @SB001A0
FINDT525 DS    0H                                              @SB001A0
*        TM    TUDFLAG1,TUDDATU   IS THE KEYWORD BEING SOUGHT  @SB001A0
*                                 A DATU  ?                    @SB001A0
*        BZ    FINDT200           NO -                         @SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        TEXT UNIT OF SPECIFIED KEY AND TYPE FOUND            *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
FINDT550 DS    0H                                              @SB001A0
         L     R6,FINDTUSR      GET RETURN ADDRESS             @SB001A0
         B     4(R6)            RETURN TO CALLER'S NORMAL EXIT-@SB001A0
         SPACE 1                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        TEXT UNIT OF SPECIFIED KEY AND TYPE NOT FOUND        *@SB001A0
*-------------------------------------------------------------*@SB001A0
         SPACE 1                                               @SB001A0
FINDT600 DS    0H                                              @SB001A0
         L     R6,FINDTUSR      GET RETURN ADDRESS             @SB001A0
         BR    R6          RETURN TO CALLER'S END-OF-DATA EXIT-@SB001A0
         SPACE 1                                               @SB001A0
         POP   USING       RESTORE USING STATUS                @SB001A0
         EJECT                                                 @SB001A0
*------------------------------------------------------------- @SB001A0
*     ERROR CONDTION  BSID LENGTH EXCEEDED WHEN ATTEMPTING     @SB001A0
*     TO ADD TEXT UNIT                                         @SB001A0
*------------------------------------------------------------- @SB001A0
UXBSEXCD DS    0H                                              @SB001A0
         L     R2,UXPARMAD        ADDRESS OF PARMLIST PASSED   @SB001A0
         USING UXPARMIN,R2                                     @SB001A0
         MVC   UXUSRMSG,MSG8ADDR  ADDRESS OF ERR MSG           @SB001A0
         LA    R15,RCFAILTR       FAIL THE TRANSACTION         @SB001A0
         B     UXRETURN           ERROR, FAIL TRANSACTION      @SB001A0
         DROP  R2                                              @SB001A0
MSG8ADDR DC    AL4(UX19MSG8)                                   @SB001A0
UX19MSG8 DC    AL1(MSG8END-MSG8STRT-1)                         @SB001A0
MSG8STRT DC    C'BDTR1908 TRANSACTION TEXT EXCEEDS MAXIMUM '   @SB001A0
         DC    C'ALLOWED.  TRANSACTION FAILED.'                @SB001A0
MSG8END  EQU   *                                               @SB001A0
*-------------------------------------------------------------*@SB001A0
*        CONSTANTS,LITERAL POOL, PATCH AREA                   *@SB001A0
*-------------------------------------------------------------*@SB001A0
UXGSDID  DC    CL4'GSD '            IDENTIFICATION FOR GSD     @SB001A0
UXWKCONS DC    CL4'WORK'            IDENTIFICATION FOR WORK ARE@SB001A0
UXMODN   DC    CL8'BDTUX19'         LOAD MODULE NAME           @SB001A0
UXRRL    DC    CL8'UXESTAE'         RECOVERY ROUTINE UPDATING S@SB001A0
UXACID   DC    CL5'30201'           COMPONENT ID               @SB001A0
UXASC    DC    CL23'BDT - USER EXIT BDTUX19'   DESCRIPTION     @SB001A0
         LTORG                                                 @SB001A0
         END   ,                                               @SB001A0
