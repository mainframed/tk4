         TITLE 'LOOK --  DCMS STORAGE ALTER/DISPLAY'
US1NG    OPSYN USING
USING    OPSYN
DR0P     OPSYN DROP
DROP     OPSYN
         MACRO
&N       HEX   &TO,&FROM
&N       UNPK  TEMP1(9),&FROM.(5,R6)
         MVC   &TO.(8,R4),TEMP1
         TR    &TO.(8,R4),HEXTAB
         MEND
LOOK     CSECT
*  INITIALIZE THE PROGRAM
         SAVE  (14,12)
         LR    R11,R15                 PERMANENT BASE
         USING LOOK,R11
         LA    R9,2048(R11)            GET A SECOND BASE REG
         LA    R9,2048(R9)
         USING LOOK+4096,R9
         GETMAIN R,LV=WORKLEN,SP=1     BUILD THE WORK AREA
         LR    R10,R1
         LR    R2,R10                  WORK AREA START
         LA    R3,WORKLEN              LENGTH TO CLEAR
         SR    R5,R5                   ZERO PAD BYTE, LENGTH 2
         MVCL  R2,R4                   CLEAR THE WORK AREA
         USING WORKAREA,R10
         ST    R13,SAVE+4              CHAIN THE SAVE AREAS
         LA    R15,SAVE
         ST    R15,8(R13)
         LR    R13,R15                 NEW SAVE AREA
         SPIE  SPIEEXIT,(4,5),MF=(E,SPIE) CATCH ADDR,PROT EXCPTNS
         LA    R2,IOAREA               START OF IO BUFFER
         LA    R3,IOLEN                SIZE OF IO BUFFER
         LA    R5,C' '                 PAD BYTE
         SLL   R5,24                   MOVE TO HIGH ORDER BYTE
         MVCL  R2,R4                   CLEAR THE IO BUFFER
         EJECT
         INUSE
***********************************************************************
*
*   INPUT SCAN
*
***********************************************************************
         SPACE 2
ISCAN    MVC   INPSAVE,INPLINE         SAVE LAST USER INPUT
ISCAN2   LA    R3,INPLINE              POINT TO START OF INPUT
         LA    R1,SCANTAB              COMMAND TABLE
         NI    FLAGS,255-INDFLAG       TURN OFF INDIRECT FLAG
         MVC   MSGLINE,TITLE           CLEAR OUT ERROR MSG
SCANLOOP L     R2,0(R1)                GET TABLE ENTRY
         CLM   R2,8,ENDTAB             SEE IF END OF TABLE
         BE    NUMSCAN                 IF YES, ASSUME NUMERIC INPUT
         CLM   R2,8,INPLINE            SEE IF COMMAND FOUND
         BER   R2                      IF YES, GO TO COMM ROUINE
         LA    R1,4(R1)                NEXT ENTRY ADDRESS
         B     SCANLOOP
SCANTAB  DS    0F                      COMMAND TABLE
         DC    C' ',AL3(FORMAT)
         DC    C'I',AL3(INDIR)
         DC    C'>',AL3(FWD)
         DC    C'<',AL3(BACK)
         DC    C'=',AL3(DEFINE)
         DC    C',',AL3(NUMSCAN)
         DC    C'M',AL3(MODE)
         DC    C'L',AL3(LINK)
         DC    C'O',AL3(OVERLAY)
ENDTAB   DC    X'FF'                   END OF TABLE INDICATOR
         SPACE 2
***********************************************************************
*
*     FORMAT THE OUTPUT SCREEN
*
***********************************************************************
         SPACE 2
FORMAT   LA    R3,INPLINE              CURSOR ADDRESS
         MVC   INPLINE,BLANKS          CLEAR INPUT LINE
FORMAT2  LH    R2,CURRENT              HISTORY TABLE POINTER
         L     R4,HISTORY(R2)          GET ADDRESS TO DISPLAY
         BAL   R14,ERASE               CLEAR THE SCREEN
         LA    R0,IOAREA               START OF SCREEN IMAGE
         SR    R3,R0                   CURSOR OFFSET
         SR    R5,R5                   CLEAR FOR IC
         IC    R5,HISTORY+4(R2)        GET DISPLAY FORMAT FROM HIST TAB
         B     *+4(R5)                 GO TO DISPLAY ROUTINE
         B     DUMP                    DUMP FORMAT IS STANDARD
         B     CBHIT                   CB MAP IN USE
         B     NOCB                    SUPPRESS CB USAGE
* NO CODE ABOVE 8 IN USE NOW
* TRY TO LOCATE A CONTROL BLOCK FORMAT
DUMP     L     R5,=V(CBMACS)           GET START OF INDEX
DUMPCB   CLI   0(R5),X'FF'             SEE IF END OF INDEX
         BE    NOCB                    IF YES, NO CB FOUND
         L     R2,4(R5)                GET CB IDENTIFIER OFFSET
         AR    R2,R4                   ADD DISPLAY ADDRESS
         LA    R0,4                    LENGTH TO FETCH
         LA    R1,DUMPDATA             TARGET ADDR
         BAL   R14,GETSTOR             FETCH THE DATA
         B     NOCB                    BR IF NO DATA FOUND
         B     NOCB                    BR IF PARTIAL DATA FOUND
         CLC   0(4,R5),DUMPDATA        SEE IF IDENTIFIER MATCH
         BE    CBHIT                   BR IF YES, CB WAS FOUND
         LA    R5,12(R5)               NEXT INDEX ENTRY
         B     DUMPCB                  AND CHECK FOR MORE CB'S
* IF NO CONTROL BLOCK TO BE SHOWN, JUST USE DUMP FORMAT
NOCB     LR    R2,R4                   COPY ADDR TO DISPLAY
         TM    MODEFLAG,ALIGN          QUADWORD ALIGNMENT WANTED
         BZ    DUMPNOA                 BR IF NOT
         N     R2,=XL4'FFFFFFF0'       ROUND DOWN FOR DISPLAY
DUMPNOA  TM    MODEFLAG,MIDDUMP        PUT ADDR INCENTER OF SCREEN?
         BZ    DUMPNOM                 BR IF NOT
         SH    R2,=H'128'              IF YES, BACK UP QUAD WORDS
* R2 NOW HAS FIRST ADDRESS TO DISPLAY
DUMPNOM  LA    R0,256                  LENGTH TO FETCH
         LA    R1,DUMPDATA             TARGET ADDRESS
         BAL   R14,GETSTOR             GO FETCH THE DATA
         NOP   0                       RETURN IF NO DATA FOUND
         NOP   0                       RETURN IF PARTIAL DATA FOUND
         LA    R6,DUMPDATA             POINT TO DATA TO DISPLAY
         LA    R4,OUT1                 FIRST OUTPUT LINE ADDR
         LA    R5,16                   NO. OF LINES TO DISPLAY
DUMPLOOP EQU   *                       FORMAT THE DISPLAY
         STCM  R2,07,TEMP2             ADDRESS TO SHOW
         UNPK  TEMP1(7),TEMP2(4)       CONVERT BINARY TO HEX
         MVC   0(6,R4),TEMP1
         TR    0(6,R4),HEXTAB
         HEX   10,0                    CONVERT FIRST WORD TO HEX
         HEX   20,4                    SECOND WORD TO HEX
         HEX   30,8                    THIRD WORD
         HEX   40,12                   FOURTH AND LAST WORD TO HEX
         MVI   50(R4),C'*'             FRAME THE EBCDIC DISPLAY
         MVI   67(R4),C'*'             FRAME THE OTHER SIDE
         MVC   51(16,R4),0(R6)         COPY THE EBCDIC DATA
         TR    51(16,R4),VALCHAR       DELETE INVALID CHARS
         LA    R2,16(R2)               NEXT LINE ADDR
         LA    R6,16(R6)               NEXT DATA AREA
         LA    R4,79(R4)               NEXT OUTPUT LINE
         BCT   R5,DUMPLOOP             GO DO NEXT LINE
         SPACE 2
*----------------------------------------------------------------------
*
* NOW BLANK OUT THE SCREEN WHERE THE PAGE DIDN'T EXIST
*
*----------------------------------------------------------------------
         SPACE 2
         LA    R4,OUT1                 POINT TO FIRST OUTPUT LINE
         SH    R2,=H'256'              FIRST ADDRESS DISPLAYED
         LA    R5,16                   NUMBER OF LINES DISPLAYED
BLLOOP   LA    R15,15(R2)              LAST ADDRESS ON CURRENT LINE
         CLM   R2,7,LOGOOD+1           IS BEGINNING OF LINE OK
         BNL   LOWOK                   BR IF OK
         CLM   R15,7,HIGOOD+1          IS HIGH END OK
         BNH   HIMIX                   BR IF LOW BAD, HI OK
* BOTH LOW AND HIGH MISSING -- BLANK ENTIRE LINE
         MVI   10(R4),C' '             START OF DATA
         MVC   11(57,R4),10(R4)        CLEAR THE LINE
         B     BLOOPEND                GO CHECK ANOTHER LINE
* BR HERE IF LOW END GOOD, HIGH MAY BE  MISSING
LOWOK    C     R15,HIGOOD              IS HIGH END OK
         BNH   BLOOPEND                BR IF YES, LINE IS GOOD
* FALL THRU IF LOW END OK, HIGH END BAD
         S     R15,HIGOOD              # OF BYTES TO BLANK
* THE FOLLOWING CODE TAKES CARE OF LOW STORAGE WRAP
         CH    R15,=H'15'              MORE THAN 15 BYTES TO CLEAR
         BH    BLOOPEND                IF YES, IT WRAPPED, NO CLEAR
         LR    R1,R15                  COPY # TO BLANK
         SRL   R1,2                    FIND # OF WORDS
         SLL   R1,1                    TWO BLANKS PER WORD
         AR    R1,R15                  ADD NUMBER OF HEX CHARS
         AR    R1,R15                  WHICH IS TWICE THE # OF BYTES
         LA    R6,48(R4)               POINT JUST AFTER HEX DATA
         SR    R6,R1                   FIRST BYTE TO CLEAR
         BCTR  R1,0                    REDUCE BY 1 FOR EXEXUTE
         EX    R1,CLEARMVC             CLEAR HEX SIDE
         LA    R6,67(R4)               POINT TO TRAILING *
         SR    R6,R15
         EX    R15,CLEARMVC            CLEAR EBCDIC AND *
         MVI   0(R6),C'*'              NOTE END OF EBCDIC DATA
         B     BLOOPEND                GO CHECK NEXT LINE
* COME HERE WHEN HI END GOOD AND LOW END MISSING
HIMIX    L     R15,LOGOOD              LOWEST GOOD ADDR
         SR    R15,R2                  NUMBER OF BYTES TO CLEAR
         LR    R1,R15                  COPY # TO CLEAR
         SRL   R1,2                    # OF WORDS TO CLEAR
         SLL   R1,1                    NUMBER OF IMBEDDED BLANKS
         AR    R1,R15                  ADD # OF HEX CHARS
         AR    R1,R15
         BCTR  R1,0                    REDUCE BY 1 FOR MVC
         LA    R6,10(R4)               START OF HEX DATA
         EX    R1,CLEARMVC             AND CLEAR THE HEX DATA
         LA    R6,50(R4)               POINT TO FIRST * ON LINE
         EX    R15,CLEARMVC            CLEAR * AND EBCDIC DATA
         LA    R6,50(R4,R6)            POINT TO FIRST CLEARED BYTE
         MVI   0(R6),C'*'              AND DROP IN A *
BLOOPEND LA    R4,79(R4)               POINT TO NEXT LINE
         LA    R2,16(R2)               ADDRESS ON NEXT LINE
         BCT   R5,BLLOOP               AND SCAN ALL LINES
         SPACE 2
*----------------------------------------------------------------------
*
*  PUT IN ARROWS TO POINT TO DESIRED DATA
*
*----------------------------------------------------------------------
         SPACE 2
         LA    R4,OUT1                 ADDR OF FIRST LINE
         TM    MODEFLAG,MIDDUMP        DOES IT BELONG IN THE MIDDLE
         BZ    ARRTOP                  BR IF NOT
         LA    R4,OUT9                 POINT TO MIDDLE LINE
ARRTOP   SR    R1,R1                   ASSUME NO QUADWORD ALIGNMENT
         TM    MODEFLAG,ALIGN          WAS ASSUMPTION CORRECT
         BZ    ARRALIGN                BR IF YES, ELSE GET OFFSET
         LA    R1,15                   MASK FOR LOW 4 BITS
         NR    R1,R2                   GET BYTE OFFSET FOR ARROW
* FIRST DO THE EBCDIC SIDE
ARRALIGN LA    R15,49(R4)              BYTE BEFORE FIRST *
         EX    R1,MOVEMVC              MOVE THE DATA BACK
         AR    R15,R1                  ADDR OF LAST BYTE MOVED
         MVI   1(R15),C'>'             PUT IN THE ARROW
* NOW MOVE THE HEX SIDE
         SR    R0,R0                   CLEAR HIGH ORDER BITS
         SLDL  R0,30                   WHICH WORD IS TO BE MOVED
         SRL   R1,30                   NUMBER OF BYTES TO BE MOVED
         LR    R15,R0                  COPY OFFSET OF WORD TO MOVE
         MH    R15,=H'10'              EACH WORD TAKES UP 10 BYTES
         LA    R15,8(R4,R15)           POINT IN FRONT OF WORD TO MOVE
         SLL   R1,R1                   NUMBER OF HEX DIGITS TO MOVE
         EX    R1,MOVEMVC              MOVE THE DATA BACK
         AR    R15,R1                  POINT TO PALCE FOR ARROW
         MVI   1(R15),C'>'             AND PUT IT THERE
* FALL  THRU TO SHOW THE SCREEN
         EJECT
* DISPLAY THE PAGE AND CHECK FOR END REQUEST
DISPLAY  TRMIO IOCB,FORMAT=SCR1,IMAGE=IOAREA
         CLI   IOCBAID,AIDPA1          SEE IF PA1 HIT
         BE    ENDIT                   IF YES, GO AWAY
         CLI   IOCBAID,X'F0'           SEE IF MIGHT BE PFK
         BH    TRYPFK1                 F1-F9 MEAN PFK 1-9
         CLI   IOCBAID,X'7C'           7A-7C ARE PFK 10-12
         BNH   TRYPFK2                 FALL THRU IF NOT PFK
NOTPFK   CLI   IOCBAID,AIDENT          SEE IF ENTER HIT
         BNE   DISPLAY                 IF NOT, DISPLAY AGAIN
         CLC   =C'END',INPLINE         SEE IF END TYPED
         BNE   ISCAN                   IF NOT, GO LOOK AT INPUT
* CLEAN UP AND GO AWAY
ENDIT    L     R13,SAVE+4              GET ORIGINAL SAVE AREA
         FREEMAIN R,LV=WORKLEN,SP=1,A=(R10)
         RETURN (14,12),RC=0
* PFK HANDLING HERE
TRYPFK1  CLI   IOCBAID,X'F9'           SEE IF WITHIN PFK 1-9
         BH    NOTPFK                  BR IF NOT
TRYPFK2  CLI   IOCBAID,X'7A'           SEE IF ABOVE FLOOR
         BL    NOTPFK                  BR IF NOT
         IC    R15,IOCBAID             GET THE PFK
         LA    R0,15                   PREPARE TO ISOLATE LOW DIGIT
         NR    R15,R0
         SLL   R15,2                   TURN PFK INTO AN INDEX
         B     *(R15)                  FIRST PFK IS 1
         B     DISPLAY                 IGNORE PFK 1
         B     DISPLAY                 IGNORE PFK 2
         B     DISPLAY                 IGNORE PFK 3
         B     DISPLAY                 IGNORE PFK 4
         B     REPEAT                  PFK 5 REPEAT LAST INPUT LINE
         B     DISPLAY                 IGNORE PFK 6
         B     UP                      PFK 7 SCROLL UP OR PREV CB SEG
         B     DOWN                    PFK 8 SCROLL DOWN OR NEXT CB SEG
         B     DISPLAY                 IGNORE PFK 9
         B     BACK                    PFK 10 - BACK THRU HISTORY
         B     FWD                     PFK 11 - FWD  THRU HISTORY
         B     DISPLAY                 IGNORE PFK 12
         SPACE 2
*----------------------------------------------------------------------
*
* CBHIT  -- CONTROL BLOCK FORMATTING ROUTINE
* AT ENTRY:    R3 HAS CURSOR OFFSET
*              R4 HAS ADDRESS TO DISPLAY
*              R5 HAS CBINDEX ENTRY ADDRESS
*
*----------------------------------------------------------------------
         SPACE 2
CBHIT    LH    R2,CURRENT              GET HISTORY TABLE POINTER
         LA    R2,HISTORY+4(R2)        GET CURRENT SEG FROM HIST TABLE
         ICM   R6,7,1(R2)              LAST SEGMENT DISPLAYED
         BNZ   CBHIT2                  BR IF A CONTINUING DISPLAY
         L     R6,8(R5)                ELSE GET FIRST SEG ADDRESS
         STCM  R6,7,1(R2)              AND SAVE FOR LATER
         MVI   0(R2),4                 NOTE CB FORMAT ROUTINE IN USE
         USING CBS,R6
CBHIT2   LH    R0,CBSSIZE              LENGTH TO FETCH
         LH    R1,DUMPDATA             TARGET ADDRESS
         LH    R2,CBSFIRST             OFFSET TO FETCH
         AR    R2,R4                   ADD START OF CB ADDRESS
         BAL   R14,GETSTOR             GO FETCH THE DATA
         B     NOCB                    BR IF NO DATA FETCHED
         B     NOCB                    BR IF PARTIAL DATA FETCHED
         LA    R5,OUT1                 START OF SCREEN
         LA    R2,DUMPDATA             POINT TO DATA BUFFER
CBLOOP   LH    R15,CBSTYPE             GET FORMAT ITEM TYPE
         B     *+4(R15)                GO TO FORMAT ROUTINE
         B     DISPLAY                 TYPE 0 END OF SEGMENT
         B     CBHEX                   TYPE 4 HEX
         B     CBCONST                 TYPE 8 EBCDIC CONSTANT
         B     CBEBCD                  TYPE 12 EBCDIC DATA
         SPACE 2
* EBCDIC CONSTANT ITEM
CBCONST  LA    R15,CBSDATA             ADDR OF DATA ITEM
         LH    R14,CBSLEN              LENGTH FOR MVC
         LH    R1,CBSOFF               SCREEN OFFSET
         AR    R1,R5                   ADD START OF SCREEM
         EX    R14,CBEMVC              MOVE EBCDIC DATA
         LA    R6,7(R14,R6)            POINT TO NEXT FOMAT ITEM
         B     CBLOOP
CBEMVC   MVC   0(0,R1),0(R15)          MOVE EBCDIC DATA
* EBCDIC DATA ITEM
CBEBCD   LH    R15,CBSDATA             OFFSET OF DATA TO SHOW
         AR    R15,R2                  ADD START OF BUFFER
         LH    R14,CBSLEN              LENGTH TO MOVE
         LH    R1,CBSOFF               SCREEN OFFSET
         AR    R1,R5                   ADD START OF SCREEN
         EX    R14,CBEMVC              MOVE THE DATA
         LA    R6,8(R6)                POINT TO NEXT FORMAT ITEM
         B     CBLOOP
* HEX DATA ITEM
CBHEX    LH    R15,CBSDATA             DATA OFFSET
         AR    R15,R2                  ADD START OF BUFFER
         UNPK  TEMP1(9),0(5,R15)       UNPACK THE DATA
         TR    TEMP1(8),HEXTAB         AND CONVERT TO HEX
         IC    R14,CBSLEN+1            GET MVC LENGTH
         LH    R1,CBSOFF               GET SCREEN OFFSET
         AR    R1,R5                   ADD START OF SCREEN
         LA    R15,TEMP1               FIRST DIGIT UNPACKED
         TM    CBSLEN,1                SEE IF NEXT DIGIT WANTED
         BZ    CBHEX2                  BR IF NOT
         LA    R15,TEMP1+1             ELSE POINT TO NEXT HEX DIGIT
CBHEX2   EX    R14,CBEMVC              MOVE THE DATA
         LA    R6,8(R6)                POINT TO NEXT FORMAT ITEM
         B     CBLOOP
         DROP  R6
         SPACE 2
***********************************************************************
*
* COMMAND ROUTINES
*
         SPACE 2
*----------------------------------------------------------------------
*
* OVERLAY  --  MANIPULATE CONTROL BLOCK FORMATTING
*
*----------------------------------------------------------------------
         SPACE 2
OVERLAY  CLC   =C'NULL',INPLINE+1      SEE IF NO FORMATTING WANTED
         BE    OVERNULL                BR IF YES
         L     R5,=V(CBMACS)           GET CB MAP INDEX
OVERLOOP CLI   0(R5),X'FF'             SEE IF END OF INDEX
         BE    FORMAT                  BR IF YES,IGNORE COMMAND
         CLC   INPLINE+1(4),0(R5)      SEE IF CB MATCH
         BE    OVERHIT                 BR IF YES
         LA    R5,12(R5)               ELSE POINT TO NEXT ENTRY
         B     OVERLOOP                AND KEEP LOOKING
OVERHIT  LH    R2,CURRENT              CURRENT HIST TABLE ENTRY OFFSET
         L     R6,8(R5)                START SEG ADDR FOR CB FOUND
         ST    R6,HISTORY+4(R2)        PUT IN HISTORY TABLE
         LA    R2,HISTORY+4(R2)        POINT AT FLAG BYTE IN HIST TABLE
         MVI   0(R2),4                 NOTE CB FORMAT IN USE
         B     FORMAT                  GO SHOW IT
OVERNULL LH    R2,CURRENT              CURRENT HIST TABLE ENTRY OFFSET
         LA    R2,HISTORY+4(R2)        POINT AT FLAG BYTE IN HIST TABLE
         MVI   0(R2),8                 SUPPRESS CB OVERLAY
         B     FORMAT                  GO SHOW STORAGE
         SPACE 2
*----------------------------------------------------------------------
*
* DOWN -- SCROLL DOWN 256 BYTES OR NEXT CTRL BLK SEGMENT
*
*----------------------------------------------------------------------
         SPACE 2
DOWN     LH    R2,CURRENT              CURRENT HIST TABLE POINTER
         LA    R2,HISTORY(R2)          POINT TO HIST TABLE ENTRY
         CLI   4(R2),4                 SEE IF THIS IS A CB DISPLAY
         BE    DOWNCB                  BR IF YES
         L     R1,0(R2)                ELSE GET ADDR TO SHOW
         AH    R1,=H'256'              AND MOVE DOWN ONE SCREEN
         STCM  R1,7,1(R2)              AND REPLACE IN HIST TABLE
         B     FORMAT                  GO DISPLAY IT
DOWNCB   L     R1,4(R2)                GET CB SEGMENT ADDR
         L     R1,4(R1)                GET NEXT SEG ADDR
         STCM  R1,7,5(R2)              AND PUT IN HIST TABLE
         B     FORMAT                  GO SHOW IT
         SPACE 2
*----------------------------------------------------------------------
*
* UP   -- SCROLL UP   256 BYTES OR PREV CTRL BLK SEGMENT
*
*----------------------------------------------------------------------
         SPACE 2
UP       LH    R2,CURRENT              CURRENT HIST TABLE POINTER
         LA    R2,HISTORY(R2)          POINT TO HIST TABLE ENTRY
         CLI   4(R2),4                 SEE IF THIS IS A CB DISPLAY
         BE    UPCB                    BR IF YES
         L     R1,0(R2)                ELSE GET ADDR TO SHOW
         SH    R1,=H'256'              AND MOVE UP ONE SCREEN
         STCM  R1,7,1(R2)              AND REPLACE IN HIST TABLE
         B     FORMAT                  GO DISPLAY IT
UPCB     L     R1,4(R2)                GET CB SEGMENT ADDR
         L     R1,0(R1)                GET PREV SEG ADDR
         STCM  R1,7,5(R2)              AND PUT IN HIST TABLE
         B     FORMAT                  GO SHOW IT
         SPACE 2
*----------------------------------------------------------------------
*
* REPEAT -- REPEAT LAST INPUT LINE
*
*----------------------------------------------------------------------
         SPACE 2
REPEAT   MVC   INPLINE(79),INPSAVE     COPY SAVED INPUT
         B     ISCAN2                  AND PROCESS IT
         SPACE 2
*----------------------------------------------------------------------
*
* LINK -- LINK TO LABELED CONTROL BLOCK FIELD
*
*----------------------------------------------------------------------
         SPACE 2
LINK     LH    R5,CURRENT              CURRENT HIST TABLE POINTER
         LA    R5,HISTORY(R5)          POINT TO HIST TABLE ENTRY
         CLI   4(R5),4                 SEE IF CB FORMATTING IN USE
         BE    LINK2                   BR IF YES
LINKERR  MVC   MSGLINE,LINKMSG         NOTE ERROR IN LINK
         B     FORMAT                  AND RESHOW SCREEN
LINK2    L     R3,4(R5)                GET CURRENT CB SEG ADDRESS
         LR    R4,R3                   SAVE THE START OF SEG ADDR
         USING CBS,R3
LINK3    LH    R15,CBSTYPE             GET ITEM TYPE
         B     *+4(R15)                GO HANDLE ITEM TYPE
         B     LINKEND                 END OF SEGMENT
         B     LINKHEX                 HEX ITEM
         B     LINKCON                 CONSTANT ITEM
         B     LINKHEX                 EBCD ITEM
         SPACE
* HEX    ITEM
LINKHEX  LA    R3,8(R3)                POINT TO NEXT ITEM
         B     LINK3                   CHECK NEXT ITEM
* CONST  ITEM  (A LABEL)
LINKCON  LH    R14,CBSLEN              GET LABEL LENGTH
         LA    R15,CBSDATA             POINT TO THE LABEL
         EX    R14,LINKEX              SEE IF ITS THE SAME
         LA    R3,7(R3,R14)            POINT TO NEXT ITEM
         BNE   LINK3                   BR IF NOT A MATCH, IGNORE IT
         LA    R1,INPLINE+2(R14)       POINT TO BYTE AT END OF COMMAND
         CLI   0(R1),C' '              SEE IF FOLLOWED BY BLANK
         BNE   LINK3                   BR IF NOT A MATCH, IGNORE IT
         CLI   CBSTYPE+1,4             SEE IF NEXT ITEM IS HEX
         BNE   LINK3                   IF NOT, ANALYZE IT FOR A LABEL
* A MATCH HAS  BEEN FOUND. FETCH THE  ADDRESS TO LINK TO
         SR    R1,R1                   CLEAR FOR IC
         IC    R1,CBSLEN+1             LENGTH OF ITEM  MINUS 1
         LA    R1,2(R1)                TRUE LENGTH PLUS 1 FOR ROUNDING
         SRL   R1,1                    GET NUMBER OF BYTES TO FETCH
         LR    R0,R1                   COPY TO LENGTH REG
         LA    R1,DUMPDATA             ADDR TO PLACE DATA IN
         LH    R2,CURRENT              HIST TABLE POINTER
         L     R2,HISTORY(R2)          CCURRENT DISPLAY ADDRESS
         AH    R2,CBSDATA              OFFSET OF ITEM TO FETCH
         AH    R2,CBSFIRST-CBS(R4)     AND REMEMBER THE SEGMENT OFFSET
         BAL   R14,GETSTOR             FETCH THE  ADDRESS
         B     LINK3                   BR IF DATA CANT BE FETCHED
         B     LINK3                   BR IF DATA CANT BE FETCHED
         BAL   R15,FWDSUB              UPDATE HIST TABLE TO NEXT
         SR    R15,R15                 CLEAR FOR IC
         IC    R15,CBSLEN+1            GET LENGTH MINUS 1 AGAIN
         LA    R15,1(R15)              GET TRUE LENGTH
         SLL   R15,2                   4 BITS PER NIBBLE
         SR    R0,R0                   CLEAR RESULT REG
         L     R1,DUMPDATA             LOAD ADDRESS THAT WAS FETCHED
         TM    CBSLEN,1                SEE IF ODD DIGIT WANTED
         BZ    LINKEVEN                BR IF NOT
         SLL   R1,4                    ELSE SHIFT OUT UNWANTED BITS
LINKEVEN SLDL  R0,0(R15)               ISOLATE DESIRED BITS
         LH    R2,CURRENT              NEW HIST TABLE POINTER
         ST    R0,HISTORY(R2)          SAVE IN HIST TAB
         SR    R0,R0                   CLEAR CBSEGMENT INDICATOR
         ST    R0,HISTORY+4(R2)        SAVE IN HIST TABLE
         B     FORMAT                  GO FORMAT THE NEW CB
         DROP  R3
         USING CBS,R4
LINKEND  L     R3,CBSNEXT              NEXT SEG ADDR
         LR    R4,R3                   SAVE IT
         CLM   R4,7,5(R5)              SEE IF SAME AS FIRST ONE
         BE    LINKERR                 YES, LABEL NOT FOUND
         B     LINK3                   ELSE SEARCH NEW SEG
         DROP  R4
LINKEX   CLC   0(0,R15),INPLINE+1      CHECK FOR LABEL MATCH
***********************************************************************
*
*  SET INDIRECT FLAG
*
***********************************************************************
         SPACE 2
INDIR    OI    FLAGS,INDFLAG           SET FLAG
         LA    R3,1(R3)                UPDATE SCAN POINTER
         CLI   INPLINE+1,C' '          USE CURRENT ADDRESS
         BNE   NUMSCAN                 IF NO, GO SCAN INPUT ADDRESS
         LH    R4,CURRENT              GET LAST ADDRESS DISPLAYED
         L     R4,HISTORY(R4)          AND USE AS BASE FOR INDIRECT
         B     ENDNUM
         SPACE 2
***********************************************************************
*
*  ESTABLISH SCREEN MODE
*
***********************************************************************
         SPACE 2
MODE     CLC   =C'1 ',INPLINE+1        MODE ONE TEST
         BE    MODE1                   BR IF YES
         CLC   =C'2 ',INPLINE+1        CHECK FOR MODE TWO
         BNE   INVAL                   IF NOT EITHER, INVALID
         NI    MODEFLAG,255-MIDDUMP    MODE 2 MEANS BACKUP 0
         B     FORMAT
MODE1    OI    MODEFLAG,MIDDUMP        MODE 1 MEANS BACK UP 7 LINES
         B     FORMAT
INVAL    MVC   MSGLINE,MODEMSG
         B     FORMAT
         SPACE 2
***********************************************************************
*
*  MANIPULATE HISTORY TABLE
*
***********************************************************************
         SPACE 2
FWD      BAL   R15,FWDSUB              FORWARD 1 IN HIST TABLE
         B     FORMAT
         SPACE
FWDSUB   LH    R1,CURRENT              CURRENT ENTRY INDEX
         LA    R1,8(R1)                ADVANCE 1
         CH    R1,=H'400'              SEE IF WRAP NEEDED
         BL    FWDEND                  BR IF NOT
         SR    R1,R1                   WRAP TO BEGINNING
FWDEND   STH   R1,CURRENT              STORE NEW OFFSET
         BR    R15                     RETURN TO CALLER
BACK     LH    R1,CURRENT
         SH    R1,=H'8'                BACK UP ONE ENTRU
         BNM   BACKEND                 BR IF NO BACKWARD WRAP
         LA    R1,8*49                 ELSE GO TO END OF TABLE
BACKEND  STH   R1,CURRENT              SAVE RESULT
         B     FORMAT
         SPACE 2
***********************************************************************
*
*   ADD ENTRY TO SYMBOL TABLE
*
***********************************************************************
         SPACE 2
DEFINE   LA    R15,50                  NUMBER OF TABLE ENTRIES
         LA    R2,NAMES                START OF TABLE
DEFLOOP1 CLI   0(R2),X'0'              SEE IF UNUSED ENTRY
         BE    NSPACE                  BR IF SPACE FOUND
         LA    R2,12(R2)               NEXT ENTRY ADDR
         BCT   R15,DEFLOOP1            TRY ALL ENTRIES
         MVC   MSGLINE,SYMFULL         TABLE IS FULL
         B     FORMAT
NSPACE   LA    R15,9                   MAX LENGTH +1
         LA    R0,INPLINE+1            ADDR OF FIRST BYTE
         LR    R1,R0                   COPY SYMBOL ADDR
DEFLOOP2 CLI   0(R1),C' '              SEE IF END OF SYM
         BE    DEFEND                  BR IF YES
         LA    R1,1(R1)                POINT TO NEXT BYTE
         BCT   R15,DEFLOOP2            CONTINUE FOR 9 BYTES
DEFBAD   MVC   MSGLINE,DEFMSG          SYMBOL TOO LONG OR TOO SHORT
         B     FORMAT2                 DISPLAY WITH CURSOR AT ERROR
DEFEND   SR    R1,R0                   SYMBOL LENGTH
         BZ    DEFBAD                  NULL SYMBOL ILLEGAL
         BCTR  R1,R0                   FOR EXECUTE
         MVC   0(8,R2),=CL8' '         CLEAR TABLE ENTRY
         EX    R1,DEFMOVE              MOVE SYMBOL
         LH    R1,CURRENT              GET CURRENT ADDRESS
         L     R1,HISTORY(R1)
         ST    R1,8(R2)                STORE WITH SYMBOL
         B     FORMAT
DEFMOVE  MVC   0(0,R2),INPLINE+1       EXECUTED TO MOVE SYMBOL
         SPACE 2
***********************************************************************
*
*  SCAN THE ADDRESS EXPRESSION
* REGISTER USAGE
* R2 LAST NUMBER SCANNED
* R3 SCAN POINTER
* R4 CURRENT OUTPUT VALUE
* R5 LAST OPERATOR SCANNED
*
***********************************************************************
         SPACE 2
NUMSCAN  LH    R4,CURRENT              HISTORY TABLE POINTER
         L     R4,HISTORY(R4)          CURRENT ENTRY IS DEFAULT
         LA    R5,0                    ASSUME DEFAULT TO BE TAKEN
         CLI   0(R3),C'+'              SEE 2F PREFIX PLUS
         BNE   NOTPLUS                 BR IF NOT
         LA    R5,2                    INDICATE PLUS OPERATOR
         B     NUMOP                   GO TO ADV POINTER
NOTPLUS  CLI   0(R3),C'-'              IS IT PREFIX MINUS
         BNE   NUMLOOP                 BR IF NOT, USE DEFAULT AS LOAD
         LA    R5,4                    INDI"ATE MINUS OPERATOR
NUMOP    LA    R3,1(R3)                POINT TO NEXT INPUT BYTE
NUMLOOP  CLI   0(R3),C','              SEE IF NAME INDICATED
         BE    NAMESCAN                BR IF YES
         TRT   0(9,R3),NUMTAB          SEARCH FOR DELIMITER
         LR    R6,R3                   SAVE START OF NUMBER
         BC    8,BADSTUFF              BR IF INPUT TOO LONG
         LR    R3,R1                   UPDATE SCAN POINTER
         SR    R1,R6                   GET LENGTH OF INPUT
         BZ    BADSTUFF                BR IF ZERO INPUT
         MVC   TEMP1(8),=CL8' '        CLEAR TEMP AREA
         LA    R15,TEMP1+8             RIGHT JUSTIFY INPUT
         SR    R15,R1                  POINT TO RIGHT SPOT IN TEMP AREA
         BCTR  R1,0                    PREPARE FOR EX
         EX    R1,HEXMOVE              MOVE INPUT RIGHT JUSTIFIED
         TR    TEMP1(8),HEXTAB         TRANSLATE TO HEX
         PACK  TEMP2(5),TEMP1(9)       CONVERT HEX TO BINARY
         ICM   R2,15,TEMP2             NEW DISPLAY ADDRESS
NUMDELIM EX    0,OPER(R5)              EXECUTE PREVIOUS OPERATOR
         CLI   0(R3),C' '              IS DELIM A BLANK
         BE    ENDNUM                  IF YES, END SCAN
         LA    R5,2                    PLUS OPERATOR CODE
         CLI   0(R3),C'+'              IS OPER L PLUS
         BE    NUMOP                   BR IF YES TO CONTINUE SAAN
         LA    R5,4                    MINUS CODE
         CLI   0(R3),C'-'              IS OPER MINUS
         BE    NUMOP                   BR IF YES
         MVC   MSGLINE,INVALOP         ELSE BAD DELIMITER
         B     FORMAT2                 DISPLAY WITH CURSOR AT ERROR
ENDNUM   TM    FLAGS,INDFLAG           IS THIS AN INDIRECT REQ
         BZ    NOTIND                  BR IF NOT
         L     R4,0(R4)                DO THE INDIRECT
NOTIND   BAL   R15,FWDSUB              UPDATE HISTORY POINTR
         LH    R15,CURRENT             NEW HIST TABLE OFFSET
         ST    R4,HISTORY(R15)         SAVE VALUE IN HISTORY TABLE
         SR    R0,R0                   INDICATE DISPLAY FORMAT IS DUMP
         ST    R0,HISTORY+4(R15)       AND SAVE WITH HISTORY
         B     FORMAT
         SPACE 2
* SCAN INPUT FOR A NAME IN THE SYMBOL TABOL
NAMESCAN LA    R3,1(R3)                SKIP OVER COMMA
         LR    R1,R3                   COPY INPUT POINTER
         LA    R15,9                   MAX LENGTH +1
NAMELOOP CLI   0(R1),C'A'              SEE IF VALID CHAR
         BL    NAMEEND                 BR IF NOT, DELIM FOUND
         LA    R1,1(R1)                POINT TO NEXT BYTE
         BCT   R15,NAMELOOP            ALLOW EIGHT BYTES PLUS DELIM
NAMEBAD  MVC   MSGLINE,NAMEMSG         NULL NAME OR NAME TOO LONG
         B     FORMAT2                 DISPLAY WITH CURSOR AT ERROR
NAMEEND  SR    R1,R3                   FIND LENGTH
         BZ    BADSTUFF                BR IF TOO SHORT
         MVC   TEMP1(8),=CL8' '        CLEAÖ  WORK AREA
         BCTR  R1,0                    SET UP FOR EX
         EX    R1,NAMEMOVE             MOVE TO WORK AREA, BLANK PAD
         LA    R3,1(R3,R1)             UPDATE POINTER
         LA    R15,50                  ENTRIES IN SYM TABLE
         LA    R1,NAMES                START OF SYM TABLE
NAMELP2  CLC   TEMP1(8),0(R1)          SEE IF NAME IN TABLE
         BE    NAMEFIND                BR IF YES
         LA    R1,12(R1)               POINT TO NEXT ENTRY
         BCT   R15,NAMELP2             BR IF ANY ENTRIES LEFT
         B     NAMEBAD                 BR IF NONE LEFT, UNDEFUNED SYM
NAMEFIND L     R2,8(R1)                GET >YMBOL VALUE
         B     NUMDELIM                USE IN EXPRESSION
*  THESE INSTRUCTIONS ARE THE OBJECT OF AN EXECUTE
OPER     LR    R4,R2                   USE HIST TABLE AS DEFAULT INPUT
         AR    R4,R2                   PLUS OPERATOR
         SR    R4,R2                   MINUS OPERATOR
HEXMOVE  MVC   0(0,R15),0(R6)          RIGHT JUSTIFY INPUT DATA
NAMEMOVE MVC   TEMP1(1),0(R3)          PAD NAME WITH BLANKS BY MOVING
         SPACE
BADSTUFF MVC   MSGLINE,ERRMSG          NOTE BAD INPUT
         B     FORMAT2                 DISPLAY WITH CURSOR AT ERROR
         SPACE 2
***********************************************************************
*
*  PROGRAM CHECK EXIT
*
***********************************************************************
         SPACE 2
SPIEEXIT MVC   MSGLINE,PCMSG           TELL USER OF PC
         LA    R0,SPIERET              SET RETURN FROM GETSTOR
         ST    R0,8(R1)                PSW IN PIE
SPIERET  BR    R14                     RETURN TO SUPV
         SPACE 2
*----------------------------------------------------------------------
*
* ERASE  --  CLEAR THE SCREEN BUFFER
*
*----------------------------------------------------------------------
         SPACE 2
ERASE    SAVE  (2,5)                   NEED SOME WORK REGS
         LA    R2,OUT1                 START OF AREA TO CLEAR
         LA    R3,79*16                LENGTH TO CLEAR
         LA    R5,C' '                 FILL CHAR
         SLL   R5,24                   MAKE PAD BYTE AND CLEAR SOURCE
         MVCL  R2,R4                   PROPAGATE BLANKS
         RETURN (2,5)                  RESTORE REGS AND RETURN
         EJECT
* EXECUTED INSTRUCTIONS AND CONSTANTS
CLEARMVC MVC   0(0,R6),BLANKS          CLEAR SOME SPACE
MOVEMVC  MVC   0(0,R15),1(R15)         MOVE DATA BACKWARDS
* TEMP CONSTANTS TILL GETSTOR IS WRITTEN
GETSTOR  MVC   DUMPDATA(256),0(R2)
         B     8(R14)
LOGOOD   DC    F'0'
HIGOOD   DC    XL4'00FFF123'
* ERROR MESSAGE FOR BAD INPUT
TITLE    DC    CL79'ZAP -- ENTER ADDRESS TO DISPLAY'
MODEMSG  DC    CL79'*****INVALID MODE'
INVALOP  DC    CL79'***** INVALID OPERATOR'
SYMFULL  DC    CL79'***** SYMBOL TABLE IS FULL'
NAMEMSG  DC    CL79'***** UNDEFINED SYMBOL'
DEFMSG   DC    CL79'***** NULL SYMBOL OR SYMBOL TOO LONG'
PCMSG    DC    CL79'***** PROGRAM CHECK'
LINKMSG  DC    CL79'***** LINK FAILED'
HEXTAB   DC    256C' '                 GENERAL HEX CONVERSION TABLE
ERRMSG   DC    CL79'INVALID ADDRESS -- TRY AGAIN'
BLANKS   DC    CL79' '
         EJECT
         ORG   HEXTAB+C'A'             INPUT CONVERSION
         DC    X'0A0B0C0D0E0F'
         ORG   HEXTAB+C'0'             OUTPUT CONVERSION
         DC    C'0123456789ABCDEF'
         ORG
* HEX INPUT TRT TABLE
NUMTAB   DC    256X'1'
         ORG   NUMTAB+C'A'
         DC    6X'0'
         ORG   NUMTAB+C'0'
         DC    10X'0'
         ORG
VALCHAR  DC    256C'.'                 TRANSLATE TO VALID CHARS
         ORG   VALCHAR+C' '
         DC    C' '
         ORG   VALCHAR+C'Ö'
         DC    C'Ö.<(+×&&'
         ORG   VALCHAR+C'!'
         DC    C'!$*);^-/'
         ORG   VALCHAR+C','
         DC    C',%_.?'
         ORG   VALCHAR+C':'
         DC    C':#@''="'
         ORG   VALCHAR+C'A'
         DC    C'ABCDEFGHI'
         ORG   VALCHAR+C'J'
         DC    C'JKLMNOPQR'
         ORG   VALCHAR+C'S'
         DC    C'STUVWXYZ'
         ORG   VALCHAR+C'0'
         DC    C'0123456789'
         ORG
*  SCREEN FORMAT
SCR1     SFMT
MSGLINE  FIELD 79
OUT1     FIELD 79,ROW=3
OUT2     FIELD 79
OUT3     FIELD 79
OUT4     FIELD 79
OUT5     FIELD 79
OUT6     FIELD 79
OUT7     FIELD 79
OUT8     FIELD 79
OUT9     FIELD 79
OUT10    FIELD 79
OUT11    FIELD 79
OUT12    FIELD 79
OUT13    FIELD 79
OUT14    FIELD 79
OUT15    FIELD 79
OUT16    FIELD 79
INPLINE  FIELD 79,ALPHA,ROW=20,CURSOR=YES
INPSAVE  FIELD 79
         SFEND
         LTORG
         SPACE 2
WORKAREA DSECT
IOCB     IOCB
SAVE     DS    18F
HISTORY  DS    100F                    HISTORY TABLE 50 ENTRIES
NAMES    DS    150F                    SYMB3L TABLE
DUMPDATA DS    XL256                   DATA RETURNED BY GETSTOR
CURRENT  DS    H                       HIST TABLE OFFSET
SPIE     SPIE  MF=L                    ROOM FOR SPIE PARM LIST
IOAREA   EQU   *
         DSGEN SCR1
IOLEN    EQU   *-IOAREA
TEMP1    DS    CL9                     CONVERSION WORK AREA
TEMP2    DS    CL9                     CONVERSION WORK AREA
FLAGS    DS    X                       ASSORTED FLAGS
INDFLAG  EQU   X'80'                   INDIRECT IN PROGRESS
MODEFLAG DS    X                       USER OPTION FLAGS
MIDDUMP  EQU   X'80'                   ON TO FORCE DISPLAY BACK 8 LINES
ALIGN    EQU   X'40'                   ON TO ALIGN DUMP ON QUADWORD
WORKLEN  EQU   *-WORKAREA
         SPACE 5
*----------------------------------------------------------------------
*
* CBS  -- CONTROL BLOCK SEGMENT
*
*----------------------------------------------------------------------
         SPACE 2
CBS      DSECT
CBSPREV  DS    A                       ADDR OF PREVIOUS SEGMENT
CBSNEXT  DS    A                       ADDR OF NEXT SEGMENT
CBSFIRST DS    H                       OFFSET OF FIRST DATA TO FETCH
CBSSIZE  DS    H                       LENGTH OF DATA TO FETCH
CBSNAME  DS    CL8                     SEGMENT IDENTIFIER
* THE FOLOWING FIELDS DESCRIBE A FORMAT ITEM
CBSTYPE  DS    H                       FORMAT TYPE
CBSLEN   DS    H                       SCREEN LENGTH OF ITEM AS REQ
*                                      BY FORMAT ROUTINE
CBSOFF   DS    H                       SCREEN OFFSET OF ITEM
CBSDATA  DS    H                       DATA OFFSET FROM START OF THIS
*                                      SEGMENTS DATA AREA OR A VARIABLE
*                                      LENGTH CONSTANT
         PRINT NOGEN
R0       EQU 0
R1 EQU 1
R2 EQU 2
R3 EQU 3
R4 EQU 4
R5 EQU 5
R6 EQU 6
R7 EQU 7
R8 EQU 8
R9 EQU 9
R10 EQU 10
R11 EQU 11
R12 EQU 12
R13 EQU 13
R14 EQU 14
R15 EQU 15
*        END
 END
