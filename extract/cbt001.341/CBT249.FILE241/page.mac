SPAM     TITLE 'S P A M- SCREEN PAGING ACTIVITY MONITOR'
*.
* SPAM
*
* THIS MONITOR IS WRITTEN TO DISPLAY  ON AN INTERVAL ( CURRENTLY 1 SEC)
* PAGING STATISTICS KEPT IN THE PVT. OUTPUT IS IN BAR-GRAPH FORMAT ON
* A 3270 TUBE UNDER THE CONTROL OF DCMS.
*
* THE FOLLOWING INPUT FUNCTIONS ARE DEFINED:
*
*       3270
*        KEY     FUNCTION
*      ________  ______________________________________________________
*
*    .  -----
*      × PA1 ×   USED TO TERMINATE SPAM
*      ×     ×
*       -----
*
*    .  -----
*      × PF1 ×   CHEAP HELP (DESCRIPTION OF WHAT YOU ARE SEEING)
*      ×     ×
*       -----
*
*    .  -----
*      × PF2 ×   WHEN STRUCK CAUSES USER TO BE PROMPTED TO
*      ×     ×   CHANGE THE UPDATE  TIME. TIME IS SPECIFIED
*       -----    AS; SSTH SECONDS, TENTHS, HUNDREDTHS
*
*    .  -----
*      ×CLEAR×   WILL GET THE USER OUT OF  SPAM  WHEN ALL
*      ×     ×   ELSE FAILS
*       -----
*
*
* AFTER SPAM GIVES YOU HIS LITTLE CHEER-LEADING PITCH YOU MUST CAUSE
* ANOTHER INTERRUPT TO FIRE HIM UP. FIELDS WILL ALWAYS BE *OVERFLOW*
* IN THE BEGINNING DUE TO OUR COMPLETE LACK OF KNOWLEDGE OF THE DAYS
* PREVIOUS EVENTS.
*
*
*    ANY PROBLEMS, COMMENTS (NON-CAUSTIC), SUGGESTIONS ARE
*    WELCOME:
*       FRED LUDDY
*       AMDAHL CORP.
*       1250 E. ARQUES
*       SUNNYVALE CA.
*    PHONE:
*       (408) - 735 - 4011
*.
         EJECT
PAGE     DCMSTART R,LV=4096,STAE=NO,IOCB=YES
*
* XSTART SAVED THE PARM PTR (R1) IN BOTH R1 & R5 SO WE FIND IT HERE
*
         L     R5,0(R5)            GET THE TDCB ADDRESS
         ST    R5,TDCBADDR         SAVE IT
         LA    R5,0(R5)
         ST    R5,IOCBTDCB
         L     R6,CVTPTR           FIND THE PVT
         L     R6,CVTPVTP-CVT(R6)  ...
         USING PVT,R6              ...
         BAL   R9,INISH            SET ORIGINAL SCREEN VALUES
         BAL   R9,WRITE            WRITE THE SCREEN
         DCMSNULL IMAGE=IOAREA,FORMAT=SCR1
         EJECT
         SPACE
*
*  TIME  LOOP  BEGINS HERE
*
         SPACE
SPAM0    EQU   *
         TIME  BIN
         LR    R15,R0              SAVE FOR LATER
         S     R0,PTIME            SUBTRACEPREVIOUS TIME
         ST    R0,ATIME            ACTUAL ELAPSED TIME
         ST    R15,PTIME           SAVEFOR NEXT LOOP
         LA    R7,SCR1LNE0+9       POINT TO FIRST DATA AREA
         LA    R8,13               GET NMBER  OF ENTRIES
         LA    R1,PVTNPIN +16***   FIRST PVT ENTRY
*                            ***   TO GET AROUND SU7 DAMAGE
*
* IF YOU HAVE AN UPDATED VERSION OF THE PVT MACRO CHANGE THE PREVIOUS
* LINE OF CODE.
*
*
         LA    R3,XVTNPIN          FIRST XVT ENTRY
         LA    R9,PCOUNTRS         ADDRESS OF PREVIOUS COUNTERS
*.
*   BEGIN PROCESS LOOP -
*   PICK UP EACH PVT VALUE (THEY ARE NEATLY BEHIND EACH OTHER) AND
*   SUBTRACT THE XVT VALUE FROM IT.. GIVING THE INTEGER TO BE PLOTTED
*   FOR THIS TIME INTERVAL.
*   IF THE VALUE IS...      THEN...
*   ------------------      --------------------------------
*   GREATER THAN  69        WRITE *OVERFLOW* IN  THE LINE
*   NEGATIVE                WRITE *FLIPOVER* IN THE LINE (OCCURS  WHEN
*                           THE PVT COUNTER TURNS OVER)
*   0 < VALUE < 140         PLOT THE VALUE ON THE SCREEN
*
*   REGISTER USAGE
*   R0       - WORK REGISTER
*   R1       - POINTER TO CURRENT PVT ENTRY
*   R2       - CURRENT PVT ENTRY
*   R3       - POINTER TO CURRENT XVT ENTRY
*   R4       - CURRENT XVT ENTRY
*   R7       - POINTER TO CURRENT OUTPUT AREA
*   R8       - BCT REGISTER
*   R9       - PTR TO PREVIOUS DIFFERENCE ( FOR ROUNDING )
*.
         SPACE 2
SPAM1    EQU   *
         MVC   0(70,R7),BLANKS     CLEAR THIS ENTRY
         L     R2,0(R1)            PICK UP ENTRY  (PVT)
         L     R4,0(R3)            DITTO          (XVT)
         ST    R2,0(R3)            SAVE FOR NEXT TIME AROUND (NEW-XVT
         SR    R2,R4               GET NEW VALUE  (PVT - XVT)
* AT THIS POINT R2 HAS THE VALUE WE WILL LOOK AT ON THE NEXT GO AROUND
*
* SMOOTHING IS DONE EXPONENTIALLY WITH TWO FACTORS, NO WEIGHT IS PLACED
* ON EITHER.
*
         LR    R0,R2               SAVE OVER ADD
         A     R2,0(R9)            SMOOTHING ( EXPONENTIAL )
         ST    R0,0(R9)            NOW WE CAN SAVE IT
         SRA   R2,1                DIVIDE BY 2 (NEW + OLD)/2
         LR    R15,R2              MOVE OVER TO EVEN/ODD PAIR
         SR    R14,R14             CLEAR H.O. REG
         MH    R15,=H'100'         ALIGN DECIMAL POINT
         D     R14,ATIME           CALCULATE ACTUAL PAGING RATE
         LTR   R2,R15              CHECK WHATS HAPPENING
         BNM   SPAM2               ITS A POSSITIVE
         MVC   0(69,R7),FLIPPED    SHOW ENTRY FLIPPED OVER
         B     SPAM4               ...
SPAM2    EQU   *
         C     R2,F69              WELL ?
         BNH   SPAM3               ITS ALL RIGHT
*
* THIS CODE WILL OVERLAY THE ORIGINAL BAR WITH AN OVERFLOW CHARACTER
* WHEN THE UTILIZATION IS OVER 70 (THE LENGTH OF THE BAR).
* EXAMPLE:
* IF THE GRAPH IS TEN CHARS LONG AND THE AMOUNT WE ARE PLOTTING IS 15
* THE GRAPH WILL LOOK LIKE THIS;
*
* 0....+....1....+
* ×>>>>>*****
*
* WHERE '>' IS THE OVERLAY CHARACTER
*
* THANKS TO KEITH SOPER OF RAND FOR THE IDEA.
*
         MVC   0(70,R7),STARS      FIRST FIELD
         SL    R2,F69
         CL    R2,F69              STILL OVERFLOWED ?
         BH    NOHOPE               HELL.. WHATS HAPPENING HERE  ?
         EX    R2,STRIPEM          MOVE THE STRIPES
         B     SPAM4
NOHOPE   EQU   *
         MVC   0(69,R7),OVERFLOW
         B     SPAM4
SPAM3    EQU   *
         EX    R2,STARMOVE         MOVE EM BOSS
SPAM4    EQU   *
         LA    R1,4(,R1)           GET NEXT SET OF ENTRIES
         LA    R3,4(,R3)           ...
         LA    R7,79(,R7)          ...
         LA    R9,4(,R9)           ...
         BCT   R8,SPAM1            GO TO IT BOZO
         SPACE
*                                                                V1
* GET THE CPU UTILIZATION AND PAGING RATE FROM THE RCT, SEE WHAT V1
* THE DREADED SRM THINKS OF THIS MESS                            V1
*                                                                V1
         MVC   SCR1HDR(HDR2LEN),HDR2 MOVE THE SKELETON           V1
         L     R1,CVTPTR                                         V1
         USING CVT,R1                                            V1
         L     R1,CVTOPCTP         POINTER TO THE OPTIMIZER'S    V1
*                                   AREA                         V1
         LA    R1,192+128+88+96(,R1) GET DISPLACDMENT INTO RMCT  V1
*                                     FOR THE RCT (SORRY BOUT IT)V1
         LH    R0,40(,R1)          GET THE CPU UTILIZATION       V1
         SRL   R0,4                DIVIDE USAGE BY 16            V1
         CVD   R0,CVTA                                           V1
         ED    SCR1HDR+PCTOFSET(4),CVTA+6                        V1
         LH    R0,46(,R1)          GET THE PAGING RATE           V1
         CVD   R0,CVTA                                           V1
         ED    SCR1HDR+PGROFSET(4),CVTA+6                        V1
         LH    R0,38(,R1)          GET THE UIC AVERAGE           V1
         CVD   R0,CVTA                                           V1
         ED    SCR1HDR+UICOFSET(4),CVTA+6                        V1
         DROP  R1                                                V1
         L     R5,INTERVAL
         ST    R5,IOCBWAIT
         OI    IOCBFLGD,IOCBDYON
*
         OI    IOCBFLGC,IOCBCRAO+IOCBCFRA
         TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=IOAREA
         PFKEY  DEF=SPAM0,K1=HELP,ATTN=SPAM5,K12=SPAM5
SPAM5    EQU   *                   TERMINATE SPAM
         B     SPAMEX
         EJECT
*.
* SET UP THE ENVIRONMENT TO MAKE THE PERSON OUTSIDE THE TUBE HAPPY..
*
*   . PUT OUT OUR LITTLE ADVERTISEMENT (GIVE ME AN S, ETC..)
*   . CLEAR THE LOCAL COUNTERS
*   . BUILD A WORKING MODEL OF THE SCREEN AND GO WAIT FOR THE FIRST
*     INTERRUPT TO WAKE US UP
*
*.
INISH    EQU   *
         LR    R7,R9               SAVE OVER YELL
         LA    R8,5
INI00    EQU   *
         XR    R5,R5
         IC    R5,MAPS(R8)         GET FIRST CHAR
         STC   R5,YELLAREA+13
*        TRMIO IOCB,WRITE,FORMAT=YELL,IMAGE=YELLAREA
*        BAL   R9,TIMEOUT
         LA    R2,IOAREA
         LA    R3,IOLEN
         SLL   R5,24               PAD TO   BLANKS
         MVCL  R2,R4
*        TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=IOAREA
*        BAL   R9,TIMEOUT
         BCT   R8,INI00
         MVC   YELLAREA,WHATSIT
*        TRMIO IOCB,WRITE,FORMAT=YELL,IMAGE=YELLAREA
*        BAL   R9,TIMEOUT
         LR    R9,R7               FIND WHERE WE CAME FROM
         MVC   SCR1SCAL(9),FIELDNM
         MVC   SCR1SCA2(9),FIELDNM
         MVC   SCR1SCAL+9(69),SCALE
         MVC   SCR1SCA2+9(69),SCALE
         MVC   SCR1MSG(L'ENTERMSG),ENTERMSG
         MVC   SCR1HDR(HDR2LEN+5),HDR2 GET THE WHOLE THING       V1
         LA    R2,NAMETBL          MOVE NAMES TO FRONT OF LINE
         LA    R3,SCR1LNE0
         LA    R8,13               THIS MANY NAMES
INI0     EQU   *                   NAME MOVE LOOP
         MVC   0(9,R3),0(R2)        MOVE THE NAME
         LA    R2,9(,R2)           INCREMENT COUNTERS
         LA    R3,79(,R3)           ...
         BCT   R8,INI0
         LA    R8,100              SET INITIAL TIME
         ST    R8,INTERVAL          INTERVAL
         XC    PCOUNTRS(4*13),PCOUNTRS   CLEAROLD VALUES
         TIME  BIN
         ST    R0,PTIME            SAVE STARTING TIME
         BR    R9
         SPACE 4
*.
* ENTER HERE TO RE-WRITE THE SCREEN WITH A BEEP.
*
* NOTE THE MODE OF WRITING OS DEFAULT CONVERSATIONAL SO THE USER
* MUST HIT ANOTHER KEY TO GET THE GAME GOING AGAIN.
*.
         SPACE
WRITE    EQU   *
*        OI    IOCBFLGC,IOCBCRAO+IOCBCFRA
         TRMIO IOCB,FORMAT=SCR1,IMAGE=IOAREA,BEEP=YES
         BR    R9
         SPACE 4
TIMEOUT  EQU   *
         STIMER WAIT,BINTVL==F'050'
         BR    R9
         SPACE 4
HELP     EQU   *
         OI    IOCBFLGC,IOCBCTIM    DO NOT TIMESTAMP SCREENS
         TRMIO IOCB,FORMAT=HELPFMT,IMAGE=HELPAREA,BEEP=YES
         NI    IOCBFLGC,255-IOCBCTIM DONOT TIMESTAMP SCREENS
         BAL   R9,WRITE            REWRITE
         B     SPAM0
         SPACE 4
*.
* ALLOW THE USER TO SPECIFY THE SCREEN UPDATE TIME. SINCE I BELIEVE IN
* CHEAP PROGRAMMING FORCE THE USER TO SPECIFY ALL FOUR DIGITS.
*
* THIS ROUTINE UPDATES THE INTERVAL=NN.NN ON THE TOP LINE OF THE SCREEN
*.
         SPACE
CINTERVL EQU   *                   CHANGE UTME
         MVC   SCR1MSG,BLANKS
         MVC   SCR1MSG(L'CIMSTXT),CIMSTXT
         BAL   R9,WRITE            WRITE THE SCREEN
CI1      EQU   *
         LA    R8,4                VALIDATE INPUT
         LA    R1,SCR1MSG+24       ...
CI2      EQU   *                   ...
         CLI   0(R1),C'0'          ...
         BL    CIERR               ...
         CLI   0(R1),C'9'          ...
         BH    CIERR               ...
         LA    R1,1(,R1)           ...
         BCT   R8,CI2              ...
         PACK  CVTA(8),SCR1MSG+24(4) O.K. UP TILL NOW
         CVB   R1,CVTA             GET IT IN USEABLE FMT
         LTR   R1,R1               WAS IT ZERO ?
         BNZ   CI3                 NO, OK...
         LA    R1,1                OTHERWISE ( FOR AVERAGING SAKE )
CI3      ST    R1,INTERVAL         SAVE IT
         MVC   SCR1HDR+INTOFSET(2),SCR1MSG+24                    V1
         MVC   SCR1HDR+INTOFSET+3(2),SCR1MSG+26                  V1
         MVC   SCR1MSG(L'ENTERMSG),ENTERMSG    RESET
         MVC   SCR1LNEG,BLANKS      ALL OUR DAMAGE
         B     SPAM0               START ALL OVER AGAIN
         SPACE 2
CIERR    EQU   *
         MVC   SCR1LNEG(L'CIERTXT),CIERTXT
         MVC   SCR1LNEG+33(4),SCR1MSG+24  WRITE BAD INPUT OUT
         MVC   SCR1MSG+24(4),BLANKS
         BAL   R9,WRITE            G I G O - DONT YOU KNOW ?
         B     CI1                 SEE HOW HE DID
         SPACE 2
CIERTXT  DC    C'>>> CORRECT FORMAT IS SSTH, NOT ''    '', SECONDS TENTH
               HS HUNDREDTHS'
CIMSTXT  DC    C'ENTER INTERVAL AS SSTH=>'
         TITLE ' S P A M - DATA  AREAS, DSECTS AND THE LIKE'
         SPACE 3
STARMOVE MVC   0(0,R7),STARS       <<< EXECUTED >>>
STRIPEM  MVC   0(0,R7),STRIPES     <<< EXECUTED >>>
         SPACE
STARS    DC    70C'*'              BICENTENNIAL
STRIPES  DC    70C'>'                 MADNESS
ENTERMSG DC    C'PRESS <PA1> TO STOP, <PF1> FOR HELP, <PF2> TO CHANGE UP
               PDATE TIME'                                       V1
HDR2     DC    C' PAGERATE='                                     V1
HDRPAGER DC    X'40202120'         PAGE RATE                     V1
         DC    C' UIC='                                          V1
HDRUIC   DC    X'40202120'         SYSTEM UIC                    V1
         DC    CL9' '              FILLER                        V1
         DC    C'<< SPAM >>'                                     V1
         DC    CL8' '                                            V1
         DC    C'CPU%='                                          V1
HDRPCT   DC    X'402021204040'     SRM CALCULATED CPU USAGE      V1
         DC    C'         '                                      V1
HDR2LEN  EQU   *-HDR2              LENGTH OF AN AFTER-THOUGHT    V1
HDRINTVL DC    C'01.00'            DEFAULT INTERVAL              V1
UICOFSET EQU   HDRUIC-HDR2         OFFSET FOR UIC VARIABLE       V1
PGROFSET EQU   HDRPAGER-HDR2       OFFSET FOR PAGERATE           V1
PCTOFSET EQU   HDRPCT-HDR2                                       V1
INTOFSET EQU   HDRINTVL-HDR2       OFFSET FOR INTERVAL           V1
MORE     DC    CL10' '
OVERFLOW DC    7CL10'*OVERFLOW*'
FLIPPED  DC    7CL10'*FLIPOVER*'
FIELDNM  DC    CL9'--NAME--×'
NAMETBL  DC    CL9'PVTNPIN ×'
         DC    CL9'PVTNPOUT×'
         DC    CL9'PVTVAMI ×'
         DC    CL9'PVTVAMO ×'
         DC    CL9'PVTVAMR ×'
         DC    CL9'PVTSPIN ×'
         DC    CL9'PVTSPOUT×'
         DC    CL9'PVTNPREC×'
         DC    CL9'PVTNSWPS×'
         DC    CL9'PVTCAIN ×'
         DC    CL9'PVTCAOUT×'
         DC    CL9'PVTCAREC×'
         DC    CL9'PVTSPREC×'
         DC    CL9'PVTCFMCT×'
         DC    CL9'THE END ×'
F69      DC    F'69'
         SPACE 4
YELL     SFMT
         FIELD 79
         SFEND
YELLAREA DC    CL79' GIVE ME AN     '
MAPS     DC    C'  MAPS'
WHATSIT  DC    CL79'  - WHAT DO YOU GOT  ?????'
         SPACE 4
HELPFMT  SFMT
HELPAREA FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         SFEND
HELPAREA EQU   *
 DC CL79'                              <<  S P A M  >>               '
 DC CL79'                      SCOPE PAGING ACTIVITY MONITOR '
 DC 79C'_'                                                           '
 DC CL79' PVTNPIN  - # OF PGS PAGED IN, EXCLUDING SWAP-INS AND VIO   '
 DC CL79' PVTNPOUT - # OF PGS PAGED OUT, EXCLUDING SWAP-OUTS AND VIO '
 DC CL79' PVTVAMI  - NUMBER OF VIO PAGE INS, EXCLUDING SWAP          '
 DC CL79' PVTVAMO  - NUMBER OF VIO PAGE OUTS, EXCLUDING SWAP         '
 DC CL79' PVTVAMR  - NUMBER OF VIO RECLAIMS                          '
 DC CL79' PVTSPIN  - NUMBER OF PAGES SWAPPED IN                      '
 DC CL79' PVTSPOUT - NUMBER OF PAGES SWAPPED OUT                     '
 DC CL79' PVTNPREC - NUMBER OF PAGES RECLAIMED                       '
 DC CL79' PVTNSWPS - NUMBER OF SUCCESSFUL SWAPS                      '
 DC CL79' PVTCAIN  - NUMBER OF NON-VIO PAGE-INS                      '
 DC CL79' PVTCAOUT - NUMBER OF COMMON AREA NON-VIO PAGE-OUTS         '
 DC CL79' PVTCAREC - # OF RECLAIMS OF COMMON AREA NON-VIO PAGE-OUTS  '
 DC CL79' PVTSPREC - NUMBER OF PRIAVTE AREA PAGEABLE PAGES RECLAIMED '
 DC CL79'            ACROSS A SWAP                                   '
 DC CL79' '
 DC CL79' PAGERATE, UIC AND CPU PERCENTAGE FIELDS ARE ALL EXTRACTED FROM
               M THE RCT'
 DC CL79' MAINTAINED BY THE DREADED SRM.'
 DC 79C'_'                                                           '
         SPACE 4
SCR1     SFMT
SCR1HDR  FIELD 79
SCR1MSG  FIELD 79,ALPHA,INTEN=HI,CURSOR=AFTER
SCR1SCAL FIELD 79
SCR1LNE0 FIELD 79,INTEN=HI
SCR1LNE1 FIELD 79,INTEN=LO
SCR1LNE2 FIELD 79,INTEN=HI
SCR1LNE3 FIELD 79,INTEN=LO
SCR1LNE4 FIELD 79,INTEN=HI
SCR1LNE5 FIELD 79,INTEN=LO
SCR1LNE6 FIELD 79,INTEN=HI
SCR1LNE7 FIELD 79,INTEN=LO
SCR1LNE8 FIELD 79,INTEN=HI
SCR1LNE9 FIELD 79,INTEN=LO
SCR1LNEA FIELD 79,INTEN=HI
SCR1LNEB FIELD 79,INTEN=LO
SCR1LNEC FIELD 79,INTEN=HI
SCR1SCA2 FIELD 79,INTEN=LO
SCR1LNED FIELD 79,INTEN=HI
SCR1LNEE FIELD 79,INTEN=HI
SCR1LNEG FIELD 79,INTEN=HI
SCR1LNEH FIELD 79,INTEN=HI
SCR1LNEI FIELD 79,INTEN=HI
*CR1LNEJ FIELD 79,INTEN=HI
         SFEND
SCALE    DC    CL70'0....+....1....+....2....+....3....+....4....+....5X
               ....+....6....+...9'
SPAMEX   DCMSTOP SCREEN=NO
IOAREA   EQU   *
         DSGEN SCR1
IOLEN    EQU   *-IOAREA
         SPACE 2
**
**             EVENT COUNTERS FOR SMF AND TUNING PURPOSES
**
**          ALL FIELDS EXCEPT XVTCFMCT ARE WRAP-AROUND COUNTS.
**
**  I APOLIGIZE.. THESE VARIABLES ARE PRE-SU7, HOWEVER THE OUTPUT
**  IS INDEED POST-SU7.
**
**
         SPACE
XVTNPIN  DC    F'0' -          NUMBER OF PAGES PAGED IN, EXCLUDING
*                              SWAP-INS AND VIO PAGE-INS
XVTNPOUT DC    F'0' -          NUMBER OF PAGES PAGED OUT, EXCLUDING
*                              SWAP-OUTS AND VIO PAGE-INS
XVTVAMI  DC    F'0' -          NUMBER OF VIO PAGE-INS, EXCLUDING SWAP
XVTVAMO  DC    F'0' -          NUMBER OF VIO PAGE-OUTS, EXCLUDING SWAP
XVTVAMR  DC    F'0' -          NUMBER OF VIO RECLAIMS
XVTSPIN  DC    F'0' -          NUMBER OF PAGES SWAPPED IN
XVTSPOUT DC    F'0' -          NUMBER OF PAGES SWAPPED OUT
XVTNPREC DC    F'0' -          NUMBER OF PAGES RECLAIMED
XVTNSWPS DC    F'0' -          NUMBER OF SUCCESSFUL SWAP-INS
XVTCAIN  DC    F'0' -          NUMBER OF NON-VIO PAGE-INS
XVTCAOUT DC    F'0' -          NUMBER OF COMMON AREA NON-VIO PAGE-OUTS
XVTCAREC DC    F'0' -          NUMBER OF RECLAIMS OF COMMON AREA
*                              NON-VIO PAGES
XVTCAVI  DC    F'0' -          NUMBER OF COMMON AREA VIO PAGE-INS
XVTCAVO  DC    F'0' -          NUMBER OF COMMON AREA VIO PAGE-OUTS
XVTCAVR  DC    F'0' -          NUMBER OF COMMON AREA VIO RECLAIMS
XVTCFMCT DC    H'0' -          NUMBER OF FRAMES CURRENTLY ASSIGNED TO
*                              PAGEABLE COMMON AREAS (CSA AND LPA)
XVTSPFR  DC    H'0' -          NUMBER OF FRAMES FREED BY SWAP-OUT
*                              WITHOUT OUTPUT I/O.
**
PCOUNTRS DC    13F'0'              PREVIOUS COUNTER VALUES
PTIME    DC    F'0'                PREVIOS TIME OF DAY
ATIME    DC    F'0'                ACTUAL ELAPSED TIME
INTERVAL DC    F'100'
CVTA     DC    D'0'
         TITLE 'S P A M  - MVS CVT DSECT DEFINITION'
         CVT   DSECT=YES
         TITLE 'S P A M  - DCMS TERMINAL DCB DEFINITION'
         TDCB
         TITLE 'S P A M  - MVS PVT DSECT DEFINITION'
         IHAPVT
         END
