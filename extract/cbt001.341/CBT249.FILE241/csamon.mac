CSAMON    TITLE  'DCMS   C S A    UTILIZATION  DISPLAY'
***********************************************************************
*
*        THIS DCMS COMMAND WILL DISPLAY THE CONTENTS OF CSA
*        (COMMON STORAGE/SERVICE AREA) IN AN MVS SYSTEM.
*        ACTUAL SIZE IN BYTES, ADDRESS, AND SIZE IN 'K' BYTES
*        ARE DISPLAYED TO THE DCMS USER.
*
***********************************************************************
CSAMON   DCMSTART R,LV=4096
OUT1     #STAK GET,FRAME=17,SIZE=30,ENTRY=30
OUT2     #STAK GET,FRAME=17,SIZE=30,ENTRY=30
OUT3     #STAK GET,FRAME=17,SIZE=300,ENTRY=30
         MVC   TOPLINE1,HDGMSG
         MVC   TOPLINE2,BLANKS
         MVC   TOPLINE3,BLANKS
         L     R2,16             GET CVT POINTER
         L     R2,560(R2)        GET GDA POINTER
         L     R2,8(R2)          GET PQE POINTER
         L     R3,4(R2)          LAST FBQE POINTER
         L     R4,0(R2)          FIRST FBQE POINTER
         SR    R8,R8
         LR    R9,R8
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
CSANEXT  L     R5,8(R4)       SIZE OF FREE BLOCK
         LR    R11,R5
         AR    R9,R5
***********************************************************************
*
*        CONVERT THE SIZE OF EACH FREE CHUNK FOR TPUT
*
***********************************************************************
         ST    R5,DW          SIZE TO WORK AREA
         UNPK  CSAMSG(7),DW+1(4) UNPACK FOR TRANSLATE
         MVI   CSAMSG+6,C' '  CLEAR EXTRA CHARACTER
         TR    CSAMSG(6),TRTABLE-240 TRANSLATE FOR PRINT
***********************************************************************
*
*        CONVERT THE SIZE TO 'K' BYTES FOR TPUT MSG
*
***********************************************************************
         SRL   R5,10          DIVIDE BY 1024
         CVD   R5,DW          CONVERT TO DECIMAL
         MVC   CSAMSG+18(6),CSASIZ  MOVE EDIT MASK TO MSG
         ED    CSAMSG+18(6),DW+5 EDIT CSA MESSAGE FIELD
         MVI   CSAMSG+19,C'('
***********************************************************************
*
*        CONVERT THE ADDRESS TO PRINTABLE FOR TPUT
*
***********************************************************************
         L     R15,12(R4)     GET ADDRESS OF STORAGE
         AR    R11,R15    GET END ADDR
         CVD   R15,DWD
         OI    DWD+7,X'0F'
         UNPK  FREESTRT,DWD+3(5)
         CVD   R11,DWD
         OI    DWD+7,X'0F'
         UNPK  FREEND,DWD+3(5)
         ST    R15,DW         ADDRESS TO STORAGE
        #PUT   OUT3,FREEMSG
         UNPK  CSAMSG+10(7),DW+1(4) UNPACK FOR TRANSLATE
         MVI   CSAMSG+16,C' '  CLEAR EXTRA CHARACTER
         TR    CSAMSG+10(6),TRTABLE-240 TRANSLATE FOR PRINT
***********************************************************************
*
*        TPUT MSG TO USER AND CHECK FOR END OF CHAIN
*
***********************************************************************
         #PUT  OUT1,CSAMSG    WRITE TO TERMINAL
         CR    R4,R3          LAST FBQE?
         BE    CSAUSED        YES, NEXT SECTION
         L     R4,0(R4)       BUMP TO NEXT FBQE
         B     CSANEXT        PROCESS NEXT FBQE
CSAUSED  D     R8,=F'1024'
         CVD   R9,DWD
         MVC   TOTAMT,=X'402020202021'
         ED    TOTAMT,DWD+5
         MVC   TOTMSG+9(9),=CL9'FREE'
         L     R1,16              CVT
         L     R7,X'230'(R1)      GDA
         L     R2,8(R7)           CSA PQE
         L     R5,20(R2)          CSA SIZE
         SR    R4,R4
         D     R4,=F'1024'
         CVD   R5,DWD
         ED    CSASIZ,DWD+5
         UNPK  CSASTWK(9),24(5,R2)   CSA START ADDR
         MVC   CSAST,CSASTWK
         L     R2,52(R7)               CSA SPQE
         SR    R3,R3
         IC    R3,11(R2)                PROTECT KEY
         SRL   R3,4
         CVD   R3,DWD
         UNPK  PK,DWD+6(2)
         OI    PK+2,X'F0'
         IC    R3,10(R2)
         CVD   R3,DWD
         UNPK  SUBPOOL,DWD+6(2)
         OI    SUBPOOL+2,X'F0'
         SR    R8,R8
         LR    R9,R8
BAL4SIZE L     R3,4(R2)                CSA DQE
         LTR   R3,R3
         BZ    NEXTSPQE
         SR    R5,R5
         LR    R4,R5
         BAL   R6,GETSIZE2
NEXTSPQE L     R2,0(R2)
         LTR   R2,R2
         BZ    SQAUSED
         SR    R3,R3
         IC    R3,11(R2)                PROTECT KEY
         SRL   R3,4
         CVD   R3,DWD
         UNPK  PK,DWD+6(2)
         OI    PK+2,X'F0'
         IC    R3,10(R2)
         CVD   R3,DWD
         UNPK  SUBPOOL,DWD+6(2)
         OI    SUBPOOL+2,X'F0'
         B     BAL4SIZE
SQAUSED  L     R2,24(R7)               SQA SPQE
         MVC   ALLOCEND+10(4),=CL4'SQA'
         SR    R3,R3
         IC    R3,10(R2)
         CVD   R3,DWD
         UNPK  SUBPOOL,DWD+6(2)
         OI    SUBPOOL+2,X'F0'
         MVC   PK,=CL3'SQA'
BAL4SQA  L     R3,4(R2)                SQA DQE
SQASW1   NOP   SQA1
         MVI   SQASW1+1,X'F0'
         L     R3,4(R3)     SKIP FIRST DQE FOR SQA
SQA1     LTR   R3,R3
         BZ    NEXTSQAE
         SR    R5,R5
         LR    R4,R5
         BAL   R6,GETSIZE2
NEXTSQAE L     R2,0(R2)
         LTR   R2,R2
         BZ    TOTALUSD
         SR    R3,R3
         IC    R3,10(R2)
         CVD   R3,DWD
         UNPK  SUBPOOL,DWD+6(2)
         OI    SUBPOOL+2,X'F0'
         B     BAL4SQA
TOTALUSD D     R8,=F'1024'
         MVC   TOPLINE2(L'CSASTMSG),CSASTMSG
         MVC   TOPLINE3(L'CSASZMSG),CSASZMSG
         MVC   TOPLINE2+48(L'TOTMSG),TOTMSG
         CVD   R9,DWD
         MVC   TOTAMT,=X'402020202021'
         ED    TOTAMT,DWD+5
         MVC   TOTMSG+9(9),=C'ALLOCATED'
         MVC   TOPLINE3+48(L'TOTMSG),TOTMSG
*
         B     SHOWN
GETSIZE  L     R3,4(R3)
GETSIZE1 LTR   R3,R3
         BZ    PKDONE
GETSIZE2 A     R5,12(R3)            DQE SIZE
         L     R11,8(R3)  GET START ADDR
         CVD   R11,DWD
         OI    DWD+7,X'0F'
         UNPK  ALLOCST,DWD+3(5)
         A     R11,12(R3)
         CVD   R11,DWD
         OI    DWD+7,X'0F'
         UNPK  ALLOCEND,DWD+3(5)
        #PUT   OUT3,ALLOCMSG
         B     GETSIZE
PKDONE   AR    R9,R5             ADD TO TOTAL USED
         D     R4,=F'1024'
         CVD   R5,DWD
         MVC   CSAHELD,=X'402020202021'
         ED    CSAHELD,DWD+5    #PUT LINE FOR THIS SUBPOOL
         #PUT   OUT2,CSADET
         BR    R6
         EJECT
SHOWN    EQU   *
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         MVC   SCREEN(237),TOPLINE1
         #FRAME FORWARD,OUT2,L6
         MVC   L5(L'CSAPKHDG),CSAPKHDG
         MVC   L5+39(L'HDXMSG),HDXMSG
         #HFRAME FORWARD,OUT1,L6+39
*
SHOWST   EQU   *
         OI    IOCBFLGC,IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY  DEF=SHOWST,K9=DCMSOVER,K3=DETAIL,K12=FINISHD,          X
               ENTER=SHOWN
DETAIL   DS    0H
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         #FRAME FORWARD,OUT3,L5
         MVC    L4(L'HDXMSG),HDXMSG
DSHOW    EQU   *
         OI    IOCBFLGC,IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY  DEF=SHOWN,K9=DCMSOVER,K3=SHOWN,K12=FINISHD,K8=DETAIL,  X
               K7=BACKDET
         B     DCMSOVER
BACKDET  EQU   *
         #FRAME BACKWARD,OUT3,L5
         B     DSHOW
         EJECT
CSASZMSG DS    0CL25
         DC    CL17'CSA SIZE '
CSASIZ   DC    X'402020202021',C' K'
CSASTMSG DS    0CL25
         DC    C'CSA START ADDR   '
CSAST    DC    CL8' '
CSAPKHDG DC    C'  P-KEY   SP     ALLOCATED              '
CSADET   DS    0CL26
         DC    C'   '
PK       DC    CL3' ',CL4' '
SUBPOOL  DC    CL3' ',CL5' '
CSAHELD  DC    CL7' ',C'K'
         DC    CL10' '
TOTMSG   DS    0CL25
         DC    CL17'TOTAL'
TOTAMT   DC    CL6' ',C' K'
HDGMSG   DS    0CL79
         ZB  'C S A    U S A G E'
HDXMSG   DC    CL30'    CSA FREE BLOCKS'
DWD      DS    D
CSASTWK  DC    CL9' '
FREEMSG  DS   0CL79
FREESTRT DC   CL9' '
         DC   C' - '
FREEND   DC   CL9' '
         DC   CL9' FREE'
         DC   CL50' '
ALLOCMSG DS   0CL79
ALLOCST  DC   CL9' '
         DC   C' - '
ALLOCEND DC   CL9' '
         DC   CL9' USED'
         DC   CL50' '
         EJECT
         DS    CL240          PADDING FOR TRANSLATE
TRTABLE  DC    C'0123456789ABCDEF' TRANSLATE TABLE
CSAMSG   DC    CL30'...... AT ......  ......K)' CSA MESSAGE
         DC    CL30' '
CSAMASK  DC    X'40202120'    EDIT MASK FOR CSA SIZE
DW       DC    D'0'           DOUBLEWORD WORK AREA
FINISHD  DCMSTOP SCREEN=YES
TOPLINE1 DS    CL79
TOPLINE2 DS    CL79
TOPLINE3 DS    CL79
OUT1     #STAKCB DSECT=NO
OUT2     #STAKCB DSECT=NO
OUT3     #STAKCB DSECT=NO
A1       DS    0H
         DS    50CL79
         END
