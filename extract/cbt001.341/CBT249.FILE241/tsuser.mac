TSUSER   TITLE 'DCMS TSO USER PROFILE DISPLAY'
TSUSER   DCMSTART R,LV=16384,STAE=YES
         MVI   @MODE,X'7F' .
RESTART  EQU   *
         LA    R2,TITUL
         ST    R2,TITULS
         MVI   TITULS,X'00'
         LA    R2,TOTALU
         ST    R2,TOTALUS
         MVI   TOTALUS,X'80'
         LA    R2,TSTABUL
         ST    R2,TOPPTR
         ST    R2,POINTOR
         L     R3,TSTABEND
         AR    R2,R3
         ST    R2,BOTPTR
         MVI   POINTOR,X'00'
         STH   R3,CLRLEN
         LA    R0,CLRLEN
         LA    R1,TSTABUL
         BAL   R14,@CLEAR
         L     R2,BOTPTR
         LA    R2,79(R2)
         MVC   0(79,R2),ENDOFTAB
*
STARTUP  EQU   *
         L     R2,TOPPTR           INIT
         ST    R2,NOWPTR               TABLE
         LA    R1,POINTOR                   POINTERS
         #SETEST SECALLS
         SR    R0,R0
         CALL  TSARRAY
         B     #COMMON
SECALLS  SR    R0,R0               AND
         MVI   MAUTO+5,C'>'
         MVI   MMANU+5,C'>'
         CALL  TEARRAY
#COMMON  SR    R14,R14              ADJUST
         L     R15,USRCOUNT               COUNTOR
         SR    R1,R1                             TO
         LA    R1,20                               NUMBER
         DR    R14,R1                                    OF
         LTR   R14,R14                                     SCREENS
         BZ    *+8
         LA    R15,1(R15)
         ST    R15,COUNTOR
         SR    R8,R8
*
*        TABLE  BUILT FOR THIS PASS  AT THIS POINT
*
PUTPAGE  EQU   *
         LA    R8,1(R8) .
         LA    R0,TUBE
         LA    R1,SCREEN
         BAL   R14,@CLEAR
         BAL   R14,GETPAGE
REDOIT   EQU   *
         OI    IOCBFLGD,IOCBDYON
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO
         CLI   @MODE,X'7F'
         BNE   TERMANUL
TERMAUTO EQU   *
         MVC   SCREEN+140(14),MAUTO
         TRMIO IOCB,WRITE,FORMAT=TUBE,IMAGE=SCREEN
         B     IOAFTER
TERMANUL EQU   *
         MVC   SCREEN+140(14),MMANU
         TRMIO IOCB,FORMAT=TUBE,IMAGE=SCREEN
IOAFTER  EQU   *
         PFKEY ENTER=PFK8,ATTN=ATTN,DEF=ENTER,K3=XMODE,                X
               K8=PFK8,K7=PFK7,K12=ATTN,K9=RESTART
XMODE    EQU   *
         CLI   @MODE,X'7F'
         BNE   MODEOFF
MODEON   EQU   *
         MVI   @MODE,X'0F'
         B     REDOIT
MODEOFF  EQU   *
         MVI   @MODE,X'7F'
         B     REDOIT
*
PFK7     EQU   *
         LA    R1,1580        PAGE
         L     R2,NOWPTR          BACKWARD
         SR    R2,R1                      UNLESS
         C     R2,TOPPTR                        AT
         BL    RESTART                            THE
         ST    R2,NOWPTR                             TOP
         B     PUTPAGE
PFK8     EQU   *
         LA    R1,1580        PAGE
         L     R2,NOWPTR          FOREWARD
         AR    R2,R1                      UNLESS
         C     R2,BOTPTR                        AT
         BH    RESTART                            BOTTOM:
         C     R8,COUNTOR         OR
         BNL   RESTART              RESTART
         ST    R2,NOWPTR                   DISPLAY
         B     PUTPAGE                            AT TOP
GETPAGE  DS    0H
*
*        MVCL  ROUTINE TO INIT A PAGE
*
*
         MVC   SCREEN(79),TITUL         TITLE AT TOP
         MVC   SCREEN+79(79),TOTALU     CURRENT USER TOTAL LINE
*
         MVC   SCREEN+131(8),=CL8'FRAME NN'
         ST    R8,DECWORK1
         LR    R3,R14
         BAL   R14,@DECIMAL
         MVC   SCREEN+137(2),DECWORK4+2
         LR    R14,R3
         LA    R2,SCREEN+158            TWENTY USERS
         LA    R3,1580                  ON LINES
         LR    R5,R3                    TWO   THRU
         L     R4,NOWPTR                TWENTY-ONE
         ICM   R5,8,=C' '
         MVCL  R2,R4                    DOIT
         BR    R14
MAUTO    DC    CL14'MODE = AUTO'
MMANU    DC    CL14'MODE = MANUAL'
TUBE     SFMT
SCREEN   FIELD 79
TUBEUSER FIELD 79,INTEN=LO,CURSOR=AFTER
TUBEDATA FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         SFEND
ENDOFTAB DC    10CL10'*END*DCMS*'
TSTABEND DC    A(120*79)           DATA AREA LIST LENGTH
ATTN     DCMSTOP  RC=(15)
COUNTOR  DS    F
POINTOR  DS    A
TITULS   DS    A
TOTALUS  DS    A
TITUL    DS    CL80
TOTALU   DS    CL80
USRCOUNT DS    F
         DS    2F
TOPPTR   DS    F
NOWPTR   DS    F
BOTPTR   DS    F
CLRLEN   DS    H
@MODE    DS    X .
         DSGEN TUBE
TSTABUL  DS    0F
         DS    150CL79             DATA AREA FOR USER LIST
         DS    CL79
LASTENT  DS    CL79
         EJECT
         CVT   DSECT=YES
         END
