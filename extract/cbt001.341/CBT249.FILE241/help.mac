HELP     TITLE 'DCMS - GENERAL HELP PROCESSOR'
         PUNCH ' ALIAS DCMSHELP '
HELP     DCMSTART R,LV=4096,STAE=NO
         L     R2,=A(HELPS00)
STRTHELP EQU   *
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         ST    R2,CURRENT    RECORD CURRENT HELP
         LA    R2,4(R2)            POINT TO
         ST    R2,CURFRAM                 FIRST FRAME
         XC    FRAMENO,FRAMENO
GETFRAME EQU   *
         L     R2,FRAMENO    ADJUST
         LA    R2,1(R2)            FRAME
         ST    R2,FRAMENO               NUMBER
         L     R2,CURFRAM    AND
         L     R2,0(R2)                     (CURRENT MODULE)
         LM    R15,R1,0(R2)     ACQUIRE
         LA    R14,L2                  SCREEN
         C     R15,LIMITS    ENFORCE
         BL    SIZEOK               LIMIT
         L     R15,LIMITS                OF
         LR    R1,R15                      20 LINES
SIZEOK   EQU   *
         MVCL  R14,R0        COPY IMAGE ONTO SCREEN
         MVC   SCREEN(79),TITLE
         L     R3,CURRENT
         L     R3,0(R3)
         MVC   SCREEN+12(56),0(R3)
         L     R3,FRAMENO
         ST    R3,DECWORK1
         BAL   R14,@DECIMAL
         MVC   SCREEN+74(2),DECWORK4+2
SHOWHELP EQU   *
         OI    IOCBFLGC,IOCBCRAO+IOCBCCLR
         OI    IOCBFLGD,IOCBDPK2
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         EJECT
         PFKEY CNCL=DCMSOVER,ATTN=HELPOUT,ENTER=MOVE,DEF=ENTER,        X
               K1=HK1,K2=HK2,K7=HK7,                                   X
               K8=HK8,K9=DCMSOVER,K12=HELPOUT
         EJECT
HK1      L     R2,=A(HELPS01)
         B     STRTHELP
HK2      L     R2,=A(HELPS02)
         B     STRTHELP
HK7      EQU   *
         L     R2,CURFRAM
         L     R1,CURRENT
         CR    R1,R2
         BNL   TOTOPS
         S     R2,=F'4'
         ST    R2,CURFRAM
         L     R2,FRAMENO
         BCTR  R2,0
         ST    R2,FRAMENO
         B     GETFRAME
HK8      EQU   *
         L     R2,CURFRAM
         LA    R2,4(R2)
         CLC   0(4,R2),=F'0'
         BE    TOTOPS
         ST    R2,CURFRAM
         B     GETFRAME
TOTOPS   EQU   *
         L     R2,CURRENT
         LA    R2,4(R2)
         XC    FRAMENO(4),FRAMENO
         ST    R2,CURFRAM
         B     GETFRAME
MOVE     EQU   *
         CLI   IOCBAID,AIDENT
         BNE   HK8
         CLC   INPLINE(8),BLANKS
         BE    HK8
         LA    R9,COMMANDZ
CMDLOOP  EQU   *
         CLI   0(R9),C' '
         BE    CMDMOVE
         CLC   INPLINE(8),0(R9)
         BE    CMDMOVE
         LA    R9,12(R9)
         B     CMDLOOP
CMDMOVE  EQU   *
         L     R2,8(R9)
         B     STRTHELP
         EJECT                                                        *
*                                                                     *
*                                                                     *
*        H E L P   D A T A    T A B L E S   A N D   C O N S T A N T S *
*                                                                     *
*                                                                     *
         SPACE 3
FORMAT   SFMT
SCREEN   FIELD 79
L2       FIELD 79
L3       FIELD 79
L4       FIELD 79
L5       FIELD 79
L6       FIELD 79
L7       FIELD 79
L8       FIELD 79
L9       FIELD 79
L10      FIELD 79
L11      FIELD 79
L12      FIELD 79
L13      FIELD 79
L14      FIELD 79
L15      FIELD 79
L16      FIELD 79
L17      FIELD 79
L18      FIELD 79
L19      FIELD 79
L20      FIELD 79
L21      FIELD 79
L22      FIELD 79
INPLINE  FIELD 79,ALPHA,CURSOR=YES
         SFEND
LIMITS   DC    A(21*79)
TITLE    DS    0CL79
         DC    CL12'DCMS HELP:'
         DC    CL56' '
         DC    CL12'FRAME '
         EJECT
COMMANDZ DS    0H
         DC     CL8'CHANNEL ',V(HELPCM01)
         DC        CL8'SCAMP   ',V(HELPCM01)
         DC     CL8'CHIMP   ',V(HELPCM02)
         DC     CL8'CPU     ',V(HELPCM03)
         DC     CL8'DA      ',V(HELPCM04)
         DC        CL8'DJ      ',V(HELPCM04)
         DC     CL8'DCMSPROF',V(HELPCM05)
         DC        CL8'PROF    ',V(HELPCM05)
         DC     CL8'DIDOCS  ',V(HELPCM06)
         DC     CL8'DMN     ',V(HELPCM08)
         DC     CL8'DU      ',V(HELPCM09)
         DC     CL8'FRAMES  ',V(HELPCM10)
         DC     CL8'JES3    ',V(HELPCM11)
         DC     CL8'LOOK    ',V(HELPCM12)
         DC     CL8'MS      ',V(HELPCM13)
         DC     CL8'PAGE    ',V(HELPCM14)
         DC        CL8'SPAM    ',V(HELPCM14)
         DC     CL8'POUND   ',V(HELPCM15)
         DC     CL8'SRM     ',V(HELPCM16)
         DC     CL8'SWAP    ',V(HELPCM17)
         DC     CL8'TIME    ',V(HELPCM18)
         DC     CL8'TSUSER  ',V(HELPCM19)
         DC     CL8'WIZARD  ',V(HELPCM20)
         DC     CL8'        ',V(HELPCM00)  ***END OF TABLE***
         EJECT
HELPOUT  DCMSTOP RC=(15),SCREEN=NO
         DSGEN FORMAT
CURRENT  DS    F
CURFRAM  DS    F
FRAMENO  DS    F
         EJECT
         HELPDEF NAME=S00,TITLE=' < D C M S *** H E L P  > ',F1=#000
         HELPDEF NAME=S01,TITLE=' < D C M S *** H E L P  > ',F1=#001
         HELPDEF NAME=S02,TITLE=' < D C M S *** H E L P  > ',F1=#002
         HELPDEF NAME=CM00,TITLE=' <   UNKNOWN  COMMAND   > ',F1=#C00
         HELPDEF NAME=CM01,TITLE=' < THE CHANNEL  COMMAND > ',F1=#C01
         HELPDEF NAME=CM02,TITLE=' < THE CHIMP    COMMAND > ',F1=#C02, X
               F2=#C03,F3=#C04,F4=#C05
         HELPDEF NAME=CM03,TITLE=' < THE CPU      COMMAND > ',F1=#C06, X
               F2=#C07,F3=#C08
         HELPDEF NAME=CM04,TITLE=' < THE DA       COMMAND > ',F1=#C00
         HELPDEF NAME=CM05,TITLE=' < THE DCMSPROF COMMAND > ',F1=#C00
         HELPDEF NAME=CM06,TITLE=' < THE DIDOCS   COMMAND > ',F1=#C00
         HELPDEF NAME=CM07,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM08,TITLE=' < THE DMN      COMMAND > ',F1=#C00
         HELPDEF NAME=CM09,TITLE=' < THE DU       COMMAND > ',F1=#C09
         HELPDEF NAME=CM10,TITLE=' < THE FRAMES   COMMAND > ',F1=#C00
         HELPDEF NAME=CM11,TITLE=' < THE JES3     COMMAND > ',F1=#C00
         HELPDEF NAME=CM12,TITLE=' < THE LOOK     COMMAND > ',F1=#C00
         HELPDEF NAME=CM13,TITLE=' < THE MS       COMMAND > ',F1=#C13
         HELPDEF NAME=CM14,TITLE=' < THE PAGE     COMMAND > ',F1=#C10, X
               F2=#C11
         HELPDEF NAME=CM15,TITLE=' < THE POUND    COMMAND > ',F1=#C00
         HELPDEF NAME=CM16,TITLE=' < THE SRM      COMMAND > ',F1=#C00
         HELPDEF NAME=CM17,TITLE=' < THE SWAP     COMMAND > ',F1=#C00
         HELPDEF NAME=CM18,TITLE=' < THE TIME     COMMAND > ',F1=#C00
         HELPDEF NAME=CM19,TITLE=' < THE TSUSER   COMMAND > ',F1=#C00
         HELPDEF NAME=CM20,TITLE=' < THE WIZARD   COMMAND > ',F1=#C00
         HELPDEF NAME=CM21,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM22,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM23,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM24,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM25,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM26,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         HELPDEF NAME=CM27,TITLE=' < THE ???????? COMMAND > ',F1=#C00
         END
