TSARRAY  TITLE 'TSO USER ARRAY BUILD ROUTINE'
         TSDATA
         EJECT
TEARRAY  DCMSTART R,LV=4096
         L     R10,0(R1)           ADDRESS OF TABLE
         USING TSDATA,R10
         TM    0(R1),X'80'         IF SECOND
         BO    NOTITLE                PARM FIELD
         L     R14,4(R1)                   PASS OUT
         MVC   0(79,R14),TITLE                  THE TITLE
NOTITLE  EQU   *
         L     R3,16
         USING CVT,R3
         L     R4,CVTASVT
         USING ASVT,R4
         LA    R9,ASVTFRST
         L     R6,CVTOPCTP
         USING IRARMCNS,R6
         XC    CTRUSE(4),CTRUSE
         L    R2,CVTTCBP
         L    R2,8(R2)
         LA   R2,0(R2)
         ST   R2,NEXTGUY
         DROP  R4
ASCBSCAN EQU   *
         LA    R9,4(R9)            MOVE TO NEXT ASCB SLOT
         CLC   0(4,R9),=XL4'80000000'
         BE    ASCBEXIT
         TM    0(R9),X'80'
         BO    ASCBSCAN
         L     R8,0(R9)
         LA    R8,0(R8)
         USING ASCB,R8
         CLC   ASCBASCB(4),NAMEASCB
         BNE   ASCBSCAN
         L     R7,ASCBOUCB
         USING OUCB,R7
         CLC   OUCBNAME(4),NAMEOUCB
         BNE   ASCBSCAN
         TM    OUCBYFL,OUCBLOG
         BNO   ASCBSCAN
         EJECT
*                                                                     *
*                                                                     *
*        F O R M A T    U S E R     L I N E                           *
*                                                                     *
*                                                                     *
         SPACE 3
         L     R2,CTRUSE
         LA    R2,1(R2)
         ST    R2,CTRUSE
         MVI   0(R10),C' '         CLEAR NEXT LINE IN BUFFER
         MVC   1(79,R10),0(R10)          TO ALL SPACES
         L     R2,ASCBJBNS         COPY
         MVC   TSUSER(7),0(R2)          JOBNAME
         L     R2,ASCBTSB                       FOR
         MVC   TSLINE,=C'???'                       USERID
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   NOAUTHOR
         MODESET KEY=ZERO
         USING TSB,R2
         MVC   TSBWORK1(2),TSBLINE
         MVC   TSBWORK2(8),TSBPSWD
         DROP  R2
         MODESET KEY=NZERO
         LH    R2,TSBWORK1
         ST    R2,HEXWORK1
         BAL   R14,@HEX
         MVC   TSLINE(3),HEXWORK2+5
         SPACE 2
NOAUTHOR EQU   *
         L     R2,@@LSW
         TM    OUCBQFL,OUCBLSW
         BO    LOCSTORE
*        LOGICAL SWAP
*                    DETECT ADDED FOR MVS/SE (SU50)
         L     R2,@@HUH
         TM    OUCBSFL,OUCBINV
         BO    LOCSTORE
         L     R2,@@TOUT
         TM    OUCBQFL,OUCBGOO
         BO    LOCSTORE
         L     R2,@@TIN
         TM    OUCBQFL,OUCBGOI
         BO    LOCSTORE
         L     R2,@@TRAN
         TM    OUCBQFL,OUCBGOB
         BO    LOCSTORE
         TM    OUCBEFL,OUCBTRM
         BO    LOCSTORE
         L     R2,@@OWT
         TM    OUCBEFL,OUCBOWT
         BO    LOCSTORE
         L     R2,@@NSW
         TM    OUCBAFL,OUCBNWT
         BO    LOCSTORE
         L     R2,@@NAS
         TM    OUCBSFL,OUCBNSW
         BO    LOCSTORE
         TM    ASCBFLG1,ASCBNSWP
         BO    LOCSTORE
         L     R2,@@DWT
         TM    OUCBEFL,OUCBMWT
         BO    LOCSTORE
         L     R2,@@LWT
         TM    OUCBEFL,OUCBLWT
         BO    LOCSTORE
         L     R2,@@RDY
         TM    OUCBCFL,OUCBRDY
         BO    LOCSTORE
         L     R2,@@RUN
         C     R8,NEXTGUY        ?????CURRENT TASK???????
         BE    LOCSTORE
         L     R2,@@IN
LOCSTORE EQU   *
         ST    R2,TSLOC
         LH    R2,ASCBASID
         ST    R2,HEXWORK1
         BAL   R14,@HEX
         MVC   TSASID(2),HEXWORK2+6
         LH    R2,ASCBSEQN
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSDQUE,DECWORK4+2
         SR    R2,R2
         IC    R2,OUCBNPG
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSPGP(2),DECWORK4+2
         TM    OUCBTFL,OUCBPCH
         BNO   DOPGP
         MVI   TSPGP+3,C'/'
         B     ENDPGP
DOPGP    EQU   *
*
         LA    R0,WPGDPGP-WPGD      BASIS OF WPGD
         LA    R1,WPGPLEN           LENGTH OF ONE ENTRY
         SR    R15,R15
         IC    R15,OUCBPGP
         LTR   R15,R15              IF ZERO
         BZ    *+6
         SR    R15,R0               SUBTRACT BASIS
         AR    R15,R1               ADJUST FOR BASE 1
         SR    R14,R14              CLEANUP
         DR    R14,R1               DIVIDE FOR
*        STC   R15,TSPGP+3          PERFORMANCE GROUP PERIOD
*        OI    TSPGP+3,X'F0'        FORMAT IT
         ST    R15,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSPGP+3(1),DECWORK4+3
ENDPGP   EQU   *
*
         SR    R2,R2
         IC    R2,OUCBDMN
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSDMN(2),DECWORK4+2
         SR    R2,R2
         IC    R2,ASCBDP
         ST    R2,HEXWORK1
         BAL   R14,@HEX
         MVC   TSDPRTY,HEXWORK2+6
         LH    R2,OUCBPSO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSOSET(4),DECWORK4
         LH    R2,OUCBWSS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSISET(4),DECWORK4
         LH    R2,ASCBFMCT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSREAL(4),DECWORK4
         L     R2,OUCBCPU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSCPUS(4),DECWORK4
         L     R2,OUCBIOC
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSIOS(4),DECWORK4
         L     R2,OUCBMSO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSMSOS(4),DECWORK4
         L     R2,ASCBOUXB
         LH    R2,OUXBUIC-OUXB(,R2)
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSSRV(3),DECWORK4+1
         L     R2,ASCBOUXB
         LH    R2,OUXBFIXC-OUXB(,R2)
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   TSWAP-1(3),DECWORK4+1
         SR    R2,R2
         IC    R2,OUCBSRC
         ST    R2,HEXWORK1
         BAL   R14,@HEX
         MVC   TSREASN(2),HEXWORK2+6
ENDFORMT LA    R10,79(R10)
         B     ASCBSCAN
*
ASCBEXIT EQU   *
         MVC   DECWORK1(4),CTRUSE
         BAL   R14,@DECIMAL
         MVI   0(R10),C' '
         MVC   1(158,R10),0(R10)
         MVI   0(R10),C'.'
         MVI   79(R10),C'.'
*        FORMAT MAXUSERS HERE
         LA    R10,79(R10)
         MVC   TSUSER(8),=CL8'< 0000 >'
         MVC   TSUSER+2(4),DECWORK4
         MVC   TSLINE(L'USRTOT),USRTOT
         MVC   DECWORK1(4),LSCTCNT
         BAL   R14,@DECIMAL
         MVC   TSPGP(4),DECWORK4
         MVC   TSDMN(L'LSWUSR),LSWUSR
*
         L     R2,PARMADDR
         TM    0(R2),X'80'
         BO    TSEXIT
         TM    4(R2),X'80'
         BO    TSEXIT
         L     R2,8(R2)
         MVC   0(79,R2),0(R10)
         MVC   80(4,R2),CTRUSE
         B     TSEXIT
         EJECT
         DS    0F
@@LSW    DC    CL4'LSW '
@@HUH    DC    CL4'??? '
@@TOUT   DC    CL4'--> '
@@TIN    DC    CL4'<-- '
@@TRAN   DC    CL4'<-> '
@@LWT    DC    CL4'LWT '
@@IWT    DC    CL4'IWT '
@@OWT    DC    CL4'OWT '
@@NSW    DC    CL4'WNS '
@@NAS    DC    CL4'N/S '
@@DWT    DC    CL4'DWT '
@@RDY    DC    CL4'RDY '
@@RUN    DC    CL4'RUN '
@@IN     DC    CL4'IN  '
LSWUSR   DC    C' IN LOGICAL SWAP'
USRTOT   DC    C'  USERS LOGGED ON '
TITLE    DC    C'USERID  LINE LOC AS DP DQ PG-P DM ISET OSET REAL'
         DC    C' SCPU SIOC SMSO UIC FIX SR                       '
X22C     DC    X'0000022C'
X200     DC    X'00000200'
QM       DC    CL1'?'
QML      EQU   *-QM
TSOLINE  DC    CL80' '
NAMEASCB DC    CL4'ASCB'
NAMEOUCB DC    CL4'OUCB'
         EJECT
TSEXIT   DCMSTOP RC=(R15)
TSBWORK1 DC    XL4'0000FF00'
TSBWORK2 DC    CL8' '
WORKA    DC    A(0)    PTR TO OUTPUT AREA
WORKB    DC    2F'0'
ZEROS    DC    F'0'
RSTOR    DS    F
MAXU     DS    F
CTRUSE   DS    F
NEXTGUY  DS    F
         DS    D
         EJECT
         CVT   DSECT=YES
         EJECT
ACSB     IHAASCB
         EJECT
*UCB     IRAOUCB
         EJECT
ASVT     IHAASVT
         EJECT
         IKJTSB  LIST=YES
         EJECT
         IRAWPGDT  DSECT=YES
        EJECT
        RMCNS
*        IHAOUXB DSECT=YES
         END
