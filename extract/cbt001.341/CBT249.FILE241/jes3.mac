         MACRO
&L       CVD3  &BIN,&DEC
&L       L     R1,&BIN
         CVD   R1,DOUBLE
         MVC   &DEC,PAT3
         ED    &DEC,DOUBLE+6
         MEND
         MACRO
&L       PUT   &SRC,&LINE
&L       L     R1,&SRC
         MVC   0(79,R1),&LINE
         LA    R1,79(R1)
         ST    R1,&SRC
         MEND
         EJECT
JES3     DCMSTART R,LV=4096,STAE=NO
         MVI   FRAMESW,X'00'
         DCMSNULL  FORMAT=FORMAT,IMAGE=SCREEN
         ZAP    PDISKS,=P'0'
         ZAP    PTAPES,=P'0'
         ZAP    PDISKSA,=P'0'
         ZAP    PTAPESA,=P'0'
         ZAP    PDISKSAL,=P'0'
         ZAP    PTAPESAL,=P'0'
         ZAP    PDISKSR,=P'0'
         ZAP    PTAPESR,=P'0'
         ZAP    PDISKSO,=P'0'
         ZAP    PTAPESO,=P'0'
         LM     R14,R1,CLEAREGS
         MVCL   R14,R0
FRAMEA   L     R9,16
         L     R9,CVTJESCT(R9)    R9->JESCT
         L     R9,JESSSCT(R9)     R9->SSCT
         L     R9,SSCTSSVT(R9)    R9->SSVT
         LA    R1,SCREEN
         ST    R1,PRINT
         PUT    PRINT,TITLEZ
         PUT   PRINT,BLANKS
         PUT   PRINT,BLANKS
         EJECT
         L     R7,SVTMPCDA(R9)    R7->MPC
         SPACE
JES200   TM    MPLFLG(R7),X'10'
         BZ    JES700             SUN IPL'D
         LH    R1,MPAINIT(R7)     ACTIVE INIT'S
         CVD   R1,DOUBLE
         MVC   INITS,PAT3
         ED    INITS,DOUBLE+6
         SPACE
         LH    R1,MPDEEP(R7)      ACTIVE JOBS
         CVD   R1,DOUBLE
         MVC   JOBS,PAT3
         ED    JOBS,DOUBLE+6
         SPACE
         SR    R1,R1
         IC    R1,MPSDEPTH(R7)    MAX SDEPTH
         CVD   R1,DOUBLE
         MVC   MXSDP,PAT3
         ED    MXSDP,DOUBLE+6
         SPACE
         IC    R1,MPSDEEP(R7)     CURR SDEPTH
         CVD   R1,DOUBLE
         MVC   CRSDP,PAT3
         ED    CRSDP,DOUBLE+6
         SPACE
         MVC   MXNAME,MPNAME(R7)
         SPACE 2
         LH    R1,MPMAXI(R7)       MAX INITS AT ONE TIME
         CVD   R1,DOUBLE
         MVC   MAXI,PAT3
         ED    MAXI,DOUBLE+6
         PUT   PRINT,MPMSG
         EJECT
         LH    R1,MPLSTOR(R7)
         CVD   R1,DOUBLE
         MVC   LSTOR,PAT2
         ED    LSTOR,DOUBLE+5
         LH    R1,MPMBINC(R7)      AVAILABLE LSTOR
         CVD   R1,DOUBLE
         MVC   AVLSTOR,PAT2
         ED    AVLSTOR,DOUBLE+5
         SPACE
         LH    R1,MPMVSALC(R7)     ALLOCATED LSTOR
         CVD   R1,DOUBLE
         MVC   ALLSTOR,PAT2
         ED    ALLSTOR,DOUBLE+5
         SPACE
         PUT   PRINT,MPMSG2
         EJECT
         L     R6,MPMEMD(R7)      R6->MEMDATA
         XC    TSOS,TSOS
         SPACE 2
MEMLOOP  LTR   R6,R6
         BZ    JES600
         SPACE
         L     R3,MEMNTRY1(R6)     R3-> MEM ENTRY
         TM    MEMFLG1(R3),X'04'
         BZ    JES400              SUN TSO
         SPACE
         L     R1,TSOS             COUNT TSO JOBS
         LA    R1,1(R1)
         ST    R1,TSOS
JES400   L     R6,MEMCHAIN(R6)     NEXT
         B     MEMLOOP
         SPACE
JES600   L     R1,TSOS             PRINT # TSO USERS
         LTR   R1,R1
         BZ    JES650
         CVD   R1,DOUBLE
         MVC   TSOJBS,PAT3
         ED    TSOJBS,DOUBLE+6
         PUT   PRINT,TSOMSG
         SPACE 2
JES650   PUT   PRINT,BLANKS
JES700   L     R7,MPNEXT(R7)
         LTR   R7,R7
         BNZ   JES200
         EJECT
         L     R2,SVTSYSUN(R9)     NOW DO SETUP VIA SYSUNIT SCAN
         SPACE
SET100   CLI   1(R2),X'FF'
         BE    ENDSET
         TM    SYSFLAG4(R2),X'40'
         BZ    SET300              SUN DISK
         AP    PDISKS,=P'1'        PACKED INSTR - UGH
         B     SET350
SET300   TM    SYSFLAG4(R2),X'80'
         BZ    SETNEXT             SUN TAPE
         AP    PTAPES,=P'1'
SET350   LA    R5,SYSMAINX(R2)
         L     R7,SVTMPCDA(R9)     FIND MPC DEVICE ATTACHED TO
         SPACE
SET360   EX    0,MPRQTEST(R7)
         BNZ   SET380              SIF ATTACHED TO THIS MAIN
         SPACE
SET370   L     R7,MPNEXT(R7)       NEXT
         LTR   R7,R7
         BNZ   SET360              SIF HAVE ANOTHER
         B     SETNEXT
         EJECT
SET380   L     R5,MPUNITS(R7)      FIND SETUNITS ENTRY FOR THIS UNIT
         SPACE
SET385   CLI   1(R5),X'FF'
         BE    SETNEXT             SIF END ???
         L     R1,SETADD(R5)
         LA    R1,0(R1)
         CR    R1,R2
         BE    SET387              SIF SETUNIT FOUND
         LA    R5,SETEND(R5)
         B     SET385
         SPACE
SET387   L     R6,SVTSETNM(R9)     SETNAMES TABLE
         MVC   DVT200+1(1),SETTYPE(R5) SET DEVICE TYPE CODE
         SPACE 2
DVT200   CLI   NAMETYPE(R6),*-*
         BE    DVT800              SIF SETNAME ENTRY FOUND
DVT300   LA    R6,10(R6)           NEXT SETNAME
         CLI   SETNAME(R6),X'FF'
         BNE   DVT200
         CLI   SETNAME+2(R6),X'FF'
         BNE   DVT300
         SPACE 2
DVT800   L     R4,=A(DEVTYPES)     R4->DEV TYPE ENTRY
SET390   CLI   0(R4),X'00'
         BE    SET394              SIF EMPTY SLOT
         CLI   0(R4),X'FF'
         BE    SETNEXT             SIF OUT OF ENTRIES
         CLC   0(8,R4),0(R6)
         BE    SET394              SIF ENTRY FOUND
         LA    R4,32(R4)
         B     SET390              NEXT
         SPACE
SET394   MVC   0(8,R4),0(R6)       SET DEVICE TYPE
         LA    R5,SYSMAINY(R2)
SET395   EX    0,MPRQTEST(R7)
         BNZ   SET398
SET396   L     R7,MPNEXT(R7)       NEXT MPC
         LTR   R7,R7
         BZ    SET800              SIF OFFLINE TO ALL
         B     SET395
SET398   TM    MPLFLG(R7),X'10'
         BZ    SET396              SIF MAIN NOT IPLED (OFFLINE)
         EJECT
         TM    SYSUCBST(R2),X'24'
         BNZ   SET500              SIF RESIDENT
         TM    SYSFLAG1(R2),X'A8'
         BNZ   SET900              SIF ALLOC/RESERVED/MOUNTED
         SPACE
         L     R1,28(R4)           FREE
         LA    R1,1(R1)
         ST    R1,28(R4)
         SPACE
         TM    SYSFLAG4(R2),X'80'
         BO    SET400              SIF TAPE
         AP    PDISKSA,=P'1'       BUMP AVAIL DISKS
         B     SETNEXT
SET400   AP    PTAPESA,=P'1'
         SPACE 2
SETNEXT  LA    R2,SYSEND(R2)       NEXT SYSUNITS
         B     SET100
         EJECT
SET500   L     R1,24(R4)           RESIDENT
         LA    R1,1(R1)
         ST    R1,24(R4)
         B     SETNEXT
         SPACE
SET800   L     R1,16(R4)           OFFLINE
         LA    R1,1(R1)
         ST    R1,16(R4)
SET810   LA    R14,PDISKSO         ASSUME DISK
         B     SET910
         SPACE 2
SET900   TM    SYSFLAG1(R2),X'08'
         BO    SET930              SIF 'MOUNTED'
         TM    SYSFLAG1(R2),X'20'
         BZ    SET920              SUN RESERVED
         L     R1,12(R4)
         LA    R1,1(R1)
         ST    R1,12(R4)
         LA    R14,PDISKSR         ASSUME DISK
         SPACE
SET910   TM    SYSFLAG4(R2),X'40'
         BO    *+8
         LA    R14,L'PDISKSR(R14)  USE TAPE
         AP    0(L'PDISKSR,R14),=P'1'
         B     SETNEXT
         SPACE
SET920   L     R1,8(R4)            ALLOCATED
         LA    R1,1(R1)
         ST    R1,8(R4)
         LA    R14,PDISKSAL        ASSUME DISK
         B     SET910
         SPACE
SET930   L     R1,20(R4)           COUNT MOUNTED
         LA    R1,1(R1)
         ST    R1,20(R4)
         B     SETNEXT
         EJECT
ENDSET   MVC   DISKS,PAT3          TOTAL DEVICES
         ED    DISKS,PDISKS
         MVC   TAPES,PAT3
         ED    TAPES,PTAPES
         MVC   DISKSA,PAT3         AVAILABLE DEVICES
         ED    DISKSA,PDISKSA
         MVC   TAPESA,PAT3
         ED    TAPESA,PTAPESA
         MVC   DISKSAL,PAT3        ALLOCATED
         ED    DISKSAL,PDISKSAL
         MVC   TAPESAL,PAT3
         ED    TAPESAL,PTAPESAL
         MVC   DISKSR,PAT3         RESERVED
         ED    DISKSR,PDISKSR
         MVC   TAPESR,PAT3
         ED    TAPESR,PTAPESR
         MVC   DISKSO,PAT3         OFFLINE
         ED    DISKSO,PDISKSO
         MVC   TAPESO,PAT3
         ED    TAPESO,PTAPESO
         PUT   PRINT,SETUPD
         PUT   PRINT,SETUPT
         PUT   PRINT,BLANKS
         MVI   FRAMESW,X'02'
         B     PUTFRAME
FRAMEB   EQU   *
         DCMSNULL  FORMAT=FORMAT,IMAGE=SCREEN
         LA    1,SCREEN
         ST    1,PRINT
         PUT   PRINT,TITLEZ
         PUT   PRINT,BLANKS
         PUT   PRINT,BLANKS
         L     1,PRINT
         LA    1,20(1)
         ST    1,PRINT
         EJECT
         L     R4,=A(DEVTYPES)     PRINT BREAKDOWN BY DEV TYPE
         PUT   PRINT,BLANKS
         PUT   PRINT,DEVTITLE
         PUT   PRINT,BLANKS
         SPACE 2
DEV100   CLI   0(R4),X'FF'
         BE    DEV900              SIF END OF MY DEV TABLE
         CLI   0(R4),0
         BE    DEV900
         SPACE
         MVC   DEVNAME,0(R4)
         CVD3  28(R4),DFREE
         CVD3  8(R4),DALLOC
         CVD3  12(R4),DRESV
         CVD3  16(R4),DOFFL
         CVD3  20(R4),DMNTD
         CVD3  24(R4),DRESD
         PUT   PRINT,DEVMSG
         SPACE 2
DEV300   LA    R4,32(R4)
         B     DEV100
         SPACE 2
DEV900   DS    0H
         MVI   FRAMESW,X'00'
         EJECT
PUTFRAME EQU   *
         OI    IOCBFLGC,IOCBCRAO
         OI    IOCBFLGD,IOCBDYON
         TRMIO IOCB,WRITE,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY DEF=NEXTONE,K9=DCMSOVER,K12=EXIT,ATTN=EXIT
NEXTONE  DS    0H
         CLI   FRAMESW,X'02'
         BE    FRAMEB
         B     DCMSOVER
         LTORG
         EJECT
FRAMESW  DC    X'00'
PRINT    DC    F'0'
TSOS     DC    F'0'
PDISKS   DC    PL2'0'
PTAPES   DC    PL2'0'
PDISKSA  DC    PL2'0'
PTAPESA  DC    PL2'0'
PDISKSAL DC    PL2'0'
PTAPESAL DC    PL2'0'
PDISKSR  DC    PL2'0'
PTAPESR  DC    PL2'0'
PDISKSO  DC    PL2'0'
PTAPESO  DC    PL2'0'
         EJECT
MPMSG    DC    CL80' '
         ORG   MPMSG
MXNAME   DC    CL8' ',C'  INITS ='
INITS    DC    CL4' ',C', JOBS ='
JOBS     DC    CL4' ',C', SDEPTH: MAX ='
MXSDP    DC    CL4' ',C', CURR ='
CRSDP    DC    CL4' ',C', MAXI ='
MAXI     DC    CL4' '
         ORG
         SPACE
MPMSG2   DC    CL80' '
         ORG   MPMSG2+10
         DC    C'LSTOR : TOTAL'
LSTOR    DC    CL6' ',C'  FREE'
AVLSTOR  DC    CL6' ',C'  ALLOC'
ALLSTOR  DC    CL6' '
         ORG
         SPACE
SETUPD   DC    CL80' '
         ORG   SETUPD
         DC    CL10'SETUP:'
         DC    C'DISKS ='
DISKS    DC    CL4' ',C', FREE ='
DISKSA   DC    CL4' ',C', ALLOC ='
DISKSAL  DC    CL4' ',C', RSVD ='
DISKSR   DC    CL4' ',C', OFFLINE ='
DISKSO   DC    CL4' '
         ORG
SETUPT   DC    CL80' '
         ORG   SETUPT+10
         DC    C'TAPES ='
TAPES    DC    CL4' ',C', FREE ='
TAPESA   DC    CL4' ',C', ALLOC ='
TAPESAL  DC    CL4' ',C', RSVD ='
TAPESR   DC    CL4' ',C', OFFLINE ='
TAPESO   DC    CL4' '
         ORG
         SPACE
TSOMSG   DC    CL80' '
         ORG   TSOMSG+10
         DC    C'TSO JOBS ='
TSOJBS   DC    CL4' '
         ORG
         SPACE 2
DEVMSG   DC    CL80' '
         ORG   DEVMSG
DEVNAME  DC    CL8' ',CL2' '
DFREE    DC    CL4' ',CL2' '
DALLOC   DC    CL4' ',CL2' '
DRESV    DC    CL4' ',CL2' '
DOFFL    DC    CL4' ',CL2' '
DMNTD    DC    CL4' ',CL2' '
DRESD    DC    CL4' '
         ORG
DEVTITLE DC    CL80'NAME      FREE  ALLC  RESV  OFFL  MNTD  RESD'
         EJECT
CLEAREGS DS    0F
         DC    A(DEVTYPES)
         DC    A(210*4)
         DC    A(0)
         DC    A(0)
TITLEZ   EQU   *
  ZB 'J E S 3   S T A T I S T I C S'
         SPACE 2
PAT2     DC    X'402020202120'    XXXXX
PAT3     DC    X'40202120'        XXX
         SPACE
CVTJESCT EQU   X'128'
JESSSCT  EQU   X'18'
SSCTSSVT EQU   X'10'
SVTMPCDA EQU   X'27C'
SVTSETNM EQU   X'284'
SVTSYSUN EQU   X'29C'
MPNEXT   EQU   X'00'
MPMAXI   EQU   X'2FC'
MPNAME   EQU   X'28C'
MPMEMD   EQU   X'360'
MPSCHFG  EQU   X'4E9'
MPADAPT  EQU   X'2A0'
MPAINIT  EQU   X'40C'
MPDEEP   EQU   X'40E'
MPSDEEP  EQU   X'416'
MPSDEPTH EQU   X'2FE'
MPLFLG   EQU   X'4E2'
MPLSTOR  EQU   X'30A'
MPMBINC  EQU   X'404'
MPMVSALC EQU   X'406'
MPUNITS  EQU   X'364'
MPRQTEST EQU   X'480'
MPSYSADD EQU   X'4F8'
SETTYPE  EQU   X'00'
SETADD   EQU   X'04'
SETEND   EQU   X'20'
SYSEND   EQU   X'20'
SYSFLAG1 EQU   X'16'
SYSFLAG4 EQU   X'19'
SYSMAINX EQU   X'04'
SYSMAINY EQU   X'08'
SYSUCBST EQU   X'1D'
MEMNTRY1 EQU   X'24'
MEMCHAIN EQU   X'04'
MEMFLG1  EQU   X'08'
SETNAME  EQU   X'00'
NAMETYPE EQU   X'08'
*
EXIT     DCMSTOP SCREEN=YES
JES3     CSECT
DEVTYPES DC    210F'0'
         DC    F'-1'
         END
