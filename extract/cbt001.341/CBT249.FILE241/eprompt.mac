EPROMPT  TITLE '3277 FULL SCREEN PROMPT/ERASE COMMAND'
*        %GOTO CBUFBSL ; /*
         MACRO
         IKJCBUF
CBUF     DSECT
CBUFLEN  DS    H
CBUFOFF  DS    H
CBUFCOMD DS    0H .           COMMAND
         MEND
**/
*%CBUFBSL: ;
*DECLARE
*  1 CBUF  BASED(CBUFPTR) UNAL,
*   2 CBUFL FIXED BINARY(15,0),
*   2 CBUFO FIXED BINARY(15,0),
*   2 CBUFC CHAR(256);
*
         IKJCPPL
         EJECT
         IKJCBUF
         EJECT
         IKJPSCB
         EJECT
         IKJUPT
         EJECT
         IKJECT
         EJECT
EPROMPT  PROLOG R,LV=4096
         USING IKJWORK,R13
         USING  CPPL,R2
         LR    R2,R1         ACQUIRE
         LTR   R2,R2                PARM
         BZ    NOPARMS                  FIELD
         LA    R5,FLASH+1       POINT TO OUTPUT LINE
         TM    0(R2),X'80'    IS IT
         BO    BATCH               BATCH?
TSO      L     R7,CPPLCBUF    - MUST BE TSO
         USING CBUF,R7
         L     R8,CPPLECT
         USING ECT,R8
         TM    ECTSWS,ECTNOPD
         BO    NOPARMS
         LA    R3,CBUFCOMD      AND POINT
         AH    R3,CBUFOFF                TO DATA
         LH    R4,CBUFLEN       COMPUTE
         SH    R4,CBUFOFF              MOVE
         BNP   NOPARMS
         BCTR  R4,0                        LENGTH
         C     R4,=F'77'
         BE    PARMOVE
         BNH   *+8
         LA    R4,77
         B     COMMON
BATCH    EQU   *
         L     R3,0(R2)          PARM FIELD
         LH    R4,0(R3)          PARM LENGTH
         LA    R3,2(R3)
         LA    R4,0(R4)               LENGTH CLEARED
         LTR   R4,R4             IGNORE ZERO
         BZ    NOPARMS             LENGTH PARMS
         BCTR  R4,0              ADJUST TO MVC LENGTH
         C     R4,=F'77'         IS LENGTH ABOVE LIMIT?
         BH    NOPARMS              IGNORE IF SO
         BE    PARMOVE           IF AT LIMIT SKIP ADJUSTMENT
COMMON   LA    R6,79             COMPUTE
         SR    R6,R4                    CENTERING
         SRL   R6,1                              OFFSET
         BCTR  R6,0
         AR    R5,R6               FOR THE MOVE
PARMOVE  EQU   *
         EX    R4,PARMVC
         B     GTSIXE
PARMVC   MVC   0(0,R5),0(R3)
NOPARMS  DS    0H
         MVC   FLASH+21(40),DEFAULTS
GTSIXE   EQU   *
         GTSIZE
         STM   0,1,TERMSIZE
         LA    R15,16
         LTR   R0,R0
         BZ    NOTUBE
         LA    R1,ERAS2
         TPUT  (1),E2LEN,FULLSCR,WAIT,NOHOLD,BREAKIN
         LA    R1,SCREEN
         TGET  (1),2200
*
         MVC   SCREEN(L'ERAS),ERAS
         LA    R1,SCREEN
         TPUT  (1),L'ERAS,FULLSCR,WAIT,NOHOLD,BREAKIN
*
         LA    R1,ERAS3
         TPUT  (1),L'ERAS3,FULLSCR,WAIT,NOHOLD,BREAKIN
         SR    R15,R15
         B     DONEFOR
NOTUBE   EQU   *
         TPUT  FLASH,80
         TPUT  L2,1
         TPUT  CONTINU,40
         LA    R1,SCREEN
         TGET  (1),2200
         B     DONEFOR
         EJECT
*RAS     DC    X'C71140403C404000115D7F4013'
ERAS     DC    X'C7115D7F1140403C4F40003C40400013'
ERAS3    DC    X'C7115D7E11404013'
E3LEN    EQU   *-ERAS3
ERAS2    DC    X'C7115D7F1140401D'
L1       DC    C' ',78C'_',C' '
L2       DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×'
FLASH    DC    CL78' '
         DC    C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',C' ',16C'=',C'>',X'1DF8'
CONTINU  DC    CL40' DEPRESS     E N T E R     TO CONTINUE: '
         DC    X'13'
INPUT    DC    X'1D40',C'<',16C'=',C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
         DC    C'×',78C' ',C'×'
LL5      DC    C'×',78C'_',C'×'
E2LEN    EQU   *-ERAS2
DEFAULTS DC    CL40'*****   PROCESSING    PAUSE:  *****'
         DS    0H .
DONEFOR  EPILOG RC=(15)
IKJWORK  DSECT
SAVE     DS    18F .
TERMSIZE DS    3F .
SCREEN   DS    10F .
         END
