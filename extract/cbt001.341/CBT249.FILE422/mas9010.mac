00001  ID DIVISION.
00002  PROGRAM-ID. MAS9010.
00003  REMARKS.
00004 *****************************************************************
00005 *
00006 *     LIST SCHEDULE FILE..
00007 *     FUNCTIONS:
00008 *
00009 *     1). PRODUCES REPORTS ON AUTOMATIC SCHEDULE FILE
00010 *         SORTED BY VARIOUS FIELDS.
00011 *
00012 *                      BY: BUENEMAN 9/13/84
00013 *
00014 *****************************************************************
00015  ENVIRONMENT DIVISION.
00016  CONFIGURATION SECTION.
00017  SPECIAL-NAMES. C01 IS CHAN-1.
00018  INPUT-OUTPUT SECTION.
00019  FILE-CONTROL.
00020      SELECT SCH-FLE  ASSIGN TO SCHED
00021          ORGANIZATION IS INDEXED
00022          ACCESS IS SEQUENTIAL
00023          RECORD KEY IS SCH-KEY
00024          PASSWORD IS W-PASSWORD
00025          FILE STATUS IS SCH-CHECK.
00026      SELECT PRT-FLE  ASSIGN TO UT-S-REPORT.
00027      SELECT SORTFILE ASSIGN TO DA-S-SORTFILE.
00028  DATA DIVISION.
00029  FILE SECTION.
00030  FD  SCH-FLE LABEL RECORD IS STANDARD
00031      BLOCK CONTAINS 0 RECORDS.
00032  01  FLE-SCH.
00033      05  SCH-KEY                PIC X(12).
00034      05  FILLER                 PIC X(289).
00035  FD  PRT-FLE  RECORDING MODE IS F LABEL RECORD IS STANDARD
00036      BLOCK CONTAINS 0 RECORDS.
00037  01  FLE-PRT                    PIC X(133).
00038  SD  SORTFILE RECORDING MODE IS F.
00039  01  FILESORT.
00040      05  FILLER                 PIC X(02).
00041      05  S-SORT-2               PIC X(08).
00042      05  FILLER                 PIC X(02).
00043      05  S-SORT-1               PIC X(08).
00044      05  S-SORT-3               PIC X(02).
00045      05  FILLER                 PIC X(279).
00046  WORKING-STORAGE SECTION.
000465 77  PAN-VALET PICTURE X(24) VALUE '005MAS9010   03/25/85'.
00047  01  SCH-WRK.
00048      05  SCH-WRK-KEY           PIC X(12).
00049      05  FILLER  REDEFINES  SCH-WRK-KEY.
00050      10  SCH-SCH-YR            PIC S9(4) COMP.
00051      10  SCH-SCH-MO            PIC S9(4) COMP.
00052      10  SCH-SCH-DY            PIC S9(4) COMP.
00053      10  SCH-SCH-HR            PIC S9(4) COMP.
00054      10  SCH-SCH-MN            PIC S9(4) COMP.
00055      10  SCH-ID-NBR            PIC S9(4) COMP.
00056      05  SCH-APPL-NAME         PIC X(08).
00057      05  SCH-DAY-OF-WEEK       PIC S9(4) COMP.
00058      05  SCH-LAST-YR           PIC S9(4) COMP.
00059      05  SCH-LAST-MO           PIC S9(4) COMP.
00060      05  SCH-LAST-DY           PIC S9(4) COMP.
00061      05  SCH-LAST-HR           PIC S9(4) COMP.
00062      05  SCH-LAST-MN           PIC S9(4) COMP.
00063      05  SCH-WIN-HR            PIC S9(4) COMP.
00064      05  SCH-WIN-MN            PIC S9(4) COMP.
00065      05  SCH-SMF-SYSID         PIC X(04).
00066      05  SCH-FLAG              PIC S9(4) COMP.
00067      05  SCH-LEN-TEXT          PIC S9(4) COMP.
00068      05  SCH-BIN-ZERO          PIC S9(4) COMP.
00069      05  SCH-COMMAND-TEXT      PIC X(01) OCCURS 255 TIMES.
00070  01  H1.
00071      05  FILLER PIC X(30) VALUE '          M A Y  +  S P E H   '.
00072      05  FILLER PIC X(30) VALUE 'SCHEDULE FILE LISTING         '.
00073      05  FILLER PIC X(05) VALUE 'DATE '.
00074      05  H-DATE  PIC X(08).
00075      05  FILLER  PIC X(16) VALUE SPACES.
00076      05  FILLER PIC X(07) VALUE '   PAGE'.
00077      05  H-PAGE  PIC ZZ,ZZ9.
00078      05  FILLER  PIC X(31) VALUE SPACES.
00079  01  H2.
00080      05  FILLER PIC X(01) VALUE SPACE.
00081      05  FILLER PIC X(30) VALUE ' ----------SCHEDULED----------'.
00082      05  FILLER PIC X(30) VALUE '   ID    APPL    -----LAST EXE'.
00083      05  FILLER PIC X(30) VALUE 'CUTION-----  -WINDOW-  SMF    '.
00084      05  FILLER PIC X(42) VALUE SPACES.
00085  01  H3.
00086      05  FILLER PIC X(01) VALUE SPACE.
00087      05  FILLER PIC X(30) VALUE ' YEAR  MTH  DAY HOUR MIN DY/WK'.
00088      05  FILLER PIC X(30) VALUE '  NBR    NAME    YEAR  MTH  DA'.
00089      05  FILLER PIC X(30) VALUE 'Y HOUR  MIN  HRS MINS SYSID  -'.
00090      05  FILLER PIC X(30) VALUE '--------------COMMAND TEXT----'.
00091      05  FILLER PIC X(12) VALUE '------------'.
00092  01  PRT-LINE.
00093      05  FILLER                 PIC X(01).
00094      05  P-SCH-YR               PIC ZZZZ9.
00095      05  P-SCH-MO               PIC ZZZZ9.
00096      05  P-SCH-DY               PIC ZZZZ9.
00097      05  P-SCH-HR               PIC ZZZZ9.
00098      05  P-SCH-MN               PIC ZZZZ9.
00099      05  P-DAY-OF-WEEK          PIC ZZZZ9.
00100      05  P-ID-NBR               PIC ZZZZ9.
00101      05  FILLER                 PIC X(02).
00102      05  P-APPL-NAME            PIC X(08).
00103      05  FILLER                 PIC X(01).
00104      05  P-LAST-YR              PIC ZZZZ9.
00105      05  P-LAST-MO              PIC ZZZZ9.
00106      05  P-LAST-DY              PIC ZZZZ9.
00107      05  P-LAST-HR              PIC ZZZZ9.
00108      05  P-LAST-MN              PIC ZZZZ9.
00109      05  P-WIN-HR               PIC ZZZZ9.
00110      05  P-WIN-MN               PIC ZZZZ9.
00111      05  FILLER                 PIC X(02).
00112      05  P-SMF-SYSID            PIC X(04).
00113      05  FILLER                 PIC X(02).
00114      05  P-COMMAND-TEXT         PIC X(01) OCCURS 43 TIMES.
00115  01  LINE-CNT                   PIC S9(05) COMP-3 VALUE +0.
00116  01  MAX-LINE                   PIC S9(05) COMP-3 VALUE +60.
00117  01  W-PAGE                     PIC S9(05) COMP-3 VALUE +0.
00118  01  SUB-A                      PIC S9(05) COMP   VALUE +0.
00119  01  SCH-CHECK                  PIC 9(02)  VALUE 0.
00120  01  W-PASSWORD                 PIC X(04) VALUE 'WORD'.
00121  01  CTL-SW                     PIC X(01) VALUE 'A'.
00122  01  FST-SW                     PIC X(01) VALUE ' '.
00123  01  OLD-APPL                   PIC X(08).
00124  01  OLD-DAY                    PIC S9(4) VALUE +0.
00125  01  STR-LINE                   PIC X(133).
00126  PROCEDURE DIVISION.
00127      MOVE CURRENT-DATE TO H-DATE.
00128      OPEN OUTPUT PRT-FLE.
00129      PERFORM Z-040-HEADING.
00130  M-010-READ-SCHEDULE.
00131      SORT SORTFILE ON ASCENDING S-SORT-1 S-SORT-2
00132          USING SCH-FLE OUTPUT PROCEDURE IS M-020-REPORT.
00133      MOVE +0 TO W-PAGE.
00134      PERFORM Z-040-HEADING.
00135      MOVE 'D' TO CTL-SW.
00136      MOVE SPACE TO FST-SW.
00137      SORT SORTFILE ON ASCENDING S-SORT-3 S-SORT-2
00138          USING SCH-FLE OUTPUT PROCEDURE IS M-020-REPORT.
00139      CLOSE PRT-FLE.
00140      STOP RUN.
00141      EJECT
00142  Z-040-HEADING.
00143      ADD +1 TO W-PAGE.
00144      MOVE W-PAGE TO H-PAGE.
00145      WRITE FLE-PRT FROM H1 AFTER CHAN-1.
00146      WRITE FLE-PRT FROM H2 AFTER 2 LINES.
00147      WRITE FLE-PRT FROM H3 AFTER 1 LINES.
00148      MOVE SPACES TO PRT-LINE.
00149      WRITE FLE-PRT FROM PRT-LINE AFTER 1 LINES.
00150      MOVE +5 TO LINE-CNT.
00151  Z-050-WRITE-LINE.
00152      WRITE FLE-PRT FROM PRT-LINE AFTER 1 LINES.
00153      ADD +1 TO LINE-CNT.
00154      IF LINE-CNT NOT LESS THAN MAX-LINE
00155          PERFORM Z-040-HEADING.
00156      MOVE SPACES TO PRT-LINE.
00157  M-020-REPORT SECTION.
00158      RETURN SORTFILE INTO SCH-WRK AT END
00159          GO TO M-020-EXIT.
00160      IF SCH-WRK-KEY EQUAL TO HIGH-VALUES
00161          GO TO M-020-REPORT.
00162      IF FST-SW EQUAL TO SPACES
00163          MOVE SCH-APPL-NAME TO OLD-APPL
00164          MOVE SCH-DAY-OF-WEEK TO OLD-DAY
00165          MOVE '1' TO FST-SW.
00166      MOVE SCH-SCH-YR      TO P-SCH-YR.
00167      MOVE SCH-SCH-MO      TO P-SCH-MO.
00168      MOVE SCH-SCH-DY      TO P-SCH-DY.
00169      MOVE SCH-SCH-HR      TO P-SCH-HR.
00170      MOVE SCH-SCH-MN      TO P-SCH-MN.
00171      MOVE SCH-ID-NBR      TO P-ID-NBR.
00172      MOVE SCH-APPL-NAME   TO P-APPL-NAME.
00173      MOVE SCH-DAY-OF-WEEK TO P-DAY-OF-WEEK.
00174      MOVE SCH-LAST-YR     TO P-LAST-YR.
00175      MOVE SCH-LAST-MO     TO P-LAST-MO.
00176      MOVE SCH-LAST-DY     TO P-LAST-DY.
00177      MOVE SCH-LAST-HR     TO P-LAST-HR.
00178      MOVE SCH-LAST-MN     TO P-LAST-MN.
00179      MOVE SCH-WIN-HR      TO P-WIN-HR.
00180      MOVE SCH-WIN-MN      TO P-WIN-MN.
00181      MOVE SCH-SMF-SYSID   TO P-SMF-SYSID.
00182      SUBTRACT +4 FROM SCH-LEN-TEXT.
00183      MOVE +1 TO SUB-A.
00184  M-021-LOOP.
00185      MOVE SCH-COMMAND-TEXT (SUB-A) TO P-COMMAND-TEXT (SUB-A).
00186      ADD +1 TO SUB-A.
00187      IF SUB-A NOT GREATER THAN SCH-LEN-TEXT AND
00188          NOT GREATER THAN +43
00189              GO TO M-021-LOOP.
00190      IF CTL-SW EQUAL TO 'A' AND SCH-APPL-NAME NOT EQUAL TO
00191          OLD-APPL
00192          MOVE PRT-LINE TO STR-LINE
00193          MOVE SPACES TO PRT-LINE
00194          PERFORM Z-050-WRITE-LINE
00195          MOVE STR-LINE TO PRT-LINE
00196          MOVE SCH-APPL-NAME TO OLD-APPL.
00197      IF CTL-SW EQUAL TO 'D' AND SCH-DAY-OF-WEEK NOT EQUAL TO
00198          OLD-DAY
00199          MOVE PRT-LINE TO STR-LINE
00200          MOVE SPACES TO PRT-LINE
00201          PERFORM Z-050-WRITE-LINE
00202          MOVE STR-LINE TO PRT-LINE
00203          MOVE SCH-DAY-OF-WEEK TO OLD-DAY.
00204      PERFORM Z-050-WRITE-LINE.
00205      GO TO M-020-REPORT.
00206  M-020-EXIT. EXIT.
