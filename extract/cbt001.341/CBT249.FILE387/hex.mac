         MACRO
&NAME    HEX   &SEND,&RECEIVE,&SLEN
         PUSH  PRINT
         PRINT OFF
         LCLC  &REG
         AIF   ('&SEND' EQ '').ERR1
         AIF   ('&RECEIVE' EQ '').ERR1
         AIF   ('&SLEN' EQ '').ERR1
         AIF   ('&SEND'(1,1) EQ '(').REG1
&NAME    STM   13,1,SV&SYSNDX
         L     1,HS&SYSNDX
         AGO   .REC1
.REG1    ANOP
         AIF   ('&SEND'(3,1) EQ ')').REG1A
&REG     SETC  '&SEND'(2,2)
         AGO   .REC0
.REG1A   ANOP
&REG     SETC  '&SEND'(2,1)
.REC0    ANOP
&NAME    STM   13,1,SV&SYSNDX
         LR    1,&REG
.REC1    ANOP
         AIF   ('&RECEIVE'(1,1) EQ '(').REG2
         L     13,HR&SYSNDX
         AGO   .SLEN1
.REG2    ANOP
         AIF   ('&RECEIVE'(3,1) EQ ')').REG2A
&REG     SETC  '&RECEIVE'(2,2)
         AGO   .SLEN0
.REG2A   ANOP
&REG     SETC  '&RECEIVE'(2,1)
.SLEN0   ANOP
         LR    13,&REG
.SLEN1   ANOP
         AIF   ('&SLEN'(1,1) EQ '(').REG3
         AIF   (T'&SLEN EQ 'N').NUM1
         L     0,&SLEN
         AGO   .MAIN1
.NUM1    L     0,HL&SYSNDX
         AGO   .MAIN1
.REG3    ANOP
         AIF   ('&SLEN'(3,1) EQ ')').REG3A
&REG     SETC  '&SLEN'(2,2)
         AGO   .MAIN0
.REG3A   ANOP
&REG     SETC  '&SLEN'(2,1)
.MAIN0   ANOP
         LR    0,&REG
.MAIN1   ANOP
L&SYSNDX SR    14,14
         SR    15,15
         IC    14,0(,1)
         SRDL  14,4
         IC    14,TB&SYSNDX.(14)
         STC   14,0(,13)
         SR    14,14
         SLDL  14,4
         IC    14,TB&SYSNDX.(14)
         STC   14,1(,13)
         LA    1,1(,1)
         LA    13,2(,13)
         BCT   0,L&SYSNDX
         B     H&SYSNDX
TB&SYSNDX DC   CL16'0123456789ABCDEF'
         AIF   ('&SEND'(1,1) EQ '(').SKP1
HS&SYSNDX DC   A(&SEND)
.SKP1    AIF   ('&RECEIVE'(1,1) EQ '(').SKP2
HR&SYSNDX DC   A(&RECEIVE)
.SKP2    ANOP
         AIF   ('&SLEN'(1,1) EQ '(').SKP3
         AIF   (T'&SLEN NE 'N').SKP3
HL&SYSNDX DC   F'&SLEN'
.SKP3    ANOP
SV&SYSNDX DC   5F'0'
H&SYSNDX LM    13,1,SV&SYSNDX
         POP   PRINT
         MEXIT
.ERR1    MNOTE 'MISSING KEYWORD - MACRO GENERATION ENDED'
         MEND
