LPACOMPR CSECT
         REGS
         $SAVE JAR
         LR    R11,R15
         LA    R12,X'800'
         LA    R12,X'800'(R12,R11)
         USING LPACOMPR,R11,R12
         IEFSAVER
         OPEN  (OLDLPA,,NEWLPA,,SYSPRINT,OUTPUT)
         TM    OLDLPA+48,X'10'     SAVED LPA OPENED OK?
         BNO   RETURN16            NO - SET RC=16
         TM    NEWLPA+48,X'10'     CURRENT LPA OPENED OK?
         BNO   RETURN16            NO - SET RC=16
         TM    SYSPRINT+48,X'10'   PRINT FILE OPENED OK?
         BO    SETUP               YES - SET UP HEADINGS
RETURN16 DS    0H
         MVI   RC,16               UNABLE TO OPEN A FILE
         B     IEFRELSE
RETURN12 DS    0H
         MVI   RC,12               EOF WHILE EXPECTING CONTROL RECORD
         B     IEFRELSE
RETURN08 DS    0H
         MVI   RC,08               SET RETURN-CODE 8
         B     IEFRELSE
RETURN04 DS    0H
         MVI   RC,04               SET RETURN-CODE 4
         B     IEFRELSE
RETURN00 DS    0H
         MVI   RC,00               SET RETURN-CODE 0
         B     IEFRELSE
IEFRELSE DS    0H
         IEFRELSE
         SLR   R15,R15             CLEAR RETURN-CODE REGISTER
         IC    R15,RC              PICK UP RETURN-CODE
         RETURN (14,12),T,RC=(15)  LET'S GET THE HELL OUT O'DODGE!
         EJECT
SETUP    DS    0H
         LA    R1,RETURN12         EOF WHILE EXPECTING CONTROL RECORD
         STCM  R1,B'0111',OLDLPA+33 STORE INTO DCB EODAD
         GET   OLDLPA
         MVC   CNTLDATA(256),0(R1) CONTROL RECORD 1
         GET   OLDLPA
         MVC   CNTLXTNT(256),0(R1) CONTROL RECORD 2
         LA    R1,RETURN08         EOF DURING NORMAL PROCESSING
         STCM  R1,B'0111',OLDLPA+33 STORE INTO DCB EODAD
         SCANTIOT   =CL8'NEWLPA',RETURN16
         SLR   R1,R1
         ICM   R1,B'0111',12(R15) ADDRESS OF JFCB PREFIX
         LA    R2,16(,R1)         POINT TO BASIC JFCB (NEW LPA)
         LA    R3,CNTLJFCB          POINT TO BASIC JFCB (OLD LPA)
         TIME  DEC                  GET CURRENT DATE AND TIME
         STM   R0,R1,CURRTIME       STASH
         LA    R4,CNTLTIME
         LA    R5,HDR3ODTE
         BAL   R9,CONVDTIM          GO CONVERT OLD DATE AND TIME
         LA    R4,CURRTIME
         LA    R5,HDR3NDTE
         BAL   R9,CONVDTIM          GO CONVERT NEW DATE AND TIME
         MVC   HDR4ODSN(44),0(R3)   MOVE OLD DATASET NAME
         MVC   HDR4NDSN(44),0(R2)   MOVE NEW DATASET NAME
         MVC   HDR4OVSN(6),118(R3)  MOVE OLD VOLUME SERIAL NUMBER
         LA    R6,53(,R3)           TTR OF FMT1DSCB (OLD)
         LA    R7,HDR5OTTR          RECEIVING FIELD
         LA    R8,3                 LENGTH
         BAL   R9,HEXIT             CONVERT TO PRINTABLE FORM
         MVC   HDR5OTTR+5(2),HDR5OTTR+4 CONVERT TTTTRR
         MVI   HDR5OTTR+4,C'.'      TO TTTT.RR
         LA    R6,53(,R2)           TTR OF FMT1DSCB (NEW)
         LA    R7,HDR5NTTR
         LA    R8,3
         BAL   R9,HEXIT
         MVC   HDR5NTTR+5(2),HDR5NTTR+4
         MVI   HDR5NTTR+4,C'.'
         LA    R4,80(,R3)           POINT TO CREATION DATE (OLD)
         LA    R5,HDR5OCRD          RECEIVING FEILD
         BAL   R9,CONVXDAT          GO CONVERT
         LA    R4,83(,R3)           POINT TO EXPIRATION DATE (OLD)
         LA    R5,HDR5OEXP          RECEIVING FEILD
         BAL   R9,CONVXDAT          GO CONVERT
         LA    R4,80(,R2)           POINT TO CREATION DATE (NEW)
         LA    R5,HDR5NCRD          RECEIVING FEILD
         BAL   R9,CONVXDAT          GO CONVERT
         LA    R4,83(,R2)           POINT TO EXPIRATION DATE (NEW)
         LA    R5,HDR5NEXP          RECEIVING FEILD
         BAL   R9,CONVXDAT          GO CONVERT
         SLR   R2,R2
         ICM   R2,B'0111',NEWLPA+45 DEB ADDRESS
         LA    R3,32(,R2)           POINT TO DASD SECTION
         L     R3,0(,R3)            UCB ADDRESS FOR FIRST EXTENT
         MVC   HDR4NVSN(6),28(R3)   MOVE CURRENT VOL-SER TO HDR4
         BAL   R9,WRITEHDG          GO WRITE HEADINGS OUT
         PUT   SYSPRINT,HEADING8
         PUT   SYSPRINT,HEADING6    EXTENT TITLES HEADING
         AP    LINECNT(2),=P'2'
         SLR   R3,R3                CLEAR REG 3
         SLR   R4,R4                CLEAR REG 4
         IC    R3,CNTL#EXT          #EXTENTS IN OLD LPALIB
         IC    R4,16(,R2)           #EXTENTS IN NEW LPALIB
         ZAP   COUNT(2),=P'1'       INITIALIZE COUNT
         LA    R2,32(,R2)           POINT TO DASD SECTION
         LA    R5,CNTLALLX          START OF EXTENTS FOR OLD LPALIB
XTNTLOOP DS    0H
         MVI   HDR7OXT#,X'40'       SPACE OUT OLD LPA EXTENT DATA
         MVC   HDR7OXT#+1(34),HDR7OXT#
         MVI   HDR7NXT#,X'40'       SPACE OUT NEW LPA EXTENT DATA
         MVC   HDR7NXT#+1(34),HDR7NXT#
         LTR   R3,R3               ANY MORE EXTENTS IN OLD DATASET?
         BZ    NEWLHDR7            NO - GO FORMAT EXTENTS FOR NEW DS
         BCTR  R3,0                YES DECREMENT COUNTER
         UNPK  HDR7OXT#(2),COUNT(2) EXTENT NUMBER
         OI    HDR7OXT#+1,X'F0'
         MVC   HDR7OCUA(3),0(R5)   MOVE UNIT NAME TO REPORT
         LA    R6,3(,R5)           SENDING FIELD
         LA    R7,WORKAREA         RECEIVING FIELD
         LA    R8,8                SENDING FIELD LENGTH
         BAL   R9,HEXIT            CONVERT CCHHCCHH TO PRINT
         LA    R5,11(,R5)          BUMP TO NEXT EXTENT
         MVC   HDR7OSCC(4),WORKAREA+00  OLD START CYLINDER
         MVC   HDR7OSHH(4),WORKAREA+04  OLD START HEAD
         MVC   HDR7OECC(4),WORKAREA+08  OLD END CYLINDER
         MVC   HDR7OEHH(4),WORKAREA+12  OLD END HEAD
         MVC   HDR7OTOX(6),=CL6'  TO  ' SEPARATOR
         MVI   HDR7OSDT,C'.'            SEPARATOR
         MVI   HDR7OEDT,C'.'            SEPARATOR
NEWLHDR7 DS    0H
         LTR   R4,R4               ANY MORE EXTENTS IN NEW DATASET?
         BZ    PUTLHDR7            NO - GO PRINT THE LINE (MAYBE)
         BCTR  R4,0                YES - DECRENEMT COUNT
         UNPK  HDR7NXT#(2),COUNT(2) EXTENT NUMBER
         OI    HDR7NXT#+1,X'F0'
         L     R1,0(,R2)           UCB ADDRESS
         MVC   HDR7NCUA(3),13(R1)  MOVE UNIT NAME TO REPORT
         LA    R6,6(,R2)           CCHHCCHH
         LA    R7,WORKAREA         RECEIVING FIELD
         LA    R8,8                SENDING FIELD LENGTH
         BAL   R9,HEXIT            CONVERT CCHHCCHH TO PRINT
         LA    R2,16(,R2)          BUMP TO NEXT EXTENT
         MVC   HDR7NSCC(4),WORKAREA+00  NEW START CYLINDER
         MVC   HDR7NSHH(4),WORKAREA+04  NEW START HEAD
         MVC   HDR7NECC(4),WORKAREA+08  NEW END CYLINDER
         MVC   HDR7NEHH(4),WORKAREA+12  NEW END HEAD
         MVC   HDR7NTOX(6),=CL6'  TO  ' SEPARATOR
         MVI   HDR7NSDT,C'.'            SEPARATOR
         MVI   HDR7NEDT,C'.'            SEPARATOR
PUTLHDR7 DS    0H
         PUT   SYSPRINT,HEADING7
         AP    LINECNT(2),=P'1'
         AP    COUNT(2),=P'1'
         LR    R0,R3                    #EXTENTS LEFT FROM OLD LPALIB
         AR    R0,R4                    ADD NUMBER FROM NEW LPALIB
         BNZ   XTNTLOOP                 IF NOT ZERO, DO NEXT EXTENT
         DS    0H
         BAL   R9,HDR8HDR9              2 HDR8'S AND 1 HDR9
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
         BAL   R9,GETOLD           PRIME
         LR    R2,R1                THE
         BAL   R9,GETNEW           PUMPS
         LR    R3,R1
COMPARE  DS    0H
         CLC   0(8,R2),0(R3)       COMPARE OLD TO NEW
         BH    ADDEDMEM            OLD HIGH - MEMBER HAS BEEN ADDED
         BL    WIPEDMEM            NEW HIGH - MEMBER HAS BEEN SCRATCHED
         CLC   0(8,R2),XFFS        HAVE I HIT EOD ON BOTH DIRECTORIES?
         BE    SAYONARA            YES - SAY GOOD NIGHT, DICK.
         CLC   8(3,R2),8(R3)       IS THE MEMBER AT THE SAME TTR?
         BNE   DIFLNTTR            NO
         CLC   22(3,R2),22(R3)     MEMBER STILL THE SAME LENGTH?
         BNE   DIFLNTTR            NO
BUMPBOTH DS    0H
         BAL   R8,NEXTOMEM         GET NEXT MEMBER FROM OLD LPALIB
         BAL   R8,NEXTNMEM         GET NEXT MEMBER FROM NEW LPALIB
         B     COMPARE             DO IT AGAIN
DIFLNTTR DS    0H
         BAL   R5,FORMTOLD         FORMAT OLD SIDE OF THE COIN
         BAL   R5,FORMTNEW         FORMAT NEW SIDE OF THE COIN
         CLC   MEMBOLEN(6),MEMBNLEN     ARE LENGTHS THE SAME?
         BE    CHECKTTR            YES - CHECK TTR'S
         MVC   MEMBFLAG(3),=CL3'***'    NO - MOVE INDICATOR
         MVI   WTONOP#1+1,X'00'    DESTROY BRANCH AROUND WTO
         MVC   DUPEOLEN(6),MEMBOLEN
         MVC   DUPENLEN(6),MEMBNLEN
         TM    11(R3),X'80'        IS THIS AN ALIAS
         BO    CHECKTTR            YES - DON'T COMPUTE LENGTH DIFF
         SLR   R6,R6
         SLR   R7,R7
         ICM   R6,B'0111',22(R2)   OLD LENGTH IN REGISTER 6
         ICM   R7,B'0111',22(R3)   NEW LENGTH IN REGISTER 7
         SR    R6,R7               SUBTRACT NEW FROM OLD
         BM    INCREASE            NEGATIVE - NEW LARGER THAN OLD
         MVC   CHNGEMSG+8(7),=CL7'SMALLER'
         B     COMPDIFF
INCREASE DS    0H
         LPR   R6,R6               POSITIVE VALUE OF DECREASE
         MVC   CHNGEMSG+8(7),=CL7'LARGER '
COMPDIFF DS    0H
         BAL   R5,COMPDLEN         GO COMPUTE LENGTH DIFFERENCE
CHECKTTR DS    0H
         CLC   MEMBOSTT(7),MEMBNSTT     COMPARE TTR'S
         BE    PRINTDIF            EQUAL
         MVC   DUPEOSTT(7),MEMBOSTT
         MVC   DUPENSTT(7),MEMBNSTT
PRINTDIF DS    0H
         BAL   R8,PRINTREC         PRINT THE ERROR RECORD
         B     BUMPBOTH            AND PROCESS NEXT MEMBERS
ADDEDMEM DS    0H
         BAL   R5,FORMTNEW         GO FORMAT MEMBER DATA
         MVC   MEMBNNTE(29),=CL29'ADDED OR RENAMED'
         MVC   MEMBFLAG(3),=CL3'***'
         TM    11(R3),X'80'        ALIAS?
         BNO   ADDEDPRT            NO
         MVC   MEMBNNTE+17(7),=CL7'(ALIAS)'
ADDEDPRT DS    0H
         BAL   R8,PRINTREC
         BAL   R8,NEXTNMEM
         B     COMPARE
WIPEDMEM DS    0H
         BAL   R5,FORMTOLD         GO FORMAT MEMBER DATA
         MVC   MEMBNNTE(29),=CL29'SCRATCHED OR RENAMED'
         MVC   MEMBFLAG(3),=CL3'***'
         TM    11(R2),X'80'        ALIAS?
         BNO   WIPEDPRT            NO
         MVC   MEMBNNTE+21(7),=CL7'(ALIAS)'
WIPEDPRT DS    0H
         BAL   R8,PRINTREC
         BAL   R8,NEXTOMEM
         B     COMPARE
SAYONARA DS    0H
         MVC   MEMBONAM(55),EODMSG      END OF DIRECTORY MESSAGE
         MVC   MEMBNNAM(55),EODMSG
         LA    R2,2
SPACRLUP PUT   SYSPRINT,HEADING8        SPACER
         BCT   R2,SPACRLUP
         PUT   SYSPRINT,MEMBDATA        WRITE END OD DIRECTORY MESSAGE
         CP    LINECNT(2),=P'50'        DO I NEED A NEW PAGE?
         BL    WRITEEND                 NO
         BAL   R9,WRITEHDG              YES - SKIP TO NEW PAGE
WRITEEND DS    0H
         PUT   SYSPRINT,HEADING8        SEPARATOR
         MVC   MEMBDATA(133),HEADING8   CLEAR LINE
         MVC   MEMBONAM(34),BLKSCONS
         MVC   MEMBNNAM(34),BLKSCONS
         ED    MEMBONAM(07),OLDDIRBK
         ED    MEMBNNAM(07),NEWDIRBK
         PUT   SYSPRINT,MEMBDATA
         MVC   MEMBDATA(133),HEADING8   CLEAR LINE
         MVC   MEMBONAM(34),MEMBCONS
         MVC   MEMBNNAM(34),MEMBCONS
         ED    MEMBONAM(07),OLDMEMBR
         ED    MEMBNNAM(07),NEWMEMBR
         PUT   SYSPRINT,MEMBDATA
         MVC   MEMBDATA(133),HEADING8   CLEAR LINE
         MVC   MEMBONAM(34),ALIACONS
         MVC   MEMBNNAM(34),ALIACONS
         ED    MEMBONAM(07),OLDALIAS
         ED    MEMBNNAM(07),NEWALIAS
         PUT   SYSPRINT,MEMBDATA
         PUT   SYSPRINT,HEADING8    SPACER
         MVI   DUPEDATA+1,X'40'
         MVC   DUPEDATA+2(131),DUPEDATA+1
         MVI   MEMBDATA,X'40'
         MVC   MEMBDATA+1(132),MEMBDATA
         MVC   MEMBDATA+1(48),WTOALLOK+8
WTONOP#1 B     NOAFFECT
         MVC   WTOALLOK+23(8),=CL8'**WILL**'
         MVC   MEMBDATA+16(8),=CL8'**WILL**'
         MVC   DUPEDATA+16(8),=CL8'**WILL**'
         MVI   XITNOP#1+1,X'F0'
         B     FINALINE
NOAFFECT DS    0H
         MVC   WTOALLOK+23(8),=CL8'WILL NOT'
         MVC   MEMBDATA+16(8),=CL8'WILL NOT'
         MVC   DUPEDATA+16(8),=CL8'WILL NOT'
FINALINE DS    0H
         PUT   SYSPRINT,MEMBDATA
         LA    R10,3
FINALOOP DS    0H
         PUT   SYSPRINT,DUPEDATA
         BCT   R10,FINALOOP
         CLOSE (OLDLPA,,NEWLPA,,SYSPRINT)
WTOALLOK WTO   'RESTARTABILITY WILL NOT BE AFFECTED BY THIS CLPA',     $
               ROUTCDE=(2,11),DESC=2
         ORG   *-2
         NOPR  0
         ORG
XITNOP#1 NOP   *+8
         B     RETURN00
         B     RETURN04
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*---------------------------------------------------------------------*
COMPDLEN DS    0H
         ST    R6,WORKAREA         STORE LENGTH INCREASE/DECTEASE
         LA    R6,WORKAREA+1       START OF LENGTH DIFFERENCE
         LA    R7,WORKAREA+4       RECEIVING AREA
         LA    R8,3                LENGTH
         BAL   R9,HEXIT            CONVERT
         MVC   MEMBNNTE(2),=C'X'''
         LA    R6,WORKAREA+4
         LA    R7,6
ZEROLOOP DS    0H
         CLI   0(R6),C'0'          LEADING ZERO?
         BNE   NZEROLEN            NO
         LA    R6,1(,R6)           NEXT BYTE
         BCT   R7,ZEROLOOP
NZEROLEN DS    0H
         BCTR  R7,0                DECREMENT BY ONE
         MVC   MEMBNNTE+2(*-*),0(R6) EXECUTED BY NEXT INSTRUCTION
         EX    R7,*-6              MOVE EDITED DIFFERENCES TO NOTE
         LA    R8,MEMBNNTE+3(R7)   POINT TO LAST BYTE OF DIFFERENCE
         MVC   0(15,R8),CHNGEMSG   MOVE REST OF MESSAGE IN
         BR    R5
         SPACE 3
PRINTREC DS    0H
         CP    LINECNT(2),=P'58'   AM I ABOUT TO OVERFLOW A PAGE?
         BL    DUPLEX              NO - GO SET UP DUPLEX
         BAL   R9,WRITEHDG         YES - PRINT PAGE HEADINGS
         BAL   R9,HDR8HDR9         ALSO PRINT WORK HEADINGS
DUPLEX   DS    0H
         MVC   DUPEFLAG(3),MEMBFLAG     DUPLEX FLAG
         MVC   DUPENNTE(29),MEMBNNTE    DUPLEX NEW NOTES
         CLC   MEMBONTE(3),=CL3'***'    CRITICAL NOTE?
         BNE   NODUPONT                 NO - DON'T DUPLEX OLD NOTE
         MVC   DUPEONTE(29),MEMBONTE    DUPLEX OLD NOTES
NODUPONT DS    0H
         PUT   SYSPRINT,HEADING8   SEPARATOR
         PUT   SYSPRINT,MEMBDATA   MEMBER DATA
         LA    R10,3
BANGBANG DS    0H
         PUT   SYSPRINT,DUPEDATA   OVERPRINT DUPLEXED DATA
         BCT   R10,BANGBANG
         AP    LINECNT,=P'2'
         MVC   MEMBDATA(133),HEADING8   SPACE OUT PRINT LINE
         MVI   DUPEFLAG,X'40'
         MVC   DUPEFLAG+1(131),DUPEFLAG AND DUPE LINE ALSO
         BR    R8                  RETURN TO CALLER
         SPACE 3
NEXTOMEM DS    0H
         CLC   0(8,R2),XFFS        AM I AT END OF OLD DIRECTORY?
         BER   R8                  YES - DON'T LOOK FOR MORE MEMBERS
         BAL   R9,GETOLD           NO - GET NEXT MEMBER
         LR    R2,R1               HOLD ADDRESS
         TM    11(R1),X'80'        IS THIS AN ALIAS?
         BNO   REALOMEM            NO, IT'S A REAL MEMBER
         AP    OLDALIAS(3),=P'1'   COUNT ALIAS
         BR    R8                  RETURN TO CALLER
REALOMEM DS    0H
         AP    OLDMEMBR(3),=P'1'   COUNT MEMBER
         BR    R8                  RETURN TO CALLER
         SPACE 3
NEXTNMEM DS    0H
         CLC   0(8,R3),XFFS        AM I AT END OF NEW DIRECTORY?
         BER   R8                  YES - DON'T LOOK FOR MORE MEMBERS
         BAL   R9,GETNEW           NO - GET NEXT MEMBER
         LR    R3,R1               HOLD ADDRESS
         TM    11(R1),X'80'        IS THIS AN ALIAS?
         BNO   REALNMEM            NO, IT'S A REAL MEMBER
         AP    NEWALIAS(3),=P'1'   COUNT ALIAS
         BR    R8                  RETURN TO CALLER
REALNMEM DS    0H
         AP    NEWMEMBR(3),=P'1'   COUNT MEMBER
         BR    R8                  RETURN TO CALLER
         SPACE 3
FORMTOLD DS    0H
         MVC   MEMBONAM(8),0(R2)   MOVE MEMBER NAME
         LA    R6,08(,R2)          STARTING TTR
         LA    R7,MEMBOSTT         RECEIVING FIELD
         LA    R8,3                LENGTH
         BAL   R9,HEXIT            GO CONVERT
         MVC   MEMBOSRR(2),MEMBODOT     SEPARATE
         MVI   MEMBODOT,C'.'       SEPARATOR
         LA    R6,20(,R2)          ATTRIBUTE FLAGS
         LA    R7,MEMBOATT
         LA    R8,2
         BAL   R9,HEXIT
         LA    R6,22(,R2)          AMOUNT OF MAIN STORAGE NEEDED
         LA    R7,MEMBOLEN
         LA    R8,3
         BAL   R9,HEXIT
         TM    11(R2),X'80'        IS THIS AN ALIAS?
         BNO   REALMEMB            NO - REAL MEMBER
         TM    20(R2),X'80'        RE-ENTRANT?
         BNO   NOTRENTR            NO - WE GOTUM PROBLEMS
         MVC   MEMBONTE(10),=CL10'ALIAS FOR '
         MVC   MEMBONTE+10(8),36(R2)  MOVE ALIAS NAME
         B     FORMOLD5            RETURN
REALMEMB DS    0H
         MVC   MEMBONTE(19),=CL19'*** REAL MEMBER ***'
         B     FORMOLD5            RETURN
NOTRENTR DS    0H
         MVC   MEMBONTE(29),=CL29'*** MODULE NOT RE-ENTRANT ***'
         B     FORMOLD5
FORMOLD5 DS    0H
         BR    R5
         SPACE 3
FORMTNEW DS    0H
         MVC   MEMBNNAM(8),0(R3)   MOVE MEMBER NAME
         LA    R6,08(,R3)          STARTING TTR
         LA    R7,MEMBNSTT         RECEIVING FIELD
         LA    R8,3                LENGTH
         BAL   R9,HEXIT            GO CONVERT
         MVC   MEMBNSRR(2),MEMBNDOT     SEPARATE
         MVI   MEMBNDOT,C'.'       SEPARATOR
         LA    R6,20(,R3)          ATTRIBUTE FLAGS
         LA    R7,MEMBNATT
         LA    R8,2
         BAL   R9,HEXIT
         LA    R6,22(,R3)          AMOUNT OF MAIN STORAGE NEEDED
         LA    R7,MEMBNLEN
         LA    R8,3
         BAL   R9,HEXIT
         BR    R5
         SPACE 3
GETOLD   DS    0H
         L     R1,NEXTOLD          ADDRESS OF NEXT MEMBER
         LTR   R1,R1               IS THERE ONE?
         BNZ   FINDNOLD            YES - GO FIND NEXT ONE
         GET   OLDLPA              NO - VISIT MY LOCAL ACCESS METHOD
         LH    R0,0(,R1)           AMOUNT OF BLOCK USED
         SH    R0,=H'2'            SUBTRACT LENGTH OF THIS FIELD
         STH   R0,OLDLEN           STORE
         XC    OLDONE(2),OLDONE    INITIALIZE AMOUNT USED
         AP    OLDDIRBK(3),=P'1'   COUNT THE DIRECTORY BLOCK
         LA    R1,2(,R1)           POINT PAST LENGTH
         ST    R1,NEXTOLD          STORE AS NEXT MEMBER
FINDNOLD DS    0H
         SLR   R15,R15
         IC    R15,11(,R1)         INDICATORS
         N     R15,=F'31'          TURN OFF TOP 3 BITS
         SLL   R15,1               MULTIPLY BY 2
         LA    R15,12(,R15)        ADD 12-BYTE BASE
         LR    R0,R15              SAVE FOR NOW
         AH    R0,OLDONE           ADD PREVIOUS COUNT
         CH    R0,OLDLEN           EQUAL TO LENGTH?
         BE    LASTOLD             YES - THIS IS LAST MEMBER IN BLOCK
         STH   R0,OLDONE           NO - STORE CURRENT COUNT
         A     R15,NEXTOLD         ADD ADDRESS OF THIS MEMBER
         ST    R15,NEXTOLD         AND STORE BACK
         BR    R9
LASTOLD  DS    0H
         XC    NEXTOLD(4),NEXTOLD  CLEAR NEXT MEMBER POINTER
         BR    R9                  RETURN TO CALLER
         SPACE 3
GETNEW   DS    0H
         L     R1,NEXTNEW          ADDRESS OF NEXT MEMBER
         LTR   R1,R1               IS THE4RE ONE?
         BNZ   FINDNNEW            YES - GO FIND NEXT ONE
         GET   NEWLPA              NO - VISIT MY LOCAL ACCESS METHOD
         LH    R0,0(,R1)           AMOUNT OF BLOCK USED
         SH    R0,=H'2'            SUBTRACT LENGTH OF THIS FIELD
         STH   R0,NEWLEN           STORE
         XC    NEWONE(2),NEWONE    INITIALIZE AMOUNT USED
         AP    NEWDIRBK(3),=P'1'   COUNT THE DIRECTORY BLOCK
         LA    R1,2(,R1)           POINT PAST LENGTH
         ST    R1,NEXTNEW          STORE AS NEXT MEMBER
FINDNNEW DS    0H
         SLR   R15,R15
         IC    R15,11(,R1)         INDICATORS
         N     R15,=F'31'          TURN OFF TOP 3 BITS
         SLL   R15,1               MULTIPLY BY 2
         LA    R15,12(,R15)        ADD 12-BYTE BASE
         LR    R0,R15              SAVE FOR NOW
         AH    R0,NEWONE           ADD PREVIOUS COUNT
         CH    R0,NEWLEN           EQUAL TO LENGTH?
         BE    LASTNEW             YES - THIS IS LAST MEMBER IN BLOCK
         STH   R0,NEWONE           NO - STORE CURRENT COUNT
         A     R15,NEXTNEW         ADD ADDRESS OF THIS MEMBER
         ST    R15,NEXTNEW         AND STORE BACK
         BR    R9
LASTNEW  DS    0H
         XC    NEXTNEW(4),NEXTNEW  CLEAR NEXT MEMBER POINTER
         BR    R9                  RETURN TO CALLER
         SPACE 3
WRITEHDG DS    0H
         AP    PAGECNT,=P'1'      BUMP PAGE NUMBER
         UNPK  HDR1PAGE(3),PAGECNT
         OI    HDR1PAGE+2,X'F0'
         LA    R10,5               NUMBER OF LINES TO PRINT
         LA    R0,HEADING1         START OF FIRST HEADING
HDG1LOOP DS    0H
         PUT   SYSPRINT            WRITE HEADING
         AH    R0,=H'133'          BUMP TO NEXT HEADING
         BCT   R10,HDG1LOOP        AND THEN THERE WERE NONE
         ZAP   LINECNT(2),=P'5'    INITIALIZE COUNT
         BR    R9                  RETREAT
         SPACE 3
HDR8HDR9 DS    0H
         LA    R10,2               LOOP TWICE
HDR8LOOP DS    0H
         PUT   SYSPRINT,HEADING8   SPACER
         AP    LINECNT,=P'1'       BUMP COUNTER
         BCT   R10,HDR8LOOP
         PUT   SYSPRINT,HEADING9   WORK HEADER
         PUT   SYSPRINT,HEADING8   SPACER
         AP    LINECNT,=P'2'       BUMP COUNT
         BR    R9                  RETURN TO CALLER
         SPACE 3
HEXIT    DS    0H
         HEX   (R6),(R7),(R8)
         BR    R9
         SPACE 3
CONVDTIM DS    0H
         CLC   4(4,R4),=XL4'0'
         BNE   GOODATE1
         MVC   0(8,R5),=CL8'**/**/**'
         B     CONVTIMD
GOODATE1 DS    0H
         MVI   0(R5),X'FE'         INDICATOR TO DATETIME
         MVC   1(4,R5),4(R4)       MOVE DATE TO BE CONVERTED
         ST    R5,PARMADDR         STORE PARM ADDRESS
         MVI   PARMADDR,X'80'      SET VL BIT
         LA    R1,PARMADDR         POINT TO PARMADDR
         L     R15,=V(DATE)        EXTRY POINT ADDRESS
         BALR  R14,R15             DO IT TO IT
CONVTIMD DS    0H
         MVI   PARMADDR+3,X'0F'    MOVE SIGN
         MVO   PARMADDR(4),0(3,R4) APPEND HHMMSS
         MVC   WORKAREA(10),=XL10'402120204B20204B2020'
         ED    WORKAREA(10),PARMADDR EDIT INTO FORM HH.MM.SS
         MVC   18(8,R5),WORKAREA+2 PLACE INTO HEADER
         BR    R9
         SPACE 3
CONVXDAT DS    0H
         SLR   R0,R0               CLEAR WORK REGISTER
         ICM   R0,B'0001',0(R4)    YEAR
         BNZ   GOODATE2            NOT ZERO IS GOOD
BADATE2  DS    0H
         MVC   0(8,R5),=CL8'**/**/**' INVALID DATE
         BR    R9
GOODATE2 DS    0H
         CVD   R0,DOUBLE
         MVO   2(2,R5),DOUBLE+6(2) MOVE YEAR TO PARM FOR DATETIME
         ICM   R0,B'0011',1(R4)    DAY NUMBER
         BZ    BADATE2
         CVD   R0,DOUBLE
         MVC   3(2,R5),DOUBLE+6    MOVE DAY TO PARM FOR DATETIME
         MVI   1(R5),X'00'         MUST BE ZERO FOR DATETIME
         MVI   0(R5),X'FE'         INDICATE THAT I'M PASSING THE DATE
         ST    R5,PARMADDR         STORE PARM ADDRESS FOR DATETIME
         MVI   PARMADDR,X'80'      SET VL BIT
         LA    R1,PARMADDR         LOAD PARAMETER REGISTER
         L     R15,=V(DATE)        LOAD EPA
         BALR  R14,R15             GO CONVERT THE DATE
         BR    R9                  AND RETURN
         EJECT
HEADING1 DC    CL133'1L---P---A---C---O---M---P---R    COMPARES LPALIB $
               DIRECTORIES'
         ORG   HEADING1+125
         DC    CL5'PAGE '
HDR1PAGE DS    CL3
         SPACE 3
HEADING2 DC    CL133'- F  .............O---L---D......L---P---A---L---I$
               ---B............. ++ .............N---E---W......L---P--$
               -A---L---I---B.............'
         SPACE 3
HEADING3 DC    CL133'  L'
         ORG   HEADING3+21
         DC    CL5'DATE '
HDR3ODTE DS    CL11
         DC    CL7'  TIME '
HDR3OTIM DS    CL8
         ORG   HEADING3+68
         DC    CL2'++'
         ORG   HEADING3+87
         DC    CL5'DATE '
HDR3NDTE DS    CL11
         DC    CL7'  TIME '
HDR3NTIM DS    CL8
         ORG
         SPACE 3
HEADING4 DC    CL133'  A'
         ORG   HEADING4+5
         DC    CL4'DSN='
HDR4ODSN DS    CL45
         DC    CL7'VOLSER='
HDR4OVSN DS    CL7
         DC    CL7'++ DSN='
HDR4NDSN DS    CL45
         DC    CL7'VOLSER='
HDR4NVSN DS    CL6
         ORG
         SPACE 3
HEADING5 DC    CL133'  G'
         ORG   HEADING5+10
         DC    CL9'FMT1-TTR='
HDR5OTTR DS    CL7
         DC    CL10'  CREATED '
HDR5OCRD DS    CL8
         DC    CL10'  EXPIRES '
HDR5OEXP DS    CL8
         ORG   HEADING5+68
         DC    CL2'++'
         ORG   HEADING5+76
         DC    CL9'FMT1-TTR='
HDR5NTTR DS    CL7
         DC    CL10'  CREATED '
HDR5NCRD DS    CL8
         DC    CL10'  EXPIRES '
HDR5NEXP DS    CL8
         ORG
         SPACE 3
HEADING6 DC    CL133' '
         ORG   HEADING6+17
         DC    C'EXTENT  CUA  CCCC-HHHH  TO  CCCC-HHHH '
         ORG   HEADING6+68
         DC    C'++'
         ORG   HEADING6+83
         DC    C'EXTENT  CUA  CCCC-HHHH  TO  CCCC-HHHH '
         ORG
         SPACE 3
HEADING7 DC    CL133' '
         ORG   HEADING7+19
HDR7OXT# DS    CL2
         DS    CL4
HDR7OCUA DS    CL3
         DS    CL2
HDR7OSCC DS    CL4
HDR7OSDT DS    CL1
HDR7OSHH DS    CL4
HDR7OTOX DS    CL6
HDR7OECC DS    CL4
HDR7OEDT DS    CL1
HDR7OEHH DS    CL4
         ORG   HEADING7+68
         DC    C'++'
         ORG   HEADING7+85
HDR7NXT# DS    CL2
         DS    CL4
HDR7NCUA DS    CL3
         DS    CL2
HDR7NSCC DS    CL4
HDR7NSDT DS    CL1
HDR7NSHH DS    CL4
HDR7NTOX DS    CL6
HDR7NECC DS    CL4
HDR7NEDT DS    CL1
HDR7NEHH DS    CL4
         ORG
         SPACE 3
HEADING8 DC    CL133' '
         ORG   HEADING8+68
         DC    C'++'
         ORG
         SPACE 3
HEADING9 DC    CL133' --- -MEMBER-  TTTT.RR  ATTR  LENGTH  N------O----$
               --T------E------S ++ -MEMBER-  TTTT.RR  ATTR  LENGTH  N-$
               -----O------T------E------S'
         SPACE 3
MEMBDATA DC    CL133' '
         ORG   MEMBDATA+1
MEMBFLAG DS    CL3,CL1
MEMBONAM DS    CL8,CL2
MEMBOSTT DS    CL4
MEMBODOT DS    CL1
MEMBOSRR DS    CL2,CL2
MEMBOATT DS    CL4,CL2
MEMBOLEN DS    CL6,CL2
MEMBONTE DS    CL29
         DC    CL4' ++ '
MEMBNNAM DS    CL8,CL2
MEMBNSTT DS    CL4
MEMBNDOT DS    CL1
MEMBNSRR DS    CL2,CL2
MEMBNATT DS    CL4,CL2
MEMBNLEN DS    CL6,CL2
MEMBNNTE DS    CL29
         ORG
DUPEDATA DC    CL133'+'
         ORG   DUPEDATA+1
DUPEFLAG DS    CL3,CL1
DUPEONAM DS    CL8,CL2
DUPEOSTT DS    CL4
DUPEODOT DS    CL1
DUPEOSRR DS    CL2,CL2
DUPEOATT DS    CL4,CL2
DUPEOLEN DS    CL6,CL2
DUPEONTE DS    CL29
         DC    CL4'    '
DUPENNAM DS    CL8,CL2
DUPENSTT DS    CL4
DUPENDOT DS    CL1
DUPENSRR DS    CL2,CL2
DUPENATT DS    CL4,CL2
DUPENLEN DS    CL6,CL2
DUPENNTE DS    CL29
         ORG
CURRTIME DS    D
PARMADDR DS    D
DOUBLE   DS    D
WORKAREA DS    2D
CNTLDATA DS    0D
CNTLTIME DS    CL4
CNTLDATE DS    CL4
CNTLJFCB DS    CL176
         DS    CL72
CNTLXTNT DS    0D
CNTL#EXT DS    XL1
CNTLALLX DS    16XL11
         DS    CL79
NEXTOLD  DC    F'0'
NEXTNEW  DC    F'0'
OLDLEN   DS    H
OLDONE   DS    H
NEWLEN   DS    H
NEWONE   DS    H
XFFS     DC    8X'FF'
COUNT    DS    PL2
LINECNT  DS    PL2
PAGECNT  DC    PL2'0'
RC       DS    XL1
OLDDIRBK DC    PL3'0'
NEWDIRBK DC    PL3'0'
OLDALIAS DC    PL3'0'
OLDMEMBR DC    PL3'0'
NEWALIAS DC    PL3'0'
NEWMEMBR DC    PL3'0'
CHNGEMSG DC    CL15''' BYTES XXXXXXX'
EODMSG   DC    CL55'E---N---D    O---F    D---I---R---E---C---T---O---R$
               ---Y'
BLKSCONS DC    CL34'XXX,XXX DIRECTORY BLOCKS PROCESSED'
         ORG   BLKSCONS
         DC    XL7'4020206B202120'
         ORG
MEMBCONS DC    CL34'XXX,XXX REAL MEMBERS PROCESSED    '
         ORG   MEMBCONS
         DC    XL7'4020206B202120'
         ORG
ALIACONS DC    CL34'XXX,XXX ALIASES PROCESSED         '
         ORG   ALIACONS
         DC    XL7'4020206B202120'
         ORG
         EJECT
OLDLPA   DCB   DDNAME=OLDLPA,DSORG=PS,MACRF=GL,EODAD=*-*,              $
               RECFM=F,LRECL=256,BLKSIZE=256
         EJECT
NEWLPA   DCB   DDNAME=NEWLPA,DSORG=PS,MACRF=GL,EODAD=RETURN08,         $
               RECFM=F,LRECL=256,BLKSIZE=256
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      $
               RECFM=FA,LRECL=133,BLKSIZE=133,BUFNO=1
         EJECT
         LTORG
         END   LPACOMPR
