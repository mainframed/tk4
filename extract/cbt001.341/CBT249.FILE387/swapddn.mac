SWAPDDN  CSECT
         REGS
         SAVE  (14,12),,(SWAPDDN..&SYSDATE..&SYSTIME...JAR)
         LR    R12,R15
         USING SWAPDDN,R12
         L     R2,0(,R1)         PICK UP PARM ADDRESS
         IEFSAVER  ,DSECTLEN
         USING WORKSECT,R13
         MVI   OLDDDN,X'40'
         MVC   OLDDDN+1(15),OLDDDN
         LH    R3,2(,R2)
         LA    R4,4(R3,R2)
         LA    R5,OLDDDN
         LH    R6,0(,R2)
         LA    R7,0(R6,R2)
         SLR   R8,R8
OLDLOOP  DS    0H
         CLI   0(R4),C'/'
         BE    NEWDD
         CH    R8,=H'8'
         BNL   OLD2LONG
         MVC   0(1,R5),0(R4)
         LA    R4,1(,R4)
         LA    R5,1(,R5)
         LA    R8,1(,R8)
         B     OLDLOOP
NEWDD    DS    0H
         LA    R5,NEWDDN
         SLR   R8,R8
         LA    R4,1(,R4)
NEWLOOP  DS    0H
         CLI   0(R4),C'/'
         BE    SWAPEM
         CH    R8,=H'8'
         BNL   NEW2LONG
         CR    R4,R7
         BH    NEW2LONG
         MVC   0(1,R5),0(R4)
         LA    R4,1(,R4)
         LA    R5,1(,R5)
         LA    R8,1(,R8)
         B     NEWLOOP
SWAPEM   DS    0H
         OC    OLDDDN(16),=CL16' '
         MODESET KEY=ZERO
         TIOTSCAN OLDDDN,NOOLDDDN
         MVC   4(8,R15),NEWDDN
         MODESET KEY=NZERO
IEFRELSE IEFRELSE
         RETURN (14,12),T,RC=0
NOOLDDDN MVC   NOOLDMSG(8),OLDDDN
         TPUT  MSG1,L'MSG1
         B     IEFRELSE
OLD2LONG TPUT  MSG2,L'MSG2
         B     IEFRELSE
NEW2LONG TPUT  MSG3,L'MSG3
         B     IEFRELSE
MSG1     DC    C'I can''t find the DD statement named xxxxxxxx'
NOOLDMSG EQU   *-8
MSG2     DC    C'Old DDN too long or no ending slash.'
MSG3     DC    C'New DDN too long or no ending slash.'
WORKSECT DSECT
SAVEAREA DS    9D
OLDDDN   DS    D
NEWDDN   DS    D
DSECTLEN EQU   16
         END   SWAPDDN
