         MACRO
&LABEL   IEFSAVER    &P1,&P2,&PAD,&DSECT=
         LCLA  &SP
         LCLC  &EXTRA
         LCLC  &PADCHAR
         AIF ('&LABEL' EQ '').A0
&LABEL   DS    0H
.A0      ANOP
         AIF   (T'&P1 NE 'O').NSPZERO
&SP      SETA  0
         AGO   .GETMORE
.NSPZERO ANOP
&SP      SETA  &P1
.GETMORE ANOP
&PADCHAR SETC  '0'
         AIF   (T'&PAD EQ 'O').SEEIFEX
&PADCHAR SETC  '&PAD'
.SEEIFEX ANOP
&EXTRA   SETC  '0'
         AIF   (T'&P2 EQ 'O').SETUP
&EXTRA   SETC  '&P2'
.SETUP   ANOP
         GETMAIN R,LV=72+&EXTRA,SP=&SP  GET NEW SAVE AREA
         XC    0(72,1),0(1)             ZERO OUT SAVE AREA (18 WORDS)
         ST    13,4(0,1)                FORWARD CHAIN SAVE AREAS
         ST    1,8(0,13)                BACKWARD CHAIN SAVE AREAS
         LR    13,1                     POINT R13 TO SAVE AREA
         AIF   (T'&DSECT EQ 'O').NOUSING
         USING &DSECT,13                DSECT ADDRESSABILITY
.NOUSING ANOP
         AIF   (T'&P2 EQ 'O').MEXIT
         LA    14,72(,13)               POINT PAST SAVEAREA PROPER
         L     15,IEF&SYSNDX            GET LENGTH TO ZERO OUT
         SLR   0,0
         LA    1,X'&PADCHAR'(,0)        SET UP PAD CHARACTER
         SLL   1,24
         MVCL  14,0                     INITALIZE GETMAINED AREA
         B     IEF&SYSNDX+4             BRANCH AROUND EXTRA LENGTH
IEF&SYSNDX DC  A(&EXTRA)                LENGTH OF EXTRA WORK AREA
.MEXIT   ANOP
         MEND
