FOXYXREF CSECT
         REGS
         SAVE  (14,12),,(FOXYXREF..&SYSDATE..&SYSTIME...JAR)
         LR    R2,R1               ADDRESS OF ORIGINAL ASSEMBLER PARMS
         LR    R12,R15
         USING FOXYXREF,R12
         IEFSAVER
         LR    R1,R2               RESTORE ORIGINAL PARMS
         LINK  EP=IFOX00           ASSEMBLE AWAY!
         ST    R15,IFOX00CC        STORE ASSEMBLER'S RETURN-CODE
         LTR   R15,R15             GOOD ASSEMBLY?
         BZ    XREFPROC            YES - GO PROCESS LISTING
IEFRELSE IEFRELSE
         L     R15,IFOX00CC        PICK UP ASSEMBLER'S RETURN CODE
         RETURN (14,12),T,RC=(15)  BACK TO WHOEVER CALLED ME
XREFPROC OPEN  SYSPRINT            PREPARE TO READ ASSEMBLER LISTING
         GETMAIN    R,LV=4096      RIP OFF A PAGE
         LR    R2,R1               R2 IS BASE FOR MNEMONIC TABLE
         LR    R4,R2               RECIEVING FIELD
         LH    R5,=H'4096'         LENGTH
         SLR   R7,R7               ZERO SENDING FIELD LENGTH
         LR    R6,R7               NO SENDING FIELD
         MVCL  R4,R6               ZERO OUT NEW PAGE
         MVI   4092(R2),X'FF'      FENCE
GETINPUT GET   SYSPRINT
         LR    R10,R1
         USING ASMLIST,R10
FINDTIME NOP   FINDPAGE
         CLC   98(8,R10),=CL8'ASM 0201'
         BNE   FINDPAGE
         MVI   FINDTIME+1,X'F0'
         MVC   HEADATIM(23),98(R10)
         B     GETINPUT
FINDPAGE CLC   ASMPAGET(5),=CL5'PAGE '
         BNE   RUNITOUT
         MVC   MAXPAGE#(4),ASMPGNUM
         B     GETINPUT
RUNITOUT NOP   GETINPUT            BRANCH IS SET AFTER I FIND END CARD
FINDLINE TM    ASMSTSEQ+4,X'F0'         IS THIS MY NEXT EXPECTED LINE?
         BNO   GETINPUT                 NO - IGNORE IT
         CLI   ASMCOMNT,C'*'            IS THIS A COMMENT CARD? (SRC)
         BE    GETINPUT                 YES - IGNORE
         CLC   ASMCOMNT(2),=C'.*'       IS THIS A COMMENT CARD? (MACRO)
         BE    GETINPUT                 YES - IGNORE
         LA    R3,ASMCOMNT         START OF ASSEMBLER STATEMENT
         LA    R4,ASMPAGET         END OF ASSEMBLER STATEMENT
PASSNAME CLI   0(R3),C' '          SPACE BEFORE OPERATION?
         BE    FINDOPER
         LA    R3,1(,R3)           NO - CHECK NEXT BYTE
         CR    R3,R4               ANY MORE BYTES?
         BL    PASSNAME            YES
         B     GETINPUT            NO - IGNORE
FINDOPER LA    R3,1(,R3)           BUMP TO NEXT COLUMN
         CR    R3,R4               ARE THERE ANY LEFT?
         BNL   GETINPUT            NO - IGNORE CARD
         CLI   0(R3),C'='          SELF-DEFINING TERM (AFTER LTORG)?
         BE    GETINPUT            YES - IGNORE
         CLI   0(R3),C' '          HAVE I FOUND A MNEMONIC YET?
         BE    FINDOPER            NO - KEEP A-LOOKIN'
         CLI   0(R3),C'A'          CHECK FOR ALPHABETIC IN FIRST BYTE
         BL    GETINPUT
         CLI   0(R3),C'I'
         BNH   INITMNEM
         CLI   0(R3),C'J'
         BL    GETINPUT
         CLI   0(R3),C'R'
         BNH   INITMNEM
         CLI   0(R3),C'S'
         BL    GETINPUT
         CLI   0(R3),C'Z'
         BH    GETINPUT
INITMNEM DS    0H
         MVC   MNEMONIC(8),=CL8' ' YES - SPACE OUT FIELD
         LA    R5,MNEMONIC
XFEROPER MVC   0(1,R5),0(R3)       MOVE 1 BYTE OF OPERATION
         LA    R3,1(,R3)           BUMP SOURCE
         LA    R5,1(,R5)           BUMP TARGET
         CR    R3,R4               ANY MORE LEFT?
         BNL   OPERSCAN            NO - SEARCH THE TABLE
         CLI   0(R3),C' '          END OF OPERATION?
         BNE   XFEROPER            NO - TRANSFER NEXT BYTE
OPERSCAN LR    R3,R2
         CLC   MNEMONIC(4),=CL4'END '  IS THIS AN END CARD?
         BNE   SCANLOOP            NO - GO PROCESS
         MVI   RUNITOUT+1,X'F0'
SCANLOOP CLC   0(8,R3),MNEMONIC    DO I HAVE THIS GUY TABLED ALREADY?
         BE    STOWSEQ#            YES - STOW HIS SEQUENCE NUMBER
         CLI   0(R3),X'00'         EXD OF FILLED TABLE?
         BE    STOWOPER            YES - STOW NEW MNEMONIC
         CLI   0(R3),X'FF'         END OF TABLE?
         BE    OTBLFULL            YES - COUNT & KILL
         LA    R3,12(,R3)          NO - BUMP TO NEXT ENTRY
         B     SCANLOOP
STOWOPER MVC   0(8,R3),MNEMONIC    STASH MNEMONIC NAME
         GETMAIN    R,LV=4096      RIP OFF ANOTHER PAGE
         LR    R4,R1               RECIEVING FIELD
         LH    R5,=H'4096'         LENGTH
         SLR   R7,R7               ZERO SENDING FIELD LENGTH
         LR    R6,R7               NO SENDING FIELD
         MVCL  R4,R6               ZERO OUT NEW PAGE
         MVI   4088(R1),X'FF'      DA FENCE
         ST    R1,8(,R3)           STORE ADDRESS OF SEQ# TABLE
STOWSEQ# L     R4,8(,R3)           PICK UP ADDRESS OF SEQ# TABLE
         SLR   R6,R6
         ICM   R6,B'0011',0(R4)
         LA    R6,1(,R6)
         STCM  R6,B'0011',0(R4)
         L     R6,TOTOPS
         LA    R6,1(,R6)
         ST    R6,TOTOPS
         LA    R4,2(,R4)
STOW#LUP CLI   0(R4),X'00'         IS THIS SLOT AVAILABLE?
         BNE   OCCUPIED            NO - SEE WHO'S SITTING THERE
         MVC   0(6,R4),ASMSTSEQ    YES - STORE SEQ# AND MAC IDENT
         OC    0(5,R4),=CL5'00000' LEADING ZEROES FOR THE XREF
         B     GETINPUT
OCCUPIED CLI   0(R4),X'FF'         HAVE I BLOWN MYSELF OUT OF THE TABLE
         BE    STBLFULL            YUP
         LA    R4,6(,R4)           NO - POINT TO NEXT ENTRY
         B     STOW#LUP
OTBLFULL LR    R5,R3
         B     ADDNPROC
STBLFULL LR    R5,R4
ADDNPROC SLR   R6,R6
         ICM   R6,B'0111',1(R5)
         LA    R6,1(,R6)
         STCM  R6,B'0111',1(R5)
         B     GETINPUT
NODDSTMT ABEND 1,DUMP
         DROP  R10
EOFINPUT CLOSE SYSPRINT            CLOSE INPUT FILE
         LR    R3,R2               MNEMONIC NAME TABLE
SRTLOOP1 TM    12(R3),X'FF'        IS SORT COMPLETED?
         BC    B'1011',SORTED      BRANCH-NOT-MIXED
         LA    R4,12(,R3)          START R4 AT NEXT ENTRY
SRTLOOP2 CLC   0(8,R3),0(R4)       DO THEY NEED SWITCHING?
         BNH   BUMPREG4            NO - BUMP TO NEXT ENTRY
         XC    0(12,R3),0(R4)      SWITCH
         XC    0(12,R4),0(R3)         THINGS
         XC    0(12,R3),0(R4)            AROUND
BUMPREG4 LA    R4,12(,R4)          BUMP R4 TO POINT TO NEXT ENTRY
         TM    0(R4),X'FF'         HAVE I HIT THE TOP ON THIS PASS?
         BM    SRTLOOP2            NO - DO THE NEXT COMPARE
         LA    R3,12(,R3)          YES - POINT TO NEXT BASE
         B     SRTLOOP1            AND CONTINUE
SORTED   SCANTIOT   SYSPRINT+40,NODDSTMT
         ICM   R3,B'0111',12(R15)  JFCB PREFIX ADDRESS
         LA    R3,16(,R3)          POINT TO THE JFCB ITSELF
         MODESET KEY=ZERO
         NI    87(R3),X'3F'        TURN OFF NEW/OLD/MOD FLAGS
         OI    87(R3),X'80'        TURN ON MOD FLAG
         MODESET KEY=NZERO
         OPEN  (SYSPRINT,OUTPUT)   OPEN IT AT THE TAIL END
         LR    R3,R2               POINT TO MNEMONIC TABLE
         PACK  DOUBLE(8),MAXPAGE#(4)
NEWNEW   B     FIRSTYME
         LA    R3,12(,R3)
FIRSTYME MVI   NEWNEW+1,X'00'
         TM    0(R3),X'FF'         AM I AT END-OF-TABLE?
         BC    B'1011',PRINTED     BRANCH-NOT-MIXED
         L     R4,8(,R3)           ADDRESS OF SEQ# TABLE
         SLR   R7,R7
         SLR   R6,R6
         ICM   R7,B'0011',0(R4)
         LA    R4,2(,R4)
         MH    R7,=H'10000'
         D     R6,TOTOPS
         CVD   R7,DBL1
         OI    DBL1+7,X'0F'
         UNPK  PCT(4),DBL1+5(3)
         MVC   PPCT(2),PCT
         MVI   PPCT+2,C'.'
         MVC   PPCT+3(2),PCT+2
         MVC   DATAMN(8),0(R3)     MNEMONIC
         LA    R5,DATAREF
         SLR   R6,R6
SEQ#LOOP CH    R6,=H'13'           IS THIS LINE FULL
         BL    INSERT#             NO - GO INSERT THE SEQ#
PUTALINE CP    LINECNT(2),=P'58'   IS THIS PAGE FULL?
         BL    PUTHISLN            NO - PUT THIS LINE
         ZAP   LINECNT(2),=P'4'
         AP    DOUBLE(8),=P'1'     ADD TO PAGE NUMBER
         MVC   MAXPAGE#(6),PATTERN
         ED    MAXPAGE#(6),DOUBLE+5
         MVC   HEADPGNM(4),MAXPAGE#+2
         PUT   SYSPRINT,HEADING1
         PUT   SYSPRINT,HEADING2
         MVI   DATACC,X'F0'
PUTHISLN AP    LINECNT(2),=P'1'
         CLI   DATACC,X'F0'
         BNE   SKIPADD
         AP    LINECNT(2),=P'1'
SKIPADD  PUT   SYSPRINT,DATALINE
         SLR   R6,R6
         LA    R5,DATAREF
         MVI   DATACC,X'40'
         MVC   DATACC+1(120),DATACC
         CLI   0(R4),X'00'
         BE    NEWNEW
         CLI   0(R4),X'FF'
         BNE   INSERT#
         SLR   R0,R0
         ICM   R0,B'0111',1(R4)
         CVD   R0,DBL1
         MVC   REFOVER+12(16),OVERMASK
         ED    REFOVER+12(16),DBL1
         MVC   REFOVER+53(8),0(R3)
         PUT   SYSPRINT,REFOVER
         MVI   REFOVER,C'+'
         PUT   SYSPRINT,REFOVER
         PUT   SYSPRINT,REFOVER
         PUT   SYSPRINT,REFOVER
         PUT   SYSPRINT,REFOVER
         MVI   REFOVER,C' '
         AP    LINECNT(2),=P'1'
         MVI   DATACC,X'F0'
         B     NEWNEW
INSERT#  TM    0(R4),X'FF'
         BC    B'1011',PUTALINE    BRANCH-NOT-MIXED
         MVC   0(6,R5),0(R4)       MOVE REFERENCE IN
         LA    R4,6(,R4)           BUMP SOURCE
         LA    R5,8(,R5)           BUMP TARGET
         LA    R6,1(,R6)           BUMP COUNTER
         B     SEQ#LOOP
PRINTED  CLI   0(R3),X'FF'
         BNE   CLOSEPRT
         SLR   R0,R0
         ICM   R0,B'0111',1(R3)
         CVD   R0,DBL1
         MVC   OPOVER+12(16),OVERMASK
         ED    OPOVER+12(16),DBL1
         PUT   SYSPRINT,OPOVER
CLOSEPRT CLOSE SYSPRINT
         B     IEFRELSE
         EJECT
DOUBLE   DS    D
DBL1     DS    D
IFOX00CC DS    F
TOTOPS   DC    F'0'
PCT      DS    CL4
MAXPAGE# DS    CL6
EDITAREA DS    CL6
         ORG   EDITAREA+1
EXPLINE# DC    CL5'    1'
PATTERN  DC    XL6'402020202120'
OVERMASK DC    XL16'5C202020202020202020202020202120'
MNEMONIC DS    CL8
LINECNT  DC    PL2'100'
HEADING1 DC    CL121'1'
         ORG   HEADING1+49
         DC    CL24'MNEMONIC CROSS-REFERENCE'
         ORG   HEADING1+112
         DC    CL5'PAGE '
HEADPGNM DC    CL4' '
         ORG
HEADING2 DC    CL121'-MNEMONIC PERCENTAGE   INVOKED ON STATEMENT'
         ORG   HEADING2+98
HEADATIM DS    CL23
DATALINE DC    CL121'0'
         ORG   DATALINE
DATACC   DS    CL1
DATAMN   DS    CL8
         DS    C
PPCT     DS    CL5
         DS    CL2
DATAREF  DS    13CL8
         ORG
REFOVER  DC    CL121'                             REFERENCES TO MNEMONI$
               C /XXXXXXXX/ COULD NOT BE PRINTED DUE TO SPACE LIMITATIO$
               NS'
OPOVER   DC    CL121'                             ADDITIONAL MNEMONIC R$
               EFERENCES COULD NOT BE PRINTED DUE TO SPACE LIMITATIONS'
         LTORG
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(GL,PM),EODAD=EOFINPUT
         EJECT
ASMLIST  DSECT
         DS    CL121
         ORG   ASMLIST+35
ASMSTSEQ DS    CL5                 STATEMENT SEQUENCE NUMBER
ASMMACRO DS    CL1                 '+' INDICATES MACRO GENERATION
ASMCOMNT DS    CL1                 '*' INDICATES A COMMENT CARD
         ORG   ASMLIST+112
ASMPAGET DS    CL5                 'PAGE ' ON HEADING LINE
ASMPGNUM DS    CL4                 ACTUAL PAGE NUMBER
         ORG
         END
