PW2MATCH CSECT
         REGS
         $SAVE JAR
         LR    R12,R15
         USING PW2MATCH,R12
         IEFSAVER
         OPEN  (PROMPTS,(INPUT),                                       $
               REPLIES,(INPUT),                                        $
               MERGED,(OUTPUT))
GETBOTH  DS    0H
         GET   PROMPTS,PROMAREA
         GET   REPLIES,REPLAREA
COMPARE  DS    0H
         CLC   PROMAREA+25(2),REPLAREA+27  COMPARE REPLY ID'S
         BH    BUMPREPL      REPLY IS LOW, SKIP IT
         BL    BUMPPROM      PROMPT IS LOW, BUMP IT
         MVC   JOB#(8),PROMAREA+15           MOVE JOB NUMBER
         MVC   JOBNAME(8),PROMAREA+42        MOVE JOB NAME
         MVC   REPLYID(2),PROMAREA+25        MOVE REPLY ID
         MVC   PROMTIME(8),PROMAREA+6        MOVE PROMPT TIME
         MVC   REPLTIME(8),REPLAREA+6        MOVE REPLY TIME
*
         CLC   PROMAREA+51(7),=CL7'DSNAME '  IS THIS A VSAM FILE?
         BE    MOVEDSNM                      YES, MOVE DATASET NAME
*
         MVC   STEPNAME(8),PROMAREA+56       MOVE STEP NAME
         MVC   DDNAME(8),PROMAREA+72         MOVE DD NAME
         MVC   DSNAME(44),NULLIFY            NULLIFY DATASET NAME
         B     PUTMERGE                      GO WRITE THE MERGED RECORD
*
MOVEDSNM DS    0H
         MVC   DSNAME(44),PROMAREA+58        MOVE DATASET NAME
         MVC   STEPNAME(8),NULLIFY           NULLIFY STEP NAME
         MVC   DDNAME(8),NULLIFY             NULLIFY DD NAME
*
PUTMERGE DS    0H
         PUT   MERGED,PWRECORD               WRITE PASSWORD RECORD
         B     GETBOTH
BUMPREPL DS    0H
         GET   REPLIES,REPLAREA
         B     COMPARE
BUMPPROM DS    0H
         GET   PROMPTS,PROMAREA
         B     COMPARE
ENDOFILE DS    0H
         CLOSE (PROMPTS,,                                              $
               REPLIES,,                                               $
               MERGED)
         IEFRELSE
         RETURN (14,12),RC=0
PROMAREA DS    CL133
REPLAREA DS    CL133
PWRECORD DS    0CL96    OUTPUT RECORD
JOB#     DS    CL8      JES2 JOB NUMBER
JOBNAME  DS    CL8      JOB NAME
STEPNAME DS    CL8      STEP NAME (IFF NON-VSAM FILE)
DDNAME   DS    CL8      DD NAME (IFF NON-VSAM FILE)
REPLYID  DS    CL2      REPLY ID FOR PASSWORD PROMPT
PROMTIME DS    CL8      TIME PROMPTED FOR PASSWORD
REPLTIME DS    CL8      TIME RESPONDED TO PROMPT
DSNAME   DS    CL44     DATASET NAME (IFF VSAM FILE)
         DC    CL2'  '  SLACK
*
NULLIFY  DC    44CL1'-' MASK FOR NULL VALUES
*
PROMPTS  DCB   DDNAME=PROMPTS,DSORG=PS,MACRF=GM,EODAD=ENDOFILE
REPLIES  DCB   DDNAME=REPLIES,DSORG=PS,MACRF=GM,,EODAD=ENDOFILE
MERGED   DCB   DDNAME=MERGED,DSORG=PS,MACRF=PM,                        $
               LRECL=96,BLKSIZE=19008,RECFM=FB
         END   PW2MATCH
