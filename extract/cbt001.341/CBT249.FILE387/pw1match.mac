PW1MATCH CSECT
         REGS
         $SAVE JAR
         LR    R12,R15
         USING PW1MATCH,R12
         IEFSAVER
         OPEN  (SYSLOG,(INPUT),                                        $
               PROMPTS,(OUTPUT),                                       $
               REPLIES,(OUTPUT))
GETALONG GET   SYSLOG
         LR    R0,R1     LOAD ADDRESS OF CURRENT RECORD
         CLC   28(7,R1),=CL7'IEC301A'   IS THIS A PASSWORD PROMPT?
         BE    DOPROMPT         YES, GO PROCESS
         CLC   29(12,R1),=CL12',SUPPRESSED ' IS THIS A REPLY?
         BNE   GETALONG         YES,GO PROCESS
DOREPLY  DS    0H
         PACK  DOUBLE(8),27(2,R1)  PACK REPLY ID INTO DOUBLEWORD
         CVB   R2,DOUBLE        CONVERT INTO INDEX
         LA    R2,REPLYMAP(R2)  POINT TO APPROPRIATE SLOT
         CLI   0(R2),X'FF'      IS THIS IN RESPONSE TO IEC301A?
         BNE   GETALONG         NO, FORGET IT!
         MVI   0(R2),X'00'      CLEAR INDICATOR
         PUT   REPLIES          WRITE RECORD TO THE REPLIES FILE
         B     GETALONG
DOPROMPT DS    0H
         PACK  DOUBLE(8),25(2,R1)  PACK REPLY ID INTO DOUBLEWORD
         CVB   R2,DOUBLE        CONVERT INTO INDEX
         LA    R3,X'FF'         PLACE A FOX-FOX INTO LO-ORD R3
         STC   R3,REPLYMAP(R2)  INDICATE THAT THIS IS AN IEC301A
         PUT   PROMPTS          WRITE RECORD TO THE PROMPTS FILE
         B     GETALONG
ENDOFILE DS    0H
         CLOSE (SYSLOG,,                                               $
               PROMPTS,,                                               $
               REPLIES)
         IEFRELSE
         RETURN (14,12),RC=0
DOUBLE   DC    D'0'
SYSLOG   DCB   DDNAME=SYSLOG,DSORG=PS,MACRF=GL,EODAD=ENDOFILE
PROMPTS  DCB   DDNAME=PROMPTS,DSORG=PS,MACRF=PM,                       $
               LRECL=133,BLKSIZE=19019,RECFM=FB
REPLIES  DCB   DDNAME=REPLIES,DSORG=PS,MACRF=PM,                       $
               LRECL=133,BLKSIZE=19019,RECFM=FB
REPLYMAP DC    100XL1'00'  REPLY MAP
         END   PW1MATCH
