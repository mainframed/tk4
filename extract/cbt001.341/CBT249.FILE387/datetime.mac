         ENTRY DATE,TIME
DATETIME CSECT
         REGS
DATE     SLR   R15,R15
TIME     STM   R14,R12,12(R13)
         BALR  R10,0
         USING *,R10
         SLR   R9,R9               SET RETURN CODE TO ZERO
         L     R4,0(,R1)
         LR    R3,R15
         IEFSAVER ,WORKLEN
         USING WORK,R13
         TIME  BIN
         LTR   R3,R3
         BNZ   TM
         CLI   0(R4),X'FE'         IS DATE SUPPLIED BYTES 2-5 00YYDDDF?
         BNE   DATEGO              NO, CURRENT DATE DESIRED
         CLI   1(R4),X'00'         ENSURE VALID 00YYDDDF SUPPLIED
         BNE   DATERR              IF INVALID, SET BAD RETURN CODE
         LA    R5,9                SET HIGHEST DECIMAL VALUE ALLOWED
         SLR   R6,R6               CLEAR WORK REG
         LA    R3,2(,R4)           GET YYDDDF ADDRESS
         NI    2(R3),X'F0'         KILL ZONE
         LA    R8,3                GET LOOP COUNT
         SPACE
VERDATE  IC    R6,0(,R3)           GET ONE BYTE OF DATE
         SRDL  R6,4                ISOLATE FOUR BITS
         CR    R6,R5               IS VALUE DECIMAL?
         BH    DATERR              NO, GO SET BAD RETURN CODE
         SLR   R6,R6               YES, CLEAR WORK REG
         SLDL  R6,4                GET LOW-ORDER FOUR BITS
         CR    R6,R5               IS VALUE DECIMAL?
         BH    DATERR              NO, GO SET BAD RETURN CODE
         LA    R3,1(,R3)           YES, BUMP TO NEXT BYTE OF DATE
         BCT   R8,VERDATE          CONTINUE TO VERIFY DATE
         SPACE
         OI    4(R4),X'0F'         RESTORE DATE TO PACKED FORMAT
         CP    3(2,R4),THREE66     IS DAY GT 366?
         BH    DATERR              YES, ERROR
         SPACE
         ICM   R1,B'1111',1(R4)    ELSE GET USER-SUPPLIED 00YYDDDF
DATEGO   ST    R1,DAT1
         MVC   DAT2+2(2),DAT1+2
         SRL   R1,12
         ST    R1,DAT1
         OI    DAT1+3,X'0F'
         MVI   DAY+2,C'/'
         MVI   DAY+5,C'/'
         UNPK  DAY+6(2),DAT1+2(2)
         DP    DAT1(4),=PL1'4'
         NI    SWITCH,255-LEAP
         TM    DAT1+3,X'F0'
         BNZ   NOLEAP
         OI    SWITCH,LEAP
         CP    DAT2+2(2),=PL2'60'
         BL    NOLEAP
         BE    FEB29
         SP    DAT2(4),=PL4'1'
NOLEAP   TM    SWITCH,LEAP
         BO    NOLEAPX
         CP    DAT2+2(2),THREE66   ENSURE DATE LT 366 FOR NO LEAP YEAR
         BNL   DATERR
NOLEAPX  LA    R2,TABLE1
         LA    R3,13
LOOP     BCT   R3,TAG2
         ABEND 99,DUMP
TAG2     CP    DAT2+2(2),0(2,2)
         BNH   TAG3
         LA    R2,2(,R2)
         B     LOOP
TAG3     SH    R2,=H'2'
         SP    DAT2+2(2),0(2,2)
         UNPK  DAY+3(2),DAT2+2(2)
         OI    DAY+4,X'F0'
         LH    R5,=H'13'
         SR    R5,R3
         BCTR  R5,0
         SLL   R5,1
         LA    R3,TABLE2
         AR    R3,R5
         MVC   DAY(2),0(R3)
         B     TAG4
FEB29    MVC   DAY(2),=CL2'02'
         MVC   DAY+3(2),=CL2'29'
TAG4     MVC   0(8,R4),DAY
         B     RETURN
TM       ST    R0,DAT1
         MVC   0(4,R4),DAT1
RETURN   IEFRELSE ,WORKLEN
         LR    R15,R9
         RETURN (14,12),T,RC=(15)
         SPACE
DATERR   LA    R9,8                SET DATE ERROR RETURN CODE
         B     RETURN
         EJECT
         DC    PL2'0'
TABLE1   DC    PL2'31'
         DC    PL2'59'
         DC    PL2'90'
         DC    PL2'120'
         DC    PL2'151'
         DC    PL2'181'
         DC    PL2'212'
         DC    PL2'243'
         DC    PL2'273'
         DC    PL2'304'
         DC    PL2'334'
THREE66  DC    PL2'366'
TABLE2   DC    CL24'010203040506070809101112'
         LTORG
         EJECT
WORK     DSECT
SAVE     DS    9D
DAT1     DC    F'0'
DAT2     DC    F'0'
DAY      DC    CL8'  /  /  '
SWITCH   DS    X
LEAP     EQU   X'80'
WORKLEN  EQU   *-WORK
         END   DATETIME
