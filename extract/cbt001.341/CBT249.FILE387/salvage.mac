SALVAGE  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
READDATA EQU   6                   COMMAND CODE FOR READ DATA
SERCHIDE EQU   X'31'               COMMAND CODE FOR SEARCH ID EQUAL
TIC      EQU   8                   COMMAND CODE FOR TRANSFER-IN-CHANNEL
CC       EQU   X'40'               COMMAND CHAINING
SLI      EQU   X'20'               SUPPRESS LENGTH INDICATION
         EJECT
         SAVE  (14,12),,(JAR-3350-EXCP-SKELETON)
         LR    R12,R15
         USING SALVAGE,R12         CSECT ADDRESSABILITY
         ST    R13,SAVEAREA+4
         LA    R2,SAVEAREA
         ST    R2,8(,R13)
         LR    R13,R2
         OPEN  (DATA3350,,QSAMDCB,(OUTPUT))
         L     R2,DATA3350+44      DEB ADDRESS
         LA    R3,16(,R2)          A(DEB-EXTENSION) LESS 16
NEWXTNT  LA    R3,16(,R3)
         SLR   R15,R15
         IC    R15,IOBEXTNT        # OF EXTENTS THET I'M FINISHED WITH
         LA    R15,1(,R15)         BUMP BY 1
         STC   R15,IOBEXTNT        AND STORE BACK IN THE IOB
         EX    R15,CMPRINST        COMPARE TO # OF EXTENTS FROM DEB
         BNH   ENDOFILE            IF HIGH, THIS IS EODAD
         MVC   IOBCYL(4),6(R3)     MOVE CC-HH INTO IOB
RECORD0  MVI   IOBREC,X'00'        REC#0 (UPDATED BEFORE READ)
READLOOP SLR   R15,R15
         IC    R15,IOBREC          BUMP
         LA    R15,1(,R15)         TO NEXT
         STC   R15,IOBREC          RECORD
         XC    ECB(4),ECB          CLEAR THE ECB
         EXCP  IOB                 ISSUE THE EXCP
         WAIT  1,ECB=ECB                AND WAIT FOR COMPLETION
         CLI   ECB,X'7F'           NORMAL COMPLETION?
         BE    GETLENTH                 YES - PROCESS
IOERROR  EQU   *              I/O ERROR - WHAT KIND?
         CLI   ECB,X'42'           EXTENT VIOLATION?
         BE    NEWXTNT                  YES - GO SET UP.
         CLI   ECB,X'41'           PERMANANT I/O ERROR?
         BE    HANDLE                   YES - SEE WHAT CAUSED IT.
S001     ABEND 1,DUMP,,SYSTEM,DUMPOPT=SNAPOPT     SYSTEM 001 ABEND
HANDLE   TM    IOB+12,X'01'        UNIT EXCEPTION?
         BO    ENDOFMEM                 YES - END-OF-MEMBER
         TM    IOBSENSE+1,X'08'    NO RECORD FOUND?
         BO    NEXTRACK                 YES - LOOK ON NEXT TRACK.
         TM    IOBCSW+5,X'40'      INCORRECT LENGTH?
         BNO   S001                     NO - ABEND.
GETLENTH DS    0H
         LH    R9,=H'32760'
         SLR   R0,R0
         ICM   R0,B'0011',IOBCSW+5
         SR    R9,R0
         SLR   R8,R8
         D     R8,=F'80'
         LTR   R8,R8
         BNZ   READLOOP
         LTR   R9,R9
         BZ    READLOOP
         LA    R8,BUF
WRITLOOP PUT   QSAMDCB,(8)
         LA    R8,80(,R8)
         BCT   R9,WRITLOOP
         B     READLOOP            AND GO READ THE FILE AGAIN
ENDOFMEM DS    0H
         PACK  DBL(8),MEMNUMBR(4)
         CVD   R15,DBL
         LA    R15,1(,R15)
         CVD   R15,DBL
         OI    DBL+7,X'0F'
         UNPK  MEMNUMBR(4),DBL
         PUT   QSAMDCB,NEWMEMBR
         B     READLOOP
NEXTRACK SLR   R15,R15
         CLI   IOBHEAD+1,X'1D'     AM I AT TRACK 30? (THIS IS A 3350).
         BNL   NEXTCYL                  YES - BUMP CYLINDER.
         IC    R15,IOBHEAD+1            NO -
         LA    R15,1(,R15)              BUMP TRACK
         STC   R15,IOBHEAD+1                 AND STORE BACK.
         B     RECORD0
NEXTCYL  ICM   R15,3,IOBCYL
         LA    R15,1(,R15)         BUMP CYLINDER
         CH    R15,=H'555'           AM I AT EOP?  (END-OF-PACK).
         BH    NEWXTNT               YES - DON'T TRY ANYTHING FANCY
         STCM  R15,3,IOBCYL             AND STORE BACK
         MVI   IOBHEAD+1,X'00'
         B     RECORD0
ENDOFILE CLOSE (DATA3350,,QSAMDCB)
         L     R13,SAVEAREA+4
         RETURN     (14,12),T,RC=0
DATA3350 DCB   DDNAME=EXCPDD,DSORG=PS,MACRF=(E)
QSAMDCB  DCB   DDNAME=QSAMDCB,DSORG=PS,MACRF=(PM),                     $
               BLKSIZE=3600,LRECL=80,RECFM=FB,BUFNO=5
SAVEAREA DS    9D
IOB      DS    0CL40               I..O..B
         DC    X'4200'   FLAGS - COMMAND CHAINING & UNRELATED
IOBSENSE DC    X'0000'             SENSE BYTES
         DC    AL4(ECB)            ECB ADDRESS
IOBSFLAG DC    X'00'               SYSTEM FLAGS
IOBCSW   DC    XL7'00'             LAST 7 BYTES OF CSW
IOBSIOCD DC    X'00'               CONDITION-CODE FROM SIO OPERATION
IOBCPADR DC    AL3(CHANPROG)       ADDRESS OF CHANNEL PROGRAM
         DC    X'0'
         DC    AL3(DATA3350)       DCB ADDRESS
         DC    XL8'00'
IOBEXTNT DC    X'FF'               M    EXTENT
IOBBIN   DC    XL2'0000'           BB   BIN-BIN
IOBCYL   DC    XL2'0000'           CC   CYL-CYL
IOBHEAD  DC    XL2'0000'           HH   HEAD-HEAD
IOBREC   DC    X'00'               R    RECORD
         SPACE 3
ECB      DC    F'0'
CHANPROG DS    0D
         CCW   SERCHIDE,IOBCYL,CC,5     SEARCH-ID-EQUAL
         CCW   TIC,*-8,CC,0             TRANSFER-IN-CHANNEL
         CCW   READDATA,BUF,SLI,32760 READ BIG BLOCK INTO BUF (&SLI)
         LTORG
CMPRINST CLI   16(R2),*-*
SNAPOPT  SNAP  SDATA=(CB,ERR,TRT,DM,IO),PDATA=(PSW,REGS,SA),MF=L
DBL      DS    D
NEWMEMBR DC    CL80' '
         ORG   NEWMEMBR
         DC    C'./   ADD   LIST=ALL,NAME=MEM#'
MEMNUMBR DC    C'0000'
         ORG
BUF      DS    128CL256    32768 BYTE BUFFER
         END   SALVAGE
