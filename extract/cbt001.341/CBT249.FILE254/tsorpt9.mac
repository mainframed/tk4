*-------------------------------------------------------------------*
×   SMF DATA REDUCTION IN AN MVS/RMF II ENVIRONMENT                 ×
×                                                                   ×
×   THE PURPOSE OF THIS JOB IS TO UTILIZE SAS TO ACCOMPLISH THE     ×
×   REDUCTION OF DATA FOR FOR THE PURPOSE OF PROVIDING REPORTS      ×
×   RELATING TO TSO ACTIVITY AND TSO UTILIZATION.                   ×
×   THE FOLLOWING SMF RECORD TYPES ARE ANALYZED:.                   ×
×   34,35,40,72.                                                    ×
×                                                                   ×
×   THIS PROGRAM IS CURRENTLY RUNNING IN AN RMF II, MVS-MP          ×
×   ENVIRONMENT AND CONTAINS A CERTAIN AMOUNT OF CPU-DEPENDENT      ×
×   CODE.                                                           ×
×                                                                   ×
×   THE FOLLOWING SECTIONS SHOULD BE INSPECTED PRIOR TO ANY USE:    ×
×   LINES 33000 TO 35300 ARE UTILIZED TO ESTABLISH A BREAKDOWN BY   ×
×   USER GROUP. AS MANY GROUPS AS NECESSARY MAY BE DEFINED. IF NO   ×
×   MATCH IS MADE ON USERID, THE DEFAULT GROUP WILL BE 'OPNS'.      ×
×   LINE 47200 DEFINES THE PERFORMANCE GROUP FOR TCAM. THIS MAY     ×
×   BE CHANGED AS NECESSARY.                                        ×
×   THE AREA OF CODE BETWEEN 47700 AND 49200 WAS INSTALLED IN ORDER ×
×   TO APPLY DIFFERENT CPU FACTORS BASED UPON THE CPUID. THE        ×
×   NECESSITY FOR THIS CODE IS BASED UPON INDIVIDUAL REQUIREMENTS   ×
×   AND SHOULD BE REVIEWED QUITE CLOSELY.                           ×
×   IN LINES 48000 AND 48700, SMF72CTS IS DIVIDED BY THE CPU        ×
×   SERVICE UNIT FACTOR. ADDITIONALLY, THIS IS MULTIPLIED BY A      ×
×   FACTOR OF .0080 FOR A 165 AND .0066 FOR A 168, THIS IS THE      ×
×   NUMBER OF SECONDS OF TASK EXECUTION TIME PER SERVICE UNIT.      ×
×   LINE 48700 CONTAINS A 1.25 FACTOR TO RELATE 168 CPU TIME TO     ×
×   A 165 BASE.                                                     ×
×   IN LINES 48100 AND 48800, THE .54 FACTOR WAS OUR OBSERVED       ×
×   CORRELATION FACTOR BETWEEN SRB AND TCB TIME FOR TCAM. THIS      ×
×   NUMBER WAS DERIVED BY USING RMFMON, AND MUST BE CHANGED TO      ×
×   MATCH THE INSTALLATION.                                         ×
×   IN LINES 48300 AND 49000, 5.0 IS THE I/O SERVICE UNIT FACTOR    ×
×   UTILIZED AT THIS INSTALLATION.                                  ×
×                                                                   ×
×   THE INPUT FILE WITH THE DDNAME OF SMF DEFINES THE SMF FILE TO BE×
×   UTILIZED FOR DATA EXTRACTION TO CREATE TSO REPORTS. NO DATE     ×
×   CHECKING IS PERFORMED AND ALL DATA PRESENTED WILL BE UTILIZED   ×
×                                                                   ×
×                                                                   ×
×                                                                   ×
×                                                                   ×
×   IT IS STRONGLY SUGGESTED THAT ONE HAVE A THOROUGH UNDERSTANDING ×
×   OF THE PROGRAM BEFORE UTILIZING IT.  BECAUSE EACH INSTALLATION  ×
×   POSSESSES ITS OWN UNIQUE REQUIREMENTS, THIS PROGRAM IS PRIMARILY×
×   INTENDED TO DEMONSTRATE THE FLEXIBILITY AND CAPABILITY OF SAS   ×
×   IN THE ANALYSIS OF SMF DATA. RATHER THAN BEING UTILIZED IN ITS  ×
×   CURRENT FORM, IT BEST SERVES AS A STARTING POINT FOR THE        ×
×   DEVELOPMENT OF FACILITIES THAT BEST SUIT THE INDIVIDUAL         ×
×   INSTALLATION                                                    ×
×                                                                   ×
×   KENNETH C. TUCKER                                               ×
×   UNION CARBIDE CORPORATION                                       ×
×   P.O. BOX 8361                                                   ×
×   SOUTH CHARLESTON, W VA. 25303                                   ×
×                                                                   ×
*-------------------------------------------------------------------*
;
MACRO GROUPSEL
GROUP='OPNS';
IF USERID EQ 'AGFM    ' OR
   USERID EQ 'ACWL    ' OR
   USERID EQ 'ARZW    ' OR
   USERID EQ 'ADTH    ' OR
   USERID EQ 'AMDN    ' THEN GROUP='ENGR';
IF USERID EQ 'AJDO    ' OR
   USERID EQ 'AAPK    ' OR
   USERID EQ 'APHS    ' OR
   USERID EQ 'AJLH    ' OR
   USERID EQ 'ATDS    ' OR
   USERID EQ 'AJNC    ' OR
   USERID EQ 'AJMB    ' OR
   USERID EQ 'AWGH    ' OR
   USERID EQ 'ARZG    ' THEN GROUP='R&D ';
IF USERID EQ 'AJHC    ' OR
   USERID EQ 'ANLW    ' OR
   USERID EQ 'AGDP    ' OR
   USERID EQ 'AFLM    ' OR
   USERID EQ 'AJFM    ' OR
   USERID EQ 'AJEK    ' OR
   USERID EQ 'ANCM    ' OR
   USERID EQ 'AREF    ' OR
   USERID EQ 'AJLS    ' OR
   USERID EQ 'APJG    ' THEN GROUP='CTS ';
%
   MACRO INPUTIT
   DATA TSO.S34 (KEEP=GROUP USERID DATE
                      CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
                      EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
                      TPAG_TR RESP_TR NO_TRANS)
        TSO.T34 (KEEP=USERID DATE TIME_ON TIVPGIN TIVPGOUT TIVRGNS
                      TIVSIN TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
                      TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN TIVCRECL
                      TIVPGSTL TPAGING GROUP)
        TSO.U34 (KEEP=DATE TIME_ON TIME_OFF CON_MIN TPUTS TGETS
                      TCB_SEC SRB_SEC CPU_SEC EXCP EXCP_D USERID GROUP)
        TSO.T35 (KEEP=NO_TRANS USERID GROUP DATE TIME_ON)
        TSO.T40 (KEEP=GROUP USERID DATE EXCP_D)
        TSO.T72 (KEEP=JDATE TIME_REC SMF72A_S SMF72SER SMF72M_S
                      SMF72MTS SMF72ITS SMF72CTS SMF72T_S TCB_SEC
                      SRB_SEC CPU_SEC SMF72IO)
        ;
   INFILE SMF LENGTH=L;
   INPUT @2 ID PIB1. @;
   IF ID = 34 THEN LINK X34;
   IF ID = 35 THEN LINK X35;
   IF ID = 40 THEN LINK X40;
   IF ID = 72 THEN LINK X72;
   RETURN;
X34:
   INPUT  @23 DATE SMFSTAMP8.
          @23 T_ONH PIB4.
          @27 DATE_ON PD4.
          @3 T_OFFH PIB4.
          @7 DATE_OFF PD4.
          @44 TPUTS PIB4. TGETS PIB4.
          @96 SRBH PIB3.
          @101 DISP_REL PIB2.
          @103 LEN_IO PIB2.
          @LEN_IO+104 TCBH PIB3.
          @DISP_REL+1  TIVPGIN  PIB4.
          @DISP_REL+5  TIVPGOUT PIB4.
          @DISP_REL+9  TIVRGNS  PIB4.
          @DISP_REL+13 TIVSIN   PIB4.
          @DISP_REL+17 TIVSOUT  PIB4.
          @DISP_REL+21 TIVVPI   PIB4.
          @DISP_REL+25 TIVVPO   PIB4.
          @DISP_REL+29 TIVSST   PIB4.
          @DISP_REL+33 TIVACT   PIB4.
          @DISP_REL+39 TIVTRANT PIB4.
          @DISP_REL+43 TIVRECLM PIB4.
          @DISP_REL+47 TIVRCLAM PIB4.
          @DISP_REL+51 TIVCPGIN PIB4.
          @DISP_REL+55 TIVCRECL PIB4.
          @DISP_REL+59 TIVPGSTL PIB4.
          @15 USERID $8. @;
   TIVACT_S=TIVACT*1024/1000000;
   TIVTRA_S=TIVTRANT*1024/1000000;
   TCB_SEC=TCBH/100;
   SRB_SEC=SRBH/100;
   CPU_SEC=TCB_SEC+SRB_SEC;
   TIME_ON=T_ONH/360000;
   TIME_OFF=T_OFFH/360000;
   TPAGING=(TIVPGIN+TIVPGOUT+TIVVPI+TIVVPO+TIVSIN+TIVSOUT);
   CON_MIN=(TIME_OFF-TIME_ON)*60+(DATE_OFF-DATE_ON)*24*60;
   EXCP=0;
   CT=(LEN_IO-2)/8;
   LOOP=1;
   INPUT @103 LEN_IO PIB2. @;
L: IF CT LT LOOP THEN GO TO OUT;
   INPUT +4 E PIB4. @;
   LOOP+1;
   EXCP+ E;
   GO TO L;
OUT:
   GROUPSEL;
   OUTPUT TSO.U34;
   OUTPUT TSO.T34;
   RETURN;
X35:
   INPUT @15 USERID $8.
         @85 NO_TRANS PIB4.
         @23 DATE SMFSTAMP8.
         @23 T_ONH PIB4.;
   TIME_ON=T_ONH/360000;
   GROUPSEL;
   OUTPUT TSO.T35;
   RETURN;
X40:
   INPUT @23 DATE SMFSTAMP8.
         @39 STEP_NO PIB1.
         @61 LEN_IO PIB2.
         @15 USERID $8. @;
   IF STEP_NO NE 1 THEN RETURN;
   EXCP_D=0;
   CT=(LEN_IO-2)/8;
   LOOP=1;
   INPUT @61 LEN_IO PIB2. @;
L1: IF CT LT LOOP THEN GO TO OUT1;
   INPUT+4 E1 PIB4. @;
   LOOP+1;
   EXCP_D+E1;
   GO TO L1;
OUT1:
   IF EXCP_D LT 1 THEN RETURN;
   GROUPSEL;
   OUTPUT TSO.T40;
   RETURN;
X72:
   INPUT @31 PGNUM PIB2.
         @11 CPUID $4.
         @15 LEN_CDS PIB2.
         @17 TIME_REC PD4.
         @21 JDATE PD4.
         @15+LEN_CDS LEN_WCS PIB2.
         @15+LEN_CDS+LEN_WCS+04 SMF72ACT PIB4.
         @15+LEN_CDS+LEN_WCS+08 SMF72SER PIB4.
         @15+LEN_CDS+LEN_WCS+12 SMF72TTM PIB4.
         @15+LEN_CDS+LEN_WCS+20 SMF72MTS PIB4.
         @15+LEN_CDS+LEN_WCS+24 SMF72ITS PIB4.
         @15+LEN_CDS+LEN_WCS+28 SMF72CTS PIB4.
         @15+LEN_CDS+LEN_WCS+32 SMF72TAT PIB4. @;
   IF PGNUM NE 65 THEN RETURN;
   IF SMF72ACT = 0 THEN RETURN;
   SMF72A_S=SMF72ACT*1024/1000000;
   SMF72M_S=SMF72TTM*1024/1000000;
   SMF72T_S=SMF72TAT*1024/1000000;
   IF CPUID NE '365B' AND
      CPUID NE '365F'
      THEN GO TO SYS368Z;
   TCB_SEC=(SMF72CTS/1.6)*.0080;
   SRB_SEC=.54*TCB_SEC; COMMENT BASED UPON PRIOR OBSERVATION;
   CPU_SEC=TCB_SEC+SRB_SEC;
   SMF72IO=SMF72ITS/5.0;
   OUTPUT TSO.T72;
   RETURN;
SYS368Z:
   TCB_SEC=(SMF72CTS/1.9)*.0066*1.25; * FACTOR TO 165 BASE;
   SRB_SEC=.54*TCB_SEC; COMMENT BASED UPON PRIOR OBSERVATION;
   CPU_SEC=TCB_SEC+SRB_SEC;
   SMF72IO=SMF72ITS/5.0;
   OUTPUT TSO.T72;
   RETURN;
   PROC SORT DATA=TSO.T34; BY GROUP USERID DATE;
   PROC SORT DATA=TSO.U34; BY GROUP USERID DATE;
   PROC SORT DATA=TSO.T35; BY GROUP USERID DATE;
   PROC SORT DATA=TSO.T40; BY GROUP USERID DATE;
   PROC SORT DATA=TSO.T72; BY JDATE TIME_REC;
   %
   INPUTIT;
DATA D;
INPUT CARD $CHAR72.;
CARDS4;
NOTE THAT ALL STATISTICS ARE GENERATED ON A SESSION BASIS


TERM           DEFINITION

JDATE          JULIAN DATE OF RECORD
DATE           DATE AND TIME OF LOGON
TIME_ON        LOGON TIME OF DAY IN HOURS
TIME_OFF       LOGOFF TIME OF DAY IN HOURS
TPUTS          NUMBER OF LINES OF TERMINAL OUTPUT
TGETS          NUMBER OF LINES OF TERMINAL INPUT
TCB_SEC        TCB STEP CPU TIME IN SECONDS
SRB_SEC        SRB STEP CPU TIME IN SECONDS
CPU_SEC        TOTAL OF TCB AND SRB TIME IN SECONDS
CON_MIN        NUMBER OF MINUTES OF CONNECT TIME, LOGOFF MINUS LOGON
EXCP           TOTAL I/O COUNT EXCLUSIVE OF DYNAMIC ALLOCATIONS
EXCP_D         TOTAL I/O COUNT OF DYNAMIC ALLOCATIONS
NO_TRANS       NUMBER OF TRANSACTIONS ENTERED DURING THE PERIOD
LOGONNUM       NUMBER OF LOGONS FOR THE USER
USERID         LOGON USER ID
TIVPGIN        NUMBER OF NON-VIO NON-SWAP PAGE-INS
TIVPGOUT       NUMBER OF NON-VIO NON-SWAP PAGE-OUTS
TIVRGNS        NUMBER OF SWAP SEQUENCES
TIVSIN         NUMBER OF PAGES SWAPPED IN
TIVSOUT        NUMBER OF PAGES SWAPPED OUT
TIVVPI         NUMBER OF VIO PAGE-INS
TIVVPO         NUMBER OF VIO PAGE-OUTS
TPAGING        TOTAL NUMBER OF PAGE-INS AND PAGE-OUTS
TIVSST         LOGON SERVICE, IN SERVICE UNITS
TIVACT         STEP TRANSACTION ACTIVE TIME, IN 1024-MICROSECOND UNITS
TIVACT_S       STEP TRANSACTION ACTIVE TIME, IN SECONDS
TIVTRANT       STEP TRANSACTION RESIDENCY TIME, IN 1024-MICROSECOND
               UNITS
TIVTRA_S       STEP TRANSACTION RESIDENCY TIME, IN SECONDS
TIVRECLM       NUMBER OF RECLAIMS
TIVRCLAM       NUMBER OF VIO RECLAIMS
TIVCPGIN       NUMBER OF COMMON AREA PAGE-INS
TIVCRECL       NUMBER OF COMMON AREA RECLAIMS
TIVPGSTL       NUMBER OF PAGES STOLEN FROM STORAGE FOR THIS STEP
CON_TR         AVERAGE NUMBER OF CONNECT MINUTES PER TRANSACTION
TPUT_TR        AVERAGE NUMBER OF TPUTS PER TRANSACTION
TGET_TR        AVERAGE NUMBER OF TGETS PER TRANSACTION
TCB_TR         AVERAGE NUMBER OF TCB SECONDS PER TRANSACTION
SRB_TR         AVERAGE NUMBER OF SRB SECONDS PER TRANSACTION
CPU_TR         AVERAGE NUMBER OF TCB AND SRB SECONDS PER TRANSACTION
EXCP_TR        AVERAGE NUMBER OF EXCPS (NON-DYNAMIC) PER TRANSACTION
EXCPD_TR       AVERAGE NUMBER OF EXCPS (DYNAMIC) PER TRANSACTION
SWAP_TR        AVERAGE NUMBER OF SWAPS PER TRANSACTION
SWAPP_TR       AVERAGE NUMBER OF SWAP PAGES PER TRANSACTION
VIO_TR         AVERAGE NUMBER OF VIO PAGES PER TRANSACTION
DMD_TR         AVERAGE NUMBER OF DEMAND PAGES PER TRANSACTION
TPAG_TR        AVERAGE NUMBER OF TOTAL PAGE_INS AND PAGE-OUTS
               PER TRANSACTION
RESP_TR        AVERAGE CALCULATED RESPONSE TIME PER TRANSACTION
SMF72ACT       ACTIVE TRANSACTION TIME IN 1024-MICROSECOND UNITS
SMF72A_S       ACTIVE TRANSACTION TIME, IN SECONDS
SMF72SER       SERVICE USED, IN SERVICE UNITS
SMF72TTM       ELAPSED TIME ACCUMULATED, IN 1024-MICROSECOND UNITS
SMF72M_S       ELAPSED TIME ACCUMULATED, IN SECONDS
SMF72MTS       MAIN STORAGE TOTAL SERVICE UNITS
SMF72ITS       I/O TOTAL SERVICE UNITS
SMF72CTS       CPUT TOTAL SERVICE UNITS
SMF72TAT       TRANSACTION RESIDENCY TIME, IN 1024-MICROSECOND UNITS
SMF72T_S       TRANSACTION RESIDENCY TIME, IN SECONDS
TCB_SEC        TOTAL TCB TIME, IN SECONDS
SMF72IO        TOTAL I/O COUNT
;;;;
   PROC PRINT;
   ID CARD;
TITLE1 LIST OF TERMINOLOGY;
TITLE2 REPORT NUMBER 1;
   PROC MEANS NOPRINT DATA=TSO.T40; BY GROUP USERID DATE;
   VARIABLES EXCP_D;
   OUTPUT OUT=AB SUM= EXCP_D;
   DATA AA;
   MERGE TSO.U34 TSO.T35 AB; BY GROUP USERID DATE;
   DATA A; SET AA;
   IF NO_TRANS NOT=.;
   PROC MEANS DATA=A; BY GROUP USERID;
   VARIABLES CON_MIN NO_TRANS TCB_SEC SRB_SEC CPU_SEC
             TPUTS TGETS EXCP EXCP_D;
   OUTPUT OUT=B N=LOGONNUM SUM= CON_MIN NO_TRANS TCB_SEC SRB_SEC
                                CPU_SEC TPUTS TGETS EXCP EXCP_D;
TITLE1 LOGON STATISTICAL EVALUATION REPORT BY USER;
TITLE2 REPORT NUMBER 2;
   PROC MEANS DATA=WORK.A; BY GROUP;
   VARIABLES CON_MIN NO_TRANS TCB_SEC SRB_SEC CPU_SEC
             TPUTS TGETS EXCP EXCP_D;
   OUTPUT OUT=K N=LOGONNUM
                SUM= CON_MIN NO_TRANS TCB_SEC SRB_SEC
                     CPU_SEC TPUTS TGETS EXCP EXCP_D;
TITLE1 LOGON STATISTICAL EVALUATION REPORT BY GROUP;
TITLE2 REPORT NUMBER 3;
   PROC MEANS DATA=WORK.A;
   VARIABLES CON_MIN NO_TRANS TCB_SEC SRB_SEC CPU_SEC
             TPUTS TGETS EXCP EXCP_D;
   OUTPUT OUT=E N=LOGONNUM
                SUM= CON_MIN NO_TRANS TCB_SEC SRB_SEC
                     CPU_SEC TPUTS TGETS EXCP EXCP_D;
TITLE1 LOGON STATISTICAL EVALUATION REPORT - SUMMARY;
TITLE2 REPORT NUMBER 4;
   DATA F; SET B E;
   IF USERID EQ '        ' THEN ID='TOTAL';
   FORMAT TCB_SEC 6. SRB_SEC 6. CPU_SEC 6. CON_MIN 6.;
   PROC SORT; BY ID GROUP USERID;
   PROC PRINT; ID ID;
TITLE1 LOGON SUMMARY REPORT BY USER;
TITLE2 REPORT NUMBER 5;
   DATA L; SET K E;
   IF GROUP EQ '        ' THEN ID='TOTAL';
   FORMAT TCB_SEC 6. SRB_SEC 6. CPU_SEC 6. CON_MIN 6.;
   PROC SORT; BY ID GROUP;
   PROC PRINT; ID ID;
TITLE1 LOGON SUMMARY REPORT BY GROUP;
TITLE2 REPORT NUMBER 6;
   DATA C; SET A B;
   IF LOGONNUM GT 0 THEN ID='TOTAL';
   FORMAT DATE DATETIME.;
   FORMAT TCB_SEC 6. SRB_SEC 6. CPU_SEC 6. CON_MIN 6.;
   DROP TIME_ON TIME_OFF;
   PROC SORT; BY GROUP USERID ID DATE;
   PROC PRINT PAGE; BY GROUP USERID; ID ID;
TITLE1 LOGON DETAIL REPORT BY USER;
TITLE2 REPORT NUMBER 7;
   PROC MEANS DATA=TSO.T34; BY GROUP USERID;
   VARIABLES TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   OUTPUT OUT=D N=LOGONNUM
                SUM=  TIVPGIN TIVPGOUT TIVRGNS TIVSIN
                      TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
                      TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
                      TIVCRECL TIVPGSTL TPAGING;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 PAGING STATISTICAL EVALUATION REPORT BY USER;
TITLE2 REPORT NUMBER 8;
   PROC MEANS DATA=TSO.T34; BY GROUP;
   VARIABLES TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   OUTPUT OUT=M N=LOGONNUM
                SUM=  TIVPGIN TIVPGOUT TIVRGNS TIVSIN
                      TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
                      TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
                      TIVCRECL TIVPGSTL TPAGING;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 PAGING STATISTICAL EVALUATION REPORT BY GROUP;
TITLE2 REPORT NUMBER 9;
   PROC MEANS DATA=TSO.T34;
   VARIABLES TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   OUTPUT OUT=G SUM=  TIVPGIN TIVPGOUT TIVRGNS TIVSIN
                      TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
                      TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
                      TIVCRECL TIVPGSTL TPAGING;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 PAGING STATISTICAL EVALUATION REPORT - SUMMARY;
TITLE2 REPORT NUMBER 10;
   DATA H; SET D G;
   IF USERID EQ '        ' THEN ID='TOTAL';
   PROC SORT; BY ID GROUP USERID;
   PROC PRINT; ID ID;
   VARIABLES GROUP USERID TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 TSO PAGING SUMMARY BY USER;
TITLE2 REPORT NUMBER 11;
   DATA N; SET M G;
   IF GROUP EQ '        ' THEN ID='TOTAL';
   PROC SORT; BY ID GROUP;
   PROC PRINT; ID ID;
   VARIABLES GROUP TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 TSO PAGING SUMMARY BY GROUP;
TITLE2 REPORT NUMBER 12;
   DATA I; SET TSO.T34 D;
   IF LOGONNUM GT 0 THEN ID='TOTAL';
   PROC SORT; BY GROUP USERID ID DATE;
   PROC PRINT PAGE; BY GROUP USERID; ID ID;
   VARIABLES DATE TIVPGIN TIVPGOUT TIVRGNS TIVSIN
             TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
             TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
             TIVCRECL TIVPGSTL TPAGING;
   FORMAT DATE DATETIME.;
   FORMAT TIVACT_S 6. TIVTRA_S 6.;
TITLE1 TSO PAGING DETAIL REPORT BY USER;
TITLE2 REPORT NUMBER 13;
   DATA Z;
   MERGE TSO.T34 A; BY GROUP USERID DATE;
   DATA TSO.S34;
   SET Z;
   CON_TR=CON_MIN/NO_TRANS;
   TPUT_TR=TPUTS/NO_TRANS;
   TGET_TR=TGETS/NO_TRANS;
   TCB_TR=TCB_SEC/NO_TRANS;
   SRB_TR=SRB_SEC/NO_TRANS;
   CPU_TR=CPU_SEC/NO_TRANS;
   EXCP_TR=EXCP/NO_TRANS;
   EXCPD_TR=EXCP_D/NO_TRANS;
   SWAP_TR=TIVRGNS/NO_TRANS;
   SWAPP_TR=(TIVSIN+TIVSOUT)/NO_TRANS;
   VIO_TR=(TIVVPI+TIVVPO)/NO_TRANS;
   DMD_TR=(TIVPGIN+TIVPGOUT)/NO_TRANS;
   TPAG_TR=(TPAGING)/NO_TRANS;
   RESP_TR=TIVACT_S/NO_TRANS;
   DROP CON_MIN TCB_SEC SRB_SEC TIME_ON TIME_OFF
        CPU_SEC TPUTS TGETS EXCP EXCP_D
        TIVPGIN TIVPGOUT TIVRGNS TIVSIN
        TIVSOUT TIVVPI TIVVPO TIVSST TIVACT_S
        TIVTRA_S TIVRECLM TIVRCLAM TIVCPGIN
        TIVCRECL TIVPGSTL TPAGING;
   PROC MEANS DATA=TSO.S34; BY GROUP USERID;
   VARIABLES CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
             EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
             TPAG_TR RESP_TR NO_TRANS;
   OUTPUT OUT=B1 N=LOGONNUM
          MEAN=CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
               EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
               TPAG_TR RESP_TR NO_TRANS;
TITLE1 TRANSACTION RESOURCE STATISTICAL EVALUATION REPORT BY USER;
TITLE2 REPORT NUMBER 14;
   PROC MEANS DATA=TSO.S34; BY GROUP;
   VARIABLES CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
             EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
             TPAG_TR RESP_TR NO_TRANS;
   OUTPUT OUT=K1 N=LOGONNUM
   MEAN=CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
        EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
        TPAG_TR RESP_TR NO_TRANS;
TITLE1 TRANSACTION RESOURCE STATISTICAL EVALUATION REPORT BY GROUP;
TITLE2 REPORT NUMBER 15;
   PROC MEANS DATA=TSO.S34;
   VARIABLES CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
             EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
             TPAG_TR RESP_TR NO_TRANS;
   OUTPUT OUT=E1 N=LOGONNUM
   MEAN=CON_TR TPUT_TR TGET_TR TCB_TR SRB_TR CPU_TR
        EXCP_TR EXCPD_TR SWAP_TR SWAPP_TR VIO_TR DMD_TR
        TPAG_TR RESP_TR NO_TRANS;
TITLE1 TRANSACTION RESOURCE STATISTICAL EVALUATION REPORT - SUMMARY;
TITLE2 REPORT NUMBER 16;
   DATA F1; SET B1 E1;
   IF USERID EQ '        ' THEN ID='AVERAGE';
   PROC SORT; BY ID GROUP USERID;
   PROC PRINT; ID ID;
TITLE1 TRANSACTION RESOURCE SUMMARY REPORT BY USER;
TITLE2 REPORT NUMBER 17;
   DATA L1; SET K1 E1;
   IF GROUP EQ '        ' THEN ID='AVERAGE';
   PROC SORT; BY ID GROUP;
   PROC PRINT; ID ID;
TITLE1 TRANSACTION RESOURCE SUMMARY REPORT BY GROUP;
TITLE2 REPORT NUMBER 18;
   DATA C1; SET TSO.S34 B1;
   IF LOGONNUM GT 0 THEN ID='AVERAGE';
   FORMAT DATE DATETIME.;
   PROC SORT; BY GROUP USERID ID DATE;
   PROC PRINT PAGE; BY GROUP USERID; ID ID;
TITLE1 TRANSACTION RESOURCE DETAIL REPORT BY USER;
TITLE2 REPORT NUMBER 19;
   PROC MEANS DATA=TSO.T72;
   VARIABLES SMF72A_S SMF72SER SMF72M_S
             SMF72MTS SMF72ITS SMF72CTS SMF72T_S
             TCB_SEC SRB_SEC CPU_SEC SMF72IO;
   OUTPUT OUT=J N=NUMRECS SUM= SMF72A_S SMF72SER SMF72M_S
                               SMF72MTS SMF72ITS SMF72CTS SMF72T_S
                               TCB_SEC SRB_SEC CPU_SEC SMF72IO;
TITLE1 TCAM RMF STATISTICAL EVALUATION REPORT;
TITLE2 REPORT NUMBER 20;
   DATA K; SET TSO.T72 J;
   IF NUMRECS GT 0 THEN ID='TOTAL';
   PROC SORT; BY ID JDATE;
   PROC PRINT; ID ID;
        VARIABLES  JDATE TIME_REC SMF72A_S SMF72SER SMF72M_S
                   SMF72MTS SMF72ITS SMF72CTS SMF72T_S
                   TCB_SEC SRB_SEC CPU_SEC SMF72IO;
   FORMAT TCB_SEC 6. SRB_SEC 6. CPU_SEC 6.;
TITLE1 TCAM RMF STATISTICS;
TITLE2 REPORT NUMBER 21;
