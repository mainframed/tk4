DATA WORKLOAD ( KEEP = PERFGRP CPU CLASS HR CPUSEC SMFDATE)
     EXCEPT72
     CPUREC ( KEEP = CPU SMFDATE HR RMFDUR CPU_BUSY CPUSEC )     ;
INFILE SMF                       ;
      INPUT @2 ID PIB1. @ ;
      IF ID = 70 THEN GOTO TYPE70               ; * CPU      RECORD ;
      IF ID NE 72 THEN DELETE ;
      INPUT @3 DATETIME SMFSTAMP8. @12 CPU $1.
            @3 TIME PIB4.2 @7 SMFDATE PD4. @ ;
      FORMAT DATETIME DATETIME. ;
      INPUT @31 PERFGRP PIB2. @51 NRPERIOD PIB2.
            @77 IOCCOEFF 4. CPUCOEFF 4. MSOCOEFF 4. @89 @ ;
   PERIOD=1 ;
HR = INT ( TIME / 3600 )    ;
IF  7 < HR < 17   ; *  DO THIS FIRST SHIFT ONLY   ;

LOOP72:
   IF NRPERIOD=0 THEN RETURN ;
   INPUT TRANS PIB4. ACTIVE PIB4. SERVICE PIB4. ELAPSED PIB4.
         WKLOAD PIB4. MSOUNITS PIB4. IOUNITS PIB4. CPUUNITS PIB4.
         RESIDENT PIB4. SWAPSEQ PIB4. DOMAIN PIB2. OBJECTIV PIB1. +1 @;
 SU_SEC=151 ;
    CPUSEC=CPUUNITS/(CPUCOEFF*SU_SEC)    ;
 IF CPUSEC  > 3600.     THEN GO TO EXCPTN  ;
 IF SERVICE > 10000000. THEN GO TO EXCPTN  ;
 IF IOUNITS > 10000000. THEN GO TO EXCPTN  ;
   GOTO FINI     ;
EXCPTN:

  OUTPUT EXCEPT72 ;
FINI:
   IF CPUSEC = 0  THEN DELETE         ;
* CLASSIFY THE PERFORMANCE GROUPS     ;
   IF PERFGRP =  1 THEN CLASS = 'BTCH'   ;
   IF PERFGRP =  2 THEN CLASS = 'TSO '   ;
   IF PERFGRP =  3 THEN CLASS = 'BTCH'   ;
   IF PERFGRP =  4 THEN CLASS = 'IMS '   ;
   IF PERFGRP =  5 THEN CLASS = 'IMS '   ;
   IF PERFGRP =  6 THEN CLASS = 'CICS'   ;
   IF PERFGRP =  7 THEN CLASS = 'HOTB'   ;
   IF PERFGRP =  8 THEN CLASS = 'TSO '   ;
   IF PERFGRP =  9 THEN CLASS = 'TSO '   ;
   IF PERFGRP = 10 THEN CLASS = 'JES3'   ;
   IF PERFGRP = 11 THEN CLASS = 'CICS'   ;
   IF PERFGRP > 30 THEN CLASS = 'TSOX'   ;
   IF PERFGRP > 20 THEN CLASS = 'SYSS'   ;
   IF PERFGRP > 10 THEN CLASS = 'SYSN'   ;
   OUTPUT WORKLOAD     ;
   NRPERIOD=NRPERIOD-1 ;
   PERIOD=PERIOD+1 ;
   GOTO LOOP72 ;
*          CPU RECORD          ;
TYPE70:
   INPUT @3 DATETIME SMFSTAMP8. @12 CPU $1.
         @3 TIME PIB4.2 @7 SMFDATE PD4.  @25 RMFDPK PD4.
         @51 NO_CPU PIB2. SIZE_CPU PIB2.   @   ;
   HR = INT ( TIME / 3600 )   ;
   IF  7 < HR < 17   ; *  DO THIS FIRST SHIFT ONLY   ;
   RMFMIN = INT (RMFDPK / 100000 )  ;
   RMFDUR = RMFMIN * 60 + (RMFDPK - RMFMIN * 100000 ) / 1000 ;
   PTR = 75      ;   COUNT = 1      ;
   GETCPU:   INPUT @PTR WAIT PIB4.  +2 CPUID PIB2.  @ ;
   CPU_BUSY  = 1 - WAIT * 16E-6 / RMFDUR                  ;
   CPUSEC = RMFDUR * CPU_BUSY              ;
   OUTPUT CPUREC  ;
   IF COUNT GE NO_CPU THEN RETURN    ;
   COUNT = COUNT + 1  ;
   PTR = PTR + SIZE_CPU   ;
   GOTO GETCPU  ;

DROP ID TIME NRPERIOD IOCCOEFF CPUCOEFF MSOCOEFF SU_SEC SERVICE IOUNITS
        PERIOD DATETIME OBJECTIV RESIDENT DOMAIN TRANS ACTIVE MSOUNITS
        ELAPSED WKLOAD SWAPSEQ;
***   ;
 PROC MEANS  DATA=WORKLOAD    N SUM MEAN      ;  VAR CPUSEC   ;
    TITLE RMF CPU USAGE ( WORKLOAD, TYPE 72 RECORDS ) ;
 PROC MEANS  DATA=CPUREC      N SUM MEAN      ;  VAR CPUSEC CPU_BUSY  ;
    TITLE RMF CPU USAGE ( CPU, TYPE 70 RECORDS ) ;
*PROC PRINT DATA=EXCEPT72   ;
PROC FREQ  DATA=WORKLOAD    ;
   TABLES HR * CLASS   / NOROW NOCOL   ;
   TABLES CPU * HR * CLASS  / NOROW NOCOL   ;
   TABLES CPU * HR     / NOROW NOCOL   ;
   WEIGHT CPUSEC         ;
   TITLE RMF CPU USAGE ( WORKLOAD ) IN CPU SECONDS   ;
PROC FREQ  DATA = CPUREC    ;
   TABLES CPU * HR     / NOROW NOCOL   ;
   WEIGHT CPUSEC                       ;
   TITLE TOTAL CPU USAGE IN CPU SECONDS   ;
