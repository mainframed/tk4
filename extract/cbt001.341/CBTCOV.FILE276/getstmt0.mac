 (CHECK(ERRCODE)):  GETSTMT:  PROCEDURE(STTYPE,STMT)RECURSIVE REORDER;
 %INCLUDE DCLSTMT;
 %INCLUDE EXTVAR,EXTVAR2,DCLRTRAN,FILES,EXTRTN;
1DECLARE
      (CHAR,INDEX,LENGTH,SUBSTR,TRANSLATE,VERIFY)BUILTIN;
0DECLARE
      STMT_BUFFER CHAR(2001)STATIC VARYING INITIAL(''),
      REMAINDER_OF_CARD CHAR(80)STATIC VARYING INITIAL(''),
      SLASH FIXED BINARY(31);
1/*   GETSTMT IS CALLED TO ASSEMBLE A STATEMENT AND DETERMINE
 /*   LATER SUBMIT STATEMENT.
 /*      THE ORIGINAL SOURCE OF STATEMENTS IS THE FILE SYSIN.
 /*           A STREAM FILE IS USED FOR PROMPTING, PADDING, ETC.
 /*           UNDER TSO.  SINCE PL/I DOES NOT TRANSLATE LOWER CASE
 /*           TO UPPER CASE FOR STREAM TERMINAL FILES, GETSTMT
 /*           MUST DO THIS.  ALTERNATE OPERATORS FOR ASCII TERMINALS
 /*           (@ FOR &, % FOR ×, AND $ FOR ^) ARE ALSO TRANSLATED
 /*           AT THIS TIME TO THEIR STANDARD COUNTERPARTS.  SUCH
 /*           OPERATORS WILL APPEAR IN ANY DIAGNOSTICS IN THEIR
 /*           STANDARD FORM.  TOUGH.
 /*      WHENEVER A CARD IS READ, BLANK (COLUMNS 73-80) CARDS ARE
 /*           SKIPPED.  THE FIRST NONBLANK CARD FOUND IS THEN
 /*           TRANSLATED.  IF THE LAST 8 CHARACTERS (COLUMNS 73-80)
 /*           OF THE CARD CONTAIN ONLY NUMBERS AND BLANKS,
 /*           THEY ARE ASSUMED TO REPRESENT A SEQUENCE NUMBER AND ARE
 /*           STRIPPED OFF.  TRAILING BLANKS ARE THEN ELIMINATED FROM
 /*           THE LINE.  A '#' IN THE LAST REMAINING COLUMN CAN BE
 /*           USED TO STOP TRAILING BLANK ELIMINATION AT THAT POINT.
 /*           THIS IS NOT MENTIONED IN ANY CURRENT USER DOCUMENTATION
 /*           BUT IS USED BY SBMIT IN CONSTRUCTING TEXT.
 /*      EVERY STATEMENT IS TERMINATED BY A '/'.  A STATEMENT MAY
 /*           RUN OVER SEVERAL LINES, AND SEVERAL STATEMENTS MAY
 /*           APPEAR ON THE SAME LINE.  THIS IS HANDLED BY ASSEMBLING
 /*           LINES IN A STATEMENT BUFFER.  THE FIRST STATEMENT
 /*           IS TAKEN OFF THE BUFFER EACH TIME.  BLANKS ARE
 /*           PERMITTED BEFORE THE IDENTIFYING KEYWORD (STTYPE) AND
 /*           MUST BE STRIPPED OFF BY GETSTMT.  A '#' IS ADDED TO
 /*           THE STATEMENT (RATHER THAN A '/') AS A TERMINATOR.
 /*      EACH STATEMENT IS VERIFIED TO CONTAIN A LIMITED CHARACTER
 /*           SET.  THIS IS NECESSARY SO THAT QUANDRY CAN INSERT
 /*           CONTROL TEXT.  (CURRENTLY ONLY '#' IS USED.)
 /*      QUANDRY PROGRAMS MUST BE TERMINATED BY AN END STATEMENT.
 /*           A MISSING END STATEMENT WILL BE SUPPLIED ON ENDFILE BY
 /*           GETSTMT.  THIS FEATURE IS USED BY SBMIT, WHICH DOES
 /*           NOT GENERATE AN END STATEMENT, AND BY OTHER ROUTINES
 /*           TO GUARANTEE TERMINATION.
 /*      GTCLEAR IS CALLED TO CLEAR THE STATEMENT BUFFER FOLLOWING
 /*            AN ERROR.  ANY STATEMENTS OR FRAGMENTS ON THE LAST
 /*            LINE ARE LOST.
 /*      GTRTRAN IS CALLED FOR RETAIN, DELETE, AND CHANGE STATEMENTS
 /*           TO SET UP THE STATEMENTS SAVED IN SBSTMTS TO BE
 /*           RETRANSLATED.  (SINCE RECURSIVE RETRANSLATION IS
 /*           PROHIBITED, RETAIN, DELETE, AND CHANGE STATEMENTS
 /*           MUST NOT BE SAVED IN SBSTMTS.)  AT ENTRY TO GTRTRAN,
 /*           THE STATEMENT BUFFER WILL CONTAIN AT MOST THAT PORTION
 /*           OF THE LAST CARD WHICH FOLLOWS THE RETAIN, DELETE, OR
 /*           CHANGE STATEMENT.  THIS IS SAVED IN REMAINDER_OF_CARD.
 /*           THE SAVED STATEMENTS ARE TRANSFERRED FROM SBSTMTS TO
 /*           TO THE STATEMENT BUFFER, AND THE EXTERNAL FLAG
 /*           RETRANSLATION_IN_PROGRESS IS SET.  SUBSEQUENT CALLS TO
 /*           GETSTMT WILL TAKE STATEMENTS FROM THE STATEMENT BUFFER,
 /*           SUPPORTING RETRANSLATION.  WHEN THE STATEMENT BUFFER
 /*           IS EXHAUSTED, RETRANSLATE_IN_PROGRESS IS RESET AND
 /*           REMAINDER_OF_CARD IS RESTORED TO THE STATEMENT BUFFER. */
1ON ENDFILE(SYSIN)STMT_BUFFER=CHAR('END/',80);
 ON STRINGSIZE ERRCODE=02002;
0     SCANPTR=1; /* INITIALIZE FOR DIAGNOSTICS */
0     IF RETRANSLATE_IN_PROGRESS&LENGTH(STMT_BUFFER)=0
         THEN DO;
              RETRANSLATE_IN_PROGRESS=0B;
              STMT_BUFFER=REMAINDER_OF_CARD;
         END;
0     IF LENGTH(STMT_BUFFER)=0 /* NO STATEMENTS IN BUFFER */
         THEN DO;
              CALL READ_CARD;
         END;
      STMT='';STTYPE='UNKNOWN   ';
      IF SUBSTR(STMT_BUFFER,1,1)=' '
         THEN STMT_BUFFER=SUBSTR(STMT_BUFFER,VERIFY(STMT_BUFFER,' '));
0     DO WHILE(INDEX(STMT_BUFFER,'/')=0);
         (STRINGSIZE):  STMT=STMT××STMT_BUFFER;
         CALL READ_CARD;
      END;
0     SLASH=INDEX(STMT_BUFFER,'/');
      (STRINGSIZE):  STMT=STMT××SUBSTR(STMT_BUFFER,1,SLASH);;
      SUBSTR(STMT,LENGTH(STMT),1)='#';
      STMT_BUFFER=SUBSTR(STMT_BUFFER,SLASH+1);
      IF STMT_BUFFER=' '
         THEN STMT_BUFFER='';
0     STTYPE=STMT;
      IF LENGTH(STMT)<=LENGTH(STTYPE)
         THEN SUBSTR(STTYPE,LENGTH(STMT),1)=' '; /* WIPE OUT '#' */
      STTYPE=SUBSTR(STTYPE,1,INDEX(STTYPE××' ',' ')-1);
0     SCANPTR=VERIFY(STMT,
         'ABCDEFGHIJKLMNOPQRSTUVWXYZ*0123456789 -.,''&×;=^><()');
      IF SCANPTR^=LENGTH(STMT) /* LOCATION OF '#' */
         THEN ERRCODE=02003;
0     RETURN;
1GTCLEAR:  ENTRY;
      IF ENV.BATCH×RETRANSLATE×RETRANSLATE_IN_PROGRESS
         THEN;
         ELSE STMT_BUFFER='';
      RETURN;
-GTRTRAN:  ENTRY;
      IF RETRANSLATE_IN_PROGRESS /* SHOULD NEVER HAPPEN */
         THEN ERRCODE=02005; /* SYSTEM ERROR */
      RETRANSLATE_IN_PROGRESS=1B;
      REMAINDER_OF_CARD=STMT_BUFFER;
      STMT_BUFFER=SBSTMTS;
      SBSTMTS='';
1READ_CARD:  PROCEDURE;
0DECLARE
      I FIXED BINARY;
0     DO UNTIL(SUBSTR(STMT_BUFFER,1,72)^=CHAR(' ',72));
         GET FILE(SYSIN)EDIT(STMT_BUFFER)(A(80));
      END;
0/*   THE FOLLOWING STATEMENT TRANSLATES FROM LOWER CASE
 /*      TO UPPER CASE.  ALTERNATE QUANDRY OPERATORS FOR
 /*      ASCII TERMINALS ARE ALSO TRANSLATED.  THE LAST
 /*      LINE OF THE STATEMENT MUST BE EDITED ASIS UNDER TSO.
 /*      IN NO CASE SHOULD STMT_BUFFER BE TRANSLATED TO
 /*      ALL BLANKS. */
-     STMT_BUFFER=TRANSLATE(STMT_BUFFER,
           'ABCDEFGHIJKLMNOPQRSTUVWXYZ&×^',
           'abcdefghijklmnopqrstuvwxyz@%$'); /* lower case */
0     IF ^ENV.TSO
         THEN PUT FILE(SYSPRINT)SKIP EDIT(STMT_BUFFER)(A);
0     IF VERIFY(SUBSTR(STMT_BUFFER,73,8),' 0123456789')=0
         THEN I=72;
         ELSE I=80;
0     DO I=I BY -1 WHILE(SUBSTR(STMT_BUFFER,I,1)=' ');
      END;
      IF SUBSTR(STMT_BUFFER,I,1)='#'&I>1
         THEN I=I-1;
      STMT_BUFFER=SUBSTR(STMT_BUFFER,1,I);
0END READ_CARD;
0END GETSTMT;
