 (CHECK(ERRCODE)):  GETSTMT:  PROCEDURE(STTYPE,STMT)RECURSIVE REORDER;
 %INCLUDE DCLSTMT;
 %INCLUDE EXTVAR,EXTVAR2,EXTRTN,
1     FILES,DCLRTRAN;
-DECLARE
      (CHAR,INDEX,LENGTH,SUBSTR,TRANSLATE,VERIFY)BUILTIN,
      STMT_BUFFER CHAR(2001)STATIC VARYING INITIAL(''),
      REMAINDER_OF_CARD CHAR(80)STATIC VARYING INITIAL(''),
      SLASH FIXED BINARY(31);
1/*   GETSTMT IS CALLED TO ASSEMBLE A STATEMENT AND DETERMINE
 /*   THE STATEMENT TYPE.
 /*      THE ORIGINAL SOURCE OF STATEMENTS IS THE FILE SYSIN.
 /*           A STREAM FILE IS USED FOR PROMPTING, PADDING, ETC.
 /*           UNDER TSO.  SINCE PL/I DOES NOT TRANSLATE LOWER CASE
 /*           TO UPPER CASE FOR STREAM TERMINAL FILES, GETSTMT
 /*           MUST DO THIS.  ALTERNATE OPERATORS FOR ASCII TERMINALS
 /*           (@ FOR &, % FOR ×, AND $ FOR ^) ARE ALSO TRANSLATED
 /*           AT THIS TIME TO THEIR STANDARD COUNTERPARTS.  SUCH
 /*           OPERATORS WILL APPEAR IN ANY DIAGNOSTICS IN THEIR
 /*           STANDARD FORM.  TOUGH.
 /*      WHENEVER A CARD IS READ, BLANK CARDS (COLUMNS 1-72) ARE
 /*           SKIPPED.  THE FIRST NONBLANK CARD FOUND IS THEN
 /*           TRANSLATED.  IF COLUMNS 73-80 ARE NUMERIC, THEY
 /*           ARE ASSUMED TO REPRESENT A SEQUENCE NUMBER AND ARE
 /*           STRIPPED OFF.  TRAILING BLANKS ARE THEN ELIMINATED FROM
 /*           THE LINE.  A '#' IN THE LAST REMAINING COLUMN CAN BE
 /*           USED TO STOP TRAILING BLANK ELIMINATION AT THAT POINT.
 /*           THIS IS NOT MENTIONED IN ANY CURRENT USER DOCUMENTATION
 /*           BUT IS USED BY SBMIT IN CONSTRUCTING TEXT.
 /*      EVERY STATEMENT IS TERMINATED BY A '/'.  A STATEMENT MAY
 /*           RUN OVER SEVERAL LINES, AND SEVERAL STATEMENTS MAY
 /*           APPEAR ON THE SAME LINE.  THIS IS HANDLED BY ASSEMBLING
 /*           LINES IN A STATEMENT BUFFER.  THE FIRST STATEMENT
 /*           IS TAKEN OFF THE BUFFER EACH TIME.  BLANKS ARE
 /*           PERMITTED BEFORE THE IDENTIFYING KEYWORD (STTYPE) AND
 /*           MUST BE STRIPPED OFF BY GETSTMT.  A '#' IS ADDED TO
 /*           THE STATEMENT (RATHER THAN A '/') AS A TERMINATOR.
 /*      EACH STATEMENT IS VERIFIED TO CONTAIN A LIMITED CHARACTER
 /*           SET.  THIS IS NECESSARY SO THAT QUANDRY CAN INSERT
 /*           CONTROL TEXT.  (CURRENTLY ONLY '#' IS USED.)
 /*      QUANDRY PROGRAMS MUST BE TERMINATED BY AN END STATEMENT.
 /*           A MISSING END STATEMENT WILL BE SUPPLIED ON ENDFILE BY
 /*           GETSTMT.  THIS FEATURE IS USED BY SBMIT, WHICH DOES
 /*           NOT GENERATE AN END STATEMENT, AND BY OTHER ROUTINES
 /*           TO GUARANTEE TERMINATION.
 /*      GTCLEAR IS CALLED TO CLEAR THE STATEMENT BUFFER FOLLOWING
 /*            AN ERROR.  ANY STATEMENTS OR FRAGMENTS ON THE LAST
 /*            LINE ARE LOST.
 /*      GTRTRAN IS CALLED FOLLOWING RETAIN, DELETE, CHANGE, AND
 /*           COMPLETE STATEMENTS TO SET UP FOR RETRANSLATION.
 /*           THE STATEMENTS SAVED IN SBSTMTS ARE TRANSFERRED TO
 /*           THE STATEMENT BUFFER.
 /*           SUBSEQUENT CALLS TO GETSTMT WILL USE THE MODIFIED
 /*           STATEMENT BUFFER.  WHEN IT HAS BECOMES EMPTY,
 /*           RETRANSLATE_IN_PROGRESS WILL BE RESET AND NORMAL
 /*           PROCESSING RESUMED. CARE HAS BEEN TAKEN TO SUPPORT
 /*           THE CASE WHERE A CHANGE OR DELETE STATEMENT HAS BEEN
 /*           PLACED IN THE PROGRAM TEXT AS A RESULT OF A MADMAN
 /*           USING THE CHANGE COMMAND. */
1ON ENDFILE(SYSIN)STMT_BUFFER=CHAR('END/',80);
 ON STRINGSIZE ERRCODE=02002;
0     SCANPTR=1; /* INITIALIZE FOR DIAGNOSTICS */
0     IF LENGTH(STMT_BUFFER)=0
         THEN DO UNTIL(SUBSTR(STMT_BUFFER,LENGTH(STMT_BUFFER),1)='/');
              RETRANSLATE_IN_PROGRESS=0B;
              IF LENGTH(REMAINDER_OF_CARD)=0
                   THEN CALL READ_CARD;
              SLASH=INDEX(REMAINDER_OF_CARD,'/');
              IF SLASH=0
                   THEN SLASH=LENGTH(REMAINDER_OF_CARD);
              (STRINGSIZE):  STMT_BUFFER=STMT_BUFFER××
                   SUBSTR(REMAINDER_OF_CARD,1,SLASH);
              REMAINDER_OF_CARD=SUBSTR(REMAINDER_OF_CARD,SLASH+1);
         END;
0     SLASH=INDEX(STMT_BUFFER,'/');
      (STRINGSIZE):  STMT=SUBSTR(STMT_BUFFER,1,SLASH);
      STMT=SUBSTR(STMT,VERIFY(STMT,' '));
      SUBSTR(STMT,LENGTH(STMT),1)='#'; /* CHANGE '/' */
      STMT_BUFFER=SUBSTR(STMT_BUFFER,SLASH+1);
0     STTYPE=STMT;
      IF LENGTH(STMT)<=LENGTH(STTYPE)
         THEN SUBSTR(STTYPE,LENGTH(STMT),1)=' '; /* WIPE OUT '#' */
      STTYPE=SUBSTR(STTYPE,1,INDEX(STTYPE××' ',' ')-1);
0     SCANPTR=VERIFY(STMT,
         'ABCDEFGHIJKLMNOPQRSTUVWXYZ*0123456789 -.,''&×;=^><()');
      IF SCANPTR^=LENGTH(STMT) /* LOCATION OF '#' */
         THEN ERRCODE=02003;
0     RETURN;
1GTCLEAR:  ENTRY;
      REMAINDER_OF_CARD='';
      RETURN;
-GTRTRAN:  ENTRY;
      STMT_BUFFER=SBSTMTS××STMT_BUFFER;
      SBSTMTS='';
1READ_CARD:  PROCEDURE;
0DECLARE
      I FIXED BINARY;
0     DO UNTIL(SUBSTR(REMAINDER_OF_CARD,1,72)^=CHAR(' ',72));
         GET FILE(SYSIN)EDIT(REMAINDER_OF_CARD)(A(80));
      END;
0/*   THE FOLLOWING STATEMENT TRANSLATES FROM LOWER CASE
 /*      TO UPPER CASE.  ALTERNATE QUANDRY OPERATORS FOR
 /*      ASCII TERMINALS ARE ALSO TRANSLATED.  THE LAST
 /*      LINE OF THE STATEMENT MUST BE EDITED ASIS UNDER TSO.
 /*      IN NO CASE SHOULD REMAINDER_OF_CARD BE NULL. */
-     REMAINDER_OF_CARD=TRANSLATE(REMAINDER_OF_CARD,
           'ABCDEFGHIJKLMNOPQRSTUVWXYZ&×^',
           'abcdefghijklmnopqrstuvwxyz@%$'); /* lower case */
0     IF ^ENV.TSO
         THEN PUT FILE(SYSPRINT)SKIP EDIT(REMAINDER_OF_CARD)(A);
0     IF VERIFY(SUBSTR(REMAINDER_OF_CARD,73,8),'0123456789')=0
         THEN I=72;
         ELSE I=80;
0     DO I=I BY -1 WHILE(SUBSTR(REMAINDER_OF_CARD,I,1)=' ');
      END;
      IF SUBSTR(REMAINDER_OF_CARD,I,1)='#'&I>1
         THEN I=I-1;
      REMAINDER_OF_CARD=SUBSTR(REMAINDER_OF_CARD,1,I);
0END READ_CARD;
0END GETSTMT;
