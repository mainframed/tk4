*PROCESS INTERRUPT;
 (CHECK(ERRCODE)):  QUANDRY:  PROCEDURE OPTIONS(MAIN)REORDER;
 %INCLUDE EXTVAR,EXTVAR2,DCLPHEAD,DCLRTRAN; /* FIRST COPY SEEN */
1%INCLUDE DCLOPT,DCLPROPT,DCLSBOPT,FILES;
1%INCLUDE EXTRTN,EXTRTN2,EXTRTN3,EXTRTN4; /* FOR DOCUMENTATION */
1DECLARE
      (NULL,ONCODE,ONFILE,ONLOC,SUBSTR)BUILTIN;
0DECLARE
      STMT CHAR(1200)VARYING,
      STTYPE CHAR(10);
0DECLARE
      FIRST_TIME BIT(1)INITIAL(1B);
-/*   QUANDRY IS THE MAIN CONTROL AND ERROR-HANDLING ROUTINE OF
 /*      QUANDRY, A TEXT-RETRIEVAL PROGRAM TO SEARCH THE ABSTRACTS
 /*      ABSTRACTS PREPARED BY THE EDUCATIONAL RESOURCES INFORMATION
 /*      CLEARINGHOUSE (E. R. I. C.).
 /*
 /*      QUANDRY MAY BE EXECUTED IN BATCH OR UNDER TSO. THE
 /*           DATASET 'CCP11.Q.TEXT' CONTAINS THE HIGH-LEVEL
 /*           DOCUMENTATION FOR QUANDRY.  THE MEMBER DOC CONTAINS
 /*           INFORMATION ON FLOW OF CONTROL, EXTERNAL VARIABLES,
 /*           AND INSTALLATION.  THE MEMBER USRGUIDE CONTAINS THE
 /*           QUANDRY USER GUIDE.  MORE DETAILED PROGRAM LOGIC
 /*           IS PROVIDED AT THE BEGINNING OF EACH ASSEMBLER AND
 /*           PL/I MODULE.
 /*
 /*      AT INITIALIZATION, QUANDRY OPENS ALL FILES EXCEPT
 /*           FILE(SUBMIT), WHICH IS NEEDED ONLY FOR SUBMIT
 /*           STATEMENTS.  TESTENV IS CALLED TO INITIALIZE ALL
 /*           SYSTEM-DEPENDENT PARAMETERS.  A LISTTIME STATEMENT
 /*           IS PASSED TO COMPILE TO PRINT THE QUANDRY HEADER.
 /*           GETSTMT IS CALLED TO READ THE FIRST STATEMENT, AND
 /*           A PERFUNCTORY CHECK IS MADE TO PROHIBIT A NON-READ
 /*           STATEMENT FROM BEING SAVED BEFORE THE FIRST READ
 /*           STATEMENT.
 /*      THE MAIN BODY OF QUANDRY IS A LOOP IN WHICH THE PREVIOUS
 /*           STATEMENT IS PROCESSED BY COMPILE AND A NEW STATEMENT
 /*           ACQUIRED FROM GETSTMT.  THE LOOP IS TERMINATED WHEN
 /*           AN END STATEMENT IS ENCOUNTERED.  (GETSTMT WILL SUPPLY
 /*           A MISSING END STATEMENT IF ENDFILE IS RAISED.)
 /*      READ STATEMENTS, FIELD STATEMENTS, AND SAVED PRINT/NOPRINT
 /*           STATEMENTS ARE SAVED IN THE PROGRAM TEXT STRING
 /*           (SBSTMTS).  CERTAIN STATEMENTS PROCESSED BY COMPILE
 /*           MODIFY THE PROGRAM TEXT AND REQUIRE RETRANSLATION OF
 /*           PREVIOUS STATEMENTS.  THIS IS ACCOMPLISHED BY CALLING
 /*           THE ROUTINE GTRTRAN IN GETSTMT TO TRANSFER THE PROGRAM
 /*           TEXT TO THE INCOMING STATEMENT BUFFER.
 /*      THE FOLLOWING FLAGS SUPPORT RETRANSLATION:
1/*           RETRANSLATE-REQUESTS QUANDRY TO INITIATE RETRANSLATION
 /*           RETRANSLATE_READ-RETRANSLATION IS TO INCLUDE THE READ
 /*                STATEMENT (OTHERWISE LIMITED TO OTHER STATEMENTS)
 /*           RETRANSLATE_IN_PROGRESS-THE INCOMING STATEMENT BUFFER
 /*                CONTAINS STATEMENTS TO BE RETRANSLATED
 /*           ERROR_IN_PROGRAM_TEXT-THE PROGRAM TEXT CONTAINS ONE OR
 /*                MORE INCORRECT STATEMENTS AND MUST NOT BE EXECUTED
 /*           UNCHECKED_STMT_SAVED-AN UNCHECKED STATEMENT HAS BEEN
 /*                SAVED AND IS NOW BEING CHECKED.  IF AN ERROR IS
 /*                DETECTED, ERROR_IN_PROGRAM_TEXT SHOULD BE SET.
 /*      ERROR HANDLING ALTERS THE NORMAL FLOW OF CONTROL.  WHEN
 /*          AN ERROR IS DETECTED IN A STATEMENT AND THE ROUTINE
 /*          DIAGNOSING THE ERROR DOES NOT CORRECT IT, THE QERROR
 /*          CONDITION IS SIGNALLED OR, USUALLY, RAISED INDIRECTLY
 /*          BY RAISING CHECK(ERRCODE).  PRTERR IS CALLED TO PRINT
 /*          A DIAGNOSTIC.  IF THE STATEMENT CAUSING THE ERROR HAS
 /*          ALREADY BEEN SAVED (RETRANS.UNCHECKED_STMT_SAVED),
 /*          ERROR_IN_PROGRAM_TEXT IS SET TO PROHIBIT EXECUTING
 /*          THE SEARCH BEFORE THE ERROR HAS BEEN CORRECTED. A
 /*          DIRECT BRANCH IS TAKEN TO THE LABEL RESTART_PROGRAM
 /*          IN QUANDRY.  (THIS IS THE ONLY TIME A GO TO IS USED,
 /*          BUT IT IS PARTICULARLY HEINOUS SINCE IT IS DONE FROM
 /*          AN ON-UNIT AND TRANSFERS CONTROL OUT OF A BLOCK).
 /*          FLUSH IS CALLED TO RECOVER AND NORMAL PROCESSING IS
 /*          RESUMED.
 /*     THE OPERATION OF THE FLUSH ROUTINE DIFFERS IN BATCH AND TSO.
 /*          IN TSO, GTCLEAR IS CALLED TO CLEAR THE REMAINDER OF THE
 /*          LINE.  IN BATCH, STATEMENTS ARE SKIPPED OR SYNTAX-CHECKED
 /*          UNTIL THE BEGINNING OF THE NEXT SEARCH.  FLUSH RETURNS
 /*          HAVING ACQUIRED THE NEXT STATEMENT TO BE PROCESSED. */
1ON CHECK(ERRCODE)SIGNAL CONDITION(QERROR);;
0ON CONDITION(QERROR)BEGIN;
      (NOCHECK(ERRCODE)):  CALL PRTERR(STMT,ERRCODE);
      IF UNCHECKED_STMT_SAVED
         THEN DO;
              UNCHECKED_STMT_SAVED=0B;
              ERROR_IN_PROGRAM_TEXT=1B;
         END;
      GO TO RESTART_PROGRAM;
 END;
0ON ERROR SNAP BEGIN;
      ON ERROR SNAP SYSTEM;
      IF ONCODE>=80&ONCODE<=93 /* UNDEFINEDFILE */
         THEN IF ONFILE='SYSPRINT'
              THEN STOP;
              ELSE PUT SKIP EDIT('THE DD CARD FOR ''',ONFILE,
                   ''' IS MISSING OR INCORRECT')(A)
                   ('CHECK YOUR JCL OR ENTER A QNDINIT TSO COMMAND')
                   (SKIP,A);
         ELSE DO;
              PUT SKIP EDIT
              ('AN ERROR OF TYPE ',ONCODE,' HAS OCCURRED IN ',ONLOC)(A)
              ('PLEASE SHOW THIS TO DON HUDSON AT THE COMPUTER CENTER')
                   (SKIP,A);
              CALL COMPILE('LISTOBJ   ','LISTOBJ#');
              CALL COMPILE('LISTTREE  ','LISTTREE#');
         END;
 END;
0ON ATTENTION;
1     OPEN FILE(SYSPRINT)OUTPUT LINESIZE(132), /* OPEN FIRST */
         FILE(ERIC)DIRECT INPUT,
         FILE(INDX)SEQUENTIAL INPUT,
         FILE(MAP)DIRECT INPUT,
         FILE(SYSIN)INPUT,
         FILE(SYSOUT)PRINT PAGESIZE(59);
0     CALL TESTENV;
0     CALL PRTITLE;
0     CALL GETSTMT(STTYPE,STMT);
      IF STTYPE^='READ      '&STTYPE^='OPTIONS   '&
         STTYPE^='END       '&SUBSTR(STTYPE,1,4)^='LIST'
         THEN ERRCODE=01001; /* ONLY CHECKED FIRST TIME--TOUGH */
-RESTART_PROGRAM:
      IF FIRST_TIME
         THEN FIRST_TIME=0B;
         ELSE CALL FLUSH;
0     DO WHILE(STTYPE^='END       ');
         IF STTYPE='READ      '
              THEN ERROR_IN_PROGRAM_TEXT=0B;
         CALL COMPILE(STTYPE,STMT);
         IF RETRANSLATE
              THEN DO;
                   RETRANSLATE,ERROR_IN_PROGRAM_TEXT=0B;
                   RETRANSLATE_IN_PROGRESS=1B;
                   OBJCODE,LITPOOL='';
                   CALL GTRTRAN;
              END;
         CALL GETSTMT(STTYPE,STMT);
      END;
-     CLOSE FILE(SYSPRINT),FILE(SYSOUT),FILE(SYSIN),
         FILE(INDX),FILE(MAP),FILE(ERIC),FILE(SUBMIT);
1FLUSH:  PROCEDURE;
      IF ENV.TSO
         THEN CALL GTCLEAR;
      CALL GETSTMT(STTYPE,STMT);
      IF ENV.BATCH
         THEN DO WHILE(STTYPE^='READ      '&STTYPE^='END       ');
              SELECT(STTYPE);
                   WHEN('GO        ','TALLY     '); /* NO RUNNING */
                   WHEN('RETAIN    ','DELETE    ','CHANGE    ',
                        'COMPLETE  ');
                        /* NO RETRANSLATE DURING FLUSH */
                   OTHERWISE CALL COMPILE(STTYPE,STMT);
              END;
              CALL GETSTMT(STTYPE,STMT);
         END;
0END FLUSH;
0END QUANDRY;
