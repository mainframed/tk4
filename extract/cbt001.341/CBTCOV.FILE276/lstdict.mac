*PROCESS INTERRUPT;
 (CHECK(ERRCODE)):  LSTDICT:  PROCEDURE(STTYPE,STMT)REORDER;
0%INCLUDE DCLSTMT,DCLINDX,EXTVAR,FILES;
1DECLARE
      FINISHED BIT(1),
      TALLY FIXED BINARY(31)AUTO INITIAL(0),
      DESCRIPTOR CHAR(32)VARYING,
      (LENGTH,SUBSTR)BUILTIN;
0/*   LSTDICT IS CALLED TO ''COMPILE'' LISTDICT STATEMENTS.
 /*      STMT CONTAINS A LISTDICT STATEMENT IN THE FORM
 /*                LISTDICT DESCRIPTOR#
 /*           OR
 /*                LISTDICT#
 /*           WITH EXACTLY ONE BLANK BEFORE THE DESCRIPTOR.
 /*      'COMPILATION' CONSISTS OF LISTING COMPLETE
 /*           DESCRIPTORS (KEYS) AND THEIR OCCURRENCE COUNTS
 /*           FOR ALL RECORDS IN FILE(INDX) WHOSE KEYS BEGIN
 /*           WITH THE SPECIFIED ACCESS KEY.  THE COMPLETE
 /*           FILE IS PRINTED IF NO DESCRIPTOR IS SPECIFIED.
 /*       ON UNITS ARE ESTABLISHED TO TERMINATE THE PRINT LOOP
 /*           WHEN AN INVALID KEY IS ENCOUNTERED, WHEN ENDFILE
 /*           OCCURS, OR WHEN AN ATTENTION INTERRUPT OCCURS.
 /*       A DESCRIPTOR LENGTH OF AT LEAST 4 IS REQUIRED UNDER
 /*           TSO. */
1ON ENDFILE(INDX)FINISHED=1B;
 ON KEY(INDX)FINISHED=1B;
 ON ATTENTION FINISHED=1B;
0     FINISHED=0B;
0     IF ENV.TSO
         THEN DO;
              IF LENGTH(STMT)<14
                   THEN ERRCODE=05012;
              PUT FILE(SYSPRINT)SKIP(2)EDIT
                   ('HIT ATTENTION TO STOP THE LISTING')(A);
         END;
      PUT FILE(SYSPRINT)SKIP(3);
0     IF LENGTH(STMT)<=10
         THEN DO;
              DESCRIPTOR='';
              READ FILE(INDX)INTO(INDX_ENTRY)KEY('*');
         END;
         ELSE DO;
              DESCRIPTOR=SUBSTR(STMT,10,LENGTH(STMT)-10);
              READ FILE(INDX)INTO(INDX_ENTRY)KEY(DESCRIPTOR);
         END;
0     DO WHILE(SUBSTR(INDX_ENTRY.DESCRIPTOR,1,LENGTH(DESCRIPTOR))=
         DESCRIPTOR&^FINISHED);
         TALLY=TALLY+INDX_ENTRY.RECORD_COUNT;
         PUT FILE(SYSPRINT)SKIP EDIT
              (INDX_ENTRY.DESCRIPTOR,INDX_ENTRY.RECORD_COUNT)
              (A,F(10));
         READ FILE(INDX)INTO(INDX_ENTRY);
      END;
0     PUT FILE(SYSPRINT)SKIP(2)EDIT
         ('SUM OF COUNTS',TALLY)(A(32),F(10));
0END LSTDICT;
