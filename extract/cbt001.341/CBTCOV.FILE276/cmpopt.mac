 (CHECK(ERRCODE)):  CMPOPT:  PROCEDURE(STTYPE,STMT)REORDER;
 %INCLUDE DCLSTMT;
 %INCLUDE EXTVAR,EXTRTN,SYSPRINT,DCLPARM,
1     DCLOPT,DCLSBOPT,DCLPROPT;
0DECLARE
      (MAX,MIN,SUBSTR)BUILTIN,
      I FIXED BINARY(31)STATIC,
      NAME CHAR(12)STATIC,
      STRING CHAR(12)STATIC,
      STRING_LENGTH FIXED BINARY(15)STATIC,
      NUMBER FIXED BINARY(31)STATIC;
1/*   CMPOPT IS CALLED TO COMPILE OPTION STATEMENTS.
 /*      CALLING ARGUMENTS ARE STTYPE (UNUSED) AND STMT.
 /*           THE FORM OF STMT IS
 /*                OPTIONS OPTION_LIST#
 /*           WHERE THE OPTION_LIST BEGINS IN COLUMN 9 AND
 /*           CONSISTS OF OPTIONS OF THE FORM
 /*                KEYWORD    OR     KEYWORD(VALUE)
 /*           SEPARATED BY BLANKS OR COMMAS.  BLANKS SHOULD ONLY
 /*           BE USED AS OPTION SEPARATORS OR AS SIGNIFICANT
 /*           BLANKS WITHIN CHARACTER VALUES.  THIS IS NOT
 /*           RIGIDLY ENFORCED.
 /*      THE STATEMENT IS SCANNED BY THE GENERAL PURPOSE
 /*           ROUTINE SCNPARM AND THE TOKENS ARE STORED IN
 /*           IN THE ARRAY PARM OF STRUCTURES.  AT THIS TIME,
 /*           VALUES WHICH LOOK LIKE NUMBERS ARE CONVERTED
 /*           TO NUMERIC FORM AND CONVERSION ERRORS DIAGNOSED.
 /*           THIS COULD BE A PROBLEM WITH ARBITRARY NON-NUMERIC
 /*           PARAMETERS (E. G., '9  9').
 /*      THE VALUES IN PARM ARE THEN MOVED INTO THE APPROPRIATE
 /*           OPTIONS STRUCTURE:  SBOPT FOR SUBMIT OPTIONS,
 /*           PROPT FOR PRINT OPTIONS, OR OPT FOR ALL OTHER
 /*           OPTIONS.  DIAGNOSTICS FOR INVALID OR MISSING
 /*           PARAMETERS ARE ISSUED.
 /*      CMPOPT DOES NOT USE THE STANDARD ERROR INTERFACE.
 /*           ERRORS ARE TRAPPED AND A MESSAGE IS ISSUED
 /*           THROUGH THE PRTERR SUBROUTINE.  THE REMAINDER
 /*           OF THE STATEMENT IS PROCESSED RATHER THAN
 /*           BEING DISCARDED.  THIS MEANS THAT ONLY THE
 /*           INCORRECT OPTIONS NEED BE REENTERED.
 /*      THE FOLLOWING UNDOCUMENTED OPTIONS ARE SUPPORTED:
 /*           CONTROL--PASSWORD FOR RESTRICTED OPTIONS.
 /*           ENV(BATCH)--SETS ENV FLAGS FOR DEBUGGING.
 /*           ENV(TSO)--SETS ENV FLAGS FOR DEBUGGING.
 /*           READLIM--SETS READ LIMIT FOR EXSRCH.
 /*           READSIZE--SETS AREA SIZE FOR CMPRD.
 /*      THE FOLLOWING UNDOCUMENTED RESTRICTIONS EXIST:
 /*           BIN--LENGTH(2:4) AND NOT NUMERIC.
 /*           COLUMN--RANGE(1:30).
 /*           CONTROL--PASSWORD VALUE.
 /*           LINESIZE--MUST BE >= 20.
 /*           PAGESIZE--MUST BE >= 59.
 /*           PRINTLIM--RANGE(0:40) FOR TSO WITHOUT CONTROL.
 /*           READSIZE--RANGE(5000:64000),(5000:20000) FOR TSO.
 /*           REMOTE--LENGTH(4:5) BEGINNING 'RMT' OR 'LOCAL'.
 /*           SKIP--MUST BE POSITIVE.
 /*           SPACE--MUST BE POSITIVE. */
1ON CHECK(ERRCODE)(NOCHECK(ERRCODE)):  CALL PRTERR(STMT,ERRCODE);
0     SCANPTR=9;
      CALL SCNPARM(STMT,PARM,NUMBER_OF_PARMS,0); /* IGNORE RETCODE */
-     DO I=1 TO NUMBER_OF_PARMS;
0        SCANPTR=PARM.SCANPTR(I);
         NAME=PARM.NAME(I); /* COPYING OVER REDUCES SIZE OF OBJ PGM */
         STRING=PARM.STRING(I);
         STRING_LENGTH=PARM.STRING_LENGTH(I);
         NUMBER=PARM.NUMBER(I);
0        IF PARM.SUBPARMS(I)×NAME='CHANGE      '×NAME='NOCHANGE    '
              THEN SELECT(NAME);
                   WHEN('USERID      ','JOBNAME     ','ENV         ',
                        'BIN         ','REMOTE      ','DEST        ',
                        'LINES       ','CHANGE      ','NOCHANGE    ',
                        'ACCOUNT     ')
                        CALL SET_PARM;
                   OTHERWISE IF ^PARM.SUBNUMERIC(I)
                        THEN ERRCODE=06015;
                        ELSE CALL SET_PARM;
              END;
              ELSE ERRCODE=06115;
      END;
1SET_PARM:  PROCEDURE;
0/* ERROR CODES 06215 AND 16016 ARE USED WITHOUT CHANGING
 /* THE MIDDLE DIGIT WHEN THE ERROR LOCATION CAN BE
 /* DETERMINED FROM THE SELECT VARIABLE. */
0        SELECT(NAME);
0             WHEN('ACCOUNT     ')SBOPT.ACCT=STRING;
0             WHEN('BIN         ')
                   IF STRING_LENGTH<2×STRING_LENGTH>4×
                        PARM.SUBNUMERIC(I)
                        THEN ERRCODE=06215;
                        ELSE SBOPT.BIN=SUBSTR(STRING,1,4);
0             WHEN('CHANGE      ')
                   OPT.CHANGE=1B;
0             WHEN('COLUMN      ')
                   IF NUMBER>30×NUMBER<1
                        THEN ERRCODE=06215;
                        ELSE PROPT.COLUMN=NUMBER;
0             WHEN('CONTROL     ')
                   IF NUMBER^=1603
                        THEN ERRCODE=06016;
                        ELSE OPT.CONTROL=NUMBER;
0             WHEN('ENV         ')
                   IF OPT.CONTROL=0
                        THEN ERRCODE=06016;
                   ELSE IF STRING='TSO         '
                        THEN DO;
                             ENV.TSO=1B;ENV.BATCH=0B;
                        END;
                   ELSE IF STRING='BATCH       '
                        THEN DO;
                             ENV.BATCH=1B;ENV.TSO=0B;
                        END;
                   ELSE ERRCODE=06215;
0             WHEN('FORMS       ')
                   SBOPT.FORMS=NUMBER;
0             WHEN('JOBNAME     ')
                   SBOPT.JOBNAME=STRING;
1             WHEN('LINES       ')
                   SBOPT.LINES=STRING;
0             WHEN('LINESIZE    ')
                   PROPT.LINESIZE=MAX(NUMBER,20);
0             WHEN('NOCHANGE    ')
                   OPT.CHANGE=0B;
0             WHEN('PAGENO      ')
                   PROPT.PAGENUM=NUMBER;
0             WHEN('PAGESIZE    ')
                   PROPT.PAGESIZE=MIN(NUMBER,59);
0             WHEN('PRINTLIM    ')
                   IF OPT.CONTROL=0&NUMBER>40&ENV.TSO
                        THEN ERRCODE=06016;
                        ELSE OPT.PRINTLIM=NUMBER;
0             WHEN('READLIM     ')
                   IF OPT.CONTROL=0
                        THEN ERRCODE=06016;
                        ELSE OPT.READLIM=NUMBER;
0             WHEN('READSIZE    ')
                   IF NUMBER<5000×NUMBER>64000×
                        (NUMBER>20000&ENV.TSO)
                        THEN ERRCODE=06215;
                        ELSE OPT.READSIZE=NUMBER;
0             WHEN('REMOTE      ' /* OLD KEYWORD */,'DEST        ')
                   IF NAME='REMOTE     '&
                        (STRING_LENGTH<4×STRING_LENGTH>5×
                        (STRING^='LOCAL       '&
                        SUBSTR(STRING,1,3)^='RMT'))
                        THEN ERRCODE=06215;
                        ELSE SBOPT.REMOTE=STRING;
1             WHEN('SEARCH      ')
                   PROPT.SEARCH=NUMBER;
0             WHEN('SKIP        ')
                   PROPT.SKIP=MAX(NUMBER,1);
0             WHEN('SPACE       ')
                    PROPT.SPACE=MAX(NUMBER,1);
0             WHEN('TIME        ')
                   SBOPT.TIME=NUMBER;
0             WHEN('USERID      ')
                   IF STRING_LENGTH<5×STRING_LENGTH>7
                        THEN ERRCODE=06215;
                        ELSE SBOPT.USERID=STRING;
0             OTHERWISE ERRCODE=06116;
         END;
0END SET_PARM;
0END CMPOPT;
