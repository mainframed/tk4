         TITLE ' N C C F   A U T H O R I Z A T I O N   R O U T I N E'
NCFOSATH CSECT
***********************************************************************
* THIS IS THE TSSO AUTHORIZATION ROUTINE. IT IS CALLED BY THE NCCF    *
* VERSION OF OSCMD TO                                                 *
* DETERMINE THE AUTHORITY LEVEL OF THE NCCF USER TO ISSUE MVS COMMANDS*
*                                                                     *
* THE AUTHORIZATION BYTE RETURNED BY THIS MODULE SHOULD BE IN THE FORM*
* REQUIRED BY THE TSSOPCMD ROUTINE. TO ALLOW FOR THE CASE WHERE NO    *
* ACCESS IS RETURNED, A RETURN CODE 4 ISRETURNED TO OSCMD. THIS MODULE*
* CAN BE REPLACED, AND THEN RELINKED INTO NCFOSCMD AS DESIRED BY THE  *
* INSTALLATION.                                                       *
*                                                                     *
*                                                                     *
* ARGUMENTS: 0(R1) - ADDRESS OF A 8 BYTE FIELD CONTAINING THE USERID  *
* ARGUMENTS: 4(R1) - ADDRESS OF A 1 BYTE FIELD CONTAINING THE AUTH BYTE
*
* RETURN CODES:
*
* 0: NCFOSATH SUCCESSFULL: AUTHORITY BYTE RETURNED IN PARM LIST.      *
* 4: NCFOSATH SUCCESSFULL: NO ACCESS TO NCFOSCMD IS PERMITTED.        *
*
* ANYTHING ELSE: NCFOSATH UNSUCCESSFULL - ABORT COMMAND.
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF NCFOSATH
         LR    R8,R1              SAVE INPUT PARMS.
         USING NCFOSATH,R12
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         GETMAIN R,LV=72          WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=4096         GETMAIN SOME PRIVATE STORAGE...
         LR    R11,R1
         USING AUTHDATD,R11
***********************************************************************
* PROCESS PARAMETERS...                                               *
*                                                                     *
***********************************************************************
         L     R2,0(R8)              GET ADDRESS OF USERID
         MVC   USERID(8),0(R2)       AND PUT THAT AWAY, ALSO
*
         L     R2,4(R8)              GET ADDRESS OF AUTHBYTE
         ST    R2,AAUTH              PUT IT AWAY.
*
FULLAUTH DS    0H
         CLC   USERID(8),=CL8'CONSINFO'   INFO ONLY USER ?
         BE    INFOAUTH
         CLC   USERID(8),=CL8'CONSNOAT'   INFO ONLY USER ?
         BE    NOAUTH
         MVI   AUTHBYTE,B'11100001'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
PARTAUTH DS    0H
***********************************************************************
* IN OUR CASE, PART AUTHORITY MEANS I/O ONLY                          *
***********************************************************************
         MVI   AUTHBYTE,B'01000000'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
INFOAUTH DS    0H
         MVI   AUTHBYTE,B'00000000'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
NOAUTH   DS    0H
         MVI   AUTHBYTE,B'00000000'  AS DEFINED BY TSSOPCMD
         MVC   RETCODE(2),=H'4'
         B     PASTAUTH
PASTAUTH DS    0H
         L     R2,AAUTH              GET ADDRESS OF OSCMD AUTHBYTE
         MVC   0(1,R2),AUTHBYTE      AND PUT IT IN CALLER PARM LIST
         B     ENDPROG
ENDPROG  DS    0H
         LH    R8,RETCODE
         FREEMAIN R,LV=4096,A=(R11)   FREEMAIN THE PRIVATE STORAGE
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
         B     ENDPROG1
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8              PICK UP RETURN CODE.
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
***********************************************************************
* DATA AREA FOR NCFOSATH CONSTANTS.                                   *
*                                                                     *
***********************************************************************
*
***********************************************************************
* DSECT AREA FOR NCFOSATH                                             *
*                                                                     *
***********************************************************************
AUTHDATD DSECT
AAUTH    DS    A                  ADDRESS OF AUTH BYTE PASSED TO US
USERID   DS    CL8
RETCODE  DS    H                  RETURN CODE FROM NCFOSATH
AUTHBYTE DS    XL1                LOCAL COPY OF AUTHBYTE
         IKJPSCB
         END   NCFOSATH
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY NCFOSATH(''TSSO V 4.3.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    NCFOSATH'
 PUNCH ' NAME     NCFOSATH(R)'
         END
