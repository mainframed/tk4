        PROC 0
        /***********************************************************/
        /* THIS CLIST WILL ISSUE A JES2 COMMAND TO DISPLAY TO      */
        /* SPOOL UTILIZATION. IF THE SPOOL UTILIZATION IS          */
        /* ABOVE 80%, THE CLIST WILL ISSUE A HILIGHTED MESSAGE     */
        /* TO THE OPERATOR. OTHERWISE, IT WILL ISSUE AN ORDINARY   */
        /* MESSAGE INDICATING HOW BUSY THE SPOOL IS. IT UTILIZES   */
        /* THE COMMAND TRAPPING FACILITIES IN TSSO 4.2             */
        /*                                                         */
        /***********************************************************/

        /*************************************************************/
        /* THE FIRST STEP IS DIRECTING THE OSCMD COMMAND PROCESSOR   */
        /* TO SEND THE RESPONSE OF THE COMMAND TO CLIST VARIABLES.   */
        /*************************************************************/
        SET CMDRESP = CLIST

        /*************************************************************/
        /* THE NEXT  STEP IS THE SETTING OF MAXCMDOUT. SINCE THE     */
        /* EXPECTED COMMAND RESPONSE IS 3 LINES, WE SET MAXCMDOUT = 3*/
        /*************************************************************/
        SET MAXCMDOUT = 3


        /*************************************************************/
        /* NEXT, WE DECIDE HOW LONG THE CLIST SHOULD WAIT FOR THE    */
        /* COMMAND. 1 SECOND IS PLENTY FOR THIS JES COMMAND.         */
        /*************************************************************/
        SET CMDWAIT = 1


        /*************************************************************/
        /* NOW THAT THE TWO INPUT VARIABLES ARE SET UP, WE WILL      */
        /* ISSUE THE OSCMD COMMAND. THE RESPONSE WILL BE PLACED IN   */
        /* CLIST VARIABLES.                                          */
        /*************************************************************/
           OSCMD $DSPOOL

        /*************************************************************/
        /* THE EXPECTED COMMAND RESPONSE LOOKS LIKE THIS.            */
        /* $DSPOOL                                                   */
        /* $HASP646 XX PERCENT SPOOL UTILIZATION.
        /*                                                           */
        /* NOTE THAT LINE1 IS ALWAYS AN ECHO OF THE ORIGINAL COMMAND */
        /*************************************************************/

        /*************************************************************/
        /* IF WE DO NOT GET BACK AT LEAST 2 LINES OF RESPONSE, THEN  */
        /* AN ERROR HAS OCCURRED. WRITE A MESSAGE TO THE OPERATOR    */
        /* AND TERMINATE.                                            */
        /*************************************************************/
           IF &CMDOUT < 2 THEN +
           DO
             OSWTOH PROBLEM WITH THE CHKSPL COMMAND - CONTACT SUPPORT
             EXIT CODE(12)
           END

        /*************************************************************/
        /* ASSUMING EVERYTHING WENT OK WITH THE COMMAND, THE RESPONSE*/
        /* IS RETURNED IN THE SECOND WORD OF THE SECOND LINE OF THE  */
        /* MESSAGE. TSSO HAS ALREADY PARSED THE MESSAGE. WE PICK UP  */
        /* THE UTILIZATION.                                          */
        /*                                                           */
        /*************************************************************/
           SET UTIL = &CMDOUT2W2


        /*************************************************************/
        /* NOW, WE LOOK AT THE SPOOL UTILIZATION, AND DETERMINE IF   */
        /* IT IS GREATER THE 80%. IF SO, SEND A HILIGHTED MESSAGE.   */
        /*                                                           */
        /*************************************************************/
           IF &UTIL > 80 THEN +
           DO
              OSWTOH SPOOL UTILIZATION IS &UTIL - CONTACT JES2 SUPPORT
           END
           ELSE +
           DO
              OSWTO  SPOOL UTILIZATION IS &UTIL PERCENT.
           END
