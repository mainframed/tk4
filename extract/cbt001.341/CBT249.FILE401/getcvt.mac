**********************************************************************
* THIS MACRO WILL RETURN THE TSSO COMMUNICIATIONS VECTOR TABLE
* METHOD OF OPERATION:
*  1) SCAN THE SUBSYSTEM VECTOR TABLE FOR TSSO
*  2) IF NOT FOUND, RC = 12 AND EXIT
*  3) IF FOUND, RETURN IN SPECIFIED REGISTER AND EXIT WITH RC = 0
*
*  NOTE THAT THE ADDRESS OF THE TSSO CVT IS STORE IN THE SSCTSUSE
*  FIELD FOR THE TSSO SSUBSYSTEM
**********************************************************************
         MACRO
&LABEL   GETCVT &R
         L     &R,16
         USING CVT,&R
         L     &R,CVTJESCT
         USING JESCT,&R
         L     &R,JESSSCT         POINT TO HEAD OF SSCT
         USING SSCT,&R
SRCH&SYSNDX DS 0H
         L     &R,SSCTSCTA        POINT TO NEXT ONE
         LTR   &R,&R
         BZ    NOTF&SYSNDX
         CLC   SSCTSNAM(4),=CL4'TSSO'   IS THIS ONE TSSO
         BE    FOUND&SYSNDX
         B     SRCH&SYSNDX
NOTF&SYSNDX DS  0H
         XR    R15,R15
         LA    R15,12             SET BAD RETURN CODE
         B     DONE&SYSNDX
FOUND&SYSNDX DS  0H
         L     &R,SSCTSUSE        LOAD UP TSSO CVT FIELD
         XR    R15,R15            SET RETURN CODE
         B     DONE&SYSNDX
DONE&SYSNDX DS  0H
         USING TSSOCVT,&R
         MEND
