REPLY    MENTER 12,EQU,CP=CP,COM=REPLYLOADER
********************************************************************
* THIS COMMAND WILL SERVE AS THE LOADING PROGRAM FOR THE REPLY     *
* COMMAND FUNCTION OF TSSO. THIS MODULE WILL LOCATE, AND LOAD THE  *
* REPLY COMMAND MODULE INTO GLOBAL STORAGE, AND THEN ISSUE A LINK  *
* TO IT, AFTER PARSING THE COMMAND USING THE TSO SERVICE ROUTINES  *
*
* METHOD OF OPERATION
*
* 1) OBTAIN COMMAND BUFFER
* 2) USING IKJPARSE TO DETERMINE SYNTAX OF COMMAND: EXPECTED SYNTAX
*    IS:
*
*    REPLY MESSAGE(XXXXXXXX) TEXT(' ')
*    IF THE MESSAGE ID IS THE IMS MESSAGE ID FOR OUTSTANDING WTORS
*    THEN THE COMMAND WILL TAKE ON THE FOLLOWING SYNTAX:
*
*
*    REPLY IMS(IMS COPY) TEXT('IMS COMMAND')
*
*
*
* 3) THE REPLY COMMAND WILL THEN BUILD A PARM LIST TO BE PASSED TO
*    THE REPLYLOA LOAD MODULE. THE PARM LIST IS AS FOLLOWS
*
*    IMS NOT SPECIFIED              IMS SPECIFIED
*
*    MSGNUM DC CL8' '               MSGNUM DC CL8'DFS999I'
*    PARMIMS DC CL4'NONE'           PARMIMS DC CL4' '
*    REPTXT DC CL100' '             REPTXT DC  CL100' '
*
* 4) THE REPLY COMMAND WILL LOAD THE REPLYLOA LOAD MODULE, AND
*    AND BRANCH TO IT, WAITING PATIENTLY UNTIL IT RETURNS.
*    FOR COMMENTS ON HOW THE REPLYLOA MODULE WILL DO ITS MAGIC,
*    READ THE COMMENTS FOR THAT MDOULE.
*
* 5) THE REPLY COMMAND WILL DELETE THE REPLYLOA LOAD MODULE, AND
*    TERMINATE.
*
********************************************************************
         L     R2,CPPLPSCB
         USING PSCB,R2
         TM    PSCBATR2,X'C0'           WELL ?
         BO    PROCEED
         L     R9,X'224'               GET ASCB INTO R9
         USING ASCB,R9
         L     R9,ASCBCSCB
         CLC   16(4,R9),=CL4'TSSO'     ALLOW TSSO TO REPLY TO ANYTHING
         BE    PROCEED
         B     IMPLEXEC
IMPLEXEC DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR8,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
PROCEED  DS    0H
         SETPARSE  PCL=REPLYPCL
         CALLTSSR EP=IKJPARS
         LTR   R15,R15
         BZ    PARSEOK
         B     PARSERR
         B     ENDPROG
PARSEOK  DS    0H
         L     R9,MYANS                 GET RESULTS OF PARSE
         USING IKJPARMD,R9
         TM    MSGIDFLD+6,X'80'         WAS MESSAGE SPECIFIED?
         BO    MSGIDOK                  YUP, HANDLE IT
         B     GETIMS                   OTHERWISE, ASSUME IMS
MSGIDOK  DS    0H
* GO AND MOVE THE MSGID INTO THE REPLYLOA PARM LIST.
         LH    R2,MSGIDFLD+4            GET LENGTH OF MESSAGE ID
         BCTR  R2,0                     SUBTRACT 1 FOR EXECUTE
         L     R3,MSGIDFLD              GET ADDRESS
         EX    R2,MOVEMSG               AND MOVE IT
         MVC   PARMIMS(4),=CL4' '       INDICATE NO IMS SPECIFIED
         B     EX1                      PROCEED
MOVEMSG  MVC   PARMMSG(0),0(R3)         ** EXECUTED **
EX1      DS    0H
         B     GETTEXT                  GET THE TEXT
GETIMS   DS    0H
         TM    IMSFLD+6,X'80'            WAS IMS SPECIFIED ?
         BO    IMSOK                    YUP...
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR3,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     PARSERR
IMSOK    DS    0H
         LH    R2,IMSFLD+4              GET LENGTH OF IMS FIELD
         CH    R2,=H'3'                 MUST BE EXACTLY 3
         BE    IMSOK3
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR4,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     PARSERR
IMSOK3   DS    0H
         L     R2,IMSFLD                GET ADDRESS
         MVC   PARMIMS+1(3),0(R2)       AND MOVE IT
         MVI   PARMIMS,C'I'             INDICATE IMS REQUESTED
         B     GETTEXT
GETTEXT  DS    0H
* HERE, IMS WAS NOT SPECIFIED  SO WE GET THE REPLY TEXT.
* NOW MOVE IN THE REPLY TEXT
         TM    REPLYFLD+6,X'80'
         BO    REPTXTOK
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR5,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     PARSERR
REPTXTOK DS    0H
         LH    R2,REPLYFLD+4
         L     R3,REPLYFLD
         BCTR  R2,0
         EX    R2,MOVETXT
         B     EX2
MOVETXT  MVC   PARMTEXT(0),0(R3)
EX2      DS    0H
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   NOAPF
         MODESET KEY=ZERO
         LOAD  EP=REPLYLOA,ERRET=NOLOAD,GLOBAL=(YES,P)
         L     R1,PARMADDR       LOAD UP PARM ADDRESS
         LR    R15,R0                   GET ADDRESS OF REPLYLOA
         OI    FLAGS,X'80'              SET FLAG TO INDICATE LOAD
         BALR  R14,R15                  BRANCH TO REPLYLOA
         LTR   R15,R15           WAS REPLY MADE ?
         BZ    REPLIED                  DIAGNOSE RETURN CODE
         CLC   PARMIMS(4),=CL4' '   ATTEMPTING TO REPLY TO IMS ?
         BE    ERRNOIMS             NOPE !
         MVC   ERRLNE1(3),PARMIMS+1
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR1,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
ERRNOIMS DS    0H
         MVC   ERRLNE2+38(8),PARMMSG
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR2,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
REPLIED  DS    0H
         B     ENDPROG
ENDPROG  DS    0H
         TM    FLAGS,X'80'       WAS REPLYLOA LOADED ?
         BZ    NODELETE          NO, DONT BOTHER DELETING
         DELETE EP=REPLYLOA
NODELETE DS    0H
         MLEAVE
NOLOAD   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR6,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
PARSERR  DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR7,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
NOAPF    DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(ERROR9,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
ERROR1   DC    H'80',H'0'
ERRLNE1  DC    CL80'XXX DOES NOT HAVE AN OUTSTANDING REPLY'
ERROR2   DC    H'80',H'0'
ERRLNE2  DC    CL80'THERE ARE NO OUTSTANDING REPLIES FOR "XXXXXXXX"'
ERROR3   DC    H'80',H'0'
ERRLNE3  DC    CL80'YOU MUST SPECIFY EITHER AN IMS COPY, OR A MESSAGE IX
               D TO REPLY TO'
ERROR4   DC    H'80',H'0'
ERRLNE4  DC    CL80'IMS IDS ARE EXACTLY 3 CHARACTERS- RE-ENTER THE COMMX
               AND WITH VALID IMS COPY'
ERROR5   DC    H'80',H'0'
ERRLNE5  DC    CL80'REPLY TEXT MISSING- RE-ENTER THE COMMAND WITH TEXT X
               FOR THE REPLY'
ERROR6   DC    H'80',H'0'
ERRLNE6  DC    CL80'SEVERE ERROR LOADING THE REPLYLOA MODULE- CONTACT  X
               A SYSTEMS PROGRAMMER'
ERROR7   DC    H'80',H'0'
ERRLNE7  DC    CL80'REPLY COMMAND TERMINATING DUE TO PARSE ERROR'
ERROR8   DC    H'80',H'0'
ERRLNE8  DC    CL80'IKJ56500I COMMAND REPLY NOT FOUND'
ERROR9   DC    H'80',H'0'
ERRLNE9  DC    CL80'REPLY WAS CALLED IN AN UNAUTHORIZED ENVIRONMENT- REX
               QUEST ABORTED'
FLAGS    DS    H
* BYTE 1:
* X'80' = REPLYLOA WAS LOADED.
WKA      DS    2D
PRINTOCC DS    F
PARMADDR DC    A(PARMS)
PARMS    DS    0D
PARMMSG  DC    CL8' '
PARMIMS  DC    CL4' '
PARMTEXT DC    CL120' '
REPLYPCL IKJPARM
MSGID    IKJKEYWD
         IKJNAME 'MESSAGE',SUBFLD=MSGIDSUB
         IKJNAME 'MSGID',SUBFLD=MSGIDSUB
REPTXT   IKJKEYWD
         IKJNAME 'REPLYTXT',SUBFLD=REPLYSUB
         IKJNAME 'TEXT',SUBFLD=REPLYSUB
IMS      IKJKEYWD
         IKJNAME 'IMS',SUBFLD=IMSSUB
MSGIDSUB IKJSUBF
MSGIDFLD IKJIDENT 'MSGID',PROMPT='MESSAGE ID',FIRST=ALPHA,             X
               OTHER=ALPHANUM,HELP=('MESSAGE NUMBER TO REPLY TO'),     X
               MAXLNTH=8
REPLYSUB IKJSUBF
REPLYFLD IKJIDENT 'REPLY',PROMPT='REPLY TEXT',FIRST=ALPHANUM,          X
               OTHER=ALPHANUM,HELP=('TEXT OF REPLY'),                  X
               MAXLNTH=120,CHAR
IMSSUB   IKJSUBF
IMSFLD   IKJIDENT 'IMS',FIRST=ALPHANUM,                                X
               OTHER=ALPHANUM,HELP=('IMS COPY TO REPLY TO'),           X
               MAXLNTH=3
         IKJENDP
         CVT DSECT=YES
         IKJIOPL
         IKJPSCB
         IHAASCB
         END REPLY
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY REPLY(''V 1.3.3 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    REPLY'
 PUNCH ' NAME     REPLY(R)'
         END
