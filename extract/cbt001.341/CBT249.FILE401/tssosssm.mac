         TITLE '   T S S O S S S M  '
***********************************************************************
*                                                                     *
*        'TSSOSSSM'                                                   *
*                                                                     *
***********************************************************************
*            THIS IS THE SUBSYSTEM SUPPORT MODULE FOR TSSO.
* THIS MODULE IS LOADED INTO CSA BY THE TSSO MAINLINE
* PROGRAM.
***********************************************************************
*
* THE VARIOUS ENTRY POINTS ARE IN THE TABLE WITH LABEL: TABSTART
*
*
***********************************************************************
*
*            C A U T I O N
*
*            TSSO REFERENCES SPECIFIC OFFSETS IN THE FIRST
*            50 BYTES OF THIS MODULE. DO NOT CHANGE THE LENGTHS
*            OF ANY FIELDS IN THIS AREA UNLESS TSSO IS ALSO CHANGED.
***********************************************************************
* NOTE: IN ALL FUNCTIONS, REGISTER 8 WILL POINT TO THE TSSO CVT,
* OBTAINED FROM THE SSCTUSE FIELD.
***********************************************************************
TSSOSSSM START
@ORIGIN  EQU   *
* IF THIS MODULE IS CALLED, OR EXECUTED IN ANY WAY, THE BR 14 WILL
* RETURN TO CALLER
         BR    R14
         DC    CL13'TSSOSSSM 4.1.0'
         DC    CL16'&SYSDATE &SYSTIME'
         ORG   @ORIGIN+40
***********************************************************************
* THE TSSO MODULE WILL BE LOADING ENTRIES INTO THE SSVT
* BASED ON THIS TABLE. THE FORMAT OF THE TABLE IS DOCUMENTED IN TSSO
***********************************************************************
         DC    F'2'                NUMBER OF FUNCTION ROUTINES
TABSTART DS    0H
         DC    F'10',A(FUNC10)
         DC    F'9',A(FUNC9)
************************************************************
*                                                          *
*        SVC 34 EXIT - COMMANDS                            *
*                                                          *
************************************************************
         SPLEVEL SET=1
FUNC10   DS     0H
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSO
         USING  FUNC10,R12         GET ADDRESSIBILITY
         LR     R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
*
         LR     R10,R8
         USING  SSOB,R10
*
         GETCVT R8                 FIND TSSO CVT
         USING TSSOCVT,R8
*
         TM    $SVSTUS,$SVSTUST    TERMINATING?
         BO    S34OEXIT            YES - IGNORE
*
*****************************************************************
* IF TSSO IS TERMINATED, UNDER NO CIRCUMSTANCES DO ANYTHING !
*****************************************************************
         L     R9,SSOBINDV         POINT TO FUNCTION AREA
         USING SSCMBGN,R9          ADDRESS IT.
         L     R2,SSCMBUFF         POINT TO COMMAND BUFFER.
         LH    R5,0(,R2)           GET LENGTH
* COMMAND BUFFER FORMAT IS AS FOLLOWS:
* 2 BYTES: LENGTH
* 2 BYTES: WHO KNOWS
* 140 BYTES: COMMAND
         CH    R5,=H'140'          OUTRAGEOUS LENGTH?
         BH    S34OEXIT            YES - IGNORE IT
         LTR   R5,R5               NEGATIVE LENGTH ?
         BNP   S34OEXIT            YES - IGNORE
         TM    $SVFLAG,X'01'       MONITORING?
         BNO   NM1
         WTO   'TSSO WILL NOT INTERFERE UNLESS # CMD'
*****************************************************************
* TSSO SHOULD NOT INTERFERE WITH THIS OS COMMAND                *
*****************************************************************
NM1      DS    0H
         CLC   4(1,R2),TSSOCHAR    MINE?
         BNE   S34OEXIT            NO - IGNORE
CMDPROC  DS    0H
         TM    $SVFLAG,X'01'       MONITORING?
         BNO   NM2
         WTO   'TSSO INTERFERING'
*****************************************************************
* HERE, WE HAVE DETERMINED THAT THE OS COMMAND SHOULD BE HANDLED*
* BY THE TSSO MAINLINE MODULE, IF TSSO IS AVAILABLE.            *
*****************************************************************
NM2      DS    0H
         TM    $SVSTUS,$SVSTUST    TERMINATING?
         BNO   STAT2               YES - IGNORE
         WTO   'TSSA023A TSSO IS IN SHUTDOWN STATUS- CMD REJECTED'
         B     S34OEXIT            IGNORE COMMAND
STAT2    DS    0H
         L     R3,NEXTCMD          ADDRESS OF NEXT AVAILABLE
         USING TSSOCMND,R3         ADDRESS RING POSITION
*                                  COMMAND BUFFER
         TM    CMNDFLAG,X'80'      IS THIS BUFFER STILL BUSY.
         BNO   STAT3               YES - BRANCH
     WTO   'TSSA024A TSSO IS BUSY WITH PREVIOUS COMMNANDS'
     WTO   'TSSA024A (CONT) THIS MAY INDICATE A TSSO SYSTEM PROBLEM'
         B     S34BUSY
STAT3    DS    0H
         MVI   CMNDCMD,X'40'
         MVC   CMNDCMD+1(139),CMNDCMD  BLANK THE COMMAND AREA
         OI    CMNDFLAG,X'80'      TURN ON COMMAND FLAG
         BCTR  R5,0                LENGTH MINUS 1 FOR EX
         EX    R5,MOVECMD          COPY THE COMMAND
         B     AR1                 BRANCH AROUND MVC
MOVECMD  MVC   CMNDCMD(0),4(R2)    (EXECUTED)
AR1      DS    0H
         MVC   CMNDLENG(4),0(R2)   COPY THE LENGTH
*****************************************************************
* CONSOLE ID WORKS AS FOLLOWS:
* IF COMMAND HAS COME FROM A CONSOLE, THEN THE FIRST 3 BYTES OF THIS
* FIELD ARE ZEROES, AND THE LAST IS THE CONSOLE ID.
*****************************************************************
         CLC   SSCMSCID(3),=XL6'000000'  LOOKS LIKE A CONSOLE ID?
         BE    STAT4                     YUP, LOOKEN GOOD.
* HERE, WE HAVE FOUND A COMMAND THAT WAS NOT ISSUED FROM A CONSOLE
* WE WILL INVALIDATE THE BUFFER BY RESETTING THE FLAG, AND
* ISSUE A MESSAGE TO THE CONSOLE, AND THE PROGRAMMER
         NI    CMNDFLAG,255-X'80'  INVALIDATE THE COMMAND
         MVC   X'A14'(4),SSCMSCID
         B     S34EXITM            EXIT WITH A MESSAGE
STAT4    DS    0H
         L     R0,SSCMSCID
         ST    R0,CMNDCONS         SAVE IT IN THIS TSSO COMMAND TOKEN.
         L     R2,CMNDNEXT
         ST    R2,NEXTCMD          DO THIS FOR NEXT TIME.
         TM    $SVFLAG,X'40'       IS 'OK' TO BE SUPPRESSED?
         BO    S34NOACK            YES - BRANCH
         WTO   '# OK',MCSFLAG=(REG0)
S34NOACK DS    0H
*****************************************************************
* NOW, THE IDEA IS TO POST THE TSSO MAINLINE ROUTINE, ONLY IF IT
* NEEDS TO BE POSTED. IF IT NEEDS TO BE POSTED, THEN THE WAIT
* BIT IN THE ECB SHOULD BE ON. WE WILL USE COMPARE AND SWAP
* FOR TWO REASONS. THE FIRST, IS THAT I NEVER USED A COMPARE AND
* SWAP, THE SECOND, IS THAT WE WANT TO ENSURE THAT TSSO DOESNT
* TURN THE WAIT BUT ON AFTER WE DO THE TM THAT WOULD BE REQUIRED, AND
* THEREFORE MISS THE POST.
*****************************************************************
         L     R2,$SVPOSTE         GET THE TSSO MAINLINE ECB
         L     R3,=X'40000000'
         TM    $SVPOSTE,X'80'      WAIT BIT ON ?
         BO    DOPOST              IF YES, DO THE POST.
         N     R2,=X'7FFFFFFFF'    COMPARE OPERAND WITH WAIT BIT OFF
         CS    R2,R3,$SVPOSTE      ** COMPARE AND SWAP **
         BE    POSTDONE            COMPARE AND SWAP DID THE POST.
DOPOST   DS    0H
         LA    R5,$SVPOSTE          GET ECB TO POST
         L     R6,$SVPASCB          GET ADDRESS SPACE OF TSSO
         LA    R7,S34ERRET          GET ERROR ROUTINE FOR XMEM POST
         LA    R1,$SVPOSTL          GET DSECT FOR XMEM POST
         SPACE
         POST  (R5),X'111',ASCB=(R6),ERRET=(R7),MF=(E,(1))
POSTDONE DS    0H
         B     S34MEXIT            INDICATE COMMAND IS MINE
*
S34BUSY  LA    R15,SSCMIMSG        COULD NOT EXECUTE
         B     S34EXIT             ISSUE IEE707I 'TEXT' NOT EXECUTED
*
S34MEXIT LA    R15,SSCMSUBC        MINE
         B     S34EXIT
*
S34OEXIT DS    0H
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
*
S34EXITM DS    0H                  NOT MINE, EXIT WITH MESSAGE
         WTO   'TSSO COMMANDS MAY NOT BE ISSUED FROM THIS ENVIRONMENT',X
               ROUTCDE=(2,11)
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
S34EXIT  DS    0H
         ST    R15,SSOBRETN
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         XR    R15,R15
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
*
S34ERRET BR    R14
@END     DC    0D'0'
         LTORG
************************************************************
* THIS IS THE ENTRY POINT FOR THE TSSO AUTOMATED OPERATIONS FACILITY
* WTO AND WTOR INTERCEPT ROUTINE. THE SUBSYSTEM INTERFACE CODE 9
* PROVIDES THE FACILITY FOR SUBSYSTEMS TO INTERCEPT ALL WTO AND WTOR
* REQUESTS, AND TAKE ACTION. THE SPECIFIC ACTIONS THAT TSSO/AOF WILL
* TAKE IS DETERMINED BY A TABLE, LOADED AT SUBSYSTEM (TSSO)
* INITIALIZATION TIME.
*
* THE FORMAT OF THE TABLE IS COMPLEX, AND BEST DOCUMENTED IN THE
* DISPAOF TSSO COMMAND, INCLUDED WITH TSSO.
*
* SPECIFIC ACTIONS ARE:
* OSCMD - ISSUE AN OPERATING SYSTEM, JES OR TSSO COMMAND IS RESPONSE
*         TO THE MESSAGE
*
* OSCMDT - SIMULATE THE ISSUANCE OF THE COMMAND, BY WTO INSTEAD
*          OF WTOR THE MESSAGE.
*
* HILITE - HILIGHT THE MESSAGE BY TURNING ON THE WQEDCMA BIT.
*
* SUPPRESS- SUPPRESS THE MESSAGE.
*
*    TSSO (VERSION 4.1) PROVIDES THE FACILITY FOR TESTING CERTAIN
* (ACTUALLY, ANY) FIELD IN THE MESSAGE BY WORD OR SUBSTRING CONTENT
* BEFORE ACTUALLY DECIDING TO PERFORM THE ACTION SPECIFIED.
*
*    TSSO (VERSION 4.1) WILL SCAN THE *ENTIRE* TABLE FOR THE
* MESSAGE IDENTIFIER, EVEN WHEN ENCOUNTERED, THUS MULTIPLE ACTIONS
* MAY BE SPECIFIED FOR THE SAME MESSAGE, UNDER DIFFERENT TEST
* CONDITIONS. THE USER IS URGED TO READ NOT ONLY THE TSSO SOURCE CODE,
* BUT THE TSSO SYSTEMS PROGRAMMING REFERENCE GUIDE, FOR DETAILS
* ON TSSO/AOF USAGE.
*                                        MARC.
*
************************************************************
*
* REGISTER USAGE:
*   R11: POINTS TO THE GETMAINED STORAGE AREA FOR THE LIFE OF THE
*        ROUTINE
*   R9 : POINTS TO THE SSOB EXTENTION FOR THE LIFE OF THE ROUTINE
*   R8 : POINTS TO THE TSSO CVT FOR THE LIFE OF THE ROUTINE.
*   R7 : POINTS TO THE TSSO AOF TABLE, FOR THE LIFE OF THE ROUTINE.
*   R6 : POINTS TO THE CURRENT TEST ENTRY FOR THE LIFE OF THE ROUTINE.
************************************************************
         DROP   R12
FUNC9    DS     0H
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSO
         USING  FUNC9,R12         GET ADDRESSIBILITY
         LR     R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
*
*
         GETMAIN R,LV=4096         GET SOME STORAGE...
         LR     R11,R1
         USING  FUN9DATA,R11
*
         LR     R10,R8
         USING  SSOB,R10
         ST     R10,SSOBADDR
*
         GETCVT R8                 FIND TSSO CVT
         USING TSSOCVT,R8
*
         L     R9,SSOBINDV         POINT TO WTO EXTENSION
         USING SSWTBGN,R9
*
         DROP  R10
************************************************************
* WE NOW HAVE ADDRESSIBILITY TO THE TSSO COMMUNICATIONS AREA
* WE BEGIN BY CHECKING IF THE WTO  INTERCEPTION FUNCTION IS
* ACTIVE.
************************************************************
         L     R7,TSSOTABL         GET ADDRESS OF TABLE.
         LTR   R7,R7               CHECK IT OUT
         BZ    FUNC9EXT            NO TABLE, NO WORK TO DO.
FUN9ACT  DS    0H
***********************************************************************
* IF WE GET HERE, THE TSSO/AOF COMPONENT IS ACTIVE, AND THERE IS A    *
* (VALID) TSSO AOF TABLE. THE VALIDITY OF THE TABLE IS DETERMINED AT  *
* TSSO STARTUP/TABLE RELOAD TIME. R7 WILL SERVE AS A BASE REGISTER    *
* FOR THE TSSO/AOF TABLE CURRENT ENTRY
***********************************************************************
* INITIALIZE VARIABLES.
***********************************************************************
         XC    FUNC9RC(2),FUNC9RC   INIT RETURN CODE
***********************************************************************
* THE FIRST PART OF THE ROUTINE WILL ISOLATE THE MESSAGE ID, BE IT A  *
* WTO OR WTOR, AND PRESERVE IT FOR FUTURE EXAMINATION.                *
***********************************************************************
         MVI   FUN9FLG,X'00'       RESET FLAG.
         L     R5,SSWTMIN          GET MINOR WQE ADDRESS
         LTR   R5,R5               CHECK IT OUT..
         BZ    MINZERO             DONT SET FLAG
         OI    FUN9FLG,X'80'       SSWTMIN ^= ZERO
MINZERO  DS    0H
         L     R5,SSWTORE          GET ORE ADDRESS.
         LTR   R5,R5               CHECK IT OUT..
         BZ    OREZERO             DONT SET FLAG
         OI    FUN9FLG,X'40'       SSWTORE ^= ZERO
         L     R5,SSWTWQE          GET QUEUE ELEMENT
         USING WQE,R5              GAIN ADDRESSABILITY TO WQE
         MVC   TXTMSG(8),WQETXT+4  MOVE IN MESSAGE FOR WTOR
         LA    R7,WQETXT+4         PICK UP STRING START FOR WQE
         ST    R7,STRSTART         AND REMEMBER FOR TSSOPARS LATER
         B     GOTORE
OREZERO  DS    0H
         L     R5,SSWTWQE          GET QUEUE ELEMENT
         LA    R7,WQETXT+1         PICK UP STRING START FOR WQE
         ST    R7,STRSTART         AND REMEMBER FOR TSSOPARS LATER
         USING WQE,R5
         MVC   TXTMSG(8),WQETXT+1  WHY THE +1-- WHY NOT ?
*                                  JUST SEEMS TO WORK BETTER
GOTORE   DS    0H
         ST    R5,WQEADDR          REMEMBER THE WQE ADDRESS
         DROP  R5                  DROP ADDRESSABILITY TO WQE
************************************************************
* HERE IS WHERE WE EXAMINE THE TEXT OF THE WTO TO SEE IF WE SHOUD
* TAKE ACTION.
* THE ADDRESS OF THE TABLE IS IN TSSOTABL, AND THE FORMAT IS DOCUMENTED
* IN THE TSSO: LOGIC GUIDE.
* R6: POINTS TO CURRENT TABLE ENTRY, DURING TEST PROCESSING.
************************************************************
         L     R7,TSSOTABL              BASE REGISTER FOR TABLE IS R7
         L     R7,0(R7)                 R7 POINTS TO THE FIRST ENTRY.
NTABENTR DS    0H
         ST    R7,CURTABAD              STORE CURRENT TABLE ADDRESS
         CLC   TXTMSG(8),4(R7)          CHECK FOR MATCH
         BE    MATCHMSG                 MESSAGE ID HAS BEEN MATCHED.
         CLC   4(8,R7),=CL8'*'          GENERIC MATCH ?
         BE    MATCHMSG                 YUP, PROCESS TEST ENTRIES.
         CLC   4(8,R7),=CL8'ZZZZZZZZ'   LAST ENTRY ?
         BE    FUNC9EXT
         L     R7,0(R7)                 GET NEXT ENTRY.
         B     NTABENTR                 WTO OR WTOR NOT FOUND.
MATCHMSG DS    0H
***********************************************************************
* WE COME HERE WHEN A MATCH HAS BEEN FOUND IN THE MESSAGE ID SECTION  *
* OF A TSSO TABLE ENTRY. THE PROCESSING CONTINUES WITH THE PERFORMING *
* OF ANY TESTS THAT NEEDS TO BE ISSUED. IF ALL TESTS ARE MATCHED, THAN*
* BRANCH TO THE PERFACT ROUTINE, WHICH WILL PERFORM ANY ACTIONS       *
* REQUIRED. IF THE TESTS ARE NOT MET, CONTINUE SEARCHING THE TABLE AS *
* THERE MAY BE ANOTHER MATCH.
***********************************************************************
         L     R6,20(R7)                 LOAD UP TEST ENTRY.
TESTLOOP DS    0H                        LOOP FOR TEST ENTRIES
         LTR   R6,R6                     ANY MORE TEST ENTRIES ?
         BZ    TESTSUCC                  NOPE, ALL TESTS MUSTBE OK
         L     R1,6(R6)            LOAD SUBSTR OR WORD NUMBER
         ST    R1,WORD1            AND REMEMBER IT.
         L     R1,10(R6)           LOAD LENGTH
         ST    R1,WORD2
         MVC   WORDSEPS(4),14(R6)  LOAD WORD SEPARATORS.
***********************************************************************
* WE MUST DETERMINE THE TEST TYPE, EITHER A WORD TYPE, OR A SUBSTR    *
* TYPE.                                                               *
***********************************************************************
         CLC   4(2,R6),=CL2'W'     IS IT A WORD ?
         BE    WORDTYPE            YUP, GO PROCESS IT
         CLC   4(2,R6),=CL2'S'     IS IT A SUBSTRING ?
         BE    SUBSTYPE            YUP, GO PROCESS IT.
* NEITHER WORD NOR SUBSTRING, ** PROBLEM **
         WTO   'TSSE032A INVALID TEST TYPE- IGNORING MESSAGE ID'
         B     TESTFAIL            SIMULATE TEST FAILING !
WORDTYPE DS    0H
***********************************************************************
* WE COME HERE WHEN WE HAVE A WORD TEST. THE ROUTINE WILL PERFORM THE
* FOLLOWING PROCEDURES.
*                                                                     *
* 1) USE TSSOPARS TO PARSE THE MESSAGE, BASED ON THE SEPARATOR CHARS  *
*    SPECIFIED IN THE TABLE.                                          *
* 2) PERFORM THE TEST BASED ON THE DESIRED WORD, USING THE LENGTH     *
*    SPECIFIED IN THE TABLE.                                          *
* 3) EITHER GO TO TESTFAIL, OR PERFORM ANOTHER TEST.                  *
*                                                                     *
***********************************************************************
         L     R10,STRSTART      GET ADDRESS OF STRING
*                                 NOTE- COULD HAVE BEEN WQETXT+1 IF WTO
*                                 OR WQETXT+4 IF WTOR.
         ST    R10,PARS1         THERES OPERAND NUMBER 1
*
         MVC   PARS2(4),=F'127'   LENGTH OF WTO MESSAGES
*
         MVI   PARARRAY,X'40'
         MVC   PARARRAY+1(255),PARARRAY      BLANK OUT PARARRAY
*        MVC   PARARRAY+255(64),PARARRAY
         MVC   PARARRAY(2),=H'20'           MAX OF 20 TOKENS.
         LA    R10,PARARRAY      PARARRAY IS THIRD PARAMETER
         ST    R10,PARS3         STORE IN PARM LIST.
*
         LA    R10,WORDSEPS      USE WORD SEPARATORS ON TEST DIRECTIVE
         ST    R10,PARS4         AND STORE IN FOURTH WORD IN PARM LIST.
*
         LA    R1,PARSPARM
         CALL  TSSOPARS          ** P A R S E   I T   **
*        MVC   PARARRAY(2),=H'1'
         LH    R10,PARARRAY     HOW MANY DID WE GET BACK
         C     R10,WORD1        SEE WHICH ONE WAS SPECIFIED IN THE
*                                TEST PARAMETER LIST.
         BL    WORDFAIL          NOT ENOUGH WORDS.. FAIL THIS TEST.
         LA    R10,PARARRAY+2   POINT TO WORD NUMBER 1
         L     R5,WORD1          GET WORD  REQUESTED
         BCTR  R5,0
         SLL   R5,4              MULTIPLY BY 16
         LA    R10,0(R5,R10)     POINT TO TOKEN
         ST    R10,TOKADDR      REMEMBER WHERE TOKEN WAS FOUND.
***********************************************************************
* AT THIS POINT, TOKADDR CONTAINS THE ADDRESS OF THE TOKEN (WORD) THAT*
* WAS LOCATED BY TSSOPARS. WE WILL COMPARE THIS WORD, FOR N CHARACTERS*
* AGAINST WHAT WAS SPECIFIED IN THE TSSO/AOF TABLE. IF MATCHED, GREAT *
* IF NOT, TEST HAS FAILED.                                            *
*
* WE NEED TO LOAD UP THE MATCH TEXT AS SPECIFIED IN THE AOF TABLE.    &
***********************************************************************
         LH    R4,18(R6)        GET MATCHING TEXT LENGTH
***********************************************************************
* NOTE, THE MATCHING TEXT STRING IS AT 20(R6) WHERE R6 POINTS TO THE
* TEST PARAMETER ENTRY (TPE).
***********************************************************************
         L     R5,TOKADDR       LOAD TOKEN ADDRESS
         BCTR  R4,0             -1 FOR HARDWARE DEFIENCY.
         EX    R4,COMPSTR       DO COMPARE !
         BE    WORDSUCC        WORD MATCHED !!
         BNE   WORDFAIL         WORD DIDNT MATCH !!!
COMPSTR  CLC   0(0,R5),20(R6)   ** EXECUTED **
WORDSUCC DS    0H
************************************************************
* WE COME HERE IF THE WORD MATCHED, IMPLYING THAT THIS PARTICULAR
* TEST WAS SUCCESSFULL. NOW WE WILL GO SEE IF THERE ARE ANY MORE
* TESTS..
************************************************************
         L     R6,0(R6)         GET NEXT TEST ENTRY
         B     TESTLOOP         AND TRY AGAIN...
*
WORDFAIL DS    0H
         B     TESTFAIL         IF THIS TEST FAILED, ALL TESTS
*                               FOR THIS ENTRY FAIL, AND WE
*                               CONTINUE SEARCHING THROUGH THE
*                               TABLE.
*
SUBSTYPE DS    0H
***********************************************************************
* THE USER REQUESTED A SUBSTRING TYPE OF TEST ON THE MESSAGE. THE     *
* ROUTINE WILL PROVIDE THAT TEST. THE TEST IS:                        *
* SUBSTR(MVSWTO,START IN AOF,LENGTH IN AOF) ?= MATCHTEX IN AOF.       *
*                                                                     *
***********************************************************************
         L     R5,STRSTART      GET START OF MVS MESSAGE.
         L     R4,WORD1         GET START AS SPECIFIED IN AOF.
         BCTR  R4,0             -1 TO GET CORRECT CHARACTER.
         LA    R5,0(R4,R5)      POINT TO START OF SUBSTRING IN WQE
         L     R4,WORD2         GET LENGTH FOR COMPARE.
         BCTR  R4,0             -1 FOR EXECUTE
         EX    R4,COMPSTR2      PERFORM THE SUBSTRING COMPARE.
         BE    SUBSSUCC         THEY MATCHED! -> TEST SUCCEDED.
         BNE   SUBSFAIL         NO MATCHED- SUBS FAILED
***********************************************************************
* NOTE THAT THE MATCH TEXT IS IN 20(R6) WHERE R6 POINTS TO THE CURRENT
* AOF TABLE.
***********************************************************************
COMPSTR2 CLC   0(0,R5),20(R6)
SUBSSUCC DS    0H
***********************************************************************
* WE GET HERE IF THE SUBSTRING TEST WAS SUCCESSFULL. TSSO/AOF WILL
* CHECK THE NEXT ENTRY IN THE TABLE AND ATTEMPT THE NEXT TEXT
***********************************************************************
         L     R6,0(R6)
         B     TESTLOOP
*
SUBSFAIL DS    0H
***********************************************************************
* WE COME HERE IF THIS SUBSTRING TEST HAS FAILED. IF THIS IS THE CASE *
* THEN WE WILL CONTINUING SEARCH AOF TABLE ENTRIES IN HOPES OF AGAIN  *
* FINDING THE MSG ID.
***********************************************************************
         B     TESTFAIL
TESTFAIL DS    0H
***********************************************************************
* WE COME HERE IF A WORD OR SUBSTRING TEST FAILS. IF THIS SHOULD      *
* HAPPEN, WE CONTINUE SEARCHING THROUGH ALL TSSO/AOF TABLE ENTRIES    *
* FOR ANOTHER MATCH ON THE MSGID.                                     *
***********************************************************************
         L     R7,0(R7)         GET NEXT AOF TABLE ENTRY
         B     NTABENTR         AND DO THE NEXT TEST
*
TESTSUCC DS    0H
***********************************************************************
* WE COME HERE IF ALL TESTS HAVE BEEN SUCCUSSFULL FOR THIS TABLE ENTRY*
* TSSO WILL GO AND PERFORM THE ACTION REQUESTED AND CONTINUE SEARCHING*
* THE TABLE FOR ANY ADDITIONAL ENTRIES.                               *
***********************************************************************
         B     PERFACT
*
PERFACT  DS    0H
************************************************************
* WE COME HERE WHEN WE HAVE FOUND A WTO IN THE TABLE, AND THE TESTS
* SPECIFIED FOR THE ENTRY HAVE BEEN MET. AT THAT TIME, WE PERFORM
* THE ACTION SPECIFIED IN THE ACTION= PARAMETER FOR THE ENTRY.
*
* PROCESSING CONTINUES AS FOLLOWS:
*
* 1) WE IDENTIFIY WHAT THE TABLE WANTS US TO DO, I.E. ISSUE AN OS
*    COMMAND, TSSO COMMAND, REPLY, OR WHATEVER. WE THEN BRANCH TO THE
*    APPROPRIATE ROUTINE.
*    NOTE THAT R7 POINTS TO THE ENTRY IN THE TSSO/AOF TABLE.
*           R8 POINTS TO THE ORE
*
************************************************************
         CLC  12(8,R7),=CL8'REPLY'      REQUEST TO REPLY ?
         BE    FUN9RPLY
         CLC  12(8,R7),=CL8'SUPPRESS'    REQUEST TO SUPPRESS.
         BE    FUN9SUPP
         CLC  12(8,R7),=CL8'HILIGHT'    REQUEST TO HILIGHT ?
         BE    FUN9LITE
         CLC  12(8,R7),=CL8'HIGHLITE'  REQUEST TO HILIGHT ?
         BE    FUN9LITE
         CLC  12(8,R7),=CL8'LOWLIGHT'   REQUEST TO TURN OFF HIGHLITE
*                                       DESCRIPTOR ?
         BE    FUN9LOW
         CLC  12(8,R7),=CL8'OSCMD'      REQUEST FOR AN OS COMMAND ?
         BE    FUN9CMND
         CLC  12(8,R7),=CL8'OSCMDT'     REQUEST FOR AN OS COMMAND ?
*                                       IN TEST MODE ?
         BE    FUN9CMDT
         WTO   'TSSOS03A INVALID DIRECTIVE'
         B     FUNC9EXT                 SCOTTY, BEAM ME UP.
FUN9CMDT DS    0H
         OI    FUN9FLG,X'20'           INDICATE TEST MODE
         B     FUN9CMND
FUN9CMND DS    0H
************************************************************
* HERE IS THE ENTRY POINT FOR ISSUEING OS COMMANDS BASED ON
* WTO'S.
************************************************************
         TM    FUN9FLG,B'10000000'      CHECK OUT SSWT TYPES
* IF SSWTMIN ^= 0 THEN WE WONT PROCESS THIS MINOR WQE.
         BO    GETNEXT          ONLY IF SSWTMIN ^= 0
CMNDGO   DS    0H
         LA    R2,MVSCMD+4       STARTING POINT FOR MVS CMD
         MVI   MVSCMD+4,X'40'         BLANK OUT THE
         MVC   MVSCMD+5(105),MVSCMD+4 COMMAND BUFFER
         LA    R3,26(R7)         START POINT FOR MVS DIRECTIVE
         LH    R4,24(R7)         LENGTH OF MESSAGE
CMNDLOOP DS    0H
         LTR   R4,R4            CHECK TO SEE IF ANY CHARS REMAIN
*                               IN BUFFER. THIS COULD HAVE HAPPENED
*                               IF THE FORMATTING OPTION HAD BEEN
*                               THE LAST THING IN THE TEXT OPERAND
         BZ    ISSUECMD
         CLI   0(R3),C'\'       SIGNIFICANCE CHARACTER ON?
         BE    SIGON             WELL.. IT IS NOW.
         MVC   0(1,R2),0(R3)
         LA    R2,1(R2)
         LA    R3,1(R3)
         BCT   R4,CMNDLOOP
         B     ISSUECMD          GO DO THE COMMAND
*
SIGON    DS    0H
************************************************************
* WE COME HERE WHEN WE FIND THE '\' DIRECTIVE IN THE TSSO TABLE
* ENTRY FOR THIS MESSAGE. THIS MEANS WE HAVE SOME WORK TO DO.
************************************************************
         LA    R3,1(R3)           POINT TO NEXT CHARACTER
         CLI   0(R3),C'P'         LOOKING FOR POSITION CHARACTER?
         BE    SIGONP             YUP.. GOT IT
         CLI   0(R3),C'W'         LOOKING FOR SOME 'WORD' ?
         BE    SIGONW             YUP, GO DO IT !!
         B     SIGWRONG           INVALID '\' DIRECTIVE...
SIGONP   DS    0H
         CLI   4(R3),C','         CHECK FOR CORRECT FORMAT.
         BNE   SIGWRONG
         CLI   7(R3),C'\'         CHECK FOR CLOSING TERMINATOR.
         BNE   SIGWRONG           OH WELL...
*
         LA    R10,WORKAREA       POINT R10 TO WORKAREA FOR CVT
         LA    R0,3               SET LENGTH FOR FIRST CONVERT
         LA    R1,1(R3)           SET ADDRESS
         CALL  JCECVTDB           ** CONVERT **
         LTR   R1,R1
         BZ    SIGWRONG           INVALID DATA
         ST    R1,SIGSTART        REMEMBER STARTING POINT
         LA    R0,2               GET LENGTH
         LA    R1,5(R3)           GET NEW START POINT FOR CONVERT
         CALL  JCECVTDB           ** CONVERT **
         LTR   R1,R1
         BZ    SIGWRONG           INVALID DATA
         ST    R1,SIGCOUNT        SAVE COUNT
************************************************************
* AT THIS POINT, WE HAVE THE PARAMETERS NEEDED FOR TEXT
* SUBSTITUTION IN THE OS COMMAND TO BE ISSUED.
************************************************************
         L     R1,SIGCOUNT              GET COUNT FOR EXECUTE
         BCTR  R1,0                     MAKE UP FOR HARDWARE FAULTS
         L     R10,STRSTART             GET START ADDRESS OF TEXT
         L     R5,SIGSTART              GET STARTING POINT
         CH    R5,=H'127'               CHECK STARTING POINT
         BH    SIGWRONG                 IF TO HIGH, FORGET IT
         LA    R10,0(R10,R5)            POINT TO STARTING POINT IN WQE
*                                       FOR MOVE.
         BCTR  R10,0                    GET EXACT CORRECT POSTITION
         EX    R1,MOVESIG               MOVE THE STUFF...
         B     AR2
MOVESIG  MVC   0(0,R2),0(R10)
AR2      DS    0H
         A     R2,SIGCOUNT              NEXT CHAR IN CMD BUFFER
         LA    R3,8(R3)                 MOVE PAST \ STUFF IN TSSO TABLE
         SH    R4,=H'9'
         LTR   R4,R4
         BM    SIGWRONG
         B     CMNDLOOP
SIGWRONG DS    0H
         WTO   'TSSOS04A ERROR IN DEFINITION (P) FOR NEXT WTO MSG'
         B     FUNC9EXT
*
SIGONW   DS    0H
         CLI   7(R3),C'\'               TERMINATION FOR W DIRECTIVE
         BNE   SIGWBAD                 SHUCKS
         MVC   SEPCHAR(4),1(R3)        GET SEPARATOR CHARACTERS
         LA    R1,5(R3)                GET START FOR CONVERT RTN
         LA    R0,2
         LA    R10,WORKAREA
         CALL  JCECVTDB                GET DECIMAL
         LTR   R1,R1                   CHECK IT OUT
         BZ    SIGWBAD
         ST    R1,WORDREQ              THIS IS THE REQUESTED WORD
         CH    R1,=H'20'               BUT IS IT OK?
         BH    SIGWBAD                 NO, GUY DIDNT READ THE DOC
************************************************************
* NOW, WE MUST SET UP THE PARSE ROUTINE. THE GENERAL ID IS TO
* PARSE THE WTO MSG INTO USER DEFINED WORDS, AND THEN GET THE ONE
* HE WANTS.
************************************************************
         L     R10,STRSTART       GET ADDRESS OF STRING
         ST    R10,PARS1          THERES OPERAND NUMBER 1
*
         MVC   PARS2(4),=F'127'   LENGTH OF WTO MESSAGES
*
         MVI   PARARRAY,X'40'
         MVC   PARARRAY+1(255),PARARRAY      BLANK OUT PARARRAY
*        MVC   PARARRAY+255(64),PARARRAY
         MVC   PARARRAY(2),=H'20'           MAX OF 20 TOKENS.
         LA    R10,PARARRAY
         ST    R10,PARS3
*
         LA    R10,SEPCHAR
         ST    R10,PARS4
*
         LA    R1,PARSPARM
         CALL  TSSOPARS          ** P A R S E   I T   **
*        MVC   PARARRAY(2),=H'1'
         LH    R10,PARARRAY      HOW MANY DID WE GET BACK
         C     R10,WORDREQ       SEE HOW MANY HE WANTED
         BL    NOWORDS           NOT ENOUGH WORDS.. FORGET IT.
         LA    R10,PARARRAY+2    POINT TO WORD NUMBER 1
         L     R5,WORDREQ        GET WORDS REQUESTED
         BCTR  R5,0
         SLL   R5,4              MULTIPLY BY 16
         LA    R10,0(R5,R10)     POINT TO TOKEN... NOW WE GET LENGTH
         ST    R10,TOKADDR       REMEMBER WHERE TOKEN WAS FOUND.
         XR    R5,R5
         LA    R1,16             TOKENS ARE 16 BYTES LONG
************************************************************
* AT THIS POINT, R10 POINTS TO THE TOKEN RETURNED BY THE PARSE ROUTINE
* THAT THE USER REQUESTED.
************************************************************
WLOOP    DS    0H
         CLI   0(R10),C' '
         BE    ENDTOKEN
         LA    R5,1(R5)
         LA    R10,1(R10)
         BCT   R1,WLOOP
ENDTOKEN DS    0H
         ST    R5,LWORDREQ       STORE LENGTH OF REQUESTED WORD
************************************************************
* AT THIS POINT, WE HAVE ALL THE INFORMATION NEEDED TO DO  *
* THE PARAMETER SUBSTITUTION. R2 POINTS TO THE SPOT TO     *
* SUBSTITUTE.                                              *
************************************************************
         L     R5,LWORDREQ              GET LENGTH OF WORD
         BCTR  R5,0                     MINUS 1 FOR EXECUTE
         L     R10,TOKADDR
         EX    R5,MOVEWORD
         B     AR3
MOVEWORD MVC   0(0,R2),0(R10)
AR3      DS    0H
         A     R2,LWORDREQ
         LA    R3,8(R3)
         SH    R4,=H'9'
         LTR   R4,R4
         BM    SIGWBAD
         B     CMNDLOOP
NOWORDS  DS    0H
         WTO   'TSSOS05W NOT ENOUGH TOKENS IN BELOW WTO TO PROCESS'
         B     FUNC9EXT
SIGWBAD  DS    0H
         WTO   'TSSOS06W ERROR PROCESSING W DIRECTIVE- MSG IGNORED'
         B     FUNC9EXT
ISSUECMD DS    0H
         TM    FUN9FLG,X'20'            WAS OSCMD SPECIFIED ?
         BNO   ISSURCMD                GO ISSUE REAL COMMAND
* HERE, WE WILL NOT ISSUE THE COMMAND, BUT WE WILL WTO OUT WHAT THE
* COMMAND WOULD HAVE BEEN
         MVC   WTO1D(WTO1CE-WTO1C),WTO1C
         MVC   WTO1D+13(109),MVSCMD+4
         WTO   MF=(E,WTO1D)
         B     GETNEXT            TRY THE NEXT TSSO/AOF TABLE ENTRY
*
ISSURCMD DS    0H
         MVC   MVSCMD(2),=H'109'
         MVC   MVSCMD+2(2),=H'0'
         LH    R0,MASTCONS
         LA    R1,MVSCMD
         SVC   34
         B     GETNEXT
FUN9LITE DS    0H
         L     R5,WQEADDR               GET ADDRESS OF THIS WQE
         USING WQE,R5                   GAIN ADDRESSABILITY TO WQE FOR
*                                       HILIGHT
         OI    WQEDC1,WQEDCB            HILITE THE SUCKER
         DROP  R5                       NO LONGER NEED ADDRESSABILITY
*                                       FOR HILIGHT
         B     GETNEXT
*
FUN9LOW  DS    0H
***********************************************************************
* THIS IS THE ROUTINE TO "LOWLITE" A MESSAGE. THE ROUTINE BASICALLY   *
* INTERCEPTS THE MESSAGE, AND FLIPS OFF THE WQEDCA BIT.               *
*                                                                     *
*                                                                     *
***********************************************************************
         L     R5,WQEADDR               GET ADDRESS OF THIS WQE
         USING WQE,R5                   GAIN ADDRESSABILITY TO WQE FOR
*                                       LOW LIGHT
         NI    WQEDC1,255-WQEDCA+WQEDCB+WQEDCC+WQEDCK
         NI    WQEXA,255-WQEWTOR        FAKE OUT COMTASK INTO THINKING
*                                       THE MESSAGE IS NOT A WTOR...
* HILIGHTING CAUSED BY DESCRIPTOR CODES 1, 2 3 AND 11.
*        MVC   X'A00'(2),WQEDC1
         DROP  R5                       NO LONGER NEED ADDRESSABILITY
         B     GETNEXT
*
FUN9SUPP DS    0H
***********************************************************************
* THIS IS THE SUPPRESS HANDLER. THE SUPPRESS FUNCTION IS PROBABLY THE *
* CLEANEST IN THE WHOLE PRODUCT, BECAUSE SUBSYSTEM CAN CAUSE MESSAGES *
* TO BE SUPPRESSED. NOTE THAT THIS FUNCTION WILL BE INEFFECTIVE FOR   *
* WTOR REQUESTS, HOWEVER, THE LOWLIGHT FUNCTION CAN BE USED.          *
***********************************************************************
         LA    R5,SSWTNDSP              LOAD CODE FOR SUPPRESSION
         STH   R5,FUNC9RC               AND STORE IT AWAY.
         B     GETNEXT                  GET A NEW ENTRY
*
FUN9RPLY DS    0H
***********************************************************************
* THIS IS THE ENTRY POINT FOR THE TSSO AOF REPLY FUNCTION. THE IDEA   *
* HERE IS TO INTERCEPT THE WTOR, FORMAT A REPLY COMMAND OF THE FORM   *
* R NN,MSG-TEXT WHERE NN IS PICKED UP FROM THE ORE (OREID) FIELD.     *
* MSG-TEXT IS PICKED UP FROM THE AOF TABLE. THIS IS USEFULL IN        *
* NUKING OUTSTANDING REPLIES, LIKE IMS. THE REPLY COMMAND CAN THEN BE *
* USED TO REPLY TO THOSE OUTSTANDING MESSAGES.                        *
***********************************************************************
         L     R5,SSWTORE               GET THE OPERATOR REPLY ELEMENT
         LTR   R5,R5                    MAKE SURE..
         BZ    GETNEXT
         USING OREF,R5                  GAIN ADDRESSABILITY TO ORE.
         MVC   REPLY(2),=H'64'
         MVC   REPLY+2(2),=H'0'
         MVC   REPLY+4(6),=CL6'REPLY'
         MVI   REPLYCOM,C','
         MVC   REPLYNUM(2),OREID        MOVE IN AUTOMATIC REPLY NUM
         LH    R1,24(R7)                PICK UP LENGTH OF REPLY
         BCTR  R1,0
         MVI   REPLYMSG,X'40'
         MVC   REPLYMSG+1(99),REPLYMSG   CLEAR REPLYMSG
         EX    R1,MOVERPLY
         B     AR4
MOVERPLY MVC   REPLYMSG(0),26(R7)          MOVE IN AUTOMATIC REPLY
AR4      DS    0H
         LH    R0,MASTCONS              LETS DO REPLY FROM MASTCONS
         LA    R1,REPLY
         SVC   34
         B     GETNEXT
         DROP  R5               DROP ADDRESSABILITY TO OREF
GETNEXT  DS    0H
************************************************************
* TSSO/AOF HAS PERFORMED THE REQUESTED FUNCTION, HOWEVER, WE CAN NOW
* CONTINUE TO SCAN THE TSSO/AOF TABLE, IN HOPES OF FINDING ANOTHER
* TABLE ENTRY TO MATCH THIS MESSAGE ID.
************************************************************
         L     R7,0(R7)         GET THE NEXT TSSO/AOF TABLE ENTRY
         B     NTABENTR         AND SEE IF WE CAN GET A MATCH
FUNC9EXT DS    0H
         LH    R8,FUNC9RC          TEMP STORAGE FOR THE RETURN CODE
         L     R10,SSOBADDR
         USING SSOB,R10
         ST    R8,SSOBRETN
         DROP  R10
         FREEMAIN R,LV=4096,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
         L     R14,12(R13)         RELOAD RETURN POINT
         XR    R15,R15             COPY RC INTO REG 15
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
         LTORG
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
WTO1C    WTO   'TSS9022I                                               X
                                                        ',MF=L
WTO1CE   DS    0H
*
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
FUN9DATA DSECT
WORKAREA DS    CL200            USED FOR CONVERT ROUTINES
SSOBADDR DS    F                ADDRESS OF THE SSOB
WQEADDR  DS    A                ADDRESS OF THE WQE WE ARE TRYING
*                               TO DEAL WITH
CURTABAD DS    A                CURRENT TSSO/AOF TABLE ADDRESS WE ARE
*                               DEALING WITH
TXTMSG   DS    CL8              FIRST 8 BYTES OF WTO/WTOR
REPLY    DC    H'109',H'0'
         DC    CL6'REPLY '
REPLYNUM DC    CL2' '
REPLYCOM DC    CL1','
REPLYMSG DC    CL100' '
MVSCMD   DC    H'109',H'0'
MVSCMD1  DS    CL105
FUNC9RC  DS    H               RETURN CODE FOR FUNCTION 9.
WTO1D    WTO   'TSS9022I                                               X
                                                 ',MF=L
************************************************************
* VARIABLES USED TO PROCESS TEST REQUESTS IN AOF TABLE.
************************************************************
WORD1    DS    F                IF TYPE=SUBSTR  STARTING CHARACTER
*                               IF TYPE=WORD    WORD NUMBER
WORD2    DS    F                LENGTH OF WORD
WORDSEPS DS    CL4              SEPARATOR CHARACTORS FOR WORDS.
STRSTART DS    A                ADDRESS OF FIRST CHARACTER OF WQE
*                               MESSAGE. THIS IS WQETXT+1 IF WTO
*                               AND WQETXT+4 IF WTOR.
************************************************************
* VARIABLES USED TO PROCESS P DIRECTIVE IN OSCMD REQUEST
************************************************************
SIGSTART DS    F                STARTING ADDRESS OF SIGNIFICANCE
SIGCOUNT DS    F                NUMBER OF CHARACTERS
************************************************************
* PARSE PARAMETERS
************************************************************
PARSPARM DS    0H
PARS1    DS    A
PARS2    DS    A
PARS3    DS    A
PARS4    DS    A
PARARRAY DS    20CL16
************************************************************
* VARIABLES USED TO PROCESS W DIRECTIVE IN OSCMD REQUEST
************************************************************
SEPCHAR  DS    F           SEPARATOR CHARACTERS FOR PARSE
WORDREQ  DS    F
LWORDREQ DS    F
TOKADDR  DS    F           ADDRESS OF REQUESTED TOKEN IN
*                          PARARRAY
************************************************************
* FLAGS USED IN PROCESSING REQUESTS FROM SUBSYSTEM CALL 9
************************************************************
FUN9FLG  DS    X
* X'80'  SSWTMIN ^= 0
* X'40'  SSWTORE ^= 0
* X'20'  OSCMDT WAS SPECIFIED
         SPACE
         IEFJESCT
         CVT   DSECT=YES
         IEFJSSVT
         TSSOCVT
         TSSOCMND
         SPACE 3
         IEFJSCVT
         EJECT
         IEFJSSOB (CM,WT),CONTIG=NO
         IHAWQE
         IHAORE
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
************************************************************
* NOTE: PREVIOUS VERSION OF TSSO HAVE BEEN WRITTEN BY:
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC., MCLEAN VA
*        DATE WRITTEN. SEPTEMBER 2 1976.
*        DATE UPDATED. AUGUST 7 1980.
************************************************************
         END
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOSSSM(''V 4.1.0 &SYSDATE &SYSTIME'') '
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' INCLUDE  SYSLMOD(TSSOPARS)'
 PUNCH ' ENTRY    TSSOSSSM'
 PUNCH ' NAME     TSSOSSSM(R)'
         END
