PROC 0 ADD
/*                                                                */
/*                                                                */
/*                                                                */
/* THE PURPOSE OF THIS CLIST IS TO NOTIFY YOU OF CALENDER         */
/* APPOINTMENTS. YOU CAN ADD TO YOUR CALENDAR FOR AN APPOINTMENT  */
/* LATER TODAY OR FUTURE DATES.                                   */
/*                                                                */
/*                                                                */
/*  REQUIREMENTS:  YOUR MUST HAVE A AN 80 BYTE DATA SET CALLED:   */
/*                 XXXX.DATE.DATA    (PS FORMAT)                  */
/*                  IN YOUR CLIST DATA SET YOU NEED THE FOLLOWING */
/*                  XXXX.CLIST(DAYTIMER)                          */
/*                  XXXX.CLIST(SIGON) WHICH CONTAINS THE FOLLOWING*/
/*            COLUMN 1                                            */
/*                   WRITE  **  EXECUTE SIGNON                    */
/*                   DAYTIMER                                     */
/*                                                                */
/*            THIS WILL DISPLAY YOUR CALENDAR ACTIVITIES FOR      */
/*            TODAY IF THE DATE YOUR LOGON DATE EQUALS TODAY.     */
/*            ITEMS YOU HAVE ENTERED FOR FUTURE REMINDERS WILL    */
/*            ONLY DISPLAY WHEN YOUR LOGON DATE EQUAL THE DATE    */
/*            YOU TOLD IT TO REMIND YOU 'OR' IF IT IS A TOMORROW  */
/*            REMINDER IT WILL ALSO DISPLAY.  ITEMS WILL AUTO-    */
/*            MATICALLY DELETE WHEN APPOINTMENT DATE IS LESS THAN */
/*            LOGGED ON DATE. IF NO ITEMS RESIDE IN YOUR DATA     */
/*            SET IT WILL DISPLAY 'CLEAR' WHEN YOU LOG ON.        */
/*                                                                */
/*            ENTER 'DAYTIMER ADD' ON YOUR TSO SCREEN TO ADD      */
/*            A REMINDER.  AFTER ENTERING MM/DD/YR AND MESSAGE    */
/*            THIS CLIST WILL ASK YOU IF YOU HAVE MORE REMINDER   */
/*            MESSAGES TO ENTER WITH EITHER A 'N' FOR NO OR 'Y'   */
/*            FOR YES.                                            */
/*                                                                */
/*            IF YOU WANT TO REVIEW CURRENT AND TOMORROWS         */
/*            REMINDERS JUST KEY IN TSO MODE 'DAYTIMER',          */
/*            IT WILL DISPLAY DATES AND MESSAGES.                 */
/*                                                                */
/* KEYWORD 'ADD' ALLOWS YOU TO ADD ADDITIONAL INFORMATION         */
/*                                                                */
/* )STARTHELP                                                     */
/*                                                                */
/* )F FUNCTION -'DATE' IS AN ONLINE APPOINTMENT MEMBER            */
/*                                                                */
/* )X SYNTAX -   'DATE'                                           */
/*                                                                */
/* )O OPERANDS - 'ADD' COMMAND WILL THEN PROMPT YOU FOR DATE      */
/*              MM/DD/YY AND THEN FOR A ONE-LINE TEXT.            */
/*                                                                */
/* )ENDHELP                                                       */
/*                                                                */
CONTROL NOMSG NOCONLIST
FREE F(DATE)
ERROR EXIT
ALLOC F(DATE) DA(DATE.DATA) SHR
OPENFILE DATE
SET SDATE EQ &SUBSTR(7:8,&SYSDATE)&SUBSTR(1:2,&SYSDATE)-
&SUBSTR(4:5,&SYSDATE)
ERROR +
DO
  IF &ACTIVITY EQ N THEN WRITE  CALENDAR CLEARED...
  CLOSFILE DATE
  OPENFILE DATE OUTPUT
  SET &M EQ 1
  DO WHILE &M NE &N
    SET XXXX EQ &&D&M
    SET &DATE EQ &STR(&XXXX)
    PUTFILE DATE
    SET &M EQ &M+1
  END
  IF &ADD NE &STR() THEN +
  DO
    MORE: WRITENR ENTER DATE IN FORM MM/DD/YY:
    READ &D
    WRITENR ENTER TEXT:
    READ
    SET &DATE EQ &STR(&D &SYSDVAL)
    PUTFILE DATE
  WRITENR ARE THERE ANY ADDITIONAL ENTRIES TO BE MADE? Y/N
  READ &YN
  IF &YN EQ Y THEN GOTO MORE
  END
  CLOSFILE DATE
  EXIT
END
SET &N EQ 1
SET &ACTIVITY EQ N
WRITE
GET: GETFILE DATE
SET DDATE EQ &SUBSTR(7:8,&DATE)&SUBSTR(1:2,&DATE)&SUBSTR(4:5,&DATE)
IF &DDATE LT &SDATE THEN +
DO
  WRITE DELETED: &DATE
  GOTO GET
END
IF &SDATE EQ &DDATE THEN +
DO
  WRITE   TODAY:  &DATE
  SET &ACTIVITY EQ Y
END
IF &SDATE+1 EQ &DDATE THEN +
DO
  WRITE TOMORROWS REMINDER: &DATE
END
SET D&N EQ &DATE
SET &N  EQ &N+1
GOTO GET
