PSUTEOF  CSECT
*
*  THIS IS AN PRODUCTION SERVICES UTILITY PROGRAM
*
**  FOR INFORMATION CALL: GEORGE RAMAS
**                        OAKLAND, CALIFORNIA 94610
**                        (415)-763-0540
**          STARTING 1990 (510)
**
*  THIS VERSION OF PSUTEOF DEMANDS THAT THE DD NAME 'FILE' BE AT
*  LEAST IN THE FIRST FOUR BYTES, ELSE IT WILL RETURN A CONDITION CODE
*  OF U0001.  YOU CAN HAVE FILEXX THRU FILEXX (AS MANY AS YOU WANT)
*
*  //FILE01 DD DSN=LOSP.TRAN10125.SORTED(+1),  **ALLOCATES WITH EOF**
*              DISP=(,CATLG,DELETE),
*              UNIT=SYSDA,
*              SPACE=(CYL,(25,15)), **NO RLSE PARAMETER HERE**
*              DCB=(MDLDSCB,RECFM=FBA,LRECL=133,BLKSIZE=9310,DSORG=PS)
*
*  NOTE:  FOR ABOVE ALLOCATION REPEAT THE SPACE PARAMETER IN THE
*         STEP THAT THE DATA SET IS BEING WRITTEN TO WITH THE
*         'RLSE' PARAMETER.
*
*  //FILEAA DD DSN=LOSP.PAYM2510.DEPOSTS(0),
*              DISP=OLD     ***WRITE EOF TO DSN***
*
*
*
* GENERAL PURPOSE REGISTERS
          SPACE
R0        EQU 0
R1        EQU 1
R2        EQU 2
R3        EQU 3
R4        EQU 4
R5        EQU 5
R6        EQU 6
R7        EQU 7
R8        EQU 8
R9        EQU 9
R10       EQU 10
R11       EQU 11
R12       EQU 12
R13       EQU 13
R14       EQU 14
R15       EQU 15
          SPACE
* FLOATING POINT REGISTERS
          SPACE
F0        EQU 0
F2        EQU 2
F4        EQU 4
F6        EQU 6
          SPACE
* EXTENDED CONTROL REGISTERS
          SPACE
C0        EQU 0
C1        EQU 1
C2        EQU 2
C3        EQU 3
C4        EQU 4
C5        EQU 5
C6        EQU 6
C7        EQU 7
C8        EQU 8
C9        EQU 9
C10       EQU 10
C11       EQU 11
C12       EQU 12
C13       EQU 13
C14       EQU 14
C15       EQU 15
         SAVE  (14,12),,*
         LR    R11,R15
         USING PSUTEOF,R11
         ST    R13,SAVEAREA+4
         LA    R3,SAVEAREA
         ST    R3,8(R13)
         LR    R13,R3
         SLR   R6,R6
*   GET TIOT FOR THIS JOB
         EXTRACT ADRTIOT,FIELDS=TIOT
         L     R7,ADRTIOT          POINT 7 TO TIOT START
         LA    R7,24(R7)           GET TO FIRST DD ENTRY IN TIOT
DOIT     CLC   4(4,R7),=CL4'FILE'  SEE IF WE HAVE A VALID DD
         BNE   NEXT                IF NOT GO TO NEXT
         MVC   FILEA+40(8),4(R7)   MOVE DDNAME TO DCB FOR WRITE
         OPEN  (FILEA,OUTPUT)      WRITE EOF
         CLOSE FILEA                   ON INDICATED DD
NEXT     IC    R6,0(R7)            LENGTH OF ENTRY
         AR    R7,R6               BUMP TO NEXT DD ENTRY IN TIOT
         CLC   0(4,R7),=F'0'       END OF TIOT TEST
         BNE   DOIT                GO DO NEXT DD
EOJ      EQU   *
         SR    15,15
         L     R13,SAVEAREA+4
         RETURN (14,12),RC=(15)
FILEA    DCB   DSORG=PS,BUFL=32760,MACRF=(PM),DDNAME=XXXXXXXX
SAVEAREA DS    18F
ADRTIOT  DC    F'0'                ADDRESS OF TIOT FROM EXTRACT MACRO
         EJECT
         END
