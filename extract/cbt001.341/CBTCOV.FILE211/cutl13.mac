         PROC 0 TEST(NO)
         /*--------------------------------*/
         /* SPF 9.13 - VM TRANSMIT UTILITY */
         /*--------------------------------*/
         SET WHAT = &STR(VM TRANSMIT UTILITY)
         SET MSG  = &STR()
         ISPEXEC VGET (ZUSER)
         SET XUSER = &ZUSER
         SET VMVOL = &STR()
         ISPEXEC VGET (VMUSER,VACCT,VMPLIB,VMTLIB,VMTLII,VMDSN, +
                       VMVOL,CMSFNAME,CMSFTYPE) PROFILE
         CONTROL NOMSG NOFLUSH
         IF &TEST = YES THEN +
         CONTROL LIST SYMLIST PROMPT MSG
L000:    ERROR OFF
         SET VOLSW  = &STR(NO)
         SET ZCMD   = &STR()
         ISPEXEC DISPLAY PANEL(VUTL13) MSG(&MSG)
         IF &LASTCC >= 8 THEN EXIT CODE(0)
         ISPEXEC VPUT (VMUSER,VACCT,CMSFNAME,CMSFTYPE) PROFILE
         SET MSG = &STR()
         SET VMOPT = &ZCMD
         SET ZCMD = &STR()
         IF &VMOPT = 1 THEN DO
           ISPEXEC DISPLAY PANEL(VUTL131) MSG(&MSG)
           IF &LASTCC >= 8 THEN GOTO L000
           ISPEXEC VPUT (VMDSN,VMVOL) PROFILE
           IF &VMVOL ^= &STR() THEN SET VOLSW = YES
           IF &SUBSTR(1:1,&VMDSN) = &STR(') +
           THEN SET VDSN  = &SUBSTR(2:&LENGTH(&VMDSN)-1,&VMDSN)
           ELSE SET VDSN  = &STR(&ZUSER..&VMDSN)
           END
         ELSE IF &VMOPT = 2 THEN DO
           ISPEXEC DISPLAY PANEL(VUTL132) MSG(&MSG)
           IF &LASTCC >= 8 THEN GOTO L000
           ISPEXEC VPUT (VMPLIB) PROFILE
           IF &SUBSTR(1:1,&VMPLIB) = &STR(') +
           THEN SET VPLIB = &SUBSTR(2:&LENGTH(&VMPLIB)-1,&VMPLIB)
           ELSE SET VPLIB = &STR(&ZUSER..&VMPLIB)
           END
         ELSE IF &VMOPT = 3 THEN DO
           ISPEXEC DISPLAY PANEL(VUTL133) MSG(&MSG)
           IF &LASTCC >= 8 THEN GOTO L000
           ISPEXEC VPUT (VMTLIB,VMTLII) PROFILE
           IF &SUBSTR(1:1,&VMTLIB) = &STR(') +
           THEN SET VTLIB = &SUBSTR(2:&LENGTH(&VMTLIB)-1,&VMTLIB)
           ELSE SET VTLIB = &STR(&ZUSER..&VMTLIB)
           IF &SUBSTR(1:1,&VMTLII) = &STR(') +
           THEN SET VTLII = &SUBSTR(2:&LENGTH(&VMTLII)-1,&VMTLII)
           ELSE SET VTLII = &STR(&ZUSER..&VMTLII)
           END
         ELSE IF &VMOPT = 4 THEN DO
           ISPEXEC DISPLAY PANEL(VUTL134) MSG(&MSG)
           IF &LASTCC >= 8 THEN GOTO L000
           SET TIME = &STR(AT &SYSTIME ON &SYSDATE)
           END
         ISPEXEC CONTROL DISPLAY REFRESH
         ISPEXEC FTOPEN TEMP
         ISPEXEC FTINCL VUTL13
         ISPEXEC FTCLOSE
         IF &LASTCC ^= 0 THEN SET &MSG = TECA001
         ELSE DO
           ISPEXEC VGET (ZTEMPF)
           CONTROL PROMPT MSG
           SUB '&ZTEMPF'
           CONTROL NOPROMPT NOMSG
           FREE DSN('&ZTEMPF')
           SET &MSG = TECA002
           END
         GOTO L000
