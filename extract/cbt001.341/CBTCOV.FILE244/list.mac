LIST     TITLE 'QUEUE COMMAND - PRINT A DATASET FROM SPOOL BY ID' ONL01
LIST     QSTART ,                  START LIST CSECT ASSEMBLY      ONL01
         GBLB  &QJTIP                                             ONL05
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
***********************************************************************
*                                                                     *
*   IF THIRD PARM IS OMITTED ASSUME CURRENT JOB                   WGH *
*                                                                     *
***********************************************************************
         LH    R2,QLNG2       LENGTH OF DSID FIELD                WGH
         SH    R2,=H'1'       IS LENGTH ZERO                      WGH
         BNM   DSIDOK         NO.. CONTINUE                       WGH
         MVC   QPARM2,QPARM1  YES.. ASSUME CURRENT JOB            WGH
         MVC   QLNG2,QLNG1    MOVE DSID TO THIRD FIELD            WGH
         MVI   QPARM1,C'='    SET CURRENT JOB                     WGH
         LA    R2,1           SET LENGTH TO 1                     WGH
         STH   R2,QLNG1       SET LENGTH                          WGH
DSIDOK   EQU   *                                                  WGH
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE DATASET ID NUMBER                           *
*                                                                     *
***********************************************************************
         LH    R2,QLNG2       LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'       IS THE DATASET ID FIELD ZERO LENGTH?
*  WGH   BM    TILT           YES. QUIT.
         BNM   LNGOK                                              WGH
         LA    R2,1           SET LENGTH TO 1                     WGH
         STH   R2,QLNG2       STORE IT                            WGH
         BCTR  R2,0           REDUCE BY 1                         WGH
         MVI   QPARM2,C'+'    SET RELATIVE DSIS                   WGH
LNGOK    EQU   *                                                  WGH
         LH    R4,QOLDDSID    GET PREVIOUS DSID VALUE             ONL04
         LA    R3,DSIDPLUS    POINT TO ADD INSTRUCTION            UF040
         CLI   QPARM2,C'+'    RELATIVE DSID NUMBER?               UF040
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDSUB     POINT TO SUBTRACT INSTRUCTION       UF040
         CLI   QPARM2,C'-'    RELATIVE DSID NUMBER?               UF020
         BE    DSIDREL        YES, GO PROCESS IT                  UF040
         LA    R3,DSIDABS     POINT TO NOP INSTRUCTION            UF040
         B     DSIDTEST       NO, CONTINUE                        UF040
DSIDREL  CLI   QPARM1,C'*'    WANT THE CURRENT JOB?               UF040
         BNE   TILTREL        NO, RELATIVE REQUEST INVALID        UF040
         MVI   QPARM2,C'0'    REPLACE WITH NUMERIC VALUE          UF040
         LTR   R2,R2          WAS FIELD ONLY THE +/-?             UF040
         BP    DSIDTEST       NO, CONTINUE AS BEFORE              UF040
         LA    R2,1           YES, SET VALUE OF 1                 UF040
         B     DSIDCHK        AND SKIP NUMERIC CHECKS             UF040
DSIDTEST DS    0H                                                 UF040
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SPACE 1                                                  UF005
DSIDCHK  DS    0H                                                 UF040
         EX    0,0(R3)        ADJUST VALUE IN R4 BY OFFSET        UF040
         LR    R2,R4          PUT BACK IN R2                      UF040
*        LTR   R2,R2          MAKE SURE IS POSITIVE                 SBG
*        BNP   TILT           ERROR IF NOT                          SBG
         STH   R2,QPDSID      STORE DATASET ID
         STH   R2,QOLDDSID    SET PREVIOUS DSID TO CURRENT        ONL04
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB AND DSID                  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QALIST            PERFORM JOB/DSID AUTHORIZATION ONL02
         SPACE 2                                                  ONL02
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE PRINT OFFSET                                *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' DEFAULT TO ZERO
         LH    R2,QLNG3       LENGTH OF OFFSET FIELD
         SH    R2,=H'1'       IS THE OFFSET FIELD ZERO LENGTH?
         BM    CALLLIST       YES. USE ZERO OFFSET.
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ2        MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK2       PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         STH   R2,QPOFFSET    STORE OFFSET
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
TILT     QTILT '*** DATASET ID INVALID ***'
TILTREL  QTILT '*** RELATIVE DATASET ID ONLY VALID IF JOBID IS "*" ***'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
DSIDPLUS AR    R4,R2          ADD TO PRESENT VALUE                UF040
DSIDSUB  SR    R4,R2          SUBTRACT FROM PRESENT VALUE         UF040
DSIDABS  LR    R4,R2          SET TO PRESENT VALUE                UF040
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
MVZ2     MVZ   QFZONES(1),QPARM3 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
PACK2    PACK  CONVERT,QPARM3(1) CONVERT TO BINARY
         LTORG
         DROP  ,                   DROP ALL ADDRESSINGS           NERDC
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
LIST     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $BUFFER                                                  UF020
         IFGRPL
         $JCT
         $HASPEQU
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
WORK     DSECT
         DS    72C
CONVERT  DS    D
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
