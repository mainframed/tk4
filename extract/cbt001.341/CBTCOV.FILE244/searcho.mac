SEARCH   TITLE 'QUEUE COMMAND - JQE AND JOE SEARCH AND FORMAT'    ONL01
SEARCH   QSTART ,                  START SEARCH CSECT ASSEMBLY    ONL01
*******************************************************************
* RNB CHANGES:                                                    *
*      (1) RNB16 - PROCESS BOTH LOCAL AND REMOTE JOE QUEUE.       *
*                  ALSO, FIX BUG IN UF020 THAT WAS CLEARING       *
*                  FIELD JOEFLAG1 IN THE WORK JOE.                *
*      (2) RNB19 - FOR SP, SEARCH DUMP Q AND CONVERSION (XEQ) Q IF*
*                  STATUS COMMAND OR DJ. ALSO OUTPUT QUEUE        *
*******************************************************************
* WGH CHANGES:                                                    *
*                                                                 *
*          MODIFDIED TO INCLUDE NJE AND SPOOL OFFLOAD QUEUES      *
*                                                                 *
*                                                                 *
*                                                                 *
*                                                                 *
*******************************************************************
         L     R10,QVCKPT     LOAD BASE REG
         USING JQE,R9         BASE REG FOR JQE DSECT
         USING JOE,R8         BASE REG FOR JOE DSECT
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA
******************************************************************UF006
*                                                                 UF006
*   CALL - READ JES2 CHECKPOINT FILE                              UF006
*                                                                 UF006
******************************************************************UF006
         L     R15,=V(CKPT)   ADDR OF CKPT ROUTINE                UF006
         BALR  R14,R15        GO TO IT                            UF006
***********************************************************************
*                                                                     *
*   BRANCH TO PROPER ROUTINE                                          *
*                                                                     *
***********************************************************************
         LH    R1,QCODEH      LOAD FUNCTION CODE INTO R1
         CH    R1,=H'60'      IS THE FUNCTION SUPPORTED?
         BH    STOP           NO. RETURN.
         B     *+4(R1)        BRANCH TO ROUTINE
         B     ST             0 OFFSET
         B     DA             4
         B     DI             8
         B     AO   DO        12
         B     AI             16
         B     AO             20
         B     HI             24
         B     HO             28
         B     DT             32
         B     DJ             36
         B     DS             40
         B     DQ             44
         B     DF             48
         B     WO             52   WTRID OUTPUT                   UF047
         B     PREFIX         56   CHANGE PREFIX                  WGH
         B     HQ             60   DISPLAY $OUTPUT Q              WGH
***********************************************************************
*                                                                     *
*   PREFIX - AUTHORIZED USER CHANGED  LEVEL                           *
*                                                                     *
***********************************************************************
PREFIX   MVC   QLOGON,QPARM1  MOVE NEW LEVEL INTO QLOGON          WGH
         MVC   QLOGONLN+2(2),QLNG1     MOVE IN LENGTH             WGH
         B     ST             ENTER STAT PROCESSING               WGH
***********************************************************************
*                                                                     *
*   STATUS - FIND ALL JOBS THAT MATCH LEVEL                           *
*                                                                     *
***********************************************************************
ST       CLI   QPARM1,C' '    DID USER SPECIFY LEVEL?
         BNE   ST2            NO. USE QLOGON.
         MVC   QPARM1,QLOGON  MOVE USER PARM1 TO LEVEL
ST2      BAL   R2,PARMLEN     DETERMINE PARM LENGTH
* SEARCH THE TSO QUEUE
         USING JQTDSECT,R1    BASE REG FOR JQT
DJ2      LH    R6,QLNG1       LENGTH OF COMPARE FOR LEVEL
         L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTTSU      ADDR OF TSO QUEUE
         MVI   QCLASS,0       INDICATE THIS IS THE TSO QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     DJ3            END OF QUEUE
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    PRTJQE         YES, LIST IT                        WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   SKIPJQE        NO. SKIP THIS RECORD.
         B     PRTJQE         PRINT THE RECORD
* SEARCH THE SYSTEM QUEUE
DJ3      L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTSTC      ADDR OF STC QUEUE
         MVI   QCLASS,4       INDICATE THIS IS THE STC QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     DJ4A           END OF QUEUE                        CL005
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    PRTJQE         YES, LIST IT                        WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   SKIPJQE        NO. SKIP THIS RECORD.
         B     PRTJQE         PRINT THE RECORD
         SPACE 1                                                  CL005
* SEARCH FOR SETUP QUEUE ELEMENT                                  CL005
DJ4A     L     R1,QCJQHEAD         LOAD ADDR OF JQT               CL005
         LA    R4,JQTSETUP         ADDR OF $SETUP QUEUE           CL005
         MVI   QCLASS,24           INDICATE THIS IS SETUP QUEUE   CL005
         BAL   R2,SRCHJQE          SEARCH THE QUEUE               CL005
         B     DJ4                 + 0 END OF QUEUE               CL005
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    PRTJQE         YES, LIST IT                        WGH
         EX    R6,STCLC            + 4 JOBNAME EQUAL TO LEVEL?    CL005
         BNE   SKIPJQE             NO, SKIP THIS RECORD           CL005
         B     PRTJQE              PRINT THE ELEMENT              CL005
         SPACE 1                                                  CL005
* SEARCH FOR HELD OUTPUT
DJ4      L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTOUT      ADDR OF $OUTPUT QUEUE
         DROP  R1
         MVI   QCLASS,8       INDICATE THIS IS THE HELD OUT QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     DJ6            END OF QUEUE                           CL
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    DJ4X           YES, LIST IT                        WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   SKIPJQE        NO. SKIP THIS RECORD.
DJ4X     EQU   *                                                  WGH42
         TM    JQEFLAG8,JQE8HLDS    ANY HELD 3540 DATASETS?       WGH42
         BNZ   PRTJQE         YES. PRINT THE RECORD.
         TM    JQEFLAG3,JQE3SYSD   PRINT JQE IF                      CL
         BO    PRTJQE               SPECIAL                          CL
         B     REJJQE         REJECT THE RECORD
* SEARCH CONVERSION (XEQ) QUEUE                                   RNB19
DJ6      L     R1,QCJQHEAD    LOAD ADDR OF JQT                    RNB19
         USING JQTDSECT,R1    BASE REG FOR JQT                    RNB19
         LA    R4,JQTXEQ      ADDR OF CONVERSION QUEUE            RNB19
         DROP  R1                                                 RNB19
         MVI   QCLASS,16      INDICATE THIS IS THE CNV QUEUE      RNB19
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    RNB19
         B     DJ7            END OF QUEUE                        RNB19
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    PRTJQE         YES, LIST IT                        WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      RNB19
         BNE   SKIPJQE        NO. SKIP THIS RECORD.               RNB19
         B     PRTJQE         PRINT THE RECORD                    RNB19
* SEARCH OUTPUT (AWAITING OUTPUT) QUEUE                           RNB19
DJ7      L     R1,QCJQHEAD    LOAD ADDR OF JQT                    RNB19
         USING JQTDSECT,R1    BASE REG FOR JQT                    RNB19
         LA    R4,JQTAWOUT    ADDR OF OUTPUT QUEUE                RNB19
         DROP  R1                                                 RNB19
         MVI   QCLASS,20      INDICATE THIS IS THE AWO QUEUE      RNB19
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    RNB19
         B     DJ8            END OF QUEUE                  WGH   RNB19
         CLI   QPARM1,C'*'    REQUEST FOR ALL                     WGH
         BE    PRTJQE         YES, LIST IT                        WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      RNB19
         BNE   SKIPJQE        NO. SKIP THIS RECORD.               RNB19
         B     PRTJQE         PRINT THE RECORD                    RNB19
* SEARCH RECEIVE QUEUE     NJE                                    WGH
DJ8      L     R1,QCJQHEAD    LOAD ADDR OF JQT                    WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                    WGH
         LA    R4,JQTRCV      ADDR OF RECEIVE QUEUE               WGH
         DROP  R1                                                 WGH
         MVI   QCLASS,28      INDICATE THIS IS THE RECEIVE        WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    WGH
         B     DJ10           END OF QUEUE                        WGH
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRTJQE         YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      WGH
         BNE   SKIPJQE        NO. SKIP THIS RECORD.               WGH
         B     PRTJQE         PRINT THE RECORD                    WGH
* SEARCH XMIT    QUEUE                                            WGH
DJ10     L     R1,QCJQHEAD    LOAD ADDR OF JQT                    WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                    WGH
         LA    R4,JQTXMIT     ADDR OF XMIT    QUEUE               WGH
         DROP  R1                                                 WGH
         MVI   QCLASS,32      INDICATE THIS IS THE XMIT  QUEUE    WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    WGH
         B     DJ11           END OF QUEUE                        WGH
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRTJQE         YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      WGH
         BNE   SKIPJQE        NO. SKIP THIS RECORD.               WGH
         B     PRTJQE         PRINT THE RECORD                    WGH
* SEARCH $INPUT  QUEUE                                            WGH
DJ11     L     R1,QCJQHEAD    LOAD ADDR OF JQT                    WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                    WGH
         LA    R4,JQTINPUT    ADDR OF INPUT   QUEUE               WGH
         DROP  R1                                                 WGH
         MVI   QCLASS,36      INDICATE THIS IS THE INPUT QUEUE    WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                    WGH
         B     ST3            END OF QUEUE                        WGH
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRTJQE         YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?      WGH
         BNE   SKIPJQE        NO. SKIP THIS RECORD.               WGH
         B     PRTJQE         PRINT THE RECORD                    WGH
* SEARCH INPUT QUEUES
ST3      MVI   QCLASS,192     START WITH CLASS A
ST4      BAL   R2,NEXTJQT     FIND NEXT QUEUE
         B     ST5            END OF QUEUES                       WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     ST4            END OF QUEUE
         CLI   JQEFLAG1,0     IS THE JOB EXECUTING OR HELD?
         BNE   ST7            YES. SPECIAL HANDLING.
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRTJQE         YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   SKIPJQE        NO. SKIP THIS RECORD.
         B     PRTJQE         PRINT THE JQE
ST7      EQU   *                                                  WGH
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRT2JQE        YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   REJJQE         NO. SKIP THIS RECORD.
         B     PRT2JQE        PRINT THE RECORD WITHOUT INCR COUNT
* SEARCH OUTPUT QUEUES
ST5      MVI   QCLASS,192     START WITH CLASS A
ST6      BAL   R2,NEXTJOT     DETERMINE NEXT QUEUE
         B     STOP           NO MORE QUEUES                      WGH
         BAL   R2,SRCHJOE     SEARCH A JOE QUEUE
         B     ST6            END OF QUEUE
         CLI   QPARM1,C'*'    REQUEST FOR ALL?                    WGH
         BE    PRTJOE         YES,  LIST IT                       WGH
         EX    R6,STCLC       IS THE JOBNAME EQUAL TO LEVEL?
         BNE   ST6WTR         NO. CHECK WTRID MATCH               WGH
         B     PRTJOE         PRINT THE RECORD
* SEARCH OUTPUT QUEUES FOR WRITID MATCH                           WGH
ST6WTR   SR    R1,R1          CLEAR REG FOR INSERT                WGH
         ICM   R1,B'0111',JOECHARB  OFFSET TO CHAR JOE            WGH
         BZ    SKIPJOE        NONE, SKIP THIS ONE                 WGH
         A     R1,QCJOTA      GET REAL JOE ADDRESS                WGH
         CLI   QPARM1,C' '    IS PREFIX BLANK?                    WGH
         BE    SKIPJOE        YES,  SKIP IT                       WGH
         EX    R6,STCLCW      IS THE WRTID   EQUAL TO LEVEL?      WGH
         BNE   SKIPJOE        NO, TRY NEXT ONE                    WGH
         B     PRTJOE         PRINT THE RECORD                    WGH
STCLCW   CLC   QPARM1(1),JOEWTRID-JOE(R1)                         WGH
***********************************************************************
* COMPARE USED TO CHECK LEVEL
STCLC    CLC   QPARM1(1),JQEJNAME IS THE JOBNAME EQUAL TO LEVEL?
***********************************************************************
*                                                                     *
*   DQ - PRINT SUMMARY OF ALL JOBS IN THE INPUT QUEUES                *
*                                                                     *
***********************************************************************
*  SEARCH ALL QUEUES
DQ       MVI   QCLASS,192           START WITH CLASS A
         MVC   QDHLINE,DQHEADER     MOVE IN THE HEADING
DQ1      BAL   R2,NEXTJQT           FIND NEXT QUEUE
         B     STOP                 END OF QUEUES                 WGH
         ZAP   QCOUNTE,=P'0'        ZERO COUNT FOR EXECUTING JOBS
         ZAP   QCOUNTA,=P'0'        ZERO COUNT FOR AWAITING JOBS
         ZAP   QCOUNTH,=P'0'        ZERO COUNT FOR HELD JOBS
         BAL   R2,SRCHJQE           SEARCH THE QUEUE
         B     DQ4                  END OF QUEUE
         TM    JQEFLAG1,JQE1BUSY    IS THIS JOB EXECUTING
         BZ    DQ2                  NO, NEXT TEST
         AP    QCOUNTE,=P'1'        YES, BUMP COUNTER
         B     SKIPJQE              PROCESS NEXT JQE
DQ2      TM    JQEFLAG1,JQE1HLDA+JQE1HLD1+JQE1HLD2 HELD JOB?
         BZ    DQ3                  NO, NEXT TEST
         AP    QCOUNTH,=P'1'        YES, BUMP COUNTER
         B     SKIPJQE              PROCESS NEXT JQE
DQ3      CLI   JQEFLAG1,0           IS THIS JOB AWAITING EXECUTION
         BNE   SKIPJQE              NO, PROCESS NEXT JQE
         AP    QCOUNTA,=P'1'        YES, BUMP COUNTER
         B     SKIPJQE              PROCESS NEXT JQE
DQ4      CLC   QCOUNT,=PL3'0'       IS THIS QUEUE EMPTY
         BE    DQ1                  YES, TRY THE NEXT QUEUE
         MVC   QDMSG,DQLINE         MOVE IN DETAIL LINE
         MVC   FCOUNT,ED5           MOVE IN
         ED    FCOUNT,QCOUNTE            NUMBER OF JOBS
         MVC   QECOUNT,FCOUNT+3                    IN EXECUTION
         MVC   FCOUNT,ED5           MOVE IN
         ED    FCOUNT,QCOUNTA            NUMBER OF JOBS
         MVC   QACOUNT,FCOUNT+3                    AWAITING EXECUTION
         MVC   FCOUNT,ED5           MOVE IN
         ED    FCOUNT,QCOUNTH            NUMBER OF JOBS
         MVC   QHCOUNT,FCOUNT+3                    IN HELD STATUS
         MVC   FCOUNT,ED5           MOVE IN THE TOTAL NUMBER OF JOBS
         ED    FCOUNT,QCOUNT             IN THIS QUEUE
         MVC   CLASS,QCLASS         MOVE IN QUEUE CLASS
         BAL   R2,DISPLAY           GO DISPLAY THIS LINE
         B     DQ1                  PROCESS THE NEXT QUEUE
***********************************************************************
*                                                                     *
*   DF - PRINT SUMMARY OF ALL JOBS IN THE OUTPUT QUEUES               *
*                                                                     *
***********************************************************************
DF       MVI   QCLASS,192           START WITH CLASS A
         MVC   QDHLINE,DFHEADER     MOVE IN THE HEADING
DF1      BAL   R2,NEXTJOT           FIND NEXT QUEUE
         B     STOP                 NO MORE QUEUES                  WGH
         ZAP   QCOUNTE,=P'0'        ZERO COUNT FOR JOBS PRINTING
         BAL   R2,SRCHJOE           SEARCH THE QUEUE
         B     DF2                  END OF QUEUE
         TM    JOEFLAG1,X'20'       IS JOB PRINTING
         BNO   SKIPJOE              NO, PROCESS NEXT JOE
         AP    QCOUNTE,=P'1'        YES, BUMP COUNTER
         B     SKIPJOE              PROCESS NEXT JOE
DF2      CLC   QCOUNT,=PL3'0'       QUEUE EMPTY
         BE    DF1                  YES, TRY THE NEXT QUEUE
         MVC   QDMSG,DFLINE         MOVE IN DETAIL LINE
         MVC   FCOUNT,ED5           MOVE IN
         ED    FCOUNT,QCOUNTE            NUMBER OF JOBS
         MVC   QECOUNT,FCOUNT+3                    PRINTING
         MVC   FCOUNT,ED5           MOVE IN TOTAL NUMBER OF JOBS
         ED    FCOUNT,QCOUNT             IN THIS QUEUE
         MVC   CLASS,QCLASS         MOVE IN QUEUE CLASS
         BAL   R2,DISPLAY           GO DISPLAY THIS LINE
         B     DF1                  PROCESS NEXT QUEUE
***********************************************************************
*                                                                     *
*   DJ - FIND A SPECIFIC JOB                                          *
*                                                                     *
***********************************************************************
DJ       CLI   QPARM1,C' '    DID USER SPECIFY JOBNAME?
         BE    DJ9            NO. TILT.
         MVC   QLNG1,=H'7'    COMPARE FOR 8 CHARACTERS
         B     DJ2            USE THE STATUS ROUTINES
DJ9      QTILT '*** YOU MUST SPECIFY JOBNAME ***'
***********************************************************************
*                                                                     *
*   DI - PRINT ALL JOBS IN INPUT QUEUES                               *
*                                                                     *
***********************************************************************
DI       CLI   QPARM1,C' '    DID USER SPECIFY CLASS?
         BNE   DI5            YES. LIMIT TO ONE QUEUE.
* SEARCH ALL QUEUES
         MVI   QCLASS,192     START WITH CLASS A
DI2      BAL   R2,NEXTJQT     DETERMINE NEXT QUEUE
         B     DI6            END OF QUEUES                         WGH
         BAL   R2,SRCHJQE     SEARCH A JQE QUEUE
         B     DI2            END OF QUEUE                    WGH
         B     PRTJQE         PRINT THE RECORD
* SEARCH ONLY ONE QUEUE
DI5      MVC   QCLASS,QPARM1  USER SPECIFIED CLASS
         BAL   R2,FINDJQT     FIND QUEUE
         B     DI6            END OF QUEUES                         WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     DI6            END OF QUEUE                    WGH
         B     PRTJQE         PRINT THE RECORD
* SEARCH XMIT QUEUE
DI6      L     R1,QCJQHEAD    LOAD ADDR OF JQT                      WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                      WGH
         LA    R4,JQTXMIT     ADDR OF XMIT QUEUE                    WGH
         DROP  R1                                                   WGH
         MVI   QCLASS,32      INDICATE THIS IS XMIT QUEUE           WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                      WGH
         B     STOP           END OF QUEUE                          WGH
         B     PRTJQE         PRINT THE RECORD                      WGH
***********************************************************************
*                                                                     *
*   AI - PRINT JOBS IN INPUT QUEUES THAT ARE AVAILABLE FOR SELECTION  *
*                                                                     *
***********************************************************************
AI       CLI   QPARM1,C' '    DID USER SPECIFY CLASS?
         BNE   AI5            YES. LIMIT TO ONE QUEUE.
* SEARCH ALL QUEUES
         MVI   QCLASS,192     START WITH CLASS A
AI2      BAL   R2,NEXTJQT     DETERMINE NEXT QUEUE
         B     AI6            END OF QUEUES                         WGH
         BAL   R2,SRCHJQE     SEARCH A JQE QUEUE
         B     AI2            END OF QUEUE
         CLI   JQEFLAG1,0     IS THE JOB BUSY OR HELD?
         BNE   REJJQE         YES. SKIP IT.
         B     PRTJQE         PRINT THE RECORD
* SEARCH ONLY ONE QUEUE
AI5      MVC   QCLASS,QPARM1  USER SPECIFIED CLASS
         BAL   R2,FINDJQT     FIND QUEUE
         B     AI6            NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     AI6            END OF QUEUE                          WGH
         CLI   JQEFLAG1,0     IS THE JOB BUSY OR HELD?
         BNE   REJJQE         YES. SKIP IT.
         B     PRTJQE         PRINT THE RECORD
* SEARCH XMIT QUEUE
AI6      L     R1,QCJQHEAD    LOAD ADDR OF JQT                      WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                      WGH
         LA    R4,JQTXMIT     ADDR OF XMIT QUEUE                    WGH
         DROP  R1                                                   WGH
         MVI   QCLASS,32      INDICATE THIS IS XMIT QUEUE           WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                      WGH
         B     STOP           END OF QUEUE                          WGH
         CLI   JQEFLAG1,0     IS THE JOB BUSY OR HELD?              WGH
         BNE   REJJQE         YES. SKIP IT.                         WGH
         B     PRTJQE         PRINT THE RECORD                      WGH
***********************************************************************
*                                                                     *
*   AO - PRINT AVAILABLE JOBS IN THE OUTPUT QUEUE                     *
*                                                                     *
***********************************************************************
AO       CLI   QPARM1,C' '    DID USER SPECIFY CLASS?
         BNE   AO5            YES. LIMIT TO ONE QUEUE.
* SEARCH ALL QUEUES
         MVI   QCLASS,192     START WITH CLASS A
AO2      EQU   *
         CLI   QSUBNAME,C'D'  IS THIS 'AO' OR 'DO' ?              WGH42
         BE    AO3            CONTINUE FOR 'DO'                   WGH42
         CLI   QCLASS,C'9'    HAVE WE REACHED HOLDQ YET?          WGH42
         BE    STOP           YES.. DO NOT DISP HOLD ON AO REQ    WGH42
AO3      BAL   R2,NEXTJOT     DETERMINE NEXT QUEUE
         B     STOP           NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJOE     SEARCH A JQE QUEUE
         B     AO2            END OF QUEUE
         B     PRTJOE         PRINT THE RECORD
* SEARCH ONLY ONE QUEUE
AO5      EQU   *              USER SPECIFIED CLASS
         CLC   QPARM1(4),=C'HELD'                                  WGH
         BNE   AO6            NO.. USE CLASS SPECIFIED
         MVI   QCLASS,C'!'    YES.. CHANGE TO ! TO SELECT HELD OUT Q
         B     AO7            AND CONTINUE                        WGH42
AO6      MVC   QCLASS,QPARM1  USER SPECIFIED CLASS
AO7      BAL   R2,FINDJOT     FIND QUEUE
         B     STOP           NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJOE     SEARCH THE QUEUE
         B     AO8            END OF QUEUE
         B     PRTJOE         YES.. PRINT THE RECORD
*   SEARCH HOLD Q FOR SELECTED CLASS                                WGH
AO8      EQU   *                                                    WGH
         CLI   QCLASS,C'!'    WAS ALL HELD OUT REQUESTED ?          WGH
         BE    STOP           YES... DO NOT CONTINUE                WGH
         CLI   QSUBNAME,C'A'  IS THIS 'AO' OR 'DO' ?              WGH42
         BE    STOP           EXIT FOR 'AO'                       WGH42
         MVI   QCLASS,C'!'    CHANGE TO ! TO SELECT HELD OUT Q      WGH
         BAL   R2,FINDJOT     FIND QUEUE
         B     STOP           NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJOE     SEARCH THE QUEUE
         B     STOP           END OF QUEUE
         CLC   JOECURCL,QPARM1     IS THIS CLASS REQUESTED ?        WGH
         BNE   REJJOE         NO... SKIP IT                         WGH
         B     PRTJOE         YES.. PRINT THE RECORD
***********************************************************************
*                                                                     *
*   WO - PRINT JOES IN OUTPUT QUEUE THAT HAVE WTRID SPECIFIED         *
*                                                                     *
***********************************************************************
* SEARCH ALL QUEUES                                               UF047
WO       MVI   QCLASS,192     START WITH CLASS A                  UF047
WO2      BAL   R2,NEXTJOT     DETERMINE NEXT QUEUE                UF047
         B     STOP           NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJOE     SEARCH A JQE QUEUE                  UF047
         B     WO2            END OF QUEUE                        UF047
         SR    R1,R1          CLEAR REG FOR INSERT                UF047
         ICM   R1,B'0111',JOECHARB  OFFSET TO CHAR JOE            UF047
         BZ    REJJOE         NONE, SKIP THIS ONE                 UF047
         A     R1,QCJOTA      GET REAL JOE ADDRESS                UF047
         TM    JOEUSER-JOE(R1),X'FF'-X'40' USERID PRESENT?        WGH42
         BNZ   PRTJOE         YES.. PRINT IT                      WGH42
         TM    JOEWTRID-JOE(R1),X'FF'-X'40' WTRID PRESENT?        UF047
         BZ    REJJOE         NO, TRY NEXT ONE                    UF047
         B     PRTJOE         PRINT THE RECORD                    UF047
***********************************************************************
*                                                                     *
*   HI - PRINT JOBS IN INPUT QUEUES THAT ARE HELD                     *
*                                                                     *
***********************************************************************
HI       CLI   QPARM1,C' '    DID USER SPECIFY CLASS?
         BNE   HI5            YES. LIMIT TO ONE QUEUE.
* SEARCH ALL QUEUES
         MVI   QCLASS,192     START WITH CLASS A
HI2      BAL   R2,NEXTJQT     DETERMINE NEXT QUEUE
         B     HI6            END OF QUEUES                         WGH
         BAL   R2,SRCHJQE     SEARCH A JQE QUEUE
         B     HI2            END OF QUEUE
         TM    JQEFLAG1,JQE1HLDA+JQE1HLD1+JQE1HLD2 IS JOB HELD?
         BZ    REJJQE         NO. SKIP IT.
         B     PRTJQE         PRINT THE RECORD
* SEARCH ONLY ONE QUEUE
HI5      MVC   QCLASS,QPARM1  USER SPECIFIED CLASS
         BAL   R2,FINDJQT     FIND QUEUE
         B     HI6            NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     HI6            END OF QUEUE                     WGH
         TM    JQEFLAG1,JQE1HLDA+JQE1HLD1+JQE1HLD2 IS JOB HELD?
         BZ    REJJQE         NO. SKIP IT.
         B     PRTJQE         PRINT THE RECORD
* SEARCH XMIT QUEUE                                                 WGH
HI6      L     R1,QCJQHEAD    LOAD ADDR OF JQT                      WGH
         USING JQTDSECT,R1    BASE REG FOR JQT                      WGH
         LA    R4,JQTXMIT     ADDR OF XMIT QUEUE                    WGH
         DROP  R1                                                   WGH
         MVI   QCLASS,32      INDICATE THIS IS XMIT QUEUE           WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE                      WGH
         B     STOP           END OF QUEUE                          WGH
         TM    JQEFLAG1,JQE1HLDA+JQE1HLD1+JQE1HLD2 IS JOB HELD?     WGH
         BZ    REJJQE         YES. SKIP IT.                         WGH
         B     PRTJQE         PRINT THE RECORD                      WGH
***********************************************************************
*                                                                     *
*   HO - LIST ALL JOBS WITH HELD OUTPUT                               *
*                                                                     *
***********************************************************************
         USING JQTDSECT,R1    BASE REG FOR JQT
HO       L     R1,QCJQHEAD    LOAD ADDR OF JQT
         B     HO5            SKIP $OUTPUT Q                     WGH42
         LA    R4,JQTOUT      ADDR OF $OUTPUT QUEUE
         MVI   QCLASS,8       INDICATE THIS IS THE HELD OUT QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
*        B     STOP           END OF QUEUE                    WGH
         B     HO5            END OF QUEUE                          WGH
*        TM    JQEFLAG8,JQE8HLDS    ANY HELD 3540 DATASETS?       WGH42
*        BNZ   PRTJQE         YES. PRINT THE RECORD.
         B     PRTJQE         PRINT THE  RECORD                   WGH42
*        B     REJJQE         REJECT THE RECORD
*        QUEUE OF HELD JOES                                       WGH42
HO5      EQU   *              HELD OUTPUT Q FOR JOES              WGH42
         MVI   QCLASS,C'!'    SPECIFY ! TO SELECT HELD OUT Q
         BAL   R2,FINDJOT     FIND QUEUE
         B     STOP           NO MORE QUEUES (NEVER COMES HERE)     WGH
         BAL   R2,SRCHJOE     SEARCH THE QUEUE
         B     STOP           END OF QUEUE
         CLI   QPARM1,C' '    DID USER SPECIFY CLASS?
         BE    PRTJOE         NO.. PRINT THE RECORD               WGH42
         CLC   JOECURCL,QPARM1 YES.  IS THIS CLASS REQUESTED ?      WGH
         BE    PRTJOE         YES.. PRINT THE RECORD                WGH
         B     REJJOE         NO... SKIP RECORD                     WGH
***********************************************************************
*                                                                     *
*   HQ - LIST ALL JOBS IN   $OUTPUT QUEUE                             *
*                                                                     *
***********************************************************************
         USING JQTDSECT,R1    BASE REG FOR JQT
HQ       L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTOUT      ADDR OF $OUTPUT QUEUE
         DROP  R1                                             WGH
         MVI   QCLASS,8       INDICATE THIS IS THE HELD OUT QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     STOP           END OF QUEUE                    WGH
         B     PRTJQE         PRINT THE  RECORD                   WGH42
***********************************************************************
*                                                                     *
*   DA - FIND ALL EXECUTING JOBS                                      *
*                                                                     *
***********************************************************************
DA       MVI   QCLASS,192     START WITH CLASS A
DA2      BAL   R2,NEXTJQT     DETERMINE NEXT QUEUE
         B     STOP           END OF QUEUE                          WGH
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     DA2            END OF QUEUE
         TM    JQEFLAG1,JQE1BUSY IS THE JOB EXECUTING?
         BZ    REJJQE         NO. REJECT IT.
         B     PRTJQE         PRINT THE RECORD
***********************************************************************
*                                                                     *
*   DT - LIST ALL TSO USERS                                           *
*                                                                     *
***********************************************************************
         USING JQTDSECT,R1    BASE REG FOR JQT
DT       L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTTSU      ADDR OF TSO QUEUE
         DROP  R1
         MVI   QCLASS,0       INDICATE THIS IS THE TSO QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     STOP           END OF QUEUE
         B     PRTJQE         PRINT THE RECORD
***********************************************************************
*                                                                     *
*   DS - LIST ALL SYSTEM STARTED TASKS                                *
*                                                                     *
***********************************************************************
         USING JQTDSECT,R1    BASE REG FOR JQT
DS       L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTSTC      ADDR OF STC QUEUE
         DROP  R1
         MVI   QCLASS,4       INDICATE THIS IS THE STC QUEUE
         BAL   R2,SRCHJQE     SEARCH THE QUEUE
         B     STOP           END OF QUEUE
         B     PRTJQE         PRINT THE RECORD
***********************************************************************
*                                                                     *
*   DETERMINE LENGTH OF LEVEL                                         *
*                                                                     *
***********************************************************************
PARMLEN  LA    R3,7           MAXIMUM LENGTH OF 8
         LA    R4,QPARM1+7    END OF FIELD
PARMLEN2 CLI   0(R4),C' '     IS THIS BYTE BLANK?
         BNE   PARMLEN3       NO. THIS IS THE LENGTH
         BCTR  R4,0           TRY PREVIOUS BYTE
         BCT   R3,PARMLEN2    LOOP
PARMLEN3 STH   R3,QLNG1       STORE THE LENGTH OF LEVEL
         BR    R2             RETURN TO CALLER
***********************************************************************
*                                                                     *
*   SEARCH A JQE QUEUE                                                *
*                                                                     *
***********************************************************************
SRCHJQE  ZAP   QCOUNT,=P'0'   ZERO THE QUEUE COUNT
         L     R9,0(R4)       LOAD FIRST JQE OFFSET               UF020
NEXTJQE  LA    R9,0(,R9)      KILL EXTRANEOUS BITS                UF020
         LTR   R9,R9          TEST FOR END OF QUEUE               UF020
         BZR   R2             END OF QUEUE. RETURN.
         A     R9,QCJQTA      ADD BASE TO OFFSET
         B     4(R2)          DETERMINE ELIGIBILITY
PRTJQE   AP    QCOUNT,=P'1'   INCREMENT COUNT
PRT2JQE  SR    R1,R1          INDICATE THIS IS A JQE
         L     R15,=V(FORMAT) ADDR OF PRINT MODULE
         BALR  R14,R15        PRINT THE JQE IN R9
REJJQE   L     R9,JQENEXT     LOAD OFFSET TO NEXT JQE             UF020
         B     NEXTJQE        GET THE NEXT JQE
SKIPJQE  AP    QCOUNT,=P'1'   INCREMENT COUNT
         B     REJJQE         CONTINUE
***********************************************************************
*                                                                     *
*   SEARCH A JOE QUEUE                                                *
*                                                                     *
***********************************************************************
SRCHJOE  ZAP   QCOUNT,=P'0'    ZERO THE QUEUE COUNT               RNB16
         CLI   QCLASS,C'@'     NJE SYSOUT XMIT QUEUE??            WGH42
         BE    FIRSTJOE        YES.. GO DO IT                     WGH42
         CLI   QCLASS,C'!'     HOLD SYSOUT QUEUE    ??            WGH42
         BE    FIRSTJOE        YES.. GO DO IT                     WGH42
         OI    QFLAG1,QFLG1LCL SHOW SEARCHING LOCAL QUEUE         RNB16
         B     FIRSTJOE        AND GO DO IT                       RNB16
*                                                                 RNB16
SRCHJOE1 TM    QFLAG1,QFLG1LCL   WERE WE PROCESSING LCL QUEUE?    WGH42
         BO    SRCHJOE2          YES.. GO DO USER QUEUE           WGH42
         NI    QFLAG1,X'FF'-QFLG1USR TURN OFF USER QUEUE FLAG     WGH42
         LA    R4,4(,R4)             POINT TO REMOTE QUEUE        WGH42
         B     FIRSTJOE          AND GO DO IT                     WGH42
SRCHJOE2 NI    QFLAG1,X'FF'-QFLG1LCL TURN OFF LOCAL QUEUE FLAG    WGH42
         OI    QFLAG1,QFLG1USR    SHOW SEARCHING USER  QUEUE      WGH42
         LA    R4,4(,R4)             POINT TO USER QUEUE          WGH42
*                                                                 RNB16
FIRSTJOE L     R8,0(R4)       LOAD FIRST JOE OFFSET               UF020
         LA    R8,0(,R8)       KILL EXTRANEOUS BITS.              UF020
         LTR   R8,R8          IS THE QUEUE EMPTY?
         BNZ   $1             /NO  - CONTINUE                     RNB16
*                             /YES -                              RNB16
         CLI   QCLASS,C'@'     NJE SYSOUT XMIT QUEUE??            WGH
         BE    0(R2)           YES.. RETURN.. NO MORE QUEUES      WGH
         CLI   QCLASS,C'!'     NJE SYSOUT XMIT QUEUE??            WGH
         BE    0(R2)           YES.. RETURN.. NO MORE QUEUES      WGH
*                             /WAS THIS THE LOCAL OR USER QUEUE   WGH42
         TM    QFLAG1,QFLG1LCL+QFLG1USR     LCL OR USR QUEUE      WGH42
         BNZ   SRCHJOE1       /YES - GO DO NEXT QUEUE             WGH42
         BR    R2             /NO  - RETURN TO CALLER             RNB16
$1       EQU   *                                                  RNB16
         XC    PRIORITY(12),PRIORITY ZERO OUT HIGHEST POINTERS
NEXTJOE  LA    R8,0(,R8)      CLEAR EXTRA BITS                    UF020
         LTR   R8,R8          TEST FOR END OF QUEUE               UF020
         BZ    TESTJOE        END OF QUEUE. PASS HIGHEST TO CALLER.
         A     R8,QCJOTA      ADD BASE TO OFFSET
         L     R9,JOEJQE      OFFSET TO JQE                       UF020
         N     R9,=A(X'00FFFFFF')  CLEAR EXTRA BITS               UF020
         BZ    TRYJOE         THIS JOE ALREADY USED. TRY NEXT ONE.
         A     R9,QCJQTA      ADD BASE TO OFFSET
*        LA    R7,255         PRESET MAXIMUM PRIORITY             WGH
*        TM    JQEPRIO,240    IS THIS JOB PRIORITY 15?            WGH
*        BO    HIGHJOE        YES. PASS TO CALLER.                WGH
         LA    R1,1           PRESET PRIORITY ONE
         CLI   JQETYPE,$HARDCPY IS THE JOB EXECUTING?
         BNE   EXECJOE        YES. USE PRIORITY ONE.
         IC    R1,JQEPRIO     INSERT JQE PRIORITY
         SRL   R1,4           USE TOP HEX DIGIT                   WGH
*EXECJOE  IC    R7,JOEPRIO     INSERT JOE PRIORITY
EXECJOE  LH    R7,JOEPRIO     INSERT JOE PRIORITY
         SRL   R7,4           REMOVE UNUSED DIGIT                 WGH
         AR    R7,R1          ADD PRIORITIES
         SRL   R7,1           BECAUSE HASP DOES IT, THAT'S WHY
HIGHJOE  C     R7,PRIORITY    IS THIS LESS THAN PREVIOUS HIGH?
         BL    TRYJOE         YES. TRY NEXT ONE.
         STM   R7,R9,PRIORITY NO. REPLACE PREVIOUS HIGH.
TRYJOE   L     R8,JOENEXT     ADDR OF NEXT JOE                    UF020
         N     R8,=A(X'00FFFFFF')  CLEAR EXTRA BITS               UF020
         B     NEXTJOE        TRY NEXT JOE
TESTJOE  LM    R7,R9,PRIORITY LOAD ADDR OF HIGHEST JOE
         LTR   R8,R8          WAS THE QUEUE EMPTY?
         BNZ   $2                                                 RNB16
         TM    QFLAG1,QFLG1LCL WAS THIS THE LOCAL QUEUE?          RNB16
         BO    SRCHJOE1       /YES - GO DO REMOTE QUEUE           RNB16
         BR    R2             /NO  - END OF QUEUE.                RNB16
$2       EQU   *                                                  RNB16
         MVC   JOEJQEB,=F'0'  INDICATE THIS JOE USED              RNB16
         B     4(R2)          DETERMINE ELIGIBILITY
PRTJOE   EQU   *                                                  WGH
         LA    R1,4           INDICATE THIS IS A JOE
         AP    QCOUNT,=P'1'   INCREMENT COUNT
         L     R15,=V(FORMAT) ADDR OF PRINT MODULE
         BALR  R14,R15        PRINT THE JOE IN R9
REJJOE   B     FIRSTJOE       GET THE NEXT JOE
SKIPJOE  AP    QCOUNT,=P'1'   INCREMENT COUNT
         B     REJJOE         CONTINUE
***********************************************************************
*                                                                     *
*   DETERMINE INPUT QUEUE                                             *
*                                                                     *
***********************************************************************
FINDJQT  TR    QCLASS,CLASSTBL DETERMINE OFFSET
         LH    R4,QCLASSH     LOAD TABLE OFFSET
         BCTR  R4,0           SUBTRACT 1
         STH   R4,QCLASSH     RESTORE VALUE
         B     NEXTJQT2       CONTINUE
NEXTJQT  TR    QCLASS,CLASSTBL DETERMINE OFFSET FROM FIRST CLASS
         LH    R4,QCLASSH     LOAD TABLE OFFSET
         CH    R4,=H'36'      IS THIS THE LAST QUEUE?
         BNL   0(R2)          YES. GO HOME                        WGH
*        BNL   NEXTJQT9       YES. GO HOME.
NEXTJQT2 TR    QCLASS,NAMETBL MOVE CLASS NAME TO QCLASS
         AR    R4,R4          MULTIPLY BY 2
         AR    R4,R4          AND BY 2 AGAIN                      UF020
         USING JQTDSECT,R1    BASE REG FOR JQT
         L     R1,QCJQHEAD    LOAD ADDR OF JQT
         LA    R4,JQTCLSA(R4) NEXT QUEUE
         DROP  R1
         B     4(R2)          RETURN TO CALLER                 WGH
*        BR    R2             RETURN TO CALLER
NEXTJQT9 CLI   QCODE,0        IS THIS A STATUS REQUEST?
         BE    ST5            YES. GO TO STATUS.
         CLI   QCODE,36       IS THIS A DJ REQUEST?
         BNE   STOP           NO. GO HOME.
         B     ST5            YES. GO TO STATUS.
***********************************************************************
*                                                                     *
*   DETERMINE OUTPUT QUEUE                                            *
*                                                                     *
***********************************************************************
FINDJOT  TR    QCLASS,CLASSTBL DETERMINE OFFSET
         LH    R4,QCLASSH     LOAD TABLE OFFSET
         BCTR  R4,0           SUBTRACT 1
         STH   R4,QCLASSH     RESTORE VALUE
         B     NEXTJOT2       CONTINUE
NEXTJOT  TR    QCLASS,CLASSTBL DETERMINE OFFSET FROM FIRST CLASS
         LH    R4,QCLASSH     LOAD TABLE OFFSET
*        CH    R4,=H'36'      IS THIS THE LAST QUEUE?   '9'
*        CH    R4,=H'37'      IS THIS THE LAST QUEUE?   '@'        WGH
         CH    R4,=H'38'      IS THIS THE LAST QUEUE?   '!'        WGH
         BNL   0(R2)          YES. GO HOME.                        WGH
*        BNL   STOP           YES. GO HOME.
NEXTJOT2 TR    QCLASS,NAMETBL MOVE CLASS NAME TO QCLASS
         CH    R4,=H'36'      IS THIS XMIT OR HOLD OUT QUEUE       WGH
*        BNE   NEXTJOT3       NO... CONTINUE                       WGH
         BL    NEXTJOT3       NO... CONTINUE                       WGH
         BE    NEXTJOTN       YES XMIT QUEUE/                      WGH
         B     NEXTJOTH       YES.. HOLD QUEUE                     WGH
NEXTJOTH L     R1,QCJOTA      LOAD ADDR OF JOT                     WGH
         USING JOT,R1         BASE REG FOR JOT                     WGH
         LA    R4,JOTHOLDQ    HELD OUTPUT QUEUE                    WGH
         DROP  R1                                                  WGH
         B     4(R2)          RETURN TO CALLER                     WGH
NEXTJOTN L     R1,QCJOTA      LOAD ADDR OF JOT                     WGH
         USING JOT,R1         BASE REG FOR JOT                     WGH
         LA    R4,JOTNTWKQ    NETWORK QUEUE (NOT A PAIR)           WGH
         DROP  R1                                                  WGH
         B     4(R2)          RETURN TO CALLER                     WGH
NEXTJOT3 MH    R4,=H'12'      MULTIPLY BY 12 (POINT TO LOCALS)   WGH42
         USING JOT,R1         BASE REG FOR JOT
         L     R1,QCJOTA      LOAD ADDR OF JOT
         LA    R4,JOTCLSQ(R4) NEXT QUEUE
         DROP  R1
         B     4(R2)          RETURN TO CALLER            WGH
*        BR    R2             RETURN TO CALLER
***********************************************************************
*                                                                     *
*   CALL DISPLAY ROUTINE                                              *
*                                                                     *
***********************************************************************
DISPLAY  LA    R1,QDMSG             SAVE ADDRESS
         ST    R1,QDMSGA                 OF THE MESSAGE
         MVC   QDMLNG,=H'80'        SET THE LENGTH
         L     R15,=V(DISPLAY)      BRANCH TO
         BALR  R14,R15                     DISPLAY
         BR    R2                   RETURN TO CALLER
***********************************************************************
*                                                                     *
*   GO HOME                                                           *
*                                                                     *
***********************************************************************
STOP     QSTOP
         LTORG
***********************************************************************
*                                                                     *
*   TABLES FOR CLASS DETERMINATION                                    *
*                                                                     *
***********************************************************************
*NAMETBL  DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'
NAMETBL  DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789@!'            WGH
CLASSTBL DC    192X'01',64X'00'                              WGH
         ORG   CLASSTBL+C'!'
         DC    AL1(38)
         ORG   CLASSTBL+C'@'
         DC    AL1(37)
         ORG   CLASSTBL+C'A'
         DC    AL1(1,2,3,4,5,6,7,8,9)
         ORG   CLASSTBL+C'J'
         DC    AL1(10,11,12,13,14,15,16,17,18)
         ORG   CLASSTBL+C'S'
         DC    AL1(19,20,21,22,23,24,25,26)
         ORG   CLASSTBL+C'0'
         DC    AL1(27,28,29,30,31,32,33,34,35,36)
         ORG   ,
*CLASSTBL DC    124X'01',X'25',67X'01'                             WGH
*         DC    X'00010203040506070809',7X'00'
*         DC    X'0A0B0C0D0E0F101112',8X'00'
*         DC    X'131415161718191A',6X'00'
*         DC    X'1B1C1D1E1F2021222324',6X'00'
***********************************************************************
*                                                                     *
*   MISCELLANEOUS GARBAGE                                             *
*                                                                     *
***********************************************************************
ED5      DC    X'402020202120'
DQHEADER DC    CL80'*** INPUT QUEUES ***'
DQLINE   DC    CL80' INPUT                        EXECUTING      WAITINX
               G      HELD'
DFHEADER DC    CL80'*** OUTPUT QUEUES ***'
DFLINE   DC    CL80' OUTPUT                       PRINTING'
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
SEARCH   CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         SPACE 1                                                  UF001
         SPACE 1                                                  UF001
         $JQE
         $JOE
         $JOT
         $JQT
         $HASPEQU
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         ORG   QDMSG
         DS    CL9
CLASS    DS    CL1
         DS    CL2
QTCOUNT  DS    CL6
         DS    CL7
QECOUNT  DS    CL3
         DS    CL12
QACOUNT  DS    CL3
         DS    CL10
QHCOUNT  DS    CL3
         ORG   QTCOUNT
FCOUNT   DS    CL6
WORK     DSECT
         DS    CL80
PRIORITY DS    3F
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
