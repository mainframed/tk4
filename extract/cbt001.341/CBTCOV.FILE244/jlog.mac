JLOG     TITLE 'QUEUE COMMAND - LIST JOBLOG MESSAGES FOR A JOB'   ONL01
JLOG     QSTART ,                  START JLOG CSECT ASSEMBLY      ONL01
         GBLB  &QJTIP         JTIP OPTION                         ONL05
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR JOB                           ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QAUTHCK QAJLOG            PERFORM JOB AUTHORIZATION      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL05
*                                                                 ONL05
*   FIND JOB LOG PDDB                                             ONL05
*                                                                 ONL05
******************************************************************ONL05
         SPACE 1                                                  ONL05
         USING PDB,R2              BASE REG FOR PDDB
         USING IOT,R3              BASE REG FOR IOT               UF046
         L     R3,QCIOTA           LOAD BASE REG
         SH    R3,=AL2(IOTSTART-IOT) ADJUST FOR HEADER            UF046
         SPACE 1                                                  ONL03
         CLC   IOTID,=CL4'IOT'     WAS AN IOT READ FROM SPOOL?    ONL03
         BNE   BADIOT              ERROR IF NO                    ONL03
         CLC   QPJOBID,IOTJBKEY    IS THE IOT'S JOB KEY VALID?    ONL03
         BNE   BADIOT              ERROR IF NO                    ONL03
         SPACE 1                                                  ONL03
         LR    R4,R3               COPY IOT ADDR                  ONL03
         A     R4,IOTPDDBP         OFFSET BEYOND LAST PDDB
         LR    R2,R3               BASE OF IOT
         A     R2,IOTPDDB          OFFSET TO FIRST PDDB IN IOT    UF046
FINDDS   CLC   PDBDSKEY,=F'2'      IS THIS THE JOB LOG            WGH42
         BE    FOUNDDS             YES. CONTINUE.
         LA    R2,PDBLENG(R2)      NO. LOOK AT NEXT PDDB
         CR    R2,R4               HAVE WE GONE PAST THE LAST PDDB
         BL    FINDDS              NO. TRY AGAIN
         B     NOJLPDDB            ERROR IF JOB LOG PDDB NOT FOUNDONL05
         SPACE 1                                                  ONL05
FOUNDDS  DS    0H                  JLOG PDDB HAS BEEN FOUND       ONL05
         SPACE 2                                                  ONL05
******************************************************************ONL05
*                                                                 ONL05
*  THE JOBLOG IS CONSIDERED AVAILABLE IF:                         ONL05
*                                                                 ONL05
*     (1) IF THE JOBLOG HAS MORE THAN ONE LINE OF OUTPUT.         ONL05
*     (2) THE JOB IS ON THE OUTPUT OR HARDCOPY QUEUES.            ONL05
*     (3) THE JOB IS NOT ON THE INPUT OR CONVERSION QUEUES.       ONL05
*     (4) THE CONVERTER RETURN CODE IS NON-ZERO.                  ONL05
*     (5) IF THE &QJTIP OPTION IS OFF AND THE JOB'S EXECUTION     ONL05
*         START TIME (JCTXEQON) IS NON-ZERO.                      ONL05
*                                                                 ONL05
*  OTHERWISE, THE JOBLOG IS CONSIDERED NOT AVAILABLE.             ONL05
*                                                                 ONL05
******************************************************************ONL05
         SPACE 1                                                  ONL05
         CLC   PDBRECCT,=F'2'      ARE WE SURE JOB LOG HAS INFO ? ONL05
         BH    LOGOK               SKIP IF YES, LIST JOBLOG       ONL05
         SPACE 1                                                  ONL05
         L     R4,QCJQEA           ADDR OF JQE                    ONL05
         USING JQE,R4              NOTE JQE ADDRESSABILITY        ONL05
         SPACE 1                                                  ONL05
         CLI   JQETYPE,$OUTPUT     IT'S OK TO                     ONL05
         BE    LOGOK                LIST THE JOBLOG               ONL05
         CLI   JQETYPE,$HARDCPY      IF THE JOB IS ON THE         ONL05
         BE    LOGOK                  OUTPUT OR HARDCOPY QUEUES   ONL05
         SPACE 1                                                  ONL05
         CLI   JQETYPE,$INPUT      DON'T LIST THE                 ONL05
         BE    NOLOG                JOBLOG IF THE JOB             ONL05
         CLI   JQETYPE,$XEQ          IS ON THE INPUT              ONL05
         BE    NOLOG                  OR CONVERSION QUEUES        ONL05
         SPACE 1                                                  ONL05
         L     R3,QCJCTA           ADDR OF JCT                    ONL05
         USING JCT,R3              NOTE JCT ADDRESSABILITY        ONL16
         SPACE 1                                                  ONL05
         CLC   JCTCNVRC,=F'0'      NON-ZERO CONVERTER RETURN CODE?ONL05
         BNE   LOGOK               SKIP IF YES, LIST JOBLOG       ONL05
         SPACE 1                                                  ONL05
         AIF   (&QJTIP).ONL05A                                    ONL05
         CLC   JCTXEQON,=F'0'      JOB EXECUTING OR EXECUTED ?    ONL05
         BNZ   LOGOK               SKIP IF YES, LIST JOBLOG       ONL05
.ONL05A  ANOP  ,                                                  ONL05
         SPACE 1                                                  ONL05
         B     NOLOG               OTHERWISE, DON'T LIST JOBLOG   ONL05
         SPACE 1                                                  ONL05
         DROP  R3                  KILL JCT ADDRESSABILITY        ONL05
         DROP  R4                  KILL JQE ADDRESSABILITY        ONL05
         SPACE 1                                                  ONL05
LOGOK    DS    0H                  OK TO LIST THE JOBLOG          ONL05
         SPACE 2                                                  ONL05
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' PRINT OFFSET FOR EACH RECORD
         MVC   QPDSID,=H'2'   DSID OF DATASET TO BE PRINTED
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
         SPACE 2                                                  ONL03
BADIOT   QTILT '*** JOB HAS PURGED (OR IOT IS INVALID) ***'       ONL03
NOLOG    QTILT '*** JOBLOG IS NOT AVAILABLE ***'                  ONL05
NOJLPDDB QTILT '*** JOBLOG PDDB NOT FOUND ***'                    ONL05
         SPACE 1                                                  ONL05
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
JLOG     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         $BUFFER                                                  UF020
         IFGRPL
         $JQE
         $TAB
         $JCT
         $PDDB
         $IOT
         $HASPEQU
         QCOMMON ,                                                ONL11
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
