CJCT     TITLE 'QUEUE COMMAND - DUMP A JCT IN HEX'                ONL01
CJCT     QSTART ,                  START CJCT CSECT ASSEMBLY      ONL01
***********************************************************************
* JCT JOBNAME <OFFSET>                                                *
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JCT                                    *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL HEXDUMP TO DUMP THE JCT                                      *
*                                                                     *
***********************************************************************
         SPACE 1                                                  ONL14
         L     R10,QVCKPT          ADDR OF CKPT WORK AREA         ONL14
         USING QCKPT,R10           NOTE CKPT WORK ADDRESSABILITY  ONL14
         SPACE 1                                                  ONL14
         TM    QFLAG1,QFLG1SDS     IS THIS A SYSTEM DATA SET JOB? ONL14
         BO    TILTSYSD            TILT IF YES                    ONL14
         SPACE 1                                                  ONL14
         L     R1,QCJCTA      ADDRESS OF JCT
         LA    R0,JCTSTART-JCT  OFFSET TO START OF JCT
         LH    R15,QLNG2      LENGTH OF USER OFFSET INTO JCT
         LTR   R15,R15        IS THE LENGTH ZERO?
         BNP   DUMP0          YES. NONE SPECIFIED
         CH    R15,=H'8'      IS THE LENGTH TOO BIG?
         BH    TILTO          YES, GIVE UP
         EX    R15,OFFTR      CONVERT TO HEX
         EX    R15,OFFPACK    PACK INTO QDWORD
         LH    R15,QDWORD     PICK UP OFFSET
         CR    R0,R15         COMPARE TO BASE OFFSET
         BNL   DUMP0          USE R0 FOR OFFSET
         LR    R0,R15         GET OTHER OFFSET
DUMP0    AR    R1,R0          ADD TO BASE ADDRESS
         LH    R15,HASPACE+62 MAX LEN INCLUDING NETWORK HEADERS
         LA    R15,JCTSTART-JCT(R15)  + LEN OF PREFIX
         SR    R15,R0         TOTAL LENGTH - OFFSET = LENGTH TO DUMP
         SLL   R0,16          MOVE OFFSET TO PROPER POSITION
         OR    R0,R15         INSERT INTO LENGTH REG
         L     R15,=V(HEXDUMP) ADDRESS OF DUMP ROUTINE
         BALR  R14,R15        LINK TO IT
STOP     QSTOP
***********************************************************************
*                                                                     *
*   EXCEPTIONS AND RETURN                                             *
*                                                                     *
***********************************************************************
TILTO    QTILT '*** INVALID OFFSET SPECIFIED ***'
TILTSYSD QTILT '*** COMMAND INVALID FOR SYSDS TYPE JOBS ***'      ONL14
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
OFFTR    TR    QPARM2,TABLEH       CONVERT TO HEX
OFFPACK  PACK  QDWORD(3),QPARM2(1) PACK TO WORK AREA
         LTORG
* TABLE FOR HEX CONVERT
TABLEH   DC    CL193' '
         DC    X'0A0B0C0D0E0F',CL41' ',C'01234567890',CL6' '
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
CJCT     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
        $BUFFER
         IFGRPL
        $JQE
        $JCT
        $HASPEQU
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
