READSPC  TITLE 'QUEUE COMMAND - READ A BLOCK FROM HASPACE'        ONL01
READSPC  QSTART ,                  START READSPC CSECT ASSEMBLY   ONL01
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CONVERT MTTR TO MBBCCHHR                                          *
*                                                                     *
***********************************************************************
*
* NOTE - THE INPUT TO THIS ROUTINE IS AS FOLLOWS:
*            QCTRAK CONTAINS THE MTTR
*            R1 CONTAINS THE IOAREA ADDRESS
*
         LR    R2,R1          MOVE IOAREA ADDR TO R2
         SR    R3,R3          ZERO R3
         IC    R3,QCTRAKM     RELATIVE DCB NUMBER
         CH    R3,=H'36'      COMPARE WITH MAX POSSIBLE SPOOLS    UF044
         BNL   QTILT1         ERROR IF TOO LARGE                  UF044
         SLL   R3,2           MULTIPLY BY 4
         L     R15,QCTRKCYL(R3)                                   UF044
         LTR   R15,R15        VALID?                              UF044
         BZ    QTILT1         ERROR IF ZERO                       UF044
         L     R15,QCSPOOLS(R3)                                   UF044
         LTR   R15,R15        VALID?                              UF044
         BZ    QTILT1         ERROR IF ZERO                       UF044
         MVC   QCDADR,QCTRAKR MOVE RECORD NUMBER
         MVC   QCDADHH,QCTRAKTT MOVE TRACK TO A HALFWORD BOUNDARY
         LH    R5,QCDADHH     LOAD RELATIVE TRACK NUMBER
         SR    R4,R4          ZERO R4
         D     R4,QCTRKCYL(R3) DIVIDE TRACKS BY TRACKS PER CYLINDER
         STH   R4,QCDADHH     STORE HEAD NUMBER
         STH   R5,QCDADCC     STORE CYLINDER NUMBER
         L     R3,QCSPOOLS(R3) DCB ADDRESS
         USING IHADCB,R3      ADDRESSING FOR DCB DSECT            UF009
         MVC   DCBSYNAD+1(3),=AL3(SYNAD)  SET SYNAD ADDR IN DCB   UF009
         DROP  R3             DROP ADDRESSING FOR DCB             UF009
         READ  HDECB2,DI,(R3),(R2),MF=E
         CHECK HDECB2
         TM    QFLAG1,QFLG1IOE  I/O ERROR?                        UF009
         BZ    READOK         SKIP IF NO                          ONL02
         NI    QFLAG1,255-QFLG1IOE  CLEAR ERROR FLAG              UF009
*        SIMULATE QTILT ACTION                                    UF009
QTILTSIM DS    0H                                                 UF044
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE             UF009
         LR    R14,R15        SET FOR LOOP                        UF009
         BR    R15            GO TO IT                            UF009
         SPACE 2                                                  ONL02
READOK   DS    0H                  SPOOL READ WAS SUCCESSFUL      ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   PERFORM AUTHORIZATION CHECK FOR SPOOL BUFFER                  ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         LR    R1,R2               PASS BUFFER ADDR TO AUTH RTN   ONL02
         QAUTHCK QASPOOL           LET AUTHORIZATION RTN SEE BUFR ONL02
         SPACE 2                                                  ONL02
******************************************************************ONL02
*                                                                 ONL02
*   RETURN TO CALLER                                              ONL02
*                                                                 ONL02
******************************************************************ONL02
         SPACE 1                                                  ONL02
         QSTOP ,                   RETURN TO CALLER               ONL02
         SPACE 2                                                  ONL02
QTILT1   MVC   QDHLINE,QBLANK CLEAR TARGET AREA                   UF044
         MVC   QDHLINE(L'QTILT1M),QTILT1M  SET ERROR MESSAGE      UF044
         UNPK  QDHLINE+QTILT1M2-QTILT1M1(9),QCTRAK(5)             UF044
         TR    QDHLINE+QTILT1M2-QTILT1M1(8),HEXTAB                UF044
         MVI   QDHLINE+QTILT1M2-QTILT1M1+8,C' '                   UF044
         B     QTILTSIM       GO TO QTILT SIMULATION ROUTINE      UF044
QTILT1M1 DC    C'*** INVALID INPUT TO READSPC - MTTR = '          UF044
QTILT1M2 DC    CL8'        '                                      UF044
         DC    C' ***'                                            UF044
QTILT1M  EQU   QTILT1M1,*-QTILT1M1  ADDRESS AND LENGTH            UF044
         SPACE 1                                                  UF044
SYNAD    SYNADAF ACSMETH=BDAM DECODE ERROR CAUSE                  UF009
         OI    QFLAG1,QFLG1IOE  SET I/O ERROR FLAG                UF009
         USING QDISPLAY,R15   BASE REG FOR DISPLAY WORK AREA      UF009
         L     R15,QVDSPL     LOAD BASE REG                       UF009
         MVC   QDHLINE,QBLANK BLANK MESSAGE AREA                  UF009
         MVC   QDHLINE(78),50(R1)  COPY SYNAD MESSAGE             UF009
         MVC   QDMLNG,=H'0'   ZERO MESSAGE LENGTH                 UF009
         DROP  R15                                                UF009
         SYNADRLS ,           RELEASE WORK AREA                   UF009
         BR    R14            RETURN TO OP SYS                    UF009
         LTORG
HEXTAB   EQU   *-C'0'                                             UF044
         DC    C'0123456789ABCDEF'                                UF044
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
READSPC  CSECT                                                    RNB03
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         DCBD  DSORG=DA,DEVD=DA                                   UF009
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
