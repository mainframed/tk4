SAVE     TITLE 'QUEUE COMMAND - CREATE A COPY OF CURRENT DATASET' ONL01
SAVE     QSTART ,                  START SAVE CSECT ASSEMBLY      ONL01
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   ALLOCATE OUTPUT DATASET                                           *
*                                                                     *
***********************************************************************
         CLC   QPDSID,=X'FFFF' IS THERE A VALID DATASET?          SBG
         BE    TILT1          NO. TELL THE USER.
         LA    R1,1           DEFAULT FIRST LINE                  WGH
         ST    R1,FRSTLINE    INIT DEFAULT                        WGH
         LA    R1,0           INIT CURRENT  LINE                  WGH
         ST    R1,LINECNT     DEFAULT CURRENT LINE                WGH
         L     R1,=F'9999999'     DEFAULT LAST LINE               WGH
         ST    R1,LASTLINE        TO ENSURE END OF DATASET        WGH
         MVC   DSNAME+2(44),QBLANK BLANK THE DSNAME
         MVC   DSNAME+2(8),QPARM1 FIRST PART OF DSNAME
         LA    R1,DSNAME      ADDR OF DSNAME
         ST    R1,DA08PDSN    STORE IN PARM LIST
         LH    R2,QLNG1       LENGTH OF QPARM1
         LA    R1,2(R2,R1)    OFFSET INTO DSNAME
         LH    R3,QLNG2       LENGTH OF QPARM2
         LTR   R3,R3          ?/LENGTH ZERO
         BNZ   CHKVOL2        NO... CHECK FURTHUR                 WGH
         BAL   R8,DATA        YES. USE DATA AS DEFAULT DS TYPE    WGH
         B     DSALLOC        CONTINUE.. NO MORE PARMS            WGH
CHKVOL2  CLC   QPARM2(2),=C'V='    IS VOLSER REQUESTED            WGH
         BNE   COUNT2         NO... SEE IF A NUMBER               WGH
         BAL   R8,DATA        USE DATA AS DEFAULT DS TYPE         WGH
         MVC   DA08SER(6),QPARM2+2  YES... SET VOLSER             WGH
         MVC   DA08UNIT(8),=C'SYSALLDA'    SET UNIT               WGH
         B     COUNT3         SEE IF COUNT SPECIFIED              WGH
COUNT2   CLI   QPARM2,C'0'    SEE IF 1ST CHAR A NUM?              WGH
         BL    SUFFIX         NO.. MUST BE SUFFIX                 WGH
         BAL   R8,DATA        USE DATA AS DEFAULT TYPE            WGH
         LA    R4,QLNG2       SET UP TO PROCESS COUNT             WGH
         LA    R5,QPARM2      COUNT FIELD                         WGH
         BAL   R8,CHKCNT      CHECK IF COUNT SPECIFIED            WGH
         CLC   QPARM3(2),=C'V='   IS VOLSER REQUESTED?            WGH
         BNE   DSALLOC        NO... CONTINUE                      WGH
         MVC   DA08SER(6),QPARM3+2  YES.. SET VOLSER              WGH
         MVC   DA08UNIT(8),=C'SYSALLDA'    SET UNIT               WGH
         B     DSALLOC        CONTINUE                            WGH
SUFFIX   MVI   0(R1),C'.'     NO.. MOVE IN DELIMITER
         MVC   1(8,R1),QPARM2  SAVE DS TYPE
         LA    R1,1(R3,R2)    LENGTH OF DSNAME
         STH   R1,DSNAME      STORE LENGTH OF DSNAME              WGH
         MVC   DA08DDN(72),ALCLIST REMAINDER OF PARM LIST
CHKVOL3  LH    R3,QLNG3       LENGTH OF QPARM3                    WGH
         LTR   R3,R3          WAS IT SPECIFIED?                   WGH
         BZ    DSALLOC        NO, SKIP THIS                       WGH
         CLC   QPARM3(2),=C'V='   IS VOLSER REQUESTED?            WGH
         BNE   COUNT3         NO... CHECK COUNT PARM              WGH
         MVC   DA08SER(6),QPARM3+2  YES.. SET VOLSER              WGH
         MVC   DA08UNIT(8),=C'SYSALLDA'    SET UNIT               WGH
         LA    R4,QLNG4       POINT TO 4TH PARM                   WGH
         LA    R5,QPARM4      USE 4TH PARM AS COUNT               WGH
         BAL   R8,CHKCNT      SEE IF COUNT SPECIFIED              WGH
         B     DSALLOC        CONTINUE                            WGH
COUNT3   LA    R4,QLNG3       POINT TO 3RD PARM                   WGH
         LA    R5,QPARM3      USE 3RD PARM AS COUNT               WGH
         BAL   R8,CHKCNT      SEE IF COUNT SPECIFIED              WGH
         CLC   QPARM4(2),=C'V='   IS VOLSER REQUESTED?            WGH
         BNE   DSALLOC        NO... CONTINUE                      WGH
         MVC   DA08SER(6),QPARM4+2  YES.. SET VOLSER              WGH
         B     DSALLOC        CONTINUE                            WGH
CHKCNT   LH    R3,0(R4)       LENGTH OF COUNT FIELD               WGH
         LTR   R3,R3          WAS IT SPECIFIED?                   WGH
         BZ    DSALLOC        NO... DEFAULT TO MAX                WGH
CURRPTR  XC    SCRATCH,SCRATCH      ZERO WORK AREA                WGH
         MVC   SCRATCH+4(4),QPREC   MOVE CURRENT LINE TO CONVERT  WGH
         CVB   R15,SCRATCH    CONVERT TO BINARY                   WGH
         LTR   R15,R15        IS IT POSITIVE?                     WGH
         BNP   CHKNUM         NO..  LEAVE DEFAULT                 WGH
         ST    R15,FRSTLINE   SAVE FOR LATER                      WGH
CHKNUM   BCTR  R3,0           YES.. LAST LINE # LENGTH -1         WGH
         LA    R14,0(R5)      POINT TO PARM FIELD                 WGH
         MVC   QFZONES,QFZONE INIT CHECK FIELD                    WGH
         EX    R3,MVZ         MOVE ZONES FROM PARM                WGH
         CLC   QFZONES,QFZONE IS IT ALL NUMERIC                   WGH
         BNE   NOTNUM         NO... EXIT                          WGH
         EX    R3,PACK        PACK THE NUMBER                     WGH
         CVB   R15,SCRATCH    LOAD THE COUNT                      WGH
         A     R15,FRSTLINE   ADD TO START FOR LAST LINE          WGH
         S     R15,=F'1'      ADJUST TO GET CORRECT COUNT         WGH
         ST    R15,LASTLINE   SAVE IT                             WGH
         BR    R8             RETURN                              WGH
         SPACE 2
DATA     MVC   0(5,R1),=C'.DATA' LAST PART OF DSNAME
         LA    R1,5(R2)       LENGTH OF DSNAME
         STH   R1,DSNAME      STORE LENGTH OF DSNAME
         MVC   DA08DDN(72),ALCLIST REMAINDER OF PARM LIST
         BR    R8             RETURN                              WGH
         SPACE 2
DSALLOC  MVI   DAIRFLAG,X'08' INDICATE ALLOC FUNCTION
         L     R15,=V(ALLOCATE) ADDR OF DAIR MODULE
         BALR  R14,R15        GO TO IT
         MVC   QCOUT+36(1),QCRECFM MOVE IN RECORD FORMAT
         NI    QCOUT+36,X'06' TURN OFF EVERTHING BUT CCTL
         OI    QCOUT+36,X'90' SPECIFY FIXED BLOCKED RECORDS
         LH    R1,QCLRECL     RECORD LENGTH
         LTR   R1,R1          IS THE LRECL ZERO?
         BNZ   RECOK          NO. SKIP.
         LA    R1,133         YES. USE LRECL 133.
RECOK    STH   R1,QCOUT+82    STORE LRECL
BIGGER   LR    R2,R1          UPDATE BLKSIZE
         AH    R1,QCOUT+82    ADD LRECL TO BLKSIZE
*        CH    R1,=H'4096'    IS THE BLKSIZE GREATER THAN 4096?
         CH    R1,=H'6233'    IS THE BLKSIZE GREATER THAN 6233?  WGH
         BL    BIGGER         NO. MAKE IT BIGGER.
         STH   R2,QCOUT+62    STORE BLKSIZE
         OPEN  MF=(E,QCOPEN)
***********************************************************************
*                                                                     *
*   REPOSITION DATASET TO TOP                                         *
*                                                                     *
***********************************************************************
         L     R4,QCSTART     TOP OF DATASET POINTER
         L     R4,0(R4)       DISK ADDR TOP OF DATASET
         L     R5,QCBLKA      IOAREA ADDRESS
         B     FIRST          GO DO IT
***********************************************************************
*                                                                     *
*   PROCESS DATASET                                                   *
*                                                                     *
***********************************************************************
NEXTBLK  L     R4,HDBNXTRK-BUFSTART(R5)   DISK ADDR NEXT BLOCK   WGH42
FIRST    LTR   R4,R4          IS THE DISK ADDR ZERO?
         BZ    END            YES. END OF DATASET.
         BAL   R8,READ        READ A BLOCK
         CLC   QPJOBID,HDBJBKEY-BUFSTART(R5)   JOBID MATCH?     WGH42
         BNE   END              NO.. END OF DATASET             WGH42
         CLC   QPDSID,HDBDSKEY-BUFSTART+2(R5)   DSID MATCH?     WGH42
         BE    IDISOK              BR IF SO                          CL
         L     R4,QCJQEA           ADDR OF JQE                       CL
         TM    JQEFLAG3-JQE(R4),JQE3SYSD END OF DATA SET             CL
         BNO   END                    IF NOT SYSTEM JQE              CL
IDISOK   LA    R4,HDBSTART-BUFSTART(R5)  FIRST RECORD IN BLOCK   WGH42
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
NEXTREC  CLI   0(R4),X'FF'    IS LENGTH BYTE FF?
         BE    NEXTBLK        YES. END OF BLOCK.
         TM    1(R4),X'10'    IS THIS A SPANNED RECORD?
         BO    SPAN           YES. SKIP IT.
         SR    R6,R6          ZERO OUT REG
         IC    R6,0(R4)       INSERT LENGTH
         TM    1(R4),X'80'    IS CCTL SPECIFIED?
         BZ    NOCCTL         NO. SKIP.
         LA    R6,1(R6)       INCREMENT LENGTH FOR CCTL
NOCCTL   TM    1(R4),X'08'    IS THIS RECORD TO BE IGNORED?
         BO    SKIPREC        YES. SKIP IT.
         MVI   BUFFER,C' '    BLANK FIRST BYTE OF BUFFER
         MVC   BUFFER+1(255),BUFFER BLANK THE BUFFER
         LR    R7,R6          DO NOT DESTROY R6
         SH    R7,=H'1'       IS LENGTH ZERO?
         BM    SKIPREC        YES. SKIP RECORD.
         EX    R7,MVCREC      MOVE RECORD TO BUFFER
         L     R1,LINECNT     GET CURRENT LINE COUNT            WGH
         LA    R1,1(R1)       ADD THIS LINE TO IT               WGH
         ST    R1,LINECNT     SAVE IT                           WGH
         C     R1,LASTLINE    IS IT GREATER THAN LAST REQUESTED WGH
         BH    END            YES... CLOSE IT                   WGH
         C     R1,FRSTLINE    IS IT LESS THAN 1ST REQUESTED?    WGH
         BL    SKIPREC        YES... SKIP IT                    WGH
         PUT   QCOUT,BUFFER
SKIPREC  LA    R4,3(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPAN     LH    R6,2(R4)       SEGMENT LENGTH
         TM    1(R4),X'08'    IS THIS THE FIRST SEGMENT?
         BO    SPANFRST       YES. USE LARGER HEADER SIZE.
         LA    R4,4(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPANFRST LA    R4,6(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
***********************************************************************
*                                                                     *
*   CLOSE UP SHOP AND GO HOME                                         *
*                                                                     *
***********************************************************************
END      CLOSE MF=(E,QCOPEN)
         MVI   DAIRFLAG,X'18' INDICATE FREE FUNCTION
         L     R15,=V(ALLOCATE) ADDR OF DAIR MODULE
         BALR  R14,R15        GO TO IT
         L     R15,=V(LISTDS) GO BACK TO LISTDS
         BALR  R14,R15        GO TO IT
         QSTOP
***********************************************************************
*                                                                     *
*   READ A BLOCK FROM HASPACE                                         *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK      STORE DISK ADDR
         LR    R1,R5          IOAREA ADDRESS
         L     R15,=V(READSPC) ADDR OF ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         BR    R8             RETURN TO CALLER
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
TILT1    QTILT '*** YOU ARE NOT PROCESSING A VALID DATASET ***'
NOTNUM   QTILT '*** INVALID RANGE FIELD SPECIFIED ***'
         LTORG
MVCREC   MVC   BUFFER(1),3(R4)
PACK     PACK  SCRATCH,0(1,R14)    PACK OPERAND                   WGH
MVZ      MVZ   QFZONES(1),0(R14)   MOVE ZONES FROM PARM FIELD     WGH
         DS    0F
ALCLIST  DC    C'HASPSAVE',CL16' ',F'0',F'10',F'50',F'0'
         DC    CL16' ',X'040202B0',F'0',CL8' '
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $JQE                                                        CL
         $HASPEQU
         IFGRPL  ,                                                WGH
         $BUFFER ,                                                WGH
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
WORK     DSECT
         DS    CL80
BUFFER   DS    CL256
DSNAME   DS    H
         DS    CL44
SCRATCH  DS    D                   DOUBLEWORD                     WGH
FRSTLINE DS    F                   FIRST LINE TO SAVE             WGH
LINECNT  DS    F                   CURRENT LINE #                 WGH
LASTLINE DS    F                   LAST LINE TO SAVE              WGH
         END
