/*  ICFLIST -
    LIST ICF CATALOG
    CURRENTLY ONLY NONVSAM AND GDGBASE ENTRIES ARE LISTED
    IF NECESSARY, MODIFY DEVICE TYPE CHECKING TO SUPPORT
    DEVICES IN YOUR SHOP.  SEE LABEL GETDEV.
*/
 DATA _NULL_;
 LENGTH CATNAME $ 44;
 LENGTH ENTTYP  $ 16;
 RETAIN CATNAME CATVOL CATLEN ENTTYP;
 INFILE ICFCAT;
 FILE ICFLIST HEADER=NEWPAGE PRINT;
 INDEX=3;
 IF _N_ = 1 THEN GO TO GETCATNM;
 INPUT @INDEX CELLSIZE PIB2.
       @INDEX+2 CELLTYPE $CHAR1. @;
 IF CELLTYPE = 'A' THEN GO TO NVSAM;
 IF CELLTYPE = 'C' THEN GO TO CLUSTER;
 IF CELLTYPE = 'B' THEN GO TO GDGBASE;
 RETURN; /* NOT INTERESTED IN ANYTHING ELSE */
 /*
    PROCESS NON-VSAM ENTRY
 */
 NVSAM:
 ENTTYP= 'NVSAM';
 INPUT @INDEX+4 NUMVOL PIB2.
       @INDEX+7 NAME $CHAR44. @;
 PUT @2 NAME  @88 ENTTYP @;
 INDEX+CELLSIZE;
 LINK GETVOLS;
 RETURN;
 /*
    PROCESS GDG ENTRY
 */
 GDGBASE:
 ENTTYP = 'GDGBASE';
 INPUT @INDEX+7 NAME $CHAR44. @;
 PUT @2 NAME  @88 ENTTYP @;
 INDEX+CELLSIZE;
 INPUT @INDEX CELLSIZE PIB2. @; /* SKIP OWNER CELL */
 INDEX+CELLSIZE;
 INPUT @INDEX CELLSIZE PIB2.
       @INDEX+4 LIMIT PIB1.
       @INDEX+5 COUNT PIB1. @;
 PUT @38 'GDG: LIMIT = '
         LIMIT Z3.
         ' COUNT = '
         COUNT Z3.;
 DO I = 1 TO COUNT;   /* ONCE FOR EACH GDG MEMBER */
   INDEX + CELLSIZE;
   INPUT @INDEX CELLSIZE PIB2.
         @INDEX+5 GENNUM PIB2.
         @INDEX+7 VERSION PIB1.
         @INDEX+8 NUMVOL PIB2. @;
   PUT @35 'G'
           GENNUM Z4.
           'V'
           VERSION Z2. @;
   INDEX+CELLSIZE;
   LINK GETVOLS;
 END;
 RETURN;
 /*
    PROCESS CLUSTER RECORD - NOT YET IMPLEMENTED
 */
 CLUSTER:
 ENTTYP = 'CLUSTER';
 RETURN;
 /*
    PROCESS DEVICE TYPE FIELD
 */
 GETDEV:
    IF DEVTYP = '3050200B'X THEN DEVICE = '3350';
     ELSE IF DEVTYP = '3010200E'X THEN DEVICE = '3380';
     ELSE IF DEVTYP = '78008080'X THEN DEVICE = '3480';
     ELSE DEVICE = 'TAPE    ';
     RETURN;
 /*
    PROCESS VOLUME SUBCELLS
 */
 GETVOLS:
 INPUT @INDEX CELLSIZE PIB2. @; /* OWNER CELL */
 INDEX+CELLSIZE;
 INPUT @INDEX CELLSIZE PIB2.
       @INDEX+2 CELLTYPE $CHAR1. @;
 IF CELLTYPE = '03'X   /* IF ASSOCIATION CELL, SKIP IT */
    THEN INDEX+CELLSIZE;
  DO  VOL = 1 TO NUMVOL;  /* PROCESS VOLUME CELLS */
   INPUT @INDEX CELLSIZE PIB2.
         @INDEX+4 VOLSER $CHAR6.
         @INDEX+10 DEVTYP $CHAR4.
         @INDEX+20 VOLFLSEQ PIB2. @;
   LINK GETDEV;
   PUT @48 VOLSER
       @56 DEVICE
       @64 DEVTYP HEX8.
       @73 VOL Z3.
       @76 '/'
       @77 VOLFLSEQ Z3.;
   INDEX + CELLSIZE;
  END;
  INDEX = INDEX - CELLSIZE;
  RETURN;
 /*
    PROCESS CATALOG RECORD
 */
 GETCATNM:
 INPUT @INDEX CELLSIZE PIB2.
       @INDEX+2 CELLTYPE $CHAR1.
       @INDEX+7 NULLNAME $CHAR6. @;
 IF ^(CELLTYPE = 'C' AND NULLNAME = '000000000000'X)
    THEN DO;
         PUT 'INPUT DATASET IS NOT AN ICF CATALOG';
         ABORT RETURN 12;
         END;
 DO UNTIL (CELLTYPE = 'D');
    INDEX+CELLSIZE;
    INPUT @INDEX CELLSIZE PIB2.
          @INDEX+2 CELLTYPE $CHAR1. @;
    END;
 INPUT @INDEX+6 CATLEN PIB1.
       @INDEX+7 CATNAME $VARYING44. CATLEN @;
 DO UNTIL (CELLTYPE = '04'X);
    INDEX+CELLSIZE;
    INPUT @INDEX CELLSIZE PIB2.
          @INDEX+2 CELLTYPE $CHAR1. @;
    END;
 INPUT @INDEX+4 CATVOL $CHAR6.;
 RETURN;
 /*
    PAGE HEADING ROUTINE
 */
 NEWPAGE:
   PUT @2 'LISTING OF ICF CATALOG '
           CATNAME $VARYING. CATLEN
           ' ON VOLUME '
           CATVOL //
       @2  'DATA SET NAME .................'
       @48 'VOLUME'
       @56 'DEVICE'
       @64 'DEVTYP'
       @73 'VOLSEQ/FILESEQ'
       @88 'ENTTYP'/;
   RETURN;
