*
* THIS PROGRAM DUMPS ANY VSAM KSDS OR RRDS TO A PHYSICAL SEQUENTIAL
* VARIABLE BLOCKED SPANNED FILE.  ITS PRIMARY PURPOSE IS TO
* DUMP ICF CATALOGS FOR PROCESSING BY SAS.  WHEN WORKING WITH
* A CATALOG, THIS PROGRAM MUST BE AUTHORIZED.  NO AUTHORIZED
* FUNCTIONS ARE USED BY THIS PROGRAM PER SE, BUT ONE MUST BE
* AUTHORIZED TO OPEN AN ICF OR VSAM CATALOG.
*
ICFDUMP  CSECT
         USING *,12
         STM  14,12,12(13)
         LR   12,15
         ST   13,SAVE+4
         LA   15,SAVE
         ST   15,8(13)
         LR   13,15
*
         OPEN (VSAMACB,,OUTFILE,OUTPUT)
         GETMAIN RU,LV=32768,SP=0,BNDRY=PAGE
         LR   2,1
         ST   2,RECADDR
         LA   3,4(2)            VSAM RECORD START
         MODCB RPL=VSAMRPL,AREA=(3)
NEXTREC  GET  RPL=VSAMRPL
SHOWRPL  SHOWCB RPL=VSAMRPL,AREA=RECLEN,LENGTH=4,FIELDS=RECLEN
         LTR  15,15
         BZ   PUTREC
         ABEND 2,DUMP
PUTREC   L    3,RECLEN
         LA   3,4(3)            ADD PREFIX LENGTH
         STH  3,0(2)            STORE RECORD LENGTH
         XC   2(2,2),2(2)       CLEAR SECOND HALFWORD
         PUT  OUTFILE,(2)
         B    NEXTREC
EOF      CLOSE (VSAMACB,,OUTFILE)
         L    13,SAVE+4
         LM   14,12,12(13)
         SR   15,15
         BR   14
*
VSAMACB  ACB  AM=VSAM,DDNAME=VSAMDD,MACRF=(KEY,SEQ,IN),EXLST=EXITS
EXITS    EXLST EODAD=EOF
VSAMRPL  RPL  ACB=VSAMACB,AREALEN=32760,AM=VSAM,                       X
              OPTCD=(KEY,SEQ,SYN,MVE)
OUTFILE  DCB  DDNAME=OUTFILE,DSORG=PS,MACRF=PM
RECLEN   DC   F'0'
RECADDR  DC   A(0)
SAVE     DC   18F'0'
         END
