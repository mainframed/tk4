    DCL
       PUTSCRN ENTRY(,BINARY FIXED(15,0),BINARY FIXED(15,0))
               OPTIONS(ASM,INTER),
       GETSCRN ENTRY(CHAR(*),*,BINARY FIXED(15,0)),
       (TELCOM,PA2) CONDITION EXTERNAL;
    %NOPRINT;
    %DCL (FIELD_NUMBER, VARIABLES_LENGTH,UNPROTECTED_FIELDS,
          REPEAT_NUMBER,FORM_LENGTH) FIXED,
         (FORM_NAME,FIELD_NAME,RESPONSE_VARIABLES,ADDRESSES,
          OFFSETS,LENGTHS,FLAGS,COMMA) CHAR,
         (START_FORM,FIELD,INSERT_CURSOR,REPEAT_TO_ADDRESS,
          ERASE_UNPROTECTED,SET_ADDRESS,FORM_TEXT,END_FORM) ENTRY;
    %FIELD_NUMBER=0;
    %FIELD_NAME='';
    %UNPROTECTED_FIELDS=0;
    %REPEAT_NUMBER=0;
    %VARIABLES_LENGTH=1;
    %FORM_NAME='';
    %FORM_LENGTH=0;
    %RESPONSE_VARIABLES='';
    %ADDRESSES='';
    %OFFSETS='';
    %LENGTHS='';
    %FLAGS='';
    %COMMA='';
 %START_FORM:
    PROC (NAME,ERASE,EAU,ALARM,INHIBIT,MDTS)
         STATEMENT RETURNS(CHAR);
    DCL
       NAME CHAR,
       (ERASE,EAU,ALARM,INHIBIT,MDTS,I) FIXED,
       (CC,WCC,L_CMD) CHAR,
       (PARMSET, SUBSTR) BUILTIN;
    IF FIELD_NUMBER^=0 THEN
       NOTE('FORM STARTED WHILE PREVIOUS FORM STILL PENDING.',12);
    FIELD_NUMBER=0;
    FIELD_NAME='FIELD_';
    UNPROTECTED_FIELDS=0;
    REPEAT_NUMBER=0;
    VARIABLES_LENGTH=1;
    FORM_NAME=NAME;
    RESPONSE_VARIABLES=' DCL 1 '××NAME××'_VARIABLES,';
    ADDRESSES='';
    OFFSETS='';
    LENGTHS='';
    FLAGS='';
    COMMA='';
    IF PARMSET(EAU) THEN
       DO;
          CC='?';
          WCC='';
          L_CMD='2';
       END;
    ELSE
       DO;
          IF PARMSET(ERASE) THEN
             CC='5';
          ELSE
             CC='1';
          IF PARMSET(ALARM) THEN
             I=3;
          ELSE
             I=0;
          IF ^PARMSET(INHIBIT) THEN
             I=I+2;
          IF ^PARMSET(MDTS) THEN
             I=I+1;
          WCC=SUBSTR(' ABCDEFG',I+1,1);
          L_CMD='3';
       END;
    FORM_LENGTH=L_CMD;
    RETURN('DCL 1 '××NAME××' STATIC, '××
     '2 COMMAND CHAR('××L_CMD××') INIT('''××CC××WCC××'''),');
    /*   AN ESCAPE CHARACTER, X'27', PRECEDES THE COMMAND CODE    */
 %END START_FORM;
 %FIELD:
    PROC (R,C,CONTENT,RESP_VAR,L_VAR,FIELD_FLAG,PROT,NUM,SKIP,
          INTENSE,SELECTABLE,SECRET,MDT) STATEMENT RETURNS(CHAR);
    DCL
       (R,C,L_VAR,PROT,NUM,SKIP,INTENSE,SELECTABLE,SECRET,MDT) FIXED,
       (CONTENT,RESP_VAR,FIELD_FLAG) CHAR,
       (FIELD_ID,STRING) CHAR,
       I FIXED;
    FIELD_NUMBER=FIELD_NUMBER+1;
    IF FIELD_NUMBER>36 THEN
       DO;
          FIELD_NAME=FIELD_NAME××'0';
          FIELD_NUMBER=1;
       END;
    FIELD_ID=SUBSTR('ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890',
                    FIELD_NUMBER,1);
    STRING='  2 FIELD_'××FIELD_ID××', ';
    IF PARMSET(R) & PARMSET(C) THEN
       STRING=STRING××SET_ADDRESS(R,C,);
    I=1;
    IF PARMSET(SKIP) THEN
       I=25;
    ELSE
       DO;
          IF PARMSET(PROT) THEN
             I=17;
          IF PARMSET(NUM) THEN
             I=I+8;
       END;
    IF PARMSET(SECRET) THEN
       I=I+6;
    ELSE IF PARMSET(INTENSE) THEN
       I=I+4;
    ELSE IF PARMSET(SELECTABLE) THEN
       I=I+2;
    IF PARMSET(MDT) THEN
       I=I+1;
    STRING=STRING××
           ' 3 ATTRIBUTE CHAR(2) INIT('''××
           SUBSTR(' ADEHI<(&JMNOP*)-/UVYZ%_014589@,',I,1)××'''),';
    /*   THE START FIELD CHARACTER IS X'ID'                       */
    FORM_LENGTH=FORM_LENGTH+2;
    IF I<=16 THEN
       DO;
          IF ^PARMSET(RESP_VAR) THEN
             DO;
                NOTE(
                'RESPONSE VARIABLE PROVIDED FOR UNPROTECTED FIELD.',
                 4);
                RESP_VAR='DEFAULT_'××FIELD_ID;
                L_VAR=1;
             END;
          IF ^PARMSET(FIELD_FLAG) THEN
             FIELD_FLAG='''1''';
          ELSE IF FIELD_FLAG='T' × FIELD_FLAG='TRANSLATE' THEN
             FIELD_FLAG='''2''';
          ELSE IF FIELD_FLAG='RADJ' THEN
             FIELD_FLAG='''3''';
          ELSE
             DO;
                NOTE(
                'INVALID PROCESSING OPTION FOR RETURNED FIELD.',8);
                FIELD_FLAG='''1''';
             END;
          IF PARMSET(SELECTABLE) THEN
             FIELD_FLAG='''4''';
          RESPONSE_VARIABLES=RESPONSE_VARIABLES××' 2 '××RESP_VAR××
                             ',';
          C=C+1;
          ADDRESSES=ADDRESSES××COMMA××''''××BASE64(R,C)××'''';
          OFFSETS=OFFSETS××COMMA××VARIABLES_LENGTH;
          LENGTHS=LENGTHS××COMMA××L_VAR;
          FLAGS=FLAGS××COMMA××FIELD_FLAG;
          VARIABLES_LENGTH=VARIABLES_LENGTH+L_VAR;
          UNPROTECTED_FIELDS=UNPROTECTED_FIELDS+1;
          COMMA=', ';
       END;
    IF PARMSET(CONTENT) THEN
       STRING=STRING××FORM_TEXT(CONTENT);
    RETURN(STRING);
 %END FIELD;
 %REPEAT_TO_ADDRESS:
    PROC (R1,C1,CH,R2,C2,TEXT) STATEMENT RETURNS(CHAR);
    DCL
       (R1,C1,R2,C2) FIXED,
       (CH,TEXT) CHAR,
       STRING CHAR;
    REPEAT_NUMBER=REPEAT_NUMBER+1;
    STRING='2 REPEAT_'××
            SUBSTR('ABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890',
                   REPEAT_NUMBER,1)××',';
    IF PARMSET(R1) & PARMSET(C1) THEN
       STRING=STRING××SET_ADDRESS(R1,C1,);
    STRING=STRING××' 3 WITH CHAR(4) INIT('''××
           BASE64(R2,C2)××CH××'''),';
    /*   THE RA CHARACTER IS X'3C'                                */
    FORM_LENGTH=FORM_LENGTH+4;
    IF PARMSET(TEXT) THEN
       STRING=STRING××FORM_TEXT(TEXT);
    RETURN(STRING);
 %END REPEAT_TO_ADDRESS;
 %INSERT_CURSOR:
    PROC (R,C) STATEMENT RETURNS(CHAR);
    DCL
       (R,C) FIXED,
       STRING CHAR;
    STRING='2 CURSOR,';
    IF PARMSET(R) & PARMSET(C) THEN
       STRING=STRING××SET_ADDRESS(R,C,);
    STRING=STRING××' 3 INSERT CHAR(1) INIT(''''),';
    /*   THE IC CHARACTER IS X'13'                                */
    FORM_LENGTH=FORM_LENGTH+1;
    RETURN(STRING);
 %END INSERT_CURSOR;
 %ERASE_UNPROTECTED:
    PROC (R1,C1,R2,C2,TEXT) STATEMENT RETURNS(CHAR);
    DCL
       (R1,C1,R2,C2) FIXED,
       TEXT CHAR,
       STRING CHAR;
    STRING='2 ERASE,';
    IF PARMSET(R1) & PARMSET(C1) THEN
       STRING=STRING××SET_ADDRESS(R1,C1,);
    STRING=STRING××
           ' 3 WITH CHAR(3) INIT('''××BASE64(R2,C2)××'''),';
    /*   THE EUA CHARACTER IS X'12'                               */
    FORM_LENGTH=FORM_LENGTH+3;
    IF PARMSET(TEXT) THEN
       STRING=STRING××FORM_TEXT(TEXT);
    RETURN(STRING);
 %END ERASE_UNPROTECTED;
 %SET_ADDRESS:
    PROC (R,C,TEXT) STATEMENT RETURNS(CHAR);
    DCL
       (R,C) FIXED,
       TEXT CHAR,
       STRING CHAR;
    STRING=' 3 ADDRESS CHAR(3) INIT('''××BASE64(R,C)××'''),';
    /*   THE SBA CHARACTER IS X'11'                               */
    FORM_LENGTH=FORM_LENGTH+3;
    IF PARMSET(TEXT) THEN
       STRING=STRING××FORM_TEXT(TEXT);
    RETURN(STRING);
 %END SET_ADDRESS;
 %FORM_TEXT:
    PROC (TEXT) RETURNS(CHAR) STATEMENT;
    DCL
       TEXT CHAR,
       L FIXED,
       COUNTER BUILTIN;
    L=LENGTH(TEXT);
    FORM_LENGTH=FORM_LENGTH+L;
    RETURN(
       ' 3 TEXT'××COUNTER××' CHAR('××L××') INIT('''××TEXT××'''),');
 %END FORM_TEXT;
 %BASE64:
    PROC (R,C) RETURNS(CHAR);
    DCL
       (R,C) FIXED,
       (I,J) FIXED,
       (ADDRESS,NUMBERS) CHAR;
    NUMBERS=' ABCDEFGHIÖ.<(+×&JKLMNOPQR!$*);^-/STUVWXYZ|,%'××
            '_>?0123456789:#@''="';
    /*   THE NUMBER BETWEEN Z AND , IS X'6A'                      */
    I=(R-1)*80+C-1;
    J=I/64;
    ADDRESS=SUBSTR(NUMBERS,J+1,1);
    J=I-J*64;
    ADDRESS=ADDRESS××SUBSTR(NUMBERS,J+1,1);
    IF J=61 THEN
       ADDRESS=ADDRESS××'''';
    RETURN(ADDRESS);
 %END BASE64;
 %END_FORM:
    PROC STATEMENT RETURNS(CHAR);
    DCL
       STRING CHAR;
    STRING=' LENGTH_OF_'××FORM_NAME××
           ' BINARY FIXED(15,0) STATIC INIT('××FORM_LENGTH××');';
    IF UNPROTECTED_FIELDS>0 THEN
       DO;
          STRING=STRING××RESPONSE_VARIABLES;
          STRING=STRING××
                 ' RESPONSES_TO_'××FORM_NAME××' CHAR('××
                 VARIABLES_LENGTH-1××') DEFINED '××FORM_NAME××
                 '_VARIABLES;';
          STRING=STRING××
          ' DCL 1 TABLE_FOR_'××FORM_NAME××' STATIC,'××
          '   2 SIZE BINARY FIXED(15,0) INIT('××UNPROTECTED_FIELDS
                    ××'),'××
          '   2 FIELDS('××UNPROTECTED_FIELDS××'),'××
          '      3 ADDRESS CHAR(2) INIT('××ADDRESSES××'),'××
          '      3 OFFSET BINARY FIXED(15,0) INIT('××OFFSETS××'),'××
          '      3 LENGTH BINARY FIXED(15,0) INIT('××LENGTHS××'),'××
          '      3 FLAG CHAR(1) INIT('××FLAGS××');';
       END;
    FIELD_NUMBER=0;
    RETURN(STRING);
 %END END_FORM;
    %PRINT;
