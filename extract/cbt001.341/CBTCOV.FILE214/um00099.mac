//$UWJSISF JOB (U4754,U4754,000000),'5798-DGN - SDSF',CLASS=C,
//     MSGCLASS=A,NOTIFY=$UWJS
/*JOBPARM ROOM=35
/*ROUTE PRINT FETCH
//BLOKHEAD EXEC PGM=BLOKHEAD,
//         PARM='/SDSF-1.2/5798-DGN/UM00099 /ISFUSER '
//SYSPRINT  DD  SYSOUT=*,DCB=(RECFM=FBA,LRECL=133,BLKSIZE=1330)
/*
//UM00099  EXEC SMP4PPAP
//SMPCNTL   DD  *
 RESTORE S(UM00099) .
 RECEIVE S(UM00099) .
 APPLY   S(UM00099) ASSEM .
/*
//SMPPTFIN  DD  *
++USERMOD(UM00099)
                               /* A SDSF INSTALLATION-DEPENDENT
                                  EXIT ROUTINE TO PROVIDE SUPPORT
                                  FOR THE TOP-SECRET SECURITY
                                  SYSTEM AS IMPLEMENTED AT SYNTEX
                                  (USA), INC.                     */ .
++VER(Z038) FMID(HDA1102) .
++SRCUPD(ISFUSER) .
./ CHANGE NAME=ISFUSER
         PRINT OFF
         COPY  ISFEQU
         PRINT ON
         EJECT
***********************************************************************
*                                                                     *
*                 SPOOL DISPLAY AND SEARCH FACILITY                   *
*                  5798-DGN COPYRIGHT IBM CORP 1981                   *
*             LICENSED MATERIAL - PROGRAM PROPERTY OF IBM             *
*        REFER TO COPYRIGHT INSTRUCTIONS FORM NUMBER G120-2083        *
*                                                                     *
*              THE AUTHORIZATION CHECK EXIT IS ENTERED KEY ONE AND    *
*        SUPERVISOR IF THE SDSF SVC IF AVAILABLE.  IF NOT AVAILABLE,  *
*        IT IS ENTERED KEY 8 PROBLEM STATE.                           *
*                                                                     *
*              THIS IS THE AUTHORIZATION CHECK USER EXIT FOR SDSF.    *
*        ON ENTRY REGISTER 0 CONTAINS THE FOLLOWING:                  *
*                                                                     *
*              0 - FOR THE INITIALIZATION CALL                        *
*              4 - FOR A COMMAND CHECK CALL                           *
*              8 - FOR A DISPLAY SYSOUT CALL                          *
*             12 - FOR THE TERMINATION CALL                           *
*                                                                     *
*              ON ENTRY REGISTER ONE CONTAINS A PARAMETER LIST        *
*        WHICH IS MAPPED BY THE USERPARM DSECT CONTAINED IN THIS      *
*        MEMBER.  A RETURN CODE OF OTHER THAN ZERO WILL CANCEL THE    *
*        FUNCTION. NOTE THAT IF THE SVC IS AVAILABLE THE PARAMETER    *
*        LIST IS IN KEY ONE STORAGE.                                  *
*               FOR AN INITIALIZATION CALL IF THE SDSFATH BYTE        *
*        IS ZERO AT THE TIME OF CALL, THE USER WILL NOT BE ALLOWED    *
*        TO USE SDSF UNLESS IT IS SET TO NON-ZERO BY EXIT.            *
*               FOR BOTH THE COMMAND CHECK AND DISPLAY SYSOUT CALL    *
*        IF THE AUTHRETC WORD IS NON-ZERO AT TIME OF CALL, THEN       *
*        THE CALL WILL BE FAILED UNLESS THE EXIT ZEROS THE WORD.      *
*               A WORD NAMED USERWORD HAS BEEN PROVIDED FOR THE       *
*        USER TO SAVE THE ADDRESS OF A WORK AREA FOR INFORMATION      *
*        NEEDED FROM ONE CALL TO THE NEXT.                            *
*               THE TERMINATION CALL IS PROVIDED FOR THE USER EXIT    *
*        TO CLEAN UP ANY WORK AREAS GOTTEN DURING PRIOR CALLS.        *
*                                                                     *
***********************************************************************
         SPACE 5
ISFUSER  CSECT
         ISFSAVE
         BALR  R12,0               BASE REGISTER
         USING *,R12
         LR    R11,R1              USER EXIT PARM
         USING USERPARM,R11
         LR    R10,R0              CALL TYPE
         B     CALLTYPE(R10)       GO TO PROPER CODE
CALLTYPE B     INITCK              INITIALIZATION
         B     CMDCK               COMMAND CHECK
         B     DSPCK               DISPLAY SYSOUT CHECK
         B     EXIT                EXIT PROCESSING
         EJECT
***********************************************************************
*                                                                     *
*        INITIALIZATION USER CHECK SECTION                            *
*                                                                     *
***********************************************************************
         SPACE 5
INITCK   DS    0H                  INITIALIZATION USER CHECK
         B     RETURN              NO PROCESSING
         SPACE 5
***********************************************************************
*                                                                     *
*        ADD USER CODE HERE                                           *
*                                                                     *
***********************************************************************
         SPACE 5
         EJECT
***********************************************************************
*                                                                     *
*        OPERATOR COMMAND CHECK SECTION                               *
*                                                                     *
***********************************************************************
         SPACE 5
CMDCK    DS    0H                  OPERATOR COMMAND CHECK
         LA    R0,RAWRKLEN         GET LENGTH OF RACHECK WORK AREA
         ICM   R0,8,SPNUM          SET FOR SUBPOOL 229
         LR    R3,R0               SAVE ADDRESS OF STORAGE
         GETMAIN R,LV=(0)          GET MODIFIED STORAGE
         LR    R10,R1              ADDRESS STORAGE
         USING WORK,R10            POINT TO WORK AREA
         CLC   UJOBNAME,=8X'00'    IS JOB A '/' COMMAND
         BE    SPECCMD             YES, THIS COMMAND IS SPECIAL
         CLI   UACTCHAR,C'T'       IS THIS JES SET COMMAND
         BE    SPECCMD             YES, THIS COMMAND IS SPECIAL
         CLI   UACTCHAR,C'E'       IS THIS MVS RESET COMMAND
         BE    SPECCMD             YES, THIS COMMAND IS SPECIAL
         CLC   UJOBTYP,=C'STC'     IS THIS FOR A STARTED TASK
         BE    SPECCMD             YES, THIS COMMAND IS SPECIAL
***********************************************************************
*                                                                     *
*        USERS EFFECT ONLY THEIR OWN JOBS IF THEY PASS THROUGH        *
*        THE ABOVE FILTERS                                            *
*                                                                     *
***********************************************************************
         MVC   ABSTRENT,=8C' '     CLEAR ABSTRACT NAME
         TRT   UJOBNAME,NSYMTBL    NATIONAL SYMBOL IN JOBNAME?
         BZ    PRODJOB             NO, THIS IS A PRODUCTION JOB
         SPACE 1
**   NON-PRODUCTION JOBS ARE ACCESSIBLE IF OWNED OR X-AUTHORIZED
         SR    R8,R8               CLEAR LENGTH
         IC    R8,SUSERIDL         GET USER ID LENGTH
         BCTR  R8,0                DECREMENT FOR EXECUTE
         LA    R9,UJOBNAME         POINT TO JOB NAME
**   R1 POINTS TO 1ST NATIONAL SYMBOL IN JOBNAME FROM TRT INSTRUCTION
         CR    R9,R1               IS 1ST CHAR NATIONAL SYMBOL?
         BE    TSOSUB              YES, SUBMITTED VIA TSO
**   JOB HAS STANDARD INTERACT NAME - CHECK FOR OWNERSHIP
         BCTR  R8,0                REDUCE LENGTH BY...
         BCTR  R8,0                TWO FOR COMPARE
         EX    R8,COMPINTJ         JOBNAME START WITH WITH USER'S LOGON
         BE    PERMIT              YES, IT IS OWNED
         EX    R8,MOVEINTJ         MOVE USERID FROM JOB TO ABSTRACT
         B     NOTOWNED            NO, MUST CHECK EXTERNAL SECURITY
TSOSUB   DS    0H
**   JOB DOESN'T HAVE INTERACT NAME - CHECK FOR OWNED TSO JOBNAME
         EX    R8,COMPTSOJ         3RD - "NTH" CHARS MATCH LOGON
         BE    PERMIT              YES, IT IS OWNED - PERMIT ACCESS
         BCTR  R8,0                REDUCE LENGTH BY...
         BCTR  R8,0                TWO FOR MOVE
         EX    R8,MOVETSOJ         MOVE USERID FROM JOB TO ABSTRACT
         SPACE 1
NOTOWNED DS    0H
**   USER DOESN'T OWN JOB BUT MAY BE X-AUTHORIZED - ASK TOP SECRET
         MVC   ABSTRCLS,=C'USR'    RESOURCE NAME PREFIX (CLASS)
         B     AUTHCHK             CHECK WITH TOP SECRET
         SPACE 1
**   PRODUCTION JOBS ARE ACCESSIBLE ONLY IF THE FIRST THREE CHARACTERS
**   OF THE JOB NAME CORRESPOND TO AN AUTHORIZED "SYS" ABSTRACT.
         SPACE 1
PRODJOB  DS    0H
         CLI   UACTCHAR,X'00'      IS THIS JUST A SELECT
         BE    PRODJOB2            YES, CHECK FOR 'SYS' PRIV
         MVC   ABSTRCLS,=C'CMD'    SET FOR COMMAND PRIVILEGES
         B     SKIPSYS             AND GO AROUND
PRODJOB2 DS    0H
         MVC   ABSTRCLS,=C'SYS'    RESOURCE NAME PREFIX (CLASS)
SKIPSYS  DS    0H
         MVC   ABSTRSYS,UJOBNAME   3 CHAR APPLICATION NAME
         B     AUTHCHK             CHECK WITH TOP SECRET
**   OPERATOR COMMANDS THAT REQUIRE SPECIAL AUTHORIZATION WILL BE
**   PROCESSED BY TOP SECRET BEFORE BEING PERMITTED
SPECCMD  DS    0H
         MVC   ABSTRENT,=8C' '     CLEAR ABSTRACT NAME
         MVC   ABSTRCLS,=C'CMD'    SET FOR OPERATOR COMMAND
         MVC   ABSTRSYS,=C'001'    HIGH LEVEL AUTHORITY
AUTHCHK  DS    0H
         LA    R8,CLSABSTR         RACF CLASS NAME
         XC    TSSPARM,TSSPARM     ZERO FEEDBACK AREA
         LA    R1,L'TSSPARM        GET LENGTH OF FEEDBACK
         STC   R1,TSSPARML         SAVE LENGTH
         MVC   TSSTSSF,=C'TSSF'    CREATE HEADER
         LA    R4,TSSPARM          FEEDBACK ADDRESS
         LA    R5,ABSTRENT         ABSTRACT RESOURCE TO BE CHECKED
         MVC   RACDEST(RACLEN),RACMODEL MOVE IN MODEL RACHECK MACRO
         RACHECK CLASS=(R8),                                           *
               INSTLN=(R4),                                            *
               ENTITY=((R5)),MF=(E,RACDEST)
**  ACEE ADDRESS NOT NEEDED FOR TSO ADDRESS APACES
         SPACE 1
         LTR   R15,R15             CHECK RETURN CODE
         BZ    PERMIT              LET THEM DO IT
         SPACE 1
DENY     DS    0H
         LR    R0,R3               POINT TO LENGTH AND SUBPOOL
         LR    R1,R10              POINT TO AREA
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE
         B     FAIL                DO NOT PERMIT ACCESS
PERMIT   DS    0H
         LR    R0,R3               POINT TO LENGTH AND SUBPOOL
         LR    R1,R10              POINT TO AREA
         FREEMAIN R,LV=(0),A=(1)   FREE STORAGE
         B     RETURN              ALLOW ACCESS
RACMODEL RACHECK MF=L
COMPINTJ CLC   SUSERID+2(0),UJOBNAME
COMPTSOJ CLC   SUSERID(0),UJOBNAME
MOVEINTJ MVC   ABSTRUSR(0),UJOBNAME
MOVETSOJ MVC   ABSTRUSR(0),UJOBNAME+2
SPNUM    DC    AL1(230)            SDSF SUBPOOL NUMBER
         SPACE 1
CLSABSTR DC    XL1'08'             RACHECK CLASS NAME LENGTH
         DC    CL8'ABSTRACT'       RACHECK CLASS NAME
         SPACE 1
NSYMTBL  DC    256X'00'            NATIONAL SYMBOL TRANSLATE TABLE
NSYMTBLE DS    0H
         ORG   NSYMTBL+C'$'
         DC    C'$'
         ORG   NSYMTBL+C'#'
         DC    C'#'
         ORG   NSYMTBL+C'@'
         DC    C'@'
         ORG   NSYMTBLE
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
*        DISPLAY SYSOUT CHECK SECTION                                 *
*                                                                     *
***********************************************************************
         SPACE 5
DSPCK    DS    0H                  DISPLAY SYSOUT CHECK
         B     CMDCK               SAME RULES AS COMMANDS
         SPACE 5
***********************************************************************
*                                                                     *
*        ADD USER CODE HERE                                           *
*                                                                     *
***********************************************************************
         SPACE 5
         EJECT
***********************************************************************
*                                                                     *
*        EXIT CLEAN-UP SECTION                                        *
*                                                                     *
***********************************************************************
         SPACE 5
EXIT     DS    0H                  EXIT PROCESSING
         B     RETURN              NO PROCESSING
         SPACE 5
***********************************************************************
*                                                                     *
*        ADD USER CODE HERE                                           *
*                                                                     *
***********************************************************************
         SPACE 5
RETURN   RETURN (14,12),T,RC=0     RETURN RETURN CODE 0
FAIL     RETURN (14,12),T,RC=4     RETURN RETURN CODE 4
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        USER EXIT COMMUNICATION AREA                                 *
*                                                                     *
***********************************************************************
         SPACE 5
USERPARM DSECT                     USER EXIT PARM
         ISFUPRM
WORK     DSECT
ABSTRENT DS    0CL8                RACHECK ENTITY FIELD
ABSTRCLS DC    CL3' '              ABSTRACT RESOURCE PREFIX
ABSTRSYS DC    CL3' '              SYNTEX PRODUCTION SYSTEM CODE
         DC    CL2' '              FILLER
         ORG   ABSTRSYS
ABSTRUSR DC    CL5' '              USER NAME
TSSPARM  DS    0CL128              TSS PARM AREA
TSSTSSF  DS    CL4'TSSF'           IDENTIFIER
TSSPARML DS    AL1(128)            LENGTH OF PARM AREA
         DS    CL123               FILLER
RACDEST  RACHECK MF=L
RACLEN   EQU   *-RACDEST           RACHECK MACRO LENGTH
RAWRKLEN EQU   *-ABSTRENT          WORKAREA LENGTH
         END
/*
//
