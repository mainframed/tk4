 /**********************************************************************
 * ABEFI IS CALLED TO INITIALIZE A NEW FILE.  IT IS LINKEDITED        *
 * SEPARATELY FROM THE ABE MAINLINE BECAUSE IT MUST ALTER TWO STATIC  *
 * VARIABLES, AND THEREFORE IS NOT REENTRANT.                          *
 **********************************************************************/
 ABEFI: PROC(PLRECL,PBLKL,PRFM,PTITLE) OPTIONS(MAIN);
   DCL (PLRECL,PBLKL) FIXED BIN(31,0);
   DCL PRFM BIT(8);
   DCL PTITLE CHAR(8);
   DCL (MYLRECL,MYBLKL) FIXED BIN(31,0) STATIC;
   DCL
     VB FILE RECORD OUTPUT
       ENV(VB RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     VBM FILE RECORD OUTPUT
       ENV(VB CTL360 RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     VBA FILE RECORD OUTPUT
       ENV(VB CTLASA RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     FB FILE RECORD OUTPUT
       ENV(FB RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     FBM FILE RECORD OUTPUT
       ENV(FB CTL360 RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     FBA FILE RECORD OUTPUT
       ENV(FB CTLASA RECSIZE(MYLRECL) BLKSIZE(MYBLKL)),
     U FILE RECORD OUTPUT
       ENV(U RECSIZE(MYLRECL) BLKSIZE(MYBLKL));
   DCL PICKFI FILE VARIABLE;
   MYLRECL=PLRECL;
   MYBLKL=PBLKL;
   SELECT(SUBSTR(PRFM,1,2));
     WHEN('01'B) DO; /* RECFM=VB */
       SELECT(SUBSTR(PRFM,6,2));
         WHEN('00'B) PICKFI=VB;
         WHEN('01'B) PICKFI=VBM;
         OTHERWISE   PICKFI=VBA;
         END; /* SELECT */
       END;
     WHEN('10'B) DO; /* RECFM=FB */
       SELECT(SUBSTR(PRFM,6,2));
         WHEN('00'B) PICKFI=FB;
         WHEN('01'B) PICKFI=FBM;
         OTHERWISE   PICKFI=FBA;
         END; /* SELECT */
       END;
     OTHERWISE PICKFI=U; /* RECFM=U */
     END; /* SELECT */
   OPEN FILE(PICKFI) TITLE(PTITLE);
   CLOSE FILE(PICKFI);
   END ABEFI;
