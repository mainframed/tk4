*$.HE ISD GENERALIZED SUBROUTINES - SYSWRIT
*$.HE PLI INTERFACE TO BPAM WRITE MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/1/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSWRIT
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE PROVIDES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE BPAM WRITE MACRO.  IT CAN BE USED TO CREATE A MEMBER OF
*$A PARTITIONED DATA SET, OR TO CHANGE ONE.
*$.SP 1
*$CALL SYSWRIT(DCBPTR,BUFFER);
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$DCBPTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS OF A BPAM DCB.  SET
*$BY A PRIOR CALL TO SYSDCBP.
*$.IN 0
*$BUFFER
*$.IN 5
*$CHARACTER STRING FROM WHICH THE BLOCK IS WRITTEN.
*$.SP 1
*$.IN 0
*$ON THE FIRST CALL TO SYSWRIT, THE ROUTINE WILL ISSUE THE NOTE
*$MACRO AFTER WRITING THE FIRST BLOCK, AND WILL SAVE THE TTR IN
*$A SAVE AREA JUST BEYOND THE DCB.  SYSDCBP SETS UP THIS SAVE
*$AREA ALONG WITH SOME OTHER FIELDS USED FOR DEBUGGING WHEN IT
*$ALLOCATES STORAGE FOR THE DCB.
*$.SP 1
*$.CE 1
*$CONDITIONS
*$.SP 1
*$SYSIOER
*$.IN 5
*$I/O ERROR.  ERROR CONDITION IS RAISED ON NORMAL RETURN FROM ON-UNIT.
*$.IN 0
*$.SP 1
*$.CE 1
*$FUNCTIONS
*$.SP 1
*$SYSERDF
*$.IN 5
*$ENTRY RETURN(PTR) - RETURNS A POINTER TO AN AREA CONTAINING
*$AN ERROR MESSAGE IN THE CASE WHEN THE SYSIOER CONDITION IS RAISED.
*$.IN 0
SYSWRIT  PLIENTRY DSA=112,COMPILE=&SYSPARM
SYSERDFR DXD   A
SYSFILEA DXD   A
         SPACE
         LM    2,4,0(1)
         L     2,0(2)        R2 = A(DCB)
         USING IHADCB,2
         LA    1,DECBLOC      R1 = A(MY DECB)
         MVC   SAVEBLK,6(1)       SAVE BLKSIZE FROM DECB
         L     5,0(3)    R5 = A(BUFFER)
         AIF   ('&SYSPARM' EQ 'F').FOPT1
         TM    6(3),X'80'      IS IT VARYING?
         BC    8,NOTV1 BR IF NOT
         MVC   6(2,1),0(5) SET LEN(BUF) IN DECB
         LA    5,2(5) R5=A(BUF)
         B     BYVAR1
NOTV1    DS    0H
         MVC   6(2,1),4(3) SET LEN(BUF) IN DECB
BYVAR1   DS    0H
         AGO   .BYFOPT1
.FOPT1   ANOP
         MVC   6(2,1),6(3) SET LEN TO WRITE FROM SDV
.BYFOPT1 ANOP
         LA    4,DDNAM SET UP PTR FOR SYSFILE
         AIF   ('&SYSPARM' EQ 'F').FOPT2
         L     4,4(12) R4=A(PRV) FROM TCA
         L     4,0(4)
         AGO   .BYFOPT2
.FOPT2   ANOP
         ST    4,0(12)
.BYFOPT2 ANOP
         ORG   *-2
         DC    QL2(SYSFILEA)
         LA    15,MYSYNAD
         ST    15,DCBSYNAD
         WRITE (1),SF,(2),(5),MF=E
         LA    1,DECBLOC      R1 = A(MY DECB)
         CHECK (1)            ISSUE A WAIT CALL FOR THE WITE
         CLC   TTRSAV,=X'000000'
         BNE   COUNT
         NOTE  (2)
         ST    1,TTRSAV
COUNT    L     4,WRITCNT  BUMP WRITE COUNT FOR DEBUG
         LA    4,1(4)
         ST    4,WRITCNT
         LA    1,DECBLOC
         MVC   6(2,1),SAVEBLK        RESTORE ORIGINAL BLKSIZE
OKEYRETN PLIEXIT
MYSYNAD  DS    0H
         AIF   ('&SYSPARM' NE 'F').BYFOPT3
         SYNADAF ACSMETH=BPAM
         LR    3,13
         L     13,4(13)
         LR    2,1
         LA    0,136
         L     15,=V'IHESADB'
         BALR  14,15
         MVC   0(136,1),0(2)  MOVE MESSAGE TO VDA
         ST    4,0(12)
         ORG   *-2
         DC    QL2(SYSERDFR)
         LR    13,3
         SYNADRLS
.BYFOPT3 ANOP
         PLISIGNL CONDITION,COND=SYSIOER
KILLIT   PLISIGNL ERROR
         B     OKEYRETN
VSYNAD   DC    V(SYSIOER)
         LTORG
SYSIOER  CSECT
         DC    X'07'
         DC    CL7'SYSIOER'
SYSERDF  CSECT
         USING *,15
         SAVE  (1,3),,*
         L     2,0(12)
         ORG   *-2
         DC    QL2(SYSERDFR)
         L     1,0(1)         RESOLVE INDIRECT ADDRESS
         ST    2,0(1)         STORE IN PASSED POINTER
         RETURN (1,3)
DSA      DSECT
SAVEBLK  DS    H
DSALEN EQU *-DSA
         DCBD  DSORG=PO
TTRSAV   DS    CL3
DCREAD   DS    F
WRITCNT  DS    F
DCFIND   DS    H
DCSTOW   DS    H
DDNAM    DS  CL8
DECBLOC  DS    0D
         END
