 /* VSPCLOG */
 VSPCLOG:  PROC OPTIONS(MAIN);
         % INCLUDE SMFDEFI;
         % INCLUDE SMFMVS;
         % INCLUDE SMFVSPC;
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         DCL TIMEOFF ENTRY(FIXED DEC(5,0),FIXED BIN(31,0),
           FIXED BIN(31,0),FIXED BIN(31,0),FIXED DEC(5,0),
           FIXED BIN(31,0));
         DCL CTOHMC ENTRY(FIXED BIN(31,0))
           RETURNS(CHAR(5));
         DCL NEWD FIXED DEC(5,0);
         DCL ARY(50,1440) CHAR(1) INIT((72000)(1)' ');
         DCL CHA CHAR(50) INIT(
           'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789)×@#$%^&*(-=_+');
         DCL CHAI FIXED BIN(15,0) INIT(0);
         PUT EDIT('  UID   TIME LOGOFF ID')(A) PAGE;
         ON ENDFILE(SMFDATA) GO TO EOJ;
         DO FOREVER=1 REPEAT FOREVER;
           DO UNTIL(BIN(TYPE)=48 & VSPCID=6);
             READ FILE(SMFDATA) INTO(SMFREC);
             END;
           B = WRITME/6000;
             /* B = ENDING MINUTE */
           CALL TIMEOFF(WRITDTE,WRITME/6000,-(VSPCCNCT/60),1440,NEWD,A);
             /* A = STARTING MINUTE */
           IF A < B & A > 0
             THEN DO;
               DO C = 1 TO 49 UNTIL(D > B);
                 DO D = A TO B WHILE(ARY(C,D)=' ');
                   END;
                 END;
               CHAI = CHAI + 1;
               IF CHAI > 50
                 THEN CHAI = 1;
               DO D = A TO B;
                 ARY(C,D)=SUBSTR(CHA,CHAI,1);
                 END;
               PUT EDIT(VSPCUID,VSPCCNCT/60,CTOHMC(WRITME),
                 SUBSTR(CHA,CHAI,1))
                 (P'9999999',X(1),P'ZZZ9',X(1),A,X(1),A) SKIP(1);
               END;
           END;
 EOJ:
         DO A = 480 TO 960;
           PUT EDIT(A)(P'9999') SKIP(1);
           DO B = 1 TO 50;
             PUT EDIT(ARY(B,A))(A);
             END;
           END;
         END;
