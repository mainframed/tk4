*$.HE GENERALIZED SUBROUTINES - FINDUCB
*$.HE FINDUCB - OBTAIN A UCB ADDR - PLI
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/17/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  FINDUCBS
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE MAY BE USED TO FIND THE MAIN
*$STORAGE ADDR OF A UCB GIVEN THE DEVICE ADDR.
*$NOTE THAT IF THE UCB CANNOT BE FOUND, THE SECOND
*$WORD WILL CONTAIN X'FFFFFFFF'.
*$.SP 1
*$.NF
*$DCL DEVNAME CHAR(3) INIT('121');
*$DCL DUM FIXED BIN(31,0) BASED(DUMPTR);
*$DUMPTR = ADDR(DEVNAME);
*$DCL DEVPTR PTR;
*$CALL FINDUCB(DUM,DEVPTR);
*$.FI L
*$.SP 1
*$DEVNAME
*$.IN 5
*$THIS 3-BYTE CHARACTER STRING SHOULD CONTAIN THE DEVICE ADDRESS
*$FOR THE UCB TO BE FOUND.
*$.IN 0
*$DEVPTR
*$ON RETURN FROM THIS ROUTINE DEVPTR WILL BE SET TO THE MAIN STORAGE
*$ADDRESS OF THE DESIRED UCB.
******** ***** *****************************************************TGS
FINDUCB  PLIENTRY ,DSA=300,COMPILE=O
         XC    WORKAREA(105),WORKAREA
         LA    R6,WORKAREA
         LA    R7,DEVCLASS
         LA    R8,ADDRUCB
         L     R2,4(R1)                R2 = ADDR 2D PARM WORD      *TGS
         MVC   0(4,R2),=4X'FF'         SET TO X'FF' JUST IN CASE   *TGS
         L     R5,0(R1)                R4 = ADDR DEVADDR           *TGS
         L     R3,X'10'                GET CVT PTR
         L     R3,X'434'(R3)           R3 = CVTUCBSC
LOOP     DS    0H
         STM   R6,R8,PARMLST           SET UP
         OI    PARMLST+8,X'80'           PARM LIST
         LA    R1,PARMLST
         LR    RF,R3                   RF=A(IOSVSUCB)
         BALR  RE,RF                   CALL UCB SCAN ROUTINE IOSVSUCB
         LTR   RF,RF                   TEST RETURN CODE
         BC    7,ALLDONE               GET OUT IF NO MORE UCBS
         L     R4,ADDRUCB
         CLC   13(3,R4),0(R5)          IS IT CORRECT UCB?
         BC    7,LOOP                  BR IF NOT
         ST    R4,0(R2)                STORE UCB ADDR              *TGS
ALLDONE  PLIEXIT                       RESTORE REGS                *TGS
         LTORG                                                     *TGS
DSA      DSECT
         DS    0D
WORKAREA DS    CL100
ADDRUCB  DS    F
DEVCLASS DS    CL1
PARMLST  DS    3F
DSALEN   EQU   *-DSA
         END
