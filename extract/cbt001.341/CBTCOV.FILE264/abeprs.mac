 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABEPRS */
 /**********************************************************************
 *                                                                     *
 * THIS SUBROUTINE PROVIDES A SIMPLIFIED METHOD OF PARSING             *
 * A COMMAND BUFFER.  IF ANY OPERANDS ARE FOUND TO BE INVALID, ABEPRS  *
 * WILL RETURN THE NUMBER OF THE INVALID OPERAND.  OTHERWISE, ABEPRS   *
 * WILL RETURN 0.                                                      *
 *                                                                     *
 **********************************************************************/
 ABEPRS:  PROC(CMDDATA,PARY,QUOTCK,NUMCK) OPTIONS(REENTRANT)
   RETURNS(FIXED BIN(15,0));
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(15,0));
   DCL CMDDATA CHAR(*) VAR;
   DCL PARY(*) CHAR(*) VAR;
     /* PARY(1) W/B SET TO THE 1ST OPERAND, PARY(2) TO THE 2D, ETC. */
   DCL QUOTCK(*) BIT(1);
     /* EACH BIT WILL BE SET TO '1'B BY ABEPRS IF THE CORRESPONDING
     OPERAND IS A QUOTED STRING. */
   DCL NUMCK BIT(*);
     /* EACH BIT CORRESPONDS TO AN OPERAND. THE CALLER SHOULD SET
     EACH BIT TO '1'B IF THAT PARTICULAR OPERAND SHOULD BE INTEGER. */
   DCL DLMNEXT ENTRY(CHAR(*) VAR, FIXED BIN(15,0), FIXED BIN(15,0));
   DCL QSTR ENTRY(CHAR(*) VAR, FIXED BIN(15,0), CHAR(*) VAR,
     FIXED BIN(15,0));

   HALT;
   PARY(*) = '';
   QUOTCK = '0'B;
   L = LENGTH(CMDDATA);
   N=0; /* INDEX TO NEXT AVLBL CHAR */
   DO I=1 TO HBOUND(PARY,1);
     CALL DLMNEXT(CMDDATA,M,N); /* GET NEXT OPERAND */
     OL=N-M; /* OL=OPERAND LENGTH */
     IF OL=0
       THEN LEAVE; /* ALL DONE */
     PARY(I)=SUBSTR(CMDDATA,M,OL);
     IF SUBSTR(PARY(I),1,1)=''''
       THEN DO;
         CALL QSTR(SUBSTR(CMDDATA,M,OL),1,PARY(I),J);
         QUOTCK(I)='1'B;
         END;
     IF SUBSTR(NUMCK,I,1)
       THEN DO;
         J=1;
         IF LENGTH(PARY(I))>1
           THEN SELECT(SUBSTR(PARY(I),1,1));
             WHEN('+','-') J=2;
             OTHERWISE J=1;
             END; /* SELECT */
         IF VERIFY(SUBSTR(PARY(I),J),'0123456789')^=0
          THEN RETURN(I); /* INVALID OPERAND */
         END;
     END;
   RETURN(0);
   END ABEPRS;
