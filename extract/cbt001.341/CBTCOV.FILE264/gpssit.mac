*$.HE GENERALIZED SUBROUTINES - GPSSIT
*$.HE GPSSIT - INITIATOR SELECTION FACILITY
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/6/76
*$AUTHOR:  T. SMITH
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE, GIVEN OPERAND B = JOB CLASS # (1-36), BYTE MATRIX
*$1 CONSISTING OF X ROWS AND Y COLUMNS WHERE X=# INITIATORS, AND THE
*$COLUMNS IN EACH ROW INDICATING THE JOB CLASS NUMBERS PROCESSED BY
*$THAT INITIATOR, AND OPERAND C = HALFWORD PARAMETER PH4 -
*$.SP 1
*$.IN 5
*$- IF PH4 (OPERAND C) = 0, THIS SUBRTNE WILL SEARCH FOR AN AVAILABLE
*$INITIATOR TO PROCESS THE JOB.  IF ONE IS FOUND, PH4 IS SET TO THE
*$INITIATOR NUMBER.  ELSE IT IS LEFT AT 0.
*$.SP 1
*$- IF PH4 (OPERAND C) NE 0, THEN PH4 EQUALS THE NUMBER OF THE
*$INITIATOR THAT HAS JUST FINISHED RUNNING THIS JOB.  THIS SUBRTNE
*$MARKS THE INITIATOR AS BEING AVAILABLE, AND THEN EXAMINES THE
*$INPUT QUEUES TO DETERMINE IF THERE IS A JOB WAITING THAT IS
*$ELIGIBLE FOR SELECTION BY THE INITIATOR JUST MADE AVAILABLE.  IF
*$SO, PH4 IS SET TO THE APPROPRIATE JOB CLASS #.  IF NOT, PH4 IS SET
*$TO ZERO.
         SAVES RETCTL
         TGSREQU
         USING HELPTAB,RA
         L     R9,CTLADR               R9 = A(CONTROL WORDS)
         L     R8,MSB(R9)              R8 = A(BYTE MATRICES)
         LA    R8,24(R8)               R8 = A(BYTE MATRIX 1 SDV)
         L     R7,0(R8)                R7 = A(BYTE MATRIX 1)
         LH    R6,4(R8)                R6 = #ROWS = #INITS
         LH    R5,6(R8)                R5 = #COLUMNS = #JOB CLASSES
         L     R4,COPND                R4 = PH4
         LTR   R4,R4                   IS IT TIME TO FREE INIT?
         BC    7,FREEINIT              BR IF YES
LOOPA    DS    0H
         BAL   RE,SETMSK
         EX    R1,TMINSTR              IS INIT AVAILABLE?
         B     BYTM
TMINSTR  TM    0(RF),X'00'
BYTM     DS    0H
         BC    8,SETLOOPB              BR IF YES
NXTINIT  DS    0H
         BCT   R6,LOOPA                ELSE TRY NEXT INIT
         B     RETCTL                  IF NO MORE INITS, ALL DONE
SETLOOPB DS    0H
         LR    R3,R6                   R3 = ROW#
         BCTR  R3,0                    LESS 1
         SR    R2,R2                   GET SET TO MULTIPLY
         MR    R2,R5
         LA    R3,0(R3,R7)             R3 = A(PROPER ROW)
         SR    R2,R2                   R2 = A(PROPER COLUMN TO START)
LOOPB    DS    0H
         LA    RE,0(R2,R3)             RE=A(NEXT COLUMN)
         CLI   0(RE),X'00'             IS IT END OF CLASSES FOR INIT?
         BC    8,NXTINIT               BR IF YES
         CLC   0(1,RE),BOPND+3         IS CLASS ELIGIBLE FOR INIT?
         BC    8,GOTINIT               BR IF YES
         LA    R2,1(R2)                BUMP COLUMN INDEX
         CR    R2,R5                   HAVE ALL COLUMNS BEEN SCANNED?
         BC    8,NXTINIT               BR IF YES
         B     LOOPB                   ELSE, LOOK AT NXT CLASS
GOTINIT  DS    0H
         EX    R1,OIINSTR              SET INIT IN USE
         B     BYOI
OIINSTR  OI    0(RF),X'00'
BYOI     DS    0H
SETPARM  DS    0H
         LR    R7,R6                   R7 = INIT# TO SET PH4
         LA    R6,4                    R6 = PARM#
         LA    R5,X'10'                X'10' HALFWORD TYPE IND
         SLL   R5,24                   SHIFT TO HI-BYTE
         OR    R6,R5                   OR INTO R6
         L     RF,STPVAL               RF = A(STPVAL RTNE)
         L     RE,4(RD)                GET A(OSA)
         LM    R2,R3,28(RE)            RESTORE R2,R3
         L     RB,64(RE)               RESTORE RB
         LR    RE,R2
         A     RE,K4096
         BALR  R5,RF                   GO SET PH4 W/INIT #
         B     RETCTL
FREEINIT DS    0H
         LR    R6,R4                   R6 = INIT#
         BAL   RE,SETMSK
         LA    R0,X'FF'
         XR    R1,R0                   INVERT MASK
         EX    R1,NIINSTR              SET INIT NOT IN USE
         B     BYNI
NIINSTR  NI    0(RF),X'00'
BYNI     DS    0H
         LR    R3,R6                   R3 = ROW#/INIT#
         BCTR  R3,0                    LESS 1
         SR    R2,R2                   GET SET TO MULTIPLY
         MR    R2,R5
         LA    R3,0(R3,R7)             R3 = A(PROPER ROW)
         SR    R2,R2                   R2 = A(PROPER COLUNM TO START)
         L     R4,QUE(R9)              R4 = A(QUEUES)
LOOPC    DS    0H
         LA    RE,0(R2,R3)             RE = A(NEXT COLUMN)
         CLI   0(RE),X'00'             IS IT END OF CLASSES FOR INIT?
         BC    8,SETZERO               BR IF YES
         SR    R1,R1
         IC    R1,0(RE)                R1 = JOB CLASS # = Q#
         LR    R6,R1                   SO DOES R6
         SLA   R1,5                    MULT BY 32
         LA    R1,0(R4,R1)             R1 = A(PROPER QUEUE)
         SR    R0,R0
         C     R0,20(R1)               ARE THERE ANY JOBS IN Q?
         BC    7,SETPARM               BR IF YES
         LA    R2,1(R2)                BUMP COLUMN INDEX
         CR    R2,R5                   HAVE ALL COLUMNS BEEN SCANNED?
         BC    4,LOOPC                 BR IF NOT
SETZERO  DS    0H
         SR    R6,R6
         B     SETPARM
SETMSK   DS    0H
* RE = RETURN
* R6 = INIT#
* AT RETURN, R1=BIT MASK, RF=BYTE ADDR IN TABLE, R3=GARBAGE
         LR    R2,R6                   R2 = INIT#
         SRDL  R2,3                    R2 = BYTE OFFSET
         LA    RF,INITARRY(R2)         RF = BYTE ADDR
         SRL   R3,29                   R3 = BIT OFFSET
         LA    R1,X'80'
         SRL   R1,0(R3)                R1 = BIT MASK
         BR    RE
INITARRY DC    2F'0'     64 INITS CAN BE MEASURED
K4096    DC    F'4096'
QUE      EQU   1056    OFFSET TO QUEUES
MSB      EQU   1096    OFFSET TO BYTE MATRICES
         LTORG
HELPTAB  DSECT
BOPND    DS    F
COPND    DS    F
DOPND    DS    F
EOPND    DS    F
FOPND    DS    F
GOPND    DS    F
CTLADR   DS    F
STPVAL   EQU   HELPTAB+52
         END
