 /* GOPHER */
 /* THIS TSO COMMAND PROCESSOR WILL PRINT THE QUEUE OF PICTURES WAITING
 FOR A GRAPHICS DEVICE */
   % INCLUDE CPINIT1;
   % @CMP = 'O';
   % INCLUDE CPINIT2;
   @PROC(0,Q(X75826X.TCCSR281.),DEVICE(SER281),DEVADDR());
   DCL LOCINDX CHAR(44) VAR;
   DCL LOCDSN ENTRY;
   DCL TERMCTL ENTRY;
   DCL TRET FIXED BIN(31,0);
   DCL (SCREENLEN,LINESIZE) FIXED BIN(15,0);
    DCL HARDCOPY BIT(1);
   DCL LOCPTR PTR;
   DCL LOCRET FIXED BIN(31,0);
   DCL LOCDSNA CHAR(44) VAR;
   DCL LOCTYPE CHAR(1);
   DCL REC CHAR(80) VAR;
   DCL EOFSW BIT(1);
   ON CONDITION(FAILURE) BEGIN;
     @WRITE('PROCESSING CANNOT CONTINUE UNTIL DATA SET IS FREED');
     GOTO EOJ;
     END;
   IF DEVICE=0
     THEN @SETL(&DEVI='');
     ELSE @SETL(&DEVI='DEVICE(&DEVICE)');
   IF DEVADDR=0
     THEN @SETL(&DEVA='');
     ELSE @SETL(&DEVA='DEVADDR(&DEVADDR)');
   CALL TERMCTL('GTSIZE',TRET,SCREENLEN,LINESIZE);
   IF SCREENLEN=0
     THEN HARDCOPY='1'B;
     ELSE HARDCOPY='0'B;
   ON ENDFILE(IN) EOFSW='1'B;
   LOCINDX=SYMC('&Q');
   LOCPTR=NULL;
   CALL LOCDSN(LOCPTR,LOCRET,LOCINDX,LOCDSNA,LOCTYPE);
   IF LOCRET^=0
     THEN DO;
       @WRITE('NO GRAPHS TO PLOT RIGHT NOW');
       RETURN;
       END;
   @SETL(&GO='');
   DO UNTIL(LOCPTR=NULL);
     CALL LOCDSN(LOCPTR,LOCRET,LOCINDX,LOCDSNA,LOCTYPE);
     IF HARDCOPY
       THEN @CMD('FORMFED'); /* SKIP TO NEW PAGE ON THE TI 820 */
       ELSE @CMD('CLEARSC'); /* CLEAR CRT SCREEN */
     @WRITE(' ');
     @SETL(&DSNAME=LOCDSNA);
     @CMD('ALLOC FI(IN) DA(''&DSNAME'') SHR REUSE');
     IF SYMA('&LASTCC')^=0
       THEN SIGNAL CONDITION(FAILURE);
     EOFSW='0'B;
     READ FILE(IN) INTO(REC);
     DO WHILE(^EOFSW);
       @WRITE(REC,MF=S);
       SELECT(SUBSTR(REC,1,3));
         WHEN('LIB') @SETL(&LIB=SUBSTR(REC,5));
         WHEN('MEM') @SETL(&MEM=SUBSTR(REC,5));
         WHEN('PIC') @SETL(&PIC=SUBSTR(REC,5));
         OTHERWISE;
         END;
       READ FILE(IN) INTO(REC);
       END;
     CLOSE FILE(IN);
     @CMD('ALLOC DA(''&LIB'') SHR REUSE');
     IF SYMA('&LASTCC')^=0
       THEN SIGNAL CONDITION(FAILURE);
     IF SYMC('&PIC')=''
       THEN @CMD('GREPLAY &MEM LIB(''&LIB'') &GO &DEVI &DEVA');
       ELSE
         @CMD('GREPLAY &MEM LIB(''&LIB'') PIC(&PIC) &GO &DEVI &DEVA');
     @CMD('BIGIDLE PROMPT(GOPHER)');
     @SETL(&GO='GO');
     @CMD('FREE FI(IN)');
     @CMD('DELETE ''&DSNAME''');
     END;
   @WRITE('GOPHER IS DONE - QUEUE IS EMPTY');
 EOJ:
   END;
