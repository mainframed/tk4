* THIS IS THE SOURCE FOR A TSO COMMAND PROCESSOR TO ALLOW THE USER
* TO EXECUTE COMMAND PROCESSORS NOT IN A LNKLST DATA SET, WITHOUT
* HAVING TO RESORT TO THE TEST COMMAND.  PRIOR TO ISSUING THE T
* COMMAND, THE USER MUST HAVE ALLOCATED THE LOAD LIBRARY CONTAINING
* THE DESIRED COMMAND PROCESSOR UNDER THE FILE NAME 'SYSLOAD'.  TO
* EXECUTE THE DESIRED COMMAND, ISSUE THE T COMMAND FOLLOWED BY THE
* DESIRED COMMAND WITH ALL OF ITS NECESSARY PARAMETERS.  FOR EXAMPLE,
* "T LISTDS LEV(MYID) VOL" MIGHT BE USED TO TEST A NEW VERSION OF THE
* LISTDS COMMAND.
T        CSECT
         SAVER RETCTL,LV=DSALEN,SP=1
         TGSREQU
         USING DSA,RD
         LR    RB,R1
         USING CPPL,RB
         L     R2,CPPLCBUF             R2=A(CBUF)
         LH    R3,0(R2)                R3=LEN(CBUF)
         LH    R4,2(R2)                R4=OFFSET TO DATA
         LA    RF,0(R3)
         SR    RF,R4                   RF=NEW LEN
         STH   RF,0(R2)                STORE NEW LEN
         SH    RF,=H'5'                RF=LEN TO MOVE FOR EX
         LA    RE,4(R2,R4)             RE=A(TO MOVE FROM)
         EX    RF,MVCMD
         B     BYMV
MVCMD    MVC   4(0,R2),0(RE)           SHIFT COMMAND TO LEFT
BYMV     DS    0H
         LA    RF,0
         STH   RF,2(R2)                RESET OFFSET
         LA    R9,MYCSPL               R9=A(CSPL)
         USING CSPL,R9
         MVC   CSPLUPT,CPPLUPT
         MVC   CSPLECT,CPPLECT
         XC    MYECB,MYECB
         LA    R1,MYECB
         ST    R1,CSPLECB
         XC    FLGWD,FLGWD
         OI    FLGWD,X'80'             SYNTAX CMD NAME
         LA    R1,FLGWD
         ST    R1,CSPLFLG
         LA    R8,MYCSOA
         USING CSOA,R8
         ST    R8,CSPLOA
         MVC   CSPLCBUF,CPPLCBUF
         LA    R1,CSPL
         LINK  EP=IKJSCAN
         LTR   RF,RF
         BC    7,BADEXIT               BR IF BAD SCAN
         TM    CSOAFLG,CSOAVWP+CSOAVNP IS COMMAND GOOD?
         BC    8,BADEXIT               BR IF NOT
         LA    RA,DSAPPL
         USING PPL,RA
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R1,MYECB
         ST    R1,PPLECB
         MVC   DSACON,LIBDCB
         LA    R2,DSADCB
         LA    R1,DSAOPEN
         OPEN  ((R2)),MF=(E,(1))
         TM    DSADCB+48,X'10'         WAS OPEN SUCCESSFUL?
         BC    1,OPNOK                 BR IF YES
         LA    R6,BADOPN
         B     PUTL
OPNOK    DS    0H
         MVC   PGM(8),=CL8' '
         LH    R1,CSOALNM              R1=LEN(CMD NAME)
         BCTR  R1,0                    LESS 1 FOR EX
         L     R2,CSOACNM              R2=A(COMMAND NAME)
         EX    R1,MVCPGM               MOVE NAME TO BLDL LIST
         B     SETECT
MVCPGM   MVC   PGM(0),0(R2)
BLDL     DS    0H
SETECT   DS    0H
         L     R6,CPPLECT
         USING ECT,R6
         TM    CSOAFLG,CSOAVNP         ARE THERE OPERANDS?
         BC    1,GOTOPER               BR IF YES
         OI    ECTSWS,ECTNOPD          INDICATE NO OPERANDS
         B     SETPGM
GOTOPER  DS    0H
         NI    ECTSWS,255-ECTNOPD      INDICATE OPERANDS
SETPGM   DS    0H
         MVC   ECTPCMD,PGM             PUT COMMAND NAME IN ECT
         LA    R3,DSADCB
         LA    RF,DSAATTCH
         LA    R1,CPPL
         LA    R2,PGM
         XC    MYECB,MYECB
         LA    R4,MYECB
         ATTACH EPLOC=(R2),DCB=(R3),TASKLIB=(R3),ECB=(R4),             C
               SHSPL=SHSPL78,MF=(E,(1)),SF=(E,(15))
         ST    R1,SUBTCB
         LA    R1,MYECB
         WAIT  ECB=(R1)
         LA    R1,SUBTCB
         DETACH (R1)
DOCLOSE  DS    0H
         LA    R1,DSACLOSE
         LA    R2,DSADCB
         CLOSE ((R2)),MF=(E,(1))
         L     RF,MYECB
         LA    RF,0(RF)
         L     R1,4(RD)
         ST    RF,16(R1)
         B     RETCTL
BADEXIT  DS    0H
         LA    R6,ERRMSG
PUTL     DS    0H
         SR    R0,R0
         ST    R0,MYECB
         LA    R1,DSAPPL
         PUTLINE PARM=PTPB,                                            C
               OUTPUT=((6),TERM,SINGLE,DATA),                          C
               MF=(E,(1))
         TM    DSADCB+48,X'10' WAS OPEN OK?
         BC    1,DOCLOSE
         B     RETCTL
         DS    0F
SHSPL78  DC    AL1(1,78)
         DS    0F
LIBDCB   DCB   DDNAME=SYSLOAD,DSORG=PO,MACRF=(R)
LIBDCBL  EQU   *-LIBDCB
         DS    0F
ATTCHLC  ATTACH DE=0,SF=L
ATTCHLL  EQU   *-ATTCHLC
         DS    0F
OPEN     OPEN  (,(INPUT)),MF=L
OPENL    EQU   *-OPEN
         DS    0F
CLOSE    CLOSE (,),MF=L
CLOSEL   EQU   *-CLOSE
         DS    0F
PTPB     PUTLINE ,MF=L
PTPBL    EQU   *-PTPB
BIGL     EQU   *-LIBDCB
BADOPN   DS    0F
         DC    AL2(BOL-*),AL2(0)
         DC    C'UNABLE TO OPEN SYSLOAD DATA SET'
BOL      EQU   *
ERRMSG   DS    0F
         DC    AL2(ERE-*),AL2(0)
         DC    C'ERROR EXIT'
ERE      EQU   *
         LTORG
         IKJCPPL
         IKJCSPL
CSPLLEN  EQU   *-CSPL
         IKJCSOA
CSOALEN  EQU   *-CSOA
         IKJPPL
PPLLEN   EQU   *-PPL
         IKJECT
DSA      DSECT
         DS    CL72 PAD FOR STD SAVE AREA
MYCSPL   DS    CL(CSPLLEN)
MYECB    DS    F
FLGWD    DS    F
MYCSOA   DS    CL(CSOALEN)
PGM      DS    CL8
SUBTCB   DS    F
DSACON   DS    0CL(BIGL)
DSADCB   DS    CL(LIBDCBL)
         DS    0F
DSAATTCH DS    CL(ATTCHLL)
         DS    0F
DSAOPEN  DS    CL(OPENL)
         DS    0F
DSACLOSE DS    CL(CLOSEL)
         DS    0F
DSAPTPB  DS    CL(PTPBL)
         DS    0F
DSAPPL   DS    CL(PPLLEN)
DSALEN   EQU   *-DSA
         END
