 BIGIDLB:  PROC(MSO,IO,CPU,SMFEXCP);
         DCL (MSO,IO,CPU,SMFEXCP) FIXED BIN(31,0);
         DCL PSAPTR PTR,
             PSAPTA FIXED BIN(31,0) DEF PSAPTR;
         PSAPTA = 0;
         DCL 1 PSA UNALIGNED BASED(PSAPTR),
              2 PSAPAD1 CHAR(16),
              2 CVTPTR PTR,
              2 PSAPAD2 CHAR(524),
              2 PSAAOLD PTR;
         DCL 1 ASCB UNALIGNED BASED(PSAAOLD),
              2 ASCBASCB CHAR(4),
              2 ASCBPAD1 CHAR(36),
              2 ASCBIOSM BIT(16), /* I/O SERVICE MEASURE */
              2 ASCBPAD2 CHAR(22),
              2 ASCBEJST BIT(64),
              2 ASCBPAD3 CHAR(72),
              2 ASCBOUCB PTR,
              2 ASCBOUXB PTR;
         DCL 1 CVT UNALIGNED BASED(CVTPTR),
              2 CVTPAD1 CHAR(604),
              2 CVTOPCTP PTR;
         DCL 1 RMCT UNALIGNED BASED(CVTOPCTP),
              2 RMCTRMCT CHAR(4),
              2 RMCTPAD1 CHAR(20),
              2 RMCTWMST PTR,
              2 RMCTPAD2 CHAR(36),
              2 RMCTADJC FIXED BIN(31,0);
                   /* THIS IS AMOUNT OF TIME IT TAKES TO
                      EXECUTE 10,000 INSTRUCTIONS */
         DCL 1 WMST UNALIGNED BASED(RMCTWMST),
              2 WMSTWMST CHAR(4),
              2 WMSTPAD1 CHAR(64),
              2 WMSTCPU FIXED BIN(31,0),
                   /* CPU SERVICE COEF. */
              2 WMSTIOC FIXED BIN(31,0),
                   /* I/O SERVICE COEF. */
              2 WMSTMSO FIXED BIN(31,0);
                   /* MSO SERVICE COEF. */
         DCL 1 OUCB UNALIGNED BASED(ASCBOUCB),
              2 OUCBNAME CHAR(4),
              2 OUCBPAD1 CHAR(104),
              2 OUCBIOSM FIXED BIN(31,0),
              2 OUCBPSS BIT(64);
                   /* CPU PAGE SECONDS */
         SMFEXCP = OUCBIOSM;
 /* ------------------------------------------------------------------
  * TOTAL SERVICE = ((MP)/K) + ((CT)/K) + EI
  * T = JOB STEP ELAPSED CPU TIME
  * K = TIME REQUIRED TO EXECUTE 10,000 INSTRUCTIONS.
  * M = MSO SERVICE COEFFICIENT SCALED BY 1/50.
  * P = NUMBER OF PAGE SECONDS USED BY THE USER
  * C = CPU SERVICE COEFFICIENT
  * E = EXCP COUNT FOR THIS INTERVAL
  * I = I/O SERVICE COEFFICIENT
  *
  * T = ASCBEJST - CONTAINS CUMULATIVE USER JOB STEP TIME.  A
  *                HEXADECIMAL POINT IS ASSUMED 3 PLACES TO THE LEFT.
  * K = RMCTADJC - TIME REQUIRED TO EXECUTE 10K INSTRUCTIONS.
  *                HAS ONE HEX PLACE.
  * M = WMSTMSO  - MSO SERVICE COEFFICIENT, SCALED BY 1/50.
  * P = OUCBPSS  - CONTAINS THE CURRENT USER PAGE-SECONDS.  A HEX
  *                POINT IS ASSUMED 0 PLACES TO THE LEFT.
  * C = WMSTCPU  - CPU SERVICE COEFFICIENT.
  * E = ASCBIOSM - CONTAINS THE CUMULATIVE USER I/O REQUEST COUNT
  * I = WMSTIOC  - CONTAINS THE I/O SERVICE COEFFICIENT.
  *
  * -------------------------------------------------------------------
  */
         DCL BF BIN FLOAT(64);
              /* FOLLOWING COMPUTES (CT)/K = CPU SERVICE */
         BF = ASCBEJST;
         BF = BF / RMCTADJC;
              /* BF NOW CONTAINS T/K TO 2 HEX PLACES */
         BF = BF * WMSTCPU;
              /* BF NOW CONTAINS CT/K TO 5 HEX PLACES */
         BF = BF / 1048576;
              /* THE ABOVE STMT ELIMINATES HEX PLACES */
         CPU = BF; /* STORE RESULT */
              /* FOLLOWING COMPUTES (MP)/K = MSO SERVICE */
         BF = OUCBPSS;
         BF = BF * 1024;
              /* SCALE FOR DIVIDE.  P TO 0 HEX PLACES
                 IN MICRO-SECONDS */
         BF = BF / RMCTADJC;
              /* BF NOW CONTAINS P/K TO -1 HEX PLACES */
         BF = BF * WMSTMSO;
              /* BF NOW CONTAINS (MP)/K TO 2 HEX PLACES */
         BF = BF / 256;
              /* THE ABOVE STMT ELIMINATES HEX PLACES */
         MSO = BF;  /* STORE RESULT */
              /* FOLLOWING COMPUTES (EI) = IO SERVICE */
         BF = OUCBIOSM;
         BF = BF * WMSTIOC;
         BF = BF / 4096; /* ELIMINATES HEX PLACES */
         IO = BF;  /* STORE RESULT */
         END BIGIDLB;
