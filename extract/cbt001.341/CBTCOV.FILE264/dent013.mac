 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENT013 - TAB,OW,BRIGHT SUBCOMMANDS */
   % DCL @PROCNAME CHAR;
   % DCL @RENTNAME CHAR;
   % @PROCNAME = 'RENT';
   % @RENTNAME = 'DENT013';
   % INCLUDE DENTSUB;
   % INCLUDE IKJECT;
   DCL SETCAPS ENTRY;
   DCL STFSMOD ENTRY(CHAR(*),CHAR(*),FIXED BIN(15,0))
     RETURNS(FIXED BIN(15,0));
   DCL STELP PTR BASED(STPTR);
   DCL STELSAV PTR;
   DCL CMDSTR CHAR(240) VAR INIT('');
   DCL PROMPT CHAR(80) VAR STATIC INIT('OWP');
   DCL (HIP,TP) PTR;
   DCL PARM CHAR(100) VAR;
   DCL HLEN FIXED BIN(15,0) BASED(ADDR(PARM));
   DCL SYSLINK ENTRY;
   DCL DUMSTR CHAR(1);
   RHRC=0;
   SELECT(CMDINDX);
     WHEN(1,2) DO; /* IT IS TAB SUBCOMMAND */
       CALL #TABSET(RHPPTR,PARY(*),SUBENT);/*CALL TABSET*/
       RHRC = 0;
       END;
     WHEN(4) CALL BRIGHT; /* IT IS BRIGHT SUBCOMMAND */
     OTHERWISE DO; /* IT IS OW SUBCOMMAND */
       IF PARY(2)^=''
         THEN RHPFRACT=PARY(2);
       STELSAV=RHECT->ECTIOWA->STELP;
       J=STFSMOD('OFF','INITIAL=NO',0);
       IF PARY(2)=''
         THEN PARM='ST';
         ELSE PUT STRING(PARM) EDIT('SRC',PSCREENLEN,PLINESIZE)
           (A(3),P'999',P'999');
       CALL SYSLINK('CLRSCRN',RHRC,HLEN);
       CALL SUBENT(34)(RHPPTR); /* CALL DENTDIS */
       CALL #CRTZAP(RHPPTR); /* CALL CRTZAP */
       DUMSTR=#PROMPT('HIT ENTER TO CONTINUE');
       HIP=NULL;
       I=0;
       J=SCREENLEN-4;
       DO TP=NXTRHEL REPEAT TP->RHNXT WHILE(TP^=NULL);
         IF TP->RHCEN>J
           THEN IF TP->RHCST>J
             THEN TP->RHCST,TP->RHCEN=0;
             ELSE TP->RHCEN=J;
         IF TP->RHCST^=0
           THEN HIP=TP;
         END;
       IF HIP^=NULL
         THEN HIP->RHCEN=J;
       END;
     END;
 BRIGHT: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE SUBCOMMAND IS BRIGHT.                               *
 *                                                                     *
 **********************************************************************/
   %INCLUDE DENTPNL;
   DCL (I,J,K,L) FIXED BIN(15,0);
   DCL SAVPTR PTR;
   DCL (ALLSW,HITSW) BIT(1) INIT('0'B);
   CALL SETCAPS(PARY(2));
   IF PARY(2)^=''
     THEN IF PARY(2)^='HI' & PARY(2)^='LO' & PARY(2)^='SU'
       THEN DO;
         MSGDATA='INVALID OPERAND = '××PARY(2);
         RHRC=8;
         RETURN;
         END;
   IF PARY(3)^=''
     THEN DO;
       CALL SETCAPS(PARY(3));
       IF PARY(3)^='ALL' & PARY(3)^='DATA' & PARY(3)^='HEADINGS'
         THEN DO;
           MSGDATA='INVALID OPERAND = '××PARY(3);
           RHRC=8;
           RETURN;
           END;
       ALLSW='1'B;
       END;
   L=@CURLOC;
   PNLPTR=RHPSTAPT;
   SFLDPTR,SAVPTR=ADDR(FLDLOC);
   LUP: DO;
     DO J=1 TO FLDCNT;
       IF BRIGHT_HIT(SAVPTR,J,FLDSBAB)
         THEN DO;
           CALL BRIGHT_SUB(SAVPTR);
           IF PARY(3)=''
             THEN LEAVE LUP;
           END;
       SAVPTR=SFLDPTR;
       SFLDADJ=SFLDADJ+FLDLEN; /* BUMP TO NEXT FIELD */
       END;
     IF BRIGHT_HIT(SAVPTR,J,32760)
       THEN CALL BRIGHT_SUB(SAVPTR);
     END LUP;

 BRIGHT_HIT: PROC(SAVPTR,J,NXTSBAB) RETURNS(BIT(1));
   DCL SAVPTR PTR;
   DCL J FIXED BIN(15,0);
   DCL HITSW BIT(1) INIT('0'B);
   SELECT;
     WHEN(PARY(3)='ALL') HITSW='1'B;
     WHEN(PARY(3)='DATA')
       IF SUBSTR(SAVPTR->FLAGS,3,1)='0'B
         THEN HITSW='1'B;
     WHEN(PARY(3)='HEADINGS')
       IF SUBSTR(SAVPTR->FLAGS,3,1)='1'B
         THEN HITSW='1'B;
     WHEN(L<NXTSBAB & L>=SAVPTR->FLDSBAB) HITSW='1'B;
     OTHERWISE HITSW='0'B;
     END; /* SELECT */
   RETURN(HITSW);
   END BRIGHT_HIT;

 BRIGHT_SUB: PROC(SAVPTR);
   DCL SAVPTR PTR;
   SELECT(PARY(2));
     WHEN('','HI') SUBSTR(SAVPTR->FLAGS,5,2)='10'B;
     WHEN('LO')    SUBSTR(SAVPTR->FLAGS,5,2)='01'B;
     OTHERWISE     SUBSTR(SAVPTR->FLAGS,5,2)='11'B;
     END; /* SELECT */
   END BRIGHT_SUB;

   END BRIGHT;

   END;
