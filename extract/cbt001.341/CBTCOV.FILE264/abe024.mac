 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* ABE024 - PFK,NAME,NAMELOC */
   %INCLUDE ABESUB;
 ABESUB(ABE024) PARS(0000000000000000) CHECK(YES);
   %INCLUDE ABESUBA;

   DCL OP CHAR(256) VAR;
   DCL (M,N,OL) FIXED BIN(15,0);
   DCL SUFFIX ENTRY(CHAR(*),CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL TRH PTR;

   N=0;
   CALL #ABEGOP(CMDBUF,M,N,OL,OP); /* GET FIRST OPERAND */
   SELECT(CMDINDX);
     WHEN(1,2) CALL DO_PFK; /* ABE024, PFK */
     WHEN(3) CALL DO_NAME; /* NAME */
     OTHERWISE DO; /* NAMELOC */
       TRH=#RHPLOC(OP,RHHDRPT);
       IF TRH=NULL
         THEN RHRC=4;
         ELSE RHRC=0;
       END;
     END; /* SELECT */

 DO_NAME: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO PROCESS A NAME SUBCOMMAND.                                *
 *                                                                     *
 **********************************************************************/
   IF OP^=''
     THEN DO;
       TRH=#RHPLOC(OP,RHHDRPT);
       IF TRH^=NULL
         THEN DO;
           MSGDATA ='NAME ALREADY IN USE';
           RHRC = 8;
           RETURN;
           END;
       RHNAME=OP;
       END;
     ELSE RHNAME ='====>';
   CALL #ABEGOP(CMDBUF,M,N,OL,OP); /* GET NEXT OPERAND, IF ANY */
   IF OP ^=''
     THEN DO;
       IF OP^=SUBSTR('SUPPORT',1,MIN(7,LENGTH(OP)))
         THEN DO;
           MSGDATA='INVALID OPERAND ='××OP;
           RHRC=8;
           RETURN;
           END;
         ELSE RHSUPSW='1'B;
       END;
   RHRC = 0;
   RETURN;
   END DO_NAME;

 DO_PFK: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO PROCESS A PFK SUBCOMMAND.                                 *
 *                                                                     *
 **********************************************************************/
   DCL PFKDFT(24) CHAR(15) VAR STATIC INIT(
     /* PFK  1 = */ 'LAST',
     /* PFK  2 = */ 'FILL 50',
     /* PFK  3 = */ 'END',
     /* PFK  4 = */ 'ABE {&P1}',
     /* PFK  5 = */ 'F',
     /* PFK  6 = */ 'C',
     /* PFK  7 = */ 'PB',
     /* PFK  8 = */ 'PF',
     /* PFK  9 = */ 'TOP',
     /* PFK 10 = */ 'COL L',
     /* PFK 11 = */ 'COL R',
     /* PFK 12 = */ 'BOTTOM',
     /* PFK 13 = */ '',
     /* PFK 14 = */ '1M 2M DEL',
     /* PFK 15 = */ '4M V',
     /* PFK 16 = */ '1M 2M COPY 3M',
     /* PFK 17 = */ '1M 2M MOVE 3M',
     /* PFK 18 = */ 'TS 0M',
     /* PFK 19 = */ 'HALFB',
     /* PFK 20 = */ 'HALFF',
     /* PFK 21 = */ 'LINES',
     /* PFK 22 = */ 'HEX',
     /* PFK 23 = */ 'CHAR',
     /* PFK 24 = */ 'ALTCASE');
   DCL 1 PFKSTR,
     2 CON CHAR(3) INIT('PFK'),
     2 PFKNUM PIC'99';
   DCL PFKNAME CHAR(5) DEF PFKSTR;
   DCL TRH PTR;
   DCL MYCMD CHAR(256) VAR;
   DCL 1 REPBUF,
     2 BUFLEN FIXED BIN(15,0),
     2 OFFSET FIXED BIN(15,0),
     2 BF CHAR(250);

   PFKNUM = OP;
   DO TRH=NXTRHEL REPEAT TRH->RHNXT WHILE(TRH^=NULL)
     UNTIL(TRH->RHNAME=PFKNAME);
     END;
   IF TRH=NULL
     THEN DO; /* EXECUTE THE DEFAULT */
       SELECT(PFKNUM);
         WHEN(4) DO;
           MYCMD='ABE {'××PARY(2)××'} ';
           DO I=3 TO HBOUND(PARY,1);
             MYCMD=MYCMD××' '××PARY(I);
             END;
           END;
         WHEN(18) /* IS IT TS PFK? */
           PUT STRING(MYCMD)
             EDIT(CURSOR_SEQ,' TS ')(P'99999999',A);
         OTHERWISE MYCMD=PFKDFT(PFKNUM);
         END; /* SELECT */
       IF INDEX(MYCMD,'&')^=0
         THEN DO;
           BUFLEN=254;
           OFFSET=0;
           BF=MYCMD;
           CALL #REP(RHHDRPT,RH_SYMTREE,REPBUF);
           MYCMD=SUBSTR(BF,1,SUFFIX(BF,' '));
           END;
       RHRC=#CMD(MYCMD,RHPTR,RHHDRPT);
       RETURN;
       END;

   /* USER HAS DEFINED HIS OWN PFK */
   IF N>LENGTH(CMDBUF)
     THEN RHRC=#CMD('RUN '××PFKNAME,RHPTR,RHHDRPT);
     ELSE RHRC=#CMD('RUN '××PFKNAME××' '××SUBSTR(CMDBUF,N),RHPTR,
       RHHDRPT);
   END DO_PFK;

   END ABE024;
