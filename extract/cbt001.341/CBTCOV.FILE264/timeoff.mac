 /* TIMEOFF */
 /*
0*$.HE GENERALIZED SUBROUTINES - TIMEOFF
0*$.HE DISPLACE A DATE/TIME BY A FACTOR
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$08/15/78
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  TIMEOFF
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE USED TO DISPLACE A DATE AND TIME BY
0*$A FACTOR EXPRESSED IN UNITS OF TIME (HOURS, MINUTES, OR SECONDS).
0*$CALL FORMAT IS AS FOLLOWS:
0*$.NF
0*$.IN 10
0*$.SP 1
0*$DCL TIMEOFF ENTRY(FIXED DEC(5,0), FIXED BIN(31,0),
0*$  FIXED BIN(31,0), FIXED BIN(31,0),
0*$  FIXED DEC(5,0), FIXED BIN(31,0));
0*$CALL TIMEOFF(DATE,TIME,-300,86400,NEWDATE,NEWTIME);
0*$  ** NEWDATE/NEWTIME WILL NOW CONTAIN THE
0*$     POINT IN TIME -300 SECONDS PRIOR
0*$     TO DATE/TIME. **
0*$.SP 1
0*$.IN 0
0*$.FI L
0*$THE DATES ARE EXPRESSED IN JULIAN FORM (YYDDD), THE UNITS
0*$IN WHICH THE TIMES ARE SPECIFIED IS INDICATED BY THE FOURTH
0*$PARAMETER.  IF THE FOURTH PARAMETER IS 1440, FOR EXAMPLE,
0*$THE TIMES ARE ALL EXPRESSED IN MINUTES.
 */
 TIMEOFF:  PROC(DTE,TME,FACT,UNITS,NDTE,NTME);
         DCL (DTE,NDTE) FIXED DEC(5,0);
         DCL (TME,FACT,NTME,UNITS) FIXED BIN(31,0);
         DCL DFACT FIXED BIN(15,0);
         DCL (MOD,ABS) BUILTIN;
         DCL DATEOFF ENTRY(FIXED DEC(5,0),
           FIXED BIN(15,0))
           RETURNS(FIXED DEC(5,0));
         NTME = TME;
         NDTE = DTE;
         DFACT = FACT/UNITS;
         IF FACT > 0
              THEN DO;
                   NTME = NTME + MOD(FACT,UNITS);
                   IF NTME >= UNITS
                        THEN DO;
                             DFACT = DFACT + 1;
                             NTME = NTME - UNITS;
                             END;
                   END;
              ELSE DO;
                   NTME = NTME - MOD(ABS(FACT),UNITS);
                   IF NTME < 0
                        THEN DO;
                             DFACT = DFACT - 1;
                             NTME = UNITS + NTME;
                             END;
                   END;
         IF DFACT ^= 0
              THEN NDTE = DATEOFF(NDTE,DFACT);
         END TIMEOFF;
