 /* MONTHEN */
 /*
0*$.HE GENERALIZED SUBROUTINES - MONTHEN
0*$.HE FIND MONTH ENDING DATE, FACTORED
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/28/77
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  MONTHEN
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE INVOKED TO FIND THE MONTH-ENDING DATE OF
0*$THE MONTH CONTAINING A GIVEN DATE.  A FACTOR CAN ALSO BE APPLIED
0*$TO DETERMINE A THE MONTH-ENDING DATE PLUS OR MINUS A GIVEN NUMBER
0*$OF MONTHS.  CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$DCL MONTHEN ENTRY(FIXED DEC(5,0),
0*$  FIXED BIN(15,0),
0*$  RETURNS(FIXED DEC(5,0));
0*$DCL GREGJUL ENTRY(CHAR(6))
0*$  RETURNS(FIXED DEC(5,0));
0*$NEWDTE = MONTHEN(GREGJUL(SUBSTR(DATE,3,4)
0*$  ××SUBSTR(DATE,1,2)),0);
0*$  /# NEWDTE = MONTH END DATE FOR CURRENT MONTH. #/
0*$.FI L
0*$.IN 0
0*$.SP 1
 */
 MONTHEN:  PROC(JULIAN,FACTOR) RETURNS(FIXED DEC(5,0));
         DCL JULIAN FIXED DEC(5,0);
         DCL FACTOR FIXED BIN(15,0);
         DCL GREGJUL ENTRY(CHAR(6)) RETURNS(FIXED DEC(5,0));
         DCL JULGREG ENTRY(CHAR(6),FIXED DEC(5,0));
         DCL GREGDTE CHAR(6),
             GREGPIC PIC'999999' DEF GREGDTE;
         DCL (Z,W,X,Y,YY,MM,DD) FIXED BIN(31,0);
         DCL MONTAB(12) FIXED BIN(15,0) STATIC INIT(
           31,28,31,30,31,30,31,31,30,31,30,31);
         CALL JULGREG(GREGDTE,JULIAN);
         MM = SUBSTR(GREGDTE,1,2);
         YY = JULIAN/1000;
         X = FACTOR/12;
         Y = MOD(FACTOR,12);
         Z = X + YY;
         W = Y + MM;
         IF W < 1
              THEN DO;
                   W = W + 12;
                   Z = Z - 1;
                   END;
              ELSE IF W > 12
                   THEN DO;
                        W = W - 12;
                        Z = Z + 1;
                        END;
         /* Z = NEW YY, W = NEW MM. */
         DD = MONTAB(W);
         IF MOD(Z,4) = 0 /* IS IT LEAP YEAR? */
              THEN IF W = 2 /* IS IT FEBRUARY? */
                   THEN DD = DD + 1;
         GREGPIC = W*10000 + DD*100 + Z;
         RETURN(GREGJUL(GREGDTE));
         END;
