         MACRO
&NAME PLIENTRY &DEBUG,       * ISSUE SPIE                              C
               &DSA=104,     * DSA SIZE                                C
               &COMPILE=,&REGS=,&BASE=10
         GBLB  &SPIE
         GBLC  &COMP
         LCLA  &NAMLEN,&W
         LCLC  &CNAME
         AIF   ('&DEBUG' EQ '').GOON
         AIF   ('&DEBUG' EQ 'DEBUG').SETSPIE
         MNOTE 4,' ** ''DEBUG'' MISPELLED, SPIE ISSUED ANYWAY '
.SETSPIE ANOP
&SPIE    SETB  1
.GOON    AIF   ('&DSA' GE '104').AHEAD
         MNOTE 12,' ** DSA MUST BE AT LEAST 104 BYTES'
         MEXIT
.AHEAD   ANOP
         AIF   ('&NAME' NE '').GOTNAM
         MNOTE 12,' ** A SYMBOL NAME MUST BE USED ON THE PLIENTRY '
         MNOTE 12,'    MACRO.  THIS NAME WILL BECOME THE CSECT    '
         MNOTE 12,'    NAME. '
         MEXIT
.GOTNAM  ANOP
&NAMLEN  SETA  K'&NAME
         AIF   (&NAMLEN LE 7).GOODNAM
         MNOTE 12,' ** CSECT NAME CANNOT EXCEED SEVEN CHARACTERS '
         MEXIT
.GOODNAM ANOP
         PLICOMP &COMPILE
         AIF   ('&COMP' EQ 'F').F
.OPT     ANOP
&NAME.1  CSECT
&CNAME   SETC  '&NAME.1'
         PLIREQU &REGS
&W       SETA  7-&NAMLEN
         AIF   (&W EQ 0).NOBLNK
         DC    CL&W.' '
.NOBLNK  ANOP
         DC    C'&NAME'
         DC    AL1(&NAMLEN.)
         ENTRY &NAME
&NAME    EQU   *
         USING *,RF
         STM   RE,RC,12(RD)
         B     *+8
&W       SETA  ((&DSA+7)/8)*8
         DC    F'&W'
         L     R1,76(RD)               R1 = NAB
         L     R0,8(RF)                R0 = LEN NEW DSA
         ALR   R0,R1                   R0 = TENTATIVE NEW NAB
         CL    R0,12(RC)               IS THERE ENUF CORE IN THIS SEG
         BC    12,*+10                 BR IF YES
         L     RF,116(RC)              ELSE, GO TO OVERFLOW RECOVERY
         BALR  RE,RF
         L     RE,72(RD)               RE = A(LWS)
         LR    RF,R0                   RE = NEW NAB
         STM   RE,R0,72(R1)            STORE A(LWS), NEW NAB, AND END
*                                           PROLOGUE NAB IN NEW DSA
         ST    RD,4(R1)                SET DSA BACKCHAIN
         LA    RD,0(R1)                RESET RD
         ST    R5,88(RD)               SET STATIC BACKCHAIN
         MVI   0(RD),X'80'             FLAG AS DSA
         MVI   1(RD),X'20'             FLAG TO RESTORE CURRENT-ENABLE
         DROP  RF
         BALR  &BASE,0
         USING *,&BASE
         MVC   84(4,RD),=X'91E091E0'   SET BLOCK & CURRENT ENABLE CELLS
         AGO   .CKSPIE
.F       ANOP
IHEQERR  DXD   A
&NAME    CSECT
&CNAME   SETC  '&NAME'
         PLIREQU &REGS
&W       SETA  ((&NAMLEN+2)/2)*2+4
         B     &W.(0,15)
         DC    AL1(&NAMLEN.)
         DC    CL&NAMLEN.'&NAME'
         STM   14,11,12(13)
         BALR  &BASE,0
         USING *,&BASE
         LA    0,&DSA
         L     15,=V(IHESADA)
         BALR  14,15
         MVI   0(13),X'80'
.CKSPIE  ANOP
         USING DSA,13
DSA      DSECT
         DS    CL92
PL1PICAH DS    F
PL1PARMW DS    D
&CNAME   CSECT
         AIF   (&SPIE EQ 0).NOSPIE
         SPIE
         ST    1,PL1PICAH
.NOSPIE  ANOP
         L     1,4(13)                 R1 = DSA BACKCHAIN
         L     1,24(1)                 R1 = CONTENTS AT ENTRY
         MEND
