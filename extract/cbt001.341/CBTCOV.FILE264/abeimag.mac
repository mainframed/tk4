   %INCLUDE ABESUB;
 ABESUB(ABEIMAG) PARS(1) CHECK(YES);
   %INCLUDE ABESUBA;
 /**********************************************************************
 *                                                                     *
 * THIS MODULE SHOULD BE INVOKED VIA THE PGM SUBCOMMAND OF ABE.  IT IS *
 * DESIGNED TO READ A GRAPHIC PRINT IMAGE FROM DDNAME IBM3800 INTO     *
 * THE BITMAP EDITED VIA THE GED SUBCOMMAND.                           *
 *                                                                     *
 * THE SOLE PARAMETER IS THE RELATIVE IMAGE NUMBER TO BE PROCESSED IN  *
 * THE INPUT FILE.  THE DEFAULT IS 1.                                  *
 *                                                                     *
 **********************************************************************/
   DCL PSFCODE ENTRY(CHAR(3)) RETURNS(CHAR(3));
   DCL PTRIN ENTRY(PTR) RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0)) RETURNS(PTR);
   DCL DUMPSTT ENTRY;

   DCL STR CHAR(4096) BASED(DUMPTR);
   DCL ENDPTR PTR;
   DCL (ICPCODE,IRDCODE) CHAR(3);
   DCL INREC CHAR(32000) VAR;
   DCL 1 ICP BASED(INRECPTR),
     2 XORG FIXED BIN(15,0),   /* X ORIGIN OF CELL */
     2 YORG FIXED BIN(15,0),   /* Y ORIGIN OF CELL */
     2 XSIZE FIXED BIN(15,0),  /* X DIMENSION OF CELL */
     2 YSIZE FIXED BIN(15,0),  /* Y DIMENSION OF CELL */
     2 XFILL FIXED BIN(15,0),  /* X FILL SIZE */
     2 YFILL FIXED BIN(15,0);  /* Y FILL SIZE */
   DCL 1 IID BASED(INRECPTR),
     2 RESERVED CHAR(18),
     2 XRI FIXED BIN(15,0),
     2 YRI FIXED BIN(15,0);
   DCL MYCMD CHAR(256) VAR;

   /*** INITIALIZE INPUT FILE *****************************************/
   OPEN FILE(RH_SEQ_1) TITLE('IBM3800') RECORD INPUT;
   ON ENDFILE(RH_SEQ_1) GOTO EOJ;
   INRECPTR=PTROUT(PTRIN(ADDR(INREC))+11);
   DUMPTR=ADDR(INREC);
   IF PARY(1)=''
     THEN I=1;
     ELSE I=PARY(1);
   DO J=1 TO I;
     CALL READIT('BIM'); /* GO FIND BEGIN IMAGE */
     END;
   CALL READIT('IID'); /* FIND IMAGE INPUT DESCRIPTOR */

   /*** INITIALIZE BITMAP *********************************************/
   XR=XRI;
   YR=YRI;
   PUT STRING(MYCMD) EDIT('GR DEVICE 240 240 ',XR,YR,' 1')
     (A,P'ZZZ9',X(1),P'ZZZ9',A);
   I=#CMD(MYCMD,RHPTR,RHHDRPT);

   /*** NOW READ IMAGE ************************************************/
   ICPCODE=PSFCODE('ICP'); /* IMAGE CELL POSITION CODE */
   IRDCODE=PSFCODE('IRD'); /* IMAGE RASTER DATA CODE */
   CALL READIT('ICP'); /* FIND FIRST IMAGE CELL POSITION */
   DO FOREVER=1 REPEAT FOREVER;
     SELECT(SUBSTR(INREC,4,3));
       WHEN(ICPCODE) DO;
         X=XORG+1;
         Y=YR-YORG;
         XS=XSIZE;
         L=XS/8; /* PEL STRING LENGTH */
         YS=YSIZE;
         XF=XFILL;
         YF=YFILL;
         END;
       WHEN(IRDCODE) DO;
         DO I=XS TO XF BY XS;
           XW=X+I-XS;
           DO J=YS TO YF BY YS;
             YW=Y-J+YS;
             DUMPTR=INRECPTR;
             ENDCK=PTRIN(ADDR(INREC))+2+LENGTH(INREC);
             DO UNTIL(PTRIN(DUMPTR)>=ENDCK);
               CALL #PIXPUTL(XW,YW,XS,SUBSTR(STR,1,L),RHPLOT79);
               YW=YW-1;
               DUMPTR=PTROUT(PTRIN(DUMPTR)+L);
               END;
             END;
           END;
         END;
       OTHERWISE;
       END; /* SELECT */
     READ FILE(RH_SEQ_1) INTO(INREC);
     END;
 EOJ:;
   CLOSE FILE(RH_SEQ_1);
   CMDDATA='GED'; /* INVOKE GED NEXT */

 READIT: PROC(PCODE);
   DCL (CODE,PCODE) CHAR(3);

   CODE=PSFCODE(PCODE); /* SEARCH FOR BEGIN IMAGE */
   DO UNTIL(SUBSTR(INREC,4,3)=CODE);
     READ FILE(RH_SEQ_1) INTO(INREC);
     END;
   END READIT;

   END ABEIMAG;
