 /* GRLOG */
 /* THIS SUBROUTINE WAS WRITTEN TO BYPASS THE PROBLEM WITH DISP=MOD
    IN TSO.  IT MAY BE USED TO ADD RECORDS TO A LOG FILE.  IT SHOULD
    BE DECLARED AS FOLLOWS:

         DCL GRLOG ENTRY(CHAR(*),CHAR(80),FIXED BIN(15,0))
           RETURNS(FIXED BIN(15,0));

    CALL FORMAT IS AS FOLLOWS:

    RET = GRLOG(TITLE,LOGREC,RET);

    WHERE RET IS FIXED BIN(15,0), AND SHOULD BE SET BY THE USER
    BEFORE INVOKING GRLOG;  ON THE FIRST CALL, RET SHOULD BE SET TO
    0.  FOR EACH SUBSEQUENT CALL, RET SHOULD BE SET TO 1.  FINALLY,
    THE CLOSE OUT THE LOG FILE, CALL GRLOG ONE LAST TIME WITH RET
    SET TO 2.  NO RECORD WILL BE WRITTEN ON THIS LAST CALL, BUT
    CERTAIN CHAIN CONTROL FIELDS IN THE FILE WILL BE UPDATED.
    IF AN ERROR OCCURS IN GRLOG, IT WILL RETURN A VALUE OF 16.
    OTHERWISE, IT WILL RETURN A VALUE OF 1 EXCEPT ON THE LAST
    CALL, IN WHICH CASE IT RETURNS A VALUE OF 0.

 TO COPY THE LOG FILE, PROCESS IT AS A SEQUENTIAL FILE AND
 THROW AWAY THE FIRST RECORD, WHICH IS A CONTROL RECORD.
 TO CLEAR THE LOGFLE, USE THE FOLLOWING JCL:

     //CLEAR EXEC PGM=TGSRESTL
     //STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR
     //SYSPRINT DD SYSOUT=A
     //LOGFLE DD DSN=&DSN,DISP=OLD

 THE LOG FILE SHOULD BE ALLOCATED WITH THE DCB ATTRIBUTES
 DCB=(LRECL=80,BLKSIZE=80,RECFM=F).
 */
 GRLOG:  PROC(TITLE,LOGREC,RET) RETURNS(FIXED BIN(15,0));
         DCL RET FIXED BIN(15,0);
         DCL TITLE CHAR(*);
         DCL LOGREC CHAR(80);
         DCL LOGFLE FILE ENV(F RECSIZE(80) REGIONAL(1)) RECORD KEYED;
         DCL LAB(0:3) LABEL;
         DCL LOGKEY FIXED DEC(7,0);
         DCL 1 LOGQCR STATIC,
              2 PAD CHAR(72),
              2 NXTAVLBL FIXED DEC(7,0),
              2 MAXAVLBL FIXED DEC(7,0);
         GO TO LAB(RET);
 LAB(0):
         OPEN FILE(LOGFLE) TITLE(TITLE) UPDATE DIRECT KEYED;
         LOGKEY = 0;
         READ FILE(LOGFLE) INTO(LOGQCR) KEY(LOGKEY);
 LAB(1):
         LOGKEY = NXTAVLBL;
         NXTAVLBL = NXTAVLBL + 1;
         IF NXTAVLBL > MAXAVLBL
              THEN RETURN(16);
         WRITE FILE(LOGFLE) FROM(LOGREC) KEYFROM(LOGKEY);
         RETURN(1);
 LAB(2):
         LOGKEY = 0;
         WRITE FILE(LOGFLE) FROM(LOGQCR) KEYFROM(LOGKEY);
         RETURN(0);
         END;
