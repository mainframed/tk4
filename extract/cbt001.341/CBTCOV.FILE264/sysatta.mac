*  SYSATTA
*$.HE GENERALIZED SUBROUTINES - SYSATTA
*$.HE INTERFACE TO ATTACH
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$09/19/79
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSATTA
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE SUPPLIES A GENERALIZED ATTACH FACILITY.
*$THE PARAMETER LIST IS AS IN THE FOLLOWING EXAMPLE:
*$.SP 1
*$.IN 5
*$.NF
*$DCL SYSATTA ENTRY;
*$DCL A EVENT, B PTR;
*$DCL PGMN CHAR(8) INIT('TESTPGM');
*$CALL SYSATTA(PGMN,A,B,PARM1,...,PARMN);
*$.FI L
*$.IN 0
*$EVENT VARIABLE A WILL BE POSTED WHEN SUBTASK
*$TERMINATES.  REGISTER 1 WILL POINT TO PARM1 IN PARM LIST
*$WHEN THE ATTACH IS ISSUED.  POINTER B WILL BE SET TO THE
*$ADDRESS OF THE SUBTASK.
SYSATTA  PLIENTRY ,DSA=400,COMPILE=O
         LM    R2,R4,0(R1)        R3=A(ECB)
         L     R2,0(R2)           R2 = A(PGM NAME)
         XC    PARMLST(PARMLEN),PARMLST
         LA    R6,PARMDUM
         ST    R6,PARMLST
         OI    PARMLST,X'80'
         MVC   ATTCHE,ATTCHL
         LTR   R4,R4              WAS A(TCB) LAST PARM?
         BC    4,ATTCH
         LA    R1,12(R1)
         LA    R5,PARMLST
PLUP     DS    0H
         L     R6,0(R1)
         ST    R6,0(R5)
         LA    R5,4(R5)
         LA    R1,4(R1)
         LTR   R6,R6
         BC    10,PLUP            BR BACK IF MORE PARMS
ATTCH    DS    0H
         LA    R1,PARMLST
         LA    R7,ATTCHE
         ATTACH EPLOC=(R2),ECB=(R3),SHSPL=SHSPL,SF=(E,(R7))
         ST    R1,0(R4)           RETURN A(SUBTASK)
         PLIEXIT
SHSPL    DC    AL1(2,1,78)
ATTCHL   ATTACH SHSPL=SHSPL,SF=L
ATTCHLN  EQU   *-ATTCHL
         LTORG
DSA      DSECT
PARMLST  DS    16F
PARMDUM  DS    H
PARMLEN  EQU   *-PARMLST
         DS    0D
ATTCHE   DS    CL(ATTCHLN)
         DS    0D
DSALEN   EQU   *-DSA
         END
