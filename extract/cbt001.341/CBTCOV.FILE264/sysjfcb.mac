*$.HE GENERALIZED SUBROUTINES - SYSJFCB
*$.HE PLI INTERFACE TO RDJFCB MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/22/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSJFCB
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE PROVIDES THE PLI PROGRAMMER WITH AN INTERFACE TO THE
*$RDJFCB MACRO.  IT CAN BE USED TO RETRIEVE A JOB FILE CONTROL BLOCK.
*$.SP 1
*$CALL SYSJFCB(DDNAME,JFCBPTR);
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$DDNAME CHAR(8)
*$.IN 5
*$DEFINES THE DDNAME ASSOCIATED WITH THE JFCB TO BE RETRIEVED.
*$.IN 0
*$JFCBPTR
*$.IN 5
*$POINTER VARIABLE WHICH MUST BE SET TO THE ADDRESS OF A 176-BYTE
*$AREA.  THE JFCB WILL BE MOVED TO THIS AREA BEFORE CONTROL IS
*$RETURNED.
SYSJFCB  PLIENTRY ,DSA=250
         LM    R2,R3,0(R1)        R2 = A(DDNAME SDV)
*                                 R3 = A(JFCBPTR)
         L     R2,0(R2)           R2 = A(DDNAME)
         MVC   DUMDCB(DCBLEN),DUMDCBC
         MVC   DUMDCB+X'28'(8),0(R2) MOVE DDNAME TO DCB
         LA    R1,EXLST
         ST    R1,EXLST
         MVC   DUMDCB+X'25'(3),EXLST+1
         L     R3,0(R3) R3 = JFCBPTR
         ST    R3,EXLST
         OI    EXLST,X'87'
         MVC   RDJFCBL,RDJFCBC
         LA    R1,RDJFCBL
         LA    R2,DUMDCB
         RDJFCB ((R2)),MF=(E,(1))
         PLIEXIT
RDJFCBC  RDJFCB (DUMDCBC),MF=L
DUMDCBC  DCB   DDNAME=DUMMY,DSORG=PS,MACRF=R,EXLST=DUMDCBC
DCBLEN   EQU   *-DUMDCBC
         LTORG
DSA      DSECT
RDJFCBL  DS    F
EXLST    DS    F
         DS    0D
DUMDCB   DS    CL100
DSALEN   EQU   *-DSA
         END
