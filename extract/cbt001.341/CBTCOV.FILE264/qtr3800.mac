 QTR3800: PROC OPTIONS(MAIN);
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   %INCLUDE @HEXCHAR;
   DCL (BUF,SAVEBUF) CHAR(32760) VAR;
   DCL WIDTH FIXED BIN STATIC INIT(2640); /* PAPER IS 2640 PELS WIDE */
   DCL HEIGHT FIXED BIN STATIC INIT(1800); /* PAPER IS 1800 PELS HIGH */

   QTR=0;
   ON ENDFILE(IBM3800) GO TO EOJ;
   DO I=1 REPEAT I+1;
     READ FILE(IBM3800) INTO(BUF);
     IF LENGTH(BUF)<7
       THEN J=1;
       ELSE SELECT(SUBSTR(BUF,4,3));
         /* FOLLOWING WHENS ARE IN THE SAME ORDER AS THE INPUT DATA */
         /* OFFSETS OF THOSE IN SH35-0073 PLUS 10 */
         WHEN(@HEXCHAR(D3A8A8)) J=(I=1);   /* BEGIN DOCUMENT */
         WHEN(@HEXCHAR(D3A8AF)) DO;        /* BEGIN PAGE */
           J=(QTR=0); /* 1ST QTR ONLY */
           IF J=1
             THEN DO;
               PAGENO=PAGENO+1;
               SUBSTR(BUF,13,5)=PAGENO;
               END;
           END;
         WHEN(@HEXCHAR(D3A8C9)) J=(QTR=0); /* BEGIN ACT. ENV. GRP. */
         WHEN(@HEXCHAR(D3A6AF)) DO;        /* PAGE DESCRIPTOR */
           J=(QTR=0); /* PGD 1ST QTR ONLY */
           SUBSTR(BUF,16,3)=TOCH(WIDTH,3); /* PAPER WIDTH */
           SUBSTR(BUF,19,3)=TOCH(HEIGHT,3); /* PAPER HEIGHT */
           END;
         WHEN(@HEXCHAR(D3A79B)) J=(QTR=0); /* COMPOSED TEXT CONTROL */
         WHEN(@HEXCHAR(D3A69B)) DO;        /* COMPOSED-TEXT DESCR. */
           J=(QTR=0); /* CTD 1ST QTR ONLY */
           SUBSTR(BUF,16,2)=TOCH(WIDTH,2); /* PAPER WIDTH */
           SUBSTR(BUF,18,2)=TOCH(HEIGHT,2); /* PAPER HEIGHT */
           END;
         WHEN(@HEXCHAR(D3A9C9)) J=(QTR=0);  /* END ACT. ENV. GRP. */
         WHEN(@HEXCHAR(D3A87B)) DO;        /* BEGIN IMAGE BLOCK  */
           J=1; /* WRITE ALL IMAGES */
           XO=(MOD(QTR,2)^=0)*WIDTH/2; /* X ORIGIN */
           YO=(QTR>1)*HEIGHT/2; /* Y ORIGIN */
           QTR=MOD(QTR+1,4); /* SET NEXT QUARTER */
           END;
         WHEN(@HEXCHAR(D3A77B)) DO;  /* IOC (IMAGE OUTPUT CONTROL) */
           J=1; /* WRITE ALL IMAGES */
           SUBSTR(BUF,10,3)=TOCH(FRCH(SUBSTR(BUF,10,3))+XO,3);
           SUBSTR(BUF,13,3)=TOCH(FRCH(SUBSTR(BUF,13,3))+YO,3);
           END;
         WHEN(@HEXCHAR(D3A67B)) J=1; /* IMAGE INPUT DESCR. */
         WHEN(@HEXCHAR(D3AC7B)) J=1;  /* IMAGE CELL POSITION */
         WHEN(@HEXCHAR(D3A9A8)) J=0; /* END DOCUMENT - LAST ONLY */
         WHEN(@HEXCHAR(D3A9AF)) DO; /* END PAGE */
           J=(QTR=0); /* END PAGE AFTER 4TH QTR ONLY */
           SUBSTR(BUF,13,5)=PAGENO;
           SAVEBUF=BUF; /* SAVE IN CASE LAST PAGE ISN'T 4 QTRS */
           END;
         OTHERWISE J=1;
         END; /* SELECT */
     IF J=1
       THEN WRITE FILE(OUT3800) FROM(BUF);
     END;
 EOJ:
   IF QTR^=0
     THEN WRITE FILE(OUT3800) FROM(SAVEBUF); /* WRITE LAST END PAGE */
   WRITE FILE(OUT3800) FROM(BUF); /* WRITE LAST END DOCUMENT CODE */

 FRCH: PROC(P) RETURNS(FIXED BIN(31,0));
   DCL P CHAR(4) VARYING;
   DCL L FIXED BIN(31,0),
       LC CHAR(4) BASED(ADDR(L));
   LC=LOW(4-LENGTH(P))××P;
   RETURN(L);
   END FRCH;

 TOCH: PROC(P,L) RETURNS(CHAR(4) VAR);
   DCL (P,L) FIXED BIN(31,0),
       PC CHAR(4) BASED(ADDR(P));
   RETURN(SUBSTR(PC,5-L,L));
   END TOCH;

   END QTR3800;
