 /* COMPRP */
 /*
0*$.HE GENERALIZED SUBROUTINES - COMPRP
0*$.HE COMPRESSES BLANKS FROM A STRING
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$04/09/80
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  COMPRP
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$COMPRP WILL STRIP ALL SETS OF TWO OR MORE CONTIGUOUS BLANKS FROM A
0*$CHARACTER STRING, REPLACING THEM WITH HALFWORD CONTROL WORDS,
0*$INDICATING THE NUMBER OF BLANKS REMOVED.  THE BLANKS CAN
0*$SUBSEQUENTLY BE REINSERTED BY CALLING THE EXPANP SUBROUTINE.
0*$CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$DCL COMPRP ENTRY(CHAR(*) VAR,CHAR(*) VAR);
0*$DCL (IN,OUT) CHAR(32767) VAR;
0*$READ FILE(INPUT) INTO(IN);
0*$CALL COMPRP(IN,OUT);
0*$WRITE FILE(OUTPUT) FROM(OUT);
 */
 COMPRP: PROC(IN,OUT);
         DCL (IN,OUT) CHAR(*) VAR;
         DCL W FIXED BIN(15,0),
             S CHAR(1) DEF W POS(2);
         DCL N FIXED BIN(15,0),
             P CHAR(1) DEF N POS(2);
         OUT = '';
         I = 0;
         DO WHILE(I < LENGTH(IN));
           J = INDEX(SUBSTR(IN,I+1),'  ')+I;
           IF J = I
             THEN J,K = LENGTH(IN)+1;
             ELSE DO;
               K = VERIFY(SUBSTR(IN,J+1),' ')+J;
               IF K = J
                 THEN K = LENGTH(IN)+1;
               END;
           W = J - I - 1; /* W,S = # NONBLANKS */
           IF W > 256
             THEN DO;
               W = 256;
               N = 0;
               K = I + 257;
               END;
             ELSE DO;
               N = K - J; /* N,P = # BLANKS */
               IF N > 256
                 THEN DO;
                   N = 256;
                   K = I + W + 257;
                   END;
               END;
           OUT = OUT××S××P××SUBSTR(IN,I+1,W);
           I = K - 1;
           END;
         END;
