 SORTSMF:  PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         % INCLUDE SMFDEFI;
         % INCLUDE SMFHDR;
         DCL PLIXOPT CHAR(15) VAR INIT('ISA(100K)') STATIC EXT;
         DCL INITSW BIT(1) STATIC INIT('1'B);
         DCL DCBPTR PTR STATIC;
         DCL DDNAME CHAR(8) STATIC INIT('PLOTLIB');
         DCL SMFOUT FILE RECORD OUTPUT ENV(VBS RECSIZE(32760)
           BLKSIZE(13030));
         DCL (JCLOUT,JCLOUTA) RECORD OUTPUT;
         DCL CTLGRET BIT(32);
         DCL AREA CHAR(265);
         DCL SYMARY(12) CHAR(44) VAR STATIC INIT(
           '&DSN','',
           '&DUMMY','',
           '&YY','',
           '&MON','',
           '&DDN','',
           '&DUM2','');
         DCL STATDSN CHAR(44) VAR STATIC INIT(
              'U.SD.U000.SMF.YEAR&YY.&MON');
         DCL DDN PIC'99' STATIC INIT(0);
         DCL JCLARY(6) CHAR(80) VAR STATIC INIT(
           '//IN&DDN DD &DUMMYDSN=&DSN,',
           '// DCB=(LRECL=32760,BLKSIZE=13030,RECFM=VBS),',
           '// DISP=(OLD,DELETE,KEEP)',
           '//OT&DDN DD DSN=&DSN,',
           '// DCB=(LRECL=32760,BLKSIZE=13030,RECFM=VBS),VOL=(,,,10),',
           '// DISP=(NEW,CATLG,DELETE),UNIT=TAPE,LABEL=EXPDT=99000');
         DCL JCLREC CHAR(80);
         DCL MONCK CHAR(2) STATIC INIT('');
         DCL JULGREG ENTRY(CHAR(6),FIXED DEC(5,0));
         DCL GREGDTE CHAR(6);
         DCL MONTAB(12) CHAR(3) STATIC INIT(
           'JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG','SEP',
           'OCT','NOV','DEC');
         OPEN FILE(JCLOUT);
         OPEN FILE(JCLOUTA);
         ON ENDFILE(JOBCARD) GO TO JOBEOJ;
         DO FOREVER=1 REPEAT FOREVER;
              READ FILE(JOBCARD) INTO(JCLREC);
              WRITE FILE(JCLOUT) FROM(JCLREC);
              END;
 JOBEOJ:
         CLOSE FILE(JOBCARD);
         DCL (PLIRETC,PLISRTD) BUILTIN;
         CALL PLISRTD(' SORT FIELDS=(11,4,CH,A,7,4,CH,A,6,1,CH,A) ',
           ' RECORD TYPE=V,LENGTH=(32760) ',
           150000,RETCODE,E15,E35);
         IF ^INITSW
              THEN CALL SYSCLOS(DCBPTR);
 E15:  PROC RETURNS(CHAR(32760) VAR);
         DCL (ERRCNT,READCNT) FIXED BIN(31,0) STATIC INIT(0);
         ON TRANSMIT(SMFDATA) BEGIN;
              PUT EDIT('I/O ERROR AFTER RECORD #',READCNT)
                (A,P'ZZ,ZZZ,ZZ9') SKIP(1);
              IF ERRCNT = READCNT
                   THEN DO;
            PUT EDIT('ERROR - TRANSMIT RAISED ON TWO CONSECUTIVE READS')
                        (A) SKIP(1);
                        SIGNAL ERROR;
                        END;
              ERRCNT = READCNT;
              GO TO READNXT;
              END;
         ON ENDFILE(SMFDATA) GO TO ENDE15;
 READNXT:
         READ FILE(SMFDATA) INTO(SMFREC);
         READCNT = READCNT + 1;
         CALL PLIRETC(12);
         RETURN(SMFREC);
 ENDE15:
         IF READCNT = 0
              THEN DO;
                 PUT EDIT('ERROR - NO VALID RECORDS FOUND')(A) SKIP(1);
                 SIGNAL ERROR;
                 END;
         CALL PLIRETC(8);
         END E15;
 E35:  PROC(SORTSTR);
         DCL SORTSTR CHAR(*);
         DCL I FIXED;
         SMFREC = SORTSTR;
         CALL PLIRETC(4);
         IF REASONABLE(WRITDTE)
           THEN DO;
             WRITE FILE(SMFOUT) FROM(SMFREC);
             CALL JULGREG(GREGDTE,WRITDTE);
             IF SUBSTR(GREGDTE,1,2) ^= MONCK
              THEN DO;
                   MONCK = SUBSTR(GREGDTE,1,2);
                   DDN = DDN + 1;
                   SYMARY(10) = DDN; /* SETS DDN */
                   PUT STRING(SYMARY(6)) EDIT((WRITDTE/1000))(P'99');
                     /* THE ABOVE SETS YY */
                   SYMARY(8)=MONTAB(MONCK); /* SETS MON */
                   SYMARY(2)=SETVAR(STATDSN); /* SETS DSN */
                   CALL SYSCTLG('NAME',CTLGRET,SYMARY(2),'SYSRES',AREA);
                   IF CTLGRET = '0'B
                        THEN SYMARY(4) = '';
                        ELSE SYMARY(4) = 'DUMMY,';
                   DO I = 1 TO 6;
                        JCLREC = SETVAR(JCLARY(I));
                        WRITE FILE(JCLOUT) FROM(JCLREC);
                        END;
                   IF PARM = 'PLOT'
                     THEN DO;
                       IF CKMEM(SYMARY(8)××SYMARY(6))
                         THEN SYMARY(12)='';
                         ELSE SYMARY(12)='DUMMY,';
    JCLREC=SETVAR('// EXEC AU109229,MON=&MON,YY=&YY,DUMMY=''&DUM2''');
                       WRITE FILE(JCLOUTA) FROM(JCLREC);
                       END;
                   END;
             END;
         END E35;
 REASONABLE:  PROC(DTE) RETURNS(BIT(1));
         DCL DTE FIXED DEC(7,0);
         ON ERROR GOTO END_REASONABLE;
         IF (DTE<99365 & DTE>65001)
              THEN RETURN('1'B);
 END_REASONABLE:
         RETURN('0'B);
         END REASONABLE;
 SETVAR:  PROC(INSTR) RETURNS(CHAR(80) VAR);
         DCL INSTR CHAR(*) VAR;
         DCL OTSTR CHAR(80) VAR;
         DCL (I,J) FIXED;
         OTSTR = INSTR;
         DO I = 1 TO 11 BY 2;
              J = INDEX(OTSTR,SYMARY(I));
              IF J ^= 0
                   THEN OTSTR=SUBSTR(OTSTR,1,J-1)××SYMARY(I+1)××
                     SUBSTR(OTSTR,J+LENGTH(SYMARY(I)));
              END;
         RETURN(OTSTR);
         END SETVAR;
 CKMEM:  PROC(PARMNAME) RETURNS(BIT(1));
         DCL PARMNAME CHAR(8);
         DCL 1 LIST STATIC,
              2 FF FIXED BIN(15,0) INIT(1),
              2 LL FIXED BIN(15,0) INIT(12),
              2 NAME CHAR(8),
              2 PAD CHAR(8);
         DCL LISTPTR PTR STATIC;
         DCL RETCODE FIXED BIN(31,0);
         IF INITSW
              THEN DO;
                   INITSW = '0'B;
                   CALL SYSDCBP(DCBPTR,DDNAME);
                   CALL SYSOPEN(DCBPTR,'INPUT');
                   LISTPTR = ADDR(LIST);
                   END;
         NAME = PARMNAME;
         CALL SYSBLDL(DCBPTR,LISTPTR,RETCODE);
         IF RETCODE = 0
              THEN RETURN('1'B);
              ELSE RETURN('0'B);
         END CKMEM;
         END;
