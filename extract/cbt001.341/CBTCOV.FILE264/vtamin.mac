 /* VTAMIN */
VTAMIN:  PROC OPTIONS(MAIN);
         DCL PGMNAME CHAR(8) INIT('VTAMIO');
         DCL SYSLOAD ENTRY;
         DCL TERMEP FIXED BIN(31,0);
         CALL SYSLOAD(PGMNAME,TERMEP);
         DCL SYSCALL ENTRY;
         DCL VTAMECB FIXED BIN(31,0),
             VTAMBIT BIT(32) DEF VTAMECB;
         DCL NLCHARDF BIT(8) STATIC INIT('00010101'B), /* X'15' */
             NLCHAR CHAR(1) DEF NLCHARDF;
         DCL STRPARS ENTRY(CHAR(*) VAR, FIXED BIN(15,0),
           CHAR(1),CHAR(*) VAR);
         DCL (OFF,LOFF) FIXED BIN(15,0);
         ON ENDFILE(SYSIN) SIGNAL FINISH;
         DCL SYSWAIT ENTRY;
         DCL WAITCNT FIXED BIN(15,0) INIT(1);
         DCL DUMPSTT ENTRY;
         DCL SUFFIX ENTRY(CHAR(*) VAR, CHAR(*) VAR)
           RETURNS(FIXED BIN(15,0));
         DCL TROUT ENTRY(CHAR(8))
               RETURNS(CHAR(16));
 GETCMD:  PROC(PR) RETURNS(CHAR(72) VAR);
         DCL PR CHAR(80) VAR;
         DCL INREC CHAR(80);
         DCL RETSTR CHAR(72) VAR;
         PUT EDIT(PR)(A) SKIP(1);
         READ FILE(SYSIN) INTO(INREC);
         RETSTR=SUBSTR(INREC,1,MAX(1,SUFFIX(INREC,' ')));
         PUT EDIT(RETSTR)(A) SKIP(1);
         RETURN(RETSTR);
         END GETCMD;
         DCL PLIXOPT CHAR(15) VAR STATIC EXT INIT('NOSTAE,NOSPIE');
         DCL MSGB CHAR(80) VAR INIT('');
         DCL DATA CHAR(2000) VAR;
         DCL OPCODE FIXED BIN(15,0) INIT(0);
         DCL RPLPTR PTR;
         DCL 1 RPL UNALIGNED BASED(RPLPTR),
           2 RPLPAD CHAR(4),
           2 RPLECB BIT(8);
         DCL RETC FIXED BIN(31,0);
         DCL RECORD CHAR(78) VAR;
         DCL PROMPT CHAR(80) VAR INIT('VTAMIO');
         DCL PROMPI CHAR(80) VAR INIT(
           'ENTER LOGON MESSAGE OR NULL');
         DCL PROMPA CHAR(80) VAR INIT(
           'ENTER APPLID');
         DCL CMDSTR CHAR(256) VAR;
         DCL GETLO CHAR(3) STATIC INIT('get');
         DCL GETLOP CHAR(4) STATIC INIT('GETP');
         DCL STOPLO CHAR(4) STATIC INIT('stop');
         DCL WAITLO CHAR(4) STATIC INIT('wait');
         MSGB = GETCMD(PROMPA);
         CMDSTR=GETCMD(PROMPI);
         IF CMDSTR=''
           THEN DATA='';
           ELSE DATA=CMDSTR;
         DO FOREVER=1 REPEAT FOREVER;
           CALL SYSCALL(TERMEP,OPCODE,DATA,MSGB,RETC,VTAMECB);
           IF MSGB^=''
             THEN DO;
               PUT EDIT(MSGB,DATA)
                 (A,A) SKIP(1);
               IF MSGB^='EOF'
                 THEN IF OPCODE^=12 /* WAS IT NOT STOP? */
                   THEN OPCODE = 24; /* INDICATE ERROR OCCURRED */
               END;
           IF RETC^=0
             THEN DO;
               PUT EDIT('BAD RETURN CODE=',RETC)
                  (A,P'ZZ,ZZZ,ZZZ')
                  SKIP(1);
               IF OPCODE^=12 /* WAS IT NOT STOP? */
                 THEN OPCODE = 24; /* INDICATE ERROR OCCURRED */
               END;
           IF OPCODE = 12 /* WAS IT STOP? */
             THEN RETURN;
           IF OPCODE=24 /* WAS IT ERROR? */
             THEN OPCODE=12;
           IF OPCODE = 16 /* WAS IT GET-POST? */
             THEN DO;
               CALL DUMPSTT(DATA);
               IF SUBSTR(DATA,1,1)=NLCHAR
                 THEN OFF=1;
                 ELSE OFF=0;
               DO UNTIL(OFF>LENGTH(DATA));
                 CALL STRPARS(DATA,OFF,NLCHAR,RECORD);
                 CALL DUMPSTT(RECORD);
                 END;
               END;
           IF OPCODE^=12 /* IS IT NOT STOP? */
             THEN OPCODE = 20;
           DO WHILE(OPCODE=20);
             CMDSTR=GETCMD(PROMPT);
             IF CMDSTR='GETP' × CMDSTR=GETLOP
               THEN OPCODE=16;
               ELSE IF CMDSTR='GET' × CMDSTR=GETLO
                 THEN OPCODE=4; /* DO A GET */
                 ELSE IF CMDSTR='STOP' × CMDSTR=STOPLO
                   THEN OPCODE=12;
                   ELSE IF CMDSTR=''
                     THEN IF SUBSTR(VTAMBIT,2,1)/*IS IT POSTED?*/
                       THEN OPCODE=16; /* DO A GETPOST */
                       ELSE;
                     ELSE IF CMDSTR='WAIT' × CMDSTR=WAITLO
                       THEN DO;
                         CALL SYSWAIT(WAITCNT,VTAMECB);
                         OPCODE=16; /* DO A GETPOST */
                         END;
                       ELSE DO;
                         OPCODE=8;  /* DO A PUT */
                         DATA=CMDSTR;
                         END;
             END;
           END;
         END;
