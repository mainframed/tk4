   /* AHMSEDT - HARDWARE MANAGEMENT SYSTEM EDIT PROGRAM */
   %INCLUDE ABESUB;
 ABESUB(AHMSEDT) CHECK(YES);
   %INCLUDE ABESUBA;
   %INCLUDE AHMSEDI;

   DCL PLIRETV BUILTIN;
   DCL (LOCPTR,LOCNO_PTR,LOCATION_PTR,THINGPTR,FEATPTR,FDESPTR,
    FEATSEL,VENDPTR,NODE_PTR,ID_PTR) PTR;
   DCL VDPTR PTR; /* USED GLOBALLY BY DEVICE/FEATURES DISPLAY */
   DCL COPYPTR PTR; /* USED GLOBALLY BY DO_COPY */

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR LOCAL PROGRAM FUNCTION KEY DEFINITIONS.*
   *                                                                   *
   ********************************************************************/

   DCL 1 PFKS,
     2 PFKNAMES CHAR(240) INIT(
       '(ZPF02 ZPF03 ZPF04 ZPF05 ZPF06 ZPF07 ZPF08 ZPF09 ZPF10 ZPF11
 ZPF12 ZPF13 ZPF14 ZPF15 ZPF16 ZPF17 ZPF18 ZPF19 ZPF20 ZPF21 ZPF22
 ZPF23 ZPF24      ZPF2 ZPF3 ZPF4 ZPF5 ZPF6 ZPF7 ZPF8 ZPF9)'),
     2 PFKTABLE(31) CHAR(2) INIT(
            '02', '03', '04', '05', '06', '07', '08', '09', '10', '11',
      '12', '13', '02', '03', '04', '05', '06', '07', '08', '21', '22',
      '23', '24',       '02', '03', '04', '05', '06', '07', '08', '09'),
     2 PFKLEN FIXED BIN(31,0) INIT(2);
   DCL HELPCON CHAR(4) INIT('HELP'),
       HELPLEN FIXED BIN(31,0) INIT(4);

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR VARIABLES IN THE THING TABLE EDIT      *
   * PANEL.                                                            *
   *                                                                   *
   ********************************************************************/

   DCL 1 PARMS,
     2 NAMES CHAR(200) INIT(
     '(ZCMD MSG ID CID PU LTER LOCNO LOC EXT ROOM TVENDOR DEVICE SERIAL
     CIDNODE PONO LU DEST EXPDT STATDATE STATUS DISTR OWNTYPE MID)'),
     2 TABLE(23) CHAR(80) INIT((23)(1)' '),
     2 LEN FIXED BIN(31,0) INIT(80),
     2 LOCATE_DATA CHAR(255) INIT(' '),
     2 LOCATE_LEN FIXED BIN(31,0) INIT(255);
   DCL ZCMDIX     FIXED BIN INIT( 1);
   DCL MSGIX      FIXED BIN INIT( 2);
   DCL IDIX       FIXED BIN INIT( 3);
   DCL CIDIX      FIXED BIN INIT( 4);
   DCL PUIX       FIXED BIN INIT( 5);
   DCL LTERIX     FIXED BIN INIT( 6);
   DCL LOCNOIX    FIXED BIN INIT( 7);
   DCL LOCIX      FIXED BIN INIT( 8);
   DCL EXTIX      FIXED BIN INIT( 9);
   DCL ROOMIX     FIXED BIN INIT(10);
   DCL TVENDRIX   FIXED BIN INIT(11);
   DCL DEVICEIX   FIXED BIN INIT(12);
   DCL SERIALIX   FIXED BIN INIT(13);
   DCL CIDNODEIX  FIXED BIN INIT(14);
   DCL PONOIX     FIXED BIN INIT(15);
   DCL LUIX       FIXED BIN INIT(16);
   DCL DESTIX     FIXED BIN INIT(17);
   DCL EXPDTIX    FIXED BIN INIT(18);
   DCL STATDATEIX FIXED BIN INIT(19);
   DCL STATUSIX   FIXED BIN INIT(20);
   DCL DISTRIX    FIXED BIN INIT(21);
   DCL OWNTYPEIX  FIXED BIN INIT(22);
   DCL MIDIX      FIXED BIN INIT(23);
   DCL OPTION FIXED BIN(15,0) INIT(0);
   DCL MSG CHAR(80) INIT('');
   DCL MAXID FIXED BIN(31,0) INIT(0);

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR VARIABLES IN THE FEATURE DESCRIPTION   *
   * EDIT PANEL.                                                       *
   *                                                                   *
   ********************************************************************/

   DCL FDNAMES CHAR(100) INIT(
     '(FMSG FFID FVENDOR FDEVICE FFEATUR FFEATDE)'),
     FDTABLE(6) CHAR(80) INIT((6)(1)' '),
     FDLEN FIXED BIN(31,0) INIT(80),
     FDLOCATE_DATA CHAR(255) INIT(' '),
     FDLOCATE_LEN FIXED BIN(31,0) INIT(255);
   DCL FMSGIX    FIXED BIN INIT( 1),
       FFIDIX    FIXED BIN INIT( 2),
       FVENDORIX FIXED BIN INIT( 3),
       FDEVICEIX FIXED BIN INIT( 4),
       FFEATURIX FIXED BIN INIT( 5),
       FFEATDEIX FIXED BIN INIT( 6);
   DCL FOPTION FIXED BIN(15,0) INIT(0);
   DCL FMSG CHAR(80) INIT('');
   DCL MAXFID FIXED BIN(31,0) INIT(0);

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR VARIABLES IN THE LOCNAMES EDIT PANEL.  *
   *                                                                   *
   ********************************************************************/

   DCL LNAMES CHAR(100) INIT(
     '(LMSG LLOCN SPEED LLOCAT)'),
     LTABLE(4) CHAR(80) INIT((4)(1)' '),
     LLEN FIXED BIN(31,0) INIT(80),
     LLOCATE_DATA CHAR(255) INIT(' '),
     LLOCATE_LEN FIXED BIN(31,0) INIT(255);
   DCL LMSGIX    FIXED BIN INIT( 1),
       LLOCNIX   FIXED BIN INIT( 2),
       SPEEDIX   FIXED BIN INIT( 3),
       LLOCATIX  FIXED BIN INIT( 4);
   DCL LOPTION FIXED BIN(15,0) INIT(0);
   DCL LMSG CHAR(80) INIT('');
   DCL MAXLOC FIXED BIN(31,0) INIT(0);

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR VARIABLES IN THE VENDORS EDIT PANEL.   *
   *                                                                   *
   ********************************************************************/

   DCL VNAMES CHAR(100) INIT(
     '(VMSG VENDOR VENDNAME VENDADDR VENDCITY VENDSTAT VENDZIP
 VENDTELE)'),
     VTABLE(8) CHAR(80) INIT((8)(1)' '),
     VLEN FIXED BIN(31,0) INIT(80),
     VLOCATE_DATA CHAR(255) INIT(' '),
     VLOCATE_LEN  FIXED BIN(31,0) INIT(255);
   DCL VMSGIX     FIXED BIN INIT( 1),
       VENDORIX   FIXED BIN INIT( 2),
       VENDNAMEIX FIXED BIN INIT( 3),
       VENDADDRIX FIXED BIN INIT( 4),
       VENDCITYIX FIXED BIN INIT( 5),
       VENDSTATIX FIXED BIN INIT( 6),
       VENDZIPIX  FIXED BIN INIT( 7),
       VENDTELEIX FIXED BIN INIT( 8);
   DCL VOPTION FIXED BIN(15,0) INIT(0);
   DCL VMSG CHAR(80) INIT('');

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR THE DEVICE/FEATURES PANEL (AHMSGPNL).  *
   *                                                                   *
   ********************************************************************/

   DCL GMISC_NAMES CHAR(200) INIT('(GMSG GID GVENDOR GDEVICE GSERIAL
     GPU GLU)'),
       GMISC_LEN FIXED BIN(31,0) INIT(80),
       GMISC_TABLE(7) CHAR(80) INIT((7)(1)' '),
       GMSGIX    FIXED BIN INIT(1),
       GIDIX     FIXED BIN INIT(2),
       GVENDORIX FIXED BIN INIT(3),
       GDEVICEIX FIXED BIN INIT(4),
       GSERIALIX FIXED BIN INIT(5),
       GPUIX     FIXED BIN INIT(6),
       GLUIX     FIXED BIN INIT(7);
   DCL GG_NAMES CHAR(100) INIT('(G01 G02 G03 G04 G05 G06 G07 G08 G09 G10
         G11 G12 G13 G14 G15 G16)'),
       GG_LEN FIXED BIN(31,0) INIT(2),
       GG_TABLE(16) CHAR(2) INIT((16)(2)' ');
   DCL GFID_NAMES CHAR(240) INIT('(GFID01 GFID02 GFID03 GFID04 GFID05
         GFID06 GFID07 GFID08 GFID09 GFID10 GFID11 GFID12 GFID13 GFID14
         GFID15 GFID16)'),
       GFID_LEN FIXED BIN(31,0) INIT(7),
       GFID_TABLE(16) CHAR(7) INIT((16)(1)' ');
   DCL GFEAT_NAMES CHAR(240) INIT('(GFEAT01 GFEAT02 GFEAT03 GFEAT04
         GFEAT05 GFEAT06 GFEAT07 GFEAT08 GFEAT09 GFEAT10 GFEAT11 GFEAT12
         GFEAT13 GFEAT14 GFEAT15 GFEAT16)'),
       GFEAT_LEN FIXED BIN(31,0) INIT(12),
       GFEAT_TABLE(16) CHAR(12) INIT((12)(1)' ');
   DCL GDESC_NAMES CHAR(240) INIT('(GDESC01 GDESC02 GDESC03 GDESC04
         GDESC05 GDESC06 GDESC07 GDESC08 GDESC09 GDESC10 GDESC11 GDESC12
         GDESC13 GDESC14 GDESC15 GDESC16)'),
       GDESC_LEN FIXED BIN(31,0) INIT(30),
       GDESC_TABLE(16) CHAR(30) INIT((16)(1)' ');
   DCL GDTE_NAMES CHAR(240) INIT('(GDTE01 GDTE02 GDTE03 GDTE04
         GDTE05 GDTE06 GDTE07 GDTE08 GDTE09 GDTE10 GDTE11 GDTE12
         GDTE13 GDTE14 GDTE15 GDTE16)'),
       GDTE_LEN FIXED BIN(31,0) INIT(6),
       GDTE_TABLE(16) CHAR(6) INIT((16)(1)' ');
   DCL GPO_NAMES CHAR(240) INIT('(GPO01 GPO02 GPO03 GPO04
         GPO05 GPO06 GPO07 GPO08 GPO09 GPO10 GPO11 GPO12
         GPO13 GPO14 GPO15 GPO16)'),
       GPO_LEN FIXED BIN(31,0) INIT(10),
       GPO_TABLE(16) CHAR(10) INIT((16)(1)' ');
   DCL S_NAMES CHAR(240) INIT('(S01 S02 S03 S04 S05 S06 S07 S08 S09 S10
         S11 S12 S13 S14 S15 S16)'),
       S_LEN FIXED BIN(31,0) INIT(1),
       S_TABLE(16) CHAR(1) INIT((16)(1)' ');
   DCL H_NAMES CHAR(240) INIT('(H01 H02 H03 H04 H05 H06 H07 H08 H09 H10
         H11 H12 H13 H14 H15 H16)'),
       H_LEN FIXED BIN(31,0) INIT(3),
       H_TABLE(16) CHAR(3) INIT((16)(1)' ');
   DCL GCNT FIXED BIN(15,0) STATIC INIT(16); /* DISPLAY COUNT */
   DCL GOPTION FIXED BIN(15,0) INIT(0);
   DCL GMSG CHAR(80) INIT('');

   /********************************************************************
   *                                                                   *
   * FOLLOWING DECLARES ARE FOR THE HMS MAIN EDIT PANEL.               *
   *                                                                   *
   ********************************************************************/

   DCL MOPTION FIXED BIN(15,0),
       MMSG CHAR(79) INIT(' '),
       MMSGLEN FIXED BIN(31,0) INIT(79);

   /***** START OF INITIALIZATION CODE ********************************/

   VDPTR,COPYPTR=NULL;

   /* LOCATE FEATDESC TABLE */
   FDESPTR=LOCDB('FEATDESC');
   I=#CMD('ORDER BY FID',FDESPTR,RHHDRPT);

   /* LOCATE FEATURES TABLE */
   FEATPTR=LOCDB('FEATURES');

   /* NOW LOCATE LOCNAMES */
   LOCPTR=LOCDB('LOCNAMES');
   CALL FINDFLD(LOCPTR,'LOCNO');
   LOCNO_PTR=RHNAMPTR;
   CALL FINDFLD(LOCPTR,'LOCATION');
   LOCATION_PTR=RHNAMPTR;

   /* LOCATE THINGTAB */
   THINGPTR=LOCDB('THINGTAB');
   CALL FINDFLD(THINGPTR,'PU');
   NODE_PTR=RHNAMPTR;
   CALL FINDFLD(THINGPTR,'ID');
   ID_PTR=RHNAMPTR;

   /* LOCATE VENDORS TABLE */
   VENDPTR=LOCDB('VENDORS');

   /* LOCATE FEATSEL */
   FEATSEL=LOCDB('FEATSEL');

   /* NOW DEFINE VARIABLES TO ISPF */
   CALL #ISPLINK('VDEFINE ',PFKNAMES,PFKTABLE,'CHAR ',PFKLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VPUT ',PFKNAMES,'PROFILE ',' ',' ');
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(ZPF01)',HELPCON,'CHAR ',HELPLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VPUT ','(ZPF01)','PROFILE ',' ',' ');
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(ZPF1)',HELPCON,'CHAR ',HELPLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VPUT ','(ZPF1)','PROFILE ',' ',' ');
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',NAMES,TABLE,'CHAR ',LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(LOCATE)',LOCATE_DATA,'CHAR ',LOCATE_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',FDNAMES,FDTABLE,'CHAR ',FDLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(FLOCATE)',FDLOCATE_DATA,'CHAR ',
     FDLOCATE_LEN);
   CALL #ISPLINK('VDEFINE ',LNAMES,LTABLE,'CHAR ',LLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(LLOCATE)',LLOCATE_DATA,'CHAR ',
     LLOCATE_LEN);
   CALL #ISPLINK('VDEFINE ',VNAMES,VTABLE,'CHAR ',VLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(VLOCATE)',VLOCATE_DATA,'CHAR ',
     VLOCATE_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ','(MMSG)',MMSG,'CHAR ',MMSGLEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GMISC_NAMES,GMISC_TABLE,'CHAR ',GMISC_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GG_NAMES,GG_TABLE,'CHAR ',GG_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GFID_NAMES,GFID_TABLE,'CHAR ',GFID_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GFEAT_NAMES,GFEAT_TABLE,'CHAR ',GFEAT_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GDESC_NAMES,GDESC_TABLE,'CHAR ',GDESC_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',H_NAMES,H_TABLE,'CHAR ',H_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',S_NAMES,S_TABLE,'CHAR ',S_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GPO_NAMES,GPO_TABLE,'CHAR ',GPO_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL #ISPLINK('VDEFINE ',GDTE_NAMES,GDTE_TABLE,'CHAR ',GDTE_LEN);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;

   /* MAINLINE */
   DO FOREVER=1 REPEAT FOREVER;
     CALL DISPLAY_LINK('AHMSMPNL ',' ',MOPTION);
     MMSG='';
     SELECT(MOPTION);
       WHEN(4) CALL GENL_EDIT(DISPLAY,OPTION,LOCATE_DATA,THINGPTR,
         DO_ADD,DO_DEL,MSG,'ID',MAXID,ODD_OPTION_THING);
       WHEN(5) CALL GENL_EDIT(FEAT_DISPLAY,FOPTION,FDLOCATE_DATA,
         FDESPTR,DO_ADD,FEAT_DO_DEL,FMSG,'FID',MAXFID,ODD_OPTION);
       WHEN(6) CALL GENL_EDIT(LOC_DISPLAY,LOPTION,LLOCATE_DATA,
         LOCPTR,DO_ADD,LOC_DO_DEL,LMSG,'LOCNO',MAXLOC,ODD_OPTION);
       WHEN(7) CALL GENL_EDIT(VEND_DISPLAY,VOPTION,VLOCATE_DATA,
         VENDPTR,VEND_DO_ADD,VEND_DO_DEL,VMSG,'VENDOR',0,ODD_OPTION);
       WHEN(2) DO; /* END NOSAVE */
         CALL END_NOSAVE_PROC;
         CMDDATA='END NOSAVE';
         RHFLUSH='1'B;
         RETURN;
         END;
       WHEN(3) DO; /* END SAVE */
         IF END_SAVE_PROC
           THEN DO;
             CALL END_NOSAVE_PROC;
             CMDDATA='END NOSAVE';
             RHFLUSH='1'B;
             RETURN;
             END;
           ELSE MMSG='SAVE UNSUCCESSFUL - YOU MAY RETRY';
         END;
       WHEN(8) DO;
         MSGDATA='TO REENTER HMS EDIT PANELS, ENTER "PGM AHMSEDT"';
         CMDDATA='LINES 9999';
         RETURN;
         END;
       OTHERWISE MMSG='INVALID OPTION='××MOPTION;
       END; /* SELECT */
     END;

 DISPLAY: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO DISPLAY THE CURRENT RECORD.               *
 *                                                                     *
 **********************************************************************/
   DCL CURSORLOC CHAR(9);
   DCL (CIDNODE,LOC) CHAR(80);

   TABLE(MSGIX)=MSG;
   MSG='';
   TABLE(IDIX)      =GETDATA('ID',THINGPTR);
   TABLE(CIDIX)     =GETDATA('CID',THINGPTR);
   TABLE(MIDIX)     =GETDATA('MID',THINGPTR);
   TABLE(CIDNODEIX),CIDNODE=GETNODE(GETDATA_FLOAT('CID',THINGPTR));
   TABLE(PUIX)      =GETDATA('PU',THINGPTR);
   TABLE(LUIX)      =GETDATA('LU',THINGPTR);
   TABLE(LTERIX)    =GETDATA('LTERM',THINGPTR);
   TABLE(DESTIX)    =GETDATA('DEST',THINGPTR);
   TABLE(LOCNOIX)   =GETDATA('LOCNO',THINGPTR);
   TABLE(LOCIX),LOC =GETLOC(GETDATA('LOCNO',THINGPTR));
   TABLE(EXTIX)     =GETDATA('EXT',THINGPTR);
   TABLE(ROOMIX)    =GETDATA('ROOM',THINGPTR);
   TABLE(TVENDRIX)  =GETDATA('VENDOR',THINGPTR);
   TABLE(DEVICEIX)  =GETDATA('DEVICE',THINGPTR);
   TABLE(SERIALIX)  =GETDATA('SERIAL',THINGPTR);
   TABLE(PONOIX)    =GETDATA('PONO',THINGPTR);
   TABLE(OWNTYPEIX) =GETDATA('OWNTYPE',THINGPTR);
   TABLE(DISTRIX)   =GETDATA('DISTR',THINGPTR);
   TABLE(EXPDTIX)   =GETDATA('EXPDT',THINGPTR);
   TABLE(STATDATEIX)=GETDATA('STATDATE',THINGPTR);
   TABLE(STATUSIX)  =GETDATA('STATUS',THINGPTR);
   IF LOCATE_DATA=''
     THEN CURSORLOC='ZCMD';
     ELSE CURSORLOC='LOCATE';
   CALL DISPLAY_LINK('AHMSTPNL ',CURSORLOC,OPTION);
   CALL PUTDATA('CID',     THINGPTR,TABLE(CIDIX));
   CALL PUTDATA('MID',     THINGPTR,TABLE(MIDIX));
   CALL PUTDATA('PU',      THINGPTR,TABLE(PUIX));
   CALL PUTDATA('LU',      THINGPTR,TABLE(LUIX));
   CALL PUTDATA('LTERM',   THINGPTR,TABLE(LTERIX));
   CALL PUTDATA('DEST',    THINGPTR,TABLE(DESTIX));
   CALL PUTDATA('LOCNO',   THINGPTR,TABLE(LOCNOIX));
   CALL PUTDATA('EXT',     THINGPTR,TABLE(EXTIX));
   CALL PUTDATA('ROOM',    THINGPTR,TABLE(ROOMIX));
   CALL PUTDATA('VENDOR',  THINGPTR,TABLE(TVENDRIX));
   CALL PUTDATA('DEVICE',  THINGPTR,TABLE(DEVICEIX));
   CALL PUTDATA('SERIAL',  THINGPTR,TABLE(SERIALIX));
   CALL PUTDATA('PONO',    THINGPTR,TABLE(PONOIX));
   CALL PUTDATA('OWNTYPE', THINGPTR,TABLE(OWNTYPEIX));
   CALL PUTDATA('DISTR',   THINGPTR,TABLE(DISTRIX));
   CALL PUTDATA('EXPDT',   THINGPTR,TABLE(EXPDTIX));
   CALL PUTDATA('STATDATE',THINGPTR,TABLE(STATDATEIX));
   CALL PUTDATA('STATUS',  THINGPTR,TABLE(STATUSIX));
   IF TABLE(CIDNODEIX)^=CIDNODE
     THEN CALL DO_LOCATE('CONNECT WHERE PU='''××
       SUBSTR(TABLE(CIDNODEIX),1,SUFFIX(TABLE(CIDNODEIX),' '))××'''',
       THINGPTR,MSG);
   IF TABLE(LOCIX)^=LOC
     THEN DO; /* WANTS TO CHANGE LOCATION */
       I=#CMD('LOCATE WHERE LOCATION='''××
         SUBSTR(TABLE(LOCIX),1,SUFFIX(TABLE(LOCIX),' '))××'''',
         LOCPTR,RHHDRPT);
       IF I=0
         THEN CALL PUTDATA('LOCNO',THINGPTR,GETDATA('LOCNO',LOCPTR));
         ELSE MSG='LOCATION='''××TABLE(LOCIX)××''' NOT FOUND';
       END;
   END DISPLAY;

 DISPLAY_LINK: PROC(PANEL,CURSORLOC,OPTION);
 /**********************************************************************
 *                                                                     *
 * CALLED TO INTERFACE WITH ISPF TO DISPLAY A PANEL.                   *
 *                                                                     *
 **********************************************************************/
   DCL (PANEL,CURSORLOC) CHAR(*);
   DCL OPTION FIXED BIN(15,0);

   TABLE(ZCMDIX)='';
   CALL #ISPLINK('DISPLAY ',PANEL,' ',CURSORLOC,' ');
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   IF TABLE(ZCMDIX)^=''
     THEN IF VERIFY(TABLE(ZCMDIX),'0123456789 ')=0
       THEN OPTION=TABLE(ZCMDIX);
       ELSE OPTION=999;
     ELSE OPTION=0;
   END DISPLAY_LINK;

 DO_ADD: PROC(ID,MAXID,TABPTR);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN AN ADD OPERATION IS REQUESTED.                          *
 *                                                                     *
 **********************************************************************/
   DCL ID CHAR(*);
   DCL MAXID FIXED BIN(31,0);
   DCL (F,TABPTR) PTR;
   DCL IDPIC PIC'ZZZZZ9';
   DCL I FIXED BIN;

   IF MAXID=0
     THEN CALL FIND_MAXID(ID,MAXID,TABPTR);
   MAXID=MAXID+1;
   IDPIC=MAXID;
   I=#CMD('B BL 1',TABPTR,RHHDRPT);
   I=#CMD('B V'   ,TABPTR,RHHDRPT);
   DO F=TABPTR->RHUSEHD REPEAT F->RHNLINK WHILE(F^=NULL);
     CALL PUTDATA(F->RHNNAME,TABPTR,'.');
     END;
   CALL PUTDATA(ID,TABPTR,SUBSTR(IDPIC,PREFIX(IDPIC,' ')));
   END DO_ADD;

 DO_COPY: PROC(LOCATE_DATA,MSG);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE COPY COMMAND IS ENTERED WHILE EDITING THINGTAB.     *
 *                                                                     *
 **********************************************************************/
   DCL LOCATE_DATA CHAR(*);
   DCL MSG CHAR(*);
   DCL I FIXED BIN;
   DCL T PTR;
   DCL FLOAT DEC FLOAT(16);
   DCL TEXT CHAR(256) VAR;

   CALL DO_COPY_COMMON(LOCATE_DATA,MSG);
   IF MSG^=''
     THEN RETURN;
   DO T=COPYPTR->RHUSEHD REPEAT T->RHNLINK WHILE(T^=NULL);
     IF T->RHNNAME^='ID'
       THEN IF T->RHNTYPE=2 /* CHAR? */
         THEN DO;
           TEXT=GETDATA(T->RHNNAME,COPYPTR);
           IF TEXT^=''
             THEN CALL PUTDATA(T->RHNNAME,THINGPTR,TEXT);
           END;
         ELSE DO;
           FLOAT=GETDATA_FLOAT(T->RHNNAME,COPYPTR);
           IF UNSPEC(FLOAT)^=UNSPEC(MISSING)
             THEN CALL PUTDATA(T->RHNNAME,THINGPTR,CALCPRT(FLOAT));
           END;
     END;
   LOCATE_DATA='';
   END DO_COPY;

 DO_COPY_COMMON: PROC(LOCATE_DATA,MSG);
 /**********************************************************************
 *                                                                     *
 * COMMON CODE FOR COPY AND EXCHANGE COMMANDS.                         *
 *                                                                     *
 **********************************************************************/
   DCL LOCATE_DATA CHAR(*);
   DCL MSG CHAR(*);
   DCL (I,J,K) FIXED BIN;
   DCL T PTR;
   DCL FLOAT DEC FLOAT(16);

   IF LOCATE_DATA=''
     THEN RETURN;
   LOCATE_DATA=SUBSTR(LOCATE_DATA,PREFIX(LOCATE_DATA,' '));
   J=INDEX(LOCATE_DATA,' ');
   IF J=0
     THEN RETURN;
   IF INDEX(LOCATE_DATA,' WHERE ')=0
     THEN DO;
       MSG='COMMAND MUST CONTAIN A WHERE CLAUSE';
       RETURN;
       END;
   IF COPYPTR=NULL
     THEN DO; /* MUST GET WORK DATA SET */
       COPYPTR=LOCDB('COPYSEL');
       IF COPYPTR=NULL
         THEN DO;
           COPYPTR=RHPTR;
           I=#CMD('ABE '''' NAME(COPYSEL) INP(USE THINGTAB)',COPYPTR,
             RHHDRPT);
           IF I^=0
             THEN DO;
               COPYPTR=NULL;
               IF MSGDATA^=''
                 THEN MSG=MSGDATA;
                 ELSE MSG='ERROR IN ALLOCATING WORK DATA SET='××I;
               RETURN;
               END;
           END;
       END;
   I=#CMD('USE ×',COPYPTR,RHHDRPT);
   I=#CMD('DEL',COPYPTR,RHHDRPT);
   I=#CMD('SELECT'××SUBSTR(LOCATE_DATA,J)××' FROM THINGTAB TO COPYSEL',
     RHPTR,RHHDRPT);
   IF I^=0
     THEN DO;
       IF MSGDATA=''
         THEN MSG='NONZERO RETURN FROM COPY='××I;
         ELSE MSG=MSGDATA;
       RETURN;
       END;
   IF COPYPTR->RECPTR=COPYPTR
     THEN DO;
       MSG='RECORD NOT FOUND';
       RETURN;
       END;
   END DO_COPY_COMMON;

 DO_DEL: PROC(MSG);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN A DEL OPERATION IS REQUESTED FOR THING TABLE.           *
 *                                                                     *
 **********************************************************************/
   DCL MSG CHAR(*);
   DCL SEQPIC PIC'ZZZZZZZ9';
   DCL (ID,CID) DEC FLOAT(16);
   DCL (RP,SAVPTR) PTR;
   DCL I FIXED BIN;

   IF THINGPTR->RECPTR=THINGPTR
     THEN RETURN; /* NO RECORD TO DELETE */
   ID=GETDATA_FLOAT('ID',THINGPTR);
   MSG='';
   SAVPTR=THINGPTR->RECPTR;
   I=#CMD('LOCATE WHERE CID='××CALCPRT(ID),THINGPTR,RHHDRPT);
   CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIGINAL CURRENT RECORD */
   IF I=0
     THEN MSG='CAN''T DELETE - ID APPEARS ELSEWHERE IN CID FIELD';
     ELSE DO; /* IT IS OK TO DELETE */
       I=#CMD('REMOVE WHERE ID='××CALCPRT(ID),FEATPTR,RHHDRPT);
       SEQPIC=THINGPTR->RECPTR->RECSEQ;
       I=#CMD(SEQPIC××' DEL',THINGPTR,RHHDRPT);
       END;
   END DO_DEL;

 DO_EXCHANGE: PROC(LOCATE_DATA,MSG);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE COPY COMMAND IS ENTERED WHILE EDITING THINGTAB.     *
 *                                                                     *
 **********************************************************************/
   DCL LOCATE_DATA CHAR(*);
   DCL MSG CHAR(*);
   DCL I FIXED BIN;
   DCL (T,PTR1,PTR2) PTR;
   DCL (FLOAT1,FLOAT2) DEC FLOAT(16);
   DCL (TEXT1,TEXT2) CHAR(256) VAR;

   CALL DO_COPY_COMMON(LOCATE_DATA,MSG);
   IF MSG^=''
     THEN RETURN;
   PTR1=THINGPTR->RECPTR; /* SAVE CURRENT POINTER */
   I=INDEX(LOCATE_DATA,' WHERE ');
   CALL DO_LOCATE('LOCATE'××SUBSTR(LOCATE_DATA,I),THINGPTR,MSG);
   IF MSG^=''
     THEN RETURN;
   PTR2=THINGPTR->RECPTR; /* SAVE LOCATED POINTER */
   DO T=COPYPTR->RHUSEHD REPEAT T->RHNLINK WHILE(T^=NULL);
     IF T->RHNNAME^='ID'
       THEN IF T->RHNTYPE=2 /* CHAR? */
         THEN DO;
           THINGPTR->RECPTR=PTR1;
           TEXT1=GETDATA(T->RHNNAME,THINGPTR);
           THINGPTR->RECPTR=PTR2;
           TEXT2=GETDATA(T->RHNNAME,THINGPTR);
           CALL PUTDATA(T->RHNNAME,THINGPTR,TEXT1);
           THINGPTR->RECPTR=PTR1;
           CALL PUTDATA(T->RHNNAME,THINGPTR,TEXT2);
           END;
         ELSE DO;
           THINGPTR->RECPTR=PTR1;
           FLOAT1=GETDATA_FLOAT(T->RHNNAME,THINGPTR);
           THINGPTR->RECPTR=PTR2;
           FLOAT2=GETDATA_FLOAT(T->RHNNAME,THINGPTR);
           CALL PUTDATA(T->RHNNAME,THINGPTR,CALCPRT(FLOAT1));
           THINGPTR->RECPTR=PTR1;
           CALL PUTDATA(T->RHNNAME,THINGPTR,CALCPRT(FLOAT2));
           END;
     END;
   CALL #NC(THINGPTR,PTR1); /* RESTORE ORIGINAL CURRENT RECORD */
   LOCATE_DATA='';
   END DO_EXCHANGE;

 DO_LOCATE: PROC(LOCATE_DATA,TABPTR,MSG) RECURSIVE;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN LOCATION CRITERIA HAS BEEN ENTERED AT THE TERMINAL.     *
 *                                                                     *
 **********************************************************************/
   DCL LOCATE_DATA CHAR(*);
   DCL TABPTR PTR;
   DCL MSG CHAR(*);
   DCL I FIXED BIN;
   DCL SAVPTR PTR;
   DCL CID DEC FLOAT(16);

   MSG='';
   IF INDEX(LOCATE_DATA,'LOCATE ')^=1
     THEN SELECT;
       WHEN(INDEX(LOCATE_DATA,'CONNECT ')=1 & TABPTR=THINGPTR) DO;
         SAVPTR=THINGPTR->RECPTR; /* SAVE CURRENT LINE # */
         CALL DO_LOCATE('LOCATE '××SUBSTR(LOCATE_DATA,8),THINGPTR,MSG);
         IF MSG=''
           THEN DO; /* FOUND OK */
             CID=GETDATA_FLOAT('ID',THINGPTR);
             CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIGINAL RECORD */
             CALL PUTDATA('CID',THINGPTR,CALCPRT(CID));
             LOCATE_DATA='';
             END;
         RETURN;
         END;
       WHEN(INDEX(LOCATE_DATA,'COPY ')=1 & TABPTR=THINGPTR) DO;
         CALL DO_COPY(LOCATE_DATA,MSG);
         RETURN;
         END;
       WHEN(INDEX(LOCATE_DATA,'EXCHANGE ')=1 & TABPTR=THINGPTR) DO;
         CALL DO_EXCHANGE(LOCATE_DATA,MSG);
         RETURN;
         END;
       WHEN(INDEX(LOCATE_DATA,'MOVE ')=1 & TABPTR=THINGPTR) DO;
         CALL DO_MOVE(LOCATE_DATA,MSG);
         RETURN;
         END;
       WHEN(INDEX(LOCATE_DATA,'ORDER BY ')=1) DO;
         I=#CMD(LOCATE_DATA,TABPTR,RHHDRPT);
         IF I=0
           THEN LOCATE_DATA='';
         RETURN;
         END;
       OTHERWISE DO;
         MSG='SELECTION CRITERIA MUST BEGIN WITH "LOCATE "';
         RETURN;
         END;
       END; /* SELECT */
   I=#CMD(LOCATE_DATA,TABPTR,RHHDRPT);
   MSG=TABPTR->MSGDATA;
   IF I^=0
     THEN IF MSG=''
       THEN MSG='NONZERO RETURN CODE FROM LOCATE='××I;
   END DO_LOCATE;

 DO_MOVE: PROC(LOCATE_DATA,MSG) RECURSIVE;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE MOVE COMMAND IS ENTERED WHILE EDITING THINGTAB.     *
 *                                                                     *
 **********************************************************************/
   DCL LOCATE_DATA CHAR(*);
   DCL MSG CHAR(*);
   DCL I FIXED BIN;
   DCL PARY(3) CHAR(256) VAR;
   DCL PARQCK(3) BIT(1);
   DCL NUMCK BIT(3) STATIC INIT('0'B);
   DCL (DELETE,ALL) CHAR(6) VAR INIT('');
   DCL (ID,MID,MIDNXT) FLOAT DEC(16);
   DCL (SAVPTR,MIDPTR) PTR;

   I=#ABEPRS(LOCATE_DATA,PARY,PARQCK,NUMCK);
   DO I=2 TO HBOUND(PARY,1) WHILE(PARY(I)^='');
     SELECT(PARY(I));
       WHEN('DELETE') DELETE='DELETE';
       WHEN('ALL') ALL='ALL';
       OTHERWISE DO;
         MSG='INVALID OPERAND='××PARY(I);
         RETURN;
         END;
       END; /* SELECT */
     END;
   MID=GETDATA_FLOAT('MID',THINGPTR);
   IF UNSPEC(MID)=UNSPEC(MISSING)
     THEN DO;
       MSG='MID IS MISSING VALUES';
       RETURN;
       END;
   ID=GETDATA_FLOAT('ID',THINGPTR);
   IF ID=MID
     THEN DO;
       MSG='MID CANNOT EQUAL ID';
       RETURN;
       END;
   SAVPTR=THINGPTR->RECPTR;
   I=#CMD('LOCATE WHERE MID='××CALCPRT(ID),THINGPTR,RHHDRPT);
   IF I=0
     THEN DO;
       CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIG RECORD */
       MSG='MOVE DEPENDENCIES EXIST';
       RETURN;
       END;
   I=#CMD('LOCATE WHERE ID='××CALCPRT(MID),THINGPTR,RHHDRPT);
   IF I^=0
     THEN DO;
       MSG='RECORD WITH ID='××CALCPRT(MID)××' NOT FOUND';
       RETURN;
       END;
   MIDPTR=THINGPTR->RECPTR;
   MIDNXT=GETDATA_FLOAT('MID',THINGPTR);
   I=#CMD('UPDATE SET CID='××CALCPRT(ID)××' WHERE CID='××CALCPRT(MID),
     THINGPTR,RHHDRPT);
   IF I^=0
     THEN SIGNAL ERROR;
   CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIGINAL CURRENT RECORD */
   CALL DO_COPY(
     'COPY CID,DEST,EXT,LOCATION,LOCNO,LTERM,LU,PU,ROOM WHERE ID='
     ××CALCPRT(MID),MSG);
   IF MSG^=''
     THEN SIGNAL ERROR;
   CALL PUTDATA('MID',THINGPTR,''); /* SET MID TO MISSING */
   CALL #NC(THINGPTR,MIDPTR); /* POINT TO NEXT REC IN CHAIN */
   IF UNSPEC(MIDNXT)^=UNSPEC(MISSING)
     THEN IF ALL='ALL'
       THEN CALL DO_MOVE('MOVE ALL '××DELETE,MSG);
       ELSE IF DELETE='DELETE'
         THEN MSG='DELETE OPTION IGNORED - A DEPENDENCY IS INVOLVED';
         ELSE;
     ELSE IF DELETE='DELETE'
       THEN CALL DO_DEL(MSG);
   CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIG REC */
   IF MSG=''
     THEN LOCATE_DATA='';
   END DO_MOVE;

 END_NOSAVE_PROC: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN AN "END NOSAVE" IS TO BE DONE FOR ALL CURRENTLY EDITED  *
 * DATA SETS.                                                          *
 *                                                                     *
 **********************************************************************/
   DCL I FIXED BIN;

   I=#CMD('A THINGTAB END N',RHPTR,RHHDRPT);
   I=#CMD('A LOCNAMES END N',RHPTR,RHHDRPT);
   I=#CMD('A FEATURES END N',RHPTR,RHHDRPT);
   I=#CMD('A FEATDESC END N',RHPTR,RHHDRPT);
   I=#CMD('A VENDORS  END N',RHPTR,RHHDRPT);
   I=#CMD('A FEATSEL  END N',RHPTR,RHHDRPT);
   I=#CMD('A VDSEL    END N',RHPTR,RHHDRPT);
   I=#CMD('A COPYSEL  END N',RHPTR,RHHDRPT);
   END END_NOSAVE_PROC;

 END_SAVE_PROC: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN AN "END SAVE" IS TO BE DONE FOR ALL CURRENTLY EDITED    *
 * DATA SETS.                                                          *
 *                                                                     *
 **********************************************************************/
   DCL I FIXED BIN;

   I=#CMD('PGM AHMSEDS',RHPTR,RHHDRPT);
   IF I=0
     THEN RETURN('1'B); /* SAVE OK */
     ELSE RETURN('0'B);
   END END_SAVE_PROC;

 FEAT_DISPLAY: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO DISPLAY THE CURRENT RECORD IN FEATDESC.   *
 *                                                                     *
 **********************************************************************/
   DCL CURSORLOC CHAR(9);

   FDTABLE(FMSGIX)=FMSG;
   FMSG='';
   FDTABLE(FFIDIX)   =GETDATA('FID',     FDESPTR);
   FDTABLE(FVENDORIX)=GETDATA('VENDOR',  FDESPTR);
   FDTABLE(FDEVICEIX)=GETDATA('DEVICE',  FDESPTR);
   FDTABLE(FFEATURIX)=GETDATA('FEATURE', FDESPTR);
   FDTABLE(FFEATDEIX)=GETDATA('FEATDESC',FDESPTR);
   IF FDLOCATE_DATA=''
     THEN CURSORLOC='ZCMD';
     ELSE CURSORLOC='FLOCATE';
   CALL DISPLAY_LINK('AHMSFPNL ',CURSORLOC,FOPTION);
   IF PLIRETV^=0
     THEN SIGNAL ERROR;
   CALL PUTDATA('VENDOR',  FDESPTR,FDTABLE(FVENDORIX));
   CALL PUTDATA('DEVICE',  FDESPTR,FDTABLE(FDEVICEIX));
   CALL PUTDATA('FEATURE', FDESPTR,FDTABLE(FFEATURIX));
   CALL PUTDATA('FEATDESC',FDESPTR,FDTABLE(FFEATDEIX));
   END FEAT_DISPLAY;

 FEAT_DO_DEL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN A DEL OPERATION IS REQUESTED FOR FEATDESC TABLE.        *
 *                                                                     *
 **********************************************************************/
   DCL SEQPIC PIC'ZZZZZZZ9';
   DCL (ID,CID) DEC FLOAT(16);
   DCL (RP,SAVPTR) PTR;
   DCL I FIXED BIN;

   IF FDESPTR->RECPTR=FDESPTR
     THEN RETURN; /* NO RECORD TO DELETE */
   FID=GETDATA_FLOAT('FID',FDESPTR);
   FMSG='';
   SAVPTR=FDESPTR->RECPTR;
   I=#CMD('LOCATE WHERE FID='××CALCPRT(FID),FEATPTR,RHHDRPT);
   CALL #NC(FDESPTR,SAVPTR); /* RESTORE ORIGINAL CURRENT RECORD */
   IF I=0
     THEN FMSG='CAN''T DELETE - FID APPEARS IN FEATURES TABLE';
     ELSE DO; /* IT IS OK TO DELETE */
       SEQPIC=FDESPTR->RECPTR->RECSEQ;
       I=#CMD(SEQPIC××' DEL',FDESPTR,RHHDRPT);
       END;
   END FEAT_DO_DEL;

 FEAT_SETUP: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN ALL THE FEATURES FOR THE CURRENT RECORD.           *
 *                                                                     *
 **********************************************************************/
   DCL I FIXED BIN;

   I=#CMD('DEL',FEATSEL,RHHDRPT);
   I=#CMD('SELECT * FROM FEATURES TO FEATSEL WHERE ID='××
     CALCPRT(GETDATA_FLOAT('ID',THINGPTR))××' ORDER BY FID',
     FEATSEL,RHHDRPT);
   END FEAT_SETUP;

 FIND_MAXID: PROC(ID,MAXID,TABPTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO LOCATE THE LARGEST ID CURRENTLY IN THINGTAB.              *
 *                                                                     *
 **********************************************************************/
   DCL ID CHAR(*);
   DCL MAXID FIXED BIN(31,0);
   DCL TABPTR PTR;
   DCL FPTR PTR;
   DCL VALCH CHAR(50) VAR;

   CALL FINDFLD(TABPTR,ID);
   FPTR=TABPTR;
   MAXID=0;
   DO FOREVER=1 REPEAT FOREVER;
     FPTR=#SUCC(RHHDRPT,TABPTR,FPTR);
     IF FPTR=TABPTR
       THEN LEAVE;
     VALCH=GETDATA_SUB(FPTR,RHNAMPTR);
     IF VERIFY(VALCH,' 0123456789')=0
       THEN MAXID=MAX(MAXID,VALCH);
     END;

   END FIND_MAXID;

 FORMAT: PROC(VALUE) RETURNS(CHAR(50) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO FORMAT A NUMERIC VALUE FOR DISPLAY.                       *
 *                                                                     *
 **********************************************************************/
   DCL VALUE FIXED BIN(31,0);
   DCL PIC PIC'ZZZZZZZ9';
   IF VALUE=0
     THEN RETURN('');
   PIC=VALUE;
   RETURN(SUBSTR(PIC,PREFIX(PIC,' ')));
   END FORMAT;

 GENL_EDIT: PROC(DISPLAY,OPTION,LOCATE_DATA,TABPTR,DO_ADD,DO_DEL,
   MSG,ID,MAXID,ODD_OPTION);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE USER HAS REQUESTED EDITING OF A TABLE.              *
 *                                                                     *
 **********************************************************************/
   DCL (DISPLAY,DO_ADD,DO_DEL,ODD_OPTION) ENTRY;
   DCL OPTION FIXED BIN(15,0);
   DCL LOCATE_DATA CHAR(*);
   DCL TABPTR PTR;
   DCL MSG CHAR(*);
   DCL ID CHAR(*);
   DCL MAXID FIXED BIN(31,0);

   DO FOREVER=1 REPEAT FOREVER;
     CALL DISPLAY;
     SELECT(OPTION);
       WHEN(0);   /* NULL OPTION */
       WHEN(7)    /* PRIOR */
         IF LOCATE_DATA=''
           THEN DO;
             TABPTR->RECPTR=#PRED(RHHDRPT,TABPTR,TABPTR->RECPTR);
             IF TABPTR->RECPTR=TABPTR
               THEN CALL #NC(TABPTR,TABPTR->LSTHDR);
             END;
       WHEN(8)    /* NEXT */
         IF LOCATE_DATA=''
           THEN DO;
             TABPTR->RECPTR=#SUCC(RHHDRPT,TABPTR,TABPTR->RECPTR);
             IF TABPTR->RECPTR=TABPTR
               THEN CALL #NC(TABPTR,TABPTR->NXTHDR);
             END;
       WHEN(4) DO;/* ADD */
         CALL DO_ADD(ID,MAXID,TABPTR);
         END;
       WHEN(5)    /* DEL */
         CALL DO_DEL(MSG);
       WHEN(3)    /* RETURN TO MAIN HMS MENU */
         RETURN;
       OTHERWISE
         CALL ODD_OPTION(OPTION,MSG);
       END; /* SELECT */
     IF LOCATE_DATA^=''
       THEN CALL DO_LOCATE(LOCATE_DATA,TABPTR,MSG);
     END;
   END GENL_EDIT;

 GETDATE: PROC(STR) RETURNS(CHAR(6));
 /**********************************************************************
 *                                                                     *
 * CALLED TO CONVERT A DATE FROM SAS FORMAT TO MMDDYY.                 *
 *                                                                     *
 **********************************************************************/
   DCL STR CHAR(*);
   DCL RETSTR CHAR(6);
   DCL I FIXED BIN;

   IF VERIFY(STR,'0123456789')^=0
     THEN RETURN('.');
   I=#CMD('CALC SASDATE('××STR××')',RHPTR,RHHDRPT);
   RETSTR=MSGDATA;
   MSGDATA='';
   RETURN(SUBSTR(RETSTR,3,4)××SUBSTR(RETSTR,1,2));
   END GETDATE;

 GETLOC: PROC(KEY) RETURNS(CHAR(50) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A LOCATION DESCRIPTION GIVEN A LOCNO.              *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL CURPTR PTR;

   CURPTR=NULL;
   DO FOREVER=1 REPEAT FOREVER;
     CURPTR=#SUCC(RHHDRPT,LOCPTR,CURPTR);
     IF CURPTR=LOCPTR
       THEN RETURN('MISSING');
     IF KEY=GETDATA_SUB(CURPTR,LOCNO_PTR)
       THEN RETURN(GETDATA_SUB(CURPTR,LOCATION_PTR));
     END;
   END GETLOC;

 GETNODE: PROC(KEY) RETURNS(CHAR(50) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A PU   FROM THINGTAB GIVEN AN ID.                  *
 *                                                                     *
 **********************************************************************/
   DCL KEY DEC FLOAT(16);
   DCL CURPTR PTR;

   IF KEY=MISSING
     THEN RETURN('MISSING');
   CURPTR=NULL;
   DO FOREVER=1 REPEAT FOREVER;
     CURPTR=#SUCC(RHHDRPT,THINGPTR,CURPTR);
     IF CURPTR=THINGPTR
       THEN RETURN('MISSING');
     IF KEY=GETDATA_FLOAT_SUB(CURPTR,ID_PTR)
       THEN RETURN(GETDATA_SUB(CURPTR,NODE_PTR));
     END;
   END GETNODE;

 LOC_DISPLAY: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO DISPLAY THE CURRENT RECORD IN LOCNAMES.   *
 *                                                                     *
 **********************************************************************/
   DCL CURSORLOC CHAR(9);

   LTABLE(LMSGIX)=LMSG;
   LMSG='';
   LTABLE(LLOCNIX) =GETDATA('LOCNO'   ,LOCPTR);
   LTABLE(SPEEDIX) =GETDATA('SPEED'   ,LOCPTR);
   LTABLE(LLOCATIX)=GETDATA('LOCATION',LOCPTR);
   IF LLOCATE_DATA=''
     THEN CURSORLOC='ZCMD';
     ELSE CURSORLOC='LLOCATE';
   CALL DISPLAY_LINK('AHMSLPNL ',CURSORLOC,LOPTION);
   CALL PUTDATA('SPEED'   ,LOCPTR,LTABLE(SPEEDIX));
   CALL PUTDATA('LOCATION',LOCPTR,LTABLE(LLOCATIX));
   END LOC_DISPLAY;

 LOC_DO_DEL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN A DEL OPERATION IS REQUESTED FOR LOCNAMES TABLE.        *
 *                                                                     *
 **********************************************************************/
   DCL SEQPIC PIC'ZZZZZZZ9';
   DCL LOCNO DEC FLOAT(16);
   DCL SAVPTR PTR;
   DCL I FIXED BIN;

   IF LOCPTR->RECPTR=LOCPTR
     THEN RETURN; /* NO RECORD TO DELETE */
   LOCNO=GETDATA_FLOAT('LOCNO',LOCPTR);
   LMSG='';
   SAVPTR=THINGPTR->RECPTR;
   I=#CMD('LOCATE WHERE LOCNO='××CALCPRT(LOCNO),THINGPTR,RHHDRPT);
   CALL #NC(THINGPTR,SAVPTR); /* RESTORE ORIGINAL CURRENT RECORD */
   IF I=0
     THEN LMSG='CAN''T DELETE - LOCNO APPEARS IN FEATURES TABLE';
     ELSE DO; /* IT IS OK TO DELETE */
       SEQPIC=LOCPTR->RECPTR->RECSEQ;
       I=#CMD(SEQPIC××' DEL',LOCPTR,RHHDRPT);
       END;
   END LOC_DO_DEL;

 ODD_OPTION: PROC(OPTION,MSG);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN AN OPTION IS ENTERED THAT GENL_EDIT DOES NOT RECOGNIZE. *
 *                                                                     *
 **********************************************************************/
   DCL OPTION FIXED BIN(15,0);
   DCL MSG CHAR(*);

   MSG='INVALID OPTION='××OPTION;
   END ODD_OPTION;

 ODD_OPTION_THING: PROC(OPTION,MSG);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN AN OPTION IS ENTERED THAT GENL_EDIT DOES NOT RECOGNIZE, *
 * AND THINGTAB IS CURRENTLY BEING EDITED.                             *
 *                                                                     *
 **********************************************************************/
   DCL OPTION FIXED BIN(15,0);
   DCL MSG CHAR(*);
   DCL (SAVPTR,PLACE) PTR;
   DCL (I,J,K) FIXED BIN(31,0);

   IF OPTION^=6
     THEN DO;
       MSG='INVALID OPTION='××OPTION;
       RETURN;
       END;

   CALL FEAT_SETUP; /* ADDED WHEN FEATURES REMOVED FROM THINGTAB EDIT */

   /* OPTION 6 - DISPLAY ALL FEATURES FOR THIS DEVICE TYPE */
   GMISC_TABLE(GIDIX)    =GETDATA('ID'    ,THINGPTR);
   GMISC_TABLE(GVENDORIX)=GETDATA('VENDOR',THINGPTR);
   GMISC_TABLE(GDEVICEIX)=GETDATA('DEVICE',THINGPTR);
   GMISC_TABLE(GSERIALIX)=GETDATA('SERIAL',THINGPTR);
   GMISC_TABLE(GPUIX)    =GETDATA('PU'    ,THINGPTR);
   GMISC_TABLE(GLUIX)    =GETDATA('LU'    ,THINGPTR);
   IF GMISC_TABLE(GVENDORIX)='' × GMISC_TABLE(GDEVICEIX)=''
     THEN DO;
       MSG='VENDOR OR DEVICE IS MISSING';
       RETURN;
       END;
   IF VDPTR=NULL
     THEN DO; /* MUST GET WORK DATA SET */
       VDPTR=LOCDB('VDSEL');
       IF VDPTR=NULL
         THEN DO;
           VDPTR=RHPTR;
           I=#CMD('ABE '''' NAME(VDSEL)',VDPTR,RHHDRPT);
           IF I^=0
             THEN DO;
               VDPTR=NULL;
               IF MSGDATA^=''
                 THEN MSG=MSGDATA;
                 ELSE MSG='ERROR IN ALLOCATING WORK DATA SET='××I;
               RETURN;
               END;
           END;
       END;
   I=#CMD('DEL',VDPTR,RHHDRPT);
   I=#CMD('SELECT * TO VDSEL FROM FEATDESC,FEATSEL WHERE (VENDOR='''××
     GMISC_TABLE(GVENDORIX)××''' & DEVICE='''××
     GMISC_TABLE(GDEVICEIX)××''') × QTY^=. MERGE BY FID',
     RHPTR,RHHDRPT);
   IF I^=0
     THEN DO;
       IF MSGDATA^=''
         THEN MSG=MSGDATA;
         ELSE MSG='ERROR WHEN SELECTING FROM FEATDESC';
       RETURN;
       END;
   GOPTION=0;
   K=1; /* CURRENT DISPLAY LINE */
   DO WHILE(GOPTION^=2 & GOPTION^=3);
     SELECT(GOPTION);
       WHEN(0); /* NOOP */
       WHEN(7) K=MAX(1,K-(GCNT+1)); /* PAGE BACK */
       WHEN(8) K=MIN(GCNT,K+(GCNT+1)); /* PAGE FORWARD */
       OTHERWISE MSG='INVALID OPTION='××GOPTION;
       END; /* SELECT */
     I=#CMD('TOP',VDPTR,RHHDRPT);
     IF VDPTR->RECPTR^=VDPTR->HDPTR
       THEN DO J=1 TO K-1 WHILE(VDPTR->RECPTR->NXTPTR^=VDPTR->HDPTR);
         VDPTR->RECPTR=#SUCC(RHHDRPT,VDPTR,VDPTR->RECPTR);
         END;
     GMISC_TABLE(GMSGIX)=MSG;
     MSG='';
     GG_TABLE(*)='';
     GFID_TABLE(*)='';
     GFEAT_TABLE(*)='';
     GDESC_TABLE(*)='';
     GG_TABLE(*)='';
     H_TABLE(*)='';
     S_TABLE(*)='';
     GPO_TABLE(*)='';
     GDTE_TABLE(*)='';
     PLACE=VDPTR->RECPTR;
     IF VDPTR->RECPTR=VDPTR->HDPTR
       THEN GMSG='NO FEATURES FOUND FOR THIS VENDOR/DEVICE';
       ELSE DO;
         PLACE=VDPTR->RECPTR; /* SAVE PLACE */
         DO J=1 TO GCNT WHILE(VDPTR->RECPTR^=VDPTR->HDPTR);
           GFID_TABLE(J) =GETDATA('FID'     ,VDPTR);
           GFEAT_TABLE(J)=GETDATA('FEATURE' ,VDPTR);
           GDESC_TABLE(J)=GETDATA('FEATDESC',VDPTR);
           H_TABLE(J)    =GETDATA('DISTR'   ,VDPTR);
           S_TABLE(J)    =GETDATA('STATUS'  ,VDPTR);
           GPO_TABLE(J)  =GETDATA('PONO'    ,VDPTR);
           GDTE_TABLE(J) =GETDATA('STATDATE',VDPTR);
           GG_TABLE(J)   =GETDATA('QTY'     ,VDPTR);
           IF GG_TABLE(J)='.'
             THEN GG_TABLE(J)='0';
           VDPTR->RECPTR=#SUCC(RHHDRPT,VDPTR,VDPTR->RECPTR);
           END;
         END;
     CALL DISPLAY_LINK('AHMSGPNL ',' ',GOPTION);
     IF PLACE^=VDPTR->HDPTR
       THEN DO; /* STORE QTY FROM DISPLAY */
         CALL #NC(VDPTR,PLACE); /* RESTORE CURRENT RECORD */
         DO J=1 TO GCNT WHILE(VDPTR->RECPTR^=VDPTR->HDPTR);
           CALL PUTDATA('QTY'     ,VDPTR, GG_TABLE(J));
           CALL PUTDATA('DISTR'   ,VDPTR,  H_TABLE(J));
           CALL PUTDATA('STATUS'  ,VDPTR,  S_TABLE(J));
           CALL PUTDATA('PONO'    ,VDPTR,GPO_TABLE(J));
           CALL PUTDATA('STATDATE',VDPTR,GDTE_TABLE(J));
           VDPTR->RECPTR=#SUCC(RHHDRPT,VDPTR,VDPTR->RECPTR);
           END;
         END;
     END;
   IF GOPTION^=2
     THEN DO;  /* OPTION^=CANCEL */
       I=#CMD('REMOVE WHERE ID='××GMISC_TABLE(GIDIX),FEATPTR,RHHDRPT);
       I=#CMD('TOP',VDPTR,RHHDRPT);
       DO WHILE(VDPTR->RECPTR^=VDPTR->HDPTR);
         K=GETDATA_FIXED('QTY',VDPTR);
         IF K^=MISSING_INTEGER & K^=0
           THEN DO;
             I=#CMD('B BL 1',FEATPTR,RHHDRPT);
             I=#CMD('B V'   ,FEATPTR,RHHDRPT);
             CALL PUTDATA('ID'    ,FEATPTR,GMISC_TABLE(GIDIX));
             CALL PUTDATA('FID'   ,FEATPTR,GETDATA('FID',VDPTR));
             CALL PUTDATA('QTY'   ,FEATPTR,CALCPRT(K));
             CALL PUTDATA('DISTR' ,FEATPTR,GETDATA('DISTR' ,VDPTR));
             CALL PUTDATA('STATUS',FEATPTR,GETDATA('STATUS',VDPTR));
             CALL PUTDATA('PONO'  ,FEATPTR,GETDATA('PONO'  ,VDPTR));
             CALL PUTDATA('STATDATE',FEATPTR,
               GETDATA('STATDATE',VDPTR));
             END;
         VDPTR->RECPTR=#SUCC(RHHDRPT,VDPTR,VDPTR->RECPTR);
         END;
       END;
   I=#CMD('DEL',VDPTR,RHHDRPT);
   END ODD_OPTION_THING;

 PUTDATE: PROC(STR) RETURNS(CHAR(4));
 /**********************************************************************
 *                                                                     *
 * CALLED TO CONVERT A DATE FROM MMDDYY TO SAS FORMAT.                 *
 *                                                                     *
 **********************************************************************/
   DCL STR CHAR(*);
   DCL INSTR CHAR(6);
   DCL RETSTR CHAR(4);
   DCL I FIXED BIN;

   INSTR=STR;
   IF VERIFY(INSTR,'0123456789')^=0
     THEN RETURN('.');
   I=#CMD('CALC DATESAS('××SUBSTR(INSTR,5,2)××SUBSTR(INSTR,1,4)××')',
     RHPTR,RHHDRPT);
   RETSTR=MSGDATA;
   MSGDATA='';
   RETURN(RETSTR);
   END PUTDATE;

 VEND_DISPLAY: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO DISPLAY THE CURRENT RECORD IN VENDORS.    *
 *                                                                     *
 **********************************************************************/
   DCL CURSORLOC CHAR(9);

   VTABLE(VMSGIX)=VMSG;
   VMSG='';
   VTABLE(VENDORIX)  =GETDATA('VENDOR'  ,VENDPTR);
   VTABLE(VENDNAMEIX)=GETDATA('VENDNAME',VENDPTR);
   VTABLE(VENDADDRIX)=GETDATA('VENDADDR',VENDPTR);
   VTABLE(VENDCITYIX)=GETDATA('VENDCITY',VENDPTR);
   VTABLE(VENDSTATIX)=GETDATA('VENDSTAT',VENDPTR);
   VTABLE(VENDZIPIX) =GETDATA('VENDZIP' ,VENDPTR);
   VTABLE(VENDTELEIX)=GETDATA('VENDTELE',VENDPTR);
   IF VLOCATE_DATA=''
     THEN CURSORLOC='ZCMD';
     ELSE CURSORLOC='VLOCATE';
   CALL DISPLAY_LINK('AHMSVPNL ',CURSORLOC,VOPTION);
   CALL PUTDATA('VENDOR'  ,VENDPTR,VTABLE(VENDORIX));
   CALL PUTDATA('VENDNAME',VENDPTR,VTABLE(VENDNAMEIX));
   CALL PUTDATA('VENDADDR',VENDPTR,VTABLE(VENDADDRIX));
   CALL PUTDATA('VENDCITY',VENDPTR,VTABLE(VENDCITYIX));
   CALL PUTDATA('VENDSTAT',VENDPTR,VTABLE(VENDSTATIX));
   CALL PUTDATA('VENDZIP' ,VENDPTR,VTABLE(VENDZIPIX));
   CALL PUTDATA('VENDTELE',VENDPTR,VTABLE(VENDTELEIX));
   END VEND_DISPLAY;

 VEND_DO_ADD: PROC(ID,MAXID,TABPTR);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO ADD A RECORD TO THE VENDORS TABLE.        *
 *                                                                     *
 **********************************************************************/
   DCL ID CHAR(*);
   DCL MAXID FIXED BIN(31,0);
   DCL TABPTR PTR;

   I=#CMD('B BL 1',TABPTR,RHHDRPT);
   I=#CMD('B V'   ,TABPTR,RHHDRPT);
   END VEND_DO_ADD;

 VEND_DO_DEL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO DELETE A RECORD FROM THE VENDORS TABLE.   *
 *                                                                     *
 **********************************************************************/
   DCL SEQPIC PIC'ZZZZZZZ9';

   IF VENDPTR->RECPTR=VENDPTR
     THEN RETURN; /* NO RECORD TO DELETE */
   SEQPIC=VENDPTR->RECPTR->RECSEQ;
   I=#CMD(SEQPIC××' DEL',VENDPTR,RHHDRPT);
   END VEND_DO_DEL;

   END AHMSEDT;
