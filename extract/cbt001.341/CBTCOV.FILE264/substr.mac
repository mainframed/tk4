*$.HE GENERALIZED SUBROUTINES - SUBSTR
*$.HE ASSIGN SUBSTRING TO ANOTHER CHARACTER VARIABLE - COBOL
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/16/82
*$AUTHOR: L. SICARD
*$SOURCE MODULE NAME: SUBSTR
*$DOCUMENTATION ID: *$
*$.FI L
*$.SP 5
*$THIS ROUTINE IS INTENDED FOR USE BY COBOL PROGRAMS ONLY. IT
*$CAN BE USED TO PERFORM A PL/I TYPE OF SUBSTRING FUNCTION
*$(I.E., WHEN USED FOR THIS PURPOSE ALONE "SUBSTR" IS MORE
*$EFFICIENT THAN THE COBOL "UNSTRING" FUNCTION).
*$"SUBSTR" CAN ALSO BE USED TO BUMP THROUGH A VARIABLE LENGTH
*$SEGMENTED RECORD (FOR THIS USAGE STRING1 IS THE WHOLE RECORD
*$THAT WAS READ IN, STRING2 IS A CHARACTER STRING REDEFINING
*$THE CURRENT SEGMENT STRUCTURE, OFFSET1 IS THE LOCATION OF
*$THE CURRENT SEGMENT RELATIVE TO THE BEGINNING OF STRING1,
*$AND LENGTH1 MUST BE SET TO THE LENGTH OF STRING2).
*$.SP 1
*$.IN 4
*$CALL 'SUBSTR' USING STRING1 STRING2 OFFSET1 LENGTH1.
*$.SP 1
*$.IN 0
*$STRING1
*$.IN 4
*$PIC X(###).  AN 01 LEVEL CHARACTER VARIABLE WHICH
*$CONTAINS THE SUBSTRING TO OVERLAY PART OR ALL OF STRING2.
*$STRING1 WILL NOT BE AFFECTED BY THE CALL. ### IS AN
*$INTEGER >= 0.
*$.IN 0
*$STRING2
*$.IN 4
*$PIC X(@@@).  AN 01 LEVEL CHARACTER VARIABLE WHICH WILL
*$HAVE A SPECIFIC NUMBER OF CHARACTERS REPLACED WITH
*$CHARACTERS FROM STRING1. @@@ IS AN INTEGER >= 0.
*$.IN 0
*$OFFSET1
*$.IN 4
*$PIC S9(9) COMP SYNC. AN 01 LEVEL FULLWORD BINARY VARIABLE
*$WHICH CONTAINS THE OFFSET FROM THE BEGINNING OF STRING1
*$FROM WHICH CHARACTERS ARE SELECTED. CHARACTER REPLACEMENT
*$IN STRING2 ALWAYS BEGINS WITH THE FIRST CHARACTER OF
*$STRING2.
*$.IN 0
*$LENGTH1
*$.IN 4
*$PIC S9(9) COMP SYNC. AN 01 LEVEL FULLWORD BINARY VARIABLE
*$WHICH CONTAINS THE LENGTH OF THE STRING TO BE MOVED FROM
*$STRING1 TO STRING2.
*$.SP 1
*$.IN 0
*$NOTE:  IF LENGTH1 IS EQUAL TO THE LENGTH OF STRING2 THEN ALL
*$OF STRING2 WILL BE REPLACED. IF LENGTH1 IS LESS THAN THE
*$LENGTH OF STRING2 THEN ONLY THAT PORTION OF STRING2 WILL BE
*$REPLACED AND THE REST WILL REMAIN UNCHANGED. INSURE THAT
*$LENGTH1 IS NEVER GREATER THAN THE LENGTH OF STRING1 OR STRING2
*$OR THE RESULTS MAY BE UNPREDICTABLE.
SUBSTR1  CSECT
         ENTRY SUBSTR
         DC    C' SUBSTR'
         DC    AL1(7)
SUBSTR   DS    0H
         STM   14,12,12(13)
         BALR  10,0
         USING *,10
         LA    4,SAVE
         ST    13,SAVE+4
         ST    4,8(13)
         LA    13,SAVE
* START OF SUBSTR ROUTINE
         L     2,0(1)             LOAD ADDR OF RECORD
         L     6,8(1)             LOAD ADDR OF RECORD OFFSET
         L     6,0(6)             LOAD RECORD OFFSET
         LA    2,0(6,2)           LOAD RECORD ADDR PLUS OFFSET
         L     4,4(1)             LOAD ADDR OF SEGMENT
         L     5,12(1)            LOAD ADDR OF SEGMENT LENGTH
         L     5,0(5)             LOAD SEGMENT LENGTH
         LR    3,5                USE SEGMENT LENGTH FOR RECORD
         MVCL  4,2                MOVE SUBSTR OF RECORD INTO SEGMENT
* END OF SUBSTR ROUTINE
         L     13,4(13)
         LM    14,12,12(13)
         SR    15,15
         BR    14
SAVE     DS    9D
         LTORG
         END
