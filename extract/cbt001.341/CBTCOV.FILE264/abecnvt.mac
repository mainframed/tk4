 /* COPYRIGHT 1985 BY THOMAS GLEN SMITH */
 /**********************************************************************
 *                                                                     *
 * ALIAS #CONVERT                                                      *
 * CALLED TO CONVERT DATA FROM ONE BASE SYSTEM TO ANOTHER.             *
 *                                                                     *
 * STR SHOULD CONTAIN THE DATA TO BE CONVERTED.                        *
 * R SHOULD POINT TO RHNAMESTR FOR INPUT DATA.                         *
 * C SHOULD POINT TO RHNAMESTR FOR OUTPUT DATA.                        *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABECNVT) PARMS(STR,R,C) RETURNS(CHAR(256) VAR);
   %INCLUDE ABESUBA;
   %INCLUDE ABEREC;

   DCL STR CHAR(*) VAR;
   DCL (C,R) PTR;

   DCL ABEFLOT ENTRY(CHAR(*) VAR,PTR) RETURNS(DEC FLOAT(16));
   DCL CALCPRT ENTRY(DEC FLOAT(16))
     RETURNS(CHAR(80) VAR);
   DCL PREFIX ENTRY(CHAR(*), CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL CONVERT ENTRY(PTR,
     CHAR(*),FIXED BIN(15,0),FIXED BIN(15,0),
     CHAR(*),FIXED BIN(15,0),FIXED BIN(15,0))
     RETURNS(CHAR(256) VAR);
   DCL TYPETAB(8) CHAR(8) VAR STATIC INIT(
     'FLOAT','CHAR','CHAR','PACKED','HEX','BIT','FIXED','BEST');
   DCL ZD FIXED BIN(15,0) STATIC INIT(3); /* ZONED DECIMAL TYPE */
   DCL CHR FIXED BIN(15,0) STATIC INIT(2); /* CHAR TYPE */

   ON ERROR GOTO CONVERT_INTFC_ERROR;
   IF LENGTH(STR)=0
     THEN RETURN(STR); /* MISSING VALUE */
   IF R->RHNTYPE=ZD × R->RHNTYPE=CHR
     THEN IF STR=''
       THEN RETURN(''); /* MISSING VALUE */
       ELSE IF SUBSTR(STR,PREFIX(STR,' '))='.'
         THEN RETURN(''); /* MISSING VALUE */
   IF (R->RHNTYPE=C->RHNTYPE & (R->RHNTYPE=2 × R->RHNTYPE=3)) ×
     (R->RHNTYPE=3 & C->RHNTYPE=2)
     THEN RETURN(STR); /* CHAR-TO-CHAR, ZONED-TO-ZONED, ZONED-TO-CHAR */
   IF C->RHNTYPE=2 × C->RHNTYPE=3 /* TO-CHAR OR TO-ZONED? */
     THEN RETURN(CALCPRT(ABEFLOT(STR,R)));
   RETURN(CONVERT(PTROUT(PTRIN(ADDR(STR))+2),
     TYPETAB(R->RHNTYPE),LENGTH(STR),R->RHNFD,
     TYPETAB(C->RHNTYPE),C->RHNLNG,C->RHNFD));
 CONVERT_INTFC_ERROR:
   RETURN('');
   END ABECNVT;
