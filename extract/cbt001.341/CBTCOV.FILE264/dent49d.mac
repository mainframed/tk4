 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 DENT49D: PROC(MAINPARM,SUBQ_HDR) OPTIONS(REENTRANT);
 /**********************************************************************
 * THIS DEBUGGING ROUTINE WILL PRINT OUT THE PARSING CONTROL BLOCKS    *
 **********************************************************************/
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   DCL NULL BUILTIN;
   %INCLUDE DENTSEL;
   PUT SKIP LIST('PRINTING THE MAIN TOKEN QUEUE NOW');
   DO MCURTOK=MTOKHDR REPEAT MCURTOK->NEXT_TOKEN WHILE(MCURTOK^=NULL);
     PUT SKIP LIST('TOKSTR=',MCURTOK->TOKSTR);
     END;
   PUT SKIP LIST('SCANNING THE SUBQUERY QUEUE NEXT');
   DO SELPTR=SUBQ_FIRST REPEAT SUBQ_NEXT WHILE(SELPTR^=NULL);
     PUT SKIP DATA(SUBQ_NUM);
     PUT SKIP DATA(SELECT_FLAGS);
     IF SELECT_LIST_PTR=NULL
       THEN PUT SKIP LIST('SELECT_FIELD QUEUE IS EMPTY');
       ELSE DO;
         PUT SKIP LIST('PROCESSING THE SELECT_FIELD QUEUE');
         DO SELFPTR=SELECT_LIST_PTR REPEAT SELECT_NEXT_FIELD
           WHILE(SELFPTR^=NULL);
           DO CURTOK=FIELD_EXP_HDR REPEAT NEXT_TOKEN
             WHILE(CURTOK^=NULL);
             PUT SKIP DATA(TOKSTR);
             END;
           END;
         END;
     IF FROM_LIST_PTR=NULL
       THEN PUT SKIP LIST('FROM CLAUSE NOT AVAILABLE');
       ELSE DO;
         PUT SKIP LIST('PROCESSING FROM CLAUSE');
         DO FROFPTR=FROM_LIST_PTR REPEAT FROM_NEXT_FIELD
           WHILE(FROFPTR^=NULL);
           PUT SKIP DATA(FROM_NAME,SHORT_FROM_NAME);
           END;
         END;
     IF WHERE_PTR=NULL
       THEN PUT SKIP LIST('WHERE CLAUSE NOT AVAILABLE');
       ELSE DO;
         PUT SKIP LIST('PROCESSING WHERE CLAUSE');
         SRCH_PTR=WHERE_PTR;
         PUT SKIP DATA(SEARCH_FLAGS);
         PUT SKIP LIST('PRINTING WHERE EXPRESSION 1');
         DO CURTOK=SEARCH_EXP(1) REPEAT NEXT_TOKEN
           WHILE(CURTOK^=NULL);
           PUT SKIP DATA(TOKSTR);
           END;
         PUT SKIP LIST('PRINTING WHERE EXPRESSION 2');
         IF SEARCH_FLAGS.IN
           THEN DO SELFPTR=SEARCH_EXP(2) REPEAT SELECT_NEXT_FIELD
             WHILE(SELFPTR^=NULL);
             PUT SKIP LIST('PRINTING NEXT IN EXPRESSION');
             DO CURTOK=FIELD_EXP_HDR REPEAT NEXT_TOKEN
               WHILE(CURTOK^=NULL);
               PUT SKIP DATA(TOKSTR);
               END;
             END;
           ELSE DO CURTOK=SEARCH_EXP(2) REPEAT NEXT_TOKEN
             WHILE(CURTOK^=NULL);
             PUT SKIP DATA(TOKSTR);
             END;
         PUT SKIP LIST('PRINTING WHERE EXPRESSION 3');
         DO CURTOK=SEARCH_EXP(3) REPEAT NEXT_TOKEN
           WHILE(CURTOK^=NULL);
           PUT SKIP DATA(TOKSTR);
           END;
         END;
     IF GROUP_BY_PTR=NULL
       THEN PUT SKIP LIST('GROUP BY CLAUSE NOT AVAIALABLE');
       ELSE DO;
         PUT SKIP LIST('PROCESSING GROUP BY CLAUSE');
         IF GROUP_NAME=NULL
           THEN PUT SKIP LIST('GROUP NAME PTR IS NULL');
           ELSE PUT SKIP LIST('GROUP_NAME = ',GROUP_NAME->TOKSTR);
         IF GROUP_SEARCH_CONDITION=NULL
           THEN PUT SKIP LIST('HAVING CLAUSE NOT AVAILABLE');
           ELSE DO;
             PUT SKIP LIST('PRINTING HAVING CLAUSE NOW');
             SRCH_PTR=GROUP_SEARCH_CONDITION;
             PUT SKIP DATA(SEARCH_FLAGS);
             DO I=1 TO 3;
               PUT SKIP LIST('PRINTING HAVING EXPRESSION',I);
               DO CURTOK=SEARCH_EXP(I) REPEAT NEXT_TOKEN
                  WHILE(CURTOK^=NULL);
                 PUT SKIP DATA(TOKSTR);
                 END;
               END;
             END;
         END;
     IF ORDER_BY_PTR=NULL
       THEN PUT SKIP LIST('ORDER BY CLAUSE NOT AVAILABLE');
       ELSE DO;
         PUT SKIP LIST('PRINTING ORDER BY CLAUSE NOW');
         DO OSPPTR=ORDER_BY_PTR REPEAT ORDER_LIST_PTR
           WHILE(OSPPTR^=NULL);
           PUT SKIP LIST(ORDER_NAME->TOKSTR,ORDER_ASCENDING);
           END;
         END;
     IF TO_PTR=NULL
       THEN PUT SKIP LIST('TO CLAUSE NOT AVAILABLE');
       ELSE PUT SKIP LIST('TO = ',TO_PTR->TOKSTR);
     END;
   END DENT49D;
