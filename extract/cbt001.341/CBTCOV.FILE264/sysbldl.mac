*$.HE ISD GENERALIZED SUBROUTINES - SYSBLDL
*$.HE PLI INTERFACE TO BLDL MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/01/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSBLDL
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE
*$TO THE BLDL MACRO, WHICH IS USED TO COMPLETE A LIST OF
*$INFORMATION FROM THE DIRECTORY OF A PARTITIONED DATA SET.
*$.SP 1
*$.IN 10
*$CALL SYSBLDL(DCBPTR,LISTPTR,RETCODE);
*$.SP 1
*$.IN 0
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$DCBPTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS OF THE BPAM DCB TO BE
*$PROCESSED (MUST BE OPEN).  A PREVIOUS CALL TO SYSDCBP WILL HAVE
*$ESTABLISHED THIS ADDRESS, AND A CALL TO SYSOPEN WILL HAVE OPENED
*$THE DCB.
*$.IN 0
*$LISTPTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS OF THE LIST TO BE COMPLETED
*$WHEN THE BLDL MACRO INSTRUCTION IS ISSUED.  THE LIST ADDRESS MUST BE
*$ON A HALFWORD BOUNDARY, AND MUST BE IN THE FOLLOWING FORMAT:
*$.IN 10
*$FF FIXED BIN(15,0);
*$.IN 15
*$CONTAINS A BINARY VALUE INDICATING THE TOTAL NUMBER OF ENTRIES
*$IN THE LIST.
*$.IN 10
*$LL FIXED BIN(15,0),
*$.IN 15
*$CONTAINS A BINARY VALUE INDICATING THE LENGTH, IN BYTES, OF EACH
*$ENTRY IN THE LIST (MUST BE AN EVEN NUMBER OF BYTES).  IF THE EXACT
*$LENGTH OF THE ENTRY IS KNOWN, SPECIFY THE EXACT LENGTH.  OTHERWISE,
*$SPECIFY AT LEAST 58 BYTES (DECIMAL) IS THE LIST IS TO BE USED WITH
*$AN ATTACH, LINK, LOAD, OR XCTL MACRO INSTRUCTION.  THE MINIMUM
*$LENGTH FOR A LIST IS 12 BYTES.
*$.SP 1
*$THE FOLLOWING FIELDS REPRESENT ONE ENTRY IN THE LIST.
*$.SP 1
*$NAME CHAR(8)
*$.IN 15
*$CONTAINS THE MEMBER NAME OR ALIAS TO BE LOCATED (LEFT JUSTIFIED AND
*$PADDED WITH BLANKS IF NECESSARY).
*$.IN 10
*$TTRK CHAR(4)
*$.IN 15
*$FILLED IN BY BLDL MACRO.  WILL CONTAIN THE RELATIVE TRACK NUMBER
*$(TT), AND RELATIVE BLOCK NUMBER (R) OF THE SPECIFIED MEMBER,
*$AND ALSO THE CONCATENATION NUMBER OF THE DATA SET (K).
*$.IN 10
*$Z BIT(8)
*$.IN 15
*$INDICATES WHERE THE SYSTEM FOUND THE DIRECTORY ENTRY:
*$.IN 20
*$.NF
*$0 - PRIVATE LIBRARY
*$1 - LINK LIBRARY
*$2 - JOB, TASK, OR STEP LIBRARY
*$3-255 - JOB, TASK, OR STEP LIBRARY
*$        OF PARENT TASK N, WHERE
*$        N = Z - 2.
*$.FI L
*$.IN 10
*$C BIT(8)
*$.IN 15
*$BIT 0=0 IF MEMBER NAME, =1 IF ALIAS.
*$BITS 1-2 INDICATE THE NUMBER OF TTRN FIELDS (MAXIMUM OF THREE)
*$IN THE USER DATA FIELD.  BITS 3-7 INDICATE THE TOTAL NUMBER OF
*$HALFWORDS IN THE USER DATA FIELD.
*$.IN 10
*$USER DATA
*$.IN 15
*$THE USER DATA FIELD CONTAINS THE USER DATA FROM THE
*$DIRECTORY ENTRY.  IF THE LENGTH OF THE USER DATA FIELD IN
*$THE BLDL LIST IS EQUAL TO OR GREATER THAN THE USER DTA FIELD
*$OF THE DIRECTORY ENTRY, THE ENTIRE USER DATA FIELD IS ENTERED
*$INTO THE LIST.  OTHERWISE, THE LIST CONTAINS ONLY THE USER DATA
*$FOR WHICH THERE IS SPACE.
*$.IN 0
*$RETCODE FIXED BIN(31,0);
*$.IN 5
*$00 SUCCESSFUL COMPLETION.
*$.IN 5
*$04 ONE OR MORE ENTRIES IN THE LIST COULD NOT BE FILLED:
*$THE LIST SUPPLIED MAY BE INVALID.  IF A SEARCH IS ATTEMPTED
*$BUT THE ENTRY IS NOT FOUND, THE R FIELD FOR THAT ENTRY IS
*$SET TO ZERO.
*$.IN 5
*$08 A PERMANENT I/O ERROR WAS DETECTED WHEN THE SYSTEM ATTEMPTED
*$TO SEARCH THE DIRECTORY.
SYSBLDL  CSECT
         USING *,15
         STM   14,12,12(13)
         LM    2,4,0(1)      LOAD PARAMETER LIST
         L     1,0(2)        LOAD DCB PTR
         LTR   1,1           TEST NULL PTR
         BP    LOADLIST
         SR    1,1           CLEAR = LINKLIB/JOBLIB/STEPLIB
LOADLIST L     0,0(3)        LOAD LIST PTR
         BLDL  (1),(0)       ISSUE SVC
         ST    15,0(4)       PASS RETURN CODE BACK
         LM    14,12,12(13)
         BR    14
         END
