*$.HE GENERALIZED SUBROUTINES - PRTBIT
*$.HE TRANSLATE A CHARACTER TO A PRINTABLE BIT STRING - COBOL
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/09/82
*$AUTHOR: L. SICARD
*$SOURCE MODULE NAME: PRTBIT
*$DOCUMENTATION ID: *$
*$.FI L
*$.SP 5
*$THIS ROUTINE IS INTENDED FOR USE BY COBOL PROGRAMS ONLY. ITS
*$FUNCTION IS TO TRANSLATE THE 8 BINARY ZEROS AND ONES OF A
*$SINGLE BYTE INTO 8 CHARACTER ZEROS AND ONES.
*$.SP 1
*$.IN 4
*$CALL 'PRTBIT' USING CHAR1 STRING2.
*$.SP 1
*$.IN 0
*$CHAR1
*$.IN 4
*$PIC X.  AN 01 LEVEL CHARACTER VARIABLE TO BE TRANSLATED.
*$THIS VARIABLE WILL REMAIN UNCHANGED AFTER THE CALL.
*$.IN 0
*$STRING2
*$.IN 4
*$PIC X(8).  AN 01 LEVEL CHARACTER VARIABLE WHICH WILL
*$CONTAIN CHARACTER ZEROS AND ONES CORRESPONDING TO THE
*$BINARY ZEROS AND ONES OF CHAR1 AFTER THE CALL.
PRTBIT1  CSECT
         ENTRY PRTBIT
         DC    C' PRTBIT'
         DC    AL1(7)
PRTBIT   DS    0H
         STM   14,12,12(13)
         BALR  10,0
         USING *,10
         LA    4,SAVE
         ST    13,SAVE+4
         ST    4,8(13)
         LA    13,SAVE
* BEGIN OF PRTBIT ROUTINE
         L     2,0(1)             LOAD ADDR OF 1ST ARGUMENT INTO REG.2
         L     3,4(1)             LOAD ADDR OF 2ND ARGUMENT INTO REG.3
         MVC   0(8,3),=C'00000000'
B0       TM    0(2),B'10000000'
         BZ    B1
         MVC   0(1,3),=C'1'
B1       TM    0(2),B'01000000'
         BZ    B2
         MVC   1(1,3),=C'1'
B2       TM    0(2),B'00100000'
         BZ    B3
         MVC   2(1,3),=C'1'
B3       TM    0(2),B'00010000'
         BZ    B4
         MVC   3(1,3),=C'1'
B4       TM    0(2),B'00001000'
         BZ    B5
         MVC   4(1,3),=C'1'
B5       TM    0(2),B'00000100'
         BZ    B6
         MVC   5(1,3),=C'1'
B6       TM    0(2),B'00000010'
         BZ    B7
         MVC   6(1,3),=C'1'
B7       TM    0(2),B'00000001'
         BZ    ENDER
         MVC   7(1,3),=C'1'
ENDER    EQU   *
* END OF PRTBIT ROUTINE
         L     13,4(13)
         LM    14,12,12(13)
         SR    15,15
         BR    14
SAVE     DS    9D
         LTORG
         END
