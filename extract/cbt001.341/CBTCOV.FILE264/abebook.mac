   %INCLUDE ABESUB;
 ABESUB(ABEBOOK) PARS(00);
   %INCLUDE ABESUBA;

   DCL SETCAPS ENTRY;
   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*),CHAR(*)) RETURNS(FIXED BIN(15,0));
   DCL TRH PTR;
   DCL HEADER CHAR(80) VAR INIT(' ');

   ON CONDITION(BADBOOK) BEGIN;
     IF MSGDATA=''
       THEN MSGDATA='ERROR CONDITION DURING BOOK GENERATION';
     RHRC=8;
     GOTO BADBOO;
     END;
   /* PARY(1)=#LINES PER PAGE */
   IF PARY(1)=''
     THEN PSIZE=55;
     ELSE PSIZE=PARY(1);
   /* PAGENO=CURRENT PAGE NUMBER */
   PAGENO=0;
   TRH=RHPTR;
   I=#CMD('ABE '''' OUTFI(CONTENT) NON NAME(CONTENT)',TRH,RHHDRPT);
   I=#CMD('ABE '''' OUTFI(INDEX) NON NAME(INDEX)',TRH,RHHDRPT);
   I=#CMD('CALC FILENUM#0',RHPTR,RHHDRPT);
   DO UNTIL(SAVERC^=0);
     I=#CMD('ZCALC FILENUM#FILENUM+1',RHPTR,RHHDRPT);
     TRH=RHPTR;
     SAVERC=#CMD(
       'R ABE '''' INFI(IN&FILENUM) OUTFI(OUT&FILENUM) NON NAME(TEXT)',
       TRH,RHHDRPT);
     IF SAVERC=0
       THEN DO;
         CALL BOOKA(TRH);
         I=#CMD('A TEXT SE',RHPTR,RHHDRPT);
         END;
     END;
   /* NOW GO DO INDEX */
   CALL BOOKE;
   I=#CMD('A CONTENT SE',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SE',RHPTR,RHHDRPT);
 BADBOO:;

 BOOKA: PROC(PRH);
   DCL PRH PTR;
   /* PSIZE SHOULD ALREADY BE SET TO # LINES PER PAGE */
   /* PAGENO SHOULD ALREADY BE SET TO STARTING PAGE NUMBER-1 */
   /* DISW INDICATES WHETHER DICTIONARY HEADERS ARE TO BE DONE */

   DISW=0;
   HEADER=' ';
   I=#CMD('LO',PRH,RHHDRPT);
   I=#CMD('SR 1',PRH,RHHDRPT);
   I=#CMD('REN 10 10',PRH,RHHDRPT);
   LINENO=PSIZE+1;
   DO PRH->RECPTR=#SUCC(RHHDRPT,PRH,NULL)
     REPEAT #SUCC(RHHDRPT,PRH,PRH->RECPTR) WHILE(PRH->RECPTR^=PRH);
     DELSW=0;
     IF INDEX(PRH->RECPTR->RECSTR,'.')=2
       THEN CALL BOOKB(SUBSTR(PRH->RECPTR->RECSTR,3,2),PRH);
     IF LINENO>PSIZE
       THEN CALL NEWPAGE(PRH);
     IF DELSW
       THEN DO;
         I=#CMD('* DEL',PRH,RHHDRPT);
         IF PRH->RECPTR=PRH->NXTHDR
           THEN PRH->RECPTR=NULL; /* GET FIRST RECORD */
         END;
       ELSE LINENO=LINENO+1;
     END;
   I=#CMD('B V',PRH,RHHDRPT);
   /* NOW MAKE SURE THERE IS AN EVEN NUMBER OF PAGES */
   IF MOD(PAGENO,2)^=0
     THEN DO;
       I=#CMD('NL',PRH,RHHDRPT);
       CALL NEWPAGE(PRH);
       END;
   /* NOW GO BUILD TABLE OF CONTENTS FOR THIS SECTION */
   CALL BOOKC(PRH);
   IF DISW
     THEN CALL BOOKD(PRH);
   I=#CMD('CLEAR 73',PRH,RHHDRPT);
   END BOOKA;

 BOOKB: PROC(CTLCK,PRH);
   DCL CTLCK CHAR(2);
   DCL PRH PTR;
   /*CALLED FROM BOOKA WHEN THE CURRENT RECORD MAY BE A CONTROL RECORD*/
   /*GLOBAL VARIABLES DELSW, DISW, HEADER, AND LINENO MAY BE CHANGED.*/
   DCL WRK CHAR(80) VAR;

   DELSW=1;
   CALL SETCAPS(CTLCK);
   SELECT(CTLCK);
     WHEN('DI') DO;
       DISW=1;
       I=#CMD(
         'LSP ''/&1 &1 2.1 FLIP &1 &1 74.1/&1 &1 2.69 SL 1'''××
         ' 1((''='':2 A))',PRH,RHHDRPT);
       END;
     WHEN('CM','GG') DO;
       DELSW=0;
       PRH->RECPTR->RECSTR=SUBSTR(PRH->RECPTR->RECSTR,2); /*SHIFT LEFT*/
       END;
     WHEN('GR') DO;
       DELSW=0;
       J=GETLINES(PRH);
       PRH->RECPTR->RECSTR=SUBSTR(PRH->RECPTR->RECSTR,2); /*SHIFT LEFT*/
       LINENO=LINENO+J-1;
       IF LINENO>=PSIZE
         THEN DO;
           CALL NEWPAGE(PRH);
           LINENO=MIN(J-1,PSIZE);
           END;
       END;
     WHEN('HE') DO;
       IF LENGTH(PRH->RECPTR->RECSTR)<5
         THEN WRK=' ';
         ELSE DO;
           WRK=SUBSTR(PRH->RECPTR->RECSTR,5);
           IF WRK=''
             THEN WRK=' ';
             ELSE DO;
               WRK=SUBSTR(WRK,PREFIX(WRK,' '));
               WRK=SUBSTR(WRK,1,SUFFIX(WRK,' '));
               END;
           END;
       HEADER=WRK;
       END;
     WHEN('IM') DO;
       DELSW=0;
       PRH->RECPTR->RECSTR=SUBSTR(PRH->RECPTR->RECSTR,2); /*SHIFT LEFT*/
       END;
     WHEN('IN') DO;
       I=#CMD('INCLUDE '××SUBSTR(PRH->RECPTR->RECSTR,5),
         PRH,RHHDRPT);
       END;
     WHEN('NI') DO;
       J=GETLINES(PRH);
       IF LINENO+J>PSIZE
         THEN LINENO=PSIZE+1;
       END;
     WHEN('NP')
       LINENO=PSIZE+1; /* FORCE A NEW PAGE */
     OTHERWISE
       DELSW=0; /* TREAT IT AS NON CONTROL */
     END; /* SELECT */
   END BOOKB;

 BOOKC: PROC(PRH);
   DCL PRH PTR;
   DCL TRH PTR;

   /* CALLED FROM BOOKA WHEN IT IS TIME TO PROCESS CONTENTS */
   TRH=PRH;
   I=#CMD('F 1 1 ALL NAME(AAA)',TRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('F @ 73 ALL NAME(BBB)',TRH,RHHDRPT);
   IF I=0
     THEN DO;
       I=#CMD('A BBB CLEAR 73',PRH,RHHDRPT);
       I=#CMD('A AAA MB BBB NODEL',PRH,RHHDRPT);
       I=#CMD(
         'A AAA LSP ''&1 &1 69.3 SUBSTR &L1 SAMECOL'' 1((^1:1 A)) T',
         PRH,RHHDRPT);
       I=#CMD('A AAA LSP ''&1 DEL'' 1((''1'':1 A))',PRH,RHHDRPT);
       I=#CMD('A AAA CO (CONTENT B)',PRH,RHHDRPT);
       END;
   TRH=PRH;
   I=#CMD('A AAA END N',TRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('A BBB END N',TRH,RHHDRPT);
   END BOOKC;

 BOOKD: PROC(PRH);
   DCL PRH PTR;
   DCL TRH PTR;

   I=#CMD('LSP ''&1 &1 1.1 SUBSTR ='' 1((=:74 A))',PRH,RHHDRPT);
   I=#CMD('CLEAR 73',PRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('FIND = 1 ALL NAME(AAA)',PRH,RHHDRPT);
   I=#CMD('A AAA 1.1 SUBSTR '' ''',PRH,RHHDRPT);
   I=#CMD('A AAA AL',PRH,RHHDRPT);
   I=#CMD('A AAA 70.1 SUBSTR =',PRH,RHHDRPT);
   I=#CMD('A AAA AC '' '' 30',PRH,RHHDRPT);
   I=#CMD('A AAA 30.1 SUBSTR =',PRH,RHHDRPT);
   I=#CMD('A AAA AC = 30',PRH,RHHDRPT);
   I=#CMD('A AAA CLEAR 30',PRH,RHHDRPT);
   I=#CMD('A AAA AL 73',PRH,RHHDRPT);
   I=#CMD('MB AAA NODEL OVERLAY',PRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('F 1 1 ALL NAME(BBB)',TRH,RHHDRPT);
   I=#CMD('A AAA MB BBB NODEL',PRH,RHHDRPT);
   I=#CMD('A BBB END N',PRH,RHHDRPT);
   I=#CMD(
     'A AAA LSP ''&1 &1 73.8 SUBSTR &L1 SAMECOL'' 1((1:1 T+1:B)) T',
     PRH,RHHDRPT);
   I=#CMD(
     'A AAA LSP ''&1 &1 1.80 SUBSTR 73 COL OFF(47) LEN(8)'' 1((1:1 A))',
     PRH,RHHDRPT);
   I=#CMD('A AAA LSP ''&1 DEL'' 1((^1:1 A))',PRH,RHHDRPT);
   I=#CMD('MB AAA NODEL',PRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('A AAA END N',TRH,RHHDRPT);
   I=#CMD('CLEAR 73',PRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('F = 1 ALL NAME(AAA)',TRH,RHHDRPT);
   I=#CMD('A AAA C '' '' ''-''',PRH,RHHDRPT);
   I=#CMD('A AAA 1.1 SUBSTR '' ''',PRH,RHHDRPT);
   I=#CMD('MB AAA NODEL',PRH,RHHDRPT);
   TRH=PRH;
   I=#CMD('A AAA END N',TRH,RHHDRPT);
   END BOOKD;

 BOOKE: PROC;
   DCL TRH PTR;

   I=#CMD('A CONTENT C ''   '' ''---''',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT 60. C ''- '' ''--''',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT 60. C ''- '' ''--''',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT 72. CLEAR',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT CO INDEX',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SL 1',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SORTQ 1 20 A',RHPTR,RHHDRPT);
   I=#CMD('A INDEX REN',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SET 5 ''.HE INDEX''',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SET 6 ''         ''',RHPTR,RHHDRPT);
   I=#CMD('A INDEX SET 7 INDEX',RHPTR,RHHDRPT);
   I=#CMD('A INDEX 7 72.1 SUBSTR @',RHPTR,RHHDRPT);
   I=#CMD('A INDEX NAME TEXT',RHPTR,RHHDRPT);
   TRH=#RHPLOC('TEXT',RHHDRPT);
   CALL BOOKA(TRH);
   I=#CMD('A TEXT NAME INDEX',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT REN',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT SET 5 ''1''',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT SET 6 '' TABLE OF CONTENTS''',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT REN 10 10',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT B MK 1',RHPTR,RHHDRPT);
   I=#CMD('A CONTENT GETMK 1 BOT',RHPTR,RHHDRPT);
   IF #SYMA(RHHDRPT,RH_SYMTREE,'BOT')>560
     THEN DO;
       CALL #SETA(RHHDRPT,RH_SYMTREE,'PS',PSIZE);
       I=#CMD('A CONTENT R LSP ''DREM AAA &&1 &&1 &&1'' 1(T+&PS:B:&PS)',
         RHPTR,RHHDRPT);
       I=#CMD('A AAA CLEAR',RHPTR,RHHDRPT);
       I=#CMD('A AAA REN +1',RHPTR,RHHDRPT);
       I=#CMD('A AAA 1.1 SUBSTR ''1''',RHPTR,RHHDRPT);
       I=#CMD('A CONTENT MB AAA',RHPTR,RHHDRPT);
       END;
   /* NOW MAKE SURE THERE ARE AN EVEN NUMBER OF PAGES IN CONTENT */
   TRH=RHPTR;
   I=#CMD('A CONTENT F 1 1 ALL NAME(PAG)',TRH,RHHDRPT);
   I=#CMD('A PAG REN 1 1',RHPTR,RHHDRPT);
   I=#CMD('A PAG B MK 1',RHPTR,RHHDRPT);
   I=#CMD('A PAG GETMK 1 NLNO',RHPTR,RHHDRPT);
   TRH=RHPTR;
   I=#CMD('A PAG END N',TRH,RHHDRPT);
   IF MOD(#SYMA(RHHDRPT,RH_SYMTREE,'NLNO'),2)^=0
     THEN DO;
       I=#CMD('A CONTENT BOTTOM',RHPTR,RHHDRPT);
       I=#CMD('A CONTENT NL 1',RHPTR,RHHDRPT);
       END;
   END BOOKE;

 GETLINES: PROC(PRH) RETURNS(FIXED BIN);
 /**********************************************************************
 *                                                                     *
 * CALL TO OBTAIN THE LINES VALUE FROM THE CURRENT .GR OR .NI          *
 * STATEMENT.                                                          *
 *                                                                     *
 **********************************************************************/
   DCL PRH PTR;
   DCL WRK CHAR(80) VAR;
   DCL (I,J) FIXED BIN;

   WRK=SUBSTR(PRH->RECPTR->RECSTR,5);
   WRK=SUBSTR(WRK,PREFIX(WRK,' '));
   I=INDEX(WRK,' ');
   IF I=0
     THEN SIGNAL CONDITION(BADBOOK);
   WRK=SUBSTR(WRK,1,I-1);
   IF VERIFY(WRK,'0123456789')^=0
     THEN SIGNAL CONDITION(BADBOOK);
   J=WRK;
   RETURN(J);
   END GETLINES;

 NEWPAGE: PROC(PRH);
   DCL PRH PTR;
   DCL (SAV,WRK) CHAR(80) VAR;
   DCL HDWRK CHAR(59);

   LINENO=0;
   PAGENO=PAGENO+1;
   HDWRK=HEADER;
   PUT STRING(WRK) EDIT('1',HDWRK,'PAGE',PAGENO)(A,A,A,P'ZZZZZZ9');
   SAV=PRH->RECPTR->RECSTR;
   PRH->RECPTR->RECSTR=WRK;
   I=#CMD('NL  ',PRH,RHHDRPT);
   I=#CMD('NL  ',PRH,RHHDRPT);
   I=#CMD('NL '××SAV,PRH,RHHDRPT);
   END NEWPAGE;

   END ABEBOOK;
