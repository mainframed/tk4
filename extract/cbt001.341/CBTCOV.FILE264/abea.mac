 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /**********************************************************************
 *                                                                     *
 * ABEA IS CALLED AFTER ALL MAINLINE INITIALIZATION HAS BEEN DONE.     *
 * ITS FUNCTION IS TO COORDINATE THE EXECUTION OF COMMANDS AS THEY ARE *
 * RECEIVED.                                                           *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABEA) PARMS(RHHDRPT) CHECK(YES);
   %INCLUDE ABESUBA;
   %INCLUDE ABEREC;
   DCL SETCAPS ENTRY;
   DCL ABECMD ENTRY(CHAR(*) VAR,PTR,PTR) RETURNS(FIXED BIN(15,0));
   DCL ABEC ENTRY;
   DCL ERRSW BIT(1) INIT('0'B);
   DCL STR CHAR(1024) VAR;

 /* ON ERROR BLOCK FOR OPTIMIZER ONLY
  */
   ON ERROR BEGIN;
     IF ERRSW
       THEN SIGNAL FINISH;
     ERRSW = '1'B;
     CURPNL = 0;
     GO TO LUP;
     END;
   #ABEATN=ABEATN;
   ON ATTENTION RH_ATNCNT=RH_ATNCNT+1;
   ON CONDITION(MYATTN) BEGIN;
     RH_ATNCNT=RH_ATNCNT+1;
     CURPNL = 0;
     GO TO LUP;
     END;
  LUP:
   ERRSW = '0'B;
   IF NXTRHEL^=NULL
     THEN DO;
       DO WHILE(NXTRHEL^=NULL); /* DO WHILE DATASETS ARE BEING EDITED */
         DO WHILE(RH_STACK^=NULL);
           CALL #POP(RHPTR,RH_STACK,RH_QUEUE,STR);
           IF RHPTR^=NULL
             THEN I=ABECMD(STR,RHPTR,RHHDRPT); /* PROCESS SUBCOMMAND */
           END;
         IF NXTRHEL^=NULL
           THEN CALL ABEC(RHHDRPT); /* GO ACCEPT TERMINAL INPUT */
         END;
       CALL #CRTZAP(RHPPTR);
       END;
 ABEATN: PROC; /* CALLED JUST TO ALLOW UPDATING OF RH_ATNCNT */
    DCL I FIXED BIN;
    I=0;
    END ABEATN;

   END ABEA;
