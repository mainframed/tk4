UT1GET   START 0                                                    TGS
********************************************************************TGS
*                  THIS RTNE WILL BE RESPONSIBLE FOR ACCESSING THE *TGS
*              SYSUT1 DATA SET.  AT EXIT, REG1 WILL CONTAIN THE    *TGS
*              ADDRESS OF THE NEXT SEQUENTIAL LOGICAL RECORD.      *TGS
*              IF EOF IS ENCOUNTERED, REG1 WILL BE SET TO ZERO     *TGS
*              BEFORE RETURN OF CONTROL.                           *TGS
*              BEFORE RETURN OF CONTROL.  REG0 WILL CONTAIN THE    *TGS
*              LENGTH OF THE RECORD AT EXIT.                       *TGS
********************************************************************TGS
         TGSREQU                                                    TGS
         SAVES RETCTL                                               TGS
         TM    INITSW,X'80'            HAS INITIALIZATION BEEN DONE TGS
         BC    1,BEGIN                 BR IF YES                    TGS
         OI    INITSW,X'80'            SET INITSW ON                TGS
         OPEN  (SYSUT1,(INPUT))                                     TGS
         TM    SYSUT1+48,X'10'         WAS IT GOOD OPEN             TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         SR    R1,R1                   SET R1 = 0                   TGS
         B     RETCTL                                               TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,SYSUT1+62            R0 = BLKSIZE                 TGS
         STH   R0,BUFLEN                                            TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,BUFADDR              STORE BUFFER ADDR            TGS
         ST    R1,NXTADDR              STORE BUFFER ADDR            TGS
         B     GETBLK                                               TGS
BEGIN    EQU   *                                                    TGS
         L     R2,NXTADDR                                           TGS
         LR    R3,R2                                                TGS
         S     R3,BUFADDR                                           TGS
         CH    R3,BLKLEN               HAS BLOCK BEEN ALL PROCESSED TGS
         BC    4,PROCREC               BR IF NOT                    TGS
GETBLK   EQU   *                                                    TGS
         L     R2,BUFADDR              R2 = AREA ADDR               TGS
         READ  DECB,SF,,(2),'S',MF=E                                TGS
         CHECK DECB                                                 TGS
         ST    R2,NXTADDR              RESET NXTADDR                TGS
         L     R3,DECB+16              R3 = IOB ADDR                TGS
         LH    R4,BUFLEN                                            TGS
         SH    R4,14(R3)               R4 = BLKSIZE - RESIDUAL CNT  TGS
*                                           FROM CSW IN IOB         TGS
         STH   R4,BLKLEN               RESET BLKLEN                 TGS
PROCREC  EQU   *                                                    TGS
         MVC   OUTREC+82(8),=CL8' '                                 TGS
         LR    R1,R2                   R1 = ADDR OF COMPRESSED REC  TGS
         LA    R2,OUTREC               R2 = ADDR OF EXPANDED REC    TGS
         L     R0,RECLEN                                            TGS
         CLC   0(5,R1),=X'02024B6107'  IS IT NEW MEMB               TGS
         BC    7,GOTLEN                                             TGS
         LA    R0,82                                                TGS
GOTLEN   EQU   *                                                    TGS
         L     RF,=V(EXPAND)                                        TGS
         BALR  RE,RF                   GO EXPAND RECORD             TGS
         ST    R1,NXTADDR              SET NXTADDR                  TGS
         LA    R1,OUTREC+2                                          TGS
         CLC   0(8,R1),=CL8'./'        IS IT NEW MEMB               TGS
         BC    8,GOTCTL                BR IF YES                    TGS
         L     R0,RECLEN                                            TGS
         B     RETCTL                                               TGS
GOTCTL   EQU   *                                                    TGS
         LA    R0,82                                                TGS
         ST    R0,RECLEN                                            TGS
         CLI   44(R1),C'L'             IS IT LONG MEMB              TGS
         BC    7,RETCTL                BR IF NOT                    TGS
         MVC   RECLEN,=F'90'                                        TGS
         B     RETCTL                                               TGS
         B     RETCTL                                               TGS
UT1END   EQU   *                                                    TGS
         CLOSE SYSUT1                                               TGS
         L     R1,BUFADDR                                           TGS
         LH    R0,BUFLEN                                            TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         SR    R1,R1                   INDICATE EOF                 TGS
         B     RETCTL                                               TGS
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,EODAD=UT1END,MACRF=R          TGS
         READ  DECB,SF,SYSUT1,MF=L                                  TGS
BUFADDR  DC    F'0'          ADDR OF BUFFER                         TGS
NXTADDR  DC    F'0'          ADDR OF NEXT SLOT IN BUFFER            TGS
RECLEN   DC    F'82'                                                TGS
BUFLEN   DS    H                                                    TGS
BLKLEN   DS    H                                                    TGS
INITSW   DC    X'00'                                                TGS
OUTREC   DS    CL90                                                 TGS
         LTORG                                                      TGS
         END
