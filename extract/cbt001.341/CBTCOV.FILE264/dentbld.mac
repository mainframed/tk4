 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENTBLD */
 DENTBLD:  PROC(DCBPATPT,DCBCOR,DDNAME,MEMNAME) RETURNS(BIT(1))
           OPTIONS(REENTRANT);
         DCL DCBPATPT PTR;
         DCL DCBCOR CHAR(112);
         DCL (DDNAME,MEMNAME) CHAR(8);
         % INCLUDE DCB;
         DCL DCBPAT CHAR(112) BASED(DCBPATPT);
         DCL (SYSDCBP,SYSOPEN,SYSBLDL,SYSCLOS) ENTRY;
         DCL NULL BUILTIN;
         DCL 1 LIST,
           2 FF FIXED BIN(15,0) INIT(1),
           2 LL FIXED BIN(15,0) INIT(12),
           2 NAME CHAR(8),
           2 TTRK CHAR(4),
           2 Z BIT(8),
           2 C BIT(8),
           2 USERDATA CHAR(64);
         DCL LISTPTR PTR;
         DCL RETCODE FIXED BIN(31,0);
         IF DCBPATPT=NULL
           THEN CALL SYSDCBP(DCBPATPT,DDNAME);
         DCBCOR=DCBPATPT->DCBPAT;
         DCBPTR=ADDR(DCBCOR);
         DCBDDNAM=DDNAME;
         CALL SYSOPEN(DCBPTR,'INPUT');
         IF ^SUBSTR(DCBFNDA.DCBOFLGS,4,1)
           THEN RETURN('0'B); /* UNSUCCESSFUL OPEN */
         LISTPTR = ADDR(LIST);
         NAME = MEMNAME;
         CALL SYSBLDL(DCBPTR,LISTPTR,RETCODE);
         CALL SYSCLOS(DCBPTR);
         IF RETCODE=0
           THEN RETURN('1'B);
           ELSE RETURN('0'B);
         END;
