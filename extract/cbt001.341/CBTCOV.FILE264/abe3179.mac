 /**********************************************************************
 * ABE3179 IS INVOKED VIA THE PGM SUBCOMMAND OF ABE ISSUED FOR A       *
 * DATASET PRODUCED AS A RESULT OF RUNNING THE FOLLOWING TWO           *
 * SUBCOMMANDS:                                                        *
 *   TPUT 'C2'                                                         *
 *   TPG  'F3000501FF02' RESPONSE                                      *
 * THIS PROGRAM WILL BREAK DOWN THE DATA IN RESPONSE TO GIVE A READABLE*
 * DESCRIPTION OF THE DEVICE.                                          *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABE3179);
   %INCLUDE ABESUBA;

   DCL TROUT ENTRY;
   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL I FIXED BIN(15,0);
   DCL CMD CHAR(80) VAR;
   DCL WRK PTR;

   I=#CMD('SPLIT 2',RHPTR,RHHDRPT);
   LUP: DO FOREVER=1 REPEAT FOREVER;
     I=UNSPEC(SUBSTR(LSTHDR->RECSTR,1,2)); /* LENGTH REPLY */
     IF I>LENGTH(LSTHDR->RECSTR)
       THEN LEAVE;
     PUT STRING(CMD) EDIT('B SPLIT ',I+1)(A,P'ZZZZ9');
     I=#CMD(CMD,RHPTR,RHHDRPT);
     END;
   IF I=16448 /* 4040? */
     THEN I=#CMD('B DEL',RHPTR,RHHDRPT);
   I=#CMD('B MK 1',RHPTR,RHHDRPT);
   I=#CMD('T+1 V',RHPTR,RHHDRPT);
   DO UNTIL(RECSEQ>MARKARY(1));
     CALL PROCESS;
     I=#CMD('DOWN 1',RHPTR,RHHDRPT);
     END;
   I=#CMD('T SUBSTR ''88''',RHPTR,RHHDRPT);
   I=#CMD('T+1 SUBSTR '' AID BYTE''',RHPTR,RHHDRPT);
   I=#CMD('T+2 1M DEL',RHPTR,RHHDRPT);
   I=#CMD('TOP',RHPTR,RHHDRPT);

 PROCESS: PROC;
 /**********************************************************************
 * CALLED ONCE FOR EACH RECORD CONTAINING A QUERY REPLY.               *
 **********************************************************************/
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=REPEAT(' ',LENGTH(RECPTR->RECSTR)*2-1);
   CALL TROUT(RECSTR,LSTHDR->RECSTR);
   I=UNSPEC(SUBSTR(RECSTR,4,1));
   SELECT(I);
     WHEN(128) CALL SUMMARY;
     WHEN(129) CALL USABLE_AREA;
     WHEN(132) CALL ALPHANUMERIC_PARTITIONS;
     WHEN(133) CALL CHARACTER_SETS;
     WHEN(134) CALL COLOR;
     WHEN(135) CALL HIGHLIGHTING;
     WHEN(136) CALL REPLY_MODE;
     WHEN(150) CALL STORAGE_POOLS;
     WHEN(153) CALL AUXILIARY_DEVICE;
     WHEN(166) CALL IMPLICIT_PARTITION;
     WHEN(168) CALL BACKGROUND_TRANSPARENCY;
     WHEN(176) CALL SEGMENT;
     WHEN(177) CALL PROCEDURE;
     WHEN(178) CALL LINE_TYPE;
     WHEN(179) CALL PORT;
     WHEN(180) CALL GRAPHIC_COLOR;
     WHEN(182) CALL GRAPHIC_SYMBOL_SETS;
     OTHERWISE;
     END; /* SELECT */
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   END PROCESS;

 ALPHANUMERIC_PARTITIONS: PROC;
   DCL 1 ALPHANUMERIC_PARTITIONS_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 MAXPART BIT(1),
     2 TOTSTOR FIXED BIN(15,0),
     2 FLAGS,
      3 VERTWIND BIT(1),
      3 HORWIND BIT(1),
      3 RESERVED1 BIT(1),
      3 APADDR BIT(1),
      3 PARTPROT BIT(1),
      3 LOCCOPY BIT(1),
      3 MODPART BIT(1),
      3 RESERVED2 BIT(1);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' ALPHANUMERIC PARTITIONS';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  DEFINES THE ZERO PARTITION SUPPORT OF THE DISPLAY';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR)
     EDIT('MAXIMUM NUMBER OF ALPHA PARTITIONS=',
     MAXPART)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR)
     EDIT('TOTAL AVAILABLE PARTITION STORAGE=',
     TOTSTOR)(X(2),A,F(4));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF VERTWIND
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('VERTICAL WINDOWING SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('VERTICAL WINDOWING NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF HORWIND
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('HORIZONTAL WINDOWING SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('HORIZONTAL WINDOWING NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF HORWIND
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('HORIZONTAL WINDOWING SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('HORIZONTAL WINDOWING NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF APADDR
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('AP ADDRESSIBILITY SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('AP ADDRESSIBILITY NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF PARTPROT
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('PARTITION PROTECTION SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('PARTITION PROTECTION NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF PARTPROT
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('PRESENTATION SPACE LOCAL COPY SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('PRESENTATION SPACE LOCAL COPY NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF PARTPROT
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('MODIFY PARTITION SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('MODIFY PARTITION NOT SUPPORTED')(X(2),A);
   END ALPHANUMERIC_PARTITIONS;

 AUXILIARY_DEVICE: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS AUXILIARY DEVICE';
   END AUXILIARY_DEVICE;

 BACKGROUND_TRANSPARENCY: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS BACKGROUND TRANSPARENCY';
   END BACKGROUND_TRANSPARENCY;

 CHARACTER_SETS: PROC;
   DCL 1 ALPHANUMERIC_PARTITIONS_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 FLAGS,
      3 GE BIT(1),
      3 RESERVED1 BIT(1),
      3 LOADPS BIT(1),
      3 LOADPSE BIT(1),
      3 ONECHAR BIT(1),
      3 TWOBYTE BIT(1),
      3 GCIDBIT BIT(1),
      3 RESERVED2 BIT(1),
     2 RESERVED3 CHAR(1),
     2 SLOTWIDTH BIT(8),
     2 SLOTHEIGHT BIT(8),
     2 SUPLOAD BIT(32),
     2 DESCLEN BIT(8),
     2 DESCONE CHAR(1);
   DCL 1 DESC BASED(DESCPT) UNALIGNED,
     2 CSID BIT(8),
     2 CSFLAGS,
      3 LOADCHAR BIT(1),
      3 SINGLEPLANE BIT(1),
      3 ONEBYTE BIT(1),
      3 LCIDCOMP BIT(1),
      3 RESERVED1 BIT(4),
     2 LCID BIT(8),
     2 GCID FIXED BIN(31,0);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS CHARACTER SETS';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  PROVIDES SPECIFIC INFORMATION ABOUT EACH';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  CHARACTER SET AS WELL AS FLAGS THAT DEFINE';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  DEVICE SUPPORT OF CERTAIN FUNCTIONS SUCH AS';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  GRAPHIC ESCAPE.';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF GE
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('GE SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('GE NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF LOADPS
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('LOAD PS SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('LOAD PS NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF LOADPSE
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('LOAD PS EXTENSION SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('LOAD PS EXTENSION NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF ONECHAR
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('NOT ONE CHARACTER SLOT SIZE ONLY')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('ONE CHARACTER SLOT SIZE ONLY')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF TWOBYTE
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('TWO-BYTE CODED CHARACTER SET SUPPORTED')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('TWO-BYTE CODED CHARACTER SET NOT SUPPORTED')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF GCIDBIT
     THEN PUT STRING(LSTHDR->RECSTR)
       EDIT('GCID IS PRESENT')(X(2),A);
     ELSE PUT STRING(LSTHDR->RECSTR)
       EDIT('GCID IS NOT PRESENT')(X(2),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT CHARACTER SLOT WIDTH=',
     SLOTWIDTH,' DOTS')(X(2),A,F(3),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT CHARACTER SLOT HEIGHT=',
     SLOTHEIGHT,' DOTS')(X(2),A,F(3),A);
   IF SUPLOAD='0111'B
     THEN DO;
       I=#CMD('B BL 1',RHPTR,RHHDRPT);
       PUT STRING(LSTHDR->RECSTR)
       EDIT('THIS DEVICE SUPPORTS LOAD PS FORMAT TYPES 1,2,3')(X(2),A);
       END;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('LENGTH OF EACH DESCRIPTOR',
     DESCLEN)(X(2),A,F(3));
   DESCPT=ADDR(DESCONE);
   J=DESCLEN;
   K=QLEN-13;
   DO K=QLEN-13 REPEAT K-J WHILE(K>0);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     PUT STRING(LSTHDR->RECSTR) EDIT('CHARACTER SET ID=',
       CSID)(X(3),A,F(3));
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     IF LOADCHAR
       THEN PUT STRING(LSTHDR->RECSTR)
         EDIT('LOADABLE CHARACTER SET')(X(3),A);
       ELSE PUT STRING(LSTHDR->RECSTR)
         EDIT('NON-LOADABLE CHARACTER SET')(X(3),A);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     IF SINGLEPLANE
       THEN PUT STRING(LSTHDR->RECSTR)
         EDIT('NOT A SINGLE-PLANE CHARACTER SET')(X(3),A);
       ELSE PUT STRING(LSTHDR->RECSTR)
         EDIT('SINGLE-PLANE CHARACTER SET')(X(3),A);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     IF ONEBYTE
       THEN PUT STRING(LSTHDR->RECSTR)
         EDIT('NOT A ONE-BYTE CODED CHARACTER SET')(X(3),A);
       ELSE PUT STRING(LSTHDR->RECSTR)
         EDIT('A ONE-BYTE CODED CHARACTER SET')(X(3),A);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     IF LCIDCOMP
       THEN PUT STRING(LSTHDR->RECSTR)
         EDIT('NO LCID COMPARE')(X(3),A);
       ELSE PUT STRING(LSTHDR->RECSTR)
         EDIT('LCID COMPARE')(X(3),A);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     PUT STRING(LSTHDR->RECSTR) EDIT('LCID=',
       LCID)(X(3),A,F(3));
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     PUT STRING(LSTHDR->RECSTR) EDIT('GCID=',
       GCID)(X(3),A,F(9));
     DESCPT=PTROUT(PTRIN(DESCPT)+J);
     END;
   END CHARACTER_SETS;

 COLOR: PROC;
   DCL OUTSTR CHAR(4);
   DCL 1 COLOR_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 RESERVED CHAR(1),
     2 PAIRCNT BIT(8),
     2 PAIRS(8) CHAR(2);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS COLOR';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   K=PAIRCNT;
   PUT STRING(LSTHDR->RECSTR) EDIT('NUMBER OF COLOR ATTRIBUTE PAIRS=',
     K)(X(2),A,F(3));
   DO J=1 TO K;
     CALL TROUT(PAIRS(J),OUTSTR);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     LSTHDR->RECSTR='  '××OUTSTR;
     END;
   END COLOR;

 GRAPHIC_COLOR: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS GRAPHIC COLOR';
   END GRAPHIC_COLOR;

 GRAPHIC_SYMBOL_SETS: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS GRAPHIC SYMBOL SETS';
   END GRAPHIC_SYMBOL_SETS;

 HIGHLIGHTING: PROC;
   DCL OUTSTR CHAR(4);
   DCL 1 HIGHLIGHTING_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 PAIRCNT BIT(8),
     2 PAIRS(8) CHAR(2);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS HIGHLIGHTING';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('NUMBER OF PAIRS=',
     PAIRCNT)(X(2),A,F(3));
   K=PAIRCNT;
   DO J=1 TO K;
     CALL TROUT(PAIRS(J),OUTSTR);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     LSTHDR->RECSTR='  '××OUTSTR;
     END;
   END HIGHLIGHTING;

 IMPLICIT_PARTITION: PROC;
   DCL 1 IMPLICIT_PARTITION_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 RESERVED1 CHAR(2),
     2 PARMLEN BIT(8),
     2 PRTSIZE BIT(8),
     2 RESERVED2 CHAR(1),
     2 PARTWIDTH FIXED BIN(15,0),
     2 PARTHEIGHT FIXED BIN(15,0),
     2 ALTWIDTH FIXED BIN(15,0),
     2 ALTHEIGHT FIXED BIN(15,0),
     2 PARMLEN2 BIT(8),
     2 CELLDIM BIT(8),
     2 RESERVED3 BIT(8),
     2 CELLWIDTH FIXED BIN(15,0),
     2 CELLHEIGHT FIXED BIN(15,0),
     2 ALTCELLWIDTH FIXED BIN(15,0),
     2 ALTCELLHEIGHT FIXED BIN(15,0);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS IMPLICIT PARTITION';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('IMPLICIT PARTITION SIZES=',
     PRTSIZE)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT PARTITION WIDTH=',
     PARTWIDTH)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT PARTITION HEIGHT=',
     PARTHEIGHT)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('ALTERNATE PARTITION WIDTH=',
     ALTWIDTH)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('ALTERNATE PARTITION HEIGHT=',
     ALTHEIGHT)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('CHARACTER-CELL DIMENSIONS=',
     CELLDIM)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR)
     EDIT('WIDTH OF CHARACTER CELL FOR DEFAULT IMPLICIT PARTITION SIZE',
     CELLWIDTH)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR)
     EDIT('HEIGHT OF CHARACTER CELL FOR DEFAULT IMPLICIT PARTITION SIZE'
     ,CELLHEIGHT)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT(
     'WIDTH OF CHARACTER CELL FOR ALTERNATE IMPLICIT PARTITION SIZE',
     ALTCELLWIDTH)(X(2),A,F(3));
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT(
     'HEIGHT OF CHARACTER CELL FOR ALTERNATE IMPLICIT PARTITION SIZE',
     ALTCELLHEIGHT)(X(2),A,F(3));
   END IMPLICIT_PARTITION;

 LINE_TYPE: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS LINE TYPE';
   END LINE_TYPE;

 PORT: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS PORT';
   END PORT;

 PROCEDURE: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS PROCEDURE';
   END PROCEDURE;

 REPLY_MODE: PROC;
   DCL 1 REPLY_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 FIELD BIT(8),
     2 EXTENDED_FIELD BIT(8),
     2 CHARACTER_MODE BIT(8);

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS REPLY MODE';
   IF FIELD='0'B
     THEN DO;
       I=#CMD('B BL 1',RHPTR,RHHDRPT);
       LSTHDR->RECSTR='  FIELD MODE IS SUPPORTED';
       END;
   IF EXTENDED_FIELD='00000001'B
     THEN DO;
       I=#CMD('B BL 1',RHPTR,RHHDRPT);
       LSTHDR->RECSTR='  EXTENDED FIELD MODE IS SUPPORTED';
       END;
   IF CHARACTER_MODE='00000010'B
     THEN DO;
       I=#CMD('B BL 1',RHPTR,RHHDRPT);
       LSTHDR->RECSTR='  CHARACTER MODE IS SUPPORTED';
       END;
   END REPLY_MODE;

 SEGMENT: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS SEGMENT';
   END SEGMENT;

 STORAGE_POOLS: PROC;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS STORAGE POOLS';
   END STORAGE_POOLS;

 SUMMARY: PROC;
   DCL OUTSTR CHAR(2);
   DCL QUERY CHAR(80) VAR;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS SUMMARY';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR='  THE QUERY REPLIES I SUPPORT ARE AS FOLLOWS:';
   J=UNSPEC(SUBSTR(RECSTR,1,2)); /* LENGTH REPLY */
   DO K=1 TO J-4;
     I=UNSPEC(SUBSTR(RECSTR,K+4,1));
     SELECT(I);
       WHEN(128) QUERY='SUMMARY';
       WHEN(129) QUERY='USABLE AREA';
       WHEN(132) QUERY='ALPHANUMERIC PARTITIONS';
       WHEN(133) QUERY='CHARACTER SETS';
       WHEN(134) QUERY='COLOR';
       WHEN(135) QUERY='HIGHLIGHTING';
       WHEN(136) QUERY='REPLY MODE';
       WHEN(150) QUERY='STORAGE POOLS';
       WHEN(153) QUERY='AUXILIARY DEVICE';
       WHEN(166) QUERY='IMPLICIT PARTITION';
       WHEN(168) QUERY='BACKGROUND TRANSPARENCY';
       WHEN(176) QUERY='SEGMENT';
       WHEN(177) QUERY='PROCEDURE';
       WHEN(178) QUERY='LINE TYPE';
       WHEN(179) QUERY='PORT';
       WHEN(180) QUERY='GRAPHIC COLOR';
       WHEN(182) QUERY='GRAPHIC SYMBOL SETS';
       WHEN(143) QUERY='OEM AUXILIARY DEVICE';
       OTHERWISE QUERY='UNKNOWN';
       END; /* SELECT */
     CALL TROUT(SUBSTR(RECSTR,K+4,1),OUTSTR);
     I=#CMD('B BL 1',RHPTR,RHHDRPT);
     LSTHDR->RECSTR='   X'''××OUTSTR××''' '××QUERY;
     END;
   END SUMMARY;

 USABLE_AREA: PROC;
   DCL 1 USABLE_AREA_STR BASED(RPT) UNALIGNED,
     2 QLEN FIXED BIN(15,0),
     2 QREP CHAR(1),
     2 QCODE CHAR(1),
     2 FLAGS,
      3 RESERVED1 BIT(2),
      3 EXSF BIT(1),
      3 HARDCOPY BIT(1),
      3 BITADDR BIT(4),
      3 VARCELLS BIT(1),
      3 MATRIXCHAR BIT(1),
      3 BYTE69 BIT(1),
      3 RESERVED2 BIT(5),
     2 UAWIDTH FIXED BIN(15,0),
     2 UAHEIGHT FIXED BIN(15,0),
     2 UNITMEAS BIT(8),
     2 HORPELNUM FIXED BIN(15,0),
     2 HORPELDEN FIXED BIN(15,0),
     2 VERPELNUM FIXED BIN(15,0),
     2 VERPELDEN FIXED BIN(15,0),
     2 CELLWIDTH BIT(8),
     2 CELLHEIGHT BIT(8),
     2 CHARBUF FIXED BIN(15,0),
     2 MINMAXCEL FIXED BIN(31,0),
     2 PARMLEN BIT(8),
     2 ALTUA BIT(8),
     2 AUAFLAGS BIT(8),
     2 AUAID BIT(8),
     2 AUAWIDTH FIXED BIN(15,0),
     2 AUAHEIGHT FIXED BIN(15,0),
     2 AUAUNITMEAS CHAR(1),
     2 AUAHORPELNUM FIXED BIN(15,0),
     2 AUAHORPELDEN FIXED BIN(15,0),
     2 AUAVERPELNUM FIXED BIN(15,0),
     2 AUAVERPELDEN FIXED BIN(15,0),
     2 AUACELLWIDTH BIT(8),
     2 AUACELLHEIGHT BIT(8);
   DCL UNITS CHAR(12) VAR;
   DCL (FLTA,FLTB,FLTC) DEC FLOAT;

   RPT=PTROUT(PTRIN(ADDR(RECSTR))+2);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   LSTHDR->RECSTR=' QUERY REPLY IS USABLE AREA';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF EXSF
     THEN LSTHDR->RECSTR='  EXSF NOT SUPPORTED';
     ELSE LSTHDR->RECSTR='  EXSF SUPPORTED';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF HARDCOPY
     THEN LSTHDR->RECSTR='  HARDCOPY DEVICE';
     ELSE LSTHDR->RECSTR='  NOT A HARDCOPY DEVICE';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   SELECT(BITADDR);
     WHEN('0001'B) LSTHDR->RECSTR='  14-BIT ADDRESSING ALLOWED';
     WHEN('0011'B) LSTHDR->RECSTR='  12/14/16-BIT ADDRESSING ALLOWED';
     WHEN('1111'B) LSTHDR->RECSTR='  DEVICE IN LU TYPE 1 MODE';
     OTHERWISE LSTHDR->RECSTR='  BIT ADDRESSING UNKNOWN';
     END; /* SELECT */
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF VARCELLS
     THEN LSTHDR->RECSTR='  VARIABLE CELLS SUPPORTED';
     ELSE LSTHDR->RECSTR='  VARIABLE CELLS NOT SUPPORTED';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF MATRIXCHAR
     THEN LSTHDR->RECSTR='  NON-MATRIX CHARACTER';
     ELSE LSTHDR->RECSTR='  MATRIX CHARACTER';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   IF BYTE69
     THEN LSTHDR->RECSTR='  NO VALUE IN BYTES 6-9 IN CELLS';
     ELSE LSTHDR->RECSTR='  VALUE IN BYTES 6-9 IN CELLS';
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('USABLE AREA WIDTH = ',UAWIDTH,
     ' CHARACTERS')(X(2),A,F(3),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('USABLE AREA HEIGHT = ',UAHEIGHT,
     ' CHARACTERS')(X(2),A,F(3),A);
   IF UNITMEAS='0'B
     THEN UNITS='INCHES';
     ELSE UNITS='MILLIMETERS';
   FLTA=HORPELNUM;
   FLTB=HORPELDEN;
   FLTC=FLTA/FLTB;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('HORIZONTAL PEL DISTANCE = ',FLTC,
     ' ',UNITS)(X(2),A,F(8,6),A,A);
   FLTA=VERPELNUM;
   FLTB=VERPELDEN;
   FLTC=FLTA/FLTB;
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('VERTICAL PEL DISTANCE = ',FLTC,
     ' ',UNITS)(X(2),A,F(8,6),A,A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   I=CELLWIDTH;
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT CELL WIDTH = ',I,' DOTS')
     (X(2),A,F(3),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   I=CELLHEIGHT;
   PUT STRING(LSTHDR->RECSTR) EDIT('DEFAULT CELL HEIGHT = ',I,' DOTS')
     (X(2),A,F(3),A);
   I=#CMD('B BL 1',RHPTR,RHHDRPT);
   PUT STRING(LSTHDR->RECSTR) EDIT('CHARACTER BUFFER SIZE = ',CHARBUF)
     (X(2),A,F(4));
   END USABLE_AREA;

    END ABE3179;
