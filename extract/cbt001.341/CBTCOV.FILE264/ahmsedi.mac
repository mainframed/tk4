   /* AHMSEDI - HARDWARE MANAGEMENT SYSTEM COMMON ROUTINES */

   /* FOLLOWING ARE GLOBAL VARIABLES */

   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*), CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL CALCPRT ENTRY(DEC FLOAT(16))
     RETURNS(CHAR(80) VAR);
   DCL CONVERT ENTRY(PTR,
     CHAR(*),FIXED BIN(15,0),FIXED BIN(15,0),
     CHAR(*),FIXED BIN(15,0),FIXED BIN(15,0))
     RETURNS(CHAR(256) VAR);
   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL TYPETAB(7) CHAR(8) VAR STATIC INIT(
     'FLOAT','CHAR','CHAR','PACKED','HEX','BIT','FIXED');
   DCL MISSING DEC FLOAT(16),
       MISSINGCH CHAR(8) BASED(ADDR(MISSING));
   DCL MISSING_PACKED FIXED DEC(15,0) STATIC INIT(-999999999999999),
       MISSING_PACKED_CH CHAR(8) BASED(ADDR(MISSING_PACKED));
   DCL MISSING_INTEGER FIXED BIN(31,0) STATIC INIT(-2147483648),
       MISSING_INTEGER_CH CHAR(4) BASED(ADDR(MISSING_INTEGER));
   DCL MISSING_SMALLINT FIXED BIN(15,0) STATIC INIT(-32768),
       MISSING_SMALLINT_CH CHAR(2) BASED(ADDR(MISSING_INTEGER));

   UNSPEC(MISSING)='1'B; /* SET MISSING VALUE */

   %DCL FEATDESC_STRUCTURE ENTRY;
 %FEATDESC_STRUCTURE: PROC STATEMENT RETURNS(CHAR);
   RETURN('
   DCL 1 ROA,
     2 FID FIXED BIN(31,0),
     2 VENDOR CHAR(3),
     2 DEVICE CHAR(10),
     2 FEATURE CHAR(11),
     2 FEATDESC CHAR(30);
   DCL ROA_IND(5) FIXED BIN(15,0);
   DCL VENDORIX   FIXED BIN(15,0) STATIC INIT(2);
   DCL DEVICEIX   FIXED BIN(15,0) STATIC INIT(3);
   DCL FEATUREIX  FIXED BIN(15,0) STATIC INIT(4);
   DCL FEATDESCIX FIXED BIN(15,0) STATIC INIT(5);
   ');
   %END FEATDESC_STRUCTURE;

   %DCL FEATURES_STRUCTURE ENTRY;
 %FEATURES_STRUCTURE: PROC STATEMENT RETURNS(CHAR);
   RETURN('
   DCL 1 ROB,
     2 ID FIXED BIN(31,0),
     2 FID FIXED BIN(31,0),
     2 COST CHAR(7),
     2 QTY FIXED BIN(31,0),
     2 POID FIXED BIN(31,0);
   DCL ROB_IND(4) FIXED BIN(15,0);
   DCL IDIX   FIXED BIN(15,0) STATIC INIT(1);
   DCL FIDIX  FIXED BIN(15,0) STATIC INIT(2);
   DCL COSTIX FIXED BIN(15,0) STATIC INIT(3);
   DCL QTYIX  FIXED BIN(15,0) STATIC INIT(4);
   DCL POIDIX FIXED BIN(15,0) STATIC INIT(5);
   ');
   %END FEATURES_STRUCTURE;

   %DCL LOCNAMES_STRUCTURE ENTRY;
 %LOCNAMES_STRUCTURE: PROC STATEMENT RETURNS(CHAR);
   RETURN('
   DCL 1 ROC,
     2 LOCATION CHAR(20),
     2 LOCNO FIXED BIN(31,0),
     2 SPEED FIXED BIN(31,0);
   DCL ROC_IND(3) FIXED BIN(15,0);
   DCL LOCATIONIX   FIXED BIN(15,0) STATIC INIT(1);
   DCL LOCNOIX      FIXED BIN(15,0) STATIC INIT(2);
   DCL SPEEDIX      FIXED BIN(15,0) STATIC INIT(3);
   ');
   %END LOCNAMES_STRUCTURE;

   %DCL THINGTAB_STRUCTURE ENTRY;
 %THINGTAB_STRUCTURE: PROC STATEMENT RETURNS(CHAR);
   RETURN('
   DCL 1 ROD,
     2 ID FIXED BIN(31,0),
     2 CID FIXED BIN(31,0),
     2 NODE CHAR(8),
     2 LTERM CHAR(4),
     2 LOCNO FIXED BIN(31,0),
     2 EXT FIXED BIN(31,0),
     2 ROOM CHAR(8),
     2 VENDOR CHAR(3),
     2 DEVICE CHAR(10),
     2 SERIAL CHAR(15);
     2 POID FIXED BIN(31,0);
   DCL ROD_IND(10) FIXED BIN(15,0);
   DCL IDIX         FIXED BIN(15,0) STATIC INIT(1);
   DCL CIDIX        FIXED BIN(15,0) STATIC INIT(2);
   DCL NODEIX       FIXED BIN(15,0) STATIC INIT(3);
   DCL LTERMIX      FIXED BIN(15,0) STATIC INIT(4);
   DCL LOCNOIX      FIXED BIN(15,0) STATIC INIT(5);
   DCL EXTIX        FIXED BIN(15,0) STATIC INIT(6);
   DCL ROOMIX       FIXED BIN(15,0) STATIC INIT(7);
   DCL VENDORIX     FIXED BIN(15,0) STATIC INIT(8);
   DCL DEVICEIX     FIXED BIN(15,0) STATIC INIT(9);
   DCL SERIALIX     FIXED BIN(15,0) STATIC INIT(10);
   DCL POIDIX       FIXED BIN(15,0) STATIC INIT(11);
   ');
   %END THINGTAB_STRUCTURE;

   %DCL VENDORS_STRUCTURE ENTRY;
 %VENDORS_STRUCTURE: PROC STATEMENT RETURNS(CHAR);
   RETURN('
   DCL 1 ROE,
     2 VENDOR CHAR(3),
     2 VENDNAME CHAR(14),
     2 VENDADDR CHAR(12),
     2 VENDCITY CHAR(11),
     2 VENDSTAT CHAR(11),
     2 VENDZIP FIXED BIN(31,0),
     2 VENDTELE CHAR(14);
   DCL ROE_IND(7) FIXED BIN(15,0);
   DCL VENDORIX     FIXED BIN(15,0) STATIC INIT(1);
   DCL VENDNAMEIX   FIXED BIN(15,0) STATIC INIT(2);
   DCL VENDADDRIX   FIXED BIN(15,0) STATIC INIT(3);
   DCL VENDCITYIX   FIXED BIN(15,0) STATIC INIT(4);
   DCL VENDSTATIX   FIXED BIN(15,0) STATIC INIT(5);
   DCL VENDZIPIX    FIXED BIN(15,0) STATIC INIT(6);
   DCL VENDTELEIX   FIXED BIN(15,0) STATIC INIT(7);
   ');
   %END VENDORS_STRUCTURE;

 CONVERT_INTFC: PROC(STR,R) RETURNS(CHAR(256) VAR);
 /**********************************************************************
 *                                                                     *
 * INTERFACE TO CONVERT SUBROUTINE.  STR IS STRING CONTAINING DATA TO  *
 * BE CONVERTED.  R POINTS TO THE FROM-RHNAMESTR.                      *
 *                                                                     *
 **********************************************************************/
   DCL STR CHAR(*) VAR;
   DCL R PTR;

   IF LENGTH(STR)=0
     THEN RETURN('NULL'); /* MISSING VALUE */
   RETURN(CALCPRT(GET_OPERAND_CONVERT(STR,R)));
   END CONVERT_INTFC;

 FINDFLD: PROC(DSPTR,KEY);
 /**********************************************************************
 *                                                                     *
 * CALLED TO LOCATE A FIELD IN THE CURRENT RECORD.                     *
 *                                                                     *
 **********************************************************************/
   DCL DSPTR PTR;
   DCL KEY CHAR(*);
   DO RHNAMPTR=DSPTR->RHUSEHD REPEAT RHNLINK WHILE(RHNAMPTR^=NULL)
     UNTIL(RHNNAME=KEY);
     END;
   IF RHNAMPTR=NULL
     THEN SIGNAL ERROR;
   END FINDFLD;

 GET_OPERAND_CONVERT: PROC(STR,R) RETURNS(DEC FLOAT(16));
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN THE NUMERIC VALUE FOR A GIVEN FIELD NAME.          *
 *                                                                     *
 **********************************************************************/
   DCL (D,R,S) PTR;
   DCL STR CHAR(*) VAR;
   DCL CON CHAR(8) VAR;
   DCL RET DEC FLOAT(16),
       RETCH CHAR(8) DEF RET;
   DCL INTYPE CHAR(8) VAR;
   IF R=NULL
     THEN INTYPE='CHAR'; /* IF R IS NULL, ASSUME CHARACTER */
     ELSE INTYPE=TYPETAB(R->RHNTYPE);
   CON=CONVERT(PTROUT(PTRIN(ADDR(STR))+2),
     INTYPE,LENGTH(STR),0,
     'FLOAT',8,0);
   IF LENGTH(CON)=0
     THEN RET=MISSING;
     ELSE RETCH=CON;
   RETURN(RET);
   END GET_OPERAND_CONVERT;

 GETDATA: PROC(KEY,TRH) RETURNS(CHAR(50) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A VALUE FROM THE CURRENT RECORD.                   *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TRH PTR;
   CALL FINDFLD(TRH,KEY);
   RETURN(GETDATA_SUB(TRH->RECPTR,RHNAMPTR));
   END GETDATA;

 GETDATA_NULLCK: PROC(STR,R) RETURNS(BIT(1));
 /**********************************************************************
 *                                                                     *
 * CALLED BY CONVERT_INTFC TO CHECK FOR MISSING VALUE.  IF SO, '1'B    *
 * WILL BE RETURNED.                                                   *
 *                                                                     *
 **********************************************************************/
   DCL STR CHAR(*) VAR;
   DCL CK CHAR(256) VAR;
   DCL (R,WRKPTR) PTR;
   DCL INTEGER FIXED BIN(31,0) BASED(WRKPTR),
       SMALLINT FIXED BIN(15,0) BASED(WRKPTR);

   IF LENGTH(STR)=0
     THEN RETURN('1'B); /* MISSING VALUE */
   SELECT(R->RHNTYPE);
     WHEN(2,3) DO; /* CHAR,ZONED */
       IF STR=''
         THEN RETURN('1'B); /* MISSING VALUE */
       CK=SUBSTR(STR,PREFIX(STR,' ')); /* ELIMINATE LEADING BLANKS */
       CK=SUBSTR(STR,1,SUFFIX(STR,' ')); /* ELIMINATE TRAILING BLANKS */
       IF CK='.'
         THEN RETURN('1'B); /* MISSING VALUE */
         ELSE RETURN('0'B); /* NOT MISSING VALUE */
       END;
     OTHERWISE DO;
       WRKPTR=PTROUT(PTRIN(ADDR(STR))+2);
       SELECT(R->RHNTYPE);
         WHEN(1) /* FLOAT */
           IF STR=MISSINGCH
             THEN RETURN('1'B);
             ELSE RETURN('0'B);
         WHEN(4) /* PACKED */
           IF STR=SUBSTR(MISSING_PACKED_CH,8-LENGTH(STR)+1)
             THEN RETURN('1'B);
             ELSE RETURN('0'B);
         WHEN(7) /* FIXED */
           IF LENGTH(STR)=4
             THEN IF STR=MISSING_INTEGER_CH
               THEN RETURN('1'B);
               ELSE RETURN('0'B);
             ELSE IF STR=MISSING_SMALLINT_CH
               THEN RETURN('1'B);
               ELSE RETURN('0'B);
         OTHERWISE DO; /* UNSUPPORTED DATA TYPE */
           MSGDATA='WARNING - UNSUPPORTED DATA TYPE SET TO MISSING';
           RETURN('1'B);
           END; /* OTHERWISE */
         END; /* SELECT */
       END; /* OTHERWISE */
     END; /* SELECT */
   END GETDATA_NULLCK;

 GETDATA_SUB: PROC(RPT,RHN) RETURNS(CHAR(50) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A VALUE FROM THE CURRENT RECORD AND FIELD.         *
 *                                                                     *
 **********************************************************************/
   DCL (RPT,RHN) PTR;
   DCL STR CHAR(50) VAR;
   IF RHN->RHNPOS>LENGTH(RPT->RECSTR)
     THEN RETURN('');
   IF RHN->RHNPOS+RHN->RHNLNG-1>LENGTH(RPT->RECSTR)
     THEN STR=SUBSTR(RPT->RECSTR,RHN->RHNPOS);
     ELSE STR=SUBSTR(RPT->RECSTR,RHN->RHNPOS,RHN->RHNLNG);
   IF GETDATA_NULLCK(STR,RHN)
     THEN RETURN('.'); /* MISSING VALUE */
     ELSE DO;
       SELECT(RHN->RHNTYPE);
         WHEN(2,3);    /* ZONED OR CHAR */
         OTHERWISE STR=CONVERT_INTFC(STR,RHN);
         END; /* SELECT */
       RETURN(STR);
       END;
   END GETDATA_SUB;

 GETDATA_FIXED: PROC(KEY,TRH) RETURNS(FIXED BIN(31,0));
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A NUMERIC VALUE IN FIXED BINARY FROM THE CURRENT   *
 * RECORD.                                                             *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TRH PTR;
   DCL RET FIXED BIN(31,0);
   CALL FINDFLD(TRH,KEY);
   ON CONVERSION BEGIN;
     RET=MISSING_INTEGER; /* RETURN MISSING VALUE */
     GOTO END_GETDATA_FIXED;
     END;
   RET=GETDATA_SUB(TRH->RECPTR,RHNAMPTR);
  END_GETDATA_FIXED:
   RETURN(RET);
   END GETDATA_FIXED;

 GETDATA_FLOAT: PROC(KEY,TRH) RETURNS(DEC FLOAT(16));
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A NUMERIC VALUE IN DECIMAL FLOAT FROM THE CURRENT  *
 * RECORD.                                                             *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TRH PTR;
   CALL FINDFLD(TRH,KEY);
   RETURN(GETDATA_FLOAT_SUB(TRH->RECPTR,RHNAMPTR));
   END GETDATA_FLOAT;

 GETDATA_FLOAT_SUB: PROC(RPT,RHN) RETURNS(DEC FLOAT(16));
 /**********************************************************************
 *                                                                     *
 * CALLED TO OBTAIN A NUMERIC VALUE IN DECIMAL FLOAT FROM THE CURRENT  *
 * RECORD.                                                             *
 *                                                                     *
 **********************************************************************/
   DCL (RPT,RHN) PTR;
   DCL RET DEC FLOAT(16);
   ON CONVERSION BEGIN;
     UNSPEC(RET)=UNSPEC(MISSING);
     GOTO END_GETDATA_FLOAT_SUB;
     END;
   RET=GETDATA_SUB(RPT,RHN);
  END_GETDATA_FLOAT_SUB:
   RETURN(RET);
   END GETDATA_FLOAT_SUB;

 LOCDB: PROC(KEY) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO LOCATE A PARTICULAR DATABASE TABLE.                       *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TRH PTR;
   DO TRH=NXTRHEL REPEAT TRH->RHNXT WHILE(TRH^=NULL)
     UNTIL(TRH->RHNAME=KEY);
     END;
   RETURN(TRH);
   END LOCDB;

 PUTDATA: PROC(KEY,TRH,DATA);
 /**********************************************************************
 *                                                                     *
 * CALLED TO REPLACE A VALUE FROM THE CURRENT RECORD.                  *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TRH PTR;
   DCL DATA CHAR(*);
   DCL OUTSTR CHAR(256) VAR;

   CALL FINDFLD(TRH,KEY);
   IF RHNTYPE=2 × RHNTYPE=3
     THEN OUTSTR=DATA;
     ELSE IF DATA=''
       THEN OUTSTR=PUTDATA_MISSING; /* GO SET MISSING VALUE */
       ELSE DO;
         OUTSTR=SUBSTR(DATA,PREFIX(DATA,' ')); /* STRIP LEADING  */
         OUTSTR=SUBSTR(DATA,1,SUFFIX(DATA,' ')); /* STRING TRAILING  */
         IF OUTSTR='.'
           THEN OUTSTR=PUTDATA_MISSING; /* GO SET MISSING VALUE */
           ELSE OUTSTR=CONVERT(ADDR(DATA),'CHAR',LENGTH(DATA),0,
             TYPETAB(RHNTYPE),RHNLNG,0);
         END;
   CALL #ABEINS(OUTSTR,ADDR(TRH->RECPTR->RECLEN),RHNPOS,RHNLNG);
   END PUTDATA;

 PUTDATA_MISSING: PROC RETURNS(CHAR(256) VAR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO RETURN A MISSING VALUE FOR THE GIVEN DATA TYPE.           *
 *                                                                     *
 **********************************************************************/
   SELECT(RHNTYPE);
     WHEN(1) /* FLOAT */
       RETURN(MISSINGCH);
     WHEN(4) /* PACKED */
       RETURN(SUBSTR(MISSING_PACKED_CH,8-RHNLNG_1));
     WHEN(7) /* INTEGER */
       IF RHNLNG=4
         THEN RETURN(MISSING_INTEGER_CH);
         ELSE RETURN(MISSING_SMALLINT_CH);
     OTHERWISE RETURN('');
     END; /* SELECT */
   END PUTDATA_MISSING;

