   %INCLUDE ABESUB;
 ABESUB(ABEBDRS);
   %INCLUDE ABESUBA;
   DCL 1 DDI01 BASED(RHPLOT79),
   %INCLUDE ABE063I;
   DCL 1 PICSTR BASED(PICPTR),
     2 XCORNER DEC FLOAT(6), /* REAL PART OF THE IMAGINARY NUMBER IN
         THE LOWER LEFT CORNER OF THE AREA IN THE COMPLEX NUMBER PLANE
         TO BE EXAMINED. */
     2 YCORNER DEC FLOAT(6), /* IMAGINARY PART OF THE IMAGINARY NUMBER
         IN THE LOWER LEFT CORNER OF THE AREA IN THE COMPLEX NUMBER
         PLANE TO BE EXAMINED. */
     2 GAP DEC FLOAT(6), /* DISTANCE IN COMPLEX PLANE BETWEEN PIXELS */
     2 XDONE FIXED BIN(15,0), /* NUMBER OF ROWS PROCESSED */
     2 YDONE FIXED BIN(15,0), /* NUMBER OF COLUMNS PROCESSED */
     2 XDIM FIXED BIN(15,0),
     2 YDIM FIXED BIN(15,0),
     2 PIC(M REFER(XDIM),N REFER(YDIM)) FIXED BIN(15,0);
   DCL PEL CHAR(1) INIT('1');

   OPEN FILE(RH_SEQ_1) TITLE('BDRY') RECORD INPUT;
   PUT SKIP EDIT('CALLING READIT')(A);
   CALL READIT;
   CLOSE FILE(RH_SEQ_1);
   PUT SKIP EDIT('CALLING DO_BDRY')(A);
   CALL DO_BDRY;
   FREE PICSTR;

 DO_BDRY: PROC;
   DCL (I,J,K,X,Y) FIXED BIN;
   DCL COLOR FIXED BIN INIT(8);

   DO X=1 TO XDIM;
     PUT SKIP DATA(X);
     DO Y=1 TO YDIM;
       DO_BDRY_LUP: DO;
         DO I=MAX(1,X-1) TO MIN(XDIM,X+1);
           DO J=MAX(1,Y-1) TO MIN(YDIM,Y+1);
             IF PIC(I,J)^=PIC(X,Y)
               THEN DO;
                 IF X<=PLMAXX & Y<=PLMAXY
                   THEN CALL #PIXPUT(X,Y,PEL,RHPLOT79);
                 LEAVE DO_BDRY_LUP;
                 END;
             END;
           END;
         END DO_BDRY_LUP;
       END;
     END;
   END DO_BDRY;

 READIT: PROC;
 /**********************************************************************
 * READIT READS IN THE PIXEL STRUCTURE.                                *
 **********************************************************************/
   DCL INREC CHAR(1000) VAR;
   DCL 1 BUMPSTR BASED(PICPTR),
     2 PAD CHAR(2),
     2 BUMPSTART CHAR(1);
   DCL 1 INSTR BASED(INPTR),
     2 INCH CHAR(1),
     2 BUMP CHAR(1);
   DCL OUTPTR PTR;
   DCL EOFSW BIT(1) INIT('0'B);

   ON ENDFILE(RH_SEQ_1) EOFSW='1'B;
   READ FILE(RH_SEQ_1) INTO(INREC);
   PICPTR=ADDR(ADDR(INREC)->BUMPSTART);
   M=XDIM;
   N=YDIM;
   ALLOCATE PICSTR;
   OUTPTR=PICPTR;
   DO UNTIL(EOFSW);
     INPTR=ADDR(ADDR(INREC)->BUMPSTART);
     DO I=1 TO LENGTH(INREC);
       OUTPTR->INCH=INPTR->INCH;
       OUTPTR=ADDR(OUTPTR->BUMP);
       INPTR=ADDR(INPTR->BUMP);
       END;
     READ FILE(RH_SEQ_1) INTO(INREC);
     END;
   END READIT;

   END ABEBDRS;
