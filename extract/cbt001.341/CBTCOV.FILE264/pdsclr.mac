PDSCLR   CSECT
         PRINT NOGEN
         SAVES RETCTLA
         TGSREQU
         RDJFCB SYSPDS
         OI    JFCBMASK+4,X'80'
         OBTAIN DSCBCAM
         LTR   RF,RF
         BC    7,BADRET
         CLI   JFCRECFM,X'00'
         BC    7,GOTRFM
         MVC   JFCRECFM,DS1RECFM
         CLI   JFCRECFM,X'00'
         BC    7,GOTRFM
         MVI   JFCRECFM,X'C0'
GOTRFM   EQU   *
         SR    R0,R0
         CH    R0,JFCBLKSI
         BC    7,GOTBLKSI
         MVC   JFCBLKSI,DS1BLKL
         CH    R0,JFCBLKSI
         BC    7,GOTBLKSI
         MVC   JFCBLKSI,=H'6144'
GOTBLKSI EQU   *
         CH    R0,JFCLRECL
         BC    7,GOTLREC
         MVC   JFCLRECL,DS1LRECL
GOTLREC  EQU   *
         L     R1,4(RD)
         L     R1,24(R1)
         L     R1,0(R1)                R1 = A(HALFWORD PARM LEN)
         LH    R2,0(R1)                R2 = LEN(PARM)
         LTR   R2,R2                   WAS PARM SPECIFIED
         BC    7,DOPARM                BR IF YES
*    RTNE TO READ DIR TO CNT NO DIR BLOCKS
GETCNT   EQU   *
         OPEN  (SYSLIB,(INPUT))
         SR    RA,RA
READ     EQU   *
         READ  DECBR,SF,SYSLIB,BUFFER,'S'
         CHECK DECBR
         BCT   RA,READ
EOF      EQU   *
         LPR   RA,RA
         CLOSE SYSLIB
         B     SETDIR
PACK     PACK  DBL(8),2(0,R1)
DOPARM   EQU   *
         BCTR  R2,0
         EX    R2,PACK
         CVB   RA,DBL
SETDIR   EQU   *
         OPEN  (SYSLIBA,(OUTPUT))
         XC    BUFFER,BUFFER
SDLOOP   EQU   *
         WRITE DECBW,SF,SYSLIBA,DUMREC,256
         CHECK DECBW
         XC    CLR,CLR
         BCT   RA,SDLOOP
         CLOSE SYSLIBA
         OPEN  (SYSPDS,(OUTPUT))
         TM    SYSPDS+48,X'10'
         BC    8,BADRET
         CLOSE SYSPDS
         B     RETCTL
BADRET   EQU   *
         LA    RF,16
         B     RETCTLA
RETCTL   EQU   *
         SR    RF,RF
         B     RETCTLA
DBL      DS    0D
         DC    XL8'0C'
DUMREC   DC    8X'FF'
CLR      DS    0CL10
         DC    H'14'
         DC    8X'FF'
BUFFER   DS    CL256
SYSLIB   DCB   DDNAME=SYSLIB,MACRF=(R,WL),EODAD=EOF,DSORG=PS,          C
               RECFM=U,BLKSIZE=256,KEYLEN=8
SYSPDS   DCB   DDNAME=SYSLIB,MACRF=W,DSORG=PO,KEYLEN=0,EXLST=EXLST
SYSLIBA  DCB   DDNAME=SYSLIB,MACRF=WL,DSORG=PS,                        C
               RECFM=U,BLKSIZE=256,KEYLEN=8
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    0C
JFCBDSNM DS    CL44
         DS    CL28
JFCBMASK DS    CL28
JFCRECFM DS    CL1
         DS    CL1
JFCBLKSI DS    CL2
JFCLRECL DS    CL2
         DS    CL12
JFCBVOL  DS    CL6
         DS    CL60
         DS    0F
DSCBCAM  CAMLST SEARCH,JFCBDSNM,JFCBVOL,DSCB
DSCB     DS    0D
         DS    CL40
DS1RECFM DS    CL1
         DS    CL1
DS1BLKL  DS    CL2
DS1LRECL DS    CL2
         DS    CL110
         LTORG
         END
