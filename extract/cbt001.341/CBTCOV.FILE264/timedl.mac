 /* TIMEDL */
 /*
0*$.HE GENERALIZED SUBROUTINES - TIMEDL
0*$.HE CALCULATE DELAY TIME
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$05/23/79
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  TIMEDL
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THE SUBROUTINE WILL RETURN WILL RETURN THE TIME IN MILLISECONDS
0*$BETWEEN THE CURRENT TIME OF DAY AND A SPECIFIED TIME OF DAY.
0*$CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$DCL TIMEDL ENTRY(CHAR(*) VAR)
0*$  RETURNS(FIXED BIN(31,0));
0*$I = TIMEDL('12:31:55');
0*$IF I>0
0*$  THE DELAY(I);
0*$.FI L
0*$.IN 0
0*$.SP 1
0*$THE TIME PASSED TO TIMEDL MUST BE A CHARACTER STRING OF THE
0*$FORM HH:MM:SS, OR HH:MM, OR HH.  IF THE TIME IS OF AN INCORRECT
0*$FORM, OR IF THE DELAY WILL EXCEED 12 HOURS, OR IF THE TIME OF
0*$DAY PASSED IS LESS THAN THE CURRENT TIME OF DAY,
0*$TIMEDL WILL RETURN -1;
 */
 TIMEDL:  PROC(PARM) RETURNS(FIXED BIN(31,0));
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         DCL PARM CHAR(*) VAR;
         DCL TIME BUILTIN;
         DCL NOWCH CHAR(9) VAR;
         NOWCH=TIME;
         NOWCH=SUBSTR(NOWCH,1,2)××':'××SUBSTR(NOWCH,3,2)××':'××
           SUBSTR(NOWCH,5,2);
         NOW = CONVTME(NOWCH);
         THEN = CONVTME(PARM);
         DEL = THEN-NOW;
         IF NOW<0 × THEN<0 × DEL<0 × DEL>43200
           THEN RETURN(-1);
           ELSE RETURN(DEL*1000);
 CONVTME:  PROC(PARM) RETURNS(FIXED BIN(31,0));
 /* CONVTME RETURNS TIME IN SECONDS */
         DCL PARM CHAR(*) VAR;
         DCL (I,J,K,R) FIXED BIN(31,0);
         DCL FACTAB(3) STATIC INIT(3600,60,1);
         I = VERIFY(PARM,' :0123456789');
         IF I^=0
           THEN RETURN(-1);
         J,R = 0;
         PARM=PARM××':';
         DO I=INDEX(PARM,':') REPEAT INDEX(PARM,':') WHILE(I^=0);
           J=J+1;
           IF J > 3
             THEN RETURN(-1);
           K=SUBSTR(PARM,1,I-1);
           R=R+K*FACTAB(J);
           PARM=SUBSTR(PARM,I+1);
           END;
         RETURN(R);
         END CONVTME;
         END;
