*$.HE ISD GENERALIZED SUBROUTINES - SYSDCBP
*$.HE SET UP A BPAM DCB - PLI
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/1/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSDCBP
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE IS CALLED FROM PLI TO CREATE A BPAM DCB WITH WHICH
*$TO PROCESS A PARTITIONED DATA SET.
*$THIS DCB IS USED WHEN SUBSEQUENTLY CALLING SUBROUTINES SYSBLDL,
*$SYSFIND, SYSREAD, SYSWRITE, SYSSTOW, SYSCLOS, AND SYSOPEN.
*$.SP 1
*$.IN 5
*$CALL SYSDCBP(DCBPTR,DDNAME);
*$.IN 0
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$DCBPTR
*$.IN 5
*$POINTER VARIABLE WHICH WILL BE SET TO THE ADDRESS IN SUBPOOL 0
*$OF THE BPAM DCB.
*$.IN 0
*$DDNAME CHAR(8)
*$.IN 5
*$NAME OF THE DD STATEMENT THAT THE NEW DCB IS TO BE ASSOCIATED WITH.
SYSDCBP  PLIENTRY ,DSA=104
         LM    R2,R3,0(R1)
         LA    R0,DCBLEN
         GETMAIN R,LV=(0)
         LA    R1,0(R1)
         ST    R1,0(R2)                SET DCB PTR
         MVC   0(DCBLEN,R1),DUMDCB
         MVC   40(8,R1),=CL8' '
         L     R4,0(R3)                R4 = A(DDNAME)
         AIF   ('&SYSPARM' EQ 'F').FOPT1
         TM    6(R3),X'80'             IS IT VARYING?
         BC    8,NOTV1                 BR IF NOT
         LH    R5,0(R4)                R5=LEN(DDNAME)
         LA    R4,2(R4)                R4=A(DDNAME)
         B     NXT1
NOTV1    DS    0H
         LH    R5,4(R3)                R5=LEN(DDNAME)
NXT1     DS    0H
         AGO   .BYFOPT1
.FOPT1   ANOP
         LH    R5,6(R3)                R5 = LEN(DDNAME)
.BYFOPT1 ANOP
         BCTR  R5,0                    LESS 1 FOR EX
         EX    R5,MVC                  MOVE DDNAME TO DCB
         PLIEXIT
MVC      MVC   40(0,R1),0(R4)
DUMDCB   DCB   DSORG=PO,MACRF=(R,W),DDNAME=DUM
         DC    4F'0'
         DC    CL8' '
DECBLOC  EQU   *-DUMDCB
         READ  DECB,SF,,,,,,,MF=L
         DS    0D
DCBLEN   EQU   *-DUMDCB
         LTORG
SYSFILE  CSECT
SYSFILEA DXD   F
         L     R1,0(R1)
         L     R1,0(R1)
         AIF   ('&SYSPARM' EQ 'F').FOPT2
         L     RF,4(RC)                RF=A(PRV) FROM TCA
         L     RF,0(RF)
         AGO   .BYFOPT2
.FOPT2   ANOP
         L     RF,0(RC)
.BYFOPT2 ANOP
         ORG   *-2
         DC    QL2(SYSFILEA)
         MVC   0(8,R1),0(RF)
         BR    RE
         END
