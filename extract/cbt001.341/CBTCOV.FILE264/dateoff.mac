 /* DATEOFF */
 /*
0*$.HE GENERALIZED SUBROUTINES - DATEOFF
0*$.HE FIND DATE + OR - DAYS FROM DATE
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/28/77
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  DATEOFF
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE INVOKED TO FIND THE DATE OF A DAY PLUS OR
0*$MINUS A GIVEN NUMBER OF DAYS FROM A GIVEN DATE.  CALL FORMAT IS AS
0*$FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$DCL DATEOFF ENTRY(FIXED DEC(5,0),
0*$  FIXED BIN(15,0))
0*$  RETURNS(FIXED DEC(5,0));
0*$NEWDTE = DATEOFF(JULIAN,-23);
0*$.FI L
0*$.IN 0
0*$.SP 1
0*$WHERE JULIAN IS THE DATE IN JULIAN FORM. THE RETURNED VALUE IS THE
0*$NEW JULIAN DATE.
 */
 DATEOFF:  PROC(JULIAN,FACTOR) RETURNS(FIXED DEC(5,0));
         DCL JULIAN FIXED DEC(5,0);
         DCL FACTOR FIXED BIN(15,0);
         DCL (Z,YY,DDD,DAYCK) FIXED BIN(31,0);
         YY = JULIAN/1000;
         DDD = JULIAN-(YY*1000);
         Z = FACTOR;
         DO WHILE(Z ^= 0);
              DAYCK = SETDDD(YY);
              DDD = DDD + Z;
              IF DDD > DAYCK
                THEN DO;
                   YY = YY + 1;
                   Z = DDD-DAYCK-1;
                   DDD = 1;
                   END;
                ELSE IF DDD < 1
                   THEN DO;
                        YY = YY - 1;
                        DAYCK = SETDDD(YY);
                        Z = DDD;
                        DDD = DAYCK;
                        END;
                  ELSE Z = 0;
             END;
         RETURN(YY*1000 + DDD);
 SETDDD:  PROC(YY) RETURNS(FIXED BIN(31,0));
         DCL YY FIXED BIN(31,0);
         IF MOD(YY,4) ^= 0
                   THEN RETURN(365);
                   ELSE RETURN(366);
         END SETDDD;
         END;
