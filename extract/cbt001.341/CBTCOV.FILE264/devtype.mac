**********************************************************************$
*$.HE GENERALIZED SUBROUTINES - DEVTYPE
*$.HE PLI INTERFACE TO DEVTYPE MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/17/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  DEVTYPE
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE DEVTYPE MACRO.  THIS MACRO IS USED TO REQUEST INFORMATION RELATIN
*$TO THE CHARACTERISTICS OF AN I/O DEVICE, AND TO CAUSE THIS
*$INFORMATION TO BE PLACED INTO A SPECIFIED AREA.
*$THE DECLARE STATEMENT DESCRIBING THE DATA RETURNED FROM THIS
*$ROUTINE IS LOCATED IN SYS2.MACLIB, MEMBER DEVDATA, AND CAN
*$BE INVOKED VIA PREPROCESSOR INCLUDE STATEMENT.
*$.SP 1
*
*$.CE 1
*$CALL FORMAT TO THE DEVTYPE ROUTINE
*$.SP 1
*
**********************************************************************$
*
*$CALL DEVTYPE(RET,DDNAME,AREA,'DEVTAB','RPS');
*$.SP 1
*
**********************************************************************$
*
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*
**********************************************************************$
*
*$.IN 0
*$AREA CHAR(24)
*$.IN 5
*$SPECIFIES THE ADDR OF AN AREA INTO WHICH THE DEVICE
*$INFORMATION IS TO BE PLACED.  THE AREA CAN BE TWO,
*$FIVE, OR SIX FULL WORDS, DEPENDING ON WHETHER OR NOT
*$THE DEVTAB AND RPS OPERANDS ARE SPECIFIED.  TO
*$ELIMINATE CONCERN OVER BOUNDARY ALIGNMENT, THIS ROUTINE*$
*$WILL PASS ITS OWN AREA TO SVC 24, AND SUBSEQUENTLY
*$COPIES ITS CONTENTS TO YOUR AREA.  YOU MUST ALWAYS
*$SUPPLY SIX FULLWORDS EVEN IF THEY WONT BE ALL USED.
*$.IN 0
*$DDNAME CHAR(8)
*$.IN 5
*$CHARACTER STRING CONTAINING THE SYMBOLIC NAME OF THE
*$DD STATEMENT TO WHICH THE DEVICE IS ASSIGNED.
*$THE STRING MUST BE 8 BYTES LONG.
*
*$.IN 0
*$'DEVTAB'
*$.IN 5
*$IF DEVTAB IS SPECIFIED, AND THE DEVICE IS DASD, FIVE
*$WORDS OF DATA ARE PLACED IN YOUR AREA.  IF DEVTAB IS
*$SPECIFIED, AND THE DEVICE IS NOT DASD, TWO WORDS ARE
*$PLACED IN YOUR AREA.  IF DEVTAB IS NOT SPECIFIED, TWO
*$WORDS ARE PLACED IN YOUR AREA.
*
*$.IN 0
*$RET
*$.IN 5
*$FIXED BINARY(31,0) - SET TO 0 IF REQUEST COMPLETED,
*$4 IF DDNAME NOT FOUND, 8 IF ERROR IN CALLING FORMAT.
*
*$.IN 0
*$'RPS'
*$.IN 5
*$IF RPS IS SPECIFIED, DEVTAB MUST ALSO BE SPECIFIED.
*$THE RPS PARAMETER CAUSES ONE ADDITIONAL FULL WORD OF
*$RPS DATA TO BE INCLUDED WITH THE DEVTAB INFORMATION.
*$.SP 1
*
**********************************************************************$
*$ANY REFERENCE TO A DUMMY DD STMT WILL CAUSE ZEROS TO BE PLACED IN
*$AREA.  A LAYOUT OF AREA MAY BE ACQUIRED IN YOUR PL1 PROGRAM BY
*$INCLUDING MEMBER DEVDATA
**********************************************************************$
DEVTYPE  PLIENTRY ,DSA=104,COMPILE=&SYSPARM
         LM    2,6,0(1)
         SR    7,7
         ST    7,0(2)                  SET RET TO 0
         SLDL  8,63
LOOP     EQU   *
         LA    15,LTRTAB(9)
         CLI   0(15),X'FF'             ARE THERE TOO MANY PARMS
         BC    8,BADFORM               BR IF YES
         IC    8,LTRTAB(9)
         EX    8,LTR                   TEST FOR LAST PARM
         EX    8,LOAD                  LOAD ACTUAL PARM ADDR
         LA    9,1(9)                  BUMP INDEX
         BC    2,LOOP
*                                      NOTE THAT AT THIS POINT,
*                                      R9 = # OF PARMS.
         LR    1,3                     R1 = A(DDNAME)
         LA    0,MYAREA
         LA    8,3                     R8 = 3
         CR    9,8
         BC    4,SVC                   BR IF DEVTAB NOT SPECIFIED
         BC    8,NORPS                 BR IF RPS NOT SPECIFIED
         LCR   1,1
NORPS    EQU   *
         LCR   0,0
SVC      EQU   *
         SVC   24
         MVC   0(24,4),MYAREA          COPY MYAREA
EXITA    ST    15,0(2)                 SET RET
EXIT     PLIEXIT
BADFORM  EQU   *
         LA    15,8
         B     EXITA
LTR      LTR   0,0
LOAD     L     0,0(0)
LTRTAB   DC    X'33445566FF'
         LTORG
DSA      DSECT
MYAREA   DS    6F
DSALEN   EQU   *-DSA
         END
