 ABEDIR:  PROC(RH_PDSDIR,DDNAME,MEMPTR) RETURNS(CHAR(8));
   DCL RH_PDSDIR FILE;
   DCL DDNAME CHAR(8);
   DCL MEMPTR PTR;

   DCL NULL BUILTIN;
   DCL BUFPTR PTR STATIC,
       BUFPADJ FIXED BIN(31,0) DEF BUFPTR;
   DCL BYTCNT BIT(16) BASED(BUFPTR);
   DCL BCNTSAV FIXED BIN(15,0) STATIC INIT(0);
   DCL 1 ENTRY BASED(BUFPTR),
    2 MEMNAM CHAR(8),
    2 TTR CHAR(3),
    2 CFIELD,
     3 CF1 BIT(3),
     3 CF2 BIT(5);  /* USER DATA HALFWORDS */
   DCL INITSW BIT(1) STATIC INIT('1'B);

   ON ENDFILE(RH_PDSDIR) GO TO DIREOF;
   IF INITSW
     THEN DO;
       INITSW = '0'B;
       OPEN FILE(RH_PDSDIR) TITLE(DDNAME) RECORD INPUT;
       END;
   IF BCNTSAV > 2
     THEN DO;
       J = 12 + 2 * CF2;
       BUFPADJ = BUFPADJ + J;
       BCNTSAV = BCNTSAV - J;
       END;
   IF ^(BCNTSAV > 2)
     THEN DO;
       READ FILE(RH_PDSDIR) SET(BUFPTR);
       BCNTSAV = SUBSTR(BYTCNT,8);
       BUFPADJ = BUFPADJ + 2;
       END;
   MEMPTR=BUFPTR;
   IF MEMNAM^=HIGH(8)
     THEN RETURN(MEMNAM);
 DIREOF:
   CLOSE FILE(RH_PDSDIR);
   INITSW='1'B;
   MEMPTR=NULL;
   RETURN(HIGH(8));
   END ABEDIR;
