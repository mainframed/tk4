*$.HE GENERALIZED SUBROUTINES - XLATET
*$.HE PLI INTERFACE TO TRT INSTRUCTION
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  XLATETS
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE IS DESIGNED TO ALLOW THE PROGRAMMER
*$USING THE PLI OPTINIZER TO USE THE TRANSLATE AND
*$TEST INSTRUCTION.  LINKAGE IS AS FOLLOWS:
*$.SP 1
*$.IN 5
*$CALL XLATET(STRING,TABLE,POS,CHAR);
*$.IN 0
*$STRING
*$.IN 5
*$NAME OF CHARACTER STRING TO BE TESTED.
*$CAN BE FIXED OR VARYING UP TO 32767 BYTES.
*$.IN 0
*$TABLE
*$.IN 5
*$NAME OF 256 BYTE CHARACTER STRING
*$REPRESENTING TRT TABLE.
*$.IN 0
*$POS
*$.IN 5
*$FIXED BINARY VARIABLE TO BE SET TO
*$LOCATION WITHIN THE STRING OF THE FIRST
*$OCCURRENCE OF A CHARACTER THAT TRANSLATES
*$TO A NON-ZERO BYTE IN THE TABLE.  (THE
*$VALUE IS RELATIVE TO ONE AND ANALOGOUS TO
*$THE VALUE RETURNED BY THE INDEX BUILT-IN
*$FUNCTION) IF NO CHARACTERS ARE FOUND - A
*$ZERO VALUE IS RETURNED.
*
********************************************************************
XLATET   CSECT
         PLIREQU
         USING *,RF                    SET BASE
         STM   RE,RC,12(RD)            SAVE CALLERS REGS
         LM    R2,R3,0(R1)             R2 = A(SDV) FOR STR TO TRANSL
         L     R3,0(R3)                R3 = A(TRT TABLE)
         L     R4,0(R2)                R4 = A(STR TO TRT)
         TM    6(R2),X'80'             IS IT VARYING STR
         BC    1,GOTVAR                BR IF YES
         LH    R5,4(R2)                R5 = LEN STR TO TRT
         B     LOOPI
GOTVAR   EQU   *
         LH    R5,0(R4)                R5 = LEN STR TO TRT
         LA    R4,2(R4)                R4 = A(STR TO TRT)
LOOPI    EQU   *
         LA    R9,0(R4)
         LA    R6,256
         LNR   R6,R6                   R6 = INDEX FOR BXLE
         SR    R7,R7                   R7 = COMPARAND FOR BXLE
         LA    R8,255                  R8 = LEN FOR EX
         B     LOOPTST
LOOP     EQU   *
         EX    R8,TRT
         BC    7,STRIKE
         LA    R4,256(R4)
LOOPTST  EQU   *
         BXH   R5,R6,LOOP              DECREMENT LEN LEFT TO TRT
*                                      AND BR BACK IF LEN LEFT GT 256
         AR    R5,R8                   R5 = LEN LEFT TO TRT -1 FOR EX
         EX    R5,TRT
         BC    7,STRIKE
         SR    R1,R1
STRIKE   EQU   *
         L     R3,24(RD)
         LM    R4,R5,8(R3)             R4 = A(POS), R5 =A(SDV FOR CHAR)
         LA    R1,0(R1)                CLEAR HI-BYTE
         ST    R1,0(R4)                SET POS
         LTR   R1,R1                   WAS THERE A HIT
         BC    8,ENDUP                 BR IF NOT
         SR    R1,R9
         LA    R1,1(R1)
         ST    R1,0(R4)                SET POS
         L     R6,0(R5)                R6 = A(CHAR)
         STC   R2,0(R6)
ENDUP    EQU   *
         LM    RE,RC,12(RD)            RESTORE CALLERS REGS
         BR    RE                      RETURN
TRT      TRT   0(0,R4),0(R3)
         LTORG
         END
