/*IWT 5
//X75826AB JOB (10-337-MISC-027-B4,1),
// 'SMITH-TCC',CLASS=E,MSGCLASS=1,NOTIFY=X75826
// EXEC PLIXC,PARM.PLI=M
//PLI.SYSLIB DD DSN=SYS2.SAS516.MACLIB,DISP=SHR
 /*-----------ABES-------------*/
 %INCLUDE MACUSER;
 ABES: PROC PORTMAIN;
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   DCL ABESG ENTRY RETURNS(PTR); /* PARSING INFORMATION */
   DCL PLILOAD ENTRY(CHAR(8),ENTRY,FIXED BIN(15,0));
   DCL TSOMOCK ENTRY;
   DCL STACK ENTRY;
   DCL (RETCODE,IOECB) FIXED BIN(31,0) INIT(0);
   DCL 1 BUF,
     2 BUFLEN FIXED BIN(15,0) INIT(12), /* LENGTH(DATA IN BF)+4 */
     2 OFFSET FIXED BIN(15,0) INIT(0), /* S/B 0 */
     2 BUFFER CHAR(4) INIT('ABS '),
     2 BUFXCOM PTR,
     2 BUFPAD CHAR(72) INIT('');
   DCL ENTRYPT ENTRY VARIABLE OPTIONS(ASSEMBLER);
   DCL (MOCKUPT,MOCKPSCB,ECTADR) PTR;
   DCL DUM CHAR(1) BASED(DUMPTR);
   DCL I FIXED BIN(15,0);

   %INCLUDE XCOM; /* COMMUNICATIONS AREA */
   %INCLUDE XLINK; /* ALL X-ROUTINES EXCEPT XINIT,XCOMGET */
   %INCLUDE STMTSTR; /* STORAGE FOR PARSED INFORMATION */
   %INCLUDE PROCPRS; /* GENERAL PARSING INFORMATION */
   %INCLUDE XINIT; /* INITIALIZES XCOM, XLINK, ETC. */

   CALL XINIT; /* FIRST TWO STATEMENTS */
   BUFXCOM,XCOMPTR=XCOMGET(); /* IN ALMOST EVERY PROC */
   CALL XSPARSE(ABESG()); /* PARSING */
   CALL XTOKE; /* PARSING */
   IF PROCERROR^=0
     THEN CALL XEXIT(PROCERROR,0);

   CALL TSOMOCK(MOCKUPT,MOCKPSCB,ECTADR); /* GET PARM VALUES */
   CALL PLILOAD('ABE     ',ENTRYPT,I);
   IF I^=0
     THEN SIGNAL ERROR;
   CALL STACK(MOCKUPT,ECTADR,IOECB,RETCODE,'TERM');
   CALL ENTRYPT(BUFLEN,MOCKUPT->DUM,MOCKPSCB->DUM,ECTADR->DUM,I);
   CALL STACK(MOCKUPT,ECTADR,IOECB,RETCODE,'TOP');

   CALL XEXIT(XEXITNORMAL,0);
   END ABES;
// EXEC GRAMMAR,FUNCNAM=ABESG,OUTSRC=PDS
//GRAMMAR.SYSUT1 DD *
#--------------GRAMMER FOR PROC ABES--------------------;
%INCLUDE STUBGRM.
PROGRAM = ABESTMT ENDJB,
#--------------STATEMENT DEFINITION---------------------;
ABESTMT=                            @PROCINIT
                                    @STMTINIT(11)
             "ABES" ABEOPT*         @DSDFLT(11,5)
                                    @STMTEND ,
#--------------PROC OPTIONS-----------------------------;
ABEOPT =    "DATA" "=" DSFIELD      @DS(11,3,1).
//DOIT.OBJSET DD DSN=&&SORC,DISP=(NEW,PASS),
// UNIT=DISK,SPACE=(TRK,(10,10)),
// DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)
// EXEC PLIXC,PARM.PLI=M
//PLI.SYSLIB DD DSN=SYS2.SAS516.MACLIB,DISP=SHR
//PLI.SYSIN DD DSN=&&SORC,DISP=(OLD,DELETE)
// EXEC LKED,PARM.LKED='LIST,LET'
//SYSLIN DD DSN=&&LOADSET,DISP=(OLD,DELETE)
// DD DDNAME=SYSIN
//SYSLMOD DD DSN=SYS2.SAS516.LIBRARY,DISP=SHR
//SYSLIB DD DSN=SYS1.PLIBASE,DISP=SHR
// DD DSN=SYS2.PLIBASE,DISP=SHR
// DD DSN=SYS2.SAS516.SUBLIB,DISP=SHR
 ENTRY ENTRY
 SETSSI AA000000
 NAME ABES(R)
