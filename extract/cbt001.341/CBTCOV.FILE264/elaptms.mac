 /* ELAPTMS */
 /*
0*$.HE GENERALIZED SUBROUTINES - ELAPTMS
0*$.HE FIND ELAPSED TIME OUTSIDE A RANGE
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$08/15/78
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  ELAPTMS
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE MAY BE INVOKED TO CALCULATE THE ELAPSED TIME
0*$BETWEEN TWO POINTS IN TIME, INCLUDING ONLY TIME IN A GIVEN RANGE.
0*$CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.NF
0*$DCL ELAPTMS ENTRY(FIXED DEC(5,0), FIXED BIN(31,0),
0*$  FIXED DEC(5,0), FIXED BIN(31,0),
0*$  FIXED BIN(31,0), FIXED BIN(31,0))
0*$  RETURNS(FIXED BIN(31,0));
0*$I = ELAPTMS(SDTE,STME,EDTE,ETME,LORANGE,HIRANGE);
0*$.IN 0
0*$.FI L
0*$.SP 1
0*$SDTE, STME, EDTE, ETME, RESPECTIVELY, ARE THE STARTING DATE
0*$AND TIME, AND ENDING DATE AND TIME OF THE RANGE FOR WHICH
0*$ELAPSED TIME IS TO BE CALCULATED.  LORANGE AND HIRANGE SPECIFY
0*$THE STARTING AND ENDING TIMES OF THE RANGE WHICH IS TO BE
0*$INCLUDED FROM THE CALCULATED ELAPSED TIME.  NOTE THAT LORANGE
0*$HIRANGE MAY SPAN MIDNIGHT.  ALL TIMES ARE IN UNITS OF .01
0*$SECONDS, AND DATES ARE IN JULIAN FORM (YYDDD).
 */
 ELAPTMS:  PROC(SDTE,STME,EDTE,ETME,LOTMEK,HITMEK)
           RETURNS(FIXED BIN(31,0));
         DCL (SDTE,EDTE) FIXED DEC(5,0);
         DCL (STME,ETME,LOTMEK,HITMEK) FIXED BIN(31,0);
         DCL TDTE FIXED DEC(5,0);
         DCL RETELAP FIXED BIN(31,0);
         DCL DATEOFF ENTRY(FIXED DEC(5,0),
           FIXED BIN(15,0))
           RETURNS(FIXED DEC(5,0));
         IF SDTE = EDTE
           THEN RETELAP = MYELA(STME,ETME,LOTMEK,HITMEK);
           ELSE DO;
             RETELAP = MYELA(STME,8640000,LOTMEK,HITMEK);
             DO TDTE = DATEOFF(SDTE,1) REPEAT DATEOFF(TDTE,1)
               WHILE(TDTE ^= EDTE);
               RETELAP = RETELAP + MYELA(0,8640000,LOTMEK,HITMEK);
               END;
             RETELAP = RETELAP + MYELA(0,ETME,LOTMEK,HITMEK);
             END;
         RETURN(RETELAP);
 MYELA:  PROC(STME,ETME,LO,HI) RETURNS(FIXED BIN(31,0)) RECURSIVE;
         DCL (STME,ETME,LO,HI) FIXED BIN(31,0);
         DCL RTME FIXED BIN(31,0) INIT(0);
         IF LO > HI /* DOES RANGE SPAN MIDNIGHT? */
           THEN RTME=MYELA(STME,ETME,LO,8640000)+MYELA(STME,ETME,0,HI);
           ELSE DO;
             IF ^(LO >= ETME × HI <= STME)
               THEN DO;
                 RTME = ETME - STME;
                 IF LO > STME
                   THEN RTME = RTME - (LO - STME);
                 IF HI < ETME
                   THEN RTME = RTME - (ETME - HI);
                 END;
             END;
         RETURN(RTME);
         END MYELA;
         END ELAPTMS;
