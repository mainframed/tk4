 /* COPYRIGHT 1988 BY THOMAS GLEN SMITH */
 /**********************************************************************
 * ABESETC - ALIAS #SETC.                                              *
 * CALLED TO SET THE CHARACTER VALUE OF A SYMBOLIC VARIABLE.           *
 * PARAMETERS ARE -                                                    *
 *   RHHDRPT - ABE CVT.                                                *
 *   TREEHDR - HEAD OF TREE.  NORMALLY POINTS TO RH_SYMTREE.           *
 *   SYMBOL CHAR(8) CONTAING SYMBOL NAME.                              *
 *   X - CHAR(*) VAR.                                                  *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABESETC)  PARMS(RHHDRPT,TREEHDR,SYMBOL,X) CHECK(YES);
   %INCLUDE ABESUBA;
   %INCLUDE ABESREC;
   %INCLUDE ABEMISS;

   DCL SYMBOL CHAR(8);
   DCL X CHAR(*) VAR;
   DCL TREEHDR PTR;

   DCL ABESET ENTRY(PTR,PTR);
   DCL ABESETN ENTRY(PTR,PTR,PTR,CHAR(*) VAR) RETURNS(PTR);
   DCL (P,Q,R) PTR;

   P=#SRCH(TREEHDR,SYMBOL,Q);
   IF P=NULL
     THEN DO; /* NOT FOUND */
       I=MAX(80,LENGTH(X)); /* LENGTH FOR REFER */
       ALLOCATE SYMREC SET(P);
       P->SRLCHILD,P->SRRCHILD=NULL;
       P->SRNAME=SYMBOL;
       P->SRBF=0;
       P->SRTYPE=2;
       P->SRCURL=LENGTH(X);
       P->SRCH=X;
       CALL ABESET(TREEHDR,P);
       END;
     ELSE DO;
       P->SRTYPE=2;
       IF LENGTH(X)>P->SRMAXL
         THEN P=ABESETN(RHHDRPT,P,Q,X); /* BIGGER ELEMENT */
         ELSE DO;
           P->SRCURL=LENGTH(X);
           P->SRCH=X;
           END;
       END;
   RETURN;

 ABESETZ: ENTRY (RHHDRPT,TREEHDR,SYMBOL,X);
   /* ENTRY FOR CLIST PROCESSING */
   DCL GBLSW BIT(1) STATIC INIT('0'B);

   CALL #ABESTP(SYMBOL,X);
   RETURN;
   END ABESETC;
