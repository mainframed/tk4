TGSENDAT START 0
         SAVES RETCTL
         TGSREQU
         USING ENTRY,R2
         L     R5,0(R1)                R5 = A(LEN)
         LA    R5,0(R5)
         LR    R1,R5                   SO DOES R1
         LR    R4,R1                   SO DOES R4
         LH    R2,0(R1)                R2 = LEN(PARM)
LOOPA    EQU   *
         LA    R1,1(R1)
         CLI   2(R1),C'/'              IS IT END OF NAME
         BC    8,GOTPARM               BR IF YES
         CLI   2(R1),C'.'              IS IT END OF INDEX QUALIFIER
         BC    7,NOQ                   BR IF NOT
         LA    R4,1(R1)                RESET R4
NOQ      EQU   *
         BCT   R2,LOOPA
         B     NOPARM
GOTPARM  EQU   *
         SH    R2,=H'2'
         CH    R2,=H'9'                IS PARM TOO LONG
         BC    4,NOTRUNC               BR IF NOT
         LA    R2,8
NOTRUNC  EQU   *
         STH   R2,PARM+4               SET LEN(PARM)
         BCTR  R2,0
         EX    R2,MVCPARM
NOPARM   EQU   *
         LR    R6,R1
         SR    R6,R5                   R6 = LEN ENTIRE NAME
         STH   R6,RNLENQ
         BCTR  R6,0                    LESS 1 FOR EX
         LR    R7,R6
         EX    R6,MVCNAME
         SR    R1,R4                   R1 = LEN LOWEST QUALIFIER
         SR    R7,R1                   R7 = LEN INDICES LESS 1 FOR EX
         BCTR  R1,0                    LESS 1 FOR EX
         EX    R1,MVCLOWQ
         ENQ   (QNAME,RNLEN,E,0,SYSTEM)
         LTR   R7,R7                   IS THE NAME QUALIFIED
         BC    4,NOMOV                 BR IF NOT
         BCTR  R7,0                    DONT MOVE LAST PERIOD
         EX    R7,MVCIND
         LOCATE INDCAM
         CH    RF,=H'12'
         BC    8,OK
         BAL   RE,BOMB
NOMOV    EQU   *
         LOCATE BLKCAMN
         B     NEWBLKA
NEWBLK   EQU   *
         MVC   TTR,ETTR
         LOCATE BLKCAM
NEWBLKA  EQU   *
         LTR   RF,RF
         BC    8,OK
BOMB     EQU   *
         ABEND 321,DUMP
OK       EQU   *
         LA    R2,AREA+2
NXT      EQU   *
         CLC   ENAME,=8X'FF'           IS IT TIME FOR NEW BLOCK
         BC    8,NEWBLK                BR IF YES
         CLC   ENAME,LOWQ              IS IT PROPER ENTRY
         BC    8,GOTENT                BR IF YES
         BC    2,BOMB
         SR    R3,R3
         IC    R3,ELEN
         SLL   R3,1
         LA    R2,12(R2,R3)
         B     NXT
GOTENT   EQU   *
         MVC   NAME1,RNAME
         MVC   NAME2,RNAME
         MVC   NAME3,RNAME
         LH    R3,RNLENQ
         LA    R4,NAME1(R3)
         MVC   0(3,R4),=C'(0)'
         LA    R4,NAME3(R3)
         MVC   0(4,R4),=C'(+1)'
         LA    R4,NAME2(R3)
         MVC   0(2,R4),=C'(-'
         SR    R5,R5
         IC    R5,CURCNT
         SLL   R5,8
         IC    R5,CURCNT+1
         BCTR  R5,0
         CVD   R5,DBL
         ED    EDFLD,DBL+6
         OI    EDFLD+3,X'F0'
         LA    R6,EDFLD+1
         LA    R7,3
LOOPB    EQU   *
         CLI   0(R6),C' '
         BC    7,GOTLEN
         LA    R6,1(R6)
         BCT   R7,LOOPB
GOTLEN   EQU   *
         EX    R7,MVCGDG
         LOCATE CAM1
         LTR   15,15
         BC    8,GOKA
         BAL   RE,BOMB
GOKA     EQU   *
         MVC   SER1,LOCSER
         LOCATE CAM2
         LTR   15,15
         BC    8,GOKB
         BAL   RE,BOMB
GOKB     EQU   *
         MVC   SER2,LOCSER
         LOCATE CAM3
         LTR   15,15
         BC    8,GOKC
         BAL   RE,BOMB
GOKC     EQU   *
         L     2,16
         L     2,0(2)
         L     2,0(2)
         L     2,12(2)
         LA    3,24
TIOTLOOP EQU   *
         LA    2,0(2,3)
         CLC   4(8,2),=CL8'SYSUT1'
         BC    8,GOTUT1
         CLC   4(8,2),=CL8'SYSUT2'
         BC    8,GOTUT2
NXTENTRY EQU   *
         IC    3,0(2)
         LTR   3,3
         BC    7,TIOTLOOP
         B     PROCFLE
GOTUT1   EQU   *
         OI    FILESW,X'80'
         B     NXTENTRY
GOTUT2   EQU   *
         OI    FILESW,X'40'
         B     NXTENTRY
PROCFLE  EQU   *
         TM    FILESW,X'80'
         BC    8,ATTACH
         RDJFCB UT1DCB
         MVC   JFCB(44),NAME1
         MVC   JFCB+118(6),SER1
         OI    JFCB+76,X'80'
         OPEN  (UT1DCB,(INPUT)),TYPE=J
         CLOSE (UT1DCB)
         TM    FILESW,X'40'
         BC    8,ATTACH
         RDJFCB UT2DCB
         MVC   JFCB(44),NAME3
         MVC   JFCB+118(6),SER2
         OI    JFCB+76,X'80'
         OPEN  (UT2DCB,(OUTPUT)),TYPE=J
         CLOSE (UT2DCB)
ATTACH   EQU   *
         LA    R1,PARM
         LINK  EP=TGSUPDAT
         TM    FILESW,X'40'
         BC    8,DEQ
         MVC   VOLSER,SER2
         CATALOG CATLG
         LTR   15,15
         BC    8,GOKD
         BAL   RE,BOMB
GOKD     EQU   *
DEQ      EQU   *
         DEQ   (QNAME,RNLEN,0,SYSTEM)
         B     RETCTL
RNLENQ   DS    0H
         DS    CL1
RNLEN    DS    CL1
RNAME    DC    CL44' '
NAME1    DS    CL44
NAME2    DS    CL44
NAME3    DS    CL44
QNAME    DC    CL8'SYSDSN'
CAM1     CAMLST NAME,NAME1,,LOCAREA
CAM2     CAMLST NAME,NAME2,,LOCAREA
CAM3     CAMLST NAME,NAME3,,LOCAREA
LOCAREA  DS    0D
         DS    CL6
LOCSER   DS    CL6
         DS    CL253
SER1     DS    CL6
SER2     DS    CL6
FILESW   DC    X'00'
R15      DC    F'0'
UT1DCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=R,EODAD=PROCFLE,EXLST=EXLST
UT2DCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=W,RECFM=U,BLKSIZE=32000,   C
               EXLST=EXLST
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL200
CATLG    CAMLST CAT,NAME3,,VOL
VOL      DC    H'1'
         DC    X'34008001'
VOLSER   DS    CL6
         DC    H'1'
PARM     DC    A(*+4)
         DC    H'0'
         DS    CL8
MVCGDG   MVC   2(0,R4),0(R6)
MVCPARM  MVC   PARM+6(0),3(R1)
MVCNAME  MVC   RNAME(0),2(R5)
MVCIND   MVC   INDICES(0),2(R5)
MVCLOWQ  MVC   LOWQ(0),2(R4)
DBL      DS    D
EDFLD    DC    X'40202020'
         DC    C')'
INDCAM   CAMLST NAME,INDICES,,AREA
BLKCAM   CAMLST BLOCK,TTR,CVOL,AREA
BLKCAMN  CAMLST BLOCK,TTR,,AREA
TTR      DC    XL3'01'
         DS    0D
AREA     DS    0CL265
         DS    CL256
NTTR     DS    CL3
CVOL     DS    CL6
LOWQ     DC    CL8' '
INDICES  DC    CL44' '
         LTORG
ENTRY    DSECT
ENAME    DS    CL8
ETTR     DS    CL3
ELEN     DS    CL1
FLGS     DS    CL1
MAXCNT   DS    CL1
CURCNT   DS    CL1
         END
