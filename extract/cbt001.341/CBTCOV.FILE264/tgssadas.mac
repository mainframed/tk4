         ISEQ  73,80
TGSSADA  START 0                                                    TGS
********************************************************************TGS
*              THIS RTNE IS USED TO OBTAIN DYNAMIC SAVE AREA/WORK  *TGS
*              AREAS.                                              *TGS
*                  THE RTNE EXPECTS TO FIND THE LENGTH OF THE      *TGS
*              REQUESTED DSA/WKA IN REG0, REG11 POINTING TO THE    *TGS
*              PRV, AND REG13 POINTING TO THE NEXT HIGHER SAVE     *TGS
*              AREA.                                               *TGS
*                  THE RTNE WILL CHECK THE THIRD WORD OF THE NEXT  *TGS
*              HIGHER SAVE AREA TO ENSURE THAT THE LSA POINTER IS  *TGS
*              ZERO.  IF IT IS NOT ZERO, THE RTNE WILL ISSUE       *TGS
*              FREEMAINS FOR ALL THE DSA/WKAS IT FINDS ON THIS     *TGS
*              LSA CHAIN, BEGINNING WITH THE FIRST ONE BELOW THE   *TGS
*              NEXT HIGHER SAVE AREA (THE NEXT HIGHER SAVE AREA    *TGS
*              BEING THE ONE POINTED TO BY REG13.)                 *TGS
*                  WHEN CONTROL IS RETURNED TO THE CALLER, REG15   *TGS
*              WILL CONTAIN UNPREDICTABLE INFORMATION, AND REG13   *TGS
*              WILL POINT TO THE DSA/WKA HE HAS REQUESTED.  THE    *TGS
*              FIRST AND SECOND FULLWORDS OF THIS DSA/WKA WILL     *TGS
*              CONTAIN THE LENGTH OF THE DSA/WKA, AND THE ADDRESS  *TGS
*              OF THE NEXT HIGHER SAVE AREA, RESPECTIVELY.  THE    *TGS
*              THIRD WORD WILL CONTAIN ZERO.  WORDS 4 THRU 18 WILL *TGS
*              CONTAIN UNPREDICTABLE INFORMATION, BUT THE REST OF  *TGS
*              THE DSA/WKA WILL BE INITIALIZED TO ZERO.            *TGS
********************************************************************TGS
R0       EQU   0                                                    TGS
R1       EQU   1                                                    TGS
R2       EQU   2                                                    TGS
R3       EQU   3                                                    TGS
R4       EQU   4                                                    TGS
R5       EQU   5                                                    TGS
R6       EQU   6                                                    TGS
R7       EQU   7                                                    TGS
R8       EQU   8                                                    TGS
R9       EQU   9                                                    TGS
RA       EQU   10                                                   TGS
RB       EQU   11                                                   TGS
RC       EQU   12                                                   TGS
RD       EQU   13                                                   TGS
RE       EQU   14                                                   TGS
RF       EQU   15                                                   TGS
*                                                                   TGS
*              *LL  OF MY DXD INSTRUCTIONS FOLLOW                   TGS
*                                                                   TGS
TGSWRK1  DXD   F                                                    TGS
TGSWRK2  DXD   F                                                    TGS
TGSLSA   DXD   F                                                    TGS
         USING *,RF                    SET TEMPORARY BASE REG       TGS
         ST    R0,0(RB,R0)             SAVE REG0                    TGS
         ORG   *-2                                                  TGS
         DC    QL2(TGSWRK1)                                         TGS
         ST    R1,0(RB,R0)             SAVE REG2                    TGS
         ORG   *-2                                                  TGS
         DC    QL2(TGSWRK2)                                         TGS
         GETMAIN R,LV=(0)              GET DSA/WKA                  TGS
         ST    R1,0(RB,R0)             SET TGSLSA                   TGS
         ORG   *-2                                                  TGS
         DC    QL2(TGSLSA)                                          TGS
         STM   R2,RA,28(R1)            SAVE REGS 2 THRU 10          TGS
         BALR  RA,R0                                                TGS
         DROP  RF                      DROP TEMPORARY BASEREG       TGS
         USING *,RA                    SET PERMANENT BASEREG        TGS
         L     R3,8(R0,RD)             GET LSA POINTER              TGS
         LTR   R3,R3                   LSA POINTER ZERO             TGS
         BC    8,NOCHAIN               BR IF YES                    TGS
FREELOOP LR    R1,R3                   REG1 EQ ADDR STORAGE TO FREE TGS
         L     R0,0(R0,R1)             REG0 EQ LENTH STORAGE TO FREETGS
         L     R3,8(R0,R1)             REG3 EQ NEXT LSA POINTER     TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         LTR   R3,R3                   LSA POINTER ZERO             TGS
         BC    7,FREELOOP              BRANCH IF NO                 TGS
         L     R1,0(RB,R0)             GET                          TGS
         ORG   *-2                          TGSLSA                  TGS
         DC    QL2(TGSLSA)                                          TGS
NOCHAIN  ST    RD,4(R0,R1)             SET BACK SA CHAIN            TGS
         ST    R1,8(R0,RD)             SET FORWARD SA CHAIN         TGS
         LR    RD,R1                   REG13 EQ ADDR DSA/WKA        TGS
         L     R0,0(RB,R0)             RETRIEVE REG0                TGS
         ORG   *-2                                                  TGS
         DC    QL2(TGSWRK1)                                         TGS
         L     R1,0(RB,R0)             RETREIVE REG1                TGS
         ORG   *-2                                                  TGS
         DC    QL2(TGSWRK2)                                         TGS
         ST    R0,0(R0,RD)             SET WORD1 OF DSA/WKA         TGS
         SR    R2,R2                   REG2 EQ ZERO                 TGS
         ST    R2,8(R0,RD)             SET WORD3 OF DSA/WKA         TGS
         L     R7,0(R0,RD)                                          TGS
         SH    R7,=H'72'                                            TGS
         LTR   R7,R7                                                TGS
         BC    8,SADADONE                                           TGS
         LA    R9,72(R0,RD)            POINT REG9 AT WKA            TGS
         MVI   0(R9),X'00'             GET READY                    TGS
         LA    R8,255                       TO INITIALIZE           TGS
SADALOOP CH    R7,=H'257'              LOOP TO INITIALIZE           TGS
         BC    4,SADALPEN                   THE WKA TO X'00'        TGS
         EX    R8,INITMVC                                           TGS
         SH    R7,=H'256'                                           TGS
         LA    R9,256(R0,R9)                                        TGS
         B     SADALOOP                                             TGS
SADALPEN SH    R7,=H'2'                                             TGS
         LTR   R7,R7                                                TGS
         BC    4,SADADONE                                           TGS
         EX    R7,INITMVC                                           TGS
SADADONE LM    R2,RA,28(RD)                                         TGS
         BR    RE                                                   TGS
INITMVC  MVC   1(0,R9),0(R9)                                        TGS
         LTORG                                                      TGS
         END
