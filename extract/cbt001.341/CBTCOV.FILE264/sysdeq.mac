         GBLC  &COMP
*$.HE GENERALIZED SUBROUTINES - SYSDEQ
*$.HE INTERFACE TO THE DEQ MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSDEQ
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS RTNE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE DEQ MACRO.  THE FORMAT OF THE PLI STATEMENT USED
*$TO INVOKE THIS RTNE IS AS FOLLOWS:
*
*$.IN 5
*$.SP 1
*$CALL SYSDEQ (QNAME,RNAME,LEV,RETURN),.
*$.IN 0
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*
*$QNAME CHAR (8)
*$.IN 5
*$MAJOR Q NAME
*
*$.IN 0
*$RNAME CHAR (*) - CAN BE VARYING STRING
*$.IN 5
*$MINOR Q NAME (1 - 255 CHAR)
*$.IN 0
*$LEV CHAR (*)
*$.IN 5
*$'SYSTEM' OR 'STEP' OR 'SYSTEMS'
*$.IN 0
*$RETURN CHAR (2)
*$.IN 5
*$'WT' - TASK CURRENTLY WAITING FOR
*$THIS RESOURCE (ONLY RETURNED
*$DURING ASYNCH EXIT)
*$.IN 5
*$'PR' - RESOURCE NOT CURRENTLY ENQD
*$UPON
*$.IN 5
*$'ER' - A FORMAT ERROR WAS FOUND
*$.IN 5
*$'OK' - DEQ WAS SUCCESSFUL
*
********************************************************************$
SYSDEQ PLIENTRY ,DSA=128,COMPILE=&SYSPARM
         LM    4,7,0(1)
         MVC   QNADR,0(4)              GET QNAME ADDR
         MVC   RNADR,0(5)              GET RNAME ADDR
         MVC   ENQLAST(4),=X'FF000000'
         AIF   ('&COMP' EQ 'F').FOPT0
         TM    6(R5),X'80'             IS RNAME VARYING?
         BC    8,NOTV1                 BR IF NOT
         L     RE,0(R5)
         MVC   RNLEN,1(RE)             GET RNAME LENGTH
         LA    RE,2(RE)
         ST    RE,RNADR
         B     BYNOTV1
NOTV1    DS    0H
         MVC   RNLEN,5(R5)             GET RNAME LENGTH
BYNOTV1  DS    0H
         AGO   .BYFOPT0
.FOPT0   ANOP
         MVC   RNLEN,7(5)              GET RNAME LENGTH
.BYFOPT0 ANOP
         L     6,0(6)                  6 = LEV ADDR
         CLC   0(4,6),=C'STEP'         IS IT STEP LEVEL
         BC    8,CKCTL                 BR IF YES
         CLC   0(5,6),=C'SYSTEM'       IS IT SYSTEM
         BC    7,ERR                   BR IF NOT
         OI    FLAGS,X'40'             IND SYSTEM SPECIFIED
         CLC   0(7,6),=C'SYSTEMS'      IS IT SYSTEMS?
         BC    7,CKCTL
         OI    FLAGS,X'48'             IND SYSTEMS
CKCTL    EQU   *
         OI    FLAGS,X'01'             IND RET=HAVE
         DEQ   MF=(E,ENQLIST)
         L     7,0(7)                  7 = RETURN ADDR
         LTR   15,15                   WAS IT GOOD DEQ
         BC    7,CKMORE                BR IF NOT
         MVC   0(2,7),=C'OK'
         B     EXIT
ERR      EQU   *
         L     7,0(7)                  7 = RETURN ADDR
         MVC   0(2,9),=C'ER'
EXIT     PLIEXIT
CKMORE   EQU   *
         SR    1,1
         IC    1,3(15)                 R1 = RETURN CODE
         B     *(1)
         B     FOUR
EIGHT    EQU   *
         MVC   0(2,7),=C'PR'           IND RESOURCE NOT ENQ'D
         B     EXIT
FOUR     EQU   *
         MVC   0(2,7),=C'WT'           TASK CURRENTLY WAITING FOR RESRC
         B     EXIT
         LTORG
DSA      DSECT
ENQLIST  DS    0F
ENQLAST  DS    CL1
RNLEN    DS    CL1
FLAGS    DS    CL1
         DS    CL1
QNADR    DS    F
RNADR    DS    F
 DS 0D
DSALEN EQU *-DSA
         END
