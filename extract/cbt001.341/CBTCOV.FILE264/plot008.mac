 /* PLOT008 - GRAPH SUBCOMMAND */
         % DCL @PROCNAME CHAR;
         % @PROCNAME = 'PLOT008';
         % INCLUDE PLOTSUB;
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         ON ERROR STOP; /*   FOR TESTING ONLY */
         DCL MONTAB(12) CHAR(3) STATIC INIT(
           'JAN','FEB','MAR','APR','MAY','JUN',
           'JUL','AUG','SEP','OCT','NOV','DEC');
         DCL (MM,YY) FIXED DEC(5,0);
         DCL TMPSTR CHAR(60) VAR;
         DCL AST CHAR(100) STATIC INIT((100)'*');
         DCL (CUR,I,J,K,L,M,N,O,P,Q,R,X,Y) FIXED BIN(31,0);
         DCL HDPTR PTR;
         HDPTR = NULL;
         DCL 1 P2A UNALIGNED BASED(P2APTR),
              2 ARRAY(0:23) FIXED BIN(31,0),
              2 INDX FIXED BIN(15,0),
              2 DTE FIXED DEC(5,0),
              2 HDR CHAR(78) VAR,
              2 NXTP2A PTR;
         ON CONDITION(BADOPER) BEGIN;
              DISPLAY(2) = 'BAD OPERAND = '××PARY(I);
              GO TO END_PLOT008;
              END;
         DCL DTECK FIXED DEC(5,0) INIT(0);
         DCL ARY(0:23) FIXED BIN(31,0) INIT((24)0);
         DCL PINDX FIXED BIN(15,0);
         DO I = 16 TO 2 BY -1;
           IF PARY(I) ^= ''
             THEN DO;
              PINDX = PARY(I);
              CALL SUBENT(7)(PINDX,P2PTR);
              IF P2PTR = NULL
                   THEN SIGNAL CONDITION(BADOPER);
              IF DTECK ^= 0
                   THEN IF P2.DTE ^= DTECK
                        THEN DO;
                             DISPLAY(2)='SORRY, DATES AREN''T ALIGNED';
                             GO TO END_PLOT008;
                             END;
              DTECK = P2.DTE;
              ALLOC P2A;
              P2A = P2, BY NAME;
              NXTP2A = HDPTR;
              HDPTR = P2APTR;
              DO J = 0 TO 23;
                   IF P2.ARRAY(J) < 0
                        THEN ARY(J) = -1;
                        ELSE ARY(J) = ARY(J) + P2.ARRAY(J);
                   END;
              END;
           END;
         IF HDPTR = NULL
              THEN RETURN;
         J = 0;
         DO I = 0 TO 23;
              IF ARY(I) > J
                   THEN J = ARY(I);
              END;
         K = J;
         J = J/55;
         IF K > J*55
              THEN J = J+1;
         IF J = 0
              THEN J = 1;
         YY = DTECK/100;
         MM = DTECK - YY*100;
         DO X = 0 TO 20;
           IF ARY(X) < 0
             THEN DO;
               CUR = 0;
               TMPSTR = 'NOT AVAILABLE';
               END;
             ELSE DO;
               Y = ARY(X)/J;
               CUR = ARY(X);
               TMPSTR = SUBSTR(AST,1,Y);
               A = 1;
               B = 0;
               DO P2APTR = HDPTR REPEAT NXTP2A WHILE(P2APTR ^= NULL);
                 B = B + 1;
                 IF ^(ARY(X)<=0 × P2A.ARRAY(X)<0)
                   THEN DO;
                     D = (P2A.ARRAY(X)*100+ARY(X)/2)/ARY(X);
                         /* D = PERCENTAGE, ROUNDED */
                     E = (Y*D + 50)/100;
                         /* E = LENGTH OF DISPLAY LINE PORTION */
                     SUBSTR(TMPSTR,A,E)=
                       REPEAT(SUBSTR('ABCDEFGHIJKLMNOPQRSTUVWXYZ',
                         B,1),E);
                     A = A + E;
                     END;
                 END;
              END;
           PUT STRING(DISPLAY(X+3)) EDIT(MONTAB(MM),YY,TMPSTR,CUR)
             (A,X(1),P'99',X(1),A,P'ZZZ,ZZZ,ZZZ');
           MM = MM + 1;
           IF MM > 12
                 THEN DO;
                      MM = 1;
                      YY = YY + 1;
                      END;
           END;
         I = 0;
         DO P2APTR = HDPTR REPEAT NXTP2A WHILE(P2APTR ^= NULL);
              I = I + 1;
              PUT STRING(SUBSTR(DISPLAY(I+2),73,5))
                EDIT(SUBSTR('ABCDEFGHIJKLMNOPQRSTUVWXYZ',I,1),'=',
                P2A.INDX)(A,A,P'ZZ9');
              END;
 END_PLOT008:
         DO P2APTR = HDPTR REPEAT HDPTR WHILE(P2APTR ^= NULL);
              HDPTR = NXTP2A;
              FREE P2A;
              END;
         END PLOT008;
