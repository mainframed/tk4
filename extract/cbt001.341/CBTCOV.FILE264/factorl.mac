 /* FACTORL
0*$.HE GENERALIZED SUBROUTINES - FACTORL
0*$.HE FACTORIAL
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$06/28/84
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME: FACTORL
0*$DOCUMENTATION ID: 0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE PROVIDES A FACTORIAL FUNCTION.  CALL FORMAT IS AS
0*$FOLLOWS:
0*$.NF
0*$.SP 1
0*$.IN 5
0*$DCL FACTORL ENTRY(DEC FLOAT(16))
0*$  RETURNS(DEC FLOAT(16));
0*$PUT LIST(FACTORL(1.5))
0*$.IN 0
0*$.SP 1
0*$.FI L
0*$FOR POSITIVE INTEGERS, FACTORL(N) IS DEFINED AS THE PRODUCT OF
0*$ALL POSITIVE INTEGERS UP TO N.  FACTORL(0) AND FACTORL(1) BOTH
0*$RETURN 1.  FACTORL WILL RETURN THE SPECIAL VALUE MISSING, WHERE
0*$UNSPEC(MISSING)='1'B, IF THE PARAMETER N IS A NEGATIVE INTEGER.
0*$FOR ALL OTHER VALUES OF N, THE VALUE RETURNED IS GAMMA(N+1).
0*$.SP 1
0*$THE FOLLOWING IS A TABLE OF SAMPLE ARGUMENTS AND THE RESULTING
0*$VALUE RETURNED BY FACTORL:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$ARGUMENT   RETURNED VALUE
0*$
0*$ 0         1
0*$ 1         1
0*$ 2         2
0*$ 3         6
0*$ 4         24
0*$-0.5        1.772453841
0*$ 0.5        0.8862269255
0*$ 1.5        1.329340388
0*$.FI L
0*$.IN 0
0*$.SP 1
 */
 FACTORL: PROC(PRM) RETURNS(DEC FLOAT(16)) OPTIONS(REENTRANT);
  DCL (PRM,F01,F23,F45,F67)  DEC  FLOAT(16);
  %INCLUDE @HEXBIT;
  DCL 1 CONSTANTS STATIC,
    2 BAL DEC FLOAT(16), /* FORCE BOUNDARY ALIGNMENT */
    2 CON001 BIT(64) INIT(@HEXBIT(C0C1B71B59A1A1F6)),
    2 CON002 BIT(64) INIT(@HEXBIT(41B33F20CFA73CB3)),
    2 CON003 BIT(64) INIT(@HEXBIT(4153CF867C239860)),
    2 CON004 BIT(64) INIT(@HEXBIT(C23EBA40FFB0397B)),
    2 CON005 BIT(64) INIT(@HEXBIT(43441182D7048BE6)),
    2 CON006 BIT(64) INIT(@HEXBIT(43C3CDE7AC8F2232)),
    2 CON007 BIT(64) INIT(@HEXBIT(42E8A532ACC72020)),
    2 CON008 BIT(64) INIT(@HEXBIT(C1A5004D879829C5)),
    2 CON009 BIT(64) INIT(@HEXBIT(41E62A3573ECF95D)),
    2 CON010 BIT(64) INIT(@HEXBIT(42C97F1D84DC37AC)),
    2 CON011 BIT(64) INIT(@HEXBIT(C327558408F56C71)),
    2 CON012 BIT(64) INIT(@HEXBIT(C358DA535E278586)),
    2 CON013 BIT(64) INIT(@HEXBIT(4411F52476FDA8AB)),
    2 CON014 BIT(64) INIT(@HEXBIT(441C1A16BED21CC5)),
    2 CON015 BIT(64) INIT(@HEXBIT(40E2DFC48DA77B56));
  DCL 1 DEFINED DEFINED CONSTANTS,
    2 BAL DEC FLOAT(16), /* FORCE BOUNDARY ALIGNMENT */
    2 GAMAA6 DEC FLOAT(16),
      /*   -0.7567002385928 */
    2 GAMAA5(0:5) DEC FLOAT(16),
      /*   11.2029121505218 */
      /*    5.2381653641874 */
      /*  -62.7275543027149 */
      /* 1089.0944433381650 */
      /* 3132.8690610495717 */
      /*  232.6453044878145 */
    2 GAMAB6 DEC FLOAT(16),
      /*  -10.3125739380508 */
    2 GAMAB5(0:5) DEC FLOAT(16),
      /*   14.3853048828456 */
      /*  201.4965441739693 */
      /* -629.3447351061687 */
      /*-1421.6453534644901 */
      /* 4597.1424406563356 */
      /* 7194.0888491935961 */
    2 GAMAC0 DEC FLOAT(16);
      /*    0.8862269254527580 */
  DCL MISSING DEC FLOAT(16);

  UNSPEC(MISSING)='1'B;
  F67=PRM;
  F23,F45=1.0;
  SELECT;
    WHEN(F67=0.0 × F67=1.0) RETURN(1.0); /* ALL DONE */
    WHEN(F67<0.0)  DO;
      DO WHILE(F67<0.0);
        F67=F67+1.0;
        F45=F45*F67;
        END;
      IF F67=0.0
        THEN RETURN(MISSING); /* ZERO IS ERROR */
      F23=F23/F45;
      END;
    OTHERWISE DO;
      DO F45=F67 REPEAT  F45*F67 UNTIL(F67<=1.0);
        F67=F67-1.0;
        END;
      IF F67=1.0
        THEN RETURN(F45);
      F23=F45;  /* SAVE REDUCTION FACTOR, F23=INT(X)! */
      END;
    END; /* SELECT */
  /* COMPUTE  GAMMA FUNCTION BY MEANS OF MINIMAX FRACTION OF
  DEGREE (7,7) FOR Z(-.5,.5). */
  F67=F67-0.5;
  F45=GAMAA6*F67;
  F01=GAMAB6+F67;
  DO I=0 TO 5;
    F45=(F45+GAMAA5(I))*F67; /* B0+B1*Z+B2*Z**2...+B7*Z**7  */
    F01=F01*F67+GAMAB5(I);
    END;
  F45=(F45/F01+GAMAC0)*F23;
  RETURN(F45);
  END FACTORL;
