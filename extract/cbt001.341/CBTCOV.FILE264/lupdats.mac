LUPDAT   CSECT                                                      TGS
********************************************************************
*                  THIS RTNE IS DESIGNED TO MAINTAIN A LOAD LIBRARY*
*              ON TAPE.                                            *
********************************************************************
         SAVES RETCTL
         TGSREQU
         L     RB,=V(NAMTAB)
         USING NAMTABD,RB
         LR    R1,RB
         LINK  EP=LMINIT               GO INITIALIZE NAMTAB
         TM    PRINT,X'80'             IS SYSPRINT AVLBL
         BC    8,NOPRNT                BR IF NOT
         OPEN  (SYSPRINT,(OUTPUT))
         TM    SYSPRINT+48,X'10'       WAS IT GOOD OPEN
         BC    8,BADOPN                BR IF NOT
NOPRNT   EQU   *
         TM    UT1,X'80'               IS SYSUT1 AVLBL
         BC    8,NOUT1                 BR IF NOT
         LOAD  EP=LUT1GET                                           TGS
         ST    R0,UT1ADDR
         LR    RF,R0                   GO GET UT1 RECORD
         BALR  RE,RF
         LTR   R1,R1                   IS IT IMMEDIATE EOF
         BC    7,GOTUT1                BR IF NOT
         NI    UT1,X'7F'               IND NO UT1
         B     NOUT1
GOTUT1   EQU   *
         STM   R0,R1,UT1RECL
         MVC   UT1NAME,4(R1)
NOUT1    EQU   *
         TM    UT3,X'80'               IS SYSUT3 AVLBL
         BC    8,NOUT3                 BR IF NOT
         LOAD  EP=LUT3GET                                           TGS
         ST    R0,UT3ADDR
         LR    RF,R0
         BALR  RE,RF                   GO GET UT3 RECORD
         LTR   R1,R1                   IS IT IMMEDIATE EOF
         BC    7,GOTUT3                BR IF NOT
         NI    UT3,X'7F'               IND NO UT3
         B     NOUT3
GOTUT3   EQU   *
         STM   R0,R1,UT3RECL
         MVC   UT3NAME,4(R1)
NOUT3    EQU   *
         CLC   UT1RECA,UT3RECA
         BC    8,RETCTL                ALL DONE IF NEITHER AVLBL
         LA    RA,CKTWO
CTLCOMP  EQU   *
         NI    DELSW,X'7F'
         CLC   UT1NAME,UT3NAME
         BC    2,USEUT3                BR IF SYSUT3 SHOULD BE USED
         BC    4,USEUT1                BR IF IT IS NOT A REPLACEMENT
         CLC   UT1NAME,=8X'FF'         IS IT EOF
         BC    8,USEUT1                BR IF YES
         MVC   RNME,UT1NAME
         ST    RA,SAVL3
         LA    R0,REPLNE
         BAL   RA,PUTSYSPR             GO TELL OF REPLACEMENT
         L     RA,SAVL3
         OI    DELSW,X'80'
USEUT1   EQU   *
         LM    R8,R9,UT1RECL           R8 = UT13 REC LEN
*                                      R9 = UT13 REC ADDR
         OI    FLIPSW,X'80'
         MVC   CURRNAME,UT1NAME
         BR    RA
USEUT3   EQU   *
         ST    RA,SAVL3
         MVC   REPCON,=CL10'ADDED'
         MVC   RNME,UT3NAME
         LA    R0,REPLNE
         BAL   RA,PUTSYSPR             GO TELL OF ADDITION
         L     RA,SAVL3
         MVC   REPCON,=CL10'REPLACED'
         LM    R8,R9,UT3RECL
         NI    FLIPSW,X'7F'
         MVC   CURRNAME,UT3NAME
         BR    RA
CKTWO    EQU   *
         TM    UT2,X'80'               IS SYSUT2 AVLBL
         BC    8,NOUT2                 BR IF NOT
         LOAD  EP=LUT2PUT
         ST    R0,UT2ADDR
NOUT2    EQU   *
         TM    IN,X'80'                IS SYSIN AVLBL
         BC    8,NOIN                  BR IF NOT
         OPEN  (SYSIN,(INPUT))
         TM    SYSIN+48,X'10'          WAS IT GOOD OPEN
         BC    1,GO                    BR IF YES
BADOPN   EQU   *
         ABEND 1111,DUMP
GO       EQU   *
         GET   SYSIN
         LR    R7,R1                   R7 = CURRENT CTL STMT ADDR
         CLC   0(9,R7),=CL9'LIST'      IS IT LIST OPTION
         BC    7,NOTLIST               BR IF NOT
         OI    LISTSW,X'80'
         B     GO
NOTLIST  EQU   *
         LTR   R9,R9                   IS UT13 AVLBL
         BC    7,POSSIBLE              BR IF YES
BADCTL   EQU   *
         MVC   29(10,R7),=CL10'REJECTED'
         LR    R0,R7
         BAL   RA,PUTSYSPR
         B     GO
POSSIBLE EQU   *
         CLC   CURRNAME,9(R7)
         BC    8,GOTMEM
         BC    2,BADCTL
         BAL   RA,GETMEM
         B     POSSIBLE
GOTMEM   EQU   *
         CLC   0(9,R7),=CL9'DELETE'    IS IT DELETE
         BC    7,NODEL                 BR IF NOT
         MVC   29(10,R7),=CL10'DELETED'
         LR    R0,R7
         BAL   RA,PUTSYSPR
         OI    DELSW,X'80'
         BAL   RA,GETMEM
         B     GO
NODEL    EQU   *
         CLC   0(9,R7),=CL9'CHNGE'     IS IT CHNGE
         BC    8,CHNGE                 BR IF YES
         CLC   0(9,R7),=CL9'CHANGE'    IS IT CHANGE
         BC    7,BADCTL                BR IF NOT
CHNGE    EQU   *
         LR    R6,R9
         AH    R6,R8
         SH    R6,=H'8'                R6 = ADDR SSI
         MVC   0(8,R6),19(R7)          RESET SSI
         MVC   29(10,R7),=CL10'CHANGED'
         LR    R0,R7
         BAL   RA,PUTSYSPR
         BAL   RA,GETMEM
         B     GO
GETMEM   EQU   *
         ST    RA,SAVL1
PRNT     EQU   *
         TM    DELSW,X'80'             IS IT DELET
         BC    1,GET                   BR IF YES
PUT      EQU   *
         L     RF,PDSRTNE                                           TGS
         LTR   RF,RF                   ARE THERE PDSS TO BE BUILT   TGS
         BC    8,PUTA                                               TGS
         LR    R0,R8                                                TGS
         LR    R1,R9                                                TGS
         BALR  RE,RF                                                TGS
PUTA     EQU   *                                                    TGS
         TM    UT2,X'80'               IS SYSUT2 AVLBL
         BC    8,GET                   BR IF NOT
         L     RF,UT2ADDR
         LR    R0,R8
         LR    R1,R9
         BALR  RE,RF                   WRITE SYSUT2
GET      EQU   *
         TM    FLIPSW,X'80'            IS SYSUT1 TO BE USED
         BC    1,GETUT1                BR IF YES
         L     RF,UT3ADDR
         BALR  RE,RF                   GO GET UT3 RECORD
         STM   R0,R1,UT3RECL           SAVE LEN/ADDR
         LTR   R1,R1                   IS IT EOF ON SYSUT3
         BC    8,UT3EOF                BR IF YES
         TM    0(R1),X'80'             IS IT NEW MEMB               TGS
         BC    8,GETA                  BR IF NOT                    TGS
         MVC   UT3NAME,4(R1)           RESET UT3NAME                TGS
GETA     L     RA,SAVL1
         TM    0(R1),X'80'             IS IT NEW MEMB
         BC    1,CTLCOMP               BR IF YES
         TM    DELSW,X'80'             IS IT DELETE
         BC    1,GET                   BR IF YES
         LR    R8,R0
         LR    R9,R1
         B     PUT
GETUT1   EQU   *
         L     RF,UT1ADDR
         BALR  RE,RF                   GO GET UT1 RECORD
         STM   R0,R1,UT1RECL           SAVE LEN/ADDR
         LTR   R1,R1                   IS IT EOF ON SYSUT1
         BC    8,UT1EOF                BR IF YES
         TM    0(R1),X'80'                                          TGS
         BC    8,GETA                                               TGS
         MVC   UT1NAME,4(R1)                                        TGS
         B     GETA
UT1EOF   EQU   *
         DELETE EP=LUT1GET
         MVC   UT1NAME,=8X'FF'
         L     RA,SAVL1
         B     CTLCOMP
UT3EOF   EQU   *
         DELETE EP=LUT3GET
         MVC   UT3NAME,=8X'FF'
         L     RA,SAVL1
         B     CTLCOMP
EODIN    EQU   *
         CLOSE SYSIN
         NI    IN,X'7F'
NOIN     EQU   *
         BAL   RA,GETMEM
         LTR   R9,R9                   IS IT EOF
         BC    7,NOIN                  BR IF NOT
         SR    R0,R0                                                TGS
         SR    R1,R1
         TM    UT2,X'80'               IS SYSUT2 AVLBL
         BC    8,TRYPDS                BR IF NOT
         L     RF,UT2ADDR
         BALR  RE,RF
TRYPDS   EQU   *
         L     RF,PDSRTNE
         LTR   RF,RF
         BC    8,RETCTL
         SR    R0,R0                                                TGS
         SR    R1,R1
         BALR  RE,RF
         B     RETCTL
PUTSYSPR EQU   *                                                    TGS
         TM    PRINT,X'80'             IS PRINT AVLBL               TGS
         BCR   8,RA                    BR IF NOT                    TGS
         PUT   SYSPRINT                                             TGS
         BR    RA                                                   TGS
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,                    C
               LRECL=40,BLKSIZE=400,RECFM=FBA
FLIPSW   DC    X'00'         BIT 0=1 IF SYSUT1 CURRENTLY IN USE
*                                 =0 IF SYSUT3 CURRENTLY IN USE
UT1NAME  DS    0CL8
         DC    8X'FF'
UT3NAME  DS    0CL8
         DC    8X'FF'
*
UT1RECL  DC    F'0'
UT1RECA  DC    F'0'
UT3RECL  DC    F'0'
UT3RECA  DC    F'0'
UT1ADDR  DC    F'0'
UT3ADDR  DC    F'0'
REPLNE   DS    0CL40
         DC    CL10' '
RNME     DS    CL8
         DC    CL12' '
REPCON   DC    CL10'REPLACED'
UT2ADDR  DC    F'0'
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=EODIN
LISTSW   DC    X'00'
CURRNAME DC    CL8' '
SAVL1    DC    F'0'
SAVL2    DC    F'0'
SAVL3    DC    F'0'
DELSW    DC    X'00'
         LTORG
NAMTABD  DSECT
         NAMTAB
         END
