 RPVSPC: PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
         /* PARM IS SET TO PRINT(1) AND/OR PRINT(2) TO INDICATE
            HOW MANY OF THE TWO POSSIBLE REPORTS ARE TO BE GENERATED.*/
         % DCL ITFACT CHAR;
         % ITFACT = '1000';
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         DCL PLIXOPT CHAR(15) VAR EXT STATIC INIT('ISA(200K)');
         DCL (SEQINDX,SEQSTRT) FIXED BIN(15,0);
         DCL 1 SEQAG UNALIGNED STATIC,
              2 CON1 CHAR(6) INIT('PRINT('),
              2 SEQPIC PIC'9',
              2 CON2 CHAR(1) INIT(')');
         DCL SEQAGD CHAR(8) DEF SEQAG;
         DCL SEQTIT(2) CHAR(80) VAR STATIC INIT(
           '(SEQUENCE DESCENDING BY CPU USAGE)',
           '(SEQUENCE BY USERID)');
         DCL SEQCK(2) ENTRY RETURNS(BIT(1));
         SEQCK(1) = PIGCK;
         SEQCK(2) = UIDCK;
         DCL 1 UIDARRAY (ITFACT),
              2 UID FIXED BIN(31,0) INIT((ITFACT) 0),
              2 TGETCUM FIXED BIN(31,0) INIT((ITFACT) 0),
                   /* NUMBER OF DISK EXCPS */
              2 TPUTCUM FIXED BIN(31,0) INIT((ITFACT) 0),
                   /* NUMBER OF TERMINAL I/O */
              2 LOGTCUM FIXED BIN(31,0) INIT((ITFACT) 0),
                   /* ELAPSED SESSION TIME */
              2 LOGTCNT FIXED BIN(31,0) INIT((ITFACT) 0),
                   /* NUMBER OF LOGONS */
              2 CPUTCUM FIXED BIN(31,0) INIT((ITFACT) 0);
                   /* CPU TIME */
         DCL 1 SRTSAV,
              2 SRT1 FIXED BIN(31,0),
              2 SRT2 FIXED BIN(31,0),
              2 SRT3 FIXED BIN(31,0),
              2 SRT4 FIXED BIN(31,0),
              2 SRT5 FIXED BIN(31,0),
              2 SRT6 FIXED BIN(31,0);
         DCL SORTSW BIT(1);
         DCL UIDCNT FIXED BIN INIT(ITFACT);
         % INCLUDE SMFHDR;
         % INCLUDE SMFVSPC;
         % INCLUDE SMFDEFI;
         DCL ELAPTME ENTRY(FIXED DEC(5,0), FIXED BIN(31,0),
                           FIXED DEC(5,0), FIXED BIN(31,0))
           RETURNS(FIXED BIN(31,0));
         DCL ROMAN ENTRY(FIXED BIN(15,0))
           RETURNS(CHAR(15) VAR);
         DCL PAGE FIXED BIN(15,0) INIT(0);
         DCL SUFFIX ENTRY(CHAR(*),CHAR(*))
           RETURNS(FIXED BIN(15,0));
         DCL SPELDTE ENTRY(CHAR(6))
           RETURNS(CHAR(40) VAR);
         DCL (DATE,LOW) BUILTIN;
         DCL CTOHMC ENTRY(FIXED BIN(31,0))
           RETURNS(CHAR(9));
         DCL TITSTR CHAR(80) VAR;
 ON ENDPAGE(SYSPRINT) BEGIN;
         PUT PAGE;
         PAGE = PAGE + 1;
         IF INDEX(PARM,'MONTHLY')^=0
              THEN PUT STRING(TITSTR)
                EDIT('MONTH-TO-DATE VSPC USAGE REPORT, DATE=',
                SPELDTE(DATE))(A,A);
              ELSE PUT STRING(TITSTR)
                EDIT('DAILY VSPC USAGE REPORT, DATE=',
                SPELDTE(DATE))(A,A);
         PUT EDIT(TITSTR,'PAGE ',ROMAN(PAGE))
           (COL(38-LENGTH(TITSTR)/2),A,COL(73),A,A);
         PUT EDIT(SEQTIT(SEQINDX))
           (COL(38-LENGTH(SEQTIT(SEQINDX))/2),A) SKIP(1);
         PUT EDIT('VSPC','TOTAL')
           (COL(26),A,COL(47),A) SKIP(2);
         PUT EDIT('USAGE','CPU TIME','SESSION TIME')
           (COL(25),A,COL(33),A,COL(44),A) SKIP(1);
         PUT EDIT('USERID','COSTS','HH:MM:SS','HH:MM:SS',
           'LOGONS','DASDIO','TERMIO')
           (COL(15),A,COL(25),A,COL(33),A,COL(46),A,
           COL(58),A,COL(68),A,COL(75),A) SKIP(1);
         PUT SKIP(2);
         END;
         ON ENDFILE(SMFDATA) GO TO DOREPORT;
       DO FOREVER=1 REPEAT FOREVER;
         READ FILE(SMFDATA) INTO(SMFREC);
         I = TYPE;
         IF I = 48 & VSPCID = 6
           THEN DO;
             CURUID = VSPCUID;
             DO I = 1 TO ITFACT WHILE((UID(I)^=0)
               &(UID(I)^=CURUID));
               END;
             IF I > ITFACT
               THEN DO;
                 PUT EDIT('# USERIDS EXCEEDED TABLE SIZE =',I)
                   (A,P'Z,ZZ9') SKIP(1);
                 SIGNAL ERROR;
                 END;
             UID(I) = CURUID;
             LOGTCNT(I) = LOGTCNT(I) + 1;
             TGETCUM(I) = TGETCUM(I) + VSPCDASD;
             TPUTCUM(I) = TPUTCUM(I) + VSPCTPCT;
             CPUTCUM(I) = CPUTCUM(I) + VSPCCPU;
             LOGTCUM(I) = LOGTCUM(I) + VSPCCNCT;
             END;
         END;
 DOREPORT:
       DO SEQINDX=1 TO 2;
         SEQPIC = SEQINDX;
         IF (INDEX(PARM,SEQAGD)^=0) × (SEQINDX=2)
           THEN DO;
             DO UNTIL(^SORTSW);
              SORTSW = '0'B;
              DO I = 1 TO ITFACT WHILE(UID(I+1)^=0);
                   IF SEQCK(SEQINDX)
                        THEN DO;
                             SORTSW = '1'B;
                             SRTSAV = UIDARRAY(I);
                             UIDARRAY(I) = UIDARRAY(I+1);
                             UIDARRAY(I+1) = SRTSAV;
                             END;
                   END;
              END;
             TOT = 0;
             SIGNAL ENDPAGE(SYSPRINT);
             DO I = 1 TO ITFACT WHILE(UID(I) ^= '');
             COST = CPUTCUM(I)*58/100;
             TOT = TOT + COST;
             PUT EDIT(
               UID(I),
               CPUTCUM(I)*58/100,
               DOT(CPUTCUM(I)),
               DOT(LOGTCUM(I)*100),
               LOGTCNT(I),
               TGETCUM(I),
               TPUTCUM(I))
              (COL(15),P'9999999',
               COL(23),P'ZZZ9.99',
               COL(32),A,
               COL(45),A,
               COL(61),P'ZZ9',
               COL(67),P'ZZ,ZZ9',
               COL(74),P'ZZ,ZZ9') SKIP(1);
             END;
           PUT EDIT('TOTAL COST = ',TOT)(COL(7),A,P'ZZ,ZZ9.99')
                   SKIP(2);
           END;
         END;
 PIGCK:  PROC RETURNS(BIT(1));
         IF CPUTCUM(I) < CPUTCUM(I+1)
              THEN RETURN('1'B); /* OUT OF SEQUENCE */
              ELSE RETURN('0'B); /* SEQUENCE OK */
         END PIGCK;
 UIDCK:  PROC RETURNS(BIT(1));
         IF UID(I) > UID(I+1)
              THEN RETURN('1'B); /* OUT OF SEQUENCE */
              ELSE RETURN('0'B); /* SEQUENCE OK */
         END UIDCK;
 DOT:  PROC(TME) RETURNS(CHAR(9));
         DCL TME FIXED BIN(31,0);
         DCL CHTME CHAR(9);
         DCL I FIXED BIN(15,0);
         CHTME = CTOHMC(TME);
         IF SUBSTR(CHTME,4,1) ^= ':'
              THEN CHTME = ' '××CHTME;
         DO UNTIL(I=0);
              I = INDEX(CHTME,'00:');
              IF I ^= 0
                   THEN SUBSTR(CHTME,I,3) = '';
              END;
         RETURN(CHTME);
         END DOT;
         END;
