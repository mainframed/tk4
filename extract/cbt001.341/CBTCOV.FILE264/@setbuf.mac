 /* @SETBUF */
 %NOPRINT;
         % @SETBUFI = 'Y';
         % DCL @SETBUF ENTRY;
 % @SETBUF:  PROC(PARM1,PARM2,PARM3,NAME,MF) RETURNS(CHAR);
 /*
0*$.HE GENERALIZED SUBROUTINE - @SETBUF
0*$.HE PLI PREPROCESSOR TEXT
0*$.HE SET UP TEXT BUFFER
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/12/76
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  @SETBUF
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS PLI PROCESSOR PROCEDURE IS INVOKED BY OTHER PREPROCESSOR
0*$PROCEDURES (SUCH AS @WRITE AND @CMD) TO SET UP A TEXT BUFFER.
0*$CALL FORMAT IS -
0*$.SP 1
0*$.IN 5
0*$STRING = @SETBUF(PARM1,PARM2,PARM3,NAME,MF);
0*$.IN 0
0*$.SP 1
0*$ALL 5 PARAMETERS ARE DEFINED CHARACTER.  PARM1, PARM2, AND PARM3
0*$CAN BE ONE OF THE FOLLOWING SETS -
0*$.SP 1
0*$.IN 5
0*$.NF
0*$PARM1   PARM2   PARM3
0*$.SP 1
0*$'TEXT'  -       -
0*$LABEL   'TEXT'  -
0*$LABEL   'TEXT'  MF=L
0*$LABEL   -       -
0*$LABEL   MF=E
0*$LABEL   MF=S
0*$.FI L
0*$.IN 0
0*$.SP 1
0*$IN CASES 1 AND 2, MF WILL BE SET TO MF=C.  IN CASE 3, MF WILL
0*$BE SET TO MF=L.  IN CASES 4, 5 & 6, MF WILL BE SET TO MF=E.
0*$IN CASES 1, 2, AND 3 THE RETURNED STRING WILL CONTAIN THE
0*$PLI DECLARE STATEMENT FOR THE TEXT BUFFER, AND NAME WILL
0*$CONTAIN THE NAME OF THE TEXT BUFFER.  IN CASES 4 AND 5, THE
0*$RETURNED STRING WILL BE A NULL, AND NAME WILL BE SET EQUAL
0*$TO LABEL.  NAME WILL ALSO EQUAL LABEL IN CASES 2, 3, AND 4.
0*$IN CASE 6, RETSTR WILL BE SET TO CODE TO ASSIGN THE NAMED
0*$CHARACTER STRING TO BUFFER STRUCTURE BUFFER@, AND NAME WILL
0*$EQUAL BUFFER@.
 */
         DCL (PARM1,PARM2,PARM3,NAME,TEXT,MF,RETSTR) CHAR;
         DCL (I,J) FIXED;
         RETSTR = '';
         IF SUBSTR(PARM1,1,1) = ''''
              THEN DO;
                   NAME = @NULL;
                   TEXT = PARM1;
                   MF = 'MF=C';
                   END;
              ELSE DO;
                   IF PARM2='MF=S'
                        THEN DO;
                             MF='MF=E';
                             NAME='BUFFER@';
                             RETSTR=' BUFLEN@=LENGTH('××PARM1××')+4;'××
                               ' BUFSTR@='××PARM1××';';
                             RETURN(RETSTR);
                             END;
                   NAME = PARM1;
                   TEXT = PARM2;
                   MF = PARM3;
                   IF PARM2 = ''
                        THEN MF = 'MF=E';
                        ELSE IF PARM3 = ''
                             THEN MF = 'MF=C';
                   END;
         I = LENGTH(TEXT);
         J = I + 2;
         IF MF ^= 'MF=E'
             THEN RETSTR=RETSTR××' DCL 1 '××NAME××' STATIC, 2 '××@NULL××
                ' FIXED BIN(15,0) INIT('××J××'), 2 '××@NULL××
                ' FIXED BIN(15,0) INIT(0), 2 '××@NULL××
                ' CHAR(256) INIT('××TEXT××');';
         RETURN(RETSTR);
         % END @SETBUF;
 %PRINT;
