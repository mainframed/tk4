 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENT047 - SETL,$SETQUOT,$INCR,$DECR,$XLATE,$SETD,$GETMK,SYMC,
                    SETQUOT, INCR, DECR, XLATE, SETD, GETMK,GETDSN,
                   GETMEM */
   % DCL @PROCNAME CHAR;
   % DCL @RENTNAME CHAR;
   % @PROCNAME = 'RENT';
   % @RENTNAME = 'DENT047';
   % INCLUDE DENTSUB;
   DCL KEYTABLE(5) CHAR(80) VAR STATIC
     INIT('ALL','PERIOD','LEFT','RIGHT','CHAR');
   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*),CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL (OPER,NUM) CHAR(80) VAR;
   DCL INDX FIXED BIN(15,0);
   DCL (ALLSW,PERIODSW,LEFTSW,RIGHTSW,CHARSW) BIT(1) INIT('0'B);
   DCL GBLSW BIT(1) STATIC INIT('0'B);
   DCL SYMBOLPM CHAR(8);
   DCL (MYCMD,ANSWER) CHAR(240) VAR;
   DCL SETTXT CHAR(100) VAR;
   DCL HELPSTR CHAR(4) VAR INIT('');
   DCL (TXT,SAVTXT) CHAR(256) VAR;
   DCL 1 REPBUF,
     2 BUFLEN FIXED BIN(15,0),
     2 OFFSET FIXED BIN(15,0),
     2 BF CHAR(256);
   DCL SETCAPS ENTRY;
   DCL (TRH,TR) PTR;
   RHRC=0;
   SELECT(CMDINDX);
     WHEN(2) DO; /* SETL */
       CALL SETCAPS(PARY(2));
       SYMBOLPM='&'××PARY(2);
       SETTXT=PARY(3);
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(3,10) DO; /* $SETQUOT,SETQUOT */
       IF PARY(3)=''
         THEN PARY(3)='ANSWER';
       CALL SETCAPS(PARY(3));
       BUFLEN = MIN(260,LENGTH(PARY(3))+5);
       OFFSET = 0;
       BF='&'××PARY(3);
       CALL #REP(REPBUF);
       MYCMD = SUBSTR(BF,1,BUFLEN-4);
       DO I = 0 REPEAT J+1 UNTIL(J=I);
         J=INDEX(SUBSTR(MYCMD,I+1),'''')+I;
         IF J>I
           THEN MYCMD=SUBSTR(MYCMD,1,J-1)××''''××
             SUBSTR(MYCMD,J);
         END;
       MYCMD=''''××MYCMD××'''';
       SETTXT=MYCMD;
       CALL SETCAPS(PARY(2));
       SYMBOLPM='&'××PARY(2);
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(4,5,11,12) DO; /* $INCR,$DECR,INCR,DECR */
       IF PARY(3)=''
         THEN K = 1;
         ELSE K = PARY(3);
       CALL SETCAPS(PARY(2));
       BUFLEN = MIN(260,LENGTH(PARY(2))+5);
       OFFSET = 0;
       BF,SYMBOLPM='&'××PARY(2);
       CALL #REP(REPBUF);
       TXT = SUBSTR(BF,1,BUFLEN-4);
       IF TXT=''
         THEN I = 0;
         ELSE I = TXT;
       IF CMDINDX=4 × CMDINDX=11
         THEN I = I + K; /* INCR */
         ELSE I = I - K; /* DECR */
       PUT STRING(SETTXT) EDIT(I)(P'99999');
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(6,13) DO;  /* $XLATE,XLATE */
       CALL SETCAPS(PARY(2)); /* NAME IN UPPER CASE */
       BUFLEN=MIN(260,LENGTH(PARY(2))+5);
       OFFSET=0;
       BF,SYMBOLPM='&'××PARY(2);
       CALL #REP(REPBUF);
       SETTXT=SUBSTR(BF,1,BUFLEN-4);
       CALL SETCAPS(SETTXT); /* TEXT IN UPPER CASE */
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(7,14) DO; /* $SETD,SETD */
       /* 1     2          3        4     5     6
       /* SETD <VARIABLE> <LINENO> <OFF> <LEN> <NAME> */
       IF PARY(6)=''
         THEN TRH=RHPTR;
         ELSE DO;
           CALL SETCAPS(PARY(6));
           DO TRH=NXTRHEL REPEAT TRH->RHNXT
             WHILE(TRH^=NULL) UNTIL(TRH->RHNAME=PARY(6));
             END;
           END;
       IF TRH=NULL
         THEN DO;
           PUT SKIP LIST('ERROR IN $SETD, NO DATA SET FOUND');
           SIGNAL ERROR;
           END;
       IF PARY(3)='*'
         THEN I=TRH->RECSEQ;
         ELSE I=PARY(3);
       IF I=TRH->RECSEQ
         THEN TR=TRH->RECPTR;
         ELSE DO TR=TRH->NXTHDR REPEAT TR->NXTPTR
           WHILE(TR^=TRH) UNTIL(TR->RECSEQ=I);
           END;
       IF TR=TRH
         THEN DO;
           PUT SKIP LIST('ERROR IN $SETD - LINE NOT FOUND');
           SIGNAL ERROR;
           END;
       OFF=PARY(4);
       LEN=PARY(5);
       IF OFF+LEN-1 <= LENGTH(TR->RECSTR)
         THEN ILEN=LEN;
         ELSE IF OFF > LENGTH(TR->RECSTR)
           THEN OFF,ILEN=0;
           ELSE ILEN=LENGTH(TR->RECSTR)-OFF+1;
       SETTXT=SUBSTR(TR->RECSTR,OFF,MIN(100,ILEN));
       CALL SETCAPS(PARY(2));
       SYMBOLPM='&'××PARY(2);
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(8,15) DO; /* GETMK <NUMBER> <LABEL> <NAME> */
       IF PARY(4)=''
         THEN TRH=RHPTR;
         ELSE DO;
           CALL SETCAPS(PARY(4));
           DO TRH=NXTRHEL REPEAT TRH->RHNXT
             WHILE(TRH^=NULL) UNTIL(TRH->RHNAME=PARY(4));
             END;
           END;
       IF TRH=NULL
         THEN DO;
           PUT SKIP LIST('ERROR IN $GETMK, NAME NOT FOUND');
           SIGNAL ERROR;
           END;
       I=TRH->MARKARY(PARY(2));
       PUT STRING(SETTXT) EDIT(I)(P'99999');
       CALL SETCAPS(PARY(3));
       SYMBOLPM='&'××PARY(3);
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(9) DO; /* SYMC <NAME> */
       CALL SETCAPS(PARY(2));
       MSGDATA=#SYMC('&'××PARY(2));
       END;
     WHEN(16) DO; /* GETDSN */
       IF PARY(2)=''
         THEN PARY(2)='SYSDSN';
         ELSE CALL SETCAPS(PARY(2));
       SYMBOLPM='&'××PARY(2);
       IF OUTDDN=NULL
         THEN SETTXT='';
         ELSE SETTXT=OUTDDN->ALCDSN;
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(17) DO; /* GETMEM */
       IF PARY(2)=''
         THEN PARY(2)='SYSMEM';
         ELSE CALL SETCAPS(PARY(2));
       SYMBOLPM='&'××PARY(2);
       IF OUTDDN=NULL
         THEN SETTXT='';
         ELSE SETTXT=OUTDDN->ALCMEM;
       CALL #SET(GBLSW,SYMBOLPM,SETTXT);
       END;
     WHEN(18) DO; /* NUMCK */
       CALL SETCAPS(PARY(2));
       NUM=#SYMC('&'××PARY(2));
       IF NUM=''
         THEN DO;
           MSGDATA=PARY(2)××' IS EMPTY';
           RHRC=4; /* FLAG AS NOT OK IF EMPTY */
           RETURN;
           END;
       DO I = 3 TO 16 WHILE(PARY(I)^='');
         CALL SETCAPS(PARY(I));
         CALL #SPARS(PARY(I),KEYTABLE,OPER,INDX);
         SELECT(INDX);
           WHEN(0) DO;
             MSGDATA='INVALID KEYWORD = '××PARY(I);
             RHRC=8;
             RETURN;
             END;
           WHEN(1) ALLSW='1'B;
           WHEN(2) PERIODSW='1'B;
           WHEN(3) LEFTSW='1'B;
           WHEN(4) RIGHTSW='1'B;
           WHEN(5) CHARSW='1'B;
           OTHERWISE; /* TO GET RID OF COMPILER MESSAGE */
           END; /* SELECT */
         END;
       IF (LEFTSW & RIGHTSW) × (ALLSW & (LEFTSW × RIGHTSW))
         THEN DO;
           MSGDATA='USE ONLY ONE OF THE KEYWORDS ALL, LEFT, RIGHT';
           RHRC=8;
           RETURN;
           END;
       IF PERIODSW & INDEX(NUM,'.')^=0
         THEN SUBSTR(NUM,INDEX(NUM,'.'))='0';
       IF LEFTSW & SUBSTR(NUM,1,1)=' '
         THEN DO;
           MSGDATA=PARY(2)××' IS NOT LEFT JUSTIFIED';
           RHRC=4;
           RETURN;
           END;
       IF RIGHTSW & SUBSTR(NUM,LENGTH(NUM),1)=' '
         THEN DO;
           MSGDATA=PARY(2)××' IS NOT RIGHT JUSTIFIED';
           RHRC=4;
           RETURN;
           END;
       IF ^CHARSW
         THEN DO;
           IF ALLSW
             THEN DO;
               I=VERIFY(NUM,'0123456789');
               IF I^=0
                 THEN DO;
                   MSGDATA=PARY(2)××' IS NOT ALL NUMERIC - FIRST BAD CHA
 R='''××SUBSTR(NUM,I,1)××'''';
                   RHRC=4;
                   END;
               END;
             ELSE DO;
               NUM=SUBSTR(NUM,PREFIX(NUM,' '));
               NUM=SUBSTR(NUM,1,SUFFIX(NUM,' '));
               I=VERIFY(NUM,'0123456789');
               IF I^=0
                 THEN DO;
                   MSGDATA=PARY(2)××' CONTAINS BAD DATA - FIRST BAD CHAR
 ='''××SUBSTR(NUM,I,1)××'''';
                   RHRC=4;
                   END;
               END;
           END;
       IF RHRC=0
         THEN MSGDATA='OK';
       RETURN;
       END;
     END; /* END SELECT */
   END;
