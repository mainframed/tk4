 /* SCANVTC */
 /*
0*$.HE GENERALIZED SUBROUTINES - SCANVTC
0*$.HE SCANVTC - ACCESS SUCCESSIVE DSCBS IN VTOC
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$7/23/76
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  SCANVTC
0*$DOCUMENTATION ID: 0*$
0*$.FI L
0*$.SP 5
0*$THIS ROUTINE CAN BE USED TO ACCESS EACH DSCB IN THE VTOC.
0*$THE ROUTINE WILL PASS BACK A NEW DSCB AT EACH INVOCATION.
0*$.SP 1
0*$FOLLOWING IS AN APLHABETIC LIST OF ALL DATA VARIABLES DECLARED
0*$WITH THE EXTERNAL ATTRIBUTE IN THIS RTNE.  NOTE THAT WITH THE
0*$EXCEPTION OF VARIABLE WKAREA, ALL OF THE VARIABLES ARE THE SAME
0*$AS THOSE USED FOR A CALL TO NEXTDD SUBROUTINE.
0*$.SP 1
0*$DDNAME CHAR(8) EXT
0*$.IN 5
0*$DDNAME ASSOCIATED WITH THE VTOC TO BE PROCESSED.  SET BY CALLER.
0*$.IN 0
0*$NXFIRST BIT(1) EXT
0*$.IN 5
0*$THIS SWITCH MUST BE GIVEN AN INITIAL VALUE BY THE CALLER.  WHEN
0*$NXFIRST = '1'B, IT TELLS THIS ROUTINE TO START FROM THE BEGINNING
0*$OF THE VTOC.
0*$.IN 0
0*$NXLAST BIT(1) EXT
0*$.IN 5
0*$WHEN THE CALLER SETS NXFIRST TO '1'B, HE SHOULD SET NXLAST TO '0'B
0*$THIS RTNE WILL SET NXLAST = '1'B BEFORE RETURNING CONTROL WHEN
0*$ALL DSCBS HAVE BEEN PROCESSED.
0*$.IN 0
0*$UCBPTR PTR EXT
0*$.IN 5
0*$ADDRESS OF THE UCB FOR THE SPECIFIED VTOC.  SET BY CALLER.
0*$.IN 0
0*$VOLUME CHAR(6) EXT
0*$.IN 5
0*$VOLUME SERIAL OF THE VTOC TO BE PROCESSED.  SET BY CALLER.
0*$.IN 0
0*$WKAREA CHAR(148) EXT;
0*$.IN 5
0*$THIS RTNE MOVES EACH SUCCESSIVE DSCB ENTRY TO THIS VARIABLE.
 */
 SCANVTC:  PROC;
         % INCLUDE UCB;
         % INCLUDE DEVDATA;
         % INCLUDE DSCB4;
         DCL VOLUME CHAR(6) EXT;
         DCL WKAREA CHAR(148) EXT;
         DCL (NXFIRST,NXLAST) BIT(1) EXT;
         DCL UCBPTR PTR EXT;
         DCL DDNAME CHAR(8)EXT;
         DCL DEVAREA CHAR(24) STATIC;
         DCL (DEVDPTR,DSCB4PTR) PTR STATIC;
         DCL RET FIXED BIN(31,0);
         DCL TT FIXED BIN(31,0) STATIC INIT(0),
             TTD CHAR(4) DEF TT;
         DCL R FIXED BIN(31,0) STATIC INIT(0),
             RD CHAR(4) DEF R;
         DCL 1 CCHHR1,
              2 CC FIXED BIN(15,0),
              2 HH FIXED BIN(15,0),
              2 R1 CHAR(1);
         DCL CCHHR CHAR(5) DEF CCHHR1;
         DCL ENDCCHH CHAR(5) STATIC;
         DCL RETB BIT(32);
         DCL DSCBPER FIXED BIN(31,0) STATIC;
         IF NXFIRST
              THEN DO;
                   NXFIRST = '0'B;
                   CALL DEVTYPE(RET,DDNAME,DEVAREA,'DEVTAB');
                   DEVDPTR = ADDR(DEVAREA);
                   SUBSTR(TTD,3,2) = SUBSTR(DISKSEG.SRTEFSCT,1,2);
                        /* TT = TT FROM TTR OF VTOC */
                   SUBSTR(RD,4,1) = SUBSTR(DISKSEG.SRTEFSCT,3,1);
                        /* R = R FROM TTR OF VTOC */
                   ENDCCHH = HIGH(5);
                        /* WILL BE SET TO END OF VTOC FROM F4 DSCB */
                   DSCB4PTR = ADDR(WKAREA);
                   END;
              ELSE DO;
                   R = R + 1;
                   IF R > DSCBPER
                        THEN DO;
                             R = 1;
                             TT = TT + 1;
                             END;
              END;
         R1 = SUBSTR(RD,4,1);
         CC = TT/DEVDNTRK;
         HH = TT - (CC * DEVDNTRK);
         IF CCHHR >= ENDCCHH
              THEN DO;
                   NXLAST = '1'B;
                   RETURN;
                   END;
         CALL SYSVTOC('SEEK',RETB,CCHHR,VOLUME,WKAREA);
         IF RETB ^= 0
              THEN SIGNAL ERROR;
         IF DS4IDFMT = '4'
              THEN DO;
                   ENDCCHH = DS4CCHHU;
                   DSCBPER = DS4DEVDT;
                   END;
         END;
