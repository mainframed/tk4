         ISEQ  73,80
TGSSADB  START 0                                                    TGS
********************************************************************TGS
*              THIS RTNE IS USED TO DYNAMICALLY OBTAIN A VARIABLE  *TGS
*              DATA AREA (VDA).  THE FOLLOWING CONTROL FIELDS WILL *TGS
*              ALWAYS BE PLACED AT THE BEGINNING OF A VDA.         *TGS
*                  WORD 1 - POINTER TO THE NEXT VDA IN THE CHAIN.  *TGS
*                           IF THIS IS THE LAST VDA IN THE CHAIN,  *TGS
*                           WILL POINT TO VDALINK IN THE PRV.      *TGS
*                  WORD 2 - POINTER TO THE PREVIOUS VDA IN THE     *TGS
*                           CHAIN.  IF THIS IS THE FIRST VDA IN THE*TGS
*                           CHAIN, WILL POINT TO VDALINK IN THE PRV*TGS
*                  WORD 3 - HALFWORD 1 - RESERVED.                 *TGS
*                           HALFWORD 2 - CONTAINS THE LENGTH OF    *TGS
*                                        THIS VDA.                 *TGS
*                                                                  *TGS
*              TGSSADB EXPECTS REG0 TO CONTAIN LENGTH OF VDA YOU   *TGS
*              DESIRE, REG11 TO CONTAIN ADDR OF PRV, REG13 TO POINT*TGS
*              AT YOUR DSA/WKA, REG14 TO CONTAIN RETURN ADDR, AND  *TGS
*              REG15 TO CONTAIN ENTRY POINT ADDR.                  *TGS
*                                                                  *TGS
*              THE ADDR OF THE VDA WILL BE RETURNED TO YOU IN REG1.*TGS
*              BEGINNING WITH WORD 4, THE VDA WILL BE INITIALIZED  *TGS
*              TO ZERO.                                            *TGS
********************************************************************TGS
R0       EQU   0                                                    TGS
R1       EQU   1                                                    TGS
R2       EQU   2                                                    TGS
R3       EQU   3                                                    TGS
R4       EQU   4                                                    TGS
R5       EQU   5                                                    TGS
R6       EQU   6                                                    TGS
R7       EQU   7                                                    TGS
R8       EQU   8                                                    TGS
R9       EQU   9                                                    TGS
RA       EQU   10                                                   TGS
RB       EQU   11                                                   TGS
RC       EQU   12                                                   TGS
RD       EQU   13                                                   TGS
RE       EQU   14                                                   TGS
RF       EQU   15                                                   TGS
*                                                                   TGS
*              ALL OF MY DXD INSTRUCTIONS FOLLOW                    TGS
*                                                                   TGS
VDALINK  DXD   3F                                                   TGS
         STM   R0,RA,20(RD)            SAVE REGS 0 THRU 10          TGS
         BALR  RA,R0                                                TGS
         USING *,RA                                                 TGS
         GETMAIN R,LV=(0)                                           TGS
         L     R0,20(R0,RD)            RESTORE REG0                 TGS
         LA    R2,0(RB,R0)             GET ADDR VDALINK             TGS
         ORG   *-2                                                  TGS
         DC    QL2(VDALINK)                                         TGS
         L     R3,4(R0,R2)             GET POINTER TO LAST VDA      TGS
         ST    R1,4(R0,R2)             RESET LAST VDA POINTER       TGS
         ST    R1,0(R0,R3)             RESET FORWARD VDA CHAIN      TGS
         ST    R2,0(R0,R1)             SET FORWARD PTR IN NEW VDA   TGS
         ST    R3,4(R0,R1)             SET BACKWARD PTR IN NEW VDA  TGS
INITVDA  SR    R3,R3                                                TGS
         ST    R3,8(R0,R1)             INITIALIZE WORD3             TGS
         STH   R0,10(R0,R1)                 OF VDA                  TGS
         MVI   12(R1),X'00'                                         TGS
         LA    R9,12(R0,R1)            POINT REG9 AT VDA+12         TGS
         LA    R8,255                  REG8 EQ 255                  TGS
         LH    R7,10(R0,R1)            REG7 EQ LENGTH VDA           TGS
         SH    R7,=H'12'                    MINUS 12                TGS
VDALOOP  CH    R7,=H'257'                                           TGS
         BC    4,VDALPEN                                            TGS
         EX    R8,INITMVC                                           TGS
         SH    R7,=H'256'                                           TGS
         LA    R9,256(R0,R9)                                        TGS
         B     VDALOOP                                              TGS
VDALPEN  SH    R7,=H'2'                                             TGS
         LTR   R7,R7                                                TGS
         BC    4,VDADONE                                            TGS
         EX    R7,INITMVC                                           TGS
VDADONE  LM    R2,RA,28(RD)                                         TGS
         BR    RE                                                   TGS
INITMVC  MVC   1(0,R9),0(R9)                                        TGS
         LTORG                                                      TGS
         END
