 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENTPFO */
 DENTPFO:  PROC(STRHD)
   RETURNS(PTR) OPTIONS(REENTRANT);
 /**********************************************************************
 *                                                                     *
 * CALLED TO CONVERT THE PANEL DESCRIPTION STORED IN THE SINGLY-LINKED *
 * LIST OF ELEMENTS (SEE STRUCTURE STREL) INTO THE CONTROL BLOCKS      *
 * DEFINED BY STRUCTURE PNLEL.  IN THE PROCESS, THE STREL ELEMENTS     *
 * WILL BE FREED.                                                      *
 *                                                                     *
 **********************************************************************/
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   DCL NULL BUILTIN;
 HALT;
   % INCLUDE DENTPFD;
   DCL (STRHD,STRTMP) PTR;
   T=0;
   DO STRPTR=STRHD REPEAT STRNXT WHILE(STRPTR^=NULL);
     T=T+LENGTH(STR);
     END;
   L=T/32767;
   M=MOD(T,32767);
   IF L=0
     THEN DO;
       ALLOCATE PNLELA SET(PNLELPT);
       ELCNT=0;
       END;
     ELSE ALLOCATE PNLEL SET(PNLELPT);
   M=0; /* # BYTES COPIED FROM CURRENT STR */
   STRPTR=STRHD;
   DO I = 1 TO ELCNT;
     L=0; /* # BYTES STORED IN CURRENT ELARY ELEMENT */
     DO WHILE(L<32767);
       N=MIN(32767-L,LENGTH(STR)-M);
       SUBSTR(ELARY(I),L+1,N)=SUBSTR(STR,M+1,N);
       L=L+N;
       M=M+N;
       IF M=LENGTH(STR)
         THEN DO;
           STRTMP=STRNXT;
           FREE STREL;
           STRPTR=STRTMP;
           M=0;
           END;
       END;
     END;
   /* NOW ALL ELEMENTS OF ELARY ARE COMPLETELY FILLED */
   L=0; /* # BYTES STORED IN ELLST OR ELLSTA */
   DO WHILE(L<ELLEN);
     N=MIN(ELLEN-L,LENGTH(STR)-M);
     IF ELCNT=0
       THEN SUBSTR(ELLSTA,L+1,N)=SUBSTR(STR,M+1,N);
       ELSE SUBSTR(ELLST,L+1,N)=SUBSTR(STR,M+1,N);
     L=L+N;
     M=M+N;
     IF M=LENGTH(STR)
       THEN DO;
         STRTMP=STRNXT;
         FREE STREL;
         STRPTR=STRTMP;
         M=0;
         END;
     END;
   RETURN(PNLELPT);
   END DENTPFO;
