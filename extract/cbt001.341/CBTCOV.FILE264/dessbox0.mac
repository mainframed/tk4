         MACRO
&NAME    DESSBOX0 &SX,&SY              GENERATE SBOX QUADRANT
.*                                     WRITTEN 07/27/83 BY PAUL DIXON
.*                                     INTERNAL MACRO FOR DESSBOX
         LCLA  &I,&J
         LCLC  &C(8),&N,&XX(64),&YY(64)
&N       SETC  '&NAME'
&I       SETA  64                      &I: 64
.LOOP1   ANOP ,                        RECODE NUMBERS INTO HEX
&J       SETA  &SX(&I)+1               &J: &SX(&I)+1
&XX(&I)  SETC  '0123456789ABCDEF'(&J,1) CONVERT TO HEX
&J       SETA  &SY(&I)+1               &J: &SY(&I)+1
&YY(&I)  SETC  '0123456789ABCDEF'(&J,1) CONVERT TO HEX
&I       SETA  &I-1                    DECREMENT &I
         AIF   (&I GT 0).LOOP1         AND CONTINUE
&I       SETA  0                       &I: 0
.LOOP2   ANOP  ,                       GENERATE ONE ROW OF TABLE
&J       SETA  (&I*8)+(&I/2*16)        GET NEW OFFSET (0,8,32,40)
&C(1)    SETC  '&XX(&J+01)&YY(&J+01)&XX(&J+17)&YY(&J+17)'
&C(2)    SETC  '&XX(&J+02)&YY(&J+02)&XX(&J+18)&YY(&J+18)'
&C(3)    SETC  '&XX(&J+03)&YY(&J+03)&XX(&J+19)&YY(&J+19)'
&C(4)    SETC  '&XX(&J+04)&YY(&J+04)&XX(&J+20)&YY(&J+20)'
&C(5)    SETC  '&XX(&J+05)&YY(&J+05)&XX(&J+21)&YY(&J+21)'
&C(6)    SETC  '&XX(&J+06)&YY(&J+06)&XX(&J+22)&YY(&J+22)'
&C(7)    SETC  '&XX(&J+07)&YY(&J+07)&XX(&J+23)&YY(&J+23)'
&C(8)    SETC  '&XX(&J+08)&YY(&J+08)&XX(&J+24)&YY(&J+24)'
&N       DC    X'&C(1)&C(2)&C(3)&C(4)&C(5)&C(6)&C(7)&C(8)'
&N       SETC  ' '                     CLEAR NAME FIELD AFTER 1ST DC
&I       SETA  &I+1                    INCREMENT &I
         AIF   (&I LT 4).LOOP2         AND CONTINUE
         MEND
