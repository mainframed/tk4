PUTLOD   CSECT                                                      TGS
********************************************************************TGS
*                  ON ENTRY - R0 = LENGTH OF RECORD OR ZERO IF EOD *TGS
*                             R1 = ADDR OF RECORD                  *TGS
*                             R2 = ADDR OF DATA BLOCK CONTAINING   *TGS
*                                  A DCB AND OTHER VARIABLE DATA   *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         LR    R3,R0                   R3 = RECORD LENGTH           TGS
         LR    R4,R1                   R4 = RECORD ADDR             TGS
         USING DATABLK,R2                                           TGS
         LA    R5,PDSDIR                                            TGS
         USING PDSDIRD,R5                                           TGS
         TM    INITSW,X'80'                                         TGS
         BC    1,BEGIN                                              TGS
         OI    INITSW,X'80'                                         TGS
         MVC   PDSDCB+40(8),PDSNAME    MOVE DDNAME TO DCB           TGS
         LA    R1,NMDCBEX                                           TGS
         ST    R1,NMEXITS+4                                         TGS
         MVI   NMEXITS+4,X'85'                                      TGS
         LA    R1,PDSDCB                                            TGS
         ST    R1,OLIST                                             TGS
         MVI   OLIST,X'8F'                                          TGS
         CNOP  0,4                                                  TGS
         BAL   1,*+8                                                TGS
OLIST    DS    F                                                    TGS
         SVC   19                      OPEN DCB                     TGS
         TM    PDSDCB+48,X'10'         WAS IT GOOD OPEN             TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1111,DUMP                                            TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,PDSDCB+62            R0 = BLKSIZE                 TGS
         STH   R0,BUFLEN                                            TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,BUFADDR                                           TGS
STOWCK   EQU   *                                                    TGS
         TM    STOWSW,X'80'            IS A STOW PENDING            TGS
         BC    8,STOWINIT              BR IF NOT                    TGS
         STOW  PDSDCB,PDSDIR,A                                      TGS
         LTR   RF,RF                   WAS IT GOOD STOW             TGS
         BC    8,STOWINIT              BR IF YES                    TGS
         ABEND 2,DUMP                                               TGS
         CH    RF,=H'4'                                             TGS
         BC    8,STOWINIT                                           TGS
STOWINIT EQU   *                                                    TGS
         TM    EODSW,X'80'             IS IT EOD                    TGS
         BC    1,CLOSEUP               BR IF YES                    TGS
         OI    STOWSW,X'80'                                         TGS
         MVC   PDSDIR,4(R4)                                         TGS
         NI    TTRSW,X'00'                                          TGS
         B     RETCTL                                               TGS
CLOSEUP  EQU   *                                                    TGS
         LA    R1,PDSDCB                                            TGS
         ST    R1,CLIST                                             TGS
         MVI   CLIST,X'80'                                          TGS
         CNOP  0,4                                                  TGS
         BAL   1,*+8                                                TGS
CLIST    DS    F                                                    TGS
         SVC   20                      ISSUE CLOSE SVC              TGS
         B     RETCTL                                               TGS
BEGIN    EQU   *                                                    TGS
         LTR   R3,R3                   IS IT EOD                    TGS
         BC    7,NOTEOD                BR IF NOT                    TGS
         OI    EODSW,X'80'                                          TGS
         B     STOWCK                                               TGS
NOTEOD   EQU   *                                                    TGS
         TM    0(R4),X'C0'             IS IT DIR ENTRY AND ALIAS    TGS
         BC    1,ALIAS                 BR IF ALIAS                  TGS
         BC    4,STOWCK                BR IF DIR ENTRY              TGS
         TM    0(R4),X'20'             IS IT NOTE LIST              TGS
         BC    8,JUSTREC               BR IF NOT                    TGS
         OI    NOTESW,X'80'                                         TGS
         STH   R3,NOTELEN                                           TGS
         LR    R0,R3                   R0 = LENGTH OF NOTE LIST     TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,NOTEADDR                                          TGS
         SR    R6,R6                                                TGS
         IC    R6,PNOTCNT              R6 = # TTRS IN NOTE LIST     TGS
         MVC   0(4,R1),0(R4)                                        TGS
INOTLP   EQU   *                                                    TGS
         LA    R1,4(R1)                                             TGS
         LA    R4,4(R4)                                             TGS
         MVC   0(3,R1),0(R4)           MOVE TTR TO SAVE AREA        TGS
         MVI   3(R1),X'80'             SET INIT SW                  TGS
         BCT   R6,INOTLP                                            TGS
         B     RETCTL                                               TGS
ALIAS    EQU   *                                                    TGS
         LA    R6,0(R3,R4)                                          TGS
         SH    R6,=H'16'               R6 = ADDR REAL MEMB NAME     TGS
         MVC   BLDLAREA(4),=X'00010040' ELL BLDL 1 ENTRY OF 64 BYTESTGS
         MVC   BLDLAREA+4(8),0(R6)     MOVE MEM NME TO BLDL LIST    TGS
         BLDL  PDSDCB,BLDLAREA                                      TGS
         MVC   4+PTTR-PNAME(3,R4),BLDLAREA+12                       TGS
         MVC   4+PTXTTTR-PNAME(3,R4),BLDLAREA+18                    TGS
         MVC   4+PNOTTTR-PNAME(3,R4),BLDLAREA+22                    TGS
         B     STOWCK                                               TGS
JUSTREC  EQU   *                                                    TGS
         TM    NOTESW,X'80'            IS THERE NOTE LIST TO PROCESSTGS
         BC    8,NONOTE                BR IF NOT                    TGS
         L     R6,NOTEADDR                                          TGS
         CLC   1(3,R4),1(R6)           IS THIS NOTE LIST REC        TGS
         BC    7,NONOTE                BR IF NOT                    TGS
         LH    RA,NOTELEN                                           TGS
         SH    RA,=H'4'                RA = LEN TO WRITE            TGS
         LA    R6,4(R6)                R6 = ADDR TO WRITE           TGS
         WRITE DECB,SF,,(6),(10),MF=E                               TGS
         CHECK DECB                                                 TGS
         LH    R0,NOTELEN                                           TGS
         L     R1,NOTEADDR                                          TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         NI    NOTESW,X'7F'                                         TGS
         NOTE  PDSDCB                                               TGS
         ST    R1,WORKWD                                            TGS
         MVC   PNOTTTR,WORKWD          MOVE NOTE LIST TTR TO DIR. ENTGS
         OI    TTRSW,X'80'                                          TGS
         B     RETCTL                                               TGS
NONOTE   EQU   *                                                    TGS
         LA    R6,4(R4)                R6 = ADDR TO WRITE           TGS
         SH    R3,=H'4'                R3 = LEN TO WRITE            TGS
         WRITE DECB,SF,,(6),(3),MF=E                                TGS
         CHECK DECB                                                 TGS
         NOTE  PDSDCB                                               TGS
         ST    R1,WORKWD                                            TGS
         TM    NOTESW,X'80'            IS NOTE LIST TO PROCESS      TGS
         BC    8,CKSCTR                BR IF NOT                    TGS
         SR    R7,R7                                                TGS
         IC    R7,PNOTCNT              R7 = # TTRS IN NOTE LIST     TGS
         L     R6,NOTEADDR                                          TGS
NOTELP   EQU   *                                                    TGS
         LA    R6,4(R6)                                             TGS
         TM    3(R6),X'80'             IS THIS TTR ALREADY RESET    TGS
         BC    8,NEWTTR                BR IF YES                    TGS
         CLC   0(3,R6),1(R4)           DO TTR'S MATCH               TGS
         BC    7,NEWTTR                BR IF NOT                    TGS
         MVC   0(4,R6),WORKWD          RESET TTR AND SWITCH         TGS
NEWTTR   EQU   *                                                    TGS
         BCT   R7,NOTELP                                            TGS
         B     CKTXT                                                TGS
CKSCTR   EQU   *                                                    TGS
         TM    TTRSW,X'80'             HAS SCTR/TRANS REC BEEN FOUNDTGS
         BC    1,CKTXT                 BR IF YES                    TGS
         CLC   PNOTTTR,1(R4)           IS IT SCTR/TRANS RECORD      TGS
         BC    7,CKTXT                 BR IF NOT                    TGS
         MVC   PNOTTTR,WORKWD          RESET TTR                    TGS
         NI    TTRSW,X'7F'             TURN OFF SWITCH              TGS
CKTXT    EQU   *                                                    TGS
         TM    TTRSW,X'40'             HAS 1ST TXT REC BEEN FOUND   TGS
         BC    1,RETCTL                BR IF YES                    TGS
         CLC   PTXTTTR,1(R4)           IS IT 1ST TXT REC            TGS
         BC    7,RETCTL                BR IF NOT                    TGS
         MVC   PTXTTTR,WORKWD          REST TTR                     TGS
         NI    TTRSW,X'BF'             TURN OFF SWITCH              TGS
         B     RETCTL                                               TGS
NMDCBEX  EQU   *                                                    TGS
         LH    R9,PDSDCB+62            R9 = BLKSIZE                 TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BCR   7,RE                    BR IF NOT                    TGS
         MVC   PDSDCB+62(2),=H'7294'   SET DEFAULT BLKSIZE          TGS
         BR    RE                                                   TGS
BLDLAREA DS    CL80                                                 TGS
WORKWD   DS    F                                                    TGS
         LTORG                                                      TGS
DATABLK  DSECT                                                      TGS
         PDSLODBK                                                   TGS
PDSDIRD  DSECT                                                      TGS
PNAME    DS    CL8 *         MEMBER NAME                            TGS
PTTR     DS    CL3 *         TTR OF MODULE                          TGS
PAFLAG   DS    CL1 *         ALIAS INDICATOR AND MISC FLAGS         TGS
PTXTTTR  DS    CL3 *         TTR OF FIRST TXT RECORD                TGS
         DS    CL1 *         X'00'                                  TGS
PNOTTTR  DS    CL3 *         TTR OF NOTE LIST                       TGS
PNOTCNT  DS    CL1 *         NO ENTRIES IN NOTE LIST                TGS
PMODATR  DS    CL2 *         MODULE ATTRIBUTES                      TGS
PMODLEN  DS    CL3 *         TOTAL CONTIGUOUS MAIN STORAGE REQ'D    TGS
PTXT1LEN DS    CL2 *         LEN OF FIRST TXT RECORD                TGS
PEPADDR  DS    CL3 *         MODULE ENTRY POINT ADDR                TGS
PTXT1ORG DS    CL3 *         ORIGIN OF 1ST TXT RECORD               TGS
*                            REST OF RECORD IS VARIABLE             TGS
         END
