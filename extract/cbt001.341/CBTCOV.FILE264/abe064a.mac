 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABE064A - GPRINT */
   %INCLUDE ABESUB;
 ABESUB(ABE064A) RENT(NO)
   PARMS(RHPTR,RHHDRPT,DEST,PTYPE);
   %INCLUDE ABESUBA;
   DCL 1 DDI01 BASED(RHPLOT79),
   %INCLUDE ABE063I;

   DCL (DEST,PTYPE) CHAR(*) VAR;

   DCL ABE064E ENTRY(PTR,,);
   DCL ABE064H ENTRY;
   DCL SETCAPS ENTRY;
   DCL TROUT ENTRY(CHAR(1)) RETURNS(CHAR(2));
   DCL HELPSTR CHAR(4) VAR STATIC INIT('');

   IF DEST=''
     THEN DO;
       MSGDATA='YOU MUST SPECIFY A DESTINATION FOR GR PRINT';
       RHRC=8;
       RETURN;
       END;
   CALL SETCAPS(DEST);
   IF DEST='PIC'
     THEN CALL DO_PIC;
     ELSE DO;
       IF PTYPE=''
         THEN PTYPE='CITOH';
       CALL SETCAPS(PTYPE);
       IF PTYPE='CI600' × PTYPE='CI3500'
         THEN PTYPE='CITOH';
       IF PTYPE^='CITOH' & PTYPE^='IGP' & PTYPE^='PRINTRONIX'
         & PTYPE^='3800'
         THEN DO;
           MSGDATA='INVALID PRINTER TYPE='××PTYPE;
           RHRC=8;
           RETURN;
           END;
       IF DEST^='PREALC'
         THEN CALL #DOCMD(
         'ALLOC FI(ABEPRT) SYSOUT(A) REUSE LR(133) BL(137) RECFM(V A)',
         HELPSTR);
       OPEN FILE(RH_SEQ_2) TITLE('ABEPRT') RECORD OUTPUT;
       SELECT(PTYPE);
         WHEN('CITOH') CALL ABE064E(RHPLOT79,RH_SEQ_2,#PIXGET);
         WHEN('IGP','PRINTRONIX') CALL PUTOUT_IGP;
         WHEN('3800') CALL ABE064H(RHPTR,RHHDRPT,RHPLOT79,RH_SEQ_2);
         OTHERWISE;
         END; /* SELECT */
       CLOSE FILE(RH_SEQ_2);
       IF DEST^='PREALC'
         THEN CALL #DOCMD('FREE FI(ABEPRT) DEST('××DEST××')',HELPSTR);
       END;

 DO_PIC: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE CURRENT SUBCOMMAND IS PIC, WHICH REQUESTS THAT THE  *
 * BITMAP BE CONVERTED INTO A DATA STREAM SUITABLE FOR TRANSMISSION TO *
 * THE CITOH PRINTER IN PICTURE MODE.                                  *
 *                                                                     *
 **********************************************************************/
   DCL (I,J,K,L,M,N,R) FIXED BIN;
   DCL CHARSTR CHAR(8);
   DCL BITSTR BIT(8),
       BITDEF CHAR(1) BASED(ADDR(BITSTR));
   DCL RPTLINE CHAR(254) VAR;
   DCL CH CHAR(1);
   DCL (NEWRH,SAVRH) PTR;


   /* FIRST FIND HOW MUCH TO TRIM FROM THE TOP */
   DO I=MAXBTY TO 1 BY -1;
     DO J=1 TO MAXBTX WHILE(#PIXGET(J,I,RHPLOT79)=' ');
       END;
     IF J<=MAXBTX
       THEN LEAVE;
     END;
   IF I=0
     THEN DO;
       MSGDATA='ERROR - NO NON-ZERO BITS IN BITMAP';
       RHRC=4;
       RETURN;
       END;
   /* NOW FIND HOW MUCH TO TRIM FROM THE RIGHT */
   DO J=MAXBTX TO 1 BY -1;
     DO K=1 TO I WHILE(#PIXGET(J,K,RHPLOT79)=' ');
       END;
     IF K<=I
       THEN LEAVE;
     END;
   /* I=TOPMOST Y INDEX, J=RIGHTMOST X INDEX */
   SAVRH=RHPTR;
   R=#CMD('ABE '''' RECSIZE(80)',RHPTR,RHHDRPT); /* VIRTUAL DATA SET */
   NEWRH=RHPTR;
   RHPTR=SAVRH;
   R=#CMD('NL ^PY^-',NEWRH,RHHDRPT);
   R=#CMD('NL ^F^-',NEWRH,RHHDRPT);
   DO K=I REPEAT K-7 WHILE(K>0);
     RPTLINE='NL ^M0101000^Q';
     DO L=1 TO J;
       CHARSTR='00000000';
       DO M=0 TO MIN(6,K-1);
         CH=#PIXGET(L,K-M,RHPLOT79);
         IF CH^=''
           THEN CH='1';
           ELSE CH='0';
         SUBSTR(CHARSTR,M+2,1)=CH;
         END;
       BITSTR=CHARSTR;
       RPTLINE=RPTLINE××TROUT(BITDEF);
       IF LENGTH(RPTLINE)>72
         THEN DO;
           RPTLINE=RPTLINE××'^X';
           R=#CMD(RPTLINE,NEWRH,RHHDRPT);
           RPTLINE='NL ^A';
           END;
       END;
     RPTLINE=RPTLINE××'^G^-';
     R=#CMD(RPTLINE,NEWRH,RHHDRPT);
     END;
   R=#CMD('NL ^PN^-',NEWRH,RHHDRPT);
   R=#CMD('TOP',NEWRH,RHHDRPT);
   R=#CMD('LINES 99999',NEWRH,RHHDRPT);
   END DO_PIC;

 PUTOUT_IGP: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN IT IS TIME TO GENERATE OUTPUT FOR A PRINTRONIX.         *
 *                                                                     *
 **********************************************************************/
   DCL CHARSTR CHAR(8);
   DCL BITSTR BIT(8),
       BITDEF CHAR(1) BASED(ADDR(BITSTR));
   DCL RPTLINE CHAR(254) VAR;
   DCL (H,I,J,K,L,M,N,PPC,R) FIXED BIN;
   DCL SKIP CHAR(10) VAR;
   DCL CH CHAR(1);

   IF PLXDPI=120
     THEN PPC=6; /* BITMAP POINTS PER PRINT CHARACTER */
     ELSE PPC=3;
   N=PPC*2; /* BITMAP POINTS PER HORIZONTAL PRINT POSITION */
   SKIP=' ';
   DO H=0 REPEAT H+STRIPLEN*PPC WHILE(H<HBOUND(BITMAP,2));
     RPTLINE='1';
     DO J=HBOUND(BITMAP,1) TO 1 BY -1;
       DO M=3 REPEAT M+3 WHILE(M<=PPC);
         IF M=3 & PPC=6
           THEN RPTLINE=RPTLINE××'^'××'^'××'04';
           ELSE DO;
             IF M=6
               THEN DO;
                 RPTLINE=RPTLINE××'0A';
                 WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
                 RPTLINE=SKIP;
                 END;
             RPTLINE=RPTLINE××'^'××'^'××'05';
             END;
         DO I=1 TO STRIPLEN/2;
           L=H+(I-1)*N;
           IF L>=HBOUND(BITMAP,2)
             THEN LEAVE;
           DO K=1 TO 6;
             IF PPC=3
               THEN R=K;
               ELSE IF M=3
                 THEN R=(K-1)*2+1;
                 ELSE R=K*2;
             CH=#PIXGET(J,H+(I-1)*N+R,RHPLOT79);
             IF ^(CH='0' × CH=' ')
               THEN CH='1';
               ELSE CH='0';
             SUBSTR(CHARSTR,8+(1-K),1)=CH;
             END;
           IF SUBSTR(CHARSTR,3,1)='0'
             THEN SUBSTR(CHARSTR,2,1)='1';
             ELSE SUBSTR(CHARSTR,2,1)='0';
           SUBSTR(CHARSTR,1,1)='0';
           BITSTR=CHARSTR;
           RPTLINE=RPTLINE××TROUT(BITDEF);
           END;
         END;
       RPTLINE=RPTLINE××'$';
       WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
       RPTLINE=SKIP;
       END;
     END;
   END PUTOUT_IGP;

   END ABE064A;
