 /* ROMANS
*$.HE GENERALIZED SUBROUTINES - ROMAN
*$.HE TRANSLATE TO ROMAN NUMERALS
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  DON MOEHRKE
*$SOURCE MODULE NAME:  ROMANS
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE WILL TRANSLATE A BINARY NUMBER INTO THE
*$EQUIVALENT ROMAN NUMERALS WHICH ARE RETURNED TO THE CALLER
*$AS A VARYING CHARACTER STRING.
*$FOR EXAMPLE, IF THIS SUBROUTINE WERE PASSED A BINARY
*$NUMBER EQUAL TO 1666, IT WOULD RETURN A VARYING CHARACTER
*$STRING EQUAL TO 'MDCLXVI'.
*$THE FOLLOWING STATEMENT MUST BE INCLUDED IN THE CALLING
*$PROGRAM -
*$DCL ROMAN ENTRY (FIXED BIN) RETURNS (CHAR(15) VARYING);
  */
 ROMAN: PROC(M) RETURNS(CHAR(15) VARYING);
         DCL (ROMA CHAR(15) VARYING,
              RN(13) CHAR(2) VARYING INITIAL
         ( 'M','CM','D','CD','C','XC','L','XL','X','IX','V','IV','I'),
              NTEST(13) INITIAL
         (1000, 900,500, 400,100,  90, 50,  40, 10,   9,  5,   4,  1 ))
              STATIC;
         DCL (N STATIC INTERNAL, M) FIXED BIN;
         DCL I FIXED BIN STATIC INTERNAL;
         N = M;
         ROMA = '';
         DO I = 1 TO 13;
              DO WHILE (N >= NTEST(I));
                   ROMA = ROMA ×× RN(I);
                   N = N-NTEST(I);
                   END;
              END;
         RETURN (ROMA);
         END ROMAN;
