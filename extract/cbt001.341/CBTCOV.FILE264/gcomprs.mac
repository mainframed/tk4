         MACRO                                                      TGS
         TRTAB
         LCLA  &A                                                   TGS
&A       SETA  0                                                    TGS
.NXT     ANOP                                                       TGS
         DC    AL1(&A)                                              TGS
&A       SETA  &A+1                                                 TGS
         AIF   (&A LT 256).NXT                                      TGS
         MEND
GCOMPR   START 0                                                    TGS
********************************************************************TGS
*                  ON ENTRY, REG1 WILL CONTAIN THE ADDR OF DATA    *TGS
*              ALIGNED ON A WORD AND HAVING THE FOLLOWING FORMAT   *TGS
*                                                                  *TGS
*                  BYTES 0 THRU 1 = TO BE FILLED IN BY THIS RTNE   *TGS
*                       WITH THE COMPRESSED STRING LENGTH          *TGS
*                       NOTE THAT THE LENGTH STORED WILL INCLUDE   *TGS
*                       THE 4-BYTE HEADER WORD CONTAINING THE      *TGS
*                       COMPRESSED AND UNCOMPRESSED LENGTHS        *TGS
*                  BYTES 2 THRU 3 = LENGTH OF DATA TO BE COMPRESSED*TGS
*                  BYTES 4 THRU 7 = ADDR OF STRING TO BE COMPRESSED*TGS
*                  BYTES 4 THRU N = TO BE FILLED IN BY THIS RTNE   *TGS
*                       WITH THE COMPRESSED STRING.  NOTE THAT AT  *TGS
*                       LEAST ENOUGH BYTES TO CONTAIN THE          *TGS
*                       UNCOMPRESSED STRING + L/256 + 1 SHOULD BE  *TGS
*                       PROVIDED, WHERE L IS THE LENGTH OF THE     *TGS
*                       UNCOMPRESSED STRING.                       *TGS
*                                                                  *TGS
*                                                                  *TGS
*                                                                  *TGS
*                                                                  *TGS
*                                                                  *TGS
********************************************************************TGS
         TGSREQU                                                    TGS
         USING *,RF                                                 TGS
         STM   RE,RC,12(RD)                                         TGS
********************************************************************TGS
*                            REGISTER  USAGE                       *TGS
*              *  - END FLAG - THIS IS SET TO 1 TO INDICATE THAT A *TGS
*                   REPEATING STRING OF TWO CHARARTERS ONLY        *TGS
*                   TERMINATE THE ENTIRE STRING, EXCEPT WHEN THE   *TGS
*                   TWO-CHARACTER REPEATING STRING IS AT THE END   *TGS
*                   OF A NON-REPEATING STRING THAT IS GT 252 BYTES *TGS
*                   IN LENGTH.                                     *TGS
*              1  - TRT INSTR                                      *TGS
*              2  - TRT INSTR                                      *TGS
*        RZ    3  - ADDR OF NXT BYTE IN STR TO COMPRESS            *TGS
*        RW    4  - WORK REG                                       *TGS
*        RV    5  - 1 - CONSTANT                                   *TGS
*        RT    11 - ADDR IN COMPRESSED STR OF BYTE TO CONTAIN THE  *TGS
*                   NEXT NON-REPEAT COUNT CALCULATED               *TGS
*        RS    7  - 252 - CONSTANT                                 *TGS
*        RR    8  - ADDR OF 1ST BYTE BEYOND STR TO COMPRESS        *TGS
*        RQ    9  - 256 - CONSTANT                                 *TGS
*        RY    10 - LENGTH FOR TRT INSTRUCTION                     *TGS
*        RX    6  - ADDR IN COMPRESSED STRING OF THE LAST CHAR      TGS
*                   ADDED                                          *TGS
*        RU    12 - CHAR CURRENTLY BEING CHECKED FOR REPEAT        *TGS
*              13 - ADDR OF REG SAVE AREA PROVIDED BY CALLER       *TGS
*        RP    14 - ORIGINAL CONTENTS OF REG1                      *TGS
*              15 - BASEREG                                        *TGS
*                                                                  *TGS
*                                                                  *TGS
*                                                                  *TGS
********************************************************************TGS
         MVI   TRTAB,X'FF'                                          TGS
         SR    R0,R0                                                TGS
         LR    RE,R1                   SAVE R1 IN RE                TGS
         L     R3,4(R1)                R3 = ADDR STR TO COMPRESS    TGS
         LR    R8,R3                                                TGS
         AH    R8,2(R1)                R8 = END ADDR STR TO COMPRESSTGS
         SR    R2,R2                                                TGS
         IC    R2,0(R3)                INITIALIZE R2                TGS
         LA    R3,1(R3)                                             TGS
         SR    RC,RC                   INITIALIZE RC                TGS
         MVC   4(3,R1),=X'000300'      SET 1ST 3-BYTE CTL TAB IN COMTGS
         LA    RB,6(R1)                INITIALIZE RB                TGS
         LR    R6,RB                   INITIALIZE R6                TGS
         LA    R9,254                  INITIALIZE R9                TGS
         LA    R7,252                  INITIALIZE R7                TGS
         LA    R5,1                    INITIALIZE R5                TGS
         B     RESET                                                TGS
RETRY    EQU   *                                                    TGS
         LNR   RA,R3                   COMPLEMENT ADDR NXT BYTE TO  TGS
         BXLE  RA,R8,GOTY              RA = LENGTH REMAINING TO     TGS
*                                           COMPRESS AND BR IF THIS TGS
*                                           LENGTH NOT GT 254       TGS
         LA    RA,253                  RA = 253                     TGS
         B     GOAGINA                                              TGS
GOTY     EQU   *                                                    TGS
         LTR   RA,RA                                                TGS
         BC    2,GOAGIN                BR IF RA GT 0                TGS
         BC    8,DOCALC                BR IF RA = 0                 TGS
         LTR   R0,R0                   IS END SWITCH ON             TGS
         BC    8,NOCALC                BR IF NOT                    TGS
DOCALC   EQU   *                                                    TGS
         LA    R4,1(R6)                                             TGS
         SR    R6,RB                                                TGS
         STC   R6,0(RB)                SET LAST NON REP CNT         TGS
         B     ENDLEN                                               TGS
NOCALC   EQU   *                                                    TGS
         LR    R4,R6                                                TGS
         SH    R6,=H'3'                                             TGS
         CLC   0(3,R6),=XL3'00'                                     TGS
         BC    8,NOCALC                                             TGS
ENDLEN   EQU   *                                                    TGS
         SR    R4,RE                   R4 = LENGTH OF COMPRESSED STRTGS
         STH   R4,0(RE)                SET LENGTH IN PARM LIST      TGS
         STC   RC,TRTAB(RC)                                         TGS
         LM    RE,RC,12(RD)            RESTORE CALLERS REGS         TGS
         BR    RE                                                   TGS
GOAGIN   EQU   *                                                    TGS
         BCTR  RA,R0                                                TGS
GOAGINA  EQU   *                                                    TGS
         EX    RA,TRT                                               TGS
         BC    7,NOTEXH                BR IF STR NOT EXHAUSTED      TGS
         LA    R1,1(RA,R3)             DUMMY UP R1                  TGS
         IC    R2,0(R1)                DUMMY UP R2                  TGS
NOTEXH   EQU   *                                                    TGS
         C     R2,=F'255'                                           TGS
         BC    7,NOT255                                             TGS
         CLI   0(R1),X'FF'                                          TGS
         BC    8,NOT255                                             TGS
         SR    R2,R2                                                TGS
NOT255   EQU   *                                                    TGS
         SR    R0,R0                   RESET END SWITCH             TGS
         LNR   R4,R3                   COMPLEMENT OLD ADDR STR TO COMPR
         LA    R3,1(R1)                R3 = NEW ADDR IN STR TO COMPRESS
         BXH   R1,R4,REPEAT            R1 = REPEAT CNT - 1 AND BR IFTGS
*                                           REPEAT CNT IS GT 1      TGS
*                                      IF WE FALL THRU HERE R1 = 0 OR 1
         BXLE  R1,R5,GOTONE            BR IF R1 WAS 0               TGS
         STC   RC,1(R6)                                             TGS
         LA    R6,1(R6)                                             TGS
         LA    R0,1                    SET END SWITCH               TGS
GOTONE   EQU   *                                                    TGS
         LNR   R4,RB                   COMPLEMENT BEGIN ADDR NONREP TGS
         BXLE  R4,R6,RESET             R4 = CURRENT NON-REPEAT CNT  TGS
*                                           AND BR IF THE CNT LT 253TGS
         STC   R4,0(RB)                STORE NON-REPEAT CNT         TGS
         XC    1(3,R6),1(R6)           CLEAR NEW 3-BYTE CTL BLOCK   TGS
         LA    R6,3(R6)                RESET R6                     TGS
         LR    RB,R6                   RESET RB                     TGS
         SR    R0,R0                   RESET END SWITCH             TGS
RESET    EQU   *                                                    TGS
         STC   R2,1(R6)                STORE NON-REPEAT CHAR        TGS
         LA    R6,1(R6)                BUMP ADDR OF COMPR STR       TGS
         STC   RC,TRTAB(RC)            RESET TRTAB                  TGS
         LTR   RC,RC                                                TGS
         BC    7,NOTZERO                                            TGS
         MVI   TRTAB,X'FF'                                          TGS
NOTZERO  EQU   *                                                    TGS
         LR    RC,R2                   RESET RU                     TGS
         LA    R1,TRTAB(RC)                                         TGS
         NI    0(R1),X'00'                                          TGS
         B     RETRY                                                TGS
REPEAT   EQU   *                                                    TGS
         LA    R1,1(R1)                SET FINAL REPEAT CNT         TGS
         STC   R1,1(R6)                STORE REPEAT CNT             TGS
         NI    2(R6),X'0'              CLEAR NON-REPEAT CNT         TGS
         LR    R4,R6                                                TGS
         SR    R4,RB                                                TGS
         BCT   R4,NROK                 SUBTRACT 1 AND BR IF THE     TGS
*                                           RESULT IS NOT ZERO      TGS
         SH    R6,=H'3'                                             TGS
         CLI   1(R6),X'00'             IS PRIOR REP CNT ZERO        TGS
         BC    7,NOBACK                BR IF NOT                    TGS
         MVC   0(3,R6),3(R6)           BACK UP 3 BYTES              TGS
         B     NROKA                                                TGS
NOBACK   EQU   *                                                    TGS
         LA    R6,3(R6)                                             TGS
NROKA    LA    R6,2(R6)                RESET R6                     TGS
         LR    RB,R6                   RESET RB                     TGS
         B     RESET                                                TGS
NROK     EQU   *                                                    TGS
         STC   R4,0(RB)                STORE NON REPEAT CNT         TGS
         B     NROKA                                                TGS
TRT      DS    0H                                                   TGS
         TRT   0(0,R3),TRTAB                                        TGS
TRTAB    EQU   *                                                    TGS
         TRTAB                                                      TGS
         LTORG                                                      TGS
         END
