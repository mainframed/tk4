*PROCESS CS(60),MACRO;
 /* ABE031 - CONTENT SUBCOMMAND   */
 /* WRITTEN 03/12/82 BY PAUL DIXON */
   %INCLUDE ABESUB;
 ABESUB(ABE031) PARS(0) CHECK(YES);
   %INCLUDE ABESUBA;

 DCL SETCAPS ENTRY;

 DCL B31 CHAR(79), M31 CHAR(9), N31 CHAR(8);
 DCL DISP_LINES FIXED BINARY(15), $RHPTR POINTER;
 DCL (RHP,SAVRHP) PTR;

 RHP=NULL;
 IF PARY(1)^=''
   THEN DO;
     CALL SETCAPS(PARY(1));
     RHP=#RHPLOC(PARY(1),RHHDRPT);
     IF RHP=NULL
       THEN DO;
         SAVRHP=RHPTR;
         I=#CMD('ABE '''' NAME('××PARY(1)××')',RHPTR,RHHDRPT);
         IF I^=0
           THEN SIGNAL ERROR;
         RHP=RHPTR;
         RHPTR=SAVRHP;
         END;
     END;

 IF: /* RETURN "N" OR "Y" BASED ON FLAG */
 PROCEDURE(F31) RETURNS(CHAR(1));
    DCL F31 BIT;
    IF F31 THEN RETURN ('Y'); ELSE RETURN ('N');
 END IF;

 COMPRESS_31: /* COMPRESS EXTRA BLANKS FROM STRING */
 PROCEDURE(P31) RETURNS(CHAR(79));
 DCL P31 CHAR(79), R31 CHAR(80) INIT('');
 DCL I31 FIXED BINARY(15), J31 FIXED BINARY(15) INIT(1);
 DCL C31 CHAR DEF(P31) POS(I31), D31 CHAR DEF(R31) POS(J31);
 DCL W31 BIT INIT('0'B) /* 1 = WAITING FOR NONBLANK */;
 DO I31 = 1 TO 79;
    IF W31 THEN IF C31 ^= ' ' THEN W31 = '0'B;
    IF ^W31 THEN
       DO;
          D31 = C31;
          IF D31 = '=' × D31 = ',' THEN W31 = '1'B;
          J31 = J31 + 1;
       END;
 END /* DO */;
 RETURN (SUBSTR(R31,1,79));
 END COMPRESS_31;

 MYTERM: PROC(PSTR);
 DCL PSTR CHAR(*);
 DCL I FIXED BIN;

 IF RHP=NULL
   THEN CALL #TERM(PSTR);
   ELSE I=#CMD('NL '××PSTR,RHP,RHHDRPT);
 END MYTERM;


 $RHPTR = RHPTR /* SAVE RHPTR */;
 DO RHPTR=NXTRHEL REPEAT RHNXT WHILE(RHPTR^=NULL);
    IF SUBSTR(RHNAME,1,1) = '='
       THEN N31 = '<<NONE>>';
       ELSE N31 = RHNAME;
    IF RHCST = 0
       THEN DISP_LINES = 0;
       ELSE DISP_LINES = RHCEN - RHCST + 1;
    PUT STRING(B31) EDIT
        ('NAME=',N31)                   (A(5),A(8))
        (' SAVED=',IF(^SAVEFLAG))       (A(7),A(1))
        (' CAPS=',IF(^LOFLAG))          (A(6),A(1))
        (' XLATE=',IF(RHTRANSW))        (A(7),A(1))
        (' HEX=',IF(HEXMODE))           (A(5),A(1))
        (' SCROLL=',PFPB)               (A(8),A(2))
        (' LINES=',DISP_LINES)          (A(7),P'Z9')
        (' SEQ=',CURSEQ)                (A(5),P'(7)Z9');
    CALL MYTERM(COMPRESS_31(B31));
    PUT STRING(B31) EDIT
        ('SUPPORT=',IF(RHSUPSW))  (X(14),A(8),A(1))
        (' RENUM=',SEQSTRT)             (A(7),P'(7)Z9')
        (',',SEQINCR)                   (A(1),P'(7)Z9');
    CALL MYTERM(COMPRESS_31(B31));
    PUT STRING(B31) EDIT
        ('MARK=',MARKARY(1))      (X(14),A(5),P'(7)Z9')
        (',',MARKARY(2))                (A(1),P'(7)Z9')
        (',',MARKARY(3))                (A(1),P'(7)Z9')
        (',',MARKARY(4))                (A(1),P'(7)Z9')
        (',',MARKARY(5))                (A(1),P'(7)Z9')
        (',',MARKARY(6))                (A(1),P'(7)Z9');
    CALL MYTERM(COMPRESS_31(B31));
    IF INDDN ^= NULL THEN
       DO;
          IF INDDN->ALCMEM = ''
             THEN B31 = INDDN->ALCDSN;
             ELSE
                DO;
                   M31 = INDDN->ALCMEM;
                   SUBSTR(M31,INDEX(M31,' '),1) = ')';
                   B31 = INDDN->ALCDSN ×× '(' ×× M31;
                END;
          CALL MYTERM('              INDSN=' ×× B31);
          PUT STRING(B31) EDIT
              ('FIXED=',IF(^SUBSTR(INDDN->ALCFLAG,3,1)))
              (X(20),A(6),A(1))
              (' NUM=',IF(^SUBSTR(INDDN->ALCFLAG,6,1)))
              (A(5)    ,A(1))
              (' LRECL=',INDDN->ALCRECL)
              (A(7),P'(4)Z9');
          CALL MYTERM(COMPRESS_31(B31));
       END;
    IF OUTDDN ^= NULL THEN
       DO;
          IF OUTDDN->ALCMEM = ''
             THEN B31 = OUTDDN->ALCDSN;
             ELSE
                DO;
                   M31 = OUTDDN->ALCMEM;
                   SUBSTR(M31,INDEX(M31,' '),1) = ')';
                   B31 = OUTDDN->ALCDSN ×× '(' ×× M31;
                END;
          CALL MYTERM('              OUTDSN=' ×× B31);
          PUT STRING(B31) EDIT
              ('FIXED=',IF(^SUBSTR(OUTDDN->ALCFLAG,3,1)))
              (X(20),A(6),A(1))
              (' NUM=',IF(^SUBSTR(OUTDDN->ALCFLAG,6,1)))
              (A(5)    ,A(1))
              (' LRECL=',OUTDDN->ALCRECL)
              (A(7),P'(4)Z9');
          CALL MYTERM(COMPRESS_31(B31));
       END;
 END;
 IF RHP^=NULL
   THEN I=#CMD('/TOP/LINES 999',RHP,RHHDRPT);
 RHPTR = $RHPTR /* RESTORE RHPTR */;
 RHRC = 0;
 END ABE031;
