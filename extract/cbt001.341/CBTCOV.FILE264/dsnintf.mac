 DSNINTF: PROC(PARM) OPTIONS(MAIN);
 /**********************************************************************
 *                                                                     *
 * DSNINTF IS A PROGRAM WHICH ALLOWS APPLICATION PROGRAMS              *
 * RUNNING UNDER DB2 (DSN COMMAND) TO USE ISPF SERVICES.  DSNINTF      *
 * SHOULD BE INVOKED DIRECTLY FROM ISPF.  DSNINTF WILL SET THE ADDRESS *
 * OF TWO ECBS INTO THE ECT, AND THEN WILL CHANGE THE COMMAND NAME IN  *
 * THE COMMAND BUFFER FROM DSNINTF TO DSN, AND WILL THEN INVOKE THE    *
 * DSN COMMAND.  AN APPLICATION PROGRAM RUNNING UNDER THE DSN COMMAND  *
 * CAN OBTAIN ISPF SERVICES BY INVOKING THE DSNLINK ROUTINE, WHICH     *
 * WILL POST ONE OF THE ECBS IN THE ECT AND WAIT ON THE OTHER FOR THE  *
 * RESULTS.                                                            *
 *                                                                     *
 **********************************************************************/
   DCL PARM CHAR(100) VAR;
   %INCLUDE IKJECT;
   DCL PLIRETC BUILTIN;
   DCL TSOMOCK ENTRY(PTR,PTR,PTR);
   DCL 1 CPPL,
     2 CPPLCBUF PTR,
     2 CPPLUPT PTR,
     2 CPPLPSCB PTR,
     2 CPPLECT PTR;
   DCL 1 CBUF,
      2 BUFLEN FIXED BIN(15,0) INIT(0),
      2 OFFSET FIXED BIN(15,0) INIT(0),
      2 BUFFER CHAR(110) INIT('');
   DCL 1 CMDSTR,
     2 CMDNAME CHAR(8) INIT('DSNINTF'),
     2 SUBTCBP PTR,
     2 SUBECB FIXED BIN(31,0) INIT(0),
     2 STAIECB FIXED BIN(31,0) INIT(0),
     2 STAIECB2 FIXED BIN(31,0) INIT(0),
     2 STAICMPL FIXED BIN(31,0) INIT(0),
     2 STAIPGM CHAR(8) INIT('');
   DCL RETCODE FIXED BIN(31,0) INIT(0);
   DCL (SYSCMD,SYSWAIT) ENTRY;
   DCL (ECBA,ECBB) FIXED BIN(31,0) INIT(0);
   DCL CNT FIXED BIN(15,0) INIT(1);
   DCL USERWORK CHAR(8);
   DCL 1 USERDEF BASED(ADDR(USERWORK)),
     2 PTRA PTR,
     2 PTRB PTR;
   DCL SAVEUSER CHAR(8);
   DCL (PCMD,SCMD,DSNPCMD,DSNSCMD) CHAR(8);

   /* SAVE PCMD,SCMD */
   PCMD=ECTPCMD;
   SCMD=ECTSCMD;

   /* NOW SET UP COMMAND BUFFER */
   BUFFER='DSN ';
   BUFLEN=8;

   /* NOW SET UP CPPL */
   CPPLCBUF=ADDR(CBUF);
   CALL TSOMOCK(CPPLUPT,CPPLPSCB,CPPLECT);

   /* NOW STACK SUBCOMMAND */
   IF PARM^=''
     THEN CALL DOSTK(PARM);

   /* SET UP ECT WITH ADDRESSES OF TWO ECBS */
   PTRA=ADDR(ECBA); /* POSTED BY OTHER */
   PTRB=ADDR(ECBB); /* POSTED BY ME */
   SAVEUSER=ECTUSER;
   ECTUSER=USERWORK;

   /* NOW INVOKE DSN COMMAND */
   CALL SYSCMD(CPPL,RETCODE,'START',ADDR(CMDSTR));
   IF RETCODE^=0
     THEN DO;
       PUT LIST('RETURN CODE FROM SYSCMD=',RETCODE);
       SIGNAL ERROR;
       END;

   /* NOW LOOP WAITING ON ECBS */
   DO FOREVER=1 REPEAT FOREVER;
     CALL SYSWAIT(CNT,ECBA,SUBECB,STAIECB);
     IF SUBSTR(UNSPEC(ECBA),2,1)
       THEN CALL CALL_ISPF;
       ELSE LEAVE;
     END;

   CALL SYSCMD(CPPL,RETCODE,'STOP',ADDR(CMDSTR));
   ECTUSER=SAVEUSER;

   CALL PLIRETC(0);
   RETURN; /* ALL DONE */

 CALL_ISPF: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN ECBA HAS BEEN POSTED BY A COMMAND RUNNING UNDER THIS    *
 * COMMAND.  ECBA SHOULD CONTAIN THE ADDRESS OF A PARAMETER LIST TO    *
 * BE PASSED TO ISPLINK.  UPON RETURN FROM ISPLINK, ECBA WILL BE       *
 * CLEARED AND ECBB WILL BE POSTED WITH THE RETURN CODE FROM ISPLINK.  *
 *                                                                     *
 **********************************************************************/
   DCL DSNINTA ENTRY EXTERNAL OPTIONS(ASM,INTER,RETCODE);
   DCL SYSPOST ENTRY(FIXED BIN(31,0),FIXED BIN(31,0));
   DCL PLIRETV BUILTIN;

   DSNPCMD=ECTPCMD;
   DSNSCMD=ECTSCMD;
   ECTPCMD=PCMD;
   ECTSCMD=SCMD;
   FETCH DSNINTA;
   CALL DSNINTA(ECBA);
   ECTPCMD=DSNPCMD;
   ECTSCMD=DSNSCMD;
   ECBA=0;
   CALL SYSPOST(ECBB,PLIRETV);
   END CALL_ISPF;

 DOSTK: PROC(CMD);
 /**********************************************************************
 *                                                                     *
 * CALLED BEFORE THE DSN SUBCOMMAND IS ATTACHED TO STACK WHATEVER      *
 * DSN SUBCOMMAND WAS SPECIFIED IN THE PARM FIELD AT INVOCATION OF     *
 * THE DSNINTF MODULE.                                                 *
 *                                                                     *
 **********************************************************************/
   DCL CMD CHAR(*) VAR;
   DCL SP FIXED BIN(15,0) INIT(78);
   DCL IOECB FIXED BIN(31,0) INIT(0);
   DCL 1 STOR78 UNALIGNED BASED(PTR78),
     2 LSDADATA PTR,
     2 LSDRCLEN FIXED BIN(15,0),
     2 LSDTOTLN FIXED BIN(15,0),
     2 LSDANEXT PTR,
     2 LSDRSVRD FIXED BIN(31,0),
     2 STARTLIST FIXED BIN(31,0);
   DCL 1 LISTEL UNALIGNED BASED(LELPTR),
     2 LELLEN FIXED BIN(15,0),
     2 LELCMD CHAR(256) VAR;
   DCL 1 LISTEL2 UNALIGNED BASED(LELPTR),
     2 LELPAD FIXED BIN(15,0),
     2 LELZRO FIXED BIN(15,0);
   DCL (LV,RETCODE) FIXED BIN(31,0);
   DCL (SYSGETM,STACK) ENTRY;

   LV=LENGTH(CMD)+20;
   CALL SYSGETM('EU',LV,PTR78,SP,RETCODE);
   IF RETCODE^=0
     THEN SIGNAL ERROR;
   LSDADATA,LSDANEXT,LELPTR=ADDR(STARTLIST);
   LSDRCLEN=0;
   LSDTOTLN=LENGTH(CMD)+4;
   LSDRSVRD=0;
   LELCMD=CMD;
   LELLEN=LENGTH(CMD)+4;
   LELZRO=0;
   CALL STACK(CPPLUPT,CPPLECT,IOECB,RETCODE,'PROCN',PTR78);
   END DOSTK;

   END;
