   /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* ABE015 - HI,LO,RESET,RE,SUPPRESS,SU,DELAY SUBCOMMANDS */
   /* 1        2  3  4     5  6        7  8                 */
   %INCLUDE ABESUB;
 ABESUB(ABE015) PARS(10) CHECK(YES);
   %INCLUDE ABESUBA;
   DCL 1 LRN_STRUCTURE BASED(LRNPTR),
   %INCLUDE ABELRN;
   %INCLUDE ABELRNA;

   DCL PREFIX ENTRY(CHAR(*), CHAR(*))RETURNS(FIXED BIN(15,0));
   DCL SETCAPS ENTRY;
   DCL TYPE FIXED BIN(15,0) INIT(0);
   DCL (FPTR,LPTR,SAVPTR,ORGPTR) PTR;
   DCL CMDLAB(8) LABEL;
   DCL RESW BIT(1) INIT('1'B);
   DCL SW FIXED BIN(15,0) INIT(1);
   DCL (FW,LW) BIT(1) INIT('0'B);

   RHRC=0;
   GO TO CMDLAB(CMDINDX);
 CMDLAB(1): /* ABE015 */
 CMDLAB(2): /* HI */
   LOFLAG ='0'B;
   RHRC = 0;
   RETURN;
 CMDLAB(3): /* LO */
   LOFLAG ='1'B;
   RHRC = 0;
   RETURN;
 CMDLAB(4): /* RESET */
 CMDLAB(5): /* RE */
   RESW='0'B;
 CMDLAB(6): /* SUPPRESS */
 CMDLAB(7): /* SU */
   IF PARY(1)^=''
     THEN CNT=PARY(1);
   IF PARY(2)^=''
     THEN DO;
       CALL SETCAPS(PARY(2));
       SELECT(PARY(2));
         WHEN('F') FW='1'B;
         WHEN('L') LW='1'B;
         WHEN('S') SW=2;
         OTHERWISE DO;
           MSGDATA='INVALID OPERAND';
           RHRC = 8;
           RETURN;
           END;
         END; /* SELECT */
       END;
   SN=999999;
   ORGPTR = RECPTR;
   IF RH_L1>RH_L2
     THEN DO; /* MUST PROCESS FRONT-TO-BACK */
       I=RH_L1;
       RH_L1=RH_L2;
       RH_L2=I;
       END;
   CALL #MD(RHPTR,RHHDRPT,RH_L1); /* CALL MDPROC */
   IF RECPTR=HDPTR
     THEN RECPTR=NXTHDR;
   IF RECPTR^=HDPTR
     THEN DO;
       IF (CMDINDX=4 × CMDINDX=5) & (FW × LW)
         THEN DO; /* IT IS RESET F/L */
           IF FW
             THEN DO RECPTR=RECPTR REPEAT LSTPTR;
               IF LSTPTR=HDPTR
                 THEN LEAVE;
               IF ^SUBSTR(LSTPTR->RECFLGS,2,1)
                 THEN LEAVE; /* GOT FIRST OF SUPPRESSED GROUP */
               END;
             ELSE DO RECPTR=RECPTR REPEAT NXTPTR;
               IF NXTPTR=HDPTR
                 THEN LEAVE;
               IF ^SUBSTR(NXTPTR->RECFLGS,2,1)
                 THEN LEAVE; /* GOT LAST OF SUPPRESSED GROUP */
               END;
           DO I=1 TO CNT WHILE(RECPTR^=HDPTR);
             IF ^SUBSTR(RECFLGS,2,1)
               THEN LEAVE;
             SUBSTR(RECFLGS,2,1)='0'B;
             IF FW
               THEN RECPTR=NXTPTR;
               ELSE RECPTR=LSTPTR;
             END;
           END;
         ELSE DO;
           SAVPTR = RECPTR;
           SO = 0;
           IF SAVPTR^=HDPTR
             THEN DO SI = 1 TO SW;
               DO RECPTR=SAVPTR REPEAT(NXTPTR) WHILE(RECSEQ<=RH_L2)
                 UNTIL(NXTPTR=HDPTR);
                 SN = PREFIX(RECSTR,' '); /*FIND FIRST NONBLNK*/
                 IF SI = 1 & SW = 2
                   THEN DO;
                     SELECT(CMDINDX);
                       WHEN(4,5) /*RESET? FIND LEFTM SUPP LINE*/
                       IF SUBSTR(RECFLGS,2,1) /*SUPPRESSED?*/
                         THEN IF (SN<SO × SO=0) & SN^=0
                           THEN SO=SN;
                       OTHERWISE /*SUPPRESS? FIND LEFTM UNSUPP LINE*/
                       IF ^SUBSTR(RECFLGS,2,1) /*UNSUPPRESSED?*/
                         THEN IF SN<SO × SO=0
                           THEN SO=SN;
                       END; /* SELECT */
                     END;
                   ELSE IF SW=1
                     THEN SELECT(CMDINDX);
                       WHEN(4,5) SUBSTR(RECFLGS,2,1)='0'B; /*RESET*/
                       OTHERWISE SUBSTR(RECFLGS,2,1)='1'B; /*SUPPRESS*/
                       END; /* SELECT */
                     ELSE SELECT(CMDINDX); /* SI=2 & SW=2 */
                       WHEN(4,5) /*RESET*/
                       IF SUBSTR(RECFLGS,2,1) /* SUPPRESSED? */
                         THEN IF SN=SO
                           THEN SUBSTR(RECFLGS,2,1)='0'B;
                       /*RESET LEFTMOST SUPPRESSED LINE */
                       OTHERWISE /*SUPPRESS*/
                       IF ^SUBSTR(RECFLGS,2,1) /* UNSUPPRESSED? */
                         THEN IF SN>SO
                           THEN SUBSTR(RECFLGS,2,1)='1'B;
                       /*SUPPRESS ALL BUT LEFTMOST LINES */
                       END; /* SELECT */
                 END;
               END;
           END;
       END;
   CALL #NC(RHPTR,ORGPTR); /* CALL NEWCUR */
   RHRC = 0;
   RETURN;
 CMDLAB(8): /* DELAY */
   DELAY(MAX(1,BIN(PARY(1))));
   RHRC = 0;
   RETURN;
   END ABE015;
