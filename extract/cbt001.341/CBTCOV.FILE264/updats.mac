UPDAT    START 0                                                    TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         L     R1,0(R1)                                             TGS
         LH    R2,0(R1)                                             TGS
         LTR   R2,R2                                                TGS
         BC    8,NOPARM                                             TGS
         CH    R2,=H'9'                                             TGS
         BC    4,NOTRUNC                                            TGS
         LA    R2,8                                                 TGS
NOTRUNC  EQU   *                                                    TGS
         BCTR  R2,0                                                 TGS
         EX    R2,MVCPARM                                           TGS
NOPARM   EQU   *                                                    TGS
         OPEN  (SYSPRINT,(OUTPUT))                                  TGS
         TM    SYSPRINT+48,X'10'                                    TGS
         BC    8,BADOPN                                             TGS
         OI    FLAGS2,X'40'            SET TMPY EOD ON SYSIN        TGS
         L     R2,16                   R2 = CVT ADDR                TGS
         L     R2,0(R2)                R2 = IEATCBP ADDR            TGS
         L     R2,0(R2)                R2 = MY TCB ADDR             TGS
         L     R2,12(R2)               R2 = MY TIOT ADDR            TGS
         LA    R3,24                   R3 = INITIAL OFFSET          TGS
TIOTLOOP LA    R2,0(R2,R3)             BUMP TO NEXT TIOT ENTRY      TGS
         CLC   4(8,R2),=CL8'PRINTER'   IS IT PRINTER                TGS
         BC    8,GOTPRT                BR IF YES                    TGS
         CLC   4(8,R2),=CL8'INDEX'     IS IT INDEX                  TGS
         BC    8,GOTIND                BR IF YES                    TGS
         CLC   4(8,R2),=CL8'SYSIN'     IS IT SYSIN                  TGS
         BC    8,GOTIN                 BR IF YES                    TGS
         CLC   4(8,R2),=CL8'SYSUT4'    IS IT SYSUT4                 TGS
         BC    8,GOTUT4                BR IF YES                    TGS
         CLC   4(8,R2),=CL8'SYSUT5'    IS IT SYSUT5                 TGS
         BC    8,GOTUT5                BR IF YES                    TGS
NXTENTRY IC    R3,0(R2)                R3 = LENGTH THIS ENTRY       TGS
         LTR   R3,R3                   IS IT END TIOT               TGS
         BC    7,TIOTLOOP              BR IF NOT                    TGS
         TM    DDSW,X'C0'              IS SYSUT4 AND/OR INDEX AVLBL TGS
         BC    8,NOPRNT                BC 8,NOPRNT BR IF NEITHER AVLTGS
         LOAD  EP=TGSP                 LOAD PRNTUPDA                TGS
         ST    R0,TGSPADDR             SAVE EP ADDR                 TGS
         MVC   TGSPADDR(1),DDSW        SET FLAGS TO PASS            TGS
         B     NOPRNT                                               TGS
GOTPRT   OI    DDSW,X'80'              IND PRINTER IS AVLBL         TGS
         B     NXTENTRY                                             TGS
GOTIND   OI    DDSW,X'40'              IND INDEX IS AVLBL           TGS
         B     NXTENTRY                                             TGS
GOTIN    OI    DDSW2,X'80'             IND SYSIN IS AVLBL           TGS
         B     NXTENTRY                                             TGS
GOTUT4   OI    DDSW2,X'40'             IND SYSUT4 IS AVLBL          TGS
         B     NXTENTRY                                             TGS
GOTUT5   EQU   *                                                    TGS
         OPEN  (SYSUT5,(OUTPUT))                                    TGS
         TM    SYSUT5+48,X'10'                                      TGS
         BC    8,BADOPN                                             TGS
         OI    FLAGS2,X'08'            IND SYSUT5 IS OPEN           TGS
         B     NXTENTRY                                             TGS
NOPRNT   EQU   *                                                    TGS
         TM    DDSW2,X'C0'             IS SYSIN AND/OR SYSIN AVLBL  TGS
         BC    8,NOIN                  BR IF NEITHER AVLBL          TGS
         NI    FLAGS2,X'BF'            IND INPUT AVLBL              TGS
         LOAD  EP=TGSI                                              TGS
         ST    R0,TGSIADDR             SAVE EP ADDR                 TGS
         MVC   TGSIADDR(1),DDSW2       SET FLAGS TO PASS            TGS
NOIN     EQU   *                                                    TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                   RA ALWAYS = CURR SYSUT1 RECORTGS
         MVC   CURRNAME,63(RA)                                      TGS
         TM    FLAGS2,X'40'            IS IT EOD ON SYSIN           TGS
         BC    1,SYSENDI               BR IF YES                    TGS
         B     GETFUNCT                                             TGS
BADOPN   EQU   *                                                    TGS
         ABEND 1111,DUMP                                            TGS
GETFUNCT NI    FLAGS1,X'80'            RESET FUNCTION BITS          TGS
         BAL   RB,GETPSIN                                           TGS
GOTFUNCT MVI   CARCTL,C'1'                                          TGS
         BAL   RB,DELREP
         NI    FLAGS1,X'80'            RESET FUNCTION BITS          TGS
         MVC   CARD,0(R9)                                           TGS
         MVC   CARDLEN,UT14LEN                                      TGS
         CLC   9(6,R9),=CL6'ADD'       IS IT ADD                    TGS
         BC    7,NOADD                 BR IF NOT                    TGS
         OI    FLAGS1,X'40'            INDICATE ADD                 TGS
         B     CKNAME                                               TGS
NOADD    TM    FLAGS1,X'80'            IS SYSUT1 AVAILABLE          TGS
         BC    1,BADCTL                BR IF NOT                    TGS
         CLC   9(6,R9),=CL6'REPL'      IS IT REPL                   TGS
         BC    7,NOREPL                BR IF NOT                    TGS
         OI    FLAGS1,X'20'            INDICATE REPL                TGS
         B     CKNAME                                               TGS
NOREPL   EQU   *                                                    TGS
         CLC   9(6,R9),=CL6'CHNGE'     IS IT CHNGE                  TGS
         BC    7,NOCHNGE               BR IF NOT                    TGS
         OI    FLAGS1,X'10'            INDICATE CHNGE               TGS
         B     CKNAME                                               TGS
NOCHNGE  EQU   *                                                    TGS
         CLC   9(6,R9),=CL6'DEL'       IS IT DELET                  TGS
         BC    7,NODEL                 BR IF NOT                    TGS
         OI    FLAGS1,X'08'            INDICATE DEL                 TGS
         B     CKNAME                                               TGS
NODEL    EQU   *                                                    TGS
         CLC   9(6,R9),=CL6'PUNCH'     IS IT PUNCH                  TGS
         BC    7,CKPRT                 BR IF NOT                    TGS
         TM    FLAGS2,X'08'            IS SYSUT5 AVLBL              TGS
         BC    1,CKNAME                BR IF YES                    TGS
         B     BADCTL                                               TGS
CKPRT    EQU   *                                                    TGS
         CLC   9(6,R9),=CL6'PRINT'                                  TGS
         BC    8,CKNAME                                             TGS
BADCTL   EQU   *                                                    TGS
         MVC   MSG,=CL8'REJECTED'                                   TGS
         BAL   RB,PUTSYSPR                                          TGS
         B     GETFUNCT                                             TGS
CKNAME   EQU   *                                                    TGS
         LA    R2,9                    R2 = 9                       TGS
         LA    R3,15(R9)               R3 = ADDR OF MEMBER NAME     TGS
NAMLENLP EQU   *                                                    TGS
         CLI   0(R3),C','              IS IT COMMA                  TGS
         BC    8,GOTNLEN               BR IF YES                    TGS
         LA    R3,1(R3)                                             TGS
         BCT   R2,NAMLENLP                                          TGS
         B     BADCTL                                               TGS
GOTNLEN  EQU   *                                                    TGS
         LA    R4,8                                                 TGS
         SR    R4,R2                                                TGS
         BC    4,BADCTL                                             TGS
*              R4 NOW EQUALS NAME LENGTH LESS 1 AND R3 = ADDR OF    TGS
*              COMMA IMMEDIATELY FOLLOWING NAME                     TGS
         MVC   NEWNAME,=CL8' '                                      TGS
         EX    R4,MOVENAME                                          TGS
         CLC   LASTNAME,NEWNAME                                     TGS
         BC    10,BADCTL                                            TGS
         TM    FLAGS3,X'80'             IS IT FIRST TIME
         BC    1,NOTFIRST              BIF NOT
         OI    FLAGS3,X'C0'
         B     DONECLR
NOTFIRST EQU   *
         CP    LNECNT,=P'0'
         BC    8,DONECLR
         MVI   PRINTLNE,C' '
         MVC   PRINTLNE+1(97),PRINTLNE
         CVB   R7,LNEDBL               R7=CURRENT LNECNT
         LA    R4,57
         SR    R4,R7                   R4=# LINES TO END OF PAGE
         ZAP   LNECNT,=P'0'
         LA    R6,3
LOOPS1   EQU   *
         SRDL  R4,32                   R5=LNECNT REMAINING, R4=0
         DR    R4,R6                   R4=REMAINDER, R5=NO LINES TO
*                                      PRINT WITH SPACING AS
*                                      SPECIFIED BY REG 6
         LTR   R5,R5                   ARE THERE LINES TO PRINT
         BC    8,LOOPS3                BR IF NOT
         LA    R7,CARTAB(R6)
LOOPS2 EQU *
         MVC   CARCTL(1),0(R7)         SET PROPER CARCTL
         BAL   RB,PUTSYSPR
         BCT   R5,LOOPS2
LOOPS3   EQU   *
         BCT   R6,LOOPS1
         MVI   CARCTL,C'-'
         MVC   MSG(8),LASTNAME
         BAL   RB,PUTSYSPR
         ZAP   LNECNT,=P'0'
         OI    FLAGS3,X'40'
DONECLR  EQU   *
         MVC   LASTNAME,NEWNAME                                     TGS
         TM    FLAGS1,X'60'            IS IT ADD OR REPL
         BC    7,NOCK1                 BR IF EITHER                 TGS
         CLC   CURRNAME,NEWNAME                                     TGS
         BC    2,BADCTL                BR IF OUT OF SEQUENCE        TGS
NOCK1    EQU   *                                                    TGS
         CLI   6(R3),C'1'              IS ALL TO SYSPRINT           TGS
         BC    7,NOSYSPR               BR IF NOT                    TGS
         OI    FLAGS1,X'04'                                         TGS
NOSYSPR  EQU   *                                                    TGS
         MVI   6(R3),C'0'                                           TGS
         MVC   63(8,R9),NEWNAME        PUT NAME IN CTL STMT         TGS
         MVC   CARD,0(R9)              RESET CTL STMT               TGS
         MVC   CARDLEN,UT14LEN                                      TGS
         TM    FLAGS1,X'80'            IS SYSUT1 AVAILABLE          TGS
         BC    1,DOADD                 IF NOT, MUST BE ADD          TGS
         B     PROCTL                                               TGS
MEMGET   EQU   *                                                    TGS
         BAL   RB,GETMEMB                                           TGS
PROCTL   EQU   *                                                    TGS
         CLC   CURRNAME,NEWNAME                                     TGS
         BC    8,GOTMEM                                             TGS
         BC    4,MEMGET                                             TGS
         TM    FLAGS1,X'60'            IS IT ADD OR REPL
         BC    7,DOADD                 BR IF EITHER                 TGS
         B     BADCTL                                               TGS
GOTMEM   EQU   *                                                    TGS
         TM    FLAGS1,X'40'            IS IT ADD                    TGS
         BC    1,BADCTL                BR IF YES                    TGS
         TM    FLAGS1,X'10'            IS IT CHNGE
         BC    8,NOLCK                 BR IF NOT
         MVC   CKLEN,=H'82'
         CLI   44(RA),C'L'             IS IT LONG MEMBER
         BC    7,NOLCK                 BR IF NOT
         MVC   CKLEN,=H'90'
         CLI   44(R9),C'L'             DOES HE KNOW IT
         BC    7,BADCTL                BR IF NOT
NOLCK    EQU   *
         MVC   MSG,=CL8' '                                          TGS
         MVI   CARCTL,C'1'                                          TGS
         BAL   RB,PUTSYSPR                                          TGS
         TM    FLAGS1,X'10'            IS IT CHNGE                  TGS
         BC    1,CHNGE                 BR IF YES                    TGS
         TM    FLAGS1,X'28'            IS IT REPL OR DELET          TGS
         BC    7,REDEL                 BR IF EITHER                 TGS
         CLC   9(6,R9),=CL6'PRINT'                                  TGS
         BC    8,DOPRT                                              TGS
         OI    FLAGS2,X'02'            ELSE IT MUST BE PUNCH        TGS
         TM    FLAGS1,X'04'                                         TGS
         BC    8,NOP5                                               TGS
DOPRT    EQU   *                                                    TGS
         OI    FLAGS2,X'20'                                         TGS
NOP5     EQU   *                                                    TGS
         BAL   RB,GETMEMB                                           TGS
         B     GETFUNCT                                             TGS
REDEL    EQU   *                                                    TGS
         NI    FLAGS2,X'7F'            IND SYSUT2 NOT TO BE WRITTEN TGS
         BAL   RB,GETMEMB                                           TGS
DELETCK  TM    FLAGS1,X'08'            IS IT DELET                  TGS
         BC    1,GETFUNCT              BR IF YES                    TGS
         MVC   CARD+9(6),=CL6'ADD'     CHNGE REPL TO ADD            TGS
DOADD    EQU   *                                                    TGS
         CLI   CARD+44,C'L'                                         TGS
         BC    8,ISEQ                                               TGS
         CLI   CARD+44,C'N'                                         TGS
         BC    7,NOSEQ                 BR IF NOT                    TGS
ISEQ     EQU   *                                                    TGS
         OI    FLAGS1,X'02'                                         TGS
         ZAP   SEQCNT,=P'0'                                         TGS
NOSEQ    EQU   *                                                    TGS
         TM    FLAGS1,X'20'            IS IT REPL                   TGS
         BC    1,ADDCON                BR IF YES                    TGS
         MVC   MSG,=CL8' '                                          TGS
         MVI   CARCTL,C'1'                                          TGS
         BAL   RB,PUTSYSPR                                          TGS
ADDCON   EQU   *                                                    TGS
         LA    0,CARD                                               TGS
         MVC   UT2LEN,CARDLEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
ADDNXT   EQU   *                                                    TGS
         BAL   RB,GETIN                                             TGS
         LR    R9,R1                   R9 = CURRENT SYSIN STMT      TGS
         CLC   0(8,R1),=CL8'./'        IS IT CTL STMT
         BC    8,GOTFUNCT              BR IF YES                    TGS
         TM    FLAGS1,X'02'            WERE SEQ NOS REQUESTED       TGS
         BC    8,NOTSEQ                BR IF NOT                    TGS
         AP    SEQCNT,=P'1'            INCR SEQ NO                  TGS
         MVC   SEQEDFLD,SEQED          SET EDIT MASK                TGS
         ED    SEQEDFLD,SEQCNT                                      TGS
         MVC   72(8,R1),SEQOUT         MOVE SEQ NO TO STMT          TGS
NOTSEQ   EQU   *                                                    TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    8,NOTSYS                BR IF NOT                    TGS
         LR    R2,R1                   SAVE STMT ADDR               TGS
         MVC   CARD,0(R1)              MOVE STMT TO PRINT LINE      TGS
         MVC   CARDLEN,UT14LEN                                      TGS
         BAL   RB,PUTSYSPR             GO PRINT IT                  TGS
         LR    R1,R2                   RESTORE R1                   TGS
NOTSYS   EQU   *                                                    TGS
         LR    R0,R1                                                TGS
         MVC   UT2LEN,UT14LEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
         B     ADDNXT                                               TGS
CHNGE    EQU   *                                                    TGS
         XC    SEQSAV,SEQSAV                                        TGS
         CLC   72(8,R9),=CL8' '                                     TGS
         BC    8,NOID                                               TGS
         MVC   72(8,RA),72(R9)                                      TGS
         CLI   7(R3),C','              WAS SETSSI SPECIFIED         TGS
         BC    7,NOSSI                 BR IF NOT                    TGS
         LR    RE,R3                                                TGS
         SR    RE,R9                                                TGS
         LA    RE,0(RE,RA)             R3 = ADDR TO SSI             TGS
         MVC   8(8,RA),8(R3)           MOVE NEW SSI TO CTL STMT     TGS
NOSSI    EQU   *                                                    TGS
NOID     EQU   *                                                    TGS
         CLI   7(R3),C','              WAS SETSSI SPECIFIED         TGS
         BC    7,NCSSI                 BR IF NOT                    TGS
         MVC   15(48,RA),15(R9)        ELSI RESET SETSSI            TGS
NCSSI    EQU   *                                                    TGS
         LR    R0,RA                                                TGS
         MVC   UT2LEN,UT13LEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                                                TGS
CHNGE2   EQU   *                                                    TGS
         BAL   RB,GETIN                                             TGS
         LR    R9,R1                   R9 = CURRENT SYSIN STMT      TGS
CHCOMP   CLC   0(8,R9),=CL8'./'        IS IT CTL STMT ON SYSIN
         BC    7,NOTCTL                BR IF NOT                    TGS
         CLC   9(6,R9),=CL6'DELET'     IS IT SEQ DEL                TGS
         BC    8,DELPROC               BR IF YES                    TGS
* NOTE - DELET CARD WITHIN MEMBER SHOULD BE WRITTEN */ ON SYSIN     TGS
         CLC   9(6,R9),=CL6'REP'
         BC    8,SETREP
* NOTE - REP CARD WITHIN MEMBER SHOULD BE WRITTEN */ ON SYSIN
         TM    FLAGS1,X'80'            IS SYSUT1 AVAILABLE          TGS
         BC    1,GOTFUNCT              BR IF NOT                    TGS
         CLC   0(8,RA),=CL8'./'        IS IT CTLSTMT ON SYSUT1
         BC    7,NUT1CTL               BR IF NOT                    TGS
         MVC   CURRNAME,63(RA)                                      TGS
         B     GOTFUNCT                                             TGS
NUT1CTL  EQU   *                                                    TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    8,CHNPR                 BR IF NOT                    TGS
         OI    FLAGS2,X'20'                                         TGS
CHNPR    LA    RB,GOTFUNCT                                          TGS
         B     GETMEMB                                              TGS
NOTCTL   EQU   *                                                    TGS
         CLC   CKLEN(2),UT14LEN+2
         BC    7,BADSEQ
         TM    FLAGS1,X'80'            HAS EOD OCCURRED ON SYSUT1   TGS
         BC    1,CHINS                 BR IF YES                    TGS
         CLC   0(8,RA),=CL8'./'        IS IT CTLSTMT ON SYSUT1
         BC    8,CHINS                 BR IF YES                    TGS
         TM    FLAGS1,X'01'            IS SYSIN OUT OF SEQ          TGS
         BC    1,NOCOMP                                             TGS
         CLC   72(8,R9),72(RA)                                      TGS
         BC    8,CHREP                 BR IF EQUAL TO REPLACE       TGS
         BC    4,CHINS                 BR IF SYSIN LT SYSUT1        TGS
NOCOMP   EQU   *                                                    TGS
         MVC   SEQSAV,72(RA)                                        TGS
         LR    R0,RA                                                TGS
         MVC   UT2LEN,UT13LEN                                       TGS
         BAL   RB,PUTUT2               PUT SYSUT1 STMT INTO SYSUT2  TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    14,GETUT                BR IF NOT                    TGS
         MVC   CARD,0(RA)              MOVE UT1 STMT TO PRINT LINE  TGS
         MVC   CARDLEN,UT13LEN                                      TGS
         MVC   MSG,=CL8' '                                          TGS
         BAL   RB,PUTSYSPR             GO PRINT UT1 STMT            TGS
GETUT    EQU   *                                                    TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                                                TGS
         B     CHCOMP                                               TGS
CHINS    EQU   *                                                    TGS
         TM    FLAGS1,X'01'            IS SYSIN OUT OF SEQUENCE     TGS
         BC    1,BADSEQ                BR IF YES                    TGS
         CLC   SEQSAV,72(R9)           IS THIS SYSIN STMT OUT OF SEQTGS
         BC    2,BADSEQ                BR IF YES                    TGS
         MVC   SEQSAV,72(R9)                                        TGS
         LR    R0,R9                                                TGS
         MVC   UT2LEN,UT14LEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
         MVC   MSG,=CL8'INSERTED'                                   TGS
         B     PRNTSIN                                              TGS
BADSEQ   EQU   *                                                    TGS
         OI    FLAGS1,X'01'            INDICATE BAD SEQUENCE        TGS
         MVC   MSG,=CL8'REJECTED'                                   TGS
PRNTSIN  EQU   *                                                    TGS
         MVC   CARD,0(R9)              MOVE SYSIN STMT TO PRINT LINETGS
         MVC   CARDLEN,UT14LEN                                      TGS
         BAL   RB,PUTSYSPR                                          TGS
         B     CHNGE2                                               TGS
CHREP    EQU   *                                                    TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    14,CHREP2                                            TGS
         MVC   MSG,=CL8'REPLACED'                                   TGS
         MVC   CARD,0(RA)                                           TGS
         MVC   CARDLEN,UT13LEN                                      TGS
         BAL   RB,PUTSYSPR             GO PRINT REPLACED UT1 STMT   TGS
CHREP2   EQU   *                                                    TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                                                TGS
         B     CHINS                                                TGS
PUTUT2   EQU   *                                                    TGS
         ST    RB,PUT2RET                                           TGS
         LR    R8,R0
         B     CKREP
CKREPRET EQU   *
         LR    RB,R8
         LA    R0,PASSREC                                           TGS
         MVC   PASSREC,0(RB)                                        TGS
         CLC   UT2LEN,=F'82'                                        TGS
         BC    8,NOMOVE                                             TGS
         MVC   PASSREC+72(8),PASSREC+80                             TGS
NOMOVE   EQU   *                                                    TGS
         L     RF,=V(BLDPDS)                                        TGS
         LTR   RF,RF                                                TGS
         BC    8,NOBLDPDS                                           TGS
         BALR  RE,RF                                                TGS
NOBLDPDS EQU   *                                                    TGS
         LR    R0,RB                                                TGS
         L     R1,UT2LEN                                            TGS
         L     RF,=V(UT2PUT)                                        TGS
         BALR  RE,RF                                                TGS
         LR    R0,RB                                                TGS
         L     R1,UT2LEN                                            TGS
         L     RF,TGSPADDR                                          TGS
         LTR   RF,RF                                                TGS
         BC    8,NOTGSP                                             TGS
         BALR  RE,RF                                                TGS
NOTGSP   EQU   *                                                    TGS
         L     RB,PUT2RET                                           TGS
         BR    RB                                                   TGS
PUT2RET  DS    F                                                    TGS
GETPSIN  EQU   *                                                    TGS
         ST    RB,GPSIRET                                           TGS
         MVI   CARCTL,C'1'             INDICATE NEW PAGE            TGS
         MVC   MSG,=CL8'REJECTED'                                   TGS
GPSI2    EQU   *                                                    TGS
         BAL   RB,GETIN                                             TGS
         LR    R9,R1                   R9 = CURRENT SYSIN STMT      TGS
         MVC   CARD,0(R1)                                           TGS
         MVC   CARDLEN,UT14LEN                                      TGS
         CLC   0(8,R1),=CL8'./'        IS IT CTL STMT
         BC    8,GPSIGOT                                            TGS
         BAL   RB,PUTSYSPR                                          TGS
         B     GPSI2                                                TGS
GPSIGOT  EQU   *                                                    TGS
         L     RB,GPSIRET                                           TGS
         BR    RB                                                   TGS
GPSIRET  DS    F                                                    TGS
GETMEMB  EQU   *                                                    TGS
         OI    FLAGS2,X'10'                                         TGS
         ST    RB,MEMBRET                                           TGS
SYSPCK   TM    FLAGS2,X'20'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    8,GMNOSYS               BR IF NOT                    TGS
         CLI   0(RA),X'02'
         BC    8,GMNOSYS
         MVC   MSG,=CL8' '                                          TGS
         MVC   CARD,0(RA)                                           TGS
         MVC   CARDLEN,UT13LEN                                      TGS
         BAL   RB,PUTSYSPR                                          TGS
GMNOSYS  EQU   *                                                    TGS
         TM    FLAGS2,X'80'            IS SYSUT2 TO BE WRITTEN      TGS
         BC    14,NOUT2                BR IF NOT                    TGS
         LR    R0,RA                                                TGS
         MVC   UT2LEN,UT13LEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
NOUT2    EQU   *                                                    TGS
         TM    FLAGS2,X'02'            IS SYSUT5 TO BE WRITTEN      TGS
         BC    8,NOUT5                 BR IF NOT                    TGS
         CLC   0(8,RA),=CL8'./'                                     TGS
         BC    8,NOUT5                                              TGS
         CLC   0(8,RA),=CL8'*/'
         BC    7,UT5AST                                             TGS
         MVI   0(RA),C'.'                                           TGS
UT5AST   EQU   *                                                    TGS
         MVC   PASSREC,0(RA)                                        TGS
         CLC   UT2LEN,=F'82'                                        TGS
         BC    8,PUT5                                               TGS
         MVC   PASSREC+72(8),PASSREC+80                             TGS
PUT5     EQU   *                                                    TGS
         LA    R0,PASSREC                                           TGS
         PUT   SYSUT5                                               TGS
NOUT5    EQU   *                                                    TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                                                TGS
         CLC   0(8,RA),=CL8'./'
         BC    7,SYSPCK                                             TGS
         MVC   CURRNAME,63(RA)                                      TGS
         NI    FLAGS2,X'4D'                                         TGS
         OI    FLAGS2,X'80'                                         TGS
         L     RB,MEMBRET                                           TGS
         BR    RB                                                   TGS
MEMBRET  DS    F                                                    TGS
PUTSYSPR EQU   *                                                    TGS
         CLI   CARD,X'02'
         BCR   8,RB
         AP    LNECNT,=P'1'                                         TGS
         CP    LNECNT,=P'57'                                        TGS
         BC    2,SKIP                                               TGS
         CLI   CARCTL,C'1'                                          TGS
         BC    7,NOSKIP                                             TGS
SKIP     EQU   *                                                    TGS
         ZAP   LNECNT,=P'0'                                         TGS
         MVI   CARCTL,C'1'                                          TGS
         TM    FLAGS3,X'40'            IS TRAILER PRINTED
         BC    1,NOSKIP                BR IF YES
         PUT   SYSPRINT,LASTPRNT
NOSKIP   EQU   *                                                    TGS
         NI    FLAGS3,X'BF'
         CLC   CARDLEN,=F'82'                                       TGS
         BC    7,DO88
         MVC   CARD+80(8),=CL8' '
         B     NOSW
DO88     EQU   *
         MVC   SAVSEQ,CARD+72                                       TGS
         MVC   CARD+72(8),CARD+80                                   TGS
         MVC   CARD+80(8),SAVSEQ                                    TGS
NOSW     EQU   *                                                    TGS
         PUT   SYSPRINT,PRINTLNE                                    TGS
         MVI   CARCTL,C' '                                          TGS
         CLC   CARDLEN,=F'82'                                       TGS
         BC    8,NOSWA                                              TGS
         MVC   CARD+80(8),CARD+72                                   TGS
         MVC   CARD+72(8),SAVSEQ                                    TGS
NOSWA    EQU   *                                                    TGS
         BR    RB                                                   TGS
GETIN    EQU   *                                                    TGS
         L     RF,TGSIADDR                                          TGS
         BALR  RE,RF                   GO GET INPUT RECORD          TGS
         ST    R0,UT14LEN                                           TGS
         LTR   R1,R1                   IS IT EOF                    TGS
         BCR   7,RB                    BR IF NOT                    TGS
SYSEND   EQU   *                                                    TGS
         DELETE EP=TGSI                                             TGS
SYSENDI  EQU   *                                                    TGS
         OI    FLAGS2,X'40'            SET EOD ON SYSIN BIT         TGS
         TM    FLAGS1,X'80'            IS SYSUT1 ALSO FINISHED      TGS
         BC    1,RETCTLC               BR IF YES                    TGS
         TM    FLAGS1,X'10'            WAS CHNGE TAKING PLACE       TGS
         BC    8,SYSEND2               BR IF NOT                    TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    8,SYSEND2               BR IF NOT                    TGS
         CLC   0(8,RA),=CL8'./'        IS IT ./ ON UT1
         BC    8,SYSEND2               BR IF YES                    TGS
         OI    FLAGS2,X'20'            IND SYSPRINT TO BE WRITTEN   TGS
SYSEND2  EQU   *                                                    TGS
         BAL   RB,GETMEMB
         BAL   RB,DELREP
         LA    RB,SYSERET                                           TGS
SYSERET  EQU   *                                                    TGS
         B     GETMEMB                 LOOP TIL EOD ON SYSUT1       TGS
RETCTLC  EQU   *                                                    TGS
         LA    R0,ENDREC2                                           TGS
         L     RF,=V(BLDPDS)                                        TGS
         LTR   RF,RF                                                TGS
         BC    8,CLOSE2                                             TGS
         BALR  RE,RF                                                TGS
CLOSE2   EQU   *                                                    TGS
         SR    R0,R0                                                TGS
         L     RF,=V(UT2PUT)                                        TGS
         BALR  RE,RF                                                TGS
         CLOSE (SYSPRINT)                                           TGS
         TM    DDSW,X'C0'              WAS TGSP LOADED              TGS
         BC    8,RETCTLD               BR IF NOT                    TGS
         SR    R0,R0                                                TGS
         L     RF,TGSPADDR                                          TGS
         BALR  RE,RF                                                TGS
         DELETE EP=TGSP                                             TGS
RETCTLD  EQU   *                                                    TGS
         SR    RF,RF                                                TGS
         TM    FLAGS2,X'08'            IS SYSUT5 OPEN               TGS
         BC    8,RETCTL                BR IF NOT                    TGS
         CLOSE (SYSUT5)                                             TGS
         SR    RF,RF                                                TGS
         B     RETCTL                                               TGS
UT1END   EQU   *                                                    TGS
         TM    FLAGS2,X'40'            IS EOD ON SYSIN DONE         TGS
         BC    1,RETCTLC               BR IF YES                    TGS
         OI    FLAGS1,X'80'            INDICATE EOD ON SYSUT1       TGS
         CLC   CURRNAME,NEWNAME                                     TGS
         BC    8,UT1EEQ                                             TGS
         BC    4,UT1ELT                                             TGS
         DC    H'0'                    SHOULD NEVER FALL THRU       TGS
UT1ELT   EQU   *                                                    TGS
         TM    FLAGS1,X'40'            IS IT ADD FUNCTION           TGS
         BC    1,DOADD                 BR IF YES                    TGS
         B     BADCTL                                               TGS
UT1EEQ   EQU   *                                                    TGS
         TM    FLAGS1,X'10'            IS IT CHNGE FUNCTION         TGS
         BC    8,UT1REPCK              BR IF NOT                    TGS
UT1ELP   EQU   *                                                    TGS
         CLC   0(8,R9),=CL8'./'        IS IT CTL STMT ON SYSIN
         BC    8,GETFUNCT              BR IF YES                    TGS
         B     CHINS                                                TGS
UT1REPCK EQU   *                                                    TGS
         TM    FLAGS1,X'20'            IS IT REPL                   TGS
         BC    8,GETFUNCT              BR IF NOT                    TGS
         B     DELETCK                                              TGS
DELPROC  EQU   *                                                    TGS
         CLC   15(8,R9),24(R9)                                      TGS
         BC    2,BADSEQ                BR IF OUT OF SEQUENCE        TGS
         CLC   SEQSAV,15(R9)                                        TGS
         BC    2,BADSEQ                BR IF OUT OF SEQUENCE        TGS
         MVC   SEQSAV,24(R9)           RESET SEQSAV                 TGS
         MVC   LODEL,15(R9)                                         TGS
         MVC   CARD,0(R9)                                           TGS
         MVC   CARDLEN,UT14LEN                                      TGS
         MVC   MSG,=CL8' '                                          TGS
         BAL   RB,PUTSYSPR                                          TGS
DELCOMP  EQU   *                                                    TGS
         CLC   LODEL,72(RA)                                         TGS
         BC    2,NOTYET                BR IF TOO LOW                TGS
         CLC   SEQSAV,72(RA)                                        TGS
         BC    4,CHNGE2                BR IF TOO HIGH               TGS
         MVC   MSG,=CL8'DELETED'                                    TGS
         MVC   CARD,0(RA)                                           TGS
         MVC   CARDLEN,UT13LEN                                      TGS
         BAL   RB,PUTSYSPR                                          TGS
         MVC   MSG,=CL8' '                                          TGS
DELGET   EQU   *                                                    TGS
         BAL   RB,GETUT1                                            TGS
         LR    RA,R1                                                TGS
         CLC   0(8,RA),=CL8'./'
         BC    8,CHNGE2                                             TGS
         B     DELCOMP                                              TGS
NOTYET   EQU   *                                                    TGS
         LR    R0,RA                                                TGS
         MVC   UT2LEN,UT13LEN                                       TGS
         BAL   RB,PUTUT2                                            TGS
         TM    FLAGS1,X'04'            IS SYSPRINT TO BE WRITTEN    TGS
         BC    8,DELGET                BR IF NOT                    TGS
         MVC   CARD,0(RA)                                           TGS
         MVC   CARDLEN,UT13LEN                                      TGS
         BAL   RB,PUTSYSPR                                          TGS
         B     DELGET                                               TGS
GETUT1   EQU   *                                                    TGS
         L     RF,=V(UT13CTL)                                       TGS
         BALR  RE,RF                                                TGS
         ST    R0,UT13LEN              SAVE RECLEN                  TGS
         LTR   R1,R1                                                TGS
         BC    8,UT1END                                             TGS
         TM    FSTMESW,X'80'                                        TGS
         BC    1,UT3TST                                             TGS
         CLC   0(8,R1),=CL8'./'
         BC    7,GETUT1                                             TGS
         OI    FSTMESW,X'80'                                        TGS
UT3TST   EQU   *                                                    TGS
         CLC   0(8,R1),=CL8'./'
         BCR   7,RB                                                 TGS
         CLI   49(R1),C'3'                                          TGS
         BCR   7,RB                                                 TGS
         MVI   49(R1),C' '                                          TGS
         CLC   PARM,=CL8' '                                         TGS
         BCR   8,RB                                                 TGS
         MVC   72(8,R1),PARM                                        TGS
         BR    RB                                                   TGS
SETREP   EQU   *
         LA    RF,15(R9)               RF = A(STR1)
         LA    R6,29                   R6 = MAX LEN(STR1) + 1
SRLOOP   EQU   *
         CLI   0(RF),C','              IS IT END STR1
         BC    8,GOTSTR1               BR IF YES
         LA    RF,1(RF)
         BCT   R6,SRLOOP
         B     BADSEQ                  BR IF ERROR IN FORMAT
GOTSTR1  EQU   *
         MVC   CARD,0(R9)
         MVC   CARDLEN,UT14LEN
         MVC   MSG,=CL8' '
         BAL   RB,PUTSYSPR
         LA    RB,29
         SR    RB,R6                   RB = LEN(STR1)
         LA    R5,8(RB,RB)             R5 = LEN(REP ENTRY)
         N     R5,ROUND8
         LA    R5,8(R5)                R5 = LEN(REP ENTRY TO DBLWD)
         LR    R0,R5
         GETMAIN R,LV=(0)
         MVC   0(4,R1),REPHDR          SET NPTR IN NEW REP ENTRY
         ST    R1,REPHDR               PUT NEW ENTRY ON TOP OF STACK
         STH   R5,4(R1)                SET LEN OF ENTRY
         STH   RB,6(R1)                SET LEN STR IN NEW ENTRY
         LA    RF,16(RB,R9)            RF = A(STR2)
         LA    RE,8(RB,R1)             RE = A(FOR STR2 IN NEW ENTRY)
         BCTR  RB,0                    LESS 1 FOR EX
         EX    RB,MVCSTR1
         EX    RB,MVCSTR2
         B     CHNGE2
MVCSTR1  MVC   8(0,R1),15(R9)
MVCSTR2  MVC   0(0,RE),0(RF)
CKREP    EQU   *
         L     R7,REPHDR
CKRLOOP  EQU   *
         LTR   R7,R7                   IS IT END OF STACK
         BC    8,CKREPRET              BR IF YES
         LH    R3,6(R7)                R3 = LEN(STR)
         BCTR  R3,0                    LESS 1 FOR EX
         LA    R5,71(R8)
         SR    R5,R3                   R5 = A(WHERE SCAN IS TO END)
         LA    R4,1                    R4 = INCREMENT
         LR    R6,R8                   R6 = INDEX
CKRBLOOP EQU   *
         EX    R3,CKRCOMP              CK FOR MATCH TO STR1
         BC    7,CKRBMP                BR IF NO MATCH
         LA    R1,9(R3,R7)             R1 = A(STR2)
         EX    R3,REPMVC
         MVC   CARD,0(R8)
         MVC   CARDLEN,UT2LEN
         MVC   MSG,=CL8'ALTERED'
         BAL   RB,PUTSYSPR
         MVC   MSG,=CL8' '
CKRBMP   EQU   *
         BXLE  R6,R4,CKRBLOOP
         L     R7,0(R7)
         B     CKRLOOP
CKRCOMP  CLC   0(0,R6),8(R7)
REPMVC   MVC   0(0,R6),0(R1)
DELREP   EQU   *
         L     R5,REPHDR
         XC    REPHDR,REPHDR           CLEAR REPHDR
DELRLOOP EQU   *
         LTR   R1,R5                   IS IT END OF STACK
         BCR   8,RB                    BR IF YES
         L     R5,0(R1)
         LH    R0,4(R1)
         FREEMAIN R,LV=(0),A=(1)
         B     DELRLOOP
ROUND8   DS    0F
         DC    X'FFFFFFF8'
REPHDR   DC    F'0'
FSTMESW  DC    X'00'
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=(PM),DSORG=PS,EXLST=SYSEX,     TGSC
               LRECL=98,RECFM=FBA                                   TGS
SYSEX    DS    0F                                                   TGS
         DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                                                TGS
         DC    AL3(SYSEXIT)                                         TGS
SYSEXIT  EQU   *                                                    TGS
         LH    R9,SYSPRINT+62          R9 = BLKSIZE                 TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BCR   7,RE                    BR IF NOT                    TGS
         MVC   SYSPRINT+62(2),=H'980'                               TGS
         BR    RE                                                   TGS
SYSUT5   DCB   DDNAME=SYSUT5,MACRF=(PM),DSORG=PS,EXLST=UT5EX,       TGSC
               LRECL=80,RECFM=FB                                    TGS
UT5EX    DS    0F                                                   TGS
         DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                                                TGS
         DC    AL3(UT5EXIT)                                         TGS
UT5EXIT  EQU   *                                                    TGS
         LH    R9,SYSUT5+62            R9 = BLKSIZE=                TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BCR   7,RE                    BR IF NOT                    TGS
         MVC   SYSUT5+62(2),=H'800'    USE DEFAULT 800              TGS
         BR    RE                                                   TGS
MVCPARM  MVC   PARM(0),2(R1)                                        TGS
PARM     DC    CL8' '                                               TGS
CARTAB   DC    C'  0-'
FLAGS1   DC    X'00'                                                TGS
*                  BIT 0=1 IF SYSUT1 IS NOT AVAILABLE               TGS
*                      1=1 IF ADD                                   TGS
*                      2=1 IF REPL                                  TGS
*                      3=1 IF CHNGE                                 TGS
*                      4=1 IF DELET                                 TGS
*                      5=1 IF SYSPRINT IS TO GET EVERYTHING         TGS
*                      6=1 IF SEQUENCE NUMBERS ARE TO BE CREATED    TGS
*                      7=1 IF OUT OF SEQ ON CHNGE OPERATION         TGS
FLAGS2   DC    X'80'                                                TGS
*                  BIT 0=1 IF SYSUT2 RECORDS ARE TO BE WRITTEN      TGS
*                            IN SUBRTNE GETMEMB                     TGS
*                      1=1 IF EOD ON SYSIN                          TGS
*                      2=1 IF SYSUT2 RECORDS ARE TO BE WRITTEN      TGS
*                            TO SYSPRINT IN SUBRTNE GETMEMB         TGS
*                      3=1 IF IN SUBRTNE GETMEMB                    TGS
*                      4=1 IF SYSUT5 IS OPEN                        TGS
*                      5=1 IF CURRENT MEMBER GOES TO SYSUT5         TGS
*                      6=                                           TGS
FLAGS3   DC    X'00'
*                  BIT 0=1 1ST TIME SWITCH FOR PRINT ROUTINE
*                      1=1 IF PAGE TRAILER PRINTED BY DONECLR
PRINTLNE DS    0CL98                                                TGS
CARCTL   DS    CL1                                                  TGS
MSG      DS    CL8                                                  TGS
         DC    CL1' '                                               TGS
CARD     DS    CL88                                                 TGS
LNEDBL   DS    0D
         DC    XL6'00'
LNECNT   DC    XL2'057C'                                            TGS
CURRNAME DC    XL8'00'       = NAME OF NEXT MEMBER IN SYSUT1 DATA SETGS
NEWNAME  DC    XL8'00'       = NAME OF NEXT MEMBER TO BE PROCESSED  TGS
MOVENAME DS    0H                                                   TGS
         MVC   NEWNAME(0),15(R9)                                    TGS
LASTPRNT DC C'-'
LASTNAME DC    XL8'00'       = NAME FROM LAST CTL STMT IN SYSIN     TGS
         DC CL89' '
SEQCNT   DS    CL3                                                  TGS
SEQED    DC    X'F02020202020'                                      TGS
SEQEDFLD DS    0CL6                                                 TGS
         DS    CL1                                                  TGS
SEQOUT   DS    CL5                                                  TGS
         DC    CL3'000'                                             TGS
SEQSAV   DS    CL8                                                  TGS
ENDREC2  DC    CL20'./       END'                                   TGS
LODEL    DS    CL8                                                  TGS
DDSW     DC    X'00'         BIT 0 = 1 IF PRINTER IS AVLBL          TGS
*                            BIT 1 = 1 IF INDEX IS AVLBL            TGS
DDSW2    DC    X'00'         BIT 0 = 1 IF SYSIN AVLBL               TGS
*                            BIT 1 = 1 IF SYSUT4 AVLBL              TGS
TGSPADDR DC    F'0'                                                 TGS
TGSIADDR DC    F'0'                                                 TGS
UT2LEN   DS    F                                                    TGS
UT13LEN  DS    F                                                    TGS
UT14LEN  DS    F                                                    TGS
CARDLEN  DS    F                                                    TGS
PASSREC  DS    CL90                                                 TGS
SAVSEQ   DS    CL8                                                  TGS
CKLEN    DS    H
         LTORG                                                      TGS
         END
