000010*$.HE GENERALIZED SUBROUTINES - DATECON
000020*$.HE JULIAN/GREGORIAN DATE CONVERSION AND WEEKDAY NAME ROUTINE -
000030*$.PL 55
000040*$.PN 0
000050*$.PA
000060*$.SS
000070*$.NF
000080*$04/15/83
000090*$AUTHOR: J. HARTSTERN
000100*$SOURCE MODULE NAME: DATECON
000110*$DOCUMENTATION ID: *$
000120*$.FI L
000130*$.SP 5
000140*$THIS ROUTINE IS INTENDED FOR USE BY COBOL PROGRAMS ONLY. IT
000150*$PROVIDES A MEANS OF CONVERTING A JULIAN DATE TO A GREGORIAN DATE
000160*$OR A GREGORIAN DATE TO A JULIAN DATE. IN EITHER CASE THE NAME OF
000170*$THE DAY OF THE WEEK IS ALSO RETURNED.
000180*$.SP 1
000190*$.IN 4
000200*$CALL 'DATECON' USING DATE-PARM.
000210*$.SP 1
000220*$.IN 0
000230*$DATE-PARM
000240*$.NF
000250*$.IN 4
000260*$AN 01 LEVEL STRUCTURE NAME TO BE PASSED TO DATECON. THIS
000270*$STRUCTURE - WHICH CAN BE COPIED INTO YOUR PROGRAM SOURCE -
000280*$RESIDES AT A.SD.A000.TEXTLIB(DATECONC) .
000290*$ 01  DATE-PARM.
000300*$     05  DATE-ID    PIC X VALUE 'B'.
000310*$     05  DATE-JUL   PIC X(5) VALUE '00000'.
000320*$     05  DATE-GREG  PIC X(8) VALUE '00/00/00'.
000330*$     05  DATE-NAME  PIC X(9) VALUE SPACES.
000340*$.FI L
000350*$.IN 2
000360*$DATE-ID
000370*$.IN 6
000380*$THIS FIELD IDENTIFIES THE DATE YOU ARE SUPPLYING TO DATECON
000390*$(EITHER 'J' FOR JULIAN OR 'G' FOR GREGORIAN). WHEN DATECON
000400*$RETURNS TO YOUR CALLING PROGRAM DATE-ID WILL BE 'B' IF THE
000410*$DATE SUPPLIED WAS INVALID OR THE DATE-ID WAS NOT 'J' OR 'G'.
000420*$.IN 2
000430*$DATE-JUL
000440*$.IN 6
000450*$THE JULIAN DATE FIELD IN THE FORM 'YYDDD'. IF DATE-ID IS 'J'
000460*$THEN DATE-JUL MUST BE SUPPLIED BY YOUR PROGRAM; IF DATE-ID IS
000470*$'G' THEN DATE-JUL WILL BE RETURNED BY DATECON.
000480*$.IN 2
000490*$DATE-GREG
000500*$.IN 6
000510*$THE GREGORIAN DATE FIELD IN THE FORM 'MM/DD/YY'. IF DATE-ID
000520*$IS 'G' THEN DATE-GREG MUST BE SUPPLIED BY YOUR PROGRAM; IF
000530*$DATE-ID IS 'J' THEN DATE-GREG WILL BE RETURNED BY DATECON.
000540*$.IN 2
000550*$DATE-NAME
000560*$.IN 6
000570*$THE DAY-OF-THE-WEEK NAME WHICH ALWAYS SUPPLIED BY DATECON IF
000580*$DATE-ID IS NOT 'B'.
000590*$.NF
000600*$.SP 1
000610*$.IN 0
000620*$EXAMPLE :   MOVE 'G' TO DATE-ID.
000630*$            MOVE "GREGORIAN DATE" TO DATE-GREG.
000640*$            CALL 'DATECON' USING DATE-PARM.
000650 ID DIVISION.
000660 PROGRAM-ID. DATECON.
000670 AUTHOR. HARTSTERN.
000680 DATE-WRITTEN. 04/15/83.
000690 DATE-COMPILED.
000700 REMARKS. COBOL JULIAN/GREGORIAN DATE CONVERSION AND WEEKDAY NAME
000710          ROUTINE.
000720 ENVIRONMENT DIVISION.
000730 CONFIGURATION SECTION.
000740 SOURCE-COMPUTER. IBM-370.
000750 OBJECT-COMPUTER. IBM-370.
000760     SKIP1
000770 DATA DIVISION.
000780 WORKING-STORAGE SECTION.
000790 01  JG-YYDDD           PIC 99999       VALUE ZERO.
000800 01  JG-YYDDD-R  REDEFINES JG-YYDDD.
000810     05  FILLER         PIC 99.
000820     05  JG-DDD         PIC 999.
000830 01  JG-YYMMDD          PIC 999999      VALUE ZERO.
000840 01  JG-YYMMDD-R  REDEFINES JG-YYMMDD.
000850     05  JG-YY          PIC 99.
000860     05  JG-MM          PIC 99.
000870     05  JG-DD          PIC 99.
000880 01  DAYS-SUM           PIC S999 COMP-3 VALUE +0.
000890 01  W-DAYS-IN-MONTH    PIC S999 COMP-3 VALUE +0.
000900     SKIP1
000910 01  GJ-YYDDD           PIC 99999       VALUE ZERO.
000920 01  GJ-YYDDD-R  REDEFINES GJ-YYDDD.
000930     05  FILLER         PIC 99.
000940     05  GJ-DDD         PIC 999.
000950 01  GJ-YYMMDD          PIC 999999      VALUE ZERO.
000960 01  GJ-YYMMDD-R  REDEFINES GJ-YYMMDD.
000970     05  GJ-YY          PIC 99.
000980     05  GJ-MM          PIC 99.
000990     05  GJ-DD          PIC 99.
001000     SKIP1
001010 01  GREGDATE      PIC X(8)        VALUE '00/00/00'.
001020 01  GREG-DATE-R  REDEFINES GREGDATE.
001030     05 GMO        PIC 99.
001040     05 FILLER     PIC X.
001050     05 GDA        PIC 99.
001060     05 FILLER     PIC X.
001070     05 GYR        PIC 99.
001080 01  DAY-YYMMDD         PIC 999999      VALUE ZERO.
001090 01  DAY-YYMMDD-R  REDEFINES DAY-YYMMDD.
001100     05  DAY-YY         PIC 99.
001110     05  DAY-MM         PIC 99.
001120     05  DAY-DD         PIC 99.
001130 01  NUM-DAYS        PIC S9(7) COMP-3  VALUE +0.
001140 01  LEAP-YEAR-DAYS  PIC S9(7) COMP-3  VALUE +0.
001150 01  DAY-ARRAY-INIT.
001160     05 FILLER          PIC X(18)    VALUE 'MONDAY   TUESDAY  '.
001170     05 FILLER          PIC X(18)    VALUE 'WEDNESDAYTHURSDAY '.
001180     05 FILLER          PIC X(18)    VALUE 'FRIDAY   SATURDAY '.
001190     05 FILLER          PIC X(9)     VALUE 'SUNDAY   '.
001200 01  DAY-ARRAY-01  REDEFINES DAY-ARRAY-INIT.
001210     05 DAY-ARRAY       PIC X(9)        OCCURS 7 TIMES.
001220 01  D-I-M-INIT.
001230     05 FILLER          PIC X(18)  VALUE '031028031030031030'.
001240     05 FILLER          PIC X(18)  VALUE '031031030031030031'.
001250 01  DAYS-IN-MONTH-01  REDEFINES D-I-M-INIT.
001260     05 DAYS-IN-MONTH   PIC 999         OCCURS 12 TIMES.
001270 01  YTD-D-I-M-INIT.
001280     05 FILLER          PIC X(18)  VALUE '000031059090120151'.
001290     05 FILLER          PIC X(18)  VALUE '181212243273304334'.
001300 01  YTD-DAYS-IN-MONTH-01  REDEFINES YTD-D-I-M-INIT.
001310     05 YTD-DAYS-IN-MONTH  PIC 999         OCCURS 12 TIMES.
001320 01  I             PIC 9999        COMP SYNC VALUE 0.
001330 01  QUOTIENT      PIC S9(5)       COMP-3    VALUE +0.
001340 01  REMAIN        PIC S99999      COMP-3    VALUE +0.
001350 LINKAGE SECTION.
001360 01  DATE-PARM.
001370     05 DATE-ID      PIC X.
001380     05 DATE-JUL     PIC X(5).
001390     05 DATE-GREG    PIC X(8).
001400     05 DATE-NAME    PIC X(9).
001410 01  DATE-PARM-R  REDEFINES DATE-PARM.
001420     05 FILLER       PIC X.
001430     05 DATE-JUL-ZD  PIC 9(5).
001440     05 FILLER       PIC X(8).
001450     05 FILLER       PIC X(9).
001460 PROCEDURE DIVISION USING DATE-PARM.
001470     IF DATE-ID = 'J' THEN
001480        IF DATE-JUL NOT NUMERIC
001490           THEN MOVE 'B' TO DATE-ID
001500        ELSE
001510           MOVE DATE-JUL-ZD TO JG-YYDDD
001520           PERFORM JULIAN-TO-GREGORIAN
001530           STRING JG-MM '/' JG-DD '/' JG-YY
001540              DELIMITED BY SIZE
001550              INTO DATE-GREG
001560           MOVE JG-YYMMDD TO DAY-YYMMDD
001570           PERFORM DAY-OF-WEEK-INDEX
001580           MOVE DAY-ARRAY(REMAIN) TO DATE-NAME
001590     ELSE IF DATE-ID = 'G' THEN
001600        MOVE DATE-GREG TO GREGDATE
001610        STRING GYR GMO GDA
001620           DELIMITED BY SIZE
001630           INTO GJ-YYMMDD
001640        IF GJ-YYMMDD NOT NUMERIC THEN MOVE 'B' TO DATE-ID
001650        ELSE
001660           PERFORM GREGORIAN-TO-JULIAN
001670           MOVE GJ-YYDDD TO DATE-JUL-ZD
001680           MOVE GJ-YYMMDD TO DAY-YYMMDD
001690           PERFORM DAY-OF-WEEK-INDEX
001700           MOVE DAY-ARRAY(REMAIN) TO DATE-NAME
001710     ELSE MOVE 'B' TO DATE-ID.
001720     GOBACK.
001730     EJECT
001740 JULIAN-TO-GREGORIAN.
001750     MOVE +0 TO DAYS-SUM.
001760     COMPUTE JG-YY = JG-YYDDD / 1000.
001770     DIVIDE JG-YY BY 4 GIVING QUOTIENT REMAINDER REMAIN.
001780     IF REMAIN = 0 THEN
001790       MOVE '029' TO DAYS-IN-MONTH(2)
001800     ELSE MOVE '028' TO DAYS-IN-MONTH(2).
001810     MOVE JG-DDD TO W-DAYS-IN-MONTH.
001820     MOVE 1 TO I.
001830     PERFORM FIND-GREG-MONTH
001840                UNTIL I GREATER THAN 11 OR
001850                      W-DAYS-IN-MONTH NOT > DAYS-IN-MONTH(I).
001860     MOVE I TO JG-MM.
001870     COMPUTE JG-DD = JG-DDD - DAYS-SUM.
001880     SKIP1
001890 FIND-GREG-MONTH.
001900     COMPUTE DAYS-SUM = DAYS-SUM + DAYS-IN-MONTH(I).
001910     COMPUTE W-DAYS-IN-MONTH = JG-DDD - DAYS-SUM.
001920     ADD 1 TO I.
001930     SKIP3
001940 GREGORIAN-TO-JULIAN.
001950     COMPUTE GJ-YYDDD = (GJ-YY * 1000) +
001960                        YTD-DAYS-IN-MONTH(GJ-MM) +
001970                        GJ-DD.
001980     DIVIDE GJ-YY BY 4 GIVING QUOTIENT REMAINDER REMAIN.
001990     IF REMAIN = 0 AND
002000        GJ-MM > 2 THEN
002010       ADD 1 TO GJ-YYDDD.
002020     SKIP3
002030 DAY-OF-WEEK-INDEX.
002040     COMPUTE NUM-DAYS = (DAY-YY - 1) * 365.
002050     COMPUTE LEAP-YEAR-DAYS = (DAY-YY - 1) / 4.
002060     COMPUTE NUM-DAYS = NUM-DAYS + LEAP-YEAR-DAYS.
002070     COMPUTE NUM-DAYS = NUM-DAYS +
002080                        YTD-DAYS-IN-MONTH(DAY-MM) +
002090                        DAY-DD.
002100     DIVIDE DAY-YY BY 4 GIVING QUOTIENT REMAINDER REMAIN.
002110     IF REMAIN = 0 AND DAY-MM GREATER THAN 2 THEN
002120       COMPUTE NUM-DAYS = NUM-DAYS + 1.
002130     DIVIDE NUM-DAYS BY 7 GIVING QUOTIENT REMAINDER REMAIN.
002140     ADD 1 TO REMAIN.
