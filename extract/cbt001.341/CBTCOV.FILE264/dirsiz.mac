 /* DIRSIZ */
 /*
0*$.HE GENERALIZED SUBROUTINES - DIRSIZ
0*$.HE OBTAIN PDS DIRECTORY SIZE AND
0*$.HE PERCENT USED
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/15/79
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  DIRSIZ
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE USED TO OBTAIN THE SIZE AND AMOUNT OF
0*$SPACE USED FOR A PARTITIONED DATA SET DIRECTORY.  CALL FORMAT
0*$IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$CALL DIRSIZ(DDNAME,BLKCNT,PERCNT);
0*$.HI 4
0*$.SP 1
0*$WHERE -
0*$.SP 1
0*$DDNAME IS CHAR(8), AND MUST CONTAIN THE DDNAME UNDER WHICH THE
0*$PARTITIONED DATA SET IS ALLOCATED.
0*$.SP 1
0*$BLKCNT IS FIXED BIN(15,0), AND WILL BE SET BY THIS SUBROUTINE
0*$TO THE NUMBER OF BLOCKS IN THE DIRECTORY.
0*$.SP 1
0*$PERCNT IS FIXED BIN(15,0), AND WILL BE SET BY THIS SUBROUTINE
0*$TO PERCENT OF SPACE USED IN THE DIRECTORY.
 */
 DIRSIZ:  PROC(DDNAME,BLKCNT,PERCNT);
         DCL DDNAME CHAR(8); /* DDNAME FOR PDS */
         DCL BLKCNT FIXED BIN(15,0); /* # BLOCKS IN DIRECTORY */
         DCL PERCNT FIXED BIN(15,0); /* PERCENT DIRECTORY USED */
         DCL INDIR FILE RECORD INPUT ENV(F RECSIZE(256) BLKSIZE(256));
         DCL BUFPTR PTR,
             BUFPADJ FIXED BIN(31,0) DEF BUFPTR;
         DCL BYTCNT FIXED BIN(15,0) BASED(BUFPTR);
         ON ENDFILE(INDIR) GO TO EOF;
         OPEN FILE(INDIR) TITLE(DDNAME);
         DCL USECNT FIXED BIN(31,0) INIT(0);
         DO BLKCNT = 0 REPEAT BLKCNT + 1;
           READ FILE(INDIR) SET(BUFPTR);
           USECNT = USECNT + BYTCNT;
           END;
 EOF:
         CLOSE FILE(INDIR);
         PERCNT = (USECNT*100)/(BLKCNT*254);
         END DIRSIZ;
