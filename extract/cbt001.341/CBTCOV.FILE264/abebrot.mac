   %INCLUDE ABESUB;
 ABESUB(ABEBROT) PARS(00);
   %INCLUDE ABESUBA;
   DCL 1 DDI01 BASED(RHPLOT79),
   %INCLUDE ABE063I;
   DCL 1 PICSTR BASED(PICPTR),
     2 XCORNER DEC FLOAT(16), /* REAL PART OF THE IMAGINARY NUMBER IN
         THE LOWER LEFT CORNER OF THE AREA IN THE COMPLEX NUMBER PLANE
         TO BE EXAMINED. */
     2 YCORNER DEC FLOAT(16), /* IMAGINARY PART OF THE IMAGINARY NUMBER
         IN THE LOWER LEFT CORNER OF THE AREA IN THE COMPLEX NUMBER
         PLANE TO BE EXAMINED. */
     2 GAP DEC FLOAT(16), /* DISTANCE IN COMPLEX PLANE BETWEEN PIXELS */
     2 XDONE FIXED BIN(15,0), /* NUMBER OF ROWS PROCESSED */
     2 YDONE FIXED BIN(15,0), /* NUMBER OF COLUMNS PROCESSED */
     2 XDIM FIXED BIN(15,0),
     2 YDIM FIXED BIN(15,0),
     2 PIC(M REFER(XDIM),N REFER(YDIM)) FIXED BIN(15,0);
   DCL 1 COLOR_ARRAY BASED(COLOR_PTR),
     2 CXDIM FIXED BIN(15,0),
     2 CYDIM FIXED BIN(15,0),
     2 COLOR(M REFER(CXDIM),N REFER(CYDIM)) FIXED BIN(15,0);

   DCL CMD CHAR(100) VAR;
   DCL EOFSW BIT(1) INIT('0'B);
   DCL PEL CHAR(1) INIT('1');
   DCL (COLOR_INTERVAL,MAX_COUNT,MIN_COUNT) FIXED BIN(15,0);
   DCL COLARY(0:8) FIXED BIN(15,0) STATIC INIT(-2,-1,0,1,2,3,4,5,6);

   IF PARY(1)^=''
     THEN COLOR_COUNT=PARY(1);
     ELSE COLOR_COUNT=9;
   OPEN FILE(RH_SEQ_1) TITLE('OUT') RECORD INPUT;
   CALL READIT;
   CLOSE FILE(RH_SEQ_1);
   ALLOCATE COLOR_ARRAY;
   MAX_COUNT=0;
   MIN_COUNT=1001;
   DO I=1 TO XDIM;
     DO J=1 TO YDIM;
       IF PIC(I,J)<1000
         THEN DO;
           MAX_COUNT=MAX(MAX_COUNT,PIC(I,J));
           MIN_COUNT=MIN(MIN_COUNT,PIC(I,J));
           END;
       END;
     END;
   COLOR_INTERVAL=MAX(1,(MAX_COUNT-MIN_COUNT+1)/COLOR_COUNT);
   PUT SKIP DATA(COLOR_INTERVAL,MAX_COUNT,MIN_COUNT);
   DO I=1 TO CXDIM;
     DO J=1 TO CYDIM;
       COLOR(I,J)=COLARY(
                         MOD(
                             (PIC(I,J
                                 )-MIN_COUNT+1
                             )/COLOR_INTERVAL-1,
                             HBOUND(COLARY,1)+1
                            )
                        );
       END;
     END;
   I=#CMD('GDDM FSINIT ',RHPTR,RHHDRPT);
   I=#CMD('GDDM GSSEG 0',RHPTR,RHHDRPT);
   IF PARY(2)=''
     THEN CALL DOCOLOR;
     ELSE CALL DOBDRY;
   I=#CMD('GDDM ASREAD 0 0 0',RHPTR,RHHDRPT);
   FREE PICSTR;

 DOBDRY: PROC;
   DCL (I,J,K,X,Y) FIXED BIN;

   I=#CMD('GR DEVICE 100 100 720 384 1',RHPTR,RHHDRPT);
   DO X=1 TO XDIM;
     DO Y=1 TO YDIM;
       DO_BDRY_LUP: DO;
         DO I=MAX(1,X-1) TO MIN(XDIM,X+1);
           DO J=MAX(1,Y-1) TO MIN(YDIM,Y+1);
             IF COLOR(I,J)^=COLOR(X,Y)
               THEN DO;
                 IF X<=PLMAXX & Y<=PLMAXY
                   THEN CALL #PIXPUT(X,Y,PEL,RHPLOT79);
                 LEAVE DO_BDRY_LUP;
                 END;
             END;
           END;
         END DO_BDRY_LUP;
       END;
     END;
   I=#CMD('GDDM GSMOVE 0 100',RHPTR,RHHDRPT);
   I=#CMD('GDDM GSIMG 0 720 384 34560 BITMAP',RHPTR,RHHDRPT);
   END DOBDRY;

 DOCOLOR: PROC;

   DO B=0 TO HBOUND(COLARY,1);
     C=COLARY(B);
     K=0;
     PUT STRING(CMD) EDIT('GDDM GSCOL ',C)(A,P'Z9');
     I=#CMD(CMD,RHPTR,RHHDRPT);
     I=#CMD('GR DEVICE 100 100 720 384 1',RHPTR,RHHDRPT);
     DO I=1 TO MIN(XDIM,720);
       DO J=1 TO MIN(YDIM,384);
         IF C=COLOR(I,J)
           THEN DO;
             CALL #PIXPUT(I,J,PEL,RHPLOT79);
             K=K+1;
             END;
         END;
       END;
     IF K>0
       THEN DO;
         PUT SKIP DATA(C,K);
         I=#CMD('GDDM GSMOVE 0 100',RHPTR,RHHDRPT);
         I=#CMD('GDDM GSIMG 0 720 384 34560 BITMAP',RHPTR,RHHDRPT);
         END;
     END;
   END DOCOLOR;

 READIT: PROC;
 /**********************************************************************
 * READIT READS IN THE PIXEL STRUCTURE.                                *
 **********************************************************************/
   DCL INREC CHAR(1000) VAR;
   DCL 1 BUMPSTR BASED(PICPTR),
     2 PAD CHAR(2),
     2 BUMPSTART CHAR(1);
   DCL 1 INSTR BASED(INPTR),
     2 INCH CHAR(1),
     2 BUMP CHAR(1);
   DCL OUTPTR PTR;
   DCL EOFSW BIT(1) INIT('0'B);

   ON ENDFILE(RH_SEQ_1) EOFSW='1'B;
   READ FILE(RH_SEQ_1) INTO(INREC);
   PICPTR=ADDR(ADDR(INREC)->BUMPSTART);
   M=XDIM;
   N=YDIM;
   ALLOCATE PICSTR;
   OUTPTR=PICPTR;
   DO UNTIL(EOFSW);
     INPTR=ADDR(ADDR(INREC)->BUMPSTART);
     DO I=1 TO LENGTH(INREC);
       OUTPTR->INCH=INPTR->INCH;
       OUTPTR=ADDR(OUTPTR->BUMP);
       INPTR=ADDR(INPTR->BUMP);
       END;
     READ FILE(RH_SEQ_1) INTO(INREC);
     END;
   CLOSE FILE(RH_SEQ_1);
   END READIT;

   END ABEBROT;
