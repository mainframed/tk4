*$.HE ISD GENERALIZED SUBROUTINES - SYSREAD
*$.HE PLI INTERFACE TO BPAM READ MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/1/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSREAD
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO THE
*$BPAM READ MACRO.  IT CAN BE USED TO READ SEQUENTIALLY THROUGH A
*$MEMBER OF A PARTITIONED DATA SET.
*$.SP 1
*$CALL SYSREAD(DCBPTR,BUFFER);
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$BUFFER
*$.IN 5
*$CHARACTER STRING INTO WHICH BLOCK WILL BE READ.  THIS SHOULD
*$BE A VARYING STRING IF THE PDS CONSISTS OF UNDEFINED OR
*$VARYING LENGTH RECORDS.
*$.IN 0
*$DCBPTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS OF THE BPAM DCB.  THE
*$SHOULD HAVE BEEN SET BY A PREVIOUS CALL TO SYSDCBP.
*$.IN 0
*$.SP 1
*$.CE 1
*$CONDITIONS
*$.SP 1
*$SYSEOD
*$.IN 5
*$END OF DATA.  NORMAL RETURN FROM THE ON-UNIT OR THE
*$ABSENCE OF AN ON-UNIT WILL RAISE THE ERROR CONDITION.
*$USE GOTO TO CONTINUE NORMAL PROCESSING
*$.IN 0
*$SYSIOER
*$.IN 5
*$UNCORRECTABLE I/O ERROR.  THE STANDARD ERROR MESSAGE ACQUIRED
*$FROM SYNADAF IS AVAILABLE VIA CALL TO SYSERDF.
*$.IN 0
*$.SP 1
*$.CE 1
*$FUNCTIONS
*$.SP 1
*$.IN 0
*$SYSFILE
*$.IN 5
*$ENTRY RETURNS(CHAR(8)) - DDNAM OF PDS.
*$.IN 0
*$SYSERDF
*$.IN 5
*$ENTRY RETURNS(PTR) - POINTER TO THE FORMATTED ERROR MESSAGE
*$TEXT.
SYSREAD  PLIENTRY ,DSA=104,COMPILE=&SYSPARM
SYSERDFR DXD   A             A(ERROR MSG IN VDA)
SYSFILEA DXD   A             A(DDNAME)
         LM    2,3,0(1)
         L     2,0(2)            R2=A(DCB)
         USING IHADCB,2
         L     5,0(3)           R5=A(BUFFER) FROM SDV
         AIF   ('&SYSPARM' EQ 'F').FOPT1
         TM    6(3),X'80'       IS IT VARYING?
         BC    8,NOTV1          BR IF NOT
         LA    R5,2(R5)         R5 = A(BUFFER)
NOTV1    DS    0H
.FOPT1   ANOP
         LH    6,4(3)           R6=(MAX BUFFER LENGTH) FROM SDV
         CH    6,DCBBLKSI       IS BUFFER TOO BIG?
         BC    4,OKLN           BR IF YES
         LH    6,DCBBLKSI       ELSE, USE SMALLER
OKLN     DS   0H
         LA    4,DDNAM     SET UP PR FOR SYSFILE
         AIF   ('&SYSPARM' EQ 'F').FOPT2
         L     4,4(12)     R4=A(PRV) FROM TCA
         L     4,0(4)
         AGO   .BYFOPT2
.FOPT2   ANOP
         ST    4,0(12,0)
.BYFOPT2 ANOP
         ORG   *-2
         DC    QL2(SYSFILEA)
         LA    15,EODAD
         ST    15,DCBEODAD
         LA    15,MYSYNAD
         ST    15,DCBSYNAD
         LA    1,DECBLOC(2)    R1=A(DECB)
         READ  (1),SF,(2),(5),(6),MF=E
         LA    1,DECBLOC(2)    R1=A(DECB)
         CHECK (1)
         L     4,READCNT       BUMP READ COUNT
         LA    4,1(4)
         ST    4,READCNT
         LA    1,DECBLOC(2)
         L     1,16(1)         ADDRESS IOB
         SH    6,14(1)       SUBTRACT RESIDUAL COUNT FROM CSW
         AIF   ('&SYSPARM' EQ 'F').FOPT4
         TM    6(3),X'80'    IS IT VARYING?
         BC    8,EXIT        BR IF NOT TO EXIT
         SH    5,=H'2'
         STH   6,0(5)        SET LENGTH
         B     EXIT
         AGO   .BYFOPT4
.FOPT4   ANOP
         STH   6,6(3)
.BYFOPT4 ANOP
EXIT PLIEXIT
MYSYNAD  DS    0H
         AIF   ('&SYSPARM' NE 'F').BYFOPT3
         SYNADAF ACSMETH=BPAM
         LR    8,13
         L     13,4(13)      GET SET TO CHAIN VDA HIS SA
         LR    2,1
         LA    0,136         GET VDA FOR MESSAGE TEXT
         L  15,=V(IHESADB)
         BALR  14,15
         MVC   8(136,1),0(2)   MOVE MESSAGE TO VDA
         LA    4,8(1)
         ST    4,0(12,0)
         ORG   *-2
         DC    QL2(SYSERDFR)
         LR    13,8
         SYNADRLS
.BYFOPT3 ANOP
         PLISIGNL CONDITION,COND=SYSIOER
         B     ERROR
EODAD  PLISIGNL CONDITION,COND=SYSEOD
ERROR   PLISIGNL ERROR
         B     EXIT
         LTORG
SYSEOD   CSECT
         DC    X'06'
         DC    CL7'SYSEOD'
SYSIOER  CSECT
         DC    X'07'
         DC    CL7'SYSIOER'
SYSERDF  CSECT
         USING *,15
         SAVE  (1,3),,*
         L     2,0(12,0)
         ORG   *-2
         DC    QL2(SYSERDFR)
         L     1,0(1)        RESOLVE INDIRECT ADDRESS
         ST    2,0(1)        STORE IN PASSED POINTER
         RETURN  (1,3)
         DCBD  DSORG=PO
DCNOTE   DS    F
READCNT  DS    F
         DS    2F
DDNAM    DS    CL8
         DS    0D
DECBLOC  EQU   *-IHADCB
         END
