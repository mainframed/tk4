  /*TGSMSGP*/
 MSGPROC: PROC(MSGNO);
         % DCL (@SPARSI,@SETBUFI,@WRITEI) CHAR;
         % @SPARSI = 'N';
         % @SETBUFI = 'N';
         % INCLUDE @NULL;
         % INCLUDE @STRIP;
         % INCLUDE @WRITE;
         DCL 1 CPPL EXT,
              2 CPPLCBUF PTR,
              2 CPPLUPT PTR,
              2 CPPLPSCB PTR,
              2 CPPLECT PTR;
         DCL 1 BUFFER@,
              2 BUFLEN@ FIXED BIN(15,0),
              2 BUFOFF@ FIXED BIN(15,0),
              2 BUFSTR@ CHAR(140);
         DCL MSGFLG BIT(1) EXT INIT('0'B);
         DCL IKJRET FIXED BIN(31,0) EXT;
         DCL MSGNO FIXED BIN(15,0);
         DCL 1 MSGINDX BASED(INDXPTR),
              2 TABPTR PTR,
                   /* PTR TO THE MESSAGE TABLE */
              2 INDX(0:256),
               3 SEVERITY FIXED BIN(15,0),
               3 OFFSET FIXED BIN(15,0);
         DCL MSGPTR PTR,
              TABSDF FIXED BIN(31,0) DEF MSGPTR;
         DCL 1 MSGDF BASED(MSGPTR),
              2 PAD CHAR(4),
              2 MSGD CHAR(1);
         DCL 1 MESSAGE BASED(MSGPTR),
              2 MNPTR FIXED BIN(31,0),
                   /* ADDR OF NEXT MESSAG LINE OR ZERO */
              2 MSG,
               3 OUTLEN FIXED BIN(15,0),
               3 OFFST FIXED BIN(15,0),
               3 MSGSTR CHAR(100);
         DCL 1 CODEMSG STATIC,
              2 CLEN FIXED BIN(15,0) INIT(19),
              2 COFF FIXED BIN(15,0) INIT(0B),
              2 CSTR CHAR(12) INIT('ERROR, CODE='),
              2 CEDT PIC'999';
         DCL LOADSW BIT(1) STATIC INIT('1'B);
         IF LOADSW
              THEN DO;
                   CALL SYSLOAD('TGSMSGTB',INDXPTR);
                   LOADSW = '0'B;
                   END;
         MSGPTR = TABPTR;
         TABSDF = TABSDF + OFFSET(MSGNO);
         CEDT = MSGNO;
         IF ^MSGFLG
              THEN PUT EDIT(CSTR,CEDT)(A,A) SKIP(1);
              ELSE @WRITE(CODEMSG);
         DO WHILE(TABSDF ^= 0B);
              IF ^MSGFLG
                   THEN PUT EDIT(SUBSTR(MSGSTR,1,OUTLEN-4))(A) SKIP(2);
                   ELSE DO;
                        BUFFER@=MSG;
                        @WRITE(BUFFER@);
                        END;
              TABSDF = MNPTR;
              END;
         IF SEVERITY(MSGNO) > 8
              THEN SIGNAL ERROR;
         END;
