*$.HE ISD GENERALIZED SUBROUTINES - SETCAPS
*$.HE CONVERT TO UPPERCASE - PLI
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$8/26/75
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  U0351
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE CAN BE CALLED FROM PLI TO CONVERT A CHARACTER STRING
*$TO UPPER CASE.  CHARACTERS WHICH ARE ALREADY UPPERCASE WILL REMAIN
*$UNCHANGED.
*$.SP 1
*$.IN 10
*$ CALL SETCAPS(CHARSTR);
*$.IN 0
*$.SP 1
*$CHARSTR CAN BE EITHER FIXED OR VARYING, AND CAN BE ANY LENGTH UP TO
*$32767 CHARACTERS.
         GBLC  &COMP
&COMP    SETC  '&SYSPARM'              SYSPARM = 'F' OR 'O'
SETCAPS  CSECT
         TGSREQU
         USING *,RF
         STM   RE,RC,12(RD)
         L     R1,0(R1)                R1=A(SDV)
         L     R3,0(R1)                R3=A(STR(MAYBE))=COMPARAND
         AIF   ('&COMP' EQ 'F').FOPT
*
*SDV FORMAT
***********
*IF VARYING, BYTE ADDR OF HALFWORD CURLEN FOLLOWED BY STRING
*IF FIXED, BYTE ADDR OF STRING
***********
*MAXLEN - 2 BYTES
***********
*X'80' IF VARYING
***********
         TM    6(R1),X'80'             IS IT VARYING
         BC    8,FIXED                 BR IF NOT
         LA    R4,2(R3)                R4=A(STR)
         AH    R4,0(R3)                R4=A(STR)+LEN(STR)=1ST OPERAND
         LA    R3,2(R3)                R3=A(STR)
         B     SETR2
FIXED    DS    0H
         LR    R4,R3                   R4=A(STR)
         AH    R4,4(R1)                R4=A(STR)+LEN(STR)+1ST OPERAND
SETR2    DS    0H
         AGO   .MAINLIN
.FOPT    ANOP
*
*SDV FORMAT
***********
*BYTE ADDR OF STRING
***********
*MAXLEN - 2 BYTES
***********
*CURLEN = 2 BYTES
***********
         LR    R4,R3
         AH    R4,6(R1)
.MAINLIN ANOP
         LA    R2,4
         LNR   R2,R2                   R2 = INCREMENT
         B     BXH
LOOP     DS    0H
         OC    0(4,R4),HEX40
BXH      DS    0H
         BXH   R4,R2,LOOP              SUBTRACT 4 FROM R4 AND BR IF
*                                        R4 GT R3 = A(STR)
         SR    R4,R3
         SR    R4,R2                   R4 = CHARS REMAINING
         BC    8,ALLDONE               BR IF NULL STR
         BCTR  R4,0                      LESS 1 FOR EX
         EX    R4,OC
ALLDONE  DS    0H
         LM    RE,RC,12(RD)
         BR    RE
OC       OC    0(0,R3),HEX40
HEX40    DS    0F
         DC    4X'40'
         LTORG
         END
