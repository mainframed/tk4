PROC 0 DA() CLASS(F) MSGCLASS(A) DISK PLOT ONLYPLOT YY() MON() NEW     -
  PERFLIB() TEST SORTED PRINT OUTDA() UNIT() SER() IWT
EDIT SMFHIST CNTL EMODE NONUM
TOP
DEL * 999999
IF &IWT^=&STR() THEN -
 DO
  IN &STR(//IWT 5)
  C '//' '/*'
  END
IN //EUP027S1 JOB (10-338-MISC-027-B4,1),
IN // 'SMITH-FARM',CLASS=&CLASS,MSGCLASS=&MSGCLASS
IF &ONLYPLOT=&STR() && &TEST=&STR() THEN -
 DO
  IF &STR(&DA)=&STR() THEN -
   DO
    WRITENR ENTER INPUT DSNAME:
    READ DA
    END
  IF &DISK=&STR() THEN -
   DO
    IN //MESSAGE      ************* 2-9TRK ************
    C '//' '/*'
    IN &STR(//SETUP        &DA,SCRATCH)
    C '//' '/*'
   END
  ELSE -
   DO
    IN //MESSAGE      ************* 1-9TRK ************
    C '//' '/*'
    IN //SETUP        SCRATCH
    C '//' '/*'
   END
  IN // EXEC AU109227,PRM='&PLOT'
  IN &STR(//SORT.SMFDATA DD DSN=&DA,DISP=SHR)
  IN //SORT.JOBCARD DD DATA
  IN //EUP027S2 JOB (10-338-MISC-027-B4,1),
  IN // 'SMITH-FARM',CLASS=&CLASS,MSGCLASS=&MSGCLASS
  IN //MERGE EXEC PGM=MERGSMF,TIME=10
  IN //STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR
  IN //SYSPRINT DD SYSOUT=*
  IN //SMFDATA DD DSN=U.SD.U000.SMF.SORTED,DISP=SHR
  IN //
  C '//' '/*'
  IN //GENR.SYSUT2 DD DSN=&SYSUID..SMFHIST.CNTL,DISP=SHR
  IN //JOBRDR.IEFRDER DD DSN=&SYSUID..SMFHIST.CNTL,DISP=SHR
 END
ELSE IF &TEST=&STR() THEN -
 DO
  IF &YY=&STR() THEN -
   DO
    WRITENR ENTER YY:
    READ YY
    END
  IF &MON=&STR() THEN -
   DO
    WRITENR ENTER MON:
    READ MON
    END
  IF &DISK=&STR() THEN -
   DO
    IN //MESSAGE      ************* 1-9TRK ************
    C '//' '/*'
    IF &STR(&DA)=&STR() THEN SET &DA=U.SD.U000.SMF.YEAR&YY..&MON
    IN &STR(//SETUP        &DA)
    C '//' '/*'
   END
  IF &NEW=&STR() THEN SET &DUM=&STR()
                 ELSE SET &DUM=&STR(DUMMY,)
  IF &PERFLIB=&STR() THEN SET &PERF=&STR()
                     ELSE SET &PERF=&STR(&STR(,PERFLIB=')&PERFLIB.')
  IN // EXEC AU109229,MON=&MON,YY=&YY,DUMMY=&DUM&PERF
  IN &STR(//PLOTGEN.SMFDATA DD DSN=&DA,DISP=SHR)
  END
ELSE -
 DO
  IF &STR(&YY&MON)^=&STR() THEN -
   DO
    IF &YY=&STR() THEN -
     DO
      WRITENR ENTER YY:
      READ YY
      END
    IF &MON=&STR() THEN -
     DO
      WRITENR ENTER MON:
      READ MON
      END
    SET &DA=&STR(U.SD.U000.SMF.YEAR&YY..&MON)
    END
  IF &DA=&STR() THEN -
   DO
    SET &DA=&STR(U.SD.U000.SMFDATA)
    SET &DISK=&STR(DISK)
    END
  IF &DISK=&STR() THEN -
   DO
    IN //MESSAGE      ************* 1-9TRK ************
    C '//' '/*'
    IN &STR(//SETUP        &DA)
    C '//' '/*'
   END
  IF &PERFLIB=&STR() THEN SET &PERFLIB=&STR(&SYSPREF..TEST.LOAD)
  IF &OUTDA=&STR() THEN SET &OUTDA=&STR(&SYSPREF..TEST.DATA)
  IF &UNIT^=&STR() THEN SET &UNIT=&STR(&STR(,UNIT=)&UNIT)
  IF &SER^=&STR() THEN SET &SER=&STR(&STR(,SER=)&SER)
  IF &PRINT^=&STR() THEN SET &SORTED=&STR(&SORTED,&PRINT)
  IN &STR(// EXEC U109229A,SORTED='&SORTED'&UNIT&SER,)
  IN &STR(// PERFLIB='&PERFLIB',)
  IN &STR(// OUTDA='&OUTDA',)
  IN &STR(// INDA='&DA')
  END
END S
SUB SMFHIST.CNTL
FREE DA(SMFHIST.CNTL)
END
