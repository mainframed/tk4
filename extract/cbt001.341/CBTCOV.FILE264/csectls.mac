 /* CSECTLS */
 /*
0*$.HE GENERALIZED UTILITIES - CSECTLS
0*$.HE LISTS ALL CSECTS IN A LOAD MODULE
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$87/07/30
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  CSECTLS
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$LISTS ON SYSPRINT ALL CSECT NAMES IN THE LOAD MODULE POINTED TO
0*$BY DDNAME IN.  SAMPLE JCL FOLLOWS:
0*$.IN 5
0*$.NF
0*$.SP 1
0*$// EXEC PGM=CSECTLS
0*$//SYSPRINT DD SYSOUT=*
0*$//IN DD DSN=X75826.TEST.LOAD(XYZ),DISP=SHR
 */
 CSECTLS: PROC OPTIONS(MAIN);
 /* LISTS ALL CSECT NAMES IN LOAD MODULE POINTED TO BY DDNAME IN */
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   %INCLUDE @HEXCHAR;

   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL (EPT,PT) PTR;
   DCL 1 CESDREC UNALIGNED BASED(PT),
     2 ID CHAR(1),
     2 PAD CHAR(5),
     2 COUNT FIXED BIN(15,0),
     2 ESDDATA CHAR(1);
   DCL 1 CESD UNALIGNED BASED(PT),
     2 NAME CHAR(8),
     2 TYPE BIT(8);

   DCL INREC CHAR(32767) VAR;
   ON ENDFILE(IN) GO TO EOJ;

   DO FOREVER=1 REPEAT FOREVER;
     READ FILE(IN) INTO(INREC);
     PT=PTROUT(PTRIN(ADDR(INREC))+2);
     IF ID=@HEXCHAR(20)
       THEN DO; /* IT IS CESD RECORD */
         L=COUNT; /* BYTES OF ESD DATA */
         EPT=PTROUT(PTRIN(ADDR(ESDDATA))+COUNT);
         DO PT=ADDR(ESDDATA) REPEAT PTROUT(PTRIN(PT)+16) UNTIL(PT=EPT);
           IF SUBSTR(TYPE,5)='0'B & NAME^=LOW(8)
             THEN PUT SKIP EDIT(NAME)(A);
           END;
         END;
     END;
 EOJ:;
   END CSECTLS;
