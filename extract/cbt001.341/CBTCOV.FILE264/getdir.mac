 /* GETDIR */
 % NOPRINT;
 /*
0*$.HE GENERALIZED SUBROUTINES - GETDIR
0*$.HE GETDIR
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$02/05/78
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  GETDIR
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$GETDIR CAN BE USED TO READ THE MEMBER NAMES FROM A PARTITIONED
0*$DATA SET DIRECTORY.  CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$DCL GETDIR ENTRY(CHAR(8))
0*$  RETURNS(CHAR(8));
0*$DCL MEMNAME CHAR(8);
0*$DO MEMNAME=GETDIR('DDNAME')
0*$  REPEAT GETDIR('DDNAME')
0*$  WHILE(MEMNAME^=HIGH(8));
0*$ .
0*$ .
0*$ .
0*$ END;
0*$.IN 0
0*$.SP 1
0*$IN THE ABOVE FORMAT, 'DDNAME' IS THE NAME OF THE DD STATEMENT
0*$UNDER WHICH THE PDS WHOSE DIRECTORY IS TO BE READ IS
0*$ALLOCATED.
0*$.SP 1
0*$THE MEMBER NAMES RETURNED WILL BE IN ASCENDING SEQUENCE,
0*$AND END-OF-DIRECTORY WILL BE INDICATED BY RETURNING A
0*$MEMBER NAME EQUAL TO HIGH(8).
0*$.SP 1
0*$NOTE THAT YOU CAN ONLY READ ONE PDS DIRECTORY AT A TIME
0*$WITH GETDIR.  THAT IS, ONCE YOU HAVE STARTED READING THE
0*$DIRECTORY FOR A GIVEN FILE, YOU MUST CONTINUE READING THAT
0*$FILE UNTIL THE MEMBER NAME HIGH(8) IS RETURNED BEFORE
0*$CALLING GETDIR WITH A DIFFERENT DDNAME.
 */
  GETDIR:PROC(DDNAME) RETURNS(CHAR(8));
    DCL DDNAME CHAR(8);
    DCL INDIR FILE RECORD INPUT ENV(F RECSIZE(256) BLKSIZE(256));
    DCL BUFPTR PTR STATIC,BUFPADJ FIXED BIN(31,0) DEF BUFPTR;
    DCL BYTCNT BIT(16) BASED(BUFPTR);
    DCL BCNTSAV FIXED BIN(15,0) STATIC INIT(0);
    DCL 1 ENTRY BASED(BUFPTR),
      2 MEMNAM CHAR(8),
      2 TTR CHAR(3),
      2 CFIELD,
        3 CF1 BIT(3),
        3 CF2 BIT(5);
    DCL MYNAME CHAR(8);
    ON ENDFILE(INDIR) GO TO DIREOF;
    DCL INITSW BIT(1) STATIC INIT('1'B);
    IF INITSW
      THEN DO;
        INITSW ='0'B;
        OPEN FILE(INDIR) TITLE(DDNAME);
        END;
    IF BCNTSAV > 2
      THEN DO;
        J = 12 + 2 * CF2;
        BUFPADJ = BUFPADJ + J;
        BCNTSAV = BCNTSAV - J;
        END;
    IF BCNTSAV < 3
      THEN DO;
        READ FILE(INDIR) SET(BUFPTR);
        BCNTSAV = SUBSTR(BYTCNT,8);
        BUFPADJ = BUFPADJ + 2;
        END;
    IF MEMNAM^=HIGH(8)
      THEN RETURN(MEMNAM);
    DIREOF:CLOSE FILE(INDIR);
    INITSW ='1'B;
    RETURN(HIGH(8));
    END GETDIR;
