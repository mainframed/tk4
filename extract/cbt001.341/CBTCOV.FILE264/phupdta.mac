 /* PHUPDTA - PHONE DIRECTORY UPDATE PROGRAM */
 PHUPDTA: PROC(DA08DDN,VSAMFLE);
         DCL DA08DDN CHAR(8);
         % INCLUDE @FULLSCR;
 @SCREEN(
 '@MSG:@
 #@MSG:@
 #@MSG:@
 #@PHONE DIRECTORY
 #
 #@NAME:&(22)X                    @ADD/CHANGE/DELETE/END/?:&(6)A
 #                      @ALT @EXT   @S/L/N    @LINE
 #    @TELEPHONE NUMBER:&9999&9999  @CODE:&A  @CODE:&9  @TYPE:&9
 #
 #    @DEPT:&XXXXX  @WS NO:&999  @D/L:&9
 #                                            @MONTHLY
 #    @DIRECTORY LISTING:&(20)X               @COST:&9999.99
 #
 #    @LOCATION:&99  @ROOM:&XXXX ');
 @ENDPNL(LAST);
         DCL VSAMFLE RECORD UPDATE KEYED
             ENV(VSAM RECSIZE(80) KEYLENGTH(22) KEYLOC(3) GENKEY);
         OPEN FILE(VSAMFLE) TITLE(DA08DDN);
         DCL 1 PHREC BASED(PHPTR),
              2 PHKC CHAR(1),           /*  1 */
                   /* ALWAYS EQUALS 1 */
              2 PHAL CHAR(1),           /*  2 */
                   /* ALWAYS EQUALS 1 */
              2 FIELD9 CHAR(22),        /*  3 */
                   /* NAME (LAST FIRST INIT) */
              2 FIELD20 CHAR(1),        /* 25 */
                   /* S/L/N CODE
                      S = SECRETARY
                      L = LEAVE WORD
                      N = NEARBY */
              2 FIELD17 CHAR(4),        /* 26 */
                   /* ALTERNATE EXTENSION */
              2 FIELD18 CHAR(4),        /* 30 */
                   /* PRIMARY EXTENSION */
              2 FIELD22 CHAR(1),        /* 34 */
                   /* LINE CODE
                      1 = EXTENSION
                      2 = XPX
                      3 = RESTRICTED EXTENSION
                      4 = BUSINESS LINE
                      5 = OTHER */
              2 FIELD24 CHAR(1),        /* 35 */
                   /* TYPE
                      1 = NON - BUT
                      2 = 6 - BUT
                      3 = 12 - STRIP
                      4 = CALL-DIR
                      5 = MULTI. LIST
                      6 = OTHER */
              2 FIELD26 CHAR(5),        /* 36 */
                   /* DEPT */
              2 FIELD28 CHAR(3),        /* 41 */
                   /* W.S. NO. */
              2 FIELD30 CHAR(1),        /* 44 */
                   /* D/L CODE
                      1 = DIRECTORY LISTING
                      2 = NO LIST */
              2 FIELD33 CHAR(20),       /* 45 */
                   /* DIRECTORY LISTING */
              2 FIELD35 PIC'999V.99',   /* 65 */
                   /* MONTHLY COST */
              2 FIELD37 PIC'99',        /* 71 */
                   /* LOCATION
                       1 = KCNOR
                       2 = KCSOU
                       3 = KCWES
                       4 = SERVICE DIV. PLANT
                       5 = LAKEVIEW
                       6 = BADGER GLOBE
                       7 = BLANK
                       8 = NEENAH PAPER
                       9 = ATLAS
                      10 = ANOSC
                      11 = MARKETING CENTER
                      12 = TRAVEL POWER - NEENAH
                      13 = KC AVIATION
                      14 = KC GARAGE
                      15 = KC LEGAL
                      16 = DF LAB.
                      17 = INTEGRATED SERVICES
                      18 = KIM TECH
                      19 = KIM CENTRAL
                      20 =
                      21 = TAMAGAMI
                      22 = KC SOUTHWEST */
              2 FIELD39 CHAR(4),        /* 73 */
                   /* ROOM */
              2 PHSEQ CHAR(4);          /* 77 */
         DCL ALTSW BIT(1);
         DCL MYNAME CHAR(22) VARYING;
         DCL INREC CHAR(80);
         PHPTR = ADDR(INREC);
         ON ENDFILE(VSAMFLE) BEGIN;
              CLOSE FILE(VSAMFLE);
              GO TO READSEQ;
              END;
         ON KEY(VSAMFLE) BEGIN;
              @FLDMOD(SCREEN1.FIELD2='NAME NOT FOUND');
              GO TO DOPNL;
              END;
 READSEQ:
         READ FILE(VSAMFLE) INTO(INREC);
         GO TO NXTA;
 NXTI:
         MYNAME = '';
 NXT:
         READ FILE(VSAMFLE) INTO(INREC) KEY(MYNAME);
 NXTA:
         @FLDMOD(SCREEN1.FIELD2 = '');
 DOPNL:
         MYNAME = PHREC.FIELD9;
         SCREEN1_OUT = PHREC, BY NAME;
         @DOPNL(1);
         ALTSW = '0'B;
         % DCL FLDPRC ENTRY;
 % FLDPRC: PROC(P1) RETURNS(CHAR);
         DCL (P1) CHAR;
         RETURN('IF SCREEN1_IN.FIELD'××P1
           ××'^='''' THEN DO; ALTSW=''1''B;'
           ××' PHREC.FIELD'××P1××'=SCREEN1_IN.FIELD'××P1××'; END');
         % END FLDPRC;
         IF SCREEN1_IN.FIELD11='D' × SCREEN1_IN.FIELD11='DELETE'
              THEN DO;
                   DELETE FILE(VSAMFLE) KEY(MYNAME);
                   GO TO READSEQ;
                   END;
         IF SCREEN1_IN.FIELD11='E' × SCREEN1_IN.FIELD11='END'
              THEN DO;
                   CLOSE FILE(VSAMFLE);
                   RETURN;
                   END;
         FLDPRC(20);
         FLDPRC(17);
         FLDPRC(18);
         FLDPRC(22);
         FLDPRC(24);
         FLDPRC(26);
         FLDPRC(28);
         FLDPRC(30);
         FLDPRC(33);
         FLDPRC(35);
         FLDPRC(37);
         FLDPRC(39);
         IF SCREEN1_IN.FIELD9 ^= ''
              THEN DO;
                   PHREC.FIELD9,MYNAME = SCREEN1_IN.FIELD9;
                   IF SCREEN1_IN.FIELD11='ADD' × SCREEN1_IN.FIELD11='A'
                        THEN DO;
                             WRITE FILE(VSAMFLE) FROM(PHREC)
                               KEYFROM(MYNAME);
                             @FLDMOD(SCREEN1.FIELD2='ADDED');
                             GO TO DOPNL;
                             END;
                   GO TO NXT; /* GO PRINT NEW NAME */
                   END;
         IF ^ALTSW
              THEN GO TO READSEQ;
         IF SCREEN1_IN.FIELD11= 'C' ×
           SCREEN1_IN.FIELD11='CHANGE' ×
           SCREEN1_IN.FIELD11= ''
              THEN DO;
                   REWRITE FILE(VSAMFLE) FROM(PHREC)
                     KEY(PHREC.FIELD9);
                   @FLDMOD(SCREEN1.FIELD2 = 'CHANGED');
                   END;
              ELSE @FLDMOD(SCREEN1.FIELD2 =
                'INVALID ACTIVITY CODE - REENTER');
         GO TO DOPNL;
         END;
