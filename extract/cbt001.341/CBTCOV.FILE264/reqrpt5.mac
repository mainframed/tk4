 /* REQRPT5 */
 /*
 THIS PROGRAM IS PART OF THE REQSPACE DISK SPACE MANAGEMENT SYSTEM,
 AND IS USED TO SET THE APPROPRIATE DATE IN THE DISK SPACE MANAGEMENT
 DATA BASE INDICATING WHEN THE MOST RECENT DAILY, WEEKLY, MONTHLY,
 QUARTERLY, OR YEARLY BACKUP WAS DONE.  THE PROGRAM EXPECTS THE
 PARM FIELD TO BE IN THE FORMAT '&SER,&CYCLE' WHERE &SER IS THE
 VOLSER OF THE VOLUME BACKED UP, AND &CYCLE IS DAILY, SWKLY, WEEKLY,
 MNTHLY, QTRLY, OR YEARLY.  BASED ON THE CYCLE SPECIFIED, THE
 APPROPRIATE VBDTE IN THE VOLSER RECORD WILL BE SET.
 */
 REQRPT5:  PROC(PARM) OPTIONS(MAIN);
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         DCL PARM CHAR(100) VARYING;
         DCL WORDTAB(6) CHAR(8) VARYING STATIC INIT(
              'DAILY',
              'SWKLY',
              'WEEKLY',
              'MNTHLY',
              'QTRLY',
              'YEARLY');
         DCL VOLUME CHAR(6);
         VOLUME = SUBSTR(PARM,1,6);
         L = LENGTH(PARM)-7;
         DO BINDX = 1 TO 6 WHILE(SUBSTR(PARM,8,L) ^= WORDTAB(BINDX));
              END;
         IF BINDX > 6
              THEN DO;
                   PUT EDIT('ERROR - CYCLE IS NOT VALID')(A) SKIP(1);
                   CALL PLIRETC(12);
                   RETURN;
                   END;
         DCL PLIRETC BUILTIN;
         DCL VOLRECS CHAR(110);
         VOLRPTR = ADDR(VOLRECS);
         % INCLUDE TGSINITA;
         % INCLUDE TGSRQENQ;
         DCL ISAMFLE FILE ENV(INDEXED) DIRECT INPUT KEYED;
         DCL DATE BUILTIN;
         DCL DATEIN CHAR(6);
         DCL GREGDTE CHAR(6);
         DCL JULIAN FIXED DEC(5,0);
         DATEIN = DATE;
         GREGDTE = SUBSTR(DATEIN,3,4) ×× SUBSTR(DATEIN,1,2);
         CALL GREGJUL(GREGDTE,JULIAN);
         OPEN FILE(BDAMFLE) UPDATE DIRECT KEYED;
         BDAMKEYA = 1;
         READ FILE(BDAMFLE) INTO(QCRMAIN) KEY(BDAMKEY);
         IF SUBSTR(QCRFLGS,1,1)
               THEN DO;
                    PUT EDIT('CATASTROPHIC ERROR - THE CONTROL FILE IS U
 NUSEABLE - NOTIFY SYSTEM SUPPORT')(A) SKIP(1);
                    CALL PLIRETC(16);
                    RETURN;
                    END;
         OPEN FILE(ISAMFLE);
         ON KEY(ISAMFLE) GO TO DSNBAD;
         ISAMKEY=VOLUME;
         READ FILE(ISAMFLE) INTO(ISAMIN) KEY(ISAMKEY);
         BDAMKEYA = ISAM.DAMASTER;
         READ FILE(BDAMFLE) INTO(VOLRECS) KEY(BDAMKEY);
         IF SUBSTR(VOLFLGS,13,1) = '1'B & BINDX = 3
              THEN VBDTE(1) = JULIAN;
         VBDTE(BINDX) = JULIAN;
         WRITE FILE(BDAMFLE) FROM(VOLRECS) KEYFROM(BDAMKEY);
         GO TO EOJ;
 DSNBAD:
         PUT EDIT('THE SPECIFIED VOLUME DOES NOT EXIST')(A) SKIP(1);
         CALL PLIRETC(12);
 EOJ:
         CALL SYSDEQ(QNAME,RNAME,'SYSTEM',ENQRET);
         END;
