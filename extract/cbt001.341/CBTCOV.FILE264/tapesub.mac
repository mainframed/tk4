*THIS SUBRTNE IS CALLED BY THE PLI MAINLINE MODULE ARCHRST.
*AT EACH INVOCATION, THIS SUBROUTINE WILL RETURN 512 BYTES OF
*DATA WHICH CONTAINS THE FORMAT 1 DSCB FOLLOWED IF NECESSARY BY
*THE ASSOCIATED FORMAT 3 DSCB.  THE SUBRTNE EXPECTS TO BE PASSED
*A VARYING CHARACTER STRING TO CONTAIN THIS INFORMATION
*AS THE SOLE PARAMETER.  THE SUBRTNE WILL INDICATE THE END OF THE
*TAPE BY SETTING THE VARYING CHARACTER STRING TO NULL.
TAPESUB  PLIENTRY
         L     R6,0(R1)                R6 = A(SDV)
         L     R6,0(R6)                R6 = A(HALFWORD LENGTH)
         LA    R8,TDAREA+512
         TM    INITSW,X'80'            IS IT FIRST TIME?
         BC    1,BYINIT                BR IF NOT
         OI    INITSW,X'80'
         RDJFCB INDCB
         OPEN  (INDCB,(INPUT))
         LA    R1,1                    SET ABEND CODE
         TM    INDCB+48,X'10'          IS IT GOOD OPEN?
         BC    1,OPNOK
ABEND    DS    0H
         ABEND (R1),DUMP
OPNOK    DS    0H
         MVI   SETMODE,X'03'
         MVC   INCCW(8),SETMODE        SET IN SETMODE CCW
         MVC   INCCW+8(8),RWCCW        SET UP TO REWIND INPUT
         OI    INCCW+12,X'60'          TURN ON CMD CH.
         MVC   INCCW+16(8),NOP         CHAIN A NOP
         BAL   RB,IOIN
         LA    R1,10
         CLI   INECB,X'7F'
         BC    7,ABEND
         MVC   INCCW+8(8),TDINCCW      SET READ CCW
         BAL   RB,IOIN                 GO READ VOL1 FROM INPUT TAPE
         LA    R1,5                    SET ABEND CODE
         CLI   INECB,X'7F'             READ OK?
         BC    7,ABEND                 BR IF NOT
         CLC   TDAREA(4),=C'VOL1'      IS IT VOLUME LABEL?
         BC    7,ABEND                 BR IF NOT
         CLC   TDAREA+4(6),INJFCB+118  IS IT PROPER INPUT VOLUME
         BC    7,ABEND                 BR IF NOT
         MVI   INSTAT+4,0
BYINIT   DS    0H
         SR    R5,R5                   ZERO RECORD COUNT
TTLOOP   DS    0H
         BAL   RB,IOIN                 GO READ A TAPE BLOCK?
         CLI   INECB,X'7F'             READ OK?
         BC    8,TT1                   BR IF YES
         LA    R1,6                    SET ABEND CODE
         TM    INSTAT+4,X'01'          UNIT EXCEPTION?
         BC    8,ABEND                 BR IF NOT - TAPE ERROR
* READING A TAPE MARK SETS UNIT EXCEPTION
         SR    R5,R5                   ZERO RECORD COUNT
         CLI   EOVCK,X'03'             IS IT TM/EOV1/EOV2/TM?
         BC    8,DORWND                BR IF YES
         MVI   EOVCK,X'01'             INDICATE TM SEQUENCE
         CLI   NULLCK,X'02'            IS IT HDR1/HDR2/TM SEQUENCE?
         BC    7,NOHHT                 BR IF NOT
         LA    R5,2                    SET RECORD COUNT
NOHHT    DS    0H
         TM    VVSTAT,X'01'            DOUBLE TAPEMARK?
         BZ    TTLOOP                  BR IF NOT
         CLI   NULLCK,X'02'            IS IT HDR1/HDR2/TM/TM SEQUENCE?
         MVI   NULLCK,X'00'
         BC    8,TTLOOP                BR IF YES
DORWND   DS    0H
         CLOSE (INDCB,REWIND)
         SR    RF,RF                   SET RC = 0
         STH   RF,0(R6)                SET STRING LENGTH = 0
EXIT     DS    0H
         PLIEXIT
TT1      DS    0H
         LH    R2,INSTAT+6             R2 = RESIDUAL BYTE COUMT
         LH    R3,TDINCCW+6            R3 = ORIGINAL BYTE COUNT
         SR    R3,R2                   R3 = BYTES READ
         STH   R3,TTCCWOUT+6           SET COUNT IN WRITE CCW
         CH    R3,=H'80'               IS LEN=80?
         BC    7,NOTHDR2               BR IF NOT
         CLC   TDAREA(4),=C'HDR1'      IS IT HDR1?
         BC    7,NOTHDR1               BR IF NOT
         MVI   NULLCK,X'01'
         MVI   EOVCK,X'00'
         B     BYCKR
NOTHDR1  DS    0H
         CLC   TDAREA(1),=C'HDR2'      IS IT HDR2?
         BC    7,NOTHDR2               BR IF NOT
         CLI   NULLCK,X'01'            WAS PREVIOUS HDR1?
         BC    7,NOTHDR2               BR IF NOT
         MVI   NULLCK,X'02'            IND HDR1/HDR2 SEQUENCE
         B     BYCKR
NOTHDR2  DS    0H
         MVI   NULLCK,X'00'
         CLI   EOVCK,X'00'             IS IT POSSIBLE EOV SEQUENCE?
         BC    8,BYCKR                 BR IF NOT
         CH    R3,=H'80'               IS LEN=80?
         BC    7,CKRESET               BR IF NOT
         CLI   EOVCK,X'01'             WAS TM LAST?
         BC    7,CK2                   BR IF NOT
         CLC   TDAREA(4),=C'EOV1'      IS IT EOV1?
         BC    7,CKRESET               BR IF NOT
         MVI   EOVCK,X'02'             INDICATE TM/EOV1 SEQUENCE
         B     BYCKR
CK2      DS    0H
         CLI   EOVCK,X'02'             WAS IT TM/EOV1 SEQ?
         BC    7,CKRESET               BR IF NOT
         CLC   TDAREA(4),=C'EOV2'      IS IT EOV2?
         BC    7,CKRESET               BR IF NOT
         MVI   EOVCK,X'03'             INDICATE TM/EOV1/EOV2 SEQ
         B     BYCKR
CKRESET  DS    0H
         MVI   EOVCK,X'00'
BYCKR    DS    0H
         LTR   R5,R5                   IS RECORD COUNT 0?
         BC    8,TTLOOP                BR IF YES
         BCT   R5,TTLOOP               DECREMENT RECORD COUNT & BR
         MVC   2(256,R6),TDAREA        MOVE FIRST 256 BYTES
         MVC   258(256,R6),TDAREA+256   MOVE REMAINING 256 BYTES
         B     EXIT
IOIN     DS    0H
         MVC   VVSTAT,INSTAT+4         SAVE LAST IOB STAT
         SR    R0,R0
         ST    R0,INECB                CLEAR ECB
         NI    INDCB+44,X'3F'          RESET IO FLAGS
         EXCP  INIOB                   DO I/O
         WAIT  ECB=INECB               WAIT FOR I/O
         BR    RB
INITSW   DC    X'00'
EOVCK    DC    X'00'
NULLCK   DC    X'00'
*
* DCB FOR TAPEIN
*
INDCB    DCB   DDNAME=TAPE,MACRF=(E),DSORG=PS,DEVD=TA,                 C
               EXLST=INEX
INEX     DS    0F
         DC    X'87',AL3(INJFCB)
INJFCB   DS    CL180
*
* ECB FOR TAPEIN
*
INECB    DC    F'0'
*
* IOB FOR INPUT DATA SET
*
INIOB    DS    0F
         DC    X'4200'
INSENS   DC    H'0'
         DC    X'7F'
         DC    AL3(INECB)
INSTAT   DC    2F'0'
         DC    A(INCCW)
         DC    A(INDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
INDASD   DC    X'00'
INSEEK   DC    XL7'00'
*
* CCWS FOR INPUT DATA SET
*
INCCW    DS    5D
VVSTAT   DC    X'00'
NOP      CCW   3,0,X'20',1             NOP CCW
SETMODE  CCW   *-*,0,X'60',1           SKELETON SETMODE CCW
TDINCCW  CCW   2,TDAREA,X'20',32767    READ CCW
BSCCW    CCW   X'27',BSCCW,X'60',1     BACKSPACE BLOCK CCW
WTCCW    CCW   31,TDAREA,X'20',1       WRITE TAPE MARK
TTCCWOUT CCW   1,TDAREA,X'20',32767    WRITE DATA
RWCCW    CCW   7,RWCCW,X'20',1
         LTORG
TDAREA   DS    80CL250
         DS    40CL250
         DS    28CL100 32800 BYTES
         END
