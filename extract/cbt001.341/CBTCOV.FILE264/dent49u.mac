 DENT49U: PROC(RHPTR,SUBENT,RHHDRPT,SUBRTNES,D) OPTIONS(REENTRANT);
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXECUTE AN UPDATE SUBCOMMAND. D POINTS TO SELECT_HTEXT    *
 * CONTROL BLOCK.                                                      *
 *                                                                     *
 **********************************************************************/
   % DCL @PROCNAME CHAR;
   % @PROCNAME='';
   % INCLUDE DENTSUB;
   % INCLUDE DENTSEL;
   HALT;
   DCL (C,D,TRH) PTR;
   DCL CALCPRT ENTRY(DEC FLOAT(16))
     RETURNS(CHAR(80) VAR);
   DCL MISSING DEC FLOAT(16);
   UNSPEC(MISSING)='1'B; /* SET MISSING VALUE */

   IF D->FROM_LIST_PTR->FROM_NAME=''
     THEN D->FROM_LIST_PTR->FROM_NAME=RHNAME;

   /* NOW LOCATE THE DATABASE */
   CALL LOC_DB(D->FROM_LIST_PTR);

   /* NOW PREPARE THE DBD */
   CALL SET_DBD(D);

   /* NOW GO CHECK FOR CHARACTER EXPRESSIONS */
   CALL CHAR_CHECK(D);

   /* NOW DO THE UPDATE */
   CALL UPDATE_PROCESS(D);


 SET_DBD: PROC(D);
 /**********************************************************************
 *                                                                     *
 * CALLED TO PREPARE THE DBD FOR PROCESSING.  IF ANY SET SPECS         *
 * ASSIGN TO FIELDS THAT DON'T CURRENTLY EXIST, THEY WILL BE ADDED TO  *
 * THE DBD.                                                            *
 *                                                                     *
 **********************************************************************/
   DCL (A,B,C,D) PTR;

   D->TO_RHP=D->FROM_LIST_PTR->FROM_RHP; /* FOR NOW, ONLY ONE */
   DO A=D->SELECT_LIST_PTR REPEAT A->SELECT_NEXT_FIELD WHILE(A^=NULL);
     C=NULL;
     DO B=D->TO_RHP->RHUSEHD REPEAT B->RHNLINK WHILE(B^=NULL);
       IF B->RHNNAME=A->SELECT_NAME
         THEN LEAVE; /* FOUND FIELD */
       C=B; /* KEEP TRACK OF PRIOR */
       END;
     IF B=NULL
       THEN CALL SET_FIELD(D,C,NULL,A->SELECT_NAME,A); /* ADD FIELD */
     END;
   END SET_DBD;

 UPDATE_EVAL: PROC(D,S,C) RETURNS(CHAR(256) VAR);
 /**********************************************************************
 *                                                                     *
 * UPDATE_EVAL IS CALLED TO EVALUATE AN EXPRESSION POINTED TO BY THE   *
 * SELECT_LIST_ELEMENT POINTED TO BY PARAMETER S.  D POINTS TO THE     *
 * SELECT_HTEXT CONTROL BLOCK, AND C POINTS TO THE RHNAMESTR STRUCTURE *
 * FOR THE FIELD TO WHICH THE RESULT IS TO BE ASSIGNED.                *
 *                                                                     *
 **********************************************************************/
   DCL (C,D,S) PTR;
   RETURN(EVAL_COMMON(D,S,C,EVAL_ODD_FUNCTION));
 EVAL_ODD_FUNCTION: PROC(MSG,PRMPTR,PARMCNT,OP) RETURNS(DEC FLOAT(16));
   DCL MSG CHAR(80) VAR;
   DCL PRMPTR PTR;
   DCL PARMCNT FIXED BIN(15,0);
   DCL OP(*) DEC FLOAT(16);
   MSG='INVALID FUNCTION REFERENCE';
   SIGNAL CONDITION(BADSTMT);
   END EVAL_ODD_FUNCTION;
   END UPDATE_EVAL;

 UPDATE_PROCESS: PROC(D);
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXECUTE THE UPDATE PROCESS.                               *
 *                                                                     *
 **********************************************************************/
   DCL (A,B,C,D,E,F,S) PTR;
   DCL I FIXED BIN;

   F=D->FROM_LIST_PTR;
   DO FOREVER=1 REPEAT FOREVER;
     F->FROM_RECPTR=#SUCC(RHHDRPT,F->FROM_RHP,F->FROM_RECPTR);
     IF F->FROM_RECPTR=F->FROM_RHP
       THEN LEAVE; /* END OF DATA */
     I=#CMD('USE NOSYM',F->FROM_RHP,SUBENT,RHHDRPT);
       /* GO SET VARIABLE OFFSETS AND LENGTHS FOR THIS RECORD */
     B=ADDR(F->FROM_RECPTR->RECLEN); /* GET ADDR BUFFER */
     IF WHERE_EVAL(D)
       THEN DO S=D->SELECT_LIST_PTR REPEAT S->SELECT_NEXT_FIELD
         WHILE(S^=NULL);
         DO C=D->TO_RHP->RHUSEHD REPEAT C->RHNLINK WHILE(C^=NULL)
           UNTIL(C->RHNNAME=S->SELECT_NAME);
           END;
         CALL RUN_BUF(B,C->RHNPOS,C->RHNLNG,UPDATE_EVAL(D,S,C),C);
         END;
     END;
   END UPDATE_PROCESS;

   END DENT49U;
