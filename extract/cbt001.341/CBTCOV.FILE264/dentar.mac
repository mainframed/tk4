 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENTAR (ADDREC) (SUBENT(11))
 THIS SUBROUTINE IS USED TO ADD A RECORD TO THE ARRAY OF RECORDS.  THE
 SINGLE PARAMETER MUST BE A POINTER VARIABLE CONTAINING THE ADDR OF THE
 RECORD BEHIND WHICH THE RECORD IS TO BE ADDED.  RECTOAD MUST CONTAIN
 RECORD TO BE ADDED. */
 DENTAR:  PROC(RHPTR,SUBENT,PRMPTRPTR,RETPTR,ADDSEQ,RECTOAD,RHPPTR)
   OPTIONS(REENTRANT);
         % DCL @PROCNAME CHAR;
         % @PROCNAME = '';
         % INCLUDE DENTSUB;
         DCL (PRMPTRPTR,PRMPTR,TMPPTR,RETPTR) UNALIGNED PTR;
         DCL RHPPTR PTR;
         DCL RECTOAD CHAR(*) VAR;
         DCL MAXLEN ENTRY(CHAR(*) VAR)
           RETURNS(FIXED BIN(15,0));
         DCL L FIXED BIN(15,0);
         L = MAXLEN(RECTOAD);
  /*     IF INDDN = NULL
           THEN L = MAXLEN(RECTOAD);
           ELSE L = INDDN->ALCRECL-INDDN->ALCSEQLEN; */
           /* DON'T TRY TO USE OUTDDN HERE; IT MAY NOT INITIALIZED */
         DCL ADDSEQ FIXED BIN(31,0);
         DCL SEQBL CHAR(8),
             SEQPIC PIC'(8)9' DEF SEQBL;
               /* MAX LEN OF SEQ FLD = 8 BYTES */
         PRMPTR = PRMPTRPTR;
         DCL I FIXED BIN(15,0);
         CALL #GETSLOT(RHPTR,SUBENT,PRMPTR,TMPPTR,L);/*CALL GETSLOT*/
         IF ADDSEQ >= 0
              THEN TMPPTR->RECSEQ = ADDSEQ;
              ELSE DO;
                   IF PRMPTR = HDPTR
                        THEN I = 1;
                        ELSE I = PRMPTR->RECSEQ+1;
                   TMPPTR->RECSEQ = I;
                   END;
         TMPPTR->RECSTR = RECTOAD;
         TMPPTR->RECFLGS = '0'B;
         TMPPTR->RECRES='0'B;
         RETPTR=TMPPTR;
         IF TMPPTR->RECSEQ>RHPMAXSQ
           THEN DO;
             CALL SUBENT(35)(RHPPTR,TMPPTR->RECSEQ); /* DENTSQ */
             CALL #COLTR(RHPPTR,SUBENT); /* CALL DENTCT */
             END;
         RETURN;
         END;
