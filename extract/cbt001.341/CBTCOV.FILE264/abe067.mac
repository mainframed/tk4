 /* ABE067,TPUT,TGET,TPG */
 /* USED TO PRODUCE TEST OUTPUT ON 3270 DEVICES */
   %INCLUDE ABESUB;
 ABESUB(ABE067) PARS(0000);
   %INCLUDE ABESUBA;
   %INCLUDE ABERHPNL;

   DCL TRIN ENTRY(CHAR(2)) RETURNS(CHAR(1));
   DCL (SETCAPS,SYSTGET,SYSTPG,SYSTPUT) ENTRY;
   DCL SUFFIX ENTRY(CHAR(*),CHAR(*)) RETURNS(FIXED BIN(15,0));
   DCL SCR CHAR(32760) VARYING INIT('');
   DCL ERRNAME CHAR(8) VAR INIT('');

   STFSMODE='0'B;
   SELECT(CMDINDX);
     WHEN(1,2)
       CALL DO_TPUT(PARY(1),PARQCK(1),PARY(2),PARY(3),PARY(4),'TPUT');
     WHEN(3)
       CALL DO_TGET(PARY(1),PARY(2),PARY(3));
     WHEN(4)
       CALL DO_TPG(PARY(1),PARQCK(1),PARY(2),PARY(3),PARY(4));
     OTHERWISE; /* TO STOP COMPILER MESSAGE */
     END; /* SELECT */
   IF RHRC^=0
     THEN PUT STRING(MSGDATA) EDIT(ERRNAME××' RHRC=',RHRC)(A,F(5));

 DO_TGET: PROC(NAM,OP1,OP2);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE CURRENT FUNCTION IS TGET.                           *
 *                                                                     *
 **********************************************************************/
   DCL (NAM,OP1,OP2) CHAR(*) VAR;
   DCL RETCODE FIXED BIN(31,0);
   DCL RHP PTR;
   DCL PARMDFT(2) CHAR(8) STATIC INIT('EDIT','WAIT');
   DCL PARM(2) CHAR(8);
   DCL I FIXED BIN;

   CALL SETCAPS(NAM);
   RHP=#RHPLOC(NAM,RHHDRPT);
   IF RHP^=NULL
     THEN I=#CMD('DEL',RHP,RHHDRPT);
     ELSE DO;
       RHP=RHPTR;
       I=#CMD('ABE '''' NAME('××NAM××') HEX',RHP,RHHDRPT);
       IF RHP=RHPTR
         THEN DO;
           MSGDATA='DATASET '××NAM××' CAN''T BE DEFINED';
           RHRC=8;
           RETURN;
           END;
       END;
   PARM(1)=OP1;
   PARM(2)=OP2;
   DO I=1 TO HBOUND(PARM,1);
     IF PARM(I)=''
       THEN PARM(I)=PARMDFT(I);
       ELSE CALL SETCAPS(PARM(I));
     END;
   DO UNTIL((RETCODE^=12) & (RETCODE^=28));
     RHP->BUFPTR->BUF='';
     CALL SYSTGET(RHP->BUFPTR->BUF,RETCODE,PARM(1),PARM(2));
     IF RHP->RECPTR=RHP
       THEN SEQ=1;
       ELSE SEQ=RHP->RECPTR->RECSEQ+1;
     CALL #ADD(RHP,RHHDRPT,RHP->RECPTR,RHP->RECPTR,SEQ,
       RHP->BUFPTR->BUF,0);
     END;
   I=#CMD('TOP',RHP,RHHDRPT);
   RHRC=RETCODE;
   ERRNAME='TGET';
   END DO_TGET;

 DO_TPG: PROC(INAM,QUOTES,ONAM,OP1,OP2);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE CURRENT FUNCTION IS TPG.                            *
 *                                                                     *
 **********************************************************************/
   DCL (INAM,OP1,OP2,ONAM) CHAR(*) VAR;
   DCL QUOTES BIT(1);

   RHRC=0;
   CALL DO_TPUT(INAM,QUOTES,'NOEDIT',OP1,OP2,'TPG');
   IF RHRC=0
     THEN CALL DO_TGET(ONAM,'ASIS','WAIT');
   END DO_TPG;

 DO_TPUT: PROC(NAM,QUOTES,OP1,OP2,OP3,FUNCTION);
 /**********************************************************************
 *                                                                     *
 * CALLED WHEN THE CURRENT FUNCTION IS TPUT.                           *
 *                                                                     *
 **********************************************************************/
   DCL (NAM,OP1,OP2,OP3) CHAR(*) VAR;
   DCL FUNCTION CHAR(*);
   DCL QUOTES BIT(1);
   DCL RETCODE FIXED BIN(31,0);
   DCL (RHP,WRK) PTR;
   DCL PARMDFT(3) CHAR(8) STATIC INIT('FULLSCR','WAIT','NOHOLD');
   DCL PARM(3) CHAR(8);
   DCL I FIXED BIN;

   CALL SETCAPS(NAM);
   IF QUOTES /* IN QUOTES? */
     THEN CALL FILLSCR(NAM);
     ELSE DO;  /* MUST BE A DATASET REFERENCE */
       RHP=#RHPLOC(NAM,RHHDRPT);
       IF RHP=NULL
         THEN DO;
           MSGDATA='DATASET '××NAM××' NOT FOUND';
           RHRC=8;
           RETURN;
           END;
       DO WRK=#SUCC(RHHDRPT,RHP,NULL)
         REPEAT #SUCC(RHHDRPT,RHP,WRK) WHILE(WRK^=RHP);
         IF WRK->RECSTR^=''
           THEN CALL FILLSCR(SUBSTR(WRK->RECSTR,1,
             SUFFIX(WRK->RECSTR,' ')));
         END;
       END;
   PARM(1)=OP1;
   PARM(2)=OP2;
   PARM(3)=OP3;
   DO I=1 TO HBOUND(PARM,1);
     IF PARM(I)=''
       THEN PARM(I)=PARMDFT(I);
       ELSE CALL SETCAPS(PARM(I));
     END;
   IF FUNCTION='TPUT'
     THEN CALL SYSTPUT(SCR,RETCODE,PARM(1),PARM(2),PARM(3));
     ELSE CALL SYSTPG(SCR,RETCODE,PARM(1),PARM(2),PARM(3));
   RHRC=RETCODE;
   ERRNAME=FUNCTION;
   END DO_TPUT;

 FILLSCR: PROC(PRM);
 /**********************************************************************
 *                                                                     *
 * CALLED TO CONVERT PRINTABLE HEX DATA STREAM TO INTERNAL HEX IN      *
 * BUFFER NAMED TO SCR TO BE PASSED TO TERMINAL.                       *
 *                                                                     *
 **********************************************************************/
   DCL PRM CHAR(*) VAR;
   DCL I FIXED BIN(15,0);

   DO I=1 REPEAT I+2 WHILE(I<LENGTH(PRM));
     SCR=SCR××TRIN(SUBSTR(PRM,I,2));
     END;
   IF MOD(LENGTH(PRM),2)^=0
     THEN SCR=SCR××TRIN(SUBSTR(PRM,LENGTH(PRM),1)××'0');
   END FILLSCR;

   END ABE067;
