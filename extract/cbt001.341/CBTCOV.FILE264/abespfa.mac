ABESPFA   CSECT
         SAVER RETCTL,LV=DSALEN
         TGSREQU
         USING DSA,RD
         MVC   CBUFOFF,=H'0'
         MVC   CBUF(L'CMDCON),CMDCON
         L     R1,0(R1)           R1=A(HALFWORD PARM LENGTH)
         LH    R2,0(R1)           R2=LENGTH(PARM FIELD)
         LTR   R2,R2              IS LENGTH 0?
         BC    8,BYMOV            BR IF YES
         LR    R3,R2
         BCTR  R3,0
         EX    R3,MVCPARM
BYMOV    DS    0H
         LA    R4,4+L'CMDCON(R2)
         STH   R4,CBUFLEN
         LA    R1,MYCBUF
         ST    R1,MYCPPL
         L     R1,X'21C'          R1=A(TCB)
         L     R1,X'B4'(R1)       R1=A(JSCB)
         L     R1,X'108'(R1)      R1=A(PSCB)
         LTR   R1,R1
         BC    8,ISBATCH          BR IF NOT TSO ENVIRONMENT
         ST    R1,MYCPPL+8        SAVE A(PSCB)
         MVC   MYCPPL+4(4),X'34'(R1) GET A(UPT) FROM PSCB
         L     R1,X'30'(R1)       R1=A(RLGB)
         MVC   MYCPPL+12(4),X'100'(R1) GET A(ECT) FROM RLGB
         B     DOLINK
ISBATCH  DS    0H
         LA    R5,MYUPT
         LA    R6,MYPSCB
         LA    R7,MYECT
         STM   R5,R7,MYCPPL+4
         XC    MYUPT(BCHLEN),MYUPT SET DUMMY BLOCKS TO X'00'
DOLINK   DS    0H
         LA    R1,MYCPPL
         LINK  EP=ABE
         L     R2,4(RD)           GET A(CALLER'S SA)
         ST    R1,24(R2)          SAVE RETURN CODE
         B     RETCTL
MVCPARM  MVC   CBUF+L'CMDCON(0),2(R1)
CMDCON   DC    C'SPF '
DSA      DSECT
         DS    18F
MYCPPL   DS    4F                 A(MYCBUF),A(MYUPT),A(MYPSCB),A(MYECT)
MYUPT    DS    6F
MYPSCB   DS    18F
MYECT    DS    10F
MYCBUF   DS    0F
BCHLEN   EQU   *-MYUPT
CBUFLEN  DS    H
CBUFOFF  DS    H
CBUF     DS    CL256
         DS    0D
DSALEN   EQU   *-DSA
         LTORG
         END
