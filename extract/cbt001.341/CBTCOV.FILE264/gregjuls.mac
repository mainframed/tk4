*$.HE GENERALIZED SUBROUTINES - GREGJUL
*$.HE CONVERT GREGORIAN TO JULIAN DATE - PLI
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/20/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  GREGJULS
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE CAN BE INVOKED FROM PLI TO CONVERT A DATE IN
*$GREGORIAN FORM (MMDDYY) TO JULIAN FORM (YYDDD).
*$.SP 1
*$.CE 1
*$CALL FORMAT
*$.NF
*$.IN 5
*$DCL JULIAN FIXED DEC(5,0);
*$DCL GREGORIAN CHAR(6) INIT('052074');
*$     /* GREGORIAN = MAY 20, 1974 */
*$ CALL GREGJUL(GREGORIAN,JULIAN);
*#.HE GENERALIZED SUBROUTINES - JULGREG
*#.HE CONVERT JULIAN TO GREGORIAN DATE - PLI
*#.PL 55
*#.PN 0
*#.PA
*#.SS
*#.NF
*#5/20/74
*#AUTHOR:  T. SMITH
*#SOURCE MODULE NAME:  GREGJULS
*#DOCUMENTATION ID:  *#
*#.FI L
*#.SP 5
*#THIS ROUTINE CAN BE INVOKED FROM PLI TO CONVERT A DATE IN
*#JULIAN FORM (YYDDD) TO GREGORIAN FORM (MMDDYY).
*#.SP 1
*#.CE 1
*#CALL FORMAT
*#.NF
*#.IN 5
*#DCL JULIAN FIXED DEC(5,0);
*#DCL GREGORIAN CHAR(6);
*#JULIAN = 74140;
*#     /* JULIAN = MAY 20, 1974 */
*# CALL JULGREG(GREGORIAN,JULIAN);
********************************************************************
*                                                                  *
*                  CONVERSION RTNE FROM GREGORIAN TO JULIAN        *
*                  AND VICE VERSA                                  *
*                                                                  *
********************************************************************
*                                                                  *
*               LINKAGE -                                          *
*                                                                  *
*                  ALL ENTRIES -                                   *
*                       RF = ADDR OF EP                            *
*                                                                  *
*                  ENTRIES WITH SUFFIX R OR WITHOUT SUFFIX         *
*                       RD = ADDR OF STD SAVE AREA                 *
*                       NOTE THAT ENTRIES WITH A SUFFIX R WILL     *
*                       RESTORE ONLY REGS 12 THRU 14.              *
*                                                                  *
*                  GREGJULP-                                       *
*                       R1 = A(PARM)                               *
*                       PARM = A(6 BYTES CONTAINING GREGORIAN DTE) *
*                              A(THREE BYTES TO BE FILLED IN WITH  *
*                                JULIAN DATE)                      *
*                                                                  *
*                  GREGJULR -                                      *
*                       R2 = A(6 BYTES CONTAINING GREGORIAN DTE)    *
*                       R3 = A(3 BYTES TO BE FILLED IN WITH JULIAN  *
*                             DTE)                                  *
*                  GREGJUL  -                                       *
*                       R1 = A(PLIST)                               *
*                       PLIST = A(SDV FOR 6 BYTES GREGORIAN DATE)   *
*                               A(3 BYTES TO BE FILLED IN WITH      *
*                                 JULIAN DTE)                       *
*                                                                   *
*                  JULGREG  - SAME AS ABOVE, EXCEPT THE JULIAN DATE *
*                  JULGREGR - IS FILLED IN BY THE CALLER INSTEAD OF *
*                  JULGREGP - THE GREGORIAN.                        *
*                                                                   *
*              TABLE DAYPERMO WILL BE SET ACCORDING TO WHETHER IT IS*
*              A LEAP YEAR OR NOT.                                  *
*                                                                   *
*********************************************************************
GREGJUL  CSECT
         ENTRY GREGJULP,GREGJULR,JULGREG,JULGREGP,JULGREGR
         TGSREQU
         USING *,RF
         BC    0,0
GREGJULP BC    0,4
JULGREG  BC    0,8
JULGREGP BC    0,12
         STM   RE,RC,12(RD)
         LM    R2,R3,0(R1)
         LH    R4,2(RF)
         SR    RF,R4
         LM    R5,R6,GREGJULP     R5=4,R6=8
         NR    R5,R4    IF EP=GREGJUL OR JULGREG, R5=0, ELSE R5=4
         BC    7,NOLOD  BR IF NOT 0
         L     R2,0(R2)
NOLOD    EQU   *
         BAL   RE,BRCK
         LM    R2,RC,28(RD)
         L     RE,12(RD)
         BR    RE
BRCK     EQU   *
         NR    R6,R4    IF EP=GREGJUL OR GREGJULP R5=0, ELSE R5=8
         BC    8,GREGJULR
         DROP  RF
JULGREGR EQU   *
         BALR  RF,0
         USING *,RF
         MVO   WORKDBL,0(1,R3)
         OI    WORKDBL+7,X'0F'
         UNPK  4(2,R2),WORKDBL
         LA    RB,SETTAB               ON RETURN, R9=A(DAYPNO)
         BALR  RA,RB
         ZAP   WORKDBL,1(2,R3)
         CVB   R8,WORKDBL
         LA    R5,1
         LA    R6,1
         LA    R7,12
LOOP     EQU   *
         SH    R8,0(R9)
         BC    12,GOTMON
         LA    R9,2(R9)
         BXLE  R5,R6,LOOP
         B     BADRET
GOTMON   EQU   *                       R5 = MM
         AH    R8,0(R9)                R8 = DD
         CVD   R8,WORKDBL
         OI    WORKDBL+7,X'0F'
         UNPK  2(2,R2),WORKDBL
         CVD   R5,WORKDBL
         OI    WORKDBL+7,X'0F'
         UNPK  0(2,R2),WORKDBL
         SR    RF,RF
         BR    RE
         DROP  RF
GREGJULR EQU   *
         BALR  RF,0
         USING *,RF
         PACK  WORKDBL,4(2,R2)         WORKDBL = YY
         MVO   0(2,R3),WORKDBL+6(2)
         LA    RB,SETTAB
         BALR  RA,RB
         PACK  WORKDBL,0(2,R2)         WORKDBL = MM
         CVB   R8,WORKDBL
         LA    R7,22(R9)
         LA    R6,2
         SR    R5,R5
LOOPA    EQU   *
         BCT   R8,LOOPB
         B     GOTMONA
LOOPB    EQU   *
         AH    R5,0(R9)
         BXLE  R9,R6,LOOPA
         B     BADRET
GOTMONA  EQU   *
         CVD   R5,WORKDBL
         ZAP   1(2,R3),WORKDBL
         PACK  WORKDBL,2(2,R2)         WORKDBL = DD
         AP    1(2,R3),WORKDBL
         SR    RF,RF
         BR    RE
BADRET   EQU   *
         LA    RF,4
         BR    RE
         DROP  RF
SETTAB   EQU   *
         USING *,RB
         L     R9,=V(DAYPMO)
         CVB   R6,WORKDBL
         SRDL  R6,32
         LA    R8,4
         DR    R6,R8                   R6 = REM
         LTR   R6,R6
         BC    8,ISLEAP
         NI    3(R9),X'FE'
         BR    RA
ISLEAP   EQU   *
         OI    3(R9),X'01'
         BR    RA
WORKDBL  DS    D
         ORG   *-1
         DC    X'0F'
DAYPMO   CSECT
         DC    H'31'    JAN
         DC    H'28'    FEB (IF LEAP YEAR, = 29)
         DC    H'31'    MAR
         DC    H'30'    APR
         DC    H'31'    MAY
         DC    H'30'    JUN
         DC    H'31'    JUL
         DC    H'31'    AUG
         DC    H'30'    SEP
         DC    H'31'    OCT
         DC    H'30'    NOV
         DC    H'31'    DEC
         END
