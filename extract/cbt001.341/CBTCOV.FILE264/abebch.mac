 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /**********************************************************************
 *                                                                     *
 * ABEBCH IS CALLED FROM THE ABE MODULE IF IT RECOGNIZES THAT ABE IS   *
 * BEING RUN AS A BATCH UTILITY.                                       *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABEBCH) PARMS(RHHDRPT,CPPLCBUF,SYSIN,SYSINSW);
   %INCLUDE ABESUBA;

   DCL CPPLCBUF PTR;
   DCL SYSIN FILE;
   DCL SYSINSW BIT(1);
   DCL SETCAPS ENTRY;
   ON UNDEFINEDFILE(SYSIN) SYSINSW  = '0'B;
   ON ENDFILE(SYSIN)       SYSINSW  = '0'B;
   #PROMPT    = BDOPRM; /* PROMPT FOR INPUT - BATCH */
   #TERM      = BTERMWRT; /* TERM OUTPUT - BATCH */
   #PROMPTL   = BDOPRMLO; /* TERM INP - LO - BA*/
   #STACK     = BDOSTK; /* TSO STACK INTERFACE */
   CALL #ABEIN(RHHDRPT,CPPLCBUF);
   CALL #ABEA(RHHDRPT);

 BDOPRM: PROC(PRMPT,INP);
  /*********************************************************************
  * #DOPRM, SUBENT(8), SUBENT(52) FOR BATCH                            *
  *********************************************************************/
   DCL PRMPT CHAR(80) VAR;
   DCL INP CHAR(240) VAR;
   PUT SKIP EDIT(PRMPT)(A);
   IF SYSINSW
     THEN READ FILE(SYSIN) INTO(INP);
   IF SYSINSW
     THEN IF LENGTH(INP)>72
       THEN INP=SUBSTR(INP,1,72);
       ELSE;
     ELSE INP='ALL END NOSAVE';
   CALL SETCAPS(INP);
   END BDOPRM;

 BDOPRMLO: PROC(PRMPT,INP);
  /*********************************************************************
  * #PROMPTL, SUBENT(30), SUBENT(54) FOR BATCH                         *
  *********************************************************************/
   DCL PRMPT CHAR(80) VAR;
   DCL INP CHAR(240) VAR;
   PUT SKIP EDIT(PRMPT)(A);
   IF SYSINSW
     THEN READ FILE(SYSIN) INTO(INP);
   IF SYSINSW
     THEN IF LENGTH(INP)>72
       THEN INP=SUBSTR(INP,1,72);
       ELSE;
     ELSE INP='ALL END NOSAVE';
   END BDOPRMLO;

 BDOSTK: PROC(CMD);
  /*********************************************************************
  * #STACK, SUBENT(41), SUBENT(55) FOR BATCH.                          *
  *********************************************************************/
   DCL CMD CHAR(*) VAR;
   PUT SKIP LIST('ERROR - #STACK IS NOT SUPPORTED IN BATCH MODE');
   SIGNAL ERROR;
   END BDOSTK;

 BTERMWRT: PROC(TMSG);
  /*********************************************************************
  * #TERM, SUBENT(29), SUBENT(53) FOR BATCH EXECUTION                  *
  *********************************************************************/
   DCL TMSG CHAR(*) VAR;
   PUT SKIP EDIT(TMSG)(A);
   END BTERMWRT;
   END ABEBCH;
