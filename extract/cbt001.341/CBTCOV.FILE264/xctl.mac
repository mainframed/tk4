*****************************************************************
*                                                                     *
*              XCTL - TSO COMMAND PROCESSOR
*                                                                  *
*************************************************************
*
* USE THE XCTL COMMAND AS A SHORT FORM OF THE CALL COMMAND
* TO CAUSE THE LOADING AND EXECUTION OF MODULES FROM THE
* LINK LIST LIBRARIES (SYS1.LINKLIB, SYS2.USERLIB, SYS1.CMDLIB
* SYS2.CMDLIB, ETC.).
*
*************************************************************
*
* XCTL MODULE 'PARAMETER'
*
*        MODULE IS THE NAME OF THE MODULE TO BE EXECUTED
*        'PARAMETER' IS OPTIONAL AND SPECIFIES PARAMETERS TO BE
*              PASSED TO THE CALLED PROGRAM (FORMAT IS SAME AS THOSE
*              PASSED IN THE PARM FIELD ON A BACKGROUND EXEC STMT.
*
*************************************************************
XCTL     CSECT
         SAVER RETCTL,LV=DSALEN,SP=1
         TGSREQU
         USING DSA,RD
         LA    RB,PPLADR               RB = A(PARSE PARAMETER LIST)
         USING PPL,RB
         USING CPPL,R1
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         XC    CPECB,CPECB
         LA    R2,CPECB
         ST    R2,PPLECB
         L     R2,=V(PCL)
         ST    R2,PPLPCL
         LA    R2,CPANS
         ST    R2,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         XC    PPLUWA,PPLUWA
         LA    R1,PPL
         LINK  EP=IKJPARS
         L     RA,CPANS
         USING IKJPARMD,RA
         LH    R3,PARMPDE+4            R3 = LEN PARM
         LA    R0,14(R3)               R0 = LEN FOR GETMAIN
         LA    R1,1                    R1 = SP
         SLL   R1,24                   SHIFT TO HI-BYTE
         OR    R0,R1                   OR INTO R0
         GETMAIN R,LV=(0)
         LR    R2,R1                   SAVE A(GOTTEN CORE)
         LA    R1,12(R1)               R1 = A(PARM LEN FLD)
         ST    R1,8(R2)                SET PARM LIST
         OI    8(R2),X'80'             SET EOL
         STH   R3,0(R1)                SET PARM LEN FLD
         LTR   R3,R3                   IS IT NULL PARM
         BC    8,DOXCTL                BR IF YES
         BCTR  R3,0                    LESS 1 FOR EX
         L     R4,PARMPDE              R4 = A(PARM)
         EX    R3,MVC
         B     DOXCTL
MVC      MVC   2(0,R1),0(R4)           MOVE PARM FLD
DOXCTL   DS    0H
         MVC   0(8,R2),=CL8' '           CLEAR PGM NAME TO BLANKS
         L     R5,PGMPDE               R5 = A(PGM NAME)
         LH    R6,PGMPDE+4             R4 = LEN NAME
         BCTR  R6,0
         EX    R6,MVCNAM
         B     DOIT
MVCNAM   MVC   0(0,R2),0(R5)
DOIT     DS    0H
         LA    R0,8                    R0=LEN FOR GETMAIN
         LA    R1,1                    R1=SP #
         SLL   R1,24
         OR    R0,R1
         GETMAIN R,LV=(0)
         LA    RF,0(R1)
         XC    0(8,RF),0(RF)           CLEAR XCTL LIST
         L     R0,0(RD)
         LR    R1,RD
         L     RD,4(RD)
         ST    RF,16(RD) SAVE RF DRUING GETMAIN
         FREEMAIN R,LV=(0),A=(1)
         L     RF,16(RD) RESTORE RF=A(XCTL PARM LIST)
         LA    R1,8(R2)
         XCTL  (2,12),EPLOC=(2),MF=(E,(1)),SF=(E,(15))
         LTORG
PCL      IKJPARM
PGMPDE   IKJIDENT 'MODULE',MAXLNTH=8,FIRST=ALPHA,OTHER=ALPHANUM,       C
               PROMPT='ENTER NAME OF MODULE TO BE EXECUTED'
PARMPDE  IKJPOSIT QSTRING
         IKJENDP
         IKJPPL
PPLLEN   EQU   *-PPL
DSA      DSECT
         DS    CL72 PAD FOR REG SAVE AREA
CPECB    DS    F
CPANS    DS    F
PPLADR   DS    0F
DSALEN   EQU   *-DSA+PPLLEN
         IKJCPPL
         END
