 /* COPYRIGHT 1986 BY THOMAS GLEN SMITH */
 /* ABE064J - GDDMSASR */
 /**********************************************************************
 *                                                                     *
 * CALLED AFTER SAS/GRAPH PROCESSING TO READ AND PROCESS COMPLOT       *
 * GRAPHICS INSTRUCTIONS.                                              *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABE064J) RENT(NO)
   PARMS(RHPTR,RHHDRPT,DDN,HSIZE,VSIZE);
   %INCLUDE ABESUBA;

   DCL DDN CHAR(8);
   DCL (HSIZE,VSIZE) DEC FLOAT(6);

   DCL TEXT CHAR(256) VAR;
   DCL INREC CHAR(256) VAR;
   DCL 1 SYMBREC UNALIGNED BASED(ADDR(INREC)),
     2 PAD FIXED BIN(15,0),
     2 KEY CHAR(4),
     2 XN DEC FLOAT(6),
     2 YN DEC FLOAT(6);
   DCL 1 PLOTREC UNALIGNED BASED(ADDR(INREC)),
     2 PAD CHAR(14),
     2 IPEN FIXED BIN(31,0);
   DCL 1 NEWPREC UNALIGNED BASED(ADDR(INREC)),
     2 PAD CHAR(6),
     2 NPEN FIXED BIN(31,0);
   DCL (XF,YF) DEC FLOAT(6);

   XF=100.0/HSIZE;
   YF=100.0/VSIZE;
   OPEN FILE(RH_SEQ_1) TITLE(DDN) RECORD INPUT;
   ON ENDFILE(RH_SEQ_1) GO TO EOFIN;
   I=0;
   DO FOREVER=1 REPEAT FOREVER;
     /* FIRST FIND THE START OF NEXT GRAPH */
     DO UNTIL(I=1);
       READ FILE(RH_SEQ_1) INTO(INREC);
       IF KEY='PLOT'
         THEN IF IPEN>0 & IPEN^=999
           THEN I=1;
       END;
     DO UNTIL(I=0);
       SELECT(KEY);
         WHEN('NEWP')
           CALL GSCOL(NPEN);
         WHEN('PLOT')
           IF IPEN<0 × IPEN=999
             THEN I=0; /* DONE WITH THIS GRAPH */
             ELSE CALL DOPLOT(XF*XN,XF*YN,IPEN);
         OTHERWISE;
         END; /* SELECT */
       IF I=1
         THEN READ FILE(RH_SEQ_1) INTO(INREC);
       END;
     END;
 EOFIN:;
   CLOSE FILE(RH_SEQ_1);

 DOPLOT: PROC(XN,YN,IPEN);
 /**********************************************************************
 *                                                                     *
 * CALLED TO MOVE THE PEN, AND PERHAPS DRAW AT THE SAME TIME.          *
 *                                                                     *
 **********************************************************************/
   DCL (XN,YN) DEC FLOAT(6);
   DCL (I,IPEN) FIXED BIN;

   SELECT(IPEN);
     WHEN(3,-3,99) CALL GSMOVE(XN,YN);
     WHEN(2)       CALL GSLINE(XN,YN);
     OTHERWISE SIGNAL ERROR;
     END; /* SELECT */
   END DOPLOT;

   %INCLUDE SYSLIB(ADMUPING);
   END ABE064J;
