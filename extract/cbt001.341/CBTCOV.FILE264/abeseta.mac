 /* COPYRIGHT 1988 BY THOMAS GLEN SMITH */
 /**********************************************************************
 * ABESETA - ALIAS #SETA.                                              *
 * CALLED TO SET THE NUMERIC VALUE OF A SYMBOLIC VARIABLE.             *
 * PARAMETERS ARE -                                                    *
 *   RHHDRPT - ABE CVT.                                                *
 *   TREEHDR - HEAD OF TREE.  NORMALLY POINTS TO RH_SYMTREE.           *
 *   SYMBOL CHAR(8) CONTAING SYMBOL NAME.                              *
 *   X - DEC FLOAT(16) CONTAINING VALUE.                               *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABESETA)  PARMS(RHHDRPT,TREEHDR,SYMBOL,X) CHECK(YES);
   %INCLUDE ABESUBA;
   %INCLUDE ABESREC;
   DCL 1 RHNAMESTR BASED(RHNAMPTR),
   %INCLUDE ABERHN;

   DCL SYMBOL CHAR(8);
   DCL X DEC FLOAT(16);
   DCL TREEHDR PTR;

   DCL ABESET ENTRY(PTR,PTR);
   DCL (PREFIX,SUFFIX) ENTRY RETURNS(FIXED BIN(15,0));
   DCL (P,Q) PTR;

   P=#SRCH(TREEHDR,SYMBOL,Q);
   IF P=NULL
     THEN DO; /* NOT FOUND */
       I=80; /* DEFAULT CHARACTER LENGTH */
       ALLOCATE SYMREC SET(P);
       P->SRLCHILD,P->SRRCHILD=NULL;
       P->SRNAME=SYMBOL;
       P->SRBF=0;
       CALL ABESET(TREEHDR,P);
       END;
   P->SRCURL=0;
   P->SRTYPE=1;
   P->SRFL=X;
   RETURN;

 ABESETY: ENTRY (RHHDRPT,TREEHDR,SYMBOL,X);
   /* ENTRY FOR CLIST PROCESSING */
   DCL VALUE CHAR(256) VAR;
   DCL GBLSW BIT(1) STATIC INIT('0'B);
   DCL (CCC,TXT) CHAR(256) VAR;
   DCL (IN,OUT) PTR;
   DCL STR CHAR(8) BASED(STRPTR);

   ALLOC RHNAMESTR SET(IN);
   IN->RHNTYPE=1; /* FLOAT */
   IN->RHNLNG=8;
   IN->RHNFD=0;
   ALLOC RHNAMESTR SET(OUT);
   OUT->RHNTYPE=8; /* BEST */
   OUT->RHNLNG=256;
   OUT->RHNFD=0;
   CCC=ADDR(X)->STR;
   TXT=#CONVERT(CCC,IN,OUT);
   IF TXT^=''
     THEN DO;
       TXT=SUBSTR(TXT,PREFIX(TXT,' '));
       TXT=SUBSTR(TXT,1,SUFFIX(TXT,' '));
       END;
   CALL #ABESTP(SYMBOL,TXT);
   FREE IN->RHNAMESTR;
   FREE OUT->RHNAMESTR;
   RETURN;
   END ABESETA;
