 % NOPRINT;
 /* @FULLSCP */
 /* THIS TEXT SHOULD BE INCLUDED TO USE THE 3277 FULL SCREEN SUPPORT
    FOR PLI UNDER TSO.  SEE DOCUMENTATION FOR THE @SCREEN, @PANEL,
    @FIELD, @ENDPNL, @DOPNL, AND @FLDMOD MACROS.  NOTE THAT IF YOU
    ARE NOT USING THE TSO COMMAND PROCESSOR SUPPORT FOR PLI, IN OTHER
    WORDS YOU HAVEN'T INCLUDED CPINIT (OR CPINIT1 AND CPINIT2),
    THEN YOU SHOULD INCLUDE @FULLSCR INSTEAD OF @FULLSCP. */
         DCL SCRWRIT ENTRY;
         DCL 1 @PNLCALL,
                   /* THIS STRUCTURE IS THE ONLY VARIABLE WHICH
                      NEEDS TO BE PASSED TO SCRWRIT.  */
              2 @CURPNL FIXED BIN(15,0) INIT(0),
                   /* NUMBER OF THE PANEL CURRENTLY WRITTEN ON THE
                      SCREEN */
              2 @NXTPNL FIXED BIN(15,0) INIT(1),
                   /* NUMBER OF THE NEXT PANEL THE USER WANTS
                      DISPLAYED.  INITIAL DEFAULT IS 1 */
              2 @ARYPTR PTR,
                   /* POINTER TO @ARYCNT */
              2 @AID BIT(8),
                   /* WILL BE SET BY SCRWRIT TO THE AID BYTE
                      READ IN FROM THE 3270
                      X'7D' = ENTER KEY
                      X'F1' = PF1 KEY
                      X'F2' = PF2 KEY
                      X'F3' = PF3 KEY
                      X'F4' = PF4 KEY
                      X'F5' = PF5 KEY
                      X'F6' = PF6 KEY
                      X'F7' = PF7 KEY
                      X'F8' = PF8 KEY
                      X'F9' = PF9 KEY
                      X'7A' = PF10 KEY
                      X'7B' = PF11 KEY
                      X'7C' = PF12 KEY
                      X'E6' = OPER ID CARD RDR
                      X'7E' = SELECTER PEN ATTN
                      X'6C' = PA1 KEY
                      X'6E' = PA2 KEY (CANCEL)
                      X'6B' = PA3 KEY
                      X'6D' = CLEAR KEY
                      X'F0' = TEST REQ KEY */
              2 @CURLOC FIXED BIN(15,0) INIT(0),
                   /* WILL BE SET BY SCRWRIT TO THE LOCATION OF THE
                      CURSOR ON INPUT.  THE VALUE RETURNED WILL BE
                      A NUMBER FROM 0 TO 3530. */
              2 @CHGCURS FIXED BIN(15,0) INIT(-1),
                   /* SET THIS FIELD TO THE DESIRED POSITION IF YOU
                      DON'T WANT THE DEFAULT SET ON THE @PANEL MACRO.
                      THIS FIELD IS RESET BY SCRWRIT EVERY TIME IT
                      IS INVOKED. -1 INDICATES TAKE THE DEFAULT.
                      OTHERWISE, VALUE S/B FROM 0 TO 3530. */
              2 @CALLFLGS BIT(8) INIT('0'B);
                   /* BIT 0=1 IF PANEL IS TO BE WRITTEN ONLY.  THAT IS,
                      SCRWRIT IS NOT TO READ FROM THE SCREEN AFTER
                      WRITING OUT THE SPECIFIED PANEL. */
         @ARYPTR = ADDR(@ARYCNT);
 /* @@PNLSTR WILL CONTAIN THE DECLARES FOR THE STATIC PORTION OF
    THE PANEL DESCRIPTIONS. */
         % DCL @@PNLSTR CHAR;
         % @@PNLSTR = '';
 /* @@PNLSTA WILL CONTAIN THE DECLARES FOR THE STATIC PORTION OF
    THE PANEL DESCRIPTIONS */
         % DCL @@PNLSTA CHAR;
         % @@PNLSTA = '';
 /* @@SBASAV WILL CONTAIN THE DECLARES FOR THE STATIC PORTION
    INVOLVING THE PANEL DESCRIPTION LENGTH FIELDS. */
         % DCL @@SBASAV CHAR;
         % @@SBASAV = '';
 /* @@PNLNUM IS A NUMBER USED TO UNIQUELY IDENTIFY EACH PANEL.
    ITS VALUE IS INCREMENTED AND ASSIGNED TO THE VARIABLE @PNLNUM
    EACH TIME THE @PANEL MACRO IS ISSUED. */
         % DCL @@PNLNUM FIXED;
         % @@PNLNUM = 0;
 /* @@DYNOFF IS USED TO RECORD THE CURRENT OFFSET IN @PNL_DYN */
         % DCL @@DYNOFF FIXED;
         % @@DYNOFF = 0;
 /* @@PNLNAME IS USED TO RECORD THE CURRENT PANEL NAME */
         % DCL @@PNLNAME CHAR;
 /* @@INOFF IS USED TO RECORD THE CURRENT OFFSET IN PANEL_IN */
         % DCL @@INOFF FIXED;
         % @@INOFF = 0;
 /* @@OUTOFF IS USED TO RECORD THE CURRENT OFFSET IN PANEL_OUT */
         % DCL @@OUTOFF FIXED;
         % @@OUTOFF = 0;
 /* @@FLDNUM IS A NUMBER USED TO UNIQUELY IDENTIFY EACH FIELD IN A
    PANEL.  ITS VALUE IS INCREMENTED AND ASSIGNED TO THE VARIABLE
    @FLDNUM. */
         % DCL @@FLDNUM FIXED;
         % @@FLDNUM = 0;
 /* @@INCNT IS A COUNTER OF UNPROTECTED FIELDS IN A PANEL */
         % DCL @@INCNT FIXED;
 /* @@INDYN WILL CONTAIN THE DECLARES FOR THE PANEL_IN SUBSTRUCTURE */
         % DCL @@INDYN CHAR;
 /* @@OUTDYN WILL CONTAIN THE DECLARES FOR THE PANEL_OUT SUBSTRUCTURE*/
         % DCL @@OUTDYN CHAR;
 /* @@PNLARY WILL CONTAIN THE INITIALIZATION VALUES FOR ARRAY @PNLARY.*/
         % DCL @@PNLARY CHAR;
         % @@PNLARY = '';
 /* @@STATOFF IS USED TO RECORD THE CURRENT OFFSET IN @PNLSTR.  IT
    IS USED TO DETERMINE THE OFFSET TO THE START OF A PANEL
    DEFINITION */
         % DCL @@STATOFF FIXED;
         % @@STATOFF = 0;
 /* @@PNLLEN IS USED TO RECORD THE LENGTH OF EACH PANEL DEFINITION IN
    @PNLSTR */
         % DCL @@PNLLEN FIXED;
 /* @@PNLDYN WILL CONTAIN THE DECLARES FOR THE @PNL_DYN STRUCTURE */
         % DCL @@PNLDYN CHAR;
         % @@PNLDYN = 'DCL 1 @PNL_DYN UNALIGNED';
 /* @ROWS IS SET TO THE NUMBER OF ROWS ON THE SCREEN TO BE DEFINED.
    THE DEFAULT VALUE FOR @ROWS IS 24, BUT YOU CAN CHANGE THIS TO
    ANY VALUE YOU LIKE BEFORE USING ANY OF THE SCREEN DEFINITION
    MACROS SUCH AS @PANEL, @SCREEN, ETC. */
         % DCL @ROWS FIXED;
         % @ROWS = 24;
 /* @COLS IS SET TO THE NUMBER OF COLUMNS ON THE SCREEN TO BE DEFINED.
    THE DEFAULT VALUE FOR @COLS IS 80, BUT YOU CAN CHANGE THIS TO
    ANY VALUE YOU LIKE BEFORE USING ANY OF THE SCREEN DEFINITION
    MACROS SUCH AS @PANEL, @SCREEN, ETC. */
         % DCL @COLS FIXED;
         % @COLS = 80;
         % INCLUDE @SBACON;
         % INCLUDE @PANEL;
         % INCLUDE @FIELD;
         % INCLUDE @ENDPNL;
         % INCLUDE @SCREEN;
         % INCLUDE @FLDMOD;
         % INCLUDE @DOPNL;
         % PRINT;
