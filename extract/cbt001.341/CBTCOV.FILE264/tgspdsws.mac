TGSPDSWR CSECT                                                      TGS
********************************************************************TGS
*                  ON ENTRY - REG 0 = ADDR OF RECORD               *TGS
*                             REG 1 = ADDR OF DATA BLOCK CONTAINING*TGS
*                                     A DCB AND VARIOUS OTHER      *TGS
*                                     VARIABLE DATA                *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         LR    RA,R0                   SAVE RECORD ADDR             TGS
         LR    RB,R1                                                TGS
         USING DATABLK,RB                                           TGS
         TM    INITSW,X'80'            HAS INITIALIZATION BEEN DONE TGS
         BC    1,BEGIN                 BR IF YES                    TGS
         OI    INITSW,X'80'            SET INITSW ON                TGS
         MVC   PDSDCB+40(8),0(R1)      MOVE DDNAME TO DCB           TGS
         LA    R2,NMDCBEX                                           TGS
         ST    R2,NMEXITS+4                                         TGS
         MVI   NMEXITS+4,X'85'         SET EXIT LIST                TGS
         LA    1,PDSDCB                                             TGS
         ST    1,OLIST                                              TGS
         MVI   OLIST,X'8F'                                          TGS
         CNOP  0,4                                                  TGS
         BAL   1,*+8                                                TGS
OLIST    DS    F                                                    TGS
         SVC   19                      ISSUE OPEN SVC               TGS
         TM    PDSDCB+48,X'10'         IS IT GOOD OPEN              TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1,DUMP                                               TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,PDSDCB+62            0=BLKSIZE                    TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,NMWRITEP             STORE ADDR OF BUFFER         TGS
         ST    R1,NMWRITET             STORE ADDR OF BUFFER         TGS
STOWCK   EQU   *                                                    TGS
         TM    BUFEMPSW,X'80'          IS THE BUFFER EMPTY          TGS
         BC    8,STOWCK2               BR IF YES                    TGS
         L     RE,NMWRITET                                          TGS
         L     RF,NMWRITEP                                          TGS
         SR    RE,RF                   RE = LENGTH OF LAST RECORD   TGS
         MVC   SAVBLKSI,PDSDCB+62      SAVE REGULAR BLKSIZE         TGS
         STH   RE,PDSDCB+62            BLKSIZE=LENGTH OF LAST RECORDTGS
         BAL   R9,WRITPDS              GO WRITE THE LAST RECORD     TGS
         MVC   PDSDCB+62(2),SAVBLKSI   RESTORE NORMAL BLKSIZE       TGS
         MVC   NMBLOCKT,NMBLOCKP                                    TGS
         MVC   NMWRITET,NMWRITEP                                    TGS
         MVI   BUFEMPSW,X'00'                                       TGS
STOWCK2  EQU   *                                                    TGS
         TM    STOWSW,X'80'            IS A STOW PENDING            TGS
         BC    8,STOWINIT              BR IF NOT                    TGS
         STOW  PDSDCB,STOWAREA,A                                    TGS
         LTR   RF,RF                   WAS IT GOOD STOW             TGS
         BC    8,STOWINIT                                           TGS
         ABEND 2,DUMP                                               TGS
STOWINIT EQU   *                                                    TGS
         TM    EODSW,X'80'             IS IT EOD                    TGS
         BC    1,CLOSEUP               BR IF YES                    TGS
         MVI   STOWSW,X'80'            SET STOWSW                   TGS
         MVI   STOWAREA,X'40'                                       TGS
         MVC   STOWAREA+1(73),STOWAREA                              TGS
         MVC   STOWAREA(8),63(RA)      MOVE MEMBER NAME             TGS
         MVI   STOWAREA+11,X'06'       SET C BYTE                   TGS
         MVC   STOWAREA+16(8),72(RA)   SET SSI-2                    TGS
         MVC   TRSSI,=8C'0'                                         TGS
         LA    R1,15(RA)               R1 = ADDR NAME + 1           TGS
LOOP     LA    R1,1(R1)                                             TGS
         CLI   0(R1),C','              IS IT COMMA                  TGS
         BC    7,LOOP                  BR IF NOT                    TGS
         CLI   7(R1),C','              IS SSE THERE                 TGS
         BC    7,NOSSI                                              TGS
         MVC   TRSSI,8(R1)                                          TGS
         LA    R1,TRPARM                                            TGS
         L     RF,=V(TRIN)                                          TGS
         LA    RE,STOWAREA+12                                       TGS
         ST    RE,OUT                                               TGS
         BALR  RE,RF                                                TGS
NOSSI    EQU   *                                                    TGS
         B     RETCTL                                               TGS
CLOSEUP  EQU   *                                                    TGS
         LA    R1,PDSDCB                                            TGS
         ST    R1,CLIST                                             TGS
         MVI   CLIST,X'80'                                          TGS
         CNOP  0,4                                                  TGS
         BAL   1,*+8                                                TGS
CLIST    DS    F                                                    TGS
         SVC   20                      ISSUE CLOSE SVC              TGS
         B     RETCTL                                               TGS
BEGIN    EQU   *                                                    TGS
         CLC   0(8,RA),=CL8'./'        IS IT CTL STMT
         BC    7,WRITREC               BR IF NOT                    TGS
         CLC   9(5,RA),=CL5'END'       IS IT EOD                    TGS
         BC    7,STOWCK                BR IF NOT                    TGS
         OI    EODSW,X'80'             SET EODSW                    TGS
         B     STOWCK                                               TGS
WRITREC  EQU   *                                                    TGS
         CLC   0(8,RA),=CL8'*/'
         BC    7,NOTCTL                                             TGS
         MVI   0(RA),C'.'                                           TGS
NOTCTL   EQU   *                                                    TGS
         BAL   R9,WRITOUT                                           TGS
         B     RETCTL                                               TGS
WRITOUT  EQU   *                                                    TGS
         OI    BUFEMPSW,X'80'                                       TGS
         LH    RE,NMBLOCKT             LOAD RECORD CNT              TGS
         BCT   RE,RRLBNMA              BR IF BLOCK ISNT COMPLETE    TGS
         L     RE,NMWRITET                                          TGS
         MVC   0(80,RE),0(RA)          MOVE RECORD TO BUFFER        TGS
WRITPDS  EQU   *                                                    TGS
         L     R8,NMWRITEP                                          TGS
         WRITE DECB,SF,,(8),'S',MF=E                                TGS
         CHECK DECB                                                 TGS
         MVC   NMWRITET,NMWRITEP       RESTORE RECORD ADDR          TGS
         MVC   NMBLOCKT,NMBLOCKP       RESTORE RECORD CNT           TGS
         MVI   BUFEMPSW,X'00'          INDICATE EMPTY BUFFER        TGS
         BR    R9                      RETURN TO CALLER             TGS
RRLBNMA  EQU   *                                                    TGS
         STH   RE,NMBLOCKT             STORE ADJUSTED RECORD CNT    TGS
         L     RE,NMWRITET                                          TGS
         MVC   0(80,RE),0(RA)          MOVE RECORD TO BUFFER        TGS
         LA    RE,80(RE)               ADD 80 TO ADDRESS            TGS
         ST    RE,NMWRITET             STORE ADDR FOR NEXT RECORD   TGS
         BR    R9                      RETURN TO CALLER             TGS
NMDCBEX  DS    0H                                                   TGS
         LH    R9,PDSDCB+62            R9 = BLKSIZE                 TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BC    8,NMNOSIZE              BR IF YES                    TGS
         SR    R8,R8                                                TGS
         D     R8,=F'80'                                            TGS
         STH   R9,NMBLOCKP             THIS IS THE                  TGS
         STH   R9,NMBLOCKT             BLOCKING FACTOR              TGS
         LTR   R8,R8                   IS BLKSIZE MULTIPLE OF 80    TGS
         BCR   8,RE                    BR IF YES                    TGS
NMNOSIZE EQU   *                                                    TGS
         MVC   PDSDCB+62(2),=H'3200'   SET BLKSIZE TO 3200          TGS
         MVC   NMBLOCKP,=H'40'         SET BLOCKING                 TGS
         MVC   NMBLOCKT,=H'40'         FACTOR TO 40                 TGS
         BR    RE                                                   TGS
TRPARM   DS    0F                                                   TGS
         DC    A(IN)                                                TGS
         DC    A(OUT)                                               TGS
IN       DC    A(TRSSI)                                             TGS
         DC    H'0'                                                 TGS
         DC    H'8'                                                 TGS
OUT      DC    F'0'                                                 TGS
         DC    H'0'                                                 TGS
         DC    H'4'                                                 TGS
TRSSI    DS    CL8                                                  TGS
         LTORG                                                      TGS
DATABLK  DSECT                                                      TGS
         TGSPDSBK                                                   TGS
         END
