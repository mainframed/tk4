*$.HE GENERALIZED UTILITIES - TESTGDG
*$.HE SET RETURN CODE TO NUMBER OF GENERATIONS IN GDG
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$4/5/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  TESTGDG
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$USE THIS UTILITY TO SET THE RETURN CODE TO THE NUMBER OF
*$GENERATIONS CURRENTLY CATALOGED IN A GENERATION DATA GROUP.
*$FORMAT FOR INVOKING THE UTILITY IS AS FOLLOWS -
*$.SP 1
*$.IN 5
*$// EXEC PGM=TESTGDG,REGION=30K,PARM='INDEX'
*$.SP 1
*$.IN 0
*$INDEX IS THE NAME OF THE GENERATION DATA GROUP TO BE TESTED.
*$IF ANY ERROR IS ENCOUNTERED WHILE SETTING THE RETURN, THIS
*$UTILITY WILL ABEND WITH A USER CODE OF 999.
TESTGDG  CSECT
         SAVES RETCTL
         TGSREQU
         L     R2,0(R1)                R2=A(LEN(GDGNAM))
         LH    R3,0(R2)                R3=LEN(GDGNAM)
         LTR   R3,R3                   WAS PARM SUPPLIED?
         BC    8,BADRET                BR IF NOT
         LA    R2,2(R2)                R2=A(GDGNAM)
         LR    RF,R2                   RF=COMPARAND
         LA    RE,1
         LNR   RE,RE                   RE=-1=INCREMENT
         LA    R1,0(R2,R3)
         BCTR  R1,0                    R1=FIRST OPERAND
DSNQLUP  DS    0H
         CLI   0(R1),C'.'              IS IT LAST PERIOD?
         BC    8,DSNQGP                BR IF YES
         BXH   R1,RE,DSNQLUP           GO CHECK NEXT CHAR
         LR    RE,R2
         BCTR  R3,0
         EX    R3,MVCQUAL              MOVE SINGLE QUAL TO QUAL
         SR    R0,R0
         ST    R0,BLK+8                SET CVOL PTR TO ZERO
         B     GETBLKA
DSNQGP   DS    0H
         LR    R4,R1
         SR    R4,RF                   R4=LEN(DSN)-(LEN(LAST QUAL)+1)
         BC    8,BADRET                BR IF BAD DSN
         LR    R5,R3
         SR    R5,R4                   R5=LEN(LAST QUAL) + 1
         SH    R5,=H'2'                SET UP FOR EX
         BC    4,BADRET                BR IF BAD DSN
         CH    R5,=H'7'                IS LEN GT 8?
         BC    2,BADRET                BR IF YES
         LA    RE,1(R4,RF)             RE=A(LAST QUAL)
         EX    R5,MVCQUAL              MOVE LAST QUAL TO QUAL
         BCTR  R4,0                    GET SET FOR EX
         EX    R4,MVCINDEX             MOVE REST OF DSN TO INDEX
         LOCATE LOCCAMG                LOCATE INDEX BLOCK
         CH    RF,=H'12'               IS IT PROPER RC?
         BC    7,BADRET                BR IF NOT
         MVC   CVOL(6),LOCAREA+259
         B     DOBLK
GETBLK   DS    0H
         CLC   8(3,R1),=AL3(0)
         BC    8,BADRET
         MVC   CTLGTTR(3),8(R1)
GETBLKA  DS    0H
         LOCATE BLK
         LTR   RF,RF
         BC    7,BADRET
DOBLK    DS    0H
         LA    R1,LOCAREA+2
DOBLKA   DS    0H
         CLC   0(8,R1),QUAL            IS IT PROPER ENTRY?
         BC    8,GOTCTLGI              BR IF YES
         CLC   0(8,R1),=8X'FF'         IS IT INDEX LINK?
         BC    8,GETBLK                BR IF YES
         SR    RF,RF                   RF = 0
         IC    RF,11(R1)               RF = HCNT
         SLL   RF,1                    *2
         LA    R1,12(RF,R1)
         B     DOBLKA
GOTCTLGI DS    0H
         CLI   11(R1),X'02'            IS IT GDG?
         BC    7,BADRET                BR IF NOT
         LH    RF,14(R1)               RF=# GENERATIONS
         B     RETCTL
BADRET   DS    0H
         ABEND 999,DUMP
MVCQUAL  MVC   QUAL(0),0(RE)
MVCINDEX MVC   INDEX(0),0(R2)
QUAL     DC    CL8' '
INDEX    DC    CL44' '
CVOL     DC    CL6' '
CTLGTTR  DC    AL3(1)
BLK      CAMLST BLOCK,CTLGTTR,CVOL,LOCAREA
LOCCAMG  CAMLST NAME,INDEX,,LOCAREA
LOCAREA  DS    0D,CL265
         LTORG
         END
