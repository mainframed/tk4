 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABE069 - SCANPDS,ML,CL,MLL,CLL,VTOC */
 /* 1        2       3  4  5   6   7    */
   %INCLUDE ABESUB;
 ABESUB(ABE069) PARS(0000000) PARSEVAL(NO) RENT(NO) CHECK(YES);
   %INCLUDE ABESUBA;

   DCL SYSDELE ENTRY(CHAR(8));
   DCL SUFFIX ENTRY(CHAR(*),CHAR(*)) RETURNS(FIXED BIN(15,0));
   DCL (OPT,RHP) PTR;
   DCL INDSN CHAR(80) VAR;

   ON CONDITION(BADALC) GO TO BADRET;
   SELECT(CMDINDX);
     WHEN(1,2) CALL SCANPDS;
     WHEN(3)   CALL ML;
     WHEN(4)   CALL CL;
     WHEN(5)   CALL MLL;
     WHEN(6)   CALL CLL;
     OTHERWISE CALL VTOC;
     END; /* SELECT */
   IF CMDINDX=1 × CMDINDX=2 × CMDINDX=3 × CMDINDX=5
     THEN DO;
       I=#CMD('END N',RHP,RHHDRPT);
       CALL SYSDELE('FAMPDSR');
       END;
 BADRET:;

 ALCIN: PROC(FAM,VOL);
   DCL (FAM,VOL) CHAR(*);
   DCL VOLSTR CHAR(80) VAR;

   SELECT;
     WHEN(PARY(1)='')
       IF INDDN^=NULL
         THEN INDSN=''''××INDDN->ALCDSN××'''';
         ELSE DO;
           MSGDATA='WHAT DATASET?';
           SIGNAL CONDITION(BADALC);
           END;
     WHEN(PARQCK(1))
       INDSN=''''××PARY(1)××'''';
     OTHERWISE
       INDSN=PARY(1);
     END; /* SELECT */
   IF VOL=''
     THEN VOLSTR='';
     ELSE VOLSTR=' VOL('××VOL××')';
   RHP=RHPTR;
   I=#CMD('ABE '××INDSN××FAM××' BPAM SUPPORT'××VOLSTR,RHP,RHHDRPT);
   IF RHP=RHPTR × I^=0
     THEN DO;
       IF MSGDATA=''
         THEN MSGDATA='FAILED - CAN''T OPEN INPUT PDS';
       RHRC=16;
       SIGNAL CONDITION(BADALC);
       END;
   END ALCIN;

 CL: PROC;
   DCL SETCAPS ENTRY;
   DCL MYQUAL CHAR(44) VAR INIT('');
   DCL LOCDSN ENTRY;
   DCL LOCPTR PTR,
       LOCRET FIXED BIN(31,0),
       LOCINDX CHAR(44) VAR INIT(''),
       NAM CHAR(44) VAR,
       LOCTYPE CHAR(1);
   DCL (I,J) FIXED BIN;
   DCL PREF CHAR(10) VAR;

   PREF=#SYMC(RHHDRPT,RH_SYMTREE,'SYSPREF');
   IF PREF=''
     THEN PREF=#SYMC(RHHDRPT,RH_SYMTREE,'SYSUID');
   DO I=1 TO 2;
     CALL SETCAPS(PARY(I));
     END;
   LOCINDX=PARY(1);  /* LEVEL KEYWORD */
   MYQUAL=PARY(2);   /* QUAL KEYWORD */
   IF LOCINDX=''
     THEN LOCINDX=PREF;
     ELSE IF SUBSTR(LOCINDX,1,1)='.'
       THEN LOCINDX=PREF××LOCINDX;
   LOCINDX=LOCINDX××'.';
   OPT=#ABEALC('@CL','',RHHDRPT,RHPTR);
   LOCPTR=NULL;
   DO UNTIL(LOCPTR=NULL);
     CALL LOCDSN(LOCPTR,LOCRET,LOCINDX,NAM,LOCTYPE);
     IF LOCTYPE^='A'
       THEN J=0;
       ELSE DO;
         J=1;
         IF MYQUAL^=''
           THEN IF LENGTH(NAM) >= LENGTH(MYQUAL)
             THEN IF SUBSTR(NAM,LENGTH(NAM)-LENGTH(MYQUAL)+1)^=MYQUAL
               THEN J=0;
         END;
     IF J=1
       THEN I=#CMD('NL '××NAM,OPT,RHHDRPT);
     END;
   I=#CMD('TOP',OPT,RHHDRPT);
   END CL;

 CLL: PROC;
   DCL (A,B) CHAR(80) VAR;
   DCL RPT PTR;

   CALL CL;
   A='ABE ''';
   B=''' INP(LINES 999)';
   DO RPT=#SUCC(RHHDRPT,OPT,NULL)
     REPEAT #SUCC(RHHDRPT,OPT,RPT) WHILE(RPT^=OPT);
     RPT->RECSTR=A××RPT->RECSTR××B;
     END;
   END CLL;

 ML: PROC;
   DCL ABEDIR ENTRY RETURNS(CHAR(8));
   DCL MEMPTR PTR;
   DCL MEMNAM CHAR(8);

   CALL ALCIN(' ',PARY(2));
   OPT=#ABEALC('@ML','',RHHDRPT,RHPTR);
   DO FOREVER=1 REPEAT FOREVER;
     MEMNAM=ABEDIR(RH_PDSDIR,RHP->INDDN->ALCDDN,MEMPTR);
     IF MEMNAM=HIGH(8)
       THEN LEAVE;
     I=#CMD('NL '××MEMNAM,OPT,RHHDRPT);
     END;
   I=#CMD('TOP',OPT,RHHDRPT);
   END ML;

 MLL: PROC;
   DCL (A,B) CHAR(80) VAR;
   DCL RPT PTR;

   CALL ML;
   IF INDEX(INDSN,'''')=1
     THEN DO;
       A=SUBSTR(INDSN,1,LENGTH(INDSN)-1);
       B=')''';
       END;
     ELSE DO;
       A=INDSN;
       B=')';
       END;
   B=B××' INP(LINES 999)';
   IF PARY(2)^=''
     THEN B=B××' VOL('××PARY(2)××')';
   A='ABE '××A××'(';
   DO RPT=#SUCC(RHHDRPT,OPT,NULL)
     REPEAT #SUCC(RHHDRPT,OPT,RPT) WHILE(RPT^=OPT);
     RPT->RECSTR=A××SUBSTR(RPT->RECSTR,1,SUFFIX(RPT->RECSTR,' '))××B;
     END;
   END MLL;

 SCANPDS: PROC;

   CALL ALCIN(' FAM(FAMPDSR) ',PARY(3));
   I=#CMD(PARY(2),RHP,RHHDRPT); /* SCANPDS */
   END SCANPDS;

 VTOC: PROC;
   %INCLUDE DSCB4;

   DCL SETCAPS ENTRY;
   DCL SYSVTOC ENTRY;

   DCL (I,J,K,L) FIXED BIN;
   DCL VOLUME CHAR(6);
   DCL 1 WK4AREA,
        2 KEY CHAR(44),
        2 DATA CHAR(96),
        2 WADR CHAR(5),
        2 PAD CHAR(3);
   DCL WK4STR CHAR(148) DEF WK4AREA;
   DCL MYWKAREA CHAR(148);
   DCL WKD CHAR(96) DEF MYWKAREA POS(45);
   DCL BKEY BIT(352) DEF KEY;
   DCL DUMDATA CHAR(148);
   DCL ENDCCHH CHAR(4);
   DCL 1 DADR,
        2 CC FIXED BIN(15,0),
        2 HH FIXED BIN(15,0),
        2 R FIXED BIN(15,0);
   DCL CCHHR CHAR(5) DEF DADR;
   DCL OPT PTR;
   DCL RET BIT(32);

   VOLUME=PARY(1);
   IF VOLUME=''
     THEN DO;
       MSGDATA='FIRST OPERAND MUST BE VOLUME SERIAL';
       RHRC=8;
       RETURN;
       END;
   DO J=2 TO HBOUND(PARY,1) WHILE(PARY(J)^='');
     SELECT(PARY(J));
       WHEN('0') PARY(J)=LOW(1);
       WHEN('1','2','3','4','5');
       OTHERWISE DO;
         MSGDATA='ERROR - ONLY DSCB NUMBERS 0 THRU 5 ARE VALID';
         RHRC=8;
         RETURN;
         END;
       END; /* SELECT */
     END;
   CALL SETCAPS(VOLUME);
   BKEY = (44)'00000100'B;
   DSCB4PTR = ADDR(WK4AREA);
   CALL SYSVTOC('SEARCH',RET,WK4AREA.KEY,VOLUME,DUMDATA);
   WK4AREA.DATA = DUMDATA;
   DSCBPER = DS4DEVDT;
   TRKPERCYL = DS4DEVTC;
   CYLPERPCK = DS4DEVCN;
   ENDCCHH = DS4CCHHU;
   CCHHR = DS4CCHHL;
   R=256;

   OPT=#ABEALC('@'××VOLUME,' RECSIZE(148) HEX',RHHDRPT,RHPTR);
   IF VTOCCK('4')=1
     THEN DO;
       I=#CMD('NL ',OPT,RHHDRPT);
       OPT->LSTHDR->RECSTR=WK4STR;
       IF J=3
         THEN RETURN; /* ONLY WANTS FORMAT 4 */
       END;
   K=1;
   LUP: DO FOREVER=1 REPEAT FOREVER;
     R=R+256;
     IF R>DSCBPER*256
       THEN DO;
         R=256;
         HH=HH+1;
         IF HH=TRKPERCYL
           THEN DO;
             HH=0;
             CC=CC+1;
             END;
         IF SUBSTR(CCHHR,1,4)>ENDCCHH
           THEN LEAVE LUP;
         END;
     CALL SYSVTOC('SEEK',RET,CCHHR,VOLUME,MYWKAREA);
     IF RET^=0
       THEN DO;
         MSGDATA='NON-ZERO RETURN CODE DURING VTOC ACCESS';
         RHRC=16;
         RETURN;
         END;
     IF J>2
       THEN K=VTOCCK(SUBSTR(MYWKAREA,45,1));
     IF K=1
       THEN DO;
         I=#CMD('NL ',OPT,RHHDRPT);
         OPT->LSTHDR->RECSTR=MYWKAREA;
         END;
     END;
   I=#CMD('TOP',OPT,RHHDRPT);

 VTOCCK: PROC(CK) RETURNS(FIXED BIN);
   DCL CK CHAR(1);
   DCL L FIXED BIN;

   IF J=2
     THEN RETURN(1);
   DO L=2 TO J-1 WHILE(CK^=PARY(L));
     END;
   IF L<J
     THEN RETURN(1);
     ELSE RETURN(0);
   END VTOCCK;
   END VTOC;

   END ABE069;
