GETLOD   CSECT                                                      TGS
********************************************************************TGS
*                  THIS RTNE WILL BE RESPONSIBLE FOR ACCESSING THE *TGS
*              SYSUT3 DATA SET.  AT EXIT, REG1 WILL CONTAIN THE    *TGS
*              ADDRESS OF THE NEXT RECORD, AND REG0 WILL CONTAIN   *TGS
*              THE LENGTH OF THE RECORD.                           *TGS
*                  THE FIRST BYTE OF EACH RECORD RETURNED BY THIS  *TGS
*              RTNE HAS SPECIAL SIGNIFICANCE, AND IS ACTUALLY      *TGS
*              CREATED AND APPENDED TO THE OTHER DATA BY THIS RTNE *TGS
*                                                                  *TGS
*                       BIT 0 = 1 IF THIS RECORD CONTAINS A PDS    *TGS
*                                 DIRECTORY ENTRY                  *TGS
*                       BIT 1 = 1 IF THIS RECORD IS A PDS DIRECTORY*TGS
*                                 ENTRY FOR AN ALIAS NAME          *TGS
*                       BIT 2 = 1 IF THIS RECORD CONTAINS A NOTE   *TGS
*                                 LIST                             *TGS
*                  THE NEXT 3 BYTES IN EACH RECORD CONTAINS THE TTR*TGS
*              OF THAT RECORD IN THE PDS BEING UNLOADED            *TGS
*                  THE LAST EIGHT BYTES OF A RECORD CONTAINING A   *TGS
*              DIRECTORY ENTRY WILL CONTAIN THE TRANSLATES SSI DATA*TGS
*                  FOR A GIVEN MEMBER, THE FIRST RECORD PASSED BACK*TGS
*              WILL CONTAIN THE PDS DIRECTORY ENTRY, THE SECOND    *TGS
*              WILL CONTAIN THE NOTE LIST IF ONE EXISTS, AND       *TGS
*              SUBSEQUENT RECORDS WILL CONTAIN MEMBER RECORDS. NOTE*TGS
*              THAT TTRS IN THE DIRECTORY ENTRY AND NOTE LIST REMAINTGS
*              UNCHANGED.                                          *TGS
*              NOTE THAT SYSUT3 CANNOT BE CONCATENATED              TGS
*                                                                  *TGS
********************************************************************TGS
         TGSREQU                                                    TGS
         SAVES RETCTL                                               TGS
         TM    INITSW,X'80'                                         TGS
         BC    1,GO                                                 TGS
         OI    INITSW,X'80'                                         TGS
         OPEN  (SYSUT3,(INPUT))                                     TGS
         TM    SYSUT3+48,X'10'         WAS IT GOOD OPEN             TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1111,DUMP                                            TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,SYSUT3+62            R0 = BLKSIZE                 TGS
         STH   R0,BUFLEN                                            TGS
         AH    R0,=H'4'                GET 4 MORE FOR TTR & FLG FLDSTGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,CTLADDR                                           TGS
         LA    R1,4(R1)                                             TGS
         ST    R1,BUFADDR                                           TGS
FIND     EQU   *                                                    TGS
         L     RF,=V(GETDIR)                                        TGS
         BALR  RE,RF                   GO GET PDS DIR ENTRY         TGS
         LTR   R2,R1                   IS IT EOF                    TGS
         BC    8,EOF1                  BR IF YES                    TGS
         LR    R3,R0                   R3 = LEN ENTRY               TGS
         L     RF,=V(BLDTAB)                                        TGS
         BALR  RE,RF                   GO BUILD IN-CORE TABLE       TGS
         ST    R1,TABADDR                                           TGS
         TM    11(R2),X'80'            IS IT AN ALIAS               TGS
         BC    1,FIND                  BR IF YES                    TGS
         LA    R4,8(R2)                POINT R4 AT TTR              TGS
         MVC   FINDTTRN,0(R4)                                       TGS
         FIND  SYSUT3,FINDTTRN,C                                    TGS
         LTR   RF,RF                   WAS IT GOOD FIND             TGS
         BC    8,GOODFIND              BR IF YES                    TGS
         ABEND 3333,DUMP                                            TGS
GOODFIND EQU   *                                                    TGS
         MVC   TTRSAV,SYSUT3           SAVE TTRN OF MEMBER          TGS
         MVC   NOTECNT,19(R2)          SAVE NOTE CNT                TGS
         MVC   NOTETTR(3),16(R2)       SAVE TTR OF NOTE LIST        TGS
         NI    NOTESW,X'00'            INDICATE NOTE LIST NOT READ  TGS
         L     R4,CTLADDR                                           TGS
         XC    0(4,R4),0(R4)           CLEAR FLAGS & TTR FLD        TGS
         OI    0(R4),X'80'             IND PDS DIR ENTRY            TGS
         MVC   4(60,R4),0(R2)          MOVE PDS DIR TO BUFFER       TGS
         LA    R5,4(R3,R4)                                          TGS
         ST    R5,OUT                                               TGS
         SH    R5,=H'4'                                             TGS
         ST    R5,IN                                                TGS
         L     RF,=V(TROUT)                                         TGS
         LA    R1,TRPARM                                            TGS
         BALR  RE,RF                   GO TRANSLATE SSI             TGS
         LR    1,4                     R1 = ADDR OF RECORD          TGS
         LA    R0,12(R3)                                            TGS
         B     RETCTL                                               TGS
GO       EQU   *                                                    TGS
         TM    EOFSW,X'80'                                          TGS
         BC    8,NOTEOF                                             TGS
         LM    R2,R4,SAVREG                                         TGS
         B     LOOPB                                                TGS
NOTEOF   EQU   *                                                    TGS
         CLI   NOTECNT,X'00'           IS THERE A NOTE LIST TO READ TGS
         BC    8,GOA                   BR IF NOT                    TGS
         MVI   NOTECNT,X'00'                                        TGS
         POINT SYSUT3,NOTETTR                                       TGS
         L     R2,BUFADDR                                           TGS
         READ  DECB,SF,,(2),'S',MF=E                                TGS
         CHECK DECB                                                 TGS
         L     R3,DECB+16              R3 = IOB ADDR                TGS
         LH    R4,BUFLEN                                            TGS
         SH    R4,14(R3)               R4 = BLKSIZE - RESIDUAL CNT  TGS
*                                           FROM CSW IN IOB         TGS
         L     R2,CTLADDR                                           TGS
         MVI   0(R2),X'20'             INDICATE A NOTE LIST         TGS
         MVC   1(3,R2),NOTETTR         SET OLD TTR FLD              TGS
         POINT SYSUT3,TTRSAV           POINT BACK AT START OF MEMB  TGS
         LR    R1,R2                   R1 = ADDR OF RECORD          TGS
         LA    R0,4(R4)                R0 = LEN OF RECORD           TGS
         B     RETCTL                                               TGS
GOA      EQU   *                                                    TGS
         L     R2,BUFADDR                                           TGS
         READ  DECB,SF,,(2),'S',MF=E                                TGS
         CHECK DECB                                                 TGS
         L     R3,DECB+16              R3 = IOB ADDR                TGS
         LH    R4,BUFLEN                                            TGS
         SH    R4,14(R3)               R4 = BLKSIZE - RESIDUAL CNT  TGS
*                                           FROM CSW IN IOB         TGS
         L     R2,CTLADDR                                           TGS
         MVI   0(R2),X'00'             IND DATA REC                 TGS
         NOTE  SYSUT3                                               TGS
         ST    R1,WORKWORD                                          TGS
         MVC   1(3,R2),WORKWORD        SET OLD TTR IN REC           TGS
         LR    R1,R2                   R1 = ADDR RECORD             TGS
         LA    R0,4(R4)                R0 = LEN OF RECORD           TGS
         B     RETCTL                                               TGS
EOF1     EQU   *                                                    TGS
         OI    EOFSW,X'80'                                          TGS
         LA    R2,TABADDR              R2 = ADDR FST BLOCK ADD      TGS
LOOP     EQU   *                                                    TGS
         L     R2,0(R2)                R2 = ADDR NXT TABLE BLOCK    TGS
         LTR   R2,R2                   IS IT EOF                    TGS
         BC    8,EOF2                  BR IF YES                    TGS
         LA    R3,8(R2)                R3 = ADDR NXT AVLBL ENTRY    TGS
         LH    R4,4(R2)                R4 = LEN FILLED IN           TGS
LOOPA    EQU   *                                                    TGS
         CH    R4,=H'8'                IS THIS BLOCK DEPLETED       TGS
         BC    8,LOOP                  BR IF YES                    TGS
         TM    11(R3),X'80'            IS IT AN ALIAS               TGS
         BC    1,GOTALIAS              BR IF YES                    TGS
LOOPB    SH    R4,=H'12'                                            TGS
         LA    R3,12(R3)                                            TGS
         B     LOOPA                                                TGS
GOTALIAS EQU   *                                                    TGS
         LA    R5,TABADDR              R5 = ADDR FST BLOCK ADD      TGS
ALOOP    EQU   *                                                    TGS
         L     R5,0(R5)                R5 = ADDR NXT TABLE BLOCK    TGS
         LTR   R5,R5                   IS IT FINISHED               TGS
         BC    8,LOOPB                 TOUGH LUCK FELLA             TGS
         LA    R6,8(R5)                R6 = ADDR NXT AVLBL ENTRY    TGS
         LH    R7,4(R5)                R4 = LEN FILLED IN           TGS
ALOOPA   EQU   *                                                    TGS
         CH    R7,=H'8'                IS THIS BLOCK DEPLETED       TGS
         BC    8,ALOOP                 BR IF YES                    TGS
         TM    11(R6),X'80'            IS IT AN ALIAS               TGS
         BC    1,ALOOPB                BR IF YES                    TGS
         CLC   8(3,R3),8(R6)           DO TTRS MATCH                TGS
         BC    8,GOTMATCH              BR IF YES                    TGS
ALOOPB   EQU   *                                                    TGS
         SH    R7,=H'12'                                            TGS
         LA    R6,12(R6)                                            TGS
         B     ALOOPA                                               TGS
GOTMATCH EQU   *                                                    TGS
         L     R8,CTLADDR                                           TGS
         MVC   0(4,R8),=X'00010040'    TELL BLDL 1 ENTRY OF 64 BYTESTGS
         MVC   4(8,R8),0(R3)           MOVE ALIAS NAME TO BLDL LIST TGS
         BLDL  SYSUT3,(8)                                           TGS
         XC    0(4,R8),0(R8)                                        TGS
         MVI   0(R8),X'C0'             IND ALIAS DIR ENTRY          TGS
         MVC   15(64,R8),17(R8)        SQUEEZE OUT KZ BYTES         TGS
         SR    R9,R9                                                TGS
         IC    R9,15(R8)                                            TGS
         N     R9,=F'31'               R9 = LEN USER DATA IN HALFWDSTGS
         AR    R9,R9                   R9 = LEN USER DATA           TGS
         LA    R9,16(R9)               R9 = RECORD LEN SO FAR       TGS
         LA    RA,0(R8,R9)             R9 = ADDR TO PUT NAME        TGS
         MVC   0(8,RA),0(R6)           MOVE REAL MEMB NAME TO REC   TGS
         SH    RA,=H'4'                RA = ADDR SSI                TGS
         ST    RA,IN                                                TGS
         LA    RA,12(RA)               RA = ADDR SSI OUT            TGS
         ST    RA,OUT                                               TGS
         L     RF,=V(TROUT)                                         TGS
         LA    R1,TRPARM                                            TGS
         BALR  RE,RF                                                TGS
         LA    R0,16(R9)               R0 = LEN RECORD              TGS
         LR    R1,R8                   R1 = ADDR RECORD             TGS
         STM   R2,R4,SAVREG                                         TGS
         B     RETCTL                                               TGS
EOF2     EQU   *                                                    TGS
         L     R2,TABADDR                                           TGS
BLOOP    EQU   *                                                    TGS
         LR    R1,R2                   R1 = ADDR TO FREE            TGS
         L     R2,0(R1)                R2 = ADDR NXT TABLE BLOCK    TGS
         LA    R0,2048                 R0 = LEN TO FREE             TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         LTR   R2,R2                   IS TABLE FREED               TGS
         BC    7,BLOOP                 BR IF NOT                    TGS
         CLOSE SYSUT3                                               TGS
         SR    R1,R1                                                TGS
         B     RETCTL                                               TGS
SYSUT3   DCB   DDNAME=SYSUT3,DSORG=PO,MACRF=R,EODAD=FIND            TGS
         READ  DECB,SF,SYSUT3,MF=L                                  TGS
INITSW   DC    X'00'                                                TGS
BUFADDR  DC    F'0'                                                 TGS
BUFLEN   DC    H'0'                                                 TGS
TTRSAV   DC    F'0'                                                 TGS
CTLADDR  DC    F'0'                                                 TGS
NOTECNT  DC    X'00'                                                TGS
TABADDR  DC    F'0'                                                 TGS
NOTETTR  DC    F'0'                                                 TGS
NOTESW   DC    X'00'                                                TGS
EOFSW    DC    X'00'                                                TGS
SAVREG   DC    3F'0'                                                TGS
TRPARM   DS    0F                                                   TGS
         DC    A(IN)                                                TGS
         DC    A(OUT)                                               TGS
IN       DS    F                                                    TGS
         DC    F'4'                                                 TGS
OUT      DS    F                                                    TGS
         DC    F'8'                                                 TGS
WORKWORD DS    F                                                    TGS
FINDTTRN DC    XL3'00',X'00'                                        TGS
         LTORG                                                      TGS
         END
