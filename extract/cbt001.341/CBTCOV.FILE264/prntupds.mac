PRNTUPDA START 0
 SAVES RETCTL
 TGSREQU
         LR    R9,R0                                                TGS
         LR    R2,R1                   SAVE RECLEN                  TGS
         TM    INITSW,X'80'                               BEEN DONE TGS
         BC    1,BEGIN                 BR IF YES                    TGS
         OI    INITSW,X'80'            SET INITSW                   TGS
         L     R1,4(RD)                                             TGS
         MVC   DDSW,16(R1)             TO AVOID CONFUSION           TGS
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NOUT4                 BR IF NOT                    TGS
         OPEN  (SYSUT4,(OUTPUT))                                    TGS
NOUT4    EQU   *                                                    TGS
         TM    DDSW,X'40'              IS INDEX AVLBL               TGS
         BC    8,NOIND                 BR IF NOT                    TGS
         OPEN  (INDEX,(OUTPUT))                                     TGS
NOIND    EQU   *                                                    TGS
BEGIN    EQU   *                                                    TGS
NEXT EQU *
         LTR   R9,R9                   IS IT EOF                    TGS
         BC    8,UT1END                BR IF YES                    TGS
         CLC   0(8,R9),=CL8'./'        IS IT NEW MEMBER
 BC 7,PUTOUT BR IF NOT
         NI    BYSW,X'7F'              CLEAR BYPASS SWITCH          TGS
         CLI   73(R9),C'O'             IS IT AN OBJ MOD             TGS
         BC    7,FSTME                 BR IF NOT                    TGS
         OI    BYSW,X'80'              SET FLAG SO DETL WONT PRINT  TGS
FSTME B FSTINIT
 MVI OUTREC,C' '
         MVC   OUTREC+1(88),OUTREC     CLEAR TO BLANKS              TGS
 MVI OUTREC,C'-'
BLNKLP1 EQU *
 CP LNECNT,=P'59'
 BC 10,BLP2
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO1                   BR IF NOT                    TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO1      EQU   *                                                    TGS
 AP LNECNT,=P'3'
 B BLNKLP1
BLP2 EQU *
 MVI OUTREC,C' '
BLNKLP2 EQU *
 CP LNECNT,=P'60'
 BC 2,BLP3
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO2                   BR IF NOT                    TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO2      EQU   *                                                    TGS
 AP LNECNT,=P'1'
 B BLNKLP2
BLP3 EQU *
 MVC OUTREC+1(8),NAMESAV
 MVC OUTREC+13(8),IDSAV
 MVC OUTREC+24(6),=X'402020202020'
         ED    OUTREC+24(6),PGECNT                                  TGS
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO3                   BR IF NOT                    TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO3      EQU   *                                                    TGS
 MVC OUTREC(1),INDCC
         MVC   OUTREC+24(6),=X'402020202020'                        TGS
         ED    OUTREC+24(6),PGECNTS                                 TGS
         TM    DDSW,X'40'              IS INDEX AVLBL               TGS
         BC    8,NO4                   BR IF NOT                    TGS
         PUT   INDEX,OUTREC                                         TGS
NO4      EQU   *                                                    TGS
 AP PGECNT,=P'1'
         ZAP   PGECNTS,PGECNT                                       TGS
 MVI INDCC,C' '
FSTINIT EQU *
 TM EOFSW,X'80'
 BC 1,UT1END2
 MVI FSTME+1,X'00'
         MVC   OUTREC+81(8),=CL8' '                                 TGS
 MVC OUTREC+1(80),0(R9) MOVE CTL STMT TO OUTREC
 MVI OUTREC,C'1' SET SKIP TO NEW PAGE
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO5                   BR IF NOT                    TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO5      EQU   *                                                    TGS
 MVI OUTREC,C' '
 ZAP LNECNT,=P'1'
 MVC NAMESAV,63(R9)
 MVC IDSAV,72(9)
         B     RETCTL                                               TGS
PUTOUT EQU *
         TM    BYSW,X'80'              IS DETL TO BE PRINTED        TGS
         BC    1,RETCTL                BR IF NOT                    TGS
         CLI   0(R9),X'02'
         BC    8,RETCTL
 CP LNECNT,=P'59' IS IT END OF PAGE
 BC 4,PUTREC BR IF NOT
 MVI OUTREC,C' '
         MVC   OUTREC+1(88),OUTREC     CLEAR TO BLANKS              TGS
 MVC OUTREC+1(8),NAMESAV
 MVC OUTREC+13(8),IDSAV
 MVC OUTREC+24(6),=X'402020202020'
 ED OUTREC+24(6),PGECNT
 MVI OUTREC,C'0'
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO6                                                TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO6      EQU   *                                                    TGS
 AP PGECNT,=P'1'
 ZAP LNECNT,=P'0'
 MVI OUTREC,C'1'
PUTREC EQU *
         MVC   OUTREC+1(88),0(R9)                                   TGS
         C     R2,=F'82'                                            TGS
         BC    8,SETBLNK                                            TGS
         MVC   SEQSAV,OUTREC+73                                     TGS
         MVC   OUTREC+73(8),OUTREC+81                               TGS
         MVC   OUTREC+81(8),SEQSAV                                  TGS
         B     DDTST                                                TGS
SETBLNK  EQU   *                                                    TGS
         MVC   OUTREC+81(8),=CL8' '                                 TGS
DDTST    EQU   *                                                    TGS
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO7                                                TGS
         PUT   SYSUT4,OUTREC                                        TGS
NO7      EQU   *                                                    TGS
 AP LNECNT,=P'1'
 MVI OUTREC,C' '
         B     RETCTL                                               TGS
UT1END EQU *
 OI EOFSW,X'80'
 B FSTME
UT1END2 EQU *
         TM    DDSW,X'40'              IS INDEX AVLBL               TGS
         BC    8,NO8                   BR IF NOT                    TGS
         CLOSE INDEX                                                TGS
NO8      EQU   *                                                    TGS
         TM    DDSW,X'80'              IS SYSUT4 AVLBL              TGS
         BC    8,NO9                                                TGS
         CLOSE SYSUT4                                               TGS
NO9      EQU   *                                                    TGS
 B RETCTL
SYSUT4   DCB   DDNAME=PRINTER,DSORG=PS,MACRF=(PM),EXLST=EX1,        TGSC
               LRECL=89,RECFM=FBA                                   TGS
INDEX    DCB   DDNAME=INDEX,DSORG=PS,MACRF=(PM),EXLST=EX2,          TGSC
               LRECL=81,RECFM=FBA                                   TGS
EX1      DS    0F                                                   TGS
         DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                                                TGS
         DC    AL3(NMEX1)                                           TGS
EX2      DS    0F                                                   TGS
         DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                                                TGS
         DC    AL3(NMEX2)                                           TGS
NMEX1    EQU   *                                                    TGS
         LH    R9,SYSUT4+62                                         TGS
         LTR   R9,R9                                                TGS
         BCR   7,RE                                                 TGS
         MVC   SYSUT4+62(2),=H'890'                                 TGS
         BR    RE                                                   TGS
NMEX2    EQU   *                                                    TGS
         LH    R9,INDEX+62                                          TGS
         LTR   R9,R9                                                TGS
         BCR   7,RE
         MVC   INDEX+62(2),=H'810'
         BR    RE
PGECNT   DC    XL3'00001C'
PGECNTS  DC    XL3'00001C'                                          TGS
INDCC    DC    C'1'
IDSAV    DS    CL8
EOFSW    DC    X'00'
LNECNT DS CL2
OUTREC   DS    CL89                                                 TGS
NAMESAV DS CL8
INITSW   DC    X'00'                                                TGS
DDSW     DC    X'00'                                                TGS
BYSW     DC    X'00'                                                TGS
SEQSAV   DS    CL8                                                  TGS
 LTORG
 END
