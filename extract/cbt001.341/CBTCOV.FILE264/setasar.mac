 /* SETASAR */
 SETASAR:  PROC(PRMPTR,VALPTR) RECURSIVE;
         DCL (PRMPTR,VALPTR) PTR; /* POINTER TO ASARHDR, ELEMENT */
         % INCLUDE ASARDCL;
         DCL ALCASAR ENTRY(PTR,PTR,PTR,BIT(8))
           RETURNS(PTR);
         DCL (I,L) FIXED BIN(15,0);
         DCL (TPT,SPT,MPTR) PTR;
         ASARHP = PRMPTR;
         I = REFASAR(PRMPTR); /* GO FIND ELEMENT IF IT EXISTS */
         IF I>ASARDCT /* DO ALL INDEXES MATCH? */
           THEN IF ASARDTY='C'
             THEN FREE ASARDPT(ASARDCT)->ASAREL->CSEL;
             ELSE;
           ELSE DO;
             /* REFASAR WILL HAVE FILLED IN FIELD ASARDPT(I) WITH THE
             ADDRESS OF THE PRIOR ELEMENT IN SEQUENCE.  IF ASARDPT(I)
             IS NULL, THE DIMENSION IS EMPTY, OR THE ELEMENT HAS A
             LOWER ASSOCIATIVE ADDRESS THAN ANY CURRENTLY IN THE
             DIMENSION. */
             DO I=I TO ASARDCT;
               IF ASARDPT(I)=NULL
                 THEN DO;
                   /* IT WILL BE FIRST OR ONLY KEY IN DIMENSION. */
                   IF I=1
                     THEN MPTR=ASARHP;
                     ELSE MPTR=ASARDPT(I-1);
                   IF MPTR->ASAREL^=NULL
                     THEN DO MPTR=MPTR REPEAT MPTR->ASAREL
                       WHILE(SUBSTR(MPTR->ASAREL->ASARELFL,1,1));
                       END;
                   END;
                 ELSE MPTR=ASARDPT(I)->ASARFGP->ASARIXP;
               IF ASARATY(I)='A'
                 THEN DO;
                   L=ASARIPT(I)->CSELN;
                   ALLOCATE CSEL;
                   CSESTR=ASARIPT(I)->CSESTR;
                   END;
                 ELSE CSELP=ASARIPT(I);
               ASARDPT(I)=ALCASAR(CSELP,MPTR,ASARDPT(I),'0'B);
               END;
             END;
         /* ALL INDEXES NOW MATCH */
         ASARDPT(ASARDCT)->ASAREL=VALPTR;
         END;
