// EXEC ASMFC,PARM.ASM='LOAD,NODECK,TERM'
//ASM.SYSTERM DD SYSOUT=C
//ASM.SYSGO DD DSN=&ST(OBJST),DISP=(NEW,PASS),
// SPACE=(TRK,(1,1,1)),VOL=REF=SYS1.WORK1,
// DCB=(LRECL=80,BLKSIZE=400,RECFM=FB)
*          DATA SET TGSPGMST   AT LEVEL 003 AS OF 06/23/71
SORT     START 0
         ENTRY DSORTN
         ENTRY DSORTA
*
*        ROUTINE TO DO INTERNAL SORT ON UP TO
*        FIVE COLUMNS OF A FORTRAN *4 ARRAY.
*
*        SORT IS DONE BY THE SHELL RADIX INTERCHANGE METHOD.
*
*        CALL DSORTX(NROW,NCOL,COL1,COL2,COL3,COL4,COL5,POINT)
*
*        NROW = NUMBER OF ROWS IN EACH COLUMN
*        NCOL = NUMBER OF COLUMNS TO BE SORTED
*        COLI = LOCATION OF 1ST ELEMENT IN I-TH COLUMN
*        POINT = LOCATION OF 1ST WORD OF POINTER ARRAY
*
         USING *,15
DSORTA   STM   0,15,REGS
         MVC   CLOP(1),COL1    SET UP LOGICAL COMPARE
         MVC   CLOP(1),COL2
         MVC   CLOP(1),COL3
         MVC   CLOP(1),COL4
         MVC   CLOP(1),COL5
         L     2,ADD
         L     15,BASE
         BCR   15,2
         USING *,15
DSORTN   STM   0,15,REGS
         MVC   COP(1),COL1    SET UP NUMERIC COMPARE
         MVC   COP(1),COL2
         MVC   COP(1),COL3
         MVC   COP(1),COL4
         MVC   COP(1),COL5
BEGIN    L     11,0(0,1)
         L     11,0(0,11)   GET NROW
         SLA   11,2(0)   NL=4*NROW IN REG 14
         LR    14,11
         L     11,8(0,1)   GET COL LOCATIONS
         LR    2,11   INTO REGS 2-7
         L     11,12(0,1)
         LR    3,11
         L     11,16(0,1)
         LR    4,11
         L     11,20(0,1)
         LR    5,11
         L     11,24(0,1)
         LR    6,11
         L     11,28(0,1)
         LR    7,11
         L     11,4(0,1)   GET NCOL
         L     11,0(0,11)
         LR    1,11   NCOL IN REG 1
         L     11,L
         SR    2,11   RESET BY ONE LOCATION
         SR    3,11
         SR    4,11
         SR    5,11
         SR    6,11
         SR    7,11
         LR    11,14
         SRA   11,3(0)   M=N/2
         SLA   11,2(0)   ML=4*M
         LR    10,11   ML IN REG 10
ALOOP    L     8,L   RL=4, SET TO SORT 1ST
         LR    0,8   SET RL
         LA    9,0(10,8)   SUBLIST OF LENGTH M
BLOOP    SR    12,12   TURN OFF SORT SWITCH
         ST    12,NS   TURN OFF SORT SWITCH
CLOOP    LR    11,1   NCOL TO REG 11
         L     12,0(8,2)   COMPARE COLUMN 1
COL1     C     12,0(9,2)
         BC    2,SWAP
         BC    4,NEXT
         BCT   11,C2
         BC    15,NEXT
C2       L     12,0(8,3)   COMPARE COLUMN 2
COL2     C     12,0(9,3)
         BC    2,SWAP
         BC    4,NEXT
         BCT   11,C3
         BC    15,NEXT
C3       L     12,0(8,4)   COMPARE COLUMN 3
COL3     C     12,0(9,4)
         BC    2,SWAP
         BC    4,NEXT
         BCT   11,C4
         BC    15,NEXT
C4       L     12,0(8,5)   COMPARE COLUMN 4
COL4     C     12,0(9,5)
         BC    2,SWAP
         BC    4,NEXT
         BCT   11,C5
         BC    15,NEXT
C5       L     12,0(8,6)   COMPARE COLUMN 5
COL5     C     12,0(9,6)
         BC    12,NEXT
SWAP     LR    11,1   INTERCHANGE TWO ITEMS
         L     12,0(8,2)
         L     13,0(9,2)
         ST    13,0(8,2)
         ST    12,0(9,2)
         BCT   11,S2
         BC    15,POINT
S2       L     12,0(8,3)
         L     13,0(9,3)
         ST    13,0(8,3)
         ST    12,0(9,3)
         BCT   11,S3
         BC    15,POINT
S3       L     12,0(8,4)
         L     13,0(9,4)
         ST    12,0(9,4)
         ST    13,0(8,4)
         BCT   11,S4
         BC    15,POINT
S4       L     12,0(8,5)
         L     13,0(9,5)
         ST    13,0(8,5)
         ST    12,0(9,5)
         BCT   11,S5
         BC    15,POINT
S5       L     12,0(8,6)
         L     13,0(9,6)
         ST    13,0(8,6)
         ST    12,0(9,6)
POINT    L     12,0(8,7)   INTERCHANGE POINTERS
         L     13,0(9,7)
         ST    13,0(8,7)
         ST    12,0(9,7)
         ST    15,NS   TURN INTERCHANGE SWITCH ON
NEXT     LA    8,0(10,8)   INCREMENT SUBSCRIPTS
         LA    9,0(10,9)
         STM   0,15,TREGS
         CR    9,14   IS SUBSCRIPT TOO BIG?
         BC    12,CLOOP
         SR    12,12
         C     12,NS   IS THIS SUBLIST SORTED?
         BC    8,UPR
         LR    8,0   NO, SORT SOME MORE
         LA    9,0(10,8)
         BC    15,BLOOP
UPR      A     0,L  RL+4 TO RL
         CR    0,10   RL VS. ML
         BC    2,CHECKM
         LR    8,0   RESET SUBSCRIPTS
         LA    9,0(10,8)
         BC    15,BLOOP
CHECKM   C     10,L   DOES M=1?
         BC    8,RETURN
         SRA   10,2(0)
         C     10,F15   INCREASE M
         BC    12,D4
         SRA   10,1(0)
D4       SRA   10,2(0)
         A     10,ONE
         SLA   10,2(0)
         BC    15,ALOOP
RETURN   LM    0,15,REGS
         BCR   15,14   AND AWAY WE GO!
REGS     DS    16F
ADD      DC    A(BEGIN)
BASE     DC    A(DSORTN)
L        DC    F'4'
NS       DC    F'0'
ZERO     DC    F'0'
F15      DC    F'15'
ONE      DC    F'1'
COP      DC    X'59'   COMPARE NUMERIC
CLOP     DC    X'55'   COMPARE LOGICAL
TREGS    DS    16F
         END
