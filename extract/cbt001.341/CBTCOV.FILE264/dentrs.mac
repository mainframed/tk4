 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
  /* DENTRS (RESEQ)
  THIS SUBROUTINE WILL RESEQUENCE FROM THE SPECIFIED RECORD TO THE
  END OF THE ARRAY OF RECORDS.  IF PRMPTR=HDPTR, RESEQUENCING
  BEGINS WITH #1.  IF SAVE IS SET APPROPRIATELY, A SAVE OPERATION
  IS ALSO DONE.  IF RENSW='RENUM', ALL RECORDS STARTING WITH THE ONE
  BEYOND THE ONE POINTED TO BY PRMPTR WILL BE RESEQUENCED USING
  SEQINCR AND SEQSTRT.  IF RENSW='NORENUM', ALL RECORDS STARTING WITH
  THE ONE BEYOND THE ONE POINTED TO BY PRMPTR WILL BE SEQUENCE
  CHECKED AND WILL BE RESEQUENCED ONLY IF NECESSARY. */
  DENTRS:PROC(RHPTR,SUBENT,PRMPTR,SAVE,RENSW,LAST,RHPPTR)
    OPTIONS(REENTRANT);
    % DCL @PROCNAME CHAR;
    % @PROCNAME ='';
    % INCLUDE DENTSUB;
    DCL RHPPTR PTR;
    DCL (SAVE,RENSW) CHAR(*);
    DCL (PRMPTR,TMPPTR,DPTR) UNALIGNED PTR;
    DCL LAST FIXED BIN(31,0);
    DPTR = NULL;
    DCL OUT FILE RECORD OUTPUT ENV(RECSIZE(80));
    DCL OPCODE CHAR(8) VAR INIT('INIT');
    DCL OPCODEL CHAR(8) VAR INIT('INITLAST');
    DCL (I,J) FIXED;
    DCL SEQBL CHAR(8),SEQPIC PIC'(8)9'DEF SEQBL; /* MAX SEQ# LEN IS 8 */
    TMPPTR = PRMPTR;
    IF NXTHDR = HDPTR
      THEN IF SAVE ='SAVE'
        THEN CALL #WRITE(RHPTR,SUBENT,OUTDDN,OPCODEL,DPTR);
        /* CALL DENTW */
        ELSE;
      ELSE DO;
        IF RENSW='RENUM'
          THEN J = SEQINCR;
          ELSE J = 1;
        IF TMPPTR = HDPTR
          THEN IF RENSW='RENUM'
            THEN I = SEQSTRT;
            ELSE I = 1;
          ELSE I = TMPPTR->RECSEQ+J;
        HI=0;
        DO I=I REPEAT I+J WHILE(TMPPTR->NXTPTR ^= HDPTR)UNTIL(SUBSTR(
        OPCODE,1,4)='LAST');
          TMPPTR = TMPPTR->NXTPTR;
          IF TMPPTR->RECSEQ < I × (RENSW='RENUM')
            THEN TMPPTR->RECSEQ = I;
            ELSE I = TMPPTR->RECSEQ;
          HI=MAX(HI,I);
          IF RECPTR = TMPPTR
            THEN CURSEQ = I;
          IF SAVE ='SAVE'
            THEN DO;
              IF TMPPTR->NXTPTR = HDPTR
                THEN OPCODE ='LAST'××OPCODE;
                ELSE IF TMPPTR->NXTPTR->RECSEQ>LAST
                  THEN OPCODE ='LAST'××OPCODE;
              CALL #WRITE(RHPTR,SUBENT,OUTDDN,OPCODE,TMPPTR);
              /* CALL DENTW */
              OPCODE ='NXT ';
              END;
          END;
        IF HI>RHPMAXSQ
          THEN DO;
            CALL SUBENT(35)(RHPPTR,HI); /* DENTSQ */
            CALL #COLTR(RHPPTR,SUBENT); /* DENTCT */
            END;
        CALL #NC(RHPTR,SUBENT,RECPTR); /* CALL NEWCUR */
        END;
    SAVEFLAG='1'B;
    RETURN;
    END DENTRS;
