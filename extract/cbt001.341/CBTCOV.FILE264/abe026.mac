 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABE026,$PURGE,$JE,$CC,$PRT,$PK,$EDIT,$PP,PURGE,PRT */
 /* 1      2      3   4   5    6   7     8   9     10  */
   %INCLUDE ABESUB;
 ABESUB(ABE026) PARS(0000000000000000) CHECK(YES);
   %INCLUDE ABESUBA;

         DCL (TP,EPTR) PTR;
         DCL SETCAPS ENTRY;
         DCL KEYTABLE(6) CHAR(6) VAR STATIC INIT(
           'DEST','SYSOUT','CC','NOCC','HOLD','SEQUENCE');
         DCL INDX FIXED BIN(15,0);
         DCL OPER CHAR(80) VAR;
         DCL (DEST,SYSOUT,CC,HOLD,SEQ) CHAR(20) VAR;
         DCL CMDSTR CHAR(240) VAR;
         DCL HELPSTR CHAR(4) VAR STATIC INIT('');
         DCL PKDATA CHAR(80) VAR STATIC INIT(
           '/$PRT/END NOSAVE');
         DCL PPDATA CHAR(80) VAR STATIC INIT(
           '/$PRT/$PURGE');
         DCL EDDATA CHAR(80) VAR STATIC INIT(
           '/ABE LASTJCL.CNTL/$PURGE');
         DCL SUFFIX ENTRY(CHAR(*), CHAR(*))
           RETURNS(FIXED BIN(15,0));
         DCL CMDLAB(10) LABEL;
         GO TO CMDLAB(CMDINDX);
 CMDLAB(1):  /* ABE026 */
 CMDLAB(2):  /* $PURGE */
 CMDLAB(9):  /* PURGE */
         IF OUTDDN^=NULL
           THEN DO;
             CALL #CRTZAP(RHPPTR); /* CALL CRTZAPS - CLEAR SCREEN */
             IF SUBSTR(OUTDDN->ALCFLAG,9,1) /* DB2? */
               THEN DO; /* IT IS DB2 TABLE */
                 RHRC=#DB2COM('DROP TABLE '××OUTDDN->ALCDSN,#TERM);
                 IF RHRC^=0
                   THEN DO;
                     IF MSGDATA=''
                       THEN MSGDATA='DB2 RETURN CODE='××RHRC;
                     RETURN;
                     END;
                 RHRC=#DB2COM('COMMIT',#TERM);
                 IF RHRC^=0
                   THEN DO;
                     IF MSGDATA=''
                       THEN MSGDATA='DB2 RC FROM COMMIT='××RHRC;
                     RETURN;
                     END;
                 END;
               ELSE DO; /* IT IS OS DATASET */
                 CMDSTR = 'DELETE '''××OUTDDN->ALCDSN;
                 IF OUTDDN->ALCMEM^=''
                   THEN CMDSTR=CMDSTR××'('××SUBSTR(OUTDDN->ALCMEM,1,
                     SUFFIX(OUTDDN->ALCMEM,' '))××')';
                 CMDSTR=CMDSTR××'''';
                 CALL #DOCMD(CMDSTR,HELPSTR);
                 END;
             END;
         CMDDATA = 'END NOSAVE';
         RHRC = 0;
         RETURN;
 CMDLAB(3): /* $JE */
         CMDDATA='F IEF376I';
         RHRC = 0;
         RETURN;
 CMDLAB(4):  /* $CC */
         DO TP=NXTHDR REPEAT TP->NXTPTR
           WHILE(TP^=HDPTR) UNTIL(TP->NXTPTR=HDPTR);
           IF INDEX(TP->RECSTR,'IEF376I')^=0
             THEN LEAVE; /* END OF MSGCLASS SYSOUT */
           IF INDEX(TP->RECSTR,'IEF142I')^=0 ×
              INDEX(TP->RECSTR,'IEF272I')^=0 ×
              INDEX(TP->RECSTR,'IEF472I')^=0
             THEN SUBSTR(TP->RECFLGS,2,1)='0'B; /* DISPLAY RECORD */
             ELSE SUBSTR(TP->RECFLGS,2,1)='1'B; /* SUPPRESS RECORD */
           END;
         CALL #NC(RHPTR,NXTHDR); /*CALL NEWCUR-GOTO TOP*/
         RHRC = 0;
         RETURN;
 CMDLAB(5):  /* $PRT */
 CMDLAB(10):  /* PRT */
         /* SET DEFAULTS */
         DEST='LOCAL';
         SYSOUT='A';
         IF OUTDDN^=NULL
           THEN SELECT(SUBSTR(OUTDDN->ALCRECFM,6,2));
             WHEN('10'B) CC='CTLASA';
             WHEN('01'B) CC='CTL360';
             OTHERWISE   CC='CTLNON';
             END; /* SELECT */
           ELSE CC='CTLNON';
         HOLD='';
         SEQ='';
         DO I = 1 TO HBOUND(PARY,1) WHILE(PARY(I)^='');
           CALL SETCAPS(PARY(I));
           CALL #SPARS(PARY(I),KEYTABLE,OPER,INDX);
             /* CALL SPARS */
           SELECT(INDX);
             WHEN(0) DO;  /* INVALID OPERAND */
               MSGDATA='INVALID OPERAND';
               RHRC = 8;
               RETURN;
               END;
             WHEN(1) DO;  /* DEST */
               DEST=OPER;
               END;
             WHEN(2) DO;  /* SYSOUT */
               SYSOUT=OPER;
               END;
             WHEN(3) DO;  /* CC */
               IF OPER='M'
                 THEN CC = 'CTL360';
                 ELSE CC = 'CTLASA';
               END;
             WHEN(4) DO;  /* NOCC */
               CC = 'CTLNON';
               END;
             WHEN(5) DO;  /* HOLD */
               HOLD = 'HOLD';
               END;
             WHEN(6) DO;  /* SEQUENCE */
               SEQ = 'SEQ';
               END;
             OTHERWISE;
             END; /* SELECT */
           END;
         CC=CC××SEQ;
         CMDSTR='ALLOC FI(ABEPRT) SYSOUT('××SYSOUT××') REUSE';
         CALL #DOCMD(CMDSTR,HELPSTR); /* CALL DOCMD */
         CALL #WRITE(RHPTR,RHHDRPT,INDDN,CC,RECPTR);
           /* CALL ABEW */
         CMDSTR='FREE FI(ABEPRT) SYSOUT('××SYSOUT××') DEST('××DEST
           ××') '××HOLD;
         CALL #DOCMD(CMDSTR,HELPSTR); /* CALL DOCMD */
         RHRC = 0;
         RETURN;
 CMDLAB(6):  /* $PK */
         CALL #PUSHLILO(RHPTR,RH_STACK,RH_QUEUE,PKDATA);
           /* CALL ABECP */
         RHRC = 0;
         RETURN;
 CMDLAB(7):  /* $EDIT */
         CALL #PUSHLILO(RHPTR,RH_STACK,RH_QUEUE,EDDATA);
           /* CALL ABECP */
         RHRC = 0;
         RETURN;
 CMDLAB(8):  /* $PP */
         CALL #PUSHLILO(RHPTR,RH_STACK,RH_QUEUE,PPDATA);
           /* CALL ABECP */
         RHRC = 0;
         END ABE026;
