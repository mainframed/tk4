*  SYSATTN
*$.HE GENERALIZED SUBROUTINES - SYSATTN
*$.HE GENERALIZED STAX SUBROUTINE
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$09/09/80
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSATTN
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE SUPPLIES A GENERALIZED ATTENTION HANDLING FACILITY.
*$THE PARAMETER LIST IS AS IN THE FOLLOWING EXAMPLE:
*$.SP 1
*$.IN 5
*$.NF
*$DCL SYSATTN ENTRY(FIXED BIN(15,0),FIXED BIN(31,0));
*$DCL COUNTER FIXED BIN(15,0);
*$DCL ECB FIXED BIN(15,0);
*$CALL SYSATTN(COUNTER,ECB);
*$.FI L
*$.IN 0
*$WHENEVER AN ATTENTION INTERRUPT IS RECEIVED, COUNTER WILL BE
*$INCREMENTED, AND ECB WILL BE POSTED.
*
SYSATTN  PLIENTRY ,DSA=256
         LR    R2,R1
         LA    R0,8
         GETMAIN R,LV=(0)
         LR    RC,R1
         MVC   0(8,RC),0(R2)     SAVE PARMS
         MVC   STAXE,STAXC
         LA    R1,STAXE
         STAX  ATTN,USADDR=(RC),MF=(E,(1))
         PLIEXIT
         DROP  RA
ATTN     DS    0H
         STM   RE,RC,12(RD)       SAVE REGS
         BALR  RA,0
         USING *,RA
         L     RC,8(R1)           RESTORE BASE REG
         LM    R2,R3,0(RC)
         LH    R4,0(R2)           R4=HALFWORD ATTENTION COUNTER
         LA    R4,1(R4)           INCREMENT BY 1
         STH   R4,0(R2)           STORE ATTENTION COUNTER BACK
         TM    0(R3),X'40'        IS ECB ALREADY POSTED?
         BC    1,GETOUT           BR IF YES
         LA    R3,0(R3)
         POST  (R3),(R4)
GETOUT   DS    0H
         LM    RE,RC,12(RD)
         BR    RE
STAXC    STAX  ATTN,MF=L
STAXLEN  EQU   *-STAXC
         LTORG
DSA      DSECT
STAXE    DS    CL(STAXLEN)
         DS    0D
DSALEN   EQU   *-DSA
         END
