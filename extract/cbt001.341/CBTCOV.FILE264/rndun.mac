* RNDUN
*$.HE GENERALIZED SUBROUTINES - RNDUN
*$.HE RANDOM NUMBERS FROM A UNIFORM DISTRIBUTION
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$09/17/76
*$AUTHOR:  HAROLD WRIGHT, HUGHES AIRCRAFT
*$SOURCE MODULE NAME:  RNDUN
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE GENERATES RANDOM NUMBERS FROM A POPULATION
*$UNIFORMLY DISTRIBUTED BETWEEN 0. AND 1.
*$CALL FORMAT IS AS FOLLOWS:
*$.IN 5
*$.SP 1
*$.NF
*$CALL RNDUN (I,Y,N)
*$.SP 1
*$WHERE:
*$.FI L
*$.HI 5
*$Y IS A FLOATING POINT ARRAY WHERE THE SEQUENCE OF RANDOM NUMBERS
*$WILL BE STORED.  THIS ARRAY MSUT BE DIMENSIONED AT LEAST N.
*$.SP 1
*$I IS AN INTEGER VARIABLE INITIALIZED BY THE USER TO A
*$POSITIVE VALUE AND USED BY THE ROUTINE
*$TO GENERATE THE RANDOM NUMBERS.
*$.SP 1
*$N IS THE NUMBER OF RANDOM NUMBERS TO BE GENERATED AND
*$TO BE STORED IN THE ARRAY Y.
*$.HI 0
*$.IN 0
*$.SP 1
*$RESTRICTION:  I MUST BE IN INTEGER VARIABLE INITIALIZED TO A
*$VALUE GREATER THAN ZERO.
*$.SP 1
*$EXAMPLE:
*$.SP 1
*$TO GENERATE A SEQUENCE OF 500 UNIFORM RANDOM NUMBERS AND A
*$DISTINCT SEQUENCE OF 250 UNIFORM RANDOM NUMBERS, EACH SEQUENCE
*$FROM THE RANGE (0.,1.)L
*$.SP 1
*$.IN 5
*$.NF
*$DIMENSION Y(500), Z(250)
*$I1 =33
*$I2 = 635
*$CALL RNDUN(I1, Y, 500)
*$CALL RNDUN(I2, Z, 250)
*           RNDUN - RANDOM NUMBERS FROM A UNIFORM DISTRIBUTION
RNDUN    CSECT CONTROL SECTION FOR FLOATING-POINT NUMBER.
         USING RNDUN,15 ESTABLISH BASE REGISTER
         STM   0,4,20(13)   SAVE GR 0-4
         LM    1,3,0(1)      ADDRESS OF PARAMETERS
         LR    4,1
         L     3,0(3)        3 = NUMBER
LOOP     EQU   *
         L     1,0(4)   LOAD X(N) IN R1
         M     0,=F'16807'  X(N+1)=K*X(N) MOD(2**31-1)  K=7**5=16807
         SLDA  0,1 ISOLATE HIGH ORDER 31 BITS (R).
         SRL   1,1 ISOLATE LOW ORDER 31 BITS. SIGN +.
         AR    0,1 X'(N+1) = K*X(N) + R (LOW ORDER 31 BITS).
         BO    OVF X'(N+1) OVERFLOW.
CONT     ST    0,0(4)  X'(N+1) GOES IN FORTRAN PROVIDED STORAGE
         SR    1,1 ZERO LOW ORDER DOUBLEWORD.
         SRDA  0,7 MAKE PCOM FOR MASK.
         A     0,=X'40000000' MASK IN FLOATING-POINT EXPONENT.
         STM   0,1,TEMP STORE UNNORMALIZED DOUBLEWORD TEMPORARILY.
         SDR   0,0 CLEAR LOW-ORDER DOUBLE FLOATING-POINT REG.
         AD    0,TEMP PLACE NORMALIZED DOUBLEWORD FLOATING-POINT       X
               NUMBER IN FLOATING-POINT REGISTER 0.
         STE   0,0(2)        VALUE TO ARRAY
         LA    2,4(2)        BUMP ARRAY
         BCT   3,LOOP        TEST FOR DONE
         MVI   12(13),X'FF'   SET RETURN INDICATOR
         LM    0,4,20(13)    RESTORE R0-R4
         BR    14 RETURN TO FORTRAN.
OVF      SLL   0,1 SIGN BIT +.
         SRL   0,1 SIGN BIT +.
         A     0,=F'1' ADD 1 TO GET X'(N+1) + 1.
         B     CONT RETURN TO MAIN CODING TO FINISH.
TEMP     DS    D TEMPORARY STORAGE ON DOUBLEWORD BOUNDARY FOR SPEED.
         LTORG FORCE LITERAL ASSEMBLY INTO THIS CONTROL SECTION.
         END
