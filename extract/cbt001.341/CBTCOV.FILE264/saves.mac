         MACRO
&LABEL   SAVES &RETURN,&REG=(2,12),&NOSAVE=DEFALT**,&SPECIAL=,&BASE=12,C
               &XCTL=(DEFALT**,DEFALT**)
         GBLB   &TEST,&LIST
         LCLA   &REG1,&REG2,&REG3,&REG4
         LCLB   &RET,&ERROR,&CONT,&NOSAV,&TRCTL,&XREG,&XADR
          AIF   (NOT &LIST).START
*
*              SAVE MACRO AUTOMATICALLY SAVES REGISTERS 14-2 PLUS ANY
*                   REGISTERS DESIGNATED IN THE &REG PARAMETER
*                   (IF &REG IS OMITTED ALL REGISTERS ARE SAVED)
*              IT ALSO PROVIDES A SAVE AREA UNLESS &NOSAVE=NOSAVE
*
*              &RETURN GENERATES A LABEL ROR RETURNING
*
*              &XCTL (A,B) IS UTILIZED TO CREATE AN ENTRY POINT FOR
*                   TRANSFERRING CONTROL TO ANOTHER PROGRAM
*
*                   A = LABEL FOR XCTL ENTRY POINT
*                   B = NAME OF THE PROGRAM TO RECEIVE CONTROL
*                            OR
*                       THE ADDRESS OF THE 8 CHARACTER NAME IN CORE
*                            OR
*                       A NUMBER DESIGNATING A REGISTER WHICH WILL
*                            CONTAIN THE 8 CHARACTER NAME ADDRESS
*                            (THIS NUMBER MUST NOT BE ENCLOSED IN
*                             PARENTHESES)
*
.START   ANOP
         AIF   (NOT &TEST).TSTSAV1
         MNOTE 0,'RETURN,REG(1),REG(2),NOSAVE,SPECIAL,BASE,XCTL'
         MNOTE 0,'&RETURN,&REG(1),&REG(2),&NOSAVE,&SPECIAL,&BASE,&XCTL'
.TSTSAV1 ANOP
         AIF   (T'&RETURN EQ 'O').SAVE1
         AIF   (T'&RETURN EQ 'U').SAVE01
         MNOTE '&RETURN IS PREVIOUSLY DEFINED'
&ERROR   SETB  1
.SAVE01  ANOP
&RET     SETB  1
.SAVE1   ANOP
 AIF ('&XCTL(1)' EQ 'DEFALT**' AND '&XCTL(2)' EQ 'DEFALT**').SAVE2
 AIF ('&XCTL(1)' EQ 'DEFALT**' OR '&XCTL(2)' EQ 'DEFALT**').SAVE1A
         AIF   (T'&XCTL(1) NE 'U' OR T'&XCTL(2) EQ 'O').SAVE1A
&TRCTL   SETB  1
         AIF   (T'&XCTL(2) EQ 'N').SAVE1B
         AIF   (T'&XCTL(2) EQ 'U').SAVE1D
         AIF   (T'&XCTL(2) EQ 'I').SAVE1A
         AIF   (T'&XCTL(2) EQ 'J').SAVE1A
         AIF   (T'&XCTL(2) EQ 'M').SAVE1A
         AIF   (T'&XCTL(2) EQ 'T').SAVE1A
         AIF   (T'&XCTL(2) EQ 'W').SAVE1A
         AIF   (T'&XCTL(2) EQ 'C').SAVE1C
         MNOTE 0,'WARNING &XCTL(2) MAY REFER TO AN ERRONEOUS ADDRESS'
.SAVE1C  ANOP
&XADR    SETB  1
         AGO   .SAVE2
.SAVE1B  ANOP
&REG4    SETA  &XCTL(2)
         AIF   (&REG4 GT 15).SAVE1A
&XREG    SETB  1
         AGO   .SAVE2
.SAVE1A  MNOTE 'XCTL = &XCTL IS IMPROPER SPECIFICATION'
&ERROR   SETB  1
         AGO   .SAVE2
.SAVE1D  AIF   ('&XCTL(2)'(1,1) EQ '(').SAVE1A
.SAVE2   ANOP
         AIF   ('&NOSAVE' EQ 'DEFALT**').SAVE3
         AIF   ('&NOSAVE' EQ 'NOSAVE').SAVE4
         MNOTE 'NOSAVE=&NOSAVE IS IMPROPER SPECIFICATION'
&ERROR   SETB  1
.SAVE4   ANOP
&NOSAV   SETB  1
.SAVE3   ANOP
         AIF   ('&BASE' EQ 'NOBASE').SAVE5
         AIF   (T'&BASE NE 'N').SAVE3E
         AIF   (&BASE LT 13 AND &BASE GT 1).SAVE5
.SAVE3E  MNOTE 'BASE EQUALS &BASE IS IMPROPER SPECIFICATION'
&ERROR   SETB  1
.SAVE5   ANOP
         AIF   (K'&REG EQ 0 AND NOT &ERROR).SAVE8
         AIF   (K'&REG EQ 0 AND &ERROR).SAVE6
         AIF   (N'&REG GT 1).SAVE3G
         AIF   (T'&REG NE 'N').SAVE3H
         AIF   (&REG(1) GE 13 OR &REG(1) LE 1).SAVE8
&REG1    SETA  &REG(1)
         AIF   (&REG1 EQ 2).SAVE9
&REG1    SETA  1
&REG2    SETA  &REG(1)
&REG3    SETA  &REG(1)
         AGO   .SAVE20
.SAVE3G  ANOP
         AIF   (T'&REG(1) EQ 'N' AND T'&REG(2) EQ 'N').SAVE7
.SAVE3H  ANOP
         MNOTE 'REG=&REG IS IMPROPER SPECIFICATION'
.SAVE6   MNOTE 'NO GENERATION'
         MEXIT
.SAVE7   ANOP
         AIF   (&ERROR).SAVE6
         AIF   (&REG(1) GT &REG(2)).SAVE15
         AIF   (&REG(1) LT 13).SAVE10
.SAVE8   ANOP
.*     HERE WHEN ONLY STANDARD REGISTERS ARE TO BE SAVED
&REG1    SETA  1
.SAVE9   ANOP
.*     INDICATE SAVE AREA IS CONTINUOUS - ONLY ONE STM IS NEEDED
&CONT    SETB  1
         AGO   .SAVE20
.SAVE10  ANOP
         AIF   (&REG(1) GT 2).SAVE13
.SAVE11  ANOP
.*     HERE WHEN STANDARD REGISTERS PLUS 2 THRU REG(2) ARE TO BE SAVED
&REG1    SETA  &REG(2)
         AIF   (&REG1 LE 12).SAVE9
.SAVE12  ANOP
.*     ALL REGISTERS ARE TO BE SAVED
&REG1    SETA  12
         AGO   .SAVE9
.SAVE13  ANOP
.*     HERE IF REG(1) IS GREATER THAN 2 BUT LESS THAN 13
&REG1    SETA  1
&REG2    SETA  &REG(1)
&REG3    SETA  &REG(2)
         AIF   (&REG3 LE 12).SAVE20
&REG3    SETA  12
         AGO   .SAVE20
.SAVE15  ANOP
         AIF   (&REG(1) GE 13).SAVE11
         AIF   (&REG(2)+1 EQ &REG(1)).SAVE12
&REG1    SETA  &REG(2)
&REG2    SETA  &REG(1)
&REG3    SETA  12
         AIF   (&REG1 GE 1).SAVE20
&REG1    SETA  1
.SAVE20  ANOP
         AIF   ('&BASE' EQ 'NOBASE').GO
         AIF   (&CONT OR (&BASE GT &REG1)).SAVE19
         MNOTE *,'BASE REGISTER CAUSES IMPLEMENTATION DIFFICULTIES'
&REG1    SETA  &REG1+1
&REG2    SETA  &REG2-1
         MNOTE *,'&REG1 TO &REG2 ARE ALSO SAVED'
&REG1    SETA  &REG3
&CONT    SETB  1
.SAVE19  ANOP
         AIF (&BASE LE &REG1 OR (&BASE GE &REG2 AND &BASE LE &REG3)).GO
         MNOTE *,'BASE REGISTER (&BASE) IS NOT SAVED'
.*
.GO      ANOP
.*                  BEGIN ACTUAL GENERATION HERE
         AIF   (NOT &TEST).TSTSAV2
         MNOTE 0,'REG1,REG2,REG3,REG4'
         MNOTE 0,'&REG1,&REG2,&REG3,&REG4'
         MNOTE 0,'RET,CONT,NOSAV,TRCTL,XREG,XADR,ERROR'
         MNOTE 0,'&RET,&CONT,&NOSAV,&TRCTL,&XREG,&XADR,&ERROR'
.TSTSAV2 ANOP
         AIF   ('&SPECIAL' EQ 'SPECIAL').ASAVE1
.*     GENERATION FOR SAVE
         AIF   ('&BASE' EQ 'NOBASE').SAVE00
         USING *,&BASE
.SAVE00  ANOP
&LABEL   STM   14,&REG1,12(13)
         AIF   (&CONT).SAVE22
         AIF   (&REG2 EQ &REG3).SAVE21
         STM   &REG2,&REG3,&REG2*4+20(13)
         AGO   .SAVE22
.SAVE21  ANOP
         ST    &REG2,&REG2*4+20(0,13)
.SAVE22  ANOP
         AIF   ('&BASE' EQ 'NOBASE').SAVE220
         LR    &BASE,15
.SAVE220 ANOP
         AIF   (&NOSAV).SAVE30
         LR    14,13
         CNOP  0,4
         BAL   13,NULL&SYSNDX
         DC    3F'0'
         DS    15F
         AIF   (NOT &RET AND NOT &TRCTL).SAVE23
         AIF   (NOT &TRCTL).SAVE239
.SAVE221 ANOP
&XCTL(1) DS    0F
         AIF   (&XREG).SAVE222
         AIF   (&XADR).SAVE223
         BAL   15,*+20
         DC    A(*+8)
         DC    A(0)
         DC    CL8'&XCTL(2)'
         AGO   .SAVE224
.SAVE222 ANOP
         ST    &REG4,*+8
         BAL   15,*+12
         DC    A(0)
         DC    A(0)
         AGO   .SAVE224
.SAVE223 ANOP
         BAL   15,*+12
         DC    A(&XCTL(2))
         DC    A(0)
.SAVE224 ANOP
         AIF   (&NOSAV).SAVE226
         L     13,4(0,13)
.SAVE226 ANOP
         L     14,12(0,13)
         AIF   (&REG1 LT 2).SAVE228
         AIF   (&REG1 EQ 2).SAVE227
         LM    2,&REG1,28(13)
         AGO   .SAVE228
.SAVE227 ANOP
         L     2,28(0,13)
.SAVE228 AIF   (&CONT).SAVE232
         AIF   (&REG2 EQ &REG3).SAVE231
         LM    &REG2,&REG3,&REG2*4+20(13)
         AGO   .SAVE232
.SAVE231 ANOP
         L     &REG2,&REG2*4+20(13)
.SAVE232 ANOP
         SVC   7             ISSUE THE XCTL
         AIF   (&NOSAV).SAVE301
.SAVE239 ANOP
         AIF   (NOT &RET).SAVE23
&RETURN  L     13,4(0,13)
.SAVE29  ANOP
         L     14,12(0,13)
         AIF   (&REG1 LT 2).SAVE25
         AIF   (&REG1 EQ 2).SAVE24
         LM    2,&REG1,28(13)
         AGO   .SAVE25
.SAVE24  ANOP
         L     2,28(0,13)
.SAVE25  ANOP
         AIF   (&CONT).SAVE27
         AIF   (&REG2 EQ &REG3).SAVE26
         LM    &REG2,&REG3,&REG2*4+20(13)
         AGO   .SAVE27
.SAVE26  ANOP
         L     &REG2,&REG2*4+20(0,13)
.SAVE27  ANOP
         BR    14
         AIF   (&NOSAV).SAVE31
.SAVE23  ANOP
NULL&SYSNDX ST 13,8(0,14)
         ST   14,4(0,13)
         MEXIT
.SAVE30  ANOP
         AIF   (NOT &RET AND NOT &TRCTL).END
         B     NULL&SYSNDX
         AIF   (&TRCTL).SAVE221
.SAVE301 ANOP
         AIF   (NOT &RET).SAVE31
&RETURN  DS    0H
         AGO   .SAVE29
.SAVE31  ANOP
NULL&SYSNDX DS 0F
         MEXIT
.ASAVE1  ANOP
.*     GENERATION FOR ASAVE
         AIF   ('&BASE' EQ 'NOBASE').ASAVE10
         USING *,15
.ASAVE10 ANOP
&LABEL   STM   13,&REG1,NULL&SYSNDX
         AIF   (&CONT).ASAVE3
         AIF   (&REG2 EQ &REG3).ASAVE2
         STM   &REG2,&REG3,NULL&SYSNDX+12+&REG2*4
         AGO   .ASAVE3
.ASAVE2  ANOP
         ST    &REG2,NULL&SYSNDX+12+&REG2*4
.ASAVE3  ANOP
         AIF   ('&BASE' EQ 'NOBASE').ASAVE30
         DROP  15
         SR    13,13
         BALR  &BASE,0
         USING *,&BASE
.ASAVE30 ANOP
         AIF   (&NOSAV).ASAVE4
         CNOP  0,4
         BAL   13,NULL&SYSNDX+64
         DC    3F'0'
         DS    15F
         AGO   .ASAVE5
.ASAVE4  ANOP
         B     NULL&SYSNDX+64
.ASAVE5  ANOP
         AIF   (NOT &RET).ASAVE9
&RETURN  LM    13,&REG1,NULL&SYSNDX
         AIF   (&CONT).ASAVE8
         AIF   (&REG2 EQ &REG3).ASAVE7
         LM    &REG2,&REG3,NULL&SYSNDX+12+&REG2*4
         AGO   .ASAVE8
.ASAVE7  ANOP
         L     &REG2,NULL&SYSNDX+12+&REG2*4
.ASAVE8  ANOP
         BR    14
.ASAVE9  ANOP
NULL&SYSNDX DS 16F
         MEXIT
.END     ANOP
         MEND
