 /* DENTFSU */
 DENTFSU:  PROC(PARPTR,RHHDRPT,SUBENT);
 /* CALLED TO PROCESS - $CURSOR <PANEL> <POS> */
   % DCL @PROCNAME CHAR;
   % @PROCNAME='';
   % INCLUDE DENTSUB;
   % INCLUDE DENTPNL;
   DCL (SETCAPS,SBACONV) ENTRY;
   DCL DENTPFN ENTRY RETURNS(PTR);
   DCL NAME CHAR(8);
   DCL AD(0:32766) CHAR(1) UNALIGNED BASED(T);
   DCL HWD UNALIGNED FIXED BIN(15,0) BASED(DUMPTR);
   DCL 1 MPNLCALL,
     2 MCURPNL FIXED BIN(15,0) INIT(0),
     2 MNXTPNL FIXED BIN(15,0) INIT(1),
     2 MARYPTR PTR,
     2 MAID BIT(8),
     2 MCURLOC FIXED BIN(15,0),
     2 MCHGCURS FIXED BIN(15,0) INIT(-1),
     2 MCALLFLGS BIT(8) INIT('0'B);
   DCL 1 @PNLARYS,
     2 @ARYCNT FIXED BIN(31,0) INIT(1),
     2 @PNLARY(1),
      3 @PNLSTAPT PTR,
       /* PTR TO STATIC PORTION OF DEFINITION */
      3 @PNLDYNPT PTR;
       /* PTR TO DYNAMIC PORTION OF DEFINITION */
   DCL SBADDR FIXED BIN(15,0),
       SBADDC CHAR(2) DEF SBADDR;
   NAME=PARY(2);
   CALL SETCAPS(NAME);
   T=DENTPFN(RHHDRPT,SUBENT,NAME); /* GO GET PANEL */
   PNLPTR=ADDR(AD(4));
   IF VERIFY(PARY(3),'0123456789')=0
     THEN DO; /* NUMERIC POSITION IS SPECIFIED */
       CURSBAB,SBADDR=PARY(3);
       CALL SBACONV(SBADDR,'1'B); /* GO CONVERT TO TRANS CODE */
       CURSBA=SBADDC;
       END;
     ELSE DO; /* IT MUST BE FIELD NAME */
       NAME=PARY(3);
       CALL SETCAPS(NAME);
       IF PNLLEN<0
         THEN SFLDPTR=ADDR(FLDLOC); /* POINT TO FIRST FIELD DESCR */
         ELSE SFLDPTR=ADDR(NEWLEN); /* POINT TO FIRST FIELD DESCR */
       DO SFLDADJ=SFLDADJ REPEAT SFLDADJ+FLDLEN
         UNTIL(FLDNUM=FLDCNT × FLDNAME=NAME);
         END;
       CURSBAB,SBADDR=FLDSBAB+1;
       CALL SBACONV(SBADDR,'1'B); /* GO CONVERT TO TRANS CODE */
       CURSBA=SBADDC;
       END;
   END DENTFSU;
