 /* BREAK */
 /*
0*$.HE GENERALIZED SUBROUTINES - BREAK
0*$.HE ATOMIZE INPUT
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$03/28/81
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  BREAK
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE USED TO ATOMIZE A STREAM OF TEXT.
0*$CALL FORMAT IS AS FOLLOWS:
0*$.IN 5
0*$.SP 1
0*$.NF
0*$DCL BREAK ENTRY(ENTRY,CHAR(*),
0*$  CHAR(*) VAR, FIXED BIN(15,0))
0*$  RETURNS(CHAR(256) VAR);
0*$DCL DELIM CHAR(3) INIT(' ,.');
0*$DCL RECORD CHAR(80) VAR INIT('');
0*$DCL OFF FIXED BIN(15,0) INIT(0);
0*$DCL WORD CHAR(256) VAR;
0*$DO UNTIL(LENGTH(WORD)=0);
0*$  WORD=BREAK(GETREC,DELIM,RECORD,OFF);
0*$  ** WORD IS EACH SUCCESSIVE WORD **
0*$  ** OR DELIMITER IN INPUT TEXT   **
0*$  END;
0*$GETREC:  PROC(RECORD);
0*$  DCL RECORD CHAR(*) VAR;
0*$  ON ENDFILE(IN) GO TO EOJ;
0*$  READ FILE(IN) INTO(RECORD);
0*$  RETURN;
0*$  EOJ: RECORD='';
0*$  END GETREC;
 */
 BREAK:  PROC(EP,DELIM,RECORD,OFF) RETURNS(CHAR(256) VAR);
 /**********************************************************************
 * WILL RETURN WORDS AND DELIMITERS.  EP SHOULD INDICATE NO MORE DATA  *
 * BY SETTING RECORD TO A NULL STRING.  ON RETURN OF CONTROL, OFF WILL *
 * ALWAYS BE SET TO THE OFFSET, RELATIVE 1, OF THE LAST CHARACTER IN   *
 * RECORD RETURNED.  RECORD SHOULD BE A NULL STRING AT INITIAL ENTRY.  *
 **********************************************************************/
   DCL EP ENTRY(CHAR(*) VAR);
   DCL DELIM CHAR(*);
   DCL RECORD CHAR(*) VAR;
   DCL OFF FIXED BIN(15,0);
   DCL (S,H,I,J,K,L,N,T) FIXED BIN(31,0);
   L=LENGTH(RECORD);
   IF L=0 × OFF>=L
     THEN DO;
       CALL EP(RECORD);
       L=LENGTH(RECORD);
       IF L=0
         THEN RETURN(''); /* ENDFILE ALREADY */
       OFF=0;
       END;
   S=OFF; /* SAVE ORIGINAL OFFSET */
   DO OFF=OFF+1 REPEAT OFF+1 WHILE(OFF<=L);
     IF INDEX(DELIM,SUBSTR(RECORD,OFF,1))^=0
       THEN IF OFF-S=1
         THEN RETURN(SUBSTR(RECORD,OFF,1)); /* IT IS DELIMITER */
         ELSE LEAVE; /* FOUND END OF WORD(TOKEN) */
     END;
   OFF=OFF-1; /* BACK UP TO LAST CHARACTER OF WORD(TOKEN) */
   RETURN(SUBSTR(RECORD,S+1,OFF-S));
   END BREAK;
