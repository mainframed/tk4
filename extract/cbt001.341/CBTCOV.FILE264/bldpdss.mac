BLDPDS   CSECT                                                      TGS
********************************************************************TGS
********************************************************************TGS
*                  ON ENTRY, R0 = ADDR OF RECORD                   *TGS
         TGSREQU                                                    TGS
         USING DATABLOK,R1
         USING *,RF                                                 TGS
         LR    R1,R0                   MAKE RECORD ADDRESSIBLE      TGS
         CLC   0(8,R1),=CL8'./'        IS IT CTL STMT
         BC    8,PROCESS               BR IF YES                    TGS
         TM    PROCSW,X'80'            ARE WE CURRENTLY PROCESSING  TGS
*                                           A MEMBER                TGS
         BCR   8,RE                    BR IF NOT                    TGS
PROCESS  EQU   *                                                    TGS
         STM   RE,RC,12(RD)            SAVE CALLERS REGS            TGS
         DROP  RF                                                   TGS
         BALR  RC,R0                                                TGS
         USING *,RC                    SET PERMANENT BASE REG       TGS
         LR    RE,RD                   SAVE RD                      TGS
         CNOP  0,4                                                  TGS
         BAL   RD,NULL0001             RD = ADDR MY SAVE AREA       TGS
         DC    3F'0'                                                TGS
         DS    15F                                                  TGS
RETCTL   L     RD,4(RD)                                             TGS
         L     RE,12(RD)                                            TGS
         LM    R2,RC,28(RD)                                         TGS
         BR    RE                                                   TGS
NULL0001 ST    RD,8(RE)                                             TGS
         ST    RE,4(RD)                                             TGS
*                                                                   TGS
*                                                                   TGS
         LR    RA,R0                   SAVE RECORD ADDR             TGS
         TM    STRTSW,X'80'            IS INITIALIZATION DONE       TGS
         BC    1,BEGIN                 BR IF YES                    TGS
         OI    STRTSW,X'80'                                         TGS
         SR    R7,R7                   R7 = ENTRY COUNT             TGS
         LA    R8,CNTRTNE                                           TGS
         BAL   R9,TIOTPROC                                          TGS
         LTR   R7,R7                   ARE ANY PDS' TO BE BUILT     TGS
         BC    7,DOBLD                 BR IF YES                    TGS
         L     RF,=V(BLDPDS)                                        TGS
         MVC   0(2,RF),=X'07FE'                                     TGS
         B     RETCTL                                               TGS
DOBLD    EQU   *                                                    TGS
         LA    R7,2(R7)                ADD 1 FOR DUM + 1 TO ROUND   TGS
         SRL   R7,1                    ROUND TO DOUBLEWORD          TGS
         SLL   R7,3                    MULTIPLY BY 4                TGS
         ST    R7,TABLEN                                            TGS
         LR    R0,R7                                                TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,TABADDR                                           TGS
         LR    R7,R1                   R7 = DDNAME TABLE ADDR       TGS
         LA    R8,TABSET                                            TGS
         BAL   R9,TIOTPROC                                          TGS
         MVC   0(4,R7),=4X'FF'         SET END OF TABLE             TGS
         LOAD  EP=TGSPDSWR                                          TGS
         ST    R0,PDSADDR              STORE ROUTINE ADDR           TGS
HDRPROC  EQU   *                                                    TGS
         CLC   9(5,RA),=CL5'END'       IS IT EOD                    TGS
         BC    8,EODPROC               BR IF YES                    TGS
         MVI   PROCSW,X'00'            TURN OFF PROCSW              TGS
         L     R7,TABADDR              R7 = TABADDR                 TGS
TABLOOP  EQU   *                                                    TGS
         L     R1,0(R7)                R1 = DDNAME ADDR             TGS
         LTR   R1,R1                   IS IT END OF TABLE           TGS
         BC    4,RETCTL                BR IF YES                    TGS
         CLI   0(R1),C'$'              IS 1ST CHAR '$'              TGS
         BC    8,BIGPROC               BR IF YES                    TGS
         CLC   0(8,R1),63(RA)          OTHERWISE, TEST FOR SPECIFIC TGS
*                                           MEMBER NAME             TGS
         BC    8,GOTMEMB               BR IF EQUAL                  TGS
BUMPTAB  LA    R7,4(R7)                BUMP TABLE INDEX             TGS
         B     TABLOOP                                              TGS
GOTMEMB  EQU   *                                                    TGS
         OI    PROCSW,X'80'            TURN ON PROCSW               TGS
         L     R7,0(R7)                                             TGS
         ST    7,CURENTRY              STORE ENTRY ADDR             TGS
         B     SORCPROC                                             TGS
BIGPROC  LA    R5,72(RA)               R5 = CURR ID ADDR            TGS
         LA    R6,8                                                 TGS
BIGLOOP  EQU   *                                                    TGS
         CLI   0(R1),C'$'              IS THIS POSITION IGNORED     TGS
         BC    7,BIGTEST               BR IF NOT                    TGS
BIGBUMP  LA    R1,1(R1)                BUMP R1                      TGS
         LA    R5,1(R5)                BUMP R5                      TGS
         BCT   R6,BIGLOOP                                           TGS
         B     GOTMEMB                 GO PROCESS THIS MEMBER       TGS
BIGTEST  EQU   *                                                    TGS
         CLI   0(R5),C'$'                                           TGS
         BC    8,BIGBUMP               BR IF YES                    TGS
         CLC   0(1,R1),0(R5)           DO CHARS MATCH               TGS
         BC    7,BUMPTAB               BR IF NOT                    TGS
         B     BIGBUMP                                              TGS
BEGIN    EQU   *                                                    TGS
         CLC   0(8,RA),=CL8'./'        IS IT CTL STMT
         BC    8,HDRPROC               BR IF YES                    TGS
SORCPROC EQU   *                                                    TGS
         L     R1,CURENTRY             R1 = ADDR                    TGS
         LR    R0,RA                   R0 = ADDR OF RECORD          TGS
         L     RF,PDSADDR              RF = SUBRTNE ADDR            TGS
         BALR  RE,RF                   GO TO SUBRTNE                TGS
         B     RETCTL                                               TGS
EODPROC  EQU   *                                                    TGS
         L     R7,TABADDR                                           TGS
EODLP1   EQU   *                                                    TGS
         L     R1,0(R7)                R1 = ADDR DATA BLOCK         TGS
         LTR   R1,R1                   IS IT END OF TABLE           TGS
         BC    4,DELP                  BR IF YES                    TGS
         TM    INITSW,X'80'            HAS PDS BEEN OPENED          TGS
         BC    1,OPNPDS                BR IF YES                    TGS
EODBUMP  LA    R7,4(R7)                BUMP TABLE INDEX             TGS
         B     EODLP1                                               TGS
OPNPDS   EQU   *                                                    TGS
         LR    R0,RA                   R0 = ./ END CARD ADDR        TGS
         L     RF,PDSADDR              RF = SUBRTNE ADDR            TGS
         BALR  RE,RF                                                TGS
         B     EODBUMP                                              TGS
DELP     EQU   *                                                    TGS
         DELETE EP=TGSPDSWR                                         TGS
         L     R7,TABADDR                                           TGS
DELPA    EQU   *                                                    TGS
         L     R1,0(R7)                                             TGS
         LTR   R1,R1                   IS IT END OF TABLE           TGS
         BC    4,FREETAB               BR IF YES                    TGS
         L     R0,BKADDR
         LTR   R0,R0
         BC    8,FREM
         DELETE EP=TGSPDSBK
         XC    BKADDR,BKADDR
         B     LPINDX
FREM     EQU   *
         LA    R0,PDSBKLEN
         FREEMAIN R,LV=(0),A=(1)
LPINDX   EQU   *
         LA    R7,4(R7)                BUMP INDEX                   TGS
         B     DELPA                                                TGS
FREETAB  EQU   *                                                    TGS
         L     R0,TABLEN                                            TGS
         L     R1,TABADDR                                           TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         B     RETCTL                                               TGS
TIOTPROC EQU   *                                                    TGS
         L     R1,16                   R1 = CVT ADDR                TGS
         L     R1,0(R1)                R1 = IEATCBP ADDR            TGS
         L     R1,0(R1)                R1 = MY TCB ADDR             TGS
         L     R1,12(R1)               R1 = MY TIOT ADDR            TGS
         LA    R2,24                   R2 = INITIAL OFFSET          TGS
TIOTLOOP LA    R1,0(R1,R2)             R1 = ADDR NEXT DD ENTRY      TGS
         IC    R2,0(R1)                R2 = LENGTH THIS DD ENTRY    TGS
         LTR   R2,R2                   IS IT END OF TIOT            TGS
         BCR   8,R9                    BR IF YES                    TGS
         LA    R3,NAMTAB                                            TGS
NAMLOOP  CLI   0(R3),X'FF'             IS IT END OF TABLE           TGS
         BCR   8,R8                    BR IF YES                    TGS
         CLC   0(8,R3),4(R1)           SHOULD DD ENTRY BE IGNORED   TGS
         BC    8,TIOTLOOP              BR IF YES                    TGS
         LA    R3,8(R3)                BUMP NAMTAB INDEX            TGS
         B     NAMLOOP                                              TGS
CNTRTNE  EQU   *                                                    TGS
         LA    R7,1(R7)                                             TGS
         B     TIOTLOOP                                             TGS
TABSET   EQU   *                                                    TGS
         LR    R6,R1                   SAVE DD ENTRY ADDR           TGS
         L     R5,BKADDR
         LTR   R5,R5
         BC    7,GOTADR
         LOAD  EP=TGSPDSBK
         ST    R0,BKADDR
         LR    R1,R0
         B     GAD
GOTADR   EQU   *
         LA    RB,PDSBKLEN
         GETMAIN R,LV=(11)
         BCTR  RB,0
         EX    RB,MVCBLK
         LA    RF,NMEXITS
         IC    RE,PDSDCB+36
         SLL   RE,24
         OR    RF,RE
         ST    RF,PDSDCB+36
         LA    RF,PDSDCB
         ST    RF,DECB+8
GAD      EQU   *
         ST    R1,0(R7)
         NI    0(R7),X'00'
         LA    R7,4(R7)                BUMP TABLE INDEX
         LR    R5,R1
         LR    R1,R6                   RESTORE DD ENTRY ADDR TO R1  TGS
         MVC   0(8,R5),4(R1)           MOVE DDNAME TO DATA BLOCK    TGS
         B     TIOTLOOP                                             TGS
BKADDR   DC    F'0'
MVCBLK   MVC   0(0,R1),0(R5)
NAMTAB   DS    0C            TABLE OF DD NAMES TO IGNORE            TGS
         DC    CL8'STEPLIB'                                         TGS
         DC    CL8'SYSUDUMP'                                        TGS
         DC    CL8'SYSABEND'                                        TGS
         DC    CL8'SYSPRINT'                                        TGS
         DC    CL8'SYSUT1'                                          TGS
         DC    CL8'SYSUT2'                                          TGS
         DC    CL8'SYSUT3'
         DC    CL8'SYSUT4'                                          TGS
         DC    CL8'SYSUT5'                                          TGS
         DC    CL8'INDEX'                                           TGS
         DC    CL8'PRINTER'                                         TGS
         DC    CL8'SYSIN'                                           TGS
         DC    CL8'DUMMY'                                           TGS
         DC    CL8'JOBLIB'                                          TGS
         DC    CL8' '                                               TGS
         DC    X'FF'         INDICATES END OF TABLE                 TGS
TABLEN   DC    F'0'          LENGTH OF DDNAME TABLE                 TGS
TABADDR  DC    F'0'          ADDR OF DDNAME TABLE                   TGS
PDSADDR  DC    F'0'          ADDR OF PDS WRITE SUBRTNE              TGS
CURENTRY DC    F'0'          ADDR OF TABLE ENTRY CURRENTLY IN USE   TGS
STRTSW   DC    X'00'         INITIALIZATION SWITCH                  TGS
PROCSW   DC    X'00'         IF 1, PROCESSING MEMBER                TGS
         LTORG                                                      TGS
DATABLOK DSECT                                                      TGS
         TGSPDSBK                                                   TGS
         END
