*$.HE GENERALIZED SUBROUTINES - DIRCNT
*$.HE DETERMINE NUMBER OF PDS DIRECTORY BLOCKS
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  DIRCNT
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBRTNE IS CALLED TO OBTAIN A COUNT OF THE DIRECTORY
*$BLOCKS ALLOCATED TO A PDS.  CALLING SEQUENCE IS AS FOLLOWS:
*$.SP 1
*$.IN 5
*$CALL DIRCNT(DSNAM,J);
*$.IN 0
*$.SP 1
*$WHERE: DSNAME IS THE 44 CHARACTER DSNAME OF THE FILE TO BE
*$ACCESSED, AND
*$J IS FIXED BIN(31,0), AND WILL BE SET BY THIS ROUTINE
*$TO THE COUNT OF DIRECTORY BLOCKS.
*$.SP 1
*$NOTE THAT THIS ROUTINE EXPECTS A DD CARD NAME PDSDIR TO POINT
*$TO THE VOLUME CONTAINING THE PDS TO BE ACCESSED.
*
***********************************************
DIRCNT   CSECT
         SAVES RETCTL
         TGSREQU
         LM    R2,R3,0(R1)             R3 = A(J)
         L     R2,0(R2)                R2 = A(DSNAM)
         TM    INITSW,X'80'
         BC    1,GOTJFCB
         OI    INITSW,X'80'
         RDJFCB PDSDIR
GOTJFCB  DS    0H
         MVC   JFCB(44),0(R2)          MOVE DSNAME TO JFCB
         OI    JFCB+52,X'08'           MAKE SURE JFCB NOT WRITTEN BACK
         OPEN  (PDSDIR,(INPUT)),TYPE=J
         SR    R4,R4
READ     DS    0H
         READ  DECB,SF,PDSDIR,BUFFER,'S'
         CHECK DECB
         LA    R4,1(R4)
         B     READ
DONE     DS    0H
         CLOSE PDSDIR
         ST    R4,0(R3)
         B     RETCTL
INITSW   DC    X'00'
PDSDIR   DCB   DDNAME=PDSDIR,DSORG=PS,MACRF=R,EODAD=DONE,              C
               RECFM=F,BLKSIZE=256,EXLST=EXLST
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL180
BUFFER   DS    64F
         LTORG
         END
