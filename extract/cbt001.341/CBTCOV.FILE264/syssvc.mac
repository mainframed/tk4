*$.HE GENERALIZED SUBROUTINES - SYSSVC
*$.HE SYSSVC - ISSUE AN SVC INSTRUCTION
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/30/78
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSSVC
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE SUPPLIES THE PLI PROGRAMMER WITH THE CAPABILITY OF
*$ISSUING ANY SVC INSTRUCTION.  CALL FORMAT IS AS FOLLOWS
*$.SP 1
*$.IN 5
*$RETCODE = SYSSVC(SVCNO,REG0,REG1);
*$.SP 1
*$.IN 0
*$RETCODE IS DECLARED FIXED BIN(31,0), AND WILL BE SET BY THIS
*$SUBROUTINE TO THE VALUE IN REGISTER 15 AFTER THE SVC IS ISSUED.
*$.SP 1
*$SVCNO IS DECLARED FIXED BIN(15,0), AND MUST BE SET BY THE CALLER
*$TO THE SVC NUMBER TO BE ISSUED.
*$.SP 1
*$REG0 IS A POINTER VARIABLE, AND MUST CONTAIN THE VALUE TO
*$BE LOADED INTO REGISTER 0 BEFORE THE SVC IS EXECUTED.
*$.SP 1
*$REG1 IS A POINTER VARIABLE, AND MUST CONTAIN THE VALUE TO BE
*$LOADED INTO REGISTER 1 BEFORE THE SVC IS EXECUTED.
*$.SP 1
SYSSVC   PLIENTRY ,DSA=256,COMPILE=O
         LM    R2,R5,0(R1)
         L     R0,0(R3)                SET REG0
         L     R1,0(R4)                SET REG1
         LH    R2,0(R2)                GET SVC#
         STC   R2,SVC#
         MVI   SVCINSTR,X'0A'          SET SVC OPCODE
         MVC   BR6INSTR,=X'07F6'
         BAL   R6,SVCINSTR
         ST    RF,0(R5)                SET RETCODE
         PLIEXIT
         LTORG
DSA      DSECT
SVCINSTR DS    0H
         DS    CL1
SVC#     DS    CL1
BR6INSTR DS    H
         END
