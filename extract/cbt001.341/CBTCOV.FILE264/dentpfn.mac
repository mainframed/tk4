 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENTPFN */
 DENTPFN:  PROC(RHHDRPT,SUBENT,NAME) RETURNS(PTR) OPTIONS(REENTRANT);
 /* CALLED TO FIND A PANEL */
   % DCL @PROCNAME CHAR;
   % @PROCNAME='';
   % INCLUDE DENTSUB;
   % INCLUDE DENTPFD;
 HALT;
   DCL DDNAME CHAR(8) STATIC INIT('SYSPNL');
   DCL (SETCAPS,SYSDCBP) ENTRY;
   DCL DENTPFO ENTRY(PTR) RETURNS(PTR);
   DCL NAME CHAR(8);
   DCL ERR FIXED BIN(15,0);
   DCL SEQCNT FIXED BIN(15,0) INIT(0);
   DCL (STRHD,STRTMP) PTR;
   STRHD,STRPTR=NULL;
   DO PNLELPT=RHPNLHDR REPEAT ELNXT WHILE(PNLELPT^=NULL);
     IF ELCNT>0
       THEN IF SUBSTR(ELARY(1),7,8)=NAME
         THEN RETURN(ADDR(ELARY(1)));
         ELSE;
       ELSE IF SUBSTR(ELLSTA,7,8)=NAME
         THEN RETURN(ADDR(ELLSTA));
     END;
   /* NOW GO LOOK ON DISK FOR PANEL */
   IF RHPNLDCB=NULL
     THEN CALL SYSDCBP(RHPNLDCB,DDNAME);
   CALL SUBENT(32)(RHPNLDCB,NAME,ALCEP,ERR,SEQCNT); /* CALL DENTPIB */
   IF ERR^=0
     THEN DO;
       IF ERR=4
         THEN PUT SKIP LIST('PANEL NAMED '××NAME××' NOT FOUND');
         ELSE PUT SKIP LIST('SYSPNL FILE INVALID OR MISSING');
       SIGNAL ERROR;
       END;
   /* NOW HAVE PANEL DEFINITION */
   PNLELPT=DENTPFO(STRHD);
   ELNXT=RHPNLHDR;
   RHPNLHDR=PNLELPT;
   IF ELCNT=0
     THEN DO;
       SUBSTR(ELLSTA,7,8)=NAME;
       RETURN(ADDR(ELLSTA));
       END;
     ELSE DO;
       SUBSTR(ELARY(1),7,8)=NAME;
       RETURN(ADDR(ELARY(1)));
       END;
 ALCEP:  PROC(INREC);
 /* IMPORTANT NOTE:  SYSPNL CANNOT HAVE LRECL > 256 */
   DCL INREC CHAR(256) VAR;
   IF STRPTR=NULL
     THEN DO;
       ALLOCATE STREL;
       STRHD=STRPTR;
       STRNXT=NULL;
       STR=INREC;
       END;
     ELSE IF LENGTH(STR)+LENGTH(INREC)<=32767
       THEN STR=STR××INREC;
       ELSE DO;
         ALLOCATE STREL SET(STRNXT);
         STRPTR=STRNXT;
         STRNXT=NULL;
         STR=INREC;
         END;
   END ALCEP;
   END DENTPFN;
