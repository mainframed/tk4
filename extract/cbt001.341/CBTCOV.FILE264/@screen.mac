 /* @SCREEN */
 %NOPRINT;
 /* @SCRNUM IS AN INCREMENTAL NUMBER TO GOVERN SCREEN/PANEL NAMES */
         % DCL @SCRNUM FIXED;
         % @SCRNUM = 0;
 /* @SFLDN IS AN INCREMENTAL NUMBER TO GOVEN FIELD NAMES */
         % DCL @SFLDN FIXED;
         % DCL @SCRNUL ENTRY;
 % @SCRNUL:  PROC(P1) RETURNS(CHAR);
 /*
0*$.HE GENERALIZED SUBROUTINES - @SCREEN
0*$.HE PLI PREPROCESSOR TEXT
0*$.HE SCREEN IMAGE PREPROCESSOR
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$06/13/77
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  @SCREEN
0*$DOCOMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THE PLI PREPROCESSOR PROCEDURE CAN BE INVOKED TO GENERATE THE
0*$@PANEL AND @FIELD MACROS DESCRIBING A SCREEN FORMAT.  THE SCREEN
0*$IMAGE CAN SUBSEQUENTLY BE WRITTEN TO THE SCREEN AND INPUT RECEIVED
0*$VIA THE @DOPNL MACRO.  THE FOLLOWING IS AN EXAMPLE OF USING THE
0*$@SCREEN MACRO:
0*$.SP 1
0*$.NF
0*$.IN 5
0*$ @SCREEN(
0*$ '@PHONE DIRECTORY
0*$ #
0*$ #@NAME:&(22)X                  @ADD/CHANGE/DELETE/?:&(6)A
0*$ #                   @ALT @EXT  @S/L/N   @LINE
0*$ # @TELEPHONE NUMBER:&9999&9999 @CODE:&A @CODE:&9 @TYPE:&9
0*$ #
0*$ # @DEPT:&XXXXX  @WS NO:&999  @D/L:&9
0*$ #                                         @MONTHLY
0*$ # @DIRECTORY LISTING:&(20)X               @COST:&9999.99
0*$ #
0*$ # @LOCATION:&99  @ROOM:&XXXX ');
0*$ @ENDPNL(LAST);
0*$.IN 0
0*$.FI L
0*$.SP 1
0*$THE FIRST PARAMETER TO THE @SCREEN MACRO IS A QUOTED STRING WHICH
0*$DEFINES THE FORMAT OF THE SCREEN IMAGE.  EACH LINE OF THE SCREEN
0*$IMAGE EXCEPT FOR THE FIRST SHOULD BE PRECEDED BY A # SIGN.
0*$THE POSITION FOR THE ATTRIBUTE BYTE FOR ANY PROTECTED FIELD SHOULD
0*$BE MARKED BY AND @ SIGN, AND FOR AN UNPROTECTED FIELD (ONE IN WHICH
0*$THE TERMINAL OPERATOR IS EXPECTED TO ENTER DATA) SHOULD BE MARKED
0*$BY AN & SIGN.  LITERALS MAY BE SPECIFIED AS SHOWN IN PROTECTED
0*$FIELDS.  A PICTURE SPECIFICATION SHOULD BE ENTERED IN EACH
0*$UNPROTECTED FIELD DESCRIBING THE FORMAT OF THE DATA TO BE ENTERED.
0*$VALID PICTURE CHARACTERS ARE:
0*$.SP 1
0*$.IN 5
0*$9 . + - X A H
0*$.IN 0
0*$.SP 1
0*$IF A FIELD IS TO BE NUMERIC, ONLY THE '9', '.', '+', AND '-'
0*$MAY APPEAR IN ITS PICTURE.  THE 'X' PICTURE CHARACTER INDICATES
0*$ALPHAMERIC, THE "A" ALPHABETIC,
0*$AND THE "H" CHARACTER HEXADECIMAL DATA.  THE ONLY OTHER VALID
0*$CHARACTERS ARE THE PARENTHESIZED REPITITION SPECIFICATIONS AS SHOWN
0*$IN THE EXAMPLE.
0*$.SP 1
0*$NOTE:  THE STATEMENT "% INCLUDE @FULLSCR;" SHOULD BE PLACED AT
0*$THE FRONT OF YOUR PROGRAM, UNLESS YOU ARE ALSO USING THE PLIST
0*$FACILITY (% INCLUDE CPINIT), IN WHICH CASE YOU SHOULD USE
0*$"% INLCUEE @FULLSCP;" INSTEAD.
 */
         /* THIS PROCEDURE TAKES A NUMBER, CONVERTS IT TO CHAR(3000) VAR
            AND STRIPS BLANKS */
         DCL (P1,I,J) FIXED;
         DCL STR CHAR;
         STR = P1;
         DO I = 1 TO 8;
              IF SUBSTR(STR,I,1) ^= ' '
                   THEN RETURN(SUBSTR(STR,I,9-I));
              END;
         % END @SCRNUL;
         % DCL @SCREEN ENTRY;
 % @SCREEN:  PROC(P1) RETURNS(CHAR);
         DCL P1 CHAR;
         DCL (I,J,K,L,M,ROW,COL) FIXED;
         DCL INDXTAB CHAR;
         INDXTAB = '#@&';
         DCL (BLNKS,LITPIC,RET) CHAR;
         BLNKS = '          ';
         DO I = 1 TO 4;
              BLNKS=BLNKS××BLNKS;
              END;
         P1 = @STRIP(P1);
         P1 = SUBSTR(P1,2,LENGTH(P1)-2);
            /* GET RID OF QUOTES */
         L = LENGTH(P1);
         @SCRNUM = @SCRNUM + 1;
         RET='@PANEL(SCREEN'××@SCRNUL(@SCRNUM)××');';
         @SFLDN = 0;
         I = 0;
 SETJ:
      /* DO J = I+1 TO L; */
         J = I+1;
         GO TO BYINCR;
 INCR:
         J = J + 1;
 BYINCR:
         IF J > L
           THEN GOTO ENDLUP;
           DO K = 1 TO 3;
             IF SUBSTR(P1,J,1)=SUBSTR(INDXTAB,K,1)
               THEN IF SUBSTR(P1,J,1) ^= '#'
                 THEN GO TO GOTNXT;
                 ELSE DO; /* IT IS NEW LINE MARK */
                   M = (J-1)/@COLS;
                   M = M*@COLS + @COLS + 1;
                   P1=SUBSTR(P1,1,J-1)××SUBSTR(BLNKS,1,
                     M-J)××SUBSTR(P1,J+1);
                   J = J + (M-J) -1;
                   L = LENGTH(P1);
                   END;
             END;
       /*  END; */
           GO TO INCR;
 ENDLUP:
 GOTNXT:
         IF I = 0
              THEN DO;
                   I = J;
                   GO TO SETJ;
                   END;
         /* NOW I = START OF NEXT FIELD TO BE PROCESSED AND
         J = END OF FIELD + 1 */
         @SFLDN = @SFLDN + 1;
         ROW = ((I-1)/@COLS)+1;
         COL = I - ((ROW-1)*@COLS);
         IF SUBSTR(P1,I,1) = '@'
              THEN LITPIC='LENGTH('××@SCRNUL(J-I-1)××'),LITERAL(';
              ELSE LITPIC = 'PICTURE(';
         DO M = J-1 TO I BY(-1);
              IF SUBSTR(P1,M,1) ^= ' '
                   THEN GO TO GOTEND;
              END;
 GOTEND:
         RET=RET××' @FIELD(NAME(FIELD'××@SCRNUL(@SFLDN)××
           '),POSITION('××@SCRNUL(ROW)××','××@SCRNUL(COL)××'),'××
           LITPIC××SUBSTR(P1,I+1,M-I)××'));';
         I = J;
         IF I < L
              THEN GO TO SETJ;
         RETURN(RET××' ');
         % END @SCREEN;
 %PRINT;
