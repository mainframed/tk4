 /* JOBCK */
 % DCL GEN CHAR;
 % GEN = 'SUB';
   /* SETTING GEN TO 'SUB' WILL CAUSE THE SUBROUTINE TO BE GENERATED */
 /*
0*#.HE GENERALIZED SUBROUTINES - JOBCK
0*#.PL 55
0*#.PN 0
0*#.PA
0*#.SS
0*#.NF
0*#07/24/80
0*#AUTHOR:  T. SMITH
0*#SOURCE MODULE NAME:  JOBCK
0*#DOCUMENTATION ID:  0*#
0*#.FI L
0*#.SP 5
0*#THIS SUBROUTINE WILL RETURN THE JOB STEP CPU TIME ACCUMULATED
0*#IN THE CURRENT JOB STEP OF THE SPECIFIED JOB.  IF THE JOB IS
0*#NOT CURRENTLY RUNNING, JOBCK WILL RETURN -1.  CALL FORMAT IS
0*#AS FOLLOWS:
0*#.SP 1
0*#.NF
0*#DCL JOBCK ENTRY(CHAR(8))
0*#  RETURNS(FIXED BIN(31,0));
0*#I = JOBCK('AU109545');
0*#.FI L
0*#.SP 1
0*#VARIABLE I WILL BE SET TO THE CPU TIME IN .01 SECONDS IF THE
0*#JOB AU109545 IS CURRENTLY RUNNING.
 */
 /*
0*$.HE GENERALIZED UTILITIES - JOBCK
0*$.HE RETURN CODE=4 IF JOB NOT RUNNING
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$07/24/80
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  JOBCK
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS UTILITY WILL SET THE STEP RETURN CODE TO 4 IF THE
0*$SPECIFIED JOBNAME IS NOT RUNNING.  JCL IS AS FOLLOWS:
0*$.SP 1
0*$.NF
0*$//JOBCK EXEC PGM=JOBCK,PARM='/RMF'
0*$//STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR
0*$//ABEND EXEC PGM=NONAME,COND=(0,EQ,JOBCK)
0*$.FI L
0*$.SP 1
0*$THE ABOVE JCL WILL CAUSE AN ABEND IF A JOB OR STARTED TASK
0*$DOES NOT EXIST WITH THE NAME "RMF".
 */
 %IF GEN='MAIN' %THEN %DO;
 JOBCK:  PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
 %END; %ELSE %DO;
 JOBCK:  PROC(PARM) RETURNS(FIXED BIN(31,0));
         DCL PARM CHAR(8);
 %END;
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         DCL 1 CVTPTRPTR PTR,
               CVTADJ FIXED BIN(31,0) DEF CVTPTRPTR;
         CVTADJ = 76;
         DCL CVTPTR BASED(CVTPTRPTR) PTR;
         DCL 1 CVT UNALIGNED BASED(CVTPTR),
           2 PAD1 CHAR(556),
           2 CVTASVT PTR;
         DCL 1 ASVT UNALIGNED BASED(CVTASVT),
           2 PAD CHAR(512),
           2 ASVTASVT CHAR(4),
           2 ASVTMAXU FIXED BIN(31,0),
           2 ASVTRS00 CHAR(4),
           2 ASVTARY(1000),
            3 ASVTAVAI BIT(1),
            3 ASVTARYP BIT(31);
         DCL 1 ASVT2 UNALIGNED BASED(CVTASVT),
           2 PAD CHAR(524),
           2 ASVTENTY(1000) PTR;
         DCL 1 ASCB UNALIGNED BASED(ASCBPTR),
           2 ASCBASCB CHAR(4),
           2 ASCBPAD1 CHAR(60),
           2 ASCBEJST BIT(64),
           2 PAD1 CHAR(100),
           2 ASCBJBNI PTR,
           2 ASCBJBNS PTR;
         DCL JOBNAME CHAR(8) BASED(JBNPTR);
         DCL ZERO PTR,
             ZEROADJ FIXED BIN(31,0) DEF ZERO;
         DCL BINFLOT BIN FLOAT(53);
         ZEROADJ = 0;
         RETCODE=4;
         DO I = 1 TO ASVTMAXU UNTIL(RETCODE=0);
           IF ^ASVTAVAI(I)
             THEN DO;
               ASCBPTR=ASVTENTY(I);
               IF ASCBJBNI^=ZERO
                 THEN IF ASCBJBNI->JOBNAME=PARM
                   THEN RETCODE=0;
               IF ASCBJBNS^=ZERO
                 THEN IF ASCBJBNS->JOBNAME=PARM
                   THEN RETCODE=0;
               END;
           END;
 %IF GEN='MAIN' %THEN %DO;
         CALL PLIRETC(RETCODE);
 %END; %ELSE %DO;
 IF RETCODE=4
   THEN RETURN(-1);
   ELSE DO;
     BINFLOT=SUBSTR(ASCBEJST,1,52); /* SRDL R2,12 */
     BINFLOT=(BINFLOT/10000.0) + .5;
     RETURN(BINFLOT); /* BINFLOT = CPU TIME, ROUNDED TO .01 SEC */
     END;
 %END;
         END;
