 /* COPYRIGHT 1985 BY THOMAS GLEN SMITH */
 /**********************************************************************
 *                                                                     *
 * ALIAS #MISSING                                                      *
 * CALLED TO CHECK FOR A MISSING VALUE.                                *
 *                                                                     *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABEMICK) PARMS(STR,R) RETURNS(BIT(1));
   %INCLUDE ABESUBA;
   %INCLUDE ABEREC;
   %INCLUDE ABEMISS;

   DCL STR CHAR(*) VAR;
   DCL R PTR;

   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*),CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL CK CHAR(256) VAR;
   DCL WRKPTR PTR;
   DCL INTEGER FIXED BIN(31,0) BASED(WRKPTR),
       SMALLINT FIXED BIN(15,0) BASED(WRKPTR);
   DCL I FIXED BIN;

   IF LENGTH(STR)=0
     THEN RETURN('1'B); /* MISSING VALUE */
   IF R=NULL
     THEN I=2; /* ASSUME CHAR */
     ELSE I=R->RHNTYPE;
   SELECT(I);
     WHEN(2,3,8) DO; /* CHAR,ZONED,BEST */
       IF STR=''
         THEN RETURN('1'B); /* MISSING VALUE */
       CK=SUBSTR(STR,PREFIX(STR,' ')); /* ELIMINATE LEADING BLANKS */
       CK=SUBSTR(STR,1,SUFFIX(STR,' ')); /* ELIMINATE TRAILING BLANKS */
       IF CK='.'
         THEN RETURN('1'B); /* MISSING VALUE */
         ELSE RETURN('0'B); /* NOT MISSING VALUE */
       END;
     OTHERWISE DO;
       WRKPTR=PTROUT(PTRIN(ADDR(STR))+2);
       SELECT(R->RHNTYPE);
         WHEN(1) /* FLOAT */
           IF STR=MISSINGCH
             THEN RETURN('1'B);
             ELSE RETURN('0'B);
         WHEN(4) /* PACKED */
           IF STR=SUBSTR(MISSING_PACKED_CH,8-LENGTH(STR)+1)
             THEN RETURN('1'B);
             ELSE RETURN('0'B);
         WHEN(6) /* BIT */
           RETURN('0'B); /* BIT IS NEVER MISSING */
         WHEN(7) /* FIXED */
           IF LENGTH(STR)=4
             THEN IF STR=MISSING_INTEGER_CH
               THEN RETURN('1'B);
               ELSE RETURN('0'B);
             ELSE IF STR=MISSING_SMALLINT_CH
               THEN RETURN('1'B);
               ELSE RETURN('0'B);
         OTHERWISE RETURN('1'B); /* UNSUPPORTED DATA TYPE */
         END; /* SELECT */
       END; /* OTHERWISE */
     END; /* SELECT */
   END ABEMICK;
