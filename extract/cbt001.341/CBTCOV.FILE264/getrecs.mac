GETREC   START 0
********************************************************************
*                  UPON ENTRY, THIS RTNE EXPECTS REG1 TO CONTAIN   *
*              THE ADDR OF TWO FULLWORDS, THE FIRST CONTAINING THE *
*              ADDR OF A WORD CONTAINING THE ADDR OF A CONTROL     *TGS
*              TABLE DEFINED BY THE MACRO                          *TGS
*              INTREGCT.  THE SECOND TO CONTAIN THE ADDR OF A      *
*              4 BYTE AREA TO BE FILLED IN WITH THE USER DATA      *
*              EXTRACTED FROM THE LIST ENTRY THIS RTNE WILL        *
*              DEQUEUE FROM THE CONTROL TABLE.                     *
*              SP 255 STORAGE USED TO STORE THE RECORD, AND        *
*                  ONCE THE USER DATA HAS BEEN MOVED TO THE USERS  *
*              PARM FIELD, A CHECK IS MADE TO SEE IF THE LIST      *
*              ELEMENT IS THAT ONE WHICH INTEGRATED INTO THE       *
*              CONTROL TABLE, AND IF IT IS NOT, A FREEMAIN IS      *
*              ISSUED FOR THE ELEMENT.                             *
********************************************************************
********************************************************************
         TGSREQU
         USING *,RF
         STM   RE,RC,12(RD)            SAVE CALLERS REGS
         LR    R0,R1                   R0 = ADDR CONTROL TABLE
         LA    R1,SVC242
         SVC   242
         LM    RE,RC,12(RD)            RESTORE CALLERS REGS
*                                           REG15 WILL HAVE BEEN SET
         BR    RE                      RETURN TO CALLER
         DROP  RF
SVC242   EQU   *
         USING *,RC
         LR    RC,R1                   SET BASE REG
         SSM   SSM                     ENABLE FOR INTERRUPTS
         ST    RE,96(R5)               SAVE RET ADDR IN SVRB EXT
         LR    R1,R0
         LM    RA,RB,0(R1)             RA = ADDR CONTROL TABLE
         L     RA,0(RA)                RA = ADDR CONTROL TABLE
*                                      RB = ADDR FOR USER DATA
         USING REGCT,RA
GETPOINT EQU   *
         L     R2,INTRRFPT             GET ADDR NXT RECORD
         LTR   R2,R2                   IS IT ZERO
         BC    7,GOTREC                BR IF NOT
         LA    R1,INTWTECB
         WAIT  ECB=(1)
         B     GETPOINT
GOTREC   EQU   *
         SSM   =X'00'                  DISABLE FOR INTERRUPTS
         L     R3,0(R2)                R3 = ADDR NXT LIST ELEM
         ST    R3,INTRRFPT             SET FIRST PTR
         LTR   R3,R3                   IS IT END OF CHAIN
         BC    7,NOTEND                BR IF NOT
         ST    R3,INTRRLPT             SET LPT TO ZERO ALSO
NOTEND   EQU   *
         MVC   0(4,RB),4(R2)           MOVE USER DATA FROM LIST ELEM
         SSM   =X'FF'                  ENABLE FOR INTERRUPTS
         LA    R6,INTCTLNK
         CR    R2,R6                   IS THIS INTEGRAL ELEMENT
         BC    8,NOFREE                BR IF YES
         LA    R0,8                    R0 = LEN TO FREE
         LA    R1,255
         SLL   R1,24
         OR    R0,R1                   OR IN SP #
         LR    R1,R2                   R1 = ADDR TO FREE
         FREEMAIN R,LV=(0),A=(1)
NOFREE   EQU   *
         L     RE,96(R5)
         BR    RE
SSM      DC    X'FF'
         LTORG
REGCT    DSECT
         INTREGCT
         END
