         MACRO
&NAME PLIPRINT &CHAR,&LGTH,&COMPILE=,&PRNTCTL=00
         GBLC  &COMP
         GBLA  &PLICNT,&PLICNTA,&PLICNTB,&PLICNTC
         LCLA  &ARITH,&A,&W
         LCLB  &RSW,&LSW,&PSW
.* &PSW IS SET TO 1 IF PL1PARMW NOT USED
.*     TO CREAT INITIAL SDV
         LCLC  &QUOTE,&REGWRK
&PLICNT  SETA  &PLICNT+1
         PLICOMP &COMPILE
         AIF   ('&NAME' EQ '').NONAM
&NAME    EQU   *
.NONAM   ANOP
&QUOTE   SETC  '&CHAR'(1,1)
         AIF   ('&LGTH' EQ '').STR
         AIF   ('&QUOTE' NE '''').OK1
         MNOTE 12,' ** SPECIFYING A SECOND OPERAND FOLLOWING A '
         MNOTE 12,' ** MESSAGE IN QUOTES IS MEANINGLESS. '
         MEXIT
.OK1     ANOP
.* MUST BE PLIPRINT SYMBOL,&LGTH OR PLIPRINT (X),&LGTH
         AIF   ('&QUOTE' EQ '(').REGA
         AIF   ('&COMP' EQ 'O').OPT1
         LA    1,&CHAR
.CK2     ANOP
         AIF   ('&LGTH'(1,1) EQ '(').REGB
         AIF   ('&COMP' EQ 'O').OPT2
         LA    2,&LGTH
.FEND    ANOP
         L     15,=V(IHEPRTA)
         BALR  14,15
         MEXIT
.REGA    ANOP
         AIF   ('&COMP' EQ 'O').OPT3
         LA    1,0&CHAR
         AGO   .CK2
.REGB    ANOP
         AIF   ('&COMP' EQ 'O').OPT4
         LA    2,0&LGTH
         AGO   .FEND
.STR     ANOP
         AIF   ('&QUOTE' EQ '(').REGC
.* MUST BE PLIPRINT 'MESSAGE' OR PLIPRINT SYMBOL
&PSW     SETB  1
&LSW     SETB  1
         AGO   .CKT
.LRET    ANOP
         AIF   ('&QUOTE' EQ '''').OK2
.* MUST BE PLIPRINT SYMBOL
         AIF   ('&COMP' EQ 'O').OPT5
         L     1,&CHAR
         LA    2,&CHAR+6
         AGO   .FEND
.REGC    ANOP
         AIF   ('&COMP' EQ 'O').OPT6
         L     1,0&CHAR
         LA    2,6&CHAR
         AGO   .FEND
.OK2     ANOP
.* MUST BE PLIPRINT 'MESSAGE'
&ARITH   SETA  K'&CHAR-2
         AIF   ('&COMP' EQ 'O').OPT7
&A       SETA  ((&ARITH+3)/2)*2+4
         BAL   2,*+&A
         DC    H'&ARITH'
         DC    CL&ARITH&CHAR
         LA    1,2(2)
         AGO   .FEND
.OPT1    ANOP
.* MUST BE PLIPRINT SYMBOL,&LGTH
         LA    R1,&CHAR
         ST    R1,PL1PARMW
         AGO   .CK2
.OPT2    ANOP
.* MUST BE PLIPRINT &CHAR,SYMBOL
         LH    R1,&LGTH
.OPT2A   ANOP
         SLL   R1,16
         ST    R1,PL1PARMW+4
         AGO   .ENDO
.OPT3    ANOP
.* MUST BE PLIPRINT (X),&LGTH
&W       SETA  K'&CHAR-2
&REGWRK  SETC  '&CHAR'(2,&W)
         ST    &REGWRK,PL1PARMW
         AGO   .CK2
.OPT4    ANOP
.* MUST BE PLIPRINT &CHAR,(X)
         LH    R1,0&LGTH
         AGO   .OPT2A
.OPT5    ANOP
.* MUST BE PLIPRINT SYMBOL
         LA    R1,&CHAR
         AGO   .ENDO
.OPT6    ANOP
.* MUST BE PLIPRINT (X)
&PSW     SETB  1
&RSW     SETB  1
&W       SETA  K'&CHAR-2
&REGWRK  SETC  '&CHAR'(2,&W)
         ST    &REGWRK,PL1PARMW
         AGO   .ENDO
.OPT7    ANOP
.* MUST BE PLIPRINT 'MESSAGE'
&A       SETA  ((&ARITH+1)/2)*2+12
         CNOP  0,4
         BAL   R1,*+&A
         DC    A(*+8)
         DC    H'&ARITH',H'0'
         DC    CL&ARITH&CHAR
.ENDO    ANOP
         BAL   2,*+6
         DC    X'&PRNTCTL.00'
         AIF   (&LSW).ENDOB
.CKT     ANOP
         L     R1,64(RC)
         L     RF,4(R1)
         LTR   RF,RF
         BC    7,PLIPRT&PLICNT
         LINK  EP=IBMBEDOA
PLIPRT&PLICNT EQU *
         AIF   (&LSW).LRET
         AIF   (NOT &RSW).ENDOA
         L     R1,PL1PARMW
         AGO   .ENDOB
.ENDOA   ANOP
         LA    R1,PL1PARMW
.ENDOB   ANOP
         L     RE,64(RC)               RE = A(DFB)
         TM    0(RE),X'A0'
&PLICNT  SETA  &PLICNT+1
&PLICNTA SETA  &PLICNT
         BC    7,PLIPRT&PLICNTA        BR IF BAD SYSPRINT
         L     RE,8(RE)                RE = A(DCLCB)
         L     RE,0(RE)                RE = PRV OFFSET FOR FCB
         A     RE,4(RC)                RE = A(A(FCB))
         L     RE,0(RE)                RE = A(FCB)
         L     RE,20(RE)               RE = A(DCB)
         LH    R0,82(RE)               R0 = LRECL
         LA    RE,5
         SR    R0,RE LESS 4 FOR RDW AND 1 FOR CARCTL
         AIF   (NOT &PSW).NOVCK
         TM    6(R1),X'80'             IS IT VARYING
&PLICNT  SETA  &PLICNT+1
&PLICNTB SETA  &PLICNT
         BC    1,PLIPRT&PLICNTB        BR IF YES
.NOVCK   ANOP
         CH    R0,4(R1)
&PLICNT  SETA  &PLICNT+1
&PLICNTC SETA  &PLICNT
         BC    10,PLIPRT&PLICNTC       BR IF MSG OK
         AIF   (NOT &PSW).USP
         MVC   PL1PARMW(8),0(R1)
         LA    R1,PL1PARMW
.USP     ANOP
         STH   R0,4(R1)                TRUNCATE
         B     PLIPRT&PLICNTC
         AIF   (NOT &PSW).NOVCK2
PLIPRT&PLICNTB EQU *
         L     RE,0(R1)                RE = A(MSG LEN)
         CH    R0,0(RE)
         BC    10,PLIPRT&PLICNTC       BR IF MSG OK
         LA    RE,2(RE)
         ST    RE,PL1PARMW
         SLL   R0,16
         ST    R0,PL1PARMW+4
         LA    R1,PL1PARMW
.NOVCK2  ANOP
PLIPRT&PLICNTC EQU *
         BALR  RE,RF
PLIPRT&PLICNTA EQU *
         MEND
