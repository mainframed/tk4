*$.HE GENERALIZED UTILITIES - TAPECOPY
*$.HE TAPECOPY - TAPE COPY UTILITY
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/6/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  TAPECOPY
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$TAPECOPY - UTILITY TO COPY A STANDARD LABEL INPUT TAPE, DDNAME IN,
*$IN ITS ENTIRETY TO A STANDARD LABEL OUTPUT TAPE, DDNAME OUT. ALL
*$DATA SETS ON A MULTIPLE DATA SET TAPE ARE COPIED.  THE VOLUME LABEL
*$IS NOT COPIED. NOTE THAT IF PARM=COPYLAB IS SPECIFIED ON THE EXECUTE
*$STATEMENT, THE VOLUME LABEL WILL BE COPIED FROM THE INPUT TAPE, AND
*$IF THE OUTPUT TAPE IS STANDARD LABEL, ITS VOLUME LABEL WILL BE
*$OVERLAYED.  IF PARM=COPYLAB IS SPECIFIED, THE OUTPUT VOLUME DOES
*$NOT HAVE TO BE A STANDARD LABEL TAPE.  IT PARM=COPYLAB IS NOT
*$SPECIFIED, THE OUTPUT TAPE MUST BE STANDARD LABEL AND THE PROGRAM
*$WILL ABEND IF IT IS NOT.
TAPECOPY CSECT
         SAVES RETCTL
         TGSREQU
         L     R1,0(R1)                R1 = A(PARMLIST)
         LH    R2,0(R1)                R2 = LEN(PARM)
         CH    R2,=H'7'                WAS COPYLAB PARM SUPPLIED?
         BC    7,NOPARM                BR IF NOT
         CLC   2(7,R1),=C'COPYLAB'     WAS COPYLAB PARM SUPPLIED?
         BC    7,NOPARM                BR IF NOT
         OI    COPYLAB,X'80'
NOPARM   DS    0H
         LR    R7,RC
         LA    R8,TDAREA+512
*        SR    R9,R9
*        OPEN  (SNAPDCB,(OUTPUT))
         RDJFCB INDCB
         RDJFCB OUTDCB
         OPEN  (INDCB,(INPUT),OUTDCB,(OUTPUT))
         LA    R1,1                    SET ABEND CODE
         TM    INDCB+48,X'10'          IS IT GOOD OPEN?
         BC    8,ABEND                 BR IF NOT
         TM    OUTDCB+48,X'10'
         BC    1,OPNOK
ABEND    DS    0H
         ABEND (R1),DUMP
OPNOK    DS    0H
         MVI   SETMODE,X'03'
         MVC   OUTCCW(8),SETMODE       SET OUT SETMODE CCW
         MVC   OUTCCW+8(8),RWCCW       SET UP TO REWIND OUTPUT
         OI    OUTCCW+12,X'60'         TURN ON CMD CH.
         MVC   OUTCCW+16(8),NOP        CHAIN A NOP
         BAL   RB,IOOUT
         LA    R1,9
         CLI   OUTECB,X'7F'
         BC    7,ABEND
         MVC   INCCW(8),SETMODE        SET IN SETMODE CCW
         MVC   INCCW+8(8),RWCCW        SET UP TO REWIND INPUT
         OI    INCCW+12,X'60'          TURN ON CMD CH.
         MVC   INCCW+16(8),NOP         CHAIN A NOP
         BAL   RB,IOIN
         LA    R1,10
         CLI   INECB,X'7F'
         BC    7,ABEND
         MVC   INCCW+8(8),TDINCCW      SET READ CCW
         TM    COPYLAB,X'80'           IS VOLUME LABEL TO BE COPIED?
         BC    1,COPY1                 BR IF YES
         MVC   OUTCCW+8(8),TDINCCW     SET UP TO READ VOL1 & HDR1
         BAL   RB,IOOUT                GO READ VOLUME LABEL
         LA    R1,2                    SET ABEND CODE
         CLI   OUTECB,X'7F'            READ SUCCESSFUL?
         BC    7,ABEND                 BR IF NOT
         CLC   TDAREA(4),=C'VOL1'      IS IT VOLUME LABLE?
         BC    7,ABEND                 BR IF NOT
*        CLC   TDAREA+4(6),OUTJFCB+118 IS IT PROPER OUTPUT VOLUME?
*        BC    7,ABEND                 BR IF NOT
         BAL   RB,IOOUT                GO READ HDR1
         LA    R1,3                    SET ABEND CODE
         CLI   OUTECB,X'7F'            READ OK?
         BC    7,ABEND                 BR IF NOT
         MVC   OUTCCW+8(8),BSCCW       SET UP BS CCW
         MVC   OUTCCW+16(8),NOP        CHAIN TO NOP
         BAL   RB,IOOUT                GO BACKSPACE OUTPUT TAPE
         LA    R1,4                    SET ABEND CODE
         CLI   OUTECB,X'7F'            BACKSPACE OK?
         BC    7,ABEND                 BR IF NOT
         MVC   INCCW+8(8),TDINCCW      SET READ CCW
         BAL   RB,IOIN                 GO READ VOL1 FROM INPUT TAPE
         LA    R1,5                    SET ABEND CODE
         CLI   INECB,X'7F'             READ OK?
         BC    7,ABEND                 BR IF NOT
         CLC   TDAREA(4),=C'VOL1'      IS IT VOLUME LABEL?
         BC    7,ABEND                 BR IF NOT
         CLC   TDAREA+4(6),INJFCB+118  IS IT PROPER INPUT VOLUME
         BC    7,ABEND                 BR IF NOT
COPY1    DS    0H
TTINIT   MVI   INSTAT+4,0
TTLOOP   DS    0H
         BAL   RB,IOIN                 GO READ A TAPE BLOCK?
         CLI   INECB,X'7F'             READ OK?
         BC    8,TT1                   BR IF YES
         LA    R1,6                    SET ABEND CODE
         TM    INSTAT+4,X'01'          UNIT EXCEPTION?
         BC    8,ABEND                 BR IF NOT - TAPE ERROR
* READING A TAPE MARK SETS UNIT EXCEPTION
         MVC   OUTCCW+8(8),WTCCW       SET UP WTM CCW
         OI    OUTCCW+12,X'60'         CHAIN IT TO A NOP
         MVC   OUTCCW+16(8),NOP        PUT NOP CCW INTO CP
         BAL   RB,IOOUT                GO WRITE A TAPE MARK
         LA    R1,7                    SET ABEND CODE
         CLI   OUTECB,X'7F'            WRITE SUCCESSFUL?
         BC    7,ABEND                 BR IF NOT
         CLI   EOVCK,X'03'             IS IT TM/EOV1/EOV2/TM?
         BC    8,DORWND                BR IF YES
         MVI   EOVCK,X'01'             INDICATE TM SEQUENCE
         TM    VVSTAT,X'01'            DOUBLE TAPEMARK?
         BZ    TTLOOP                  BR IF NOT
         CLI   NULLCK,X'02'            IS IT HDR1/HDR2/TM/TM SEQUENCE?
         MVI   NULLCK,X'00'
         BC    8,TTLOOP                BR IF YES
DORWND   DS    0H
         CLOSE (INDCB,REWIND)
         CLOSE (OUTDCB,REWIND)
         SR    RF,RF                   SET RC = 0
         B     RETCTL                  ALL DONE
TT1      DS    0H
         LH    R2,INSTAT+6             R2 = RESIDUAL BYTE COUMT
         LH    R3,TDINCCW+6            R3 = ORIGINAL BYTE COUNT
         SR    R3,R2                   R3 = BYTES READ
         STH   R3,TTCCWOUT+6           SET COUNT IN WRITE CCW
         CH    R3,=H'80'               IS LEN=80?
         BC    7,NOTHDR2               BR IF NOT
         CLC   TDAREA(4),=C'HDR1'      IS IT HDR1?
         BC    7,NOTHDR1               BR IF NOT
         MVI   NULLCK,X'01'
         MVI   EOVCK,X'00'
         B     BYCKR
NOTHDR1  DS    0H
         CLC   TDAREA(1),=C'HDR2'      IS IT HDR2?
         BC    7,NOTHDR2               BR IF NOT
         CLI   NULLCK,X'01'            WAS PREVIOUS HDR1?
         BC    7,NOTHDR2               BR IF NOT
         MVI   NULLCK,X'02'            IND HDR1/HDR2 SEQUENCE
         B     BYCKR
NOTHDR2  DS    0H
         MVI   NULLCK,X'00'
         CLI   EOVCK,X'00'             IS IT POSSIBLE EOV SEQUENCE?
         BC    8,BYCKR                 BR IF NOT
         CH    R3,=H'80'               IS LEN=80?
         BC    7,CKRESET               BR IF NOT
         CLI   EOVCK,X'01'             WAS TM LAST?
         BC    7,CK2                   BR IF NOT
         CLC   TDAREA(4),=C'EOV1'      IS IT EOV1?
         BC    7,CKRESET               BR IF NOT
         MVI   EOVCK,X'02'             INDICATE TM/EOV1 SEQUENCE
         B     BYCKR
CK2      DS    0H
         CLI   EOVCK,X'02'             WAS IT TM/EOV1 SEQ?
         BC    7,CKRESET               BR IF NOT
         CLC   TDAREA(4),=C'EOV2'      IS IT EOV2?
         BC    7,CKRESET               BR IF NOT
         MVI   EOVCK,X'03'             INDICATE TM/EOV1/EOV2 SEQ
         B     BYCKR
CKRESET  DS    0H
         MVI   EOVCK,X'00'
BYCKR    DS    0H
         MVC   OUTCCW+8(8),TTCCWOUT    MOVE WRITE CCW
         BAL   RB,IOOUT
         CLI   OUTECB,X'7F'            WRITE OK?
         BC    8,TTLOOP                BR IF YES
         LA    R1,8                    SET ABEND CODE
         B     ABEND
IOOUT    DS    0H
         SR    R0,R0
         ST    R0,OUTECB               CLEAR ECB
         NI    OUTDCB+44,X'3F'         RESET IO FLAGS
         EXCP  OUTIOB                  DO I/O
         WAIT  ECB=OUTECB              WAIT FOR I/O
*        LA    R9,1(R9)
*        SNAP  DCB=SNAPDCB,PDATA=(PSW,REGS),STORAGE=((R7),(R8))
         BR    RB
IOIN     DS    0H
         MVC   VVSTAT,INSTAT+4         SAVE LAST IOB STAT
         SR    R0,R0
         ST    R0,INECB                CLEAR ECB
         NI    INDCB+44,X'3F'          RESET IO FLAGS
         EXCP  INIOB                   DO I/O
         WAIT  ECB=INECB               WAIT FOR I/O
*        LA    R9,1(R9)
*        SNAP  DCB=SNAPDCB,PDATA=(PSW,REGS),STORAGE=((R7),(R8))
         BR    RB
COPYLAB  DC    X'00'
EOVCK    DC    X'00'
NULLCK   DC    X'00'
*SNAPDCB DCB   DDNAME=SNAPDD,DSORG=PS,RECFM=VBA,MACRF=(W),LRECL=125,  C
*              BLKSIZE=882
*
* DCB FOR TAPEIN
*
INDCB    DCB   DDNAME=IN,MACRF=(E),DSORG=PS,DEVD=TA,                   C
               EXLST=INEX
INEX     DS    0F
         DC    X'87',AL3(INJFCB)
INJFCB   DS    CL180
*
* ECB FOR TAPEIN
*
INECB    DC    F'0'
*
* IOB FOR INPUT DATA SET
*
INIOB    DS    0F
         DC    X'4200'
INSENS   DC    H'0'
         DC    X'7F'
         DC    AL3(INECB)
INSTAT   DC    2F'0'
         DC    A(INCCW)
         DC    A(INDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
INDASD   DC    X'00'
INSEEK   DC    XL7'00'
*
* CCWS FOR INPUT DATA SET
*
INCCW    DS    5D
*
* DCB FOR OUTPUT DATA SET
*
OUTDCB   DCB   DDNAME=OUT,MACRF=(E),DSORG=PS,DEVD=TA,EXLST=OUTEX
OUTEX    DS    0F
         DC    X'87',AL3(OUTJFCB)
OUTJFCB  DS    CL180
*
* ECB FOR OUTPUT DATA SET
*
OUTECB   DC    F'0'
*
* IOB FOR OUTPUT DATA SET
*
OUTIOB   DS    0F
         DC    XL2'4200'
OUTSENS  DC    H'0'
         DC    X'7F'
         DC    AL3(OUTECB)
OUTSTAT  DC    2F'0'
         DC    A(OUTCCW)
         DC    A(OUTDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
OUTDASD  DC    X'00'
OUTSEEK  DC    XL7'00'
*
* CCW'S FOR OUTPUT DATA SET
*
OUTCCW   DS    5D
VVSTAT   DC    X'00'
NOP      CCW   3,0,X'20',1             NOP CCW
SETMODE  CCW   *-*,0,X'60',1           SKELETON SETMODE CCW
TDINCCW  CCW   2,TDAREA,X'20',32767    READ CCW
BSCCW    CCW   X'27',BSCCW,X'60',1     BACKSPACE BLOCK CCW
WTCCW    CCW   31,TDAREA,X'20',1       WRITE TAPE MARK
TTCCWOUT CCW   1,TDAREA,X'20',32767    WRITE DATA
RWCCW    CCW   7,RWCCW,X'20',1
         LTORG
TDAREA   DS    80CL250
         DS    40CL250
         DS    28CL100 32800 BYTES
         END
