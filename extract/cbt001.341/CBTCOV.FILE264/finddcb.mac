 /* FINDDCB */
 /*
0*$.HE GENERALIZED SUBROUTINES - FINDDCB
0*$.HE FIND AN OPEN DCB
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$08/14/78
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  FINDDCB
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE USED TO FIND THE ADDRESS OF AN OPENED
0*$DCB.  NOTE THAT THE FILE MUST BE OPEN WHEN FINDDCB IS CALLED.
0*$CALL FORMAT IS AS FOLLOWS:
0*$.NF
0*$.IN 10
0*$.SP 1
0*$DCL DCBPTR PTR;
0*$DCL FINDDCB ENTRY(CHAR(8))
0*$  RETURNS(PTR);
0*$OPEN FILE(IN);
0*$DCBPTR = FINDDCB('IN');
0*$  ** PASS THE DDNAME TO FINDDCB **
 */
 FINDDCB:  PROC(DDNAME) RETURNS(PTR) OPTIONS(REENTRANT);
         DCL DDNAME CHAR(8);
         DCL NULL BUILTIN;
         % INCLUDE DEB;
         % INCLUDE DCB;
         DCL OFFSET FIXED BIN(15,0),
             OFFSETB BIT(16) DEF OFFSET;
         DCL TIKPTR PTR,
             TIKADJ FIXED BIN(31,0) DEF TIKPTR;
         DCL TIKSTR CHAR(8) BASED(TIKPTR);
         DCL 1 TCB UNALIGNED BASED(TCBPTR),
              2 PAD CHAR(8),
              2 TCBDEB FIXED BIN(31,0),
              2 TCBTIO PTR;
         DCL CVTPTR PTR BASED(CVTPTRPTR),
             CVTADJ FIXED BIN(31,0) DEF CVTPTRPTR;
         DCL IEATCBPTR PTR BASED(CVTPTR);
         DCL 1 IEATCBP BASED(IEATCBPTR),
              2 PAD CHAR(4),
              2 TCBPTR PTR;
         DCL DEBPTR PTR,
             DEBADJ FIXED BIN(31,0) DEF DEBPTR;
         DCL DCBPTR PTR,
             DCBADJ FIXED BIN(31,0) DEF DCBPTR;
         CVTADJ = 16;
         DO DEBADJ = TCBDEB REPEAT DEBDEBAD
           WHILE(DEBADJ ^= 0)
           UNTIL(TIKSTR = DDNAME);
              DCBADJ = DEBDCBAD;
              OFFSETB = DCBTIOT;
              TIKPTR = TCBTIO;
              TIKADJ = TIKADJ + OFFSET + 4;
              END;
         IF DEBADJ = 0
              THEN RETURN(NULL);
              ELSE RETURN(DCBPTR);
         END FINDDCB;
