 SORTSMG:  PROC OPTIONS(MAIN);
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   % INCLUDE SMFDEFI;
   % INCLUDE SMFHDR;
   DCL PLIXOPT CHAR(15) VAR INIT('ISA(100K)') STATIC EXT;
   DCL (JCLOUT,JCLOUTA) RECORD OUTPUT;
   DCL CTLGRET BIT(32);
   DCL AREA CHAR(265);
   DCL SYMARY(12) CHAR(44) VAR STATIC INIT(
     '&DSN','',
     '&DUMMY','',
     '&YY','',
     '&MON','',
     '&DDN','',
     '&DUM2','');
   DCL STATDSN CHAR(44) VAR STATIC INIT(
        'U.SD.U000.SMF.YEAR&YY.&MON');
   DCL DDN PIC'99' STATIC INIT(0);
   DCL JCLARY(6) CHAR(80) VAR STATIC INIT(
     '//IN&DDN DD &DUMMYDSN=&DSN,',
     '// DCB=(LRECL=32760,BLKSIZE=13030,RECFM=VBS),',
     '// DISP=(OLD,DELETE,KEEP)',
     '//OT&DDN DD DSN=&DSN,',
     '// DCB=(LRECL=32760,BLKSIZE=13030,RECFM=VBS),VOL=(,,,10),',
     '// DISP=(NEW,CATLG,DELETE),UNIT=TAPE,LABEL=EXPDT=99000');
   DCL JCLREC CHAR(80);
   DCL MONCK CHAR(2) STATIC INIT('');
   DCL JULGREG ENTRY(CHAR(6),FIXED DEC(5,0));
   DCL GREGDTE CHAR(6);
   DCL MONTAB(12) CHAR(3) STATIC INIT(
     'JAN','FEB','MAR','APR','MAY','JUN','JUL','AUG','SEP',
     'OCT','NOV','DEC');

   OPEN FILE(JCLOUT);
   OPEN FILE(JCLOUTA);
   ON ENDFILE(JOBCARD) GO TO JOBEOJ;
   DO FOREVER=1 REPEAT FOREVER;
        READ FILE(JOBCARD) INTO(JCLREC);
        WRITE FILE(JCLOUT) FROM(JCLREC);
        END;
 JOBEOJ:
   CLOSE FILE(JOBCARD);
   ON ENDFILE(SMFDATA) GO TO EOJ;
   DO FOREVER=1 REPEAT FOREVER;
     READ FILE(SMFDATA) INTO(SMFREC);
     IF REASONABLE(WRITDTE)
       THEN DO;
         CALL JULGREG(GREGDTE,WRITDTE);
         IF SUBSTR(GREGDTE,1,2) ^= MONCK
           THEN DO;
             MONCK = SUBSTR(GREGDTE,1,2);
             DDN = DDN + 1;
             SYMARY(10) = DDN; /* SETS DDN */
             PUT STRING(SYMARY(6)) EDIT((WRITDTE/1000))(P'99');
               /* THE ABOVE SETS YY */
             SYMARY(8)=MONTAB(MONCK); /* SETS MON */
             SYMARY(2)=SETVAR(STATDSN); /* SETS DSN */
             CALL SYSCTLG('NAME',CTLGRET,SYMARY(2),'SYSRES',AREA);
             IF CTLGRET = '0'B
               THEN SYMARY(4) = '';
               ELSE SYMARY(4) = 'DUMMY,';
             DO I = 1 TO 6;
               JCLREC = SETVAR(JCLARY(I));
               WRITE FILE(JCLOUT) FROM(JCLREC);
               END;
             SYMARY(12)='DUMMY,';
    JCLREC=SETVAR('// EXEC AU109229,MON=&MON,YY=&YY,DUMMY=''&DUM2''');
             WRITE FILE(JCLOUTA) FROM(JCLREC);
             END;
         END;
     END;
 EOJ:
   RETURN;

 REASONABLE:  PROC(DTE) RETURNS(BIT(1));
   DCL DTE FIXED DEC(7,0);
   ON ERROR GOTO END_REASONABLE;
   IF (DTE<99365 & DTE>65001)
     THEN RETURN('1'B);
 END_REASONABLE:
   RETURN('0'B);
   END REASONABLE;

 SETVAR:  PROC(INSTR) RETURNS(CHAR(80) VAR);
   DCL INSTR CHAR(*) VAR;
   DCL OTSTR CHAR(80) VAR;
   DCL (I,J) FIXED;
   OTSTR = INSTR;
   DO I = 1 TO 11 BY 2;
     J = INDEX(OTSTR,SYMARY(I));
     IF J ^= 0
       THEN OTSTR=SUBSTR(OTSTR,1,J-1)××SYMARY(I+1)××
         SUBSTR(OTSTR,J+LENGTH(SYMARY(I)));
     END;
   RETURN(OTSTR);
   END SETVAR;

   END SORTSMG;
