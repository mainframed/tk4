 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   DCL 1 REC BASED(RECPTR),
             /* THIS IS THE DEFINITION OF THE ELEMENTS OF THE
                MAIN STORAGE CHAIN OF RECORDS */
        2 LSTPTR PTR,
             /* POINTER TO PREVIOUS RECORD OR ADDR(RECHDR) */
        2 NXTPTR PTR,
             /* POINTER TO NEXT RECORD OR ADDR(RECHDR) */
        2 RECPNLN FIXED BIN(15,0),
             /* LENGTH OF ADD-ON MEMORY FOR APL VARIABLES */
        2 RECFLGS BIT(8),
             /* BIT 1 = 1 IF RECORD IS VERIFIED */
             /* BIT 2 = 1 IF RECORD IS NOT TO BE DISPLAYED */
             /* BIT 3 = 1 IF CURSOR IS TO BE SET TO THIS RECORD */
             /* BIT 4 = IS USED DURING SELECT PROCESSING TO
                        INDICATE WHETHER THIS RECORD HAS BEEN
                        PROCESSED */
             /* BIT 5 = 1 IF RECORD IS TO BE DELETED BY #ABELSP */
        2 RECRES BIT(8),
             /* IF BIT 2 OF RECFLGS IS SET, THIS WILL BE THE
                OFFSET, RELATIVE 0, WHERE THE CURSOR IS TO BE
                PLACED ON THIS LINE. */
        2 RECSEQ FIXED BIN(31,0),
             /* SEQ # FOR THIS REC */
        2 RECLEN FIXED BIN(15,0),
             /* MAXIMUM LENGTH OF RECORD */
        2 RECSTR CHAR(L REFER (RECLEN)) VAR
        @RHADDON;

   DCL 1 RECHDR BASED(RHPTR),
        2 LSTHDR PTR,
             /* POINTER TO THE LAST RECORD */
        2 NXTHDR PTR,
             /* POINTER TO THE FIRST RECORD */
        2 HDPTR PTR,
             /* ADDR(RECHDR) */
        2 RHLST PTR,
             /* PTR TO PREVIOUS RECHDR */
        2 RHNXT PTR,
             /* PTR TO NEXT RECHDR */
        2 RHNAME CHAR(8),
             /* NAME ASSOCIATED WITH THIS ARRAY OF RECORDS */
        2 RHCST FIXED BIN(15,0),
             /* NUMBER OF FIRST LINE IN DISPLAY ALLOCATED TO
                THIS ARRAY OF RECORDS.  THE QUEUE OF RHELS
                IS ORDERED BY THIS FIELD, EXCEPT THAT RHELS
                IN WHICH THIS FIELD IS ZERO ARE STORED AT THE
                END OF THE Q */
        2 RHCEN FIXED BIN(15,0),
             /* NUMBER OF LAST LINE IN DISPLAY ALLOCATED TO
                THIS ARRAY OF RECORDS. */
        2 RH_DISPLAY_HDR PTR,
             /* THIS FIELD IS SET BY THE DISPLAY SUBCOMMAND.  IT IS THE
             HEAD OF THE DISPLAY DBD LIST. POINTS TO A SINGLY-LINKED
             LIST OF ELEMENTS DESCRIBING THE FIELDS TO BE DISPLAYED
             FROM THE OF THE RECORDS IN THIS DATA SET.  SEE STRUCTURE
             RHNAMESTR FOR A DESCRIPTION OF THESE ELEMENTS.  NOTE THAT
             THE ONLY RHNTYPE VALUES TO APPEAR IN THIS LIST WILL BE
             2=CHAR, 3=ZONED. EACH TIME THE USE SUBCOMMAND IS
             SUCCESSFULLY ISSUED, THE DISPLAY SUBCOMMAND WILL BE
             AUTOMATICALLY EXECUTED TO REBUILD THIS LIST. */
        2 RECPTR PTR,
             /* PTR TO CURRENT REOCRD OR PTR
                TO RECHDR IF NO RECORDS IN
                IN-CORE LIST */
        2 FINDTEXT CHAR(80) VAR,
             /* DEFAULT TEXT FOR FIND SUBCOMMAND */
        2 INDDN PTR,
             /* DDNAME FOR REFRESH SUBCOMMAND (SEE ALCSTR) */
        2 OUTDDN PTR,
             /* DDNAME FOR SAVE SUBCOMMAND (SEE ALCSTR) */
        2 BUFPTR PTR,
             /* ADDR OF WORK BUFFER */
        2 SEQSTRT FIXED BIN(31,0),
             /* STARTING SEQUENCE FOR RESEQ SUBCOMMAND */
        2 SEQINCR FIXED BIN(31,0),
             /* INCREMENT FOR RESEQ SUBCOMMAND */
        2 CURSEQ FIXED BIN(31,0),
             /* SEQUENCE CONTROL FIELD.  IN SCAN MODE, THIS FIELD
                WILL BE SET TO THE SEQUENCE NUMBER OF THE RECORD
                LISTED ON THE TOPMOST LINE OF THE DISPLAY.  IN
                INSERT MODE, THIS FIELD IS INCREMENTED TO OBTAIN
                THE NEW SEQUENCE NUMBERS */
        2 PFPB CHAR(2),
             /* SET TO EITHER 'PF' OR 'PB' TO INDICATE THE
                DEFAULT DIRECTION TO SCROLL. */
        2 RHFLAGS,
         3 LOFLAG BIT(1),
             /* ON IF DATA IS TO REMAIN IN LOWER CASE */
         3 SAVEFLAG BIT(1),
             /* ON IF SAVE HAS NOT BEEN DONE SINCE LAST CHANGE */
         3 TERMFLAG BIT(1),
           /* ON IF THIS IS FOR VIRTUAL TERMINAL */
         3 LOGNFLAG BIT(1),
           /* ON IF LOGON COMMAND YET TO BE ENTERED */
         3 CMODEFLAG BIT(1),
           /* ON IF IN COMMAND MODE, OFF IF EDIT MODE */
         3 HEXMODE BIT(1),
           /* ON IF DATA IS TO BE DISPLAYED IN HEX */
         3 RHTRANSW BIT(1),
           /* ON IF TRANSLAT SUBCOMMAND HAS BEEN DONE */
         3 RHSUPSW BIT(1),
           /* ON IF THIS DATA SET IS DEFINED AS A SUPPORT DATA
              SET (SUPPORT KEYWORD ON SSE SUBCOMMAND, OR
              SUPPORT SUBCOMMAND).  IT WILL NOT BE DISPLAYED
              UNLESS A SPECIFIC SHOW SUBCOMMAND IS ISSUED FOR
              IT.  IF THE ONLY DATA SETS CURRENTLY BEING EDITED
              ARE SUPPORT DATA SETS, AND "END N" WILL BE
              ISSUED AUTOMATICALLY FOR ALL OF THEM. */
        2 CMDDATA CHAR(1024) VAR,
             /* SET TO NULL STRING BEFORE CALLING ABEB.
                SUBCOMMANDS MAY STORE NEXT COMMAND TO BE
                EXECUTED HERE. */
        2 MSGDATA CHAR(80) VAR,
             /* SET TO NULL STRING BEFORE CALLING ABEC.
                SUBCOMMANDS MAY STORE MESSAGES TO BE
                DISPLAYED IN THE MESSAGE FIELD HERE. */
        2 TERMECB FIXED BIN(31,0),
          /* ECB FOR OUTSTANDING RECEIVE IF THIS IS FOR
             A VIRTUAL TERMINAL SESSION */
        2 TERMEP PTR,
          /* ENTRY TO VTAMIO SUBRTNE */
        2 TERMID CHAR(8) VAR,
          /* APPLID - TSO VSPC,ETC. */
        2 MARKARY(9) FIXED BIN(31,0),
          /* ARRAY OF MARKED LINES */
        2 LASTCMD CHAR(1024) VAR,
          /* LAST SSE SUBCOMMAND EXECUTED, OR LAST
             LINE TRANSMITTED TO ALTERNATE SESSION */
        2 CMDDISPLAY CHAR(256) VAR,
          /* DATA TO BE DISPLAYED IN THE COMMAND AREA
             OF THE DISPLAY */
        2 CURSOR_SEQ FIXED BIN(31,0),
          /* LINE # OF LINE CONTAINING CURSOR.  IF DATA SET IS
          NOT ON THE DISPLAY, THIS FIELD IS MEANINGLESS.  IF
          CURSOR IS NOT ON ANY DATA LINE OF THE DISPLAY,
          CURSOR_SEQ WILL BE 0. */
        2 RHSPFFLST CHAR(256) VAR,
          /* LAST SPFF SUBCOMMAND ISSUED */
        2 RHSPFCLST CHAR(256) VAR,
          /* LAST SPFC SUBCOMMAND ISSUED */
        2 RHLRECL FIXED BIN(15,0),
          /* DEFAULT LRECL ASSOCIATED WITH THIS DATA SET */
        2 RHBLKL FIXED BIN(15,0),
          /* DEFAULT BLKSIZE ASSOCIATED WITH THIS DATA SET */
        2 RHRECFM BIT(8),
          /* DEFAULT DCBRECFM FIELD */
        2 RHFLAGS2,
         3 RHNONSW BIT(1),
          /* ON IF DEFAULT NONUM SETTING IS NONUM */
         3 RHFAMSW BIT(1),
          /* ON IF RHFAMEP CONTAINS THE ENTRY POINT OF A FILE
             ACCESS MODULE (FAM) FOR THIS DATA SET. */
         3 RHFAMDS BIT(1),
          /* THE FAM MODULE SHOULD TURN THIS FLAG ON IF IT IS
          TO RECEIVE CONTROL TO DISPLAY DATA AND ACCEPT INPUT
          SUBCOMMANDS (CODE=5). */
         3 RHDBSAVE BIT(1),
          /* IF EITHER THIS FLAG OR SAVEFLAG IS ON, DATA HAS
          BEEN MODIFIED SINCE THE LAST SAVE.  THIS FLAG IS
          USED BY THE SAS SUBCOMMAND TO DETERMINE WHETHER
          DATASETS WITH DATABASE DEFINITIONS SHOULD BE WRITTEN
          TO LASTJCL. */
         3 RHNOSHOWSW BIT(1),
          /* ON IF NOSHOW IS IN EFFECT FOR THIS DATA SET */
         3 RHPLOTSW BIT(1),
          /* ON IF PLOT KEYWORD SPECIFIED FOR THIS DATA SET */
         3 RHF2RES BIT(2),
          /* RESERVED */
        2 RHFAMEP ENTRY(PTR,PTR,PTR,FIXED BIN(15,0))
          RETURNS(PTR),
          /* CONTAINS THE ENTRY TO A FILE-ACCESS MODULE (FAM)
          IF THE FAM KEYWORD WAS SPECIFIED ON THE SSE SUBCOMMAND
          FOR THIS DATA SET. */
        2 RHFAMCB PTR,
          /* RESERVED FOR THE USE OF THE FILE ACCESS MODULE. */
        2 RHSPCPT PTR,
          /* POINTS TO THE SELECT PROCESSING CONTROL BLOCK
          (SPCB) DURING SELECT PROCESSING. */
        2 RHUSENM CHAR(8),
          /* CONTAINS THE USE NAME IF ONE WAS SPECIFIED VIA
          EITHER THE USE KEYWORD OR THE USE SUBCOMMAND */
        2 RHUSEHD PTR,
          /* HEAD OF LIST FOR USAGE DBD. IF THE USE KEYWORD HAS BEEN
          GIVEN, OR THE USE SUBCOMMAND HAS BEEN ISSUED, THIS FIELD WILL
          BE THE HEAD OF A SINGLY-LINKED LIST OF ELEMENTS DESCRIBING
          THE FIELDS OF THE RECORDS IN THIS DATA SET.  SEE STRUCTURE
          RHNAMESTR FOR A DESCRIPTION OF THESE ELEMENTS.  EACH TIME THE
          USE SUBCOMMAND IS SUCCESSFULLY ISSUED, THIS QUEUE WILL BE
          DELETED AND REBUILT. */
        2 RH_OFF_DFT FIXED BIN(15,0),
          /* DEFAULT <OFF> VALUE - INITIALLY SET TO 1 */
        2 RH_LEN_DFT FIXED BIN(15,0),
          /* DEFAULT <LEN> VALUE - INITIALLY SET TO 32767 */
        2 RH_NCOLS_DFT FIXED BIN(15,0),
          /* DEFAULT <NCOLS> VALUE - INITIALLY SET TO 1 */
        2 RHCOUNT FIXED BIN(31,0),
          /* NUMBER OF RECORDS IN THIS DATA SET. */
        2 RH_APL PTR,
          /* POINTER TO CONTROL BLOCK CONTAINING RANK & DIMENSIONS */
        2 RHUSETR PTR;
          /* IF THIS FIELD IS NOT NULL IT WILL POINT TO AN AVL
          HEIGHT-BALANCED BINARY TREE IN WHICH EACH NODE IS AN
          ELEMENT OF THE LIST POINTED TO BY RHUSEHD. */


   DCL 1 RHNAMESTR BASED(RHNAMPTR),
   %INCLUDE ABERHN;

   DCL 1 ALCSTR BASED(ALCPTR),
        2 ALCDDN CHAR(8),
        2 ALCDSN CHAR(44) VAR,
        2 ALCMEM CHAR(8),
        2 ALCFLAG BIT(16),
             /* BYTE 1
                ..1. .... RECFM=V OR U
                .... 1... WON'T BE FREED AT END
                .... .1.. NONUM
                .... ..1. IMMEDIATE EOF W/B INDICATED
                .... ...1 LOWERCASE
                BYTE 2
                1... .... THIS IS A DB2 TABLE
                .1.. .... THIS IS A SAS TABLE
                */
        2 ALCSEQOFF FIXED BIN(15,0),
             /* OFFSET (REL 0) TO SEQUENCE FIELD */
        2 ALCSEQLEN FIXED BIN(15,0),
             /* LEN SEQUENCE FIELD */
        2 ALCRECL FIXED BIN(15,0),
             /* MAXIMUM LRECL */
        2 ALCBUF CHAR(L REFER(ALCRECL)) VAR,
             /* BUFFER FOR READS AND WRITES */
        2 ALCBLKL FIXED BIN(15,0),
             /* BLKSIZE */
        2 ALCRECFM BIT(8);
             /* DS1RECFM OR DCBRECFM */
   DCL 1 BUFSTR BASED(BUFPTR),
       /* THIS BUFFER IS USED BY VARIOUS SUBCOMMANDS WHEN
          MANIPULATING DATA.  THE BUFFER ITSELF MAY BE
          ALCBUF IF A FILE IS INVOLVED, OR IT MAY BE
          SEPARATELY ALLOCATED IF THE RECORDS INVOLVED
          ARE FROM AN IN-CORE SPINOFF. */
     2 BUFLN FIXED BIN(15,0),
       /* LENGTH BUFFER */
     2 BUF CHAR(L REFER(BUFLN)) VAR;
       /* WORK BUFFER */
