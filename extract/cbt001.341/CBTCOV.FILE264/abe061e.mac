 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABE061E - APL */
   %INCLUDE ABESUB;
 ABESUB(ABE061E) CHECK(YES) RECURS(YES)
   PARMS(RHPTR,RHHDRPT,PARMSPTR,SYMBOLS,SYMBOL_NAMES);
   %INCLUDE ABESUBA;

   DCL PARMSPTR PTR;
   DCL SYMBOLS(72) CHAR(3);
   DCL SYMBOL_NAMES(72) CHAR(14);
   DCL 1 PARMS_STRUCTURE BASED(PARMSPTR),
   %INCLUDE ABE061A;
   DCL 1 FORMWRK BASED(FORMPTR),
     /******************************************************************
     *                                                                 *
     * THIS STRUCTURE IS USED IN THE TOP_NULL SUBROUTINES TO STORE     *
     * WIDTH/PRECISION INFORMATION.                                    *
     *                                                                 *
     ******************************************************************/
     2 COLCNT FIXED BIN(15,0),
     2 WIDPR(L REFER(COLCNT)),
      3 WIDTH FIXED BIN(15,0),
      3 PRECISION FIXED BIN(15,0);
   DCL FIXED BUILTIN;

   DO_ACCENT_BASE=DO_ACCENT_BASE_REAL;
   DO_TOP_NULL=DO_TOP_NULL_REAL;
   DO_DOMINO=DO_DOMINO_REAL;
   CALL MAIN_LOOP(0);

 DO_ACCENT_BASE_REAL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO PERFORM THE ACCENT BASE OPERATOR.                         *
 *                                                                     *
 **********************************************************************/
   DCL (CBP,LEFT,RHP,RIGHT,RPT) PTR;
   DCL (CIX,I,J,K,L,M,MIX,NEWLEN,NEWTYPE) FIXED BIN;
   DCL DFTLEN(7) FIXED BIN(31,0) STATIC INIT(4,20,20,8,4,4,4);

   CALL GET_DYADIC_OPERANDS('ACCENT BASE',LEFT,RIGHT);
   IF LEFT->RH_APL->RH_APL_RANK^=1
     THEN DO;
       MSGDATA='LEFT OPERAND TO ACCENT RHO MUST BE SCALAR OR VECTOR';
       SIGNAL CONDITION(BADAPL);
       END;
   RPT=NULL;
   NEWTYPE=GET_INTEGER_SEQ('FORWARD',LEFT,RPT,CIX,MIX);
   IF RPT=NULL
     THEN DO;
       MSGDATA='LEFT OPERAND TO ACCENT BASE MUST NOT BE EMPTY';
       SIGNAL CONDITION(BADAPL);
       END;
   IF ^(NEWTYPE>=1 & NEWTYPE<=7)
     THEN DO;
       MSGDATA='TYPE VALUE TO ACCENT BASE MUST RANGE FROM 1 TO 7';
       SIGNAL CONDITION(BADAPL);
       END;
   NEWLEN=GET_INTEGER_SEQ('FORWARD',LEFT,RPT,CIX,MIX);
   IF RPT=NULL
     THEN NEWLEN=DFTLEN(NEWTYPE);
   CBP=CREATE_APLCB(RIGHT->RH_APL->RH_APL_RANK);
   CBP->RH_APL_LEN=RIGHT->RH_APL->RH_APL_LEN;
   CBP->RH_APL_FLAGS=RIGHT->RH_APL->RH_APL_FLAGS;
   CBP->RH_APL_TYPE=NEWTYPE;
   CBP->RH_APL_ELEN=NEWLEN;
   CBP->RH_APL_RHCOUNT=RIGHT->RH_APL->RH_APL_RHCOUNT;
   CBP->RH_APL_ELCOUNT=RIGHT->RH_APL->RH_APL_ELCOUNT;
   CBP->RH_APL_DEMARC=RIGHT->RH_APL->RH_APL_DEMARC;
   DO I=1 TO CBP->RH_APL_RANK;
     CBP->RH_APL_DIM(I)=RIGHT->RH_APL->RH_APL_DIM(I);
     END;
   L=CBP->RH_APL_ELCOUNT*CBP->RH_APL_ELEN; /* LENGTH OF TO-RECORD */
   RHP=CREATE_VAR(L,CBP,''); /* CREATE TO DATASET */
   RHP->HEXMODE=RIGHT->HEXMODE;
   CALL COPY_DATA(RIGHT,RHP);
   CALL END_OPER(LEFT,RIGHT,RHP);
   END DO_ACCENT_BASE_REAL;

 DO_DOMINO_INVERSE: PROC(WMAT);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO INVERSE.  WMAT CONTAINS 'DENOMINATOR' IN LEFT PART,    *
 * AND 'NUMERATOR' IN RIGHT PART.  UPON RETURN OF CONTROL FROM THIS    *
 * SUBROUTINE, THE 'NUMERATOR' WILL HAVE BEEN REPLACED WITH THE        *
 * INVERSE.                                                            *
 *                                                                     *
 **********************************************************************/
   DCL WMAT(*,*) DEC FLOAT(16);
   DCL (COLS,I,J,K,ROWS) FIXED BIN;
   DCL (W,X) DEC FLOAT(16);

   ROWS=HBOUND(WMAT,1);
   COLS=HBOUND(WMAT,2);
   DO I=1 TO ROWS; /* ONCE FOR EVERY ROW */
     K,W=0;
     DO J=I TO ROWS; /* ELEMENTS ABOVE ROW I ALREADY DONE */
       X=ABS(WMAT(J,I));
       IF X>W
         THEN DO; /* NEW POTENTIAL MAX VALUE */
           K=J; /* SAVE ROW NUMBER */
           W=X; /* SAVE NEW POTENTIAL MAX VALUE */
           END;
       END;
     IF K=0
       THEN DO;
         MSGDATA='NO SOLUTION CAN BE OBTAINED FROM DOMINO';
         SIGNAL CONDITION(BADAPL);
         END;
     IF K^=I
       THEN DO J=1 TO COLS; /* INTERCHANGE ROWS */
         W=WMAT(I,J);
         WMAT(I,J)=WMAT(K,J);
         WMAT(K,J)=W;
         END;
     /* MAKE THE LEFTMOST NONZERO ELEMENT, WHICH IS ON THE DIAGONAL,
     1 BY DIVIDING THE ROW BY ITSELF */
     IF WMAT(I,I)^=1E0
       THEN DO;
         W=WMAT(I,I);
         DO J=I TO COLS; /* COLS TO LEFT ARE ALREADY 0 */
           WMAT(I,J)=WMAT(I,J)/W; /* SCALE */
           END;
         END;
     /* NOW MAKE ALL ELEMENTS IN THIS COLUMN BELOW THE DIAGONAL 0 */
     IF I<ROWS
       THEN DO;
         DO J=I+1 TO ROWS;
           IF WMAT(J,I)^=0
             THEN DO;
               W=1/WMAT(J,I); /* FACTOR */
               WMAT(J,I)=0;
               DO K=I+1 TO COLS; /* THERE ARE MORE ROWS THAN COLS */
                 WMAT(J,K)=WMAT(J,K)*W-WMAT(I,K);
                 END;
               END;
           END;
         END;
     /* NOW MAKE ALL ELEMENTS IN THIS COLUMN ABOVE THE DIAGONAL 0 */
     IF I>1
       THEN DO;
         DO J=I-1 TO 1 BY -1;
           IF WMAT(J,I)^=0
             THEN DO;
               W=WMAT(J,I); /* FACTOR */
               WMAT(J,I)=0;
               DO K=I+1 TO COLS; /* THERE ARE MORE ROWS THAN COLS */
                 WMAT(J,K)=WMAT(J,K)-W*WMAT(I,K);
                 END;
               END;
           END;
         END;
     END;
   END DO_DOMINO_INVERSE;

 DO_DOMINO_REAL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO DOMINO (MATRIX INVERSE AND MATRIX DIVIDE).             *
 *                                                                     *
 **********************************************************************/
   DCL (LEFT,RIGHT,TMP) PTR;
   DCL 1 LHRHO,
     2 ROWS FIXED BIN,
     2 COLS FIXED BIN,
     2 ELMS FIXED BIN,
     2 RANK FIXED BIN;
   DCL 1 RHRHO LIKE LHRHO;
   DCL (COLCNT,MDTEMP,MM,N,PPP,RRANK,R1,R2,YCNT) FIXED BIN;

   CALL GET_OPERANDS('DOMINO',LEFT,RIGHT);
   IF RIGHT=NULL
     THEN DO;
       MSGDATA='DOMINO FUNCTION MUST HAVE A RIGHT ARGUMENT';
       SIGNAL CONDITION(BADAPL);
       END;
   CALL DO_DOMINO_RHOARG(RIGHT,RHRHO); /* GET PSEUDO-SHAPE */
   IF RHRHO.ROWS<RHRHO.COLS
     THEN DO;
       MSGDATA='#ROWS M/B >= #COLS IN RIGHT OPERAND TO DOMINO';
       SIGNAL CONDITION(BADAPL);
       END;
   RHRHO.ELMS=RHRHO.ROWS*RHRHO.COLS; /* # ELEMENTS IN RIGHT */
   IF LEFT=NULL
     THEN DO; /* IT IS MONADIC MATRIX INVERSE */
       LHRHO.ROWS,LHRHO.COLS=RHRHO.ROWS;
       RRANK=RIGHT->RH_APL->RH_APL_RANK; /* RANK OF RESULT */
       END;
     ELSE DO; /* IT IS DYADIC MATRIX DIVIDE */
       CALL DO_DOMINO_RHOARG(LEFT,LHRHO); /* GET PSEUDO-SHAPE */
       IF LHRHO.ROWS^=RHRHO.ROWS
         THEN DO;
           MSGDATA='# ROWS DON''T MATCH TO DYADIC DOMINO';
           SIGNAL CONDITION(BADAPL);
           END;
       RRANK=LEFT->RH_APL->RH_APL_RANK+RIGHT->RH_APL->RH_APL_RANK-2;
       /* RRANK=RANK OF RESULT=(RANK OF LEFT)+(RANK OF RIGHT)-2 */
       END;
   LHRHO.ELMS=LHRHO.ROWS*LHRHO.COLS; /* # ELEMENTS IN LEFT */
   IF RHRHO.ROWS=RHRHO.COLS
     THEN TMP=DO_DOMINO_REGULAR(LEFT,RIGHT,LHRHO,RHRHO,2*RHRHO.COLS,
       RRANK);
     ELSE TMP=DO_DOMINO_REGRESS(LEFT,RIGHT,LHRHO,RHRHO,RRANK);
   CALL END_OPER(LEFT,RIGHT,TMP);
   END DO_DOMINO_REAL;

 DO_DOMINO_REGRESS: PROC(LEFT,RIGHT,LHRHO,RHRHO,RRANK) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO DOMINO (MATRIX INVERSE AND MATRIX DIVIDE) WHEN         *
 * RIGHT ARGUMENT IS NOT SQUARE.  NOTE THAT IN THE COMMENTS            *
 * DESCRIBING MATRIX OPERATIONS, THE SYNTAX USED IN PROC MATRIX IN     *
 * SAS IS USED.                                                        *
 *                                                                     *
 * GIVEN -                                                             *
 *   X = MATRIX POINTED TO BY RIGHT.                                   *
 *   Y = MATRIX POINTED TO BY LEFT.  IF LEFT=NULL, AN IDENTITY MATRIX  *
 *       WILL BE USED.                                                 *
 *   BETA = MATRIX THAT IS THE DESIRED RESULT.                         *
 *                                                                     *
 * IN MATRIX NOTATION, BETA WILL BE PRODUCED AS FOLLOWS:               *
 *   XPX=X'*X CROSS PRODUCTS MATRIX                                    *
 *   XPXI=INV(XPX) INVERSE CROSS PRODUCTS MATRIX                       *
 *   XPY=X'*Y SUM(OF SY) AND SUM(OF XY)                                *
 *   BETA=XPXI*XPY                                                     *
 *                                                                     *
 **********************************************************************/
   DCL (CBP,LEFT,RIGHT,TMP) PTR;
   DCL RRANK FIXED BIN;
   DCL 1 LHRHO,
     2 ROWS FIXED BIN,
     2 COLS FIXED BIN,
     2 ELMS FIXED BIN,
     2 RANK FIXED BIN;
   DCL 1 RHRHO LIKE LHRHO;

   DCL X(RHRHO.ROWS,RHRHO.COLS) DEC FLOAT(16);
   DCL Y(LHRHO.ROWS,LHRHO.COLS) DEC FLOAT(16);
   DCL XPX(RHRHO.COLS,2*RHRHO.COLS) DEC FLOAT(16);
     /* XPX CONTAINS ROOM BOTH FOR X'*X AND INV(X'*X) */
   DCL XPY(RHRHO.COLS,LHRHO.COLS) DEC FLOAT(16);
     /* XPY CONTAINS X'*Y */
   DCL BETA(RHRHO.COLS,LHRHO.COLS) DEC FLOAT(16);

   DCL (I,J,K,LCIX,LMIX,M,N,OCIX,OMIX,RCIX,RMIX) FIXED BIN;
   DCL W DEC FLOAT(16);
   DCL (LPT,RPT) PTR;

   /* GET X VALUES */
   RPT=NULL;
   DO I=1 TO RHRHO.ROWS;
     DO J=1 TO RHRHO.COLS;
       X(I,J)=GET_SCALAR_SEQ('FORWARD',RIGHT,RPT,RCIX,RMIX);
       END;
     END;
   /* GET Y VALUES */
   LPT=NULL;
   DO I=1 TO LHRHO.ROWS;
     DO J=1 TO LHRHO.COLS;
       IF LEFT=NULL
         THEN Y(I,J)=(I=J); /* IDENTITY MATRIX */
         ELSE Y(I,J)=GET_SCALAR_SEQ('FORWARD',LEFT,LPT,LCIX,LMIX);
       END;
     END;
   /* NOW CALCULATE XPX=X'*X */
   DO I=1 TO RHRHO.COLS;
     DO J=1 TO RHRHO.COLS;
       XPX(I,J)=0;
       DO K=1 TO RHRHO.ROWS;
         XPX(I,J)=XPX(I,J)+X(K,I)*X(K,J);
         END;
       END;
     END;
   /* NOW CALCULATE XPY=X'*Y */
   DO I=1 TO RHRHO.COLS;
     DO J=1 TO LHRHO.COLS;
       XPY(I,J)=0;
       DO K=1 TO LHRHO.ROWS; /* NOTE LHRHO.ROWS=RHRHO.ROWS */
         XPY(I,J)=XPY(I,J)+X(K,I)*Y(K,J);
         END;
       END;
     END;
   /* NOW OBTAIN XPXI=INVERSE OF XPX */
   /* FIRST DEVELOP IDENTITY MATRIX */
   DO I=1 TO RHRHO.COLS;
     DO J=1 TO RHRHO.COLS;
       XPX(I,J+RHRHO.COLS)=(I=J);
       END;
     END;
   CALL DO_DOMINO_INVERSE(XPX); /* OBTAIN INVERSE */
   /* NOW OBTAIN BETA=XPXI*XPY */
   DO I=1 TO RHRHO.COLS; /* ONCE FOR EACH ROW IN BETA */
     DO J=1 TO LHRHO.COLS; /* ONCE FOR EACH COL IN BETA */
       BETA(I,J)=0; /* INITIALIZE */
       DO K=1 TO RHRHO.COLS;
         BETA(I,J)=BETA(I,J)+XPX(I,RHRHO.COLS+K)*XPY(K,J);
         END;
       END;
     END;
   TMP=DO_DOMINO_TMP(BETA,1,LHRHO.COLS,RRANK);
   RETURN(TMP);
   END DO_DOMINO_REGRESS;

 DO_DOMINO_REGULAR: PROC(LEFT,RIGHT,LHRHO,RHRHO,COLCNT,RRANK)
   RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO DOMINO (MATRIX INVERSE AND MATRIX DIVIDE) WHEN         *
 * RIGHT ARGUMENT IS SQUARE.                                           *
 *                                                                     *
 **********************************************************************/
   DCL (CBP,LEFT,RIGHT,TMP) PTR;
   DCL 1 LHRHO,
     2 ROWS FIXED BIN,
     2 COLS FIXED BIN,
     2 ELMS FIXED BIN,
     2 RANK FIXED BIN;
   DCL 1 RHRHO LIKE LHRHO;
   DCL (COLCNT,RRANK) FIXED BIN;

   DCL WMAT(RHRHO.ROWS,COLCNT) DEC FLOAT(16);

   DCL (I,J,K,LCIX,LMIX,OCIX,OMIX,RCIX,RMIX) FIXED BIN;
   DCL (W,X) DEC FLOAT(16),
       WCH CHAR(8) BASED(ADDR(W));
   DCL (LPT,RPT) PTR;

   LPT,RPT=NULL;
   DO I=1 TO RHRHO.ROWS;
     /* GET RIGHT OPERAND VALUES */
     DO J=1 TO RHRHO.COLS;
       WMAT(I,J)=GET_SCALAR_SEQ('FORWARD',RIGHT,RPT,RCIX,RMIX);
       WMAT(I,RHRHO.COLS+J)=(I=J);
       END;
     END;
   CALL DO_DOMINO_INVERSE(WMAT);
   /* INVERSE IS IN RIGHT PART OF WMAT */
   IF LEFT=NULL
     THEN TMP=DO_DOMINO_TMP(WMAT,COLCNT/2+1,COLCNT/2,RRANK);
     ELSE BEGIN; /* MUST DO MATRIX MULTIPLY */
       DCL B(LHRHO.ROWS,LHRHO.COLS) DEC FLOAT(16);
       DCL C(RHRHO.ROWS,LHRHO.COLS) DEC FLOAT(16);
       DCL (I,J,K,N) FIXED BIN;

       N=COLCNT/2;
       DO I=1 TO LHRHO.ROWS;
         DO J=1 TO LHRHO.COLS;
           B(I,J)=GET_SCALAR_SEQ('FORWARD',LEFT,LPT,LCIX,LMIX);
           END;
         END;
       DO I=1 TO HBOUND(C,1);
         DO J=1 TO HBOUND(C,2);
           C(I,J)=0;
           DO K=1 TO N;
             C(I,J)=C(I,J)+WMAT(I,N+K)*B(K,J);
             END;
           END;
         END;
       TMP=DO_DOMINO_TMP(C,1,HBOUND(C,2),RRANK);
       END;
   RETURN(TMP);
   END DO_DOMINO_REGULAR;

 DO_DOMINO_RHOARG: PROC(PPTR,RHO);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO DOMINO (MATRIX INVERSE AND MATRIX DIVIDE).             *
 *                                                                     *
 **********************************************************************/
   DCL PPTR PTR;
   DCL 1 RHO,
     2 ROWS FIXED BIN,
     2 COLS FIXED BIN,
     2 ELMS FIXED BIN,
     2 RANK FIXED BIN;

   RANK=0; /* ASSUME RANK=0 */
   ROWS,COLS=1; /* ASSUME 1 */
   IF PPTR->RH_APL->RH_APL_SCALAR
     THEN RETURN;
   RANK=PPTR->RH_APL->RH_APL_RANK;
   SELECT(RANK);
     WHEN(1) DO; /* IT IS VECTOR */
       ROWS=PPTR->RH_APL->RH_APL_DIM(1);
       COLS=1;
       END;
     WHEN(2) DO; /* IT IS MATRIX */
       ROWS=PPTR->RH_APL->RH_APL_DIM(1);
       COLS=PPTR->RH_APL->RH_APL_DIM(2);
       END;
     OTHERWISE DO; /* RANK ERROR */
       MSGDATA='RANK ERROR IN DOMINO FUNCTION';
       SIGNAL CONDITION(BADAPL);
       END;
     END; /* SELECT */
   END DO_DOMINO_RHOARG;

 DO_DOMINO_TMP: PROC(WMAT,COLSTRT,COLCNT,RRANK) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * SOLUTION TO DOMINO FUNCTION IS IN RIGHT HALF OF WMAT.  THIS         *
 * WILL CREATE A NEW VARIABLE IN STORE THE SOLUTION IN IT.             *
 *                                                                     *
 **********************************************************************/
   DCL WMAT(*,*) DEC FLOAT(16);
   DCL (COLSTRT,COLCNT,RRANK) FIXED BIN;
   DCL (CBP,TMP) PTR;
   DCL (W,X) DEC FLOAT(16),
       WCH CHAR(8) BASED(ADDR(W));
   DCL (I,J,OCIX,OMIX) FIXED BIN;

   /* SOLUTION IS IN RIGHT PART OF WMAT */
   IF RRANK=0
     THEN DO; /* RESULT IS SCALAR */
       CBP=CREATE_APLCB(1);
       CBP->RH_APL_SCALAR='1'B;
       CBP->RH_APL_DIM(1)=1;
       CBP->RH_APL_ELCOUNT=1;
       I=1;
       END;
     ELSE DO;
       CBP=CREATE_APLCB(RRANK);
       CBP->RH_APL_DIM(1)=HBOUND(WMAT,1);
       IF RRANK=2
         THEN DO;
           CBP->RH_APL_DIM(2)=COLCNT;
           I,CBP->RH_APL_ELCOUNT=CBP->RH_APL_DIM(1)*CBP->RH_APL_DIM(2);
           END;
         ELSE I,CBP->RH_APL_ELCOUNT=CBP->RH_APL_DIM(1);
       END;
   CBP->RH_APL_RHCOUNT=1;
   TMP=CREATE_VAR(I*8,CBP,'');
   DO I=1 TO CBP->RH_APL_DIM(1);
     DO J=COLSTRT TO COLSTRT+COLCNT-1;
       W=WMAT(I,J);
       CALL PUT_EL(WCH,TMP,OCIX,OMIX);
       END;
     END;
   RETURN(TMP);
   END DO_DOMINO_TMP;

 DO_TOP_NULL_COMMON: PROC(FPTR,RIGHT) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO TOP NULL (FORMAT) BOTH MONADIC AND DYADIC PROCESSING   *
 *                                                                     *
 **********************************************************************/
   DCL (CBP,FPTR,RIGHT,RIXW,RPT,TMP) PTR;
   DCL (CIX,I,J,K,M,MIX,N) FIXED BIN;

   L=RIGHT->RH_APL->RH_APL_RANK;
   ALLOCATE IXW SET(RIXW);
   RIXW->IXWRHP=RIGHT;
   J=0; /* J WILL EQUAL FINAL DIMENSION, I.E. TOTAL WIDTH */
   DO I=1 TO FPTR->COLCNT;
     K=DO_TOP_NULL_COMMON_WIDTH(I,RIXW,FPTR->PRECISION(I));
     IF FPTR->WIDTH(I)=0
       THEN FPTR->WIDTH(I)=K;
       ELSE IF FPTR->WIDTH(I)<K-1
         THEN DO; /* DOMAIN ERROR */
           FREE RIXW->IXW;
           FREE FPTR->FORMWRK;
           MSGDATA='WIDTH TOO SMALL TO FORMAT FUNCTION';
           SIGNAL CONDITION(BADAPL);
           END;
     J=J+FPTR->WIDTH(I);
     END;
   CBP=CREATE_APLCB(L);
   CBP->RH_APL_DEMARC=L-1;
   K=1;
   DO I=1 TO L-1;
     CBP->RH_APL_DIM(I)=RIGHT->RH_APL->RH_APL_DIM(I);
     K=K*CBP->RH_APL_DIM(I);
     END;
   CBP->RH_APL_DIM(L)=J;
   CBP->RH_APL_ELEN=1;
   CBP->RH_APL_ELCOUNT=J; /* #ELS/REC */
   CBP->RH_APL_RHCOUNT=K; /* #RECS */
   CBP->RH_APL_TYPE=CHAR_TYPE;
   TMP=CREATE_VAR(J,CBP,''); /* CREATE OUTPUT DATASET */
   TMP->HEXMODE='0'B; /* TURN OFF HEX MODE */
   RPT=NULL;
   DO M=1 TO K; /* LOOP ONCE FOR EACH RECORD */
     I=#CMD('NL',TMP,RHHDRPT); /* TIME TO GET NEW RECORD */
     DO N=1 TO FPTR->COLCNT; /* LOOP ONCE FOR EACH COLUMN */
       TMP->RECPTR->RECSTR=TMP->RECPTR->RECSTR××
         DO_TOP_NULL_COMMON_FORMAT(
           GET_SCALAR_SEQ('FORWARD',RIGHT,RPT,CIX,MIX),
           FPTR->WIDTH(N),FPTR->PRECISION(N));
       END;
     END;
   FREE RIXW->IXW;
   RETURN(TMP);
   END DO_TOP_NULL_COMMON;

 DO_TOP_NULL_COMMON_FORMAT: PROC(VALUE,WIDTH,PRECISION)
   RETURNS(CHAR(256) VAR);
 /**********************************************************************
 *                                                                     *
 * THIS ROUTINE IS INVOKED DURING TOP_NULL PROCESSING TO FORMAT        *
 * A VALUE.                                                            *
 *                                                                     *
 **********************************************************************/
   DCL (COLUMN,I,J,K,PRECISION,WIDTH) FIXED BIN;
   DCL (P,R,VALUE) DEC FLOAT(16);
   DCL RETSTR CHAR(256) VAR;

   IF UNSPEC(VALUE)=UNSPEC(MISSING)
     THEN RETSTR='.'; /* INDICATE MISSING VALUE */
     ELSE DO;
       IF PRECISION<0
         THEN DO; /* SCIENTIFIC NOTATION IS TO BE USED */
           /* GET ROUNDED VALUE */
           P=10**(ABS(PRECISION)-1); /* ADJUSTMENT FOR ROUNDING */
           R=.5+VALUE*P;
           R=R/P; /* R=COLUMN ELEMENT, ROUNDED */
           PUT STRING(RETSTR) LIST(R);
           RETSTR=SUBSTR(RETSTR,PREFIX(RETSTR,' '));
           RETSTR=SUBSTR(RETSTR,1,SUFFIX(RETSTR,' '));
           I=INDEX(RETSTR,'E');
           IF I=0
             THEN RETSTR='.'; /* SOMETHING WRONG - SET MISSING VALUE */
             ELSE IF PRECISION=-1
               THEN RETSTR=SUBSTR(RETSTR,1,1)××SUBSTR(RETSTR,I);
               ELSE RETSTR=SUBSTR(RETSTR,1,ABS(PRECISION)+1)××
                 SUBSTR(RETSTR,I);
           END;
         ELSE DO;
           PUT STRING(RETSTR) EDIT(VALUE) (F(WIDTH,PRECISION));
           RETSTR=SUBSTR(RETSTR,PREFIX(RETSTR,' '));
           RETSTR=SUBSTR(RETSTR,1,SUFFIX(RETSTR,' '));
           END;
       END;
   IF LENGTH(RETSTR)<WIDTH
     THEN RETSTR=REPEAT(' ',WIDTH-LENGTH(RETSTR)-1)××RETSTR;
   RETURN(RETSTR);
   END DO_TOP_NULL_COMMON_FORMAT;

 DO_TOP_NULL_COMMON_WIDTH: PROC(COLUMN,RIXW,PRECISION)
   RETURNS(FIXED BIN(31,0));
 /**********************************************************************
 *                                                                     *
 * THIS ROUTINE IS INVOKED DURING TOP_NULL PROCESSING WHEN A WIDTH     *
 * SPECIFICATION OF ZERO IS ENCOUNTERED.  IT WILL DETERMINE AND        *
 * RETURN THE APPROPRIATE WIDTH SPECIFICATION.                         *
 *                                                                     *
 * IF PRECISION>=0, THIS IMPLIES DECIMAL FORM WITH PRECISION           *
 * SPECIFYING THE NUMBER OF DIGITS TO THE RIGHT OF THE DECIMAL.        *
 * IF PRECISION<0, THIS IMPLIES SCALED FORM WITH PRECISION SPECIFYING  *
 * THE NUMBER OF DIGITS IN THE MULTIPLIER.                             *
 *                                                                     *
 **********************************************************************/
   DCL RIXW PTR;
   DCL (COLUMN,I,J,K,PRECISION) FIXED BIN;
   DCL (W,X,Z) FIXED BIN(31,0);
   DCL WRKSTR CHAR(256) VAR;
   DCL (F,P,R) DEC FLOAT(16),
       FCH CHAR(8) BASED(ADDR(F));

   RIXW->IXWROW=MISSING_INTEGER;
   RIXW->IX(*)=1;
   J=RIXW->IXWRHP->RH_APL->RH_APL_RANK; /* COLUMN DIMENSION # */
   IF J<=1
     THEN K=1; /* # ELEMENTS PER COLUMN */
     ELSE K=RIXW->IXWRHP->RH_APL->RH_APL_DIM(J-1);
   Z=0; /* Z WILL BE MINIMUM WIDTH INDICATED BY PRECISION VALUE */
   IF ^(PRECISION=0 × PRECISION=-1)
     THEN Z=Z+1; /* A DECIMAL POINT IS NEEDED */
   Z=Z+ABS(PRECISION); /* EITHER THE NUMBER OF POSITIONS TO THE RIGHT
     OF THE DECIMAL, OR THE NUMBER OF POSITIONS IN THE MULTIPLIER. */
   IF PRECISION<0
     THEN DO;
       Z=Z+5;
       P=10**(ABS(PRECISION)-1); /* ADJUSTMENT FOR ROUNDING */
       END;
     ELSE DO;
       Z=Z+1; /* ADD 1 FOR PAD BETWEEN COLUMNS */
       P=10**ABS(PRECISION);
       END;
   W=Z; /* WILL BE WIDTH TO RETURN - MINIMUM WIDTH REQUIRED */
   DO I=1 TO K;
     RIXW->IX(J)=COLUMN;
     WRKSTR=GET_ELEMENT(RIXW);
     FCH=CONVERT(PTROUT(PTRIN(ADDR(WRKSTR))+2),
       TYPETAB(RIXW->IXWRHP->RH_APL->RH_APL_TYPE),LENGTH(WRKSTR),0,
       'FLOAT',8,0);
     IF F=0
       THEN R=F;
       ELSE DO;
         R=.5+F*P;
         R=R/P; /* R=COLUMN ELEMENT, ROUNDED */
         END;
     X=Z; /* SET PREDEFINED MINIMUM */
     IF R<0
       THEN X=X+1; /* 1 FOR NEGATIVE SIGN */
     IF PRECISION>=0
       THEN X=X+MAX(0,FIXED(LOG10(ABS(R)+(R=0)),31)+1);
     W=MAX(W,X);
     RIXW->IX(J)=K; /* FORCE BUMP */
     CALL GET_ELEMENT_AXIS(RIXW,J,1,1,1);
     END;
   RETURN(W);
   END DO_TOP_NULL_COMMON_WIDTH;

 DO_TOP_NULL_REAL: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO TOP NULL (FORMAT).
 *                                                                     *
 **********************************************************************/
   DCL (FPTR,LEFT,RIGHT,TMP) PTR;
   DCL I FIXED BIN;

   CALL GET_OPERANDS('TOP NULL',LEFT,RIGHT);
   IF RIGHT=NULL
     THEN DO;
       MSGDATA='MISSING RIGHT OPERAND TO TOP NULL';
       SIGNAL CONDITION(BADAPL);
       END;
   L=RIGHT->RH_APL->RH_APL_DIM(RIGHT->RH_APL->RH_APL_RANK);
   ALLOCATE FORMWRK SET(FPTR);
   IF LEFT=NULL
     THEN TMP=DO_TOP_NULL_MONADIC(FPTR,RIGHT); /* MONADIC FORMAT */
     ELSE TMP=DO_TOP_NULL_DYADIC(FPTR,LEFT,RIGHT); /* DYADIC FORMAT */
   FREE FPTR->FORMWRK;
   CALL END_OPER(LEFT,RIGHT,TMP);
   END DO_TOP_NULL_REAL;

 DO_TOP_NULL_MONADIC: PROC(FPTR,RIGHT) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO TOP NULL (FORMAT).                                     *
 *                                                                     *
 **********************************************************************/
   DCL (CBP,FPTR,RIGHT,TMP) PTR;
   DCL (I,J,K,L) FIXED BIN;

   IF RIGHT->RH_APL->RH_APL_TYPE=CHAR_TYPE
     THEN DO; /* AN EXACT COPY IS TO BE RETURNED */
       CBP=CREATE_APLCB(RIGHT->RH_APL->RH_APL_RANK);
       CBP->RH_APL_TYPE=CHAR_TYPE;
       CBP->RH_APL_ELEN=RIGHT->RH_APL->RH_APL_ELEN;
       CBP->RH_APL_DEMARC=CBP->RH_APL_RANK-1;
       DO I=1 TO CBP->RH_APL_RANK;
         CBP->RH_APL_DIM(I)=RIGHT->RH_APL->RH_APL_DIM(I);
         END;
       J=1;
       DO I=1 TO CBP->RH_APL_DEMARC;
         J=J*CBP->RH_APL_DIM(I);
         END;
       /* J=NEW RHCOUNT */
       K=1;
       DO I=CBP->RH_APL_DEMARC+1 TO CBP->RH_APL_RANK;
         K=K*CBP->RH_APL_DIM(I);
         END;
       /* K=NEW ELCOUNT */
       CBP->RH_APL_RHCOUNT=J;
       CBP->RH_APL_ELCOUNT=K;
       L=K*CBP->RH_APL_ELEN; /* NEW RECSIZE */
       TMP=CREATE_VAR(L,CBP,''); /* NEW DATA SET */
       TMP->HEXMODE=RIGHT->HEXMODE;
       CALL COPY_DATA(RIGHT,TMP);
       END;
     ELSE DO; /* NON-CHAR M/B FORMATTED */
       DO I=1 TO FPTR->COLCNT;
         FPTR->WIDTH(I)=0;
         FPTR->PRECISION(I)=2; /* DEFAULT PRECISION OF 2 DEC PLACES */
         END;
       TMP=DO_TOP_NULL_COMMON(FPTR,RIGHT);
       END;
   RETURN(TMP);
   END DO_TOP_NULL_MONADIC;

 DO_TOP_NULL_DYADIC: PROC(FPTR,LEFT,RIGHT) RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO TOP NULL (FORMAT).
 *                                                                     *
 **********************************************************************/
   DCL (FPTR,LEFT,RIGHT,RPT) PTR;
   DCL (CIX,I,J,K,MIX) FIXED BIN;

   RPT=NULL;
   I=0;
   J=2;
   DO FOREVER=1 REPEAT FOREVER;
     K=GET_INTEGER_SEQ('FORWARD',LEFT,RPT,CIX,MIX);
     IF RPT=NULL
       THEN LEAVE;
     IF J=2
       THEN DO;
         J=1;
         I=I+1;
         FPTR->WIDTH(I)=K;
         END;
       ELSE DO;
         J=2;
         FPTR->PRECISION(I)=K;
         END;
     END;
   SELECT;
     WHEN(I=0) DO;
       MSGDATA='LEFT OPERAND TO TOP NULL CANNOT BE NULL';
       FREE FPTR->FORMWRK;
       SIGNAL CONDITION(BADAPL);
       END;
     WHEN(I=1 & J=1) DO; /* SINGLE NUMBER SUPPLIED */
       FPTR->PRECISION(1)=FPTR->WIDTH(1);
       FPTR->WIDTH(1)=0;
       END;
     WHEN(I=1 & J=2); /* SINGLE PAIR OF NUMBERS SUPPLIED */
     WHEN(I=FPTR->COLCNT & J=2); /* ALL PAIRS OF NUMBERS SUPPLIED */
     OTHERWISE DO;
       MSGDATA='INCORRECT NUMBER ELEMENTS LEFT ARGUMENT TO TOP-NULL';
       FREE FPTR->FORMWRK;
       SIGNAL CONDITION(BADAPL);
       END;
     END; /* SELECT */
   IF I<FPTR->COLCNT
     THEN DO I=I+1 TO FPTR->COLCNT;
       FPTR->WIDTH(I)=FPTR->WIDTH(I-1);
       FPTR->PRECISION(I)=FPTR->PRECISION(I-1);
       END;
   RETURN(DO_TOP_NULL_COMMON(FPTR,RIGHT));
   END DO_TOP_NULL_DYADIC;

   END ABE061E;
