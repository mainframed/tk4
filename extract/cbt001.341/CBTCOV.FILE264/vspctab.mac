 /* VSPCTAB */
 VSPCTAB:  PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
         % INCLUDE SMFHDR;
         % INCLUDE SMFVSPC;
         % INCLUDE SMFDEFI;
         DCL IDPIC PIC'9999999';
         DCL CTOHMC ENTRY(FIXED BIN(31,0))
           RETURNS(CHAR(11));
         DCL JULGREG ENTRY(CHAR(6),FIXED DEC(5,0));
         DCL TIMEOFF ENTRY(FIXED DEC(5,0), FIXED BIN(31,0),
           FIXED BIN(31,0), FIXED BIN(31,0),
           FIXED DEC(5,0), FIXED BIN(31,0));
         DCL GREGDTE CHAR(6);
         DCL SDTE FIXED DEC(5,0);
         DCL STME FIXED BIN(31,0);
         ON ENDPAGE(SYSPRINT) BEGIN;
           PUT EDIT('VSPC JOURNAL - VSPCTAB.PLI')(A) PAGE;
           PUT EDIT('CPU TIME','DATE','START','END','DURATION')
             (COL(9),A,COL(21),A,COL(28),A,COL(37),A,COL(46),A) SKIP(2);
           PUT EDIT('VSPC ID HH:MM:SS.TH MMDDYY',
             ('HH:MM:SS' DO IP = 1 TO 3))
             (A,3 (X(1),A)) SKIP(1);
             PUT SKIP(1);
           END;
         SIGNAL ENDPAGE(SYSPRINT);
         ON ENDFILE(SMFDATA) GO TO EOJ;
         DO FOREVER=1 REPEAT FOREVER;
           DO UNTIL(BIN(TYPE)=48 & VSPCID=6);
             READ FILE(SMFDATA) INTO(SMFREC);
             END;
           IF OK
             THEN DO;
               CALL JULGREG(GREGDTE,WRITDTE);
               CALL TIMEOFF(WRITDTE,WRITME,-VSPCCNCT*100,
                 8640000,SDTE,STME);
               PUT EDIT(IDPIC,CTOHMC(VSPCCPU),GREGDTE,
                 CTOHMC(STME),CTOHMC(WRITME),CTOHMC(VSPCCNCT*100))
                 (A(7),X(1),A(11),X(1),A(6),3 (X(1),A(8)))
                 SKIP(1);
               END;
           END;
 OK:  PROC RETURNS(BIT(1));
         IDPIC = VSPCUID;
         IF PARM = ''
           THEN RETURN('1'B);
         IF INDEX(PARM,IDPIC)^=0
           THEN RETURN('1'B);
           ELSE RETURN('0'B);
         END OK;
 EOJ:
         END;
