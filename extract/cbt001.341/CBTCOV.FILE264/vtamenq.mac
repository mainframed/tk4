 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
* LULUENQ
* (R0) FUNCTION CODE
*      0 = ENQUEUE
*      4 = DEQUEUE
* (R1) A(APPLID AREA) - 9 BYTES MUST BE SUPPLIED
* (R2) ADDR INCREMENTAL NUMBER IN APPLID
* AT EXIT -
*
* (RF) 0 IF OK, 4 IF NOT.
LULUENQ1 CSECT
         DC    C'LULUENQ'
         DC    AL1(7)
         ENTRY LULUENQ
LULUENQ  DS    0H
         SAVES RETCTL
         TGSREQU
         LR    R5,R2              SAVE A(INCREMENTAL #)
         LR    R2,R0              R2 = FUNCTION CODE
         LR    R3,R1              R3=A(APPLID)
         B     BRTAB(R2)          BR TO APPROPRIATE SUBROUTINE
BRTAB    DS    0H
         B     ENQ                OFFSET 0
         B     DEQ                       4
ENQ      DS    0H
         LR    R6,R5
BLUP     DS    0H
         LA    R6,1(R6)
         CLI   0(R6),C' '         IS IT BLANK?
         BC    7,BLUP             BR IF NOT
         SR    R6,R5              R6=LEN # = L2
         BCTR  R6,0               LESS 1 FOR EX
         LA    R7,1               R7=L1
         SLL   R7,4               SHIFT
         OR    R6,R7              AND COMBINE L1,L2
         EX    R6,PCK             PACK INCREMENTAL #
         B     *+10
PCK      PACK  IDPCK(0),0(0,R5)
LUP      DS    0H
         MVC   EDWORK,EDMASK
         EDMK  EDWORK,IDPCK
         LA    R4,EDWORK+2
         SR    R4,R1              R4=LENGTH FOR EX
         EX    R4,MVCNUM
         B     *+10
MVCNUM   MVC   0(0,R5),0(R1)
         ENQ   (QNAME,(R3),E,0,SYSTEM),RET=USE
         LTR   RF,RF              ENQ OK?
         BC    8,RETCTL
         AP    IDPCK,=P'1'        INCREMENT RESOURCE ID
         CP    IDPCK,=P'99'       IS ID MAX VALUE?
         BC    4,LUP              BR IF NOT
         LA    RF,4
DEQ      DS    0H
         DEQ   (QNAME,(R3),0,SYSTEM)
         B     RETCTL
QNAME    DC    CL8'KCENQ'
EDMASK   DC    X'202020'
EDWORK   DS    CL3
IDPCK    DS    PL2
         LTORG
         END
