 /* CALCPRT */
 /*
0*$.HE GENERALIZED SUBROUTINES - CALCPRT
0*$.HE SETS FLOATING POINT NUMBER TO PRINT
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$07/12/82
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  CALCPRT
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE ACCEPTS A FLOATING POINT NUMBER (DEC FLOAT(16))
0*$AND RETURNS CHAR(80) VARYING CONTAINING THE PRINTABLE
0*$EQUIVALENT IN THE MOST APPROPRIATE FORMAT (E.G. EXPONENTIAL
0*$IF EXCESSIVELY LARGE OR SMALL, OR DDDDD.DDDD OTHERWISE).
0*$.SP 1
0*$NOTE THAT IF THE ARGUMENT IS THE MAXIMUM NEGATIVE NUMBER
0*$(HAS THE INTERNAL FORMAT X'80000000') IT IS CONSIDERED A MISSING
0*$VALUE AS IT WOULD IN SAS, AND THE CHARACTER STRING RETURNED WILL
0*$BE '.'.
 */
 CALCPRT:  PROC(VALUE) RETURNS(CHAR(80) VAR) OPTIONS(REENTRANT);
   DCL VALUE DEC FLOAT(16);
   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*), CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL FD FIXED DEC(15,7);
   DCL FI FIXED DEC(15,0);
   DCL LG FIXED BIN(15,0);
   DCL (PRTVAL,VALSTR) CHAR(80) VAR;
   DCL MISSING DEC FLOAT(16);
   UNSPEC(MISSING)='1'B; /* SET MISSING VALUE */
   IF UNSPEC(VALUE)=UNSPEC(MISSING)
     THEN RETURN('.');
   IF VALUE=0
     THEN LG=0;
     ELSE LG=LOG10(ABS(VALUE));
   IF MOD(VALUE,1)=0 & LG<15
     THEN DO; /* IT IS INTEGER VALUE */
       FI=VALUE;
       IF FI=0
         THEN VALSTR='0';
         ELSE DO;
           PUT STRING(VALSTR) EDIT(FI) (F(16));
           VALSTR=SUBSTR(VALSTR,PREFIX(VALSTR,' 0'));
           END;
       END;
     ELSE IF LG>7 × LG<-6
       THEN PUT STRING(VALSTR) LIST(VALUE);
       ELSE DO;
         FD=VALUE;
         FD=ROUND(FD,7);
         PUT STRING(VALSTR) EDIT(FD) (F(16,6));
         VALSTR=SUBSTR(VALSTR,PREFIX(VALSTR,' 0'));
         I=INDEX(VALSTR,'.');
         J=MIN(LENGTH(VALSTR),SUFFIX(VALSTR,' 0'));
         IF I=J
           THEN K=1;
           ELSE K=0;
         VALSTR=SUBSTR(VALSTR,1,J+K);
         END;
   IF VALSTR='.0'
     THEN VALSTR='0';
     ELSE IF SUBSTR(VALSTR,LENGTH(VALSTR)-1)='.0'
       THEN VALSTR=SUBSTR(VALSTR,1,LENGTH(VALSTR)-2);
   RETURN(VALSTR);
   END CALCPRT;
