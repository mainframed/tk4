 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* DENT051 - NL,R SUBCOMMANDS */
   % DCL @PROCNAME CHAR;
   % DCL @RENTNAME CHAR;
   % @PROCNAME = 'RENT';
   % @RENTNAME = 'DENT051';
   % INCLUDE DENTSUBA;
   HALT;
   DCL L FIXED BIN(15,0);
   DCL 1 BLSTR BASED(BLPTR),
     2 NBLPTR PTR,
     2 BLCMDLEN FIXED BIN(15,0),
     2 BLCMD CHAR(L REFER(BLCMDLEN)) VAR;
   DCL 1 REPBUF,
     2 BUFLEN FIXED BIN(15,0),
     2 OFFSET FIXED BIN(15,0),
     2 BF CHAR(250);
   DCL (PREFIX,SUFFIX) ENTRY(CHAR(*),CHAR(*))
     RETURNS(FIXED BIN(15,0));
   DCL SAVPTR PTR;
   DCL LAST FIXED BIN(31,0) INIT(99999999);
   RHRC=0;
   CMDDATA=SUBSTR(CMDDATA,PREFIX(CMDDATA,' '));/*CHOP LEAD BLANKS*/
   I=INDEX(CMDDATA,' ')+1; /* INDEX PAST CMD NAME PLUS 1 BLANK */
   IF I=1 × I>LENGTH(CMDDATA)
     THEN BUF='';
     ELSE BUF=SUBSTR(CMDDATA,I); /* CHOP CMD NAME PLUS 1 BL */
   SELECT(CMDINDX);
     WHEN(1,2) DO; /* IT IS NL SUBCOMMAND */
       SAVPTR=RECPTR;
       SEQ=-1;
       CALL #ADD(RHPTR,SUBENT,SAVPTR,SAVPTR,SEQ,BUF,RHPPTR);
       CALL #RESEQ(RHPTR,SUBENT,SAVPTR->LSTPTR,'NOSAVE','NORENUM',
         LAST,RHPPTR); /* CALL RESEQ */
       CALL #NC(RHPTR,SUBENT,SAVPTR); /* CALL NEWCUR */
       END;
     OTHERWISE DO; /* IT IS R SUBCOMMAND */
       IF BUF=''
         THEN RETURN; /* GET OUT - NOTHING TO DO */
       BUFLEN=MIN(254,LENGTH(BUF)+4);
       OFFSET=0;
       BF=BUF;
       CALL #REP(REPBUF); /* GO DO SYMBOLIC REPLACEMENT */
       BUF=SUBSTR(BF,1,BUFLEN-4);
       IF BUF^=''
         THEN DO; /* QUEUE NEW COMMAND SO IT IS DONE NEXT */
           L=LENGTH(BUF);
           ALLOCATE BLSTR;
           NBLPTR=BLHDR;
           BLHDR=BLPTR;
           BLCMD=BUF;
           END;
       END;
     END; /* SELECT */
   END;
