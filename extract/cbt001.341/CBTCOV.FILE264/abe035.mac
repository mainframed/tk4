   /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* ABE035 - SET,INCLUDE,SETRC SUBCOMMANDS */
   %INCLUDE ABESUB;
 ABESUB(ABE035) PARS(0000000000000000) CHECK(YES);
   %INCLUDE ABESUBA;
   DCL 1 LRN_STRUCTURE BASED(LRNPTR),
   %INCLUDE ABELRN;
   %INCLUDE ABELRNA;

   DCL PLIRETC BUILTIN;
   DCL SETCAPS ENTRY;
   DCL (SAVPTR,ORGPTR) PTR;
   DCL CMDLAB(4) LABEL;
   RHRC=0;
   GOTO CMDLAB(CMDINDX);
 CMDLAB(1): /* ABE035 */
 CMDLAB(2): /* SET */
   ORGPTR = RECPTR;
   IF PARY(1)='*'
     THEN DO;
       IF RECPTR=HDPTR
         THEN I=0;
         ELSE I=RECSEQ;
       END;
     ELSE DO;
       CALL SETCAPS(PARY(1));
       IF PARY(1)=SUBSTR('NEW',1,MIN(3,LENGTH(PARY(1))))
         THEN IF LSTHDR=HDPTR
           THEN I=1; /* WILL BE FIRST RECORD */
           ELSE I=LSTHDR->RECSEQ+SEQINCR;
         ELSE I = PARY(1); /* I=SEQUENCE NUMBER */
       END;
   CALL #MD(RHPTR,RHHDRPT,I);/*CALL MDPROC*/
   IF RECSEQ^=I
     THEN DO;
       /* WE MUST ALLOCATE A NEW RECORD */
       IF RECSEQ>I
         THEN SAVPTR=LSTPTR;
         ELSE SAVPTR=RECPTR;
       BUF='';
       SEQ=I;
       CALL #ADD(RHPTR,RHHDRPT,SAVPTR,SAVPTR,SEQ,BUF,0);
       RECPTR=SAVPTR;
       END;
   RECSTR=PARY(2); /* ASSIGN NEW DATA TO RECORD */
   IF ORGPTR = HDPTR
     THEN CALL #NC(RHPTR,NXTHDR);
     ELSE CALL #NC(RHPTR,ORGPTR);
   RHRC = 0;
   SAVEFLAG='1'B;
   RETURN;
 CMDLAB(3):; /* INCLUDE */
   DCL DDNAME CHAR(8) INIT('SYSLIB');
   DCL MEMNAME CHAR(8);
   % INCLUDE DCB;
   DCL REPSW BIT(1) INIT('0'B);
   DCL (SYSDCBP,SYSCLOS) ENTRY;
   DCL RET FIXED BIN(15,0);
   DCL SEQCNT FIXED BIN(15,0) STATIC INIT(8);
   DCL LAST FIXED BIN(31,0) INIT(99999999);
   ORGPTR,SAVPTR=RECPTR;
   IF RHPDSDCB=NULL
     THEN CALL SYSDCBP(RHPDSDCB,DDNAME);
   CALL SETCAPS(PARY(1));
   I=INDEX(PARY(1),'(');
   IF I=0
     THEN MEMNAME=PARY(1);
     ELSE DO;
       DDNAME=SUBSTR(PARY(1),1,I-1);
       J=INDEX(PARY(1),')');
       IF J=0
         THEN J=LENGTH(PARY(1))+1;
       MEMNAME=SUBSTR(PARY(1),I+1,J-I-1);
       END;
   DO I=2 TO HBOUND(PARY,1) WHILE(PARY(I)^='');
     CALL SETCAPS(PARY(I));
     IF PARY(I)='REP'
       THEN REPSW='1'B;
       ELSE IF VERIFY(PARY(I),'0123456789')=0
         THEN DO;
           J=PARY(I);
           IF J=0
             THEN CALL #NC(RHPTR,HDPTR);
             ELSE CALL #MD(RHPTR,RHHDRPT,J);
           SAVPTR=RECPTR;
           END;
         ELSE IF PARY(I)='LAST'
           THEN SAVPTR=LSTHDR;
           ELSE DO;
             MSGDATA='INVALID OPERAND='××PARY(2);
             RHRC=8;
             RETURN;
             END;
     END;
   RHPDSDCB->DCBDDNAM=DDNAME;
   CALL #ABEPIB(RHPDSDCB,MEMNAME,ALCEP,RET,SEQCNT);
   IF SUBSTR(RHPDSDCB->DCBFNDA.DCBOFLGS,4,1) /* WAS OPEN SUCCESSFUL? */
     THEN CALL SYSCLOS(RHPDSDCB); /* IF SO, CLOSE IT. */
   IF RET=0
     THEN RHRC=0;
     ELSE DO;
       RHRC=8;
       SELECT(RET);
         WHEN(8) MSGDATA='UNABLE TO OPEN '××DDNAME;
         OTHERWISE MSGDATA='UNABLE TO FIND MEMBER '××MEMNAME;
         END;
       END;
   CALL #RESEQ(RHPTR,RHHDRPT,SAVPTR->LSTPTR,'NOSAVE','NORENUM',LAST);
   IF ORGPTR=HDPTR
     THEN CALL #NC(RHPTR,NXTHDR);
     ELSE CALL #NC(RHPTR,ORGPTR);
   RETURN;

 ALCEP: PROC(PREC);
   DCL PREC CHAR(256) VAR;
   DCL 1 REPBUF,
     2 BUFLEN FIXED BIN(15,0),
     2 OFFSET FIXED BIN(15,0),
     2 BF CHAR(256);
   DCL SEQ FIXED BIN(31,0) INIT(-1);
   IF REPSW
     THEN DO;
       BUFLEN=MIN(260,LENGTH(PREC)+4);
       OFFSET=0;
       BF=PREC;
       CALL #REP(RHHDRPT,RH_SYMTREE,REPBUF); /* GO DO SYMBOLIC SUBS */
       BUF=SUBSTR(BF,1,BUFLEN-4);
       END;
     ELSE BUF=PREC;
   CALL #ADD(RHPTR,RHHDRPT,SAVPTR,SAVPTR,SEQ,BUF,0);
   END ALCEP;

 CMDLAB(4): /* SETRC */
   CALL PLIRETC(PARY(1));
   RETURN;
   END ABE035;
