*************************************************************$
*$.HE GENERALIZED SUBROUTINES - PUTLINE
*$.HE PUTLINE - INTERFACE TO TSO PUTLINE MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$03/15/76
*$AUTHOR:  T. SMITH
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*
*************************************************************$
*$THIS ROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE TSO PUTLINE MACRO.  IT CAN BE USED TO PREPARE A LINE AND
*$WRITE IT TO THE TERMINAL.  USE PUTLINE TO PUT OUT LINES THAT
*$DO NOT REQUIRE IMMEDIATE RESPONSE FROM THE TERMINAL; USE
*$PUTGET TO PUT OUT LINES THAT REQUIRE IMMEDIATE RESPONSE.  THE
*$THE TYPES OF LINES WHICH DO NOT REQUIRE RESPONSE FROM THE
*$TERMINAL ARE DEFINED AS DATA LINES AND INFORMATION MESSAGE
*$LINES.
*$.SP 1
*$.NF
*
*$CALL PUTLINE(CPPLUPT,CPPLECT,IOECB,OUTPUT,RETCODE,               *$
*$             'TERM' OR 'FORMAT',                                 *$
*$             'SINGLE' OR 'MULTLVL' OR 'MULTLIN',                 *$
*$             'INFOR' OR 'DATA'                                   *$
*$             'EDIT' OR 'ASIS' OR 'CONTROL',                      *$
*$             'WAIT' OR 'NOWAIT',                                 *$
*$             'NOHOLD' OR 'HOLD'                                  *$
*$             'NOBREAK' OR 'BREAKIN'                              *$
*$             'ENTRY',                                            *$
*$             FORMPTR                                             *$
*$             EPPTR;                                              *$
*$.SP 1
*$.FI L
*                                              *
*$NOTE - IN THE ABOVE FORMAT DESCRIPTION, THOSE CHARACTER LITERALS
*$SPECIFIED ON THE SAME LINE ARE MUTUALLY EXCLUSIVE, AND ONLY ONE
*$CAN BE SPECIFIED.  FOR A GIVEN GROUP,
*$THE FIRST ONE SPECIFIED ON THE LINE IS THE DEFAULT.
*                                              *
********************************************************$
*                                              *
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*                                              *
********************************************************$
*                                              *
*$.IN 0
*$'ASIS'
*$.IN 5
*$SPECIFIES THAT MINIMAL EDITING IS TO BE PERFORMED BY TPUT
*$.IN 0
*$'BREAKIN'
*$.IN 5
*$SPECIFIES THAT OUTPUT HAS PRECEDENCE OVER INPUT.  IF THE USER
*$THE TERMINAL IS TRANSMITTING, HE IS INTERRUPTED, AND THE
*$OUTPUT LINE IS SENT.  ANY DATA THAT WAS RECEIVED BEFORE
*$THE INTERRUPTION IS KEPT AND DISPLAYED AT THE TERMINAL
*$FOLLOWING THE OUTPUT LINE.
*$.IN 0
*$'CONTROL'
*$.IN 5
*$SPECIFIES THAT THE OUTPUT LINE IS COMPOSED OF TERMINAL CONTROL
*$CHARACTERS AND WILL NOT PRINT OR MOVE THE CARRIER
*$ON THE TERMINAL. THIS OPTIONS SHOULD BE USED FOR
*$TRANSMISSION OF CHARACTERS SUCH AS 'BYPASS',
*$'RESTORE', OR 'BELL RING'.
*$.IN 0
*$CPPLECT
*$.IN 5
*$POINTER VARIABLE SPECIFYING THE ADDRESS OF THE ENVIRONMENT
*$CONTROL TABLE (ECT).  YOU MAY OBTAIN THIS ADDRESS FROM THE
*$CPPL POINTED TO WHEN A COMMAND PROCESSOR IS ATTACHED
*$BY THE TERMINAL MONITOR PROGRAM.
*$.IN 0
*$CPPLUPT
*$.IN 5
*$POINTER VARIABLE SPECIFYING THE ADDRESS OF THE USER PROFILE
*$TABLE (UPT).  YOU MAY OBTAIN THIS ADDRESS FROM THE CPPL
*$POINTED TO WHEN A COMMAND PROCESSOR IS ATTACHED BY
*$THE TERMINAL MONITOR PROGRAM.
*$.IN 0
*$'DATA'
*$.IN 5
*$THE OUTPUT LINE IS A DATA LINE.
*$.IN 0
*$'EDIT'
*$.IN 5
*$SPECIFIES EDITING OVER AND ABOVE THE MINIMAL (ASIS).
*$.IN 0
*$'ENTRY'
*$.IN 5
*$SPECIFIES THAT EPPTR HAS BEEN SUPPLIED.
*$.IN 0
*$EPPTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS THE PUTLINE SERVICE
*$ROUTINE ACQUIRED BY PREVIOUSLY CALLING SYSLOAD USING
*$USING THE NAME IKJPTGT.
*$.IN 0
*$'FORMAT'
*$.IN 5
*$THE OUTPUT REQUEST IS ONLY TO FORMAT A SINGLE MESSAGE
*$AND NOT TO PUT THE MESSAGE OUT TO THE TERMINAL.  THE
*$PUTLINE INTERFACE RETURNS THE ADDRESS OF THE FORMATTED
*$LINE IN THE POINTER VARIABLE SPECIFIED BY FORMPTR.
*$.IN 0
*$FORMPTR
*$.IN 5
*$POINTER VARIABLE THAT SHOULD BE SPECIFIED ONLY IF
*$'FORMAT' WAS ALSO SPECIFIED.  THIS ROUTINE WILL PLACE
*$THE ADDRESS OF THE FORMATTED LINE HERE.
*$.IN 0
*$'HOLD'
*$.IN 5
*$SPECIFIES THAT THE MODULE THAT ISSUED THE PUTLINE
*$MACRO IS NOT TO RESUME PROCESSING UNTIL THE OUTPUT
*$LINE HAS BEEN PUT OUT TO THE TERMINAL OR DELETED.
*$.IN 0
*$'INFOR'
*$.IN 5
*$THE OUTPUT LINE IS AN INFORMATIONAL MESSAGE.
*$.IN 0
*$IOECB
*$.IN 5
*$SPECIFIES THE ADDRESS OF AN ECB.THIS ADDRESS WILL BE
*$PLACED INTO THE IOPL.
*$.IN 0
*$'MULTLIN'
*$.IN 5
*$THE OUTPUT DATA CONSISTS OF MULTIPLE LINES.  'DATA'
*$MUST BE SPECIFIED ALSO.
*$.IN 0
*$'MULTLVL'
*$.IN 5
*$THE OUTPUT MESSAGE CONSISTS OF MULTIPLE LEVELS.
*$INFOR MUST BE SPECIFIED.
*$.IN 0
*$'NOBREAK'
*$.IN 5
*$SPECIFIES THAT IF THE TERMINAL USER HAS STARTED
*$TO ENTER INPUT, HE IS NOT TO BE INTERRUPTED.
*$THE OUTPUT MESSAGE IS PLACED ON THE OUTPUT QUEUE
*$TO BE PRINTED AFTER THE TERMINAL USER HAS
*$COMPLETED THE LINE.
*$.IN 0
*$'NOHOLD'
*$.IN 5
*$SPECIFIES THAT CONTROL IS RETURNED TO THE ROUTINE
*$THAT ISSUED THE PUTLINE MACRO INSTRUCTION, AND IT CAN
*$CONTINUE PROCESSING, AS SOON AS THE OUTPUT LINE HAS BEEN
*$PLACED ON THE OUTPUT QUEUE.
*$.IN 0
*'NOWAIT'
*$.IN 5
*$SPECIFIES THAT CONTROL SHOULD BE RETURNED WHETHER
*$OR NOT A TERMINAL OUTPUT BUFFER IS AVAILABLE.  IF
*$NO BUFFER IS AVAILABLE, A RETURN CODE OF 8 IS
*$RETURNED IN RETCODE.
*$.IN 0
*$OUTPUT
*$.IN 5
*$ADDRESS OF A STRUCTURE CONTAINING THE FIRST OR
*$OR ONLY OUTPUT LINE, OR CONTAINING AN OUTPUT
*$LINE DESCRIPTOR (OLD).  THE TYPE OF LINE PROVIDED
*$AND THE PROCESSING TO BE PERFORMED ON THAT LINE
*$BY THE PUTLINESERVICE ROUTINE ARE DESCRIBED BY
*$THE OUTPUT SUBLIST OPERANDS 'TERM', 'FORMAT',
*$'SINGLE', 'MULTLBL', 'MULTLIN', 'INFOR', AND
*$'DATA'.  THE DEFAULT VALUES ARE 'TERM', 'SINGLE',
*$AND 'INFOR'.  THE OUTPUT DIFFERS DEPENDING UPON WHETHER
*$THE OUTPUT LINE IS AN INFORMATIONAL MESSAGE OR A DATA LINE.
*$FOR DATA REQUESTS, IT IS A STRUCTURE BEGINNING WITH THE
*$FULLWORD HEADER OF A DATA RECORD TO BE PUT OUT TO THE
*$TERMINAL.  FOR INFORMATION MESSAGE REQUESTS
*$(INFOR), IT IS A STRUCTURE CONTAINING AN OUTPUT LINE
*$DESCRIPTOR.  THE OUTPUT LINE DESCRITPOR (OLD) DESCRIBES
*$THE MESSAGE TO BE PUT OUT, AND CONTAINS THE ADDRESS OF
*$THE BEGINNING (THE FULL-WORD HEADER) OF THE MESSAGE OR
*$MESSAGES TO BE WRITTEN TO THE TERMINAL BY THE PUTLINE
*$SERVICE ROUTINE.
*$.IN 0
*$RETCODE
*$.IN 5
*$A FIXED BIN(31,0) VARIABLE TO CONTAIN THE RETURN CODE -
*$.IN 10
*$0 - PUTLINE COMPLETED NORMALLY.
*$.IN 10
*$4 - THE PUTLINE SERVICE ROUTINE DID NOT COMPLETE.
*$AN ATTENTION INTERRUPTION OCCURRED DURING ITS
*$EXECUTION, AND THE ATTENTION HANDLER TURNED
*$ON THE COMPLETION BIT IN THE COMMUNICATIONS ECB.
*$.IN 10
*$8 - THE NOWAIT OPTION WAS SPECIFIED, AND THE LINE
*$WAS NOT WRITTEN TO THE TERMINAL.
*$.IN 10
*$12 - INVALID PARAMETERS WERE SUPPLIED TO THE PUTLINE
*$SERVICE ROUTINE.
*$.IN 10
*$16 - A CONDITION GETAMIN WAS ISSUED BY PUTLINE
*$FOR OUTPUT BUFFERS AND THERE WAS NOT
*$SUFFICIENT REAL STORAGE TO SATISFY THE
*$REQUEST.
*$.IN 0
*$'SINGLE'
*$.IN 5
*$THE OUTPUT LINE IS A SINGLE LINE.
*$.IN 0
*$'TERM'
*$.IN 5
*$WRITE THE LINE OUT TO THE TERMINAL.
*$.IN 0
*$'WAIT'
*$.IN 5
*$SPECIFIES THAT CONTROL WILL NOT BE RETURNED UNTIL
*$THE OUTPUT LINE HAS BEEN PLACED INTO A TERMINAL
*$OUTPUT BUFFER.
*                                              *
******************************************************************$
PUTLINE  PLIENTRY ,DSA=152,COMPILE=&SYSPARM
         XC    ENTRYFLG(MYLEN),ENTRYFLG
         OI    PTPBCTLF,X'12'          SET DEFAULTS
         LR    2,1
         SR    0,0
LOOP1    DS 0H
         TM    0(2),X'80'              IS IT LAST PARM
         BC    1,ELOOP1                BR IF YES
         LA    2,4(2)                  BUMP R2
         BCT   0,LOOP1
ELOOP1   DS    0H
         LPR   0,0                     R0 = #PARMS-1
         CH    0,=H'14'                TOO MANY PARMS?
         BC    2,BADFORM               BR IF YES
         CH    0,=H'4'                 TOO FEW PARMS?
         BC    4,BADFORM               BR IF YES
         L     2,0(1)                  R2 = A(CPPLUPT)
         MVC   IOPLUPT,0(2)
         L     2,4(1)                  R2 = A(CPPLECT)
         MVC   IOPLECT,0(2)
         L     2,8(1)                  R2 = A(IOECB)
         XC    0(4,2),0(2)             CLEAR IOECB
         ST    2,IOPLECB
         L     2,12(1)                 R2 = A(SDV FOR OUTPUT)
         L     2,0(2)                  R2 = A(OUTPUT)
         ST    2,PTPBOPUT
         LA    2,PTPB
         ST    2,IOPLIOPB
         CH    0,=H'4'                 ANY MORE?
         BC    8,DEFAULT               NO, TAKE DEFAULTS
         SH    0,=H'5'                 R0 = # PARMS -6
         LA    2,20(1)
         SR    4,4
         LA    5,ENTRYFLG
         ST    5,WORKF
         B     LOOP2
LOOP2B   DS    0H
         LA    2,4(2)                  BUMP R2
LOOP2    DS    0H
         L     15,0(2)                 RF = A(SDV)
         L     15,0(15)                RF = A(STRING)
         LA    3,OPTAB
LOOP21   DS    0H
         CLI   0(3),X'FF'              IS IT END OF TABLE
         BC    8,BADFORM               BR IF YES
         IC    4,2(3)                  R4 = LEN(OPTION NAME) - 1
         EX    4,CLC
         BC    8,ELOOP21               BR IF OPTION FOUND
         LA    3,4(3,4)                BUMP R3 TO NEXT OPTAB ENTRY
         B     LOOP21                  GO TRY AGAIN
ELOOP21  DS    0H
         NI    WORKF+3,X'F8'
         OC    WORKF+3(1),0(3)
         L     5,WORKF                 R5 = A(PROPER BYTE IN PTPB)
         OC    0(1,5),1(3)             SET OPTION FLAGS
         LTR   0,0                     WAS THAT THE LAST PARM
         BC    8,ELOOP2                BR IF YES
         CH    0,=H'2'                 ARE THERE TWO PARAMETERS LEFT
         BC    8,NEXTLST               BR IF YES
LOOP2C   DS    0H
         BCT   0,LOOP2B                DECREMENT R0 AND BR BACK IF
*                                           NOT LAST
         TM    ENTRYFLG,X'80'          WAS 'ENTRY'SPECIFIED?
         BC    8,NOTENT                BR IF NOT
ISENT    DS    0H
         L     6,0(2)                  R6 = A(EPPTR)
         L     6,0(6)                  R6 = EPPTR
         B     ELOOP2
NOTENT   DS    0H
         TM    PTPBCTLF+1,X'20'        WAS ?FORMAT? SPECIFIED?
         BC    8,LOOP2B                BR IF NOT
         L     7,0(2)                  R7 = A(FORMPTR7
         B     ELOOP2
NEXTLST  DS    0H
         TM    PTPBCTLF+1,X'20'        WAS ' FORMAT' SPECIFIED?
         BC    8,LOOP2C                BR IF NOT
         TM    ENTRYFLG,X'80'          WAS 'ENTRY' SPECIFIED?
         BC    8,LOOP2C                BR IF NOT
         L     7,0(2)                  R7 = A(FORMPTR)
         LA    2,4(2)
         B     ISENT                   GO SET EPPTR IN R6
ELOOP2   DS 0H
         TM    PTPBCTLF,X'20'          WAS 'DATA' SPECIFIED?
         BC    8,NODATA                BR IF NOT
         NI    PTPBCTLF,X'FD'          MAKE SURE 'INFOR' NOT ON
NODATA   DS    0H
         TM    PTPBCTLF,X'08' WAS MULTLIN SPECIFIED?
         BC    8,NOMULT BR IF NOT
         NI    PTPBCTLF,X'EF' ELSE MAKE SURE 'SINGLE' NOT ON
NOMULT   DS    0H
         LA    2,MUTXTAB               R2 = A(MUTUALLY EXCLUSIVE TABLE)
         LA    3,PTPB
         ST    3,WORKF
         SR    4,4
LOOP3    DS 0H
         NI    WORKF+3,X'FC'
         OC    WORKF+3(1),0(2)
         L     3,WORKF                 R3 = A(BYTE IN PTPB FOR TM#1)
         IC    4,1(2)                  R4 = MASK FOR TM#1
         EX    4,TM                    EXECUT TM#1
         BC    8,LOOP3B                BR NOT ON
         NI    WORKF+3,X'FC'
         OC    WORKF+3(1),2(2)
         L     3,WORKF                 R3 = A(BYTE IN PTPB) FOR TM#2
         IC    4,3(2)                  R4 = MASK FOR TM#2
         EX    4,TM                    EXECUTE TM#2
         BC    7,BADFORM               BR IF MUTUALLY EXCLUSIVE
*                                           PARAMETERS WERE SPECIFIED
LOOP3B   DS    0H
         LA    2,4(2)                  BUMP TABLE INDEX
         CLI   0(2),X'FF'              IS IT END OF TABLE?
         BC    7,LOOP3                 BR IF NOT
DEFAULT DS 0H
         L     2,16(1)                 R2 = A(RETCODE)
         LA    1,IOPL
         TM    ENTRYFLG,X'80'          WAS 'ENTRY' SPECIFIED?
         BC    1,DOENTRY               BR IF YES
         LINK  EP=IKJPUTL
         B     PUTRET
DOENTRY  DS 0H
         LR    15,6
         BALR  14,15
PUTRET   DS    0H
         ST    15,0(2)                 SET RETCODE
         TM    PTPBCTLF+1,X'20'        WAS'FORMAT' SPECIFIED?
         BC    8,EXIT                  BR IF NOT
         MVC   0(4,7),PTPBFLN          MOVE A(FORMATTED LINE7 TO FORMPT
EXIT     PLIEXIT
BADFORM  DS    0H
         L     2,16(1)
         LA    15,12
         ST    15,0(2)
         B     EXIT
OPTAB    DS    0C
*                BYTE OFFSET DSECT FROM ENTRYFLG
*                      MASK FOR OR INSTRUCTION
*                            LENGTH OPTION NAME - 1
*                                  OPTION NAME
         DC    X'00',X'00',X'03',C'TERM'
         DC    X'05',X'20',X'05',C'FORMAT'
         DC    X'04',X'10',X'05',C'SINGLE'
         DC    X'04',X'04',X'06',C'MULTLVL'
         DC    X'04',X'08',X'06',C'MULTLIN'
         DC    X'04',X'02',X'04',C'INFOR'
         DC    X'04',X'20',X'03',C'DATA'
         DC    X'00',X'00',X'03',C'EDIT'
         DC    X'06',X'01',X'03',C'ASIS'
         DC    X'06',X'02',X'06',C'CONTROL'
         DC    X'00',X'00',X'03',C'WAIT'
         DC    X'06',X'10',X'05',C'NOWAIT'
         DC    X'00',X'00',X'05',C'NOHOLD'
         DC    X'06',X'08',X'03',C'HOLD'
         DC    X'00',X'00',X'06',C'NOBREAK'
         DC    X'06',X'04',X'06',C'BREAKIN'
         DC    X'00',X'80',X'04',C'ENTRY'
         DC    X'FF' END-OF-TABLE
MUTXTAB  DS    0C
* IF THE CONDITION CODE FROM THE 1ST TM INSTR IS NOT 0
* THEN IF THE CONDITION CODE FROM THE 2ND TM INSTR IS NOT 0
* THEN MUTUALLY EXCLUSIVE PARAMETERS HAVE BEEN SPECIFIED.
*                BYTE OFFSET IN PTPB FOR 1ST TM INSTR
*                      MASK FOR 1ST TM INSTR
*                            BYTE OFFSET IN PTPB FOR 2ND TM INSTR
*                                  MASK FOR 2ND TM INSTR
         DC    X'00',X'20',X'00',X'02' DATA/INFOR
         DC    X'00',X'10',X'00',X'0C' SINGLE/MULTLIN/MULTLVL
         DC    X'00',X'08',X'00',X'14'
         DC    X'01',X'20',X'00',X'0C' FORMAT/MULTLIN/MULTLVL
         DC    X'02',X'02',X'02',X'01' ASIS/CONTROL
         DC    X'FF' END-OF-TABLE
         DS 0H
TM       TM    0(3),X'00'
CLC      CLC   0(0,15),3(3)
DSA      DSECT
         DS    0D
ENTRYFLG DS    CL1 SET TO X'80' IF ENTRY SPECIFIED
         DS    CL3
PTPB     DS    0F
PTPBCTLF DS    CL2 CONTROL FLAGS
*              ..1. .... DATA
*              ...1 .... SINGLE
*              .... 1... MULTLIN
*              .... .1.. MULTLVL
*              .... ..1. INFOR
*              BYTE 2
*              ..1. .... FORMAT
PTPBTPUT DS    CL2 TPUT OPTIONS FIELD
*              ...1 .... NOWAIT
*              .... 1... HOLD
*              .... .1.. BREAKIN
*              .... ..1. CONTROL
*              .... ...1 ASIS
PTPBOPUT DS    F ADDR OF OUTPUT
PTPBFLN  DS    F ADDR OF FORMATTED LINE
IOPL     DS    0F
IOPLUPT  DS    F A(USER PROFILE TABLE)
IOPLECT  DS    F A(ENVIRONMENT CONTROL TABLE)
IOPLECB  DS    F A(ECB)
IOPLIOPB DS    F A(PTPB)
WORKF    DS    F
MYLEN    EQU   *-ENTRYFLG
DSALEN EQU *-DSA
         END
