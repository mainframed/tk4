*$.HE GENERALIZED SUBROUTINES - PLIMVCL
*$.HE EXECUTE A MVCL (MOVE CHARACTER LONG) INSTRUCTION - PLI
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/16/82
*$AUTHOR: L. SICARD
*$SOURCE MODULE NAME: PLIMVCL
*$DOCUMENTATION ID: *$
*$.SP 1
*$ENTRY DECLARATION: A.SD.A000.TEXTLIB(IPLIMVCL)
*$LOAD MODULE: SYS2.PLIBASE(PLIMVCL)
*$.FI L
*$.SP 3
*$THIS ROUTINE IS INTENDED FOR USE BY PL/I PROGRAMS ONLY. IT
*$PERFORMS A MOVE OF CHARACTER STRINGS OF LENGTH GREATER THAN
*$256 BYTES AT ONE TIME. THE ARGUMENTS ARE FULLWORDS WHICH ARE
*$TRANSFERED TO THIS SUBROUTINE AND LOADED INTO REGISTERS USED
*$IN THE MVCL INSTRUCTION. SEE HOW THE MVCL INSTRUCTION WORKS
*$IN THE "IBM SYSTEM/370 PRINCIPLES OF OPERATION" MANUAL. THE
*$REGISTERS 2 AND 3 ARE THE PAIR USED IN THE SECOND OPERAND OF
*$THE MVCL INSTRUCTION AND 4 AND 5 ARE THE PAIR USED IN THE
*$FIRST OPERAND.
*$.SP 1
*$.IN 4
*$CALL PLIMVCL(REGISTER2,REGISTER3,REGISTER4,REGISTER5);
*$.SP 1
*$.IN 0
*$REGISTER2
*$.IN 4
*$DECLARED POINTER - THIS FIELD CONTAINS THE ADDRESS OF
*$THE STORAGE TO BE MOVED.
*$.SP 1
*$.IN 0
*$REGISTER3
*$.IN 4
*$DECLARED FIXED BIN(31,0) - THIS FIELD CONTAINS THE LENGTH
*$OF THE STORAGE POINTED TO BY REGISTER2. THE HIGH ORDER BYTE
*$MAY CONTAIN A PADDING CHARACTER OTHER THAN THE X'00' VALUE
*$THAT IT WOULD CONTAIN FROM THE ASSIGNMENT OF A LENGTH IN THE
*$CALLING, PL/I PROGRAM.
*$.SP 1
*$.IN 0
*$REGISTER4
*$.IN 4
*$DECLARED POINTER - THIS FIELD CONTAINS THE ADDRESS OF
*$THE STORAGE THAT WILL BE CHANGED BY THE MOVE.
*$.SP 1
*$.IN 0
*$REGISTER5
*$.IN 4
*$DECLARED FIXED BIN(31,0) - THIS FIELD CONTAINS THE LENGTH
*$OF THE STORAGE POINTED TO BY REGISTER4.
*$.SP 1
*$.IN 0
*$NOTE:  NO CHECKS ARE MADE BY THIS SUBROUTINE.
PLIMVCL1 CSECT
         ENTRY PLIMVCL
         DC    C' PLIMVCL'
         DC    AL1(8)
PLIMVCL  DS    0H
         STM   14,11,12(13)
         BALR  10,0
         USING *,10
         LA    4,SAVE
         ST    13,SAVE+4
         ST    4,8(13)
         LA    13,SAVE
* START OF PLIMVCL ROUTINE
         L     2,0(1)        LOAD ADDR OF RECORD1 ADDR INTO R2
         L     2,0(2)        LOAD ADDR OF RECORD1 INTO R2
         L     3,4(1)        LOAD ADDR OF RECORD1 LENGTH ADDR INTO R3
         L     3,0(3)        LOAD ADDR OF RECORD1 LENGTH INTO R3
         L     4,8(1)        LOAD ADDR OF RECORD2 ADDR INTO R4
         L     4,0(4)        LOAD ADDR OF RECORD2 INTO R4
         L     5,12(1)       LOAD ADDR OF RECORD2 LENGTH ADDR INTO R5
         L     5,0(5)        LOAD ADDR OF RECORD2 LENGTH INTO R5
         MVCL  4,2           MOVE RECORD1 TO RECORD2
* END OF PLIMVCL ROUTINE
         L     13,4(13)
         LM    14,11,12(13)
         SR    15,15
         BR    14
SAVE     DC    10D'0'
         END
