 /* FILEPRNT */
 % DCL @PATH CHAR;
 % @PATH = 'B';
 /* NOTE THAT IF PREPROCESSOR VARIABLE @PATH IS SET TO 'B', THE NORMAL
    FILEPRNT BATCH UTILITY WILL BE GENERATED.  IF @PATH IS SET TO 'T'
    HOWEVER, THE TSODUMP COMMAND PROCESSOR WILL BE GENERATED.  TSODUMP
    MAY BE USED TO OBTAIN A HEXADECIMAL FILE PRNT AT THE TERMINAL.
    FORMAT FOR THE COMMAND IS AS FOLLOWS:

         TSODUMP 'DSN' PROMPT

    THE COMMAND EXPECTS SYSPRINT, WHICH IS WHERE THE HEX PRINT WILL BE
    DIRECTED, TO BE PREALLOCATED.  IF PROMPT IS NOT SPECIFIED, THE
    PROGRAM WILL DUMP THE FIRST 1000 RECORDS.  IF PROMPT IS SPECIFIED,
    THE PROGRAM EXPECTS SYSIN TO BE ALLOCATED TO A DATA SET CONTAINING
    PRINT CONTROL STATEMENTS (MAY BE THE TERMINAL) AS DESCRIBED IN THE
    DOCUMENTATION FOR THE FILEPRNT UTILITY.  NOTE THAT THE FILE PRINT
    CONTROL STATEMENT IS NOT ALLOWED.

0*$.HE GENERALIZED UTILITIES - FILEPRNT
0*$.HE FILEPRNT - GENERAL FILE PRINT PROGRAM
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$6/6/74
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  TGSGPRNT
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THE FILEPRNT UTILITY WILL PROVIDE A HEXADECIMAL/DECIMAL PRINTOUT
0*$OF SPECIFIED RECORDS FROM A FILE.  IF THE PARM FIELD OF THE
0*$EXEC STATEMENT IS PARM='/TSO', THE RECORDS WILL BE DUMPED OUT
0*$HEXADECIMALLY IN A FORMAT WHICH GIVES A PRINT LINE LENGTH
0*$LESS THAN 80 CHARACTERS.  OTHERWISE THE FORMAT WILL BE SUCH
0*$THAT THE PRINT LINE LENGTH WILL BE 132 CHARACTERS.
0*$CONSIDERABLE CONTROL IS PROVIDED
0*$OVER WHAT RECORDS ARE PRINTED VIA A SIMPLE CONTROL LANGUAGE
0*$DESCRIBED BELOW.
0*$.SP 1
0*$.IN 5
0*$.NF
0*$// EXEC PGM=FILEPRNT
0*$//STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR
0*$//SYSPRINT DD SYSOUT=A
0*$//DDNAME1 DD . . . DD STATEMENT
0*$          DESCRIBING 1ST FILE TO
0*$          BE PRINTED.
0*$ .
0*$ .
0*$ .
0*$//DDNAMEN DD . . . DD STATEMENT
0*$          DESCRIBING LAST FILE TO
0*$          BE PRINTED.
0*$//SYSIN DD *
0*$  (FILEPRNT CONTROL STATEMENTS
0*$  - SEE BELOW)
0*$/*
0*$.IN 0
0*$.FI L
0*$.SP 1
0*$ANY NUMBER OF FILES CAN BE PRINTED DURING ONE EXECUTION OF THE
0*$FILEPRNT UTILITY PROGRAM.  EACH FILE TO BE PRINTED IS DESCRIBED
0*$ON A SEPARATE DD STATEMENT HAVING A UNIQUE DDNAME OTHER THAN
0*$ONE OF THE FOLLOWING:
0*$.SP 1
0*$.CE 11
0*$SYSPRINT
0*$SYSIN
0*$JOBLIB
0*$PGM=*.DD
0*$SYSABEND
0*$SYSUDUMP
0*$PL1DUMP
0*$SYSOUT
0*$STEPLIB
0*$SYSLIN
0*$SYSLOUT
0*$.SP 1
0*$THE FOLLOWING CONTROL STATEMENTS MAY BE SPECIFED IN THE SYSIN
0*$DATA SET TO CONTROL WHAT RECORDS ARE TO BE PRINTED FROM EACH FILE:
0*$.SP 1
0*$FILE(DDNAME)
0*$.IN 5
0*$FILE IDENTIFIED BY THE SPECIFIED DDNAME.  THIS
0*$CONTROL STATEMENT IS OPTIONAL FOR THE FIRST
0*$OR ONLY FILE.  IF THE STANDARD DEFAULT FOR A
0*$FILE IS TO BE TAKEN (IN OTHER WORDS, NO OTHER
0*$CONTROL STATEMENTS ARE TO BE ENTERED FOR THE
0*$FILE), THIS STATEMENT MUST NOT BE ENTERED.  IF
0*$COMMANDS FOR MORE THAN ONE FILE ARE IN SYSIN,
0*$THEY MUST BE IN THE SAME ORDER AS THE
0*$ASSOCIATED DD STMTS.
0*$.IN 0
0*$SKIP(N)
0*$.IN 5
0*$SKIP N RECORDS.  IF, FOR EXAMPLE, THIRD
0*$RECORD ON A FILE HAD JUST BEEN READ AND
0*$PROCESSED WHEN THE COMMAND 'SKIP(6)', THE FILE
0*$WILL BE POSITIONED SO THAT THE NEXT RECORD READ
0*$WILL BE RECORD 10
0*$.IN 0
0*$SAMPLE(M,L)
0*$.IN 5
0*$PRINT L RECORDS SELECTING EVERY M-TH
0*$RECORD FROM THE FILE.  IF M IS NOT SPECIFIED,
0*$THE DEFAULT IS 100. IF L IS NOT SPECIFIED,
0*$1000 RECORDS WILL BE PRINTED.  IF YOU WANT THE
0*$SAMPLE TO CONTINUE UNTIL THE ENDFILE CONDITION
0*$IS RAISED SET L EQUAL TO 'ALL'.  IF THE WORD
0*$SAMPLE IS SPECIFIED WITHOUT A PARENTHESIS
0*$FOLLOWING, DEFAULTS ARE TAKEN FOR BOTH M AND L
0*$.IN 0
0*$RECORD(K)
0*$.IN 5
0*$SKIP TO RECORD NUMBER K.  THE WORDS
0*$'SKIP TO' OR 'SKIPTO' MAY BE USED IN PLACE OF
0*$'RECORD'.  FOR EXAMPLE, THE COMMAND RECORD(10)
0*$WOULD CAUSE THE FILE TO BE POSITIONED SO THAT
0*$RECORD 10 WOULD BE THE NEXT RECORD READ.
0*$.IN 0
0*$PRINT(L)
0*$.IN 5
0*$PRINT L RECORDS FROM A FILE.  IF ALL
0*$RECORDS ARE TO BE PRINTED, SPECIFY 'ALL'.
0*$IF (L) IS NOT SPECIFIED, THE DEFAULT IS 1000
0*$.IN 0
0*$.SP 1
0*$.CE 1
0*$EXAMPLE
0*$.SP 1
0*$FOR EXAMPLE, IF THE FOLLOWING CONTROL STATEMENTS WERE
0*$ENCOUNTERED IN THE SYSIN DATA SET, RECORDS 5, 6, 7, 8, 9, 10,
0*$111, 112, 113, 163, 213, 263, ETC. FROM THE FIRST FILE
0*$ENCOUNTERED WOULD BE PRINTED ON SYSPRINT -
0*$.SP 1
0*$.IN 5
0*$.NF
0*$SKIP(4)
0*$PRINT(6)
0*$SKIP(100)
0*$PRINT(3)
0*$SAMPLE(50)
0*$.SP 1
0*$.IN 0
0*$.FI L
0*$A SYSIN FILE NEED NOT BE SUPPLIED.  THE STANDARD DEFAULT
0*$IS PRINT(1000).
0*$.SP 1
0*$CURRENT VERSION WILL PROCESS ANY FILE ORGANIZATION
0*$SUPPORTED BY THE PLI(F) COMPILER -
0*$.SP 1
0*$.IN 5
0*$.NF
0*$CONSECUTIVE
0*$INDEXED
0*$REGIONAL(1) - DIRECT
0*$REGIONAL(2) - DIRECT KEYED
0*$REGIONAL(3) - DIRECT KEYED
0*$.SP 1
0*$.IN 0
0*$.FI L
0*$NOTE - FOR A DISK FILE, YOU MAY NOT USE DEFERRED
0*$MOUNT
 *****************************************************************/
 % IF @PATH = 'T'
      % THEN % DO;
         % INCLUDE CPINIT1;
         % @CMP='O';
         % INCLUDE CPINIT2;
         @PROC(1,PRDSN#(DSNAME),PROMPT);
         IF PROMPT^=0
              THEN SYSINSW = '1'B;
         @SETDSN(PRDSN,,PR);
         @ALLOC(SHR);
         DCL DDNAME CHAR(8);
         DDNAME = DA08DDN;
         DCL TPASW BIT(1) INIT('1'B);
         DCL CVTPTR PTR BASED(CVTPTRPTR);
         DCL CVTPTRPTR PTR,
             CVTADJ FIXED BIN(31,0) DEF CVTPTRPTR;
         CVTADJ = 16;
         DCL IEATCBPTR PTR BASED(CVTPTR);
         DCL 1 IEATCBP BASED(IEATCBPTR),
              2 PAD CHAR(4),
              2 TCBPTR PTR;
   % END;
   % ELSE % DO;
 GPRNT: PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
         DCL ((NXFIRST, NXLAST) BIT(1),
              DDCARD CHAR (150) VARYING,
              DDNAME CHAR(8),
              TYPE BIT(32),
              TCBPTR PTR,
              XTCBDEB PTR) EXTERNAL;
         DCL (LRECL, BLKSIZE, KEYLEN, RKP) PIC '9999';
         DCL (RECFM,DSORG) CHAR(3) VARYING;
         DCL RFM(8) BIT(1) UNALIGNED BASED(RPTR);
         DCL BSAM(4) CHAR(1) INIT('B','S','A','M');
         DCL DSRG(3) CHAR(2) INIT('IS','PS','DA');
         DCL DD2 CHAR(150) VARYING;
         DCL ROMAN ENTRY (FIXED BIN) RETURNS (CHAR(15) VARYING);
         DCL DAY CHAR(8) EXTERNAL;
         DAY = DATE;
         DCL PAGE FIXED BIN INIT (0B);
 ON ENDPAGE(SYSPRINT) BEGIN;
         PUT PAGE;
         PAGE = PAGE + 1;
         PUT EDIT ('PAGE ',ROMAN(PAGE),'GENERAL FILE PRINT','DATE=',
              DAY) (A,A,COL(50),A,COL(110),A,A);
         PUT EDIT(DDCARD)(A) SKIP(2);
         PUT SKIP;
         PUT EDIT(DD2) (A);
         PUT SKIP(3);
         END;
         OPEN FILE(SYSPRINT) LINESIZE(132);
         NXFIRST = '1'B;
         NXLAST = '0'B;
         DO WHILE (NXLAST = '0'B);
              CALL D10201C;
              IF DDNAME = 'SYSIN' THEN SYSINSW = '1'B;
              END;
         NXFIRST = '1'B;
         NXLAST = '0'B;
 % END;
         DCL OFFSET FIXED BIN(15,0),
             OFFSETB BIT(16) DEF OFFSET;
         DCL TIKPTR PTR,
             TIKADJ FIXED BIN(31,0) DEF TIKPTR;
         DCL TIKSTR CHAR(8) BASED(TIKPTR);
         DCL DCBL(5) LABEL;
         DCL RTSAV FIXED BIN(15,0);
         DCL RTCOD FIXED BIN(15,0) INIT (1B);
         DCL 1 TCB BASED(TCBPTR),
              2 PAD CHAR(8),
              2 TCBDEB PTR,
              2 TCBTIO PTR;
         DCL (DCBIS ENV(INDEXED),
              DCBPS ENV(CONSECUTIVE),
              DCBDA1 ENV(REGIONAL(1)),
              DCBDA2 ENV(REGIONAL(2))) RECORD INPUT SEQUENTIAL;
         DCL RECORD CHAR(LEN) VARYING CONTROLLED;
         DCL CTL CHAR(80);
         DCL CTLA(80) CHAR(1) DEF CTL;
         DCL INDDN CHAR(8) INIT(' ');
         DCL CTLKEY(7) CHAR(7) INIT('SAMPLE','RECORD','PRINT','FILE',
           'SKIPTO','SKIP TO','SKIP');
         DCL CTLKLN(7) FIXED BIN(15,0) INIT(6,6,5,4,6,7,4);
         % INCLUDE JFCB;
         DCL JFCBCOR CHAR(176) EXT;
         JFCBPTR = ADDR(JFCBCOR);
         DCL DEBPTR PTR,
             DEBADJ FIXED BIN(31,0) DEF DEBPTR;
         % INCLUDE DEB;
         DCL DCBPTR PTR,
             DCBADJ FIXED BIN(31,0) DEF DCBPTR;
         % INCLUDE DCB;
         % INCLUDE DCBQSAM;
         DCL RET BIT(32);
         DCL WKAREA CHAR(148);
         DCL DSCB1PTR PTR,
             DSCB1ADJ FIXED BIN(31,0) DEF DSCB1PTR;
         DSCB1PTR = ADDR(WKAREA);
         DSCB1ADJ = DSCB1ADJ - 44;
         % INCLUDE DSCB1;
         DCL (I,J,LEN,NUMREC) FIXED BIN(31,0);
         DCL (EOFSW,SYSINSW,FIRSTSW) BIT(1) INIT('0'B);
         DCL (K,L,M,N) FIXED BIN(31,0);
         CALL GETIN;
         GO TO DCBL(IDCB);
 DCBL(1):
         CALL DOFILE(DCBIS);
         GO TO DCBL(IDCB);
 DCBL(2):
         CALL DOFILE(DCBPS);
         GO TO DCBL(IDCB);
 DCBL(3):
         CALL DOFILE(DCBDA1);
         GO TO DCBL(IDCB);
 DCBL(4):
         CALL DOFILE(DCBDA2);
         GO TO DCBL(IDCB);
 DCBL(5):
         % IF @PATH = 'T'
              % THEN % DO;
                   DA18DDN = DDNAME;
                   @FREE(FI);
                   % END;
         RETURN;
 DOFILE: PROC(INPUT);
         DCL CTLL(7) LABEL;
         DCL INPUT FILE;
         ON ENDFILE(INPUT) GO TO ENDFILE;
         OPEN FILE(INPUT) TITLE(DDNAME);
         DEBPTR=TCBDEB;
         DO WHILE (DEBADJ ^= 0);
              DCBADJ = DEBDCBAD;
              OFFSETB = DCBTIOT;
              TIKPTR=TCBTIO;
              TIKADJ=TIKADJ+OFFSET+4;
              IF TIKSTR = DDNAME
                   THEN GO TO GOTDCB;
              DEBADJ = DEBDEBAD;
              END;
 GOTDCB:
         IF SUBSTR(DCBRECFM,1,2) = '11'B
              THEN LEN = DCBBLKSI;
              ELSE LEN = DCBLRECL;
         ALLOCATE RECORD;
         EOFSW = '0'B;
         NUMREC = 0;
 DOLOOP:
         CALL GETIN;
         GO TO CTLL(ICTL);
 CTLL(7):   /* SKIP(N) */
         NUMREC = NUMREC + N;
         READ FILE(INPUT) IGNORE(N);
         GO TO DOLOOP;
 CTLL(1):   /* SAMPLE(M,L) */
         DO I = 1 TO L;
              NUMREC = NUMREC + 1;
              READ FILE(INPUT) INTO(RECORD);
              PUT EDIT(NUMREC) (P'ZZZZZ9') SKIP(1);
              % IF @PATH = 'T'
                  % THEN % DO;
              CALL DUMPSTT(RECORD);
              PUT EDIT(' ')(A) SKIP(1);
                       % END;
                  % ELSE % DO;
             IF PARM='TSO'
               THEN CALL DUMPSTT(RECORD);
               ELSE CALL DUMPSTR(RECORD);
                       % END;
              NUMREC = NUMREC + M - 1;
              READ FILE(INPUT) IGNORE(M-1);
              END;
         GO TO DOLOOP;
 CTLL(2): CTLL(5): CTLL(6): /* RECORD(K) SKIPTO(K), SKIP TO (K) */
         I = K-NUMREC-1;
         IF I <= 0 THEN GO TO DOLOOP;
         NUMREC = K-1;
         READ FILE(INPUT) IGNORE(I);
         GO TO DOLOOP;
 CTLL(3):   /* PRINT(L) */
         DO I = 1 TO L;
              NUMREC = NUMREC + 1;
              READ FILE(INPUT) INTO(RECORD);
              PUT EDIT(NUMREC) (P'ZZZZZ9') SKIP(1);
              % IF @PATH = 'T'
                  % THEN % DO;
              CALL DUMPSTT(RECORD);
              PUT EDIT(' ')(A) SKIP(1);
                       % END;
                  % ELSE % DO;
              CALL DUMPSTR(RECORD);
                       % END;
              END;
         GO TO DOLOOP;
 CTLL(4):   /* FILE(DDNAME) */
         CLOSE FILE(INPUT);
         EOFSW = '0'B;
         FREE RECORD;
         RETURN;
 ENDFILE:
         EOFSW = '1'B;
         PUT EDIT ('ENDFILE') (A) SKIP(1);
         PUT SKIP;
         % IF @PATH = 'T'
              % THEN % DO;
         IDCB = 5;
         RETURN;
                     % END;
              % ELSE % DO;
         GO TO DOLOOP;
                     % END;
         END DOFILE;
 GETIN: PROC;
         ON ENDFILE(SYSIN) GO TO EOFIN;
         DCL   (RTE(5),CTLL2(7)) LABEL;
         DCL RETURN LABEL STATIC;
 GETINA:
         GO TO RTE(RTCOD);
 RTE(2):
         ICTL = 3;
         L = 1000;
         RTCOD=3;
         RETURN;
 RTE(3):
         ICTL = 4;
         RTCOD=1;
         GO TO PF;
 RTE(1):
         IF SYSINSW = '0'B THEN GO TO PF;
         % IF @PATH = 'T'
             % THEN % DO;
                @WRITE('ENTER NEXT PRINT CONTROL STATEMENT');
                % END;
         READ FILE(SYSIN) INTO(CTL);
         CALL SETCAPS(CTL);
         IF CTL = ''
              THEN GO TO EOFIN;
 RTE(5):
         RTCOD = 1;
         IF FIRSTSW ^= '0'B THEN PUT EDIT (CTL) (A) SKIP(3);
         DO I = 1 TO 80;
              IF CTLA(I) ^= ' ' THEN GO TO GOTFIRST;
              END;
 REJECT:
         IF FIRSTSW = '0'B THEN PUT EDIT (CTL) (A) SKIP(3);
         % IF @PATH = 'T'
              % THEN %DO;
                   @WRITE('REJECTED');
                   % END;
              % ELSE % DO;
         PUT EDIT ('REJECTED')(COL(5), A);
                   % END;
         PUT SKIP;
         GO TO GETINA;
 GOTFIRST:
         DO ICTL = 1 TO 7;
              IF CTLKEY(ICTL) = SUBSTR(CTL,I,(CTLKLN(ICTL)))
                   THEN GO TO GOTKEY;
              END;
         GO TO REJECT;
 GOTKEY:
         IF FIRSTSW = '0'B THEN DO;
              FIRSTSW = '1'B;
              IF ICTL = 4 THEN GO TO RTE(5);
              RTCOD = 5;
              GO TO PF;
              END;
         I = INDEX(CTL,'(');
         L = 1000;
         M = 100;
         IF I = 0
              THEN IF ICTL = 1 × ICTL = 3
                   THEN RETURN;
                   ELSE GO TO REJECT;
         I = I + 1;
         DO I = I TO 80;
              IF CTLA(I) ^= ' ' THEN GO TO GOTSECND;
              END;
         GO TO REJECT;
 GOTSECND:
         IF CTLA(I) = ','
              THEN IF ICTL = 1 THEN DO;
              J = I + 1;
                   GO TO GOTTHIRDA;
                   END;
         DO J = I TO (I+9) WHILE (I <= 80);
              IF (CTLA(J) = ' ') × (CTLA(J) = ',') × (CTLA(J) = ')')
                   THEN GO TO ENDNUM1;
                   IF (ICTL ^= 4 & ICTL ^= 3)
                        THEN IF (CTLA(J) < '0') × (CTLA(J) > '9')
                   THEN GO TO REJECT;
              END;
         GO TO REJECT;
 ENDNUM1:
         IF ICTL ^= 4 THEN IF EOFSW = '1'B THEN GO TO REJECT;
         % IF @PATH = 'B'
              % THEN % DO;
         IF NXLAST = '1'B THEN GO TO REJECT;
                     % END;
         GO TO CTLL2(ICTL);
 CTLL2(7):  /* SKIP(N) */
         N = SUBSTR(CTL,I,(J-I));
         RETURN;
 CTLL2(1):  /* SAMPLE(M,L) */
         M = SUBSTR(CTL,I,(J-I));
         RETURN = GOTTHIRD;
 BLP:
         DO J = J TO 80;
              IF CTLA(J) ^= ' ' THEN GO TO RETURN;
              END;
         GO TO REJECT;
 GOTTHIRD:
         IF CTLA(J) = ')' THEN RETURN;
         IF CTLA(J) ^= ',' THEN GO TO REJECT;
         J = J + 1;
 GOTTHIRDA:
         RETURN = GOTFORTH;
         GO TO BLP;
 GOTFORTH:
         IF SUBSTR(CTL,J,3) = 'ALL' THEN DO;
              L = 200000000;
              RETURN;
              END;
         DO I = J TO (J+10) WHILE (J <= 80);
              IF ((CTLA(I) < '0') × (CTLA(I) > '9'))
                   THEN IF ((CTLA(I) ^= ' ') & (CTLA(I) ^= ')'))
                        THEN GO TO REJECT;
                        ELSE GO TO GOTFITH;
              END;
         GO TO REJECT;
 GOTFITH:
         L = SUBSTR(CTL,J,(I-J));
         RETURN;
 CTLL2(2): CTLL2(5): CTLL2(6): /* RECORD(K), SKIPTO(K), SKIP TO(K)*/
         K = SUBSTR(CTL,I,(J-I));
         RETURN;
 CTLL2(3):  /* PRINT(L) */
         IF 3 = (J-I) THEN IF SUBSTR(CTL,I,(J-I)) = 'ALL'
              THEN DO;
                   L = 200000000;
                   RETURN;
                   END;
         L = SUBSTR(CTL,I,(J-I));
         RETURN;
 CTLL2(4):
         % IF @PATH = 'T'
              % THEN % DO;
                   GO TO REJECT;
                   % END;
         INDDN = SUBSTR(CTL,I, (J-I));
 PF:
         % IF @PATH = 'B'
              % THEN % DO;
         CALL NEXTDD;
         OFFSETB = OFFSET;
         IF NXLAST = '1'B
              THEN IF SYSINSW = '1'B
              % END;
         % ELSE % DO;
         IF TPASW
              THEN TPASW = '0'B;
              ELSE IF SYSINSW = '1'B
              % END;
                   THEN GO TO REJECT;
                   ELSE DO;
                        IDCB = 5;
                        ICTL = 4;
                        RETURN;
                        END;
         IF (RTCOD = 5) × (DDNAME = INDDN) THEN GO TO PFA;
         RTCOD = 2;
 PFA:
         % IF @PATH = 'B'
              % THEN % DO;
         RECFM, DSORG = ' ';
         LRECL, BLKSIZE, KEYLEN, RKP = 0;
              % END;
         CALL SYSJFCB(DDNAME, JFCBPTR);
         % IF @PATH = 'B'
              % THEN % DO;
         DDCARD = DDCARD ×× ',DSN=' ××
              SUBSTR(JFCBDSNM,1,((INDEX(JFCBDSNM,' ')-1))) ×× ',';
         IF SUBSTR(TYPE,17,8) = '00100000'B THEN GO TO DA;
         RTSAV = RTCOD;
         RTCOD = 4;
         IDCB = 2;
         RETURN;
 RTE(4):
 TAPE:
         RTCOD = RTSAV;
         DSORG = 'PS';
         RETURN = DCBA;
         RPTR = ADDR(DCBRECFM);
         GO TO GETFRM;
 DCBA:
         BLKSIZE = DCBBLKSI;
         LRECL = DCBLRECL;
         RETURN = RTE(RTCOD);
         GO TO SIGFORM;
 DA:
              % END;
         CALL SYSVTOC('SEARCH',RET,JFCBDSNM,JFCBVOLS(1),WKAREA);
         % IF @PATH = 'B'
              % THEN % DO;
         RPTR = ADDR(DS1RECFM);
         RETURN = DAR1;
         GO TO GETFRM;
 DAR1:
              % END;
         DO IDCB = 1 TO 3;
              IF SUBSTR(DS1DSORG,IDCB,1) = '1'B THEN GO TO GOTDSORG;
              END;
         IDCB = 2;
         % IF @PATH = 'B'
              % THEN % DO;
         DSORG = 'PO';
              % END;
         GO TO GOTDSORGA;
 GOTDSORG:
         % IF @PATH = 'B'
              % THEN % DO;
         DSORG = DSRG(IDCB);
              % END;
 GOTDSORGA:
         IF IDCB = 3 THEN IF DS1KEYL ^= 0 THEN IDCB = 4;
                                          ELSE IDCB = 2;
         % IF @PATH = 'T'
              % THEN % DO;
                   RETURN;
                   % END;
              % ELSE % DO;
         LRECL = DS1LRECL;
         BLKSIZE = DS1BLKL;
         KEYLEN = DS1KEYL;
         RKP = DS1RKP;
         RETURN = DAR2;
         GO TO SIGFORM;
 DAR2:
         RETURN;
 GETFRM:
         IF RFM(1) = '1'B
              THEN IF RFM(2) = '1'B
                   THEN RECFM = 'U';
                   ELSE RECFM = 'F';
              ELSE RECFM = 'V';
         DO I = 1 TO 4;
              IF RFM(I+3) = '1'B THEN RECFM = RECFM ×× BSAM(I);
              END;
         GO TO RETURN;
 SIGFORM:
         DD2 = '// DCB=(DSORG=' ×× DSORG ×× ',RECFM='
              ×× RECFM ×× ',BLKSIZE=';
         IF BLKSIZE=0 THEN DD2 = DD2 ×× '0';
                        ELSE DD2 = DD2
         ×× SUBSTR(BLKSIZE,5-LOG10(BLKSIZE+1));
         IF RECFM ^= 'U' THEN DO;
              DD2 = DD2 ×× ',LRECL=';
              IF LRECL = 0 THEN DO;
                   DD2 = DD2 ×× '0';
                   END;
                         ELSE DD2 = DD2
         ×× SUBSTR(LRECL,5-LOG10(LRECL+1));
              END;
                         ELSE LEN = BLKSIZE;
         IF KEYLEN ^= 0 THEN DO;
              DD2 = DD2 ×× ',KEYLEN='
         ×× SUBSTR(KEYLEN,5-LOG10(KEYLEN+1)) ×× 'RKP=';
         IF RKP = 0 THEN DD2 = DD2 ×× '0';
                    ELSE DD2 = DD2
         ×× SUBSTR(RKP,5-LOG10(RKP+1));
         END;
         DD2 = DD2 ×× ')';
         SIGNAL ENDPAGE(SYSPRINT);
         GO TO RETURN;
              % END;
 EOFIN:
         CLOSE FILE(SYSIN);
         SYSINSW = '0'B;
         GO TO PF;
         END GETIN;
         END;
