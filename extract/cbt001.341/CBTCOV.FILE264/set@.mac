 /* SET@ */
 /*
0*$.HE GENERALIZED SUBROUTINES - SET@
0*$.HE SET SYSTEM VARIABLE - (&NAME)
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/10/76
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  SET@
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS PROCEDURE CAN BE INVOKED TO SET THE VALUE FOR EITHER A LOCAL OR
0*$GLOBAL VARIABLE.  THESE ARE VARIABLES WHICH HAVE NAMES 8 CHARACTERS
0*$OR LESS IN LENGTH, THE FIRST CHARACTER BEING AN AMPERSAND (&), AND
0*$ALL SUBSEQUENT CHARACTERS BEING ALPHANUMERIC.  SUCH VARIABLES CAN BE
0*$SPRINKLED ABOUT IN A SPECIFIED CHARACTER STRING, AND VIA
0*$A CALL TO SUBROUTINE REP@ (SEE DOCUMENATION ELSEWHERE), CAN BE
0*$REPLACED WITH THE VALUES THEY REPRESENT.  CALL FORMAT IS AS FOLLOWS -
0*$.SP 1
0*$.IN 5
0*$CALL SET@(GBLSW,SYMBOLPM,TEXTPM);
0*$.IN 0
0*$.SP 1
0*$GBLSW
0*$.IN 5
0*$IS BIT(1), AND IS SET TO '1'B IF A GLOBAL VARIABLE IS TO BE SET.
0*$.IN 0
0*$SYMBOLPM
0*$.IN 5
0*$IS A CHARACTER STRING 2 TO 8 BYTES LONG CONTAINING THE NAME OF THE
0*$VARIABLE TO BE SET (FOR EXAMPLE, &USER).  NOTE THAT IF THE VARIABLE
0*$DOES NOT ALREADY EXIST, IT WILL BE CREATED AUTOMATICALLY BEFORE
0*$BEING ASSIGNED THE VALUE.
0*$.IN 0
0*$TEXTPM
0*$.IN 5
0*$IS A VARYING CHARACTER STRING 0 TO 256 BYTES LONG CONTAINING
0*$THE TEXT TO BE ASSIGNED TO THE SYSTEM VARIABLE.
0*$.IN 0
 */
 SET@:  PROC(GBLSW,SYMBOLPM,TEXTPM) OPTIONS(REENTRANT);
         DCL GBLSW BIT(1);
              /* = '1'B IF GLOBAL IS TO BE SET; */
         DCL SYMBOLPM CHAR(8);
         DCL TEXTPM CHAR(256) VARYING;
         DCL SYMHD@ PTR EXT;
         DCL 1 CPPL EXT,
              2 CPPLCBUF PTR,
              2 CPPLUPT PTR,
              2 CPPLPSCB PTR,
              2 CPPLECT PTR;
         DCL NULL BUILTIN;
         % INCLUDE IKJECT;
         DCL MYHD PTR,
             MYHDC CHAR(4) DEF MYHD,
             MYHDD FIXED BIN(31,0);
         DCL AVCBPTR PTR,
             AVCBPTC CHAR(4) DEF AVCBPTR,
             AVCBPTD FIXED BIN(31,0) DEF AVCBPTR;
         DCL GLAREA AREA(1000) BASED(AVCBPTR);
         IF GBLSW
              THEN DO;
                   MYHDC = SUBSTR(ECTUSER,5,4);
                   AVCBPTD = MYHDD+4;
                   END;
              ELSE MYHD = SYMHD@;
         DCL 1 SYMTAB BASED(SYMPTR),
              2 NXTPTR PTR,
              2 LSTPTR PTR,
              2 SYMBOL CHAR(8),
              2 MAXLEN FIXED BIN(15,0),
              2 CURLEN FIXED BIN(15,0),
              2 TEXT CHAR(TXTLEN REFER(MAXLEN));
         DCL TXTLEN FIXED BIN(15,0);
         DCL (SYMPTRA,SYMPTRB) PTR;
         TXTLEN = LENGTH(TEXTPM);
         SYMPTRA = NULL;
         SYMPTR = MYHD;
         DO WHILE(SYMPTR ^= NULL);
              IF SYMBOL = SYMBOLPM
                   THEN DO;
                        IF TXTLEN > MAXLEN
                             THEN DO;
                                  SYMPTRA = SYMPTR;
                                  IF GBLSW
                                       THEN ALLOC SYMTAB IN(GLAREA)
                                         SET(SYMPTR);
                                       ELSE ALLOC SYMTAB SET(SYMPTR);
                                  NXTPTR = SYMPTRA->NXTPTR;
                                  LSTPTR = SYMPTRA->LSTPTR;
                                  SYMBOL = SYMPTRA->SYMBOL;
                                  IF GBLSW
                                       THEN FREE SYMPTRA->SYMTAB
                                         IN(GLAREA);
                                       ELSE FREE SYMPTRA->SYMTAB;
                                  CURLEN = TXTLEN;
                                  GO TO ALLOCA;
                                  END;
                        TEXT = TEXTPM;
                        CURLEN = TXTLEN;
                        RETURN;
                        END;
              IF SYMBOL > SYMBOLPM
                   THEN GO TO ALLOC;
              SYMPTRA = SYMPTR;
              SYMPTR = NXTPTR;
              END;
 ALLOC:
         SYMPTRB = SYMPTR;
         IF GBLSW
             THEN DO;
                  ALLOC SYMTAB IN(GLAREA) SET(SYMPTR);
                  END;
             ELSE DO;
                  ALLOC SYMTAB SET(SYMPTR);
                  END;
         NXTPTR = SYMPTRB;
         LSTPTR = SYMPTRA;
         SYMBOL = SYMBOLPM;
         CURLEN = MAXLEN;
 ALLOCA:
         TEXT = TEXTPM;
         IF NXTPTR ^= NULL
              THEN NXTPTR->LSTPTR = SYMPTR;
         IF LSTPTR ^= NULL
              THEN LSTPTR->NXTPTR = SYMPTR;
              ELSE DO;
                   MYHD = SYMPTR;
                   IF GBLSW
                        THEN SUBSTR(ECTUSER,5,4)=MYHDC;
                        ELSE SYMHD@ = SYMPTR;
                   END;
         END;
