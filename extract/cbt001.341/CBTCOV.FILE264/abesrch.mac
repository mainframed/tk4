 /* COPYRIGHT 1988 BY THOMAS GLEN SMITH */
 /**********************************************************************
 * CALLED TO SEARCH A BINARY TREE.  RETURNS A PTR TO THE ELEMENT, OR   *
 * NULL.  THE THREE PARAMETERS ARE -                                   *
 *   T - HEAD OF TREE POINTER.                                         *
 *   FNAME - KEY TO BE SEARCHED FOR.                                   *
 *   Q - PTR TO BE SET BY ABESRCH WITH PARENT ADDR.                    *
 **********************************************************************/
   %INCLUDE ABESUB;
 ABESUB(ABESRCH) PARMS(T,FNAME,Q) RETURNS(PTR);
   %INCLUDE ABESUBA;
   %INCLUDE ABESREC;

   DCL FNAME CHAR(8);
   DCL (P,Q,T) PTR;

   Q=NULL;
   IF T=NULL
     THEN RETURN(NULL);
   P=T;
   DO WHILE(P^=NULL); /* SEARCH T FOR INSERTION POINT FOR X */
     SELECT;
       WHEN(FNAME<P->SRNAME) DO; /* TAKE LEFT BRANCH */
         Q=P; /* SAVE PARENT POINTER */
         P=P->SRLCHILD;
         END;
       WHEN(FNAME>P->SRNAME) DO; /* TAKE RIGHT BRANCH */
         Q=P; /* SAVE PARENT POINTER */
         P=P->SRRCHILD;
         END;
       OTHERWISE RETURN(P); /* FOUND IT */
       END; /* SELECT */
     END;
   RETURN(P);
   END ABESRCH;
