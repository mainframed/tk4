 ARCHSIZ:  PROC OPTIONS(MAIN);
         DCL JFCBAREA CHAR(176);
         JFCBPTR = ADDR(JFCBAREA);
         DCL DEVAREA CHAR(24);
         DEVDPTR = ADDR(DEVAREA);
         DCL (I,J) FIXED BIN(31,0);
         DCL RETCODE FIXED BIN(31,0);
         DCL DDNAME CHAR(8) INIT('ALLOC');
         DCL INREC CHAR(32767) VARYING;
         DCL WRKPTR PTR,
             WRKADJ FIXED BIN(31,0) DEF WRKPTR;
         DCL RECORD CHAR(BLKSI) CONTROLLED;
         DCL BLKSI FIXED BIN(31,0) STATIC;
         DCL ALLOC FILE RECORD OUTPUT ENV(U BLKSIZE(BLKSI));
         DCL PARMSTR FIXED BIN(15,0) INIT(0);
         DCL 1 EXTARY(9) BASED(EXTPTR),
              2 EXTFL BIT(8),
              2 EXTSQ BIT(8),
              2 CB1 FIXED BIN(15,0),
              2 HB1 FIXED BIN(15,0),
              2 CB2 FIXED BIN(15,0),
              2 HB2 FIXED BIN(15,0);
         DCL EXTCNT FIXED BIN(15,0);
         DCL TOTRK FIXED BIN(15,0) INIT(0);
         CALL DEVTYPE(RETCODE,DDNAME,DEVAREA,'DEVTAB');
         IF RETCODE ^= 0
              THEN DO;
                   PUT EDIT('ERROR = ',RETCODE,' FROM DEVTYPE MACRO')
                     (A,P'99',A) SKIP(1);
                   SIGNAL ERROR;
                   END;
         BLKSI = DEVDMAXB;
         ALLOCATE RECORD;
         READ FILE(TAPE1) INTO(INREC);
         READ FILE(TAPE1) INTO(INREC);
         CLOSE FILE(TAPE1);
         WRKPTR = ADDR(INREC);
         WRKADJ = WRKADJ + 42;
         DSCB1PTR = WRKPTR;
         IF DS1DSORG & '01000010'B = '0'B
              THEN TOTRK = 1; /* HE IS GOING TO BOMB */
              ELSE DO;
                   WRKADJ = WRKADJ + 148;
                   DSCB3PTR = WRKPTR;
                   EXTPTR = ADDR(DS1EXT);
                   EXTCNT = 3;
                   CALL CNTSUB;
                   IF DS1PTRDS^=LOW(5)
                        THEN DO;
                             WRKPTR = DSCB3PTR;
                             WRKADJ = WRKADJ + 4;
                             EXTPTR = WRKPTR;
 /* BUG FIXED BY LAST 3 LIN  EXTPTR = ADDR(DS3EXT); ES */
                             EXTCNT = 4;
                             CALL CNTSUB;
                             EXTPTR = ADDR(DS3ADEXT);
                             EXTCNT = 9;
                             CALL CNTSUB;
                             END;
                   CALL SYSJFCB(DDNAME,JFCBPTR);
                   I = JFCBPQTY;
                   J = JFCBSQTY;
                   I = (I+J*16)*DEVDNTRK;
                   IF I < TOTRK
                        THEN TOTRK = 1;  /* HE IS GOING TO BOMB */
                   END;
         IF TOTRK < 1
              THEN TOTRK = 1;
  CNTSUB:  PROC;
          DO I = 1 TO EXTCNT;
              IF EXTFL(I) ^= '0'B
                   THEN TOTRK = TOTRK + (CB2(I)-CB1(I)-1)*DEVDNTRK
                     +DEVDNTRK-HB1(I)+HB2(I)+1;
              END;
         END CNTSUB;
         DO TOTRK = TOTRK TO 1 BY -1;
              WRITE FILE(ALLOC) FROM(RECORD);
              END;
         CLOSE FILE(ALLOC);
         FREE RECORD;
         CALL SYSLINK('FDRDSF',RETCODE,PARMSTR);
         IF RETCODE ^= 0
              THEN SIGNAL ERROR;
         % INCLUDE DEVDATA;
         % INCLUDE DSCB1;
         % INCLUDE DSCB3;
         % INCLUDE JFCB;
         END;
