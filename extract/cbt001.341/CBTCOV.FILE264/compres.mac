COMPRES START 0                                                    TGS
*$.HE GENERALIZED SUBROUTINES - COMPRES
*$.HE REMOVE BLANKS FROM STRING - BAL
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/17/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  COMPRES
*$DOCUMENTATION ID:  *$
*$.SS
*$.FI L
*$THIS ROUTINE CAN BE CALLED FROM AN ASSEMBLER PROGRAM TO COMPRESS
*$BLANKS OUT OF A CHARACTER STRING.
*$ON ENTRY, REG1 WILL CONTAIN THE ADDRESS OF DATA
*$HAVING THE FOLLOWING FORMAT:
*$.IN 5
*$BYTE 0 = TOTAL LENGTH OF DATA TO BE COMPRESSED,
*$INCLUDING BYTE 0
*$.IN 5
*$BYTE 1 = BLANK CHARACTER
*$.IN 5
*$BYTES 2 THRU N CONTAIN DATA THAT IS TO BE
*$COMPRESSED
*$.IN 0
*$ON EXIT, REG1 WILL CONTAIN THE ADDR OF THE COMPRESSD
*$DATA STRING, AND REG0 WILL CONTAIN THE TOTAL LENGTH
*$OF THE COMPRESSED DATA STRING
********************************************************************TGS
         TGSREQU                                                    TGS
         USING *,RF                                                 TGS
         STM   RE,RC,12(RD)                                         TGS
         SR    RE,RE                                                TGS
         SR    R2,R2                                                TGS
         IC    R2,0(R1)                R2 = LENGTH OF RECORD        TGS
         MVI   0(R1),X'40'                                          TGS
         LR    R5,R1                   R5 = RECORD ADDR             TGS
LPINIT   EQU   *                                                    TGS
         LR    R3,R1                   R3 = RECORD ADDR             TGS
LOOP     EQU   *                                                    TGS
         CLI   0(R3),X'40'             IS IT BLANK                  TGS
         BC    7,ENDLOOP               BR IF NOT                    TGS
         LA    R3,1(R3)                BUMP R3                      TGS
         BCT   R2,LOOP                                              TGS
         LA    RE,1                                                 TGS
ENDLOOP  EQU   *                                                    TGS
         LR    R4,R3                                                TGS
         SR    R3,R1                   R3 = CNT OF BLNKS            TGS
         STC   R3,0(R1)                SET CNT IN RECORD            TGS
         LTR   RE,RE                                                TGS
         BC    7,NOCOMPR                                            TGS
         LR    R3,R2                                                TGS
         BCTR  R3,R0                                                TGS
         EX    R3,MVC                  COMPRESS RECORD              TGS
NOCOMPR  EQU   *                                                    TGS
         MVI   1(R1),X'00'                                          TGS
         LR    R6,R1                                                TGS
         LA    R1,2(R6)                                             TGS
         LR    R4,R1                                                TGS
         LTR   RE,RE                   ARE WE ALL DONE              TGS
         BC    7,ALLDONE               BR IF YES                    TGS
BLOOP    EQU   *                                                    TGS
         CLC   0(2,R1),=CL2' '         IS IT TWO BLANKS             TGS
         BC    8,MOREBLNK              BR IF YES                    TGS
         LA    R1,1(R1)                BUMP R1                      TGS
         BCT   R2,BLOOP                                             TGS
         LA    RE,1                                                 TGS
MOREBLNK EQU   *                                                    TGS
         LR    R7,R1                                                TGS
         SR    R7,R4                   R7 = LENGTH NONBLANK CHARS   TGS
         STC   R7,1(R6)                SET LENGTH IN RECORD         TGS
         LTR   RE,RE                   ARE WE ALL DONE              TGS
         BC    8,LPINIT                BR IF NOT                    TGS
ALLDONE  EQU   *                                                    TGS
         SR    R1,R5                                                TGS
         LR    R0,R1                   R0 = COMPRESSED LENGTH       TGS
         LM    R1,RC,24(RD)            RESTORE CALLERS REGS         TGS
         L     RE,12(RD)               RESTORE RE                   TGS
         BR    RE                                                   TGS
MVC      MVC   2(0,R1),0(R4)                                        TGS
         LTORG                                                      TGS
         END
