UT2PUT   START 0                                                    TGS
********************************************************************TGS
*                  THIS ROUTINE WILL BE RESPONSIBLE FOR CREATING   *TGS
*              THE SYSUT2 DATA SET.  AT ENTRY, REG0 WILL CONTAIN   *TGS
*              THE ADDRESS OF THE NEXT SEQUENTIAL RECORD,          *TGS
*              UNLESS IT IS EOJ, IN WHICH CASE REG0 WILL CONTAIN   *TGS
*              ZERO.  REG1 WILL CONTAIN THE LENGTH OF THE RECORD.  *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         LR    R2,R0                   SAVE RECORD ADDR             TGS
         STC   R1,INREC                SET RECORD LEN               TGS
         TM    INITSW,X'80'            HAS INITIALIZATION BEEN DONE TGS
         BC    1,BEGIN                 BR IF YES                    TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    8,RETCTL                BR IF YES                    TGS
         OI    INITSW,X'80'            SET INITSW                   TGS
         L     R1,16                   R1 = CVT SDDR                TGS
         L     R1,0(R1)                R1 = IEATCBP ADDR            TGS
         L     R1,0(R1)                R1 = MY TCB ADDR             TGS
         L     R1,12(R1)               R1 = TIOTADDR                TGS
         LA    R3,24                   R3 = INITIAL OFFSET          TGS
TIOTLOOP LA    R1,0(R1,R3)             BUMP TO NEXT TIOT ENTRY      TGS
         CLC   4(8,R1),=CL8'SYSUT2'    IS IT SYSUT2                 TGS
         BC    8,GOTUT2                BR IF YES                    TGS
         IC    R3,0(R1)                R3 = LENGTH THIS ENTRY       TGS
         LTR   R3,R3                   IS IT END OF TIOT            TGS
         BC    7,TIOTLOOP              BR IF NOT                    TGS
         L     RF,=V(UT2PUT)           SET UP TO NOT WRITE          TGS
         MVC   0(2,RF),=X'07FE'             SYSUT2 DATA SET         TGS
         B     RETCTL                                               TGS
GOTUT2   EQU   *                                                    TGS
         OPEN  (SYSUT2,(OUTPUT))                                    TGS
         TM    SYSUT2+48,X'10'         IS IT GOOD OPEN              TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1111,DUMP                                            TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,SYSUT2+62            R0 = BLKSIZE                 TGS
         STH   R0,BUFLEN                                            TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,BUFADDR                                           TGS
         ST    R1,NXTADDR                                           TGS
         B     PUTREC                                               TGS
BEGIN    EQU   *                                                    TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    8,PUTBLK                BR IF YES                    TGS
PUTREC   EQU   *                                                    TGS
         MVC   INREC+2(88),0(R2)       MOVE RECORD TO COMPRESS AREA TGS
         MVI   INREC+1,X'40'           PUT BLANK IN RECORD          TGS
         LA    R1,INREC                                             TGS
         L     RF,=V(COMPRES)                                      TGS
         BALR  RE,RF                   GO COMPRESS RECORD           TGS
         LR    R3,R0                   SAVE COMPRESSED LENGTH       TGS
RETRY    EQU   *                                                    TGS
         L     R4,NXTADDR                                           TGS
         LR    R5,R4                   SAVE OLD NXTADDR             TGS
         LA    R4,0(R3,R4)             R4 = NEW NXTADDR             TGS
         LR    R6,R4                   SAVE NEW NXTADDR             TGS
         S     R4,BUFADDR                                           TGS
         CH    R4,BUFLEN                                            TGS
         BC    2,PUTBLK                BR IF BUFFER IS FULL         TGS
         ST    R6,NXTADDR              UPDAT NXTADDR                TGS
         BCTR  R3,R0                   REDUCE LENGTH FOR EX         TGS
         EX    R3,MVC                  MOVE RECORD TO BUFFER        TGS
         B     RETCTL                                               TGS
MVC      MVC   0(0,R5),INREC                                        TGS
PUTBLK   EQU   *                                                    TGS
         L     RA,NXTADDR                                           TGS
         S     RA,BUFADDR              RA = BLK LEN                 TGS
         L     R6,BUFADDR                                           TGS
         WRITE DECB,SF,,(6),(10),MF=E                               TGS
         CHECK DECB                                                 TGS
         MVC   NXTADDR,BUFADDR         RESET NXTADDR                TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    7,RETRY                 BR IF NOT                    TGS
         CLOSE SYSUT2                                               TGS
         LH    R0,BUFLEN                                            TGS
         L     R1,BUFADDR                                           TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         B     RETCTL                                               TGS
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=W,EXLST=NMEXITS,           C
               RECFM=U
NMEXITS  DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                   DCB EXIT INDICATOR = 05      TGS
         DC    AL3(NMEXIT)                                          TGS
NMEXIT   EQU   *                                                    TGS
         LH    R9,SYSUT2+62            R9 = BLKSIZE                 TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BCR   7,RE                    BR IF NOT                    TGS
         MVC   SYSUT2+62(2),=H'7294'   FOR NOW, USE A DEFAULT       TGS
*                                           BLKSIZE OF 7294         TGS
         BR    RE                                                   TGS
         WRITE DECB,SF,SYSUT2,MF=L                                  TGS
BUFADDR  DC    F'0'          ADDR OF BUFFER                         TGS
NXTADDR  DC    F'0'          ADDR OF NEXT SLOT IN BUFFER            TGS
INREC    DS    CL90                                                 TGS
BUFLEN   DS    H                                                    TGS
INITSW   DC    X'00'                                                TGS
         LTORG                                                      TGS
         END
