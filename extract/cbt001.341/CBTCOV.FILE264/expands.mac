*$.HE GENERALIZED SUBROUTINES - EXPAND
*$.HE REINSERT BLANKS INTO A STRING - BAL
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/17/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  EXPANDS
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE CAN BE CALLED FROM AN ASSEMBLER PROGRAM TO
*$REINSERT BLANKS THAT HAD PREVIOUSLY BEEN REMOVED BY A CALL
*$TO COMPRES.
*$ON ENTRY, REG1 WILL CONTAIN THE ADDR OF A COMPRESSED*TGS
*$RECORD, REG2 WILL CONTAIN THE ADDR OF AN AREA INTO  *TGS
*$WHICH THE RECORD IS TO BE EXPANDED, AND REG0 WILL   *TGS
*$CONTAIN THE EXPANDED RECORD LENGTH.  ON EXIT, REG1  *TGS
*$WILL CONTAIN THE ADDRESS OF THE FIRST BYTE BEYOND   *TGS
*$THE COMPRESSED RECORD.    *TGS
********************************************************************TGS
EXPAND   START 0                                                    TGS
         TGSREQU                                                    TGS
         USING *,RF                                                 TGS
         STM   RE,RC,12(RD)                                         TGS
         SR    R3,R3                                                TGS
         LR    R4,R0                   R4 = EXPANDED RECORD LENGTH  TGS
         MVI   0(R2),X'40'                                          TGS
         LR    R5,R4                                                TGS
         BCTR  R5,R0                                                TGS
         BCTR  R5,R0                                                TGS
         EX    R5,MOVEBLNK             CLEAR TO BLANKS              TGS
STRT     EQU   *                                                    TGS
         IC    R3,0(R1)                R3 = CNT OF BLANKS           TGS
         LA    R2,0(R3,R2)             BUMP TO NONBLANK POSITION    TGS
         SR    R4,R3                   DECREMENT LENGTH LEFT        TGS
         BC    8,ALLDONE               BR IF ALL DONE               TGS
         IC    R3,1(R1)                R3 = CNT OF NON BLANKS       TGS
         BCTR  R3,R0                   MINUS 1 FOR EX               TGS
         EX    R3,MVC                  MOVE NON BLANKS TO RECORD    TGS
         LA    R3,1(R3)                FULL COUNT                   TGS
         LA    R2,0(R2,R3)             BUMP TO BLNK POS             TGS
         SR    R4,R3                   DECREMENT LENGTH LEFT        TGS
         BC    8,ALLDONA               BR IF ALL DONE               TGS
         LA    R1,2(R3,R1)                                          TGS
         B     STRT                                                 TGS
MVC      MVC   0(0,R2),2(R1)                                        TGS
MOVEBLNK MVC   1(0,R2),0(R2)                                        TGS
ALLDONA  EQU   *                                                    TGS
         LA    R1,0(R3,R1)             BUMP TO NXT REC-2            TGS
ALLDONE  EQU   *                                                    TGS
         LA    R1,2(R1)                BUMP TO NXT REC              TGS
         LM    R2,RC,28(RD)            RESTORE CALLERS REGS         TGS
         L     RE,12(RD)               RESTORE RE                   TGS
         BR    RE                                                   TGS
         LTORG                                                      TGS
         END
