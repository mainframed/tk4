 /* TSOACCT - THIS SUBROUTINE IS USED BY RPUSERID TO RETRIEVE THE
    FIRST ACCOUNT NUMBER FROM THE UADS DATA SET GIVEN THE TSO
    USERID. */
 TSOACCT:  PROC(UID) RETURNS(CHAR(5));
         DCL UID CHAR(8) VAR;
 DCL DUMPSTT ENTRY;
         DCL MEMNAM CHAR(8);
         DCL UADCOR CHAR(8000),
             UADARY(100) CHAR(80) DEF UADCOR;
         DCL UADAREA AREA BASED(SPTR);
         DCL 1 HDR BASED(SPTR),
              2 PAD1 CHAR(24),
              2 POBOFF OFFSET(UADAREA);
                   /* PASSOWRD OFFSET BLOCK */
         DCL 1 POB BASED(POBOFF),
              2 PAD1 CHAR(4),
              2 AOBOFF OFFSET(UADAREA);
                   /* ACCT # OFFSET BLOCK */
         DCL 1 AOB BASED(AOBOFF),
              2 PAD1 CHAR(8),
              2 ABOFF OFFSET(UADAREA);
                   /* ACCT # BLOCK */
         DCL 1 AB BASED(ABOFF),
              2 PAD1 CHAR(45),
              2 ACTNM CHAR(6);
         DCL DDNAME CHAR(8) STATIC INIT('SYSUADS');
         DCL DCBPTR PTR STATIC;
         DCL RETCODE FIXED BIN(31,0);
         DCL 1 DCB2 BASED(DCBPTR),
              2 DCBPAD1 CHAR(36),
              2 DCBRECFM BIT(8),
              2 DCBPAD2 CHAR(45),
              2 DCBLRECL FIXED BIN(15,0);
         DCL INITSW BIT(1) STATIC INIT('1'B);
         IF INITSW
              THEN DO;
                   INITSW = '0'B;
                   CALL SYSDCBP(DCBPTR,DDNAME);
                   CALL SYSOPEN(DCBPTR,'INPUT');
                   END;
         SPTR = ADDR(UADCOR);
         MEMNAM = UID××'0';
         CALL SYSFIND(DCBPTR,MEMNAM,'D',RETCODE);
         IF RETCODE ^= 0
              THEN RETURN('');
         ON CONDITION(SYSEOD) GO TO EOD;
         DO I = 1 TO 100;
              CALL NXTREC(UADARY(I));
              END;
 EOD:
         RETURN(SUBSTR(ACTNM,1,2)××SUBSTR(ACTNM,4,3));
 NXTREC:  PROC(REC);
         DCL BUFPTR PTR STATIC,
             BUFPADJ FIXED BIN(31,0) DEF BUFPTR;
         DCL 1 LRECV BASED(BUFPTR),
              2 BYTCNT FIXED BIN(15,0),
              2 RECSTR CHAR(300);
         DCL BCNTSAV FIXED BIN(15,0) STATIC INIT(0);
         DCL REC CHAR(80);
         DCL BUFFER CHAR(10000) VAR STATIC;
         DCL LRECF CHAR(300) BASED(BUFPTR);
         IF BCNTSAV = 0
              THEN DO;
                   CALL SYSREAD(DCBPTR,BUFFER);
                   BCNTSAV = LENGTH(BUFFER);
                   BUFPTR = ADDR(BUFFER);
                   BUFPADJ = BUFPADJ + 2;
                   END;
         REC = SUBSTR(LRECF,1,DCBLRECL);
         BCNTSAV = BCNTSAV - DCBLRECL;
         BUFPADJ = BUFPADJ + DCBLRECL;
         END NXTREC;
         END;
