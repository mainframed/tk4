UT13CTL  CSECT                                                      TGS
********************************************************************TGS
*                  THIS ROUTINE WILL BE INVOKED BY UPDAT WHENEVER  *TGS
*              IT DESIRES THE NEXT SEQUENTIAL OLD MASTER RECORD.   *TGS
*              THIS ROUTINE WILL INTERFACE WITH BOTH UT1GET AND    *TGS
*              UT3GET TO OBTAIN THE NEXT SEQUENTIAL RECORD FROM THE*TGS
*              SYSUT1 AND SYSUT3 DATA SETS.  THE ROUTINE WILL      *TGS
*              DETERMINE WHICH RECORD TO PASS BACK TO UPDAT        *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         TM    INITSW,X'80'                                         TGS
         BC    1,GO                                                 TGS
         OI    INITSW,X'80'                                         TGS
         L     R2,16                   R2 = CVT ADDR                TGS
         L     R2,0(R2)                R2 = IEATCBP ADDR            TGS
         L     R2,0(R2)                R2 = MY TCB ADDR             TGS
         L     R2,12(R2)               R2 = TIOT ADDR               TGS
         LA    R3,24                   R3 = INITIAL OFFSET          TGS
TIOTLOOP LA    R2,0(R2,R3)             BUMP TO NEXT TIOT ENTRY      TGS
         CLC   4(8,R2),=CL8'SYSUT1'    IS IT SYSUT1                 TGS
         BC    8,GOTUT1                BR IF YES                    TGS
         CLC   4(8,R2),=CL8'SYSUT3'    IS IT SYSUT3                 TGS
         BC    8,GOTUT3                BR IF YES                    TGS
NXTENTRY IC    R3,0(R2)                R3 = LENGTH THIS ENTRY       TGS
         LTR   R3,R3                   IS IT END TIOT               TGS
         BC    7,TIOTLOOP              BR IF NOT                    TGS
         CLC   UT1RECA,UT3RECA                                      TGS
         BC    7,CTLCOMP               BR IF EITHER IS AVLBL        TGS
         SR    R1,R1                   IND NEITHER AVLBL            TGS
         B     RETCTL                                               TGS
GOTUT1   EQU   *                                                    TGS
         LOAD  EP=UT1GET                                            TGS
         ST    R0,UT1ADDR                                           TGS
         LR    RF,R0                                                TGS
         BALR  RE,RF                   GO GET UT1 RECORD            TGS
         LTR   R1,R1                   IS IT GOOD UT1               TGS
         BC    8,NXTENTRY              BR IF NOT                    TGS
         STM   R0,R1,UT1RECL                                        TGS
         MVC   UT1NAME,63(R1)                                       TGS
         B     NXTENTRY                                             TGS
GOTUT3   EQU   *                                                    TGS
         LOAD  EP=UT3GET                                            TGS
         ST    R0,UT3ADDR                                           TGS
         LR    RF,R0                                                TGS
         LA    R1,=CL8'SYSUT3'                                      TGS
         BALR  RE,RF                   GO GET UT3 RECORD            TGS
         LTR   R1,R1                   IS IT GOOD UT3               TGS
         BC    8,NXTENTRY              BR IF NOT                    TGS
         ST    R1,UT3RECA                                           TGS
         MVC   UT3NAME(8),63(R1)                                    TGS
         B     NXTENTRY                                             TGS
CTLCOMP  EQU   *                                                    TGS
         CLC   UT1NAME(8),UT3NAME                                   TGS
         BC    2,USEUT3                BR IF SYSUT3 SHOULD BE USED  TGS
         BC    4,USEUT1                BR IF SYSUT1 S/B USED        TGS
         CLC   UT3NAME,=8X'FF'         IS IT EOF                    TGS
         BC    8,USEUT1                BR IF YES                    TGS
LOOP3    EQU   *                                                    TGS
         L     RF,UT1ADDR                                           TGS
         BALR  RE,RF                   GO GET UT3 RECORD            TGS
         STM   R0,R1,UT1RECL           SAVE REC ADDR & LEN          TGS
         LTR   R1,R1                   IS IT EOF ON SYSUT1          TGS
         BC    8,UT1EOF                BR IF YES                    TGS
         CLC   0(15,R1),=CL15'./       ADD'                         TGS
         BC    7,LOOP3                                              TGS
         MVC   UT1NAME,63(R1)          RESET UT1NAME                TGS
         B     USEUT3                                               TGS
USEUT1   EQU   *                                                    TGS
         OI    FLIPSW,X'80'            IND SYSUT1 SHOULD BE USED    TGS
         LM    R0,R1,UT1RECL                                        TGS
         B     RETCTL                                               TGS
USEUT3   EQU   *                                                    TGS
         NI    FLIPSW,X'7F'            IND SYSUT3 SHOULD BE USED    TGS
         LM    R0,R1,UT3RECL                                        TGS
         B     RETCTL                                               TGS
GO       EQU   *                                                    TGS
         TM    FLIPSW,X'80'            IS SYSUT1 TO BE USED         TGS
         BC    1,GETUT1                BR IF YES                    TGS
         L     RF,UT3ADDR                                           TGS
         BALR  RE,RF                   GO GET UT3 RECORD            TGS
         ST    R1,UT3RECA                                           TGS
         L     R0,UT3RECL                                           TGS
         LTR   R1,R1                   IS IT EOF ON SYSUT3          TGS
         BC    8,UT3EOF                BR IF YES                    TGS
         CLC   0(15,R1),=CL15'./       ADD'                         TGS
         BC    7,RETCTL                                             TGS
         MVC   UT3NAME,63(R1)          RESET UT3NAME                TGS
         B     CTLCOMP                                              TGS
UT3EOF   EQU   *                                                    TGS
         MVC   UT3NAME,=8X'FF'                                      TGS
         B     CTLCOMP                                              TGS
GETUT1   EQU   *                                                    TGS
         L     RF,UT1ADDR                                           TGS
         BALR  RE,RF                   GO GET UT1 RECORD            TGS
         STM   R0,R1,UT1RECL                                        TGS
         LTR   R1,R1                   IS IT EOF ON SYSUT1          TGS
         BC    8,UT1EOF                BR IF YES                    TGS
         CLC   0(15,R1),=CL15'./       ADD'                         TGS
         BC    7,RETCTL                                             TGS
         MVC   UT1NAME,63(R1)          RESET UT1NAME                TGS
         B     CTLCOMP                                              TGS
UT1EOF   EQU   *                                                    TGS
         DELETE EP=UT1GET                                           TGS
         MVC   UT1NAME,=8X'FF'                                      TGS
         B     CTLCOMP                                              TGS
INITSW   DC    X'00'                                                TGS
FLIPSW   DC    X'00'         BIT 0 = 1 IF SYSUT1 CURRENTLY IN USE   TGS
UT1NAME  DS    0CL8                = 0 IF SYSUT3 CURRENTLY IN USE   TGS
         DC    8X'FF'                                               TGS
UT3NAME  DS    0CL8                                                 TGS
         DC    8X'FF'                                               TGS
UT1RECL  DC    F'0'                                                 TGS
UT1RECA  DC    F'0'                                                 TGS
UT3RECL  DC    F'82'                                                TGS
UT3RECA  DC    F'0'                                                 TGS
UT1ADDR  DC    F'0'                                                 TGS
UT3ADDR  DC    F'0'                                                 TGS
         LTORG                                                      TGS
         END
