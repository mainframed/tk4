PROBJ    START 0
         SAVES RETNCTL
         BC    15,BEGIN
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,EODAD=DONE,MACRF=(GM)
SYSOUT DCB DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),BLKSIZE=1330,LRECL=133,   C
               RECFM=FBA
BEGIN    OPEN  (SYSIN,(INPUT),SYSOUT,(OUTPUT))
NEXT     AP    RECTR,=P'1'
         CP    RECTR,=P'10'
         BC    7,GO
         SP    RECTR,RECTR
         MVI   IDLNE1,C'1'
GO       GET   SYSIN,CARD
         MVC   CC2TH4,CARDID
         PUT   SYSOUT,IDLNE1
         MVI   IDLNE1,C'-'
         LA    1,FIVE1
         BAL   6,TRANSLAT
         MVC   CC5TH16,PRINTABL
         LA    1,FIVE2
         BAL   6,TRANSLAT
         MVC   CC5TH16+10(10),PRINTABL
         LA    1,FIVE3
         BAL   6,TRANSLAT
         MVC   CC5TH16+20(4),PRINTABL
         PUT   SYSOUT,IDLNE2
         LA    1,TEXT
         LA    2,CC17TH72
LOOP     BAL   6,TRANSLAT
         MVC   0(10,2),PRINTABL
         LA    1,5(0,1)
         LA    2,10(0,2)
         AP    LPCTR,=P'1'
         CP    LPCTR,=P'11'
         BC    4,LOOP
         SP    LPCTR,LPCTR
         BAL   6,TRANSLAT
         MVC   0(2,2),PRINTABL
         PUT   SYSOUT,TXTLNE
         MVC   CC73TH80,CARDEND
         PUT   SYSOUT,ENDLNE
         BC    15,NEXT
DONE     CLOSE (SYSIN,,SYSOUT)
         B     RETNCTL
TRANSLAT UNPK  PRINTABL(9),0(5,1)
         MVZ   PRINTABL+9(1),PRINTABL+8
         PACK  PRINTABL+9(1),PRINTABL+9(1)
         MVZ   PRINTABL,ALLZERO
         TR    PRINTABL,TRTABLE
         BCR   15,6
TRTABLE  DS    0CL16
         DC    C'0'
         DC    C'1'
         DC    C'2'
         DC    C'3'
         DC    C'4'
         DC    C'5'
         DC    C'6'
         DC    C'7'
         DC    C'8'
         DC    C'9'
         DC    C'A'
         DC    C'B'
         DC    C'C'
         DC    C'D'
         DC    C'E'
         DC    C'F'
RECTR    DC    XL3'0C'
ALLZERO  DC    XL10'00'
LPCTR    DC    XL2'0C'
PRINTABL DS    CL10
CARD     DS    0CL80
         DS    CL1
CARDID   DS    CL3
FIVE1    DS    CL5
FIVE2    DS    CL5
FIVE3    DS    0CL5
         DS    CL2
TEXT     DS    11CL5
TEXTEND  DS    CL1
CARDEND  DS    CL8
IDLNE1   DS    0CL133
         DC    C'-'
CC2TH4   DS    CL3
         DC    CL129' '
IDLNE2   DS    0CL133
         DC    C'0'
         DC    CL10' '
CC5TH16  DS    CL24
         DC    CL98' '
TXTLNE   DS    0CL133
         DC    C'0'
         DC    CL20' '
CC17TH72 DS    CL112
ENDLNE   DS    0CL133
         DC    C'0'
         DC    CL30' '
CC73TH80 DS    CL8
         DC    CL94' '
         LTORG
         END
