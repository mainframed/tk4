 /* LSQR */
 /*
0*$.HE GENERALIZED SUBROUTINES - LSQR
0*$.HE LINEAR REGRESSION
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$06/17/78
0*$AUTHOR:  PAUL DIXON
0*$SOURCE MODULE NAME:  LSQR
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE CAN BE USED TO PERFORM LINEAR REGRESSION.  CALL
0*$FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$.IN 5
0*$CALL LSQR(MAT,SLOPE,INTERCEPT,CORRELATION);
0*$.IN 0
0*$.SP 1
0*$WHERE -
0*$.HI 5
0*$.SP 1
0*$MAT IS DECLARED MATRIX(0:Y,0:X) BIN FLOAT(53),
0*$WHERE Y AND X CAN BE ANY VALUES.  YOU
0*$CAN THINK OF MAT REPRESENTING A TWO-DIMENSIONAL GRID.  MAT(M,N)
0*$CONTAINS A COUNT OF THE NUMBER OF POINTS ON THE GRID HAVING THE
0*$COORDINATES N AND M.
0*$.SP 1
0*$SLOPE IS ALSO BIN FLOAT(53), AND WILL BE SET BY LSQR TO THE
0*$SLOPE VALUE FOR THE EQUATION REPRESENTING THE
0*$SUM-OF-LEAST-SQUARES LINE (Y = SLOPE*X + INTERCEPT).
0*$.SP 1
0*$INTERCEPT IS BIN FLOAT(53), AND WILL BE SET BY LSQR TO THE
0*$INTERCEPT VALUE FOR THE EQUATION REPRESENTING THE
0*$SUM-OF-LEAST-SQUARES LINE (Y = SLOPE*X + INTERCEPT).
0*$.SP 1
0*$CORRELATION IS BIN FLOAT(53), AND WILL BE SET BY LSQR TO THE
0*$COEFFICIENT OF CORRELATION.
 */
 LSQR:  PROC(MAT,SLOPE,INTERCEPT,CORRELATION);
         DFT RANGE(*) BIN FLOAT VALUE(BIN FLOAT(53));
         DCL (MAT(*,*),SLOPE,INTERCEPT,CORRELATION) BIN FLOAT(53);
         DCL (SQRT,HBOUND) BUILTIN;
         DCL (SX,SY,SXY,SYY,SXX,N) BIN FLOAT(53) INIT(0);
         DCL (I,X,Y,XD,YD) BIN FIXED(15,0);
         YD = HBOUND(MAT,1);
         XD = HBOUND(MAT,2);
         DO Y = 0 TO YD;
              DO X = 0 TO XD;
                   DO I = 1 TO MAT(Y,X);
                        N = N + 1;
                        SX = SX + X;
                        SY = SY + Y;
                        SXX = SXX + X * X;
                        SXY = SXY + X * Y;
                        SYY = SYY + Y * Y;
                        END;
                   END;
              END;
         SLOPE,INTERCEPT,CORRELATION = 0;
         IF N > 0
              THEN DO;
                   DXY = SXY - SX * SY/N;
                   DXX = SXX - SX * SX/N;
                   DYY = SYY - SY * SY/N;
                   IF DXX > 0
                        THEN DO;
                             SLOPE = DXY/DXX;
                             INTERCEPT = (SY - SLOPE*SX)/N;
                             TMP = SQRT(DXX)*SQRT(DYY);
                             IF TMP > 0
                                  THEN CORRELATION = DXY/TMP;
                             END;
                   END;
         END LSQR;
