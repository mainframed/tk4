   /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* DENT011 - SUBMIT,SUB,JOB SUBCOMMANDS */
   % DCL @PROCNAME CHAR;
   % DCL @RENTNAME CHAR;
   % @PROCNAME ='RENT';
   % @RENTNAME ='DENT011';
   % INCLUDE DENTSUB;
   DCL INTRDR ENTRY(,CHAR(*) VAR,FIXED BIN(15,0),FIXED BIN(15,0));
   DCL MYPTR PTR;
   DCL LAST FIXED BIN(31,0) STATIC INIT(99999999);
   DCL ALCODE CHAR(3) VAR STATIC INIT('OUT');
   DCL HELPSTR CHAR(4) VAR STATIC INIT('');
   DCL CMDA CHAR(240) VAR STATIC INIT('SUB LASTJCL ');
   DCL SUBDSN CHAR(44) VAR STATIC INIT('LASTJCL.CNTL');
   DCL SAVDDN PTR;
   DCL MYREC CHAR(80);
   IF LENGTH(PARY(2))^=0
     THEN DO;
       MSGDATA='ERROR - NO OPERANDS ALLOWED ON SUBMIT SUBCOMMAND';
       RHRC = 8;
       RETURN;
       END;
   IF CMDINDX=4
     THEN DO; /* IT IS JOB SUBCOMMAND */
       CALL INTRDR(RH_SEQ_INTERNAL_RDR,MSGDATA,0,0);
       IF MSGDATA^=''
         THEN RHRC=16;
         ELSE DO;
           DO MYPTR=NXTHDR REPEAT MYPTR->NXTPTR WHILE(MYPTR^=HDPTR);
             MYREC=MYPTR->RECSTR;
             WRITE FILE(RH_SEQ_INTERNAL_RDR) FROM(MYREC);
             END;
           CLOSE FILE(RH_SEQ_INTERNAL_RDR);
           MSGDATA='JOB SUCCESSFULLY SUBMITTED';
           RHRC=0;
           END;
       RETURN;
       END;
   SAVDDN = OUTDDN;
   CALL #DOALC(SUBDSN,ALCODE,OUTDDN,NULL); /* CALL DOALC */
   IF OUTDDN = NULL
     THEN DO;
       MSGDATA='ERROR IN ALLOCATE';
       OUTDDN = SAVDDN;
       RHRC = 8;
       RETURN;
       END;
   IF SAVDDN^=NULL
     THEN SUBSTR(OUTDDN->ALCFLAG,6,1)=SUBSTR(SAVDDN->ALCFLAG,6,1);
   /* SET NONUM OPTION */
   MYPTR = HDPTR;
   CALL #RESEQ(RHPTR,SUBENT,MYPTR,'SAVE','NORENUM',LAST,RHPPTR);
   /* CALL RESEQ */
   CALL #CRTZAP(RHPPTR); /* CALL CRTZAPS */
   CALL #DOCMD(CMDA,HELPSTR); /* CALL DOCMD */
   CALL #DOFRE(OUTDDN); /* CALL DOFRE */
   OUTDDN = SAVDDN;
   CMDDATA ='X';
   RHRC = 0;
   END;
