 /* PLOT009 - FREQDIST,FREQ,FR SUBCOMMAND */
         % DCL @PROCNAME CHAR;
         % @PROCNAME = 'PLOT009';
         % INCLUDE PLOTSUB;
         DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
         ON ERROR SIGNAL FINISH; /* FOR TESTING ONLY */
         DCL RX FIXED BIN(15,0) STATIC INIT(1);
         DCL TARY(0:70) FIXED BIN(31,0) INIT((71)0);
         DCL TOT FIXED BIN(31,0) INIT(0);
         DCL START FIXED BIN(31,0) INIT(0);
         DCL FREQTITLE CHAR(78) STATIC INIT('FREQUENCY DISTRIBUTION');
         DCL 1 PLOTSTR UNALIGNED,
              2 PIX FIXED BIN(31,0),
              2 XCOEF FIXED BIN(31,0) INIT(0),
              2 YCOEF BIN FLOAT,
              2 PHDR CHAR(78);
         DCL PLOPCODE CHAR(4) VAR INIT('INIT');
         DCL GREGJUL ENTRY(CHAR(6)) RETURNS(FIXED DEC(5,0));
         DCL LODTE FIXED DEC(5,0) INIT(0);
         DCL HIDTE FIXED DEC(5,0) INIT(99999);
         DCL DOWSTR BIT(8) INIT('0'B);
         DCL HOURSTR BIT(24) INIT('0'B);
         DCL DOW ENTRY(FIXED DEC(5,0))
           RETURNS(FIXED BIN(15,0));
         DCL WSW BIT(1) INIT('0'B);
         DCL KEYLAB(0:15) LABEL;
         DCL KEYTAB(14) CHAR(20) VAR STATIC INIT(
           'SUN','MON','TUE','WED','THU','FRI','SAT',
           'LODTE','HIDTE','XCOEF','YCOEF','PRIMETIME',
           'WEIGHTED','START');
         DCL KEYVAL CHAR(80) VAR;
         DCL KEYINDX FIXED BIN(15,0);
         DO I = 3 TO 16 WHILE(PARY(I) ^= ' ');
              IF VERIFY(PARY(I),'0123456789 ')=0
                   THEN GOTO KEYLAB(15);
              CALL SUBENT(8)(PARY(I),KEYTAB,KEYVAL,KEYINDX);
                   /* CALL SPARS */
              GO TO KEYLAB(KEYINDX);
 BADOPER:
 KEYLAB(0):  /* BAD OPERAND */
              DISPLAY(2) = 'BAD OPERAND';
              RETURN;
 KEYLAB(1): /* SUN */
 KEYLAB(2): /* MON */
 KEYLAB(3): /* TUE */
 KEYLAB(4): /* WED */
 KEYLAB(5): /* THU */
 KEYLAB(6): /* FRI */
 KEYLAB(7): /* SAT */
              SUBSTR(DOWSTR,KEYINDX,1) = '1'B;
              GO TO ENDCASE;
 KEYLAB(8): /* LODTE */
              IF LENGTH(KEYVAL) = 6
                   THEN LODTE = GREGJUL(KEYVAL);
                   ELSE LODTE = KEYVAL;
              GO TO ENDCASE;
 KEYLAB(9): /* HIDTE */
              IF LENGTH(KEYVAL) = 6
                   THEN HIDTE = GREGJUL(KEYVAL);
                   ELSE HIDTE = KEYVAL;
              GO TO ENDCASE;
 KEYLAB(10): /* XCOEF */
              XCOEF = KEYVAL;
              GO TO ENDCASE;
 KEYLAB(11): /* YCOEF */
              YCOEF = KEYVAL;
              GO TO ENDCASE;
 KEYLAB(12): /* PRIMETIME */
              SUBSTR(DOWSTR,2,5) = '11111'B;
                   /* SET MON TUE WED THU FRI */
              SUBSTR(HOURSTR,9,8) = '11111111'B;
                   /* SET 8:00 A.M. THRU 4:00 P.M. */
              GO TO ENDCASE;
 KEYLAB(13): /* WEIGHTED */
              WSW = '1'B;
              GO TO ENDCASE;
 KEYLAB(14): /* START */
              START = KEYVAL;
              GO TO ENDCASE;
 KEYLAB(15): /* HOUR */
              K = PARY(I);
              IF K < 0 × K > 24
                  THEN GO TO BADOPER;
              SUBSTR(HOURSTR,K+1,1) = '1'B;
              GOTO ENDCASE;
 ENDCASE:
              END;
         IF HOURSTR = '0'B
              THEN HOURSTR = (24)'1'B;
         IF DOWSTR = '0'B
              THEN DOWSTR = '11111110'B;
         PIX = PARY(2); /* SET X INDEX */
         DO UNTIL(P2PTR = NULL);
              CALL SETBUF; /* GO PRIME BUFFER */
              IF P2PTR ^= NULL
                   THEN CALL SGRPH; /* GO UPDATE SCATTER PLOT */
              END;
         PLOPCODE = 'EOF';
         CALL SUBENT(1)(PLOPCODE,RX,P2PTR); /* CALL READIN */
         CALL CRTGRPH;
 /* SETBUF - FINDS NEXT RECORD WHICH IS ELIGIBLE */
 SETBUF:  PROC;
 LUP:    DO UNTIL((INDX=PIX) &
           (DTE>=LODTE) &
           (SUBSTR(DOWSTR,DOW(DTE),1)));
              CALL SUBENT(1)(PLOPCODE,RX,P2PTR);
                   /* CALL READIN */
              PLOPCODE = 'NXT';
              IF P2PTR = NULL
                   THEN LEAVE LUP;
              END;
         IF P2PTR ^= NULL
           THEN DO;
              PHDR = HDR;
              IF DTE > HIDTE
                   THEN DO;
                        PLOPCODE = 'EOF';
                        CALL SUBENT(1)(PLOPCODE,RX,P2PTR);
                             /* CALL READIN */
                        PLOPCODE = 'INIT';
                        P2PTR = NULL;
                        END;
              END;
         END SETBUF;
 CRTGRPH: PROC;
         DCL (I,J,K,X,Y) FIXED;
         DCL IPIC PIC'99';
         DCL (BIGY,FX,FY,XCF,YCF,FTOT) BIN FLOAT(53);
         DCL YARY(0:70) BIN FLOAT(53) INIT((71)0);
         FTOT = TOT;
         IF YCOEF = 0
              THEN DO;
                   BIGY = 0;
                   DO X = 0 TO 70;
                        FY = (TARY(X)*100/FTOT);
                        IF FY > BIGY
                             THEN BIGY = FY;
                        END;
                   YCOEF = BIGY/19;
                   END;
         DO X = 0 TO 70;
              YARY(X) = TARY(X)*100/FTOT/YCOEF;
              IF YARY(X) < 20 & ^WSW
                   THEN SUBSTR(DISPLAY(22-YARY(X)),X+4,1)='*';
              END;
         IF WSW /* WEIGHTED AVERAGES WANTED? */
              THEN DO X = 2 TO 69;
                   Y = (YARY(X-1) + YARY(X)*2 + YARY(X+1))/4;
                   IF Y < 20
                        THEN SUBSTR(DISPLAY(22-Y),X+4,1)='@';
                   END;
         DO Y = 19 TO 0 BY -1;
              IPIC = Y;
              SUBSTR(DISPLAY(22-Y),1,2) = IPIC;
              END;
         DISPLAY(1) = PHDR;
         DISPLAY(2) = FREQTITLE;
         PUT STRING(SUBSTR(DISPLAY(1),60,18))
           EDIT('X, 1 = ',XCOEF)(A,P'ZZZ,ZZZ,ZZ9');
         PUT STRING(SUBSTR(DISPLAY(2),60,18))
           EDIT('Y, 1 = ',YCOEF)(A,P'ZZZ,ZZ9V.999');
         DISPLAY(24) = '   '××(7)'0123456789'××'012345';
         DO I = 1 TO 7;
              SUBSTR(DISPLAY(23),I*10+4,1)=SUBSTR(DISPLAY(24),I+4);
              END;
         END CRTGRPH;
 /* SGRPH - UPDATE SCATTER PLOT */
 SGRPH:  PROC;
         DCL (X,Y,I,J,K,IX(2)) FIXED;
         IF XCOEF = 0
              THEN DO;
                   DO J = 0 TO 23;
                        IF ARRAY(J) > XCOEF
                             THEN XCOEF=ARRAY(J);
                        END;
                   XCOEF = XCOEF/71;
                   IF XCOEF = 0
                        THEN XCOEF = 1;
                   END;
         DO J = 0 TO 23;
          IF ARRAY(J) >= 0 & SUBSTR(HOURSTR,J+1,1) & ARRAY(J) >= START
            THEN DO;
                 X = (ARRAY(J)-START)/XCOEF;
                 IF X > 0
                      THEN X = X-1;
                 IF X > 70
                      THEN RETURN;
                 IF X < 0
                      THEN X = -X;
                 TARY(X) = TARY(X) + 1;
                 TOT = TOT + 1;
                 END;
              END;
         END SGRPH;
         END PLOT009;
