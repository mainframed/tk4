TSORDRB  CSECT
         SAVES RETCTL
         TGSREQU
         EXTRACT CSCBFLDS,FIELDS=COMM
         L     R7,CSCBFLDS
         MVC   ECBLIST+1(3),1(R7)      SET A(COMM ECB) IN ECBLIST
         LA    R0,4(R7)                R0 = A(A(CIB))
         LA    R1,1
         QEDIT CIBCTR=(1),ORIGIN=(0)
         WTO   'TSORDR IS ACTIVE'
         L     R1,4(R7)                R1 = A(CIB)
         LTR   R1,R1
         BC    8,NOCIB
FREECIB  DS    0H
         L     R1,4(R7)
         LA    R0,4(R7)
         QEDIT BLOCK=(1),ORIGIN=(0) FREE CIB
NOCIB    DS    0H
         EXTRACT TIOTADDR,'S',FIELDS=(TIOT)
         L     R2,TIOTADDR
         LA    R3,24                   R3=INITIAL OFFSET
         SR    R4,R4                   R4=INDEX
TIOTLOOP DS    0H
         LA    R2,0(R2,R3)             R2 = A(NXT ENTRY)
         CLI   0(R2),X'0'              IS IT END TIOT?
         BC    8,OPENS                 BR IF YES
         CLC   4(3,R2),=C'RDR'         IS IT RDRXX DD?
         BC    7,NOMTCH                BR IF NOT
         LA    R4,1(R4)                BUMP COUNT
NOMTCH   DS    0H
         IC    R3,0(R2)                R3 = LEN(THIS ENTRY)
         B     TIOTLOOP
OPENS    DS    0H
         OPEN  (INTRDR,(OUTPUT))
         OPEN  (BDAMDCB,(UPDAT))
         B     BYWAIT
NXT      DS    0H
         XC    TIMERECB(4),TIMERECB
         STIMER REAL,TIMEXIT,BINTVL=BINTVL
         WAIT  1,ECBLIST=ECBLIST
         L     R1,ECBLIST              R1 = A(COMM ECB)
         TM    TIMERECB,X'40'          IS IT EOJ?
         BC    8,RETCTL                BR IF YES
BYWAIT   DS    0H
         LR    R3,R4                   MOVE INDCB CNT TO R3
INLOOP   DS    0H
         ST    R3,BLOCKNO
         READ  BDAMDECB,DI,BDAMDCB,BDAMREC,'S',0,BLOCKNO+1
         CHECK BDAMDECB
         TM    BDAMREC,X'80'           IS THERE JCL?
         BC    8,NOJOB                 BR IF NOT
         LR    R1,R3
         SLL   R1,1                    R1=INDEX TO DDNAM TABLE
         LA    R1,TSODSN(R1)           R1 = A(PROPER DDNAME QUALIFIER)
         MVC   TSORDR+DDNAMO+3(2),0(R1) SET DDNAME
         OPEN  (TSORDR,(INPUT))
COPY     DS    0H
         GET   TSORDR
         CLC   0(10,R1),=CL10'/*COMMAND ' IS IT COMMAND CARD?
         BC    7,NOCOMMND              BR IF NOT
         MVC   COMMAND+4(60),10(R1)    MOVE COMMAND
         LA    R0,COMMAND
         LA    R1,MYSVC
         SVC   242
         B     COPY
MYSVC    DS    0H
         LR    R1,R0
         SR    R0,R0
         SVC   34
         SVC   3
NOCOMMND DS    0H
         LR    R0,R1
         PUT   INTRDR,(0)
         B     COPY
RDREOD   DS    0H
         PUT   INTRDR,EOFREC
         CLOSE (TSORDR)
         FREEPOOL TSORDR
         NI    BDAMREC,X'7F'           RESET CONTROL FLAG
         WRITE WRDECB,DI,BDAMDCB,BDAMREC,'S',0,BLOCKNO+1
         CHECK WRDECB
NOJOB    DS    0H
         BCT   R3,INLOOP               GO TRY THE NEXT FILE
         B     NXT                     GO WAIT SOME MORE
         DROP  RC
TIMEXIT  DS    0H
         STM   RE,RC,12(RD)
         BALR  RC,0
         USING *,RC
         POST  TIMERECB,0
         LM    RE,RC,12(RD)
         BR    RE
CSCBFLDS DS    2F
ECBLIST  DC    F'0'
         DC    AL1(128)
         DC    AL3(TIMERECB)
TIMERECB DC    F'0'
BINTVL   DC    F'3000'                 30 SECONDS
TSORDR   DCB   DDNAME=RDR00,DSORG=PS,EODAD=RDREOD,MACRF=(GL)
TSODSN   DC    C'00010203040506070809101112131415'
DDNAMO   EQU   40
INTRDR   DCB   DDNAME=INTRDR,DSORG=PS,MACRF=(PM),                      C
               LRECL=80,BLKSIZE=80,RECFM=F
BDAMDCB  DCB   DDNAME=BDAMFLE,BLKSIZE=80,DSORG=DA,MACRF=(RIC,WIC),     C
               OPTCD=R,RECFM=F
BDAMREC  DS    CL80
BLOCKNO  DS    F
EOFREC   DC    CL80'/*EOF'
TIOTADDR DS    F
COMMAND  DC    0A(0),AL2(64,0),CL60' '
         LTORG
         END
