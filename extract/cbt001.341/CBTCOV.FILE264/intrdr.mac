 /* INTRDR */
 /*
0*$.HE GENERALIZED SUBROUTINES - INTRDR
0*$.HE CALLED TO ALLOCATE AN INTERNAL READER
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$05/05/83
0*$AUTHOR:  P. DIXON, MODIFIED BY T. SMITH
0*$SOURCE MODULE NAME:  INTRDR
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE OPENS THE SPECIFIED FILE POINTING TO AN OS
0*$INTERNAL READER.  YOU MAY WRITE CARD-IMAGES TO THIS FILE JUST
0*$AS THOUGH YOU WERE READING THEM IN ON A STANDARD CARD READER.
0*$WHEN YOU ISSUE A CLOSE FOR THE SPECIFIED FILE, THE INTERNAL
0*$READER WILL BE RELEASED BACK TO THE SYSTEM.
0*$.SP 1
0*$CALL FORMAT IS AS FOLLOWS:
0*$.SP 1
0*$CALL INTRDR(FILE,MSG,ERROR_CODE,INFO_CODE);
0*$.SP 1
0*$WHERE -
0*$.SP 1
0*$DCL TMPFILE FILE RECORD OUTPUT ENV(F BLKSIZE(80));
0*$.SP 1
0*$DCL MSG CHAR(*) VAR - MUST BE AT LEAST 80 CHARACTERS LONG.  IF
0*$MSG IS NOT BLANK UPON RETURN OF CONTROL, THE SUBROUTINE WAS NOT
0*$SUCCESSFUL IN OBTAINING AN INTERNAL READER, MSG WILL CONTAIN
0*$TEXT DESCRIBING THE ERROR.
0*$.SP 1
0*$DCL (ERROR_CODE,INFO_CODE) FIXED BIN(15,0) - IF AN ERROR OCCURS
0*$DURING THE ALLOCATION OF THE INTERNAL READER, THESE TWO FIELDS
0*$WILL CONTAIN REASON CODES AS DOCUMENTED IN THE SYSTEM-PROGRAMMER'S
0*$LIBRARY (SPL) MANUAL.  IF YOU DON'T CARE ABOUT THESE CODES, JUST
0*$PUT CONSTANTS HERE IN THE CALL STATEMENT, E.G.
0*$CALL INTRDR(FILE,MSG,0,0);
 */
 INTRDR: PROC(TMPFILE,MSG,ERROR_CODE,INFO_CODE) OPTIONS(REENTRANT);
   %INCLUDE @9,@9SYSOUT,@9WRITER,@9DDNRET,@9CLOSE,@9ALLOC,@9DCLSVC;
   DCL TMPFILE FILE;
   DCL DDNAME CHAR(8);
   DCL MSG CHAR(*) VAR;
   DCL (ERROR_CODE,INFO_CODE) FIXED BIN(15,0);
   MSG='';
   @9SYSOUT('*');
   @9WRITER('INTRDR');
   @9DDNRET(DDNAME);
   @9CLOSE;
   @9ALLOC(ERROR_CODE,INFO_CODE);
   IF ERROR_CODE ^= 0
     THEN DO;
       PUT STRING(MSG) EDIT
           ('ERROR ALLOCATING INTERNAL READER, CODE = ') (A)
           (ERROR_CODE)                                  (F(5))
           ('  INFO_CODE = ')(A)              (INFO_CODE)(F(5));
       RETURN;
       END;
   OPEN FILE(TMPFILE) TITLE(DDNAME);
   END INTRDR;
