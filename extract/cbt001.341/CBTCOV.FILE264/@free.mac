 /* @FREE */
 %NOPRINT;
         % @FREEI = 'Y';
         % DCL @FREE ENTRY;
         % IF @DA18SW ^= 'Y'
              % THEN %DO;
                   % @DA18SW = 'Y';
                   % INCLUDE DAIR18;
                   DCL D18PTR PTR;
                   D18PTR = ADDR(DAIR18);
                   % END;
 % @FREE:  PROC(P1,P2,P3,P4,P5,P6,P7,P8) STATEMENT RETURNS(CHAR);
 /*
0*$.HE GENERALIZED SUBROUTINES - @FREE
0*$.HE PLI PREPROCESSOR TEXT
0*$.HE DYNAMICALLY ALLOCATE A DATA SET
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$09/09/76
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  @FREE
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS PLI PREPROCESSOR PROCEDURE CAN BE INVOKED TO GENERATE THE
0*$NECESSARY PLI STATEMENTS TO DYNAMICALLY FREE A DATA SET.
0*$CALL FORMAT IS AS FOLLOWS -
0*$.SP 1
0*$.IN 5
0*$@FREE(KEYWORD-PARAMETER-LIST);
0*$.IN 0
0*$.SP 1
0*$KEYWORD-PARAMETER-LIST MAY BE A MIXTURE OF THE FOLLOWING
0*$PARAMETERS.  NOTE THAT THE PARAMETERS MAY BE SPECIFIED IN ANY
0*$ORDER IN THE LIST, AND THAT KEYWORDS MAY BE ABBREVIATED TO ANY
0*$DEPTH THAT STILL PROVIDES UNIQUENESS.
0*$.SP 1
0*$DATASET(DSNAME)
0*$.IN 5
0*$SPECIFIES THE NAME OF THE DATA SET THAT IS TO BE ALLOCATED.
0*$STANDARD TSO CONVENTIONS ARE OBSERVED -
0*$.SP 1
0*$EXAMPLE 1:  DA('SYS1.PROCLIB')
0*$.IN 10
0*$REFERENCES DATA SET SYS1.PROCLIB.
0*$.IN 5
0*$EXAMPLE 2:  DA(JCL.CNTL)
0*$.IN 10
0*$REFERENCES DATA SET USERID.JCL.CNTL.
0*$IF THE KEYWORD IS SPECIFIED WITHOUT A PARENTHESIZED
0*$NAME SUFFIXED, THE NAME POINTER FIELD IN THE FREE
0*$CONTROL BLOCK WILL BE SET TO ZERO.
0*$.IN 0
0*$FILE(NAME)
0*$.IN 5
0*$SPECIFIES THE DDNAME TO BE ASSOCIATED WITH THE DATA SET.  IF
0*$THE DATASET KEYWORD IS ALSO SPECIFIED, THE FILE KEYWORD WILL BE
0*$IGNORED.  IF NEITHER DATASET NOR FILE ARE SPECIFIED, IT IS
0*$ASSUMED THAT THE DAIR18 BLOCK IS ALREADY SET CORRECTLY.
0*$.IN 0
0*$SYSOUT(CLASS)
0*$.IN 5
0*$IF THE DATASET WAS ALLOCATED AS A SYSOUT DATA SET, THIS KEYWORD
0*$CAN BE USED TO SPECIFY THE SYSOUT CLASS TO WHICH IT WILL BE
0*$QUEUED WHEN FREED.
0*$.IN 0
0*$PERMANENT
0*$.IN 5
0*$IF THIS KEYWORD IS SPECIFIED, PERMANENETLY ALLOCATED DATA SETS
0*$ARE MARKED "NOT IN USE" BUT ARE NOT UNALLOCATED.
0*$.IN 0
0*$.NF
0*$KEEP
0*$DELETE
0*$CATLG
0*$UNCATLG
0*$.FI L
0*$DISPOSITION OF DATA SET.  IF NONE SPECIFIED, DEFAULT IS KEEP.
 */
         DCL (P1,P2,P3,P4,P5,P6,P7,P8) CHAR;
         DCL KEYTAB CHAR;
         KEYTAB = 'DATASET,FILE,SYSOUT,PERMANENT,KEEP,DELETE,CATLG'××
           ',UNCATLG';
         DCL INDX FIXED;
         DCL (RETSTR,PMASK,PARM,TEXT,DSNLAB) CHAR;
         DCL (I,J,K,L,M) FIXED;
         PMASK='000000000';
         RETSTR='DO; DA18FLG=''0''B; DA18DARC,DA18CTRC=0; '××
           'DA18CTL=''00010000''B; DA18JBNM='''';';
         DO I = 1 TO 8;
              IF I = 1 THEN PARM = P1; ELSE
              IF I = 2 THEN PARM = P2; ELSE
              IF I = 3 THEN PARM = P3; ELSE
              IF I = 4 THEN PARM = P4; ELSE
              IF I = 5 THEN PARM = P5; ELSE
              IF I = 6 THEN PARM = P6; ELSE
              IF I = 7 THEN PARM = P7; ELSE
              IF I = 8 THEN PARM = P8;
              IF PARM = ''
                   THEN GO TO ENDLP;
              PARM=@STRIP(PARM);
              TEXT = @SPARS(PARM,KEYTAB,INDX);
              IF INDX ^= 0
                   THEN PMASK=SUBSTR(PMASK,1,INDX-1)××'1'××
                     SUBSTR(PMASK,INDX+1,8-INDX);
              L = LENGTH(TEXT);
              IF INDX = 0
                   THEN DO;
                        RETSTR='PARAMETER #'××I××'IS IN ERROR';
                        RETURN(RETSTR);
                        END;
              IF INDX = 1 /* TEST FOR DATASET KEYWORD */
                   THEN IF L = 0
                        THEN DO;
                             PMASK='0'××SUBSTR(PMASK,2,7);
                             RETSTR=RETSTR××' DA18PDSN=@NULLZ;';
                             END;
                        ELSE DO;
                             IF SUBSTR(TEXT,1,1) = ''''
                                  THEN DO;
                                       TEXT = SUBSTR(TEXT,2,L-2);
                                       L = L -2;
                                       RETSTR = RETSTR ××
                                        ' SUBSTR(DA18CTL,3,1)=''0''B;';
                                       END;
                                  ELSE RETSTR = RETSTR ××
                                   ' SUBSTR(DA18CTL,3,1)=''1''B;';
                             J = INDEX(TEXT,'(');
                             IF J = 0
                                  THEN RETSTR=RETSTR××' DA08MNM='''';';
                                  ELSE DO;
                                       RETSTR=RETSTR××' DA08MNM='''××
                                        SUBSTR(TEXT,J+1,L-J-1)××''';';
                                       L = J - 1;
                                       END;
                             M = L + 4;
                             DSNLAB = @NULL;
                             RETSTR=RETSTR××' DCL 1 '××DSNLAB××
                              ' STATIC, 2 ' ×× @NULL ××
                              ' FIXED BIN(15,0) INIT(' ×× M ×× '), 2' ××
                              @NULL ×× ' FIXED BIN(15,0) INIT(0), 2 ' ××
                              @NULL××' CHAR(44) INIT('''××
                              SUBSTR(TEXT,1,L) ×× '''); DA18PDSN=ADDR('
                              ×× DSNLAB ×× ');';
                             END;
              ELSE
              IF INDX = 2 /* TEST FOR FILE KEYWORD */
                 THEN IF L > 0
                   THEN RETSTR = RETSTR ×× ' DA18DDN=''' ×× TEXT ××
                     ''';';
                   ELSE;
              ELSE
              IF INDX = 3 /* TEST FOR SYSOUT KEYWORD */
                   THEN RETSTR = RETSTR ×× ' DA18SCLS=''' ×× TEXT ××
                     ''';';
              ELSE
              IF INDX = 4 /* TEST FOR PERMANENT KEYWORD */
                   THEN RETSTR=RETSTR××' SUBSTR(DA18CTL,4,1)=''1''B;';
              ELSE
              IF INDX = 5 /* TEST FOR KEEP */
                   THEN RETSTR=RETSTR××' DA18DPS2=''00001000''B;';
              ELSE
              IF INDX = 6 /* TEST FOR DELETE */
                   THEN RETSTR=RETSTR××' DA18DPS2=''00000100''B;';
              ELSE
              IF INDX = 7 /* TEST FOR CATLG */
                   THEN RETSTR=RETSTR××' DA18DPS2=''00000010''B;';
              ELSE
              IF INDX = 8 /* TEST FOR UNCATLG */
                   THEN RETSTR=RETSTR××' DA18DPS2=''00000001''B;';
              END;
 ENDLP:
         IF SUBSTR(PMASK,1,2) ^= '00' /* DATASET AND FILE */
              THEN IF SUBSTR(PMASK,1,1) = '0'
                   THEN RETSTR=RETSTR××' DA18PDSN=@NULLZ; '××
                     'DA18MNM='''';';
                   ELSE RETSTR=RETSTR××' DA18DDN='''';';
         IF SUBSTR(PMASK,3,1) = '0' /* SYSOUT */
              THEN RETSTR=RETSTR××' DA18SCLS='''';';
         IF SUBSTR(PMASK,5,4) = '0000' /* DISPOSITION */
              THEN RETSTR=RETSTR ×× 'DA18DPS2=''00001000''B;';
         RETSTR=RETSTR××' CALL SYSDAIR(CPPLUPT,CPPLECT,CPECB,'××
           'CPPLPSCB,DAIR18,IKJRET);';
         RETSTR=RETSTR ×× ' END;';
         RETURN(RETSTR);
         % END @FREE;
 %PRINT;
