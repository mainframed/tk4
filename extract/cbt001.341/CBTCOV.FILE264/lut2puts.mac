LUT2PUT  CSECT                                                      TGS
********************************************************************TGS
*                  THIS RTNE WILL BE RESPONSIBLE FOR CREATING THE  *TGS
*              SYSUT2 DATA SET.  AT ENTRY, R1 WILL CONTAIN THE     *TGS
*              ADDR OF THE NEXT RECORD, AND R0 WILL CONTAIN THE    *TGS
*              LENGTH OF THAT RECORD, UNLESS IT IS EOJ, IN WHICH   *TGS
*              CASE R0 WILL BE ZERO.                               *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         LR    R2,R0                   R2 = RECORD LENGTH           TGS
         LR    R3,R1                   R3 = RECORD ADDR             TGS
         TM    INITSW,X'80'                                         TGS
         BC    1,GO                                                 TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    8,RETCTL                BR IF YES                    TGS
         OI    INITSW,X'80'                                         TGS
         OPEN  (SYSUT2,(OUTPUT))                                    TGS
         TM    SYSUT2+48,X'10'         IS IT GOOD OPEN              TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1111,DUMP                                            TGS
GOODOPN  EQU   *                                                    TGS
         LH    R0,SYSUT2+62            R0 = BLKSIZE                 TGS
         STH   R0,BUFLEN                                            TGS
         GETMAIN R,LV=(0)                                           TGS
         ST    R1,BUFADDR                                           TGS
         ST    R1,NXTADDR                                           TGS
         B     PUTREC                                               TGS
GO       EQU   *                                                    TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    8,PUTBLK                BR IF YES                    TGS
PUTREC   EQU   *                                                    TGS
         SR    R4,R4                   R4 = 0                       TGS
         LR    R5,R2                   R2 = RECORD LENGTH           TGS
         D     R4,=F'253'                                           TGS
         LTR   R4,R4                   WAS THERE A REMAINDER        TGS
         BC    8,NOREM                                              TGS
         LA    R5,1(R5)                                             TGS
NOREM    EQU   *                                                    TGS
         MH    R5,=H'3'                                             TGS
         LA    R5,4(R2,R5)             R5 = MAX COMPR LEN           TGS
RETRY    L     R4,NXTADDR                                           TGS
         S     R4,BUFADDR                                           TGS
         SH    R4,BUFLEN                                            TGS
         LPR   R4,R4                   R4 = LEN REM IN BUFFER       TGS
         CR    R4,R5                   IS THERE ROOM IN BUFFER      TGS
         BC    4,PUTBLK                BR IF BUFFER IS FULL         TGS
         L     R1,NXTADDR                                           TGS
         STM   R2,R3,0(R1)             SET PARM FOR COMPRESS        TGS
         L     RF,=V(GCOMPR)                                        TGS
         BALR  RE,RF                   GO COMPRESS RECORD INTO BUFFETGS
         L     R1,NXTADDR                                           TGS
         AH    R1,0(R1)                R1 = NEU NXTADDR             TGS
         STC   R1,WORDCK                                            TGS
         TM    WORDCK,X'03'                                         TGS
         BC    8,ONWORD                                             TGS
         SRL   R1,2                                                 TGS
         LA    R1,1(R1)                                             TGS
         SLL   R1,2                                                 TGS
ONWORD   EQU   *                                                    TGS
         ST    R1,NXTADDR                                           TGS
         B     RETCTL                                               TGS
PUTBLK   EQU   *                                                    TGS
         L     RA,NXTADDR                                           TGS
         S     RA,BUFADDR              RA = BLK LEN                 TGS
         L     R6,BUFADDR                                           TGS
         WRITE DECB,SF,,(6),(10),MF=E                               TGS
         CHECK DECB                                                 TGS
         MVC   NXTADDR,BUFADDR         RESET NXTADDR                TGS
         LTR   R2,R2                   IS IT EOJ                    TGS
         BC    7,RETRY                 BR IF NOT                    TGS
         CLOSE SYSUT2                                               TGS
         LH    R0,BUFLEN                                            TGS
         L     R1,BUFADDR                                           TGS
         FREEMAIN R,LV=(0),A=(1)                                    TGS
         B     RETCTL                                               TGS
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=W,EXLST=NMEXITS,        TGSC
               RECFM=U                                              TGS
         DS    0F                                                   TGS
NMEXITS  DC    X'07'                                                TGS
         DC    AL3(0)                                               TGS
         DC    X'85'                   DCB EXIT IND = 05            TGS
         DC    AL3(NMEXIT)                                          TGS
NMEXIT   EQU   *                                                    TGS
         LH    R9,SYSUT2+62            R9 = BLKSIZE                 TGS
         LTR   R9,R9                   IS BLKSIZE ZERO              TGS
         BCR   7,RE                    BR IF NOT                    TGS
         MVC   SYSUT2+62(2),=H'32000'  USE A DEFAULT OF 32K         TGS
         BR    RE                                                   TGS
         WRITE DECB,SF,SYSUT2,MF=L                                  TGS
BUFLEN   DC    H'0'                                                 TGS
BUFADDR  DC    F'0'                                                 TGS
NXTADDR  DC    F'0'                                                 TGS
INITSW   DC    X'00'                                                TGS
WORDCK   DS    CL1                                                  TGS
         LTORG                                                      TGS
         END
