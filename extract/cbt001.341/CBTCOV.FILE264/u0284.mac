*$.HE ISD GENERALIZED SUBROUTINES - TESTCP
*$.HE INTERFACE TO PLI CHECKOUT COMPILER
*$.HE FOR COMMAND PROCESSORS
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$8/29/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  U0284
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE CAN BE USED TO TEST COMMAND PROCESSORS WRITTEN IN
*$PLI USING THE CHECKOUT COMPILER.  THE NORMAL SEQUENCE OF SOURCE
*$STATEMENTS FOR A COMMAND PROCESSOR ARE AS FOLLOWS:
*$.SP 1
*$.IN 5
*$.NF
*$CP:  PROC(CPPL1,CPPL2,CPPL3,CPPL4) OPTIONS(MAIN);
*$  % INCLUDE IKJCPPL
*$.FI L
*$.SP 1
*$.IN 0
*$TO USE THE CHECKOUT COMPILER TO DEBUG A COMMAND PROCESSOR,
*$REPLACE THE ABOVE STATEMENTS IN YOUR PROGRAM WITH THE FOLLOWING:
*$.SP 1
*$.IN 5
*$.NF
*$TEST:  PROC OPTIONS(MAIN);
*$  % INCLUDE IKJCPPL;
*$  CALL TESTCP(CPPLCBUF,CPPLUPT,
*$    CPPLPSCB,CPPLECT);
*$.SP 1
*$.IN 0
*$.FI L
*$AT LINKEDIT TIME, SPECIFY TESTCPE AS THE ENTRY POINT.  WHEN THE
*$MODULE IS INVOKED (USE THE TEST COMMAND SPECIFYING THE CP KEYWORD),
*$YOU WILL BE PROMPTED FOR PARAMETERS TO THE CHECKER BY THE
*$MESSAGE 'ENTER CHECKER PARAMETERS.'.
TESTCPE  CSECT
         SAVES RETCTL
         TGSREQU
         ST    R1,SAVCPPL
         LM    R2,R4,4(R1)             R2=A(UPT),R4=A(ECT)
         PUTGET PARM=PGPB,UPT=(R2),ECT=(R4),ECB=MYECB,MF=(E,IOPL)
         L     R1,PGPB+12              R1=A(INPUT BUFFER)
         LH    R2,0(R1)                R2=LENGTH BUF
         SH    R2,=H'4'                LESS 4 FOR HDR
         STH   R2,2(R1)                SET PARM LENGTH
         BC    8,NOXC                  BR IF NULL LINE
         BCTR  R2,0                    LESS 1 FOR EX
         EX    R2,SETCAPS
NOXC     DS    0H
         LA    R1,2(R1)
         ST    R1,MYPARM
         OI    MYPARM,X'80'
         LA    R1,MYPARM
         L     RF,=V(PLISTART)
         BALR  RE,RF
         B     RETCTL
SETCAPS  OC    4(0,R1),=80X'40'
         DROP  RC
         ENTRY TESTCP
TESTCP   DS    0H
         USING *,RF
         STM   RE,RC,12(RD)
         LM    R2,R5,0(R1)
         L     R1,SAVCPPL
         LM    R6,R9,0(R1)
         ST    R6,0(R2)
         ST    R7,0(R3)
         ST    R8,0(R4)
         ST    R9,0(R5)
         LM    RE,RC,12(RD)
         BR    RE
PARM     DC    H'0'
SAVCPPL  DS    F
PGPB     PUTGET OUTPUT=(OLD),TERMPUT=(HOLD),MF=L
OLD      DC    F'1'
         DC    A(MSG)
MSG      DC    AL2(MSGEND-*),AL2(0),C'     ENTER CHECKER PARAMETERS'
MSGEND   DS    0C
IOPL     DC    4F'0'
MYECB    DC    F'0'
MYPARM   DC    F'0'
         LTORG
         END
