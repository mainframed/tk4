*$.HE GENERALIZED SUBROUTINES - STFSMOD
*$.HE INTERFACE TO STFSMODE MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$4/17/80
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  STFSMOD
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE IS AN INTERFACE TO THE STFSMODE MACRO, WHICH IS
*$USED UNDER TSO/VTAM TO SPECIFY WHETHER AN IBM 3270 DISPLAY
*$TERMINAL IS TO OPERATE IN FULL SCREEN MODE.  CALL FORMAT IS
*$AS FOLLOWS:
*$.SP 1
*$.IN 5
*$.NF
*$DCL STFSMOD ENTRY(CHAR(*),CHAR(*),
*$  FIXED BIN(15,0))
*$  RETURNS(FIXED BIN(15,0));
*$I = STFSMOD('ON','INITIAL=YES',0);
*$.FI L
*$.IN 0
*$.SP 1
*$THE FIRST PARAMETER MAY BE EITHER 'ON' OR 'OFF'.  'ON' INDICATES
*$THAT FULL SCREEN MODE IS IN OPERATION.  'OFF' INDICATES THAT FULL
*$SCREEN MODE IS NOT IN OPERATION.
*$.SP 1
*$THE SECOND PARAMETER MAY BE EITHER 'INITIAL=YES' OR 'INITIAL=NO'.
*$'INITIAL=YES' INDICATES THAT THIS IS THE FIRST TIME DURING THE
*$EXECUTION OF A COMMAND PROCESSOR THAT THE COMMAND PROCESSOR HAS
*$ENTERED FULL SCREEN MODE.  THIS OPERAND PREVENTS THE FIRST TPUT
*$FULLSCR ISSUED BY THE COMMAND PROCESSOR FROM FORCING A PAGING
*$CONDITION WHEN THE LAST TRNASACTION AT THE TERMINAL WAS INPUT.
*$'INITIAL=NO' INDICATES THAT FORCED PAGING IS TO OCCUR NORMALLY
*$WHENEVER A TPUT WITH FULLSCR FOLLOWS A TPUT WITHOUT FULLSCR.
*$.SP 1
*$THE THIRD PARAMETER INDICATES THE PROGRAM FUNCTION KEY TO BE
*$USED AS THE RESHOW KEY.  IF ZERO IS SPECIFIED, THE SYSTEM DEFAULT
*$(PFK 9) IS USED.
*$.SP 1
*$THE SUBROUTINE RETURNS THE RETURN CODE SET IN REGISTER 15
*$AFTER THE STFSMODE MACRO IS ISSUED.
********************************************************************
STFSMOD  PLIENTRY ,COMPILE=O
         LM    R2,R5,0(R1)
         L     R2,0(R2)           R2=A(ON/OFF)
         L     R3,0(R3)           R3=A(INITIAL=YES/NO)
         LH    R4,0(R4)           R4=RSHWKEY
*                                 R5=A(RETURN CODE)
         SR    R1,R1
         CLC   0(2,R2),=C'ON'
         BC    7,DOSVC            BR IF STFSMODE OFF
         LA    R1,128             INDICATE STFSMODE ON
         CLC   0(10,R3),=C'INITIAL=NO'
         BC    8,BYINIT
         LA    R1,64(R1)          INDICATE INITIAL=YES
BYINIT   DS    0H
         SLL   R1,24              SHIFT TO HIGH ORDER BYTE
         OR    R1,R4              LOAD RSHWKEY
DOSVC    DS    0H
         LA    0,18               LOAD ENTRY CODE
         SLL   0,24               SHIFT TO HIGH ORDER BYTE
         SVC   94
         STH   RF,0(R5)           SET RETURN CODE
         PLIEXIT
         END
