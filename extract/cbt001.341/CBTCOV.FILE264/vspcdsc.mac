 /* VSPCDSC */
 /* THIS PROGRAM IS USED WHEN ARCHIVING VSPC DATA SETS.  IT IS A
    FINAL CHECK TO MAKE SURE THAT THE DATA SET WAS SUCCESSFULLY
    STORED ON TAPE.   THE RETURN CODE WILL BE SET TO 16 IF THE
    ARCHIVE WAS NOT SUCCESSFUL. */
 VSPCDSC:  PROC(PARM) OPTIONS(MAIN);
         DCL PARM CHAR(100) VAR;
         % INCLUDE CTLGDEF;
         DCL VSPCDSN CHAR(44);
         DCL PLIRETC BUILTIN;
         DCL 1 ABND STATIC,
           2 DUM BIN FLOAT(53),
           2 MAREA CHAR(265) INIT('');
         DCL RET BIT(32);
         ON ERROR BEGIN;
           PUT EDIT('YOUR DATA SET WAS NOT SUCCESSFULLY ARCHIVED.')(A)
             SKIP(1);
           PUT EDIT('SAVE THE OUTPUT FROM THIS JOB AND CONTACT THE')(A)
             SKIP(1);
           PUT EDIT('TIME-SHARING SERVICES SUPPORT GROUP.')(A)
             SKIP(1);
           CALL PLIRETC(16);
           GO TO EOJ;
           END;
         VSPCDSN = PARM;
         CTLGPTR = ADDR(MAREA);
         CALL SYSCTLG('NAME',RET,VSPCDSN,'SYSRES',MAREA);
         IF RET^='0'B
           THEN SIGNAL ERROR;
         IF SUBSTR(VLSTBLK.VOLID.VOLSER(1),1,1) ^= '0'
           THEN SIGNAL ERROR;
         PUT EDIT('YOUR DATA SET HAS BEEN ARCHIVED ON TAPE ',
           VLSTBLK.VOLID.VOLSER(1),' UNDER THE DSNAME ',VSPCDSN)
           (A,A,A,A) SKIP(1);
         CALL PLIRETC(0);
 EOJ:
         END;
