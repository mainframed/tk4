 /* IWTLIST */
0*$.HE GENERALIZED TSO COMMANDS - IWTLIST
0*$.HE DETAILED IWT REPORT GENERATOR
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$10/02/78
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  U0247A
0*$DOCUMENTATION ID:  0*$
0*$HELP ID:  0*#
0*$.FI L
0*$.SP 5
0*$THIS COMMAND INTERFACES TO THE PROGRAM U0247, WHICH GENERATES A
0*$DETAILED REPORT OF IWT USAGE BASED ON SELECTION CRITERIA
0*$SPECIFIED ON THE IWTLIST COMMAND.  IF IS WRITTEN IN PLI, AND
0*$THE FOLLOWING SEQUENCE OF COMMANDS SHOULD BE USED TO
0*$COMPILE/LINKEDIT THE COMMAND:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$PLI U0247A M LIB('SYS2.MACLIB') OBJ CS(60)
0*$LINK (U0247A *) LOAD('SYS3.COMLIB(IWTLIST)') -
0*$  LIB('SYS2.PLIBASE') PLIB PR(*)
0*$ENTER LINK EDIT CONTROL STATEMENTS -
0*$ ENTRY PLICALLA
0*#)F FUNCTION
0*# USE THE IWTLIST COMMAND TO OBTAIN A LIST OF JOBS BASED
0*# ON CRITERIA ESTABLISHED BY KEYWORDS OF THE IWTLIST COMMAND.
0*# NOTE THAT A LIST OF JOBS FOR ONE DAY WILL TYPICALLY COST
0*# ABOUT $2.50 IN COMPUTER CHARGES, AND A LIST COVERING AN
0*# ENTIRE MONTH WILL COST ABOUT $15.00.
0*#)X SYNTAX
0*# IWTLIST TERM DAILY MONTH() YY() SUN MON TUE WED THU FRI SAT -
0*#         JOBNAME() IWTMAX() JOBCLAS() ACCT() LOTME() HITME() -
0*#         LODTE() HIDTE() PRIME OPTIONS DIV() AT() JOBCARD() -
0*#         OVERDUE
0*# EXAMPLES -
0*#   IWTLIST TERM JOBN(EUP) IWT(10) JOBCL(E) ACCT(10-338)
0*#      (LISTS AT THE TERMINAL ALL JOBS RUN ON THE PREVIOUS
0*#       WORKDAY THAT HAVE JOB NAMES BEGINNING WITH THE
0*#       CHARACTERS "EUP", AND THAT HAVE REQUESTED IWT VALUES
0*#       LESS THAN OR EQUAL TO 10 MINUTES, AND THAT HAVE A JOB CLASS
0*#       OF "E", AND HAVE AN ACCOUNT NUMBER BEGINNING WITH THE
0*#       CHARACTERS "10-338".)
0*#   IWTLIST MONTH(CURRENT) DIV(02-03-10)
0*#      (SUBMITS A BATCH JOB TO LIST ALL JOBS RUN SO FAR IN THE
0*#       CURRENT MONTH FOR DIVISIONS 2, 3, AND 10.)
0*#
0*# NOTE:  TO SEE HOW TO CREATE YOUR OWN JCL TO OBTAIN A LISTING
0*#        IN BATCH WITHOUT THE USE OF THE IWTLIST COMMAND,
0*#        ENTER "HELP IWTLIST O(BATCH)".
0*#
0*#)O OPERANDS
0*#))DAILY - SPECIFYING THIS KEYWORD INDICATES THAT THE SMF DATA USED
0*#    AS INPUT SHOULD BE THAT GENERATED ON THE PREVIOUS WORK DAY.
0*#    IF THE CURRENT WORK DAY IS MONDAY, THEN THE COMBINED SMF DATA
0*#    FROM SATURDAY AND SUNDAY WILL BE USED.  (THE DATA SET NAME
0*#    ACTUALLY USED WILL BE U.SD.U000.SMFDATA).
0*#))MONTH() - THIS KEYWORD IS USED TO INDICATE THAT SMF DATA FOR AN
0*#    ENTIRE MONTH WILL BE USED AS INPUT.  NOTE THAT THIS KEYWORD
0*#    CANNOT BE USED IF THE "TERM" KEYWORD IS ALSO SPECIFIED.
0*#    "MONTH(JAN)" INDICATES THAT THE LISTING IS TO BE PRODUCED FROM
0*#    THE SMF DATA FOR JANUARY.  OTHER POSSIBLE MONTH SPECIFICATIONS
0*#    ARE FEB, MAR, APR, MAY, JUN, JUL, AUG, SEP, OCT, NOV, DEC, AND
0*#    CURRENT.  NOTE THAT IF THE CURRENT MONTH IS JULY, AND YOU WANT
0*#    THE REPORT FROM THE SMF DATA GENERATED SO FAR IN JULY, SPECIFY
0*#    "MONTH(CURRENT)" INSTEAD OF "MONTH(JUL)".  NOTE THAT USING THE
0*#    "MONTH" KEYWORD WILL RESULT IN A BATCH JOB BEING SUBMITTED
0*#    TO PRODUCE THE LISTING.  THE "JOBCARD" KEYWORD MAY BE USED TO
0*#    INDICATE THE JOB STATEMENT TO BE USED FOR THIS JOB.  SEE ALSO
0*#    THE "YY" KEYWORD, WHICH MAY BE USED TO SPECIFY THE YEAR FOR
0*#    THE MONTH TO BE LISTED.
0*#))YY() - THIS KEYWORD IS USED IN CONJUNCTION WITH THE "MONTH"
0*#    KEYWORD.  IF YOU DON'T SPECIFY THE "YY" KEYWORD, THE YEAR
0*#    FOR THE MONTH TO BE LISTED WILL DEFAULT TO THE CURRENT YEAR.
0*#    TO INDICATE THE YEAR 1977, FOR EXAMPLE, SPECIFY "YY(77)".
0*#))TERM - USE THIS KEYWORD IF YOU WANT THE REPORT TO BE LISTED AT
0*#    THE TERMINAL, RATHER THAN IN THE SYSOUT FOR A BATCH JOB.
0*#    NOTE THAT ONLY DATA FOR THE PREVIOUS WORKDAY (SEE HELP DATA
0*#    FOR THE "DAILY" OPERAND) MAY BE LISTED DIRECTLY AT THE
0*#    TERMINAL.  IF "TERM" IS SPECIFIED, THE KEYWORDS "MONTH" AND
0*#    "YY" WILL BE IGNORED.
0*#))SUN - SEE HELP FOR "SAT" KEYWORD.
0*#))MON - SEE HELP FOR "SAT" KEYWORD.
0*#))TUE - SEE HELP FOR "SAT" KEYWORD.
0*#))WED - SEE HELP FOR "SAT" KEYWORD.
0*#))THU - SEE HELP FOR "SAT" KEYWORD.
0*#))FRI - SEE HELP FOR "SAT" KEYWORD.
0*#))SAT - USE THIS KEYWORD IN CONJUNCTION WITH THE SUN, MON, TUE
0*#    WED, THU, FRI, AND SAT KEYWORDS TO LIMIT THE LISTING TO
0*#    PARTICULAR DAYS OF THE WEEK.
0*#))JOBNAME() - USE THIS KEYWORD TO LIMIT THE LISTING TO JOBS WITH
0*#    PARTICULAR CHARACTERS IN SPECIFIC POSITIONS IN THEIR JOB NAMES.
0*#    SPECIFYING "JOBNAME(EUP)", FOR EXAMPLE, WOULD LIMIT THE LISTING
0*#    TO THOSE JOBS WITH NAMES BEGINNING WITH "EUP".
0*#    "JOBNAME(EUP...A)" WOULD LIMIT THE LISTING TO JOBS WITH NAMES
0*#    HAVING THE CHARACTERS "EUP" IN POSITIONS 1 TO 3, AND THE
0*#    CHARACTER "A" IN POSITION 7.
0*#))IWTMAX() - USED TO LIMIT THE LISTING TO JOBS WITH A REQUESTED
0*#    IWT LESS THAN OR EQUAL TO THE SPECIFIED VALUE.  "IWT(20)" FOR
0*#    EXAMPLE, LIMITS THE LISTING TO THOSE JOBS WITH IWT VALUES
0*#    REQUESTED OF LESS THAN OR EQUAL TO 20 MINUTES.
0*#))JOBCLAS() - (MISPELLING INTENTIONAL - CAN ONLY BE 7 CHARACTERS).
0*#    USED TO LIMIT THE LISTING TO JOBS WITH PARTICULAR
0*#    JOB CLASSES.  "JOBCLAS(C-E)" FOR EXAMPLE, LIMITS THE LISTING
0*#    TO JOBS IN CLASSES C AND E.  NOTE THAT ANY NUMBER OF JOB
0*#    CLASSES CAN BE SPECIFIED, WITH EACH CLASS DELIMITED BY A
0*#    HYPHEN AS SHOWN.
0*#))ACCT() - USED TO LIMIT THE LISTING TO JOBS WITH A PARTICULAR
0*#    ACCOUNT NUMBER.  "ACCT(10-338)" FOR EXAMPLE, LIMITS THE
0*#    LISTING TO THOSE JOBS WITH ACCOUNT NUMBERS BEGINNING WITH
0*#    THE CHARACTERS "10-338".  "ACCT(10.....DETL)" WOULD LIMIT
0*#    THE LISTING TO ACCOUNT NUMBERS WITH THE CHARACTERS "10"
0*#    IN POSITIONS 1 AND 2, AND THE CHARACTERS "DETL" IN
0*#    POSITIONS 8 THRU 11.
0*#))LOTME() - USED IN CONJUNCTION WITH THE "HITME" KEYWORD TO
0*#    LIMIT THE LISTING TO JOBS WHICH WERE COMPLETED DURING
0*#    A SPECIFIC RANGE OF TIME.  BOTH "LOTME" AND "HITME" ARE
0*#    EXPRESSED IN MINUTES.  "LOTME(400) HITME(960)" FOR EXAMPLE,
0*#    INDICATES THAT ONLY JOBS COMPLETED BETWEEN 0800 AND 1600
0*#    HOURS ARE TO BE LISTED.  IF LOTME OR HITME IS NOT SPECIFIED,
0*#    THE DEFAULTS ARE "LOTME(0) HITME(9999)".
0*#))HITME() - SEE HELP DATA FOR THE "LOTME" OPERAND.
0*#))LODTE() - USED IN CONJUNCTION WITH THE "HIDTE" KEYWORD TO
0*#    LIMIT THE LISTING TO JOBS WHICH WERE COMPLETED DURING A
0*#    A RANGE OF DAYS.  "LODTE(77180) HIDTE(77187)" FOR EXAMPLE,
0*#    INDICATES THAT ONLY JOBS COMPLETED BETWEEN THE DATES 77180
0*#    AND 77187 ARE TO BE LISTED.  NOTE THAT THE DATES CAN ALSO
0*#    BE SPECIFIED IN GREGORIAN FORM (MMDDYY).
0*#    "LODTE(062977) HIDTE(070677)" FOR EXAMPLE, IS EXACTLY THE
0*#    SAME SPECIFICATION AS IN THE PREVIOUS EXAMPLE IN WHICH
0*#    JULIAN DATES WERE USED.  IF LODTE OR HIDTE IS NOT SPECIFIED,
0*#    THE DEFAULTS ARE "LODTE(0) HIDTE(99999)'.
0*#))HIDTE() - SEE HELP DATA FOR THE "LODTE" OPERAND.
0*#))PRIME - SPECIFYING THIS KEYWORD IS THE EQUIVALENT OF
0*#    SPECIFYING "MON TUE WED THU FRI LOTME(400) HITME(960)".
0*#))OPTIONS - SPECIFYING THIS KEYWORD CAUSES THE OPTIONS USED
0*#    TO BE LISTED ON THE FIRST PAGE OF THE REPORT.  NOTE THAT
0*#    ONLY OPTIONS PERTAINING TO SELECTION CRITERIA WILL BE
0*#    PRINTED.  THAT IS, VALUES FOR THE DAILY, MONTHLY, AND
0*#    JOBCLASS KEYWORDS WILL NOT BE PRINTED.
0*#))DIV() - USE THIS KEYWORD TO LIMIT THE LISTING TO JOBS IN
0*#    WITH SPECIFIED DIVISION CODES IN THE FIRST TWO POSITIONS
0*#    OF THE ACCOUNT NUMBER.  "DIV(02-03)" FOR EXAMPLE, LIMITS
0*#    THE LISTING TO THOSE JOBS WITH DIVISION CODES "02" AND "03"
0*#    IN POSITIONS 1 AND 2 OF THE ACCOUNT NUMBER.  NOTE THAT
0*#    AS MANY DIVISION NUMBERS AS DESIRED MAY BE SPECIFIED, WITH
0*#    EACH ONE DELIMITED WITH A HYPHEN AS SHOWN IN THE EXAMPLE.
0*#))AT() - USE THIS KEYWORD TO SPECIFY THE JES REMOTE STATION
0*#    AT WHICH THE LISTING IS TO BE PRINTED.  "AT(REMOTE15)" FOR
0*#    EXAMPLE, SPECIFIES THE LISTING BE PRINTED AT REMOTE15.
0*#    THIS KEYWORD IS IGNORED IF THE "TERM" KEYWORD IS ALSO
0*#    SPECIFIED.  THE DEFAULT IS "AT(LOCAL)".
0*#))JOBCARD() - USED TO SPECIFY THE NAME OF A DATA SET CONTAINING
0*#    A VALID JOB STATEMENT IF THE LISTING IS TO BE PRODUCED IN
0*#    BATCH.  THE DEFAULT IS "JOBCARD(JOBCARD.CNTL)".
0*#))OVERDUE - SPECIFY THIS KEYWORD IF YOU WANT A LIST OF JOBS THAT
0*#    WERE REPORTED ON THE IWT REPORT AS OVERDUE.  NOTE THAT SOME JOBS
0*#    WILL APPEAR THAT SHOW AN IWT VALUE RECEIVED LESS THAN THE
0*#    IWT VALUE REQUESTED.  THESE JOBS ARE ONES THAT THE CONSOLE
0*#    OPERATOR INCREASED THE PRIORITY ON AFTER THEY WERE READ INTO
0*#    THE INPUT QUEUE.  THIS CAUSES THEM TO BE REPORTED AS OVERDUE.
0*#))BATCH - THERE IS NO "BATCH" KEYWORD ON THE IWTLIST COMMAND.
0*#    THIS IS A KEY TO AN EXPLANATION OF HOW TO OBTAIN AN
0*#    IWT REPORT IN BATCH WITHOUT THE AID OF THE IWTLIST
0*#    COMMAND UNDER TSO.  THE NECESSARY JCL IS AS FOLLOWS:
0*#
0*#      // EXEC PGM=U0247,PARM='/&OPERANDS'
0*#      //STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR
0*#      //SYSPRINT DD SYSOUT=A
0*#      //SMFDATA DD DSN=&DSN,DISP=SHR
0*#
0*#    WHERE -
0*#      &OPERANDS ARE ANY OF THE KEYWORD OPERANDS ALLOWABLE
0*#         ON THE IWTLIST COMMAND, EXCEPT "TERM", "DAILY",
0*#         "MONTH", "YY", "AT", AND "JOBCARD".  EACH OPERAND
0*#         SHOULD BE DELIMITED BY A COMMA OR A BLANK.
0*#         "PARM='/JOBNAME(EUP) IWT(10)' " IS AN EXAMPLE.  NOTE
0*#         THAT ANY OF THE KEYWORDS IN THE PARM FIELD MAY BE
0*#         ABBREVIATED, SO LONG AS THEY REMAIN UNIQUE.
0*#      &DSN IS THE NAME OF THE INPUT SMF DATA SET.  FOR THE
0*#         PREVIOUS WORK DAY (AS DESCRIBED UNDER THE HELP DATA
0*#         FOR THE "DAILY" KEYWORD), USE DATA SET
0*#         U.SD.U000.SMFDATA.  FOR THE CURRENT MONTH, USE DATA
0*#         SET U.SD.U000.MVSSMFMO(0).  FOR ANY OTHER MONTH, USE
0*#         A DATA SET NAME OF THE FORM U.SD.U000.SMF.YEAR&YY..&MON
0*#         WHERE &YY IS THE TWO-DIGIT YEAR (78, FOR EXAMPLE),
0*#         AND &MON IS JAN, FEB, MAR, APR, MAY, JUN, JUL, AUG,
0*#         SEP, OCT, NOV, OR DEC.  TO OBTAIN THE SMF DATA FOR
0*#         JANUARY, 1978, FOR EXAMPLE, SPECIFY THE DATA SET NAME
0*#         U.SD.U000.SMF.YEAR78.JAN.
0*#))LENGTH - "LENGTH" IS NOT A LEGITIMATE KEYWORD OF THE IWTLIST
0*#    COMMAND.  IT IS MERELY A KEY TO THIS EXPLANATION OF THE
0*#    LENGTH RESTRICTION ON THE SELECTION OPTIONS YOU
0*#    SPECIFY ON THE IWTLIST COMMAND.  THE SELECTION OPTIONS
0*#    YOU SPECIFY ARE PASSED TO A REPORT PROGRAM IN THE PARM FIELD
0*#    ON THE EXEC JCL STATEMENT.  IF YOU SPECIFIED
0*#    "IWTLIST DAILY JOBNAME(EUP) JOBCLAS(C-E)" FOR EXAMPLE,
0*#    THE RESULTING JCL EXEC STATEMENT WOULD APPEAR AS
0*#    "// EXEC PGM=U0247,PARM='/ JOBN(EUP) JOBC(C-E)'".  YOU
0*#    WILL NOTICE THAT THE KEYWORD OPTIONS ARE ABBREVIATED AS
0*#    MUCH AS POSSIBLE,  BUT YOU ARE STILL LIMITED TO A TOTAL OF
0*#    58 CHARACTERS IN THE PARM FIELD.
 */
         % INCLUDE CPINIT1;
         % @CMP = 'O';
         % INCLUDE CPINIT2;
 @PROC(0,DAILY,MONTH(),YY(),TERM,SUN,MON,TUE,WED,THU,FRI,SAT,
  JOBNAME(),IWTMAX(),JOBCLAS(),ACCT(),LOTME(),HITME(),LODTE(),
  HIDTE(),PRIME,OPTIONS,DIV(),AT(LOCAL),JOBCARD(JOBCARD),OVERDUE);
         DCL KEYTAB(19) CHAR(7) VAR STATIC INIT(
           'SUN','MON','TUE','WED','THU','FRI','SAT',
           'JOBNAME','IWTMAX','JOBCLAS','ACCT','LOTME',
           'HITME','LODTE','HIDTE','PRIME','OPTIONS','DIV','OVERDUE');
         DCL KEYABR(19) CHAR(7) VAR STATIC INIT(
           'SU', 'M',  'TU', 'W',  'TH', 'F',  'SA',
           'JOBN',   'I',     'JOBCL',  'A',   'LOT',
           'HIT',  'LOD',  'HID',  'P',    'OP',     'D',  'OV');
         DCL PARM CHAR(100) VAR INIT('');
         DCL RETVAL CHAR(20) VAR;
         DCL DSN CHAR(44) VAR;
         DCL MUN CHAR(7) VAR;
         DCL Y CHAR(2);
         DCL JCLA(11) CHAR(80) STATIC INIT(
           '/*MESSAGE      ************ 1-9TRK *********',
           '/*SETUP        &DSN',
           '/*ROUTE  PRINT &AT',
           '// EXEC PGM=U0247,',
           '// PARM=''/&PARM''',
           '//STEPLIB DD DSN=U.SD.U000.TGSLOAD,DISP=SHR',
           '//SYSPRINT DD SYSOUT=*',
           '//SMFDATA DD DSN=&DSN,DISP=SHR',
           '//SORTWK01 DD UNIT=DISK,SPACE=(TRK,(10))',
           '//SORTWK02 DD UNIT=DISK,SPACE=(TRK,(10))',
           '//SORTWK03 DD UNIT=DISK,SPACE=(TRK,(10))');
         DCL REC CHAR(80);
         DCL TMPFILE FILE RECORD OUTPUT ENV(FB RECSIZE(80)
           BLKSIZE(800));
         DO I = 1 TO 19;
           RETVAL = SETVAL(I);
           IF RETVAL ^= ''
             THEN PARM = PARM××' '××RETVAL;
           END;
         IF LENGTH(PARM) > 60
           THEN DO;
             @WRITE('ERROR - LENGTH OF OPTIONS IS TOO LONG.');
             SIGNAL ERROR;
             END;
         @SETL(&PARM = PARM);
         IF TERM ^= 0
          THEN DO;
           @CMD('ALLOC FI(SMFDATA) DA(''U.SD.U000.SMFDATA'') SHR REUS');
           @CMD('ALLOC FI(SYSPRINT) DA(*) REUSE');
           @CMD('ALLOC FI(SORTWK01) TR SP(10) NEW REUSE');
           @CMD('ALLOC FI(SORTWK02) TR SP(10) NEW REUSE');
           @CMD('ALLOC FI(SORTWK03) TR SP(10) NEW REUSE');
           @CMD('CALL ''U.SD.U000.TGSLOAD(U0247)'' ''/&PARM''');
           @CMD('FREE FI(SMFDATA SORTWK01 SORTWK02 SORTWK03)');
           END;
          ELSE DO;
           IF DAILY ^= 0 × SYMC('&MONTH')=''
            THEN DSN = 'U.SD.U000.SMFDATA';
            ELSE DO;
             MUN = SYMC('&MONTH');
             IF MUN = 'CURRENT'
              THEN DSN = 'U.SD.U000.MVSSMFMO(0)';
              ELSE DO;
               Y = SYMC('&YY');
               IF Y = ''
                THEN Y = DATE;
               DSN = 'U.SD.U000.SMF.YEAR'××Y××'.'××MUN;
               END;
             END;
           @SETL(&DSN = DSN);
           @ALLOC(DA(BPRT.CNTL),WORK);
           OPEN FILE(TMPFILE) TITLE(DA08DDN);
           DO I = 1 TO 11;
            @REP(REC=JCLA(I));
            WRITE FILE(TMPFILE) FROM(REC);
            END;
           CLOSE FILE(TMPFILE);
           @CMD('SUBMIT (&JOBCARD BPRT)');
           @CMD('DELETE BPRT.CNTL');
           END;
 SETVAL:  PROC(I) RETURNS(CHAR(20) VAR);
         DCL I FIXED BIN(15,0);
         DCL VAL CHAR(20) VAR;
         VAL = SYMC('&'××KEYTAB(I));
         SELECT(VAL);
           WHEN('') RETURN('');
           WHEN(KEYTAB(I)) RETURN(KEYABR(I));
           OTHERWISE RETURN(KEYABR(I)××'('××VAL××')');
           END;
         END SETVAL;
         END;
