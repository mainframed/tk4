         MACRO
&NAME PLISIGNL &TYPE,   * ON-CONDITION, SUCH AS ERROR OR FINISH        C
               &COND=,&FILE=,&COMPILE=
         GBLC  &COMP
         LCLA  &ARITH,&I
         LCLC  &CNM(26),&BYTE1,&BYTE2,&BYTE3,&NAM,&NAM1,&NAM2
&CNM(1)  SETC  ' '
&CNM(2)  SETC  'ZERODIVIDE'                 02
&CNM(3)  SETC  'FIXEDOVERFLOW'              03
&CNM(4)  SETC  'SIZE'                       04
&CNM(5)  SETC  'CONVERSION'                 050040
&CNM(6)  SETC  'OVERFLOW'                   06
&CNM(7)  SETC  'UNDERFLOW'                  07
&CNM(8)  SETC  'STRINGSIZE'                 08
&CNM(9)  SETC  'STRINGRANGE'                09
&CNM(10) SETC  'SUBSCRIPTRANGE'             0A
&CNM(11) SETC  'AREA'                       0B02
&CNM(12) SETC  'ERROR'                      0C
&CNM(13) SETC  'FINISH'                     0D
&CNM(14) SETC  ' ' CHECK -NOT SUPPORTED     0E
&CNM(15) SETC  'CONDITION'                  0F
&CNM(16) SETC  'KEY'                        100091
&CNM(17) SETC  'RECORD'                     110091
&CNM(18) SETC  'UNDEFINEDFILE'              120091
&CNM(19) SETC  'ENDFILE'                    130091
&CNM(20) SETC  'TRANSMIT'                   140091
&CNM(21) SETC  'NAME'                       150098
&CNM(22) SETC  'ENDPAGE'                    160090
&CNM(23) SETC  ' '
&CNM(24) SETC  ' '
&CNM(25) SETC  ' ' PENDING - NOT SUPPORTED
&CNM(26) SETC  ' ' ATTENTION - NOT SUPPORTED
         PLICOMP &COMPILE
         AIF   ('&COMP' EQ 'O').OPT
         AIF   ('&TYPE' EQ 'ERROR').ERR     * SIGNAL ERROR
         AIF   ('&TYPE' EQ 'FINISH').FIN    * SIGNAL FINISH
         AIF   ('&TYPE' EQ 'CONDITION').CONDCKF
         AIF   ('&TYPE' NE '').SERR1
.CONDCKF ANOP
         AIF   ('&COND' EQ '').SERR2
&ARITH   SETA  K'&COND                      * SIGNAL CONDITION(NAME)
         AIF   (&ARITH LE 8).OK
         MNOTE 12,' ** CONDITION NAME MUST BE LE 8 CHARS'
         MEXIT
.SERR1   MNOTE 12,' ** ONLY ''ERROR'' OR ''FINISH'' ARE VALID '
         MEXIT
.SERR2   MNOTE 12,' ** CONDITION TO SIGNAL MUST BE SPECIFIED '
         MEXIT
.OK      ANOP
&NAME    L     15,=V(&COND)                 * SIGNAL CONDITION
         ST    15,PL1PARMW
         MVI   PL1PARMW,X'50'
         LA    1,PL1PARMW
         L     15,=V(IHEERRD)
         BALR  14,15
         MEXIT
.ERR     ANOP
&NAME    LA    1,0(12)                      * SIGNAL ERROR
         ORG   *-2
         DC    QL2(IHEQERR)
         MVI   0(1),X'60'
         L     15,=V(IHEERRB)
         BALR  14,15
         MEXIT
.FIN     ANOP
&NAME    LA    1,0(12,0)                    * SIGNAL FINISH
         ORG   *-2
         DC    QL2(IHEQERR)
         MVI   0(1),X'58'
         L     15,=V(IHEERRB)
         BALR  14,15
         MEXIT
.OPT     ANOP
         AIF   ('&NAME' EQ '').NONAM
&NAME    EQU   *
.NONAM   ANOP
         XC    PL1PARMW,PL1PARMW       CLEAR ICB
         AIF   ('&TYPE' NE '').LOOPI
&I       SETA  15
         AGO   .GOTI
.LOOPI   ANOP
&I       SETA  1
&NAM     SETC  '&TYPE'
.LOOP    ANOP
         AIF   ('&CNM(&I)' EQ '&NAM').GOTI
&I       SETA  &I+1
         AIF   (&I LE 22).LOOP
         MNOTE 12,'THE ON-CONDITION TYPE IS NOT RECOGNIZED '
         MEXIT
.GOTI    ANOP
&BYTE1   SETC  '&I'
         MVI   PL1PARMW,&BYTE1
&BYTE2   SETC  '00'
&BYTE3   SETC  '00'
         AIF   (&I GT 15).IOCOND
         AIF   (&I EQ 15).COND
         AIF   (&I EQ 5).CONV
         AIF   (&I EQ 11).AREA
.ENDUP   ANOP
         LA    R1,PL1PARMW             R1 = ADDR ICB
         L     RF,120(RC)              RF = ADDR ERROR HANDLER
         BALR  RE,RF
         MEXIT
.MVC     ANOP
         MVC   PL1PARMW+1(2),=X'&BYTE2&BYTE3'
         AGO   .ENDUP
.AREA    ANOP
&BYTE2   SETC  '02'
         AGO   .MVC
.CONV    ANOP
&BYTE3   SETC  '40'
         AGO   .MVC
.COND    ANOP
         AIF   ('&COND' NE '').SETNAMC
         MNOTE 12,' ** THE ON-CONDITION TYPE ''CONDITION'' HAS BEEN '
         MNOTE 12,' ** SPECIFIED WITHOUT SPECIFYING THE COND= OPERAND.'
         MEXIT
.SETNAMC ANOP
         AIF   ('&COND'(1,1) EQ '(').REGC
&NAM     SETC  '&COND'
&ARITH   SETA  K'&COND
         AGO   .SETNAM
.IOCOND  ANOP
         AIF   (&I GT 20).CKNE
&BYTE3   SETC  '91'
         AGO   .CKNAMI
.CKNE    ANOP
         AIF   (&I EQ 21).GOT21
&BYTE3   SETC  '90'
         AGO   .CKNAMI
.GOT21   ANOP
&BYTE3   SETC  '98'
.CKNAMI  ANOP
         AIF   ('&FILE' NE '').SETNAMI
         MNOTE 12,' ** AN I/O ON-CONDITION TYPE HAS BEEN SPECIFIED '
         MNOTE 12,' ** WITHOUT THE ''FILE='' OPERAND. '
         MEXIT
.SETNAMI ANOP
         AIF   ('&FILE'(1,1) EQ '(').REGI
&NAM     SETC  '&FILE'
&ARITH   SETA  K'&FILE
.SETNAM  ANOP
         AIF   (&ARITH LT 8).GOODNAM
&NAM1    SETC  '&NAM'(1,4)
&NAM2    SETC  '&NAM'(&ARITH-2,3)
         MNOTE 4,'NAME ''&NAM'' HAS BEEN SHORTENED TO ''&NAM1&NAM2''.'
&NAM     SETC  '&NAM1&NAM2'
.GOODNAM ANOP
         L     R1,=V(&NAM)
.ST      ANOP
         ST    R1,PL1PARMW+4
         AGO   .MVC
.REGC    ANOP
         LA    R1,0&COND
         AGO   .ST
.REGI    ANOP
         LA    R1,0&FILE
         AGO   .ST
         MEND
