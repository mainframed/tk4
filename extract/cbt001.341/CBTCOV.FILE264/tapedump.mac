*$.HE GENERALIZED UTILITIES - TAPEDUMP
*$.HE TAPEDUMP - TAPE COPY UTILITY
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/6/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  TAPEDUMP
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$TAPEDUMP UTILITY CAN BE USED TO DUMP A TAPE TO PRINTER.  INPUT IS
*$DDNAME IN, AND OUTPUT IS SNAPDD.
*$PARM FIELD CAN BE SET TO NUMBER OF RECORDS TO BE PRINTED - DEFAULT
*$IS 255.
TAPEDUMP CSECT
         SAVES RETCTL
         TGSREQU
         LA    RA,255 RA=DEFAULT NUMBER OF RECORDS TO READ
         L     R1,0(R1) R1=A(HALFWORD LENGTH)
         LH    R2,0(R1) R2=LENGTH
         SH    R2,=H'1' GET SET FOR EX
         BC    4,NOPARM BR IF NOT PARM
         EX    R2,PACKPRM
         B     BYEX
PACKPRM  PACK  DBLETM(8),2(0,R1)
DBLETM   DS    D
BYEX     DS    0H
         CVB   RA,DBLETM
NOPARM   DS    0H
         LA    R7,TDAREA
         SR    R9,R9
         OPEN  (SNAPDCB,(OUTPUT))
         OPEN  (INDCB,(INPUT))
         TM    INDCB+48,X'10'          IS IT GOOD OPEN?
         BC    1,OPNOK                 BR IF OK
         LA    1,1
ABEND    DS    0H
         ABEND (R1),DUMP
OPNOK    DS    0H
         MVI   SETMODE,X'03'
         MVC   INCCW(8),SETMODE        SET IN SETMODE CCW
         MVC   INCCW+8(8),TDINCCW      SET READ CCW
         BAL   RB,IOIN                 GO READ VOL1 FROM INPUT TAPE
TTINIT   MVI   INSTAT+4,0
TTLOOP   DS    0H
         BAL   RB,IOIN                 GO READ A TAPE BLOCK?
         B     TTLOOP                  LOOP
DORWND   DS    0H
         CLOSE (INDCB,REWIND)
         SR    RF,RF                   SET RC = 0
         B     RETCTL                  ALL DONE
IOIN     DS    0H
         CR    R9,RA                   HAVE ALL RECORDS BEEN READ?
         BC    8,DORWND                BR IF YES
         MVC   VVSTAT,INSTAT+4         SAVE LAST IOB STAT
         SR    R0,R0
         ST    R0,INECB                CLEAR ECB
         NI    INDCB+44,X'3F'          RESET IO FLAGS
         EXCP  INIOB                   DO I/O
         WAIT  ECB=INECB               WAIT FOR I/O
         CLI   INECB,X'7F'             READ OK?
         BC    8,SETLEN                BR IF YES
         LA    R1,2                    SET ABEND CODE
         TM    INSTAT+4,X'01'          UNIT EXCEPTION?
         BC    8,ABEND                 BR IF NOT - TAPE ERROR
         MVC   TDAREA(8),=C'TAPEMARK'
         LA    R8,8(R7)
         B     SNAP
SETLEN   DS    0H
         LH    R2,INSTAT+6             R2 = RESIDUAL BYTE COUMT
         LH    R3,TDINCCW+6            R3 = ORIGINAL BYTE COUNT
         SR    R3,R2                   R3 = BYTES READ
         LA    R8,0(R7,R3)
         LA    R9,1(R9)
SNAP     DS    0H
         SNAP  DCB=SNAPDCB,STORAGE=((R7),(R8))
         BR    RB
EOVCK    DC    X'00'
SNAPDCB  DCB   DDNAME=SNAPDD,DSORG=PS,RECFM=VBA,MACRF=(W),LRECL=125,   C
               BLKSIZE=882
*
* DCB FOR TAPEIN
*
INDCB    DCB   DDNAME=IN,MACRF=(E),DSORG=PS,DEVD=TA
*
* ECB FOR TAPEIN
*
INECB    DC    F'0'
*
* IOB FOR INPUT DATA SET
*
INIOB    DS    0F
         DC    X'4200'
INSENS   DC    H'0'
         DC    X'7F'
         DC    AL3(INECB)
INSTAT   DC    2F'0'
         DC    A(INCCW)
         DC    A(INDCB)
         DC    F'0'
         DC    H'1'
         DC    H'0'
INDASD   DC    X'00'
INSEEK   DC    XL7'00'
*
* CCWS FOR INPUT DATA SET
*
INCCW    DS    5D
VVSTAT   DC    X'00'
NOP      CCW   3,0,X'20',1             NOP CCW
SETMODE  CCW   *-*,0,X'60',1           SKELETON SETMODE CCW
TDINCCW  CCW   2,TDAREA,X'20',32767    READ CCW
BSCCW    CCW   X'27',BSCCW,X'60',1     BACKSPACE BLOCK CCW
WTCCW    CCW   31,TDAREA,X'20',1       WRITE TAPE MARK
TTCCWOUT CCW   1,TDAREA,X'20',32767    WRITE DATA
RWCCW    CCW   7,RWCCW,X'20',1
         LTORG
         DS    0D
TDAREA   DS    80CL250
         DS    40CL250
         DS    28CL100 32800 BYTES
         END
