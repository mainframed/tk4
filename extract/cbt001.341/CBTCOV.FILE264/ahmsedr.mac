   /* AHMSEDR - HARDWARE MANAGEMENT SYSTEM EDIT RETRIEVE */
   %INCLUDE ABESUB;
 ABESUB(AHMSEDR) CHECK(YES);
   %INCLUDE ABESUBA;
   %INCLUDE AHMSEDI;

   EXEC SQL INCLUDE SQLCA;
   DCL CALCPRT ENTRY(DEC FLOAT(16))
     RETURNS(CHAR(80) VAR);
   DCL (PLIRETV,DIM) BUILTIN;
   DCL DSNTIAR ENTRY OPTIONS(ASM,INTER,RETCODE);
   DCL DATA_LEN FIXED BIN(31) STATIC INIT(120);
   DCL DATA_DIM FIXED BIN(31) STATIC INIT(8);
   DCL 1 ERROR_MESSAGE,
     2 ERROR_LEN FIXED BIN(15,0) UNAL INIT((DATA_LEN*DATA_DIM)),
     2 ERROR_TEXT(DATA_DIM) CHAR(DATA_LEN);
   DCL MSG CHAR(130) VAR;

   EXEC SQL WHENEVER SQLWARNING CONTINUE;
   EXEC SQL WHENEVER SQLERROR GOTO SQLERROR;
   ON CONDITION(BADSAVE) GOTO SQLERROR;
   GOTO BY_SQLERROR;
 SQLERROR:
   IF MSGDATA=''
     THEN DO;
       PUT STRING(MSG) EDIT('SQL ERROR, RETCODE=',SQLCODE)(A,F(10));
       CALL #TERM(MSG);
       FETCH DSNTIAR;
       CALL DSNTIAR(SQLCA,ERROR_MESSAGE,DATA_LEN);
       IF PLIRETV=0
         THEN DO I=1 TO DIM(ERROR_TEXT,1);
           CALL #TERM(ERROR_TEXT(I));
           END;
       MSGDATA='SQLERROR ENCOUNTERED';
       END;
   RHRC=16;
   RETURN;
 BY_SQLERROR:;

   CALL FEATDESC_DEFINE;
   CALL FEATURES_DEFINE;
   CALL LOCNAMES_DEFINE;
   CALL THINGTAB_DEFINE;
   CALL VENDORS_DEFINE;

 END_NOSAVE: PROC(KEY);
 /**********************************************************************
 *                                                                     *
 * CALLED TO DO AN "END NOSAVE" COMMAND FOR DATA SET.                  *
 *                                                                     *
 **********************************************************************/
   DCL KEY CHAR(*);
   DCL TABPTR PTR;
   DCL I FIXED BIN;

   TABPTR=LOCDB(KEY);
   IF TABPTR=NULL
     THEN RETURN;
   I=#CMD('END NOSAVE',TABPTR,RHHDRPT);
   END END_NOSAVE;

 FEATDESC_DEFINE: PROC RETURNS(PTR);
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXTRACT THE FEATDESC TABLE FROM DB2.                      *
 *                                                                     *
 **********************************************************************/
   DCL I FIXED BIN;
   DCL TABPTR PTR;
   FEATDESC_STRUCTURE;

   CALL END_NOSAVE('FEATDESC');
   TABPTR=RHPTR;
   I=#CMD('SSE '''' NAME(FEATDESC)',TABPTR,RHHDRPT);
   I=#CMD('USE INPUT FID 1-4 VENDOR $ 5-7 DEVICE $ 8-17 FEATURE $ 18-28
     FEATDESC $ 29-58',TABPTR,RHHDRPT);
   EXEC SQL
     DECLARE FDCURSOR CURSOR FOR
       SELECT FID,VENDOR,DEVICE,FEATURE,FEATDESC
         FROM SYNFEATD ORDER BY FID;
   EXEC SQL OPEN FDCURSOR;
   DO FOREVER=1 REPEAT FOREVER;
     EXEC SQL FETCH FDCURSOR INTO :ROA:ROA_IND;
     IF SQLCODE^=0
       THEN LEAVE; /* END-OF-FILE */
     I=#CMD('B BL 1',TABPTR,RHHDRPT);
     I=#CMD('B V'   ,TABPTR,RHHDRPT);
     CALL PUTDATA('FID'     ,TABPTR,CALCPRT(FID));
     IF ROA_IND(VENDORIX)<0
       THEN CALL PUTDATA('VENDOR'  ,TABPTR,'.');
       ELSE CALL PUTDATA('VENDOR'  ,TABPTR,VENDOR);
     IF ROA_IND(DEVICEIX)<0
       THEN CALL PUTDATA('DEVICE'  ,TABPTR,'.');
       ELSE CALL PUTDATA('DEVICE'  ,TABPTR,DEVICE);
     IF ROA_IND(FEATUREIX)<0
       THEN CALL PUTDATA('FEATURE' ,TABPTR,'.');
       ELSE CALL PUTDATA('FEATURE' ,TABPTR,FEATURE);
     IF ROA_IND(FEATDESCIX)<0
       THEN CALL PUTDATA('FEATDESC',TABPTR,'.');
       ELSE CALL PUTDATA('FEATDESC',TABPTR,FEATDESC);
     END;
   EXEC SQL CLOSE FDCURSOR;
   I=#CMD('TOP',TABPTR,RHHDRPT);
   END FEATDESC_DEFINE;

 FEATURES_DEFINE: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXTRACT THE FEATURES TABLE FROM DB2.                      *
 *                                                                     *
 **********************************************************************/
   DCL TABPTR PTR;
   FEATURES_STRUCTURE;
   DCL I FIXED BIN;

   CALL END_NOSAVE('FEATURES');
   TABPTR=RHPTR;
   I=#CMD('SSE '''' NAME(FEATURES)',TABPTR,RHHDRPT);
   I=#CMD('USE INPUT ID 1-4 FID 5-8 COST $ 9-15 QTY 16-19',
     TABPTR,RHHDRPT);
   EXEC SQL
     DECLARE FECURSOR CURSOR FOR
       SELECT ID,FID,COST,QTY
         FROM SYNFEATU ORDER BY ID,FID;
   EXEC SQL OPEN FECURSOR;
   DO FOREVER=1 REPEAT FOREVER;
     EXEC SQL FETCH FECURSOR INTO :ROB:ROB_IND;
     IF SQLCODE^=0
       THEN LEAVE; /* END-OF-FILE */
     I=#CMD('B BL 1',TABPTR,RHHDRPT);
     I=#CMD('B V'   ,TABPTR,RHHDRPT);
     IF ROB_IND(IDIX)<0
       THEN CALL PUTDATA('ID'  ,TABPTR,'.');
       ELSE CALL PUTDATA('ID'  ,TABPTR,CALCPRT(ID));
     IF ROB_IND(FIDIX)<0
       THEN CALL PUTDATA('FID' ,TABPTR,'.');
       ELSE CALL PUTDATA('FID' ,TABPTR,CALCPRT(FID));
     IF ROB_IND(COSTIX)<0
       THEN CALL PUTDATA('COST',TABPTR,'.');
       ELSE CALL PUTDATA('COST',TABPTR,COST);
     IF ROB_IND(QTYIX)<0
       THEN CALL PUTDATA('QTY',TABPTR,'.');
       ELSE CALL PUTDATA('QTY',TABPTR,CALCPRT(QTY));
     END;
   EXEC SQL CLOSE FECURSOR;
   I=#CMD('TOP',TABPTR,RHHDRPT);
   END FEATURES_DEFINE;

 LOCNAMES_DEFINE: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXTRACT THE LOCNAMES TABLE FROM DB2.                      *
 *                                                                     *
 **********************************************************************/
   DCL TABPTR PTR;
   LOCNAMES_STRUCTURE;
   DCL I FIXED BIN;

   CALL END_NOSAVE('LOCNAMES');
   TABPTR=RHPTR;
   I=#CMD('SSE '''' NAME(LOCNAMES)',TABPTR,RHHDRPT);
   I=#CMD('USE INPUT LOCATION $ 1-20 LOCNO 21-26 SPEED 27-32',
     TABPTR,RHHDRPT);
   EXEC SQL
     DECLARE LOCURSOR CURSOR FOR
       SELECT LOCATION,LOCNO,SPEED
         FROM SYNLOCNA ORDER BY LOCNO;
   EXEC SQL OPEN LOCURSOR;
   DO FOREVER=1 REPEAT FOREVER;
     EXEC SQL FETCH LOCURSOR INTO :ROC:ROC_IND;
     IF SQLCODE^=0
       THEN LEAVE; /* END-OF-FILE */
     I=#CMD('B BL 1',TABPTR,RHHDRPT);
     I=#CMD('B V'   ,TABPTR,RHHDRPT);
     IF ROC_IND(LOCATIONIX)<0
       THEN CALL PUTDATA('LOCATION' ,TABPTR,'.');
       ELSE CALL PUTDATA('LOCATION' ,TABPTR,LOCATION);
     IF ROC_IND(LOCNOIX)<0
       THEN CALL PUTDATA('LOCNO'    ,TABPTR,'.');
       ELSE CALL PUTDATA('LOCNO'    ,TABPTR,CALCPRT(LOCNO));
     IF ROC_IND(SPEEDIX)<0
       THEN CALL PUTDATA('SPEED'    ,TABPTR,'.');
       ELSE CALL PUTDATA('SPEED'    ,TABPTR,CALCPRT(SPEED));
     END;
   EXEC SQL CLOSE LOCURSOR;
   I=#CMD('TOP',TABPTR,RHHDRPT);
   END LOCNAMES_DEFINE;

 THINGTAB_DEFINE: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXTRACT THE THINGTAB TABLE FROM DB2.                      *
 *                                                                     *
 **********************************************************************/
   DCL TABPTR PTR;
   THINGTAB_STRUCTURE;
   DCL I FIXED BIN;

   CALL END_NOSAVE('THINGTAB');
   TABPTR=RHPTR;
   I=#CMD('SSE '''' NAME(THINGTAB)',TABPTR,RHHDRPT);
   I=#CMD('USE INPUT ID 1-4 CID 5-8 NODE $ 9-16 LTERM $ 18-21
     LOCNO 23-28 EXT 29-32 ROOM $ 33-40 VENDOR $ 41-43 DEVICE $ 44-53
     SERIAL $ 54-68',
     TABPTR,RHHDRPT);
   EXEC SQL
     DECLARE THCURSOR CURSOR FOR
       SELECT ID,CID,NODE,LTERM,LOCNO,EXT,ROOM,VENDOR,DEVICE,SERIAL
         FROM SYNTHING ORDER BY ID;
   EXEC SQL OPEN THCURSOR;
   DO FOREVER=1 REPEAT FOREVER;
     EXEC SQL FETCH THCURSOR INTO :ROD:ROD_IND;
     IF SQLCODE^=0
       THEN LEAVE; /* END-OF-FILE */
     I=#CMD('B BL 1',TABPTR,RHHDRPT);
     I=#CMD('B V'   ,TABPTR,RHHDRPT);
     IF ROD_IND(IDIX)<0
       THEN CALL PUTDATA('ID'    ,TABPTR,'.');
       ELSE CALL PUTDATA('ID'    ,TABPTR,CALCPRT(ID));
     IF ROD_IND(CIDIX)<0
       THEN CALL PUTDATA('CID'   ,TABPTR,'.');
       ELSE CALL PUTDATA('CID'   ,TABPTR,CALCPRT(CID));
     IF ROD_IND(NODEIX)<0
       THEN CALL PUTDATA('NODE'  ,TABPTR,'.');
       ELSE CALL PUTDATA('NODE'  ,TABPTR,NODE);
     IF ROD_IND(LTERMIX)<0
       THEN CALL PUTDATA('LTERM' ,TABPTR,'.');
       ELSE CALL PUTDATA('LTERM' ,TABPTR,LTERM);
     IF ROD_IND(LOCNOIX)<0
       THEN CALL PUTDATA('LOCNO' ,TABPTR,'.');
       ELSE CALL PUTDATA('LOCNO' ,TABPTR,CALCPRT(LOCNO));
     IF ROD_IND(EXTIX)<0
       THEN CALL PUTDATA('EXT'   ,TABPTR,'.');
       ELSE CALL PUTDATA('EXT'   ,TABPTR,CALCPRT(EXT));
     IF ROD_IND(ROOMIX)<0
       THEN CALL PUTDATA('ROOM'  ,TABPTR,'.');
       ELSE CALL PUTDATA('ROOM'  ,TABPTR,ROOM);
     IF ROD_IND(VENDORIX)<0
       THEN CALL PUTDATA('VENDOR',TABPTR,'.');
       ELSE CALL PUTDATA('VENDOR',TABPTR,VENDOR);
     IF ROD_IND(DEVICEIX)<0
       THEN CALL PUTDATA('DEVICE',TABPTR,'.');
       ELSE CALL PUTDATA('DEVICE',TABPTR,DEVICE);
     IF ROD_IND(SERIALIX)<0
       THEN CALL PUTDATA('SERIAL',TABPTR,'.');
       ELSE CALL PUTDATA('SERIAL',TABPTR,SERIAL);
     END;
   EXEC SQL CLOSE THCURSOR;
   I=#CMD('TOP',TABPTR,RHHDRPT);
   END THINGTAB_DEFINE;

 VENDORS_DEFINE: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO EXTRACT THE VENDORS TABLE FROM DB2.                       *
 *                                                                     *
 **********************************************************************/
   DCL TABPTR PTR;
   VENDORS_STRUCTURE;
   DCL I FIXED BIN;

   CALL END_NOSAVE('VENDORS');
   TABPTR=RHPTR;
   I=#CMD('SSE '''' NAME(VENDORS)',TABPTR,RHHDRPT);
   I=#CMD('USE INPUT VENDOR $ 1-3 VENDNAME $ 5-18 VENDADDR $ 19-30
     VENDCITY $ 31-41 VENDSTAT $ 42-52 VENDZIP 53-57 VENDTELE $ 58-71',
     TABPTR,RHHDRPT);
   EXEC SQL
     DECLARE VECURSOR CURSOR FOR
       SELECT VENDOR,VENDNAME,VENDADDR,VENDCITY,VENDSTAT,VENDZIP,
         VENDTELE  FROM SYNVENDO ORDER BY VENDOR;
   EXEC SQL OPEN VECURSOR;
   DO FOREVER=1 REPEAT FOREVER;
     EXEC SQL FETCH VECURSOR INTO :ROE:ROE_IND;
     IF SQLCODE^=0
       THEN LEAVE; /* END-OF-FILE */
     I=#CMD('B BL 1',TABPTR,RHHDRPT);
     I=#CMD('B V'   ,TABPTR,RHHDRPT);
     IF ROE_IND(VENDORIX)<0
       THEN CALL PUTDATA('VENDOR'  ,TABPTR,'.');
       ELSE CALL PUTDATA('VENDOR'  ,TABPTR,VENDOR);
     IF ROE_IND(VENDNAMEIX)<0
       THEN CALL PUTDATA('VENDNAME',TABPTR,'.');
       ELSE CALL PUTDATA('VENDNAME',TABPTR,VENDNAME);
     IF ROE_IND(VENDADDRIX)<0
       THEN CALL PUTDATA('VENDADDR',TABPTR,'.');
       ELSE CALL PUTDATA('VENDADDR',TABPTR,VENDADDR);
     IF ROE_IND(VENDCITYIX)<0
       THEN CALL PUTDATA('VENDCITY',TABPTR,'.');
       ELSE CALL PUTDATA('VENDCITY',TABPTR,VENDCITY);
     IF ROE_IND(VENDSTATIX)<0
       THEN CALL PUTDATA('VENDSTAT',TABPTR,'.');
       ELSE CALL PUTDATA('VENDSTAT',TABPTR,VENDSTAT);
     IF ROE_IND(VENDZIPIX)<0
       THEN CALL PUTDATA('VENDZIP' ,TABPTR,'.');
       ELSE CALL PUTDATA('VENDZIP' ,TABPTR,CALCPRT(VENDZIP));
     IF ROE_IND(VENDTELEIX)<0
       THEN CALL PUTDATA('VENDTELE',TABPTR,'.');
       ELSE CALL PUTDATA('VENDTELE',TABPTR,VENDTELE);
     END;
   EXEC SQL CLOSE VECURSOR;
   I=#CMD('TOP',TABPTR,RHHDRPT);
   END VENDORS_DEFINE;

   END AHMSEDR;
