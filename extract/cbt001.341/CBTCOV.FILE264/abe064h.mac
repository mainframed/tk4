 /* COPYRIGHT 1986 BY THOMAS GLEN SMITH */
 /* ABE064H - PRINT GRAPHICS BITMAP TO IBM 3800 PRINTER */
   %INCLUDE ABESUB;
 ABESUB(ABE064H) PARMS(RHPTR,RHHDRPT,RHPLOT79,ABEPRT);
   %INCLUDE ABESUBA;

   DCL RHPLOT79 PTR;
   DCL ABEPRT FILE;

   %INCLUDE @HEXCHAR;
   DCL 1 DDI01 BASED(RHPLOT79),
   %INCLUDE ABE063I;

   /* PLOT79 MUST HAVE BEEN INITIALIZED WITH -

      GR DEVICE 240 240 P1 P2 1

   WHERE -
     P1=WIDTH IN PELS, DIVISIBLE BY 32, NORMALLY 2624
     P2=HEIGHT IN PELS, DIVISIBLE BY 32, NORMALLY 1792

   THE FOLLOWING COMMAND OR SOMETHING SIMILAR SHOULD ALSO HAVE BEEN
   EXECUTED -

      ALLOC FI(ABEPRT) SYSOUT REUSE LR(404) BL(408) RECFM(V A) REUSE

   */
   DCL PTRIN ENTRY(PTR)
     RETURNS(FIXED BIN(31,0));
   DCL PTROUT ENTRY(FIXED BIN(31,0))
     RETURNS(PTR);
   DCL (ICPREC,IRDREC,OUTREC) CHAR(400) VAR;
   DCL BITSTR CHAR(400) BASED(BITPTR);
   DCL IRDLEN CHAR(2);
   DCL CELLSIDE_BITS FIXED BIN(15,0) STATIC INIT(32);
   DCL CELLSIDE_BYTES FIXED BIN(15,0) STATIC INIT(4);
   DCL CELLSIZE FIXED BIN(15,0) STATIC INIT(128);
   DCL (AP,BP,CP) PTR;

   WIDTH=PLMAXX/CELLSIDE_BITS;
   WIDTH=WIDTH*CELLSIDE_BITS;
   HEIGHT=PLMAXY/CELLSIDE_BITS;
   HEIGHT=HEIGHT*CELLSIDE_BITS;
   IF WIDTH^=PLMAXX × HEIGHT^=PLMAXY
     THEN DO;
       PUT STRING(MSGDATA) EDIT(
         'WARNING - DIMENSIONS OF BITMAP SHOULD BE DIVISIBLE BY ',
         CELLSIDE_BITS)(A,P'ZZ9');
       RHRC=4;
       END;
   IF BITMAP_ELSIZE^=1
     THEN DO;
       MSGDATA=
         'ERROR - BITMAP MUST BE INITIALIZED WITH ELEMENT SIZE = 1';
       RHRC=16;
       RETURN;
       END;

   /* BEGIN DOCUMENT (BDT) */
   OUTREC=
     @HEXCHAR(5A0010D3A8A8000000C4D6C3F0F0F0F0F0);
   /*                           D O C 0 0 0 0 0  */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* BEGIN PAGE (BPG) */
   OUTREC=
     @HEXCHAR(5A0010D3A8AF000000D7C1C7F0F0F0F0F0);
   /*                           P A G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* BEGIN ACTIVE ENVIRONMENT GROUP (BAG) */
   OUTREC=
     @HEXCHAR(5A0010D3A8C9000000C1C5C7F0F0F0F0F0);
   /*                           A E G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* BEGIN PAGE DESCRIPTOR (PGD) */
   OUTREC=
     @HEXCHAR(5A0017D3A6AF00000000000960096000)××
       GETHALF(WIDTH)×× /* WIDTH IN PELS OF THE PAGE. */
                        /* OK RANGE 120-3330 */
       @HEXCHAR(00)××
       GETHALF(HEIGHT)×× /* HEIGHT IN PELS OF THE PAGE.  OK RANGE IS */
                         /* 120-2640, 2880, OR 3000 FOR 11, 12, OR   */
                         /* 12.5 INCH PAPER.                         */
       @HEXCHAR(0004A0);
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* COMPOSED TEXT CONTROL (CTC) */
   OUTREC=
     @HEXCHAR(5A0012D3A79B00000000000000000000002D00);
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* COMPOSED TEXT CONTROL (CTD) */
   OUTREC=
     @HEXCHAR(5A001AD3A69B000000000009600960)××
       GETHALF(WIDTH)×× /* WIDTH IN PELS OF COMPOSED TEXT BLOCK. MUST */
                        /* MATCH BYTES 6-8 OF THE PGD.                */
       GETHALF(HEIGHT)×× /* HEIGHT IN PELS OF COMPOSED TEXT BLOCK     */
                         /* MUST MATCH BYTES 9-11 OF THE PGD.         */
     @HEXCHAR(00000004A0000000);
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* END ACTIVE ENVIRONMENT GROUP - (EAG) */
   OUTREC=
     @HEXCHAR(5A0010D3A9C9000000C1C5C7F0F0F0F0F0);
   /*                           A E G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* BEGIN IMAGE DEFINITION (BIM) */
   OUTREC=
     @HEXCHAR(5A0010D3A87B000000C9D4C7F0F0F0F0F0);
   /*                           I M G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* IMAGE OUTPUT CONTROL (IOC) POSITION IMAGE */
   OUTREC=@HEXCHAR
   (5A0020D3A77B00000000000000000000002D00000000000000000003E803E8FFFF);
   /*                 ×     ×     ×       ×               ×   ×       */
   /*                 ×     ×     ×       ×               ×   VERTICAL*/
   /*                 ×     ×     ×       ×               ×   SC. F.  */
   /*                 ×     ×     ×       ×               ×           */
   /*                 ×     ×     ×       ×               HORIZONTAL  */
   /*                 ×     ×     ×       ×               SCALE FACT. */
   /*                 ×     ×     ×                                   */
   /*                 ×     ×     ORIENTATION - CAN'T BE CHANGED      */
   /*                 ×     ×                                         */
   /*                 ×     VERTICAL DISPLACEMENT IN PELS             */
   /*                 ×                                               */
   /*                 HORIZONTAL DISPLACEMENT IN PELS                 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* IMAGE INPUT DESCRIPTOR (IID) DEFINE IMAGE SIZE */
   OUTREC=
     @HEXCHAR(5A002CD3A67B000000000000000000000000000000000009600960)××
     GETHALF(WIDTH)×× /* IMAGE WIDTH IN PELS */
     GETHALF(HEIGHT)×× /* IMAGE HEIGHT IN PELS */
     @HEXCHAR(000000002D00)××
     GETHALF(CELLSIDE_BITS)×× /* DEFAULT WIDTH OF AN IMAGE CELL */
     GETHALF(CELLSIDE_BITS)×× /* DEFAULT HEIGHT OF AN IMAGE CELL */
     @HEXCHAR(0001FFFF);
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* NOW WRITE OUT IMAGE RASTER PATTERN */
   ROW_BYTE_SIZE=PLMAXX/8;
   IRDLEN=GETHALF(CELLSIZE+8); /* LENGTH FOR EACH IRD RECORD */
   J=HEIGHT-CELLSIDE_BITS;
   DO I=0 TO J BY CELLSIDE_BITS;
     /* LOOP FOR EACH ROW OF CELLS */
     DO K=0 TO WIDTH-CELLSIDE_BITS BY CELLSIDE_BITS;
       /* LOOP ONCE FOR EACH CELL IN CURRENT ROW */
       IRDREC='';
       DO M=CELLSIDE_BITS TO 1 BY -1;
         /* LOOP ONCE PER ROW OF CURRENT CELL */
         IRDREC=IRDREC××#PIXGETL(K+1,I+M,CELLSIDE_BITS,RHPLOT79);
         END;
       IF IRDREC^=LOW(CELLSIZE)
         THEN DO; /* CELL IS NOT EMPTY */
           /* IMAGE CELL POSITION (ICP) */
           ICPREC=@HEXCHAR(5A0014D3AC7B000000)××
             GETHALF(K)×× /*HORIZONTAL OFF. FROM ORIGIN OF IMAGE*/
             GETHALF(J-I)×× /*VERTICAL   OFF. FROM ORIGIN OF IMAGE*/
             GETHALF(CELLSIDE_BITS)×× /* WIDTH OF CELL */
             GETHALF(CELLSIDE_BITS)×× /* HEIGHT OF CELL */
             GETHALF(CELLSIDE_BITS)×× /* FILL SIZE WIDTH */
             GETHALF(CELLSIDE_BITS);  /* FILL SIZE HEIGHT */
           WRITE FILE(ABEPRT) FROM(ICPREC);

           /* IMAGE RASTER DATA (IRD) */
           OUTREC=@HEXCHAR(5A)××IRDLEN××@HEXCHAR(D3EE7B000000)××IRDREC;
           WRITE FILE(ABEPRT) FROM(OUTREC);
           END;
       END;
     END;

   /* END THE IMAGE DEFINITION */
   OUTREC=
     @HEXCHAR(5A0010D3A97B000000C9D4C7F0F0F0F0F0);
     /*                         I M G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* END PAGE (EPG) */
   OUTREC=
     @HEXCHAR(5A0010D3A9AF000000D7C1C7F0F0F0F0F0);
     /*                         P A G 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);

   /* END DOCUMENT (EDT)*/
   OUTREC=
     @HEXCHAR(5A0010D3A9A8000000C4D6C3F0F0F0F0F0);
     /*                         D O C 0 0 0 0 0 */
   WRITE FILE(ABEPRT) FROM(OUTREC);


 GETHALF: PROC(I) RETURNS(CHAR(2));
   DCL I FIXED BIN(15,0),
       IC CHAR(2) BASED(ADDR(I));
   RETURN(IC);
   END GETHALF;

   END ABE064H;
