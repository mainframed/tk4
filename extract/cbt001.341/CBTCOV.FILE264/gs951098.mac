*   **** END-OF-VOLUME EXIT ****
*
*   THIS EXIT GETS CONTROL WHEN EOV CONDITION IS SENSED.
*   THIS ROUTINE CAN BE CALLED FROM COBOL OR PL/I.
*   THE CALLING PROGRAM ISSUES ONLY ONE CALL TO THIS ROUTINE
*   AFTER THE FILE HAS BEEN OPENED.
*   THE CALLING PROGRAM PASSES THREE PARAMETERS -
*       1--- ADDRESS OF FILE (FILENAME)
*       2--- ADDRESS OF A ONE BYTE NUMERIC EOV INDICATOR
*       3--- ADDRESS OF A ONE BYTE NUMERIC INDICATOR
*            INDICATING TYPE OF LANGUAGE CALLING PROGRAM
*            IS WRITTEN IN
*   IF PL/I, THE FILE MUST BE RECORD I/O
*   IF COBOL, IT MUST BE A QUEUED FILE.
*
*
*
GS951098 CSECT
         ENTRY GS9510
GS9510   STM   14,12,12(13)  SAVE REGISTERS
         USING *,15
         ST    13,SAVE+4
         LR    11,13
         LA    13,SAVE
         ST    13,8(11)
NOPBR    BC    0,SETSW
         MVI   NOPBR,X'F0'
         L     6,8(0,1)      LOAD ADDR OF 3RD PARAMETER
         CLI   0(6),X'F1'    PL/I PROGRAM ?
         BE    PLI           YES
         L     4,0(0,1)      ADDR OF DCB
         L     5,ADEXIT
         STCM  5,7,37(4)     PUT EXIT RTN ADDR IN DCB
         B     SAVIND
PLI      L     2,0(0,1)      ADDR OF DCLCB
         LH    3,0(0,2)      PRV OFFSET
         L     4,0(3,12)     ADDR OF FCB
         L     5,ADEXIT
         STCM  5,7,75(4)     PUT EXIT RTN ADDR IN DCB
SAVIND   MVC   EOVIND(4),4(1)  SAVE ADDR OF EOV INDICATOR
         B     RETURN
SETSW    L     2,EOVIND
         MVI   0(2),X'F1'    SET EOV INDICATOR IN CALLING PROG
RETURN   L     13,4(13)
         LM    14,12,12(13)
         BR    14            RETURN TO OS
SAVE     DC    18A(0)
ADEXIT   DC    A(GS951098)
EOVIND   DC    F'0'
         END
