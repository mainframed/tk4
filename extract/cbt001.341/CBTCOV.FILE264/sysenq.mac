         GBLC  &COMP
*$.HE GENERALIZED SUBROUTINES - SYSENQ
*$.HE PLI INTERFACE TO ENQ MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSENQ
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
********************************************************************
*$THIS RTNE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE ENQ FACILITY OF OS.  THE FORMAT OF THE PLI STATEMENT USED
*$TO INVOKE THIS RTNE IS AS FOLLOWS:
**
*$.SP 1**
*$CALL SYSENQ (QNAME,RNAME,LEV,CTL,TYPE,RETURN,UCBPTR);
*$.SP 1**
*$.CE 1**
*$DEFINITION OF TERMS
*$.SP 1**
**
*$.IN 0
*$QNAME CHAR (8)
*$.IN 5
*$MAJOR Q NAME
**
*$.IN 0
*$RNAME CHAR (*)
*$.IN 5
*$MINOR Q NAME (1 - 255 CHAR) - MAY BE VARYING STRING.
**
*$.IN 0
*$LEV CHAR(*)
*$.IN 5
*$'SYSTEM' OR 'STEP' OR 'SYSTEMS'
**
*$.IN 0
*$CTL CHAR (1)
*$.IN 5
*$'E' - EXCLUSIVE CONTROL
*$.IN 5
*$'S' - SHARED CONTROL
**
*$.IN 0
*$TYPE CHAR (*)
*$.IN 5
*$'TEST' - TEST IF AVLBL
*$.IN 5
*$'USE'  - ENQ IF AVLBL NOW
*$.IN 5
*$'WAIT' - WAIT UNTIL AVLBL & ENQ
*$.IN 5
*$'HAVE' - ENQ IF NOT ALREADY ENQ'D
*$WAIT IF NOT AVLBL
**
*$.IN 0
*$RETURN CHAR (2)
*$.IN 5
*$'OK' - HAS BEEN ENQ'D OR AVLBL IF'TEST'
*$.IN 5
*$'NA' - NOT AVLBL - ONLY RETURNED
*$IF 'TEST' OR 'USE'
*$.IN 5
*$'PR' - PREV ENQ FROM THIS TASK IS
*$IN EFFECT
*$.IN 5
*$'ER' - A FORMAT ERROR WAS FOUND
*$.IN 0
*$UCBPTR PTR
*$.IN 5
*$POINTER VARIABLE CONTAINING THE ADDRESS OF THE UCB
*$FOR WHICH A RESERVE IS TO BE ISSUED.
**
********************************************************************$
SYSENQ PLIENTRY ,DSA=128,COMPILE=&SYSPARM
         LM    4,9,0(1)
         MVC   QNADR,0(4)              GET QNAME ADDR
         MVC   RNADR,0(5)              GET RNAME ADDR
         MVC   ENQLAST(4),=X'FF000000'
         AIF   ('&COMP' EQ 'F').FOPT0
         TM    6(R5),X'80'             IS RNAME VARYING?
         BC    8,NOTV1                 BR IF NOT
         L     RE,0(R5)
         MVC   RNLEN,1(RE)             GET RNAME LENGTH
         LA    RE,2(RE)
         ST    RE,RNADR               SET RNAME ADDR
         B     BYNOTV1
NOTV1    DS    0H
         MVC   RNLEN,5(R5)             GET RNAME LENGTH
BYNOTV1  DS    0H
         AGO   .BYFOPT0
.FOPT0   ANOP
         MVC   RNLEN,7(5)              GET RNAME LENGTH
.BYFOPT0 ANOP
         L     6,0(6)                  6 = LEV ADDR
         CLC   0(4,6),=C'STEP'         IS IT STEP LEVEL
         BC    8,CKCTL                 BR IF YES
         CLC   0(7,6),=C'SYSTEMS'      IS IT SYSTEMS?
         BC    7,CKSYSTEM              BR IF NOT
         OI    FLAGS,X'48'             IND SYSTEMS SPECIFIED
         L     14,24(1)                RE = A(UCBPTR)
         MVC   UCBADDR,0(14)           PUT A(UCB) IN LIST
         LA    14,UCBADDR
         ST    14,UCBPTR
         B     CKCTL
CKSYSTEM DS    0H
         CLC   0(5,6),=C'SYSTEM'       IS IT SYSTEM
         BC    7,ERR                   BR IF NOT
         OI    FLAGS,X'40'             IND SYSTEM SPECIFIED
CKCTL    EQU   *
         L     7,0(7)                  7 = CTL ADDR
         CLI   0(7),C'E'               IS IT EXCLUSIVE REQUEST
         BC    8,CKTYPE                BR IF YES
         CLI   0(7),C'S'               IS IT SHARE REQUEST
         BC    7,ERR                   BR IF NOT
         OI    FLAGS,X'80'             IND SHARE SPECIFIED
CKTYPE   EQU   *
         L     8,0(8)                  8 = CTL ADDR
         CLC   0(4,8),=C'WAIT'         IS IT WAIT
         BC    8,DOIT                  BR IF YES
         OI    FLAGS,X'01'
         CLC   0(4,8),=C'HAVE'         IS IT HAVE
         BC    8,DOIT                  BR IF YES
         OI    FLAGS,X'02'
         CLC   0(3,8),=C'USE'          IS IT USE
         BC    8,DOIT                  BR IF YES
         OI    FLAGS,X'04'
         CLC   0(4,8),=C'TEST'         IS IT TEST
         BC    8,DOIT                  BR IF YES
ERR      EQU   *
         L     9,0(9)                  9  = RETURN ADDR
         MVC   0(2,9),=C'ER'
EXIT     PLIEXIT
DOIT     EQU   *
         ENQ   MF=(E,ENQLIST)
         L     9,0(9)                  9  = RETURN ADDR
         LTR   15,15                   WAS IT GOOD ENQ
         BC    7,CKMORE                BR IF NOT
         MVC   0(2,9),=C'OK'
         B     EXIT
CKMORE   EQU   *
         SR    1,1
         IC    1,3(15)                 R1 = RETURN CODE
         B     *(1)
         B     FOUR
EIGHT    EQU   *
         MVC   0(2,9),=C'PR'           IND PRIOR REQUEST
         B     EXIT
FOUR     EQU   *
         MVC   0(2,9),=C'NA'           IND NOT AVLBL
         B     EXIT
         LTORG
*** FLAGS
* BIT 0 - ON IF REQ FOR SHR
*     1 - ON IF 'SYSTEM'
*     2 - ON IF SMC=SYSTEM
*     3 - ON IF SMC=STEP
*     4 - ON IF 'SYSTEMS' SPECIFIED
*     5 - ON IF RET=TEST
*     6 - ON IF RET=TEST OR USE
*     7 - ON IF RET=TEST OR USE OR HAVE
***
DSA      DSECT
ENQLIST  DS    0F
ENQLAST  DS    CL1           LAST ENTRY = X'FF'
RNLEN    DS    CL1           LENGTH OF RNAME
FLAGS    DS    CL1
         DS    CL1           RETURN CODE = X'00'
QNADR    DS    F             QNAME ADDR
RNADR    DS    F             RNAME ADDR
UCBPTR   DS    F
UCBADDR  DS    F
DSALEN EQU *-DSA
         END
