LODPDS   CSECT                                                      TGS
********************************************************************TGS
*                  THIS RTNE WILL ROUTE LOAD MODULE RECORDS TO THE *TGS
*              APPROPRIATE PDS.  AT ENTRY, THE RTNE EXPECTS R0 TO  *TGS
*              CONTAIN THE LENGTH OF THE RECORD OR ZERO IF EOD,    *TGS
*              R1 TO CONTAIN THE ADDR OF THE RECORD, AND RB TO     *TGS
*              CONTAIN THE ADDR OF NAMTAB.                         *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         USING NAMTAB,RB                                            TGS
         LTR   R0,R0                   IS IT EOD                    TGS
         BC    8,EODPROC               BR IF YES                    TGS
         TM    0(1),X'80'              IS IT STRT OF NEW MEMB       TGS
         BC    1,HDRPROC               BR IF YES                    TGS
         TM    PROCSW,X'80'            CURRENTLY PROCESSING MEMBER? TGS
         BC    8,RETCTL                BR IF NOT                    TGS
SORCPROC L     R2,CURENTRY                                          TGS
         L     RF,=V(PUTLOD)                                        TGS
         BALR  RE,RF                   GO PUT REC IN PDS            TGS
         B     RETCTL                                               TGS
EODPROC  EQU   *                                                    TGS
         L     R7,TABADDR                                           TGS
EODLOOP  EQU   *                                                    TGS
         L     R2,0(R7)                R2 = ADDR DATA BLOCK         TGS
         LTR   R2,R2                   IS IT END OF TABLE           TGS
         BC    4,RETCTL                BR IF YES                    TGS
         SR    R0,R0                                                TGS
         L     RF,=V(PUTLOD)                                        TGS
         BALR  RE,RF                                                TGS
         LA    R7,4(R7)                                             TGS
         B     EODLOOP                                              TGS
HDRPROC  EQU   *                                                    TGS
         MVI   PROCSW,X'00'            TURN OFF PROCSW              TGS
         LA    RA,4(R1)                RA = MEMBNAME ADDR           TGS
         L     R7,TABADDR              R7 = TABADDR                 TGS
TABLOOP  EQU   *                                                    TGS
         L     R2,0(R7)                R2 = DDNAME ADDR             TGS
         LTR   R2,R2                   IS IT END OF TABLE           TGS
         BC    4,RETCTL                BR IF YES                    TGS
         CLI   0(R2),C'$'              IS 1ST CHAR $                TGS
         BC    8,BIGPROC               BR IF YESS                   TGS
         CLC   0(8,R2),0(RA)           OTHERWISE TEST FOR SPEC NAME TGS
         BC    8,GOTMEMB               BR IF EQUAL                  TGS
BUMPTAB  EQU   *                                                    TGS
         LA    R7,4(R7)                BUMP TABLE INDEX             TGS
         B     TABLOOP                                              TGS
GOTMEMB  EQU   *                                                    TGS
         OI    PROCSW,X'80'            TURN ON PROCSW               TGS
         L     R7,0(R7)                                             TGS
         ST    R7,CURENTRY                                          TGS
         B     SORCPROC                                             TGS
BIGPROC  LA    R5,8(RA)                R5 = SSI ADDR                TGS
         LA    R6,8                                                 TGS
BIGLOOP  EQU   *                                                    TGS
         CLI   0(R2),C'$'              IS THIS POSITION IGNORED     TGS
         BC    7,BIGTEST               BR IF NOT                    TGS
BIGBUMP  EQU   *                                                    TGS
         LA    R2,1(R2)                BUMP R1                      TGS
         LA    R5,1(R5)                BUMP R5                      TGS
         BCT   R6,BIGLOOP                                           TGS
         B     GOTMEMB                 GO PROCESS THIS MEMBER       TGS
BIGTEST  EQU   *                                                    TGS
         CLC   0(1,R2),0(R5)           DO CHARS MATCH               TGS
         BC    7,RETCTL                BR IF NOT                    TGS
         B     BIGBUMP                                              TGS
PROCSW   DC    X'00'                                                TGS
CURENTRY DC    F'0'                                                 TGS
         LTORG                                                      TGS
NAMTAB   DSECT                                                      TGS
         NAMTAB                                                     TGS
         END
