TSORDRA  CSECT
         SAVES RETCTL
         TGSREQU
         LR    RB,R1
         USING CPPL,RB
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC DAPLPSCB,CPPLPSCB
         LA    RA,MYPPL
         USING PPL,RA
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R1,MYECB
         ST    R1,PPLECB
         L     R1,=V(PCL)
         ST    R1,PPLPCL
         LA    R1,PDLAWD
         ST    R1,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         LA    R1,MYPPL
         LINK  EP=IKJPARS
         LTR   RF,RF
         BC    7,0
         DROP  RA
         LOAD  EP=IKJEFD00
         ST    R0,DAIREP
         STAX  ,DEFER=YES
         BAL   R8,CALLDAIR             GO ALLOC BDAMFLE
         MVC   CTLFLG,PREFLG
         MVC   BDAMDCB+DDNAMO(8),DA08DDN
         MVC   DA08DDN(8),=CL8' '
         OPEN  (BDAMDCB,(UPDAT))
         ENQ   (,RNAME,E),RET=HAVE,MF=(E,ENQL)
         SR    R2,R2
BDAMLUP  DS    0H
         ST    R2,BLOCKNO
         BAL   R4,READ
         LTR   R2,R2                   HAVE WE READ 1ST REC?
         BC    7,BYFIRST               BR IF YES
         LH    R2,BDAMREC              R2 = FILECNT
         B     BDAMLUP
BYFIRST  DS    0H
         TM    BDAMREC,X'80'           IS FILE AVAILABLE
         BC    8,GOTFILE               BR IF YES
BCTINST  DS    0H
         BCT   R2,BDAMLUP
NORDR    DS    0H
         BAL   R4,DEQ
         LA    R6,NOFILEMS
         BAL   R7,PUTMSG
         B     RETCTLB
GOTFILE  DS    0H
         LR    R3,R2
         SLL   R3,1                    MULT INDEX BY 2
         LA    R3,RDRINDX(R3)
         MVC   RNAME2+4(2),0(R3)       SET RNAME2
         ENQ   (,RNAME2,E),RET=USE,MF=(E,ENQL)
         LTR   RF,RF                   NEQ SUCCESSFUL?
         BC    7,BCTINST
         OI    BDAMREC,X'40'           RESERVE FILE
         BAL   R4,DEQ
         MVC   RDRNO,0(R3)       SET DSNAME
         MVC   DSNBUF(46),DSNBUF2
         MVC   DA08MNM,=CL8' '
         BAL   R8,CALLDAIR
         MVC   OUTFLG,PREFLG
         MVC   TSORDR+DDNAMO(8),DA08DDN
         MVC   DA08DDN(8),=CL8' '
         OPEN  (TSORDR,(OUTPUT))
         L     RA,PDLAWD
         USING IKJPARMD,RA
         LA    RA,BGDNAM               GET FIRST DSNAME
         DROP  RA
         USING PDE,RA
DSNLOOP  DS    0H
         L     R9,DSNPTR
         MVC   DSNBUF(2),DSNLEN        MOVE LEN(DSN)
         MVC   DSNBUF+2(44),0(R9)      MOVE DSN
         TM    DSNFLGS,X'40'
         BC    8,CKCNTL                BR IF DSNAME NOT IN QUOTES
         NI    DA00CTL,X'DF'           INDICATEDON'T PREFIX USERID
         B     GUDQUAL
CKCNTL   DS    0H
         OI    DA00CTL,X'20'
         LH    R1,DSNLEN
         SH    R1,=H'5'
         LA    R1,0(R1,R9)
         CLC   0(5,R1),=C'.CNTL'       IS LOW QUAL = '.CNTL'?
         BC    8,GUDQUAL               BR IF YES
         LH    R1,DSNLEN
         LA    R2,DSNBUF+2(R1)         R2 = A(END DSN)
         MVC   0(5,R2),=C'.CNTL'        ADD CNTL QUAL
         LA    R1,5(R1)                BUMP LEN
         STH   R1,DSNLEN
         STH   R1,DSNBUF
GUDQUAL  DS    0H
         NI    DA08CTL,X'DF'
         OC    DA08CTL(1),DA00CTL
         MVC   DA08MNM(8),=CL8' '
         TM    MEMFLGS,X'80'           IS THERE A MEMBER NAME?
         BC    8,NOMEM
         L     R9,MEMPTR
         LH    R8,MEMLEN
         BCTR  R8,0                    LESS 1 FOR EX
         EX    R8,MVCMEM               MOVE IN MEMBER NAME
         B     NOMEM
MVCMEM   MVC   DA08MNM(0),0(R9)
NOMEM    DS    0H
         LA    R8,DODSN
CALLDAIR DS    0H
         L     R1,DAPLDAPB
         CLC   0(2,R1),=H'8'           IS IT ALLOC?
         BC    7,CALL18                BR IF NOT
         OI    PREFLG,X'80'
         LA    R1,DA00
         ST    R1,DAPLDAPB
         LA    R1,DAPL
         L     RF,DAIREP
         BALR  RE,RF                   GO TO DAIR
         STH   RF,TINRC                SAVE RF
         LTR   RF,RF
         BC    7,PRTRC
         TM    DA00DSO,X'60'           IS DSORG=PS OR DA?
         BC    8,CALL08                BR IF NOT
         TM    DA00FLG,X'0A'           IS DSNAME ALREADY ALLOCATED?
         BC    8,CALL08                BR IF NOT
         NI    PREFLG,X'00'
CALL08   DS    0H
         LA    R1,DA08
         ST    R1,DAPLDAPB
CALL18   DS    0H
         MVC   DA08DDN,=CL8' '
         LA    R1,DAPL
         L     RF,DAIREP
         BALR  RE,RF                   GO TO DAIR
         LTR   RF,RF
         BCR   8,R8                    BR IF RC=0
         STH   RF,TINRC                SAVE RF
         L     R5,DAPLDAPB
         CLC   4(2,R5),=H'0'           DYNAMIC ALLOC OK?
         BC    8,TSTCTRC
         LA    R1,4(R5)
         ST    R1,TINSDV
         LA    R1,OUTRCDA
         ST    R1,TOUTSDV
         LA    R6,DARCMSG
         BAL   R7,CALLTROU
TSTCTRC  DS    0H
         CLC   6(2,R5),=H'0'           CTLG MGT OK?
         BC    8,PRTRC                BR IF YES
         LA    R1,6(R5)
         ST    R1,TINSDV
         LA    R1,OUTRCDB
         ST    R1,TOUTSDV
         LA    R6,CTRCMSG
         BAL   R7,CALLTROU
PRTRC    DS    0H
         LA    RF,TINRC
         ST    RF,TINSDV
         LA    RF,OUTRC
         ST    RF,TOUTSDV
         LA    R6,BADRCMSG
         BAL   R7,CALLTROU
         LH    R1,DSNBUF               R1 = LEN(DSN)
         LA    R1,8(R1)                BUMP TO PRINT
         STH   R1,DSNMSG               SET PRINT LENGTH
         MVC   DSNMSGA,DSNBUF+2
         LA    R6,DSNMSG
         BAL   R7,PUTMSG               GO PRINT DSN
         B     RETCTLA
DODSN    DS    0H
         MVC   INFLG,PREFLG
         MVC   INDCB+DDNAMO(8),DA08DDN
         MVC   DA08DDN(8),=CL8' '
         OPEN  (INDCB,(INPUT))
         TM    INDCB+OFLGO,X'10'      GOOD OPEN?
         BC    8,RETCTLA               BR IF NOT
READLUP  DS    0H
         GET   INDCB
         LR    R0,R1
         PUT   TSORDR,(0)
         B     READLUP
INEOD    DS    0H
         CLOSE (INDCB)
         FREEPOOL INDCB
         TM    INFLG,X'80'             FREE DATA SET?
         BC    0,NFIN TEMPORARILY ZAPPED TO NOP
         LA    R1,DA18
         ST    R1,DAPLDAPB
         MVC   DA18DDN,INDCB+DDNAMO
         BAL   R8,CALLDAIR
         LA    R1,DA08
         ST    R1,DAPLDAPB
NFIN     DS    0H
         L     RA,PDEPTR
         LTR   RA,RA                   IS IT END OF LIST?
         BC    2,DSNLOOP
         CLOSE (TSORDR)
         FREEPOOL TSORDR
         MVI   BDAMREC,X'80'
         BAL   R4,WRITE
         CLOSE (BDAMDCB)
         LA    R1,DA18
         ST    R1,DAPLDAPB
         TM    OUTFLG,X'80'            FREE DATA SET?
         BC    8,NFOUT                 BR IF NOT
         MVC   DA18DDN,TSORDR+DDNAMO
         BAL   R8,CALLDAIR
NFOUT    DS    0H
         TM    CTLFLG,X'80'            FREE DATA SET?
         BC    0,NFCTL                 BR IF NOT (TEMPY NOP)
         MVC   DA18DDN,BDAMDCB+DDNAMO
         BAL   R8,CALLDAIR
NFCTL    DS    0H
RETCTLA  DS    0H
         BAL   R4,DEQ2
RETCTLB  DS    0H
         DELETE EP=IKJEFD00
         B     RETCTL
WRITE    DS    0H
         WRITE WRDECB,DI,BDAMDCB,BDAMREC,'S',0,BLOCKNO+1
         CHECK WRDECB
         BR    R4
READ     DS    0H
         READ  RDDECB,DI,BDAMDCB,BDAMREC,'S',0,BLOCKNO+1
         CHECK RDDECB
         BR    R4
DEQ2     DS    0H
         DEQ   (,RNAME2),MF=(E,DEQL)
         BR    R4
DEQ      DS    0H
         DEQ   (,RNAME),MF=(E,DEQL)
         BR    R4
DEQL     DEQ   (QNAME,,,SYSTEM),RET=HAVE,MF=L
ENQL     ENQ   (QNAME,,,,SYSTEM),MF=L
MYPPL    DS    7F
MYECB    DC    F'0'
PDLAWD   DC    F'0'
DSNBUF   DC    H'17'
         DC    CL44'U.SD.U000.TSOBDAM'
QNAME    DC    CL8'TGSRDR'
RNAME    DC    AL1(3),CL3'TGS'
RNAME2   DC    AL1(5),CL5'RDR00'
DSNBUF2  DC    AL2(DSNBLEN-*-2)
         DC    C'U.SD.U000.RDR'
RDRNO    DS    CL2
DSNBLEN  EQU   *
RDRINDX  DC    C'00010203040506070809101112131415'
BDAMDCB  DCB   DDNAME=BDAMFLE,BLKSIZE=80,DSORG=DA,MACRF=(RIC,WIC),     C
               OPTCD=R,RECFM=F
OFLGO    EQU   48
DDNAMO   EQU   40
BDAMREC  DC    XL80'0'
BLOCKNO  DS    F
TSORDR   DCB   DDNAME=RDR00,DSORG=PS,MACRF=(PM)
INDCB    DCB   DDNAME=DUMMY,DSORG=PS,MACRF=(GL),EODAD=INEOD
DAPL     DS    0F
DAPLUPT  DS    F
DAPLECT  DS    F
DAPLECBA DC    A(DAPLECB)
DAPLPSCB DS    F
DAPLDAPB DC    A(DA08)
DAPLECB  DC    F'0'
DAIREP   DC    F'0'
DA08     DS    0F
         DC    H'8'
DA08FLG  DC    H'0'
DA08DARC DC    H'0' RC FROM DYNAMIC ALLOCATION
DA08CTRC DC    H'0' RC FROM CATALOG MANAGEMENT
DA08PDSN DC    A(DSNBUF)
DA08DDN  DC    CL8' ' DDNAME FIELD
DA08UNIT DC    CL32' '
DA08MNM  DC    CL8' ' MEMBER NAME FIELD
DA08PSWD DC    CL8' '
DA08DSP1 DC    X'08' DISP=SHR
DA08DPS2 DC    X'08' DISP=KEEP
DA08DPS3 DC    X'08' DISP=KEEP
DA08CTL  DC    X'08'  ?
         DC    XL3'0'
DA08DSO  DS    CL1
DA08ALN  DC    CL8' '
DA18     DS    0F
DA18CD   DC    X'0018'
DA18FLG  DC    H'0'
DA18DARC DC    2H'0'
DA18PDSN DC    F'0'
DA18DDN  DS    CL8
         DC    CL10' '
DA18DPS2 DC    X'08'
DA18CTL  DC    X'10'
DA18JBNM DC    CL8' '
DA00     DS    0F
DA00CD   DC    H'0'
DA00FLG  DC    H'0'
DA00PDSN DC    A(DSNBUF)
DA00DDN  DC    CL8' '
DA00CTL  DC    X'0'
         DC    XL2'0'
DA00DSO  DC    X'0'
PREFLG   DS    CL1
CTLFLG   DC    X'00' SET TO X'80' IF DATA SET S/B FREED AT END
INFLG    DC    X'00' DITTO
OUTFLG   DC    X'00' DITTO
TROUTPRM DC    A(TINSDV),X'80',AL3(TOUTSDV)
TINSDV   DC    A(0),H'2',H'2'
TOUTSDV  DC    A(0),H'4',H'4'
TINRC    DS    H
CALLTROU DS    0H
         LA    R1,TROUTPRM
         L     RF,=V(TROUT)
         BALR  RE,RF
PUTMSG   DS    0H
         LA    R1,MYPPL
         PUTLINE PARM=PTPB,                                            C
               OUTPUT=((6),TERM,SINGLE,DATA),                          C
               MF=(E,(1))
         LTR   RF,RF
         BCR   8,R7
         DC    H'0'
PTPB     PUTLINE ,MF=L
BADRCMSG DS    0F
         DC    AL2(BRCLEN-*)
         DC    H'0'
         DC    C'RETURN CODE = '
OUTRC    DS    CL4
         DC    C' FROM DAIR'
BRCLEN   EQU   *
DARCMSG  DS    0F
         DC    AL2(DARCLEN-*)
         DC    H'0'
         DC    C'RETURN CODE = '
OUTRCDA  DS    CL4
         DC    C' FROM DYNAMIC ALLOCATION'
DARCLEN  EQU   *
CTRCMSG  DS    0F
         DC    AL2(CTRCLEN-*)
         DC    H'0'
         DC    C'RETURN CODE = '
OUTRCDB  DS    CL4
         DC    C' FROM CATALOG MANAGEMENT'
CTRCLEN  EQU   *
NOFILEMS DS    0F
         DC    AL2(NOFLEN-*)
         DC    H'0'
         DC    C'ALL THE READER FILES ARE FULL - TRY LATER'
NOFLEN   EQU   *
DSNMSG   DS    0F
         DC    F'0'
         DC    C'DSN='
DSNMSGA  DS    CL44
         LTORG
PCL      IKJPARM
BGDNAM   IKJPOSIT DSNAME,LIST,                                         C
               PROMPT=' THE DSNAME'
         IKJENDP
         IKJCPPL
         IKJPPL
PDE      DSECT
DSNPTR   DS    F
DSNLEN   DS    H
DSNFLGS  DS    H
MEMPTR   DS    F
MEMLEN   DS    H
MEMFLGS  DS    H
PASSPTR  DS    F
PASSLEN  DS    H
PASSFLGS DS    H
PDEPTR   DS    F
         END
