 /* DSIZESB */
 /*
0*$.HE GENERALIZED SUBROUTINES - DSIZESB
0*$.HE DSIZESB - DETERMINE TOTAL TRACKS FOR DATASET
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$7/27/76
0*$AUTHOR: T. SMITH
0*$SOURCE MODULE NAME: DSIZESB
0*$DOCUMENTATION ID: 0*$
0*$.FI L
0*$.SP 5
0*$THIS ROUTINE CAN BE USED TO DETERMINE THE TOTAL TRACKS USED BY
0*$A DATA SET.  CALL FORMAT IS AS FOLLOWS -
0*$.SP 1
0*$.IN 10
0*$CALL DSIZESB(DSCB1PTR,TRKPERCYL,VOLUME,TOTRK);
0*$.IN 5
0*$.SP 1
0*$WHERE
0*$.SP 1
0*$.IN 0
0*$DSCB1PTR PTR
0*$.IN 5
0*$POINTER VARIABLE CONTAINING THE ADDRESS OF THE FORMAT 1 DSCB
0*$FOR THE DATA SET FOR WHICH TOTAL TRACKS USED IS TO BE
0*$DETERMINED.
0*$.IN 0
0*$TOTRK FIXED BIN(15,0)
0*$.IN 5
0*$WILL BE SET BY THIS ROUTINE TO THE TOTAL TRACKS USED BY THE DATA
0*$SET.
0*$.IN 0
0*$TRKPERCYL FIXED BIN(15,0)
0*$.IN 5
0*$MUST CONTAIN THE NUMBER OF TRACKS PER CYLINDER ON THE VOLUME
0*$CONTATINING THE SPECIFIED DATA SET.
0*$.IN 0
0*$VOLUME CHAR(6)
0*$.IN 5
0*$MUST CONTAIN THE VOLUME SERIAL OF THE VOLUME CONTAINING THE
0*$SPECIFIED DATA SET.
 */
 DSIZESB:  PROC(DSCB1PTR,TRKPERCYL,VOLUME,TOTRK);
         DCL DSCB1PTR PTR;
              /* ADDR OF FORMAT 1 DSCB */
         DCL TRKPERCYL FIXED BIN(15,0);
              /* # TRKS PER CYLINDER */
         DCL TOTRK FIXED BIN(15,0);
              /*WILL BE SET TO TOTAL TRKS USED BY DATA SET */
         DCL VOLUME CHAR(6);
              /* VOLUME CONTAINING SPECIFIED DATA SET */
         % INCLUDE DSCB1;
         % INCLUDE DSCB2;
         % INCLUDE DSCB3;
         DCL (DSCB2PTR,DSCB3PTR) PTR STATIC;
         DSCB2PTR,DSCB3PTR = ADDR(WKAREA);
         DCL 1 EXTARY(9) BASED(EXTPTR),
              2 EXTFL BIT(8),
              2 EXTSQ BIT(8),
              2 CB1 FIXED BIN(15,0),
              2 HB1 FIXED BIN(15,0),
              2 CB2 FIXED BIN(15,0),
              2 HB2 FIXED BIN(15,0);
         DCL RETLAB LABEL;
         DCL EXTCNT FIXED BIN(15,0);
         DCL WKAREA CHAR(148);
         DSCB2PTR,DSCB3PTR = ADDR(WKAREA);
         DCL RET BIT(32);
         DCL MYCCHH CHAR(5);
         TOTRK = 0;
         EXTPTR = ADDR(DS1EXT);
         EXTCNT = 3;
         RETLAB = RET1;
         GO TO CNTSUB;
 RET1:
         IF DS1PTRDS = LOW(5)
              THEN RETURN;
         MYCCHH = DS1PTRDS;
 CALLVTOC:
         CALL SYSVTOC('SEEK',RET,MYCCHH,VOLUME,WKAREA);
         IF RET ^= 0
              THEN SIGNAL ERROR;
         IF DS2FMTID = '2'
              THEN IF DS2PTRDS = LOW(5)
                   THEN RETURN;
                   ELSE DO;
                        MYCCHH = DS2PTRDS;
                        GO TO CALLVTOC;
                        END;
         EXTPTR = ADDR(DS3EXT);
         EXTCNT = 4;
         RETLAB = RET2;
         GO TO CNTSUB;
 RET2:
         EXTPTR = ADDR(DS3ADEXT);
         EXTCNT = 9;
         RETLAB = RET3;
         GO TO CNTSUB;
 CNTSUB:
         DO I = 1 TO EXTCNT;
              IF EXTFL(I) ^= '0'B
                   THEN TOTRK = TOTRK + (CB2(I)-CB1(I)-1)*TRKPERCYL
                     +TRKPERCYL-HB1(I)+HB2(I)+1;
              END;
         GO TO RETLAB;
 RET3:
         END;
