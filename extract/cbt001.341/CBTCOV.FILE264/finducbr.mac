FINDUCB  START 0                                                    TGS
********************************************************************TGS
*                  THIS ROUTINE MAY BE USED TO FIND THE MAIN       *TGS
*              STORAGE ADDR OF A UCB GIVEN THE DEVICE ADDR.        *TGS
*                  UPON ENTRY REG1 SHOULD CONTAIN THE ADDR OF TWO  *TGS
*              CONTIGUOUS FULLWORDS.  THE FIRST WORD SHOULD CONTAIN*TGS
*              THE ADDR OF A THREE-BYTE CHARACTER STRING           *TGS
*              REPRESENTING THE DEVICE ADDR.  THE SECOND WORD      *TGS
*              SHOULD CONTAIN THE ADDR OF A WORD INTO WHICH THE    *TGS
*              UCB ADDR IS TO BE STORED.                           *TGS
********************************************************************TGS
R0       EQU   0                                                    TGS
R1       EQU   1                                                    TGS
R2       EQU   2                                                    TGS
R3       EQU   3                                                    TGS
R4       EQU   4                                                    TGS
R5       EQU   5                                                    TGS
R6       EQU   6                                                    TGS
R7       EQU   7                                                    TGS
R8       EQU   8                                                    TGS
R9       EQU   9                                                    TGS
RA       EQU   10                                                   TGS
RB       EQU   11                                                   TGS
RC       EQU   12                                                   TGS
RD       EQU   13                                                   TGS
RE       EQU   14                                                   TGS
RF       EQU   15                                                   TGS
         STM   RE,RB,12(RD)            SAVE REGS 14 THRU 11         TGS
         BALR  RA,R0                                                TGS
         USING *,RA                                                 TGS
         LA    R7,15                                                TGS
         L     R6,0(R0,R1)             REG6 EQ DEVADDR PTR          TGS
         L     R2,16(R0)               GET CVT PTR                  TGS
         L     R3,36(R0,R2)            REG3 EQ ADDR IECILK1         TGS
         L     R2,40(R0,R2)            REG2 EQ ADDR IECILK2         TGS
         SR    R4,R4                   REG4 EQ ZERO                 TGS
         IC    R4,0(R0,R6)             REG4 EQ CHAN ADDR            TGS
         NR    R4,R7                   GET RID OF ZONE              TGS
         AR    R4,R3                   REG4 EQ ADDR OF K            TGS
         SR    R5,R5                   REG5 EQ ZERO                 TGS
         IC    R5,0(R0,R4)             REG5 EQ K                    TGS
         SR    R4,R4                   REG4 EQ ZERO                 TGS
         IC    R4,1(R0,R6)             REG4 EQ UNIT ADDR            TGS
         NR    R4,R7                   GET RID OF ZONE              TGS
         AR    R4,R3                                                TGS
         AR    R4,R5                   REG5 EQ ADDR OF L            TGS
         SR    R5,R5                   REG5 EQ ZERO                 TGS
         IC    R5,0(R0,R4)             REG5 EQ L                    TGS
         MH    R5,=H'2'                REG5 EQ 2L                   TGS
         SR    R4,R4                   REG4 EQ ZERO                 TGS
         IC    R4,2(R0,R6)             REG4 EQ DEV ADDR             TGS
         NR    R4,R7                   GET RID OF ZONE              TGS
         MH    R4,=H'2'                REG4 EQ 2(DEV ADDR)          TGS
         AR    R4,R5                                                TGS
         AR    R4,R2                   REG4 EQ PTR TO UCB ADDR      TGS
         LH    R5,0(R0,R4)             REG5 EQ UCB ADDR             TGS
         L     R8,4(R0,R1)             GET ADDR OF WORD             TGS
         ST    R5,0(R0,R8)             STORE UCB ADDR IN WORD       TGS
         LM    RE,RB,12(RD)            RESTORE REGS                 TGS
         BR    RE                           AND RETURN              TGS
         LTORG                                                      TGS
         END
