   /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
   /* ABE011 - SUBMIT,SUB,JOB SUBCOMMANDS */
   /* 1        2      3   4               */
   %INCLUDE ABESUB;
 ABESUB(ABE011) CHECK(YES);
   %INCLUDE ABESUBA;

   DCL 1 LRN_STRUCTURE BASED(LRNPTR),
   %INCLUDE ABELRN;
   %INCLUDE ABELRNA;

   DCL INTRDR ENTRY(,CHAR(*) VAR,FIXED BIN(15,0),FIXED BIN(15,0));
   DCL MYPTR PTR;
   DCL LAST FIXED BIN(31,0) STATIC INIT(99999999);
   DCL ALCODE CHAR(3) VAR STATIC INIT('OUT');
   DCL HELPSTR CHAR(4) VAR STATIC INIT('');
   DCL CMDA CHAR(240) VAR STATIC INIT('SUB LASTJCL ');
   DCL SUBDSN CHAR(44) VAR STATIC INIT('LASTJCL.CNTL');
   DCL SAVDDN PTR;
   DCL MYREC CHAR(80);
   IF CMDINDX=4
     THEN CALL DOJOB; /* IT IS JOB SUBCOMMAND */
     ELSE CALL DOSUB; /* IT IS SUB SUBCOMMAND */

 DOJOB: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO PROCESS JOB SUBCOMMAND.                                   *
 *                                                                     *
 **********************************************************************/
   CALL INTRDR(RH_SEQ_INTERNAL_RDR,MSGDATA,0,0);
   IF MSGDATA^=''
     THEN RHRC=8;
     ELSE DO;
       DO FOREVER=1 REPEAT FOREVER;
         CALL #ABELSP(RHHDRPT,LRNPTR); /* RECPTR ALSO W/B ALTERED */
         IF LRN_BUFP=NULL
           THEN LEAVE; /* ALL DONE */
         MYREC=LSPBUF;
         WRITE FILE(RH_SEQ_INTERNAL_RDR) FROM(MYREC);
         END;
       CLOSE FILE(RH_SEQ_INTERNAL_RDR);
       MSGDATA='JOB SUCCESSFULLY SUBMITTED';
       RHRC=0;
       END;
   RETURN;
   END DOJOB;

 DOSUB: PROC;
 /**********************************************************************
 *                                                                     *
 * CALLED TO PROCESS SUB SUBCOMMAND.                                   *
 *                                                                     *
 **********************************************************************/
   SAVDDN = OUTDDN;
   CALL #DOALC(SUBDSN,ALCODE,OUTDDN,NULL,''); /* CALL DOALC */
   IF OUTDDN = NULL
     THEN DO;
       MSGDATA='ERROR IN ALLOCATE';
       OUTDDN = SAVDDN;
       RHRC = 8;
       RETURN;
       END;
   IF SAVDDN^=NULL
     THEN SUBSTR(OUTDDN->ALCFLAG,6,1)=SUBSTR(SAVDDN->ALCFLAG,6,1);
   /* SET NONUM OPTION */
   MYPTR = HDPTR;
   CALL #RESEQ(RHPTR,RHHDRPT,MYPTR,'SAVE','NORENUM',LAST);
   /* CALL RESEQ */
   CALL #CRTZAP(RHPPTR); /* CALL CRTZAPS */
   CALL #DOCMD(CMDA,HELPSTR); /* CALL DOCMD */
   CALL #DOFRE(OUTDDN); /* CALL DOFRE */
   OUTDDN = SAVDDN;
   CMDDATA ='X';
   RHRC = 0;
   END DOSUB;

   END ABE011;
