 /* WTORMSG
0*$.HE GENERALIZED UTILITIES - WTORMSG
0*$.HE QUERY THE CONSOLE OPERATOR
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$6/20/74
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  WTORMSG
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS UTILITY PROGRAM PROVIDES THE USER WITH THE FACILITY FOR
0*$SENDING MESSAGE TO THE CONSOLE OPERATOR FROM HIS JOB, AND TO
0*$CONTROL SUBSEQUENT EXECUTION BY SETTING A RETURN CODE BASED
0*$ON A "YES" OR "NO" RESPONSE FROM THE OPERATOR.  THE RETURN CODE
0*$CAN BE TESTED BY SUBSEQUENT STEPS IN THE JOB TO CONTROL THEIR
0*$EXECUTION.
0*$.SP 1
0*$NOTE:  THE OPERATOR IS RESPONSIBLE FOR KEEPING TRACK OF A GREAT
0*$NUMBER OF CONSOLE MESSAGES.  DO NOT USE THIS PROGRAM UNLESS IT IS
0*$CRITICAL THAT YOU DO SO.
0*$.SP 1
0*$.NF
0*$.IN 5
0*$// EXEC PGM=WTORMSG,REGION=80K,
0*$// PARM='MESSAGE'
0*$//SYSPRINT DD SYSOUT=A
0*$//SYSIN DD *
0*$MESSAGE
0*$MESSAGE
0*$ .
0*$ .
0*$ .
0*$MESSAGE
0*$/*
0*$.IN 0
0*$.SP 1
0*$.FI L
0*$IF YOU HAVE ONLY ONE MESSAGE, YOU MAY ENTER IT VIA THE PARM FIELD AND
0*$OMIT THE SYSIN DATA SET.  ALSO, IF YOU HAVE MORE THAN ONE MESSAGE,
0*$YOU MAY OMIT THE PARM FIELD AND ENTER ALL MESSAGES IN THE SYSIN DATA
0*$SET.
0*$.SP 1
0*$AFTER THE PROGRAM HAS WRITTEN ALL OF THE MESSAGES YOU HAVE SPECIFIED,
0*$IT WILL WRITE THE FOLLOWING MESSAGE TO THE CONSOLE, AND WAIT FOR
0*$A RESPONSE FROM THE OPERATOR:
0*$.SP 1
0*$.IN 10
0*$XXXXXXXX - RESPOND YES OR NO
0*$.IN 0
0*$.SP 1
0*$XXXXXXXX IN THE ABOVE MESSAGE IS A CHARACTER STRING CONSISTING OF THE
0*$FIRST EIGHT CHARACTERS IN THE LAST MESSAGE YOU SPECIFIED.  THIS
0*$SHOULD BE IDENTIFYING CHARACTERS SO THAT THE CONSOLE OPERATOR CAN
0*$ASSOCIATE THE MESSAGE REQUIRING A RESPONSE WITH THE PRECEDING
0*$INFORMATION MESSAGES.
0*$.SP 1
0*$IF THE OPERATOR RESPONDS "YES" THE PROGRAM WILL SET A RETURN CODE
0*$OF 0.  A RETURN CODE OF 4 WILL BE SET FOR A RESPONSE OF "NO".
0*$.SP 1
0*$.CE 1
0*$EXAMPLE
0*$.SP 1
0*$SUPPOSE THAT YOU HAVE A JOB THAT CAUSES THE OPERATING SYSTEM TO
0*$FAIL WHENEVER IT RUNS.  TECHNICAL SUPPORT HAS SUGGESTED A POSSIBLE
0*$CORRECTION, AND YOU WANT TO MAKE A TEST RUN TO SEE IF IT WORKS.
0*$SO YOU CONTACT OPERATIONS AND SCHEDULE THE RUNNING OF YOUR JOB AT
0*$A NON-CRITICAL TIME.  BUT TO BE ABSOLUTELY CERTAIN THAT YOU TEST
0*$DOESN'T GET RUN AT THE WRONG TIME, YOU CAN INSERT THE FOLLOWING JCL
0*$AFTER THE JOB STATEMENT IN YOUR TEST DECK:
0*$.SP 1
0*$.IN 5
0*$.NF
0*$//CHECK EXEC PGM=WTORMSG,REGION=80K
0*$//SYSPRINT DD SYSOUT=A
0*$//SYSIN DD *
0*$SPECIAL - THIS JOB HAS CAUSED OS
0*$SPECIAL - TO FAIL IN THE PAST,
0*$SPECIAL - AND HAS BEEN SCHEDULED
0*$SPECIAL - TO RUN AT A NON-CRITICAL
0*$SPECIAL - TIME.  CAN YOU AFFORD
0*$SPECIAL - AN OS FAILURE AT THIS TIME?
0*$//STEP2 EXEC PGM=NONAME,
0*$// REGION=20K,COND=(0,EQ,CHECK)
0*$.SP 1
0*$.IN 0
0*$.FI L
0*$IN THIS EXAMPLE, IF THE OPERATOR RESPONDS "NO", YOUR TEST WILL ABORT
0*$IN STEP2 WITH AN 806 ABEND.  IF HE RESPONDS "YES", STEP2 WILL BE
0*$BYPASSED AND THE REST OF YOUR JOB WILL EXECUTE.
 */
 WTORMSG.. PROC(PARM) OPTIONS(MAIN),.
         DCL PARM CHAR(100) VARYING,.
         DCL REPLY CHAR(133) VARYING,.
         DCL IHESARC ENTRY(FIXED BIN(31,0)),.
         ON ENDFILE(SYSIN) GO TO EOJ,.
         DCL ((NXFIRST,NXLAST) BIT(1),
              DDNAME CHAR(8)) EXTERNAL,.
         NXFIRST = '1'B,.
         NXLAST = '0'B,.
         DCL INSW BIT(1) INIT('0'B),.
         DO WHILE(NXLAST = '0'B),.
         CALL D10201C,.
         IF DDNAME = 'SYSIN' THEN INSW = '1'B,.
         END,.
         DCL 1 INREC,
              2 DISPLAY CHAR(72),
              2 PAD CHAR(8),.
         DISPLAY = PARM,.
         IF DISPLAY NE ' ' THEN GO TO DODISP,.
         IF INSW = '0'B THEN GO TO EOJ,.
 NEXT..
         READ FILE(SYSIN) INTO(INREC),.
 DODISP..
         DISPLAY(DISPLAY),.
         IF INSW = '1'B THEN GO TO NEXT,.
 EOJ..
         DISPLAY = SUBSTR(DISPLAY,1,8) CAT ' - RESPOND YES OR NO',.
 EOJA..
         REPLY = '   ',.
         DISPLAY(DISPLAY) REPLY(REPLY),.
         CALL SETCAPS(REPLY),.
         IF REPLY = 'YES' THEN CALL IHESARC(0),.
          ELSE IF REPLY = 'NO' THEN CALL IHESARC(4),.
          ELSE GO TO EOJA,.
         END,.
