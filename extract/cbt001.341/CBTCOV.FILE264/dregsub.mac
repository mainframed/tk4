*$.HE GENERALIZED SUBROUTINES - DREGSUB
*$.HE PLI INTERFACE TO GETMAIN MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$3/19/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  DREGSUB
*$DOCUMENTATION ID:  0*$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE PROVIDES THE PLI PROGRAMMER WITH THE CURRENT REGION
*$SIZE IN K BELOW THE LINE.  CALL FORMAT IS AS FOLLOWS -
*$.SP 1
*$.IN 5
*$.NF
*$DCL DREGSUB ENTRY RETURNS(FIXED BIN(31,0));
*$FETCH DREGSUB;
*$I=DREGSUB;
*$.SP 1
*$.FI L
DREGSUB  PLIENTRY ,DSA=200,COMPILE=O
         L     R2,0(R1)           A(RETURN PARAMETER)
         L     R3,X'10'           R3=A(CVT)
         USING CVT,R3
         TM    CVTDCB,CVTMVSE
         BC    8,NOTMVSXA
         LA    R4,WKAREA
         VSMREGN WKAREA=(R4)
         L     RF,REGLEN
         B     EXIT
NOTMVSXA DS    0H
         L     R4,544             R4=A(ASCB)
         L     R5,48(R4)          R5=A(LDA)
         L     RF,1468(R5)        RF=REGION SIZE IN BYTES
EXIT     DS    0H
         SRA   RF,10              DIVIDE BY 1024
         STH   RF,0(R2)           STORE REGION ADDR
         PLIEXIT
         LTORG
DSA      DSECT
WKAREA   DS    0D 16-BYTE WORK AREA
         DS    F
REGLEN   DS    F
         DS    2F
         CVT   DSECT=YES
         END
