 TSTSPCE:  PROC(PARMSER,PARMSPCE,PARMRET);
 /* THIS SUBROUTINE WILL DETERMINE IF THE SPECIFIED NUMBER OF TRACKS,
    PARMSPCE, ARE CURRENTLY AVAILABLE ON THE SPCIFIED VOLUME, PARMSER.
    PARMRET WILL BE SET TO -

         0 - THE VOLUME IS MOUNTED, AND THE SPACE IS AVAILABLE.
         4 - THE VOLUME IS MOUNTED, AND THE SPACE IS NOT AVAILABLE.
         8 - THE VOLUME IS NOT MOUNTED.
 */
         % INCLUDE DSCB4;
         DCL PARMRET FIXED BIN(31,0);
         DCL PARMSER CHAR(6);
         DCL PARMSPCE FIXED BIN(31,0);
         DCL MYRET FIXED BIN(31,0);
         DCL RET BIT(32) DEF MYRET;
         DCL CVTPTR PTR BASED(CVTPTRPTR);
         DCL CVTPTRPTR PTR,
             CVTADJ FIXED BIN(31,0) DEF CVTPTRPTR;
         CVTADJ = 16;
         DCL 1 CVT BASED(CVTPTR),
              2 IEATCBPTR PTR,
              2 CVTPAD CHAR(112),
              2 CVTDCB BIT(8);
                   /* .... ...1  VS2.2 */
         DCL 1 CVTA BASED(CVTPTR),
              2 PAD CHAR(40),
              2 UCBTABP PTR;
         DCL WRKPTR PTR,
             WRKADJ FIXED BIN(31,0) DEF WRKPTR;
         DCL UCBPTR PTR,
             UCBADJ FIXED BIN(31,0) DEF UCBPTR;
         DCL TABENT BIT(16) BASED(WRKPTR);
         DCL 1 UCB BASED(UCBPTR),
              2 PAD CHAR(28),
              2 UCBSER CHAR(6);
         DCL DSNB BIT(352) STATIC INIT((44)'00000100'B);
         DCL DSNAME CHAR(44) DEF DSNB;
         DCL WKAREA CHAR(148);
         DCL DSCB4PTR PTR;
         DCL DSCB4ADJ FIXED BIN(31,0) DEF DSCB4PTR;
         DSCB4PTR = ADDR(WKAREA);
         DSCB4ADJ = DSCB4ADJ - 44;
         DCL 1 LSMSG,
              2 PAD1 CHAR(6), /*SPACE=*/
              2 LCYL PIC'9999',
              2 PAD2 CHAR(1),
              2 LTRK PIC'9999',
              2 PAD3 CHAR(15);
         WRKPTR = UCBTABP;
 NEXTUCB:
         IF TABENT = 0
              THEN GO TO INCR1;
         IF TABENT = (16)'1'B
              THEN GO TO NOTMNT;
         UCBADJ = TABENT;
         IF UCBSER = PARMSER
              THEN GO TO FNDUCB;
 INCR1:
         WRKADJ = WRKADJ + 2;
         GO TO NEXTUCB;
 NOTMNT:
         PARMRET = 8;
         RETURN;
 FNDUCB:
         CALL LSPACE(LSMSG,UCBPTR,MYRET);
         IF MYRET ^= 0
              THEN GO TO NOTMNT;
         CALL SYSVTOC('SEARCH',RET,DSNAME,PARMSER,WKAREA);
         IF SUBSTR(CVTDCB,8,1) /* IS IT MVS? */
              THEN IF RET ^= '0'B
                   THEN GO TO NOTMNT;
                   ELSE;
              ELSE IF SUBSTR(RET,1,8) ^= '00001000'B
                   THEN GO TO NOTMNT;
         I = DS4DEVTC; /* I=TRKS/CYL*/
         I = I*LCYL + LTRK; /* I=TRKS AVLBL */
         IF I >= PARMSPCE
              THEN PARMRET = 0;
              ELSE PARMRET = 4;
         END;
