*$.HE GENERALIZED SUBROUTINES - SYSGETM
*$.HE PLI INTERFACE TO GETMAIN MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$3/19/76
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSGETM
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE PROVIDES THE PLI PROGRAMMER WITH AN INTERFACE TO THE
*$GETMAIN MACRO.  CALL FORMATS ARE AS FOLLOWS -
*$.SP 1
*$.IN 5
*$.NF
*$CALL SYSGETM('EC',LV,A,SP,RETCODE);
*$CALL SYSGETM('EU',LV,A,SP,RETCODE);
*$CALL SYSGETM('LC',LA,A,SP,RETCODE,CNT);
*$CALL SYSGETM('LU',LA,A,SP,RETCODE,CNT);
*$CALL SYSGETM('VC',LA,A,SP,RETCODE);
*$CALL SYSGETM('VU',LA,A,SP,RETCODE);
*$CALL SYSGETM('RC',LV,A,SP,RETCODE);
*$CALL SYSGETM('RU',LV,A,SP,RETCODE);
*$.SP 1
*$.FI L
*$.IN 0
*$RETCODE IS FIXED BIN(31,0).
*$.SP 1
*$'E*' - SPECIFIES A REQUEST FOR A SINGLE AREA OF MAIN STORAGE.
*$.SP 1
*$'L*' - SPECIFIES A REQUEST FOR ONE OR MORE AREAS OF MAIN STORAGE.
*$.SP 1
*$'V*' - SPECIFIES A REQUEST FOR A SINGLE AREA OF MAIN STORAGE OF
*$A LENGTH BETWEEN TWO SPECIFIED VALUES.
*$.SP 1
*$'R*' - SPECIFIES A REQUEST FOR A SINGLE AREA OF MAIN STORAGE ABOVE
*$THE 16M LINE.
*$.SP 1
*$'*C' - INDICATES THE REQUEST IS CONDITIONAL AND THAT THE TASK IS
*$NOT TO BE ABNORMALLY TERMINATED IF MORE MAIN STORAGE IS
*$REQUESTED THAN IS AVAILABLE.  IF THE REQUEST IS SATISFIED,
*$RETCODE IS SET TO ZERO; IF NOT SATISFIED, RETCODE IS SET TO FOUR.
*$.SP 1
*$'*U' - INDICATES THE REQUEST IS UNCONDITIONAL.  A REQUEST FOR
*$MORE MAIN STORAGE THAN IS AVAILABLE WILL RESULT IN ABNORMAL
*$TERMINATION OF THE REQUESTING TASK.
*$.SP 1
*$LV - FIXED BIN(31,0) - EQUALS THE LENGTH, IN BYTES, OF THE
*$REQUESTED MAIN STORAGE AREA.
*$.SP 1
*$LA - DEPENDS ON WHETHER 'L*' OR 'V*' WAS SPECIFIED.
*$.SP 1
*$.IN 5
*$'L*' - THE LV PARAMETER MUST BE THE NAME OF THE FIRST ELEMENT IN
*$A STRUCTURE OF FIXED BIN(31,0)
*$ELEMENTS.  EACH ELEMENT MUST CONTAIN THE LENGTH, IN BYTES, OF
*$A REQUESTED MAIN STORAGE AREA.
*$.SP 1
*$'V*' - THE LV PARAMETER MUST BE THE NAME OF THE FIRST ELEMENT IN
*$A STRUCTURE OF FIXED BIN(31,0)
*$PAIRED ELEMENTS.  THE FIRST ELEMENT IN EACH PAIR MUST CONTAIN THE
*$MINIMUM LENGTH REQUIRED, THE SECOND ELEMENT CONTAINS THE MAXIMUM
*$LENGTH.
*$.SP 1
*$.IN 0
*$CNT - FIXED BIN(15,0) - MUST CONTAIN THE COUNT OF MAIN STORAGE
*$AREAS IN THE LIST SPECIFIED BY THE LA PARAMETER.
*$.SP 1
*$A - THE NAME OF THE FIRST ELEMENT IN
*$A STRUCTURE OF ONE OR MORE FIXED BIN(31,0) ELEMENTS AND POINTER
*$VARIABLES.  EACH POINTER VARIABLE WILL BE SET TO THE ADDRESS OF A
*$ALLOCATED MAIN STORAGE AREA.  IF 'E*' WAS CODED, ONE POINTER VARIABLE
*$IS REQUIRED.  IF 'V*' WAS CODED, TWO WORDS ARE REQUIRED.
*$THE FIRST IS A POINTER VARIABLE TO CONTAIN THE ADDRESS OF THE
*$ALLOCATED MAIN STORAGE AREA.  THE SECOND WORD IS FIXED BIN(31,0),
*$AND WILL CONTAIN THE LENGTH ACTUALLY ALLOCATED.  IF L WAS CODED,
*$ONE WORD IS REQUIRED FOR EACH ENTRY IN THE LA LIST.
*$.SP 1
*$SP - FIXED BIN(15,0) - IS THE NUMBER OF THE SUBPOOL FROM WHICH THE
*$MAIN STORAGE AREA IS TO BE ALLOCATED.
SYSGETM  PLIENTRY ,DSA=160,COMPILE=O
         LM    R2,R6,0(R1)
         L     R2,0(R2)
         LA    R7,MODETAB
MODELOOP DS    0H
         CLC   0(2,R2),0(R7)
         BC    8,GOTMODE
         LA    R7,3(R7)
         B     MODELOOP
GOTMODE  DS    0H
         MVC   GETMODE,2(R7)           SET GETMAIN MODE
         CLI   0(R2),C'R'              IS IT 'R*'?
         BC    7,STLALV                BR IF NOT
         L     R0,0(R3)                R0=LENGTH TO GETMAIN
         SR    R1,R1                   R1=0
         ST    R1,WORK                 WORK=0
         MVC   WORK+2(1),SP            WORK=AL2(0),AL1(SUBPOOL)
         MVC   WORK+3(1),GETMODE       WORK=AL2(0),AL1(SP),AL1(GETMODE)
         L     RF,WORK
         SVC   120
         ST    R1,0(R4)                RETURN A(GOTTEN STORAGE)
         B     SETRF
STLALV   DS    0H
         ST    R3,LALV
         CLI   0(R2),C'E'              IS IT 'E*'?
         BC    7,LALVOK                BR IF NOT
         L     R3,0(R3)                R3 = LEN(TO GETMAIN)
         ST    R3,LALV
LALVOK   DS    0H
         ST    R4,A
         MVC   SP,1(R5)
         LA    R1,LALV
         SVC   4
SETRF    DS    0H
         ST    RF,0(R6)
EXIT     PLIEXIT
MODETAB  DC    C'EC',X'20'
         DC    C'EU',X'00'
         DC    C'LC',X'A0'
         DC    C'LU',X'80'
         DC    C'RC',X'70'
         DC    C'RU',X'72'
         DC    C'VC',X'E0'
         DC    C'VU',X'C0'
         DC    X'FF'
         LTORG
DSA      DSECT
LALV     DS    F  ADDR OF LA/LV PARM
A        DS    F  ADDR OF A PARM
GETMODE  DS    X  GETMAIN MODE
SP       DS    X  SUBPOOL #
WORK     DS    F
         END
