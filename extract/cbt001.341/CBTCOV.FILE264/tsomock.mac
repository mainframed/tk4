 /* TSOMOCK */
 /*
0*$.HE GENERALIZED SUBROUTINES - TSOMOCK
0*$.HE FILLS IN THE CPPL
0*$.PL 55
0*$.PN 0
0*$.PA
0*$.SS
0*$.NF
0*$11/05/82
0*$AUTHOR:  T. SMITH
0*$SOURCE MODULE NAME:  TSOMOCK
0*$DOCUMENTATION ID:  0*$
0*$.FI L
0*$.SP 5
0*$THIS SUBROUTINE ACCEPTS THREE PARAMETERS, CPPLUPT, CPPLPSCB, AND
0*$CPPLECT, ALL OF WHICH ARE DECLARED POINTER.  TSOMOCK WILL FILL
0*$THESE POINTERS IN WITH THE ADDRESS OF THE CONTROL BLOCKS UPT, PSCB,
0*$AND ECT.  WITH THESE ADDRESSES, THE CALLING PROGRAM CAN PERFORM
0*$TASKS SUCH AS THOSE PERFORMED BY TSO COMMAND PROCESSORS.
0*$.SP 1
0*$NOTE UNLESS YOUR PROGRAM WAS INVOKED UNDER IKJEFT01, THESE CONTROL
0*$BLOCKS WILL NOT HAVE BEEN BUILT.  IN THIS CASE, THE ADDRESSES
0*$RETURNED POINT TO DUMMY AREAS INSIDE TSOMOCK.
 */
 TSOMOCK: PROC(CPPLUPT,CPPLPSCB,CPPLECT) OPTIONS(REENTRANT);
   DCL (CPPLUPT,CPPLPSCB,CPPLECT) PTR;
   DCL TCBPTR PTR BASED(TCBPTRPTR);
   DCL 1 TCB BASED(TCBPTR),
     2 TCBPAD CHAR(180),
     2 TCBJSCB PTR;
   DCL 1 JSCB BASED(TCBJSCB),
     2 JSCBPAD CHAR(264),
     2 JSCBPSCB PTR;
   DCL 1 PSCB BASED(JSCBPSCB),
     2 PSCBPAD1 CHAR(48),
     2 PSCBRLGB PTR,
     2 PSCBUPT PTR;
   DCL 1 RLGB BASED(PSCBRLGB),
     2 RLGBPAD CHAR(256),
     2 RLGBECT PTR;
   DCL (MYUPT,MYPSCB,MYECT) CHAR(256) STATIC;
   UNSPEC(TCBPTRPTR)=UNSPEC(BINARY(540,31,0));
   CPPLUPT=PSCBUPT;
   CPPLPSCB=JSCBPSCB;
   CPPLECT=RLGBECT;
   IF UNSPEC(CPPLECT)=0
     THEN DO;
       MYUPT,MYPSCB,MYECT=LOW(256);
       CPPLUPT=ADDR(MYUPT);
       CPPLPSCB=ADDR(MYPSCB);
       CPPLECT=ADDR(MYECT);
       END;
   END TSOMOCK;
