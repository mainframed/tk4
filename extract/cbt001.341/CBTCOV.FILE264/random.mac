*           RNDG - RANDOM NUMBERS FROM A NORMAL DISTRIBUTION
RNDG     CSECT CONTROL SECTION FOR FLOATING-POINT NUMBER.
         USING RNDG,15  ESTABLISH BASE REGISTER
         STM   0,3,20(13)      SAVE R0-R3
         L     1,0(1)        ADDRESS OF PARAMETERS
         LR    3,1
         LD    0,=D'0.0'     ZERO
         STD   0,TEMP1       CLEAR TEMPORARY ACCUMULATOR
         LA    2,12(0)       INDEX
LOOP     EQU   *
         L     1,0(3)  PICK UP X(N)
         M     0,=F'16807'  X(N+1)=K*X(N) MOD(2**31-1)  K=7**5=16807
         SLDA  0,1 ISOLATE HIGH ORDER 31 BITS (R).
         SRL   1,1 ISOLATE LOW ORDER 31 BITS. SIGN +.
         AR    0,1 X'(N+1) = K*X(N) + R (LOW ORDER 31 BITS).
         BO    OVF X'(N+1) OVERFLOW.
CONT     ST    0,0(3)   X'(N+1) IN FORTRAN PROGRAM
         SR    1,1 ZERO LOW ORDER DOUBLEWORD.
         SRDA  0,7 MAKE PCOM FOR MASK.
         A     0,=X'40000000' MASK IN FLOATING-POINT EXPONENT.
         STM   0,1,TEMP STORE UNNORMALIZED DOUBLEWORD TEMPORARILY.
         SDR   0,0 CLEAR LOW-ORDER DOUBLE FLOATING-POINT REG.
         AD    0,TEMP PLACE NORMALIZED DOUBLEWORD FLOATING-POINT       X
               NUMBER IN FLOATING-POINT REGISTER 0.
         AD    0,TEMP1       ADD ACCUMULATOR
         STD   0,TEMP1       NEW ACCUMULATOR
         BCT   2,LOOP        TEST FOR DONE
         SD    0,=D'6.0'     MINUS 6
         LM    0,3,20(13)    RESTORE R0-R3
         MVI   12(13),X'FF'   SET RETURN INDICATOR
         BR    14 RETURN TO FORTRAN.
OVF      SLL   0,1 SIGN BIT +.
         SRL   0,1 SIGN BIT +.
         A     0,=F'1' ADD 1 TO GET X'(N+1) + 1.
         B     CONT RETURN TO MAIN CODING TO FINISH.
TEMP     DS    D TEMPORARY STORAGE ON DOUBLEWORD BOUNDARY FOR SPEED.
TEMP1    DS    D             TEMPORARY ACCUMULATOR
         LTORG FORCE LITERAL ASSEMBLY INTO THIS CONTROL SECTION.
         END
*           RNDGN - RANDOM NUMBERS FROM A NORMAL DISTRIBUTION
RNDGN    CSECT CONTROL SECTION FOR FLOATING-POINT NUMBER.
         USING RNDGN,15 ESTABLISH BASE REGISTER
         STM   14,12,12(13)   SAVE REGISTERS
         LM    1,3,0(1)      ADDRESS OF PARAMETERS
         LR    5,1
         L     3,0(3)        3 = NUMBER
LOOP1    LD    0,=D'0.0'     ZERO
         STD   0,TEMP1       CLEAR TEMPORARY ACCUMULATOR
         LA    4,12(0)       INDEX
LOOP     EQU   *
         L     1,0(5)   PICK UP X(N)
         M     0,=F'16807'  X(N+1)=K*X(N) MOD(2**31-1)  K=7**5=16807
         SLDA  0,1 ISOLATE HIGH ORDER 31 BITS (R).
         SRL   1,1 ISOLATE LOW ORDER 31 BITS. SIGN +.
         AR    0,1 X'(N+1) = K*X(N) + R (LOW ORDER 31 BITS).
         BO    OVF X'(N+1) OVERFLOW.
CONT     ST    0,0(5)   STORE X(N+1)
         SR    1,1 ZERO LOW ORDER DOUBLEWORD.
         SRDA  0,7 MAKE PCOM FOR MASK.
         A     0,=X'40000000' MASK IN FLOATING-POINT EXPONENT.
         STM   0,1,TEMP STORE UNNORMALIZED DOUBLEWORD TEMPORARILY.
         SDR   0,0 CLEAR LOW-ORDER DOUBLE FLOATING-POINT REG.
         AD    0,TEMP PLACE NORMALIZED DOUBLEWORD FLOATING-POINT       X
               NUMBER IN FLOATING-POINT REGISTER 0.
         AD    0,TEMP1       ADD ACCUMULATOR
         STD   0,TEMP1       NEW ACCUMULATOR
         BCT   4,LOOP        TEST FOR DONE
         SD    0,=D'6.0'     MINUS 6
         STE   0,0(2)        VALUE TO ARRAY
         LA    2,4(2)        BUMP ARRAY
         BCT   3,LOOP1       TEST FOR DONE
         LM    14,12,12(13)   RESTORE REGISTERS
         MVI   12(13),X'FF'   SET RETURN INDICATOR
         BR    14 RETURN TO FORTRAN.
OVF      SLL   0,1 SIGN BIT +.
         SRL   0,1 SIGN BIT +.
         A     0,=F'1' ADD 1 TO GET X'(N+1) + 1.
         B     CONT RETURN TO MAIN CODING TO FINISH.
TEMP     DS    D TEMPORARY STORAGE ON DOUBLEWORD BOUNDARY FOR SPEED.
TEMP1    DS    D             TEMPORARY ACCUMULATOR
         LTORG FORCE LITERAL ASSEMBLY INTO THIS CONTROL SECTION.
         END
*           RNDU - RANDOM NUMBERS FROM A UNIFORM DISTRIBUTION
RNDU     CSECT CONTROL SECTION FOR FLOATING-POINT NUMBER.
         USING RNDU,15 ESTABLISH BASE REGISTER FOR NEW SECTION.
         STM   0,2,20(13)    SAVE REG. 0-2
         L     1,0(,1) GET ADDRESS OF NO (FORTRAN) IN REGISTER
         LR    2,1
         L     1,0(2)    X(N)
         M     0,=F'16807'  X(N+1)=K*X(N) MOD(2**31-1)  K=7**5=16807
         SLDA  0,1 ISOLATE HIGH ORDER 31 BITS (R).
         SRL   1,1 ISOLATE LOW ORDER 31 BITS. SIGN +.
         AR    0,1 X'(N+1) = K*X(N) + R (LOW ORDER 31 BITS).
         BO    OVF X'(N+1) OVERFLOW.
CONT     ST    0,0(,2)   X'(N+1) STORE X'(N+1) IN ADDRESS FROM FORTRAN
         SR    1,1 ZERO LOW ORDER DOUBLEWORD.
         SRDA  0,7 MAKE PCOM FOR MASK.
         A     0,=X'40000000' MASK IN FLOATING-POINT EXPONENT.
         STM   0,1,TEMP STORE UNNORMALIZED DOUBLEWORD TEMPORARILY.
         SDR   0,0 CLEAR LOW-ORDER DOUBLE FLOATING-POINT REG.
         AD    0,TEMP PLACE NORMALIZED DOUBLEWORD FLOATING-POINT       X
               NUMBER IN FLOATING-POINT REGISTER 0.
         LM    0,2,20(13)    RESTORE   R0-R2
         MVI   12(13),X'FF'   SET RETURN INDICATOR
         BR    14 RETURN TO FORTRAN.
OVF      SLL   0,1 SIGN BIT +.
         SRL   0,1 SIGN BIT +.
         A     0,=F'1' ADD 1 TO GET X'(N+1) + 1.
         B     CONT RETURN TO MAIN CODING TO FINISH.
TEMP     DS    D TEMPORARY STORAGE ON DOUBLEWORD BOUNDARY FOR SPEED.
         LTORG FORCE LITERAL ASSEMBLY INTO THIS CONTROL SECTION.
         END
*           RNDUN - RANDOM NUMBERS FROM A UNIFORM DISTRIBUTION
RNDUN    CSECT CONTROL SECTION FOR FLOATING-POINT NUMBER.
         USING RNDUN,15 ESTABLISH BASE REGISTER
         STM   0,4,20(13)   SAVE GR 0-4
         LM    1,3,0(1)      ADDRESS OF PARAMETERS
         LR    4,1
         L     3,0(3)        3 = NUMBER
LOOP     EQU   *
         L     1,0(4)   LOAD X(N) IN R1
         M     0,=F'16807'  X(N+1)=K*X(N) MOD(2**31-1)  K=7**5=16807
         SLDA  0,1 ISOLATE HIGH ORDER 31 BITS (R).
         SRL   1,1 ISOLATE LOW ORDER 31 BITS. SIGN +.
         AR    0,1 X'(N+1) = K*X(N) + R (LOW ORDER 31 BITS).
         BO    OVF X'(N+1) OVERFLOW.
CONT     ST    0,0(4)  X'(N+1) GOES IN FORTRAN PROVIDED STORAGE
         SR    1,1 ZERO LOW ORDER DOUBLEWORD.
         SRDA  0,7 MAKE PCOM FOR MASK.
         A     0,=X'40000000' MASK IN FLOATING-POINT EXPONENT.
         STM   0,1,TEMP STORE UNNORMALIZED DOUBLEWORD TEMPORARILY.
         SDR   0,0 CLEAR LOW-ORDER DOUBLE FLOATING-POINT REG.
         AD    0,TEMP PLACE NORMALIZED DOUBLEWORD FLOATING-POINT       X
               NUMBER IN FLOATING-POINT REGISTER 0.
         STE   0,0(2)        VALUE TO ARRAY
         LA    2,4(2)        BUMP ARRAY
         BCT   3,LOOP        TEST FOR DONE
         MVI   12(13),X'FF'   SET RETURN INDICATOR
         LM    0,4,20(13)    RESTORE R0-R4
         BR    14 RETURN TO FORTRAN.
OVF      SLL   0,1 SIGN BIT +.
         SRL   0,1 SIGN BIT +.
         A     0,=F'1' ADD 1 TO GET X'(N+1) + 1.
         B     CONT RETURN TO MAIN CODING TO FINISH.
TEMP     DS    D TEMPORARY STORAGE ON DOUBLEWORD BOUNDARY FOR SPEED.
         LTORG FORCE LITERAL ASSEMBLY INTO THIS CONTROL SECTION.
         END
