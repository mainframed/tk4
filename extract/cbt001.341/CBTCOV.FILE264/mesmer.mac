 MESMER:  PROC OPTIONS(MAIN);
         DFT RANGE(*) FIXED BIN;
         DCL PLIXOPT CHAR(80) VAR EXT INIT('ISA(50K)');
         DCL AST CHAR(80) INIT((80)'*');
         DCL 1 BUFFER UNALIGNED,
              2 STRLEN FIXED BIN(15,0) INIT(1952),
              2 WCC BIT(8) INIT('11110011'B),
                   /*              11 SPECIFIES 80 CHAR LINE
                                     0 DON'T START PRINTER
                                      0 DON'T SOUND ALARM
                                       1 RESET INPUT INHIBITED
                                        1 RESET MDT BITS
                   */
              2 SBACOD BIT(8) INIT('00010001'B),
              2 SBADDR FIXED BIN(15,0) INIT(0),
              2 LINES(24),
               3 SFC BIT(16) INIT((24)(1)'0001110101000000'B),
                   /*                        1   D   4   0   */
               3 LINE CHAR(79) INIT((24)(79)' ');
         DCL SWITCH BIT(8) STATIC INIT('1'B);
         CALL SBACONV(SBADDR,SWITCH);
         DCL OUTBUF CHAR(1950) VARYING BASED(BUFPTR);
         BUFPTR = ADDR(STRLEN);
         DCL RETCODE FIXED BIN(31,0);
 NXT:
         LINE(*) = ' ';
         SUBSTR(LINE(12),38,1)='*';
         DO I = 0 TO 9 BY 3;
           IF I ^= 0
             THEN DO;
              HORS = 38 - I*4;
              HORE = 38 + I*4;
              IF I < 12
                   THEN DO;
                        VIRS = 12 - I;
                        VIRE = 12 + I;
                        END;
                   ELSE DO;
                        VIRS = 1;
                        VIRE = 24;
                        END;
              LH = HORE-HORS;
              LV = LH/2;
              IF LV > 4
                  THEN LV = 4;
              SUBSTR(LINE(VIRS),HORS,HORE-HORS)=AST;
              SUBSTR(LINE(VIRE),HORS,HORE-HORS)=AST;
              HORE = HORE - LV;
              DO K = VIRS TO VIRE;
                   SUBSTR(LINE(K),HORS,LV)=AST;
                   SUBSTR(LINE(K),HORE,LV)=AST;
                   END;
              END;
              CALL SYSTPUT(OUTBUF,RETCODE,'FULLSCR','WAIT','HOLD');
              END;
         GO TO NXT;
         END;
