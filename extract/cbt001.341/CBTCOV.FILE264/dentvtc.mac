 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* DENTVTC */
 DENTVTC:  PROC(NEWCMD,RHPTR,SUBENT,RHHDRPT);
         DFT RANGE(*) STATIC;
         % DCL @PROCNAME CHAR;
         % @PROCNAME = '';
         % INCLUDE DENTSUB;
         % INCLUDE @HEXCHAR;
         DCL AT CHAR(2) STATIC INIT('AT');
         DCL LOAT CHAR(2) STATIC INIT(@HEXCHAR(81A3));
           /* LOAT = LOWER CASE AT */
         DCL WRKBUF CHAR(256) VAR;
         DCL UIDSYM CHAR(8) STATIC INIT('&SYSUID');
         DCL UID CHAR(80) VAR;
         DCL NEWCMD CHAR(*) VAR;
         DCL OPCODE FIXED BIN(15,0);
         DCL GCODE FIXED BIN(15,0) STATIC INIT(4);
         DCL RETCODE FIXED BIN(31,0);
         DCL DATABUF CHAR(256) VAR STATIC INIT('');
         DCL (SYSCALL,SETCAPS) ENTRY;
         DCL MYRPTR PTR;
         IF LSTHDR=HDPTR
           THEN DO;
             ADDSEQ=1;
             MYRPTR=LSTHDR;
             CALL #ADD(RHPTR,SUBENT,MYRPTR,MYRPTR,ADDSEQ,DATABUF,
               RHPPTR); /* CALL ADDREC */
             END;
         MYRPTR=LSTHDR;
         MYRPTR->RECSTR=MYRPTR->RECSTR××NEWCMD;
         ADDSEQ = LSTHDR->RECSEQ+1;
         CALL #ADD(RHPTR,SUBENT,MYRPTR,MYRPTR,ADDSEQ,DATABUF,
           RHPPTR); /* CALL ADDREC */
         /* NOW SET RECPTR */
         J = RHCEN-RHCST-1;
         DO RECPTR=MYRPTR REPEAT LSTPTR WHILE(J>0)
           UNTIL(LSTPTR=HDPTR);
           J=J-1;
           END;
         CURSEQ = RECSEQ;
         IF ^LOGNFLAG
           THEN DO;
             IF TERMID='TSO'
               THEN DO;
                 WRKBUF=NEWCMD;
                 CALL SETCAPS(WRKBUF);
                 UID=#SYMC(UIDSYM);
                 IF INDEX(WRKBUF,'LOGON ')^=0
                   THEN IF INDEX(WRKBUF,SUBSTR(UID,1,6))=0
                     THEN DO;
                       MSGDATA='INVALID USERID';
                       RETURN;
                       END;
                 END;
             MSGDATA = TERMID; /* PASS APPLICATION ID */
             OPCODE = 0; /* INDICATE START */
             LOGNFLAG = '1'B;
             END;
           ELSE IF NEWCMD=AT × NEWCMD=LOAT
             THEN OPCODE = 20; /* INDICATE SIGNAL ATTENTION */
             ELSE OPCODE = 8; /* INDICATE PUT */
         CALL SYSCALL(TERMEP,OPCODE,NEWCMD,
           MSGDATA,RETCODE,TERMECB);
           /*CALL VTAMIO TO DO PUT (OR MAYBE START) */
         IF RETCODE^=0
           THEN IF MSGDATA=''
             THEN MSGDATA = 'BAD RETCODE FROM VTAMIO';
             ELSE;
           ELSE IF OPCODE = 0 /* WAS AT START? */
             THEN DO;
               CALL SYSCALL(TERMEP,GCODE,BUF,MSGDATA,RETCODE,TERMECB);
                 /* ESTABLISH OUTSTANDING GET */
               IF RETCODE^=0 × MSGDATA^=''
                 THEN SIGNAL ERROR;
               END;
         END;
