*$.HE GENERALIZED SUBROUTINES - GETREG
*$.HE GET CONTENTS OF GENERAL REGISTERS
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$12/07/79
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  GETREG
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS SUBROUTINE CAN BE USED TO OBTAIN THE CURRENT VALUE IN
*$ANY GENERAL REGISTER.  CALL FORMAT IS AS FOLLOWS:
*$.SP 1
*$.IN 5
*$.NF
*$DCL GETREG ENTRY(FIXED BIN(15,0))
*$  RETURNS(FIXED BIN(31,0));
*$I = GETREG(0); ** RETURNS REGISTER 0 **
GETREG   CSECT
         TGSREQU
         USING *,RF
         STM   RE,RC,12(RD)
         LM    R2,R3,0(R1)        R3 = A(TO STORE REG)
         LH    R2,0(R2)           R2 = DESIRED REG #
         SLL   R2,2(0)            MULTIPLY BY 4
         CH    R2,=H'48'          IS IT > REG 12
         BC    2,HIREG
         L     R4,20(R2,RD)       RF = DESIRED REG
         B     STORE
HIREG    DS    0H
         CH    R2,=H'52'          IS IT REG 13?
         BC    2,RERF             BR IF NOT
         LR    R4,RD              R4 = DESIRED REG
         B     STORE
RERF     DS    0H
         SH    R2,=H'44'
         L     R4,0(R2,RD)        R4 = DESIRED REG
STORE    DS    0H
         ST    R4,0(R3)
         LM    RE,RC,12(RD)
         BR    RE
         END
