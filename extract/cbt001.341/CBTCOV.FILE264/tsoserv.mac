*$.HE ISD GENERALIZED SUBROUTINES - TSOSERV
*$.HE INTERFACE TO TSO SERVICES OR ANY SVC
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$05/05/85
*$AUTHOR:  PAUL DIXON
*$SOURCE MODULE NAME:  TSOSERV
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$PASSES CONTROL TO A TSO SERVICE ROUTINE FROM PL/I. RECEIVES:
*$('NAME',OTHER PARAMETERS FOR THE SERVICE) NAME CAN BE IKJDAIRX,
*$IKJCT441, IKJEFTSR, IKJGETL, IKJPUTL, IKJPTGT, IKJSTCK, IKJSCAN,
*$IKJPARS, IKJDAIR, IKJEHDEF, IKJEHCIR, OR IKJEFF02.
*$.SP 1
*$SVC:  IF THE NAME IS 'SVC' THEN THE NEXT PARAMETERS ARE: HALFWORD
*$SVC NUMBER, FULLWORD R15 DATA, FULLWORD R0 DATA, AND FULLWORD R1
*$DATA.  R15-R1 ARE LOADED FROM THE PARAMETERS, THE SVC IS ISSUED,
*$AND R15-R1 ARE STORED BACK INTO THE PARAMETERS.  R15 ALSO BECOMES
*$THE RETURN CODE.  YOU CAN %INCLUDE @9DCLSVC TO OBTAIN A PL/I
*$DECLARATION OF TSOSERV.
*$.SP 1
*$RETURNS:  0054 IF TSO SERVICE IS NOT KNOWN 0154 IF TSO SERVICE IS
*$NOT AVAILABLE ELSE THE RETURN CODE FROM THE TSO SERVICE OR SVC
*$.SP 1
*$ATTRIB:   REENTRANT, REFRESHABLE, REUSABLE
*$.SP 1
*$NOTE:     THIS WOULDN'T BE NECESSARY IF PL/I WOULD FETCH
*$A MODULE FROM SYS1.LPALIB
*$.SP 1
*$.NF
*$EXAMPLES:
*$CALL TSOSERV('IKJPUTL ',UPT,ECT,0,PTPB);
*$CALL TSOSERV('IKJDAIR ',UPT,ECT,0,PSCB,DAPB08);
*$CALL TSOSERV('IKJEFTSR',1,'LISTA',  5,RC1,RC2,RC3);
*$CALL TSOSERV('IKJEFTSR',2,'IEBCOPY',7,RC1,RC2,RC3);
*$CALL TSOSERV('SVC',10,0,72,-1); (GETMAIN 72 BYTES)
*$CALL TSOSERV('SVC',13,4,0,366); (ABEND 366,REAS=4)
*$CALL TSOSERV('SVC',35,0,0,ADDR(WTOMSG));
*$CALL TSOSERV('SVC',93,0,LENGTH(TPUT),ADDR(TPUT));
*$CALL TSOSERV('SVC',99,0,0,ADDR(S99RBPTR));
*$RELEASE TSOSERV /* IDENTIFY AS FETCHED MODULE */;
X0054    CSECT ,   INTERFACE TO TSO SERVICES OR ANY SVC
         SAVE  (14,12),,               SAVE REGISTERS                  +
               'X0054 V1R2M0 &SYSDATE &SYSTIME'
         LR    12,15                   R12: ...
         USING X0054,12                ... PROGRAM BASE
         LR    2,1                     R2: @ PARAMETER LIST
         LA    0,WORK#                 R0: # WORK AREA
         GETMAIN R,LV=(0)              R1: @ WORK AREA
         ST    13,4(1)                 CHAIN NEW SAVE AREA TO OLD
         ST    1,8(13)                 CHAIN OLD SAVE AREA TO NEW
         LR    13,1                    R13: ...
         USING WORK,13                 ... WORK AREA
         LA    1,4(2)                  R1: @ 2ND PARAMETER ADDRESS
         L     2,0(2)                  R2: @ SERVICE NAME
         CLC   =C'SVC     ',0(2)       SVC?
         BNE   CK#DAIRX                NO, TRY IKJCT441
         LM    3,6,0(1)                R3:SVC# R4:@R15 R5:@R1 R6:@R0
         LH    3,0(3)                  R3:  SVC#
         L     15,0(4)                 R15: R15 DATA
         L     0,0(5)                  R0:  R0  DATA
         L     1,0(6)                  R1:  R1  DATA
         EX    3,SVC                   SVC *-*
         ST    15,0(4)                 R15: R15 DATA
         ST    0,0(5)                  R0:  R0  DATA
         ST    1,0(6)                  R1:  R1  DATA
         B     EXIT                    CLEANUP AND RETURN
CK#DAIRX LA    11,CK#EP                R11: @ ENTRY POINT CHECK CODE
         LA    14,EXIT                 R14: @ EXIT CODE
         L     15,CVTPTR               R15: ...
         USING CVT,15                  ... @ CVT
         CLC   =C'IKJDAIRX',0(2)       IKJDAIRX?
         BNE   CK#CT441                NO, TRY IKJCT441
         ICM   15,B'1111',CVTDAIRX     R15: IKJDAIRX ENTRY POINT
         BR    11                      GO CALL IKJDAIR
CK#CT441 CLC   =C'IKJCT441',0(2)       IKJCT441?
         BNE   CK#EFTSR                NO, TRY IKJEFTSR
         ICM   15,B'1111',CVTTVT       R15: @ TSO VECTOR TABLE
         BZ    ERROR154                EXIT IF SERVICE NOT AVAILABLE
         ICM   15,B'1111',TSVTVACC-TSVT(15) R15: IKJCT441 ENTRY POINT
         BR    11                      GO CALL IKJCT441
CK#EFTSR CLC   =C'IKJEFTSR',0(2)       IKJEFTSR?
         BNE   CK#GETL                 NO, TRY IKJGETL
         ICM   15,B'1111',CVTTVT       R15: @ TSO VECTOR TABLE
         BZ    ERROR154                EXIT IF SERVICE NOT AVAILABLE
         ICM   15,B'1111',TSVTASF-TSVT(15) R15: IKJEFTSR ENTRY POINT
         BR    11                      GO CALL IKJEFTSR
CK#GETL  CLC   =C'IKJGETL ',0(2)       IKJGETL?
         BNE   CK#PUTL                 NO, TRY IKJPUTL
         ICM   15,B'1111',CVTGETL      R15: IKJGETL  ENTRY POINT
         BR    11                      GO CALL IKJGETL
CK#PUTL  CLC   =C'IKJPUTL ',0(2)       IKJPUTL?
         BNE   CK#PTGT                 NO, TRY IKJPTGT
         ICM   15,B'1111',CVTPUTL      R15: IKJPUTL  ENTRY POINT
         BR    11                      GO CALL IKJPUTL
CK#PTGT  CLC   =C'IKJPTGT ',0(2)       IKJPTGT?
         BNE   CK#STCK                 NO, TRY IKJSTCK
         ICM   15,B'1111',CVTPTGT      R15: IKJPTGT  ENTRY POINT
         BR    11                      GO CALL IKJPTGT
CK#STCK  CLC   =C'IKJSTCK ',0(2)       IKJSTCK?
         BNE   CK#SCAN                 NO, TRY IKJSCAN
         ICM   15,B'1111',CVTSTCK      R15: IKJSTCK  ENTRY POINT
         BR    11                      GO CALL IKJSTCK
CK#SCAN  CLC   =C'IKJSCAN ',0(2)       IKJSCAN?
         BNE   CK#PARS                 NO, TRY IKJPARS
         ICM   15,B'1111',CVTSCAN      R15: IKJSCAN  ENTRY POINT
         BR    11                      GO CALL IKJSCAN
CK#PARS  CLC   =C'IKJPARS ',0(2)       IKJPARS?
         BNE   CK#DAIR                 NO, TRY IKJDAIR
         ICM   15,B'1111',CVTPARS      R15: IKJPARS  ENTRY POINT
         BR    11                      GO CALL IKJPARS
CK#DAIR  CLC   =C'IKJDAIR ',0(2)       IKJDAIR?
         BNE   CK#EHDEF                NO, TRY IKJEHDEF
         ICM   15,B'1111',CVTDAIR      R15: IKJDAIR  ENTRY POINT
         BR    11                      GO CALL IKJDAIR
CK#EHDEF CLC   =C'IKJEHDEF',0(2)       IKJEHDEF?
         BNE   CK#EHCIR                NO, TRY IKJEHCIR
         ICM   15,B'1111',CVTEHDEF     R15: IKJEHDEF ENTRY POINT
         BR    11                      GO CALL IKJEHCIR
CK#EHCIR CLC   =C'IKJEHCIR',0(2)       IKJEHCIR?
         BNE   CK#EFF02                NO, TRY IKJEFF02
         ICM   15,B'1111',CVTEHCIR     R15: IKJEHCIR ENTRY POINT
         BR    11                      GO CALL IKJEHCIR
CK#EFF02 CLC   =C'IKJEFF02',0(2)       IKJEFF02?
         BNE   ERROR054                NO, ERROR
         ICM   15,B'1111',CVTEFF02     R15: IKJEFF02 ENTRY POINT
         DROP  15                      DONE WITH CVT
CK#EP    BNZR  15                      CALL TSO SERVICE
ERROR154 LA    15,154                  R15: ERROR RETURN CODE
         BR    14                      CLEANUP AND RETURN
ERROR054 LA    15,054                  R15: ERROR RETURN CODE
EXIT     LR    2,15                    R2: TSO SERVICE RETURN CODE
         LA    0,WORK#                 R0: # WORK AREA
         DROP  12                      PROGRAM BASE
         LR    1,13                    R1: @ WORK AREA
         L     13,4(13)                R13: @ PREVIOUS SAVE AREA
         DROP  13                      WORK AREA
         FREEMAIN R,LV=(0),A=(1)       FREE WORK AREA
         LR    15,2                    R15: TSO SERVICE RETURN CODE
         RETURN (14,12),RC=(15)        RETURN TO CALLER
         EJECT
SVC      SVC   *-*                     EXECUTED
         LTORG ,                       ASSEMBLE LITERALS HERE
WORK     DSECT ,                       X0054 WORK AREA
         DS    18F                     OUR SAVE AREA
WORK#    EQU   *-WORK                  LENGTH OF WORK AREA
         PRINT NOGEN                   DON'T PRINT CVT
         CVT   DSECT=YES               SYS1.AMODGEN
         IKJTSVT ,                     SYS1.MACLIB
         END
