         ISEQ  73,80
GETJFCB  START 0                                                    TGS
********************************************************************TGS
*                  THIS RTNE CAN BE USED TO OBTAIN A JFCB          *TGS
*              ASSOCIATED WITH A GIVEN DD NAME.  IT IS SPECIFICALLY*TGS
*              DESIGNED TO INTERFACE WITH PLI. HOWEVER, IT IS      *TGS
*              SUFFICIENTLY INDEPENDENT OF THAT LANGUAGE TO BE     *TGS
*              INTERFACED WITH COBOL, OR ASSEMBLER LANGUAGE.       *TGS
*                  NOTE THAT THIS ROUTINE IS SERIALLY REUSABLE, AND*TGS
*              CARE SHOULD BE TAKEN IN A MULTITASKING ENVIRONMENT. *TGS
*                  UPON ENTRY, THIS ROUTINE EXPECTS REG1 TO        *TGS
*              CONTAIN THE ADDRESS OF A TWO-WORD PARAMETER LIST.   *TGS
*              THE FIRST WORD SHOULD CONTAIN THE ADDRESS OF A WORD *TGS
*              CONTAINING THE ADDRESS OF THE EIGHT BYTE DD NAME    *TGS
*              ASSOCIATED WITH THE JFCB THAT IS TO BE READ IN.     *TGS
*              THE SECOND WORD SHOULD CONTAIN THE ADDRESS OF A     *TGS
*              WORD CONTAINING THE ADDRESS OF A AREA NOT LESS THAN *TGS
*              176 BYTES IN LENGTH, THE JFCB WILL BE READ INTO THIS*TGS
*              AREA.                                               *TGS
*                  NOTE THAT THIS ROUTINE WILL ISSUE A SPIE MACRO  *TGS
*              UPON ENTRY TO NULLIFY THE USE OF PLIS ERROR HANDLING*TGS
*              ROUTINE.  PLIS PICA WILL BE RESTORED PRIOR TO       *TGS
*              RETURN OF CONTROL.                                  *TGS
*                  NOTE THAT THIS ROUTINE CONTAINS THE ADDITIONAL  *TGS
*              ENTRY POINT, PUTJFCB.  FOR AN EXPLANATION OF WHAT   *TGS
*              IS ACCOMPLISHED AT THIS ENTRY POINT, REFER TO THE   *TGS
*              COMMENTS IN THIS LISTING UNDER THE STATEMENT        *TGS
*              LABELLED PUTJFCB.                                   *TGS
********************************************************************TGS
R0       EQU   0                                                    TGS
R1       EQU   1                                                    TGS
R2       EQU   2                                                    TGS
R3       EQU   3                                                    TGS
R4       EQU   4                                                    TGS
R5       EQU   5                                                    TGS
R6       EQU   6                                                    TGS
R7       EQU   7                                                    TGS
R8       EQU   8                                                    TGS
R9       EQU   9                                                    TGS
RA       EQU   10                                                   TGS
RB       EQU   11                                                   TGS
RC       EQU   12                                                   TGS
RD       EQU   13                                                   TGS
RE       EQU   14                                                   TGS
RF       EQU   15                                                   TGS
BEGIN    EQU   *                                                    TGS
         USING *,RC                                                 TGS
         STM   RE,RC,12(RD)            SAVE CALLERS REGS            TGS
         LR    RC,RF                   SET BASE REG                 TGS
         BAL   RA,INIT                                              TGS
         RDJFCB DUMDCB                                              TGS
         B     ALLDONE                                              TGS
         DROP  RC                                                   TGS
         ENTRY PUTJFCB                                              TGS
PUTJFCB  EQU   *                                                    TGS
********************************************************************TGS
*                  THIS ROUTINE IS USED IF A CALLER HAS ALREADY    *TGS
*              INVOKED GETJFCB, HAS MODIFIED THE JFCB HE OBTAINED, *TGS
*              AND NOW WISHES TO PLACE THAT JFCB BACK IN           *TGS
*              SYSJOBQE.                                           *TGS
*                  THE RTNE EXPECTS REG1 TO CONTAIN THE ADDRESS OF *TGS
*              A TWO-WORD PARAMETER LIST.  THE FIRST WORD SHOULD   *TGS
*              CONTAIN THE ADDRESS OF A WORD CONTAINING THE        *TGS
*              ADDRESS OF THE EIGHT BYTE DD NAME ASSOCIATED WITH   *TGS
*              THE JFCB THAT IS TO BE READ IN.  THE SECOND WORD    *TGS
*              SHOULD CONTAIN THE ADDRESS OF A WORD CONTAINING THE *TGS
*              ADDR OF THE JFCB TO BE WRITTEN BACK INTO SYSJOBQE.  *TGS
*                  NOTE THAT THIS ROUTINE WILL ISSUE A SPIE MACRO  *TGS
*              UPON ENTRY TO NULLIFY THE USE OF PLIS ERROR         *TGS
*              HANDLING ROUTINE.  PLIS PICA WILL BE RESTORED PRIOR *TGS
*              TO RETURN OF CONTROL.                               *TGS
********************************************************************TGS
         USING *,RF                                                 TGS
         STM   RE,RC,12(RD)            SAVE CALLERS REGS            TGS
         L     RC,BEGINADR             SET BASE REG                 TGS
         DROP  RF                                                   TGS
         USING BEGIN,RC                                             TGS
         BAL   RA,INIT                                              TGS
         L     R3,4(R0,R2)             GET JFCB PARM PTR            TGS
         L     R3,0(R0,R3)             GET JFCB PTR                 TGS
         OI    72(R3),X'80'            TURN ON JFCBMASK+4, BIT 0    TGS
         OPEN  (DUMDCB,(OUTPUT)),TYPE=J                             TGS
         CLOSE (DUMDCB)                                             TGS
         B     ALLDONE                                              TGS
INIT     EQU   *                                                    TGS
         LR    R2,R1                   SAVE REG1 IN REG2            TGS
         SPIE
         ST    R1,PICAHOLD                                          TGS
         L     R3,0(R0,R2)             GET DDNAME PARM PTR          TGS
         L     R3,0(R0,R3)             GET DDNAME PTR               TGS
         MVC   DUMDCB+40(8),0(R3)      MOVE DDNAME TO DUMDCB        TGS
         L     R3,4(R0,R2)             GET JFCBAREA PARM PTR        TGS
         L     R3,0(R0,R3)             GET JFCBAREA PTR             TGS
         ST    R3,EXLST                SET EXLST                    TGS
         OI    EXLST,X'87'             INDICATE LAST ENTRY          TGS
         BR    RA                                                   TGS
ALLDONE  EQU   *                                                    TGS
         L     R5,PICAHOLD             RESTORE                      TGS
         SPIE  MF=(E,(5))                   CALLERS SPIE            TGS
         LM    RE,RC,12(RD)            RESTORE CALLERS REGS         TGS
         BR    RE                           AND RETURN              TGS
PICAHOLD DS    F                                                    TGS
DUMDCB   DCB   DDNAME=DUMMY,DSORG=PS,MACRF=(GM),EXLST=EXLST         TGS
EXLST    DS    F                                                    TGS
BEGINADR DC    A(BEGIN)                                             TGS
         LTORG                                                      TGS
         END
