TGSI     CSECT                                                      TGS
********************************************************************TGS
*                  THIS RTNE WILL BE INVOKED BY UPDAT WHENEVER IT  *TGS
*              DESIRES THE NEXT SEQUENTIAL INPUT RECORD.  THE RTNE *TGS
*              PERFORMS A MERGE OF THE MEMBERS FROM SYSIN AND      *TGS
*              SYSUT4.                                             *TGS
********************************************************************TGS
         SAVES RETCTL                                               TGS
         TGSREQU                                                    TGS
         TM    INITSW,X'80'                                         TGS
         BC    1,GO                                                 TGS
         OI    INITSW,X'80'                                         TGS
         L     R1,4(RD)                                             TGS
         MVC   DDSW,16(R1)             SET DDSW                     TGS
         TM    DDSW,X'80'              IS SYSIN AVLBL               TGS
         BC    8,NOIN                  BR IF NOT                    TGS
         OPEN  (SYSIN,(INPUT))                                      TGS
         TM    SYSIN+48,X'10'          WAS IT GOOD OPEN             TGS
         BC    1,GOODOPN               BR IF YES                    TGS
         ABEND 1111,DUMP                                            TGS
GOODOPN  EQU   *                                                    TGS
         BAL   RB,GETIN                GO GET INPUT RECORD          TGS
         LTR   R1,R1                   IS IT IMMEDIATE EOF          TGS
         BC    8,NOIN                  BR IF YES                    TGS
         ST    R0,INRECL                                            TGS
         MVC   INNAME,63(R1)                                        TGS
NOIN     EQU   *                                                    TGS
         TM    DDSW,X'40'              IS SYSUT4 AVLBL              TGS
         BC    8,EOFCK                 BR IF NOT                    TGS
         LOAD  EP=UT3GET                                            TGS
         ST    R0,UT4ADDR                                           TGS
         LR    RF,R0                                                TGS
         LA    R1,=CL8'SYSUT4'                                      TGS
         BALR  RE,RF                   GO GET SYSUT4 RECORD         TGS
         LTR   R1,R1                   IS IT IMMEDIATE EOF          TGS
         BC    8,EOFCK                 BR IF YES                    TGS
         L     RF,UT4ADDR                                           TGS
         BALR  RE,RF                                                TGS
         MVC   UT4REC(80),0(R1)                                     TGS
         MVC   UT4REC+80(8),=CL8' '                                 TGS
         LA    R0,82                                                TGS
         ST    R0,UT4RECL                                           TGS
         MVI   0(R1),C'.'                                           TGS
         LA    RF,TWOSW4                                            TGS
         BAL   RB,NEWMEM                                            TGS
         MVC   UT4NAME,63(R1)                                       TGS
EOFCK    EQU   *                                                    TGS
         CLC   INRECA,UT4RECA                                       TGS
         BC    7,CTLCOMP               BR IF EITHER FILE AVLBL      TGS
         SR    R1,R1                                                TGS
         B     RETCTL                                               TGS
CTLCOMP  EQU   *                                                    TGS
         CLC   INNAME,UT4NAME                                       TGS
         BC    2,USEUT4                BR IF SYSUT4 SHOULD BE USED  TGS
         OI    FLIPSW,X'80'            IND SYSIN  SHOULD BE USED    TGS
         LM    R0,R1,INRECL                                         TGS
         B     RETCTL                                               TGS
USEUT4   EQU   *                                                    TGS
         NI    FLIPSW,X'7F'            IND SYSUT4 SHOULD BE USED    TGS
         LM    R0,R1,UT4RECL                                        TGS
         B     RETCTL                                               TGS
GO       EQU   *                                                    TGS
         TM    FLIPSW,X'80'            IS SYSIN TO BE USED          TGS
         BC    1,GETINA                BR IF YES                    TGS
GOA      L     RF,UT4ADDR                                           TGS
         BALR  RE,RF                   GO GET UT4 RECORD            TGS
         LTR   R1,R1                   IS IT EOF ON SYSUT4          TGS
         BC    8,UT4EOF                BR IF YES                    TGS
         MVC   UT4REC+80(8),=CL8' '                                 TGS
         MVC   UT4REC(80),0(R1)                                     TGS
         LA    R0,82                   R0 = DEFAULT LEN             TGS
         LA    R1,UT4REC                                            TGS
         CLC   0(15,R1),=CL15'./       ADD'                         TGS
         BC    7,PSLSHCK                                            TGS
         NI    TWOSW4,X'7F'            TURN OFF ENDUPSW             TGS
         B     GOA                                                  TGS
PSLSHCK  EQU   *                                                    TGS
         CLC   0(8,R1),=CL8'*/'        IS IT CTL STMT
         BC    7,CKTWO4                BR IF NOT                    TGS
         TM    TWOSW4,X'80'            IS ENDUPSW ON                TGS
         BC    8,NEWMEM4               BR IF NOT                    TGS
         CLC   0(15,R1),=CL15'*/       ENDTG'                       TGS
         BC    7,CKTWO4                BR IF NOT ENDTG              TGS
         NI    TWOSW4,X'7F'            TURN OFF ENDUPSW             TGS
         B     GOA                                                  TGS
CKTWO4   EQU   *                                                    TGS
         TM    TWOSW4,X'40'                                         TGS
         CLC   0(15,R1),=CL15'./       DELET'                       TGS
         BC    8,RETCTL                                             TGS
         CLC   0(15,R1),=CL15'./       REP'
         BC    8,RETCTL                                             TGS
         CLC   0(15,R1),=CL15'./       ENDTG'
         BC    8,RETCTL
         MVC   UT4REC+80(8),UT4REC+72                               TGS
         TM    TWOSW4,X'20'                                         TGS
         BC    8,NOGET24                                            TGS
         L     RF,UT4ADDR                                           TGS
         BALR  RE,RF                                                TGS
         LTR   R1,R1                                                TGS
         BC    8,UT4EOF                                             TGS
         MVC   UT4REC+72(8),0(R1)                                   TGS
NOGET24  EQU   *                                                    TGS
         LA    R1,INREC                                             TGS
         LA    R0,90                                                TGS
         STM   R0,R1,UT4RECL                                        TGS
         B     RETCTL                                               TGS
NEWMEM4  EQU   *                                                    TGS
         MVI   0(R1),C'.'                                           TGS
         CLC   0(15,R1),=CL15'./       DELET'                       TGS
         BC    8,RETCTL                                             TGS
         CLC   0(15,R1),=CL15'./       REP'
         BC    8,RETCTL
         LA    RF,TWOSW4                                            TGS
         BAL   RB,NEWMEM                                            TGS
         MVC   UT4NAME,63(R1)          RESET UT4NAME                TGS
         LA    R0,82                                                TGS
         ST    R0,UT4RECL                                           TGS
         B     CTLCOMP                                              TGS
UT4EOF   EQU   *                                                    TGS
         MVC   UT4NAME,=8X'FF'                                      TGS
         ST    R1,UT4RECA                                           TGS
         B     CTLCOMP                                              TGS
GETINA   EQU   *                                                    TGS
         BAL   RB,GETIN                GET SYSIN RECORD             TGS
         ST    R0,INRECL                                            TGS
         LTR   R1,R1                   IS IT EOF ON SYSIN           TGS
         BC    8,INEOF                                              TGS
         CLC   0(8,R1),=CL8'./'
         BC    7,RETCTL                                             TGS
         CLC   0(15,R1),=CL15'./       DELET'                       TGS
         BC    8,RETCTL                                             TGS
         CLC   0(15,R1),=CL15'./       REP'
         BC    8,RETCTL
         MVC   INNAME,63(R1)           RESET INNAME                 TGS
         B     CTLCOMP                                              TGS
INEOF    EQU   *                                                    TGS
         MVC   INNAME,=8X'FF'                                       TGS
         B     CTLCOMP                                              TGS
GETIN    EQU   *                                                    TGS
         GET   SYSIN                                                TGS
         MVC   INREC+80(8),=CL8' '                                  TGS
         MVC   INREC(80),0(R1)                                      TGS
         LA    R1,INREC                                             TGS
         LA    R0,82                   SET DEFAULT LEN              TGS
         CLC   0(8,R1),=CL8'./'
         BC    7,CKTWOI                BR IF NOT                    TGS
         CLC   0(15,R1),=CL15'./       DELET'                       TGS
         BCR   8,RB                                                 TGS
         CLC   0(15,R1),=CL15'./       REP'
         BCR   8,RB
         TM    TWOSWI,X'80'            IS ENDUP EXPECTED            TGS
         LA    RF,TWOSWI                                            TGS
         BC    8,NEWMEM                BR IF NOT                    TGS
         MVI   0(R1),C'*'                                           TGS
         CLC   0(15,R1),=CL15'*/       ENDTG'                       TGS
         BC    7,CKTWOI                                             TGS
         NI    TWOSWI,X'7F'            TURN OFF ENDUPSW             TGS
         B     GETIN                                                TGS
CKTWOI   EQU   *                                                    TGS
         TM    TWOSWI,X'40'                                         TGS
         BCR   8,RB                                                 TGS
         MVC   INREC+80(8),INREC+72                                 TGS
         TM    TWOSWI,X'20'                                         TGS
         BC    8,NOGET21                                            TGS
         GET   SYSIN                                                TGS
         MVC   INREC+72(8),0(R1)                                    TGS
NOGET21  EQU   *                                                    TGS
         LA    R1,INREC                                             TGS
         LA    R0,90                                                TGS
         BR    RB                                                   TGS
* NOT THAT AN ENDUP CARD DOES NOT SIGNIFY THE END OF MEMBER DATA ON TGS
* THE INPUT STREAM, BUT MERELY INDICATES THAT NO MORE STMTS BEGINNINTGS
* WITH ./ WILL APPEAR WITHIN MEMBER                                 TGS
NEWMEM   EQU   *                                                    TGS
         NI    0(RF),X'00'                                          TGS
         LA    R2,9                    R2 = 9                       TGS
         LA    R3,15(R1)               R3 = ADDR MEMB NAME          TGS
NAMLENLP EQU   *                                                    TGS
         CLI   0(R3),C','              IS IT COMMA                  TGS
         BC    8,GOTNLEN               BR IF YES                    TGS
         LA    R3,1(R3)                                             TGS
         BCT   R2,NAMLENLP                                          TGS
BADCTL   MVC   63(8,R1),15(R1)         BAD NAME                     TGS
         BR    RB                                                   TGS
GOTNLEN  EQU   *                                                    TGS
         LA    R4,8                                                 TGS
         SR    R4,2                                                 TGS
         BC    4,BADCTL                                             TGS
*              R4 NOW EQUALS NAME LENGTH LESS 1 AND R3 = ADDR OF    TGS
*              COMMA IMMEDIATELY FOLLOWING NAME                     TGS
         MVC   63(8,R1),=CL8' '                                     TGS
         EX    R4,MVC                                               TGS
         CLI   4(R3),C'1'              SHOULD ENDUP BE EXPECTED     TGS
         BC    7,CKLONG                BR IF NOT                    TGS
         OI    0(RF),X'80'                                          TGS
CKLONG   EQU   *                                                    TGS
         CLI   44(R1),C'L'             ARE THESE TO BE LONG RECORDS TGS
         BCR   7,RB                    BR IF NOT                    TGS
         OI    0(RF),X'40'                                          TGS
         CLC   9(6,R1),=CL6'CHNGE'                                  TGS
         BCR   7,RB                                                 TGS
         OI    0(RF),X'20'                                          TGS
         BR    RB                                                   TGS
         BR    RB                                                   TGS
SYSEND   EQU   *                                                    TGS
         CLOSE (SYSIN)                                              TGS
         SR    R1,R1                                                TGS
         ST    R1,INRECA                                            TGS
         BR    RB                                                   TGS
MVC      MVC   63(0,R1),15(R1)                                      TGS
INITSW   DC    X'00'                                                TGS
DDSW     DC    X'00'         BIT 0 = 1 IF SYSIN AVLBL               TGS
*                                1 = 1 IF SYSUT4 AVLBL              TGS
SYSIN    DCB   DDNAME=SYSIN,MACRF=(GL),DSORG=PS,EODAD=SYSEND        TGS
FLIPSW   DC    X'00'         BIT 0 = 1 IF SYSIN CURRENTLY IN USE    TGS
*                                  = 0 IF SYSUT3 CURRENTLY IN USE   TGS
INNAME   DS    0CL8                                                 TGS
         DC    8X'FF'                                               TGS
UT4NAME  DS    0CL8                                                 TGS
         DC    8X'FF'                                               TGS
INRECL   DC    F'0'                                                 TGS
INRECA   DC    A(INREC)                                             TGS
UT4RECL  DC    F'0'                                                 TGS
UT4RECA  DC    A(UT4REC)                                            TGS
INADDR   DC    F'0'                                                 TGS
UT4ADDR  DC    F'0'                                                 TGS
TWOSWI   DC    X'00'    BIT 0 = 1 IF ENDUP IS EXPECTED              TGS
TWOSW4   DC    X'00'        1 = 1 IF LONG RECORD ARE TO BE USED     TGS
*                           2 = 1 IF CHNGE FOR LONG RECORDS         TGS
INREC    DS    CL88                                                 TGS
UT4REC   DS    CL88                                                 TGS
         LTORG                                                      TGS
         END
