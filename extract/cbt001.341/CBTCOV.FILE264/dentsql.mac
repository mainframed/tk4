 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 DENTSQL: PROC(RHPTR,SUBENT,RHHDRPT,CPPLCBUF,CPPLUPT,CPPLPSCB,CPPLECT)
   OPTIONS(REENTRANT);
   DCL (CPPLCBUF,CPPLUPT,CPPLPSCB,CPPLECT) PTR;
   % DCL @PROCNAME CHAR;
   % @PROCNAME='';
   % INCLUDE DENTSUB;
   DCL PLIXOPT CHAR(80) VAR INIT('ISA(1500K) ') STATIC EXT;
   DCL TSO ENTRY RETURNS(BIT(1));
   DCL (STACK,DENTA,DENTIN) ENTRY;
   DCL SQLIN FILE RECORD INPUT ENV(FB RECSIZE(80));
   DCL SYSINSW BIT(1) INIT('1'B);
   DCL SYSOPSW BIT(1) INIT('0'B);
   DCL DREGSUB ENTRY RETURNS(FIXED BIN(15,0));
   DCL SETCAPS ENTRY;
   DCL SASPLO ENTRY; /* TELL IT IS OPTIMIZER & OPEN SYSPRINT */
   DCL IOECB FIXED BIN(31,0) INIT(0);
   DCL IKJRET FIXED BIN(31,0);
   FETCH SASPLO;
   CALL SASPLO;
   ON UNDEFINEDFILE(SQLIN) SYSINSW  = '0'B;
   ON ENDFILE(SQLIN)       SYSINSW  = '0'B;
   IF TSO
     THEN CALL STACK(CPPLUPT,CPPLECT,IOECB,IKJRET,'TERM');
     ELSE DO;
       SUBENT(8)  = BDOPRM; /* PROMPT FOR INPUT - BATCH */
       SUBENT(29) = BTERMWRT; /* TERM OUTPUT - BATCH */
       SUBENT(30) = BDOPRMLO; /* TERM INP - LO - BA*/
       SUBENT(41) = BDOSTK; /* TSO STACK INTERFACE */
       END;
   IF DREGSUB < 3000
     THEN DO;
       CALL SUBENT(29)('REGION SIZE MUST BE AT LEAST 3000K');
       RETURN;
       END;
   CALL DENTIN(RHPTR,SUBENT,RHHDRPT,CPPLCBUF,CPPLUPT,CPPLPSCB,CPPLECT);
   CALL DENTA(SUBENT,RHHDRPT,CPPLECT);
   CALL STACK(CPPLUPT,CPPLECT,IOECB,IKJRET,'TOP');
 BDOPRM: PROC(PRMPT,INP);
  /*********************************************************************
  * #DOPRM, SUBENT(8), SUBENT(52) FOR BATCH                            *
  *********************************************************************/
   DCL PRMPT CHAR(80) VAR;
   DCL INP CHAR(240) VAR;
   IF ^SYSOPSW
     THEN DO;
       OPEN FILE(SQLIN) RECORD INPUT;
       SYSOPSW='1'B;
       END;
   PUT SKIP EDIT(PRMPT)(A);
   IF SYSINSW
     THEN READ FILE(SQLIN) INTO(INP);
   IF SYSINSW
     THEN IF LENGTH(INP)>72
       THEN INP=SUBSTR(INP,1,72);
       ELSE;
     ELSE INP='ALL END NOSAVE';
   CALL SETCAPS(INP);
   END BDOPRM;
 BDOPRMLO: PROC(PRMPT,INP);
  /*********************************************************************
  * #PROMPTL, SUBENT(30), SUBENT(54) FOR BATCH                         *
  *********************************************************************/
   DCL PRMPT CHAR(80) VAR;
   DCL INP CHAR(240) VAR;
   IF ^SYSOPSW
     THEN DO;
       OPEN FILE(SQLIN) RECORD INPUT;
       SYSOPSW='1'B;
       END;
   PUT SKIP EDIT(PRMPT)(A);
   IF SYSINSW
     THEN READ FILE(SQLIN) INTO(INP);
   IF SYSINSW
     THEN IF LENGTH(INP)>72
       THEN INP=SUBSTR(INP,1,72);
       ELSE;
     ELSE INP='ALL END NOSAVE';
   END BDOPRMLO;
 BDOSTK: PROC(CMD);
  /*********************************************************************
  * #STACK, SUBENT(41), SUBENT(55) FOR BATCH.                          *
  *********************************************************************/
   DCL CMD CHAR(*) VAR;
   PUT SKIP LIST('ERROR - #STACK IS NOT SUPPORTED IN BATCH MODE');
   SIGNAL ERROR;
   END BDOSTK;
 BTERMWRT: PROC(TMSG);
  /*********************************************************************
  * #TERM, SUBENT(29), SUBENT(53) FOR BATCH EXECUTION                  *
  *********************************************************************/
   DCL TMSG CHAR(*) VAR;
   PUT SKIP EDIT(TMSG)(A);
   END BTERMWRT;
   END DENTSQL;
