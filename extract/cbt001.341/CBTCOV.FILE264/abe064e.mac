 /* COPYRIGHT 1983 BY THOMAS GLEN SMITH */
 /* ABE064E - PRINT GRAPHICS BITMAP TO CITOH PRINTER */
 ABE064E: PROC(RHPLOT79,RH_SEQ_2,#PIXGET);
   DFT RANGE(*) FIXED BIN VALUE(FIXED BIN(31,0));
   DCL RHPLOT79 PTR;
   DCL 1 DDI01 BASED(RHPLOT79),
   %INCLUDE ABE063I;
   %INCLUDE @HEXCHAR;
   DCL RH_SEQ_2 FILE;
   DCL #PIXGET ENTRY(FIXED BIN(31,0),FIXED BIN(31,0),PTR)
     RETURNS(CHAR(1));

   DCL TROUT ENTRY(CHAR(1)) RETURNS(CHAR(2));

   DCL STRIPLEN FIXED BIN STATIC INIT(124); /* MAX PRINTLINE SIZE */
   DCL PRINTSTRIP FIXED BIN STATIC INIT(792); /* MAX STRIP TO PRINT */
   DCL RPTLINE CHAR(254) VAR;

   DO I=1 REPEAT I+PRINTSTRIP WHILE(I<MAXBTX);
     RPTLINE='1';
     WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
     RPTLINE=' ^PY^-';
     WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
     /* IF PLYDPI>72 THEN CALL HIGH_DENSITY('START'); */
     RPTLINE=' ^F^-';
     WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
     CALL DO_STRIP(I);
     /* IF PLYDPI>72 THEN CALL HIGH_DENSITY('STOP'); */
     RPTLINE=' ^PN^-';
     WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
     END;

 DO_STRIP: PROC(XST);
   DCL XST FIXED BIN; /* STARTING X INDEX FOR THIS STRIP */
   DCL (H,I,J,K,L,M,N,P,R) FIXED BIN;
   DCL CHARSTR CHAR(8);
   DCL BITSTR BIT(8),
       BITDEF CHAR(1) BASED(ADDR(BITSTR));
   DCL CH CHAR(1);

   RPTLINE=' ^Q';
   DO J=MAXBTY TO 1 BY -1; /* J=Y INDEX TO PIXEL */
     DO P=MIN(MAXBTX,XST+PRINTSTRIP-1) TO (XST+6) BY -1;
       CH=#PIXGET(P,J,RHPLOT79);
       IF ^(CH=' ' × CH='0' × CH=LOW(1))
         THEN LEAVE;
       END;
     /* P=X INDEX OF RIGHTMOST NONBLANK PIXEL TO PRINT */
     DO H=XST-1 REPEAT H+STRIPLEN*3 WHILE(H<P);
       /* H=(X INDEX TO FIRST PIXEL TO PRINT IN THIS STRIP)-1 */
       DO I=0 TO STRIPLEN/2-1;
         L=H+I*6; /* L=(X INDEX TO NEXT PIXEL TO PRINT)-1 */
         R=P-L; /* R=#PIXELS LEFT TO PRINT IN THIS ROW */
         IF R<1
           THEN LEAVE;
         CHARSTR='00000000';
         DO K=1 TO MIN(6,R);
           CH=#PIXGET(L+K,J,RHPLOT79);
           IF ^(CH=' ' × CH='0'× CH=LOW(1))
             THEN CH='1';
             ELSE CH='0';
           SUBSTR(CHARSTR,2+K,1)=CH;
           END;
         SUBSTR(CHARSTR,1,2)='11';
         BITSTR=CHARSTR;
         RPTLINE=RPTLINE××TROUT(BITDEF);
         END;
       RPTLINE=RPTLINE××'^X';
       WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
       RPTLINE=' ^A';
       END;
     RPTLINE=RPTLINE××'^*';
     WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
     RPTLINE=' ^Q';
     END;
   END DO_STRIP;

 HIGH_DENSITY: PROC(PARM);
   DCL PARM CHAR(*);

   IF PARM='START'
     THEN DO;
       RPTLINE=' ^;6^-';
       WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
       END;
     ELSE DO;
       RPTLINE=' ^;7^-';
       WRITE FILE(RH_SEQ_2) FROM(RPTLINE);
       END;
   END HIGH_DENSITY;
   END ABE064E;
