*$.HE GENERALIZED SUBROUTINES - SYSLINK
*$.HE PLI INTERFACE TO LINK MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$5/22/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSLINK
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE TO
*$THE LINK MACRO.  THIS ROUTINE CAN BE USED TO CAUSE CONTROL TO BE
*$PASSED TO A SPECIFIED ENTRY POINT; THE ENTRY POINT NAME MUST BE
*$A MEMBER NAME OR AN ALIAS IN A DIRECTORY OF A PARTITIONED DATA SET.
*$THE LOAD MODULE CONTAINING THE PROGRAM IS BROUGHT IN TO MAIN
*$STORAGE IF A USEABLE COPY IS NOT AVAILABLE.
*$.SP 1
*$CALL SYSLINK(PGMNAME,RETCODE,OTHERPARMS);
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*$OTHERPARMS
*$.IN 5
*$THESE ARE ANY PARAMETERS YOU WISH TO PASS TO THE LINKED-TO PROGRAM.
*$.IN 0
*$PGMNAME CHAR(*)
*$.IN 5
*$FROM ONE TO EIGHT CHARACTERS SPECIFYING THE ENTRY POINT NAME TO BE
*$GIVEN CONTROL.
*$.IN 0
*$RETCODE FIXED BIN(31,0)
*$.IN 5
*$WILL CONTAIN THE RETURN CODE (IN REGISTER 15) FROM THE INVOKED
*$PROGRAM.
*$.IN 0
*$.SP 1
*$.CE 1
*$EXAMPLE #1
*$CALLER:
*$.IN 5
*$.NF
*$TEST:  PROC OPTIONS(MAIN);
*$DCL RET FIXED BIN(31,0);
*$DCL 1 STR,
*$2 LEN FIXED BIN(15,0) INIT(18),
*$2 CHA CHAR(18) INIT('/HOW NOW BROWN COW');
*$CALL SYSLINK('TEST2',RET,LEN);
*$PUT DATA(RET);
*$END;
*$.IN 0
*$CALLED MODULE:
*$.IN 5
*$TEST2:  PROC(PARM) OPTIONS(MAIN);
*$DCL PARM CHAR(100) VARYING;
*$PUT DATA(PARM);
*$END;
*$.IN 0
*$.SP 1
*$.CE 1
*$EXAMPLE #2
*$CALLER:
*$.IN 5
*$TEST:  PROC OPTIONS(MAIN);
*$DCL RET FIXED BIN(31,0);
*$DCL (A,B,C) FIXED BIN(15,0);
*$A = 1;
*$B = 2;
*$C = 3;
*$DCL STR CHAR(100) VARYING INIT('HOW NOW BROWN COW');
*$CALL SYSLINK('TEST2',RET,STR,A,B,C);
*$PUT DATA(RET);
*$END;
*$.IN 0
*$CALLED PROGRAM:
*$.IN 5
*$TEST2:  PROC(STR,A,B,C) OPTIONS(MAIN);
*$DCL STR CHAR(100) VARYING;
*$DCL (A,B,C) FIXED BIN(15,0);
*$PUT DATA(STR,A,B,C);
*$END;
*$.IN 0
*$.FI L
*$NOTE THAT IN EXAMPLE #2, ENTRY PLICALLA MUST BE SPECIFIED WHEN
*$LINK-EDITING THE CALLED PROGRAM (TEST2).
SYSLINK  PLIENTRY ,DSA=200
         L     2,0(1)                  R2 = A(SDV)
         LH    3,4(2)                  R3 = LEN NAME
         BCTR  3,0
         LA    4,7
         CR    3,4
         BC    12,OK
         LR    3,4
OK       EQU   *
         L     2,0(2)
         MVC   NAME,=CL8' '
         EX    3,MVC
         L     2,4(1)
         LA    1,8(1)
         LA    3,NAME
         MVC   LINKEX,LINKCON
         LA    4,LINKEX
         LINK  EPLOC=(3),SF=(E,(4))
         ST    15,0(2)
         PLIEXIT
MVC      MVC   NAME(0),0(2)
         DS    0D
LINKCON  LINK  SF=L
LINKLEN  EQU   *-LINKCON
         LTORG
DSA      DSECT
NAME     DS    CL8
         DS    0D
LINKEX   DS    CL(LINKLEN)
         END
