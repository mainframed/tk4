         GBLC  &COMP
         AIF   ('&SYSPARM' EQ 'TPUT').TPUTCOM
         AIF   ('&SYSPARM' EQ 'TPG').TPGCOM
SYSTGET  PLIENTRY DEBUG,DSA=144,COMPILE=O
***************************************************************
*
*$.HE GENERALIZED SUBROUTINES - SYSTGET
*$.HE INTERFACE TO TSO TGET MACRO
*$.PL 55
*$.PN 0
*$.PA
*$.SS
*$.NF
*$6/19/74
*$AUTHOR:  T. SMITH
*$SOURCE MODULE NAME:  SYSTPUT
*$DOCUMENTATION ID:  *$
*$.FI L
*$.SP 5
*$THIS ROUTINE SUPPLIES THE PLI PROGRAMMER WITH AN INTERFACE
*$TO THE TSO TGET MACRO INSTRUCTION.  THE TGET MACRO CAN
*$BE USED TO READ A LINE OF INPUT FROM THE TSO TERMINAL.
*$A LINE OF INPUT IS DEFINED AS ALL THE DATA BETWEEN THE BEGINNING
*$OF THE INPUT LINE AND A LINE-END DELIMITER.  A LINE-END
*$DELIMITER IS ANY CHARACTER OR COMBINATION OF CHARACTERS WHICH
*$CAUSES THE CARRIER TO RETURN TO THE LEFT-HAND MARGIN ON A NEW LINE,
*$OR WHICH TERMINATES TRANSMISSION FROM THE TERMINAL.  YOU CAN
*$USE THE TGET INTERFACE IN ANY TSO ROUTINES, AND IN ANY
*$APPLICATION PROGRAMS TO BE RUN UNDER TSO.  NOTE HOWEVER, THAT TGET
*$DOES NOT PROVIDE ACCESS TO IN-STORAGE LISTS, NOR DOES IT PERFORM
*$ANY TYPE OF LOGICAL LINE PROCESSING ON THE RETURNE*$D LINE.  IF YOU
*$REQUIRE THESE FEATURES, USE THE GETLINE INTERFACE.
*$.SP 1
*$.NF
*
***************************************************************$
*
*$CALL SYSTGET(BUFSTR,RETCODE,
*$       'EDIT' OR 'ASIS',
*$       'WAIT' OR 'NOWAIT');
*
*$.SP 1
*$.FI L
*$NOTE - IN THE ABOVE FORMAT DESCRIPTION, THOSE CHARACTER LITERALS
*$SPECIFIED ON THE SAME LINE ARE MUTUALLY EXCLUSIVE, AND ONLY ONE
*$CAN BE SPECIFIED.  FOR A GIVEN GROUP, THE FIRST ONE SPECIFIED ON
*$THE LINE IS THE DEFAULT.
*
**************************************************************$
*
*$.SP 1
*$.CE 1
*$DEFINITION OF TERMS
*$.SP 1
*
**************************************************************$
*
*$'ASIS'
*$.IN 5
*$SPECIFIES THAT MINIMAL EDITING IS TO BE DONE BY THE TGET SVC.
*$THE FOLLOWING EDITING FUNCTIONS WILL BE PERFORMED BY TGET:
*$.IN 10
*$A. TRANSMISSION CONTROL CHARACTERS ARE REMOVED.
*$.IN 10
*$B. THE LINE OF INPUT IS TRANSLATED FROM TERMINAL CODE TO
*$EBCDIC.
*$.IN 10
*$D. LINE DELETION AND CHARACTER DELETION EDITING
*$ARE PERFORMED.
*$.IN 0
*$.SP 1
*$BUFSTR
*$.IN 5
*$CHAR(X) VARYING - (MUST BE VARYING CHARACTER STRING).  THIS
*$IS THE BUFFER TO RECEIVE THE INPUT LINE.
*$.IN 0
*$.SP 1
*$'EDIT'
*$.IN 5
*$SPECIFIES THAT IN ADDITION TO MINIMAL EDITING (SEE ASIS)
*$THE INPUT BUFFER IS TO BE FILLED OUT WITH TRAILING BLANKS.
*$.IN 0
*$.SP 1
*$'NOWAIT'
*$.IN 5
*$SPECIFIES THAT CONTRO IS TO BE RETURNED TO THE ROUTINE THAT
*$ISSUED THE GETLINE MACRO WHETHER OR NOT A LINE OF INPUT IS
*$AVAILABLE.  IF A LINE OF INPUT IS NOT AVAILABLE,
*$A RETURN CODE OF 12 DECIMAL IS RETURNED TO
*$THE COMMAND PROCESSOR.
*$.IN 0
*$.SP 1
*$RETCODE
*$.IN 5
*$FIXED BIN(31,0) TO CONTAIN THE RETURN CODE AS FOLLOWS:
*$.SP 1
*$00 COMPLETED SUCCESSFULLY.
*$.SP 1
*$04 NOWAIT WAS SPECIFIED AND NO INPUT WAS AVAILABLE TO BE
*$BE READ INTO YOUR INPUT BUFFER.
*$.SP 1
*$08 AN ATTENTION INTERRUPTION OCCURRED WHILE THE TGET SVC
*$ROUTINE WAS PROCESSING
*$.SP 1
*$0C YOUR INPUT BUFFER WAS NOT LARGE ENOUGH TO ACCEPT THE
*$ENTIRE LINE OF INPUT ENTERED AT THE TERMINAL.  SUBSEQUENT
*$TGET MACRO INSTRUCTIONS WILL OBTAIN THE REST OF THE
*$INPUT LINE.
*$.SP 1
*$10 INVALID PARAMETERS WERE PASSED TO THE TGET SVC.
*$.SP 1
*$14 THE TERMINAL HAS BEEN DISCONNECTED AND COULD NOT BE REACHED
*$.IN 0
*$.SP 1
*$'WAIT'
*$.IN 5
*$SPECIFIES THAT CONTROL IS TO BE RETURNED TO THE ROUTINE
*$THAT ISSUED THE GETLINE MACRO INSTRUCTION ONLY AFTER IN
*$INPUT MESSAGE HAS BEEN READ.
*
*************************************************************
         AGO   .BYTPCOM
.TPUTCOM ANOP
SYSTPUT  PLIENTRY DEBUG,DSA=128,COMPILE=O
***************************************************************#
*
*#.HE GENERALIZED SUBROUTINES - SYSTPUT
*#.HE PLI INTERFACE TO TSO TPUT MACRO
*#.PL 55
*#.PN 0
*#.PA
*#.SS
*#.NF
*#6/19/74
*#AUTHOR:  T. SMITH
*#SOURCE MODULE NAME:  SYSTPUT
*#DOCUMENTATION ID:  *#
*#.FI L
*#.SP 5
*#THIS ROUTINE PROVIDES THE PLI PROGRAMMER WITH AN INTERFACE TO
*#THE TPUT MACRO INSTRUCTION.  TPUT (SVC 93) IS USED TO TRANSMIT A
*#LINE OF OUTPUT TO THE TERMINAL.  YOU CAN USE THE TPUT MACRO
*#INSTRUCTION IN ANY TSO ROUTINES YOU WRITE, AND IN ANY
*#APPLICATION PROGRAMS TO BE RUN UNDER TSO.  NOTE HOWEVER, THAT
*#TPUT DOES NOT PROVIDE MESSAGE ID STRIPPING, TEXT INSERTION,
*#OR SECOND LEVEL MESSAGE CHAINING.  IF YOU REQUIRE THESE
*#FEATURES, USE THE PUTLINE INTERFACE.
*#.SP 1
*#.NF
*#CALL SYSTPUT(BUFSTR,RETCODE,
*#       'EDIT' OR 'ASIS' OR 'CONTROL' OR 'FULLSCR' OR 'NOEDIT',
*#       'WAIT' OR 'NOWAIT',
*#       'NOHOLD' OR 'HOLD',
*#       'NOBREAK' OR 'BREAKIN',
*#       'HIGHP' OR 'LOWP',
*#       'TJID',TJID OR 'USERID',USERID
*#       );
*
*#.SP 1
*#.FI L
*#NOTE - IN THE ABOVE FORMAT DESCRIPTION, THOSE CHARACTER LITERALS
*#SPECIFIED ON THE SAME LINE ARE MUTUALLY EXCLUSIVE, AND ONLY ONE
*#CAN BE SPECIFIED.  FOR A GIVEN GROUP, THE FIRST ONE SPECIFIED ON
*#THE LINE IS THE DEFAULT.
*
**************************************************************#
*
*#.SP 1
*#.CE 1
*#DEFINITION OF TERMS
*#.SP 1
*
**************************************************************#
*
*#'ASIS'
*#.IN 5
*#SPECIFIES THAT MINIMAL EDITING IS TO BE PERFORMED BY TPUT
*#.IN 0
*#'BREAKIN'
*#.IN 5
*#SPECIFIES THAT OUTPUT HAS PRECEDENCE OVER INPUT.  IF THE USER
*#THE TERMINAL IS TRANSMITTING, HE IS INTERRUPTED, AND THE
*#OUTPUT LINE IS SENT.  ANY DATA THAT WAS RECEIVED BEFORE
*#THE INTERRUPTION IS KEPT AND DISPLAYED AT THE TERMINAL
*#FOLLOWING THE OUTPUT LINE.
*#.IN 0
*#BUFSTR
*#.IN 5
*#CHARACTER STRING OR STRUCTURE CONTAINING THE OUTPUT BUFFER.
*#MUST BE DECLARED VARYING.
*#.IN 0
*#'CONTROL'
*#.IN 5
*#SPECIFIES THAT THE OUTPUT LINE IS COMPOSED OF TERMINAL CONTROL
*#CHARACTERS AND WILL NOT PRINT OR MOVE THE CARRIER
*#ON THE TERMINAL. THIS OPTIONS SHOULD BE USED FOR
*#TRANSMISSION OF CHARACTERS SUCH AS 'BYPASS',
*#'RESTORE', OR 'BELL RING'.
*#.IN 0
*#'EDIT'
*#.IN 5
*#SPECIFIES EDITING OVER AND ABOVE THE MINIMAL (ASIS).
*#.IN 0
*#'HIGHP'
*#.IN 5
*#SPECIFIES THAT THIS MESSAGE MUST BE SENT TO THE TERMINAL,
*#EVEN THOUGH THE DESTINATION TERMINAL HAS DISALLOWED MESSAGES
*#FROM OTHER TERMINALS.  THIS OPERAND COUNTERS THE EFFECT OF
*#THE INTERTERMINAL COMMUNICATION BIT WHEN SET IN THE
*#TERMINAL STATUS BLOCK (TSB).  (THE HIGHP OPERAND IS USED
*#BY THE OPERATOR SEND SUBCOMMAND THE SEND OPERATOR COMMAND.)
*#THE OPERAND IS RECOGNIZED ONLY IF THE ISSUING TASK IS OPERATIN
*#UNDER ZERO PROTECT KEY.  THE TJID KEYWORD MUST ALSO BE
*#SPECIFIED.  HIGHP IS THE DEFAULT IF NEITHER HIGHP OR LOWP
*#IS SPECIFIED, AND THE IDDUING PROGRAM IS OPERATING UNDER
*#ZERO PROTECTION KEY.
*#.IN 0
*#'HOLD'
*#.IN 5
*#SPECIFIES THAT THE MODULE THAT ISSUED THE TPUT
*#MACRO IS NOT TO RESUME PROCESSING UNTIL THE OUTPUT
*#LINE HAS BEEN PUT OUT TO THE TERMINAL OR DELETED.
*#.IN 0
*#'LOWP'
*#.IN 5
*#SPECIFIES THAT THE TPUT WITH TJID MODULE SHOULD TEST THE
*#INTERTERMINAL COMMUNICATION BIT IN THE TERMINAL STATUS BLOCK.
*#IF THE USER OF THE DESTINATION TERMINAL ALLOWS INTERTYERMINAL
*#MESSAGES, THIS MESSAGE WILL BE SENT.  2F SUCH MESSAGES ARE
*#NOT ALLOWED THE MESSAGE WILL NOT BE SENT, AND THE RETURN CODE
*#OF '0C' WILL INDICATE NO MESSAGE WAS SENT.  THE LOWP OPERAND
*#IS RECOGNIZED ONLY WHEN TJID IS SPECIFIED.  THE ISSUER MUST
*#BE OPERATING UNDER ZERO PROTECTION KEY.
*#.IN 0
*#'NOBREAK'
*#.IN 5
*#SPECIFIES THAT IF THE TERMINAL USER HAS STARTED
*#TO ENTER INPUT, HE IS NOT TO BE INTERRUPTED.
*#THE OUTPUT MESSAGE IS PLACED ON THE OUTPUT QUEUE
*#TO BE PRINTED AFTER THE TERMINAL USER HAS
*#COMPLETED THE LINE.
*#.IN 0
*#'NOHOLD'
*#.IN 5
*#SPECIFIES THAT CONTROL IS RETURNED TO THE ROUTINE
*#THAT ISSUED THE TPUT MACRO INSTRUCTION, AND IT CAN
*#CONTINUE PROCESSING, AS SOON AS THE OUTPUT LINE HAS BEEN
*#PLACED ON THE OUTPUT QUEUE.
*#.IN 0
*#'NOWAIT'
*#.IN 5
*#SPECIFIES THAT CONTROL SHOULD BE RETURNED WHETHER
*#OR NOT A TERMINAL OUTPUT BUFFER IS AVAILABLE.  IF
*#NO BUFFER IS AVAILABLE, A RETURN CODE OF 4 IS
*#RETURNED IN RETCODE.
*#.IN 0
*#RETCODE
*#.IN 5
*#A FIXED BIN(31,0) VARIABLE TO CONTAIN THE RETURN CODE.
*#.IN 10
*#00 - TPUT COMPLETED SUCCESSFULLY.
*#.IN 10
*#04 - NOWAIT WAS SPECIFIED AND NO TERMINAL
*#OUTPUT BUFFER WAS AVAILABLE.
*#.IN 10
*#08 - AN ATTENTION INTERRUPTION OCCURRED WHILE
*#THE TPUE SVC ROUTINE WAS PROCESSING.
*#.IN 10
*#0C - A TPUT MACRO INSTRUCTION WITH A TJID
*#OPERAND WAS ISSUED BUT THE USER AT THE TERMINAL
*#INDICATED BY THE TJID REQUESTED THAT
*#TERMINAL MESSAGES NOT BE PRINTED ON HIS TERMINAL.
*#THE MESSAGE WAS NOT SENT.
*#.IN 10
*#10 - INVALID PARAMETERS WERE PASSED TO THE
*#TPUT SVC.
*#.IN 10
*#14 - THE TERMINAL HAS BEEN DISCONNECTED AND
*#COULD NOT BE REACHED.
*#.IN 10
*#18 - AN INVALID PARAMETER WAS SPECIFIED
*#.IN 0
*#'TJID'
*#.IN 5
*#INDICATES THAT THE TJID PARAMETER HAS BEEN SUPPLIED.  THE TJID
*#PARAMETER MUST BE NEXT IN THE PARAMETER LIST, AND IT MUST BE THE
*#LAST PARAMETER IN THE LIST.
*#.IN 0
*#TJID
*#.IN 5
*#FIXED BIN(15,0) - SPECIFIES THE TJID OF THE TARGET TERMINAL.
*#THIS FACILITY IS USED FOR SUPERVISOR COMMUNICATION WITH
*#THE TERMINAL, AND FOR INTER-USER CONVERSATION BETWEEN
*#TERMINALS (THE SEND COMMAND).  IF THIS OPTION IS USED,
*#NOHOLD IS THE REQUIRED OPTION AND IS DEFAULTED TO.
*#IF YOU SPECIFY TJID, YOU MUST SUPPLY A TJID NUMBER,
*#NOTE:  IF A TPUT WITHOUT TJID IS CODED IN A
*#BACKGROUND PROGRAM, THE RESULT IS A NOP.  IF HOWEVER,
*#THE TPUT SPECIFIES TJID, THE MESSAGE IS SENT TO
*#THE TARGET TERMINAL.
*#.IN 0
*#'USERID'
*#.IN 5
*#INDICATES THAT THE USERID PARAMETER HAS BEEN SUPPLIED.  THE USERID
*#PARAMETER MUST BE NEXT IN THE PARAMETER LIST, AND IT MUST BE THE
*#LAST PARAMETER IN THE LIST.
*#.IN 0
*#USERID
*#.IN 5
*#CHARACTER VARIABLE WHICH MAY BE EITHER FIXED OR VARYING CONTAINING
*#THE 8-BYTE TSO USERID OF THE USER TO RECEIVE THIS MESSAGE.
*#.IN 0
*#'WAIT'
*#.IN 5
*#SPECIFIES THAT CONTROL WILL NOT BE RETURNED UNTIL
*#THE OUTPUT LINE HAS BEEN PLACED INTO A TERMINAL
*#OUTPUT BUFFER.
*#.IN 0
*
******************************************************************#
         AGO   .BYTPCOM
.TPGCOM ANOP
SYSTPG   PLIENTRY DEBUG,DSA=128,COMPILE=O
***************************************************************#
*
*@.HE GENERALIZED SUBROUTINES - SYSTPG
*@.HE PLI INTERFACE TO TSO TPG MACRO
*@.PL 55
*@.PN 0
*@.PA
*@.SS
*@.NF
*@1/03/86
*@AUTHOR:  T. SMITH
*@SOURCE MODULE NAME:  SYSTPUT
*@DOCUMENTATION ID:  *@
*@.FI L
*@.SP 5
*@THIS ROUTINE PROVIDES THE PLI PROGRAMMER WITH AN INTERFACE TO
*@THE TPG MACRO INSTRUCTION.  TPG (SVC 93) IS USED TO TRANSMIT A
*@LINE OF OUTPUT TO THE TERMINAL IF THAT LINE OF OUTPUT WILL CAUSE
*@THE DEVICE TO RESPOND IMMEDIATELY WITH INPUT.  THE MAIN USE OF
*@TPG IS TO PERFORM THE QUERY FUNCTION FOR A USER WHO HAS INCLUDED
*@A READ PARTION STRUCTURED FIELD.  A TGET MACRO MUST BE ISSUED
*@TO RETRIEVE THE QUERY RESPONSE.
*@.SP 1
*@.NF
*@CALL SYSTPG(BUFSTR,RETCODE,
*@       'WAIT' OR 'NOWAIT',
*@       'NOHOLD' OR 'HOLD');
*
*@.SP 1
*@.FI L
*@NOTE - IN THE ABOVE FORMAT DESCRIPTION, THOSE CHARACTER LITERALS
*@SPECIFIED ON THE SAME LINE ARE MUTUALLY EXCLUSIVE, AND ONLY ONE
*@CAN BE SPECIFIED.  FOR A GIVEN GROUP, THE FIRST ONE SPECIFIED ON
*@THE LINE IS THE DEFAULT.
*
**************************************************************#
*
*@.SP 1
*@.CE 1
*@DEFINITION OF TERMS
*@.SP 1
*
**************************************************************#
*
*@.IN 0
*@BUFSTR
*@.IN 5
*@CHARACTER STRING OR STRUCTURE CONTAINING THE OUTPUT BUFFER.
*@MUST BE DECLARED VARYING.
*@'HOLD'
*@.IN 5
*@SPECIFIES THAT THE MODULE THAT ISSUED THE TPUT
*@MACRO IS NOT TO RESUME PROCESSING UNTIL THE OUTPUT
*@LINE HAS BEEN PUT OUT TO THE TERMINAL OR DELETED.
*@.IN 0
*@'NOHOLD'
*@.IN 5
*@SPECIFIES THAT CONTROL IS RETURNED TO THE ROUTINE
*@THAT ISSUED THE TPUT MACRO INSTRUCTION, AND IT CAN
*@CONTINUE PROCESSING, AS SOON AS THE OUTPUT LINE HAS BEEN
*@PLACED ON THE OUTPUT QUEUE.
*@.IN 0
*@'NOWAIT'
*@.IN 5
*@SPECIFIES THAT CONTROL SHOULD BE RETURNED WHETHER
*@OR NOT A TERMINAL OUTPUT BUFFER IS AVAILABLE.  IF
*@NO BUFFER IS AVAILABLE, A RETURN CODE OF 4 IS
*@RETURNED IN RETCODE.
*@.IN 0
*@RETCODE
*@.IN 5
*@A FIXED BIN(31,0) VARIABLE TO CONTAIN THE RETURN CODE.
*@.IN 10
*@00 - TPG COMPLETED SUCCESSFULLY.
*@.IN 10
*@04 - NOWAIT WAS SPECIFIED AND NO TERMINAL
*@OUTPUT BUFFER WAS AVAILABLE.
*@.IN 10
*@08 - AN ATTENTION INTERRUPTION OCCURRED WHILE
*@THE TPG SVC ROUTINE WAS PROCESSING.
*@.IN 10
*@10 - INVALID PARAMETERS WERE PASSED TO THE
*@TPUT SVC.
*@.IN 10
*@14 - THE TERMINAL HAS BEEN DISCONNECTED AND
*@COULD NOT BE REACHED.
*@.IN 0
*@'WAIT'
*@.IN 5
*@SPECIFIES THAT CONTROL WILL NOT BE RETURNED UNTIL
*@THE OUTPUT LINE HAS BEEN PLACED INTO A TERMINAL
*@OUTPUT BUFFER.
*@.IN 0
*
******************************************************************#
.BYTPCOM ANOP
         XC    MYDSA(MYLEN),MYDSA
         SR    RF,RF                   CLEAR A(USERID)
         SR    R2,R2                   R2 = PARM CNT * 4
LOOPA    DS    0H
         L     R3,0(R1,R2)             R3 = NXT PARM LIST PTR
         CH    R2,=H'4'                IS IT ONE OF 1ST 2 PARMS?
         AIF   ('&SYSPARM' NE 'TPUT').ALTBR
         BC    2,NOTFST                BR IF NOT
         AGO   .BYALT
.ALTBR   ANOP
         BC    2,NULLCK                BR IF NOT
.BYALT   ANOP
         LR    RB,R3                   RB=POSSIBLE A(RETCODE)
         BC    8,ELOOPA                BR IF 2D PARM = RETCODE
*                                      R3 = A(BUFSTR SDV)
         L     R9,0(R3)                R9 = A(HALFWORD LEN PREFIX)
         LA    R8,2(R9)                R8 = A(BUFFER)
         AIF   ('&SYSPARM' EQ 'TPG').DOTPGBF
         ST    R8,DOPT                 STORE A(BUFFER) IN PARM LIST
         AGO   .BYTPGBF
.DOTPGBF ANOP
         ST    R8,DBFADR
.BYTPGBF ANOP
*              NOTE - BUFSTR MUST BE DEFINED VARYING
         AIF   ('&SYSPARM' EQ 'TGET').TGET1
.TPUT1   ANOP
         MVC   DBFSZ,0(R9)             SET CURLEN IN DBFSZ
         AGO   .BYPLF
.TGET1   ANOP
         MVC   DBFSZ,4(R3)             SET MAXLEN IN DBFSZ
.BYPLF   ANOP
ELOOPA   DS    0H
         LA    R2,4(R2)                BUMP R2
         LTR   R3,R3                   WAS IT LAST PARM?
         BC    2,LOOPA                 BR IF NOT
         CH    R2,=H'8'                WERE AT LEAST 2 PARMS PASSED?
         BC    4,BADFORM               BR IF NOT
         AIF   ('&SYSPARM' NE 'TPUT').BYTST2
         TM    DOPT,X'80'              WAS 'TJID' SPECIFIED?
         BC    1,BADFORM               BR IF YES
         B     DOSVC
NOTFST   DS    0H
         TM    DOPT,X'80'              WAS 'TJID' SPECIFIED?
         BC    1,LASTCK                BR IF YES
         TM    DOPT,X'40'              WAS 'USERID' SPECIFIED?
         BC    8,NULLCK                BR IF NOT
LASTCK   DS    0H
         LTR   R3,R3                   IS IT LAST PARM?
         BC    2,NULLCK                BR IF NOT
         TM    DOPT,X'80'              WAS 'TJID' SPECIFIED?
         BC    1,ISTJID                BR IF YES
         LR    RE,R3                   RE=A(SDV)
         L     RF,0(RE)                RF=A(USERID) OR LEN
         TM    6(RE),X'80'             IS IT VARYING?
         BC    8,NOTV1                 BR IF NOT
         LH    RE,0(RF)                RE=LEN(USERID)
         LA    RF,2(RF)                RF=ADDR(USERID)
         B     DOMOV
NOTV1    DS    0H
         LH    RE,4(RE)                RE=LEN(USERID, RF=ADDR
DOMOV    DS    0H
         C     RE,=F'8'                IS LEN < 8
         BC    4,LENOK                 BR IF YES
         LA    RE,=F'8'                TRUNCATE
LENOK    DS    0H
         BCTR  RE,0                    LESS 1 FOR EX
         MVC   UID,=CL8' '             CLEAR TO BLANKS
         EX    RE,MVCUID               MOVE USERID TO BUFFER
         B     BYUID
MVCUID   MVC   UID(0),0(RF)
BYUID    DS    0H
         LA    RF,UID
         B     DOSVC
ISTJID   DS    0H
         MVC   DTJID,0(R3)             MOVE TJID TO PARM LIST
DOSVC    DS   0H
         NI    DOPT,X'7F'              TURN OFF 'TJID' FLAG
         TM    NOEDSW,X'80'            WAS NOEDIT SPECIFIED?
         BC    8,REGPARM               BR IF NOT
         ST    RF,UIDADR
         MVI   FLAG2,X'81'             INDICATE TPUT/NOEDIT
         AGO   .BYTPGSE
.BYTST2  ANOP
         AIF   ('&SYSPARM' NE 'TGET').TPGSET
         OI    DOPT,X'80'              INDICATE TGET
         AGO   .TPUT2
.TPGSET  ANOP
         MVI   FLAG2,X'83'             INDICATE TPG/NOEDIT
.BYTPGSE ANOP
         L     R0,=X'80000000'
         LA    R1,DTJID
         OR    R1,R0                   HI BIT=1 IN R1
         AIF   ('&SYSPARM' EQ 'TPG').BYLM
         B     SVC
REGPARM  DS    0H
.TPUT2   ANOP
         LM    R0,R1,DTJID             LOAD PARAMETER REGS
SVC      DS    0H
.BYLM    ANOP
         SVC   93                      ISSUE TGET/TPUT SVC
         AIF   ('&SYSPARM' NE 'TGET').BYSET
         STH   R1,0(R9)                SET NEW CURLEN
.BYSET   ANOP
SETRET   DS    0H
         ST    RF,0(RB)                SET RETCODE
EXIT     PLIEXIT
BADFORM  DS    0H
         LA    RF,24
         B     SETRET
NULLCK   DS    0H
         L     R4,0(R3)                R4 = A(OPTION NAME)
         CLC   0(6,R4),=C'NOEDIT'
         BC    7,BYNOED
         OI    NOEDSW,X'80'
BYNOED   DS    0H
         LA    R5,OPTAB
         SR    R6,R6
LOOPB    DS    0H
         CLI   0(R5),X'FF'             IS IT END-OF-TABLE?
         BC    8,BADFORM               BR IF YES
         IC    R6,1(R5)                R6 = LEN OPTION NAME - 1
         EX    R6,CLC
         BC    8,ELOOPB                BR IF OPTION FOUND
         LA    R5,3(R5,R6)             BUMP R5 TO NEXT OPTAB ENTRY
         B     LOOPB
ELOOPB   DS    0H
         OC    DOPT,0(R5)              SET OPTION FLAG
         B     ELOOPA
         DS    0H
CLC      CLC   0(0,R4),2(R5)
OPTAB    DS    0C
*                MASK FOR OR INSTRUCTION
*                      LEN OPTION NAME - 1
*                            OPTION NAME
         AIF   ('&SYSPARM' EQ 'TGET').TGESKP1
         DC    X'03',X'05',C'NOEDIT'
.TGESKP1 ANOP
         AIF   ('&SYSPARM' EQ 'TPG').TPGSKP1
         DC    X'00',X'03',C'EDIT'
         DC    X'01',X'03',C'ASIS'
.TPGSKP1 ANOP
         DC    X'00',X'03',C'WAIT'
         DC    X'10',X'05',C'NOWAIT'
         AIF   ('&SYSPARM' EQ 'TGET').SKIP
         DC    X'08',X'03',C'HOLD'
         DC    X'00',X'05',C'NOHOLD'
         DC    X'04',X'06',C'BREAKIN'
         DC    X'00',X'06',C'NOBREAK'
         DC    X'00',X'04',C'HIGHP'
         DC    X'20',X'03',C'LOWP'
         AIF   ('&SYSPARM' EQ 'TPG').SKIP
         DC    X'80',X'03',C'TJID' MUST BE TURNED OFF LATER
         DC    X'40',X'05',C'USERID'
         DC    X'03',X'06',C'FULLSCR'
         DC    X'02',X'06',C'CONTROL'
.SKIP    ANOP
         DC    X'FF' END-OF-TABLE
         LTORG
DSA      DSECT
MYDSA    DS    0F
DTJID    DS    H   TJID, ASID
DBFSZ    DS    H   BUFFER SIZE
         AIF   ('&SYSPARM' NE 'TPG').BYTPG1
DBFADR   DS    F                       A(BUFFER)
         DS    F                       RESERVED
FLAG2    DS    C                       S/B X'83' FOR TPG
.BYTPG1  ANOP
DOPT     DS    C FLAG1 OPTIONS
*              0... .... ALWAYS ZERO FOR TPUT,TPG
*              1... .... ALWAYS ONE FOR TGET
*              .1.. .... R15=A(USERID)
*              ..0. .... HIGHP
*              ..1. .... LOWP
*              ...0 .... WAIT
*              ...1 .... NOWAIT
*              .... 0... NOHOLD
*              .... 1... HOLD
*              .... .0.. NOBREAK
*              .... .1.. BREAKIN
*              .... ..00 EDIT
*              .... ..10 CONTROL
*              .... ..11 FULLSCR
*              .... ..01 ASIS
         AIF   ('&SYSPARM' NE 'TPG').BYTPG2
         DS    H RESERVED
         AGO   .BYTPG3
.BYTPG2  ANOP
DBFADR   DS    CL3
UIDADR   DS    F
FLAG2    DS    C
         DS    CL3 RESERVED
UID      DS    CL8
.BYTPG3  ANOP
NOEDSW   DS    C
MYLEN    EQU   *-MYDSA
DSALEN   EQU   *-DSA
         END
