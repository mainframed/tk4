//BDFS0000 JOB   (07,1,08500,CORP),BOB.TONAHILL,NOTIFY=BOB,MSGCLASS=A,
//      CLASS=Q
//ASM      EXEC   ASMFCL,LIB='SYS1.LPALIB(IMSROUTE)',
//       AOPTS='RENT',LOPTS='RENT'
//ASM.SYSIN   DD  *
         TITLE 'IMSROUTE - CHANGE  DESTINATION/CLASS OF IMS MESSAGE REG*
               ION DUMPS'
***********************************************************************
* IMSROUTE - CALLED BY IEFDB401 (ALLOCATION EXIT ROUTINE) WHEN A DUMP *
* FROM AN IMS MESSAGE REGION IS BEING DEALLOCATED.  IMSROUTE CAN      *
* CHANGE THE DESTINATION AND/OR THE CLASS OF THE DUMP IN ORDER TO GET *
* IT TO THE RIGHT PLACE IN A MORE TIMELY MANNER.                      *
*                                                                     *
* ON ENTRY, R1 POINTS TO A 256 BYTE WORKAREA.                         *
*                                                                     *
* RETURN 0 IN R15 IF NO CHANGE IS TO BE MADE.                         *
* IF THE DESTINATION AND/OR CLASS IS TO BE CHANGED, RETURN 4 IN R15   *
* AND IN R1 PUT A POINTER TO THE 7 BYTES FOR THE REMOTE FOLLOWED BY   *
* ONE BYTE FOR THE CLASS.  NOTE THAT BOTH MUST BE SUPPLIED IF EITHER  *
* IS TO BE CHANGED.                                                   *
*                                                                     *
***********************************************************************
         EJECT
IMSROUTE CSECT
         SAVE  (14,12),,IMSROUTE-PS00037-&SYSDATE
         LR    R12,R15
         USING IMSROUTE,R12
         LR    R11,R1              SET UP WORK AREA
         USING WORKAREA,R11
         SPACE
         L     R3,536              GET ADDRESS OF TCB FROM PSA
         L     R4,12(0,R3)         GET TIOT ADDRESS IN R4
         CLC   0(4,R4),=C'BIMS'    Q. IS THIS AN IMS JOB?
         BNE   LEAVE               ...NO, FORGET IT
         CLC   0(8,R4),=C'BIMS001T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS002T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS003T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS004T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS005T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS006T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS007T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS008T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS009T'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS9970'    IS THIS OUR JOB NAME
         BE    GOOD
         CLC   0(8,R4),=C'BIMS030T'    IS THIS OUR JOB NAME
         BE    GOOD
LEAVEX   DS    0H                  THIS IS NOT FOR US
         MVC   WTOWORK(63),WTO1    MOVE MESSAGE TO PASSED WORKAREA
         MVC   WTOWORK+51(8),0(R4) MOVE IN JOB NAME
         LA    R1,WTOWORK          POINT TO WTO
         WTO   MF=(E,(1))          WRITE OUT MESSAGE
LEAVE    DS    0H
         RETURN (14,12),RC=0
         EJECT
GOOD     DS    0H
         L     R5,36(0,R3)         R3 POINTS TO LOAD LIST ELEMENT
         LTR   R5,R5               IF POINTER IS ZERO GO AWAY
         BZ    LEAVEX
LOOP     DS    0H
         L     R4,4(0,R5)          PUT CDE ADDRESS IN R4
BACK     DS    0H
         LTR   R4,R4               IF POINTER IS ZERO DO NOT ABEND
         BZ    LEAVE
         CLC   8(8,R4),=C'DFSPRPX0'  IS THIS MOD THAT HAS PSB NAME
         BE    MINE
*
*      CHECK THE MINOR CODE.  IF IT IS ON THEN THIS CDE IS A MINOR CDE
*      AND HAS ANOTHER CDE CHAINED OFF OF IT. (CHAIN TO IT AT NEXTCDE)
*      IF THE MINOR CODE IS NOT ON, THIS IS THE LAST CDE CHAINED OFF
*      THIS LLE.  GO GET THE NEXT ENTRY IN THE LLE CHAIN.  IF ZERO-EOF
*
         TM    28(R4),X'04'        IS THIS A MINOR CDE
         BO    NEXTCDE
         L     R5,0(0,R5)
         LTR   R5,R5               IS THIS THE END OF LLE
         BNZ   LOOP
         B     NONE
NEXTCDE  DS    0H
         L     R4,20(0,R4)         PUT MAJOR CODE FROM CDE INTO R4
         B     BACK
         EJECT
***********************************************************************
*                                                                     *
* THIS IS A GUY THAT WE WANT.  ROUTE HIM OR SKIP HIM.                 *
*                                                                     *
***********************************************************************
         SPACE
MINE     DS    0H
         MVI   NEWCLASS,C'A'       SET DEFAULT CLASS
         L     R3,16(0,R4)         PUT EPA OF PRPX0 IN R3
         CLC   112(5,R3),=C'S2100' IS THIS AN OLD PLOUGH PGM
         BE    MAKEM               MAKE THEM GO TO MEMPHIS
         CLI   112(R3),C'S'        CHECK FIRST BYTE FOR UNION
         BE    CHANGEU                AT 112 IN PRPX0
         CLI   112(R3),C'R'        CHECK 1ST BYTE FOR RESEARCH
         BE    CHANGEB
         CLI   112(R3),C'I'        CHECK 1ST BYTE FOR INTERNATIONAL
         BE    CHANGEK
         CLC   112(5,R3),=C'FDB67' CHECK 1ST FIVE BYTES FOR FINANCE
         BE    CHANGEH
         CLC   112(4,R3),=C'BHIS'  CHECK 1ST THREE BYTES FOR FINANCE
         BE    CHANGEH
         CLI   112(R3),C'C'        CHECK 1ST BYTE FOR CORP
         BE    CHANGEH             ALL CORP GOES TO N.J.
*        IF NO HIT THEN SEND TO RMT44
         EJECT
MAKEM    EQU   *
         MVI   NEWCLASS,C'D'       MBL/PLO WANT CLASS D FOR DUMPS
         LA    R6,RMT44            MAKE IT PLOUGH/MBLN
         B     MOVENAME
*
CHANGEU  EQU   *
         LA    R6,RMT07            MAKE IT UNION
         B     MOVENAME
*
CHANGEB  EQU   *
         LA    R6,RMT02            MAKE IT BLOOMFIELD
         B     MOVENAME
*
CHANGEK  EQU   *
         LA    R6,RMT05            MAKE IT KENILWORTH
         B     MOVENAME
*
CHANGEH  EQU   *
         LA    R6,RMT04            MAKE IT CHATHAM IN NJ
         B     MOVENAME
         EJECT
MOVENAME DS    0H
*        R6 POINTS TO NEW DEST, NEWCLASS IS ALREADY SET
         MVC   NEWDEST,0(R6)       MOVE IN NEW DESTINATION
         MVC   WTOWORK(64),WTO2    WORK MESSAGE TO WTO WORKAREA
         MVC   WTOWORK+38(5),0(R6)  MOVE DESTINATION TO  WORKAREA
         MVC   WTOWORK+52(8),112(R3)  PUT PSB NAME IN MESSAGE
         LA    R1,WTOWORK          POINT TO WTO
         WTO   MF=(E,(1))          DO LIST FORM OF WTO
         ST    R11,24(R13)         POINT TO PARM VIA CALLER'S R1
         RETURN (14,12),RC=4       TELL CALLER IT IS CHANGED VIA R15
*
NONE     EQU   *
         WTO   'IMSROUTE - NO DFSPRPX0 WAS FOUND IN CDE',              *
               ROUTCDE=11
         B     LEAVE
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
RMT02    DC    CL7'RMT02'          BLOOMFIELD
RMT04    DC    CL7'RMT04'          CHATHAM
RMT05    DC    CL7'RMT05'          KENILWORTH
RMT07    DC    CL7'RMT07'          UNION
RMT44    DC    CL7'RMT44'          MEMPHIS
*               456789012345678901234567890123456789012345678901234567
WTO1     WTO   'IMSROUTE - DESTINATION WAS NOT CHANGED FOR JOB XXXXXXXX*
               ',ROUTCDE=11,MF=L
WTO2     WTO   'IMSROUTE - DESTINATION CHANGED TO RMTXX FOR PGM XXXXXXX*
               X',ROUTCDE=11,MF=L
         LTORG
         EJECT
WORKAREA DSECT
NEWDEST  DS    CL7                 CHANGED DESTINATION
NEWCLASS DS    CL1                 CHANGED CLASS
WTOWORK  EQU   *                   WTO FIELD
         SPACE
         END
