IEFDB401 TITLE 'LINKAGE CONVENTIONS'
IEFDB401 CSECT
         SAVE (14,12),,IEFDB401-PS00037-&SYSDATE SAVE CALLER'S REGS
         LR    R12,R15             SET BASE
         USING IEFDB401,R12        ESTABLISH ADDRESSABILITY
         LR    R2,R1               SAVE PARM REGISTER
         L     R1,4(,R2)           GET ADDR OF WORKAREA ADDR
         L     R1,0(,R1)           GET ADDR OF WORKAREA
         LA    R1,TUPO(0,R1)       POINT PAST ADDITIONAL TEXT UNIT PTRS
         ST    R13,4(,R1)          CHAIN
         ST    R1,8(,R13)            SAVE
         LR    R13,R1                  AREAS
         TITLE 'SCAN TEXT UNITS FOR DESIRED KEYS'
         USING WORKAREA,R13        GET ADDRESSABILITY TO WORKAREA
         XC    FROM(LENGTH),FROM   ZERO WORK AREA
         MODESET EXTKEY=ZERO       NEED KEY ZERO FOR FETCH PROTECT
         L     R4,0(,R2)           POINT TO SVC99 REQUEST BLOCK
         USING S99RB,R4            GET ADDRESSABILITY TO SVC99 RB
         ICM   R0,B'1111',S99FLAG2 ANY FLAGS INDICATING AUTH REQ
         BNZ   EXIT                YES - EXIT
         L     R5,S99TXTPP         NO - GET ADDR OF TEXT UNIT POINTERS
         USING S99TUPL,R5          GET ADDRESSABILITY TO TU POINTERS
TULOOP   DS    0H
         ICM   R7,B'0111',S99TUPL+1 GET A TEXT UNIT ADDRESS
         BZ    NEXTTU              SKIP NULL ENTRIES
         USING S99TUNIT,R7         GET ADDRESSABILITY TO TEXT UNITS
         CLI   S99TUKEY+1,DALSSREQ IS THIS SUBSYSTEM REQ
         BE    EXIT                YES - EXIT
         CLI   S99TUKEY+1,DALUNIT  NO - IS THIS A UNIT KEY
         BNE   TRYDDNAM            NO - GO CHECK NEXT KEY
         ST    R7,TUUNIT           YES - SAVE ADDR OF UNIT TEXT UNIT
         B     NEXTTU              GO CHECK NEXT KEY
TRYDDNAM DS    0H                  CHECK FOR DDNAME KEY
         CLI   S99TUKEY+1,DUNDDNAM IS THIS A DDNAME KEY
         BNE   TRYDSNAM            NO - GO CHECK NEXT KEY
         ST    R7,TUDDNAM          YES - SAVE ADDR OF DDNAME TEXT UNIT
         B     NEXTTU              GO CHECK NEXT KEY
TRYDSNAM DS    0H
         CLI   S99TUKEY+1,DUNDSNAM IS THIS A DSN KEY
         BNE   NEXTTU              NO - GO CHECK NEXT KEY
         ST    R7,TUDSNAM          YES - SAVE ADDR OF DSNAME TEXT UNIT
NEXTTU   DS    0H
         TM    S99TUPL,S99TUPLN    LAST TEXT UNIT
         BO    ENDTUS              YES - GET OUT OF LOOP
         LA    R5,4(R5)            NO - GET NEXT TEXT POINTER
         B     TULOOP              AND LOOP
ENDTUS   DS    0H
         ST    R5,TUEND            SAVE ADDRESS OF LAST TEXT UNIT
         TITLE 'PROCESS ALLOCATION REQUESTS'
         CLI   S99VERB,S99VRBAL    IS THIS ALLOCATION REQUEST
         BNE   UNALLOC             NO - MAY BE UNALLOCATION
         L     R15,PSAAOLD-PSA     YES - GET ASCB ADDRESS FROM PSA
         ICM   R15,B'1111',ASCBTSB-ASCB(R15) IS TSB PRESENT
         BZ    EXIT                NO - NOT RUNNING UNDER TSO
         ICM   R7,B'0111',TUDSNAM+1   YES - WAS DSNAME TEXT UNIT FOUND
         BZ    ALTUUNIT            NO - GO TRY UNIT TEXT UNIT
         CLI   S99TULNG+1,0        YES - WAS LENGTH ZERO
         BE    ALTUUNIT            YES - GO TRY UNIT TEXT UNIT
* LOCATE SPF TEMPORARY DATA SETS AND SKIP PCF SO THEY WILL BE
*        ALLOCATED ON STORAGE PACKS
         LH    R1,S99TULNG         GET LENGTH FOR MOVE
         BCTR  R1,0                DECREMENT FOR EXECUTE
         MVC   DSNAME,BLANKS       BLANK TARGET AREA
         EX    R1,MOVEDSNM         MOVE DSNAME TO WORK AREA
         TRT   DSNAME(9),TRANDSNM  LOOK FOR FIRST PERIOD
         BZ    ALTUUNIT            NO PERIODS SO CHECK UNIT TEXT UNIT
         CLC   0(4,R1),=C'.SPF'    Q. IS THIS A SPF TEMP DATA SET
         BE    EXIT                YES - DONT GO TO PCF
         TRT   1(9,R1),TRANDSNM    LOOK FOR SECOND PERIOD
         BZ    ALTUUNIT            NO PERIODS SO CHECK UNIT TEXT UNIT
         CLC   0(4,R1),=C'.SPF'    Q. IS THIS A SPF TEMP DATA SET
         BE    EXIT                YES - DONT GO TO PCF
*                                  NO - GO CHECK UNIT TEXT UNIT
         SPACE
ALTUUNIT DS    0H
         ICM   R7,B'0111',TUUNIT+1 YES - WAS UNIT TEXT UNIT FOUND
         CLI   S99TULNG+1,0        YES - WAS LENGTH ZERO
         BE    EXITPCF             YES - GO ON TO PCF
         IC    R1,S99TULNG+1       NO - GET LENGTH FOR MOVE
         BCTR  R1,0                DECREMENT FOR EXECUTE
         MVC   UNITNAME,BLANKS     BLANK TARGET AREA
         EX    R1,MOVEUNIT         MOVE UNIT NAME TO WORK AREA
         L     R3,CVTPTR           GET ADDRESS OF CVT
         L     R15,CVTLPDSR-CVT(,R3) GET ADDR OF LPDE SEARCH ROUTINE
         LM    R0,R1,DEVNAMET      GET 'DEVNAMET' IN R0 & R1
         BALR  R14,R15             CALL IEAVVMSR - DESTROYS R6, R8 & R9
         B     GOTNAMET            +0 - DEVNAMET FOUND
         B     EXITPCF             +4 - DEVNAMET NOT FOUND - GO TO PCF
GOTNAMET DS    0H                  DEVNAMET LPDE FOUND
         LR    R1,R0               GET ADDRESS OF DEVNAMET LPDE
         L     R1,LPDENTP-LPDE(,R1) GET EP ADDRESS OF DEVNAMET
         L     R15,0(,R1)          GET NUMBER OF ENTRIES
         LA    R1,4(,R1)           GET ADDRESS OF 1ST ENTRY
DEVLOOP  DS    0H                  LOOP LOOKING FOR UNIT NAME
         CLC   UNITNAME,0(R1)      IS THIS THE REQUESTED UNIT
         BE    GOTUNIT             YES - UNIT NAME FOUND
         LA    R1,12(,R1)          NO - POINT TO NEXT ENTRY
         BCT   R15,DEVLOOP         LOOP UNTIL END
         B     EXITPCF             NOT FOUND - GO ON TO PCF
GOTUNIT  DS    0H                  UNIT NAME FOUND IN DEVNAMET
         TM    10(R1),X'20'        DOES THIS UNIT NAME CONTAIN DASD
         BZ    EXIT                NO - EXIT
         B     EXITPCF             YES - GO ON TO PCF
         TITLE 'PROCESS UNALLOCATION REQUESTS'
UNALLOC  DS    0H                  ENTRY WAS NOT ALLOCATION REQUEST
         CLI   S99VERB,S99VRBUN    IS THIS UNALLOCATION REQUEST
         BNE   EXIT                NO - EXIT
         ICM   R7,B'1111',TUDDNAM  YES - WAS DDNAME TEXT UNIT FOUND
         BZ    EXIT                NO - EXIT
         CLI   S99TULNG+1,8        YES - WAS LENGTH EIGHT
         BNE   EXIT                NO - EXIT
         CLC   SYSUDUMP,S99TUPAR   YES - WAS THIS SYSUDUMP
         BNE   EXIT                NO - EXIT
         L     R3,CVTPTR           YES - GET ADDRESS OF CVT
         L     R15,CVTLPDSR-CVT(,R3) GET ADDR OF LPDE SEARCH ROUTINE
         LM    R0,R1,IMSROUTE      GET 'IMSROUTE' IN R0 & R1
         BALR  R14,R15             CALL IEAVVMSR - DESTROYS R6, R8 & R9
         B     GOTROUTE            +0 - IMSROUTE FOUND
         B     EXIT                +4 - IMSROUTE NOT FOUND - EXIT
GOTROUTE DS    0H                  IMSROUTE FOUND
         LR    R1,R0               GET ADDRESS OF IMSROUTE LPDE
         L     R15,LPDENTP-LPDE(R1) GET ENTRY POINT ADDRESS
         LA    R1,WORKMASK         POINT TO WORK AREA FOR IMSROUTE
         BALR  R14,R15             CALL IMSROUTE
         LTR   R15,R15             DID IMS WANT TO REROUTE
         BZ    EXIT                NO - EXIT
         MVC   N1TUPAR,0(R1)       YES - MOVE NEW DEST TO TEXT UNIT
         MVI   N1TUKEY+1,DUNOVSUS  SET DESTINATION KEY
         MVI   N1TUNUM+1,1         SET COUNT
         MVI   N1TULNG+1,7         SET LENGTH
         MVC   N2TUPAR,7(R1)       MOVE NEW CLASS TO TEXT UNIT
         MVI   N2TUKEY+1,DUNOVCLS  SET OVERRIDE CLASS KEY
         MVI   N2TUNUM+1,1         SET COUNT
         MVI   N2TULNG+1,1         SET LENGTH
         L     R5,TUEND            GET END OF TEXT UNIT LIST PTR
         MVI   S99TUPL,0           CLEAR LIST END INDICATOR
         LA    R5,4(R5)            ADD NEW UNIT
         LA    R7,N1TU             POINT TO CREATED DESTINATION UNIT
         STCM  R7,B'0111',S99TUPL+1 STORE IN TEXT UNIT POINTER LIST
         MVI   S99TUPL,0           CLEAR LIST END INDICATOR
         LA    R5,4(R5)            ADD NEW UNIT
         LA    R7,N2TU             POINT TO CREATED CLASS UNIT
         STCM  R7,B'0111',S99TUPL+1 STORE IN TEXT UNIT POINTER LIST
         MVI   S99TUPL,S99TUPLN    SET NEW LIST END
         TITLE 'EXIT BACK TO SCHEDULER'
EXIT     DS    0H
         SLR   R15,R15             SET RETURN CODE
         MODESET EXTKEY=SCHED      SET SCHEDULER KEY
         L     R13,4(,R13)         UNCHAIN
         RETURN (14,12),RC=(15)    RETURN TO SCHEDULER
         TITLE 'EXIT TO PCF'
EXITPCF  DS    0H
         MODESET EXTKEY=SCHED      SET SCHEDULER KEY
         L     R13,4(,R13)         UNCHAIN
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         BALR  R15,0               ESTABLISH TEMPORARY BASE
         USING *,R15               ESTABLISH TEMPORARY ADDRESSABILITY
         XCTL  EPLOC=PCFALOC       XCTL TO PCF
         DROP  R15                 DROP TEMPORARY ADDRESSABILITY
         TITLE 'LTORG'
         LTORG ,
         TITLE 'CONSTANTS'
         DS    0D                  NEEDED TO ALIGN FOR LM
DEVNAMET DC    CL8'DEVNAMET'
PCFALOC  DC    CL8'PCFALOC'
IMSROUTE DC    CL8'IMSROUTE'
SYSUDUMP DC    CL8'SYSUDUMP'
BLANKS   DC    CL44' '             USED TO BLANK OUT UNITNAME/DSNAME
TRANDSNM DC    256X'00'            DSNAME TRANSLATE TABLE
         ORG   TRANDSNM+X'4B'
         DC    X'4B'               LOOKING FOR PERIODS
         ORG
MOVEDSNM MVC   DSNAME(*-*),S99TUPAR MOVE DSNAME TO WORK AREA
MOVEUNIT MVC   UNITNAME(*-*),S99TUPAR MOVE UNIT NAME TO WORK AREA
         TITLE 'PATCH AREA'
PATCH    DC    50S(*)              GENERATE SCON'S FOR PATCH AREA
         TITLE 'REGISTER EQUATES'
R0       EQU   0
R1       EQU   1                   PARAMETER & WORK REGISTER
R2       EQU   2                   COPY OF R1 ON ENTRY
R3       EQU   3                   ADDRESS OF CVT REQUIRED BY IEAVVMSR
R4       EQU   4                   ADDRESS OF SVC99 REQUEST BLOCK
R5       EQU   5                   ADDRESS OF SVC99 TEXT UNIT POINTER
R6       EQU   6                   DESTROYED BY IEAVVMSR
R7       EQU   7                   ADDRESS OF SVC99 TEXT UNIT
R8       EQU   8                   DESTROYED BY IEAVVMSR
R9       EQU   9                   DESTROYED BY IEAVVMSR
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE - EP + 0
R13      EQU   13                  SAVE AREA ADDRESS
R14      EQU   14                  OS LINKAGE REGISTER
R15      EQU   15                  OS LINKAGE REGISTER
         TITLE 'DSECT MAPPING PASSED WORK AREA'
WORKAREA DSECT
* THIS WORK AREA IS CONTIGUOUS TO THE TEXT POINTER LIST.  ANY
* ADDITIONS TO THE TEXT POINTER LIST MUST BE ACCOUNTED FOR HERE.
ADDLTUP1 DS    F                   ADDITIONAL TEXT POINTER 1 (DEST)
ADDLTUP2 DS    F                   ADDITIONAL TEXT POINTER 2 (CLASS)
TUPO     EQU   *-WORKAREA          TEXT UNIT POINTERS OFFSET
SAVEAREA DS    9D                  REGISTER SAVE AREA
FROM     DS    0D                  BEGINNING OF AREA TO BE ZEROED
TUUNIT   DS    F                   ADDRESS OF DALUNIT TEXT UNIT
TUDDNAM  DS    F                   ADDRESS OF DALDDNAM TEXT UNIT
TUDSNAM  DS    F                   ADDRESS OF DALDSNAM TEXT UNIT
TUEND    DS    F                   ADDRESS OF LAST TEXT UNIT
DSNAME   DS    CL44                DSNAME EXTRACTED FROM TEXT UNIT
UNITNAME DS    CL8                 UNIT NAME EXTRACTED FROM TEXT UNIT
N1TU     DS    0H                  NEW DESTINATION TEXT UNIT
N1TUKEY  DS    H                   X'0058' - DUNSUSER KEY
N1TUNUM  DS    H                   X'0001' - DUNSUSER COUNT
N1TULNG  DS    H                   X'0007' - DUNSUSER LENGTH
N1TUPAR  DS    CL7                 C'       ' - DUNSUSER PARM
N2TU     DS    0H                  NEW CLASS TEXT UNIT
N2TUKEY  DS    H                   X'0018' - DUNOVCLS KEY
N2TUNUM  DS    H                   X'0001' - DUNOVCLS COUNT
N2TULNG  DS    H                   X'0001' - DUNOVCLS LENGTH
N2TUPAR  DS    CL1                 C' '    - DUNOVCLS PARM
LENGTH   EQU   *-FROM              LENGTH TO BE ZEROED
         DS    0D                  ALIGN WORKMASK
WORKMASK DS    256X                WORK COPY OF UNIT NAME'S MASK
         TITLE 'SVC99 PARAMETER LIST DSECT'
         IEFZB4D0
         TITLE 'SVC99 TEXT UNIT DSECT'
         IEFZB4D2
         TITLE 'CVT DSECT'
         CVT   DSECT=YES
         TITLE 'PSA DSECT'
         IHAPSA
         TITLE 'ASCB DSECT'
         IHAASCB
         TITLE 'LPDE DSECT'
         IHALPDE
         SPACE 10
         END
