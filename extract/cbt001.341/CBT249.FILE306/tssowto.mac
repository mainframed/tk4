         TITLE '   T S S O W T O   '
***********************************************************************
*                                                                     *
*        'TSSOWTO'                                                    *
*                                                                     *
***********************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY, PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC, MCLEAN VA.
*        DATE WRITTEN. MAY 19 1976.
*        DATE UPDATED. JUNE 10 1982.
*        ATTRIBUTES. RE-ENTRANT.
*        DESCRIPTION.
*            THIS PROGRAM READS A DATASET AND WRITES EVERY
*            RECORD ON THE OPERATOR'S CONSOLE.
*
*            IT IS INVOKED BY THE TSSO SUBSYSTEM, WHICH IS
*            RUNNING IN SUPERVISOR STATE KEY ZERO, SO THERE
*            IS SOME IRREGULAR CODE AT THE FREEPOOL MACRO.
*
*            ONLY THE FIRST 72 BYTES OF FIXED-LENGTH RECORDS
*            ARE DISPLAYED. ONLY THE FIRST 72 BYTES OF VARIABLE
*            LENGTH RECORDS ARE DISPLAYED. NO MORE THAN 50
*            RECORDS ARE DISPLAYED.
*            THE PARM FIELD CONTAINS 3 HALFWORDS AND A CHARACTER.
*            1) THE MAXIMUM NUMBER OF RECORDS TO BE DISPLAYED.
*               (CANNOT EXCEED 50).
*            2) THE CONSOLE ID WHERE THE RECORDS ARE TO BE DISPLAYED.
*            3) THE NUMBER OF RECORDS TO SKIP BEFORE DISPLAYING.
*            4) DISPLAY ON TSO TERMINAL IF EXECUTED BY A SESSION
*               (FOR TESTING PURPOSES). (SET T = 'T').
*        LOG OF CHANGES.
*         24JUL81 - IGNORE READY MESSAGE.
*         10JUN82 - NEW PARAMETER ALLOWS SKIPPING RECORDS.
         SPACE
TSSOWTO  START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'TSSOWTO'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1
         L     0,@SIZE
         GETMAIN R,LV=(0)
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         LR    R1,R2     RESTORE R1
         USING @DATA,R13
         SPACE
         XC    PARMAREA(7),PARMAREA DEFAULT CONSOLE 0
         MVI   TERM,C'N'           DEFAULT TERM NO
         SR    R4,R4               DEFAULT SKIPNUM
         LA    R5,20               DEFAULT MAXIMUM LINES
         L     R6,0(,R1)           POINT TO PARM FIELD
         LH    R14,0(,R6)          LENGTH OF PARM
         LTR   R14,R14             LENGTH ZERO?
         BZ    PARMX               YES - USE DEFAULT
         BCTR  R14,0               LENGTH MINUS 1
         B     *+10
         MVC   PARMAREA(0),2(R6)   MOVE PARM
         EX    R14,*-6
         LH    R5,PARMAREA         MAX LINES
         CH    R5,MAX              GREATER THAN MAX?
         BNH   *+8                 NO, SKIP NEXT INSTR
         LH    R5,MAX              YES, USE MAX
         LH    R4,SKIPNUM          NUMBER OF LINES TO SKIP
PARMX    EQU   *
         MVC   MAXLEN,MAXLENV
         MVC   IDCB(IDCBL),IDCBMODL
         LA    R2,IDCB
         MVI   OPEN,X'80'
         SPACE
         OPEN  ((R2),INPUT),MF=(E,OPEN)
         SPACE
         TM    OFLGS(R2),X'10'     OPEN SUCCESSFUL?
         BNO   EXIT16              NO - BRANCH
         MVC   WTOW(12),WTOMR0
         MVC   WTOL+2(2),WTOW+2
         SPACE
READLOOP EQU   *
         MVI   WORKA,C' '
         MVC   WORKA+1(MAXL-1),WORKA
         SPACE
         GET   (R2),WORKA
         SPACE
         LTR   R4,R4               ARE WE SKIPPING RECORDS
         BZ    SKIPX               NO, BRANCH
         SPACE
         BCTR  R4,0                REDUCE R4 BY 1
         B     READLOOP            GO READ ANOTHER
         SPACE
SKIPX    EQU   *
         TM    IDCB+RECFM,B'11000000'
         BO    NOTV
         TM    IDCB+RECFM,B'01000000'
         BZ    NOTV
         LH    R7,WORKA            GET LENGTH FROM VARIABLE RDW
         TM    IDCB+RECFM,B'00000110' ASA OR MACHINE CHAR?
         BZ    NOVCHAR             NEITHER - BRANCH
         MVC   WORKA+4(100),WORKA+5
         BCTR  R7,0
NOVCHAR  EQU   *
         MVC   WORKA(100),WORKA+4
         CH    R7,MAXLEN           LENGTH GREATER THAN MAX?
         BNH   WTOL7               NO - BRANCH
         LH    R7,MAXLEN           YES - USE MAXIMUM
         B     WTOL7
NOTV     LA    R7,72+4             LENGTH
         LA    R8,WORKA+71
         LA    R0,70
TRAILOOP CLI   0(R8),C' '          LAST NONBLANK FOUND?
         BNE   TRAILXIT            YES - BRANCH
         BCTR  R8,0                SUBTRACT 1 FROM POINTER
         BCTR  R7,0                SUBTRACT 1 FROM LENGTH
         BCT   R0,TRAILOOP         BRANCH 70 TIMES
TRAILXIT EQU   *
WTOL7    CLC   WORKA(7),READY      IS IT READY
         BE    AFTERSVC            YES, SKIP IT
         LA    R1,WTOL             POINT TO WTO MF=L
         STH   R7,0(,R1)           SET LENGTH
         CLI   TERM,C'T'
         BE    TERMOUT
         LH    R15,WTOW+2          GET MCS FLAGS FROM MODEL
         STH   R15,2(,R1)          SET MCS FLAGS
         LA    R14,0(R7,R1)        POINT TO END OF TEXT
         MVC   0(4,R14),WTOW+8     APPEND ROUTING AND DESC FROM MODEL
         LH    R0,CONSOLE          CONSOLE ID
         SVC   35
         SPACE
AFTERSVC BCT   R5,READLOOP
         B     EODAD
         SPACE
TERMOUT  LH    R0,0(,R1)
         SH    R0,=H'4'
         LA    R1,4(,R1)
         SVC   93
         B     AFTERSVC
         SPACE
EXIT16   LA    R15,16
         B     EXIT
         SPACE
EODAD    MVI   OPEN,X'80'
         SPACE
         CLOSE ((R2)),MF=(E,OPEN)
         SPACE
         FREEPOOL (R2)
         SPACE
*               SEE MVS DATA MGMT SERVICES GUIDE, PAGE 77
         ORG   *-2                 BACK UP TO SVC 10 IN FREEPOOL
         LA    R15,252             SUBPOOL 252 FOR PROTECT KEY 0
         SLL   R15,24              SHIFT 252 TO LEFTMOST BYTE
         OR    R0,R15              CHANGE SUBPOOL TO 252
         SVC   10                  FREEMAIN
         SPACE
EXIT0    SR    15,15
EXIT     LR    1,13
         L     0,@SIZE
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
RECFM    EQU   36
OFLGS    EQU   48
MAXL     EQU   72
MAXLENV  DC    0H'0',AL2(MAXL+4)
         PRINT NOGEN
IDCBMODL DCB   DDNAME=SYSTSPRT,DSORG=PS,MACRF=GM,EODAD=EODAD
IDCBL    EQU   *-IDCBMODL
         PRINT GEN
READY    DC    C'READY ',X'15'
MAX      DC    H'50'
WTOM     WTO   'TEST',DESC=(5),MF=L
WTOML    EQU   *-WTOM
WTOMR0   WTO   'TEST',DESC=(5),MCSFLAG=(REG0,RESP),MF=L
WTOMR0N  WTO   'TEST',DESC=(5),MCSFLAG=(REG0,RESP,NOCPY),MF=L
         LTORG
         DC    0D'0'
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F
DOUBLE   DS    D
OPEN     DS    F
MAXLEN   DS    H
*
PARMAREA DS    0D
         DS    H
CONSOLE  DS    H
SKIPNUM  DS    H
TERM     DS    C
*
IDCB     DS    0D,XL(IDCBL)
WTOW     DS    0D,XL12
WTOL     DS    0D,2H
WORKA    DS    260X
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
