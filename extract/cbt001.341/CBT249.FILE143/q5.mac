DISPLAY  QSTART 'QUEUE COMMAND - 3270 DISPLAY ROUTINES'
         GBLB  &QPFK          GLOBAL PFK SWITCH             *HMD 12/79*
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         MVC   QDPLUSSS,QBLANK BLANK OUT THE OVERFLOW IND.  *HMD 12/79*
         LH    R4,QDMLNG      LOAD MSG LENGTH
         CH    R4,=H'1680'    IS THE MSG LENGTH MORE THAN 1680?
         BH    RETURN         YES. GO AWAY.
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR         YES. WRITE SCREEN.
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         MVC   QDPLUSSS,=C' (TO CONTINUE DISPLAY DEPRESS RETURN KEY)'
* ABOVE IS FOR SS TERMINALS AND BATCH                       *HMD 12/79*
         AH    R4,QDNEXT      ADD CURRENT LOCATION ON SCREEN
         CH    R4,=H'1680'    IS THERE ROOM ON THE SCREEN?
         BH    WRTSCR         NO. WRITE SCREEN.
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER
         LR    R1,R4          SAVE LINE NUMBER
         LA    R4,QDLINE1(R4) LOAD ADDRESS OF NEXT LINE
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE
         LA    R5,80          LENGTH MUST BE MULTIPLE OF 80
DSP3     CR    R5,R7          IS 5 NOT LESS THAN 7?
         BNL   DSP4           YES. GO DO IT.
         LA    R5,80(R5)      INCR BY 80
         B     DSP3           TRY AGAIN
DSP4     AR    R1,R5          UPDATE LINE NUMBER
         STH   R1,QDNEXT      STORE LINE NUMBER
         MVCL  R4,R6          MOVE THE MESSAGE TO THE SCREEN
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY                               *
*                                                                     *
***********************************************************************
WRTSCR   TR    QDLINE1(256),TABLE ENSURE PRINTABILITY
         TR    QDLINE1+256(256),TABLE
         TR    QDLINE1+512(256),TABLE
         TR    QDLINE1+768(256),TABLE
         TR    QDLINE1+1024(256),TABLE
         TR    QDLINE1+1280(256),TABLE
         TR    QDLINE1+1536(144),TABLE
TPUTSCRN TM    QDSSFLG,QDSSBAT     IS THIS SS OR BATCH?     *HMD 12/79*
         BO    SPECPUTL            SPECIAL PUTLINE          *HMD 12/79*
         LA    R1,QDSCREEN    LOAD ADDRESS OF SCREEN BUFFER
         TPUT  (1),QDSLNG,FULLSCR    WRITE SCREEN
SPECRETN LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE
         LA    R5,1680        LOAD LENGTH OF SCREEN
         SR    R6,R6          NO SENDING FIELD NEEDED
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER
         SR    R7,R7          FILL SCREEN WITH NULLS
         MVCL  R4,R6          CLEAR THE SCREEN
         TM    QDSSFLG,QDSSBAT  IS THIS SS OR BATCH?        *HMD 12/79*
         BNO   FULLSCR        NO,  USE TGET MACRO           *HMD 12/79*
         TM    QDSSFLG,QDBATC IS THIS A BATCH JOB?          *HMD 12/79*
         BNO   PROMPTR        NO, PROMPT FOR MORE INPUT     *HMD 12/79*
         MVC   QDPLUSSS,QBLANK    BLANK PROMPT LINE         *HMD 12/79*
         CLI   QDPLUS,C'+'    IS THIS A CONTINUATION?       *HMD 12/79*
         BE    NOINPUT        YES, JUST FINISH DISPLAY      *HMD 12/79*
         B     PROMPTR        OTHERWISE GET NEXT INPUT LINE *HMD 12/79*
         AIF   (&QPFK).PFK1   SKIP NON-PFK CODE             *HMD 12/79*
FULLSCR  LA    R1,QDREPLY     LOAD ADDRESS OF REPLY AREA
         CNOP  0,4            BECAUSE OF DUMB PTF IN ERROR  *HMD 11/80*
         TGET  (1),63         WAIT FOR REPLY
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?
         BNE   NOCLEAR        NO. CONTINUE.
         TCLEARQ INPUT        CLEAR THE QUEUE
NOCLEAR  CLI   QDREPLY,X'6E'  VTAM RESHOW INDICATION?       *HMD 05/79*
         BNE   NRESHOW        NO RESHOW                     *HMD 05/79*
         SLR   R1,R1          PRETEND NO INPUT              *HMD 05/79*
         MVC   QDREPLY(2),=C'  '  BLANK OUT REPLY AREA      *HMD 05/79*
NRESHOW  DS    0H             POINT TO BRANCH TO            *HMD 05/79*
NRSHW1   STH   R1,QDRLNG                                    *HMD 12/79*
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         AGO   .PFK2                                        *HMD 12/79*
.PFK1    ANOP                 PFK CODE                      *HMD 12/79*
FULLSCR  LA    R6,QDREPLY     LOAD ADDRESS OF REPLY AREA    *HMD 12/79*
         XC    PFREPLY,PFREPLY   CLEAR PFKEY REPLY AREA     *HMD 12/79*
         XC    QDREPLY,QDREPLY   CLEAR NORMAL REPLY AREA    *HMD 12/79*
         LA    R1,PFREPLY     LOAD ADDRESS OF REPLY AREA    *HMD 12/79*
         CNOP  0,4            BECAUSE OF DUMB PTF ERROR     *HMD 11/80*
         TGET  (1),69,ASIS    GET REPLY FROM USER           *HMD 12/79*
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?  *HMD 12/79*
         BNE   NOCLEAR        NO, CONTINUE                  *HMD 12/79*
         TCLEARQ INPUT        CLEAR THE QUEUE               *HMD 12/79*
NOCLEAR  CLI   PFREPLY,X'6E'  VTAM RESHOW INDICATION? (PA2) *HMD 05/79*
         BNE   NRESHOW        NO RESHOW                     *HMD 05/79*
         SLR   R1,R1          PRETEND NO INPUT              *HMD 05/79*
         MVC   QDREPLY(2),=C'  '  BLANK OUT REPLY AREA      *HMD 05/79*
         B     NRSHW1         BRANCH TO RESHOW AREA         *HMD 12/79*
NRESHOW  IC    R4,PFCODE      GET PFK CODE                  *HMD 12/79*
         XR    R5,R5          CLEAR R5                      *HMD 12/79*
         N     R4,=X'0000000F'  EXTRACT PF-KEY NUMBER       *HMD 12/79*
         CH    R4,=H'12'      ENTER KEY?                    *HMD 12/79*
         BH    ENTKEY         YES, PROCEED WITH THAT IN MIND*HMD 12/79*
         BCTR  R4,0           DECREMENT R4                  *HMD 12/79*
         MH    R4,=H'5'       INDEX INTO PF TABLE           *HMD 12/79*
         LA    R5,PFKTAB      GET TABLE ADDRESS             *HMD 12/79*
         LA    R5,0(R4,R5)    POINT TO RIGHT CODE           *HMD 12/79*
         MVC   QDREPLY(5),0(R5)   MOVE WORD TO REPLY AREA   *HMD 12/79*
         LA    R5,5           PUT '5' IN R5                 *HMD 12/79*
         LA    R6,3(,R6)      BUMP R6                       *HMD 12/79*
ENTKEY   DS    0H             COME HERE IF ENTER KEY IS HIT *HMD 12/79*
         SH    R1,=H'3'       BUMP PAST PFK TEXT            *HMD 12/79*
         BZ    NOTXT          NO TEXT AVAILABLE             *HMD 12/79*
         MVC   0(60,R6),PFTXT MOVE TEXT TO BUFFER           *HMD 12/79*
         LA    R5,3                                         *HMD 12/79*
         SH    R1,=H'3'                                     *HMD 12/79*
NOTXT    LA    R1,0(R5,R1)    GET LENGTH OF REPLY           *HMD 12/79*
NRSHW1   STH   R1,QDRLNG      SAVE LENGTH IN QUEUE AREA     *HMD 12/79*
         LH    R1,QDRLNG      LOAD REPLY LENGTH
         LTR   R1,R1          WAS THERE INPUT FROM USER?    *HMD 12/79*
         BNZ   INTER                                        *HMD 12/79*
.PFK2    ANOP                                               *HMD 12/79*
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW        *HMD 12/79*
         B     DSP2           CONTINUE PROCESSING           *HMD 12/79*
INTER    L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
         EJECT                                              *HMD 12/79*
*************************************************************HMD 12/79*
*   SPECIAL PUTLINE/GETLINE ROUTINE FOR SS TERMINALS        *HMD 12/79*
*   AND BATCH PROCESSING.                                   *HMD 12/79*
*************************************************************HMD 12/79*
SPECPUTL DS    0H             PUTLINE/GETLINE ROUTINE       *HMD 12/79*
         MVC   QDIOBUF,QBLANK BLANK OUTPUT BUFFER           *HMD 12/79*
         MVC   QDIOBUF(L'QDTITLE),QDTITLE  TITLE LINE       *HMD 12/79*
         MVC   QDIOBUF+L'QDTITLE(47),QDREPLY                *HMD 12/79*
         XC    IOECB,IOECB    CLEAR EVENT CNTL BLK          *HMD 12/79*
         L     R15,QVPUTL     LOAD ADDRESS OF PUTLINE       *HMD 12/79*
         PUTLINE PARM=QDPTPB,OUTPUT=(QDOUTL,TERM,SINGLE,DATA),         X
               MF=(E,QDIOPL),ENTRY=(15)                     *HMD 12/79*
         LA    R3,22           PUT OUT 22 SCREEN LINES      *HMD 12/79*
         LA    R4,QDHLINE      LOAD FIRST LINE ADDRESS      *HMD 12/79*
PUTLOOP  MVC   QDIOBUF,0(R4)   MOVE LINE TO OUTPUT AREA     *HMD 12/79*
         CLC   QDIOBUF,QBLANK  IS LINE ALL BLANKS?          *HMD 12/79*
         BE    PUTBCT          IF SO, JUST GET NEXT LINE    *HMD 12/79*
         L     R15,QVPUTL      LOAD PUTLINE ADDRESS         *HMD 12/79*
         XC    IOECB,IOECB     CLEAR ECB                    *HMD 12/79*
         PUTLINE PARM=QDPTPB,OUTPUT=(QDOUTL,TERM,SINGLE,DATA),         X
               MF=(E,QDIOPL),ENTRY=(15)                     *HMD 12/79*
PUTBCT   LA    R4,80(R4)        BUMP R4 TO POINT TO NEXT LN *HMD 12/79*
         BCT   R3,PUTLOOP       LOOP UNTIL ALL LINES OUT    *HMD 12/79*
         B     SPECRETN         SPECIAL RETURN ADDRESS      *HMD 12/79*
PROMPTR  L     R15,QVPUTL       PROMPT FOR INPUT            *HMD 12/79*
         XC    IOECB,IOECB      CLEAR ECB                   *HMD 12/79*
         PUTLINE  PARM=QDPTPB,OUTPUT=(QDPROMPT,TERM,SINGLE,DATA),      X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),          *HMD 12/79*X
               MF=(E,QDIOPL),ENTRY=(15)                     *HMD 12/79*
GETL     XC    IOECB,IOECB          CLEAR ECB               *HMD 12/79*
         L     R15,QVGETL           GET LINE ENTRY ADDR     *HMD 12/79*
         GETLINE  PARM=QDGTPB,MF=(E,QDIOPL),ENTRY=(15)      *HMD 12/79*
         CH    R15,=H'16'           END OF DATA             *HMD 12/79*
         BE    EOINLST              END OF IN STORAGE LIST  *HMD 12/79*
         CH    R15,=H'4'            SEE IF GETLINE WAS OK   *HMD 12/79*
         BH    NOINPUT              ASSUME NO INPUT RETURNED*HMD 12/79*
         L     R3,QDGTPB+4          GET INPUT BUFF ADDR     *HMD 12/79*
         LH    R4,0(R3)             GET INPUT LINE LNGTH    *HMD 12/79*
         SH    R4,=H'5'             SUBTRACT HDR LENGTH + 1 *HMD 12/79*
         BM    NOINPUT              MINUS MEANS NO INPUT    *HMD 12/79*
         CH    R4,=H'62'            IS REPLY TOO LONG       *HMD 12/79*
         BNH   RPLYOK               NO REPLY IS OK          *HMD 12/79*
         LA    R4,62                ONLY ALLOW 62 CHARS     *HMD 12/79*
RPLYOK   MVC   QDREPLY,QBLANK       BLANK OUT REPLY AREA    *HMD 12/79*
         EX    R4,MVCRPLY           MOVE REPLY TO AREA      *HMD 12/79*
         LA    R4,1(R4)             GET TRUE LENGTH         *HMD 12/79*
FREEDBUF LTR   R4,R4                IS THERE INPUT          *HMD 12/79*
         BZ    JUSTRTN              NO, SKIP FREEMAIN       *HMD 12/79*
         FREEMAIN RU,LV=(4),A=(3),SP=1  FREE INPUT BUFFER   *HMD 12/79*
JUSTRTN  DS    0H                   SKIP FREEMAIN           *HMD 12/79*
         LR    R1,R4                USE MAINLINE CODE NOW   *HMD 12/79*
         B     NRSHW1               HANDLE REPLY            *HMD 12/79*
EOINLST  DS    0H                   END OF IN-STORAGE LIST  *HMD 12/79*
         MVC   QDREPLY,QBLANK       BLANK OUT REPLY AREA    *HMD 12/79*
         MVC   QDREPLY(4),=C'END '  INDICATE END            *HMD 12/79*
         LA    R1,4                 LENGTH OF REPLY         *HMD 12/79*
         B     NRSHW1               TERMINATE COMMAND       *HMD 12/79*
MVCRPLY  MVC   QDREPLY(0),4(R3)     MOVE REPLY TO AREA      *HMD 12/79*
NOINPUT  SLR   R4,R4                CLEAR REG. 4            *HMD 12/79*
         B     FREEDBUF             FREE BUFFER             *HMD 12/79*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
         AIF   (NOT &QPFK).PFK3                             *HMD 12/79*
         SPACE 2                                            *HMD 12/79*
* PF-KEY DEFINITIONS                                        *HMD 12/79*
PFKTAB   DS    0CL60                                        *HMD 12/79*
PF1      DC    CL5'HELP'     HELP                           *HMD 12/79*
PF2      DC    CL5'DC'       DISPLAY CPU                    *HMD 12/79*
PF3      DC    CL5'E'        END                            *HMD 12/79*
PF4      DC    CL5'TOP'      MOVE DISPLAY TO TOP            *HMD 12/79*
PF5      DC    CL5'F'        FIND                           *HMD 12/79*
PF6      DC    CL5'DI'       DISPLAY INPUT QUEUE            *HMD 12/79*
PF7      DC    CL5'PB'       PAGE BACKWARD                  *HMD 12/79*
PF8      DC    CL5'PF'       PAGE FORWARD                   *HMD 12/79*
PF9      DC    CL5'DO'       DISPLAY OUTPUT QUEUE           *HMD 12/79*
PF10     DC    CL5'CO'       COLUMN 1                       *HMD 12/79*
PF11     DC    CL5'CO 50'    COLUMN 50                      *HMD 12/79*
PF12     DC    CL5'ST'       STATUS                         *HMD 12/79*
         SPACE 2                                            *HMD 12/79*
.PFK3    ANOP                                               *HMD 12/79*
TABLE    DC    CL64' '
*     0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF
 DC C'          Ö.<(+×&&         !$*);^-/         ,%_>?          :#@'''
 DC C'="' CONTINUANCE OF PREVIOUS LINE BECAUSE OF DUPLICATION
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ                      '
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ      0123456789      '
         QCOMMON
         END
