QUEUE    QSTART 'QUEUE COMMAND - MAINLINE MODULE',MAIN=YES
         GBLA    &QLEVEL                                    *HMD 04/79*
***********************************************************************
*                                                                     *
*   CALL - INITIALIZATION                                             *
*                                                                     *
***********************************************************************
CALLINIT L     R15,=V(INIT)   ADDR OF INIT
         BALR  R14,R15        GO TO IT
         LA    R10,LOOP       INTERRUPTED RETURN ADDRESS
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL - READ JES2 CHECKPOINT FILE                                  *
*                                                                     *
***********************************************************************
LOOP     L     R15,=V(CKPT)   ADDR OF CKPT
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL - COMMAND LINE PARSE                                         *
*                                                                     *
***********************************************************************
         L     R15,=V(PARSE)  ADDR OF PARSE
         BALR  R14,R15        GO TO IT
         CLC   =C'E ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'EXIT ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'END ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'STOP ',QSUBNAME STOP?
         BE    EXIT           YES.
***********************************************************************
*                                                                     *
*   CALL - SUB-COMMAND MODULE SELECTED BY PARSE                       *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,DUMMY  NO OUTPUT MESSAGE
         L     R15,QSUBCMD    ADDR OF SUBCMD FROM QCOMMON
         BALR  R14,R15        GO TO IT
         MVC   QDMLNG,=H'0'   ZERO OUT MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        WRITE LAST SCREEN, GET NEXT INPUT
         B     LOOP           DO IT AGAIN
***********************************************************************
*                                                                     *
*   CLEAN UP AND GO HOME                                              *
*                                                                     *
***********************************************************************
EXIT     DS    0H             EXIT ROUTINE                  *HMD 12/79*
         TM    QDSSFLG,QDSSBAT IS THIS BATCH OR SS TERM?    *HMD 12/79*
         BO    EXITA          IF YES, FORGET SCREEN CLEAR   *HMD 12/79*
         TPUT  CLEAR,8,FULLSCR CLEAR THE SCREEN
         USING QCKPT,R8 BASE REG FOR CKPT WORK AREA
EXITA    L     R8,QVCKPT      LOAD BASE REG
         CLOSE MF=(E,HOCKPT)
         CLOSE MF=(E,QCSPOOLS)
         EJECT
***********************************************************************
*                                                           *HMD 06/79*
*   FREE ALLOCATED FILES                                    *HMD 06/79*
*                                                           *HMD 06/79*
***********************************************************************
         USING IHADCB,R6                ADDRESS DCB SYMBOLS *HMD 06/79*
FREEALL  XC    DA18PDSN,DA18PDSN        CLEAR DDNAME ADDRESS*HMD 06/79*
         L     R6,HOCKPT                LOAD CKPT DCB ADDR  *HMD 06/79*
         MVC   DA18DDN,DCBDDNAM         MOVE IN DDNAME      *HMD 06/79*
         MVI   DAIRFLAG,X'18'           INDICATE FREE       *HMD 06/79*
         L     R15,=V(ALLOCATE)         FIND ALLOCATE ROUT. *HMD 06/79*
         BALR  R14,R15                  GO TO IT            *HMD 06/79*
FREESPAC LA    R5,QCSPOOLS              SPOOL DCB ADDRS     *HMD 06/79*
SPOLLOOP L     R6,0(R5)                 LOAD DCB ADDRESS    *HMD 06/79*
         LTR   R6,R6                    WHAT DO WE DO NOW?  *HMD 06/79*
         BM    LASTONE                  LAST DCB TO FREE    *HMD 06/79*
         BZ    ENDLOOP                  NO DCB TO FREE      *HMD 06/79*
         MVC   DA18DDN,DCBDDNAM         MOVE IN DDNAME      *HMD 06/79*
         MVI   DAIRFLAG,X'18'           INDICATE FREE       *HMD 06/79*
         L     R15,=V(ALLOCATE)         FIND ALLOCATE ROUT. *HMD 06/79*
         BALR  R14,R15                  FREE THE DDNAME     *HMD 06/79*
         LA    R5,4(,R5)                BUMP DCB POINTER    *HMD 06/79*
         B     SPOLLOOP                 CONTINUE TO FREE    *HMD 06/79*
LASTONE  MVC   DA18DDN,DCBDDNAM         FREE LAST DDNAME    *HMD 06/79*
         MVI   DAIRFLAG,X'18'           INDICATE FREE       *HMD 06/79*
         L     R15,=V(ALLOCATE)         GET ALLOCATE ROUT.  *HMD 06/79*
         BALR  R14,R15                  FREE DDNAME         *HMD 06/79*
ENDLOOP  DS    0H                       END OF FREE LOOP    *HMD 06/79*
FREESTOR L     R0,QCLV                  LENGTH TO FREE      *HMD 04/79*
         L     R1,QCSTART               ADDRESS TO FREE     *HMD 04/79*
         FREEMAIN RU,LV=(0),A=(1),SP=1  ISSUE REGMAIN SVC   *HMD 04/79*
         L     R0,QCDCBLV               LENGTH OF DCB AREA  *HMD 04/79*
         L     R1,QCDCBAD               ADDR OF DCB AREA    *HMD 04/79*
         FREEMAIN RU,LV=(0),A=(1),SP=1  ISSUE REGMAIN SVC   *HMD 04/79*
         AIF   (&QLEVEL EQ 2).LVL2
         AIF   (&QLEVEL EQ 3).LVL2      SAME AS LEVEL 2     *HMD 12/79*
         L     R0,QCJCTLV               LOAD LENGTH TO FREE *HMD 04/79*
         L     R1,QCJCTA                LOAD ADDR TO FREE   *HMD 04/79*
         FREEMAIN RU,LV=(0),A=(1),SP=1  ISSUE REGMAIN SVC   *HMD 04/79*
         AGO   .EXIT
.LVL2    L     R0,QJQTLV                LOAD LENGTH TO FREE *HMD 05/79*
         L     R1,QCJQTL                LOAD AREA TO FREE   *HMD 04/79*
         FREEMAIN RU,LV=(0),A=(1),SP=1  ISSUE REGMAIN SVC   *HMD 04/79*
.EXIT    ANOP
EXIT1    STFSMODE  OFF                  TURN OFF FULL SCR   *HMD 05/79*
***                                                         *HMD 12/79*
***  DELETE TSO SERVICE ROUTINES                            *HMD 12/79*
***                                                         *HMD 12/79*
         DELETE EPLOC=QAPUTLN           DELETE PUTLINE      *HMD 12/79*
         DELETE EPLOC=QAGETLN           DELETE GETLINE      *HMD 12/79*
***                                                         *HMD 12/79*
***  RETURN TO MVS                                          *HMD 12/79*
***                                                         *HMD 12/79*
         LR    R1,R13                   SAVE SAVE AREA ADDR *HMD 04/79*
         L     R13,4(R13)               LOAD PREVIOUS SA    *HMD 04/79*
         LA    R0,1024                  LOAD LENGTH TO FREE *HMD 04/79*
         FREEMAIN R,LV=(0),A=(1)        FREE SA STORAGE     *HMD 04/79*
         LM    R14,R12,12(R13)          RELOAD REGS         *HMD 04/79*
         XR    R15,R15                  CLEAR RETURN CODE   *HMD 04/79*
         BR    R14                      RETURN              *HMD 04/79*
***********************************************************************
*                                                                     *
*   CONSTANTS AND OTHER ODDITIES                                      *
*                                                                     *
***********************************************************************
         LTORG
CLEAR    DC    X'C11140403C404000'
DUMMY    DC    CL80'    NO DATA IS AVAILABLE FOR YOUR REQUEST'
         QCOMMON
         EJECT
         DCBD  DSORG=(PS,DA),DEVD=DA                        *HMD 06/79*
         END
