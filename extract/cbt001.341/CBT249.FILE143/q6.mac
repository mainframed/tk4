FINDJOB  QSTART 'QUEUE COMMAND - LOCATE JQE, JCT, AND IOT BY JOBNAME'
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   DETERMINE IF SEARCH IS BY JOBNUMBER OR JOBNAME                    *
*                                                                     *
***********************************************************************
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
         CLI   QPARM1,C'0'    IS THE FIRST CHARACTER NUMERIC?
         BL    JOBNAME        NO. SEARCH BY JOBNAME
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R1,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R1,PACK        PACK THE FIELD
         CVB   R5,CONVERT     CONVERT TO BINARY
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOBNAME                                             *
*                                                                     *
***********************************************************************
JOBNAME  DS    0H             NOW, LOOK FOR JOBNAME         *HMD 04/79*
         L     R2,QCJQHEAD    LOAD ADDR OF JQT
         USING JQTDSECT,R2    BASE REG FOR JQT
         LA    R2,JQTOUT      FIRST JQE QUEUE
         LA    R1,JQTQMAX     MAXIMUM NUMBER OF QUEUES
         DROP  R2
NEXTJQT  LH    R3,0(R2)       OFFSET TO FIRST JQE IN QUEUE
NEXTJQE  SLA   R3,2           MULTIPLY BY 4
         BZ    ENDJQE         END OF QUEUE
         A     R3,QCJQTA      ADD BASE TO OFFSET
         USING JQEDSECT,R3    BASE REG FOR JQE
         CLI   QPARM1,C'0'    IS SEARCH BY JOBNUMBER?
         BL    CLCNAME        NO. SEARCH BY JOBNAME.
         CH    R5,JQEJOBNO    IS THIS THE RIGHT JOBNUMBER?
**********************************************************************
**           USING JES2 INTERNAL 'JOB' NUMBERS IN SEARCH            **
**                                                                  **
** TSO USERS AND STC TASKS, IN RESPONSE TO A $DA,T OR $DA,S, APPEAR **
** ON CONSOLES AS A 3 OR 4 DIGIT TSU/STC NUMBER. IN REALITY YOU     **
** MUST ADD 20,000 TO THE TSU NUMBER FOR THE INTERNAL JES2 'JOB'    **
** NUMBER. FOR STARTED TASKS, 10000 IS ADDED TO THE JES2 STC NUMBER **
** NUMBER TO DETERMINE THE JES2 INTERNAL NUMBER.                    **
**                                                                  **
** THE QUEUE DISPLAY ROUTINES HAVE BEEN ALTERED TO DISPLAY THE TRUE **
** JES2 INTERNAL NUMBERS AND THE SEARCH ROUTINES USE THESE INTERNAL **
** NUMBERS INSTEAD OF THE CONSOLE DISPLAY 'JOB' NUMBERS. THEREFORE  **
** ALWAYS DO A 'STATUS' OR 'DA' UNDER QUEUE AND USE THE 'JOB'       **
** NUMBER DISPLAYED WHEN REQUESTING QUEUE SERVICES BY 'JOB' NUMBER. **
**              ** HOWARD M. DEAN  10/13/79 **                      **
**********************************************************************
         BNE   NOMATCH        NOPE, TRY NEXT JQE           *HMD 04/79*
         B     FOUND          YEP, WE FINALLY FOUND ONE    *HMD 04/79*
CLCNAME  CLC   QPARM1,JQEJNAME IS THIS THE RIGHT JOBNAME?
         BE    FOUND          YES. PROCESS IT.
NOMATCH  LH    R3,JQECHAIN    NO. TRY NEXT ENTRY.
         B     NEXTJQE        LOOP
ENDJQE   LA    R2,2(R2)       TRY NEXT QUEUE
         BCT   R1,NEXTJQT     LOOP IF NOT LAST QUEUE
TILT     QTILT '*** JOBNAME NOT FOUND OR INVALID ***'
TILT2    QTILT '*** JOBNAME MUST BEGIN WITH YOUR USERID ***'
***********************************************************************
*                                                                     *
*   CHECK FOR AUTHORIZATION NOW - AUTH IS BASED ON PSCBATR2 VALUES    *
*                                                                     *
***********************************************************************
FOUND    DS    0H             IF NOT PRIV, USERID MUST = JN *HMD 06/81*
         TM    QXAUTH,1       AUTHORIZED?                   *HMD 06/81*
         BO    READJCT        FORGET CHECK                  *HMD 06/81*
         LA    R6,7           MAX USERID LENGTH             *HMD 06/81*
         LA    R7,QLOGON+7    LAST BYTE OF USERID           *HMD 06/81*
JLOOP    CLI   0(R7),C' '     IS THIS BYTE BLANK?           *HMD 06/81*
         BNE   JCHECK         NO. CONTINUE                  *HMD 06/81*
         BCTR  R7,0           BUMP DOWN 1                   *HMD 06/81*
         BCT   R6,JLOOP       TRY AGAIN                     *HMD 06/81*
JCHECK   EX    R6,JCLC        IS JOBNAME VALID?             *HMD 06/81*
         BNE   TILT2          NO, TELL HIM                  *HMD 06/81*
*** LEAVE EXECUTED INSTR WITHIN ADDRESSIBILITY RANGE        *HMD 06/81*
JCLC     CLC   QLOGON(1),JQEJNAME    CHECK FOR USERID MATCH *HMD 06/81*
***********************************************************************
*                                                                     *
*   READ JCT AND IOT                                                  *
*                                                                     *
***********************************************************************
READJCT  ST    R3,QCJQEA      SAVE THE ADDRESS
         MVC   QCTRAK,JQETRAK DISK ADDR OF JCT
         DROP  R3
         L     R3,QCJCTA      ADDR OF IOAREA FOR JCT
         LR    R1,R3          PARM FOR READSPC
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
         USING JCTDSECT,R3    BASE REG FOR JCT
         MVC   QPJOBID,JCTJBKEY JOB IDENTIFICATION
         MVC   QCTRAK,JCTIOT  DISK ADDR OF IOT
         DROP  R3
         L     R1,QCIOTA      ADDR OF IOAREA FOR IOT
         L     R15,=V(READSPC) ROUTINE TO READ HASPACE
         BALR  R14,R15        GO TO IT
STOP     QSTOP
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM1 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM1(1) CONVERT TO BINARY
         LTORG
JCT      EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $JQE
         $JCT
         $JQT
         QCOMMON
WORK     DSECT
         DS    72C
CONVERT  DS    D
         END
