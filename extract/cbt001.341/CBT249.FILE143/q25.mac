SYSOUT   QSTART 'QUEUE COMMAND - MANIPULATE SYSOUT'
         USING QCKPT,R10
         L     R10,QVCKPT
         USING QDISPLAY,R9
         L     R9,QVDSPL
         USING WORK,R13
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT AND IOT                       *
*                                                                     *
***********************************************************************
FJOB     LA    R1,8               MAX USER ID LENGTH        *CDG  3/82*
         L     R14,16             CVT                       *CDG  3/82*
         L     R14,0(R14)         TCB                       *CDG  3/82*
         L     R14,12(R14)        ASCB                      *CDG  3/82*
         L     R14,176(R14)       DOES THE CURRENT USER ID  *CDG  3/82*
         LA    R15,QPARM1           MATCH THE JOBNAME ?     *CDG  3/82*
CHKLOOP  CLI   0(R14),C' '        END OF USER ID ?          *CDG  3/82*
         BE    FJOBX              IF SO GO DO PROCESSING    *CDG  3/82*
         CLC   0(1,R14),0(R15)    CHECK FOR JOB NAME MATCH  *CDG  3/82*
         BNE   CHKAUTH            IF ID NOT IN JNAME, OPER? *CDG  3/82*
         LA    R14,1(R14)         NEXT ID CHAR              *CDG  3/82*
         LA    R15,1(R15)         NEXT JOBNAME CHAR         *CDG  3/82*
         BCT   R1,CHKLOOP         DO IT AGAIN               *CDG  3/82*
         B     FJOBX              IF ALL MATCH, GO PROCESS  *CDG  3/82*
CHKAUTH  TM    QXAUTH,X'02'       IF JNAME AND ID NO MATCH  *CDG  3/82*
         BNO   TILTID             TELL THE GUY              *CDG  3/82*
FJOBX    L     R15,=V(FINDJOB)    ADDR OF MODULE TO FIND JOB
         BALR  R14,R15            GO TO IT
         USING JCTDSECT,R4        BASE REG FOR JCT
         L     R4,QCJCTA          A(JCT)
***********************************************************************
*                                                                     *
*   VALIDATE THE FUNCTION CODE                                        *
*                                                                     *
***********************************************************************
         LH    R1,QCODEH          GET FUNCTION CODE
         CH    R1,=H'8'           ?/VALID FUNCTION CODE
         BH    STOP               NO. EXIT
***********************************************************************
*                                                                     *
*   BRANCH TO THE APPROPIATE PROCESSOR                                *
*                                                                     *
***********************************************************************
         LA    R7,SSOBHDR         ADDR FOR SUBSYSTEM OPTION BLOCK
         USING SSOB,R7
         B     *+4(R1)
         B     DELETE             0   DELETE REQUEST
         B     REQUEUE            4   REQUEUE REQUEST
         B     CANCEL             8   CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS CANCEL REQUEST                                            *
*                                                                     *
***********************************************************************
CANCEL   LA    R5,SSCSBGN         A(CANCEL PARAMETER LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB.
         MVC   SSOBFUNC,=AL2(SSOBCANC)  SET THE FUNCTION
         XC    SSCSBGN(SSCSIZE),SSCSBGN CLEAR PARM LIST
         MVC   SSCSLEN,=AL2(SSCSIZE) SET LENGTH OF CANCEL PARM LIST
         CLI   QPARM2,C'P'        ?/PURGE THE OUTPUT
         BNE   CAN000             NO. DON'T SET THE FLAG
         OI    SSCSFLGS,SSCSCOUT  YES
CAN000   MVC   SSCSJOBN,JCTJNAME  JOBNAME
         MVC   SSCSJOBI,JCTJOBID  JES2 JOB ID
         MVC   SSCSDIMP,=H'16'
         LA    R5,SSCSJOBI        A(JES2 JOB ID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO IMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
         L     R1,SSOBRETN        GET RC FOR CANCEL FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  CANCEL COMPLETED
         B     TILTNOJB           4 -  JOBNAME NOT FOUND
         B     TILTBADI           8 -  INVALID JOBNAME/JOB ID COMB.
         B     TILTNCAN           12 - JOB NOT CANCELLED - DUP JOBN
         B     TILTMALL           16 - STATUS ARRAY TOO SMALL
         B     TILTOUTP           20 - JOB NOT CANCELLED - ON OUTPUT Q
         B     TILTYNTX           24 - JOB ID WITH INVALID SYNTAX
         B     TILTICAN           28 - INVALID CANCEL REQUEST
***********************************************************************
*                                                                     *
*   PROCESS REQUEUE REQUEST                                           *
*                                                                     *
***********************************************************************
REQUEUE  LA    R5,SSSOBGN         A(REQUEUE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSOSETC  USE SSSOCLAS AS DISP
         OI    SSSOUFLG,SSSORLSE  RELEASE ALL SELECTED DATA SETS
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD SYSOUT DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME IS PRESENT
         OI    SSSOFLG1,SSSOSJBI  JOB ID IS PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JOB ID
         CLC   QLNG2,=H'0'        WAS PARM2 SPECIFIED?      *HMD 06/81*
         BE    TILTNCLS           NO, NEWCLASS IS REQUIRED  *HMD 06/81*
         MVC   SSSOCLAS,QPARM2    NEWCLASS
         CLC   QLNG3,=H'0'        WAS PARM3 SPECIFIED(DEST) *HMD 06/81*
         BE    REQRTN1            NO DEST SPECIFIED         *HMD 06/81*
         OI    SSSOUFLG,SSSOROUT  INDICATE DEST SPECIFIED   *HMD 06/81*
         MVC   SSSODEST,QPARM3    MOVE IN DESTINATION       *HMD 06/81*
REQRTN1  LA    R5,SSSOJOBI        A(JES JOB ID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN
CHKSORC  L     R1,SSOBRETN        GET RETURN CODE FOR SYSOUT FUNCTION
         B     *+4(R1)
         B     FUNCTOK            0 -  SYSOUT COMPLETED
         B     TILTEODS           4 -  NO MORE DS TO SELECT
         B     TILTNJOB           8 -  JOB NOT FOUND
         B     TILTINVA           12 - INVALID SEARCH ARGUMENTS
         B     TILTUNAV           16 - UNABLE TO PROCESS NOW
         B     TILTDUPJ           20 - DUPLICATE JOB NAMES
         B     TILTINVJ           24 - INVALID JOBN/JOBID COMBO
         B     TILTIDST           28 - INVALID DEST SPECIFIED
***********************************************************************
*                                                                     *
*   PROCESS DELETE  REQUEST                                           *
*                                                                     *
***********************************************************************
DELETE   LA    R5,SSSOBGN         A(DELETE SYSOUT PARM LIST)
         BAL   R6,INITSSOB        GO INITIALIZE THE SSOB
         MVC   SSOBFUNC,=AL2(SSOBSOUT) INDICATE SYSOUT IS THE FUNCTION
         XC    SSSOBGN(SSSOSIZE),SSSOBGN CLEAR PARM LIST
         MVC   SSSOLEN,=AL2(SSSOSIZE) SET LENGTH OF SYSOUT PARM LIST
         OI    SSSOUFLG,SSSODELC  INDICATE DELETE REQUEST
         OI    SSSOFLG1,SSSOHLD   SELECTION INCLUDES HELD DS
         OI    SSSOFLG1,SSSOSJBN  JOB NAME PRESENT
         OI    SSSOFLG1,SSSOSJBI  JES2 JOB ID PRESENT
         OI    SSSOFLG2,SSSOCTRL  PROCESSING COMPLETED
         MVC   SSSOJOBN,JCTJNAME  JOBNAME
         MVC   SSSOJOBI,JCTJOBID  JES2 JOBID
         LA    R5,SSSOJOBI        A(JES2 JOBID)
         BAL   R6,FIXJOBID        ENSURE JOBID HAS NO EMBEDDED BLANKS
         BAL   R6,VERREQ          VERIFY THE REQUEST
         BAL   R6,CALLSSI         GO CALL SUBSYSTEM INTERFACE RTN.
         B     CHKSORC            GO CHECK RC
***********************************************************************
*                                                                     *
*   INITIALIZE THE SUBSYSTEM OPTION BLOCK (SSOB)                      *
*                                                                     *
*        R5 - ADDRESS OF FUNCTION PARM LIST                           *
*        R6 - RETURN ADDRESS                                          *
*        R7 - A(SSOB)                                                 *
*                                                                     *
***********************************************************************
INITSSOB XC    SSOBEGIN(SSOBHSIZ),SSOBEGIN CLEAR THE SSOB
         MVC   SSOBID,=C'SSOB'
         MVC   SSOBLEN,=AL2(SSOBHSIZ) LENGTH OF SSOB HEADER
         ST    R5,SSOBINDV        FUNCTION DEPENDENT AREA POINTER
         ST    R7,SSOBPTR         SAVE ADDR OF SSOB
         OI    SSOBPTR,X'80'      REQUIRED FOR IEFSSREQ INTERFACE
         BR    R6
***********************************************************************
*                                                                     *
*   REMOVE EMBEDDED BLANKS IN JES2 JOB ID                             *
*                                                                     *
***********************************************************************
FIXJOBID LA    R8,5               MAX LENGTH OF SCAN
         LA    R5,3(R5)           START LOCATION FOR SCAN
FIX000   CLI   0(R5),C' '         ?/EMBEDDED BLANK
         BNE   FIX010             NO. CONTINUE WITH THE SCAN
         MVI   0(R5),C'0'         YES. REPLACE WITH 0
FIX010   LA    R5,1(R5)           POINT TO NEXT BYTE
         BCT   R8,FIX000          GO DO IT AGAIN
         BR    R6                 ALL OVER. RETURN TO CALLER
***********************************************************************
*                                                                     *
*   TELL THE USER WHAT HE IS ABOUT TO DO                              *
*                                                                     *
***********************************************************************
VERREQ   MVC   VCLEAR,WARNING     MOVE IN THE WARNING MSG
         MVC   VJOBN(8),JCTJNAME
         MVC   VJOBID(8),JCTJOBID
         LR    R2,R6              SAVE RETURN ADDR
         LA    R5,VJOBID          A(JES JOB ID)
         BAL   R6,FIXJOBID        INSURE NO IMBEDDED BLANKS
         LR    R6,R2              RESTORE RETURN ADDR
         CLI   QSUBNAME,C'D'      ?/DELETE COMMAND
         BNE   VER000
         MVC   VCMD(6),=C'DELETE'
         B     VER020
VER000   CLI   QSUBNAME,C'C'      ?/CANCEL COMMAND
         BNE   VER010
         MVC   VCMD(6),=C'CANCEL'
         CLC   QLNG2,=H'5'           LENGTH OF 5?           *HMD 06/81*
         BNE   VER0CK                NO, CHECK FOR P        *HMD 06/81*
         CLC   QPARM2(5),=C'PURGE'   PURGE SPECIFIED?
         BE    VER00X
VER0CK   CLC   QLNG2,=H'1'           WAS "P" SPECIFIED?     *HMD 06/81*
         BNE   VERTILTX              NO, TILT               *HMD 06/81*
         CLI   QPARM2,C'P'           WAS CHARACTER "P"      *HMD 06/81*
         BNE   VERTILTX              NO, TILT               *HMD 06/81*
VER00X   MVC   VACTION(5),QPARM2
         B     VER020
***                                                         *HMD 06/81*
VER010   MVC   VCMD(7),=C'REQUEUE'                          *HMD 06/81*
         MVC   VACTION,RQACT1     MOVE IN REQUEUE ACTION 1  *HMD 06/81*
         MVC   VCLASS(1),QPARM2                             *HMD 06/81*
         CLC   QLNG3,=H'0'        DEST SPECIFIED?           *HMD 06/81*
         BE    VER020             NO, DO NOT DISPLAY        *HMD 06/81*
         MVC   VACTION1,RQACT2    MOVE IN REQUEUE ACTION 2  *HMD 06/81*
         LH    R15,QLNG3          GET LENGTH OF PARM3       *HMD 06/81*
         BCTR  R15,0              DOWN 1 FOR EXECUTE        *HMD 06/81*
         EX    R15,MVCDEST        MOVE THE DESTINATION      *HMD 06/81*
         LA    R15,VDEST+1(R15)   POINT TO END              *HMD 06/81*
         MVI   0(R15),C')'        CLOSE THE PARENTHESIS     *HMD 06/81*
***                                                         *HMD 06/81*
VER020   MVC   QDMLNG,=H'0'       TELL DISPLAY TO PRINT IT NOW
         L     R15,=V(DISPLAY)    A(MODULE TO DISPLAY THE MESSAGE)
         BALR  R14,R15            GO DISPLAY THE WARNING
         BR    R6                 RETURN TO THE CALLER
MVCDEST  MVC   VDEST(1),QPARM3    MOVE DESTINATION          *HMD 06/81*
***********************************************************************
*                                                                     *
*   INTERFACE TO THE SUBSYSTEM                                        *
*                                                                     *
***********************************************************************
CALLSSI  L     R2,CVTPTR          A(CVT)                    *HMD 11/80*
         L     R2,CVTJESCT-CVT(R2)   A(JESCT)               *HMD 11/80*
         RWHOLE AUTHON            ** AUTHORIZE ON THE FLY **
         MODESET MODE=SUP         GET SUPER
         LA    R1,SSOBPTR         ADDR OF PTR TO SSOB
         L     R15,20(R2)         A(JESSSREQ)
         BALR  R14,R15
         LR    R2,R15             SAVE RETURN CODE
         MODESET MODE=PROB        BACK TO NORMAL
         RWHOLE AUTHOFF           ** TAKE AWAY AUTHORIZATION **
         B     *+4(R2)
         BR    R6                 0 -  SUCCESSFUL INSTRUCTION
         BR    R6                      DUMMY INSTRUCTION
         B     TILTNSUP           4 -  SS DOESN'T SUPPORT THIS FUNCTION
         B     TILTNTUP           8 -  SS EXIST, BUT IS NOT UP
         B     TILTNOSS           12 - SS DOES NOT EXIST
         B     TILTDIST           16 - FUNCTION NOT SUPPORTED
         B     TILTLERR           20 - LOGICAL ERROR
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
STOP     QSTOP
FUNCTOK  QTILT ' *** COMMAND SUCCESSFULLY PROCESSED ***'
***********************************************************************
*                                                                     *
*   ERROR MESSAGES                                                    *
*                                                                     *
***********************************************************************
VERTILTX QTILT ' *** INVALID CANCEL SYNTAX - SPECIFY ''PURGE'' OR ''P''X
                TO CANCEL OUTPUT ***'                       *HMD 06/81*
TILTNSUP QTILT ' *** QUEUE LOGIC ERROR -- RC =4  FROM SSI ***'
TILTID   QTILT ' *** USE A JOBNAME THAT STARTS WITH YOUR USER ID ***'
TILTNCLS QTILT ' *** NEWCLASS MUST BE SPECIFIED FOR REQUEUE ***'
TILTNTUP QTILT ' *** JES2 IS NOT UP ***'
TILTNOSS QTILT ' *** QUEUE LOGIC ERROR -- RC =12 FROM SSI ***'
TILTDIST QTILT ' *** DISASTROUS ERROR DURING PROCESSING ***'
TILTLERR QTILT ' *** QUEUE LOGIC ERROR -- RC =20 FROM SSI ***'
TILTNOJB EQU   *
TILTNJOB QTILT ' *** JOBNAME NOT FOUND ***'
TILTBADI EQU   *
TILTINVJ QTILT ' *** INVALID JOBNAME/JOB ID COMBINATION ***'
TILTNCAN EQU   *
TILTDUPJ QTILT ' *** DUPLICATE JOBNAME AND NO JOBID GIVEN ***'
TILTEODS QTILT ' *** JOB HAS NO HELD DATA SETS ***'
TILTICAN QTILT ' *** CAN''T CANCEL YOUR TSO SESSION OR A STARTED TASK *X
               **'
TILTOUTP QTILT ' *** JOB NOT CANCELLED - JOB ON OUTPUT QUEUE ***'
TILTMALL EQU   *
TILTYNTX EQU   *
TILTINVA EQU   *
TILTUNAV EQU   *
TILTUNKN QTILT ' *** QUEUE LOGIC ERROR - CONTACT YOUR SYSTEMS PROGRAMMEX
               R ***'                                       *HMD 06/81*
TILTIDST QTILT ' *** INVALID DESTINATION GIVEN FOR REQUEUE ***'
***********************************************************************
*                                                                     *
*   MISCELLANEOUS NUTS, BOLTS, ETC.                                   *
*                                                                     *
***********************************************************************
WARNING  DS    0CL80
         DC    CL21' *** '
         DC    CL1'('
         DC    CL8' '
         DC    CL1')'
         DC    CL7' '
         DC    CL14' '                                      *HMD 06/81*
         DC    CL28' ==> PRESS ENTER IF OK  ***'
RQACT1   DC    CL6'NEW( )'                                  *HMD 06/81*
RQACT2   DC    CL14' DEST(        '                         *HMD 06/81*
         LTORG
JCT      EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $TAB
         $JCT
         $IOT
         $PDDB
         $CVT     LIST=YES                                  *HMD 11/80*
         IEFJSSOB (SO,CS),CONTIG=YES
         QCOMMON
         ORG   QDHLINE
VCLEAR   DS    0CL80                                        *HMD 06/81*
         DS    CL5                                          *HMD 06/81*
VCMD     DS    CL8                                          *HMD 06/81*
VJOBN    DS    CL9                                          *HMD 06/81*
VJOBID   DS    CL10                                         *HMD 06/81*
VACTION  DS    CL6                                          *HMD 06/81*
VACTION1 DS    CL14                                         *HMD 06/81*
         DS    CL28                                         *HMD 06/81*
VCLASS   EQU   VACTION+4                                    *HMD 06/81*
VDEST    EQU   VACTION1+6                                   *HMD 06/81*
WORK     DSECT
FILLER   DS    CL512              BIG FILLER
SSOBPTR  DS    F
SSOBHDR  DS    CL140
         END
