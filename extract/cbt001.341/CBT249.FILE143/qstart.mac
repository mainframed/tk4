         MACRO
&NAME    QSTART &TITLE,&MAIN=NO,&TYPE=NORMAL
&NAME    START 0
***********************************************************************
*                                                                     *
*   REGISTER USAGE TABLE                                              *
*                                                                     *
***********************************************************************
R0       EQU   0              TEMP WORK
R1       EQU   1              TEMP WORK
R2       EQU   2              WORK REG
R3       EQU   3              WORK REG
R4       EQU   4              WORK REG
R5       EQU   5              WORK REG
R6       EQU   6              WORK REG
R7       EQU   7              WORK REG
R8       EQU   8              WORK REG
R9       EQU   9              ADDRESS OF COMMON SUB-AREA
R10      EQU   10             ADDRESS OF COMMON SUB-AREA
R11      EQU   11             ADDRESS OF COMMON VECTOR TABLE
R12      EQU   12             BASE REGISTER
R13      EQU   13             SAVE AREA AND TEMPORARY WORK AREA
R14      EQU   14             TEMP WORK
R15      EQU   15             TEMP WORK
.**********************************************************************
.*                                                                    *
.*  GLOBAL FLAG DEFINITIONS                                           *
.*                                                                    *
.**********************************************************************
.**********************************************************************
         GBLA  &QLEVEL        VERSION LEVEL OF JES2 CHECKPOINT
&QLEVEL  SETA  3              CURRENT LEVEL                 *HMD 12/79*
.* &QLEVEL=1 IS JES2 4.1 BEFORE THE PERFORMANCE PTF
.* &QLEVEL=2 IS JES2 4.1 AFTER PTF AZ20010
.* &QLEVEL=3 IS JES2 4.1 AFTER PTF AZ27300 OR FOR NJE 3.0   *HMD 11/80*
.**********************************************************************
         GBLB  &QPFK          PFK KEY SUPPORT SWITCH        *HMD 12/79*
&QPFK    SETB  1              YES, WE WILL SUPPORT PFKEYS   *HMD 12/79*
.* &QPFK=0   SELECTS NO PFK SUPPORT                         *HMD 12/79*
.* &QPFK=1   SELECTS PFK SUPPORT                            *HMD 12/79*
.**********************************************************************
         GBLB  &NJE           NJE RELEASE 3.1 SUPPORT   NJE *HMD 07/80*
&NJE     SETB  1              YES, WE WILL SUPPORT NJE  NJE *HMD 11/80*
.* &NJE=0    SKIP CODE TO SUPPORT NJE RELEASE 3.0       NJE *HMD 07/80*
.* &NJE=1    GEN  CODE TO SUPPORT NJE RELEASE 3.0       NJE *HMD 07/80*
.**********************************************************************
.**********************************************************************
.* INSURE THAT QLEVEL = 3 FOR NJE 3.0 SUPPORT           NJE *HMD 11/80*
         AIF   (NOT &NJE).OK
         AIF   (&QLEVEL EQ 3).OK
         MNOTE 16,'** ERROR: &&QLEVEL=&QLEVEL INVALID FOR NJE 3.0 **'
         MEXIT
.**********************************************************************
.OK      ANOP
***********************************************************************
*                                                                     *
*   THE QUEUE COMMAND WAS WRITTEN FOR YOUR AMUSEMENT AND AMAZEMENT BY *
*     THE INTERGALACTIC MESSIANIC INDIVIDUAL GROUP THERAPY DIVISION   *
*     OF TRW SYSTEMS, 1 SPACE PARK, REDONDO BEACH, CA. 90278          *
*   PLEASE ADDRESS ANY COMMENTS, SUGGESTIONS, COMPLAINTS, OR THREATS  *
*     TO ANDY ZIDE (R3/1040).                                         *
*                                                                     *
*   THE QUEUE COMMAND WAS INSTALLED AN MODIFIED AT GTE DATA SERVICES  *
*     BY HOWARD M. DEAN SYSTEMS SUPPORT UNIT (SSU). PLEASE ADDRESS    *
*     ANY COMMENTS AND COMPLAINTS TO THE ABOVE AT 821-0511 EXT. 330.  *
*     ADDRESS - 4750 LINCOLN BLVD. MARINA DEL REY, CA.                *
*   CHANGES BY HOWARD DEAN ARE MARKED ---> *HMD MM/YY*  IN COL 61-71  *
*                                                                     *
***********************************************************************
         AIF   ('&TYPE' EQ 'NORMAL').GO
         MEXIT
.GO      ANOP
&NAME    TITLE &TITLE
         USING *,R12          BASE REGISTER
         USING QCOMMON,R11    ACCESS TO COMMON VECTOR TABLE
         STM   R14,R12,12(R13) STANDARD REGISTER SAVE
         LR    R12,R15        LOAD BASE REG
         B     *+28           BRANCH AROUND IDENTIFIER
         DC    CL8'&NAME'     MODULE IDENTIFIER
         DC    CL8'&SYSDATE'  ASSEMBLY DATE
         DC    CL8' &SYSTIME'  ASSEMBLY TIME
         AIF   ('&MAIN' EQ 'YES').MAINYES
         LR    R15,R13        RETAIN SAVE AREA ADDR
         LA    R13,72(R13)    POINT TO NEXT SAVE AREA
         AGO   .MAINNO
.MAINYES ANOP
         LR    R2,R1          SAVE PARAMETER REGISTER
         GETMAIN R,LV=2048    GET STORAGE FOR SAVEAREA      *HMD 01/81*
         ST    R1,8(R13)      FORWARD POINTER
         ST    R13,4(R1)      BACKWARD POINTER
         LR    R13,R1         MOVE ADDR TO R13              *HMD 12/79*
         MEXIT
.MAINNO  ANOP
         ST    R13,8(R15)     FORWARD POINTER
         ST    R15,4(R13)     BACKWARD POINTER
         MEND
