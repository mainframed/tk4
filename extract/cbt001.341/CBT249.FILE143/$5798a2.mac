         EJECT                                                  ITLALFA
****************************************************************ITLALFA
****************************************************************ITLALFA
**                                                            **ITLALFA
**  THIS PORTION OF THE PROGRAM WAS ADDED TO CALL THE BLOCK   **ITLALFA
**  HEADER ROUTINE (IEFSD095) TO CREATE A PAGE SEPARATOR      **ITLALFA
**  WITH THE TSO USERID.  FOR ADDITIONAL STATEMENTS ADDED     **ITLALFA
**  IN SUPPORT OF THIS ROUTINE, DO:  FIND 'ITLALFA'           **ITLALFA
**                                                            **ITLALFA
**                                   LESLIE ALNAS 3/26/82     **ITLALFA
**                                                            **ITLALFA
****************************************************************ITLALFA
****************************************************************ITLALFA
         SPACE 2                                                ITLALFA
$R0      EQU   1                    WORK REGISTER               ITLALFA
$R1      EQU   1                    WORK REGISTER               ITLALFA
$R2      EQU   2                    LOCATION OF ICBTSOID FIELD  ITLALFA
$R3      EQU   3                    LOCATION OF COUNTER FIELD   ITLALFA
$R4      EQU   4                    BLOCK HEADER BUILD AREA     ITLALFA
$R5      EQU   5                    BLOCK HEADER RETURN REG.    ITLALFA
$R6      EQU   6                    CALL ROUTINE STOP COUNT     ITLALFA
$R7      EQU   7                    CALL ROUINTE COUNTER        ITLALFA
$R14     EQU   14                   ASEND RETURN REGISTER       ITLALFA
$R15     EQU   15                   TEST CODES WORK REGISTER    ITLALFA
         B     $BLKHDR              BRANCH AROUND CONSTANTS     ITLALFA
         SPACE 2                                                ITLALFA
*  DATA AREAS USED BY THE BLOCK HEADER ROUTINE                  ITLALFA
         SPACE 2                                                ITLALFA
$LENGTH  DC    F'7'                 LENGTH OF TSOID             ITLALFA
$SENDTOP DC    X'F5C90C5C5C'        COMMAND/WCC/FF/*/*          ITLALFA
         SPACE 2                                                ITLALFA
$BLKHDR  DS    0H                                               ITLALFA
         STM   $R2,$R7,@SA00003+8   SAVE REGISTERS BEFORE ME    ITLALFA
         L     $R4,AREA@            SEND WORK AREA              ITLALFA
         MVC   0(5,$R4),$SENDTOP    MOVE F5/C9/0C/*/*  PREFIX   ITLALFA
         MVC   5(93,$R4),4($R4)     REPEAT CHAR 93 TIMES        ITLALFA
         LA    $R4,98(,$R4)         POINT PAST ASTERISKS        ITLALFA
         MVI   0($R4),X'15'         NEW LINE COMMAND            ITLALFA
         MVC   1(3,$R4),0($R4)      PUT THREE MOVE NL'S         ITLALFA
         LA    $R4,4(,$R4)          POINT PAST 4 NL'S           ITLALFA
         L     $R2,ICB@             LOAD ICB DATA AREA          ITLALFA
         LA    $R2,ICBTSOID(,$R2)   TSO USERID ADDRESS          ITLALFA
         LA    $R3,@SA00003         COUNT FIELD SAVE AREA       ITLALFA
         LA    $R6,13               CALL ROUTINE STOPPER        ITLALFA
         LA    $R7,1                LOAD STARTING LINE COUNT    ITLALFA
         ST    $R7,@SA00003         INITIAL LINE COUNT VALUE    ITLALFA
$NEXTBLK EQU   *                                                ITLALFA
         MVI   0($R4),C' '          MOVE IN A BLANK             ITLALFA
         MVC   1(96,$R4),0($R4)     CLEAR THE OUTPUT AREA       ITLALFA
         BAL   $R5,$BLKRTN          GO CALL BLOCK HDR ROUTINE   ITLALFA
         CR    $R6,$R7              IS THIS THE LAST LINE?      ITLALFA
         BNE   $NEXTBLK             GO BLOCK THE NEXT LINE      ITLALFA
         MVI   0($R4),X'15'         NEW LINE COMMAND            ITLALFA
         MVC   1(50,$R4),0($R4)     SKIP REMAINDER OF PAGE      ITLALFA
         B     $SENDBLK             GO SEND THE BLOCK HEADER    ITLALFA
$BLKRTN  EQU   *                                                ITLALFA
         CALL  IEFSD095,(($R2),($R3),($R4),$LENGTH)             ITLALFA
         MVI   96($R4),X'15'        PUT IN NEW LINE CHARACTER   ITLALFA
         LA    $R4,97(,$R4)         NEXT HDR LINE OUTPUT AREA   ITLALFA
         LA    $R7,1($R7)           ADD 1 TO LINE COUNT         ITLALFA
         ST    $R7,@SA00003         NEW LINE COUNT VALUE        ITLALFA
         BR    $R5                                              ITLALFA
$SENDBLK EQU   *                                                ITLALFA
         LA    $R1,1317             LENGTH OF SEND DATA         ITLALFA
         ST    $R1,BUFFRPOS         STORE NEW LENGTH            ITLALFA
         MVI   FRSTSEND,C'F'        SET FIRST SEND INDICATOR    ITLALFA
         MVI   #BADSEND,X'00'       CLEAR BADSEND INDICATOR     ITLALFA
         BAL   $R14,ASEND           GO SEND BLOCK HEADER PAGE   ITLALFA
         L     $R15,RTRNCODE        LOAD RETURN CODE            ITLALFA
         C     $R15,@CF00512        COMPARE RETURN CODE TO 4    ITLALFA
         BNL   $ENDBLK              GO TO END BLOCK HEADER      ITLALFA
         L     $R15,ICB@            ICB AREA ADDRESS            ITLALFA
         CLI   ICBCCODE($R15),8     AWAITING ACQUISITION?       ITLALFA
         BNE   $ENDBLK              GO TO END OR ROUTINE        ITLALFA
         MVI   ICBCCODE(@15),X'00'  RESET WAITING INDICATOR     ITLALFA
         L     $R2,DOCBPL@          DOCBPL AREA ADDRESS         ITLALFA
         L     $R3,RPL1@            RPL1 AREA ADDRESS           ITLALFA
         MODCB AM=VTAM,RPL=($R3),BRACKET=(NBB,NEB),MF=(G,($R2)) ITLALFA
         LTR   $R15,$R15            TEST RETURN CODE            ITLALFA
         BNZ   @RF00871             PUT OUT MESSAGE CPF579875   ITLALFA
$ENDBLK  EQU   *                                                ITLALFA
         LM    $R2,$R7,@SA00003+8   RESTORE REGISTERS AS BEFORE ITLALFA
****************************************************************ITLALFA
****************************************************************ITLALFA
**                                                            **ITLALFA
**       END OF CODE TO CREATE A BLOCK PRINT PAGE SEPARATOR   **ITLALFA
**                                                            **ITLALFA
****************************************************************ITLALFA
****************************************************************ITLALFA
