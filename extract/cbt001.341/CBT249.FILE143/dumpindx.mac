//ITLACDGA JOB (991000CG,337),'C.GRIFFIN',CLASS=S,NOTIFY=ITLACDG,
// MSGCLASS=T
//S1 EXEC ASMFCL
DUMPINDX CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
DCBOFLGS EQU   48
JFCBDSN  EQU   0                  OFFSET TO DSNAME IN JFCB
JFCBVOLS EQU   118                1ST BYTE OF JFCB VOLUME LIST
         EJECT
         PRINT NOGEN
         USING *,R15              BASE FOR INITIALIZATION ONLY
         STM   R14,R12,12(R13)    SAVE STARTING REGS
         ST    R13,SAVE+4         BACKWARD CHAIN
         MVC   8(4,R13),=A(SAVE)  FORWARD CHAIN
         CNOP  0,4                ALIGN FOR DS
         BAL   R13,SAVE+72        AROUND SAVE AREA AND LOAD R13
SAVE     DS    18F                SAVE AREA
         DROP  R15
         USING SAVE,R13           SAVE AREA AND BASE REGISTER1
         OPEN  (SYSUT2,INPUT)     DUMP INDEX DATA SET
         TM    SYSUT2+DCBOFLGS,X'10' DID IT OPEN ?
         BZ    EXITRC8            IF NOT RC=8
         RDJFCB SYSUT1            GET JFCB INFO FOR DUMP TAPE
         OPEN  (SYSUT1,INPUT)     DUMP TAPE
         TM    SYSUT1+DCBOFLGS,X'10' DID IT OPEN ?
         BZ    EXITRC8            IF NOT RC=8
         TIME  DEC                GET DATE
         ST    R1,W2              STORE DATE (00YYDDDF)
         TM    W2+1,01            IF ODD NOT LEAP.
         BO    NOLEAP             NOT LEAP
         TM    W2+1,X'12'         TEST FOR LEAP (VALID TILL 1999)
         BM    NOLEAP             1NOT LEAP
         MVI   MONTHTBL+5,2       SETUP FEB LEAP YEAR
NOLEAP   UNPK  MMDDYY+6(3),W2+1(2) UNPK YR
         XC    W1(6),W1           CLEAR YR FOR DAY RTN.
         CVB   R4,W1              GET DAY
         LA    R5,MONTHTBL-4      SET BACK PTR
MONLUPE  LA    R5,4(R5)           INCR THRU MON TBL
         SH    R4,0(R5)           DROP DOWN THRU MONTBL
         BH    MONLUPE            NOT YET, TRY AGAIN
         AH    R4,0(R5)           ADD BACK THE DAY
         CVD   R4,W1              MAKE
         OI    W2+3,X'0F'         THE DAY
         UNPK  MMDDYY+2(3),W2+2(2) PRINTABLE
         MVI   MMDDYY+5,C'/'      RESTORE SLASH
         MVC   MMDDYY(2),2(R5)    MOVE THE MONTH
         MVI   MMDDYY+2,C'/'      RESTORE SLASH
         XR    R3,R3              COUNT OF RECS IN DUMP INDEX
         LA    R4,TAPEDATA        DUMP INDEX DATA SET STORAGE
READINDX GET   SYSUT2,0(R4)       DUMP INDEX DATA SET TO STORAGE
         LA    R3,1(R3)           NUMBER OF RECS IN DUMP INDEX
         LA    R4,80(R4)          NEXT DATA RECORD GOES HERE
         B     READINDX           GET NEXT RECORD
SYSUT2EF CLOSE SYSUT2             DONE WITH INPUT
         CH    R3,=H'30'          DID WE GET ANY RECORDS ?
         BE    GETHEAD            IF SO GO GET THE HEADER
         LA    R4,TAPEDATA        AREA TO CLEAR
         LA    R5,2700            30 X 80
         XR    R7,R7              ZERO FROM LENGTH
         ICM   R7,B'1000',=C' '   BLANK FILL
         MVCL  R4,R6              CLEAR TAPE DATA
         LA    R4,TAPEDATA        AREA TO CLEAR
         LA    R6,10              USE R6 TO COUNT
         LA    R5,1               START WITH DUMP01
INITINDX CVD   R5,DWORK           GET TO DECIMAL
         UNPK  NUM(2),DWORK(8)    GET TO PRINTABLE
         OI    NUM+1,X'F0'        FIX THE C
         MVC   0(6,R4),DUMP       SET DUMPXX INTO RECORD
         LA    R4,240(R4)         3 RECORDS/TAPE
         LA    R5,1(R5)
         BCT   R6,INITINDX        INITIALIZE NEXT TAPE
GETHEAD  L     R5,=A(BUFFER)
         GET   SYSUT1,0(R5)       GET TAPE RECORD
         MVI   EMPTY,X'00'        GOT A RECORD
SYSUT1EF CLOSE SYSUT1             DONE WITH INPUT TAPE
         LA    R6,30              USE R6 TO COUNT
         LA    R4,TAPEDATA        FIND THE CORRECT ENTRY FOR THIS TAPE
FNDENTRY CLC   0(6,R4),JFCB+JFCBVOLS THIS TAPES VOLSER
         BE    GOTENTRY           IF EQ THEN THIS IS IT
         LA    R4,80(R4)          NEXT ENTRY
         BCT   R6,FNDENTRY        CHECK NEXT
         B     EXITRC8
GOTENTRY MVC   22(44,R4),JFCB+JFCBDSN 35 BYTES OF DSN
         CLI   EMPTY,X'FF'        ANY TAPE RECORDS
         BE    BLANKIT
         MVC   90(8,R4),4(R5)     MODULE NAME
         MVC   99(50,R4),20(R5)   DUMP TITLE
         MVC   179(50,R4),70(R5)  DUMP TITLE
         B     DODATE
BLANKIT  MVC   90(8,R4),=CL50' '
         MVC   99(50,R4),=CL50'*** SAVEDUMP OF AN EMPTY SYS1.DUMPXX DATX
               A SET ***'
         MVC   179(50,R4),=CL50' '
DODATE   MVC   13(8,R4),MMDDYY    PUT DATE INTO RECORD
         LA    R4,TAPEDATA        R4 INDEX INTO TAPEDATA
         LR    R5,R4              R5 LOOK FOR G00V00
         LA    R6,GENNUM          GENERATION NUMBER BY ENTRY
         LA    R7,10              DO THIS WHOLE THING 10 TIMES
LOOP1    LR    R5,R4              RECORD TO SEARCH
         LA    R8,70              SEARCH 70 POSITIONS
FINDGEN  CLC   0(2,R5),=CL2'.G'   FIRST PART OF G00V00
         BE    LOOKSOK            COULD BE WHAT WE WANT
NOGOOD   LA    R5,1(R5)           NEXT PLACE TO LOOK
         BCT   R8,FINDGEN         GO CHECK AGAIN
         B     NEXTONE            IF NOT FOUND
LOOKSOK  CLI   6(R5),C'V'         LAST PART OF G00V00
         BNE   NOGOOD             NO, GO CHECK MORE
         XC    DWORK(8),DWORK
         PACK  DWORK(8),2(4,R5)   GET GENERATION
         CVB   R9,DWORK
         STH   R9,0(R6)           SAVE GEN NUM
NEXTONE  LA    R4,240(R4)         NEXT INDEX
         LA    R6,2(R6)           NEXT GENERATION
         BCT   R7,LOOP1           DO NEXT ENTRY
         LA    R6,GENNUM          FIND LARGEST GENERATION
         LA    R7,10              DO 10 ENTRIES
         XR    R5,R5
FINDLARG CH    R5,0(R6)           THIS GENERATION THE LARGEST ?
         BH    CHKNXT             IF NOT CHECK THE NEXT
         LH    R5,0(R6)           MAKE THIS THE HIGEST
CHKNXT   LA    R6,2(R6)           NEXT ENTRY
         BCT   R7,FINDLARG        CHECK THE NEXT ONE
         LA    R4,TAPEDATA
         LA    R6,GENNUM          FIND LARGEST GENERATION
         LA    R7,10              DO 10 ENTRIES
         XR    R8,R8              FOR ICM
LOOP2    ICM   R8,B'0011',0(R6)   THIS GENERATION THE LARGEST ?
         BZ    BLANK              IF NOT CHECK THE NEXT
         LR    R9,R5              HIGH GENERATION
         SR    R9,R8              GET RELATIVE TO LARGEST GENERATION
         CVD   R9,DWORK
         UNPK  9(2,R4),DWORK(8)
         OI    10(R4),X'F0'
         MVI   11(R4),C')'
         CLC   9(2,R4),=CL2'00'
         BE    PLUS
         CLI   9(R4),C'0'
         BNE   TWO
         MVC   7(3,R4),=CL3' (-'
         B     NEXT
PLUS     MVC   7(3,R4),=CL3' (+'
         B     NEXT
BLANK    MVC   7(5,R4),=CL5'     '
         B     NEXT
TWO      MVC   7(2,R4),=CL2'(-'
NEXT     LA    R6,2(R6)           NEXT ENTRY
         LA    R4,240(R4)
         BCT   R7,LOOP2           CHECK THE NEXT ONE
         LA    R4,TAPEDATA
         LA    R6,30              FOR COUNTING
         OPEN  (SYSUT2,OUTPUT)    WRITE BACK DUMP INDEX DATA SET
WRTINDEX PUT   SYSUT2,0(R4)       WRITE A RECORD
         LA    R4,80(R4)          NEXT RECORD
         BCT   R6,WRTINDEX        DO NEXT RECORD
         CLOSE SYSUT2
EXIT     MVI   RCINSTR+3,X'00'    RC=0
EXITRC8  DS    0H
         L     R13,SAVE+4         PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)       RESTORE THE REGS
RCINSTR  LA    15,8
         BR    R14                BYE
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=(GM,PM),DSORG=PS,RECFM=FB,LRECL=80, X
               EODAD=SYSUT2EF
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,DSORG=PS,RECFM=U,DEVD=TA,        X
               BLKSIZE=8192,EODAD=SYSUT1EF,EXLST=EXITLIST
EXITLIST DC    X'07'              READ JFCB
         DC    AL3(JFCB)          WHERE TO PUT THE JFCB
JFCB     DS    CL176              JFCB
DUMP     DC    CL6'DUMP01'
         ORG   *-2
NUM      DS    CL2
         ORG
EMPTY    DC    X'FF'              NO TAPE DATA
DWORK    DS    D
         LTORG
MONTHTBL DS    0CL48
MTK      DC    H'31',C'01' JAN
         DC    H'28',C'02' FEB
         DC    H'31',C'03' MAR
         DC    H'30',C'04' APR
         DC    H'31',C'05' MAY
         DC    H'30',C'06' JUN
         DC    H'31',C'07' JLY
         DC    H'31',C'08' AUG
         DC    H'30',C'09' SEP
         DC    H'31',C'10' OCT
         DC    H'30',C'11' NOV
         DC    H'255',C'12' DEC
         DS    0D
W1       DS    F              WORKARE1
W2       DS    F              WORKAREA2
MMDDYY   DS    CL9
GENNUM   DC    10H'0'             GENERATION NUMBER
TAPEDATA DC    30CL80' '          SAPCE FOR 10 DUMP TAPES (3X10)
BUFFER   DS    12000C
         END
/*
//LKED.SYSLMOD DD DISP=SHR,DSN=ITL1.LINKLIB(DUMPINDX)
