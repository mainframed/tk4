JLOG     QSTART 'QUEUE COMMAND - LIST THE JOBLOG MESSAGES FOR A JOB'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15         GO TO IT
***********************************************************************
*                                                                     *
*   DETERMINE IF JOBLOG IS AVAILABLE                                  *
*                                                                     *
***********************************************************************
         USING PDBDSECT,R2    BASE REG FOR PDDB             *HMD 10/80*
         USING IOTDSECT,R3    BASE REG FOR IOT              *HMD 10/80*
         L     R3,QCIOTA      LOAD BASE REG                 *HMD 10/80*
NEXTIOT  LR    R4,R3          BASE OF IOT                   *HMD 10/80*
         A     R4,IOTPDDBP    OFFSET BEYOND LAST PDDB       *HMD 10/80*
         LR    R2,R3          BASE OF IOT                   *HMD 10/80*
         A     R2,QCPDDB1     OFFSET TO FIRST PDDB IN JOT   *HMD 10/80*
FINDDS   CLC   PDBDSKEY,=H'2' IS THIS JOBLOG PDDB?          *HMD 10/80*
         BE    FOUNDDS        YES, CONTINUE.                *HMD 10/80*
         LA    R2,PDBLENG(R2) NO, LOOK AT NEXT PDDB         *HMD 10/80*
         CR    R2,R4          HAVE WE GONE PAST LAST PDDB?  *HMD 10/80*
         BL    FINDDS         NO, TRY AGAIN                 *HMD 10/80*
         B     TILT           JOBLOG IS NOT AVAILABLE       *HMD 10/80*
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
FOUNDDS  DS    0H             ALLOW JLOG LIST FOR EXEC JOB  *HMD 02/81*
         MVC   QPOFFSET,=H'0' PRINT OFFSET FOR EACH RECORD
         MVC   QPDSID,=H'2'   DSID OF DATASET TO BE PRINTED
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
TILT     QTILT '*** JOBLOG IS NOT AVAILABLE ***'
         LTORG
JCT      EQU   0                                            *HMD 10/80*
BUFSTART EQU   0                                            *HMD 10/80*
BUFDSECT EQU   0                                            *HMD 10/80*
         $JQE      ,
         $TAB      ,                                        *HMD 10/80*
         $JCT      ,                                        *HMD 10/80*
         $IOT      ,                                        *HMD 10/80*
         $PDDB     ,                                        *HMD 10/80*
         QCOMMON
         END
