         TITLE 'UNION OIL IKJEFF10'
***********************************************************************
*    TSO SUBMIT EXIT                                                  *
*                                                                     *
*       FUNCTION:                                                     *
*                                                                     *
*                CHECK JOB CARD ACCOUNT NUMBER.                       *
*                ACCOUNT NUMBER IS AN 8 CHARACTER FIELD WITH THE      *
*                FOLLWOING FORMAT...                                  *
*                                                                     *
*                   (SSNNNNII)                                        *
*                                                                     *
*                    SS         SUBSIDIARY CODE                       *
*                      0000     UNUSED                                *
*                    AFENNN     AFE NUMBER (1ST 2 DIGS ARE SUBSID)    *
*                          II   USER INITIALS (ALPHA)                 *
*                                                                     *
*                    SUBSIDIARY 99 SPECIAL CASE                       *
*                                                                     *
*                   (99N000II)                                        *
*                    99         DP INTERNAL                           *
*                      N        1=SYSTEMS,2=APP,3=OPERATORS,4=OTHERS  *
*                       000     UNUSED                                *
*                          II   USERS INITIALS (ALPHA)                *
*                                                                     *
* 8/01/81                                                             *
*     MODIFIED TO COMPARE FIRST 6 DIGITS OF ACCOUNT NUMBER.  PROGRAM  *
*     PREVIOUSLY JUST CHECKED 1ST TWO DIGITS, THE SUBSIDIARY CODE.    *
*     THE TABLE OF ACCOUNT NUMBERS WAS ALSO MODIFIED TO REFLECT THE   *
*     VALID ACCOUNT NUMBERS.                                          *
*                                                MIKE GALLANDER       *
*                                                                     *
*                                                OONG YOUN            *
***********************************************************************
*
IKJEFF10 CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         NOPR  0                        ZAP TO XR 15,15 (17FF)
         NOPR  0                        ZAP TO BR 14    (07FE)
         STM   R14,R12,12(R13)
         BALR  R2,0
         USING *,R2
         L     R3,0(R1)                 SAVE PARM LIST ADDR
         GETMAIN R,LV=72                GET A SAVE AREA
         ST    R13,4(R1)                FORWARD CHAIN
         ST    R1,8(R13)                BACKWARD CHAIN
         LR    R13,R1                   SET SA POINTER
         USING PARMLIST,R3
         L     R4,CONTROL               GET CONTROL SWITCHES POINTER
         L     R6,MESSAGE               WAS THERE A PREVIOUS MESSAGE
         LTR   R6,R6                    IF THERE IS AN ADDR.. YES
         BNZ   RETURN16                 IF SO LAST RC=8, THIS RC=16
         OI    0(R4),X'80'              ONLY NEED EXIT FOR JOB CARDS
         TM    2(R4),X'80'              IS IT A JOB CARD
         BO    JOBC                     GO PROCESS THE JOB CARD
         B     RETURN                   OTHERWISE RETURN
         TITLE 'IKJEFF10 - PROCESS JOB CARD'
JOBC     L     R5,USERID                GET USERID ADDR
*        CLC   0(7,R5),=CL7'ITLAMDG'      *-*-*-* REMOVE AFTER TESTING
*        BNE   RETURN                     *-*-*-* REMOVE AFTER TESTING
         TM    2(R4),X'01'              ONLY DO CARD#1 OF MULTI JOBC
         BO    RETURN
         L     R5,CURRENT               ADDR OF JOB CARD
         LA    R6,80-8-1                MAX LENGTH-SEQL-CONTCOL
         XR    R7,R7                    FOR IC OF OPERAND OFFSET
         IC    R7,1(R4)                 OPERAND OFFSET
         LTR   R7,R7                    CHECK FOR NOT ZERO
         BZ    RETURN                   A PROBLEM
         BCTR  R7,R0                    GET RELATIVE TO 0
         AR    R5,R7                    WHERE TO FIND THE ACCOUNT FIELD
         SR    R6,R7                    SEARCH LENGTH LESS OFFSET
         CLI   0(R5),C'('               IN PARENS ?
         BNE   NUMBER                   IF NOT DO NUMBER
         LA    R5,1(R5)                 SKIP IT
         BCTR  R6,R0                    REDUCE LENGTH
NUMBER   XR    R7,R7                    ZERO CHAR COUNTER
CLOOP    CLI   0(R5),C','               LOOK FOR END OF ACCOUNT FIELD
         BE    CHKACT                   GO CHECK ACCOUNT NUM
         CLI   0(R5),C')'               LOOK FOR END OF ACCOUNT FIELD
         BE    CHKACT                   GO CHECK ACCOUNT NUM
         CLI   0(R5),C' '               LOOK FOR END OF ACCOUNT FIELD
         BE    CHKACT                   GO CHECK ACCOUNT NUM
         LA    R5,1(R5)                 NEXT CHAR
         LA    R7,1(R7)                 GOT ONE MORE ACCOUNT DIGIT
         BCT   R6,CLOOP                 GO CHECK IT
CHKACT   CH    R7,=H'8'                 MUST BE 8 CHARS LONG
         BNE   BADGUY                   IF NOT SAY GOOD NIGHT
         SH    R5,=H'8'                 POINT TO START OF ACCOUNT NUM
         CLI   6(R5),C'A'               LAST 2 DIGS MUST BE ALPHA
         BL    BADGUY                   SAY GOOD NIGTH
         CLI   7(R5),C'A'                       "
         BL    BADGUY                           "
         CLI   6(R5),C'Z'                       "
         BH    BADGUY                           "
         CLI   7(R5),C'Z'                       "
         BH    BADGUY                           V
         LA    R6,TABLE
TSEARCH  CLC   0(6,R6),0(R5)            FIND 1ST 6 DIGITS OF ACCOUNT
         BE    CHK99                    IF FOUND A GOOD GUY
         LA    R6,6(R6)                 CHECK NEXT ENTRY
         CLI   0(R6),X'FF'              END OF TABLE
         BE    BADGUY                   IF SO NOT FOUND
         B     TSEARCH
CHK99    CLC   0(2,R5),=C'99'           INTERNAL CODE ?
         BNE   RETURN                   IF NOT NO MORE CHECKS
         CLI   2(R5),C'1'               3 DIG MUST BE 1-4
         BL    BADGUY                           "
         CLI   2(R5),C'5'                       "
         BH    BADGUY                           V
         B     RETURN
BADGUY   EQU   *
         GETMAIN R,LV=80,SP=4
         MVC   0(2,R1),=H'80'           SET LL
    MVC   2(78,R1),=CL78'ACCOUNT NUMBER INVALID. SUBMIT TERMINATED.'
         ST    R1,MESSAGE               SET MESSAGE ADDR IN PARM LIST
         B     RETURN8                  DO RETURN WITH RC=8
RETURN   L     R1,4(R13)
         LR    R13,R1
         L     R1,8(R13)
         LR    R6,R1
         FREEMAIN R,LV=72,A=(6)
         RETURN (14,12),RC=0
RETURN8  L     R1,4(R13)
         LR    R13,R1
         L     R1,8(R13)
         LR    R6,R1
         FREEMAIN R,LV=72,A=(6)
         RETURN (14,12),RC=8
RETURN16 L     R6,MESSAGE
         LTR   R6,R6
         BZ    NOFREE
         FREEMAIN R,LV=80,A=(6),SP=4
NOFREE   L     R1,4(R13)
         LR    R13,R1
         L     R1,8(R13)
         LR    R6,R1
         FREEMAIN R,LV=72,A=(6)
         RETURN (14,12),RC=16
         LTORG
*                            *
******************************
*                            *
*  ACCOUNT NUMBER TABLE      *
*                            *
******************************
*                            *
TABLE    DS    0F
         DC    C'541500'
         DC    C'522EX2'
         DC    C'522EX3'
         DC    C'522EX4'
         DC    C'522MO2'
         DC    C'622506'
         DC    C'552506'
         DC    X'FFFFFFFF'
*
PARMLIST DSECT
CURRENT  DS    F                        ADDR OF CURRENT STATEMENT
MESSAGE  DS    F                        ADDR OF MSG TO BE DISPLAYED
RESPONSE DS    F                        ADDR OF RESPONSE
USERID   DS    F                        ADDR OF USERID
CONTROL  DS    F                        ADDR OF CONTROL SWITCHES
*
*  BYTE ZERO SPECIFIES UNDER WHAT CONDITIONS SUBMIT WILL CALL EXIT
*
*    X... ....  CALL FOR JOB CARD
*    .X.. ....  EXEC
*    ..X. ....  DD
*    ...X ....  COMMAND
*    .... X...  NULL
*    .... .X..  RESERVED
*    .... ..X.  RESERVED
*    .... ...X  RESERVED
*
*  BYTE ONE (IF NON-ZERO) CONTAINS THE CARD COLUMN WHERE THE
*           OPERAND FIELD BEGINS - IN HEX
*
*  BYTE TWO SPECIFIES WHAT THE CURRENT STATEMENT IS
*
*    X... ....  JOB
*    .X.. ....  EXEC
*    ..X. ....  DD
*    ...X ....  COMMAND
*    .... X...  NULL
*    .... .X..  OPERAND TO BE CONTINUED
*    .... ..X.  STATEMENT TO BE CONTINUED
*    .... ...X  STATEMENT CONTINUATION
*
*  IF BIT 5 IS ON, BIT 6 MUST BE ON,
*  BUT BIT 6 MAY BE ON AND BIT 5 OFF
*
JOB      EQU   X'80'
EXEC     EQU   X'40'
DD       EQU   X'20'
COMMAND  EQU   X'10'
NULL     EQU   X'08'
CONTOPER EQU   X'04'
CONTSTMT EQU   X'02'
CONTATN  EQU   X'01'
*
*  BYTE THREE IS UNUSED
*
EXITUSE  DS    F                        FOR EXITS USE
         END   IKJEFF10
