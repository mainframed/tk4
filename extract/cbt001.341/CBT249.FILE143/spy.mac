         TITLE 'S P Y --  MVS CONSOLE SPY PROGRAM  --  VERSION 3.0'
         PRINT OFF
         MACRO
         SPYMAC &OPBIT=YES,&SP3=NO,&PASWRD=NO
***********************************************************************
*                                 S P Y                               *
*                               02/16/82   (MVS SP3)                  *
*                       OPERATOR CONSOLE MONITOR                      *
*     THIS PROGRAM DISPLAYS ALL ACTIVE OPERATORS CONSOLES ON          *
*     A 3270 TSO TERMINAL. SINCE THIS BUFFER CAN BE 35 LINES LONG,    *
*     IT MUST BE DISPLAYED IN TWO 'PAGES'. VARIOUS CONTROL            *
*     COMMANDS ARE AVAILABLE AND ARE DESCRIBED BELOW.                 *
*                                                                     *
*     COMMAND      DESCRIPTION                                        *
*                                                                     *
*        ?         DISPLAYS HELP FOR SPY                              *
*        1         SHIFT TO DISPLAY MODE 1                            *
*        2         SHIFT TO DISPLAY MODE 2                            *
*        B         BYE; END THE PROGRAM                               *
*        E         END; END THE PROGRAM                               *
*        F         FREEZE DISPLAY ON THE CURRENT PAGE                 *
*        R         RELEASE DISPLAY; SHOW ALTERNATING PAGES            *
*        WXX       AUTOMATICALLY REFRESH THE SCREEN                   *
*                  XX TIMES, THEN RETURN TO NORMAL MODE.              *
*                  IF XX IS NOT ENTERED, 30 ITERATIONS IS ASSUMED.    *
*                  IF XX = 0, TIMER WILL CONTINUE UNTIL INTERRUPT     *
*                  IS PRESSED.                                        *
*        CXX       SWITCH DISPLAY TO SYSTEM CONSOLE XX                *
*                  IF XX IS NOT A VALID CONSOLE, '1' IS ASSUMED       *
*        DXX       SET DELAY TIME BETWEEN REFRESHES TO XX TENTHS      *
*                  OF A SECOND.                                       *
*        L         LINK TO SWAP PROGRAM (MUST BE IN SAME LOAD MODULE  *
*                  AS SPY)                                            *
*     DISPLAY MODE 1 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 12 LINES                       *
*     DISPLAY MODE 2 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 22 LINES                       *
*                                                                     *
*     HITTING THE ATTENTION KEY WHILE IN TIMER MODE WILL CAUSE        *
*     THE TIMER TO BE RESET TO ZERO AND WAIT MODE TERMINATED.         *
*                                                                     *
*     SPY GIVES A TSO USER THE CAPABILITY TO ENTER SYSTEM AND JES     *
*     OPERATOR COMMANDS. THIS FACILITY IS PROTECTED BY A SIMPLE       *
*     3 CHARACTER PASSWORD (IN VARIABLE VIPWORD) TO PREVENT YOU       *
*     FROM ACCIDENTALLY ENTERING AN OPERATOR COMMAND. THE SYNTAX IS   *
*                                                                     *
*     JES COMMAND:  $...  ANY JES COMMAND ...                         *
*     OPER COMMAND: /...  ANY OPER COMMAND ...                        *
*                                                                     *
*     TYPING THE 3-LETTER VIP PASSWORD 'TOGGLES' THE VIP FLAG.        *
*     THE AUTHORIZATION LEVEL OF COMMANDS YOU CAN ISSUE DEPENDS ON    *
*     HOW YOUR INSTALLATION HAS SYSGENED ITS INTERNAL READERS.        *
*     THIS FACILITY WORKS BY DYNAMICALLY ALLOCATING AN INTERNAL       *
*     READER AND THROWING THE COMMAND THROUGH IT.                     *
*                                                                     *
*     SPY WAS DEVELOPED ON A 370/168 MVS RELEASE 3.7; IT WILL         *
*     PROBABLY *NOT* WORK ON ANYTHING BUT MVS...                      *
*                                                                     *
*                        STEVE LANGLEY                                *
*                        SOUTHERN CALIF. EDISON                       *
*                        ROSEMEAD, CALIF   91770                      *
*                        213 572-3521                                 *
*                                                                     *
*  CONDITIONAL ASSEMBLY... &OPBIT= YES OR NO                *CDG  1/82*
*  SPY NOW CHECKS FOR UADS OPER PRIVILEGE AND WILL NOT ALLOW*CDG  2/82*
*  USE WITHOUT THE OPER BIT ON.                             *CDG  2/82*
*                                                           *CDG  2/82*
*  CONDITIONAL ASSEMBLY... &SP3= YES OR NO                  *CDG  2/82*
*  WITH MVS SP3 THE CONSOLE BUFFERS HAVE BEEN MOVED TO      *CDG  1/82*
*  AN ADDRESS SPACE FOR THE CONSOLE. CROSS MEMORY SERVICES  *CDG  1/82*
*  MUST BE USED TO OBTAIN THE CONSOLE BUFFERS.              *CDG  1/82*
*                                                           *CDG  1/82*
*  CONDITIONAL ASSEMBLY... &PASWRD= YES OR NO               *CDG  1/82*
*  THE USE OF A PASSWORD TO ENTER COMMANDS IS NOW OPTIONAL  *CDG  1/82*
*                                                           *CDG  1/82*
* NOTE THAT THIS PROGRAM  REQUIRES THE FOLLOWING MACROS:    *CDG  1/82*
*                                                           *CDG  1/82*
*         CVT       FROM SYS1.AMODGEN                       *CDG  1/82*
*         IEECUCM   FROM SYS1.AMODGEN                       *CDG  1/82*
*         IEERDCM   FROM OPTIONAL MATERIAL (SEE NEXT NOTE)  *CDG  1/82*
*         IEETDCM   FROM OPTIONAL MATERIAL (SEE NEXT NOTE)  *CDG  1/82*
*         RWHOLE    FROM ITL1.MACLIB (AUTHORIZATION SVC INVOKER)  1/82*
*                                          (SEE NEXT NOTE)  *CDG  1/82*
*                                                           *CDG  1/82*
* NOTE THAT IEERDCM AND IEETDCM HAVE BEEN COPIED FRON SP3   *CDG  1/82*
* OPTIONAL SOURCE AND INCLUDED IN THIS DATA SET. THEY       *CDG  1/82*
* WERE ON OPTIONAL REEL #1 (SYM-01) FILE #1 (COMPONET AAPVT).CDG  1/82*
* THE SP3 VERSIONS OF THESE MACROS WORK ON SE2              *CDG  2/82*
* THE USER MACRO RWHOLE IS ALSO  INCLUDED.                  *CDG  2/82*
*                                                           *CDG  1/82*
*             ROY TATAR AND CHUCK GRIFFIN                   *CDG  1/82*
*             UNION OIL INTERNATIONAL                       *CDG  1/82*
*             461 SOUTH BOYLSTON STREET                     *CDG  1/82*
*             LOS ANGELES, CALIFORNIA                       *CDG  1/82*
*             213 977-7586                                  *CDG  1/82*
*                                                           *CDG  1/82*
*************************************************************CDG  1/82*
         EJECT
         LCLB  &OPER,&PASS,&CM                              *CDG  2/82*
&OPER    SETB  1                  WANT OPER BIT CHECKING    *CDG  2/82*
&PASS    SETB  1                  WANT PASSWORD             *CDG  2/82*
&CM      SETB  1                  WANT CROSS MEMORY         *CDG  2/82*
         AIF   ('&OPBIT' EQ 'YES').CKPA                     *CDG  2/82*
&OPER    SETB  0                  NO OPER BIT CHECKING      *CDG  2/82*
.CKPA    ANOP                                               *CDG  2/82*
         AIF   ('&PASWRD' EQ 'YES').CKSP                    *CDG  2/82*
&PASS    SETB  0                  NO CMND PASSWORD CHECKING *CDG  2/82*
.CKSP    ANOP                                               *CDG  2/82*
         AIF   ('&SP3' EQ 'YES').CODE                       *CDG  2/82*
&CM      SETB  0                  NO SP3 CROSS MEMORY CODE  *CDG  2/82*
.CODE    ANOP                                               *CDG  2/82*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SPY      CSECT
         USING *,R15                                        *CDG  2/82*
         STM   R14,R12,12(R13)    SAVE REGS                 *CDG  2/82*
         ST    R13,SAVE+4         BACKWARD CHAIN            *CDG  2/82*
         MVC   8(4,R13),SAVEA     FOREARD CHAIN             *CDG  2/82*
         CNOP  0,4                FULLWORD ALIGN FOR BAL    *CDG  2/82*
         BAL   R13,SAVEE          LOAD BASE REG AND SKIP SA *CDG  2/82*
SAVE     DS    18F                SAVE AREA                 *CDG  2/82*
SAVEA    DC    A(SAVE)            FOR MOVE                  *CDG  2/82*
SAVEE    DS    0H                 END OF SAVE AREA ETC      *CDG  2/82*
         DROP  R15                                          *CDG  2/82*
         USING SAVE,R13,R12,R11                             *CDG  2/82*
         LA    R12,4095(R13)      FIX UP 2ND BASE REG       *CDG  2/82*
         LA    R12,1(R12)         ""              ""        *CDG  2/82*
         LA    R11,4095(R12)      FIX UP 3RD BASE REG       *CDG  2/82*
         LA    R11,1(R11)         ""              ""        *CDG  2/82*
         L     R3,16              LOAD ADDRESS OF CVT       *LFA 10/81*
         L     R3,0(R3)           LOAD ADDR OF TCBP         *LFA 10/81*
         L     R3,4(R3)           CURRENT TCB               *CDG  1/82*
         L     R3,180(R3)         JSCB                      *CDG  1/82*
         L     R3,348(R3)         CURRENT JSCB              *CDG  1/82*
         AIF   (NOT &OPER).SKIPOPR                          *CDG  2/82*
*   CHECK FOR OPER AUTHORIZED USER                          *LFA 10/81*
         L     R3,264(R3)         PSCB                      *CDG  1/82*
         TM    16(R3),X'80'       OPER AUTH ?               *CDG  1/82*
         BNO   ALLDONE            IF NOT DON'T ALLOW SPY USE*CDG  1/82*
.SKIPOPR ANOP                                               *CDG  2/82*
*        RWHOLE AUTHON                                      *HMD 01/80*
         RWHOLE AUTHON                                      *HMD 01/80*
         AIF   (NOT &CM).SKIPCM                             *CDG  2/82*
*        MODESET KEY=ZERO         NEED KEY 0 FOR AXSET      *CDG  2/82*
         MODESET KEY=ZERO         NEED KEY 0 FOR AXSET      *CDG  2/82*
*        AXRES AXLIST=AXLIST,RELATED=A1                     *CDG  2/82*
         AXRES AXLIST=AXLIST,RELATED=A1                     *CDG  2/82*
*        AXSET AX=H1,RELATED=A1   AUTH SPY TO SSAR          *CDG  2/82*
         AXSET AX=H1,RELATED=A1   AUTH SPY TO SSAR          *CDG  2/82*
*        MODESET KEY=NZERO        BACK TO PROBLEM KEY       *CDG  2/82*
         MODESET KEY=NZERO        BACK TO PROBLEM KEY       *CDG  2/82*
         B     INIT               AROUND THE AXLIST         *CDG  2/82*
AXLIST   DS    0F                 FULL WORD BOUNDRY         *CDG  2/82*
         DC    H'1'               ONLY ONE ENTRY            *CDG  2/82*
         DC    H'0'               START WITH NO AUTHORITY   *CDG  2/82*
H1       DC    H'1'               FOR AXSET                 *CDG  2/82*
.SKIPCM  ANOP                                               *CDG  2/82*
         EJECT
***********************************************************************
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
***********************************************************************
INIT     LA    R3,STAXLIST        R3 = ADDRESS OF STAX LIST MACRO
*        STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         SPACE 2
*        GETMAIN R,LV=2892        FOR CONSOLE BUFFERS +     *CDG  2/82*
         GETMAIN R,LV=2892        FOR CONSOLE BUFFERS +     *CDG  2/82*
         ST    R1,CBUF            SAVE AREA ADDR            *CDG  2/82*
*        GTSIZE
         GTSIZE
         LTR   R0,R0              R0 = NUMBER OF LINES PER SCREEN
         BZ    HARDCOPY           IF NONZERO ASSUME A CRT IS IN USE
CRT      STH   R0,LPSCREEN        R0 = LINES PER SCREEN
         STH   R1,CPLINE          R1 = CHARACTERS PER LINE
         CH    R0,=H'43'          IS IT A MOD 4?            *CDG  2/82*
         BL    FLSCREEN           IF NOT FORCE ONLY 24 LINES*CDG  2/82*
         MVI   MOD4FLG,X'FF'      SET 3278-4 FLAG ON
         MVC   CMDCTRL(3),R41C1   ROW 41, COL 1
         MVC   PHEADING(3),R42C1  ROW 42, COL 1
         MVC   R24C1(3),R43C1     ROW 43, COL 1
*FLSCREEN STFSMODE ON,INITIAL=YES  TURN ON VTAM FULL SCREEN MODE
FLSCREEN STFSMODE ON,INITIAL=YES  TURN ON VTAM FULL SCREEN MODE
         B     BLDUCMS
         SPACE
*HARDCOPY STSIZE SIZE=80           OTHERWISE, HARDCOPY; SET LSIZE=80
HARDCOPY STSIZE SIZE=80           OTHERWISE, HARDCOPY; SET LSIZE=80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CMDCTRL(5),BLANKS  ZAP OUT 3277 CNTRL CHARS
         MVC   PHEADING(5),BLANKS ZAP OUT 3277 CNTRL CHARS
         EJECT
***********************************************************************
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
***********************************************************************
*BLDUCMS  MODESET KEY=ZERO         UZ26386 - TAPE 7907      *HMD 01/80*
BLDUCMS  MODESET KEY=ZERO         UZ26386 - TAPE 7907       *HMD 01/80*
         L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMASCB         COM TASK ASCB             *CDG  2/82*
         LH    R5,36(R5)          ASCB ASID FIELD           *CDG  2/82*
         STH   R5,ASIDCOM         SAVE ASID FOR SSAR        *CDG  2/82*
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB          R8 = ADDR OF UCMTAB
         XR    R10,R10            R10 = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(R8)           SAVE UCM ADDRESS IN UCMTAB
         LA    R10,1(R10)         R10 = R10 + 1  (ONE MORE UCM)
         LA    R8,4(R8)           R8 = ADDR OF NEXT UCMTAB ENTRY
         CLI   0(R8),X'FF'        END OF UCMTAB?            *CDG  2/82*
         BE    UCMDONE            YES; LEAVE LOOP           *CDG  2/82*
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R10,NUMUCMS        SAVE NUMBER OF UCMS FOUND
*        MODESET KEY=NZERO        UZ26385 - TAPE 7907       *HMD 01/80*
         MODESET KEY=NZERO        UZ26385 - TAPE 7907       *HMD 01/80*
         DROP  R4
         EJECT
***********************************************************************
*                                                                     *
*                            TOP OF LOOP                              *
*              LOCATE SCREEN BUFFER AND PREPARE TO TPUT               *
*                                                                     *
***********************************************************************
NEXTPAGE CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    NOATTN             NO
*                                 ATTENTION KEY HIT; PROCESS IT
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITFLG,X'00'      TURN OFF WAIT FLAG
         XC    TIMERV,TIMERV      SET TIMER TO 0
         CLI   CRTFLAG,X'FF'      IS THIS A 3270 OR HRDCPY? *HMD 12/79*
         BE    NOATTN             IS A 3270                 *HMD 12/79*
         B     DOLASTH            HARDCOPY GETS SPECIAL END *HMD 12/79*
NOATTN   DS    0H
*        MODESET KEY=ZERO         UZ26385 - TAPE 7907       *HMD 01/80*
         MODESET KEY=ZERO         UZ26385 - TAPE 7907       *HMD 01/80*
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         L     R4,CONSOLE         R4 = CONSOLE TO BE DISPLAYED
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
         MVC   ERROR(26),ERRMSG1  CONSOLE NUMBER INVALID
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   LTR   R4,R4              CHECK FOR 0 OR -          *CDG  2/82*
         BNP   RESETCN            IF IT IS RESET TO PREV CON*CDG  2/82*
         BCTR  R4,R0              GET RELATIVE TO 0         *CDG  2/82*
         SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(R5)           R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING DCMTSRT,R6
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         MVC   ERROR(26),ERRMSG4  NON-GRAPHIC CONSOLE
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   CTYPE(28),BLANKS   BLANK OUT CONSOLE TYPE FIELD
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH               NO
         MVC   MASTER(6),=CL6'MASTER' YES
AUTH     TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   SYS(3),=CL3'SYS'   YES
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   IO(3),=CL3'I/O'    YES
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CONS(4),=CL4'CONS' YES
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         MVC   UNIT(3),13(R7)     MOVE UNIT ADDR INTO LINE
         L     R7,DCMADTRN        R7 = ADDR OF TDCM
         USING DCMSTRT,R7
         L     R3,CBUF            CONSOLE BUFFERS +         *CDG  2/82*
         USING CBUFD,R3                                     *CDG  2/82*
         AIF   (NOT &CM).SKIPCM1                            *CDG  2/82*
         LH    R2,ASIDCOM         ASID OF COM TASK          *CDG  2/82*
*        SSAR  R2                 GET LINK WITH COM TASK    *CDG  2/82*
         SSAR  R2                 GET LINK WITH COM TASK    *CDG  2/82*
         LA    R4,79              ACTUAL LENGTH             *CDG  2/82*
         LA    R5,XCMINPUT        ADDR OF SPYS DATA         *CDG  2/82*
         LA    R8,DCMINPUT        ADDR OF COM TASK DATA     *CDG  2/82*
         XR    R9,R9              KEY OF 0                  *CDG  2/82*
         BAL   R2,MVCP            DO THE MOVE               *CDG  2/82*
         LA    R4,4               ACTUAL LENGTH             *CDG  2/82*
         LA    R5,XCMASCRN        ADDR OF SPYS DATA         *CDG  2/82*
         LA    R8,DCMASCRN        ADDR OF COM TASK DATA     *CDG  2/82*
         BAL   R2,MVCP            DO THE MOVE               *CDG  2/82*
         LA    R4,4               ACTUAL LENGTH             *CDG  2/82*
         LA    R5,XCMAWARN        ADDR OF END OF DATA       *CDG  2/82*
         LA    R8,DCMAWARN        ADDR OF END OF DATA       *CDG  2/82*
         BAL   R2,MVCP            DO THE MOVE               *CDG  2/82*
         LA    R4,2               ACTUAL LENGTH             *CDG  2/82*
         LA    R5,XCMMSGAL        ADDR OF SPYS DATA         *CDG  2/82*
         LA    R8,DCMMSGAL        ADDR OF COM TASK DATA     *CDG  2/82*
         BAL   R2,MVCP            DO THE MOVE               *CDG  2/82*
         LA    R4,XCMBUFF         FOR MVCL TO CLEAR THE BUF *CDG  3/82*
         LA    R5,M4BUFLEN        MAX BUFFER SIZE           *CDG  3/82*
         XR    R9,R9              ZERO FROM LENGTH          *CDG  3/82*
         MVCL  R4,R8              DO THE CLEAR              *CDG  3/82*
         L     R4,XCMAWARN        END OF DATA               *CDG  3/82*
         S     R4,XCMASCRN        - BEGIN OF DATA = LENGTH  *CDG  3/82*
*********LA    R4,M4BUFLEN        ACTUAL LENGTH**************CDG  2/82*
         LA    R5,XCMBUFF         ADDR OF SPYS DATA         *CDG  2/82*
         L     R8,XCMASCRN        ADDR OF COM TASK DATA     *CDG  2/82*
         BAL   R2,MVCP            DO THE MOVE               *CDG  2/82*
         B     MOVEDONE                                     *CDG  2/82*
*MVCP     MVCP  0(R4,R5),0(R8),R9  MOVE FROM SEC TO PRIM    *CDG  2/82*
MVCP     MVCP  0(R4,R5),0(R8),R9  MOVE FROM SEC TO PRIM     *CDG  2/82*
         BZR   R2                 IF TRUE LEN = 0 RET TO CALLERG  2/82*
         AL    R5,=F'256'         NEXT BLOCK TO MOVE        *CDG  2/82*
         AL    R8,=F'256'         ""               ""       *CDG  2/82*
         SL    R4,=F'256'         DECREMENT TRUE LENGTH     *CDG  2/82*
         B     MVCP               GO DO NEXT BLOCK          *CDG  2/82*
MOVEDONE DS    0H                                           *CDG  2/82*
*        EPAR  R2                 GET SPY ASID              *CDG  2/82*
         EPAR  R2                 GET SPY ASID              *CDG  2/82*
*        SSAR  R2                 RETURN TO NON SS MODE     *CDG  2/82*
         SSAR  R2                 RETURN TO NON SS MODE     *CDG  2/82*
         AGO   .SKIPSE2                                     *CDG  2/82*
.SKIPCM1 ANOP                                               *CDG  2/82*
         MVC   XCMINPUT(79),DCMINPUT                        *CDG  2/82*
         MVC   XCMASCRN(4),DCMASCRN                         *CDG  2/82*
         MVC   XCMAWARN(4),DCMAWARN                         *CDG  2/82*
         MVC   XCMMSGAL(2),DCMMSGAL                         *CDG  2/82*
         L     R8,XCMASCRN        CONSOLE BUFFER ADDR       *CDG  2/82*
         LA    R5,M4BUFLEN        LARGEST BUFFER LENGTH     *CDG  2/82*
         L     R9,XCMAWARN        END OF DATA               *CDG  3/82*
         S     R9,XCMASCRN        - BEGIN OF DATA = LENGTH  *CDG  3/82*
*                                 PAD CHAR S/B X'00'        *CDG  3/82*
*********LR    R9,R5              ""                  ""*****CDG  2/82*
         LA    R4,XCMBUFF         OUR OWN BUFFER            *CDG  2/82*
         MVCL  R4,R8              MOVE THE BUFFER           *CDG  2/82*
.SKIPSE2 ANOP                                               *CDG  2/82*
         DROP  R7                                           *CDG  2/82*
         EJECT
***********************************************************************
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
***********************************************************************
         L     R4,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R4,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         MVC   LASTLINE(79),XCMINPUT  MOVE INPUT BUFFER LINE*CDG  2/82*
         MVI   MODE+1,C' '        BLANK OUT VIP INDICATOR
         CLI   VIPFLG,X'FF'       IS THE VIP FLAG ON?
         BNE   CHKINTEG           NOPE
         MVI   MODE+1,C'*'        TURN ON VIP INDICATOR
         EJECT
***********************************************************************
*                                                                     *
*                    CHECK FOR INTEGRATED CONSOLE                     *
*                                                                     *
*     IF THIS IS A INTEGRATED CONSOLE, CHECK IF THE USER HAS A        *
*     327X-2 OR THE LARGE-SCREEN 327X-4.                              *
*                                                                     *
***********************************************************************
CHKINTEG MVI   INTEGFLG,X'FF'     TURN ON INTEGRATED CONSOLE FLAG
*        TM    UCMDISP,UCMDISPH   IS THIS REALLY INTEGRATED CONSOLE?DCT
*        BO    CHKMODEL           YES                               DCT
         CLC   XCMMSGAL(2),=H'30' CHECK FOR 30-LINE 168 INTEG CONS *CDG
         BNL   CHKMODEL     YES                                     DCT
         MVI   INTEGFLG,X'00'     NO; TURN OFF INTEGRATED CONSOLE FLAG
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD2TPUT SET TPUT LENGTH FOR SHORT SCREEN
         B     MOD2               AND TREAT AS MOD2 FOR NOW
         SPACE 1
CHKMODEL CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?
         BNE   MOD2               NO - MUST BE A MOD2
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD4TPUT SET LENGTH FOR FULL 3278-4 SCREEN
         LA    R8,XCMBUFF         R8 = ADDR OF SCREEN BUFFER*CDG  2/82*
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M4BUFLEN        R5 = 3278-4 BUFFER LEN (35 LINES)
         LA    R9,M4BUFLEN        R9 = 3278-4 BUFFER LEN (35 LINES)
         B     MOVEBUFF
         SPACE 2
MOD2     CLI   PAGE,C'1'          ARE WE ON PAGE 1?
         BNE   ONTWO              NO, SO WE MUST BE ON 2
         CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 1?
         BNE   PAGE2              NO, SO DISPLAY PAGE 2
         B     PAGE1              YES, SO DISPLAY PAGE 1
ONTWO    CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 2?
         BE    PAGE2              YES, SO DISPLAY PAGE 2
PAGE1    MVI   PAGE,C'1'          PAGE = 1
         LA    R8,XCMBUFF         R8 = ADDR OF SCREEN BUFFER*CDG  2/82*
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUF (21 LINES)
         LA    R9,M2BUFLEN        R9 = CONSOLE BUFFER LEN   (21 LINES)
         B     MVETRAIL           GO MOVE THE BUFFER
PAGE2    MVI   PAGE,C'2'          PAGE = 2
         LA    R8,XCMBUFF         R8 = ADDR OF SCREEN BUFFER*CDG  2/82*
         CLI   MODE,C'2'          ARE WE IN DISPLAY MODE 2?
         BE    DMODE2             YES, BRANCH TO DMODE2
DMODE1   LA    R8,LEN22(R8)       MOVE POINTER DOWN 23 LINES
         LA    R9,LEN13           R9 = LENGTH OF LAST 12 LINES
         B     CONTINUE           JUMP AROUND MODE 2 DISPLAY
DMODE2   LA    R8,LEN9(R8)        MOVE POINTER DOWN 9 LINES
         LA    R9,LEN21           R9 = LENGTH OF SOURCE BUFFER
CONTINUE LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUFFER
MVETRAIL MVC   ENDMOD2(TRAILEN),CMDCTRL MOVE IN TRAILER
MOVEBUFF ICM   R9,8,PAD           MAKE BLANK THE PAD CHARACTER
         MVCL  R4,R8              MOVE CONSOLE BUFFER TO OUTPUT BUFFER
         DROP  R5,R6,R3                                     *CDG  2/82*
*        MODESET KEY=NZERO        UZ26385 - TAPE 7907       *HMD 01/80*
         MODESET KEY=NZERO        UZ26385 - TAPE 7907       *HMD 01/80*
         CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    TPUTCRT            YES
         EJECT
***********************************************************************
*                                                                     *
*      DISPLAY THE OPERATOR'S SCREEN ON A TSO HARDCOPY TERMINAL       *
*                                                                     *
***********************************************************************
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,BUF             SET POINTER TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R3,R1              SAVE R1 SINCE TPUT ZAPS IT
*        TPUT  (1),(0),R          PRINT ONE LINE ON HARDCOPY
         TPUT  (1),(0),R          PRINT ONE LINE ON HARDCOPY
         LA    R8,1(R8)           ADD 1 TO COUNTER
NEXTL1   C     R8,=F'20'          HAVE WE PRINTED LAST LINE?*HMD 05/80*
         BE    DOLAST2            YES, CONTINUE
         LA    R1,80(R3)          NOPE, POINT TO NEXT LINE
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONSOLE?
         BE    NOT3270            YES
         MVC   0(5,R1),BLANKS     BLANK OUT 3270 CTRL CHARS
         LA    R1,4(R1)           ADD 4 EXTRA BYTES TO SKIP CTRL CHARS
NOT3270  L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
*DOLAST2  TPUT  LASTLINE,79,EDIT   DISPLAY OPER CMD LINE
DOLAST2  TPUT  LASTLINE,79,EDIT   DISPLAY OPER CMD LINE
*        TPUT  HEADING,79,EDIT    DISPLAY CONSOLE STATUS LINE
         TPUT  HEADING,79,EDIT    DISPLAY CONSOLE STATUS LINE
*        TPUT  USERLINE,79,EDIT   DISPLAY USER CMD LINE
         TPUT  USERLINE,79,EDIT   DISPLAY USER CMD LINE
         CLI   ATTNFLG,X'00'      IS ATTN FLAG ON?          *HMD 12/79*
         BE    INPUT              YES, READ NEXT COMMAND    *HMD 12/79*
         B     READCHAR           ELSE NORMAL PROCESSING    *HMD 12/79*
         EJECT
***********************************************************************
*                                                                     *
*               DISPLAY THE OPERATORS SCREEN ON A 3277                *
*                                                                     *
***********************************************************************
TPUTCRT  LA    R1,CLEAR           R1 = ADDR OF OUTPUT STREAM
         L     R0,TPUTLEN         R0 = LENGTH OF TPUT
         ICM   R1,8,FULLSCR       SET ASIS TYPE FOR TPUT
*        TPUT  (1),(0),R          DISPLAY ENTIRE SCREEN
         TPUT  (1),(0),R          DISPLAY ENTIRE SCREEN
INPUT    MVC   ERROR(32),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITFLG,X'FF'      IS THE WAIT FLAG ON?
         BNE   READCHAR           NO, SO GO GET A COMMAND
*        STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         L     R2,TIMERV          R2 = CURRENT VALUE OF TIMER
         BCTR  R2,0               TIMER = TIMER - 1
         ST    R2,TIMERV          STORE NEW VALUE OF TIMER
         CVD   R2,WORK            CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         LTR   R2,R2              HAS TIMER HIT ZERO?
         BNZ   NEXTPAGE           NO, CONTINUE TO COUNT
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         XI    WAITFLG,X'FF'      TOGGLE WAIT FLAG OFF
         B     NEXTPAGE           AND GO ON AS IF NOTHING HAPPENED..
         EJECT
***********************************************************************
*                                                                     *
*                       READ COMMAND FROM USER                        *
*                                                                     *
***********************************************************************
*READCHAR TGET  REPLY,79           GET 79 CHARACTERS FROM TERMINAL
READCHAR TGET  REPLY,79           GET 79 CHARACTERS FROM TERMINAL
         CLI   REPLY,X'6E'        IS THIS A RESHOW? (VTAM ONLY)
         BE    NEXTPAGE           YES, JUST GO REFRESH
         CLI   REPLY,C' '         JUST A BLANK?
         BE    NEXTPAGE           YES, JUST GO REFRESH
         OC    REPLY(79),BLANKS   CONVERT CHARS TO UPPER CASE
***********************************************************************
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
***********************************************************************
         CLI   REPLY,C'W'         DO WE SHIFT TO WAIT MODE?
         BNE   CDELAY             NO, SO CONTINUE
         XI    WAITFLG,X'FF'      TURN ON WAIT FLAG
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,TIMERV          STORE STARTING TIMER VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   CCONSOLE           NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*                    C  --  SET CONSOLE NUMBER                        *
*                                                                     *
***********************************************************************
CCONSOLE CLI   REPLY,C'C'         DO WE CHANGE CONSOLES?
         BNE   BYE                NO, SO CONTINUE
         L     R2,CONSOLE         SET DEFAULT CONSOLE
         ST    R2,OLDCONS         SAVE OLD CONSOLE #
         LA    R2,1               SET DEFAULT CONSOLE 1     *HMD 12/79*
         LA    R15,CONVBIN        BRANCH TO
         BALR  R14,R15            EBCDIC->BINARY CONVERTOR
         ST    R2,CONSOLE         STORE R2 AWAY AS CONSOLE NUMBER
         B     NEXTPAGE           CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                        B  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
BYE      CLI   REPLY,C'B'         IS IT A 'B'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*                        E  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
END      CLI   REPLY,C'E'         IS IT AN 'E'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*               F  --  FREEZE DISPLAY ON CURRENT PAGE                 *
*                                                                     *
***********************************************************************
F        CLI   REPLY,C'F'         IS IT AN 'F'?
         BNE   R                  NO, SO CONTINUE ON
         MVI   FREEZE,C'F'        TURN ON FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*           R  --  RELEASE FREEZE ON CURRENT PAGE DISPLAY             *
*                                                                     *
***********************************************************************
R        CLI   REPLY,C'R'         IS IT AN 'R'?
         BNE   MODE1              NO, SO CONTINUE ON
         MVI   FREEZE,C'R'        TURN OFF FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*                1  --  SHIFT TO MODE 1 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE1    CLI   REPLY,C'1'         DO WE SHIFT TO MODE 1 DISPLAY?
         BNE   MODE2              NO, SO CONTINUE
         MVI   MODE,C'1'          SET MODE INDICATOR
         B     NEXTPAGE
***********************************************************************
*                                                                     *
*                2  --  SHIFT TO MODE 2 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE2    CLI   REPLY,C'2'         DO WE SHIFT TO MODE 2 DISPLAY?
         BNE   GETHELP            NO, SO CONTINUE
         MVI   MODE,C'2'          SET MODE INDICATOR
         B     NEXTPAGE
         EJECT
***********************************************************************
*                                                                     *
*           ?  --  LIST HELP FOR SPY COMMANDS ON TERMINAL             *
*                                                                     *
***********************************************************************
GETHELP  CLI   REPLY,C'?'         IS HE ASKING FOR HELP?
         BNE   SWAPLINK           NO,SO CONTINUE
         CLI   CRTFLAG,X'FF'      IS THIS A DISPLAY TERM?   *HMD 12/79*
         BE    DO3270             YES, DISPLAY HELP ON 3270 *HMD 12/79*
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,HELP2           GET HELP FOR HARDCOPY     *HMD 12/79*
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF HEADER LINE  *HMD 12/79*
NEXTHLP  CLI   ATTNFLG,X'FF'      DID WE HAVE ATTN INT??    *HMD 12/79*
         BE    DOLASTH            DO LAST HELP LINE AND STOP*HMD 12/79*
         LR    R3,R1              SAVE R1 OVER TPUT         *HMD 12/79*
*        TPUT  (1),(0),R          PUT OUT HEADER            *HMD 12/79*
         TPUT  (1),(0),R          PUT OUT HEADER            *HMD 12/79*
         LA    R8,1(R8)           ADD 1 TO COUNTER          *HMD 12/79*
         C     R8,=F'21'          HAVE WE PRINTED LAST LINE?
         BE    DOLASTH            YES, CONTINUE
         LA    R1,80(R3)          NOPE, POINT TO NEXT LINE
         L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTHLP            PRINT NEXT LINE
*DOLASTH  TPUT  LASTHL,50,EDIT     DISPLAY LAST HEADER LINE *HMD 01/80*
DOLASTH  TPUT  LASTHL,50,EDIT     DISPLAY LAST HEADER LINE  *HMD 01/80*
         MVI   ATTNFLG,X'00'      RESET ATTENTION FLAG      *HMD 12/79*
         B     READCHAR           READ A CHAR. FROM TERM    *HMD 12/79*
DO3270   LA    R1,HELP            R1 = ADDR OF HELP PAGE
         LA    R0,HLENGTH         R0 = LENGTH OF HELP PAGE
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS
*        TPUT  (1),(0),R          DISPLAY HELP
         TPUT  (1),(0),R          DISPLAY HELP
         B     READCHAR
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                    L  --  LINK TO SWAP PROGRAM                      *
*                                                                     *
***********************************************************************
SWAPLINK CLI   REPLY,C'L'         SHOULD WE XCTL TO SWAP?
         BNE   VIP                NO, SO CONTINUE
*        RWHOLE AUTHOFF           UZ26386 - TAPE 7907       *HMD 01/80*
         RWHOLE AUTHOFF           UZ26386 - TAPE 7907       *HMD 01/80*
         L     R13,SAVE+4         CALLERS SAVE AREA POINTER.
*        XCTL  (2,12),EP=SWAP     XCTL TO SWAP
         XCTL  (2,12),EP=SWAP     XCTL TO SWAP
***********************************************************************
*                                                                     *
*                       CHECK FOR VIP PASSWORD                        *
*                                                                     *
***********************************************************************
VIP      DS    0H
         AIF   (NOT &PASS).SKIPAS1                          *CDG  2/82*
         CLC   REPLY(3),VIPWORD   WAS VIP PASSWORD ENTERED?
         BNE   JESOPER            NO, CONTINUE
         XI    VIPFLG,X'FF'       TOGGLE VIP FLAG
         B     NEXTPAGE
         EJECT                                              *HMD 12/79*
.SKIPAS1 ANOP                                               *CDG  2/82*
***********************************************************************
*                                                                     *
*                      SYSTEM OPERATOR COMMANDS                       *
*                                                                     *
***********************************************************************
JESOPER  DS    0H
         AIF   (NOT &PASS).SKIPAS2                          *CDG  2/82*
         CLI   VIPFLG,X'FF'       ARE WE IN VIP MODE?
         BNE   BADCMD             NO, SO CONTINUE
.SKIPAS2 ANOP                                               *CDG  2/82*
OPER     CLI   REPLY,C'/'         IS THIS AN OPERATOR COMMAND?
         BNE   JES                NO, CHECK FOR JES CMD
         LA    R1,REPLY+1         R1 = ADDR OF FIRST CHAR IN COMMAND
         LA    R7,OPERCMD         R7 = ADDR OF FIRST CHAR IN RECORD
         LA    R3,N1              R3 = ADDR OF COLUMN 72 IN RECORD
CHKCHAR  CLI   0(R1),C''''        IS THIS A TICK MARK?
         BNE   MOVECHAR           NOPE; GO ON
         MVI   0(R7),C''''        TUCK IN AN EXTRA TICK
         LA    R7,1(R7)           R7 = NEXT CHAR IN RECORD
MOVECHAR MVC   0(1,R7),0(R1)      MOVE ONE CHAR INTO BUFFER
         LA    R7,1(R7)           R7 = NEXT CHAR IN RECORD
         LA    R1,1(R1)           POINT TO NEXT CHAR IN BUFFER
         CR    R7,R3              ARE WE POINTING TO THE 'N'?
         BL    CHKCHAR            NO, GO GET ANOTHER CHAR
         MVC   REPLY(79),BLANKS   YES; QUIT AND BLANK OUT REPLY FIELD
NCHAR    BCTR  R3,0               R3 = R3 - 1
         CLI   0(R3),C' '         IS THIS CHAR A BLANK?
         BE    NCHAR              YES, BACK UP ANOTHER CHAR
         MVI   1(R3),C''''        MAKE A ' THE LAST CHAR IN THE CMD
         LA    R3,OPERCARD        R3 = ADDR OF OPER COMMAND
         B     DYNA               BRANCH TO DYNAMIC ALLOCATE
         EJECT
***********************************************************************
*                                                                     *
*                       JES OPERATOR COMMANDS                         *
*                                                                     *
***********************************************************************
JES      CLI   REPLY,C'$'         IS THIS A JES COMMAND?
         BNE   BADCMD             NO, CONTINUE
         MVC   JESCMD(69),REPLY   MOVE COMMAND INTO PLACE
         MVC   REPLY(79),BLANKS   BLANK OUT REPLY FIELD
         LA    R3,JESCARD         R3 = ADDR OF JES COMMAND
DYNA     LA    R1,RBPTR           R1 = ADDR OF RB POINTER
*        DYNALLOC                 <<< DYNAMIC ALLOCATION >>>
         DYNALLOC                 <<< DYNAMIC ALLOCATION >>>
         LA    R7,READER          R7 = ADDR OF READER DCB
         USING IHADCB,R7
         MVC   DCBDDNAM,DDNAME    MOVE DDNAME INTO DCB
         DROP  R7
*        OPEN  (READER,OUTPUT)    OPEN INTRDR FOR OUTPUT
         OPEN  (READER,OUTPUT)    OPEN INTRDR FOR OUTPUT
*        PUT   READER,(3)         PUT OPER CMD TO INTRDR
         PUT   READER,(3)         PUT OPER CMD TO INTRDR
*        CLOSE READER             CLOSE AND FREE INTERNAL READER
         CLOSE READER             CLOSE AND FREE INTERNAL READER
         B     NEXTPAGE
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                      COMMAND WAS INVALID                            *
*                                                                     *
***********************************************************************
BADCMD   MVC   ERROR(26),ERRMSG3  COMMAND WAS INVALID
         B     NEXTPAGE
         SPACE 5
DONE     DS    0H                                           *HMD 01/80*
         AIF   (NOT &CM).SKIPCM2                            *CDG  2/82*
*        MODESET KEY=ZERO         NEED KEY 0 FOR AXSET      *CDG  2/82*
         MODESET KEY=ZERO         NEED KEY 0 FOR AXSET      *CDG  2/82*
         XR    R2,R2                                        *CDG  2/82*
*        AXSET AX=(R2),RELATED=A1                           *CDG  2/82*
         AXSET AX=(R2),RELATED=A1                           *CDG  2/82*
*        AXFRE AXLIST=AXLIST,RELATED=A1                     *CDG  2/82*
         AXFRE AXLIST=AXLIST,RELATED=A1                     *CDG  2/82*
*        MODESET KEY=NZERO                                  *CDG  2/82*
         MODESET KEY=NZERO                                  *CDG  2/82*
.SKIPCM2 ANOP                                               *CDG  2/82*
*        RWHOLE AUTHOFF           UZ26385 - TAPE 7907       *HMD 01/80*
         RWHOLE AUTHOFF           UZ26385 - TAPE 7907       *HMD 01/80*
         CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?
         BE    ALLDONE            YES
*        TPUT  CLR,CLRLEN,FULLSCR NO, LETS CLEAR THE SCREEN FIRST
         TPUT  CLR,CLRLEN,FULLSCR NO, LETS CLEAR THE SCREEN FIRST
*        STFSMODE OFF             TURN OFF FS MODE
         STFSMODE OFF             TURN OFF FS MODE
ALLDONE  L     R13,SAVE+4         RESTORE POINTER TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         LA    R15,0
         BR    R14                RETURN TO SYSTEM
         EJECT
***********************************************************************
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(26),ERRMSG2  CONSOLE NUMBER ERROR
         B     RTRN
         DROP  12
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
***********************************************************************
ATTNEXIT LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         BR    R14                RETURN TO CALLER
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
***********************************************************************
CBUF     DS    F                   ADDR OF CONSOLE BUF      *CDG  2/82*
SPYASID  DC    H'0'                ASID OF SPY FOR AXEXT    *CDG  2/82*
ASIDCOM  DC    H'0'                ASID OF COM TASK FOR SSAR*CDG  2/82*
         DS    0D
WORK     DS    D                   WORK AREA FOR PACKS
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP
OLDCONS  DC    F'1'                PREVIOUS CONSOLE NUMBER
CONSOLE  DC    F'1'                CONSOLE TO BE LOOKED AT
TPUTLEN  DC    A(MOD2LEN)          LENGTH OF MOD 2 TPUT
MOD4TPUT DC    A(MOD4LEN)          LENGTH OF MOD4 TPUT
MOD2TPUT DC    A(MOD2LEN)          LENGTH OF MOD2 TPUT
LPSCREEN DC    H'0'                LINES PER SCREEN
CPLINE   DC    H'0'                CHARACTERS PER LINE
TIMERV   DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'100'              DELAY FOR 100 HUNDREDTHS OF A SECOND
MOD4FLG  DC    X'00'               X'FF' INDICATES 3278-4 IN USE
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
CRTFLAG  DC    X'FF'               X'FF' INDICATES CRT IN USE
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
VIPFLG   DC    X'00'               X'FF' INDICATES VIP MODE
INTEGFLG DC    X'00'               INTEGRATED CONSOLE FLAG
FULLSCR  DC    X'03'               TPUT ASIS FLAG
EDITFLG  DC    X'00'               TPUT EDIT FLAG
VIPWORD  DC    C'GAK'              VIP PASSWORD
NULLS    DC    80X'00'             JUST NULLS
R41C1    DC    X'11F240'           3278-4  --  ROW 41, COL 1
R42C1    DC    X'11F350'           3278-4  --  ROW 42, COL 1
R43C1    DC    X'11F460'           3278-4  --  ROW 43, COL 1
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
REPLY    DC    CL80' '             USERS COMMAND INPUT FIELD
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
*STAXLIST STAX  ATTNEXIT,MF=L
STAXLIST STAX  ATTNEXIT,MF=L
BLANKS   DC    CL80' '
         EJECT
***********************************************************************
*                                                                     *
*        PARAMETERS FOR DYNAMIC ALLOCATION OF INTERNAL READER         *
*                                                                     *
***********************************************************************
         DS    0F
RBPTR    DC    X'80'
         DC    AL3(RB)
RB       DC    X'14'              RB LENGTH
         DC    X'01'              DSNAME ALLOCATION REQUESTED
         DC    X'1000'            FLAGS1
INFOCODE DC    H'0'               ERROR CODE
ERORCODE DC    H'0'               INFORMATION BYTES
         DC    A(TXTUNITS)        ADDR OF TEXT UNIT POINTERS
         DC    2F'0'
TXTUNITS DC    A(TU1)
         DC    A(TU2)
         DC    A(TU3)
         DC    X'80',AL3(TU4)
TU1      DC    X'0019'            SYSOUT INTRDR REQUESTED
         DC    X'0001'
         DC    X'0006'
         DC    CL8'INTRDR'
TU2      DC    X'001C'            FREE DDNAME AT CLOSE
         DC    X'0000'
TU3      DC    X'0018'            SYSOUT CLASS TEXT UNIT
         DC    X'0001'
         DC    X'0001'
         DC    CL1'A'             SYSOUT=(A,INTRDR)
TU4      DC    X'0055'            RETURN GENERATED DDNAME
         DC    X'0001'
         DC    X'0008'
DDNAME   DC    CL8' '             DDNAME RETURN  HERE
         EJECT
***********************************************************************
*                                                                     *
*                      DCB AND COMMAND BUFFERS                        *
*                                                                     *
***********************************************************************
*READER   DCB   DSORG=PS,MACRF=PM,RECFM=F,LRECL=80,BLKSIZE=80,DDNAME=X
READER   DCB   DSORG=PS,MACRF=PM,RECFM=F,LRECL=80,BLKSIZE=80,DDNAME=X
JESCARD  DC    C'/*'
JESCMD   DC    CL69' '
N        DC    CL9'N'             SUPRESS ECHO OF COMMAND
         SPACE 2
OPERCARD DC    CL7'/*$VS,'''
OPERCMD  DC    CL64' '
N1       DC    CL9'N'             SUPRESS ECHO OF COMMAND
         EJECT
***********************************************************************
*                                                                     *
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*                                                                     *
***********************************************************************
CLR      DC    X'C1'              WCC - CLEAR SCREEN
         DC    X'115D7E'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'13'              INSERT CURSOR
CLRLEN   EQU   *-CLR
         EJECT
***********************************************************************
*                                                                     *
*                  DISPLAY SCREEN - HEADER SECTION                    *
*                                                                     *
***********************************************************************
HEADER   EQU   *
CLEAR    DC    X'C1'              WCC
         DC    X'115D7F'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'1D60'            ATTR BYTE - PROTECTED FIELD
         DC    X'40'
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - IMAGE BUFFER SECTION                 *
*                                                                     *
***********************************************************************
BUF      DC    21CL80' '          OPERATORS SCREEN BUFFER
         DC    14CL80' '          PLUS EXTRA FOR 3278-4
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - TRAILER SECTION                      *
*                                                                     *
***********************************************************************
TRAILER  EQU   *
CMDCTRL  DC    X'115A50'          SBA TO ROW 22, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
LASTLINE DC    CL79' '            OPERATORS COMMAND INPUT LINE
PHEADING DC    X'115B60'          SBA TO ROW 23, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
HEADING  DC    CL8'CONSOLE '
CONNUM   DC    CL2' 1'            CONSOLE NUMBER
CTYPE    DC    CL4' '
MASTER   DC    CL8' '             MASTER CONSOLE
SYS      DC    CL4' '             SYS  AUTHORIZATION
IO       DC    CL4' '             I/O  AUTHORIZATION
CONS     DC    CL5' '             CONS AUTHORIZATION
         DC    CL3' '
         DC    CL5'UNIT'
UNIT     DC    CL4' '             UNIT ADDR OF CONSOLE
HEADING1 DC    CL7'TIMER: '
TIME     DC    CL3' '             SECONDS REMAINING ON TIMER
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'           DELAY IN SECONDS
         DC    CL2' '
HEADING2 DC    CL6'MODE: '
FREEZE   DC    C'F'               FREEZE/RELEASE MODE
MODE     DC    CL1'2'             DISPLAY MODE 2/1
         DC    CL2' '
         DC    CL5'PAGE '
PAGE     DC    CL1'2'             PAGE NUMBER
R24C1    DC    X'115CF0'          SBA TO ROW 24, COL 1
         DC    X'1D40'            ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'              INSERT CURSOR
USERLINE DC    CL13' '            USERS COMMAND INPUT LINE
ERROR    DC    CL66' '            ERROR MSG FIELD
ENDTRAIL EQU   *
         EJECT
         LTORG
         EJECT
         SPACE 5
ERRMSG1  DC    CL26'ERROR - CONSOLE NOT ACTIVE'
ERRMSG2  DC    CL26'ERROR - NON-NUMERIC VALUE '
ERRMSG3  DC    CL26'ERROR - INVALID COMMAND   '
ERRMSG4  DC    CL26'ERROR - NON-CRT CONSOLE   '
         EJECT
***********************************************************************
*                                                                     *
*                           USER HELP PAGE                            *
*                                                                     *
***********************************************************************
HELP     DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'COMMAND     DESCRIPTION'
         DC    X'11C150'
         DC    X'11C260',C'   B        END SPY'
         DC    X'11C3F0',C'   C        SWITCH MONITOR TO CONSOLE 1'
         DC    X'11C540',C'   CXX      SWITCH MONITOR TO CONSOLE XX'
         DC    X'11C650',C'   DXX      SET DELAY TO XX TENTHS SECONDS'
         DC    X'11C760',C'   E        END SPY'
         DC    X'11C8F0',C'   F        FREEZE DISPLAY ON CURRENT PAGE'
         DC    X'114A40',C'   L        LINK TO SWAP PROGRAM'
         DC    C' (IF AVAILABLE)'
         DC    X'114B50',C'   R        RELEASE DISPLAY'
         DC    X'114C60',C'   W        START TIMER MODE FOR 30 SECONDS'
         DC    X'114DF0',C'   WXX      START TIMER MODE FOR XX SECONDS'
         DC    X'114F40',C'   W0       START TIMER MODE UNTIL '
         DC    C'INTERRUPT'
         DC    X'115050',C'   ?        DISPLAY THIS PAGE'
         DC    X'11D160',C'   1        DISPLAY MODE 1'
         DC    X'11D2F0',C'   2        DISPLAY MODE 2'
         DC    X'11D440'
         DC    X'11D550',C'HITTING INTERRUPT WILL STOP THE WAIT TIMER'
         DC    X'11D660'
         DC    X'11D7F0'
         DC    X'11D940'
         DC    X'115A50'
         DC    X'115B60'
         DC    X'115CF0'        ROW 24, COL 1
         DC    C'HIT ENTER TO CONTINUE'
         DC    X'115DC6'        ROW 24, COL 23
         DC    X'1D40'
         DC    X'1340'
         DC    X'1DE8'
MARKER1  EQU   *
HLENGTH  EQU   MARKER1-HELP     LENGTH OF HELP TPUT
         EJECT
***********************************************************************
*                                                                     *
*          HELP TEXT FOR HARDCOPY TERMINALS                           *
*                                                                     *
***********************************************************************
HELP2    DC    CL80'COMMAND     DESCRIPTION'
         DC    CL80' '
         DC    CL80'   B        END SPY'
         DC    CL80'   C        SWITCH MONITOR TO CONSOLE 1'
         DC    CL80'   CXX      SWITCH MONITOR TO CONSOLE XX'
         DC    CL80'   DXX      SET DELAY TO XX TENTHS SECONDS'
         DC    CL80'   E        END SPY'
         DC    CL80'   F        FREEZE DISPLAY ON CURRENT PAGE'
         DC    CL80'   L        LINK TO SWAP PROGRAM'
         DC    CL80'            (IF AVAILABLE)'
         DC    CL80'   R        RELEASE DISPLAY'
         DC    CL80'   W        START TIMER MODE FOR 30 SECONDS'
         DC    CL80'   WXX      START TIMER MODE FOR XX SECONDS'
         DC    CL80'   W0       START TIMER MODE UNTIL '
         DC    CL80'            INTERRUPT'
         DC    CL80'   ?        DISPLAY THIS PAGE'
         DC    CL80'   1        DISPLAY MODE 1'
         DC    CL80'   2        DISPLAY MODE 2'
         DC    CL80' '                                      *HMD 12/79*
         DC    CL80'HITTING INTERRUPT WILL STOP THE WAIT TIMER'
         DC    CL80' '
LASTHL   DC    CL50'HIT ENTER TO CONTINUE'
NUMUCMS  DS    H
UCMTAB   DS    99F      PROVIDE SPACE FOR 99 UCM ADDRESSES  *CDG  2/82*
         DC    X'FF'    STOPPER                             *CDG  2/82*
         EJECT                                              *HMD 12/79*
***********************************************************************
*                                                                     *
*                           E Q U A T E S                             *
*                                                                     *
***********************************************************************
LEN9     EQU   9*80             NUMBER OF BYTES IN  9 LINES
LEN21    EQU   21*80            NUMBER OF BYTES IN 21 LINES
LEN22    EQU   22*80            NUMBER OF BYTES IN 22 LINES
LEN13    EQU   13*80            NUMBER OF BYTES IN 13 LINES
LEN14    EQU   14*80            NUMBER OF BYTES IN 14 LINES
M2BUFLEN EQU   21*80            LENGTH OF BUFFER 3278-2
M4BUFLEN EQU   35*80            LENGTH OF BUFFER 3278-4
HEADLEN  EQU   BUF-HEADER       LENGTH OF HEADER
TRAILEN  EQU   ENDTRAIL-TRAILER LENGTH OF TRAILER
MOD4LEN  EQU   ENDTRAIL-HEADER  LENGTH OF TPUT FOR MOD4
MOD2LEN  EQU   MOD4LEN-LEN14    LENGTH OF TPUT FOR MOD2
ENDMOD2  EQU   BUF+M2BUFLEN     ADDR OF TRAILER FOR 3278-2
CBUFD    DSECT                                              *CDG  2/82*
XCMASCRN DS    F                A OF SCREEN IMMAGE BUFFER   *CDG  2/82*
XCMAWARN DS    F                END OF DATA                 *CDG  3/82*
XCMMSGAL DS    H                # LINES IN MESSAGE AREA     *CDG  2/82*
XCMINPUT DS    CL79             INPUT MESSAGE TEXT          *CDG  2/82*
XCMBUFF  DS    CL2800           ACTUAL DISPLAY BUFFER       *CDG  2/82*
         EJECT
         MEND
         PRINT ON
         SPYMAC
         PRINT NOGEN
         DCBD  DSORG=BS,DEVD=DA
         CVT   DSECT=YES,LIST=NO
         IEERDCM
         IEETDCM
         IEECUCM FORMAT=NEW
         END   SPY
