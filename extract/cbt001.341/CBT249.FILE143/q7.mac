FORMAT   QSTART 'QUEUE COMMAND - JQE AND JOE FORMAT ROUTINES'
         GBLB  &NJE           FOR NJE REL 3.0 SUPPORT       *HMD 11/80*
         GBLA  &QLEVEL        CHECK LEVEL FOR DEVICE PRT    *HMD 06/81*
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     LOAD BASE REG
         USING JQEDSECT,R9    BASE REG FOR JQE DSECT
         USING JOEDSECT,R8    BASE REG FOR JOE DSECT
         USING WORK,R13       BASE FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   INPUT TO THIS MODULE -                                            *
*     R1 = 0 INDICATES PRINT JQE                                      *
*     R1 = 4 INDICATES PRINT JOE                                      *
*     R9 CONTAINS JQE ADDRESS                                         *
*     R8 CONTAINS JOE ADDRESS                                         *
*                                                                     *
***********************************************************************
*                                                                     *
*   BRANCH TO PROPER ROUTINE                                          *
*                                                                     *
***********************************************************************
         STM   R0,R1,SAVE01    SAVE VOLITILE REGISTERS      *HMD 06/81*
         MVC   FCLEAR,QBLANK   CLEAR THE PRINT AREA
         MVC   FQUEUE,QCLASS   CLASS NAME
         MVC   QDHLINE,HEADING REPORT HEADING
         CLI   QSUBNAME,C'X'   IS THE REQUEST FOR A HEX DUMP?
         BE    ENDCHK          YES. SKIP FORMATTING RTN     *HMD 06/81*
         AIF   (&NJE).LBL001   USE NJE ROUTING INFO         *HMD 11/80*
***********************************************************************
*                                                                     *
*      PROCESS JOB ROUTING INFORMATION                                *
*                                                                     *
***********************************************************************
ROUTFM   DS    0H             PROCESS ROUTE INFO            *HMD 06/81*
         LA    R1,256         LOAD REMOTE CONSTANT          *HMD 06/79*
         SR    R0,R0          CLEAR REG. ZERO               *HMD 06/79*
         MVC   FREMOTE,QBLANK CLEAR REMOTE FIELD            *HMD 06/79*
         MVC   FREMOTE(4),=C'LOC '  START WITH LOCAL        *HMD 06/79*
         ICM   R0,3,JQEPRTRT  GET PRINT ROUTE               *HMD 06/79*
         BZ    LOC01                MUST BE LOCAL           *HMD 06/79*
         CR    R0,R1           IS THIS A 'U' OR 'R'?        *HMD 06/79*
         BL    U001            IS A 'U' ROUTE OR 'R' ROUTE  *HMD 06/79*
         SR    R0,R1           GET RID OF INDICATORS        *HMD 06/79*
         BZ    LOC01           LOCAL ROUTE AFTER ALL        *HMD 06/79*
         MVI   FREMOTE,C'R'    INDICATE REMOTE DEST         *HMD 06/79*
         CVD   R0,CONVERT      CONVERT REMOTE NUMBER        *HMD 06/79*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/79*
         OI    FREMOTE+3,X'F0' FIX SIGN                     *HMD 06/79*
         B     LOC01           BRANCH AROUND 'U' CODE       *HMD 06/79*
U001     MVI   FREMOTE,C'U'    INDICATE USER DESTINATION    *HMD 06/79*
         CVD   R0,CONVERT      CONVERT REMOTE NUMBER        *HMD 06/79*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/79*
         OI    FREMOTE+3,X'F0' FIX SIGN                     *HMD 06/79*
LOC01    MVI   FREMOTE+4,C','  SEPERATE PUNCH ROUTE         *HMD 06/79*
         MVC   FREMOTE+5(4),=C'LOC '                        *HMD 06/79*
         ICM   R0,3,JQEPUNRT   GET PUNCH ROUTE CODE         *HMD 06/79*
         BZ    LOC02           THIS IS A LOCAL JOBBER       *HMD 06/79*
         CR    R0,R1           IS THIS FOR REMOTE OR USER   *HMD 06/79*
         BL    U002            NOPE, MUST BE USER ROUTE     *HMD 06/79*
         SR    R0,R1           GET RID OF DEST INDICATOR    *HMD 06/79*
         BZ    LOC02           LOCAL AFTER ALL              *HMD 06/79*
         MVI   FREMOTE+5,C'R'  INDICATE REMOTE DESTINATION  *HMD 06/79*
         CVD   R0,CONVERT      CONVERT TO DECIMAL           *HMD 06/79*
         UNPK  FREMOTE+6(3),CONVERT+6(2)   MAKE PRINTABLE   *HMD 06/79*
         OI    FREMOTE+8,X'F0'  FIX SIGN                    *HMD 06/79*
         B     LOC02            BRANCH AROUND USER CODE     *HMD 06/79*
U002     MVI   FREMOTE+5,C'U'   USER INDICATION             *HMD 06/79*
         CVD   R0,CONVERT       CONVERT TO DECIMAL          *HMD 06/79*
         UNPK  FREMOTE+6(3),CONVERT+6(2)  MAKE PRINTABLE    *HMD 06/79*
         OI    FREMOTE+8,X'F0'   FIX SIGN                   *HMD 06/79*
LOC02    DS    0H                                           *HMD 06/79*
         AGO   .LBL002        BRANCH AROUND NJE CODE        *HMD 11/80*
.LBL001  ANOP                                               *HMD 11/80*
***********************************************************************
*                                                                     *
*      PROCESS JOB ROUTING INFORMATION - FOR NJE                      *
*                                                                     *
***********************************************************************
ROUTFM   DS    0H                PROCESS JOB ROUTING INFO   *HMD 06/81*
         MVC   FREMOTE,QBLANK    CLEAR DEST FIELD           *HMD 06/81*
         MVC   FREMOTE(4),=C'LOC ' START WITH LOCAL         *HMD 06/81*
         SLR   R15,R15           CLEAR FOR NODE RESOLUTION  *HMD 06/81*
         IC    R15,JQEPRTRT      GET PRINT ROUTING          *HMD 06/81*
         LTR   R15,R15           ANY NODE SPECIFIED         *HMD 06/81*
         BZ    OURANODE          MUST BE LOCAL              *HMD 06/81*
         CLM   R15,B'0001',QXONODE    IS IT OURS?           *HMD 06/81*
         BE    OURANODE          YES, PROCESS REMOTE INFO   *HMD 06/81*
         SLL   R15,2             MULTIPLY NODE NO. BY 4     *HMD 06/81*
         LA    R15,QNJNODE(R15)  GET NODE NAME              *HMD 06/81*
         MVC   FREMOTE(4),0(R15) MOVE IN NODE NAME          *HMD 06/81*
         B     LOC01             PROCESS PUNCH INFO         *HMD 06/81*
***                                                         *HMD 06/81*
OURANODE CLI   JQEPRTRT,0        NODE NUMBER PRESENT?       *HMD 06/81*
         BE    U001              NO, TEST FOR LOCAL ID      *HMD 06/81*
         SLR   R0,R0             CLEAR REGISTER ZERO        *HMD 06/81*
         IC    R0,JQEPRTRT+1     GET ROUTE INFO             *HMD 06/81*
         LTR   R0,R0             ANY ROUTING ID?            *HMD 06/81*
         BZ    LOC01             THIS IS REALLY A LOCAL     *HMD 06/81*
         MVI   FREMOTE,C'R'      INDICATE REMOTE DEST       *HMD 06/81*
         CVD   R0,CONVERT        CONVERT REMOTE NUMBER      *HMD 06/81*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+3,X'F0' FIX SIGN                     *HMD 06/81*
         B     LOC01             BRANCH AROUND 'U' CODE     *HMD 06/81*
***                                                         *HMD 06/81*
U001     SLR   R0,R0             CLEAR FOR INSERT CHAR      *HMD 06/81*
         IC    R0,JQEPRTRT+1     GET LOCAL PRINTER ID       *HMD 06/81*
         LTR   R0,R0             ANY LOCAL PRT ID?          *HMD 06/81*
         BZ    LOC01             NO, JUST 'LOCAL' (NO ID)   *HMD 06/81*
         MVI   FREMOTE,C'U'      INDICATE USER DESTINATION  *HMD 06/81*
         CVD   R0,CONVERT        CONVERT REMOTE NUMBER      *HMD 06/81*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+3,X'F0'   FIX SIGN                   *HMD 06/81*
***                                                         *HMD 06/81*
LOC01    MVI   FREMOTE+4,C','    SEPERATE PUNCH ROUTE       *HMD 06/81*
         MVC   FREMOTE+5(4),=C'LOC '  ASSUME LOCAL ROUTE    *HMD 06/81*
         SLR   R15,R15           CLEAR FOR NODE RESOLUTION  *HMD 06/81*
         IC    R15,JQEPUNRT      GET PUNCH ROUTING          *HMD 06/81*
         LTR   R15,R15           WAS ANY SPECIFIED?         *HMD 06/81*
         BZ    OURBNODE          NO, MUST BE LOCAL          *HMD 06/81*
         CLM   R15,B'0001',QXONODE   IS IT OURS?            *HMD 06/81*
         BE    OURBNODE          YES, PROCESS REMOTE INFO   *HMD 06/81*
         SLL   R15,2             MULTIPLY NODE NO. BY 4     *HMD 06/81*
         LA    R15,QNJNODE(R15)  POINT TO NODE NAME         *HMD 06/81*
         MVC   FREMOTE+5(4),0(R15)     MOVE IN NODE NAME    *HMD 06/81*
         B     LOC02             BRANCH AROUND LOCAL CODE   *HMD 06/81*
***                                                         *HMD 06/81*
OURBNODE CLI   JQEPUNRT,0        NODE NUMBER PRESENT?       *HMD 06/81*
         BE    U002              NO, TEST FOR LOCAL ID      *HMD 06/81*
         SLR   R0,R0             CLEAR REGISTER ZERO        *HMD 06/81*
         IC    R0,JQEPUNRT+1     GET LOCAL ID               *HMD 06/81*
         LTR   R0,R0             ANY LOCAL PRT ID?          *HMD 06/81*
         BZ    LOC02             THIS IS REALLY A LOCAL     *HMD 06/81*
         MVI   FREMOTE+5,C'R'    INDICATE REMOTE DEST       *HMD 06/81*
         CVD   R0,CONVERT        CONVERT REMOTE NUMBER      *HMD 06/81*
         UNPK  FREMOTE+6(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+8,X'F0'   FIX SIGN                   *HMD 06/81*
         B     LOC02             BRANCH AROUND 'U' CODE     *HMD 06/81*
***                                                         *HMD 06/81*
U002     SLR   R0,R0             CLEAR FOR INSERT CHAR      *HMD 06/81*
         IC    R0,JQEPUNRT+1     GET LOCAL PRINTER ID       *HMD 06/81*
         LTR   R0,R0             ANY LOCAL PRINTER ID?      *HMD 06/81*
         BZ    LOC02             NO, JUST LOCAL             *HMD 06/81*
         MVI   FREMOTE+5,C'U'    INDICATE USER DESTINATION  *HMD 06/81*
         CVD   R0,CONVERT        CONVERT REMOTE NUMBER      *HMD 06/81*
         UNPK  FREMOTE+6(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+8,X'F0'   FIX SIGN                   *HMD 06/81*
***                                                         *HMD 06/81*
LOC02    DS    0H                                           *HMD 06/81*
.LBL002  ANOP
         TM    JQEFLAGS,QUEBUSY  IS THE JOB EXECUTING?
         BZ    NEXTCHK           NO. SKIP THIS ROUTINE.     *HMD 06/81*
         IC    R15,JQEFLAGS      GET SYSTEM NUMBER
         N     R15,=F'7'         ZERO OUT UNWANTED BITS
         SLL   R15,3             MULTIPLY BY 8
         LA    R15,QSYSID(R15)   OBTAIN SYSTEM ID           *HMD 06/81*
         MVC   FSYSID(2),=C'E-'  INDICATE EXECUTING         *HMD 06/81*
         MVC   FSYSID+2(4),0(R15) MOVE SYSTEM ID TO DISPLAY *HMD 06/81*
         B     ENDCHK            BRANCH AROUND CODE         *HMD 06/81*
***                                                         *HMD 06/81*
NEXTCHK  DS    0H                NEXT FIELD TO CHECK        *HMD 06/81*
         AIF   (NOT &NJE).LBL003                            *HMD 06/81*
         CLI   QCLASS,16         AWAITING TRANSMISSION?     *HMD 06/81*
         BNE   NOXMIT            NO, PROCESS NORMALLY       *HMD 06/81*
         SLR   R15,R15           CLEAR FOR ICM              *HMD 06/81*
         IC    R15,JQEXEQND      GET EXECUTION NODE         *HMD 06/81*
         SLL   R15,2             MULTIPLY BY 4              *HMD 06/81*
         LA    R15,QNJNODE(R15)  POINT TO NODE NAME         *HMD 06/81*
         MVC   FSYSID(2),=C'T-'  INDICATE WAITING XMIT      *HMD 06/81*
         MVC   FSYSID+2(3),0(R15) MOVE IN NODE NAME         *HMD 06/81*
         B     ENDCHK            BYPASS NEXT SEGMENT        *HMD 06/81*
NOXMIT   DS    0H                                           *HMD 06/81*
.LBL003  ANOP                    COME HERE IF NOT NJE       *HMD 06/81*
***                                                         *HMD 06/81*
DOINPUT  DS    0H                CHECK FOR INPUT QUEUE      *HMD 06/81*
         OC    SAVE01+4(4),SAVE01+4                         *HMD 06/81*
         BNZ   ENDCHK                                       *HMD 06/81*
*                                CHECK FOR JQE PROCESS      *HMD 06/81*
         MVC   FSYSID(2),=C'W-'  INDICATE WAITING EXECUTING *HMD 06/81*
         LA    R1,AWAITBL-2      LOAD WAIT TABLE ADDRESS-2  *HMD 06/81*
         NI    JQEFLAG2,QUESYSAF CLEAR HI-BIT FLAGS         *HMD 06/81*
         SLR   R15,R15           CLEAR R15 FOR ICM          *HMD 06/81*
         IC    R15,JQEFLAG2      INSERT SYSAFF ROUTE        *HMD 06/81*
         CLM   R15,B'0001',=AL1(QUESYSAF)                   *HMD 06/81*
*                                IS SYSAFF SPECIFIED?       *HMD 06/81*
         BNE   GETSYSID          YES, GET SYSTEM ID         *HMD 06/81*
***                                                         *HMD 06/81*
ANYRTN   MVC   FSYSID+2(4),=C'ANY ' ANY CPU WILL DO         *HMD 06/81*
         B     ENDCHK            BRANCH AROUND SEGMENT      *HMD 06/81*
***                                                         *HMD 06/81*
GETSYSID DS    0H                SEARCH FOR SYSTEM ID
         LA    R1,2(,R1)         POINT TO NEXT ENTRY        *HMD 06/81*
         CLI   0(R1),X'FF'       END OF TABLE?              *HMD 06/81*
         BE    ANYRTN            YES, ROUTE NOT AVAILABLE   *HMD 06/81*
         CLM   R15,B'0001',0(R1) IS THIS THE ONE?           *HMD 06/81*
         BNE   GETSYSID          NO, KEEP SEARCHING         *HMD 06/81*
         IC    R15,1(R1)         GET SYSTEM NUMBER          *HMD 06/81*
         SLL   R15,3             MULTIPLY BY 8              *HMD 06/81*
         LA    R15,QSYSID(R15)   GET SYSTEM NAME            *HMD 06/81*
         MVC   FSYSID+2(4),0(R15)    MOVE IN SYSTEM NAME    *HMD 06/81*
         B     ENDCHK            BRANCH AROUND NEXT CHECK   *HMD 06/81*
***                                                         *HMD 06/81*
ENDCHK   DS    0H                END OF JQE CHECK           *HMD 06/81*
         LM    R0,R1,SAVE01      RESTORE VITAL REGISTERS    *HMD 06/81*
         CLI   QCLASS,0          IS THIS THE TSO QUEUE?
         BE    LISTTSO           YES. DO IT.
         CLI   QCLASS,4          IS THIS THE STC QUEUE?
         BE    LISTSTC           YES. DO IT.
         CLI   QCLASS,8          IS THIS THE HELD OUTPUT QUEUE?
         BE    LISTHO            YES. DO IT.
         AIF   (NOT &NJE).LBL004 IF NOT NJE THEN SKIP THIS  *HMD 05/81*
         CLI   QCLASS,16         IS THIS THE TRANSMIT QUEUE *HMD 05/81*
         BE    LISTDX            YES. DO IT.                *HMD 05/81*
.LBL004  ANOP                    BRANCH HERE IF NOT NJE     *HMD 05/81*
         LTR   R1,R1             IS REQUEST FOR JQE OR JOE?
         BZ    LISTJQE           JQE.
***********************************************************************
*                                                                     *
*   FORMAT JOE                                                        *
*                                                                     *
***********************************************************************
LISTJOE  MVC   FQNAME,=C'OUTPUT' MOVE IN NAME OF QUEUE
         MVC   FSYSID,QBLANK     CLEAR ACTIVE SYSTEM ID     *HMD 06/81*
         CLI   QSUBNAME,C'X'  IS THE REQUEST FOR A HEX DUMP?
         BE    HEXO           YES. DO IT.
         AIF   (&NJE).LBL005  GO TO NJE ROUTING CODE        *HMD 11/80*
         LA    R1,256         REMOTE INDICATOR              *HMD 06/79*
         SR    R0,R0          CLEAR FOR ICM                 *HMD 06/79*
         MVC   FREMOTE,QBLANK CLEAR REMOTE INDICATOR        *HMD 06/79*
         MVC   FREMOTE(4),=C'LOC ' INDICATE LOCAL           *HMD 06/79*
         ICM   R0,3,JOEROUT   GET ROUTE CODE                *HMD 06/79*
         BZ    LOC03          LOCAL ROUTING                 *HMD 06/79*
         CR    R0,R1          IS THIS A REMOTE USER         *HMD 06/79*
         BL    U003           NO, MUST BE LOCAL USERID      *HMD 06/79*
         SR    R0,R1          CLEAR REMOTE INDICATOR        *HMD 06/79*
         BZ    LOC03          LOCAL AFTER ALL               *HMD 06/79*
         MVI   FREMOTE,C'R'   INDICATE REMOTE DEVICE        *HMD 06/79*
         CVD   R0,CONVERT     CONVERT TO DECIMAL            *HMD 06/79*
         UNPK  FREMOTE+1(3),CONVERT+6(2)  MAKE PRINTABLE    *HMD 06/79*
         OI    FREMOTE+3,X'F0'  FIX SIGN                    *HMD 06/79*
         B     LOC03           BRANCH AROUND USER CODE      *HMD 06/79*
U003     MVI   FREMOTE,C'U'    INDICATE USERID              *HMD 06/79*
         CVD   R0,CONVERT      CONVERT ROUTE CODE           *HMD 06/79*
         UNPK  FREMOTE+1(3),CONVERT+6(2)   MAKE PRINTABLE   *HMD 06/79*
         OI    FREMOTE+3,X'F0'  FIX SIGN                    *HMD 06/79*
LOC03    DS    0H              END OF JOE ROUTING PROCESS   *HMD 06/79*
         AGO   .LBL006                                      *HMD 11/80*
.LBL005  ANOP                                               *HMD 11/80*
         MVC   FREMOTE,QBLANK       CLEAR REMOTE FIELD      *HMD 06/81*
         MVC   FREMOTE(4),=C'LOC '  START WITH LOCAL        *HMD 06/81*
         SLR   R15,R15              CLEAR R15 FOR IC        *HMD 06/81*
         IC    R15,JOEROUT          GET ROUTING CODE        *HMD 06/81*
         LTR   R15,R15              ANY NODE PRESENT?       *HMD 06/81*
         BZ    OURCNODE             MUST BE LOCAL           *HMD 06/81*
         CLM   R15,B'0001',QXONODE  IS THIS OURS?           *HMD 06/81*
         BE    OURCNODE             PROCESS REMOTE INFO     *HMD 06/81*
         SLL   R15,2                MULITPLY BY 4           *HMD 06/81*
         LA    R15,QNJNODE(R15)     POINT TO NODE NAME      *HMD 06/81*
         MVC   FREMOTE(4),0(R15)    MOVE IN NODE NAME       *HMD 06/81*
***                                                         *HMD 06/81*
OURCNODE CLI   JOEROUT,0            NODE NUMBER PRESENT?    *HMD 06/81*
         BE    U003                 NO, TEST FOR LOCAL ID   *HMD 06/81*
         SLR   R0,R0                CLEAR REGISTER ZERO     *HMD 06/81*
         IC    R0,JOEROUT+1         GET ROUTE INFO          *HMD 06/81*
         LTR   R0,R0                ANY ROUTE INFO?         *HMD 06/81*
         BZ    LOC03                THIS IS REALLY A LOCAL  *HMD 06/81*
         MVI   FREMOTE,C'R'         INDICATE REMOTE DEST    *HMD 06/81*
         CVD   R0,CONVERT           CONVERT REMOTE NUMBER   *HMD 06/81*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+3,X'F0'      FIX SIGN                *HMD 06/81*
         B     LOC03                BRANCH AROUND 'U' CODE  *HMD 06/81*
***                                                         *HMD 06/81*
U003     SLR   R0,R0                CLEAR REGISTER ZERO     *HMD 06/81*
         IC    R0,JOEROUT+1         GET LOCAL PRINTER ID    *HMD 06/81*
         LTR   R0,R0                IS ID PRESENT?          *HMD 06/81*
         BZ    LOC03                NO, INDICATE LOCAL      *HMD 06/81*
         CVD   R0,CONVERT           CONVERT PRINTER ID      *HMD 06/81*
         MVI   FREMOTE,C'U'         INDICATE LOCAL ID       *HMD 06/81*
         UNPK  FREMOTE+1(3),CONVERT+6(2) MAKE IT PRINTABLE  *HMD 06/81*
         OI    FREMOTE+3,X'F0'      FIX SIGN                *HMD 06/81*
LOC03    DS    0H                   END OF ROUTING PROCESS  *HMD 06/81*
.LBL006  ANOP                                               *HMD 11/80*
***                                                         *HMD 06/81*
         L     R0,JOERECCT          NUMBER OF PRINT LINES   *HMD 06/81*
         CVD   R0,CONVERT           CONVERT TO DECIMAL      *HMD 06/81*
         MVC   FLINES,ED6           PREPARE FOR EDIT        *HMD 06/81*
         ED    FLINES,CONVERT+4     EDIT NUMBER OF LINES    *HMD 06/81*
         TM    JOEFLAG,$JOEPRT+$JOEPUN PRINTING OR PUNCH    *HMD 06/79*
         BZ    LIST           NO. SKIP.                     *HMD 06/79*
***                                                         *HMD 06/81*
         AIF   (&QLEVEL NE 3).LBL007                        *HMD 06/81*
DEVC     LA    R3,JOEDEVID    GET DEVICE ID                 *HMD 11/80*
         BAL   R7,SETDEVIC    SET DEVICE NAME               *HMD 11/80*
         TM    JQEFLAGS,QUEHOLD1+QUEPURGE+QUEOPCAN          *HMD 06/81*
         BZ    PRTG           JOB IS REALLY PRINTING        *HMD 06/81*
         MVC   FSYSID,QBLANK  BLANK OUT DEVICE NAME         *HMD 06/81*
PRTG     DS    0H                                           *HMD 06/81*
.LBL007  ANOP                                               *HMD 06/81*
***                                                         *HMD 06/81*
CCKPT    TM    JOEFLAG,$JOECKV IS THE CKPT VALID            *HMD 06/79*
         BZ    LIST           NO. SKIP IT.
         LH    R8,JOECKPT     OFFSET TO CHECKPOINT JOE
         SLA   R8,2           MULTIPLY BY 4
         BZ    LIST           CHECKPOINT DOES NOT EXIST. SKIP.
         USING QCKPT,R1       BASE REG FOR CKPT WORK AREA
         L     R1,QVCKPT      LOAD BASE REG
         A     R8,QCJOTA      ADD BASE TO OFFSET
         DROP  R1
         S     R0,JOETLNC     SUBTRACT RECORDS PRINTED FROM TOTAL
         CVD   R0,CONVERT     CONVERT TO DECIMAL
         MVC   FLINES,ED6     PREPARE FOR EDIT             *HMD 04/79*
         ED    FLINES,CONVERT+4 PRINT UPDATED LINE COUNT
         B     LIST           FORMAT REST OF LINE
***********************************************************************
*                                                                     *
*   FORMAT JQE                                                        *
*                                                                     *
***********************************************************************
LISTTSO  MVC   FQNAME(8),=C'TSO USER' NAME OF QUEUE
         B     LIST           CONTINUE
LISTSTC  MVC   FQNAME(8),=C'SYSTEM Q' NAME OF QUEUE
         B     LIST           CONTINUE
LISTHO   MVC   FQNAME(8),=C'HELD OUT' NAME OF QUEUE
         MVC   FSYSID,QBLANK  CLEAR ACTIVE SYSTEM ID        *HMD 06/81*
         B     LIST           CONTINUE
         AIF   (NOT &NJE).LBL008                            *HMD 05/81*
LISTDX   MVC   FQNAME(8),=C'TRANSMIT' NAME OF QUEUE         *HMD 05/81*
         B     LIST                                         *HMD 05/81*
.LBL008  ANOP                                               *HMD 05/81*
LISTJQE  MVC   FQNAME,=C' INPUT' NAME OF QUEUE
LIST     CLI   QSUBNAME,C'X'  IS THE REQUEST FOR A HEX DUMP?
         BE    HEX            YES. DO IT.
         MVC   FCOUNT,ED5     PREPARE FOR EDIT
         ED    FCOUNT,QCOUNT  EDIT THE POSITION IN QUEUE
         MVC   FNAME,JQEJNAME MOVE IN JOBNAME
         MVC   FJOBNO,ED5     PREPARE FOR EDIT
         LH    R0,JQEJOBNO    LOAD HASP JOBNUMBER
         CVD   R0,CONVERT     CONVERT TO DECIMAL
         ED    FJOBNO,CONVERT+5 EDIT HASP JOBNUMBER
SNOBLK   DS    0H
         SR    R0,R0          ZERO OUT REGISTER
         IC    R0,JQEPRIO     LOAD JQE PRIORITY
         SRL   R0,4           DIVIDE BY 16
         CVD   R0,CONVERT     CONVERT TO DECIMAL
         MVC   FPRIO,ED3      PREPARE FOR EDIT
         ED    FPRIO,CONVERT+6 EDIT JQE PRIORITY
         TM    JQEFLAGS,QUEHOLDA+QUEHOLD1+QUEHOLD2          *HMD 06/79*
         BZ    NOHOLD         BRANCH IF NOT HELD AT ALL     *HMD 06/79*
         MVC   FHOLD,=CL9'HELD'    MOVE IN HELD INDICATOR   *HMD 11/80*
         TM    JQEFLAGS,QUEHOLD2   DUPLICATE JOB NAME?      *HMD 06/79*
         BZ    NOHOLD         NOPE, PROCESS AS JUST HELD    *HMD 06/79*
         MVC   FHOLD,=CL9'DUPLICATE'                        *HMD 11/80*
NOHOLD   TM    JQEFLAGS,QUEPURGE    PURGE IN PROCESS?       *HMD 06/79*
         BZ    NOPURG         NOPE                          *HMD 06/79*
         MVC   FHOLD,=CL9'PURGED'   JOB HAS BEEN PURGED     *HMD 11/80*
NOPURG   TM    JQEFLAGS,QUEOPCAN  DID THE OPERATOR CANCEL IT*HMD 06/79*
         BZ    NOCAN          NOPE                          *HMD 06/79*
         MVC   FHOLD,=CL9'CANCELLED'                        *HMD 11/80*
NOCAN    LA    R1,QDMSG       ADDR OF MESSAGE AREA          *HMD 06/79*
         ST    R1,QDMSGA      STORE MESSAGE ADDR
         MVC   QDMLNG,=H'80'  LENGTH OF DISPLAY LINE
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        DISPLAY THE LINE
STOP     QSTOP
***********************************************************************
*                                                                     *
*   TAKE HEX DUMP OF JOE                                              *
*                                                                     *
***********************************************************************
HEXO     UNPK  FHEX1,0(8,R8)  UNPK FIRST PART OF JOE INTO HEX
         UNPK  FHEX2,7(8,R8)  SECOND
         UNPK  FHEX3,14(8,R8) THIRD
         UNPK  FHEX4,21(8,R8) FOURTH
         UNPK  FHEX5,28(5,R8) FIFTH
         MVI   FHEXOC,C' '    CLEAR LAST BYTE
         TR    FHEXO,TABLE    CHANGE TO PRINTABLE HEX
         LA    R1,QDMSG       ADDR OF MESSAGE AREA
         ST    R1,QDMSGA      STORE MESSAGE ADDR
         MVC   QDMLNG,=H'80'  LENGTH OF DISPLAY LINE
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        DISPLAY THE LINE
***********************************************************************
*                                                                     *
*   TAKE HEX DUMP OF JQE                                              *
*                                                                     *
***********************************************************************
HEX      UNPK  FHEX1,0(8,R9)  UNPK FIRST PART OF JOE INTO HEX
         UNPK  FHEX2,7(8,R9)  SECOND
         UNPK  FHEX3,14(8,R9) THIRD
         UNPK  FHEX4,21(8,R9) FOURTH
         MVC   FHEXC,QBLANK   CLEAR LAST BYTES
         TR    FHEX,TABLE     CHANGE TO PRINTABLE HEX
         B     NOHOLD         CALL DISPLAY
         AIF   (&QLEVEL NE 3).LBL009  ONLY FOR LEVEL 3      *HMD 06/81*
***********************************************************************
*                                                                     *
*   SET DEVICE SUBROUTINE - GET DEVICE DATA (R3 POINTS TO DEVID)      *
*                                                                     *
***********************************************************************
SETDEVIC DS    0H       SUBROUTINE TO INTERPRET JOEDEVID    *HMD 11/80*
         ST    R7,SETDHOLD    SAVE REGISTER 7               *HMD 11/80*
         MVC   FSYSID,QBLANK  CLEAR AREA                    *HMD 11/80*
         TM    0(R3),HIGHBIT  IS THIS A REMOTE DEVICE?      *HMD 11/80*
         BO    RMTDEV         YES, HANDLE REMOTE NUMBER     *HMD 11/80*
         SLR   R1,R1          CLEAR FOR IC                  *HMD 11/80*
         IC    R1,0(R3)       GET DEVICE TYPE               *HMD 11/80*
         SRL   R1,4           RIGHT JUSTIFIED               *HMD 11/80*
         MH    R1,DEVTYPEL    TYPE * LENGTH OF ENTRY        *HMD 11/80*
         LA    R1,DEVTABLE(R1)   GET DEVICE NAME ENTRY      *HMD 11/80*
         MVC   FSYSID(8),1(R1)   MOVE IN DEVICE NAME        *HMD 06/81*
         CLI   0(R3),0        INTERNAL READER?              *HMD 11/80*
         BE    SETDRTN        YES, GIVE THE USER INFO       *HMD 11/80*
         SLR   R15,R15        FOR THE INSERT CHAR           *HMD 11/80*
         IC    R15,1(R3)      GET DEVICE NUMBER             *HMD 11/80*
         CVD   R15,DOUBLEWD   IN PACKED DECIMAL             *HMD 11/80*
         IC    R15,0(R1)      OFFSET IN PRINT FIELD         *HMD 11/80*
         LA    R15,FSYSID(R15)   POINT TO 1ST DIGIT POS.    *HMD 11/80*
         MVC   1(L'DIGITS3,R15),DIGITS3   MOVE IN PATTERN   *HMD 11/80*
         EDMK  0(L'DIGITS3+1,R15),DOUBLEWD+6                *HMD 11/80*
         MVC   0(L'DIGITS3+1,R15),0(R1)                     *HMD 11/80*
         B     SETDRTN        GO EXIT                       *HMD 11/80*
***                                                         *HMD 11/80*
RMTDEV   SR    R0,R0         FOR THE INSERT CHARACTER       *HMD 11/80*
         IC    R0,1(R3)      GET THE REMOTE NUMBER          *HMD 11/80*
         CVD   R0,DOUBLEWD   IN PACKED DECIMAL              *HMD 11/80*
         MVI   FSYSID,C'R'   INDICATE REMOTE DEVICE         *HMD 11/80*
         MVC   FSYSID+2(L'THREEPT),THREEPT                  *HMD 11/80*
         EDMK  FSYSID+1(L'THREEPT),DOUBLEWD+6               *HMD 11/80*
         MVC   FSYSID+1(L'THREEPT),0(R1)                    *HMD 11/80*
         LA    R1,FSYSID+1   A(SPOT JUST BEFORE SEP)        *HMD 11/80*
FINDPT   LA    R1,1(R1)      A(NEXT BYTE)                   *HMD 11/80*
         CLI   0(R1),C'.'    IS THIS THE DOT.               *HMD 11/80*
         BNE   FINDPT        NO, TRY NEXT BYTE              *HMD 11/80*
         SR    R15,R15       CLEAR FOR IC                   *HMD 11/80*
         IC    R15,0(R3)     DEVICE TYPE INDICATOR          *HMD 11/80*
         SRL   R15,3         RIGHT JUSTIFIED                *HMD 11/80*
         LA    R15,RMTDEVS-HIGHBIT/8(R15)  A(DEVICE TYPE)   *HMD 11/80*
         MVC   1(2,R1),0(R15) MOVE IT IN                    *HMD 11/80*
         MVC   3(1,R1),0(R3)  PUT IN THE DEVICE NUMBER      *HMD 11/80*
         OI    3(R1),X'F0'    MAKE IT PRINTABLE             *HMD 11/80*
         B     SETDRTN                                      *HMD 11/80*
*                                                           *HMD 11/80*
SETDRTN  L     R7,SETDHOLD    LOAD SAVE LINK REGISTER       *HMD 11/80*
         BR    R7             RETURN TO CALLER              *HMD 11/80*
         SPACE 1                                            *HMD 11/80*
HIGHBIT  EQU   X'80'                                        *HMD 11/80*
DIGITS3  DC    X'202020'                                    *HMD 11/80*
THREEPT  DC    X'2020204B'                                  *HMD 11/80*
         DS    0H                                           *HMD 11/80*
DEVTABLE DC    AL1(0),CL8'INTRDR',AL1(6),CL8'READER'        *HMD 11/80*
         DC    AL1(7),CL8'PRINTER',AL1(5),CL8'PUNCH'        *HMD 11/80*
DEVTYPEL DC    AL2((*-DEVTABLE)/4)                          *HMD 11/80*
RMTDEVS  DC    C'**',C'RD',C'PR',C'PU'                      *HMD 11/80*
.LBL009  ANOP                                               *HMD 06/81*
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
ED5      DC    X'402020202120'
ED6      DC    X'4020202020202120'                          *HMD 06/79*
ED3      DC    X'40202120'
******************************                              *HMD 06/81*
** AWAITING EXECUTION TABLE **                              *HMD 06/81*
******************************                              *HMD 06/81*
AWAITBL  DS    0H                 AWAITING EXECUTION TBL    *HMD 06/81*
         DC    X'01',X'01'        SYSTEM 1                  *HMD 06/81*
         DC    X'02',X'02'        SYSTEM 2                  *HMD 06/81*
         DC    X'04',X'03'        SYSTEM 3                  *HMD 06/81*
         DC    X'08',X'04'        SYSTEM 4                  *HMD 06/81*
         DC    X'10',X'05'        SYSTEM 5                  *HMD 06/81*
         DC    X'20',X'06'        SYSTEM 6                  *HMD 06/81*
         DC    X'40',X'07'        SYSTEM 7                  *HMD 06/81*
         DC    X'FF',X'FF'        END OF TABLE              *HMD 06/81*
***                                                         *HMD 06/81*
HEADING  DC    C'  QUEUE  POSITION JOBNAME    JOB#  PRIORITY LINES'
         DC    CL40'  EXEC      STATUS    ROUTE'            *HMD 11/80*
TABLE    EQU   *-240
         DC    C'0123456789ABCDEF' TRANSLATE TO PRINTABLE HEX
WORK     DSECT
         DS    CL80
CONVERT  DS    D
SAVE01   DS    2F             SAVE AREA FOR REG 0 & 1       *HMD 06/79*
SETDHOLD DS    F              SAVE AREA FOR REG 7           *HMD 11/80*
DOUBLEWD DS    D                                            *HMD 11/80*
FORMAT   CSECT
         $JQE
         $JOE
         QCOMMON
         ORG   QDMSG
FCLEAR   DS    0CL80          FORMAT FOR QUEUE RECORDS
FQNAME   DS    CL6            NAME OF QUEUE
         DS    C
FQUEUE   DS    C              CLASS NAME
         DS    CL2
FCOUNT   DS    CL6            POSITION IN QUEUE
         DS    CL2
FNAME    DS    CL8            JOBNAME
         DS    C
FJOBNO   DS    CL6            JES2 JOB NUMBER
         DS    CL2
FPRIO    DS    CL4            JOB PRIORITY
         DS    CL2                                        *HMD 04/79*
FLINES   DS    CL8            NUMBER OF OUTPUT LINES      *HMD 04/79*
         DS    CL2
FSYSID   DS    CL9            SYSTEM ID
         DS    C                                          *HMD 11/79*
FHOLD    DS    CL9            JOB HOLD STATUS
         DS    C                                          *HMD 04/79*
FREMOTE  DS    CL9            REMOTE JOB (RJE) DEST       *HMD 06/79*
         ORG   FCOUNT
FHEX     DS    0CL56          LENGTH OF JQE HEX DUMP
FHEXO    DS    0CL64          LENGTH OF JOE HEX DUMP
FHEX1    DS    0CL15
         DS    CL14
FHEX2    DS    0CL15
         DS    CL14
FHEX3    DS    0CL15
         DS    CL14
FHEX4    DS    0CL15
         DS    CL14
FHEXC    DS    0CL9           CLEAR LAST BYTES FOR JQE
FHEX5    DS    0CL9
         DS    CL8
FHEXOC   DS    C              CLEAR LAST BYTE FOR JOE
         END
