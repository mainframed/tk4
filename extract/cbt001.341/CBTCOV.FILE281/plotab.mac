 /*      PLOTAB ---- PL/I OPTIMIZER ANALYZER TABULATOR COMPONENT    */
0/**********************************************************************
0NOTE: THIS PROGRAM HAS BEEN MODIFIED TO RUN EITHER UNDER PL/I(F) OR THE
       OPTIMIZER. A COMPILE-TIME VARIABLE CALLED 'TYPE' CONTROLS
       THE SELECTION OF SOURCE TEXT FOR THE COMPILER. IT SHOULD BE SET
       TO 'PL1F' FOR PL/I(F), OR 'PLO' FOR THE OPTIMIZER. ANYTHING
       ELSE HAS THE EFFECT OF SPECIFYING 'PLO'.
0**********************************************************************/
0%DCL TYPE CHAR;%TYPE='PLO';
0PLOTAB: PROCEDURE OPTIONS(MAIN) REORDER;
0/*-----------------------------------------------------------------*/
 /*                                                                 */
 /*      THE PURPOSE OF THIS PROGRAM IS TO READ THE TRACE FILE      */
 /*      PRODUCED BY THE PLOTRAP COMPONENT, AND TO TABULATE THE     */
 /*      RESULTS OF THE ANALYSIS.                                   */
 /*      THE LOGIC IS AS FOLLOWS: FIRST THE TRACE FILE              */
 /*      IS READ AND TABLES ARE BUILT FROM SOME RECORDS AND OTHERS  */
 /*      ARE REFORMATTED AND WRITTEN OUT TO A TEMPORARY FILE FOR    */
 /*      SORTING. BY USING A SORT WE CAN ALMOST ELIMINATE           */
 /*      RESTRICTIONS ON THE SIZE OF THE PROGRAM BEING ANALYSED     */
 /*      AND IT IS THE EASIEST WAY TO HANDLE LINE NUMBERS.          */
 /*      THE SORTED FILE IS THEN READ BACK IN AND THE TABULATIONS   */
 /*      ARE PRODUCED FROM THE SEQUENCED RECORDS                    */
 /*                                                                 */
 /*-----------------------------------------------------------------*/
-/*                      DECLARATIONS                               */
0DCL     BUF             CHAR(24) BASED(P);
0DCL 1 RUNIN             BASED(P),      /* RUN RECORD FORMAT        */
         5 OPCODE        CHAR(3),       /* 'RUN'                    */
         5 FLAG1         BIT(1),        /* ON MEANS MAIN PROC       */
         5 FLAG2         BIT(1),        /* ON MEANS LINE NUMBER     */
         5 FLAG6         BIT(6),        /* NOT USED                 */
         5 PSW           FIXED BIN(31), /* PSW AT INTERRUPT         */
         5 STATLIN       FIXED BIN(31), /* STATEMENT OR LINE NO     */
         5 IC            CHAR(1),       /* INTERRUPT CODE           */
         5 POFFS         CHAR(3),       /* PROC OFFSET IN LOAD MOD  */
         5 NAME          CHAR(8);       /* PROCEDURE NAME           */
0DCL 1 RUNSORT           BASED(Q),      /* RUN SORT RECORD          */
         5 CODE          CHAR(4),       /* M=MAIN PROC, S=SECONDARY */
         5 NAME          CHAR(8),       /* PROCEDURE NAME           */
         5 POFFS         CHAR(3),       /* PROC OFFSET IN LOAD MOD  */
         5 CODE2         CHAR(1),       /* SEQUENCE MARKER          */
         5 STATLIN       FIXED BIN(31), /* STATEMENT OR LINE NO     */
         5 PSW           FIXED BIN(31), /* PSW AT INTERRUPT         */
         5 SPARE         CHAR(2),       /* NOT USED                 */
         5 IC            CHAR(1),       /* INTERRUPT CODE           */
         5 LINE          CHAR(1);       /* ON MEANS LINE NUMBER     */
0DCL 1 MESGIN            BASED(P),      /* MESSAGE RECORD           */
         5 OPCODE        CHAR(4),       /* 'MESG'                   */
         5 MESSNUM       FIXED BIN(31), /* MESSAGE NUMBER           */
         5 SPARE         CHAR(16);      /* NOT USED                 */
0DCL 1 LOADIN            BASED(P),      /* LOAD MODULE RECORD       */
         5 OPCODE        CHAR(4),       /* 'LOAD'                   */
         5 LOADPT        FIXED BIN(31), /* LOAD POINT OF MODULE     */
         5 MODSIZE       FIXED BIN(31), /* SIZE OF MODULE IN BYTES  */
         5 SPARE1        CHAR(4),       /* NOT USED                 */
         5 NAME          CHAR(8);       /* LOAD MODULE NAME         */
0DCL 1 LOADSORT          BASED(Q),      /* LOAD SORT RECORD         */
         5 CODE          CHAR(4),       /* SORT SEQUENCER           */
         5 NAME          CHAR(8),       /* LOAD MODULE NAME         */
         5 LOADPT        FIXED BIN(31), /* LOAD POINT OF MODULE     */
         5 MODSIZE       FIXED BIN(31), /* SIZE OF MODULE IN BYTES  */
         5 SPARE         CHAR(8);       /* NOT USED                 */
0DCL 1 PGCKIN            BASED(P),      /* PROGRAM CHECK RECORD     */
         5 OPCODE        CHAR(4),       /* 'PGCK'                   */
         5 OFFSET        CHAR(4),       /* OFFSET WITHIN PLOTRAP    */
         5 INTADDR       FIXED BIN(31), /* ORIGINAL PL/I INTERRUPT  */
         5 INTCODE       FIXED BIN(31), /* INTERRUPT CODE IN PLOTRAP*/
         5 SPARE         CHAR(8);       /* NOT USED                 */
0DCL 1 PGCKSORT     BASED(Q),           /* PROG CHECK SORT RECORD   */
         5 CODE          CHAR(4),       /* SORT SEQUENCER           */
         5 OFFSET        CHAR(4),       /* OFFSET WITHIN PLOTRAP    */
         5 INTCODE       FIXED BIN(31), /* INTERRUPT CODE IN PLOTRAP*/
         5 SPARE2        CHAR(16);      /* NOT USED                 */
0DCL 1 ENDIN             BASED(P),      /* END RECORD               */
         5 OPCODE        CHAR(3),       /* 'END'                    */
         5 SPARE1        CHAR(1),       /* NOT USED                 */
         5 ICOUNT        FIXED BIN(31), /* INTERRUPT COUNT          */
         5 SPARE2        CHAR(4),       /* NOT USED                 */
         5 START_TIME    FIXED BIN(31), /* START TIME OF RUN        */
         5 END_TIME      FIXED BIN(31), /* STOP TIME OF RUN         */
         5 SPARE3        CHAR(4);       /* NOT USED                 */
0DCL 1 STARTIN           BASED(P),      /* START RECROD             */
         5 OPCODE        CHAR(4),       /* 'STRT'                   */
         5 LOADPT        FIXED BIN(31), /* LOAD POINT OF PL/I PGM   */
         5 ENTRYPT       FIXED BIN(31), /* ENTRY POINT OF PL/I PGM  */
         5 INTERVAL      FIXED BIN(31), /* SAMPLING INTERVAL .01SEC */
         5 PGM           CHAR(8);       /* NAME OF PROGRAM ANALYSED */
0DCL 1 INVALSORT         BASED(Q),      /* INVALID RECORDS          */
         5 CODE          CHAR(4),
         5 REC           CHAR(24);
0DCL     SORTREC   CHAR(28)STATIC INIT(' ');
 %IF TYPE='PLO' %THEN %DO;
0DCL     TRACE      FILE RECORD INPUT SEQL ENV(TOTAL FB RECSIZE(24));
 DCL     SORTIN     FILE RECORD OUTPUT SEQL ENV(TOTAL FB RECSIZE(28));
 DCL     SORTOUT    FILE RECORD INPUT SEQL ENV(TOTAL FB RECSIZE(28));
 %END; %ELSE %DO;
 DCL     TRACE      FILE RECORD INPUT SEQL ENV(F(480,24));
 DCL     SORTIN     FILE RECORD OUTPUT SEQL ENV(F(13020,28));
 DCL     SORTOUT    FILE RECORD INPUT SEQL ENV(F(13020,28));
 %END;
0DCL 1 MESSTAB(980:990)STATIC,         /* MESSAGE TABLE              */
         5 COUNT         FIXED BIN(31)  INIT((11)0),
         5 TEXT          CHAR(56)       INIT (
          'PROGRAMMER SPECIFIED TIME EXCEEDED, EXECUTION TERMINATED',
          'UNABLE TO FIND PL/I PRB, SAMPLE ABANDONED',
          'UNABLE TO FIND RB SAVE AREA, SAMPLE ABANDONED',
          'UNABLE TO FIND PL/I DSA, SAMPLE ABANDONED',
          'UNABLE TO FIND PL/I DSA, SAMPLE ABANDONED',
          'UNABLE TO FIND INTERRUPT OFFSET, SAMPLE ABANDONED',
          'UNABLE TO FIND INTERRUPT OFFSET, SAMPLE ABANDONED',
          'ERROR LIMIT EXCEEDED, SAMPLING TERMINATED',
          'SAMPLE LIMIT EXCEEDED, SAMPLING TERMINATED',
          'ERROR IN PARAMETER LIST, EXECUTION TERMINATED',
          'ILLEGAL LANGUAGE SPECIFICATION, EXECUTION TERMINATED');
0DCL 1 MOD_TAB(NAM_COUNT) CTL,
         5 NAME          CHAR(8),       /* LOAD MODULE NAME         */
         5 LBD           FIXED BIN(31), /* MODULE START ADDR        */
         5 HBD           FIXED BIN(31), /* MODULE END ADDR          */
         5 COUNT         FIXED BIN(31); /* FREQUENCY COUNT          */
0DCL     INT_TYPE(8)         CHAR(13)STATIC INIT(
               '    OPERATION','PRIVELEGED OP',
               '      EXECUTE','   PROTECTION',
               '   ADDRESSING','SPECIFICATION',
               '         DATA','FIXED OVERFLO');
 DCL     (P,Q,R) POINTER STATIC INIT(NULL); /* GENERAL POINTERS */
 DCL SH2SW CHAR(1)STATIC INIT('0'); /* ON MEANS PRINT 2ND HEADRS     */
 DCL MESS_SW CHAR(1)STATIC INIT('0'); /* ON MEANS MESSAGES TO PRINT  */
 DCL NOEND CHAR(1)STATIC INIT('0'); /* ON MEANS NO END RECORD        */
 DCL LINESW CHAR(1) STATIC INIT('0'); /* ON MEANS LINE NUMBERS       */
 DCL SAVNAME CHAR(8) STATIC INIT(' '); /* SAVE CURRENT NAME          */
 DCL TPROC CHAR(9) STATIC INIT('   MAIN  ');
 DCL TSTMT CHAR(4) STATIC INIT(' ');
 DCL     CH4        CHAR(4) BASED(R);
 DCL PSL CHAR(11)STATIC INIT(' ');
 DCL GRAPH CHAR(85) STATIC INIT(' ');
 DCL STAR CHAR(85) STATIC INIT((85)'*');
 DCL     SUBHEAD1   CHAR(220) VAR INIT('
                 BASIC STATISTICS')STATIC;
 DCL SUBHEAD2 CHAR(100)STATIC INIT(' ');
 DCL SORT_COUNTP PIC'999999' STATIC INIT(0);
 DCL     SORT_COUNTP2    CHAR(6) DEF SORT_COUNTP;
 DCL NOSTMT FIXED BIN(31)INIT(0) STATIC; /* NO STATNO COUNT          */
 DCL RUN_COUNT FIXED BIN(31)INIT(0)STATIC; /* NO OF RUN RECS         */
 DCL INREC FIXED BIN(31)STATIC INIT(0); /* TRACE FILE RECORD COUNT */
 DCL NAM_COUNT FIXED BIN(31)STATIC INIT(0); /* NUMBER OF MODULE NAMES*/
 DCL INT_COUNT FIXED BIN(31)STATIC INIT(0); /* INTERRUPTS PER PROC   */
 DCL INV_COUNT FIXED BIN(31)STATIC INIT(0); /* NO OF INVALID RECORDS */
 DCL PAGENO FIXED BIN(31)STATIC INIT(0); /* REPORT PAGE NUMBER       */
 DCL STL_COUNT FIXED BIN(31)STATIC INIT(0); /* STMT FREQ COUNT       */
 DCL SORT_COUNT FIXED BIN(31)INIT(0)STATIC; /* NUMBER OF SORT RECORDS*/
 DCL     (LINEBIN,OFFSBIN,PERCENT,PERTOT,RCODE)
                   FIXED BIN(31)INIT(0)STATIC; /* TEMPORARIES        */
 DCL     (I,J,K,L) FIXED BIN(31)INIT(0)STATIC; /* COUNTERS           */
 DCL SAVSL FIXED BIN(31)STATIC INIT(0); /* SAVE CURRENT STMT/LINE    */
 DCL     BINWK FIXED BIN(31)STATIC INIT(0);
 %IF TYPE='PLO' %THEN %DO;
 DCL BITWK BIT(32)BASED(ADDR(BINWK));
 DCL 1 SAVBIT            BASED(ADDR(SAVSL)),  /* RE-DEFINITION      */
 %END;
 %ELSE %DO;
 DCL     BITWK BIT(32)ALIGNED BASED(PTR1);
 DCL PTR1 POINTER STATIC,PTR2 POINTER STATIC;
         PTR1=ADDR(BINWK);PTR2=ADDR(SAVSL);
 DCL 1 SAVBIT BASED(PTR2), /* RE-DEFINITION */
 %END;
         5 LINEBIT       BIT(27),
         5 OFFSBIT       BIT(5);
 %IF TYPE='PLO' %THEN %DO;
0DCL(ADDR,ALLOCATION,HIGH,LENGTH,PLIDUMP,PLISRTA,SUBSTR,NULL)BUILTIN;
 %END;
 %ELSE %DO;
0DCL (ADDR,ALLOCATION,HIGH,LENGTH,SUBSTR)BUILTIN,
     IHEDUMP ENTRY EXTERNAL,
     IHESRTA ENTRY(CHAR(*),CHAR(*),FIXED BIN(31),FIXED BIN(31),CHAR(*),
         CHAR(*))EXTERNAL;
 DCL HEXSTR ENTRY RETURNS(CHAR(100)VAR);
 %END;
-/*                      ON UNITS                                   */
0ON ERROR SNAP BEGIN REORDER;
         ON ERROR SYSTEM;
 %IF TYPE='PLO' %THEN %DO;
         CALL PLIDUMP ('T');
 %END;
 %ELSE %DO;
         CALL IHEDUMP;
 %END;
 END;
0ON ENDFILE(TRACE) BEGIN REORDER;
         NOEND = '1';
         MESS_SW = '1';
         GOTO TAB0400;
         END;
0ON ENDPAGE(SYSPRINT) BEGIN REORDER;
         PAGENO = PAGENO + 1;
         IF PAGENO > 1 THEN PUT PAGE;
         PUT EDIT ('PLEA EXTRACTION ANALYSIS','PAGE ',PAGENO)
                    (COL(45),A,COL(100),A,P'ZZ9')
                   ( SUBHEAD1,' ') (SKIP(2),A,SKIP,A);
         IF SH2SW = '1'                 /* SECONDARY SUB HEADING    */
         THEN DO;
               PUT EDIT (SUBHEAD2) (SKIP,A);
               PUT SKIP;
               END;
 END;
-/*                      INITIALISATION                             */
         OPEN FILE(SYSPRINT)LINESIZE(132),FILE(TRACE),FILE(SORTIN);
         Q = ADDR(SORTREC);
         SIGNAL ENDPAGE(SYSPRINT);
0        PUT EDIT ('PL/I OPTIMIZER EXECUTION ANALYSER, VERSION 2.1','')
            (SKIP,A,SKIP(2),A(0));
-/*                      MAIN LOGIC                                 */
0TAB0100:
         READ FILE(TRACE) SET(P);
         INREC = INREC + 1;
         IF RUNIN.OPCODE = 'RUN'        /* RUN RECORD               */
         THEN DO;
               RUN_COUNT = RUN_COUNT + 1;
               RUNSORT = RUNIN, BY NAME;
               IF RUNIN.FLAG1 = '1'B
               THEN RUNSORT.CODE = 'M'; /* MAIN PROC                */
               ELSE RUNSORT.CODE = 'S'; /* SECONDARY PROC           */
               IF RUNIN.FLAG2 = '1'B
               THEN RUNSORT.LINE = '1'; /* LINE NUMBERS             */
               ELSE RUNSORT.LINE = '0'; /* STATEMENT NUMBERS        */
               RUNSORT.CODE2 = '1';     /* TYPE 1 RUN SORT          */
               WRITE FILE(SORTIN) FROM(RUNSORT);
               RUNSORT.CODE2 = '2';     /* TYPE 2 RUN SORT          */
               WRITE FILE(SORTIN) FROM(RUNSORT);
               IF RUNIN.STATLIN = 0     /* NO STAT/LINE NO          */
               THEN NOSTMT = NOSTMT + 1;
               SORT_COUNT = SORT_COUNT + 2;
               GOTO TAB0100;
               END;
0        IF LOADIN.OPCODE = 'LOAD'      /* LOAD MODULE RECORD       */
         THEN DO;
               LOADSORT = LOADIN, BY NAME;
               LOADSORT.CODE = 'K';     /* K VERSION                */
               WRITE FILE(SORTIN) FROM (LOADSORT);
               LOADSORT.CODE = 'L';     /* L VERSION                */
               WRITE FILE(SORTIN) FROM(LOADSORT);
               SORT_COUNT = SORT_COUNT + 2;
               GOTO TAB0100;
               END;
0        IF MESGIN.OPCODE = 'MESG'      /* MESSAGE RECORD           */
         THEN DO;
               MESS_SW = '1';
               MESSTAB(MESSNUM).COUNT = MESSTAB(MESSNUM).COUNT + 1;
               GOTO TAB0100;
               END;
0        IF PGCKIN.OPCODE = 'PGCK'      /* PROGRAM CHECK RECORD     */
         THEN DO;
               MESS_SW = '1';
               PGCKSORT = PGCKIN, BY NAME;
               PGCKSORT.CODE = 'C';
               WRITE FILE(SORTIN) FROM (PGCKSORT);
               SORT_COUNT = SORT_COUNT + 1;
               GOTO TAB0100;
               END;
0        IF ENDIN.OPCODE = 'END'        /* END RECORD               */
         THEN DO;
 TAB0400:
               RUNSORT.CODE = 'Z';      /* END MARKER               */
               RUNSORT.CODE2 = '1';
               RUNSORT.STATLIN = -1;
               RUNSORT.NAME = HIGH(8);
               WRITE FILE(SORTIN) FROM(RUNSORT);
               SORT_COUNT = SORT_COUNT + 1;
               GOTO TAB0800;
               END;
0        IF STARTIN.OPCODE = 'STRT'     /* START RECORD             */
         THEN DO;
               PUT EDIT ('NAME OF LOAD MODULE ANALYSED IS ',STARTIN.PGM)
                         (SKIP,A,A);
               PUT EDIT ('INTERRUPT INTERVAL (TASK CPU TIME) IS ',
                    INTERVAL,' SECONDS')
                    (SKIP,A,P'Z9.99',A);
               GOTO TAB0100;
               END;
0/*      INVALID RECORDS FALL THROUGH                               */
0        INV_COUNT = INV_COUNT + 1;     /* KEEP A COUNT OF INVALIDS */
         IF INV_COUNT > 11
         THEN GOTO TAB0100;
         INVALSORT.CODE = 'A';
         INVALSORT.REC = BUF;
         WRITE FILE(SORTIN) FROM(INVALSORT);
         SORT_COUNT = SORT_COUNT + 1;
         MESS_SW = '1';
         GOTO TAB0100;
0TAB0800:
         PUT EDIT ('NO OF RECORDS ON TRACE FILE',INREC)
                  (SKIP(2),X(4),A,F(7));
         PUT EDIT ('STMT/LINE NO AVAILABLE ',RUN_COUNT-NOSTMT,' TIMES')
                    (SKIP(2),X(9),A,F(6),A);
         PUT EDIT ('NO STMT/LINE NO AVAILABLE ',NOSTMT,' TIMES')
                    (SKIP,X(6),A,F(6),A);
         IF NOEND = '1'                 /* END RECORD MISSING       */
         THEN GOTO TAB0900;
         PUT EDIT ('TOTAL NUMBER OF CALLS ABANDONED ',
                    ICOUNT-RUN_COUNT) (SKIP,A,F(6));
         PUT EDIT ('TOTAL NUMBER OF INTERRUPTIONS ',ICOUNT)
                    (SKIP(2),X(2),A,F(6));
         PUT FILE(SYSPRINT)EDIT('ELAPSED TIME FOR EXTRACTION RUN',
               (END_TIME - START_TIME),' SECONDS')
                         (SKIP(2),A,P'ZZZZZZ9.99',A);
0TAB0900:CLOSE FILE(SORTIN),FILE(TRACE);
         SORT_COUNTP = SORT_COUNT;
1/*      THE SORT FILE IS NOW SORTED IN ASCENDING SEQUENCE IN THE   */
 /*      FOLLOWING ORDER:                                           */
 /*      1. INVALID           CODE=A                                */
 /*      2. PROGRAM CHECK     CODE=C                                */
 /*      3. LOAD MODULES      CODE=K                                */
 /*      4. LOAD MODULES      CODE=L                                */
 /*      5. RUN               CODE=M (MAIN PROC) TYPE 1             */
 /*      6. RUN               CODE=M (MAIN PROC) TYPE 2             */
 /*      7. RUN               CODE=S (SECONDARY PROC) TYPE 1        */
 /*      8. RUN               CODE=S (SECONDARY PROC) TYPE 2        */
 /*         7,8 REPEATED FOR EACH SECONDARY PROC                    */
 /*      9. END MARKER        CODE=Z                                */
 %IF TYPE='PLO' %THEN %DO;
0        CALL PLISRTA
               (' SORT FIELDS=(1,20,CH,A),SIZE=' ×× SORT_COUNTP2 ×× ' ',
                ' RECORD TYPE=F,LENGTH=(28) ',40000,RCODE,'','CP');
 %END;
 %ELSE %DO;
0        CALL IHESRTA
               (' SORT FIELDS=(1,20,CH,A),SIZE=' ×× SORT_COUNTP2 ×× ' ',
                ' RECORD TYPE=F,LENGTH=(28) ',40000,RCODE,'','CP');
 %END;
         IF RCODE = 16
         THEN DO;
               PUT EDIT ('*** SORT FAILED, CHECK SORTOUT LISTING.')
               (SKIP(2),A);
               GOTO TAB9000;
               END;
0/*      HAVING SORTED THE DATA, IT IS READ BACK IN, DIAGNOSTIC     */
 /*      MESSAGES (IF ANY) ARE PRINTED, THE LOAD MODULE TABLE IS    */
 /*      BUILT AND THE STATEMENT/LINE NO COUNTS ARE TABULATED       */
0        OPEN FILE(SORTOUT);
         ON ENDFILE(SORTOUT) BEGIN REORDER;
               PUT EDIT ('*** ERROR IN FILE SORTOUT.') (SKIP(2),A);
               GOTO TAB9000;
               END;
         INV_COUNT = 0;                 /* RESET                    */
0        IF MESS_SW = '1'               /* DIAGNOSTICS PRESENT      */
         THEN DO;
               PUT EDIT('*** DIAGNOSTIC MESSAGES ***',' ')
                    (SKIP(4),COL(30),A,SKIP,A);
               IF NOEND = '1'      /* NO END RECORD                 */
               THEN PUT EDIT ('END RECORD MISSING. ABNORMAL END ',
               'OF TEST. RESULTS WILL BE INCOMPLETE.') (SKIP,A,A);
               END;
0TAB1000:
         READ FILE(SORTOUT) SET(Q);
         IF INVALSORT.CODE = 'A'        /* INVALID RECORDS          */
         THEN DO;
               INV_COUNT = INV_COUNT + 1;
               IF INV_COUNT > 11
               THEN GOTO TAB1000;
               IF INV_COUNT = 11
               THEN PUT EDIT ('*** TOO MANY INVALID RECORDS, ',
                    'NO MORE WILL BE PRINTED') (SKIP(2),A,A);
               ELSE PUT EDIT ('*** INVALID TRACE RECORD: ',
                    HEXSTR(INVALSORT.REC)) (SKIP,A,A);
               GOTO TAB1000;
               END;
0        IF PGCKSORT.CODE = 'C'         /* PROGRAM CHECK            */
         THEN DO;
               PUT EDIT
          ('*** ',INT_TYPE(PGCKSORT.INTCODE),' INTERRUPT AT OFFSET ',
               HEXSTR(PGCKSORT.OFFSET),' IN PLOTRAP.')
               (SKIP,A,A,A,A,A);
               GOTO TAB1000;
               END;
0        DO I = 980 TO 989;
               IF MESSTAB(I).COUNT = 1
               THEN PUT EDIT ('MSG',I,MESSTAB(I).TEXT)
                              (SKIP,A,P'999',X(3),A);
               IF MESSTAB(I).COUNT > 1
               THEN PUT EDIT ('MSG',I,MESSTAB(I).TEXT,MESSTAB(I).COUNT,
                    ' TIMES') (SKIP,A,P'999',X(3),A,F(4),A);
         END;
0        IF MESS_SW = '1'
   THEN PUT EDIT ('*** END OF DIAGNOSTICS ***')(SKIP(2),COL(30),A);
0        IF LOADSORT.CODE ^= 'K'        /* TEST FOR LOAD MODULE RECS*/
         THEN GOTO TAB4000;
1/*      THE NEXT SET OF RECORDS CONTAIN THE LOAD MODULE NAMES      */
 /*      THEY ARE ARRANGED IN TWO IDENTICAL SERIES, THE K AND THE L.*/
 /*      THE K SERIES IS USED TO DETERMINE HOW MANY DIFFERENT LOAD  */
 /*      MODULES ARE PRESENT SO THAT THE CORRECT SIZE OF MOD_TAB    */
 /*      CAN BE ALLOCATED. THE L SERIES IS THEN USED TO RECORD THE  */
 /*      MODULES AND THEIR EXTENTS IN THE TABLE.                    */
 /*      MOD_TAB IS USED BY THE STATEMENT PROCESSING PHASE WHICH    */
 /*      FOLLOWS TO DETERMINE WHERE THE INTERRUPTS TOOK PLACE.      */
0        GOTO TAB2100;        /* WE ALREADY HAVE FIRST 'K' RECORD   */
0TAB2000:
         READ FILE(SORTOUT) SET(Q);
         IF LOADSORT.CODE = 'L'
         THEN GOTO TAB2200;             /* SECOND SERIES STARTS     */
         IF LOADSORT.NAME = SAVNAME
         THEN GOTO TAB2000;
 TAB2100:
         SAVNAME = LOADSORT.NAME;
         NAM_COUNT = NAM_COUNT + 1;     /* NO OF ELEMS IN MOD_TAB   */
         GOTO TAB2000;
 TAB2200:
         ALLOCATE MOD_TAB;              /* SIZE = NAM_COUNT         */
         DO I = 1 TO NAM_COUNT;
               MOD_TAB(I).COUNT = 0;    /* INITIALISE               */
         END;
         I = 0;
         GOTO TAB2400;             /* WE ALREADY HAVE FIRST L RECORD*/
0TAB2300:                     /* READ THE 'L' SERIES AND FILL MOD_TAB*/
         READ FILE(SORTOUT) SET(Q);
         IF LOADSORT.CODE ^= 'L'        /* END OF MODULE RECS       */
         THEN GOTO TAB4000;
         IF LOADSORT.NAME = SAVNAME
         THEN GOTO TAB2300;
 TAB2400:
         SAVNAME = LOADSORT.NAME;       /* RECORD NEW NAME          */
         I = I + 1;
         MOD_TAB(I).NAME = LOADSORT.NAME;
         MOD_TAB(I).LBD = LOADSORT.LOADPT;
         MOD_TAB(I).HBD = LOADSORT.LOADPT + LOADSORT.MODSIZE;
         GOTO TAB2300;
1/*      THE NEXT SET OF RECORDS CONTAIN THE RUN STATISTICS.        */
 /*      THEY ARE SORTED IN ASCENDING SEQUENCE OF PROCEDURE NAME    */
 /*      WITH THE MAIN PROCEDURE FIRST. EACH PROCEDURE HAS TWO SETS */
 /*      OF IDENTICAL RECORDS, THE FIRST IS USED TO COUNT THE       */
 /*      NUMBER OF RECORDS FOR THE BLOCK SO THAT PERCENTAGES CAN    */
 /*      BE CALCULATED WHEN THE TABULATIONS ARE MADE FROM THE       */
 /*      SECOND SET.                                                */
0TAB4000:
   SH2SW = '1';               /* WE NEED 2 SECONDARY HEADERS        */
0  DO WHILE (RUNSORT.NAME ^= HIGH(8)); /* PROCESS RUN RECORDS       */
0     INT_COUNT = 0;                    /* ANOTHER PROC IS STARTING */
      NOSTMT = 0;
      LINESW = '0';
0     DO WHILE(RUNSORT.CODE2 = '1');    /* TYPE 1 FOR THIS PROC     */
         INT_COUNT = INT_COUNT + 1; /*COUNT OF THIS PROCS INTERRUPTS*/
         IF RUNSORT.STATLIN = 0
         THEN NOSTMT = NOSTMT + 1;  /* COUNT OF NO STATNO AVAILABLE */
         ELSE LINESW = RUNSORT.LINE;
         READ FILE (SORTOUT) SET (Q);
      END;
0     SAVNAME = RUNSORT.NAME;      /* NAME OF NEW PROC              */
      IF RUNSORT.CODE = 'S'
      THEN TPROC = 'SECONDARY';
      IF LINESW = '1'
      THEN TSTMT = 'LINE';
      ELSE TSTMT = 'STMT';
      IF INT_COUNT = NOSTMT
      THEN TSTMT = ' ';  /* BLANK IF NO STMT AVAILABLE              */
      SUBHEAD1 = 'STATEMENT TRAP COUNTS FOR ' ×× TPROC
            ×× ' PROCEDURE ' ×× SAVNAME ×× ', OFFSET '
            ×× HEXSTR(RUNSORT.POFFS) ×× ' IN LOAD MODULE.';
      SUBHEAD2 = '      ' ×× TSTMT ×× '   TRAP COUNT   '
      ×× '% THIS PROC  % TOTAL                   TOTAL PERCENTAGE'
      ×× ' GRAPH, * = 1%';
      SIGNAL ENDPAGE(SYSPRINT);         /* WRITE HEADINGS           */
0     DO WHILE(RUNSORT.CODE2 = '2'); /* TYPE 2 FOR THIS PROC */
         SAVSL = RUNSORT.STATLIN;  /* RESET STATEMENT COUNTERS */
         STL_COUNT = 0;
         DO WHILE ((RUNSORT.STATLIN = SAVSL) & (RUNSORT.CODE2 = '2'));
    /* DO ALL RECORDS FOR THIS STATEMENT */
            STL_COUNT = STL_COUNT + 1;  /* COUNTER FOR THIS STATNO  */
            DO I = 1 TO NAM_COUNT; /* RECORD INTERRUPT LOCATIONS    */
               IF RUNSORT.PSW >= MOD_TAB(I).LBD
               THEN IF RUNSORT.PSW < MOD_TAB(I).HBD
                    THEN MOD_TAB(I).COUNT = MOD_TAB(I).COUNT + 1;
            END;
            READ FILE(SORTOUT) SET (Q);
         END;
0        PERCENT = (1000 * STL_COUNT) / INT_COUNT;
         PERTOT  = (1000 * STL_COUNT) / RUN_COUNT;
         J = PERTOT / 10;
         GRAPH = SUBSTR(STAR,1,J);
         IF TSTMT = 'LINE'
         THEN DO;
            BINWK=0;
            SUBSTR(BITWK,28,5)=OFFSBIT;
            OFFSBIN=BINWK+1;       /* GET OFFSET */
            SUBSTR(BITWK,6,27)=LINEBIT;
            LINEBIN=BINWK; /* GET LINE NUMBER */
               PUT STRING(PSL) EDIT (LINEBIN,'.',OFFSBIN)
                               (P'ZZZZZZZ9',A,P'99');
               END;
         ELSE IF TSTMT = 'STMT'
               THEN PUT STRING(PSL) EDIT (SAVSL,' ') (X(3),F(6),A);
               ELSE PSL = ' ';
         PUT EDIT (PSL,STL_COUNT,PERCENT,PERTOT,GRAPH)
           (SKIP,A,X(3),F(7),X(8),P'ZZ9.9',X(5),P'ZZ9.9',X(3),A);
      END;
0     PUT EDIT ('INTERRUPTS FOR THIS PROC ',INT_COUNT)
                    (SKIP(2),A,COL(30),F(6));
      PUT EDIT ('NO STATEMENT NUMBER AVAILABLE ',NOSTMT)
                    (SKIP,A,COL(32),F(4));
      PERTOT = (1000 * INT_COUNT) / RUN_COUNT;
      PUT EDIT ('THIS PROC CONSUMED ',PERTOT,' PERCENT OF THE ',
                    'TOTAL CPU TIME') (SKIP(2),A,P'ZZ9.9',A,A);
   END;
1/*      THE NEXT STEP IS TO TABULATE THE CONTENTS OF THE LOAD      */
 /*      MODULE TABLE, IF ANY.                                      */
0TAB6000:
         IF ALLOCATION(MOD_TAB) = 0     /* BRANCH IF NO TABLE       */
         THEN GOTO TAB9500;
0        SUBHEAD1 = 'EACH LINE REPRESENTS A MODULE LOADED DURING THE'
         ×× ' JOB STEP. NAMES STARTING WITH ''IBMB'' ARE TRANSIENT'
     ×× ' PL/I MODULES.                    COUNT INDICATES WHICH '
         ×× ' ONES WERE BEING EXECUTED WHEN SAMPLES TOOK PLACE';
         SUBHEAD2 = 'NAME        LOCATION       COUNT     PERCENTAGE'
         ×× '        PERCENTAGE GRAPH, * = 2%';
         SIGNAL ENDPAGE(SYSPRINT);
         DO I = 1 TO NAM_COUNT;
               PERCENT = (1000 * MOD_TAB(I).COUNT) / RUN_COUNT;
               J = PERCENT / 20;
               GRAPH = SUBSTR(STAR,1,J);
               R = ADDR(MOD_TAB(I).LBD);
               PUT EDIT (MOD_TAB(I).NAME,HEXSTR(CH4))
                             (SKIP,A(8),X(4),A(8))
                   (MOD_TAB(I).COUNT,PERCENT,SUBSTR(GRAPH,1,50))
                    (X(8),F(4),X(8),P'ZZ9.9',X(8),A(50));
         END;
0        GOTO TAB9500;                  /* NORMAL END OF JOB        */
0TAB9000:
         PUT EDIT ('*** ABNORMAL TERMINATION. CONTACT',
               ' YOUR PLEA PROGRAMMER') (SKIP,A,A);
-/*                 INTERNAL PROCEDURE SECTION                      */
0HEXSTR:PROC(INSTRING)RETURNS(CHAR(100)VAR)REORDER;
0/*-----------------------------------------------------------------*/
 /*                                                                 */
 /*      THE PURPOSE OF THIS ROUTINE IS TO RETURN THE HEXADECIMAL   */
 /*      EQUIVALENT OF THE ARGUMENT 'INSTRING'.                     */
 /*                                                                 */
 /*-----------------------------------------------------------------*/
0DCL     INSTRING        CHAR(*);
 DCL     SOURCE(200)     BIT(4) BASED (SPTR);
 DCL TGT(400)BIT(4)INIT((400)(1)'0000'B)STATIC;
 %IF TYPE='PLO' %THEN %DO;
 DCL     TGT2            CHAR(200) BASED (ADDR(TGT));
 %END;
 %ELSE %DO;
 DCL     TGT2 CHAR(200)BASED(PTR3),
         PTR3 POINTER STATIC;PTR3=ADDR(TGT);
 %END;
 DCL SPTR POINTER STATIC;
0        SPTR = ADDR(INSTRING);
         L = LENGTH(INSTRING) * 2;      /* L = NO OF HEX DIGITS     */
         BINWK=0;
         DO J = 1 TO L;
               IF SOURCE(J) < '1010'B
               THEN DO;                 /* 0 -> 9                   */
                    TGT(J * 2 - 1) = '1111'B;
                    TGT(J * 2) = SOURCE(J);
                    END;
               ELSE DO;                 /* A -> F                   */
                    TGT(J * 2 - 1) = '1100'B;
                    SUBSTR(BITWK,29,4)=SOURCE(J);
                    BINWK=BINWK-9;
                    TGT(J*2)=SUBSTR(BITWK,29,4);
                    END;
         END;
         RETURN (SUBSTR(TGT2,1,L));
 END;
0TAB9500:
 END;
