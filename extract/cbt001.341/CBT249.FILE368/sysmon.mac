         TITLE 'SYSTEM MONITOR PROGRAM'
SYSMON   START 0
         SAVE  (14,12),,SPECIAL-LOOK-ASM-&SYSDATE-&SYSTIME
         LR    REG12,REG15
         USING SYSMON,REG12
         LA    REG02,SAVE
         ST    REG13,4(REG02)
         ST    REG02,8(REG13)
         LR    REG13,REG02
*
         L     REG04,0(REG01)
         OC    2(2,REG04),=2C' '
         CLC   2(2,REG04),=C'GO'
         BNE   M500
         EJECT
NEXTSCR  EQU   *
         CLI   KIND,C'A'             PRINT ALL ?
         BNE   INPTNOTA              NO --- BRANCH
         MVI   SW39,0                *IN-OUT SWITCH*
         MVI   SW45,0                *TSO SWITCH*
         MVI   SW35,0                *SKIP SWITCH*
         B     DOIT
INPTNOTA EQU   *
         CLI   KIND,C'T'             PRINT TSO USERS ONLY ?
         BNE   INPTNOTT              NO --- BRANCH
         MVI   SW45,X'80'
         B     DOIT
INPTNOTT EQU   *
         CLI   KIND,C'B'             EXCLUDE TSO USERS ?
         BNE   INPTNOTB              NO --- BRANCH
         MVI   SW45,X'10'
         B     DOIT
INPTNOTB EQU   *
         CLI   KIND,C'C'             BOTH TSO AND NON-TSO ?
         BNE   INPTNOTC              NO --- BRANCH
         MVI   SW45,X'00'
         B     DOIT
INPTNOTC EQU   *
         CLI   KIND,C'I'             ACTIVE JOBS ONLY ?
         BNE   INPTNOTI              NO --- BRANCH
         MVI   SW39,X'10'
         B     DOIT
INPTNOTI EQU   *
         CLI   KIND,C'O'             WAITING JOBS ONLY ?
         BNE   INPTNOTO              NO --- BRANCH
         MVI   SW39,X'80'
         B     DOIT
INPTNOTO EQU   *
         CLI   KIND,C'J'             WAITING AND ACTIVE JOBS ?
         BNE   INPTNOTJ              NO --- BRANCH
         MVI   SW39,X'00'
         B     DOIT
INPTNOTJ EQU   *
         CLI   KIND,C'Y'             PRINT NAMES IN SKIP TABLE ?
         BNE   INPTNOTY              NO --- BRANCH
         MVI   SW35,X'00'
         B     DOIT
INPTNOTY EQU   *
         CLI   KIND,C'N'             EXCLUDE NAMES IN SKIP TABLE ?
         BNE   DOIT                  NO --- BRANCH
         MVI   SW35,X'80'
         EJECT
DOIT     EQU   *
         LA    REG00,HEADLEN
         STH   REG00,COUNT
         LA    REG07,TIOALINE
         USING PSA,REG00               PREFIXED SAVE AREA
         L     REG15,FLCCVT            LOAD CVT ADDRESS
         USING CVT,REG15               COMMUNICATIONS VECTOR TABLE
         L     REG15,CVTASVT           LOAD ASVT ADDRESS
         DROP  REG15
         USING ASVT,REG15              ADDRESS SPACE VECTOR TABLE
         L     REG03,ASVTMAXU          LOAD ASVTMAXU
         LA    REG02,ASVTENTY-4        ADDR OF 4 BYTES BEFORE ASVTENTRY
         DROP  REG15
NEXTLINE EQU   *
         MVC   ALIN(2),=X'1DF0'
         LA    REG02,4(REG02)          POINT REG02 TO NEXT ASVT ENTRY
         ICM   REG04,15,0(REG02)       REG04 CONTAINS ADDRESS OF ASCB
         BNP   SKIPIT                  SKIP ENTRY IF AVAILABLE (OPEN)
         TM    102(REG04),X'04'        SEE IF SWAPPED OUT ADDRESS SPACE
         IC    REG15,SW39
         EX    REG15,SKIPTEST
         L     REG05,144(REG04)        REG05=ADDRESS OF SRM CONTROL
*                                        BLOCK (ASCBOUCB)
         TM    18(REG05),X'20'         CHECK IF LOGON CREATED USER
         IC    REG15,SW45
         EX    REG15,SKIPTEST
SKIPTEST NOP   SKIPIT
         MVI   ONELINE,C' '
         MVC   ONELINE+1(79),ONELINE
         MVC   JOBNAME,=C'STARTING'
         ICM   REG05,15,172(REG04)     REG05 CONTAINS ADDR OF JOBNAME
         BZ    NOTINITJ
         MVC   JOBNAME,0(REG05)        CHKEY IN CSCB
         MVC   PSTEPNAM,24(REG05)      CHPROCSN IN CSCB
         MVC   STEPNAME,56(REG05)      CHSTEP IN CSCB
         B     COMPNAME
NOTINITJ EQU   *
         ICM   REG05,15,176(REG04)     REG05=ADR OF JOBNAME FOR
*                                       START/MOUNT/LOGON CREATED SPACE
         BZ    AFTRNAME
         MVC   JOBNAME,0(REG05)
         ICM   REG15,15,56(REG04)      REG15=ADDR OF CSCB
         BZ    COMPNAME
         MVC   STEPNAME,16(REG15)
         MVC   PSTEPNAM,8(REG15)
COMPNAME EQU   *
         CLC   JOBNAME,STEPNAME
         BNE   AFTRNAME
         CLC   JOBNAME,=CL8'INIT'
         BE    SKIPIT
AFTRNAME EQU   *
         IC    REG14,SW35
         LM    REG15,REG01,=A(SKIPNAME,8,SKIPEND)
SKIPLOOP EQU   *
         CLC   JOBNAME,0(REG15)
         EX    REG14,SKIPTEST
         BXLE  REG15,REG00,SKIPLOOP
         LH    REG05,152(REG04)        REG05=ALLOC. PAGE FRAME COUNT
         SLL   REG05,2
         CVD   REG05,DBLWD
         MVC   CORE+1(6),MASK1
         ED    CORE,DBLWD+5
         L     REG05,144(REG04)        REG05=ADDRESS OF OUCB
         LH    REG15,78(REG05)         WORKING SET SIZE
         CVD   REG15,DBLWD
         MVC   WSSZ+1(5),MASK1
         ED    WSSZ,DBLWD+5
         TM    18(REG05),X'20'
         BZ    ISNTTSO
         MVI   SORT,C'T'
         MVC   PSTEPNAM,=CL8'        '
         B     AFTRTSO
ISNTTSO  EQU   *
         MVI   SORT,C'S'
AFTRTSO  EQU   *
         MVC   INROUT,=C'IN '
         MVI   ALIN+1,X'F8'
         TM    17(REG05),X'80'
         BZ    NOTNS
         MVC   INROUT,=C'N/S'
         B     AFTRIO
NOTNS    EQU   *
         TM    16(REG05),X'08'
         BZ    NOTWT
         MVC   INROUT,=C'WT '
         MVI   ALIN+1,X'F0'
         B     AFTRIO
NOTWT    EQU   *
         TM    16(REG05),X'04'
         BZ    NOTOUT
         MVC   INROUT,=C'OUT'
         MVI   ALIN+1,X'F0'
         B     AFTRIO
NOTOUT   EQU   *
         TM    16(REG05),X'02'
         BZ    AFTRIO
         MVC   INROUT,=C'TRV'
AFTRIO   EQU   *
         TM    16(REG05),X'E0'
         BZ    NOTTRANS
         MVC   INROUT,=C'<->'
NOTTRANS EQU   *
         UNPK  DBLWD(3),43(2,REG04)
         TR    DBLWD(2),TTABL-240
         MVC   DPRTY,DBLWD
         SR    REG15,REG15
         IC    REG15,24(REG05)         PERFORMANCE GROUP
         CVD   REG15,DBLWD
         MVC   PG+1(3),MASK1+2
         ED    PG,DBLWD+6
         LH    REG15,38(REG05)         SWAP COUNT
         CVD   REG15,DBLWD
         MVC   SC+1(3),MASK1+2
         ED    SC,DBLWD+6
         LM    REG14,REG15,64(REG04)   ELAPSED JOBSTEP TIME (DOUBLE WD)
         SRDL  REG14,18                       OUTPUT IN SECONDS
         SR    REG14,REG14
         SLDL  REG14,6
         D     REG14,=F'10000'
         CVD   REG15,DBLWD
         MVC   TIME+1(8),MASK2
         ED    TIME,DBLWD+4
         LM    REG14,REG15,200(REG04)   ELAPSED SRB TIME (DOUBLE WD)
         SRDL  REG14,18                        OUTPUT IN SECONDS
         SR    REG14,REG14
         SLDL  REG14,6
         D     REG14,=F'10000'
         CVD   REG15,DBLWD
         MVC   TIME2+1(8),MASK2
         ED    TIME2,DBLWD+4
         MVC   0(81,REG07),ALIN
         LA    REG07,81(REG07)
         LH    REG15,COUNT
         LA    REG15,81(REG15)
         STH   REG15,COUNT
         CH    REG15,=Y(TIOALEN)
         BNL   PUTIT
SKIPIT   EQU   *
         BCT   REG03,NEXTLINE
         EJECT
PUTIT    EQU   *
         LH    REG00,COUNT
         TPUT  SCREEN,(0),FULLSCR,HOLD
         TGET  KIND,1,EDIT
         OI    KIND,C' '
M400     EQU   *
         CLI   KIND,C'S'
         BNE   NEXTSCR
M500     EQU   *
         TPUT  SCREEN,8,FULLSCR,HOLD
         L     REG13,4(REG13)
         RETURN (14,12),T,RC=0
         EJECT
REG00    EQU   0
REG01    EQU   1
REG02    EQU   2
REG03    EQU   3
REG04    EQU   4
REG05    EQU   5
REG06    EQU   6
REG07    EQU   7
REG08    EQU   8
REG09    EQU   9
REG10    EQU   10
REG11    EQU   11
REG12    EQU   12
REG13    EQU   13
REG14    EQU   14
REG15    EQU   15
         EJECT
SAVE     DC    9D'0'
DBLWD    DS    D
COUNT    DS    H
KIND     DC    C' '
SW39     DC    X'00'
SW45     DC    X'10'
SW35     DC    X'80'
*
SKIPNAME DC    CL8'*MASTER*'
         DC    CL8'*AUXSTM*'
         DC    CL8'JES2'
         DC    CL8'TCAM'
         DC    CL8'SUDP'
         DC    CL8'APLSV2'
         DC    CL8'APLTSIO2'
         DC    CL8'APLISAM2'
         DC    CL8'MF1'
         DC    CL8'APLSV'
         DC    CL8'APLTSIO'
         DC    CL8'APLISAM'
         DC    CL8'RMFA'
         DC    CL8'OMEGAMON'
         DC    CL8'SCH'
         DC    CL8'TCAMTSO'
         DC    CL8'VIDIO'
         DC    CL8'CICSPKG'
         DC    CL8'CICSPKGC'
         DC    CL8'CICSDB'
         DC    CL8'CICSDBC'
SKIPEND  EQU   *-1
*
MASK1    DC    X'2020202120D2'
MASK2    DC    X'20202021204B2020'
*
*
TTABL    DC    C'0123456789ABCDEF'
*
ALIN     DC    X'1DF0'
ONELINE  DC    CL80' '
JOBNAME  EQU   ONELINE,8
STEPNAME EQU   ONELINE+10,8
PSTEPNAM EQU   ONELINE+19,8
CORE     EQU   ONELINE+28,6
WSSZ     EQU   ONELINE+35,6
SORT     EQU   ONELINE+43,1
INROUT   EQU   ONELINE+45,3
DPRTY    EQU   ONELINE+49,2
PG       EQU   ONELINE+51,4
SC       EQU   ONELINE+55,4
TIME     EQU   ONELINE+60,9
TIME2    EQU   ONELINE+70,9
SCREEN   DC    X'C11140403C4040001140401D4013401DF840401DF8'
         DC    C'JOB  STEP     PROCSTEP    CORE   WSS  T LOC '
         DC    C'DP  PG  SC  CPU(SEC)  SRB(SEC)'
HEADLEN  EQU   *-SCREEN
TIOALINE DS    23CL81
TIOALEN  EQU   *-SCREEN
         LTORG
         EJECT
         CVT   DSECT=YES,LIST=YES
         EJECT
         IHAPSA
         EJECT
         IHAASVT
SYSMON   CSECT
         END   SYSMON
