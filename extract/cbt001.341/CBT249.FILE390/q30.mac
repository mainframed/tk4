ROUTE    QSTART 'QUEUE COMMAND - REROUTE A JOB     Q30'
         GBLB  &QACF2
         L     R15,=V(CHKAUTH)    BRANCH TO AUTHORIZATION
         BALR  R14,R15                         CHECK ROUTINE
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB)     ADDR OF MODULE TO FIND JOB
         BALR  R14,R15             GO TO IT
         AIF   (&QACF2).ACF1
         TM    QFLAG1,QXPASS       IS USER AUTHORIZED
         BO    COMMAND             YES   BYPASS JOBNAME CHECKING
***********************************************************************
*                                                                     *
*   ENSURE JOBNAME BEGINS WITH USER ID                                *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA          ADDR OF JCT
         USING JCTDSECT,R9        BASE REG FOR JCT
         LA    R2,7               MAXIMUM LENGTH OF USER ID
         LA    R3,QLOGON+7        LAST BYTE OF USER ID
LOOP     CLI   0(R3),C' '         IS THIS BYTE BLANK?
         BNE   CHECK              NO. CONTINUE.
         BCTR  R3,0               TRY PREVIOUS BYTE
         BCT   R2,LOOP            TRY AGAIN
CHECK    EX    R2,CLC             IS THE JOBNAME VALID?
         BNE   TILT               NO. TILT.
.ACF1    ANOP  ,                  ACF2 HAS ALREADY CHECKED AUTHORITY
         SPACE 4
***********************************************************************
*                                                                     *
*   BUILD ROUTE COMMAND AND ISSUE SVC                                 *
*                                                                     *
***********************************************************************
COMMAND  EQU   *
         CLI   QPARM2,C' '           WAS SECOND PARM BLANK
         BE    NOTSO                 YES
         MVC   RALL,=C'$RALL,J=T'    MOVE FIRST PART OF COMMAND
         B     YESTSO                BARCH AROUND JOB COMMAND
NOTSO    MVC   RALL,=C'$RALL,J=J'    MOVE FIRST PART OF COMMAND
YESTSO   MVC   JOBN,QPARM1           MOVE JOB NUMBER TO COMMAND
         MVC   DEST,=C',D=LOCAL '    MOVE SECOND PART OF COMMAND
         LA    R1,L'RALL+L'JOBN+L'DEST  LOAD  LENGTH OF COMMAND
         LA    R1,4(,R1)                ADD   LENGTH OF HEADER
         STH   R1,SVC34PL               STORE LENGTH OF COMMAND
         SR    R0,R0                 CLEAR REG ZERO FOR SVC
         STH   R0,SVC340             CLEAR RESERVED AREA
         MODESET  MODE=SUP,KEY=ZERO  GET SUPERVISOR STATE AND KEY 0
         LA    R1,SVC34PL            LOAD ADDRESS OF COMMAND AREA
         SVC   34                    ISSUE COMMAND
         LR    R2,R15                SAVE RETURN CODE
         MODESET  MODE=PROB,KEY=NZERO  RETURN TO PROBLEM STATE AND KEY
         LTR   R2,R2                 CHECK RETURN CODE FROM SVC 34
         BZ    COMMOK                COMMAND WAS EXECUTED
         QTILT '**** ERROR IN ROUTE COMMAND ****'
COMMOK   QTILT '**** JOB HAS BEEN RE-ROUTED ****'
         EJECT
         AIF   (&QACF2).ACF2
CLC      CLC   QLOGON(1),JCTJNAME    IS THE JOBNAME EQUAL TO USERID
TILT     QTILT '**** JOBNAME MUST BEGIN WITH USERID  ****'
.ACF2    ANOP
         LTORG
BUFSTART EQU   0
BUFDSECT EQU   0
        $JCT
         QCOMMON
         ORG   QWRKSPC
SVC34PL  DS    0H             ASSUME HALF-WORD BOUNDARY
SVC34LEN DC    AL2(L'RALL+L'JOBN+L'DEST)    LENGTH OF COMMAND
SVC340   DC    H'0'           RESERVED
RALL     DS    CL9            FIRST HALF
JOBN     DS    CL4            JOB NUMBER
DEST     DS    CL9            SECOND HALF
         END
