DISPLAY  QSTART 'QUEUE COMMAND - 3270 DISPLAY ROUTINES   Q5'
         GBLB  &QPFK          PF-KEY OPTION. DEFINED BY QSTART     ICBC
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
         USING QCPRINT,R9     BASE REG FOR PRINT WORK AREA         FCI*
         L     R9,QVPRINT     ADDR OF PRINT WORK AREA              FCI*
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         LH    R4,QDMLNG      LOAD MSG LENGTH
         CH    R4,QDSCRLEN    IS THE MSG LENGTH > SCRSIZE?         MOD5
         BH    RETURN         YES. GO AWAY.
         MVC   QPRSAVE,QDTLINE  SAVE SUBTITLE LINE ON ENTRY        FCI*
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR         YES. WRITE SCREEN.
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         AH    R4,QDNEXT      ADD CURRENT LOCATION ON SCREEN
         CH    R4,QDSCRLEN    IS THERE ROOM ON THE SCREEN?         MOD5
         BH    WRTSCR         NO. WRITE SCREEN.
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER
         LR    R1,R4          SAVE LINE NUMBER
         LA    R4,QDLINE1(R4) LOAD ADDRESS OF NEXT LINE
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE
         LH    R5,QDLNELEN    LENGTH MUST BE MULTIPLE OF LINESIZE  MOD5
DSP3     CR    R5,R7          IS 5 NOT LESS THAN 7?
         BNL   DSP4           YES. GO DO IT.
         AH    R5,QDLNELEN    INCR BY LINE LENGTH
         B     DSP3           TRY AGAIN
DSP4     AR    R1,R5          UPDATE LINE NUMBER
         STH   R1,QDNEXT      STORE LINE NUMBER
         MVCL  R4,R6          MOVE THE MESSAGE TO THE SCREEN
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
         EJECT
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY                               *
*                                                                     *
***********************************************************************
WRTSCR   DS    0H                                                   RGR
         CLI   QDAPLUS,C' '      IS ALTERNATE CONTINUATION BLANK?   RGR
         BE    WRTSCR0           YES, DON'T USE IT                  RGR
         MVC   QDPLUS,QDAPLUS    NO,  MOVE IT INTO SCREEN           RGR
         MVI   QDAPLUS,C' '           AND CLEAR IT                  RGR
WRTSCR0  DS    0H                                                   RGR
         LA    R1,QDLINE1               ENSURE PRINTABILITY        MOD5
         LH    R15,QDSCRLEN             LENGTH TO TRANSLATE        MOD5
         LA    R14,255                  SET FOR EXECUTE            MOD5
WRTSCR1  CR    R15,R14                  SEE IF ONLY ONE NEEDED     MOD5
         BNH   WRTSCRN                  YES, DO IT                 MOD5
         EX    R14,WRTSCRTR             TRANSLATE PART OF BUFFER   MOD5
         LA    R1,1(R1,R14)             POINT TO NEXT SLOT         MOD5
         SR    R15,R14                  DROP FOR LENGTH DONE       MOD5
         BCTR  R15,0                    END FOR EXECUTE FIX        MOD5
         B     WRTSCR1                  LOOP TILL DONE             MOD5
         SPACE 1                                                   MOD5
WRTSCRN  BCTR  R15,0                    DROP FOR EXECUTE           MOD5
         LTR   R15,R15                  TEST FOR NULL              MOD5
         BM    *+8                      SKIP IF NULL               MOD5
         EX    R15,WRTSCRTR             TRANSLATE END OF BUFFER    MOD5
         SPACE 1                                                   MOD5
TPUTSCRN DS    0H                                                  MOD5
         MVC   QCONST,=C'C='                                        BFG
         MVC   QDONST,=C' D='                                       BFG
         L     R1,16                  LOAD CVT ADDR                 BFG
         L     R1,604(R1)             LOAD ADDR OF RMCT             BFG
         L     R1,228(R1)             LOAD ADDR OF RCT              BFG
         LH    R15,64(R1)             LOAD RCTCPUA FIELD            BFG
         SRL   R15,4                  GET VALUE                     BFG
         CVD   R15,QDWORD                                           BFG
         UNPK  QCPU,QDWORD                                          BFG
         OI    QCPU+2,X'F0'                                         BFG
         LH    R15,74(R1)                                           BFG
         CVD   R15,QDWORD                                           BFG
         UNPK  QDPR,QDWORD                                          BFG
         OI    QDPR+2,X'F0'                                         BFG
         LH    R0,QDSCRPLN            LOAD LENGTH FOR TPUT         MOD5
         TPUT  QDSCREEN,(0),FULLSCR,MF=(E,QTPUT)                   MOD5
         MVC   QDTLINE,QPRSAVE        RESTORE SUBTITLE INFO        FCI*
         AIF  (&QPFK).PFK1    SKIP NON-PFK CODE                    ICBC
         TGET  QDREPLY,63,MF=(E,QTGET)                             PWF*
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?
         BNE   NOCLEAR        NO. CONTINUE.
         TCLEARQ INPUT        CLEAR THE QUEUE
NOCLEAR  STH   R1,QDRLNG      STORE LENGTH OF REPLY
         AGO   .PFK2                                               ICBC
.PFK1    ANOP                                                      ICBC
         LA    R6,QDREPLY                                          ICBC
         XC    PFREPLY,PFREPLY                                     ICBC
         XC    QDREPLY,QDREPLY                                     ICBC
         TGET  PFREPLY,69,ASIS,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?         ICBC
         BNE   NOCLEAR        NO. CONTINUE.                        ICBC
         TCLEARQ INPUT        CLEAR THE QUEUE                      ICBC
NOCLEAR  IC    R4,PFCODE                                           ICBC
         XR    R5,R5                                               ICBC
         N     R4,=X'0000000F'   EXTRACT PF-KEY NUMBER             ICBC
         CH    R4,=H'12'                                           ICBC
         BH    ENTKEY           "ENTER" KEY                        ICBC
         BCTR  R4,0                                                ICBC
         MH    R4,=H'5'                                            ICBC
         LA    R5,PFKTAB                                           ICBC
         LA    R5,0(R4,R5)                                         ICBC
         MVC   QDREPLY(5),0(R5)  MOVE PF-KEY VALUE                 ICBC
         LA    R5,5                                                ICBC
         LA    R6,3(,R6)                                           ICBC
ENTKEY   EQU   *                                                   ICBC
         SH    R1,=H'3'                                            ICBC
         BZ    NOTXT                                               ICBC
         MVC   0(60,R6),PFTXT                                      ICBC
         LA    R5,3                                                ICBC
         SH    R1,=H'3'                                            ICBC
NOTXT    LA    R1,0(R5,R1)                                         ICBC
         STH   R1,QDRLNG      STORE LENGTH OF REPLY                ICBC
.PFK2    ANOP                                                      ICBC
         OC    QDREPLY,QBLANK     UPPERCASE THE COMMAND            FCI*
         CLI   QDREPLY,C'$'       IS IT A JES2 COMMAND              BFG
         BE    JES2CMD            YES, PROCESS IT                   BFG
         CLC   =C'PR',QDREPLY     POSSIBLE PRINT COMMAND       PWF FCI*
         BNE   CLSCRN             NOPE..SPLIT NORMALLY             FCI*
*                                                                  FCI*
         L     R15,=V(PRINT)  FETCH PRINT ENTRY ADDRESS            FCI*
         BALR  R14,R15        AND CALL HIM                         FCI*
         LTR   R15,R15        HOW IS HIS RETURN CODE               FCI*
         BZ    TPUTSCRN       GO REPOST SCREEN                     FCI*
CLSCRN   LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE           FCI*
         LH    R5,QDSCRLEN    LOAD LENGTH OF SCREEN                MOD5
         SR    R6,R6          NO SENDING FIELD NEEDED              FCI*
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER            FCI*
         SR    R7,R7          FILL SCREEN WITH NULLS               FCI*
         MVCL  R4,R6          CLEAR THE SCREEN                     FCI*
*
         LH    R1,QDRLNG      STORE LENGTH OF REPLY
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW
         B     DSP2           CONTINUE PROCESSING
INTER    L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
         EJECT
***********************************************************************
*                                                                     *
*   JES2 COMMANDS USING SVC 34                                        *
*                                                                     *
***********************************************************************
JES2CMD  EQU   *
         TM    QFLAG1,QXPASS       IS USER AUTHORIZED?
         BNO   TPUTSCRN            NO RETURN TO DISPLAY
         L     R15,=V(CHKAUTH)     BRANCH TO AUTHORIZATION
         BALR  R14,R15                           CHECKING ROUTINE
         MVC   QJESCMD,QBLANK      BLANK OUT COMMAND AREA
         MVC   QJESCMD,QDREPLY     PUT IN REST OF COMMAND
         LA    R1,1(R1)            ADD 1 MORE CHARACTERS
         STH   R1,SVC34PL          STORE LENGTH
         SR    R0,R0               CLEAR REG ZERO
         STH   R0,SVC340           ZERO RESERVED AREA
         MODESET  MODE=SUP,KEY=ZERO  GET SUPERVISOR STATE AND KEY 0
         LA    R1,SVC34PL          LOAD ADDRESS OF COMMAND AREA
         SVC   34                  ISSUE COMMAND
         LR    R2,R15              SAVE RETURN CODE
         MODESET  MODE=PROB,KEY=NZERO  RETURN TO PROBLEM STATE
         LTR   R2,R2               CHECK RETURN CODE
         BZ    TPUTSCRN            RE-DISPLAY SCREEN
         QTILT '**** ERROR IN JES2 COMMAND ****'
         EJECT
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
WRTSCRTR TR    0(*-*,R1),TABLE          FIX UNPRINTABLES           MOD5
         LTORG
         AIF  (NOT &QPFK).PFK3                                     ICBC
* PF-KEY DEFINITIONS                                               ICBC
PFKTAB   DS    0CL120                                              ICBC
PF1      DC    CL5'?'                                              ICBC
PF2      DC    CL5'DB'                                             ANRP
PF3      DC    CL5'E'                                              ICBC
PF4      DC    CL5'DEL *'                                          ANRP
PF5      DC    CL5'F'                                              ICBC
PF6      DC    CL5'DI'                                             ICBC
PF7      DC    CL5'PB'                                             MOD5
PF8      DC    CL5'PF'                                             MOD5
PF9      DC    CL5'DO'                                             ICBC
PF10     DC    CL5'CO 1'                                           ICBC
PF11     DC    CL5'CO 41'                                          ICBC
PF12     DC    CL5'ST'                                             ICBC
PF13     DC    CL5'?'                                              ANRP
PF14     DC    CL5'DB'                                             ANRP
PF15     DC    CL5'E'                                              ANRP
PF16     DC    CL5'DEL *'                                          ANRP
PF17     DC    CL5'F'                                              ANRP
PF18     DC    CL5'DI'                                             ANRP
PF19     DC    CL5'PB'                                             MOD5
PF20     DC    CL5'PF'                                             MOD5
PF21     DC    CL5'DO'                                             ANRP
PF22     DC    CL5'CO 1'                                           ANRP
PF23     DC    CL5'CO 41'                                          ANRP
PF24     DC    CL5'ST'                                             ANRP
.PFK3    ANOP                                                      ANRP
         EJECT
* TABLE OF PRINTABLE CHARACTERS
TABLE    DC    CL64' '
         DC    CL48'          Ö.<(+×&&         !$*);^-/         ,%_>?'
         DC    CL16'          :#@''=" '
         DC    127AL1(*-TABLE)        REST OF TABLE IS OK         MOD5
         ORG   TABLE+138
         DC    C'       '
         ORG   TABLE+154
         DC    C'        '
         ORG   TABLE+170
         DC    C'                       '
         ORG   TABLE+202
         DC    C'       '
         ORG   TABLE+218
         DC    C'        '
         ORG   TABLE+234
         DC    C'      '
         ORG   TABLE+251
         DC    C'      '
         ORG
         QCOMMON
         ORG   QWRKSPC
SVC34PL  DS    0H                     ASSUME HALF WORD BOUNDARY
SVC34LEN DC    AL2(L'QJESCMD)         LENGTH OF COMMAND
SVC340   DC    H'0'                   RESERVED
QJESCMD  DS    CL40                   COMMAND AREA
         END
