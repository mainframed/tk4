DOPTION  QSTART 'QUEUE COMMAND - CHANGE CURRENT DISPLAY OPTIONS   Q33'
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
***********************************************************************
*                                                                     *
*   DETERMINE FUNCTION REQUESTED                                      *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,=CL80'**** DISPLAY OPTION CHANGE LIST ****'
         LA    R2,QPARM1          ADDR 1ST PARM                   LBDMC
         LA    R3,4               4 PARM FIELDS                   LBDMC
OPTLOOP  DS    0H                                                 LBDMC
         BAL   R4,CHGOPT          CHANGE OPTION                   LBDMC
         LA    R2,12(R2)          POINT NEXT PARM                 LBDMC
         BCT   R3,OPTLOOP         AND CONTINUE                    LBDMC
         TM    QFLAG1,QDSPBLK+QDSPCC ANY OPTIONS IN EFFECT        LBDMC
         BZ    NOOPTS              NO.                            LBDMC
         QWTO  'DISPLAY OPTION CHANGES ARE IN EFFECT'             LBDMC
         BAL   R4,DISPOPT         DISPLAY OPTIONS                 LBDMC
         B     RETURN             RETURN                          LBDMC
NOOPTS   QWTO  'DEFAULT DISPLAY OPTIONS ARE IN EFFECT'            LBDMC
         BAL   R4,DISPOPT         DISPLAY OPTIONS                 LBDMC
RETURN   QSTOP ,                  RETURN TO CALLER                LBDMC
***********************************************************************
*                                                                     *
*   CHANGE OPTIONS                                                    *
*                                                                     *
***********************************************************************
CHGOPT   DS    0H                                                 LBDMC
         CLC   =C'NOBL',0(R2)     DON'T SHOW BLANK LINES?         LBDMC
         BNE   OPT2                NO, CHECK OPTION 2             LBDMC
         OI    QFLAG1,QDSPBLK     DON'T SHOW BLANK LINES          LBDMC
         BR    R4                 ONLY 1 OPTION PER PARM          LBDMC
OPT2     DS    0H                                                 LBDMC
         CLC   =C'BL',0(R2)       SHOW BLANK LINES?               LBDMC
         BNE   OPT3                NO, CHECK OPTION 3             LBDMC
         NI    QFLAG1,255-QDSPBLK SHOW BLANK LINES                LBDMC
         BR    R4                 ONLY 1 OPTION PER PARM          LBDMC
OPT3     DS    0H                                                 LBDMC
         CLC   =C'CC',0(R2)       SHOW CARRIAGE CONTROL?          ANRPC
         BNE   OPT4                NO, CHECK OPTION 4             LBDMC
         OI    QFLAG1,QDSPCC      SHOW CARRIAGE CONTROL           ANRPC
         BR    R4                 ONLY 1 OPTION PER PARM          LBDMC
OPT4     DS    0H                                                 LBDMC
         CLC   =C'NOCC',0(R2)     DON'T SHOW CARRIAGE CONTROL?    ANRPC
         BNE   OPT5                NO, CHECK OPTION 4             LBDMC
         NI    QFLAG1,255-QDSPCC DON'T SHOW CARRIAGE CONTROL      ANRPC
         BR    R4                 ONLY 1 OPTION PER PARM          LBDMC
OPT5     DS    0H                                                 LBDMC
         CLC   =C'DEF',0(R2)      RESET TO DEFAULT OPTIONS?       LBDMC
         BNER  R4                  NO, RETURN TO CALLER           LBDMC
         NI    QFLAG1,255-(QDSPBLK+QDSPCC)   SET DEFAULT OPTION   LBDMC
         BR    R4                 ONLY 1 OPTION PER PARM          LBDMC
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
DISPOPT  DS    0H                                                 LBDMC
         TM    QFLAG1,QDSPBLK     DON'T SHOW BLANK LINES?         LBDMC
         BNO   DISPOPT2            NO, CHECK OPTION 2             LBDMC
         QWTO  '2 OR MORE BLANK LINES WILL BE EDITED TO 1 BLANK LINE'
         B     DISPOPT3                                           LBDMC
DISPOPT2 QWTO  'BLANK LINES IN SYSOUT DATASETS WILL BE DISPLAYED'
DISPOPT3 TM    QFLAG1,QDSPCC      SHOW OVERFLOW DATA?             ANRPC
         BNO   DISPOPT4            NO, CHECK OPTION 4             LBDMC
         QWTO  'CARRAIGE CONTROL CHARACTERS WILL BE DISPLAYED'
         BR    R4                                                 LBDMC
DISPOPT4 QWTO  'CARRIAGE CONTROL CHARACTERS WILL NOT BE DISPLAYED'
         BR    R4                                                 LBDMC
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
         QCOMMON
         END
