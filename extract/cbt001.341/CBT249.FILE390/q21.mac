SYSLOG   QSTART 'QUEUE COMMAND - PRINT THE SYSTEM LOG DATASET   Q21'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         TM    QFLAG1,QXPASS  IS USER AUTHORIZED
         BNO   NONO           NO  DISALLOW COMMAND
         MVI   QCODE,X'00'    TURN OFF QCODE FLAG
***********************************************************************
*                                                                     *
*   SUPPLY DEFAULT SYSLOG                                             *
*                                                                     *
***********************************************************************
         CLI   QPARM1,C' '           ?/SPECIFIC SYSLOG SPECIFIED
         BNE   CALLFJOB              YES. BYPASS DEFAULT AND CALL JOB
         MVC   QPARM1,QLOGON         TSO USERID
         MVC   QLNG1,=H'8'           LENGTH OF QPARM1
         L     R15,=V(FINDJOB)       ADDRESS OF FINDJOB
         BALR  R14,R15               GO TO FINDJOB
         SR    R1,R1                 CLEAR REG
         L     R2,QCJQEA             A(JQE)
         USING JQEDSECT,R2           JQE ADDRESSABILITY
         IC    R1,JQEFLAGS           INSERT JQE FLAGS
         N     R1,=X'00000007'       TURN OFF USELESS BITS
         MVC   QPARM1,=CL8'SYSLOG'   JOBNAME FOR JQE SEARCH
         MVC   QLNG1,=H'8'           LENGTH OF JOBNAME
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
CALLFJOB L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK JOBNAME EQUAL SYSLOG                                        *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF IOAREA FOR JCT
         USING JCTDSECT,R9    BASE REG FOR JCT
         CLC   JCTJNAME,=CL8'SYSLOG' IS THIS SYSLOG?
         BNE   TILT           NO. TILT.
         EJECT
***********************************************************************
*                                                                     *
*   DETERMINE NUMBER OF DATASETS FROM CURRENT DATASET                 *
*                                                                     *
***********************************************************************
         LH    R3,JCTPDDBK    HIGHEST DATASET ID NUMBER
         LH    R2,QLNG2       LENGTH OF BACKUP PARM
         SH    R2,=H'1'       IS THE BACKUP PARM ZERO LENGTH?
         BM    CALLLIST       YES. SKIP.
         CLI   QPARM2,C'-'    IS THERE A MINUS SIGN?
         BNE   PLUS           NO. SKIP.
         MVI   QPARM2,C'0'    CHANGE MINUS TO ZERO
PLUS     MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,QDWORD      CONVERT TO BINARY
         SR    R3,R2          BACK UP NUMBER OF DATASETS
         CH    R3,=H'101'     IS THE NUMBER LESS THAN 101?
         BL    TILT           YES. TILT.
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST STH   R3,QPDSID      STORE DATASET ID
         MVC   QPOFFSET,=H'0' PRINT OFFSET ZERO
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
         EJECT
TILT     QTILT '*** INVALID PARAMETER ***'
NONO     QTILT '**** USER NOT AUTHORIZED ****'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM2     CHECK FOR NUMERIC
PACK     PACK  QDWORD,QPARM2(1)      CONVERT TO BINARY
         LTORG
BUFSTART EQU   0
BUFDSECT EQU   0
        $JCT
        $JQE
         QCOMMON
         END
