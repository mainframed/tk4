CKPT     QSTART 'QUEUE COMMAND - READ JES2 CKPT RECORDS    Q3'
         USING QCKPT,R10      BASE REG FOR HASP WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   READ CHECKPOINT MASTER RECORD                                     *
*                                                                     *
***********************************************************************
         LA    R1,HASPCKPT             SET DCB ADDR
         ST    R1,QIOBDCB                IN IOB
         L     R1,HASPCKPT+DCBDEBAD    LOAD DEB ADDRESS
DCBDEBAD EQU   44,4
         MVC   QIOBSEEK+1(6),36(R1)    SET CCHH FROM DEB
         MVI   QIOBR,3                 SET RECORD THREE
         MVC   QCCWS+5(1),QCKPSCTR     SET SECTOR NUMBER
         MVC   QCCWREAD+1(3),QCJQTL+1  SET CHECKPOINT
         MVC   QCCWREAD+6(2),QMASTERL   ADDR & LENGTH
         L     R2,QCCWNEXT             GET 1ST CCW PACKET
         USING CCWDSECT,R2
         TM    DEBDVMOD(R1),B'00011000'
         BO    NOMTRK                  B IF WE CANNOT DO MULTI-TRACK
         CLC   QIOBCC,CCWSEEK+3        1ST PACKET & MSTR ON SAME CYL ?
         BNE   NOMTRK                  B IF NOT
         MVI   QCCWREAD+4,X'40'        CHAIN THE MASTER CKPT READ
         LA    R1,CCWSEEK+1             TO THE 1ST CCW PACKET BY
         STCM  R1,B'0111',QCCWREAD+9     ADDING A SEEK HEAD AND
         LA    R1,CCWSET                  A TIC TO THE CCWS
         STCM  R1,B'0111',QCCWREAD+17      *
         MVI   QCCWREAD+16,X'08'            *
         B     *+8
NOMTRK   LA    R2,QCCWNEXT-(CCWNEXT-CCWDSECT)
         LA    R1,QCCWS                SET
         ST    R1,QIOBSTRT              IOBSTART
         XC    QIOBECB,QIOBECB         CLEAR ECB
         B     READ                    GO ENTER LOOP
LOOP     MVC   QIOBSEEK,CCWSEEK        SET MBBCCHHR
         LA    R1,CCWSET               SET
         ST    R1,QIOBSTRT              IOBSTART
         XC    QIOBECB,QIOBECB         CLEAR ECB
READ     BAL   R14,DIO                 GO READ MASTER RECORD
         ICM   R2,B'1111',CCWNEXT      GET NEXT CCW PACKET
         BNZ   LOOP                    GO READ NEXT RECORD
         MVI   QCCWREAD+4,X'00'        TURN COMMAND CHAINING OFF
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
         QSTOP
         EJECT
***********************************************************************
*                                                                     *
*   DO ECXP IO AND ABORT IF I/O ERROR                                 *
*                                                                     *
***********************************************************************
DIO      XC    QIOBECB,QIOBECB   CLEAR ECB
         EXCP  QIOB              EXECUTE CHANNEL PROGRAM
         WAIT  ECB=QIOBECB       WAIT TIL DONE
         CLI   QIOBECB,X'7F'     Q. NORMAL COMPLEATION ?
         BER   R14               EXIT IF NORMAL
         SYNADAF ACSMETH=EXCP,PARM1=QIOB
         MVC   4(21,R1),=C'I/O ERROR ON HASPCKPT'
         LA    R14,4(,R1)        GET ADDRESS
         LH    R15,0(,R1)        GET LENGTH
         TPUT  (R14),(R15),EDIT,MF=(E,QTPUT)
         ABEND 95,DUMP
         SPACE 4
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
DEBDVMOD EQU   X'44',1,C'B'            TEST FILE MASK IN DEB
         QCCW
         QCOMMON
         END
