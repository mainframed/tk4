READSPC  QSTART 'QUEUE COMMAND - READ A BLOCK FROM HASPACE   Q17'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CONVERT MTTR TO MBBCCHHR                                          *
*                                                                     *
***********************************************************************
*
* NOTE - THE INPUT TO THIS ROUTINE IS AS FOLLOWS:
*            QCTRAK CONTAINS THE MTTR
*            R1 CONTAINS THE IOAREA ADDRESS
*
         STCM  R1,7,QCCWREAD+1  SET IOAREA IN CCW
         MVC   QCCWREAD+6(2),QBUFSIZE
         SR    R3,R3            ZERO R3
         IC    R3,QCTRAKM       RELATIVE DCB NUMBER
         SLL   R3,2             MULTIPLY BY 4
         MVC   QIOBR,QCTRAKR    MOVE RECORD NUMBER
         LH    R5,*-*           LOAD
         ORG   *-2               RELATIVE
         DC    S(QCTRAKTT)        TRACK NUMBER
         SR    R4,R4            ZERO R4
         D     R4,QCTRKCYL(R3)  DIVIDE TRACKS BY TRACKS PER CYLINDER
         STH   R4,*-*           STORE
         ORG   *-2               HEAD
         DC    S(QIOBHH)          NUMBER
         STH   R5,*-*           STORE
         ORG   *-2               CYLINDER
         DC    S(QIOBCC)          NUMBER
         L     R3,QCSPOOLS(R3)  GET DEVICE TYPE CODE & DCB ADDRESS
         STCM  R3,7,QIOBDCB+1   SET DCB ADDRESS IN IOB
***********************************************************************
*                                                                     *
*              CODE TO COMPUTE SECTOR ADDRESS                         *
*                                                                     *
***********************************************************************
         STM   R14,R12,12(R13)   SAVE REGISTERS
         SR    R0,R0             SET ..0.
         ICM   R0,12,QBUFSIZE    SET XX..
         IC    R0,QIOBR          SET ...R
         LA    R2,QCCWS+5        SET RETURN POINTER
         N     R3,=X'FF000000'   SET DEVICE TYPE
         OR    R2,R3                CODE IN REG 2
         L     R15,CVTPTR        ADDRESS OF CVT
         USING CVT,R15           SET TEMP ADDRESSABILITY
         L     R15,CVT0SCR1      ADDRESS OF SECTOR CONV. RTN
         DROP  R15               DROP TEMP ADDRESSABILITY
         BALR  R14,R15           COMPUTE SECTOR #
         LM    R14,R12,12(R13)   RESTORE REGISTERS
         EJECT
***********************************************************************
*                                                                     *
*   DO ECXP IO AND ABORT IF I/O ERROR                                 *
*                                                                     *
***********************************************************************
         LA    R3,QCCWS         GET CCW ADDRESS
         ST    R3,QIOBSTRT       SET 1'ST CCW ADDRESS IN IOB
         XC    QIOBECB,QIOBECB   CLEAR ECB
         EXCP  QIOB              EXECUTE CHANNEL PROGRAM
         WAIT  ECB=QIOBECB       WAIT TIL DONE
         CLI   QIOBECB,X'7F'     Q. NORMAL COMPLEATION ?
         BE    QSTOP             EXIT IF NORMAL
         SYNADAF ACSMETH=EXCP,PARM1=QIOB
         MVC   4(21,R1),=C'I/O ERROR ON HASPACE'
         LA    R14,4(,R1)        GET ADDRESS
         LH    R15,0(,R1)        GET LENGTH
         TPUT  (R14),(R15),EDIT,MF=(E,QTPUT)
         ABEND 95,DUMP
QSTOP    QSTOP
         SPACE 4
         LTORG
         CVT   DSECT=YES         MAP THE CVT
         QCOMMON
         END
