         MACRO
&NAME    QSTART &TITLE,&MAIN=NO,&TYPE=NORMAL,&SEC=NO,&REGS=YES
&NAME    START 0
.**********************************************************************
.*                                                                    *
.*  GLOBAL FLAG DEFINITIONS                                           *
.*                                                                    *
.**********************************************************************
         GBLB  &QPFK          PF-KEY OPTION (SEE Q5 AND Q8)
&QPFK    SETB  1
.*  QPFK=0 SELECTS NO PF-KEY SUPPORT
.*  QPFK=1 SELECTS PF=KEY SUPPORT
         GBLB  &QACF2         ACF2 AUTH CHECKING (SEE Q6 AND Q14)  FCI*
&QACF2   SETB  1                                                   FCI*
.* QACF2=0 SELECTS NO ACF2 CHECKING (SHOPS WITHOUT ACF2)           FCI*
.* QACF2=1 SELECTS ACF2 AUTH TO SYSOUT VIA DSN='SYSOUT.LID.JOBNAME'
.*         SEE Q6 (FINDJOB) FOR ACF2 AUTH CHECKING                 FCI*
         GBLB  &QGEN          FORCE PRINT GEN OF MACRO EXPANSIONS
&QGEN    SETB  0
.*  QGEN=0 SELECTS PRINT NOGEN OPTION
.*  QGEN=1 SELECTS PRINT GEN   OPTION
.**********************************************************************
         AIF   ('&TYPE' EQ 'GLOBAL').GEN   FORCE PRINT GEN FOR QCOMMON
         AIF   (&QGEN).GEN
         PRINT NOGEN
         AGO   .START
.GEN     PRINT GEN
.START   ANOP
 MNOTE *,'PFK=&QPFK, ACF2=&QACF2, GEN=&QGEN '                   PWF FCI
         AIF    ('&REGS' EQ 'NO').NOREGS
***********************************************************************
*                                                                     *
*   REGISTER USAGE TABLE                                              *
*                                                                     *
***********************************************************************
R0       EQU   0              TEMP WORK
R1       EQU   1              TEMP WORK
R2       EQU   2              WORK REG
R3       EQU   3              WORK REG
R4       EQU   4              WORK REG
R5       EQU   5              WORK REG
R6       EQU   6              WORK REG
R7       EQU   7              WORK REG
R8       EQU   8              WORK REG
R9       EQU   9              ADDRESS OF COMMON SUB-AREA
R10      EQU   10             ADDRESS OF COMMON SUB-AREA
R11      EQU   11             ADDRESS OF COMMON VECTOR TABLE
R12      EQU   12             BASE REGISTER
R13      EQU   13             SAVE AREA AND TEMPORARY WORK AREA
R14      EQU   14             TEMP WORK
R15      EQU   15             TEMP WORK
.NOREGS  ANOP
***********************************************************************
*                                                                     *
*   THE QUEUE COMMAND WAS WRITTEN FOR YOUR AMUSEMENT AND AMAZEMENT BY *
*     THE INTERGALACTIC MESSIANIC INDIVIDUAL GROUP THERAPY DIVISION   *
*     OF TRW SYSTEMS, 1 SPACE PARK, REDONDO BEACH, CA. 90278          *
*   THE ORIGINAL PROGRAMMING WAS DONE BY ANDY ZIDE, CHIEF PROGRAMMER  *
*     AND RESIDENT FLAKE WHO HAS SINCE DEPARTED TRW TO PLAY WITH      *
*     MICROCODE.                                                      *
*   CHANGES FOR ANR PRODUCTION COMPANY BY TED BROWN AND GLENN HARPER  *
*     FOR THE FOLLOWING ITEMS:                                        *
*     1.  JES2 SP 1.3.3 SUPPORT                                       *
*     2.  ACF2 SUPPORT UPDATED TO THE 3.1.4/3.1.5 LEVEL               *
*     3.  TSO COMMAND SUPPORT UPDATED TO PREVENT 706 ABENDS           *
*     4.  MOD5 SUPPORT FROM JACK SHUDDELL OF U OF F EXPANDED TO DUMP  *
*         AND HISTORY                                                 *
*     5.  JES2 COMMANDS WILL USE SVC 34 INSTEAD OF INTERNAL READER    *
*     6.  NEW SELECTION ROUTINE OBTAINED FROM LANE BRYANT (FILE 308)  *
*     7.  IF REQUESTING DIFFERENT UNIT AND VOLUME FOR CHECKPOINT THE  *
*         SUBSYSTEM NAME IS ALSO REQUIRED.                            *
*     8.  WHEN DUMPING THE PDDB(S) FOR A JOB A SPECIFIC PDDB MAY      *
*         NOW BE REQUESTED.                                           *
*     9.  WHEN DUMPING THE JOE(S) FOR A JOB THE WORK AND CHAR. JOES   *
*         ARE DISPLAYED.                                              *
*    10.  THERE HAVE BEEN REPORTS OF INDISCRIMANATE 0C4 ABENDS UNDER  *
*         MVS/XA.  THESE OCCUR BECUASE THE UTILITIZATION FIGURES      *
*         USED IN THE DISPLAY ROUTINE (Q5) ARE ABOVE THE 16MB LINE.   *
*    11.  THE WORK AREA IN QCOMMON IS USED WHENEVER POSSIBLE, AND     *
*         WHEN NOT POSSIBLE THERE IS A 2K WORK AREA POINTED TO BY     *
*         REG 13.  BE SURE TO LEAVE THE FIRST 72 BYTES ALONE AS THIS  *
*         IS THE SAVE AREA USED BY THE PROGRAM.                       *
*                                                                     *
*   PLEASE ADDRESS ANY COMMENTS, SUGGESTIONS, OR COMPLAINTS TO        *
*        GLENN HARPER               (713) 623-0300                    *
***********************************************************************
*                                                                     *
*   THE FOLLOWING INDIVIDUALS HAVE MADE MODIFICATIONS TO QUEUE WHICH  *
*     HAVE BEEN INCORPORATED INTO THIS CURRENT VERSION.               *
*                                                                     *
*        VILKO MACEK                                                  *
*        INSURANCE CORPORATION OF BRITISH COLUMBIA                    *
*        MODIFICATION: PFK SUPPORT                                    *
*                                                                     *
*        TRW ISD                                                      *
*        ANAHEIM, CALIFORNIA                                          *
*        MODIFICATION: DISPLAY CPU TIME FOR BATCH, STC AND TSO        *
*                      DISPLAY INITIATORS                             *
*                                                                     *
*        KEN TRUE                                                     *
*        FAIRCHILD CAMERA AND INSTRUMENT                              *
*        MOUNTAIN VIEW, CALIFORNIA                                    *
*        MODIFICATION: ACF2 SUPPORT                                   *
*                      PRINT SCREEN SUPPORT                           *
*                                                                     *
*        W. A. SAPP                                                   *
*        CALIFORNIA HEALTH AND WELFARE                                *
*        MODIFICATION: BYPASS CHECKPOINT READ FOR CERTAIN COMMANDS    *
*                                                                     *
*        TED BROWN AND GLENN HARPER                                   *
*        AMERICAN NATURAL RESOURCES PRODUCTION COMPANY                *
*        5075 WESTHEIMER  SUITE 1000 EAST                             *
*        HOUSTON, TEXAS  77056                                        *
*        MODIFICATION: SEE ABOVE                                      *
*                                                                     *
***********************************************************************
*
         AIF   ('&TYPE' EQ 'NORMAL').GO
         MEXIT
.GO      ANOP
&NAME    TITLE &TITLE
         USING *,R12          BASE REGISTER
         AIF   ('&SEC' EQ 'NO').NS1
         USING *+4095,R10     SECOND BASE REGISTER
.NS1     ANOP
         USING QCOMMON,R11    ACCESS TO COMMON VECTOR TABLE
         STM   R14,R12,12(R13) STANDARD REGISTER SAVE
         LR    R12,R15        LOAD BASE REG
         AIF   ('&SEC' EQ 'NO').NS2
         LA    R10,4095(R12)  LOAD THE SECOND BASE
.NS2     ANOP
         B     *+28           BRANCH AROUND IDENTIFIER
         DC    CL8'&NAME'     MODULE IDENTIFIER
         DC    CL8'&SYSDATE'  ASSEMBLY DATE
         DC    CL8' &SYSTIME' ASSEMBLY TIME
         AIF   ('&MAIN' EQ 'YES').MAINYES
         LR    R15,R13        RETAIN SAVE AREA ADDR
         LA    R13,72(R13)    POINT TO NEXT SAVE AREA
         AGO   .MAINNO
.MAINYES ANOP
         LR    R2,R1          SAVE PARAMETER REGISTER
         GETMAIN R,LV=2048    GET STORAGE FOR SAVEAREA
         ST    R1,8(R13)      FORWARD POINTER
         ST    R13,4(R1)      BACKWARD POINTER
         LR    R13,R1         MOVE ADDR TO R1
         MEXIT
.MAINNO  ANOP
         ST    R13,8(R15)     FORWARD POINTER
         ST    R15,4(R13)     BACKWARD POINTER
         MEND
