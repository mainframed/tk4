JMSG     QSTART 'QUEUE COMMAND - LIST THE SYSTEM MESSAGES   Q13'
         GBLB  &QACF2
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15         GO TO IT
         AIF   (&QACF2).ACF1
         TM    QFLAG1,QXPASS   IS USER AUTHORIZED
         BO    LISTIT          YES
***********************************************************************
*                                                                     *
*   ENSURE JOBNAME BEGINS WITH USER ID                                *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF JCT
         USING JCTDSECT,R9    BASE REG FOR JCT
         LA    R2,7           MAXIMUM LENGTH OF USER ID
         LA    R3,QLOGON+7    LAST BYTE OF USER ID
LOOP     CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BNE   CHECK          NO. CONTINUE.
         BCTR  R3,0           TRY PREVIOUS BYTE
         BCT   R2,LOOP        TRY AGAIN
CHECK    EX    R2,CLC         IS THE JOBNAME VALID?
         BNE   TILT           NO. TILT.
.ACF1    ANOP  ,              ACF2 HAS ALREADY CHECKED AUTHORITY
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
LISTIT   MVC   QPOFFSET,=H'0' PRINT OFFSET FOR EACH RECORD
         MVC   QPDSID,=H'4'   DSID OF DATASET TO BE PRINTED
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
         EJECT
         AIF   (&QACF2).ACF2
TILT     QTILT '**** JOBNAME MUST START WITH USERID ****'
CLC      CLC   QLOGON(1),JCTJNAME     IS THE JOBNAME EQUAL TO USERID
.ACF2    ANOP
         LTORG
BUFSTART EQU   0
BUFDSECT EQU   0
        $JCT
         QCOMMON
         END
