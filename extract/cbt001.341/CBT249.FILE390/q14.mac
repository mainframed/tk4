LIST     QSTART 'QUEUE COMMAND - PRINT A DATASET FROM SPOOL BY ID  Q14'
         GBLB  &QACF2         IS ACF2 AUTH CHECKING TO BE DONE     FCI*
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
         AIF   (&QACF2).ACF1
***********************************************************************
*                                                                     *
*   ENSURE JOBNAME BEGINS WITH USER ID                                *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF JCT
         USING JCTDSECT,R9    BASE REG FOR JCT
         LA    R2,7           MAXIMUM LENGTH OF USER ID
         LA    R3,QLOGON+7    LAST BYTE OF USER ID
LOOP     CLI   0(R3),C' '     IS THIS BYTE BLANK?
         BNE   CHECK          NO. CONTINUE.
         BCTR  R3,0           TRY PREVIOUS BYTE
         BCT   R2,LOOP        TRY AGAIN
CHECK    EX    R2,CLC         IS THE JOBNAME VALID?
         BNE   TILT2          NO. TILT.
.ACF1    ANOP  ,              ACF2 HAS ALREADY CHECKED AUTHORITY
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE DATASET ID NUMBER                           *
*                                                                     *
***********************************************************************
         LH    R2,QLNG2       LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'       IS THE DATASET ID FIELD ZERO LENGTH?
         BM    TILT           YES. QUIT.
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         ZAP   QDWORK,=P'0'   CLEAR WORK AREA
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,QDWORK      CONVERT TO BINARY
         SPACE 1
         TM    QFLAG1,QXPASS  IS USER AUTHORIZED?
         BO    ANYDSID        YES, ALLOW ANY DSID
         SPACE 1
         CH    R2,=H'101'     IS THE DATASET ID LESS THAN 101?
         BL    TILT           YES. TILT.
ANYDSID  STH   R2,QPDSID      STORE DATASET ID
***********************************************************************
*                                                                     *
*   CHECK AND CONVERT THE PRINT OFFSET                                *
*                                                                     *
***********************************************************************
         MVC   QPOFFSET,=H'0' DEFAULT TO ZERO
         LH    R2,QLNG3       LENGTH OF OFFSET FIELD
         SH    R2,=H'1'       IS THE OFFSET FIELD ZERO LENGTH?
         BM    CALLLIST       YES. USE ZERO OFFSET.
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ2        MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         ZAP   QDWORK,=P'0'   CLEAR WORK AREA
         EX    R2,PACK2       PACK THE FIELD
         CVB   R2,QDWORK      CONVERT TO BINARY
         STH   R2,QPOFFSET    STORE OFFSET
         SPACE
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST L     R15,=V(LISTDS)  ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
         SPACE 4
TILT     QTILT '*** DATASET ID OR OFFSET INVALID ***'
         AIF   (&QACF2).ACF2
TILT2    QTILT '*** JOBNAME MUST BEGIN WITH USERID ***'
CLC      CLC   QLOGON(1),JCTJNAME    IS THE JOBNAME EQUAL TO USERID
.ACF2    ANOP
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM2     CHECK FOR NUMERIC
MVZ2     MVZ   QFZONES(1),QPARM3     CHECK FOR NUMERIC
PACK     PACK  QDWORK,QPARM2(1)      CONVERT TO BINARY
PACK2    PACK  QDWORK,QPARM3(1)      CONVERT TO BINARY
         LTORG
BUFSTART EQU   0
BUFDSECT EQU   0
        $JCT
         QCOMMON
         END
