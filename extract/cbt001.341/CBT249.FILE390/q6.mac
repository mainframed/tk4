FINDJOB  QSTART 'QUEUE COMMAND - LOCATE JQE, JCT, AND IOT    Q6',      X
               REGS=NO
         GBLB  &QACF2         ACF2 CHECKING FOR AUTH               FCI*
         AIF   (NOT &QACF2).NACF1                                  FCI*
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
*   ADD ACF2 AUTH CHKING FOR USER AUTH TO LOOK AT STUFF.           FCI*
*    UPDATE FOR ACF2 RELEASE 3.1.4                                ANRP*
*    OPER CHKS ALL..                                               FCI*
*    USER CHKS OWN GOODIES (LOGONID = LOGONID IN JCB)              FCI*
*    OTHER: ISSUE ACFVLD CHK FOR AUTH                              FCI*
*******************************************************************FCI*
         GBLC  &ACF2JCB                                           ANRP*
         #ACFJES2 ,                                               ANRP*
.NACF1   ANOP                                                      FCI*
         USING QCKPT,R10      BASE REG FOR CHECKPOINT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   DETERMINE IF SEARCH IS BY JOBNUMBER OR JOBNAME                    *
*                                                                     *
***********************************************************************
         STC   R1,AFFBYTE1    ONLY VALID IF CALLER IS SYSLOG.
         LH    R1,QLNG1       LENGTH OF PARAMETER FIELD
         SH    R1,=H'1'       IS THE LENGTH ZERO?
         BM    TILT           YES. TILT.
         CLI   QPARM1,C'0'    IS THE FIRST CHARACTER NUMERIC?
         BL    JOBNAME        NO. SEARCH BY JOBNAME
         MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R1,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. TILT.
         EX    R1,PACK        PACK THE FIELD
         CVB   R5,QDWORK      CONVERT TO BINARY
         EJECT
***********************************************************************
*                                                                     *
*   LOCATE JQE BY JOBNAME                                             *
*                                                                     *
***********************************************************************
JOBNAME  LA    R1,$JQTYPES
         L     R2,QCJQHEAD
         B     ENDJQE         SKIP FREE QUEUE
NEXTJQT  ICM   R3,7,1(R2)
         BZ    ENDJQE         END OF QUEUE
NEXTJQE  AL    R3,QCJQTA
         USING JQEDSECT,R3    BASE REG FOR JQE
         CLI   QPARM1,C'0'    IS SEARCH BY JOBNUMBER?
         BL    CLCNAME        NO. SEARCH BY JOBNAME.
         CH    R5,JQEJOBNO    IS THIS THE RIGHT JOBNUMBER?
         BNE   NOMATCH        NO. TRY NEXT JQE.
         B     FOUND          YES. PROCESS IT.
CLCNAME  CLC   QPARM1,JQEJNAME IS THIS THE RIGHT JOBNAME?
         BE    FOUND          YES. PROCESS IT.
NOMATCH  ICM   R3,7,JQENEXT+1
         BNZ   NEXTJQE
ENDJQE   LA    R2,$JQHEADL(,R2)
         BCT   R1,NEXTJQT
TILT     QTILT '*** JOBNAME NOT FOUND OR INVALID ***'
***********************************************************************
*                                                                     *
*   READ JCT AND IOT                                                  *
*                                                                     *
***********************************************************************
FOUND    CLC   =C'SL',QSUBNAME     ?/SYSLOG COMMAND
         BNE   FOUND1              NO. CONTINUE NORMAL PROCESSING
         CLC   QPARM1,=CL8'SYSLOG' ?/SPECIAL SYSLOG PROCESSING
         BNE   FOUND1              NO. CONTINUE NORMAL PROCESSING
         MVC   AFFBYTE2,JQEFLAGS   SAVE JQEFLAGS.
         NI    AFFBYTE2,X'07'      TURN OFF USELESS BITS
         CLC   AFFBYTE1,AFFBYTE2   ?/SYSLOG FOR THIS SYSTEM
         BNE   NOMATCH             NO. CONTINUE JQE SEARCH
FOUND1   ST    R3,QCJQEA           SAVE THE ADDRESS
         MVC   QCTRAK,JQETRAK      DISK ADDR OF JCT
         DROP  R3
         L     R3,QCJCTA           ADDR OF IOAREA FOR JCT
         LR    R1,R3               PARM FOR READSPC
         L     R15,=V(READSPC)     ROUTINE TO READ HASPACE
         BALR  R14,R15             GO TO IT
         USING JCTDSECT,R3         BASE REG FOR JCT
         L     R0,QCJQEA           LOAD  ADDRESS OF JQE
         SL    R0,QCJQTA           MINUS ADDRESS OF JQT
         CL    R0,JCTJQE           IS THE JQE STILL VALID?
         BNE   TILT                NO ABORT
         MVC   QPJOBID,JCTJBKEY    JOB IDENTIFICATION
         AIF    (NOT &QACF2).NACF2                                 FCI*
         EJECT
*******************************************************************FCI*
* MOD 1 - K TRUE  - 22 OCT 79 -                                    FCI*
* MOD 2 - G HARPER - 4 MARCH 83 -                                 ANRP*
*                                                                  FCI*
         TM    QFLAG1,QXPASS  IS USER QUEUE AUTHORIZED?           ANRP*
         BO    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
*                                                                  FCI*
         ICM   R8,15,&ACF2JCB  TEST, GET ACF2 AREA OFFSET         ANRP*
         BZ    ACF2OK          BRANCH IF NONE FOUND               ANRP*
         CH    R8,QBUFSIZE     IS JCB STILL WITHIN JCT            ANRP*
         BH    NOJCB           NO SHOW ERROR                      ANRP*
**********************************************                    ANRP*
****   THE FOLLOWING SUBTRACTION IS BECAUSE THE IOB IS NOT USED   ANRP*
****     IN QUEUE AND THEREFORE THE JCT ADDRESS NEEDS TO BE       ANRP*
****     REDUCED ACCORDINGLY.                                     ANRP*
         S     R8,QBUFLEN       ADJUST FOR HASP BUFFER PREFIX     ANRP*
**********************************************                    ANRP*
         LA    R8,0(R3,R8)      CALCULATE ADDRESS                 ANRP*
         USING QACFJCB,R8       ADDRESS ACF2 JCT                  ANRP*
         CLC   QLOGON,QLID                                        ANRP*
*                      SEE IF USER'S LOGONID=JOB'S ACF LOGON ID   ANRP*
         BE    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
*                                                                  FCI*
*** ALLOW READ-ONLY ACCES TO JOBS WITH A GROUP                    ANRP*
*                                                                 ANRP*
         CLC   =C'DE',QSUBNAME   IS THIS DELETE?                  ANRP*
         BE    NOTOK             YES DISALLOW                     ANRP*
         CLC   =C'SA',QSUBNAME   IS THIS SAVE?                    ANRP*
         BE    NOTOK             YES DISALLOW                     ANRP*
         CLI   QSUBNAME,C'R'     IS THIS REQ OR ROUTE?            ANRP*
         BE    NOTOK             YES DISALLOW                     ANRP*
*                                                                 ANRP*
         CLC   QLOGON(2),QLID                                     ANRP*
*                      SEE IF USER'S LOGONID = SAME GROUP JOB LID ANRP*
         BE    ACF2OK         YES..CONTINUE FORTHWITH              FCI*
*                                                                  FCI*
         MVC   DSNAME,=CL44'SYSOUT. '  INITIALIZE DSNAME TO USE    FCI*
         XC    ACFSPARM(ACFSPRML),ACFSPARM   CLEAR REQUEST BLOCK   FCI*
         MVI   ACFSPREQ,ACFSPRDS  DSNAME ACCESS ONLY               FCI*
         MVI   ACFSPID1,ACFSPIUR  DIS AM DE USER TALKING....       FCI*
         LA    R7,DSNAME          GET DSNAME ADDRESS               FCI*
         ST    R7,ACFSPDSN        AND GIVE IT TO ACF PARM LIST     FCI*
*                                                                  FCI*
*  GENERATE DSNAME OF FORMAT 'SYSOUT.LOGONID.JOBNAME' FOR CHKING   FCI*
*                                                                  FCI*
         MVC   DSNAME+7(8),QLID    MOVE LID TO DSNAME              FCI*
         CLI   DSNAME+7,C' '       IS THE LID BLANK?               FCI*
         BNE   ADSNCHK0            NO..NORMAL PROCESS              FCI*
         DROP  R8               DROP ADDRESSABILITY               ANRP*
*                                                                  FCI*
*  GOT HERE BECAUSE LID IS ' ' (BLANK)..SUBSTITUTE 'SYSTEM'        FCI*
*                                                                  FCI*
         MVC   DSNAME+7(8),=CL8'SYSTEM'                            FCI*
         SPACE 2                                                   FCI*
ADSNCHK0 LA    R1,DSNAME+7         GET ADDRESS                     FCI*
         LA    R7,8                LOAD COUNT                      FCI*
ADSNCHK  CLI   0(R1),C' '          LOOK FOR BLANK                  FCI*
         BE    ADSNCHK1            GOTIT..                         FCI*
         LA    R1,1(R1)            BUMP AND                        FCI*
         BCT   R7,ADSNCHK            GRIND                         FCI*
ADSNCHK1 MVI   0(R1),C'.'          MOVE IN PERIOD..                FCI*
         MVC   1(8,R1),JCTJNAME    MOVE IN JOBNAME                 FCI*
         SPACE 2
         ACFINCVT R8,NONE=ACF2OK                                  ANRP*
         USING   ACCVT,R8         ESTABLISH ADDRESSABILITY        ANRP*
         ACFSVC ACFSPARM,TYPE=S,NONE=ACF2OK,CVT=HAVE              ANRP*
         DROP R8
*                                                                  FCI*
         LTR   R15,R15            HOW DID YOU LIKE THEM APPLES?    FCI*
         BZ    ACF2OK             ..OK BY YOU...CONTINUE..         FCI*
         TCLEARQ  INPUT    CLEAR INPUT QUEUE TO PREVENT LOOP      ANRP*
***  ACCESS DENIED  MUST CHANGE COMMAND TO PREVENT LOOPING        ANRP*
         MVC   QSUBNAME,=CL8'STATUS'  CHANGE COMMAND              ANRP*
         B     STOP                      AND RETURN               ANRP*
         SPACE 2
NOTOK    QTILT '*** SORRY..NO ACF2 AUTHORITY TO LOOK AT THIS JOB'
NOJCB    QTILT '*** SORRY..NO ACF2 JCB AVAILABLE TO CHECK AUTH'   ANRP*
*******************************************************************FCI*
*******************************************************************FCI*
ACF2OK   DS    0H                                                  FCI*
.NACF2   ANOP                                                      FCI*
         MVC   QCTRAK,JCTIOT      DISK ADDR OF IOT
         L     R1,QCIOTA          ADDR OF IOAREA FOR IOT
         L     R15,=V(READSPC)    ROUTINE TO READ HASPACE
         BALR  R14,R15            GO TO IT
         USING IOTDSECT,R1
         CLC   QPJOBID,IOTJBKEY   IS THE IOT STILL VALID
         BNE   TILT               NO
         DROP  R1,R3
STOP     QSTOP
         EJECT
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM1     CHECK FOR NUMERIC
PACK     PACK  QDWORK,QPARM1(1)      CONVERT TO BINARY
         LTORG
         COPY  $HASPGBL
BUFSTART EQU   0
BUFDSECT EQU   0
        $HASPEQU
        $JCT
        $JQE
        $PCE
        $PDDB
        $TAB
        $JOE
        $JOT
        $CAT
        $MIT
        $HCT
        $IOT
         $QINDEX
         QCOMMON
         ORG  QWRKSPC
AFFBYTE1 DS    C
AFFBYTE2 DS    C
DSNAME   DS    CL44
         AIF    (NOT &QACF2).NACF3
WORK     DSECT
         DS   4CL72          BIG FILLER
         ACDSV DSECT=NO
         ACCVT
         ACUCB
         QACFJCB TYPE=QUEUE                                       ANRP*
.NACF3   ANOP
         END
