00010004PROC     0 PRINT NOPRINT TERMINAL NOTERMINAL TEST NOTEST
00020004IF       &TEST = TEST +
00030004THEN     CONTROL PROMPT   LIST   CONLIST   SYMLIST MSG
00040004ELSE     CONTROL PROMPT NOLIST NOCONLIST NOSYMLIST
00050004IF       &PRINT    = PRINT    | &NOPRINT    = NOPRINT    | +
00060004         &TERMINAL = TERMINAL | &NOTERMINAL = NOTERMINAL | +
00070004         &TEST     = TEST     | &NOTEST     = NOTEST       +
00080004THEN     SETDEF &PRINT &NOPRINT &TERMINAL &NOTERMINAL &TEST &NOTEST
00090000   GLOBAL &HEXVALUE &VALUE &RELCODE
00100000/**********************************************************************/
00110000/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
00120000/*     H     H   E         A     A   D     D   E         R     R      */
00130000/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
00140000/*     H     H   E         A     A   D     D   E         R   R        */
00150000/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
00160000/*                                                                    */
00170000/**********************************************************************/
00180000/*  CLIST NAME: IMSDIAG           REL/DATE: 1.0/03.21.79              */
00190000/*                                                                    */
00200000/*  DOC: THIS IS THE MAIN MENU CLIST FOR IMSDIAG.   IT ALLOWS THE     */
00210000/*       USER TO SELECT THE OPTION HE WISHES TO RUN. UPON COMPLETION  */
00220000/*       OF EACH OPTION THE USER IS RETURNED TO THIS MENU.            */
00230000/*                                                                    */
00240000/*  SYMBOLS EQUATED: NONE                                             */
00250000/*                                                                    */
00260000/*  SYMBOLS FOUND: NONE                                               */
00270000/*                                                                    */
00280000/*  RETURN: NONE                                                      */
00290000/*                                                                    */
00300000/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT TSO COMMAND             */
00310003/*                    CLRSCRN BE INSTALLED IN YOUR ENVIRONMENT.       */
00320000/*                                                                    */
00330000/*  USE: USER WILL BE PROMPTED FOR INPUT AS TO SELECTION.             */
00340000/*                                                                    */
00350000/*SJM/RGM**************************************************************/
00360006            L SCD NODISPLAY
00370006            IF &LASTCC^=0 THEN DO
00380006            SET &RELCODE=X
00390006            GOTO BEGIN
00400006            END
00410006            EVAL SCD+14 LE(2)
00420006            SET &RELCODE=&LASTCC
00430006           IF &RELCODE=276 THEN SET &RELCODE=4
00440006           IF &RELCODE=277 THEN SET &RELCODE=5
00450004   BEGIN:   /* CLRSCRN */ /* CLEAR THE SCREEN */ +
00460000           SET &CTR=1
00470000            NOTE '****************************************************'
00480000            NOTE '*******   IMSDIAG    DUMP ANALYSIS CLIST   *********'
00490000            NOTE '****************************************************'
00500000            WRITE *    OPTIONS AVAILABLE:                            *
00510000            WRITE *       (1) INITIALIZE IMS ADDRESS SPACE POINTERS  *
00520000            WRITE *           (MUST BE RUN FIRST)                    *
00530000            WRITE *       (2) ANALYZE IMS SCD                        *
00540000            WRITE *       (3) ANALYZE SAPS                           *
00550000            WRITE *       (4) ANALYZE ZIBS                           *
00560000            WRITE *       (5) ANALYZE CLBS AND CTBS                  *
00570000            WRITE *       (X) EXIT CLIST                             *
00580000            WRITE *    ENTER  1, 2, 3, 4, 5, OR X.                   *
00590000            WRITE ****************************************************
00600000           WRITE
00610000           WRITENR ENTER OPTION DESIRED ===>
00620000            READ &ANSWR
00630000            IF &ANSWR LT 1 | &ANSWR GT 5 THEN GOTO ISITEXIT
00640000            IF &ANSWR = &STR(1) THEN DO      /* OPTION 1 IS IMSINIT */
00650005            %IMSINIT &TEST &NOTEST            /* GO THERE */
00660001            L SCD NODISPLAY
00670001            IF &LASTCC^=0 THEN DO
00680001            SET &RELCODE=X
00690001            GOTO BEGIN
00700001            END
00710001            EVAL SCD+14 LE(2)
00720001            SET &RELCODE=&LASTCC
00730001           IF &RELCODE=276 THEN SET &RELCODE=4
00740001           IF &RELCODE=277 THEN SET &RELCODE=5
00750000            GOTO BEGIN                       /* BACK TO MAIN MENU   */
00760000            END
00770000            IF &ANSWR = &STR(2) THEN DO      /* OPTION 2 IS IMSSCD  */
00780005            %IMS&RELCODE.SCD &TEST &NOTEST
00790004                               /* GO TO EITHER IMS4SCD OR IMS5SCD   */
00800000            GOTO BEGIN                       /* BACK TO MAIN MENU   */
00810000            END
00820000            IF &ANSWR = &STR(3) THEN DO      /* OPTION 3 IS IMSSAPS */
00830005            %IMSSAPS &TEST &NOTEST
00840000            GOTO BEGIN                       /* BACK TO MAIN MENU   */
00850000            END
00860000            IF &ANSWR = &STR(4) THEN DO      /* OPTION 4 IS IMSZIBS */
00870005            %IMSZIBS &TEST &NOTEST
00880000            GOTO BEGIN                       /* BACK TO MAIN MENU   */
00890000            END
00900000            IF &ANSWR = &STR(5) THEN DO      /* OPTION 5 IS IMSCLBS */
00910005            %IMS&RELCODE.CLB &TEST &NOTEST
00920000            GOTO BEGIN                       /* BACK TO MAIN MENU   */
00930000            END
00940000  ISITEXIT: IF &ANSWR = &STR(X) THEN EXIT    /* IF 'X' THEN EXIT    */
00950000            WRITE INVALID REPLY: DEPRESS ENTER AND TRY AGAIN.
00960000            READ
00970000            GOTO BEGIN
