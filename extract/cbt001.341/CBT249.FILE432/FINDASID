00010000PROC     1     JOBNAME DEBUG
00020000/**********************************************************************/
00030000/*       IPCS CLIST TO FIND THE ASID OF A SPECIFIED JOB NAME.  THE    */
00040000/*       PARAMETER SPECIFIES THE NAME OF A BATCH OR DEMAND SELECT JOB.*/
00050000/*       IF THE JOB IS FOUND, THE ASID IS DISPLAYED, AND IS SET AS    */
00060000/*       THE DEFAULT.                                                 */
00070000/**********************************************************************/
00080000IF       &DEBUG = DEBUG +
00090000THEN     CONTROL PROMPT LIST CONLIST SYMLIST
00100000SET      &NAME = &SUBSTR(1:8,&JOBNAME       )
00110000SET      &ASCBJBNI = 172N
00120000SET      &ASCBJBNS = 176N
00130000SET      &ASVTAVAL = X'80'
00140000SET      &ASVTFRST = 524N
00150000SET      &ASVTMAXU = 516N
00160000EVALUATE ASVT STRUCTURE(ASVT)
00170000SET      &ASID = 1
00180000EVALUATE ASVT+&ASVTMAXU LENGTH(4)
00190000SET      &LASTASID = &LASTCC
00200000SET      &JOBASID = 0
00210000DO       WHILE &ASID <= &LASTASID && &JOBASID = 0 /* ASID LOOP */
00220000         SET   &ASVTENTY = &STR(ASVT+&ASVTFRST+&EVAL(&ASID*4)N)
00230000         COMPARE ADDRESS(&ASVTENTY) +
00240000                 WITH(VALUE(&ASVTAVAL)) LENGTH(1)
00250000         IF    &LASTCC ^= 0 +
00260000         THEN  DO    /* CHECK JOB NAME */
00270000               EQUATE XASCB &ASVTENTY% STRUCTURE(ASCB)
00280000               SET   &JOBPTR = &STR(XASCB+&ASCBJBNI)
00290000               COMPARE ADDRESS(&JOBPTR) +
00300000                       WITH(VALUE(X'00000000')) LENGTH(4)
00310000               IF    &LASTCC = 0 +
00320000               THEN  SET   &JOBPTR = &STR(XASCB+&ASCBJBNS)
00330000               COMPARE ADDRESS(&JOBPTR%) +
00340000                       WITH(VALUE(C'&NAME')) LENGTH(8)
00350000               IF &LASTCC = 0 +
00360000               THEN  SET   &JOBASID = &ASID
00370000               END   /* CHECK JOB NAME */
00380000         SET   &ASID = &ASID + 1
00390000         END   /* ASID LOOP */
00400000IF       &JOBASID ^= 0 +
00410000THEN     DO    /* DEFINE ASID */
00420000         NOTE  'JOB &JOBNAME IS ASID &JOBASID'
00430000         SETDEF ASID(&JOBASID)
00440000         END   /* DEFINE ASID */
00450000ELSE     NOTE  'JOB &JOBNAME NOT FOUND'
