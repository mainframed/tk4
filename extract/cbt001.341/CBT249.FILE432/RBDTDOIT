00010002PROC 1 OPT
00020039   CONTROL NOMSG
00030000/* ================================================================= */
00040002/* THIS CLIST IS A GENERALIZED CLIST CALLED BY MENU PANEL RBDTDOIT   */
00050037/* VALID OPTIONS ARE:  B - BROWSE THE TRANSACTION                    */
00060000/*                     K - KEEP THE TRANSACTION (APPLICABLE TO GMJD) */
00070000/*                     E - EDIT THE TRANSACTION                      */
00080000/*                     S - SUBMIT THE TRANSACTION TO TQI             */
00090010/*                     H - SUBMIT THE TRANSACTION TO TQI HOLD        */
00100002/*                                                                   */
00110000/* ================================================================= */
00120038   ISPEXEC VGET (Z,BDTJNAME,DOPT,BDTOPT,BDTNCOND,BDTNHOLD,+
00130034                   BDTNREL)
00140020   IF &OPT = C THEN +
00150021     GOTO CANCEL
00160004   SET HOLD = &Z
00170015   IF &OPT = H THEN +
00180015     DO
00190015       SET HOLD = HOLD
00200015       SET OPT = S
00210038       SET DOPT = INIT
00220015     END
00230011/* ================================================================= */
00240011/* PERFORM ISPF FILE TAILORING BUILD OF THE TRANSACTION OR GMJD      */
00250011/* IT WILL BE IN THE DATASET DEFINED BY THE ZTEMPF VARIABLE.         */
00260011/* ================================================================= */
00270038   IF &DOPT = INIT THEN +
00280020     DO
00290020       ISPEXEC FTOPEN TEMP
00300020       ISPEXEC FTINCL RBDTSK01
00310020       ISPEXEC FTCLOSE
00320020       ISPEXEC VGET ZTEMPF
00330020     END
00340023   ELSE +
00350023     ISPEXEC VGET (ZTEMPF)
00360011/* ================================================================= */
00370011/* DETERMINE WHICH OPTION                                            */
00380011/* ================================================================= */
00390007   IF &OPT = B THEN GOTO BROWSE
00400015   IF &OPT = K THEN GOTO KEEP
00410007   IF &OPT = E THEN GOTO EDITIT
00420015   IF &OPT = S THEN GOTO SUBMIT
00430000/* ================================================================= */
00440000/* INVOKE ISPF BROWSE ON THE BUILT TRANSACTION                       */
00450000/* ================================================================= */
00460000 
00470000BROWSE: +
00480002     ISPEXEC BROWSE DATASET('&ZTEMPF')
00490038     IF &BDTOPT = T THEN +
00500038        SET DOPT = S
00510038     ELSE +
00520038        SET DOPT = K
00530031     GOTO OUT
00540000 
00550000/* ================================================================= */
00560000/* INVOKE ISPF EDIT ON THE TRANSACTION TO ALLOW CHANGES              */
00570000/* ================================================================= */
00580000 
00590000EDITIT: +
00600002     ISPEXEC EDIT DATASET('&ZTEMPF')
00610038     IF &BDTOPT = T THEN +
00620038        SET DOPT = S
00630038     ELSE +
00640038        SET DOPT = K
00650031     GOTO OUT
00660000 
00670002/* ================================================================= */
00680002/* SUBMIT THE TRANSACTION TO TQI (NOT FOR GMJD PROCESSING)           */
00690002/* ================================================================= */
00700000 
00710015SUBMIT: +
00720019     EX '&ZTEMPF'
00730018     IF &HOLD = &Z THEN +
00740018       SET HOLD = ISSUED
00750018     ELSE +
00760018       SET HOLD = HELD
00770019     ISPEXEC VPUT (HOLD) SHARED
00780018     SET &SHORT = &STR(BDT COMMAND &HOLD)
00790018     SET &LONG  = &STR(MVS/BDT COMMAND &BDTJNAME HAS BEEN &HOLD)
00800015     ISPEXEC SETMSG MSG(RBDT010)
00810036     ISPEXEC VPUT (SHORT LONG)
00820038     SET DOPT = &Z
00830031     GOTO OUT
00840000/* ================================================================= */
00850000/* SAVE THE TRANSACTION IN THE GMJDLIB (GMJD PROCESSING ONLY)        */
00860000/* ================================================================= */
00870017KEEP: +
00880029     ISPEXEC VGET (GMJDLIB BDTJNAME)
00890015     FREE F(SYSUT1 SYSUT2 SYSPRINT SYSIN)
00900015     ALLOC F(SYSUT1) DS('&ZTEMPF')
00910029     ALLOC F(SYSUT2) DS(&GMJDLIB(&BDTJNAME))
00920015     ALLOC F(SYSPRINT) DUMMY
00930015     ALLOC F(SYSIN) DUMMY
00940015     CALL 'SYS1.LINKLIB(IEBGENER)'
00950015     FREE F(SYSUT1 SYSUT2 SYSPRINT SYSIN)
00960015     ALLOC F(SYSPRINT) DS(*)
00970015     ALLOC F(SYSIN) DS(*)
00980040     SET SHORT = &STR(MEMBER KEPT)
00990040     SET LONG = &STR(MEMBER &BDTJNAME KEPT IN &GMJDLIB)
01000040     ISPEXEC SETMSG MSG(RBDT010)
01010038     SET DOPT = &Z
01020031     GOTO OUT
01030020/* ================================================================= */
01040020/* CANCEL THE TRANSACTION                                            */
01050020/* ================================================================= */
01060021CANCEL: +
01070020   SET SHORT = &STR(COMMAND CANCELLED)
01080020   SET LONG = &STR(MVS/BDT COMMAND &BDTJNAME EXECUTION CANCELLED)
01090036   ISPEXEC VPUT (SHORT,LONG)
01100020   ISPEXEC SETMSG MSG(RBDT010)
01110038   SET DOPT = &Z
01120031OUT: +
01130043   IF &OPT = S | &OPT = K | &OPT = C THEN +
01140041      ISPEXEC CONTROL NONDISPL END
01150038   ISPEXEC VPUT (DOPT)
