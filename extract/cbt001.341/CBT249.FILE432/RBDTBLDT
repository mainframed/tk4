00010000PROC 1 OPT
00020052CONTROL    NOMSG /* LIST CONLIST
00030000ISPEXEC VGET (CENTER,Z)
00040000/* ================================================================= */
00050000/* PERFORM BUILD OF A TRANSACTION                                    */
00060000/* ================================================================= */
00070000DOIT: +
00080039   SET PDS = &Z /* NO FOR DSORG=PS, YES FOR DSORG=PO               */
00090039   SET MEM = &Z /* SET FOR ANY DSORG=PO TO DIR, ALL OR ONE        */
00100034   SET DSN1 = &Z
00110000   ISPEXEC DISPLAY PANEL(RBDTDSN1)
00120000   SET LASTRC = &LASTCC
00130000GETDSNFM: +
00140000   IF &LASTRC > 4 THEN +
00150000     EXIT
00160000   IF &ZCMD ^= &Z THEN +
00170000     DO
00180000       SET &SHORT = INVALID COMMAND
00190000       SET &LONG = &STR("&ZCMD" NOT RECOGNIZED AS A VALID COMMAND)
00200000       ISPEXEC DISPLAY CURSOR(ZCMD) MSG(RBDT010)
00210000       SET LASTRC = &LASTCC
00220000       GOTO GETDSNFM
00230000     END
00240000   SET CTR1 = 0
00250000   IF &VOL1 ^= &Z THEN +
00260000     SET CTR1 = &CTR1 + 1
00270000   IF &VOLREF1 ^= &Z THEN +
00280000     SET CTR1 = &CTR1 + 1
00290000   IF &CTR1 > 1 THEN +
00300000     DO
00310000       SET &SHORT = MUTUALLY EXCLUSIVE FIELD
00320000       SET &LONG = &STR(ONLY THE VOLUME OR THE VOLREF FIELD +
00330000                        MAY BE SPECIFIED)
00340000       ISPEXEC DISPLAY MSG(RBDT010) CURSOR(VOL1)
00350000       SET LASTRC = &LASTCC
00360000       GOTO GETDSNFM
00370000     END
00380000/* ================================================================= */
00390000/* SET BDTFDSN = PANEL CONTENTS
00400000/* ================================================================= */
00410000IF &STR(&DSN1) = &Z THEN +
00420000   DO
00430000      SET &BDTFDSN = &PRJ1..&LIB1
00440000      IF &TYP1 ^=  THEN +
00450000         SET BDTFDSN = &BDTFDSN..&TYP1
00460000      ISPEXEC VPUT (BDTFDSN) SHARED
00470007      IF &STR(&MEMBER) ^= &Z THEN +
00480039        DO
00490039          SET PDS = YES
00500039          IF &STR(&MEMBER) = &STR(*) THEN +
00510039            SET MEM = ALL
00520039          ELSE +
00530039            SET MEM = ONE
00540039        END
00550000   END
00560000ELSE +
00570000   DO
00580031 /*              DETECT MEMBER NAME OR (*)                     */
00590031      IF &SUBSTR(&LENGTH(&STR(&DSN1)),&STR(&DSN1)) = ' THEN +
00600031         DO
00610031           SET SUFFIX = '
00620031           SET DSN1 = &SUBSTR(1:&LENGTH(&STR(&DSN1))-1,&STR(&DSN1))
00630031         END
00640031      ELSE +
00650031         SET SUFFIX = &Z
00660031 /*                   LOOK FOR A CLOSING PAREN                     */
00670031      IF &SUBSTR(&LENGTH(&STR(&DSN1)),&STR(&DSN1)) = ) THEN +
00680014        DO
00690039          SET PDS = YES
00700030          IF &SUBSTR(&LENGTH(&STR(&DSN1))-1,+
00710030                &STR(&DSN1)) = &STR(*) THEN +
00720014            DO                                 /* '*' SPECIFIED */
00730030              SET DSN1 = &SUBSTR(1:&LENGTH(&STR(&DSN1))-3,+
00740030                            &STR(&DSN1))        /* SHORTEN       */
00750039              SET MEM = ALL
00760014            END
00770014          ELSE +
00780039            SET MEM = ONE
00790014        END
00800031      SET DSN1 = &DSN1&SUFFIX        /* PUT BACK ' IF THERE WAS ONE */
00810030      SET BDTFDSN = &STR(&DSN1)
00820030/* ================================================================= */
00830030/* SET UP THE DSNAME FULLY QUALIFIED WITHOUT QUOTES                  */
00840030/* ================================================================= */
00850030      ISPEXEC VPUT (BDTFDSN) SHARED
00860030      ISPEXEC SELECT PGM(FIXUPDSN) PARM(BDTFDSN)
00870030      ISPEXEC VGET (BDTFDSN) SHARED
00880000   END
00890000  IF &VOLREF1 ^= &Z THEN +
00900000    DO
00910000/* ================================================================= */
00920000/* SET UP THE VOLREF NAME FULLY QUALIFIED WITHOUT QUOTES  */
00930000/* ================================================================= */
00940000      ISPEXEC VPUT (VOLREF1) SHARED
00950000      ISPEXEC SELECT PGM(FIXUPDSN) PARM(VOLREF1)
00960000      ISPEXEC VGET (VOLREF1) SHARED
00970000    END
00980000/* ================================================================= */
00990000/* DETERMINE IF WE ARE AT THE SENDING NODE                           */
01000000/* ================================================================= */
01010034SET DSCAT = NO
01020000IF &CENTER = &BDTFNODE THEN +
01030000  DO                            /* FROM NODE HERE  */
01040000    IF &CTR1 = 0 THEN +
01050000      DO                        /* NO VOL OR VOLREF, M/B CATALOGED */
01060000    /*  IF &SYSDSN('&BDTFDSN') = OK THEN +  /* TSO-E                   */
01070000 
01080046        WHATIF DATASET('&BDTFDSN') CATALOGED  /* DOES THE DS EXIST ?    */
01090000        IF &LASTCC ^= 0 THEN +
01100000          DO        /* DATA SET WAS NOT THERE                            */
01110000            SET SHORT = &STR(FROM DATASET NOT FOUND)
01120004            SET LONG = &STR(DATASET &BDTFDSN NOT FOUND)
01130051            IF &DSN1 ^= &Z THEN +
01140048               SET CURS = DSN1
01150048            ELSE +
01160048               SET CURS = MEMBER
01170050            ISPEXEC DISPLAY MSG(RBDT010) CURSOR(&CURS)
01180048            SET LASTRC = &LASTCC
01190048            GOTO GETDSNFM
01200000          END
01210000        ELSE +
01220000          DO                                /* DATA SET IS CATALOGED */
01230000            SET  DSNINFO = '&BDTFDSN'
01240000            ISPEXEC  VPUT DSNINFO
01250000            ISPEXEC  SELECT PGM(DSNINFO)
01260000            ISPEXEC VGET (DSORG ALCYL TRALC INUNIT UNIT SCALO DIRALLOC  RECFM +
01270000                          LRECL BLKSIZE )
01280001            SET DSCAT = YES
01290000            IF &PDS = &Z THEN +
01300001              IF &DSORG = PO THEN +
01310039                DO
01320039                  SET PDS = YES
01330039                  SET MEM = DIR
01340039                END
01350001              ELSE +
01360001                 SET PDS = NO              /* SEQUENTIAL DSORG       */
01370000          END                              /* END DATA SET CATALOGED */
01380000      END                                  /* END NO VOL SPECIFIED   */
01390000  END                                        /* END SENDING CENTER */
01400022IF &PDS = &Z THEN +
01410001  DO                                     /* WE STILL DON'T KNOW DSORG*/
01420004    ISPEXEC DISPLAY PANEL(RBDTDORG)      /* GET DSORG           */
01430000    IF  &DSORG ^= PO THEN +
01440001      SET PDS = NO                           /* WAS NOT A PDS  */
01450001    ELSE +
01460039      DO
01470039        SET PDS = YES
01480039        SET MEM = DIR
01490039      END
01500001  END
01510000/* ================================================================= */
01520000/*      GET THE TO DATA SET INFORMATION                              */
01530000/* ================================================================= */
01540000GETDSN2: +
01550021   SET MEMBER2 = &Z
01560000   IF &OPT = T THEN +
01570000       SET WHICH = &STR(TRANSACTION NAME)
01580000   ELSE +
01590045       SET WHICH = &STR(GMJD MEMBER NAME)
01600041   ISPEXEC DISPLAY PANEL(RBDTDSN2)
01610037   IF &LASTCC > 0 THEN +
01620037     GOTO DOIT
01630000GETDSNTO: +
01640000   IF &ZCMD ^= &Z THEN +
01650000     DO
01660000       SET &SHORT = INVALID COMMAND
01670000       SET &LONG = &STR("&ZCMD" NOT RECOGNIZED AS A VALID COMMAND)
01680000       ISPEXEC DISPLAY CURSOR(ZCMD) MSG(RBDT010)
01690000       IF &LASTCC > 0 THEN +
01700000         GOTO DOIT
01710000       GOTO GETDSNTO
01720000     END
01730000   SET &SHORT = MUTUALLY EXCLUSIVE FIELD
01740000   SET &LONG = &STR(ONLY 1 OF THE FIELDS VOLUME, VOLREF OR UNIT +
01750000                    MAY BE SPECIFIED)
01760000   SET CTR2 = 0
01770000   IF &VOL2 ^= &Z THEN +
01780000     SET CTR2 = &CTR2 + 1
01790000   IF &VOLREF2 ^= &Z THEN +
01800000     SET CTR2 = &CTR2 + 1
01810000   IF &TOUNIT ^= &Z THEN +
01820000     SET CTR2 = &CTR2 + 1
01830000   IF &CTR2 > 1 THEN +
01840000     DO
01850000       ISPEXEC DISPLAY MSG(RBDT010) CURSOR(VOL2)
01860000       IF &LASTCC > 0 THEN +
01870000         GOTO DOIT
01880000       GOTO GETDSNTO
01890000     END
01900000/*
01910000/*  DETERMINE WE ARE NOT DOING A THIRD PARTY TRANSACTION             */
01920000/*
01930000   IF &CENTER ^= &BDTFNODE && &CENTER ^= &BDTTNODE THEN +
01940000      DO
01950000        SET SHORT = &STR(THIRD PARTY TRANSACTION)
01960000        SET LONG = &STR(YOUR PRESENT COMPUTING CENTER MUST BE EITHER +
01970000                        SENDER OR RECEIVER)
01980048        ISPEXEC DISPLAY MSG(RBDT010) CURSOR(BDTTNODE)
01990048        SET LASTRC = &LASTCC
02000048        GOTO GETDSNTO
02010000      END
02020000/* ================================================================= */
02030000/*   CONTINUE WITH "TO" DATASET PROCESSING                           */
02040000/* ================================================================= */
02050000   IF &STR(&DSN2) = &Z THEN +
02060000      DO
02070000         SET &BDTTDSN = &PRJ2..&LIB2
02080000         IF &TYP2 ^=  THEN +
02090000            SET BDTTDSN = &BDTTDSN..&TYP2
02100000         ISPEXEC VPUT (BDTTDSN) SHARED
02110000      END
02120000   ELSE +
02130000     DO                                       /* DSN2 WAS SPECIFIED  */
02140000      SET BDTTDSN = &STR(&DSN2)
02150000/* ================================================================= */
02160000/* SET UP THE DSNAME FULLY QUALIFIED WITHOUT QUOTES                  */
02170000/* ================================================================= */
02180000      ISPEXEC VPUT (BDTTDSN) SHARED
02190000      ISPEXEC SELECT PGM(FIXUPDSN) PARM(BDTTDSN)
02200000      ISPEXEC VGET (BDTTDSN) SHARED
02210000      IF &SUBSTR(&LENGTH(&BDTTDSN),&BDTTDSN) = ) THEN +
02220042        IF &MEM ^= &Z && &MEM ^= ONE THEN +
02230001          DO
02240001            SET SHORT = &STR(MEMBER NOT ALLOWED)
02250001            SET LONG = &STR(A MEMBER NAME FOR THE "TO" DATA SET IS +
02260001              NOT ALLOWED)
02270001            ISPEXEC SETMSG MSG(RBDT010)
02280001            ISPEXEC DISPLAY CURSOR(DSN2)
02290001            IF &LASTCC > 0 THEN +
02300001              GOTO DOIT
02310001            GOTO GETDSNTO
02320001          END                     /* MEMBER SPECIFIED                  */
02330000     END                        /* DSN2 SPECIFIED                    */
02340000  IF &VOLREF2 ^= &Z THEN +
02350000    DO
02360000/* ================================================================= */
02370000/* SET UP THE VOLREF NAME FULLY QUALIFIED WITHOUT QUOTES  */
02380000/* ================================================================= */
02390000      ISPEXEC VPUT (VOLREF2) SHARED
02400000      ISPEXEC SELECT PGM(FIXUPDSN) PARM(VOLREF2)
02410000      ISPEXEC VGET (VOLREF2) SHARED
02420000    END
02430000  IF &BDTTDISP = NEW THEN +
02440000    DO
02450000      ISPEXEC DISPLAY PANEL(RBDTNUDS)
02460000      IF &LASTCC > 0 THEN +
02470000        EXIT
02480000      IF &BDTDCBDS ^= &Z THEN +
02490000        DO
02500000/* ================================================================= */
02510000/* SET UP THE DCBDS  NAME FULLY QUALIFIED WITHOUT QUOTES  */
02520000/* ================================================================= */
02530000          SET DCBDS = &BDTDCBDS
02540000          ISPEXEC VPUT (DCBDS) SHARED
02550000          ISPEXEC SELECT PGM(FIXUPDSN) PARM(DCBDS)
02560000          ISPEXEC VGET (DCBDS) SHARED
02570000        END
02580010    END
02590010  ELSE +
02600010    DO                         /* NOT NEW DS                         */
02610000/* ================================================================= */
02620000/* SEE IF WE ARE ON THE RECEIVING CENTER                             */
02630000/* ================================================================= */
02640000 
02650010      IF &CENTER = &BDTTNODE THEN +
02660010        DO                 /* CENTER IS RECEIVER                     */
02670010          IF &CTR2 = 0 THEN +
02680010            DO                          /*  MUST BE CATALOGED        */
02690046              WHATIF DATASET('&BDTTDSN') CATALOGED
02700011              IF &LASTCC ^= 0 THEN +
02710011                DO                    /* DS IS NOT THERE                 */
02720011                  SET SHORT = DATASET NOT FOUND
02730011                  SET LONG = &STR(OLD DATASET &BDTTDSN NOT +
02740011                    FOUND IN CATALOG)
02750011                  GOTO GETDSN2
02760011                END
02770011              ELSE +
02780011                DO
02790000/* ================================================================= */
02800000/* CHECK THE DSORG OF RECEIVING  DATASET                             */
02810000/* ================================================================= */
02820010 
02830035                  SET &DSNINFO = '&BDTTDSN'
02840010                  ISPEXEC VPUT DSNINFO
02850010                  ISPEXEC SELECT PGM(DSNINFO)
02860010                  ISPEXEC VGET (DSORG)
02870040                  IF &PDS = YES && &MEM ^= ONE && &DSORG = PS THEN +
02880010                      DO                  /* DSORGS MUST MATCH      */
02890010                        SET SHORT = &STR(DSORGS DO NOT MATCH)
02900035                        SET LONG = +
02910010                         &STR(SENDING AND RECEIVING DATASETS MUST +
02920010                          BOTH BE SEQUENTIAL (PS) OR PARTITIONED (PO))
02930010                        ISPEXEC SETMSG MSG(RBDT010)
02940010                        GOTO DOIT
02950010                      END
02960039                   ELSE +
02970040                    IF &PDS = NO && &DSORG = PO THEN +
02980039                        DO                  /* DSORGS MUST MATCH      */
02990039                          SET SHORT = &STR(DSORGS DO NOT MATCH)
03000039                          SET LONG = +
03010039                           &STR(SENDING AND RECEIVING DATASETS MUST +
03020039                            BOTH BE SEQUENTIAL (PS) OR PARTITIONED (PO))
03030039                          ISPEXEC SETMSG MSG(RBDT010)
03040039                          GOTO DOIT
03050039                        END
03060011                END                        /* DATA SET WAS THERE     */
03070010            END                           /* MUST BE CATALOGED       */
03080010        END                               /* CENTER IS RECEIVER      */
03090010    END                                   /* DATA SET WAS NOT NEW    */
03100000/* ================================================================= */
03110036/* CHECK IF A DTC NETWORK WAS INDICATED                              */
03120000/* ================================================================= */
03130038   IF &BDTDTC = Y THEN +
03140038     DO
03150038       ISPEXEC DISPLAY PANEL(RBDTDTC)
03160038       IF &LASTCC > 0 THEN +
03170038         GOTO DOIT
03180038     END
03190038  ELSE +
03200038     DO
03210038       SET BDTNID = &Z
03220038       SET BDTNREL = &Z
03230038       SET BDTNCOND = &Z
03240038       SET BDTNHOLD = &Z
03250038     END
03260036/* ================================================================= */
03270036/* CHECK FOR A PDS AND DO NECESSARY MEMBER PROCESSING                */
03280036/* ================================================================= */
03290002  IF &MEM = DIR THEN +
03300002     DO                                  /* ASK FOR MEMBER NAMES     */
03310000/*================================================================== */
03320000/*   BUILD A SELECTION TABLE OF ALL MEMBER NAMES IN THE PDS        */
03330000/*================================================================== */
03340002       ISPEXEC VPUT (PDS MEM BDTFDSN)
03350002       IF &DSCAT = YES THEN +
03360002         %RBDTGETD
03370002       ELSE +
03380002         %RBDTGETM
03390002       SET &DIRNAME =
03400002       ISPEXEC TBTOP MEMTABLE /* PUT FIRST ONE IN FIRSTMEM */
03410002       ISPEXEC TBSKIP MEMTABLE
03420002       IF &LASTCC = 8 THEN +
03430002         DO                       /* NO MEMBERS SELECTED AT ALL */
03440002           SET SHORT = &STR(NO MEMBERS SELECTED)
03450002           SET LONG = +
03460002             &STR(DIRECTORY REQUESTED, BUT NO MEMBERS WERE +
03470002               SELECTED)
03480002           ISPEXEC SETMSG MSG(RBDT010)
03490002           ISPEXEC TBEND MEMTABLE
03500002           GOTO DOIT
03510002         END
03520023       SET &FIRSTMEM = &DIRNAME
03530002       ISPEXEC TBDELETE MEMTABLE
03540002       ISPEXEC TBSKIP MEMTABLE
03550002       IF &LASTCC = 8 THEN +
03560023         SET MEMS = ONE
03570023       ELSE +
03580023         SET MEMS = MORE
03590002     END             /* PO AND MEMBER LIST                      */
03600000/* ================================================================= */
03610000/* SET UP DAP WITH CORRECT VALUE BASED UPON DSORG DETERMINED ABOVE   */
03620000/* ================================================================= */
03630043   IF &PDS = YES && &MEM ^= ONE THEN +
03640013     SET DAP = PDS
03650000   ELSE +
03660013     SET DAP = SEQ
03670000/* ================================================================= */
03680000/* SET THE NECESSARY VARIABLES                                       */
03690000/* ================================================================= */
03700000   SET DOPT = INIT
03710000   ISPEXEC VPUT (BDTJNAME,BDTFDSN,BDTFDISP,DAP,BDTFNODE,DOPT,+
03720000     VOL1,VOLREF1,BDTMSG,FIRSTMEM,BDTTREP,MEMS,BDTTDSN,INTRDR,+
03730013     BDTTNODE,BDTDCBDS,VOL2,VOLREF2,BDTTDISP,TOUNIT,RLSE,+
03740013     PDS,MEM,MEMBER,MEMBER2)
03750000   IF &BDTTDISP = NEW THEN +
03760000     DO
03770000       ISPEXEC VPUT (BDTTDSP2,RECFM,LRECL,BLKSIZE,PRI)
03780000       ISPEXEC VPUT (SCALO,ALLOC,DIRALLOC)
03790000     END
03800000   IF &OPT = T THEN +
03810000     SET WHAT = TRANSACTION
03820000   ELSE +
03830000     SET WHAT = GMJD MEMBER
03840000   ISPEXEC VPUT (WHAT)
03850000   ISPEXEC SELECT PANEL(RBDTDOIT)
03860000/* ================================================================= */
03870000/* IF WE HAD A TABLE OF MEMBERS THEN CLOSE IT                        */
03880000/* ================================================================= */
03890002   IF &MEM = DIR THEN +
03900000      ISPEXEC TBEND MEMTABLE
03910033   GOTO DOIT
03920000 EXIT
