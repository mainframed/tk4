00010001PROC     0 TEST NOTEST
00020001IF       &TEST = TEST +
00030001THEN     CONTROL PROMPT   LIST   CONLIST   SYMLIST MSG
00040001ELSE     CONTROL PROMPT NOLIST NOCONLIST NOSYMLIST
00050000           GLOBAL &HEXVALUE &VALUE &RELCODE
00060000/**********************************************************************/
00070000/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
00080000/*     H     H   E         A     A   D     D   E         R     R      */
00090000/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
00100000/*     H     H   E         A     A   D     D   E         R   R        */
00110000/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
00120000/*                                                                    */
00130000/**********************************************************************/
00140000/*  CLIST NAME: IMSSAPS           REL/DATE: 1.0/01.__.79              */
00150000/*                                                                    */
00160000/*  DOC: THIS CLIST WILL INITIALIZE AND DISPLAY SAP POINTERS. IT      */
00170000/*       ALSO ALLOWS THE USER TO ANALYZE SELECTED SAPS, AND THEIR     */
00180000/*       SAVEAREAS.                                                   */
00190000/*                                                                    */
00200000/*  SYMBOLS EQUATED: SAP# = THE SAP WITH A UNIQUE NUMBER ATTACHED     */
00210000/*                   SAP#SA# = THE SAVEAREA # CHAINED TO SAP #
00220000/*                                                                    */
00230000/*  SYMBOLS FOUND: SAME AS ABOVE                                      */
00240000/*                                                                    */
00250000/*  RETURN: NONE                                                      */
00260000/*                                                                    */
00270000/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT TSO COMMAND             */
00280000/*                   CLRSCRN BE INSTALLED IN YOUR ENVIRONMENT.        */
00290000/*                                                                    */
00300000/*  USE: UPON ENTRY, THE USER HAS THE OPTION TO INITIALIZE AND        */
00310000/*       DISPLAY THE SAP POINTERS, OR TO ANALYZE A SELECTED SAP.      */
00320000/*       SAP INITIALIZATION MUST RUN FIRST, BUT ONLY ONCE PER DUMP.   */
00330000/*       * NOTE ON FORMATTING OR SAVEAREAS AND SAPS * CURRENTLY,      */
00340000/*       TWO SAVEAREAS OR THREE SAPS WILL BE DISPLAYED PER SCREEN.    */
00350000/*       TSO CLIST 'WRITE' COMMANDS ARE USED ABOVE LABELS 'SKIPFMT'   */
00360000/*       AND 'NEXTSAP' TO PAD TO THE BOTTOM OF EACH SCREEN. OTHER     */
00370000/*       SCREEN SIZES MAY REQUIRE DELETING OR ADDING 'WRITE' STMTS.   */
00380000/*                                                                    */
00390000/*RGM******************************************************************/
00400000            L SCD NODISPLAY
00410000            IF &LASTCC=0 THEN DO
00420000               EVAL SCD+14 LE(2)       /* GET IT FROM SCD+X'14' */
00430000               SET &RELCODE=&LASTCC
00440000               IF &RELCODE=276 THEN DO             /* 114 ?  */
00450000                EVAL SCD+372 L(2)      /* PICK UP TOTAL NUMBER OF   */
00460000                SET &TOT = &LASTCC     /* SAPS FROM SCD+372 FOR THIS */
00470000               END                    /* CLIST TO USE. */
00480000               ELSE IF &RELCODE=277 THEN DO
00490000                EVAL SCD+3AE LE(2)        /* FOR  */
00500000                SET &TOT = &LASTCC        /* IMS115. */
00510000               END
00520000               ELSE DO /*NEITHER!*/
00530000       WRITE RELEASE CODE OBTAINED FROM SCD IS INVALID. RETURN AND
00540000       WRITENR RE-INITIALIZE THE DUMP. DEPRESS ENTER TO CONTINUE.
00550000               READ
00560000               EXIT            /* DO NOT EVEN ATTEMPT TO CONTINUE  */
00570000               END             /* TIL THIS IS STRAIGHTENED OUT.    */
00580000            END
00590000               ELSE DO
00600000            WRITE THE SCD HAS NOT BEEN INITIALIZED YET. RETURN AND
00610000            WRITENR INITIALIZE WITH IMSINIT. DEPRESS ENTER TO CONTINUE.
00620000               READ
00630000               EXIT
00640000            END
00650000    DOSAPS: /* CLRSCRN */ +
00660000            SET &SAPN=0           /* INIT SAP NO TO 0. */
00670000NOTE '*****************************************************************'
00680000NOTE '* ANALYZE IMS SAVE AREA PREFIXES AND ASSOCIATED SAVEAREA CHAINS *'
00690000NOTE '*                                                               *'
00700000WRITE *  SYMBOLS EQUATED:  SAP# = A SAP WITH A UNIQUE NUMBER ATTACHED *
00710000WRITE *                    SAP#SA# = THE SAVEAREA # CHAINED TO SAP #  *
00720000WRITE *****************************************************************
00730000WRITE
00740000WRITENR DO YOU WANT THE PRINT OPTION ?  (Y)/N ===>
00750000READ &ANS
00760000IF &ANS=N THEN SETD NOPRINT /*IF ANSWER IS N, USER DOESN'T WANT PRINT */
00770000          ELSE SETD PRINT   /* ANY OTHER ANSWER, HE'S GOT DEFAULT = Y */
00780000WRITE
00790000OPTMENU: WRITE ++++ IMSSAPS
00800000WRITE ++++ OPTIONS ARE: (A) INITIALIZE SAP POINTERS, AND DISPLAY SAPS
00810000WRITE                   (B) ANALYZE SELECTED SAPS AND SAVE AREAS
00820000WRITE                   (X) RETURN TO OPTIONS MENU
00830000WRITE
00840000WRITENR ENTER A, B, OR X ===>
00850000            READ &ANSAN
00860000            IF &ANSAN = &STR(A) THEN GOTO DOSAP1
00870000            IF &ANSAN = &STR(B) THEN GOTO SAVARA11
00880000            IF &ANSAN = &STR(X) THEN EXIT
00890000            WRITE INVALID REPLY: DEPRESS ENTER AND TRY AGAIN.
00900000            READ &ANAA
00910000            /* CLRSCRN */
00920000            GOTO OPTMENU
00930000  DOSAP1:   WRITE
00940000          WRITE SELECT DISPLAY: (A) DISPLAY ALL SAPS (&TOT IN THIS DUMP)
00950000            WRITE                 (B) DISPLAY ONLY ACTIVE SAPS
00960000     SAPRD: WRITE
00970000            WRITENR ENTER A OR B ===>
00980000            READ &DISPLAY
00990000            IF &DISPLAY = &STR(A) THEN GOTO SAPOK  /* EITHER WAY, GO */
01000000            IF &DISPLAY = &STR(B) THEN GOTO SAPOK  /* TO SAPOK. */
01010000            WRITE
01020000            WRITE ERROR: &DISPLAY IS INVALID.
01030000            GOTO SAPRD                 /* WE NEED &DISPLAY FOR LATER. */
01040000     SAPOK: SETD DISPLAY(NOMACH NOREM NOREQ NOSYM STORAGE)
01050000            SET &OFST = 0        /* SET OFFSET TO 0. */
01060000            SET &CNT = 1         /* SET COUNT OF SAPS TO 1  */
01070000            /* CLRSCRN */
01080000   SAPLOOP: EQ SAP&CNT SAPSTART+&OFST.N LE(X'80') STR(IMSSAP)
01090000            EVAL SAP&CNT L(1)        /* EVAL HI-ORDER BYTE */
01100000            SET &STAT1 = &LASTCC     /* SET IT TO &STAT1  */
01110000            IF &DISPLAY = B THEN DO   /*  ONLY ACTIVE SAPS ? */
01120000                IF &STAT1 LE 127 THEN GOTO NEXTSAP /* NOT ACTIVE. */
01130000                GOTO ACT              /* ELSE ACTIVE, DISPLAY IT. */
01140000            END
01150000            IF &STAT1 LE 127 THEN +
01160000      NOTE '***  SAP&CNT  ********************************************'
01170000            ELSE DO
01180000       ACT: EQU SAP&CNT.SA1 SAP&CNT+8% LENGTH(72) /* IF ACTIVE DO EQU */
01190000            IF &STAT1 GE 192 THEN +
01200000      NOTE '***  SAP&CNT  ***  ACTIVE  ***  WAITING  *****************'
01210000                             ELSE +
01220000      NOTE '***  SAP&CNT  ***  ACTIVE  *******************************'
01230000            END
01240000      NOTE '*                 SAPFLAG1 AND SAPFLAG2  |.1.|.1.|    **'
01250000      NOTE '*          SAP    | |      SAPSFLG0 AND SAPSFLG1 |1|1|**'
01260000      NOTE '*OFFSET   ADDR    V V      V V      V 1ST SAVEAREA    **'
01270000            L SAP&CNT L(16)
01280000      NOTE '********************************************************'
01290000      WRITE
01300000            SET &SAPN=&SAPN+1     /* THIS COUNTER IS FOR FORMATTING */
01310000            IF &SAPN=3 THEN DO    /* THE SAPS EVENLY ON A 3278-2,   */
01320000            SET &SAPN=0   /* AND MAY BE REMOVED OR MODIFIED. THE    */
01330000   /* WRITES BELOW ARE USED FOR PADDING TO REACH THE END OF THE SCREEN*/
01340000          WRITE
01350000          WRITE
01360000            END
01370000   NEXTSAP: SET &OFST = &OFST + 128          /* OFFSET IN */
01380000            SET &CNT = &CNT+1                /* ADD 1 TO COUNT */
01390000            IF &CNT GT &TOT THEN GOTO SAPQUIT  /* REACH TOTAL YET ? */
01400000            GOTO SAPLOOP        /* YES, END. OTHERWISE REPEAT PROCESS */
01410000SAPQUIT: WRITENR NO MORE SAPS. TO SEE ASSOCIATED SAVEAREAS, ENTER Y ===>
01420000            READ &ANSWR
01430000            IF &ANSWR = &STR(Y) THEN GOTO SAVARA  /* IF YOU WANT TO */
01440000            GOTO OPTMENU        /* SEE THE SAVEAREAS NOW, NEED MENU.*/
01450000  SAVARA11: L SAP1 NODISPLAY     /* HERE TO LIST SAPS. CHECK FOR */
01460000            IF &LASTCC = 0 THEN GOTO SAVARA  /* SAPS INITIALIZED YET. */
01470000            WRITE
01480000        WRITE SAP POINTERS NOT INITIALIZED. DEPRESS ENTER AND TRY AGAIN.
01490000            READ &ANANA
01500000            GOTO OPTMENU
01510000    SAVARA: /* CLRSCRN */      /* EVERYTHING OK. ASK FOR SAP# */ +
01520000            SETD DISPLAY(NOMACH NOREM NOREQ NOSYM STORAGE)
01530000            WRITE TO FORMAT THE SAVEAREA CHAIN OF ANY ACTIVE SAP,
01540000     RDSAP: WRITENR ENTER THE ASSIGNED SAP NUMBER (I.E. 3 FOR SAP3) ===>
01550000            READ &CNT                 /* PICK UP THE NUMBER */
01560000            L SAP&CNT NODISPLAY /*LIST SAP TO SEE IF ITS BEEN EQUATED */
01570000            IF &LASTCC^=0 THEN DO  /* ABOVE. ELSE WE CAN'T FIND IT. */
01580000            WRITE
01590000            WRITE SAP&CNT IS INVALID.
01600000            WRITE
01610000            GOTO RDSAP
01620000            END
01630000            SET &S = 1         /* SET UP FOR COUNTING SAVEAREA'S */
01640000            SET &S2 = &S       /* CHAINED OFF THE GIVEN SAP.   */
01650000            /* CLRSCRN */
01660000    BEGIN2: EVAL SAP&CNT.SA&S.+D LE(3) /* CHECK REG 14 IN SAVEAREA */
01670000            IF &LASTCC=0 THEN DO     /* IF = 0, SAVEAREA IS UNUSED. */
01680000               WRITE
01690000  WRITE ***  REG 14 = 0.  ***  SAP&CNT.SA&S IS PROBABLY UNUSED.  ***
01700000               L SAP&CNT.SA&S         /* LIST IT ANYWAY */
01710000               WRITE
01720000               WRITENR DEPRESS ENTER TO CONTINUE.
01730000               READ
01740000               /* CLRSCRN */
01750000               GOTO SKIPFMT /*BUT DON'T FORMAT IT, OR TRY TO FIND CODE*/
01760000            END
01770000  SAFORMAT: NOTE '* SAVEAREA &S STORED INTO BY THE FOLLOWING CODE (RE+
01780000            G 15)  ************'
01790000            L SAP&CNT.SA&S.+10% LENGTH(24) /*FIND EYECATCHER VIA REG15*/
01800000            NOTE '*               **  SAP&CNT.SA&S  **               **'
01810000            L SAP&CNT.SA&S        /* LIST THE SAVEAREA */
01820000            NOTE '************************************************+
01830000            *******************'
01840000            WRITE
01850000  SKIPFMT:  EVAL SAP&CNT.SA&S.+8 LENGTH(4)   /* ANY MORE SAVEAREA'S */
01860000            SET &NXT = &LASTCC
01870000            IF &NXT LT 256 THEN GOTO NOMO    /* NO - GET OUT. */
01880000            EVAL SAP&CNT.SA&S LENGTH(4)      /* YES, GET POINTER  */
01890000            SET &NXT = &LASTCC
01900000            IF &NXT EQ 16777215 THEN GOTO NOMO  /* IF 'FFFFFFFF' */
01910000            SET &S2 = &S + 1               /* NO MORE - GET OUT  */
01920000            EQU SAP&CNT.SA&S2 SAP&CNT.SA&S.+8% LENGTH(72) /* OTHERWISE*/
01930000            SET &S = &S2             /* DO EQUATE FOR SAVEAREA+1 */
01940000            GOTO BEGIN2              /* START FROM THE VERY BEGINNING */
01950000      NOMO: WRITE WHEN YOU ARE READY ...
01960000   BADINPUT:WRITENR ENTER Y TO SEE ANOTHER ACTIVE SAP, N TO EXIT ===>
01970000            READ &CNT                         /* IF USER WANTS TO SEE */
01980000            IF &CNT = &STR(Y) THEN GOTO RDSAP /*ANOTHER ACTIVE, GO ASK*/
01990000            IF &CNT = &STR(N) THEN GOTO OPTMENU /* IF NOT, SHOW MENU  */
02000000            WRITE
02010000            GOTO BADINPUT       /* ELSE TYPO, ASK AGAIN... */
