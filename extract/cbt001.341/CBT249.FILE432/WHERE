00010008PROC     1     ADDRESS        /* HEXADECIMAL STORAGE LOCATION       */ +
00020008               ASID(0)        /* ADDRESS SPACE NUMBER               */ +
00030008               DEBUG          /* REQUEST FULL OUTPUT LISTING        */
00040008/**********************************************************************/
00050008/*       DETERMINE LOCATION OF ADDRESS.  OUTPUT CONSISTS OF:          */
00060008/*       (1) AREA WITHIN ADDRESS SPACE                                */
00070008/*       (2) SUBPOOL AND STORAGE KEY                                  */
00080008/*       (3) WHETHER OR NOT STORAGE IS FREE                           */
00090008/*       (4) LOAD MODULE CONTAINING ADDRESS, IF ANY                   */
00100008/**********************************************************************/
00110008IF       &DEBUG = DEBUG +
00120008THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00130005GLOBAL   SUBPOOL KEY FREE
00140001SET      &CVTLPDIA = 360N  /* LPA */
00150001SET      &CVTMZ00 = 164N   /* HIGHEST ADDRESS */
00160001SET      &CVTNUCB = 128N   /* PRIVATE AREA */
00170001SET      &CVTSHRVM = 416N  /* CSA */
00180000SET      &HEX = 0123456789ABCDEF
00190000SET      &LOCATION = 0
00200000SET      &DIGIT = 1
00210008SET      &LAST = &LENGTH(&ADDRESS)
00220008IF       &SUBSTR(&LAST,&ADDRESS) = . +
00230008THEN     SET   &LAST = &LAST-1
00240008DO       WHILE &DIGIT <= &LAST /* CONVERT ADDRESS */
00250000         SET   &INDEX = 0
00260001         DO    /* CONVERT DIGIT */ +
00270001               WHILE &SUBSTR(&DIGIT,&ADDRESS) ^= &SUBSTR(&INDEX+1,&HEX)
00280000               SET   &INDEX = &INDEX+1
00290000               IF    &INDEX > 15 +
00300001               THEN  DO    /* ADDRESS ERROR */
00310004                     WRITE &ADDRESS IS NOT HEXADECIMAL
00320001                     EXIT  CODE(12)
00330001                     END   /* ADDRESS ERROR */
00340001               END   /* CONVERT DIGIT */
00350000         SET   &LOCATION = &LOCATION*16+&INDEX
00360000         SET   &DIGIT = &DIGIT+1
00370000         END   /* CONVERT ADDRESS */
00380001EVALUATE CVT STRUCTURE(CVT)
00390003EVALUATE CVT+&CVTNUCB LENGTH(4)
00400003SET      &PRIVATE = &LASTCC
00410003EVALUATE CVT+&CVTSHRVM LENGTH(4)
00420003SET      &COMMON = &LASTCC
00430003EVALUATE CVT+&CVTMZ00 LENGTH(4)
00440003SET      &MAXIMUM = &LASTCC
00450003SET      &AREA = INVALID
00460003IF       0 <= &LOCATION && &LOCATION < &PRIVATE +
00470001THEN     SET   &AREA = NUCLEUS
00480003IF       &PRIVATE <= &LOCATION && &LOCATION < &COMMON +
00490003THEN     SET   &AREA = PRIVATE
00500003IF       &COMMON <= &LOCATION && &LOCATION <= &MAXIMUM +
00510001THEN     SET   &AREA = COMMON
00520004NOTE     '&ADDRESS IS IN THE &AREA AREA'
00530010IF       &AREA = PRIVATE +
00540011THEN     EXEC  'JES3.CLIST(WHEREP)' '&LOCATION ASID(&ASID) &DEBUG'
00550001IF       &AREA = COMMON +
00560011THEN     EXEC  'JES3.CLIST(WHEREC)' '&LOCATION &DEBUG'
00570003IF       &AREA = INVALID +
00580003THEN     EXIT  CODE(12)
00590008ELSE     EXIT  CODE(0)
