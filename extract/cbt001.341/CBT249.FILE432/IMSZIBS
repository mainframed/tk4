00010001PROC     0 TEST NOTEST
00020001IF       &TEST = TEST +
00030001THEN     CONTROL PROMPT   LIST   CONLIST   SYMLIST MSG
00040001ELSE     CONTROL PROMPT NOLIST NOCONLIST NOSYMLIST
00050000           GLOBAL &HEXVALUE &VALUE &RELCODE
00060000/**********************************************************************/
00070000/*     H     H   EEEEEEE    AAAAA    DDDDDD    EEEEEEE   RRRRRR       */
00080000/*     H     H   E         A     A   D     D   E         R     R      */
00090000/*     HHHHHHH   EEEEEEE   AAAAAAA   D     D   EEEEEEE   RRRRRR       */
00100000/*     H     H   E         A     A   D     D   E         R   R        */
00110000/*     H     H   EEEEEEE   A     A   DDDDDD    EEEEEEE   R     R      */
00120000/*                                                                    */
00130000/**********************************************************************/
00140000/*  CLIST NAME: IMSZIBS           REL/DATE: 1.0/01.__.79              */
00150000/*                                                                    */
00160000/*  DOC: THIS CLIST WILL INITIALIZE POINTERS TO ZIBS AND DISPLAY      */
00170000/*       THEM. IT ALLOWS THE USER TO ANALYZE SELECTED ZIBS AND        */
00180000/*       THEIR ASSOCATED FREE FAQES (AFTER THE POINTERS HAVE BEEN     */
00190000/*       INITIALIZED).                                                */
00200000/*                                                                    */
00210000/*  SYMBOLS EQUATED: ZIB# = A ZIB WITH A UNIQUE NUMBER ATTACHED       */
00220000/*                   FAQE# = A FAQE WITH A UNIQUE NUMBER ATTACHED     */
00230000/*                                                                    */
00240000/*  SYMBOLS FOUND: SAME AS ABOVE                                      */
00250000/*                                                                    */
00260000/*  RETURN: NONE                                                      */
00270000/*                                                                    */
00280000/*  DEPENDENCIES: 1) THIS CLIST REQUIRES THAT TSO COMMAND             */
00290000/*                   CLRSCRN BE INSTALLED IN YOUR ENVIRONMENT.        */
00300000/*                                                                    */
00310000/*  USE: UPON ENTRY, THE USER IS ASKED TO SELECT THE OPTION HE        */
00320000/*       WISHES TO RUN.  OPTION 'A' MUST BE RUN AT LEAST ONCE         */
00330000/*       BEFORE RUNNING OPTION 'B'.                                   */
00340000/*                                                                    */
00350000/*SJM/RGM**************************************************************/
00360000            /* CLRSCRN */
00370000NOTE '****************************************************************'
00380000NOTE '*  ANALYZE IMS ZIBS AND FAQES FOR STORAGE POOL ALLOCATION      *'
00390000NOTE '****************************************************************'
00400000WRITE
00410000WRITE ****************************************************************
00420000WRITE *  SYMBOLS EQUATED: ZIB# = A ZIB WITH A UNIQUE NUMBER ATTACHED *
00430000WRITE *              FAQE# = A FAQE WITH A UNIQUE NUMBER ATTACHED    *
00440000WRITE ****************************************************************
00450000WRITE
00460000WRITENR DO YOU WANT THE PRINT OPTION ?  (Y)/N ===>
00470000READ &ANS
00480000IF &ANS=N THEN SETD NOPRINT   /* DO SETD FOR NO PRINT IF ANS=N */
00490000          ELSE SETD PRINT  /* ANYTHING ELSE, TAKE DEFAULT OF YES. */
00500000  DOZIBS: WRITE
00510000WRITE
00520000WRITE ++++ OPTIONS ARE:
00530000WRITE                (A) INITIALIZE POINTERS TO ZIBS AND DISPLAY THEM
00540000WRITE                (B) ANALYZE SELECTED ZIBS AND ASSOCIATED FREE FAQES
00550000WRITE                (X) EXIT AND RETURN TO OPTION MENU
00560000WRITE
00570000WRITENR ENTER A, B, OR X ===>
00580000            READ &ANSAN
00590000            IF &ANSAN = &STR(A) THEN GOTO DOZIB1  /* GO INITIALIZE */
00600000            IF &ANSAN = &STR(B) THEN GOTO EP02AA  /* GO GET SPECIFICS*/
00610000            IF &ANSAN = &STR(X) THEN EXIT         /* GO BYE-BYE */
00620000            WRITE INVALID REPLY. DEPRESS ENTER AND TRY AGAIN. /*DUMMY*/
00630000            READ &ANAA
00640000            /* CLRSCRN */
00650000            GOTO DOZIBS                       /* ASK 'EM AGAIN. */
00660000    DOZIB1: SETD DISPLAY(SYMBOL STORAGE NOMACHINE NOREQUEST NOREMARK)
00670000            SET &NUMB = 1      /* INITIALIZE A COUPLE OF COUNTERS */
00680000            SET &NUMB2 = &NUMB + 1  /* TO KEEP TRACK OF ZIBS AND FAQES*/
00690000            EQU ZIB&NUMB MAINZIB+20% LENGTH(X'2C') STR(ZIB) /* DO EQU */
00700000            /* CLRSCRN */
00710000   ZIBLOOP: LIST ZIB&NUMB               /* LIST THE ZIB */
00720000            EQU FAQE&NUMB.1 ZIB&NUMB.+1C% LENGTH(X'C') /* FIRST FAQE */
00730000            RUNCHAIN ADDRESS(FAQE&NUMB.1) LINK(4) NAME(FAQE&NUMB.)
00740000            EVAL ZIB&NUMB.+4  /* RUN CHAIN OF FAQE'S (ABOVE) THEN  */
00750000            SET &ADDRNXT = &LASTCC  /* SET UP TO RUN NEXT ZIB, IF ANY */
00760000            IF &ADDRNXT = 0 THEN GOTO EP02 /* IF NONE, GOTO FORMATTING*/
00770000            EQU ZIB&NUMB2 ZIB&NUMB.+4% LENGTH(X'2C') STR(ZIB) /* ELSE,*/
00780000            SET &NUMB = &NUMB2        /* DO EQUATE FOR THIS ZIB, AND  */
00790000            SET &NUMB2 = &NUMB + 1    /* INCREMENT COUNTERS TO GET   */
00800000            GOTO ZIBLOOP              /* NEXT ONE.  */
00810000    EP02AA: L ZIB1 NODISPLAY /* CHECK FOR ZIBS INITIALIZED YET. IF SO,*/
00820000            IF &LASTCC = 0 THEN GOTO EP02 /* SYMBOL ZIB1 MUST EXIST. */
00830000            WRITE ZIB POINTERS NOT INITIALIZED. HIT ENTER AND TRY AGAIN.
00840000            READ &ANAN
00850000            GOTO DOZIBS          /* NOPE, MUST GO THRU ALL ZIBS FIRST.*/
00860000      EP02: SETD DISPLAY(NOMACH NOREQ NOSYM NOREMARK STORAGE)
00870000   LASTZIB: WRITENR ENTER IPCS NAME OF ZIB TO BE ANALYZED, OR END ===>
00880000            READ &ANS
00890000            IF &ANS = &STR(END) THEN GOTO ZIBQUIT /* IF ANS=END, DO IT*/
00900000            L &ANS NODISPLAY    /* OTHERWISE, LIST THE NAME WE GOT AS */
00910000            IF &LASTCC^=0 THEN DO /*INPUT TO SEE IF THE SYMBOL EXISTS.*/
00920000            WRITE &ANS IS INVALID.
00930000            WRITE
00940000            GOTO LASTZIB  /* SYMBOL GIVEN DOESN'T EXIST, ASK AGAIN. */
00950000            END
00960000    ZZLOOP: /* CLRSCRN */ +
00970000         NOTE '********************************************************'
00980000         NOTE '*  ZIB          |   POOL     NEXT     POOL     POOL   **'
00990000         NOTE '*OFFSET   ADDR  |   SIZE      ZIB     ADDR     NAME   **'
01000000            LIST &ANS L(16)
01010000         NOTE '*                                                     **'
01020000         NOTE '*               |                     BUFF     FREE   **'
01030000         NOTE '*               |    QCB (8 BYTES)    SIZE     FAQE   **'
01040000            LIST &ANS.+10 L(16)
01050000         NOTE '*                                                     **'
01060000         NOTE '*               |   ALLOC    AMT       MAX            **'
01070000         NOTE '*               |   FAQE    ALLOC     ALLOC           **'
01080000            LIST &ANS.+20 L(12)
01090000         NOTE '********************************************************'
01100000            SET &LEN = &LENGTH(&ANS.)      /* GET LENGTH OF ANSWER */
01110000            SET &NUMB = &SUBSTR(4:&LEN.,&ANS.) /*TAKE ZIB NUMBER OUT */
01120000            SET &N = 1         /* COUNTER FOR FAQE NUMBERING. */
01130000            SET &L = 0      /* COUNTER FOR FORMATTING ON 3278-2 */
01140000            EQU FAQE&NUMB.&N &ANS.+1C% LENGTH(X'C') /* DO EQUATE OF */
01150000            NOTE ' '                             /* FREE FAQE  */
01160000            NOTE '          FREE FAQE EVALUATION'
01170000   FAQZIBL: NOTE '************************************************'
01180000            NOTE '* FAQE  |   AREA     NEXT     START           **'
01190000            NOTE '* ADDR  |   SIZE     FAQE     ADDR            **'
01200000            LIST FAQE&NUMB.&N DISPLAY(NOSYMBOL)
01210000            NOTE '************************************************'
01220000            IF &N=1 THEN DO         /* FIRST SCREEN WILL CONTAIN */
01230000               NOTE '  '            /* THE ZIB AND THE FIRST FAQE.*/
01240000               NOTE '  '            /* PUT IN EXTRA BLANK LINE TO */
01250000            END                     /* HIT BOTTOM OF SCREEN.      */
01260000               ELSE DO              /* ALL OTHER SCREENS WILL     */
01270000               SET &L=&L+1          /* CONTAIN FOUR FAQE'S PER    */
01280000               IF &L=4 THEN DO      /* SCREEN.   FOR OTHER SCREEN */
01290000                  SET &L=0          /* SIZES THIS FORMATTING MAY  */
01300000                  GOTO CONTINUE     /* NOT RESULT IN FAQE'S BEING */
01310000               END                  /* DISPLAYED CORRECTLY. FEEL  */
01320000               NOTE '  '            /* FREE TO REMOVE THIS CODE.  */
01330000            END                     /* IT WILL NOT AFFECT ANY LOGIC.*/
01340000  CONTINUE: EVAL FAQE&NUMB.&N.+4    /* ANY MORE FAQE'S ON CHAIN ? */
01350000            IF &LASTCC = 0 THEN GOTO ENDFAQE /* NO - GO TO NEXT ZIB.*/
01360000            SET &N2 = &N + 1                 /* YES - GOTO NEXT FAQE*/
01370000            EQU FAQE&NUMB.&N2 FAQE&NUMB.&N.+4% LENGTH(X'C') /* EQUATE*/
01380000            SET &N = &N2                /* NEXT FAQE, BUMP COUNTER. */
01390000            GOTO FAQZIBL                /* GO DISPLAY IT. */
01400000   ENDFAQE: NOTE 'LAST FAQE HAS BEEN EVALUATED:'
01410000            WRITENR ENTER NEXT ZIB TO BE EVALUATED, OR 'END' ===>
01420000            READ &ANS
01430000            IF &ANS = &STR(END) THEN GOTO ZIBQUIT
01440000            GOTO ZZLOOP
01450000   ZIBQUIT: WRITE ZIB ANALYSIS COMPLETE. DEPRESS ENTER TO CONTINUE.
01460000            READ
01470000            GOTO DOZIBS
