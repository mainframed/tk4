00010011PROC     0                     +
00020011         ALLOCATE              +
00030011         ASID()                +
00040011         CONFIRM   NOCONFIRM   +
00050011         CPU()                 +
00060011         DATASET() NODATASET   +
00070011         DEBUG                 +
00080011         DISPLAY() NODISPLAY() +
00090011         DSNAME()  NODSNAME    +
00100011         FLAG()                +
00110011         FREE                  +
00120011         LENGTHOF()            +
00130011         LIST      NOLIST      +
00140011         NEW                   +
00150011         PRINT     NOPRINT     +
00160011         PROBLEM() NOPROBLEM   +
00170011         TERMINAL  NOTERMINAL  +
00180011         TEST      NOTEST      +
00190011         VERIFY    NOVERIFY
00200011GLOBAL DSN LVL VOLSER
00210011CONTROL END(QUAZ) NOFLUSH
00220011/**********************************************************************/
00230011/*       IPCS CLIST TO EXECUTE THE IPCS SETDEF SUBCOMMAND AND SAVE    */
00240011/*       THE PARAMETERS IN A DATA SET.  ALL PARAMETERS ARE THE SAME   */
00250011/*       AS THE SETDEF SUBCOMMAND, EXCEPT FOR THE ADDITION OF         */
00260011/*       ALLOCATE, FREE, AND NEW, WHICH MANIPULATE THE DATA SET.      */
00270011/*       PARAMETERS WHICH ARE NOT SPECIFIED ARE NOT CHANGED IN THE    */
00280011/*       DATA SET.  FURTHERMORE, VALUES IN THE DATA SET ARE PASSED TO */
00290011/*       THE SETDEF SUBCOMMAND EACH TIME THE CLIST IS EXECUTED.       */
00300011/**********************************************************************/
00310011IF       &DEBUG = DEBUG +
00320011THEN     CONTROL CONLIST LIST MSG PROMPT SYMLIST
00330011/**********************************************************************/
00331011DONEW:   +
00340011IF       &NEW = NEW +
00350011THEN     DO    /* CREATE NEW IPCS FILE */
00360011         DELETE IPCSPARM
00370011         ATTR  IPCS RECFM(V B) LRECL(255) BLKSIZE(3120)
00380011         FREE  FILE(DEFAULT)
00390011         ALLOC FILE(DEFAULT) DA(IPCSPARM) NEW SP(1) TRACK USING(IPCS)
00400011         FREE  ATTR(IPCS)
00410011         OPENFILE DEFAULT OUTPUT
00420011         SET   &DEFAULT = &STR( )
00430011         SET   &INDEX = 1
00440011         DO    WHILE &INDEX <= 13 /* WRITE BLANK RECORDS */
00450011               PUTFILE DEFAULT
00460011               SET   &INDEX = &INDEX+1
00470011               QUAZ /* WRITE BLANK RECORDS */
00480011         CLOSFILE DEFAULT
00490011         QUAZ /* CREATE NEW IPCS FILE */
00500011/**********************************************************************/
00510011IF       &ALLOCATE = ALLOCATE +
00520011THEN     DO    /* ALLOCATE IPCS FILE */
00530011         CONTROL NOMSG
00540011         FREE  FILE(DEFAULT)
00560011         ALLOC FILE(DEFAULT) DA(IPCSPARM)
00561011         IF &LASTCC ^= 0 THEN DO
00562011                              SET &NEW = NEW
00563011                              GOTO DONEW
00564011                              QUAZ
00565011         CONTROL MSG
00570011         QUAZ  /* ALLOCATE IPCS FILE */
00580011/**********************************************************************/
00590011IF       &FREE = FREE +
00600011THEN     DO    /* FREE IPCS FILE */
00610011         FREE  FILE(DEFAULT)
00620011         EXIT
00630011         QUAZ  /* FREE IPCS FILE */
00640011/**********************************************************************/
00650011OPENFILE DEFAULT INPUT
00660011GETFILE  DEFAULT
00670011IF       &ASID = +
00680011THEN     SET   &XASID = &STR(&DEFAULT)
00690011ELSE     IF    &ASID = 0 +
00700011         THEN  SET   &XASID = &STR( )
00710011         ELSE  SET   &XASID = ASID(&ASID)
00720011SET      &DEFAULT = &STR(&XASID)
00730011GETFILE  DEFAULT
00740011IF       &CONFIRM = && &NOCONFIRM = +
00750011THEN     SET   &XCONFIRM = &STR(&DEFAULT)
00760011ELSE     SET   &XCONFIRM = &CONFIRM&NOCONFIRM
00770011SET      &DEFAULT = &STR(&XCONFIRM)
00780011GETFILE  DEFAULT
00790011IF       &CPU = +
00800011THEN     SET   &XCPU = &STR(&DEFAULT)
00810011ELSE     SET   &XCPU = CPU(&CPU)
00820011SET      &DEFAULT = &STR(&XCPU)
00830011GETFILE  DEFAULT
00840011IF       &DATASET = && &NODATASET = +
00850011THEN     IF    &DSNAME = +
00860011         THEN  SET   &XDATASET = &STR(&DEFAULT)
00870011         ELSE  SET   &XDATASET = &STR( )
00880011ELSE     IF    &DATASET = +
00890011         THEN  SET   &XDATASET = &NODATASET
00900011         ELSE  SET   &XDATASET = DATASET('&DATASET')
00910011SET      &DEFAULT = &STR(&XDATASET)
00920011GETFILE  DEFAULT
00930011IF       &DISPLAY = && &NODISPLAY = +
00940011THEN     SET   &XDISPLAY = &STR(&DEFAULT)
00950011ELSE     IF    &DISPLAY = +
00960011         THEN  SET   &XDISPLAY = NODISPLAY(&NODISPLAY)
00970011         ELSE  SET   &XDISPLAY = DISPLAY(&DISPLAY)
00980011SET      &DEFAULT = &STR(&XDISPLAY)
00990011GETFILE  DEFAULT
01000011IF       &DSNAME = && &NODSNAME = +
01010011THEN     IF    &DATASET = +
01020011         THEN  SET   &XDSNAME = &STR(&DEFAULT)
01030011         ELSE  SET   &XDSNAME = &STR( )
01040011ELSE     IF    &DSNAME = +
01050011         THEN  SET   &XDSNAME = &NODSNAME
01060011         ELSE  SET   &XDSNAME = DSNAME('&DSNAME')
01070011SET      &DEFAULT = &STR(&XDSNAME)
01080011GETFILE  DEFAULT
01090011IF       &FLAG = +
01100011THEN     SET   &XFLAG = &STR(&DEFAULT)
01110011ELSE     SET   &XFLAG = FLAG(&FLAG)
01120011SET      &DEFAULT = &STR(&XFLAG)
01130011GETFILE  DEFAULT
01140011IF       &LENGTHOF = +
01150011THEN     SET   &XLENGTH = &STR(&DEFAULT)
01160011ELSE     SET   &XLENGTH = LENGTH(&LENGTHOF)
01170011SET      &DEFAULT = &STR(&XLENGTH)
01180011SET      &XLIST = &LIST&NOLIST
01190011GETFILE  DEFAULT
01200011IF       &PRINT = && &NOPRINT = +
01210011THEN     SET   &XPRINT = &STR(&DEFAULT)
01220011ELSE     SET   &XPRINT = &PRINT&NOPRINT
01230011SET      &DEFAULT = &STR(&XPRINT)
01240011GETFILE  DEFAULT
01250011IF       &PROBLEM = && &NOPROBLEM = +
01260011THEN     SET   &XPROBLEM = &STR(&DEFAULT)
01270011ELSE     IF    &PROBLEM = +
01280011         THEN  SET   &XPROBLEM = &NOPROBLEM
01290011         ELSE  SET   &XPROBLEM = PROBLEM(&PROBLEM)
01300011SET      &DEFAULT = &STR(&XPROBLEM)
01310011GETFILE  DEFAULT
01320011IF       &TERMINAL = && &NOTERMINAL = +
01330011THEN     SET   &XTERMINAL = &STR(&DEFAULT)
01340011ELSE     SET   &XTERMINAL = &TERMINAL&NOTERMINAL
01350011SET      &DEFAULT = &STR(&XTERMINAL)
01360011GETFILE  DEFAULT
01370011IF       &TEST = && &NOTEST = +
01380011THEN     SET   &XTEST = &STR(&DEFAULT)
01390011ELSE     SET   &XTEST = &TEST&NOTEST
01400011SET      &DEFAULT = &STR(&XTEST)
01410011GETFILE  DEFAULT
01420011IF       &VERIFY = && &NOVERIFY = +
01430011THEN     SET   &XVERIFY = &STR(&DEFAULT)
01440011ELSE     SET   &XVERIFY = &VERIFY&NOVERIFY
01450011SET      &DEFAULT = &STR(&XVERIFY)
01460011CLOSFILE DEFAULT
01470011/**********************************************************************/
01480011SETDEF   &XASID   +
01490011         &XCONFIRM +
01500011         &XCPU    +
01510011         &XDATASET +
01520011         &XDISPLAY +
01530011         &XDSNAME +
01540011         &XFLAG   +
01550011         &XLENGTH +
01560011         &XLIST   +
01570011         &XPRINT  +
01580011         &XPROBLEM +
01590011         &XTERMINAL +
01600011         &XTEST   +
01610011         &XVERIFY
01620011/**********************************************************************/
01630011OPENFILE DEFAULT OUTPUT
01640011SET      &DEFAULT = &STR(&XASID)
01650011PUTFILE  DEFAULT
01660011SET      &DEFAULT = &STR(&XCONFIRM)
01670011PUTFILE  DEFAULT
01680011SET      &DEFAULT = &STR(&XCPU)
01690011PUTFILE  DEFAULT
01700011SET      &DEFAULT = &STR(&XDATASET)
01710011PUTFILE  DEFAULT
01720011SET      &DEFAULT = &STR(&XDISPLAY)
01730011PUTFILE  DEFAULT
01740011SET      &DEFAULT = &STR(&XDSNAME)
01750011PUTFILE  DEFAULT
01760011SET      &DEFAULT = &STR(&XFLAG)
01770011PUTFILE  DEFAULT
01780011SET      &DEFAULT = &STR(&XLENGTH)
01790011PUTFILE  DEFAULT
01800011SET      &DEFAULT = &STR(&XPRINT)
01810011PUTFILE  DEFAULT
01820011SET      &DEFAULT = &STR(&XPROBLEM)
01830011PUTFILE  DEFAULT
01840011SET      &DEFAULT = &STR(&XTERMINAL)
01850011PUTFILE  DEFAULT
01860011SET      &DEFAULT = &STR(&XTEST)
01870011PUTFILE  DEFAULT
01880011SET      &DEFAULT = &STR(&XVERIFY)
01890011PUTFILE  DEFAULT
01900011CLOSFILE DEFAULT
