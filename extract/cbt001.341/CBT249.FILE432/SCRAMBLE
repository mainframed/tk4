00010000 PROC 2 DSN KEY  VOLUME() UNIT(SYSDA) ENCIPHER DECIPHER TIME SKIPREC(0)
00020000 CONTROL NOMSG MAIN END(ENDD)
00030000 IF (&SYSPCMD = EDIT | &SYSPCMD = E ) THEN /* OOPS..NOT UNDER EDIT */+
00040000   DO
00050000     WRITE ****RETURN TO TSO COMMAND LEVEL TO EXECUTE SCRAMBLE ***
00060000     EXIT QUIT
00070000   ENDD
00080000 IF (&ENCIPHER =  && &DECIPHER =   ) THEN +
00090000   DO
00100000     WRITE ..YOU MUST SPECIFY 'ENCIPHER' OR 'DECIPHER' AS PART OF
00110000     WRITE   SCRAMBLE COMMAND (E.G. 'SCRAMBLE DSNAME MYKEY ENCI')
00120000     EXIT QUIT
00130000   ENDD
00140000 IF (&ENCIPHER ^=  && &DECIPHER ^=   ) THEN +
00150000   DO
00160000     WRITE ..YOU MUST SPECIFY EITHER 'ENCIPHER' OR 'DECIPHER' (BUT
00170000     WRITE   NOT BOTH) AS PART OF THE SCRAMBLE COMMAND.
00180000     EXIT QUIT
00190000   ENDD
00200000 IF &ENCIPHER ^=   THEN  SET &OP = &STR(E)
00210000                   ELSE  SET &OP = &STR(D)
00220000 IF (&LENGTH(&KEY) > 8) THEN +
00230000   DO
00240000     WRITE ..INVALID KEY '&KEY'. MUST BE 1 TO 8 CHARACTERS IN LENGTH
00250000     WRITE   FIRST CHARACTER IS ALPHEBETIC.
00260000     EXIT QUIT
00270000   ENDD
00280000 IF (&DATATYPE(&SKIPREC) ^= NUM) THEN +
00290000   DO
00300000     WRITE ..INVALID NUMBER IN SKIPREC(&SKIPREC)..
00310000     WRITE   MUST BE VALID NUMBER OF RECORDS TO SKIP BEFORE OPERATION
00320000     EXIT QUIT
00330000   ENDD
00340000 FREE F(SYSPRINT,DATASET)
00350000 ALLOC F(SYSPRINT) DA(*)
00360000 IF &VOLUME ^=   THEN +
00370000   DO
00380000     SET &V = &STR(VOLUME(&VOLUME) UNIT(&UNIT) )
00390000     WRITE &DSN ON &VOLUME WILL BE USED
00400000  ENDD
00410000 ELSE +
00420000     SET &V = &STR()
00430000 CONTROL MSG
00440000 ALLOC F(DATASET) DA(&DSN.) SHR &V
00450000 SET &RC = &LASTCC
00460000 CONTROL NOMSG
00470000 IF &RC ^= 0 THEN +
00480000   DO
00490000     WRITE ..UNABLE TO RUN SCRAMBLE..I QUIT
00500000     EXIT QUIT
00510000   ENDD
00520000 ATTN +
00530000   DO
00540000     ATTN OFF
00550000     WRITE **ATTENTION ACCEPTED**
00560000     WRITENR IF YOU WANT TO QUIT AND SCREW UP THE DATASET, REPLY 'YES'
00570000     READ
00580000     IF &STR(&SYSDVAL) = &STR(YES) THEN +
00590000       DO
00600000         WRITE ..OK, ITS YOUR FUNERAL...
00610000         FREE F(DATASET)
00620000         EXIT QUIT
00630000       ENDD
00640000     WRITE ..RETURNING TO SCRAMBLE
00650000     RETURN
00660000   ENDD
00670000 IF &TIME ^=  THEN DTIME
00680001 $P SCRAMBLE    '/&OP,&KEY,&SKIPREC'
00690000 IF &TIME ^=  THEN DTIME
00700000 FREE F(DATASET)
00710000 END
