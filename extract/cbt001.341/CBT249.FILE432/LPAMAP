00010001PROC 0 DEBUG
00020001   IF &DEBUG^=DEBUG THEN CONTROL MSG NOSYM NOCON NOLIST NOPROMPT
00030001                    ELSE CONTROL MSG   SYM   CON   LIST   PROMPT
00040001      WRITENR ADDRESS  ADDRESS/(LIST) ===>
00050001      READ &ADDRESS
00060001      IF &ADDRESS=  THEN SET &ADDRESS=LIST
00070001      WRITENR DO YOU WANT PRINT THE OUTPUT?       Y/(N) ===>
00080001      READ &ANS
00090001      IF &ANS EQ Y  THEN SETDEF PRINT
00100001                    ELSE SETDEF NOPRINT
00110000      WRITENR DO YOU WANT OUTPUT TO THE TERMINAL? (Y)/N ===>
00120000      READ &ANS
00130000      IF &ANS EQ N  THEN SETDEF NOTERMINAL
00140000                    ELSE SETDEF TERMINAL
00150000    SETDEF DISPLAY(SYMBOL,NOMACHINE)
00160000    GLOBAL &DECVALUE
00170000    SET &FOUND = 0
00180000    NOTE ' ******************************************************** '
00190000    NOTE '                  LPAMAP DISPLAY                          '
00200000    NOTE ' ******************************************************** '
00210000    EQ CVTPTR 10. LEN(4) PTR
00220000    EQ CVTLPAQ CVTPTR%+BC LEN(4) PTR
00230000    EQ CDCHAIN CVTLPAQ% LEN(4)
00240007    IF &ADDRESS NE LIST THEN  +
00250006        %DECIMAL &ADDRESS &DEBUG
00260007NEXTCDE: +
00270000    EQ CDENTRY CDCHAIN% LEN(32)
00280000    COMPARE ADDR(CDCHAIN) WITH (VALUE(X'00000000'))
00290000    IF &LASTCC EQ 0 THEN GOTO EXIT
00300000    EQ CDCHAIN CDCHAIN% LEN(4) PTR
00310000    EQ CDNAME CDCHAIN+8 LEN(8) CHAR
00320000    EQ CDXLMJP CDCHAIN+14% LEN(4) PTR
00330000    EVALUATE  CDXLMJP+8
00340000    SET &XTLMSBLN = &LASTCC
00350000    EVALUATE    CDXLMJP+C
00360000    SET &XTLMSBAA = &LASTCC
00370000    SET &ENDLOC = &XTLMSBAA+&XTLMSBLN
00380000    IF &ADDRESS EQ LIST THEN GOTO LISTCDE
00390000    COMPARE ADDR(CDENTRY+1C) WITH (VALUE(X'04')) LEN(1) MASK(X'04')
00400000    IF &LASTCC = 0 THEN GOTO NEXTCDE /* SKIP MINOR CDE */
00410000    IF &DECVALUE LT &XTLMSBAA   THEN GOTO NEXTCDE
00420000    IF &DECVALUE GE &ENDLOC     THEN GOTO NEXTCDE
00430000    SET &FOUND = 1
00440000LISTCDE:                                                              +
00450000    NOTE ' '
00460000    LIST CDNAME
00470000    LIST CDXLMJP+8 LEN(8)
00480000    IF &ADDRESS EQ LIST THEN GOTO NEXTCDE
00490000EXIT:                                                                 +
00500000    IF &FOUND = 0 THEN DO
00510000        NOTE ' ADDRESS &ADDRESS NOT FOUND IN MASTER CDE LIST '
00520000        END
00530000    NOTE ' ********************************************************* '
00540000    NOTE ' *             END OF STORAGE LISTER                     * '
00550000    NOTE ' ********************************************************* '
