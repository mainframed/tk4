00010000PROC 0 VOLUME(IPCS01) PREFIX(IPOIPCS) NUMRECS(500) +
00020000DEBUG  LSTVOL()
00030000/*********************************************************************/
00040000/* REFER   IPO1.CMDPROC(SAVEDMPI)                                    */
00050000/* COMPID  SYSTEMS                                                   */
00060000/* DOC     THIS CLIST IS USED FOR INITIALIZATION OF THE DATASETS     */
00070000/*         NEEDED BY CLIST SAVEDMP. THE DMPLST DATASET AND THE       */
00080000/*         IPCS DUMP DIRECTORY ARE INITIALIZED, ALSO, THE MASTER     */
00090000/*         PROBLEM IS CREATED.                                       */
00100000/*                                                                   */
00110000/*********************************************************************/
00120000CONTROL NOLIST NOMSG  NOFLUSH NOPROMPT END(ENDO)
00130000IF &DEBUG EQ DEBUG THEN CONTROL LIST CONLIST SYMLIST MSG
00140000IF &LSTVOL EQ THEN SET LSTVOL=&VOLUME
00150000SET OPENDMP=N
00160000SET OPENPRT=N
00170000SET DELDMP=N
00180000SET DELDDR=N
00190000FREE FI(IPCSDDIR,IPCSPRNT,DMPLST) ATTR(V,F)
00200000CONTROL LIST MSG
00210000/*********************************************************************/
00220000/* ALLOCATE DMPLST DATASET                                           */
00230000/*********************************************************************/
00240000ATTRIB F RECFM(F B) LRECL(132) BLKSIZE(6072) DSORG(PS) BUFNO(1)
00250000ERROR DO
00260000  WRITE  ALLOCATION FOR SAVEDMP.DMPLST FALIED
00270000  GOTO CLEANUP
00280000ENDO
00290000ALLOC  FILE(DMPLST) DA('&PREFIX..SAVEDMP.DMPLST') USING(F)    +
00300000      VOLUME(&LSTVOL.) SPACE(&NUMRECS) BLOCK(132) ROUND
00310000SET DELDMP=Y
00320000OPENFILE DMPLST OUTPUT
00330000SET OPENDMP=Y
00340000/*********************************************************************/
00350000/* ALLOCATE NEW DUMP DIRECTORY FOR SAVEDMP                           */
00360000/*********************************************************************/
00370000ERROR DO
00380000  WRITE  ALLOCATION FOR SAVEDMP CLUSTER FAILED LASTCC=&LASTCC
00390000  GOTO CLEANUP
00400000ENDO
00410000DEFINE CLUSTER(NAME('&PREFIX..SAVEDMP.DEBUG') VOL(&VOLUME.)) +
00420000      INDEX(NAME('&PREFIX..SAVEDMP.DEBUGI') TRACKS(1,1)) +
00430000      DATA(NAME('&PREFIX..SAVEDMP.DEBUGD') +
00440000           CYLINDERS(1,1) KEYS(128 0) +
00450000      BUFFERSPACE(X'10000') CONTROLINTERVALSIZE(X'1000'))
00460000SET DELDDR=Y
00470000ERROR DO
00480000  WRITE  INITIALIZATION OF SAVEDMP CLUSTER FAILED LASTCC=&LASTCC
00490000  GOTO CLEANUP
00500000ENDO
00510000IPCSDDIR '&PREFIX..SAVEDMP.DEBUG' /* INITIALIZE THE DUMP DIRECTORY */
00520000/*********************************************************************/
00530000/* ALLOCATE THE DUMP DIRECTORY AND PRINT FILES                       */
00540000/*********************************************************************/
00550000CONTROL LIST MSG PROMPT
00560000ALLOC FI(IPCSDDIR) DA('&PREFIX..SAVEDMP.DEBUG') SHR
00570000ATTRIB V RECFM(V A) LRECL(125)        /* ATTRIUTE LIST FOR IPCSPRNT*/+
00580000  BUFNO(1) BLKSIZE(129) DSORG(PS)     /* FORCE A WRITE WITH BUFNO=1*/
00590000ALLOC FI(IPCSPRNT) SP(2,2) BLOCK(6144)/* ALLOCATE IPSCPRNT TEMP FIL*/+
00600000  USING(V)                            /* FORCE A WRITE EACH TIME   */
00610000/*********************************************************************/
00620000/* INVOKE IPCS AND SET UP AN INITIAL SETDEF IPCS SUBCOMMAND          */
00630000/*********************************************************************/
00640000ERROR DO
00650000  WRITE  INITIALIZATION OF IPCS FAILED LASTCC=&LASTCC
00660000  GOTO CLEANUP
00670000ENDO
00680000IPCS
00690000SETDEF LEN(32) FLAG(ERROR) TERMINAL NOPRINT NOCONFIRM
00700000/*********************************************************************/
00710000/* CREATE SAVEDMP MASTER PROBLEM
00720000/*********************************************************************/
00730000 ADDPROB ABSTRACT('THIS IS THE SAVEDMP DUMMY MASTER PROBLEM') +
00740000         GROUP(SAVEDMP) OWNER(SAVEDMP) DEFAULT                +
00750000         DESCRIPTION('SAVEDMP MASTER PROBLEM ADDED &SYSDATE &SYSTIME')
00760000/*********************************************************************/
00770000/* LIST THE PROB TO IPCSPRNT DATASET TO BE READ LATTER               */
00780000/*********************************************************************/
00790000  LISTPROB PRINT NOTERM DSN DESC        /* LIST PROBLEM TO IPCSPRNT  */
00800000  END      /* END IPCS SESSION */
00810000/*********************************************************************/
00820000/* GET PROBLEM NUMBER BY READING IPCSPRNT DATASET FROM LISTPROB ABOVE*/
00830000/*********************************************************************/
00840000 ERROR DO
00850000 MSGERR: +
00860000     WRITE IPCSPRNT COULD NOT BE READ LASTCC=&LASTCC
00870000     GOTO CLEANUP
00880000 ENDO
00890000  OPENFILE IPCSPRNT INPUT               /* OPEN IPCSPRNT FILE FOR IN */
00900000  SET OPENPRT=Y
00910000  GETFILE  IPCSPRNT                     /* IGNORE 1ST RECORD         */
00920000  GETFILE  IPCSPRNT                     /* IGNORE 2ND RECORD         */
00930000  GETFILE  IPCSPRNT                     /* IGNORE 3RD RECORD         */
00940000  GETFILE  IPCSPRNT                     /* USE 4TH RECORD            */
00950000  SET SYSDVAL = &STR(&IPCSPRNT)         /* INIT SYSDVAL FOR READDVAL */
00960000  READDVAL XXX PROBNAME                 /* GET THE PROBLEM NUMBER    */
00970000  IF &LENGTH(&PROBNAME) NE 8 THEN       /* DOUBLE CHECK FOR VALID    */+
00980000    GOTO MSGERR                         /* ERROR CODE FOR PROB# ERR  */
00990000  SET PROBNUM=&SUBSTR(4:8,&PROBNAME)    /* GET PROBLEM NUMBER        */
01000000  IF &DATATYPE(&PROBNUM) NE NUM THEN    /* CHECK NUMERIC PROB#       */+
01010000    GOTO MSGERR                         /* IF INVALID PROB# BR OUT   */
01020000  WRITE SAVEDMP MASTER PROBLEM IS &PROBNAME
01030000  SET I=1
01040000  SET DMPLST=&STR($ &PROBNUM   SAVEDMPI CREATED MASTER PROBLEM RECORD)
01050000  PUTFILE DMPLST
01060000  SET DMPLST=&STR(    )
01070000  DO WHILE &I < &NUMRECS
01080000    SET I=&I+1
01090000    PUTFILE DMPLST
01100000  ENDO
01110000  SET DELDMP=N
01120000  SET DELDDR=N
01130000CLEANUP: ERROR RETURN
01140000CONTROL NOMSG LIST NOFLUSH
01150000IF &OPENDMP=Y THEN +
01160000  CLOSFILE DMPLST
01170000IF &OPENPRT=Y THEN +
01180000  CLOSFILE IPCSPRNT
01190000IF DELDMP=Y THEN +
01200000  DELETE '&PREFIX..SAVEDMP.DMPLST'
01210000IF DELDDR=Y THEN +
01220000  DELETE '&PREFIX..SAVEDMP.DEBUG' CLUSTER
01230000FREE FI(IPCSDDIR,IPCSPRNT,DMPLST) ATTR(V,F)
