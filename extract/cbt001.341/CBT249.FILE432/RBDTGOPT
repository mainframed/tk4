00010001PROC 1 OPT
00020022CONTROL NOMSG
00030001   ISPEXEC VGET (GMJDLIB GMJDLIBX Z)
00040023   SET GMJDLIBY = &GMJDLIB
00050023   ISPEXEC VPUT GMJDLIBY SHARED
00060023   ISPEXEC SELECT PGM(FIXUPDSN) PARM(GMJDLIBY)
00070023   ISPEXEC VGET GMJDLIBY
00080023   IF &GMJDLIBX ^= &GMJDLIBY THEN +
00090023     DO                             /* WE HAVE CHANGED LIBRARIES  */
00100028       WHATIF DATASET(&GMJDLIB) CATALOGED
00110023       IF &LASTCC = 0 THEN +
00120023         DO
00130023           SET GMJDLIBX = &GMJDLIBY
00140023           ISPEXEC VPUT GMJDLIBX SHARED/* STOW THE NEW NAME          */
00150023         END
00160023       ELSE +
00170023         DO
00180023           SET SHORT = &STR(GMJDLIB NOT THERE)
00190023           SET LONG = &STR(GMJDLIB &GMJDLIB DOES NOT EXIST)
00200023           ISPEXEC SETMSG MSG(RBDT010)
00210023           EXIT
00220023         END
00230023     END
00240027   ISPEXEC VPUT GMJDLIB PROFILE                   /* STUFF IT        */
00250001   IF &OPT = B THEN +
00260019     DO
00270019        SET GOPT = &Z
00280019        ISPEXEC VPUT (GOPT)
00290019        %RBDTBLDT G
00300019     END
00310000/* ================================================================= */
00320000/* BUILD A SELECTION MENU OF THE AVAILABLE GMJD'S                    */
00330000/* ================================================================= */
00340000 
00350001   IF &OPT = D THEN +
00360000      DO
00370000/* ================================================================= */
00380000/* INVOKE DIRBUILD TO CONSTRUCT AN ISPF TABLE OF MEMBER NAMES        */
00390000/* ================================================================= */
00400000        ISPEXEC SELECT PGM(DIRBUILD) PARM(GMJDTBL,GMJDLIBX)
00410000/* ================================================================= */
00420000/* DETERMINE IF THE TABLE IS EMPTY OR NOT                            */
00430000/* ================================================================= */
00440000        ISPEXEC TBQUERY GMJDTBL ROWNUM(ROWS)
00450000        IF &ROWS = 0 THEN +
00460000           DO
00470018             SET GOPT = B
00480017             SET SHORT = &STR(GMJDLIB EMPTY)
00490017             SET LONG  = &STR(GMJD LIBRARY EMPTY - BUILD A GMJD FIRST)
00500018             ISPEXEC VPUT (SHORT LONG GOPT)
00510013             ISPEXEC SETMSG MSG(RBDT010)
00520007             EXIT
00530000           END
00540000/* ================================================================= */
00550000/* DISPLAY TABLE OF MEMBERS FOR PROCESSING                           */
00560000/* ================================================================= */
00570000TBDISP: +
00580009        SET DESC = &STR(ENTER "E" TO EDIT, "S" TO SUBMIT A MEMBER)
00590009        SET BDTFDSN = &GMJDLIB
00600002DISPFRST: +
00610020        ISPEXEC TBDISPL GMJDTBL PANEL(RBDTDIR) CURSOR(SEL)
00620009        SET TABLCC = &LASTCC
00630002CKSEL: +
00640009        IF &SEL ^= S && &SEL ^= E THEN +
00650009           DO
00660021             IF &TABLCC = 8 THEN +
00670021               GOTO ENDTAB
00680020             SET SHORT = NOT SUBMIT OR EDIT
00690009             SET LONG = &STR(FIELD MUST CONTAIN EITHER "S" OR "E")
00700009             ISPEXEC SETMSG MSG(RBDT010)
00710009             SET SEL = &Z
00720009             ISPEXEC TBDISPL GMJDTBL CURSOR(SEL)
00730009             SET &TABLCC = &LASTCC
00740009             GOTO CKSEL
00750009           END
00760009        GOTO CKIT
00770002DISPITEM: +
00780009        SET SEL =
00790009        ISPEXEC TBDISPL GMJDTBL CURSOR(SEL)           /* REDISPLAY  */
00800009        SET TABLCC = &LASTCC
00810002CKIT: +
00820009        ISPEXEC TBQUERY GMJDTBL POSITION(ROW)
00830009        IF &TABLCC = 8 THEN +
00840009           DO
00850009              IF &ROW = 0 THEN            +
00860009                 GOTO ENDTAB /* GOTO END OF INPUT, ELSE FALL THROUGH */
00870009           END
00880021        IF &ZCMD ^= &Z THEN +
00890021           DO                                   /* GARBAGE COMMAND   */
00900021             SET SEL = &Z
00910021             SET SHORT = &STR(INVALID COMMAND)
00920021             SET LONG = &STR(THE ENTRY IN THE COMMAND FIELD IS NOT +
00930021                RECOGNIZED)
00940021             ISPEXEC SETMSG MSG(RBDT010)
00950021             ISPEXEC TBDISPL MBDTAB
00960021             SET TABLCC = &LASTCC
00970021             GOTO CKIT
00980021           END
00990002 /*
01000002 /*   WE HAVE SOMETHING TO PROCESS WHEN &TABLCC ^> 8            */
01010002 /*
01020009        IF &TABLCC > 8 THEN +
01030009          DO
01040009            WRITE &STR(SEVERE ERROR, TBDISPL OF GMJDTBL, RC = &TABLCC)
01050009            GOTO ENDTAB
01060009          END
01070000/* ================================================================= */
01080000/* OPTION E - EDIT THE GMJD MEMBER                                   */
01090000/* ================================================================= */
01100002        IF &SEL = E THEN +
01110000          DO
01120008            ISPEXEC CONTROL DISPLAY SAVE
01130000            ISPEXEC EDIT DATASET(&GMJDLIB(&DIRNAME))
01140008            ISPEXEC CONTROL DISPLAY RESTORE
01150000            SET &DIRACT = EDITED
01160000            ISPEXEC TBPUT GMJDTBL
01170002            GOTO DISPITEM
01180000          END
01190000/* ================================================================= */
01200000/* SUBMIT GMJD TO MVS/BDT FOR PROCESSING                             */
01210000/* ================================================================= */
01220002        IF &SEL = S  THEN +
01230000          DO
01240000            SET &GMJDNAME = &DIRNAME
01250008            ISPEXEC CONTROL DISPLAY SAVE
01260000            ISPEXEC DISPLAY PANEL(RBDTGOVR)
01270025            SET DISPCC = &LASTCC
01280008            ISPEXEC CONTROL DISPLAY RESTORE
01290025            IF &DISPCC > 0 THEN +
01300025               EXIT
01310012            SET BDTGMJD = &STR(BDT &GMJDNAME GMJDLIB('&GMJDLIBX'))
01320000            &BDTGMJD &BDTGMJD1 &BDTGMJD2 &BDTGMJD3 &BDTGMJD4
01330024            SET &DIRACT = SUBMIT'D
01340000            ISPEXEC TBPUT GMJDTBL
01350002            GOTO DISPITEM
01360000          END
01370026        GOTO DISPITEM                       /* NOT E OR S            */
01380002ENDTAB: +
01390008      ISPEXEC TBEND GMJDTBL
01400003   END
01410001EXIT
