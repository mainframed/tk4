00010021PROC      0 +
00020026          DATASET('''AOST.SOFTLOGX''') +
00030002          DEBUG
00040001IF       &DEBUG = DEBUG +
00050001THEN     CONTROL   CONLIST NOFLUSH   LIST   MSG   PROMPT   SYMLIST
00060001ELSE     CONTROL NOCONLIST NOFLUSH NOLIST NOMSG   PROMPT NOSYMLIST
00070006FREE      FILE(ACCIN,DIRECTWK,EREPPT,SYSIN,TOURIST)
00080001CONTROL   MSG
00090003ALLOCATE  FILE(ACCIN) DATASET(&DATASET) SHR
00100025ALLOCATE  FILE(DIRECTWK) SPACE(5) CYLINDER NEW DELETE
00110021ALLOCATE  FILE(EREPPT) DATASET(EREPPT.LIST) NEW SPACE(1,1) TRACKS
00120011IF        &LASTCC ^= 0 +
00130006THEN      ALLOCATE  FILE(EREPPT) DATASET(EREPPT.LIST) OLD
00140024ALLOCATE  FILE(SYSIN) DUMMY
00150008IF        &DEBUG = DEBUG +
00160008THEN      ALLOCATE  FILE(TOURIST) DATASET(*)
00170008ELSE      ALLOCATE  FILE(TOURIST) DUMMY
00180024ISPEXEC   TBCREATE  SOFTLOG   NAMES(COMPMOD,CPU,CSECTID,DATE,JOBNAME,REASON,RECTYP,SEQNO,TIME) NOWRITE REPLACE
00190021ISPEXEC   SELECT    PGM(SOFTLOG)
00200021ISPEXEC   TBQUERY   SOFTLOG   ROWNUM(ROWNUM)
00210018SET       &ROWNUM = &EVAL(&ROWNUM)
00220021ISPEXEC   TBGET     SOFTLOG
00230021ISPEXEC   TBTOP     SOFTLOG
00240021ISPEXEC   TBSCAN    SOFTLOG   ARGLIST(DATE)
00250021ISPEXEC   TBDISPL   SOFTLOG   PANEL(SOFTLOG)
00260006SET       &TBDISPL = &LASTCC
00270006DO        WHILE     &TBDISPL <= 4 /* ENTER KEY PRESSED */
00280021          ISPEXEC   TBGET     SOFTLOG
00290006          IF        &LASTCC = 0 +
00300006          THEN      DO        /* ROW SELECTED */
00310006                    SET       &PACC = &STR(ACC=N)
00320006                    SET       &YY = &SUBSTR(1:2,&STR(&DATE))
00330006                    SET       &DDD = &SUBSTR(4:6,&STR(&DATE))
00340014                    SET       &PDATE = &STR(,DATE=(&YY&DDD))
00350006                    IF        &SEQNO ^= +
00360014                    THEN      SET       &PERRORID = &STR(,ERRORID=(&SEQNO))
00370013                    ELSE      SET       &PERRORID =
00380014                    SET       &PHIST = &STR(,HIST=Y)
00390014                    SET       &PLINECNT = &STR(,LINECT=999)
00400014                    SET       &PPRINT = &STR(,PRINT=PT)
00410012                    SET       &HH1 = &SUBSTR(1:2,&STR(&TIME))
00420012                    SET       &MM1 = &SUBSTR(4:5,&STR(&TIME))
00430012                    SET       &HH2 = &HH1
00440012                    SET       &MM2 = &MM1 + 1
00450012                    IF        &MM2 > 59 +
00460012                    THEN      DO        /* NEW HOUR */
00470012                              SET       &HH2 = &HH1 + 1
00480021                              IF        &HH2 = 24 +
00490012                              THEN      SET       &HH2 = 0
00500012                              SET       &MM2 = 0
00510012                              END       /* NEW HOUR */
00520012                    IF        &HH2 < 10 +
00530021                    THEN      SET       &HH2 = &STR(0&HH2)
00540012                    IF        &MM2 < 10 +
00550021                    THEN      SET       &MM2 = &STR(0&MM2)
00560014                    SET       &PTIME = &STR(,TIME=(&HH1&MM1,&HH2&MM2))
00570014                    SET       &PTYPE = &STR(,TYPE=)&SUBSTR(1,&STR(&RECTYP))
00580014                    SET       &PZERO = &STR(,ZERO=N)
00590014                    SET       &PARM = &STR(&PACC&PDATE&PERRORID&PHIST&PLINECNT&PPRINT&PTIME&PTYPE&PZERO)
00600006                    SYSCALL   IFCEREP1  '&PARM'
00610020                    %GENPRINT DSN(EREPPT.LIST) DEOPT(K) NODEL
00620006                    END       /* ROW SELECTED */
00630021          ISPEXEC   TBDISPL   SOFTLOG   PANEL(SOFTLOG)
00640006          SET       &TBDISPL = &LASTCC
00650006          END       /* ENTER KEY PRESSED */
00660021ISPEXEC   TBEND     SOFTLOG
00670024FREE      FILE(ACCIN,DIRECTWK,SYSIN,TOURIST)
00680017FREE      FILE(EREPPT) DELETE
