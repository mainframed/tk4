00010006PROC 1 REG1 PRINT NOPRINT TERMINAL NOTERMINAL TEST NOTEST
00020002/* ****************************************************************** */
00030002/*                                                                    */
00040006/*  CLIST NAME: SVC99                   REL/DATE  2.00/04.18.80       */
00050002/*                                                                    */
00060002/*  FUNCTION: DISPLAY THE DATA AREA STRUCTURE FOR DYNAMIC ALLOCATION  */
00070002/*                                                                    */
00080002/*  SYMBOLS EQUATED: NONE                                             */
00090002/*                                                                    */
00100002/*  SYMBOLS USED:                                                     */
00110002/*                REG1    - DYNAMIC ALLOCATION PARM ADDRESS           */
00120006/*                PRINT   - FOR HARDCOPY OUTPUT                      */
00130006/*                TERMINAL- FOR TERMINAL OUTPUT                      */
00140006/*                TEST    - OPTIONAL PARM FOR DEBUGING                */
00150002/*                                                                    */
00160002/*  RETURN: THIS CLIST WILL RETURN TO CALLER.                         */
00170002/*                                                                    */
00180002/*  CLIST CALLED BY: USER                                             */
00190002/*                                                                    */
00200002/*  CLISTS CALLED: DUMPDSN                                            */
00210002/*                                                                    */
00220002/*  DEPENDENCIES: MUST BE CALLED AS A SUBCOMMAND OF IPCS              */
00230002/*                                                                    */
00240002/*  USE: DISPLAY THE TEXT UNITS PASSED TO DYNAMIC ALLOCATION          */
00250002/*                                                                    */
00260002/* ****************************************************************** */
00270006    IF &TEST NE TEST THEN CONTROL MSG NOSYM NOCON NOLIST NOPROMPT
00280006      ELSE                CONTROL MSG   SYM   CON   LIST   PROMPT
00290006    IF &PRINT EQ  AND &NOPRINT EQ  THEN                                +
00300006      DO              /* DO LOOP #1 */
00310006      WRITENR DO YOU WANT PRINT THE OUTPUT? Y/(N)  ===>
00320006      READ &ANS
00330006      END                                              /* DO END  #1 */
00340006    IF &ANS EQ Y  THEN SET &PRINT = PRINT
00350006    IF &PRINT EQ PRINT THEN SETDEF PRINT
00360006    ELSE SETDEF NOPRINT
00370006    IF &NOTERMINAL EQ NOTERMINAL THEN SETDEF NOTERMINAL
00380006    SETDEF DISPLAY(MACHINE,NOSYMBOL)
00390002    NOTE ' ***************************************************** ' PAGE
00400003    NOTE '                SVC99 DISPLAY                          '
00410004    %DUMPDSN
00420002    NOTE ' ***************************************************** '
00430002/*
00440002/** * * * * * * * * * FIND THE REQUEST BLOCK * * * * * * * * * * * * */
00450002/*                         AND VALIDATE                              */
00460002/*                                                                   */
00470002    SET &NUMTXT = 0
00480002    EVAL &REG1.. LEN(1)
00490002    SET &TSTBIT = &LASTCC
00500002    IF &TSTBIT LT 128 THEN DO
00510002      NOTE '&TSTBIT'
00520002      NOTE '*** PTR TO SVC99 REQUEST BLOCK INVALID,'
00530002      NOTE '    HIGH ORDER BIT IS ZERO.'
00540002      GOTO END99
00550002      END
00560002    EQU S99RB &REG1..% LEN(20) STRUCTURE(S99RB)
00570002    NOTE ' SVC99 REQUEST BLOCK IS ...'
00580002    LIST S99RB DISPLAY(NOREQ)
00590000/*
00600000/* * * * * * * * * LOCATE TEXT BLOCK POINTER LIST * * * * * * * * * */
00610000/*
00620002    EQU TXTPTR S99RB+8% LEN(4)
00630002    SET &ENDIT = NO
00640002    SET &INCR = 0
00650000/*
00660000/* * * * * * * * * * LOOP TILL END OF TXT UNIT PTR LIST  * * * * * * */
00670000/*
00680002    DO WHILE &ENDIT EQ NO
00690002      COMPARE ADDR(TXTPTR+&INCR.N) LEN(4) WITH(VALUE(X'00000000')) +
00700002       MASK(X'00FFFFFF')
00710002      IF &LASTCC EQ 0 THEN GOTO EMPTY    /* IF TXT UNIT PTR IS 0,     */
00720002                                         /* IT IS IGNORED.            */
00730002      SET &NUMTXT = &NUMTXT+1            /* COUNT TOTAL VALID TXT UNTS*/
00740002      NOTE ' TEXT UNIT NUMBER &NUMTXT DATA IS ..... ' SPACE
00750002      EVAL TXTPTR+&INCR.N%+4 LEN(2)     /* GET LENGTH OF 1ST ENTRY.  */
00760002      SET &TXTLN = &LASTCC+6            /* SET LENGTH TO BE SHOWN.   */
00770002      EVAL TXTPTR+&INCR.N%+2 LEN(2)
00780002      IF &LASTCC LE 1 THEN GOTO OK      /* WE DON'T SUPPORT MULTI-   */
00790002                                        /* ENTRY TEXT UNIT (MVS DOES)*/
00800002      NOTE ' *** WARNING: MULTIPLE ENTRIES FOUND IN THE '
00810002      NOTE '     FOLLOWING TEXT UNIT.  ONLY THE FIRST IS DISPLAYED.'
00820003OK:       +
00830003      LIST TXTPTR+&INCR.N% LEN(&TXTLN) DISPLAY(NOREQUEST)
00840003EMPTY:   +
00850003      EVAL TXTPTR+&INCR.N LEN(1)        /* TEST FOR END OF X'80'     */
00860003      SET &VALU = &LASTCC                /* (END OF LIST) AND        */
00870003      IF &VALU GE 128 THEN SET &ENDIT=YES /* GET OUT OF LOOP.        */
00880003      SET &INCR = &INCR+4                /* NOT END, BUMP LIST PTR.  */
00890003      END
00900007END99:                                                                 +
00910005    NOTE ' TOTAL OF &NUMTXT TEXT UNITS FOUND ' SPACE
00920005    NOTE SPACE
00930002    NOTE ' ********************************************************** '
00940003    NOTE ' *             END OF SVC99 DISPLAY                       * '
00950002    NOTE ' ********************************************************** '
00960002    SETDEF DISPLAY(NOMACHINE,SYMBOL) NOPRINT TERMINAL
00970006    SYSCALL IEFBR14
00980002    EXIT
