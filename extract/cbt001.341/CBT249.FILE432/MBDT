00010012PROC 0 SDSN()
00020009 
00030009/* ================================================================= */
00040009/* CLIST MBDT                                                        */
00050009/*                                                                   */
00060009/* FUNCTION: TO DRIVE THE MVS/BDT ISPF DIALOG                        */
00070009/*                                                                   */
00080009/* FLOW:  1. INVOKE SHOWSPF TO EXTRACT THE CURRENT DEFAULT NODE NAME */
00090009/*        2. DISPLAY PANEL MBDT                                      */
00100009/*        3. IF OPTION = C THEN SELECT PANEL BDTUCMD                 */
00110009/*        4. IF OPTION = G THEN DO GMJD PROCESSING                   */
00120009/*           A. DETERMINE IF GMJDLIB EXISTS AND IF NOT               */
00130009/*              1. PROMPT THE USER WITH PANEL MBDTAGM                */
00140009/*              2. AND ALLOCATE THE GMJDLIB IF REQUESTED             */
00150009/*           B. INVOKE FIXUPDSN FOR GMJDLIBX VARIABLE                */
00160009/*           C. IF OPTION = S THEN DISPLAY PANEL MBDTGM TO ALLOW     */
00170009/*              FOR OVER-RIDES OF THE GMJD PRIOR TO SUBMIT TO TQI    */
00180009/*           D. IF OPTION = E THEN INVOKE ISPF EDIT TO ALLOW THE     */
00190009/*              USER TO ALTER THE GMJD.                              */
00200009/*           E. IF OPTION = B THEN GO BUILD A GMJD IN THE SAME       */
00210009/*              MANNER AS A TRANSACTION IS BUILT.                    */
00220009/*           F. IF OPTION IS NULL THEN BUILD A SELECTION MENU OF     */
00230009/*              ALL GMJD'S USING PROGRAM DIRBUILD.                   */
00240009/*              1. PROMPT THE USER WITH PANEL MBDTMEMS AND ALLOW     */
00250009/*                 EDIT AND SUBMIT OF THE GMJD.                      */
00260009/*        5. IF OPTION = T OR A GMJD BUILD REQUEST                   */
00270009/*           A. INVOKE FIXUPDSN TO FIX THE FROMDSN FOR FUTURE USE.   */
00280009/*           B. IF GMJD BUILD FIXUP THE TODSN.                       */
00290009/*           C. IF ON THE SENDING NODE -                             */
00300009/*              1. DETERMINE IF THE SENDING DATASET EXISTS AND       */
00310009/*                 VGET THE DCB, DSORG AND SPACE                     */
00320009/*                 A. IF DSORG=PO AND REQUESTED USE DIRBUILD TO BUILD*/
00330009/*                    A SELECTION MENU OF ALL MEMBERS.               */
00340009/*                 B. PROMPT THE USER WITH PANEL MBDTMEMS            */
00350009/*              2. IF THE DATASET DOES NOT EXIST                     */
00360009/*                 A. PROMPT THE USER WITH PANEL MBDTGG FOR THE      */
00370009/*                    DSORG                                          */
00380009/*                  B. IF A PDS AND A MEMBER SELECTION LIST WAS      */
00390009/*                     REQUESTED PROMPT THE USER WITH MEMU MBDTMEM   */
00400009/*           D. IF ON THE RECEIVEING NODE -                          */
00410009/*              1. DETERMINE IF THE DATASET EXISTS                   */
00420009/*                 A. IF IT DOES THEN INVOKE DSNINFO FOR DCB, SPACE  */
00430009/*                    AND DSORG                                      */
00440009/*                 B. IF NOT THEN PROMPT THE USER WITH PANEL MBDTGG  */
00450009/*                    FOR THE DSORG.                                 */
00460009/*                   1. IF REQUESTED THE PROMPT THE USER WITH PANEL  */
00470009/*                      MBDTMEM FOR UPTO 16 MEMBERS FOR SELECTION.   */
00480009/*           E. IF THE RECEIVEING DISPOSITION IS NEW THEN PROMPT     */
00490009/*              THE USER WITH PANEL MBDTTN TO VERIFY/CHANGE THE      */
00500009/*              ALLOCATION INFORMATION.                              */
00510009/*           F. IF VOLREF WAS SPECIFIED THEN INVOKE FIXUPDSN TO      */
00520009/*              CLEANUP THE VOLREF DSNAME.                           */
00530009/*           G. SET THE CORRECT DAP VALUE BASED UPON THE DSORG .     */
00540009/*           H. PERFORM ISPF FILE TAILORING USING SKELETON MBDT.     */
00550009/*           I. INVOKE CLIST MBDTGP TO PROMPT THE USER FOR THE       */
00560009/*              ACTION TO TAKE WITH THE COMPLETED TRANSACTION/GMJD.  */
00570009/*           J. IF IT WAS A GMJD THEN:                               */
00580009/*              1. EDIT THE GMJD                                     */
00590009/*              2. BROWSE THE GMJD                                   */
00600009/*              3. PRINT THE GMJD                                    */
00610009/*              4. CANCEL THE GMJD                                   */
00620009/*              5. SAVE THE GMJD BY USING IEBGENER TO COPY IT INTO   */
00630009/*                 THE GMJDLIB.                                      */
00640009/*           K. IF IT WAS A TRANSACTION THEN:                        */
00650009/*              1. EDIT THE TRANSACTION                              */
00660009/*              2. BROWSE THE TRANSACTION                            */
00670009/*              3. PRINT THE TRANSACTION                             */
00680009/*              4. CANCEL THE TRANSACTION                            */
00690009/*              5. EXECUTE THE TRANSACTION                           */
00700009/*           L. GO BACK TO (2) TO CONTINUE.                          */
00710009/*                                                                   */
00720009/* ================================================================= */
00730009/* IF VALUE SDSN IS ENTERED AS A PARAMETER WHEN THIS CLIST IS        */
00740009/* EXECUTED, PROCESSING WILL PROCEED AS IF OPTION T WAS SELECTED     */
00750009/* AND THE CLIST WILL EXIT AFTER THE TRANSACTION HAS BEEN            */
00760009/* ISSUED (OR CANCELLED).                                            */
00770009/* ================================================================= */
00780009/* ISPF PROGRAMS USED:                                               */
00790009/*    FIXUPDSN - FOR AN UNQUALIFIED DSN APPEND USERS PREFIX          */
00800009/*                FOR QUALIFIED DSN REMOVE QUOTES                    */
00810009/*    DSNINFO  - RETURN IN ISPF VARIABLES DCB AND SPACE INFO         */
00820009/*    SHOWSPF  - RETURN DEFAULT BDTNODE INTO ISPF VARIABLE           */
00830009/*    DIRBUILD - CONSTRUCT AN ISPF TABLE CONTAINING THE MEMBER NAMES */
00840009/*               FROM A PDS                                          */
00850009/* ================================================================= */
00860009 
00870009 
00880009 
00890009/* ================================================================= */
00900009/* EXTRACT THE CURRENT BDT NODE NAME FROM THE USVT AND UPDATE        */
00910009/* VARIABLES BDTFNODE AND CENTER WITH IT.                            */
00920009/* AND GET THE BDTTNODE FROM THE PROFILE.                            */
00930009/* ================================================================= */
00940009 
00950009ISPEXEC SELECT PGM(SHOWSPF) PARM(BDTNODE,BDTFNODE)
00960009ISPEXEC VGET BDTFNODE
00970009SET &CENTER = &STR(&BDTFNODE)
00980009 
00990009ISPEXEC VGET BDTTNODE
01000009 
01010009/* ================================================================= */
01020009/* TEST SDSN: IF PASSED THEN WE DO IT                                */
01030009/* ================================================================= */
01040009   IF &SDSN = THEN GOTO START
01050009 
01060009              SET &BDTFDSN = &SDSN
01070009              ISPEXEC VPUT BDTFDSN
01080009              SET &BDTOPT = T
01090009              SET &LCC = 0
01100009              GOTO DOIT
01110009 
01120009 
01130009 
01140009/* ================================================================= */
01150009/* DISPLAY SELECTION PANEL MBDT                                      */
01160009/* ================================================================= */
01170009 
01180010START: +
01190010        IF &SDSN ^= THEN EXIT
01200010 
01210010            ISPEXEC DISPLAY PANEL(MBDT)
01220010            IF &LASTCC > 0 THEN EXIT
01230009 
01240009/* ================================================================= */
01250009/* SET CLIST DEBUG OPTIONS IF DEBUG = DEBUG                          */
01260009/* ================================================================= */
01270009 
01280009IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST NOFLUSH PROMPT MSG
01290009          ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
01300009 
01310009/* ================================================================= */
01320009/* IF OPTION C (USER COMMANDS) SELECT DIALOG PANEL BDTUCMD           */
01330009/* ================================================================= */
01340009 
01350009IF &BDTOPT = C THEN DO
01360009                    ISPEXEC SELECT PANEL(BDTUCMD)
01370009                    GOTO START
01380009                    END
01390009 
01400009/* ================================================================= */
01410009/* INITIALIZE LCC VARIABLE                                           */
01420009/* ================================================================= */
01430009 
01440009SET &LCC = 0
01450009 
01460009/* ================================================================= */
01470009/* PROCESS ALL GMJD REQUESTS IF OPTION = G                           */
01480009/* ================================================================= */
01490009 
01500009GMJD: +
01510009IF &BDTOPT = G THEN DO
01520009 
01530009/* ================================================================= */
01540009/* DISPLAY PANEL MBDTG FOR GMJD PROCESSING OPTION                    */
01550009/* ================================================================= */
01560009 
01570009   ISPEXEC DISPLAY PANEL(MBDTG)
01580009   IF &LASTCC > 4 THEN GOTO START
01590009 
01600009/* ================================================================= */
01610009/* DETERMINE IF GMJDLIB EXISTS.                                      */
01620009/* IF NOT THEN DISPLAY PANEL MBDTAGM TO PROMPT THE USER IF HE WANTS  */
01630009/* TO ALLOCATE THE LIBRARY OR TO CANCEL THE REQUEST.                 */
01640009/* ================================================================= */
01650009 
01660009$C WHATIF DATASET(&GMJDLIB) CATALOGED
01670009IF &LASTCC = 4 THEN DO
01680009 
01690009/* ================================================================= */
01700009/* IF NOT THEN DISPLAY PANEL MBDTAGM TO PROMPT THE USER IF HE WANTS  */
01710009/* TO ALLOCATE THE LIBRARY OR TO CANCEL THE REQUEST.                 */
01720009/* ================================================================= */
01730009 
01740009ISPEXEC DISPLAY  PANEL(MBDTAGM)
01750009IF &LASTCC > 0 THEN GOTO START
01760009 
01770009/* ================================================================= */
01780009/* ALLOCATE THE NEW GMJD LIBRARY                                     */
01790009/* ================================================================= */
01800009 
01810009FREE ATTR(GMJDAT)
01820009ATTR GMJDAT LRECL(80) BLKSIZE(&BLKSZ) RECFM(F B)
01830009ALLOC DS(&GMJDLIB) NEW SPA(&PRI,&SEC) &UNITS DIR(&DIR) USI(GMJDAT)
01840009FREE ATTR(GMJDAT)  DATASET(&GMJDLIB)
01850009END
01860009 
01870009/* ================================================================= */
01880009/* CLEAN UP GMJDLIB DSNAME FOR USE BY DIRBUILD ROUTINE IF NEEDED     */
01890009/* ================================================================= */
01900009 
01910009SET &GMJDLIBX = &GMJDLIB
01920009ISPEXEC VPUT GMJDLIBX
01930009ISPEXEC SELECT PGM(FIXUPDSN) PARM(GMJDLIBX)
01940009ISPEXEC VGET GMJDLIBX
01950009 
01960009IF &GMJDOPT = B THEN GOTO DOIT
01970009 
01980009 
01990009/* ================================================================= */
02000009/* PROCESS THE SUBMIT OPTION FOR GMJD PROCESSING.                    */
02010009/* DISPLAY PANEL MBDTGM TO ALLOW TAILORING OF THE GMJD.              */
02020009/* ================================================================= */
02030009 
02040009IF &GMJDOPT = S THEN DO
02050009ISPEXEC DISPLAY PANEL(MBDTGM)
02060009IF &LASTCC > 0 THEN  GOTO GMJD
02070009 
02080009/* ================================================================= */
02090009/* EXECUTE THE GMJD AND DISPLAY IT ON THE TERMINAL FOR THE USER      */
02100009/* ================================================================= */
02110009 
02120009&BDTGMJD &BDTGMJD1 &BDTGMJD2 &BDTGMJD3 &BDTGMJD4
02130009WRITE &BDTGMJD &BDTGMJD1 &BDTGMJD2 &BDTGMJD3 &BDTGMJD4
02140009GOTO GMJD
02150009END
02160009 
02170009 
02180009/* ================================================================= */
02190009/* ALLOW THE USER TO EDIT THE PRE-BUILD GMJD                         */
02200009/* ================================================================= */
02210009 
02220009IF &GMJDOPT = E THEN DO
02230009ISPEXEC EDIT DATASET(&GMJDLIB(&GMJDNAME))
02240009SET &GMJDOPT = S
02250009GOTO GMJD
02260009END
02270009 
02280009 
02290009/* ================================================================= */
02300009/* BUILD A SELECTION MENU OF THE AVAILABLE GMJD'S                    */
02310009/* ================================================================= */
02320009 
02330009IF &GMJDOPT  = THEN DO
02340009SET &LCC = 0
02350009 
02360009 
02370009/* ================================================================= */
02380009/* INVOKE DIRBUILD TO CONSTRUCT AN ISPF TABLE OF MEMBER NAMES        */
02390009/* ================================================================= */
02400009 
02410009ISPEXEC SELECT PGM(DIRBUILD) PARM(GMJDTBL,GMJDLIBX)
02420009 
02430009/* ================================================================= */
02440009/* DETERMINE IF THE TABLE IS EMPTY OR NOT                            */
02450009/* ================================================================= */
02460009 
02470009ISPEXEC TBQUERY GMJDTBL ROWNUM(ROWS)
02480009 
02490009IF &ROWS = 0 THEN DO
02500009     SET &GMJDOPT = B
02510009     SET &SHORT = &STR(GMJDLIB EMPTY)
02520009     SET &LONG  = &STR(GMJD LIBRARY EMPTY - BUILD A GMJD FIRST)
02530009     ISPEXEC VPUT (SHORT LONG)
02540009     ISPEXEC SETMSG MSG(AOST010)
02550009     GOTO GMJD
02560009                  END
02570009 
02580009 
02590009/* ================================================================= */
02600009/* DISPLAY TABLE OF MEMBERS FOR PROCESSING                           */
02610009/* ================================================================= */
02620009 
02630009TBDISP: IF &LCC = 0 THEN ISPEXEC TBDISPL GMJDTBL PANEL(MBDTMEMS)
02640009                    ELSE ISPEXEC TBDISPL GMJDTBL
02650009SET &LCC = &LASTCC
02660009 
02670009/* ================================================================= */
02680009/* IF RETURN CODE = 4 THEN CLOSE TABLE AND RETURN TO GMJD MENU       */
02690009/* ================================================================= */
02700009 
02710009IF &LCC > 4 THEN DO
02720009     ISPEXEC TBEND GMJDTBL
02730009     GOTO GMJD
02740009     END
02750009 
02760009/* ================================================================= */
02770009/* OPTION E - EDIT THE GMJD MEMBER                                   */
02780009/* ================================================================= */
02790009 
02800009IF &SELECT = E THEN DO
02810009    ISPEXEC EDIT DATASET(&GMJDLIB(&DIRNAME))
02820009    SET &DIRACT = EDITED
02830009    ISPEXEC TBPUT GMJDTBL
02840009    END
02850009 
02860009/* ================================================================= */
02870009/* SUBMIT GMJD TO MVS/BDT FOR PROCESSING                             */
02880009/* ================================================================= */
02890009 
02900009IF &SELECT = S  THEN DO
02910009    SET &GMJDNAME = &DIRNAME
02920009    ISPEXEC DISPLAY PANEL(MBDTGM)
02930009    &BDTGMJD &BDTGMJD1 &BDTGMJD2 &BDTGMJD3 &BDTGMJD4
02940009    WRITE &BDTGMJD &BDTGMJD1 &BDTGMJD2 &BDTGMJD3 &BDTGMJD4
02950009    SET &DIRACT = SUBMITTED
02960009    ISPEXEC TBPUT GMJDTBL
02970009    END
02980009 
02990009SET &SELECT =
03000009GOTO TBDISP
03010009END
03020009END
03030009 
03040009 
03050009/* ================================================================= */
03060009/* PERFORM BUILD OF A TRANSACTION OR GMJD                            */
03070009/* ================================================================= */
03080009 
03090009DOIT: +
03100009ISPEXEC DISPLAY PANEL(MBDTT)
03110009IF &LASTCC > 4 THEN GOTO START
03120009 
03130009/* ================================================================= */
03140009/* SAVE THE TO NODE IN THE PROFILE                                   */
03150009/* ================================================================= */
03160009 
03170009ISPEXEC VPUT (BDTTNODE) PROFILE
03180009 
03190009/* ================================================================= */
03200009/* SET FROMDSN = BDTFDSN AND FIXUP FOR USE BY DSNINFO                */
03210009/* ================================================================= */
03220009 
03230009SET &FROMDSN = &BDTFDSN
03240009ISPEXEC VPUT FROMDSN
03250009ISPEXEC SELECT PGM(FIXUPDSN) PARM(FROMDSN)
03260009ISPEXEC VGET FROMDSN
03270009 
03280009/* ================================================================= */
03290009/* IF GMJD BUILD THEN FIXUP THE TODSN WITH BDTTDSN                   */
03300009/* ================================================================= */
03310009 
03320009IF &BDTOPT = G THEN DO
03330009SET &TODSN = &BDTTDSN
03340009ISPEXEC VPUT TODSN
03350009ISPEXEC SELECT PGM(FIXUPDSN) PARM(TODSN)
03360009ISPEXEC VGET TODSN
03370009END
03380009 
03390009/* ================================================================= */
03400009/* DETERMINE IF WE ARE AT THE SENDING NODE                           */
03410009/* ================================================================= */
03420009 
03430009IF &CENTER = &BDTFNODE THEN DO  /* FROM NODE HERE ? */
03440009 
03450009$C WHATIF DATASET(&BDTFDSN) CATALOGED  /* DOES THE DATASET EXIST ? */
03460009 
03470009IF &LASTCC = 0 THEN DO
03480009SET  &DSNINFO = &BDTFDSN
03490009ISPEXEC  VPUT DSNINFO
03500009ISPEXEC  SELECT PGM(DSNINFO)
03510009ISPEXEC VGET (DSORG VOL ALCYL TRALC INUNIT UNIT SCALO DIRALLOC RECFM +
03520009              LRECL BLKSIZE )
03530009 
03540009IF  &DSORG = PO THEN DO
03550009IF &BDTMEMX = YES THEN DO
03560009 
03570009/* ================================================================= */
03580009/* BUILD A SELECTION TABLE OF ALL MEMBER NAMES IN THE PDS            */
03590009/* ================================================================= */
03600009 
03610009ISPEXEC SELECT PGM(DIRBUILD) PARM(MBDTAB,FROMDSN)
03620009 
03630009/* ================================================================= */
03640009/* ADD THE ANY MEMBERS SPECIFIED ON THE INITIAL PANEL                */
03650009/* ================================================================= */
03660009 
03670009ISPEXEC TBCREATE MEMTABLE NAMES(DIRNAME)
03680009          SET &DIRNAME = &BDTFMEM1
03690009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
03700009          SET &DIRNAME = &BDTFMEM2
03710009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
03720009          SET &DIRNAME = &BDTFMEM3
03730009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
03740009          SET &DIRNAME = &BDTFMEM4
03750009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
03760009          SET &DIRNAME = &BDTFMEM5
03770009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
03780009SET &TABLCC = 0
03790009 
03800009BDTTAB: IF &TABLCC = 4 THEN ISPEXEC TBDISPL MBDTAB
03810009           ELSE ISPEXEC TBDISPL MBDTAB PANEL(MBDTMEMS)
03820009 
03830009SET &TABLCC = &LASTCC
03840009 
03850009IF &TABLCC > 4  THEN GOTO BDTTABE
03860009 
03870009IF &SELECT ^=  THEN ISPEXEC TBADD MEMTABLE
03880009 
03890009SET &DIRACT = SELECTED
03900009 
03910009ISPEXEC TBPUT MBDTAB
03920009 
03930009SET &SELECT =
03940009GOTO BDTTAB
03950009 
03960009BDTTABE:  ISPEXEC TBEND MBDTAB
03970009 
03980009SET &DIRNAME =
03990009 
04000009ISPEXEC TBTOP MEMTABLE
04010009 
04020009ISPEXEC TBSKIP MEMTABLE
04030009 
04040009SET &FIRSTMEM =  &DIRNAME
04050009ISPEXEC TBDELETE MEMTABLE
04060009ISPEXEC TBBOTTOM MEMTABLE
04070009IF &LASTCC = 0 THEN DO
04080009SET &LASTMEM = &DIRNAME
04090009ISPEXEC TBDELETE MEMTABLE
04100009END
04110009END
04120009END
04130009END
04140009 
04150009ELSE DO
04160009ISPEXEC DISPLAY PANEL(MBDTGG)
04170009 
04180009IF &DSORG = PO THEN DO
04190009IF &BDTMEMX = YES THEN DO
04200009 
04210009/* ================================================================= */
04220009/* CREATE TABLE OF MEMBERS FOR TRANSFER                              */
04230009/* ================================================================= */
04240009 
04250009ISPEXEC TBCREATE MEMTABLE NAMES(DIRNAME)
04260009 
04270009/* ================================================================= */
04280009/* ADD ANY MEMBERS SPECIFIED ON THE PANEL                            */
04290009/* ================================================================= */
04300009 
04310009          SET &DIRNAME = &BDTFMEM1
04320009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04330009          SET &DIRNAME = &BDTFMEM2
04340009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04350009          SET &DIRNAME = &BDTFMEM3
04360009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04370009          SET &DIRNAME = &BDTFMEM4
04380009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04390009          SET &DIRNAME = &BDTFMEM5
04400009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04410009 
04420009/* ================================================================= */
04430009/* NOW DISPLAY PANEL MBDTMEM FOR UPTO 16 MORE MEMBERS                */
04440009/* ================================================================= */
04450009 
04460009ISPEXEC DISPLAY PANEL(MBDTMEM)
04470009IF &LASTCC ^= 0 THEN GOTO BDTTABE2
04480009 
04490009/* ================================================================= */
04500009/* AND PLACE THEM INTO THE TABLE AS WELL                             */
04510009/* ================================================================= */
04520009 
04530009          SET &DIRNAME = &M1
04540009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04550009          SET &DIRNAME = &M2
04560009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04570009          SET &DIRNAME = &M3
04580009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04590009          SET &DIRNAME = &M4
04600009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04610009          SET &DIRNAME = &M5
04620009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04630009          SET &DIRNAME = &M6
04640009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04650009          SET &DIRNAME = &M7
04660009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04670009          SET &DIRNAME = &M8
04680009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04690009          SET &DIRNAME = &M9
04700009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04710009          SET &DIRNAME = &M10
04720009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04730009          SET &DIRNAME = &M11
04740009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04750009          SET &DIRNAME = &M12
04760009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04770009          SET &DIRNAME = &M13
04780009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04790009          SET &DIRNAME = &M14
04800009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04810009          SET &DIRNAME = &M15
04820009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04830009          SET &DIRNAME = &M16
04840009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
04850009 
04860009ISPEXEC TBSKIP MEMTABLE NUMBER(-1)
04870009SET &LASTMEM = &DIRNAME
04880009ISPEXEC TBDELETE  MEMTABLE
04890009ISPEXEC TBTOP MEMTABLE
04900009ISPEXEC TBSKIP MEMTABLE
04910009SET &FIRSTMEM = &DIRNAME
04920009ISPEXEC TBDELETE  MEMTABLE
04930009 
04940009BDTTABE2:  END
04950009END
04960009END
04970009END
04980009 
04990009/* ================================================================= */
05000009/* SEE IF WE ARE ON THE RECEIVEING CENTER                            */
05010009/* ================================================================= */
05020009 
05030009ELSE DO
05040009IF &CENTER = &BDTTNODE THEN DO
05050009 
05060009/* ================================================================= */
05070009/* DETERMINE IF THE RECEIVEING DATASET EXISTS  (IT MUST BE CATALOGED)*/
05080009/* ================================================================= */
05090009 
05100009$C WHATIF DATASET(&BDTTDSN) CATALOGED
05110009IF &LASTCC = 0 THEN DO
05120009 
05130009/* ================================================================= */
05140009/* DETERMINE DSORG OF RECEIVEING DATASET FOR DAP INITIALIZATION      */
05150009/* ================================================================= */
05160009 
05170009SET &DSNINFO = &BDTTDSN
05180009ISPEXEC VPUT DSNINFO
05190009ISPEXEC SELECT PGM(DSNINFO)
05200009ISPEXEC VGET (DSORG VOL ALCYL TRALC INUNIT UNIT SCALO DIRALLOC RECFM +
05210009              LRECL BLKSIZE )
05220009END
05230009 
05240009/* ================================================================= */
05250009/* IF IT DOES NOT THEN ASK FOR THE DSORG                             */
05260009/* ================================================================= */
05270009 
05280009ELSE ISPEXEC DISPLAY PANEL(MBDTGG)
05290009END
05300009 
05310009/* ================================================================= */
05320009/* DATASET NOT KNOWN ON EITHER NODE - ASK FOR DSORG                  */
05330009/* ================================================================= */
05340009 
05350009ELSE ISPEXEC DISPLAY PANEL(MBDTGG)
05360009 
05370009IF &DSORG = PO THEN DO
05380009IF &BDTMEMX = YES THEN DO
05390009 
05400009/* ================================================================= */
05410009/* CREATE MEMBER SELECTION TABLE                                     */
05420009/* ================================================================= */
05430009 
05440009ISPEXEC TBCREATE MEMTABLE NAMES(DIRNAME)
05450009 
05460009/* ================================================================= */
05470009/* ADD ANY SPECIFIED MEMBERS                                         */
05480009/* ================================================================= */
05490009 
05500009          SET &DIRNAME = &BDTFMEM1
05510009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05520009          SET &DIRNAME = &BDTFMEM2
05530009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05540009          SET &DIRNAME = &BDTFMEM3
05550009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05560009          SET &DIRNAME = &BDTFMEM4
05570009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05580009          SET &DIRNAME = &BDTFMEM5
05590009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05600009 
05610009/* ================================================================= */
05620009/* DISPLAY PANEL MBDTMEM FOR UPTO 16 ADDITIONAL MEMBERS              */
05630009/* ================================================================= */
05640009 
05650009ISPEXEC DISPLAY PANEL(MBDTMEM)
05660009 
05670009IF &LASTCC ^= 0 THEN GOTO BDTTABE3
05680009 
05690009/* ================================================================= */
05700009/* ADD THEM TO THE TABLE                                             */
05710009/* ================================================================= */
05720009 
05730009          SET &DIRNAME = &M1
05740009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05750009          SET &DIRNAME = &M2
05760009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05770009          SET &DIRNAME = &M3
05780009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05790009          SET &DIRNAME = &M4
05800009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05810009          SET &DIRNAME = &M5
05820009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05830009          SET &DIRNAME = &M6
05840009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05850009          SET &DIRNAME = &M7
05860009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05870009          SET &DIRNAME = &M8
05880009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05890009          SET &DIRNAME = &M9
05900009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05910009          SET &DIRNAME = &M10
05920009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05930009          SET &DIRNAME = &M11
05940009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05950009          SET &DIRNAME = &M12
05960009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05970009          SET &DIRNAME = &M13
05980009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
05990009          SET &DIRNAME = &M14
06000009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
06010009          SET &DIRNAME = &M15
06020009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
06030009          SET &DIRNAME = &M16
06040009          IF &DIRNAME  ^= THEN ISPEXEC TBADD MEMTABLE
06050009 
06060009ISPEXEC TBSKIP MEMTABLE NUMBER(-1)
06070009SET &LASTMEM = &DIRNAME
06080009ISPEXEC TBDELETE MEMTABLE
06090009ISPEXEC TBTOP MEMTABLE
06100009ISPEXEC TBSKIP MEMTABLE
06110009SET &FIRSTMEM = &DIRNAME
06120009ISPEXEC TBDELETE MEMTABLE
06130009 
06140009BDTTABE3: END
06150009 
06160009END
06170009END
06180009 
06190009/* ================================================================= */
06200009/* IF NEW DISP THEN PROMPT FOR SPACE ALLOCATIONS                     */
06210009/* THESE ARE PRE-DEFINED IF THE DATASET ALREADY EXISTED BY DSNINFO   */
06220009/* ================================================================= */
06230009 
06240009IF &BDTTDISP = NEW THEN DO
06250009ISPEXEC DISPLAY PANEL(MBDTTN)
06260009IF &LASTCC > 4 THEN GOTO START
06270009 
06280009IF &BDTOPT = G THEN DO
06290009 
06300009IF &VOLREF ^= THEN DO
06310009/* ================================================================= */
06320009/* FIXUP VOLREF DATASET NAME FOR GMJD BUILD                          */
06330009/* ================================================================= */
06340009          SET &VOLREFX = &VOLREF
06350009          ISPEXEC  VPUT VOLREFX
06360009          ISPEXEC SELECT PGM(FIXUPDSN) PARM(VOLREFX)
06370009          ISPEXEC  VGET VOLREFX
06380009          END
06390009 
06400009ELSE DO
06410009SET &VOLREFX =
06420009ISPEXEC VPUT VOLREFX
06430009END
06440009 
06450009END
06460009END
06470009 
06480009ELSE DO
06490009SET &VOLREFX =
06500009ISPEXEC VPUT VOLREFX
06510009END
06520009 
06530009/* ================================================================= */
06540009/* SET UP DAP WITH CORRECT VALUE BASED UPON DSORG DETERMINED ABOVE   */
06550009/* ================================================================= */
06560009 
06570009IF &DSORG = PS THEN SET &DAP = SEQ
06580009ELSE SET &DAP = PDS
06590009 
06600009/* ================================================================= */
06610009/* ================================================================= */
06620009/* PERFORM ISPF FILE TAILORING BUILD OF THE TRANSACTION OR GMJD      */
06630009/* IT WILL BE IN THE DATASET DEFINED BY THE ZTEMPF VARIABLE.         */
06640009/* ================================================================= */
06650009/* ================================================================= */
06660009 
06670009ISPEXEC FTOPEN TEMP
06680009ISPEXEC FTINCL MBDT
06690009ISPEXEC FTCLOSE
06700009ISPEXEC VGET ZTEMPF
06710009 
06720009/* ================================================================= */
06730009/* IF WE HAD A TABLE OF MEMBERS THEN CLOSE IT                        */
06740009/* ================================================================= */
06750009 
06760009IF &DSORG = PO THEN DO
06770009                    IF &BDTMEMX = YES THEN ISPEXEC TBEND MEMTABLE
06780009                    END
06790009 
06800009/* ================================================================= */
06810009/* ================================================================= */
06820009/* GO TO THE GENERALIZED PROMPTING CLIST TO DETERMINE THE ACTION TO  */
06830009/* PERFORM WITH THE NEWLY CONSTRUCTED TRANSACTION OR GMJD.           */
06840009/* ================================================================= */
06850009/* ================================================================= */
06860009 
06870009SET &MBDTOPT = 0
06880009%MBDTGP DSN('''&ZTEMPF''') &DEBUG BDTOPT(&BDTOPT)
06890009SET &MBDTOPT = &LASTCC
06900009 
06910009 
06920009IF &BDTOPT = G THEN DO
06930009/* ================================================================= */
06940009/* IF GMJD AND CODE = 4 THEN COPY INTO GMJDLIB FOR FUTURE USE        */
06950009/* ================================================================= */
06960009IF &MBDTOPT = 4 THEN GOTO GMJD
06970009FREE F(SYSUT1 SYSUT2 SYSPRINT SYSIN)
06980009ALLOC F(SYSUT1) DS('&ZTEMPF')
06990009ALLOC F(SYSUT2) DS('&GMJDLIBX(&GMJDNAME)')
07000009ALLOC F(SYSPRINT) DUMMY
07010009ALLOC F(SYSIN) DUMMY
07020009CALL 'SYS1.LINKLIB(IEBGENER)'
07030009FREE F(SYSUT1 SYSUT2 SYSPRINT SYSIN)
07040009ALLOC F(SYSPRINT) DS(*)
07050009ALLOC F(SYSIN) DS(*)
07060009SET &GMJDOPT = E
07070009GOTO GMJD
07080009END
07090009 
07100009/* ================================================================= */
07110009/* ================================================================= */
07120009/* IF CODE = 0 THEN EXECUTE THE TRANSACTION.  IT IS IN CLIST FORMAT  */
07130009/* AND IS EXECUTED VIA THE CLIST EX COMMAND WITH THE LIST OPTION     */
07140009/* TO ALLOW THE USER TO VIEW THE TRANSACTION AS IT IS EXECUTED.      */
07150009/* ================================================================= */
07160009/* ================================================================= */
07170009 
07180009IF &MBDTOPT = 0 THEN DO
07190009EX '&ZTEMPF' LIST
07200009SET &SHORT = &STR(TRANSACTION ISSUED)
07210009SET &LONG  = &STR(MVS/BDT TRANSACTION &BDTJNAME ISSUED)
07220009ISPEXEC VPUT (SHORT LONG)
07230009ISPEXEC SETMSG MSG(AOST010)
07240009END
07250009 
07260009/* ================================================================= */
07270009/* OTHERWISE CANCEL THE TRANSACTION/GMJD AND TELL THE USER           */
07280009/* ================================================================= */
07290009 
07300009ELSE DO
07310009SET &SHORT = &STR(TRANSACTION CANCELLED)
07320009SET &LONG  = &STR(MVS/BDT TRANSACTION &BDTJNAME EXECUTION CANCELLED)
07330009ISPEXEC VPUT (SHORT LONG)
07340009ISPEXEC SETMSG MSG(AOST010)
07350009END
07360009 
07370009/* ================================================================= */
07380009/* AND RETURN TO THE STARTING POINT FOR MORE TRANSACTIONS/GMJD'S     */
07390009/* ================================================================= */
07400009 
07410009IF &SDSN = THEN GOTO START
07420009           ELSE EXIT
