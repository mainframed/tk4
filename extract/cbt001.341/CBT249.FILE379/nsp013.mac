         TITLE '*----NSP013----*  JCL XREF  --  DATA SET NAME CROSSREFE*
               RENCE'
***********************************************************************
*                                                                     *
*                                                                     *
*     DSNAME XREF PRINT PROGRAM - 'NSP013'                            *
*                                                                     *
*                                                                     *
*     THIS PROGRAM PRINTS OUTPUT FROM PROGRAM 'NSP011'                *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         GENESIS CSECT=NSP013,ID=NSP013,EQU=Y,BASE=R12
*                                                                     *
         PRINT NOGEN
BLANK    EQU   C' '                     EQUATE
CHR0     EQU   C'0'
CHRI     EQU   C'I'
CHRO     EQU   C'O'
*                                                                     *
HEX00    EQU   X'00'
HEXFF    EQU   X'FF'
*                                                                     *
A10      EQU   *
         LOAD  EP=NSP016                LOAD DATE ROUTINE
         LR    R15,R0
         CALL  (15),(DATEWK),VL
         MVC   H1DATE,DATE3             MOVE CURRENT DATE TO HEADER
*                                                                     *
         OPEN  (INFILE,(INPUT),PRTFILE,(OUTPUT))
         MVC   PRTREC,PRTREC-1
         BAL   R10,HEADER10             PRINT HEADERS
A20      EQU   *
         GET   INFILE,XREC              GET INPUT RECORD
         CLC   DSNAME,XDSN              DID DSNAME CHANGE
         BE    A100                     NO
         MVC   DSNAME,XDSN              SAVE NEW DSNAME
         CLI   FIRSTSW,HEX00            IS THIS FIRST TIME THRU
         BE    A30                      YES
         MVI   H3CC,BLANK               SET TO SINGLE SPACE
         PUT   PRTFILE,HDR3             PUT SEPARATOR LINE
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         C     R1,LINEMAX1              IS IT PAGE OVERFLOW
         BNH   A30                      NO
         BAL   R10,HEADER10             PRINT HEADERS
A30      EQU   *
         MVI   FIRSTSW,HEXFF
         MVI   PCC,BLANK                SET TO SINGLE SPACE
         MVC   PDSN(L'PDSN+L'PUNIT),XDSN     MOVE DSNAME TO PRINT
A100     EQU   *
         L     R1,LINECTR
         C     R1,LINEMAX2              IS IT PAGE OVERFLOW
         BNH   A101                     NO
         BAL   R10,HEADER10             PRINT HEADERS
         MVC   PDSN,XDSN
         MVC   PDSN+20(7),=C' * CONT'   CONTINUE ON NEXT PAGE
A101     EQU   *
         CLI   XIO,CHRI                 IS IT INPUT
         BE    A110                     YES
         CLC   XUNIT,BLANKS             IS UNIT BLANK
         BE    A102                     YES - DO NOT MOVE UNIT
         MVC   PUNIT,XUNIT              NO
A102     EQU   *
         MVC   PBLKSIZE,XBLKSIZE
         MVC   PLRECL,XLRECL
         MVC   PRECFM,XRECFM
         MVC   PEXPDT,XEXPDT
         MVC   PSPACE,XSPACE
         MVC   POUTPUT,XPROC
         B     A120
A110     EQU   *
         MVC   PINPUT,XPROC
A120     EQU   *
         MVC   PSTEP,XSTEP
         MVC   PPGM,XPGM
         MVC   PDISP2,XDISP2
         MVC   PDISP3,XDISP3
         MVC   PRELGEN,XRELGEN
*                                                                     *
         PUT   PRTFILE,PRTREC           PRINT RECORD
         MVC   PRTREC,PRTREC-1          CLEAR
         L     R1,LINECTR
         A     R1,=F'1'                 BUMP LINE COUNTER
         ST    R1,LINECTR
         B     A20
*                                                                     *
         EJECT
HEADER10 EQU   *                        HEADER ROUTINE
         L     R1,PAGECTR
         A     R1,=F'1'                 BUMP PAGE COUNTER
         ST    R1,PAGECTR
         CVD   R1,DWRD                  MOVE PAGE NO.
         MVC   H1PAGE,MASK
         ED    H1PAGE,DWRD+6
         PUT   PRTFILE,HDR1             PRINT HEADER 1
         PUT   PRTFILE,HDR2             PRINT HEADER 2
         SR    R1,R1
         ST    R1,LINECTR
         PUT   PRTFILE,PRTREC
         MVC   PRTREC,PRTREC-1
         BR    R10
*                                                                     *
INEOF    EQU   *                        END OF FILE ROUTINE
         MVI   H3CC,BLANK
         PUT   PRTFILE,HDR3
         CLOSE (INFILE,,PRTFILE)
         EXODUS RC=0
*                                                                     *
         EJECT
INDCB    DS    0F
INFILE   DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GM,EODAD=INEOF
*                                                                     *
PRTDCB   DS    0F
PRTFILE  DCB   DDNAME=SYSPRNT1,DSORG=PS,MACRF=PM,                      X
               RECFM=FBA,LRECL=133,BLKSIZE=1330
*                                                                     *
         EJECT
LINECTR  DC    F'0'                     LINE COUNTER
LINEMAX1 DC    F'49'                    MAX LINES PER PAGE
LINEMAX2 DC    F'54'
PAGECTR  DC    F'0'                     PAGE COUNTER
DWRD     DS    D
FIRSTSW  DC    X'00'                    FIRST TIME SWITCH
MASK     DC    X'40202021'
BLANKS   DC    CL10' '
*                                                                     *
DATEWK   DS    0CL55                    DATE AREA
         DC    CL19' '
DATE3    DC    CL8' '
         DC    CL28' '
*                                                                     *
DSNAME   DC    CL44' '
*                                                                     *
XREC     DS    0CL135                   INPUT RECORD
XDSN     DS    CL44                     DSNAME
XIO      DS    CL1                      INPUT/OUTPUT
XPROC    DS    CL8                      PROC
XPGM     DS    CL8                      PROGRAM
XSTEP    DS    CL8                      STEP
XRELGEN  DS    CL4                      RELATIVE GEN NO.
XDISP    DS    0CL9                     DISP
XDISP1   DS    CL3
XDISP2   DS    CL3
XDISP3   DS    CL3
XUNIT    DS    CL8                      UNIT
XEXPDT   DS    CL5
XSPACE   DS    CL25                     SPACE
XDCB     DS    0CL15
XDSORG   DS    CL2                      DSORG
XRECFM   DS    CL3                      RECFM
XLRECL   DS    CL5                      LRECL
XBLKSIZE DS    CL5                      BLKSIZE
*                                                                     *
         DC    C' '
PRTREC   DS    0CL133                   PRINT RECORD
PCC      DS    CL1
PDSN     DS    CL28                     DSNAME
PUNIT    DS    CL6                      UNIT
         DS    CL2
PBLKSIZE DS    CL5                      BLKSIZE
         DS    CL2
PLRECL   DS    CL5                      LRECL
         DS    CL2
PRECFM   DS    CL3                      RECFM
         DS    CL2
PEXPDT   DS    CL5                      EXPDT
         DS    CL2
PSPACE   DS    CL18                     SPACE
         DS    CL1
POUTPUT  DS    CL8                      OUTPUT FROM PROC
         DS    CL2
PINPUT   DS    CL8                      INPUT TO PROC
         DS    CL2
PSTEP    DS    CL8                      STEP
         DS    CL1
PPGM     DS    CL8                      PROGRAM
         DS    CL2
PDISP2   DS    CL3                      DISP 2
         DS    CL1
PDISP3   DS    CL3                      DISP 3
         DS    CL2
PRELGEN  DS    CL3                      RELATIVE GEN NO.
*                                                                     *
HDR1     DS    0CL133                   HEADER 1
         DC    CL21'1  NSP013            '
         DC    CL17' '
         DC    CL55'D S N   CROSS REFERENCE LISTING'
         DC    CL9'RUN DATE'
H1DATE   DC    CL8' '
         DC    CL9'    PAGE'
H1PAGE   DC    CL4' '
         DC    CL10' '
*                                                                     *
HDR2     DS    0CL133                   HEADER 2
         DC    CL29'0DATA SET NAME'
         DC    CL8'UNIT'
         DC    CL7'BLKSI'
         DC    CL7'LRECL'
         DC    CL5'FMT'
         DC    CL7'EXPDT'
         DC    CL19'SPACE'
         DC    CL10'OUTPUT'
         DC    CL10'INPUT'
         DC    CL9'STEP'
         DC    CL10'PROGRAM'
         DC    CL9'DISP'
         DC    CL3'GEN'
*                                                                     *
HDR3     DS    0CL133                   SEPARATOR LINE
H3CC     DC    C' '
         DC    132C'-'
*                                                                     *
         LTORG
         END
