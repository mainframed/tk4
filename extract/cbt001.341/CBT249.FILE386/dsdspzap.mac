*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
*
*   AT LABEL AUTHDSN THERE IS A CHECK FOR AUTHORIZED ZAP OF A SYS1
*   DATASET. YOU WILL NEED TO NULLIFY OR MODIFY TO FIT YOUR NEEDS
*
*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
         SPACE  5
         SPACE  5
*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
*
*   AT LABEL AUTHDSN THERE IS A CHECK FOR AUTHORIZED ZAP OF A SYS1
*   DATASET. YOU WILL NEED TO NULLIFY OR MODIFY TO FIT YOUR NEEDS
*
*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
         SPACE  5
         SPACE  5
*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
*
*   AT LABEL AUTHDSN THERE IS A CHECK FOR AUTHORIZED ZAP OF A SYS1
*   DATASET. YOU WILL NEED TO NULLIFY OR MODIFY TO FIT YOUR NEEDS
*
*NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE  NOTE
         EJECT
         SPACE  5
         SPACE  5
         SPACE  5
         SPACE  5
         SPACE  3
***********************************************************************
*                                                                     *
*       FIRST ATLANTA INC.                                            *
*       MC 2331                                                       *
*       DATA SERVICES DIVISION                                        *
*       2400 PIEDMONT ROAD                                            *
*       ATLANTA, GEORGIA 30324                                        *
*                                                                     *
***********************************************************************
         TITLE 'DSDSPZAP - S U P E R Z A P   A U D I  T   T R  A I L'
***********************************************************************
*                                                                     *
*      PROGRAM ID:                                                    *
*               DSDSPZAP.                                             *
*                                                                     *
*      DATE WRITTEN:                                                  *
*              11/14/80                                               *
*                                                                     *
*      PROGRAM PURPOSE:                                               *
*              PRODUCE SMF RECORDS FOR A AUDIT TRAIL FOR PROGRAM      *
*                     "AMASPZAP". A SMF RECORD IS PRODUCED FOR        *
*                     EACH NON COMMENT CONTROL CARD READ BY AMASPZAP. *
*                                                                     *
*      AUTHOR:                                                        *
*              CURTISS G. STANLEY                                     *
*                                                                     *
*      LANGUAGE:                                                      *
*              ALC                                                    *
*                                                                     *
*      ATTRIBUTES:                                                    *
*              AUTHORIZATION CODE OF 1.                               *
*                                                                     *
*      FREQUENCY:                                                     *
*              AFTER EACH CONTROL CARD READ BY AMASPZAP.              *
*                                                                     *
*      PROCESSING TIME:                                               *
*              MINIMAL                                                *
*                                                                     *
*      PROGRAM ORGANIZATION:                                          *
*              PROGRAM PROLOG AND DESCRIPTIVE BLOCK                   *
*              MAINTENANCE HISTORY CHART                              *
*              PROGRAM ENTRY LINKAGE AND INITIALIZATION SECTION       *
*              PROCESSING OF PASSED DATA                              *
*              PROCESSING OF "SMCA" DATA                              *
*              PROCESSING OF "JMR" DATA                               *
*              PROCESSING OF "JCT" DATA                               *
*              PROCESSING OF "SSIB" DATA                              *
*              WRITING THE SMF RECORD                                 *
*              SMFWTM ERROR HANDLING                                  *
*              RETURN LINKAGE AND CONSTANTS                           *
*              TESTING CODE                                           *
*              DUMMY CONTROL SECTIONS                                 *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      DSECT'S                                                        *
*              SMCA     - SYSTEM MANAGEMENT FACILITIES CONTROL TABLE  *
*              SSIB     - SUBSYSTEM IDENTIFICATION BLOCK              *
*              TCB      - TASK CONTROL BLOCK                          *
*              CVT      - COMMUNICATION VECTOR TABLE                  *
*              PSA      - PREFIXED SAVE AREA                          *
*              JSCB     - JOB/STEP CONTROL BLOCK                      *
*              JCT      - JOB CONTROL TABLE                           *
*              ACT      - ACCOUNT CONTROL TABLE                       *
*              TCT      - SMF TIMING CONTROL TABLE                    *
*              JMR      - JOB MANAGEMENT RECORD                       *
*              JFCB     - JOB FILE CONTROL BLOCK                      *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      FILES:                                                         *
*              OUTPUT:                                                *
*               DDNAME=FILE     - DD STATEMENT IS ONLY USED WHEN      *
*                                 TESTING AND NOT WRITING TO SMF.     *
*                                                                     *
*      PASSED INFORMATION AND USE:                                    *
*                                                                     *
*              REGISTER R1 - POINTER TO TWO FOUR BYTE POINTERS        *
*                POINTER 1 - ADDRESS OF "JFCBAREA" IN AMASPZAP        *
*                POINTER 2 - ADDRESS OF "CDBUF" IN AMASPZAP           *
*                POINTER 3 - ADDRESS OF VOLSER FOR SYSLIB             *
*                                                                     *
*      SWITCHES:                                                      *
*              NAME      VALUE  EQU       MEANING                     *
*                                                                     *
*                                                                     *
*      MACROS:                                                        *
*              SMFZPD   - USED TO CREATE A MAP OF THE SMF RECORD      *
*                         PRODUCED BY DSDSPZAP.                       *
*                                                                     *
*      ABENDS:                                                        *
*              USER 4095. REASON EXPLAINED BY A WTO.                  *
*                                                                     *
*      TABLES:                                                        *
*              NONE                                                   *
*                                                                     *
*      EDITS:                                                         *
*              NONE                                                   *
*                                                                     *
*      RETURN CODES:                                                  *
*              NONE                                                   *
*                                                                     *
*      COPY STATEMENTS:                                               *
*              NONE                                                   *
*                                                                     *
*      CALLED SUB-ROUTINES:                                           *
*              NONE                                                   *
*                                                                     *
*      GENERAL LOGIC FLOW:                                            *
*              YES                                                    *
*                                                                     *
***********************************************************************
         TITLE ' SUPERZAP AUDIT TRAIL MAINTENANCE HISTORY CHART'
         SPACE 2
***********************************************************************
*                                                                     *
*       MAINTENANCE HISTORY CHART                                     *
*                                                                     *
*       DATE      RFDPS  ID   BY           PURPOSE OF CHANGE          *
*       11/14/80  MAINT  N/A  C. G. STANLEY INITIAL MODULE CODING     *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'PROGRAM ENTRY - SUPERZAP AUDIT TRAIL'
DSDSPZAP CSECT
         SPACE 2
***********************************************************************
*                                                                     *
*      REGISTER USAGE                                                 *
*                                                                     *
R0       EQU   0        LINKAGE REGISTER                              *
R1       EQU   1        LINKAGE REGISTER                              *
R2       EQU   2        WORK REGISTER                                 *
R3       EQU   3        OPEN                                          *
R4       EQU   4        JFCB AND PSA BASE                             *
R5       EQU   5        TCB BASE                                      *
R6       EQU   6        CVT BASE                                      *
R7       EQU   7        SMCA AND JSCB BASE                            *
R8       EQU   8        JCT, ACT, SSIB, AND TCT BASE                  *
R9       EQU   9        JMR BASE, WORK REGISTER                       *
R10      EQU   10       WORK REGISTER                                 *
R11      EQU   11       OPEN                                          *
R12      EQU   12       PROGRAM BASE                                  *
R13      EQU   13       SAVE AREA POINTER                             *
R14      EQU   14       LINKAGE RAGISTER                              *
R15      EQU   15       LINKAGE REGISTER                              *
*                                                                     *
***********************************************************************
         EJECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING DSDSPZAP,R12
         ST    13,SAVEAREA+4            ADDRESS CHAIN BACKWARD
         LR    14,13                    SAVE OLD SAVE AREA POINTER
         LA    13,SAVEAREA              LOAD NEW SAVE AREA POINTER
         ST    13,8(0,14)               ADDRESS CHAIN FORWARD
         B     SKPSAVE                  SKIP OVER STAMPS AND DSECTS
         DC    CL1' '
         DC    CL8'&SYSDATE'
         DC    CL1' '
         DC    CL5'&SYSTIME'
         DC    CL1' '
         DS    0D                       ALIGN ON DOUBLE WORD BOUNDRY
         TITLE 'SMF DATA DEFINED BY THE CDBUF PASSED FROM AMASPZAP'
*
SKPSAVE  EQU   *
*
         LM    R4,R6,0(R1)        LOAD JFCB ADDRESS AND CDBUF ADDRESS
         CLI   0(R5),C'*'         IS CONTROL CARD A COMMENT CARD
         BE    RETURN             NO NEED FOR AN SMF RECORD
         MVC   SMFZPCRD,0(R5)     MOVE CONTROL CARD TO SMF RECORD
*                                 WORK AREA.
         TITLE 'SMF DATA DEFINED BY JFCB AREAS FROM AMASPZAP'
*
         USING JFCB,R4            DEFINE JFCB
         CLI   JFCBDSNM,X'04'     IS THIS A VTOC ZAP?
         BNE   MOVEDSN            NO - MOVE DIRECTLY TO SMF RECORD
         MVI   SMFZPDSN,BLANK     MOVE IN A BLANK
         MVC   SMFZPDSN+1(L'SMFZPDSN-1),SMFZPDSN   BLANK OUT DSN FIELD
         MVC   SMFZPDSN(L'DSCB),DSCB   SET DSN TO "FORMAT4.DSCB"
         B     DSNCMP             GO MOVE VOLSER
*
MOVEDSN  EQU   *
*
         MVC   SMFZPDSN,JFCBDSNM  MOVE IN FULL 44 BYTE JFCB DSNAME
*
DSNCMP   EQU   *
*
         MVC   SMFZPVOL,0(R6)     MOVE IN VOLSER OF FILE BEING ZAPED
         DROP  R4
         TITLE 'INITIAL TCB SET-UP AND SMF DATA DEFINED BY SMCA AREA'
*
         LA    R4,0                    SET UP R4 AS PSA BASE
*                                      PSA IS 1ST 4K OF STORAGE
         USING PSA,R4                  SET UP PSA TAGS
         L     R5,PSATOLD              LOAD POINTER TO MY TCB IN R5
         USING TCB,R5                  SET UP TCB TAGS
         L     R6,FLCCVT               LOAD CVT POINTER
         USING CVT,R6                  SET TAGS FOR CVT
         L     R7,CVTSMCA              LOAD PTR TO SMCA
         USING SMCABASE,R7
         MVC   SMFZPSID,SMCASID        MOVE OVER CPUID
         DROP  R7
         TITLE 'JMR FIELDS FOR READER TIMES'
         CLC   TCBTCT,=F'0'            IS THE TIMING CONTROL TABLE
*                                      ADDRESS VALID?
         BNE   TCTOK
*
*     DO NOT ALLOW AMASPZAP TO EXECUTE IF TCBTCT FIELD DOES NOT
*     HAVE AN ADDRESS. WHEN THIS FIELD IS ZERO IT MEANS THAT THE
*     SMF IS NOT ACTIVE ON THIS MACHINE AND I CANNOT WRITE AN
*     SMF RECORD IF IT IS INACTIVE.
*
         WTO   'SUPERZAP - SMF IS INACTIVE, AUDIT TRAIL IMPOSSIBLE'
         ABEND 4095
*
TCTOK    EQU   *
*
         L     R8,TCBTCT              GET ADDRESS OF TCT
*
         USING SMFTCT,R8              SET UP TAGS
         L     R9,TCTJMR              LOAD ADDRESS OF JMR
*
         DROP  R8
         USING JMR,R9                 SET UP TAGS
         MVC   SMFZPRST,JMRENTRY      MOVE TIME READER RECOGNIZED JOB
         MVC   SMFZPRSD,JMREDATE      MOVE DATE READER RECOGNIZED JOB
*
         DROP  R9
         TITLE 'SMF DATA DEFINED BY JCT CONTROL BLOCK'
*
         L     R7,TCBJSCB              LOAD ADDRESS OF JSCB
         USING IEZJSCB,R7
         L     R8,JSCBJCT              GET ADDRESS OF JCT
         LA    R8,X'10'(R8)            BYPASS PREFIX
         USING JCT,R8
         XC    SMFZPJST(8),SMFZPJST  CLEAR JOB START TIME AND DATE
         XC    SMFZPSST(8),SMFZPSST  CLEAR STEP START TIME AND DATE
*
         MVC   SMFZPJBN,JCTJNAME     MOVE JOBNAME TO SMF RECORD
*
         MVC   SMFZPJST+1(3),JCTJMRJT  MOVE JOB START EXECUTION TIME
         MVC   SMFZPJSD+1(3),JCTJMRJD  MOVE JOB START EXECUTION DATE
*
         MVC   SMFZPSST+1(3),JCTJMRSS  MOVE IN STEP START TIME
         MVC   SMFZPSSD+1(3),JCTSSD    MOVE IN STEP START DATE
         SPACE 2
*
*                 SET UP "ACT" FIELDS
*
         L     R8,JCTACTAD             GET ADDRESS OF "ACT" FROM "JCT"
         SRL   R8,8                    SHIFT FOR A GOOD ADDRESS
         LA    R8,X'10'(R8)            GET PASS PREFIX AREA
         DROP  R8
         USING ACTDSKAD,R8             SET UP "ACT" ADDRESSING
         MVC   SMFZPPNM,ACTPRGNM       MOVE OVER PROGRAMMED FIELD
*                                      LENGTH SEGMENT
         CLI   ACTJNFLD,X'00'          ARE THERE ANY ACCOUNTING FIELDS
         BNE   MOVEACCT                YES - GO MOVE ACCOUNTING TO SMF
         XR    R1,R1                   CLEAR REGISTER 1
         B     SETRCDLN                GO SET UP RECORD LENGTH
*
MOVEACCT EQU   *
*
         XR    R9,R9                   CLEAR REGISTER 9
         XR    R10,R10                 CLEAR REGISTER 10
         LA    R2,ACTJNFLD+1           GET DISP OF ACCOUNT FIELDS
         IC    R9,ACTJNFLD             PUT # OF ACCOUNT FIELDS IN R9
         XR    R1,R1                   CLEAR ADDITION REGISTER
         MVC   SMFZPACT,ACTJNFLD+1     MOVE OVER ACCOUNT FIELDS
         CLC   =C'SYS1.',SMFZPDSN      IS THIS A ZAP FOR A SYS1 DATASET
         BE    AUTHDSN                 CHECK AUTHORIZED ACCOUNT
         CLC   DSCB,SMFZPDSN           IS THIS A FORMAT4.DSCB ZAP
         BNE   NONAUTH                 NO AUTHORITY NECESSARY
*
AUTHDSN  EQU   *
*
         CLC   =C'SYS1.TEST',SMFZPDSN  IS IT A TEST DATASET
         BE    NONAUTH                 NO AUTHORITY NECESSARY
         CLI   SMFZPACT+1,C'T'         IS THIS A PRODUCTION JOB
         BNE   NONAUTH                 AUTHORIZED FOR RESTRICTED ZAPS
         CLI   SMFZPACT+3,C'9'         IS THIS A SYSTEMS SUPPORT JOB
         BE    NONAUTH                 AUTHORIZED FOR RESTRICTED ZAPS
         WTO   'SUPERZAP - UNAUTHORIZED ZAP OF SYSTEM DATASET'
         ABEND 4095
         SPACE 2
*
NONAUTH  EQU   *
*
*
ACCTLOOP EQU   *
*
         IC    R10,0(R2)               PUT FIELD LENGTH IN REG 10
         LA    R1,1(R10,R1)            ADD LENGTH TOGEATHER
*                                      THIS LENGTH + PREVIOUS LENGTH
*                                      + 1 FOR LENGTH OF FIELD
         LA    R2,1(R10,R2)            GET TO NEXT LENGTH FIELD
*                                      ADD SAME TO GET TO NEXT FIELD
         BCT   R9,ACCTLOOP             LOOP FOR # OF ACCOUNT FIELDS
*
SETRCDLN EQU   *
*
         LA    R2,SMFZPFSL            PUT LENGTH OF FIXED LENGTH
*                                     SEGMENT IN REGISTER 2
         LA    R2,5(R2,R1)            BUMP BY LENGTH OF ACCOUNT FIELDS
*                                     FIX SEGMENT + ACCOUNT FIELDS +
*                                     5 DEEP = 1 FOR # ACCOUNT FIELDS
*                                     + 4 FOR THE RDW
         STH   R2,SMFZPRCL            STORE RECORD LENGTH IN RECORD
         XC    SMFZPSEG,SMFZPSEG      CLEAR SEGMENT AREA
         MVC   SMFZP#AC,ACTJNFLD      MOVE OVER # ACCOUNTING FIELDS
         DROP  R8
         TITLE 'SMF DATA DEFINED BY SSIB CONTROL BLOCK'
*
         L     R8,JSCBSSIB
         USING SSIB,R8
         MVC   SMFZPJID,SSIBJBID     MOVE IN JOB IDENTIFER
         DROP  R8
*
*                DROP ALL REGISTERS IN USE
*
         DROP  R4
         DROP  R5
         DROP  R6
         DROP  R7
*
*
*
         TITLE 'PUT SMF BUFFER TIME IN SMF RECORD AND WRITE RECORD'
*
         TIME  BIN
         STM   R0,R1,WORKD             SAVE CURRENT TIME AND DATE
         MVC   SMFZPTME(8),WORKD       MOVE TIME AND DATE TO RECORD
         SPACE 2
*
*            WRITE THE SMF RECORD
*
         SPACE 3
*
*
*             TO TEST THIS MODULE AND NOT WRITE ANY SMF RECORDS
*        TO THE SMF FILES:
*
*    1. DROP THE COMMENT ON THE FOLLOWING BRANCH INSTRUCTION.
*    2. ADD A DD STATEMENT TO THE SUPERZAP JCL FOR A DDNAME
*       OF "FILE". THE DATASET MUST HAVE A MOD DISPOSITION.
*    3. ASSEMBLE THIS MODULE.
*    4. STEPLIB TO THE LIBRARY THIS MODULE IS IN.
         SPACE 2
*
*
*        B     NOSMF
*
*
         SPACE 2
         LA    R4,ERRTABLE             GET ADDRESS OF BRANCH TABLE
         SPACE 2
*
*        CHECK TO MAKE SURE I AM AUTHORIZED BEFORE TRING TO DO
*        A MODE SET.
*
         SPACE 2
         TESTAUTH FCTN=1
         SPACE 2
         LTR   R15,R15                 TEST R15 FOR ZERO/NON-ZERO
         BZ    SETMODE                 IT'S AUTHORIZED
         WTO   'SUPERZAP - MODULE DOES NOT HAVE AUTHORIZATION'
         ABEND 4095
         SPACE 2
SETMODE  EQU   *
         MODESET KEY=ZERO
         SMFWTM  SMFZPD
         B     0(R4,R15)               BRANCH FOR RETURN CODE
*
         TITLE 'BRANCH TABLE AND ERROR HANDLING ROUTINES'
*
ERRTABLE EQU   *
         B     RETURN                  GOOD RETURN FROM SMF WRITE
         B     ABEND4                  RECORD WOULD NOT FIT IN AN
*                                      EMPTY SMF DATASET
         B     ABEND8                  RDW LESS THAN 18 BYTES
         B     ABEND12                 INVALID SMFWTM RETURN
         B     ABEND16                 SMF NOT ACTIVE OF NOT WRITTING
         B     ABEND20                 IEFU83 IS FLUNKING MY RECORDS
         SPACE 2
ABEND4   EQU   *
         WTO   'SUPERZAP - SMFWTM RETURN CODE = 4'
         ABEND 4095,DUMP
         SPACE 2
ABEND8   EQU   *
         WTO   'SUPERZAP - SMFWTM RETURN CODE = 8'
         ABEND 4095,DUMP
         SPACE 2
ABEND12  EQU   *
         WTO   'SUPERZAP - SMFWTM RETURN CODE = 12 (UNKNOWN)'
         ABEND 4095,DUMP
         SPACE 2
ABEND16  EQU   *
         WTO   'SUPERZAP - SMFWTM RETURN CODE = 16'
         ABEND 4095,DUMP
         SPACE 2
ABEND20  EQU   *
         WTO   'SUPERZAP - SMFWTM RETURN CODE = 20'
         ABEND 4095,DUMP
         TITLE 'RETURN TO SUPERZAP MODULE AND CONSTANTS'
*
RETURN   EQU   *
         MODESET KEY=NZERO
         L     R13,4(R13)
         RETURN (14,12),RC=0
         SPACE 2
BLANK    EQU   C' '
DSCB     DC    C'FORMAT4.DSCB'
SAVEAREA DC    18F'0'
WORKD    DC    D'0'
         TITLE 'DSDSPZAP SMF RECORD DESCRIPTION'
         SMFZPD   DSECT=NO
         LTORG
         TITLE 'TESTING ROUTINE USED TO NOT WRITE TO THE SMF FILE'
*
NOSMF    EQU   *
*
*                           TESTING ROUTINE
*
         SPACE 2
         OPEN  (FILE,(OUTPUT))
         PUT   FILE,SMFZPD
         CLOSE FILE
         B     RETURN
         TITLE 'TESTING DCB USED IN TESTING ROUTINE ONLY'
FILE     DCB   DDNAME=FILE,DSORG=PS,MACRF=PM,RECFM=VB,LRECL=352,       X
               BLKSIZE=3524
         TITLE 'DSECTS NEEDED TO MAP SYSTEM CONTROL BLOCKS'
         IEESMCA
         EJECT
         IEFJSSIB
         EJECT
         IKJTCB
         EJECT
         CVT DSECT=YES
         EJECT
         IHAPSA
         EJECT
         IEZJSCB
         EJECT
JCT      DSECT
         IEFAJCTB
         EJECT
         IEFTCT
         EJECT
         IEFJMR
         EJECT
JFCB     DSECT
         IEFJFCBN
         END
