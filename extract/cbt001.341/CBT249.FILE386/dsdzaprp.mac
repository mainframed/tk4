         SPACE  5
         SPACE  5
         SPACE  5
         SPACE  5
         SPACE  3
***********************************************************************
*                                                                     *
*       FIRST ATLANTA INC.                                            *
*       MC 2331                                                       *
*       DATA SERVICES DIVISION                                        *
*       2400 PIEDMONT ROAD                                            *
*       ATLANTA, GEORGIA 30324                                        *
*                                                                     *
***********************************************************************
         TITLE 'DSDZAPRP - S U P E R Z A P   A U D I  T   T R  A I L'
***********************************************************************
*                                                                     *
*      PROGRAM ID:                                                    *
*               DSDZAPRP.                                             *
*                                                                     *
*      DATE WRITTEN:                                                  *
*              11/14/80                                               *
*                                                                     *
*      PROGRAM PURPOSE:                                               *
*              PRODUCE A REPORT FROM ANY SMF RECORDS PRODUCED BY      *
*                     "AMASPZAP".                                     *
*                                                                     *
*      AUTHOR:                                                        *
*              CURTISS G. STANLEY                                     *
*                                                                     *
*      LANGUAGE:                                                      *
*              ALC                                                    *
*                                                                     *
*      ATTRIBUTES:                                                    *
*              NONE                                                   *
*                                                                     *
*      FREQUENCY:                                                     *
*              DAILY AFTER DUMP OF SMF FILES.                         *
*                                                                     *
*      PROCESSING TIME:                                               *
*              MINIMAL                                                *
*                                                                     *
*      PROGRAM ORGANIZATION:                                          *
*              PROGRAM PROLOG AND DESCRIPTIVE BLOCK                   *
*              MAINTENANCE HISTORY CHART                              *
*              PROGRAM ENTRY LINKAGE AND INITIALIZATION SECTION       *
*              PROCESSING MAINLINE                                    *
*              PROCESSING NEW JOB AND/OR DSN                          *
*              PROCESSING TIME CONVERSION                             *
*              PROCESSING END OF JOB                                  *
*              RETURN LINKAGE                                         *
*              DCBS                                                   *
*              CONSTANTS AND WORK AREAS                               *
*              DUMMY CONTROL SECTIONS                                 *
*                                                                     *
*                                                                     *
*      DSECT'S                                                        *
*              SMFZPD   - MAP SMF RECORD PRODUCED BY "AMASPZAP"       *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*      FILES:                                                         *
*              INPUT:                                                 *
*               DDNAME=SYSUT1   - SMF RECORDS INPUT                   *
*              OUTPUT:                                                *
*               DDNAME=SYSPRINT - REPORT FILE OUTPUT                  *
*              IN/OUT:                                                *
*               DDNAME=SORTIN   - WORK FILE FOR SORT                  *
*               DDNAME=SORTOUT  - WORK FILE FOR SORT                  *
*                                                                     *
*      PASSED INFORMATION AND USE:                                    *
*                                                                     *
*              NONE                                                   *
*                                                                     *
*      SWITCHES:                                                      *
*              NAME      VALUE  EQU       MEANING                     *
*                                                                     *
*                                                                     *
*      MACROS:                                                        *
*              NONE                                                   *
*                                                                     *
*      ABENDS:                                                        *
*              4095 - SORT FAILED                                     *
*                                                                     *
*      TABLES:                                                        *
*              NONE                                                   *
*                                                                     *
*      EDITS:                                                         *
*              NONE                                                   *
*                                                                     *
*      RETURN CODES:                                                  *
*              NONE                                                   *
*                                                                     *
*      COPY STATEMENTS:                                               *
*              NONE                                                   *
*                                                                     *
*      CALLED SUB-ROUTINES:                                           *
*              NONE                                                   *
*                                                                     *
*      GENERAL LOGIC FLOW:                                            *
*              YES                                                    *
*                                                                     *
***********************************************************************
         TITLE ' SUPERZAP AUDIT TRAIL MAINTENANCE HISTORY CHART'
         SPACE 2
***********************************************************************
*                                                                     *
*       MAINTENANCE HISTORY CHART                                     *
*                                                                     *
*       DATE      RFDPS  ID   BY           PURPOSE OF CHANGE          *
*       11/14/80  MAINT  N/A  C. G. STANLEY INITIAL MODULE CODING     *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'PROGRAM ENTRY - SUPERZAP AUDIT TRAIL'
DSDZAPRP CSECT
         SPACE 2
***********************************************************************
*                                                                     *
*      REGISTER USAGE                                                 *
*                                                                     *
R0       EQU   0        LINKAGE REGISTER                              *
R1       EQU   1        LINKAGE REGISTER                              *
R2       EQU   2        WORK REGISTER                                 *
R3       EQU   3        OPEN                                          *
R4       EQU   4        JFCB AND PSA BASE                             *
R5       EQU   5        TCB BASE                                      *
R6       EQU   6        CVT BASE                                      *
R7       EQU   7        SMCA AND JSCB BASE                            *
R8       EQU   8        JCT, ACT, SSIB, AND TCT BASE                  *
R9       EQU   9        JMR BASE, WORK REGISTER                       *
R10      EQU   10       WORK REGISTER                                 *
R11      EQU   11       OPEN                                          *
R12      EQU   12       PROGRAM BASE                                  *
R13      EQU   13       SAVE AREA POINTER                             *
R14      EQU   14       LINKAGE RAGISTER                              *
R15      EQU   15       LINKAGE REGISTER                              *
*                                                                     *
***********************************************************************
         EJECT
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING DSDZAPRP,R12
         ST    13,SAVEAREA+4            ADDRESS CHAIN BACKWARD
         LR    14,13                    SAVE OLD SAVE AREA POINTER
         LA    13,SAVEAREA              LOAD NEW SAVE AREA POINTER
         ST    13,8(0,14)               ADDRESS CHAIN FORWARD
         B     SKPSAVE                  SKIP OVER STAMPS AND DSECTS
         DC    CL1' '
         DC    CL8'&SYSDATE'
         DC    CL1' '
         DC    CL5'&SYSTIME'
         DC    CL1' '
         DS    0D                       ALIGN ON DOUBLE WORD BOUNDRY
         TITLE 'MAINLINE ROUTINE'
*
SKPSAVE  EQU   *
*
         OPEN  (SYSPRINT,(OUTPUT),SORTIN,(OUTPUT),SYSUT1)
*
         LA    R11,RECORD+4
         USING SMFZPD,R11
         TIME  BIN
         STM   R0,R1,TIMEDATE
*
READSMF  EQU   *
*
         GET   SYSUT1,RECORD
         CLI   SMFZPRTY,SMFEQRTY       CHECK FOR MY TYPE OF RECORD
         BNE   READSMF
         MVI   SMFSORT+1,X'00'         SET FOR RECORDS FOUND
         PUT   SORTIN,RECORD           PUT RECORD ON WORK FILE
         B     READSMF                 GO READ AGAIN
SMFSORT  B     NOSMF
         CLOSE (SORTIN,,SYSUT1)
         LA    R1,PARMLIST             LOAD PARM POINTER
         LINK  EP=SORT                 GIVE SORT CONTROL
         LTR   R15,R15
         BZ    SORTOK
         WTO   'DSDZAPRP - BAD SORT RETURN'
         ABEND 4095
SORTOK   EQU   *
*
         OPEN  (SORTOUT)              OPEN SORTED SMF RECORDS
*
SMFRPT   EQU   *
         GET   SORTOUT,RECORD         LOAD ADDRESS OF JMR
         LA    R10,SMFRPT
         CLC   NEWJOB,SMFZPJBN        IS THIS A NEW JOB
         BNE   TITLEPRT
         CLC   NEWDSN,SMFZPDSN         CHECK FOR DSN CHANGE
         BNE   TITLEPRT                CHANGED - NEW TITLE
         MVC   CTLCARD,SMFZPCRD       MOVE CARD TO REPORT
*
PRINTRCD EQU   *
*
         PUT   SYSPRINT,REPORT        WRITE PRINT RECORD
         MVC   REPORT,REPORT-1        CLEAR PRINT AREA
         SP    LINECNT,=P'1'
         CP    LINECNT,=P'0'
         BHR   R10
         GET   SORTOUT,RECORD
         B     TITLEPRT
         TITLE 'HANDLE NEW RUN OF "AMASPZAP"'
TITLEPRT EQU   *
         CLC   NEWDSN,SMFZPDSN         SAME DSN/VOLSER
         BE    DSNSET                  YES - SKIP INITIALIZE
         MVC   NEWDSN,SMFZPDSN         MOVE IN NEW DSN
*
DSNSET   EQU   *
*
         AP    PAGECT,=P'1'           ADD 1 TO PAGE COUNT
         MVC   NEWJOB,SMFZPJBN        MOVE IN NEW CONTROL
         ZAP   LINECNT,=P'50'
         ST    R10,SAVE10
         MVC   REPORT,REPORT-1        CLEAR PRINT AREA
         MVC   REPORT+1(L'TITLE),TITLE     MOVE IN TITLE
         ED    TPAGE#,PAGECT          PUT PAGE COUNT IN TITLE
         ED    TDATE,TIMEDATE+5       PUT DATE IN TITLE
         LA    R9,TTIME
         L     R5,TIMEDATE
         BAL   R10,TIMECONV           CONVERT TIME OF DAY
         MVI   REPORT,C'1'            TOP-OF-PAGE
         BAL   R10,PRINTRCD           GO PRINT TITLE
*
         MVC   REPORT+1(L'TITLE2),TITLE2  MOVE IN 2ND TITLE
         MVI   REPORT,C'-'                SKIP 2 LINES
         BAL   R10,PRINTRCD           PRINT TITLE 2
*
         LA    R9,JOBTIME             REPORT JOB START TIME
         MVC   TIMEWORK(4),SMFZPJST
         L     R5,TIMEWORK            SMF JOB START TIME
         BAL   R10,TIMECONV           GO CONVERT TIME
*
         LA    R9,STPTIME             REPORT STEP START TIME
         MVC   TIMEWORK(4),SMFZPSST
         L     R5,TIMEWORK            SMF STEP START TIME
         BAL   R10,TIMECONV           GO CONVERT TIME
*
         MVC   JOBDATE-1(6),DATEMASK  MOVE JOB START DATE MASK
         ED    JOBDATE-1(6),SMFZPJSD+1  EDIT IN JOB START DATE
*
         MVC   JOBSID,SMFZPSID        MOVE IN CPUID
         MVC   JOBDSN,SMFZPDSN        MOVE IN DSN BEING ZAPED
         MVC   JOBVOL,SMFZPVOL        MOVE IN VOLSER BEING ZAPED
         MVC   JOBNAME,SMFZPJBN       MOVE IN JOBNAME
         MVC   JOBID,SMFZPJID         MOVE IN JOB ID NUMBER
         MVC   JOBPGMR,SMFZPPNM       MOVE IN PROGRAMMER NAME FIELD
         MVI   REPORT,C'0'
         BAL   R10,PRINTRCD           GO PRINT RECORD
*
         CLI   SMFZP#AC,X'00'         ANY ACCOUNTING FIELDS
         BE    NOACCT
         MVI   REPORT,C'-'
         MVC   REPORT+(L'REPORT-L'TITLE3)/2(L'TITLE3),TITLE3
         BAL   R10,PRINTRCD           PRINT TITLE 3
         XR    R14,R14                 CLEAR REGISTER 14
         XR    R15,R15                 CLEAR REGISTER 15
         XR    R2,R2                   CLEAR REGISTER 2
         LA    R1,SMFZPACT             GET ADDRESS OF ACCOUNT FIELD
         IC    R14,SMFZP#AC            PUT # OF ACCOUNT FIELDS IN R14
*
ACCOUNTL EQU   *
*
         IC    R15,0(R1)               PUT FIELD LENGTH IN R15
         LA    R2,1(R2,R15)            COUNT UP ACCOUNT LENGTH
         LA    R1,1(R15,R1)            ADD LENGTH OF ACCT FIELD
         BCT   R14,ACCOUNTL
*                                      + 1 FOR LENGTH OF FIELD
         LA    R1,132                  GET REPORT LENGTH
         CH    R2,=H'132'
         BL    MAXOK                   LOOP FOR # OF ACCOUNT FIELDS
         IC    R2,=X'20'               SET NEW LENGTH
         MVC   SMFZPACT(L'ACTMSG),ACTMSG
         MVI   SMFZP#AC,X'01'
*
MAXOK    EQU   *
*                                     SEGMENT IN REGISTER 2
         SR    R1,R2                  BUMP BY LENGTH OF ACCOUNT FIELDS
         SRL   R1,1                   DIVIDE BY 2
         LA    R9,REPORT+1
         LA    R9,0(R1,R9)            DISPLACEMENT FOR ACCOUNT START
         BCTR  R2,0
         EX    R2,MVCACT              MOVE IN ACCOUNT FILEDS
         B     SKIPMVC
MVCACT   MVC   0(0,R9),SMFZPACT       MOVE ACCOUNT TO REPORT
*
SKIPMVC  EQU   *
*
         XR    R14,R14                 CLEAR REGISTER 14
         XR    R15,R15                 CLEAR REGISTER 15
         XR    R2,R2                   CLEAR REGISTER 2
         LR    R1,R9
         IC    R14,SMFZP#AC            PUT # OF ACCOUNT FIELDS IN R14
         IC    R15,0(R1)               PUT 1ST LENGTH IN R15
*
ACCOUNT2 EQU   *
*
         LA    R1,1(R15,R1)            ADD LENGTH OF ACCT FIELD
         IC    R15,0(R1)               INSERT NEXT LENGTH
         MVI   0(R1),C','              MOVE COMMA TO REPORT
         BCT   R14,ACCOUNT2
*                                      + 1 FOR LENGTH OF FIELD
         MVI   0(R9),C'('
         MVI   0(R1),C')'
         MVI   REPORT,C'0'
         BAL   R10,PRINTRCD
*
NOACCT   EQU   *
*
         MVC   REPORT+(L'CTLCARD-L'TITLE4)(L'TITLE4),TITLE4
         MVI   REPORT,C'-'
         BAL   R10,PRINTRCD
*
         MVC   CTLCARD,SMFZPCRD        MOVE CONTROL CARD TO PRINT
         MVI   REPORT,C'0'
         BAL   R10,PRINTRCD
         L     R10,SAVE10
         BR    R10
         TITLE 'HANDLE TIME CONVERSIONS'
*
TIMECONV EQU   *
         XR    R4,R4
         D     R4,=F'360000'           DIVIDE BY H/MINUTES
         CVD   R5,TIMEWORK             CONVERT TIME HOURS
         UNPK  WORK3,TIMEWORK+6(2)     UNPACK HOURS
         OI    WORK3+2,240             SET SIGN TO PRINTABLE
         MVC   0(2,R9),WORK3+1         MOVE HOURS TO PRINT
         MVI   2(R9),C':'
         LA    R9,3(R9)                GET TO MINUTE AREA
         SRDL  R4,32                   SHIFT REMAINDER FOR DIVIDE
         D     R4,=F'6000'
         CVD   R5,TIMEWORK             CONVERT TIME MINUTES
         UNPK  WORK3,TIMEWORK+6(2)     UNPACK MINUTES
         OI    WORK3+2,240             SET SIGN TO PRINTABLE
         MVC   0(2,R9),WORK3+1         MOVE MINUTES TO PRINT
         MVI   2(R9),C':'
         LA    R9,3(R9)                GET TO MINUTE AREA
         SRDL  R4,32                   SHIFT REMAINDER FOR DIVIDE
         D     R4,=F'100'
         CVD   R5,TIMEWORK             CONVERT TIME SECONDS
         UNPK  WORK3,TIMEWORK+6(2)     UNPACK SECONDS
         OI    WORK3+2,240             SET SIGN TO PRINTABLE
         MVC   0(2,R9),WORK3+1         MOVE SECONDS TO PRINT
         BR    R10                     GO BACK
         TITLE 'END OF JOB ROUTINES'
NOSMF    EQU   *
         MVC   REPORT+1(L'TITLE),TITLE   MOVE IN TITLE
         ZAP   LINECNT,=P'60'          SET LINE COUNT
         AP    PAGECT,=P'1'            ADD 1 TO PAGE COUNT
         ED    TPAGE#,PAGECT           PUT PAGE NUMBER ON PAGE
         MVI   REPORT,C'1'
         LA    R9,TTIME
         L     R5,TIMEDATE
         BAL   R10,TIMECONV              CONVERT TIME
         ED    TDATE,TIMEDATE+5          EDIT DATE
         BAL   R10,PRINTRCD              GO PRINT RECORD
         MVI   REPORT,C'-'               ADVANCE DOWN PAGE SOME
         BAL   R10,PRINTRCD
         CLI   SMFSORT+1,X'00'           HAVE WE PROCESSED ANYTHING
         BE    SOMEZAPS
         MVC   REPORT+(L'REPORT-L'NOZAPS)/2(L'NOZAPS),NOZAPS
         MVI   REPORT,C'-'
         BAL   R10,PRINTRCD
*
SOMEZAPS EQU   *
*
         MVI   REPORT,C'-'
         BAL   R10,PRINTRCD
         MVC   REPORT+(L'REPORT-L'ENDJOB)/2(L'ENDJOB),ENDJOB
         MVI   REPORT,C'-'
         BAL   R10,PRINTRCD              PRINT END OF REPORT
         CLI   SMFSORT+1,X'00'           HAVE WE PROCESSED ANYTHING
         BE    EOJ
         CLOSE (SYSUT1,,SORTIN,,SYSPRINT)
         B     RETURN
EOJ      EQU   *
         CLOSE (SYSPRINT,,SORTOUT)
         TITLE 'RETURN LINKAGE'
RETURN   EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0
         TITLE 'DCB DEFINITIONS'
         PRINT NOGEN
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=FA,LRECL=133,MACRF=PM,DSORG=PS,   X
               BLKSIZE=133
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GM,DSORG=PS,EODAD=SMFSORT
SORTIN   DCB   DDNAME=SORTIN,DSORG=PS,MACRF=PM
SORTOUT  DCB   DDNAME=SORTOUT,DSORG=PS,EODAD=NOSMF,MACRF=GM
         TITLE 'CONSTANTS AND WORK AREAS'
         PRINT GEN
PARMLIST DC    X'80',AL3(SORTLIST)     SORT PARAMITER LIST POINTER
         CNOP  2,4                     BOUNDARY ALIGN
SORTLIST DC    AL2(LEND-LSTART)        LIST LENGTH
LSTART   EQU   *
         DC    A(SORTS)                SORT STATMENT
         DC    A(SORTX)                END OF SORT STATMENT
         DC    A(RECORDS)              RECORD STATMENT
         DC    A(RECORDE)              END OF RECORD STATMENT
         DC    A(0)                    SORT E15 INPUT EXIT
         DC    A(0)                    SORT E35 OUTPUT EXIT
         DC    X'00',CL3'MAX'          MAIN STORAGE
         DC    X'01',AL3(4096)         RESERVED STORAGE
         DC    X'FF',CL3'(I)'          PRINT MESSAGES
         DC    CL4'BALN'               BALANCED TECHNIQUE
LEND     EQU   *
SORTS    DC    C' SORT  FIELDS=(58,50,CH,A,18,16,BI,A)'
         DC    C',FILSZ=E100000  '
SORTX    DC    C'   '
RECORDS  DC    C' RECORD TYPE=V,LENGTH=(32760,,32760,204,349)  '
RECORDE  DC    C'  '
BLANK    EQU   C' '
DSCB     DC    C'FORMAT4.DSCB'
SAVEAREA DC    18F'0'
WORKD    DC    D'0'
WORK3    DC    CL3' '
ENDJOB   DC    C'END-OF-REPORT'
NOZAPS   DC    C'NO -AMASPZAP- (SUPERZAPS) EXECUTED TODAY'
ACTMSG   DS    0CL32
         DC    X'20'
         DC    C'ACCOUNT MORE THAN 131 CHARACTERS'
DATEMASK DC    X'402020202120'
NEWDSN   DC    CL50' '
PAGECT   DC    PL2'0'
LINECNT  DC    PL2'57'
SAVE10   DC    F'0'
NEWJOB   DC    CL16' '
TIMEDATE DC    D'0'
TIMEWORK DC    D'0'
         DC    C' '
REPORT   DS    0CL133
CC       DC    CL1' '
         DC    CL2' '
JOBDSN   DC    CL44' '
         DC    CL2' '
JOBVOL   DC    CL6' '
         DC    CL2' '
JOBNAME  DC    CL8' '
         DC    CL2' '
JOBID    DC    CL8' '
         DC    CL2' '
JOBSID   DC    CL4' '
         DC    CL2' '
JOBTIME  DC    CL8' '
         DC    CL2' '
STPTIME  DC    CL8' '
         DC    CL2' '
JOBDATE  DC    CL5' '
         DC    CL2' '
JOBPGMR  DC    CL20' '
         DC    CL3' '
         ORG   REPORT+26
CTLCARD  DS    CL80
         ORG REPORT+20
TDATE    DS    0CL6
         ORG REPORT+114
TTIME    DS    0CL8
         ORG REPORT+129
TPAGE#   DS    0CL4
         ORG
TITLE    DS    0CL132
         DC    CL12'DPR TSZAP-01'
         DC    CL3' '
         DC    CL4'DATE'
         DC    X'402020202020'
         DC    CL8' '
         DC    CL50'F I R S T   A T L A N T A   S U P E R Z A P   A U '
         DC    CL17'D I T   T R A I L'
         DC    CL8' '
         DC    CL5'TIME '
         DC    CL8' '
         DC    CL3' '
         DC    CL4'PAGE'
         DC    XL4'40202120'
TITLE2   DS    0CL132
         DC    CL2' '
         DC    CL25'D A T A   S E T   N A M E'
         DC    CL21' '
         DC    CL6'VOLSER'
         DC    CL2' '
         DC    CL8'JOB NAME'
         DC    CL2' '
         DC    CL8' JOB ID '
         DC    CL1' '
         DC    CL6'SYSTEM'
         DC    CL1' '
         DC    CL8'JOB TIME'
         DC    CL2' '
         DC    CL8'STP TIME'
         DC    CL2' '
         DC    CL5'DATE '
         DC    CL2' '
         DC    CL20'PROGRAMMER  NAME    '
         DC    CL3' '
TITLE3   DC    C'J O B   A C C O U N T I N G   I N F O R M A T I O N'
TITLE4   DC    C'       S U P E R Z A P   C O N T R O L   C A R D S'
         LTORG
         TITLE 'SMDZPD - SMF RECORD DESCRIPTION'
         SMFZPD
DSDZAPRP CSECT
RECORD   DS    32760C
         END
