PDS      TITLE 'T S O   P D S   C O M M A N D   P R O C E S S O R'
***********************************************************************
*                                                                     *
*                                                                     *
* TITLE -      P D S   -   TSO PDS PROCESSOR                          *
*                                                                     *
* FUNCTION -   PROVIDE THE TSO USER WITH THE CAPABILITY               *
*              TO MANIPULATE A PARITIONED DATA SET                    *
*              DIRECTORY AND SELECTED MEMBERS.                        *
*                                                                     *
*                                                                     *
* OPERATION -  OPERATES AS A STANDARD COMMAND PROCESSOR UNDER         *
*              TSO. ALLOCATES THE DATA SET(S) REQUESTED BY THE        *
*              USER. PROVIDES DISPLAY, RENAME, SCRATCH, AND           *
*              ALIAS FUNCTIONS FOR THE DIRECTORY ENTRIES.             *
*              PRODUCES LOAD MODULE MAPS AND HISTORY SUMMARIES.       *
*              DISPLAYS THE CONTENTS OF MEMBERS OF THE DATA SET.      *
*                                                                     *
*                                                                     *
* INPUT -      STANDARD COMMAND PROCESSOR PARAMETER LIST              *
*              POINTED TO BY REGISTER 1                               *
*                                                                     *
*                                                                     *
* OUTPUT -     NOT APPLICABLE.                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES - REENTRANT, REUSEABLE, REFRESHABLE.                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         MACRO
&NAME    SCREEM &TYPE
&NAME    SVC   236 .                    ISSUE SPNB ERASE SVC
         MEND
         SPACE 3
         MACRO
&NAME    CLEAR &FIELD
         LCLC  &L
&L       SETC  'L'''
&NAME    MVI   &FIELD,C' '
         MVC   &FIELD+1(&L&FIELD-1),&FIELD
         MEND
         SPACE 3
         MACRO
&NAME    MSG   &TEXT
         LCLA  &A
&A       SETA  K'&TEXT-2+4
&NAME    DC    0H'0',AL2(&A,0),C&TEXT
         MEND
         EJECT
         MACRO
&NAME    ENTER &TYPE,&PDL=
         GBLC  &MAIN
         AIF   ('&TYPE' NE '').A1
&MAIN    SETC  '&NAME'
&NAME    CSECT
         SAVE  (14,12),,*
         LR    BASEREG1,R15
         LA    BASEREG2,4095(,BASEREG1)
         LA    BASEREG2,1(,BASEREG2)
         USING &NAME,BASEREG1,BASEREG2
         SPACE 2
         B     IHB&SYSNDX
         DC    C'COPYRIGHT 1975 BY SECURITY PACIFIC NATIONAL BANK'
IHB&SYSNDX DS  0H
         MEXIT
.A1      AIF   ('&TYPE' NE 'VALCHECK').A2
&NAME    CSECT
         SAVE  (14,12),,*
         LR    VLDBASE,R15 .            ESTABLISH BASE REGISTER
         USING &NAME,VLDBASE
         L     WORKREG,4(,R1) .         WORK AREA ADDRESS
         LM    BASEREG1,BASEREG2,BASES .RELOAD MAIN BASE REGISTERS
         L     RPDL,0(,R1) .            PDE ADDRESS
         LA    R15,VALSAVE              VALIDITY CHECK SAVE AREA
         AGO   .SETSAVE
.A2      ANOP
         AIF   ('&TYPE' NE 'ATTNEXIT').A3
&NAME    SAVE  (14,12)
         L     WORKREG,8(,R1)
         LM    BASEREG1,BASEREG2,BASES
         LA    R15,ATTNSAVE
         AGO   .SETSAVE
.A3      ANOP
         AIF   ('&TYPE' NE 'COMMAND').A4
&NAME    CSECT
         SAVE  (14,12),,*
         LR    CMDBASE,R15 .            ESTABLISH BASE REGISTER
         USING &NAME,CMDBASE
         AIF   ('&PDL' EQ '').NOPDL
         USING &PDL,RPDL .          ESTABLISH BASE FOR PDL
.NOPDL   ANOP
         LA    R15,CMDSAVE
         AGO   .SETSAVE
.A4      ANOP
         MNOTE 8,'INVALID TYPE ''&TYPE'''
         MEXIT
.SETSAVE ANOP
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         MEND
         EJECT
         MACRO
&NAME    EXIT  &TYPE,&LV=
         GBLC  &MAIN
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&LV' EQ '').A1
         LR    R2,R13         ADDR OF THIS SAVE AREA
.A1      L     R13,4(,R13)
         STM   R15,R1,16(R13) RETURN REGS 15, 0, 1
         AIF   ('&LV' EQ '').A2 NOT DYNAMIC CORE
         FREEMAIN R,LV=&LV,A=(R2)
.A2      SPACE
         RETURN (14,12),T
&MAIN    CSECT
         MEND
         SPACE 3
         MACRO $PUTGET -  OUTPUT A DATA LINE AND GET RESPONSE
&NAME   $PUTGET  &LINE,&ATTN=
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
         LA    R1,&LINE .            ADDRESS OF DATA LINE
         AGO   .CALL
.R       ANOP
         LR    R1,&LINE(1) .                ADDRESS OF DATA LINE
.CALL    ANOP
         BAL   R14,$PUTGET .            INVOKE PUTGET INTERFACE
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN .               EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0 .                      IGNORE ATTENTIONS
.END     MEND
         EJECT
         MACRO $PUTLINE -  OUTPUT A DATA LINE
&NAME   $PUTLINE &LINE,&ATTN=
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
         LA    R1,&LINE .            ADDRESS OF DATA LINE
         AGO   .CALL
.R       ANOP
         LR    R1,&LINE(1) .                ADDRESS OF DATA LINE
.CALL    ANOP
         BAL   R14,$PUTLINE .           INVOKE PUTLINE INTERFACE
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN .               EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0 .                      IGNORE ATTENTIONS
.END     MEND
         SPACE 2
         MACRO MESSAGE - OUTPUT AN ERROR/STATUS MESSAGE
&NAME    MESSAGE &MSG1,&MSG2
         AIF   ('&MSG1' NE '').MSG1OK
         MNOTE 12,'PRIMARY MESSAGE ADDRESS NOT SPECIFIED'
         AGO   .END
.MSG1OK  ANOP
         AIF   ('&NAME' EQ '').NONAME
&NAME    DS    0H
.NONAME  ANOP
         AIF   ('&MSG1' EQ '(1)' OR '&MSG1' EQ '(R1)').MSG2
         AIF   ('&MSG1'(1,1) EQ '(').RMSG1
         LA    R1,&MSG1 .               ADDRESS OF PRIMARY MESSAGE
         AGO   .MSG2
.RMSG1   ANOP
         LR    R1,&MSG1(1) .            ADDRESS OF PRIMARY MESSAGE
.MSG2    ANOP
         AIF   ('&MSG2' EQ '').NOMSG2 NO SECONDARY MESSAGE
         AIF   ('&MSG2' EQ '(0)' OR '&MSG2' EQ '(R0)').CALL
         AIF   ('&MSG2'(1,1) EQ '(').RMSG2
         LA    R0,&MSG2 .               ADDRESS OF SECONDARY MESSAGE
         AGO   .CALL
.RMSG2   ANOP
         LR    R0,&MSG2(1) .            ADDRESS OF SECONDARY MESSAGE
.CALL    ANOP
         BAL   R14,MESSAGE .            SEND MESSAGES
         AGO   .END
.NOMSG2  ANOP
         BAL   R14,MESSAGE0 .           SEND PRIMARY MESSAGE
.END     MEND
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
RPDL     EQU   8
WORKREG  EQU   9
BASEREG1 EQU   10
BASEREG2 EQU   11
VLDBASE  EQU   12
CMDBASE  EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 5
PDS      ENTER
         LR    R2,R1          SAVE ADDR OF CPPL
         SPACE
         L     R3,CORESIZE
         GETMAIN R,LV=(R3)
         SPACE 2
         LR    WORKREG,R1    ADDR OF WORK AREA
         USING WORKAREA,WORKREG
         EJECT
*
*        CLEAR WORK AREA
*
         SPACE 2
         LA    R14,0(,R3)     LENGTH OF AREA
         BCTR  R14,0          EXECUTED LENGTH
         LA    R15,255        LOOP FACTOR
LOOP01   CR    R14,R15        LAST PASS?
         BNH   LOOP00         YES
CLEAR1   XC    0(0,R1),0(R1)
         EX    R15,CLEAR1
         LA    R1,256(,R1)
         SR    R14,R15
         BCT   R14,LOOP01     IF MORE TO CLEAR
         SPACE
LOOP00   EX    R14,CLEAR1     CLEAR LAST AREA
         SPACE 2
         STM   BASEREG1,BASEREG2,BASES SAVE BASE REGISTERS
         SPACE 3
*
*        INITIALIZE WORK AREA
*
         SPACE 2
         MVC   BLDLLIST,BLDLPARM
         SPACE
         GTSIZE ,             GET TERMINAL LINE SIZE
         STH   R1,LINESIZE
         SPACE 3
         LA    R0,IOECB                 ECB ADDRESS
         ST    R0,IOBECB                SET INTO IOB
         LA    R0,INDCB                 DCB ADDRESSS
         ST    R0,IOBDCB                SET INTO DCB
         LA    R0,CCWS                  CCW CHAIN ADDRESS
         ST    R0,IOBCCW                SET INTO IOB
         MVI   IOBFLAG,X'42'            SET IOB FLAGS
         EJECT
*
*        ESTABLISH STAE EXIT
*
         SPACE 3
         MVC   STAEPARM(LSTAE),STAE MOVE PATTERN STAE TO WORK AREA
         STAE  STAEEXIT,CT,PARAM=WORKAREA,MF=(E,STAEPARM) ISSUE STAE
         SPACE
         LA    R15,MAINSAVE
         ST    R15,8(,R13)
         ST    R13,4(,R15)
         LR    R13,R15
         ST    R13,ADDRSAVE             ADDRESS OF FIRST SAVE AREA
         EJECT
*
*        LOAD TSO SERVICE ROUTINES
*
         SPACE 3
         USING CPPL,R2                  BASE FOR COMMAND PARM LIST
         MVC   ADDRUPT,CPPLUPT          ADDR OF USER PROFILE TABLE
         MVC   ADDRPSCB,CPPLPSCB        ADDR OF P S C B
         MVC   ADDRECT,CPPLECT          ADDR OF ENVIROMENT TABLE
         MVC   ADDRCBUF,CPPLCBUF        ADDR OF FIRST CMD BUFFER
         ST    R2,CPPLADDR              SAVE CPPL ADDRESS
         DROP  R2
         SPACE 3
         LA    R1,PARSELST
         USING PPL,R1
         MVC   PPLUPT,ADDRUPT           SET ADDRESSES OF
         MVC   PPLECT,ADDRECT           CONTROL BLOCKS IN
         ST    WORKREG,PPLUWA           PARSE PARAMETER LIST
         LA    R0,ATTNECB
         ST    R0,PPLECB
         SPACE 2
         LOAD  EPLOC=IKJPUTL            PUTLINE SERVICE ROUTINE
         ST    R0,ADDRPUTL
         LOAD  EPLOC=IKJGETL            GETLINE SERVICE ROUTINE
         ST    R0,ADDRGETL
         SPACE 2
         LOAD  EPLOC=IKJPTGT            PUTGET SERVICE ROUTINE
         ST    R0,ADDRPTGT
         SPACE 2
         LOAD  EPLOC=IKJSCAN            COMMAND SCAN SERVICE ROUTINE
         ST    R0,ADDRSCAN
         EJECT
*
*
*        SET UP TO INVOKE PARSE TO GET DATA SET NAME
*
*
         SPACE 2
         STAX  ,
         L     R1,MAINPCL               ADDRESS OF PCL FOR DATASET NAME
         BAL   R14,GOPARSE              GET THE DATA SET NAME
         B     EXIT12                   ERROR EXIT
         SPACE 2
         XC    ADDRCBUF,ADDRCBUF        NO COMMAND BUFFER LEFT
         SPACE 2
RESTART  DS    0H
         BAL   R14,ALLOCATE             ALLOCATE THE DATA SET
         B     EXIT12                   IF ALLOCATION UNSUCCESSFUL
         SPACE 2
         NI    FLAGS,255-FRESTART
         MVC   INDCB(LEXCPDCB),EXCPDCB  MOVE PATTERN DCBS TO
         MVC   STOWDCB(LSAMDCB),SAMDCB  WORK AREA
         MVC   DCBDDNAM-IHADCB+INDCB,DDNAME
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         SPACE
         XC    OPENLIST(8),OPENLIST
         MVI   OPENLIST,X'80'           SET E-O LIST INDICATOR
         LA    R1,EXLSTX
         ST    R1,0(,R13)
         MVC   DCBEXLST+1-IHADCB+INDCB(3),1(R13)
         LA    R1,JFCB
         ST    R1,EXLSTX
         MVI   EXLSTX,X'87'
         RDJFCB (INDCB,INPUT),MF=(E,OPENLIST)
         XC    OPENLIST(8),OPENLIST     RESET MF=L AREA
         MVI   OPENLIST+4,X'80'         FOR 2 DCBS
         OPEN  (INDCB,INPUT,STOWDCB,INPUT),MF=(E,OPENLIST)
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)
         SPACE
         TM    INDCB+48,X'10' DID DCB OPEN?
         BO    NEWCMD                   CONTINUE IF YES
         SPACE 2
         MESSAGE MSGNOPEN               NO, ISSUE MESSAGE AND EXIT
         B     EXIT12                   STAGE DOOR RIGHT
         EJECT
*
*
*        DISPLAY PROGRAM OPTIONS FOR USER
*
*
         SPACE 1
LISTOPTS DS    0H
         SPACE
PUTOPTNS DS    0H
         L     R2,VOPTNTBL
         SPACE
NEXTOPTN DS    0H
        $PUTLINE (R2)                   DISPLAY OPTION LINE
         SPACE
         MVC   0(2,R13),0(R2)
         LA    R0,1
         LH    R1,0(,R13)               ADJUST TO NEXT OPTION
         AR    R1,R0
         OR    R1,R0
         XR    R1,R0
         AR    R2,R1
         CLI   0(R2),X'FF'              LAST ONE?
         BNE   NEXTOPTN                 NO
         SPACE 2
NEWCMD   DS    0H
         MVI   ATTNECB,0
         NI    FLAGS,255-FMEMBER1-FMEMBER2-FATTR
         NI    FLAGS+1,255-FATTN
         MVI   FLAGS+2,0
         XC    ATTRYES,ATTRYES
         XC    ATTRNO,ATTRNO
         SPACE 2
         STAX  ,                           NULLIFY ATTN EXITS
         SPACE 2
         TM    FLAGS,FCMD               COMMAND BUFFER AVAILABLE?
         BO    HAVECMD                  YES
         SPACE
        $PUTGET MSGOP,ATTN=EXIT8
         SPACE
         EJECT
*
*
*        SET UP FOR SUBCOMMAND SCAN
*
*
         SPACE 2
HAVECMD  DS    0H
         NI    FLAGS,255-FCMD
         SPACE 2
         LA    R1,PARMLIST
         USING CSPL,R1
         SPACE
         MVC   CSPLUPT,ADDRUPT
         MVC   CSPLECT,ADDRECT
         LA    R0,ATTNECB
         ST    R0,CSPLECB
         MVI   ATTNECB,0
         LA    R0,SCANANSR
         ST    R0,CSPLOA
         XC    SCANANSR,SCANANSR
         LA    R0,R14SAVE
         ST    R0,CSPLFLG
         XC    R14SAVE,R14SAVE
         SPACE
         MVC   CSPLCBUF,ADDRCBUF
         SPACE
         L     R15,ADDRSCAN
         BALR  R14,R15                  INVOKE COMMAND SCAN
         SPACE
         LA    R1,SCANANSR              ADDR OF ANSWER AREA
         USING CSOA,R1
         SPACE
         TM    CSOAFLG,CSOANOC          EMPTY BUFFER?
         BO    NEWCMD                   YES, STILL NEED COMMAND
         SPACE
         TM    CSOAFLG,CSOAEXEC         IMPLICIT EXEC?
         BZ    HAVECMD1                 YES, DON'T INVOKE EXEC
         SPACE 1
         L     R2,ADDRCBUF
         XC    2(2,R2),2(R2)            CLEAR OFFSET
         SPACE
EXEC     L     R2,ADDRCBUF
         L     R1,CPPLADDR
         ST    R2,0(,R1)
         SPACE 1
         LINK  EP=EXEC
         SPACE
         B     NEWCMD
         SPACE
HAVECMD1 DS    0H
         TM    CSOAFLG,CSOAVWP+CSOAVNP  VALID COMMAND?
         BNZ   CHECKCMD                 YES
         SPACE
         MESSAGE MSGINVLD               INVALID COMMAND
         B     NEWCMD
         EJECT
CHECKCMD DS    0H
         OI    FLAGS,FOPTIONS
         TM    CSOAFLG,CSOAVWP          OPERAND PRESENT?
         BO    *+8                      YES
         XI    FLAGS,FOPTIONS           NO
         SPACE
         L     R2,CSOACNM               ADDR OF COMMAND NAME
         LH    R3,CSOALNM               LENGTH OF NAME
         BCTR  R3,0
         DROP  R1
         SPACE
         MVC   SUBNAME(0),0(R2)
         EX    R3,*-6                   SAVE SUB COMMAND NAME
         SPACE
         L     R1,VCMDTAB
         SR    R15,R15                  SET UP FOR COMMAND SCAN
         SR    R14,R14
         SPACE 2
* COMMAND SCAN ROUTINE
*    NOTE - IN THE CASE OF A POTENTIALLY AMBIGUOUS ENTRY
*           (E.G. ''A'') THE FIRST MATCH IN THE CMDTABLE
*           IS USED
         SPACE
CMDSCAN  IC    R15,8(,R1)               GET CMD LENGTH
         CR    R15,R3                   CHECK CMD LENGTH
         BNH   NEXTCMD
         SPACE
         CLC   0(0,R1),0(R2)
         EX    R3,*-6                   CHECK COMMAND NAME
         BE    CMDVALID                 MATCH, MUST BE COMMAND
         SPACE
NEXTCMD  DS    0H
         LA    R1,16(,R1)               NEXT ENTRY IN TABLE
         CLI   0(R1),X'FF'              END OF TABLE?
         BNE   CMDSCAN                  NO, CONTINUE
         SPACE
         LA    R1,MSGUNKN               NOT A VALID COMMAND
         SPACE
CMDERROR DS    0H
         MESSAGE (R1)
         B     NEWCMD
         SPACE
CMDVALID DS    0H
         LR    R14,R1                   SAVE CMD ENTRY
         MVC   ADDRPCL,12(R14)          SAVE PCL ADDR
         L     R1,8(,R14)               GET COMMAND PROCESSOR ADDRESS
         ST    R1,ADDRCMD               SAVE IT
         SPACE 2
         TM    ADDRPCL,X'40'            IGNORE OPTIONS?
         BO    CALLCMD                  YES
         SPACE
         TM    ADDRPCL,X'80'            OPERANDS REQUIRED?
         BZ    CMDPARSE                 YES
         TM    FLAGS,FOPTIONS           NO, WERE ANY SPECIFIED?
         BZ    CALLCMD
         SPACE 2
CMDPARSE DS    0H
         L     R1,ADDRPCL
         BAL   R14,GOPARSE              GET THE COMMAND OPERANDS
         B     NEWCMD                   IF PARSE NOT SUCCESSFUL
         EJECT
CALLCMD  DS    0H
         XC    PARMLIST(20),PARMLIST
         SPACE 2
         XC    PARMLIST,PARMLIST
         STAX  ATTNEXIT,USADDR=(WORKREG),REPLACE=YES,IBUF=0,OBUF=0,    X
               MF=(E,PARMLIST)
         SPACE 2
         L     R15,ADDRCMD
         L     RPDL,ADDRPDL             ADDRESS OF PARSE DESC LIST
         BALR  R14,R15                  LINK TO COMMAND PROCESSOR
         SPACE
         BAL   R14,FREEPDL              CLEAN UP AFTER COMMAND
         SPACE 3
         B     NEWCMD
         EJECT
*
*
*        TERMINAL ATTENTION EXIT ROUTINE
*
*
         SPACE 3
ATTNEXIT ENTER ATTNEXIT
         TM    FLAGS+1,FATTN  ALLOW ATTENTIONS?
         BZ    EXITATTN       NO
         SPACE 2
        $PUTGET MSGOP                   GET RESPONSE TO ATTN
         SPACE
         TM    FLAGS,FNULL    NULL LINE ENTERED?
         BO    EXITATTN       YES, JUST LEAVE
         SPACE
         OI    FLAGS,FCMD     INDICATE COMMAND AVAILABLE
         SPACE
         POST  ATTNECB
         SPACE
EXITATTN DS    0H
         SR    R15,R15
         EXIT
         EJECT
*
*
*        LIST SUBCOMMAND
*
*
         SPACE 3
LIST     ENTER COMMAND,PDL=PDLLIST      ESTABLISH SUBCOMMAND
         TM    FLAGS,FLOADMOD           THIS A LOAD MODULE LIBRARY?
         LA    R1,MSGNLIST              MSG IF YES
         BO    LISTERR                  YES, ERROR
         SPACE 2
         MVC   DIRNAME,MEMBER1          LOCATE MEMBER TO BE LISTED
         BLDL  INDCB,BLDLLIST
         B     *+4(R15)
         SPACE 2
         B     LIST1                    00 - SUCCESSFUL
         B     LISTNONE                 04 - MEMBER NOT FOUND
         B     LISTDERR                 08 - I/O ERROR IN DIRECTORY
         SPACE 2
LIST1    DS    0H
         SPACE 2
         OI    FLAGS+1,FATTN            INDICATE ATTENTIONS ACCEPTED
         EJECT
*
*
*        INITIALIZE CHANNEL PROGRAM TO READ SELECTED MEMBER
*
*
*        CCW CHAIN USED IS -      SEARCH ID    -----+
*                                 TIC           ----+
*                                 READ COUNT
*                                 READ DATA
*
         SPACE 2
         XC    CCWS(LCCWS),CCWS         CLEAR CCW AREA
         SPACE 2
         LA    R0,IOBSEEK               ADDRESS OF IOB SEEK
         LA    R1,CCWS                  FIRST CCW - SEARCH CCW
         L     R2,BUFFADDR              ADDRESS OF BUFFER
         ST    R0,CCWS                  SEEK ADDRESS IN CCW1
         ST    R1,CCWS+8                TIC ADDRESS IN CCW2
         ST    R0,CCWS+16               READ COUNT ADDR IN CCW3
         ST    R2,CCWS+24               BUFFER ADDR IN CCW4
         LH    R3,BUFFSIZE              LENGTH OF BUFFER
         STH   R3,CCWS+30               SET IN CCW4
         SPACE 2
         OC    CCWS(LLISTCCW),LISTCCWS  COPY PATTERN CCW CHAIN
         SPACE 2
*
*        CONVERT TTR OF START OF MEMBER TO CCHHR FORMAT
*
         SPACE 2
         STM   14,12,12(13)             SAVE REGISTERS FOR RETURN
         LA    R2,IOBSEEK-3             RETURN CCHHR IN IOBSEEK
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         MVI   DIRTTR+3,0               CLEAR TTR CONCATENTION FIELD
         L     R0,DIRTTR                GET TTR0 OF MEMBER
         LA    R15,X'100'               GET TTR0 OF PREVIOUS RECORD
         SLR   R0,R15
         L     R15,CVT
         L     R15,CVTPCNVT(,R15)       ADDRESS OF TTR-CCHHR CONVERT
         LR    R3,R13                   SAVE ADDRESS OF SAVE AREA
         BALR  R14,R15
         LR    R13,R3                   RESTORE SAVE AREA ADDRESS
         LM    14,12,12(13)             RESTORE REGISTERS
         EJECT
LIST2    DS    0H
         MVI   IOECB,0
         EXCP  IOB                      READ NEXT RECORD
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0  CLEAR I/O STATUS FLAGS
         SPACE 2
         TM    IOBCSW+3,X'01'           END-OF-FILE?
         BO    LISTEND                  YES
         SPACE 2
         CLI   IOECB,X'42'              OUT OF EXTENT?
         BNE   LIST3                    NO, CHECK STATUS
         BAL   R14,NEWEXTNT             YES, ADJUST FOR NEW EXTENT
         MVI   IOBSEEK+4,0              CORRECT TO RECORD 0
         B     LIST2                    THEN RE-READ RECORD
         SPACE
LIST3    DS    0H
         CLI   IOECB,X'7F'              SUCCESSFUL?
         LA    R1,MSGLISTI              ERROR MESSAGE FOR I/O ERROR
         BNE   LISTERR                  NO, I/O ERROR
         SPACE 2
LIST5    DS    0H
         LH    R3,BUFFSIZE              MAXIMUM RECORD LENGTH
         SH    R3,IOBCSW+5              SUBTRACT RESIDUAL COUNT
         L     R4,BUFFADDR              START OF BUFFER
         AR    R3,R4                    ADDRESS OF END OF BUFFER
         SPACE 2
         TM    DCBRECFM-IHADCB+INDCB,X'40' IS FILE VARIABLE FORMAT?
         LH    R2,LRECL                 GET RECORD LENGTH IF NOT
         LA    R5,LISTFIX               FIXED LENGTH RECORD PROCESSOR
         BZ    LIST6                    NO, FIXED FORMAT
         SPACE 2
         MVC   0(2,R13),0(R4)           GET BLOCK LENGTH
         LH    R3,0(,R13)
         AR    R3,R4                    END OF VARIABLE-LENGTH BLOCK
         LA    R4,4(,R4)                START OF RECORD ENTRIES
         LA    R5,LISTVAR               VARIABLE LEN RECORD PROCESSOR
         SPACE 3
LIST6    DS    0H
         BCTR  R3,0                     SET END OF BUFFER ADDRESS
         BR    R5                       PROCESS FIRST RECORD
         EJECT
LISTVAR  DS    0H
         MVC   0(2,R13),0(R4)           GET LENGTH OF VARIABLE RECORD
         LA    R4,4(,R4)                SKIP OVER HEADER
         LH    R2,0(,R13)               GET LOGICAL RECORD LENGTH
         LA    R0,4
         SR    R2,R0                    SUBTRACT HEADER LENGTH
         BP    LISTFIX                  CONTINUE IF RECORD EXISTS
         SPACE
         SR    R2,R2
         B     LIST7
         SPACE 2
LISTFIX  DS    0H
         TM    DCBRECFM-IHADCB+INDCB,X'06' SPECIAL CONTROL CHARACTERS?
         BZ    LIST8                    NO
         LA    R4,1(,R4)                YES, SKIP CARRIAGE CONTROL
         BCT   R2,LIST8
         SPACE 2
LIST7    DS    0H
         BXLE  R4,R2,0(R5)              SKIP TO NEXT RECORD
         B     LIST2                    IF END OF BLOCK
         SPACE 3
LIST8    DS    0H
         LR    R1,R2                    LENGTH OF DATA AREA
         BCTR  R1,0                     DECREMENT RECORD LEN FOR MOVE
         MVC   MSGTEXT1+4(0),0(R4)
         EX    R1,*-6                   MOVE RECORD TO LINE BUFFER
         LA    R1,5(,R1)                LINE LENGTH PLUS HEADER FIELD
         SLL   R1,16
         ST    R1,MSGTEXT1              CREATE OUTPUT LINE HEADER
         LA    R1,MSGTEXT1
LISTIT   DS    0H
        $PUTLINE MSGTEXT1,ATTN=LISTEND  OUTPUT THE NEXT LINE
         SPACE 1
         B     LIST7                    GET NEXT RECORD
         EJECT
LISTERR  DS    0H                       ERROR DURING PROCESSING
         MESSAGE (R1)                   ISSUE ONLY PRIMARY MESSAGE
         B     LISTEND                  EXIT
         SPACE 2
LISTNONE DS    0H
         MESSAGE MSGNONE                ERROR - MEMBER DOES NOT EXIST
         B     LISTEND
         SPACE 2
LISTDERR DS    0H
         MESSAGE MSGIOERR               I/O ERROR IN DIRECTORY
         SPACE 3
LISTEND  DS    0H
         EXIT  COMMAND                  EXIT FROM SUBCOMMAND
         EJECT
*
*
*              HELP  INVOCATION SUBCOMMAND
*
*
         SPACE 3
HELP     ENTER COMMAND     *** NO PDL IS USED FOR THIS SUBCOMMAND ***
         SPACE 2
         L     R2,ADDRCBUF              GET INPUT BUFFER ADDR
         L     R1,CPPLADDR
         USING CPPL,R1
         ST    R2,CPPLCBUF              SET CMD ADDRESS
         DROP  R1
         SPACE
HELPLINK LINK  EP=HELP                  INVOKE THE HELP CMD PROCESSOR
         SPACE
         EXIT COMMAND
         EJECT
*
*
*              USAGE STATISTICS SUBCOMMAND
*
*
         SPACE 3
USAGE    ENTER COMMAND     *** NO PDL IS USED FOR THIS SUBCOMMAND ***
         SPACE 2
         OI    FLAGS+1,F1STREAD         INDICATE FIRST ENTRY
         OI    FLAGS+2,FDIRSCAN         ALSO DIRECTORY SCAN REQUEST
         SPACE 2
         BAL   R14,READDIR              SCAN THROUGH THE DIRECTORY
         B     USE1                     IF END OF DIRECTORY
         B     USE2                     IF I/O ERROR
         B     USE3                     IF PREMATURE EXIT
         SPACE 2
USE1     DS    0H
         LA    R2,MSGUSEB               MSG - TOTAL BLOCKS ALLOCATED
         LH    R1,TOTBLOCK              GET COUNTER
         BAL   R4,USEFMT                FORMAT MESSAGE
         SPACE
         LA    R2,MSGUSED               MSG - TOTAL BLOCKS USED
         LH    R1,TOTUSED               GET COUNTER
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSER               MSG - TOTAL MEMBERS
         LH    R1,TOTMEMR               GET COUNTER
         AH    R1,TOTMEMA
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSEA               MSG - TOTAL ALIASES
         LH    R1,TOTMEMA
         LTR   R1,R1                    ANY ALIASES?
         BZ    USETOTAL                 NO
         BAL   R4,USEFMT
         SPACE 2
USETOTAL DS    0H
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         SR    R14,R14
         SR    R15,R15
         SPACE 2
*
*        CALCULATE TOTAL SIZE OF DATA SET AND UNUSED SPACE
*
         SPACE 2
         IC    R14,DEBXTNT#(,R1)        GET NUMBER OF EXTENTS IN D.S
         STH   R14,DSNEXTNT             SAVE VALUE
         LA    R15,DEBAMLNG             GET SIZE OF EACH EXTENT
         SR    R0,R0
USEXTNT  DS    0H
         AH    R0,DEBEXTNT+14(,R1)      ADD NUMBER OF TRACKS IN EXTENT
         AR    R1,R15                   TO TOTAL AND JUMP TO NEW EXTENT
         BCT   R14,USEXTNT              IF MORE EXTENTS
         SPACE
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         SPACE 2
         L     R1,CVT                   GET CVT ADDRESS
         L     R1,0(,R1)
         L     R1,4(,R1)
         L     R1,12(,R1)               ADDRESS OF TIOT
         AH    R1,DCBTIOT-IHADCB+INDCB  ADDRESS OF TIOT ENTRY
         L     R1,16(,R1)               ADDRESS OF UCB
         MVC   VOLUME,UCBVOLI(R1)       EXTRACT VOLUME SERIAL
         SPACE 2
         TM    FLAGS+1,FQUOTE IS DATASET QUOTED ?
         BO    USE1A          YES, BRANCH
         LA    R15,DSNAME1    NO, POINT TO 'TO' AREA
         L     R1,ADDRPSCB    GET PSCB ADDRESS
         SR    R14,R14        CLEAR R14
         IC    R14,7(R1)      GET USERID LEN
         BCTR  R14,0
         EX    R14,DSNMVE1    MOVE USERID
         AR    R15,R14        ADD TO 'TO' ADDR
         MVI   0(R15),C'.'    MOVE IN A PERIOD
         LA    R15,1(R15)     BUMP POINTER
         LA    R1,44          GET MAX LENGTH
         LA    R14,1(R14)     BUMP LENGTH
         SR    R1,R14         GET WHAT IS LEFT
         EX    R1,DSNMVE2     MOVE REST OF DSN
         B     USE1B
DSNMVE1  MVC   0(0,R15),0(R15) MOVE USERID
DSNMVE2  MVC   0(0,R15),DSNAME
USE1A    MVC   DSNAME1(44),DSNAME
USE1B    EQU   *
         L     R14,OBTAIN               GET OBTAIN FLAGS
         LA    R15,DSNAME               ADDRESS OF FULL DATA SET NAME
         LA    R0,VOLUME                ADDRESS OF VOLUME SERIAL
         LA    R1,DSCB                  ADDRESS OF DSCB/WORKAREA
         STM   R14,R1,CAMLST            SAVE FOR OBTAIN
         SPACE
         SR    R0,R0
         OBTAIN CAMLST                  GET DSCB
         LTR   R15,R15
         BNZ   USENDSCB
         SPACE 2
         LH    R1,DS1LSTAR+DSCB-44      GET TT OF LAST TRACK
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         STH   R1,DSNTUSED
         SPACE 3
         LH    R1,DSNTOTAL              TOTAL SPACE USED
         LA    R2,MSGUSESI              MSG FOR TOTAL SPACE
         BAL   R4,USEFMT
         SPACE
         LH    R1,DSNTUSED              TOTAL FREE SPACE
         LA    R2,MSGUSEMP
         BAL   R4,USEFMT
         SPACE
         LH    R1,DSNEXTNT              TOTAL EXTENTS
         LA    R2,MSGUSEXT
         BAL   R4,USEFMT
         SPACE 1
         MVC   MSGTEXT1(26),MSGUSEV     MESSAGE PATTERN
         MVC   MSGTEXT1+19(6),VOLUME      VOLID
        $PUTLINE MSGTEXT1
         SPACE
         B     USEEXIT
         SPACE 2
USENDSCB DS    0H
         MESSAGE MSGNDSCB
         B     USEEXIT
         SPACE 3
USEFMT   DS    0H
         CVD   R1,DOUBLE                CONVERT COUNTER TO DECIMAL
         LA    R14,10
         SR    R15,R15
USEFMT1  DS    0H
         SR    R0,R0
         DR    R0,R14                   COMPUTE NUMBER OF SIGIF. DIGITS
         LTR   R1,R1                    RESIDUAL FACTOR ZERO?
         LA    R15,1(,R15)
         BNZ   USEFMT1                  NO, KEEP GOING
         BCTR  R15,0                    YES, REDUCE FOR EXECUTE
         LR    R1,R15
         SLL   R1,4                     SHIFT FOR EXECUTED UNPACK
         OI    DOUBLE+7,X'0F'           CORRECT SIGN FOR EXECUTE
         SPACE
         UNPK  MSGTEXT1+4(0),DOUBLE
         EX    R1,*-6
         LA    R1,MSGTEXT1+5(R15)
         SPACE
         LH    R15,0(,R2)
         LA    R0,5
         SR    R15,R0
         MVC   0(,R1),4(R2)
         EX    R15,*-6
         LA    R15,1(R1,R15)
         LA    R0,MSGTEXT1
         SR    R15,R0
         SLL   R15,16
         ST    R15,MSGTEXT1
         SPACE 2
        $PUTLINE MSGTEXT1
         SPACE
         BR    R4                       RETURN
         SPACE 3
USE2     DS    0H
USE3     DS    0H
         MESSAGE MSGUSERR
         SPACE 3
USEEXIT  EXIT  COMMAND
         EJECT
*
*
*        RENAME SUBCOMMAND
*
*
         SPACE 3
RENAME   ENTER COMMAND,PDL=PDLRENAM     ESTABLISH SUBCOMMAND
         SPACE
         BAL   R14,OPENSTOW             OPEN STOW DCB
         BZ    RENEXIT                  EXIT IF DATA SET NOT OPENED
         SPACE 1
         STOW  STOWDCB,MEMBERS,C        ISSUE STOW W/ CHANGE OPTION
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RENAME1                   0- SUCESSFUL
         B     RENDUP                    4- MEMBER EXISTS WITH NEW NAME
         B     RENAMENO                  8- CURRENT NAME DOES NOT EXIST
         B     RENAMEUN                 12- INVALID RETURN FROM RENAME
         B     RENAMERR                 16- IO ERROR IN DIRECTORY
         EJECT
RENAME1  DS    0H
         MESSAGE MSGRENAM               SEND MESSAGE - MEMBER RENAMED
         B     RENEXIT
         SPACE 3
RENAMENO DS    0H
         MESSAGE MSGNONE                ERROR - MEMBER DOES NOT EXIST
         B     RENEXIT
         SPACE 2
RENDUP   DS    0H
         MESSAGE MSGEXIST               ERROR - DUPLICATE MEMBER
         B     RENEXIT
         SPACE 2
RENAMEUN DS    0H
         MESSAGE MSGSTOWC               ERROR RETURN FROM RENAME
         B     RENEXIT
         SPACE 2
RENAMERR DS    0H
         MESSAGE MSGIOERR               I/O ERROR IN DIRECTORY
         SPACE 3
RENEXIT  DS    0H
         EXIT  COMMAND
         EJECT
*
*
*        ALIAS SUBCOMMAND
*
*
         SPACE
ALIAS    ENTER COMMAND,PDL=PDLALIAS     ESTABLISH SUBCOMMAND
         SPACE 2
         MVC   DIRNAME,MEMBER1          ORIGINAL MEMBER NAME
         SPACE
         BLDL  INDCB,BLDLLIST           LOCATE DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         B     ALIAS1                   00-SUCCESSFUL
         B     ALIASNON                 04-MEMBER NOT FOUND
         B     ALIASDIR                 08-I/O ERROR
         SPACE 3
ALIAS1   DS    0H
         TM    DIRFLAG,X'80'            IS EXISTING MODULE AN ALIAS?
         BZ    ALIAS2                   NO
         SPACE
         MESSAGE MSGNREAL               YES, ERROR
         B     ALIASEND
         EJECT
ALIAS2   DS    0H
         MVC   DIRNAME,MEMBER2          SET ALIAS NAME
         OI    DIRFLAG,X'80'            SET ALIAS FLAG
         SPACE
         TM    FLAGS,FLOADMOD           IS THIS A LOAD MODULE?
         BZ    ALIAS4                   NO
         SPACE
         TM    DIRATTR,ATTRSCAT         SCATTER LOADED?
         BZ    ALIAS2A                  NO, OK
         MESSAGE MSGASCAT               COMPLAIN
         B     ALIASEND                 EXIT SUB COMMAND
         SPACE
MSGASCAT MSG   ' ALIAS NOT SUPPORTED FOR SCATTER LOAD MODULES'
         SPACE
ALIAS2A  DS    0H
         XC    DIRAPF2,DIRAPF2          ZERO ALIAS APF DATA
         TM    DIRATTR2,X'88'           VS LKED & APF DATA PRES?
         BZ    ALIAS2Z                  NO, SKIP THE FOLLOWING
         SPACE
         LA    R2,DIRAPF                POINT TO APF INFO
         TM    DIRATTR2,X'10'           SSI PRESENT?
         BZ    ALIAS2B
         SPACE
         LA    R2,1(,R2)                ROUND TO 1/2 WD
         N     R2,=F'-2'                FFFFFFFE MASK
         LA    R2,4(,R2)                POINT TO APF DATA
         MESSAGE MSGSSIDR               TELL SSI DROPPED
         SPACE
ALIAS2B  NI    DIRATTR2,255-X'10'       TURN OFF SSI INDICATOR
         CLI   0(R2),1                  IS SSI LENGTH GOOD
         BE    ALIAS2D
         SPACE
ALIAS2C  MESSAGE MSGAABAD
         B     ALIASEND
         SPACE
MSGAABAD MSG   ' BAD APF INFORMATION FORMAT - REQUEST NOT PERFORMED'
MSGSSIDR MSG   ' SSI INFORMATION PRESENT - LEFT ONLY IN PRIMARY NAME '
         SPACE
ALIAS2D  MVC   DIRAPF2+1(1),1(R2)       MOVE IN DATA
         MVI   DIRAPF2,1                AND SET PROPER LENGTH
         SPACE
ALIAS2Z  DS    0H
         MVC   DIRREAL,MEMBER1          REAL MODULE NAME
         MVC   DIREP,DIRENTRY           REAL MODULE ENTRY ADDRESS
         NI    DIRFLAG,X'E0'            SET DIRECTORY ENTRY LENGTH
         OI    DIRFLAG,(DIREND2-DIRUSER+1)/2
         SPACE
ALIAS3   DS    0H
         BAL   R14,READCESD             LOCATE POSSIBLE EXTERNAL NAME
         B     ALIAS4                   NOT FOUND, MUST BE PSUEDO ENTRY
         SPACE
         LTR   R1,R1                    ENTRY ADDRESS ZERO?
         BZ    ALIAS3A                  ES
         NI    DIRATTR+1,255-ATTREP0    NO, INSURE ATTR FLAG OFF
ALIAS3A  DS    0H
         ST    R1,0(,R13)               MOVE ENTRY ADDRESS FOR
         MVC   DIRENTRY,1(R13)          ALIAS TO DIRECTORY ENTRY
         SPACE 2
ALIAS4   DS    0H
         SPACE
         BAL   R14,OPENSTOW             OPEN OUTPUT DCB
         BZ    ALIASEND                 EXIT IF DCB NOT OPENED
         SPACE
         STOW  STOWDCB,DIRNAME,A        ISSUE STOW TO ADD ALIAS ENTRY
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE 2
         B     ALIAS5                   00-SUCCESSFUL
         B     ALIASDUP                 04-MEMBER ALREADY EXISTS
         B     ALIASERR                 08-INVALID RETURN FROM STOW
         B     ALIASFUL                 12-DIRECTORY IS FULL
         B     ALIASDIR                 16-I/O ERROR IN DIRECTORY
         SPACE
         LTORG
         SPACE 2
ALIAS5   DS    0H
         MESSAGE MSGALIAS               INDICATE ALIAS ASSIGNED
         SPACE
         TM    FLAGS,FLOADMOD           MUST WE GIVE ENTRY POINT?
         BZ    ALIASEND                 NO
         SPACE
         MVC   MSGTEXT1,MSGENTRY        BUILD MESSAGE FOR ENTRY POINT
         LH    R15,MSGTEXT1             LENGTH OF MSG
         LA    R14,MSGTEXT1(R15)
         LA    R15,6(,R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRENTRY
         MVI   DOUBLE+3,X'04'
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         SPACE
         MESSAGE MSGTEXT1               INDICATE ENTRY POINT FOR ALIAS
         B     ALIASEND
         SPACE 3
ALIASFUL DS    0H
         MESSAGE MSGFULL                INDICATE DIRECTORY FULL
         B     ALIASEND
         SPACE 2
ALIASDUP DS    0H
         MESSAGE MSGEXIST               INDICATE MEMBER EXISTS
         B     ALIASEND
         SPACE 2
ALIASDIR DS    0H
         MESSAGE MSGIOERR               I/O ERROR IN DIRECTORY
         B     ALIASEND
         SPACE 2
ALIASNON DS    0H
         MESSAGE MSGNONE                MEMBER DOES NOT EXIST
         B     ALIASEND
         SPACE 2
ALIASERR DS    0H
         MESSAGE MSGSTOW8               UNUSUAL RETURN CODE FROM STOW
         SPACE 2
ALIASEND DS    0H
         EXIT  COMMAND
         EJECT
*
*
*        SCRATCH SUBCOMMAND
*
*
         SPACE 2
SCRATCH  ENTER COMMAND,PDL=PDLDELET     ESTABLISH SUBCOMMAND
         SPACE
         BAL   R14,OPENSTOW             OPEN STOW DCB
         BZ    SCRATCHE                 EXIT IF DCB OPEN FAILED
         SPACE
         STOW  STOWDCB,MEMBER1,D        ISSUE STOW W/ DELETE OPTION
         SPACE 2
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE 2
         B     SCRATCH1                 00-SUCESSFUL
         B     SCRERROR                 04-SHOULD NOT OCCUR
         B     SCRNONE                  08-MEMBER DOES NOT EXIST
         B     SCRBAD                   12-SHOULD NOT OCCUR
         B     SCRDIRER                 16-I/O ERROR IN DIRECTORY
         EJECT
SCRATCH1 DS    0H
         MESSAGE MSGGONE                INDICATE MEMBER SCRATCHED
         B     SCRATCHE
         SPACE 2
SCRDIRER DS    0H
         MESSAGE MSGIOERR               I/O ERROR IN DIRECTORY
         B     SCRATCHE
         SPACE 2
SCRNONE  DS    0H
         MESSAGE MSGNONE                MEMBER DOES NOT EXIST
         B     SCRATCHE
         SPACE 2
SCRERROR DS    0H
         MESSAGE MSGSTOW4               ERROR RETURN FROM STOW
         B       SCRATCHE
SCRBAD   DS    0H
         MESSAGE MSGSTOWC               ERROR RETURN FROM STOW
         SPACE 3
SCRATCHE EXIT  COMMAND
         EJECT
*
*
*        DISPLAY SUBCOMMAND
*
*
         SPACE 2
DISPLAY  ENTER COMMAND,PDL=PDLDSPLY     ESTABLISH SUBCOMMAND
         TM    FLAGS,FMEMBER1+FMEMBER2  DISPLAY RANGE SPECIFIED?
         BNO   DISPLAY0                 NO
         LH    R14,LMEMBER1             LENGTH OF STARTING NAME
         LH    R15,LMEMBER2             LENGTH OF ENDING NAME
         CR    R14,R15                  DETERMINE MINIMUM NAME LENGTH
         BNH   DSPCHECK
         LR    R14,R15
DSPCHECK DS    0H
         SPACE
         CLC   MEMBER1(0),MEMBER2
         EX    R14,*-6                  INSURE RANGE IS VALID
         BNH   DISPLAY0                 YES, FIRST NAME NOT HIGHER
         SPACE
         MESSAGE MSGRANGE               INDICATE INVALID RANGE
         B     DISPLAYE
         SPACE 2
DISPLAY0 DS    0H
         OI    FLAGS+1,F1STREAD+FATTN
         NI    FLAGS+1,255-FEXIST-FLINESET-FRANGE
         EJECT
DISPLAY1 DS    0H
         BALR  R3,0                     RETURN ADDRESS
         LH    R4,LINESIZE              GET TERMINAL LINE SIZE
         LA    R0,11
         SR    R4,R0
         LA    R4,MSGTEXT1+3(R4)        END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4            START OF LINE
         SPACE
         CLEAR MSGTEXT1
         SPACE 2
DISPLAY2 DS    0H
         BAL   R14,READDIR              GET NEXT DIRECTORY ENTRY
         B     DISPLAY6                 LAST DIRECTORY BLOCK PROCESSED
         B     DISPLAY6                 LAST MEMBER IN DIRECTORY
         SPACE 2
         OI    FLAGS+1,FEXIST           MEMBER EXISTS FLAG
         TM    FLAGS,FMEMBER1           START ENTRY SPECIFIED?
         BZ    DISPLAY3                 NO
         SPACE
         LH    R15,LMEMBER1             LENGTH-1 OF MEMBER NAME
         CLC   MEMBER1(0),MEMNAME
         EX    R15,*-6
         BH    DISPLAY2                 NOT WANTED, GET NEXT
         XI    FLAGS,FMEMBER1
         SPACE 2
DISPLAY3 DS    0H
         TM    FLAGS,FMEMBER2           LAST ENTRY SPECIFIED?
         BZ    DISPLAY4                 NO
         SPACE
         LH    R15,LMEMBER2
         CLC   MEMBER2(0),MEMNAME       THIS PAST END?
         EX    R15,*-6
         BL    DISPLAY6                 YES, END OF DISPLAY
         SPACE
DISPLAY4 DS    0H
         TRT   DIRNAME(8),TRTMN         CHECK FOR UNPRINTABLE
         BZ    DISPLAY8                 SKIP IF ALL PRINTABLE
         TM    FLAGS+1,FLINESET         LINE IN PROGRESS?
         BZ    DISPLAY9                 NO, SKIP THE PUTLINE
         LA    R1,MSGTEXT1              COMPUTE LENGTH
         SR    R5,R1
         SLL   R5,16                    PUT IN UPPER HALF WD
         ST    R5,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=DISPLAYE
         NI    FLAGS+1,255-FLINESET
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+4            START OF LINE
         SPACE 2
DISPLAY9 DS    0H                       PRINT IN HEX ROUTINE
         UNPK  0(9,R5),DIRNAME(5)       DO 1ST HALF OF NAME
         UNPK  8(9,R5),DIRNAME+4(5)     DO 2ND HALF OF NAME
         TR    0(16,R5),TRTABLE         FINISH XLATION
         MVI   16(R5),C' '              CLEAN OUT GARBAGE
         TM    DIRFLAG,X'80'            ALIAS?
         BZ    *+10                     NO, SKIP THE '-A' SET
         MVC   16(L'IDALIAS,R5),IDALIAS SET ALIAS
         MVI   19(R5),C'*'              CHAR FORM OF MEMBER NAME
         MVC   20(8,R5),DIRNAME
         MVI   28(R5),C'*'
         OI    FLAGS+1,FLINESET+FRANGE
         LA    R5,31(,R5)               SET LENGTH
         B     DISPLAY5                 DISPLAY LINE
         SPACE 2
DISPLAY8 DS    0H
         MVC   0(8,R5),DIRNAME
         TM    DIRFLAG,X'80'            ALIAS?
         BZ    *+10                     NO
         MVC   8(L'IDALIAS,R5),IDALIAS
         LA    R5,10+L'IDALIAS(R5)
         SPACE 2
DISPLAY7 DS    0H
         OI    FLAGS+1,FLINESET+FRANGE
         CR    R5,R4                    LINE FULL?
         BL    DISPLAY2                 NO, CONTINUE
         SPACE 2
DISPLAY5 DS    0H
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=DISPLAYE
         SPACE
         NI    FLAGS+1,255-FLINESET
         BR    R3                       RETURN
         SPACE 3
DISPLAY6 DS    0H
         LA    R3,DISPLAYE              EXIT ADDRESS
         TM    FLAGS+1,FLINESET         BUFFER IN PROGRESS?
         BO    DISPLAY5                 YES, OUTPUT IT
         SPACE 2
         TM    FLAGS+1,FRANGE           MEMBER IN RANGE?
         BO    DISPLAYE                 YES
         TM    FLAGS+1,FEXIST           NO, BUT ANY IN DIRECTORY?
         LA    R1,MSGEMPTY
         BZ    *+8                      NO
         LA    R1,MSGNODSP              YES, THEN NONE IN RANGE
         SPACE
         MESSAGE (R1)
         SPACE 2
DISPLAYE DS    0H
         EXIT  COMMAND
         EJECT
*
*
*        ATTRIBUTE SUBCOMMAND
*
*
         SPACE 2
ATTR     ENTER COMMAND,PDL=PDLATTR      ESTABLISH SUBCOMMAND
         MVC   DIRNAME,MEMBER1          MEMBER NAME FOR BLDL
         CLEAR MSGTEXT1
         MVC   MSGTEXT1+5(23),=C'ATTRIBUTES FOR MEMBER -'
         MVC   MSGTEXT1+29(8),MEMBER1
         LA    R1,37                    MESSAGE LENGTH
         SLL   R1,16
         ST    R1,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE
         BLDL  INDCB,BLDLLIST           GET DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE 2
         B     ATTR1                    00-SUCESSFUL
         B     ATTRNONE                 04-MEMBER NOT FOUND
         B     ATTRIO                   08-I/O ERROR
         SPACE 3
ATTR1    DS    0H
         LA    R2,DIRUSER               LOAD START OF USER AREA (4 SSI)
         TM    FLAGS,FLOADMOD           LOAD MODULE LIBRARY?
         BZ    ATCK4SSI                 NO
         SPACE
         TM    FLAGS+2,FAPFON+FAPFOFF   CHANGE APF PROCESSING?
         BZ    ATTR1B                   NO, SKIP
         SPACE
         TM    DIRATTR2,X'88'           CAN CHANGE APF INFO?
         BNO   A1NOGO
         SPACE
         LA    R2,DIRAPF                FIND APF DATA
         TM    DIRATTR,X'04'            SCATTER?
         BNO   *+8
         LA    R2,8(,R2)                SCATTER SIZE
         SPACE
         TM    DIRFLAG,X'80'            ALIAS
         BNO   *+8                      ALIAS SIZE
         LA    R2,11(,R2)               ADD ALIAS LENGTH
         SPACE
         TM    DIRATTR2,X'10'           SSI?
         BNO   *+12
         LA    R2,5(,R2)                ADD 4 AND ROUND TO 1/2 WD
         N     R2,APFROUND
         SPACE
         CLI   0(R2),1                  LENGTH RIGHT?
         BNE   A1NOGO
         SPACE
         TM    FLAGS+2,FAPFON           TURN ON?
         MVI   1(R2),0
         BNO   *+8
         MVI   1(R2),1
         B     ATTR1D                   CONTINUE
         SPACE
A1NOGO   MESSAGE MSGADNC
         B     ATTR3                    CONTINUE W/DISPLAY
         SPACE 1
ATTR1B   TM    FLAGS,FATTR              CHANGE LOAD MODULE ATTRIBUTES?
         BZ    ATTR3                    NO
         SPACE
ATTR1D   XC    ATTRNO,FF
         OC    DIRATTR,ATTRYES
         NC    DIRATTR,ATTRNO
         SPACE 5
*
*        THE FOLLOWING CODE TAKES CARE OF THE RESTRICTION
*        IN CONTENTS SUPERVISION THAT REQUIRES THE 'REUS'
*        OPTION IN ADDITION TO THE 'RENT' OPTION IF A LOAD
*        MODULE IS TO BE ACCEPTED AS REENTERANT.
*
         SPACE 2
         TM    DIRATTR,ATTRRENT         WAS RE-ENTERANT SPECIFIED?
         BZ    *+8                      NO
         OI    DIRATTR,ATTRREUS         YES, ALSO FORCE REUSABLE
         SPACE 2
         BAL   R14,OPENSTOW             OPEN STOW DCB
         BZ    ATTREXIT                 IF DCB DID NOT OPEN
         SPACE
         MVC   DCBRELAD-IHADCB+STOWDCB,DIRTTR
         MVI   DCBRELAD-IHADCB+STOWDCB+3,0
         STOW  STOWDCB,DIRNAME,R
         SPACE 2
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE
         B     ATTR3                    00-SUCESSFUL
         B     ATTRERR                  04-SHOULD NOT OCCUR
         B     ATTRERR                  08-SHOULD NOT OCCUR
         B     ATTRFULL                 12-DIRECTORY FULL
         B     ATTRIO                   16-I/O ERROR
         SPACE 5
ATTR3    DS    0H
         MESSAGE MSGATTRS               ATTRIBUTES TITLE LINE
         SPACE 2
         CLEAR MSGTEXT1
         SPACE 2
         LA    R1,MSGTEXT1+3
         SR    R0,R0                    NO ATTRIBUTES FLAG
         SPACE
         LA    R14,TBLATTR-12           ATTRIBUTES TABLE
         LH    R2,DIRATTR               GET MODULE ATTRIBUTES
         SPACE 2
NEXTATTR DS    0H
         LA    R14,12(R14)
         CLI   0(R14),X'FF'             END OF TABLE?
         BE    LASTATTR                 YES
         SPACE
         LH    R15,0(R14)               GET ATTRIBUTE FLAGS
         SPACE
         TM    2(R14),X'80'             POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'                 MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'                 MASK FOR NEG (FOR BNZ) INST
         SPACE
         NR    R15,R2                   CHECK FOR ATTRIBUTE PRESENT
         BC    0,NEXTATTR
         EX    R3,*-4
         SPACE
         BALR  R0,0                     INDICATE ATTRIBUTE EXISTS
         LA    R3,X'7F'
         MVI   0(R1),C','               DELIMITER AFTER LAST ATTR
         IC    R15,2(R14)               LENGTH-1 OF ATTRIBUTE
         NR    R15,R3
         MVC   2(0,R1),3(R14)
         EX    R15,*-6
         LA    R1,3(R15,R1)             JUMP POINTER
         B     NEXTATTR
         SPACE 3
LASTATTR DS    0H
         LTR   R0,R0                    ANY ATTRIBUTES PRESENT?
         LA    R1,NONEMSG
         BZ    NOATTRS                  NO
         SPACE
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         LA    R1,MSGTEXT1
         SPACE
NOATTRS  DS    0H
         MESSAGE (R1)
         SPACE 2
         CLEAR MSGTEXT1
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE 1
* FORMAT NEW VS INFORMATION
         SPACE 1
         LA    R2,DIRAPF                POINT TO APF INFO,
*                                       WILL ADJUST IF NECESSARY
         TM    DIRATTR,X'04'            IS SCATTER FMT?
         BNO   *+8                      NO, SKIP ADD
         LA    R2,8(,R2)                ADD SCATTER SIZE
         SPACE
         TM    DIRFLAG,X'80'            ALIAS?
         BNO   *+8
         LA    R2,11(,R2)               ADD ALIAS LENGTH
         SPACE
         TM    DIRATTR2,X'80'           AOS LKED?
         BO    ATCKSSI                  YES, CONTINUE
         MESSAGE MSGNOVSL
         TM    DIRFLAG,X'80'            ALIAS?
         BZ    ATGOTSSI
         B     ATNOSSI
         SPACE
ATCKSSI  TM    DIRATTR2,X'10'           SSI PRESENT?
         BNO   ATNOSSI                  SKIP SSI PROC
         SPACE
ATGOTSSI LA    R2,1(,R2)                ROUND UP TO 1/2 WD
         N     R2,APFROUND
         SPACE
* R2 NOW POINTS TO SSI, FMT MSG AND CONTINUE
         SPACE
ATCK4SSI SR    R0,R0                    CK FOR ZERO
         C     R0,0(,R2)
         BE    ATNOSSI
         BCTR  R0,0                     CK FOR -1
         C     R0,0(,R2)
         BE    ATNOSSI
         SPACE
         MVC   MSGTEXT1+5(16),=C'SSI INFORMATION:'
         UNPK  MSGTEXT1+22(9),0(5,R2)   CVT TO HEX
         TR    MSGTEXT1+22(8),TRTABLE
         MVI   MSGTEXT1+30,C' '         CLEAR GARBAGE
         SPACE
         LA    R1,MSGTEXT1
         MESSAGE (R1)
         CLEAR MSGTEXT1
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE
         TM    FLAGS,FLOADMOD           IS LOAD MODULE LIB?
         BZ    ATTR2                    NO, SKIP THE FOLLOWING
         LA    R2,4(,R2)                ADD SSI SIZE
         SPACE
ATNOSSI  TM    DIRATTR2,X'80'           AOS LKED?
         BZ    ATTR2                    NO, CAN'T BE AUTH
         TM    DIRATTR2,X'08'           APFPRES?
         BO    ATAPF
         MESSAGE MSGNOAPF
         B     ATTR2
         SPACE
ATAPF    CLI   0(R2),1                  CHECK APF DATA LEN
         BE    ATAPFLOK                 IF LEN OK
         MESSAGE MSGAPFLB
         B     ATTR2
         SPACE
ATAPFLOK CLI   1(R2),1                  CHECK APF DATA
         BE    ATAPFOK                  IS AUTH
         BH    ATAPF2B                  VALUE TOO BIG
         MESSAGE MSGNAUTH
         B     ATTR2
         SPACE
ATAPFOK  MESSAGE MSGYAUTH
         B     ATTR2
ATAPF2B  MESSAGE MSGAPF2B
         SPACE 2
ATTR2    DS    0H
         OI    FLAGS+1,FATTN
         NI    FLAGS+2,255-FREALMOD
         NI    FLAGS+1,255-FLINESET
         TM    DIRFLAG,X'80'            TRUE MODULE OR ALIAS?
         BO    ATTR4                    ALIAS
         OI    FLAGS+2,FREALMOD         TRUE NAME
         SPACE
ATTR4    DS    0H
         MVC   SAVETTR,DIRTTR           TTR OF REAL MODULE
         MVC   MSGTEXT1,MSGNOMOD
         OI    FLAGS+1,F1STREAD
         SPACE 2
ATTR5    DS    0H
         BAL   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     ATTRLAST                 END OF DIRECTORY
         B     ATTRLAST                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   SAVETTR,DIRTTR           WANTED MODULE?
         BNE   ATTR5                    NO
         SPACE
         TM    FLAGS+2,FREALMOD         LOOK FOR ALL ALIASES?
         BZ    ATTR6                    NO
         TM    DIRFLAG,X'80'            YES, THIS ONE OF THEM?
         BZ    ATTR5                    NO
         SPACE 2
         TM    FLAGS+1,FLINESET         LINE IN PROGRESS?
         BO    ATTR5A                   YES
         MESSAGE MSGOTHER
         SPACE 2
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+3
         OI    FLAGS+1,FLINESET
         SPACE 2
ATTR5A   DS    0H
         LA    R2,DIRNAME+7
ATTR5B   DS    0H
         CLI   0(R2),C' '
         BNE   ATTR5C
         BCT   R2,ATTR5B
ATTR5C   DS    0H
         LA    R0,DIRNAME
         SR    R2,R0
         BNM   ATTR5D
         SR    R2,R2
ATTR5D   DS    0H
         LA    R15,0(R5,R2)
         LA    R1,MSGTEXT1
         SR    R15,R0
         CH    R15,LINESIZE             CURRENT LINE FULL?
         BNH   ATTR5E                   NO
         SLL   R15,16
         ST    R15,MSGTEXT1
         MESSAGE (R1)
         SPACE 2
         CLEAR MSGTEXT1
         LA    R5,MSGTEXT1+3
         SPACE 2
ATTR5E   DS    0H
         MVI   0(R5),C','
         MVC   2(,R5),DIRNAME
         EX    R2,*-6
         LA    R5,3(R2,R5)
         B     ATTR5
         EJECT
ATTR6    DS    0H
         TM    DIRFLAG,X'80'            THIS THE ALIAS ENTRY?
         BO    ATTR5                    YES, CONTINUE ON
         MVC   MSGTEXT1,MSGREAL
         LH    R1,MSGTEXT1
         LA    R0,8(R1)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         MVC   0(8,R1),DIRNAME
         SPACE
         LA    R1,MSGTEXT1
ATTR7    DS    0H
         MESSAGE (R1)
         B     ATTREXIT
         SPACE 3
ATTRLAST DS    0H
         TM    FLAGS+2,FREALMOD ALIAS OR TRUE MODULE?
         LA    R1,NONEMSG               ERROR MSG - TRUE NAME NOT FOUND
         BZ    ATTR7
         SPACE
         TM    FLAGS+1,FLINESET         LINE IN PROGRESS?
         BZ    ATTREXIT                 NO, EXIT
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
         B     ATTR7
         EJECT
ATTRNONE DS    0H
         MESSAGE MSGNONE
         B     ATTREXIT
         SPACE 2
ATTRIO   DS    0H
         MESSAGE MSGIOERR
         B     ATTREXIT
         SPACE 2
ATTRFULL DS    0H
         MESSAGE MSGFULL
         B     ATTREXIT
         SPACE 2
ATTRERR  DS    0H
         MESSAGE MSGSTOWC
         B     ATTREXIT
         SPACE 2
APFROUND DC    F'-2'                    FFFFFFFE MASK
         PRINT NOGEN
MSGYAUTH MSG   ' APF AUTHORIZED'
MSGNAUTH MSG   ' NOT APF AUTHORIZED'
MSGNOVSL MSG   ' NOT APF AUTHORIZED, NON VS LKED'
MSGNOAPF MSG   ' NOT APF AUTHORIZED, APF DATA MISSING'
MSGAPFLB MSG   ' NOT APF AUTHORIZED, APF DATA WRONG LENGTH'
MSGAPF2B MSG   ' APF AUTHORIZED, APF DATA VALUE  < 1'
MSGADNC  MSG   ' * APF DATA COULD NOT BE CHANGED *'
         SPACE
         PRINT GEN
         LTORG
         SPACE 2
ATTREXIT DS    0H
         EXIT  COMMAND
         EJECT
*
*
*        MAP SUBCOMMAND
*
*
         SPACE 2
MAP      ENTER COMMAND,PDL=PDLMAP       ESTABLISH SUBCOMMAND
         TM    FLAGS,FLOADMOD           PROCESSING LOAD LIBRARY?
         BO    MAP1                     YES
         SPACE
         LA    R1,MSGNLOAD              NOT LOAD LIBRARY
         SPACE
MAP0     DS    0H
         MESSAGE (R1)
         B     MAPEND
         SPACE 2
MAP1     DS    0H
         OI    FLAGS+1,FATTN            ALLOW INTERRUPTIONS
         MVC   DIRNAME,MEMBER1          MEMBER NAME TO BLDL LIST
         BLDL  INDCB,BLDLLIST           LOCATE DIRECTORY ENTRY
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE
         B     MAP2                     00-SUCESSFUL
         B     MAPNONE                  04-MEMBER NOT FOUND
         B     MAPIO                    08-I/O ERROR
         SPACE 2
MAP2     DS    0H
         OI    FLAGS+1,FALLESD          INDICATE LIST REQUEST
         SPACE 2
         BAL   R14,READCESD             FORMAT THE CESD DATA
         B     MAPNOESD
         SPACE 2
         LA    R4,ESDPTR                ESD TABLE CHAIN POINTER
         SR    R5,R5                    CLEAR SEGTAB/ENTAB SWITCH
         USING ESDENTRY,R4              BASE FOR TABLE
         SPACE 2
MAPESD   DS    0H
         L     R4,ESDLINK
         LTR   R4,R4                    END OF CHAIN?
         BZ    MAPLAST                  YES
         CLEAR MSGTEXT1
         SPACE
         CLI   ESDTYPE,CODESD           THIS EXTERNAL DEFINITION?
         BE    MAPSD                    YES
         SPACE
         CLI   ESDTYPE,CODELR           THIS EXTERNAL REFERENCE?
         BE    MAPLR                    YES
         SPACE
         CLI   ESDTYPE,CODEPC           THIS PRIVATE CODE?
         BE    MAPPC                    YES
         SPACE
*        MUST BE SEGTAB OR ENTAB ENTRY
         SPACE
         LTR   R5,R5                    SEGTAB PROCESSED?
         MVC   MSGTEXT1+4(8),$SEGTAB
         BZ    MAPSEGTB
         MVC   MSGTEXT1+4(8),$ENTAB
MAPSEGTB DS    0H
         BALR  R5,0                     SEGTAB PROCESSED CODE
         B     MAPSD1
         SPACE 2
MAPPC    DS    0H
MAPSD    DS    0H
         MVC   MSGTEXT1+4(8),ESDNAME
         SPACE
MAPSD1   DS    0H
         TM    DIRATTR,ATTROVLY         OVERLAY PROGRAM?
         BZ    MAPNOVLY                 NO
         SPACE
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
MAPNOVLY DS    0H
         SPACE 3
         MVC   DOUBLE(3),ESDADDR
         MVI   DOUBLE+3,X'04'
         UNPK  MSGTEXT1+14(7),DOUBLE(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVC   DOUBLE(3),ESDLEN
         UNPK  MSGTEXT1+22(7),DOUBLE(4)
         TR    MSGTEXT1+22(6),TRTABLE
         LA    R1,32
         B     MAPPRINT
         SPACE 3
MAPLR    DS    0H
         MVC   MSGTEXT1+30(8),ESDNAME
         MVC   DOUBLE(3),ESDADDR
         MVI   DOUBLE+3,X'04'
         UNPK  MSGTEXT1+40(7),DOUBLE(4)
         TR    MSGTEXT1+40(6),TRTABLE
         LA    R1,46
         SPACE
MAPPRINT DS    0H
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE
MSGPRINT DS    0H
        $PUTLINE MSGTEXT1,ATTN=MAPEND
         SPACE 3
         B     MAPESD
         SPACE 2
MAPLAST  DS    0H
         BAL   R14,FREEESD
         SPACE 3
         CLEAR MSGTEXT1
         LA    R0,6
         SLL   R0,16
         ST    R0,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=MAPEND
         SPACE 3
MAPNOESD DS    0H
         MVC   MSGTEXT1,MSGENTRY
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         LA    R15,6(,R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRENTRY
         MVI   DOUBLE+3,X'04'
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         SPACE 2
         NI    FLAGS+1,255-FATTN
         SPACE
         MESSAGE MSGTEXT1
         SPACE 3
         MVC   MSGTEXT1,MSGLEN
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         LA    R15,6(R15)
         STH   R15,MSGTEXT1
         SPACE
         MVC   DOUBLE(3),DIRCORE
         UNPK  0(7,R14),DOUBLE(4)
         TR    0(6,R14),TRTABLE
         SPACE
         TM    DIRFLAG,X'80'            IS MODULE AN ALIAS?
         BZ    MAPREAL                  NO, NO ALIAS INFORMATION
         SPACE
         MESSAGE MSGTEXT1
         SPACE
         MVC   SAVETTR,DIRTTR           TTR OF REAL MODULE
         MVC   MSGTEXT1,MSGNOMOD
         OI    FLAGS+1,F1STREAD
         SPACE 2
MAPALIAS DS    0H
         BAL   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     MAPREAL                  END OF DIRECTORY
         B     MAPREAL                  LAST MEMBER PROCESSED
         SPACE 2
         TM    DIRFLAG,X'80'            THIS MODULE AN ALIAS?
         BO    MAPALIAS                 YES, IGNORE
         SPACE
         CLC   SAVETTR,DIRTTR           CORRECT MODULE?
         BNE   MAPALIAS                 NO
         SPACE
         MVC   MSGTEXT1,MSGREAL
         LH    R1,MSGTEXT1
         LA    R0,8(,R1)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         MVC   0(8,R1),DIRNAME
         SPACE
MAPREAL  DS    0H
         MESSAGE MSGTEXT1
         B     MAPEND
         DROP  R4
         SPACE 3
MAPNONE  DS    0H
         MESSAGE MSGNONE
         B     MAPEND
         SPACE 2
MAPIO    DS    0H
         MESSAGE MSGIOERR
         SPACE 3
MAPEND   DS    0H
         EXIT  COMMAND
         EJECT
*
*
*        HISTORY SUBCOMMAND
*
*
         SPACE 2
HISTORY  ENTER COMMAND,PDL=PDLHIST      ESTABLISH SUBCOMMAND
         TM    FLAGS,FLOADMOD           PROCESSING LOAD LIBRARY?
         BO    HISTORY1                 YES
         SPACE
         LA    R1,MSGNLOAD              NOT LOAD LIBRARY
         B     HISTORY0
         SPACE
NOHIST   DS    0H
         LA    R1,MSGNHIST
         SPACE
HISTORY0 DS    0H
         MESSAGE (R1)
         B     EXITHIST
         SPACE 2
HISTORY1 DS    0H
         OI    FLAGS+1,FATTN            ALLOW INTERRUPTIONS
         MVC   DIRNAME,MEMBER1          MEMBER NAME TO BLDL LIST
         BLDL  INDCB,BLDLLIST           FIND THE MEMBER
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE
         B     HISTORY2                 00-SUCESSFUL
         B     HISTNONE                 04-MEMBER NOT FOUND
         B     HISTIOE                  08-I/O ERROR
         SPACE 2
HISTORY2 DS    0H
         OI    FLAGS+1,FALLESD          INDICATE BUILD ESD LIST
         SPACE 2
         BAL   R14,READCESD             FORMAT THE CESD DATA
         NOP   0                        NO ESD ENTRIES EXIST, CONTINUE
         SPACE 2
         BAL   R14,READIDR              FORMAT THE HISTORY DATA
         B     NOHIST                   HISTORY NOT AVAILABLE
         SPACE 3
         MVC   MSGTEXT1,MSGHIST1        TITLE MESSAGE
         LH    R1,MSGTEXT1
         LA    R0,L'MEMBER1(,R1)        ADJUST TOTAL MSG LENGTH
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         SPACE
         MVC   0(L'MEMBER1,R1),MEMBER1
         SPACE
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         SPACE 3
         MVC   MSGTEXT1,MSGHISTD        LAST LINKEDIT DATE MSG
         LH    R1,MSGTEXT1
         LA    R0,L'DATEMASK(R1)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         SPACE 2
         MVC   0(L'DATEMASK,R1),DATEMASK
         ED    0(L'DATEMASK,R1),LKEDDATE
         SPACE
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         SPACE 2
         L     R1,ESDPTR      ANY ESD ENTRIES?
         LTR   R1,R1
         BZ    EXITHIST                 NO, PROCESSING COMPLETED
         EJECT
*
*        FORMAT IMASPZAP IDR DATA ENTRIES
*
         SPACE 1
         LA    R2,ESDPTR                START OF ESD CHAIN
         USING ESDENTRY,R2
         OI    FLAGS+1,F1IDR
         SPACE
HISTZAP  DS    0H
         L     R2,ESDLINK               NEXT ESD ENTRY
         LTR   R2,R2                    END OF CHAIN?
         BZ    HISTZAP0                 YES
         SPACE
         CLI   ESDTYPE,CODESD           CSECT ENTRY?
         BNE   HISTZAP                  NO, SKIP THIS
         SPACE
         LA    R3,IDRPTR                SCAN IDR CHAIN
         USING IDRENTRY,R3
         SPACE
HISTZAP1 DS    0H
         L     R3,IDRLINK
         LTR   R3,R3                    END OF CHAIN?
         BZ    HISTZAP                  YES, NEXT ESD ENTRY
         SPACE
         CLC   ESDID,IDRESDID           REQUESTED IDR RECORD?
         BNE   HISTZAP1                 NO
         CLI   IDRTYPE,IDRZAP           IMASPZAP ENTRY?
         BNE   HISTZAP1                 NO
         SPACE
         TM    FLAGS+1,F1IDR            FIRST IDR RECORD?
         BZ    HISTZAP2                 NO
         XI    FLAGS+1,F1IDR
        $PUTLINE MSGHISTZ,ATTN=EXITHIST IMASPZAP TITLE LINE
         SPACE
HISTZAP2 DS    0H
         CLEAR MSGTEXT1
         SPACE
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         SPACE
         LA    R0,L'ESDNAME+L'DATEMASK+L'IDRZDATA+12
         SLL   R0,16
         ST    R0,MSGTEXT1
        $PUTLINE MSGTEXT1,ATTN=EXITHIST PRINT IMASPZAP DATA LINE
         B     HISTZAP1                 NEXT IDR DATA RECORD
         EJECT
HISTZAP0 DS    0H
         SPACE 3
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE 2
         LA    R2,ESDPTR                ADDRESS OF ESD CHAIN
         OI    FLAGS+1,F1IDR
         SPACE
HISTUSER DS    0H
         L     R2,ESDLINK               NEXT ESD ENTRY
         LTR   R2,R2                    END OF CHAIN?
         BZ    HISTUSR0                 YES
         SPACE
         CLI   ESDTYPE,CODESD           CSECT ENTRY?
         BNE   HISTUSER                 NO, NEXT ESD ENTRY
         SPACE 2
         LA    R3,IDRPTR
         SPACE
HISTUSR1 DS    0H
         L     R3,IDRLINK               NEXT IDR RECORD
         LTR   R3,R3                    END OF CHAIN?
         BZ    HISTUSER                 YES, NEXT ESD ENTRY
         SPACE
         CLI   IDRTYPE,IDRUSER          USER IDR DATA RECORD?
         BNE   HISTUSR1                 NO
         CLC   ESDID,IDRESDID           WANTED ESD RECORD?
         BNE   HISTUSR1                 NO
         SPACE 2
         TM    FLAGS+1,F1IDR            TITLE LINE REQUIRED?
         BZ    HISTUSR2                 NO
         XI    FLAGS+1,F1IDR
         SPACE
        $PUTLINE MSGHISTU,ATTN=EXITHIST USER-SUPPLIED IDR TITLE
         SPACE 2
HISTUSR2 DS    0H
         CLEAR MSGTEXT1
         SPACE 2
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         IC    R1,IDRLDATA
         CLI   IDRLDATA,X'FF'           ZERO BYTES???
         BE    HISTUSRS                 YA, SKIP MOVE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(0),IDRDATA
         EX    R1,*-6
         B     *+6
HISTUSRS SR    R1,R1
         LA    R1,13+L'ESDNAME+L'DATEMASK(R1)
         SLL   R1,16
         ST    R1,MSGTEXT1
         SPACE 2
        $PUTLINE MSGTEXT1,ATTN=EXITHIST
         SPACE
         B     HISTUSR1
         SPACE 3
HISTNONE DS    0H
         MESSAGE MSGNONE                MEMBER DOES NOT EXIST
         B     EXITHIST
         SPACE 3
HISTIOE  DS    0H
         MESSAGE MSGIOERR               I/O ERROR IN DIRECTORY
         SPACE 3
HISTUSR0 DS    0H
EXITHIST DS    0H
         BAL   R14,FREEESD
         BAL   R14,FREEIDR
         SPACE 2
         NI    FLAGS+1,255-F1IDR
         SPACE 2
         EXIT  COMMAND
         SPACE 2
         DROP  R2,R3
         EJECT
*
*
*        IDR SCAN SUBROUTINE
*
*
         SPACE 3
READIDR  DS    0H
         SAVE  (14,12)                  SAVE REGISTERS
         SPACE 2
         SPACE
         MVC   0(3,R13),DIRTTR
         MVI   3(R13),0
         L     R0,0(,R13)               GET STARTING TTR OF MODULE
         LA    R2,MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         SPACE 3
         LR    R3,R13
         L     R15,CVT
         L     R15,CVTPCNVT(,R15)       TTR CONVERT
         BALR  R14,R15
         LR    R13,R3
         LM    14,12,12(13)
         SPACE 2
*
*        BUILD CHANNEL PGM TO READ IDR RECORDS
*
         SPACE 2
         LA    R0,IOBSEEK               ADDRESS OF SEEK FIELD
         LA    R1,CCWS
         LA    R2,IDRBUFF
         LA    R3,IOBSEEK
         XC    CCWS(LCCWS),CCWS
         ST    R0,CCWS
         ST    R1,CCWS+8
         ST    R2,CCWS+16
         ST    R3,CCWS+24
         OC    CCWS(LIDRCCWS),IDRCCWS
         SPACE 2
         MVI   IOBFLAG,X'42'
         SPACE
         LA    R2,IDRPTR
         USING IDRENTRY,R2
         NI    FLAGS+1,255-FIDR-F1IDR
         EJECT
IDREXCP  DS    0H
         MVI   IOECB,0
         EXCP  IOB                      START I/O
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0
         SPACE
         CLI   IOECB,X'42'              EXTENT VIOLATION?
         BNE   IDREXCP1                 NO
         BAL   R14,NEWEXTNT             YES, ADJUST FOR NEW EXTENT
IDREXCP1 DS    0H
         SPACE
         CLI   IOECB,X'7F'              SUCCESSFUL?
         BNE   LASTIDR                  NO, MUST BE LAST IDR DATA
         SPACE
         CLI   IDRBUFF,X'40'            TEST SYM RECORD?
         BE    IDREXCP                  YES, SKIP RECORD
         SPACE
         CLI   IDRBUFF,X'20'            CESD RECORD?
         BE    IDREXCP                  NO
         SPACE
         CLI   IDRBUFF,X'80'            IDR RECORD?
         BNE   LASTIDR                  NO
         SPACE
         LA    R3,IDRBUFF+3             START OF IDR DATA
         TM    FLAGS+2,FIDRHCON         CONTINUED HEADER?
         BZ    IDREXCP3                 NO, SKIP
         SPACE
         L     R15,IDROFSAV             PICK UP AMOUNT WE GOT
         LA    R14,IDRUPREF+1(R15)      TARGET OF MOVE
         LA    R1,IDRBUFF+3+5-1         FIND LENGTH BYTE
         SR    R1,R15                   BACK UP BY WHAT WE GOT
         SR    R4,R4                    ZERO FOR LENGTH PICKUP
         IC    R4,0(R1)                 PICK UP LENGTH
         LR    R1,R4                    SAVE LENGTH
         LA    R4,4(,R1)                GET TOTAL LENGTH OF REC
         MVI   IDRTYPE,IDRUSER          SET TYPE
         BCTR  R1,0                     DECREMENT
         STC   R1,IDRLDATA
         SR    R4,R15                   COMPUTE LENGTH TO MOVE
         MVC   0(0,R14),IDRBUFF+3
         EX    R4,*-6
         MVC   IDRESDID,IDRUPREF        SET ESDID
         NI    FLAGS+2,255-FIDRHCON     OFF FLAG
         LA    R3,1(R3,R4)              ADJUST R3 TO NEXT REC
         SPACE
IDREXCP3 TM    FLAGS+2,FIDRCONT         CONTINUED IDR REC?
         BZ    IDREXCP2                 NO, SKIP THE FOLLOWING
         SPACE
         SR    R5,R5                    FIND WHERE TO START MOVE
         IC    R5,IDRLDATA              DATA LENGTH
         LA    R5,IDRDATA+1(R5)         END OF DATA
         L     R15,IDROFSAV             LENGTH IN THIS RECORD
         SR    R5,R15                   PLACE TO START MOVE
         LA    R3,0(R15,R3)             MOVE R3 PAST CONTINUED PART
         BCTR  R15,0                    DEC FOR MOVE
         MVC   0(0,R5),IDRBUFF+3        DO THE MOVE
         EX    R15,*-6
         NI    FLAGS+2,X'FF'-FIDRCONT   OFF CONTINUED FLAG
         SPACE 2
IDREXCP2 SR    R5,R5
         IC    R5,IDRBUFF+1             BYTE COUNT THIS RECORD
         LA    R5,IDRBUFF(R5)           END OF BUFFER ADDRESS
         SPACE 2
         TM    IDRBUFF+2,X'80'          LAST RECORD OF LOAD MODULE?
         BZ    *+8                      NO
         OI    FLAGS+1,F1IDR            YES, SET STOP FLAG
         SPACE
         NI    IDRBUFF+2,X'0F'          CLEAR MISC FLAGS
         CLI   IDRBUFF+2,IDRZAP         IMASPZAP IDR RECORD?
         BE    ZAPIDR                   YES
         CLI   IDRBUFF+2,IDRLKED        LINKAGE EDITOR IDR RECORD?
         BE    LKEDIDR                  YES
         CLI   IDRBUFF+2,IDRUSER        USER-SUPPLIED IDR RECORD?
         BNE   NEXTIDR                  NO, NEXT RECORD
         EJECT
*        PROCESS USER-SUPPLIED DATA RECORDS
         SPACE
USERIDR  DS    0H
         BAL   R14,GETIDR               GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1                    ADDRESS OF NEW RECORD
         SPACE
         LR    R15,R5                   SEE IF SPANNED RECORD HEADER
         SR    R15,R3                   FIND BYTES LEFT IN BUFFER
         LR    R14,R15                  SAVE VALUE
         SH    R15,=H'5'                IS ROOM FOR HEADER?
         BNM   USERIDR0                 IF ROOM EXISTS
         OI    FLAGS+2,FIDRHCON         ON 'HDR CONT' FLAG
         MVC   IDRUPREF(0),0(R3)        MOVE IN PART OF HEADER
         EX    R14,*-6
         ST    R14,IDROFSAV             SAVE LENGTH FOR LATER
         B     NEXTIDR                  READ NEXT IDR
         SPACE
USERIDR0 MVI   IDRTYPE,IDRUSER
         MVC   IDRESDID,0(R3)           MOVE ESDID TO IDR AREA
         LA    R1,2(,R3)                YYDDD IDR RECORD CREATED
         BAL   R14,SETIDRDT
         SR    R15,R15
         IC    R15,5(,R3)               LENGTH OF USER DATA AREA
         LA    R4,6(,R15)               LENGTH OF THIS RECORD ENTRY
         MVI   IDRTYPE,IDRUSER          INDICATE USER-SUPPLIED ENTRY
         BCTR  R15,0
         BCTR  R4,0
         STC   R15,IDRLDATA
         SPACE
         MVC   IDRUPREF(0),0(R3)        MOVE DATA TO RECORD
         EX    R4,*-6
         LA    R4,1(,R4)
         SPACE 1
         BXLE  R3,R4,USERIDR
         SR    R3,R5                    ENTRY SPAN AN IDR BLOCK??
         BCTR  R3,0
         LTR   R3,R3                    TEST
         BNP   NEXTIDR                  NO SPAN
         OI    FLAGS+2,FIDRCONT         MARK FOR CONTINUE PROCESSING
         ST    R3,IDROFSAV              SAVE AMOUNT IN NEXT IDR BLK
         B     NEXTIDR
         SPACE
         LTORG
         EJECT
*        PROCESS IMASPZAP IDR RECORDS
         SPACE
ZAPIDR   DS    0H
         SR    R6,R6
         NI    0(R3),X'3F'
         IC    R6,0(R3)                 GET COUNT OF IMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R6,1(R6)                 JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  DS    0H
         BAL   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         SPACE
         MVC   IDRESDID,0(R3)           MOVE ESDID TO IDR RECORD
         SPACE
         LA    R1,2(,R3)                ADDRESS OF DATE OF RECORD
         BAL   R14,SETIDRDT
         SPACE
         MVC   IDRDATA,5(R3)            MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8               SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP           INDICATE IMASPZAP ENTRY
         SPACE
         LA    R3,13(,R3)               JUMP DATA ADDRESS
         SPACE
ZAPIDR1  DS    0H
         BCT   R6,ZAPIDR2               IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR                  READ NEXT IDR RECORD
         EJECT
*        LINKAGE EDITOR IDR RECORD PROCESSOR
         SPACE
LKEDIDR  DS    0H
         OI    FLAGS+1,FIDR
         LA    R1,12(,R3)               ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAL   R14,SETLKDTE
         SPACE 2
NEXTIDR  DS    0H
         TM    FLAGS+1,F1IDR            LAST IDR RECORD?
         BZ    IDREXCP                  NO
         EJECT
LASTIDR  DS    0H
         NI    FLAGS+2,X'FF'-FIDRCONT-FIDRHCON   OFF CONT FLAGS
         LA    R2,IDRPTR
SORTIDR  DS    0H
         L     R2,IDRLINK
         LTR   R2,R2
         BZ    SORTIDR1
         SPACE
         L     R1,IDRLINK
         SPACE
SORTIDR2 DS    0H
         LTR   R1,R1
         BZ    SORTIDR
         SPACE
         CLC   IDRDATE,IDRDATE-IDRENTRY(R1)
         BNL   SORTIDR3
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
SORTIDR3 DS    0H
         L     R1,IDRLINK-IDRENTRY(R1)
         B     SORTIDR2
SORTIDR1 DS    0H
         LM    14,12,12(13)
         TM    FLAGS+1,FIDR             ANY IDR RECORDS?
         BCR   8,R14                    NO
         B     4(,R14)                  YES
         EJECT
SETIDRDT DS    0H
         LA    R15,IDRDATE
         SPACE
SETLKDTE DS    0H
         ST    R14,R14SAVE
         MVC   DAYTABLE,DAYMONTH
         XC    DOUBLE,DOUBLE
         SR    R14,R14                  COMPUTE FOR LEAPYEAR
         IC    R14,0(R1)
         SLL   R14,4
         ST    R14,DOUBLE+4
         OI    DOUBLE+7,X'0F'
         LR    R14,R1
         CVB   R1,DOUBLE                GET YEAR IN BINARY
         SR    R0,R0
         D     R0,FOUR
         LTR   R0,R0                    THIS A LEAP YEAR?
         BNZ   *+8                      NO
         MVI   DAYTABLE+1,29            YES, FEB HAS 29 DAYS
         SPACE
         MVC   2(1,R15),0(R14)          YEAR RECORD CREATED
         MVC   DOUBLE+6(2),1(R14)
         CLI   DOUBLE+7,0               IS THE DATE BAD?
         BE    DAYSKIP                  YES, DON'T 0C7
         CVB   R1,DOUBLE                GET RELATIVE DAYS IN BINARY
         SPACE
         LA    R14,DAYTABLE
         SR    R0,R0
DAYLOOP  DS    0H
         IC    R0,0(,R14)               DAYS THIS MONTH
         SR    R1,R0                    WITHIN THIS MONTH?
         BNP   DAYNOW
         LA    R14,1(R14)
         B     DAYLOOP
DAYNOW   DS    0H
         AR    R1,R0                    GET DAY OF THE MONTH
         LA    R0,10
         MR    R0,R0
         CVD   R1,DOUBLE
         MVC   1(1,R15),DOUBLE+6
         LA    R1,DAYTABLE-1
         SR    R14,R1
         LA    R1,10
         MR    R0,R14
         CVD   R1,DOUBLE
         MVC   0(1,R15),DOUBLE+6
         SPACE
DAYSKIP  L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        CESD SCAN SUBROUTINE
*
*
         SPACE 3
READCESD DS    0H
         ST    R14,R14LINK
         SPACE
         TM    DIRATTR+1,ATTRNE         DOES MODULE HAVE ESD ENTRIES?
         MVC   MSGTEXT1,MSGNOESD
         BCR   1,R14                    NO, EXIT W/ ERROR
         STM   14,12,12(13)             SAVE REGISTERS
         SPACE 2
         MVC   0(3,R13),DIRTTR
         MVI   3(R13),0
         L     R0,0(,R13)               GET STARTING TTR OF MODULE
         LA    R2,MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         SPACE 3
         LR    R3,R13
         L     R15,CVT
         L     R15,CVTPCNVT(,R15)       TTR CONVERT
         BALR  R14,R15
         LR    R13,R3
         LM    14,12,12(13)
         SPACE 2
*
*        BUILD CHANNEL PGM TO READ CESD RECORDS
*
         SPACE 2
         LA    R0,IOBSEEK               ADDRESS OF SEEK FIELD
         LA    R1,CCWS
         LA    R2,ESDBUFF
         LA    R3,IOBSEEK
         XC    CCWS(LCCWS),CCWS
         ST    R0,CCWS
         ST    R1,CCWS+8
         ST    R2,CCWS+16
         ST    R3,CCWS+24
         OC    CCWS(LESDCCWS),ESDCCWS
         SPACE 2
         MVI   IOBFLAG,X'42'
         SPACE
         LA    R2,ESDPTR
         EJECT
ESDEXCP  DS    0H
         MVI   IOECB,0
         EXCP  IOB                      START I/O
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0
         SPACE
         CLI   IOECB,X'42'              EXTENT VIOLATION?
         BNE   ESDEXCP1                 NO
         BAL   R14,NEWEXTNT             YES, ADJUST FOR NEW EXTENT
ESDEXCP1 DS    0H
         SPACE
         CLI   IOECB,X'7F'              SUCESSFUL?
         BNE   ESDLAST                  NO, MUST BE LAST ESD DATA
         SPACE
         CLI   ESDBUFF,X'40'            TEST SYM RECORD?
         BE    ESDEXCP                  YES, SKIP RECORD
         SPACE
         CLI   ESDBUFF,X'20'            CESD RECORD?
         BNE   ESDLAST                  NO
         SPACE
         LH    R6,ESDBUFF+4             RELATIVE # OF 1ST ESD ID
         LA    R3,ESDBUFF+8             START OF ESD DATA
         LH    R5,ESDBUFF+6             LENGTH OF DATA IN BUFFER
         AR    R5,R3
         LA    R4,16                    LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
ESDSCAN  DS    0H
         USING ESDNAME,R3
         IC    R0,ESDTYPE
         LA    R1,CODESEG               CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         TM    FLAGS+1,FALLESD          SEARCH REQUEST?
         BZ    ESDMATCH                 YES, SCAN ESD ENTRIES
         CR    R0,R1                    THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1                  YES
         SPACE
         CLI   ESDTYPE,CODESD           SD ENTRY?
         BE    ESDUSE                   YES
         SPACE
         CLI   ESDTYPE,CODELR           LR ENTRY?
         BE    ESDUSE
         SPACE
         CLI   ESDTYPE,CODEPC           PRIVATE ENTRY?
         BE    ESDUSEPC                 YES
         SPACE 2
NEXTESD  DS    0H
         LA    R6,1(,R6)
         BXLE  R3,R4,ESDSCAN
         SPACE
         B     ESDEXCP                  GET NEXT ESD RECORD
         SPACE 3
ESDUSEPC DS    0H
         MVC   ESDNAME,$PRIVATE         IDENTIFY PRIVATE ENTRY
         B     ESDUSE
         SPACE
ESDUSE1  DS    0H
         STC   R0,ESDTYPE                RESTORE SEGTAB/ENTAB TYPE
ESDUSE   DS    0H
         BAL   R14,GETESD
         DROP  R3
         USING ESDENTRY,R2
         ST    R1,ESDLINK
         LR    R2,R1
         STH   R6,ESDID                 SAVE ESD ID
         MVC   ESDNAME(LENESD1),0(R3)   SAVE ESD ENTRY
         B     NEXTESD
         SPACE 2
ESDLAST  DS    0H
         OC    ESDPTR,ESDPTR
         MVC   MSGTEXT1,MSGNOESD
         L     R14,R14LINK
         BZ    ESDEXIT                  EXIT - NO ESD DATA AVAILABLE
         SPACE 2
         LA    R2,ESDPTR
         SPACE
ESDSORT  DS    0H
         L     R2,ESDLINK
         L     R1,ESDLINK
         LTR   R1,R1
         BZ    ESDSORT1                 LAST ESD ENTRY
         SPACE
ESDSORT2 DS    0H
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT #S
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE
ESDSWAP  DS    0H
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE
ESDSORT3 DS    0H
         L     R1,ESDLINK-ESDENTRY(,R1)
         LTR   R1,R1
         BNZ   ESDSORT2
         B     ESDSORT
         SPACE 3
ESDSORT1 DS    0H
ESDOUT   DS    0H
         L     R14,R14LINK
         LA    R14,4(,R14)              ADJUST EXIT ADDRESS
         NI    FLAGS+1,255-FALLESD
         SPACE 2
ESDEXIT  DS    0H
         LM    15,12,16(13)             RESTORE REGISTERS
         BR    R14                      EXIT
         DROP  R2
         SPACE 3
ESDMATCH DS    0H
         USING ESDNAME,R3
         CLI   ESDTYPE,CODESD           VALID EXTERNAL SYMBOL?
         BE    ESDCHECK                 YES
         CLI   ESDTYPE,CODELR           ANOTHER VALID ENTRY
         BNE   NEXTESD                  NO VALID, CONTINUE
         SPACE
ESDCHECK DS    0H
         CLC   ESDNAME,DIRNAME          REQUESTED NAME?
         BNE   NEXTESD                  NO
         SPACE
         CLI   ESDSEG#,1                IS SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD                  NO, CONTINUE
         SPACE
         L     R1,ESDADDR-1             GET SYMBOL OFFSET
         LA    R1,0(,R1)
         ST    R1,24(,13)               RETURN OFFSET IN REG 1
         B     ESDOUT
         EJECT
*
*
*        ESD TABLE SUBROUTINES
*
*
         SPACE 2
GETESD   DS    0H
         GETMAIN R,LV=LENESD
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 5
FREEESD  DS    0H
         ST    R14,R14SAVE
         SPACE
         L     R14,ESDPTR
         SPACE
FREEESD1 DS    0H
         LTR   R1,R14
         BZ    FREEESD2
         L     R14,ESDLINK-ESDENTRY(,R14)
         SPACE
         FREEMAIN R,LV=LENESD,A=(1)
         B     FREEESD1
         SPACE 2
FREEESD2 DS    0H
         XC    ESDPTR,ESDPTR
         L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        IDR TABLE SUBROUTINES
*
*
         SPACE 2
GETIDR   DS    0H
         OI    FLAGS+1,FIDR             IDR DATA USED
         GETMAIN R,LV=LENIDR
         XC    0(LENIDR,R1),0(R1)
         BR    R14
         SPACE 5
FREEIDR  DS    0H
         ST    R14,R14SAVE
         SPACE
         L     R14,IDRPTR
         SPACE
FREEIDR1 DS    0H
         LTR   R1,R14
         BZ    FREEIDR2
         L     R14,IDRLINK-IDRENTRY(,R14)
         SPACE
         FREEMAIN R,LV=LENIDR,A=(1)
         B     FREEIDR1
         SPACE 2
FREEIDR2 DS    0H
         XC    IDRPTR,IDRPTR
         L     R14,R14SAVE
         BR    R14
         EJECT
*
*        CHANGE SUBCOMMAND SPECIAL ENTRY
*
         SPACE 2
CHANGE   DS    0H
         OI    FLAGS,FRESTART           INDICATE RESTART
         SPACE 2
*
*        END SUBCOMMAND SPECIAL ENTRY
*
         SPACE 2
END      DS    0H
         L     R1,BUFFADDR              BUFFER ADDRESS
         LTR   R1,R1                    BUFFER PRESENT?
         BZ    FREENONE                 NO
         XC    BUFFADDR,BUFFADDR        CLEAR BUFFER ADDRESS
         LH    R0,DCBBLKSI-IHADCB+INDCB
         FREEMAIN R,LV=(0),A=(1)
         SPACE
FREENONE DS    0H
         MVI   PARMLIST,0
         MVI   PARMLIST+4,X'80'
         CLOSE (INDCB,,STOWDCB),MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGS,FRESTART           RESTART REQUEST?
         BO    RESTART                  YES
         SPACE 2
         MVC   DDNAME1,DCBDDNAM-IHADCB+INDCB DDNAME TO DE-ALLOCATE
         BAL   R14,DEALLOC              FREE THE DATA SET
         SPACE
         B     EXIT0
         EJECT
*
*
*        INPUT BUFFER RELEASE SUBROUTINE
*
*
         SPACE 2
FREEBUFF DS    0H
         TM    FLAGS,FCMD               BUFFER IN USE?
         BCR   1,R14                    YES, LEAVE IT
         STM   R14,R1,12(R13)
         L     R1,ADDRCBUF
         LTR   R1,R1
         BZ    NOBUFF                   NONE EXISTS
         SPACE
         LH    R0,0(,R1)
         LA    R15,1          SUBPOOL IS 1
         SLL   R15,24
         OR    R0,R15
         SPACE
         FREEMAIN R,LV=(0),A=(1)
         SPACE
NOBUFF   DS    0H
         LM    R14,R1,12(R13)
         XC    ADDRCBUF,ADDRCBUF
         BR    R14
         SPACE 3
*
*
*        DATA SET END-OF-EXTENT PSUEDO APPENDAGE
*
*
         SPACE 2
NEWEXTNT DS    0H
         SR    R0,R0
         SR    R15,R15
         L     R1,DCBDEBAD-IHADCB+INDCB
         IC    R15,MBBCCHHR             CURRENT EXTENT #
         IC    R0,DEBXTNT#(R1)          # EXTENTS IN DATA SET
         LA    R15,1(,R15)              ONE MORE DONE
         CR    R15,R0                   CHECK FOR END OF DATA SET
         BCR   2,R14                    YES, EXIT
         STC   R15,MBBCCHHR             NO, SAVE NEW EXTENT COUNT
         MVI   IOBSEEK+4,X'01'
         SLL   R15,4                    COMPUTE EXTENT OFFSET
         AR    R1,R15                   IN DEB
         MVC   IOBSEEK(4),DEBEXTNT+6(R1)
         MVI   IOECB,X'7F'
         BR    R14
         EJECT
*
*
*        DIRECTORY READ SUBROUTINE
*
*
         SPACE 2
READDIR  DS    0H
         ST    R14,R14SAVE
         SPACE
         TM    FLAGS+1,F1STREAD         IS THIS FIRST TIME?
         BZ    DEBLOCK                  NO
         SPACE
         XI    FLAGS+1,F1STREAD
         NI    FLAGS+2,255-FDIREND      END OF DIRECTORY TO COME
         SR    R0,R0                    CLEAR DIRECTORY TOTALS
         STH   R0,TOTUSED
         STH   R0,TOTBLOCK
         STH   R0,TOTMEMR               TOTAL REAL MEMBERS
         STH   R0,TOTMEMA               TOTAL ALIASES
         SPACE
*
*        INITIALIZE DIRECTORY READ SUBROUTINE
*
         SPACE
         LA    R0,X'100'                TTR OF FIRST BLOCK
         L     R1,DCBDEBAD-IHADCB+INDCB
         LA    R2,MBBCCHHR
         SPACE
         L     R15,CVT
         L     R15,CVTPCNVT(,R15)       TTR TO CCHHR CONVERT ROUTINE
         STM   14,12,12(R13)            SAVE REGISTERS
         LR    R3,R13
         BALR  R14,R15                  CONVERT TTR
         LR    R13,R3
         LM    14,12,12(R13)            RESTORE REGISTERS
         SPACE 2
         MVI   IOBFLAG,X'42'
         XC    CCWS(LCCWS),CCWS
         SPACE
         LA    R14,IOBSEEK              CREATE CHANNEL PGM TO READ
         LA    R15,CCWS                 DIRECTORY BLOCKS
         LA    R0,IOBSEEK
         LA    R1,DIRBLOCK
         SPACE
         ST    R14,CCWS                 ADDR FOR SEARCH CCW
         ST    R15,CCWS+8               ADDR FOR TIC CCW
         ST    R1,CCWS+16               ADDR FOR READ DATA
         ST    R0,CCWS+24               ADDR FOR READ COUNT CCW
         SPACE
         OC    CCWS(LDIRCCWS),DIRCCWS   MERGE IN REAL CCWS
         SPACE 3
READDIR1 DS    0H
         MVI   IOECB,0
         EXCP  IOB
         SPACE
         WAIT  ECB=IOECB
         MVI   DCBIFLGS-IHADCB+INDCB,0 CLEAR STATUS FLAGS
         SPACE
         CLI   IOECB,X'42'              EXTENT VIOLATION?
         BNE   *+8                      NO
         BAL   R14,NEWEXTNT             YES, ADJUST FOR NEW EXTENT
         SPACE
         CLI   IOECB,X'7F'              SUCESSFUL COMPLETION?
         BE    READDIR2                 YES, DELOCK DIRECTORY
         SR    R15,R15
         TM    IOBCSW+3,X'01'           END OF FILE?
         BO    READDIR4                 YES, EXIT AT END OF DIRECTORY
         SPACE
         B     READDIR5                 EXIT AS IF LAST MEMBER
         SPACE 3
READDIR2 DS    0H
         LH    R14,TOTBLOCK             JUMP TOTAL DIRECTORY
         LA    R14,1(,R14)              BLOCK COUNTER
         STH   R14,TOTBLOCK
         SPACE
         TM    FLAGS+2,FDIRSCAN+FDIREND END OF DIRECTORY?
         BO    READDIR1
         SPACE
         LA    R15,DIRBLOCK
         LA    R0,2
         LH    R1,DIRBLOCK
         LA    R1,DIRBLOCK-1(R1)
         STM   R15,R1,DIRPTRS
         SPACE
         LH    R14,TOTUSED
         LA    R14,1(,R14)
         STH   R14,TOTUSED              COUNT USED BLOCK
         SPACE 2
DEBLOCK  DS    0H
         LM    R15,R1,DIRPTRS
         BXH   R15,R0,READDIR1
         STM   R15,R1,DIRPTRS
         SPACE
         CLC   FF(8),0(R15)             LAST MEMBER?
         BE    READDIR3                 YES
         SPACE
         LA    R14,X'1F'
         IC    R0,11(,R15)
         NR    R0,R14
         AR    R0,R0
         LA    R14,12
         AR    R0,R14                   LENGTH OF CURRENT ENTRY
         STM   R15,R1,DIRPTRS           SAVE RESTART POINTERS
         SPACE
         MVC   DIRNAME(74),0(R15)       COPY ENTRY TO WORK AREA
         SPACE 2
         TM    11(R15),X'80'            ENTRY AN ALIAS?
         LA    R14,TOTMEMR              ASSUME NOT
         BZ    *+8                      NO, REAL MEMBER
         LA    R14,TOTMEMA              YES, ALIAS MEMBER
         SPACE
         LH    R15,0(,R14)
         LA    R15,1(,R15)
         STH   R15,0(,R14)
         SPACE 2
         TM    FLAGS+2,FDIRSCAN         SCAN TOTAL DIRECTORY?
         BO    DEBLOCK                  YES, CONTINUE THIS BLOCK
         SPACE 2
         LA    R15,8                    EXIT OFFSET
         B     READDIR4
         SPACE 2
READDIR3 DS    0H
         OI    FLAGS+2,FDIREND          END OF DIRECTORY
         TM    FLAGS+2,FDIRSCAN         STATISTICS SCAN?
         BO    READDIR1                 YES, CONTINUE IT
         SPACE
READDIR5 DS    0H
         LA    R15,4                    LAST MEMBER EXIT OFFSET
         SPACE 2
READDIR4 DS    0H
         L     R14,R14SAVE
         NI    FLAGS+2,255-FDIRSCAN-FDIREND
         B     0(R14,R15)
         EJECT
*
*
*        PARSE INTERFACE SUBROUTINE
*
*
         SPACE 3
GOPARSE  DS    0H
         ST    R1,ADDRPCL
         ST    R14,R14PARSE
         BAL   R14,FREEPDL
         SPACE 3
*
*        BUILD PARSE PARAMETER LIST AND INVOKE
*        IKJPARS TO ANALYZE COMMAND OPERANDS
*
         SPACE 3
         LA    R1,PARSELST              AREA FOR PARSE PARAMETERS
         USING PPL,R1                   BASE FOR PARSE PARAMETER LIST
         SPACE 2
         MVC   PPLUPT,ADDRUPT           PASS UPT ADDRESS
         MVC   PPLECT,ADDRECT           AND ECT ADDRESS
         MVC   PPLCBUF,ADDRCBUF         AND COMMAND BUFFER ADDR
         SPACE
         ST    WORKREG,PPLUWA           ALSO PASS WORK AREA ADDRESS
*                                       FOR VALIDITY CHECK EXITS
         SPACE
         LA    R0,ATTNECB               ECB FOR ATTN INTERRUPTS
         MVI   ATTNECB,0                CLEAR ECB
         ST    R0,PPLECB                PASSE TO PARSE
         SPACE
         LA    R0,ADDRPDL               PASS ADDR OF WORD WHERE PARSE
         ST    R0,PPLANS                RETURNS PDL ADDRESS
         SPACE
         MVC   PPLPCL,ADDRPCL           ADDRESS OF PARSE CONTROL LIST
         SPACE 3
         LINK  EPLOC=IKJPARS            INVOKE PARSE
         DROP  R1
         SPACE 2
         LA    R14,MAXPARSE             RETURN CODE LIMIT
         CR    R15,R14                  CHECK RETURN CODE WITHIN LIMITS
         BNL   PARSEBAD                 NO, ERROR
         SPACE
         B     *+4(R15)                 PROCESS RETURN CODE
         SPACE
PARSERET B     PARSEOK                  00- SUCESSFUL
         B     EXIT8                    04- PARSE UNABLE TO PROMPT
         B     PARSE1                   08- USER ENTERED ATTENTION
         B     PARSEBAD                 12- INVALID PARAMETERS
         B     PARSEBAD                 16- PARSE INTERNAL FAILURE
         B     EXIT8                    20 - VALIDITY CHECK ERROR
MAXPARSE EQU   *-PARSERET               MAXIMUM PARSE RETURN CODES
         SPACE 2
         SPACE 2
PARSEBAD DS    0H
         MVC   MSGTEXT2+4(L'MSGPARSE),MSGPARSE
         LA    R1,MSGTEXT2+4+L'MSGPARSE
         SPACE
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  0(2,R1),DOUBLE
         SPACE
         LA    R0,MSGTEXT2-2
         SR    R1,R0
         SLL   R1,16
         ST    R1,MSGTEXT2
         SPACE 2
         LA    R0,MSGTEXT2    PUT OUT 'COMMAND ERROR' MSG
         MESSAGE MSGCMDER,(R0)
         B     EXIT12
         SPACE 2
PARSE1   DS    0H
         SR    R15,R15        RETURN AT OFFSET 0
         B     PARSEOUT
         SPACE 2
PARSEOK  DS    0H
         LA    R15,4
         SPACE
PARSEOUT DS    0H
         L     R14,R14PARSE
         B     0(R15,R14)               EXIT FROM PARSE
         EJECT
PUTERROR DS    0H
GETERROR DS    0H
EXIT12   DS    0H
         LA    R15,12                   ERROR CODE 12
         B     RETURN
         SPACE
EXIT8    DS    0H
         LA    R15,8                    ERROR CODE 8
         B     RETURN
         SPACE
EXIT4    DS    0H
         LA    R15,4
         B     RETURN
         SPACE
EXIT0    DS    0H
         SR    R15,R15
         SPACE 3
RETURN   DS    0H
         LTR   R2,R15                   NORMAL EXIT?
         BZ    RETURN1                  YES
         SPACE 2
         LA    R1,PARMLIST              AREA FOR STACK PARM LIST
         USING IOPL,R1
         SPACE
         MVC   IOPLUPT,ADDRUPT
         MVC   IOPLECT,ADDRECT
         LA    R0,ATTNECB
         MVI   ATTNECB,0
         ST    R0,IOPLECB
         SPACE 2
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1))
         SPACE 3
         TCLEARQ INPUT                  CLEAR INPUT BUFFERS
         SPACE 3
RETURN1  DS    0H
         BAL   R14,FREEPDL              FREE THE PARSE STROAGE
         SPACE
         DELETE EPLOC=IKJPUTL
         SPACE
         DELETE EPLOC=IKJGETL
         SPACE
         DELETE EPLOC=IKJSCAN
         SPACE 3
         STAE  0,OV                     CANCEL STAE EXIT
         SPACE 3
         L     R13,ADDRSAVE             RESTORE FIRST SAVE AREA ADDR
         LR    R15,R2                   RESTORE RETURN CODE
         SPACE
         L     R0,CORESIZE
         SPACE
         EXIT  LV=(0)
         EJECT
*
*        STAE EXIT
*
         SPACE 3
STAEEXIT DS    0H
         USING *,R15                    TEMPORARY BASE
         SPACE
         CH    R0,STAECODE              IS WORK AREA PROVIDED?
         BE    STAE1                    NO
         SPACE
         L     WORKREG,0(R1)            YES, GET OUR WORK AREA ADDR
         B     STAE2
         SPACE 2
STAE1    DS    0H
         LR    WORKREG,R2               OUR WORK AREA ADDR
         SPACE 2
STAE2    DS    0H
         STM   0,15,STAEREGS
         SPACE 2
         LM    BASEREG1,BASEREG2,BASES
         DROP  R15
         SPACE
         LA    R13,STAESAVE
         SPACE 2
STAE4    LM    0,15,STAEREGS
         SR    R15,R15                  NO RETRY EXIT
         BR    R14
         SPACE
STAECODE DC    H'20'
         EJECT
*
*
*        STOW DCB OPEN SUBROUTINE
*
*
         SPACE 3
OPENSTOW DS    0H
         TM    STOWDCB+48,X'10'         DCB OPEN?
         BO    4(,R14)                  YES, EXIT
         MVI   OPENLIST,X'80'
         ST    R14,0(,R13)              SAVE RETURN ADDRESS
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)
         L     R14,0(,R13)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10' DID DCB OPEN?
         BO    4(,R14)                  YES, RETURN
         SPACE 2
         MESSAGE MSGNOPEN
         SPACE 2
         L     R14,0(,R13)
         BR    R14
         EJECT
*
*
*        MEMBER NAME VALIDITY CHECK SUBROUTINE
*
*
         SPACE 3
VERMEMBR ENTER VALCHECK
         TM    6(RPDL),X'80'            WAS MEMBER NAME SPECIFIED?
         BZ    SKIPTHIS                 NO
         SPACE 2
         L     R15,0(,RPDL)             ADDR OF MEMBER NAME
         LH    R14,4(,RPDL)             LENGTH OF NAME
         BCTR  R14,0                    EXECUTE LENGTH
         SPACE 2
         OI    FLAGS,FMEMBER2
         CLI   SUBNAME,C'D'             PAD WITH F'S?
         LM    R6,R7,FF                 LOAD HIGH VALUES PAD
         BE    *+8                      YES, SKIP BLANK PAD
         LM    R6,R7,BLANKS
         TM    FLAGS,FMEMBER1           FIRST MEMBER PROCESSED?
         LA    R1,MEMBER2
         LA    R2,LMEMBER2
         BO    MEM2ND                   YES
         XI    FLAGS,FMEMBER1+FMEMBER2
         LM    R6,R7,BLANKS             CHANGE PAD FOR 1ST
         LA    R1,MEMBER1
         LA    R2,LMEMBER1
MEM2ND   DS    0H
         SPACE
         MVI   0(R1),C' '               BLANK MEMBER AREA
         MVC   1(7,R1),0(R1)
         STH   R14,0(,R2)               STASH LENGTH
         SPACE
         LTR   R14,R14                  ONE BYTE MEMBER NAME?
         BZ    VERCHAR                  YES, CAN'T BE HEX
         CLC   =C'X''',0(R15)           BEGINS WITH  X' ?
         BE    VERHEX                   YES, IS HEX MEMBER NAME
         CLI   5(RPDL),8                TOO LONG?
         BNH   VERCHAR                  YES, IS CHARACTER NAME
         SPACE
VERBAD   LA    R15,4                    SET ERROR RETURN CODE
         B     VEREXIT                  GO TRY AGAIN
         SPACE
VERHEX   SR    R4,R4                    COUNT NIBBLES IN R4
         LA    R5,0(R14,R15)            POINT TO LAST CHAR
         CLI   0(R5),C''''              IS IT A QUOTE?
         BNE   VERBAD                   NO, INVALID SYNTAX
         BCTR  R5,0                     BACK UP ONE CHAR
         SR    R14,R14                  BXH INCREMENT
         BCTR  R14,0
         LA    R15,1(,R15)              POINT FRONT TO '
         CR    R5,R15                   NULL STRING?
         BNH   VERBAD                   YES, INVALID SYNTAX
         SPACE
VERHLOOP IC    R3,0(R5)                 GET CHAR IN REG 3
         CLI   0(R5),C'A'               A-F?
         BL    VERBAD                   LOWER THAN 'A' IS BAD
         CLI   0(R5),C'F'
         BNH   VERAF
         CLI   0(R5),C'0'               0-9?
         BL    VERBAD
         CLI   0(R5),C'9'
         BH    VERBAD
         SPACE
         B     *+8                      SKIP NEXT INS
VERAF    LA    R3,9(,R3)                UP LOW NIB FROM 1-5 TO A-F
         SLL   R3,28                    GET NIBBLE IN UPPER FOUR
         SRDL  R6,4                     REG 6 AND 7 ARE TARGET
         OR    R6,R3                    ADD NIBBLE TO ALREADY GOT
         LA    R4,1(,R4)                COUNT NIBBLES
         BXH   R5,R14,VERHLOOP          CHECK NEXT BYTE
         B     VERHLEND                 SKIP LITERAL POOL
         SPACE
         LTORG
         SPACE
VERHLEND ST    R4,DOUBLE                CHECK IF NUM CHAR IS ODD
         TM    DOUBLE+3,1               ODD NIBBLES?????
         BZ    *+8
         SRDL  R6,4                     IF ODD NIBBLES SPECIFIED
         SPACE 1
         STM   R6,R7,0(R1)              SAVE VALUE
         B     SKIPTHIS                 EXIT
         SPACE
VERCHAR  MVC   0(0,R1),0(R15)           PROCESS CHAR FORM OF MEM NAME
         EX    R14,*-6
         SPACE 2
SKIPTHIS DS    0H
         SR    R15,R15
VEREXIT  EXIT
         EJECT
*
*
*        NEW ATTRIBUTES VALIDITY CHECK SUBROUTINE
*
*
         SPACE 2
VERATTR  ENTER VALCHECK
VERATTR4 DS    0H
         L     R15,0(,RPDL)
         LH    R14,4(,RPDL)
         SPACE 2
         LA    R3,ATTRNO
         LA    R1,ATTRYES
         SPACE
         LA    R2,L'NO
         CR    R14,R2                   CAN 'NO' OPTION BE PREFIXED?
         BL    VERATTR1                 NO
         SPACE
         CLC   NO,0(R15)                'NO' OPTION PREFIXED?
         BNE   VERATTR1                 NO
         SPACE
         AR    R15,R2                   ADJUST ATTRIBUTE ADDRESS
         SR    R14,R2                   DECREMENT LENGTH
         BZ    VERATTR0                 LENGTH ERROR
         LA    R1,ATTRNO
         LA    R3,ATTRYES
         SPACE 2
VERATTR1 DS    0H
         BCTR  R14,0                    DECREMENT LENGTH FOR EXECUTE
         LA    R0,LATTR                 MAXIMUM ATTRIBUTE LENGTH
         CR    R14,R0
         BNL   VERATTR0                 INVALID LENGTH
         SPACE 2
         SR    R0,R0                    NO FIRST ATTRIBUTE
         SPACE
         LA    R2,ATTRTBL               TABLE OF CHANGEABLE ATTRIBUTES
         SPACE
LOOPATTR DS    0H
         CLC   0(0,R15),0(R2)
         EX    R14,*-6
         BE    VERATTR2
VERATTR5 DS    0H
         LA    R2,LATTRTBL(R2)
         CLI   0(R2),X'FF'
         BNE   LOOPATTR
         LTR   R2,R0                    PREVIOUS MATCH?
         BNZ   VERATTR3                 YES
         B     VERATTR0                 INVALID ATTRIBUTE
         SPACE
VERATTR2 DS    0H
         LTR   R0,R0                    FIRST MATCH
         BNZ   VERATTRX                 NO
         SPACE
         LR    R0,R2                    SAVE ADDRESS
         B     VERATTR5                 CONTINUE SCAN
         SPACE 2
VERATTR3 DS    0H
         TM    LATTR(R2),X'40'          AUTH REQUEST?
         BZ    VERATTRY
         SPACE
         LA    R0,ATTRNO                TURN ON OR OFF?
         CR    R0,R1
         BE    VERATOFF
         SPACE 1
         OI    FLAGS+2,FAPFON           TURN ON
         B     EXITATT0
VERATOFF OI    FLAGS+2,FAPFOFF
         B     EXITATT0
         SPACE
VERATTRY TM    LATTR(R2),X'80'          REVERSE MATCH?
         BZ    VERATTR6                 NO
         LR    R1,R3                    ALTERNATE BIT MASK
VERATTR6 DS    0H
         OC    0(L'ATTRYES,R1),LATTR+1(R2) SET ATTRIBUTE STATUS FLAGS
         SPACE 2
         OI    FLAGS,FATTR              INDICATE CHANGE ATTRIBUTES
         SPACE
EXITATT0 SR    R15,R15
         SPACE
EXITATTR DS    0H
         EXIT
         SPACE 3
VERATTR0 DS    0H
         MVC   MSGTEXT1,MSGBADAT
BADATTR  DS    0H
         LH    R1,MSGTEXT1
         LA    R0,1(R1,R14)
         STH   R0,MSGTEXT1
         LA    R1,MSGTEXT1(R1)
         MVC   0(0,R1),0(R15)
         EX    R14,*-6
         SPACE 2
         MESSAGE MSGTEXT1
         SPACE 2
         LA    R15,8
         B     EXITATTR
         SPACE 3
VERATTRX DS    0H
         MVC   MSGTEXT1,MSGDUPAT
         B     BADATTR
         EJECT
*
*
*        INPUT DATA SET VALIDITY CHECK EXIT
*
*
         SPACE 3
VERDSN   ENTER VALCHECK
         CLEAR DSNAME
         SPACE 2
         CLEAR PASSWORD
         SPACE 2
         CLEAR VOLUME
         SPACE 2
         CLEAR UNIT                                       MDW 03/80
         SPACE 2
         L     R15,0(,RPDL)             ADDR OF DSNAME
         LH    R14,4(,RPDL)             LENGTH OF DSNAME
         STH   R14,DSNLEN
         BCTR  R14,0
         MVC   DSNAME(0),0(R15)
         EX    R14,*-6                  MOVE DSNAME TO AREA
         SPACE 2
         TM    22(RPDL),X'80'           PASSWORD SUPPLIED?
         BZ    NOPASS                   NO
         SPACE
         L     R15,16(,RPDL)
         LH    R14,20(,RPDL)
         BCTR  R14,0
         MVC   PASSWORD(0),0(R15)
         EX    R14,*-6                  MOVE PASSWORD TO AREA
NOPASS   DS    0H
         SPACE 3
         OI    FLAGS+1,FQUOTE           ASSUME DSNAME QUOTED
         TM    6(RPDL),X'40'            IS DSNAME IN QUOTES?
         BO    NOUSERID                 YES
         XI    FLAGS+1,FQUOTE           NO, MUST QUALIFY NAME
NOUSERID DS    0H
         SPACE 3
         SR    R15,R15
         SPACE
         EXIT
         EJECT
*
*        VOLUME SERIAL VALIDITY CHECK
*
         SPACE 2
VERVOL   ENTER VALCHECK
         LH    R15,4(,RPDL)
         L     R1,0(,RPDL)
         BCTR  R15,0
         SPACE
         MVC   VOLUME(0),0(R1)
         EX    R15,*-6
         SPACE
         MVC   UNIT(8),SYSALLDA                     MDW 03/80
         SPACE 2
         SR    R15,R15
         EXIT
         EJECT
*
*        FREE DATA SET NO LONGER REQUIRED
*
         SPACE 2
DEALLOC  DS    0H
         ST    R14,R14LINK
         TM    FLAGS+2,FPERMDS          DATA SET PERMANENTLY ALLOCATED?
         BO    DEALLOC1                 YES, SKIP EXPLICIT DEALLOCATION
         SPACE
         LA    R1,DAIRBLK
         XC    DAIRBLK(LDAIRBLK),DAIRBLK
         USING DAPB18,R1
         MVC   DA18DDN,DDNAME           DE-ALLOCATE THE DATA SET
         LA    R0,X'18'
         STH   R0,DA18CD
         SPACE
         BAL   R14,CALLDAIR
         SPACE 2
DEALLOC1 DS    0H
         NI    FLAGS+2,255-FPERMDS      CLEAR DATA SET STATUS
         L     R14,R14LINK
         BR    R14
         EJECT
*
*        PARSE CLEANUP ROUTINE
*
         SPACE 3
FREEPDL  DS    0H
         SPACE
         ST    R14,R14SAVE
         SPACE
         L     R1,ADDRPDL               ADDR OF PDL
         LA    R1,0(R1)
         LTR   R1,R1                    DOES ONE EXIST?
         BCR   8,R14                    NO, EXIT
         SPACE
         IKJRLSA ADDRPDL                RELEASE THE STORAGE
         SPACE
         L     R14,R14SAVE
         XC    ADDRPDL,ADDRPDL
         BR    R14
         EJECT
*
*        DATA SET ALLOCATION SUBROUTINE
*
         SPACE 2
ALLOCATE DS    0H
         ST    R14,R14ALLOC             SAVE LINKAGE REGISTER
         NI    FLAGS+2,255-FPERMDS      CLEAR DATA SET STATUS
         SPACE 2
RETRY    DS    0H
         LA    R1,DAIRBLK
         XC    DAIRBLK(LDAIRBLK),DAIRBLK
         USING DAPB08,R1
         SPACE
         MVC   DA08PSWD,PASSWORD
         SPACE
         TM    FLAGS+1,FQUOTE           QUALIFY DATA SET NAME?
         BO    NQUALIFY                 NO
         OI    DA08CTL,DA08UID
NQUALIFY DS    0H
         SPACE 2
         LA    R0,X'08'                 DATA SET ALLOCATION CODE
         STH   R0,DA08CD
         SPACE
         LA    R0,DSNLEN                ADDR OF DSNAME FIELD
         ST    R0,DA08PDSN
         SPACE
         CLEAR DA08DDN
         SPACE
         MVC   DA08UNIT,UNIT            PUT IN UNIT        MDW 03/80
         SPACE
         MVC   DA08SER,VOLUME           VOLUME SPECIFIED
         SPACE
         CLEAR DA08MNM                  NO MEMBER NAME
         SPACE
         MVI   DA08DSP1,DA08SHR         DISP=(SHR,KEEP,KEEP)
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         SPACE
         BAL   R14,CALLDAIR
         LA    R1,DAIRBLK
         LTR   R15,R15                  SUCCESSFUL?
         BNZ   DAIRFAIL                 NO, ERROR
         SPACE 2
         MVC   DDNAME,DA08DDN           SAVE DDNAME
         SPACE
         TM    DA08DSO,X'02'            IS DATA SET DSORG=PO?
         BO    STATALOC                 YES, DETERMINE DATA SET STATUS
         SPACE 3
         MVC   DDNAME1,DDNAME           DDNAME TO DE-ALLOCATE
         BAL   R14,DEALLOC              FREE THE DATA SET
         SPACE 2
         MESSAGE MSGNLIB
         SPACE 2
RECOVERY DS    0H
ASKAGAIN DS    0H
        $PUTGET MSGRETRY,ATTN=NOTALLOC
         SPACE 2
         TM    FLAGS,FNULL              USER WANT ANOTHER DATA SET?
         BO    ASKAGAIN                 NULL REPLY, ASK AGAIN
         SPACE
         L     R1,MAINPCL               YES, INVOKE PARSE
         BAL   R14,GOPARSE
         B     NOTALLOC                 EXIT - PARSE FAILURE
         B     RETRY                    SUCCESSFUL - RETRY ALLOCATION
         SPACE 2
STATALOC DS    0H
         XC    DAIRBLK(LDAIRBLK),DAIRBLK
         USING DAPB00,R1                BASE FOR DSE STATUS REQUEST
         MVC   DA00DDN,DDNAME           DDNAME FOR STATUS REQUEST
         BAL   R14,CALLDAIR             GET DATA SET STATUS
         LTR   R15,R15                  SUCESSFUL?
         LA    R1,DAIRBLK               RESTORE ADDR OF PARM AREA
         LA    R15,4                    RETURN OFFSET
         BNZ   EXITALOC                 NO, JUST RETURN
         TM    DA00FLG,DA00PERM         DATA SET PERM ALLOCATED?
         BZ    EXITALOC                 NO, EXIT
         OI    FLAGS+2,FPERMDS          YES, INDICATE PERMANENT DATASET
         B     EXITALOC
         SPACE 2
NOTALLOC DS    0H
         SR    R15,R15                  ERROR EXIT - NOT ALLOCATED
         SPACE 3
EXITALOC DS    0H
         L     R14,R14ALLOC
         B     0(R14,R15)               EXIT FROM ALLOCATION
         EJECT
*
*        DYNAMIC ALLOCATION INVOCATION SUBROUTINE
*
         SPACE 3
CALLDAIR DS    0H
         ST    R14,R14SAVE              SAVE LINKAGE REGISTER
         SPACE 2
         LA    R1,PARMLIST              BUILD DYNAMIC ALLOC PARM LIST
         USING DAPL,R1
         SPACE
         MVC   DAPLUPT,ADDRUPT
         MVC   DAPLECT,ADDRECT
         MVC   DAPLPSCB,ADDRPSCB
         SPACE
         LA    R0,ATTNECB
         MVI   ATTNECB,0
         ST    R0,DAPLECB
         SPACE
         LA    R0,DAIRBLK
         ST    R0,DAPLDAPB
         SPACE 2
         LINK  EPLOC=IKJDAIR
         SPACE 2
         L     R14,R14SAVE              RESTORE LINKAGE REGISTER
         BR    R14                      EXIT
         EJECT
DAIRFAIL DS    0H
         NI    FLAGS+1,255-FNORETRY
         SPACE 2
         LA    R14,MAXDAIR
         CR    R15,R14                  CHECK DAIR RETURN CODE
         BH    DAIRBAD                  VERY BAD ERROR
         SPACE 3
         B     *(R15)                   DAIR ERROR PROCESSING
         B     DAIRBAD                  RC=04 INVALID DAIR PARAMETERS
         B     DAIRCAM                  RC=08 ERROR IN CATALOG MGMT
         B     DAIRDYN                  RC=12 ERROR IN DYNAMIC ALLOC
         B     DAIRFULL                 RC=16 NO SPACE IN TIOT
MAXDAIR  EQU   16
         SPACE 3
DAIRFULL LA    R1,MSGFULL               ERROR MSG
         LA    R0,MSGFULL1              SECOND LEVEL MSG
         B     ALLOCBAD
         EJECT
*
*        PROCESS ERRORS FROM CATALOG MANAGEMENT
*
         SPACE 2
DAIRCAM  DS    0H
         USING DAPB08,R1
         SPACE
         LH    R15,DA08CTRC             GET CAM RETURN CODE
         LA    R14,MAXCAMCD             MAX CAM ERROR CODE
         CR    R15,R14
         BH    DAIRCAMX                 IF DISASTER ERROR CODE
         SPACE
         B     *(R15)                   PROCESS ERROR CODE
         B     CAMERR4                  04 - VOLUME NOT MOUNTED
         B     CAMERR8                  08 - INVALID CATALOG STRUCTURE
         B     CAMERR12                 12 -     ''          ''
         B     CAMERR16                 16
         B     CAMERR20                 20
MAXCAMCD EQU   20
         SPACE 3
CAMERR4  LA    R1,MSGVOL                LAST PART OF 1ST LEVEL MSG
CAMERROR SR    R0,R0                    NO SECOND LEVEL MSG
         B     ALLOCERR                 PUT OUT ERROR MSG
         SPACE 2
CAMERR8  DS    0H
         LA    R1,MSGNOCAT              MSG 'DATA SET NOT CATALOGED'
         B     CAMERROR
CAMERR16 DS    0H
CAMERR12 LA    R1,MSGINDEX              INCONSISTENT CTLG STRUCTURE
         LA    R0,MSGDSFMT
         B     ALLOCERR
         SPACE 2
CAMERR20 DS    0H
         LA    R1,MSGSYNTX              INVALID DSNAME SYNTAX'
         LA    R0,MSGDSN
         B     ALLOCERR
         SPACE 3
DAIRCAMX DS    0H
         LH    R1,MSGCATER              LEN OF ERROR MSG
         SPACE
         MVC   MSGTEXT2(0),MSGCATER
         EX    R1,*-6
         SPACE
         LA    R1,MSGTEXT2(R1)
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  0(2,R1),DOUBLE
         LA    R1,2(R1)
         LA    R0,MSGTEXT2
         SR    R1,R0
         SLL   R1,16
         ST    R1,MSGTEXT2
         SPACE 2
         SR    R1,R1
         B     ALLOCBAD
         EJECT
*
*        PROCESS ERRORS FROM DYNAMIC ALLOCATION
*
         SPACE 3
DAIRDYN  DS    0H
         USING DAPB08,R1
         LH    R15,DA08DARC   RETURN CODE FROM DYNAMIC ALLOCATION
         SPACE
         LA    R14,DYNMSGS    START OF MSG LOOKUP TABLE
         LA    R1,DYNMSGE     LAST MSG IN TABLE
         SPACE 2
DAIRDYN1 CH    R15,0(R14)     THIS ERROR CODE?
         BE    DAIRDYN2       YES
         SPACE
         TM    2(R14),X'80'   IS SECOND LEVEL MSG THERE?
         LA    R0,6           LENGTH FOR SINGLE MSG
         BZ    DAIRDYN3       NO
         LA    R0,10          LENGTH FOR 2 MSG LEVELS
         SPACE 2
DAIRDYN3 BXLE  R14,R0,DAIRDYN1
         SPACE 2
DAIRBAD  DS    0H
         SR    R1,R1          DEFAULT TO FIRST LEVEL MSG
         LH    R14,MSGDYNER
         MVC   MSGTEXT2(0),MSGDYNER
         EX    R14,*-6        CREATE MSG - DYNAMIC ALLOCATION FAILURE
         STH   R15,DOUBLE
         LA    R14,MSGTEXT2(R14)
         UNPK  0(5,R14),DOUBLE(3)
         TR    0(4,R14),TRTABLE
         LA    R0,MSGTEXT2-4
         SR    R14,R0
         SLL   R14,16
         ST    R14,MSGTEXT2
         LA    R0,MSGTEXT2
         B     ALLOCBAD
         SPACE 3
DAIRDYN2 MVC   DOUBLE(8),2(R14) MOVE ADDR OF ERROR MSGS
         L     R1,DOUBLE      ADDR OF FIRST LEVEL MSG
         SR    R0,R0          DEFAULT - NO LEVEL 2 MSG
         LTR   R1,R1          ANY SECOND LEVEL?
         LA    R1,0(R1)       FIRST LEVEL MSG ADDR
         BNM   DAIRDYN4       ONLY LEVEL 1
         L     R0,DOUBLE+4    SECOND LEVEL MSG
DAIRDYN4 DS    0H
         B     ALLOCERR
         SPACE 3
DYNMSGS  DS    0H
         DC    X'6708',X'00',AL3(MSG6708)
         DC    X'0210',X'80',AL3(MSG210),AL4(MSG210X)
         DC    X'0214',X'80',AL3(MSG214),AL4(MSG214X)
         DC    X'0218',X'80',AL3(MSG218),AL4(MSG218X)
         DC    X'021C',X'80',AL3(MSG21C),AL4(MSG21CX)
         DC    X'0334',X'00',AL3(MSG334)
DYNMSGE  DS    0H
         DC    X'041C',X'80',AL3(0),AL4(MSG41CX)
         EJECT
*
*
*        ALLOCATION ERROR MESSAGE SUBROUTINE
*
*
         SPACE 3
ALLOCBAD DS    0H
         OI    FLAGS+1,FNORETRY INDICATE NO ALLOCATION RETRY
         SPACE
ALLOCERR DS    0H
         LA    R15,MSGTEXT1+4  BUFFER ADDRESS
         MVC   0(L'MSG1,R15),MSG1 MOVE 'DATA SET ' MSG TO BUFFER
         LH    R14,DSNLEN     LENGTH OF DSNAME
         LA    R15,L'MSG1(R15)
         SPACE
         MVC   0(0,R15),DSNAME
         EX    R14,*-6        MOVE DSNAME TO BUFFER
         AR    R15,R14
         SPACE 2
         MVC   0(L'MSG3,R15),MSG3 MOVE ' NOT ALLOCATED, ' MSG TO BUFFER
         LA    R15,L'MSG3(R15)
         SPACE 2
         LTR   R1,R1          DEFAULT FIRST LEVEL MSG?
         BNZ   ALOCERR4       NO
         SPACE
         LA    R1,MSG4        YES
         SPACE
ALOCERR4 LH    R14,0(R1)      GET MSG LEN + 4
         SPACE
         MVC   0(0,R15),4(R1)
         EX    R14,*-6
         AR    R15,R14
         LA    R14,MSGTEXT1+4
         SR    R15,R14
         SLL   R15,16
         ST    R15,MSGTEXT1   STORE MESSAGE LENGTH
         SPACE
         MESSAGE MSGTEXT1,(R0)
         SPACE 2
         TM    FLAGS+1,FNORETRY BYPASS ALLOCATION RECOVERY?
         BO    NOTALLOC       YES
         B     RECOVERY       NO, THEN ATTEMPT RETRY
         EJECT
MESSAGE0 DS    0H
         SR    R0,R0                    NO SECONDARY MESSAGE
         SPACE 2
MESSAGE  DS    0H
         ST    R14,R14SAVE    SAVE LINKAGE REGISTER
         SPACE
         LTR   R0,R0          SECOND LEVEL MSG?
         BZ    ERRORM1        NO
         SPACE
         MVC   MSGTEXT1,0(R1) INSURE MSG IN WORK AREA
         LA    R1,MSGTEXT1
         SPACE
         LH    R14,0(R1)      LENGTH OF FIRST LEVEL MSG
         LA    R15,0(R14,R1)  ADDR OF END OF MSG
         LA    R14,1(R14)     JUMP MSG LENGTH
         STH   R14,0(R1)
         MVI   0(R15),C'+'    INDICATE SECOND LEVEL MSG EXISTS
         SPACE 2
         SR    R14,R14        CLEAR CHAIN FIELD
         LA    R15,1          ONE SEGMENT IN 2ND MSG
         STM   R14,R0,PUTOLD2 CREATE SECOND-LEVEL
*                             OUTPUT LINE DESCRIPTOR ('OLD')
         LA    R0,PUTOLD2
         SPACE 3
ERRORM1  LR    R14,R0         NEXT 'OLD' ADDR OR ZERO
         LA    R15,1          ONE SEGMENT
         LR    R0,R1          MSG ADDR
         STM   R14,R0,PUTOLD1 FIRST LEVEL 'OLD'
         SPACE
         LA    R1,PARMLIST
         USING IOPL,R1
         SPACE
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         SPACE
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         MVI   ATTNECB,0
         SPACE 3
         L     R15,ADDRPUTL
         XC    PARMLIST+16(4),PARMLIST+16
         PUTLINE PARM=PARMLIST+16,ENTRY=(15),MF=(E,(1)),               X
               OUTPUT=(PUTOLD1,TERM,MULTLVL,INFOR)
         SPACE 3
         L     R14,R14SAVE
         BR    R14
         EJECT
*
*
*        PUTLINE PROCESSING
*
*
         SPACE 3
$PUTLINE DS    0H
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BCR   1,R14          YES, IMMED RETURN
         ST    R14,R14PUTL
         BAL   R14,FREEBUFF
         SPACE
         LR    R14,R1         SAVE DATA LINE POINTER
         SPACE
         LA    R1,PUTLPARM
         USING IOPL,R1
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         SPACE
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         SPACE
         L     R15,ADDRPUTL
         XC    IOPLEND(16),IOPLEND
         LR    R0,R14         ADDR OF DATA LINE
         SPACE
         PUTLINE PARM=IOPLEND,ENTRY=(15),MF=(E,(1)),                   X
               OUTPUT=((0),TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BO    PUTLINE2       YES
         SPACE
         LA    R14,4
         CR    R15,R14        CHECK COMPLETION
         BH    PUTERROR       PUTLINE ERROR
         LR    R15,R14
         BL    PUTLINE1
         SPACE
PUTLINE2 DS    0H
         SR    R15,R15
         SPACE
PUTLINE1 DS    0H
         L     R14,R14PUTL
         B     0(R14,R15)
         EJECT
*
*
*        PUTGET PROCESSING
*
*
         SPACE 3
$PUTGET  DS    0H
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BCR   1,R14          YES, IMMED RETURN
         ST    R14,R14PTGT
         BAL   R14,FREEBUFF
         NI    FLAGS,255-FNULL
         SPACE
         LA    R0,1           ONLY ONE SEGMENT
         STM   R0,R1,PUTOLD1
         SPACE
         LA    R1,PTGTPARM
         USING IOPL,R1
         MVC   IOPLECT,ADDRECT
         MVC   IOPLUPT,ADDRUPT
         SPACE
         LA    R0,ATTNECB
         ST    R0,IOPLECB
         SPACE
         L     R15,ADDRPTGT
         XC    IOPLEND(16),IOPLEND
         SPACE 2
         PUTGET PARM=IOPLEND,MF=(E,(1)),ENTRY=(15),                    X
               OUTPUT=(PUTOLD1,SINGLE,MODE),                           X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               TERMGET=(EDIT,WAIT)
         SPACE
         LTR   R15,R15                  ERASE SCREEN ONLY ON ZERO RC
         BNZ   $PUTGETA
*        SCREEN ,                       ERASE SCREEN
         SPACE
$PUTGETA LA    R14,8
         CR    R15,R14
         BH    GETERROR       PUTGET ERROR
         SR    R15,R15
         TM    ATTNECB,X'40'  ATTN OCCUR?
         BO    PUTGET1        YES
         SPACE
         LA    R1,PTGTPARM
         LA    R1,IOPLEND
         USING PGPB,R1
         L     R1,PGPBIBUF
         LA    R0,4
         CH    R0,0(R1)       IS THIS A NULL LINE
         ST    R1,ADDRCBUF
         BL    NOTNULL        NO
         OI    FLAGS,FNULL    INDICATE NULL LINE
NOTNULL  DS    0H
         LA    R15,4
         SPACE
PUTGET1  DS    0H
         L     R14,R14PTGT
         B     0(R14,R15)
         EJECT
*
*        DCB OPEN EXIT
*
*
         SPACE 2
OPENEXIT DS    0H
         USING IHADCB,R1
         NI    FLAGS,255-FLOADMOD
         TM    DCBRECFM,X'C0' IS DATA SET IN LOAD MODULE FORMAT?
         BNO   OPEN1          NO, NOT RECFM=U
         OI    FLAGS,FLOADMOD INDICATE LOAD MODULE
OPEN1    DS    0H
         OC    BUFFADDR,BUFFADDR BUFFER PRESENT?
         BCR   7,R14          YES
         SPACE
         LH    R2,DCBBLKSI    GET SIZE OF BUFFER
         MVC   LRECL,DCBLRECL
         LR    R3,R14
         GETMAIN EC,LV=(R2),SP=0,A=BUFFADDR,MF=(E,PARMLIST)
         LR    R14,R3
         LTR   R15,R15        SUCESSFUL?
         BZ    OPEN2          YES
         XC    BUFFADDR,BUFFADDR
OPEN2    DS    0H
         STH   R2,BUFFSIZE
         BR    R14
         DROP  R1
         EJECT
*
*
*        P A R S E   C O N T R O L   L I S T
*
*
         SPACE 3
         PRINT NOGEN
PCLMAIN  IKJPARM DSECT=MAINPDL
INPUTDSN IKJPOSIT DSNAME,VALIDCK=VERDSN,                               $
               PROMPT='NAME OF DATA SET',                              $
               HELP=('NAME OF THE PARTITIONED DATA SET TO BE PROCESSED'$
               )
OPTVOL   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=$VOLUME
$VOLUME  IKJSUBF
VOLNAME  IKJIDENT 'VOLUME NAME',MAXLNTH=6,FIRST=ALPHANUM,              X
               OTHER=ALPHANUM,VALIDCK=VERVOL,                          X
               PROMPT='VOLUME NAME',                                   X
               HELP=('VOLUME WHERE DATA SET RESIDES')
         IKJENDP
         SPACE
PCLDSPLY IKJPARM DSECT=PDLDSPLY
PCLD1    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ALPHA,OTHER=ANY,                                  $
               VALIDCK=VERMEMBR
PCLD2    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ALPHA,OTHER=ANY,                                  $
               VALIDCK=VERMEMBR
         IKJENDP
         SPACE
PCLALIAS IKJPARM DSECT=PDLALIAS
PCLAL1   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='CURRENT MEMBER NAME',                           $
               HELP=('NAME OF THE MEMBER TO BE ASSIGNED AN ALIAS'),    $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
PCLAL2   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='ALIAS NAME',                                    $
               HELP=('NAME TO BE ASSIGNED AS AN ALIAS'),               $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         SPACE
PCLDELET IKJPARM DSECT=PDLDELET
PCLS1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='MEMBER TO BE DELETED',                          $
               HELP=('NAME OF THE MEMBER TO BE SCRATCHED'),            $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         SPACE
PCLLIST  IKJPARM DSECT=PDLLIST
PCLL1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='MEMBER TO BE LISTED',                           $
               HELP=('NAME OF THE MEMBER TO BE DISPLAYED'),            $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         SPACE
PCLRENAM IKJPARM DSECT=PDLRENAM
PCLREN1  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='CURRENT MEMBER NAME',                           $
               HELP=('NAME OF THE MEMBER TO BE RENAMED'),              $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
PCLREN2  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='NEW MEMBER NAME',                               $
               HELP=('NEW NAME OF THE MEMBER'),                        $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         SPACE
PCLMAP   IKJPARM DSECT=PDLMAP
PCLM1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='MEMBER TO BE MAPPED',                           $
               HELP=('NAME OF THE MEMBER FOR WHICH A LOAD MODULE MAP IS$
                TO BE PRODUCED'),                                      $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         SPACE
PCLATTR  IKJPARM DSECT=PDLATTR
PCLAT1   IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='MEMBER NAME',                                   $
               HELP=('NAME OF THE MEMBER FOR WHICH THE ATTRIBUTES ARE T$
               TO BE LISTED'),                                         $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
*        AGO   .$00001        NO SUPPORT FOR RELEASE 20
PCLAT2   IKJIDENT 'NEW ATTRIBUTES',LIST,FIRST=ALPHA,OTHER=ALPHA,       $
               VALIDCK=VERATTR,MAXLNTH=19,                             $
               HELP=('THE NEW ATRIBUTES TO BE ASSIGNED TO THIS LOAD MOD$
               ULE','NULLIFY ATTRIBUTES BY PREFIXING ''NO'' TO THE ATTR$
               IBUTE NAME')
.$00001  ANOP
         IKJENDP
         SPACE
PCLHIST  IKJPARM DSECT=PDLHIST
PCLH1    IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ANY,                 $
               PROMPT='MEMBER NAME',                                   $
               HELP=('NAME OF THE LOAD MODULE FOR WHICH THE HISTORY IS $
               TO BE LISTED'),                                         $
               VALIDCK=VERMEMBR,                                       $
               MAXLNTH=19
         IKJENDP
         PRINT GEN
         EJECT
*
*
*        CONSTANTS
*
*
         SPACE 2
CVT      EQU   16
CVTPCNVT EQU   28
         SPACE
DEBXTNT# EQU   16             OFFSET IN DEB FOR # OF EXTENTS
DEBEXTNT EQU   32             OFFSET IN DEB FOR D.A. EXTENT ENTRY
DEBAMLNG EQU   16             SIZE OF DIRECT ACCESS EXTENT IN DEB
         SPACE 2
UCBVOLI  EQU   28             OFFSET IN UCB FOR VOLUME SERIAL
         SPACE 2
DS1LSTAR EQU   98             OFFSET IN DSCB FOR LAST TTR USED
         SPACE 2
         PRINT NOGEN
         DS    0F
CORESIZE DC    AL1(1),AL3(LENWORK)
MAINPCL  DC    A(PCLMAIN)     PARSE LIST FOR DATA SET NAME
         SPACE 3
IKJDAIR  DC    CL8'IKJDAIR'   DYNAMIC ALLOCATION INTERFACE ROUTINE
IKJPUTL  DC    CL8'IKJPUTL'   PUTLINE SERVICE ROUTINE
IKJGETL  DC    CL8'IKJGETL'   GETLINE SERVICE ROUTINE
IKJPTGT  DC    CL8'IKJPTGT'   PUTGET SERVICE ROUTINE
IKJSCAN  DC    CL8'IKJSCAN'   COMMAND SCAN SERVICE ROUTINE
IKJPARS  DC    CL8'IKJPARS'   PARSE SERVICE ROUTINE
         SPACE 2
STAE     STAE  0,CT,MF=L      PATTERN STAE EXIT
LSTAE    EQU   *-STAE
         SPACE 2
EXCPDCB  DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=E
LEXCPDCB EQU   *-EXCPDCB
SAMDCB   DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
LSAMDCB  EQU   *-SAMDCB
EXLST    DC    X'85',AL3(OPENEXIT)
         SPACE 3
OBTAIN   CAMLST SEARCH,0,0,0
         ORG   OBTAIN+4
         SPACE 3
BLDLPARM DC    AL2(1,74)
DATEMASK DC    X'402120612020612020'
         SPACE 2
NO       DC    C'NO'
IDALIAS  DC    C'-A'
         DS    0F
FF       DC    8X'FF'
SYSALLDA DC    CL8'SYSALLDA'
BLANKS   DC    8C' '
TRTABLE  EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
         SPACE 2
FOUR     DC    F'4'
         SPACE
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
         SPACE 2
* TRANSLATE AND TEST TABLE TO DETERMINE IF A MEMBER NAME
*  IS ALL PRINTABLE.   - SPNB ASSUMES A 2260 KEYBOARD
         SPACE 1
DISPLAY  CSECT ,                      PUT THIS TABLE IN DISPLAY'S CSECT
TRTMN    DC    256X'FF'                 SET ALL BAD
         ORG   TRTMN+C' '               A SPACE IS OK
         DC    X'00'
         ORG   TRTMN+C'.'               GOOD CHARACTERS
         DC    6X'00'                   .<(+&   6 CHARS
         ORG   TRTMN+C'$'               GOOD CHARACTERS
         DC    7X'00'                   $*);^-/    7 CHARS
         ORG   TRTMN+C','               GOOD CHARACTERS
         DC    5X'00'                   , _>?      5 CHARS
         ORG   TRTMN+C':'               GOOD CHARACTERS
         DC    5X'00'                   :#@'=      5 CHARS
         ORG   TRTMN+C'A'               GOOD CHARACTERS
         DC    9X'00'                   ABCDEFGHI
         ORG   TRTMN+C'J'               GOOD CHARACTERS
         DC    9X'00'                   JKLMNOPQR
         ORG   TRTMN+C'S'               GOOD CHARACTERS
         DC    8X'00'                   STUVWXYZ
         ORG   TRTMN+C'0'               GOOD CHARACTERS
         DC    10X'00'                  0123456789
         ORG
PDS      CSECT ,                        GO BACK TO MAIN CSECT
         EJECT
*
*        NOTE - THE SAME SET OF LOGICAL CCWS CAN BE USED
*        TO READ A PDS DIRECTORY, OR THE CESD ENTRIES, OR
*        IDR RECORDS WITHIN A LOAD MODULE.
*
         SPACE
IDRCCWS  DS    0D
ESDCCWS  DS    0D
DIRCCWS  DS    0D
         CCW   X'31',0,X'60',5
         CCW   X'08',0,X'60',1
         CCW   X'86',0,X'60',256
         CCW   X'92',0,X'20',5
LDIRCCWS EQU   *-DIRCCWS
LESDCCWS EQU   *-ESDCCWS
LIDRCCWS EQU   *-IDRCCWS
         SPACE 2
LISTCCWS DS    0D
         CCW   X'31',0,X'60',5
         CCW   X'08',0,X'60',1
         CCW   X'92',0,X'60',5
         CCW   X'86',0,X'20',0
LLISTCCW EQU   *-LISTCCWS
         SPACE 3
TBLATTR  DS    0H
         DC    BL2'1000000000000000',AL1(3),CL9'RENT'
         DC    BL2'0100000000000000',AL1(3),CL9'REUS'
         DC    BL2'0000000000000001',AL1(3),CL9'REFR'
         DC    BL2'0010000000000000',AL1(6),CL9'OVERLAY'
         DC    BL2'0001000000000000',AL1(3),CL9'TEST'
         DC    BL2'0000100000000000',AL1(8),CL9'LOAD ONLY'
         DC    BL2'0000010000000000',AL1(3),CL9'SCTR'
         DC    BL2'0000000000001000',AL1(7),CL9'NOT EDIT'
         DC    BL2'0000001000000000',AL1(128+7),CL9'NOT EXEC'
         DC    BL2'0000000010000000',AL1(128+1),CL9'DC'
         DC    X'FF'
         SPACE 2
ATTRRENT EQU   X'80'
ATTRREUS EQU   X'40'
ATTROVLY EQU   X'20'
ATTRTEST EQU   X'10'
ATTRNE   EQU   X'08'          NOT-EDITABLE ATTRIBUTE
ATTRSCAT EQU   X'04'
ATTREP0  EQU   X'20'          ASSIGNED ENTRY PT AT OFFSET 0
ATTRREFR EQU   X'01'
ATTRLOAD EQU   X'08'
ATTREXEC EQU   X'02'
ATTRTBL  DS    0X
         DC    CL8'RENT',X'00',AL1(ATTRRENT,0)
LATTRTBL EQU   *-ATTRTBL
LATTR    EQU   8
         DC    CL8'REUS',X'00',AL1(ATTRREUS,0)
         DC    CL8'REFR',X'00',AL1(0,ATTRREFR)
         DC    CL8'EXEC',X'00',AL1(ATTREXEC,0)
         DC    CL8'LOAD',X'00',AL1(ATTRLOAD,0)
         DC    CL8'AUTH',X'40',AL1(0,0)
         DC    X'FF'
$SEGTAB  DC    CL8'$SEGTAB'
$ENTAB   DC    CL8'$ENTAB'
$PRIVATE DC    CL8'$PRIVATE'
         EJECT
OPTNTBL  CSECT
         MSG   'THE FOLLOWING OPTIONS ARE AVAILABLE:'
         MSG   'ALIAS   -   ASSIGN AN ALIAS TO A MEMBER'
         MSG   'ATTR    -   LIST LOAD MODULE ATTRIBUTES'
         MSG   'CHANGE  -   SELECT A NEW DATA SET'
         MSG   'END     -   TERMINATE THE PDS COMMAND'
         MSG   'EXEC    -   INVOKE THE EXEC COMMAND'
         MSG   'HELP    -   DISPLAY HELP TEXT'
         MSG   'HISTORY -   LIST HISTORY OF LOAD MODULE'
         MSG   'LIST    -   LIST CONTENTS OF A MEMBER'
         MSG   'LISTDIR -   LIST DIRECTORY'
         MSG   'MAP     -   MAP STRUCTURE OF LOAD MODULE'
         MSG   'OPTIONS -   DISPLAY THIS MENU'
         MSG   'RENAME  -   RENAME A MEMBER'
         MSG   'SCRATCH -   SCRATCH A MEMBER'
         MSG   'USAGE   -   LIST DIRECTORY STATISTICS'
         DC    X'FFFF'
         SPACE 3
CMDTABLE CSECT
         DC    CL8'LIST    ',AL1(7),AL3(LIST),X'80',AL3(PCLLIST)
         DC    CL8'RENAME  ',AL1(6),AL3(RENAME),X'00',AL3(PCLRENAM)
         DC    CL8'SCRATCH ',AL1(7),AL3(SCRATCH),X'00',AL3(PCLDELET)
         DC    CL8'ATTR    ',AL1(4),AL3(ATTR),X'00',AL3(PCLATTR)
         DC    CL8'ALIAS   ',AL1(5),AL3(ALIAS),X'00',AL3(PCLALIAS)
         DC    CL8'MAP     ',AL1(3),AL3(MAP),X'00',AL3(PCLMAP)
         DC    CL8'HELP    ',AL1(4),AL3(HELP),X'40',AL3(0)
         DC    CL8'HISTORY ',AL1(7),AL3(HISTORY),X'00',AL3(PCLHIST)
         DC    CL8'CHANGE  ',AL1(6),AL3(CHANGE),X'00',AL3(PCLMAIN)
         DC    CL8'LISTDIR ',AL1(4),AL3(DISPLAY),X'00',AL3(PCLDSPLY)
         DC    CL8'LD      ',AL1(4),AL3(DISPLAY),X'00',AL3(PCLDSPLY)
         DC    CL8'USAGE   ',AL1(5),AL3(USAGE),X'40',AL3(0)
         DC    CL8'OPTIONS ',AL1(7),AL3(LISTOPTS),X'40',AL3(0)
         DC    CL8'END     ',AL1(3),AL3(END),X'40',AL3(0)
         DC    CL8'EXEC    ',AL1(4),AL3(EXEC),X'40',AL3(0)
         DC    X'FF'
PDS      CSECT
         SPACE
VOPTNTBL DC    V(OPTNTBL)
VCMDTAB  DC    V(CMDTABLE)
         EJECT
*
*        PROGRAM MESSAGES
*
         SPACE 2
         PRINT NOGEN
         SPACE
MSGUSEV  MSG   'VOLUME SERIAL         '
MSGOP    MSG   ' PDS'
MSGUSEB  MSG   ' DIRECTORY BLOCKS ALLOCATED '
MSGUSED  MSG   ' DIRECTORY BLOCKS IN USE '
MSGUSER  MSG   ' MEMBERS EXIST'
MSGUSEA  MSG   ' OF THEM ARE ALIASES'
MSGUSESI MSG   ' TRACKS ALLOCATED '
MSGUSEMP MSG   ' TRACKS USED '
MSGUSEXT MSG   ' EXTENTS USED '
MSGUSERR MSG   ' DIRECTORY ERROR'
MSGNDSCB MSG   ' OBTAIN ERROR'
MSGSTOWC MSG   ' STOW ERROR, CODE = 12'
MSGSTOW8 MSG   ' STOW ERROR, CODE = 8'
MSGSTOW4 MSG   ' STOW ERROR, CODE = 4'
MSGNLIST MSG   ' UNABLE TO LIST LOAD MODULES'
MSGLISTI MSG   ' I/O ERROR READING MEMBER'
MSGNLIB  MSG   ' DATA SET NOT PARTITIONED'
MSGNLOAD MSG   ' DATA SET NOT LOAD LIBRARY'
MSGNOPEN MSG   ' UNABLE TO PROCESS DATA SET'
MSGBADAT MSG   ' INVALID ATTRIBUTE, '
MSGDUPAT MSG   ' CONFLICTING ATTRIBUTE IGNORED - '
MSGIOERR MSG   ' DIRECTORY I/O ERROR'
MSGEMPTY MSG   ' NO MEMBERS EXIST'
MSGRANGE MSG   ' INVALID RANGE'
MSGNODSP MSG   ' NO MEMBERS WITHIN RANGE'
MSGEXIST MSG   ' MEMBER CURRENTLY EXISTS'
MSGNONE  MSG   ' MEMBER NOT FOUND'
MSGGONE  MSG   ' SCRATCHED'
MSGRENAM MSG   ' RENAMED'
MSGATTRS MSG   ' ATTRIBUTES FOR THIS MODULE ARE:'
MSGNREAL MSG   ' MODULE IS AN ALIAS, ALIAS NOT ASSIGNED'
MSGREAL  MSG   ' MODULE IS ALIAS FOR '
MSGOTHER MSG   ' ALIASES FOR THIS MODULE ARE:'
MSGNOMOD MSG   ' MODULE IS ALIAS BUT REAL MODULE DOES NOT EXIST'
NONEMSG  MSG   ' NONE'
MSGNOESD MSG   ' MODULE HAS NO EXTERNAL SYMBOLS'
MSGALIAS MSG   ' ALIAS ASSIGNED'
MSGENTRY MSG   ' ENTRY POINT AT '
MSGLEN   MSG   ' MODULE LENGTH  '
MSGHIST1 MSG   'HISTORY SUMMARY FOR MODULE '
MSGHISTD MSG   'LAST LINK-EDITED ON'
MSGHISTZ MSG   'IMASPZAP UPDATE HISTORY BY CSECT -'
MSGHISTU MSG   'USER-SUPPLIED UPDATE HISTORY BY CSECT -'
MSGNHIST MSG   ' NO HISTORY AVAILABLE'
MSGUNKN  MSG   ' INVALID SUBCOMMAND.  FOR HELP, ENTER - OPTIONS'
MSGRETRY MSG   ' RE-ENTER DATA SET NAME'
MSGVOL   MSG   ' REQUIRED VOLUME NOT MOUNTED'
MSGINVLD EQU   MSGUNKN
MSGNOCAT MSG   ' DATA SET NOT CATALOGED'
MSGINDEX MSG   ' INCONSISTENT CATALOG STRUCTURE'
MSGCATER MSG   ' CATALOG ERROR CODE '
MSGSYNTX MSG   ' INVALID SYNTAX IN DATA SET NAME'
MSGDSFMT MSG   ' DATA SET NAME ALREADY EXISTS AT A LOWER LEVEL'
MSGDSN   MSG   ' AN INDEX OF THE DATA SET NAME EXCEEDS 8 CHARACTERS'
MSG20C   MSG   'DATA SET IN USE'
MSG20CX  MSG   ' DATA SET IN USE BY ANOTHER USER OR A BACKGROUND JOB'
MSG210   EQU   MSG20C
MSG210X  EQU   MSG20CX
MSG214   MSG   'VOLUME NOT AVAILABLE'
MSG214X  MSG   ' VOLUME CAN NOT BE MOUNTED'
MSG218   EQU   MSG214
MSG218X  EQU   MSG214X
MSG21C   EQU   MSG214
MSG21CX  MSG   ' USER ATTRIBUTES INCORRECT'
MSG334   MSG   'NAME EXCEEDS 44 CHARACTERS'
MSG41CX  MSG   ' DATA SET ON MORE THAN ONE VOLUME'
MSG6708  MSG   'DATA SET NOT ON VOLUME'
MSGDYNER MSG   ' DYNAMIC ALLOCATION ERROR CODE '
MSGFULL  MSG   'TOO MANY DATA SETS'
MSGFULL1 MSG   ' FREE DATA SETS NO LONGER REQUIRED'
MSGCMDER MSG   ' COMMAND SYSTEM ERROR'
MSG4     MSG   'SYSTEM ERROR'
MSG1     DC    C' DATA SET '
MSG3     DC    C' NOT ALLOCATED, '
MSGPARSE DC    C' PARSE ERROR CODE '
         SPACE 3
PATCH    DC    10F'0'
         EJECT
*
*        DYNAMIC WORK AREA
*
         SPACE 3
WORKAREA DSECT
MAINSAVE DS    18A
CMDSAVE  DS    18A
VALSAVE  DS    18A
ATTNSAVE DS    18A
PARSELST DS    7A                       AREA FOR PARSE PARM LIST
ADDRSAVE DS    A                        ADDR OF FIRST SAVE AREA
CPPLADDR DS    A                        ADDR OF CPPL
         SPACE
STAEREGS DS    16A
STAESAVE DS    18A
R14SAVE  DS    A
R14PARSE DS    A
R14ATTN  DS    A
R14ALLOC DS    A
R14LINK  DS    A
R14PUTL  DS    A
R14GETL  DS    A
R14PTGT  DS    A
         SPACE
BASES    DS    2A             PROGRAM BASE ADDRESSES
         SPACE
ADDRUPT  DS    A
ADDRECT  DS    A
ADDRPSCB DS    A
ADDRCBUF DS    A
         SPACE
ADDRPCL  DS    A              ADDR OF PCL FOR PARSE
ADDRPUTL DS    A              ADDR OF IKJPUTL
ADDRGETL DS    A              ADDR OF IKJGETL
ADDRPTGT DS    A              ADDR OF IKJPTGT
ADDRSCAN DS    A              ADDR OF IKJSCAN
         SPACE
ADDRPDL  DS    A
ADDRCMD  DS    A
OPENLIST DS    2A
         SPACE 2
ATTNECB  DS    F
         SPACE
ESDPTR   DS    A              ADDRESS OF ESD DATA CHAIN
IDRPTR   DS    A
         SPACE
PARMLIST DS    10A
GETLPARM DS    10A
PUTLPARM DS    10A
PTGTPARM DS    10A
SCANANSR DS    XL8
SUBNAME  DS    CL8
BUFFADDR DS    A
BUFFSIZE DS    H
LRECL    DS    H
ATTRYES  DS    XL2
ATTRNO   DS    XL2
         SPACE 2
LINESIZE DS    H
LMEMBER1 DS    H
LMEMBER2 DS    H
         SPACE 2
TOTBLOCK DS    H
TOTUSED  DS    H
TOTMEMR  DS    H
TOTMEMA  DS    H
DSNTOTAL DS    H
DSNTUSED DS    H
DSNEXTNT DS    H
         SPACE
MEMBERS  DS    0X
MEMBER1  DS    CL8            FIRST MEMBER NAME
MEMBER2  DS    CL8            SECOND MEMBER NAME
         SPACE
SAVETTR  DS    XL3,X
LKEDDATE DS    XL3
         SPACE 2
VOLUME   DS    CL8
         SPACE 2
UNIT     DS    CL8
         SPACE
DOUBLE   DS    D
         SPACE
STAEPARM STAE  0,CT,MF=L
         SPACE
FLAGS    DS    XL4            GENERAL STATUS FLAGS
*        FLAGS AT BYTE 0
FOPTIONS EQU   X'80'          OPTIONS PRESENT WITH COMMAND
FCMD     EQU   X'40'          SUBCOMMAND BUFFER AVAILABLE
FNULL    EQU   X'20'          NULL LINE ENTERED
FMEMBER1 EQU   X'10'          FIRST MEMBER FLAG
FMEMBER2 EQU   X'08'          SECOND MEMBER FLAG
FLOADMOD EQU   X'04'          DATA SET IN LOAD MODULE FORMAT
FRESTART EQU   X'02'          RESTART W/ NEW DATA SET
FATTR    EQU   X'01'          CHANGE ATTRIBUTES REQUEST
*        FLAGS AT BYTE 1
F1STREAD EQU   X'80'          INITIAL ENTRY TO READ SUBROUTINE
FLINESET EQU   X'40'          OUTPUT LINE IN PROGRESS
FEXIST   EQU   X'20'          MEMBERS EXIST IN DATA SET
FRANGE   EQU   X'10'          MEMBERS EXIST IN DISPLAY RANGE
FIDR     EQU   X'40'          IDR RECORDS EXIST
F1IDR    EQU   X'20'          IDR TITLE PROCESSED
FALLESD  EQU   X'08'          REQUEST ALL ESD ENTRIES
FNORETRY EQU   X'04'          NO ALLOCATION RETRY ALLOWED
FQUOTE   EQU   X'02'          DATA SET NAME IS QUOTED
FATTN    EQU   X'01'          INTERRUPTIONS ALLOWED
*        FLAGS AT BYTE 2
FREALMOD EQU   X'80'          TRUE MODULE NAME SPECIFIED
FPERMDS  EQU   X'40'          DATA SET IS PERMANENTLY ALLOCATED
FDIRSCAN EQU   X'20'          SCAN DIRECTORY STATUS
FDIREND  EQU   X'10'          LAST USED BLOCK ENCOUNTERED
FIDRCONT EQU   X'08'          PROCESSING CONTINUED IDR RECORD
FIDRHCON EQU   X'04'
FAPFON   EQU   X'02'          TURN ON APF REQUESTED
FAPFOFF  EQU   X'01'          TURN OFF APF REQUESTED
         SPACE 2
DDNAME   DS    CL8
DDNAME1  DS    CL8
PASSWORD DS    CL8
DSNLEN   DS    H
DSNAME   DS    CL44
DSNAME1  DS    CL44
         ORG   DSNAME
JFCB     DS    XL176                    AREA FOR JFCB
         ORG
         SPACE 2
         DS    0D
DSCB     DS    XL148                    AREA FOR OBTAIN WORKAREA/DSCB
         ORG   DSCB
DAIRBLK  DS    25D            SPACE FOR DAIR PARAMETER LIST
LDAIRBLK EQU   *-DAIRBLK
         ORG
         SPACE 2
CAMLST   CAMLST SEARCH,0,0,0
         SPACE
         DS    0F
MSGTEXT1 DS    XL124
MSGTEXT2 DS    XL124
         SPACE
         ORG   MSGTEXT1
DAYTABLE DS    XL12
         ORG
         SPACE 2
PUTOLD1  DS    3F
PUTOLD2  DS    3F
         SPACE 2
INDCB    DCB   DSORG=PO,MACRF=(R,W),DDNAME=X
STOWDCB  DCB   DSORG=PO,MACRF=(R,W),DDNAME=X
EXLSTX   DS    A
         SPACE 2
         DS    0F
BLDLLIST DS    XL4
MEMNAME  DS    0CL8
DIRNAME  DS    CL8
DIRTTR   DS    XL3
DIRFLAG  DS    X
DIRUSER  DS    XL62
         ORG   DIRUSER
*        THE FOLLOWING FIELDS APPLY TO LOAD MODULES
DIRSTART DS    XL4            TTR OF FIRST TEXT BLOCK
DIRNOTE  DS    XL3            TTR OF NOTE LIST
DIRNOTE# DS    X
DIRATTR  DS    XL2
DIRCORE  DS    XL3            SIZE OF LOAD MODULE
DIRTEXTL DS    XL2            LENGTH OF 1ST TEXT RECORD
DIRENTRY DS    XL3            ENTRY POINT ADDRESS
DIRATTR2 DS    XL1            ADDITIONAL ATTRIB BYTES
         DS    XL2            RESERVED
DIRAPF   DS    0XL2           APF (IF NOT SCAT, SSI, OR ALIAS)
DIREP    DS    XL3            ENTRY POINT (REAL MEMBER)
DIRREAL  DS    CL8            REAL NAME OF MEMBER
DIRAPF2  DS    XL2            APF INFO IF W/ALIAS
DIREND2  EQU   *              END OF ALIAS SECTION
         ORG
DIREND   EQU   *
         SPACE 2
         DS    0D
         SPACE 2
CCWS     DS    6D
LCCWS    EQU   *-CCWS
         SPACE
IOECB    DS    F
IOB      DS    0F
IOBFLAG  DS    X'42000000'
IOBECB   DS    A(IOECB)
         DS    X
IOBCSW   DS    XL7
IOBCCW   DS    A(CCWS)
IOBDCB   DS    A(INDCB)
         DS    2F
MBBCCHHR DS    XL3
IOBSEEK  DS    XL5
         DS    4F
LIOB     EQU   *-IOB
         SPACE 2
IDROFSAV DS    F              CONTINUED IDR BLK OFFSET SAVE AREA
DIRPTRS  DS    3F
*        NOTE - THE SAME BUFFER CAN BE USED TO
*        CONTAIN A PDS DIRECTORY BLOCK OR AN IDR OR
*        CESD RECORD FROM A LOAD MODULE
IDRBUFF  DS    0XL256
ESDBUFF  DS    0XL256
DIRBLOCK DS    XL256
         DS    XL80                     PAD WORK AREA FOR MVS
         SPACE 5
LENWORK  EQU   *-WORKAREA
         PRINT GEN
         EJECT
*
*
*        ESD TABLE FORMAT
*
*
         SPACE 2
ESDENTRY DSECT
ESDLINK  DS    F
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINATION
CODELR   EQU   X'03'          EXTERNAL REFERENCE
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
ESDID    DS    XL2            RELATIVE ESD ID #
LENESD2  EQU   *-ESDNAME      LENGTH OF ESD DATA
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY
         EJECT
IDRENTRY DSECT
IDRLINK  DS    A
IDRSTART EQU   *
IDRESDID DS    XL2
IDRTYPE  DS    X
IDRZAP   EQU   X'01'
IDRLKED  EQU   X'02'
IDRUSER  EQU   X'08'
IDRDATE  DS    XL3
IDRLDATA DS    X
IDRUPREF DS    XL6                      USER REC PREFIX
IDRZDATA DS    0CL8
IDRDATA  DS    CL40
LENIDR1  EQU   *-IDRSTART
LENIDR   EQU   *-IDRENTRY
         EJECT
         IKJPPL
         EJECT
         IKJIOPL
IOPLEND  DS    0F
         EJECT
         IKJPSCB
         EJECT
         IKJECT
         EJECT
         IKJCPPL
         EJECT
         IKJCSPL
         EJECT
         IKJCSOA
         EJECT
         IKJPGPB
         EJECT
         IKJGTPB
         EJECT
         IKJDAPL
         EJECT
         IKJDAP00
         EJECT
         IKJDAP04
         EJECT
         IKJDAP08
         EJECT
         IKJDAP0C
DA0CDDN  DS    XL8 START OF DDNAME LIST
         EJECT
         IKJDAP10
         EJECT
         IKJDAP14
         EJECT
         IKJDAP18
         EJECT
         IKJDAP1C
         EJECT
         IKJDAP24
         EJECT
         IKJDAP28
         EJECT
         IKJDAP2C
         EJECT
         IKJDAP30
         EJECT
         DCBD  DSORG=PO
         SPACE 5
         END
