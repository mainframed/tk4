CQ       CSECT
*
*        WRITTEN BY: ED MOLNAR
*                    COMPUTER DATA SYSTEMS,INC. (CDSI)
*                    BETHSDA MD.
*                    (301) 567-1730 EXT.343
*
*       THIS CP IS USED TO CANCEL AND DELETE ALL DATA SETS OFF THE
*       QUEUE.  THIS COMMAND ONLY AUTHORIZES ITSELF WHEN CALLING
*       SUBSYSTEM INTERFACE.  THE SVC NUMBER MUST BE CHANGED FOR THE
*       AUTHORIZING OF THIS FUNCTION TO WHAT EVER NUMBER IS USED FOR
*       YOUR INSTALLATION.  THIS SHOULD BE THE ONLY INSTALATION
*       DEPENDENT ITEM IN THIS CODE.  THE FORMAT FOR THIS COMMAND IS:
*       "CQ JOBNAME" OR "CQ (JOBNAME(JOBID),JOBNAME(JOBID)....JOBNAME)"
*       THIS WILL REMOVE ONLY SELECTED JOBS WHEN GIVEN THE JOBID OR
*       ALL JOBS WITH A SPECIFIED JOBNAME AND NO JOBID.  BE CAREFUL.
*
*
         SAVE      (14,12),,CQ-&SYSTIME-&SYSDATE
         LR        12,15
         USING     CQ,12
         LA        11,SAVEAREA
         ST        11,8(13)
         ST        13,SAVEAREA+4
         LR        13,11
         USING     CPPL,R2                  CPPL DSECT.
         LR        R2,R1                    SAVE CPPL.
********************************************************************
*
*        GET USER INFO OFF CPPL.
*
********************************************************************
         L         R1,CPPLPSCB              GET THE PSCB.
         IC        R3,7(R1)                 GET USER ID LENGTH.
         BCTR      R3,0                     SUBTRACT 1.
         STH       R3,USERLEN               SAVE IT.
         EX        R3,USER                  GET THE USERID.
         CLI       16(R1),X'80'             OPER SET?
         BL        SETUPARS                 NOPE.
         OI        AUTHUSER,1               SET OPER FLAG.
SETUPARS DS        0H
         USING     PPL,R15                  PPL DSECT.
         LA        R15,MYPPL                ADDRESS OF MYPPL.
         MVC       PPLUPT(4),CPPLUPT        SAVE UPT.
         MVC       PPLECT(4),CPPLECT        SAVE ECT.
         LA        R0,MYECB                 ADDRESS OF MY ECB.
         ST        R0,PPLECB                STORE IN PPL.
         XC        MYECB,MYECB              CLEAR IT.
         L         R0,=A(JOBS)              ADDRESS OF IKJPARS START.
         ST        R0,PPLPCL                STORE IT IN PPL.
         LA        R0,MYANS                 ADDRESS OF MY ANSWER.
         ST        R0,PPLANS                STORE IT IN PPL.
         MVC       PPLCBUF(4),CPPLCBUF      ADDRESS OF COMMAND BUFFER.
         LA        R0,DUMMY                 DUMMY ROUTINE.
         ST        R0,PPLUWA                STORE IT IN PPL
         DROP      R15
CALLPARS DS        0H
         CALLTSSR  EP=IKJPARS,MF=(E,MYPPL)   CALL IKJPARS.
         LTR       R15,R15                   GOOD RETURN?
         BZ        OKPARS                    YEP,CONTINUE FORWARD.
         XC        MSG1,MSG1                 OH NO,CLEAR MSG.
         MVC       MSG1(14),=C'IKJPARS FAILED'  MOVE MSG.
         BAL       R10,PUTMSG                LET THEM CRY.
         B         FREEAREA                  GO CLEAN UP.
OKPARS   DS        0H
***********************************************************************
*
*            INIT FOR SUBSYSTEM INTERFACE.
*
***********************************************************************
INITSSI  DS        0H
         L         R0,GETSIZE               GET NUMBER OF BYTES.
         GETMAIN   R,LV=(0)                 GET SOME MEMORY.
         LR        R7,R1                    SAVE THE ADDRESS.
         USING     SSOB,R7                  SET ADDRESABILITY.
         LA        R5,SSCSBGN               GET THE BEGINING.
         XC        SSOBEGIN(SSOBHSIZ),SSOBEGIN   CLEAR THE AREA.
         MVC       SSOBID,=C'SSOB'          MOVE IN SSOB ID.
         MVC       SSOBLEN,=AL2(SSOBHSIZ)   MOVE IN THE SIZE.
         ST        R5,SSOBINDV              SAVE ADDR. OF REQUEST AREA.
         ST        R7,SSOBPTR               SAVE POINTER OF OUR AREA.
         OI        SSOBPTR,X'80'            SET FOR FUNCTION.
***********************************************************************
*
*            SET TO READ IKJPARS.
*
***********************************************************************
         USING     IKJPARMD,R11              DSECT FOR RETURNED INFO.
         L         R11,MYANS                 ADDRESS OF ANSWER.
         LA        R3,JOBNAME0               ADDRESS OF IKJ PROMPT.
NEXTJOB  DS        0H
         L         R5,0(R3)                  GET THE ADDRESS.
         TM        AUTHUSER,1                AUTH USER?
         BO        CONTJOB                   YES,CONTINUE.
         LH        R10,USERLEN               LENGTH OF USERID -1.
         EX        R10,USERCOMP              COMPARE
         BE        CONTJOB                   OK,JOB HAS USERID.
         XC        MSG1,MSG1                 CLEAR MSG.
         MVC       MSG1(32),=C'JOB MUST START WITH YOUR USER ID'
         BAL       R10,PUTMSG
         B         NEXTPDE
CONTJOB  DS        0H
         LH        R4,4(R3)                  GET THE LENTH.
         BCTR      R4,0                      SUBTRACT 1.
         MVC       JOBNAME,=CL8' '           BLANK OUT THE JOB NAME.
         MVC       JOBID,=CL8' '             BLANK OUT THE JOB ID.
         EX        R4,NAME                   MOVE JOBNAME IN PLACE.
         TM        14(R3),X'80'              TEST FOR JOB ID.
         BNO       NOJOBID                   NOT FOUND.
         LH        R4,12(R3)                 ITS HERE,GET THE LENGTH.
         L         R5,8(R3)                  GET THE ADDRESS.
         BCTR      R4,0                      SUBTRACT 1 FROM LENGTH.
         EX        R4,NAME2                  MOVE IN THE JOB ID.
         MVC       SSOBFUNC,=AL2(SSOBCANC)   TELL IT THE NEW FUNCTION.
         MVC       SSCSDIMP,=H'16'           TELL IT THE NEW ARRAY SIZ.
         OI        SSCSFLGS,SSCSCOUT         SET THE PURGE FLAGS.
         MVC       SSCSJOBN,JOBNAME          MOVE IN JOBNAME.
         MVC       SSCSJOBI,JOBID            MOVE IN JOBID.
         BAL       R10,CALLSSI               CALL THE SUBSYSTEM.
         BAL       R10,CKRETN                CHECK THE RETURN CODE.
         XC        MSG1,MSG1                 CLEAR THE MSG.
         MVC       MSG1(8),SSCSJOBN          MOVE IN THE JOBNAME.
         MVC       MSG1+9(8),SSCSJOBI        MOVE IN THE JOBID.
         MVC       MSG1+18(8),=C'CANCELED'   TELL THEM ITS CANCELED.
         BAL       R10,PUTMSG                GIVE THE MSG.
         B         NEXTPDE                   GO CKECK FOR ANOTHER.
NOJOBID  DS        0H
         NI        SSCSFLGS,SSCSCOUT         SET THE PURGE FLAGS OFF.
***********************************************************************
*
*            SET UP TO GET JOBNUMBERS.
*                  JOB STATUS.
***********************************************************************
JOBSTAT  DS        0H
         XC        SSCSBGN(SSCSIZE),SSCSBGN  CLEAR OUR REQUEST AREA.
         MVC       SSCSLEN,=AL2(SSCSIZE)     MOVE IN THE SIZE.
         MVC       SSOBFUNC,=AL2(SSOBSTAT)   MOVE IN THE FUNCTION.
         MVC       SSCSJOBN,JOBNAME          MOVE IN JOBNAME.
         MVC       SSCSJOBI,=CL8' '          BLANK OUT THE JOB ID.
         MVC       SSCSDIMP,=XL2'3E80'       TELL IT THE SPACE IT HAS.
         BAL       R10,CALLSSI               GET JOBID.
         BAL       R10,CKRETN                CHECK RETURN CODE.
         LH        R5,SSCSDIMR               GET THE SPACE IT USED.
         SRL       R5,4                      DIVIDE BY BYTES IN ARRAY.
         LA        R6,SSCSARAY               GET THE ARRAY ADDRESS.
***********************************************************************
*
*            SET UP TO CANCEL.
*
***********************************************************************
         MVC       SSOBFUNC,=AL2(SSOBCANC)   TELL IT THE NEW FUNCTION.
         MVC       SSCSDIMP,=H'16'           TELL IT THE NEW ARRAY SIZ.
         OI        SSCSFLGS,SSCSCOUT         SET THE PURGE FLAGS.
NOJOBID1 DS        0H
         MVC       SSCSJOBI,0(R6)            MOVE IN THE JOB ID.
         BAL       R10,CALLSSI               CALL THE SUBSYSTEM.
         BAL       R10,CKRETN                CKECK THE RETURN CODE.
         XC        MSG1,MSG1                 CLEAR THE MSG.
         MVC       MSG1(8),SSCSJOBN          MOVE IN THE JOB NAME.
         MVC       MSG1+9(8),SSCSJOBI        MOVE IN THE JOB ID.
         MVC       MSG1+18(8),=C'CANCELED'   MOVE IN MSG.
         BAL       R10,PUTMSG                GIVE THEM THE MSG.
         LA        R6,16(R6)                 GET NEXT ARRAY.
         BCT       R5,NOJOBID1               GO PROCESS IT.
         XC        MSG1,MSG1                 CLEAR THE MSG.
         MVC       MSG1(8),SSCSJOBN          MOVE IN THE JOB NAME.
         MVC       MSG1+9(11),=C'JOBS PURGED' MOVE IN MSG.
         BAL       R10,PUTMSG                TELL THEM.
NEXTPDE  DS        0H
         L         R3,16(R3)                 ADDRESS OF NEXT PDE.
         C         R3,=X'FF000000'           VALID?
         BNZ       NEXTJOB                   YES,GO PROCESS.
FREEAREA DS        0H                        CLEAN UP.
         L         R0,GETSIZE
         FREEMAIN  R,LV=(0),A=(R7)
HOMEWARD DS        0H
         IKJRLSA   MYANS
*-------------------------------------------------------------------
         L         13,SAVEAREA+4
         ST        15,16(13)
         RETURN    (14,12),T
***********************************************************************
*
*            CALL SUBSYSTEM INTERFACE.
*
***********************************************************************
CALLSSI  DS        0H
         L         R9,16            A(CVT)
         L         R9,296(R9)       A(JESCT)
         L         R0,=C'ON  '      LOAD COMMAND CODE
         SVC       243              ISSUE SVC
         MODESET   MODE=SUP         GET SUPER
         LA        R1,SSOBPTR       ADDR OF PTR TO SSOB
         L         R15,20(R9)       A(JESSSREQ)
         BALR      R14,R15          GO TO IT.
         LR        R9,R15           SAVE RETURN CODE
         MODESET   MODE=PROB        BACK TO NORMAL
         L         R0,=C'OFF '      LOAD COMMAND CODE
         SVC       243              ISSUE SVC
         SLL       R9,1
         B         *+8(R9)
         B         ENDSSI          DUMMY BRANCH.
         MVI       MSG1,C'0' 0  - SUCCESSFUL INSTRUCTION
         B         ENDSSI
         MVI       MSG1,C'4' 4  - SS DOESN'T SUPPORT THIS FUNCTION
         B         SSIERR
         MVI       MSG1,C'8' 8  - SS EXIST, BUT IS NOT UP
         B         SSIERR
         MVI       MSG1,C'C' 12 - SS DOES NOT EXIST
         B         SSIERR
         MVI       MSG1,C'G' 16 - FUNCTION NOT SUPPORTED
         B         SSIERR
         MVI       MSG1,C'H' 20 - LOGICAL ERROR
*
SSIERR   DS        0H
         XC        MSG1,MSG1
         MVC       MSG1+2(33),=C'RETURN CODE FROM SSI CALL SYSTEMS'
         BAL       R10,PUTMSG
         B         HOMEWARD
ENDSSI   DS        0H
         BR        R10              GO BACK.
***********************************************************************
*
*                  CHECK THE RETURN CODE FROM STATUS AND CANCEL
*
***********************************************************************
CKRETN   DS        0H
         XC        MSG1,MSG1
         L         R1,SSOBRETN
         B         *+4(R1)
         B         CKRETN0           0  - GOOD RETURN
         B         CKRETN4           4  - JOBNAME NOT FOUND
         B         CKRETN8           8  - INVALID JOBNAME/JOB COMBO.
         B         CKRETN12          12 - JOB NOT CANCELED - DUPLICATE
         B         CKRETN16          16 - STATUS ARRAY TO SMALL
         B         CKRETN20          20 - JOB NOT CANCELED ON OUTPUT
         B         CKRETN24          24 - JOBID WITH INVALID SYNTAX
         B         CKRETN28          28 - INVALID JOB, TSO USER OR STC
CKRETN0  BR        R10                    GOOD RETURN
*        MVC       MSG1(11),=C'GOOD RETURN'
*        BAL       R10,PUTMSG
*        B         NEXTPDE
CKRETN4  MVC       MSG1(8),SSCSJOBN
         MVC       MSG1+9(8),SSCSJOBI
         MVC       MSG1+18(16),=C'JOB(S) NOT FOUND'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN8  MVC       MSG1(25),=C'INVALID JOBNAME/JOB COMBO'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN12 MVC       MSG1(28),=C'JOB NOT CANCELED - DUPLICATE'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN16 MVC       MSG1(21),=C'STATUS ARRAY TO SMALL'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN20 MVC       MSG1(26),=C'JOB NOT CANCELED ON OUTPUT'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN24 MVC       MSG1(25),=C'JOBID WITH INVALID SYNTAX'
         BAL       R10,PUTMSG
         B         NEXTPDE
CKRETN28 MVC       MSG1(28),=C'INVALID JOB, TSO USER OR STC'
         BAL       R10,PUTMSG
         B         NEXTPDE
**********************************************************************
*
* GET MSG ROUTINE.
*
**********************************************************************
GETMSG   DS        0H
         L         R8,CPPLUPT
         L         R9,CPPLECT
         GETLINE   PARM=GETBLOCK,UPT=(R8),ECT=(R9),ECB=ECBADS,         *
               MF=(E,IOPLADS)
         BR        R10
**********************************************************************
*
*        PUTMSG ROUTINE.
*
**********************************************************************
PUTMSG   DS        0H
         L         R8,CPPLUPT
         L         R9,CPPLECT
         PUTLINE   PARM=PUTBLOCK,UPT=(R8),ECT=(R9),                    *
               ECB=ECBADS,OUTPUT=(PROMPT,TERM,SINGLE,DATA),            *
               MF=(E,IOPLADS)
         BR        R10
**********************************************************************
*
*        DUMMY ROUTINE FOR IKJPARS.
*
**********************************************************************
DUMMY    DS      0H
         LA      R15,0
         BR      R14
**********************************************************************
*
*
*
**********************************************************************
*
*
*------------------------ VARIALBELS -------------------------------
USER     MVC     USERID(1),0(R1)
USERCOMP CLC     USERID(1),0(R5)
NAME     MVC     JOBNAME(1),0(R5)
NAME2    MVC     JOBID(1),0(R5)
JOBNAME  DC      CL8' '
JOBID    DC      CL8' '
COMMAND  DC      CL8' '
USERID   DC      CL8' '
USERLEN  DC      H'00'
AUTHUSER DC      X'00'
GETSIZE  DC      A(16384)
SSOBPTR  DS      F
MYPPL    DS      7F
MYANS    DS      F
MYECB    DS      F
MYUPT    DS      F
MYECT    DS      F
PARSAREA DS      D
ECBADS   DC      F'0'
IOPLADS  DC      4F'0'
PROMPT   DS      0F
         DC      H'76',H'0'
MSG1     DC      CL72' '
PUTBLOCK PUTLINE MF=L
GETBLOCK GETLINE INPUT=(TERM,PHYSICAL),TERMGET=(EDIT,WAIT),MF=L
*-------------------------------------------------------------------
JOBS     IKJPARM
JOBNAME0 IKJPOSIT JOBNAME,LIST,PROMPT='ENTER JOBNAME'
         IKJENDP
SAVEAREA DS      18F
R0       EQU       0
R1       EQU       1
R2       EQU       2
R3       EQU       3
R4       EQU       4
R5       EQU       5
R6       EQU       6
R7       EQU       7
R8       EQU       8
R9       EQU       9
R10      EQU       10
R11      EQU       11
R12      EQU       12
R13      EQU       13
R14      EQU       14
R15      EQU       15
         LTORG
         IKJCPPL
         EJECT
         IKJPPL
         EJECT
         IEFJSSOB  (SO,CS),CONTIG=YES
         EJECT
         CVT
         END
