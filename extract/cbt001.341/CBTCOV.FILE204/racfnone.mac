          PROC 1 VOL REPLY() UACC()
          CONTROL MSG NOFLUSH
          /**********************************************************/
          /*  THIS CLIST WILL DISPLAY ALL DATASETS ON A SPECIFIED VOL*/
          /*  THAT ARE NOT DEFINED TO RACF. */
          /***********************************************************/
        GETTSO: -
          WRITENR ARE YOU IN SPF/TSO ?
          READ &ANS
          SET &REPLY=&ANS
          IF &REPLY= THEN -
            GOTO GETTSO
          IF &SUBSTR(1:1,&REPLY)=Y THEN -
            DO
              WRITE PLEASE EXIT SPF AND EXECUTE THIS CLIST AGAIN.
              GOTO EXIT
            END
          DELETE IEHLIST.DATA
          FREE ATTR(SYSP)
          ATTR SYSP BLKSIZE(80) LRECL(80) DSORG(PS) RECFM(F)
          FREE FI(DD1,SYSPRINT,SYSIN)
          ALLOC DD(SYSIN) USING(SYSP) NEW DELETE SPACE(1) TRACK DELETE-
            UNIT(V2314)
          FREE ATTR(SYSP)
          OPENFILE SYSIN OUTPUT
          SET &SYSIN=&STR( LISTVTOC VOL=DISK=&VOL)
          PUTFILE SYSIN
          CLOSFILE SYSIN
          ALLOC FI(DD1) UNIT(DISK) VOL(&VOL) NEW SP(0) TRACKS
          ALLOC FI(SYSPRINT) DA(IEHLIST.DATA)  NEW SP(1,1) CYL
          CALL 'SYS1.LINKLIB(IEHLIST)'
        OPEN: -
          OPENFILE SYSPRINT INPUT
        LOOP: -
          SET &LASTREC=&SYSPRINT
          GETFILE SYSPRINT
          IF &SUBSTR(2:4,&SYSPRINT)=IEH THEN -
            DO
              WRITE &SYSPRINT
              GOTO CLEANUP
            END
          IF &SUBSTR(3:11,&SYSPRINT)=&STR(THERE ARE) THEN -
            GOTO EOF
          SET RACFBIT=&SUBSTR(114:120,&STR(&SYSPRINT))
          IF &SUBSTR(1,&RACFBIT) EQ &STR( ) THEN -
            GOTO LOOP
          SET RACFBIT=&RACFBIT
          IF &RACFBIT=NONE THEN -
            DO
              SET DSNAME=&SUBSTR(2:45,&STR(&SYSPRINT))
              SET DSNAME=&DSNAME
              WRITE RACF BIT OFF ---> &DSNAME
        GETANS: -
              WRITENR CREATE A PROFILE FOR '&DSNAME' ?
              READ &ANS
              SET &REPLY=&ANS
              IF &REPLY= THEN -
                GOTO GETANS
              IF &SUBSTR(1:1,&REPLY)=Y THEN -
                GOTO CREPROF
            END
          GOTO LOOP
        CREPROF: -
          IF &UACC= THEN -
            DO
              WRITENR WHAT DO YOU WANT THE UACC OF THE DATASET TO BE ?
              READ &ANS
              SET &UACC=&ANS
              GOTO CREPROF
            END
          AD '&DSNAME' UACC(&UACC)
          GOTO LOOP
        EOF: -
          WRITE PROCESSING COMPLETE
        CLEANUP: -
          CLOSFILE SYSPRINT
        EXIT: -
          END
