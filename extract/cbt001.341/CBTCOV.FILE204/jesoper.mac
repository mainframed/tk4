         TITLE 'J E S O P E R  --  MVS CONSOLE READ PROGRAM  --  VERSIO*
               N 2.1'
         LCLC  &INST               INSTALLATION VARIABLE         GMM
&INST    SETC  'DWR'               INDIACTE DWR FOR THE VERSION GMM
*---------------------------------------------------------------------*
*                                                                     *
*                             J E S O P E R                           *
*                                                                     *
*                               08/29/80                              *
*                                                                     *
*                       OPERATOR CONSOLE MONITOR                      *
*                                                                     *
*     THIS PROGRAM DISPLAYS ALL ACTIVE OPERATORS CONSOLES ON          *
*     A 3270 TSO TERMINAL. SINCE THIS BUFFER CAN BE 35 LINES LONG,    *
*     IT MUST BE DISPLAYED IN TWO 'PAGES'. VARIOUS CONTROL            *
*     COMMANDS ARE AVAILABLE AND ARE DESCRIBED BELOW.                 *
*                                                                     *
*     COMMAND      DESCRIPTION                                        *
*                                                                     *
*        ?         DISPLAYS HELP FOR JESOPER                          *
*        E         END; END THE PROGRAM                               *
*        WXX       AUTOMATICALLY REFRESH THE SCREEN                   *
*                  XX TIMES, THEN RETURN TO NORMAL MODE.              *
*                  IF XX IS NOT ENTERED, 30 ITERATIONS IS ASSUMED.    *
*                  IF XX = 0, TIMER WILL CONTINUE UNTIL INTERRUPT     *
*                  IS PRESSED.                                        *
*        CXX       SWITCH DISPLAY TO SYSTEM CONSOLE XX                *
*                  IF XX IS NOT A VALID CONSOLE, '1' IS ASSUMED       *
*        DXX       SET DELAY TIME BETWEEN REFRESHES TO XX TENTHS      *
*                  OF A SECOND.                                       *
*        L         LINK TO RMFMON                                     *
         AIF       ('&INST' EQ 'DWR').NDWR3                        GMM
*        1         SHIFT TO DISPLAY MODE 1                            *
*        2         SHIFT TO DISPLAY MODE 2                            *
*        F         FREEZE DISPLAY ON THE CURRENT PAGE                 *
*        R         RELEASE DISPLAY; SHOW ALTERNATING PAGES            *
*     DISPLAY MODE 1 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 12 LINES                       *
*     DISPLAY MODE 2 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 22 LINES                       *
*                                                                     *
.NDWR3   ANOP                                                         *
*     HITTING THE ATTENTION KEY WHILE IN TIMER MODE WILL CAUSE        *
*     THE TIMER TO BE RESET TO ZERO AND WAIT MODE TERMINATED.         *
*                                                                     *
*     JESOPER GIVES A TSO USER THE CAPABILITY TO ENTER SYSTEM AND JES *
*     OPERATOR COMMANDS.                                              *
*     THE SYNATX OF THE JES AND OPERATOR COMMAND SUPPORT FOLLOWS: GMM *
*                                                                 GMM *
*     JES COMMAND:  $...  ANY JES COMMAND ...                     GMM *
*     OPER COMMAND: ...  ANY OPER COMMAND ...                     GMM *
*                                                                 GMM *
*                                                                 GMM *
*  THIS PROGRAM CAME FROM THE SHARE MODS TAPES AND HAS BEEN           *
*  MODIFIED AT OUR INSTALLATION FOR ADDITIONAL CAPABILITY.            *
*                                                                     *
*  DEAN WITTER REYNOLDS MAKES NO CLAIMS AS TO THE ACCURACY OF         *
*  THIS PROGRAM AND ACKNOWLEDGES THAT IT IS AN ENHANCEMENT OF         *
*  AN EXISTING PROGRAM.                                               *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         REGS  R
JESOPER  CSECT
         SAVE  (14,12),,*         BRANCH AROUND SAVE AREAS
         LR    R12,R15            R12 = ADDR OF ENTRY POINT
         USING JESOPER,R12,R11,R10 ADDRESABILITY TO CSECT          GMM
         LA    R11,4095(0,R12)    R11 WILL BE
         LA    R11,1(0,R11)       SECOND BASE REGISTER
         LA    R10,4095(0,R11)    R10 WILL BE                      GMM
         LA    R10,1(0,R10)       THIRD BASE REGISTER              GMM
         LA    R14,SAVE           R14 = ADDR OF OUR SAVE AREA
         ST    R13,SAVE+4         SAVE POINTER TO CALLERS SAVE AREA
         ST    R14,8(0,R13)       SAVE PTR TO OUR SAVE AREA IN CALLER'S
         LR    R13,R14            R13 = ADDR OF OUR SAVE AREA
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
*---------------------------------------------------------------------*
         LA    R3,STAXLIST        R3 = ADDRESS OF STAX LIST MACRO
         STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         BAL   R14,TESTAUTH       GO SEE IF USER IS AUTHORIZED     GMM
         TM    AUTH,AUTHOPR       DOES HE HAVE OPERATOR PRIVS ?    GMM
         BO    AUTHOK             YES ? OK LET HIM CONTINUE        GMM
         XC    PSWDFG,PSWDFG      TURN OFF COMMAND AUTHORIZATION   JL1
         MVC   ERROR(37),ERRMSG5  MOVE UNAUTH MESSAGE TO BUFFER    GMM
         MVC   ERROR+37(8),LOGON  COPY LOGON ID TO BUFFER          GMM
         TPUT  ERROR,44           WRITE OUT THE MSG TO THE USER    GMM
         B     ALLDONE            GO RETURN CONTROL TO USER ...    GMM
         SPACE 2
AUTHOK   GTSIZE
         LTR   R0,R0              R0 = NUMBER OF LINES PER SCREEN
         BZ    HARDCOPY           IF NONZERO ASSUME A CRT IS IN USE
         SPACE
CRT      STH   R0,LPSCREEN        R0 = LINES PER SCREEN
         STH   R1,CPLINE          R1 = CHARACTERS PER LINE
         CH    R0,=H'24'          IS USER ON A 3277?
         BE    FLSCREEN           YES, JUST CONTINUE
         MVI   MOD4FLG,X'FF'      SET 3278-4 FLAG ON
         MVC   CMDCTRL(3),R41C1   ROW 41, COL 1
         MVC   PHEADING(3),R42C1  ROW 42, COL 1
         MVC   R24C1(3),R43C1     ROW 43, COL 1
FLSCREEN STFSMODE ON,INITIAL=YES  TURN ON VTAM FULL SCREEN MODE
         B     BLDUCMS
         SPACE
HARDCOPY STSIZE SIZE=80           OTHERWISE, HARDCOPY; SET LSIZE=80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CMDCTRL(6),BLANKS  ZAP OUT 3277 CNTRL CHARS
         MVC   PHEADING(6),BLANKS ZAP OUT 3277 CNTRL CHARS
         MVC   HELP(14),BLANKS    ZAP OUT 3277 CNTRL CHARS
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
*---------------------------------------------------------------------*
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R2,R2              R2  = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(0,R8)         SAVE UCM ADDRESS IN UCMTAB
         LA    R2,1(0,R2)         R2  = R2  + 1  (ONE MORE UCM)
         LA    R8,4(0,R8)         R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R2,NUMUCMS         SAVE NUMBER OF UCMS FOUND
         DROP  R4
*---------------------------------------------------------------------*
*        SCAN THE UCM FOR THE MASTER CONSOLE AS THE DEFAULT        GMM*
*        CONSOLE WHEN ENTERING THE "JESOPER" COMMAND.              GMM*
*---------------------------------------------------------------------*
SETMAST  LA    R4,1               SET DEFAULT TO FIRST CONSOLE     JL1
MASTSCAN LA    R5,UCMTAB          SET POINTER TO UCM ADDRESS TABLE GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?              GMM
         BNH   MASTCONT           NO, CONTINUE                     GMM
         ABEND 1                  IMPOSSIBLE .. NO MASTER CONSOLE ?GMM
MASTCONT SLL   R4,2               MAKE CONSOLE NUMBER TABLE INDEX  GMM
         LA    R5,0(R5,R4)        SET POINTER TO UCM ADDRESS SLOT  GMM
         L     R5,0(0,R5)         SET POINTER TO UCM               GMM
         USING UCMLIST,R5         TELL ASSEMBLER ABOUT UCM         GMM
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?        GMM
         BO    MASTFND            YES ? FOUND THE MASTER CONSOLE ..GMM
         SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE GMM
         LA    R4,1(0,R4)         SET POINTER TO NEXT CONSOLE      GMM
         B     MASTSCAN           CONTINUE MASTER CONSOLE SCAN LOOPGMM
MASTFND  SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE GMM
         ST    R4,OLDCONS         SET VALID OLD CONSOLE NUMBER     GMM
         ST    R4,CONSOLE         SET VALID CONSOLE NUMBER (MASTER)GMM
         DROP  R5                 DONT NEED UCM ADDRESSABILITY NOW GMM
*---------------------------------------------------------------------*
*                                                                     *
*                            TOP OF LOOP                              *
*              LOCATE SCREEN BUFFER AND PREPARE TO TPUT               *
*                                                                     *
*---------------------------------------------------------------------*
NEXTPAGE CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    NOATTN             NO
*                                 ATTENTION KEY HIT; PROCESS IT
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITFLG,X'00'      TURN OFF WAIT FLAG
         XC    TIMER,TIMER        SET TIMER TO 0
NOATTN   EQU   *
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         ICM   R4,15,CONSOLE      R4 = CONSOLE TO BE DISPLAYED
         BZ    WRTERR4            WE HAVE NO CONSOLE ZERO (MASTER) GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
         MVC   ERROR(26),ERRMSG1  CONSOLE NUMBER INVALID
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(0,R5)         R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING IEERDCM,R6                                          GMM
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
WRTERR4  MVC   ERROR(26),ERRMSG4  NON-GRAPHIC CONSOLE              GMM
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   CTYPE(28),BLANKS   BLANK OUT CONSOLE TYPE FIELD
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH0              NO
         MVC   MASTER(6),=CL6'MASTER' YES
AUTH0    TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   SYS(3),=CL3'SYS'   YES
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   IO(3),=CL3'I/O'    YES
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CONS(4),=CL4'CONS' YES
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         MVC   UNIT(3),13(R7)     MOVE UNIT ADDR INTO LINE
         L     R7,DCMADTRN        GET ADDRESS OF TDCM              GMM
         L     R7,0(0,R6)         R7 = ADDR OF TDCM                GMM
         USING IEETDCM,R7                                          GMM
         SPACE
*---------------------------------------------------------------------*
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
*---------------------------------------------------------------------*
         L     R3,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R3,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET KEY=ZERO                                          GMM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         MVC   LASTLINE(79),DCMINPUT   MOVE IN INPUT BUFFER LINE   GMM
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*                    CHECK FOR INTEGRATED CONSOLE                     *
*                                                                     *
*     IF THIS IS A INTEGRATED CONSOLE, CHECK IF THE USER HAS A        *
*     327X-2 OR THE LARGE-SCREEN 327X-4.                              *
*                                                                     *
*---------------------------------------------------------------------*
CHKINTEG MVI   INTEGFLG,X'FF'     TURN ON INTEGRATED CONSOLE FLAG
         CLC   DCMMSGAL(2),=H'30' IS THIS REALLY INTEGRATED CONSOL GMM
         BE    CHKMODEL           YES
         CLC   DCMMSGAL(2),=H'19' IS THIS A TYPE 3277 ?            GMM
         BE    CONTXXXX           YES
         ABEND 777                WHAT IS IT
CONTXXXX MVI   INTEGFLG,X'00'     NO; TURN OFF INTEGRATED CONSOLE FLAG
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD2TPUT SET TPUT LENGTH FOR SHORT SCREEN
         B     MOD2               AND TREAT AS MOD2 FOR NOW
         SPACE 1
CHKMODEL CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?
         BNE   MOD2               NO - MUST BE A MOD2
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD4TPUT SET LENGTH FOR FULL 3278-4 SCREEN
*        L     R8,DCMASCRN        R8 = ADDR OF SCREEN BUFFER       GMM
         L     R8,48(0,R7)        R8 = ADDR OF SCREEN BUFFER       GMM
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M4BUFLEN        R5 = 3278-4 BUFFER LEN (35 LINES)
         LA    R9,M4BUFLEN        R9 = 3278-4 BUFFER LEN (35 LINES)
         B     MOVEBUFF
         SPACE 2
MOD2     CLI   PAGE,C'1'          ARE WE ON PAGE 1?
         BNE   ONTWO              NO, SO WE MUST BE ON 2
         CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 1?
         BNE   PAGE2              NO, SO DISPLAY PAGE 2
         B     PAGE1              YES, SO DISPLAY PAGE 1
ONTWO    CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 2?
         BE    PAGE2              YES, SO DISPLAY PAGE 2
PAGE1    MVI   PAGE,C'1'          PAGE = 1
*        L     R8,DCMASCRN        R8 = ADDR OF SCREEN IMAGE BUFFER GMM
         L     R8,48(0,R7)        R8 = ADDR OF SCREEN BUFFER       GMM
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUF (21 LINES)
         LA    R9,M2BUFLEN        R9 = CONSOLE BUFFER LEN   (21 LINES)
         B     MVETRAIL           GO MOVE THE BUFFER
PAGE2    MVI   PAGE,C'2'          PAGE = 2
*        L     R8,DCMASCRN        R8 = ADDR OF SCREEN IMAGE BUFFER GMM
         L     R8,48(0,R7)        R8 = ADDR OF SCREEN BUFFER       GMM
         CLI   MODE,C'2'          ARE WE IN DISPLAY MODE 2?
         BE    DMODE2             YES, BRANCH TO DMODE2
DMODE1   LA    R8,LEN22(R8)       MOVE POINTER DOWN 23 LINES
         LA    R9,LEN13           R9 = LENGTH OF LAST 12 LINES
         B     CONTINUE           JUMP AROUND MODE 2 DISPLAY
DMODE2   LA    R8,LEN9(R8)        MOVE POINTER DOWN 9 LINES
         LA    R9,LEN21           R9 = LENGTH OF SOURCE BUFFER
CONTINUE LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUFFER
MVETRAIL MVC   ENDMOD2(TRAILEN),CMDCTRL MOVE IN TRAILER
MOVEBUFF ICM   R9,8,PAD           MAKE BLANK THE PAD CHARACTER
         MVCL  R4,R8              MOVE CONSOLE BUFFER TO OUTPUT BUFFER
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET KEY=NZERO                                         GMM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         DROP  R5,R6,R7                                            GMM
         CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    TPUTCRT            YES
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*      DISPLAY THE OPERATOR'S SCREEN ON A TSO HARDCOPY TERMINAL       *
*                                                                     *
*---------------------------------------------------------------------*
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,BUF             SET POINTER TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R3,R1              SAVE R1 SINCE TPUT ZAPS IT
         TPUT  (1),(0),R          PRINT ONE LINE ON HARDCOPY
         LA    R8,1(0,R8)         ADD 1 TO COUNTER
         C     R8,=F'21'          HAVE WE PRINTED LAST LINE?
         BE    DOLAST2            YES, CONTINUE
         LA    R1,80(0,R3)        NOPE, POINT TO NEXT LINE
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONSOLE?
         BE    NOT3270            YES
         MVC   0(5,R1),BLANKS     BLANK OUT 3270 CTRL CHARS
         LA    R1,4(0,R1)         ADD 4 EXTRA BYTES TO SKIP CTRL CHARS
NOT3270  L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
DOLAST2  TPUT  LASTLINE,79,EDIT   DISPLAY OPER CMD LINE
         TPUT  HEADING,79,EDIT    DISPLAY CONSOLE STATUS LINE
         TPUT  USERLINE,79,EDIT   DISPLAY USER CMD LINE
         B     INPUT
         SPACE 5
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*               DISPLAY THE OPERATORS SCREEN ON A 3277                *
*                                                                     *
*---------------------------------------------------------------------*
TPUTCRT  LA    R1,CLEAR           R1 = ADDR OF OUTPUT STREAM
         L     R0,TPUTLEN         R0 = LENGTH OF TPUT
         ICM   R1,8,FULLSCR       SET ASIS TYPE FOR TPUT
         TPUT  (1),(0),R          DISPLAY ENTIRE SCREEN
INPUT    MVC   ERROR(32),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITFLG,X'FF'      IS THE WAIT FLAG ON?
         BNE   READCHAR           NO, SO GO GET A COMMAND
         STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         L     R2,TIMER           R2 = CURRENT VALUE OF TIMER
         BCTR  R2,0               TIMER = TIMER - 1
         ST    R2,TIMER           STORE NEW VALUE OF TIMER
         CVD   R2,WORK            CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         LTR   R2,R2              HAS TIMER HIT ZERO?
         BNZ   NEXTPAGE           NO, CONTINUE TO COUNT
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         XI    WAITFLG,X'FF'      TOGGLE WAIT FLAG OFF
         B     NEXTPAGE           AND GO ON AS IF NOTHING HAPPENED..
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                       READ COMMAND FROM USER                        *
*                                                                     *
*---------------------------------------------------------------------*
READCHAR MVC   IBUFFER(80),BLANKS BLANK OUT INPUT BUFFER ...       GMM
         MVC   REPLY-1(81),BLANKS BLANK OUT REPLY BUFFER ...       GMM
         MVI   CLEAR,X'C1'        TURN OFF BELL BIT IN WCC         JL2
         TGET  IBUFFER,80,ASIS    GET 79 CHARACTERS FROM TERMINAL  GMM
         CH    R15,=H'12'         IS INPUT LONGER THAN BUFFER ?    GMM
         BNE   NOBCLEAR           NO ? DONT CLEAR BUFFER ...       GMM
         TCLEARQ INPUT            FLUSH EXCESS CHARACTERS          GMM
NOBCLEAR IC    R2,IBUFFER         GET THE CONTROL CHARACTER        GMM
         N     R2,=X'0000000F'    EXTRACT PFK NUMBER               GMM
         CH    R2,=H'1'           PF KEY NUMBER ONE OR GREATER ?   GMM
         BL    NOTPFK             NO ? NO A PROGRAM FUNCTION KEY   GMM
         CH    R2,=H'12'          PF KEY NUMBER TWELVE OR LOWER ?  GMM
         BH    NOTPFK             NO ? NO A PROGRAM FUNCTION KEY   GMM
         BCTR  R2,0               MAKE PFK NUMBER RELATIVE ...     GMM
         SLL   R2,2               MAKE PFK NUMBER A TABLE INDEX    GMM
         LA    R1,PFKTAB          SET POINTER TO PFK TABLE         GMM
         L     R1,0(R2,R1)        GET ADDRESS OF PKF TEXT ...      GMM
         SR    R3,R3              CLEAR R3 FOR LENGTH              GMM
         IC    R3,0(0,R1)         GET LENGTH OF SUBSTITUTION TEXT  GMM
         N     R3,=X'0000000F'    EXTRACT REPLACEMENT TEXT LENGTH  GMM
         BCTR  R3,0               DECREMENT TEXT LENGTH FOR MVC    GMM
         EX    R3,PFKMVC          COPY SUBSTITUTION TEXT           GMM
         TM    0(R1),X'10'        CAN THIS HAVE APPENDED TEXT ?    GMM
         BNO   INGBUFER           NO ? IGNORE USER INPUT BUFFER    GMM
*        IS THERE ANY TEXT IN THE USER BUFFER ?                    GMM
         CLC   IBUFFER+6(74),BLANKS                                GMM
         BE    INGBUFER           NO ? DONT APPEND USER TEXT ...   GMM
         LA    R2,REPLY(R3)       SET POUNTER TO COMMAND SLOT      GMM
         TM    0(R1),X'20'        CAN THIS HAVE A COMMA ?          GMM
         BO    INGCOMMA           YES ? INSERT A COMMA THEN ...    GMM
         MVI   0(R2),C','         PLACE A COMMA IN FOR APPEND      GMM
         LA    R2,1(0,R2)         SET POINTER TO NEXT SLOT         GMM
INGCOMMA MVC   0(68,R2),IBUFFER+6 APPEND USER TEXT                 GMM
         B     INGBUFER           DONT COPY ENTIRE USER BUFER CMD  GMM
*        MAKE A COPY OF THE INPUT BUFFER ...                       GMM
NOTPFK   MVC   REPLY-1(73),IBUFFER+6                               GMM
INGBUFER CLI   REPLY-1,X'6E'      IS THIS A RESHOW? (VTAM ONLY)
         BE    NEXTPAGE           YES, JUST GO REFRESH
         CLI   REPLY-1,C' '       JUST A BLANK?
         BE    NEXTPAGE           YES, JUST GO REFRESH
         OC    REPLY-1(80),BLANKS CONVERT CHARS TO UPPER CASE
         CLI   REPLY-1,C'.'       IS IT A "INTERNAL" COMMAND ?
         BE    WAITCMD            NO ? IT MAY BE AN "END" COMMAND ...
         CLC   REPLY-1(3),=C'END' IS IT AN END COMMAND ?
         BE    DONE               YES ? GO EXIT JESOPER COMMAND    GMM
         CLC   REPLY-1(4),=C'QUIT'                                 GMM
         BE    DONE               YES ? GO EXIT JESOPER COMMAND    GMM
         CLC   REPLY-1(4),=C'EXIT'                                 GMM
         BE    DONE               YES ? GO EXIT JESOPER COMMAND    GMM
         B     PVSCMD
* PF-KEY DEFINITIONS
PFKTAB   DS    0F
         DC    A(PF1)                                              GMM
         DC    A(PF2)                                              GMM
         DC    A(PF3)                                              GMM
         DC    A(PF4)                                              GMM
         DC    A(PF5)                                              GMM
         DC    A(PF6)                                              GMM
         DC    A(PF7)                                              GMM
         DC    A(PF8)                                              GMM
         DC    A(PF9)                                              GMM
         DC    A(PF10)                                             GMM
         DC    A(PF11)                                             GMM
         DC    A(PF12)                                             GMM
PF1      DC    X'02',CL2'.H'                                       JL1
PF2      DC    X'02',CL2'.?'                                       JL1
PF3      DC    X'03',CL3'END'                                      JL1
PF4      DC    X'02',CL2'.C'                                       JL1
PF5      DC    X'03',CL3'D D'                                      JL1
PF6      DC    X'03',CL3'.W0'                                      JL1
PF7      DC    X'04',CL4'.W99'                                     JL1
PF8      DC    X'03',CL3'.D1'                                      JL1
PF9      DC    X'04',CL4'.D10'                                     JL1
PF10     DC    X'05',CL5'D R,L'                                    JL1
PF11     DC    X'05',CL5'D A,L'                                    JL1
PF12     DC    X'05',CL5'D J,L'                                    JL1
PFKMVC   MVC   REPLY-1(*-*),1(R1)  COPY PFK TEXT INTO PROCESS BUFR GMM
*---------------------------------------------------------------------*
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
*---------------------------------------------------------------------*
WAITCMD  CLI   REPLY,C'W'         DO WE SHIFT TO WAIT MODE?
         BNE   GETHELP            NO, SO CONTINUE
         XI    WAITFLG,X'FF'      TURN ON WAIT FLAG
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,TIMER           STORE STARTING TIMER VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
*---------------------------------------------------------------------*
*                                                                     *
*           ?  --  LIST HELP FOR JESOPER COMMANDS ON TERMINAL         *
*                                                                     *
*---------------------------------------------------------------------*
GETHELP  CLC   REPLY(2),=C'? '    IS HE ASKING FOR HELP?           GMM
         BNE   CDELAY             NO,SO CONTINUE
         LA    R1,HELP            R1 = ADDR OF HELP PAGE
         LA    R0,HLENGTH         R0 = LENGTH OF HELP PAGE
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS
         TPUT  (1),(0),R          DISPLAY HELP
         B     READCHAR
*---------------------------------------------------------------------*
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
*---------------------------------------------------------------------*
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   CCONSOLE           NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
*---------------------------------------------------------------------*
*                                                                     *
*                    C  --  SET CONSOLE NUMBER                        *
*                                                                     *
*---------------------------------------------------------------------*
CCONSOLE CLI   REPLY,C'C'         DO WE CHANGE CONSOLES?
         BNE   END                NO, SO CONTINUE
         CLI   REPLY+1,C' '       DO WE FIND THE MASTER CONSOLE ?   JL1
         BE    SETMAST            YES ? GO FIND IT                  JL1
         L     R2,CONSOLE         SET DEFAULT CONSOLE
         ST    R2,OLDCONS         SAVE OLD CONSOLE #
         LA    R15,CONVBIN        BRANCH TO
         BALR  R14,R15            EBCDIC->BINARY CONVERTOR
         ST    R2,CONSOLE         STORE R2 AWAY AS CONSOLE NUMBER
         B     NEXTPAGE           CONTINUE
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                        E  --  TERMINATE JESOPER                     *
*                                                                     *
*---------------------------------------------------------------------*
END      CLC   REPLY(2),=C'E '    DOES HE WANT TO QUIT ?           GMM
         BE    DONE               YES, SO QUIT
         CLC   REPLY(3),=C'END'   DOES HE WANT TO QUIT ?           GMM
         BE    DONE               YES, SO QUIT
         AIF   ('&INST' EQ 'DWR').NDWR5                            GMM
*---------------------------------------------------------------------*
*                                                                     *
*               F  --  FREEZE DISPLAY ON CURRENT PAGE                 *
*                                                                     *
*---------------------------------------------------------------------*
F        CLI   REPLY,C'F'         IS IT AN 'F'?
         BNE   R                  NO, SO CONTINUE ON
         MVI   FREEZE,C'F'        TURN ON FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
*---------------------------------------------------------------------*
*                                                                     *
*           R  --  RELEASE FREEZE ON CURRENT PAGE DISPLAY             *
*                                                                     *
*---------------------------------------------------------------------*
R        CLI   REPLY,C'R'         IS IT AN 'R'?
         BNE   MODE1              NO, SO CONTINUE ON
         MVI   FREEZE,C'R'        TURN OFF FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
*---------------------------------------------------------------------*
*                                                                     *
*                1  --  SHIFT TO MODE 1 TYPE DISPLAY                  *
*                                                                     *
*---------------------------------------------------------------------*
MODE1    CLI   REPLY,C'1'         DO WE SHIFT TO MODE 1 DISPLAY?
         BNE   MODE2              NO, SO CONTINUE
         MVI   MODE,C'1'          SET MODE INDICATOR
         B     NEXTPAGE
*---------------------------------------------------------------------*
*                                                                     *
*                2  --  SHIFT TO MODE 2 TYPE DISPLAY                  *
*                                                                     *
*---------------------------------------------------------------------*
MODE2    CLI   REPLY,C'2'         DO WE SHIFT TO MODE 2 DISPLAY?
         BNE   BYPASS             NO, SO CONTINUE                  JL1
         MVI   MODE,C'2'          SET MODE INDICATOR
         B     NEXTPAGE
         EJECT
.NDWR5   ANOP                                                      GMM
*---------------------------------------------------------------------*
*                                                                     *
*                        B  --  PASSWORD COMMAND SCAN                 *
*                                                                     *
*---------------------------------------------------------------------*
BYPASS   XC    PSWDFG,PSWDFG      TURN OFF COMMAND AUTHORIZATION   JL1
         CLI   REPLY,C'B'         IS IT A 'B'?                     JL1
         BNE   HELP2              NO, CONTINUE                     JL1
         L     R4,16                                               JL1
         USING CVT,R4                                              JL1
         L     R8,CVTTCBP                                          JL1
         L     R8,12(0,R8)                                         JL1
         LR    R4,R8                                               JL1
         USING ASCB,R4                                             JL1
         L     R8,ASCBTSB                                          JL1
         LR    R4,R8                                               JL1
         USING TSB,R4                                              JL1
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET KEY=ZERO                                          JL1
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         MVC   USPSWD(8),TSBPSWD                                   JL1
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET KEY=NZERO                                         JL1
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         CLC   USPSWD(8),BLANKS    IS THE LOGON PSWD BLANK ?       JL1
         BNE   USELOGON            NO ? USE IT                     JL1
         OI    PSWDFG,PGMPSWD      WE HAVE TO USE PROGRAM PASSWORD JL1
USELOGON LA    R1,PSWDRD           R1 = ADDR OF PASSWORD PAGE      JL1
         LA    R0,PLENGTH1         R0 = LENGTH OF PASSWORD PAGE    JL1
         ICM   R1,8,FULLSCR        INSERT ASIS CNTL CHARS          JL1
         TPUT  (1),(0),R           DISPLAY PASSWORD PAGE           JL1
         MVC   IBUFFER(80),BLANKS BLANK OUT INPUT BUFFER ...       JL1
         MVC   REPLY-1(81),BLANKS BLANK OUT REPLY BUFFER ...       JL1
         TGET  IBUFFER,80,ASIS    GET 79 CHARACTERS FROM TERMINAL  JL1
         LTR   R1,R1               WAS A PASSWORD TYPED ?          JL1
         BZ    NEXTPAGE            NO ? EXIT THIS ROUTINE          JL1
         OC    IBUFFER+6(8),BLANKS   MAKE IT UPPER CASE            JL1
         TM    PSWDFG,PGMPSWD      USE PROGRAM PASSWORD ?          JL1
         BZ    LOGPASS             NO ? USE LOGON PASSWORD         JL1
         CLC   IBUFFER+6(8),PASSWORD IS THE PASSWORD CORRECT ?     JL1
         BE    OKPASS              YES ? GIVE HIM PRIVILEGE TO CMD JL1
         B     INVPSWD             ELSE ERROR                      JL1
LOGPASS  CLC   IBUFFER+6(8),USPSWD IS THE PASSWORD CORRECT ?       JL1
         BE    OKPASS              YES ? GIVE HIM PRIVILEGE TO CMD JL1
INVPSWD  MVC   ERROR(26),INVPASS  PASSWORD WAS INVALID             JL1
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     NEXTPAGE                                            JL1
OKPASS   OI    PSWDFG,OKAUTH      GIVE HIM AUTHORIZATION           JL1
         B     NEXTPAGE                                            JL1
         DROP  R4                                                  JL1
*---------------------------------------------------------------------*
*                                                                     *
*                        H  --  HELP                                  *
*                                                                     *
*---------------------------------------------------------------------*
HELP2    CLI   REPLY,C'H'         IS IT A 'H'?                     JL1
         BNE   LINKRMF            NO, CHECK FOR LINK COMMAND       JL1
         LA    R1,HELPPFK         R1 = ADDR OF HELP PAGE           JL1
         LA    R0,HLENGTH2        R0 = LENGTH OF HELP PAGE         JL1
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS           JL1
         TPUT  (1),(0),R          DISPLAY HELP                     JL1
         B     READCHAR                                            JL1
LINKRMF  CLI   REPLY,C'L'         IS IT AN 'L' ?                   JL2
         BNE   INVCMD             NO ? INVALID COMMAND             JL2
         TM    CNTLFG,LOAD        WAS LOAD DONE ?                  JL2
         BO    NOLOD              YES ? THEN DONT DO IT AGAIN      JL2
         LOAD  EP=RMFMON          LOAD THE RMFMON COMMAND          JL2
         ST    R0,RMFPTR          SAVE ENTRY POINT                 JL2
         OI    CNTLFG,LOAD        INDICATE LOAD DONE               JL2
NOLOD    L     R15,RMFPTR         GET ENTRY POINT                  JL2
         BALR  R14,R15            GO PROCESS RMFMON COMMAND        JL2
         B     NEXTPAGE            GO GET ANOTHER COMMAND          JL2
INVCMD   MVC   ERROR(26),ERRMSG3  COMMAND WAS INVALID              JL1
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     NEXTPAGE                                            JL1
*---------------------------------------------------------------------*
*                                                                     *
*        PROCESS POSSIBLE VS OR JES COMMANDS                          *
*                                                                     *
*---------------------------------------------------------------------*
PVSCMD   CLI   REPLY-1,C'$'       IS THIS A MVS COMMAND ?          GMM
         BE    PJESCMD            NO ? IT MAY BE A JES COMMAND ... GMM
         MVC   PCMD(80),REPLY-1   COPY REPLY BUFFER INTO CMD BUFFR GMM
         CLC   PCMD,BLANKS        IS THERE A COMMAND ?             GMM
         BE    BADCMD             NO ? GO WRITE OUT AN ERROR       GMM
PRCACMD  TM    AUTH,AUTHOPR       IS HE AN AUTHORIZED USER ?       GMM
         BNO   BADCMD             NO ? TELL HIM ITS A INVALID CMD  GMM
         LA    R1,PCMD+79         POINT TO END OF COMMAND BUFFER   GMM
         LA    R2,PCMD            SET POINTER TO COMMAND BUFFER    GMM
PCLOOP   CLI   0(R1),C' '         SCAN BACKWARDS FOR A NON-BLANK   GMM
         BNE   FOUNDCMD           WE FOUND A COMMAND ... SEND IT   GMM
         CR    R1,R2              HAVE WE HIT THE END OF THE CMD   GMM
         BNH   BADCMD             YES ? GO TELL USER OF ERROR      GMM
         BCT   R1,PCLOOP          OTHERWISE CONTINUE LOOKING       GMM
FOUNDCMD MVC   REPLY-1,BLANKS     BLANK OUT REPLY AREA             GMM
         LA    R1,1(0,R1)         BUMP UP COMMAND END POINTER      GMM
         SR    R1,R2              GET THE LENGTH OF THE COMMAND    GMM
         BNP   BADCMD             NOT POSITIVE ... INVALID CMD     GMM
         LA    R1,4(0,R1)         ADD LENGTH OF CONTROL FIELD      GMM
         STH   R1,PCMDLEN         STORE THE COMMAND LENGTH         GMM
         B     SENDCMD            GO SHIP THE COMMAND OUT ...      GMM
PJESCMD  MVC   PCMD(80),REPLY-1   MAKE A COPY OF THE COMMAND       GMM
         CLC   PCMD+1(79),BLANKS  IS THERE A COMMAND ?             GMM
         BE    BADCMD             NO ? GO PROCESS AS ERROR         GMM
         B     PRCACMD            PARSE AND CHECK THE COMMAND      GMM
SENDCMD  EQU   *
         TM    PSWDFG,OKAUTH       CAN HE ISSUE ALL COMMANDS ?     JL1
         BO    ALLCMDS             YES ? DON'T CHECK TABLE         JL1
         LA    R1,CMDTAB           POINT TO COMMAND TABLE          JL1
CMDSCN   SR    R2,R2               CLEAR R2 FOR LENGTH             JL1
         IC    R2,0(R1)            GET LENGTH OF COMMAND           JL1
         LTR   R2,R2               END OF TABLE ?                  JL1
         BZ    BADCMD              YES ? INVALID COMMAND           JL1
         BCTR  R2,0                MAKE ONE LESS FOR EXECUTE       JL1
         EX    R2,CHKCMD           IS THIS COMMAND ALLOWED ?       JL1
         BE    ALLCMDS             YES ? PROCESS THE COMMAND       JL1
         LA    R1,2(R1)            POINT TO COMMAND                JL1
         AR    R1,R2               POINT TO LENGTH OF NEXT COMMAND JL1
         B     CMDSCN              CONTINUE TABLE SCAN             JL1
CHKCMD   CLC   1(*-*,R1),PCMD                                      JL1
ALLCMDS  LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO GMM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         L     R0,CONSOLE         GET CONSOLE IDENTIFIER           GMM
         LA    R1,PCOMMAND        SET POINTER TO POSSIBLE COMMAND  GMM
         SVC   34                 PLACE IT INTO THE COMMAND CHAIN  GMM
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET MF=(E,PROBMOD)   GET BACK PROB STATE AND TCB KEY  GMM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
         XC    PCMDLEN,PCMDLEN    ZAP JES OR VS COMMAND LENGTH     GMM
         MVC   PCMD,BLANKS        BLANK OUT COMMAND BUFFER         GMM
         B     NEXTPAGE           GO WRITE OUT NEW OPER SCREEN     GMM
BADCMD   MVC   ERROR(26),ERRMSG3  COMMAND WAS INVALID
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     NEXTPAGE
         SPACE 5
DONE     CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?
         BE    ALLDONE            YES
         TPUT  CLR,CLRLEN,FULLSCR NO, LETS CLEAR THE SCREEN FIRST
         STFSMODE OFF             TURN OFF FS MODE
ALLDONE  L     R13,SAVE+4         RESTORE POINTER TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         LA    R15,0
         BR    R14                RETURN TO SYSTEM
         AIF   ('&INST' NE 'DWR').NDWR6                            GMM
.NDWR6   ANOP                                                      GMM
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
*---------------------------------------------------------------------*
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(26),ERRMSG2  CONSOLE NUMBER ERROR
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     RTRN
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
*---------------------------------------------------------------------*
ATTNEXIT LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         BR    R14                RETURN TO CALLER
         DROP  R7
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
*---------------------------------------------------------------------*
SAVE     DC    18F'0'             SAVE AREA
WORK     DS    D                   WORK AREA FOR PACKS
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP
OLDCONS  DC    F'1'                PREVIOUS CONSOLE NUMBER
CONSOLE  DC    F'1'                CONSOLE TO BE LOOKED AT
LOGON    DC    CL8' '              LOGON USER ID                   GMM
PROFID   DC    CL8' '              USER ID SET VIA PROFILE COMMAND GMM
PROFLEN  DC    X'00'               LENGTH OF USER SET PROFILE      GMM
AUTH     DC    X'00'           JESOPER COMMAND AUTHORIZATION BYTE  GMM
AUTHOPR  EQU   X'80'               TSO OPERATOR PRIVILEGE          GMM
*        JES AND VS COMMAND BUFFER AND LENGTH                      GMM
PCOMMAND EQU   *                   JES OR VS COMMAND BLOCK         GMM
PCMDLEN  DC    H'0'                LENGTH OF COMMAND BLOCK         GMM
         DC    H'0'                UNUSED NIBBLE                   GMM
PCMD     DC    CL80' '             "DWR" OPERATOR COMMAND          GMM
TPUTLEN  DC    A(MOD2LEN)          LENGTH OF MOD 2 TPUT
MOD4TPUT DC    A(MOD4LEN)          LENGTH OF MOD4 TPUT
MOD2TPUT DC    A(MOD2LEN)          LENGTH OF MOD2 TPUT
LPSCREEN DC    H'0'                LINES PER SCREEN
CPLINE   DC    H'0'                CHARACTERS PER LINE
TIMER    DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'100'              DELAY FOR 100 HUNDREDTHS OF A SECOND
MOD4FLG  DC    X'00'               X'FF' INDICATES 3278-4 IN USE
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
CRTFLAG  DC    X'FF'               X'FF' INDICATES CRT IN USE
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
INTEGFLG DC    X'00'               INTEGRATED CONSOLE FLAG
FULLSCR  DC    X'03'               TPUT ASIS FLAG
EDITFLG  DC    X'00'               TPUT EDIT FLAG
NULLS    DC    80X'00'             JUST NULLS
R41C1    DC    X'11F240'           3278-4  --  ROW 41, COL 1
R42C1    DC    X'11F350'           3278-4  --  ROW 42, COL 1
R43C1    DC    X'11F460'           3278-4  --  ROW 43, COL 1
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
REPLYCDE DC    CL1' '              USERS COMMAND PREFIX FIELD
REPLY    DC    CL80' '             USERS COMMAND INPUT FIELD
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
STAXLIST STAX  ATTNEXIT,MF=L
BLANKS   DC    CL80' '
IBUFFER  DC    CL80' '             INPUT BUFFER FOR PFK PROCESSING GMM
PASSWORD DC    CL8'I01IO'          PASSWORD FOR COMMAND AUTHORIZATION
USPSWD   DC    CL8' '                                              JL1
PSWDFG   DC    X'00'                                               JL1
OKAUTH   EQU   X'01'                                               JL1
PGMPSWD  EQU   X'02'                                               JL1
RMFPTR   DS    F                   ENTRY POINT TO RMFMON           JL2
CNTLFG   DC    X'00'               CONTROL FLAG                    JL2
LOAD     EQU   X'80'               RMFMON WAS LOADED               JL2
         SPACE 2
         CNOP  0,8
*---------------------------------------------------------------------*
*                                                                     *
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*                                                                     *
*---------------------------------------------------------------------*
CLR      DC    X'C1'              WCC - CLEAR SCREEN
         DC    X'115D7E'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'13'              INSERT CURSOR
CLRLEN   EQU   *-CLR
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*                  DISPLAY SCREEN - HEADER SECTION                    *
*                                                                     *
*---------------------------------------------------------------------*
HEADER   EQU   *
CLEAR    DC    X'C1'              WCC
         DC    X'115D7F'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'1D60'            ATTR BYTE - PROTECTED FIELD
         DC    X'40'
*---------------------------------------------------------------------*
*                                                                     *
*               DISPLAY SCREEN - IMAGE BUFFER SECTION                 *
*                                                                     *
*---------------------------------------------------------------------*
BUF      DC    21CL80' '          OPERATORS SCREEN BUFFER
         DC    14CL80' '          PLUS EXTRA FOR 3278-4
*---------------------------------------------------------------------*
*                                                                     *
*               DISPLAY SCREEN - TRAILER SECTION                      *
*                                                                     *
*---------------------------------------------------------------------*
TRAILER  EQU   *
CMDCTRL  DC    X'C1115A50'        SBA TO ROW 22, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
LASTLINE DC    CL79' '            OPERATORS COMMAND INPUT LINE
PHEADING DC    X'115B60'          SBA TO ROW 23, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
HEADING  DC    CL8'Console '
CONNUM   DC    CL2' 1'            CONSOLE NUMBER
CTYPE    DC    CL4' '
MASTER   DC    CL8' '             MASTER CONSOLE
SYS      DC    CL4' '             SYS  AUTHORIZATION
IO       DC    CL4' '             I/O  AUTHORIZATION
CONS     DC    CL5' '             CONS AUTHORIZATION
         DC    CL3' '
         DC    CL5'Unit'
UNIT     DC    CL4' '             UNIT ADDR OF CONSOLE
HEADING1 DC    CL7'Timer: '
TIME     DC    CL3' '             SECONDS REMAINING ON TIMER
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'           DELAY IN SECONDS
         DC    CL2' '
HEADING2 DC    CL6'Mode: '
FREEZE   DC    C'F'               FREEZE/RELEASE MODE
MODE     DC    CL1'2'             DISPLAY MODE 2/1
         DC    CL2' '
         DC    CL5'Page '
PAGE     DC    CL1'2'             PAGE NUMBER
R24C1    DC    X'115CF0'          SBA TO ROW 24, COL 1
         DC    X'1D40'            ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'              INSERT CURSOR
USERLINE DC    CL13' '            USERS COMMAND INPUT LINE
ERROR    DC    CL66' '            ERROR MSG FIELD
ENDTRAIL EQU   *
         LTORG
         DS    0F
UCMTAB   DS    F
         DS    35F                PROVIDE SPACE FOR 20 UCM ADDRESSES
UCMTABE  EQU   *
NUMUCMS  DS    H
         SPACE 5
ERRMSG1  DC    CL26'ERROR - Console not active'
ERRMSG2  DC    CL26'ERROR - Non-numeric value '
ERRMSG3  DC    CL26'ERROR - Invalid command   '
ERRMSG4  DC    CL26'ERROR - NON-CRT CONSOLE   '
ERRMSG5  DC    CL37'"JESOPER" COMMAND NOT AUTHORIZED FOR '
INVPASS  DC    CL26'ERROR - INVALID PASSWORD '                     JL1
*---------------------------------------------------------------------*
*                                                                     *
*                           USER HELP PAGE                            *
*                                                                     *
*---------------------------------------------------------------------*
HELP     DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Command     Description'
         DC    X'11C150'
         DC    X'11C260',C'   .C       SWITCH MONITOR TO MASTER CONSOLE*
                '
         DC    X'11C540',C'   .CXX     SWITCH MONITOR TO CONSOLE XX'
         DC    X'11C3F0',C'   .DXX     SET DELAY TO XX TENTHS SECONDS'
         DC    X'11C540',C'   .E       END JESOPER'
         DC    X'11C650',C'   .W       START TIMER MODE FOR 30 SECONDS'
         DC    X'11C760',C'   .WXX     START TIMER MODE FOR XX SECONDS'
         DC    X'11C8F0',C'   .W0      START TIMER MODE UNTIL '
         DC    C'interrupt'
         DC    X'114A40',C'   .L       LINK TO RMFMON'
         DC    X'114B50',C'   .?       DISPLAY THIS PAGE'
         AIF   ('&INST' EQ 'DWR').NDWR1                            GMM
         DC    X'11C8F0',C'   .F       FREEZE DISPLAY ON CURRENT PAGE'
         DC    X'114B50',C'   .R       RELEASE DISPLAY'
         DC    X'11D160',C'   .1       DISPLAY MODE 1'
         DC    X'11D2F0',C'   .2       DISPLAY MODE 2'
.NDWR1   ANOP                      END OF UNSUPPORTED JESOPER CMDS GMM
         DC    X'114C60',C'HITTING INTERRUPT WILL STOP THE WAIT TIMER'
         DC    X'114F40',C'VALID OS AND JES2 COMMANDS ARE:'        JL1
         DC    X'11D160',C'   DISPLAY(D), $LSYS, START(S), $AJ, $A'','
         DC    X'11D2F0',C'   $DJ, $D'', $R, $TJ, $T'''            JL1
         DC    X'115CF0'        ROW 24, COL 1
         DC    C'Hit ENTER to continue'
         DC    X'115DC6'        ROW 24, COL 23
         DC    X'1D40'
         DC    X'1340'
         DC    X'1DE8'
MARKER1  EQU   *
HLENGTH  EQU   MARKER1-HELP     LENGTH OF HELP TPUT
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                           PFK HELP PAGE                             *
*                                                                     *
*---------------------------------------------------------------------*
HELPPFK  DC    X'C1'               WCC                             JL1
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0) JL1
         DC    X'114040'           SBA TO ROW 1, COL 1             JL1
         DC    X'3C404000'         FILL SCREEN WITH NULLS          JL1
         DC    X'114040',X'1DE8',C'Command     Pfk Key'            JL1
         DC    X'11C260',C'   .H         PF1'                      JL1
         DC    X'11C3F0',C'   .?         PF2'                      JL1
         DC    X'11C540',C'   END        PF3'                      JL1
         DC    X'11C650',C'   .C         PF4'                      JL1
         DC    X'11C760',C'   D D        PF5'                      JL1
         DC    X'11C8F0',C'   .W0        PF6'                      JL1
         DC    X'114A40',C'   .W99       PF7'                      JL1
         DC    X'114B50',C'   .D1        PF8'                      JL1
         DC    X'114C60',C'   .D10       PF9'                      JL1
         DC    X'114DF0',C'   D R,L      PF10'                     JL1
         DC    X'114F40',C'   D A,L      PF11'                     JL1
         DC    X'115050',C'   D J,L      PF12'                     JL1
         DC    X'115CF0'        ROW 24, COL 1                      JL1
         DC    C'Hit ENTER to continue'                            JL1
         DC    X'115DC6'        ROW 24, COL 23                     JL1
         DC    X'1D40'                                             JL1
         DC    X'1340'                                             JL1
         DC    X'1DE8'                                             JL1
MARKER2  EQU   *                                                   JL1
HLENGTH2 EQU   MARKER2-HELPPFK  LENGTH OF HELP TPUT                JL1
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                         PASSWORD READ TABLE                         *
*                                                                     *
*---------------------------------------------------------------------*
PSWDRD   DC    X'C5'               WCC                             JL1
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0) JL1
         DC    X'114040'           SBA TO ROW 1, COL 1             JL1
         DC    X'3C404000'         FILL SCREEN WITH NULLS          JL1
         DC    X'114040'        ROW 1, COL 1                       JL1
         DC    X'1DE8',C'Input Password:'                          JL1
         DC    X'114050'        ROW 1, COL 17                      JL1
         DC    X'1D4C'                                             JL1
         DC    X'1340'                                             JL1
         DC    X'1140D9'        ROW 1, COL 26                      JL1
         DC    X'1DE8'                                             JL1
MARKER3  EQU   *                                                   JL1
PLENGTH1 EQU   MARKER3-PSWDRD   LENGTH OF PASSWORD READ            JL1
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*                   COMMAND VALIDATION TABLE                          *
*                                                                     *
*---------------------------------------------------------------------*
CMDTAB   DC    X'08',C'DISPLAY '                                   JL1
         DC    X'02',C'D '                                         JL1
         DC    X'06',C'START '                                     JL1
         DC    X'02',C'S '                                         JL1
         DC    X'03',C'$AJ'                                        JL1
         DC    X'03',C'$A'''                                       JL1
         DC    X'03',C'$DJ'                                        JL1
         DC    X'03',C'$D'''                                       JL1
         DC    X'02',C'$R'                                         JL1
         DC    X'03',C'$T'''                                       JL1
         DC    X'03',C'$TJ'                                        JL1
         DC    X'06',C'$LSYS '                                     JL1
         DC    X'00'                  END OF TABLE                 JL1
*---------------------------------------------------------------------*
*                                                                     *
*                           E Q U A T E S                             *
*                                                                     *
*---------------------------------------------------------------------*
LEN9     EQU   9*80             NUMBER OF BYTES IN  9 LINES
LEN21    EQU   21*80            NUMBER OF BYTES IN 21 LINES
LEN22    EQU   22*80            NUMBER OF BYTES IN 22 LINES
LEN13    EQU   13*80            NUMBER OF BYTES IN 13 LINES
LEN14    EQU   14*80            NUMBER OF BYTES IN 14 LINES
M2BUFLEN EQU   21*80            LENGTH OF BUFFER 3278-2
M4BUFLEN EQU   35*80            LENGTH OF BUFFER 3278-4
HEADLEN  EQU   BUF-HEADER       LENGTH OF HEADER
TRAILEN  EQU   ENDTRAIL-TRAILER LENGTH OF TRAILER
MOD4LEN  EQU   ENDTRAIL-HEADER  LENGTH OF TPUT FOR MOD4
MOD2LEN  EQU   MOD4LEN-LEN14    LENGTH OF TPUT FOR MOD2
ENDMOD2  EQU   BUF+M2BUFLEN     ADDR OF TRAILER FOR 3278-2
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L                            GMM
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L                          GMM
*---------------------------------------------------------------------*
*                                                                     *
*        THIS ROUTINE WILL DETERMINE THE PRIVILEGE LEVEL OF A         *
*        TSO USER AND SET THE AUTH FLAGS ACCORDINGLY.                 *
*        THE SEPERATE FLAGS WILL HELP DETERMINE WHAT FUNCTIONS        *
*        MAY BE USED BY A TSO USER.                                   *
*                                                                     *
*        THE ADVANTAGE OF MAKING THIS A SUBROUTINE IS THAT AN         *
*        ACCESS PROTOCOL CAN BE ESTABLISHED. THIS PROTOCOL CAN        *
*        BE TAILORED TO THE INSTALLATIONS NEEDS. FOR EXAMPLE HERE     *
*        AT DWR WE LIMIT JESOPER COMMAND ACCESS TO TSO USERS WHO      *
*        HAVE OPERATOR LEVEL AUTHORIZATION.                           *
*                                                                     *
*---------------------------------------------------------------------*
TESTAUTH L     R5,16               ADDR OF CVT                     GMM
         L     R5,0(0,R5)          ADDR OF DISPATCH QUEUE          GMM
         L     R4,4(0,R5)          POINTER TO OUR TCB              GMM
         L     R5,12(0,R5)         GET ADDRESS OF CURRENT ASCB     GMM
         L     R5,176(0,R5)        GET ADDRESS OF JOBNAME          GMM
         MVC   LOGON,0(R5)         MAKE A COPY OF THE JOBNAME ...  GMM
         L     R4,180(0,R4)        POINTER TO JSCB                 GMM
         L     R4,264(0,R4)        PSCB POINTER                    GMM
         USING PSCB,R4             TELL ASSEMBLER ABOUT PSCB       GMM
         MVC   AUTH(1),PSCBATR1    COPY AUTHORIZATION BYTE         GMM
         NI    AUTH,X'F0'          KEEP ONLY AUTHORIZATION LEVEL   GMM
         DROP  R4                  DONT NEED PSCB ANYMORE ...      GMM
         BR    R14                 RESTORE REGISTERS AND RETURN    GMM
         CNOP  0,8                                                 GMM
         PRINT NOGEN                                               GMM
TCBWORDS DSECT                                                     JL1
TCBNEXT  DS    F                                                   JL1
TCBPTR   DS    F                                                   JL1
ASCBNEXT DS    F                                                   JL1
ASCBPTR  DS    F                                                   JL1
         TITLE '*** PROTECTED STEP CONTROL BLOCK DSECT ***'        GMM
         IKJPSCB                   COPY THE PSCB FORMAT DSECT      GMM
         TITLE '*** COMMAND PROCESSOR PARAMETER LIST DSECT ***'    GMM
         IKJCPPL                   COMMAND PROCESSOR PARM LIST     GMM
         TITLE '*** TSO USER PROFILE TABLE DSECT ***'              GMM
         IKJUPT                    TSO USER PROFILE TABLE          GMM
         TITLE '*** COMMUNICATION VECTOR TABLE ***'                GMM
         CVT   DSECT=YES                                           GMM
         TITLE '*** RDCM DSECT ***'                                GMM
IEERDCM  DSECT ,                                                   GMM
         IEECRDCM                                                  GMM
         TITLE '*** TDCM DSECT ***'                                GMM
IEETDCM  DSECT ,                                                   GMM
         IEECDCM                                                   GMM
         TITLE '*** MULTIPLE CONSOLE SUPPORT (MCS) UCM PREFIX ***' GMM
         IEECUCM FORMAT=NEW
         TITLE '*** ADDRESS SPACE CONTROL BLOCK ***'               JL1
         IHAASCB                                                   JL1
         TITLE '*** TSB DSECT ***'                                 JL1
         IKJTSB LIST=YES                                           JL1
         END
