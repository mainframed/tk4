$control write
$cmd clearsc
$ start
Conversion of distances.

  01 centimeter                08 meter
  02 decimeter                 09 nautical mile
  03 dekameter                 10 statute mile
  04 foot                      11 millimeter
  05 hectometer                12 myriameter
  06 inch                      13 rod
  07 kilometer                 14 yard

Enter a 4-digit number, the first two digits representing the unit to
convert from, and the last two digits the unit to convert to, e.g.
0608 requests conversion from inches (06) to meters (08).
$cmd assist promptno
$if length(number)=0
  $goto eoj
$if '&NUMBER'='.'
  Invalid entry.
  $cmd clearsc 1
  $goto start
$* everything will be converted to meters initially.
$cmd calc frunit#int(number/100)
$cmd calc tounit#mod(number,100)
$cmd setl pref fr
$call subrtne
$cmd setl pref to
$call subrtne
Enter &FRDESCR:
$cmd assist promptno
$cmd setl frunits &NUMBER
$cmd zcalc tounits#frunits*&FRFACT*1/&TOFACT
&FRUNITS &FRDESCR = &TOUNITS &TODESCR
$goto eoj
$ subrtne
$cmd calc unit#&PREF.unit
$select
  $when unit=01
    $cmd setl &PREF.descr 'centimeters'
    $cmd setl &PREF.fact .01
  $when unit=02
    $cmd setl &PREF.descr 'decimeters'
    $cmd setl &PREF.fact .1
  $when unit=03
    $cmd setl &PREF.descr 'dekameters'
    $cmd setl &PREF.fact 10
  $when unit=04
    $cmd setl &PREF.descr 'feet'
    $cmd setl &PREF.fact .304801
  $when unit=05
    $cmd setl &PREF.descr 'hectometers'
    $cmd setl &PREF.fact 100
  $when unit=06
    $cmd setl &PREF.descr 'inches'
    $cmd setl &PREF.fact .0254
  $when unit=07
    $cmd setl &PREF.descr 'kilometers'
    $cmd setl &PREF.fact 1000
  $when unit=08
    $cmd setl &PREF.descr 'meters'
    $cmd setl &PREF.fact 1
  $when unit=09
    $cmd setl &PREF.descr 'nautical miles'
    $cmd setl &PREF.fact (6076*.304801)
  $when unit=10
    $cmd setl &PREF.descr 'statute miles'
    $cmd setl &PREF.fact (5280*.304801)
  $when unit=11
    $cmd setl &PREF.descr 'millimeters'
    $cmd setl &PREF.fact .001
  $when unit=12
    $cmd setl &PREF.descr 'myriameters'
    $cmd setl &PREF.fact 10000
  $when unit=13
    $cmd setl &PREF.descr 'rods'
    $cmd setl &PREF.fact 5.0292
  $when unit=14
    $cmd setl &PREF.descr 'yards'
    $cmd setl &PREF.fact .9144
  $otherwise
    Invalid entry.
    $cmd clearsc 1
    $goto start
$return
