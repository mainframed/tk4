In the simplest case, the find subcommand is used to locate a
character string. More sophisticated criteria can be specified so
that, for example, the character string is located only if it appears
as a word.  Thus, "FIND I" would find the line "he is YOUR FRIEND" but
"FIND I WORD" would not.

First, you will be prompted for the Line Specification Unit (LSU).

$cmd aid lsu

You will subsequently be prompted to enter the string to be located,
<ver>.  Would you like to enter it in hex, e.g. "f1f2f3" instead of
"123"?  Enter a null line for yes.
$input
$if answer=''
  $cmd setl hex hex
$else
  $cmd setl hex ''

$until answer='1' × answer='2'
  1 - Find only the first occurrence of <ver>.
  2 - Find all occurrences of <ver>.

  Enter 1 or 2.
  $input

$if answer='2'
  $cmd setl all all
  The located lines will be copied to a newly created virtual data set.
  If you make any changes in this new data set, you can use the MB
  subcommand to later merge the lines back into the original data set.

  Enter the name to be assigned to the virtual data set, or a null
  line if you want ABE to generate the name for you.
  $input
  $if answer=''
    $cmd setl name ''
  $else
    $cmd setl name name(&ANSWER)
$else
  $cmd setl all ''
  $cmd setl name ''

$until answer='1' × answer='2' × answer='3' × answer='4' × +
  answer='5' × answer='6' × answer='7'
  1 - Simplest case - find string <ver>.
  2 - Find only where <ver> occurs as a word.
  3 - Find only where <ver> occurs as a prefix.
  4 - Find only where <ver> occurs as a suffix.
  5 - Find where <ver> matches disregarding upper/lower case, e.g.
      find any occurrence such as tom, TOM, and Tom.
  6 - Find where any one char in <ver> matches, e.g. if <ver>="abc",
      find the first column containing "a" or "b" or "c".
  7 - Find according to a specified pattern, e.g. if <ver>="___",
      then any string of three digits will be found.
      You will be given further instructions about how to specify
      <ver>.
  $input
$select
  $when answer='1'
    $cmd setl type ''
  $when answer='2'
    $cmd setl type word
  $when answer='3'
    $cmd setl type prefix
  $when answer='4'
    $cmd setl type suffix
  $when answer='5'
    $cmd setl type text
  $when answer='6'
    $cmd setl type any
  $when answer='7'
    $cmd setl type picture
    <ver> may be a string containing any combination of the following
    characters:
       = - any character (don't care)
       ^ - any non-blank character
       . - any non-displayable character
       # - any numeric character (0-9)
       _ - any non-numeric character
       @ - any alphabetic character (upper or lower case)
       < - any lowercase alphabetic character
       > - any uppercase alphabetic character
       $ - any special character (not alpha or numeric)
$until answer^=''
  Enter the string specifying what is to be found.  Do not enter
  the string in apostrophes, even though it may contain apostrophes,
  blanks or other delimiters.  I will take care of that for you.
  $input
$cmd setquot ver

The generated command is as follows:
$write &LSU find &VER &TYPE &HEX &ALL &NAME
Do you want the command executed?  Enter a null line for yes.
$input
$if answer=''
  $cmd &LSU find &VER &TYPE &HEX &ALL &NAME
