$control write
$cmd clearsc
$ start
Conversion of areas.

  01 acre                      07 square inch
  02 are                       08 square kilometer
  03 centare                   09 square meter
  04 hectare                   10 square mile
  05 square centimeter         11 square rod
  06 square foot               12 square yard

Enter a 4-digit number, the first two digits representing the unit to
convert from, and the last two digits the unit to convert to, e.g.
0609 requests conversion from square feet (06) to square meters (09).
$cmd assist promptno
$if length(number)=0
  $goto eoj
$if '&NUMBER'='.'
  invalid entry.
  $cmd clearsc 1
  $goto start
$* Everything will be converted to square meters.
$cmd calc frunit#int(number/100)
$cmd calc tounit#mod(number,100)
$cmd setl pref fr
$call subrtne
$cmd setl pref to
$call subrtne
Enter &FRDESCR:
$cmd assist promptno
$cmd setl frunits &NUMBER
$cmd zcalc tounits#frunits*&FRFACT*1/&TOFACT
&FRUNITS &FRDESCR = &TOUNITS &TODESCR
$goto eoj
$ subrtne
$cmd calc unit#&PREF.unit
$select
  $when unit=01
    $cmd setl &PREF.descr 'acres'
    $cmd setl &PREF.fact 4046.875
  $when unit=02
    $cmd setl &PREF.descr 'ares'
    $cmd setl &PREF.fact 100
  $when unit=03
    $cmd setl &PREF.descr 'centares'
    $cmd setl &PREF.fact 1
  $when unit=04
    $cmd setl &PREF.descr 'hectares'
    $cmd setl &PREF.fact 10000
  $when unit=05
    $cmd setl &PREF.descr 'square centimeters'
    $cmd setl &PREF.fact .0001
  $when unit=06
    $cmd setl &PREF.descr 'square feet'
    $cmd setl &PREF.fact .092903466
  $when unit=07
    $cmd setl &PREF.descr 'square inches'
    $cmd setl &PREF.fact .00064516296141666
  $when unit=08
    $cmd setl &PREF.descr 'square kilometers'
    $cmd setl &PREF.fact 1000000
  $when unit=09
    $cmd setl &PREF.descr 'square meterss'
    $cmd setl &PREF.fact 1
  $when unit=10
    $cmd setl &PREF.descr 'square miles'
    $cmd setl &PREF.fact 2590000
  $when unit=11
    $cmd setl &PREF.descr 'square rods'
    $cmd setl &PREF.fact 25.292968739
  $when unit=12
    $cmd setl &PREF.descr 'square yards'
    $cmd setl &PREF.fact .836131198
  $otherwise
    Invalid response.
    $cmd clearsc 1
    $goto start
$return
