CONSOLE  TITLE '- C O N S O L E - CONSOLE INTERFACE'
         SPACE
         PRINT NOGEN
*
* CONSOLE
*
* THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A TSO TERMINAL.
* IT GOES OUT AND FINDS A GRAPHICS (327X) ACTIVE CONSOLE AND USES
* THAT CONSOLES SCREEN IMAGE FOR OUTPUT AND UCM-ID FOR SVC 34.
* THE SCREEN ADDRESS USED IS THE ONE SPECIFIED IN THE CONSOLE COMMAND.
* IF NONE IS CODED, THEN THE DEFAULT IS USED, THE MASTER CONSOLE.
*
* THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34 INTERFACE.
* ANYTHING ENTERED ON THE INPUT LINE WILL BE PASSED TO MVS VIA SVC34
*
* TO TERMINATE THIS COMMAND, HIT PF3, PF12, P15, OR PF24
*
* PF10 AND PF11 ARE USED TO SWITCH TO VIEW ANOTHER CONSOLE.
*
* SET &SP3 TO 'N' IF THIS COMMAND IS TO RUN ON AN SP 1.1 SYSTEM
* SET &SP3 TO 'Y' IF THIS COMMAND IS TO RUN ON AN SP 1.3 OR XA SYSTEM
*
***********************************************************************
*
* UPDATED TO ADD SUPPORT FOR MVS XA 2.2.0.  ALSO CHANGED TO SUPPORT
* MULTIPLE VERSIONS FROM ONE PROGRAM.  SUPPORTS SP1.3 THRU XA 2.2.
*
* USES MAGIC SVC (251) TO GAIN APF AUTHORIZATION IF NECESSARY.  CODE
* CAN BE DELETED, IF NECESSARY.  IF YOU DON'T USE MAGIC SVC, THEN YOU
* WILL HAVE TO USE OTHER METHOD TO GAIN APF AUTHORIZATION, SUCH AS
* ENTRY IN IKJEFTE2.
*
* CODE HAS A USERID CHECK ROUTINE THAT WILL NEED TO BE MODIFIED (OR
* ELIMINATED) TO SUIT YOUR INSTALLATION. (OR YOU CAN CHANGE YOUR
* USERID'S TO MATCH OUR TABLE)
*
* CODE CHANGED TO BLOW SMOKESCREEN TO UNAUTHORIZED USERS.
*
* NEEDS MACRO IEERDCM FROM XA220 OPTIONAL MATERIALS.
*
*                                    26 APRIL 88   SAM C THEIS
*
***********************************************************************
*
*
*
         GBLC  &SP3
&SP3     SETC  'Y'
CONSOLE  CSECT
*        EQUATES
R0  EQU 0
R1  EQU 1
R2  EQU 2
R3  EQU 3
R4  EQU 4
R5  EQU 5
R6  EQU 6
R7  EQU 7
R8  EQU 8
R9  EQU 9
R10 EQU 10
R11 EQU 11
R12 EQU 12
R13 EQU 13
R14 EQU 14
R15 EQU 15
         USING *,R15                DEFINE TEMP BASE REGISTER      SCT
         B     AROUND               BRANCH AROUND ID AREA          SCT
         DC    CL9'CONSOLE'         IDENTIFIER                     SCT
         DC    CL9'&SYSDATE'                                       SCT
         DC    CL7'&SYSTIME'                                       SCT
         DC    CL13'VERSION 2.2'                                   SCT
AROUND   STM   R14,R12,12(R13)      SAVE INCOMING REGISTERS        SCT
         LR    R12,R15              SET UP BASE REGISTER
         DROP  R15                                                 SCT
         USING CONSOLE,R12                                         SCT
         LR    R11,R13              COPY SAVE AREA POINTER
         LA    R13,SAVE             POINT TO MY SAVE AREA
         USING SAVE,R13             USE R13 AS EXTRA BASE REGISTER
         ST    R13,8(,R11)          CHAIN NEW OFF OLD
         ST    R11,SAVE+4           AND OLD OFF NEW
         LR    R2,R1                COPY CPPL POINTER
         TESTAUTH FCTN=1            CHECK IF AUTHORIZED
         LTR   R15,R15              TEST RETURN CODE
         BNZ   NOTAPF               BRANCH IF NOT AUTHORIZED
         USING CPPL,R2              MAP CPPL
APFOK    L     R3,CPPLPSCB          LOAD PSCB POINTER              SCT
         USING PSCB,R3              NOW MAP PSCB
         CLC   PSCBUSER(3),=C'SYS'     RMH GRP ID SYS
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HJJCA'   SEC ADM ID HJJCA
         BE    GOODONE
         CLC   PSCBUSER(6),=C'OPLEKR'  SEC ADM ID OPLEKR
         BE    GOODONE
         CLC   PSCBUSER(6),=C'OPLRST'  P&T GRP ID OPLRST
         BE    GOODONE
         CLC   PSCBUSER(6),=C'IBMDCC'  IBM CE  ID IBMDCC
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HKEBO'   KEN BONDS ID HKEBO
         BE    GOODONE
         CLC   PSCBUSER(2),=C'CS'      COMPUTER SERVICE IDS
         BE    GOODONE
         CLC   PSCBUSER(3),=C'HOS'     HOUSTON SYSTEM GRP ID
         BE    GOODONE
         CLC   PSCBUSER(3),=C'SEC'     HOUSTON SYSTEM GRP ID
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HNARU'   CHANGE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(6),=C'OPFHHO'  RESOURCE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HDETO'   CHANGE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(6),=C'OPDMRO'  HOUSTON HELP DESK
         BE    GOODONE
         CLC   PSCBUSER(5),=C'OPAAT'  HOUSTON HELP DESK
         BE    GOODONE
         CLC   PSCBUSER(6),=C'OPRCHE'  HOUSTON HELP DESK
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HRMHC'   RESOURCE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HRMHI'   RESOURCE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(5),=C'HRMHH'   RESOURCE MANAGEMENT ID
         BE    GOODONE
         CLC   PSCBUSER(2),=C'OP'      OPERATIONS ID
         BNE   NOTALLWD                ACCCKK !!! --> HACKERS !!
GOODONE  MVC   USERID,PSCBUSER      COPY USERID TO WTO MSG
         DROP  R3                   UNMAP PSCB
         USING CVT,R1                                              SCT
         L     R1,16              FIND THE CVT                     SCT
         LR    R3,R1                IS THIS XA 2.2?                SCT
         S     R3,=F'40'            FIND THE RELEASE LEVEL         SCT
         CLC   0(5,R3),=C'SP2.2'    CHECK IT OUT                   SCT
         BNE   NOT22                NOPE, OLDER CODE               SCT
         MVI   XA22,X'FF'           FLAG IT FOR LATER              SCT
NOT22    L     R1,CVTASVT         FIND THE ASCB VECTOR TABLE       SCT
         DROP  R1                                                  SCT
         LA    R1,528(,R1)        FIND THE POINTERS TO ASCBS       SCT
FINDCONS L     R3,0(,R1)          IS THIS THE CONSOLE ASCB?        SCT
         USING ASCBEGIN,R3                                         SCT
         L     R4,ASCBJBNS                                         SCT
         CLC   0(8,R4),=C'CONSOLE '                                SCT
         BE    FNDCONS               YES                           SCT
         LA    R1,4(,R1)             NO, TRY THE NEXT ASCB         SCT
         B     FINDCONS                                            SCT
FNDCONS  LH    R4,ASCBASID                                         SCT
         STH   R4,ASIDCONS        SQUIRREL AWAY THE CONSOLE ASID   SCT
         DROP  R3                                                  SCT
         MODESET KEY=NZERO                                         SCT
         GTSIZE ,                   GET SIZE OF SCREEN
         LTR   R0,R0                TEST SCREEN DEPTH
         BZ    NOTSCRN              EXIT IF INVALID
         C     R1,F80               CHECK SCREEN WIDTH
         BE    NOTMOD5              BRANCH IF NOT A MOD 5
         MVI   CLEAR,X'F5'          SET TO ERASE WRITE TO GIVE MOD 2
         LA    R0,24                PUT MOD 2 SIZE IN R0
NOTMOD5  DS    0H
         ST    R0,DEPTH             SAVE SCREEN DEPTH FOR LATER
         TITLE '- GET STARTED ON THE REAL WORK NOW -'
         LA    R3,PPLAREA           POINT TO MY PARSE PARAMETER LIST
         USING PPL,R3               MAP IT
         XC    PPLUPT(28),PPLUPT    CLEAR IT OUT
         MVC   PPLUPT,CPPLUPT       COPY UPT POINTER
         MVC   PPLECT,CPPLECT       AND ECT POINTER
         MVC   PPLECB,=A(ECB)       POINT AT MY ECB
         MVC   PPLPCL,=V(PCL)       POINT AT MY PARSE CONTROL AREA
         MVC   PPLANS,=A(ANSWER)    POINT AT MY ANSWER AREA
         MVC   PPLCBUF,CPPLCBUF     COPY COMMAND BUFFER POINTER
         LR    R1,R3                PUT PPL POINTER IN PARM REG
         CALLTSSR EP=IKJPARS        CALL PARSE SERVICE ROUTINE
         LTR   R15,R15              CHECK IF PARSE WORKED
         BNZ   BADPARM              RETURN IF NOT
         L     R4,PPLANS            LOAD POINTER TO PARSE RESULT
         L     R4,0(,R4)            LOAD POINTER TO PDL
         USING PDL,R4               MAP THE PARSE RESULTS
         ICM   R5,B'1111',PARM      LOAD POINTER TO PARM
         BZ    NOTSPCFC             IF NOT THERE, USE DEFAULT
         LH    R6,PARM+4            LOAD LENGTH
         DROP  R2,R3,R4             DROP ALL THE BASE REGISTERS
         C     R6,F3                SEE IF 3 BYTE PARM
         BNE   BADPARM              IF NOT, SAY BAD PARAMETER
SPECIFIC DS    0H
         MVC   MYUCB,0(R5)          COPY UCB FROM PARM TO ME
NOTSPCFC DS    0H
         TITLE '- SEARCH MVS FOR THE CONSOLE, ETC. -'
SRCH000  XR    R8,R8                NUMBER OF TABLE ENTRIES = ZERO
         ST    R8,CLTCOUNT          STORE IT
         LA    R8,CLTWORK           CONSOLE LOOKUP TABLE WORK AREA
         USING CLT,R8               LET MR. ASSEMBLER KNOW
         ST    R8,CLTSTART          INITIALIZE FIRST SLOT
         ST    R8,CLTEND            INITIALIZE FIRST MT SLOT
         ST    R8,CLTMSTR           INITIALIZE WHERE MASTER CLT IS
         ST    R8,CLTNOW            INITIALIZE CURRENT CLT
*----------------------------------------------------------------------
* TO ALL OF U AMUZED BY 'CLT' .... JUST THINK OF WHAT WOULD HAVE
* HAPPENED IT IT HAD BEEN THE 'CONSOLE LOOKUP AND INITIALIZATION
* TABLE'?
*----------------------------------------------------------------------
         AIF   ('&SP3' NE 'Y').SP1A
         USING PSA,R0               MAP PSA
         L     R2,PSAAOLD           LOAD MY ASCB POINTER
         DROP  R0                   UNMAP PSA
         USING ASCB,R2              MAP THE ASCB
         MVC   MYASID,ASCBASID      GET MY ASID
         DROP  R2                   UNMAP THE ASCB
         LA    R2,1                 WANT AUTHORIZATION INDEX OF 1
         MODESET KEY=ZERO           MUST BE KEY ZERO TO SET AX
         AXSET AX=(R2)              GIVE IT TO US
         LH    R1,ASIDCONS          GET THE CONSOLE ASID
         SSAR  R1                   SET IT AS OUR SECONDARY
.SP1A    ANOP
         L     R1,CVTPTR            LOAD CVT POINTER
         USING CVT,R1               MAP THE CVT
         L     R1,CVTCUCB           GET UCM ADDRESS
         DROP  R1                   UNMAP IT NOW
         USING UCM,R1               MAP THE UCM
         LM    R3,R5,UCMVEA         R3 -> TO THE FIRST UCME
*                                   R4 CONTAINS UCME LENGTH
*                                   R5 -> TO THE LAST UCME
         USING UCMLIST,R3           MAP THE UCME(S)
UCMLOOP  DS    0H                   SEARCH FOR SOMETHING USEFUL
         L     R1,UCMUCB            GET UCB ADDRESS
         MVC   HOLDUCB,13(R1)       COPY CONSOLE ADDRESS (EBCDIC)
         TM    UCMATR,UCMUF         IS IT ACTIVE ?
         BZ    NEXTUCM              USELESS IF NOT
         ICM   R10,B'1111',UCMXB    IS IT GRAPHICS ?
         BZ    NEXTUCM              USELESS IF NOT
         USING DCMTSRT,R10          MAP THE RESIDENT DCM
         AIF   ('&SP3' NE 'Y').SP1B1
         CLI   XA22,X'FF'           IS IT XA 2.2?                  SCT
         BNE   OLDSTUFF             NOPE                           SCT
         LA    R14,DCMSIZE          LENGTH OF RDCM                 SCT
         SR    R11,R11              KEY                            SCT
         MVCP  MYRDCM(R14),0(R10),R11 GET A COPY OF RDCM           SCT
         LA    R10,MYRDCM           FOOL THE OTHERS                SCT
OLDSTUFF ICM   R6,B'1111',DCMADTRN  FIND THE PAGEABLE DCM (TDCM)
         ST    R6,VDELCON           STORE THE ADDRESS EYE
*                                   (NOW IN 'CONSOLE' ADDRESS SPACE)
         BZ    NEXTUCM              FORGET IT IF NOT THERE
         USING DCMSTRT,R6           MAP IT, BUT REMEMBER IN OTHER SPACE
         SLR   R11,R11              SET TO PROTECT KEY 0
         LA    R14,1                SAY ONE BYTE TO MOVE
         MVCP  MYIONDX(R14),DCMIONDX,R11  COPY DCMIONDX TO ME
         CLI   MYIONDX,X'10'        IS IT A 3270 ?
         BNE   NEXTUCM              BRANCH AROUND IF NOT
         MVC   XUCMID,UCMID         SAVE UCMID FOR SVC34
*----------------------------------------------------------------------
*    WE HAVE AN ACTIVE 3277 TYPE CONSOLE ...
*    COMMENTED OUT CODE USED TO CHECK FOR A SPECIFIC CONSOLE PASSED
*    IN THE PARM. THIS CHECK IS NOW MOVED OUTSIDE THE LOOP.
*----------------------------------------------------------------------
*        OC    MYUCB,MYUCB          WANT SPECIFIC CONSOLE?
*        BNZ   TESTUCB              YES, GO TEST UCB
*        TM    UCMDISP1,UCMDISPA    NO, THEN SEE IF MASTER CONSOLE
*        BO    GOTOIT               YES, USE IT IF IT IS
*        B     NEXTUCM              NO, SKIP IT
*TESTUCB  DS    0H
*        CLC   MYUCB,HOLDUCB        SEE IF THIS IS RIGHT ONE
*        BE    GOTOIT               BRANCH IF IT IS
         L     R9,CLTCOUNT          NUMBER OF ACTIVE CLT'S
         C     R9,=F'9'             MORE THAN 9 ENTRIES?
         BH    SRCH100              YES - GO WITH WHAT WE GOT
         LA    R9,1(,R9)            NUMBER OF ENTRIES += 1
         ST    R9,CLTCOUNT          REMEMBER FOR NEXT TIME
         L     R8,CLTEND            R8 => AN EMPTY CLT ENTRY
         MVC   CLTUCB(3),HOLDUCB    SAVE CONSOLE UCB ADDRESS
         MVC   CLTUCMID(1),XUCMID   AND UCMID FOR SVC 34
         ST    R10,CLTUCMXB         ADDR OF RESIDENT DCM
         ST    R6,CLTADTRN          ADDR OF PAGEABLE DCM
         TM    UCMDISP1,UCMDISPA    SET IF MASTER CONSOLE
         BNO   SRCH010              NOT THE MASTER ...
         ST    R8,CLTMSTR           THIS IS IT!
SRCH010  LA    R8,12(,R8)           NEXT ENTRY IN TABLE
         ST    R8,CLTEND            AND REMEMBER
NEXTUCM  DS    0H
         BXLE  R3,R4,UCMLOOP        GET THE NEXT ENTRY
         DROP  R3                   UNMAP THE UCM
SRCH100  EQU   *                    SEARCH CLT FOR MATCH ON CONSOLE
         MVC   CLTNOW(4),CLTMSTR    ASSUME MASTER CONSOLE
         CLC   MYUCB(3),=X'000000'  JUST ANY OLD CONSOLE?
         BE    SRCH130              YEP - USE THE MASTER
         L     R8,CLTSTART          BEGIN AT THE BEGINNING
         L     R9,CLTCOUNT          WITH THIS MANY TO GO ...
         LTR   R9,R9                ARE THERE ANY?
         BZ    SRCH900              NO ... GET OUT
SRCH110  CLC   MYUCB(3),CLTUCB      IS THIS THE ONE WE WANT
         BE    SRCH120              U BETCHA!
         LA    R8,12(,R8)           NEXT ENTRY IN TABLE
         BCT   R9,SRCH110           CONTINUE SCAN IF MORE COLUMNS
         B     SRCH130              FELL THROUGH ... USE THE MASTER
SRCH120  ST    R8,CLTNOW            AFTER FINDING SPECIFIC CONSOLE
SRCH130  L     R8,CLTNOW            PICK UP THE CONSOLE WE GOT
         MVC   HOLDUCB(3),CLTUCB    RESET UCB ADDRESS
         MVC   XUCMID(1),CLTUCMID   RESET UCMID FOR SVC 34
         L     R10,CLTUCMXB         RESET ADDR OF RESIDENT DCM
         L     R6,CLTADTRN          RESET ADDR OF PAGEABLE DCM
         ST    R6,VDELCON           STORE THE ADDRESS
         B     GOTOIT               RESUME 'OLD' CODE
         DROP  R8                   DUMP THE CLT
SRCH900  LH    R2,MYASID            GET MY ASID BACK
         SSAR  R2                   GET OUT OF SECONDARY MODE
         MODESET KEY=NZERO          BACK TO USER KEY FOR CLEANUP
         B     NOCONSL              FINI
         AGO   .SP1B2
.SP1B1   ANOP
         ICM   R6,B'1111',DCMADTRN  FIND THE PAGEABLE DCM (TDCM)
         BZ    NEXTUCM              FORGET IT IF NOT THERE
         USING DCMSTRT,R6           MAP IT
         CLI   DCMIONDX,X'10'       IS IT A 3270 ?
         BNE   NEXTUCM              BRANCH AROUND IF NOT
         MVC   XUCMID,UCMID         SAVE UCMID FOR SVC34
         OC    MYUCB,MYUCB          WANT SPECIFIC CONSOLE?
         BNZ   TESTUCB              YES, GO TEST UCB
         TM    UCMDISP1,UCMDISPA    NO, THEN SEE IF MASTER CONSOLE
         BO    GOTOIT               YES, USE IT IF IT IS
         B     NEXTUCM              NO, SKIP IT
TESTUCB  DS    0H
         CLC   MYUCB,HOLDUCB        SEE IF THIS IS RIGHT ONE
         BE    GOTOIT               BRANCH IF IT IS
NEXTUCM  DS    0H
         BXLE  R3,R4,UCMLOOP        GET THE NEXT ENTRY
         DROP  R3                   UNMAP THE UCM
         MODESET KEY=NZERO          BACK TO USER KEY FOR CLEANUP
         B     NOCONSL              FINI
.SP1B2   ANOP
         TITLE '- SET UP SBA''S FOR APPROPRIATE CONSOLE SIZE -'
GOTOIT   DS    0H
         AIF   ('&SP3' NE 'Y').SP1C1
         SLR   R11,R11              SET TO PROTECT KEY 0
         LA    R14,2                TWO BYTES TO MOVE
         MVCP  MYMSGAL(R14),DCMMSGAL,R11  COPY NUMBER OF LINES ON SCRN
         LA    R14,4                FOUR BYTES THIS TIME
         MVCP  MYASCRN(R14),DCMASCRN,R11  COPY ADDRESS OF SCREEN BUFFER
         LH    R2,MYASID            GET MY ASID BACK
         SSAR  R2                   GET OUT OF SECONDARY MODE
         AGO   .SP1C2
.SP1C1   ANOP
         MVC   MYMSGAL,DCMMSGAL     COPY NUMBER OF LINES ON SCRN
         MVC   MYASCRN(4),DCMASCRN  COPY ADDRESS OF SCREEN BUFFER
.SP1C2   ANOP
         MODESET KEY=NZERO          BACK TO USER KEY FOR NORMAL CODE
         LH    R11,MYMSGAL          NUMBER  OF LINES IN MSG AREA
         MVC   MSGLINE+8(3),HOLDUCB PUT CONSOLE ADDRESS INTO MSG
         C     R11,F20              COMPARE TO LIMIT OF MOD2 SCREEN
         BH    TRYMOD3              IF TOO BIG, TRY FOR MOD3
SETMOD2  DS    0H
         ST    R11,MAXLINES         SAVE AS MAX LINES FOR SCREEN
         MVC   LASTLINE,LASTMOD2    SAVE ADDRESS OF LAST LINE ALLOWED
         MVC   MSGSBA(3),SBA2101    SET UP ALL
         MVC   COMNDSBA(3),SBA2201     DYNAMIC SBAS
         MVC   BOTTMSBA(3),SBA2401        FOR MOD 2'S
         MVC   LENGTH,LENGTH2            SET UP LENGTH
         B     CLEARIT                   AND GO TO IT
TRYMOD3  DS    0H
         C     R11,F28              COMPARE TO LIMIT OF MOD3 SCREEN
         BH    TRYMOD4              IF TOO BIG, TRY FOR MOD4
         CLC   DEPTH,F32            TEST IF OUR SCREEN IS BIG ENOUGH
         BNL   SETMOD3              BRANCH IF WE ARE A MOD3 OR MOD4
         TPUT  TRUNCATE,LTRUNCT,EDIT TELL USER WILL TRUNCATE DISPLAY
         L     R11,DEPTH            LOAD OUR SIZE
         S     R11,F4               BACK UP THE 4 LINES AT THE BOTTOM
         B     SETMOD2              PRETEND CONSOLE A MOD 2
SETMOD3  DS    0H
         ST    R11,MAXLINES         SAVE MAX LINES ON SCREEN
         MVC   LASTLINE,LASTMOD3    SAVE ADDRESS OF LAST LINE ALLOWED
         MVC   MSGSBA(3),SBA2901    SET UP ALL
         MVC   COMNDSBA(3),SBA3001     DYNAMIC SBAS
         MVC   BOTTMSBA(3),SBA3201        FOR MOD 3'S
         MVC   LENGTH,LENGTH3            SET UP LENGTH
         B     CLEARIT                   AND GO TO IT
TRYMOD4  DS    0H
         C     R11,F39              COMPARE TO LIMIT OF MOD4 SCREEN
         BNH   *+8                  IF NOT TOO BIG, CONTINUE
         EX    R0,*                 OTHERWISE "BANG"
         CLC   DEPTH,F43            TEST IF OUR SCREEN IS BIG ENOUGH
         BNL   OKMOD4               BRANCH IF WE ARE A MOD 4
         TPUT  TRUNCATE,LTRUNCT,EDIT  TELL USER WILL TRUNCATE DISPLAY
         L     R11,DEPTH            LOAD OUR SIZE
         S     R11,F4               BACK UP THE 4 LINES AT THE BOTTOM
         CLC   DEPTH,F32            SEE IF WE ARE A MOD 3
         BE    SETMOD3              BRANCH TO MOD3 CODE IF ARE
         B     SETMOD2              ELSE BRANCH TO MOD2 CODE
OKMOD4   DS    0H
         ST    R11,MAXLINES         SAVE MAX LINES ON SCREEN
         MVC   LASTLINE,LASTMOD4    SAVE ADDRESS OF LAST LINE ALLOWED
         MVC   MSGSBA(3),SBA4001    SET UP ALL
         MVC   COMNDSBA(3),SBA4101     DYNAMIC SBAS
         MVC   BOTTMSBA(3),SBA4301        FOR MOD 4'S
         MVC   LENGTH,LENGTH4       SET UP LENGTH
         TITLE '- DISPLAY THE CONSOLE BUFFER -'
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FOR THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*        R6 ->  TDCM
*
CLEARIT  DS    0H
         STFSMODE ON                SAY IN FULL SCREEN MODE
         STTMPMD  ON,KEYS=ALL       ALSO SAY WANT ALL KEYS
         TPUT  CLEAR,LCLEAR,NOEDIT  CLEAR THE SCREEN
KEYZERO  DS    0H
         MODESET KEY=ZERO           MUST BE KEY ZERO TO COPY BUFFER
GOTCONSL DS    0H
         AIF   ('&SP3' NE 'Y').SP1D1
         LH    R1,ASIDCONS          GET THE CONSOLE ASID
         SSAR  R1                   SET IT AS OUR SECONDARY
*        EYE
         L     R7,VDELCON           SAVED ADDRESS OF TDCM
         USING DCMSTRT,R7           ADDRESS IT (IN CONOLE A.S.)
         LA    R8,8                 I WANT 12 BYTES
         XR    R11,R11              PROTECT KEY=0
         MVCP  DELCON(R8),DCMMFRMF,R11  COPY DEL/CON/SEG VALUES
*        EYE
         LH    R1,MYMSGAL           NUMBER  OF LINES IN MSG AREA
         L     R7,MYASCRN           POINT TO  THE  FIRST INPUT LINE
         SH    R7,H2                BACK UP TO ATTRIBUTE BYTE
         LA    R8,BUFFER            WORK AREA
         SLR   R11,R11              PROTECT KEY OF 0
         LA    R14,80               LENGTH TO MOVE
GETLINES DS    0H
         MVCP  0(R14,R8),0(R7),R11  MOVE A LINE
         TR    1(79,R8),BLANKTAB    GET IT READABLE
         LA    R8,80(,R8)           OUR SIZE
         LA    R7,84(,R7)           THEIRS
         BCT   R1,GETLINES          GET EM ALL
         LH    R2,MYASID            GET MY ASID BACK
         SSAR  R2                   GET OUT OF SECONDARY MODE
         AGO   .SP1D2
.SP1D1   ANOP
         LH    R1,MYMSGAL           NUMBER  OF LINES IN MSG AREA
         L     R7,MYASCRN           POINT TO  THE  FIRST INPUT LINE
         SH    R7,H2                BACK UP TO ATTRIBUTE BYTE
         LA    R8,BUFFER            WORK AREA
GETLINES DS    0H
         MVC   0(80,R8),0(R7)       MOVE A LINE
         TR    1(79,R8),BLANKTAB    GET IT READABLE
         LA    R8,80(,R8)           OUR SIZE
         LA    R7,84(,R7)           THEIRS
         BCT   R1,GETLINES          GET EM ALL
.SP1D2   ANOP
         MODESET KEY=NZERO          BACK TO USER KEY FOR NORMAL CODE
         S     R8,F80               BACK UP TO LAST LINE STORED
         L     R2,MAXLINES          MAXIMUM LINES TO DISPLAY
         L     R3,LASTLINE          LOAD ADDRESS OF LAST OUTPUT LINE
COPYLOOP DS    0H
         MVC   0(80,R3),0(R8)       COPY IT
         S     R3,F81               BACK UP ONE OUTPUT LINE
         S     R8,F80               AND ONE BUFFER LINE
         BCT   R2,COPYLOOP          LOOP TILL SCREEN FULL
* EXPLORATORY SURGERY ... EYE
         MVC   MODE(L'MODEMSTR),MODEMSTR 'K S,DEL=' ETC
*        MVC   CHAR4(4),DELCON         PART ONE OF THREE
*        UNPK  MSGLINE+4(9),CHAR4(5)   UNPACK IT
*        MVC   CHAR4(4),DELCON+4       PART TWO OF THREE
*        UNPK  MSGLINE+12(9),CHAR4(5)  UNPACK IT
*        TR    MSGLINE+4(16),TRHEX-240 MAKE IT READABLE
*        MVI   MSGLINE+20,X'40'        ZAP THE LAST 'DIGIT'
         MVC   MODE+48(1),DELCON       SET UP MFORM=T
         MVC   MODE+8(2),DELCON+1      SET UP DEL=RD
         MVC   MODE+22(1),DELCON+3     SET UP CON=N
         XR    R0,R0                   PREPARE TO IC
         IC    R0,DELCON+4             GET SEG VALUE
         CVD   R0,DOUBLE               MAKE PACKED DECIMAL
         MVC   MODE+13(4),PIC3D        MOVE IN A PIC FOR SEG
         ED    MODE+13(4),DOUBLE+6     MOVE TO OUTPUT LINE
         MVC   MODE+13(2),=C'G='       CLEAN UP MESS FROM PICTURE
         IC    R0,DELCON+5             GET RNUM VALUE
         CVD   R0,DOUBLE               MAKE PACKED DECIMAL
         MVC   MODE+27(4),PIC3D        MOVE IN A PIC FOR RNUM
         ED    MODE+27(4),DOUBLE+6     MOVE TO OUTPUT LINE
         MVC   MODE+27(2),=C'M='       CLEAN UP MESS FROM PICTURE
         LH    R0,DELCON+6             GET RTIME VALUE
         CVD   R0,DOUBLE               MAKE PACKED DECIMAL
         MVC   MODE+37(4),PIC3D        MOVE IN A PIC FOR RTIME
         ED    MODE+37(4),DOUBLE+6     MOVE TO OUTPUT LINE
         MVI   MODE+37,C'='            CLEAN UP MESS FROM PICTURE
         CLI   MODE+9,X'40'            WAS IT 'DEL=N ,'
         BNE   DOTIME                  NO ... GO DO THE TIME
         MVC   MODE+9(40),MODE+10      COLLAPSE THE BLANK
         MVI   MODE+48,X'40'           MAKE 'MFORM=XX' 'MFORM=X '
DOTIME   TIME  DEC                     WHAT TIME IS IT?
         ST    R0,DOUBLE               THANK YOU.
         MVC   MSGLINE+70(9),=X'F021207A20207A2020'  MOVE IN A PIC
         ED    MSGLINE+70(9),DOUBLE    MAKE TIME PRETTY
         MVI   MSGLINE+70,X'40'        KNOCK OUT THE FILL BYTE
         MVC   MSGLINE+12(9),=C'---------' ASSUME NOT MASTER
         CLC   CLTNOW(4),CLTMSTR       IS THIS MASTER CONSOLE?
         BNE   DOWRITE                 NO ... ASSUMPTION IS GOOD
         MVC   MSGLINE+12(9),=C'*MASTER* ' SET MASTER
* END EXPLORATORY SURGERY ... EYE
DOWRITE  BAL   R9,WRITE             DISPLAY ON SCREEN
         BAL   R9,READ              READ A COMMAND (MAYBE)
         TM    CLTCHNG,X'80'        WANNA NEW CONSOLE?
         BO    SRCH000              YES - GO DO IT
         CLC   ENTRLNE,ENTRLNE-1    NULL INPUT ?
         BE    KEYZERO              LOOP BACK IF IS
         MVC   CMNDBUF(116),ENTRLNE COPY COMMAND TO SVC 34 BUFFER
         MVC   ENTRLNE,ENTRLNE-1    RESET INPUT
         OC    CMNDBUF(116),ENTRLNE ENSURE COMMAND IN UPPER CASE
         MVC   WTOCMD,CMNDBUF       TAKE COPY OF COMMAND
         CLC   =C'RP',CMNDBUF       SEE IF PASSWORD REPLY
         BNE   TESTGCD              BRANCH IF NOT
         MVI   CMNDBUF+1,C' '       IS, SO REMOVE 'P'
         B     NODOWTO              AND BRANCH PAST WTO
TESTGCD  DS    0H
         CLI   CMNDBUF,C'.'         TEST IF GCD COMMAND
         BNE   TESTPW               BRANCH IF NOT
         CLC   =C'RP',CMNDBUF+3     SEE IF PASSWORD REPLY
         BNE   TESTPW               BRANCH IF NOT
         MVI   CMNDBUF+4,C' '       IS, SO REMOVE 'P'
         B     NODOWTO              AND BRANCH PAST WTO
TESTPW   DS    0H
         CLI   CMNDBUF,C'0'         TEST FOR SHORT FORMAT REPLY
         BNL   REPLY                BRANCH REPLY
         CLC   =C'R ',CMNDBUF       TEST FOR LONG FORMAT REPLY
         BNE   DOWTO                BRANCH IF NOT
         LA    R0,78                REMAINING BUFFER SIZE
         LA    R1,WTOCMD+2          NEXT BUFFER CHAR
PWLOOP   DS    0H
         CLI   0(R1),C','           START OF REPLY DATA?
         BE    PWEND                YES
         LA    R1,1(R1)             NEXT BUFFER CHAR
         BCT   R0,PWLOOP            LOOP IF NOT LAST
         B     DOWTO                REPLY DELIM NOT FOUND
PWEND    DS    0H
         LA    R14,1(R1)            START OF REPLY DATA
         LR    R15,R0               DATA
         BCTR  R15,0                 SIZE
         B     BLANKOUT
REPLY    DS    0H
         LA    R14,WTOCMD+1         POSSIBLE REPLY DATA
         LA    R15,79               REPLY DATA SIZE
         CLI   WTOCMD+1,C'0'        2 CHAR REPLY?
         BL    BLANKOUT             NO
         LA    R14,1(R14)           REPLY DATA START
         BCTR  R15,0                REPLY DATA LENGTH
BLANKOUT DS    0H
         SR    R0,R0                ZERO
         LA    R1,64                LOAD BLANK
         SLL   R1,24                MOVE TO HI BYTE
         MVCL  R14,R0               CLEAR REPLY BUFFER
DOWTO    DS    0H
         LA    R1,WTOAREA           POINT TO WTO VERSION OF COMMAND
         SVC   35                   DISPLAY ON CONSOLE
NODOWTO  DS    0H
         MODESET KEY=ZERO           KEY 0 TO DO SVC 34
         LA    R1,CMND              POINT TO COMMAND
         XR    R0,R0                CLEAR FOR INSERT CHARACTER
         IC    R0,XUCMID            GET UCMID OF OUR 'CONSOLE'
         SVC   34                   FIRE THE COMMAND THROUGH
         B     GOTCONSL             GO TRY AGAIN
         TITLE '- TERMINAL I/O SUBROUTINES -'
WRITE    DS    0H
         LA    R1,SCREEN
         L     R0,LENGTH
         TPUT  (1),(0),FULLSCR      PUT BUFFER TO SCREEN
         BR    R9                   AND THEN RETURN
         SPACE 2
READ     DS    0F
         MVI   CLTCHNG,X'00'        RESET THE NEW CONSOLE BIT
         TGET  INPUT,122,ASIS       GET INPUT FROM USER
         XC    INPUTSCR,INPUTSCR    ZERO OUT INPUT AREA OF SCREEN BUFR
         CLI   INPUT,X'6C'          TEST IF PA1 HIT
         BE    RESHWCMD             BRANCH IF WAS
         CLI   INPUT,X'6D'          TEST IF CLEAR WAS HIT
         BE    RESHWSCR             BRANCH IF WAS
         CLI   INPUT,X'6E'          TEST IF PA2/RESHOW HIT
         BE    RESHWSCR             BRANCH IF WAS
         CLI   INPUT,X'F3'          TEST IF PF3 HIT
         BE    GOHOME               GO HOME IF WAS
         CLI   INPUT,X'C3'          TEST IF PF15 HIT
         BE    GOHOME               GO HOME IF WAS
         CLI   INPUT,X'7C'          TEST IF PF12 HIT
         BE    GOHOME               GO HOME IF WAS
         CLI   INPUT,X'4C'          TEST IF PF24 HIT
         BE    GOHOME               GO HOME IF WAS
         CLI   INPUT,X'7A'          TEST IF PF10 HIT
         BE    PF10000              GO LEFT IF IT WAS
         CLI   INPUT,X'4A'          TEST IF PF22 HIT
         BE    PF10000              GO LEFT IF IT WAS
         CLI   INPUT,X'7B'          TEST IF PF11 HIT
         BE    PF11000              GO RIGHT IF IT WAS
         CLI   INPUT,X'4B'          TEST IF PF23 HIT
         BE    PF11000              GO RIGHT IF IT WAS
         C     R1,F6                SEE IF ANY
         BNH   NOINPUT              BRANCH IF NOT
         S     R1,F7                DECREMENT FOR EX
         EX    R1,MOVEINP           COPY TO ENTRLNE
         ST    R1,LINPUT            SAVE LENGTH FOR LATER
         BR    R9                   GO BACK TO CALLER
RESHWSCR DS    0H
         TPUT  CLEAR,LCLEAR,NOEDIT  RESET SCREEN SIZE IF NECESSARY
RESHWCMD DS    0H
         LA    R1,INPUTSCR          GET ADDRESS OF SCREEN INPUT AREA
         L     R15,LINPUT           LOAD LENGTH OF LAST COMMAND
         EX    R15,MINPUT           COPY LAST COMMAND
NOINPUT  DS    0H
         MVC   ENTRLNE,ENTRLNE-1    CLEAR INPUT AREA
         BR    R9                   GO BACK TO CALLER
         USING CLT,R8
PF10000  EQU   *                    MOVE LEFT
         OI    CLTCHNG,X'80'        SET 'WERE CHANGING CONSOLES'
         L     R8,CLTNOW            GET THE CURRENT ONE
         S     R8,=F'12'            BACKUP ONE ENTRY
         C     R8,CLTSTART          COMPARE WITH THE START OF THE TABLE
         BNL   PF10010              USE THE ENTRY IF NOT BEFORE START
         L     R8,CLTEND            POINT AT EMPTY ENTRY
         S     R8,=F'12'            BACKUP TO THE LAST ENTRY AND USE IT
PF10010  MVC   MYUCB(3),CLTUCB      SAY WHICH ONE WE WANT
         B     NOINPUT              CLEAN UP
PF11000  DS    0F                   MOVE RIGHT
         OI    CLTCHNG,X'80'        SET 'WERE CHANGING CONSOLES'
         L     R8,CLTNOW            GET THE CURRENT ONE
         LA    R8,12(,R8)            POINT TO THE NEXT ENTRY
         C     R8,CLTEND            COMPARE WITH THE END OF THE TABLE
         BL    PF11010              USE THE ENTRY IF NOT AT END
         L     R8,CLTSTART          POINT AT FIRST ENTRY
PF11010  MVC   MYUCB(3),CLTUCB      SAY WHICH ONE WE WANT
         B     NOINPUT              CLEAN UP
         DROP  R8
         SPACE 2
MOVEINP  MVC   ENTRLNE(0),INPUT+6   COPY INPUT TO WORK AREA
MINPUT   MVC   0(0,R1),CMNDBUF      RESTORE LAST COMMAND ISSUED
         TITLE '- TERMINATION CODE -'
GOHOME   DS    0H
         AIF   ('&SP3' NE 'Y').SP1E
         LH    R2,MYASID            GET MY ASID BACK
         SSAR  R2                   GET OUT OF SECONDARY MODE
         SLR   R2,R2                SET UP AX OF 0
         MODESET KEY=ZERO           MUST BE KEY ZERO TO DO IT
         AXSET AX=(R2)              GET BACK TO DEFAULT AX
         MODESET KEY=NZERO          BACK TO USER KEY
.SP1E    ANOP
         MVI   CLEAR,X'7E'          RESET TO ERASE WRITE ALTERNATE
         TPUT  CLEAR,LCLEAR,NOEDIT  CLEAR THE SCREEN
         STTMPMD  OFF,KEYS=NO       SAY NOT IN FULL CONTROL NOW
         STLINENO LINE=1
         STFSMODE OFF               TURN OFF FULL SCREEN MODE
BYEBYE   DS    0H
         CLI   APFTRIP,X'FF'        DID WE HAVE TO DIDDLE          SCT
         BNE   NODIDDLE             NOPE                           SCT
         LA    R0,0                 HOKUS, POKUS                   SCT
         SVC   251                  SCT
NODIDDLE L     R13,4(,R13)          GET OLD SAVE AREA BACK
         LM    R14,R12,12(R13)      RESTORE ORIGINAL REGISTERS
         SLR   R15,R15              CONDITION CODE 0
         BR    R14                  RETURN TO MOTHER
         TITLE '- ERROR MESSAGES SENT HERE -'
* NOTALLWD TPUT  NOTALLWM,L'NOTALLWM,EDIT  SAY NOT ALLOWED TO USE  SCT
*          B     BYEBYE                                            SCT
         USING CPPL,R2                                             SCT
NOTALLWD L     R1,CPPLCBUF                                         SCT
         XC    2(2,R1),2(R1)            SET CBUF TO IMPLICIT EXEC  SCT
         L     R1,CPPLECT               GET ECT ADDRESS            SCT
         CLI   20(R1),C' '              IS THIS A SUBCOMMAND       SCT
         BNE   *+10                     SAY SUBCOMMAND NOT FOUND   SCT
         MVC   12(8,R1),=CL8'EXEC'      ELSE COMMAND NOT FND       SCT
         DROP  R1,R2                                               SCT
         L     R13,4(,R13)                                         SCT
         L     R1,24(,R13)              RESTORE CPPL POINTER       SCT
         LA    R15,20(,R13)             POINT TO 2-WORD XCTL PARM  SCT
         XC    0(8,R15),0(R15)          CLEAR IT                   SCT
         XCTL  (2,12),EP=EXEC,SF=(E,(15)) COMPLETE THE SMOKESCREEN SCT
         SPACE 2
NOTAPF   LA    R0,1                 TRY DIDDLING WITH IT           SCT
         SVC   251                                                 SCT
         MVI   APFTRIP,X'FF'        FLAG IT FOR EXIT               SCT
         TESTAUTH FCTN=1            CHECK IT NOW                   SCT
         LTR   R15,R15              HAVE WE FIXED IT               SCT
         BZ    APFOK                PROPER INCANTATION USED        SCT
         TPUT  NOTAPFM,L'NOTAPFM,EDIT    SAY NOT APF AUTHORIZED
         B     BYEBYE
         SPACE 2
NOTSCRN  TPUT  NOTSCRNM,L'NOTSCRNM,EDIT  SAY CANNOT SUPPORT NON-327X
         B     BYEBYE
         SPACE 2
BADPARM  TPUT  BADPARMM,L'BADPARMM,EDIT  SAY BAD PARAMETER SPECIFIED
         B     BYEBYE
         SPACE 2
NOCONSL  TPUT  NOCONSLM,L'NOCONSLM,EDIT  SAY CONSOLE NACT/NFG
         B     BYEBYE
         PRINT GEN
         TITLE '- DATA AREAS NEEDED TO RUN PARSE -'
         DS    F
PASSWORD DC    X'ABCFEDAD'
ECB      DC    F'0'                 ECB FOR PARSE
ANSWER   DC    F'0'                 ADDRESS OF ANSWER AREA
PPLAREA  DC    7F'0'                PARSE PARAMETER LIST
         TITLE '- OTHER DATA AREAS AND THE LIKE -'
XA22     DC    X'00'                                               SCT
APFTRIP  DC    X'00'                                               SCT
MYRDCM   DS    CL(DCMSIZE)                                         SCT
F3       DC    F'3'
F4       DC    F'4'
F6       DC    F'6'
F7       DC    F'7'
F20      DC    F'20'                MESSAGE AREA LIMIT ON MOD 2 SCREEN
F28      DC    F'28'                MESSAGE AREA LIMIT ON MOD 3 SCREEN
F32      DC    F'32'                FULL SIZE OF MOD 3 SCREEN
F39      DC    F'39'                MESSAGE AREA LIMIT ON MOD 4 SCREEN
F43      DC    F'43'                FULL SIZE OF MOD 4 SCREEN
F80      DC    F'80'                ONLY SUPPORTED SCREEN WIDTH
F81      DC    F'81'                SIZE OF EACH OUTPUT BUFFER LINE
MAXLINES DC    F'0'                 SAVE MAX NUMBER OF LINES WE SUPPORT
DEPTH    DC    F'0'                 MY SCREEN DEPTH
MYASCRN  DC    F'0'                 MY COPY OF CONSOLE BUFFER ADDRESS
LASTLINE DC    A(0)                 ADDRESS OF LAST OUTPUT LINE ALLOWED
LASTMOD2 DC    A(LINE20)            ADDRESS OF LAST LINE ON MODEL 2
LASTMOD3 DC    A(LINE28)            ADDRESS OF LAST LINE ON MODEL 3
LASTMOD4 DC    A(LINE39)            ADDRESS OF LAST LINE ON MODEL 4
H2       DC    H'2'                 CONSTANT
         AIF   ('&SP3' NE 'Y').SP1F
MYASID   DC    H'0'                 MY ADDRESS SPACE ID
ASIDCONS DC    H'7'                 ASID OF 'CONSOLE' ADDRESS SPACE
.SP1F    ANOP
MYMSGAL  DC    H'0'                 MY COPY OF CONSOLE INPUT AREA SIZE
RESOURCE DC    H'7',C'CONSOLE'      RESOURCE PARAMETER FOR ACF2 CHECKER
         SPACE 1
WTOAREA  DC    H'101',X'8000',C'### '  WTO OF INPUT COMMAND
USERID   DC    CL7' '               USERID IN ABOVE
         DC    C' ###: '            REST OF FILLER
WTOCMD   DS    CL80                 COPY OF INPUT BUFFER
ROUTCDE  DC    X'00008000'          ROUT=(1)
MYIONDX  DC    X'0'                 MY COPY OF DCMIONDX
CLTCHNG  DC    X'00'                '80' - CHANGE OF CONSOLE REQUESTED
CLTCOUNT DC    1F'0'                COUNT OF ENTRIES IN CLT
CLTMSTR  DC    1F'0'                ADDR IN CLT FOR MASTER CONSOLE
CLTSTART DC    1F'0'                ADDR OF FIRST SLOT IN CLT
CLTEND   DC    1F'0'                ADDR OF FIRST AVAIL SLOT IN CLT
CLTNOW   DC    1F'0'                ADDR OF ACTIVE CLT
CLTWORK  DS    120X'00'             CONSOLE LOOKUP TABLE
VDELCON  DS    1F                   ADDR DEL/CON/SEG VALUES EYE
DELCON   DC    XL12'0'              DEL/CON/SEG VALUES EYE
CHAR4    DS    1F                   UPACK SOURCE
         DC    X'DD'                DUMMY SIGN
DOUBLE   DC    1D'0'                WORKING DOUBLE WORD
TRHEX    DC    C'0123456789ABCDEF'  HEX TRANSLATE
PIC3D    DC    X'F0212020'   P'999'
MODEMSTR DC    C'K S,DEL=RD,SEG=16,CON=N,RNUM=16,RTIME=001,MFORM=J   '
MYUCB    DC    XL3'0'               MY COPY OF EBCDIC DEVICE ADDRESS
HOLDUCB  DC    XL3'0'               COPY OF DEVICE ADDRESS OF COMMAND
         SPACE 1
NOTALLWM DC    C'YOU ARE NOT AUTHORIZED - CONTACT 3259 WWL FOR SUPPORT'
NOTAPFM  DC    C'ERROR: NOT RUNNING APF AUTHORIZED'
NOTSCRNM DC    C'ERROR: ONLY 327X TYPE TERMINALS ARE SUPPORTED'
BADPARMM DC    C'ERROR: PARAMETER INVALID'
NOCONSLM DC    C'ERROR: CONSOLE INACTIVE OR INVALID'
TRUNCATE DC    C'WARNING: DISPLAY TRUNCATED. CONSOLE LARGER THAN YOUR TX
               ERMINAL'
LTRUNCT  EQU   *-TRUNCATE
         SPACE 1
CMND     DC    H'120',H'0'          PARAMETER TO SVC 34
CMNDBUF  DC    CL116' '             COMMAND BUFFER
XUCMID   DC    X'0'                 CONSOLE ID FOR SVC 43
         SPACE 1
INPUT    DC    CL122' '             INPUT BUFFER
         DC    C' '
ENTRLNE  DC    CL116' '             EDITED COPY OF ABOVE
         SPACE 1
CLEAR    DC    X'7E73114040'
LCLEAR   EQU   *-CLEAR
         TITLE '- SBA''S FOR DYNAMIC PARTS OF SCREEN -'
         SPACE 3
SBA2101  DC    X'11D940'            FOR MOD 2 SCREEN
SBA2201  DC    X'115A50'
SBA2202  DC    X'115AD1'
SBA2380  DC    X'115C6F'
SBA2401  DC    X'115CF0'
SBA2901  DC    X'11E340'            FOR MOD 3 SCREEN
SBA3001  DC    X'11E450'
SBA3002  DC    X'11E4D1'
SBA3180  DC    X'11E66F'
SBA3201  DC    X'11E6F0'
SBA4001  DC    X'11F0F0'            FOR MOD 4 SCREEN
SBA4101  DC    X'11F240'
SBA4102  DC    X'11F2C1'
SBA4280  DC    X'11F45F'
SBA4301  DC    X'11F460'
         TITLE '- SAVE AREA AND LITERAL POOL -'
SAVE     DC    18F'0'               MY SAVE AREA
         SPACE 5
         LTORG
         TITLE '- SCREEN BUFFER -'
SCREEN   DS    0C
         DC    X'42'
MSGSBA   DC    X'110000'            21,01/29,1/40,1
         DC    X'1D68'
MSGLINE  DC    C'CONSOLE:XXX ----------- PF10,PF11:SWITCH'
         DC    C'CONSOLE --------- DISPLAY TIME099:99:99'
COMNDSBA DC    X'1100001D4113'      22,01/30,1/41,1
INPUTSCR DC    XL128'00'
         DC    X'1D68'
BOTTMSBA DC    X'110000'            24,01/32,1/43,1
         DC    C' '
MODE     DC    C'K S,DEL=RD,SEG=16,CON=N,RNUM=16,RTIME=001,MFORM=J   '
*S,DEL=RD,SEG=16,CON=N,RNUM=16,RTIME=001,MFORM=J '
         DC    X'1140401D'
LINE1    DC    CL80' '
         DC    X'1D'
LINE2    DC    CL80' '
         DC    X'1D'
LINE3    DC    CL80' '
         DC    X'1D'
LINE4    DC    CL80' '
         DC    X'1D'
LINE5    DC    CL80' '
         DC    X'1D'
LINE6    DC    CL80' '
         DC    X'1D'
LINE7    DC    CL80' '
         DC    X'1D'
LINE8    DC    CL80' '
         DC    X'1D'
LINE9    DC    CL80' '
         DC    X'1D'
LINE10   DC    CL80' '
         DC    X'1D'
LINE11   DC    CL80' '
         DC    X'1D'
LINE12   DC    CL80' '
         DC    X'1D'
LINE13   DC    CL80' '
         DC    X'1D'
LINE14   DC    CL80' '
         DC    X'1D'
LINE15   DC    CL80' '
         DC    X'1D'
LINE16   DC    CL80' '
         DC    X'1D'
LINE17   DC    CL80' '
         DC    X'1D'
LINE18   DC    CL80' '
         DC    X'1D'
LINE19   DC    CL80' '
         DC    X'1D'
LINE20   DC    CL80' '
ENDMOD2  EQU   *
         DC    X'1D'
LINE21   DC    CL80' '
         DC    X'1D'
LINE22   DC    CL80' '
         DC    X'1D'
LINE23   DC    CL80' '
         DC    X'1D'
LINE24   DC    CL80' '
         DC    X'1D'
LINE25   DC    CL80' '
         DC    X'1D'
LINE26   DC    CL80' '
         DC    X'1D'
LINE27   DC    CL80' '
         DC    X'1D'
LINE28   DC    CL80' '
ENDMOD3  EQU   *
         DC    X'1D'
LINE29   DC    CL80' '
         DC    X'1D'
LINE30   DC    CL80' '
         DC    X'1D'
LINE31   DC    CL80' '
         DC    X'1D'
LINE32   DC    CL80' '
         DC    X'1D'
LINE33   DC    CL80' '
         DC    X'1D'
LINE34   DC    CL80' '
         DC    X'1D'
LINE35   DC    CL80' '
         DC    X'1D'
LINE36   DC    CL80' '
         DC    X'1D'
LINE37   DC    CL80' '
         DC    X'1D'
LINE38   DC    CL80' '
         DC    X'1D'
LINE39   DC    CL80' '
ENDMOD4  EQU   *
LMOD2    EQU   ENDMOD2-SCREEN
LMOD3    EQU   ENDMOD3-SCREEN
LMOD4    EQU   ENDMOD4-SCREEN
         SPACE 1
LENGTH   DC    A(0)
LENGTH2  DC    A(LMOD2)
LENGTH3  DC    A(LMOD3)
LENGTH4  DC    A(LMOD4)
         SPACE 1
LINPUT   DC    F'0'
         TITLE '- TRANSLATE TABLE - CONVERT NON-PRINTABLES TO BLANKS -'
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
BUFFER   DS    39CL80               BUFFER FOR HOLDING SCREEN COPY
         TITLE '- DEFINE THE PARAMETERS EXPECTED (FOR IKJPARSE) -'
PCL      IKJPARM DSECT=PDL
PARM     IKJIDENT 'CHAR',UPPERCASE,FIRST=ANY,OTHER=ANY
         IKJENDP
         TITLE '- UCM DEFINITION -'
         IEECUCM FORMAT=NEW
         TITLE '- RESIDENT DCM DEFINITION (RDCM) -'
DCMTSRT  DSECT
         IEERDCM
         TITLE '- TRANSIENT DCM DEFINITION (TDCM) -'
DCMSTRT  DSECT
         IEECDCM
         AIF   ('&SP3' NE 'Y').SP1G
         TITLE '- PREFIX STORAGE AREA DEFINITION (PSA) -'
         IHAPSA
         TITLE '- ADDRESS SPACE CONTROL BLOCK DEFINITION (ASCB) -'
         IHAASCB
.SP1G    ANOP
         TITLE '- ENVIRONMENT CONTROL TABLE -'                     SCT
         IKJECT                                                    SCT
         TITLE '- COMMAND PROCESSOR PARAMETER LIST DEFINITION (CPPL) -'
         IKJCPPL
         TITLE '- PROTECTED STEP CONTROL BLOCK DEFINITION (PSCB) -'
         IKJPSCB
         TITLE '- PARSE PARAMETER LIST -'
         IKJPPL
         TITLE '- CVT DEFINITION -'
         CVT   DSECT=YES,LIST=YES
         TITLE 'CONSOLE LOOKUP TABLE (FOR PF10/PF11) -'
CLT      DSECT
CLTUCB   DS    3C             UCB ADDRESS
CLTUCMID DS    1C             UCMID FOR SVC34
CLTUCMXB DS    1F             ADDR OF RESIDENT DCM
CLTADTRN DS    1F             ADDR OF PAGEABLE DCM
         END
