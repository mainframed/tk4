         MACRO
&NAME    RETRN &R,&FPR,&SAVE=,&BASE=,&SPIE=YES,&ID=YES,&RC=,&SAVEREG=
         LCLC  &BASE1,&HOLD,&SPIE1
.*
.*       AUTHOR - GARY R. KOENIG
.*
.*       DATE - NOVEMBER 1, 1969
.*
.*       FUNCTION - THE RETRN MACRO IS USED IN ASSEMBLER ROUTINES TO
.*         RETURN TO THE CALLING PROGRAM.  RETRN SHOULD BE USED ONLY
.*         AND ALWAYS IF THE ENTER MACRO IS USED.
.*
.*       CODING - RETRN IS CODED INTO THE ASSEMBLER PROGRAM ACCORDING
.*         TO THE RULES OF MACRO INSERTION.  ALL PARAMETERS SHOULD BE
.*         CODED EXACTLY THE WAY THEY WERE FOR THE ENTER MACRO.  IN
.*         ADDITION TWO MORE KEYWORD PARAMETERS CAN BE CODED FOR THE
.*         RETRN MACRO.  THESE ARE AS FOLLOWS:
.*         1)  &RC IS USED TO PASS A RETURN CODE THROUGH REGISTER 15.
.*           IF RC=20 IS CODED, 20 WILL BE THE RETURN CODE PASSED IN
.*           REGISTER 15.  IF REGISTER 15 ALREADY CONTAINS THE RETURN
.*           CODE, CODE RC=(15).
.*         2)  &SAVEREG IS USED TO INDICATE THE SUBPROGRAM IS A FUNC-
.*           TION AND A VALUE IS BEING RETURNED IN REGISTER 0.  TO
.*           INDICATE THIS, CODE &SAVEREG=0.
&BASE1   SETC  '&BASE'
&HOLD    SETC  '&R'.'2'
&SPIE1   SETC  '&SPIE'
         AIF   ('&BASE' NE '15' AND '&BASE' NE '1').C0
&SPIE1   SETC  'NO'
         AGO   .C2
.C0      AIF   ('&BASE1' NE '2' AND '&SAVEREG' NE '2').C1
&HOLD    SETC  '&R'.'3'
         AGO   .C2
.C1      AIF  ('&BASE' NE '14' AND '&BASE' NE '0' AND '&BASE' NE '').C3
&BASE1   SETC  '13'
.C2      AIF   ('&BASE1' NE '13' OR '&SAVE' EQ '').C3
&BASE1   SETC  '12'
.C3      AIF   ('&NAME' EQ '').C4
&NAME    DS    0H
.C4      AIF   ('&BASE1' EQ '15').C7
         AIF   ('&SPIE1' EQ 'NO').C5
         L     &R.1,0(&R.13)      RELOAD THE PREVIOUS PICA ADDRESS
         SVC   14                 ISSUE SPIE SVC
.C5      AIF   ('&ID' EQ 'NO' OR '&SAVE' EQ 'RENT').C6
         MVI   0(&R.13),X'FF'     FLAG ROUTINE COMPLETE
.C6      L     &R.13,4(&R.13)     LOAD ADDR OF PREVIOUS SAVE AREA
         AIF   ('&SAVE' EQ 'RENT').C8
.C7      AIF   ('&SAVEREG' EQ '0' OR '&SAVEREG' EQ '&R.0').C12
         AGO   .D8
.C8      AIF   ('&SAVEREG' EQ '0' OR '&SAVEREG' EQ '&R.0').C11
         LA    &R.0,72            LOAD LENGTH
         SVC   10                 ISSUE FREEMAIN SVC
.D8      AIF   ('&RC' EQ '(15)' OR '&RC' EQ '(&R.15)').C10
         AIF   ('&RC' NE '').C9
         LM    &R.14,&R.12,12(&R.13)   RESTORE REGISTERS
         AGO   .END
.C9      LA    &R.15,&RC          LOAD RETURN CODE
.C10     L     &R.14,12(&R.13)    RESTORE
         LM    &R.0,&R.12,20(&R.13) REGISTERS
         AGO   .END
.C11     LR    &HOLD,&R.0         SAVE REG 0
         LA    &R.0,72            LOAD LENGTH
         SVC   10                 ISSUE FREEMAIN SVC
         LR    &R.0,&HOLD         RESTORE REG 0
.C12     AIF   ('&RC' EQ '(15)' OR '&RC' EQ '(&R.15)').C14
         AIF   ('&RC' NE '').C13
         LM    &R.14,&R.15,12(&R.13)   RESTORE
         LM    &R.1,&R.12,24(&R.13)      REGISTERS
         AGO   .END
.C13     LA    &R.15,&RC          LOAD RETURN CODE
.C14     L     &R.14,12(&R.13)    RESTORE
         LM    &R.1,&R.12,24(&R.13) REGISTERS
.END     BR    &R.14              RETURN CONTROL
         MEND
