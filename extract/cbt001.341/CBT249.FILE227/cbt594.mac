*          DATA SET CBT594     AT LEVEL 001 AS OF 01/14/80
CBT594   SAVEALL REG=2              SAVE EVERYTHING BASE IS REG 2
*
* THIS PROGRAM ATTACHES THE TERMINAL MONITOR PROGRAM SO A PROGRAMMER
* WILL BE ABLE TO GO BACK TO READY WHENEVER HE/SHE WISHES
*
STAE     STAE  0                    TURN OFF DUMP
         LTR   R15,R15              IS IT ZERO YET
         BZ    STAE                 NO IT IS NOT
         LINK  EP=CLEAR             CLEAR THE SCREEN FOR THEM
         TPUT  HELP,60              TELL THEM WERE WE ARE
         SR    1,1                  ZERO OUT REGISTER 1 FOR ATTACH
         SVC   254                  PREPARE FOR ATTACH
         ATTACH EP=IKJEFT01,ECB=WRKECB,STAI=CBTSTAI,PARAM=(OPTLST),VL=1
         ST    R1,WRKTCB            SAVE THE TCB ADDRESS
         WAIT  ECB=WRKECB           WAIT UNTIL HE IS DONE
         DETACH WRKTCB              DETATCH IKJEFT01
         LA    1,4                  PUT A 4 IN REGISTER 1 FOR DETACH
         SVC   254                  CLEAN UP AFTER DETACH
         ENDALL
WRKECB   DC    F'0'                 AN ECB
WRKTCB   DC    F'0'                 A TCB ADDRESS
OPTLST   DC    F'0'                 NEED AN OPTLIST LIST OF 0'S
HELP     DC    CL60'FOR INFORMATION ON READY TYPE IN HELP READY'
     EJECT
CBTSTAI  CSECT                      A NEW CSECT TO HANDLE ABEND
         SAVE  (14,12)              SAVE
         LR    R3,R15               START FOR USING
         USING CBTSTAI,R3           BASE
         ST    R13,SAVA+4           SET UP
         LA    R13,SAVA             STORING CONVENTIONS
         LA    1,4                  A 4 IN REGISTER 1
         SVC   254                  SVC 254
         L     R13,SAVA+4           RESTORE
         RETURN (14,12),RC=0        AND RETURN
SAVA     DC    18F'0'               PLACE TO SAVE REGISTERS
         END
