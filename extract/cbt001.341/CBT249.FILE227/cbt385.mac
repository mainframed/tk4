CBT385   SAVEALL REG=(2)            SAVE EVERYTHING BASE IS REG 2
*
* THIS PROGRAM ATTACHES IEBCOPY SO A USER MAY DO
* IT FROM HIS TERMINAL
*
         STAX  DEFER=YES            NO INTERRUPTS
         TPUT  MSG1,L'MSG1          TELL HIM NO INTERRUPTS
STAE     STAE  0                    TURN OFF DUMP
         LTR   R15,R15              IS IT ZERO YET
         BZ    STAE                 NO IT IS NOT
         SR    1,1                  ZERO OUT REGISTER 1
         SVC   254                  SVC 254
         ATTACH EP=IEBCOPY,ECB=WRKECB,STAI=CBTSTAI,PARAM=(OPTLST),VL=1
         ST    R1,WRKTCB            SAVE THE TCB ADDRESS
         WAIT  ECB=WRKECB           WAIT UNTIL HE IS DONE
         DETACH WRKTCB              DETATCH IEBCOPY
         LA    1,4                  PUT A 4 IN REGISTER 1
         SVC   254                  SVC 254
         STAX  DEFER=NO             LET HIM INTERRUPT
         ENDALL
WRKECB   DC    F'0'                 AN ECB
WRKTCB   DC    F'0'                 A TCB ADDRESS
OPTLST   DC    F'0'                 NEED AN OPTLIST LIST OF 0'S
MSG1     DC    C'***    YOU CANNOT INTERRUPT FROM THIS COMMAND     ***'
     EJECT
CBTSTAI  CSECT                      A NEW CSECT TO HANDLE ABEND
         SAVE  (14,12)              SAVE
         LR    R3,R15               START FOR USING
         USING CBTSTAI,R3           BASE
         ST    R13,SAVA+4           SET UP
         LA    R13,SAVA             STORING CONVENTIONS
         LA    1,4                  A 4 IN REGISTER 1
         SVC   254                  SVC 254
         L     R13,SAVA+4           RESTORE
         RETURN (14,12),RC=0        AND RETURN
SAVA     DC    18F'0'               PLACE TO SAVE REGISTERS
         END
