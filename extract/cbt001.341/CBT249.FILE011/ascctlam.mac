ACTL     TITLE 'ASCCTLAM - PROBLEM STATE SUBSYSTEM OPEN RTN'
***************************************************************
* NAME = ASCCTLAM                                             *
* DESCRIPTIVE NAME = CONTROL CHARACTER TERMINAL OUTPUT        *
* (C) COPYRIGHT 1982 YALE COMPUTER CENTER                     *
* STATUS = VERSION 1 MODIFICATION LEVEL 0                     *
* SYSTEM = MVS 3.8                                            *
* ATTRIBUTES = NONE                                           *
* LINKAGE = FROM GPSMOPEN AT OPEN AND CLOSE                   *
* NORMAL RETURN =                                             *
*      GPSMAMEP SET TO GET/PUT ENTRY POINT OF ROUTINE         *
***************************************************************
         PRINT OFF
         COPY  HAL
         PRINT ON
ASCCTLAM CSECT
         USING *,R15
         SAVE  (14,12),,*
         LR    R10,R15
         USING ASCCTLAM,R10
         DROP  R15
         LR    R2,R1
         USING AMPARM,R2
         L     R6,GPSMSSOB        GET SSOB POINTER
         USING SSOB,R6            NOW CHAIN DOWN TO ACB
DEBUG1   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * IF ZAPPED ON, INVOKE THE YALE DEBUGGER             *
*        ******************************************************
         LOAD  EP=DBC
         LR    R3,R0
         ST    R3,DBCADDR         SAVE ADDRESS FOR PUT/GET
         ESTAE (R3)
         #DIE
         BEND  DEBUG1
         TITLE 'ASCCTLAM - PROCESS OPEN/CLOSE REQUEST'
OPENCLOS IF    (SSOBFUNC+1,EQ,SSOBOPEN,CLI),THEN
*        ******************************************************
*        * FUNCTION IS OPEN.                                  *
*        ******************************************************
         LA    R1,PUTGET
         ST    R1,GPSMAMEP
         ELSE  ,
*        ******************************************************
*        * THE FUNCTION IS CLOSE                              *
*        ******************************************************
         IF    (FLAGWORD,ON,DEBUG),THEN
         DELETE EP=DBC
         BEND  ,
         BEND  OPENCLOS
         TITLE 'ASCCTLAM - RETURN TO CALLER'
DEBUG2   IF    (FLAGWORD,ON,DEBUG),THEN
*        ******************************************************
*        * CANCEL THE YALE DEBUGGER                           *
*        ******************************************************
         ESTAE 0
         BEND  DEBUG2
RET      L     R13,4(R13)         BACK TO SAVE1
         RETURN (14,12),RC=0
         DROP  ,
         TITLE 'ASCCTLAM - PUT/GET ROUTINE ENTRY POINT'
***************************************************************
* ENTRY = PUTGET                                              *
* FUNCTION = HANDLE I/O REQUEST TO GPSAM FILE                 *
* LINKAGE = FROM GET/PUT/NOTE/POINT/ ETC. MACRO               *
*                                                             *
*        L     R1,RPL                                         *
*        LA    R0,FUNCTION                                    *
*        L     R15,24(R1)         POINT TO ACB                *
*        L     R15,8(R1)          POINT TO MY ENTRY POINT     *
*        BALR  R14,R15                                        *
*                                                             *
* FUNCTION CODES:                                             *
*        0     GET                READ LINE ASIS FROM TERM    *
*        1     PUT                TPUT CONTROL                *
*       >1                        NOT SUPPORTED               *
***************************************************************
PUTGET   BLOCK ,
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         LR    R2,R1              SAVE RPL ADDRESS
         USING IFGRPL,R2
         L     R1,RPLAREA         PICK UP BUFFER
         IF    (R0,EQ,0),THEN     IF ITS A GET
         LR    R3,R1
         L     R0,RPLBUFL         GET BUFFER LENGTH
         TGET  (1),(0),ASIS       READ DATA
         ST    R1,RPLRLEN
         IF    (R1,GE,2),AND,     LOOK FOR "/*"                        X
               (0(2,R3),EQ,SLSHSTAR,CLC)
         MVC   RPLFDBK,EOFCODE
         ELSE  ,
         XC    RPLFDBK,RPLFDBK
         BEND  ,
         ELSE  ,
         L     R0,RPLRLEN         GET DATA LENGTH
         TPUT  (1),(0),CONTROL    TRANSMIT WITHOUT EDIT
         XC    RPLFDBK,RPLFDBK    CLEAR RETURN CODE
         BEND  ,
         LR    R1,R2
         L     R15,=V(GPSMEXIT)
         BR    R15
         BEND  PUTGET
         TITLE 'ASCCTLAM - CONSTANTS, VARIABLE, AND DSECTS'
FLAGWORD DC    AL1(0)
DEBUG    EQU   X'80'
         DC    3AL1(0)
DBCADDR  DC    A(*-*)
EOFCODE  DC    AL1(RPLLOGER,0,RPLEODER)
SLSHSTAR DC    C'/*'
         LTORG
         SPACE 5
AMPARM   DSECT
GPSMMPRM DS    A                  POINTER TO MODULE PARM
GPSMSSOB DS    A                  POINT TO OPEN SSOB
GPSMAMEP DS    A                  RETURN ACB AM ENTRY POINT
GPSMSAVA DS    A                  WORD TO SAVE FOR CLOSE
GPSMDDN  DS    CL8                DDNAME
         PRINT NOGEN
         EQUREGS
         IDARMRCD
         IFGRPL
         IEFJSSOB DA
         END
