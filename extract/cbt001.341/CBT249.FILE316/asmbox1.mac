         TITLE '   A S M B O X 1    '
*$DOC@*****************************************************************
*                                                                     *
*  ASMBOX1        - INVOKE ASSEMBLER H, BORDER THE LISTING            *
*                                                                     *
***********************************************************************
*
*  WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*  INSTALLATION. AIR FORCE DATA SERVICES CENTER, THE PENTAGON.
*  DATE WRITTEN. DECEMBER 9 1981.
*  DATE UPDATED.
*  ATTRIBUTES. RE-ENTRANT.
*
*  THIS PROGRAM INVOKES THE ASSEMBLER (H) AND THEN
*  INVOKES A PROGRAM WHICH PUTS A BORDER AROUND EACH PAGE OF THE
*  LISTING ON A 3800 PRINTER.
*
*  THE ASSEMBLER PRINT OUTPUT IS DIRECTED TO AN INTERMEDIATE
*  SYSUT9 FILE.  AFTER THE ASSEMBLER ENDS, THAT FILE IS READ
*  BY THE BORDER PROGRAM, AND WRITTEN TO DDNAME 'BORDER'.
*
*  BEFORE CALLING THE BORDER PROGRAM, A CHECK IS MADE TO SEE IF
*  DDNAME 'BORDER' IS ABSENT OR DUMMY, AND IF SO, THEN THE BORDER
*  PROGRAM IS NOT CALLED AND THIS PROGRAM COPIES SYSUT9 TO SYSPRINT.
*
*  DDNAMES REQUIRED -
*     SAME DDNAMES AS ASSEMBLER H REQUIRES, EXCEPT SYSPRINT.
*        NAMELY - SYSIN, SYSLIB, SYSUT1,
*        AND OPTIONALLY SYSLIN, SYSPUNCH, SYSTERM.
*     PLUS 2 NEW DDNAMES -
*     SYSUT9 - WORKSPACE WHERE ASSEMBLER WILL WRITE SYSPRINT
*              AND THE BORDER PROGRAM WILL READ IT BACK IN.
*     BORDER - BORDERED PRINT OUTPUT, USUALLY ALLOCATED TO SYSOUT
*              FOR A 3800 PRINTER WITH CHARS=(FM10,GS10).
*     IF BORDER IS DUMMY OR ABSENT, SYSPRINT IS REQUIRED.
*
*  PARM FIELD -
*     WILL BE PASSED TO THE ASSEMBLER UNCHANGED.
*
*  EXTERNAL CALLS -
*     INVOKES 'IEV90' (IBM PROGRAM PRODUCT, ASSEMBLER H),
*     AND 'P38BOX1' (PRINT BORDER PROGRAM).
*
*  CHANGES -
*     NONE YET.
*
*  WHY NOT JUST LET THE ASSEMBLER WRITE TO SYSPRINT WHEN THE
*  BORDER DD IS MISSING OR DUMMY?  BECAUSE WE HAVE SOME PROCS
*  THAT PUT A BANNER IN A PRINT FILE IN STEP 1, PASS THE FILE
*  TO STEP 2 (THIS PROGRAM) UNDER DDNAME SYSUT9, WHERE THE
*  ASSEMBLY LISTING IS APPENDED TO THE BANNER DISP=MOD.
*  THE PROCS REQUIRE THAT THE LISTING BE PUT IN SYSUT9.
*
*$DOC$*****************************************************************
         EJECT
ASMBOX1  START
         USING *,R15
         B     @PROLOG
         DC    AL1(11),CL11'ASMBOX1'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
         SPACE
         ENTRY ASMBOX1S
         USING *,R15
ASMBOX1S STM   14,12,12(R13)       SAVE REGISTERS
         LA    R3,1                INDICATE ALTERNATE ENTRY
         SH    R15,@ENTRYB         ADJUST BASE ADDRESS
         USING ASMBOX1,R15         ADJUST ASSEMBLER TO NEW BASE
         B     @PROLOGE            JUMP IN
@ENTRYB  DC    AL2(ASMBOX1S-ASMBOX1) OFFSET FROM TOP
         SPACE
@PROLOG  STM   14,12,12(R13)       SAVE REGISTERS
         SLR   R3,R3               INDICATE PRIMARY ENTRY
@PROLOGE LR    R12,R15             LOAD BASE REGISTER
         USING ASMBOX1,R12
         DROP  R15
         LR    R2,R1               PARM POINTER
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R9,R1               INITIALIZE WORKAREA POINTER
         USING @DATA,R9
         SPACE 1
         XC    000(256,R1),000(R1)
         XC    256(256,R1),256(R1)
         XC    512(@DATAL-512,R1),512(R1)
         SPACE 1
         ST    R13,4(,R9)          CHAIN SAVEAREAS
         ST    R9,8(,R13)          CHAIN SAVEAREAS
         LR    R13,R9              UPDATE SAVEAREA POINTER
         EJECT
************************************************************
*                                                          *
*        DETERMINE WHICH PROGRAM TO INVOKE                 *
*                                                          *
************************************************************
         SPACE
         MVC   ASMNAME,=CL8'IEV90'
         MVC   PGMNAME,=CL8'P38BOX1'
         SPACE
************************************************************
*                                                          *
*        SET UP PARM LISTS                                 *
*                                                          *
************************************************************
         SPACE
         L     R2,0(,R2)
         ST    R2,PARM1PTR         STORE ADDRESS OF PARM
         MVI   PARM1PTR,0
         LA    R0,DDNL1            POINT TO DDNAME LIST
         ST    R0,PARM1PTR+4       STORE ADDRESS OF DD LIST
         OI    PARM1PTR+4,X'80'    INDICATE END OF ADDRESS LIST
         LA    R0,PARM2LEN         POINT TO BORDER PARM
         ST    R0,PARM2PTR         STORE ADDRESS OF PARM
         LA    R0,PARM2DDL         POINT TO BORDER DDNAMES
         ST    R0,PARM2PTR+4       STORE ADDRESS OF DDNAMES
         OI    PARM2PTR+4,X'80'    INDICATE END OF ADDRESS LIST
         XC    PARM2LEN(2),PARM2LEN
         EJECT
************************************************************
*                                                          *
*        DDNAME LIST                                       *
*                                                          *
************************************************************
         SPACE
*                      ASSEMBLER H
*                      --------
*                   1  SYSLIN
*                   2  .
*                   3  .
*                   4  SYSLIB
*                   5  SYSIN
*                   6  SYSPRINT
*                   7  SYSPUNCH
*                   8  SYSUT1
*                   9  SYSTERM
         SPACE
************************************************************
*                                                          *
*        CHANGE ASSEMBLER DDNAMES                          *
*                                                          *
************************************************************
         SPACE
         MVC   DDNL1(2),=AL2(09*8) LENGTH OF DDNAMES
         LA    R1,DDNL1+2
         XC    0(09*8,R1),0(R1)
         MVC   (6-1)*8(8,R1),=CL8'SYSUT9' SYSPRINT NOW SYSUT9
         SPACE
************************************************************
*                                                          *
*        CHANGE BORDER DDNAMES                             *
*                                                          *
************************************************************
         SPACE
         LA    R0,72               LENGTH OF DDNAME LIST
         STH   R0,PARM2DDL
         MVC   PARM2DDL+58(8),=CL8'SYSUT9' INPUT DDNAME
         MVC   PARM2DDL+66(8),BORDERDD     OUTPUT DDNAME
         EJECT
************************************************************
*                                                          *
*        CALL THE ASSEMBLER                                *
*                                                          *
************************************************************
         SPACE
         LA    R1,PARM1PTR
         SPACE
         LINK  EPLOC=ASMNAME,SF=(E,LINKAREA)
         SPACE
         LR    R2,R15              SAVE ASSEMBLER RETURN CODE
         SPACE
************************************************************
*                                                          *
*        IF BORDER IS DUMMY, COPY SYSUT9 TO SYSPRINT       *
*                                                          *
************************************************************
         SPACE
         DEVTYPE BORDERDD,DEVDATA
         LTR   R15,R15             IS BORDER DD PRESENT
         BNZ   COPY                NO, BRANCH TO COPY
         NC    DEVDATA(8),DEVDATA  IS BORDER DD DUMMY
         BNZ   BORDER              NO, BRANCH TO BORDER
COPY     EQU   *
         LA    R3,UT9W
         LA    R4,PRTW
UT9R     EQU   3                   REGISTER CONTAINING DCB ADDRESS
PRTR     EQU   4                   REGISTER CONTAINING DCB ADDRESS
         MVC   0(UT9L,UT9R),UT9    COPY DCB TO KEEP CODE RE-ENTRANT
         MVC   0(PRTL,PRTR),PRT    COPY DCB TO KEEP CODE RE-ENTRANT
         MVI   OPENW,X'80'         ONLY ONE DCB IN OPEN/CLOSE LIST
         OPEN  ((UT9R),INPUT),MF=(E,OPENW)
         OPEN  ((PRTR),OUTPUT),MF=(E,OPENW)
COPYREAD EQU   *
         GET   (UT9R)
         LR    R0,R1               GET RECORD ADDRESS INTO R0
         PUT   (PRTR),(0)
         B     COPYREAD
COPYEOD  EQU   *
         CLOSE ((PRTR)),MF=(E,CLOSEW)
         CLOSE ((UT9R)),MF=(E,CLOSEW)
         B     EXIT
         SPACE
PRTEXIT  CLI   RECFM(R4),0             IS RECFM SPECIFIED
         BNE   *+10                    YES, SKIP NEXT INST
         MVC   RECFM(1,R4),RECFM(R3)   NO, USE INPUT RECFM
         NC    LRECL(2,R4),LRECL(R4)   IS LRECL SPECIFIED
         BNZ   *+10
         MVC   LRECL(2,R4),LRECL(R3)   NO, USE INPUT LRECL
         NC    BLKSI(2,R4),BLKSI(R4)   IS BLKSI SPECIFIED
         BNZ   *+10
         MVC   BLKSI(2,R4),BLKSI(R3)   NO, USE INPUT BLKSI
         BR    R14
         SPACE
************************************************************
*                                                          *
*        CALL THE BORDER PROGRAM                           *
*                                                          *
************************************************************
         SPACE
BORDER   LA    R1,PARM2PTR
         SPACE
         LINK  EPLOC=PGMNAME,SF=(E,LINKAREA)
         SPACE
EXIT     LR    R15,R2              RESTORE ASSEMBLER RETURN CODE
         LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         LR    R2,R15              SAVE RETURN CODE
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2              RESTORE RETURN CODE
         LM    0,12,20(R13)        RESTORE REGISTERS 0 - 12
         L     R14,12(,R13)        LOAD RETURN ADDRESS
         BR    14                  RETURN
         EJECT
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
BORDERDD DC    CL8'BORDER'
         PRINT NOGEN
UT9      DCB   DDNAME=SYSUT9,DSORG=PS,MACRF=(GL),EODAD=COPYEOD
UT9L     EQU   *-UT9
PRT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),EXLST=EXLST
PRTL     EQU   *-PRT
         PRINT GEN
EXLST    DC    0F'0',X'85',AL3(PRTEXIT)
RECFM    EQU   36                  OFFSET INTO DCB
BLKSI    EQU   62                  OFFSET INTO DCB
LRECL    EQU   82                  OFFSET INTO DCB
         DC    0D'0'               END OF CSECT
         SPACE
************************************************************
*                                                          *
*        MAP OF GETMAINED WORKAREA                         *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
LINKAREA DS    2F
ASMNAME  DS    CL8
PGMNAME  DS    CL8
PARM1PTR DS    2F
PARM1LEN DS    H
PARM1    DS    CL100               COPY OF PARM TEXT
         DS    0D
         DS    3H
DDNL1    DS    H
         DS    09CL8               DDNAMES
PARM2PTR DS    2F
PARM2LEN DS    H
PARM2    DS    CL6                 NOT USED
         DS    0D
         DS    3H
PARM2DDL DS    H
         DS    09CL8               DDNAMES
DEVDATA  DS    2F
OPENW    DS    0F                  OPEN MF=L
CLOSEW   DS    F                   CLOSE MF=L
UT9W     DS    0F,(UT9L)X          DCB
PRTW     DS    0F,(PRTL)X          DCB
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
