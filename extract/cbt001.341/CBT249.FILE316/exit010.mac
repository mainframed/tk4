EXIT010  TITLE 'JES2/SP3 EXIT #010'
*$DOC@*
* * * * * * * * * *  NOTES ON THE NEW JES2  * * ** * * * * * ** * * *
* 1.  THE JESPARMS THAT CORRESPOND TO THIS EXIT ARE:                  *
*       . . .                                                         *
*       >LOAD EXIT010                                                 *
*       >EXIT001 (EXIT010),ENABLE,TRACE=YES                           *
*       . . .                                                         *
*                                                                     *
* 2.  THE ORDER OF THE DSECTS IN THE PREAMBLE ASSEMBLY  CAN BE        *
*     SIGNIFICANT.  ALSO THE REGISTER CONTENTS ARE AS FOLLOWS UPON    *
*     ENTRY TO THIS EXIT:                                             *
*         R0 = N/A                                                    *
*         R1 = ADDRESS OF THE CMB.                                    *
*        R11 = ADDRESS OF THE HCT.                                    *
*                                                                     *
* 3.  THIS EXIT WILL SCAN EACH WTO MESSAGE PRODUCED BY JES. IT IS     *
*     KEYING ON ALL WTO FOR RECEIPT, I HAVE CHOSEN (GOD KNOWS WHY)    *
*     THE MESSAGE ID OF 066 AS A RECEIPT IF IBM CHOSES TO USE THIS    *
*     ID THEN THE ID WILL HAVE TO BE CHANGED.                         *
*                                                                     *
* 4.  THIS EXIT WILL SCAN FOR THE HASP317 DATSET RELEASED MESSAGE.    *
*     UPON RECEIVING THIS IT WILL DISCARD THE MESSAGE.                *
*                                                                     *
*                    *   *   *   *   *                                *
*                    PAUL FINDLEY                                     *
*                    AIR FORCE DATA SERVICES CENTER                   *
*                    IBM OPERATING SYSTEMS SUPPORT BRANCH             *
*                    PENTAGON, CMIS, ROOM 1C1039                      *
*                    WASHINGTON  D.C.      20330                      *
*                    CALL: 202/695-1274                               *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*$DOC$*
         TITLE 'JES2/SP3 EXIT #010 - MIT CSECT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                  P R E A M B L E  -  E X I T 1 0                  *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         PRINT OFF
         $SYSPARM (OFF,NOGEN,NODATA,NO,NO)   DOCUMENTATION OPTIONS
         COPY  $HASPGEN                 COPY HASPGEN PARAMETERS
         PRINT ON
         $SYSPARM (ON,GEN,DATA,YES,YES)    DOCUMENTATION OPTIONS
MITABLE  $MODULE                        CREATE THE MIT CSECT
         $BUFFER  DOC=YES               REQ'D BY $GETBUF, $FREEBUF,
*                                       AND MUST PRECEDE $JCT
         $JCT     DOC=YES               MUST PRECEDE $CAT
         $JQE     DOC=YES               MUST PRECEDE $HCT
         $PCE     DOC=YES               MUST PRECEDE $HCT
         $CAT     DOC=YES               MUST PRECEDE $HCT
         $DCT     DOC=YES               NORMALLY REQUIRED FOR EXIT #1
         $HCT     DOC=YES               REQ'D BY $SAVE, $RETURN...
         $PDDB    DOC=YES               MUST PRECEDE $IOT
         $TAB     DOC=YES               MUST PRECEDE $IOT
         $IOT     DOC=YES
         $JOE     DOC=YES               REQ'D TO GET SYSOUT CLASS
         $MIT     DOC=YES               REQ'D BY $MODEND
         $PPPWORK DOC=YES               REQ'D TO FIND JOE
         $CMB     DOC=YES               REQ'D TO FIND JOE
         PRINT GEN
         TITLE 'JES2/SP3 EXIT #10- WTO CSECT'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*         BEGIN CSECT TO SCAN EACH WTO MESSAGE FOR RECEIPTS         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
EXIT010  $ENTRY CSECT=YES,BASE=R12      EXIT ROUTINE ENTRY POINT
         $SAVE
         LR       R12,R15               LOAD BASE REGISTER
         XR       R15,R15               CLEAR R15 IN CASE PUNCH
         LR       R9,R1                 SAVE INPUT PARAMETERS
         USING    CMBDSECT,R9           SETUP ADDRESSABILITY
BODY     EQU   *
         CLC      CMBMID+1(7),MESDEL     CHECK FOR DISCARD ID
         BNE      LABEL1                 IF NOT FOUND CHECK FOR OTHERS
         LA       R15,8                  TELL JES TO BYPASS HASPWQUE
         B        RETURN
LABEL1   CLC      CMBMID+1(7),MESSID     CHECK FOR RECEIPT ID
         BNE      FINI                   IF NOT FOUND GET OUT
         MVC      CMBMID+1(7),BLANKS     ELSE BLANK OUT HASP ID
         MVC      CMBROUT,=X'0004'       SET ROUTE CODE TO 14
FINI     LA       R15,4                  SET RETURN CODE
RETURN   $RETURN RC=(R15)                RETURN TO HASPPRPU
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                  C O N S T A N T S                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MESSID   DC     C'HASP066'               MESSAGE ID FOR RECEIPT
MESDEL   DC     C'HASP317'               MESSAGE ID TO DISCARD
BLANKS   DC     C'       '
         LTORG
         $MODEND
         END
