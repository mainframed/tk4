         TITLE  'SMF IEFU83 EXIT PGM---PROCEDURE CODE'
*-$DOC@-------------------------------------------------------------*
*                 I   E   F   U   8   3                             *
*                                              REL 2.0 (13 DEC 81)  *
*                                                                   *
*                                                                   *
*        MAJOR  MODIFICATION OF THE IEFU83 EXIT I GOT FROM          *
*        BLUE CROSS - BLUE SHIELD, RICHMOND, VA. IT HAD THE         *
*        BRANCH TABLE CONCEPT IN IT, BUT IT USED THE BRANCH         *
*        TABLE TO DECIDE ONLY IF IT WAS TO WRITE A RECORD OR        *
*        NOT. I USED THE TABLE TO BRANCH TO THE APPROPRIATE         *
*        CODE TO FURTHER PROCESS A TYPE OF RECORD.                  *
*                                                                   *
*                             JIM MARSHALL, LT, USAF                *
*                             AIR FORCE DATA SERVICE CENTER         *
*                             IBM OPERATING SYSTEMS SUPPORT BR      *
*                             CMIS - ROOM 1C1037                    *
*                             WASHINGTON D.C.   20330               *
*                             1-202-695-1274/2478                   *
*                                                                   *
*  INSTALLED: 6 JUNE 1981                                           *
*                                                                   *
*  CHANGE LOG :                                                     *
*                                                                   *
*      25 NOV 81 -  JIM MARSHALL, AFDSC/CMIS                        *
*         CORRECTED SOME BUGS AS TO THROWING AWAY TYPE 40 RECORDS   *
*         BUT ALSO VIO TOO. DID NOT PLAN ON IT. ALSO I WAS SAVING   *
*         TYPE 40 RECORDS FOR TERM, DUMMY, ETC. DID NOT PLAN ON     *
*         THAT EITHER.                                              *
*                                                                   *
*      13 DEC 81 - JIM MARSHALL, AFDSC/CMIS                         *
*         TYPE 30 RECORD PROCESSING WAS LIMITED TO LENGTH 6412.     *
*         WE ARE NOT REALLY GOING TO USE THEM YET AND DO NOT        *
*         WANT THE LONG RECORDS THEY CAN PRODUCE. WILL REMOVE LATER *
*                                                                   *
*-$DOC$-------------------------------------------------------------*
         SPACE 2
IEFU83   CSECT
R0       EQU   0                  NOT USED
R1       EQU   1                  ENTRY VALUE IS SAVED
R2       EQU   2                  WORK REGISTER
R3       EQU   3                  SAVED REGISTER 1 AT ENTRY
R4       EQU   4                  WORK REGISTER
R5       EQU   5                  WORK REGISTER
R6       EQU   6                  WORK REGISTER
R7       EQU   7                  WORK REGISTER
R8       EQU   8                  ADDRESSABILITY TO SMF REC DSECT
R9       EQU   9                  ADDRESSABILITY TO WORKAREA DSECT
R10      EQU   10                 KEEP SPARE, POSSIBLE TRIPLE BASE
R11      EQU   11           ×---- DOUBLE BASE
R12      EQU   12   --------×----    REGISTER
R13      EQU   13                 POINTER TO SAVE AREA ( MINE )
R14      EQU   14                 DO NOT USE !!!!!!
R15      EQU   15                 UPON ENTRY THE BASE ADDRESS, AFTER
*                                    THAT A WORK REGISTER
         USING *,R15
         B     PROLOG
         DC    AL1(L'ID)           PROLOG LENGTH
ID       DC    C'IEFU83   &SYSDATE'
         DS    0H
PROLOG   EQU   *
         STM   14,12,12(13)
         LR    R12,R15             SETUP BASE REGISTER
         LA    R11,4095(R12)
         LA    R11,1(R11)          ENTRY POINT ADDR + X'1000'
         DROP  R15                 DROP OLD BASE REGISTER
         USING IEFU83,R12,R11      GET NEW BASE REGISTERS
         LR    R3,R1               SAVE REGISTER 1
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)          GO GET IT
         LR    R9,R1               SAVE THE ADDRESS
         USING WORKAREA,R9         ESTABLISH ADDRESSABILITY
         ST    R13,SAVE+4          SETUP
         ST    R9,8(R13)            SAVEAREA
         LR    R13,R9                   CHAIN
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        SMF EXIT. USES SMF RECORD TYPE AS INDEX INTO                *
*         A BYTE TABLE. THE BYTE IS THEN MULTIPLIED BY 4             *
*         AND USED AS A BRANCH INDEX TO BRANCH TO A BRANCH.          *
*                                                                    *
*        INPUT  R1 POINTS TO THE ADDRESS OF THE SMF RECORD           *
*               ( SAVED IN R3 )                                      *
*        OUTPUT R15 CONTAINS                                         *
*            0 - WRITE THE RECORD                                    *
*            4 - DELETE THE RECORD                                   *
*        EXIT - RETURN VIA R14                                       *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
         L     R8,0(R3)            GET ADDRESS OF RECORD
         USING SMFREC,R8           GET ADDR TO RECORD
         SPACE
         L     R7,TABADD           GET OFFSET TO TABLE DSECT
         LA    R7,0(R7,R12)        ADDRESS OF TABLE
         SR    R15,R15             CLEAR WORK REGISTER
         SR    R4,R4               CLEAR WORK REGISTER
         IC    R4,5(R8)            GET REC TYPE AT  SMFREC+4
         IC    R15,0(R4,R7)        USE AS INDEX INTO TABLE
         SLL   R15,2               MULTIPLY BY 4
         B     BRTAB(R15)          BRANCH TO BRANCH
         B     RETURN0             DUMMY FOR SAFETY
         SPACE
*--------BRANCH TABLE AS FOLLOWS ------------------------------------*
         SPACE
BRTAB    DS    0H                  BRANCH TO CORRECT ROUTINE
         B     RETURN0       0     WRITE
         B     RETURN4       1     NOWRITE
         B     RETURN0       2     ( FUTURE USE - TYPE 6 RECEIPT )
         B     TYP1415R      3     LOOK AT 14 & 15 'S
         B     TYP40R        4     LOOK AT 40 'S
         B     RETURN0       5     ( FUTURE USE - TYPE 21 ERRORS )
         B     TYP30R        6     TEMP CODE FOR TYPE 30 RECS
         SPACE
*--------RETURN WITH RET CODE = 0   ( WRITE THE RECORD )  -----------*
         SPACE
RETURN0  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,0               GET RETURN CODE
         ST    R15,16(R4)          PUT IN RETURN CODE
         B     RETURN              GO RETURN
         SPACE
*------- RETURN WITH RET CODE = 4   ( DO NOT WRITE RECORD )  --------*
         SPACE
RETURN4  EQU   *
         L     R4,4(R13)           GET HIGH SAVE AREA
         LA    R15,4               PUT IN RETURN CODE = 4
         ST    R15,16(R4)          PUT IN RETURN CODE
         SPACE
*------- RETURN TO CALLER FOR GOOD, DO FREEMAIN AND GO AWAY !!!  ----*
         SPACE
RETURN   EQU   *
         LR    R1,R13              GET ADDRESS FOR FREEMAIN
         L     R0,WORKSIZE         GET SUBPOOL AND LENGTH
         L     R13,4(R13)          GET HIGH SAVE ADDRESS
         FREEMAIN R,LV=(0),A=(1)   FREE SAVE AREA
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         BR    R14                 RETURN FOR GOOD
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        PROCESS THE TYPE 14 & 15 RECORDS. WE WANT TO ELIMINATE      *
*        14 & 15 RECORDS FOR TEMPORARY FILES EXCEPT:                 *
*                                                                    *
*        1.  TAPE  ( OUR TAPE MGMT SYSTEM NEEDS FOR POSSIBLE         *
*                    OVERWRITES).                                    *
*        2.  MSS 3850 RECORDS ( SHOULD NOT BE ANY ).                 *
*        3.  VIO RECORDS ( WANT TO KNOW IF CUSTOMERS ARE DOING IT ). *
*                                                                    *
*--------------------------------------------------------------------*
         SPACE
TYP1415R EQU   *
         CLI   SMF14NUC,X'01'      IS IT FOR MORE THAN 1 DD CARD    JDM
         BNE   RETURN0             IF NOT CONTINUE CHECKING
         CLI   SMFUCBTY+2,X'80'    IS IT TAPE ????
         BE    RETURN0             IF SO WRITE IT.                  JDM
         TM    SMF14RIN,SMF14VIO   IS IT VIO ????                   JDM
         BO    RETURN0             IF YES, WRITE IT.                JDM
         TM    SMFUCBCH,X'80'      IS IT MSS ???
         BO    RETURN0             IF SO, WRITE IT
         TM    SMF14RIN,SMF14TDS   IS IT TEMPORARY, RULED OUT TAPE, JDM
*                                     MSS, AND VIO, SO MUST BE DASD JDM
         BO    RETURN4             IF TEMP, BIT BUCKET IT
         B     RETURN0             WRITE THE REST
         SPACE
*--------------------------------------------------------------------*
*        WE ARE CONCERNED ABOUT SMF RECORD TYPE 30 BEING LONGER      *
*        THAN 6412 BYTES. THIS IS ONLY TEMPORARILY UNTIL WE GET      *
*        OUT ACCOUNTING PROGRAMS CHANGED TO ACCOMDATE THE NEW        *
*        COMBINED TYPE 30 RECORD.                                    *
*--------------------------------------------------------------------*
         SPACE
TYP30R   EQU   *
         CLC   SMFREC(2),=H'6412'  IS IT GREATER THAN 6412 ??
         BH    RETURN4             IF SO DO NOT WRITE IT
         B     RETURN0             IF LESS THEN WRITE IT
         SPACE
*--------------------------------------------------------------------*
*        PROCESS THE TYPE 40 RECORDS. WE WANT TO ELIMINATE           *
*           1.  DD DUMMY RECORDS IF THEY ARE NOT IN A CONCATENATION  *
*           2.  TERM=TS                                              *
*           3.  ZERO EXCP COUNTS IN THE RECORD                       *
*--------------------------------------------------------------------*
         SPACE
TYP40R   EQU   *
         CLC   TDDVAR(2),=X'000A'     IS THERE ONLY ONE DD CARD ?  JDM
         BNE   RETURN0                IF CONCAT, THEN WRITE IT
         CLC   TDDCUAD(2),=X'0FFF'    IS IT VIO ?                  JDM
         BE    RETURN0                IF SO, WRITE IT
         CLI   TDDDEVC,X'00'          IS IT DUMMY,TERM, OR         JDM
*                                        UNALLOCATED DYNAM         JDM
         BE    RETURN4                IF SO, DO NOT WRITE IT
         CLC   TDDNEXCP(4),=XL4'0'    IS EXCP COUNT = 0 ??
         BE    RETURN4                IF SO, DO NOT WRITE IT
         B     RETURN0                GO WRITE IT
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS                                                   *
*--------------------------------------------------------------------*
         DS    0F
WORKSIZE DC    AL1(245),AL3(WORKEND-WORKAREA)
TABADD   DC    A(SMFTAB-IEFU83)
         LTORG
         SPACE
*--------------------------------------------------------------------*
*                                                                    *
*        THIS TABLE CONTAINS A BRANCH INDEX VALUE FOR EACH           *
*        SMF RECORD TYPE. THE VALUE IS THEN MULTIPLIED BY 4          *
*        TO GET TO THE PROCESSING PROCESSING CODES (BRANCH           *
*        TABLE.  PRESENTLY WE ARE PROCESSING :                       *
*                                                                    *
*         0   -  RETURN WITH R15=0 AND WRITE THE RECORD              *
*         1   -  RETURN WITH R15=4 AND DO NOT WRITE THE RECORD       *
*         2   -  SPARE ( FUTURE TYPE 6 RECEIPT SYSTEM )              *
*         3   -  PROCESS TYPE 14 & 15 RECORDS                        *
*         4   -  PROCESS TYPE 40 RECORDS                             *
*         5   -  SPARE                                               *
*         6   -  PROCESS TYPE 30 RECORDS                             *
*--------------------------------------------------------------------*
         SPACE
SMFTAB   CSECT
SMFTYPES DS    0XL256
TYPE0    DC    HL1'0'        WRITE
TYPE1    DC    HL1'0'        WRITE
TYPE2    DC    HL1'0'        WRITE
TYPE3    DC    HL1'0'        WRITE
TYPE4    DC    HL1'0'        WRITE
TYPE5    DC    HL1'0'        WRITE
TYPE6    DC    HL1'0'        WRITE
TYPE7    DC    HL1'0'        WRITE
TYPE8    DC    HL1'0'        WRITE
TYPE9    DC    HL1'0'        WRITE
TYPE10   DC    HL1'0'        WRITE
TYPE11   DC    HL1'0'        WRITE
TYPE12   DC    HL1'0'        WRITE
TYPE13   DC    HL1'0'        WRITE
TYPE14   DC    HL1'3'        ***** - SPECIAL PROCESSING FOR TYPE 14
TYPE15   DC    HL1'3'        ***** - SPECIAL PROCESSING FOR TYPE 15
TYPE16   DC    HL1'0'        WRITE
TYPE17   DC    HL1'0'        WRITE
TYPE18   DC    HL1'0'        WRITE
TYPE19   DC    HL1'0'        WRITE
TYPE20   DC    HL1'0'        WRITE
TYPE21   DC    HL1'0'        WRITE
TYPE22   DC    HL1'0'        WRITE
TYPE23   DC    HL1'0'        WRITE
TYPE24   DC    HL1'0'        WRITE
TYPE25   DC    HL1'0'        WRITE
TYPE26   DC    HL1'0'        WRITE
TYPE27   DC    HL1'0'        WRITE
TYPE28   DC    HL1'0'        WRITE
TYPE29   DC    HL1'0'        WRITE
TYPE30   DC    HL1'6'        ***** - SPECIAL PROCESSING FOR TYPE 30
TYPE31   DC    HL1'0'        WRITE
TYPE32   DC    HL1'0'        WRITE
TYPE33   DC    HL1'0'        WRITE
TYPE34   DC    HL1'0'        WRITE
TYPE35   DC    HL1'0'        WRITE
TYPE36   DC    HL1'0'        WRITE
TYPE37   DC    HL1'0'        WRITE
TYPE38   DC    HL1'0'        WRITE
TYPE39   DC    HL1'0'        WRITE
TYPE40   DC    HL1'4'        ***** - SPECIAL PROCESSING FOR TYPE 40
TYPE41   DC    HL1'0'        WRITE
TYPE42   DC    HL1'0'        WRITE
TYPE43   DC    HL1'0'        WRITE
TYPE44   DC    HL1'0'        WRITE
TYPE45   DC    HL1'0'        WRITE
TYPE46   DC    HL1'0'        WRITE
TYPE47   DC    HL1'0'        WRITE
TYPE48   DC    HL1'0'        WRITE
TYPE49   DC    HL1'0'        WRITE
TYPE50   DC    HL1'0'        WRITE
TYPE51   DC    HL1'0'        WRITE
TYPE52   DC    HL1'0'        WRITE
TYPE53   DC    HL1'0'        WRITE
TYPE54   DC    HL1'0'        WRITE
TYPE55   DC    HL1'0'        WRITE
TYPE56   DC    HL1'0'        WRITE
TYPE57   DC    HL1'0'        WRITE
TYPE58   DC    HL1'0'        WRITE
TYPE59   DC    HL1'0'        WRITE
TYPE60   DC    HL1'0'        WRITE
TYPE61   DC    HL1'0'        WRITE
TYPE62   DC    HL1'0'        WRITE
TYPE63   DC    HL1'0'        WRITE
TYPE64   DC    HL1'0'        WRITE
TYPE65   DC    HL1'0'        WRITE
TYPE66   DC    HL1'0'        WRITE
TYPE67   DC    HL1'0'        WRITE
TYPE68   DC    HL1'0'        WRITE
TYPE69   DC    HL1'0'        WRITE
TYPE70   DC    HL1'0'        WRITE
TYPE71   DC    HL1'0'        WRITE
TYPE72   DC    HL1'0'        WRITE
TYPE73   DC    HL1'0'        WRITE
TYPE74   DC    HL1'0'        WRITE
TYPE75   DC    HL1'0'        WRITE
TYPE76   DC    HL1'0'        WRITE
TYPE77   DC    HL1'0'        WRITE
TYPE78   DC    HL1'0'        WRITE
TYPE79   DC    HL1'0'        WRITE
TYPE80   DC    HL1'0'        WRITE
TYPE81   DC    HL1'0'        WRITE
TYPE82   DC    HL1'0'        WRITE
TYPE83   DC    HL1'0'        WRITE
TYPE84   DC    HL1'0'        WRITE
TYPE85   DC    HL1'0'        WRITE
TYPE86   DC    HL1'0'        WRITE
TYPE87   DC    HL1'0'        WRITE
TYPE88   DC    HL1'0'        WRITE
TYPE89   DC    HL1'0'        WRITE
TYPE90   DC    HL1'0'        WRITE
TYPE91   DC    HL1'0'        WRITE
TYPE92   DC    HL1'0'        WRITE
TYPE93   DC    HL1'0'        WRITE
TYPE94   DC    HL1'0'        WRITE
TYPE95   DC    HL1'0'        WRITE
TYPE96   DC    HL1'0'        WRITE
TYPE97   DC    HL1'0'        WRITE
TYPE98   DC    HL1'0'        WRITE
TYPE99   DC    HL1'0'        WRITE
TYPE100  DC    HL1'0'        WRITE
TYPE101  DC    HL1'0'        WRITE
TYPE102  DC    HL1'0'        WRITE
TYPE103  DC    HL1'0'        WRITE
TYPE104  DC    HL1'0'        WRITE
TYPE105  DC    HL1'0'        WRITE
TYPE106  DC    HL1'0'        WRITE
TYPE107  DC    HL1'0'        WRITE
TYPE108  DC    HL1'0'        WRITE
TYPE109  DC    HL1'0'        WRITE
TYPE110  DC    HL1'0'        WRITE
TYPE111  DC    HL1'0'        WRITE
TYPE112  DC    HL1'0'        WRITE
TYPE113  DC    HL1'0'        WRITE
TYPE114  DC    HL1'0'        WRITE
TYPE115  DC    HL1'0'        WRITE
TYPE116  DC    HL1'0'        WRITE
TYPE117  DC    HL1'0'        WRITE
TYPE118  DC    HL1'0'        WRITE
TYPE119  DC    HL1'0'        WRITE
TYPE120  DC    HL1'0'        WRITE
TYPE121  DC    HL1'0'        WRITE
TYPE122  DC    HL1'0'        WRITE
TYPE123  DC    HL1'0'        WRITE
TYPE124  DC    HL1'0'        WRITE
TYPE125  DC    HL1'0'        WRITE
TYPE126  DC    HL1'0'        WRITE
TYPE127  DC    HL1'0'        WRITE
TYPE128  DC    HL1'0'        WRITE
TYPE129  DC    HL1'0'        WRITE
TYPE130  DC    HL1'0'        WRITE
TYPE131  DC    HL1'0'        WRITE
TYPE132  DC    HL1'0'        WRITE
TYPE133  DC    HL1'0'        WRITE
TYPE134  DC    HL1'0'        WRITE
TYPE135  DC    HL1'0'        WRITE
TYPE136  DC    HL1'0'        WRITE
TYPE137  DC    HL1'0'        WRITE
TYPE138  DC    HL1'0'        WRITE
TYPE139  DC    HL1'0'        WRITE
TYPE140  DC    HL1'0'        WRITE
TYPE141  DC    HL1'0'        WRITE
TYPE142  DC    HL1'0'        WRITE
TYPE143  DC    HL1'0'        WRITE
TYPE144  DC    HL1'0'        WRITE
TYPE145  DC    HL1'0'        WRITE
TYPE146  DC    HL1'0'        WRITE
TYPE147  DC    HL1'0'        WRITE
TYPE148  DC    HL1'0'        WRITE
TYPE149  DC    HL1'0'        WRITE
TYPE150  DC    HL1'0'        WRITE
TYPE151  DC    HL1'0'        WRITE
TYPE152  DC    HL1'0'        WRITE
TYPE153  DC    HL1'0'        WRITE
TYPE154  DC    HL1'0'        WRITE
TYPE155  DC    HL1'0'        WRITE
TYPE156  DC    HL1'0'        WRITE
TYPE157  DC    HL1'0'        WRITE
TYPE158  DC    HL1'0'        WRITE
TYPE159  DC    HL1'0'        WRITE
TYPE160  DC    HL1'0'        WRITE
TYPE161  DC    HL1'0'        WRITE
TYPE162  DC    HL1'0'        WRITE
TYPE163  DC    HL1'0'        WRITE
TYPE164  DC    HL1'0'        WRITE
TYPE165  DC    HL1'0'        WRITE
TYPE166  DC    HL1'0'        WRITE
TYPE167  DC    HL1'0'        WRITE
TYPE168  DC    HL1'0'        WRITE
TYPE169  DC    HL1'0'        WRITE
TYPE170  DC    HL1'0'        WRITE
TYPE171  DC    HL1'0'        WRITE
TYPE172  DC    HL1'0'        WRITE
TYPE173  DC    HL1'0'        WRITE
TYPE174  DC    HL1'0'        WRITE
TYPE175  DC    HL1'0'        WRITE
TYPE176  DC    HL1'0'        WRITE
TYPE177  DC    HL1'0'        WRITE
TYPE178  DC    HL1'0'        WRITE
TYPE179  DC    HL1'0'        WRITE
TYPE180  DC    HL1'0'        WRITE
TYPE181  DC    HL1'0'        WRITE
TYPE182  DC    HL1'0'        WRITE
TYPE183  DC    HL1'0'        WRITE
TYPE184  DC    HL1'0'        WRITE
TYPE185  DC    HL1'0'        WRITE
TYPE186  DC    HL1'0'        WRITE
TYPE187  DC    HL1'0'        WRITE
TYPE188  DC    HL1'0'        WRITE
TYPE189  DC    HL1'0'        WRITE
TYPE190  DC    HL1'0'        WRITE
TYPE191  DC    HL1'0'        WRITE
TYPE192  DC    HL1'0'        WRITE
TYPE193  DC    HL1'0'        WRITE
TYPE194  DC    HL1'0'        WRITE
TYPE195  DC    HL1'0'        WRITE
TYPE196  DC    HL1'0'        WRITE
TYPE197  DC    HL1'0'        WRITE
TYPE198  DC    HL1'0'        WRITE
TYPE199  DC    HL1'0'        WRITE
TYPE200  DC    HL1'0'        WRITE
TYPE201  DC    HL1'0'        WRITE
TYPE202  DC    HL1'0'        WRITE
TYPE203  DC    HL1'0'        WRITE
TYPE204  DC    HL1'0'        WRITE
TYPE205  DC    HL1'0'        WRITE
TYPE206  DC    HL1'0'        WRITE
TYPE207  DC    HL1'0'        WRITE
TYPE208  DC    HL1'0'        WRITE
TYPE209  DC    HL1'0'        WRITE
TYPE210  DC    HL1'0'        WRITE
TYPE211  DC    HL1'0'        WRITE
TYPE212  DC    HL1'0'        WRITE
TYPE213  DC    HL1'0'        WRITE
TYPE214  DC    HL1'0'        WRITE
TYPE215  DC    HL1'0'        WRITE
TYPE216  DC    HL1'0'        WRITE
TYPE217  DC    HL1'0'        WRITE
TYPE218  DC    HL1'0'        WRITE
TYPE219  DC    HL1'0'        WRITE
TYPE220  DC    HL1'0'        WRITE
TYPE221  DC    HL1'0'        WRITE
TYPE222  DC    HL1'0'        WRITE
TYPE223  DC    HL1'0'        WRITE
TYPE224  DC    HL1'0'        WRITE
TYPE225  DC    HL1'0'        WRITE
TYPE226  DC    HL1'0'        WRITE
TYPE227  DC    HL1'0'        WRITE
TYPE228  DC    HL1'0'        WRITE
TYPE229  DC    HL1'0'        WRITE
TYPE230  DC    HL1'0'        WRITE
TYPE231  DC    HL1'0'        WRITE
TYPE232  DC    HL1'0'        WRITE
TYPE233  DC    HL1'0'        WRITE
TYPE234  DC    HL1'0'        WRITE
TYPE235  DC    HL1'0'        WRITE
TYPE236  DC    HL1'0'        WRITE
TYPE237  DC    HL1'0'        WRITE
TYPE238  DC    HL1'0'        WRITE
TYPE239  DC    HL1'0'        WRITE
TYPE240  DC    HL1'0'        WRITE
TYPE241  DC    HL1'0'        WRITE
TYPE242  DC    HL1'0'        WRITE
TYPE243  DC    HL1'0'        WRITE
TYPE244  DC    HL1'0'        WRITE
TYPE245  DC    HL1'0'        WRITE
TYPE246  DC    HL1'0'        WRITE
TYPE247  DC    HL1'0'        WRITE
TYPE248  DC    HL1'0'        WRITE
TYPE249  DC    HL1'0'        WRITE
TYPE250  DC    HL1'0'        WRITE
TYPE251  DC    HL1'0'        WRITE
TYPE252  DC    HL1'0'        WRITE
TYPE253  DC    HL1'0'        WRITE
TYPE254  DC    HL1'0'        WRITE
TYPE255  DC    HL1'0'        WRITE
         SPACE 2
         DS    0F
WORKAREA DSECT
SAVE     DS    18F
WORKEND  EQU   *
         SPACE
         DS    0F
SMFREC   DSECT
         IFASMFR 14
         ORG   SMFJFCB1
         IEFJFCBN LIST=YES
         ORG   SMFREC
         IFASMFR 40
         END
