         TITLE 'A F D S C  -   IEFUTL TIME LIMIT EXIT '
*-$DOC@---------------------------------------------------------------*
*                                                                     *
*                      I  E  F  U  T  L                               *
*                                                                     *
*                                                                     *
* COURTESY =  JIM MARSHALL                                            *
*             AIR FORCE DATA SERVICES CENTER                          *
*             IBM OPERATING SYSTEMS SUPPORT BRANCH                    *
*             CMIS - ROOM 1C1039 - PENTAGON                           *
*             WASHINGTON D.C.  20330                                  *
*             (202) 695-1274/2478                                     *
*                                                                     *
*                                                                     *
*             DESCRIPTIVE NAME =  AFDSC MODIFICATION OF THE IBM       *
*                                 MVS/SP SYSTEM IPO SUPPLIED          *
*                                 'IEFUTL' CPU TIME EXCESSION OR      *
*                                 WAIT TIME EXCESSION EXIT.           *
*                                                                     *
*             COPYRIGHT = NONE   (IPO BASED LOGIC THOUGH PLUS .. )    *
*                                                                     *
*             FUNCTION =                                              *
*              1 BATCH JOBS ARE CANCELLED FOR CPU TIME EXCESSION      *
*              2 TSO USERS ARE CANCELLED FOR CPU TIME EXCESSION       *
*                WITH APPROPRIATE MSGS SENT TO THEIR TERMINALS AND    *
*                PLACED IN THERE MAILBOX FOR NEXT LOGON.              *
*              3 BATCH JOBS IN WAITSTATE CAN BE OPTIONALLY CANCELLED  *
*                BY THE OPERATOR OR EXTENDED IN THE WAIT STATE.       *
*              4 TSO USERS IN WAITSTATE BEYOND LIMIT ARE CANCELLED    *
*                WITH APPROPRIATE MSGS SENT TO THEIR TERMINALS AND    *
*                PLACED IN THEIR MAILBOXES FOR NEXT LOGON. THIS       *
*                IS IN CASE THEY HAVE LEFT THE TERMINAL UNATTENDED    *
*                AND FORGET TO RETURN. ( THEY, OF COURSE, NEVER       *
*                DO THIS BECAUSE IT IS A SECURITY VIOLATION. )        *
*                                                                     *
*                DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE  *
*                   IF A DIFFERENT CHARACTER SET IS NEEDED.           *
*                                                                     *
*                RESTRICTIONS = NONE                                  *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                  'ZAP*'S)   MAY YOU NEVER NEED IT !!!!!             *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = NOT TOO BIG ( ASSEMBLE TO SEE )        *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS = IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM INITIATOR VIA MODULE IEATLEXT                *
*                                                                     *
*             INPUT = REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                THE FIRST OF WHICH POINTS TO THE SMF COMMON          *
*                EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT          *
*                                                                     *
*             OUTPUT = REGISTER 15 - RETURN CODE                      *
*                REGISTER 1 - TIME LIMIT EXTENSION                    *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = SEE FUNCTION ABOVE                     *
*                                                                     *
*             EXIT - ERROR = NONE                                     *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = 0  TO CANCEL THE JOB                   *
*                              8  TO EXTEND THE JOB ( TIME IS IN      *
*                                    R1 IN SECONDS )                  *
*                                                                     *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THE MESSAGES ISSUED BY IEFUTL ARE FOUND IN THE       *
*                   DATA CONSTANT DEFINITION AREA.                    *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
*  NOTES: SINCE IN OUR  SHOP WE HAVE THE TSO USERIDS AS 7 CHAR-       *
*         ACTERS LONG FOR THE CUSTOMERS AND 6 CHAR FOR THE GOOD       *
*         GUYS ( SYSTEMS TYPES ), YOU WILL SEE SOME ADDITIONAL        *
*         WHICH YOU MAY WANT TO REMOVE IF ALL YOU USERIDS ARE THE     *
*         SAME LENGTHS.                                               *
*                                                                     *
*         IF YOU ARE EXPECTING SLICK CODE YOU WILL BE SUPRISED.       *
*         THIS IS NOT THE MOST EFFICIENT CODE GOING BUT HOPEFULLY     *
*         STRAIGHT FORWARD. REDUNDANT AT TIMES TOO. BUT ABLE TO       *
*         BE FOLLOWED BY MORTAL MAN OR WOMAN.   J.MARSHALL            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0                       ENTRY CONDITION, THEN SPEC USE
R1       EQU   1                       ENTRY PARM PTER, THEN SPEC USE
R2       EQU   2                       TIMER EXT IF RC=8
R3       EQU   3                       UNUSED
R4       EQU   4                       WORK REGISTER
R5       EQU   5                       LINKAGE REGISTER (BAL)
R6       EQU   6                       POINTER TO WTOAREA
R7       EQU   7                       UNUSED
R8       EQU   8                       ENTRY CONDITION SAVED
R9       EQU   9                       ENTRY PARM POINTER SAVED, THEN
*                                      UNUSED
R10      EQU   10                      ADDR TO SMF PARM AREA DSECT
R11      EQU   11                      SPARE (POSSIBLE 2ND BASE)
R12      EQU   12                      BASE REGISTER
R13      EQU   13                      ADDR TO GETMAINED AREA
R14      EQU   14                      RETURN ADDR
R15      EQU   15                      RETURN CODE
         EJECT
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL_AFDSC_&SYSTIME_&SYSDATE SAVE REG
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         LR    R9,R1                   SAVE PARM POINTER
         LR    R8,R0                   SAVE ENTRY CONDITION
         LA    R0,ENDDSA-DSA           LENGTH FOR GOTTEN AREA
         O     R0,SUBPOOL              INDICATE SUBPOOL 241
         GETMAIN R,LV=(0)              GO DO IT
         USING DSA,R1                  TEMPORARY ADDRESSABILITY
         XC    SAVE,SAVE               CLEAR SAVE AREA
         ST    R13,SAVE+4              PLUG IN LOW SAVE
         ST    R1,8(R13)               PLUG IN HIGH SAVE
         LR    R13,R1                  POINT TO GETMAIN AREA
         DROP  R1                      RID OURSELVES OF R1
         USING DSA,R13                 RE-ESTABLISH R13 AS BASE
         SPACE
*---------------------------------------------------------------------*
*        ESTABLISH ADDRESSABILITY TO SMF PARAMETER AREA DSECT         *
*        EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED              *
*                            = 4 - STEP CPU TIME EXCEEDED             *
*                            = 8 - JOB WAIT TIME EXCEEDED             *
*---------------------------------------------------------------------*
         SPACE
         L     R10,D0(R9)              LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R10             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         C     R8,FULL4                SEE IF JOB/STEP TIME EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
*                                      TIME EXCEEDED
         SPACE
*---------------------------------------------------------------------*
*        CPU TIME EXCEEDED. BATCH JOBS WILL GET A CANCEL ONLY.        *
*        TSO USERS WILL GET A MSG BACK AT THERE TERMINAL AND          *
*        ALSO ONE MSG IN THERE BRODCAST SLOT FOR THE NEXT LOGON       *
*        THIS IS FOR THE PEOPLE WITH 3270 TYPE TERMINAL AND USE       *
*        VTAM. HARDCPY TERMINALS ARE NO PROBLEM.                      *
*---------------------------------------------------------------------*
         SPACE
BCPU00   EQU   *
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   CANCEL                  NO.....GO CANCEL BATCH CPU
*                                      EXCESSIONS
         LA    R6,MSGWTO               SETUP R6 FOR OUR WORK
         SPACE
*---------------------------------------------------------------------*
*        ISSUE 1ST MSG TO TSO PERSON TO SEE NOW.                      *
*        SE 'XXXXXXX CANCELLED: MAX CPUTIME EXCEEDED',                *
*               USER=(XXXXXXX),LOGON                                  *
*---------------------------------------------------------------------*
         SPACE
         CLC   SMFJOBN(4),ASMI         IS IT SYSTEM TYPE PERSON ??
         BE    BCPU05                  DO SPECIAL THINGS
         MVC   0(MSG4L,R6),MSG4        MOVE IN MSG
         MVC   MSG4J(7,R6),SMFJOBN     MOVE IN USERID TO MSG
         MVC   MSG4JJ(7,R6),SMFJOBN    MOVE IN USERID TO MSG
         BAL   R5,SVC34                ISSUE THIS COMMAND
         B     BCPU10
         SPACE
BCPU05   EQU   *
         MVC   0(MSG4CL,R6),MSG4C      MOVE IN MSG
         MVC   MSG4CJ(6,R6),SMFJOBN    MOVE IN USERID TO MSG
         MVC   MSG4CJJ(6,R6),SMFJOBN   MOVE IN USERID TO MSG
         BAL   R5,SVC34                ISSUE THIS COMMAND
         SPACE
*---------------------------------------------------------------------*
*        ISSUE 2ND MSG TO TSO PERSON TO SEE LATER.                    *
*        SE 'XXXXXXX CANCELLED: MAX CPUTIME EXCEEDED',                *
*               USER=(XXXXXX),SAVE                                    *
*---------------------------------------------------------------------*
         SPACE
BCPU10   EQU   *
         CLC   SMFJOBN(4),ASMI         IS IT SYSTEM TYPE PERSON ??
         BE    BCPU15                  DO SPECIAL THINGS
         MVC   0(MSG5L,R6),MSG5        MOVE IN MSG
         MVC   MSG5J(7,R6),SMFJOBN     MOVE IN USERID TO MSG
         MVC   MSG5JJ(7,R6),SMFJOBN    MOVE IN USERID TO MSG
         BAL   R5,SVC34                ISSUE THIS COMMAND
         B     CANCEL
         SPACE
BCPU15   EQU   *
         MVC   0(MSG5CL,R6),MSG5C      MOVE IN MSG
         MVC   MSG5CJ(6,R6),SMFJOBN    MOVE IN USERID TO MSG
         MVC   MSG5CJJ(6,R6),SMFJOBN   MOVE IN USERID TO MSG
         BAL   R5,SVC34                ISSUE THIS COMMAND
         B     CANCEL                  GO CANCEL NOW
         SPACE
         EJECT
*---------------------------------------------------------------------*
*        WAIT TIME EXCEEDED                                           *
*           - ALLOW TSO SESSION TO BE CANCELLED                       *
*           - ASK THE OPERATOR IF THE JOB IS TO BE EXTENDED ( WE      *
*             FORCE 15 MINUTES MAX ) OR SHOULD IT BE CANCELLED.       *
*---------------------------------------------------------------------*
         SPACE
WAITTIME DS    0H
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   TWAIT50                 NO..SO MUST BE BACKGROUND
         SPACE
*---------------------------------------------------------------------*
*        THIS IS A TSO USER, SO WE MUST KILL HIM.                     *
*---------------------------------------------------------------------*
         SPACE
         LA    R6,MSGWTO               SETUP R6 FOR OUR MSGS
         SPACE
*---------------------------------------------------------------------*
*        FIRST WE MUST TELL THE OPERATOR OF OUR INTENT.   I.E.        *
*        WTO   'TSO USER XXXXXXX CANCELLED: INACTIVITY'               *
*---------------------------------------------------------------------*
         SPACE
         MVC   0(MSG1L,R6),MSG1        MOVE IN MSG
         MVC   MSG1J(7,R6),SMFJOBN     MOVE IN USERID
         BAL   R5,SVC35                GO ISSUE WTO
         SPACE
*--------------------------------------------------------------------*
*        SECOND, WE SEND MSG TO USER TO SEE NOW.    I.E.             *
*        SE   'XXXXXXX CANCELLED: INACTIVITY',USER=(XXXXXXX),LOGON   *
*--------------------------------------------------------------------*
         SPACE
TWAIT00  EQU   *
         CLC   SMFJOBN(4),ASMI         IS IT A SYSTEMS PERSON ??
         BE    TWAIT05                 GO SPECIAL PROCESS
         MVC   0(MSG2L,R6),MSG2        MOVE IN THE MSG
         MVC   MSG2J(7,R6),SMFJOBN     MOVE IN USERID
         MVC   MSG2JJ(7,R6),SMFJOBN    MOVE IN USERID
         BAL   R5,SVC34                GO ISSUE THE MSG
         B     TWAIT20
         SPACE
TWAIT05  EQU   *
         MVC   0(MSG2CL,R6),MSG2C      MOVE IN THE MSG
         MVC   MSG2CJ(6,R6),SMFJOBN    MOVE IN USERID
         MVC   MSG2CJJ(6,R6),SMFJOBN   MOVE IN USERID
         BAL   R5,SVC34                GO ISSUE THE MSG
         SPACE
*--------------------------------------------------------------------*
*        THIRD , WE SEND MSG TO USER TO SEE NOW.    I.E.             *
*        SE   'XXXXXXX CANCELLED: INACTIVITY',USER=(XXXXXXX),SAVE    *
*--------------------------------------------------------------------*
         SPACE
TWAIT20  EQU   *
         CLC   SMFJOBN(4),ASMI         IS IT A SYSTEMS PERSON ??
         BE    TWAIT25                 GO SPECIAL PROCESS
         MVC   0(MSG3L,R6),MSG3        MOVE IN THE MSG
         MVC   MSG3J(7,R6),SMFJOBN     MOVE IN USERID
         MVC   MSG3JJ(7,R6),SMFJOBN    MOVE IN USERID
         BAL   R5,SVC34                GO ISSUE THE MSG
         B     CANCEL
         SPACE
TWAIT25  EQU   *
         MVC   0(MSG3CL,R6),MSG3C      MOVE IN THE MSG
         MVC   MSG3CJ(6,R6),SMFJOBN    MOVE IN USERID
         MVC   MSG3CJJ(6,R6),SMFJOBN   MOVE IN USERID
         BAL   R5,SVC34                GO ISSUE THE MSG
         B     CANCEL                  GO TO CANCEL RETURN
         SPACE
*---------------------------------------------------------------------*
*        THIS AREA IS FOR THE BATCH CANCEL. BUT WE DO NOT JUST WANT   *
*        JOBS TO BE BLANKETLY CANCELLED BECAUSE THE OPERATORS         *
*        FORGOT TO MOUNT A TAPE. THUS WE WILL TELL THEM OF THE        *
*        WAITSTATE AND LET THEM HAVE THE OPTION.                      *
*---------------------------------------------------------------------*
         SPACE
TWAIT50  EQU   *
         LA    R6,RPLYADDR             POINT TO AREA
         MVC   0(MSGWAITL,R6),MSGWAIT  MOVE IN THE MSG
         MVC   MSGWAITJ(8,R6),SMFJOBN  MOVE IN THE JOB NAME
         LA    R4,WTORRPLY             COMPLETE WTOR
         ST    R4,RPLYADDR             PLUG IN 1 BYTE
         LA    R4,L'WTORRPLY           GET ITS LENGTH
         STC   R4,RPLYADDR             PLUG IN 1 BYTE
         LA    R4,WTORECB              SETUP ECB
         ST    R4,ECBADDR              NOW WE HAVE ADDR
         SPACE
BWRETRY  EQU   *
         XC    WTORECB,WTORECB         CLEAR THE ECB
         WTOR  ,,,,MF=(E,WTORLIST)     ISSUE WTOR
         WAIT  R1,ECB=WTORECB,LONG=YES WAIT FOR THE OPERATOR
         OI    WTORRPLY,X'40'          FORCE UPPER CASE
         CLI   WTORRPLY,C'C'           DO WE CANCEL JOB   ??
         BE    CANCEL                  GO DO IT
         CLI   WTORRPLY,C'E'           DO WE EXTEND WAITSTATE
         BNE   ERRMSG                  OPERATOR GOOFED, HELP THEM
         L     R2,LIMIT                GET MAX WAIT STATE
         B     EXTEND                  SAFETY INSTRUCTION HERE
         SPACE
*---------------------------------------------------------------------*
*        THIS AREA IS FOR THE EXTENSION WHEN YOU RETURN TO SYSTEM.    *
*---------------------------------------------------------------------*
         SPACE
EXTEND   EQU   *
         LA    R0,ENDDSA-DSA          GET LENGTH OF AREA WE GOT
         O     R0,SUBPOOL             GET SUBPOOL NUMBER
         LR    R1,R13                 GET ADDRESS OF GOTTEN AREA
         L     R13,SAVE+4             GET LOW SAVE ADDRESS
         FREEMAIN R,A=(1),LV=(0)      RETURN WHAT WE GO
         ST    R2,24(R13)             TIME EXT IN R1 IN SECONDS
         RETURN (14,12),RC=8          CONTINUE WAITSTATE
         SPACE
*---------------------------------------------------------------------*
*        THIS AREA IS FOR THE CANCEL WHEN YOU RETURN TO SYSTEM.       *
*---------------------------------------------------------------------*
         SPACE
CANCEL   EQU   *
         LA    R0,ENDDSA-DSA           GET LENGTH OF AREA WE GOT
         O     R0,SUBPOOL              GET SUBPOOL NUMBER
         LR    R1,R13                  GET ADDRESS OF GOTTEN AREA
         L     R13,SAVE+4              GET LOW SAVE ADDRESS
         FREEMAIN R,A=(1),LV=(0)       GIVETH BACK AREA
         RETURN (14,12),RC=0           GO BACK WITH CANCEL
         SPACE
*---------------------------------------------------------------------*
*        AREA FOR SOME SUBROUTINES                                    *
*---------------------------------------------------------------------*
         SPACE
SVC34    EQU   *
         XR    R0,R0                   SETUP FOR SVC 34
         LA    R1,MSGWTO               POINT TO COMMAND
         SVC   OPSVC                   GO DO IT
         BR    R5                      RETURN WHENCE WE CAME
         SPACE
SVC35    EQU   *
         LA    R1,MSGWTO               POINT TO COMMAND
         SVC   WTOSVC                  DO THE SVC
         BR    R5                      RETURN WHENCE WE CAME
         SPACE
ERRMSG   EQU   *
         LA    R1,MSGERR               POINT TO MSG
         SVC   WTOSVC                  DO THE SVC
         B     BWRETRY                 RETRY QUESTION
         EJECT
*---------------------------------------------------------------------*
*        THE MSGS THAT CAN BE SENT TO THE OPERATOR OR TSO USER        *
*        ARE DEFINED BELOW. THESE ARE REALLY IN THE FORM OF THE       *
*        LIST VERSION OF 'WTO'. BUT SINCE WE MUST BUILD SOME OF       *
*        THE MSGS, IT IS MORE EFFICIENT TO CODE THEM IN THE           *
*        EXPANDED FORM. THUS HOPEFULLY IF YOU HAVE TO ADD SOME-       *
*        THING, THE EXPANDED FORM WILL TAKE CARE OF YOU               *
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG1     WTO   'CMI0050I U=XXXXXX  CANCELLED, INACTIVITY',ROUTCDE=(2), X
               MF=L
MSG1L    EQU   *-MSG1
MSG1J    EQU   15
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG2     WTO   'SE ''XXXXXXX CANCELLED: INACTIVITY'',USER=(XXXXXXX),LOGX
               ON        IEFUTL',ROUTCDE=(2),MF=L
MSG2L    EQU   *-MSG2
MSG2J    EQU   8
MSG2JJ   EQU   45
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG2C    WTO   'SE ''XXXXXX  CANCELLED: INACTIVITY'',USER=(XXXXXX),LOGOX
               N        IEFUTL',ROUTCDE=(2),MF=L
MSG2CL   EQU   *-MSG2C
MSG2CJ   EQU   8
MSG2CJJ  EQU   45
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG3     WTO   'SE ''XXXXXXX CANCELLED: INACTIVITY'',USER=(XXXXXXX),SAVX
               E         IEFUTL',ROUTCDE=(2),MF=L
MSG3L    EQU   *-MSG3
MSG3J    EQU   8
MSG3JJ   EQU   45
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG3C    WTO   'SE ''XXXXXX  CANCELLED: INACTIVITY'',USER=(XXXXXX),SAVEX
                        IEFUTL',ROUTCDE=(2),MF=L
MSG3CL   EQU   *-MSG3C
MSG3CJ   EQU   8
MSG3CJJ  EQU   45
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG4     WTO   'SE ''XXXXXXX CANCELLED: MAX CPUTIME EXCEEDED'',USER=(YYX
               YYYYY),LOGON      IEFUTL',ROUTCDE=(2),MF=L
MSG4L    EQU   *-MSG4
MSG4J    EQU   8
MSG4JJ   EQU   55
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG4C    WTO   'SE ''XXXXXX  CANCELLED: MAX CPUTIME EXCEEDED'',USER=(YYX
               YYYY),LOGON      IEFUTL',MF=L
MSG4CL   EQU   *-MSG4C
MSG4CJ   EQU   8
MSG4CJJ  EQU   55
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG5     WTO   'SE ''XXXXXXX CANCELLED: MAX CPUTIME EXCEEDED'',USER=(YYX
               YYYYY),SAVE       IEFUTL',ROUTCDE=(2),MF=L
MSG5L    EQU   *-MSG5
MSG5J    EQU   8
MSG5JJ   EQU   55
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSG5C    WTO   'SE ''XXXXXX  CANCELLED: MAX CPUTIME EXCEEDED'',USER=(YYX
               YYYY),SAVE       IEFUTL',MF=L
MSG5CL   EQU   *-MSG5C
MSG5CJ   EQU   8
MSG5CJJ  EQU   55
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSGWAIT  WTOR  'CMI0051A J=XXXXXXX  IN WAITSTATE. REPLY ''CAN'' TO CANCX
               EL OR ''EXT'' TO CONTINUE',MF=L
MSGWAITL EQU   *-MSGWAIT
MSGWAITJ EQU   23
         SPACE
*---------------------------------------------------------------------*
         SPACE
         DS    0F
MSGERR   WTO   'CMI0052I REPLY NOT ''CAN'' OR ''EXT''  ',MF=L
         SPACE
*---------------------------------------------------------------------*
*        LITERAL POOL NEXT AND CONSTANTS
*---------------------------------------------------------------------*
         SPACE
FULL4    DC    F'4'                    DEFINE 4
ASMI     DC    C'ASMI'                 1ST 4 CHAR OF SYSTEMS IDS.
LIMIT    DC    F'900'                  15 MINUTES ( 900 SECS )
         DS    0F
SUBPOOL  DC    X'F1000000'   F1        SUBPOOL 241
         SPACE
         LTORG
         SPACE
*---------------------------------------------------------------------*
*        PATCH AREA ( PRAY WE NEVER NEED IT ).                        *
*---------------------------------------------------------------------*
         DS    0F
PATCH    DC    8CL4'ZAP*'
         EJECT
*---------------------------------------------------------------------*
*        DSECTS DEFINED HERE
*---------------------------------------------------------------------*
         SPACE 2
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE
         DS    0D
DSA      DSECT
SAVE     DS    18F                     SAVE AREA
WTORECB  DS    F
WTORLIST DS    0F
RPLYADDR DS    A
ECBADDR  DS    A
MSGWTO   DS    0F
MSGLN    DS    H                       TEXT LENGTH
MSGFLGS  DS    H                       MCS FLAGS
MSG      DS    CL122                   TEXT
WTORRPLY DS    CL3
ENDDSA   DS    0D
         SPACE
*---------------------------------------------------------------------*
*        EQUATES                                                      *
*---------------------------------------------------------------------*
D0       EQU   0
WTOSVC   EQU   35                      SVC 35     ( WTO )
OPSVC    EQU   34                      SVC 34     ( MSGR )
         SPACE
         END   IEFUTL
