         TITLE 'IEECR2D2 - AUTOMATIC REPLY SUBTASK OF IEECVXIT'
*-$DOC@-------------------------------------------------------------*
*                                                                   *
*              I  E  E  C  R  2  D  2     ( R 2 D 2 )               *
*                                                                   *
*      THIS MODULE IS ATTACHED AS A SUBTASK OF IEECVXIT TO DO       *
*      AUTOMATIC REPLIES TO CERTAIN MSGS GIVEN TO THE OPERATOR      *
*      AND STARTED TASKS. THIS MAY NOT BE THE SLICKEST CODE,        *
*      FOR MY INTENT IS TO MAKE IT AS STRAIGHT FORWARD AS           *
*      POSSIBLE. YOU WILL SEE THAT IN THE OPERATOR COMMANDS         *
*      I ISSUE, 'R2D2' APPEARS IN THE MSG BUT ONLY ON SYSLOG.       *
*      THAT IS TO KNOW IF YE OLE OPERATOR DECIDED TO DO IT OR       *
*      NOT; GENERALLY NOT.                                          *
*                                                                   *
*      THE STIMER IS NOTE WORTHY OF DISCUSSION. WHEN WE GO INTO     *
*      R2D2 WANTING A STARTED TASK, IT IS POSSIBLE FOR THE STARTED  *
*      TASK TO APPEAR ON THE CONSOLE LOGS AHEAD OF THE MSG THAT     *
*      CAUSED IT TO HAPPEN. THIS IS BECAUSE THE 'MCS' EXIT GETS     *
*      TO VIEW THE MSG BEFORE IT GETS ISSUED. SO TO KEEP OPS        *
*      MANAGERS FROM GETTING CONFUSED (HEAVEN FORBID), WE WAIT      *
*      FOR TWO SECONDS. THE WAIT FOR THE AUTO REPLIES IS LONGER.    *
*      SINCE THE WQE HAS NOT BEEN BUILT WHEN WE DETECT IT IS        *
*      COMING, THEN WE MUST LAY BACK FOR A GREATER PERIOD OF TIME,  *
*      IN MY CASE ON A 4-MEG HEAVILY LOADED 3032 (NUMBER HUMMERS)   *
*      TSO/SPF AND INQUIRE ( BOO--HISS ), 5 SECONDS IS FINE.        *
*                                                                   *
*      IF ANYONE HAS SUGGESTIONS OR FINDS BUGS, PLEASE CALL, THIS   *
*      CONCEPT HAS MANY, MANY POSSIBILITIES !!!!!!!!                *
*                                                                   *
*                                 JIM MARSHALL, LT, USAF            *
*                                 AIR FORCE DATA SVCS CENTER        *
*                                 IBM OPERATING SYSTEMS SUPPORT BR  *
*                                 CMIS - ROOM 1C1037                *
*                                 WASHINGTON D.C.  20330            *
*                                 (202) 695-1274/2478               *
*                                                                   *
*-------------------------------------------------------------------*
*      REPORTED PROBLEMS:  BEFORE ANYONE GOES TO PUT 'R2D2' IN THE  *
*                          THE SCRAP HEAP, READ AND EVALUATE SOME   *
*                          BUGS (NOT REALLY) OR REALLY STRANGE      *
*                          HAPPENINGS. BELOW ARE THE REPORTING      *
*                          PEOPLE WHO HAVE FIGURED OUT WHAT         *
*                          OCCURRED. ( I WILL RELATE IT )           *
*                                                                   *
*                                                                   *
*   1)  A. PROBLEM - INITIATOR ABENDS WITH S0BB.                    *
*                                                                   *
*          BILL CALLAN (COCA-COLA OF ATLANTA, GA) 404-765-1318      *
*          IBM 3033MP, 'R2D2' STIMER SET TO 10 SEC. HEAVILY         *
*          LOADED SYSTEM. JOB GOES AFTER TAPE DRIVE AND GETS        *
*          FAMOUS 'HOLD', 'NOHOLD' MSG. IMMEDIATELY BEFORE THAT     *
*          OCCURRED, A TAPE DRIVE BECAME AVAILABLE, SO REPLY        *
*          IMMEDIATE, 'HOLD'.  THEN INITIATOR ABENDS WITH S0BB.     *
*                                                                   *
*       B. WHAT OCCURRED WAS WHEN THE 'HOLD' WAS ISSUED BY THE      *
*          OPERATOR, THE INITIATOR ATTACHED THE JOBTASK. BILL       *
*          SAYS THAT THE INITIATOR CAN ONLY HAVE ONE (1) TASK       *
*          ACTIVE. SINCE THE 'HOLD' WAS RESPONDED SO QUICKLY        *
*          NOW THE INITIATOR HAS THE 'R2D2' ATTACHED ON ITS         *
*          BEHALF AND THE JOBTASK. THUS  S0BB ABEND.                *
*          SOLUTION--DECREASE THE AMOUNT OF TIME IN THE STIMER.     *
*                    PLUS DO NOT REPLY 'HOLD'. IF YOU REALLY        *
*                    HAVE TO GET THE JOB ITS DRIVES, VARY           *
*                    ALL DRIVES OFFLINE AND THEN WHEN YOUR          *
*                    WANTS A SPARE DRIVE YOU WILL GET THE TAPE      *
*                    UNIT OFFLINE MSG, ETC, ETC.                    *
*                                                                   *
*   2)  A. PROBLEM - INITIATOR ABENDS WITH SA03                     *
*                                                                   *
*          DON MARQUARDT (MAY & SPEH) 312-620-8060, NAS AS-5,       *
*          ENCOUNTERED SA03 WHEN HE MODIFIED IEECVXIT & IEECR2D2    *
*          TO REPLY TO UNEXPIRED DATASETS ON TAPE. EVENTUAL SOL-    *
*          WAS TO FIRE THE OPERATOR FOR BEING TOO QUICK ON THE      *
*          TRIGGER. REALLY WHAT OCCURRED WAS THE OPERATOR WAS       *
*          REPLYING TO THE MSG VERSUS LETTING 'R2D2' DO IT. ALSO    *
*          DON HAD THE STIMER SET A 10 SEC. SO YOU REDUCE THE       *
*          STIMER TO SAY 4 SECS AND TELL THE OPERATOR TO GO         *
*          BACK TO SLEEP.                                           *
*                                                                   *
*                                                                   *
*   3)     FLASH - MVS/SP1.3 INFORMATION                            *
*                                                                   *
*          BILL HORTON (TENNESSEE EASTMAN) 615-245-2111 EXT 3388    *
*          CALLED AND TOLD ME R2D2 NEEDS SOME DESIGN CHANGES        *
*          FOR MVS/SP1.3. IT SEEMS SINCE THEY MOVED COMTASK TO      *
*          ITS OWN ADDRESS SPACE, THE ADDRESS OF THE WQE POOL       *
*          IN THE UCM STILL LOOKS VALID. EXCEPT IF YOU GO THERE     *
*          IT IS BINARY ZEROS (SIGNAL OF END OF THE CHAIN). SINCE   *
*          I AM NOT ON MVS/SP1.3 YET I CAN NOT HELP. IF ANYONE      *
*          GETS R2D2 TO WORK, SEND ME THE CODE.                     *
*                                                                   *
*                                                                   *
*      THE VALUE IN REGISTER 1 IS USED TO DETERMINE WHAT WE WANT    *
*      TO DO.  THIS COMES FROM IEECVXIT.                            *
*                                                                   *
*         (  R1  )            COMMAND                               *
*                                                                   *
*            0         -    ( SOME AUTO REPLY )                     *
*            4         -    S $SMITRAC           (DUMP MSS TRACE)   *
*            8         -    S TSO                                   *
*           12         -    S $SMIMSG            (DUMP MSS LOG)     *
*                                                                   *
*      AUTO REPLIES :                                               *
*   ----------------------------------------------------------      *
*  1.  R XX,'NOHOLD'     IEF433D JOBNAME - WAIT REQUESTED -- REPLY  *
*                        'HOLD' OR 'NOHOLD'                         *
*                                                                   *
*     ACTION: WE ALWAYS WANT TO REPLY 'NOHOLD'. OTHERWISE Q4        *
*             IS LOCKED AND WE ARE BACK LIKE WE WERE IN THE         *
*             OS/MVT DAYS.                                          *
*                                                                   *
*   ----------------------------------------------------------      *
*  2.  R XX,'NOHOLD'     IEF434D JOBNAME - INVALID REPLY, REPLY     *
*                        'HOLD' OR 'NOHOLD'                         *
*                                                                   *
*     ACTION: THE OPERATOR REPLIED TO THE IEF433D MESSAGE           *
*             INCORRECTLY SO WE WANT TO REPLY 'NOHOLD' FOR THE      *
*             REASONS STATED ABOVE.                                 *
*                                                                   *
*   ----------------------------------------------------------      *
*  3.  R XX,'U'          IKT012D TCAS TERM IN PROGRESS, 'U' OR      *
*                        'DUMP'                                     *
*                                                                   *
*     ACTION: VTAM HAS TERMINATED. WE ALWAYS REPLY U                *
*                                                                   *
*   ----------------------------------------------------------      *
*  4.  R XX,'U'          ICB423D REPLY WITH MSVC PARAMTERS OR U     *
*                                                                   *
*     ACTION: MSS BEAST IS BROUGHT FOLLOWING AND NOT PART OF AN     *
*             IPL. WE INITIALIZE AND ALWAYS REPLY 'U' .             *
*                                                                   *
*   ----------------------------------------------------------      *
*  5.  R XX,'GO'         ERB306D REPLY OPTIONS OR GO                *
*                                                                   *
*     ACTION: RMF WANT TO KNOW THE OPTIONS OR GO, WE REPLY GO       *
*                                                                   *
* CHANGE LOG: 10 MAY 82 - 1. TOOK OUT 'Z NET,QUICK' TO BRING DOWN   *
*                            VTAM WHEN TSO GOES.                    *
*                         2. INCLUDE MORE MSGS TO SUPPRESS WHILE    *
*                            WAIT FOR MVS/SP1.3                     *
*                         3. ADDED SOME DETECTION FOR MSS MSGS      *
*                            THUS GOING TO R2D2 FOR SERVICE.        *
*                                                                   *
*                                                                   *
*-$DOC$-------------------------------------------------------------*
         EJECT
*-------------------------------------------------------------------*
*        MACROS                                                     *
*-------------------------------------------------------------------*
         MACRO
&DOIT    SVC34 &CMDAREA
         LA    R1,&CMDAREA
         SR    R0,R0
         SVC   34
         B     RETURN
         MEND
         SPACE
IEECR2D2 CSECT
         SAVE  (14,12),,IEECR2D2_AFDSC_&SYSDATE_&SYSTIME
         LR    R12,R15       SETUP BASE REGISTER
         USING IEECR2D2,R12  GET NEW BASE REGISTER
         LR    R11,R1        SAVE ENTRY CODE
         L     R0,WORKSIZE          GET SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)           GO DO IT
         LR    R10,R1               SAVE THE ADDRESS
         USING WORKAREA,R10         GET ADDRESSABILITY
         ST    R13,SAVE+4           SETUP
         ST    R10,8(R13)             SAVEAREA
         LR    R13,R10                    CHAIN
         LTR   R11,R11              WHAT ARE WE HERE FOR ?
         BNZ   STARTCMD             MUST BE START COMMAND
         STIMER WAIT,BINTVL=FIVESEC WAIT FOR MCS TO CATCH UP
         EJECT
*--------------------------------------------------------------------*
*        SOME AUTOMATIC REPLY IS NEEDED TO BE SERVICED. SO WE ARE    *
*        GOING TO RUN THE WQE CHAIN LOOKING FOR THE BEAST, DETER-    *
*        MINE WHAT WE ARE REPLYING TO, COPY THE REPLY TO AN AREA     *
*        OF GETMAINED CORE, MOVE IN THE REPLY NUMBER, AND ISSUE      *
*        SVC 34 TO DO IT.  NEAT !!!!!!!!!                            *
*--------------------------------------------------------------------*
         L     R6,16                LOAD A(CVT)
         L     R6,100(R6)           LOAD A(UCM)
         L     R7,24(R6)            LOAD A( 1ST WQE )
         XR    R8,R8                CLEAR R8
         LH    R8,58(R6)            GET COUNT OF WQE'S
         LTR   R7,R7                SEE IF OPERATOR HAS DONE IT,
         BZ    RETURN               JUST IN CASE !!!!!!
LOOP     EQU   *
         CLC   35(L'IEF433D,R7),IEF433D
         BNE   CONT1
         MVC   WTOAREA(REPLY1L),REPLY1
         B     ISSUE                GO DO IT !!!!
CONT1    EQU   *
         CLC   35(L'IKT012D,R7),IKT012D
         BNE   CONT2
         MVC   WTOAREA(REPLY2L),REPLY2
         B     ISSUE
CONT2    EQU   *
         CLC   35(L'ICB423D,R7),ICB423D
         BNE   CONT3
         MVC   WTOAREA(REPLY2L),REPLY2
         B     ISSUE
CONT3    EQU   *
         CLC   35(L'ERB306D,R7),ERB306D
         BNE   CONT4
         MVC   WTOAREA(REPLY3L),REPLY3
         B     ISSUE
CONT4    EQU   *
         CLC   35(L'IEF434D,R7),IEF434D
         BNE   CONTEND
         MVC   WTOAREA(REPLY1L),REPLY1
         B     ISSUE
CONTEND  EQU   *
         L     R7,0(R7)             BUMP THE ADDRESS
         LTR   R7,R7                SEE IF OPER HAS MAYBE DONE IT
         BZ    RETURN
         BCT   R8,LOOP
         B     RETURN
         SPACE
ISSUE    EQU   *
         MVC   WTOAREA+6(2),32(R7)       MOVE IN REPLY NUMBER
         LA    R1,WTOAREA
         SR    R0,R0             WE WILL ISSUE CMD
         SVC   34
         B     RETURN
         SPACE
*--------------------------------------------------------------------*
*        R11 (SAVED R1) CONTAINS A NUMBER. WILL USE THAT NUMBER      *
*        TO GO INTO A BRANCH TABLE TO GET THE PROPER 'START'         *
*        COMMAND TO DO. THE 2 SECOND WAIT IS TO ALLOW THE MSG        *
*        TO COME OUT IN SYSLOG BEFORE THE START CMD APPEARS.         *
*--------------------------------------------------------------------*
         SPACE
STARTCMD EQU  *
         STIMER WAIT,BINTVL=TWOSEC  GIVE A LITTLE DELAY
         B    BRTAB(R11)            BRANCH TO BRANCH
         B    RETURN                SAFETY VALVE
         SPACE
BRTAB    EQU  *
         B    RETURN                 SAFETY VALVE
         B    CMD4                  DO START FOR R1=4
         B    CMD8                  DO START FOR R1=8
         B    CMD12                 DO START FOR R1=12
         B    RETURN                FUTURE USE
         SPACE
CMD4     EQU  *
         SVC34 CMD4MSG            POINT TO MSG
         SPACE
CMD8     EQU  *
         SVC34 CMD8MSG
         SPACE
CMD12    EQU  *
         SVC34 CMD12MSG
         SPACE
RETURN   EQU *
         LR   R1,R13
         L    R0,WORKSIZE
         L    R13,SAVE+4           GET ADDRESS IN SAVE AREA
         FREEMAIN R,LV=(0),A=(1)   FREE UP GOTTEN CORE
         LM   R14,R12,12(R13)
         LA   R15,0(0,0)           SET RC=0
         BR   R14                  SO LONG  !!!!!!
         SPACE
         EJECT
*-------------------------------------------------------------------*
*        CONSTANTS                                                  *
*-------------------------------------------------------------------*
         SPACE
         DS   0F
WORKSIZE DC    AL1(230),AL3(WORKEND-WORKAREA)
FIVESEC  DC    A(5*100)              5 SECOND DELAY
TWOSEC   DC    A(2*100)              2 SECOND DELAY
IEF433D  DC    C'IEF433D'            NOHOLD REPLY MSG
IEF434D  DC    C'IEF434D'            NOHOLD IN ERROR RETRY
IKT012D  DC    C'IKT012D'            VTAM MSG
ICB423D  DC    C'ICB423D'            REPLY U
ERB306D  DC    C'ERB306D'            REPLY GO FOR RMF
         DS    0F
         SPACE
CMD4MSG  WTO   'S $SMITRAC       R2D2 ',MF=L
         SPACE
CMD8MSG  WTO   'S TSO            R2D2',MF=L
         SPACE
CMD12MSG WTO   'S $SMIMSG        R2D2',MF=L
         SPACE
*------------------------------------------------------------------*
REPLY1   WTO   'R XX,''NOHOLD''     R2D2 ',ROUTCDE=(1),DESC=(5),MF=L
REPLY1L  EQU   *-REPLY1
*------------------------------------------------------------------*
REPLY2   WTO   'R XX,''U''   R2D2 ',ROUTCDE=(1),DESC=(5),MF=L
REPLY2L  EQU   *-REPLY2
*------------------------------------------------------------------*
REPLY3   WTO   'R XX,''GO''    R2D2  ',ROUTCDE=(1),DESC=(5),MF=L
REPLY3L  EQU   *-REPLY3
*------------------------------------------------------------------*
         SPACE
R0       EQU  0
R1       EQU  1
R2       EQU  2
R3       EQU  3
R4       EQU  4
R5       EQU  5
R6       EQU  6
R7       EQU  7
R8       EQU  8              ADDRESSABILITY TO SMFREC
R9       EQU  9
R10      EQU  10
R11      EQU  11             SAVED ENTRY CODE FROM R1
R12      EQU  12             BASE REGISTER
R13      EQU  13
R14      EQU  14
R15      EQU  15
         DS   0F
WORKAREA DSECT
SAVE     DS   18F                     SAVE AREA
WTOAREA  DS   CL128
WORKEND  EQU  *
         END
