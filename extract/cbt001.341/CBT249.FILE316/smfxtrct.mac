*          DATA SET SMFXTRCT   AT LEVEL 019 AS OF 03/24/82
*$DOC@****************************************************************
*                        MACRO DEFINITIONS                           *
**********************************************************************
         MACRO
&A       CLEAR &N,&M
&A       MVI   &N,X'40'
         MVC   &N+1(132),&N
         MVI   &N,&M
         MEND
         EJECT
**********************************************************************
*                                                                    *
*        SMFXTRCT - THIS PROGRAM EXTRACTS DATA FROM THE SMF RAW      *
*                   TAPE AND WRITES IT TO AN OUTPUT DATASET          *
*                   UNCHANGED IN FORMAT, USING THE SAME DATA SET     *
*              CHARACTERISTICS AS THE SMF RAW TAPE (I.E., RECFM=VBS  *
*              LRECL=2048,BLKSIZE=2052).  THIS IS ACHIEVED BY        *
*              COPYING THE INPUT DCB ATTRIBUTES UNLESS THE OUTPUT    *
*              DCB ATTRIBUTES ARE TO BE DIFFERENT FROM INPUT.        *
*              THE RDJFCB MACRO IS USED TO DETERMINE IF THE PERT-    *
*              INENT CHARACTERISTICS SUCH AS RECFM, LRECL AND        *
*              BLOCKSIZE ARE DIFFERENT.  IF THEY ARE DIFFERENT,      *
*              THESE VALUES ARE COPIED INTO THE OUTPUT DCB INSTEAD   *
*              OF USING THE INPUT DCB VALUES.                        *
*              EFFECTIVE WITH THIS VERSION ARE SEVERAL PROGRAM       *
*              IMPROVEMENTS OVER VERSION 3; THESE ARE (1) IMPROVED   *
*              PARM FIELD INFORMATION DECODE TO HANDLE MORE THAN     *
*              ONE PARAMETER IN ANY ORDER; (2) FIX FOR ERROR MESSAGE *
*              ISX001 FOR NO CONTROL CARD OR INSUFFICIENT NUMBER     *
*              OF CONTROL CARDSF AND (3) A USER OPTION TO LINK TO    *
*              THE SORT PROGRAM (IGHRCO00) BY SPECIFYING AN OPTION   *
*              IN THE PARM FIELD.  WHEN THE SORT OPTION IS SPECIFIED *
*              THE USER MUST SUPPLY THE SORT CONTROL CARDS BEHIND    *
*              THE CONTROL CARD FOR SMFXTRCT IN THE INPUT STREAM.    *
*              AN INSUFFICIENT NUMBER OF SORT CONTROL CARDS WILL     *
*              CAUSE AN SMFXTRCT ERROR, NOT A SORT ERROR (ISX001).   *
*                                                                    *
*                                                                    *
*              THIS PROGRAM REQUIRES THE FOLLOWING DD CARDS:         *
*                                                                    *
*              SYSPRINT -- PRINTED OUTPUT DATA SET                   *
*              SYSIN    -- CONTROL CARD INPUT                        *
*              SYSUT1   -- INPUT SMF DATASET                         *
*              SYSUT2   -- OUTPUT EXTRACTED RECORD DATASET           *
*                                                                    *
*              IN ADDITION, ALL NECESSARY JCL FOR SORT SHOULD BE     *
*              INCLUDED IF PARM=SORT WILL BE SPECIFIED.              *
*                                                                    *
**********************************************************************
         EJECT
******************************************************************RVH*
*                                                                 RVH*
*                                                                 RVH*
*   CONTROL CARD(S) FORMAT  COL 1-70 WITH AN "*" IN 72 FOR CONT.  RVH*
*                                                                 RVH*
*   1    RECTYPE=ALL                                              RVH*
*               =X                                                RVH*
*               =(X,Y,Z)             MAX OF 20                    RVH*
*                                                                 RVH*
*   2    EXCLUDE=ALL                                              RVH*
*               =X                                                RVH*
*               =(X,Y,Z)             MAX OF 20                    RVH*
*                                                                 RVH*
*   3    DATE=YYDDD                                               RVH*
*            =(YYDDD,YYDDD)          RANGE OF DATES               RVH*
*                                                                 RVH*
*   4    NUMBER=XXXXXXX              OUTPUT RECORD COUNT LIMIT    RVH*
*                                                                 RVH*
*   5    SUBCODE=XXXX                                             RVH*
*               =(XXXX,YYYY)         MAX OF 5                     RVH*
*                                                                 RVH*
*   6    TIME=HH.MM.SS                                            RVH*
*            =(HH.MM.SS,HH.MM.SS)    RANGE OF TIMES               RVH*
*                                                                 RVH*
*   7    FIELD=(DISP,LENGTH,VALUE)   DISP=OFFSET+4 FOR RDW        RVH*
*                                                                 RVH*
*   8    MASK=(DISP,OPERATOR,MASK)   OPERATOR = BO,BM,BZ,BNO      RVH*
*                                    SELECT IF OPER IS TRUE       RVH*
*                                                                 RVH*
******************************************************************RVH*
         EJECT
**********************************************************************
*                                                                    *
*        THIS SECTION ESTABLISHES BASE REGISTERS, TAKES CARE OF      *
*        LINKAGE CONVENTIONS, AND OPENS THE DCB'S FOR THE DATA SETS. *
*        THE PROGRAM HEADER IS PRINTED, AND THE JFCB IS READ IN      *
*        TO DETERMINE IF ANY CHANGES WERE MADE BY THE USER IN THE    *
*        DCB ATTRIBUTES OF THE OUTPUT DATA SET.  IF THERE WERE, THE  *
*        DCB ATTRIBUTES SUPPLIED BY THE USER ON THE SYSUT2 CARD ARE  *
*        SUBSTITUTED INTO THE OUTPUT DCB FOR SYSUT2.  IF NO CHANGES  *
*        ARE INDICATED, DCB ATTRIBUTES ARE COPIED FROM SYSUT1.   IF  *
*        THE DCB ATTRIBUTES ARE DIFFERENT FOR OUTPUT, A MESSAGE STAT-*
*        THE NEW DCB ATTRIBUTES IS ALSO PRINTED.                     *
*                                                                    *
*        MODIFICATION MAKE TO PRINT THE TOTALS OF EACH RECORD     JDM*
*        TYPE OUTPUTED.    J.MARSHALL, AIR FORCE DATA SVCS        JDM*
*        23 MARCH 1982.    MODIFICATIONS MADE TO PROGRAM FOR THE  RVH*
*                          FOLLOWING AREAS:                       RVH*
*           1.  ADDED MASK ROUTINE                                RVH*
*           2.  ADDED JOBNAME ROUTINE                             RVH*
*           3.  CHANGED CONTROL CARD ROUTINE                      RVH*
*           4.  FIXED SOME BUGS (BAD BASE REG FOR ONE)            RVH*
*           5.  STARTED TO USE REGISTER EQUATES                   RVH*
*                                                                 RVH*
**$DOC$***************************************************************
         SPACE 2
SMFXTRCT CSECT
         STM   14,12,12(13)
         LR    12,15
         USING SMFXTRCT,12,11,10
         LA    11,4095(12)
         LA    11,1(11)
         LA    10,4095(11)
         LA    10,1(10)           3 BASE REGISTERS - MY, MY...
         ST    13,SAVE+4
         LA    9,SAVE
         ST    9,8(13)
         LR    13,9
         LR    2,1                GET ADDR OF FULLWORD POINTER TO
*                                 PARM AREA
         L     2,0(2)             LOAD 2 WITH ADDRESS OF PARM AREA
*                                 THIS IS DONE BECAUSE THE CHECK
*                                 FOR PARM INFORMATION  OCCURS AFTER
*                                 THE PUT MACROS FOR THE TITLE,
*                                 WHICH USE REGISTER 1.
         OPEN  (CONTROLC,,SMFTAPE)
         RDJFCB (OUTDCB)
         CLC   JFCBWORK+102(2),=X'0000'  SEE IF USER OVERRODE BLOCKSIZE
         BNE   MOVEBLKZ           IF HE DID, GO COPY JFCBBLKS INTO
*                                 OUTPUT DCB (OUTDCB).
         MVC   OUTDCB+62(2),SMFTAPE+62  OTHERWISE JUST COPY OUTPUT
*                                 BLOCKSIZE FROM INPUT.
         B     CLRECL
MOVEBLKZ MVC   OUTDCB+62(2),JFCBWORK+102    COPY JFCB BLOCKSIZE
CLRECL   CLC   JFCBWORK+104(2),=X'0000'  SEE IF USER OVERRODE LRECL
         BNE   MOVELRCL           IF HE DID, GO COPY DCBLRECL FOR
*                                 OUTPUT FROM JFCB
         MVC   OUTDCB+82(2),SMFTAPE+82  OTHERWISE JUST COPY OUTPUT
*                                 DCBLRECL FROM INPUT.
         B     CRECFM
MOVELRCL MVC   OUTDCB+82(2),JFCBWORK+104  COPY LRECL FROM JFCB
CRECFM   CLI   JFCBWORK+100,X'00' SEE IF USER OVERRODE RECFM
         BNE   MOVERECF           IF SO, COPY RECFM FROM JFCB
         MVC   OUTDCB+36(1),SMFTAPE+36  OTHERWISE COPY OUTPUT
*                                 DCBRECFM FROM INPUT.
         B     OPENOUT
MOVERECF MVC   OUTDCB+36(1),JFCBWORK+100  COPY RECFM FROM JFCB
OPENOUT  OPEN  (OUTDCB,(OUTPUT),PRINTDCB,(OUTPUT))
         TM    OUTDCB+36,X'C0'    TEST FOR RECFM=U.
         BM    *+8                IF NOT U, IT IS EITHER F OR V.
         B     *+24               IF RECFM=U, TREAT IT AS V FOR NOW...
         TM    OUTDCB+36,X'80'
         BZ    *+16
         LA    3,WORKA+4
         ST    3,AWORK
         B     PRNTTITL
         LA    3,WORKA
         ST    3,AWORK            NECESSARY BECAUSE REGISTER 3 IS USED
*                                 IN THE NEXT SECTION IN A BCT.
PRNTTITL MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F1'
         MVC   PRINTOUT+39(54),ALLSTARS
         PUT   PRINTDCB,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+40(52),ALLSPACE
         PUT   PRINTDCB,PRINTOUT
         MVC   PRINTOUT+41(50),=C'SMFXTRCT - SMF DATA EXTRACTION PROGRAX
               M - VERSION 5'
         PUT   PRINTDCB,PRINTOUT
         MVC   PRINTOUT+40(52),ALLSPACE
         PUT   PRINTDCB,PRINTOUT
         MVC   PRINTOUT+40(52),ALLSTARS
         PUT   PRINTDCB,PRINTOUT
         MVI   PRINTOUT,X'40'
         MVC   PRINTOUT+1(132),PRINTOUT
         MVI   PRINTOUT,X'F0'
         PUT   PRINTDCB,PRINTOUT
         CLC   OUTDCB+36(1),SMFTAPE+36
         BNE   DIFFDCB
         CLC   OUTDCB+62(2),SMFTAPE+62
         BNE   DIFFDCB
         CLC   OUTDCB+82(2),SMFTAPE+82
         BNE   DIFFDCB
         CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+1(58),=C'OUTPUT DATASET WILL HAVE SAME DATASET X
               ATTRIBUTES AS INPUT.'
         PUT   PRINTDCB,PRINTOUT
         B     SEEPARM
DIFFDCB  CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+1(40),=C'OUTPUT DATASET HAS FOLLOWING ATTRIBUTES
               S:'
         PUT   PRINTDCB,PRINTOUT
         CLEAR PRINTOUT,X'F0'
         MVC   PRINTOUT+10(8),=C'RECFM = '
         LA    4,PRINTOUT+19
TM1      TM    OUTDCB+36,X'C0'    TEST FOR RECFM=U
         BO    *+8
         B     TM2
         MVC   0(1,4),=C'U'
         B     PRTRECFM
TM2      TM    OUTDCB+36,X'80'    TEST FOR RECFM=F
         BZ    TM3                IF NOT U OR F, IT MUST BE V...
         MVC   0(1,4),=C'F'       MOVE OUT INDICATOR
         LA    4,1(4)             POINT TO NEXT PRINT POSITION
         B     TM4                NO POINT GOING TO THE TM FOR RECFM=V
*                                 IF YOU ALREADY KNOW IT IS F.
TM3      TM    OUTDCB+36,X'40'    TEST FOR RECFM=V..
         BZ    TM4
         MVC   0(1,4),=C'V'
         LA    4,1(4)
TM4      TM    OUTDCB+36,X'10'    TEST FOR RECFM=.B.
         BZ    TM5
         MVC   0(1,4),=C'B'
         LA    4,1(4)
TM5      TM    OUTDCB+36,X'08'    TEST FOR RECFM=..S
         BZ    PRTRECFM
         MVC   0(1,4),=C'S'
PRTRECFM PUT   PRINTDCB,PRINTOUT
         CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+10(8),=C'LRECL = '
         LA    5,0
         LH    5,OUTDCB+82
         CVD   5,DBLWORD
         MVC   PRINTOUT+18(6),=X'402020202021'
         ED    PRINTOUT+18(6),DBLWORD+5
         PUT   PRINTDCB,PRINTOUT
         MVC   PRINTOUT+10(8),=C'BLKSZ = '
         LH    5,OUTDCB+62
         CVD   5,DBLWORD
         MVC   PRINTOUT+18(6),=X'402020202021'
         ED    PRINTOUT+18(6),DBLWORD+5
         PUT   PRINTDCB,PRINTOUT
         CLEAR PRINTOUT,X'F0'
         PUT   PRINTDCB,PRINTOUT
         MVI   PRINTOUT,X'40'
SEEPARM  EQU   *
         EJECT
**********************************************************************
*                                                                    *
*        DETERMINE EXISTENCE OF PARM INFORMATION ON THE EXEC CARD,   *
*        AND TRANSFER IT TO A PARM WORK AREA.  PARM INFO WILL ALSO   *
*        BE PRINTED OUT IN THIS SECTION, AND ANY ACTION  INDICATED   *
*        BY THE PARM INFO WILL BE TAKEN AT THIS TIME.                *
*                                                                    *
**********************************************************************
         SPACE 2
         CLC   0(2,2),=X'0000'    SEE IF PARM COUNT IS ZERO
         BE    NOPARM             IF SO, NO PARM INFO EXISTS
         MVI   PARMAREA,X'40'
         MVC   PARMAREA+1(99),PARMAREA
         LA    3,0                INITIALIZE REG 3 TO 0.
         LH    3,0(2)             LOAD REG 3 WITH NUMBER OF BYTES
*                                 OF PARM INFO.
         LA    4,PARMAREA         LOAD REG 4 WITH ADDR OF PARMAREA.
         LA    2,2(2)             LOAD REG 2 WITH ADDR OF FIRST BYTE
*                                 OF PARM INFO.
         ST    2,PARMSAVE         STORE ADDRESS FOR USE LATER ON.
PARMA    CLI   0(2),C','          IS THIS CHARACTER A DELIMITING COMMA
         BE    ANALYZEP           IF SO GO TO ANALYZE PREVIOUS PARM
         BCT   3,*+18             IF MORE PARM INFO, KEEP GOING
         MVI   MOREPARM,C'N'      SET SWITCH TO SHOW THIS IS LAST PARM.
         MVC   0(1,4),0(2)        MOVE LAST BYTE OF PARM INFO OUT
         B     ANALYZEP           GO ANALYZE THE PARM INFO
         MVC   0(1,4),0(2)        MOVE A BYTE OF PARM INFO TO TEST AREA
         LA    4,1(4)             POINT TO NEXT SLOT IN TEST AREA.
         LA    2,1(2)             POINT TO NEXT CHARACTER IN PARM FIELD
         B     PARMA              GO BACK AND DO IT AGAIN.
ANALYZEP CLC   PARMAREA(14),=C'ABEND ON ERROR'   WAS THIS PARM SPEC?
         BE    PARMB              IF SO SET APPROPRIATE SWITCH.
         CLC   PARMAREA(14),=C'ABEND ON EOJ  '   WAS THIS PARM SPEC?
         BE    PARMC              SET APPROPRIATE SWITCH
         CLC   PARMAREA(14),=C'SORT          '   WAS THIS PARM SPEC?
         BE    PARMD              SET APPROPRIATE SWITCH.
         B     PARME              OTHERWISE PARM HAS NO SPECIAL MEANING
*                                 AND IS ASSUMED INFORMATIONAL.
PARMB    MVI   ABENDSW,C'E'
         B     PARME
PARMC    MVI   ABENDSW,C'J'
         B     PARME
PARMD    MVI   SORTSW,C'S'
PARME    LA    4,PARMAREA         RESET REG 4 TO BEGINNING OF TEST AREA
         MVI   PARMAREA,X'40'
         MVC   PARMAREA+1(99),PARMAREA
         CLI   MOREPARM,C'N'      IS THIS THE LAST PARM?
         BE    ENDPARM
         LA    2,1(2)             POINT PAST COMMA
         B     PARMA
ENDPARM  L     2,PARMSAVE         LOAD REG 2 WITH ADDR OF PARM INFO
         CLEAR PRINTOUT,X'F0'     CLEAR OUT PRINT LINE
         MVC   PRINTOUT+1(5),=C'*****' PARM INFO SIGNAL
         LA    4,PRINTOUT+7       POINT REG 4 TO NEXT FIELD OF PRINTOUT
         LA    3,0
         S     2,=F'2'            POINT TO PARM LENGTH FIELD
         LH    3,0(2)             LOAD LENGTH OF PARM INFO INTO REG 3.
         EX    3,MVCPARM          USE 3 AS LENGTH FACTOR FOR MVCPARM
         B     PRNTPARM           GO AND PRINT THE PARM INFO.
         SPACE 2
MVCPARM  MVC   0(0,4),2(2)        VARIABLE-LENGTH MOVE INSTRUCTION
         SPACE 2
PRNTPARM PUT   PRINTDCB,PRINTOUT
NOPARM   EQU   *
         EJECT
***********************************************************************
*                                                                     *
*              ANALYZE CONTROL CARD FOR PARAMETERS                    *
*                                                                     *
***********************************************************************
*                                                                    *
*        THIS SECTION IS RESPONSIBLE OF DECODING ALL CONTROL CARD    *
*        PARAMETERS AND SETTING UP CONTROL DATA WHEN NEEDED.  THE    *
*        MODUS OPERANDI IS SIMPLY THIS:  EACH TIME A PARTICULAR PARA-*
*        METER IS DECODED, THE CORRESPONDING FUNCTION BYTE IS SET TO *
*        A CHARACTER 1 (F1).  OTHERWISE IT REMAINS ZERO.  WHERE TWO  *
*        PARAMETERS ARE MUTUALLY EXCLUSIVE, THE CODING FOR THAT PARA-*
*        METER TURNS ON ITS OWN FUNCTION SWITCH AND ALSO TURNS OFF   *
*        THE FUNCTION BYTES OF THOSE PARAMETERS IT IS MUTUALLY EXCLU-*
*        SIVE TO.  THUS, THE LAST PARAMETER CODED HAS THE FINAL      *
*        INFLUENCE.  LATER ON, IN THE RECORD SELECTION SECTION,      *
*        DIFFERENT SUBROUTINES ARE GONE TO BASED ON THE SETTING      *
*        OF THE FUNCTION BYTES IN THIS SECTION.                      *
*                                                                    *
*        IN THIS SEGMENT OF THE PROGRAM, REGISTER 9 IS USED AS A     *
*        POINTER TO THE CONTROL CARD WORK AREA.  REGISTER 9 ALWAYS   *
*        POINTS TO THE BEGINNING OF THE FIELD UNDER ANALYSIS.  OTHER *
*        REGISTER ASSIGNMENTS ARE MADE WITHIN THE ANALYSIS ROUTINES  *
*        THEMSELVES, AND ARE INDEPENDENT OF EACH OTHER.  THE INTEG-  *
*        RITY OF REGISTER 9, HOWEVER, IS MAINTAINED THROUGHOUT  ALL  *
*        ROUTINES IN THIS SECTION.                                   *
*                                                                    *
*                                                                    *
**********************************************************************
         SPACE 2
         GET   CONTROLC,CARDIN    READ IN CONTROL CARD
CC1      CLEAR PRINTOUT,X'F0'     BLANK OUT PRINT LINE
         MVC   PRINTOUT+1(5),=C'*****'
         MVC   PRINTOUT+6(19),=C'CONTROL CARD IMAGE:'
         MVC   PRINTOUT+26(80),CARDIN                               RVH
         PUT   PRINTDCB,PRINTOUT
         LA    R9,CARDIN          LOAD ADDR FOR SEARCH ROUTINES     RVH
         B     CCCHECK            AND GO TO THE SEARCH ROUTINES     RVH
NXTCCARD GET   CONTROLC,CARDIN    READ NEXT CONTROL CARD            RVH
         CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+26(80),CARDIN                               RVH
         PUT   PRINTDCB,PRINTOUT
         LA    9,CARDIN           LOAD ADDR OF CARD IMAGE INTO REG 9
CCCHECK  CLC   0(4,9),=C'RECT'    WAS RECTYPE CODED?
         BE    RECTYPE            IF SO GO TO RECTYPE DECODE ROUTINE
         CLC   0(4,9),=C'EXCL'    WAS EXCLUDE CODED?
         BE    EXCLUDE            GO TO EXCLUDE DECODE ROUTINE
         CLC   0(4,9),=C'DATE'    WAS DATE CODED?
         BE    DATE               GO TO DATE DECODE ROUTINE.
         CLC   0(4,9),=C'SUBC'    WAS SUBCODE CODED?
         BE    SUBCODE            GO TO SUBCODE DECODE ROUTINE
         CLC   0(4,9),=C'OPRC'    WAS OPRCODE CODED?
         BE    OPRCODE            GO TO OPRCODE DECODE ROUTINE
         CLC   0(4,9),=C'JOBN'    WAS JOBNAMES CODED?
         BE    JOBNAMES           GO TO JOBNAMES DECODE ROUTINE
         CLC   0(4,9),=C'NUMB'    WAS NUMBER PARAMETER CODED?
         BE    NUMBER             GO TO NUMBER PARAMETER DECODE ROUTINE
         CLC   0(4,9),=C'TIME'    WAS TIME PARAMETER CODED?
         BE    TIME               IF SO GO TO IIME PARAMETER DECODE RTN
         CLC   0(4,9),=C'FIEL'    FIELD PARAMETER SPECIFIED?
         BE    FIELD              YES, GO TO FIELD DECODE ROUTINE
         CLC   0(4,9),=C'MASK'    MASK PARAMETER SPECIFIED?         RVH
         BE    MASK               YES, GO TO MASK DECODE ROUTINE    RVH
         CLI   0(R9),C' '         IS THERE A PARAMETER AT ALL?      RVH
         BNE   ISX004             IF NOT GO TO ERR1 ROUTINE BECAUSE RVH
*                                 OF INVALID KEYWORD ON CONTROL CARD.
         CLI   CARDIN+71,C' '     ELSE - IS THERE A CONTINUATION    RVH
         BE    GETRECZ            NO   - GO DO THE PROCESSING       RVH
         B     NXTCCARD           YES  - GO GET THE NEXT CARD       RVH
*
*
*        THE INDIVIDUAL PARAMETER-ANALYSIS ROUTINES WHICH FOLLOW
*        EACH START ON A NEW PAGE.  EACH ONE HAS THE TASK OF ANALYZING
*        THE PARAMETER FOR CORRECTNESS, AND BRANCHING TO ANY ERROR
*        MESSAGE ROUTINES WHICH ARE NEEDED; THEY SET UP ALL NECESSARY
*        DEFAULTS IF NEEDED, AND THEY CHECK FOR THE EXISTENCE OF THE
*        NEXT PARAMETER (AND POINT TO IT IF IT EXISTS).  CONTROL IS
*        THEN RETURNED TO THE CCCHECK ROUTINE OR TO GETRECZ, AS
*        APPROPRIATE.
*
         EJECT
************
*          *
* RECTYPE  *                      RECTYPE PARAMETER DECODE ROUTINE
*          *
************
*
*
*        RECTYPE (AND EXCLUDE) WORK BY GENERATING A TABLE OF RECORD
*              TYPES WHICH ARE MATCHED AGAINST INCOMING SMF RECORDS
*              LATER ON IN THE PROGRAM.  THIS SECTION GENERATES THAT
*              TABLE.
*
*              REGISTER CONVENTIONS FOR THIS SECTION ARE AS FOLLOWS:
*
*              3:  WORK REGISTER FOR NUMBER CONVERSIONS.
*              4:  ADDRESS REGISTER.  THE LAST INSTRUCTION OF THE
*                  PARAMETER CONVERSION PORTION OF THE
*                  RECTYPE PARAMETER CONTAINS A BR 4 INSTRUCTION.
*                  THIS SECTION STARTS AT STATEMENT R2 AND RUNS
*                  ON DOWN TO THE BR 4 INSTRUCTION.
*                  NORMALLY REGISTER 4 HAS IN IT THE ADDRESS OF CCCHECK
*                  SO THAT AFTER THE RECTYPE PARAMETER HAS BEEN FULLY
*                  CHECKED, RETURN WILL BE MADE TO CCCHECK.  HOWEVER,
*                  IF THE MULTIPLE-RECORD-FORMAT OPTION OF RECTYPE
*                  IS USED THE SERIES OF INSTRUCTIONS FROM R2 ON DOWN
*                  TO THE BR4 WILL HAVE TO BE REPEATEDLY EXECUTED FOR
*                  EACH PARAMETER CODED WITHIN THE PARENTHESIS.
*                  THUS, PART OF THE FUNCTION OF "RMANYREC" IS TO SET
*                  THE ADDRESS IN REGISTER 4 TO CAUSE A RETURN TO AN
*                  INSTRUCTION WHICH CHECKS TO SEE IF THERE ARE ANY
*                  MORE RECORD TYPES.  IF THERE ARE, THE CODING
*                  STARTING AT R2 IS RE-ENTERED, THIS TIME AS A
*                  SUBROUTINE.
*              6:  AS AN ADDRESS POINTER FOR RECTABLE, WHERE ALL THE
*                  RECORD TYPES TO BE SELECTED OR EXCLUDED RESIDE.
*              7:  ADDRESS POINTER TO HOLDREC.   HOLDREC IS A 3-BYTE
*                  FIELD USED TO RIGHT-JUSTUFY THE RECORD TYPES CODED
*                  ON THE RECTYPE/EXCLUDE PARAMETER PRIOR TO THEIR
*                  BEING CONVERTED TO BINARY AND STORED IN RECTABLE.
*              8:  USED AS A COUNTER TO DETERMINE HOW MANY DIGITS EACH
*                  RECTYPE SUBPARAMETER HAS.
*              9:  STANDARD USAGE AS A POINTER TO THE CONTROL CARD
*                  WORK AREA.
*---------------------------------------------------------------------
         SPACE 2
RECTYPE  LA    9,4(9)             POINT PAST FIRST FOUR TO SHORTEN SCAN
         LA    6,XCBRCTBL         LOAD R6 WITH ADDR OF REC TABLE
         LA    4,CCCHECK          LOAD REG 4 WITH ADDRESS OF CCCHECK
         MVI   XCBIND0,C'1'       ENABLE RECTYPE CHECK
         MVI   XCBIND1,C'0'       DISABLE EXCLUDE CHECK BECAUSE M.E.
REC0     CLI   0(9),C'='          CHECK FOR EQUAL SIGN
         BE    REC1               WHEN FOUND GO TO R1
         LA    9,1(9)             OTHERWISE INCREMENT ADDRESS IN 9 BY 1
         B     REC0                  AND GO TRY IT AGAIN.
REC1     LA    9,1(9)             POINT PAST = SIGN.
         CLC   0(3,9),=C'ALL'     RECTYPE=ALL CODED?
         BNE   *+16+4             IF NOT JUMP AHEAD 20 AND CONTINUE
         MVI   XCBIND9,C'1'       SET FOR RCTYPE=ALL CONDITION...
         MVI   XCBIND0,C'0'       RECTYPE=ALL IMPLIES NO RECTYPE EDIT
         LA    9,4(9)             POINT TO NEXT PARM, IF THERE IS ONE
         B     CCCHECK
         LA    7,HOLDREC          LOAD 7 WITH ADDR OF HOLDREC
         LA    8,0                INITIALIZE 8 TO ZERO.
         CLI   0(9),C'('          IS THIS CHARACTER A (?
         BE    RMANYREC           IF SO, INDICATES MORE THAN 1 REC-TYPE
REC2     MVC   0(1,7),0(9)        MOVE FIRST DIGIT TO HOLDREC
         LA    7,1(7)             POINT TO NEXT SLOT IN HOLDREC
         LA    8,1(8)             COUNT NUMBER OF DIGITS IN HOLDREC
         C     8,=F'3'            MORE THAN 3 CHARACTERS?
         BH    ISX016             IF SO FLAG THE ERROR
         LA    9,1(9)             POINT TO NEXT SLOT IN CARDIN.
         CLI   0(9),C','          IS THIS CHARACTER A COMMA?
         BE    RCOMMA             IF SO GO TO RCOMMA
         CLI   0(9),C')'          IS THIS CHARACTER A )?
         BE    RPAREN             IF SO GO TO RPAREN
         CLI   0(9),C' '          IS THIS CHARACTER A SPACE?
         BE    RCOMMA             IF SO NO MORE PARAMETERS EXIST.
         B     REC2               GO BACK TO MOVETHE OTHER CHARACTERS
RPAREN   MVI   MULTISW,C'0'       TURN OFF MULTISW
RCOMMA   C     8,=F'1'            1 CHARACTER CODED?
         BNE   REC3               IF NOT GO TO R3
         MVC   HOLDREC3,HOLDREC1  RIGHT JUSTIFY THE DIGIT IN HOLDREC
         MVC   HOLDREC(2),=C'00'  ZERO OUT 2 HI-ORDER BYTES.
         B     RPACKIT            GO TO PACK THE NUMBER IN HOLDREC
REC3     C     8,=F'2'            IS THE NUMBER IN HOLDREC 2 DIGITS?
         BNE   RPACKIT            IF NOT, HOLDREC MUST BE FULL, SO
*                                 NO RIGHT JUSTIFICATION IS NEEDED.
         MVC   HOLDREC3,HOLDREC2  RIGHT JUSTIFY UNITS PLACE.
         MVC   HOLDREC2,HOLDREC1  RIGHT JUSTIFY TENS PLACE.
         MVI   HOLDREC,C'0'       ZERO OUT HI-ORDER BYTE.
RPACKIT  PACK  DBLWORD,HOLDREC    PACK HOLDREC INTO DBLWORD SO IT CAN
         OI    DBLWORD+7,X'0F'    OR THE LAST BYTE FOR PROPER SIGN
         UNPK  TESTRECT,DBLWORD+6(2)  UNPACK TO DO NUMERIC TEST
         CLC   TESTRECT,HOLDREC   IF EQUAL, DATA WAS NUMERIC
         BNE   ISX018             IF NOT EQUAL, VALUE IN HOLDREC IS BAD
         CVB   3,DBLWORD             BE CONVERTED TO BINARY INTO REG 3.
         STC   3,0(6)             STORE RIGHTMOST CHARACTER INTO FIRST
*                                 ELEMENT OF RECTABLE .
         LA    6,1(6)             POINT REG 6 TO NEXT BYTE OF RECTABLE
         LA    7,HOLDREC          RESET ADDR IN REG 7 TO BEGINNING.
         LA    8,0                REINITIALIZE 8 TO ZERO.
         AP    ELEMENTS,=P'1'     COUNT # OF ELEMENTS IN RECTABLE.
         CP    ELEMENTS,=P'20'    ARE THERE 20 RECORD TYPES YET?
         BH    ISX010             MORE THAN 20 WILL OVERFLOW RECTABLE
         LA    9,1(9)             POINT PAST COMMA OR PAREN THAT
*                                 SENT YOU INTO THIS ROUTINE.
         BR    4                  BRANCH TO THE ADDRESS IN REG 4.
RMANYREC MVI   MULTISW,C'1'       TURN MULTISW ON.
         LA    9,1(9)             POINT PAST ( TO FIRST RECORD TYPE
         LA    7,HOLDREC          LOAD R7 WITH ADDR OF HOLDREC
         LA    8,0                RESET REG 8 TO 0.
         BAL   4,REC2             GO AND EXECUTE THE CHARACTER MOVE
*                                 AND TRANSLATION PROCESS.
RRETURN  CLI   MULTISW,C'1'       IS MULTISW STILL ON?
         BNE   RSPACE             SEE IF NEXT PARAMETER CODED.
         LA    7,HOLDREC          LOAD R7 WITH ADDR OF HOLDREC
         LA    8,0                RESET REG 8 TO 0.
         BE    REC2               EXECUTE THE CHAR MOVE/TRANS. ROUTINE
RSPACE   CLI   0(9),C' '          IS THIS CHARACTER A SPACE?
         BE    GETRECZ            NO MORE PARMS, THEN.
         CLI   0(9),C','          IS THIS CHARACTER A COMMA?
         BNE   ISX007             IF NOT ERROR MESSAGE TIME.
         LA    9,1(9)             POINT REG 9 PAST COMMA
         B     CCCHECK
         EJECT
************
*          *
* EXCLUDE  *                      EXCLUDE DECODE ROUTINE
*          *
************
         SPACE 2
EXCLUDE  MVI   XCBIND1,C'1'       ENABLE EXCLUDE CHECK
         MVI   XCBIND0,C'0'       DISABLE RECTYPE IF ALREADY ENABLED
         LA    9,4(9)             POINT AHEAD A LITTLE
         LA    6,XCBRCTBL         LOAD R6 WITH ADDR OF REC TABLE
         LA    4,CCCHECK          LOAD REG 4 WITH ADDR OF CCCHECK
         B     REC0               USE RECTYPE CODING TO BUILD TABLE
         SPACE 2
         EJECT
********
*      *
* DATE *                          DATE PARAMETER DECODE ROUTINE
*      *
********
         SPACE 2
DATE     MVI   XCBIND2,C'1'       TURN ON DATE FUNCTION BYTE
         LA    9,4(9)             POINT PAST "DATE" TO NEXT CHARACTER
D0       CLI   0(9),C'='          IS THIS THE EQUAL SIGN?
         BE    D1                 IF SO GO TO D1
         LA    9,1(9)             POINT TO NEXT CHARACTER
         B     D0
         SPACE 1
DATECHK  PACK  WORK5A,0(5,9)      ×
         OI    WORK5A+4,X'0F'     ×    THIS IS A SUBROUTINE USED ONLY
         UNPK  WORK5B,WORK5A      ×    IN THIS SECTION; IT CHECKS A
         CLC   WORK5B,0(9)        ×    GIVEN DATE PARAMETER FOR BEING
         BNE   ISX023             ×    NUMERIC.  IF IT IS NUMERIC,
         MVC   0(4,4),WORK5A+1    ×    THE DATE IS MOVED TO THE XCB.
         LA    9,5(9)             ×    IF NON-NUMERIC, CONTROL IS
         BR    5                  ×    TRANSFERRED TO ISX023.  R9 IS
*                                 ×    ALSO UPDATED TO THE LOCATION
*                                 ×    OF THE NEXT (ASSUMED) DELIMITER
         SPACE 1
D1       LA    9,1(9)             POINT PAST =
         LA    4,XCBDATE1         ADDR OF XCBDATE1 IN R4 FOR DATECHK
         CLI   0(9),C'('          PAREN HERE? (MORETHAN 1 DATE?)
         BE    D3                 YES, GO TO TWO-DATE ROUTINE
         BAL   5,DATECHK          OTHERWISE "DO" FIRST DATE
         MVC   XCBDATE2,XCBDATE1  IF ONLY 1 DATE CODED,SET BOTH EQU RVH
DNEXT    CLI   0(9),C','          IS THIS CHARACTER A COMMA?
         BE    D2                 YES, GO INCREMENT 9
         CLI   0(9),C' '          IF NOT COMMA, IS IT A SPACE?
         BE    GETRECZ            YES, NO MORE PARAMS TO DECODE
         B     ISX007             OTHERWISE INVALID CHARACTER
D2       LA    9,1(9)             POINT TO START OF NEXT PARAMETER
         B     CCCHECK            GO DECODE THIS PARAMETER
D3       LA    9,1(9)             POINT PAST ( THAT SENT YOU HERE
         BAL   5,DATECHK          GO "DO" FIRST DATE
         CLI   0(9),C','          COMMA HERE?
         BE    D4                 YES, SECOND DATE FOLLOWS
         CLI   0(9),C')'          PAREN HERE? (NO MORE DATES)
         BE    D5                 YES GO POINT PAST IT
         B     ISX027             OTHERWISE INVALID CHARACTER
D4       LA    9,1(9)             POINT PAST MIDDLE COMMA
         LA    4,XCBDATE2         ADDR OF XCBDATE2 IN R4 FOR DATECHK
         BAL   5,DATECHK          GO "DO" SECOND DATE
         CLI   0(9),C')'          PAREN HERE?
         BNE   ISX027             IF NOT, ERROR TIME...
D5       LA    9,1(9)             POINT PAST )
         B     DNEXT              GO CHECK FOR DELIMITERS
         EJECT
**********
*        *
* NUMBER *                        NUMBER PARAMETER DECODE ROUTINE
*        *
**********
         SPACE 2
NUMBER   MVI   XCBIND6,C'1'       ENABLE NUMBER FUNCTION
         LA    9,4(9)             ADVANCE POINTER 4 POSITIONS
N0       CLI   0(9),C'='          IS THIS AN EQUAL SIGN?
         BE    N1                 IF SO GO TO N1
         LA    9,1(9)             ELSE POINT TO NEXT POSITION OF PARAM
         B     N0                 ...AND GO BACK AND TRY AGAIN.
N1       LA    9,1(9)             POINT PAST = SIGN.
         LR    8,9                LOAD R8 WITH ADDR IN R9
*                                 R8 WILL BE USED AS A SCAN POINTER
*                                 WITHIN THIS ROUTINE ONLY.
         LA    7,7                LOAD R7 WITH 7
N2       CLI   0(8),C' '          IS THIS CHARACTER A SPACE?
         BE    NMOVE              YES, GO PROCESS PARAMETER
         CLI   0(8),C','          IF NOT SPACE, IS IT A COMMA?
         BE    NMOVE              YES, GO PROCESS PARAMETER
         LA    8,1(8)             OTHERWISE ASSUME VALID CHAR FOR NOW
         B     N2                 GO CHECK NEXT CHARACTER
NMOVE    SR    8,9                SUBTRACT TO FIND LENGTH OF PARAMETER
         C     8,=F'7'            CANNOT EXCEED 7 DIGITS
         BH    ISX031             IF IT DOES, ISSUE ERROR MESSAGE
         SR    7,8                SUBTRACT TO FIND OFFSET (USED LATER)
         BCTR  8,0                SUBTRACT 1 TO USE R8 AS LEN IN PACK
         EX    8,NPACKER          PACK PARAMETER INTO XCBNUMBR
         OI    XCBNUMBR+3,X'0F'   ENSURE VALID SIGN
         UNPK  NUMWORK,XCBNUMBR   UNPACK INTO NUMBER WORK AREA
         LA    3,NUMWORK          GET ADDR OF WORK AREA INTO R3
         AR    3,7                INCREMENT R3 BY OFFSET IN R7
         EX    8,NCHK             IS THIS SAME AS ON THE CARD?
         BNE   ISX035             IF NOT, DATA WAS NOT NUMERIC
         LA    8,1(8)             ADD 1 TO R8, THEN ADD...
         AR    9,8                ...R8 TO R9 TO POINT R9 TO DELIMITER
         B     N3                 GO CHECK DELIMITER
         SPACE 1
NPACKER  PACK  XCBNUMBR,0(0,9)    PLACES VALUE INTO XCBNUMBR
NCHK     CLC   0(0,3),0(9)        PERFORMS THE NUMERIC CHECK
         SPACE 1
N3       CLI   0(9),C' '          IS NEXT CHARACTER A SPACE?
         BE    GETRECZ            IF SO THERE ARE NO MORE PARAMS
         CLI   0(9),C','          IS THIS CHARACTER A COMMA?
         BNE   ISX007             IF NOT, GO TO ERROR MESSAGE
         LA    9,1(9)             POINT PAST COMMA
         B     CCCHECK            GO BACK AND ANALYZE NEXT PARAM
         EJECT
***********
*         *
* SUBCODE *                       SUBCODE PARAMETER DECODE ROUTINE
*         *
***********
         SPACE 2
SUBCODE  MVI   XCBIND4,C'1'       ENABLE SUBCODE CHECK
         MVI   XCBIND5,C'0'       DISABLE JOBNAME IF SUBCODE CODED
         LA    9,4(9)             POINT AHEAD A LITTLE BIT
         LA    6,5                LOAD R6 WITH MAX # OF ENTRIES ALLOWED
S0       CLI   0(9),C'='          EQUAL SIGN HERE?
         BE    S1                 YES, GO START DECODE
         LA    9,1(9)             ELSE POINT TO NEXT CHARACTER...
         B     S0                 ...AND CHECK AGAIN
         SPACE 1
TBLFILL  MVC   0(4,7),0(9)        THIS IS A LOCAL SUBROUTINE USED ONLY
         LA    7,4(7)             BY SUBCODE AND OPRCODE.  IT FILLS
         LA    9,4(9)             XCBSTBL OR XCBOTBL AND INCREMENTS
         BR    5                  CONTROL CARD AND TABLE POINTERS.
         SPACE 1
S1       LA    9,1(9)             POINT PAST =
         LA    7,XCBSTBL          GET ADDR OF SUBCODE TABLE IN R7
         CLI   0(9),C'('          MORE THAN 1 SUBCODE?
         BE    S3                 YES, GO TO S3
         BAL   5,TBLFILL          ELSE GO PLACE ENTRY IN XCBSTBL
         MVI   XCBSCNT,X'01'      SET XCBSCNT TO 1
S2       CLI   0(9),C' '          SPACE HERE (NO MORE PARAMETERS) ?
         BE    GETRECZ            YES, GET READY TO READ DATA
         CLI   0(9),C','          COMMA HERE IF NOT SPACE?
         BNE   ISX007             IF NOT COMMA OR SPACE, INV DELIMITER
         LA    9,1(9)             POINT PAST COMMA
         B     CCCHECK            GO ANALYZE NEXT PARM
S3       LA    9,1(9)             POINT PAST LEFT PAREN
         BAL   5,TBLFILL          GO PLACE ENTRY IN SUBCODE TABLE
         CLI   0(9),C','          NEXT CHARACTER AFTER ENTRY A COMMA?
         BNE   S4                 NO, GO CHECK FOR RIGHT PAREN
         BCT   6,S3               YES, LOOP BACK TO S3 AND CONTINUE
         B     ISX051             ELSE TOO MANY PARAMETERS: ERROR
S4       CLI   0(9),C')'          BETTER BE A RIGHT PAREN HERE...
         BNE   ISX054             IF NOT, ERROR TIME
         LA    7,5                LOAD R7 WITH VALUE OF 5.
         SR    7,6                SUBTRACT QUANTITY IN R6 FROM 5
         STC   7,XCBSCNT          THIS GIVES NUMBER OF SUBCODES PRESENT
         LA    9,1(9)             POINT PAST RIGHT PAREN
         B     S2                 NOW GO CHECK FOR COMMA OR SPACE
         EJECT
**********
*         *
* OPRCODE *                       OPRCODE PARAMETER DECODE ROUTINE
*         *
***********
         SPACE 2
OPRCODE  B     ISX004             GO TO INVALID KEYWORD ERROR MESSAGE
         EJECT
************
*          *
* JOBNAMES *                      JOBNAMES PARAMETER DECODE ROUTINE
*          *
************
         SPACE 2
JOBNAMES MVI   XCBIND5,C'1'       SET INDICATOR FOR JOBNAMES        RVH
         LA    R9,4(,R9)          PAST FIRST 4 CHAR                 RVH
J0       CLI   0(R9),C'='         IS THIS THE = FOR START OF NAME   RVH
         BE    J1                 YES  - GOT START OF JOBNAMES      RVH
         LA    R9,1(,R9)          ELSE - CONTINUE TO LOOK FOR =     RVH
         B     J0                 CONTINUE THE SCAN                 RVH
J1       LA    R9,1(,R9)          PAST "=" CHARACTER                RVH
         MVC   XCBJOBN,0(R9)      SAVE JOBNAME                      RVH
         LA    R9,9(,R9)          PAST THE JOBNAME FIELD AND ","    RVH
         B     CCCHECK            AND RETURN TO CHECK MORE FILEDS   RVH
         EJECT
**********
*        *
*  TIME  *                        TIME PARAMETER DECODE ROUTINE
*        *
**********
         SPACE 2
TIME     EQU   *
         MVI   XCBIND7,C'1'       ENABLE TIME FUNCTION BYTE
         LA    7,TIMEFLD          GET ADDR OF BEGINNING OF TIMEFLD
         LA    8,XCBTIME1         GET ADDR OF START TIME FIELD
T0       CLI   0(9),C'='          SCAN FOR EQUAL SIGN
         BE    T1                 WHEN FOUND GO TO T1
         LA    9,1(9)             OTHERWISE POINT TO NEXT CHARACTER
         B     T0                 GO CHECK FOR = AGAIN.
T1       LA    9,1(9)             POINT PAST EQUAL SIGN
         CLI   0(9),C'('          ( INDICATES START AND END TIMED CODED
         BE    TWOTIMES           IF GO GO TO APPROPRIATE ROUTINE
         BAL   5,TIMESCAN         OTHERWISE JUST DO TIMESCAN ONCE,...
         MVC   XCBTIME2,XCBTIME1  IF ONLY 1 TIME CODED, SET BOTH SAME
         LA    9,1(9)             ...POINT PAST ASSUMED COMMA,...
         B     CCCHECK            ...AND GO TO CHECK FOR MORE PARAMETER
TWOTIMES LA    9,1(9)             POINT PAST LEFT PAREN
         BAL   5,TIMESCAN         GO FIGURE OUT START TIME
         LA    9,1(9)             WHEN YOU GET BACK SKIP OVER COMMA,...
         LA    7,TIMEFLD          RESET POINTER TO TIMEFLD
         BAL   5,TIMESCAN         ...AND GO FIGURE OUT END TIME.
         LA    9,2(9)             WHEN BACK, POINT PAST ASSUMED ),
         B     CCCHECK            ...AND RETURN TO PARAMETER CHECK RNT.
TIMESCAN MVC   0(2,7),0(9)        MOVE HRS FIELD TO FIRST 2 OF TIMEFLD
         LA    9,2(9)             POINT WHERE NEXT DELIMITER SHOULD BE
         CLI   0(9),C','          ×
         BE    TCONVERT           ×
         CLI   0(9),C' '          ×  A COMMA, SPACE OR A ) WILL TERMIN-
         BE    TCONVERT           × ATE THE SCAN, WHILE A PERIOD INDI-
         CLI   0(9),C')'          × CATES  MORE TIME FIELDS.  ANY OTHER
         BE    TCONVERT           × CHARACTER WILL CAUSE AN ERROR.
         CLI   0(9),C'.'          ×
         BNE   ISX040             ×
         LA    7,2(7)             POINT TO NEXT SLOTS IN TIMEFLD
         LA    9,1(9)             POINT PAST PERIOD
         B     TIMESCAN           GO MOVE NEXT TIME FIELD INTO TIMEFLD
TCONVERT PACK  TIMETEST,TIMEFLD   ×
         OI    TIMETEST+4,X'0F'   ×
         UNPK  TIMETST2,TIMETEST  ×  IS TIME PARAMETER NUMERIC?
         CLC   TIMETST2,TIMEFLD   ×
         BNE   ISX041             ×
         PACK  ANSW,TIMEFLD(2)    PACK HOURS INTO ANSW.
         MP    ANSW,=P'360000'    MULT TO CNVRT TO BINARY HUND. OF SEC.
         ZAP   TOTLTIME,ANSW      ACCUMULATE BINARY TIME
         PACK  ANSW,TIMEFLD+2(2)  PACK MINUTES INTO ANSW.
         MP    ANSW,=P'6000'      CONVERT TO BINARY HUND. OF SEC.
         AP    TOTLTIME,ANSW      ACCUMULATE TOTAL HUND. SECS.
         PACK  ANSW,TIMEFLD+4(2)  PACK SECONDS INTO ANSW
         MP    ANSW,=P'100'       CONVERT TO BINARY HUND. OF SEC.
         AP    TOTLTIME,ANSW      ACCUMULATE TOTAL HUND. OF SEC.
         PACK  ANSW,TIMEFLD+6(2)  PACK BINARY HUND.OF SEC. FIELD
         AP    TOTLTIME,ANSW      ACCUMULATE INTO TOTLTIME
         CVB   1,TOTLTIME         CONVERT DECIMAL QUANTITY TO BINARY
         ST    1,0(8)             R8 POINTS TO XCBTIME1; NEXT TIME...
         LA    8,8(8)             ...R8 WILL POINT TO XCBTIME2
         ZAP   TOTLTIME,=P'0'     CLEAR TOTLTIME
         ZAP   ANSW,=P'0'         CLEAR ANSW
         MVC   TIMEFLD,=C'00000000'    REINITIALIZE TIMEFLD
*                                 ROUTINE IS REENTERED.
         BR    5                   GO BACK WHERE YOU CAME FROM
         EJECT
*********
*       *
* FIELD *
*       *
*********
         SPACE 2
FIELD    MVI   XCBIND8,C'1'       ENABLE FIELD CHECK
         LA    9,4(9)             POINT AHEAD A BIT
F0       CLI   0(9),C'='          FOUND EQUAL SIGN YET?
         BE    F1                 YES, GO START DECODE
         LA    9,1(9)             ELSE POINT TO NEXT BYTE...
         B     F0                 ...AND GO CHECK FOR = AGAIN.
F1       CLI   1(9),C'('          MAKE SURE HE CODED LEFT PAREN.
         BNE   ISX062             IF NOT, ERROR MESSAGE TIME
         LA    9,2(9)             POINT PAST =(
         MVC   NUMWORK,=C'0000000'     INITIALIZE NUMWORK TO ZERO
*** THIS CODE DECODES THE DISPLACEMENT PORTION OF FIELD PARAMETER *****
F2       CLI   0(9),C','          FOUND COMMA YET?
         BE    F3                 YES, GO TO F3
         CLI   0(9),X'F0'         IS DIGIT LOWER THAN EBCDIC 0?
         BL    ISX066             YES, ERROR MESSAGE TIME
         MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK CONTENTS LEFT 1
         MVC   NUMWORK+6(1),0(9)  MOVE DIGIT INTO LOW ORDER POSITION
         LA    9,1(9)             POINT TO NEXT BYTE
         B     F2                 GO DO IT AGAIN
F3       PACK  DBLWORD,NUMWORK    PACK DISPLACEMENT INTO DBLWORD
         CVB   8,DBLWORD          CONVERT TO BINARY IN R8
         STH   8,XCBFDISP         STORE DISPLACEMENT IN XCB
*** THIS CODE DECODES THE LENGTH PORTION OF THE PARAMETER  ************
         MVC   NUMWORK,=C'0000000'     RESET NUMWORK TO ZERO
         LA    9,1(9)             POINT AHEAD 1 BYTE PAST COMMA
F4       MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK 1 BYTE LEFT
         CLI   0(9),X'F0'         IS DIGIT NUMERIC?
         BL    ISX069             NO, ERROR MESSAGE TIME
         MVC   NUMWORK+6(1),0(9)  MOVE DIGIT TO NUMWORK LOW ORDER BYTE
         LA    9,1(9)             POINT TO NEXT BYTE
         CLI   0(9),C','          COMMA HERE?
         BNE   F4                 IF NOT ASSUME DIGIT AND GO BACK TO F4
         LA    9,1(9)             POINT PAST COMMA
         PACK  DBLWORD,NUMWORK    PACK LENGTH INTO DBLWORD
         CVB   8,DBLWORD          CONVERT LENGTH TO BINARY
         C     8,=F'21'           IS FIELD LEN > 21?
         BH    ISX064             IF SO, ERRORMESSAGE TIME
         BCTR  8,0                DECREMENT LEN BY 1
         STC   8,XCBFLDLN         STORE LEN OF FIELD - 1 IN XCB
         STC   8,FMOVE+1          STORE RESULT AS LEN FACTOR IN FMOVE
FMOVE    MVC   XCBFIELD,0(9)      MOVE FIELD DATA TO XCB
         LA    8,1(8)             RE-INCREMENT R8 SO RESULT IS ACT. LEN
         AR    9,8                ADD R8 TO R8 TO FIND RIGHT PAREN
         CLI   0(9),C')'          RIGHT PAREN HERE?
         BNE   ISX063             NO, ERROR MESSAGE TIME
         LA    9,1(9)             POINT PAST LEFT PAREN
         CLI   0(9),C','          COMMA HERE?
         BE    F5                 YES, GO TO F5
         CLI   0(9),C' '          SPACE HERE INSTEAD?
         BNE   ISX007             IF NOT, DELIMITER ERROR EXISTS
         B     GETRECZ            ELSE GO SELECT SMF DATA
F5       LA    9,1(9)             POINT TO NEXT PARAMETER LOCATION
         B     CCCHECK            GO CHECK CONTROL CARD
         EJECT                                                      RVH
*********                                                           RVH
*       *                                                           RVH
* MASK  *                                                           RVH
*       *                                                           RVH
*********                                                           RVH
         SPACE 2                                                    RVH
MASK     MVI   XCBIND8A,C'1'      ENABLE MASK CHECK                 RVH
         LA    9,4(9)             POINT AHEAD A BIT                 RVH
M0       CLI   0(9),C'='          FOUND EQUAL SIGN YET?             RVH
         BE    M1                 YES, GO START DECODE              RVH
         LA    9,1(9)             ELSE POINT TO NEXT BYTE...        RVH
         B     M0                 ...AND GO CHECK FOR = AGAIN.      RVH
M1       CLI   1(9),C'('          MAKE SURE HE CODED LEFT PAREN.    RVH
         BNE   ISX062             IF NOT, ERROR MESSAGE TIME        RVH
         LA    9,2(9)             POINT PAST =(                     RVH
         MVC   NUMWORK,=C'0000000'     INITIALIZE NUMWORK TO ZERO   RVH
*** THIS CODE DECODES THE DISPLACEMENT PORTION OF FIELD PARAMETER **RVH
M2       CLI   0(9),C','          FOUND COMMA YET?                  RVH
         BE    M3                 YES, GO TO M3                     RVH
         CLI   0(9),X'F0'         IS DIGIT LOWER THAN EBCDIC 0?     RVH
         BL    ISX066             YES, ERROR MESSAGE TIME           RVH
         MVC   NUMWORK(6),NUMWORK+1    SHIFT NUMWORK CONTENTS LEFT 1RVH
         MVC   NUMWORK+6(1),0(9)  MOVE DIGIT INTO LOW ORDER POSITIONRVH
         LA    9,1(9)             POINT TO NEXT BYTE                RVH
         B     M2                 GO DO IT AGAIN                    RVH
M3       PACK  DBLWORD,NUMWORK    PACK DISPLACEMENT INTO DBLWORD    RVH
         CVB   8,DBLWORD          CONVERT TO BINARY IN R8           RVH
         STH   8,XCBMDISP         STORE DISPLACEMENT IN XCB         RVH
*** THIS CODE DECODES THE OPERATOR DESCRIPTION PARAMETER   *********RVH
         LA    9,1(9)             POINT AHEAD 1 BYTE PAST COMMA     RVH
         CLC   0(2,9),=C'BO'      IS IT "BO"                        RVH
         BNE   M4CK4              NO  - CHECK NEXT TYPE             RVH
         LA    9,3(9)             YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'10'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE.                     RVH
M4CK4    CLC   0(2,9),=C'BM'      IS IT "BM"                        RVH
         BNE   M4CK8              NO  - CHECK NEXT TYPE             RVH
         LA    9,3(9)             YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'40'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE                      RVH
M4CK8    CLC   0(2,9),=C'BZ'      IS IT "BZ"                        RVH
         BNE   M4CKE              NO  - CHECK NEXT TYPE             RVH
         LA    9,3(9)             YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'80'     AND SET BC INSTR. TYPE            RVH
         B     MMOVE              AND CONTINUE                      RVH
M4CKE    CLC   0(3,9),=C'BNO'     IS IT "BNO"                       RVH
         BNE   ISX4               NO  - GIVE AN ERROR               RVH
         LA    9,4(9)             YES - BUMP REG 9                  RVH
         MVI   XCBMOPRN,X'E0'     AND SET BC INSTR. TYPE            RVH
MMOVE    L     R8,TOHEXADD        GET ADDR OF TR TABLE              RVH
         MVC   NUMWORK+4(2),0(R9) PUT CHAR(S) IN WORK AREA          RVH
         TR    NUMWORK+4(2),0(R8) TRANSLATE FOR CONVERT TO HEX      RVH
         PACK  NUMWORK(2),NUMWORK+4(3) CONVERT TO HEX               RVH
         MVC   XCBMASK(1),NUMWORK SAVE HEX CHARACTER                RVH
         LA    R9,2(,R9)          ADD 2 TO R9 TO FIND RIGHT PAREN   RVH
         CLI   0(9),C')'          RIGHT PAREN HERE?                 RVH
         BNE   ISX063             NO, ERROR MESSAGE TIME            RVH
         LA    9,1(9)             POINT PAST LEFT PAREN             RVH
         CLI   0(9),C','          COMMA HERE?                       RVH
         BE    M5                 YES, GO TO M5                     RVH
         CLI   0(9),C' '          SPACE HERE INSTEAD?               RVH
         BNE   ISX007             IF NOT, DELIMITER ERROR EXISTS    RVH
         B     GETRECZ            ELSE GO SELECT SMF DATA           RVH
M5       LA    9,1(9)             POINT TO NEXT PARAMETER LOCATION  RVH
         B     CCCHECK            GO CHECK CONTROL CARD             RVH
         EJECT
**********************************************************************
*                                                                    *
*                           ERROR MESSAGES                           *
*                                                                    *
**********************************************************************
         SPACE 2
ISX001   CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+1(71),=C'ISX001:  INSUFFICIENT OR NON-EXISTENT C
               CONTROL CARD(S).  JOB TERMINATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX002   CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(45),=C'ISX002:  BLANK CONTROL CARD.  JOB TERMI
               INATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX004   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX4               IF NOT GO PRODUCE ERROR MESSAGE
         ABEND 4,DUMP             OTHERWISE ABEND WITH U0004.
ISX4     CLEAR PRINTOUT,X'40'
         MVC   PRINTOUT+1(41),=C'ISX004:  INVALID KEYWORD ON CONTROL CAR
               RD:'
         LA    8,PRINTOUT+44      ×
ISX4L    CLI   0(9),C'='          ×
         BE    ISX4E              ×
         CLI   0(9),C' '          ×
         BE    ISX4E              ×    LOCATE AND MOVE OUT
         MVC   0(1,8),0(9)        ×    ENTIRE INVALID KEYWORD
         LA    9,1(9)             ×
         LA    8,1(8)             ×
         B     ISX4L              ×
ISX4E    B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX007   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX7               IF NOT GO PROCESS ERROR MESSAGE
         ABEND 7,DUMP             ABEND WITH U0007.
ISX7     CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT, SINGLE-SPACE.
         MVC   PRINTOUT+1(62),=C'ISX007:  EXPECTING COMMA OR SPACE.  FOU
               UND " ".  ASSUMED SPACE.'
         MVC   PRINTOUT+44(1),0(9)   MOVE OUT INVALID CHARACTER
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE
         B     GETRECZ            ASSUME NO MORE PARAMETERS; GO TRY
*                                 TO WORK WITH DATA YOU HAVE.
         SPACE 2
ISX009   CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(74),=C'ISX009:  ABEND ON EOJ REQUESTED;  ABNOR
               RMAL TERMINATION FOLLOWS IMMEDIATELY.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE
         BR    5                  GO BACK TO WHERE YOU CAME FROM
         SPACE 2
ISX010   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX10              IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 10,DUMP            ABEND WITH U0010
ISX10    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT,SINGLE-SPACE
         MVC   PRINTOUT+1(78),=C'ISX010:  MORE THAN 20 RECORD TYPES SPEC
               CIFIED FOR RECTYPE/EXCLUDE.  20 ASSUMED.'
         PUT   PRINTDCB,PRINTOUT
         ZAP   ELEMENTS,=P'20'    SET COUNTER TO 20
         LA    1,CARDIN+159       GET ADDR OF END OF CONTROL CARD FIELD
ISX10LP  LA    9,1(9)             INCREMENT REGISTER 9
         CLI   0(9),C')'          HAVE WE REACHED A RIGHT PAREN YET?
         BE    ISX10X             IF SO GO CHECK FOR DELIMITERS
         CR    9,1                HAVE WE REACHED END OF CONTROL CARD?
         BE    ISX014             IF SO, USER LEFT OFF PAREN - GO TELL
         B     ISX10LP            ELSE GO BACK THROUGH LOOP AGAIN.
         LA    9,1(9)              ×  THE START OF THE NEXT PARAMETER.
ISX10X   CLI   0(9),C','          × IF NOT COMMA OR SPACE,
         BNE   *+12               × ISSUE ISX007 ERROR MESSAGE
         LA    9,1(9)             ×
         B     CCCHECK            ×
         CLI   0(9),C' '          ×
         BE    GETRECZ            ×
         B     ISX007             ×
         SPACE 2
ISX014   CLI   ABENDSW,C'E'       IS ABENDSW SET?
         BNE   ISX14              IF NOT GO PROCESS ERRMSG NORMALLY
         ABEND 14,DUMP            ELSE ABEND WITH U0014.
ISX14    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT WITH SINGLE-SPACE
         MVC   PRINTOUT+1(84),=C'ISX014:  MISSING RIGHT PAREN FOR RECTYP
               PE/EXCLUDE.  ANY REMAINING PARAMETERS IGNORED.'
         PUT   PRINTDCB,PRINTOUT
         B     GETRECZ
         SPACE 2
ISX016   CLI   ABENDSW,C'E'       IS ABENDSW ON?
         BNE   ISX16              IF NOT GO WRITE ERROR MESSAGE.
         ABEND 16,DUMP            ELSE ABEND WITH U0016.
ISX16    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT;SINGLE-SPACE.
         MVC   PRINTOUT+1(82),=C'ISX016:  A RECTYPE/EXCLUDE SUBPARAMETER
               R IS GREATER THAN 3 DIGITS.  JOB TERMINATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX018   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX18              IF NOT GO PROCESS ERROR MESSAGE.
         ABEND 18,DUMP            ELSE ABEND WITH U0018.
ISX18    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT AND SINGLE SPACE.
         MVC   PRINTOUT+1(83),=C'ISX018:  A RECTYPE/EXCLUDE SUBPARAMETER
               R CONTAINS NON-NUMERIC DATA.  JOB TERMINATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX023   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX23              IF NOT PROCESS ERROR MESSAGE
         ABEND 23,DUMP            OTHERWISE ABEND WITH U0023
ISX23    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(72),=C'ISX023:  A DATE SUBPARAMETER CONTAINS N
               NON-NUMERIC DATA.  JOB TERMINATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX027   CLI   ABENDSW,C'E'       ABEND SWITCH SET?
         BNE   ISX27              IF NOT GO PROCESS ERROR MESSAGE
         ABEND 27,DUMP            ELSE ABEND WITH U0027
ISX27    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(88),=C'ISX027:  EXPECTED RIGHT PAREN NOT FOUND
               D IN DATE PARAMETER; REMAINING PARAMETERS IGNORED.'
         PUT   PRINTDCB,PRINTOUT
         B     GETRECZ            GO GET SET TO PROCESS SMF DATA
         SPACE 2
ISX031   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX31              IF NOT GO PROCESS ERROR MESSAGE
         ABEND 31,DUMP            OTHERWISE ABEND WITH U0031
ISX31    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(67),=C'ISX031:  THE NUMBER PARAMETER HAS A MAX
               XIMUM LENGTH OF 7 CHARACTERS.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE
         CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT, SINGLE-SPACE
         MVC   PRINTOUT+9(47),=C'DELETING CHARACTERS UNTIL COMMA OR SPAC
               CE FOUND.'
         PUT   PRINTDCB,PRINTOUT
I31L     CLI   0(9),C' '          IS THIS CHARACTER A SPACE?
         BE    GETRECZ            IF SO THERE ARE NO MORE PARAMS.
         CLI   0(9),C','          IS THIS CHARACTER A COMMA?
         BE    I31L1              IF SO GO TO I31L
         LA    9,1(9)             POINT TO NEXT CHARACTER
         B     I31L               GO CHECK THIS BYTE
I31L1    LA    9,1(9)             POINT PAST COMMA
         B     CCCHECK            GO CHECK FOR MORE PARAMS
         SPACE 2
ISX035   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX35              IF NOT GO PROCESS ERROR MESSAGE
         ABEND 35,DUMP            ELSE ABEND WITH U0035
ISX35    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT AND SINGLE-SPACE
         MVC   PRINTOUT+1(59),=C'ISX035:  NON-NUMERIC DATA IN NUMBER PAR
               RAMETER. 100 ASSUMED.'
         PUT   PRINTDCB,PRINTOUT
         ZAP   XCBNUMBR,=P'100'   DEFAULT TO 100 RECORDS FOR NUMBER
         LA    9,1(9)             POINT TO NEXT PARAM
         B     CCCHECK            GO ANALYZE IT.
         SPACE 2
ISX040   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX40              IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 40,DUMP            OTHERWISE ABEND WITH U0040
ISX40    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT AND SINGLE-SPACE
         MVC   PRINTOUT+1(58),=C'ISX040:  INVALID DELIMITER IN TIME FIEL
               LD.  JOB TERMINATED.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX041   CLI   ABENDSW,C'E'       IS ABEND SWITCH SET?
         BNE   ISX41              IF NOT GO PROCESS NORMAL ERROR MSG
         ABEND 41,DUMP            ELSE ABEND WITH U0041
ISX41    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(56),=C'ISX041:  A TIME SUB-PARAMETER CONTAINSX
                NON-NUMERIC DATA.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISX051   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX51              IF NOT, GO PROCESS ERROR MESSAGE
         ABEND 51,DUMP            ELSE ABEND WITH U0051
ISX51    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(50),=C'ISX051:  MORE THAN 5 PARAMETERS CODED F
               FOR SUBCODE.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE
         CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT, SINGLE-SPACE
         MVC   PRINTOUT+10(44),=C'FIRST FIVE USED;  REMAINDER OF CARD IG
               GNORED.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE SECOND LINE
         MVI   XCBSCNT,X'05'      SET COUNT FOR XCBSTBL TO 5
         B     GETRECZ            GET READY TO PROCESS SMF DATA
         SPACE 2
ISX054   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX54              IF NOT, GO PROCESS ERROR MESSAGE
         ABEND 54,DUMP            ELSE ABEND WITH U0054
ISX54    CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(70),=C'ISX054:  EXPECTED RIGHT PAREN OR COMMAX
                NOT FOUND IN SUBCODE PARAMETER.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE
         CLEAR PRINTOUT,X'40'     CLEAR PRINTOUT; SINGLE-SPACE
         MVC   PRINTOUT+1(29),=C'REMAINING PARAMETERS IGNORED.'
         PUT   PRINTDCB,PRINTOUT  WRITE ERROR MESSAGE SECOND LINE
         LA    7,5                PUT A VALUE OF 5 IN R7
         SR    7,6                SUBTRACT CONTENTS OF R6 FROM 5
         STC   7,XCBSCNT          RESULT IS NUMBER OF SUBCODES CODED
         B     GETRECZ            GO PROCESS SMF DATA
         SPACE 2
ISX062   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX62              NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 62,DUMP            ELSE ABEND WITH U0062
ISX62    MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER
         MVC   PRINTOUT+1(44),=C'ISX062:  FIELD PARAMETER MISSING LEFT P
               PAREN.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
ISX063   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX63              NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 63,DUMP            ELSE ABEND WITH U0063
ISX63    MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER
         MVC   PRINTOUT+1(45),=C'ISX063:  FIELD PARAMETER MISSING RIGHTX
                PAREN.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
ISX064   MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER
         MVC   PRINTOUT+1(49),=C'ISX064:  FIELD PARAMETER LENGTH CANNOTX
                EXCEED 21.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
ISX066   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX66              NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 66,DUMP            ELSE ABEND WITH U0066
ISX66    MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER
         MVC   PRINTOUT+1(50),=C'ISX066:  FIELD PARAMETER DISPLACEMENT N
               NOT NUMERIC.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
ISX069   CLI   ABENDSW,C'Y'       ABEND SWITCH SET?
         BNE   ISX69              NO, PROCESS ERROR MESSAGE NORMALLY
         ABEND 69,DUMP            ELSE ABEND WITH U0069
ISX69    MVI   PRINTOUT,X'40'     SET CONTROL CHARACTER
         MVC   PRINTOUT+1(44),=C'ISX069:  FIELD PARAMETER LENGTH NOT NUM
               MERIC.'
         B     ISXERPRT           GO PRINT ERROR MESSAGE
         SPACE 2
ISXERPRT PUT   PRINTDCB,PRINTOUT  WRITE OUT ERROR MESSAGE
         CLOSE (CONTROLC,,SMFTAPE,,OUTDCB,,PRINTDCB)
         LA    15,12              CONTROL CARD ERRORS CAUSE RC=12
         B     LOAD13             GO PREPARE FOR RETURN
         EJECT
************
*          *
* GETRECZ  *
*          *
************
         SPACE 1
GETRECZ  L     3,AWORK            GET ADDRESS OF WORKA FOR PUT-
*                                 AWORK WILL CONTAIN THE ADDRESS OF
*                                 WORKA+4 IF FIXED OUTPUT WAS SPECIFIED
*                                 IN JCL; OTHERWISE AWORK WILL CONTAIN
*                                 THE ADDRESS OF WORKA.
         CLC   XCBINDIC,=12C'0'   MAKE SURE WE HAVE WORK TO DO
         BE    ISX002             IF NOT ISSUE ERROR MESSAGE
         EJECT
**********************************************************************
*                                                                    *
*        THIS SECTION HANDLES ALL RECORD SELECTION, AND ACCUMULATES  *
*        JOB STATISTICS SUCH AS NUMBER OF RECORDS READ, AND THE      *
*        LARGEST AND SMALLEST RECORDS READ.                          *
*                                                                    *
*        THE RECORD SELECTION IS ACCOMPLISHED VIA BRANCH-AND-LINKS   *
*        TO VARIOUS EDIT ROUTINES BASED ON THE SETTING OF THE        *
*        APPROPRIATE FUNCTION BYTES.  IF A RECORD PASSES THE         *
*        EDIT OF A PARTICULAR ROUTINE, CONTROL IS RETURNED TO THE    *
*        NEXT EDIT CHECK AND ULTIMATELY TO THE "CHECKRDW" ROUTINE.   *
*        IF A RECORD FAILS THE PARTICULAR EDIT, CONTROL IS RETURNED  *
*        TO THE GETREC INSTRUCTIONS TO READ IN ANOTHER RECORD        *
*                                                                    *
*                                                                    *
*        EACH DECODE ROUTINE LOADS THE REGISTERS WITH THE VALUES IT  *
*        NEEDS FOR EXECUTION;  THUS THE CONTENTS OF REGISTERS WILL   *
*        CHANGE FROM ROUTINE TO ROUTINE.    THE FOLLOWING CON-       *
*        VENTIONS ARE ADHERED TO, HOWEVER, IN ALL ROUTINES:          *
*                                                                    *
*        5:  COMMUNICATION REGISTER FOR USE WITH BAL COMMANDS.       *
*        6:    ADDRESS POINTER TO TABLE ELEMENTS.                    *
*        8:  LOOP COUNTER.                                           *
*                                                                    *
*        THE FOLLOWING REGISTERS ARE UNUSED IN THIS SECTION:         *
*                                                                    *
*              2     3     4     7     9                             *
*                                                                    *
*                                                                    *
**********************************************************************
         SPACE 2
************
*          *
*  GETREC  *
*          *
************
         SPACE 1
GETREC   GET   SMFTAPE,WORKA      READ SMF RECORD
*---------------------------------------------------------------------
CHKF0    CLI   XCBIND0,C'0'       IS RECTYPE DISABLED?
         BE    CHKF1              IF SO,  TRY NEXT EDIT.
         BAL   5,EDITRECT         OTHERWISE BRANCH TO  RECORD SELECTION
*                                 BY RECORD-TYPE ROUTINE.
*---------------------------------------------------------------------
CHKF1    CLI   XCBIND1,C'0'       IS EXCLUDE DISABLED?
         BE    CHKF2              IF SO GO TRY NEXT EDIT
         BAL   5,EDITEXCL         OTHERWISE GO TO RECORD-SELECTION
*                                 BY EXCLUSION ROUTINE.
*---------------------------------------------------------------------
CHKF2    CLI   XCBIND2,C'0'       IS DATE DISABLED?
         BE    CHKF3              IF SO GO TRY NEXT EDIT.
         BAL   5,EDITDATE         OTHERWISE GO TO RECORD-SELECTION BY
*                                 DATE ROUTINE.
*---------------------------------------------------------------------
CHKF3    CLI   XCBIND3,C'0'       IS OPR-CODE DISABLED?
         BE    CHKF4              IF SO GO CHECK NEXT FUNCTION-BYTE
         B     CHKF4              (WILL BE REPLACED WHEN OPR ACTIVATED)
*---------------------------------------------------------------------
CHKF4    CLI   XCBIND4,C'0'       IS SUB-CODE DISABLED?
         BE    CHKF5              IF SO, GO CHECK NEXT FUNCTION-BYTE.
         BAL   5,EDITSUBC         OTHERWISE GO TO RECORD-SELECTION
*                                 BY SUBCODE ROUTINE
*---------------------------------------------------------------------
CHKF5    CLI   XCBIND5,C'0'       IS JOBNAMES DISABLED?
         BE    CHKF7              IF SO GO CHECK NEXT FUNCTION BYTE.
         BAL   R5,EDITJOBN        OTHERWISE GO TO RECORD SELETEION  RVH
*                                 BY JOBNAME ROUTINE.
*---------------------------------------------------------------------
CHKF7    CLI   XCBIND7,C'0'       IS TIME DISABLED?
         BE    CHKF8              IF SO GO CHECK NEXT FUNCTION BYTE
         BAL   5,EDITTIME         OTHERWISE GO TO RECORD SELECTION BY
*                                 TIME ROUTINE.
*---------------------------------------------------------------------
CHKF8    CLI   XCBIND8,C'0'       IS FIELD PARAMETER DISABLED?
         BE    CHKF9              IF SO GO CHECK NEXT FUNCTION BYTE RVH
         BAL   5,EDITFLD          OTHERWISE GO TO RECORD SELECTION BY
*                                 FIELD ROUTINE.
*---------------------------------------------------------------------
CHKF9    CLI   XCBIND8A,C'0'      IS MASK PARAMETER DISABLED?       RVH
         BE    CHECKRDW           IF SO THERE ARE NO MORE EDITS     RVH
         BAL   5,EDITMASK         OTHERWISE GO TO RECORD SELECTION  RVH
*                                 BY MASK ROUTINE.                  RVH
*-------------------------------------------------------------------RVH
         B     CHECKRDW           NOW GO TO OUTPUT WRITING ROUTINE
         EJECT
**********************************************************************
*              RECORD SELECTION SUBROUTINES.                         *
**********************************************************************
         SPACE 2
EDITRECT LA    6,XCBRCTBL         LOAD 6 WITH ADDR OF RECTABLE
         LA    8,0                ENSURE REG 8 IS 0.
         CVB   8,ELEMENTS         PUT # OF ELEMENTS IN RECTABLE INTO 8.
LOOP1    CLC   SMFRECTP,0(6)      DOES RECORD TYPE MATCH TABLE ELEMENT?
         BE    GOBACK1            IF THEY MATCH, RETURN TO NEXT EDIT.
         LA    6,1(6)             POINT TO NEXT ELEMENT OF RECTABLE.
         BCT   8,LOOP1            LOOP BACK FOR AS MANY TIMES AS THERE
*                                 ARE ELEMENTS IN RECTABLE.
         LA    5,GETREC           IF FALL-THROUGH OCCURS, THE CURRENT
*                                 RECORD IS NOT ONE TO BE SELECTED.
GOBACK1  BR    5                  RETURN TO ADDRESS IN REGISTER 5
         SPACE 3
EDITEXCL LA    6,XCBRCTBL         LOAD 6 WITH ADDR OF RECTABLE
         LA    8,0                ENSURE REG 8 IS 0.
         CVB   8,ELEMENTS         PUT # OF ELEMENTS IN RECTABLE INTO 8.
LOOP2    CLC   SMFRECTP,0(6)      DOES RECORD TYPE MATCH TABLE ELEMENT?
         BNE   *+12               IF NOT JUMP AHEAD 3 INSTRUCTIONS.
         LA    5,GETREC           A MATCH INDICATES THAT THIS RECORD
*                                 IS TO BE EXCLUDED.
         B     GOBACK2
         LA    6,1(6)             POINT TO NEXT ELEMENT IN RECTABLE.
         BCT   8,LOOP2            REPEAT FOR AS MANY TIMES AS THERE ARE
*                                 ELEMENTS IN RECTABLE.
GOBACK2  BR    5                  RETURN TO ADDRESS IN REGISTER 5
         SPACE 3
EDITDATE CLC   SMFDATE,XCBDATE1   CHECK SMFDATE AGAINST LOWER-BOUND
         BL    DZ                 IF SMFDATE LOWER STILL WE DONT WANTIT
         CLC   SMFDATE,XCBDATE2   CHECK SMFDATE AGAINST UPPER-BOUND
         BNH   GOBACK3            IF LOW OR EQUAL, WE WANT IT.
DZ       LA    5,GETREC           OTHERWISE PLAN TO RETURN TO GETREC
GOBACK3  BR    5
         SPACE 3
EDITTIME CLC   SMFTIME(4),XCBTIME1     CHECK SMF TIME IN RECORD
         BL    TOOEARLY           IF LOW, RECORD TIME IS TOO EARLY
         CLC   SMFTIME(4),XCBTIME2     CHECK TIME AGAINST UPPER BOUND
         BNH   GOBACK4            IF NOT HIGH, RECORD FALLS WITHIN LMTS
TOOEARLY LA    5,GETREC           CHANGE RETURN ADDRESS TO READ ANOTHER
GOBACK4  BR    5                  SMF RECORD
         EJECT
         SPACE 3
EDITSUBC LA    6,XCBSTBL          ADDR OF SUBCODE TABLE IN R6
         LA    8,0                SET R8 TO 0
         IC    8,XCBSCNT          INSERT # OF SUBCODES INTO R8
         CLI   SMFRECTP,X'04'     ×    CHECK TO MAKE SURE THAT
         BE    LOOP3              ×    EACH RECORD HAS A VALID SUBCODE
         CLI   SMFRECTP,X'05'     ×    FIELD; THIS WOULD INCLUDE ONLY
         BE    LOOP3              ×    TYPES 4, 5, 14, AND 15 RECORDS.
         CLI   SMFRECTP,X'0E'     ×    IF THE CURRENT RECORD IS NOT
         BE    LOOP3              ×    ONE OF THESE, NO EDITTING OCCURS
         CLI   SMFRECTP,X'0F'     ×    AND CONTROL IS RETURNED TO THE
         BE    LOOP3              ×    CALLING ROUTINE TO PERFORM
         BR    5                  ×    OTHER EDITS.
LOOP3    CLC   SMFJOBNM(4),0(6)   COMPARE FIRST 4 OF JOBNAME WITH
*                                 A SUBCODE TABLE ELEMENT.
         BNE   *+6                IF NOT EQUAL, TRY NEXT ELEMENT
         BR    5                  IF EQUAL, RETURN TO CALLING ROUTINE
         LA    6,4(6)             POINT R6 TO NEXT SUBCODE TABLE ELEMNT
         BCT   8,LOOP3            GO BACK AND CHECK XCBSTBL AGAIN...
         LA    5,GETREC           ...UNLESS ALL HAVE BEEN CHECKED.
         BR    5                  IN WHICH CASE RETURN TO GETREC
         SPACE 2
EDITFLD  LA    6,WORKA            ADDR OF WORKA IN R6
         AH    6,XCBFDISP         FIND LOCATION OF ITEM TO BE CHECKED
         IC    7,XCBFLDLN         INSERT LEN OF FIELD DATA INTO R7
         EX    7,COMPARE          EXECUTE CLC INSTRUCTION FOR FIELD
         BNE   GOBACK6            NOT EQUAL GET ANOTHER SMF RECORD
         BR    5                  ELSE RETURN TO DO MORE EDITS
GOBACK6  LA    5,GETREC           ADDR OF GETREC ROUTINE IN R5
         BR    5                  BRANCH TO ADDR IN R5
COMPARE  CLC   0(0,6),XCBFIELD    COMPARE SMF DATA AGAINST FIELD DATA
         SPACE 2                                                    RVH
EDITMASK LA    6,WORKA            ADDR OF WORKA IN R6               RVH
         AH    6,XCBMDISP         FIND LOC. OF ITME TO BE CHECKED   RVH
         OI    XCBMOPRN,X'05'     PUT IN THE REGISTER FOR EX INSTR  RVH
         IC    7,XCBMASK          INSERT MASK OF DATA INTO R7       RVH
         EX    7,TMINSTR          EXECUTE TM  INSTRUCTION FOR DATA  RVH
         IC    7,XCBMOPRN         GET TYPE OF OPERAND TO USE        RVH
         EX    7,BCINSTR          AND EXECUTE THE BC INSTRUCTION    RVH
GOBACK7  LA    5,GETREC           ADDR OF GETREC ROUTINE IN R5      RVH
         BR    5                  BRANCH TO ADDR IN R5              RVH
TMINSTR  TM    0(6),X'00'         USE TM INSTRUCTION FOR DATA       RVH
BCINSTR  BCR   0,R5               IF TRUE SELECT THIS RECORD        RVH
         SPACE 1                                                    RVH
EDITJOBN CLC   SMFJOBNM(8),XCBJOBN IS THIS THE JOBNAME WE WANT      RVH
         BER   R5                 YES SELECT THE RECORD             RVH
         LA    R5,GETREC          OTHERWISE PLAN TO RETURN TO       RVH
         BR    R5                 GET THE NEX RECORD                RVH
         EJECT
************
*          *
* CHECKRDW *
*          *
************
         SPACE 1
CHECKRDW MVC   FSIZE+2(2),RDW
         L     5,FSIZE            LOAD RDW INTO R5
         CVD   5,DSIZE            CONVERT RDW TO DECIMAL
         MVC   FSIZE,=X'00000000' RESET FSIZE TO BINARY 0 SO THAT
*                                 NEXT TIME THRU FSIZE WILL NOT
*                                 CONTAIN OLD VALUE OF RDW.
         UNPK  FTEST,DSIZE+4(4)   UNPACK RDW
         OI    FTEST+3,X'F0'      OR THE SIGN
         CLC   FTEST,MAXREC       COMPARE CURRENT RDW AGAINST
*                                 LARGEST VALUE SO FAR.
         BNH   CLCLOW             IF LOW OR EQUAL GO CHECK FOR LOW
         MVC   MAXREC,FTEST       OTHERWISE REPLACE MAXREC WITH NEW
*                                 HIGHER VALUE
CLCLOW   CLC   FTEST,MINREC       SEE IF CURRENT RDW LESS THAN LOWEST
*                                 VALUE STORED SO FAR
         BNL   OUTPUT             IF RDW NOT SMALLEST GO TO OUTPUT
         MVC   MINREC,FTEST       OTHERWISE REPLACE MINREC WITH NEW
*                                 LOWEST VALUE.
OUTPUT   PUT   OUTDCB,(3)         WRITE THE SELECTED RECORD
*                                 TO THE OUTPUT DATASET.
         AP    CTR,=P'1'          COUNT EACH RCD AS IT IS WRITTEN
         LA    7,COUNTR           GET ADDR FOR RECORD CNTR         JDM
         SR    9,9                CLEAR REG 9                      JDM
         IC    9,SMFRECTP         PICK UP RECORD TYPE              JDM
         SLL   9,2                MULTIPLY BY 4                    JDM
         L     8,0(9,7)           GET PROPER COUNTER               JDM
         LA    8,1(8)             UP CT BY ONE                     JDM
         ST    8,0(9,7)           SAVE NEW COUNT                   JDM
         CLI   XCBIND6,C'1'       IS NUMBER ENABLED?
         BNE   GETREC             IF NOT GO READ SOME MORE RECORDS.
         CP    XCBNUMBR,CTR       SEE IF NUMBER LIMIT REACHED
         BNE   GETREC             IF NOT GO PROCESS MORE DATA.
*                                 FALL THROUGH OTHERWISE TO EOJ.
         EJECT
**********************************************************************
*                                                                    *
*        END-OF-JOB PROCESSING                                       *
*                                                                    *
**********************************************************************
         SPACE 2
EOJ      UNPK  COUNT,CTR
         OI    COUNT+6,X'F0'      FIX THE SIGN
         CLEAR PRINTOUT,X'F0'     CLEAR PRINTOUT, DOUBLE-SPACE
         MVC   PRINTOUT+1(18),=C'RECORDS SELECTED: '
         MVC   PRINTOUT+19(7),COUNT    MOVE RECORD COUNT OUT
         PUT   PRINTDCB,PRINTOUT
         CLEAR PRINTOUT,X'40'                                      JDM
         LA    7,COUNTR           GET ADDRESS FOR RECORD COUNTS    JDM
MORE     CLC   0(4,7),=F'0'       ZERO ENTRY ?                     JDM
         BE    NEXTONE            YES                              JDM
         L     9,0(7)             PICK UP THE COUNT                JDM
         CVD   9,DBLWORD          MAKE IT DECIMAL                  JDM
         MVC   PRINTOUT+1(L'LINE7),LINE7                           JDM
         MVC   PRINTOUT+12(4),=X'40202120'                         JDM
         ED    PRINTOUT+12(4),RECTYPX                              JDM
         MVC   PRINTOUT+18(6),=X'402020202120'                     JDM
         ED    PRINTOUT+18(6),DBLWORD+5                            JDM
         PUT   PRINTDCB,PRINTOUT                                   JDM
NEXTONE  AP    RECTYPX,=P'1'      SET LOW                          JDM
         CP    RECTYPX,=P'256'    ARE WE DONE                      JDM
         BE    FINI                                                JDM
         LA    7,4(7)             BUMP COUNTER                     JDM
         B     MORE                                                JDM
FINI     CLEAR PRINTOUT,X'60'     CLEAR PRINTOUT, TRIPLE-SPACE     JDM
*                                                                  JDM
         MVC   PRINTOUT+1(21),=C'RECORD SIZES -- MAX: '
         MVC   PRINTOUT+22(4),MAXREC
         MVC   PRINTOUT+26(15),=C'          MIN: '
         MVC   PRINTOUT+41(4),MINREC
         PUT   PRINTDCB,PRINTOUT
         STM   0,15,MYREGS
         CLOSE (CONTROLC,,SMFTAPE,,OUTDCB)
         CLI   ABENDSW,C'J'       DOES ABENDSW=J?
         BNE   LOAD13             IF NOT, TERMINATE NORMALLY
         BAL   5,ISX009           GO PRINT ISX009 MESSAGE
         CLOSE (PRINTDCB)
         ABEND 9,DUMP             ABEND WITH U0009
LOAD13   L     13,SAVE+4          GET REGISTER 13 BACK
         C     15,=F'12'          CONTROL CARD ERROR DETECTED?
         BE    RETURN12           IF SO, TERMINATE WITH RETURN CODE 12
         CP    CTR,=P'0'          NO DATA FOUND?
         BE    RETURN8            IF NOT, TERMINATE WITH RC = 8
         RETURN (14,12),RC=0
RETURN8  RETURN (14,12),RC=8      INDICATES NO DATA FOUND
RETURN12 RETURN (14,12),RC=12
         EJECT
**********************************************************************
*                                                                    *
*              DATA CONTROL BLOCK DEFINITIONS                        *
*                                                                    *
**********************************************************************
         SPACE 2
CONTROLC DCB   DSORG=PS,MACRF=(GM),DDNAME=SYSIN,EODAD=ISX001
         SPACE 1
SMFTAPE  DCB   DSORG=PS,MACRF=(GM),DDNAME=SYSUT1,EODAD=EOJ
         SPACE 1
OUTDCB   DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSUT2,EXLST=JFCBAREA
         SPACE 1
PRINTDCB DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,RECFM=FA,           X
               LRECL=133,BLKSIZE=133
         EJECT
**********************************************************************
*                                                                    *
*              DATA NAME DEFINITIONS AND ALIGNMENT                   *
*                                                                    *
**********************************************************************
         SPACE 2
*******************   DOUBLE WORD ALIGNMENT    ***********************
         SPACE 2
         DS    0D
         DC    C'    DATANAME DEFINITIONS'       DUMP HEADER
SAVE     DS    9D
CARDIN   DC    CL160' '           CONTROL CARD SCAN AREA
DSIZE    DS    CL8
DBLWORD  DS    CL8
ELEMENTS DC    PL8'0'
TIMEFLD  DC    CL8'00000000'
TIMETST2 DS    CL8
TOTLTIME DC    PL8'0'
         SPACE 1
XCB      DS    0D  *****  EXTRACT CONTROL BLOCK  *****
         SPACE 1
XCBINDIC DS    0CL12 IF PARAMETER CODED, BYTE IS 1, ELSE 0
XCBIND0  DC    C'0'               RECTYPE
XCBIND1  DC    C'0'               EXCLUDE
XCBIND2  DC    C'0'               DATE
XCBIND3  DC    C'0'               OPRCODE
XCBIND4  DC    C'0'               SUBCODE
XCBIND5  DC    C'0'               JOBNAME
XCBIND6  DC    C'0'               NUMBER
XCBIND7  DC    C'0'               TIME
XCBIND8  DC    C'0'               FIELD
XCBIND8A DC    C'0'               MASK
XCBIND9  DC    C'0'               "RECTYPE=ALL" FUNCTION REQUESTED
XCBIND10 DC    C'0'               RESERVED FOR LATER FUNCTIONS
XCBIND11 DC    C'0'               RESERVED FOR LATER FUNCTIONS
XCBABSW  DC    C'N'               ABEND ON CONTROL CARD ERROR OR NOT
XCBSRTSW DC    C'N'               SORT SPECIFIED IN PARM FIELD
XCBMSW   DC    C'0'               SHOWS MORE THAN 1 RECTYPE SUBPARAM.
XCBRECNT DC    X'00'              NO. OF RECTYPE/EXCLUDE SUBPARAMERTRS
XCBRCTBL DS    CL20               TABLE OF RECORD TYPES (MAX OF 20)
XCBDT1   DS    0CL8               LOWER-BOUND TIMESTAMP
XCBDATE1 DS    CL4                LOWER-BOUND DATE
XCBTIME1 DS    CL4                LOWER-BOUND DATE
XCBDT2   DS    0CL8               UPPER-BOUND TIMESTAMP
XCBDATE2 DS    CL4                UPPER-BOUND DATE
XCBTIME2 DS    CL4                UPPER-BOUND TIME
XCBNUMBR DS    CL4                VALUE CODED FOR NUMBER PARAMETER
         DC    X'00'              (ALIGNMENT)
XCBSCNT  DC    X'00'              COUNT FOR # OF CODES IN XCBSTBL
XCBSTBL  DS    CL20               TABLE OF SUBCODES (MAX OF 5)
XCBFDISP DC    H'0'               FIELD: DISPLACEMENT FROM BEG. OF REC
XCBFLDLN DC    X'00'              FIELD: LENGTH OF FIELD DATA
XCBFIELD DC    21C' '             FIELD: FIELD DATA
         SPACE 1
XCBMDISP DC    H'0'               MASK: DISP. FROM BEG. OF REC      RVH
XCBMOPRN DC    X'00'              MASK: BC INSTRUCTION OPRN FILED   RVH
XCBMASK  DC    X'00'              MASK: MASK DATA                   RVH
         SPACE 1                                                    RVH
XCBJOBN  DC    CL8' '             JOBNAME: NAME OF JOB TO SELECT    RVH
         SPACE 1
********************   FULLWORD ALIGNMENT   **************************
         SPACE 2
FSIZE    DC    F'0'
         SPACE 1
JFCBAREA DS    0F
HEXCODE  DC    X'07'
AJFCB    DC    AL3(JFCBWORK)
JFCBWORK DS    CL176
         SPACE 1
AWORK    DS    F                  ADDRESS OF BEGINNING OF WORKA
*                                 DEPENDING ON WHETHER RECFM=F OR V.
         DC    C'  MYREGS'
MYREGS   DS    16F
         DC    C'CODETABL'
PARMSAVE DC    F'0'               SAVE LOC FOR ADDR OF PARM INFO.
         SPACE 2
********************     HALF-WORD ALIGNMENT    **********************
         SPACE 2
ANSW     DC    PL6'0'
         SPACE 2
********************    BYTE  ALIGNMENT    ***************************
         SPACE 2
PRINTOUT DS    CL133
CTR      DC    PL4'0'
COUNT    DS    CL7
HOLDREC  DS    0CL3
HOLDREC1 DS    CL1
HOLDREC2 DS    CL1
HOLDREC3 DS    CL1
TESTRECT DS    CL3                USED FOR NUMERIC TEST IN "RECTYPE"
TIMETEST DS    CL5
HOLDER   DC    C'     '
DATE1    DS    CL4
DATE2    DS    CL4
MAXREC   DC    CL4'0000'
MINREC   DC    CL4'9999'
FTEST    DS    CL4
ABENDSW  DC    C'N'               N (DEFAULT) MEANS NO ABEND;
*                                 E MEANS ABEND ON ERROR (CONTROL CARD)
*                                 J MEANS ABEND ON EOJ (AFTER CLOSE)
SORTSW   DC    C'N'               N INDICATES PARM=SORT NOT CODED;
*                                 S INDICATES PARM=SORT WAS CODED
I        DC    PL2'0'             A COUNTER USED WITH RECTYPE/EXCLUDE
MULTISW  DC    C'0'               FOR RECTYPE=(.......)
ALLSTARS DC    54C'*'
ALLSPACE DC    54C' '
LPCNTR   DC    PL1'3'
WORK5A   DS    CL5
WORK5B   DS    CL5
NUMWORK  DS    CL7                WORK AREA FOR NUMBER
MOREPARM DC    C'Y'               ADDITIONAL PARM PARAMETER SWITCH
PARMAREA DC    CL100' '
         LTORG
RECTYPX  DC    PL2'0'                                              JDM
LINE7    DC    C'RECORD TYPE     :          '                      JDM
COUNTR   DC    256F'0'            RECORD TYPE COUNTERS             JDM
*                                                                  JDM
TOHEXADD DC    A(TOHEX)           ADDR OF TR TABLE                 RVH
WORKA    DS    0CL6416            SMF RECORD WORKA AREA
RDW      DS    CL4
         DS    CL1
SMFRECTP DS    CL1
         DS    CL4
SMFDATE  DS    CL4
         DS    CL6402
SMFTIME  EQU   WORKA+6
SMFJOBNM EQU   WORKA+18
SMFOPRCC EQU   WORKA+112
TOHEX    DC    256X'00'           TRANSLATE TABLE FOR HEX CONV.     RVH
         ORG   TOHEX+C'A'                                           RVH
         DC    X'FAFBFCFDFEFF'                                      RVH
         ORG   TOHEX+C'0'                                           RVH
         DC    X'F0F1F2F3F4F5F6F7F8F9'                              RVH
         ORG   ,                                                    RVH
R0       EQU   0                                                    RVH
R1       EQU   1                                                    RVH
R2       EQU   2                                                    RVH
R3       EQU   3                                                    RVH
R4       EQU   4                                                    RVH
R5       EQU   5                                                    RVH
R6       EQU   6                                                    RVH
R7       EQU   7                                                    RVH
R8       EQU   8                                                    RVH
R9       EQU   9                                                    RVH
R10      EQU   10                                                   RVH
R11      EQU   11                                                   RVH
R12      EQU   12                                                   RVH
R13      EQU   13                                                   RVH
R14      EQU   14                                                   RVH
R15      EQU   15                                                   RVH
         END
