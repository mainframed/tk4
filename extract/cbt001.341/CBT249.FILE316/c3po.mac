         TITLE 'C 3 P O '
*$DOC@***************************************************************
*                                                                   *
*   C 3 P O  - IS A DO NOTHING AUTHORIZED STARTED TASK FOR          *
*              R 2 D 2   TO ATTACH BY SRB.  SEE                     *
*              JIM MARSHALL'S BATCH UTILITY PROGRAMS                *
*              MODS TAPE RELEASE 9, DATED 10 / AUGUST / 1983        *
*              FOR INFORMATION ON R 2 D 2 .                         *
*                                                                   *
*   DATE CREATED. DECEMBER 21, 1983.         (KIND OF)              *
*                                                                   *
*********************************************************************
*                                                                   *
*                  D I S C L A I M E R  !!!!                        *
*                                                                   *
*   THE AUTHOR SUPPLIES THIS CODE WITH NO GUARANTEES EXPRESSED OR   *
*   IMPLIED BY THE AUTHOR AND/OR THE U. S. POSTAL SERVICE.          *
*                                                                   *
*********************************************************************
*                                                                   *
*   THIS PROGRAM IS BASICALLY STOLEN CODE FROM "MODREP" PRO-        *
*   GRAM OFF THE SAME TAPE MENTION EARLIER.                         *
*                                                                   *
*   THE PURPOSE OF THIS TASK IS TO SIT AROUND ALL THE TIME          *
*   SO THE MCS CONSOLE EXIT (IEECVXIT) OR MORE COMMONLY KNOWN       *
*   AS R 2 D 2   CAN ATTACH VIA SRB'S TO ANSWER WTOR AND ISSUE      *
*   COMMANDS AFTER SPECIFED MESSAGES ARE ISSUED.  C 3 P O           *
*   WILL ACCEPT TWO COMMANDS.  1) " P C3PO" WILL BRING DOWN         *
*   C 3 P O   AND BASICALLY DISABLE  R 2 D 2.                       *
*   2)  "F C3PO,DIPLAY"  -OR-  "F C3PO,DI" WILL ISSUE A MUTLI-      *
*   LINE WTO BACK TO THE CONSOLE THAT ISSUE THE MODIFY COMMAND.     *
*   THE INFORMATION CONTAIN IN THE WTO IS WHAT R2D2 IS DOING FOR    *
*   YOU.                                                            *
*                                                                   *
*                                                                   *
*   SAMPLE PROC                                                     *
*     //IEFPROC EXEC PGM=C3PO,TIME=1440                             *
*                                                                   *
*                                                                   *
*   MESSAGES ISSUED:                                                *
*                                                                   *
*                                                                   *
*     HELLO MESSAGE:                                                *
*                                                                   *
*        "R 2 D 2   AND  C 3 P O   HERE AT YOUR SERVICE."           *
*        "R 2 D 2   AND I WILL HELP YOU IN RUNNING YOUR CPU."       *
*        "IF YOU WANT TO KNOW WHAT WE ARE DOING FOR YOU"            *
*        "JUST ASK BY ENTERING:  F C3PO,DISPLAY .  IF YOU"          *
*        "NO LONGER DESIRE OUR SEVICES JUST ENTER:"                 *
*        "P C3PO ."                                                 *
*                                                                   *
*        -- THIS IS THE GREETING C3PO GIVES WHEN HE COMES UP.       *
*           THIS MESSAGE IS ISSUE BY MULTIPLE WTOS WITH ROUTE       *
*           CODES OF 2 AND 11.                                      *
*                                                                   *
*     ERROR MESSAGE:                                                *
*                                                                   *
*        "C 3 P O  -  XXXXXXXX  - INVALID COMMAND"                  *
*                                                                   *
*        -- THIS MESSAGE WILL DISPLAY ONLY ON THE CONSOLE           *
*           ISSUSING THE INVALID COMMAND.                           *
*                                                                   *
*     DISPLAY MESSAGE:                                              *
*                                                                   *
*        "C 3 P O  - "R 2 D 2"   FUNCTIONS ARE:"                    *
*        "C 3 P O  - ACTION        TO       MESSAGE"                *
*        "C 3 P O  - NOHOLD                 IEF433D"                *
*        "C 3 P O  - HOHOLD                 IEF434D"                *
*        "C 3 P O  -   U                    IKT012D"                *
*        "C 3 P O  -   U                    IEFTMS0"                *
*        "C 3 P O  -  POST                  IEC804A"                *
*        "C 3 P O  - S TSO                  IST020I"                *
*        "C 3 P O  - V NET,ACT,ID=TP31NCPM  IST020I"                *
*                                                                   *
*        -- THIS MESSAGE WILL DISPLAY ONLY ON THE CONSOLE           *
*           USING THE "DISPLAY" COMMAND.  THE MESSAGES              *
*           REPRESENTS WHAT R2D2 WILL DO WHEN THAT MESSAGE          *
*           IS ISSUSED (IF C3PO IS RUNNING!!!).                     *
*                                                                   *
*     GOOD-BYE MESSAGE:                                             *
*                                                                   *
*        "R 2 D 2   AND  C 3 P O   ARE SHUTTING DOWN"               *
*                                                                   *
*        -- THIS MESSAGE IS ISSUSED WHEN THE STOP COMMAND           *
*           IS ISSUSED.  THE ROUTE CODES ARE 2 AND 11.              *
*                                                                   *
*                      A U T H O R                                  *
*                                                                   *
*                    BRIAN SCOTT                                    *
*                    UNITED STATES POSTAL SERVICE   (UTK)           *
*                    WESTERN AREA SUPPLY CENTER                     *
*                    TOPEKA, KANSAS   66624-9997                    *
*                                                                   *
*********************************************************************
*********************************************************************
*                                                                   *
*                  D I S C L A I M E R  !!!!                        *
*                                                                   *
*   THE AUTHOR SUPPLIES THIS CODE WITH NO GUARANTEES EXPRESSED OR   *
*   IMPLIED BY THE AUTHOR AND/OR THE U. S. POSTAL SERVICE.          *
*                                                                   *
*********************************************************************
*********************************************************************
*                                                                   *
*                                                                   *
*   AUTHOR'S NOTES:                                                 *
*      .  THIS PROGRAM ATTACHES A SUBTASK THAT DOES A               *
*         STIMER FOR 10 SECONDS AND CHECKS TO SEE IF IT IS          *
*         TIME TO QUIT.  IF YOU ARE RUNNING  C 3 P O   ALL          *
*         THE TIME WITH TIME=1440 THEN YOU CAN DELETE THIS          *
*         UNNESSARY WASTE OF CPU TIME (LITTLE THOUGH IT MAYBE) .    *
*      .  THE DISPLAY COMMAND IS HARD CODED IN C3PO SO IF YOU       *
*         CHANGE R2D2 THEN YOU WILL HAVE TO REMEBER TO CHANGE       *
*         C3PO.                                                     *
*                                                                   *
*                                                                   *
*$DOC$***************************************************************
         EJECT
C3PO     START
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE  'C3PO INITIALIZATION SECTION'
         SPACE 2
C3PO     CSECT
         SPACE 3
         B     @PROLOG-*(,R15)
         DC    AL1(7),CL7'C 3 P O'
         DC    CL16' &SYSDATE &SYSTIME '
         SPACE 1
@PROLOG  STM   R14,R12,12(R13)            SAVE CALLER'S REGS
         LR    R12,R15                    ZERO BASE
         USING C3PO,R12                   TELL ASSEMBLER THE NEWS
         LA    R11,SAVE                   SAVE
         ST    R11,8(R13)                 AREA
         ST    R13,4(R11)                 CHAIN
         LR    R13,R11                    CONVENTIONS
         SPACE 1
         L     R1,X'224'                  PSAAOLD, CURRENT ASCB
         L     R15,X'AC'(,R1)             ASCBJBNI
         SPACE 2
         LTR   R15,R15                    IS THIS A BATCH JOB
         BNZ   EXIT                       YES, NOT ALLOWED
         SPACE 1
         L     R15,X'3C'(,R1)             ASCBTSB
         SPACE 2
         LTR   R15,R15                    IS THIS A TSO SESSION
         BZ    ENTER                      NO, THIS IS A STC
         SPACE 2
EXIT     LM    R14,R12,12(R13)
         LA    R15,4
         BR    R14
         EJECT
ENTER    EXTRACT COMM,FIELDS=COMM
         SPACE 2
         L     R1,COMM             ADDRESS OF COMM AREA
         L     R2,4(,R1)           ADDRESS OF CIB
         LA    R3,4(,R1)           ADDRESS OF ADDRESS OF CIB
         SPACE 2
         LTR   R2,R2               IS THERE A CIB
         BZ    NOSTART             NO, NOTHING TO FREE
         SPACE 1
         USING CIB,R2
         SPACE 1
         CLI   CIBVERB,CIBSTART
         BNE   NOSTART
         SPACE 1
         DROP  R2
         SPACE 1
         QEDIT ORIGIN=(3),BLOCK=(2) FREE THE CIB
         SPACE 3
NOSTART  QEDIT ORIGIN=(3),CIBCTR=5
         EJECT
*
*     C 3 P O   SPEAKS TO THE OPERATOR.
*
         SPACE 3
         LA    R15,0               BITS 0-2 MUST BE ZERO FOR
*                                  MULTIPLE-LINE MESSAGE.
         SPACE 2
         WTO   'R 2 D 2   AND  C 3 P O   HERE AT YOUR SERVICE.',       X
               ROUTCDE=(2,11)
         SPACE 2
         WTO   'R 2 D 2   AND I WILL HELP YOU IN RUNNING YOUR CPU.',   X
               ROUTCDE=(2,11)
         SPACE 2
         WTO   'IF YOU WANT TO KNOW WHAT WE ARE DOING FOR YOU ',       X
               ROUTCDE=(2,11)
         SPACE 2
         WTO   'JUST ASK BY ENTERING:  F C3PO,DISPLAY .  IF YOU ',     X
               ROUTCDE=(2,11)
         SPACE 2
         WTO   'NO LONGER DESIRE OUR SERVICES JUST ENTER:  ',          X
               ROUTCDE=(2,11)
         SPACE 2
         WTO   'P C3PO .',ROUTCDE=(2,11)
         EJECT
         TITLE  'C 3 P O   COMMUNICATIONS SECTION'
         SPACE 3
WTOR     L     R1,COMM                    POINT TO COMM AREA
         L     R1,0(,R1)                  POINT TO COMM ECB
         SPACE 1
         WAIT  ECB=(1)                    WAIT FOR MODIFY/STOP
         SPACE 2
         L     R1,COMM                    POINT TO COMM AREA
         L     R2,4(,R1)                  POINT TO CIB
         SPACE 1
         USING CIB,R2
         SPACE 1
         LA    R3,4(,R1)                  POINT TO CIB POINTER
         SPACE 1
         CLI   CIBVERB,CIBSTOP            STOP
         BE    BYEBYE                     YES, SAME AS DELETE
         SPACE 2
         MVC   REPLY(8),=CL8' '
         LH    R1,CIBDATLN                GET LENGTH OF OPERAND
         SPACE 1
         CH    R1,=H'8'                   TOO LONG
         BNH   NTOOLNG                    NO
         SPACE 2
         LH    R1,=H'8'                   YES, TRUNCATE IT
         SPACE 1
NTOOLNG  LTR   R1,R1                      NULL
         BZ    FREECIB                    YES, SAME AS BLANKS
         SPACE 2
         BCTR  R1,0                       LENGTH MINUS 1 FOR EX
         SPACE 1
         EX    R1,ORUPPER                 MOVE OPERAND TO REPLY
         SPACE 1
FREECIB  QEDIT ORIGIN=(3),BLOCK=(2)
         SPACE 2
         CLC   =C'DISPLAY',REPLY          DISPLAY STATUS?
         BE    DISPLAY                    YES.
         SPACE 2
         CLC   =C'DI ',REPLY              DISPLAY STATUS?
         BE    DISPLAY                    YES.
         SPACE 1
BADCMD   MVC   WTOERROR+16(8),REPLY
         SPACE 2
         LA    R0,0                CLEAR REG ZERO
         IC    R0,CIBCONID         GET CONSOLE ID FOR MESSAGE
         SPACE 2
         WTO   MF=(E,WTOERROR)
         SPACE 1
         B     WTOR
         EJECT
DISPLAY  LA    R0,0                CLEAR REG ZERO
         IC    R0,CIBCONID         GET CONSOLE ID FOR MESSAGE
         SPACE 2
         WTO   ('C 3 P O  - "R 2 D 2"   FUNCTIONS ARE:',D),            X
               ('C 3 P O  - ACTION        TO       MESSAGE',D),        X
               ('C 3 P O  - NOHOLD                 IEF433D',D),        X
               ('C 3 P O  - NOHOLD                 IEF434D',D),        X
               ('C 3 P O  -   U                    IKT012D',D),        X
               ('C 3 P O  -   U                    IEFTMS0',D),        X
               ('C 3 P O  -  POST                  IEC804A',D),        X
               ('C 3 P O  - S TSO                  IST020I',D),        X
               ('C 3 P O  - V NET,ACT,ID=TP31NCPM  IST020I',DE),       X
               DESC=(5,8,9),MCSFLAG=(REG0,RESP)
         SPACE 2
         B     WTOR
         EJECT
BYEBYE   WTO   'R 2 D 2   AND  C 3 P O   ARE SHUTTING DOWN',           X
               ROUTCDE=(2,11)
         SPACE 2
         SLR   R15,R15               WAKE UP!!!
         SPACE 1
         SVC   3                     THE NIGHTMARE IS OVER
         SPACE 5
ORUPPER  OC    REPLY(0),CIBDATA        MOVE OPERAND TO REPLY
         TITLE  'C 3 P O    CONSTANTS/DSECTS SECTION'
         SPACE 1
*********************************************************************
*      CONSTANTS, VARIABLES, PATCH AREA, ETC.                       *
*********************************************************************
         SPACE 3
SAVE     DS    18F                     SAVE
COMM     DS    F
REPLY    DC   CL8' '                   WTOR REPLY
WTOERROR WTO   'C 3 P O  -  XXXXXXXX  - INVALID COMMAND',              X
               MCSFLAG=(REG0),MF=L
         LTORG
         EJECT
CIB      DSECT
*        IEZCIB
CIBNEXT  DS    A -      ADDRESS OF NEXT CIB IN QUEUE (ZERO FOR LAST)
CIBVERB  DS    C -      COMMAND VERB CODE
CIBSTART EQU   X'04' -  COMMAND CODE FOR START
CIBMODFY EQU   X'44' -  COMMAND CODE FOR MODIFY
CIBSTOP  EQU   X'40' -  COMMAND CODE FOR STOP
CIBMOUNT EQU   X'0C' -  COMMAND CODE FOR MOUNT
CIBLEN   DS    FL1 -    LENGTH IN DOUBLEWORDS OF CIB INCLUDING CIBDATA
         DS    XL4 -    RESERVED FOR CSCB COMPATIBILITY
CIBASID  DS    0H -     ADDRESS SPACE ID (OS/VS2)                MDC001
CIBTJID  DS    CL2 -    TSO TERMINAL JOB IDENTIFIER (OS/VS1)     MDC001
CIBCONID DS    C -      IDENTIFIER OF CONSOLE ISSUING COMMAND
         DS    X -      RESERVED
CIBDATLN DS    H -      LENGTH IN BYTES OF DATA IN CIBDATA
CIBDATA  DS    CL8 -    DATA FROM COMMAND OPERAND
*        (LENGTH OF CIBDATA IS A MULTIPLE OF EIGHT BYTES
*        DEPENDING ON THE VALUE CONTAINED IN CIBLEN)
*              START -  FOURTH POSITIONAL PARAMETER (PARMVALUE)
*              MODIFY - RESIDUAL OPERAND IMAGE FOLLOWING COMMA
*                       TERMINATING FIRST POSITIONAL PARAMETER
*              STOP -   NONE (CIB GENERATED ONLY TO GIVE CONSOLE ID)
         SPACE 3
         END
