 TITLE 'SQBLNK SUBROUTINE -- COMPRESS OUT BLANKS FROM AREA'
*$DOC@*
* CALLING SEQUENCE: CALL SQBLNK,(AREA,LENGTH) WHERE LENGTH IS
* FULLWORD OF LE 256.  ALL MULTIPLE BLANKS WILL BE REDUCED TO
* SINGLE BLANKS, FOR DATA COMPACTION.  USED BY P38R90 PROGRAM ET AL.
*$DOC$*
SQBLNK   CSECT
         SAVE  (14,12),,SQBLNK_&SYSDATE
         LR    R12,R15
         USING SQBLNK,R12
         LR    R9,R1               SAVE PARM@
         L     R10,4(R9)           LENGTH OF AREA (@ FIRST,THEN VALUE)
         L     R10,0(R10)
         LA    R0,(AREA-SIZE)+1(R10) PLUS ONE BYTE PLUS WORK
         LR    R2,R0               SAVE SIZE
         GETMAIN R,LV=(0)          GET CORE FOR WORKAREA
         BCTR  R10,0               COMPUTE INTERNAL LENGTH CODE
         L     R7,0(R9)            @ OF AREA
         LR    R11,R1
         USING DSECT,R11
         ST    R2,SIZE             SAVE SIZE OF AREA
         LA    R2,AREA
         LA    R3,1(R10)           SIZE OF AREA
         XR    R4,R4
         XR    R5,R5
         ICM   R5,8,=C' '
         MVCL  R2,R4               BLANK AREA
         MVC   AREA(*-*),0(R7)     MOVE DATA TO AREA
         EX    R10,*-6             NOTE LIMIT ON SIZE OF AREA
         LA    R3,AREA(R10)        LAST CHAR TO SCAN
         MVI   1(R3),C' '          INSERT PAD CHARACTER TO RIGHT
         LA    R2,1
         LA    R1,AREA
LOOP1    CLI   0(R1),C' '          SCAN FOR AT LEAST 1 NONBLANK
         BNE   ELOOP1
         BXLE  R1,R2,LOOP1
         B     RC4                 NO NONBLANK CHARS
ELOOP1   LA    R1,AREA
         LA    R3,AREA-3(R10)
LOOP2    CLC   0(2,R1),=CL2' '
         BE    ELOOP2
         BXLE  R1,R2,LOOP2
         B     RC8                 NO COMPRESSION POSSIBLE
*              BACK-SCAN FROM END TO FIRST NONBLANK
ELOOP2   ST    R1,@FIRST
         LA    R3,AREA(R10)        LAST CHAR
LOOP3    CLI   0(R3),C' '
         BNE   ELOOP3
         BCT   R3,LOOP3            WE KNOW AT LEAST ONE HERE
ELOOP3   ST    R3,@END
         IF (CLC,@FIRST,GE,@END)
            B     RC0
         ENDIF
LOOP4    L     R2,@FIRST           LOC OF DBLE BLANK
         LA    R2,1(R2)            POINT TO 2ND BLANK
         L     R3,@END             LOC LAST NONBLANK
         SR    R3,R2               COMPUTE LENGTH OF STRING
         MVC   0(*-*,R2),1(R2)     MOVE DATA LEFT 1 BYTE
         EX    R3,*-6
         L     R3,@END
         LA    R2,1
         L     R1,@FIRST
LOOP5    CLC   0(2,R1),=CL2' '
         BE    ELOOP5
         BXLE  R1,R2,LOOP5
         B     RC0                 COMPLETED COMPACTION
ELOOP5   ST    R1,@FIRST
         LA    R2,AREA(R10)
LOOP6    CLI   0(R3),C' '
         BNE   ELOOP6
         BCT   R3,LOOP6
ELOOP6   ST    R3,@END
         IF (CLC,@FIRST,GE,@END)
            B     RC0
         ENDIF
         B     LOOP4
RC0      L     R7,0(R9)            @ AREA
         MVC   0(*-*,R7),AREA
         EX    R10,*-6
         LA    R6,0
RETX     LR    R1,R11
         L     R0,SIZE
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R6
         RETURN (14,12),RC=(15)
RC4      LA    R6,4
         B     RETX
RC8      LA    R6,8
         B     RETX
         EQUREG
DSECT    DSECT
SIZE     DS    F
@FIRST   DS    A
@END     DS    A
AREA     DS    X
         END
