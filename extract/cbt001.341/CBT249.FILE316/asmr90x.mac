         TITLE '   A S M R 9 0      '
*$DOC@*****************************************************************
*                                                                     *
*  ASMR90/ASMR90B - INVOKE ASSEMBLER H, ROTATE THE LISTING            *
*                                                                     *
***********************************************************************
*
*  WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*  INSTALLATION. AIR FORCE DATA SERVICES CENTER, THE PENTAGON.
*  DATE WRITTEN. JUNE 5 1980.
*  DATE UPDATED. SEPTEMBER 30 1980.
*  ATTRIBUTES. RE-ENTRANT.
*
*  THIS PROGRAM INVOKES THE ASSEMBLER (H) AND THEN
*  INVOKES A PROGRAM WHICH MODIFIES THE LISTING FOR A
*  90-DEGREE ROTATION CHARACTER SET, 12 LINES PER INCH,
*  ON A 3800 PRINTER.
*
*  THE ASSEMBLER PRINT OUTPUT IS DIRECTED TO AN INTERMEDIATE
*  SYSUT9 FILE, WHERE IT IS READ BY THE ROTATION PROGRAM,
*  ROTATED, AND FINALLY WRITTEN TO DDNAME 'ROTATE'.
*
*  THERE ARE 2 ENTRY POINTS,
*  ASMR90 - FOR THE P38R90 PROGRAM (COMPRESSED PAGES)
*  ASMR90B - FOR THE P38R90B PROGRAM (2 PAGES PER SHEET)
*
*  DDNAMES REQUIRED -
*     SAME DDNAMES AS ASSEMBLER H REQUIRES, EXCEPT SYSPRINT.
*        NAMELY - SYSIN, SYSLIB, SYSUT1,
*        AND OPTIONALLY SYSLIN, SYSPUNCH, SYSTERM.
*     PLUS 2 NEW DDNAMES -
*     SYSUT9 - WORKSPACE WHERE ASSEMBLER WILL WRITE SYSPRINT
*              AND THE ROTATE PROGRAM WILL READ IT BACK IN.
*     ROTATE - ROTATED PRINT OUTPUT, USUALLY ALLOCATED TO SYSOUT
*              FOR A 3800 PRINTER WITH SPECIAL FCB AND CHARS.
*     IF ROTATE IS DUMMY OR ABSENT, SYSPRINT IS REQUIRED.
*
*  PARM FIELD -
*     WILL BE PASSED TO THE ASSEMBLER UNCHANGED, UNLESS THE
*     FIRST KEYWORD IS ANY OF THE FOLLOWING -
*       SQUISH,  NOSQUISH,  SQUISH(YES),  SQUISH(NO),  SQUISH() .
*     IF THE FIRST KEYWORD IS ANY OF THE ABOVE, FOLLOWED BY
*     A COMMA, THEN THE TEXT FOLLOWING THE COMMA WILL BE
*     PASSED ON TO THE ASSEMBLER.  THE COMMA MAY BE OMITTED
*     ONLY IF IT IS THE LAST CHARACTER IN THE PARM FIELD. IF
*     ANYTHING OTHER THAN A COMMA FOLLOWS THE KEYWORD, THE
*     KEYWORD WILL NOT BE RECOGNIZED AND WILL BE PASSED ON
*     TO THE ASSEMBLER UNCHANGED.
*
*  EXTERNAL CALLS -
*     INVOKES 'IEV90' (IBM PROGRAM PRODUCT, ASSEMBLER H),
*     AND 'P38R90' OR 'P38R90B' (PRINT ROTATE PROGRAM).
*
*  CHANGES -
*     11JUN80 - PARM FIELD SQUISH KEYWORD SUPPORT ADDED.
*     30SEP80 - TEST FOR ROTATE DD ABSENT OR DUMMY, AND
*               COPY TO SYSPRINT IF SO.
*
*$DOC$*****************************************************************
         EJECT
ASMR90   START
         USING *,R15
         B     @PROLOG
         DC    AL1(11),CL11'ASMR90'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
         SPACE
         ENTRY ASMR90B
         USING *,R15
ASMR90B  STM   14,12,12(R13)       SAVE REGISTERS
         LA    R3,1                INDICATE ALTERNATE ENTRY
         SH    R15,@ENTRYB         ADJUST BASE ADDRESS
         USING ASMR90,R15          ADJUST ASSEMBLER TO NEW BASE
         B     @PROLOGE            JUMP IN
@ENTRYB  DC    AL2(ASMR90B-ASMR90) OFFSET FROM TOP
         SPACE
@PROLOG  STM   14,12,12(R13)       SAVE REGISTERS
         SLR   R3,R3               INDICATE PRIMARY ENTRY
@PROLOGE LR    R12,R15             LOAD BASE REGISTER
         USING ASMR90,R12
         DROP  R15
         LR    R2,R1               PARM POINTER
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R9,R1               INITIALIZE WORKAREA POINTER
         USING @DATA,R9
         SPACE 1
         XC    000(256,R1),000(R1)
         XC    256(@DATAL-256,R1),256(R1)
         SPACE 1
         ST    R13,4(,R9)          CHAIN SAVEAREAS
         ST    R9,8(,R13)          CHAIN SAVEAREAS
         LR    R13,R9              UPDATE SAVEAREA POINTER
         EJECT
************************************************************
*                                                          *
*        DETERMINE WHICH PROGRAM TO INVOKE                 *
*                                                          *
************************************************************
         SPACE
         MVC   ASMNAME,=CL8'IEV90'
         MVC   PGMNAME,=CL8'P38R90'
         LTR   R3,R3               PRIMARY ENTRY
         BZ    *+8                 YES, SKIP NEXT INSTR
         MVI   PGMNAME+6,SQN       NO, DO NOT SQUISH
         SPACE
         L     R2,0(,R2)           GET PARM ADDRESS
         LA    R2,0(,R2)           ZERO HIGH ORDER BYTE
         LH    R3,0(,R2)           GET PARM LENGTH
         LA    R4,2(,R2)           POINT TO PARM TEXT
         LTR   R3,R3               IS PARM LENGTH ZERO
         BZ    SAMEPARM            YES, BRANCH
         LA    R5,6                LOOK AT 6-CHAR STRINGS
         CR    R3,R5               IS TEXT LONG ENOUGH
         BL    SAMEPARM            NO, BRANCH
         CLC   0(6,R4),SQUICON+2   SQUISH
         BE    SQUISH              YES, BRANCH
         LA    R5,8                LOOK AT 8-CHAR STRING
         CR    R3,R5               IS TEXT LONG ENOUGH
         BL    SAMEPARM            NO, BRANCH
         CLC   0(8,R4),SQUICON     NOSQUISH
         BE    SQNISH              YES, BRANCH
         B     SAMEPARM
         SPACE
SQUISH   SR    R3,R5               GET LENGTH OF REM TEXT
         BZ    SQUSET              BRANCH IF NOTHING FOLLOWS
         AR    R4,R5               POINT TO REST OF TEXT
         CLI   0(R4),C','          COMMA
         BNE   SQSUB               NO, BRANCH
         LA    R4,1(,R4)           POINT PAST COMMA
         BCTR  R3,0                REDUCE LENGTH BY 1
SQUSET   MVI   PGMNAME+6,SQU       YES, VALID KEYWORD
         B     COPYPARM
         SPACE
SQNISH   SR    R3,R5               GET LENGTH OF REM TEXT
         BZ    SQNSET              BRANCH IF NOTHING FOLLOWS
         AR    R4,R5               POINT TO REST OF TEXT
         CLI   0(R4),C','          COMMA
         BNE   SAMEPARM            NO, PASS ENTIRE PARM
         LA    R4,1(,R4)           POINT PAST COMMA
         BCTR  R3,0                REDUCE LENGTH BY 1
SQNSET   MVI   PGMNAME+6,SQN
         B     COPYPARM
         SPACE
SQSUB    CLI   0(R4),C'('          OPEN PAREN, IS SUBFIELD PRESENT
         BNE   SAMEPARM            NO, BRANCH
         LA    R5,2                LOOK AT 2-CHAR STRING
         CR    R3,R5               IS TEXT LONG ENOUGH
         BL    SAMEPARM            NO, BRANCH
         CLC   0(2,R4),=C'()'      IS SUBFIELD NULL
         BE    SQSUBN              YES, SAME AS (NO)
         LA    R5,4                LOOK AT 4-CHAR STRING
         CR    R3,R5               IS TEXT LONG ENOUGH
         BL    SAMEPARM            NO, BRANCH
         CLC   0(4,R4),=C'(NO)'    IS SUBFIELD NO
         BE    SQSUBN              YES, BRANCH
         LA    R5,5                LOOK AT 5-CHAR STRING
         CR    R3,R5               IS TEXT LONG ENOUGH
         BL    SAMEPARM            NO, BRANCH
         CLC   0(5,R4),=C'(YES)'   IS SUBFIELD YES
         BNE   SAMEPARM            NO, BRANCH
SQSUBY   SR    R3,R5               GET LENGTH OF REM TEXT
         BZ    SQUSET              BRANCH IF NOTHING ELSE
         AR    R4,R5               POINT TO REM TEXT
         CLI   0(R4),C','          COMMA
         BNE   SAMEPARM
         LA    R4,1(,R4)           POINT PAST COMMA
         BCTR  R3,0                REDUCE LENGTH BY 1
         B     SQUSET
SQSUBN   SR    R3,R5               GET LENGTH OF REM TEXT
         BZ    SQNSET              BRANCH IF NOTHING ELSE
         AR    R4,R5               POINT TO REM TEXT
         CLI   0(R4),C','          COMMA
         BNE   SAMEPARM
         LA    R4,1(,R4)           POINT PAST COMMA
         BCTR  R3,0                REDUCE LENGTH BY 1
         B     SQNSET
COPYPARM LA    R2,PARM1LEN         POINT TO NEW PARM AREA
         STH   R3,0(,R2)           SET LENGTH IN NEW PARM AREA
         LTR   R3,R3               IS LENGTH ZERO
         BZ    NULLPARM            YES, BYPASS MOVE
         BCTR  R3,0                LENGTH MINUS 1 FOR EX
         B     *+10                BRANCH AROUND EXECUTED MVC
         MVC   2(0,R2),0(R4)       (EXECUTED)
         EX    R3,*-6              MOVE REST OF PARM
NULLPARM EQU   *
         SPACE
************************************************************
*                                                          *
*        SET UP PARM LISTS                                 *
*                                                          *
************************************************************
         SPACE
SAMEPARM ST    R2,PARM1PTR         STORE ADDRESS OF PARM
         LA    R0,DDNL1            POINT TO DDNAME LIST
         ST    R0,PARM1PTR+4       STORE ADDRESS OF DD LIST
         OI    PARM1PTR+4,X'80'    INDICATE END OF ADDRESS LIST
         LA    R0,PARM2LEN         POINT TO ROTATE PARM
         ST    R0,PARM2PTR         STORE ADDRESS OF PARM
         OI    PARM2PTR+0,X'80'    INDICATE END OF ADDRESS LIST
         EJECT
************************************************************
*                                                          *
*        DDNAME LIST                                       *
*                                                          *
************************************************************
         SPACE
*                      ASSEMBLER H
*                      --------
*                   1  SYSLIN
*                   2  .
*                   3  .
*                   4  SYSLIB
*                   5  SYSIN
*                   6  SYSPRINT
*                   7  SYSPUNCH
*                   8  SYSUT1
*                   9  SYSTERM
         SPACE
************************************************************
*                                                          *
*        CHANGE ASSEMBLER DDNAMES                          *
*                                                          *
************************************************************
         SPACE
         MVC   DDNL1(2),=AL2(09*8) LENGTH OF DDNAMES
         LA    R1,DDNL1+2
         XC    0(09*8,R1),0(R1)
         MVC   (6-1)*8(8,R1),=CL8'SYSUT9' SYSPRINT NOW SYSUT9
         SPACE
************************************************************
*                                                          *
*        CHANGE ROTATE DDNAMES                             *
*                                                          *
************************************************************
         SPACE
         LA    R0,16               LENGTH OF PARM
         STH   R0,PARM2LEN
         MVC   PARM2+0(8),=CL8'SYSUT9' INPUT DDNAME
         MVC   PARM2+8(8),ROTATEDD     OUTPUT DDNAME
         EJECT
************************************************************
*                                                          *
*        CALL THE ASSEMBLER                                *
*                                                          *
************************************************************
         SPACE
         LA    R1,PARM1PTR
         SPACE
         LINK  EPLOC=ASMNAME,SF=(E,LINKAREA)
         SPACE
         LR    R2,R15              SAVE ASSEMBLER RETURN CODE
         SPACE
************************************************************
*                                                          *
*        IF ROTATE IS DUMMY, GO COPY SYSUT9 TO SYSPRINT    *
*                                                          *
************************************************************
         SPACE
         DEVTYPE ROTATEDD,DEVDATA
         LTR   R15,R15             IS ROTATE DD PRESENT
         BNZ   COPY                NO, BRANCH TO COPY
         CLI   DEVDATA,0           IS ROTATE DD DUMMY
         BNE   ROTATE              YES, BRANCH TO COPY
COPY     EQU   *
         LA    R3,UT9W
         LA    R4,PRTW
         MVC   UT9W(UT9L),UT9
         MVC   PRTW(PRTL),PRT
         OPEN  (UT9W,INPUT,PRTW,OUTPUT)
COPYREAD EQU   *
         GET   (R3)
         LR    R5,R1
         PUT   (R4),R5
         B     COPYREAD
COPYEOD  EQU   *
         CLOSE (PRTW,UT9W)
         B     EXIT
PRTEXIT  CLI   RECFM(R4),0             IS RECFM SPECIFIED
         BNE   *+10                    YES, SKIP NEXT INST
         MVC   RECFM(1,R4),RECFM(R3)   NO, COPY FROM UT9
         NC    LRECL(2,R4),LRECL(R4)   IS LRECL SPECIFIED
         BNZ   *+10
         MVC   LRECL(2,R4),LRECL(R3)   NO, COPY FROM UT9
         NC    BLKSI(2,R4),BLKSI(R4)   IS BLKSI SPECIFIED
         BNZ   *+10
         MVC   BLKSI(2,R4),BLKSI(R3)   NO, COPY FROM UT9
         BR    R14
         SPACE
************************************************************
*                                                          *
*        CALL THE ROTATER                                  *
*                                                          *
************************************************************
         SPACE
ROTATE   LA    R1,PARM2PTR
         SPACE
         LINK  EPLOC=PGMNAME,SF=(E,LINKAREA)
         SPACE
EXIT     LR    R15,R2              RESTORE ASSEMBLER RETURN CODE
         LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         ST    R15,16(,R13)        STORE RETURN CODE FOR LM
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(R13)       LOAD RETURN ADDRESS AND RC
         BR    14                  RETURN
         EJECT
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
ROTATEDD DC    CL8'ROTATE'
SQUICON  DC    C'NOSQUISH'
SQUACON  DC    C'NOSQUASH'
SQU      EQU   X'40'         SQUISH PGM NAME P38R90
SQN      EQU   C'B'        NOSQUISH PGM NAME P38R90B
         PRINT NOGEN
UT9      DCB   DDNAME=SYSUT9,DSORG=PS,MACRF=(GL),EODAD=COPYEOD
UT9L     EQU   *-UT9
PRT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),EXLST=EXLST
PRTL     EQU   *-PRT
         PRINT GEN
EXLST    DC    0F'0',X'85',AL3(PRTEXIT)
RECFM    EQU   36
LRECL    EQU   82
BLKSI    EQU   62
         DC    0D'0'               END OF CSECT
         SPACE
************************************************************
*                                                          *
*        MAP OF GETMAINED WORKAREA                         *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
LINKAREA DS    2F
ASMNAME  DS    CL8
PGMNAME  DS    CL8
DEVDATA  DS    4F
PARM1PTR DS    2F
PARM1LEN DS    H
PARM1    DS    CL100               COPY OF PARM TEXT
         DS    0D
         DS    3H
DDNL1    DS    H
         DS    09CL8               DDNAMES
PARM2PTR DS    2F
PARM2LEN DS    H
PARM2    DS    02CL8               DDNAMES
UT9W     DS    0F,(UT9L)X          DCB
PRTW     DS    0F,(PRTL)X          DCB
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
