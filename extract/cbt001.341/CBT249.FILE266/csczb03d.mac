         TITLE 'IEEZB03D -   ##B  DEVICE BUSY MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEEZB03D -       ##B  DEVICE BUSY MONITOR                 ***
***                                                                 ***
***       VERSION -        2.3                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   TO MVS/SP1.3 LEVEL        ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANDLE ESTAE           ***
***                        01/26/83  AXC-CBT CHANGE ESTAE PARM LEN  ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEEZB03D CSECT
         LR    R12,R15
         USING IEEZB03D,R12
         LR    R4,R14              SAVE RETURN ADDR
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R4,SAVRET           SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(20),ESTAEOVL    AXC-CBT
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         LR    R1,R13
         A     R1,=A(SEEKTAB-WORKSTRT)
         ST    R1,SEEKTABA
         XC    SEEKTOT(4),SEEKTOT
         L     R3,XAL              PARM LIST ADDR
         LA    R3,0(0,R3)
         MVI   NFNDSW,X'00'        SET NOTHING FOUND
         LTR   R3,R3               ANY PARM
         BZ    RETURN              NO
         MVC   PARM(3),0(R3)       SAVE PARM
         SR    R1,R1
         IC    R1,0(0,R3)          CHANNEL
         SLL   R1,28
         SRL   R1,28
         TM    0(R3),X'F0'         NUMERIC CHANNEL
         BO    *+8                 YES
         LA    R1,9(0,R1)
         STC   R1,CHANNEL          SAVE CHANNEL ID
         CLI   0(R3),C'X'          ALL DASD REQ
         BNE   *+8                 NO
         MVI   CHANNEL,C'X'
         MVC   DSPSW(1),4(R3)      SAVE LIST PARM
         CLI   DSPSW,C'J'          JOB LIST
         BE    PARM1               YES
         CLI   DSPSW,C'S'          SEEK ANALYSIS
         BNE   PARM0               NO
         CLI   PARM+2,C'X'         SINGLE DEVICE
         BNE   PARM1               YES
         SPACE 1
PARM0    MVI   DSPSW,C'D'          DEVICE LIST
         SPACE 1
PARM1    MVC   WTOLIST(4),=X'00540100'
         L     R1,XAT              WAITTIME ADDR
         L     R15,8(0,R1)         LOAD HEX TIME
         LTR   R15,R15             TIME NOT SPECIFIED
         BNZ   BYPASS              NO
         MVC   0(8,R1),=CL8'00003000'  30 SECONDS DEFAULT
         MVC   8(4,R1),=F'30'          30 SECONDS DEFAULT
         SPACE 1
BYPASS   L     R3,8(0,R1)          LOAD MONITOR TIME
         MH    R3,=H'5'            TIMES 5
         LR    R5,R3
         B     UCBTIME1
         EJECT
UCBTIME  STIMER WAIT,DINTVL=INTERVL
         SPACE 1
UCBTIME1 BAL   R7,UCBLOOP
         BCT   R3,UCBTIME
         B     UCBBUSY
         EJECT
UCBLOOP  STM   R0,R15,SAVEREGS
         XC    UCBDMSAV(24),UCBDMSAV
         L     R9,16               .CVT
         L     R9,40(0,R9)         .UCB LOOKUP TABLE
         SPACE 1
CHKUCB1A BAL   R10,UCBDML#0        GET NEXT UCB ADDR
         LTR   R1,R1               END OF UCBS
         BZ    CHKUCB5             YES, EXIT
         CLI   CHANNEL,C'X'        ALL DASD REQ
         BE    CHKUCB1X            YES, ALL MATCH
         CLC   13(3,R1),PARM       UNIT MATCH
         BE    CHKUCB1B            YES
         CLI   PARM+2,C'X'         CONTROL UNIT REQ
         BNE   CHKUCB1A            NO, NEXT UCB
         CLC   13(2,R1),PARM       UNIT MATCH
         BNE   CHKUCB1A            NO
         B     CHKUCB1B
         SPACE 1
CHKUCB1X CLI   18(R1),X'20'        DASD
         BNE   CHKUCB1A            NO, NEXT
         SPACE 1
CHKUCB1B MVI   NFNDSW,X'FF'
         TM    3(R1),X'88'         ONLINE/ALLOCATED
         BNO   CHKUCB1C            NO
         LA    R6,UCBTAB-22
         LR    R14,R1             UCB ADDR
         S     R14,=F'4'          DECR ADDR -4
         L     R14,0(0,R14)       IOQ ADDR
         LA    R14,0(0,R14)
         LTR   R14,R14            ANY IOQ
         BZ    CHKUCB1D           NO
         L     R14,8(0,R14)       IOSB ADDR
         LA    R14,0(0,R14)
         LTR   R14,R14            EXIST
         BZ    CHKUCB1D           NO
         CLI   DSPSW,C'S'         SEEK ANALYSIS
         BNE   CHKUCB1C           NO
         SR    R15,R15
         ICM   R15,3,103(R14)     SEEK ADDR
         SLL   R15,2
         A     R15,SEEKTABA
         L     R0,0(0,R15)
         A     R0,=F'1'
         ST    R0,0(0,R15)
         L     R0,SEEKTOT
         A     R0,=F'1'
         ST    R0,SEEKTOT
         SPACE 1
CHKUCB1C SR    R15,R15
         ICM   R15,3,6(R14)       ASID
         LA    R14,=CL8'*MASTER*'
         CH    R15,=H'1'          MASTER
         BE    CHKUCB2
         SLL   R15,2              X 4
         L     R14,16             CVT
         L     R14,556(0,R14)     ASVT
         L     R15,524(R15,R14)   ASCB
         L     R14,172(0,R15)      JOBNAME POINTER
         LTR   R14,R14             ZERO
         BNZ   CHKUCB2             NO
         L     R14,176(0,R15)      STC/TSU NAME POINTER
         LTR   R14,R14             ZERO
         BNZ   CHKUCB2             NO
         LA    R14,=CL8'*UNKNWN*'
         B     CHKUCB2
         SPACE 1
CHKUCB1D SR    R14,R14
         EJECT
CHKUCB2  LA    R6,22(0,R6)         NEXT UCB ENTRY
         CLI   0(R6),X'00'         EMPTY SLOT
         BE    CHKUCB3             YES
         CLI   0(R6),X'FF'         EMPTY SLOT
         BE    CHKUCB3             YES
         CLC   0(3,R6),13(R1)      UCB ADDR MATCH
         BNE   CHKUCB2             NO
         LTR   R14,R14             DEVICE NOT BUSY
         BZ    CHKUCB1A            YES
         CLI   DSPSW,C'D'          DEVICE ONLY
         BE    CHKUCB4             YES
         CLC   10(8,R6),0(R14)     JOBNAME MATCH
         BNE   CHKUCB2             NO
         B     CHKUCB4
         SPACE 1
CHKUCB3  XC    18(4,R6),18(R6)
         MVC   0(3,R6),13(R1)      UNIT ADDR
         MVC   4(6,R6),=CL6' '
         MVC   10(8,R6),0(R14)     JOBNAME
         TM    18(R1),X'A0'        DASD OR TAPE
         BZ    CHKUCB3A            NO
         MVC   4(6,R6),28(R1)      VOLSER
         SPACE 1
CHKUCB3A LTR   R14,R14             DEVICE NOT BUSY
         BZ    CHKUCB1A            YES
         SPACE 1
CHKUCB4  LH    R14,18(0,R6)        BUSY COUNT
         LA    R14,1(0,R14)
         STH   R14,18(0,R6)
         CLC   4(1,R1),CHANNEL     CHANNEL MATCH
         BNE   CHKUCB1A            NO
         LH    R14,20(0,R6)        BUSY COUNT
         LA    R14,1(0,R14)
         STH   R14,20(0,R6)
         B     CHKUCB1A
         SPACE 1
CHKUCB5  LA    R6,UCBTAB-22
         SPACE 1
CHKUCB5A LA    R6,22(0,R6)         NEXT UCB ENTRY
         CLI   0(R6),X'00'         EMPTY SLOT
         BNE   CHKUCB5A            NO
         MVI   0(R6),X'FF'         SET END OF TABLE
         SPACE 1
CHKUCB5B LM    R0,R15,SAVEREGS
         BR    R7
         EJECT
UCBDML#0 ST    R8,UCBDMSAV         .SAVE WORK REG 1
         ST    R10,UCBDMSAV+8      .SAVE WORK REG 3
         ST    R11,UCBDMSAV+12     .SAVE WORK REG 4
         L     R8,UCBDMLOW         .SET SEARCH ARGUMENT
         SPACE 1
UCBDML#1 LA    R8,1(0,R8)          .NEXT UNIT ADDR POSSIBLE
         L     R10,=X'0000FFFF'    .SET MAX NEXT LEAST HIGH
         SPACE 1
UCBDML#2 CLC   0(2,R9),=X'FFFF'    .END OF TABLE
         BE    UCBDMOUT            .YES, GO PROCESS
         SR    R1,R1
         ICM   R1,3,0(R9)          .LOAD UCB ADDR
         LTR   R1,R1               .ZERO ADDR
         BNZ   UCBDML#4            .NO, CONTINUE
         SPACE 1
UCBDML#3 LA    R9,2(0,R9)          .NEXT UCB LOOKUP TABLE ENTRY
         B     UCBDML#2
         SPACE 1
UCBDML#4 CLI   18(R1),X'10'        GRAPHICS
         BE    UCBDML#3            YES, NEXT UCB
         CLI   18(R1),X'40'        COMM
         BE    UCBDML#3            YES, NEXT UCB
         TM    6(R1),X'8A'         BUSY AND/OR CHANNEL PROGRAM ACTIVE
         BZ    UCBDML#3            NO, NEXT UCB
         BAL   R11,UCBDMCNV        .CONVERT UNIT ADDR TO HEX
         CR    R1,R8               .UNIT EQUAL
         BL    UCBDML#3            .LOW NOT EQUAL, NEXT UCB
         LR    R10,R1              .SET NEXT LEAST HIGH UNIT ADDR
         MVC   UCBDMCUR+2(2),0(R9) .SET NEXT LEAST HIGH UCB ADDR
         B     UCBDMOUT            .GO PROCESS
         EJECT
UCBDMCNV SR    R14,R14
         SR    R15,R15
         IC    R15,13(0,R1)        .UNIT ADDR DIGIT
         TM    13(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         IC    R15,14(0,R1)        .UNIT ADDR DIGIT
         TM    14(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         IC    R15,15(0,R1)        .UNIT ADDR DIGIT
         TM    15(R1),X'F0'        .NUMERIC
         BO    *+8                 .YES, BYPASS VALUE ADJUST
         LA    R15,9(0,R15)        .SET VALUE TO COVER A-F
         SLL   R15,28              .SHIFT OUT ALL BUT PERTINENT
         SLDL  R14,4               .INSERT INTO RESULTS REGISTER
         LR    R1,R14              .SET RESULTS REG
         BR    R11
         SPACE 3
UCBDMOUT C     R10,=X'0000FFFF'    .ALL DONE
         BE    UCBDMEX             .YES
         ST    R10,UCBDMLOW        .NEXT LOW UNIT ADDR
         L     R1,UCBDMCUR         .LOAD UCB ADDR FOR FOUND UNIT
         B     UCBDMCON            .CONTINUE
         SPACE 1
UCBDMEX  SR    R1,R1               .CLEAR UCB ADDR REGISTER
         L     R8,UCBDMSAV         .RELOAD WORK REG 1
         L     R10,UCBDMSAV+8      .RELOAD WORK REG 3
         L     R11,UCBDMSAV+12     .RELOAD WORK REG 4
         B     UCBDMRET            .END OF UCBS
         SPACE 1
UCBDMCON L     R8,UCBDMSAV         .RELOAD WORK REG 1
         L     R10,UCBDMSAV+8      .RELOAD WORK REG 3
         L     R11,UCBDMSAV+12     .RELOAD WORK REG 4
         SPACE 1
UCBDMRET EQU   *
         BR    R10
         EJECT
UCBBUSY  LA    R6,UCBTAB-22
         MVC   CURRUNIT(3),=X'FFFFFF'
         XC    CURRCNT(4),CURRCNT
         SPACE 1
UCBBUSY1 LA    R6,22(0,R6)
         MVC   WTOLIST+4(80),DEVOVLY
         CLI   0(R6),X'00'         EMPTY SLOT
         BE    UCBBUSY1            YES
         CLI   0(R6),X'FF'         END OF UCB TABLE
         BE    UCBBUSY3            YES
         CLC   CURRUNIT(2),0(R6)   CURRENT UNIT CHECK
         BL    UCBBUSY1            CURRENT LOW, CONTINUE
         BH    UCBBUSY2            CURRENT HIGH, REPLACE
         SR    R14,R14
         IC    R14,CURRUNIT+2
         SR    R15,R15
         IC    R15,2(0,R6)
         SLL   R14,28
         SRL   R14,28
         SLL   R15,28
         SRL   R15,28
         TM    CURRUNIT+2,X'F0'    NUMERIC
         BO    *+8                 NO
         LA    R14,9(0,R14)
         TM    2(R6),X'F0'         NUMERIC
         BO    *+8                 NO
         LA    R15,9(0,R15)
         CR    R14,R15             CURRENT UNIT CHECK
         BL    UCBBUSY1            CURRENT LOW, CONTINUE
         BH    UCBBUSY2            CURRENT HIGH, REPLACE
         LH    R1,CURRCNT
         AH    R1,18(0,R6)
         STH   R1,CURRCNT
         LH    R1,CURRCNT+2
         AH    R1,20(0,R6)
         STH   R1,CURRCNT+2
         B     UCBBUSY1
         SPACE 1
UCBBUSY2 MVC   CURRUNIT(3),0(R6)
         MVC   CURRVOL(6),4(R6)
         MVC   CURRCNT(4),18(R6)
         B     UCBBUSY1
         SPACE 1
UCBBUSY3 CLC   CURRUNIT(3),=X'FFFFFF'   END OF LIST
         BE    RETURN
         MVC   DEVSTRT(3),CURRUNIT UNIT ADDR
         MVI   DEVSTRT+3,C' '
         MVC   DEVSTRT+4(6),CURRVOL VOLSER
         SR    R14,R14
         LH    R15,CURRCNT         UCB BUSY COUNT
         M     R14,=F'100'
         DR    R14,R5              PERCENT BUSY
         CVD   R15,DOUBLE
         LR    R1,R15
         MVC   BUSY(4),=X'40202120'
         ED    BUSY(4),DOUBLE+6
         SR    R14,R14
         LH    R15,CURRCNT+2       UCB BUSY COUNT THIS CHANNEL
         M     R14,=F'100'
         DR    R14,R5              PERCENT BUSY
         CVD   R15,DOUBLE
         MVC   BUSY+4(4),=X'40202120'
         ED    BUSY+4(4),DOUBLE+6
         MVI   BUSY+4,C'%'
         LR    R15,R1
         SRL   R15,1
         CH    R15,=H'50'
         BNH   *+8
         LH    R15,=H'50'
         EX    R15,PCTMVE
         LA    R15,PCTGRAPH(R15)
         MVI   0(R15),C'>'
         LA    R1,WTOLIST
         BAL   R11,MSGRTN
         CLI   DSPSW,C'S'          SEEK ANALYSIS
         BE    UCBSEEK             YES
         LA    R6,UCBTAB-22
         SPACE 1
UCBBUSY4 LA    R6,22(0,R6)
         MVC   WTOLIST+4(80),DEVOVLY
         CLI   0(R6),X'00'         EMPTY SLOT
         BE    UCBBUSY4            YES
         CLI   0(R6),X'FF'         END OF UCB TABLE
         BE    UCBBUSY             YES
         CLC   CURRUNIT(3),0(R6)   CURRENT UNIT CHECK
         BNE   UCBBUSY4            NO MATCH
         CLI   DSPSW,C'J'          JOB DISPLAY
         BNE   UCBBUSY5            NO
         MVC   DEVSTRT+3(8),10(R6)   JOBNAME
         MVC   DEVSTRT(3),=C'-- '
         MVC   DEVSTRT+11(4),=CL4' '
         SR    R14,R14
         LH    R15,18(0,R6)        UCB BUSY COUNT
         M     R14,=F'100'
         DR    R14,R5              PERCENT BUSY
         CVD   R15,DOUBLE
         MVC   BUSY(4),=X'40202120'
         ED    BUSY(4),DOUBLE+6
         SR    R14,R14
         LH    R15,20(0,R6)        UCB BUSY COUNT THIS CHANNEL
         M     R14,=F'100'
         DR    R14,R5              PERCENT BUSY
         CVD   R15,DOUBLE
         MVC   BUSY+4(4),=X'40202120'
         ED    BUSY+4(4),DOUBLE+6
         MVI   BUSY+4,C'%'
         LA    R1,WTOLIST
         BAL   R11,MSGRTN
         MVI   0(R6),X'00'         CLEAR ENTRY
         B     UCBBUSY4
         SPACE 1
UCBBUSY5 MVI   0(R6),X'00'         CLEAR ENTRY
         B     UCBBUSY
         SPACE 3
PCTMVE   MVC   PCTGRAPH(0),PCTLINE
         EJECT
UCBSEEK  MVC   WTOLIST+4(80),SEEKHDR
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         L     R4,SEEKTABA         SEEK TABLE ADDR
         SR    R5,R5
         LA    R6,100
         SPACE 1
UCBSEEK0 LA    R7,10               CYL PER LINE
         LA    R8,WTOLIST+16
         SR    R9,R9               TOTAL
         CVD   R5,DOUBLE           CYL ADDR
         MVI   WTOLIST+4,C' '
         MVC   WTOLIST+5(79),WTOLIST+4
         UNPK  WTOLIST+4(3),DOUBLE+6(2)
         OI    WTOLIST+6,X'F0'
         SPACE 1
UCBSEEK1 SR    R14,R14
         L     R15,0(0,R4)         CYLINDER ACCESS COUNT
         A     R9,0(0,R4)          TOTAL CYLINDER ACCESS COUNT
         LTR   R15,R15             ANY ACCESS
         BNZ   UCBSEEK4            YES
         SPACE 1
UCBSEEK2 LA    R5,1(0,R5)          ADD TO CYL COUNT
         LA    R4,4(0,R4)          NEXT CYL
         LA    R8,7(0,R8)          NEXT PRINT ADDR
         BCT   R7,UCBSEEK1
         LTR   R15,R9              TOTAL FOR CYL RANGE
         BZ    UCBSEEK3
         M     R14,=F'100'
         D     R14,SEEKTOT
         CVD   R15,DOUBLE
         MVC   WTOLIST+9(5),=X'402021206C'
         ED    WTOLIST+9(4),DOUBLE+6
         LA    R1,WTOLIST
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         SPACE 1
UCBSEEK3 BCT   R6,UCBSEEK0
         B     RETURN
         SPACE 1
UCBSEEK4 M     R14,=F'100'
         D     R14,SEEKTOT
         CVD   R15,DOUBLE
         MVC   0(5,R8),=X'402021206C'
         ED    0(4,R8),DOUBLE+6
         B     UCBSEEK2
         EJECT
MSGRTN   SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
TSTTSSVC SVC   35                  ISSUE WTO
         BR    R11
         SPACE 3
RETURN   CLI   NFNDSW,X'00'        ANY OUTPUT
         BNE   RETURN1             YES
         LA    R1,NFNDMSG
         BAL   R11,MSGRTN
         SPACE 3
RETURN1  EQU   *
         ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R5,SAVRET           RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         LR    R14,R5
         BR    R14
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEEZB03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         LA    R11,RETURN          LOAD RET ADDR
         B     MSGRTN
         EJECT
         LTORG
         DS    0D
INTERVL  DC    CL8'00000020'
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
DEVOVLY  DC    CL80'UUU VVVVVV BUSY NNN/NNN%'
         SPACE 3
SEEKHDR  DC    CL80'CYL    ALL    --0    --1    --2    --3    --4    --X
               5    --6    --7    --8    --9'
         SPACE 3
PCTLINE  DC    CL51'0====1====2====3====4====5====6====7====8====9===='
         SPACE 3
         DS    0F
NFNDMSG  DC    X'00300100'
         DC    CL44'UNIT SPECIFIED NOT FOUND OR 00% BUSY'
         SPACE 3
         DS    0F
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
SAVEREGS DS    18F
DOUBLE   DS    2F
XSASAVE  DS    F
SEEKTABA DS    F
SEEKTOT  DS    F
DSPSW    DS    C
NFNDSW   DS    C
CHANNEL  DS    C
CURRUNIT DS    CL3
CURRVOL  DS    CL6
CURRCNT  DS    F
PARM     DS    F
ESTAELST DS    5F              AXC-CBT
         SPACE 3
UCBDMSAV DS    4F
UCBDMLOW DS    F
UCBDMCUR DS    F
         SPACE 5
         DS    0F
WTOLIST  DS    F
DEVSTRT  DS    CL8
         DS    CL2
         DS    CL5
BUSY     DS    CL8
         DS    CL2
PCTGRAPH DS    CL51
         DS    CL4
         SPACE 3
         DS    0F
UCBTAB   DS    2000CL22
         SPACE 3
SEEKTAB  DS    1000F
         SPACE 3
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
CHAN1    EQU   256
CHAN2    EQU   512
CHAN3    EQU   768
CHAN4    EQU   1024
CHAN5    EQU   1280
CHAN6    EQU   1536
CHAN7    EQU   1792
CHAN8    EQU   2048
CHAN9    EQU   2304
CHAN10   EQU   2560
CHAN11   EQU   2816
CHAN12   EQU   3072
CHAN13   EQU   3328
CHAN14   EQU   3584
CHAN15   EQU   3840
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
