INIT     QSTART 'QUEUE COMMAND - INITIALIZATION ROUTINES'
         GBLA  &QLEVEL        JES2 LEVEL. DEFINED BY QSTART MACRO.
         $JOE
         $JOT
         TITLE 'QUEUE COMMAND - INITIALIZATION ROUTINES'
***********************************************************************
*                                                                     *
*   LOAD QCOMMON                                                      *
*                                                                     *
***********************************************************************
*
****
*******  IF YOU WANT TO CHANGE THE NAME FOR THE COMMON AREA,
****               THIS IS THE ONLY REFERENCE TO IT.
*
         LOAD  EP=CSCGQQ00    QUEUE COMMON AREA              (CMDSBMON)
         LR    R11,R0         ADDR OF QCOMMON
         L     R1,4(R13)      PREVIOUS SAVE AREA
         ST    R11,64(R1)     UPDATE R11 IN PREVIOUS SAVE AREA
         ST    R1,QFRSTSA     STORE ADDR OF FIRST SAVEAREA IN QCOMMON
         USING QDAIR,R10      BASE REG FOR DAIR WORK
         L     R10,QVDAIR     LOAD BASE REG
         USING QCKPT,R9       BASE REG FOR CKPT WORK
         L     R9,QVCKPT      LOAD BASE REG
         USING QDISPLAY,R8    BASE REG FOR DISPLAY WORK
         L     R8,QVDSPL      LOAD BASE REG
***********************************************************************
*                                                                     *
*   MOVE PARMS FROM CPPL TO DAPL                                      *
*                                                                     *
***********************************************************************
         USING CPPL,R2        ADDR OF CPPL IS IN R2
*        MVC   DAPLUPT,CPPLUPT USER PROFILE TABLE            (CMDSBMON)
*        MVC   DAPLPSCB,CPPLPSCB PROTECTED STORAGE CNTL BLK  (CMDSBMON)
*        MVC   DAPLECT,CPPLECT ENVIRONMENT CNTL TABLE        (CMDSBMON)
         MVC   DAPLUPT,=F'0'   USER PROFILE TABLE            (CMDSBMON)
         MVC   DAPLPSCB,=F'0'    PROTECTED STORAGE CNTL BLK  (CMDSBMON)
         MVC   DAPLECT,=F'0'   ENVIRONMENT CNTL TABLE        (CMDSBMON)
         ST    R2,QCMD1PTR     SAVE CMDSBMON PARMLIST ADDR   (CMDSBMON)
         L     R2,48(0,R2)     LOAD Q CMD SUB-PARM ADDR      (CMDSBMON)
         MVC   QDREPLY(63),0(R2)                             (CMDSBMON)
         LA    R2,63           SAVE REPLY LENGTH             (CMDSBMON)
         STH   R2,QDRLNG                                     (CMDSBMON)
***********************************************************************
*                                                                     *
*   MOVE COMMAND BUFFER TO REPLY BUFFER                               *
*                                                                     *
***********************************************************************
*        L     R1,CPPLCBUF    ADDR OF COMMAND BUFFER         (CMDSBMON)
*        LH    R3,0(R1)       LENGTH OF COMMAND BUFFER       (CMDSBMON)
*        LH    R4,2(R1)       OFFSET TO FIRST DATA BYTE      (CMDSBMON)
*        LA    R1,4(R1,R4)    FIRST DATA BYTE                (CMDSBMON)
*        SR    R3,R4          SUBTRACT OFFSET FROM LENGTH    (CMDSBMON)
*        SH    R3,=H'4'       SUBTRACT OVERHEAD              (CMDSBMON)
*        SH    R3,=H'1'       IS LENGTH ZERO?                (CMDSBMON)
*        BM    SKIP           YES. SKIP IT.                  (CMDSBMON)
*        EX    R3,OCBUF       TRANSLATE TO UPPER CASE        (CMDSBMON)
*        CLC   =C'CKPT(',0(1) IS REQUEST FOR CKPT?           (CMDSBMON)
*        BE    CKPT           YES. DO IT.                    (CMDSBMON)
*        MVC   QDREPLY,QBLANK BLANK THE REPLY LINE           (CMDSBMON)
*        CH    R3,=H'62'      IS LENGTH OVER 63?             (CMDSBMON)
*        BNH   *+8            NO. USE IT.                    (CMDSBMON)
*        LA    R3,62          USE MAXIMUM LENGTH             (CMDSBMON)
*        EX    R3,MVCBUF      MOVE THE DATA                  (CMDSBMON)
*        LA    R3,1(R3)       INCREMENT TO TRUE LENGTH       (CMDSBMON)
*        STH   R3,QDRLNG      STORE REPLY LENGTH             (CMDSBMON)
***********************************************************************
*                                                                     *
*   LOCATE LOGON ID, MOVE TO QLOGON                                   *
*                                                                     *
***********************************************************************
SKIP     L     R1,16          ADDR OF CVT
         L     R1,0(R1)       ADDR OF DISPATCH QUEUE
         L     R1,12(R1)      ADDR OF CURRENT ASCB
         L     R1,176(R1)     ADDR OF JOBNAME
         MVC   QLOGON,0(R1)   MOVE JOBNAME TO QLOGON
***********************************************************************
*                                                                     *
*   OBTAIN BLOCK ADDR TABLE FOR LISTDS                                *
*                                                                     *
***********************************************************************
         GETMAIN R,LV=65536
         ST    R1,QCSTART     STORE STARTING ADDR OF TABLE
         A     R1,=F'65536'   END OF TABLE
         ST    R1,QCEND
***********************************************************************
*                                                                     *
*   ALLOCATE HASPCKPT                                                 *
*                                                                     *
***********************************************************************
         MVC   DA08DDN,=CL8'HASPCKPT' DDNAME FOR ALLOCATE
         MVC   DA08PDSN,=A(DSNCKPT) DSNAME FOR ALLOCATE
         MVI   DAIRFLAG,X'08' REQUEST ALLOCATE FUNCTION
         L     R15,=V(ALLOCATE) ADDR OF ALLOCATE MODULE
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   OPEN HASPCKPT, READ FIRST BLOCK OF CHECKPOINT                     *
*                                                                     *
***********************************************************************
         OPEN  MF=(E,HOCKPT)  OPEN HASPCKPT
         L     R2,QCSTART     USE TABLE AREA FOR IOAREA
         AIF   (&QLEVEL NE 3).NOPT
         POINT HASPCKPT,TIR3  POINT PAST SYNC RECORDS
.NOPT    ANOP
         READ  HDECB1,SF,,(R2),MF=E READ FIRST RECORD
         CHECK HDECB1
         AIF   (&QLEVEL EQ 1).LVL1
         AIF   (&QLEVEL EQ 2).LVL2
         AIF   (&QLEVEL EQ 3).LVL2
         MNOTE 8,'QLEVEL IS INVALID. SEE QSTART MACRO DEFINITION.'
.LVL1    ANOP
***********************************************************************
*                                                                     *
*   LOCATE FIXED PORTION OF CHECKPOINT RECORD                         *
*                                                                     *
***********************************************************************
         USING QSEDSECT,R2    BASE REG FOR QSE BLOCKS
         LA    R3,QSESTAT+L'QSESTAT+7-QSEDSECT LENGTH OF QSE
         N     R3,=F'-8'      ROUND TO DOUBLE WORD
         LR    R4,R3          OFFSET PAST ALL QSES
TRYQSE   TM    QSEFLAGS,QSELAST IS THIS THE LAST QSE?
         BO    LASTQSE        YES. BRANCH.
         AR    R2,R3          INCREMENT TO NEXT QSE
         AR    R4,R3          ACCUMULATE OFFSET PAST QSE
         B     TRYQSE         TRY NEXT QSE
LASTQSE  LA    R4,$JQHEADS+2-$SAVEBEG(R4) OFFSET TO JQE HEADERS
         ST    R4,QCJQHEAD    SAVE OFFSET
         AR    R2,R3          INCREMENT TO NEXT QSE
***********************************************************************
*                                                                     *
*   COMPUTE OFFSET TO FIRST PDDB IN IOT                               *
*                                                                     *
***********************************************************************
         DROP  R2
         USING $SAVEBEG,R2    BASE REG FOR CHECKPOINT
         LH    R5,$NUMTGV     NUMBER OF TRACK GROUPS PER VOLUME
         LA    R5,7(R5)       ROUND UP TO MULTIPLE OF 8
         SRL   R5,3           DIVIDE BY 8
         SR    R0,R0          ZERO OUT R0
         IC    R0,$NUMDA      NUMBER OF SPOOL VOLUMES
         MR    R4,R0          LENGTH OF TRACK GROUP MAP IN R5
         LR    R1,R5          SAVE LENGTH OF TRACK GROUP MAP
         LA    R5,IOTTGMAP-IOTDSECT+TGMAP-TGMDSECT+3(R5) OFFSET
         N     R5,=F'-4'      ROUND TO FULL WORD BOUNDARY
         ST    R5,QCPDDB1     SAVE OFFSET TO FIRST PDDB IN IOT
***********************************************************************
*                                                                     *
*   COMPUTE LENGTH OF JOBQUEUE RECORD (JQT)                           *
*                                                                     *
***********************************************************************
         SR    R4,R4          COMPUTE
         IC    R4,$NUMDA             SIZE OF
         MH    R4,=H'6'                     DA CKPT INFO
         LH    R5,$NUMRJE     COMPUTE
         LR    R6,R5                 SIZE OF
         LA    R5,1(R5)                     MSG SPOOLING
         MH    R5,=H'3'                                  QUEUE
         MH    R6,=H'3'              AND
         LA    R6,7(R6)                 REMOTE
         SRL   R6,3                           SIGNON TABLE
         ALR   R4,R5          ADD
         ALR   R4,R6          ADD
         MH    R3,$QSENO      SIZE OF QSE SEGMENT
         LA    R4,$SAVELEN+JQELNGTH-1(R3,R4) ADD FIXED PORTION SIZE
         ALR   R1,R4          ADD LENGTH OF TRACK GROUP MAP
         SLR   R0,R0          PREPARE FOR DIVIDE
         D     R0,=A(JQELNGTH) DIVIDE BY LENGTH OF JQE
         AH    R1,$MAXJOBS    ADD NUMBER OF JQES
         M     R0,=A(JQELNGTH) MULTIPLY BY LENGTH OF JQE
         ST    R1,QCJQTL      STORE LENGTH OF CHECKPOINT RECORD
***********************************************************************
*                                                                     *
*   COMPUTE SIZE OF JOT                                               *
*                                                                     *
***********************************************************************
NJOTPRFX EQU   (JOTJOES-JOTDSECT)/JOESIZE
         L     R3,=A(NJOTPRFX) SIZE OF JOT PREFIX
         AH    R3,$NUMJOES    NUMBER OF JOES
         MH    R3,=AL2(JOESIZE) MULTIPLY BY JOE SIZE
         ST    R3,QCJOTL      SIZE OF JOT
***********************************************************************
*                                                                     *
*   COMPUTE HASPACE BUFFER SIZE                                       *
*                                                                     *
***********************************************************************
         LH    R4,$BUFSIZE    BLKSIZE FOR HASPACE
         STH   R4,HASPACE+62  STORE IN DCB
         STH   R4,HDECB2+6    STORE IN DECB
         LA    R4,63(R4)      PREPARE TO ROUND
         N     R4,=F'-64'     ROUND TO 64 BYTE BOUNDARY
***********************************************************************
*                                                                     *
*   OBTAIN BUFFERS FOR HASPCKPT AND HASPACE                           *
*                                                                     *
***********************************************************************
         LR    R0,R4          HASPACE BUFFER SIZE
         MH    R0,=H'3'       3 BUFFERS
         AR    R0,R1          JQT LENGTH
         AR    R0,R3          JOT LENGTH
         A     R0,=F'32768'   ADD SLOP FOR IOS PAGE FIX
         ST    R0,QCGT1MSZ                                   (CMDSBMON)
         GETMAIN R,LV=(0)     OBTAIN BUFFERS
         ST    R1,QCGT1MAD                                   (CMDSBMON)
         ST    R1,QCJCTA      BUFFER FOR JCT
         AR    R1,R4          INCREMENT
         ST    R1,QCIOTA      BUFFER FOR IOT
         AR    R1,R4          INCREMENT
         ST    R1,QCBLKA      BUFFER FOR DATA BLOCKS
         AR    R1,R4          INCREMENT
         ST    R1,QCJOTA      BUFFER FOR JOT
         AR    R1,R3          INCREMENT
         ST    R1,QCJQTA      BUFFER FOR JQT
         A     R1,QCJQHEAD    ADD OFFSET TO BASE
         ST    R1,QCJQHEAD    ADDR OF FIRST JQE HEADER
***********************************************************************
*                                                                     *
*   ALLOCATE AND OPEN HASPACE                                         *
*                                                                     *
***********************************************************************
         LA    R3,$SAVEEND    ADDR OF DA CKPT INFO
         AGO   .EXIT
.LVL2    ANOP
***********************************************************************
*                                                                     *
*   COMPUTE OFFSET TO FIRST PDDB IN IOT                               *
*                                                                     *
***********************************************************************
         USING $SAVEBEG,R2    BASE REG FOR CHECKPOINT
         LH    R5,$NUMTGV     NUMBER OF TRACK GROUPS PER VOLUME
         LA    R5,7(R5)       ROUND UP TO MULTIPLE OF 8
         SRL   R5,3           DIVIDE BY 8
         SR    R0,R0          ZERO OUT R0
         IC    R0,$NUMDA      NUMBER OF SPOOL VOLUMES
         MR    R4,R0          LENGTH OF TRACK GROUP MAP IN R5
         LR    R1,R5          SAVE LENGTH OF TRACK GROUP MAP
         LA    R5,IOTTGMAP-IOTDSECT+TGMAP-TGMDSECT+3(R5) OFFSET
         N     R5,=F'-4'      ROUND TO FULL WORD BOUNDARY
         ST    R5,QCPDDB1     SAVE OFFSET TO FIRST PDDB IN IOT
***********************************************************************
*                                                                     *
*   COMPUTE NUMBER OF JQE BLOCKS ON CKPT                              *
*                                                                     *
***********************************************************************
         LH    R6,$MAXJOBS    NUMBER OF JQES
         AIF   (&QLEVEL NE 3).NOEYE
         LA    R6,1(,R6)       PLUS 1 FOR EYE-CATCHER
.NOEYE   ANOP
         MH    R6,=AL2(JQELNGTH) MULTIPLY BY LENGTH OF JQE
         LA    R6,4095(R6)    PREPARE TO ROUND
         SRL   R6,12          DIVIDE BY 4096
         STH   R6,QCJQTL      NUMBER OF BLOCKS FOR JQES
***********************************************************************
*                                                                     *
*   COMPUTE NUMBER OF JOT BLOCKS IN CKPT                              *
*                                                                     *
***********************************************************************
NJOTPRFX EQU   (JOTJOES-JOTDSECT)/JOESIZE
         LH    R3,$NUMJOES    NUMBER OF JOES
         LA    R3,NJOTPRFX(,R3) ADJUSTED LENGTH OF JOT PREFIX
         MH    R3,=AL2(JOESIZE) MULTIPLY BY LENGTH OF JOE
         LA    R3,4095(R3) PREPARE TO ROUND
         SRL   R3,12          DIVIDE BY 4096
         STH   R3,QCJOTL      NUMBER OF BLOCKS FOR JOT
***********************************************************************
*                                                                     *
*   COMPUTE TOTAL LENGTH OF QSES                                      *
*                                                                     *
***********************************************************************
         AIF   (&QLEVEL EQ 3).LVL3
         LA    R4,QSESTAT+L'QSESTAT-QSEDSECT QSE LENGTH
         AR    R4,R6          ADD ONE BYTE FOR EACH JQE BLOCK
         AR    R4,R3          ADD ONE BYTE FOR EACH JOT BLOCK
         LA    R4,7(R4)       PREPARE TO ROUND
         N     R4,=F'-8'      ROUND TO MULTIPLE OF 8
         MH    R4,$QSENO      MULTIPLY LENGTH TIMES NUMBER OF QSES
         AGO   .NOLVL3
.LVL3    ANOP
         LA    R4,QSELEN      QSE LENGTH
         MH    R4,$QSENO      MULTIPLY LENGTH TIMES NUMBER OF QSES
         ALR   R4,R6          ADD ONE BYTE FOR EACH JQE BLOCK
         ALR   R4,R3          ADD ONE BYTE FOR EACH JOT BLOCK
.NOLVL3  ANOP
***********************************************************************
*                                                                     *
*   COMPUTE HASPACE BUFFER SIZE                                       *
*                                                                     *
***********************************************************************
         LH    R5,$BUFSIZE    BLKSIZE FOR HASPACE
         STH   R5,HASPACE+62  STORE IN DCB
         STH   R5,HDECB2+6    STORE IN DECB
         LA    R5,63(R5)      PREPARE TO ROUND
         N     R5,=F'-64'     ROUND TO 64 BYTE BOUNDARY
***********************************************************************
*                                                                     *
*   OBTAIN BUFFERS FOR HASPCKPT AND HASPACE                           *
*                                                                     *
***********************************************************************
         LR    R14,R5         HASPACE BUFFER SIZE
         MH    R14,=H'3'      3 BUFFERS
         LA    R1,1(R6,R3)    NUMBER OF BLOCKS IN CKPT DS
         ST    R1,QCJOTL      STORE RECORD COUNT
         SLL   R1,12          MULTIPLY BY 4096
         LA    R0,256(R1,R14) ADD CKPT BUFFERS, HASPACE BUFFERS, SLOP
         ST    R0,QCGT2MSZ                                   (CMDSBMON)
         GETMAIN R,LV=(0)     OBTAIN BUFFERS
         ST    R1,QCGT2MAD                                   (CMDSBMON)
         ST    R1,QCJQTL      BUFFER FOR FIRST CKPT REC
         AH    R1,=H'4096'    INCREMENT
         ST    R1,QCJQTA      BUFFER FOR JQE BLOCKS
         SLL   R6,12          MULTIPLY BY 4096
         AR    R1,R6          INCREMENT
         ST    R1,QCJOTA      BUFFER FOR JOE BLOCKS
         SLL   R3,12          MULTIPLY BY 4096
         AR    R1,R3          INCREMENT
         ST    R1,QCJCTA      BUFFER FOR JCT
         AR    R1,R5          INCREMENT
         ST    R1,QCIOTA      BUFFER FOR IOT
         AR    R1,R5          INCREMENT
         ST    R1,QCBLKA      BUFFER FOR DATA BLOCKS
         LA    R1,$JQHEADS+2-$SAVEBEG OFFSET TO FIRST JQE HEADER
         A     R1,QCJQTL      BASE OF FIRST CKPT REC
         ST    R1,QCJQHEAD    ADDR OF FIRST JQE HEADER
***********************************************************************
*                                                                     *
*   ALLOCATE AND OPEN HASPACE                                         *
*                                                                     *
***********************************************************************
         LA    R3,$SAVEEND(R4) ADDR OF DA CKPT INFO
.EXIT    ANOP
         MVC   DA08DDN,=CL8'HASPACE' DDNAME FOR ALLOCATE
         MVC   DA08PDSN,=A(DSNSPACE) DSNAME FOR ALLOCATE
         SR    R4,R4          ZERO R4
         IC    R4,$NUMDA      MAXIMUM NUMBER OF SPOOL VOLUMES
         LA    R7,QCDCBL      LENGTH OF HASPACE DCB
         MR    R6,R4          COMPUTE LENGTH OF DCB POOL
         ST    R7,QCGT3MSZ                                   (CMDSBMON)
         GETMAIN R,LV=(R7)    OBTAIN DCB POOL
         ST    R1,QCGT3MAD                                   (CMDSBMON)
         LR    R7,R1          SAVE ADDR OF DCB POOL
         LA    R8,QCSPOOLS-4  ADDR OF OPEN LIST
         SR    R6,R6          ACTUAL NUMBER OF SPOOL VOLUMES
         MVC   DA08SER(5),$SPOOL PATTERN FOR VOLSER
SPOOL1   LA    R5,DEVTAB      ADDR OF DEVICE CHARACTERISTICS TBL
         CLI   0(R3),0        IS THIS VOLUME UNUSED?
         BE    SPOOL4         YES. TRY NEXT.
SPOOL2   CLI   0(R5),X'FF'    IS THIS THE END OF TABLE?
         BE    ABORT          YES. UNSUPPORTED DEVICE TYPE.
         CLC   0(1,R5),0(R3)  IS THIS A MATCH?
         BE    SPOOL3         YES. GO WITH IT.
         LA    R5,12(R5)      NEXT TABLE ENTRY
         B     SPOOL2         TRY NEXT ENTRY
SPOOL3   MVC   150(2,R8),2(R5) MOVE TRK/CYL TO TRK/CYL LIST
         MVC   DA08UNIT,4(R5) MOVE UNIT NAME
         MVC   DA08SER+5(1),1(R3) LAST DIGIT OF VOLSER
         LA    R6,1(R6)       INCREASE COUNT BY ONE
         STC   R6,DA08DDN+7   UPDATE DDNAME
         OI    DA08DDN+7,X'F0' MAKE IT A VALID NUMBER
         L     R15,=V(ALLOCATE) ADDR OF ALLOCATE MODULE
         BALR  R14,R15        GO TO IT
         MVC   0(QCDCBL,R7),HASPACE MOVE PATTERN DCB TO POOL
         MVC   47(1,R7),DA08DDN+7 UPDATE THE DDNAME
         ST    R7,4(R8)       STORE DCB ADDR IN OPEN LIST
         LA    R7,QCDCBL(R7)  INCREMENT TO NEXT DCB
         LA    R8,4(R8)       NEXT ENTRY IN OPEN LIST
SPOOL4   LA    R3,6(R3)       NEXT VOLUME
         BCT   R4,SPOOL1      BRANCH IF MORE VOLUMES.
         OI    0(R8),X'80'    INDICATE END OF OPEN LIST
         OPEN  MF=(E,QCSPOOLS) OPEN HASPACE
***********************************************************************
*                                                                     *
*   GO HOME                                                           *
*                                                                     *
***********************************************************************
         QSTOP
***********************************************************************
*                                                                     *
*   PROCESS REQUEST FOR DIFFERENT UNIT AND VOL ON SYS1.HASPCKPT       *
*                                                                     *
***********************************************************************
*
*** FORMAT - QUEUE CKPT(UNIT,VOLSER)
*
CKPT     MVC   DA08UNIT(16),QBLANK BLANK THE UNIT AND VOLSER FIELDS
         LA    R5,DA08UNIT    START OF UNIT FIELD
         LA    R6,DA08SER     START OF VOLSER FIELD
         SH    R3,=H'4'       SUBTRACT OVERHEAD FROM LENGTH
CKPT1    CLI   5(R1),C','     IS THIS THE END OF UNIT FIELD?
         BE    CKPT2          YES. PROCESS VOLSER NEXT.
         MVC   0(1,R5),5(R1)  MOVE ONE BYTE OF UNIT NAME
         LA    R5,1(R5)       ADD 1 TO RECEIVING ADDR
         LA    R1,1(R1)       ADD 1 TO SENDING ADDR
         BCT   R3,CKPT1       BRANCH IF NOT EXHAUSTED.
         B     ABORT2         INVALID PARAMETERS.
CKPT2    CLI   6(R1),C')'     IS THIS THE END OF VOLSER FIELD?
         BE    CKPT3          YES. CONTINUE PROCESSING.
         MVC   0(1,R6),6(R1)  MOVE ONE BYTE TO VOLSER
         LA    R6,1(R6)       ADD ONE TO RECEIVING ADDR
         LA    R1,1(R1)       ADD ONE TO SENDING ADDR
         BCT   R3,CKPT2       BRANCH IF NOT EXHAUSTED.
         B     ABORT2         INVALID PARAMETER.
CKPT3    CLI   DA08UNIT,C' '  IS THERE A UNIT?
         BE    ABORT2         NO. INVALID.
         CLI   DA08SER,C' '   IS THERE A VOLSER?
         BE    ABORT2         NO. INVALID.
         CLI   DA08BLK,0      DID WE GO TOO FAR?
         BE    SKIP           NO. EVERTHING LOOKS GOOD.
*ABORT2  TPUT  MESSAGE2,L'MESSAGE2 TELL THE USER             (CMDSBMON)
ABORT2   LA    R1,MESSAGE2    TELL THE USER                  (CMDSBMON)
         LA    R0,1           NO. OF LINES                   (CMDSBMON)
         QCLNK                                               (CMDSBMON)
         ABEND 97,DUMP        QUIT
***********************************************************************
*                                                                     *
*   UNSUPPORTED DEVICE TYPE. ABORT.                                   *
*                                                                     *
***********************************************************************
*ABORT   TPUT  MESSAGE,L'MESSAGE TELL THE USER               (CMDSBMON)
ABORT    LA    R1,MESSAGE     TELL THE USER                  (CMDSBMON)
         LA    R0,1           NO. OF LINES                   (CMDSBMON)
         QCLNK                                               (CMDSBMON)
         ABEND 98,DUMP        QUIT.
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
OCBUF    OC    0(1,R1),QBLANK TRANSLATE COMMAND TO UPPER CASE
MVCBUF   MVC   QDREPLY(1),0(R1) MOVE DATA TO REPLY
DEVTAB   DC    X'0900',H'19',CL8'3330' DEVTYPE,TRK/CYL,DEVNAME
         DC    X'0B00',H'30',CL8'3350'
         DC    X'0D00',H'19',CL8'3330-1'
         DC    X'FFFF'
DSNCKPT  DC    H'13',CL44'SYS1.HASPCKPT'
DSNSPACE DC    H'12',CL44'SYS1.HASPACE'
MESSAGE  DC    CL80'UNSUPPORTED DEVICE TYPE SPECIFIED FOR SPOOL'
MESSAGE2 DC    CL80'INVALID PARAMETER SPECIFIED - CKPT(UNIT,VOLSER)'
         DS    0F
TIR3     DC    X'00000300'    POINT PAST SYNC RECORDS
         GBLC  &VERSION
&VERSION SETC  '0'
BASE1    EQU   0
$RPS     EQU   0
$MSGID   EQU   0
$DUPVOLT EQU   0
$PRIOOPT EQU   0
$PRTBOPT EQU   0
$PRTRANS EQU   0
$QSONDA  EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $TAB
         $QSE
         $HCT
         $IOT
         $TGM
         $JQE
         IKJCPPL
         QCOMMON
         END
