         TITLE 'IEESP03D -   #P  SU CONFIGURATION'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESP03D -       #P  SU CONFIGURATION                     ***
***                                                                 ***
***       VERSION -        1.2                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8. TO MVS/SP1.3                ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANDLE ESTAE           ***
***                        01/26/83  AXC-CBT CHANGE ESTAE PARM LEN  ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESP03D CSECT
         LR    R12,R15            SET ADDRESSABILITY
         USING IEESP03D,R12
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         USING XSA,R2
         ST    R14,SAVRET          PUT RETURN ADDRESS IN UNUSED SLOT
         ST    R2,XSASAVE          SAVE XSA ADDR
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(20),ESTAEOVL      AXC-CBT
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         MVI   TYPE,C'S'          SET SU SELECT SCAN TYPE
         L     R3,XAL             LOAD SUB-PARM ADDR
         LTR   R3,R3              ANY SUB-PARM
         BZ    *+10               NO
         MVC   TYPE(1),0(R3)      SET SCAN TYPE
         L     R3,16              CVT
         L     R4,196(0,R3)       SMCA
         LA    R9,0(0,R3)
         S     R9,=F'8'
         MVC   WTOLIST(52),SYSOVLY
         MVC   WTOLIST+14(4),16(R4) SYSID
         UNPK  WTOLIST+29(5),2(3,R9) CPU MODEL
         OI    WTOLIST+32,X'F0'
         MVI   WTOLIST+33,C' '
         MVC   WTOLIST+48(4),4(R9)  OPER SYSTEM LEVEL
         MVC   WTOLIST+48(1),5(R9)  OPER SYSTEM LEVEL
         MVI   WTOLIST+49,C'.'
         LA    R1,WTOLIST          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         L     R3,1044(0,R3)      SU BITS
         LA    R4,SUTABLE-52
         LA    R8,4                NO. OF SUBIT WORDS
         EJECT
SUBIT1   L     R7,0(0,R3)          1ST WORD OF SU BITS
         SR    R6,R6
         LA    R5,32
         SPACE 1
SUBIT2   SLDL  R6,1
         LA    R4,52(0,R4)
         CLI   TYPE,C'C'           PRINT CURRENT
         BE    SUBIT5
         CLI   TYPE,C'A'           PRINT ALL
         BE    SUBIT6
         LTR   R6,R6               SU BIT ON
         BZ    SUBIT3              NO
         CLI   TYPE,C'I'           PRINT INSTALLED
         BE    SUBIT4              YES
         CLC   12(9,R4),=CL9'-OBSOLETE'
         BNE   SUBIT4
         SPACE 1
SUBIT3   BCT   R5,SUBIT2           NEXT BIT
         LA    R3,4(0,R3)          NEXT WORD
         BCT   R8,SUBIT1
         B     EXIT
         SPACE 1
SUBIT4   LA    R1,0(0,R4)          SU BIT TABLE MESSAGE
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SR    R6,R6
         B     SUBIT3
         SPACE 1
SUBIT5   CLC   12(9,R4),=CL9'-OBSOLETE'
         BE    SUBIT3
         SPACE 1
SUBIT6   CLI   12(R4),C' '         VALID SU
         BNE   SUBIT4              YES
         B     SUBIT3
         SPACE 3
EXIT     EQU   *
         ESTAE 0
         L     R0,SIZE
         L     R14,SAVRET
         LR    R1,R13
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESP03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     EXIT
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORK)
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
SYSOVLY  DC    X'00340100'
         DC    CL48'SYSTEM -  XXXX    CPU -  XXXX    VS2 REL -  XXXX'
         EJECT
SUTABLE  DC    X'00340100',CL48' '
 DC X'00340100',CL48'SU1     VTAM RELEASE 2            FMID(EVT0108)'
 DC X'00340100',CL48'SU2     -OBSOLETE-                             '
 DC X'00340100',CL48'SU3     -OBSOLETE-                             '
 DC X'00340100',CL48'SU4     SCHEDULER IMPROVEMENTS                 '
 DC X'00340100',CL48'SU5     SUPERVISOR PERF. # 1                   '
 DC X'00340100',CL48'SU6     -OBSOLETE-                             '
 DC X'00340100',CL48'SU7     SUPERVISOR PERF. # 2                   '
 DC X'00340100',CL48'SU8     DATA MANAGEMENT                        '
 DC X'00340100',CL48'SU9     SAM-E  5740-AM3    (PP)   FMID(JDM1112)'
 DC X'00340100',CL48'SU10    3800 SUPPORT                           '
 DC X'00340100',CL48'SU11    TSO CMD 5740-XT6   (PP)   FMID(JBB1112)'
 DC X'00340100',CL48'SU12    -OBSOLETE-                             '
 DC X'00340100',CL48'SU13    -OBSOLETE-                             '
 DC X'00340100',CL48'SU14    -OBSOLETE-                             '
 DC X'00340100',CL48'SU15    -OBSOLETE-                             '
 DC X'00340100',CL48'SU16    SCHEDULER/IOS SUPPORT                  '
 DC X'00340100',CL48'SU17    SERVICE DATA IMPROV.                   '
 DC X'00340100',CL48'SU18    -OBSOLETE-                             '
 DC X'00340100',CL48'SU19    -OBSOLETE-                             '
 DC X'00340100',CL48'SU20    -OBSOLETE-                             '
 DC X'00340100',CL48'SU21    -OBSOLETE-                             '
 DC X'00340100',CL48'SU22    -OBSOLETE-                             '
 DC X'00340100',CL48'SU23    JES2 NJE3 5740-XR8 (PP)   FMID(JJE1112)'
 DC X'00340100',CL48'SU24    3850 MSS R3 IMPROVMENTS   FMID(EMS1102)'
 DC X'00340100',CL48'SU25    JES2 R4.1                 FMID(EJE1102)'
 DC X'00340100',CL48'SU26    JES3 R3                   FMID(EJS1102)'
 DC X'00340100',CL48'SU27    EREP                                   '
 DC X'00340100',CL48'SU28                                           '
 DC X'00340100',CL48'SU29    3838 SUPPORT                           '
 DC X'00340100',CL48'SU30    3895 SUPPORT                           '
 DC X'00340100',CL48'SU31                                           '
 DC X'00340100',CL48'SU32    RACF                                   '
 DC X'00340100',CL48'SU33    DUMP IMPROVEMENTS                      '
 DC X'00340100',CL48'SU34    ACF/VTAM 5735-RC2  (PP)   FMID(JVT1122)'
 DC X'00340100',CL48'SU35    ACF/VTAM 5735-RC2  (PP)   FMID(JVT1112)'
 DC X'00340100',CL48'SU36    TCAM 10  NCP/VS DIRECT    FMID(ETC0108)'
 DC X'00340100',CL48'SU37    SSS R5                    FMID(ESS1102)'
 DC X'00340100',CL48'SU38                                           '
 DC X'00340100',CL48'SU39    CRYPT0   5740-XY5  (PP)   FMID(HCR1102)'
 DC X'00340100',CL48'SU40    ACF/VTAM SCP              FMID(EVT1102)'
 DC X'00340100',CL48'SU41    DASDR              (PP)   FMID(JDS1112)'
 DC X'00340100',CL48'SU42    3800 OFFL 5748-UT2 (PP)   FMID(JUT1112)'
 DC X'00340100',CL48'SU43    -OBSOLETE-                             '
 DC X'00340100',CL48'SU44    ACF/TCAM 5735-RC1  (PP)   FMID(JCT1122)'
 DC X'00340100',CL48'SU45    NOSP  5734-XX2     (PP)   FMID(HNO1102)'
 DC X'00340100',CL48'SU46    ACF/TCAM 5735-RC1  (PP)   FMID(JTC1112)'
 DC X'00340100',CL48'SU47    -OBSOLETE-                             '
 DC X'00340100',CL48'SU48    3800 12 LINES/INCH                     '
 DC X'00340100',CL48'SU49    -OBSOLETE-                             '
 DC X'00340100',CL48'SU50    MVS/SE R1 5740-XE1 (PP)   FMID(JBB1122)'
 DC X'00340100',CL48'SU51    PROCESSOR SUPPORT # 1                  '
 DC X'00340100',CL48'SU52    -OBSOLETE-                             '
 DC X'00340100',CL48'SU53    RMF2 R2.0 5740-XY4 (PP)   FMID(HRM2205)'
 DC X'00340100',CL48'SU54    ACF/VTAM CRYPTO    (PP)   FMID(JVT1132)'
 DC X'00340100',CL48'SU55    HARDWARE RECOVERY IMP.                 '
 DC X'00340100',CL48'SU56    VPSS   5744-CK1    (PP)   FMID(EVP1102)'
 DC X'00340100',CL48'SU57    IPCS                      FMID(EIP1102)'
 DC X'00340100',CL48'SU58    TSO/VTAM LEVEL 2          FMID(ETV0108)'
 DC X'00340100',CL48'SU59    ACF/TCAM SCP  TCAM11      FMID(ETC1102)'
 DC X'00340100',CL48'SU60    DATA MANAGEMENT SUPPORT                '
 DC X'00340100',CL48'SU61    RMF2 R2.2 5740-XY4 (PP)   FMID(HRM2204)'
 DC X'00340100',CL48'SU62    RACF V1 R3         (PP)   FMID(HRF1302)'
 DC X'00340100',CL48'SU63    -OBSOLETE-                             '
 DC X'00340100',CL48'SU64    PROCESSOR SUPPORT # 2                  '
 DC X'00340100',CL48'SU65    MVS/SE WITH SU64                       '
 DC X'00340100',CL48'SU66    HSM SUPPED BY SU99 (PP)   FMID(HHM1102)'
 DC X'00340100',CL48'SU67    SMP4                                   '
 DC X'00340100',CL48'SU68    DEMF                      FMID(EDE1102)'
 DC X'00340100',CL48'SU69                                           '
 DC X'00340100',CL48'SU70                                           '
 DC X'00340100',CL48'SU71                                           '
 DC X'00340100',CL48'SU72    AMS CRYPT 5740-AM8 (PP)   FMID(JDM1122)'
 DC X'00340100',CL48'SU73                                           '
 DC X'00340100',CL48'SU74                                           '
 DC X'00340100',CL48'SU75                                           '
 DC X'00340100',CL48'SU76                                           '
 DC X'00340100',CL48'SU77                                           '
 DC X'00340100',CL48'SU78    TSO SESION MANAGER (PP)   FMID(HTS1202)'
 DC X'00340100',CL48'SU79                                           '
 DC X'00340100',CL48'SU80                                           '
 DC X'00340100',CL48'SU81                                           '
 DC X'00340100',CL48'SU82                                           '
 DC X'00340100',CL48'SU83                                           '
 DC X'00340100',CL48'SU84    DEVICE SUPPORT FACILITY                '
 DC X'00340100',CL48'SU85                                           '
 DC X'00340100',CL48'SU86                                           '
 DC X'00340100',CL48'SU87                                           '
 DC X'00340100',CL48'SU88                                           '
 DC X'00340100',CL48'SU89                                           '
 DC X'00340100',CL48'SU90                                           '
 DC X'00340100',CL48'SU91                                           '
 DC X'00340100',CL48'SU92                                           '
 DC X'00340100',CL48'SU93                                           '
 DC X'00340100',CL48'SU94                                           '
 DC X'00340100',CL48'SU95    RMF SPPTZ (MVS/SE) (PP)                '
 DC X'00340100',CL48'SU96    RMF SPPTZ (NON/SE) (PP)                '
 DC X'00340100',CL48'SU97                                           '
 DC X'00340100',CL48'SU98                                           '
 DC X'00340100',CL48'SU99    HSM                (PP)   FMID(HHM1202)'
 DC X'00340100',CL48'SU100                                          '
 DC X'00340100',CL48'SU101                                          '
 DC X'00340100',CL48'SU102                                          '
 DC X'00340100',CL48'SU103                                          '
 DC X'00340100',CL48'SU104                                          '
 DC X'00340100',CL48'SU105                                          '
 DC X'00340100',CL48'SU106                                          '
 DC X'00340100',CL48'SU107                                          '
 DC X'00340100',CL48'SU108                                          '
 DC X'00340100',CL48'SU109                                          '
 DC X'00340100',CL48'SU110                                          '
 DC X'00340100',CL48'SU111                                          '
 DC X'00340100',CL48'SU112                                          '
 DC X'00340100',CL48'SU113                                          '
 DC X'00340100',CL48'SU114                                          '
 DC X'00340100',CL48'SU115                                          '
 DC X'00340100',CL48'SU116                                          '
 DC X'00340100',CL48'SU117                                          '
 DC X'00340100',CL48'SU118                                          '
 DC X'00340100',CL48'SU119                                          '
 DC X'00340100',CL48'SU120                                          '
 DC X'00340100',CL48'SU121                                          '
 DC X'00340100',CL48'SU122                                          '
 DC X'00340100',CL48'SU123                                          '
 DC X'00340100',CL48'SU124                                          '
 DC X'00340100',CL48'SU125                                          '
 DC X'00340100',CL48'SU126                                          '
 DC X'00340100',CL48'SU127                                          '
         EJECT
WORK     DSECT
SAVRET   DS    F
XSASAVE  DS    F
TYPE     DS    F
WTOLIST  DS    CL52
         SPACE 3
ESTAELST DS    5F           AXC-CBT
WORKEND  DS    0D
         EJECT
DSXSA    DSECT
XSA      DS    0D
XAP      DS    F                  PTR TO XCTL NAME
XAD      DS    F                  DCB PTR
XAX      DS    D                  XCTL NAME
XAE      DS    0C                 ERROR CODE
XAR      DS    F                  PTR TO PARM LIST (REG1)
XAN      DS    0C                 VERB INDEX
XAL      DS    F                  PTR TO LIST POSITION
XAV      DS    D                  VERB
XAS      DS    D                  TEMP SAVE-AREA
XAU      DS    C                  UCM ENTRY INDICATOR
         DS    C                  RESERVED
XAJ      DS    H                  TJID ENTRY INDICATOR
         DS    F                  RESERVED
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
