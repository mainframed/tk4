CKPT     QSTART 'QUEUE COMMAND - READ JES2 CKPT RECORDS'
         GBLA  &QLEVEL        JES2 LEVEL. DEFINED BY QSTART MACRO.
         USING QCKPT,R10      BASE REG FOR HASP WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
***********************************************************************
*                                                                     *
*   REPOSITION DATASET                                                *
*                                                                     *
***********************************************************************
         AIF   (&QLEVEL EQ 3).RCD3
         POINT HASPCKPT,=F'1' START AT BEGINNING OF DATASET
         AGO   .RCD1
.RCD3    ANOP
         POINT HASPCKPT,TIR3  POINT PAST SYNC RECORDS
.RCD1    ANOP
         AIF   (&QLEVEL EQ 1).LVL1
         AIF   (&QLEVEL EQ 2).LVL2
         AIF   (&QLEVEL EQ 3).LVL2
         MNOTE 8,'QLEVEL IS INVALID. SEE QSTART MACRO DEFINITION.'
.LVL1    ANOP
***********************************************************************
*                                                                     *
*   READ FIRST CHECKPOINT RECORD (IT CONTAINS JQES)                   *
*                                                                     *
***********************************************************************
         L     R2,QCJQTA      ADDR OF IOAREA
         LR    R3,R2          IOAREA ADDR
         A     R3,QCJQTL      ADD LENGTH OF RECORD FOR END OF BLOCK
LOOP1    READ  HDECB1,SF,,(R2),MF=E
         CHECK HDECB1
         L     R1,HDECB1+16   ADDR OF IOB
         AH    R2,HDECB1+6    ADD BLKSIZE TO ACCUM LNGTH
         SH    R2,14(R1)      SUBTRACT RESIDUAL COUNT
         CR    R2,R3          IS THAT ALL OF THE RECORD?
         BL    LOOP1          NO. READ SOME MORE.
***********************************************************************
*                                                                     *
*   READ SECOND CHECKPOINT RECORD (IT CONTAINS JOES)                  *
*                                                                     *
***********************************************************************
         L     R2,QCJOTA      ADDR OF IOAREA
         LR    R3,R2          IOAREA ADDR
         A     R3,QCJOTL      ADD LENGTH OF RECORD FOR END OF BLOCK
LOOP2    READ  HDECB1,SF,,(R2),MF=E
         CHECK HDECB1
         L     R1,HDECB1+16   ADDR OF IOB
         AH    R2,HDECB1+6    ADD BLKSIZE TO ACCUM LNGTH
         SH    R2,14(R1)      SUBTRACT RESIDUAL COUNT
         CR    R2,R3          IS THAT ALL OF THE RECORD?
         BL    LOOP2          NO. READ SOME MORE.
         AGO   .EXIT
.LVL2    ANOP
***********************************************************************
*                                                                     *
*   READ CHECKPOINT DATASET                                           *
*                                                                     *
***********************************************************************
         L     R2,QCJQTL      ADDR OF IOAREA FOR CKPT HEADER REC
         L     R3,QCJOTL      NUMBER OF RECORDS IN CKPT DATASET
LOOP     READ  HDECB1,SF,,(R2),MF=E
         CHECK HDECB1
         AH    R2,HDECB1+6    INCREMENT TO NEXT BUFFER
         BCT   R3,LOOP        READ NEXT RECORD.
.EXIT    ANOP
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
         QSTOP
         LTORG
         DS    0F
TIR3     DC    X'00000300'    POINT PAST SYNC RECORDS
         QCOMMON
         END
