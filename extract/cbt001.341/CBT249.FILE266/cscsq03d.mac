         TITLE 'IEESQ03D -   #Q  ENQUEUE ANALYZER'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESQ03D -       #Q  ENQUEUE ANALYZER                     ***
***                                                                 ***
***       VERSION -        1.1                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITHOUT  SE2 OR SU64.     ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANDLE ESTAE           ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESQ03D CSECT
         LR    R12,R15            SET ADDRESSABILITY
         USING IEESQ03D,R12
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         USING XSA,R2
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(16),ESTAEOVL
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         ST    R14,SAVRET          PUT RETURN ADDRESS IN UNUSED SLOT
         ST    R2,XSASAVE          SAVE XSA ADDR
         MVI   FOUND,X'00'
         MVC   MAJLINE(4),=X'00360100'
         MVC   MINLINE(4),=X'00360100'
         MVC   JOBLINE(4),=X'00360100'
         MVC   MAJLINE+4(8),=CL8'MAJOR='
         MVC   MINLINE+4(8),=CL8'MINOR='
         MVC   MAJOR(44),=CL44' '
         MVC   MINOR(44),=CL44' '
         L     R3,16              CVT
         L     R3,640(0,R3)       FIRST VALID MAJOR QCB
         B     NEXTMAJ+4          1ST MAJOR PROCESS
         SPACE 1
NEXTMAJ  L     R3,0(0,R3)         NEXT MAJOR
         LTR   R3,R3              EXIST
         BZ    NOTFOUND           NO, END OF QCBS
         MVC   MAJOR(8),16(R3)    SAVE MAJOR QCB
         L     R4,8(0,R3)         1ST MINOR
         LTR   R4,R4              EXIST
         BZ    NEXTMAJ            NO
         SPACE 1
MINCHK   CLC   20(3,R4),=C'SYS'   SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         CLC   28(2,R4),=C'.T'    SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         CLI   36(R4),C'.'        SYSTEM NAME
         BNE   CHKQEL             NO, CONTINUE
         MVC   NUMCHK(6),=6C'0'   SET NUMERIC FIELD
         MVZ   NUMCHK(5),23(R4)   MOVE NUMERIC ZONES
         CLC   NUMCHK(6),=6C'0'   STILL NUMERIC
         BNE   CHKQEL             NO, NOT SYSTEM NAME
         MVZ   NUMCHK(6),30(R4)   MOVE NUMERIC ZONES
         CLC   NUMCHK(6),=6C'0'   STILL NUMERIC
         BNE   CHKQEL             NO, NOT SYSTEM NAME
         SPACE 1
NEXTMIN  L     R4,0(0,R4)         NEXT MINOR QCB
         LTR   R4,R4              EXIST
         BZ    NEXTMAJ            NO
         B     MINCHK
         SPACE 3
MINMOVE  MVC   MINOR(0),20(R4)
         EJECT
CHKQEL   SR    R5,R5
         IC    R5,16(0,R4)        MINOR NAME LENGTH
         BCTR  R5,0
         CH    R5,=H'43'          MAX
         BNH   *+8
         LH    R5,=H'43'          FORCE MAX
         MVC   MINOR(44),=CL44' '
         EX    R5,MINMOVE         MOVE MINOR NAME
         CLI   MINOR,C'9'         VALID CHAR
         BH    CHKQEL1
         CLI   MINOR,C'A'         VALID CHAR
         BL    CHKQEL1
         B     CHKQEL2
         SPACE 1
CHKQEL1  MVC   MINOR(44),=CL44'*---UNKNOWN---*'
         SPACE 1
CHKQEL2  L     R5,8(0,R4)         1ST QEL
         SPACE 1
CHKQEL3  LTR   R5,R5              EXIST
         BZ    NEXTMIN            NO
         TM    12(R5),X'80'       SHARED
         BZ    CHKQEL4            NO
         L     R5,0(0,R5)         NEXT QEL
         LA    R5,0(0,R5)
         B     CHKQEL3
         SPACE 1
CHKQEL4  C     R5,8(0,R4)         IS EXCL QEL THE 1ST QEL
         BNE   CHKQEL4A           NO
         L     R1,0(0,R5)         NEXT QEL
         LA    R1,0(0,R1)
         LTR   R1,R1              EXIST
         BZ    NEXTMIN            NO
         SPACE 1
CHKQEL4A L     R5,8(0,R4)         1ST QEL
         LA    R1,MAJLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         LA    R1,MINLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         MVI   FOUND,X'FF'        INDICATE OUTPUT
         EJECT
CHKQEL5  SR    R14,R14
         ICM   R14,3,14(R5)       ASID
         SLL   R14,2              X 4
         L     R15,16             CVT
         L     R15,556(0,R15)     ASVT
         L     R15,524(R14,R15)   ASCB
         L     R14,56(0,R15)      CSCB
         MVC   JOBNAME+8(44),=CL44' '
         CLI   28(R14),X'03'      INIT CSCB
         BE    CHKQELJB           GET JOBNAME
         SPACE 1
CHKQEL6  LA    R14,0(0,R14)
         LTR   R14,R14            CSCB EXIST
         BZ    CHKQELAS           NO, PRINT ASID
         MVC   JOBNAME(8),8(R14)
         SPACE 1
CHKQEL7  MVC   EXCLSHR(4),=CL4'SHR'
         TM    12(R5),X'80'       SHARED
         BO    CHKQEL8            YES
         MVC   EXCLSHR(4),=CL4'EXCL'
         SPACE 1
CHKQEL8  MVC   RESERVE(8),=CL8' '
         TM    12(R5),X'10'       RESERVED
         BZ    CHKQEL9            NO
         MVC   RESERVE(8),=CL8'RESERVED'
         SPACE 1
CHKQEL9  LA    R1,JOBLINE
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         L     R5,0(0,R5)         NEXT QEL
         LA    R5,0(0,R5)
         LTR   R5,R5              EXIST
         BZ    NEXTMIN            NO
         B     CHKQEL5
         EJECT
CHKQELJB LR    R15,R14            CSCB
         SR    R1,R1
         ICM   R1,3,30(R15)       ASID
         SPACE 1
CHKQELJ1 L     R14,0(0,R14)       NEXT CSCB
         LTR   R14,R14            END OF CSCBS
         BNZ   CHKQELJ2           NO
         LR    R14,R15            ORIG CSCB
         B     CHKQEL6
         SPACE 1
CHKQELJ2 CR    R14,R15            SAME CSCB
         BE    CHKQEL6            YES
         CH    R1,30(0,R14)       ASID MATCH
         BNE   CHKQELJ1           NO, CHECK NEXT CSCB
         B     CHKQEL6
         SPACE 5
CHKQELAS MVC   JOBNAME(4),=CL4'AS='
         UNPK  JOBNAME+4(5),14(3,R5)
         NC    JOBNAME+4(4),=4X'0F'
         TR    JOBNAME+4(4),=C'0123456789ABCDEF'
         MVI   JOBNAME+8,C' '
         B     CHKQEL7
         SPACE 5
NOTFOUND CLI   FOUND,X'00'        ANY OUTPUT
         BNE   EXIT
         LA    R1,NFNDMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 1
EXIT     EQU   *
         ESTAE 0
         L     R0,SIZE
         L     R14,SAVRET
         LR    R1,R13
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESQ03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     EXIT
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORK)
NFNDMSG  DC    X'00300100'
         DC    CL48'NO ENQ CONFLICTS EXIST'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
WORK     DSECT
SAVRET   DS    F
XSASAVE  DS    F
FOUND    DS    X
NUMCHK   DS    CL6
         SPACE 1
         DS    0F
MAJLINE  DS    0CL56
         DS    CL12
MAJOR    DS    CL44
         SPACE 1
         DS    0F
MINLINE  DS    0CL56
         DS    CL12
MINOR    DS    CL44
         SPACE 1
         DS    0F
JOBLINE  DS    0CL56
         DS    CL4
JOBNAME  DS    CL8
         DS    CL4
EXCLSHR  DS    CL4
         DS    CL4
RESERVE  DS    CL8
         DS    CL24
         SPACE 3
ESTAELST DS    4F
WORKEND  DS    0D
         EJECT
DSXSA    DSECT
XSA      DS    0D
XAP      DS    F                  PTR TO XCTL NAME
XAD      DS    F                  DCB PTR
XAX      DS    D                  XCTL NAME
XAE      DS    0C                 ERROR CODE
XAR      DS    F                  PTR TO PARM LIST (REG1)
XAN      DS    0C                 VERB INDEX
XAL      DS    F                  PTR TO LIST POSITION
XAV      DS    D                  VERB
XAS      DS    D                  TEMP SAVE-AREA
XAU      DS    C                  UCM ENTRY INDICATOR
         DS    C                  RESERVED
XAJ      DS    H                  TJID ENTRY INDICATOR
         DS    F                  RESERVED
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
