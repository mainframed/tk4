         TITLE 'IEESS03D -   #E  EXECUTE MVS COMMAND'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESS03D -       #E  EXECUTE MVS COMMAND                  ***
***                                                                 ***
***       VERSION -        1.2                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   TO MVS/SP1.3              ***
***                                                                 ***
***       UPDATED -        07/21/81  AXC-CBT HANDLE ESTAE           ***
***                        01/26/83  AXC-CBT CHANGE ESTAE PARM LEN  ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESS03D CSECT
         LR    R12,R15
         USING IEESS03D,R12
         L     R0,SIZE
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         XC    CMDLIST(4),CMDLIST
         MVI   CMDLIST+4,C' '
         MVC   CMDLIST+82(4),=CL4' '
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(20),ESTAEOVL   AXC-CBT
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         L     R3,XAL              LOAD SUB-PARM ADDR
         LTR   R3,R3               SUB-PARM PRESENT
         BZ    CMDERR              NO, ERROR
         SPACE 1
         CLC   0(6,R3),=CL6'V PATH'  VARY PATH COMMAND
         BE    VARY                YES
         B     COMMAND
         SPACE 1
CMDERR   LA    R1,ERRMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 3
EXIT     EQU   *
         ESTAE 0
         L     R0,SIZE             LOAD FREEMAIN SIZE
         LR    R1,R13              LOAD FREEMAIN ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14                 EXIT
         EJECT
VARY     MVC   VPTHLIM(1),13(R3)   SET UPPER LIMIT
         CLI   9(R3),C'9'
         BH    CMDERR
         CLI   9(R3),C'0'
         BNL   *+20
         CLI   9(R3),C'A'
         BL    CMDERR
         CLI   9(R3),C'F'
         BH    CMDERR
         CLI   13(R3),C'9'
         BH    CMDERR
         CLI   13(R3),C'0'
         BNL   *+20
         CLI   13(R3),C'A'
         BL    CMDERR
         CLI   13(R3),C'F'
         BH    CMDERR
         CLI   6(R3),C'('
         BNE   CMDERR
         CLI   16(R3),C')'
         BNE   CMDERR
         CLI   10(R3),C'-'
         BNE   CMDERR
         CLI   14(R3),C','
         BNE   CMDERR
         MVC   CMDLIST+5(10),0(R3)
         MVC   CMDLIST+15(63),14(R3)
         MVC   CMDLIST+76(10),=CL10' '
         LA    R9,16               MAX POSSIBLE COMMANDS
         LA    R14,CMDLIST+5
         LA    R15,CMDLIST+85
         EJECT
VARY1    CLI   0(R15),C' '         END OF COMMAND
         BNE   VARY2               YES
         CR    R14,R15             BEGIN OF COMMAND
         BE    VARY2               YES
         BCT   R15,VARY1
         SPACE 1
VARY2    SR    R15,R14
         LA    R15,11(0,R15)
         SRL   R15,2
         SLL   R15,2
         STH   R15,CMDLIST
         SPACE 1
VARY3    MVC   ISSMSG(52),ISSMSGT
         MVC   ISSMSG+18(34),CMDLIST+5
         LA    R1,CMDLIST          YES
         SR    R0,R0
         IC    R0,XAU
         SVC   34
         LA    R1,ISSMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         CLI   CMDLIST+14,C'F'           LIMIT REACHED
         BE    EXIT                      YES
         CLC   CMDLIST+14(1),VPTHLIM     LIMIT REACHED
         BE    EXIT                      YES
         SR    R1,R1
         IC    R1,CMDLIST+14
         LA    R1,1(0,R1)
         STC   R1,CMDLIST+14             SAVE NEW DEVICE
         C     R1,=X'000000FA'           WAS PREVIOUS DEV XX9
         BE    VARY4                     YES
         BCT   R9,VARY3
         B     EXIT                      NEXT VARY
         SPACE 1
VARY4    MVI   CMDLIST+14,X'C1'          FORCE SWITCH FROM 9 TO A
         B     VARY3
         EJECT
COMMAND  MVC   CMDLIST+5(77),0(R3) COMMAND
         LA    R14,CMDLIST+5
         LA    R15,CMDLIST+85
         SPACE 1
COMMAND1 CLI   0(R15),C' '         END OF COMMAND
         BNE   COMMAND2            YES
         CR    R14,R15             BEGIN OF COMMAND
         BE    COMMAND2            YES
         BCT   R15,COMMAND1
         SPACE 1
COMMAND2 SR    R15,R14
         LA    R15,11(0,R15)
         SRL   R15,2
         SLL   R15,2
         STH   R15,CMDLIST
         MVC   ISSMSG(52),ISSMSGT
         MVC   ISSMSG+18(34),0(R3)   COMMAND
         LA    R1,CMDLIST          YES
         SR    R0,R0
         IC    R0,XAU
         SVC   34
         LA    R1,ISSMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     EXIT
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESS03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     EXIT
         EJECT
         LTORG
         SPACE 5
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
ERRMSG   DC    X'00300100'
         DC    CL44'EXECUTE COMMAND PARAMETER ERROR'
         SPACE 3
ISSMSGT  DC    X'00340100'
         DC    CL48'CMD ISSUED -  X'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    F
XSASAVE  DS    F
VPTHLIM  DS    F
ISSMSG   DS    CL52
         SPACE 3
         DS    0F
CMDLIST  DS    CL86
         SPACE 3
ESTAELST DS    5F                 AXC-CBT
WORKEND  DS    0D
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAV      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
