QUEUE    QSTART 'QUEUE COMMAND - MAINLINE MODULE',MAIN=YES
***********************************************************************
*                                                                     *
*   CALL - INITIALIZATION                                             *
*                                                                     *
***********************************************************************
         L     R15,=V(INIT)   ADDR OF INIT
         BALR  R14,R15        GO TO IT
*        LA    R10,LOOP       INTERRUPTED RETURN ADDRESS     (CMDSBMON)
         LA    R10,EXIT       INTERRUPTED RETURN ADDRESS     (CMDSBMON)
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
***********************************************************************
*                                                                     *
*   CALL - READ JES2 CHECKPOINT FILE                                  *
*                                                                     *
***********************************************************************
LOOP     L     R15,=V(CKPT)   ADDR OF CKPT
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL - COMMAND LINE PARSE                                         *
*                                                                     *
***********************************************************************
         L     R15,=V(PARSE)  ADDR OF PARSE
         BALR  R14,R15        GO TO IT
         CLC   =C'E ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'EXIT ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'END ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'STOP ',QSUBNAME STOP?
         BE    EXIT           YES.
***********************************************************************
*                                                                     *
*   CALL - SUB-COMMAND MODULE SELECTED BY PARSE                       *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,DUMMY  NO OUTPUT MESSAGE
         L     R15,QSUBCMD    ADDR OF SUBCMD FROM QCOMMON
         BALR  R14,R15        GO TO IT
         MVC   QDMLNG,=H'0'   ZERO OUT MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        WRITE LAST SCREEN, GET NEXT INPUT
*        B     LOOP           DO IT AGAIN                    (CMDSBMON)
***********************************************************************
*                                                                     *
*   CLEAN UP AND GO HOME                                              *
*                                                                     *
***********************************************************************
EXIT     L     R1,QCSTART     GETMAIN AREA                   (CMDSBMON)
         L     R0,=F'65536'   GETMAIN LENGTH                 (CMDSBMON)
         LTR   R0,R0          ANYTHING TO FREE               (CMDSBMON)
         BZ    EXIT1          NO                             (CMDSBMON)
         FREEMAIN R,LV=(0),A=(1)   FREE AREA                 (CMDSBMON)
         SPACE 1
EXIT1    L     R0,QCGT1MSZ    GETMAIN LENGTH                 (CMDSBMON)
         LTR   R0,R0          ANYTHING TO FREE               (CMDSBMON)
         BZ    EXIT2          NO                             (CMDSBMON)
         L     R1,QCGT1MAD    GETMAIN ADDR                   (CMDSBMON)
         FREEMAIN R,LV=(0),A=(1)   FREE AREA                 (CMDSBMON)
         SPACE 1
EXIT2    L     R0,QCGT2MSZ    GETMAIN LENGTH                 (CMDSBMON)
         LTR   R0,R0          ANYTHING TO FREE               (CMDSBMON)
         BZ    EXIT3          NO                             (CMDSBMON)
         L     R1,QCGT2MAD    GETMAIN ADDR                   (CMDSBMON)
         FREEMAIN R,LV=(0),A=(1)   FREE AREA                 (CMDSBMON)
         SPACE 1
EXIT3    L     R0,QCGT3MSZ    GETMAIN LENGTH                 (CMDSBMON)
         LTR   R0,R0          ANYTHING TO FREE               (CMDSBMON)
         BZ    EXIT4          NO                             (CMDSBMON)
         L     R1,QCGT3MAD    GETMAIN ADDR                   (CMDSBMON)
         FREEMAIN R,LV=(0),A=(1)   FREE AREA                 (CMDSBMON)
EXIT4    EQU   *                                             (CMDSBMON)
         SPACE 1
*        TPUT  CLEAR,8,FULLSCR CLEAR THE SCREEN              (CMDSBMON)
         USING QCKPT,R8 BASE REG FOR CKPT WORK AREA
         L     R8,QVCKPT      LOAD BASE REG
         CLOSE MF=(E,HOCKPT)
         CLOSE MF=(E,QCSPOOLS)
         DELETE EP=CSCGQQ00   DELETE QUEUECMN                (CMDSBMON)
         QSTOP
***********************************************************************
*                                                                     *
*   CONSTANTS AND OTHER ODDITIES                                      *
*                                                                     *
***********************************************************************
         LTORG
CLEAR    DC    X'C11140403C404000'
DUMMY    DC    CL80'    NO DATA IS AVAILABLE FOR YOUR REQUEST'
         QCOMMON
         END
