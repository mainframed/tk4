         TITLE 'IEESF03D -   #F  DISPLAY STORAGE USAGE'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       IEESF03D -       #F  DISPLAY STORAGE USAGE                ***
***                                                                 ***
***       VERSION -        1.2                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/SE  3.8.   WITHOUT  SE2 OR SU64.     ***
***                                                                 ***
***       UPDATED -        07/19/81 AXC-CBT HANDLE ESTAE            ***
***                                                                 ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEESF03D CSECT
         LR    R12,R15
         USING IEESF03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         XC    OUTMSLIM(4),OUTMSLIM
         USING XSA,R2
         MVI   VOSWIT,X'00'        CLEAR OFFLINE SWITCH
         MVI   VBSWIT,X'00'        CLEAR BAD FRAME SWITCH
         MVI   OUTMSG+4,C' '
         MVC   OUTMSG+5(43),OUTMSG+4
         LR    R10,R13             LOAD SAVE AREA ADDR
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(16),ESTAEOVL
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         L     R3,XAL              LOAD LIST POINTER
         LA    R3,0(0,R3)          CLEAR HIGH ORDER
         LTR   R3,R3               PARM EXIST
         BZ    PFTLIST             NO, GO PROCESS PFT LIST
         CLI   0(R3),C'B'          BAD FRAME REQUEST
         BE    VBDISP              YES, GO PROCESS
         CLI   0(R3),C'C'          CSA DISPLAY REQUEST
         BE    VCDISP              YES, GO PROCESS
         CLI   0(R3),C'R'          V=R DISPLAY REQUEST
         BE    VRDISP              YES, GO PROCESS
         CLI   0(R3),C'M'          MEGABYTE DISPLAY REQUEST
         BE    VVDISP              YES, GO PROCESS
         CLI   0(R3),C'O'          OFFLINE INTERCEPT REQUEST
         BE    VODISP              YES, GO PROCESS
         LA    R11,RETURN          LOAD RET ADDR
         LA    R1,ERRMSG           PARM ERROR
         B     PRINT               GO ISSUE WTO
         EJECT
PFTLIST  MVC   OUTMSG(4),=X'00300100'
         L     R14,16              LOAD CVT ADDR
         L     R6,556(0,R14)       LOAD ASVT ADDR
         L     R7,516(0,R6)        MAX NO. OF ASCBS
         LA    R6,524(0,R6)        1ST ASCB SLOT
         L     R3,356(0,R14)       LOAD PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AH    R4,16(0,R3)         PFT ACTUAL ADDR
         SR    R5,R5               INDEX
         IC    R5,18(0,R3)            TO
         SLL   R5,8                      END OF
         IC    R5,19(0,R3)                  PFT
         SH    R5,16(0,R3)
         SRL   R5,4                DIVIDE BY 16
         STM   R4,R5,PFTREGS       SAVE PFT REGS
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SRL   R14,4               DIVIDE BY 16
         AH    R14,8(0,R3)         NON-NUCLEUS FRAMES
         CVD   R14,DOUBLE          CONVERT TO DECIMAL
         MVC   OUTMSG+4(12),=CL12'TOTAL'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SRL   R14,4               DIVIDE BY 16
         CVD   R14,DOUBLE          CONVERT TO DECIMAL
         MVC   OUTMSG+4(12),=CL12'NUCLEUS'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         L     R2,=F'-1'
         BAL   R11,PGPRINT         GO PRINT
         LH    R14,1830(0,R3)      TOTAL FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'TOT FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         L     R2,=F'-1'
         BAL   R11,PGPRINT
         LH    R14,1832(0,R3)      TOTAL SQA FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'SQA FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         L     R2,=F'-1'
         BAL   R11,PGPRINT
         LH    R14,1834(0,R3)      COMMON FIXED
         CVD   R14,DOUBLE
         MVC   OUTMSG+4(12),=CL12'CMN FIXED'
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         L     R2,=F'-1'
         BAL   R11,PGPRINT
         L     R10,=X'0000FFFF'
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         L     R2,XSASAVE
         EJECT
GETASCB  LA    R6,4(0,R6)          NEXT SLOT
         BCT   R7,ASCBFND          GO PROCESS
         SPACE 3
RETURN   EQU   *
         ESTAE 0
         L     R0,SIZE
         LR    R1,R13              WORK AREA ADDR
         L     R14,SAVRET          LOAD RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         SR    R15,R15
         BR    R14
         SPACE 3
ASCBFND  TM    0(R6),X'80'         AVAILABLE SLOT
         BO    GETASCB             YES, GET NEXT SLOT
         L     R8,0(0,R6)          LOAD ASCB ADDR
         SR    R10,R10             LOAD
         IC    R10,36(0,R8)
         SLL   R10,8                  ASID
         IC    R10,37(0,R8)
         BAL   R11,PAGEPRNT        GO COUNT AND FORMAT
         L     R2,XSASAVE
         B     GETASCB
         EJECT
PAGEPRNT LM    R4,R5,PFTREGS       LOAD PFT REGS
         SR    R14,R14             CLEAR FRAME COUNT
         SR    R2,R2               CLEAR LSQA COUNT
         SR    R9,R9               CLEAR AVAILABLE FRAME COUNT
         SPACE 3
PAGEPR1  SR    R15,R15             LOAD
         IC    R15,0(0,R4)            PFT
         SLL   R15,8                     ASID
         IC    R15,1(0,R4)
         CR    R10,R15             ASID MATCH
         BNE   PAGEPR2             NO, DONT INCR FRAME COUNT
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   *+12
         TM    12(R4),X'80'        AVAILABLE PAGE
         BO    AVAILCNT            GO ADD TO AVAILABLE COUNT
         LA    R14,1(0,R14)        ADD 1 TO FRAME COUNT
         TM    12(R4),X'20'        LSQA FRAME
         BZ    PAGEPR2             NO
         LA    R2,1(0,R2)          ADD 1 T0 LSQA FRAME COUNT
         SPACE 3
PAGEPR2  LA    R4,16(0,R4)         NEXT PFT
         BCT   R5,PAGEPR1          PROCESS NEXT
         MVC   OUTMSG+4(12),=CL12'ASID'
         C     R10,=X'00000001'    MASTER
         BE    ASIDMSTR            YES, GO PRINT MASTER
         C     R10,=X'0000FFFF'    SYS PAGES
         BNE   PAGEPR3             NO, ASID
         MVC   OUTMSG+4(12),=CL12'SYS PAGES'
         B     PAGEPR4
         SPACE 3
PAGEPR3  LTR   R14,R14             ANY FRAMES
         BCR   8,R11               NO, BYPASS PRINT
         STH   R10,DOUBLE+6        SAVE ASID
         UNPK  DOUBLE(5),DOUBLE+6(3)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+9(4),DOUBLE  ASID TO MSG
         SPACE 3
PAGEPR4  CVD   R14,DOUBLE
         MVC   OUTMSG+15(5),=X'2020202120'
         ED    OUTMSG+14(6),DOUBLE+5
         CLC   OUTMSG+4(4),=C'ASID'
         BE    JOBFIND
         EJECT
PGPRINT  LA    R1,OUTMSG           LOAD OUTPUT MSG ADDR
         CVB   R14,DOUBLE
         SLL   R14,2               CHG FRAMES TO K
         CVD   R14,DOUBLE          CONVERT TO DECIMAL
         MVC   OUTMSG+22(7),=X'402020202120D2'
         ED    OUTMSG+22(6),DOUBLE+5
         MVI   OUTMSG+32,C' '
         MVC   OUTMSG+33(15),OUTMSG+32
         LTR   R2,R2               LSQA/SQA COUNT
         BM    PRINT               NO
         SLL   R2,2                CHG FRAMES TO K
         CVD   R2,DOUBLE           CONVERT TO DECIMAL
         MVC   OUTMSG+35(5),=C'LSQA='
         CLC   OUTMSG+4(12),=CL12'SYS PAGES'
         BNE   PGPRINT1            NOT SYS PAGES
         MVC   OUTMSG+35(5),=C'SQA= '
         SPACE 1
PGPRINT1 MVC   OUTMSG+40(7),=X'402020202120D2'
         ED    OUTMSG+40(6),DOUBLE+5
         SPACE 3
PRINT    SR    R0,R0
         L     R2,XSASAVE
         IC    R0,XAU
         SVC   35
         CLC   OUTMSG+4(12),=CL12'SYS PAGES'
         BCR   7,R11               NOT SYS PAGES, NOT AVAILABLE
         MVC   OUTMSG+4(12),=CL12'AVAILABLE'
         LR    R14,R9              AVAILABLE COUNT
         L     R2,=F'-1'
         B     PAGEPR4             GO FORMAT
         SPACE 3
AVAILCNT LA    R9,1(0,R9)          ADD 1 TO COUNT
         B     PAGEPR2
         SPACE 3
ASIDMSTR MVC   OUTMSG+4(12),=CL12'MASTER'
         B     PAGEPR4
         SPACE 3
VONONE   LA    R1,VOMSG            NO OFFLINE INTERCEPT MSG
         LA    R11,RETURN
         B     PRINT
         SPACE 3
VBNONE   LA    R1,VBMSG            NO BAD FRAMES MSG
         LA    R11,RETURN
         B     PRINT
         EJECT
JOBFIND  EQU   *
         L     R9,56(0,R8)         CSCB ADDR
         CLI   28(R9),X'03'        INIT
         BE    JOBFIND1            YES, GO SEARCH
         SPACE
JOBFIND0 MVC   OUTMSG+4(8),8(R9)   JOBNAME
         MVI   OUTMSG+12,C' '
         B     PGPRINT
         SPACE
JOBFIND1 LA    R1,0(0,R9)          SAVE ADDR
         SR    R14,R14             LOAD
         IC    R14,30(0,R9)
         SLL   R14,8                  ASID
         IC    R14,31(0,R9)
         SPACE
JOBFIND2 L     R9,0(0,R9)          NEXT CSCB
         LA    R9,0(0,R9)
         LTR   R9,R9               END OF CHAIN
         BNZ   JOBFIND3            NO, CONTINUE
         LR    R9,R1               USE ORIG CSCB
         B     JOBFIND0
         SPACE
JOBFIND3 CR    R9,R1               END OF CHAIN
         BE    JOBFIND0            YES, USE ORIG CSCB
         CH    R14,30(0,R9)        ASID EQUAL
         BE    JOBFIND0            YES, USE THIS CSCB
         B     JOBFIND2
         EJECT
VRDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVC   OUTMSG(20),VRHDR1   SET 1ST HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVC   OUTMSG(20),VRHDR2   SET 2ND HDR
         BAL   R11,VRDISP5D        GO WTO IT
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AH    R4,20(0,R3)         PFT V=R ACTUAL ADDR
         LH    R5,22(0,R3)         INDEX TO V=R END
         SH    R5,20(0,R3)         SUBTRACT V=R START
         SRL   R5,4                NO. OF V=R PAGES
         LA    R5,1(0,R5)          ADJUST NO OF FRAMES
         MVI   VRSWIT,X'00'        CLEAR V=R END SWITCH
         MVC   OUTMSG(20),VRMSG    SET UP V=R MSG BASE
         B     VRDISP3             GO PROCESS
         SPACE 3
VRDISP1  CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BE    VRDISP1O            NO
         TM    12(R4),X'01'        OFFLINE INTERCEPT FLAG BIT ON
         BNO   VRDISP2O            NO, NEXT PFTE
         SPACE 3
VRDISP1O CLC   0(2,R4),VRASID      ASID MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         CLC   12(0,R4),VRFLAGS    FLAGS MATCH
         BNE   VRDISP4             NO, GO FORMAT AND PRINT
         SPACE 3
VRDISP2  L     R14,VRNUM           ADD 1
         LA    R14,1(0,R14)           TO EQUAL
         ST    R14,VRNUM                 ENTRY COUNT
         SPACE 3
VRDISP2O LA    R4,16(0,R4)         NEXT PFTE
         BCT   R5,VRDISP1
         MVI   VRSWIT,X'FF'        SET END OF V=R SWITCH
         B     VRDISP4             GO FORMAT AND PRINT LAST
         SPACE 3
VRDISP3  XC    VRNUM(4),VRNUM      CLEAR COUNT
         MVC   VRASID(2),0(R4)     SET ASID
         MVC   VRFLAGS(3),12(R4)   SET FLAGS
         B     VRDISP2
         EJECT
VRDISP4  L     R14,VRNUM           NO. OF FRAMES
         CVD   R14,DOUBLE          DECIMAL
         UNPK  OUTMSG+4(3),DOUBLE+6(2)
         OI    OUTMSG+6,X'F0'
         UNPK  DOUBLE(5),VRASID(3) TRANSLATE ASID
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   OUTMSG+10(4),DOUBLE PUT ASID IN MSG
         SPACE 3
VRDISP5A MVI   OUTMSG+17,C'O'      OFFLINE
         TM    VRFLAGS+1,X'40'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'C'      OFFLINE INTERCEPT
         TM    VRFLAGS,X'01'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'R'      V=R ALLOCATED
         TM    VRFLAGS,X'02'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'I'      V=R INTERCEPT
         TM    VRFLAGS,X'40'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'A'      AVAILABLE
         TM    VRFLAGS,X'80'
         BO    VRDISP5B
         MVI   OUTMSG+17,C'-'      NO STATUS
         SPACE 3
VRDISP5B MVI   OUTMSG+18,C'B'      BAD PAGE
         TM    VRFLAGS,X'04'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'F'      LONG TERM FIXED
         TM    VRFLAGS,X'10'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'S'      LSQA/SQA PAGE
         TM    VRFLAGS,X'20'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'V'      VIO PAGE
         TM    VRFLAGS+1,X'10'
         BO    VRDISP5C
         MVI   OUTMSG+18,C'-'      NO STATUS
         EJECT
VRDISP5C MVI   OUTMSG+19,C'A'      AVAILABLE
         TM    VRFLAGS+2,X'FF'
         BZ    VRDISP5D
         MVI   OUTMSG+19,C'N'      NOT QUEUED
         BO    VRDISP5D
         MVI   OUTMSG+19,C'S'      SQA
         TM    VRFLAGS+2,X'C0'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'L'      LSQA
         TM    VRFLAGS+2,X'84'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'R'      SQA RESERVED
         TM    VRFLAGS+2,X'04'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'C'      COMMON
         TM    VRFLAGS+2,X'08'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'U'      LOCAL
         TM    VRFLAGS+2,X'80'
         BO    VRDISP5D
         MVI   OUTMSG+19,C'-'      NO STATUS
         SPACE 3
VRDISP5D LA    R1,OUTMSG
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         TM    VRSWIT,X'FF'        TEST HDR/END SWITCH
         BO    RETURN              END
         BCR   4,R11               HDR
         B     VRDISP3
         EJECT
VBDISP   MVI   VBSWIT,X'FF'        SET BAD FRAME ONLY SW
         B     VVDISP
         SPACE 3
VODISP   MVI   VOSWIT,X'FF'        SET OFFLINE INTERCEPT ONLY SW
         SPACE 3
VVDISP   MVI   VRSWIT,X'F0'        SET HDR SWITCH
         MVI   DOUBLE,X'F0'        TEST
         MVZ   DOUBLE(1),1(R3)        NUMERIC
         CLI   DOUBLE,X'F0'              MEGABYTE NUMBER
         BNE   VVDISP1             NOT NUMERIC, ERROR
         MVC   OUTMSG(20),VRHDR1   1ST HDR
         BAL   R11,VRDISP5D
         MVC   OUTMSG(20),VRHDR2   2ND HDR
         BAL   R11,VRDISP5D
         MVI   VRSWIT,X'00'        CLEAR HDR SWITCH
         MVC   OUTMSG(20),VRMSG    BASE MSG
         B     VVDISP2
         EJECT
VVDISP1  LA    R11,RETURN          PARM
         LA    R1,ERRMSG              ERROR
         B     PRINT
         SPACE 3
VVDISP2  IC    R5,1(0,R3)          CALCULATE
         SLL   R5,28                  PFTE INDEX
         SRL   R5,16                     TO THIS MEG
         LTR   R5,R5               ZERO MEG
         BZ    VVDISP1             YES, ERROR
         L     R14,16              CVT ADDR
         L     R3,356(0,R14)       PVT ADDR
         L     R4,12(0,R3)         PFT APPARENT ADDR
         AR    R4,R5               PFT MEG START ADDR
         SR    R1,R1
         ICM   R1,3,18(R3)         LAST FRAME INDEX
         CR    R5,R1               PAST END OF PFT
         BH    VVDISP1             YES, ERROR
         LA    R5,256              NO. OF FRAMES IN × MEG
         CLI   VOSWIT,X'00'        OFFLINE INTERCEPT REQUEST
         BNE   VODISP1             YES, CONTINUE PROCESS
         CLI   VBSWIT,X'00'        BAD FRAME REQUEST
         BNE   VBDISP1             YES, CONTINUE PROCESS
         B     VRDISP3             GO PROCESS
         SPACE 3
VODISP1  TM    12(R4),X'01'        OFFLINE INTERCEPT FLAG BIT ON
         BO    VRDISP3             YES, GO CONTINUE PROCESS
         LA    R4,16(0,R4)         NEXT PFTE
         BCT   R5,VODISP1          GO CHECK NEXT
         B     VONONE              NO INTERCEPT OFFLINE, GO MSG
         SPACE 3
VBDISP1  TM    12(R4),X'04'        BAD FRAME BIT ON
         BO    VRDISP3             YES, GO CONTINUE PROCESS
         LA    R4,16(0,R4)         NEXT PFTE
         BCT   R5,VBDISP1          GO CHECK NEXT
         B     VBNONE              NO INTERCEPT OFFLINE, GO MSG
         EJECT
VCDISP   L     R1,16               CVT
         L     R3,356(0,R1)        LOAD PVT ADDR
         L     R1,560(0,R1)        GDA
         LR    R6,R1               SAVE GDA ADDR
         L     R4,8(0,R6)          CSA PQE
         L     R5,0(0,R4)          1ST FBQE FOR CSA
         SR    R7,R7
         CR    R4,R5               END OF CHAIN
         BE    VCDISPB             YES
         SPACE 1
VCDISPA  A     R7,8(0,R5)          ADD FREE AREA
         L     R5,0(0,R5)          NEXT FBQE
         CR    R4,R5               END OF CHAIN
         BNE   VCDISPA             NO
         L     R5,0(0,R4)          1ST FBQE FOR CSA
         SPACE 1
VCDISPB  MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'CSA SIZE ='
         L     R1,20(0,R4)         CSA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'    CSA ADDR ='
         UNPK  CSADISP+42(7),25(4,R4)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         EJECT
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'SQA FREE ='
         L     R1,28(0,R6)         SQA AVAIL
         SR    R1,R7               SUBTRACT CSA
         BP    *+6                 IF NOT NEGATIVE, BYPASS
         SR    R1,R1
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'   SQA AVAIL ='
         L     R1,28(0,R6)         SQA AVAIL
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+41(8),=X'402020212020D240'
         ED    CSADISP+41(6),DOUBLE+5
         MVC   CSADISP+49(3),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'NUC END ='
         LH    R14,16(0,R3)        NUCLEUS FRAMES
         SLL   R14,8               NUCLEUS END ADDR
         ST    R14,DOUBLE
         UNPK  CSADISP+19(7),DOUBLE+1(4)
         NC    CSADISP+19(6),=8X'0F'
         TR    CSADISP+19(6),=C'0123456789ABCDEF'
         MVC   CSADISP+25(17),=CL17'     SEG END ='
         UNPK  CSADISP+42(7),17(4,R6)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 1
VCDISPC  STM   R4,R5,VCSASAV
         L     R4,12(0,R6)         V=R PQE
         L     R5,0(0,R4)          1ST FBQE FOR V=R
         SPACE 1
VCDISPD  MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'V=R SIZE ='
         L     R1,20(0,R4)         V=R SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         MVC   CSADISP+25(17),=CL17'    V=R ADDR ='
         UNPK  CSADISP+42(7),25(4,R4)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         L     R15,20(0,R4)        V=R SIZE
         A     R15,24(0,R4)        DYNAMIC AREA ADDR
         ST    R15,DOUBLE
         S     R15,16(0,R6)        SUB PRIVATE AREA ORIGIN
         MVC   CSADISP+25(17),=CL17'    PRV ADDR ='
         UNPK  CSADISP+42(7),DOUBLE+1(4)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(14),=CL14'PRV SIZE ='
         L     R1,20(0,R6)         PRIVATE AREA SIZE
         SR    R1,R15
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+18(8),=X'402020212020D240'
         ED    CSADISP+18(6),DOUBLE+5
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         SPACE 1
         EJECT
VCDISP0  LM    R4,R5,VCSASAV
         L     R1,24(0,R4)         CSA ADDR
         C     R1,12(0,R5)         1ST CHUNK OF CSA FREE
         BE    VCDISP1             YES
         L     R14,12(0,R5)        1ST FREE AREA ADDR
         B     VCDISP3             GO PROCESS ALLOCATED CHUNK
         SPACE 1
VCDISP1  CR    R4,R5               END OF FBQES
         BE    RETURN              YES
         MVC   CSADISP(4),=X'00340100'
         L     R1,8(0,R5)          FREE AREA SIZE
         SRL   R1,10               DIVIDE BY 1K
         CVD   R1,DOUBLE
         MVC   CSADISP+4(30),=CL30'FREE        SIZE='
         MVC   CSADISP+34(9),=CL9'ADDR ='
         MVC   CSADISP+22(8),=X'402020202120D240'
         ED    CSADISP+22(6),DOUBLE+5
         UNPK  CSADISP+42(7),13(4,R5)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0 SIZE =
         IC    R0,XAU
         SVC   35
         L     R1,OUTMSLIM
         LA    R1,1(0,R1)
         ST    R1,OUTMSLIM
         C     R1,=F'100'           200 +  LINES OF OUTPUT
         BH    RETURN               YES, THATS ALL.  AVOID SQA FILL
         EJECT
VCDISP2  L     R1,12(0,R5)         FREE AREA ADDR
         A     R1,8(0,R5)          ADD FREE AREA LENGTH
         L     R5,0(0,R5)          NEXT FBQE
         CR    R4,R5               END OF FBQES
         BE    VCDISP4             YES
         L     R14,12(0,R5)        NEXT FREE AREA ADDR
         SPACE 3
VCDISP3  SR    R14,R1
         SRL   R14,10              USED AREA LENGTH
         CVD   R14,DOUBLE
         ST    R1,DOUBLE           USED AREA ADDR
         MVC   CSADISP(4),=X'00340100'
         MVC   CSADISP+4(30),=CL30'ALLOCATED   SIZE='
         MVC   CSADISP+34(9),=CL9'ADDR ='
         MVC   CSADISP+22(8),=X'402020202120D240'
         ED    CSADISP+22(6),DOUBLE+5
         UNPK  CSADISP+42(7),DOUBLE+1(4)
         NC    CSADISP+42(6),=8X'0F'
         TR    CSADISP+42(6),=C'0123456789ABCDEF'
         MVC   CSADISP+48(4),=CL4' '
         LA    R1,CSADISP
         SR    R0,R0
         IC    R0,XAU
         SVC   35
         B     VCDISP1
         SPACE  3
VCDISP4  L     R14,24(0,R4)        CSA ADDR
         A     R14,20(0,R4)        ADD CSA LENGTH
         B     VCDISP3
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEESF03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
         SVC   35                  ISSUE WTO
         B     RETURN
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
ERRMSG   DC    X'001C0100'
         DC    CL24'PARAMETER ERROR'
         SPACE 3
STAEMSG  DC    X'00360100'
         DC    CL50'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 3
         DS    0F
VRHDR1   DC    X'00140100'
         DC    CL16'PAGES.ASID.FLAGS'
         SPACE 3
         DS    0F
VRHDR2   DC    X'00140100'
         DC    16C'-'
         SPACE 3
         DS    0F
VRMSG    DC    X'00140100'
         DC    CL16'   ...    ...   '
         SPACE 3
         DS    0F
VBMSG    DC    X'00220100'
         DC    CL30'NO BAD FRAMES'
         SPACE 3
         DS    0F
VOMSG    DC    X'00220100'
         DC    CL30'NO FRAMES INTERCEPTED OFFLINE'
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
VCSASAV  DS    2F
XSASAVE  DS    F
PFTREGS  DS    2F
VRNUM    DS    F
VRFLAGS  DS    CL3
VRSWIT   DS    C
VOSWIT   DS    C
VBSWIT   DS    C
VRASID   DS    H
         DS    0F
OUTMSG   DS    48C
         SPACE 1
         DS    0F
CSADISP  DS    CL52
OUTMSLIM DS    F
ESTAELST DS    4F
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAV      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
