***********************************************************************
*  Rexx Function Library interface to PCR
*
*  - Can be renamed to RXUSERFN, RXLOCFN or RXSYSFN
*  - Autoloads PLI program that are Rexx Funs/Subs via PCR LOAD
*
***********************************************************************
*
RXUSERFN CSECT
         REGEQU
         USING    *,R12
         XR       R15,R15      ;This will default RC to 0
         STM      R14,R12,12(R13)
         LR       R10,R0       ;Save these Regs
         LR       R11,R1       ;Save these Regs
*
* Load as Nucleus Extension
*
         L        R9,MODSTAT
         BR       R9           ;Either NOLMOD or LMOD
LMOD     EQU      *
         LA       R1,LMODPL    ;Get parms for Nucxloading
         MVC      LMPNAME,0(R11)
         SVC      202          ;..
         DC       AL4(1)       ;Check error ourself
         LTR      R15,R15      ;Was it good
         BNE      FIXLMOD      ;No, must already be an extension
         LR       R1,R11
         LR       R0,R10
         SVC      202          ;Invoke the now Nucxloaded module
         DC       AL4(1)
         ST       R15,16(R13)  ;save return
         LM       R14,R12,12(R13) ;And leave...
         BR       R14
FIXLMOD  EQU      *
         LA       R9,NOLMOD
         ST       R9,MODSTAT   ;Now Nucxload will to be done again
NOLMOD   EQU      *
         CLI      8(R11),X'FF'  ;Any parms
         BE       LEAVE        ;No, load and leave
         CLC      8(8,R11),=CL8'LOAD'
         BE       LOADREQ      ;Do Load request
         CLC      8(8,R11),=CL8'RESET'
         BE       RESETREQ     ;Do Reset request
         CLM      R11,B'1000',=X'FF' ;Could be an abend call
         BE       LEAVE
         LA       R15,4        ;Set error code
         ST       R15,16(R13)
         B        LEAVE
************************
** Load Request - External functions first
*
LOADREQ  EQU      *
         LA       R4,XFUNTAB  ;Access the function table
         USING    XFUNCB,R4
         LR       R5,R4
LR$XLOOP LR       R4,R5
         CLI      0(R4),X'FF'  ;Last entry?
         BE       LR$PLI
         LA       R5,XFCBLEN(R4)
         CLC      16(8,R11),XFCBNAME ;Does name match
         BNE      LR$XLOOP
* Found an external one
         LA       R1,XPLIST         ;Set up to issue NUCXLOAD
         MVC      XPNAME,XFCBNAME    ;
         MVC      XPMODN,XFCBMODN    ;
         SVC      202               ;
         DC       AL4(1)
         C        R15,=F'1'         ;More than error 1
         BNH      LEAVE             ;Return with good news
         B        LR$BAD
         DROP     R4
**********
** Search for PLI External functions
**
LR$PLI   LR       R4,R5
         LA       R4,XPLITAB  ;Access the function table
         USING    XPLICB,R4
         LR       R5,R4
LR$PLOOP LR       R4,R5
         CLI      0(R4),X'FF'  ;Last entry?
         BE       LR$LOCAL
         LA       R5,XPCBLEN(R4)
         CLC      16(8,R11),XPCBNAME ;Does name match
         BNE      LR$PLOOP
* Found an external one
         LA       R1,PCRPLIST      ;Set up to issue PCR LOAD
         MVC      PCRNAME,XPCBNAME    ;
         MVC      PCRNUCX,XPCBNUCX    ;
         MVC      PCRMOD,XPCBMOD      ;
         SVC      202               ;
         DC       AL4(1)
         C        R15,=F'1'         ;More than error 1
         BNH      LEAVE             ;Return with good news
         B        LR$BAD
         DROP     R4
**********
** Search for Local (link) functions
**
LR$LOCAL EQU      *
         LA       R4,LFUNTAB  ;Access the local function table
         USING    LFUNCB,R4
         LR       R5,R4
LR$LLOOP LR       R4,R5
         CLI      0(R4),X'FF'  ;Last entry?
         BE       LR$PACKS
         LA       R5,LFCBLEN(R4)
         CLC      16(8,R11),LFCBNAME ;Does name match
         BNE      LR$LLOOP
* Found a local link one
         LA       R1,LPLIST         ;Set up to issue NUCEXT
         MVC      LPNAME,LFCBNAME
         MVC      LPADDR,LFCBADDR
         SVC      202
         DC       AL4(1)
         MVI      LFCBFLAG,C'Y'      ;Indicate it is loaded
         B        LEAVE
         DROP     R4
**********
** Search other packages next (and last)
**
LR$PACKS EQU      *
         LA       R4,PACKTAB  ;Access the local function table
         USING    PACKCB,R4
         LR       R5,R4
         MVC      PKPFNAME,16(R11) ;Move over request from rexx
LR$PKLP  LR       R4,R5
         CLI      0(R4),X'FF'  ;Last entry?
         BE       LR$BAD
         LA       R5,PKCBLEN(R4)
         MVC      PKPMNAME,PKCBNAME   ;Move name of package into PLIST
         LA       R1,PKPLIST        ;call this package
         SVC      202
         DC       AL4(1)
         LTR      R15,R15
         BNE      LR$PKLP           ;Not in this package - search more
         B        LEAVE             ;Yes, found....
         DROP     R4
********************************
**
** The name was not found - return with rc=4
**
LR$BAD   EQU      *
         LA       R15,4        ;Set error code of 1
         ST       R15,16(R13)
         B        LEAVE
***************************************
**
** Reset Request - Must undefine any local ones
**
RESETREQ EQU      *
         LA       R4,LFUNTAB  ;Access the function table
         USING    LFUNCB,R4
         LR       R5,R4
         LA       R1,RPLIST
RR$LOOP  LR       R4,R5
         CLI      0(R4),X'FF'  ;Last entry?
         BE       LEAVE
         LA       R5,LFCBLEN(R4)
         CLI      LFCBFLAG,C'Y'   ;Is it a NUCEXT?
         BNE      RR$LOOP
* Found one to undefine
         MVC      RPNAME,LFCBNAME
         MVI      LFCBFLAG,C'N'
         SVC      202
         DC       AL4(1)
         B        RR$LOOP
         DROP     R4
***********************************
**
**Leave now
**
LEAVE    EQU      *
         LM       R14,R12,12(R13)
         BR       R14
**********************************************
**
** Other function packatge modules to be search (called)
** (Since Rexx will only search RXSYSFN, RXLOCFN, and RXUSERFN)
**
** Make sure other RXUSERFN or RXLOCFN modules can handle having
** a different name before renaming them and placing them into
** this table.
**
         DS       0D
PACKTAB  EQU      *
**       DC       CL8'RXUSERF2'       ;Other module to call
PACKTEND DC       8X'FF'
**********************************************
**
** Functions located Externally in their own modules
** (This table is search first, then the Local table)
**
         DS       0D
XFUNTAB  EQU      *
***      DC       CL16'RXASM   ASM'       ;RXname/Modname
XFUNEND  DC       8X'FF'
**********************************************
**
** Functions located Externally and are PLI programs
** (Searched after XFUN, but before internal)
** Three names are given, Rexx, Nucxload, and Module)
**
         DS       0D
XPLITAB  EQU      *
         DC       CL24'RXPLIFUNrxplifunPCRTEST1' Rexx/Nucx/Mod
XPLIEND  DC       8X'FF'
**********************************************
**
** Functions linked within this module as V() constants
** (This table is search last, after the External table)
**
         DS       0D
LFUNTAB  EQU      *
*        DC       CL8'RXA2E'    ;RXname of entry
*        DC       V(A2E)        ;RXname/Modname
*        DC       CL4'N'        ;flags
LFUNEND  DC       8X'FF'
**********************************************
         DS       0D
LMODPL   DC       CL8'NUCXLOAD'
LMPNAME  DC       CL8'RXUSERFN'
         DC       CL8'('
         DC       CL8'SYSTEM'
         DC       CL8'SERVICE'
         DC       8X'FF'
*
XPLIST   DC       CL8'NUCXLOAD'
XPNAME   DS       CL8
XPMODN   DS       CL8
         DC       CL8'('
         DC       CL8'SYSTEM'
         DC       8X'FF'
*
PCRPLIST DC       CL8'PCR'
         DC       CL8'LOAD'
PCRNAME  DS       CL8
         DC       CL8'REXX'
PCRNUCX  DS       CL8
PCRMOD   DS       CL8
         DC       8X'FF'
*
LPLIST   DC       CL8'NUCEXT'
LPNAME   DS       CL8
         DC       X'FF048000'
LPADDR   DS       AL4
         DC       A(0)
         DC       A(0)
         DC       A(0)
*
RPLIST   DC       CL8'NUCEXT'
RPNAME   DS       CL8
         DC       16X'00'
*
PKPLIST  DS       0D
PKPMNAME DS       CL8
         DC       CL8'LOAD'
PKPFNAME DS       CL8
         DC       8X'FF'
         DC       16X'00'
*
MODSTAT  DC       A(LMOD)
*
         LTORG
XFUNCB   DSECT
XFCBNAME DS       CL8
XFCBMODN DS       CL8
XFCBLEN  EQU      *-XFCBNAME
*
XPLICB   DSECT
XPCBNAME DS       CL8
XPCBNUCX DS       CL8
XPCBMOD  DS       CL8
XPCBLEN  EQU      *-XPCBNAME
*
LFUNCB   DSECT
LFCBNAME DS       CL8
LFCBADDR DS       AL4
LFCBFLAG DS       CL1
LFCBPAD  DS       CL3
LFCBLEN  EQU      *-LFCBNAME
*
PACKCB   DSECT
PKCBNAME DS       CL8
PKCBLEN  EQU      *-PKCBNAME
         END
