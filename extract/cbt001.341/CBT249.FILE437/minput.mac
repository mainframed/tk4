MINPUT   $PROLOG
*-------------------------------------------------------------------*
*   FUNCTION:  COPY MULTIPLE INPUT FILES TO A SINGLE OUTPUT FILE.   *
*                                                                   *
*   OPERATION: A) READ PARM, MUST BE 2 NUMERIC VALUES OR NULL AND   *
*                 = THE MAXIMUM NUMBER OF INPUT FILES TO READ       *
*              B) INCREMENT DDNAME AND OPEN - IF PRESENT THEN       *
*              C) COPY TO OUTPUT DD                                 *
*              D) CONTINUE WITH B                                   *
*              E) CLOSE OUTPUT                                      *
*                                                                   *
*        THIS  PROGRAM WILL COPY FB INPUT TO VB OUTPUT    *LBD 05/84*
*        AND VB INPUT TO FB OUTPUT.                       *LBD 05/84*
*        NOTE: SEQUENCE NUMBERS ARE NOT MODIFIED IN THE   *LBD 05/84*
*              COPY OPERATION.                            *LBD 05/84*
*                                                                   *
*        MAX LRECL PROCESSED IS 255....                   *LBD 05/84*
*                                                                   *
*   DDNAMES:   OUT = OUTPUT FILE (MUST SPECIFY DCB)                 *
*              INXX = INPUT FILE(S)  XX = 01 THRU 99                *
*                                                                   *
*-------------------------------------------------------------------*
* THIS PROGRAM WAS WRITTEN BY   LIONEL DYCK                         *
*                               ROCKWELL INTERNATIONAL              *
*                               2201 SEAL BEACH BLVD.               *
*                               PO BOX 2515                         *
*                               SEAL BEACH, CALIFORNIA 90740        *
*                               MAIL CODE 110-SH28                  *
*                               PHONE (213) 594-1125                *
*-------------------------------------------------------------------*
         EJECT
         PARM
         OPEN  (OUTPUT,OUTPUT)
         TM    OUTPUT+X'24',X'40'  VARIABLE ?
         BZ    NEXTDDN             NO - SKIP IT
         SETON VARIOUT             YES - SET FLAG
NEXTDDN  AP    DDN,=P'01'                 INCR DDNAME
         UNPK  DDNUP,DDN
         OI    DDNUP+1,X'F0'              INSURE NUMERIC
         MVC   INPUT(DCBL),INDCB          RESET DCB
         MVC   INPUT+42(2),DDNUP          FIXUP DDNAME
         OPEN  (INPUT)
         TESTOPEN INPUT,CLOSEUP
         TM    INPUT+X'24',X'40'   VARIABLE INPUT ?
         BZ    READ                NON - SKIP IT
         SETON VARIIN
         IFON  VARIOUT,READ                                *LBD 05/84*
         SETON VIN                     IN VAR ONLY         *LBD 05/84*
READ     GET   INPUT
         MVC   DATA,BLANKS                                 *LBD 05/84*
         LR    R2,R1
         LH    R5,INPUT+X'52'      LOAD LRECL
         BCTR  R5,R0
         IFON  VIN,MV                                      *LBD 05/84*
         B     MVIT                                        *LBD 05/84*
MV       DS    0H                                          *LBD 05/84*
         SH    R5,=H'04'               LESS RDW LEN        *LBD 05/84*
         EX    R5,MOVEITV                                  *LBD 05/84*
         LA    R2,DATA                 -> DATA             *LBD 05/84*
         B     PUTIT                                       *LBD 05/84*
MVIT     EX    R5,MOVEIT
         LA    R2,DATA
         IFOFF VARIOUT,PUTIT
         IFON  VARIIN,PUTIT
         LH    R5,INPUT+X'52'      LOAD LRECL
         STH   R5,VDATA            SAVE IT
         LA    R2,VDATA
PUTIT    PUT   OUTPUT,(R2)
         B     READ
EOF      CLOSE INPUT
         SETOF VARIIN
         CLC   DDNUP,PARM
         BE    CLOSEUP
         B     NEXTDDN
CLOSEUP  CLOSE OUTPUT
         $EPILOG
         LTORG
         BITSW VARIIN,VARIOUT,VIN
MOVEIT   MVC   DATA(0),0(R2)
MOVEITV  MVC   DATA(0),4(R2)
PARM     DC    CL2'999'
DDN      DC    PL2'0'
DDNUP    DC    CL2' '
         DS    0F
VDATA    DC    H'00'
         DC    H'00'
DATA     DC    CL255' '
BLANKS   DC    CL255' '
OUTPUT   DCB   DSORG=PS,MACRF=PM,DDNAME=OUT
INPUT    DCB   DSORG=PS,MACRF=GL,DDNAME=IN,EODAD=EOF
DCBL     EQU   *-INPUT
INDCB    DCB   DSORG=PS,MACRF=GL,DDNAME=IN,EODAD=EOF
         END
