         TITLE 'LINK EDIT DRIVER USING THE SMPCDS 09-23-83'
*--------------------------------------------------------------------*
* NAME: SMPLINKP                                                     *
*                                                                    *
* FUNCTION:                                                          *
*        THIS PROGRAM IS DESIGNED TO BE RUN UNDER ISPF               *
*        AND WILL RETRIEVE INFORMATION FROM ISPF PANELS FOR          *
*        PROCESSING.                                                 *
*                                                                    *
*        THE INFORMATION TO BE PROVIDED BY THE DRIVER PANEL IS:      *
*              OBJECT MODULE NAME(S) UP TO 8                         *
*              IN VARIABLES M1 THRU M8                               *
*                                                                    *
*              LOAD LIBRARY TO RECEIVE THE OUTPUT OF IEWL            *
*                                                                    *
*              DEFAULT AUTHORIZATION AND RENT OR REUS FOR NEW LMODS  *
*                                                                    *
*                                                                    *
* ISPF VARIABLES NEEDED ON INPUT:                                    *
*                                                                    *
*        SLOAD - LOAD LIBRARY FOR IEWL                               *
*        M1 THRU M8 - OBJECT MODULE NAME                             *
*        ROPT - RENT OR REUS FLAG OR BLANK FOR NEW LMODS             *
*        AUTH - Y OR N FOR NEW LMODS                                 *
*                                                                    *
* ISPF VARIABLES OUTPUT TO SMPLINKP PANEL:                           *
*        SLOAD                                                       *
*        M1 THRU M8                                                  *
*        SYSLIB - THE SMP DETERMINED TARGET LIBRARY DDNAME           *
*                 OF SYSLMOD IF NEW                                  *
*        LEPARM - SMP DETERMINED LE PARM OR THAT PROVIDED BY ROPT    *
*        NEW - INDICATES THAT YOU HAVE A NEW MODULE OR LMOD          *
*        DELMOD  - INDICATES DEFAULT LMOD FOR NEW MODULES IF DESIRED *
*                                                                    *
* SUB-PROGRAMS:                                                      *
*              SMPDATA - VIA ATTACH TO ACQUIRE SMPCDS INFORMATION    *
*              IEWL    - VIA ATTACH TO PERFORM LINKEDIT              *
*              IKJEFF18- VIA LINK TO PROVIDE DYNAMIC ALLOCATION      *
*                        FAILURE ANALYSIS                            *
* RETURN:                                                            *
*        00 - OK                                                     *
*        04 - FAILURE IN DYNAMIC ALLOCATION                          *
*                                                                    *
* DD'S REQUIRED THRU PRE ALLOCATION:                                 *
*              SYSLIN                                                *
*              SYSUT1                                                *
*              SYSLIB                                                *
*              SYSPRINT                                              *
*              OBJ - OBJECT DECK PDS                                 *
*              SMPCDS - SMP CDS                                      *
*                                                                    *
* DD'S REQUIRED THRU DYNAMIC ALLOCATION:                             *
*              SYSLMOD                                               *
*                                                                    *
*--------------------------------------------------------------------*
         EJECT
*--------------------------------------------------------------------*
* SAMPLE CLIST :                                                     *
*                                                                    *
*PROC 0                                                              *
*CONTROL NOMSG                                                       *
*FREE F(SMPCDS SYSLIN SYSUT1 SYSPRINT OBJ SYSLIB SYSLMOD SMPPUNCH)   *
*CONTROL MSG                                                         *
*       ALLOC F(SYSLIN) UNIT(SYSVIO) SPA(1,1) TR                     *
*ISPEXEC VGET (OBJDSN LDSN1 LDSN2 LDSN3 LDSN4 LDSN5 +                *
*          CDSDSN CDSVOL CDSUNIT)                                    *
*CONTROL MSG                                                         *
* ALLOC F(SYSLIB) DS(&LDSN1 &LDSN2 &LDSN3 &LDSN4 &LDSN5) SHR         *
* ALLOC F(SYSPRINT) DS(*)                                            *
* ALLOC F(SYSUT1) UNIT(SYSDA) SPA(35,10) TR                          *
* ALLOC F(OBJ) DS(&OBJDSN) SHR                                       *
* IF &CDSVOL = THEN   ALLOC F(SMPCDS) DS(&CDSDSN) SHR                *
*  ELSE ALLOC F(SMPCDS) DS(&CDSDSN) SHR VOL(&CDSVOL) UNIT(&CDSUNIT)  *
*     ISPEXEC SELECT PGM(SMPLINKP)                                   *
*       WRITE CONDITION CODE FROM LINKAGE EDITOR = &LASTCC           *
*CONTROL NOMSG                                                       *
*FREE F(SMPCDS SYSLIN SYSUT1 SYSPRINT OBJ SYSLIB SYSLMOD SMPPUNCH)   *
*                                                                    *
*--------------------------------------------------------------------*
         EJECT
*--------------------------------------------------------------------*
* SAMPLE PRIMARY PANEL:                                              *
*                                                                    *
*%LINKJ ---------------- JES3 LINKEDIT PANEL -------------------------*
*%ENTER/VERIFY PARAMETERS BELOW                                       *
*+                                                                    *
*+OBJECT DECK DATASET         %===>_OBJDSN                            *
*+                                                                    *
*+OBJECT MEMBERS:    %=>_MEM1    %=>_MEM2    %=>_MEM3    %=>_MEM4    +*
*+                   %=>_MEM5    %=>_MEM6    %=>_MEM7    %=>_MEM8    +*
*+                                                                    *
*+SYSLMOD DSNAME:             %===>_SLOAD                             *
*+                                                                    *
*+SYSLIB DSNAMES:            %1===>_LDSN1                             *
*%                            2===>_LDSN2                             *
*%                            3===>_LDSN3                             *
*%                            4===>_LDSN4                             *
*%                            5===>_LDSN5                             *
*+                                                                    *
*%LINK+OPTIONS FOR%NEW+MODULES ONLY:                                  *
*+  LMOD:_DELMOD  +RENT+AUTH:_AUTH REUS:_ROPT    +AUTH:_AUTH
*+                                                                    *
*+CDS DSNAME:  %===> _CDSDSN                                          *
*+    VOLUME   %===> _CDSVOL+ UNIT %===>_CDSUNIT+  IF NOT CATALOGED   *
*+                                                                    *
*)INIT                                                                *
* .CURSOR = MEM1                                                      *
* &ROPT = &Z                                                          *
* &Q = ''''                                                           *
* &AUTH = TRANS (&AUTH  ' ',N *,*)                                    *
* &DELMOD = TRANS (&DELMOD ' ',' ' *,*)                               *
* &SLOAD = TRANS (&SLOAD   ' ','''JES3.NAME.LOAD''' *,*)              *
* &OBJDSN = TRANS (&OBJDSN  ' ','''&ZPREFIX..MODS.OBJ''' *,*)         *
* &CDSDSN = TRANS (&CDSDSN ' ','''JES3.SP131.SMPCDS''' *,*)           *
* &LDSN1  = TRANS (&LDSN1  ' ','''JES3.DUMMY.LOAD''' *,*)             *
* &LDSN2  = TRANS (&LDSN2  ' ','''JES3.TEST131.LOAD''' *,*)           *
* &LDSN3  = TRANS (&LDSN3  ' ','''AJES.TESTL131.LOAD''' *,*)          *
* &LDSN4 = TRANS (&LDSN4 ' ','''JES3.PROD131.LOAD''' *,*)             *
* &LDSN5 = TRANS (&LDSN5 ' ','''AJES.PRODL131.LOAD''' *,*)            *
*)PROC                                                                *
* VER (&OBJDSN,NONBLANK)  VER (&OBJDSN,DSNAME)                        *
* VER (&MEM1,NONBLANK)   VER (&MEM1,NAME)                             *
* IF (&MEM2 ^= &Z)                                                    *
*    VER (&MEM2,NAME)                                                 *
* IF (&MEM3 ^= &Z)                                                    *
*    VER (&MEM3,NAME)                                                 *
* IF (&MEM4 ^= &Z)                                                    *
*    VER (&MEM4,NAME)                                                 *
* IF (&MEM5 ^= &Z)                                                    *
*    VER (&MEM5,NAME)                                                 *
* IF (&MEM6 ^= &Z)                                                    *
*    VER (&MEM6,NAME)                                                 *
* IF (&MEM7 ^= &Z)                                                    *
*    VER (&MEM7,NAME)                                                 *
* IF (&MEM8 ^= &Z)                                                    *
*    VER (&MEM8,NAME)                                                 *
* VER (&SLOAD,NONBLANK)         VER (&SLOAD,DSNAME)                   *
* VER (&CDSDSN,NONBLANK)        VER (&CDSDSN,DSNAME)                  *
* IF (&CDSVOL ^= &Z)                                                  *
*    VER (&CDSVOL,NAME)                                               *
*    VER (&CDSUNIT,NONBLANK)                                          *
* IF (&DELMOD ^= &Z)                                                  *
*    VER (&DELMOD,NAME)                                               *
* VER (&LDSN1,NONBLANK)    VER (&LDSN1,DSNAME)                        *
* IF (&LDSN2 ^= &Z)                                                   *
*      VER (&LDSN2,NONBLANK)    VER (&LDSN2,DSNAME)                   *
* IF (&LDSN3 ^= &Z)                                                   *
*      VER (&LDSN3,NONBLANK)    VER (&LDSN3,DSNAME)                   *
* IF (&LDSN4 ^= &Z)                                                   *
*      VER (&LDSN4,NONBLANK)    VER (&LDSN4,DSNAME)                   *
* IF (&LDSN5 ^= &Z)                                                   *
*      VER (&LDSN5,NONBLANK)    VER (&LDSN5,DSNAME)                   *
* IF (&ROPT ^= &Z)                                                    *
*    VER (&ROPT,LIST,RENT,REUS)                                       *
* VER (&AUTH,NONBLANK)          VER (&AUTH,LIST,Y,N)                  *
* &SEL = 'CMD(%LINKJT)'                                               *
*)END                                                                 *
*                                                                    *
*--------------------------------------------------------------------*
         EJECT
*--------------------------------------------------------------------*
* SAMPLE SMPLINKP PANEL                                              *
*                                                                    *
*%SMPLINKP ----------- SMP LINKEDIT PROMPT PANEL --------------------*
*+                                                                   *
*+    LOAD MODULE NAME:%&LMOD    %   &NEW                            *
*+                                                                   *
*+    OBJECT DECKS FOR THIS LINK: &M1 &M2 &M3 &M4                    *
*+                                &M5 &M6 &M7 &M8                    *
*+                                                                   *
*+    SMP DETERMINED LOAD LIBRARY:%&SYSLIB                           *
*+                                                                   *
*+    LOAD LIBRARY SPECIFIED %===>_SLOAD                             *
*+                                                                   *
*+    SMP DETERMINED LINK EDIT ATTRIBUTES: &LEPARM                   *
*+                                                                   *
*+                                                                   *
*+                                                                   *
*+                                                                   *
*+                                                                   *
*+                                                                   *
*%    CHANGE THE LOAD LIBRARY IF DESIRED AND DEPRESS ENTER           *
*+                                                                   *
*+                                                                   *
*)INIT                                                               *
* .CURSOR = SLOAD                                                    *
*)PROC                                                               *
* VER (&SLOAD,NONBLANK)   VER (&SLOAD,DSNAME)                        *
*)END                                                                *
*                                                                    *
*--------------------------------------------------------------------*
         EJECT
*--------------------------------------------------------------------*
* AUTHOR:  LIONEL DYCK                                               *
*          ROCKWELL INTERNATIONAL                                    *
*          PO BOX 2515                                               *
*          SEAL BEACH, CALIF 90740                                   *
*          MAIL CODE 110-SH28                                        *
*          (213) 594-1125                                            *
*--------------------------------------------------------------------*
* THIS PROGRAM HAS RUN SUCCSSFULLY UNDER ISPF-PDF UNDER TSO VTAM     *
* USING VTAM V2, MVS SP 1.3 AND JES3.  IT HAS RUN ON 3033'S AND      *
* 3081 PROCESSORS.                                                   *
*--------------------------------------------------------------------*
         SPACE
*------------  MACROS USED ------------------------------------------*
*     $EPILOG $PROLOG $VGET $VPUT ALLOC BITSW CALL CLOSE DCB         *
*     DYNSPACE FREE IEFZB4D0 IEFZB4D2 IFOFF IFON LINK OPEN PUT       *
*     SETOF SETON S99FAIL TPUT VGETSUB VPUTSUB                       *
*--------------------------------------------------------------------*
         EJECT
SMPLINK  $PROLOG R11,R12
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        PROCESS THE PARM TO GET THE PANEL TO DISPLAY OR             *
*        DEFAULT AND USE PANEL NAME SMPLINKP                         *
*                                                                    *
*--------------------------------------------------------------------*
         L     1,0(1)              * LOAD ADDR OF PARM
         LH    2,0(1)              * LOAD LENGTH OF PARM
         LTR   2,2                 * LENGTH OF ZERO
         BZ    NOPARM              * NO - BYPASS MOVE
         CH    2,=H'08'            * GREATER THAN 8 ?
         BH    BADPARM             * YES - TAKE DEFAULT - AND TELL IT
         BCTR  2,0                 * SUBTRACT LENGTH BY 1
         EX    2,MVCPANEL          * EXECUTE THE MOVE
         B     HAVEPARM
MVCPANEL MVC   PANEL(0),2(1)
BADPARM  TPUT  BADPMSG,L'BADPMSG
NOPARM   EQU   *                   USE DEFAULT PANEL ID
         MVC   PANEL,DEFPANEL
HAVEPARM EQU   *
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        GO GET VIA VGET USING MY OWN SPECIAL MACRO THE NAMES OF THE *
*        OBJECT DECKS TO BE LINKEDITED                               *
*                                                                    *
*--------------------------------------------------------------------*
         $VGET DELMOD
         MVC   SLMOD,0(R1)
         L     R10,TAB
         USING MTABLE,R10
         $VGET MEM1
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM2
         CLI   0(R1),C' '
         BE    *+12
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM3
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM4
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM5
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM6
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM7
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+12
         BAL   R9,SMPDATA
         LA    R10,ENTL(R10)
         $VGET MEM8
         MVC   $MEM,0(R1)
         CLI   0(R1),C' '
         BE    *+8
         BAL   R9,SMPDATA
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        OK WE NOW HAVE ALL THE MODULE AND LMOD INFORMATION          *
*        NOW DEFINE ALL THE NEEDED ISPF VARIABLES AND                *
*        SELECT 1 LMOD AT A TIME FOR PROCESSING                      *
*                                                                    *
*--------------------------------------------------------------------*
         MVC   FUNCTION(4),=CL4'LMOD'
         SETON DOLMOD
         CALL  ISPLINK,(VDEF,NLIST,VLIST,FORMAT,LEN,OPT),VL
         CALL  ISPLINK,(VDEF,NSYSLIB,SYSLIB,FORMAT,LSYSLIB,OPT),VL
         CALL  ISPLINK,(VDEF,NEWNL,NEW,FORMAT,LENN,OPT),VL
         MVC   LMOD,BLANKS         INITIALIZE LMOD
         LH    R1,=H'44'
         ST    R1,LENS
         CALL  ISPLINK,(VCOPY,SLOADNL,LENS,SLOAD,MOVE),VL
         $VGET ROPT
         MVC   SLEPARM,0(R1)        SAVE SPECIFIED LEPARM
         $VGET AUTH
         MVC   AUTH,0(R1)          SAVE SPECIFIED AUTH CODE
         EJECT
         L     R10,TAB
         LA    R8,M1
         LA    R3,8                MAX NUMBER OF ENTRIES
         OPEN  (SYSLIN,OUTPUT)
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        HERE IS PROCESS THE MODULE TABLE TO FIND ONE LMOD AT A      *
*        TIME TO SEND THRU THE SMPLINKP PANEL AND THEN THRU          *
*        IEWL .                                                      *
*                                                                    *
*--------------------------------------------------------------------*
PROCLMOD DS    0H                  PROCESS FOR LOAD MODULE NOW
         TM    $FLAG,$USED
         BO    PROCINCR
         CLI   $MEM,C' '
         BE    PROCINCR
         CLI   LMOD,C' '
         BE    FSTLMOD
         CLC   LMOD,$LMOD
         BNE   PROCINCR
FSTLMOD  DS    0H
         MVC   LMOD,$LMOD
         CLI   $NEW,C'N'
         BNE   CKLE
         MVC   NEW,=CL16'NEW MODULE'
CKLE     IFON  NEWMOD,CKSYSL
         MVC   LEPARM,SLEPARM
CKSYSL   CLI   SYSLIB,C' '
         BNE   MVCINCL
         MVC   SYSLIB(18),=CL18'SYSLMOD '
MVCINCL  MVC   INCLMOD,LMOD
         LA    R1,INCLMOD
         LA    R2,8
INCLMP   CLI   0(R1),C' '
         BE    DOINCLM
         LA    R1,1(R1)
         BCT   R2,INCLMP
DOINCLM  MVI   0(R1),C')'
         OI    $FLAG,$USED
         SETON HAVELM
         MVC   0(8,R8),$MEM
         MVC   INCLMEM,$MEM
         LA    R1,INCLMEM
         LA    R2,8
INCLP    CLI   0(R1),C' '
         BE    DOINCL
         LA    R1,1(R1)
         BCT   R2,INCLP
DOINCL   MVI   0(R1),C')'
         PUT   SYSLIN,INCLUDEN
         LA    R8,8(R8)
PROCINCR LA    R10,ENTL(R10)
         BCT   R3,PROCLMOD
         IFON  HAVELM,PROCSMP
         B     EPILOG              LAST OF IT
PROCSMP  BAL   R9,SMPDATA
         PUT   SYSLIN,INCLUDEO
         MVI   NAME,C' '
         MVC   NAME+1(10),NAME     CLEAR NAME FIELD
         IFOFF JCLIN,DONAME
         AR    R5,R6
JCLINLP  CLI   0(R5),X'00'
         BE    DONAME
         PUT   SYSLIN,0(R5)
         BXLE  R5,R6,JCLINLP
DONAME   MVC   NAME,LMOD
         LA    R1,NAME
         LA    R2,8
NLP      CLI   0(R1),C' '          END OF NAME ?
         BE    ENAME
         LA    R1,1(R1)
         BCT   R2,NLP
ENAME    MVC   0(3,R1),=C'(R)'
         CLI   NEW,C' '
         BE    PUTNAME
         CLI   AUTH,C'Y'
         BNE   PUTNAME
         PUT   SYSLIN,SETCODE      SET AC(1)
PUTNAME  PUT   SYSLIN,NAMECD
         LH    R1,=H'44'
         ST    R1,LENS
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        UPDATE THE ISPF VARIABLES AND DISPLAY PANEL SMPLINKP        *
*        OR THE PANEL SPECIFIED IN THE PARM                          *
*                                                                    *
*--------------------------------------------------------------------*
         CALL  ISPLINK,(VREPLACE,SLOADNL,LENS,SLOAD),VL
         CALL  ISPLINK,(VPUT,NEWNL),VL
         CALL  ISPLINK,(VPUT,NLIST),VL
         CALL  ISPLINK,(VPUT,NSYSLIB),VL
         CALL  ISPLINK,(DISPLAY,PANEL),VL
         ST    R15,RC
         LTR   R15,R15
         BNZ   EPILOG
         CLOSE (SYSLIN,REREAD)
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        GET CURRENT SLOAD IN CASE THE USER CHANGED HIS MIND         *
*        AND CHANGED THE SLOAD THEN DO DYNAMIC ALLOC                 *
*        IF IT DIFFERENT.                                            *
*        BUT FIRST PARSE THE DSN OF SLOAD                            *
*--------------------------------------------------------------------*
         LH    R1,=H'44'
         ST    R1,LENS
         CALL  ISPLINK,(VCOPY,SLOADNL,LENS,SLOAD,MOVE),VL
         CLI   SLOAD,X'7D'         ' ?
         BNE   GETPRFX
         MVC   ADSN,BLANKS
         LA    R1,ADSN
         LA    R2,SLOAD+1
         LA    R3,43
DSNMVC   CLI   0(R2),X'7D'         END OF DSN
         BE    DOALLOC
         MVC   0(1,R1),0(R2)
         LA    R1,1(R1)
         LA    R2,1(R2)
         BCT   R3,DSNMVC
         B     DOALLOC
GETPRFX  $VGET ZPREFIX
         MVC   ADSN(8),0(R1)
         LA    R1,ADSN
         LA    R2,SLOAD
         LA    R3,8
DSNMVC2  CLI   0(R1),C' '
         BE    MVIPEROD
         LA    R1,1(R1)
         BCT   R3,DSNMVC2
MVIPEROD MVI   0(R1),C'.'
         LA    R1,1(R1)
         LA    R3,36
DSNMVC3  CLI   0(R2),C' '          END OF DSN
         BE    DOALLOC             YES
         MVC   0(1,R1),0(R2)
         LA    R1,1(R1)
         LA    R2,1(R2)
         BCT   R3,DSNMVC3
DOALLOC  CLC   ADSN,SDSN
         BE    DOLKED
         MVC   SDSN,ADSN
         FREE  DDN='SYSLMOD'
         ALLOC DDN='SYSLMOD',DSN=ADSNA,DISP=SHR,ERROR=S99FAIL
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        PERFORM THE LINKEDIT OF THE LOAD MODULE                     *
*        BY INVOKING IEWL                                            *
*                                                                    *
*--------------------------------------------------------------------*
DOLKED   CLI   LEPARM,C' '
         BE    SETLPARM
         MVC   ATT(8),LEPARM
         MVI   ATTC,C','
SETLPARM LA    R1,LPARM
         XC    WAITECB,WAITECB
         ATTACH EP=IEWL,SZERO=NO,PURGE=NONE,RSAPF=YES,ECB=WAITECB
         ST    R1,TCBADDR
         WAIT  ECB=WAITECB
         DETACH TCBADDR
         MVC   ATTC(9),BLANKS
         OPEN  (SYSLIN,OUTPUT)
         L     R10,TAB
         LA    R8,M1
         LA    R3,8                MAX NUMBER OF ENTRIES
         MVC   NEW,BLANKS
         MVC   NAME,BLANKS
         MVI   VLIST,C' '
         MVC   VLIST+1(VLISTL-1),VLIST
         SETOF HAVELM,JCLIN,NEWMOD
         B     PROCLMOD
S99FAIL  S99FAIL
         MVC   RC,=F'04'           SET RETURN CODE
         B     EPILOG
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        INVOKE THE SMPDATA SUB-ROUTINE VIA LINK AND PARSE THE       *
*        RESPONSE INTO THE CORRECT ISPF VARIABLES                    *
*                                                                    *
*--------------------------------------------------------------------*
SMPDATA  DS    0H                  SMPDATA SUBROUTINE
*
**       RESET SMPDATA RECEIVE TABLE TO ZERO'S
*
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6               REDUCE BY 1 FOR BXLE
CLRLOOP  XC    0(80,R5),0(R5)      XC SMPDATA TABLE
         BXLE  R5,R6,CLRLOOP
*
**       INVOKE SMPDATA SUBROUTINE FOR SMP CDS ACCESS
*
         IFON  DOLMOD,SMPLMOD
         CLI   $MEM,C' '
         BER   R9
         MVC   MEMBER,$MEM
         B     CALLIT
SMPLMOD  MVC   MEMBER,LMOD
CALLIT   DS    0H
         L     R1,PARMPTR
         XC    WAITECB,WAITECB
         ATTACH EP=SMPDATA,SZERO=NO,PURGE=NONE,RSAPF=YES,ECB=WAITECB
         ST    R1,TCBADDR
         WAIT  ECB=WAITECB
         DETACH TCBADDR
         ICM   R15,7,WAITECB+1
         LTR   R15,R15
         BNZ   NEWMEM
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6               REDUCE BY 1 FOR BXLE
         IFON  DOLMOD,LMODLOOP
         B     MODLOOP
NEWMEM   DS    0H
         IFON  DOLMOD,NEWLMOD
         MVI   $NEW,C'N'           SET ON NEW FLAG
         CLI   SLMOD,C' '
         BE    MVCMEM
         MVC   $LMOD,SLMOD
         B     NEXTMOD
MVCMEM   MVC   $LMOD,$MEM
NEXTMOD  EQU   *
         BR    R9                  GO FOR NEXT ENTRY
NEWLMOD  SETON NEWMOD
         BR    R9
MODLOOP  DS    0H
         CLC   =C'LMOD',0(R5)
         BE    DOLMODX
         BXLE  R5,R6,MODLOOP
         B     NEWMEM              ASSUME NEW
DOLMODX  MVC   $LMOD,10(R5)
         B     NEXTMOD
LMODLOOP DS    0H
         CLI   0(R5),X'00'         END ??
         BE    ENDLMOD
         IFON  JCLIN,DOJCLIN
         CLC   =C'SYSLIB',0(R5)
         BE    DOSYSLIB
         CLC   =C'LEPARM',0(R5)
         BE    DOLEPARM
         CLC   =C'++LMODIN',0(R5)
         BE    DOLMODIN
LMODBXLE BXLE  R5,R6,LMODLOOP
         B     NEWLMOD             ASSUME NEW
DOSYSLIB MVC   SYSLIB,10(R5)
         B     LMODBXLE
DOLEPARM MVC   LEPARM,10(R5)
         CLC   =C'COPY',LEPARM
         BE    CLRLPARM
         CLC   =C'STD',LEPARM
         BE    CLRLPARM
         B     LMODBXLE
CLRLPARM MVC   LEPARM,BLANKS
         B     LMODBXLE
DOJCLIN  EQU   *
DOLMODIN SETON JCLIN
ENDLMOD  BR    R9
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        GET OUT OF HERE BUT FIRST CLOSE SYSLIN AND DELETE           *
*        THE ISPF VARIABLES WE DEFINED THEN REPLACE THE SLOAD        *
*        FOR FUTURE USE.                                             *
*                                                                    *
*--------------------------------------------------------------------*
EPILOG   CLOSE (SYSLIN)
         CALL  ISPLINK,(VDEL,NLIST),VL
         CALL  ISPLINK,(VDEL,NSYSLIB),VL
         CALL  ISPLINK,(VDEL,NEWNL),VL
         $VPUT SLOAD,SLOAD         * REPLACE THE ORIG SLOAD IF DIFF
         L     R15,RC              LOAD RETURN CODE
         $EPILOG
         EJECT
         VGETSUB
         EJECT
         VPUTSUB
         EJECT
*--------------------------------------------------------------------*
*                                                                    *
*        BELOW YOU WILL FIND MY DATA AREAS USED TO CONTAIN THE DATA  *
*        THAT I WILL MANIPULATE AND OR OTHERWISE REFERENCE           *
*                                                                    *
*--------------------------------------------------------------------*
         LTORG
         BITSW NEWMOD,HAVELM,DOLMOD,JCLIN,LSTLMOD
         EJECT
RC       DC    F'00'               RETURN CODE
WAITECB  DC    F'00'
TCBADDR  DC    F'00'
         SPACE
LPARM    DC    X'80',AL3(LPTR)
LPTR     DC    AL2(LINKLEN)
LINKATTS DC    C'XREF,LIST,LET,NCAL,SIZE=(768K,100K)'
ATTC     DC    C' '
ATT      DC    CL20' '
LINKLEN  EQU   *-LINKATTS
         SPACE
ADSNA    DC    A(ADSN),Y(44)
ADSN     DC    CL44' '             ALLOC DSN
SDSN     DC    CL44' '             SAVE DSN
         EJECT
VPUT     DC    C'VPUT'
VGET     DC    C'VGET'
VDEL     DC    C'VDELETE'
VREPLACE DC    C'VREPLACE'
VCOPY    DC    C'VCOPY'
MOVE     DC    C'MOVE'             PARM FOR VCOPY
DISPLAY  DC    C'DISPLAY'
PANEL    DC    CL8' '
DEFPANEL DC    CL8'SMPLINKP'
SLOADNL  DC    C'SLOAD'
LENS     DC    F'44'               LEN OF SLOAD
NEWNL    DC    C'(NEW)'
LENN     DC    F'16'               LEN OF NEW
*
NSYSLIB  DC    C'(SYSLIB)'
SYSLIB   DC    CL18' '
LSYSLIB  DC    F'18'
*
NEW      DC    CL16' '
VDEF     DC    C'VDEFINE'
NLIST    DC    C'(LMOD,LEPARM,M1,M2,M3,M4,M5,M6,M7,M8)'
FORMAT   DC    C'CHAR'
LEN      DC    F'8'
OPT      DC    C'(COPY)'
VLIST    DS    0H                  VARLIST FOR VDEFINE
LMOD     DC    CL8' '              LMOD NAME
LEPARM   DC    CL8' '              LEPARM FROM CDS OR ROPT IF NEW
M1       DC    CL8' '              OBJECT MODULE NAME
M2       DC    CL8' '              OBJECT MODULE NAME
M3       DC    CL8' '              OBJECT MODULE NAME
M4       DC    CL8' '              OBJECT MODULE NAME
M5       DC    CL8' '              OBJECT MODULE NAME
M6       DC    CL8' '              OBJECT MODULE NAME
M7       DC    CL8' '              OBJECT MODULE NAME
M8       DC    CL8' '              OBJECT MODULE NAME
VLISTL   EQU   *-VLIST
         EJECT
AUTH     DC    C' '                DEFAULTS FOR NEW MODULE'S ONLY
SLEPARM  DC    CL20' '             DEFAULTS FOR NEW MODULE'S ONLY
SLOAD    DC    CL44' '
SLMOD    DC    CL8' '              DEFAULT FOR NEW MODULES ONLY
         SPACE
PARMPTR  DC    A(WORDS)
WORDS    DC    A(FUNCTION,MEMBER,DATALEN)
FUNCTION DC    CL8'MOD'
MEMBER   DC    CL8' '
BLANKS   DC    CL80' '
DATALEN  DC    A(100*80)
DADDR    DC    A(DATA)
         SPACE
BADPMSG  DC    C'***  PARM TOO LARGE - TAKING DEFAULT ***'
         EJECT
INCLUDEN DC    CL80' '
         ORG   INCLUDEN
         DC    C' INCLUDE OBJ('
INCLMEM  DC    CL8' '
         DC    C'            * INCLUDE NEW OBJECT   '
         ORG
         SPACE
INCLUDEO DC    CL80' '
         ORG   INCLUDEO
         DC    C' INCLUDE SYSLIB('
INCLMOD  DC    CL8' '
         DC    C'         * INCLUDE ORIGINAL LMOD'
         ORG
NAMECD   DC    CL80' '
         ORG   NAMECD
         DC    C' NAME    '
NAME     DC    CL8' '
         DC    C'                * MODULE NAME '
         ORG
SETCODE  DC    CL80'  SETCODE AC(1)  ****** AUTHORIZED ***'
         ORG
         EJECT
         PRINT NOGEN
SYSLIN   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSLIN,LRECL=80,RECFM=FB,      *
               BLKSIZE=3120
         EJECT
TAB      DC    A(TABLE)
TABLE    DC    8CL18' '       TABLE DATA
TABLEE   EQU   *
         SPACE
         DYNSPACE
         SPACE
DATA     DC    100XL80'00'
         SPACE
         DSECT
MTABLE   DS    0H
$MEM     DS    CL8
$LMOD    DS    CL8
$NEW     DS    C
$FLAG    DS    X
$USED    EQU   X'80'
ENTL     EQU   *-MTABLE
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         END
