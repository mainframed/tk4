         TITLE 'BUILD SMP JCLIN FOR NEW MODULES'
*---------------------------------------------------------------------*
*                                                                     *
* THIS IS AN ISPF PROGRAM ONLY.                                       *
*                                                                     *
*                                                                     *
* ISPF VARIABLES USED:                                                *
*        MEMBER = MODULE NAME                                         *
*    **  DELMOD = DEFAULT LOAD MODULE NAME                            *
*     *  RENT   = RENT LE ATTRIBUTES (IMPLIES REUS)                   *
*     *  REUS   = REUS LE ATTRIBUTES                                  *
*     *  AUTH    = AUTHORIZED LE ATTRIBUTE                            *
*     *  SYSLIBO = TARGET LIBRARY FOR LOAD MODULE                     *
*     *  DISTLIBO = DLIB FOR MODULE                                   *
*                                                                     *
*     *  - USED ONLY FOR NEW LMODS                                    *
*    **  - REQUIRED ONLY IF LMOD NAME IS DIFF THAN MODULE NAME        *
*                                                                     *
*                                                                     *
* DD'S REQUIRED:                                                      *
*        SMPJCLIN DD DSN=JCLIN OUTPUT FILE                            *
*        SMPCDS DD DSN= SMP CDS DSN                                   *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES:                                                  *
*        SMPDATA - VIA LINK TO ACQUIRE SMP CDS INFORMATION            *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
BLDJCLIN $PROLOG
         $VGET MEMBER
         MVC   MEMBER,0(R1)
         MVC   NAME,MEMBER
         MVC   SNAME,MEMBER
         L     R1,PARMPTR
         LINK  EP=SMPDATA
         LTR   R15,R15
         BNZ   NEWMEM
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6               REDUCE BY 1 FOR BXLE
         B     MODLOOP
MVLMOD   MVC   NAME,LMOD
         B     DOLMOD              GO CHECK FOR LMOD
MODLOOP  DS    0H
         CLC   =C'LMOD',0(R5)
         BE    DOLMODX
         CLC   =C'DISTLIB',0(R5)
         BE    DODIST
MBXLE    BXLE  R5,R6,MODLOOP
         B     DOLMOD
NEWMEM   EQU   *                   SET ON NEW FLAG
         $VGET DELMOD
         CLI   0(R1),C' '
         BNE   MVDELMOD
MVCMEM   MVC   LMOD,MEMBER
         B     GDIST
MVDELMOD MVC   LMOD,0(R1)
GDIST    $VGET DISTLIBO
         MVC   DLIBDS,0(R1)
         MVC   DLIBDSN,0(R1)
         MVC   DLIBINCL,0(R1)
         B     MVLMOD
DOLMODX  MVC   LMOD,10(R5)
         MVC   NAME,LMOD
         B     MBXLE
DODIST   MVC   DLIBDS,10(R5)
         MVC   DLIBDSN,10(R5)
         MVC   DLIBINCL,10(R5)
         B     MBXLE
         EJECT
DOLMOD   LA    R1,DLIBINCL+1
DLP      CLI   0(R1),C' '
         BE    MVNAME
         LA    R1,1(R1)
         B     DLP
MVNAME   EQU   *
         MVI   0(R1),C'('
         MVC   1(8,R1),SNAME
         LA    R1,1(R1)
DLP2     CLI   0(R1),C' '
         BE    MVRP
         LA    R1,1(R1)
         B     DLP2
MVRP     MVI   0(R1),C')'
*
**       RESET SMPDATA RECEIVE TABLE TO ZERO'S
*
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6               REDUCE BY 1 FOR BXLE
CLRLOOP  XC    0(80,R5),0(R5)      XC SMPDATA TABLE
         BXLE  R5,R6,CLRLOOP
         MVC   FUNCTION,=CL8'LMOD'
SMPLMOD  MVC   MEMBER,LMOD
         L     R1,PARMPTR
         LINK  EP=SMPDATA
         LTR   R15,R15
         BNZ   NEWLMOD
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6               REDUCE BY 1 FOR BXLE
         B     LMODLOOP
NEWLMOD  SETON NEWMOD
         $VGET SYSLIBO
         MVC   SYSLDSN,0(R1)
         $VGET RENT
         CLI   0(R1),C'Y'
         BE    RENT
         CLI   0(R1),C'Y'
         BE    REUS
         B     ENDLMOD
RENT     MVC   LEATT,=CL8'RENT'
         MVI   LEATTC,C','
         MVI   LEATT+4,X'7D'
         B     ENDLMOD
REUS     MVC   LEATT,=CL8'REUS'
         MVI   LEATTC,C','
         MVI   LEATT+4,X'7D'
         B     ENDLMOD
LMODLOOP DS    0H
         CLI   0(R5),X'00'         END ??
         BE    ENDLMOD
         CLC   =C'SYSLIB',0(R5)
         BE    DOSYSLIB
         CLC   =C'LEPARM',0(R5)
         BE    DOLEPARM
         CLC   =C'++LMODIN',0(R5)
         BE    DOLMODIN
LMODBXLE BXLE  R5,R6,LMODLOOP
         B     NEWLMOD             ASSUME NEW
DOSYSLIB MVC   SYSLDSN,10(R5)
         B     LMODBXLE
DOLEPARM MVC   LEATT,10(R5)
         CLC   =C'COPY',LEATT
         BE    CLRLPARM
         CLC   =C'STD',LEATT
         BE    CLRLPARM
         MVI   LEATTC,C','
         MVI   LEATT+8,X'7D'
         B     LMODBXLE
CLRLPARM MVC   LEATT,BLANKS
         MVI   LEATTC,X'7D'
         B     LMODBXLE
DOLMODIN SETON JCLIN
         AR    R5,R6
ENDLMOD  OPEN  (SMPJCLIN,OUTPUT)
         PUT   SMPJCLIN,J1
         PUT   SMPJCLIN,J2
         PUT   SMPJCLIN,J3
         PUT   SMPJCLIN,J4
         PUT   SMPJCLIN,J5
         PUT   SMPJCLIN,J6
         PUT   SMPJCLIN,INCLUDE
         IFOFF JCLIN,CKNEW
PUTJCLIN PUT   SMPJCLIN,0(R5)
         AR    R5,R6
         CLI   0(R5),X'00'
         BNE   PUTJCLIN
CKNEW    IFOFF NEWMOD,PNAME
         $VGET AUTH
         CLI   0(R1),C'Y'
         BNE   PNAME
         PUT   SMPJCLIN,SETCODE
PNAME    PUT   SMPJCLIN,NAMECD
         PUT   SMPJCLIN,J7
         CLOSE SMPJCLIN
         $EPILOG
         EJECT
         VGETSUB
         EJECT
         VPUTSUB
         EJECT
         LTORG
         BITSW JCLIN,NEWMOD
         EJECT
PARMPTR  DC    A(WORDS)
WORDS    DC    A(FUNCTION,MEMBER,DATALEN)
FUNCTION DC    CL8'MOD'
MEMBER   DC    CL8' '
BLANKS   DC    CL80' '
SNAME    DC    CL8' '
LMOD     DC    CL8' '
DATALEN  DC    A(100*80)
DADDR    DC    A(DATA)
         SPACE
INCLUDE  DC    CL80' '
         ORG   INCLUDE
         DC    C' INCLUDE '
DLIBINCL DC    CL8' '
         DC    CL10' '
         DC    C'         * INCLUDE ORIGINAL MODULE'
         ORG
NAMECD   DC    CL80' '
         ORG   NAMECD
         DC    C' NAME    '
NAME     DC    CL8' '
         DC    C'                   * LOAD MODULE NAME '
         ORG
SETCODE  DC    CL80'  SETCODE AC(1)  ****** AUTHORIZED ***'
         ORG
SMPJCLIN DCB   DSORG=PS,MACRF=PM,DDNAME=SMPJCLIN,LRECL=80,RECFM=FB,   XX
               BLKSIZE=6160
J1       DC    CL80' '
         ORG   J1
         DC    C'//LINK  EXEC PGM=IEWL,PARM=''XREF,LIST,LET,NCAL'
         DC    C',SIZE=(768K,100K)'
LEATTC   DC    X'7D'
LEATT    DC    CL8' '
         ORG
J2       DC    CL80'//SYSUT1   DD UNIT=SYSDA,SPACE=(CYL,(1,1))'
J3       DC    CL80'//'
         ORG   J3+2
DLIBDS   DC    CL8' ',C' DD DISP=SHR,DSN=SYS1.'
DLIBDSN  DC    CL8' '
         ORG
J4       DC    CL80'//'
         ORG   J4+2
         DC    C'SYSLMOD  DD DISP=SHR,DSN=SYS1.'
SYSLDSN  DC    CL8' '
         ORG
J5       DC    CL80'//SYSPRINT DD SYSOUT=A'
J6       DC    CL80'//SYSLIN   DD *'
J7       DC    CL80'/*'
         EJECT
DATA     DC    100XL80'00'
         END
