         MACRO
         @M    &ID
         DC    CL5'&ID'
         MEND
FINDMAC  $PROLOG 12
         OPEN  (IN,,MACOUT,OUTPUT)
READ     GET   IN
         CLI   0(R1),C'*'     COMMENT ?
         BE    READ           YES
         CLC   0(2,R1),=C'.*'     COMMENT ?
         BE    READ           YES
         LR    R4,R1          -> INPUT
         LA    R2,10               LOAD MAX SEARCH
INLOOP   CLI   0(R4),C' '          BLANK
         BE    FINDNB              YES - FIND NBLANK
         LA    R4,1(R4)            INCR R5
         BCT   R2,INLOOP
         B     READ                MUST BE IN ERROR
FINDNB   LA    R2,10               LOAD MAX SEARCH
NBLOOP   CLI   0(R4),C' '          BLANK ?
         BNE   HAVEIT              NO - MUST HAVE MACRO OR INST
         LA    R4,1(R4)            INCR R5
         BCT   R2,NBLOOP
         B     READ                MUST BE IN ERROR
HAVEIT   EQU   *
         LM    R5,R7,TBLM     -> TABLE
         USING TABLE,R5
LOOP     DS    0H
         CLC   0(5,R4),INSTR
         BE    READ           BONA FIDE INSTRUCTION
         BXLE  R5,R6,LOOP
         LA    R3,MACRO
         MVC   MACRO,=CL8' '       CLEAR MACRO
         LA    R2,9                MAX SIZE
MVLOOP   MVC   0(1,R3),0(R4)
         CLI   1(R4),C' '          END OF MACRO ?
         BE    UPMTAB
         LA    R3,1(R3)
         LA    R4,1(R4)
         BCT   R2,MVLOOP           KEEP MOVING
UPMTAB   LM    R5,R7,MACTLM
         USING MACTB,R5
MLOOP    CLI   TBMAC,X'00'         NULL ENTRY
         BE    MVMAC
         CLC   TBMAC,MACRO         ALREADY IN TABLE ?
         BE    INTAB               YES
         BXLE  R5,R6,MLOOP
         ABEND 2                   TABLE NOT LARGE ENOUGH
MVMAC    MVC   TBMAC,MACRO
INTAB    LH    R1,MCNT
         AH    R1,=H'01'
         STH   R1,MCNT
         B     READ
EOF      LM    R5,R7,MACTLM
         SR    R9,R9
CNTLP    CLI   0(R5),X'00'         END OF TABLE
         BE    DOSORT              YES
         LA    R9,1(R9)            INCR COUNTER
         BXLE  R5,R6,CNTLP
         SPACE
DOSORT   BCTR  R9,R0               REDUCE R9 BY 1
DOSORTIT LM    R5,R6,MACTLM
         LR    R10,R9                   COUNT OF ENTRIES TO SORT
         SPACE
SORTL1   CLC   TBMAC,10(R5)
         BNH   BUMPIT
         XC    TBMAC(10),10(R5)           SWAP
         XC    10(10,R5),TBMAC              ENTRIES
         XC    TBMAC(10),10(R5)                QUICKLY
BUMPIT   AR    R5,R6
         BCT   R10,SORTL1
         BCT   R9,DOSORTIT
         SPACE
         LM    R5,R7,MACTLM
PLOOP    CLI   TBMAC,X'00'
         BE    CLOSE
         LH    R2,MCNT
         CVD   R2,DWK
         MVC   MACRO,TBMAC
         MVC   MUSE(6),=X'402020202021'
         ED    MUSE(6),DWK+5
PUT      PUT   MACOUT,OPLINE
         BXLE  R5,R6,PLOOP
CLOSE    CLOSE (IN,,MACOUT)
         $EPILOG
         LTORG
DWK      DS    D
IN       DCB   DSORG=PS,MACRF=GL,DDNAME=IN,EODAD=EOF
MACOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=MACOUT,LRECL=80,               X
               RECFM=FB
OPLINE   DC    CL80' '
         ORG   OPLINE+1
MACRO    DC    CL8' '
         DC    CL6' '
MUSE     DC    CL6' '
         ORG
TBLM     DS    0F
         DC    A(TABS)
         DC    A(5)
         DC    A(TABE-5)
TABS     EQU   *
         DC    CL5'DC'
         DC    CL5'DS'
         DC    CL5'MACRO'
         DC    CL5'MEND'
         DC    CL5'EQU'
         DC    CL5'EJECT'
         DC    CL5'SPACE'
         DC    CL5'PRINT'
         DC    CL5'AIF'
         DC    CL5'AGO'
         DC    CL5'SETA'
         DC    CL5'SETB'
         DC    CL5'SETC'
         DC    CL5'START'
         @M    CSECT
         @M    DSECT
         @M    DXD
         @M    CXD
         @M    ENTRY
         @M    EXTRN
         @M    WXTRN
         @M    USING
         @M    DROP
         @M    TITLE
         @M    PUNCH
         @M    ICTL
         @M    ISEQ
         @M    REPRO
         @M    ORG
         @M    OPSYN
         @M    PUSH
         @M    POP
         @M    LTORG
         @M    CNOP
         @M    COPY
         @M    END
         @M    MEXIT
         @M    MNOTE
         @M    ACTR
         @M    ANOP
         @M    GBLA
         @M    GBLB
         @M    GBLC
         @M    LCLA
         @M    LCLB
         @M    LCLC
         @M    B
         @M    BR
         @M    NOP
         @M    NOPR
         @M    BH
         @M    BHR
         @M    BL
         @M    BLR
         @M    BE
         @M    BER
         @M    BNH
         @M    BNHR
         @M    BNL
         @M    BNLR
         @M    BNE
         @M    BNER
         @M    BO
         @M    BOR
         @M    BNO
         @M    BNOR
         @M    BP
         @M    BPR
         @M    BM
         @M    BMR
         @M    BNP
         @M    BNPR
         @M    BNM
         @M    BNMR
         @M    BNZ
         @M    BNZR
         @M    BZ
         @M    BZR
         DC    CL5'SPM'
         DC    CL5'BALR'
         DC    CL5'BCTR'
         DC    CL5'BCR'
         DC    CL5'SSK'
         DC    CL5'ISK'
         DC    CL5'SVC'
         DC    CL5'MVCL'
         DC    CL5'CLCL'
         DC    CL5'LPR'
         DC    CL5'LNR'
         DC    CL5'LTR'
         DC    CL5'LCR'
         DC    CL5'NR'
         DC    CL5'CLR'
         DC    CL5'OR'
         DC    CL5'XR'
         DC    CL5'LR'
         DC    CL5'CR'
         DC    CL5'AR'
         DC    CL5'SR'
         DC    CL5'MR'
         DC    CL5'DR'
         DC    CL5'ALR'
         DC    CL5'SLR'
         DC    CL5'LPDR'
         DC    CL5'LNDR'
         DC    CL5'LTDR'
         DC    CL5'LCDR'
         DC    CL5'HDR'
         DC    CL5'LRDR'
         DC    CL5'MXR'
         DC    CL5'MXDR'
         DC    CL5'LDR'
         DC    CL5'CDR'
         DC    CL5'ADR'
         DC    CL5'SDR'
         DC    CL5'MDR'
         DC    CL5'DDR'
         DC    CL5'AWR'
         DC    CL5'SWR'
         DC    CL5'LPER'
         DC    CL5'LNER'
         DC    CL5'LTER'
         DC    CL5'LCER'
         DC    CL5'HER'
         DC    CL5'LRER'
         DC    CL5'AXR'
         DC    CL5'SXR'
         DC    CL5'LER'
         DC    CL5'CER'
         DC    CL5'AER'
         DC    CL5'SER'
         DC    CL5'MER'
         DC    CL5'DER'
         DC    CL5'AUR'
         DC    CL5'SUR'
         DC    CL5'STH'
         DC    CL5'LA'
         DC    CL5'STC'
         DC    CL5'IC'
         DC    CL5'EX'
         DC    CL5'BAL'
         DC    CL5'BCT'
         DC    CL5'BC'
         DC    CL5'LH'
         DC    CL5'CH'
         DC    CL5'AH'
         DC    CL5'SH'
         DC    CL5'MH'
         DC    CL5'CVD'
         DC    CL5'CVB'
         DC    CL5'ST'
         DC    CL5'N'
         DC    CL5'CL'
         DC    CL5'O'
         DC    CL5'X'
         DC    CL5'L'
         DC    CL5'C'
         DC    CL5'A'
         DC    CL5'S'
         DC    CL5'M'
         DC    CL5'D'
         DC    CL5'AL'
         DC    CL5'SL'
         DC    CL5'STD'
         DC    CL5'MXD'
         DC    CL5'LD'
         DC    CL5'CD'
         DC    CL5'AD'
         DC    CL5'SD'
         DC    CL5'MD'
         DC    CL5'DD'
         DC    CL5'AW'
         DC    CL5'SW'
         DC    CL5'STE'
         DC    CL5'LE'
         DC    CL5'CE'
         DC    CL5'AE'
         DC    CL5'SE'
         DC    CL5'ME'
         DC    CL5'DE'
         DC    CL5'AU'
         DC    CL5'SU'
         DC    CL5'SSM'
         DC    CL5'LPSW'
         DC    CL5'WRD'
         DC    CL5'RDD'
         DC    CL5'BXH'
         DC    CL5'BXLE'
         DC    CL5'SRL'
         DC    CL5'SLL'
         DC    CL5'SRA'
         DC    CL5'SLA'
         DC    CL5'SRDL'
         DC    CL5'SLDL'
         DC    CL5'SRDA'
         DC    CL5'SLDA'
         DC    CL5'STM'
         DC    CL5'TM'
         DC    CL5'MVI'
         DC    CL5'TS'
         DC    CL5'NI'
         DC    CL5'CLI'
         DC    CL5'OI'
         DC    CL5'XI'
         DC    CL5'LM'
         DC    CL5'STNSM'
         DC    CL5'STOSM'
         DC    CL5'SIGP'
         DC    CL5'MC'
         DC    CL5'LRA'
         DC    CL5'STCTL'
         DC    CL5'LCTL'
         DC    CL5'CS'
         DC    CL5'CDS'
         DC    CL5'CLM'
         DC    CL5'STCM'
         DC    CL5'ICM'
         DC    CL5'MVN'
         DC    CL5'MVC'
         DC    CL5'MVZ'
         DC    CL5'NC'
         DC    CL5'CLC'
         DC    CL5'OC'
         DC    CL5'XC'
         DC    CL5'TR'
         DC    CL5'TRT'
         DC    CL5'ED'
         DC    CL5'EDMK'
         DC    CL5'SRP'
         DC    CL5'MVO'
         DC    CL5'PACK'
         DC    CL5'UNPK'
         DC    CL5'ZAP'
         DC    CL5'CP'
         DC    CL5'AP'
         DC    CL5'SP'
         DC    CL5'MP'
         DC    CL5'DP'
         DC    CL5'SIO'
         DC    CL5'SIOF'
         DC    CL5'TIO'
         DC    CL5'CLRIO'
         DC    CL5'HIO'
         DC    CL5'HDV'
         DC    CL5'TCH'
         DC    CL5'IPK'
         DC    CL5'PTLB'
         DC    CL5'RRB'
         DC    CL5'SCK'
         DC    CL5'SCKC'
         DC    CL5'SPT'
         DC    CL5'SPX'
         DC    CL5'SPKA'
         DC    CL5'STIDC'
         DC    CL5'STCK'
         DC    CL5'STCKC'
         DC    CL5'STAP'
         DC    CL5'STIDP'
         DC    CL5'STPT'
         DC    CL5'STPX'
TABE     EQU   *
MACTLM   DC    A(MACTS)
         DC    A(10)
         DC    A(MACTE-10)
MACTS    DC    200XL10'00'          200 MACRO DIFF MACRO ENTRIES
MACTE    EQU   *
         DSECT
TABLE    EQU   *
INSTR    DS    CL5
         SPACE 2
         DSECT
MACTB    DS    0D
TBMAC    DS    D
MCNT     DS    H
         END
