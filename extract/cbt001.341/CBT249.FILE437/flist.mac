         TITLE 'FLIST ISPF TABLE BUILD APPLICATION'
*---------------------------------------------------------------------*
*  FLIST                                                              *
*                                                                     *
*  FUNCTION:  THIS PROGRAM IS AN ISPF APPLICATION WHICH WILL          *
*             BUILD AN ISPF TABLE OF THE NAME FLIST.  THE INPUT       *
*             IS DETERMINED BY ISPF VARIABLE HLIST.  IF HLIST IS      *
*             YES THEN THE INPUT IS THE OUTPUT OF THE HSM TSO COMMAND *
*             HLIST.  OTHERWISE THE INPUT IS THE OUTPUT OF THE        *
*             LISTC COMMAND.                                          *
*                                                                     *
*             IF VARIABLE HLIST = ARCH THEN DATASET ARCHIVE IS        *
*             OPENED AND THE TABLE CREATED WITH VARIABLE CLACT        *
*             BEING SET TO THE ARCHIVE DATE.                          *
*                                                                     *
*                                                                     *
*  OPERATION: 1) TBCREATE TABLE FLIST WITH VARIABLES CLDSN CLVOL CLACT*
*             2) VDEFINE VARIABLES CLDSN, CLVOL, CLACT AND HLIST      *
*             3) VGET HLIST                                           *
*             3A) VGET FOPT                                           *
*             4) SET HLIST MODE (YES OR NO)                           *
*             5A) IF NOT HLIST THEN INPUT IS OUTPUT OF LISTC          *
*             5B) IF HLIST THEN INPUT IS OUTPUT OF HSM HLIST          *
*             6) OPEN INPUT FILE                                      *
*             7A) READ INPUT AND SELECT DSN AND VOL FOR LISTC MODE    *
*             7B) READ INPUT AND SELECT DSN AND DATE FOR HLIST MODE   *
*             7C) PROCESS BASED UPON FOPT                             *
*             7D) TBADD                                               *
*             8) CLOSE INPUT FILE                                     *
*             9) TBTOP                                                *
*                                                                     *
*  EXTERNAL REFERENCES:                                               *
*             MODULES:  ISPLINK                                       *
*             ISPF VARIABLES: HLIST                                   *
*                                                                     *
*  MACROS:    $PROLOG, $EPILOG, BITSW, IFON, CALL, SETON,             *
*             OPEN, CLOSE, GET, DCB                                   *
*                                                                     *
*---------------------------------------------------------------------*
*  AUTHOR:    LIONEL DYCK                                             *
*             ROCKWELL INTERNATIONAL                                  *
*             2201 SEAL BEACH BLVD.                                   *
*             P.O. BOX 2515                                           *
*             SEAL BEACH, CALIFORNIA 90740                            *
*             MAIL CODE 110-SH28                                      *
*             PHONE (213) 594-1125                                    *
*---------------------------------------------------------------------*
*  CHANGE HISTORY:                                                    *
*             V1.0 - CODED 06-23-83                                   *
*             V1.1 - 9/20/83 ADDED CODE FOR ROCKWELL ARCHIVE FILE     *
*                            ADDED REPLACE TO TBCREATE                *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
FLIST    $PROLOG
CREATE   DS    0H
         CALL  ISPLINK,(TBCREATE,TABLENAM,,NAMES,NOWRITE,REPLACE),VL
         CALL  ISPLINK,(VDEFINE,CLDSN,VCLDSN,CHAR,LCLDSN),VL
         CALL  ISPLINK,(VDEFINE,CLVOL,VCLVOL,CHAR,LCLVOL),VL
         CALL  ISPLINK,(VDEFINE,CLACT,VCLACT,CHAR,LCLACT),VL
         CALL  ISPLINK,(VDEFINE,HLIST,VHLIST,CHAR,LHLIST),VL
         CALL  ISPLINK,(VDEFINE,FOPT,VFOPT,CHAR,LFOPT),VL
         CALL  ISPLINK,(VGET,HLIST),VL
         CALL  ISPLINK,(VGET,FOPT),VL
         CLI   VHLIST,C'Y'         HLIST MODE ?
         BE    SHLIST              YES
         CLI   VHLIST,C'A'         ARCHIVE MODE ?
         BE    ARCH                YES
         B     NHLIST
SHLIST   DS    0H
         SETON MHLIST              YES - SET HLIST MODE
         B     NHLIST
ARCH     DS    0H
         SETON FARCH               SET ARCHIVE FLAG ON
NHLIST   DS    0H
         OPEN  (FLISTIN)
         IFON  MHLIST,DOHLIST
READ     GET   FLISTIN
         IFON  FARCH,DOARCH
         LH    R2,0(R1)            LOAD RDW
         CH    R2,=H'30'
         BL    READ
         SH    R2,=H'05'           REDUCE BY RDW LENGTH AND CC
         LR    R5,R1               SAVE INPUT ADDR
         LA    R5,4(R5)            -> RDW
         CLC   =C'NONVSAM',1(R5)
         BE    DSN1
         CLC   =C'VOLSER',8(R5)
         BE    VOL1
         B     READ
DSN1     EQU   *
         MVC   VCLDSN,BLANKS
         MVC   VCLVOL,BLANKS
         LR    R1,R2               LOAD RECORD LENGTH
         SH    R1,=H'17'           REDUCE BY OFFSET
         CH    R1,=H'44'           MORE THAN 44 ?
         BL    MOVEIT              NO - USE WHAT WE HAVE
         LH    R1,=H'44'           ELSE USE 44
MOVEIT   EX    R1,MVCDSN
         B     READ
MVCDSN   MVC   VCLDSN(0),17(R5)    MOVE DSN TO CLDSN
VOL1     MVC   VCLVOL(6),26(R5)    MOVE VOL AND ADD
ADDIT    EQU   *                   ADD ENTRY TO TABLE
         CLI   VFOPT,C'B'
         BE    DOADD
         CLI   VFOPT,C'M'          MIGRATED D/S ONLY ?
         BE    DOMIG               YES
         CLI   VFOPT,C'O'          ONLINE D/S ONLY ?
         BE    DOON                YES
         B     DOADD
DOMIG    DS    0H
         CLC   VCLVOL(6),=C'MIGRAT'  MIGRATED D/S ?
         BE    DOADD               YES - ADD IT
         B     READ                NO  - SKIP IT
DOON     DS    0H
         CLC   VCLVOL(6),=C'MIGRAT'   MIGRATED D/S
         BE    READ                YES - SKIP IT
DOADD    DS    0H
         CALL  ISPLINK,(TBADD,TABLENAM),VL
         IFON  MHLIST,DOHLIST
         B     READ
DOARCH   DS    0H
         CLI   0(R1),X'00'         FIRST ARCHIVE RECORD ?
         BE    READ                YES - SKIP IT
         MVC   VCLDSN,0(R1)        SET DSN
         MVC   VCLVOL,=CL8'ARCHIVE' SET VOL AS ARCHIVE
         MVC   VCLACT(6),44(R1)    SET CLACT AS ARCHIVE DATE
         B     DOADD
DOHLIST  EQU   *
         GET   FLISTIN
         CLC   =C'DSNAME =',1(R1)
         BE    DSN2                HAVE DSN
         B     DOHLIST
DSN2     MVC   VCLDSN,10(R1)        MOVE DSN TO CLDSN
         GET   FLISTIN
         GET   FLISTIN
         GET   FLISTIN
         GET   FLISTIN
         GET   FLISTIN
         MVC   VCLVOL(8),67(R1)     MOVE DATE TO CLVOL
         B     ADDIT
EOF      CLOSE (FLISTIN)
         CALL  ISPLINK,(TBTOP,TABLENAM),VL
         $EPILOG
         EJECT
*----------------------------------------------------------------------
         BITSW MHLIST,FARCH
*----------------------------------------------------------------------
CHAR     DC    C'CHAR'
TABLENAM DC    CL8'FLIST'
TBADD    DC    C'TBADD'
NOWRITE  DC    C'NOWRITE'
REPLACE  DC    C'REPLACE'
TBCREATE DC    C'TBCREATE'
TBTOP    DC    C'TBTOP'
VDEFINE  DC    C'VDEFINE'
VGET     DC    C'VGET'
*----------------------------------------------------------------------
NAMES    DC    C'(CLDSN,CLVOL,CLACT)'
CLDSN    DC    C'(CLDSN)'
CLVOL    DC    C'(CLVOL)'
CLACT    DC    C'(CLACT)'
HLIST    DC    C'(HLIST)'
FOPT     DC    C'(FOPT)'
*----------------------------------------------------------------------
VCLDSN   DC    CL44' '
VCLVOL   DC    CL8' '
VCLACT   DC    CL8' '
VHLIST   DC    CL4' '
VFOPT    DC    CL1' '
*----------------------------------------------------------------------
LCLDSN   DC    A(L'VCLDSN)
LCLVOL   DC    A(L'VCLVOL)
LCLACT   DC    A(L'VCLACT)
LHLIST   DC    A(L'VHLIST)
LFOPT    DC    A(L'VFOPT)
*----------------------------------------------------------------------
BLANKS   DC    CL44' '
         EJECT
FLISTIN  DCB   DSORG=PS,MACRF=GL,DDNAME=FLISTIN,EODAD=EOF
         END
