         TITLE '* * *  SETSCRN  * * *'
***********************************************************************
* SETSCRN                                                             *
*                                                                     *
* PURPOSE - TO PROVIDE A MEANS OF SETTING A 3278-M5 SCREEN SIZE TO    *
*           24X80 FOR A TIME AND THEN RESETTING IT BACK TO WHATEVER   *
*           IT WAS.                                                   *
*                                                                     *
* OPERATION - SETSCRN USES THE UPTUSER FIELD FOR SAVING AND RESTORING *
*             THE SCREEN SIZE.  IF THIS IS NOT A TUBE, NOTHING IS     *
*             DONE.  IF THIS IS A TUBE AND THE UPTUSER FIELD IS       *
*             HEX ZEROS, THEN THE CURRENT SCREEN SIZE IS SAVED.  IF   *
*             IT IS NOT ZEROS, THE SCREEN SIZE IS RESTORED AND THE    *
*             UPTUSER FIELD IS RESET TO HEX ZEROS.                    *
*                                                                     *
* WRITTEN BY DOUG MOORE - JAN 81                                      *
*                                                                     *
***********************************************************************
         SPACE
SETSCRN  CSECT
SETSCR   DS    0H                  SIX CHAR ENTRY FOR FORTRAN PROGRAMS
         ENTRY SETSCR
         SAVE  (14,12),,SETSCRN-&SYSDATE-&SYSTIME   SAVE REGISTERS
         USING SETSCRN,R12
         LR    R12,R15             LOAD BASE REGISTER
         LR    R2,R13              SAVE PREVIOUS RSA POINTER
         LA    R13,RSA             POINT TO MY RSA
         ST    R2,4(R13)           CHAIN SAVE AREAS
         ST    R13,8(R2)           CHAIN SAVE AREAS
         SPACE
         L     R2,PSAAOLD          POINT TO CURRENT ASCB
         USING ASCB,R2
         L     R3,ASCBTSB          POINT TO TSB
         LTR   R3,R3               Q. IS THIS TSO
         BZ    RETURN              ...NO, FORGET IT
         L     R2,PSATOLD          POINT TO CURRENT TCB
         USING TCB,R2
         L     R2,TCBJSCB          POINT TO JSCB
         USING IEZJSCB,R2
         L     R2,JSCBPSCB         POINT TO PSCB
         USING PSCB,R2
         L     R2,PSCBUPT          POINT TO UPT
         USING UPT,R2
         OC    UPTUSER+8(2),UPTUSER+8  Q. IS ANYTHING SAVED
         BNZ   RESET               ...YES, DO RESET
         EJECT
SET      DS    0H
         GTSIZE                    , GET SCREEN SIZE
         LTR   R0,R0               Q. IS THIS A TUBE
         BZ    RETURN              ...NO, DONT DO ANYTHING
         STC   R0,UPTUSER+8        STORE NUMBER OF LINES/SCREEN
         STC   R1,UPTUSER+9        STORE NUMBER OF CHARS/LINE
         STSIZE SIZE=80,LINE=24    SET SCREEN TO 24X80
         B     RETURN              ALL DONE
         SPACE
RESET    DS    0H
         XC    SVLINE(8),SVLINE    ZERO SVLINE AND SVSIZE
         MVC   SVLINE+3(1),UPTUSER+8  GET SAVED # LINES/SCREEN
         MVC   SVSIZE+3(1),UPTUSER+9  GET SAVED # CHARS/LINE
         STSIZE SIZELOC=SVSIZE,LINELOC=SVLINE   RESTORE SCREEN SIZE
         XC    UPTUSER+8(2),UPTUSER+8  CLEAR FIELD
         B     RETURN              ALL DONE
         EJECT
RETURN   DS    0H
         L     R13,4(R13)          RESTORE CALLERS RSA
         RETURN (14,12),RC=0       RESTORE REGISTERS AND RETURN
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
PSAAOLD  EQU   X'224'              ASCB OLD POINTER IN PSA
PSATOLD  EQU   X'21C'              TCB OLD POINTER IN PSA
         EJECT
RSA      DS    18D                 REGISTER SAVE AREA
SVLINE   DC    F'0'                NUMBER OF LINES/SCREEN
SVSIZE   DC    F'0'                NUMBER OF CHARS/LINE
         SPACE 5
         LTORG
         EJECT
         PRINT  NOGEN
         IHAASCB
         IKJTCB
         IEZJSCB
         IKJPSCB
         IKJUPT
         EJECT
         END   SETSCRN
