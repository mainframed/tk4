         TITLE '  R E V S M F   '
***********************************************************************
*                                                                     *
*        REVSMF - A SUBROUTINE OF THE REVIEW TSO COMMAND              *
*                                                                     *
***********************************************************************
*
*        WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).
*        INSTALLATION. AIR FORCE DATA SERVICES CENTER, PENTAGON.
*        DATE WRITTEN. MARCH 18 1982.
*        DATE UPDATED. MARCH 18 1982.
*        ATTRIBUTES. RE-ENTRANT.
*        LOCAL MACROS USED. NONE.
*        DESCRIPTION.
*         THIS SUBROUTINE FORMATS AN SMF RECORD INTO A LINE OF DATA
*         FOR THE SCREEN.
*
         SPACE
REVSMF   START
         USING *,R10
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'REVSMF '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(0)
@PROLOG  STM   14,12,12(13)
         LR    R10,R15             BASE
         LM    R1,R4,0(R1)         LENGTH, RECORD, OUTPUT, WORK
         USING @DATA,R4
         LR    R6,R3
FILLSMF  MVI   0(R6),C' '
         MVC   1(79,R6),0(R6)
         SR    R0,R0
         IC    R0,1(,R2)           RECORD TYPE
         CVD   R0,DOUBLE
         MVC   0(4,R6),=X'40202120'
         ED    0(4,R6),DOUBLE+6
         MVC   0(3,R6),1(R6)
         MVI   3(R6),C' '
         MVC   DOUBLE(4),2(R2)     TIME
         L     R1,DOUBLE
         LA    R15,4(,R6)
         BAL   R14,TIMEX
         UNPK  14(5,R6),7(3,R2)    DATE
         OI    18(R6),X'F0'
         MVC   13(2,R6),14(R6)
         MVI   15(R6),C'.'
         MVC   20(4,R6),10(R2)
         CLI   1(R2),70
         BL    *+12
         CLI   1(R2),75
         BNH   TYPTR               TYPES 70-75
         CLI   1(R2),0
         BE    TYPTR
         CLI   1(R2),9
         BE    TYPTR
         CLI   1(R2),21
         BE    TYPTR
         CLI   1(R2),47
         BE    TYP47
         CLI   1(R2),48
         BNE   NOT47
TYP47    MVC   34(16,R6),24(R2)
         B     TYPTR
NOT47    EQU   *
         MVC   25(8,R6),14(R2)     JOBNAME
         CLI   1(R2),14
         BE    TYP14
         CLI   1(R2),15
         BNE   NOT14
TYP14    MVC   34(44,R6),64(R2)
         B     TYPTR
NOT14    EQU   *
TYPTR    EQU   *
         LM    14,12,12(R13)
         BR    R14
         SPACE
************************************************************
*                                                          *
*         SUBROUTINE TO CONVERT TIME TO HH.MM.SS           *
*                                                          *
************************************************************
         SPACE
*         INPUT: TIME IN R1, OUTPUT ADDRESS IN R15
         SPACE
TIMEX    SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  0(2,R15),DOUBLE+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'6000'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  3(2,R15),DOUBLE+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'100'
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  6(2,R15),DOUBLE+6(2)
         MVI   2(R15),C'.'
         MVI   5(R15),C'.'
         BR    R14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
         SPACE
         DC    0D'0'               END OF CSECT
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D                   SIZE OF THIS AREA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
