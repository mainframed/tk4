* FROM   SHARE TAPE FILE 106                              *LBD 02/84*
         TITLE 'MAPVTOC - REPLACE SPF 3.7 FOR INDEXED VTOC'
***********************************************************************
*                                                                     *
*  THIS PROGRAM REPLACE THE SPF 3.7 FUNCTION TO LIST THE ENTRIES      *
*  IN A VTOC.  IT USES THE CVAF MACROS TO ACCESS A VTOC, THEREFORE    *
*  IT REQUIRES DF/DS IN THE SYSTEM, EVEN IF THE VTOC BEING LISTED     *
*  IS NOT INDEXED.                                                    *
*                                                                     *
*  OUTPUT IS TO SYSPRINT, TO BE BROWSED VIA THE DIALOG MANAGER        *
*                                                                     *
*  RETURN CODES:                                                      *
*    0 - EVERYTHING COPASETIC                                         *
*    4 - VOLUME NOT MOUNTED                                           *
*    8 - INTERNAL PROGRAM ERROR                                       *
*                                                                     *
***********************************************************************
         SPACE 1
         MACRO
         CLRCVPL
         XC    CVFCTN(CLEAR1),CVFCTN
         XC    CVIRCDS(CLEAR2),CVIRCDS
         MEND
         SPACE 1
******   REGISTER EQUATES   ******
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4                        -> I/O AREA (DSCB)
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8                        -> VTOC DEB
R9       EQU   9
R10      EQU   10                       INTERNAL BAL REGISTER
R11      EQU   11                       2ND BASE REGISTER
R12      EQU   12                       BASE REGISTER
R13      EQU   13                       SAVE AREA POINTER
R14      EQU   14
R15      EQU   15
         SPACE 1
         IEZBITS
         TITLE 'MAPVTOC - LINKAGE AND INITIALIZATION'
MAPVTOC  CSECT
         SAVE  (14,12),,*.&SYSDATE..&SYSTIME
         LR    R12,R15
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         L     R3,0(,R1)                SAVE PARMS
         USING MAPVTOC,R12,R11
         GETMAIN RU,LV=4096
         LR    R15,R13                  STANDARD LINKAGE
         LR    R13,R1                   OUR SAVE AREA
         ST    R13,8(,R15)              FORWARD CHAIN
         ST    R15,4(,R13)              BACK CHAIN
         USING WORKAREA,R13
         MVC   VOLID,2(R3)              SAVE VOL SER
* MAKE SURE THIS VOLUME IS REALLY AN ONLINE DISK
         L     R15,16                POINT TO CVT
         SLR   R1,R1
         L     R15,40(,R15)
UCBEND   DS    0H
         CLC   0(2,R15),ZERO         NULL ENTRY?
         BE    BUMP                  YES
         CLC   =X'FFFF',0(R15)       END OF UCB LIST?
         BE    RETURN4               YES
         ICM   R1,B'0011',0(R15)
         TM    18(R1),X'20'          DASD?
         BO    CHKDASD               YES
BUMP     DS    0H
         LA    R15,2(,R15)            NEXT ADDR
         B     UCBEND
CHKDASD  DS    0H
         TM    3(R1),X'80'           ONLINE?
         BZ    BUMP                  NO
         CLC   VOLID,28(R1)          VOL SER THAT WE WANT?
         BNE   BUMP                  NO
         ST    R1,UCB                SAVE THE UCB ADDR
         LR    R3,R1
         EJECT
*
* GOT UCB FOR VOLUME, BUILD FIRST LINE OF OUTPUT
*
         MVC   HDR1VOL,VOLID
         SLR   R2,R2
         IC    R2,19(,R1)               GET UNIT TYPE FIELD
         MH    R2,=H'6'                 ADJUST BY LENGTH OF FIELD
         LA    R2,DASDTBL(R2)           GET DEVTYPE
         MVC   HDR1UNIT,0(R2)
         MVC   HDR1ADDR,13(R1)          UNIT ADDRESS
         MVC   HDR1MNT,=CL7'PRIVATE'
         TM    34(R1),BIT3              IS IT REALLY PRIVATE
         BO    CHECKIX
         MVC   HDR1MNT,=CL7'STORAGE'
         TM    34(R1),BIT5              IS IT REALLY STORAGE
         BO    CHECKIX
         MVC   HDR1MNT,=CL7'PUBLIC'
*
* CHECK FOR INDEXED VTOC
*
CHECKIX  MVC   INPUTUCB,13(R3)       SAVE DEVADDR FOR DYNALLOC
         CVAFTST UCB=(R3)
         CH    R15,=H'8'             INDEX VTOC?
         BL    ALLOC                    NO
         OI    PARMBIT,INDEX         YES
         TITLE 'MAPVTOC - ALLOCATE THE VTOC'
*
* DYNAMICALLY ALLOC THE VTOC
*
ALLOC    DS    0H
         MVC   INPUTDD,VOLID-1             VOLSER OF PACK FOR DDNAME
         MVC   INPUTSER,VOLID              VOLSER OF PACK
         LA    R5,S99BLKPT                 POINTER TO REQUEST BLK
         LA    R6,S99BLK                   POINT TO REQUEST BLK
         ST    R6,S99BLKPT                 STORE IT AT THE POINTER
         USING S99RBP,R5
         USING S99RB,R6
         MVI   S99RBPTR,S99RBPND           LAST INDICATOR
         MVC   S99RBLN(20),ALS99           FORMAT THE REQUEST BLK
         LA    R7,INPUT                    TEXT POINTER
         ST    R7,S99TXTPP                 PUT IT IN CONTROL BLK
         LR    R1,R5                       ADDR OF RB
         DYNALLOC
         LTR   R15,R15                     DID IT WORK?
         BNZ   ALNOPE                      NO
         RDJFCB VTOC                       GOTTA PUT X'04' IN DSNAME
         MVI   JFCBDSNM,X'04'              PUT IT IN
         MVC   JFCBDSNM+1(43),JFCBDSNM
         MVI   JFCBNVOL,X'01'              ONE VOLUME
         MVC   JFCBVOLS,VOLID              VOLSER TO JFCB
         OI    JFCBTSDM,X'48'              DON'T REWRITE IT
         MVC   VTOC+40(7),VOLID-1          DDNAME TO DCB
         SPACE 1
         OPEN  (VTOC,(INPUT)),TYPE=J
         OPEN  (SYSPRINT,OUTPUT)           OPEN THE FILES
         TITLE 'MAPVTOC - PROCESS FORMAT 4 DSCB'
FORMAT4  DS    0H
         XC    BUFLIST(BFLHLN+BFLELN),BUFLIST ZERO BUFFER LIST
         OI    BFLHFL,BFLHDSCB          DSCB'S TO BE READ
         MVI   BFLHNOE,1                ONE BUFFER LIST ENTRY
         LA    R4,FORMAT                ADDRESS OF DSCB BUFFER
         USING DSCB4,R4
         ST    R4,BFLEBUF
         OI    BFLEFL,BFLECHR           CCHHR OF DSCB RETURNED BY CVAF
         MVI   BFLELTH,DSCB4LTH         DATA PORTION OF DSCB
         L     R8,VTOC+44               DEB ADDRESS
         CLRCVPL
         MVI   ERRORCDE,X'01'
         CVAFDIR ACCESS=READ,DSN=F4DSN,DEB=(8),MF=(E,CVPL)
         LTR   R15,R15                  FORMAT 4 SHOULD BE THERE
         BNZ   RETURN8
         MVC   F4CCHHR,BFLEARG          SAVE CCHHR OF FORMAT 4
         EJECT
* NUMBER OF AVAILABLE DSCB'S
         SLR   R1,R1
         ICM   R1,B'0011',DS4DSREC
         ST    R1,DSCB0CNT
* NUMBER OF REMAINING ALTERNATE TRACKS
         LH    R1,DS4NOATK
         CVD   R1,WORK
         ED    HDR6ALT,WORK+6
* SAVE TRACKS PER CYLINDER
         MVC   TRKCYL,DS4DEVSZ+2
* CALCULATE DEVICE SIZE
         SLR   R1,R1
         ICM   R1,B'0011',DS4DEVSZ      CYLINDERS ON DEVICE
         MH    R1,TRKCYL
         CVD   R1,WORK
         ED    HDR4TRKS,WORK+5
         SH    R1,DS4NOATK
         ST    R1,TRKONDEV
* CHECK VSAM INDICATOR
         TM    DS4VSIND,BIT0
         BZ    F4L100
         MVC   HDR7VSAM,=C'OWNED'
F4L100   DS    0H
* DETERMINE VTOC LENGTH
         LA    R9,DS4VTOCE
         XC    TRKLTH,TRKLTH            CLEAR ACCUMULATOR
         BAL   R10,EXTCALC
         L     R1,TRKLTH
         CVD   R1,WORK
         ED    HDRDVLTH,WORK+5
* CALCULATE DSCB'S IN VTOC
         SLR   R0,R0
         SLR   R15,R15
         IC    R15,DS4DEVDT
         MR    R0,R15                   NUMBER OF DSCBS IN VTOC
         ST    R1,DSCBVTOC
* DISPLAY VTOC EXTENT
         LA    R15,HDRCVEXT
         BAL   R10,EXTDIS
         EJECT
* IF INDEXED VTOC, RETRIEVE NUMBER OF AVAILABLE DSCB 0'S FROM INDEX
         TM    PARMBIT,INDEX         INDEXED VTOC?
         BZ    F4L300
         CLRCVPL
         MVI   ERRORCDE,X'02'
         CVAFDSM ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,CTAREA=DSCB0CNT,    X
               DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         BNZ   RETURN8
F4L300   DS    0H
* FORMAT NUMBER OF DSCB 0'S AVAILABLE
         L     R0,DSCB0CNT
         CVD   R0,WORK
         ED    HDRFVFRE,WORK+5
* CALCULATE PERCENT VTOC IN USE
         L     R1,DSCBVTOC             DSCB'S IN VTOC
         SR    R1,R0                    GET USED COUNT
         SLR   R0,R0
         M     R0,=F'100'               ADJUST FOR PERCENT CALC
         D     R0,DSCBVTOC              CALCULATE PERCENT
         CVD   R1,WORK
         ED    HDREUSED,WORK+6
         EJECT
* CHECK FOR OS/CVOL ON VOLUME
         MVI   BFLELTH,96
         MVI   ERRORCDE,X'03'
         CLRCVPL
         CVAFDIR ACCESS=READ,DSN=SYSCTLG,DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         BZ    F4L500
         CH    R15,=H'4'
         BNE   RETURN8
         USING CVPL,R1
         CLI   CVSTAT,STAT001
         DROP  R1
         BNE   RETURN8
         MVC   HDR8CVOL,=CL3'NO'
F4L500   DS    0H
         EJECT
* CHECK FOR SYS1.VVDS ON VOLUME                             *LBD 04/84*
         MVI   BFLELTH,96                                   *LBD 04/84*
         MVI   ERRORCDE,X'03'                               *LBD 04/84*
         MVC   VVDSV,HDR1VOL                                *LBD 04/84*
         CLRCVPL                                            *LBD 04/84*
         CVAFDIR ACCESS=READ,DSN=VVDS,DEB=(8),MF=(E,CVPL)   *LBD 04/84*
         LTR   R15,R15                                      *LBD 04/84*
         BZ    F4L540                                       *LBD 04/84*
         CH    R15,=H'4'                                    *LBD 04/84*
         BNE   RETURN8                                      *LBD 04/84*
         USING CVPL,R1                                      *LBD 04/84*
         CLI   CVSTAT,STAT001                               *LBD 04/84*
         DROP  R1                                           *LBD 04/84*
         BNE   RETURN8                                      *LBD 04/84*
         B     F4L541                                       *LBD 04/84*
F4L540   DS    0H                                           *LBD 04/84*
         MVC   HDRAVVDS(3),=CL3'YES'                        *LBD 04/84*
F4L541   DS    0H                                           *LBD 04/84*
         EJECT
* CHECK FOR SYS1.VTOCIX ON VOLUME                           *LBD 04/84*
         MVI   BFLELTH,96                                   *LBD 04/84*
         MVI   ERRORCDE,X'03'                               *LBD 04/84*
         MVC   VTOCIXV,HDR1VOL                              *LBD 04/84*
         CLRCVPL                                            *LBD 04/84*
         CVAFDIR ACCESS=READ,DSN=VTOCIX,DEB=(8),MF=(E,CVPL) *LBD 04/84*
         LTR   R15,R15                                      *LBD 04/84*
         BZ    F4L550                                       *LBD 04/84*
         CH    R15,=H'4'                                    *LBD 04/84*
         BNE   RETURN8                                      *LBD 04/84*
         USING CVPL,R1                                      *LBD 04/84*
         CLI   CVSTAT,STAT001                               *LBD 04/84*
         DROP  R1                                           *LBD 04/84*
         BNE   RETURN8                                      *LBD 04/84*
         MVC   HADRB(2),HDRDLTH2                            *LBD 04/84*
         MVC   HDRC(2),HDRDLTH2                             *LBD 04/84*
         MVC   HDR9IXVT,=CL3'NO'                            *LBD 04/84*
F4L550   DS    0H                                           *LBD 04/84*
         EJECT
*
* IF NO INDEXED VTOC, ADJUST OUTPUT LINE LENGTHS
*
         TM    PARMBIT,INDEX
         BO    F4L600
         MVC   HDRCIEXT(13),=CL13'NOT ACTIVE'               *LBD 04/84*
         MVC   HDRD(2),HDRDLTH2        CHANGE LENGTH ON OUTPUT
         MVC   HDRE(2),HDRELTH2        FOR A LINE WITHOUT
         MVC   HDRF(2),HDRFLTH2        THE INDEXED INFORMATION
         B     F4L900
*
* IF INDEXED VTOC, GET THE INFORMATION ABOUT THE INDEX
*
F4L600   DS    0H
         MVI   BFLELTH,DSCB1LTH-L'DS1DSNAM
         MVI   ERRORCDE,X'04'
         CLRCVPL
         CVAFSEQ DSN=SYS1VTOC,ACCESS=GT,DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         USING DS1FMTID,R4
         BNZ   RETURN8
* DISPLAY FIRST EXTENT
         LA    R15,HDRCIEXT
         LA    R9,DS1EXT1
         BAL   R10,EXTDIS
* DETERMINE INDEXED VTOC LENGTH
         XC    TRKLTH,TRKLTH            ZERO ACCUMULATOR
         BAL   R10,EXTCALC              GET LENGTH FIRST EXTENT
         OC    DS1EXT2+2(4),DS1EXT2     ANY MORE
         BZ    F4L610                   NO BYPASS
         LA    R9,DS1EXT2
         BAL   R10,EXTCALC              SECOND EXTENT
         OC    DS1EXT3+2(4),DS1EXT3     ANY MORE
         BZ    F4L610                   NO, BYPASS
         LA    R9,DS1EXT3
         BAL   R10,EXTCALC
         OC    DS1PTRDS,DS1PTRDS        NEED ANOTHER EXTENT??
         BZ    F4L610
         MVC   CCHHR,DS1PTRDS
         BAL   R10,READVTOC
         BAL   R10,FORMAT3
F4L610   DS    0H
         L     R1,TRKLTH                DISPLAY INDEXED VTOC LENGTH
         CVD   R1,WORK
         ED    HDRDILTH,WORK+5
         EJECT
* DETERMINE FREE VIR'S IN INDEXED VTOC
F4L700   DS    0H
         MVI   ERRORCDE,X'06'
         CLRCVPL
         CVAFDSM ACCESS=MAPDATA,MAP=INDEX,COUNT=YES,CTAREA=FREEVIR,    X
               DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         BNZ   RETURN8
         L     R1,FREEVIR
         CVD   R1,WORK
         ED    HDRFIFRE,WORK+5
         EJECT
* DETERMINE LEVELS IN INDEXED VTOC
F4L800   XC    BFLEARG,BFLEARG          SET RBA TO ZERO
         LA    R1,2048
         MVI   BFLELTH,2048/256         INDICATE LENGTH OF VIR
         MVI   BFLEFL,BFLERBA           INDICATE READ BY RBA
         OI    BFLHFL,BFLHVIR           BUF LIST DESCRIBE VIR
         NI    BFLHFL,255-BFLHDSCB        AND NOT DSCB
         MVI   ERRORCDE,X'07'
         CLRCVPL
         CVAFDIR ACCESS=READ,BUFLIST=BUFLIST,DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         BNZ   RETURN8
         CLC   =C'VIXM',FORMAT          VIXM SHOULD BE FIRST
         BNE   RETURN8
         MVC   BFLEARBA,FORMAT+40       GET RBA OF HIGH VIER
         MVI   ERRORCDE,X'08'
         CLRCVPL
         CVAFDIR ACCESS=READ,BUFLIST=BUFLIST,DEB=(8),MF=(E,CVPL)
         LTR   R15,R15
         BNZ   RETURN8
         SLR   R1,R1
         IC    R1,FORMAT+16             GET LEVEL OF THIS VIER
         CVD   R1,WORK
         ED    HDREILVL,WORK+6
F4L900   DS    0H
         TITLE 'MAPVTOC - PROCESS FREE SPACE EXTENTS'
F5L000   DS    0H
         TM    PARMBIT,INDEX            CHECK FOR INDEXED VTOC
         BO    F5L200
         SPACE 2
* UNINDEXED VTOC, READ FORMAT 5'S FOR FREE SPACE
         MVC   CCHHR(4),F4CCHHR
         MVI   CCHHR+4,X'02'            READ SECOND DSCB IN VTOC
F5L005   BAL   R10,READVTOC
         USING DSCB5,R4
         LA    R9,DS5AVEXT
         LA    R2,8                     EIGHT EXTENTS HERE
F5L010   DS    0H
         CLC   0(2,R9),ZERO
         BE    F5L100
         BAL   R10,FREECALC
         LA    R9,5(,R9)
         BCT   R2,F5L010
         LA    R9,DS5MAVET              18 EXTENTS HERE
         LA    R2,18
F5L020   DS    0H
         CLC   0(2,R9),ZERO
         BE    F5L100
         BAL   R10,FREECALC
         LA    R9,5(,R9)
         BCT   R2,F5L020
F5L100   DS    0H
         CLC   DS5PTRDS,ZERO            ANY MORE FORMAT 5'S
         BE    F5L700                   NO, GO DO FINAL STUFF
         MVC   CCHHR,DS5PTRDS           YES, SAVE CCHHR
         B     F5L005                   AND LOOP THROUGH AGAIN
         EJECT
F5L200   DS    0H
* GET FREE SPACE INFORMATION FROM INDEXED VTOC
         MVI   0(R4),255                GET UP TO 255 EXTENTS
         XC    1(2,R4),1(R4)            START AT THE FIRST ONE
         MVI   ERRORCDE,X'09'
F5L205   CVAFDSM ACCESS=MAPDATA,COUNT=NO,MAP=VOLUME,EXTENTS=(4),       X
               MF=(E,CVPL)
         LTR   R15,R15
         BZ    F5L207
         CH    R15,=H'4'
         BH    RETURN8
         CLI   CVSTAT,STAT032           ONLY VALID FOR RC=4
         BNE   RETURN8
F5L207   LA    R2,255                   EXTENTS TO GO THOUGH
         LA    R9,1(,R4)                POINT TO FIRST
F5L210   DS    0H
         CLC   0(2,R9),ZERO
         BE    F5L300
         BAL   R10,FREECALC
         LA    R9,5(,R9)
         BCT   R2,F5L210
F5L300   DS    0H
         CLI   CVSTAT,STAT032           ANY MORE TO FIND
         BE    F5L700                     NO, GO DO FINAL STUFF
         SH    R9,=H'5'                 BACK UP ONE EXTENT
         MVC   1(2,R4),0(R9)              SAVE RELATIVE TRACK
         B     F5L205                     AND GO READ AGAIN
         EJECT
F5L700   DS    0H
* MAKE ALL THIS STUFF ABOUT FREE SPACE PRINTABLE
         L     R1,FREECYL               FREE CYLINDERS
         CVD   R1,WORK
         ED    HDR4FCYL,WORK+6
         MH    R1,TRKCYL
         A     R1,FREETRK               FREE TRACKS
         CVD   R1,WORK
         ED    HDR4FTRK,WORK+5
         L     R1,FREECYLE              FREE CYLINDER EXTENTS
         CVD   R1,WORK
         ED    HDR5FCYL,WORK+6
         L     R1,FREETRKE              FREE TRACK EXTENTS
         CVD   R1,WORK
         ED    HDR5FTRK,WORK+5
         LH    R1,FREE1                 LARGEST FREE CYLINDERS
         CVD   R1,WORK
         ED    HDR6FCYL,WORK+6
         MH    R1,TRKCYL
         SLR   R0,R0
         IC    R0,FREE1+2               LARGEST FREE TRACKS
         AR    R1,R0
         CVD   R1,WORK
         ED    HDR6FTRK,WORK+5
         LH    R1,FREE2
         CVD   R1,WORK
         ED    HDR7FCYL,WORK+6
         MH    R1,TRKCYL
         SLR   R0,R0
         IC    R0,FREE2+2
         AR    R1,R0
         CVD   R1,WORK
         ED    HDR7FTRK,WORK+5
         LH    R1,FREE3
         CVD   R1,WORK
         ED    HDR8FCYL,WORK+6
         MH    R1,TRKCYL
         SLR   R0,R0
         IC    R0,FREE3+2
         AR    R1,R0
         CVD   R1,WORK
         ED    HDR8FTRK,WORK+5
         LH    R1,FREE4
         CVD   R1,WORK
         ED    HDR9FCYL,WORK+6
         MH    R1,TRKCYL
         SLR   R0,R0
         IC    R0,FREE4+2
         AR    R1,R0
         CVD   R1,WORK
         ED    HDR9FTRK,WORK+5
         EJECT
* CALCULATE TOTAL FREE SPACE IN TRACKS TO FIGURE PERCENT VOLUME IN USE
         L     R0,FREECYL
         MH    R0,TRKCYL
         A     R0,FREETRK
         L     R1,TRKONDEV
         SR    R1,R0                    R1 NOW HAS PERCENT IN USE
         SLR   R0,R0
         M     R0,=F'100'              ADJUST FOR PERCENT
         D     R0,TRKONDEV
         CVD   R1,WORK
         ED    HDR5USED,WORK+6
         TITLE 'MAPVTOC - FORMAT AND PRINT HEADER LINES'
ENDVTOC  DS    0H
         PUT   SYSPRINT,HDR1
         PUT   SYSPRINT,HDR2
         PUT   SYSPRINT,HDR3
         PUT   SYSPRINT,HDR4
         PUT   SYSPRINT,HDR5
         PUT   SYSPRINT,HDR6
         PUT   SYSPRINT,HDR7
         PUT   SYSPRINT,HDR8
         PUT   SYSPRINT,HDR9
         LH    R1,FREE5
         CVD   R1,WORK
         MVC   HDRAFCYL,=X'40202120'
         ED    HDRAFCYL,WORK+6
         MH    R1,TRKCYL
         SLR   R0,R0
         IC    R0,FREE5+2
         AR    R1,R0
         CVD   R1,WORK
         MVC   HDRAFTRK,=X'402020202120'
         ED    HDRAFTRK,WORK+5
         PUT   SYSPRINT,HDRA
         PUT   SYSPRINT,HDR2
         PUT   SYSPRINT,HADRB
         PUT   SYSPRINT,HDRC
         PUT   SYSPRINT,HDRD
         PUT   SYSPRINT,HDRE
         PUT   SYSPRINT,HDRF
         B     ENDIT
         TITLE 'MAPVTOC - SUBROUTINE FOR ALLOCATION FAILURE'
***********************************************************************
*                                                                     *
* ALNOPE - ALLOCATION FAILED, GIVE WTO WITH DIAGNOSTIC INFO           *
*                                                                     *
***********************************************************************
ALNOPE   DS    0H
         ST    R15,DYNCODE                 SAVE THE SVC 99 RETURN CODE
         MVC   WTOVERB,S99VERB             GET VERB CODE
         TR    WTOVERB,DECTAB              MAKE IT DECIMAL
         UNPK  WORK(5),S99FLG11(3)         GET FLAGS 1 CODE
         NC    WORK(5),=X'0F0F0F0F00'      TURN OFF SIGNS
         MVC   WTOFLG1,WORK                MOVE TO WTO AREA
         TR    WTOFLG1,DECTAB              MAKE IT DECIMAL
         UNPK  WORK(5),S99ERROR(3)         GET ERROR CODE
         NC    WORK(5),=X'0F0F0F0F00'      TURN OFF SIGNS
         MVC   WTOERR,WORK                 MOVE TO WTO AREA
         TR    WTOERR,DECTAB               MAKE IT DECIMAL
         UNPK  WORK(5),S99INFO(3)          GET INFO CODE
         NC    WORK(5),=X'0F0F0F0F00'      TURN OFF SIGNS
         MVC   WTOINFO,WORK                MOVE TO WTO AREA
         TR    WTOINFO,DECTAB              MAKE IT DECIMAL
         UNPK  WORK(5),S99FLG21(3)         1ST PART OF FLAGS 2
         NC    WORK(5),=X'0F0F0F0F00'      TURN OFF SIGNS
         MVC   WTOFLG21,WORK               MOVE TO WTO AREA
         TR    WTOFLG21,DECTAB             MAKE IT DECIMAL
         UNPK  WORK(5),S99FLG23(3)         2ND PART OF FLAGS 2
         NC    WORK(5),=X'0F0F0F0F00'      TURN OFF SIGNS
         MVC   WTOFLG22,WORK               MOVE TO WTO AREA
         TR    WTOFLG22,DECTAB             MAKE IT DECIMAL
         MVC   WTO(WTOLEN),WTOWORK
         LA    R2,WTO                      BAD RETURN FROM DYNALLOC
         LA    R3,53
         TPUT  (R2),(R3)
         LA    R15,8                    INDICATE INTERNAL PGM ERROR
         B     RETURN2
         TITLE 'MAPVTOC - SUBROUTINE TO CALCULATE EXTENT LENGTH'
***********************************************************************
*                                                                     *
* EXTCALC - CALCULATE SPACE IN EXTENT                                 *
*                                                                     *
*   ENTRY VIA BAL R10,EXTCALC                                         *
*                                                                     *
*   AT ENTRY R9 -> AN EXTENT FIELD                                    *
*                                                                     *
*   SPACE IN THIS EXTENT IS ADDED TO FIELDS TRKLTH                    *
*                                                                     *
*   REGISTERS 0, 1, AND 15 ARE USED AS WORK REGISTERS                 *
*                                                                     *
***********************************************************************
         SPACE 1
EXTCALC  DS    0H
         SLR   R0,R0                    CLEAR ACCUMULATORS
         SLR   R1,R1
         SLR   R15,R15
         ICM   R0,B'0011',2(R9)         START CYLINDER
         ICM   R1,B'0011',4(R9)         START TRACK
         MH    R0,TRKCYL                CONVERT TO TRACKS
         ALR   R0,R1                    RELATIVE TRACK
         ICM   R1,B'0011',6(R9)         ENDING CYLINDER
         ICM   R15,B'0011',8(R9)        END TRACK
         MH    R1,TRKCYL                CONVERT TO TRACKS
         ALR   R1,R15                   RELATIVE TRACK
         SLR   R1,R0                    TRACKS IN DATASET - 1
         LA    R1,1(,R1)                ADJUST
         A     R1,TRKLTH                ACCUMULATE
         ST    R1,TRKLTH
         BR    R10                      RETURN
         TITLE 'MAPVTOC - SUBROUTINE TO CALCULATE SPACE IN FORMAT 3'
***********************************************************************
*                                                                     *
* MAPVTOC - CALCULATE SPACE USED FROM FORMAT 3 BY CALLING EXTCALC     *
*                                                                     *
*   ENTRY VIA BAL R10,FORMAT3                                         *
*                                                                     *
*   AT ENTRY R4 -> FORMAT 3 DSCB                                      *
*                                                                     *
*   REGISTER 2 IS USED AS A WORK REGISTER                             *
*                                                                     *
***********************************************************************
         SPACE 1
FORMAT3  DS    0H
         USING DSCB3,R4
         ST    R10,FMT3SAVE             SAVE RETURN ADDRESS
         LA    R2,4                     EXTENTS IN FIRST PART
         LA    R9,DS3EXTNT              POINT TO IT
FMT3L100 DS    0H
         OC    4(4,R9),4(R9)            IS EXTENT VALID
         BZ    FMT3END                  NO, WERE DONE
         BAL   R10,EXTCALC              GO DO CALCULATIONS
         LA    R9,10(,R9)               POINT TO NEXT
         BCT   R2,FMT3L100              SO MANY TIMES
         SPACE 1
         LA    R2,9                     EXTENTS IN SECOND PART
         LA    R9,DS3ADEXT              POINT TO ADDITIONAL EXTENTS
FMT3L200 DS    0H
         OC    4(4,R9),4(R9)            IS EXTENT VALID
         BZ    FMT3END                  NO, WERE DONE
         BAL   R10,EXTCALC              GO DO CALCULATIONS
         LA    R9,10(,R9)               POINT TO NEXT
         BCT   R2,FMT3L200              SO MANY TIMES
         SPACE 1
FMT3END  DS    0H
         L     R10,FMT3SAVE
         BR    R10
         SPACE 1
FMT3SAVE DS    F'0'
         TITLE 'MAPVTOC - SUBROUTINE TO DISPLAY EXTENTS'
***********************************************************************
*                                                                     *
* EXTDIS - DISPLAY EXTENTS IN CCC.HH:CCC.HH FORMAT                    *
*                                                                     *
*   ENTRY VIA BAL R10,EXTCALC                                         *
*                                                                     *
*   AT ENTRY R9 -> AN EXTENT FIELD                                    *
*            R15-> WHERE TO PUT DISPLAY                               *
*                                                                     *
***********************************************************************
         SPACE 1
EXTDIS   DS    0H
* STARTING CC
         MVC   WORK(2),2(R9)
         MVI   WORK+2,X'0F'
         UNPK  WORK+3(5),WORK(3)
         MVC   0(3,R15),WORK+4
         TR    0(3,R15),DECTAB-240
* STARTING HH
         MVI   3(R15),C'.'
         MVC   WORK(2),4(R9)
         MVI   WORK+2,X'0F'
         UNPK  WORK+3(5),WORK(3)
         MVC   4(2,R15),WORK+5
         TR    4(2,R15),DECTAB-240
* ENDING CC
         MVI   6(R15),C':'
         MVC   WORK(2),6(R9)
         UNPK  WORK+3(5),WORK(3)
         MVC   7(3,R15),WORK+4
         TR    7(3,R15),DECTAB-240
* ENDING HH
         MVI   10(R15),C'.'
         MVC   WORK(2),8(R9)
         MVI   WORK+2,X'0F'
         UNPK  WORK+3(5),WORK(3)
         MVC   11(2,R15),WORK+5
         TR    11(2,R15),DECTAB-240
         BR    R10
         TITLE 'MAPVTOC - SUBROUTINE TO READ VTOC BY DSCB'
***********************************************************************
*                                                                     *
* READVTOC - READ A DSCB BY CCHHR                                     *
*                                                                     *
*   ENTRY VIA BAL R10,READVTOC                                        *
*                                                                     *
*   FIELD CCHHR CONTAINS ADDRESS OF DSCB TO READ                      *
*                                                                     *
***********************************************************************
         SPACE 1
READVTOC DS    0H
         NI    BFLHFL,255-BFLHVIR       READ BY DSCB, NOT VIR
         OI    BFLHFL,BFLHDSCB
         MVC   BFLEARG,CCHHR            CCHHR TO READ
         MVI   BFLEFL,BFLECHR           INDICATE READ BY CCHHR
         MVI   BFLELTH,140              FULL LENGTH DSCB
         MVI   ERRORCDE,X'05'
         CLRCVPL
         CVAFDIR ACCESS=READ,BUFLIST=BUFLIST,DEB=(8),DSN=F4DSN,        X
               MF=(E,CVPL)              DSN PARAMETER WILL BE IGNORED
         LTR   R15,R15                  IF ZERO RETURN CODE
         BZR   R10                        RETURN TO CALLER
         B     RETURN8                    ELSE GO HOME
         TITLE 'MAPVTOC - SUBROUTINE TO ACCUMULATE FREE SPACE'
***********************************************************************
*                                                                     *
* FREECALC - ACCUMULATE FREE SPACE                                    *
*                                                                     *
*   ENTRY VIA BAL R10,READVTOC                                        *
*                                                                     *
*   R9 -> FREE SPACE EXTENT                                           *
*         BYTES 1-2   RELATIVE TRACK ADDRESS OF THE FIRST TRACK       *
*         BYTES 3-4   NUMBER OF UNUSED CYLINDERS IN EXTENT            *
*         BYTE  5     NUMBER OF ADDITIONAL UNUSED TRACKS              *
*                                                                     *
*   REGISTERS 1 IS USED AS A WORK REGISTER                            *
*                                                                     *
***********************************************************************
         SPACE 1
FREECALC DS    0H
* FIRST ACCUMULATE TO TOTALS
         SLR   R1,R1
         ICM   R1,B'0011',2(R9)         NUMBER OF CYLINDERS
         BZ    FREE020
         A     R1,FREECYL
         ST    R1,FREECYL
         L     R1,FREECYLE              NUMBER OF EXTENTS
         LA    R1,1(,R1)
         ST    R1,FREECYLE
         SLR   R1,R1
FREE020  DS    0H
         ICM   R1,B'0001',4(R9)         NUMBER OF TRACKS
         BZ    FREE050
         A     R1,FREETRK
         ST    R1,FREETRK
FREE050  DS    0H
         L     R1,FREETRKE              TOTAL NUMBER OF EXTENTS
         LA    R1,1(,R1)
         ST    R1,FREETRKE
* KEEP TRACK OF FIVE LARGEST
         CLC   2(2,R9),FREE5            IF THIS IS LOWER THAN LOWEST,
         BL    FREE900                    BYPASS ALL
*
FREE100  CLC   2(2,R9),FREE1            LARGER THAN CURRENT LARGEST
         BL    FREE200                    NO, CONTINUE
         BH    FREE150                    HIGH, USE IT
         CLC   4(1,R9),FREE1+2            EQUAL, CHECK TRACKS
         BL    FREE200
FREE150  DS    0H
         MVC   FREE5(12),FREE4          MOVE 1->2, 2->3, 3->4, AND 4->5
         MVC   FREE1,2(R9)              SAVE AS LARGEST
         B     FREE900                  AND RETURN
FREE200  DS    0H
         CLC   2(2,R9),FREE2            DO SAME FOR SECOND LARGEST
         BL    FREE300
         BH    FREE250
         CLC   4(1,R9),FREE2+2
         BL    FREE300
FREE250  DS    0H
         MVC   FREE5(9),FREE4
         MVC   FREE2,2(R9)
         B     FREE900
FREE300  DS    0H
         CLC   2(2,R9),FREE3            AND THIRD
         BL    FREE400
         BH    FREE350
         CLC   4(1,R9),FREE3+2
         BL    FREE400
FREE350  DS    0H
         MVC   FREE5(6),FREE4
         MVC   FREE3,2(R9)
         B     FREE900
FREE400  DS    0H
         CLC   2(2,R9),FREE4            FOURTH
         BL    FREE500
         BH    FREE450
         CLC   4(1,R9),FREE4+2
         BL    FREE500
FREE450  DS    0H
         MVC   FREE5(3),FREE4
         MVC   FREE4,2(R9)
         B     FREE900
FREE500  DS    0H
         CLC   2(2,R9),FREE5            AND FIFTH
         BL    FREE900
         BH    FREE550
         CLC   4(1,R9),FREE5+2
         BL    FREE900
FREE550  DS    0H
         MVC   FREE5,2(R9)
*
FREE900  BR    R10                      RETURN TO CALLER
         TITLE 'MAPVTOC - RETURNS'
RETURN4  DS    0H
         LA    R15,4
         B     RETURN2
         SPACE 1
RETURN8  DS    0H
         LA    R15,4
         B     RETURN2
         SPACE 1
ENDIT    DS    0H
         CLOSE (VTOC,,SYSPRINT)
         SPACE 1
RETURN   DS    0H
         SLR   R15,R15                  INDICATE ZERO RETURN CODE
         SPACE 1
RETURN2  DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
         TITLE 'MAPVTOC - CONSTANTS AND WORK AREA'
         LTORG
         DS    0D
ZERO     DC    D'0'
DASDTBL  DS    0H
         DC    CL6' '             0
         DC    CL6'2311'          1
         DC    CL6'2301'          2
         DC    CL6'2303'          3
         DC    CL6'2302'          4
         DC    CL6'2321'          5
         DC    CL6'2305-1'        6
         DC    CL6'2305-2'        7
         DC    CL6'2314'          8
         DC    CL6'3330'          9
         DC    CL6'3340'          A
         DC    CL6'3350'          B
         DC    CL6'3375'          C
         DC    CL6'3330-1'        D
         DC    CL6'3380'          E
         DC    CL6' '             F
WTO      DC    C'VERB=X FLAG1=XXXX ERROR=XXXX INFO=XXXX FLAG2=XXXXXXXX'
DECTAB   DC    C'0123456789ABCDEF'
WTOWORK  EQU   *
         DC    C'VERB='
WTOVERB  DS    C
         DC    C' FLAG1='
WTOFLG1  DS    CL4
         DC    C' ERROR='
WTOERR   DS    CL4
         DC    C' INFO='
WTOINFO  DS    CL4
         DC    C' FLAG2='
WTOFLG21 DS    CL4
WTOFLG22 DS    CL4
WTOLEN   EQU   *-WTOWORK
INPUT    DC    A(INPUT01)
         DC    A(INPUT02)
         DC    A(INPUT03)
         DC    A(INPUT04)
         DC    A(INPUT05)
         DC    X'80'
         DC    AL3(INPUT1C)
DYNCODE  DC    F'0'
EXLST    DS    0F
         DC    X'87'
         DC    AL3(JFCBAREA)
S99BLKPT DS    F
S99BLK   DS    CL20
ALS99    DC    AL1(S99RBEND-S99RB),AL1(S99VRBAL),18AL1(0)
*******************************************************************
*                  ALLOCATE INPUT FILE                            *
*******************************************************************
         DS    0F
INPUT01  DC    AL2(DALDDNAM),AL2(1),AL2(7)
INPUTDD  DC    CL7' '
         DS    0F
INPUT02  DC    AL2(DALDSNAM),AL2(1),AL2(12)
INPUTDSN DC    C'FORMAT4.DSCB'
         DS    0F
INPUT03  DC    AL2(DALSTATS),AL2(1),AL2(1),X'08'
         DS    0F
INPUT04  DC    AL2(DALVLSER),AL2(1),AL2(6)
INPUTSER DS    CL6
         DS    0F
INPUT05  DC    AL2(DALUNIT),AL2(1),AL2(3)
INPUTUCB DS    CL3
         DS    0F
INPUT1C  DC    AL2(DALCLOSE),AL2(0)
*******************************************************************
*                  END TEXT UNITS                                 *
*******************************************************************
JFCBAREA DS    0CL176
         IEFJFCBN
HDR1     DC    AL2(HDR1LTH,0)
         DC    C' VOLUME: '
HDR1VOL  DS    CL6
         DC    CL3' '
         DC    C'UNIT: '
HDR1UNIT DS    CL6
         DC    CL3' '
         DC    C'ADDRESS: '
HDR1ADDR DS    CL3
         DC    CL3' '
         DC    C'MOUNT: '
HDR1MNT  DS    CL7
HDR1LTH  EQU   *-HDR1
         SPACE 1
HDR2     DC    XL5'0005000040'
         SPACE 1
HDR3     DC    AL2(HDR3LTH,0)
         DC    CL29' VOLUME DATA:'
         DC    C'FREE SPACE:  TRACKS  CYLINDERS'
HDR3LTH  EQU   *-HDR3
         SPACE 1
HDR4     DC    AL2(HDR4LTH,0)
         DC    C'   TRACKS:  '
HDR4TRKS DC    XL6'402020202120'        TRACKS ON VOLUME
         DC    CL13' '
         DC    C'TOTAL:    '
HDR4FTRK DC    XL6'402020202120'        FREE TRACKS ON VOLUME
         DC    CL5' '
HDR4FCYL DC    XL4'40202120'            FREE CYLINDERS ON VOLUME
HDR4LTH  EQU   *-HDR4
         SPACE 1
HDR5     DC    AL2(HDR5LTH,0)
         DC    C'   % USED:    '
HDR5USED DC    XL4'40202120'            PERCENT VOLUME IN USE
         DC    CL13' %'
         DC    C'EXTENTS:  '
HDR5FTRK DC    XL6'402020202120'        FREE TRACK EXTENTS
         DC    CL5' '
HDR5FCYL DC    XL4'40202120'            FREE CYLINDER EXTENTS
HDR5LTH  EQU   *-HDR5
         SPACE 1
HDR6     DC    AL2(HDR6LTH,0)
         DC    C'   ALTS:      '
HDR6ALT  DC    XL4'40202120'            AVAILABLE ALTERNATE TRACKS
         DC    CL13'  '
         DC    C'LARGEST:  '
HDR6FTRK DC    XL6'402020202120'        LARGEST TRACKS
         DC    CL5' '
HDR6FCYL DC    XL4'40202120'            LARGEST CYLINDERS
HDR6LTH  EQU   *-HDR6
         SPACE 1
HDR7     DC    AL2(HDR7LTH,0)
         DC    C'   VSAM:         '
HDR7VSAM DC    CL5'NO'                  VSAM INDICATOR - YES OR NO
         DC    CL9'  '
         DC    C'    2ND:  '
HDR7FTRK DC    XL6'402020202120'        SECOND LARGEST EXTENT TRACKS
         DC    CL5' '
HDR7FCYL DC    XL4'40202120'            SECOND LARGEST EXTENT CYLINDERS
HDR7LTH  EQU   *-HDR7
         SPACE 1
HDR8     DC    AL2(HDR8LTH,0)
         DC    C'   CVOL:         '
HDR8CVOL DC    CL3'YES'                 CVOL ON VOLUME - YES OR NO
         DC    CL11'  '
         DC    C'    3RD:  '
HDR8FTRK DC    XL6'402020202120'        THIRD LARGEST EXTENT TRACKS
         DC    CL5' '
HDR8FCYL DC    XL4'40202120'            THIRD LARGEST EXTENT CYLINDERS
HDR8LTH  EQU   *-HDR8
         SPACE 1
HDR9     DC    AL2(HDR9LTH,0)
HDR9IX   DC    C'IX VTOC:         '
HDR9IXVT DC    CL3'YES'                 INDEX VTOC ON VOLUME - Y OR N
         DC    CL15'  '
HDR9TYPE DC    C'4TH:  '
HDR9FTRK DC    XL6'402020202120'        4TH LARGEST EXTENT TRACKS
         DC    CL5' '
HDR9FCYL DC    XL4'40202120'            4TH LARGEST EXTENT CYLS
HDR9LTH  EQU   *-HDR9
         SPACE 1
HDRA     DC    AL2(HDRALTH,0)
HDRAVV   DC    C'   VVDS:         '
HDRAVVDS DC    CL3'NO'                  VVDS ON VOLUME - Y OR N
         DC    CL15'  '
HDRATYPE DC    C'5TH:  '
HDRAFTRK DC    XL6'402020202120'        5TH LARGEST EXTENT TRACKS
         DC    CL5' '
HDRAFCYL DC    XL4'40202120'            5TH LARGEST EXTENT CYLS
HDRALTH  EQU   *-HDRA
         SPACE 1
HADRB    DC    AL2(HADRBLTH,0)
         DC    CL29' VTOC DATA:'
HADRBNOIX EQU  *-HADRB
         DC    C'INDEXED VTOC DATA:'
HADRBLTH EQU   *-HADRB
         SPACE 1
HDRC     DC    AL2(HDRCLTH,0)
         DC    C'   EXTENTS:  '
HDRCVEXT DC    CL13' '                  VTOC EXTENT CCC.HH:CCC.HH
HDRCNOIX EQU   *-HDRC
         DC    CL5' '
         DC    C'EXTENTS:  '
HDRCIEXT DC    CL13' '                  IX VTOC EXTENT CCC.HH:CCC.HH
HDRCLTH  EQU   *-HDRC
         SPACE 1
HDRD     DC    AL2(HDRDLTH,0)
         DC    C'   LENGTH:  '
HDRDVLTH DC    XL6'402020202120'        VTOC LENGTH IN TRACKS
HDRDNOIX EQU   *-HDRD
         DC    CL13' TRKS'
         DC    C'LENGTH:   '
HDRDILTH DC    XL6'402020202120'        IX VTOC LENGTH IN TRACKS
         DC    C' TRKS'
HDRDLTH  EQU   *-HDRD
         SPACE 1
HDRE     DC    AL2(HDRELTH,0)
         DC    C'   %USED:     '
HDREUSED DC    XL4'40202120'            PERCENT VTOC USED
         DC    CL2' %'
HDRENOIX EQU   *-HDRE
         DC    CL11' '
         DC    C'LEVELS:     '
HDREILVL DC    XL4'40202120'            LEVELS IN INDEXED VTOC
HDRELTH  EQU   *-HDRE
         SPACE 1
HDRF     DC    AL2(HDRFLTH,0)
         DC    C'   FREE:    '
HDRFVFRE DC    XL6'402020202120'        FREE DSCB 0'S IN VTOC
HDRFNOIX EQU   *-HDRF
         DC    CL13' '
         DC    C'FREE:     '
HDRFIFRE DC    XL6'402020202120'        FREE VIR'S IN IX VTOC
HDRFLTH  EQU   *-HDRF
HADRBLTH2 DC   AL2(HADRBNOIX)
HDRCLTH2 DC    AL2(HDRCNOIX)
HDRDLTH2 DC    AL2(HDRDNOIX)
HDRELTH2 DC    AL2(HDRENOIX)
HDRFLTH2 DC    AL2(HDRFNOIX)
         EJECT
         DC    C'Z'                       THIS DC MUST PRECEDE
VOLID    DS    CL6                        VOLID
F4DSN    DC    44X'04'                  DSN OF FORMAT 4
SYSCTLG  DC    CL44'SYSCTLG'            DSNAME OF OS/CVOL
SYS1VTOC DC    CL44'SYS1.VTOCIX'
VTOCIX   DC    CL44' '                  DSNAME OF IX VTOC   *LBD 04/84*
         ORG   VTOCIX                                       *LBD 04/84*
         DC    C'SYS1.VTOCIX.'                              *LBD 04/84*
VTOCIXV  DC    CL6' '                                       *LBD 04/84*
         ORG   ,                                            *LBD 04/84*
VVDS     DC    CL44' '                  DSNAME OF VVDS      *LBD 04/84*
         ORG   VVDS                                         *LBD 04/84*
         DC    C'SYS1.VVDS.V'                               *LBD 04/84*
VVDSV    DC    CL6' '                                       *LBD 04/84*
         ORG   ,                                            *LBD 04/84*
VOLBIT   EQU   B'10000000'                VOLUME SPECIFIED
DSNBIT   EQU   B'01000000'                DSNAME SPECIFIED
INDEX    EQU   B'00100000'                INDEX VTOC
VTOC     DCB   DSORG=PS,MACRF=R,RECFM=F,BLKSIZE=96,KEYLEN=44,          C
               EODAD=ENDVTOC,EXLST=EXLST
SYSPRINT DCB   DDNAME=SYSPRINT,RECFM=VB,LRECL=255,BLKSIZE=6233,        C
               MACRF=(PM),DSORG=PS
BUFLIST  ICVAFBFL DSECT=NO
CVPL     CVAFDIR ACCESS=READ,BUFLIST=BUFLIST,MF=L
         ORG   CVPL
CVPLMAP  ICVAFPL DSECT=NO
CLEAR2   EQU   *-CVIRCDS                LENGTH TO CLEAR END OF CVPL
CLEAR1   EQU   CVBUFL-CVFCTN            LENGTH TO CLEAR FIRST OF CVPL
         TITLE 'MAPVTOC - GETMAINED WORK AREA'
WORKAREA DSECT
SAVEAREA DS    9D
WORK     DS    D
DSCB0CNT DC    F'0'                     AVAILABLE DSCB 0'S
FREEVIR  DC    F'0'                     FREE VIR'S IN INDEX
FREECYL  DC    F'0'
FREETRK  DC    F'0'
FREECYLE DC    F'0'
FREETRKE DC    F'0'
TRKONDEV DC    F'0'                     TRKS ON DEVICE (NOT INCL ALT)
DSCBVTOC DC    F'0'                     DSCB'S IN THE VTOC
TRKLTH   DC    F'0'                     ACCUMULATOR FOR EXTCALC
UCB      DC    F'0'
TRKCYL   DC    H'0'                     TRACKS PER CYLINDER ON DEVICE
F4CCHHR  DS    CL5                      CCHHR OF FORMAT 4
CCHHR    DS    CL5
PARMBIT  DC    X'0'
ERRORCDE DC    X'0'                     SAVE LAST CALL INDICATOR
FREETBL  EQU   *
FREE5    DS    CL3                      FIFTH LARGEST FREE AREA
FREE4    DS    CL3                      FOURTH LARGEST FREE AREA
FREE3    DS    CL3                      THIRD LARGEST FREE AREA
FREE2    DS    CL3                      SECOND LARGEST FREE AREA
FREE1    DS    CL3                      FIRST LARGEST FREE AREA
         DS    0D
FORMAT   DS    XL2048
         IEFZB4D0
         IEFZB4D2
DSCB1    DSECT
         IECSDSL1 1
DSCB1LTH EQU   *-DSCB1
DSCB3    DSECT
         IECSDSL1 3
DSCB3LTH EQU   *-DSCB3
DSCB4    DSECT
         IECSDSL1 4
DSCB4LTH EQU   *-DSCB4
DSCB5    DSECT
         IECSDSL1 5
DSCB5LTH EQU   *-DSCB5
         END
