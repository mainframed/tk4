         TITLE 'AUDIT UTILITY PROGRAM 03-02-84'             *LBD 03/84*
*---------------------------------------------------------------------*
*  THIS PROGRAM IS DESIGNED TO GENERATE A HASH IN AN ACCUMULATOR FOR  *
*  DSORG=PS OR DSORG=PO DATA-SETS.  THE HASH IS STORED IN A HASHLIB   *
*  PDS FOR AUDIT PURPOSES OR IS PUNCHED OUT FOR DATA-TRANSFER AND     *
*  THEN READ FROM THE CARD (IMAGE) FOR COMPARISON OF THE HASH.        *
*                                                                     *
*  PARM OPTIONS ARE:                                                  *
*        REPORT - GENERATE A HASH REPORT ONLY                         *
*        AUDIT - GENERATE A HASH REPORT AND COMPARE WITH HASH FILE    *
*        PUNCH - GENERATE A HASH REPORT AND PUNCH CARD (IMAGE) HASH   *
*                RECORD                                               *
*        AUDITC - ACCUMULATE HASH AND COMPARE WITH HASH CARD          *
*                                                                     *
*        DEFAULT IS TO GENERATE A HASH AND STORE IN HASHLIB FOR       *
*                DSORG=PO FILES.                                      *
*                                                                     *
*        OPTIONS AUDITC AND PUNCH ARE VALID ONLY FOR PS FILES OR      *
*                DSNAME(MEMBER).                                      *
*                                                                     *
*        DD'S:                                                        *
*        REPORT  - REPORT FILE                                        *
*        HASHIN  - FILE TO BE HASHED                                  *
*        PDS     - SAME AS HASHIN IF HASHIN IS A PDS                  *
*        HASHLIB - PDS WHERE HASH RECORD WILL BE STORED OR AUDITED    *
*                  (NOT USED FOR REPORT,PUNCH,AUDITC OPTIONS)         *
*        SYSIN   - OPTION AUDITC INPUT HASH CARD RECORD               *
*        SYSPUNCH - OPTION PUNCH CARD RECORD OF HASH                  *
*        COPYCC  - IEBCOPY SELECT MEMBER= FOR AUDIT FAILED  *LBD 02/84*
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
* CAVEATS:  SPF PDS'S HAVE STATISTICS AND A RECORD MAY NOT CHANGE     *
*           CONTENTS BUT THE LEVEL IN COL 79-80 COULD CHANGE.         *
*                                                                     *
*           LOAD LIBRARIES: SUPERZAP SETS IDR RECORDS WITH DATE       *
*           OF ZAP, LINK CHANGES DATE WHICH CAN CHANGE THE HASH.      *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         MACRO
&L       @CONVERT &TO,&FROM,&LEN
&L       LA    R1,&TO
         LA    R2,&FROM
         LA    R3,&LEN
         CONVERT (R1,R2,R3)
         MEND
AUDITPGM $PROLOG
         L    1,0(1)            * LOAD ADDR OF PARM
         LH   2,0(1)            * LOAD LENGTH OF PARM
         LTR   2,2              * LENGTH OF ZERO
         BZ   OPEN              * NO - NO PARM
         CLC   =C'PUNCH',2(R1)
         BNE   CKREP
         SETON PUNCH
         B     OPEN
CKREP    CLC   =C'REPORT',2(R1)
         BNE   CKAUDITC
         SETON REPORTB
         B    OPEN
CKAUDITC CLC   =C'AUDITC',2(R1)
         BNE   CKAUDP
         SETON AUDITC
         B     OPEN
CKAUDP   CLC   =C'AUDIT',2(R1)
         BNE   OPEN
         SETON AUDIT
OPEN     OPEN  (REPORT,OUTPUT,COPYCC,OUTPUT)
         OPEN  HASHIN
         CLOSE HASHIN
         RDJFCB HASHIN
         LA    R9,HASHJFCB
         USING INFMJFCB,R9
         TM    JFCDSRG1,JFCORGPS      IT PS
         BO    ORGPS
         TM    JFCBIND1,JFCPDS  MEMBER OF PDS
         BO    ORGPDS                  YES - BUT FLAG IT
         TM    JFCDSRG1,JFCORGPO      IT PO
         BO    ORGPO
         ABEND 1                NOT A VALID DSORG
ORGPS    EQU   *
         OPEN  (HASHIN)
         B     STARTHSH
ORGPDS   EQU   *
         MVC   MEMBER,JFCBELNM
         OPEN  (HASHIN)
         SETON PDSB             FLAG AS PDS
         B     STARTHSH
ORGPO    SETON PO
         CLOSE HASHIN
         IFON  AUDITC,ABEND2
         IFON  PUNCH,ABEND3
         B     READDIR
ABEND2   ABEND 2                AUDITC PARM NOT VALID FOR PDS
ABEND3   ABEND 3                PUNCH  PARM NOT VALID FOR PDS
OPENHSH  EQU   *
         IFON  DONE,CLOSE       ALL DONE - GET OUT OF HERE
         OPEN  HASHIN,TYPE=J
STARTHSH EQU   *
         LA    R9,1             INIT BYTE COUNT
         SR    R8,R8            INIT HASH COUNTER
READ     GET   HASHIN
         LH    R2,HASHIN+X'52'  LOAD LRECL OF INPUT RECORD
         LR    R4,R1
         LR    R5,R1
         AR    R5,R2            -> END OF RECORD
LOOP     EQU   *
         SR    R6,R6
         SR    R7,R7
         IC    R7,0(R4)
         LTR   R7,R7
         BNZ   MR
         LR    R7,R9            LOAD WITH REL BYTE NUMBER
MR       MR    R6,R9
         AR    R8,R7
INCR     EQU   *
         LA    R4,1(R4)
         CR    R4,R5            END OF RECORD
         BE    READ
         LA    R9,1(R9)         INCR BYTE COUNT BY 1
         B     LOOP
EOF      EQU   *
         CLOSE HASHIN
         ST    R9,BYTES
         ST    R8,HASH
         @CONVERT BYTEOUT,BYTES,4
         @CONVERT HASHOUT,HASH,4
         LA    R9,HASHJFCB
         MVC   HASHDSN,JFCBDSNM
         MVC   HASHMEM,JFCBELNM
         $STCK NOGEN
         IFON  AUDITC,DOAUDC
         IFON  AUDIT,LETSAUD
         PUT   REPORT,HASHLINE
         IFON  PUNCH,DOPUN
         IFON  PDSB,CKHSH       IF PDS MEMBER THEN HASH IT
         IFOFF PO,CLOSE
CKHSH    IFOFF REPORTB,DOHASH
         IFON  PDSB,CKAUD
DOHASH   RDJFCB  HASHLIB
         LA    R9,HLJFCB
         MVC   JFCBELNM,MEMBER
         OI    JFCBTSDM,X'08'      TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS
         OPEN  (HASHLIB,OUTPUT),TYPE=J
         PUT   HASHLIB,HASHLINE
         CLOSE HASHLIB
         IFON  PDSB,CLOSE
         B     READDIR
CKAUD    IFOFF AUDIT,CLOSE
LETSAUD  EQU   *
DOAUD    EQU   *
         OPEN  HASHLIBD
         MVC   BMEM,MEMBER
         BLDL  HASHLIBD,BLIST
         LR    R9,R15
         CLOSE HASHLIBD
         LTR   R9,R9
         BNZ   NEWMEM
         RDJFCB  HASHLIB
         LA    R9,HLJFCB
         MVC   JFCBELNM,MEMBER
         OI    JFCBTSDM,X'08'      TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS
         OPEN  HASHLIB,TYPE=J
         GET   HASHLIB
         USING AUDLINE,R1
         MVC   ERRLINE,AUDLINE
         CLC   HASHOUT,AUDOUT
         BE    AUDOK
         SETON FAIL             TURN ON AUDIT FAILURE FLAG
         TESTOPEN COPYCC,NCC                                *LBD 02/84*
         MVC   SELMEM,HASHMEM                               *LBD 02/84*
         IFON  PCOPY,PTCOPY                                 *LBD 02/84*
         PUT   COPYCC,COPYCARD                              *LBD 02/84*
         SETON PCOPY                                        *LBD 02/84*
PTCOPY   PUT   COPYCC,SELCARD                               *LBD 02/84*
         SPACE 1                                            *LBD 02/84*
NCC      DS    0H                                           *LBD 02/84*
         MVC   ERRLINE+1(44),AUDITERR
         PUT   REPORT,HASHLINE
         PUT   REPORT,ERRLINE
         CLOSE HASHLIB
         IFON  PDSB,CLOSE
         B     READDIR
NEWMEM   MVC   HASHLINE+1(44),NEWMEML
         MVC   ERRLINE,HASHLINE
         TESTOPEN COPYCC,NCC2                               *LBD 02/84*
         MVC   SELMEM,HASHMEM                               *LBD 02/84*
         IFON  PCOPY,PTCOPY2                                *LBD 02/84*
         PUT   COPYCC,COPYCARD                              *LBD 02/84*
         SETON PCOPY                                        *LBD 02/84*
PTCOPY2  PUT   COPYCC,SELCARD                               *LBD 02/84*
         SPACE 1                                            *LBD 02/84*
NCC2     DS    0H                                           *LBD 02/84*
         SETON FAIL             TURN ON FAILURE FLAG
AUDOK    CLOSE HASHLIB
         PUT   REPORT,ERRLINE
         IFON  PDSB,CLOSE
         B     READDIR
DOPUN    EQU   *
         OPEN  (SYSPUNCH,OUTPUT)
         MVC   PHASH,HASHOUT
         MVC   PBYTES,BYTEOUT
         PUT   SYSPUNCH,PLINE
         CLOSE SYSPUNCH
         B     CLOSE
DOAUDC   EQU   *
         OPEN  SYSIN
         GET   SYSIN
         MVC   PLINE,0(R1)
         MVC   PMSG,AUDITOK
         CLC   PHASH,HASHOUT
         BE    AUDCOK
         SETON FAIL
         MVC   PMSG,AUDITERR
         SPACE 1                                            *LBD 02/84*
         TESTOPEN COPYCC,AUDCOK                             *LBD 02/84*
         MVC   SELMEM,HASHMEM                               *LBD 02/84*
         IFON  PCOPY,PUTCOPY                                *LBD 02/84*
         PUT   COPYCC,COPYCARD                              *LBD 02/84*
         SETON PCOPY                                        *LBD 02/84*
PUTCOPY  PUT   COPYCC,SELCARD                               *LBD 02/84*
         SPACE 1                                            *LBD 02/84*
AUDCOK   EQU   *
         PUT   REPORT,PLINE
         CLOSE SYSIN
         B     CLOSE
CLOSE    CLOSE (REPORT,,COPYCC)
         IFON  FAIL,BADRC
         $EPILOG
BADRC    $EPILOG 4
READDIR  EQU   *
         FTNOP LPS1
         OPEN  PDS
RPDS     GET   PDS
         LR    R10,R1           SAVE INPUT ->
         USING DIR,R10          SET UP ADDRESSABILITY
         LH    R11,0(R10)
         AR    R11,R10          -> END OF DIR BLOCK
PLPS     LA    R10,2(R10)       -> PAST LEN
LPS      EQU   *
         CLC   NAME,=8X'FF'
         BE    EXIT
         MVC   MEMBER,NAME
         NI    FLAG,X'1F'       TURN OFF MISC BITS
         SR    R1,R1            CLEAR REG 3
         IC    R1,FLAG
         LA    R1,6(R1)         ADD FIXED # OF HALF WORDS
         SLA   R1,1             MULT BY 2 FOR # OF BYTES OF DATA
         AR    R10,R1
         CR    R10,R11
         BL    RETURN
         SETON GORPDS
         B     RETURN
EXIT     CLOSE PDS
         SETON DONE
RETURN   EQU   *
*        RDJFCB HASHIN
         LA    R9,HASHJFCB
         MVC   JFCBELNM,MEMBER
         OI    JFCBTSDM,X'08'      TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS
         B     OPENHSH
LPS1     EQU   *
         IFOFF GORPDS,LPS
         SETOF GORPDS
         B     RPDS
         LTORG
         BITSW PUNCH,FAIL,PO,DONE,GORPDS,AUDIT,REPORTB,PDSB
         BITSW AUDITC,PCOPY
BYTES    DC    F'0'
HASH     DC    F'0'
ERRLINE  DC    CL116' '
AUDITERR DC    CL44'**** AUDIT FAILED ****'
AUDITOK  DC    CL44' AUDIT IS SUCCESSFUL'
NEWMEML  DC    CL44'***** NEW MEMBER - AUDIT FAILED *****'
PLINE    DC    CL80' '
         ORG   PLINE
         DC    C'HASH='
PHASH    DC    CL8' '
         DC    CL2' '
         DC    C'BYTES='
PBYTES   DC    CL8' '
         DC    CL2' '
PMSG     DC    CL44' '
         ORG
HASHLINE DC    CL116' '
         ORG   HASHLINE+1
HASHDSN  DC    CL44' '
         DC    CL2' '
HASHMEM  DC    CL8' '
         DC    CL2' '
         DC    C' HASH = '
HASHOUT  DC    CL8' '
         DC    CL2' '
         DC    C'TOTAL BYTES ='
BYTEOUT  DC    CL8' '
         DC    CL2' '
DATE     DC    CL8' '
         DC    CL2' '
TIME     DC    CL8' '
         ORG
DAY      DC    C' '
MEMBER   DC    CL8' '
         PRINT NOGEN
HASHIN   DCB   DSORG=PS,MACRF=GL,DDNAME=HASHIN,EODAD=EOF,EXLST=JFCB
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=REPORT,RECFM=FBA,LRECL=116,    *
               BLKSIZE=116
COPYCC   DCB   DSORG=PS,MACRF=PM,DDNAME=COPYCC,LRECL=80,    *LBD 02/84*X
               RECFM=FB,BLKSIZE=6160                        *LBD 02/84*
PDS      DCB   DSORG=PS,MACRF=GL,DDNAME=PDS,LRECL=256,RECFM=F, *       *
               BLKSIZE=256
HASHLIB  DCB   DSORG=PS,MACRF=(GL,PM),DDNAME=HASHLIB,LRECL=116,        *
               BLKSIZE=6148,RECFM=FB,EXLST=HJFCB
HASHLIBD DCB   DSORG=PO,MACRF=R,DDNAME=HASHLIB
SYSPUNCH DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPUNCH,LRECL=80,RECFM=F,     *
               BLKSIZE=80
SYSIN DCB      DSORG=PS,MACRF=GL,DDNAME=SYSIN,LRECL=80,RECFM=F,        *
               BLKSIZE=80
         PRINT GEN
         EJECT
COPYCARD DC    CL80' COPY INDD=((IN,R)),OUTDD=OUT'          *LBD 02/84*
SELCARD  DC    CL80' '                                      *LBD 02/84*
         ORG   SELCARD                                      *LBD 02/84*
         DC    C' SELECT MEMBER='                           *LBD 02/84*
SELMEM   DC    CL8' '                                       *LBD 02/84*
         ORG   ,                                            *LBD 02/84*
         SPACE 2                                            *LBD 02/84*
JFCB     DC    X'87',AL3(HASHJFCB)
HASHJFCB DC    CL176' '
         SPACE
HJFCB    DC    X'87',AL3(HLJFCB)
HLJFCB   DC    CL176' '
         SPACE
         DS    0F
BLIST    DC    AL2(1),AL2(76)
BMEM     DC    CL8' '
         DC    CL76' '
         EJECT
         DSECT
         IEFJFCBN LIST=YES
         EJECT
         DSECT
AUDLINE  DS    CL116
         ORG   AUDLINE+1
AUDDSN   DS    CL44
         DS    CL2
AUDMEM   DS    CL8
         DS    CL2
         DS    CL8
AUDOUT   DS    CL8
         DS    CL2
         DS    CL13
AUDBYTE  DS    CL8
         DS    CL2
AUDDATE  DS    CL8
         DS    CL2
AUDTIME  DS    CL8
         ORG
         DSECT
DIR      DS    0C
NAME     DS    CL8
TTR      DS    CL3
FLAG     DS    X
ALIAS    EQU   128
         END
