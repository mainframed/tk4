**********************************************************************
*                                                                    *
*    SACOPY                                                          *
*              THIS PROGRAM IS DESIGNED TO COPY A                    *
*              STAND ALONE DUMP FROM FILE IEFRDER                    *
*              TO A DYNAMICALLY CREATED FILE ON THE                  *
*              MSVGP IPCSDUMP.                                       *
*                                                                    *
*              A SYSPRINT FILE IS CREATED WHICH CONTAINS             *
*              THE DATASET NAME OF THE OUTPUT FILE AND               *
*              A COUNT OF THE # OF RECORDS IN THE O/P FILE.          *
*                                                                    *
*              THE DUMPLOG FILE IS A MOD DATA SET THAT CONTAINS      *
*              THE DSN, CPUID, DATE, TIME, AND TITLE OF THE DUMP     *
*              THAT IS MIGRATED.                                     *
*                                                                    *
*              THE JCL TO EXECUTE SACOPY ARE:                        *
*                                                                    *
*                   //A EXEC PGM=SACOPY                              *
*                   //SYSPRINT DD SYSOUT=A                           *
*                   //IEFRDER  DD DSN=SYS1.DUMP01,DISP=SHR,          *
*                   //         UNIT=3400-5,VOL=SER=SADUMP            *
*                   //DUMPLOG  DD DSN=#A1000.DUMPLOG,DISP=MOD        *
*                                                                    *
*                                                                    *
*        MACROS : $PROLOG,TIME,,CVT,$EPILOG,                         *
*                 DYNALLOC,ABEND,OPEN,CLOSE,GET,PUT,DATEROU,         *
*                 CALL,DCB,REGEQU,IEFZB4D0,IEFZB4D2,                 *
*                 AMDDATA,WTOR,WAIT                                  *
*                                                                    *
**********************************************************************
SACOPY $PROLOG R11,R12
         EJECT
         OPEN  DUMPIN
         SR    R8,R8
         LA    R9,10
READHDR  GET   DUMPIN           READ AN INPUT RECORD
         AH    R8,=H'01'
         TM    RSW,X'01'                 GOT HEADER ?
         BO    READHDR                   YES
         LR    R10,R1
         USING PRDINPUT,R10
         CLI   PRDRECID,PRDHDREC HEADER RECORD ?
         BE    HEADER           YES - GO PROCESS IT
         BCT   R9,READHDR       NO LOOP THRU
         WTO   'SACOPY - NO HEADER RECORD FOUND - PROBABLE INVALID DUMPX
               TAPE',ROUTCDE=1
         ABEND 2,DUMP           NO HEADER RECORD FOUND
HEADER   EQU   *                DUMP HEADER ROUTINE
         OI    RSW,X'01'                 SET GOT HEADER
         USING PRDINPUT,R10
         MVC   TOD,PRDTODVL SAVE DATE
         LINK  EP=BLSUMTOD,PARAM=(TOD,TDATE),VL=1
         MVC   DSNT(2),DTIME              HH
         MVC   DSNT+2(2),DTIME+3          MM
         MVC   DSND(2),TDATE              MM
         MVC   DSND+2(2),TDATE+3          DD
         MVC   DSND+4(2),TDATE+6          YY
         MVC   TITLE,PRDTITLE   MOVE TITLE TO OUTPUT LINE
         B     READHDR          BACK TO READ NEXT RECORD
         DROP  R10
OK       EQU   *
         OI    RSW,X'10'                 SET 1ST PASS DONE
         ST    R8,WORK
         MVC   PRIML(3),WORK+1
ALLOC    L     R3,CVTPTR          -> CVT
         USING CVTMAP,R3
         MVC   DSNAME(DSNL),DSNM MOVE DSN INTO PARAMETER
         LA    R1,RB            ADDRESS OF REQUEST BLOCK
         USING S99RB,R1        AND ESTAB. ADDRESSABILITY
         LA    R2,S99RBEND-S99RB LOAD LENGTH OF RB
         STC   R2,S99RBLN        AND STORE IT FOR USE
         OI    S99VERB,S99VRBAL  SPECIFY ALLOCATION
         OI    S99FLG11,S99NOCNV DO NOT USE AN EXISTING ALLOCATION
         LA    R2,TXTPTRS        ADDRESS OF TEXT POINTERS
         ST    R2,S99TXTPP       STORE INTO RB
         DROP  R1
         LA    R1,RBPTR          -> TO RB
         DYNALLOC
         LTR  R15,R15           TEST RETURN
         BZ   CLOSE1            ALLOCATION WAS OK
         S99FAIL
         WTO   'SACOPY - DYNAMIC ALLOCATION ERROR',ROUTCDE=1
         ABEND 1,DUMP           DYNAMIC ALLOCATION ERROR
         SPACE
CLOSE1   CLOSE (DUMPIN,REREAD)
         OPEN  (DUMPIN,,SYSPRINT,OUTPUT,DUMPOUT,OUTPUT,DUMPLOG,OUTPUT)
         SPACE
READ     GET   DUMPIN           READ AN INPUT RECORD
         LR    R10,R1
PUTIT    PUT   DUMPOUT,(10)     DON'T STOP - PUT IT OUT
         AP    CTR,=P'01'       ADD TO RECORD COUNTER
         B     READ             CONTINUE THE READ UNTIL
         EJECT
         EJECT
EOJ      EQU   *
         TM    RSW,X'10'                 PASS 1
         BZ    OK                        YES
         UNPK  COUNT(6),CTR     UNPACK RECORD COUNTER
         OI    COUNT+5,X'F0'    SET FULL DISPLAY
         PUT   SYSPRINT,MSGA    MESSAGE OF DSNAME OF INPUT
         PUT   SYSPRINT,MSGB    MESSAGE OF DSNAME OF OUTPUT
         PUT   SYSPRINT,TITLINE AND PRINT WHAT WE HAVE
         MVC   DUMPDSN(DSNL),DSNM
         MVC   DUMPTLE,TITLE
         PUT   DUMPLOG,DUMPDATA
         XC    ECB,ECB
WTOR     WTOR  ' SACOPY - ENTER SA DUMP REASON OR END:',REPLY,100,ECB, X
               ROUTCDE=1
         WAIT  ECB=ECB
         XC    ECB,ECB
         CLC   REPLY(4),=C'END '
         BE    WTOREND
         PUT   DUMPLOG,REPLYL
         PUT   SYSPRINT,REPLYL
         MVI   REPLY,C' '
         MVC   REPLY+1(99),REPLY
         B     WTOR
WTOREND  EQU   *
         MVC   FL4TITLE,TITLE      MOVE TITLE
         MVC   FL5DSN(DSNL),DSNM
         PUT   SYSPRINT,FL1
         PUT   SYSPRINT,FL2
         PUT   SYSPRINT,FL3
         PUT   SYSPRINT,FL4
         PUT   SYSPRINT,FL5
*        PUT   SYSPRINT,FL6
*        PUT   SYSPRINT,FL7
*        PUT   SYSPRINT,FL8
*        PUT   SYSPRINT,FL9
         PUT   SYSPRINT,FL10
         PUT   SYSPRINT,FL11
         PUT   SYSPRINT,FL12
         PUT   SYSPRINT,FL13
         PUT   SYSPRINT,FL14
         PUT   SYSPRINT,FL15
         PUT   SYSPRINT,FL16
         PUT   SYSPRINT,FL17
CLOSE    CLOSE (SYSPRINT,,DUMPIN,FREE,DUMPOUT,FREE,DUMPLOG)
         $EPILOG 0
         EJECT
         LTORG
ECB      DC    F'0'
         SPACE
RBPTR    DC    X'80',AL3(RB)
RB       DC    20X'00'          DYNAMIC ALLOCATION REQUEST BLOCK
TXTPTRS  DC    A(DSN,STAT,DISP,CDISP,SPACEC,PRIM,SEC)
         DC    A(ROUND,MSVGP,DEV)
         DC    X'80',AL3(DDN)   END OF PARAMETERS
WORK     DC    F'0'             WORK AREA FOR DATE/TIME
CTR      DC    PL4'0'           COUNT FOR RECORDS WRITTEN
TOD      DC    D'00'            WORK AREA FOR DUMP TIME/DATE
EDMASK   DC    X'21204B20204B2020'  TIME EDIT MASK
         DS    0F
TRTBL    DC    C'0123456789ABCDEF'
BLANK    DC    CL20' '
REPLYL   DC    C'           OPERATOR COMMENTS:  '
REPLY    DC    CL120' '
         DC    CL20' '                   FILLER
         SPACE
         PRINT NOGEN
DUMPIN   DCB   DSORG=PS,MACRF=GL,DDNAME=IEFRDER,EODAD=EOJ,             X
               LRECL=4104,BLKSIZE=4104,RECFM=F
         SPACE
DUMPOUT  DCB   DSORG=PS,MACRF=PM,DDNAME=DUMPOUT,RECFM=F,BLKSIZE=4104,  X
               LRECL=4104,OPTCD=W
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
DUMPLOG DCB    DSORG=PS,MACRF=PM,DDNAME=DUMPLOG
         PRINT GEN
         EJECT
RSW      DC    X'00'
         SPACE 2
MSGA     DC    CL5'1DSN='
DSNM     DC    C'AOS.SADUMP'
         DC    C'.D'
DSND     DC    CL6' ',C'.T' DSN DATE
DSNT     DC    CL4' '       DSN TIME
DSNL     EQU   *-DSNM       LENGTH OF MESSAGE
         DC    C'   RECORDS WRITTEN = '
COUNT    DC    CL6' ',CL80' '
         SPACE
MSGB     DC    C'0INPUT DSN='   INPUT DSNAME
IDSN     DC    CL24'STAND ALONE DUMP'
         DC    C'    VOL='
SAVOL    DC    CL6' '
         DC    CL80' '    FILLER
DUMPDATA DS    0CL155
COMM     DC    CL5' '
         DC    CL6' '
DUMPDSN  DC    CL31' '
         DC    CL15' '
DUMPTLE  DC    CL100' '
         EJECT
TITLINE  DC    CL6' '
TDATE    DC    CL9' '
DTIME    DC    CL8' '
         DC    CL4' '
TITLE    DC    CL100' '
         EJECT
DSN      DC    AL2(2)     DSNAME KEY
         DC    AL2(1),AL2(DSNL) COUNT AND LENGTH
DSNAME   DC    CL44' '    DSNAME FOR ALLOCATION
         SPACE
STAT     DC    AL2(4),AL2(1),AL2(1)  STATUS KEY,COUNT,LENGTH
         DC    X'04'      NEW
         SPACE
DISP     DC    AL2(5),AL2(1),AL2(1)  DISP KEY,COUNT,LENGTH
         DC    X'02'      CATLG
         SPACE
CDISP    DC    AL2(6),AL2(1),AL2(1)  C-DISP KEY,COUNT,LENGTH
         DC    X'04'      DELETE
         SPACE
SPACEC   DC    AL2(9),AL2(1),AL2(3),AL3(4104)  BLOCK ALLOCATION
*                                             SIZE OF 4104
         SPACE
PRIM     DC    X'000A',AL2(1),AL2(3)  PRIM ALLOC QTY KEY,COUNT,LENGTH
PRIML    DC    AL3(0)     PRIMARY SPACE
         SPACE
SEC      DC    X'000B',AL2(1),AL2(3)  SECONDARY ALLOCATION KEY,COUNT,L
SECL     DC    AL3(05)     SEC ALLOC LENGTH
         SPACE
ROUND    DC    X'000F',AL2(0)          ROUND TO CYLINDER ALLOCATION
         SPACE
MSVGP    DC    X'005E'                 MSVGP
         DC    AL2(1)                  COUNT
         DC    AL2(8)                  LENGTH OF MSVGP ID
         DC    C'IPCSDUMP'
         SPACE
DEV      DC    X'0015',AL2(1),AL2(5),C'3330V'  DEVICE TYPE
         SPACE
DDN      DC    X'0001'                 DDNAME KEY
         DC    AL2(1)                  COUNT OF DDN'S
         DC    AL2(7)                  LENGTH OF DDNAME
         DC    CL7'DUMPOUT'            DDNAME
         EJECT
******************************************************************
*        THIS AREA CONTAINS THE DUMP FORM USED TO ROUTE          *
*        INFORMATION ON SYSTEM DUMPS TO THE RESPONSIBLE          *
*        GROUP                                                   *
******************************************************************
FL1      DC    CL121'1            SYSTEM DUMP NOTIFICATION FORM'
FL2      DC    C'0',CL5' ',CL115'FROM : CONTROL PROGRAM SERVICES '
FL3      DC    CL6' ',C'TO   : '
FL3COMM  DC    CL120' '            TO GROUP
FL4      DC    CL6'0'
         DC    C'DUMP TITLE :'
         DC    CL7' '
FL4TITLE DC    CL100' '            DUMP TITLE + FILLER
FL5      DC    CL6'0'
         DC    C'DATA-SET NAME :'
         DC    CL4' '
FL5DSN   DC    CL110' '
*L6      DC    CL6' '
*        DC    C'FROM DUMP D/S'    S :'
*        DC    CL4' ',9C'_',C'×',CL4' ',C'×',CL5' ',C'×',CL7' ',C'×'
*        DC    CL100' '            FILLER
*L7      DC    CL6' '
*        DC    C'TAKEN ON CPU :'
*        DC    CL5' ',14C'_',C'×',CL5' ',C'×',CL7' ',C'×'
*        DC    CL80' '             FILLER
*L8      DC    CL6' '
*        DC    C'DATE (MMDDYY) :'
*        DC    CL4' ',20C'_',C'×',CL7' ',C'×'
*        DC    CL80' '             FILLER
*L9      DC    CL6' '
*        DC    C'TIME (HHMMSS) :'
*        DC    CL4' ',28C'_',C'×'
*        DC    CL80' '             FILLER
FL10     DC    CL6'0'
         DC    CL115'PLEASE SUPPLY ME WITH THE SPR # IF THIS DUMP IS '
FL11     DC    CL6' '
         DC    CL115'SPR''D OR INFORM ME WHEN THE DUMP IS NO LONGER'
FL12     DC    CL6' '
         DC    CL115'NEEDED AND IT WILL BE DELETED.....'
FL13     DC    CL6'0'
         DC    CL115'THIS DATA-SET WILL BE DELETED IN 7 DAYS FROM '
FL14     DC    CL6' '
         DC    CL115'CREATION UNLESS I AM INFORMED OTHERWISE!'
FL15     DC    CL6'0'
         DC    CL115'PLEASE CHECK AND RETURN :'
FL16     DC    CL6'0'
         DC    C'THE SPR NUMBER IS _____ - HOLD THIS DUMP UNTIL'
         DC    CL100' THE SPR IS CLOSED'
FL17     DC    CL6'0'
         DC    CL115'THIS DUMP MAY BE DELETED _______'
******************************************************************
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         AMDDATA
         EJECT
         CVT   DSECT=YES
         END
