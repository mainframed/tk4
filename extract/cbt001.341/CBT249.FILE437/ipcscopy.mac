         TITLE 'SVC DUMP COPY PROGRAM  - OUTPUT TO MSVGP'
**********************************************************************
*                                                                    *
*    IPCSCOPY                                                        *
*              THIS PROGRAM IS DESIGNED TO COPY A SYS1.DUMP DATA SET *
*              FROM FILE DUMPIN TO A DYNAMICALLY CREATED FILE ON THE *
*              DISK PACK SPECIFIED IN THE PARM.                      *
*                                                                    *
*              A SYSPRINT FILE IS CREATED WHICH CONTAINS THE DATASET *
*              NAME OF THE OUTPUT FILE AND A COUNT OF THE # OF       *
*              RECORDS IN THE O/P FILE.                              *
*                                                                    *
*              THE DUMPLOG FILE IS A MOD DATA SET THAT CONTAINS THE  *
*              DSN, CPUID, DATE, TIME, AND TITLE OF THE DUMP THAT IS *
*              MIGRATED.                                             *
*                                                                    *
*              THE JCL TO EXECUTE IPCSCOPY ARE:                      *
*                                                                    *
*                   //A EXEC PGM=IPCSCOPY,PARM='SEE BELOW'           *
*                   //SYSPRINT DD SYSOUT=A                           *
*                   //DUMPIN   DD DSN=SYS1.DUMPXX,DISP=SHR           *
*                   //DUMPLOG  DD DSN=#A1000.DUMPLOG,DISP=MOD        *
*                                                                    *
*        PARM  VALUES:                                               *
*        *     MSVGP= MSVGP NAME                                     *
*              SPR=   SPR OR OWNER ID                                *
*        *     NODE=  ORIGINATING CENTER NODE ID                     *
*        *     DUMPNO= 2 POSITION DUMP ID                            *
*        *     CPU=   1 POSITION CPU IDENTIFIER                      *
*                                                                    *
*        *     =      REQUIRED                                       *
*                                                                    *
*        ABENDS:                                                     *
*              1  - DYNAMIC ALLOCATION ERROR ON OUTPUT DATASET       *
*              2  - NO DUMP HEADER RECORD FOUND                      *
*              3  - NO PARM SPECIFIED                                *
*              10 - NO MSVGP SPECIFIED                               *
*              11 - NO DUMPNO SPECIFIED                              *
*              12 - NO NODE SPECIFIED                                *
*              13 - NO CPU SPECIFIED                                 *
*                                                                    *
*                                                                    *
*        MACROS : $PROLOG,TIME,RDJFCB,$EPILOG,                       *
*                 ABEND,OPEN,CLOSE,GET,PUT,DATEROU,                  *
*                 CALL,DCB,REGEQU,IEFZB4D0,IEFZB4D2,IEFJFCBN,        *
*                 AMDDATA,S99FAIL,ALLOC,DYNSPACE                     *
*                                                                    *
**********************************************************************
IPCSCOPY $PROLOG R11,R12
         EJECT
         L    1,0(1)            * LOAD ADDR OF PARM
         LH   2,0(1)            * LOAD LENGTH OF PARM
         LTR   2,2              * LENGTH OF ZERO
         BZ    NOPARM           * NO - BYPASS MOVE
         LA    R4,2(R1)         * -> PARM DATA
         LR    R5,R4
         AR    R5,R2            * -> END OF PARM DATA
PLOOP    EQU   *
*---------------------------------------------------------------------*
         CLC   =C'CPU=',0(R4)
         BE    DOCPU
*---------------------------------------------------------------------*
         CLC   =C'NODE=',0(R4)
         BE    DONODE
*---------------------------------------------------------------------*
         CLC   =C'SPR=',0(R4)
         BE    DOSPR
*---------------------------------------------------------------------*
         CLC   =C'DUMPNO=',0(R4)
         BE    DODUMPNO
*---------------------------------------------------------------------*
         CLC   =C'MSVGP=',0(R4)
         BE    DOMSVGP
*---------------------------------------------------------------------*
         B     INCR
*---------------------------------------------------------------------*
DOCPU    LA    R4,4(R4)                -> CPU=
         MVC   DSNSID(1),0(R4)         MOVE CPUID
         B     INCR
*---------------------------------------------------------------------*
DODUMPNO LA    R4,7(R4)                -> DUMPNO=
         MVC   DSNNO(2),0(R4)          MOVE DUMP NUMBER
         B     INCR
*---------------------------------------------------------------------*
DOMSVGP  LA    R4,6(R4)                 -> MSVGP=
         LA    R7,MSVGP
         MVC   MSVGP,=CL8' '           CLEAR OUT MSVGP FOR OVER-RIDE
         SR    R1,R1
MSVLOOP  EQU   *
         CLI   0(R4),C','
         BE    ENDMSV
         CLI   0(R4),C' '
         BL    ENDMSV
         MVC   0(1,R7),0(R4)
         LA    R1,1(R1)
         LA    R7,1(R7)                 -> NEXT BYTE
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDMSV
         B     MSVLOOP
ENDMSV   EQU   *
         MVC   PARMX,MSVGP
         B     INCR
*---------------------------------------------------------------------*
DOSPR    LA    R4,4(R4)                 -> SPR=
         LA    R7,COMM
         SR    R1,R1
SPRLOOP  EQU   *
         CLI   0(R4),C','
         BE    ENDSPR
         CLI   0(R4),C' '
         BL    ENDSPR
         MVC   0(1,R7),0(R4)
         LA    R1,1(R1)
         LA    R7,1(R7)                 -> NEXT BYTE
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDSPR
         B     SPRLOOP
ENDSPR   EQU   *
         B     INCR
*---------------------------------------------------------------------*
DONODE   LA    R4,5(R4)                 -> NODE=
         LA    R7,NODE
         SR    R1,R1
NODELOOP EQU   *
         CLI   0(R4),C','
         BE    ENDNODE
         CLI   0(R4),C' '
         BL    ENDNODE
         MVC   0(1,R7),0(R4)
         LA    R1,1(R1)
         LA    R7,1(R7)                 -> NEXT BYTE
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDNODE
         B     NODELOOP
ENDNODE  EQU   *
         B     INCR
*---------------------------------------------------------------------*
INCR     EQU   *
         LA    R4,1(R4)
         CR    R4,R5
         BH    ENDSCAN
         B     PLOOP
ENDSCAN  EQU   *
         MVC   FL3COMM,COMM
         CLI   MSVGP,C' '              HAVE MSVGP ?
         BNE   CKP2
         ABEND 10                      NO MSVGP SPECIFIED
CKP2     CLI   DSNNO,C' '              HAVE DUMP ID ?
         BNE   CKP3
         ABEND 11                      NO DUMP ID SPECIFIED
CKP3     CLI   NODE,C' '               NODE ID ?
         BNE   CKP4
         ABEND 12                      NO NODE ID SPECIFIED
CKP4     CLI   DSNSID,C' '
         BNE   HAVEPARM
         ABEND 13                      NO CPU SPECIFIED
         B     HAVEPARM
         EJECT
NOPARM   EQU   *
         ABEND 3                       NO PARM SPECIFIED
HAVEPARM EQU   *
         EJECT
         RDJFCB (DUMPIN)
         LA    R5,JFCB
         USING INFMJFCB,R5
         MVC   IDSN(24),JFCBDSNM       MOVE INPUT DSN TO MSG
         MVC   IVOLSER(6),JFCBVOLS     MOVE VOLSER OF INPUT DATASET
         DROP  R5
         SPACE
         LA    R9,10
         OPEN  DUMPIN
READHDR  GET   DUMPIN                  READ AN INPUT RECORD
         LR    R10,R1
         USING PRDINPUT,R10
         CLI   PRDRECID,PRDHDREC       HEADER RECORD ?
         BE    HEADER                  YES - GO PROCESS IT
         BCT   R9,READHDR              NO LOOP THRU
         ABEND 2,DUMP                  NO HEADER RECORD FOUND
ALLOC    DS    0H
         MVC   DSNAME(DSNL),DSNM       MOVE DSN INTO PARAMETER
         EJECT
         ALLOC DDN='DUMPOUT',DSN=DSNRB,MSVGP=MSVGPRB,UNIT='3330V',     X
               DISP=(NEW,CATLG,DELETE),SPACE=(CYL,(20,10),RLSE),       X
               ERROR=S99FAIL
         EJECT
         CLOSE (DUMPIN,REREAD)
         OPEN  (DUMPIN,,SYSPRINT,OUTPUT,DUMPOUT,OUTPUT)
         MVI   OPENSW,C'Y'
         SPACE
READ     GET   DUMPIN                  READ AN INPUT RECORD
         LR    R10,R1
PUTIT    PUT   DUMPOUT,(10)            DON'T STOP - PUT IT OUT
         AP    CTR,=P'01'              ADD TO RECORD COUNTER
         B     READ                    CONTINUE THE READ UNTIL
         EJECT
HEADER   EQU   *                       DUMP HEADER ROUTINE
         USING PRDINPUT,R10
         MVC   TOD,PRDTODVL            SAVE DATE
         LINK  EP=BLSUMTOD,PARAM=(TOD,TDATE),VL=1
         MVC   DSNT(2),DTIME
         MVC   DSNT+2(2),DTIME+3
         MVC   TITLE,PRDTITLE          MOVE TITLE TO OUTPUT LINE
         DROP  R10
         MVC   DSND(2),TDATE
         MVC   DSND+2(2),TDATE+3
         MVC   DSND+4(2),TDATE+6
         B     ALLOC                   BACK TO READ NEXT RECORD
         EJECT
EOJ      EQU   *
         CLI   OPENSW,C'Y'
         BNE   EXIT
         UNPK  COUNT(6),CTR            UNPACK RECORD COUNTER
         OI    COUNT+5,X'F0'           SET FULL DISPLAY
         PUT   SYSPRINT,MSGA           MESSAGE OF DSNAME OF INPUT
         PUT   SYSPRINT,MSGB           MESSAGE OF DSNAME OF OUTPUT
         PUT   SYSPRINT,TITLINE        AND PRINT WHAT WE HAVE
         MVC   DUMPDSN(DSNL),DSNM
         MVC   DUMPTLE,TITLE
         OPEN  (DUMPLOG,EXTEND)
         PUT   DUMPLOG,DUMPDATA
         CLOSE (DUMPLOG,FREE)
         MVC   FL4TITLE,TITLE          MOVE TITLE
         MVC   FL5DSN(DSNL),DSNM
         PUT   SYSPRINT,FL1
         PUT   SYSPRINT,FL1A
         PUT   SYSPRINT,FL1A
         PUT   SYSPRINT,FL2
         PUT   SYSPRINT,FL3
         PUT   SYSPRINT,FL4
         PUT   SYSPRINT,FL5
         PUT   SYSPRINT,FL6
         PUT   SYSPRINT,FL7
         PUT   SYSPRINT,FL8
         PUT   SYSPRINT,FL9
         PUT   SYSPRINT,FL10
         PUT   SYSPRINT,FL11
         PUT   SYSPRINT,FL12
         PUT   SYSPRINT,FL13
         PUT   SYSPRINT,FL13A
         PUT   SYSPRINT,FL13A
         PUT   SYSPRINT,FL14
         PUT   SYSPRINT,FL15
         PUT   SYSPRINT,FL16
         PUT   SYSPRINT,FL17
         PUT   SYSPRINT,FL18
CLOSE    CLOSE (SYSPRINT,,DUMPIN,FREE,DUMPOUT,FREE)
EXIT     $EPILOG
         EJECT
S99FAIL  S99FAIL
         ABEND 1,DUMP
         EJECT
         LTORG
         SPACE
WORK     DC    F'0'                    WORK AREA FOR DATE/TIME
CTR      DC    PL4'0'                  COUNT FOR RECORDS WRITTEN
TOD      DC    D'00'                   WORK AREA FOR DUMP TIME/DATE
EDMASK   DC    X'21204B20204B2020'     TIME EDIT MASK
OPENSW   DC    C'N'                    TO ELIMINATE 0C1 ABEND WHEN NO
*                                      DUMP
         SPACE
         PRINT NOGEN
DUMPIN   DCB   DSORG=PS,MACRF=GL,DDNAME=DUMPIN,EODAD=EOJ,EXLST=DMPEXT
         SPACE
DUMPOUT  DCB   DSORG=PS,MACRF=PM,DDNAME=DUMPOUT,RECFM=F,BLKSIZE=4104,  X
               LRECL=4104,OPTCD=W
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BLKSIZE=121,RECFM=FA
DUMPLOG  DCB   DSORG=PS,MACRF=PM,DDNAME=DUMPLOG,                       X
               RECFM=FB,LRECL=155
         PRINT GEN
         EJECT
         DS    0F
DMPEXT   DC    X'87',AL3(JFCB)
JFCB     DC    CL176' '
         SPACE 2
MSGA     DC    CL5'1DSN='
DSNM     DC    C'AOS.'                 DATA SET NAME
DSNSID   DC    C' ',C'.D'
DSND     DC    CL6' ',C'.T'            DSN DATE
DSNT     DC    CL4' ',C'N'             DSN TIME
DSNNO    DC    CL2'  '
DSNL     EQU   *-DSNM                  LENGTH OF MESSAGE
         DC    C'   MSVGP = '
PARMX    DC    CL8'IPCSDUMP'           MSVGP OF OUTPUT
         DC    C'   RECORDS WRITTEN='
COUNT    DC    CL6' ',CL80' '
         SPACE
MSGB     DC    C'0INPUT DSN='          INPUT DSNAME
IDSN     DC    CL10' '
         DC    C'INPUT VOL=SER='       INPUT VOLSER
IVOLSER  DC    CL6' '
         DC    CL80' '                 FILLER
DUMPDATA DS    0CL155
COMM     DC    CL6' '
         DC    CL5' '
DUMPDSN  DC    CL24' '
         DC    C' '
NODE     DC    CL8' '
         DC    CL13' '
DUMPTLE  DC    CL100' '
         EJECT
TITLINE  DC    CL6' '
TDATE    DC    CL9' '
DTIME    DC    CL8' '
         DC    CL4' '
TITLE    DC    CL100' '
         EJECT
DSNRB    DC    A(DSNAME),Y(DSNL)       DSNAME KEY
DSNAME   DC    CL44' '                 DSNAME FOR ALLOCATION
MSVGPRB  DC    A(MSVGP),Y(8)
MSVGP    DC    CL8'IPCSDUMP'           MSVGP ID
         EJECT
******************************************************************
*        THIS AREA CONTAINS THE DUMP FORM USED TO ROUTE INFOR-   *
*        MATION ON SYSTEM DUMPS TO THE RESPONSIBLE GROUP         *
******************************************************************
FL1      DC    C'1',CL12' ',CL108'SVC DUMP NOTIFICATION FORM'
FL1A     DC    C'+',CL12' ',CL108'SVC DUMP NOTIFICATION FORM'
FL2      DC    C'0',CL5' ',CL115'FROM : CONTROL PROGRAM SERVICES '
FL3      DC    CL6' '
         DC    C'TO   : '
FL3COMM  DC    CL120' '                TO GROUP
FL4      DC    CL6'0'
         DC    C'DUMP TITLE    :'
         DC    CL4' '
FL4TITLE DC    CL100' '                DUMP TITLE + FILLER
FL5      DC    CL6'0'
         DC    C'DATA-SET NAME :'
         DC    CL7' '
FL5DSN   DC    CL110' '
FL6      DC    CL6' '
         DC    C'TAKEN ON CPU  :'
         DC    CL4' ',7C'_',C'×',CL5' ',C'×',CL6' ',C'×',CL2' ',C'×'
         DC    CL100' '                FILLER
FL7      DC    CL6' '
         DC    C'DATE (MMDDYY) :'
         DC    CL4' ',13C'_',C'×',CL6' ',C'×',CL2' ',C'×'
         DC    CL80' '                 FILLER
FL8      DC    CL6' '
         DC    C'TIME (HHMMSS) :'
         DC    CL4' ',20C'_',C'×',CL2' ',C'×'
         DC    CL80' '                 FILLER
FL9      DC    CL6' '
         DC    C'FROM DUMP NO. :'
         DC    CL4' ',23C'_',C'×'
         DC    CL80' '                 FILLER
FL10     DC    CL6'0'
         DC    CL115'PLEASE SUPPLY ME WITH THE SPR # IF THIS DUMP IS '
FL11     DC    CL6' '
         DC    CL115'SPR''D OR INFORM ME WHEN THE DUMP IS NO LONGER'
FL12     DC    CL6' '
         DC    CL115'NEEDED AND IT WILL BE DELETED.'
FL13     DC    CL6'0'
         DC    CL115'         ***** N O T I C E *****'
FL13A    DC    CL6'+'
         DC    CL115'         ***** N O T I C E *****'
FL14     DC    CL6'0'
         DC    CL115'THIS DATA-SET WILL BE DELETED IN 7 DAYS FROM '
FL15     DC    CL6' '
         DC    CL115'CREATION UNLESS I AM INFORMED OTHERWISE!'
FL16     DC    CL6'0'
         DC    CL115'PLEASE CHECK AND RETURN :'
FL17     DC    CL6'0'
         DC    C'THE SPR NUMBER IS _____.  HOLD THIS DUMP UNTIL'
         DC    CL100' THE SPR IS CLOSED'
FL18     DC    CL6'0'
         DC    CL115'THIS DUMP MAY BE DELETED _______'
******************************************************************
         EJECT
         DYNSPACE
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         DSECT
         IEFJFCBN
         EJECT
         AMDDATA
         END
