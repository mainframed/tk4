         MACRO
         COPY1 &N
         MVC   C&N,V&N
         MEND
         SPACE 1
         MACRO
         COPY2 &N
         MVC   V&N,C&N
         MEND
         SPACE 1
         MACRO
         SPFDF &N,&L
L&N      DC    A(L'V&N)           LEN OF VARIABLE
&N       DC    C'(&N)'            ISPF VARIABLE NAME
V&N      DC    CL&L.' '           ISPF VARIABLE FIELD
C&N      DC    CL&L.' '           COPY OF ISPF VARIABLE
         SPACE 1
         MEND
         SPACE 2
         MACRO
         SPFVD &N
         L     R15,ISPLINK
         CALL  (15),(VDEFINE,&N,V&N,CHAR,L&N),VL
         MEND
         SPACE 2                                          *LBD 06/84*
         MACRO
         SPFVDL &N
         L     R15,ISPLINK
         CALL  (15),(VDELETE,&N),VL
         MEND
         EJECT
PHONE    $PROLOG
         $REGS
         L     1,0(1)              * LOAD ADDR OF PARM
         LH    2,0(1)              * LOAD LENGTH OF PARM
         LTR   2,2                 * LENGTH OF ZERO
         BZ    NOPARM              * NO - BYPASS MOVE
         BCTR  2,0                 * SUBTRACT LENGTH BY 1
         MVC   TABLENAM,BLANKS     * CLEAR TABLE NAME
         EX    R2,MVCPARM          * MOVE TABLE NAME
         B     NOPARM
MVCPARM  MVC   TABLENAM(0),2(R1)   ** EXECUTED INSTRUCTION
NOPARM   DS    0H
         L     R15,ISPLINK
         CALL  (15),(CONTROL,ERRORS,RETURN),VL
         SPFVD SEL
         SPFVD LAST
         SPFVD FIRST
         SPFVD INITIAL
         SPFVD CORP
         SPFVD ADDRL1
         SPFVD ADDRL2
         SPFVD ADDRL3
         SPFVD CITYST
         SPFVD ZIP
         SPFVD PHONENM
         SPFVD EXTEN
         L     R15,ISPLINK
         CALL  (15),(TBOPEN,TABLENAM),VL
         LTR   R15,R15
         BZ    TBCOK
         L     R15,ISPLINK
         CALL  (15),(TBCREATE,TABLENAM,,NAMES,WRITE,REPLACE),VL
         LTR   R15,R15
         BZ    ADD                NEW TABLE
         CH    R15,=H'04'         RC> 4
         BNH   TBCOK              CREATE OK
         B     EXITNG
TBCOK    DS    0H
         SR    R11,R11
DISP     DS    0H
         CH    R11,=H'04'
         BE    DISPNP             DISP WITH NO PANEL
         L     R15,ISPLINK
         CALL  (15),(TBDISPL,TABLENAM,PANEL),VL
         CH    R15,=H'04'
         BH    EXITOK
         LR    R11,R15            SAVE RC
         B     DOIT
DISPNP   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBDISPL,TABLENAM),VL
         CH    R15,=H'04'
         BH    EXITOK
         LR    R11,R15            SAVE RC
DOIT     DS    0H
         L     R15,ISPLINK
         MVC   LZCMD,=A(L'VZCMD)  RESET ZCMD LENGTH
         CALL  (15),(VCOPY,ZCMD,LZCMD,VZCMD,MOVE),VL
         L     R5,LZCMD                LOAD LEN OF CMD    *LBD 05/84*
         CLI   VZCMD,C' '
         BE    CKSEL
         OC    VZCMD,BLANKS
         MVC   SAVEZCMD,VZCMD
         MVC   VZCMD,BLANKS
         L     R15,ISPLINK
         CALL  (15),(VREPLACE,ZCMD,LZCMD,VZCMD),VL
         SPACE 2
         CLC   =C'ADD',SAVEZCMD   ADD
         BE    ADD
         CLC   =C'F ',SAVEZCMD    FIND                      *LBD 04/84*
         BE    FIND                                         *LBD 04/84*
         CLC   =C'LIST',SAVEZCMD  LIST
         BE    LIST
         CLC   =C'MAIL',SAVEZCMD  MAIL LIST (I.E. NO PHONE)
         BE    LIST
         CLC   =C'SAVE',SAVEZCMD  SAVE
         BE    SAVE
         CLC   =C'CANCEL',SAVEZCMD CANCEL
         BE    CANCEL
         B     DISP
CKSEL    DS    0H
         OC    VSEL,BLANKS
         CLC   =C'S',VSEL         REVIEW/UPDATE ITEM
         BE    UPDATE
         CLC   =C'D',VSEL         DELETE
         BE    DELETE
SELOK    DS    0H
         MVC   VSEL,BLANKS
         L     R15,ISPLINK
         MVC   LSEL,=A(2)         SET LENGTH FOR REPLACE
         CALL  (15),(VREPLACE,SEL,LSEL,VSEL),VL
         B     DISP
SAVE     DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBSAVE,TABLENAM,REPLCOPY),VL
         B     DISP
FIND     DS    0H                                           *LBD 04/84*
         SH    R5,=H'02'               LESS 'F '          *LBD 05/84*
         BCTR  R5,R0                   LESS 1 FOR CLC     *LBD 05/84*
         SR    R11,R11                                      *LBD 04/84*
         L     R15,ISPLINK                                  *LBD 04/84*
         CALL  (15),(TBTOP,TABLENAM),VL                     *LBD 04/84*
FINDSK   DS    0H                                           *LBD 04/84*
         L     R15,ISPLINK                                  *LBD 04/84*
         CALL  (15),(TBSKIP,TABLENAM),VL                    *LBD 04/84*
         LTR   R15,R15                                      *LBD 04/84*
         BNZ   DISP                                         *LBD 04/84*
         MVC   HLAST,VLAST                                 *LBD 05/84*
         OC    HLAST,BLANKS            UPPER CASE IT       *LBD 05/84*
         EX    R5,FCLC                                    *LBD 05/84*
         BE    DISP                                         *LBD 05/84*
         B     FINDSK                                       *LBD 04/84*
FCLC     CLC   HLAST(0),SAVEZCMD+2     EXECUTED             *LBD 04/84*
CANCEL   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBEND,TABLENAM),VL
         B     EXITOUT
         EJECT                                            *LBD 06/84*
LIST     DS    0H                                         *LBD 06/84*
         OPEN  (LISTDCB,OUTPUT)                           *LBD 06/84*
         L     R15,ISPLINK                                *LBD 06/84*
         CALL  (15),(TBTOP,TABLENAM),VL                   *LBD 06/84*
NEXTL    DS    0H                                         *LBD 06/84*
         CP    LCTR,=P'42'                                *LBD 06/84*
         BNH   DOLINE                                     *LBD 06/84*
         MVI   LINEA,C'1'                                 *LBD 06/84*
         ZAP   LCTR,=P'00'                                *LBD 06/84*
DOLINE   DS    0H                                         *LBD 06/84*
         L     R15,ISPLINK                                *LBD 06/84*
         CALL  (15),(TBSKIP,TABLENAM),VL                  *LBD 06/84*
         LTR   R15,R15                                    *LBD 06/84*
         BNZ   ENDL                                       *LBD 06/84*
         MVI   LINEA,C'-'                                 *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
         MVC   LINE(15),VLAST                             *LBD 06/84*
         TRT   LINE,TRTBL                                 *LBD 06/84*
         MVI   0(R1),C','                                 *LBD 06/84*
         MVC   2(15,R1),VFIRST                            *LBD 06/84*
         LA    R2,2(R1)                                   *LBD 06/84*
         TRT   0(20,R2),TRTBL                             *LBD 06/84*
         MVC   2(1,R1),VINITIAL                           *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         MVI   LINEA,C' '                                 *LBD 06/84*
         AP    LCTR,=P'03'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
         CLC   VCORP,BLANKS            ANY CORP           *LBD 06/84*
         BE    L2                      NO                 *LBD 06/84*
         MVC   LINE(30),VCORP                             *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L2       DS    0H                                         *LBD 06/84*
         CLC   VADDRL1(4),BLANKS                          *LBD 06/84*
         BE    L3                                         *LBD 06/84*
         MVC   LINE(30),VADDRL1                           *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L3       DS    0H                                         *LBD 06/84*
         CLC   VADDRL2(4),BLANKS                          *LBD 06/84*
         BE    L4                                         *LBD 06/84*
         MVC   LINE(30),VADDRL2                           *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L4       DS    0H                                         *LBD 06/84*
         CLC   VADDRL3(4),BLANKS                          *LBD 06/84*
         BE    L5                                         *LBD 06/84*
         MVC   LINE(30),VADDRL3                           *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L5       DS    0H                                         *LBD 06/84*
         CLC   VCITYST(4),BLANKS                          *LBD 06/84*
         BE    L6                                         *LBD 06/84*
         MVC   LINE(30),VCITYST                           *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L6       DS    0H                                         *LBD 06/84*
         CLC   VZIP(4),BLANKS                             *LBD 06/84*
         BE    L7                                         *LBD 06/84*
         MVC   LINE(10),VZIP                              *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
L7       DS    0H                                         *LBD 06/84*
         CLC   SAVEZCMD(4),=C'MAIL'                       *LBD 06/84*
         BE    LP                      YES - SKIP PHONE   *LBD 06/84*
         CLC   VPHONENM(4),BLANKS                         *LBD 06/84*
         BE    L8                                         *LBD 06/84*
         MVC   LINE(20),VPHONENM                          *LBD 06/84*
L8       DS    0H                                         *LBD 06/84*
         CLI   VEXTEN,C' '                                *LBD 06/84*
         BE    LP                                         *LBD 06/84*
         TRT   LINE,TRTBL                                 *LBD 06/84*
         MVI   2(R1),C'('                                 *LBD 06/84*
         MVC   3(20,R1),VEXTEN                            *LBD 06/84*
         LA    R2,4(R1)                                   *LBD 06/84*
         TRT   0(20,R2),TRTBL                             *LBD 06/84*
         MVI   0(R1),C')'                                 *LBD 06/84*
LP       DS    0H                                         *LBD 06/84*
         PUT   LISTDCB,LINEA                              *LBD 06/84*
         AP    LCTR,=P'01'                                *LBD 06/84*
         MVC   LINE,BLANKS                                *LBD 06/84*
         B     NEXTL                                      *LBD 06/84*
ENDL     DS    0H                                         *LBD 06/84*
         CLOSE (LISTDCB)                                  *LBD 06/84*
         L     R15,ISPLINK                                *LBD 06/84*
         CALL  (15),(TBTOP,TABLENAM),VL                   *LBD 06/84*
         SR    R11,R11                 CLEAR R11          *LBD 06/84*
         B     SELOK                                      *LBD 06/84*
         EJECT                                            *LBD 06/84*
ADD      DS    0H
         L     R15,ISPLINK
         CALL  (15),(DISPLAY,ADDPAN),VL
         LTR   R15,R15
         BNZ   DISP
         COPY1 LAST
         COPY1 FIRST
         COPY1 INITIAL
         COPY1 CORP
         COPY1 ADDRL1
         COPY1 ADDRL2
         COPY1 ADDRL3
         COPY1 CITYST
         COPY1 ZIP
         COPY1 PHONENM
         COPY1 EXTEN
         L     R15,ISPLINK
         CALL  (15),(TBTOP,TABLENAM),VL
         MVC   VLAST,BLANKS                                 *LBD 04/84*
SKIP     DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBSKIP,TABLENAM),VL
         LTR   R15,R15
         BZ    ADDCLC
         CH    R15,=H'08'         END OF TABLE
         BE    DOBADD
         B     EXITNG
ADDCLC   DS    0H
         CLC   VLAST,BLANKS                                 *LBD 04/84*
         BE    DOADDC                                       *LBD 04/84*
         CLC   CLAST,VLAST
         BE    DOADDC
         BH    SKIP                                         *LBD 04/84*
         BL    DOPADD                                       *LBD 04/84*
DOBADD   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBBOTTOM,TABLENAM),VL
         B     DOADDC
DOPADD   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBSKIP,TABLENAM,SN),VL
DOADDC   DS    0H
         COPY2 LAST
         COPY2 FIRST
         COPY2 INITIAL
         COPY2 CORP
         COPY2 ADDRL1
         COPY2 ADDRL2
         COPY2 ADDRL3
         COPY2 CITYST
         COPY2 ZIP
         COPY2 PHONENM
         COPY2 EXTEN
DOADD    DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBADD,TABLENAM),VL
         B     ADD
UPDATE   DS    0H
         L     R15,ISPLINK
         CALL  (15),(DISPLAY,UPDPAN),VL
         LTR   R15,R15
         BNZ   SELOK
         L     R15,ISPLINK
         CALL  (15),(TBPUT,TABLENAM),VL
         B     SELOK
DELETE   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBDELETE,TABLENAM),VL
         B     SELOK
EXITOK   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBCLOSE,TABLENAM),VL
         SPFVDL SEL
         SPFVDL LAST
         SPFVDL FIRST
         SPFVDL INITIAL
         SPFVDL CORP
         SPFVDL ADDRL1
         SPFVDL ADDRL2
         SPFVDL ADDRL3
         SPFVDL CITYST
         SPFVDL ZIP
         SPFVDL PHONENM
         SPFVDL EXTEN
EXITOUT  DS    0H
         $EPILOG 0
EXITNG   DS    0H
         L     R15,ISPLINK
         CALL  (15),(TBCLOSE,TABLENAM),VL
         SPFVDL SEL
         SPFVDL LAST
         SPFVDL FIRST
         SPFVDL INITIAL
         SPFVDL CORP
         SPFVDL ADDRL1
         SPFVDL ADDRL2
         SPFVDL ADDRL3
         SPFVDL CITYST
         SPFVDL ZIP
         SPFVDL PHONENM
         SPFVDL EXTEN
         $EPILOG 4
         LTORG
         SPACE 1
ISPLINK  DC    V(ISPLINK)
         SPACE 1
BLANKS   DC    CL80' '
         SPACE 2                                          *LBD 06/84*
LCTR     DC    PL2'99'                                    *LBD 06/84*
LINEA    DC    CL10' '                                    *LBD 06/84*
LINE     DC    CL80' '                                    *LBD 06/84*
         SPACE 2                                          *LBD 06/84*
SAVEZCMD DC    CL30' '
HLAST    DC    CL15' '                                    *LBD 05/84*
         SPACE 2                                            *LBD 04/84*
OUTLINE  DC    CL80' '                                      *LBD 04/84*
         SPACE 1
SN       DC    A(-1)                                        *LBD 04/84*
ADDPAN   DC    CL8'PHONEADD'
UPDPAN   DC    CL8'PHONEUPD'
CHAR     DC    CL8'CHAR'
CONTROL  DC    CL8'CONTROL'
ERRORS   DC    CL8'ERRORS'
RETURN   DC    CL8'RETURN'
DISPLAY  DC    CL8'DISPLAY'
MOVE     DC    CL8'MOVE'
PANEL    DC    CL8'PHONE'
REPLACE  DC    CL8'REPLACE'
REPLCOPY DC    CL8'REPLCOPY'
TABLENAM DC    CL8'PHONE'
TBADD    DC    CL8'TBADD'
TBBOTTOM DC    CL8'TBBOTTOM'
TBCLOSE  DC    CL8'TBCLOSE'
TBCREATE DC    CL8'TBCREATE'
TBDELETE DC    CL8'TBDELETE'
TBDISPL  DC    CL8'TBDISPL'
TBEND    DC    CL8'TBEND'
TBOPEN   DC    CL8'TBOPEN'
TBPUT    DC    CL8'TBPUT'
TBSAVE   DC    CL8'TBSAVE'
TBSKIP   DC    CL8'TBSKIP'
TBTOP    DC    CL8'TBTOP'
VCOPY    DC    CL8'VCOPY'
VDEFINE  DC    CL8'VDEFINE'
VDELETE  DC    CL8'VDELETE'
VREPLACE DC    CL8'VREPLACE'
WRITE    DC    CL8'WRITE'
         EJECT
*        DEFINE ISPF VARIABLES
NAMES    DC    C'(LAST,FIRST,INITIAL,CORP,ADDRL1,ADDRL2,'
         DC    C'ADDRL3,CITYST,ZIP,PHONENM,EXTEN)'
         SPACE 1
         SPFDF ZCMD,30
         SPFDF SEL,2
         SPFDF LAST,15
         SPFDF FIRST,15
         SPFDF INITIAL,1
         SPFDF CORP,30
         SPFDF ADDRL1,30
         SPFDF ADDRL2,30
         SPFDF ADDRL3,30
         SPFDF CITYST,30
         SPFDF ZIP,10
         SPFDF PHONENM,20
         SPFDF EXTEN,20
         EJECT
LISTDCB  DCB   DSORG=PS,MACRF=PM,DDNAME=PHONLIST,RECFM=FBA, *LBD 04/84*X
               LRECL=80,BLKSIZE=6160                        *LBD 04/84*
         SPACE 2                                          *LBD 06/84*
TRTBL    DC    256X'00'                                   *LBD 06/84*
         ORG   TRTBL+64                                   *LBD 06/84*
         DC    X'40'                                      *LBD 06/84*
         ORG   ,                                          *LBD 06/84*
         END
