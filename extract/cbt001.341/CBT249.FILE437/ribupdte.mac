         TITLE 'ROCKWELL IEBUPDTE SIMULATOR  09-11-84'
*---------------------------------------------------------------------*
*  MODULE: RIBUPDTE                                                   *
*                                                                     *
*  DESCRIPTION: THIS PROGRAM WILL SIMULATE IEBUPDTE.  IT MAY BE       *
*  INVOKED EITHER VIA A TSO CALL, BATCH PROGRAM.             WHEN     *
*  INVOKED RIBUPDTE WILL SIMULATE THE INSERT, REPLACE FUNCTIONS OF    *
*  IEBUPDTE PLUS WILL DETERMINE IF A USERMOD RECORD IS BEING OVERLAYED*
*  OR IF AN INSERT IS BEING PLACED WITHIN 10 CARDS OF A USERMOD CARD. *
*                                                                     *
*  AN INDEX IS PRINTED AFTER THE UPDATE RUN WITH THE ELEMENT NAME     *
*  AND PAGE NUMBER ALONG WITH A FLAG TO INDICATE IF ANY OVERLAY'S     *
*  WERE ENCOUNTERED.                                                  *
*                                                                     *
*  A USERMOD RECORD (CARD) IS DETERMINED IN THE FOLLOWING MANNER:     *
*                                                                     *
*            1) FROM SYSUT1 ONLY                                      *
*            2) DOES COL 64 = @                                       *
*            3) IS COL 65 A CHAR FROM K THRU T                        *
*            4) IF BOTH 2 AND 3 ARE TRUE THEN IT MUST BE A USERMOD    *
*               RECORD.                                               *
*---------------------------------------------------------------------*
* JCL:                                                                *
*            //RIBUP EXEC PGM=RIBUPDTE,PARM=                          *
*            //SYSPRINT DD SYSOUT=*                                   *
*            //SYSIN  DD -- SAME AS IEBUPDTE                          *
*            //SYSUT1 DD -- SAME AS IEBUPDTE                          *
*            //SYSUTA DD -- ALTERNATE SYSUT1                          *
*                                                                     *
*---------------------------------------------------------------------*
* PARM:                                                               *
*        ALL      LIST ALL RECORDS                                    *
*        NOSEP    NO PAGE SEPARATOR WITHIN A MEMBER                   *
*        ALNOSEP  LIST ALL AND NO PAGE SEPERATOR                      *
*                                                                     *
*---------------------------------------------------------------------*
* FLAGS:                                                              *
*        MODID  =  SYSUT1 INPUT RECORD IS A USERMOD RECORD            *
*                  (COL 64 CHECKED FOR @ AND 65 FOR VALID             *
*                   USERMOD PREFIX K-T)                               *
*        INSW   =  HAVE JUST PERFORMED AN INSERT                      *
*        NOSEP  =  NO SEPERATORS WITHIN A MEMBER ONLY BETWEEN         *
*        REPSW  =  HAVE JUST PERFORMED AN REPLACE                     *
*        DOUT1  =  SKIP READ OF SYSIN                                 *
*        UT1CL  =  SYSUT1 IS CLOSED IF OFF                            *
*        PROCESS=  INDICATES THAT WE HAVE NOT READ A VALID IEBUPDTE   *
*                  CC FROM SYSIN.                                     *
*---------------------------------------------------------------------*
         EJECT
RIBUPDTE $PROLOG
         LR    R9,R1                    SAVE PARM REGISTER
         L    1,0(1)            * LOAD ADDR OF PARM
         LH   2,0(1)            * LOAD LENGTH OF PARM
         LTR   2,2              * LENGTH OF ZERO
         BZ    START            * NO - BYPASS MOVE
         BCTR 2,0               * SUBTRACT LENGTH BY 1
         CLC  =C'ALL',2(R1)     * PARM = ALL SPECIFIED
         BNE   CKPARM2
         SETON LISTALL
         B     START                                      *LBD 08/84*
CKPARM2  DS    0H                                         *LBD 08/84*
         CLC   =C'NOSEP',2(R1)                            *LBD 08/84*
         BNE   CKPARM3                                    *LBD 08/84*
         SETON NOSEP                                      *LBD 08/84*
CKPARM3  DS    0H                                         *LBD 08/84*
         CLC   =C'ALNOSEP',2(R1)                          *LBD 08/84*
         BNE   START                                      *LBD 08/84*
         SETON NOSEP,LISTALL                              *LBD 08/84*
START    EQU   *                        BEGIN OUR PHONEY IEBUPDTE
         SPACE 3
OPEN     OPEN  (SYSIN,,SYSPRINT,OUTPUT)
         EJECT
         L     R2,ENTLEN           LOAD LEN OF TABLE
         GETMAIN R,LV=(R2)         DO GETMAIN
         ST    R1,LOGADDR          SAVE ENTRY ADDRESS
         LR    R9,R1
         USING LOG,R9
         SETOF DOUT1
RSYSIN   EQU   *
         IFON  ENDIN,RSYSUT1                                    *LBD*
         GET   SYSIN
         LR    R5,R1                    -> INPUT RECORD
         CLC   0(2,R5),=C'./'           IEBUPDTE CONTROL CARD ?
         BE    CHKUP                    YES
         CLC   0(2,R5),=C'++'           SMP CONTROL ?
         BE    SMPCC                    YES - GO PROCESS IT
         IFOFF PROCESS,RSYSIN           KEEP READING IF NOT PROCESSING
         IFOFF UT1CL,INSERT
         IFON  INSW,DOCLC
         SPACE 2
RSYSUT1  EQU   *
         IFOFF UT1CL,INSERT
         IFOFF LISTALL,READUT1
         IFON  LISTED,READUT1
         PUT   SYSPRINT,RLINE
         MVC   RLINE,BLANKS
         AP    LINECTR,=P'01'
         CP    LINECTR,LINEPPG
         BL    READUT1
         ZAP   LINECTR,=P'05'
         AP    PAGECTR,=P'01'
         UNPK  PAGE,PAGECTR
         OI    PAGE+3,X'F0'
         IFON  NOSEP,READUT1                              *LBD 08/84*
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,T2
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,T3
READUT1  GET   SYSUT1
         SETOF MODID,DOUT1,LISTED       TURN OFF SWITCHES
         LR    R4,R1                    -> SYSUT1 INPUT
         MVC   RREC,0(R4)
         MVC   RST,BLANKS
         IFON  ENDIN,RSYSUT1                                    *LBD*
         AP    INCTR,=P'01'
         CLI   63(R4),C'@'              MOD ID
         BNE   DOCLC
         CLI   64(R4),C'K'
         BL    DOCLC                    IBM MOD
         CLI   64(R4),C'T'
         BH    DOCLC                    IBM MOD
         SETON MODID                    FLAG AS A USERMOD CARD
         SPACE 2
DOCLC    CLC   72(8,R5),72(R4)          SEQ =
         BL    INSERT                   INSERT
         BE    REPLACE                  REPLACE
         BH    CKINSRT                  CHECK FOR INSERT PRIOR
INSERT   MVC   OREC,0(R5)
         MVC   OST,INSERTM
         SETON INSW
         ZAP   INCTR,=P'01'             UPDATE INSERT-REPLACE COUNTER
         B     PUTIT
         SPACE 2
CKINSRT  DS    0H
         IFOFF MODID,RSYSUT1
         IFON  REPSW,CKNEAR
         IFOFF INSW,RSYSUT1
CKNEAR   DS    0H
         CP    INCTR,=P'10'             REPLACE OR INSERT WITHIN 10
         BH    RSYSUT1                  NO - MORE THAN 10
         MVC   OREC,0(R4)
         MVC   NEARCT(3),=X'202021'
         ED    NEARCT-1(4),INCTR
         MVC   OST,PRIORM
         MVI   FLAG,C'I'           SET FLAG AS INSERT
         ZAP   INCTR,=P'00'             RESET COUNTER
         SETOF INSW,REPSW               TURN OFF SWITCHES
         SETON DOUT1,LISTED
         B     PUTIT
         SPACE 2
REPLACE  MVC   OREC,0(R5)
         SETOF INSW                     TURN OFF INSERT SWITCH
         SETON REPSW                    SET ON REPLACE SWITCH
         ZAP   INCTR,=P'00'             UPDATE INSERT-REPLACE COUNTER
         IFOFF MODID,DOREP
         MVC   OST,UMODM
         MVI   FLAG,C'O'           SET OVERLAY FLAG
         B     PUTREP
         SPACE 2
DOREP    MVC   OST,REPLM
         SPACE
PUTREP   IFON  SBLK,MVREC
         PUT   SYSPRINT,BLANKS
         AP    LINECTR,=P'01'
MVREC    MVC   RREC,0(R4)
         MVC   RST,REPDM
         PUT   SYSPRINT,RLINE
         MVC   RLINE,BLANKS
         PUT   SYSPRINT,OLINE
         PUT   SYSPRINT,BLANKS
         AP    LINECTR,=P'03'
         SETON SBLK
         B     CLROLINE
         EJECT
PUTIT    PUT   SYSPRINT,OLINE
         AP    LINECTR,=P'01'
         SETOF SBLK
CLROLINE MVC   OLINE,BLANKS
         CP    LINECTR,LINEPPG
         BL    CKRETURN
         ZAP   LINECTR,=P'05'
         AP    PAGECTR,=P'01'
         UNPK  PAGE,PAGECTR
         OI    PAGE+3,X'F0'
         IFON  NOSEP,CKRETURN                             *LBD 08/84*
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,T2
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,T3
         SPACE
CKRETURN DS    0H
         IFON  NEWUP,READUT1$                             *LBD 09/84*
         IFON  DOUT1,RSYSUT1
         B     RSYSIN
         EJECT
READUT1$ DS    0H                                         *LBD 09/84*
         GET   SYSUT1                                     *LBD 09/84*
         MVC   RREC,0(R1)                                 *LBD 09/84*
         MVC   RST,BLANKS                                 *LBD 09/84*
         PUT   SYSPRINT,RLINE                             *LBD 09/84*
         MVC   RLINE,BLANKS                               *LBD 09/84*
         AP    LINECTR,=P'01'                             *LBD 09/84*
         CP    LINECTR,LINEPPG                            *LBD 09/84*
         BL    READUT1$                                   *LBD 09/84*
         ZAP   LINECTR,=P'05'                             *LBD 09/84*
         AP    PAGECTR,=P'01'                             *LBD 09/84*
         UNPK  PAGE,PAGECTR                               *LBD 09/84*
         OI    PAGE+3,X'F0'                               *LBD 09/84*
         IFON  NOSEP,READUT1$                             *LBD 09/84*
         PUT   SYSPRINT,T1                                *LBD 09/84*
         PUT   SYSPRINT,T2                                *LBD 09/84*
         PUT   SYSPRINT,BLANKS                            *LBD 09/84*
         PUT   SYSPRINT,T3                                *LBD 09/84*
         B     READUT1$                                   *LBD 09/84*
         EJECT
EOFUT1   EQU   *
         CLOSE SYSUT1
         IFON  NEWUP,CHKUPA                               *LBD 09/84*
         MVC   OLINE,BLANKS                               *LBD 09/84*
NOTFND   EQU   *
         SETOF UT1CL,DOUT1
         IFON  ENDIN,ENDIT                                      *LBD*
         LA    R1,SYSUT1
         USING IHADCB,R1
         MVC   DCBDDNAM,=CL8'SYSUT1'
         DROP  R1
         B     INSERT
         EJECT
DONEWUP  DS    0H                                         *LBD 09/84*
         IFOFF UT1CL,CHKUPA                               *LBD 09/84*
         SETON NEWUP                                      *LBD 09/84*
         MVC   OLINE,BLANKS                               *LBD 09/84*
         CLC   RREC,BLANKS                                *LBD 09/84*
         BE    READUT1$                                   *LBD 09/84*
         PUT   SYSPRINT,RLINE                             *LBD 09/84*
         MVC   RLINE,BLANKS                               *LBD 09/84*
         B     READUT1$                                   *LBD 09/84*
CHKUP    EQU   *
         IFON  LISTALL,DONEWUP                            *LBD 09/84*
CHKUPA   DS    0H                                         *LBD 09/84*
         ZAP   INCTR,=P'00'             REINIT INSERT-REPLACE COUNTER
         SETOF NEWUP,PROCESS,INSW
         LR    R6,R5
         LA    R7,66                    -> END OF WHERE TO SCAN
FINDCHG  CLC   =C' CHANGE ',0(R6)
         BE    HAVECHG                  YES
         CLC   =C' ENDUP ',0(R6)
         BE    ENDUP
         LA    R6,1(R6)                 INCR ->
         BCT   R7,FINDCHG               KEEP LOOKING
         B     RSYSIN                   NOT CHANGE
         SPACE
SMPCC    EQU   *
         SETOF PROCESS,INSW
         B     RSYSIN
         SPACE
HAVECHG  LA    R6,8(R6)                 -> PAST CHANGE
FINDNAME CLC   =C'NAME=',0(R6)          FIND NAME
         BE    HAVENAME
         LA    R6,1(R6)
         BCT   R7,FINDNAME
         B     RSYSIN                   CAN'T FIND PROB INVALID CARD
         EJECT
HAVENAME EQU   *
         CLOSE SYSUT1
         MVC   MEMBER,BLANKS
         LA    R8,MEMBER
         LA    R6,5(R6)                 -> NAME
MOVENAME MVC   0(1,R8),0(R6)
         LA    R8,1(R8)
         LA    R6,1(R6)
         CLI   0(R6),C' '
         BE    ENDNAME
         CLI   0(R6),C','
         BNE   MOVENAME
ENDNAME  EQU   *
         LA    R9,LOGLEN(R9)       POINT TO NEXT ENTRY
         MVC   ELEMENT,MEMBER
         L     R1,ENTRY#
         LA    R1,1(R1)            INCR ENTRY # BY 1
         ST    R1,ENTRY#           AND SAVE IT
         USING IHADCB,R1
         LA    R1,SYSUT1
         MVC   DCBDDNAM,=CL8'SYSUT1'    REPLACE OUR DDNAME
         LA    R1,BLDLDD
         MVC   DCBDDNAM,=CL8'SYSUT1'    REPLACE OUR DDNAME
         MVC   BMEM,MEMBER
         OPEN  BLDLDD
         BLDL  BLDLDD,BLIST
         LR    R2,R15                   SAVE RETURN CODE
         CLOSE BLDLDD
         LTR   R2,R2
         BZ    USEUT1                   OK
         LA    R1,BLDLDD
         MVC   DCBDDNAM,=CL8'SYSUTA'    REPLACE OUR DDNAME
         OPEN  BLDLDD
         TESTOPEN BLDLDD,NODD
         BLDL  BLDLDD,BLIST
         LR    R2,R15                   SAVE RETURN CODE
         CLOSE BLDLDD
         LTR   R2,R2
         BZ    USEUTA                   OK
NOMEM    MVC   NOTFMSG(8),MEMBER
         TPT   NOTFMSG
         B     NOTFND
NODD     LA    R1,BLDLDD
         MVC   NODDMSG(8),DCBDDNAM
         CLC   DCBDDNAM,=CL8'SYSUT1'
         BE    NOUT1
         TPT   NODDMSG
         B     NOMEM
NOUT1    TPT   NOUT1MSG
         ABEND 1
         SPACE
USEUTA   LA    R1,SYSUT1
         MVC   DCBDDNAM,=CL8'SYSUTA'
         SPACE
USEUT1   RDJFCB SYSUT1
         OI    JFCBTSDM,X'08'           TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS          FLAG AS MEMBER OF PDS
         MVC   JFCBELNM,MEMBER
         OPEN  SYSUT1,TYPE=J
         SETON PROCESS,UT1CL
         MVC   OREC,0(R5)
         ZAP   LINECTR,=P'05'
         AP    PAGECTR,=P'01'
         UNPK  PAGE,PAGECTR
         OI    PAGE+3,X'F0'
         CLI   ELEMPAGE,X'00'
         BNE   NOEPAGE
         MVC   ELEMPAGE,PAGE       DO THIS ONCE PER ENTRY
NOEPAGE  DS    0H
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,T2
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,T3
         B     PUTIT
         EJECT
ENDUP    EQU   *
         SETON ENDIN                                            *LBD*
         IFOFF UT1CL,ENDIT                                      *LBD*
         B     RSYSUT1                                          *LBD*
ENDIT    PUT   SYSPRINT,ELINE
         PUT   SYSPRINT,ILINE1     PUT INDEX LINE 1
         L     R6,ENTRY#           LOAD NUMBER OF ENTRIES
DOSORT1  L     R9,LOGADDR          POINT TO LOG TABLE
         LR    R5,R6
DOSORT   DS    0H
         CLC   ELEMENT,ELEMENT2
         BNH   BUMPIT
         XC    ELEMENT(LOGLEN),ELEMENT2
         XC    ELEMENT2(LOGLEN),ELEMENT
         XC    ELEMENT(LOGLEN),ELEMENT2
BUMPIT   LA    R9,LOGLEN(R9)       NEXT ENTRY
         BCT   R5,DOSORT
         BCT   R6,DOSORT1
ENDSORT  DS    0H
         L     R9,LOGADDR
         L     R10,ENTRY#
         CLI   ELEMENT,X'00'       FIRST ENTRY NULL ?
         BNE   DOINDEX             NO
         LA    R9,LOGLEN(R9)       YES - SKIP IT
DOINDEX  MVC   IELEM,ELEMENT
         MVC   IPAGE,ELEMPAGE
         CLI   FLAG,X'00'
         BE    PINDEX
         CLI   FLAG,C'O'           OVERLAY ?
         BE    EOVER               YES
         CLI   FLAG,C'I'           INSERT ?
         BE    EINSERT             YES
PINDEX   PUT   SYSPRINT,ILINE
         LA    R9,LOGLEN(R9)
         MVC   STATUS,BLANKS
         CLI   ELEMENT,X'00'       NULL ENTRY
         BE    ENDALL
         BCT   R10,DOINDEX
         B     ENDALL
EOVER    MVC   STATUS,=CL30'OVERLAY'
         B     PINDEX
EINSERT  MVC   STATUS,=CL30'INSERT OR REPLACE'
         B     PINDEX
ENDALL   DS    0H
         CLOSE (SYSIN,,SYSUT1,,SYSPRINT)
         L     R1,LOGADDR
         L     R2,ENTLEN
         FREEMAIN R,LV=(R2),A=(R1)
         $EPILOG 0
         EJECT
         LTORG
         SPACE
         BITSW SBLK,DOUT1,MODID,NOSEP,INSW,UT1CL,PROCESS,LISTED
         BITSW LISTALL,ENDIN,REPSW,NEWUP
         EJECT
ENTRIES  EQU   10000
LOGADDR  DS    F                   STARTING ADDR OF LOG TABLE
ENTLEN   DC    A(ENTRIES*LOGLEN)   LEN OF ENTRYS
ENTRY#   DC    A(0)                NUMBER OF ENTRIES
         SPACE
NOTFMSG  DC    C'         NOT FOUND IN RIBUPDTE PROCESSING'
NODDMSG  DC    C'         DD NOT FOUND'
NOUT1MSG DC    C'*** NO SYSUT1 - RIBUPDTE TERMINATING ***'
LINECTR  DC    PL2'99'
LINEPPG  DC    PL2'54'
PAGECTR  DC    PL2'00'
INCTR    DC    PL2'0'
BLANKS   DC    CL121' '
RLINE    DC    CL121' '
         ORG   RLINE+3
RREC     DC    CL80' '
         DC    CL4' '
RST      DC    CL26' '
         ORG
OLINE    DC    CL121' '
         ORG   OLINE+3
OREC     DC    CL80' '
         DC    CL4' '
OST      DC    CL26' '
         ORG
         SPACE
INSERTM  DC    CL26'**           INSERTED **'
UMODM    DC    CL26'++   USER MOD OVERLAY ++'
REPLM    DC    CL26'**        REPLACEMENT **'
REPDM    DC    CL26'**           REPLACED **'
PRIORM   DC    CL26' '
         ORG   PRIORM
         DC    C'++ INSERT/REPL W/IN '
NEARCT   DC    CL3' '
         DC    C' ++'
         ORG
         SPACE
T3       DC    CL121' '
         ORG   T3+3
         DC    C'----×----1----×----2----×----3----×----4----×----5'
         DC    C'----×----6----×----7----×----8'
         DC    CL4' '
         DC    C'STATUS'
         ORG
         SPACE
ELINE    DC    CL121'-    RIBUPDTE PROCESSING COMPLETED '
T1       DC    CL121'1'
         ORG   T1+20
         DC    C'RIBUPDTE STATUS REPORT'
         DC    CL6' '
         DC    C'UPDATE MEMBER = '
MEMBER   DC    CL8' '
         ORG   T1+100
         DC    C'PAGE: '
PAGE     DC    CL4'0001'
         ORG
         SPACE
T2       DC    CL121'0'
         SPACE
BLIST    DC    AL2(1),AL2(76)
BMEM     DC    CL8' ',CL76' '
         SPACE
ILINE1   DC    CL121'1'
ILINE    DC    CL121' '
         ORG   ILINE+5
IELEM    DS    CL8
         DS    CL4
IPAGE    DS    CL4
         DS    CL4
STATUS   DS    CL30
         ORG
         SPACE
         PRINT NOGEN
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=121,RECFM=FBA
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=ENDUP,LRECL=80
SYSUT1   DCB   DSORG=PS,MACRF=GL,DDNAME=SYSUT1,EODAD=EOFUT1,EXLST=JFCB,X
               BUFL=19040,LRECL=80
BLDLDD   DCB   DSORG=PO,MACRF=R,DDNAME=DUMMY
         PRINT GEN
         EJECT
JFCB     DC    X'87',AL3(INFMJFCB)
         IEFJFCBN
         EJECT
         DCBD  DSORG=PS
         EJECT
LOG      DSECT
ELEMENT  DS    CL8                 SOURCE/MACRO NAME
ELEMPAGE DS    CL4                 PAGE NUMBER
FLAG     DS    X                   FLAG BYTE I=INSERT O=OVERLAY OR NULL
         DS    X                   RESERVED FOR FUTURE USE
LOGLEN   EQU   *-LOG               LENGTH OF LOG ENTRY
ELEMENT2 DS    CL8                 NEXT ELEMENT NAME
         END
