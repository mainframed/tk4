         TITLE 'MODIFICATION REPORT V4'
*---------------------------------------------------------------------*
*                                                                     *
* THIS PROGRAM WILL SCAN A SOURCE PDS AND WILL GENERATE 3 REPORTS:    *
*                                                                     *
* REPORT 1:   ALL USERMODS BY MODULE                                  *
*                                                                     *
* REPORT 2:   ALL USERMODS WITH MODULES AFFECTED                      *
*             (NOTE: THE FIRST 5 POSITIONS OF THE USERMOD ARE         *
*              USED FOR THIS REPORT)                                  *
*                                                                     *
* REPORT 3:   DOCUMENTATION REPORT LISTING ALL PROLOGS (OPTIONAL)     *
*                                                                     *
* JCL:        EXEC PGM=MODSREP,PARM='TITLE ID'                        *
*             SYSPRINT DD SYSOUT=A                                    *
*             PROLOG   DD SYSOUT=A                                    *
*             SOURCE   DD DSN=SOURCE PDS                              *
*             SORTIN   DD DSN=SEQ FILE (OPTIONAL)                     *
*                                                                     *
* IF DD SORTIN IS PRESENT THEN A THIRD REPORT IS GENERATED CONSISTING *
* OF ALL PROLOGS FOUND.  A PROLOG IS DETERMINED TO BE AN *@ OR .*@    *
* IN COLUMNS 1 THRU 2 OR 1 THRU 3 OF A SOURCE OR MACRO MEMBER.        *
* THIS IS ACCOMPLISHED BY INVOKING SORT INTERNALLY AND THEN           *
* PRODUCING THE THIRD REPORT ON DD PROLOG.                            *
*                                                                     *
* USERMODS ARE DETERMINED BY AN @ IN COLUMN 64 AND A VALID USERMOD    *
* CHARACTER IN COLUMN 65 (K THRU T EXCEPT O)                          *
*                                                                     *
* ALL SORT FILES ARE DYNAMICALLY ALLOCATED AND SORT IS THEN INVOKED   *
* VIA LINK.                                                           *
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
*        AUTHOR:                                                      *
*              LIONEL DYCK                                            *
*              ROCKWELL INTERNATIONAL                                 *
*              PO BOX 2515                                            *
*              SEAL BEACH, CALIF 90740                                *
*              (213) 594-1125                                         *
*---------------------------------------------------------------------*
         EJECT
MODSREP  $PROLOG R11,R12
         L     R1,0(R1)            * LOAD ADDR OF PARM
         LH    R2,0(R1)            * LOAD LENGTH OF PARM
         LTR   R2,R2               * LENGTH OF ZERO
         BZ    NOPARM              * NO - BYPASS MOVE
         C     R2,=F'20'                  GREATER THAN 20 ?
         BNH   BCTROK                     NO - ALL IS OK
         LA    R2,20                      NO SET TO 20
BCTROK   BCTR  R2,R0               * SUBTRACT LENGTH BY 1
         EX    R2,MOVEPARM         * EXECUTE THE MOVE
         B     NOPARM
MOVEPARM MVC   T1PARM(0),2(R1)
NOPARM   EQU   *
         EJECT
         L     R2,MODTSIZE
         GETMAIN R,LV=(R2)
         ST    R1,MODTAB
         USING MODTABLE,R8
         L     R2,DIRSIZE
         GETMAIN R,LV=(R2)
         ST    R1,DIRTAB
         AR    R1,R2
         S     R1,DIRENT                REDUCE BY 1
         ST    R1,DIRTEND
         EJECT
         OPEN  (SOURCE,,SYSPRINT,OUTPUT,SORTIN,OUTPUT)
         CLOSE SOURCE
         RDJFCB SOURCE
         LA    R9,PDSJFCB
         USING INFMJFCB,R9
         ZAP   LINECTR,=P'99'
         TM    JFCDSRG1,JFCORGPO      IT PO
         BO    READDIR
         ABEND 99
OPENPDS  DS    0H
         OPEN  SOURCE,TYPE=J
READ     GET   SOURCE
         LR    R5,R1               -> INPUT RECORD
         CLI   63(R5),C'@'         POSSIBLE USERMOD ?
         BNE   READ                NO SKIP THIS CARD
         CLC   64(2,R5),=C'OZ'     IBM APAR ID
         BE    READ
         CLI   64(R5),C'K'         VALID USERMOD ID ?
         BL    READ                NO SKIP IT
         CLI   64(R5),C'T'         VALID USERMOD ID ?
         BH    READ                NO SKIP IT
         TESTOPEN SORTIN,NOSORTP
         CLC   =C'*@',0(R5)        PROLOG ?
         BE    SORTP               YES PUT IT
         CLC   =C'.*@',0(R5)       MACRO PROLOG ?
         BNE   NOSORTP             NO
SORTP    MVC   SORTMEM,MEMBER
         MVC   SORTDATA,0(R5)
         PUT   SORTIN,SORTREC
NOSORTP  EQU   *
         CLC   64(7,R5),USERMOD    HAVE CURRENT ENTRY IN TABLE ?
         BE    UPMODT              YES
         L     R8,MODTAB
         L     R3,MODENTS          LOAD # OF SOURCE MODULES
FINDUMOD CLC   64(7,R5),USERMOD
         BE    UPMODT
         CLI   USERMOD,X'00'
         BE    NEWENT              NEW TABLE ENTRY
INCR     LA    R8,MODENTL(R8)
         BCT   R3,FINDUMOD
NEWENT   ZAP   COUNT,=P'00'
         ZAP   COUNTC,=P'00'
         MVC   MODULE,MEMBER
         MVC   USERMOD(7),64(R5)
UPMODT   CLC   MODULE,MEMBER
         BNE   INCR
         CLI   0(R5),C'*'              COMMENT
         BE    ADDC                    YES
         CLC   0(2,R5),=C'.*'          COMMENT
         BE    ADDC                    YES
         AP    COUNT,=P'01'
         B     READ
ADDC     AP    COUNTC,=P'01'
         B     READ
         ABEND 98
EOF      DS    0H
         CLOSE SOURCE
         B     READDIR
DOOP1    L     R8,MODTAB
         L     R6,MODENTS          LOAD # OF SOURCE MODULES
         SR    R9,R9
CNTENT   CLI   USERMOD,X'00'
         BE    SETCNT
         LA    R8,MODENTL(R8)
         LA    R9,1(R9)
         BCT   R6,CNTENT
SETCNT   EQU   *
         ST    R9,TOTENTS
         BCTR  R9,R0
DOSORT1  L     R8,MODTAB          -> TABLE
         LA    R6,MODENTL
         LR    R10,R9             COUNT OF ENTRIES TO SORT
SORTL1   DS    0H                  SORT IN MODULE ORDER
         CLC   MODULE,MODENTL+MODULE-MODTABLE(R8)
         BNH   BUMPIT1
         XC    USERMOD(MODENTL),MODENTL(R8)   SWAP
         XC    MODENTL(MODENTL,R8),USERMOD ENTRIES
         XC    USERMOD(MODENTL),MODENTL(R8)   QUICKLY
BUMPIT1  AR    R8,R6
         BCT   R10,SORTL1
         BCT   R9,DOSORT1
         L     R8,MODTAB           -> START OF TABLE
         L     R9,TOTENTS
OP1      CLC   OPMODS,MODULE
         BE    OP1A
         CLI   OPMODS,C' '
         BE    OP1N
         MVC   SUBTOT,=X'4020202020202021'
         ED    SUBTOT,SUBCTR
         MVC   SUBTOTC,=X'4020202020202021'
         ED    SUBTOTC,SUBCTRC
         MVC   SUBID,OPMODS
         LA    R2,SUBLINE
         BAL   R3,OUTROU
         LA    R2,BLANKS
         BAL   R3,OUTROU
         BAL   R3,OUTROU
OP1N     MVC   OPMODS,MODULE
         MVC   OPMOD1,MODULE
         ZAP   SUBCTR,=P'00'
         ZAP   SUBCTRC,=P'00'
         AP    MCTR,=P'01'         ADD 1 TO MODULE COUNTER
         B     OP1B
OP1A     MVC   OPMOD1,BLANKS
OP1B     MVC   OPUMOD1,USERMOD
         AP    SUBCTR,COUNT
         AP    TOTCTR,COUNT
         AP    SUBCTRC,COUNTC
         AP    TOTCTRC,COUNTC
         MVC   OPCNT1,=X'4020202020202021'
         ED    OPCNT1,COUNT
         MVC   OPCNT1C,=X'4020202020202021'
         ED    OPCNT1C,COUNTC
         LA    R2,OPLINE1
         BAL   R3,OUTROU
         LA    R8,MODENTL(R8)
         BCT   R9,OP1
         MVC   TREPTYPE,UMODTYPE
         MVC   SUBTOT,=X'4020202020202021'
         ED    SUBTOT,SUBCTR
         MVC   SUBTOTC,=X'4020202020202021'
         ED    SUBTOTC,SUBCTRC
         MVC   SUBID,OPMODS
         LA    R2,SUBLINE
         BAL   R3,OUTROU
         MVC   TOTAL,=XL10'40202020202020202021'
         ED    TOTAL,TOTCTR
         MVC   TOTALC,=XL10'40202020202020202021'
         ED    TOTALC,TOTCTRC
         LA    R2,TOTLINE
         BAL   R3,OUTROU
         ED    MTOT,MCTR
         LA    R2,MTOTLINE
         BAL   R3,OUTROU
         ZAP   LINECTR,=P'99'
         ZAP   SUBCTR,=P'00'
         ZAP   SUBCTRC,=P'00'
         ZAP   PWK,PAGECTR
         DP    PWK,=P'02'
         CP    PWK+2(2),=P'00'            EVEN OR ODD
         BE    NOSKIP1                    ODD SO NO SKIP
         MVI   BLANKS,C'1'
         PUT   SYSPRINT,BLANKS
         MVI   BLANKS,C' '
NOSKIP1  EQU   *                          NO EXTRA PAGE SKIP
         ZAP   PAGECTR,=P'00'
         L     R9,TOTENTS
DOSORT2  L     R8,MODTAB          -> TABLE
         LR    R10,R9             COUNT OF ENTRIES TO SORT
SORTL2   DS    0H                  SORT IN USERMOD ORDER
         CLC   USERMOD(5),MODENTL+USERMOD-MODTABLE(R8)
         BNH   BUMPIT2
         XC    USERMOD(MODENTL),MODENTL(R8)   SWAP
         XC    MODENTL(MODENTL,R8),USERMOD ENTRIES
         XC    USERMOD(MODENTL),MODENTL(R8)   QUICKLY
BUMPIT2  AR    R8,R6
         BCT   R10,SORTL2
         BCT   R9,DOSORT2
         L     R8,MODTAB           -> START OF TABLE
         L     R9,TOTENTS
         MVC   OPMODS,BLANKS
         LA    R8,MODENTL(R8)
OP2      CLC   OPMODS(5),USERMOD
         BE    OP2A
         CLI   OPMODS,C' '
         BE    OP2N
         MVC   SUBTOT,=X'4020202020202021'
         ED    SUBTOT,SUBCTR
         MVC   SUBTOTC,=X'4020202020202021'
         ED    SUBTOTC,SUBCTRC
         MVC   SUBID,OPMODS
         LA    R2,SUBLINE
         BAL   R3,OUTROU
         LA    R2,BLANKS
         BAL   R3,OUTROU
         BAL   R3,OUTROU
OP2N     MVC   OPMODS(5),USERMOD
         MVC   OPUMOD2,USERMOD
         ZAP   SUBCTR,=P'00'
         ZAP   SUBCTRC,=P'00'
         AP    SCTR,=P'01'         ADD 1 TO SMP USERMOD COUNTER
         B     OP2B
OP2A     MVC   OPUMOD2,BLANKS
OP2B     MVC   OPUMOD2A,USERMOD
         AP    SUBCTR,COUNT
         AP    SUBCTRC,COUNTC
         MVC   OPMOD2,MODULE
         MVC   OPCNT2,=X'4020202020202021'
         ED    OPCNT2,COUNT
         MVC   OPCNT2C,=X'4020202020202021'
         ED    OPCNT2C,COUNTC
         LA    R2,OPLINE2
         BAL   R3,OUTROU
         LA    R8,MODENTL(R8)
         BCT   R9,OP2
         MVC   SUBTOT,=X'4020202020202021'
         ED    SUBTOT,SUBCTR
         MVC   SUBTOTC,=X'4020202020202021'
         ED    SUBTOTC,SUBCTRC
         MVC   SUBID,OPMODS
         LA    R2,SUBLINE
         BAL   R3,OUTROU
         LA    R2,BLANKS
         BAL   R3,OUTROU
         LA    R2,TOTLINE
         BAL   R3,OUTROU
         ED    STOT,SCTR
         LA    R2,STOTLINE
         BAL   R3,OUTROU
         TESTOPEN SORTIN,EXITIT
         CLOSE (SORTIN,,SYSPRINT)
         FREE  DDN='SYSIN'
         FREE  DDN='SORTOUT'
         FREE  DDN='SORTWK01'
         FREE  DDN='SORTWK02'
         FREE  DDN='SORTWK03'
         FREE  DDN='SORTLIB'
         ALLOC DDN='SYSIN',UNIT='SYSVIO',SPACE=(TRK,(1)),DISP=NEW
         ALLOC DDN='SORTLIB',DSN='SYS1.SORTLIB',DISP=SHR
         ALLOC DDN='SORTWK01',UNIT='SYSDA',SPACE=(CYL,(25)),DISP=NEW
         ALLOC DDN='SORTWK02',UNIT='SYSDA',SPACE=(CYL,(25)),DISP=NEW
         ALLOC DDN='SORTWK03',UNIT='SYSDA',SPACE=(CYL,(25)),DISP=NEW
         ALLOC DDN='SORTOUT',UNIT='SYSDA',SPACE=(CYL,(15,5),RLSE)
         OPEN  (SYSIN,OUTPUT,SORTOUT,OUTPUT)
         PUT   SYSIN,SORTCC1
         PUT   SYSIN,SORTCC2
         CLOSE (SYSIN,,SORTOUT)
         LA    R1,DUMMYPRM         -> DUMMY PARM FOR SORT
         LINK  EP=SORT
         EJECT                                            *LBD 01/85*
         MVC   SYSPRINT+40(8),=CL8'PROLOG'                *LBD 01/85*
         SPACE 1                                          *LBD 01/85*
         OPEN  (SYSPRINT,OUTPUT)                          *LBD 01/85*
         ZAP   PWK,PAGECTR
         DP    PWK,=P'02'
         CP    PWK+2(2),=P'00'
         BE    NOSKIP2
         MVI   BLANKS,C'1'
         PUT   SYSPRINT,BLANKS
         MVI   BLANKS,C' '
NOSKIP2  EQU   *
         ZAP   PAGECTR,=P'00'
         MVC   T1TIT(L'TSORT),TSORT
         OPEN  SORTOUT
RSORT    GET   SORTOUT
         CLC   SMOD(6),72(R1)             SAME MOD ?
         BE    MVCOPS                     YES
         MVC   SMOD,72(R1)                RESET SAVE MOD
         MVC   T1+2(7),SMOD               SET UP TITLE
         MVC   T1TIT(8),0(R1)             SET UP TITLE
         ZAP   LINECTR,=P'99'             NO - SET HIGH LINE COUNT
MVCOPS   MVC   OPSDATA,8(R1)
         LA    R2,OPSLINE
         BAL   R3,OUTROU
         B     RSORT
ENDSORT  CLOSE (SORTOUT)
EXITIT   DS    0H
         CLOSE SYSPRINT
         $EPILOG
READDIR  IFON  HAVEPDS,GIVENEXT
         SETON HAVEPDS
         L     R10,DIRTAB
CLRDIR   XC    0(8,R10),0(R10)     CLEAR DIRECTORY TABLE
         A     R10,DIRENT
         C     R10,DIRTEND
         BNH   CLRDIR
         L     R10,DIRTAB          RESET ->
         OPEN  PDS
RPDS     GET   PDS
         LR    R2,R1            SAVE INPUT ->
         USING DIR,R2           SET UP ADDRESSABILITY
         LH    R8,0(R2)
         AR    R8,R2            -> END OF DIR BLOCK
PLPS     LA    R2,2(R2)         -> PAST LEN
LPS      CLC   NAME,=8X'FF'
         BE    EXIT
         MVC   0(8,R10),NAME
         SR    R1,R1            CLEAR REG 1
         IC    R1,FLAG
         N     R1,=X'0000001F'          TURN OFF MISC BITS
         LA    R1,6(R1)         ADD FIXED # OF HALF WORDS
         SLA   R1,1             MULT BY 2 FOR # OF BYTES OF DATA
         AR    R2,R1
         CR    R2,R8
         BL    NEXTDIR
         SETON GORPDS
NEXTDIR  A     R10,DIRENT               -> NEXT ENTRY
         C     R10,DIRTEND               AT END OF DIRECTORY TABLE ?
         BNH   LPS1                     NO - KEEP LOOKING
         ABEND 2000                     NOT ENOUGH ENTRIES
EXIT     CLOSE PDS
         L     R10,DIRTAB
         MVC   MEMBER(8),0(R10)         MOVE 1ST MEMBER NAME
         B     RETURN                   AND RETURN
LPS1     DS    0H
         IFOFF GORPDS,LPS
         SETOF GORPDS
         B     RPDS
         EJECT
GIVENEXT DS    0H                       PASS THE NEXT DIR ENTRY
         L     R10,SAVEREGA
         A     R10,DIRENT               -> NEXT ENTRY
         C     R10,DIRTEND               AT END OF DIRECTORY ?
         BE    DOOP1
         CLC   =H'00',0(R10)            NULL ENTRY
         BE    DOOP1
         MVC   MEMBER,0(R10)            MOVE MEMBER NAME
RETURN   DS    0H
         ST    R10,SAVEREGA
         LA    R9,PDSJFCB
         MVC   JFCBELNM,MEMBER
         OI    JFCBTSDM,X'08'      TURN ON DO NOT WRITE BACK FLAG
         OI    JFCBIND1,JFCPDS
         L     R8,MODTAB
         B     OPENPDS
         EJECT
OUTROU   DS    0H
         AP    LINECTR,=P'01'
         CP    LINECTR,LINEPPG
         BH    NEWPAGE
         PUT   SYSPRINT,0(R2)
         BR    R3
NEWPAGE  DS    0H
         ZAP   LINECTR,=P'04'
         AP    PAGECTR,=P'01'
         UNPK  PAGE,PAGECTR
         OI    PAGE+3,X'F0'
         PUT   SYSPRINT,T1
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,0(R2)
         BR    R3
         LTORG
         DYNSPACE
         SPACE 2
         BITSW GORPDS,HAVEPDS
         SPACE 2
         PRINT NOGEN
SOURCE   DCB   DSORG=PS,MACRF=GL,DDNAME=SOURCE,EODAD=EOF,EXLST=JFCB,   X
               LRECL=80
         SPACE
PDS      DCB   DSORG=PS,MACRF=GL,DDNAME=SOURCE,LRECL=256,RECFM=F,      *
               BLKSIZE=256
         SPACE
SORTIN   DCB   DSORG=PS,MACRF=PM,DDNAME=SORTIN,LRECL=88,RECFM=FB,      *
               BLKSIZE=6160
         SPACE
SYSIN    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSIN,BLKSIZE=80,RECFM=F
         SPACE
SORTOUT  DCB   DSORG=PS,MACRF=(GL,PM),DDNAME=SORTOUT,EODAD=ENDSORT,    *
               LRECL=88,BLKSIZE=6160,RECFM=FB
         SPACE
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=121
         PRINT GEN
         EJECT
JFCB     DC    X'87',AL3(PDSJFCB)
PDSJFCB DC     CL176' '
         SPACE
SORTCC1  DC    CL80' SORT FIELDS=(72,7,CH,A,80,8,CH,A) '
SORTCC2  DC    CL80' END '
         SPACE
TOTENTS  DS    F
         SPACE
         DS    0F                                         *LBD 01/85*
DUMMYPRM DC    X'80',AL3(DPARM)
DPARM    DC    A(0)
         SPACE
SMOD     DC    CL7' '                     SAVE MODID
PWK      DC    PL4'0'
         SPACE
SAVEREGA DS    F
LINECTR  DC    PL2'99'
PAGECTR  DC    PL2'00'
LINEPPG  DC    PL2'54'
SUBCTR   DC    PL4'0'
TOTCTR   DC    PL5'0'
SUBCTRC  DC    PL4'0'
TOTCTRC  DC    PL5'0'
MCTR     DC    PL5'0'
SCTR     DC    PL5'0'
         SPACE
MODENT   EQU   5000                MAX # OF SOURCE MODULES FOR TABLE
MODENTS  DC    A(MODENT)
MODTSIZE DC    A(MODENT*MODENTL)
MODTAB   DC    A(0)                START OF MODULE NAME TABLE
         SPACE
DIRSIZE  DC    A(2000*8)                2000 DIRECTORY ENTRIES MAX
DIRTAB   DC    A(0)                     START OF DIRECTORY TABLE
DIRENT   DC    A(8)                     LEN OF ENTRY
DIRTEND  DC    A(0)                     END OF DIR TABLE-1 ENT
         SPACE
BLANKS   DC    CL121' '
         SPACE
OPMODS   DC    CL8' '              SAVE AREA FOR PRINT MODULE
         SPACE
OPLINE1  DC    CL121' '
         ORG   OPLINE1+1
OPMOD1   DS    CL8
         DC    CL5' '
OPUMOD1  DC    CL7' '
         DC    CL4' '
OPCNT1   DC    CL8' '
         DC    CL4' '
OPCNT1C  DC    CL8' '
         ORG
OPLINE2  DC    CL121' '
         ORG   OPLINE2+1
OPUMOD2  DC    CL5' '
         DC    CL6' '
OPUMOD2A DC    CL7' '
         DC    CL6' '
OPMOD2   DS    CL8
         DC    CL3' '
OPCNT2   DC    CL8' '
         DC    CL3' '
OPCNT2C  DC    CL8' '
         ORG
OPSLINE  DC    CL121' '
         ORG   OPSLINE+10
OPSDATA  DC    CL72' '
         ORG
SUBLINE  DC    CL121' '
         ORG   SUBLINE+12
         DC    C'ELEMENT '
SUBID    DC    CL8' '
         DC    C' TOTAL LINES OF MODIFICATION =   '
SUBTOT   DC    CL8' '
         DC    C'  COMMENTS = '
SUBTOTC  DC    CL8' '
         ORG
TOTLINE  DC    CL121'0'
         ORG   TOTLINE+12
         DC    C'TOTAL NUMBER OF MODIFICATION RECORDS FOUND   = '
TOTAL    DC    XL10'40202020202020202021'
         DC    C'  COMMENTS = '
TOTALC   DC    XL10'40202020202020202021'
         ORG
MTOTLINE DC    CL121'0'
         ORG   MTOTLINE+12
         DC    C'TOTAL NUMBER OF UPDATED ELEMENTS             = '
MTOT     DC    XL10'40202020202020202021'
         ORG
STOTLINE DC    CL121'0'
         ORG   STOTLINE+12
         DC    C'TOTAL NUMBER OF USER MODIFICATIONS           = '
STOT     DC    XL10'40202020202020202021'
         ORG
T1       DC    CL121'1'
         ORG   T1+11
T1TIT    DC    C'MODIFICATION REPORT '
TREPTYPE DC    C'MODULE ORDER      '
         DC    CL2' '                     FILLER
T1PARM   DC    CL20' '
         DC    C'PAGE '
PAGE     DC    CL4' '
         ORG
TSORT    DC    C'MODULE   DOCUMENTATION  PROLOG REPORT'
UMODTYPE DC    C'USERMOD ORDER     '
MEMBER   DC    CL8' '
         DC    C' '
SORTREC  DC    CL88' '
         ORG   SORTREC
SORTMEM  DC    CL8' '
SORTDATA DC    CL80' '
         ORG
         EJECT
         DSECT
         IEFJFCBN LIST=YES
         EJECT
         DSECT
MODTABLE DS    0H
USERMOD  DS    CL8                 USERMOD ID
MODULE   DS    CL8                 MODULE NAME
COUNT    DS    CL4                 CARD COUNT
COUNTC   DS    CL4                 COMMENT CARD COUNT
MODENTL  EQU   *-MODTABLE         SIZE OF ENTRY
         DSECT
DIR      DS    0C
NAME     DS    CL8
TTR      DS    CL3
FLAG     DS    X
ALIAS    EQU   128
         EJECT
         IEFZB4D0
         IEFZB4D2
         END
