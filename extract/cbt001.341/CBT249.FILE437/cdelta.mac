         TITLE 'SMP DELTA DECK GENERATOR PROGRAM 02-17-83'
*---------------------------------------------------------------------*
*        MODULE: CDELTA                                               *
*---------------------------------------------------------------------*
*        FUNCTION:                                                    *
*              THIS PROGRAM WILL GENERATE AN IEBUPDTE DECK WITH SMP   *
*              CONTROL CARDS (OPTIONAL) BY COMPARING 2 SOURCE DECKS.  *
*              ONLY THE REPORT IS PRODUCED IF SMP=N IS SPECIFIED.     *
*---------------------------------------------------------------------*
*        FLOW:                                                        *
*              1) ACQUIRE PARM FROM ISPF VARIABLES                    *
*              2) INVOKE SMPDATA TO GET CDS INFORMATION.              *
*              3) READ NEWSRC FILE AND COMPARE WITH OLDSRC.           *
*              4) ALL NEW RECORDS GENERATE A RECORD IN DELTA CARD.    *
*              5) ALL MISSING RECORDS GENERATE A MESSAGE IN THE       *
*                 REPORT AND WILL     GENERATE A DELTA DECK CARD.     *
*              6) A CHECK IS MADE FOR AT LEAST 1 VALID PROLOG         *
*                 DOCUMENTATION RECORD WHICH IS IDENTIFIED BY         *
*                 EITHER *@ IN COLS 1-2 OR .*@ IN COLS 1-3 FOR        *
*                 MACROS.                                             *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
*        JCL: NOT APPLICABLE - ISPF INVOKATION ONLY                   *
*---------------------------------------------------------------------*
*    TSO ISPF INVOKATION:                                             *
*                                                                     *
*              ALLOC F(REPORT) DS(*)                                  *
*              ALLOC F(DELTA) DS(OUTPUT FILE FOR DELTA DECK)          *
*              ALLOC F(OLDSRC)  DS(ORIGINAL SOURCE/MACRO)             *
*              ALLOC F(NEWSRC)  DS(NEW/UPDATED SOURCE/MACRO)          *
*              ALLOC F(SMPCDS)  DS(SMP CDS)                           *
*              ISPEXEC SELECT PGM(CDELTA)                             *
*---------------------------------------------------------------------*
*   SPF PARAMETERS:                                                   *
*                                                                     *
*          *   MEMBER   ELEMENT NAME (I.E. MACRO NAME OR SRC NAME)    *
*                                                                     *
*          *   MODID    SMP USERMOD ID (I.E. TT00011)                 *
*                                                                     *
*              PREREQ   PRE REQUISITES PTF                            *
*                                                                     *
*              FMID     SMP FMID                                      *
*                                                                     *
*              SYSLIB   SMP SYSLIB                                    *
*                                                                     *
*              DISTLIB  SMP DLIB                                      *
*                                                                     *
*              NEW      NEW MODULE OR MACRO (NOT IN SMPCDS YET)       *
*                                                                     *
*              MERGE    MERGE MULTIPLE NEW ELEMENTS (REQUIRES NEW)    *
*                                                                     *
*              COREQ    CO-REQUISITES PTF                             *
*                                                                     *
*              SRCMAC   SRC OR MAC  (DEF.= SRC)                       *
*                                                                     *
*              DELFULL  COMPLETE DELTA OR JUST FOR MODID              *
*                       (SMP = Y ONLY)                                *
*                                                                     *
*              SMP      Y - GENERATE SMP CONTROL CARDS (DEFAULT)      *
*                       N - NO DECK - REPORT ONLY                     *
*                                                                     *
*              ADEL     Y - ALLOW RECORDS TO BE DELETED               *
*                       N - DO NOT ALLOW DELETED RECORDS (NO COMMENT  *
*                           (DEFAULT=N)                   FOR DELETE  *
*                           EXCEPT FOR USERMOD RECORDS ×  IS GENERATED*
*                                                                     *
*        UM1-UMG ASSEMBLY MODULE NAMES FOR MACRO UPDATE               *
*                                                                     *
*                                                                     *
*                                                                     *
*        * = REQUIRED                                                 *
*---------------------------------------------------------------------*
         EJECT
*        UNIQUE MACROS FOR CDELTA
         SPACE 1
         MACRO
&L       $VGETA &ID
&L       $VGET &ID
         CLI   0(R1),C' '          ANY ASSEM NAMES ?
         BE    NOASSEME            NO
         MVC   ASSNAMES,0(R1)
         PUT   DELTA,ASSEM
         MEND
         SPACE 2
         MACRO
&L       $VGETB &ID
&L       $VGET &ID
         CLI   0(R1),C' '          ANY ASSEM NAMES ?
         BE    NASSEME             NO
         MVC   ASSNAMES,0(R1)
         PUT   DELTA,ASSEM
         MEND
         EJECT
CDELTA   $PROLOG R10,R11,R12
         $VGET ADEL                     -> DEL
         CLI   0(R1),C'Y'               ALLOW DELETE
         BE    CKSMP
         CLI   0(R1),C'N'               DIS-ALLOW DELETE
         BE    SETDELF
         B     CKSMP
SETDELF  SETON DEL                      ALLOW DELETES
         SPACE
CKSMP    $VGET SMP                      -> SMP
         CLI   0(R1),C'Y'               CREATE SMP (DEFAULT)
         BE    SETSMPF
         CLI   0(R1),C'N'               NO SMP CREATION
         BE    CKTYPE
SETSMPF  SETON SMP
         EJECT
CKTYPE   $VGET SRCMAC                   -> TYPE=
         MVC   SMPTYPE,0(R1)
         EJECT
         $VGET FMID                     -> FMID=
         MVC   SMPFMID,0(R1)
         EJECT
         $VGET NEW              -> NEW=Y OR ANYTHING FOR NO
         CLI   0(R1),C'Y'
         BNE   CKPRE
         SETON NEW
         EJECT
CKPRE    $VGET PREREQ           -> PRE=PRE REQ PTF
         MVC   PREPTF,0(R1)             MAX 40 CHARS (5 SYSMOD ID'S)
         EJECT
         $VGET SYSLIB           -> SYSLIB=SYSLIB
         MVC   SYSLIB$,0(R1)
         EJECT
         $VGET DISTLIB          -> DISTLIB=DISTLIB
         MVC   DISTLIB$,0(R1)
         EJECT
         $VGET COREQ            -> REQ=REQ PTF
         MVC   REQPTF,0(R1)             MAX 5 SYSMOD ID'S
         EJECT
         $VGET MEMBER           -> MEMBER=NAME
         MVC   NAME,0(R1)
         EJECT
         $VGET MODID            -> MOD ID
         MVC   MOD#,0(R1)
         EJECT
         $VGET MERGE
         CLI   0(R1),C'Y'          MERGE REQUESTED
         BNE   CKDEL
         SETON MERGE
         IFON  NEW,CKDEL
         TPUT  INVMERGE,L'INVMERGE
         LA    R15,4
         B     EPILOG
         EJECT
CKDEL    $VGET DELFULL
         CLI   0(R1),C'F'               FULL DELTA DECK ?
         BE    SETFULL
         B     ENDVGET                  LEAVE OFF - MODID ONLY
SETFULL  SETON DELFULL
         SPACE 2
ENDVGET  EQU   *                        END OF VGET'S OF ISPF VARIABLES
         EJECT
START    EQU   *
*-------------  INVOKE SMPDATA FOR CDS INFORMATION ----------------
         SPACE 2
OPEND    OPEN  (DELTA,OUTPUT)
         IFOFF MERGE,MNAME
         OPEN  (DELTAIN)
RDEL     GET   DELTAIN
         LR    R5,R1
         PUT   DELTA,0(R5)
         B     RDEL
EODDEL   CLOSE DELTAIN
         B     MNAME
MNAME    MVC   SMPELEM,NAME
         MVC   SMPMODID,MOD#
         MVC   FUNCTION(3),SMPTYPE
         MVC   MEMBER,NAME
         IFOFF SMP,OPENIT               NO SMP FLAG
         IFON  NEW,OPENIT               NEW - SKIP SMP
         L     R1,PARMPTR
         LINK  EP=SMPDATA
         LTR   R15,R15
         BZ    PROCESS
         TPUT  INVALIDM,INVALIDL
         SETON NEW
PROCESS  EQU   *
         L     R5,DADDR
         LA    R6,80
         LR    R7,R5
         A     R7,DATALEN
         SR    R7,R6
SMPLP    EQU   *
         CLI   0(R5),X'00'
         BE    EOF$
         CLC   =C'FMID',0(R5)
         BE    DOFMID$
         CLC   =C'RMID',0(R5)
         BE    DORMID
         CLC   =C'UMID',0(R5)
         BE    DOUMID
         CLC   =C'SYSLIB',0(R5)
         BE    DOSLIB
         CLC   =C'DISTLIB',0(R5)
         BE    DODLIB
SMPINCR  BXLE  R5,R6,SMPLP
         ABEND 11
DODLIB   MVC   DISTLIB,10(R5)
         B     SMPINCR
DOSLIB   MVC   SYSLIB,10(R5)
         B     SMPINCR
DOFMID$  MVC   SMPFMID,10(R5)           MOVE FMID TO ++VER
         SETON LOST                     LOST = HAVE SMP INFO
         PUT   DELTA,USERMOD
         PUT   DELTA,VER1
         CLI   REQPTF,C' '
         BE    CKPREPTF
         PUT   DELTA,SMPREQ
CKPREPTF CLI   PREPTF,C' '              ANY PRE SPECIFIED
         BNE   DOPRE$
         B     SMPINCR
DOPRE$   MVC   PRE1(40),PREPTF
         PUT   DELTA,VERP1
         SETON $PRE1
         B     SMPINCR
DORMID   EQU   *
         CLC   SMPFMID,10(R5)           FMID = RMID ?
         BE    SMPINCR                  YES - IGNORE IT
         MVC   PRE1,10(R5)
         PUT   DELTA,VERP1
         SETON $PRE1
         B     SMPINCR
DOUMID   EQU   *
         IFON  $PRE1,UMIDLP
         MVC   PRE1,10(R5)
         PUT   DELTA,VERP1
         SETON $PRE1
         B     SMPINCR
UMIDLP   MVC   PRE2,10(R5)
         PUT   DELTA,VERP2
         B     SMPINCR
EOF$     EQU   *
         IFOFF LOST,CLOSE$
         IFON  $PRE1,PUV
         MVI   VERPEP,C' '              ERASE PAREN AS NOT NEEDED
         CLI   PRE1,C' '
         BE    PLCC
PUV      PUT   DELTA,VERPE
PLCC     PUT   DELTA,LASTCC                                     *LBD*
         PUT   DELTA,UPDTE
         CLC   =C'MAC',SMPTYPE                                *RI*
         BNE   NOASSEM                                        *RI*
         $VGET UM1
         CLI   0(R1),C' '          ANY ASSEM NAMES ?
         BE    NOASSEM             NO
         MVC   ASSNAMES,0(R1)
         PUT   DELTA,ASSEMCC
         PUT   DELTA,ASSEM
         $VGETA UM2
         $VGETA UM3
         $VGETA UM4
         $VGETA UM5
         $VGETA UM6
         $VGETA UM7
         $VGETA UM8
         $VGETA UM9
         $VGETA UMA
         $VGETA UMB
         $VGETA UMC
         $VGETA UMD
         $VGETA UME
         $VGETA UMF
         $VGETA UMG
NOASSEME PUT   DELTA,ASSEMCCE
NOASSEM  EQU   *
         CLI   DISTLIB,C' '
         BE    PUPDTEE
         PUT   DELTA,UPDTED
         CLI   SYSLIB,C' '
         BE    PUPDTEE
         PUT   DELTA,UPDTES
PUPDTEE  PUT   DELTA,UPDTEE
CLOSE$   SETOF LOST
         B     OPENIT
OUTERR   S99FAIL
         ABEND 100
LOGERR   S99FAIL
         ABEND 200
CNTLERR  S99FAIL
         ABEND 300
         EJECT
OPENIT   OPEN  (NEWSRC,,REPORT,OUTPUT)
         IFON  NEW,NEWSMP
         OPEN  (OLDSRC)
         B     DOPUT
NEWSMP   EQU   *
         IFOFF SMP,DOPUT
         MVC   SMPTU(3),BLANKS
         IFON  MERGE,CKNEWU
         PUT   DELTA,USERMOD
         PUT   DELTA,VER1
         CLI   PREPTF,C' '
         BE    $CKREQ
         MVC   PRE1,PREPTF
         PUT   DELTA,VERP1
$CKREQ   EQU   *
         CLI   PRE1,C' '
         BNE   $PVPE
         MVI   VERPEP,C' '              ERASE PAREN AS NOT NEEDED
         CLI   PRE1,C' '                                        *LBD*
         BE    NOPPE                                            *LBD*
$PVPE    PUT   DELTA,VERPE
NOPPE    CLI   REQPTF,C' '
         BNE   $PTREQ
         B     $PULASTC                                         *LBD*
$PTREQ   PUT   DELTA,SMPREQ
$PULASTC PUT   DELTA,LASTCC                                     *LBD*
CKNEWU   IFOFF NEW,PUPDTE
         CLC   =C'SRC ',SMPTYPE
         BNE   PUPDTE
         OPEN  SMPJCLIN
         PUT   DELTA,JCLIN
RJCLIN   GET  SMPJCLIN
         LR    R5,R1
         PUT   DELTA,0(R5)
         B     RJCLIN
EJCLIN   CLOSE SMPJCLIN
PUPDTE   PUT   DELTA,UPDTE
         $VGET UM1
         CLI   0(R1),C' '          ANY ASSEM NAMES ?
         BE    NASSEM              NO
         MVC   ASSNAMES,0(R1)
         PUT   DELTA,ASSEMCC
         PUT   DELTA,ASSEM
         $VGETB UM2
         $VGETB UM3
         $VGETB UM4
         $VGETB UM5
         $VGETB UM6
         $VGETB UM7
         $VGETB UM8
         $VGETB UM9
         $VGETB UMA
         $VGETB UMB
         $VGETB UMC
         $VGETB UMD
         $VGETB UME
         $VGETB UMF
         $VGETB UMG
NASSEME  PUT   DELTA,ASSEMCCE
NASSEM   EQU   *
         MVC   DISTLIB,DISTLIB$
         MVC   SYSLIB,SYSLIB$
         PUT   DELTA,UPDTES
         PUT   DELTA,UPDTED
         PUT   DELTA,UPDTEE
         B     NODOPUT             IEBUP CC NOT NEEDED HERE
DOPUT    PUT   DELTA,CHANGE
NODOPUT  MVC   HELEM,NAME
         MVC   HMODID,MOD#
         MVC   DELRCDMD,MOD#
         PUT   REPORT,HEADER
         B     READ2
CLC      CLC   72(8,R3),72(R4)
         BE    CKDATA
         BL    NEWREC
         BH    LOSTREC
         ABEND 1                NEVER SHOULD GET HERE
CKDATA   EQU   *
         CLC   0(72,R3),0(R4)   DATA IDENTICAL
         BE    READ2
         CLI   64(R4),C'K'        USERMOD ID ?
         BL    SOR
         CLI   64(R4),C'T'         VALID USERMOD ID ?
         BL    MVCRX               OK TO REPLACE
SOR      SETON REPLACE
         IFOFF DEL,MVCRX
         SETON LOST
MVCRX    MVC   RCD+1(80),0(R3)
         MVC   MSG,INVMSG
         CLC   64(7,R3),MOD#
         BE    PRCD
         MVC   MSG,BADREPID
PRCD     EQU   *
         CLC   0(2,R3),=C'*@'           ANY PROLOG DOC ?
         BE    SETP1                    YES
         CLC   0(2,R3),=C'.*@'          ANY PROLOG DOC ?
         BNE   DP                       NO
SETP1    SETON PROLOG                   YES
DP       DS    0H
         IFON  DELFULL,DPP
         CLC   64(7,R3),MOD#       OUR MODID
         BNE   DPB
DPP      EQU   *
         IFOFF DEL,DPPP
         IFON  REPLACE,DPB
DPPP     PUT   DELTA,0(R3)              PUT INTO DELTA DECK
DPB      MVC   ORCD+1(80),0(R4)
         SETOF REPLACE
         MVC   OMSG,ORGMSG
         PUT   REPORT,BLANKS
         PUT   REPORT,ORCD
         PUT   REPORT,RCD       TELL SOMEONE
READ2    GET   NEWSRC
         LR    R3,R1
         IFON  NEW,NEWREC
         GET   OLDSRC
         LR    R4,R1
         B     CLC
NEWREC   EQU   *
         CLC   0(2,R3),=C'.*@'          ANY PROLOG DOC ?
         BE    SETP2                    YES
         CLC   0(2,R3),=C'*@'           ANY PROLOG DOC ?
         BNE   DNMVC                    NO
SETP2    SETON PROLOG                   YES
DNMVC    MVC   RCD+1(80),0(R3)
         MVC   MSG,NEWMSG
         IFON  DELFULL,PD1
         CLC   64(7,R3),MOD#
         BNE   BPD1
PD1      PUT   DELTA,0(R3)
         CLC   64(7,R3),MOD#
         BE    PUTNEWR
BPD1     MVC   RCD+1(80),0(R3)
         MVC   MSG,BADID
PUTNEWR  PUT   REPORT,RCD
GETMOD   GET   NEWSRC
         LR    R3,R1
         IFON  NEW,NEWREC
         B     CLC
LOSTREC  EQU   *
         SETON LOST
         MVC   RCD+1(80),0(R4)
         MVC   MSG,LOSTMSG
         PUT   REPORT,RCD
         MVC   DELRCD#(8),72(R4)        MOVE SEQ #
         IFON  DEL,NODRCD
         PUT   DELTA,DELRCD             GENERATE DELETE RCD
         MVC   RCD+1(80),DELRCD
         MVC   MSG,INVMSG
         PUT   REPORT,RCD
NODRCD   EQU   *                        NO DELETE RCD GENERATED
         GET   OLDSRC
         LR    R4,R1
         B     CLC
EOF      EQU   *
         IFON  NEW,DOCLOSE
         SETON BEOFM
         IFON  BEOFO,CLOSE
GEOFOLP  GET   OLDSRC
         MVC   RCD+1(80),0(R1)
         SETON LOST
         MVC   MSG,LOSTMSG
         PUT   REPORT,RCD
         B     GEOFOLP
EOFO     EQU   *
         SETON BEOFO
         IFON  BEOFM,CLOSE
         MVC   MSG,NEWMSG
GEOFMLP  GET   NEWSRC
         LR    R3,R1
         IFON  DELFULL,DP2
         CLC   64(7,R3),MOD#
         BNE   GEOFMLP
DP2      PUT   DELTA,0(R3)
         MVC   RCD+1(80),0(R3)
         B     GEOFMLP
CLOSE    EQU   *
         IFON  PROLOG,CKLST
         PUT   REPORT,PROLL
         TPUT  PROLL+1,72
CKLST    IFOFF LOST,DOCLOSE
         IFOFF DEL,DOCLOSE
         PUT   REPORT,LOSTL
         TPUT  LOSTL+1,72
DOCLOSE  CLOSE (OLDSRC,FREE,NEWSRC,FREE,DELTA,FREE,REPORT,FREE)
EPILOG   EQU   *
         $EPILOG
         EJECT
         BITSW $PRE1,SMP,LOST,BEOFM,BEOFO,DEL,PROLOG,NEW
         BITSW DELFULL,REPLACE,MERGE
         EJECT
         LTORG
         EJECT
         VGETSUB
         EJECT
PREPTF   DC    CL40' '
SYSLIB$  DC    CL8' '
DISTLIB$ DC    CL8' '
SMPREQ   DC    CL80' '
         ORG   SMPREQ+5
         DC    C'REQ('
REQPTF   DC    CL40' ',C') /* CO-REQ SYSMOD */'
         ORG
         EJECT
CHANGE   DC    C'./ CHANGE NAME='
NAME     DC    CL8' '
         DC    CL60' '
MOD#     DC    CL8' '
         SPACE
LOSTL    DC    CL121'0  ***   WARNING ORIGINAL RECORDS WERE DELETED OR X
               REPLACED ***'
PROLL    DC    CL121'0  ***   WARNING NO PROLOG FOUND  !!!!!!!!!!!!!  '
         SPACE
HEADER   DC    CL121'1 '
         ORG   HEADER+10
         DC    C'SMP DELTA DECK GENERATOR   ELEMENT: '
HELEM    DC    CL8' '
         DC    CL4' ',C'USERMOD ID: '
HMODID   DC    CL8' '
         ORG
BLANKS   DC    CL121' '
         SPACE
RCD      DC    CL121' '
         ORG   RCD+85
MSG      DC    CL35' '
         ORG
ORCD     DC    CL121' '
         ORG   ORCD+85
OMSG     DC    CL35' '
         ORG
DELRCD   DC    CL72'*'
DELRCD#  DC    CL8' '
         ORG   DELRCD+30
         DC    C'THIS CARD DELETED BY MOD '
         ORG   DELRCD+63
         DC    C'@'
DELRCDMD DC    CL8' '
         ORG
         SPACE 2
INVALIDM DC    C'** ELEMENT SPECIFIED NOT FOUND IN CDS  -  PROCESSING '
         DC    C'CONTINUES **'
INVALIDL EQU   *-INVALIDM
         SPACE 4
INVMERGE DC    C'*** MERGE INVALID UNLESS NEW SPECIFIED ***'
         SPACE 4
LOSTMSG  DC    CL35'***  NOT IN NEW CODE         ***'
NEWMSG   DC    CL35'***   NEW RECORD             ***'
BADID    DC    CL35'*** NEW RECORD  W/INVALID ID ***'
BADREPID DC    CL35'*** REPLACEMENT W/INVALID ID ***'
INVMSG   DC    CL35'*** REPLACES ORIG CARD       ***'
ORGMSG   DC    CL35'***   ORIGINAL CARD          ***'
USERMOD  DC    CL80' '
         ORG   USERMOD
         DC    C'++USERMOD('
SMPMODID DC    CL7' ',C')  /*  ROCKWELL INTERNATIONAL USERMOD */. '
         ORG
VER1     DC    CL80' '
         ORG   VER1
         DC    C'++VER(Z038) FMID('
SMPFMID  DC    CL7' ',C')'
         ORG
VERP1    DC    CL80' '
         ORG   VERP1
         DC    C'            PRE('
PRE1     DC    CL40' '
         ORG
VERP2    DC    CL80' '
         ORG   VERP2
         DC    C'                '
PRE2     DC    CL40' '
         ORG
VERPE    DC    CL80' '
         ORG   VERPE+30
VERPEP   DC    C')'                     ENDING PAREN
         DC    C'         /*  END OF PREREQS  */    '
         ORG
LASTCC   DC    CL80'        /* END OF ++VER */  .'              *LBD*
ASSEMCC  DC    CL80'  ASSEM(      /* START OF ASSEMBLE NAMES */'
ASSEMCCE DC    CL80'    )         /* END OF ASSEMBLE NAMES   */ '
ASSEM    DC    CL80' '
         ORG   ASSEM+1
ASSNAMES DC    CL73' '
         ORG
JCLIN    DC    CL80'++JCLIN.'
UPDTE    DC    CL80' '
         ORG   UPDTE
         DC    C'++'
SMPTYPE  DC    CL3'SRC'
SMPTU    DC    C'UPD ('
SMPELEM  DC    CL8' ',C') '
         ORG
UPDTEE   DC    CL80'    /* END OF USERMOD */ .'
UPDTES   DC    CL80' '
         ORG   UPDTES
         DC    C'   SYSLIB('
SYSLIB   DC    CL8' ',C')'
         ORG
UPDTED   DC    CL80' '
         ORG   UPDTED
         DC    C'  DISTLIB('
DISTLIB  DC    CL8' ',C')'
         ORG
         DS    0F
PARMPTR  DC    A(WORDS)
WORDS    DC    A(FUNCTION,MEMBER,DATALEN)
FUNCTION DC    CL8' '
MEMBER   DC    CL8' '
DATALEN  DC    A(100*80)
DADDR    DC    A(DATA)
         SPACE 2
OLDSRC   DCB   DSORG=PS,MACRF=GL,DDNAME=OLDSRC,EODAD=EOFO
NEWSRC   DCB   DSORG=PS,MACRF=GL,DDNAME=NEWSRC,EODAD=EOF
DELTA    DCB   DSORG=PS,MACRF=PM,DDNAME=DELTA,RECFM=FB,LRECL=80
DELTAIN  DCB   DSORG=PS,MACRF=GL,DDNAME=DELTA,EODAD=EODDEL
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=REPORT,LRECL=121,RECFM=FBA,    *
               BLKSIZE=6171
SMPJCLIN DCB   DSORG=PS,MACRF=GL,DDNAME=SMPJCLIN,EODAD=EJCLIN
         SPACE 2
DATA     DC    100XL80'00'
         END
