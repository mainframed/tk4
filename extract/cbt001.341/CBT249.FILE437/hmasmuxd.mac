         TITLE 'ROCKWELLSMP RECEIVE EXIT '
*---------------------------------------------------------------------*
*  MODULE NAME: HMASMUXD
*
*  FUNCTION:    THIS EXIT WILL RECEIVE CONTROL FOR ALL RECORDS OF EVERY
*               SYSMOD SMP ATTEMPTS TO RECEIVE.
*
*               A CHECK IS FIRST PERFORMED TO DETERMINE THE TYPE OF
*               PROCESSING TO BE PERFORMED.
*
*               THE PRESENCE OF DDNAME EXITI CAUSES THE EXIT TO VERIFY
*               EVERY USERMOD RECORD TO INSURE THAT THE USERMOD ID
*               IS FOUND IN EVERY RECORD IN COLUMNS 65-71 AND THAT
*               A @ IS FOUND IN COLUMN 64 TO INDICATE THAT 65-71 ARE
*               IN FACT A USERMOD ID.
*
*               DDNAME EXITD CAUSES THE SUPPRESSION OF ALL ++MOD'S
*               ON THE RECEIVE.  THIS ELIMINATES THE POSSIBILITY
*               OF VANILA IBM OBJECT DECKS BEING USED INSTEAD OF
*               AN ASSEMBLY OF THE UPDATED MODULE.
*
*               DDNAME EXITB CAUSES BOTH ID CHECK AND ++MOD SUPPRESSION
*               TO TAKE PLACE.
*
*               THE FORMAT OF THE DD CARD IS
*               //EXIT_  DD SYSOUT=*
*
*               AND A REPORT OF ALL ACTIVITY IS PRODUCED ON THIS
*               DD.
*
*   NOTE:  THE ID CHECK IS NOT PERFORMED FOR ++SRC OR ++MAC SINCE
*          THESE ARE TOTAL REPLACEMENT FUNCTIONS.
*---------------------------------------------------------------------*
*   AUTHOR:  LIONEL DYCK
*            ROCKWELL INTERNATIONAL
*            2201 SEAL BEACH BLVD
*            PO BOX 2515
*            SEAL BEACH, CALIF 90740
*            MAIL CODE 110-SH28
*            PHONE 594-1125
*---------------------------------------------------------------------*
         EJECT
PAGELEN  EQU   53                  PAGE LENGTH EQUATE
         SPACE
HMASMUXD PRIME 12,SAVE,,,Y
         L     R4,0(R1)            -> PARM LIST
         USING HMASMUXP,R4         SET UP ADDR
         CLC   UXPUXNUM,=X'0001'
         BNE   EXIT                NOT RECIEVE SO TERME
         IFON  FIRST,SKIP
         RDJFCB SYSPRINT
         LTR   R15,R15
         BZ    FID
         USING IHADCB,R3
         LA    R3,SYSPRINT
         MVC   DCBDDNAM,EXITBDDN        TRY EXITB
         RDJFCB SYSPRINT
         LTR   R15,R15
         BZ    FBOTH
         MVC   DCBDDNAM,EXITDDDN   TRY EXITD
         RDJFCB SYSPRINT
         LTR   R15,R15
         BZ    FDEL
         B     EXIT                     NOT THERE SO SKIP IT
         DROP  R3
FID      SETON IDCHECK
         B     DOIT
FBOTH    SETON IDCHECK,DELCHK
         B     DOIT
FDEL     SETON DELCHK
DOIT     EQU   *
         SETON FIRST
         OPEN  (SYSPRINT,OUTPUT)
SKIP     EQU   *
         ST    R12,UXPUXAD
         L     R7,UXPPRMAD         -> INPUT RECORD
         LTR   R7,R7
         BZ    ZERO
         USING UX001PRM,R7
         TM    UX001RC,UX001EOF    EOF ?
         BO    EOF                 YES
         CLC   =C'++',UX001RCD     SMP CONTROL CARD ?
         BE    SMPCC               YES
         IFON  MOD,BYPASS
         IFON  UPDATE,IDCHK
EXIT     TERME  RC=UXPGOOD
BYPASS   TERME  RC=UXPDEL
ZERO     EQU   *
         B     EXIT
REJECT   MVC   ACTION,REJECTM
         BAL   R3,PUTR
         TERME  RC=UXPTPTF
EOF      EQU   *
         CLOSE SYSPRINT
         SETOF MOD,UPDATE,USERMOD,FIRST,REJECTF,IDCHECK,DELCHK
         B     EXIT
SMPCC    EQU   *
         SETOF MOD,UPDATE
         LA    R3,10               SET LIMIT FOR LOOP
         LA    R8,UX001RCD+2
RCDLP    EQU   *
         CLC   =C'(',0(R8)         END OF SEARCH
         BE    EXIT                YES -DEPART
         CLC   =C'MOD',0(R8)
         BE    MODR
         CLC   =C'PTF',0(R8)
         BE    SYSMODR
         CLC   =C'APAR',0(R8)
         BE    SYSMODR
         CLC   =C'FUNCTION',0(R8)
         BE    SYSMODR
         CLC   =C'USERMOD',0(R8)
         BE    USERMODR
         CLC   =C'MACUPD',0(R8)
         BE    UPDTER
         CLC   =C'SRCUPD',0(R8)
         BE    UPDTER
         CLC   =C'UPDTE',0(R8)
         BE    UPDTER
         LA    R8,1(R8)            INCR RCD BY 1
         BCT   R3,RCDLP            KEEP LOOPING THRU TABLE
*        ASSUME NOT A RECORD WE WANT
         B     EXIT
MODR     EQU   *
         IFOFF DELCHK,EXIT         BYPASS - NO DELETE CHECK
         SETON MOD                 TURN MOD SWITCH ON
         LA    R1,3(R8)            -> PAST ++MOD
         LA    R3,10               SET SEARCH LIMIT
         SR    R2,R2               CLEAR R2
MLP1     CLI   0(R1),C'('          START OF MODULE NAME ?
         BE    MLP1E               YES
         LA    R1,1(R1)
         BCT   R3,MLP1             KEEP LOOKING
         B     MODM                FORGET IT
MLP1E    LA    R1,1(R1)            -> START OF MODULE NAME
         LR    R10,R1              SAVE -> MOD NAME
         LA    R3,9                SET SEARCH LIMIT
MLP2     CLI   1(R1),C')'          END OF MODULE NAME
         BE    MLP2E               YES
         LA    R1,1(R1)
         AH    R2,=H'01'           INCR BY 1
         BCT   R3,MLP2
         B     MODM                FORGET IT
MLP2E    MVC   MODNAME(8),BLANKS
         EX    R2,MODMOVE
MODM     MVC   ACTION,MODDEL
         BAL   R3,PUTR
         B     BYPASS
         SPACE
MODMOVE  MVC   MODNAME(0),0(R10)
         SPACE
USERMODR EQU   *
         SETON USERMOD
         SETOF REJECTF,UPDATE,MOD,IEBCC
*        FIND SYSMOD ID
FNDID    LA    R3,16               SET SEARCH LIMIT
         LA    R1,3(R8)            -> SYSMOD PARAMETER
ULP      CLI   0(R1),C'('          START OF ID ?
         BE    CID                 YES
         LA    R1,1(R1)            NO - INCR
         BCT   R3,ULP              KEEP LOOKING
         B     ERROR               DID NOT FIND - TELL SOMEONE
CID      CLI   8(R1),C')'          END OF SYSMOD ID ?
         BNE   ERROR               NO - ITS BAD
         MVC   UMODID(7),1(R1)     MOVE MOD ID
         MVC   CLCMOD+1(7),UMODID
         B     EXIT
ERROR    MVC   ACTION,UMODER
         BAL   R3,PUTR
         B     EXIT
UPDTER   EQU   *
         SETON UPDATE
         SETOF IEBCC
         B     EXIT
SYSMODR  EQU   *
         SETOF USERMOD,REJECTF,UPDATE,MOD,IEBCC
         B     FNDID               GO FIND ID
IDCHK    EQU   *
         IFOFF IDCHECK,EXIT        BYPASS - NO CHECK
         IFOFF USERMOD,EXIT        BYPASS - NOT A USERMOD
         LA    R8,UX001RCD
         CLC   0(2,R8),=C'./'      IEBUPDTE CC
         BE    HAVEICC
         IFOFF IEBCC,EXIT          SKIP IT TILL WE HAVE ./
         CLC   63(8,R8),CLCMOD     IS USERMOD ID + @ IN COL 64-71
         BE    EXIT                YES
         MVC   SEQ#,72(R8)         MOVE SEQ #
         MVC   ACTION,COL65E
         BAL   R3,PUTR
         MVC   SEQ#,BLANKS
         IFON  REJECTF,EXIT
         SETON REJECTF
         B     REJECT              NO - TURN OFF RECEIVE
HAVEICC  SETON IEBCC
         B     EXIT
PUTR     EQU   *
         LH    R1,PL
         CH    R1,MPL
         BNL   NEWP                YES
         AH    R1,=H'01'
         STH   R1,PL
         PUT   SYSPRINT,M3
         BR    R3
NEWP     LH    R1,=H'04'
         STH   R1,PL
         PUT   SYSPRINT,M1
         PUT   SYSPRINT,M2
         PUT   SYSPRINT,M3
         BR    R3
         EJECT
         LTORG
         SPACE 2
         BITSW MOD,UPDATE,USERMOD,FIRST,REJECTF,IDCHECK,DELCHK,IEBCC
         SPACE 2
         PRINT NOGEN
EXITBDDN DC    CL8'EXITB'
EXITDDDN DC    CL8'EXITD'
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=EXITI,LRECL=121,              XX
               RECFM=FBA,BLKSIZE=121,EXLST=JFCB
JFCB     DC    X'87',AL3(JFCBD)
JFCBD    DS    CL176
         PRINT GEN
         EJECT
ANS      DS    F
PL       DC    AL2(PAGELEN+1)
MPL      DC    AL2(PAGELEN)
M1       DC    CL121'1                 SMP RECEIVE EXIT MESSAGES '
M2       DC    CL121'0      MODID     ACTION'
M3       DC    CL121' '
         ORG   M3
         DC    C'     '
UMODID   DC    CL7' '
         DC    CL5' '
ACTION   DC    CL50' '
         ORG
CLCMOD   DC    CL8'@ '
BLANKS   DC    CL121' '
UMODER   DC    CL50'*** USERMOD ID NOT FOUND ***'
COL65E   DC    CL50' '
         ORG   COL65E
         DC    C'*** USERMOD ID NOT IN COL 65-71 ***'
         DC    C'  SEQ#='
SEQ#     DC    CL8' '
         ORG
REJECTM  DC    CL50'*** RECEIVE OF USERMOD TERMINATED ***'
MODDEL   DC    CL50' '
         ORG   MODDEL
         DC    C'*** ++MOD BYPASSED ***'
         DC    C'  NAME='
MODNAME  DC    CL8' '
         ORG
         EJECT
         HMASMUXP
         EJECT
         DSECT
         IEFTIOT1
         EJECT
         DCBD  DSORG=PS
         END
