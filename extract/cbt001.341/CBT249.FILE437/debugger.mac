         TITLE  'DEBUGGER - THE SYSTEM PROGRAMMERS TOOL'    *HMD 02/82*
         MACRO
&NAME    TRX   &A,&B
         GBLC  &X555555
         AIF   ('&NAME' EQ '').NONAME
&X555555 SETC  '&NAME'
.NONAME  ANOP
         ORG   &X555555+&A
         AIF   ('&B' EQ '').NULLB
         DC    &B
         ORG
         MEXIT
.NULLB   ANOP
         DC    &A
         ORG
         MEND
         EJECT
         MACRO
&NAME    TPT   &WHAT,&LEN
&NAME    LA    1,&WHAT .                GET ADDRESS OF MESSAGE
         LA    0,&LEN .                 ABSOLUTE LENGTH ONLY
         L     15,=A(PUTLINE)           PUTLINE DATA MODE   *HMD 11/79*
         BALR  14,15                    LINK TO PUTLINE     *HMD 11/79*
         MEND
         EJECT                                              *HMD 11/79*
         MACRO                                              *HMD 11/79*
&NAME    TPTI  &WHAT,&LEN                                   *HMD 11/79*
&NAME    LA    1,&WHAT .                GET ADDRESS OF MSG  *HMD 11/79*
         LA    0,&LEN  .                ABSOLUTE LENGTH ONLY*HMD 11/79*
         L     15,=A(PUTLINEI)          PUTLINE INFO MODE   *HMD 11/79*
         BALR  14,15                    LINK TO PUTLINE     *HMD 11/79*
         MEND                                               *HMD 11/79*
DBUG     TITLE 'DEBUGGER - THE TSO DEBUGGER '
DEBUGGER TSOENTER REGS=YES,PL=(CPPL,CSCAN,IO,DAIR),SP=1,WORK=DSECTL
         LA    R11,4095(,R12)           DEFINE SECOND BASE
         LA    R11,1(,R11)
         LUSE  DEBUGGER+4096,R11
         INUSE ,                        SEE WHOSE ON FIRST  *HMD 11/79*
         EJECT                                              *HMD 11/79*
***********************************************************************
*                                                                     *
*        THE DEBUGGER                                                 *
*        PURPOSE: SYSTEM'S PROGRAMMERS TOOL                           *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
         SPM   R13                      (I KNOW THE HI BYTE IS ZERO)
         L     R14,CVTPTR               GET CVT POINTER
         L     R14,0(,R14)              DISPATCH D-WORD
         L     R14,4(,R14)              NEW TCB
         ST    R14,DA2CTCB              DAIR TCB ADDRESS    *HMD 11/79*
         XC    ADDRJSCB,ADDRJSCB        CLEAR JSCB ADDR     *HMD 11/79*
         MVC   ADDRJSCB+1(3),TCBJSCB-TCB+1(R14)  GET JSCB   *HMD 11/79*
         SPACE 1
AREAUTH  DS    0H                                           *HMD 02/82*
         GTSIZE ,                       GET CURRENT SCREEN SIZE
         STM   R0,R1,SCRSAVE
         AH    R0,=H'5'                 ADD FIVE  TO SCR LEN
         STM   R0,R1,BIGSAVE            BIG SCREEN SIZE
         TESTAUTH FCTN=1                                    *LBD 01-83*
         LTR   R15,R15                  ARE WE AUTH ?       *LBD 01-83*
         BNZ   NOTAUTH                                      *LBD 01-83*
         MODESET KEY=ZERO,MODE=SUP                          *LBD 01-83*
NOTAUTH  EQU   *                                            *LBD 01-83*
         TITLE 'DEBUGGER - LOAD SERVICE ROUTINES'
         LOAD  EPLOC=IKJPUTL            LOAD PUTLINE        *HMD 11/79*
         ST    R0,ADDRPUTL                                  *HMD 11/79*
         LOAD  EPLOC=IKJGETL            LOAD GETLINE        *HMD 11/79*
         ST    R0,ADDRGETL                                  *HMD 11/79*
         LOAD  EPLOC=IKJDAIR            LOAD DAIR           *HMD 11/79*
         ST    R0,ADDRDAIR                                  *HMD 11/79*
         LOAD  EPLOC=IKJSCAN            LOAD COMMAND SCAN   *HMD 11/79*
         ST    R0,ADDRSCAN                                  *HMD 11/79*
         B     GOODUSER
         TITLE 'DEBUGGER - EXIT ROUTINE'
*                                                           *HMD 11/79*
*        EXIT ROUTINE
*
LEAVE    STSIZE SIZELOC=SCRSAVE+4,LINELOC=SCRSAVE
LEAVE01  DS     0H                                          *HMD 02/82*
         DELETE EPLOC=IKJPUTL           DELETE PUTLINE      *HMD 11/79*
         DELETE EPLOC=IKJGETL           DELETE GETLINE      *HMD 11/79*
         DELETE EPLOC=IKJDAIR           DELETE DAIR         *HMD 11/79*
         DELETE EPLOC=IKJSCAN           DELETE SCAN         *HMD 11/79*
         TESTAUTH FCTN=1                                    *LBD 01-83*
         LTR   R15,R15                  ARE WE AUTH ?       *LBD 01-83*
         BNZ   NOTAUTHL                                     *LBD 01-83*
         MODESET KEY=NZERO,MODE=PROB                        *LBD 01-83*
NOTAUTHL EQU   *                                            *LBD 01-83*
         TSOLEAVE ,                     LEAVE HIM HIGH AND DRY
         TITLE 'DEBUGGER - INITIALIZE TRACE AND NAME TABLES'
GOODUSER DS    0H                                           *HMD 11/79*
         LA    R14,NAMEINIT             LOAD RETURN ADDRESS
CLRTRACE STM   R3,R5,12(R13)            SAVE IN NON-STD
         LA    R3,@TRACE                START OF TRACE TABLE
         LA    R4,4                     LENGTH OF EACH ENTRY
         LA    R5,@TRACEND              END OF TRACE TABLE
         STM   R3,R5,@TRACECB
CLRTRAC1 XC    0(4,R3),0(R3)            CLEAR IT
         BXLE  R3,R4,CLRTRAC1           TRY AGAIN
         LM    R3,R5,12(R13)            RESTORE REGS
         BR    R14                      RETURN TO CALLER
         SPACE 3
NAMEINIT LA    R14,STARTUP              LOAD RET ADDRESS
         SPACE 1
CLRNAME  STM   R3,R5,12(R13)            SAVE REGS
         LA    R3,@NAME                 LOAD NAME TABLE
         LA    R4,12                    LENGTH OF EACH ENTRY
         LA    R5,@NAMEND               END OF TABLE
         STM   R3,R4,@NAMECB            SAVE IN TABLE
CLRNAME1 XC    0(12,R3),0(R3)           CLEAR ENTRY
         BXLE  R3,R4,CLRNAME1           TRY AGAIN
         LM    R3,R5,12(R13)            RELOAD SAVED REGS
         BR    R14                      RETURN TO CALLER
LTORG1   LTORG
         TITLE 'DEBUGGER - SUBROUTINES - CHECKNXT - FIXUP'
CHECKNXT CLI   0(R3),C' '               END?
         BE    4(,R14)                  YES
         CLI   0(R3),C','               CONTINUE
         BE    4(,R14)
         CLI   0(R3),C'+'               SPECIAL CASE
         BE    4(,R14)                  YES
         CLI   0(R3),C'-'               SPECIAL CASE
         BNER  R14                      NO
         B     4(,R14)                  RETURN NOW          *HMD 11/79*
         SPACE 2
         PUSH  USING                                        *HMD 11/79*
         USING FIXUP,R15
FIXUP    CH    R0,=H'12'                OUT OF CORE?
         BNE   FIXUPPOS                 WE HAVE CORE        *HMD 11/79*
         SR    R15,R15                  CONTINUE ABEND
         LR    R14,R5
         BR    R14
         SPACE 1
FIXUPPOS STM   R14,R12,12(R13)          SAVE REGS
         LR    R10,R1
         USING SDWA,R10
         BALR  R12,0
         USING *,R12,R11
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         CLC   SDWAABCC+1(2),=X'13E0'   IS DETACH?
         BE    ATTNOUT                  YES, GOING BYBY
         CLC   SDWAABCC+1(2),=X'33E0'   IS ATTN?
         BE    ATTNOUT                  YES, GOING BYBY
         SPACE 1
         CLC   SDWAABCC+1(2),=X'0470'   LOST AUTHORITY?
         BNE   ABENDED                  NO, THEN WE ABENDED
         SPACE 1
         B     RETRY
         SPACE
ABENDED  UNPK  MSG15(9),SDWAABCC(5)     CONVERT TO HEX
         TR    MSG15(8),TRTOHEX
         MVI   MSG15+8,C' '
         SPACE 1
         OI    ABENDFLG,MSG15FLG        SEND MSG15 UPON RET.*HMD 11/79*
         SPACE
RETRY    SETRP WKAREA=(10),REGS=(14,12),RC=4,RETADDR=FLUSH,            *
               RETREGS=YES,FRESDWA=YES
         SPACE
ATTNOUT  STSIZE SIZELOC=SCRSAVE+4,LINELOC=SCRSAVE
ATTNOUT1 DS    0H                                           *HMD 02/82*
         SETRP WKAREA=(10),REGS=(14,12),RC=0
         SPACE
         LTORG
         POP USING                                          *HMD 11/79*
         SPACE
         TITLE 'DEBUGGER - SUBROUTINES - TOHEX'
         ST    R14,SA142                SAVE RETURN REG     *HMD 11/79*
TOHEX    UNPK  HEX(9),PLACE(5)
         UNPK  HEX+8(9),PLACE+4(5)
         UNPK  HEX+16(9),PLACE+8(5)
         UNPK  HEX+24(9),PLACE+12(5)
         TR    HEX(32),TRTOHEX
         BR    R14                      RETURN
         TITLE 'DEBUGGER - SUBROUTINES - AUTHON AND AUTHOFF'
AUTHON   DS    0H                       TURN AUTH BACK ON   *HMD 11/79*
         ST    R14,SA142                SAVE RETURN REG     *HMD 11/79*
         L     R15,ADDRJSCB             GET JSCB ADDR       *HMD 11/79*
         OI    236(R15),X'01'           TURN ON AUTH IN JSCB*HMD 11/79*
*        MODESET KEY=NZERO              BACK TO PROBLEM KEY *HMD 11/79*
         L     R14,SA142                RESTORE R14         *HMD 11/79*
         BR    R14                      RETURN TO CALLER    *HMD 11/79*
AUTHOFF  DS    0H                       TURN AUTH OFF       *HMD 11/79*
         ST    R14,SA142                SAVE RETURN REG     *HMD 11/79*
*        MODESET KEY=ZERO               GET INTO KEY ZERO   *HMD 11/79*
         L     R15,ADDRJSCB             GET JSCB ADDR       *HMD 11/79*
         NI    236(R15),254             TURN AUTH OFF       *HMD 11/79*
         L     R14,SA142                RESTORE RETURN REG  *HMD 11/79*
         BR    R14                      RETURN TO CALLER    *HMD 11/79*
         TITLE 'DEBUGGER - SUBROUTINES - PUTTRACE'
PUTTRACE ST    R14,SA141                SAVE RETURN REG     *HMD 11/79*
         STM   R3,R5,SA1+12             SAVE REGS DESTROYED
         LM    R3,R5,@TRACECB           GET TRACE CONTROL
         MVC   1(3,R3),DPLYADDR+1       TRACE DISPLAY ADDRESS
         BXLE  R3,R4,ENDTRACE           END IT?
         LA    R3,@TRACE                LOAD START
         SPACE 1
ENDTRACE STM   R3,R5,@TRACECB           RESTORE TRACE CB
         LM    R3,R5,SA1+12             GET INFO
         BR    R14                      RETURN TO CALLER
         TITLE 'DEBUGGER - SUBROUTINES - PUTNAME'
PUTNAME  STM   R0,R5,SA1+12             SAVE WIPED REGS
         LM    R4,R5,@NAMECB+4          LOAD NAME CTL BLECHS
         LA    R3,@NAME                 LOAD FIRST ENTRY
         XR    R2,R2                    CLEAR POINTER REG
         SPACE 1
PUTNAME1 CLC   4(8,R3),CURNAME          IS IT SAME?
         BE    PUTNAME3                 YES
         OC    4(8,R3),4(R3)            TEST FOR NULL
         BNZ   PUTNAME2                 NOT IT
         LTR   R2,R2                    TEST IT
         BNZ   PUTNAME2                 FORGET IT
         LR    R2,R3                    LOAD FIRST NULL POINTER
         SPACE 1
PUTNAME2 BXLE  R3,R4,PUTNAME1           TRY AGAIN
         L     R3,@NAMECB               LOAD DEFAULT NEXT
         LTR   R2,R2                    TEST IT
         BNP   PUTNAME4                 NO NULL ENTRY
         LR    R3,R2                    DEFINE POINTER
         B     PUTNAME4                 GO TO IT
         SPACE 1
PUTNAME3 MVC   1(3,R3),WHERE+1          MOVE IN ID
         MVC   MSG04+3(8),CURNAME       MOVE IN
         TPTI  MSG04,L'MSG04            PUT OUT MSG
         B     PUTNAMEE                 DONE
         SPACE 1
PUTNAME4 BP    PUTNAME5                 IS THERE A NULL
         MVC   MSG05+3(8),4(R3)         MOVE IN REUSED
         TPTI  MSG05,L'MSG05            PUT OUT MSG
PUTNAME5 MVC   1(11,R3),WHERE+1         MOVE IN NAME STUFF
         BXLE  R3,R4,PUTNAMER           TRY AGAIN
         LA    R3,@NAME                 REUSE TABLE
         SPACE 1
PUTNAMER STM   R3,R5,@NAMECB            SAVE CB
PUTNAMEE LM    R0,R5,SA1+12             GET SAVE AREA
         L     R14,SA141                RESTORE RETURN REG  *HMD 11/79*
         BR    R14                      RETURN TO CALLER
         TITLE 'DEBUGGER - SUBROUTINES - FORMAT'
*
*        FUNCTION - FORMAT A LINE FOR DISPLAY
*
         SPACE 1
*
*        CALLING - R1 -> OUTPUT
*                  R0 -> INPUT ADDRESS
*
         SPACE 1
*
*        USES DPLYADDR TO DETERMINE INSERT CHAR
*
         SPACE 1
FORMAT   STM   R14,R12,12(R13)          SAVE REGS
         SRL   R0,4                     DIVIDE BY 16
         SLL   R0,4                     MULT. BY 16
         ST    R0,PLACE                 SAVE ADDRESS
         LR    R15,R0                   SAVE IT
         BAL   R14,TOHEX                CONVERT IT
         MVC   0(6,R1),HEX+2            OUTPUT FROM ROUTINE
         MVC   PLACE(16),0(R15)         MOVE IN THINGS
         BAL   R14,TOHEX                CONVERT IT
         MVI   6(R1),C' '               CLEAR OUTPUT
         MVC   7(73,R1),6(R1)           FINNISH (FISCHEY ??)
         LA    R3,8(,R1)                OUTPUT PLACE
         LA    R4,4                     NUMBER FOR EACH TIME
         LA    R5,HEX                   LOCATION
         SPACE 1
FORMAT1  MVC   0(4,R3),0(R5)            4 BYTES AT A TIME
         MVC   5(4,R3),4(R5)            WITH A BLANK
         LA    R5,8(,R5)                NEXT SET
         LA    R3,11(,R3)               OFFSET TO NEXT FIELD
         BCT   R4,FORMAT1               TRY AGAIN
         SPACE 1
         TR    PLACE(16),TRTOCHAR       MAKE DISPLAYABLE
         LA    R3,53(,R1)               OUTPUT LOC
         MVI   52(R1),C'×'              DELIMETER
         MVI   71(R1),C'×'              SAME
         LA    R6,2                     NUMBER OF TIMES
         LA    R5,PLACE                 PLACE ADDRESS
         SPACE 1
FORMAT2  LA    R4,4                     NUMBER OF TIMES
FORMAT3  MVC   0(2,R3),0(R5)            CHECK IT
         LA    R5,2(,R5)                NEXT POSITION
         LA    R3,2(,R3)                SAME HERE
         BCT   R4,FORMAT3               CONTINUE?
         LA    R3,2(,R3)                EXTRA 2 THIS TIME
         BCT   R6,FORMAT2               TRY AGAIN
         MVI   DPLYADDR,0               CLEAR HI BYTE
         C     R0,DPLYADDR              GET LIKELY ADDRESS
         BH    FORMATR                  RETURN - CAN'T BE
         L     R0,DPLYADDR              GET ADDRESS TO DISPLAY AROUND
         SRL   R0,4                     /16 FOR DISPLAY CHECK
         SLL   R0,4                     *16 FOR DISPLAY CHECK
         CR    R0,R15                   SAME ADDRESS NOW?
         LR    R15,R0                   ASSUME YES
         BNE   FORMATR                  CAN'T BE IT
         SPACE 1
*        DPLYADDR POINTS TO SOMETHING IN THIS DISPLAY
         SPACE 1
         LA    R3,7(,R1)                FIRST POSITION
         MVI   SWITCH,0                 SET > SWITCH
         LA    R6,8                     NUMBER OF TIMES IN LOOP
FORMATLP C     R15,DPLYADDR             IS THIS IT?
         BE    FORMATPT                 YES, FORMAT POINTER
         LA    R3,5(,R3)                NEXT POINTER
         LA    R15,1(,R15)              UP POSITION
         XI    SWITCH,X'FF'             FLIP IT
         C     R15,DPLYADDR             IS THIS IT?
         BE    FORMATPT                 YES
         XI    SWITCH,X'FF'             FLIP IT
         LA    R15,1(,R15)              UP ONE MORE
         C     R15,DPLYADDR             IS THIS IT, MAYBE?
         BE    FORMATPT                 YES
         XI    SWITCH,X'FF'             FLIP IT AGAIN
         LA    R3,5(,R3)                UP IT
         LA    R15,1(,R15)              MORE
         C     R15,DPLYADDR             THIS IT?
         BE    FORMATPT                 DO IT
         LA    R3,1(,R3)                UP THAT POINTER
         LA    R15,1(,R15)              SAME
         MVI   SWITCH,X'00'             RESET TO '>'
         BCT   R6,FORMATLP
         EX    0,*                      THIS CAN'T HAPPEN
FORMATPT TM    SWITCH,X'FF'             WHICH IS IT?
         LA    R15,C'>'                 ASSUME NOT (50 P.C. CHANCE)
         BE    *+8                      RIGHT
         LA    R15,C'<'                 OH, WELL
         EX    R15,MVI0R3               DO IT
         SPACE 3
FORMATR  LM    R14,R12,12(R13)          RELOAD CALLER'S REGS
         BR    R14                      RETURN TO CALLER
         SPACE 2
MVI0R3   MVI   0(R3),X'00'              <<EXECUTED>>
         TITLE 'DEBUGGER - STARTUP AND DISPLAY ROUTINES'
STARTUP  MVI   INPUT,C'0'             SET INIT COMMAND TO '0'
         SPACE 1
         ESTAE FIXUP,TERM=YES
         SPACE 1
         L     R3,CVTPTR                GET CVT
         L     R3,CVTMZ00-CVT(,R3)      GET HIGHEST MACHINE ADDRESS
         ST    R3,MZ00                  SAVE FOR ABS. CHECK
         SRL   R3,4                     /16
         SLL   R3,4                     SET BACK
         ST    R3,STOPAT
         TPUT  CLEAR,L'CLEAR,FULLSCR
         B     PARSER
         SPACE 3
DISPLAY  CLI   GETLRC,4                 WAS LAST GETLINE FROM STACK?
         BE    TGET                     YES, THEN SKIP DISPLAY
         MVC   PLACE(4),DPLYADDR        MOVE ADDRESS
         BAL   R14,TOHEX                GIVE IT TO HIM
         MVC   MSG10A(8),HEX            GIVE HEX TO HIM
         TPT   MSG10,MSG10L             PUT OUT MESSAGE
DISPLAYA LA    R1,SCREEN                LOAD OUTPUT ADDRESS
         MVI   DPLYADDR,X'00'           CLEAR ANY HI BITS
         SPACE 1
DISPLAY1 L     R10,DPLYADDR             DISPLAY ADDRESS
DISPLAYT L     R0,DPLYADDR              GET ADDRESS
         A     R0,=F'96'                CHECK HIGHER ADDRESS
         CL    R0,MZ00                  CHECK IT
         BNH   DISPLAY0                 OK, CONTINUE
         SL    R0,=F'96'
         CL    R0,MZ00
         BNH   DISPLAYZ                 OK, AT LEAST
         MVC   DPLYADDR(4),MZ00         MOVE IN HIGHEST
         B     ANERROR                  OOPS
         SPACE 1
DISPLAYZ L     R10,MZ00                 LOAD HIGHEST ADDRESS
         S     R10,=F'128'
         SPACE 1
DISPLAY0 EQU   *
*        MODESET  KEY=ZERO
         SPACE
         LA    R1,SCREEN
         LA    R3,19                    LOAD LINE COUNT
         MVI   DPLYADDR,X'00'           CLEAR HI BYTE HERE
DISPLAYF LR    R0,R10                   GET ADDRESS
         BAL   R14,FORMAT               FORMAT IT
         LA    R1,80(,R1)               NEXT LINE
         A     R10,=F'16'               UP NUMBER
         BCT   R3,DISPLAYF
*        MODESET KEY=NZERO
         SPACE 1
         BAL   R14,PUTTRACE             TRACE THIS ENTRY
         LA    R3,SCREEN                SET UP DISPLAY
         LA    R4,80                    LENGTH
         LA    R5,SCREEN+19*80-1         END FOR SCREEN
         SPACE 1
DISPLAYQ LR    R1,R3               GET OUTPUT DATA          *HMD 11/79*
         LR    R0,R4               GET OUTPUT LENGTH        *HMD 11/79*
         L     R15,=A(PUTLINE)     GET PUTLINE ADDR         *HMD 11/79*
         BALR  R14,R15             LINK TO PUTLINE          *HMD 11/79*
         BXLE  R3,R4,DISPLAYQ
         TITLE 'DEBUGGER - INPUT AND PARSE'
TGET     XC    TSOECB,TSOECB           CLEAR COMM ECB       *HMD 11/79*
PUTL001  L     R15,ADDRPUTL            GET PUTLINE ENTRY PT *HMD 11/79*
         PUTLINE OUTPUT=(OLDPROM,SINGLE,INFOR),             *HMD 11/79*X
               PARM=PTPB,MF=(E,IOPL),ENTRY=(15)             *HMD 11/79*
GETLINE  XC    TSOECB,TSOECB                                *HMD 11/79*
         L     R15,ADDRGETL            GET GETLINE ENTRY PT *HMD 11/79*
         GETLINE PARM=GETPARM,                              *HMD 11/79*X
               TERMGET=(EDIT,WAIT),                         *HMD 11/79*X
               MF=(E,IOPL),ENTRY=(15)
         SPACE
         STC   R15,GETLRC               SAVE GETLINE RET CODE
         TPUT  CLEAR,L'CLEAR,FULLSCR
         CLI   GETLRC,16                EOD ON INCORE LIST?
         BE    DISPLAY                  YES, GO DISPLAY
         BH    GETERR                   ELSE, GETLINE ERROR
         SPACE
         L     R1,GETPARM+4             ADDR OF RETURNED LINE
         LH    R2,0(R1)                 GET LENGTH OF LINE  *HMD 11/79*
         CH    R2,=H'80'                ALLOW ONLY 80 CHARS *HMD 11/79*
         BNH   CHKINPT1                                     *HMD 11/79*
         LA    R2,80                    ONLY MOVE 80 CHARS  *HMD 11/79*
CHKINPT1 SH    R2,=H'5'                 GET READY FOR EX    *HMD 11/79*
         BM    NOTPCFX                  NO WAY              *RI  08/82*
         EX    R2,OCBUFF                FORCE UPPER CASE    *HMD 11/79*
         B     NOTPCFX                                      *RI  08/82*
OCBUFF   OC    4(0,R1),=CL80' '         FOR UPPER CASE      *HMD 11/79*
         SPACE
NOTPCFX  MVI   INPUT,C' '               BLANK INPUT LINE
         MVC   INPUT+1(L'INPUT-1),INPUT
         LH    R2,0(R1)                 LOAD LENGTH OF LINE
         LR    R3,R2                    SAVE FOR LATER FREEMAIN
         CH    R2,=H'80'                ALLOW ONLY 80 CHARS *HMD 11/79*
         BNH   NOTPCF01                                     *HMD 11/79*
         LA    R2,80                                        *HMD 11/79*
NOTPCF01 SH    R2,=H'5'                 SUBTRACT FOR MVC
         BM    FRESP1                   IF ZERO LEN, THEN FREE BUFFER
         MVC   INPUT(0),4(R1)           * EXECUTED *
         EX    R2,*-6
         SPACE
FRESP1   FREEMAIN R,LV=(3),A=(1),SP=1
         B     PARSER
         SPACE
GETERR   TPTI  MSG02,L'MSG02            ISSUE MESSAGE
         TCLEARQ INPUT                  THROW IT AWAY
         B     FLUSH                    TRY AGAIN
         SPACE
PARSER   STSIZE SIZELOC=BIGSAVE+4,LINELOC=BIGSAVE   SET BIG SCREEN
         LA    R3,INPUT
         LA    R4,1                     INCREMENT
         LA    R5,L'INPUT-1(,R3)        END
         LR    R6,R3                    PLACE FOR IT
COMPRESS CLI   0(R3),C' '               NULL?
         BE    COMPRES1                 YES
         MVC   0(1,R6),0(R3)            MOVE IN CHAR
         LA    R6,1(,R6)                POINT AT NEXT
COMPRES1 BXLE  R3,R4,COMPRESS           TRY AGAIN
         MVC   0(80,R6),BLANKS          INSURE A BLANK TERMINATOR
         LA    R3,INPUT                 GET START ADDRESS
         SR    R6,R3                    GET COUNT
         TITLE 'DEBUGGER - COMMAND SCAN'
         LA    R3,INPUT                 RELOAD START
         BZ    DISPLAY                  ZERO COUNT, REDISPLAY
CKEND    CLC   =CL3'END',0(R3)          END REQUEST?
         BE    LEAVE                    YES
         CLI   0(R3),C'R'               REPEAT ?
         BNE   OPSCAN                   NO
         MVC   INPUT,SAVEINPT
         L     R6,SAVER6
         SPACE 1
OPSCAN   CLC   =C'DELETE=',0(R3)       DELETE A LOAD MODULE?
         BE    DELCMD
         CLI   0(R3),C'D'               DISPLAY?
         BNE   OPSCANND                 NO
         AR    R3,R4                    INCREMENT
         SR    R6,R4                    DOWN COUNT
         BNP   DISPLAY                  THAT'S IT
         B     ASSUMED                  DO IT
OPSCANND EQU   *
         MVC   SAVEINPT,INPUT
         ST    R6,SAVER6
         CLC   =C'PF',0(R3)        PAGE FORWARD
         BE    PF
         CLC   =C'PB',0(R3)        PAGE BACKWARD
         BE    PB
         CLC   =C'HF',0(R3)        HALF PAGE FORWARD
         BE    HF
         CLC   =C'HB',0(R3)        HALF PAGE BACKWARD
         BE    HB
         CLC   =C'ASCB=',0(R3)         ASCB ID?
         BE    ASCBSCAN
         CLC   =C'CSCB=',0(R3)         CSCB ID?
         BE    CSCBSCAN
         CLC   =C'LOAD=',0(R3)         FIND A LOAD MODULE?
         BE    LOADCMD
         CLI   0(R3),C'F'               FULLWORD?
         BE    FW                       YES
         CLI   0(R3),C'H'               HALFWORD?
         BE    HW                       YES
         CLI   0(R3),C'='               DEFINE A NAME?
         BE    DEFINE                   YES
         CLI   0(R3),C'>'               TRACE?
         BE    FORWARD
         CLI   0(R3),C'<'               TRACE?
         BE    BACK                     YES
         CLI   0(R3),C'C'               COMPLEMENT?
         BE    COMPL                    YES
         CLC   =CL3'UCB',0(R3)          UCB COMMAND?
         BE    UCB
         CLC   =CL2'?T',0(R3)           TRACE TABLE?
         BE    QT                       YES
         CLC   =CL2'?N',0(R3)           NAME TABLE?
         BE    QN                       YES
         CLC   =CL2'?C',0(R3)           CSCB LOOK?
         BE    QC                       YES
         CLI   0(R3),C'+'               ADD?
         BE    ADD
         CLI   0(R3),C'-'               SUBTRACT?
         BE    SUBTRACT                 YES
         CLI   0(R3),C' '               END?
         BE    DISPLAY                  YES
         CLI   0(R3),C','               SEPARATOR?
         BE    UPPTR                    YES
ASSUMED  BAL   R14,MAKENUM              IS THIS NUMBER?
         B     OPGETNM                  NO... TRY AGAIN
         B     UPPTR                    DO IT
OPGETNM  BAL   R14,GETNAME              SEE IF A NAME?
         B     ANERROR                  ERROR
         MVC   DPLYADDR(4),WHERE        MOVE IN ID
         SPACE 1
UPPTR    CLI   0(R3),C'+'               SPECIAL CASE
         BE    OPSCAN                   YES
         CLI   0(R3),C'-'               SPECIAL CASE?
         BE    OPSCAN                   YES
         AR    R3,R4                    INCREMENT POINTER
         SH    R6,=H'1'                 LESS ONE
         BP    OPSCAN                   TRY AGAIN
         B     DISPLAY                  DO DISPLAY THING
ADDRESS  BAL   R14,MAKENUM              GO TO ROUTINE
         B     OPGETNM                  NOT VALID NUMBER
         B     UPPTR                    UP IT
         SPACE 3
ANERROR  TPTI  INPUT,80                 PUT OUT ERROR MESSAGE
         MVI   INPUT,C' '               BLANK INPUT
         MVC   INPUT+1(L'INPUT-1),INPUT
         MVI   0(R3),C'×'
         TPTI  INPUT,L'INPUT
         TPTI  MSG02,L'MSG02            PUT OUT ERROR MESSAGE
         SPACE
FLUSH    XC    TSOECB,TSOECB                                *HMD 11/79*
         STACK PARM=STPB,DELETE=ALL,MF=(E,IOPL)             *HMD 11/79*
TRYMSG15 TM    ABENDFLG,MSG15FLG        REQUEST FOR MSG15?  *HMD 11/79*
         BZ    TGET                                         *HMD 11/79*
         TPTI  MSG15,L'MSG15            PUT OUT MSG15       *HMD 11/79*
         B     TGET                                         *HMD 11/79*
         TITLE 'DEBUGGER - PUTLINE SUBROUTINES'             *HMD 11/79*
PUTLINE  DS    0H                                           *HMD 11/79*
         ST    14,SAVER14             SAVE REG 14.          *HMD 11/79*
         MVC   OUTAREA,BLANKS         CLEAR OUTPUT AREA     *HMD 11/79*
         LR    R2,R0                  SAVE LENGTH VALUE     *HMD 11/79*
         CH    R2,=H'79'              LONGER THAN 80 CHARS  *HMD 11/79*
         BNH   PUTLINE1               LOW OR EQUAL          *HMD 11/79*
         LA    R2,79                  LIMIT TO 80 CHARS     *HMD 11/79*
PUTLINE1 SH    R2,=H'1'               BUMP DOWN FOR EXECUTE *HMD 11/79*
         BNPR  R14                    DON'T EXECUTE PUTLINE *HMD 11/79*
         EX    R2,MVCPUTL             MOVE TO OUTPUT AREA   *HMD 11/79*
         XC    TSOECB,TSOECB          CLEAR ECB             *HMD 11/79*
         L     R15,ADDRPUTL           GET PUTLINE ENTRY PT  *HMD 11/79*
         PUTLINE PARM=PTPB,OUTPUT=(@LINE,SINGLE,DATA),      *HMD 11/79*X
               MF=(E,IOPL),ENTRY=(15)                       *HMD 11/79*
         L     R14,SAVER14            LOAD RETURN REG.      *HMD 11/79*
         BR    R14                    RETURN TO CALLER      *HMD 11/79*
         SPACE 1                                            *HMD 11/79*
PUTLINEM DS    0H                     SAME AS PUTLINEI      *HMD 11/79*
PUTLINEI DS    0H                                           *HMD 11/79*
         ST    R14,SAVER14            SAVE RETURN REG.      *HMD 11/79*
         MVC   OUTAREAI,BLANKS        CLEAR OUTPUT AREA     *HMD 11/79*
         LR    R2,R0                  SAVE LENGTH           *HMD 11/79*
         CH    R2,=H'71'              TOO LONG?             *HMD 11/79*
         BNH   PUTLINE2               NO, CONTINUE          *HMD 11/79*
         LA    R2,71                  LIMIT LENGTH          *HMD 11/79*
PUTLINE2 SH    R2,=H'1'               BUMP DOWN FOR EX      *HMD 11/79*
         BNPR  R14                    IF NOGOOD, LEAVE      *HMD 11/79*
         XC    TSOECB,TSOECB          CLEAR EVENT CNTL BLK  *HMD 11/79*
         EX    R2,MVCPUTLI            MOVE PUTLINE INFORM   *HMD 11/79*
         L     R15,ADDRPUTL           GET PUTLINE ENTRY PT  *HMD 11/79*
         PUTLINE PARM=PTPB,OUTPUT=(OLDIE,TERM,SINGLE,INFOR),           X
               MF=(E,IOPL),ENTRY=(15)                       *HMD 11/79*
         L     R14,SAVER14                                  *HMD 11/79*
         BR    R14                    RETURN TO CALLER      *HMD 11/79*
         SPACE 1                                            *HMD 11/79*
SAVER14  DC    F'0'                   FOR SAVING R14.       *HMD 11/79*
OLDIE    DC    F'1'                   INDICATE 1 SEGMENT    *HMD 11/79*
         DC    A(@LINEI)              POINT TO LINE SEG     *HMD 11/79*
@LINE    DC    AL2(83)                                      *HMD 11/79*
         DC    AL2(0)                                       *HMD 11/79*
OUTAREA  DC    CL80' '                OUTPUT AREA           *HMD 11/79*
@LINEI   DC    AL2(83)                OUTPUT LINE           *HMD 11/79*
         DC    AL2(0)                                       *HMD 11/79*
OUTID    DC    C'HMD456I '                                 *HMD 11/79*
OUTAREAI DC    CL(79-(*-OUTID))' '    INFORM LINE           *HMD 11/79*
MVCPUTL  MVC   OUTAREA(0),0(R1)       MOVE MESSAGE TO AREA  *HMD 11/79*
MVCPUTLI MVC   OUTAREAI(0),0(R1)      MOVE MESSAGE TO AREA  *HMD 11/79*
         TITLE 'DEBUGGER - MAKENUM ROUTINE'
MAKENUM  STM   R3,R15,SA2+12             SAVE USED REGS
         XR    R15,R15                  CLEAR COUNT
         MVI   COUNT+1,0                SAME
         BAL   R14,CHECKNXT             SEE IF FIRST IS IT
         B     MAKENUM1                 NOT IT
         B     CVRETRN                  YES, IMMED. RETURN
         SPACE 1
MAKENUM1 CLI   0(R3),C'*'               THIS 'HERE'?
         BE    CVRETRN                  YES... JUST RETURN
         XR    R7,R7                    TEMP
         SR    R8,R8
         XR    R9,R9
NLOOP    CLI   0(R3),C'.'               DECIMAL?
         BE    DEC                      YES
         BAL   R14,CHECKNXT             CHECK FOR DELIMETER
         B     NXTNUM                   NOT A DELIMETER
         B     NBLNK                    SIMULATE END OF STRING
NXTNUM   LA    R15,1(,R15)              UP COUNT
         IC    R7,0(R3)                 GET CHAR
         N     R7,=F'15'                STRIP ZONE
         CLI   0(R3),C'0'               A NUMBER
         BNL   PNUM                     NOT HEX
         CLI   0(R3),C'A'               CHECK IT
         BL    CVERROR                  TEMP
         CLI   0(R3),C'F'               CHECK IT
         BH    CVERROR                  TEMP
         LA    R7,9(,R7)                SCALE IT
PNUM     SLL   R9,4                     HEX BY 16
         MH    R8,=H'10'                DECIMAL
         AR    R8,R7                    ADD NUMBER TO BOTH
         AR    R9,R7                    SAME
         AR    R3,R4                    ADD ONE
         SR    R6,R4
         BP    NLOOP                    TRY AGAIN
NBLNK    ST    R9,DPLYADDR
         STH   R15,COUNT                SAVE CHAR COUNT
         B     CVRETRN
DEC      LTR   R15,R15                  TEST FOR JUST A '.'
         BNP   CVERROR                  INVALID INPUT
         MVI   COUNT+1,8                ALL DECIMAL IS 4 BYTES
         ST    R8,DPLYADDR              SAVE CONTENTS OF DEC
         SPACE 1
CVRETRN  LM    R7,R15,SA2+28             GET REGS BACK
         B     4(,R14)                  RETURN GOOD
         SPACE 1
CVERROR  LM    R3,R15,SA2+12             GET REGS BACK
         BR    R14                      RETURN TO CALLER
         TITLE 'DEBUGGER - GETNAME SUBROUTINE'
GETNAME  STM   R1,R9,08(R13)            SAVE REGS
         LM    R4,R5,@NAMECB+4          LOAD NAME CTL BLECHS
         LR    R7,R3                    SAVE POINTER
         LA    R3,@NAME                 FIRST NAME ENTRY
         SPACE 1
         LA    R8,1                     INCREMENT
         LA    R9,INPUT+L'INPUT-1       LAST POSSIBLE CHAR
         LA    R2,8                     MAXIMUM LENGTH
         MVC   CURNAME(8),BLANKS
         LA    R1,CURNAME               GET NAME
         SPACE 1
GETNAME1 CLI   0(R7),C' '               BLANK?
         BE    GETNAMEB                 YES
         CLI   0(R7),C','               TERMINATOR?
         BE    GETNAMEB                 YES
         CLI   0(R7),C'+'               SPECIAL CASE
         BE    GETNAMEB                 YES
         CLI   0(R7),C'-'               SPECIAL CASE
         BE    GETNAMEB
         CLI   0(R7),C'A'               CHECK FOR VALID
         BNL   GETNAMEN                 GET NEXT
         CLI   0(R7),C'#'
         BE    GETNAMEN
         CLI   0(R7),C'/'               FOR STRANGE NAMES
         BE    GETNAMEN                 OK, CONTINUE
         CLI   0(R7),C'@'               ANOTHER?
         BE    GETNAMEN                 YES... CONTINUE
         CLI   0(R7),C'$'
         BNE   GETNAMEE                 ERROR
         SPACE 1
GETNAMEN SH    R2,=H'1'
         BCTR  R6,0                     SUBTRACT ONE
         BM    GETNAMEE                 OOPS
         MVC   0(1,R1),0(R7)            MOVE THIS CHARACTER
         AR    R1,R8                    UP PTR BY ONE
         BXLE  R7,R8,GETNAME1           TRY AGAIN
         SPACE 1
GETNAMEB CLC   CURNAME(8),4(R3)         IS THIS IT?
         BE    GETNAMEO                 YES
         BXLE  R3,R4,GETNAMEB           TRY AGAIN
         SPACE 1
GETNAMEE MVI   WHERE,X'FF'              INDICATE ERROR
         B     GETNAMEQ
         SPACE 1
GETNAMEO MVI   0(R3),X'00'              CLEAR HI BYTE (FOR SURE..)
         MVC   WHERE(4),0(R3)           MOVE IN ADDRESS
GETNAMEQ LR    R3,R7                    GET NEXT POINTER
         L     R2,12(,R13)              RELOAD REGS
         LM    R4,R5,20(R13)
         LM    R7,R9,32(R13)            REST
         CLI   WHERE,X'FF'              ERROR?
         BER   R14                      YES
         B     4(,R14)                  RETURN GOOD
         EJECT
DOBITS   EQU   *
DOCHARS  EQU   *
NOTIMPL  TPTI  MSG03,L'MSG03            PUT ERROR MESSAGE
         B     FLUSH
         TITLE 'DEBUGGER - DEFINE ROUTINE'
DEFINE   AR    R3,R4                    INCREMENT POINTER
         SR    R6,R4
         BNP   DEFCLEAR                 CLEAR IT
         CLI   0(R3),C','               CLEAR IT?
         BE    DEFCLEAR                 YES
         L     R15,DPLYADDR             LOAD CURRENT ADDRESS
         BAL   R14,GETNAME              GET IT
         B     DEFINERR                 CHECK IT, THOUGH
DEFINEDO ST    R15,WHERE                SAVE WHERE
         BAL   R14,PUTNAME              PUT IN NAME
         B     UPPTR                    CONTINUE
         SPACE 1
DEFINERR BAL   R14,CHECKNXT             CHECK DELIMETER
         B     ANERROR                  ERROR
         B     DEFINEDO                 DO IT
         SPACE 2
DEFCLEAR BAL   R14,CLRNAME              DO IT
         AR    R3,R4                    ADD IT
         SR    R6,R4
         B     OPSCAN                   NEXT
         TITLE 'DEBUGGER - NAME AND TRACE TABLE DISPLAY'
QT       STM   R0,R15,SA2+8             SAVE REGS
         TPTI  MSG07,L'MSG07
         LM    R4,R5,@TRACECB+4         GET THAT
         LA    R3,@TRACE                START
QT1      MVI   TLINE,C' '               CLEAR LINE
         MVC   TLINE+1(79),TLINE
         LA    R6,10                    NUMBER PER LINE
         LA    R7,TLINE                 LOAD ADDRESS
         LR    R1,R7                    SAVE IT
         SPACE 1
QT2      MVC   PLACE(4),0(R3)           MOVE IN
         BAL   R14,TOHEX                MOVE IT OUT
         MVC   1(6,R7),HEX+2            MOVE IN
         C     R3,@TRACECB              THIS IT?
         BNE   *+8                      NO
         MVI   0(R7),C'*'               POINTER
         LA    R7,8(,R7)                INCREMENT
         BXLE  R3,R4,QT4                DO NEXT
         B     QTPUTL                   LAST LINE
QT4      BCT   R6,QT2                   DO IT
         LA    R0,80                    LENGTH
         L     R15,=A(PUTLINE)          GET PUTLINE ADDR
         BALR  R14,R15                  LINK TO PUTLINE     *HMD 11/79*
         B     QT1                      TRY AGAIN
         SPACE 1
QTPUTL   LA    R0,80
         L     R15,=A(PUTLINE)
         BALR  R14,R15                  LINK TO PUTLINE     *HMD 11/79*
         TPTI  MSG08,L'MSG08
         LM    R0,R15,SA2+8             CONTINUE
QT3      CLI   0(R3),C' '               UPDATE PTR
         BE    QT3R                     YES
         CLI   0(R3),C','               DETERMINATIVE?
         BE    QT3R                     YES
         AR    R3,R4                    ADD ONE
         SR    R6,R4                    SUBTRACT 1
         BP    QT3                      OK
QT3R     B     TGET                     GET NEW COMMAND
         SPACE 3
QN       STM   R0,R15,SA2+8             SAVE REGS
         TPTI  MSG09,L'MSG09
         LM    R4,R5,@NAMECB+4          GET NAME CTL
         LA    R3,@NAME                 GET NAME
QN1      LA    R7,TLINE                 LOAD LINE ADDRESS
         MVI   TLINE,C' '               CLEAR IT
         MVC   TLINE+1(79),TLINE
         LR    R1,R7                    SAVE ADDRESS
         LA    R6,4                     NUMBER PER LINE
QN2      OC    0(8,R7),4(R3)            NAME
         MVC   PLACE(4),0(R3)           MOVE IN ADDRESS
         BAL   R14,TOHEX                DO IT
         MVC   9(6,R7),HEX+2            MOVE IN REP
         LA    R7,20(,R7)               INCREMENT
         BXLE  R3,R4,QN4                TRY IT
         B     QTPUTL                   LAST
QN4      BCT   R6,QN2                   TRY AGAIN
         LA    R0,80                    LENGTH
         L     R15,=A(PUTLINE)          DO IT
         BALR  R14,R15                  LINK TO PUTLINE     *HMD 11/79*
         B     QN1                      TRY AGAIN
         TITLE 'DEBUGGER - FORWARD AND BACK ROUTINES -(TRACE)'
FORWARD  LM    R7,R9,@TRACECB           GET TRACE CTL
         BXLE  R7,R8,FORWARD1           OK
         LA    R7,@TRACE                GET START AGAIN
FORWARD1 MVC   DPLYADDR(4),0(R7)        MOVE IN TRACE ENTRY
         STM   R7,R9,@TRACECB           RESTORE
         B     UPPTR                    INCREMENT
         SPACE 3
BACK     LM    R7,R9,@TRACECB           LOAD TRACE CTL
         LA    R14,@TRACE               LOAD ADDR OF FIRST
         SR    R7,R8                    ONE LESS TRACE
         CR    R7,R14                   BELOW FIRST?
         BNL   BACK1                    NO
         LR    R7,R9                    MAKE IT LAST
BACK1    MVC   DPLYADDR(4),0(R7)        MOVE IN TRACE ENTRY
         STM   R7,R9,@TRACECB           SAVE IT
         B     UPPTR                    DO IT
         TITLE 'DEBUGGER - UCB ROUTINE'
UCB      LA    R3,3(,R3)                POINT AFTER UCB
         SH    R6,=H'3'                 LENGTH OF 'UCB'
         BNP   ANERROR                  ERROR CONDITION
         L     R2,DPLYADDR              LOAD DISPLAY ADDRESS
         BAL   R14,MAKENUM              GET IT NUMERIC
         B     UCBBAD                   NO SUCH ANIMULE
         CLI   COUNT+1,0                SAY SOMETHING LIKE UCB. OR UCB*
         BE    UCBBAD                   YES... IT'S INVALID
         CLC   DPLYADDR(4),=X'00000FFF' MAX?
         BH    UCBBAD                   OOPS
*        ICM   R15,15,DPLYADDR          LOAD AND TEST IT
         UNPK  HEX(9),DPLYADDR(5)       MAKE EBCDIC
         TR    HEX(9),TRTOHEX           IS NOW EBCDIC
*        MUST COMPARE LEN=3 ON HEX+5
         OC    DPLYADDR(4),DPLYADDR     CHECK FOR ZERO
         ST    R2,DPLYADDR
         L     R1,CVTPTR                SO FAR, IT'S ASSUMED GOOD
         BZ    UCBBAD                   ERROR CONDITION
         L     R1,CVTILK2-CVT(,R1)      GET LOOKUP TABLE
         SPACE 1
UCBGET   SR    R14,R14
         ICM   R14,B'0011',0(R1)        GET UCB ADDRESS
         LA    R1,2(,R1)                GET NEXT MEANTIME
         BZ    UCBGET                   DUMMY, GET NEXT
         CLM   R14,B'0011',=X'FFFF'     RUN OUT OF TABLE?
         BE    ANERROR                  OOPS
         SPACE 1
         CLC   HEX+5(3),13(R14)         IS THIS IT?
         BNE   UCBGET                   NO
         ST    R14,DPLYADDR             SAVE IT AS DISPLAY ADDRESS
         B     UPPTR                    DO IT
         SPACE 1
UCBBAD   ST    R2,DPLYADDR              SAVE IT
         B     ANERROR                  ERROR MAKER
         LTORG                                              *HMD 11/79*
         TITLE 'DEBUGGER - PAGE FORWARD-BACK ROUTINE'
PF       MVC   0(4,R3),=C'+130'    PAGE FORWARD
         AH    R6,=H'02'
         B     ADD
PB       MVC   0(4,R3),=C'-130'    PAGE BACKWARD
         AH    R6,=H'02'
         B     SUBTRACT
HF       MVC   0(3,R3),=C'+98'     HALF PAGE FORWARD
         AH    R6,=H'01'
         B     ADD
HB       MVC   0(3,R3),=C'-98'     HALF PAGE BACKWARD
         AH    R6,=H'01'
         B     SUBTRACT
         TITLE 'DEBUGGER - ADD AND SUBTRACT'
ADD      MVI   SW1,X'00'                SET ADD FLAG
         B     ADDSUB                   GO
SUBTRACT MVI   SW1,X'FF'                SET SUBTRACT FLAG
         SPACE 1
ADDSUB   L     R2,DPLYADDR              SAVE CURRENT ADDRESS
         AR    R3,R4                    ADD ONE TO LOC CTR
         SR    R6,R4                    SUB ONE FROM COUNT
         BNP   DISPLAY                  END
         BAL   R14,MAKENUM              GET A NUMBER
         B     ADDNAME                  NOT A NUMBER
         CLI   COUNT+1,0                ANYTHING AFTER OPERATOR?
         BNE   ADDCOM                   YES
         CLI   0(R3),C'*'               HERE REQUEST?
         BNE   UPPTR                    NO, JUST FORGET THIS OPER
         B     ADDCOM                   GO DO A +/- *
         SPACE 1
ADDNAME  BAL   R14,GETNAME              SEE IF IT IS A NAME
         B     ANERROR                  ERROR CONDITION
         MVC   DPLYADDR(4),WHERE        MOVE IN RETURNED NAME
ADDCOM   CLI   SW1,X'FF'                ADD OR SUBTRACT?
         BE    ADDENDUM                 SUBTRACT
         AL    R2,DPLYADDR              ADD CURRENT TO IT
         ST    R2,DPLYADDR              SAVE IT
         B     UPPTR                    TRY NEXT
         SPACE 1
ADDENDUM SL    R2,DPLYADDR              DO ONLY LOGICAL OPERATIONS
         ST    R2,DPLYADDR              SAVE IT
         B     UPPTR                    TRY NEXT
         TITLE 'DEBUGGER - INDIRECT ADDRESSING COMMANDS'
FW       MVI   SW2,X'00'                SET SWITCH
         B     INDIR                    GO TO IT
HW       MVI   SW2,X'01'                SET SWITCH
         B     INDIR
COMPL    MVI   SW2,X'02'                SET FLAG
         SPACE 2
INDIR    AR    R3,R4                    UP LOC CTR
         SR    R6,R4                    DOWN COUNT
         BNP   INDIRDO                  RELATIVE TO HERE
         BAL   R14,MAKENUM              MAKE IT A NUMBER?
         B     INDNAME                  NAME?
         B     INDIRDO                  NO
         SPACE 1
INDNAME  BAL   R14,GETNAME              GET A NAME
         B     ANERROR                  SORRY, CHARLIE
         MVC   DPLYADDR(4),WHERE        MOVE IN RETURNED ADDR
         SPACE 2
INDIRDO  MVI   DPLYADDR,X'00'           GET HI BYTE
         CLC   DPLYADDR(4),MZ00         IN MACHINE?
         BH    DISPLAY                  SIMULATE ERROR CONDITION
         L     R2,DPLYADDR              GET ADDRESS
         LR    R1,R2                    SAVE IT
         TM    SW2,X'01'                HALFWORD?
         BO    INDIRX                   YES... OTHERS MUST BE 1F DOWN
         LA    R2,3(,R2)                END POINTER
         C     R2,MZ00                  STILL IN MACHINE?
         BH    ANERROR                  NO
         MVC   FULLWORD(4),0(R1)        MOVE IN ADDRESS
         L     R2,FULLWORD              GET IT
         TM    SW2,X'02'                COMPL?
         BNO   *+6                      NO
         LCR   R2,R2                    COMPLEMENT IT
         ST    R2,DPLYADDR              SAVE IT
         B     UPPTR                    DO IT
         SPACE 3
INDIRX   LA    R2,1(,R2)                SEE IF IN MACHINE
         C     R2,MZ00                  CHECK IT
         BH    ANERROR                  ERROR
         XC    DPLYADDR(4),DPLYADDR     CLEAR IT
         MVC   DPLYADDR+2(2),0(R1)      MOVE IN ADDRESS
         B     UPPTR
         TITLE 'DEBUGGER - ADDRESS SPACE CONTROL BLOCK ROUTINE'
ASCBSCAN AH    R3,=H'5'                 MOVE OFF - ASCB'
         SH    R6,=H'5'                 DOWN LENGTH
         BNP   ANERROR                  NOTHING LEFT? IS BAD
         BAL   R14,GETNAME              EXTRACT NAME FROM INPUT
         NOP   ANERROR
         SPACE 1
ASCBFIND STM   R0,R15,SA2+8
         L     R1,16                    FIND CVT
         L     R1,X'22C'(,R1)           FIND ASVT
         LA    R3,X'210'(,R1)           FIND FIRST ENTRY
         LA    R4,4                     LOAD ENTRY SIZE
         L     R5,X'204'(,R1)           MAX ADDR SPACES
         SLA   R5,2                     MULT BY FOUR
         LA    R5,0(R3,R5)              FIND END OF TABLE
         BCTR  R5,0
ASCBLOOP TM    0(R3),X'FF'              NULL ENTRY?
         BNZ   ASCBBXLE                 YES SKIP
         L     R2,0(,R3)                PICK UP PNTR TO ASCB
         ICM   R1,15,172(R2)            NAME OF INIT PGMS
         BZ    *+14                     SKIP IF NO NAME
         CLC   CURNAME,0(R1)            IS THIS IT?
         BE    ASCBGOT
         ICM   R1,15,176(R2)            NAME OF START/MOUNT
         BZ    *+14                     SKIP IF NO NAME
         CLC   CURNAME,0(R1)            IS THIS IT?
         BE    ASCBGOT
ASCBBXLE BXLE  R3,R4,ASCBLOOP
         B     CSCBNOTF                 USE CSCB RTN ERROR MSG CODE
         SPACE
ASCBGOT  ST    R2,DPLYADDR
         LM    R0,R15,SA2+8
         B     UPPTR
         TITLE 'DEBUGGER - LOAD MODULE DELETE ROUTINE'
DELCMD   AH    R3,=H'7'                 MOVE OFF - DELETE'
         SH    R6,=H'7'                 DOWN LENGTH
         BNP   ANERROR                  NOTHING LEFT? IS BAD
         BAL   R14,GETNAME              EXTRACT NAME FROM INPUT
         NOP   ANERROR
         SPACE 1
         DELETE EPLOC=CURNAME
         SPACE 1
         MVC   MSG16(8),CURNAME                             *HMD 11/79*
         TPTI  MSG16,L'MSG16            PUT OUT MSG         *HMD 11/79*
         XC    DPLYADDR,DPLYADDR        PREVENT 0C4         *HMD 11/79*
         B     TGET                     CONTINUE
         TITLE 'DEBUGGER - LOAD MODULE DISPLAY ROUTINE'
LOADCMD  AH    R3,=H'5'                 MOVE OFF - LOAD'
         SH    R6,=H'5'                 DOWN LENGTH
         BNP   ANERROR                  NOTHING LEFT? IS BAD
         BAL   R14,GETNAME              EXTRACT NAME FROM INPUT
         NOP   ANERROR
         SPACE 1
         LOAD  EPLOC=CURNAME,ERRET=CSCBNOTF
         ST    R0,DPLYADDR              SAVE EP ADDR        *HMD 11/79*
         SPACE 1
         B     UPPTR                    CONTINUE
         SPACE 5
* CDE  DISPLAY ROUTINE
         SPACE 1
QC       STM   R0,R15,SA2+8
         STSIZE SIZELOC=SCRSAVE+4,LINELOC=SCRSAVE  SET SMALL SCREEN
         L     R5,X'21C'                FIND MY TCB
         ICM   R5,15,X'24'(R5)          FIND FIRST LLE
         BNZ   LLETITLE
         TPTI  =C'* NO LOADED MODULES FOUND *',27
         B     TGET
         SPACE 1
LLETITLE TPT   =C'MOD NAME  EP/MAJOR  START ADR   LEN',35
LLELOOP  MVI   TLINE,C' '               BLANK LINE
         MVC   TLINE+1(79),TLINE
         SPACE
         L     R7,4(,R5)                GO TO CDE FROM LLE
         MVC   TLINE(8),8(R7)           MOVE IN NAME
         UNPK  TLINE+10(9),16(5,R7)     CONVERT ENTRY PT
         TR    TLINE+10(8),TRTOHEX
         MVI   TLINE+18,C' '            ERASE GARBAGE CHARACTER
         SPACE 1
         TM    28(R7),X'04'             IS THIS A MINOR CDE?
         BZ    C4XTNTL                  NO, CHECK FOR EXTENT LIST
         L     R6,20(,R7)               GO TO MAJOR CDE
         MVC   TLINE+20(8),8(R6)        MOVE IN MAJOR NAME
         B     NOXTNT
         SPACE
C4XTNTL  TM    29(R7),X'20'             EXTENT LIST PRES?
         BZ    NOXTNT
         SPACE
         L     R6,20(,R7)               GO TO EXTENT LIST
         UNPK  TLINE+20(9),12(5,R6)     STARTING ADDRESS
         TR    TLINE+20(8),TRTOHEX
         MVI   TLINE+28,C' '
         UNPK  TLINE+30(7),9(4,R6)      MODULE LENGTH
         TR    TLINE+30(6),TRTOHEX
         MVI   TLINE+36,C' '
         SPACE 1
NOXTNT   TPT   TLINE,80
         ICM   R5,B'1111',0(R5)         FOLLOW LLE CHAIN
         BNZ   LLELOOP
         B     TGET
         TITLE 'DEBUGGER - CSCB DISPLAY ROUTINE'
CSCBSCAN AH    R3,=H'5'                 MOVE OFF - CSCB'
         SH    R6,=H'5'                 DOWN LENGTH
         BNP   ANERROR                  NOTHING LEFT? IS BAD
         BAL   R14,GETNAME              EXTRACT NAME FROM INPUT
         NOP   ANERROR
         SPACE 1
CSCBFIND STM   R0,R15,SA2+8
         L     R1,16                    FIND CVT
         L     R2,X'94'(,R1)            FIND MSRDA (BASEA)
CSCBLOOP ICM   R2,15,0(R2)              GO TO NEXT CSCB
         BZ    CSCBNOTF                 IF NONE
         CLC   CURNAME,8(R2)
         BE    CSCBGOT
         CLC   CURNAME,16(R2)
         BE    CSCBGOT
         B     CSCBLOOP
         SPACE
CSCBNOTF TPTI  MSG11,L'MSG11
         TPTI  CURNAME,8
         XC    DPLYADDR,DPLYADDR        CLEAR DISPLAY ADDR  *HMD 11/79*
         B     FLUSH
         SPACE
CSCBGOT  ST    R2,DPLYADDR
         LM    R0,R15,SA2+8
         B     UPPTR
         SPACE 3
         LTORG
         TITLE 'DEBUGGER - CONSTANTS AND MESSAGES'          *HMD 11/79*
TLINE    DC    CL80' '
SCREEN   DC    19CL80' '                OUTPUT LINES
         SPACE
GETPARM  GETLINE MF=L                   GETLINE PARM BLOCK  *HMD 11/79*
PTPB     PUTLINE MF=L                   PUTLINE PARM BLOCK  *HMD 11/79*
STPB     STACK MF=L
GETECB   DC    F'0'
GETLRC   DC    X'00'                    SAVE AREA FOR GETLINE RET CODE
ABENDFLG DC    X'00'                                        *HMD 11/79*
MSG15FLG EQU   X'40'                    PUT MSG15 TO USER   *HMD 11/79*
INPUT    DC    CL80' '                  INPUT LINE
BLANKS   DC    CL80' '
SAVER6   DS    F
SAVEINPT DC    CL80' '                  SAVE'D INPUT
CLEAR    DC    X'C11140403C4040001140C113'
OLDPROM  DC    A(1)                     NUMBER OF SEGMENTS  *HMD 11/79*
         DC    A(MSG01)                 ADDRESS OF SEGEMNT  *HMD 11/79*
         DC    A(0)                     DUMMY SEGMENT       *HMD 11/79*
MSG01    DC    AL2(48),AL2(0)                               *HMD 11/79*
         DC    C'- DEBUGGER RUNNING - ENTER COMMAND          '
MSG02    DC    C'- INVALID INPUT OR SYNTAX, RE-ENTER - '
MSG03    DC    C'- SORRY, COMMAND NOT IMPLEMENTED - '
MSG04    DC    C'- ''XXXXXXXX'' REDEFINED - '
MSG05    DC    C'- ''XXXXXXXX'' ENTRY REUSED - '
MSG07    DC    C'-  TRACE TABLE, * = CURRENT - '
MSG08    DC    C'-      END OF ? REQUEST     - '
MSG09    DC    C'-       NAME   TABLE        -'
MSG10    DC    C'- DEBUGGER - STORAGE FROM '
MSG10A   DC    C'XXXXXXXX DISPLAYED - '
MSG10L   EQU   *-MSG10
MSG11    DC    C'-  NAME NOT FOUND -'
MSG15    DC    C'XXXXXXXX <- ABEND PREVENTED -'
MSG16    DC    C'XXXXXXXX DELETED'                          *HMD 11/79*
         SPACE 3
@NAMECB  DC    A(@NAME,12,@NAMEND)
@NAME    DC    35XL12'00'               NAME TABLE/36 ENTRIES
@NAMEND  DC    XL12'00'
         SPACE 2
@TRACECB DC    A(@TRACE,4,@TRACEND)
@TRACE   DC    29XL4'00'                TRACE TABLE/30 ENTRIES
@TRACEND DC    XL4'00'
         SPACE 3
SCRSAVE  DC    2F'0'                    SCREEN SIZE SAVE AREA
BIGSAVE  DC    2F'0'              *BIG* SCREEN SIZE SAVE AREA
DPLYADDR DC    A(0)                    DISPLAY ADDRESS
SWITCH   DC    X'00'                    SWITCHES
SW1      DC    X'00'
SW2      DC    X'00'
SW3      DC    X'00'
         EJECT
TRTOCHAR DC    256C'.'                  BASIC TRANSLATE TABLE
TRTOCHAR TRX   C' '
         TRX   C'.',C'.<(+×&&'
         TRX   C'$',C'$*);^-/'
         TRX   C','
         TRX   X'6C'                    PERCENT SIGN
         TRX   C'_',C'_>?'
         TRX   C':',C':#@''='
         TRX   X'7F'                    DOUBLE QUOTE
         TRX   C'A',C'ABCDEFGHI'
         TRX   C'J',C'JKLMNOPQR'
         TRX   C'S',C'STUVWXYZ'
         TRX   C'0',C'0123456789'
         EJECT
COUNT    DC    H'0'                     CURRENT COUNT
HEX      DC    5D'0'
PLACE    DC    3D'0'
FULLWORD DC    2F'0'                    USED FOR MOVE
STOPAT   DC    A(0)                     CVTMZ00 TO A DOUBLE-WORD
MZ00     DC    A(0)                     CVTMZ00
SA1      DC    18A(0)                   SAVE AREA
SA2      DC    18A(0)                   SAVE AREA
SA141    DC    A(0)                     RET. REG SAVE AREA  *HMD 11/79*
SA142    DC    A(0)                     RET. REG SAVE AREA  *HMD 11/79*
WHERE    DC    A(0)                     CURRENT PLACE
CURNAME  DC    CL8' '                   CURRENT NAME
TRTOHEX  EQU   *-C'0'
         DC    C'0123456789ABCDEF'
* DAIR PARAMETER LIST                                       *HMD 11/79*
DAIR2C   DS    0F                                           *HMD 11/79*
DA2CCD   DC    X'002C'                                      *HMD 11/79*
DA2CFLG  DC    X'0001'                                      *HMD 11/79*
DA2CTCB  DC    A(0)                                         *HMD 11/79*
DA2CDDN  DC    CL8'SYSHELP'                                 *HMD 11/79*
         SPACE 1                                            *HMD 11/79*
* EPLOCS FOR LOAD ROUTINES                                  *HMD 11/79*
IKJPUTL  DC    CL8'IKJPUTL'                                 *HMD 11/79*
IKJGETL  DC    CL8'IKJGETL'                                 *HMD 11/79*
IKJDAIR  DC    CL8'IKJDAIR'                                 *HMD 11/79*
IKJSCAN  DC    CL8'IKJSCAN'                                 *HMD 11/79*
         TITLE 'DEBUGGER - SOME NEEDED DSECTS'
TSODSECT DSECT                         RESUME USER DSECT    *HMD 11/79*
CSOAD    DS     3F                     COMMAND SCAN FLAGS   *HMD 11/79*
ADDRPUTL DS     F          ADDRESS OF IKJPUTL               *HMD 11/79*
ADDRGETL DS     F          ADDRESS OF IKJGETL               *HMD 11/79*
ADDRDAIR DS     F          ADDRESS OF IKJDAIR               *HMD 11/79*
ADDRSCAN DS     F          ADDRESS OF IKJSCAN               *HMD 11/79*
ADDRJSCB DS     F          ADDRESS OF JOB STEP CNTL BLOCK   *HMD 11/79*
DSECTL   EQU    *-TSOUSER              LENGTH OF DSECT      *HMD 11/79*
         EJECT                                              *HMD 11/79*
CVT      DSECT
         CVT    LIST=YES
         EJECT                                              *HMD 11/79*
         IKJTCB LIST=YES
         EJECT                                              *HMD 11/79*
         IKJCSOA                                            *HMD 11/79*
         EJECT                                              *HMD 11/79*
         IKJECT
         EJECT
         IHASDWA ,                      FOR DIAG WORK AREA (ESTAE)
         END   DEBUGGER
