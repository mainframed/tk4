LOWERCSE TITLE 'CONVERT AN ALL UPPER CASE FILE TO UPPER AND LOWER'
*---------------------------------------------------------------------*
* THIS PROGRAM IS DESIGNED TO CONVERT A TEXT FILE OF FIXED LENGTH     *
* RECORDS UP TO LRECL 150 FROM ALL UPPER CASE INTO A UPPER AND        *
* LOWER CASE TEXT FILE.  THE PROGRAM WILL RECOGNIZE THE FIRST LETTER  *
* OF EACH SENTENCE AS WELL AS ASSUME THAT THE FIRST LETTER OF THE     *
* FIRST SENTENCE AFTER A BLANK LINE AND CONVERT IT TO UPPER CASE.     *
* IT WILL ALSO RECOGNIZE THE WORD I.                                  *
*                                                                     *
* TO PROVIDE THE USER WITH FLEXIBILITY THE PROGRAM HAS TWO SPECIAL    *
* INPUT FILES TO ALLOW THE USER TO SPECIFY WORDS REQUIRING ALL CAPS   *
* AND THOSE WORDS WHERE JUST THE FIRST LETTER IS TO BE CAPPED.        *
*                                                                     *
* THE JCL TO EXECUTE LOWERCSE ARE:                                    *
*                                                                     *
*       //STEPLOW EXEC PGM=LOWERCSE                                   *
*       //UPPERIN  DD DSN= UPPER CASE INPUT FILE                      *
*       //LOWEROUT DD DSN= UPPER AND LOWER OUTPUT FILE                *
*       //CAPSIN   DD *                                               *
*        WORDS WHERE THE FIRST LETTER WILL BE CAPPED SEPERATED BY     *
*        BLANKS OR COMMAS.                                            *
*       //WORDIN   DD *                                               *
*        WORDS WHERE ALL THE LETTERS WIL BE CAPPED SEPERATED BY       *
*        BLANKS OR COMMAS.                                            *
*---------------------------------------------------------------------*
* THERE ARE 2 USER ABEND CODES:  1000 MEANS THAT THE ALL CAPS WORD    *
*                                TABLE IS FULL.  YOU WILL HAVE TO     *
*                                RE-ASSEMBLE AFTER CHANGING THE TABLE *
*                                SIZE AT LABEL WORDTBLS.              *
*                                                                     *
*                                2000 MEANS THAT THE FIRST CAP WORD   *
*                                TABLE FILLED UP AND YOU WILL HAVE    *
*                                TO CHANGE IT AT LABEL CAPSTBLS.      *
*                                                                     *
* NOTE: THE WORD TABLE HAS 500 ENTRIES.                               *
*       THE CAPS FIRST TABLE HAS 300 ENTRIES.                         *
*---------------------------------------------------------------------*
* MACROS USED ARE: $PROLOG, $EPILOG,  OPEN, CLOSE, BITSW,             *
*                  IFON, IFOFF, SETON, SETOF, GETMAIN, FREEMAIN, GET, *
*                  DCB, ABEND                                         *
*---------------------------------------------------------------------*
* VERSION 1.0 10-01-82                                                *
*---------------------------------------------------------------------*
* THIS PROGRAM WAS WRITTEN BY   LIONEL DYCK                           *
*                               ROCKWELL INTERNATIONAL                *
*                               PO BOX 2515                           *
*                               SEAL BEACH, CALIF 90740               *
*                               (213) 594-1125                        *
*---------------------------------------------------------------------*
         EJECT
LOWERCSE $PROLOG
         OPEN  WORDIN
         GETMAIN R,LV=WORDTBLS
         ST    R1,WORDTBL
         A     R1,=A(WORDTBLS)
         SH    R1,WORDLEN
         ST    R1,WORDTBLE
         LM    R5,R7,WORDTBL
         USING WORDS,R5
         LA    R8,WORD
         OC    WORD,BLANKS
READW    GET   WORDIN
         SETON WORDB
         SR    R9,R9
         LR    R3,R1
         LH    R4,WORDIN+X'52'  LOAD LRECL
         EX    R4,CAPALL
         AR    R4,R3            -> END OF CARD
GWLOOP   EQU   *
         CLI   0(R3),C'A'
         BL    NOTWORD
         IFON  WORDB,CHARMV
         BXLE  R5,R6,CHARMVN
         ABEND 1000             END OF TABLE
CHARMVN  LA    R8,WORD
         OC    WORD,BLANKS
         SETON WORDB
CHARMV   MVC   0(1,R8),0(R3)
         LA    R9,1(R9)
INCRGW   LA    R3,1(R3)
         LA    R8,1(R8)
         CR    R3,R4
         BL    GWLOOP
         BXLE  R5,R6,BXLEW
BXLEW    LA    R8,WORD
         OC    WORD,BLANKS
         B     READW
NOTWORD  EQU   *
         IFOFF WORDB,INCRGW
         STH   R9,WORDLNGH
         SR    R9,R9
         SETOF WORDB
         B     INCRGW
WORDEOF  EQU   *
         IFOFF WORDB,CLOSEW
         STH   R9,WORDLNGH
CLOSEW   CLOSE WORDIN
         EJECT
         OPEN  CAPSIN
GETMAINC GETMAIN R,LV=CAPSTBLS
         ST    R1,CAPTBL
         A     R1,=A(CAPSTBLS)
         S     R1,CAPLEN
         ST    R1,CAPTBLE
         LM    R5,R7,CAPTBL
         LA    R8,WORD
         OC    WORD,BLANKS
READC    GET   CAPSIN
         SETON WORDB
         SR    R9,R9
         LR    R3,R1
         LH    R4,CAPSIN+X'52'  LOAD LRECL
         EX    R4,CAPALL
         AR    R4,R3            -> END OF CARD
GCLOOP   EQU   *
         CLI   0(R3),C'A'
         BL    NOTCAPS
         IFON  WORDB,CHARMVC
         BXLE  R5,R6,CHARMVCN
         ABEND 2000             END OF TABLE
CHARMVCN LA    R8,WORD
         OC    WORD,BLANKS
         SETON WORDB
CHARMVC  MVC   0(1,R8),0(R3)
         LA    R9,1(R9)
INCRGC   LA    R3,1(R3)
         LA    R8,1(R8)
         CR    R3,R4
         BL    GCLOOP
         BXLE  R5,R6,BXLEC
BXLEC    LA    R8,WORD
         OC    WORD,BLANKS
         B     READC
NOTCAPS  EQU   *
         IFOFF WORDB,INCRGC
         STH   R9,WORDLNGH
         SR    R9,R9
         SETOF WORDB
         B     INCRGC
CAPSEOF  EQU   *
         IFOFF WORDB,CLOSEC
         STH   R9,WORDLNGH
CLOSEC   CLOSE CAPSIN
         B     OPEN
         EJECT
OPEN     OPEN  (UPPERIN)
         OPEN  (LOWEROUT,OUTPUT)
         SETON FIRST,WORDB
READ     GET   UPPERIN
         LR    R9,R1            SAVE INPUT ADDR
         LR    R3,R1            SAVE INPUT ADDR
         LH    R4,UPPERIN+X'52' LOAD LRECL
         EX    R4,CAPALL
         AR    R4,R3            -> END OF RECORD
         IFOFF FIRST,LOOP       BYPASS IF NOT FIRST
         CLC   0(2,R3),=C'I '   FIRST WORD IS I ???
         BE    INCR             YES - LEAVE UP
LOOP     EQU   *
         CLC   0(2,R3),=C'//'   JCL IMAGE
         BE    PUTITJ           ACCEPT REST OF RECORD AS ALL UPPER
         CLI   0(R3),C'.'       END OF SENTENCE
         BE    ENDOSENT         YES
         CLC   =C' I',0(R3)
         BE    DOI
         CLI   0(R3),C' '       BLANK
         BE    BLANK            YES
         CLI   0(R3),C'A'       NOT INTERESTED ?
         BL    INCRA
         CLI   0(R3),C'Z'       NOT INTERESTED ?
         BH    INCRA
IFWRD    IFOFF WORDB,SETLOW
         LM    R5,R7,WORDTBL
WLOOP    EQU   *
         CLI   WORD,C' '        END OF TABLE
         BE    CAPIF
         LH    R2,WORDLNGH
         BCTR  R2,R0
         LR    R1,R3
         AR    R1,R2
         CLI   1(R1),C'A'
         BL    EXCLC
         CLI   1(R1),C'Z'
         BL    WBXLE
EXCLC    EX    R2,CLC
         BE    LEAVEUP          BYPASS LOWER CASE SET
WBXLE    BXLE  R5,R6,WLOOP
CAPIF    IFON  FIRST,INCR       BYPASS LOWER SHIFT THIS CHAR
         LM    R5,R7,CAPTBL
CAPSLP   EQU   *
         CLI   WORD,C' '        END OF TABLE
         BE    SETLOW           SET IF LOW
         LH    R2,WORDLNGH
         BCTR  R2,R0
         LR    R1,R3
         AR    R1,R2
         CLI   1(R1),C'A'
         BL    CEXCLC
         CLI   1(R1),C'Z'
         BL    CBXLE
CEXCLC   EX    R2,CLC
         BE    INCR             LEAVE 1ST CAP
CBXLE    BXLE  R5,R6,CAPSLP
SETLOW   NI    0(R3),X'BF'      MAKE LOWER
INCR     LA    R3,1(R3)
         SETOF FIRST,WORDB
         SETON FOUND
CR       CR    R4,R3
         BE    PUTIT
         B     LOOP
INCRA    LA    R3,1(R3)
         B     CR
ENDOSENT SETON FIRST
BLANK    SETON WORDB
         LA    R3,1(R3)
         B     CR
DOI      EQU   *
         IFON  FIRST,DOIT       FIRST TEST
         CLI   2(R3),C'A'       IS IT A WORD
         BNL   BLANK            PROBABLY
DOIT     LA    R3,1(R3)         LEAVE UPPER
         B     INCR
LEAVEUP  EQU   *
         AR    R3,R2
         B     INCR
PUTITJ   SETON FIRST
PUTIT    PUT   LOWEROUT,0(R9)
         SETON WORDB
         IFON  FOUND,DFOUND
         SETON FIRST
         B     READ
DFOUND   SETOF FOUND
         B     READ
EOF      CLOSE (UPPERIN,,LOWEROUT)
         FREEMAIN R,LV=WORDTBLS,A=WORDTBL
         FREEMAIN R,LV=CAPSTBLS,A=CAPTBL
         $EPILOG
WORDTBL  DC    A(0)
WORDLEN  DC    A(LEN)
WORDTBLE DC    A(0)
CAPTBL   DC    A(0)
CAPLEN   DC    A(LEN)
CAPTBLE  DC    A(0)
         SPACE
LEN      EQU   22               LENGTH OF WORD ENTRY
WORDTBLS EQU   500*LEN
CAPSTBLS EQU   300*LEN
         SPACE
ALLLOW   NC    0(0,R3),ALLOFF
CLC      CLC   0(0,R3),WORD
CAPALL   OC    0(0,R3),BLANKS
ALLOFF   DC    150X'BF'
BLANKS   DC    CL150' '
         BITSW FIRST,FOUND,WORDB
         LTORG
         EJECT
UPPERIN  DCB   DSORG=PS,MACRF=GL,DDNAME=UPPERIN,EODAD=EOF
LOWEROUT DCB   DSORG=PS,MACRF=PM,DDNAME=LOWEROUT
WORDIN   DCB   DSORG=PS,MACRF=GL,DDNAME=WORDIN,EODAD=WORDEOF
CAPSIN   DCB   DSORG=PS,MACRF=GL,DDNAME=CAPSIN,EODAD=CAPSEOF
         EJECT
         DSECT
WORDS    DS    0F
WORDLNGH DS    H
WORD     DS    CL20
         END
