CHKVTOC  $PROLOG R12
         EJECT
         SPACE 3
         LA    R8,DSCB
         USING DSCBD,R8
         EJECT
         TIME  DEC
         SPACE
         DATEROU
         MVC   DATE(8),0(R1)       MOVE DATE TO HEAD
         EJECT
**********************************************************
*
*        THIS PROGRAM WILL SCAN THE UCB CHAIN
*        AND REPORT ON THE TOTAL FREE SPACE
*        LARGEST CONTIGOUS SPACE,
*        AND THE PERCENT OF SPACE FREE
*        FOR EVERY MOUNTED DASD
*
*        THIS IS ACCOMPLISHED THRU THE USE OF
*        SVC 78
*
*        THE JCL TO EXECUTE THIS PROGRAM ARE:
*
*        //STEP EXEC PGM=CHKVTOC
*        //SYSPRINT DD SYSOUT=A
*        //DISK DD UNIT=SYSDA,VOL=SER=(ANY PACK),DISP=SHR
*
***********************************************************
         EJECT
         OPEN  (OUT,OUTPUT)
         BAL   R9,HEAD
         SPACE
         SR    R4,R4
         EJECT
UCB      EQU   *
         L     R3,16               CVT POINTER
         L     R3,40(R3)           UCB LOOKUP TABLE POINTER
         SPACE
UCBLP    EQU   *
*
*        NOW DO SOME WORK
*
         LH    R2,0(R3)            LOAD UCB ADDRESS INTO R2
*
         N     R2,=X'0000FFFF'
         BZ    UCBSKIP
         C     R2,=X'0000FFFF'
         BE    EXIT
*
         CR    R2,R4               LOW ADDRESS?
         BL    UCBSKIP             YES SKIP IT
         LR    R4,R2               SAVE NEW ADDRESS
*
         CLC   24(4,R2),=F'00'     UCB FOR VIRTUAL DEVICE ? NO VTOC
         BE    UCBSKIP             YES
*
         CLI   18(R2),X'20'        DASD DEVICE?
         BNE   UCBSKIP
         LR    R0,R2               LOAD UCB ADDRESS INTO R0
         LA    R1,WORK             LOAD STORAGE ADDRESS
         SVC   78                  ISSUE DISP SPACE SVC
*
         CLC   WORK(6),=XL6'00'    NULL UCB
         BE    UCBSKIP
*
         TM    3(R2),X'80'         ONLINE?
         BZ    UCBSKIP             NO - IGNORE IT
*
         TM    6(R2),X'40'         NOT READY?
         BO    UCBSKIP             YES - IGNORE IT
*
         CLI   19(R2),X'09'        3330?
         BE    D3330
         CLI   19(R2),X'0D'        3330-1
         BE    D33301
         CLI   19(R2),X'0B'        3350?
         BE    D3350                                      *LBD 08/84*
         CLI   19(R2),X'0E'            3380               *LBD 08/84*
         BNE   UCBSKIP             NOT A VALID DASD
         EJECT
*        ASSUME 3380                                      *LBD 08/84*
D3380    DS    0H
         ZAP   TOTCYL,=P'13475'    TRKS PER 3380
         ZAP   TRK,=P'15'          TRKS PER CYL
         MVC   DEV(6),=CL6'3380'
         ZAP   WRKB,A3380          MAX ALTS ON 3380
         B     COMP
D3350    DS    0H
         ZAP   TOTCYL,=P'16650'    TRKS PER 3350
         ZAP   TRK,=P'30'          TRKS PER CYL
         MVC   DEV(6),=CL6'3350'
         ZAP   WRKB,A3350          MAX ALTS ON 3350
         B     COMP
D3330    ZAP   TOTCYL,=P'7676'     TRKS PER 3330
         MVC   DEV(6),=CL6'3330'
         ZAP   WRKB,A3330          MAX ALTS ON 3330
         ZAP   TRK,=P'19'
         B     COMP
D33301   ZAP   TOTCYL,=P'15352'
         ZAP   TRK,=P'19'
         MVC   DEV(6),=CL6'3330-1'
         ZAP   WRKB,A3330          MAX ALTS ON 3330
         B     COMP
         EJECT
UCBSKIP  LA    R3,2(R3)            UP UCB POINTER
         B     UCBLP               CONTINUE SCAN
         SPACE 2
HEAD     PUT   OUT,HEAD1
         PUT   OUT,HEAD2
         PUT   OUT,HEAD3
         PUT   OUT,HEAD4
         ZAP   LCTR,=P'01'
         AP    PG,=P'01'
         MVC   PAGE(5),=X'4020202021'
         ED    PAGE,PG
         BR    R9
         EJECT
COMP     EQU   *
         SR    R1,R1              CLEAR R1
         LH    R1,4(R2)           LOAD DEVICE ADDRESS
         SLL   R1,4               SHIFT LEFT 1/2 BYTE
         STH   R1,WRK             STORE FOR UNPACK
         OI    WRK+1,X'0F'        SIMULATE TRUE PACKED DATA  GIVE SIGN
         UNPK  ADDR(3),WRK(2)     UNPACK DEV ADDRESS
         TR    ADDR(3),TRTBL-C'0' TRANSLATE TO PRINTABLE
         MVC   VOLUME,28(R2)       GET VOLSER FROM UCB
         MVC   OTCF,TCYLF+1
         MVC   OTTF,TTRKF
         MVC   OCC,LCYL+1
         MVC   OTC,LTRK
         MVC   EXT,TEXT
         PACK  WRK,TCYLF           PACK TOTAL FREE CYL
         MP    WRK,TRK             GET TOTAL TRACKS BY *TRK
         PACK  WRKA,TTRKF          PACK TOTAL FREE TRACKS
         AP    WRK,WRKA            AND ADD TO TRACKS FROM CYL*TRK
         MP    WRK,=P'10000'       MULTIPLY BY 10000 FOR DIVISION
         DP    WRK,TOTCYL          DIVIDE BY TRKS/PACK FOR PERCENT
         MVC   PERC(7),=X'402020214B2020'
         ED    PERC(7),WRK+2
         OBTAIN DSCBRD
         LTR   R15,R15
         LH    R2,DSCBALT          LOAD # OF AVAIL ALT TRKS
         XC    WRK,WRK
         CVD   R2,WRK
         SP    WRKB,WRK+6(2)       GET ALT TRKS IN USE
         UNPK  OALT(3),WRKB
         OI    OALT+2,X'F0'
         MVI   IXVTOCF,C' '                               *LBD 08/84*
         TM    DSCBDIRF,IXVTOC                            *LBD 08/84*
         BZ    NOIX                                       *LBD 08/84*
         MVI   IXVTOCF,C'Y'                               *LBD 08/84*
NOIX     DS    0H                                         *LBD 08/84*
         MVI   DRF,C' '
         TM    DSCBDIRF,DIRF       IS DIRF BIT ON
         BZ    PUT                 NO
         MVI   DRF,C'Y'            YES - SO SET MSG FLAG
PUT      PUT   OUT,LINE
         AP    LCTR,=P'01'
         CP    LCTR,=P'25'
         BNE   UCBSKIP
         BAL   R9,HEAD
         B     UCBSKIP
         EJECT
EXIT     EQU   *
*
*        END OF JOB ROUTINE
*
         CLOSE OUT
         $EPILOG 0
         EJECT
*        STORAGE AREAS FOR MY WORK
         SPACE 2
HEAD1    DC    C'1',CL30' '
         DC    C'DASD UTILIZATION REPORT',CL10' '
DATE     DC    CL8' ',CL21' ',C'PAGE '
PAGE     DC    CL5'    1',CL20' '
         SPACE
HEAD2    DC    C'0',CL30' ',C'TOTAL FREE'
         DC    CL3' ',C'CONTIG'
         DC    CL19' ',C'ALTER',CL4' ',C'DIRF',CL6' ',C'IXVTOC'
         DC    CL70' '                                    *LBD 08/84*
         ORG   HEAD2+100
         DC    C'PERCENT'
         ORG
         SPACE
HEAD3    DC    C' ',CL10'   ADDR',C'VOLUME',CL4' ',C'DEVTYPE'
         DC    CL4' ',C'CYL  TRK',CL4' ',C'CYL  TRK',CL5' ',C'EXTENTS'
         DC    CL5' ',C'USED ',CL5' ',C'ON'
         DC    CL9' ',C'ON'                               *LBD 08/84*
         DC    CL60' '
         ORG   HEAD3+99
         DC    C'AVAILABLE'
         ORG
         SPACE
HEAD4    DC    C'0',120C'-'
         EJECT
TRTBL    DC    C'0123456789ABCDEF'
LCTR     DC    PL2'00'
PG       DC    PL2'01'
TRK      DC    PL3'00'
         DS    0D
WRK      DC    PL8'0'
WRKA     DC    PL4'0'
WRKB     DC    PL2'0'
A3330    DC    PL2'133'            MAX AVAIL ALT TRKS ON 3330
A3350    DC    PL2'150'            MAX AVAIL ALTS ON A 3350
A3380    DC    PL2'15'             MAX AVAIL ALTS ON A 3380
         SPACE 2
*        STORAGE FOR SVC 78 DATA
WORK     EQU   *
         DS    CL6
TCYLF    DS    CL4
         DS    C
TTRKF    DS    CL4
         DS    C
TEXT     DS    CL4
         DS    C
LCYL     DS    CL4
         DS    C
LTRK     DS    CL4
         DS    C
         DC    CL10' '
TOTCYL   DS    PL3
         SPACE
         PRINT NOGEN
         SPACE
OUT      DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=121,            *
               BLKSIZE=2420,RECFM=FBA
         EJECT
         DS    0F
         SPACE 4
DSCBRD   CAMLST SEARCH,DSNDSCB,VOLUME,DSCB
DSNDSCB  DC    44X'04'             DSN OF DSCB TYPE 4
         DS    0D                  DOUBLE WORD ALIGNMENT
DSCB     DC    CL148' '            DSCB STORAGE AREA
         EJECT
*
*        OUTPUT LINE
*
LINE     DC    C'0'
         DC    CL4' '
ADDR     DC    CL4' '
         DC    CL2' '
VOLUME   DC    CL6' '
         DC    CL5' '
DEV      DC    CL6' '
         DC    CL4' '
OTCF     DC    CL3' '
         DC    CL1' '
OTTF     DC    CL4' '
         DC    CL4' '
OCC      DC    CL3' '
         DC    CL1' '
OTC      DC    CL4' '
         DC    CL7' '
EXT      DC    CL4' '
           DC  CL6' '
OALT     DC    CL6' '
         DC    CL6' '
DRF      DC    C' '
         DC    CL6' '                                     *LBD 08/84*
IXVTOCF  DC    C' '                                       *LBD 08/84*
         DC    CL70' '
         ORG   LINE+102
PERC     DC    CL6' ',C'%'
         ORG
         EJECT
DSCBD    DSECT
         DS    CL6
DSCBAVAL DS    CL2
NEXTALT  DS    CL4
DSCBALT  DS    CL2
DSCBDIRF DS    C
         SPACE
DIRF     EQU   X'04'
IXVTOC   EQU   X'01'                                      *LBD 08/84*
         END
