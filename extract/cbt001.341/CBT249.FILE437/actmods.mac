ACTMODS  $PROLOG
         EJECT
         OPEN  (IN,,SYSPRINT,OUTPUT)
         USING ENTRYD,R5
         EJECT
         B     READ2                                        *LBD 12/83*
NEWMEM   DS    0H
         MVC   OMEM,13(R5)      SAVE ELEMENT NAME
         SPACE
READ2    GET   IN
         LR    R5,R1
NEWMOD   DS    0H                                           *LBD 12/83*
         MVC   OMOD,USERMOD                                 *LBD 12/83*
         MVC   OPGMR,NAME                                   *LBD 12/83*
         MVC   ODATE,DATE                                   *LBD 12/83*
         CLI   0(R5),C'*'          IGNORE
         BE    READ2               YES
         CLI   0(R5),C' '          IGNORE
         BE    READ2               YES
         CLC   =C'.*',0(R5)        IGNORE
         BE    READ2               YES
         CLC   =C'*@',0(R5)        IGNORE
         BE    READ2               YES
         CLC   =C'./ NUMBER',0(R5) IGNORE
         BE    READ2               YES
         CLC   =C'./ ADD',0(R5)
         BE    NEWMEM              NEW MEMBER
         CLC   =C'ACT',STATUS
         BE    INUSE
         CLC   =C'CAN',STATUS
         BE    FINISH
         CLC   =C'COM',STATUS
         BE    FINISH
         B     READ2
INUSE    DS    0H
         MVC   OLDMOD,USERMOD                               *LBD 12/83*
         GET   IN                                           *LBD 12/83*
         LR    R5,R1                                        *LBD 12/83*
         CLI   0(R1),C'A'                                   *LBD 12/83*
         BL    NEWONE                                       *LBD 12/83*
         CLC   OLDMOD,USERMOD                               *LBD 12/83*
         BE    READ2                   OK                   *LBD 12/83*
         PUT   SYSPRINT,OLINE                               *LBD 12/83*
         B     NEWMOD
NEWONE   DS    0H                                           *LBD 12/83*
         PUT   SYSPRINT,OLINE                               *LBD 12/83*
         B     NEWMEM
FINISH   DS    0H
         PUT   SYSPRINT,OLINE                               *LBD 12/83*
         B     READ2
         EJECT
EOF      CLOSE (IN,,SYSPRINT)
         $EPILOG
         EJECT
OLDMOD   DC    CL7' '                                       *LBD 12/83*
         SPACE
OLINE    DC    CL80' '
         ORG   OLINE+5
OMEM     DC    CL8' '
         DC    CL2' '
OMOD     DC    CL7' '                                       *LBD 12/83*
         DC    CL2' '                                       *LBD 12/83*
         DC    C'IN USE BY '                                *LBD 12/83*
OPGMR    DC    CL12' '                                      *LBD 12/83*
         DC    CL2' ',C'SINCE'                              *LBD 12/83*
         DC    CL2' '                                       *LBD 12/83*
ODATE    DC    CL8' '                                       *LBD 12/83*
         ORG
         SPACE 2
         LTORG
         EJECT
IN       DCB   DSORG=PS,MACRF=GL,DDNAME=IN,EODAD=EOF
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=80,RECFM=FBA,   X
               BLKSIZE=6160
         EJECT
ENTRYD   DSECT
NAME     DS    CL12                PROGRAMMER NAME
DATE     DS    CL8                 LOG DATE
         DS    CL2
TIME     DS    CL8                 LOG TIME
         DS    CL2
STATUS   DS    CL10                STATUS
         DS    CL2
USERMOD  DS    CL7                 USERMOD ID
         END
