         TITLE 'JES3 ACCUMULATED HISTORY REPORT PROGRAM - VERSION 1.0'
*---------------------------------------------------------*
*        NAME: J3ACTRPT                                   *
*                                                         *
*    FUNCTION: TO GENERATE A REPORT OF THE HISTORY        *
*              RECORDS GENERATED BY PROGRAM SMFJRPT FROM  *
*              SMF RECORDS 6, 26  AND 57.                 *
*                                                         *
*       INPUT: SMFJRPT HISTORY RECORD.                    *
*                                                         *
*      OUTPUT: REPORT 1 - SYSOUT CLASS USAGE              *
*              REPORT 2 - PRINT ACTIVITY BY PRINTER       *
*              REPORT 3 - JOB CLASS USAGE                 *
*              REPORT 4 - NJE ACTIVITY BY DESTINATION NODE*
*                                                         *
*        JCL:  SYSPRINT - SYSOUT REPORT DD                *
*              HISTORY  - HISTORY RECORD                  *
*              SYSIN    - CONTROL CARDS                   *
*                                                         *
*                                                         *
* CONTROL CARDS:                                          *
*                                                         *
*              NODE=NODENAME OR ALL                       *
*              REPORT=ALL         FOR ALL REPORTS         *
*              REPORT=SOC         TO LIMIT TO SYSOUT CLASS*
*                                 USAGE REPORT            *
*              REPORT=NJE         TO LIMIT TO NJE REPORT  *
*              REPORT=JOB         TO LIMIT TO JOB REPORT  *
*              REPORT=PRT         TO LIMIT TO PRT REPORT *
*              PRT=ID OR ALL      TO LIMIT TO SPECIFIC    *
*                                 PRINTER ID              *
*              FROM=YYDDD         SET FROM DATE FOR REPORT*
*              TO=YYDDD           SET TO DATE FOR REPORT  *
*              SOC=CLASS OR ALL   SELECT SPECIFIC SYSOUT  *
*                                 CLASS                   *
*                                                         *
*        DEFAULTS:  NODE=ALL                              *
*                   REPORT=ALL                            *
*                   PRT=ALL                               *
*                   FROM=00000                            *
*                   TO=99999                              *
*                   SOC=ALL                               *
*                                                         *
*        ABENDS:                                          *
*              1   INVALID CONTROL CARDS                  *
*              2   SORTQ FAILED                           *
*             11   INVALID REPORT OPTION                  *
*             12   INVALID SYSOUT CLASS SELECTION         *
*                                                         *
*---------------------------------------------------------*
*---------------------------------------------------------*
* AUTHOR:      LIONEL DYCK                                *
*              ROCKWELL INTERNATIONAL                     *
*              PO BOX 2515                                *
*              2201 SEAL BEACH BLVD.                      *
*              SEAL BEACH, CALIF  90740                   *
*              MAIL CODE 110-SH28                         *
*              PHONE (213) 594-1125                       *
*              COMNET 374-1125                            *
*---------------------------------------------------------*
*---------------------------------------------------------*
         EJECT
J3ACTRPT $PROLOG R11,R12
         EJECT
         OPEN  (SYSPRINT,OUTPUT,HISTORY,INPUT,SYSIN,INPUT)
         SPACE 2
*
**       ACQUIRE TABLE FOR SYSOUT CLASS AND PRINTER ANALYSIS
*
         L     R2,SOCSIZE         SIZE OF SYSOUT CLASS TBL
         GETMAIN R,LV=(R2)        AND GETMAIN IT
         ST    R1,SOCADDR         SAVE ADDR
         AR    R1,R2              ADD SIZE TO BASE
         S     R1,=A(SOCENT)      LESS 1 ENT FOR BXLE
         ST    R1,SOCEADDR        AND SAVE IT
         SPACE 2
*
**       ACQUIRE JOB CLASS ANALYSIS
*
         L     R2,JOBSIZE         SIZE OF JOB ANALYSIS TABLE
         GETMAIN R,LV=(R2)        AND GETMAIN IT
         ST    R1,JOBADDR         SAVE ADDR
         AR    R1,R2              ADD SIZE TO BASE
         S     R1,=A(JOBENT)      LESS 1 ENT FOR BXLE
         ST    R1,JOBEADDR        AND SAVE IT
         SPACE 2
*
**       ACQUIRE NJE ANALYSIS
*
         L     R2,NETSIZE         SIZE OF NJE ANALYSIS TABLE
         GETMAIN R,LV=(R2)        AND GETMAIN IT
         ST    R1,NETADDR         SAVE ADDR
         AR    R1,R2              ADD SIZE TO BASE
         S     R1,=A(NETENT)      LESS 1 ENT FOR BXLE
         ST    R1,NETEADDR        AND SAVE IT
         EJECT
         MVC   OLINE+5(50),=CL50'JES3 ACTIVITY REPORT CONTROL CARDS'
         MVI   OLINE,C'1'         SKIP TO CH 1
         PUT   SYSPRINT,OLINE
         PUT   SYSPRINT,BLINE
         MVC   OLINE,BLINE
READSYS  GET   SYSIN
         LR    R5,R1
         MVC   OLINE+5(80),0(R5)  MOVE CONTROL CARD
         PUT   SYSPRINT,OLINE
CKSYSIN  DS    0H
         CLC   =C'NODE=',0(R5)
         BE    RNODE
         CLC   =C'REPORT=',0(R5)
         BE    RREP
         CLC   =C'PRT=',0(R5)
         BE    RPRT
         CLC   =C'FROM=',0(R5)
         BE    RFROM
         CLC   =C'TO=',0(R5)
         BE    RTO
         CLC   =C'SOC=',0(R5)
         BE    RSOCSEL
         CLC   =C'END',0(R5)      END OF INPUT
         BE    ENDOFSYS           YES
         SPACE 1
         ABEND 1
         SPACE 1
RSOCSEL  LA    R5,4(R5)           -> CLASS
         LA    R2,CLASSTBL        -> CLASS TBL
         SR    R3,R3              CLEAR COUNTER
         SETON SOCSEL
RSOCLP   CLC   0(4,R2),0(R5)      SAME CLASS
         BE    HAVESOCL           YES
         LA    R2,4(R2)           UP CLASS ->
         CLI   0(R2),X'FF'        END OF TABLE
         BE    RSOCAB             YES - BAD CLASS
         LA    R3,1(R3)           UP COUNTER
         B     RSOCLP
RSOCAB   DS    0H
         CLI   1(R5),C' '         VALID CLASS ?
         BNE   RSOCAB1
         CLI   0(R5),C'A'
         BNL   HAVESOCX
RSOCAB1  ABEND 12                 ABEND - BAD CLASS
         SPACE 1
HAVESOCX DS    0H
         MVC   SOCSELL,0(R5)
         B     READSYS
         SPACE 1
HAVESOCL STC   R3,SOCSELL
         B     READSYS
         SPACE 1
RFROM    LA    R5,5(R5)
         PACK  RFDATE(4),0(5,R5)
         B     READSYS
         SPACE 1
RTO      LA    R5,3(R5)
         PACK  RTDATE(4),0(5,R5)
         B     READSYS
         SPACE 1
RREP     DS    0H
         SETON RPT
         LA    R5,7(R5)
         CLC   =C'ALL',0(R5)
         BE    RALL
         CLC   =C'SOC',0(R5)
         BE    RSOC
         CLC   =C'NJE',0(R5)
         BE    RNJE
         CLC   =C'JOB',0(R5)
         BE    RJOB
         CLC   =C'PRT',0(R5)
         BE    RPRTR
         ABEND 11
RALL     SETON SOCR,NJER,JOBR,PRTR
         B     READSYS
RSOC     SETON SOCR
         B     READSYS
RNJE     SETON NJER
         B     READSYS
RJOB     SETON JOBR
         B     READSYS
RPRTR    SETON PRTR
         B     READSYS
         SPACE 1
RPRT     LA    R5,4(R5)
         CLC   =C'ALL',0(R5)
         BE    READSYS
         LA    R2,RXPRT
         SR    R3,R3              CLEAR COUNTER
RPRTM    MVC   0(1,R2),0(R5)
         LA    R5,1(R5)
         CLI   0(R5),C' '
         BE    READSYS
         LA    R3,1(R3)           INCR COUNTER
         STC   R3,RXPRTL          SAVE LENGTH
         LA    R2,1(R2)           UP OUT LOCATION
         B     RPRTM
         SPACE 1
RNODE    LA    R5,5(R5)
         CLC   =C'ALL',0(R5)
         BE    READSYS
         LA    R2,RXNODE
RNODEM   MVC   0(1,R2),0(R5)
         LA    R5,1(R5)
         CLI   0(R5),C' '
         BE    READSYS
         LA    R2,1(R2)
         B     RNODEM
         EJECT
ENDOFSYS CLOSE SYSIN
         MVC   OLINE,BLINE
         MVC   NODE,RXNODE
         CLI   NODE,C' '          ANY NODE
         BNE   CKRPT
         MVC   NODE(3),=C'ALL'
CKRPT    DS    0H
         IFON  RPT,READ
         SETON SOCR,PRTR,JOBR,NJER
         SPACE 2
READ     GET   HISTORY
         LR    R5,R1              -> INPUT RECORD
         USING HISTREC,R5
         CLI   RXNODE,C' '        ANY SELECT NODE ?
         BE    NONODE             NO - SKIP CHECK
         CLC   RXNODE,HNODE       SAME NODE ?
         BNE   READ               NO
NONODE   DS    0H
         CP    RFDATE,HFDATE      FROM DATE SELECT
         BH    READ               NO - SKIP IT
         CP    RTDATE,HTDATE      TO DATE SELECT
         BL    READ               TO HIGH - SKIP IT
         EJECT
DOBUILD  DS    0H
         SPACE 1
         IFON  SOCR,DOBS
         IFOFF PRTR,NOBS          SKIP SOC TABLE BUILD
         SPACE 1
DOBS     DS    0H
         LH    R4,HLSOC           LEN OF SOC
         LTR   R4,R4              ANY SOC TABLE ?         *LBD 01/85*
         BZ    NOBS                                       *LBD 01/85*
         LA    R6,HSOCT
         LA    R8,SOCENT
         L     R9,SOCEADDR
SOCBUILD DS    0H
         L     R7,SOCADDR         -> START OF TABLE AGAIN
SOCBLD   DS    0H
         USING SOCTBL,R6
         CLI   RXPRT,C' '         PRINTER DEST SELECT ?
         BE    NOPRS              NO
         SR    R14,R14            CLEAR FOR EX
         IC    R14,RXPRTL         LOAD LEN
EXCLC    DS    0H                 LABEL FOR TEST USE
         EX    R14,RXPRTCLC       AND DO COMPARE
         BNE   UPSOCE             NO - SKIP THIS REC
NOPRS    DS    0H
         IFOFF SOCSEL,NOSOCL      NO CLASS SELECT
         CLC   SOCSELL,SOC        CLASS WE WANT ?
         BNE   UPSOCE             NO -  SKIP THIS RECORD
NOSOCL   DS    0H
         CLI   0(R7),X'00'        OPEN SLOT
         BE    BSADD              YES
         CLC   SOC,0(R7)          SAME CLASS ?
         BNE   UPSOCT             NO
         CLC   SOCDEST,SOCDEST-SOC(R7)  SAME DEST
         BNE   UPSOCT
BSADD    DS    0H
         MVC   0(L'SOC,R7),SOC
         MVC   SOCDEST-SOC(L'SOCDEST,R7),SOCDEST
         L     R2,SOCL-SOC(R7)
         A     R2,SOCL
         ST    R2,SOCL-SOC(R7)
UPSOC    DS    0H
         SH    R4,=AL2(SOCENT)    LESS 1 ENT
         LTR   R4,R4              ANY MORE ENTRIES
         BZ    NOBS               NO - THAT'S ALL FOLKS
         LA    R6,SOCENT(R6)      -> NEXT ENTRY
         BXLE  R7,R8,SOCBUILD
         B     NOBS
UPSOCT   DS    0H
         BXLE  R7,R8,SOCBLD
NOBS     DS    0H
         B     DOBJ
UPSOCE   DS    0H
         SH    R4,=AL2(SOCENT)    LESS 1 ENT
         LTR   R4,R4              ANY MORE ENTRIES
         BZ    NOBS               NO - THAT'S ALL FOLKS
         LA    R6,SOCENT(R6)      -> NEXT ENTRY
         B     SOCBUILD
         SPACE 2
RXPRTCLC CLC   RXPRT(0),SOCDEST   SAME PRINTER ?
         DROP  R6
         EJECT
DOBJ     DS    0H                 BUILD JOB TABLE
         IFOFF JOBR,NOJB          SKIP JOB REPORT ??
         LA    R6,HJOBT
         LH    R4,HLSOC           LEN OF SOC ENTRIES
         AR    R6,R4              -> PAST IT TO THE JOB ENTRIES
         LH    R4,HLJOB           LEN OF JOB ENTRIES
         LTR   R4,R4              ANY JOB TABLE ?         *LBD 01/85*
         BZ    NOJB               NO - SKIP IT            *LBD 01/85*
         LA    R8,JOBENT
         L     R9,JOBEADDR
         USING JOBTBL,R6
JOBBUILD DS    0H
         L     R7,JOBADDR         -> START OF TABLE
JOBBLD   DS    0H
         CLI   0(R7),X'00'        OPEN SLOT
         BE    ADDJOB             YES
         CLC   JCLASS,0(R7)           SAME CLASS ?
         BNE   UPJOBT             NO
ADDJOB   DS    0H
         MVC   0(L'JCLASS,R7),JCLASS
         SPACE
         L     R2,JCNT-JCLASS(R7)
         A     R2,JCNT
         ST    R2,JCNT-JCLASS(R7)
         SPACE
         L     R2,JRID-JCLASS(R7)
         A     R2,JRID
         ST    R2,JRID-JCLASS(R7)
         SPACE
         L     R2,JCID-JCLASS(R7)
         A     R2,JCID
         ST    R2,JCID-JCLASS(R7)
         SPACE
         L     R2,JXID-JCLASS(R7)
         A     R2,JXID
         ST    R2,JXID-JCLASS(R7)
         SPACE
         L     R2,JOID-JCLASS(R7)
         A     R2,JOID
         ST    R2,JOID-JCLASS(R7)
         SPACE
UPJOB    DS    0H
         SH    R4,=AL2(JOBENT)    LESS 1 ENT
         LTR   R4,R4              ANY MORE ENTRIES
         BZ    NOJB               NO - THAT'S ALL FOLKS
         LA    R6,JOBENT(R6)      -> NEXT ENTRY
         BXLE  R7,R8,JOBBUILD
         B     NOJB
UPJOBT   DS    0H
         BXLE  R7,R8,JOBBLD
NOJB     DS    0H
         DROP  R6
         EJECT
DONR     DS    0H                 BUILD NET TABLE
         IFOFF NJER,NONET         SKIP NJE REPORT ??
         LA    R6,HNETT
         LH    R4,HLSOC           LEN OF SOC ENTRIES
         AR    R6,R4              -> TO IT
         LH    R4,HLJOB           LEN OF JOB ENTRIES
         AR    R6,R4              -> TO IT
         LH    R4,HLNET           LEN OF NET ENTRIES
         LTR   R4,R4              ANY NET TABLE ?         *LBD 01/85*
         BZ    NONET                                      *LBD 01/85*
         L     R7,NETADDR
         LA    R8,NETENT
         L     R9,NETEADDR
NETBUILD DS    0H
         L     R7,NETADDR
NETBLD   DS    0H
         USING NETTBL,R6
         CLI   0(R7),X'00'        OPEN SLOT
         BE    ADDNET             YES
         CLC   NETDEST,0(R7)      SAME DEST ?
         BNE   UPNETT             NO
ADDNET   DS    0H
         MVC   0(L'NETDEST,R7),NETDEST
         SPACE
         L     R2,NETJRCNT-NETDEST(R7)
         A     R2,NETJRCNT
         ST    R2,NETJRCNT-NETDEST(R7)
         SPACE
         L     R2,NETSRCNT-NETDEST(R7)
         A     R2,NETSRCNT
         ST    R2,NETSRCNT-NETDEST(R7)
         SPACE
         L     R2,NETSUCNT-NETDEST(R7)
         A     R2,NETSUCNT
         ST    R2,NETSUCNT-NETDEST(R7)
         SPACE
         L     R2,NETJUCNT-NETDEST(R7)
         A     R2,NETJUCNT
         ST    R2,NETJUCNT-NETDEST(R7)
         SPACE
UPNET    DS    0H
         SH    R4,=AL2(NETENT)    LESS 1 ENT
         LTR   R4,R4              ANY MORE ENTRIES
         BZ    NONET              NO - THAT'S ALL FOLKS
         LA    R6,NETENT(R6)      -> NEXT ENTRY
         BXLE  R7,R8,NETBUILD
         B     NONET
UPNETT   DS    0H
         BXLE  R7,R8,NETBLD
NONET    DS    0H
         DROP  R6
         EJECT
RECOK    DS    0H
         CP    FDATE,HFDATE       NEW FROM DATE
         BH    NEWFDTE            YES
CKTDTE   CP    TDATE,HTDATE       NEW TO DATE
         BL    NEWTDTE
         B     READ
NEWFDTE  DS    0H
         MVC   FDATE,HFDATE
         MVC   FTIME,HFTIME
         B     CKTDTE
NEWTDTE  DS    0H
         MVC   TDATE,HTDATE
         MVC   TTIME,HTTIME
         B     READ               GET NEXT RECORD
         EJECT
ENDHIST  DS    0H
         CLOSE HISTORY
         MVC   DWD(4),FTIME        TIME
         L     R1,DWD
         LA    R15,TFTIME
         BAL   R14,TIMEX
         MVC   DWD(4),TTIME        TIME
         L     R1,DWD
         LA    R15,TTTIME
         BAL   R14,TIMEX
         MVC   TFDATE(6),=X'20214B202020'
         MVC   TTDATE(6),=X'20214B202020'
         ED    TFDATE,FDATE+1
         ED    TTDATE,TDATE+1
         EJECT
*
**       PREPARE THE SYSOUT CLASS AND DESTINATION ACTIVITY REPORT
*
         SPACE 1
         IFOFF SOCR,DESTRPT       SKIP SOC REPORT ?
         SPACE 1
         USING SOC,R7
         L     R7,SOCADDR         -> TABLE
         LA    R8,SOCENT
         L     R9,SOCEADDR
         SR    R2,R2
         SR    R3,R3
S1       DS    0H
         CLI   SOC,X'00'          END OF TABLE
         BE    S1DO
         LA    R2,1(R2)           UP COUNTER
         LA    R3,SOCENT(R3)
         BXLE  R7,R8,S1
S1DO     DS    0H
         STH   R3,SOCLENS
         LTR   R2,R2              ANY INFO TO SORT ?
         BZ    NOSOC              NO - SO SKIP IT
         ST    R2,SQSIZE
         ST    R8,SQLEN
         MVC   SQNSTRT,=A(1)
         MVC   SQNWIDTH,=A(1)
*
**       CALL SORTQ FOR SYSOUT CLASS
*
         L     R7,SOCADDR
         CALL  SORTQ,((R7),SQSIZE,SQLEN,SQIERR,SQNSTRT,SQNWIDTH,       X
               SQTYPE,SQSEQ),VL
         L     R15,SQIERR
         LTR   R15,R15
         BNZ   SORTNQ
         EJECT
*
**       SORT DONE - NOW REPORT IT
*
         L     R7,SOCADDR
         LA    R8,SOCENT
         L     R9,SOCEADDR
         ZAP   LCNT,=P'99'
         LA    R3,SOCL1           -> SOC TITLE LINE
         ST    R3,STITLE          SAVE NEW SUB TITLE
         MVC   SOCS(1),SOC        INITIALIZE SYSOUT CLASS
         SR    R10,R10            INITIALIZE COUNTER
         XC    SOCTOT,SOCTOT      CLEAR TOTAL COUNTER
SOCP1    DS    0H
         CLC   SOC,SOCS           SAME CLASS
         BE    SOCT1
         CVD   R10,DWD            CONVERT TO DECIMAL
         A     R10,SOCTOT
         ST    R10,SOCTOT         SAVE IT
         MVC   OLINE+5(1),SOCS
         CLI   SOCS,X'C0'         STANDARD CLASS ?
         BH    SE11
         SR    R1,R1
         IC    R1,SOCS            LOAD INDEX VALUE
         SLL   R1,2               MULTIPLY BY 4
         LA    R2,CLASSTBL(R1)    -> INDEX INTO CLASS TABLE
         MVC   OLINE+5(4),0(R2)   AND MOVE CLASS VALUE
         SPACE 2
SE11     DS    0H
         MVC   OLINE+10(11),=X'2020206B2020206B202020'
         ED    OLINE+9(12),DWD+3
         B     SOCPUT1
SOCPUT1  DS    0H
         LA    R3,OLINE
         BAL   R10,OUTROU
         MVC   OLINE,BLINE        CLEAR OLINE
SOCCE1   DS    0H                 CHECK FOR
         CLI   SOC,X'00'          END OF TABLE
         BE    ENDSOC1            YES
         SR    R10,R10
SOCT1    DS    0H
         A     R10,SOCL
         MVC   SOCS,SOC
         BXLE  R7,R8,SOCP1
ENDSOC1  DS    0H
         L     R10,SOCTOT
         CVD   R10,DWD            CONVERT TO DECIMAL
         MVC   OLINE+3(5),=C'TOTAL'
         MVC   OLINE+10(11),=X'2020206B2020206B202020'
         ED    OLINE+9(12),DWD+3
         LA    R3,BLINE
         BAL   R10,OUTROU
         LA    R3,OLINE
         BAL   R10,OUTROU
         EJECT
DESTRPT  DS    0H
*
**       CALL SORTQ FOR DESTINATION
*
         SPACE 1
         IFOFF PRTR,JOBRPT        SKIP DEST REPORT ?
         SPACE 1
         L     R7,SOCADDR
         LA    R8,SOCENT
         L     R9,SOCEADDR
         SR    R2,R2
S2       DS    0H
         CLI   SOC,X'00'          END OF TABLE
         BE    S2DO
         LA    R2,1(R2)           UP COUNTER
         BXLE  R7,R8,S2
S2DO     DS    0H
         LTR   R2,R2              ANY INFO TO SORT ?
         BZ    NOSOC              NO - SO SKIP IT
         ST    R2,SQSIZE          SAVE COUNT OF ENTRIES IN TABLE
         ST    R8,SQLEN           SAVE ENTRY SIZE
         MVC   SQNSTRT,=A(SOCDEST-SOC+1)  SET UP START COLUMN
         MVC   SQNWIDTH,=A(8)     SET UP WIDTH OF SORT FIELD
         L     R7,SOCADDR
         CALL  SORTQ,((R7),SQSIZE,SQLEN,SQIERR,SQNSTRT,SQNWIDTH,       X
               SQTYPE,SQSEQ),VL
         L     R15,SQIERR
         LTR   R15,R15
         BNZ   SORTNQ
         EJECT
*
**       SORT DONE - NOW REPORT IT
*
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         MVC   DESTS,BLINE        AND DEST SAVE AREA
         L     R7,SOCADDR
         LA    R8,SOCENT
         L     R9,SOCEADDR
         ZAP   LCNT,=P'99'
         LA    R3,SOCL2           -> DEST TITLE LINE
         ST    R3,STITLE          SAVE NEW SUB TITLE
         MVC   DESTS,SOCDEST      INIT DEST
         XC    FWD,FWD            CLEAR FWD
         MVC   SOCS,SOC           INIT CLASS SAVE
         SR    R4,R4              INITIALIZE COUNTER
SOCP2    DS    0H
         CLC   SOCDEST,DESTS      SAME DESTINATION
         BE    SOCT2
         CVD   R4,DWD             CONVERT TO DECIMAL
         SR    R4,R4              RE-INIT COUNTER
         MVC   OLINE+5(8),DESTS
         MVC   OLINE+15(11),=X'2020206B2020206B202020'
         ED    OLINE+14(12),DWD+3
         LA    R3,OLINE
         BAL   R10,OUTROU
         CLI   SOCDEST,X'00'      END OF TABLE
         BE    ENDSOC             YES
         SR    R4,R4
SOCT2    DS    0H
         A     R4,SOCL
         MVC   DESTS,SOCDEST      REINIT DEST
         MVC   OLINE,BLINE        CLEAR O/P LINE
         BXLE  R7,R8,SOCP2
         B     ENDSOC
         SPACE 2
NOSOC    DS    0H                 NO SOC RECORDS TO REPORT ON
         LA    R3,SOCNOR          -> INFO LINE
         BAL   R10,OUTROU         AND TELL SOMEONE
ENDSOC   DS    0H
         DROP  R7
         EJECT
JOBRPT   DS    0H
         MVC   OLINE,BLINE
*
**       PREPARE THE JOB (SMF 26) INFORMATION REPORT
*
         SPACE 1
         IFOFF JOBR,NJERPT        SKIP JOB REPORT ?
         SPACE 1
         USING JCLASS,R7
         L     R7,JOBADDR         -> TABLE
         LA    R8,JOBENT          GET SIZE OF ENTRY
         L     R9,JOBEADDR        -> END OF TABLE -1
         SR    R2,R2
         SR    R3,R3
J1       DS    0H
         CLI   JCLASS,X'00'       END OF TABLE
         BE    J1DO
         LA    R2,1(R2)           UP COUNTER
         LA    R3,JOBENT(R3)
         BXLE  R7,R8,J1
J1DO     DS    0H
         STH   R3,JOBLENS
         LTR   R2,R2              ANY INFO TO SORT ?
         BZ    NOJOB              NO - SO SKIP IT
         ST    R2,SQSIZE          SAVE RECORD COUNT
         ST    R8,SQLEN           SAVE LENGTH OF ENTRY
         MVC   SQNSTRT,=A(1)
         MVC   SQNWIDTH,=A(8)     SORT BY JOB CLASS
*
**       CALL SORTQ FOR JOB CLASS ANAYLSIS
*
         L     R7,JOBADDR
         CALL  SORTQ,((R7),SQSIZE,SQLEN,SQIERR,SQNSTRT,SQNWIDTH,       X
               SQTYPE,SQSEQ),VL
         L     R15,SQIERR
         LTR   R15,R15
         BNZ   SORTNQ
         EJECT
*
**       SORT DONE - NOW REPORT IT
*
         L     R7,JOBADDR
         LA    R8,JOBENT
         L     R9,JOBEADDR
         ZAP   LCNT,=P'99'
         LA    R3,JOBL1           -> JOB TITLE LINE
         ST    R3,STITLE          SAVE NEW SUB TITLE
         LA    R3,JOBL2           -> JOB TITLE LINE 2
         ST    R3,STITLE2         SAVE NEW SUB TITLE 2
         MVC   SCLASS,JCLASS      INITIALIZE JOB CLASS
         SR    R10,R10            INITIALIZE COUNTER
         XC    JOBTOT,JOBTOT      CLEAR TOTAL COUNTER
         XC    JOBTOTR,JOBTOTR    CLEAR TOTAL COUNTER
         XC    JOBTOTC,JOBTOTC    CLEAR TOTAL COUNTER
         XC    JOBTOTX,JOBTOTX    CLEAR TOTAL COUNTER
         XC    JOBTOTO,JOBTOTO    CLEAR TOTAL COUNTER
         EJECT
DOJOB    DS    0H
         CLI   JCLASS,X'FF'       SPECIAL CLASS ?
         BE    SPECJOB            YES
         CLI   JCLASS,X'00'       END OF TABLE ??
         BE    ENDJOB             YES
         L     R1,JOBTOT          LOAD TOTAL COUNTER
         L     R2,JCNT            LOAD CLASS USE COUNTER
         AR    R1,R2              ADD THEM AND
         ST    R1,JOBTOT          SAVE TOTAL COUNTER
         MVC   OLINE+7(8),JCLASS  SET JOB CLASS IN OLINE
         L     R1,JCNT            LOAD TOTAL USE COUNT
         CVD   R1,DWD             CONVERT TO DECIMAL
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         SPACE 2
         L     R1,JOBTOTR         LOAD TOTAL RDR JOB COUNTER
         L     R2,JRID            LOAD CLASS RDR JOB COUNTER
         AR    R1,R2              ADD THEM AND THEN
         ST    R1,JOBTOTR         SAVE THEM
         SPACE 2
         L     R1,JOBTOTC         LOAD TOTAL C/I JOB COUNTER
         L     R2,JCID            LOAD CLASS C/I JOB COUNTER
         AR    R1,R2              ADD THEM AND THEN
         ST    R1,JOBTOTC         SAVE THEM
         SPACE 2
         L     R1,JOBTOTX         LOAD TOTAL EXEC JOB COUNTER
         L     R2,JXID            LOAD CLASS EXEC JOB COUNTER
         AR    R1,R2              ADD THEM AND THEN
         ST    R1,JOBTOTX         SAVE THEM
         SPACE 2
         L     R1,JOBTOTO         LOAD TOTAL OUTPUT JOB COUNTER
         L     R2,JOID            LOAD CLASS OUTPUT JOB COUNTER
         AR    R1,R2              ADD THEM AND THEN
         ST    R1,JOBTOTO         SAVE THEM
         SPACE 2
         LA    R3,OLINE
         BAL   R10,OUTROU         GO PRINT IT
         BXLE  R7,R8,DOJOB
SPECJOB  DS    0H
         MVC   OLINE,BLINE
         IFON  SC,SPECJOBA
         LA    R3,OLINE
         BAL   R10,OUTROU
         SETON SC
SPECJOBA DS    0H
         MVC   OLINE+7(7),JCLASS+1  MOVE CLASS TYPE
         L     R1,JCNT            LOAD TOTAL USE COUNT
         CVD   R1,DWD             CONVERT TO DECIMAL
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU         GO PRINT IT
         BXLE  R7,R8,DOJOB
         SETOF SC
         EJECT
ENDJOB   DS    0H
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         LA    R3,OLINE
         BAL   R10,OUTROU         PRINT A BLANK LINE
         MVC   OLINE+30(30),=CL30'TOTAL JOBS PROCESSED'
         L     R1,JOBTOT
         CVD   R1,DWD
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         MVC   OLINE+30(30),=CL30'TOTAL READER JOBS PROCESSED'
         L     R1,JOBTOTR
         CVD   R1,DWD
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         MVC   OLINE+30(30),=CL30'TOTAL C/I JOBS PROCESSED'
         L     R1,JOBTOTC
         CVD   R1,DWD
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         MVC   OLINE+30(30),=CL30'TOTAL EXEC JOBS PROCESSED'
         L     R1,JOBTOTX
         CVD   R1,DWD
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE,BLINE        CLEAR OUTPUT LINE
         MVC   OLINE+30(30),=CL30'TOTAL OUTPUT JOBS PROCESSED'
         L     R1,JOBTOTO
         CVD   R1,DWD
         MVC   OLINE+20(9),=X'206B2020206B202020'
         ED    OLINE+19(10),DWD+4 EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         LA    R3,BLINE           PRINT A BLANK LINE
         BAL   R10,OUTROU
         LA    R3,JOBE1           AND NOW THE NOCLASS EXPLAINATION
         BAL   R10,OUTROU
         B     ENDJOB2
NOJOB    DS    0H
         LA    R3,JOBNOR
         BAL   R10,OUTROU
ENDJOB2  DS    0H
         EJECT
NJERPT   DS    0H
         MVC   OLINE,BLINE
*
**       PREPARE THE NETWORK ACTIVITY TABLE TO REPORT ON
*
         SPACE 1
         IFOFF NJER,NONETR
         SPACE 1
         USING NETTBL,R7
         L     R7,NETADDR         -> TABLE
         LA    R8,NETENT
         L     R9,NETEADDR
         SR    R2,R2
         SR    R3,R3
N1       DS    0H
         CLI   NETDEST,X'00'      END OF TABLE
         BE    N1DO
         LA    R2,1(R2)           UP COUNTER
         LA    R3,NETENT(R3)
         BXLE  R7,R8,N1
N1DO     DS    0H
         STH   R3,NETLENS
         LTR   R2,R2              ANY INFO TO SORT ?
         BZ    NONETB             NO - SO SKIP IT
         ST    R2,SQSIZE
         ST    R8,SQLEN
         MVC   SQNSTRT,=A(1)
         MVC   SQNWIDTH,=A(8)
*
**       CALL SORTQ FOR NETWORK ACTIVIY REPORT
*
         L     R7,NETADDR
         CALL  SORTQ,((R7),SQSIZE,SQLEN,SQIERR,SQNSTRT,SQNWIDTH,       X
               SQTYPE,SQSEQ),VL
         L     R15,SQIERR
         LTR   R15,R15
         BNZ   SORTNQ
         EJECT
*
**       SORT DONE - NOW REPORT IT
*
         L     R7,NETADDR
         LA    R8,NETENT
         L     R9,NETEADDR
         ZAP   LCNT,=P'99'
         MVC   OLINE,BLINE
         LA    R3,NETL1           -> NET TITLE LINE
         ST    R3,STITLE          SAVE NEW NET TITLE
         LA    R3,NETL2           -> NET TITLE LINE
         ST    R3,STITLE2         SAVE NEW NET TITLE 2
         XC    NETJTOT,NETJTOT    CLEAR TOTAL COUNTER
         XC    NETSTOT,NETSTOT    CLEAR TOTAL COUNTER
         XC    NETJTOTU,NETJTOTU  CLEAR TOTAL COUNTER
         XC    NETSTOTU,NETSTOTU  CLEAR TOTAL COUNTER
DONJE    DS    0H
         CLI   NETDEST,X'00'      END OF TABLE ?
         BE    ENDNET
         MVC   OLINE+7(8),NETDEST
         L     R1,NETSRCNT        LOAD SYSOUT RECORD COUNT
         CVD   R1,DWD
         L     R2,NETSTOT         GET RECORD TOTAL
         AR    R2,R1              ADD
         ST    R2,NETSTOT         AND SAVE IT
         MVC   OLINE+20(11),=X'2020206B2020206B202020'
         ED    OLINE+19(12),DWD+3
         L     R1,NETJRCNT        LOAD JOB RECORD COUNT
         CVD   R1,DWD
         L     R2,NETJTOT         GET RECORD TOTAL
         AR    R2,R1              ADD
         ST    R2,NETJTOT         AND SAVE IT
         MVC   OLINE+50(11),=X'2020206B2020206B202020'
         ED    OLINE+49(12),DWD+3
         L     R1,NETSUCNT        LOAD USE COUNT
         CVD   R1,DWD
         L     R2,NETSTOTU
         AR    R2,R1
         ST    R2,NETSTOTU
         MVC   OLINE+35(6),=X'20206B202020'
         ED    OLINE+34(7),DWD+5  EDIT IT
         L     R1,NETJUCNT        LOAD USE COUNT
         CVD   R1,DWD
         L     R2,NETJTOTU
         AR    R2,R1
         ST    R2,NETJTOTU
         MVC   OLINE+65(6),=X'20206B202020'
         ED    OLINE+64(7),DWD+5  EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         BXLE  R7,R8,DONJE
         B     ENDNET
NONETB   DS    0H
         LA    R3,NETNOR
         BAL   R10,OUTROU
         EJECT
ENDNET   DS    0H
         MVC   OLINE,BLINE
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 1
         MVC   OLINE+20(30),=CL30'TOTAL JB NJE JOBS'
         L     R2,NETJTOTU
         CVD   R2,DWD
         MVC   OLINE+13(6),=X'20206B202020'
         ED    OLINE+12(7),DWD+5  EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE+20(30),=CL30'TOTAL SO NJE JOBS'
         L     R2,NETSTOTU
         CVD   R2,DWD
         MVC   OLINE+13(6),=X'20206B202020'
         ED    OLINE+12(7),DWD+5  EDIT IT
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE+20(30),=CL30'TOTAL JB NJE RECORDS'
         L     R2,NETJTOT
         CVD   R2,DWD
         MVC   OLINE+8(11),=X'2020206B2020206B202020'
         ED    OLINE+7(12),DWD+3
         LA    R3,OLINE
         BAL   R10,OUTROU
         SPACE 2
         MVC   OLINE+20(30),=CL30'TOTAL SO NJE RECORDS'
         L     R2,NETSTOT
         CVD   R2,DWD
         MVC   OLINE+8(11),=X'2020206B2020206B202020'
         ED    OLINE+7(12),DWD+3
         LA    R3,OLINE
         BAL   R10,OUTROU
ENDNET2  DS    0H
NONETR   DS    0H
         EJECT
EXIT     DS    0H
         CLOSE SYSPRINT
         EJECT
*        FREE  THE SYSOUT ANALYSIS DATA TABLE
         L     R2,SOCADDR
         L     R3,SOCSIZE
         FREEMAIN R,LV=(R3),A=(R2)
         SPACE 2
*        FREE  THE JOB ANALYSIS DATA TABLE
         L     R2,JOBADDR
         L     R3,JOBSIZE
         FREEMAIN R,LV=(R3),A=(R2)
         SPACE 2
*        FREE  THE NET ANALYSIS DATA TABLE
         L     R2,NETADDR
         L     R3,NETSIZE
         FREEMAIN R,LV=(R3),A=(R2)
         SPACE 2
         $EPILOG 0
         SPACE 2
SORTNQ   ABEND 2
         EJECT
OUTROU   DS    0H
         CP    LCNT,=P'56'
         BH    NEWPAGE
         AP    LCNT,=P'01'
         PUT   SYSPRINT,0(R3)
         BR    R10
NEWPAGE  DS    0H
         PUT   SYSPRINT,TITLE
         L     R2,STITLE
         PUT   SYSPRINT,0(R2)
         PUT   SYSPRINT,BLINE
         L     R2,STITLE2
         PUT   SYSPRINT,0(R2)
         PUT   SYSPRINT,0(R3)
         ZAP   LCNT,=P'05'
         BR    R10
         EJECT
*        OPEN  EXIT FOR SYSPRINT DCB
*              IF NO BLKSIZE SPECIFIED THEN SET TO LRECL
         SPACE
EXLSYSP  DS    0H
         USING IHADCB,R1
         CLC   DCBBLKSI(2),DCBLRECL BLKSIZE OK ?
         BER   R14                YES - RETURN
         CLC   DCBBLKSI(2),=H'0' BLKSIZE SPECIFIED
         BNER  R14                YES
         MVC   DCBBLKSI(2),DCBLRECL        FIX IT
         DROP  R1
         BR    R14                AND RETURN
         EJECT
************************************************************
*                                                          *
*         SUBROUTINE TO CONVERT TIME TO HH.MM.SS           *
*                                                          *
************************************************************
         SPACE
*         INPUT: TIME IN R1, OUTPUT ADDRESS IN R15
         SPACE
TIMEX    SLR   R0,R0
         D     R0,=F'360000'
         CVD   R1,DWD
         OI    DWD+7,X'0F'
         UNPK  0(2,R15),DWD+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'6000'
         CVD   R1,DWD
         OI    DWD+7,X'0F'
         UNPK  3(2,R15),DWD+6(2)
         LR    R1,R0
         SLR   R0,R0
         D     R0,=F'100'
         CVD   R1,DWD
         OI    DWD+7,X'0F'
         UNPK  6(2,R15),DWD+6(2)
         MVI   2(R15),C'.'
         MVI   5(R15),C'.'
         BR    R14
         EJECT
SYSIN    DCB   DSORG=PS,MACRF=GL,DDNAME=SYSIN,EODAD=ENDOFSYS
         EJECT
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,            X
               LRECL=121,EXLST=EXLSYSP
         EJECT
HISTORY  DCB   DSORG=PS,MACRF=GL,DDNAME=HISTORY,BFTEK=A,EODAD=ENDHIST
         EJECT
         LTORG
         EJECT
*
**       PARM DATA USED FOR SORTQ CALLS
*
SQSIZE   DC    A(0)               NUMBER OF ELEMENTS
SQLEN    DC    A(0)               LEN OF ELEMENT
SQIERR   DC    A(0)               RETURN CODE
SQNSTRT  DC    A(0)               START COLUMN
SQNWIDTH DC    A(0)               WIDTH OF ELEMENT
SQTYPE   DC    C'CH'              CHARACTER
SQSEQ    DC    C'A'               ASCENDING
         EJECT
*
**       CONSTANTS AND WORK AREAS
*
DWD      DS    D
FWD      EQU   DWD,4
LCNT     DC    PL2'99'
STITLE   DC    A(BLINE)           SUB TITLE ADCON
STITLE2  DC    A(BLINE)           SUB TITLE ADCON
RXPRTL   DC    X'00'
RXPRT    DC    CL8' '
RXNODE   DC    CL8' '
DESTS    DC    CL8' '
SCLASS   DC    CL8' '             SAVE CLASS
SOCTOT   DC    F'0'               TOTAL PRINT LINES
RFDATE   DC    PL4'00000'         REPORT FROM DATE
RTDATE   DC    PL4'99999'         REPORT TO DATE
FDATE    DC    PL4'99999'         FROM DATE
TDATE    DC    PL4'0'             TO DATE
FTIME    DC    X'FFFFFFFF'        FROM TIME
TTIME    DC    F'0'               TO TIME
JOBTOT   DC    F'0'               TOTAL JOBS
JOBTOTR  DC    F'0'               TOTAL READER JOBS
JOBTOTC  DC    F'0'               TOTAL C/I JOBS
JOBTOTX  DC    F'0'               TOTAL EXECUTION JOBS
JOBTOTO  DC    F'0'               TOTAL OUTPUT JOBS
NETSTOT  DC    F'0'               NJE SYSOUT RECORD COUNT
NETJTOT  DC    F'0'               NJE JOB RECORD COUNT
NETSTOTU DC    F'0'               NJE SYSOUT RECORD USE COUNT
NETJTOTU DC    F'0'               NJE JOB RECORD USE COUNT
SOCLENS  DC    H'0'
JOBLENS  DC    H'0'
NETLENS  DC    H'0'
SOCS     DC    C' '
SOCSELL  DC    X'00'
OLINE    DC    CL121' '
BLINE    DC    CL121' '
         EJECT
         BITSW SC,SOCR,NJER,JOBR,PRTR,RPT,SOCSEL
         EJECT
*
**       REVERSE RCLSTBL CODE
*
CLASSTBL DS    0H        X'CODE'
         DC    CL4'N/A'    00     NOT USED
         DC    CL4'AQRT'   01     AQRT
         DC    CL4'CRT'    02     CRT
         DC    CL4'DTP'    03     DISK-TO-PRINT
         DC    CL4'OTHR'   04     OTHER CLASS
         DC    CL4'MTP'    05     MYLAR TAPE PUNCH
         DC    CL4'MTR'    06     MYLAR TAPE READ
         DC    CL4'PTP'    07     PAPER TAPE PUNCH
         DC    CL4'PTR'    08     PAPER TAPE READ
         DC    CL4'RMTT'   09     REMOTE TAPE TO TAPE
         DC    CL4'TD'     0A     TAPE DUMP
         DC    CL4'CT'     0B     CARD TO TAPE DSP
         DC    CL4'TC'     0C     TAPE TO CARD
         DC    CL4'CP'     0D     CARD TO PRINT
         DC    CL4'TT'     0E     TAPE TO TAPE
         DC    CL4'TP'     0F     TAPE TO PRINT
         DC    CL4'DISP'   10     DISPLAY DSP
         DC    CL4'VPS'    11     VPS PRINT OUTPUT
         DC    CL4'SPOL'   12     SPOOL DSP
         DC    CL4'NET'    13     NETWORK JOB
         DC    CL4'PRT'    14     STANDARD PRINT ?
         DC    CL4'DTP*'   15     DISK-TO-PRINT WITH .* RECORDS
         DC    4X'FF'             END OF TABLE
         EJECT
*
**       REPORT TITLES
*
TITLE    DC    CL121'1'
         ORG   TITLE+10
         DC    C'JES3 SYSTEM ACTIVITY REPORT'
         DC    C' FOR CENTER '
NODE     DC    CL8' '
         DC    C' FROM '
TFDATE   DC    CL6' ',C'/'
TFTIME   DC    CL8' '
         DC    C'  TO '
TTDATE   DC    CL6' ',C'/'
TTTIME   DC    CL8' '
         ORG   ,
         SPACE 1
NETNOR   DC    CL121' '
         ORG   NETNOR+10
         DC    C'NO NJE INFORMATION AVAILABLE SO NO NJE'
         DC    C' ACTIVITY REPORT WILL BE GENERATED '
         ORG   ,
         SPACE 1
JOBNOR   DC    CL121' '
         ORG   JOBNOR+10
         DC    C'NO JOB CLASS INFORMATION AVAILABLE SO NO JOB CLASS'
         DC    C' ACTIVITY REPORT WILL BE GENERATED '
         ORG   ,
         SPACE 1
SOCNOR   DC    CL121' '
         ORG   SOCNOR+10
         DC    C'NO SYSOUT/PRINTER INFORMATION AVAILABLE'
         DC    C' SO NO SYSOUT CLASS OR'
         DC    C' PRINTER ACTIVITY REPORT WILL BE GENERATED '
         ORG   ,
         SPACE 1
NETL1    DC    CL121' '
         ORG   NETL1+10
         DC    C'NJE ACTIVITY REPORT BY DESTINATION NODE'
         ORG   ,
         SPACE 1
NETL2    DC    CL121' '
         ORG   NETL2+7
         DC    C'NODE'
         ORG   NETL2+17
         DC    C'SYSOUT RECORDS'
         ORG   NETL2+36
         DC    C'USAGE'
         ORG   NETL2+50
         DC    C'JOB RECORDS'
         ORG   NETL2+66
         DC    C'USAGE'
         ORG   ,
         SPACE 1
JOBE1    DC    CL121' '
         ORG   JOBE1+10
         DC    C'JOBS WITH NOCLASS ARE EITHER OUTPUT ONLY OR'
         DC    C' NJE STORE AND FORWARD'
         ORG   ,
         SPACE 1
JOBL1    DC    CL121' '
         ORG   JOBL1+10
         DC    C'JOB ACTIVITY REPORT BY CLASS'
         ORG   ,
         SPACE 1
JOBL2    DC    CL121' '
         ORG   JOBL2+7
         DC    C'CLASS'
         ORG   JOBL2+20
         DC    C'    COUNT'
         ORG   ,
         SPACE 1
SOCL1    DC    CL121' '
         ORG   SOCL1+10
         DC    C'SYSOUT CLASS ACTIVITY REPORT BY CLASS'
         ORG   ,
         SPACE 1
SOCL2    DC    CL121' '
         ORG   SOCL2+10
         DC    C'SYSOUT ACTIVITY REPORT BY LOCATION'
         ORG   ,
         SPACE 1
         EJECT
*
**       EQUATES FOR ENTRY COUNTS IN TABLES
*
SOCENTC  EQU   600                COUNT OF SYSOUT/DEST ENTRIES
JOBENTC  EQU   1200               COUNT OF JOB CLASS ENTRIES
NETENTC  EQU   200                COUNT OF NET TABLE ENTRIES
         SPACE 2
SOCADDR  DS    A                  ADDRESS OF SOC TABLE
SOCEADDR DS    A                  END OF TABLE
SOCSIZE  DC    A(SOCENT*SOCENTC)  SIZE OF TABLE
         SPACE 2
JOBADDR  DS    A                  ADDRESS OF JOB TABLE
JOBEADDR DS    A                  END OF TABLE
JOBSIZE  DC    A(JOBENT*JOBENTC)  SIZE OF TABLE
         SPACE 2
NETADDR  DS    A                  ADDRESS OF NET TABLE
NETEADDR DS    A                  END OF TABLE
NETSIZE  DC    A(NETENT*NETENTC)  SIZE OF TABLE
         EJECT
T6       DSECT
         IFASMFR 6
         EJECT
T26      DSECT
         IFASMFR 26
         EJECT
T57      DSECT
         IATYNSM
         EJECT
         DCBD  DSORG=PS
         EJECT
SOCTBL   DSECT
SOC      DS    C                  SYSOUT CLASS
         DS    CL3                RESERVED FOR FUTURE USE
SOCL     DS    CL4                LINE COUNT
SOCDEST  DS    CL8                OUTPUT DEST
SOCENT   EQU   *-SOC              SIZE OF ENTRY
         EJECT
JOBTBL   DSECT
JCLASS   DS    CL8                JOB CLASS
JCNT     DS    CL4                USAGE COUNT
JRID     DS    CL4                COUNTER INPUT PROC
JCID     DS    CL4                COUNTER CONV PROC
JXID     DS    CL4                COUNTER EXEC PROC
JOID     DS    CL4                COUNTER OUTPUT PROC
JOBENT   EQU   *-JCLASS           SIZE OF ENTRY
         EJECT
NETTBL   DSECT
NETDEST  DS    CL8                DESTINATION NODE
NETJRCNT DS    CL4                JB RECORD COUNT
NETSRCNT DS    CL4                SO RECORD COUNT
NETJUCNT DS    CL4                JB USE COUNT
NETSUCNT DS    CL4                SO USE COUNT
NETENT   EQU   *-NETDEST          SIZE OF ENTRY
         SPACE 2
HISTREC  DSECT
HRDW     DS    H
         DS    H
HNODE    DS    CL8                NODE NAME
HFDATE   DS    CL4                FROM DATE
HFTIME   DS    CL4                FROM TIME
HTDATE   DS    CL4                TO DATE
HTTIME   DS    CL4                TO TIME
HLSOC    DS    CL2                LEN OF SOC TABLE
HLJOB    DS    CL2                LEN OF JOB TABLE
HLNET    DS    CL2                LEN OF NET TABLE
HISTHDRL EQU   *-HRDW             LEN OF HEADER
HSOCT    DS    0H                 SOC TABLE
HJOBT    DS    0H                 JOB TABLE
HNETT    DS    0H                 NET TABLE
         SPACE 2
         END
         COPY  SORTQ
         COPY  SORTAQ
         END
