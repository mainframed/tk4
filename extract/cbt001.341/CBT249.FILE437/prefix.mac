PREFIX   TITLE 'UPDATE THE PREFIX IN THE UPT'
         MACRO
         BEGIN &NAME=' ',&BASE=YES,&SAVE=NO,&R1=14,&R2=12,             *
               &BASEREG=,&USING=YES,&TIME=,&CHAIN=NO,&SAVLEN=72
         LCLC  &WORK
         AIF   ('&NAME' NE ' ').INNAME
         MNOTE 12,'*** PARM NAME=???????? OMITTED ***'
         MEXIT
.INNAME  ANOP
         AIF   ('&TIME' EQ 'ONLY').NAME
DATESTMP CSECT
         DC    CL8'&NAME'
&WORK    SETC  '&SYSTIME'(1,2).'&SYSTIME'(4,2)
         DC    CL4'&WORK'              TIME OF ASSEMBLY
&WORK    SETC  '&SYSDATE'(1,2).'&SYSDATE'(4,2).'&SYSDATE'(7,2)
         DC    CL6'&WORK'              DATE OF ASSEMBLY
&NAME    CSECT
         ENTRY &NAME
         AIF   ('&SAVE' EQ 'NO').NOSAVE
         SAVE  (&R1,&R2)
.NOSAVE  ANOP
         AIF   ('&BASE' EQ 'USING').USING
         AIF ('&BASE' EQ 'NO').NOBASE
         BALR  BASEREG,0
.USING   ANOP
$NTRY    DS    0H
         USING *,BASEREG
.NOBASE  ANOP
         AIF   ('&CHAIN' EQ 'NO').NOCHAIN
         GETMAIN R,LV=&SAVLEN
         LR    15,1
         LR    11,13
         ST    13,4(15)            STANDARD
         ST    15,8(13)               REGISTER
         LR    13,15                        CONVENTIONS
         LM    0,1,20(11)                LINKAGE
.NOCHAIN ANOP
         AIF   ('&BASEREG' EQ '').NAME
BASEREG  EQU   &BASEREG                BASE REGISTER EQUATE
.NAME    ANOP
.END     MEND
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
BASEREG  EQU   R12
PPLBAR   EQU   R11
CPPLBAR  EQU   R10
PDLBAR   EQU   R9
UPTBAR   EQU   R8
IOPLBAR  EQU   R7
         BEGIN NAME=PREFIX,CHAIN=YES,SAVE=YES
         USING CPPL,CPPLBAR
         USING UPT,UPTBAR
         USING PPL,PPLBAR
         USING IKJPARMD,PDLBAR
         USING IOPL,IOPLBAR
         LA    PPLBAR,PPLA         BASE TO THE PPL
         LR    CPPLBAR,R1          BASE FOR CPPL
         MVC   PPLCBUF,CPPLCBUF    COMMAND BUFFER
         MVC   PPLUPT,CPPLUPT      USER PROFILE TABLE
         MVC   PPLECT,CPPLECT      ENVIORNMENT CONTROL TABLE
         MVC   PPLECB,AECB         ADDRESS OF ECB
         MVC   PPLPCL,APARMTAB     PARAMETER TABLE POINTER
         MVC   PPLANS,APDLPT       POINTER TO THE POINTER TO THE PDL
         CALLTSSR EP=IKJPARS,MF=(E,PPL)
         CLI   PDLPT,X'FF'         PARSE UNNCESSFUL
         BE    ABEND
         L     PDLBAR,PDLPT        POINTER TO THE PDL
         L     UPTBAR,CPPLUPT      POINTER TO THE UPT       *HMD 05/82*
         MVC   OPFXMSG2,=CL7'()'   ASSUME NO PREFIX         *HMD 05/82*
         SR    R1,R1               CLEAR                    *HMD 05/82*
         ICM   R1,1,UPTPREFL       PREFIX LENGTH            *HMD 05/82*
         BNP   ZEROLN              ZERO LENGTH              *HMD 05/82*
         BCTR  R1,0                DECREMENT BY 0           *HMD 05/82*
         EX    R1,OPFXM2           MOVE OLD PREFIX TO MSG LINE
ZEROLN   L     R2,PRFX             ADDRESS OF THE PARSED DATA
         SLR   R1,R1               CLEAR REGISTER 1         *HMD 05/82*
         ICM   R1,3,PRFX+4         LENGTH OF COMMAND ENTERED
         BZ    NOPREF              NO PREFIX DESIRED        *HMD 05/82*
         CLI   0(R2),C'$'          NATIONAL CHARACTERS
         BE    PRFXOK
         CLI   0(R2),C'#'
         BE    PRFXOK
         CLI   0(R2),C'@'
         BE    PRFXOK
         CLI   0(R2),C'A'          MUST BE ALPHABETIC
         BL    PRFXBAD
         CLI   0(R2),C'Z'
         BH    PRFXBAD
PRFXOK   DS    0H
         STC   R1,UPTPREFL         STORE NEW LENGTH
         BCTR  R1,0                DECREMENT LENGTH
         MVC   UPTPREFX,=CL7' '    CLEAR PREFIX
         EX    R1,NEWPREFX         MOVE NEW PREFIX TO UPT
         EX    R1,NPFXM2           MOVE NEW PREFIX TO MSG LINE
         MVC   OLD(4*4),MSG2       MESSAGE TO TERMINAL OPERATOR
         LA    R1,6(R1)            INCREMENT THE LENGTH FOR PUTLINE
         STH   R1,PRFXM2S2         STORE LENGTH IN TEXT BLOCK
         B     PUTLINE                                      *HMD 05/82*
NOPREF   DS    0H                                           *HMD 05/82*
         XC    UPTPREFX(8),UPTPREFX      CLEAR PREFIX       *HMD 05/82*
         MVC   NPFXMSG2(8),=CL8'()'      INDICATE NOPREFIX  *HMD 05/82*
         MVC   OLD(4*4),MSG2       CREATE PARM LIST         *HMD 05/82*
         LA    R1,7                LENGTH OF SEGMENT        *HMD 05/82*
         STH   R1,PRFXM2S2         SAVE LENGTH IN TEXT BLK  *HMD 05/82*
PUTLINE  DS    0H
         LA    IOPLBAR,IOPLADS     I/O PARM LIST BASE
         MVC   IOPLUPT,CPPLUPT     USER PROFILE TABLE
         MVC   IOPLECT,CPPLECT     ENVIORNMENT CONTROL TABLE
         MVC   IOPLECB,AECB        ECB POINTER
         XC    ECB,ECB             CLEAR ECB
         PUTLINE OUTPUT=OLD,MF=(E,IOPLADS),PARM=PUTBLOCK
RETURN   DS    0H
         L     R13,4(R13)          RESTORE OLD SAVE AREA
         LM    R14,R12,12(R13)     RESTORE REGISTERS
         SR    R15,R15             SET 0 RETURN CODE
         BR    R14
PRFXBAD  DS    0H
         MVC   OLD(8),MSG3
         B     PUTLINE
ABEND    DS    0H
         ABEND 1,DUMP
         EJECT
NEWPREFX MVC   UPTPREFX(0),0(R2)   NEW PROFILE PREFIX
OPFXM2   MVC   OPFXMSG2(0),UPTPREFX    MOVE PROFILE PREFIX TO MSG LINE
NPFXM2   MVC   NPFXMSG2(0),0(R2)   MOVE NEW PROFILE PREFIX TO MSG LINE
         SPACE 3
IOPLADS  DC    4F'0'
PUTBLOCK PUTLINE MF=L
MSG2     DC    A(3),A(PRFXMSG2),A(PRFXM2S2),A(PRFXM2S3)
MSG3     DC    A(1),A(PRFXMSG3)
OLD      DS    F                   NUMBER OF ENTRIES
         DS    F
         DS    F
         DS    F
         DS    F
PRFXMSG3 DC    H'49'               LENGTH OF MESSAGE
         DC    H'0'                RESERVED
         DC    CL45'FIRST CHAR MUST BE AN ALPHA OR A NATIONAL'
PRFXMSG2 DC    H'35'               LENGTH OF SEGMENT
         DC    H'0'                SEGMENT POSITION
         DC    CL31' NEW PREFIX IS OLD PREFIX WAS  '
PRFXM2S2 DC    H'12'               LENGTH OF SEGMENT
         DC    H'15'               SEGMENT POSITION
NPFXMSG2 DC    CL8' '
PRFXM2S3 DC    H'11'               LENGTH OF SEGMENT
         DC    H'30'               SEGMENT POSITION
OPFXMSG2 DC    CL7' '
APDLPT   DC    A(PDLPT)            POINTER TO THE PDS
APARMTAB DC    V(PARMTAB)          POINTER TO THE PARM TABLE
AECB     DC    A(ECB)              POINTER TO THE ECB FOR PARSE
ECB      DS    F                   PARSE ECB
PDLPT    DS    F
PPLA     DS    7F                  PPL LIST
OPREFX   DS    CL7                 OLD PROFILE PREFIX
OPREFXL  DS    XL1                 LENGTH OF OLD PREFIX
         EJECT                                              *HMD 05/82*
PARMTAB  IKJPARM
PRFX     IKJIDENT 'PREFIX',PROMPT='PROFILE PREFIX',MAXLNTH=7,          *
               FIRST=ALPHA,OTHER=ALPHANUM,CHAR
         IKJENDP
         SPACE 3
         LTORG
         EJECT
         IKJCPPL
         EJECT
         IKJPPL
         EJECT
         IKJUPT
         EJECT
         IKJIOPL
         EJECT
         CVT   DSECT=YES,LIST=YES
          END  PREFIX
