PDS61    TITLE 'P D S  --  PDS COMMAND PROCESSOR              02/06/84'
*   DATA SET PDS AT LEVEL 6.1 ON   2/06/84  (ABL -- THIS VERSION)     *
*   DATA SET PDS AT LEVEL 6 AS OF  7/16/83  (ABL -- MVS/XA VERSION)   *
*   DATA SET PDS AT LEVEL 5 AS OF  7/08/82  (ABL -- MEMBER GROUPS)    *
*   DATA SET PDS AT LEVEL 4 AS OF  9/30/80  (ABL -- MVS ONLY VERSION) *
*   DATA SET PDS AT LEVEL 3 AS OF  1/28/80  (ABL -- MVS VERSION)      *
*   DATA SET PDS AT LEVEL 2 AS OF 11/14/77  (ABL -- MVT VERSION)      *
*   DATA SET PDS AT LEVEL 1 AS OF  6/12/75  (UCLA VERSION)            *
*   DATA SET PDS AT LEVEL 0 AS OF  3/20/72  (FIREMAN'S FUND VERSION)  *
*                                                                     *
*                                                                     *
***                                                                   *
*** BEFORE ASSEMBLING, READ THE FOLLOWING NOTE EXPLANING THE VALUES   *
*** WHICH MAY BE CODED FOR THE SYSPARM VALUE (XXXX BELOW).            *
***                                                                   *
*** TO ASSEMBLE:  ASM  PDS RENT LIB('SYS1.AMODGEN') SYSPARM(XXXX)     *
***                                                                   *
*** TO LINK:      LINK PDS RENT REUS REFR LOAD(LIBNAME(PDS)) PR(*)    *
***                                                                   *
*** NOTE: PDS CAN BE LINKED INTO SYS1.LPALIB TO REDUCE EACH PDS       *
***       USER'S SWAP LOAD; HOWEVER, YOU SHOULD ALSO KEEP A COPY      *
***       OF PDS IN YOUR LINK LIST (WITH THE SAME OR A DIFFERENT      *
***       MEMBER NAME) AS A BACKUP IN CASE YOUR LPALIB IS REPLACED    *
***       IN A FUTURE SYSTEM.                                         *
***                                                                   *
         SPACE 5                                                      *
*   THE PDS COMMAND HAS AN OPTIONAL INTERFACE TO SPF EDIT AND         *
*   SPF BROWSE WHICH IS CONTROLLED BY SYSPARM AS FOLLOWS:             *
*                                                                     *
*           A.  FOR SYSPARM(SPF) OR NO SYSPARM, PDS WILL BE           *
*               GENERATED WITH AN INTERFACE TO THE SPF PROGRAM        *
*               PRODUCT (5668-009).                                   *
*                                                                     *
*           B.  FOR SYSPARM(ISPF), PDS WILL BE GENERATED WITH A       *
*               INTERFACE TO THE ISPF AND ISPF/PDF PROGRAM PRODUCTS   *
*               (5668-960 AND 5665-268).                              *
*                                                                     *
*           C.  FOR SYSPARM(NOSPF), NO SPF OR ISPF INTERFACE WILL     *
*               BE GENERATED IN THE ASSEMBLY OF PDS.                  *
*                                                                     *
*                                                                     *
*   NOTE: IF THE SPF AND THE ISPF PROGRAM PRODUCTS WILL COEXIST AT    *
*   YOUR INSTALLATION, SYSPARM(SPF) SHOULD BE USED:                   *
*                                                                     *
*           A.  THE ONLY DIFFERENCE IN THE PDS CODE GENERATED         *
*               INVOLVES THE LINE LABELLED "ISPFTEXT" AS FOLLOWS:     *
*               FOR SPF  - "ISPF . . . PARM(++12345678)             " *
*               FOR ISPF - "ISPF . . . PARM(++12345678) NEWAPPL(ISR)" *
*                                                                     *
*           B.  THE ABOVE LINE COULD BE MODIFIED BY REASSEMBLING      *
*               PDS WITH A DIFFERENT SYSPARM OR WITH A ZAP PROGRAM    *
*               OF SOME KIND.                                         *
*                                                                     *
*           C.  IF NEWAPPL(ISR) IS PRESENT ON A CALL TO SPF, PDS      *
*               WILL BE TERMINATED.                                   *
*                                                                     *
*           D.  IF NEWAPPL(ISR) IS NOT PRESENT ON A CALL TO ISPF,     *
*               THE DEFAULT VALUES OF ISPF'S PF KEYS WILL BE IN       *
*               EFFECT FOR THE PDS/ISPF SESSION.                      *
*                                                                     *
*                                                                     *
*  THE SPF INTERFACE CONSISTS OF TWO PDS SUBCOMMANDS, SPFEDIT AND     *
*  BROWSE, WHICH REQUEST SPF EDIT AND BROWSE SERVICE FROM THE PDS     *
*  COMMAND.  ON THE FIRST SPFEDIT OR BROWSE SUBCOMMAND, SPF IS        *
*  ATTACHED AND SPF RECURSIVELY CALLS THE PDS COMMAND AS A DIALOG.    *
*                                                                     *
*  SUBSEQUENT USES OF SPFEDIT OR BROWSE SUBCOMMANDS ENTER SPF EDIT    *
*  OR BROWSE SERVICE DIRECTLY.  THE INTERFACE TO SPF REMAINS ACTIVE   *
*  UNTIL THE PDS COMMAND IS TERMINATED.                               *
*                                                                     *
*  SPFEDIT AND BROWSE SUBCOMMANDS CAN NOT BE USED WHILE PROCESSING    *
*  UNDER SPF OPTION 6 SINCE SPF IS ATTACHED BY THE PDS COMMAND AS     *
*  A TSO COMMAND PROCESSOR.  IBM RECOGNIZES THIS AS A GENERAL         *
*  PROBLEM AND SUPPORT MAY BE ADDED IN ISPF TO FIX THIS.              *
*                                                                     *
*  NOTE: IN PDS VERSION 6, AN ADDITIONAL SPF INTERFACE WAS INSTALLED  *
*  WHICH ALLOWS PDS TO BE INVOKED DIRECTLY AS A SPF DIALOG.  WHEN     *
*  PDS IS INVOKED AS A DIALOG BY SPF, SPFEDIT AND BROWSE OPERATE      *
*  WITHOUT THE NEED TO RECURSIVELY INVOKE PDS AS A DIALOG.            *
*                                                                     *
*                                                                     *
*  WHEN THE SPF INTERFACE IS ACTIVE, THE FOLLOWING SHOULD BE NOTED:   *
*                                                                     *
*    A.  ABEND DEBUGGING IN PDS OR AN EXTERNAL SUBCOMMAND IS MORE     *
*        DIFFICULT BECAUSE OF THE STAE PROCESSING PERFORMED BY SPF.   *
*                                                                     *
*    B.  WHEN AN SPFEDIT OR BROWSE SUBCOMMAND IS ENTERED, THREE       *
*        ASTERISKS MAY FIRST APPEAR BELOW THE ENTERED SUBCOMMAND;     *
*        THIS IS VTAM'S PAGE PROTECTION -- THE ENTER KEY MUST BE      *
*        PUSHED TO GET THE BROWSE OR EDIT SCREEN DISPLAYED.           *
*                                                                     *
*    C.  THE SPFEDIT AND BROWSE INTERFACE SHOULD WORK CORRECTLY       *
*        ON ANY SYSTEM WHICH SUPPORTS SPF OR ISPF; THAT IS, IT        *
*        SHOULD WORK WITH ANY VERSION OF VTAM OR TCAM.                *
*                                                                     *
*    D.  FOR THE SPF VERSION OF SPF: IF ANY SPFEDIT OR BROWSE         *
*        SUBCOMMAND IS TERMINATED BY ANY OPTION REQUEST (=1, =2, ...) *
*        AND THE RETURN KEY, THE PDS END SUBCOMMAND MAY RESULT IN A   *
*        S30A ABEND.  PTF UZ61659 AND ITS COREQUISITE, UZ61864, FROM  *
*        THE 8304 PUT TAPE FIX THIS PROBLEM.  ***NOTE: FOR PDS V6.1   *
*        THIS PROBLEM APPEARS TO HAVE LEFT.                           *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         TITLE 'P D S  --  PDS SUPPORT REPRESENTATIVE         02/06/84'
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
*                                                                     *
*                                                                     *
* PLEASE REPORT ANY PROBLEMS, ENHANCEMENTS, SUGGESTIONS OR COMMENTS   *
* CONCERNING THE PDS COMMAND TO BRUCE LELAND AT:                      *
*                                                                     *
*     HITACHI AMERICA, LTD.     OR         6084 CRIMSON DRIVE         *
*     1800 BERING DRIVE                    SAN JOSE, CALIF 95120      *
*     SAN JOSE, CALIF. 95112                                          *
*     (408) 292-6404                       (408) 997-2366             *
*                                                                     *
*                                                                     *
*                                                                     *
*  IF AN ERROR OR ABEND CONDITION PERSISTS IN PDS, TRY THE            *
*  FOLLOWING STEPS FOR PROBLEM RESOLUTION:                            *
*                                                                     *
*                                                                     *
*   1.  CONTACT THE IBM SUPPORT CENTER FOR KNOWN MVS PROBLEMS         *
*                                                                     *
*                                                                     *
*   2.  REPORT THE PROBLEM (WHETHER RESOLVED OR NOT) TO BRUCE LELAND. *
*       PLEASE INCLUDE APPROPRIATE DOCUMENTATION FOR UNRESOLVED       *
*       PROBLEMS AS FOLLOWS:                                          *
*                                                                     *
*       A.  PLEASE USE AN UNLABELLED, 1600 OR 6250 BPI TAPE WITH      *
*           AS MANY FILES AS REQUIRED FOR DOCUMENTATION.              *
*                                                                     *
*       B.  INDICATE IF YOU HAVE SPF OR ISPF AND IF THE PROBLEM       *
*           INVOLVES THE SPF INTERFACE CODE IN PDS.                   *
*                                                                     *
*       C.  IF POSSIBLE, DUPLICATE THE ERROR ON PAPER BY EXECUTING    *
*           PDS IN BACKGROUND MODE.                                   *
*                                                                     *
*       D.  NOTE YOUR MVS OPERATING SYSTEM LEVEL (FOR EXAMPLE,        *
*           SP 1.3.1 AT 8306).                                        *
*                                                                     *
*       E.  NOTE THE PDS LEVEL (OR BETTER YET, UNLOAD A COPY OF THE   *
*           PDS SOURCE CODE INSTALLED AT YOUR INSTALLATION).          *
*                                                                     *
*       F.  UNLOAD A COPY OF THE PDS LOAD MODULE USING IEBCOPY.       *
*                                                                     *
*       G.  UNLOAD THE DATA SET IN QUESTION WITH IEBCOPY OR AN        *
*           APPROPRIATE UTILITY.                                      *
*                                                                     *
*       H.  BE SURE TO INCLUDE YOUR NAME AND PHONE NUMBER SO THAT     *
*           ANY RESULTS OR QUESTIONS CAN BE REFERRED BACK TO YOU.     *
*                                                                     *
*                                                                     *
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         TITLE 'P D S  --  PDS COMMAND PURPOSE                02/06/84'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* TITLE -      P D S     --    TSO PDS COMMAND PROCESSOR              *
*                                                                     *
* FUNCTION -   PROVIDE A TSO USER WITH THE CAPABILITY                 *
*              TO MANIPULATE A PARTITIONED DATA SET                   *
*                                                                     *
* OPERATION -  ACCEPT FROM THE TSO USER THE NAME OF A                 *
*              PARTITIONED DATA SET NAME. AFTER ALLOCATING            *
*              THE DATA SET, PERFORM ANY OF THE SUBCOMMANDS:          *
*                                                                     *
*        ATTRIB   - DISPLAY (OR MODIFY) LOAD MODULE ATTRIBUTES        *
*        ALIAS    - ASSIGN AN ALIAS TO A MEMBER                       *
*        BROWSE   - BROWSE A MEMBER WITH SPF (SPF DIALOG)             *
*        CHANGE   - CHANGE TO A DIFFERENT DATA SET                    *
*        CONTROL  - SET PROGRAM CONTROL VARIABLES                     *
*        DISPLAY  - DISPLAY ALL OR PART OF THE DIRECTORY              *
*        DIRENTRY - DUMP A MEMBER'S DIRECTORY ENTRY                   *
*        DELETE   - DELETE A MEMBER                                   *
*        EDIT     - EDIT A MEMBER                                     *
*        END      - TERMINATE THE PDS COMMAND                         *
*        EXEC     - EXECUTE PDS SUBCOMMANDS FROM A CLIST SOURCE       *
*        FIND     - LIST LINES CONTAINING A SEARCH STRING             *
*        FIXPDS   - MODIFY THE ALLOCATED PARTITIONED DATA SET         *
*        FSE      - EDIT A MEMBER WITH THE FSE COMMAND                *
*        HELP     - DISPLAY SUBCOMMAND HELP FOR PDS                   *
*        HISTORY  - LIST HISTORY OF A LOAD MODULE                     *
*        IF       - TEST A MEMBER FOR GIVEN ATTRIBUTES                *
*        LIST     - LIST CONTENTS OF A MEMBER                         *
*        MAP      - MAP A LOAD MODULE                                 *
*        MEMBERS  - DISPLAY MEMBERS IN A GROUP                        *
*        OPTIONS  - DISPLAY THE SUBCOMMAND MENU                       *
*        OUTCOPY  - OUTPUT IEBCOPY SELECT STATEMENTS                  *
*        PATTERN  - DISPLAY DIRECTORY BASED ON MEMBER NAME SEGMENTS   *
*        PDS      - CHANGE TO A DIFFERENT DATA SET                    *
*        PRINTOFF - PRINT A MEMBER WITH THE TSO PRINTOFF COMMAND      *
*        RECALL   - DISPLAY OR REISSUE THE PREVIOUS SUBCOMMAND        *
*        RENAME   - RENAME A MEMBER                                   *
*        RESTORE  - RESURRECT A PREVIOUSLY DELETED MEMBER             *
*        REVIEW   - BROWSE DATA WITH THE REVIEW COMMAND               *
*        SPFEDIT  - EDIT A MEMBER WITH SPF (SPF DIALOG)               *
*        SUBMIT   - SUBMIT A JCL MEMBER                               *
*        TSO      - EXECUTE A TSO COMMAND                             *
*        TSOLIST  - LIST A MEMBER WITH THE TSO LIST COMMAND           *
*        USAGE    - LIST DATA SET STATISTICS                          *
*        VERIFY   - CHECK THE DATA SET FOR VALIDITY ERRORS            *
*                                                                     *
*                                                                     *
* WRITTEN    - BY TOM SPRINGER AT FIREMAN'S FUND                      *
*                                                                     *
* MODIFIED   - BY WILLIAM FINKELSTEIN, WITH CITIBANK IN LOS ANGELES   *
*                                                                     *
* MAINTAINED - BY BRUCE LELAND WITH HITACHI AMERICA, LTD.             *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'P D S  --  PDS COMMAND CHANGE DOCUMENTATION   02/06/84'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*  CHANGE SECTION:                                                    *
*                                                                     *
****VER 2.0: ADDED FORMATTING OPTIONS FOR LIST;                    ****
*            ADDED EDIT, FIND AND SUBMIT SUBCOMMANDS.                 *
*                                                                     *
****VER 2.2: CORRECTED AN ERROR IN LIST AND FIND FOR DATA SETS     ****
*       WITH ASA OR MACHINE CARRIAGE CONTROLS.                        *
*                                                                     *
****VER 2.3: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY DINESH      ****
*       DATTANI FROM PRUDENTIAL INSURANCE CO. OF AMERICA, TORONTO     *
*       1.  OUTPUT THE PROGRAM OPTION LIST IN SORTED ORDER.           *
*       2.  ADDED SEVERAL EDIT DESCRIPTIVE QUALIFIERS.                *
*                                                                     *
****VER 3.0: UPDATED THE PDS COMMAND FOR MVS USE BASED ON A        ****
*       VERSION OF THE PDS COMMAND ON THE CBT MODS TAPE.              *
*       1.  ADDED EXEC (EXPLICIT AND IMPLIED) AND HELP SUBCOMMANDS.   *
*       2.  A SINGLE CHARACTER SUBCOMMAND NAME IS NOT AMBIGUOUS.      *
*       3.  SUPPORT WAS ADDED IN THE FOLLOWING AREAS:                 *
*           -- SSI INFORMATION DISPLAY                                *
*           -- APF DATA CAN BE LISTED AND MODIFIED                    *
*           -- HEXADECIMAL MEMBER NAMES ALLOWED AS INPUT              *
*           -- HEXADECIMAL OUTPUT ADDED - DISPLAY, PATTERN, MEMBERS   *
*           -- $PUTGET, $PUTLINE AND MESSAGE MACROES WERE ADDED       *
*           -- ATTRIB LISTS ALL ALIASES FOR A GIVEN MODULE            *
*       4.  SEVERAL SOURCES OF 0C7'S WERE CORRECTED FOR IDR DATA.     *
*       5.  THE MEMBER NAME VALIDITY CHECK ROUTINE HAD TWO BUGS:      *
*           --  THE MEMBER NAME LENGTH USED WAS THE INPUT NAME        *
*               LENGTH (INCORRECT FOR HEXADECIMAL NAMES)              *
*           --  PROMPTING FOR INVALID MEMBER NAMES WAS INCORRECT      *
*       6.  THE ATTRIB SUBCOMMAND SOMETIMES LISTED SSI INFORMATION    *
*           FOR NON-LOAD MODULES WHICH DID NOT HAVE ANY SSI DATA.     *
*                                                                     *
****VER 3.1: SEVERAL GENERAL CHANGES WERE MADE                     ****
*       1.  THE PATTERN SUBCOMMAND WAS ADDED.                         *
*       2.  DEFAULT MEMBER NAMES ARE ALLOWED FOR ATTRIB, BROWSE,      *
*           DELETE, DIRENTRY, EDIT, FSE, FIND, HISTORY, IF, LIST,     *
*           MAP, MEMBERS, OUTCOPY, PRINTOFF, REVIEW, SPFEDIT,         *
*           SUBMIT AND TSOLIST SUBCOMMANDS.                           *
*       3.  PDS WAS ADDED AS AN ALIAS FOR THE CHANGE SUBCOMMAND.      *
*       4.  ALIAS SUBCOMMAND IS NOT PREVENTED FROM WORKING IF THE     *
*           APF DATA LENGTH IS INVALID (APF=0 IS USED).               *
*       5.  EDIT PREVIOUSLY USED THE PREVIOUS PDL FOR DEFAULT         *
*           MEMBERS; NOW THE EDIT WORK AREA IS SAVED BEFORE EACH      *
*           SUBCOMMAND SCAN.                                          *
*                                                                     *
****VER 4.0: MADE SEVERAL IMPROVEMENTS AS SUGGESTED BY SEYMOUR     ****
*        METZ FROM COMNET IN WASHINGTON, D.C.:                        *
*        1.  REPLACED IBM/360 INSTRUCTIONS WITH THEIR IBM/370         *
*            EQUIVALENTS (ICM, STCM, MVCL, ...)                       *
*        2.  REPLACED BCR INSTRUCTIONS WITH EQUIVALENT MNEMONICS.     *
*        3.  TSO SERVICE ROUTINE ADDRESSES ARE NOW OBTAINED FROM      *
*            THE CVT INSTEAD OF VIA LOAD/DELETE CODE.                 *
*                                                                     *
****VER 4.1: SEVERAL ADDITIONAL CHANGES WERE MADE TO TAKE          ****
*        ADVANTAGE OF MVS PROGRAM LOGIC AND SERVICE ROUTINES:         *
*        1.  USED THE DAIRFAIL MESSAGE ROUTINE INSTEAD OF INTERNALLY  *
*            GENERATING MESSAGES AS SUGGESTED BY SEYMOUR METZ.        *
*        2.  USED THE GENERAL FAIL MESSAGE ROUTINE FOR PARSE ERRORS.  *
*        3.  USED THE TSO DEFAULT ROUTINE TO FULLY QUALIFY DSNAMES.   *
*        4.  CHANGED THE ALLOCATION STRATEGY TO "PERMANENTLY"         *
*            ALLOCATE DATA SETS SO THAT EXTERNAL PDS SUBCOMMANDS      *
*            CAN USE AN UNCATALOGED DATA SET AFTER IT IS ALLOCATED    *
*            BY PDS.  THIS CHANGE WAS FIRST SUGGESTED BY JOE RAREY    *
*            FROM MARTIN-MARRIETA IN FLORIDA.                         *
*                                                                     *
****VER 4.2: AFTER THE DAIRFAIL CHANGES INDICATED ABOVE, THE       ****
*            VOLUME PARAMETER FOR UNCATALOGED DATA SETS DID NOT WORK. *
*                                                                     *
****VER 4.3: CORRECTED ONE SOURCE OF 0C4'S IN LISTING HISTORY      ****
*            DATA AS REPORTED BY ARNIE CASINGHINO FROM CBT.           *
*                                                                     *
****VER 4.4: ADDED THE RESTORE SUBCOMMAND TO RESURRECT DELETED     ****
*            PDS MEMBERS.  ALSO, CLEANED UP SOME PDS CODE AS          *
*            SUGGESTED BY SEYMOUR METZ FROM COMNET AND ADDED A        *
*            SYSPARM ASSEMBLY PARAMETER FOR GENERATING PDS.           *
*                                                                     *
****VER 4.5: ADDED THE FOLLOWING SUBCOMMANDS                       ****
*                                                                     *
*            TSOLIST: A SUBCOMMAND TO LINK TO THE TSO LIST COMMAND    *
*            AS SUGGESTED BY SEYMOUR METZ.                            *
*                                                                     *
*            PRINTOFF: A SUBCOMMAND TO LINK TO THE PRINTOFF COMMAND   *
*            AS SUGGESTED BY DAVID FILSINGER OF FACSO (SHARE CODE CB) *
*                                                                     *
*            DELETE:  NEW, PREFERRED ALIAS FOR THE SCRATCH SUBCOMMAND *
*                                                                     *
****VER 5.0: ADDED ANOTHER BASE REGISTER AND MADE SEVERAL          ****
*            SUBCOMMAND CHANGES:                                      *
*                                                                     *
*        ATTRIB, BROWSE, DELETE, DIRENTRY, EDIT, FIND, FSE, HISTORY,  *
*        IF, LIST, MAP, MEMBERS, OUTCOPY, PRINTOFF, REVIEW, SPFEDIT,  *
*        SUBMIT AND TSOLIST:                                          *
*          1.  ADDED THE * NOTATION (MEANS USE PREVIOUS MEMBER NAME)  *
*          2.  ADDED MEMBER NAME GROUPS (RANGES AND PATTERNS)         *
*          3.  ENTER "HELP GROUP" FOR MORE INFORMATION                *
*                                                                     *
*        BROWSE AND SPFEDIT: ADDED SUBCOMMANDS WHICH MAKE PDS A SPF   *
*        DIALOG SO IT CAN USE BROWSE AND EDIT SERVICE FOR A MEMBER.   *
*        HELP ON FULL SCREEN, SPF AND ISPF INTERFACES WAS PROVIDED    *
*        BY MIKE LOOS WITH DELUXE CHECK PRINTERS IN MINNESOTA.        *
*                                                                     *
*        ATTRIB:                                                      *
*          1.  FORMAT AND DISPLAY ANY SPF STATISTICS IN SPF FORMAT.   *
*          2.  DISPLAY ENTRY POINT ADDRESS, MODULE SIZE AND THE LAST  *
*              LINKAGE-EDIT DATE FOR LOAD MODULES.                    *
*          3.  FIXED A STORAGE OVERLAY FOR MEMBERS WITH MANY ALIASES. *
*          4.  ADDED THE PAGE AND NOPAGE KEYWORDS.                    *
*                                                                     *
*        ALIAS:                                                       *
*          1.  NOW WORKS FOR SCATTER LOADED MEMBERS                   *
*          2.  IF A MODULE IS CREATED BY THE OS/VS LINKAGE EDITOR     *
*              AND HAS SSI INFORMATION, THE ALIAS WILL ALSO.          *
*          3.  AN ALIAS CAN NOW BE ASSIGNED TO AN ALIAS MEMBER.       *
*          4.  FIXED A BUG: AN ASSIGNED ENTRY POINT OF ZERO           *
*              SOMETIMES DID NOT GET THE ENTRY POINT ZERO FLAG SET.   *
*                                                                     *
*        EDIT: ALLOWS EDITING OF NEW MEMBERS.                         *
*                                                                     *
*        RESTORE: NOW OPERATES ON LOAD MODULES.                       *
*                                                                     *
*        ATTRIB AND MAP: IF NO REAL MEMBER NAME CAN BE FOUND FOR AN   *
*        ALIAS IN A LOAD LIBRARY, THE NAME STORED IN THE DIRECTORY    *
*        ENTRY AS THE REAL ENTRY IS DISPLAYED.                        *
*                                                                     *
*        FIND: ADDED FIRST/NOFIRST KEYWORDS AND HEXADECIMAL STRINGS.  *
*                                                                     *
*        FIND AND LIST:                                               *
*          1.  ADDED SUPPORT FOR RECORD FORMAT U DATA SETS.           *
*          2.  ADDED SUPPORT FOR NON-PARTITIONED DATA SETS.           *
*          3.  ADDED LBLOCK, LDUMP, BLOCK AND DUMP FORMATS.           *
*          4.  ADDED SKIPREC, MAXIN, MAXOUT, SKIPCOL AND MAXLEN.      *
*          5.  NUM FORMAT CHECKS FOR DATA SAVED BY SPF EDIT.          *
*          6.  NUM FORMAT CHECKS FOR NUMERIC LINE NUMBERS AND         *
*              SWITCHES TO NONUM MODE FOR THE REMAINDER OF A          *
*              MEMBER IF AN INVALID LINE NUMBER IS ENCOUNTERED.       *
*                                                                     *
*        PATTERN AND DISPLAY:  UNPRINTABLE LETTERS ARE DISPLAYED AS   *
*        PERIODS.  ALSO, HEXADECIMAL/CHARACTER DISPLAYS ARE FORCED    *
*        FOR ANY MEMBER NAME WHICH IS NOT VALID.  NOTE: VALID MEMBER  *
*        NAMES CONTAIN UPPER-CASE ALPHANUMERIC CHARACTERS (WHICH      *
*        INCLUDES #, $ AND @) AND THE FIRST CHARACTER OF A VALID      *
*        MEMBER NAME IS UPPER-CASE ALPHABETIC.                        *
*                                                                     *
*        CHANGE AND PDS:                                              *
*          1.  IF A DSNAME IS NOT ENTERED WITH THE SUBCOMMAND,        *
*              THE PREVIOUSLY USED DATA SET IS ASSUMED.               *
*          2.  ADDED THE SHR AND OLD DATA SET KEYWORDS.               *
*                                                                     *
*        DIRENTRY: SUBCOMMAND WAS ADDED TO DUMP A DIRECTORY ENTRY.    *
*                                                                     *
*        MEMBERS: SUBCOMMAND WAS ADDED TO DISPLAY A MEMBER GROUP.     *
*                                                                     *
*        RESTORE, LIST, MAP, HISTORY AND ALIAS: FIXED A BUG; THE      *
*        NEWEXTENT SUBROUTINE ALLOWED THE USE OF ONE TOO MANY EXTENTS.*
*                                                                     *
****VER 5.1:  MADE SOME CODE CLEANUP CHANGES;                      ****
*                                                                     *
*        DELETED THE READ OF THE JFCB FOR THE INPUT DATA SET.         *
*                                                                     *
*        EXCP'S ARE NOW ALL IN AN EXCP SUBROUTINE; SET AND READ       *
*        SECTORS ARE PERFORMED FOR ALL DISK DRIVES EXCEPT 2314'S.     *
*        THE EXCP SUBROUTINE REALLOCATES ITS INPUT BUFFER IF NEEDED.  *
*                                                                     *
*        AN OBTAIN IS ALWAYS PERFORMED ONCE FOR EACH INPUT DATA       *
*        SET (NOT WHEN NEEDED AS BEFORE IN RESTORE AND USAGE).        *
*                                                                     *
*        ALL PDS BLDL'S ARE PERFORMED BEFORE INVOKING THE PROCESSING  *
*        SUBROUTINE.  IN ADDITION, PDS MAINTAINS THE STATUS OF THE    *
*        CURRENT BLDL SO THAT UNNECESSARY BLDL'S CAN BE AVOIDED.      *
*                                                                     *
*        ADDED A RESTORE SUBCOMMAND TEST FACILITY:  AN EXISTING       *
*        MEMBER MAY BE "RESTORED" IF THE RESTORE MEMBER NAME BEGINS   *
*        WITH "TEMP".  USE THE DIRENTRY SUBCOMMAND TO COMPARE THEIR   *
*        DIRECTORY ENTRIES (REMEMBER TO DELETE THE TEMP MEMBER SINCE  *
*        YOU HAVE AN APPARENT ALIAS -- NEITHER HAS AN ALIAS BIT SET). *
*                                                                     *
****VER 5.2:  ADDED SUPPORT FOR TRACK OVERFLOW RECORDS AS WELL     ****
*        AS THE FOLLOWING OTHER CHANGES:                              *
*                                                                     *
*        CHANGE AND PDS: IF A VOLUME NAME IS SUPPLIED, SYSALLDA IS    *
*        USED AS THE UNIT NAME; OTHERWISE, THE UNIT NAME FROM THE     *
*        PSCB WOULD BE USED (WHICH MIGHT NOT COVER THE VOLUME TO BE   *
*        ALLOCATED).  REPORTED BY BILL COWAN WITH THE COCA-COLA       *
*        COMPANY IN ATLANTA, GEORGIA.                                 *
*                                                                     *
*        DELETE: ALLOWS MEMBER GROUPS AND DEFAULT MEMBER NAMES AS     *
*        REQUESTED BY ARNIE CASINGHINO FROM CBT.                      *
*                                                                     *
*        ATTENTION HANDLING IMPROVEMENTS -- TWO ATTENTIONS IN A ROW   *
*        ARE GENERALLY REQUIRED TO TERMINATE PDS; ALL SUBCOMMANDS     *
*        NOW RECOGNIZE ATTENTION INTERRUPTS TO SOME DEGREE:           *
*        A.  PDS ATTENTION HANDLING IS NOW DOCUMENTED IN PDS'S HELP   *
*            (ENTER "HELP ATTN" FOR MORE INFORMATION).                *
*        B.  FIXED A BUG: A NULL RESPONSE TO "ENTER OPTION" AFTER     *
*            AN ATTENTION DID NOT ALLOW THE SUBCOMMAND TO CONTINUE.   *
*        C.  THE INPUT ROUTINE NOW ALSO CHECKS FOR ATTENTION -- THIS  *
*            ELIMINATES WAITING UNTIL A MESSAGE IS OUTPUT AS BEFORE.  *
*                                                                     *
*        OPEN: FIXED ABEND 013-D0 FOR RECFM=FS OR FBS.  THIS PROBLEM  *
*        WAS REPORTED BY JAMES BERRY FROM WESTLAKE VILLAGE, CALIF.    *
*                                                                     *
*        ALIAS, RENAME AND RESTORE: CHANGE THE DEFAULT MEMBER NAME AS *
*        SUGGESTED BY JOHN SULLIVAN AT LOMA LINDA UNIVERSITY, CALIF.  *
*                                                                     *
****VER 5.3:  CHANGED THE CSECT STRUCTURE OF PDS AND MADE THE      ****
*        FOLLOWING OTHER CHANGES:                                     *
*                                                                     *
*        SPFEDIT AND BROWSE:                                          *
*        A.  THE INITIALIZATION ADDRESS FOR PDS PASSED TO ISPF IS     *
*            NOW CONVERTED TO DECIMAL BEFORE THE CALL AND CONVERTED   *
*            BACK TO BINARY ON THE RECURSIVE ENTRY IN PDS DUE TO THE  *
*            POTENTIAL PROBLEM OF A X'5D' (A RIGHT PARENTHESIS) IN    *
*            THE ADDRESS.  THIS WAS REPORTED BY SEYMOUR METZ, U.S.    *
*            DEPARTMENT OF STATE.                                     *
*        B.  A PARAMETER LIST PASSED TO INITIALIZE SPF OR ISPF MUST   *
*            NOT BE REUSED BY THE CALLING PROGRAM SINCE SPF USES IT   *
*            LATER IF IT SPLITS THE USER'S SCREEN.  THIS PROBLEM WAS  *
*            RESOLVED BY MIKE LOOS WITH DELUXE CHECK PRINTERS.        *
*        BROWSE: THE ALLOCATED FILE IS NOW FREED BEFORE EACH CALL     *
*        TO SPF SINCE THE DATA SET COULD BE UPDATED IN A SUBSEQUENT   *
*        SPLIT SCREEN.                                                *
*                                                                     *
*        EDIT AND SPFEDIT: ADDED NEW AND OLD KEYWORDS TO ALLOW BLDL   *
*        CHECKING OF MEMBER NAMES BEFORE ENTERING EITHER EDITOR.      *
*                                                                     *
*        END: THE RETURN CODE ASSIGNED BY THE PROGRAM IS NOW SAVED    *
*        TEMPORARILY IN THE PROGRAM'S STORAGE; IN VERSION 5.2 THE     *
*        RETURN CODE IS CHANGED DURING A CALL TO FREE THE FILE AND    *
*        A STACK DELETE IS INCORRECTLY ISSUED AT PDS TERMATION.       *
*                                                                     *
*        CHANGE AND PDS: THE DATA SET ALLOCATION STATUS IS DISPLAYED  *
*        AFTER EACH DATA SET IS ALLOCATED.                            *
*                                                                     *
*        ATTRIB:                                                      *
*          1.  ADDED ALIASINFO AND NOALIASINFO KEYWORDS               *
*          2.  ADDED LKEDDATE AND NOLKEDDATE KEYWORDS                 *
*          3.  A LIST OF ATTRIBUTES TO BE ASSIGNED TO A MODULE        *
*              CAN NO LONGER BE PLACED IN PARENTHESES.                *
*                                                                     *
*        UNDOCUMENTED SUBCOMMANDS:                                    *
*          1. DSNAME (ALLOCATION STATUS OF THE CURRENT DATA SET)      *
*          2. KLEAR (OR K -- CLEARS 3270 DISPLAY SCREENS)             *
*          3. W (DUMMY SUBCOMMAND FOR EXECUTING PDS UNDER TEST)       *
*          4. OUTCOPY  --  OPERANDS RECORD AND NORECORD CONTROL       *
*                WRITING TERMINAL INPUT AND OUTPUT TO FILE(PDSOUT)    *
*                                                                     *
*        IF: ADDED SUBCOMMAND TO TEST MEMBERS FOR GIVEN ATTRIBUTES    *
*        AND PERFORM CONDITIONAL PROCESSING AS SUGGESTED BY JIM BERRY *
*        FROM WESTLAKE VILLAGE, CALIFORNIA:                           *
*           COULD ADD:  CREATED(81039:82039)   FOR LOAD AND SPF       *
*                       MODIFIED(81039:82039)  FOR LOAD AND SPF       *
*                                                                     *
*        FIND: ADDED THEN AND ELSE CONDITIONAL PROCESSING             *
*        CAPABILITIES AS IMPLEMENTED IN THE IF SUBCOMMAND.            *
*                                                                     *
*        MAP: DELETED THE SMAP SUBCOMMAND AND MADE SEVERAL CHANGES -  *
*           1.  ADDED FULL, SHORT AND ENTRY KEYWORDS                  *
*           2.  ADDED MODULE NAME FILTERING                           *
*                                                                     *
*        HISTORY: ADDED TRANSLATOR IDENTIFICATION MESSAGES AND        *
*        MADE SEVERAL ADDITIONAL CHANGES -                            *
*           1.  ADDED MODULE NAME FILTERING                           *
*           2.  ADDED TRAN(PARTNAME)                                  *
*           3.  ADDED ZAP(PARTNAME)                                   *
*           4.  ADDED USER(PARTNAME)                                  *
*           5.  FIXED A BUG - IF A IDR USER RECORD HAD CONTINUED      *
*               HEADER DATA, ITS DATE WAS NOT INITIALIZED.            *
*                                                                     *
*        MEMBER PROTECTION: THE DELETE, EDIT, FIXPDS, FSE, SPF AND    *
*        RENAME SUBCOMMANDS ISSUE SPF-STYLE ENQUEUE CHECKS BEFORE     *
*        UPDATING THE ALLOCATED DATA SET.  THIS ENQUEUE CHECK CANNOT  *
*        DETECT A USER FROM ANOTHER CPU UPDATING THE MEMBER THROUGH   *
*        SHARED DASDI (THIS IS ALSO AN SPF PROBLEM).                  *
*                                                                     *
*        DATA SET PROTECTION: THE PDS GENERATION OPTIONS OF OLD, SHR, *
*        AND OPER HAVE BEEN REMOVED -- THE DATA SET'S INTEGRITY CAN   *
*        BE INSURED BY USING DISPOSITION "OLD" ON THE CHANGE OR PDS   *
*        SUBCOMMAND.  ALSO, SEE THE FOLLOWING NOTES ON SHR UPDATES    *
*        AND MEMBER PROTECTION.                                       *
*                                                                     *
*        SHR UPDATES: PDS NOW USES SPF RESERVE LOGIC TO PROTECT THE   *
*        INTEGRITY OF DATA SETS ALLOCATED AS SHR DURING ANY UPDATES.  *
*        THIS CHANGE AFFECTS THOSE PDS SUBCOMMANDS WHICH UPDATE THE   *
*        DATA SET BEING PROCESSED (ALIAS, ATTRIB WITH ATTRIBUTE       *
*        CHANGES, DELETE, FIXPDS, RENAME, RESTORE AND VERIFY          *
*        SUBCOMMANDS) AND WAS SUGGESTED BY ARNIE CASINGHINO WITH CBT. *
*        ENTER "HELP UPDATE" FOR MORE INFORMATION.                    *
*                                                                     *
*        ESTAE PROCESSING: PDS CAN NOW RECOVER FROM ABENDS (EXCEPT    *
*        IN THE SPFEDIT AND BROWSE SUBCOMMANDS).  ENTER "HELP ABEND"  *
*        FOR MORE INFORMATION.                                        *
*                                                                     *
*        RESTORE: ADDED CONTROL KEYWORDS AS SUGGESTED BY              *
*        GEORGE GOFFE WITH THE AMDAHL CORPORATION, SUNNYVALE, CALIF.  *
*                                                                     *
*        EXTERNAL SUBCOMMANDS: SUBCOMMANDS EXTERNAL TO PDS ARE NOW    *
*        ATTACHED INSTEAD OF LINKED TO.  THIS WAS SUGGESTED BY        *
*        SEYMOUR METZ WITH THE STATE DEPARTMENT.  THIS CHANGE MAKES   *
*        THE FOLLOWING IMPROVEMENTS POSSIBLE:                         *
*           1.  PDS CAN RECOVER FROM ABENDS IN THESE SUBCOMMANDS      *
*           2.  PDS CAN FREE ALLOCATED FILES AND MOST OTHER RESOURCES *
*               OBTAINED BY THESE SUBCOMMANDS                         *
*           3.  PDS CAN PERFORM ATTENTION PROCESSING IN BEHALF OF     *
*               THESE SUBCOMMANDS                                     *
*                                                                     *
*        ATTRIB: ANY APPARENT ALIASES FOR A MEMBER ARE DISPLAYED (AS  *
*        WELL AS ACTUAL ALIASES) DURING THE ALIAS SEARCH FOR A MEMBER.*
*                                                                     *
*        ATTRIB AND MAP: ALL CORRESPONDING REAL MEMBERS FOR AN ALIAS  *
*        MEMBER ARE DISPLAYED (NOT JUST THE FIRST ONE AS BEFORE).     *
*                                                                     *
*        BACKGROUND USE OF PDS: A BLANK LINE IS OUTPUT BETWEEN EACH   *
*        SUBCOMMAND FOR READABILITY; ALSO, THE PROGRAM CIRCUMVENTS    *
*        PROMPTS FOR YES/NO DECISIONS BY SIMULATING YES RESPONSES.    *
*                                                                     *
*        TSSO SUPPORT:  A SINGLE SUBCOMMAND MAY NOW BE ENTERED ON THE *
*        PRIMARY COMMAND LINE; PDS WILL PROCESS THE SUBCOMMAND AND    *
*        TERMINATE.  THIS WAS SUGGESTED BY ARNIE CASINGHINO WITH CBT. *
*                                                                     *
*        FIXPDS:  ADDED SUBCOMMAND TO MODIFY THE ALLOCATED DATA SET.  *
*        THE EXPANDDIR OPTION WAS DERIVED FROM FILE 288 OF THE CBT    *
*        TAPE AS SUGGESTED BY ARNIE CASINGHINO WITH CBT.              *
*                                                                     *
*        NON-PARTITIONED DATA SETS:  PARSE DATA FOR THE MEMBER NAME   *
*        POSITITION IS NO LONGER ENTERED. ALSO, SUPPORT HAS BEEN      *
*        ADDED FOR NON-PARTITIONED DATA SETS FOR BROWSE, EDIT, FIND,  *
*        FSE, LIST, PRINTOFF, SPFEDIT, SUBMIT, REVIEW, TSOLIST AND    *
*        VERIFY.                                                      *
*                                                                     *
****VER 6.0:  MADE THE FOLLOWING CHANGES:                          ****
*                                                                     *
*        SPF AND ISPF USERS: PDS CAN BE INVOKED AS A SPF DIALOG USING *
*        THE PROVIDED SPF PANEL AND MESSAGE MEMBER.  THIS CHANGE      *
*        WAS SUGGESTED BY JAMES W. BERRY FROM WESTLAKE VILLAGE, CAL.  *
*                                                                     *
*        RENAME: IF A NON-ALIAS LOAD MODULE IS RENAMED, ADDITIONAL    *
*        CODE IN RENAME WILL UPDATE THE CHANGED NAME POINTERS IN      *
*        ANY ASSOCIATED ALIAS DIRECTORY ENTRIES.                      *
*                                                                     *
*        VERIFY: ADDED A SUBCOMMAND TO CHECK A DATA SET FOR           *
*        VALIDITY ERRORS.                                             *
*                                                                     *
*        IF: ADDED SEVERAL MEMBER VALIDITY CHECKS IN SUPPORT OF       *
*        THE NEW VERIFY SUBCOMMAND.                                   *
*                                                                     *
*        FROM ARNIE CASINGHINO (CBT):                                 *
*        A.  MODIFIED THE SCREEN CLEAR TPUT FOR KLEAR TO SUPPORT      *
*            3278-5 TERMINALS UNDER ACF/TCAM.                         *
*        B.  MODIFIED RESERVE LOGIC FOR BATCH EXECUTIONS OF PDS       *
*            TO HOLD RESERVES UNTIL A NEW DATA SET IS SELECTED.       *
*        C.  ADDED KEYWORDS TO THE RESTORE SUBCOMMAND TO ALLOW        *
*            MULTIPLE MEMBER RESTORATIONS WITH AND WITHOUT PROMPTING. *
*        D.  MODIFIED FIXPDS TO ADD AN PADDING DIRECTORY BLOCK IF     *
*            A TRACK WILL BE FILLED WITH DIRECTORY BLOCKS.  THIS      *
*            IS DONE TO ENSURE THAT THE END OF FILE MARK IS ON THE    *
*            FOLLOWING TRACK AND IT AVOIDS IEBCOPY ABENDS.            *
*        E.  MODIFIED RENAME TO WORK ON A RANGE OF MEMBERS.           *
*                                                                     *
*        FROM GEORGE GOFFE (AMDAHL CORPORATION):                      *
*        A.  ADDED A SWAP OPERAND TO THE RENAME SUBCOMMAND TO         *
*            ALLOW TWO MEMBERS TO EXCHANGE MEMBER NAMES.              *
*        B.  THE PATTERN SUBCOMMAND MAY BE ENTERED WITHOUT OPERANDS   *
*            AND THE PREVIOUS PATTERN OPERAND(S) WILL BE USED.        *
*        C.  ADDED THE RECALL SUBCOMMAND TO DISPLAY OR REISSUE        *
*            AND DISPLAY THE PREVIOUSLY ENTERED SUBCOMMAND.           *
*                                                                     *
*        FROM JOHN SULLIVAN (LOMA LINDA UNIVERSITY MEDICAL CENTER):   *
*        A.  ADDED A NONE OPERAND FOR THE ATTRIB SUBCOMMAND TO        *
*            CHANGE ALL ATTRIBUTES OF A MODULE TO DEFAULT VALUES.     *
*        B.  CHANGED ALL MODULE SIZE DISPLAYS TO OUTPUT THE NEXT      *
*            HIGHER 1K BOUNDARY RATHER THAN THE CLOSEST 1K BOUNDARY.  *
*        C.  MODIFIED ATTRIB TO ALLOW DC ATTRIBUTE CHANGES.           *
*                                                                     *
*        HISTORY:  CORRECTED TO LIST HISTORY DATA IN DESCENDING ORDER *
*        IN EACH GROUP BY LAST CHANGE DATE, ADDED LINKAGE-EDITOR NAME *
*        FILTERING AND CHANGED SUBCOMMAND DEFAULTS.                   *
*                                                                     *
*        USE OF PDS CLISTS OR STACKED INPUT: THE PROGRAM CIRCUMVENTS  *
*        PROMPTS FOR YES/NO DECISIONS BY ASSUMING YES RESPONSES.      *
*                                                                     *
*        IF, VERIFY, ATTRIB: MADE CHANGES TO CHECK FOR MUTUALLY       *
*        EXCLUSIVE LOAD MODULE ATTRIBUTES (FOR EXAMPLE, OVERLAY       *
*        ATTRIBUTE WITH THE REENTRANT ATTRIBUTE).                     *
*                                                                     *
*        MVS/XA SUPPORT - MADE CHANGES FOR THE FOLLOWING SUBCOMMANDS: *
*        A.  IF: ADDED SELECTION BY RMODE AND AMODE                   *
*        B.  ATTRIB: ADDED RMODE AND AMODE ATTRIBUTE SUPPORT:         *
*              1.  ADDED RMODE AND AMODE CHANGE CAPABILITY            *
*              2.  ADDED RMODE AND AMODE DISPLAY CAPABILITY FOR       *
*                  MODULES WHOSE RMODE AND AMODE ARE NOT BOTH 24      *
*              3.  IF RMODE OR AMODE IS CHANGED FOR A NON-ALIAS       *
*                  MODULE, ADDITIONAL CODE IN ATTRIB WILL UPDATE      *
*                  THE RMODE AND THE MAIN MODULE AMODE VALUE FOR      *
*                  ANY ASSOCIATED ALIAS DIRECTORY ENTRIES.            *
*        C.  RESTORE: ADDED CODE TO SET THE RMODE AND AMODE FOR       *
*            A MODULE BASED ON INFORMATION FROM ITS ESD ENTRIES       *
*        D.  ALIAS: ADDED CODE TO SET THE RMODE AND AMODE BASED ON    *
*            ESD INFORMATION AND THE ALIAS BASE ENTRY'S ATTRIBUTES    *
*        E.  MAP: ADDED CODE TO DISPLAY RMODE AND AMODE FOR CSECTS    *
*            WHOSE RMODE AND AMODE ARE NOT BOTH 24                    *
*                                                                     *
*        OUTCOPY: ADDED A NEW SUBCOMMAND TO PRODUCE IEBCOPY CONTROL   *
*        STATEMENTS WHICH CAN BE USED TO SELECTIVELY COPY PDS MEMBERS *
*                                                                     *
*        FROM WALT FARRELL WITH RAINER NATIONAL BANK IN SEATTLE:      *
*        A.  THE "ENTER OPTION" PROMPT MESSAGE HAS BEEN EXPANDED      *
*            TO INCLUDE THE DSNAME AND VOLUME SERIAL NAME.            *
*        B.  ATTRIB FOR NON-LOAD MEMBERS, BROWSE, DELETE, FIND, FSE,  *
*            HISTORY, IF, MAP, OUTCOPY, PRINTOFF, REVIEW AND SPF      *
*            SUBCOMMANDS:  THE MEMBER NAME IDENTIFICATION FOR MEMBER  *
*            GROUPS IS NORMALLY SUPPRESSED.  FOR THE DELETE, FIND,    *
*            HISTORY, IF AND MAP SUBCOMMANDS, HOWEVER, THE HEADER IS  *
*            WRITTEN IF ANY OUTPUT IS GENERATED FOR A MEMBER.         *
*                                                                     *
*        SPFEDIT AND BROWSE: A DIALOG ERROR NO LONGER TERMINATES THE  *
*        PDS COMMAND, HOWEVER, THE PDS COMMAND WILL NOT ATTEMPT TO    *
*        REUSE THE SPF INTERFACE AFTER A DIALOG ERROR.                *
*                                                                     *
*        ATTENTION PROCESSING: STAX PROCESSING HAS BEEN MODIFIED      *
*        AND TWO ATTENTIONS SHOULD NOW TERMINATE A PDS PROGRAM LOOP.  *
*                                                                     *
*        FIXPDS:  MODIFIED TO PREVENT A RESET OR EXPANDED DIRECTORY   *
*        FROM OVERFLOWING THE FIRST DISK EXTENT.                      *
*                                                                     *
*        FROM RICHARD MCCARTY WITH HITACHI AMERICA IN SAN JOSE:       *
*        IN FIXPDS EXPAND, IF THE LAST ALLOCATED DIRECTORY BLOCK WAS  *
*        COMPLETELY FULL, PDS COULD NOT ADD ANY DIRECTORY BLOCKS.     *
*                                                                     *
*        DELETE: ADDED THE ALIAS OPERAND                              *
*                                                                     *
*        ATTRIB FOR NON-LOAD MEMBERS, BROWSE, FSE, PRINTOFF, REVIEW   *
*        AND SPFEDIT SUBCOMMANDS: THE DEFAULT MEMBER NAME MESSAGE IS  *
*        SUPPRESSED TO REDUCE THE LEVEL OF REDUNDANT INFORMATION.     *
*                                                                     *
*        FROM BEN MOORE (FEDERAL EXPRESS):                            *
*        A.  IF, ATTRIB, VERIFY: CHECKS FOR THE DIRECTORY             *
*            RLD/CONTROL COUNT NOT MATCHING THE FIRST RLD ENTRY.      *
*        B.  IF: ADDED THE RLDZERO AND NORLDZERO KEYWORDS.            *
****VER 6.1:  MADE SEVERAL MODIFICATIONS:                          ****
*                                                                     *
*        FIND AND LIST:                                               *
*        A.  CHANGED LOGIC TO SUPPORT OUTPUT OF CSECT INFORMATION     *
*            AS SUGGESTED BY BEN MOORE WITH FEDERAL EXPRESS.          *
*        B.  ADDED A SECOND DISPLACEMENT FIELD FOR LBLOCK, LDUMP      *
*            BLOCK AND DUMP OUTPUT FORMATS.                           *
*        C.  CHANGED SPELLING OF MULTILINE TO LBLOCK.                 *
*        D.  CHANGED SPELLING OF LINEDUMP TO LDUMP.                   *
*        E.  DEFAULT OUTPUT FORMAT FOR LOAD MODULES IS LDUMP.         *
*        F.  ADDED MODULE AND OFFSET OPERANDS FOR LBLOCK AND LDUMP.   *
*                                                                     *
*        FROM JOHN SULLIVAN (LOMA LINDA MEDICAL CENTER):              *
*        A.  MODIFIED HISTORY TO LIST SPF STATISTICS (LIKE THE        *
*            ATTRIB SUBCOMMAND) FOR NON-LOAD MEMBERS.                 *
*        B.  ADDED THE CONTROL SUBCOMMAND.                            *
*        C.  ADDED THE LIKE KEYWORD ON THE RESTORE SUBCOMMAND.        *
*        D.  CHANGED RESTORE SCAN TO ALSO CHECK THE PROVIDED TTR      *
*            TO SEE IF IT IS A DELETED MEMBER.                        *
*                                                                     *
*        PROGRAM INPUT PROCESSING: DOUBLE BUFFERING HAS BEEN ADDED.   *
*                                                                     *
*        DELETE: REWRITTEN TO TAKE ADVANTAGE OF DOUBLE BUFFERING.     *
*                                                                     *
*        PDS DIALOG ENTRY: CODE FOR LOCATING THE TSO ECT HAS BEEN     *
*        ADDED AS PROVIDED BY MIKE LOOS WITH DELUXE CHECK PRINTERS.   *
*                                                                     *
*        TSO SERVICE ROUTINE ADDRESSES: SINCE SOME INSTALLATIONS      *
*        MAY MOVE ROUTINES LIKE IKJPARS OUT OF LPALIB, PDS HAS BEEN   *
*        MODIFIED TO TEST FOR AN ACTUAL ADDRESS IN THE CVT AND        *
*        TO LOAD AND DELETE ANY REQUIRED MODULES AS SUGGESTED BY      *
*        JERRY MORRIS WITH GTE DATA SERVICES IN TAMPA, FLORIDA.       *
*                                                                     *
*        FIXPDS: BLOCKSIZE VALUES FOR DATA KEYWORDS SUCH AS ASM,      *
*        CLIST, CNTL, LOAD OR OBJECT ARE BASED ON THE DEVICE TYPE.    *
*                                                                     *
*        TSO TIME SUBCOMMAND: IF TIME IS REQUESTED, PDS LINKS TO      *
*        IKJEFT25 INSTEAD OF TIME AS SUGGESTED BY MARK HUTCHINSON     *
*        WITH BURLINGTON INDUSTRIES IN GREENSBORO, NORTH CAROLINA.    *
*                                                                     *
*        REVIEW: SUBCOMMAND ADDED TO LINK TO THE REVIEW COMMAND       *
*        FROM THE CBT TAPE, FILE 300 (AIR FORCE TSO MODIFICATIONS).   *
*                                                                     *
*        EDIT, FSE, PRINTOFF, REVIEW, SUBMIT AND TSOLIST: PDS CHECKS  *
*        FOR VALID MEMBER NAMES BEFORE LINKING TO THE COMMAND.        *
*                                                                     *
*        IF: FIXED A MINOR PROBLEM WITH AUTHORIZED MODULE CHECKING;   *
*        ALSO, ADDED THE APFERR AND NOAPFERR KEYWORDS.                *
*                                                                     *
*        DISPLAY AND PATTERN: ADDED MEMBER GROUP SYNTAX AS AN         *
*        ALTERNATE WAY TO SPECIFY MEMBER NAMES.                       *
*                                                                     *
*        VERIFY: ADDED THE UPDATE AND NOUPDATE KEYWORDS.              *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TITLE 'P D S  --  PDS MACROS                         02/06/84'
         MACRO
&NAME    CLEAR &FIELD
         LCLC  &L
&L       SETC  'L'''
&NAME    MVI   &FIELD,C' '
         MVC   &FIELD+1(&L&FIELD-1),&FIELD
         MEND
         SPACE 1
         MACRO
&NAME    ENTER &TYPE
         LCLC  &LABEL
&LABEL   SETC  'IHB'.'&SYSNDX'
         AIF   ('&TYPE' EQ 'VALCHECK').VALCK
         AIF   ('&TYPE' EQ 'ATTNEXIT').ATTN
         AIF   ('&TYPE' EQ '').MAIN
         MNOTE 8,'INVALID TYPE ''&TYPE'''
         MEXIT
.MAIN    ANOP
&NAME    CSECT
         SAVE  (14,12),,*
         LCLA  &R
&R       SETA  0
.EQU     ANOP
R&R      EQU   &R
&R       SETA  &R+1
         AIF   (&R LT 16).EQU
         LR    R9,R15
         USING &NAME,R9,R10,R11,R12
         LA    R12,2048
         LA    R10,2048(R9,R12)
         LA    R11,2048(R10,R12)
         LA    R12,2048(R11,R12)
         MEXIT
.VALCK   ANOP
&NAME    CSECT
         SAVE  (14,12)
         LR    R8,R15             BASE REGISTER ADDRESS
         USING &NAME,R8           BASE REGISTER NOTIFICATION
         L     R7,4(,R1)          WORK AREA ADDRESS
         LM    R9,R12,BASES       RESTORE BASE REGISTERS
         L     R6,0(R1)           PDE ADDRESS
         LA    R15,VALSAVE
         AGO   .CHAIN
.ATTN    ANOP
&NAME    SAVE  (14,12)
         L     R7,8(,R1)
         LM    R9,R12,BASES
         LA    R15,ATTNSAVE
.CHAIN   ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         SPACE 1
         MEND
         SPACE 6
         MACRO
&L       PRSTBL &Z,&PF,&RTN,&O,&PCL,&P,&PP
&L DC 0F'0',CL8'&Z',X'&PF',AL3(&RTN),AL1(&O),AL3(&PCL),AL1(&P),AL3(&PP)
         MEND
         SPACE 2
         MACRO
&NAME    EXIT  &LV=
         AIF   ('&LV' EQ '').A0
&NAME    LR    R2,R13                         ADDR OF THIS SAVE AREA
         L     R13,4(,R13)
         AGO   .A1
.A0      ANOP
&NAME    L     R13,4(,R13)
.A1      STM   R15,R1,16(R13)                 RETURN REGS 15, 0, 1
         AIF   ('&LV' EQ '').A2 NOT DYNAMIC STORAGE
         FREEMAIN R,LV=&LV,A=(R2)
.A2      ANOP
         RETURN (14,12),T
         MEND
         SPACE 2
         MACRO
&NAME    MSG   &TEXT,&SHORT
         LCLA  &A
&A       SETA  K'&TEXT-2+4
         AIF   ('&SHORT' EQ 'S').SHORT
&NAME    DC    AL2(&A,0),C&TEXT              NORMAL MESSAGE
         MEXIT
.SHORT   ANOP
&NAME    DC    C'S',AL2(&A,0),C&TEXT         MESSAGE (NOT PARTITIONED)
         MEND
         SPACE 3
         MACRO $PUTGET -  OUTPUT A DATA LINE AND GET RESPONSE
&NAME   $PUTGET  &LINE,&ATTN=
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
&NAME    LA    R1,&LINE
         BAL   R14,$PUTGET
         AGO   .ATTN
.R       ANOP
&NAME    LR    R1,&LINE(1)
         BAL   R14,$PUTGET
         AGO   .ATTN
.CALL    ANOP
&NAME    BAL   R14,$PUTGET
.ATTN    ANOP
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                         EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                             IGNORE ATTENTIONS
.END     MEND
         SPACE 15
         MACRO $PUTLINE -  OUTPUT A DATA LINE
&NAME   $PUTLINE &LINE,&ATTN=
         AIF   ('&LINE' EQ '(1)' OR '&LINE' EQ '(R1)').CALL
         AIF   ('&LINE'(1,1) EQ '(').R
&NAME    LA    R1,&LINE                      ADDRESS OF DATA LINE
         BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
         AGO   .ATTN
.R       ANOP
&NAME    LR    R1,&LINE(1)                   ADDRESS OF DATA LINE
         BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
         AGO   .ATTN
.CALL    ANOP
&NAME    BAL   R14,$PUTLINE                  INVOKE PUTLINE INTERFACE
.ATTN    ANOP
         AIF   ('&ATTN' EQ '').NOATTN
         B     &ATTN                         EXIT IF ATTENTION OCCURRED
         AGO   .END
.NOATTN  NOP   0                             IGNORE ATTENTIONS
.END     MEND
         SPACE 2
         MACRO MESSAGE - OUTPUT AN ERROR/STATUS MESSAGE
&NAME    MESSAGE &MSG1,&MSG2
         AIF   ('&MSG1' EQ '(1)' OR '&MSG1' EQ '(R1)').MSGDS
         AIF   ('&MSG1'(1,1) EQ '(').RMSG1
         AIF   ('&MSG1' EQ 'MSGTEXT1' AND '&MSG2' EQ '').NOR1
&NAME    LA    R1,&MSG1
         AGO   .MSG2
.NOR1    ANOP
&NAME    BAL   R14,MESSTXT1
         MEXIT
.RMSG1   ANOP
&NAME    LR    R1,&MSG1(1)
         AGO   .MSG2
.MSGDS   ANOP
         AIF   ('&MSG2' NE '').SECOND
&NAME    BAL   R14,MESSAGE0
         MEXIT
.SECOND  ANOP
&NAME    DS    0H
.MSG2    ANOP
         AIF   ('&MSG2' EQ '').NOMSG2 NO SECONDARY MESSAGE
         AIF   ('&MSG2' EQ '(0)' OR '&MSG2' EQ '(R0)').CALL
         AIF   ('&MSG2'(1,1) EQ '(').RMSG2
         LA    R0,&MSG2
         AGO   .CALL
.RMSG2   ANOP
         LR    R0,&MSG2(1)
.CALL    ANOP
         BAL   R14,MESSAGE
         AGO   .END
.NOMSG2  ANOP
         BAL   R14,MESSAGE0
.END     MEND
         TITLE 'P D S  --  PDS PROGRAM GENERATION OPTIONS     02/06/84'
*
*        SET PROGRAM GENERATION OPTIONS
*
         GBLC  &SPFGEN
&SPFGEN  SETC  'SPF'     ***  ASSUME THE SPF INTERFACE IS DESIRED
         AIF   ('&SYSPARM' EQ '').MNOTE
         AIF   ('&SYSPARM' EQ 'SPF').MNOTE
         AIF   ('&SYSPARM' EQ 'ISPF').ISPF
         AIF   ('&SYSPARM' EQ 'NOSPF').NOSPF
         SPACE 3
         MNOTE 12,'*** SYSPARM(&SYSPARM.) IS INVALID ***'
         SPACE 2
         AGO   .NOGEN
.NOSPF   ANOP
&SPFGEN  SETC  'NO SPF'
         AGO   .MNOTE
.ISPF    ANOP
&SPFGEN  SETC  'ISPF'
.MNOTE   ANOP
         SPACE 3
         MNOTE *,'&SPFGEN INTERFACE CODE WILL BE GENERATED'
         SPACE 2
.NOERR   ANOP
         SPACE 3
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
***                                                                 ***
***    HISTORICAL SECTION:                                          ***
***                                                                 ***
***                                                                 ***
***     VERSION   LAST MOD   SIZE   IN K   # SUBS   SOURCE   HELP   ***
***     =======   ========   ====   ====   ======   ======   ====   ***
***                                                                 ***
***      PDS30    1/28/80    47A0    18K     19      4851     512   ***
***                                                                 ***
***      PDS45    8/20/81    45A8    18K     22      5037     517   ***
***                                                                 ***
***      PDS52   11/08/82    6090    25K     25      6253    1123   ***
***                                                                 ***
***      PDS53    3/22/83    9610    38K     28      8978    1502   ***
***                                                                 ***
***      PDS60    9/30/83    CF28    52K     32     12068    1997   ***
***                                                                 ***
***      PDS61    2/06/84    E940    59K     34     13490    2270   ***
***                                                                 ***
***                                                                 ***
*** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** *** ***
         TITLE 'P D S  --  PDS PROGRAM ENTRY                  02/06/84'
PDS61    ENTER
         SPACE
         LR    R6,R1                SAVE ADDR OF CPPL
         USING WORKAREA,R7          WORK AREA DSECT
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF050
         L     R1,0(,R1)            POINT TO PASSED PARAMETER LIST
         CLC   2(2,R1),=C'++'       SPF ADDRESS MARKER FROM PDS?
         BNE   GETMAIN              NO, NOT AN ENTRY FROM SPF
         CVB   R7,4(,R1)            WORK AREA POINTER
         L     R0,PDSENTRY          NUMBER OF ENTRIES
         A     R0,=F'1'
         ST    R0,PDSENTRY
         C     R0,=F'1'             FIRST SPF ENTRY?
         BH    RETURN2              NO, END STATEMENT
         LA    R15,SPFMAIN          SPF REGISTER SAVE AREA
         ST    R15,8(,R13)
         ST    R13,4(,R15)          CHAIN TO SECONDARY SAVEAREA
         LR    R13,R15
         MVI   ISPFTEXT+1,12        LENGTH OF COUNT AND "ISPF    "
         B     RESTART              REALLOCATE THE DATA FILES
.NSPF050 ANOP
         TITLE 'P D S  --  PDS WORK AREA INITIALIZATION       02/06/84'
GETMAIN  L     R5,WORKSIZE
         LA    R0,0(,R5)                CLEAR THE TOP BYTE
         GETMAIN RU,LV=(0),SP=1,BNDRY=PAGE
         LR    R7,R1                    WORK AREA ADDRESS
         LR    R4,R1                    CLEAR WORK AREA
         SR    R1,R1                    NULL SOURCE
         MVCL  R4,R0                    FILL WITH PAD 0 FROM R1(0:7)
         SPACE 1
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF100
         L     R1,0(,R6)                POINT TO PASSED PARAMETER LIST
         CLI   2(R1),X'40'              INVOKED AS A SPF DIALOG?
         BNH   INITADDR                 NO, BRANCH
*
***   PDS HAS BEEN INVOKED AS A SPF DIALOG  -- TO ALLOW THE USE OF
*     NORMAL TSO SERVICES, A CPPL MUST BE BUILT.
*     A.  GETMAIN AN AREA FOR CREATED CONTROL BLOCKS IN SUBPOOL 78
*         (THE SUBPOOL TSO USES FOR ECT I/O SERVICE AND WORK AREAS)
*     B.  COPY IN THE SYSTEM UPT AND PSCB ADDRESSES
*     C.  CREATE A TSO COMMAND BUFFER FROM THE INPUT PARAMETERS
*     D.  CREATE AN ECT AREA
*         1.  THE ECT POINTS TO AN I/O SERVICE AREA
*         2.  THE I/O SERVICE AREA POINTS TO AN I/O WORK AREA WHICH
*             IS INITIALLY 32 BYTES LONG.
*     E.  LOCATE THE ISPLINK SERVICE ROUTINE
*     F.  A METHOD OF LOCATING THE ACTUAL TSO ECT HAS BEEN DEVELOPED.
*         IN A FOLLOWING RELEASE OF PDS, THE CREATED ECT AREA WILL
*         BE DELETED; FOR NOW, THE LOCATED ECT IS ACTUALLY USED.
*     G.  AT PROGRAM END, CLEAN UP THE OBTAINED AREA
*         1.  IF THE I/O WORK AREA WAS FREED AND EXPANDED, RELEASE
*             THE NEW I/O WORK AREA.
*         2.  RELEASE THE OBTAINED SUBPOOL 78 CONTROL BLOCK AREA.
         SPACE 1
         OI    FLAGSFF,FSPFDIAL+FSPFOPT6  SET APPROPRIATE SPF FLAGS
         MVI   SUBPDIAL,78              GETMAIN SUBPOOL
         LA    R0,16+96+64+16+32        GETMAIN AREA SIZE
         ICM   R0,B'1000',SUBPDIAL      GETMAIN SUBPOOL
         GETMAIN R,LV=(0)
         XC    0(16+96+64+16+32,R1),0(R1)
         STCM  R1,B'0111',SUBPDIAL+1    CREATED CPPL START
         LA    R2,16(,R1)               CREATED CBUF START
         LA    R3,96(,R2)               CREATED ECT START
         LA    R4,64(,R3)               CREATED I/O SERVICE AREA
         LA    R5,16(,R4)               CREATED I/O WORK AREA
         ST    R2,CPPLCBUF-CPPL(,R1)    START OF CBUF
         ST    R3,CPPLECT-CPPL(,R1)     START OF ECT
         MVI   0(R5),X'80'              MARK END OF THE LIST
         ST    R5,0(,R4)                FIRST I/O WORK ELEMENT
         ST    R5,4(,R4)                LAST USED I/O WORK ELEMENT
         MVC   8(2,R4),=H'32'           INITIAL I/O WORK AREA LENGTH
         L     R5,16                    --> CVT
         L     R5,0(,R5)                --> TCB WORDS
         L     R5,4(,R5)                --> TCB
         L     R5,180(,R5)              --> JSCB
         L     R5,264(,R5)              --> PSCB
         ST    R5,CPPLPSCB-CPPL(,R1)
         L     R5,52(,R5)               --> UPT
         ST    R5,CPPLUPT-CPPL(,R1)
         MVC   ECTPCMD-ECT(8,R3),BLANKS CLEAR THE PRIMARY COMMAND
         MVC   ECTSCMD-ECT(8,R3),BLANKS CLEAR THE SECONDARY COMMAND
         ST    R4,ECTIOWA-ECT(,R3)      START OF THE I/O SERVICE AREA
         L     R15,0(,R6)               START OF THE PARM AREA
         LR    R6,R1                    POINT TO THE CREATED CPPL
         LH    R5,0(,R15)               PARM LENGTH
         LA    R1,4(,R5)
         STH   R1,0(,R2)                CBUF STRING LENGTH
         BCTR  R5,0                     MACHINE STRING LENGTH
         MVC   4(*-*,R2),2(R15)         <<EXECUTED>>
         EX    R5,*-6                   MOVE IN THE PARM STRING
         LA    R15,4(,R2)               PREPARE FOR STRING SCAN
         LR    R14,R15
         SPACE 1
         LA    R15,1(,R15)              FIND
         CLI   0(R15),X'40'                 FIRST
         BNE   *-8                               NON-BLANK
         SPACE 1
         LR    R5,R15
         SR    R5,R14                   LENGTH OF PDS PROGRAM NAME
         BCTR  R5,0                     MACHINE LENGTH
         MVC   ECTPCMD-ECT(*-*,R3),4(R2)  <<EXECUTED>>
         EX    R5,*-6                   MOVE IN THE PDS PROGRAM NAME
         SPACE 1
         LA    R15,1(,R15)              FIND
         CLI   0(R15),X'40'                 NEXT
         BE    *-8                              BLANK
         SPACE 1
         SR    R15,R14
         STH   R15,2(,R2)               OFFSET TO OPERANDS
         MVI   ECTSWS2-ECT(R3),ECTDEFCS DEFAULT DELETE CHARACTERS USED
         LOAD  EP=ISPLINK               ADDRESS OF SPF INTERFACE MODULE
         ST    R0,ISPLINK               SAVE ADDRESS FOR LATER
         BAL   R2,CLEAR1                TURN OFF FULL-SCREEN MODE
         SPACE 3
         NOP   INITADDR                   ZAP FOR TESTING
         L     R2,16                   ** CHECK FOR TSO/E RELEASE 2
         ICM   R2,B'1111',X'9C'(R2)       --> LOGON WORK AREA?
         BZ    FECTSTD                        NO, BRANCH
         ICM   R2,B'1111',X'20'(R2)       --> ECT?
         BNZ   FECTUPD                        YES, BRANCH
         SPACE 1
FECTSTD  L     R2,X'21C'               ** PRE-TSO/E RELEASE 2 SYSTEM
         L     R2,X'7C'(,R2)              --> FIRST JSTCB
         L     R2,X'50'(,R2)              --> IKJEFT01 REGISTER 8
         L     R2,X'2C'(,R2)              --> ECT
         SPACE 1
FECTUPD  L     R1,SUBPDIAL             ** UPDATE THE ECT ADDRESS
         ST    R2,CPPLECT-CPPL(,R1)       NEW ECT POINTER
         MVC   ECTPCMD-ECT(8+8,R2),ECTPCMD-ECT(R3)
.NSPF100 ANOP
         SPACE 3
INITADDR LM    R2,R4,CPPLUPT-CPPL(R6)   INITIALIZE ADDRUPT,
         L     R5,CPPLCBUF-CPPL(R6)       ADDRPSCB, ADDRECT,
         STM   R2,R6,ADDRUPT              ADDRCBUF AND ADDRCPPL
         MVC   PDSNAME,ECTPCMD-ECT(R4)  SAVE THE PDS COMMAND NAME
         TM    ECTSWS-ECT(R4),ECTBKGRD  BACKGROUND MODE?
         BNO   *+8                      NO, BRANCH
         OI    FLAGSEE,FBKGRND          YES, SET A FLAG FOR LATER
         SPACE 1
         STM   R9,R12,BASES             SAVE BASE REGISTERS
         MVC   BLDLLIST,BLDLPARM        BLDL NUMBER OF MEMBERS & LENGTH
         ST    R7,8(,R13)               CHAIN
         ST    R13,4(,R7)                    SAVE
         LR    R13,R7                            AREAS
         SPACE 1
         LA    R0,256*2                 ALTERNATE GETMAIN AREA
         GETMAIN RU,LV=(0),SP=1,BNDRY=PAGE
         ST    R1,AREA2
         ST    R1,IODIR1                INITIALIZE FIRST DIR IOAREA
         LA    R1,256(,R1)
         ST    R1,IODIR2                INITIALIZE SECOND DIR IOAREA
         SPACE 1
         GTSIZE ,                       TERMINAL PAGESIZE/LINESIZE
         STM   R0,R1,PAGESIZE           LINES/SCREEN
*        ST    R1,LINESIZE              CHARACTERS/LINE
         SPACE 1
         MVC   STAEPARM(4),STAELIST     SET A ESTAE EXIT
         ESTAE STAEEXIT,CT,PARAM=(R7),MF=(E,STAEPARM)
         TITLE 'P D S  --  PDS SERVICE ROUTINE ADDRESSES      02/06/84'
*
*    LOCATE THE TSO SERVICE ROUTINES AND THE DISK ADDRESS CONVERTERS
*
         LA    R2,ADDRPUTL              FIRST SERVICE ROUTINE ADDRESS
         LA    R3,MODULEN               FIRST LOAD ADDRESS
         SPACE 1
SERVLOOP L     R1,CVTPTR                CVT ADDRESS
         AH    R1,0(,R3)                OFFSET IN THE CVT
         ICM   R0,B'1111',0(R1)         ANY PRELOADED ADDRESS?
         BNZ   SERVCVTA                 YES, BRANCH
         XC    PARMLIST(12),PARMLIST    CLEAR THE PARMLIST AREA
         LOAD  EPLOC=2(R3),SF=(E,PARMLIST)
         MVI   0(R2),X'D6'              MARK TO DELETE LATER
         SPACE 1
SERVCVTA STCM  R0,B'0111',1(R2)         UPDATE THE ADDRESS
         LA    R2,4(,R2)                NEXT SERVICE ADDRESS
         LA    R3,10(,R3)               NEXT MODULES ADDRESS
         CLI   0(R3),X'FF'              END OF MODULES?
         BNE   SERVLOOP                 NO, BRANCH
         SPACE 2
         L     R2,ADDRPSCB              ADDRESS OF THE PSCB
         MVC   PARMLIST(4),ENQSTEP      SET OPTION BITS FOR ENQ
         ENQ   (SPFUSER,PSCBUSER-PSCB(R2),E,7,STEP),RET=TEST,          X
               MF=(E,PARMLIST)          CHECK FOR SPF ENQ
         SPACE 1
         LTR   R15,R15                  SPF ALREADY INITIALIZED?
         BZ    PRIM00                   NO, BRANCH
         XI    FLAGSFF,FSPFOPT6         YES, INVERT THE OPTION 6 FLAG
         TITLE 'P D S  --  PDS PRIMARY SUBCOMMAND CHECK       02/06/84'
*
*     GET THE DATA SET NAME AND ANY SUBCOMMAND FROM THE COMMAND LINE
*
         SPACE 1
PRIM00   MVC   MSGTEXT1,MSGPDS61   PROGRAM IDENTIFIER MESSAGE
         MVC   MSGTEXT1+4(8),PDSNAME
         MESSAGE MSGTEXT1
*        MESSAGE MSGBLANK          ONE BLANK LINE
         L     R2,ADDRCBUF         COMMAND BUFFER
         LH    R3,0(,R2)           TOTAL COMMAND LENGTH
         LR    R1,R3               TOTAL COMMAND LENGTH
         BCTR  R1,0                MACHINE LENGTH
         MVC   RECALLST(*-*),0(R2) <<EXECUTED>>
         EX    R1,*-6              SAVE PREVIOUS SUBCOMMAND
         XC    RECALLST+2(2),RECALLST+2  CLEAR THE SUBCOMMAND OFFSET
         SPACE 1
         LH    R4,2(,R2)           OFFSET TO OPERANDS
         LA    R4,4(,R4)           PLUS COUNT BYTES
         LA    R5,PRIMOPTN-2       PDS VALID KEYWORD TABLE START -2
         LA    R2,0(R2,R4)         START OF OPERANDS
         SR    R3,R4               BYTES LEFT TO SCAN - ANY?
         BNP   PRIM90              NO, BRANCH
         SPACE 1
PRIM10   OI    0(R2),X'40'         UPPER-CASE THE OPERANDS
         CLI   0(R2),X'40'         BLANK?
         BE    PRIM20              YES, BRANCH
         LA    R2,1(,R2)           NO, TRY THE NEXT ONE
         BCT   R3,PRIM10
         B     PRIM90              NOT FOUND, BRANCH
         SPACE 1
PRIM20   OI    0(R2),X'40'         UPPER-CASE THE OPERANDS
         CLI   0(R2),X'40'         BLANK?
         BNE   PRIM30              NO, BRANCH
         LA    R2,1(,R2)           YES, TRY THE NEXT ONE
         BCT   R3,PRIM20
         B     PRIM90              NOT FOUND, BRANCH
         SPACE 1
PRIM30   LA    R5,2(,R5)           NEXT VALID KEYWORD ENTRY
         CLI   0(R5),X'FF'         END OF KEYWORD TABLE?
         BE    PRIM40              YES, FOUND A SUBCOMMAND
         CLC   0(1,R2),0(R5)       FIRST CHARACTER MATCH?
         BNE   PRIM30              NO, CONTINUE LOOPING
         SPACE 1
         C     R3,=F'1'            ANOTHER CHARACTER?
         BNH   PRIM90              NO, NO SUBCOMMAND ADDED
         OI    1(R2),X'40'         UPPER-CASE THE NEXT CHARACTER
         CLC   1(1,R2),1(R5)       MATCH SECOND CHARACTER TOO?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         CLI   1(R2),X'40'         SECOND CHARACTER A BLANK?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         CLI   1(R2),C'('          PARENTHESIS FOLLOWING V (FOR VOL)?
         BE    PRIM10              YES, --NOT A SUBCOMMAND--
         EJECT
PRIM40   LA    R0,256              COUNT OF SUBCOMMAND CHARACTERS
         ICM   R0,B'1000',=X'01'   SUBPOOL 1
         GETMAIN R,LV=(0)
         MVI   0(R1),X'40'         BLANK THE
         MVC   1(255,R1),0(R1)              SUBCOMMAND STORAGE
         XC    0(4,R1),0(R1)       CLEAR THE FIRST WORD
         MVI   0(R1),1             LENGTH=1*256
         ST    R1,SUBCMNDP         SAVE THE STORAGE POINTER
         SPACE 1
PRIM42   OC    4(1,R1),0(R2)       MOVE AND UPPER-CASE THE CHARACTER
         MVI   0(R2),X'40'         BLANK THE COMMAND LINE CHARACTER
         LA    R1,1(,R1)
         LA    R2,1(,R2)
         BCT   R3,PRIM42
         SPACE 3
PRIM90   MVC   ##SUBCOM(20),MAINPCL  PCL FOR DATA SET NAME
         L     R15,=A(PARSE)       PARSE ADDRESS
         BALR  R14,R15             CALL PARSE
         B     EXIT12N             ERROR EXIT
         SPACE 2
         ICM   R1,B'1111',SUBCMNDP    ADDRESS OF SUBCOMMAND?
         ST    R1,ADDRCBUF
         BZ    PRIM96                 NO - ZERO, BRANCH
         OI    FLAGSBB,FCMD+FONESHOT  YES, INDICATE ONE SUBCOMMAND ONLY
         B     RESTART                     AND SKIP THE PROGRAM MESSAGE
         SPACE 1
PRIM96   LA    R1,DSN              ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD         EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE   FLAG TO CONTINUE
         TITLE 'P D S  --  PDS DATA SET INITIALIZATION        02/06/84'
RESTART  DS    0H                  ** ALLOCATE OR REALLOCATE DATA SET
         SPACE 1
         MVC   DSPALLOC,DSPREQST   SET INITIAL DISPOSITION SHR/OLD
         SPACE 2
         L     R15,=A(ALLOCATE)    ALLOCATION ROUTINE ADDRESS
         XC    RECOVER,RECOVER     CANNOT RECOVER - NOT INITIALIZED YET
         BALR  R14,R15             ALLOCATE THE DATA SET
         B     EXIT12N             ALLOCATION UNSUCCESSFUL
         SPACE 1
         MVC   INDCB(LEXCPDCB),EXCPDCB MOVE PATTERN DCB
         MVC   STOWDCB(LSAMDCB),SAMDCB ALSO FOR STOW OPERATIONS
         MVC   DCBDDNAM-IHADCB+INDCB,DDNAME
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVC   DCBDSORG-IHADCB+STOWDCB(1),DSORG
         TM    DSORG,DS1DSGPO                    DSORG=PO?
         BO    *+8                               YES, BRANCH
         MVI   DCBDSORG-IHADCB+STOWDCB,DS1DSGPS  NO, USE DSORG=PS
         SPACE 2
RESTART4 MVI   OPENLIST,X'00'      ** REOPEN THE DATA SET          **
         MVI   OPENLIST+4,X'80'
         XC    RECOVER,RECOVER     CANNOT RECOVER - NOT INITIALIZED YET
         OPEN  (STOWDCB,INPUT,INDCB,INPUT),MF=(E,OPENLIST)
         SPACE
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)       OPENED FOR DCB EXIT ONLY
         SPACE
         TM    DCBOFLGS-IHADCB+INDCB,DCBOFOPN  DCB OPEN?
         BNO   EXIT12O                         NO, QUIT
         SPACE 2
         NI    FLAGSCC,FF-FBLDLOK              BLDL IS INVALID NOW
         EJECT
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         MVC   NUMEXT+1(1),DEBNMEXT(R1)        NUMBER OF D.A. EXTENTS
         L     R4,DEBUCBAD(,R1)                ADDRESS OF FIRST UCB
         ST    R4,UCBADDR                      ADDRESS OF FIRST UCB
         MVC   VOLALLOC,UCBVOLI(R4)            SAVE VOLUME NAME USED
         MVC   BYTEUCB+1(1),UCBTYP+3(R4)       SAVE THE UCB DEVICE TYPE
         L     R14,OBTAIN                      OBTAIN FLAGS
         LA    R15,DSNAME                      ADDRESS OF DSNAME
         LA    R0,VOLALLOC                     VOLUME SERIAL
         LA    R1,DS1FMTID                     OUTPUT START ADDRESS
         STM   R14,R1,PARMLIST                 SAVE CAMLIST PARAMETERS
         OBTAIN PARMLIST                       GET THE FORMAT 1 DSCB
         LA    R1,MSGNDSCB                     NO DSCB FOUND MESSAGE
         LTR   R15,R15                         SUCCESSFUL?
         BNZ   EXIT12M                         NO, QUIT
         SPACE 2
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         ICM   R0,B'0111',DS1LSTAR             DS1LSTAR --> MBBCCHHR
         SLL   R0,8                            MOVE TO HIGH POSTION
         L     R1,DCBDEBAD-IHADCB+INDCB        DEB ADDRESS
         LA    R2,LSTARMBB                     RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BALR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         SPACE 2
         LA    R0,IOECB                        ECB ADDRESS
         ST    R0,IOBECB                       SET INTO IOB
         LA    R0,INDCB                        DCB ADDRESSS
         ST    R0,IOBDCB                       SET INTO DCB
         MVI   IOB,X'C2'                       SET IOB FLAGS
         SPACE 1
         MVC   CCWS(LCCWINIT),CCWINIT          INITIALIZE CCWS
         SPACE 1
         LA    R0,IOBSEEK+3
         LA    R1,CCW1A
         STCM  R0,B'0111',CCW1A+1              ***
         STCM  R1,B'0111',CCW2A+1              *** -- INIT. CCW'S
         STCM  R0,B'0111',CCW3A+1              ***
         SPACE 1
         LA    R1,SNO
         LA    R2,CCW1
         L     R3,BUFFADDR
         STCM  R1,B'0111',CCW0+1               ***
         STCM  R2,B'0111',CCW2+1               ***
         STCM  R3,B'0111',CCW3+1               ***  -- READ CCW'S
         STCM  R1,B'0111',CCW5+1               ***
         ST    R3,IOBUFF1                      INPUT BUFFER 1
         AH    R3,BUFFSIZE
         ST    R3,IOBUFF2                      INPUT BUFFER 2
         SPACE 8
         LA    R1,CCW0                         FIRST CCW (SET SECTOR)
         ST    R1,EXCPCCWI                     SAVE FOR EXCP ROUTINE
         CLI   UCBTYP+3(R4),X'08'              2314 DISK?
         BNE   NOT2314                         NO, BRANCH
         ST    R2,EXCPCCWI                     YES, DO NOT READ SECTOR
         XI    CCW4+4,X'40'                    DO NOT CHAIN CCW4
         SPACE 1
NOT2314  MVI   DSORG+1,0                       CLEAR SECOND DSORG BYTE
         TM    DSORG,DS1DSGIS+DS1DSGPS+DS1DSGDA+DS1DSGPO  DSORG KNOWN?
         BM    *+10                                       YES, BRANCH
         MVC   DSORG(2),DS1DSORG                          NO, USE LABEL
         SPACE 1
         MVC   MSGENTDS,MSGENTER               " ENTER OPTIONS -- DSN=.
         LA    R1,MSGENTDS+25                  WHERE TO ADD DSNAME
         MVC   0(44,R1),DSNAME                 ADD DSNAME
         AH    R1,DSNLEN                       POINT TO DSNAME END
         MVC   0(9,R1),VOLSERTX+1              ADD ",VOL=SER="
         MVC   9(6,R1),VOLALLOC                ADD VOLUME SERIAL
         SPACE 1
         MVC   LABLRECL(2),LRECL               SAVE THE DCB LRECL FIELD
         TM    FLAGSCC,RECFMF                  FIXED FORMAT?
         BNO   NEWSTAX                         NO, BRANCH
         TM    RECFM,DCBRECBR                  RECFM=FB?
         BO    NEWSTAX                         YES, BRANCH
         MVC   LRECL(2),BLKSI                  RECFM=F ONLY
         SPACE 1
NEWSTAX  LA    R0,NEWCMD              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),PDSNAME   RESET PDS COMMAND NAME
         SPACE 1
         STAX  ATTNEXIT,USADDR=(R7),REPLACE=YES,IBUF=0,OBUF=0,         X
               MF=(E,STAXPARM)
         SPACE 1
         ICM   R8,B'1111',##ADRCMD    SUBCOMMAND TO BE CONTINUED?
         BNM   NEWCMD                 NO, BRANCH
         NI    ##ADRCM#,FF-CONTINUE           TURN OFF CALL FLAG
         BALR  R2,R8                  YES, BRANCH
         TITLE 'P D S  --  PDS NEW SUBCOMMAND PROCESSING      02/06/84'
*
*  GET THE NEXT SUBCOMMAND
*
         SPACE 1
NEWCMD   SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT     ANY TEMPORARY STORAGE?
         MVI   SUBPOOLT,0              CLEAR THE SUBPOOL CHARACTER
         BZ    NEWCMD10                NO, LINK TO THE SUBCOMMAND
         FREEMAIN R,SP=(0)             YES, RELEASE IT
         SPACE 1
NEWCMD10 TM    FLAGSAA,FMEM#MEM        MEMBER GROUP IN PROGRESS?
         BO    MEMSGET                 YES, BRANCH
         SPACE 2
         MVI   FLAGSAA,0               CLEAR THE MAJOR CONTROL FLAGS
         TM    FLAGSII,FIOPEND         ANY I/O PENDING?
         BNO   NEWCMD20                NO, BRANCH
         LH    R1,IODODIS1             ONE MORE
         LA    R1,1(,R1)                       DISCARDED
         STH   R1,IODODIS1                              INPUT RECORD
         WAIT  ECB=IOECB               WAIT FOR INPUT COMPLETION
         MVI   IOECB,0                 CLEAR THE I/O FLAG
         SPACE 1
NEWCMD20 LH    R1,IODODIS1
         LA    R1,1(,R1)
         TM    FLAGSII,FDIRNEXT        DIRECTORY READ READY?
         BNO   *+8                     NO, BRANCH
         STH   R1,IODODIS1             ONE MORE DISCARDED INPUT RECORD
         SPACE 1
         MVI   FLAGSII,0               CLEAR THE I/O FLAGS
         MVI   STARTTR+3,0             CLEAR THE READ ADDRESS
         BAL   R2,SHUTSTOW             CLOSE STOW; RELEASE ANY ENQUEUES
         TM    FLAGSBB,FCMD            COMMAND BUFFER AVAILABLE?
         BO    HAVECMD                 YES, BRANCH
         SPACE 1
         TM    FLAGSBB,FONESHOT        ONE SUBCOMMAND ONLY?
         BO    RETURN                  YES, ALL DONE
         SPACE 1
         TM    FLAGSEE,FBKGRND         BACKGROUND MODE?
         BNO   NEWCMD30                NO, BRANCH
         MESSAGE MSGBLANK              YES, OUTPUT A SEPARATOR LINE
         SPACE 8
NEWCMD30 MVI   ATTNECB,2+1             FLAG -- CAN BE ATTENTIONED
        $PUTGET MSGENTDS,ATTN=NEWCMD40 FIRST TRY FOR A COMMAND
         B     HAVECMD                 GOT A COMMAND
         SPACE 2
NEWCMD40 MVI   ATTNECB,2+1             FLAG -- CAN BE ATTENTIONED
        $PUTGET MSGENTDS,ATTN=EXIT8N   SECOND TRY FOR A COMMAND
         TITLE 'P D S  --  PDS SUBCOMMAND INVOKE SCAN         02/06/84'
*
*        SET UP FOR SUBCOMMAND SCAN
*
         SPACE 1
HAVECMD  NI    FLAGSBB,FF-FCMD
         SPACE 2
         LA    R15,PARMLIST
         USING CSPL,R15
         SPACE
         L     R1,ADDRUPT           ADDRESS OF THE UPT
         L     R2,ADDRECT           ADDRESS OF THE ECT
         LA    R3,ATTNECB           ADDRESS OF THE ATTENTION ECB
         LA    R4,ZERO              ADDRESS OF A FLAG AREA (WITH ZEROS)
         LA    R5,SCANANSR          ADDRESS OF THE RESULT AREA
         L     R6,ADDRCBUF          ADDRESS OF THE SUBCOMMAND BUFFER
         MVC   MSGTEXT1(256),RECALLST  SAVE THE LAST SUBCOMMAND
         CLC   0(2,R6),=H'4'           NULL INPUT BUFFER?
         BE    *+10                    YES, IGNORE THIS SUBCOMMAND
         MVC   RECALLST(256),0(R6)     NO, RESET THE RECALL SUBCOMMAND
         STM   R1,R6,CSPLUPT        INITIALIZE CSPLUPT, CSPLECT,
         DROP  R15                  CSPLECB, CSPLFLG, CSPLOA & CSPLCBUF
         SPACE 2
         MVI   ATTNECB,0            CLEAR ECB
         LR    R1,R15               PARAMETER LIST START
         L     R15,ADDRSCAN
         BALR  R14,R15              INVOKE COMMAND SCAN
         SPACE
         LA    R15,SCANANSR         ADDR OF ANSWER AREA
         USING CSOA,R15
         SPACE
         TM    CSOAFLG,CSOANOC      EMPTY BUFFER?
         BO    NEWCMD               YES, STILL NEED COMMAND
         SPACE
         TM    CSOAFLG,CSOAEXEC     IMPLICIT EXEC?
         BZ    HAVECMD1             NO, BRANCH
         L     R2,ADDRCBUF          YES, IMPLICIT FORM OF EXEC
         XC    2(2,R2),2(R2)        CLEAR OFFSET
         B     EXEC                 CONTINUE WITH EXEC SUBCOMMAND
         SPACE 2
HAVECMD1 TM    CSOAFLG,CSOAVWP+CSOAVNP VALID COMMAND?
         LA    R1,MSGINVLD             ASSUME NOT
         BZ    MSGNEW                  NO, BRANCH
         SPACE 2
         TM    CSOAFLG,CSOAVWP         ANY OPERAND?
         BNO   *+8                     NO, BRANCH
         OI    FLAGSAA,FOPTIONS        YES, NOTE FOR LATER
         TITLE 'P D S  --  PDS SUBCOMMAND TABLE SCAN          02/06/84'
*
*   COMMAND SCAN - IN THE CASE OF A POTENTIALLY AMBIGUOUS ENTRY
*                  (E.G. 'A ') THE FIRST MATCH IN THE COMMAND TABLE
*                  (ATTR IN THIS CASE) IS USED.
*
         L     R2,CSOACNM              ADDR OF COMMAND NAME
         LH    R3,CSOALNM              LENGTH OF NAME
         BCTR  R3,0                    MACHINE LENGTH
         DROP  R15
         SPACE 2
         LA    R1,CMDTABLE             START OF COMMAND SCAN
         SPACE 2
         CLC   0(*-*,R1),0(R2)         <<EXECUTED>>
CMDSCAN  EX    R3,*-6                  THIS SUBCOMMAND?
         BNE   CMDSCAN2                NO, BRANCH
         SPACE
         MVC   ##SUBCOM(20),0(R1)      INITIALIZE ##SUBCOM, ##ADRCMD,
*                                        ##ADRPCL AND ##ADRPAR
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BO    CMDFND                  YES, BRANCH
         TM    ##ADRPC#,@S             SUPPORTED IF NOT PARTITIONED?
         BO    CMDFND2                 YES, BRANCH
         SPACE
CMDSCAN2 LA    R1,20(R1)               NEXT ENTRY IN TABLE
         CLI   0(R1),X'FF'             END OF TABLE?
         BNE   CMDSCAN                 NO, CONTINUE
         SPACE
         SR    R2,R2
         L     R8,OPTLIT+8             OPTIONS SUBCOMMAND START
         BR    R8
         TITLE 'P D S  --  PDS SUBCOMMAND INITIAL PROCESSING  02/06/84'
CMDFND   NI    ##ADRPC#,FF-@S          PARTITIONED, TURN OFF OTHER FLAG
         SPACE 2
CMDFND2  MVC   ##ANSWER(LISUBS),ISUBS  INITIALIZE THE PDL SAVE AREA
         CLC   0(8,R1),RECALLIT        RECALL SUBCOMMAND?
         BNE   CMDCHECK                NO, BRANCH
         MVC   RECALLST(256),MSGTEXT1  YES, USE THE OLD SUBCOMMAND
         MESSAGE MSGTEXT1              DISPLAY THE OLD SUBCOMMAND
         LH    R1,2(,R6)               OFFSET TO OPERANDS
         LA    R1,4(R1,R6)             POINT TO OPERANDS
         TM    FLAGSAA,FOPTIONS        ANY OPERANDS?
         BNO   NEWCMD                  NO, BRANCH
         OI    0(R1),X'40'             UPPER CASE THE OPERAND
         CLI   0(R1),C'E'              ENTER OPERAND?
         BNE   NEWCMD                  NO, BRANCH
         NI    FLAGSAA,FF-FOPTIONS     TURN OFF THE OPERAND FLAG
         MVC   PUTGETBF(256),RECALLST  CHANGE TO THE OLD SUBCOMMAND
         B     HAVECMD                 SCAN AND PARSE THIS AGAIN
         SPACE 2
CMDCHECK TM    ##ADRPC#,@N       IGNORE OPTIONS?
         BO    CALLCMD           YES, BRANCH
         SPACE 2
         CLC   ##SUBCOM,PATTLIT  PATTERN SUBCOMMAND?
         BNE   CMDCHK2           NO, BRANCH
         TM    FLAGSAA,FOPTIONS  ANY OPERAND SPECIFIED?
         BO    CMDPARSE          YES, USE THE SPECIFIED DATA
         CLI   MEMBERP,X'00'     ANY DEFAULT PATTERN YET?
         BE    CMDPARSE          NO, FORCE A CALL TO PARSE
         SPACE 1
         MVC   LMEMBER1(2+8+8+2),MEMBERP+1  RESET DEFAULT PATTERN
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERP   RESTORE THE DEFAULT FLAGS
         B     CALLCMD
         SPACE 2
CMDCHK2  TM    ##ADRPC#,@G       MEMBER NAME DEFAULT (AND GROUP)?
         BNO   CMDREQD           NO, CHECK IF OPERANDS REQUIRED
         TM    FLAGSAA,FOPTIONS  ANY OPERAND SPECIFIED?
         BO    CMDPARSE          YES, USE THE SPECIFIED DATA
         SPACE 2
         TM    ##ADRPC#,@S       NON-PARTITIONED DATA SET?
         BO    CALLCMD9          YES, DON'T NEED A MEMBER NAME
         CLI   MEMBERD,X'00'     ANY DEFAULT MEMBER NAME YET?
         BE    CMDPARSE          NO, FORCE A CALL TO PARSE
         SPACE 1
         MVC   LMEMBER1(2+8+8+2),MEMBERD+1  RESET DEFAULT MEMBER NAMES
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERD   RESTORE THE DEFAULT FLAGS
         TM    FLAGSAA,FMEM#MEM  MEMBER GROUP REQUESTED?
         BO    CALLCMD           YES, BRANCH
         SPACE 1
         TM    ##ADRPA#,@Q              DEFAULT MESSAGE SUBCOMMAND?
         BZ    CMDDEFMS                 YES, BRANCH (DEFAULT MESSAGE)
         CLI   ##SUBCOM,C'A'            ATTRIB SUBCOMMAND?
         BNE   CALLCMD                  NO, BRANCH (NO DEFAULT MESSAGE)
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   CALLCMD                  NO, BRANCH (NO DEFAULT MESSAGE)
         SPACE 3
CMDDEFMS MVC   MSGTEXT1,MSGDEFM
         MVC   MSGTEXT1+MSGDEFM1-MSGDEFM(8),MEMBER1
         TR    MSGTEXT1+MSGDEFM1-MSGDEFM(8),TRLINE
         MESSAGE MSGTEXT1        TELL THE USER WHO IS BEING PROCESSED
         B     CALLCMD           CALL THE COMMAND
         SPACE 3
CMDREQD  TM    ##ADRPC#,@R       OPERANDS REQUIRED?
         BO    CMDPARSE          YES, BRANCH
         TM    FLAGSAA,FOPTIONS  ANY OPERANDS SPECIFIED?
         BZ    CALLCMD           NO, BRANCH
         SPACE 2
CMDPARSE OI    FLAGSAA,FOPTIONS  NOW THERE ARE OPERANDS
         L     R15,=A(PARSE)     PARSE ADDRESS
         BALR  R14,R15           GET THE COMMAND OPERANDS
         B     NEWCMD            INVALID PARSE, IGNORE
         TITLE 'P D S  --  PDS SUBCOMMAND BLDL CHECKING       02/06/84'
CALLCMD  TM    ##ADRPC#,@S         NON-PARTITIONED DATA SET?
         BO    CALLCMD9            YES, BRANCH
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP DESIRED?
         BO    CALLCMD8            YES, BRANCH
         TM    ##ADRPC#,@C         CHANGE DIRECTORY AND NO BLDL?
         BNO   CALLCMD2            NO, BRANCH
         MVC   DIRNAME,MEMBER1     GET MEMBER NAME
         NI    FLAGSCC,FF-FBLDLOK  DIRECTORY WILL BE CHANGED
         SPACE 2
CALLCMD2 TM    ##ADRPC#,@B         BLDL REQUIRED?
         BNO   CALLCMD8            NO, BRANCH
         TM    FLAGSCC,FBLDLOK     BLDL STILL VALID?
         BNO   CALLCMD4            NO, BRANCH
         TM    FLAGSAA,FOPTIONS    ANY MEMBER NAME SPECIFIED?
         BO    CALLCMD4            YES, DO A BLDL THEN
         CLC   DIRNAME(8),MEMBER1  STILL THIS MEMBER NAME?
         BE    CALLCMD8            YES, NO NEED TO BLDL
         SPACE 2
CALLCMD4 XC    DIRUSER,DIRUSER     CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1     ORIGINAL MEMBER NAME
         NI    FLAGSCC,FF-FBLDLOK  ASSUME MEMBER NOT FOUND
         SPACE 2
         BLDL  INDCB,BLDLLIST      LOCATE DIRECTORY ENTRY
         B     *+4(R15)            PROCESS RETURN CODE
         B     CALLCMD6              00 - SUCCESSFUL
         B     NOMEMBER              04 - MEMBER NOT FOUND
         B     IOERROR               08 - I/O ERROR
         SPACE 2
CALLCMD6 OI    FLAGSCC,FBLDLOK     MEMBER FOUND
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 2
CALLCMD8 TM    FLAGSAA,FMEM#MEM           MEMBER GROUP TO START?
         BNO   CALLCMDZ                   NO, BRANCH
         NI    FLAGSCC,FF-FBLDLOK         YES, FORCE A BLDL NEXT TIME
         SPACE 1
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ANY MEMBER NAMES?
         BNZ   MEMSINIT                   YES, BRANCH
         TM    ##ADRPC#,@I                SPFEDIT OR BROWSE?
         BNO   MEMSINIT                   NO, BRANCH
         SPACE 2
CALLCMD9 NI    FLAGSAA,FF-FMEM#MEM        TURN OFF MEMBER GROUPS
         B     CALLCMDZ                   CALL THE SUBCOMMAND
         TITLE 'P D S  --  PDS SUBCOMMAND MEMBER GROUPS       02/06/84'
MEMSINIT MVC   #SUBHOLD(#LSUB),##SUBCOM SAVE SUBCOMMAND CHANGED DATA
         XC    DIRNAME(8),DIRNAME       CLEAR THE MEMBER NAME
         OI    FLAGSII,FDIRGRP          INPUT FOR MEMBER GROUP
         TM    FLAGSAA,FMEMRANG         MEMBER RANGE?
         BO    DISPLAY                  YES, BRANCH
         B     PATTERN                  NO, MEMBER PATTERN
         SPACE 1
MEMSGET  MVC   DIRPTRS(12),DIRPTRS2     RESTORE THE DIRPTRS
         MVC   CURMBB(8),CURDIR         RESTORE THE DIRECTORY ADDRESS
         OI    FLAGSII,FDIRGRP          INPUT FOR MEMBER GROUP
         B     DISPLAY2                 GET NEXT MEMBER
         SPACE 1
MEMSNEXT NI    FLAGSII,FF-FDIRGRP       MEMBER GROUP INPUT IS COMPLETE
         OI    FLAGSBB,FRANPAT          MEMBERS EXIST IN THIS RANGE
         MVC   DIRPTRS2(12),DIRPTRS     SAVE THE DIRPTRS
         MVC   CURDIR(8),CURMBB         SAVE THE DIRECTORY ADDRESS
         MVC   ##SUBCOM(#LSUB),#SUBHOLD RESET CHANGED INFORMATION
         L     R14,DIRPTRS              START OF CURRENT MEMBER NAME
         CLC   DIRNAME(8),0(R14)        NAME PROCESSED BEFORE?
         BNL   MEMSGET                  YES, BRANCH AND IGNORE
         XC    DIRNAME(74),DIRNAME      CLEAR THE CURRENT ENTRY
         IC    R15,MEMFLAG              DIRECTORY FLAG BYTE
         N     R15,=XL4'0000001F'       NUMBER OF HALFWORDS
         LA    R15,11(R15,R15)          MACHINE LENGTH OF ENTRY
         MVC   DIRNAME(*-*),0(R14)      <<EXECUTED>>
         EX    R15,*-6                  MOVE IN THE CURRENT ENTRY
         SPACE 1
         MVC   MSGTEXT1,MSGMEMGP        SUBCOMMAND AND MEMBER MESSAGE
         MVC   MSGTEXT1+4+3(8),##SUBCOM
         MVC   MSGTEXT1+4+3+8+1(8),DIRNAME
         TR    MSGTEXT1+4+3+8+1(8),TRLINE
         TM    ##ADRPC#,@Q              HEADER DELAYED SUBCOMMAND?
         BNO   MEMSMSG                  NO, BRANCH
         CLI   ##SUBCOM,C'A'            ATTRIB SUBCOMMAND?
         BNE   MEMSKIP                  NO, BRANCH
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    MEMSMSG                  YES, BRANCH
         SPACE 1
MEMSKIP  MVC   FINDMEMQ(90),MSGTEXT1    SAVE FOR THEN/ELSE ACTION
         OI    FLAGSAA,FINDMSG          SUBCOMMAND IS AVAILABLE
         B     CALLCMDZ                 CALL THE SUBCOMMAND
         SPACE 2
MEMSMSG  MESSAGE MSGBLANK
         MESSAGE MSGTEXT1
         SPACE 3
CALLCMDZ L     R8,##ADRCMD              SUBCOMMAND ROUTINE START
         LA    R2,NEWCMD                SUBCOMMAND TERMINATION ADDRESS
         BR    R8                       LINK TO THE SUBCOMMAND
         TITLE 'P D S  --  PDS DISPLAY, MEMBERS, PATTERN      02/06/84'
***********************************************************************
***      DISPLAY SUBCOMMAND                                         ***
***                                                                 ***
***      MEMBERS SUBCOMMAND    ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***      PATTERN SUBCOMMAND    ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
DISPLAY  OI    FLAGSAA,FMEMRANG          **  MEMBER RANGE SELECTION
         TM    FLAGSAA,FMEMBER1+FMEMBER2 DISPLAY RANGE?
         BNO   DISPLAY0                  NO, NO RANGE CHECK
         LH    R14,LMEMBER1
         CH    R14,LMEMBER2              DETERMINE MIN NAME LENGTH
         BNH   *+8
         LH    R14,LMEMBER2
         CLC   MEMBER1(*-*),MEMBER2      <<EXECUTED>>
         EX    R14,*-6                   VALID DISPLAY RANGE?
         LA    R1,MSGRANGE               INVALID RANGE MESSAGE
         BH    MSGNEWXX                  YES, BRANCH
         SPACE 1
PATTERN  DS    0H                        **  MEMBER PATTERN SELECTION
         SPACE 1
MEMBERS  DS    0H                        **  MEMBER GROUP DISPLAY
         SPACE 1
DISPLAY0 CLC   MEMBLIT,##SUBCOM          MEMBERS SUBCOMMAND?
         BNE   *+8                       NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       YES, TURN OFF ANY MEMBER GROUP
         NI    FLAGSBB,FF-FEXIST-FLINESET-FRANPAT
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 1
DISPLAY1 L     R4,LINESIZE               GET TERMINAL LINE SIZE
         LA    R4,MSGTEXT1+3-11(R4)      END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4             START OF LINE
         CLEAR MSGTEXT1
         SPACE 1
DISPLAY2 BAL   R14,READDIR               GET NEXT DIRECTORY ENTRY
         B     DISPLAY6                  LAST MEMBER IN DIRECTORY
         SPACE 1
         OI    FLAGSBB,FEXIST            MEMBER EXISTS FLAG
         TM    FLAGSAA,FMEMRANG          MEMBER NAME RANGE?
         BNO   PATTERN1                  NO, BRANCH
         TM    FLAGSAA,FMEMBER1          START ENTRY SPECIFIED?
         BZ    DISPLAY3                  NO, BRANCH
         SPACE
         LH    R15,LMEMBER1              LENGTH-1 OF MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6
         BH    DISPLAY2                  NOT WANTED, GET NEXT
         XI    FLAGSAA,FMEMBER1
         SPACE 5
DISPLAY3 TM    FLAGSAA,FMEMBER2          LAST ENTRY SPECIFIED?
         BZ    DISPLAY4                  NO, BRANCH
         SPACE
         LH    R15,LMEMBER2
         CLC   MEMBER2(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6                   PAST END?
         BL    DISPLAY6                  YES, END OF DISPLAY
         B     DISPLAY4                  NO, DISPLAY THIS MEMBER NAME
         SPACE 1
PATTERN1 LH    R15,LMEMBER1              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER1        <<EXECUTED>>
PATTERN2 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    PATTERN3                  YES, CHECK FOR ANOTHER PATTERN
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN2               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
PATTERN3 TM    FLAGSAA,FMEMBER2          A SECOND PATTERN SPECIFIED?
         BZ    DISPLAY4                  NO, DISPLAY THE MEMBER NAME
         LH    R15,LMEMBER2              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER2        <<EXECUTED>>
PATTERN4 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    DISPLAY4                  YES, DISPLAY THE MEMBER NAME
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN4               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
DISPLAYT TRT   MEMNAME(*-*),TRTMEM       <<EXECUTED>>
DISPLAY4 TM    FLAGSAA,FMEM#MEM          MEMBER NAME GROUP DESIRED?
         BO    MEMSNEXT                  YES, FOUND A MEMBER
         LA    R1,7                      MEMBER NAME MACHINE LENGTH
         LA    R14,MEMNAME+8             END OF MEMBER NAME +1
DISPLAYB BCTR  R14,0                     SCAN
         CLI   0(R14),X'40'                  BACKWARDS
         BNE   *+8                                    FOR A
         BCT   R1,DISPLAYB                                 NON-BLANK
         CLI   MEMNAME,C'0'              FIRST DIGIT NUMERIC?
         BNL   *+12                      YES, FORCE A HEX DISPLAY
         EX    R1,DISPLAYT               ANY INVALID OR UNPRINTABLE?
         BZ    DISPLAY8                  NO, BRANCH
         TM    FLAGSBB,FLINESET          LINE IN PROGRESS?
         BZ    DISPLAY9                  NO, SKIP THE PUTLINE
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
         MESSAGE (R1)
         NI    FLAGSBB,FF-FLINESET
         EJECT
DISPLAY9 UNPK  MSGTEXT1+4(9),MEMNAME(5)  FIRST HALF OF NAME
         UNPK  MSGTEXT1+12(9),MEMNAME+4(5) SECOND HALF OF NAME
         TR    MSGTEXT1+4(16),TRTABLE    FINISH TRANSLATION
         MVC   MSGTEXT1+20(8),BLANKS     CLEAN UP GARBAGE
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    *+10                      NO, SKIP -A
         MVC   MSGTEXT1+20(2),=C'-A'     YES, SET -A
         MVI   MSGTEXT1+24,C'*'
         MVC   MSGTEXT1+25(8),MEMNAME    MEMBER NAME FROM DIRECTORY
         TR    MSGTEXT1+25(8),TRLINE     TRANSLATE TO PRINTABLE
         MVI   MSGTEXT1+33,C'*'
         OI    FLAGSBB,FLINESET+FRANPAT  OUTPUT AND MEMBERS EXIST NOW
         LA    R5,MSGTEXT1+34
         B     DISPLAY5
         SPACE
DISPLAY8 MVC   0(8,R5),MEMNAME
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    DISPLAY7                  NO, BRANCH
         MVC   8(2,R5),=C'-A'
DISPLAY7 OI    FLAGSBB,FLINESET+FRANPAT
         LA    R5,10+2(R5)
         CR    R5,R4                     LINE FULL?
         BL    DISPLAY2                  NO, CONTINUE
         SPACE
DISPLAY5 LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
         MESSAGE (R1)
         NI    FLAGSBB,FF-FLINESET
         B     DISPLAY1                  RETURN
         SPACE 2
DISPLAY6 TM    FLAGSBB,FLINESET          BUFFER IN PROGRESS?
         BNO   DISPLAYZ                  NO, BRANCH
         LA    R1,MSGTEXT1
         SR    R5,R1
         SLL   R5,16
         ST    R5,MSGTEXT1
         TM    FLAGSAA,FMEM#MEM          MEMBER GROUP IN PROGRESS?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DISPLAYZ NI    FLAGSAA,FF-FMEM#MEM       TERMINATE ANY MEMBER GROUP
         TM    FLAGSBB,FRANPAT           MEMBER IN RANGE?
         BO    NEWCMD                    YES, BRANCH
         TM    FLAGSBB,FEXIST            NO, BUT ANY IN DIRECTORY?
         LA    R1,MSGEMPTY
         BZ    MSGNEW                    NO, ** EMPTY DIRECTORY **
         LA    R1,MSGNODSP               ASSUME NONE IN RANGE
         TM    FLAGSAA,FMEMRANG MEMBER   CORRECT?
         BO    MSGNEW                    YES, BRANCH
         LA    R1,MSGNOPAT               NO, NONE MATCHING PATTERN
         B     MSGNEW
         TITLE 'P D S  --  PDS EXEC                           02/06/84'
***********************************************************************
***      EXEC SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
EXEC     L     R2,ADDRCBUF
         L     R1,ADDRCPPL
         ST    R2,0(,R1)
         SPACE 1
         LA    R3,EXECLIT           TSO EXEC CLIST PROCESSOR
         BAL   R2,ATTACH            GO ATTACH IT
         B     NEWSTAX
         TITLE 'P D S  --  PDS ALIAS                          02/06/84'
***********************************************************************
***      ALIAS SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE
ALIAS    CSECT
         USING *,R8
         MVC   MEMBERD+1(2),LMEMBER2      CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2     CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1           ONLY ONE MEMBER NAME NOW
         SPACE 2
         MVC   DIRNAME,MEMBER2            SET ALIAS NAME
         MVI   ENTRYPT,C'?'               ENTRY POINT NOT KNOWN YET
         TM    DIRFLAG,X'80'              MODULE A CURRENT ALIAS?
         BZ    ALIAS2                     NO, BRANCH
         TM    FLAGSCC,RECFMU             LOAD MODULE?
         BO    ALIAS3                     YES, BRANCH
         B     ALIAS4                     NO, BRANCH
         SPACE
ALIAS2   TM    FLAGSCC,RECFMU             LOAD MODULE?
         BZ    ALIAS4                     NO, BRANCH
         SPACE
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG VS LKED & APF DATA PRESENT?
         BZ    ALIAS2Y                    NO, BRANCH
         SPACE
         LA    R2,DIRAPF                  POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BZ    *+8                        NO, BRANCH
         LA    R2,DIRAPF3                 YES, POINT TO APF DATA
         LA    R4,11(,R2)                 POINT TO ALIAS APF DATA
         TM    DIRATTR2,DIR2SSI           SSI PRESENT?
         BZ    ALIAS2B                    NO, BRANCH
         SPACE
         LA    R2,1(,R2)                  ROUND TO HALFWORD
         N     R2,=F'-2'                  FFFFFFFE MASK
         LA    R4,1(,R4)                  ROUND TO HALFWORD
         N     R4,=F'-2'                  FFFFFFFE MASK
         MVC   0(4,R4),0(R2)              MOVE SSI INFORMATION
         LA    R4,4(,R4)                  MOVE PAST SSI INFORMATION
         LA    R2,4(,R2)                  POINT TO APF DATA
         SPACE
ALIAS2B  CLI   0(R2),1                    IS APF LENGTH OK?
         BE    ALIAS2D                    YES, BRANCH
         MESSAGE MSGAABAD                 BAD APF INFORMATION FORMAT
         LA    R2,ZERO                    SET APF=0
         SPACE
ALIAS2D  MVI   0(R4),1                    SET PROPER LENGTH
         MVC   1(1,R4),1(R2)              MOVE IN APF DATA
         EJECT
ALIAS2Y  LA    R4,DIREP                   POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND2-DIRUSER+1)/2   NUMBER OF HALFWORDS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   ALIAS2Z                    NO, BRANCH
         LA    R4,DIREPSC                 POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND3-DIRUSER+1)/2   NUMBER OF HALFWORDS
         SPACE 2
ALIAS2Z  MVC   0(3,R4),DIREPA             MOVE IN REAL MODULE ENTRY
         MVC   3(8,R4),MEMBER1            ADD REAL MODULE NAME
         TM    DIRATTR2,DIR2SSI           SSI INFORMATION?
         BNO   *+8                        NO, BRANCH
         LA    R5,2(,R5)                  YES, TWO MORE HALFWORDS
         NI    DIRFLAG,X'E0'              TURN OFF HALFWORD COUNT
         OI    DIRFLAG,*-*                <<EXECUTED>>
         EX    R5,*-4                     MOVE IN NUMBER OF HALFWORDS
         SPACE 1
ALIAS3   TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ALIAS3S                    YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    DIRATTR3,DIRAM24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    DIRATTR3,DIRAM31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS3S  L     R15,=A(READESD)            SCAN ESD FOR MEMBER NAME
         BALR  R14,R15                    MEMBER NAME IN ESD ENTRIES?
         B     ALIAS4                     NO, PSEUDO ENTRY
         SPACE 1
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ALIAS4                     YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE
ALIAS4   OI    DIRFLAG,X'80'              SET ALIAS FLAG
         BAL   R2,OPENSTOW                OPEN STOW DCB; ENQUEUES
         B     NEWCMD                     COULD NOT OPEN -- ERROR
         STOW  STOWDCB,DIRNAME,A          ADD ALIAS TO DIRECTORY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     ALIAS5                     00 - SUCCESSFUL
         B     MEMEXIST                   04 - MEMBER ALREADY EXISTS
         EX    0,*                        08 - SHOULD NOT HAPPEN - ADD
         B     FULLDIR                    12 - DIRECTORY IS FULL
         B     IOERROR                    16 - I/O ERROR IN DIRECTORY
         SPACE 3
ALIAS5   MESSAGE MSGALIAS                 MSG - ALIAS ASSIGNED
         TM    FLAGSCC,RECFMU             MUST WE GIVE ENTRY POINT?
         BZ    NEWCMD                     NO, BRANCH
         MVC   MSGTEXT1,MSGENTRY   ENTRY POINT ADDRESS AND SYMBOL
         UNPK  MSGTEXT1+MSGENTR1-MSGENTRY(7),DIREPA(4)
         TR    MSGTEXT1+MSGENTR1-MSGENTRY(6),TRTABLE
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         MVC   0(6,R14),=C'  --  '
         MVC   6(8,R14),ENTRYPT    NAME OF THE ENTRY POINT
         CLI   ENTRYPT,C'?'        ANY FOUND?
         BE    *+8                 NO, BRANCH
         LA    R15,14(,R15)        YES, SHOW ENTRY SYMBOL
         STH   R15,MSGTEXT1
         MESSAGE MSGTEXT1
         B     NEWCMD
         SPACE 3
         PRINT NOGEN
MSGALIAS MSG   'ALIAS ASSIGNED'
MSGAABAD MSG   'BAD APF INFORMATION FORMAT, ZERO USED'
         PRINT GEN
         TITLE 'P D S  --  PDS ATTRIB                         02/06/84'
***********************************************************************
***      ATTRIB SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
ATTRIB   CSECT
         USING *,R8
         LA    R1,MSGNLIB          ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    ATTR020             YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1,MSGSPFHD
         TM    FLAGSAA,FHEAD       HEADER WRITTEN YET?
         BO    ATTR000             YES, BRANCH
         MESSAGE MSGTEXT1
         OI    FLAGSAA,FHEAD       NOW IT HAS BEEN WRITTEN
         SPACE 1
ATTR000  MVC   MSGTEXT1+5(80),MSGTEXT1+4
         MVC   MSGTEXT1+6(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   MSGTEXT1+6+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   ATTR010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   ATTR010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   ATTR010             NO, BRANCH
         SPACE 1
         LA    R2,MSGTEXT1
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         B     ATTR012                 NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
ATTR010  CLC   ZERO,0(R2)              ZERO?
         BE    ATTR012                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR012                 YES, NO SSI
         SPACE
         MVC   MSGTEXT1+6+8+6(4),=C'SSI:'
         UNPK  MSGTEXT1+6+8+6+5(9),0(5,R2)
         TR    MSGTEXT1+6+8+6+5(8),TRTABLE
         MVI   MSGTEXT1+6+8+6+5+8,X'40'
         SPACE 1
ATTR012  MESSAGE MSGTEXT1              OUTPUT STATISTICS
         CLI   #OPTOPT,FF              ANY ATTRIBUTE CHANGE?
         BNE   ATTR600                 NO, CHECK FOR ALIAS
         MESSAGE MSGNLOAD              OUTPUT A MESSAGE
         B     ATTR600                 CHECK FOR ALIAS
         SPACE
ATTR020  MVI   RLDCOUNT,X'FF'          ASSUME NO RLD/CONTROL DATA
         MVI   LKEDDATE,X'FF'          ASSUME NO LKED DATE
         CLI   #RLDERR,X'01'           "RLDFIX" OPERAND?
         BE    ATTR024                 YES, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE"?
         BE    ATTR030                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    ATTR024                 YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR030                 YES, BRANCH
ATTR024  L     R15,=A(READIDR)         FIRST RLD COUNT, LKED DATA
         BALR  R14,R15                 IDR DATA AVAILABLE?
         MVI   LKEDDATE,X'FF'          NO, ERROR
         SPACE 1
ATTR030  CLI   #OPTOPT,FF              ANY CHANGED ATTRIBUTES?
         BNE   ATTR170                 NO, BRANCH
         SPACE
         CLI   #RLDERR,X'01'           "RLDFIX"?
         BNE   ATTR038                 NO, BRANCH
         TM    DIRATTR2,DIRAOSLE       OS/VS LINKAGE-EDITOR?
         BNO   ATTR034                 NO, BRANCH
         CLI   RLDCOUNT,X'FF'          ANY RLD/CONTROL DATA FOUND?
         BE    ATTR034                 NO, BRANCH
         MVC   DIRATTR4(1),RLDCOUNT    MODIFY THE DIRECTORY ENTRY
         B     ATTR038
ATTR034  MESSAGE MSGRLDNO              NO CHANGE IS POSSIBLE
         SPACE 2
ATTR038  CLI   #AUTHOPT,X'01'          CHANGE APF PROCESSING?
         BL    ATTR060                 NO, BRANCH
         BE    ATTR040                 YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE APF INFORMATION?
         BNO   ATTR060                     NO, BRANCH
         SPACE 1
ATTR040  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE APF INFORMATION?
         BNO   ATTR050                     NO, BRANCH
         LA    R2,DIRAPF                   YES, POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               SCATTER SIZE
         TM    DIRFLAG,X'80'           ALIAS?
         BNO   *+8                     NO, BRANCH
         LA    R2,11(,R2)              ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI        SSI?
         BNO   *+12                    NO, BRANCH
         LA    R2,5(,R2)               ADD 4 AND ROUND TO HALFWORD
         N     R2,=F'-2'
         CLI   0(R2),1                 LENGTH RIGHT?
         BNE   ATTR050                 NO, BRANCH
         MVI   1(R2),1                 ASSUME TURN AUTHORIZATION ON
         CLI   #AUTHOPT,X'01'          CORRECT?
         BE    ATTR060                 YES, BRANCH
         MVI   1(R2),0                 NO, TURN OFF
         B     ATTR060
         SPACE 3
ATTR050  MESSAGE MSGADNC               INVALID APF DATA LENGTH
         SPACE 2
ATTR060  CLI   #PAGEOPT,X'01'              CHANGE PAGE ALIGNMENT?
         BL    ATTR100                     NO, BRANCH
         BE    ATTR070                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE PAGE INFO?
         BNO   ATTR100                     NO, BRANCH
         SPACE 1
ATTR070  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE PAGE INFORMATION?
         BNO   ATTR080                     NO, BRANCH
         OI    DIRATTR2,DIR2PAGA           ASSUME TURN ON PAGE BOUNDARY
         CLI   #PAGEOPT,X'01'              CORRECT?
         BE    ATTR100                     YES, BRANCH
         NI    DIRATTR2,FF-DIR2PAGA        NO, TURN OFF PAGE BOUNDARY
         B     ATTR100
         SPACE
ATTR080  MESSAGE MSGNOPAG              PAGE ALIGNMENT CANNOT BE CHANGED
         SPACE 2
ATTR100  CLI   #RMODE,1                    ANY RMODE CHANGE?
         BL    ATTR120                     NO, BRANCH
         BH    ATTR110                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR120                     NO, BRANCH
         SPACE 1
ATTR110  LA    R1,MSGNOLKD                 NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR150                     NO, BRANCH
         NI    DIRATTR3,FF-DIRRMANY        SET RMODE=24
         CLI   #RMODE,1                    CORRECT?
         BE    ATTR120                     YES, BRANCH
         OI    DIRATTR3,DIRRMANY           NO, TURN ON RMODE ANY
         SPACE 2
ATTR120  CLI   #AMODE,1                    ANY AMODE CHANGE?
         BL    ATTR160                     NO, BRANCH
         BH    ATTR130                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR160                     NO, BRANCH
         SPACE 1
ATTR130  LA    R1,MSGNOLKD                 NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR150                     NO, BRANCH
         TM    DIRFLAG,X'80'               ALIAS ENTRY?
         BO    ATTR140                     YES, BRANCH
         OI    DIRATTR3,DIRAM24+DIRAM31    MAIN ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAM31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAM31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAM24            SET AMODE=31
         B     ATTR160
         SPACE
ATTR140  OI    DIRATTR3,DIRAA24+DIRAA31    ALIAS ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAA31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAA31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAA24            SET AMODE=31
         B     ATTR160
         SPACE
ATTR150  MESSAGE (R1)                      AMODE/RMODE MESSAGE
         SPACE 2
ATTR160  OC    DIRATTR,#ATTRYES        ADD POSITIVE ATTRIBUTES
         NC    DIRATTR,#ATTRNO         REMOVE NEGATIVE ATTRIBUTES
         SPACE 1
*
*   RESTRICTION: BOTH RENT AND REUS MUST BE SPECIFIED FOR REENTRANT
         TM    DIRATTR,ATTRRENT        WAS REENTRABLE SPECIFIED?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR,ATTRREUS        YES, ALSO FORCE REUSABLE
         SPACE
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR
         STOW  STOWDCB,DIRNAME,R       REPLACE OPTION
         LR    R5,R15                  SAVE RETURN CODE
***      BAL   R2,SHUTSTOW             *** CLOSE STOW AFTER NEWCMD
         SPACE 1
         B     *+4(R5)                 PROCESS RETURN CODE
         B     ATTR170                   00 - SUCCESSFUL
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
ATTR170  MESSAGE MSGATTRS              ATTRIBUTES MSG
         SPACE 1
         CLEAR MSGTEXT1
         LA    R1,MSGTEXT1+3           INDENT A SPACE
         SR    R0,R0                   NO ATTRIBUTES FLAG
         LA    R14,TBLATTR-12          ATTRIBUTES TABLE
         LH    R2,DIRATTR              GET MODULE ATTRIBUTES
         SPACE 2
ATTR300  LA    R14,12(R14)
         CLI   0(R14),X'FF'            END OF TABLE?
         BE    ATTR310                 YES, BRANCH
         SPACE
         LH    R15,0(R14)              GET ATTRIBUTE FLAGS
         TM    2(R14),X'80'            POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'                MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'                MASK FOR NEG (FOR BNZ) INST
         NR    R15,R2                  CHECK FOR ATTRIBUTE PRESENT
         NOP   ATTR300                 <<BZ OR BNZ>>
         EX    R3,*-4                  BRANCH IF NO DISPLAY ATTRIBUTE
         SPACE
         BALR  R0,0                    INDICATE ATTRIBUTE EXISTS
         LA    R3,X'7F'
         MVI   0(R1),C','              DELIMITER AFTER LAST ATTR
         IC    R15,2(R14)              LENGTH-1 OF ATTRIBUTE
         NR    R15,R3
         MVC   2(*-*,R1),3(R14)        <<EXECUTED>>
         EX    R15,*-6
         LA    R1,3(R15,R1)            JUMP POINTER
         B     ATTR300
         SPACE 3
ATTR310  LTR   R0,R0                   ANY ATTRIBUTES PRESENT?
         LA    R1,MSGBNONE
         BZ    ATTR320                 NO, BRANCH
         SPACE
         LA    R1,L'MSGTEXT1
         SLL   R1,16
         ST    R1,MSGTEXT1
         LA    R1,MSGTEXT1
         SPACE
ATTR320  MESSAGE (R1)
         SPACE 3
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE
         TM    DIRFLAG,X'80'           ALIAS?
         BNO   *+8                     NO, BRANCH
         LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR330                 YES, BRANCH
         MESSAGE MSGNOVSL              NON-VS LINKAGE EDITOR MESSAGE
         B     ATTR340
         SPACE
ATTR330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR360                 NO, SKIP SSI PROCESSING
         SPACE
ATTR340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE
ATTR350  CLC   ZERO,0(R2)              ZERO?
         BE    ATTR360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR360                 YES, NO SSI
         SPACE
         MVC   MSGTEXT1,MSGSSI         SSI MESSAGE
         UNPK  MSGTEXT1+MSGSSI1-MSGSSI(9),0(5,R2)
         TR    MSGTEXT1+MSGSSI1-MSGSSI(8),TRTABLE
         MESSAGE MSGTEXT1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTR360  TM    FLAGSCC,RECFMU          LOAD MODULE LIB?
         BZ    ATTR600                 NO, BRANCH
         SPACE
         MVI   MODESAVE,0
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTR540                     NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    ATTR400                     NO, BRANCH
         MVC   MSGTEXT1,MSGMODES           RMODE/AMODE MESSAGE
         LA    R1,=C'24 '                  ASSUME RMODE 24
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+8                         NO, BRANCH
         LA    R1,=C'ANY'                  YES, RMODE=ANY
         MVC   MSGTEXT1+MSGMODER(3),0(R1)  MOVE IN THE RMODE TEXT
         LA    R1,=C'24 '                  ASSUME AMODE 24
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+8                         NO, BRANCH
         LA    R1,=C'31 '                  YES, AMODE=31
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+8                         NO, BRANCH
         LA    R1,=C'ANY'                  YES, AMODE=ANY
         MVC   MSGTEXT1+MSGMODEA(3),0(R1)  MOVE IN THE AMODE TEXT
         MESSAGE MSGTEXT1
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTR400                     NO, BRANCH
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    ATTR370                     YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    ATTR400                     YES, BRANCH
         LA    R1,=C'24 '                  AMODE IS 24
ATTR370  MVC   MSGTEXT1,MSGRESAD
         MVC   MSGTEXT1+MSGRESAM(3),0(R1)  MOVE IN THE AMODE TEXT
         MESSAGE MSGTEXT1
         SPACE 2
ATTR400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTR500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTR500                 YES, BRANCH
         MESSAGE MSGRLDCN
         SPACE 2
ATTR500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTR510                 NO, BRANCH
         MESSAGE MSGPAGA               PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTR510  LA    R1,MSGNOAPF             ASSUME NO APF DATA
         TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   ATTR530                 NO, BRANCH
         SPACE
         LA    R1,MSGAPFLB             ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTR530                 NO, BRANCH
         SPACE 3
         LA    R1,MSGYAUTH             ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTR530                 YES, BRANCH
         BL    ATTR540                 NO, APF=0 (NO MESSAGE NEEDED)
         LA    R1,MSGAPF2B             APF DATA IS TOO LARGE
         SPACE 1
ATTR530  MESSAGE (R1)
         SPACE 2
ATTR540  TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   ATTR550                         NO, BRANCH
         MVC   MSGTEXT1,MSGOVCON               OVERLAY AND REENTRANT
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,ATTR580                      YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),REUSTXT+3  OVERLAY AND REUSABLE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,ATTR580                      YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),REFRTXT+3  OVERLAY AND REFRESHABLE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,ATTR580                      YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),SCTRTXT+3  OVERLAY AND SCATTER
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,ATTR580                      YES, ERROR MESSAGE
         TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   ATTR550                         NO, BRANCH
         MVC   MSGTEXT1+MSGOVCOD(5),=C'RMODE'
         MVC   MSGTEXT1+MSGOVCOD+6(3),=C'ANY'  RMODE ANY
         TM    MODESAVE,DIRRMANY               CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,ATTR580                      YES, ERROR MESSAGE
         MVI   MSGTEXT1+MSGOVCOD,C'A'          AMODE ANY
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    ATTR550                         YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    *+10                            YES, BRANCH
         MVC   MSGTEXT1+MSGOVCOD+6(3),=C'31 '  NO, AMODE 31
         BAL   R2,ATTR580                      ERROR MESSAGE
         SPACE 1
ATTR550  TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   ATTR560                         NO, BRANCH
         MESSAGE MSGTSTNE                      YES, ERROR
         SPACE 1
ATTR560  TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   ATTR570                         NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    ATTR570                         YES, BRANCH
         MESSAGE MSGRENTR                      ERROR MESSAGE
         SPACE 1
ATTR570  TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   ATTR590                         NO, BRANCH
         MESSAGE MSGREUSC                      YES, ERROR
         B     ATTR590
         SPACE 1
ATTR580  MESSAGE MSGTEXT1                      OUTPUT AN ERROR MESSAGE
         BR    R2
         SPACE 2
ATTR590  MVC   MSGTEXT1,MSGENTRY   ENTRY POINT ADDRESS
         UNPK  MSGTEXT1+MSGENTR1-MSGENTRY(7),DIREPA(4)
         TR    MSGTEXT1+MSGENTR1-MSGENTRY(6),TRTABLE
         MESSAGE MSGTEXT1
         SPACE 1
         MVC   MSGTEXT1,MSGLEN
         UNPK  MSGTEXT1+MSGLEN1-MSGLEN(7),DIRSTORE(4)
         TR    MSGTEXT1+MSGLEN1-MSGLEN(6),TRTABLE
         MVI   MSGTEXT1+MSGLEN1-MSGLEN+6,X'40'
         SPACE 1
         ICM   R1,B'0111',DIRSTORE ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)        NEXT HIGHER
         SRL   R1,10                          1K VALUE
         CVD   R1,DOUBLE
         ED    MSGTEXT1+MSGLEN2-MSGLEN(6),DOUBLE+5
         MESSAGE MSGTEXT1
         SPACE 2
         CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTR600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTR600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR600                 YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1,MSGHISTD       LAST LINKEDIT DATE MSG
         ED    MSGTEXT1+DATEMASK-MSGHISTD(L'DATEMASK),LKEDDATE
         MESSAGE MSGTEXT1
         SPACE 3
ATTR600  TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTR630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTR630                 NO, BRANCH
         TM    FLAGSGG,FALUPCON        NO ALIAS UPDATE?
         BO    ATTR630                 YES, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM24
         MVC   MSGTEXT1,MSGALIUP       ALIAS UPDATED: (MEMBER)
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTR610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTR630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTR610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTR610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM24
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTR610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM24
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   MSGTEXT1+MSGALIUN(8),DIRNAME  MEMBER NAME
         TR    MSGTEXT1+MSGALIUN(8),TRLINE   MAKE PRINTABLE
         MESSAGE MSGTEXT1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTR610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTR630  NI    FLAGSBB,FF-FLINESET
         CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
         BE    ATTR640                 YES, BRANCH
         BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD                  YES, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BO    ATTR640                 YES, BRANCH
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTR640  MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTR900                 NO, FIND ALL ALIASES
         SPACE 2
*  LIST ANY CORRESPONDING REAL MODULE NAMES
         SPACE 1
ATTR800  BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTR810            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTR800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTR800            NO, BRANCH
         MVC   MSGTEXT1,MSGREAL
         MVC   MSGTEXT1+MSGREAL1-MSGREAL(8),MEMNAME
         TR    MSGTEXT1+MSGREAL1-MSGREAL(8),TRLINE
         MESSAGE MSGTEXT1
         B     ATTR800
         SPACE 1
ATTR810  CLC   MSGTEXT1(20),MSGREAL  ANY REAL(S) FOUND?
         BE    NEWCMD                YES, BRANCH
         MESSAGE MSGNOMOD
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   MSGTEXT1,MSGXREAL  DIRECTORY NAME OF REAL MODULE
         LA    R1,MSGTEXT1
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREALS
         TR    MSGTEXT1+MSGXREA1-MSGXREAL(8),TRLINE
         B     MSGNEW
         SPACE 2
*  LIST ANY ALIASES FOR THE MODULE
         SPACE 1
ATTR900  BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     ATTR940              LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   ATTR900              NO, BRANCH
         TM    MEMFLAG,X'80'        AN ALIAS?
         BO    ATTR910              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME   DID I FIND MYSELF?
         BE    ATTR900              YES, BRANCH
         MVC   MSGTEXT2,MSGAPPAR    THIS IS AN APPARENT ALIAS MEMBER
         MVC   MSGTEXT2+MSGAPPMB-MSGAPPAR(8),MEMNAME
         TR    MSGTEXT2+MSGAPPMB-MSGAPPAR(8),TRLINE
         MESSAGE MSGTEXT2
         B     ATTR900
         SPACE
ATTR910  LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BO    ATTR920              YES, BRANCH
         OI    FLAGSBB,FLINESET     LINE NOW IN PROGRESS
         MVC   MSGTEXT1,MSGOTHER
         LH    R1,MSGTEXT1
         LA    R5,MSGTEXT1(R1)      POINT TO END OF MESSAGE
         SPACE
ATTR920  CLI   0(R2),X'40'          SCAN FOR
         BNE   *+8                          LAST
         BCT   R2,ATTR920                       NON-BLANK
         SPACE
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R5,R2)
         LA    R1,MSGTEXT1
         SR    R15,R1
         C     R15,LINESIZE
         BNH   ATTR930
         SR    R5,R1
         STH   R5,MSGTEXT1
         MESSAGE (R1)
         MVC   MSGTEXT1,MSGBLANK
         LH    R1,MSGTEXT1
         LA    R5,MSGTEXT1-1(R1)    INDENT FOR BLANKS
         SPACE
ATTR930  MVI   0(R5),X'40'
         MVC   1(8,R5),MEMNAME
         TR    1(8,R5),TRLINE
         LA    R1,2(R2,R5)
         MVI   0(R1),C','
         LA    R5,3(R2,R5)
         B     ATTR900
         SPACE 2
ATTR940  TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BZ    NEWCMD               NO, EXIT
         LA    R1,MSGTEXT1
         SR    R5,R1
         BCTR  R5,0                 DELETE FINAL COMMA
         STH   R5,MSGTEXT1
         B     MSGNEW
         SPACE 2
         PRINT NOGEN
MSGATTRS MSG   'ATTRIBUTES ARE:'
MSGBNONE MSG   'NONE'
MSGYAUTH MSG   'APF AUTHORIZED'
MSGNOVSL MSG   'NOT APF AUTHORIZED, NOT OS/VS LINKED'
MSGNOAPF MSG   'NOT APF AUTHORIZED, APF DATA MISSING'
MSGAPFLB MSG   'NOT APF AUTHORIZED, APF DATA WRONG LENGTH'
MSGAPF2B MSG   'APF AUTHORIZED, APF DATA VALUE > 1'
MSGADNC  MSG   'APF DATA COULD NOT BE CHANGED'
MSGRLDNO MSG   'RLD/CONTROL COUNT COULD NOT BE CHANGED'
MSGPAGA  MSG   'PAGE ALIGNMENT IS REQUIRED'
MSGNOLKD MSG   'NEITHER RMODE NOR AMODE CAN BE CHANGED -- OBSOLETE LINKX
               AGE-EDITOR'
MSGNOPAG MSG   'PAGE ALIGNMENT COULD NOT BE CHANGED -- OBSOLETE LINKAGEX
               -EDITOR'
MSGMODES MSG   'MVS/XA RESIDENCE MODE IS 123 -- ADDRESSING MODE IS 123'
MSGMODER EQU   29
MSGMODEA EQU   29+26
MSGSSI   MSG   'SSI INFORMATION: 12345678'
MSGSSI1  EQU   MSGSSI+21,8
MSGOTHER MSG   'ALIASES FOR THIS MEMBER ARE: '
         PRINT GEN
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF150
         TITLE 'P D S  --  PDS BROWSE, SPFEDIT                02/06/84'
***********************************************************************
***     BROWSE SUBCOMMAND      ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***     SPFEDIT SUBCOMMAND     ADDED BY BRUCE LELAND -- JULY, 1982  ***
***********************************************************************
*
         SPACE 1
BROWSE   CSECT
         USING *,R8
         LA    R2,EDITLIT               ASSUME SPFEDIT SUBCOMMAND
         CLI   ##SUBCOM,C'S'            CORRECT?
         BE    *+8                      YES, BRANCH
         LA    R2,BROWSLIT              NO, USE BROWSE
         SPACE 2
         LA    R1,MSGOPT6               ASSUME ALREADY UNDER SPF
         TM    FLAGSFF,FSPFOPT6         CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
         LA    R1,MSGSPERR              ASSUME SPF DID NOT INITIALIZE
         TM    FLAGSFF,FSPFERR          CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
         MVC   MSGTEXT1(8),0(R2)        SPF SERVICE REQUESTED
         CLI   #SPFOLD,X'01'            "NEW" SPECIFIED?
         BNE   SPFMEMOK                 NO, BRANCH
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    SPFMEMOK                 YES, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     SPFMEMOK                   04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
SPFMEMOK CLI   MSGTEXT1,C'B'           BROWSE SUBCOMMAND?
         BE    SPFINIT                 YES, ALLOW IT
         MVC   DSNMEMQ(8),DIRNAME      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         LA    R1,MSGILOAD             ASSUME THIS IS A LOAD LIBRARY
         TM    FLAGSCC,RECFMU          CORRECT?
         BO    MSGNEWXX                YES, BRANCH
         SPACE 2
SPFINIT  TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF ENVIRONMENT INITIALIZED?
         BM    SPFINTRF                   YES, CALL SPF EDIT/BROWSE
         OI    FLAGSFF,FSPFERR          SPF ERROR FLAG
         MVI   ##ADRCM#,CONTINUE+EDITOR RECURSIVE CALL FROM ISPF
         BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE SPF
         BAL   R2,DEALLDCB              AND FREE THE DATA SET
         MVC   ISPFTEXT+4(L'ISPF),ISPF        ISPF   PGM(PDS45678) P...
         MVC   ISPFTEXT+ISPFPGM+4(8),PDSNAME  PDS COMMAND NAME
         CVD   R7,ISPFTEXT+4+ISPFPARM         ADD STORAGE ADDRESS
         LA    R1,L'ISPF+4              LENGTH OF STRING PASSED TO SPF
         SLL   R1,16                    CLEAR BOTTOM TWO BYTES
         ST    R1,ISPFTEXT              SAVE STRING TOTAL LENGTH
*        MESSAGE ISPFTEXT               DELETE * TO OUTPUT COMMAND
         SPACE 2
         MVI   ISPFTEXT+3,8             OFFSET OF PGM(... PARAMETER
         LA    R1,ISPFTEXT              START OF PARAMETER LIST
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LOAD  EP=ISPLINK               ADDRESS OF SPF INTERFACE MODULE
         ST    R0,ISPLINK               SAVE ADDRESS FOR LATER
         SPACE 1
         XI    FLAGSFF,FSPFERR+FSPFCALL SPF ERROR OFF; SPF CALL ON
         LA    R1,ADDRTEXT              START OF CPPL PARAMETERS
         ATTACH EPLOC=ISPF,ECB=SPFECB,SZERO=NO,SHSPV=78,              XX
               SF=(E,ATTACHS1)
         ST    R1,SPFTCB                TCB ADDRESS FOR THE SUBTASK
         WAIT  ECB=SPFECB               WAIT FOR COMPLETION
         DETACH SPFTCB                  REMOVE THIS SUBTASK
         NI    FLAGSFF,FF-FSPFCALL      SPF FLAG TURNED OFF
         SR    R5,R5
         CLC   ##SUBCOM(8),ENDLIT       END SUBCOMMAND?
         BE    RETURN8                  YES, DONE
         OI    FLAGSFF,FSPFERR          SPF ERROR
         B     RESTART                  REALLOCATE THE FILE AND REOPEN
         SPACE 3
SPFINTRF BAL   R2,CLOSEIT               CLOSE THE INPUT DATA SET
         XC    RECOVER,RECOVER          NO ESTAE RECOVER ATTEMPTED
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'         SECOND PARAMETER
         LA    R2,=CL8'REFRESH'         THIRD PARAMETER
         STM   R0,R2,MSGTEXT2           SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'         LAST
         LA    R1,MSGTEXT2                  PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15                  LET SPF KNOW
         SPACE 1
         MVI   MSGTEXT1+8,C''''         ADD A QUOTE
         MVC   MSGTEXT1+8+1(44),DSNAME  ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R2,MSGTEXT1+8(R15)       POINT TO CURRENT BYTE
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    SPFALLM                  YES, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS DESIRED?
         BNZ   SPFMEMBR                   NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP IN PROGRESS?
         BNO   SPFALLM                    NO, BRANCH
         SPACE 1
SPFMEMBR MVI   1(R2),C'('               MEMBER NAME PARENTHESIS
         MVC   2(8,R2),DIRNAME          ADD MEMBER NAME
         MVI   10(R2),X'40'             INSURE SCAN STOPS
         LA    R2,1(,R2)                SCAN
         CLI   0(R2),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R2),C')'               ADD FINAL PARENTHESIS
         SPACE 2
SPFALLM  MVI   1(R2),C''''              ADD FINAL QUOTE
         MVI   2(R2),X'40'              ADD FINAL BLANK
         LA    R2,MSGTEXT1              FIRST PARAMETER
         LA    R3,MSGTEXT1+8            SECOND PARAMETER
         LA    R4,VOLUME                THIRD PARAMETER
         LA    R5,PASSWORD              FOURTH PARAMETER
         STM   R2,R5,MSGTEXT2           SAVE PARAMETER ADDRESSES
         MVI   MSGTEXT2+12,X'80'        MARK END OF LIST (FOUR ITEMS)
         SPACE 1
         LA    R1,MSGTEXT2              PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15                  SPF INTERFACE ROUTINE
         BAL   R2,CLEAR                 TURN OFF FULL SCREEN MODE
         SPACE 1
         ESTAE 0                        CANCEL THE ESTAE ADDRESS
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRESS
         ESTAE STAEEXIT,CT,PARAM=(R7),MF=(E,STAEPARM)
         B     RESTART4                 OPEN THE DATA SET AGAIN
         SPACE 3
         PRINT NOGEN
MSGOPT6  MSG   'SUBCOMMAND NOT OPERATIONAL -- ALREADY UNDER SPF'
MSGSPERR MSG   'SPF IS NOT OPERATIONAL NOW'
         PRINT GEN
.NSPF150 ANOP
         TITLE 'P D S  --  PDS CONTROL                        02/06/84'
***********************************************************************
***      CONTROL SUBCOMMAND    ADDED BY BRUCE LELAND -- OCT., 1983  ***
***********************************************************************
*
         SPACE 1
CONTROL  CSECT
         USING *,R8
         MESSAGE MSGBLANK          ONE BLANK LINE
         MVC   MSGTEXT1,MSGPDS61   PROGRAM IDENTIFIER MESSAGE
         MVC   MSGTEXT1+4(8),PDSNAME
         MESSAGE MSGTEXT1
         MESSAGE MSGBLANK          ONE BLANK LINE
         SPACE 1
         CLI   #OUT+0,1            ANY CONTROL OPERAND?
         BL    CONT020             NO, BRANCH
         BH    CONT012             YES, THE NO FORM
         NI    FLAGSGG,FF-FALINCON YES FORM
         B     CONT020
CONT012  OI    FLAGSGG,FALINCON    NO FORM
         SPACE 1
CONT020  CLI   #OUT+1,1            ANY CONTROL OPERAND?
         BL    CONT030             NO, BRANCH
         BH    CONT022             YES, THE NO FORM
         NI    FLAGSGG,FF-FALUPCON YES FORM
         B     CONT030
CONT022  OI    FLAGSGG,FALUPCON    NO FORM
         SPACE 1
CONT030  CLI   #OUT+2,1            ANY CONTROL OPERAND?
         BL    CONT040             NO, BRANCH
         BH    CONT032             YES, THE NO FORM
         NI    FLAGSGG,FF-FLKEDCON YES FORM
         B     CONT040
CONT032  OI    FLAGSGG,FLKEDCON    NO FORM
         SPACE 1
CONT040  CLI   #OUT+3,1            ANY CONTROL OPERAND?
         BL    CONT050             NO, BRANCH
         BH    CONT042             YES, THE NO FORM
         NI    FLAGSGG,FF-FPROMCON YES FORM
         B     CONT050
CONT042  OI    FLAGSGG,FPROMCON    NO FORM
         SPACE 1
CONT050  CLI   #OUT+4,1            ANY CONTROL OPERAND?
         BL    CONT060             NO, BRANCH
         BH    CONT052             YES, THE NO FORM
         NI    FLAGSGG,FF-FRECVCON YES FORM
         B     CONT060
CONT052  OI    FLAGSGG,FRECVCON    NO FORM
         SPACE 1
CONT060  CLI   #OUT+5,1            ANY CONTROL OPERAND?
         BL    CONT100             NO, BRANCH
         BH    CONT062             YES, THE NO FORM
         NI    FLAGSGG,FF-FDOUBCON YES FORM
         B     CONT100
CONT062  OI    FLAGSGG,FDOUBCON    NO FORM
         SPACE 2
CONT100  MVC   MSGTEXT1,MSGCONTR
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1+4(8),BLANKS
         LA    R1,MSGTEXT1+7-2     START OF GLOBAL TEXT
         SPACE 1
         TM    FLAGSGG,FALINCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CALINCON,R1),CALINCON
         LA    R1,L'CALINCON(R1)
         TM    FLAGSGG,FDOUBCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CDOUBCON,R1),CDOUBCON
         LA    R1,L'CDOUBCON(R1)
         TM    FLAGSGG,FLKEDCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON,R1),CLKEDCON
         LA    R1,L'CLKEDCON(R1)
         TM    FLAGSGG,FPROMCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CPROMCON,R1),CPROMCON
         LA    R1,L'CPROMCON(R1)
         TM    FLAGSGG,FRECVCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CRECVCON,R1),CRECVCON
         LA    R1,L'CRECVCON(R1)
         TM    FLAGSGG,FALUPCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CALUPCON,R1),CALUPCON
         LA    R1,L'CALUPCON(R1)
         SPACE 1
         MVC   0(8,R1),BLANKS      DELETE THE LAST COMMA
         CLI   MSGTEXT1+5+2,X'40'  ANY MESSAGE?
         BE    *+8                 NO, BRANCH
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1,MSGCONTR
         MVC   MSGTEXT1+4(8),BLANKS
         LA    R1,MSGTEXT1+7-2     START OF GLOBAL TEXT
         SPACE 2
         TM    FLAGSGG,FALINCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CALINCON-2,R1),CALINCON+2
         LA    R1,L'CALINCON-2(R1)
         TM    FLAGSGG,FDOUBCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CDOUBCON-2,R1),CDOUBCON+2
         LA    R1,L'CDOUBCON-2(R1)
         TM    FLAGSGG,FLKEDCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON-2,R1),CLKEDCON+2
         LA    R1,L'CLKEDCON-2(R1)
         TM    FLAGSGG,FPROMCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CPROMCON-2,R1),CPROMCON+2
         LA    R1,L'CPROMCON-2(R1)
         TM    FLAGSGG,FRECVCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CRECVCON-2,R1),CRECVCON+2
         LA    R1,L'CRECVCON-2(R1)
         TM    FLAGSGG,FALUPCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CALUPCON-2,R1),CALUPCON+2
         LA    R1,L'CALUPCON-2(R1)
         SPACE 1
         MVC   0(8,R1),BLANKS      DELETE THE LAST COMMA
         CLI   MSGTEXT1+5+2,X'40'  ANY MESSAGE?
         BE    *+8                 NO, BRANCH
         MESSAGE MSGTEXT1
         MESSAGE MSGBLANK          ONE BLANK LINE
         SPACE 2
CONT200  CLI   #OUT+7,1            CPULOOP?
         BNE   CONT300             NO, BRANCH
         MESSAGE MSGCLOOP          YES, LOOP MESSAGE
         B     *                   LOOP
         SPACE 1
CONT300  CLI   #OUT+7,2            ABEND?
         BNE   CONT400             NO, BRANCH
         MESSAGE MSGCABEN          YES, ABEND MESSAGE
         DC    H'0'                LOOP
         SPACE 1
CONT400  CLI   #OUT+7,3            OUTLOOP?
         BNE   CONT500             NO, BRANCH
         MESSAGE MSGCOUT           YES, LOOP MESSAGE
         B     CONT400             LOOP
         SPACE 1
CONT500  CLI   #OUT+7,4            IOSTATS DESIRED?
         BNE   NEWCMD              NO, BRANCH
         LH    R1,IODONE           ** INPUT ROUTINE ENTRIES
         LA    R14,MSGCOA1
         BAL   R2,CONT900
         LH    R1,IODOUBLE         ** INITIATED DOUBLE READS
         LA    R14,MSGCOA2
         BAL   R2,CONT900
         LH    R1,IODOUSE          ** USABLE DOUBLE READS
         LA    R14,MSGCOA3
         BAL   R2,CONT900
         LH    R1,IODODIS1         ** DISCARDED -- NEW SUBROUTINE
         LA    R14,MSGCOA4
         BAL   R2,CONT900
         LH    R1,IODODIS2         ** DISCARDED -- NEW TTR
         LA    R14,MSGCOA5
         BAL   R2,CONT900
         LH    R1,IODODIS3         ** DISCARDED -- AT CLOSE
         LA    R14,MSGCOA6
         BAL   R2,CONT900
         LH    R1,IODONEW          ** TTR CHANGES
         LA    R14,MSGCOA7
         BAL   R2,CONT900
         XC    IODONE(14),IODONE
         B     NEWCMD
         SPACE 2
CONT900  MVC   MSGTEXT1,0(R14)
         MVC   MSGTEXT1+4(7),MSGCOAM
         LTR   R1,R1
         BZR   R2
         CVD   R1,DOUBLE
         ED    MSGTEXT1+4(7),DOUBLE+5
         MESSAGE MSGTEXT1
         BR    R2
         SPACE 2
         PRINT NOGEN
MSGCONTR MSG   'GLOBAL OPERANDS:                                       X
                                  '
MSGCOA1  MSG   ' 12,345 INPUT ROUTINE ENTRIES'
         ORG   MSGCOA1+4
MSGCOAM  DC    X'4020206B202120'
         ORG   ,
MSGCOA2  MSG   ' 12,345 INITIATED DOUBLE READS'
MSGCOA3  MSG   ' 12,345 USABLE DOUBLE READS'
MSGCOA4  MSG   ' 12,345 DISCARDED READS -- NEW SUBCOMMAND'
MSGCOA5  MSG   ' 12,345 DISCARDED READS -- NEW TTR'
MSGCOA6  MSG   ' 12,345 DISCARDED READS -- AT CLOSE'
MSGCOA7  MSG   ' 12,345 TTR CHANGES'
MSGCLOOP MSG   'CPULOOP -- CPU LOOP FOLLOWS:'
MSGCABEN MSG   'ABEND -- SYSTEM 0C1 FOLLOWS:'
MSGCOUT  MSG   'OUTLOOP -- THIS DATA REPEATS'
         PRINT GEN
CALINCON DC    C'NOALIASINFO, '
CDOUBCON DC    C'NODOUBLE, '
CLKEDCON DC    C'NOLKEDDATE, '
CPROMCON DC    C'NOPROMPT, '
CRECVCON DC    C'NORECOVER, '
CALUPCON DC    C'NOUPDATEALIAS, '
         SPACE 1
         TITLE 'P D S  --  PDS DELETE                         02/06/84'
***********************************************************************
***    DELETE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- OCT., 1983  ***
***********************************************************************
*
         SPACE 1
DELETE   CSECT
         USING *,R8
         LA    R1,MSGNLIB                ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO            CORRECT?
         BZ    MSGNEW                    YES, BRANCH
         SPACE 2
         NI    FLAGSCC,FF-FBLDLOK        BLDL IS INVALIDATED
         TM    FLAGSII,FSINGLE           IF OR FIND THEN(DELETE)?
         BNO   DEL010                    NO, BRANCH
         MVC   IODIR1+4+12+00(8),CURDIR  RESET READ ADDRESS
         MVI   IODIR1+4+12+08,X'08'      RESET KEY LENGTH
         MVC   IODIR1+4+12+09(2),=H'256' RESET DATA LENGTH
         MVI   IODIR1+4+12+11,0          RESET SECTOR ADDRESS TO ZERO
         MVI   IOECB,X'7F'               LAST READ WAS OK
         B     DEL100                    PROMPT BY DEFAULT
         SPACE 1
DEL010   TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    DEL200                YES, BRANCH
         MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         OI    #DELFLAG,FDELONE      ONLY A SINGLE MEMBER
         TM    #DELFLAG,FDELASS2     DELETE ASSOCIATES TOO?
         BO    DEL200                YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMSTAR      ASTERISK NOTATION?
         BO    DEL110                YES, NO PROMPTING
         TM    FLAGSAA,FOPTIONS      ANY MEMBER NAME?
         BO    DEL110                YES, NO PROMPTING
         SPACE 2
DEL100   LA    R1,MSGDEL             DELETE SINGLE MEMBER
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO -- DONE
         SPACE 1
DEL110   LA    R4,DIRNAME            DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         MVC   DSNMEMQ(8),DIRNAME    MEMBER NAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         B     NEWCMD                YES, IGNORE THE COMMAND
         B     DEL900                NO, DELETE IT
         SPACE 3
DEL200   OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         MVC   MSGTEXT2,MSGDELS      MEMBERS TO BE DELETED
         NI    FLAGSBB,FF-FLINESET   NO LINE IN PROGRESS
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         SPACE 1
DEL210   SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   DEL220                YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     DEL500
         SPACE 1
DEL220   SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
DEL300   BAL   R14,READDIR           GET NEXT DIRECTORY ENTRY
         B     DEL600                END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #DELFLAG,FDELASSP     ASSOCIATES PHASE?
         BO    DEL400                YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMRANG      MEMBER NAME RANGE?
         BNO   DEL340                NO, BRANCH
         TM    FLAGSAA,FMEMBER1      START ENTRY SPECIFIED?
         BNO   DEL330                NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER1          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               ABOVE THIS VALUE?
         BH    DEL300                YES, BRANCH
         XI    FLAGSAA,FMEMBER1      FIRST NAME IS SATISFIED
         SPACE 2
DEL330   TM    FLAGSAA,FMEMBER2      END ENTRY SPECIFIED?
         BNO   DEL500                NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER2(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               BELOW THIS VALUE?
         BL    DEL600                YES, BRANCH
         B     DEL500
         SPACE 3
DEL340   LH    R15,LMEMBER1          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER1    <<EXECUTED>>
DEL350   EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DEL360                YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DEL350             MAYBE, CHECK ALL OTHER POSITIONS
         B     DEL300                NO, IGNORE THIS ENTRY
         SPACE 2
DEL360   TM    FLAGSAA,FMEMBER2      A SECOND PATTERN TOO?
         BNO   DEL500                NO, USE THIS ENTRY
         LH    R15,LMEMBER2          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER2    <<EXECUTED>>
DEL370   EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DEL500                YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DEL370             MAYBE, CHECK ALL OTHER POSITIONS
         B     DEL300                NO, IGNORE THIS ENTRY
         SPACE 3
DEL400   L     R1,#MEMPTR            START OF MEMBER LIST
         B     DEL410+4              SKIP FIRST ADD
DEL410   LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DEL300                YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   DEL410                NO, BRANCH
         SPACE 3
         L     R1,#MEMPTR            START OF MEMBER LIST
         B     DEL430+4              SKIP FIRST ADD
DEL430   LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DEL500                YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    DEL300                YES, IGNORE THIS ENTRY
         B     DEL430                NO, CONTINUE SEARCHING
         SPACE 3
DEL500   MVC   DSNMEMQ(8),MEMNAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         OI    #DELFLAG,FDELENQ      YES, MARK TO EXIT LATER
         SPACE 1
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET      LINE IN PROGRESS?
         BO    DEL510                YES, BRANCH
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
         LH    R1,MSGTEXT2
         LA    R6,MSGTEXT2(R1)       POINT TO END OF MESSAGE
DEL510   CLI   0(R2),X'40'           SCAN
         BNE   *+8                       FOR LAST
         BCT   R2,DEL510                         NON-BLANK
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGTEXT2
         SR    R15,R1
         C     R15,LINESIZE          FIT ON THIS OUTPUT LINE?
         BNH   DEL520                YES, BRANCH
         SR    R6,R1
         STH   R6,MSGTEXT2
         MESSAGE (R1)
         MVC   MSGTEXT2,MSGBLANK
         LH    R1,MSGTEXT2
         LA    R6,MSGTEXT2-1(R1)     INDENT FOR BLANKS
         SPACE 1
DEL520   MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE        MAKE PRINTABLE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','            ADD A COMMA
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   DEL210                NO, BRANCH
         TM    #DELFLAG,FDELONE      A SINGLE MEMBER?
         BNO   DEL300                NO, BRANCH
         SPACE 3
DEL600   TM    FLAGSBB,FLINESET      ANY MEMBERS FOUND?
         BNO   DEL610                NO, BRANCH
         LA    R1,MSGTEXT2
         SR    R6,R1
         BCTR  R6,0
         STH   R6,MSGTEXT2           DELETE THE FINAL COMMA
         MESSAGE (R1)
         MESSAGE MSGBLANK
         SPACE 1
DEL610   TM    #DELFLAG,FDELASSP     ASSOCIATES PHASE STARTED?
         BO    DEL700                YES, BRANCH
         TM    #DELFLAG,FDELASS2     DELETE ASSOCIATE MEMBERS TOO?
         BNO   DEL700                NO, BRANCH
         OI    #DELFLAG,FDELASSP     YES, ASSOCIATES PHASE STARTED
         NI    #DELFLAG,FF-FDELONE   WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         NI    FLAGSBB,FF-FLINESET   NEW OUTPUT LINE
         MVC   MSGTEXT2,MSGDELAS     ASSOCIATED MEMBERS TO BE DELETED:
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     DEL300                DO ANY ASSOCIATED MEMBERS
         SPACE 3
DEL700   NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         TM    #DELFLAG,FDELENQ      ANY MEMBERS IN USE?
         BO    NEWCMD                YES, BRANCH
         SPACE 1
         LA    R1,MSGDELAL           DELETE PROMPT MESSAGE
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO, QUIT
         SPACE 2
DEL900   BAL   R2,OPENSTOW           OPEN STOW DCB; ENQUEUE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 1
         L     R3,#MEMPTR            START OF MEMBER LIST
         B     DEL910+4              SKIP FIRST ADD
DEL910   LA    R3,16(,R3)
         CR    R3,R4
         BNL   DEL980
         STOW  STOWDCB,0(R3),D       ISSUE STOW WITH DELETE OPTION
         SPACE 1
         B     *+4(R15)              PROCESS RETURN CODE
         B     DEL940                  00 - SUCCESSFUL
         EX    0,*                     04 - SHOULD NOT OCCUR
         B     DEL960                  08 - MEMBER NOT FOUND
         B     IOERROR                 12 - I/O ERROR IN DIRECTORY
         SPACE 1
DEL940   MVC   MSGTEXT1,MSGGONE
         MVC   MSGTEXT1+4(8),0(R3)
         TR    MSGTEXT1+4(8),TRLINE
         MESSAGE MSGTEXT1
         B     DEL910
         SPACE 1
DEL960   MVC   MSGTEXT1,MSGNONE
         MVC   MSGTEXT1+4(8),0(R3)
         TR    MSGTEXT1+4(8),TRLINE
         MESSAGE MSGTEXT1
         B     DEL910
         SPACE 1
DEL980   BAL   R2,SHUTSTOW            CLOSE FOR: IF * THEN(DELETE)
         B     NEWCMD
         SPACE 3
*#DELFLAG -- BIT MEANINGS
FDELASSP EQU   X'80'                  ASSOCIATES PHASE OF DELETE
FDELONE  EQU   X'10'                  DELETE A SINGLE MEMBER
FDELENQ  EQU   X'08'                  A DELETE MEMBER IS "IN USE"
FDELASS2 EQU   X'01'                  DELETE ASSOCIATES TOO
         SPACE 1
         PRINT NOGEN
MSGGONE  MSG   '(MEMBER) DELETED'
MSGDELS  MSG   'MEMBERS TO BE DELETED ARE: '
MSGDELAS MSG   'ASSOCIATED MEMBERS TO BE DELETED ARE: '
MSGDEL   MSG   ' DO YOU WANT TO DELETE THIS MEMBER - ENTER Y (FOR YES) X
               OR N (FOR NO):'
MSGDELAL MSG   ' DO YOU WANT TO DELETE ALL THE ABOVE MEMBERS - ENTER Y X
               (FOR YES) OR N (FOR NO):'
         PRINT GEN
         TITLE 'P D S  --  PDS EDIT AND FSE                   02/06/84'
***********************************************************************
***      EDIT AND FSE SUBCOMMANDS                                   ***
***********************************************************************
*
         SPACE
EDIT     CSECT
         USING *,R8
         MVC   DSNMEMQ(8),DIRNAME       MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST              MEMBER IN USE?
         NOP   0                        IF YES, OUTPUT A WARNING ONLY
         CLI   #EDITOLD,X'01'           "NEW" SPECIFIED?
         BNE   EDIT5                    NO, BRANCH
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    EDIT5                    YES, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     EDIT5                      04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
EDIT5    MVC   MSGTEXT2+4(8),##SUBCOM   MOVE IN "EDIT" OR "FSE"
         MVI   MSGTEXT2+12,C''''        ADD A QUOTE
         MVC   MSGTEXT2+13(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R3,MSGTEXT2+12(R15)      POINTER TO CURRENT BYTE
         MVI   1(R3),C'('               MEMBER NAME PAREN
         SR    R5,R5                    LENGTH OF LAST QUALIFIER
         LR    R14,R3                   BACKWARD SCAN POINTER
         SPACE 1
EDIT7    BCTR  R14,0                    SCAN
         LA    R5,1(,R5)                    TO
         CLI   0(R14),C'.'                    PREVIOUS
         BNE   EDIT7                                  PERIOD
         MVC   MSGTEXT1+4(9),1(R14)
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    EDIT8                    YES, BRANCH
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R1,8                     MAXIMUM MACHINE LENGTH+1
         SPACE 1
EDIT7A   BCTR  R3,0                     SCAN
         CLI   0(R3),X'40'                  BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R1,EDIT7A                                      NON-BLANK
         SPACE 1
         BCTR  R1,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R1,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
EDIT8    MVI   1(R3),C''''              ADD A QUOTE
         MVI   2(R3),X'40'              ADD A BLANK
         LA    R3,3(,R3)
         CLI   #EDITNON,1               NONUM CODED?
         BNE   EDIT9                    NO, BRANCH
         SPACE 5
         MVC   0(6,R3),=C'NONUM '       YES
         LA    R3,6(,R3)
EDIT9    CLI   #EDITOLD,1               "NEW" SPECIFIED?
         BNE   EDIT10                   NO, BRANCH
         MVC   0(4,R3),=C'NEW '         YES
         LA    R3,4(,R3)
EDIT10   CLI   #EDITASI,1               ASIS CODED?
         BNE   EDIT11                   NO, BRANCH
         MVC   0(5,R3),=C'ASIS '        YES
         LA    R3,5(,R3)
EDIT11   ICM   R15,B'1111',#EDITTYP     ANY DATA TYPE CODED?
         BZ    EDIT13                   NO, BRANCH
         MH    R15,=H'7'                INDEX  =  OPTIONUM * 7
         LA    R15,EDITTYPE-7(R15)      POINT TO THE PROPER OPTION
         B     EDIT20                   DONE WITH FORMATTING
         SPACE 1
EDIT13   BCTR  R5,0                     MACHINE QUALIFIER LENGTH
         LA    R15,EDITTYPE-7
EDIT15   LA    R15,7(,R15)              TABLE WIDTH IS 7
         CLC   PLIF,0(R15)              END OF DESCRIPTIVE QUALIFIERS?
         BE    EDIT17                   YES, USE "DATA" OR "GOFORT"
         CLC   0(*-*,R15),MSGTEXT1+4    <<EXECUTED>>
         EX    R5,*-6                   THIS QUALIFIER?
         BNE   EDIT15                   NO, TRY THE NEXT ONE
         B     EDIT20                   YES, USE THIS QUALIFIER
         SPACE 1
EDIT17   LA    R15,EDITTYPE             NOT FOUND, ASSUME "DATA"
         CLC   =C'FORT(',MSGTEXT1+4     FORT QUALIFIER?
         BNE   EDIT20
         LA    R15,GOFORT               YES, USE "GOFORT" (EDIT RULES)
         SPACE 1
EDIT20   MVC   0(7,R3),0(R15)           MOVE IN DATA TYPE
         LA    R1,MSGTEXT2
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LA    R3,7(,R3)
         SR    R3,R1                    COMMAND LENGTH
         SLL   R3,16
         ST    R3,MSGTEXT2              SAVE LENGTH OF STRING
*        MESSAGE MSGTEXT2               DELETE * TO OUTPUT THE COMMAND
         MVI   MSGTEXT2+3,8             OPERAND OFFSET
         BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE THE EDIT
         LA    R0,RESTART4              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         SPACE 1
         L     R4,ADDRECT
         MVC   ECTPCMD-ECT(8,R4),##SUBCOM      COMMAND IS EDIT OR FSE
         NI    ECTSWS-ECT(R4),FF-ECTNOPD       WE ALWAYS HAVE OPERANDS
         LA    R1,ADDRTEXT                     THE CPPL FOR EDIT/FSE
         LA    R3,##SUBCOM                     TSO EDIT/FSE COMMAND
         BAL   R2,ATTACH2                      GO ATTACH IT
         BAL   R2,DEALLDCB                     FREE THE ALLOCATED FILE
         MVI   ##ADRCM#,EDITOR                 NO WARNING MESSAGE
         B     RESTART                         ALLOCATE AND OPEN AGAIN
         TITLE 'P D S  --  PDS FIXPDS                         02/06/84'
***********************************************************************
***      FIXPDS SUBCOMMAND     ADDED BY BRUCE LELAND -- NOV., 1982  ***
***********************************************************************
*
         SPACE 1
FIXPDS   CSECT
         USING *,R8
         SPACE 1
         ICM   R1,B'1111',#RECFM       NEW DCB=RECFM SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #RECFM+3(1),RECFM       NO, USE PREVIOUS RECFM
         ICM   R2,B'1111',#LRECL       NEW DCB=LRECL SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #LRECL+2(2),LABLRECL    NO, USE THE LRECL FROM THE DCB
         TM    #RECFM+3,X'C0'          RECFM=U..?
         BNO   *+10                    NO, BRANCH
         XC    #LRECL,#LRECL           YES, USE LRECL=0
         ICM   R3,B'1111',#BLKSI       NEW DCB=BLKSIZE SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #BLKSI+2(2),BLKSI       NO, USE PREVIOUS BLOCKSIZE
         SPACE 2
         TM    #RECFM+3,DCBRECF        RECFM=F OR RECFM=U?
         BNO   FIXPDS10                NO, BRANCH
         TM    #RECFM+3,DCBRECV        RECFM=U?
         BO    FIXPDS10                YES, BRANCH
         SPACE 1
         L     R15,#BLKSI              REQUESTED BLOCKSIZE
         SR    R14,R14
         ICM   R4,B'1111',#LRECL       ANY LRECL?
         BZ    FIXPDS10                NO, AVOID DIVIDE ERROR
         DR    R14,R4                  CHECK LRECL'S PER BLOCK
         LTR   R14,R14                 INTEGRAL NUMBER?
         BZ    FIXPDS10                YES, BRANCH
         MESSAGE MSGDIVER              NO, ERROR MESSAGE
         SPACE 2
FIXPDS10 CLI   #FIXOPT,X'02'           DCB?
         BL    FIXPDS50                YES, BRANCH
         SPACE 1
         MVI   ##SUBCOM,C'U'           TEMPORARY SUBCOMMAND IDENTIFIER
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         BAL   R14,READDIR             NUMBER OF DIRECTORY BLOCKS
         MVI   ##SUBCOM,C'F'           RESET THE SUBCOMMAND NAME
         SPACE 1
         ICM   R3,B'1111',TOTMEMR      REAL (2 BYTES) AND ALIASES (2)
         LH    R6,TOTBLOCK             NUMBER OF DIRECTORY BLOCKS
         LTR   R6,R6                   ANY CURRENT DIRECTORY BLOCKS?
         BP    FIXPDS20                YES, BRANCH
         LA    R1,MSGNEEDB             ASSUME RESET OR EXPAND & NOCOUNT
         CLC   #NEWDIR,ZERO            CORRECT?
         BE    NEWCMD                  YES, ERROR EXIT
         SPACE 1
FIXPDS20 CLI   #FIXOPT,X'02'           EXPANDDIR?
         BE    FIXPDS30                YES, BRANCH
         ICM   R1,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         ST    R6,#NEWDIR              NO, USE CURRENT NUMBER
         SR    R6,R6                   CLEAR CURRENT DIRECTORY NUMBER
         LTR   R3,R3                   ANY REAL OR ALIAS MEMBERS?
         BZ    FIXPDS30                NO, A WARNING IS NOT NEEDED
         MESSAGE MSGWLOSE              YES, ALL MEMBERS WILL BE LOST
         SPACE 1
FIXPDS30 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#EOF1TTR             TTR OF CURRENT DIRECTORY EOF
         SPACE 1
         ICM   R4,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         LA    R4,50                   NO, ADD 50 TO THE CURRENT NUMBER
         AR    R4,R6                   DESIRED + CURRENT DIR. BLOCKS
         ST    R4,#NEWDIR              UPDATE (NOW TOTAL # OF BLOCKS)
         LTR   R3,R3                   ANY REAL OR ALIAS MEMBERS?
         BNZ   *+6                     YES, BRANCH
         SR    R6,R6                   NO, CLEAR DIRECTORY COUNT
         SPACE 1
         LR    R1,R4
         SR    R1,R6                   NUMBER OF BLOCKS TO ADD
         ST    R1,#NEWDIR              UPDATE (NOW NUMBER TO ADD)
         SPACE 1
         L     R14,=X'01080100'        R=1, K=08, DD=256 BYTES
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         ST    R0,#DIRBTRK             SAVE DIRECTORY BLOCKS PER TRACK
         SRDA  R4,32                   TOTAL BLOCKS
         DR    R4,R0                   TOTAL / ENTRIES PER TRACK
         STH   R5,#EOF2TT              TRACK OF NEW DIRECTORY EOF
         L     R2,DCBDEBAD-IHADCB+INDCB  DEB ADDRESS
         LA    R1,MSGDIRXT             ASSUME MULTI-EXTENT DIRECTORY
         CH    R5,32+14(,R2)           CORRECT?
         BNL   MSGNEW                  YES, ERROR
         SPACE 1
         LTR   R4,R4                   PARTIAL TRACK USED?
         BP    FIXPDS32                YES, BRANCH
         MESSAGE MSGADDIR              NO, WARNING MESSAGE
         L     R1,#NEWDIR              ONE ADDITIONAL
         A     R1,=F'1'                       DIRECTORY
         ST    R1,#NEWDIR                           BLOCK USED
         SPACE 2
FIXPDS32 LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         OI    ##ADRCM#,@S             FORCE SINGLE BUFFERING NOW
         USING DIRLINK,R3              NO, BRANCH
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
FIXPDS40 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     FIXPDS50                LAST MEMBER, BRANCH
         SPACE 1
         CLI   #FIXOPT,X'03'           RESET DIRECTORY?
         BE    FIXPDS42                YES, BRANCH
         CLC   #EOF2TT(2),MEMTTR       IN THE NEW DIRECTORY AREA?
         BL    FIXPDS40                NO, BRANCH
         MVI   SUBPOOLT,21             MARK TEMPORARY SUBPOOL IN USE
         LA    R0,DIREND-DIRLINK       GET ANOTHER DIRECTORY ENTRY
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,DIRLINK              CHAIN TO THE
         LR    R3,R1                               NEXT ENTRY
         L     R14,DIRPTRS             ACTUAL DIRECTORY ENTRY
         MVC   DIRNAME(74),0(R14)      ADD THE DIRECTORY INFORMATION
         MVC   MSGTEXT1(80),MSGEMOVE   MEMBER MOVE MESSAGE
         MVC   MSGTEXT1+4(8),DIRNAME
         TR    MSGTEXT1+4(8),TRLINE
         MESSAGE MSGTEXT1
         SPACE 2
FIXPDS42 MVC   DSNMEMQ(8),MEMNAME      MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     FIXPDS40                YES, OUTPUT WARNING MESSAGE ONLY
         B     FIXPDS40                REPEAT FOR ALL MEMBERS
         SPACE 2
FIXPDS50 MESSAGE MSGBLANK              ONE SPACING LINE
         BAL   R2,DSN                  DISPLAY ALLOCATION STATUS
         LA    R1,MSGDSNOK             CORRECT DATA SET
         BAL   R2,YESNO                RESPONSE "YES" OR "NO"?
         B     NEWCMD                  "NO", QUIT
         SPACE 1
         MVI   #DIRFIX,X'F9'           HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),FIXLIT     TEMPORARY STOW AND DELETE NAME
         BAL   R6,RESERVE              RESERVE/ENQUE AS REQUIRED
         L     R4,#NEWDIR              DIRECTORY BLOCKS DESIRED
         CLI   #FIXOPT,X'02'           WHICH PROCESSING MODE?
         BL    FIXDCB                    DCB       -- CHANGE DCB ONLY
***      BE    EXPAND                    EXPANDDIR -- ADD TO DIRECTORY
         BH    FIXRES                    RESETDIR  -- REWRITE DIRECTORY
         SPACE 3
EXPAND   ICM   R1,B'1111',TOTMEMR      ANY REAL OR ALIAS MEMBERS?
         BZ    FIXRES                  NO, USE RESETDIR INSTEAD
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAL   R2,OPENSTOW             OPEN OUTPUT DCB; RESERVE IS DONE
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         CLC   #MEMPTR(4),ZERO         ANY MEMBERS TO MOVE?
         BE    EXPAND80                NO, BRANCH
         LA    R0,80                   FIRST RECORD IS 80 BYTES LONG
         SPACE 2
*  ONE OR MORE MEMBERS MUST BE MOVED
*    WRITE GARBAGE RECORDS TO OCCUPY SPACE UP TO THE NEW DIRECTORY EOF
EXPAND02 STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,IOBUFF1              START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         LH    R0,BLKSI                MAXIMUM OUTPUT RECORD LENGTH
         CLM   R1,B'1100',#EOF2TT      PAST THE NEW DIRECTORY EOF?
         BNH   EXPAND02                NO, BRANCH
         ST    R1,#FIXTTRO             HIGHEST OUTPUT TTR
         LA    R3,#MEMPTR
         SPACE 1
EXPAND12 ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    EXPAND20                END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   EXPAND18                NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
EXPAND14 ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    EXPAND18                      NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    EXPAND14                      YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   EXPAND14                      NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    EXPAND16                      YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    EXPAND14                      YES, BRANCH
         SPACE 1
EXPAND16 MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
EXPAND18 TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    EXPAND12                      NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     EXPAND12
         SPACE 3
EXPAND20 LA    R3,#MEMPTR
EXPAND22 ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    EXPAND80                NO, BRANCH
         MVC   MSGTEXT1(80),MSGEMOVN   MEMBER BEING MOVED MESSAGE
         MVC   MSGTEXT1+4(8),DIRNAME   MEMBER NAME
         TR    MSGTEXT1+4(8),TRLINE
         MESSAGE MSGTEXT1
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   EXPAND22                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND24 EX    R5,*-4                  ANOTHER TTR?
         BZ    EXPAND30                NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    EXPAND26                NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         BAL   R14,EXCP                INPUT IT
         LA    R1,MSGNOTEF             ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
EXPAND26 LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,EXPAND24
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
EXPAND30 MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         SPACE 2
EXPAND32 BAL   R14,EXCP
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND34                  00 - GOOD READ
         B     EXPAND60                  04 - END OF MEMBER
         B     EXPAND60                  08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
EXPAND34 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND50                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND36 EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    EXPAND38                NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    EXPAND40                YES, BRANCH
EXPAND38 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,EXPAND36
         B     EXPAND50                NOT THIS RECORD, BRANCH
         SPACE 2
EXPAND40 SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,MSGNOTEU             ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
EXPAND42 TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,EXPAND42             MOVE AND CHECK ALL ENTRIES
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
EXPAND50 L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND32                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND52 EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   EXPAND54                NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   EXPAND54                NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND54 EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   EXPAND58                NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
EXPAND56 CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,EXPAND56
EXPAND58 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,EXPAND52
         B     EXPAND32               MOVE ALL MEMBERS
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
EXPAND60 LA    R1,MSGTTRUP             ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   EXPAND62                NO, BRANCH
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         LA    R1,MSGNODEL
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,MSGNOADD
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
EXPAND62 TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO REAL)?
         BNO   EXPAND70                NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND70                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
EXPAND70 LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
EXPAND72 MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND74                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 1
EXPAND74 ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    EXPAND22                       END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   EXPAND74                       NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    EXPAND74                       YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
EXPAND76 EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   EXPAND72                       NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,EXPAND76
         B     EXPAND72
         DROP  R3
         SPACE 3
*  WRITE THE ADDED DIRECTORY RECORDS
EXPAND80 L     R6,#NEWDIR                     DIRECTORY BLOCKS TO ADD
         LA    R0,CCW1                        FIRST CCW
         ST    R0,IOBCCW                               TO USE
         MVI   CCW3,X'1D'                     COMMAND FOR WRITE C,K,D
         LA    R0,MSGTEXT1+3                  STORAGE FOR
         STCM  R0,B'0111',CCW3+1                         WRITE ADDRESS
         NI    CCW3+4,FF-X'40'                LAST CCW IN CHAIN
         MVC   CCW3+6(2),=H'8'                DATA LENGTH IS EIGHT
         XC    MSGTEXT1(20),MSGTEXT1          CLEAR THE WRITE ADDRESS
         MVI   MSGTEXT1+8,8                   KEY IS EIGHT BYTES
         MVI   MSGTEXT1+9,1                   DATA IS 1*256 BYTES
         MVI   OPENLIST,X'80'
         CLOSE (INDCB),MF=(E,OPENLIST)
         OPEN  (INDCB,UPDAT),MF=(E,OPENLIST)
         SPACE 2
EXPAND82 L     R0,#EOF1TTR                    POINT TO LAST RECORD
         STM   R15,R12,12+4(R13)              CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)     DEB ADDRESS
         LA    R2,MSGTEXT1                    RETURN CCHHR
         L     R15,ADDRCNVT                   TTR TO MBBCCHHR CONVERT
         LR    R3,R13                         SAVE AREA ADDRESS
         BALR  R14,R15
         LR    R13,R3                         SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)              RESTORE REGISTERS
         MVC   IOBSEEK(8),MSGTEXT1
         SR    R1,R1
         IC    R1,IOBSEEK+7
         S     R1,=F'1'                       REDUCE INITIAL TTR
         STC   R1,IOBSEEK+7
         MVI   IOECB,0
         EXCP  IOB                            WRITE THIS RECORD
         WAIT  ECB=IOECB                      WAIT FOR I/O COMPLETION
         LA    R1,MSGIODIR                    I/O ERROR IN DIRECTORY
         CLI   IOECB,X'7F'                    SUCCESSFUL WRITE?
         BNE   EXIT8M                         NO, ERROR
         SR    R1,R1
         IC    R1,#EOF1TTR+2                  INCREMENT
         A     R1,=F'1'                                R OF
         STC   R1,#EOF1TTR+2                               TTR
         C     R1,#DIRBTRK                    FIT ON THIS TRACK?
         BNH   EXPAND84                       YES, BRANCH
         LH    R1,#EOF1TTR                    INCREMENT
         A     R1,=F'1'                                TT OF
         STH   R1,#EOF1TTR                                  TTR
         MVI   #EOF1TTR+2,1                   RESET R OF TTR
         SPACE 1
EXPAND84 S     R6,=F'1'                       MORE BLOCKS?
         BP    EXPAND82                       YES, BRANCH
         SPACE 2
*  WRITE THE DATA SET END-OF-FILE MARKER
         OC    MSGTEXT1+8(3),MSGTEXT1+8       END-OF-FILE WRITTEN?
         BZ    FIXDCB                         YES, BRANCH
         XC    MSGTEXT1+8(3),MSGTEXT1+8       SET KEY, DATA LENGTH TO 0
         B     EXPAND82                       ONE MORE LOOP
         SPACE 3
FIXRES   BAL   R2,CLOSEIT                        CLOSE INPUT DATA SET
         LA    R0,CHANGE2                        ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   BAMDCB(LDIRDCB),DIRDCB            COPY PROTTYPE DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB,DDNAME     CURRENT DDNAME
         MVI   OPENLIST,X'80'                    END OF LIST MARKER
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  DIRECTORY OPEN?
         BNO   EXIT12O                       NO, QUIT
         MVC   MSGTEXT1(18),KEYDATA          MOVE IN THE KEY AND DATA
         XC    MSGTEXT1+18(256),MSGTEXT1+18  FOLLOWED BY ZEROES
         SPACE 1
FIXRES30 WRITE FIXPDSD,SF,BAMDCB,MSGTEXT1,'S',MF=E
         CHECK FIXPDSD                 CHECK THIS OUTPUT OPERATION
         XC    MSGTEXT1(18),MSGTEXT1   ZERO THE KEY AND DATA PORTION
         S     R4,=F'1'                SUFFICIENT BLOCKS WRITTEN?
         BP    FIXRES30                NO, BRANCH
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         SPACE 3
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN        MARK FOR DCB OPEN EXIT USE
         SPACE 2
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 1
         STOW  STOWDCB,#DIRFIX,A       ADD A NULL MEMBER
         LA    R1,MSGNOADD             NOT ADDED MESSAGE
         LTR   R15,R15                 ADDED?
         BP    EXIT8M                  NO, QUIT
         STOW  STOWDCB,#DIRFIX,D       DELETE THE DUMMY MEMBER
         LA    R1,MSGNODEL             NOT DELETED MESSAGE
         LTR   R15,R15                 DELETED?
         BP    EXIT8M                  NO, QUIT
         B     FIXDCB20                YES, CLOSE AND END
         SPACE 3
FIXDCB   BAL   R2,CLOSEIT              CLOSE THE INPUT DCB
         LA    R0,CHANGE2              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         BAL   R6,RESERVE
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 2
FIXDCB20 CLOSE (STOWDCB),MF=(E,OPENLIST)
         MESSAGE MSGBLANK              ONE SPACING LINE
         LA    R1,DSN                  ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD             EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE       FLAG TO CONTINUE
         BAL   R2,DEALLDCB             FREE THE DATA SET
         B     RESTART                 REALLOCATE AND START OVER
         SPACE 2
         PRINT NOGEN
MSGNOTEU MSG   'A NOTELIST POINTER WAS NOT FOUND IN THIS MEMBER'
MSGEMOVE MSG   '(MEMBER) WILL BE MOVED'
MSGEMOVN MSG   '(MEMBER) IS BEING MOVED'
MSGDSNOK MSG   ' VERIFY THAT YOU WISH TO MODIFY THIS DATA SET - ENTER YX
                (FOR YES) OR N (FOR NO):'
MSGADDIR MSG   'NOTE: ONE EXTRA PADDING DIRECTORY BLOCK WILL BE ADDED'
MSGWLOSE MSG   'NOTE: ALL MEMBERS IN THIS PDS WILL BE LOST'
MSGNOTEF MSG   'A NOTELIST RECORD COULD NOT BE INPUT FOR THIS MEMBER'
MSGNEEDB MSG   'THE NUMBER OF DESIRED DIRECTORY BLOCKS MUST BE SPECIFIEX
               D'
         PRINT GEN
         TITLE 'P D S  --  PDS HELP                           02/06/84'
***********************************************************************
***      HELP  SUBCOMMAND      ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
HELP     CSECT
         USING *,R8
         MVC   DDNAMEH,DDNAME        SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP        DDNAME TO FREE
         BAL   R2,DEALLOCZ           FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH        RESTORE ACTUAL DDNAME
         L     R2,ADDRCBUF           INPUT BUFFER ADDRESS
         L     R1,ADDRCPPL
         USING CPPL,R1               ACTUAL ADDRESS
         ST    R2,CPPLCBUF           SET COMMAND ADDRESS
         DROP  R1
         LA    R3,HELPLIT            TSO HELP COMMAND
         BAL   R2,ATTACH             GO ATTACH IT
         B     NEWSTAX               NEXT COMMAND
         SPACE 2
*  NOTE: THE SYSHELP FILE IS FREED BEFORE THE HELP
*        CALL IN CASE A PCF EXIT ALREADY REQUESTED HELP.
         TITLE 'P D S  --  PDS HISTORY                        02/06/84'
***********************************************************************
***      HISTORY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
HISTORY  CSECT
         USING *,R8
         LA    R1,MSGNLIB          ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    HIST020             YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1,MSGSPFHD
         TM    FLAGSAA,FHEAD       HEADER WRITTEN YET?
         BO    HIST000             YES, BRANCH
         MESSAGE MSGTEXT1
         OI    FLAGSAA,FHEAD       NOW IT HAS BEEN WRITTEN
         SPACE 1
HIST000  MVC   MSGTEXT1+5(80),MSGTEXT1+4
         MVC   MSGTEXT1+6(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   MSGTEXT1+6+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   HIST010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   HIST010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   HIST010             NO, BRANCH
         SPACE 1
         LA    R2,MSGTEXT1
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         LA    R1,MSGTEXT1
         B     MSGNEW                  NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
HIST010  LA    R1,MSGTEXT1
         CLC   ZERO,0(R2)              ZERO?
         BE    MSGNEW                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MSGNEW                  YES, NO SSI
         SPACE 1
         MVC   MSGTEXT1+6+8+6(4),=C'SSI:'
         UNPK  MSGTEXT1+6+8+6+5(9),0(5,R2)
         TR    MSGTEXT1+6+8+6+5(8),TRTABLE
         MVI   MSGTEXT1+6+8+6+5+8,X'40'
         B     MSGNEW
         SPACE 3
HIST020  OC    #ZAPOPT(4),#ZAPOPT      ANY ZAP, USER, TRAN OR LKED?
         BNZ   *+10                    YES, BRANCH
         OC    #ZAPOPT(4),=X'01010101' NO, DEFAULT TO DO THEM ALL
         SPACE 1
         L     R15,=A(READESD)     FORMAT THE ESD DATA
         BALR  R14,R15             ANY ESD DATA?
         NOP   0                   NO, CONTINUE ANYWAY
         SPACE 2
         L     R15,=A(READIDR)     FORMAT THE IDR DATA
         BALR  R14,R15             IS IDR DATA AVAILABLE?
         B     NOHIST              NO, MESSAGE AND QUIT
         B     HISTTRN$            YES, CONTINUE
         SPACE 1
NOHIST   SR    R6,R6
         BAL   R4,HISTHDR
         LA    R1,MSGNHIST
         B     MSGNEW
         SPACE 3
HISTHDR  TM    FLAGSAA,FINDMSG     NEED A GROUP HEADER?
         BNO   HISTHDR2            NO, BRANCH
         NI    FLAGSAA,FF-FINDMSG
         MESSAGE MSGBLANK
         MESSAGE FINDMEMQ
HISTHDR2 TM    FLAGSCC,F1IDR       FIRST IDR RECORD?
         BZR   R4                  NO, RETURN
         NI    FLAGSCC,FF-F1IDR
         LTR   R1,R6               ANY SECOND HEADER?
         BZR   R4                  NO, RETURN
         MESSAGE (R1)              INDIVIDUAL HISTORY TITLE
         BR    R4
         EJECT
*
*        FORMAT TRANSLATOR IDR DATA ENTRIES
*
HISTTRN$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         SPACE 2
HISTTRN  ICM   R3,B'1111',IDRLINK  END OF ESD CHAIN?
         BZ    HISTZAP$            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTTRN             NO, BRANCH
         SPACE
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
         SPACE 2
HISTTRN1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTTRN             YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTTRN1            NO, SKIP THIS
         SPACE
         CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTTRN1            NO, BRANCH
         SPACE
         LA    R6,MSGHISTT
         BAL   R4,HISTHDR
         CLEAR MSGTEXT1
         SPACE
         MVC   MSGTEXT1+4(8),ESDNAME
         MVC   MSGTEXT1+12(9),DATEMASK
         ED    MSGTEXT1+12(9),IDRDATE
         MVC   MSGTEXT1+24(10),IDRTDATA   TRANSLATOR 1
         UNPK  DOUBLE(5),IDRTDATA+10(3)   VVMM PACKED
         MVI   MSGTEXT1+36,C'V'
         MVC   MSGTEXT1+36+1(2),DOUBLE    VV
         MVI   MSGTEXT1+40,C'M'
         MVC   MSGTEXT1+40+1(2),DOUBLE+2  MM
         CLI   IDRLDATA,15                TWO TRANSLATORS?
         BL    HISTTRN3                   NO, BRANCH
         SPACE 2
         OI    IDRTDATA+27+2,X'0F'        FIX THE SIGN FIELD
         LA    R15,FULLWORD               OUTPUT LOCATION
         LA    R1,IDRTDATA+27             SECOND YYDDD
         BAL   R14,CONVDATE               CONVERT TO MMDDYY FORMAT
         MVC   MSGTEXT1+48(9),DATEMASK
         ED    MSGTEXT1+48(9),FULLWORD
         MVC   MSGTEXT1+60(10),IDRTDATA+15 TRANSLATOR 2
         UNPK  DOUBLE(5),IDRTDATA+25(3)   VVMM PACKED
         MVI   MSGTEXT1+72,C'V'
         MVC   MSGTEXT1+72+1(2),DOUBLE    VV
         MVI   MSGTEXT1+76,C'M'
         MVC   MSGTEXT1+76+1(2),DOUBLE+2  MM
         SPACE 1
HISTTRN3 LA    R0,80               MAXIMUM LENGTH OF MESSAGE
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         B     HISTTRN1            NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 3
*
*        FORMAT AMASPZAP IDR DATA ENTRIES
*
HISTZAP$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         SPACE
HISTZAP  ICM   R3,B'1111',IDRLINK  END OF ESD CHAIN?
         BZ    HISTUSR$            YES, BRANCH
         CLI   IDRTYPE,IDRZAP      AMASPZAP ENTRY?
         BNE   HISTZAP             NO, BRANCH
         SPACE
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
         SPACE
HISTZAP1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTZAP             YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTZAP1            NO, SKIP THIS
         SPACE
         CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTZAP1            NO, BRANCH
         SPACE
         LA    R6,MSGHISTZ
         BAL   R4,HISTHDR
         CLEAR MSGTEXT1
         SPACE
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         SPACE
         LA    R0,L'ESDNAME+L'DATEMASK+L'IDRZDATA+12
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         B     HISTZAP1            NEXT IDR DATA RECORD THIS ESD ENTRY
         EJECT
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE
HISTUSR$ LA    R3,#IDRPTR
         OI    FLAGSCC,F1IDR
         SPACE
HISTUSR  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTLKD$            YES, BRANCH
         CLI   IDRTYPE,IDRUSER     USER IDR DATA RECORD?
         BNE   HISTUSR             NO, BRANCH
         SPACE
         LA    R2,#ESDPTR          ADDRESS OF ESD CHAIN
         SPACE
HISTUSR1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTUSR             YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTUSR1            NO, BRANCH
         CLC   ESDID,IDRESDID      WANTED ESD RECORD?
         BNE   HISTUSR1            NO, BRANCH
         SPACE 1
         LA    R6,MSGHISTU
         BAL   R4,HISTHDR
         CLEAR MSGTEXT1
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         IC    R1,IDRLDATA
         CLI   IDRLDATA,X'FF'      ZERO BYTES?
         BE    HISTUSRS            YES, SKIP THE MOVE (LENGTH ZERO)
         SPACE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(*-*),IDRDATA  <<EXEC>>
         EX    R1,*-6
         B     HISTUSRS+2          LENGTH IN R1 IS VALID
         SPACE 1
HISTUSRS SR    R1,R1               R1 LENGTH IS NOW ZERO
         LA    R1,13+L'ESDNAME+L'DATEMASK(R1)
         SLL   R1,16
         ST    R1,MSGTEXT1
         MESSAGE MSGTEXT1
         B     HISTUSR1
         DROP  R2,R3
         EJECT
*
*        FORMAT THE LINKAGE-EDITOR IDR DATA RECORD
*
HISTLKD$ CLI   #LKEDOPT,X'01'           "LKED" REQUESTED?
         BNE   NEWCMD                   NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN      ANY LKED NAME CHECK?
         BZ    HISTLKDG                 NO, BRANCH
         BCTR  R1,0                     MACHINE LENGTH
         CLC   #LKEDTXT(*-*),LKEDNAME   <<EXECUTED>>
         EX    R1,*-6                   THIS LINKAGE EDITOR?
         BNE   NEWCMD                   NO, BRANCH
HISTLKDG SR    R6,R6
         BAL   R4,HISTHDR
         MVC   MSGTEXT1,MSGHISTD        LAST LINKEDIT DATE MSG
         ED    MSGTEXT1+DATEMASK-MSGHISTD(L'DATEMASK),LKEDDATE
         LH    R1,MSGTEXT1              CURRENT LENGTH
         LA    R15,MSGTEXT1(R1)         END OF CURRENT MESSAGE
         MVC   0(10,R15),=CL10'  BY LKED'
         MVC   10(10,R15),LKEDNAME
         MVC   10+10(2,R15),=C' V'
         UNPK  10+10+2(3,R15),LKEDVVMM(2)
         MVC   10+10+2+2(2,R15),=C' M'
         UNPK  10+10+2+2+2(3,R15),LKEDVVMM+2(2)
         LA    R1,10+10+2+2+2+2(,R1)
         STH   R1,MSGTEXT1
         SPACE
         MESSAGE MSGTEXT1
         B     NEWCMD
         SPACE 2
         PRINT NOGEN
MSGNHIST MSG   'NO HISTORY AVAILABLE'
MSGHISTZ MSG   'AMASPZAP UPDATE HISTORY BY CSECT -'
MSGHISTT MSG   'TRANSLATOR HISTORY BY CSECT -'
MSGHISTU MSG   'USER-SUPPLIED UPDATE HISTORY BY CSECT -'
         PRINT GEN
         TITLE 'P D S  --  PDS HISTORY IDR SCAN SUBROUTINE    02/06/84'
*
*        IDR SCAN SUBROUTINE
*
         SPACE 3
READIDR  STM   R14,R12,ESDIDRSV    SAVE REGISTERS
         LR    R8,R15              BASE FOR THIS SUBROUTINE
         USING READIDR,R8
         NI    FLAGSCC,255-FIDR    IDR DATA DOES NOT EXIST YET
         LA    R6,IDRNORML         BRANCH ADDRESS IF NOT CONTINUED
         LA    R2,#IDRPTR          BASE FOR IDR LIST
         USING IDRENTRY,R2
         MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
IDREXCP  BAL   R14,EXCP
         B     *+4(R15)            PROCESS RETURN CODE
         B     IDREXCP1              00 - GOOD READ
         B     LASTIDR               04 - END OF MEMBER
         B     LASTIDR               08 - END OF DATA SET
         B     NEWCMD                12 - I/O ERROR
         SPACE
IDREXCP1 LR    R15,R0              START OF RECORD
         TM    0(R15),X'50'        TEST SYM OR SCATTER RECORD?
         BM    IDREXCP             YES, SKIP IT
         SPACE
         CLI   0(R15),X'20'        CESD RECORD?
         BE    IDREXCP             YES, SKIP
         SPACE
         CLI   0(R15),X'80'        IDR RECORD?
         BNE   LASTIDR             NO, BRANCH
         SPACE
         LA    R3,3(,R15)          START OF IDR DATA
         SR    R5,R5
         IC    R5,1(,R15)          BYTE COUNT THIS RECORD
         LA    R5,0(R5,R15)        END OF BUFFER ADDRESS
         BR    R6                  BRANCH TO IDRNORML OR AS CONTINUED
         SPACE 2
IDRNORML TM    2(R15),IDRLKED      LINKAGE EDITOR IDR RECORD?
         BO    LKEDIDR             YES, BRANCH
         TM    2(R15),IDRTRAN      TRANSLATOR IDR RECORD?
         BO    TRANIDR             YES, BRANCH
         TM    2(R15),IDRZAP       AMASPZAP IDR RECORD?
         BO    ZAPIDR              YES, BRANCH
         TM    2(R15),IDRUSER      USER-SUPPLIED IDR RECORD?
         BNO   NEXTIDR             NO, NEXT RECORD
         EJECT
*  USER-SUPPLIED IDR RECORDS:
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
*   0 - 1    2 - 4            5               6 - 6+COUNT          *
*   ESDID    IDENTIFY DATE    COUNT (0-40)    IDRDATA              *
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
         SPACE
USERIDR  CLI   #USEROPT,X'01'      "USER"?
         BNE   NEXTIDR             NO, BRANCH
         BAL   R14,GETIDR          GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1               ADDRESS OF NEW RECORD
         SPACE 2
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH MOVED
         CH    R4,=H'5'            CONTINUED USER HEADER?
         BNL   USER20              NO, BRANCH
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R4,*-6              MOVE IN DATA
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         LA    R14,IDRUPREF+1(R4)  TARGET OF MOVE
         LA    R15,5-1(,R3)        LENGTH BYTE ADDRESS-1 IF NOT SPLIT
         SR    R15,R4              AMOUNT WE ALREADY HAVE
         SR    R1,R1
         IC    R1,0(,R15)          LENGTH BYTE
         LA    R15,6-2(,R1)        MACHINE LENGTH OF RECORD -1
         SR    R15,R4              MACHINE LENGTH TO MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         B     USER30
         SPACE 2
USER20   SR    R15,R15
         IC    R15,5(,R3)          LENGTH OF USER DATA AREA
         LA    R15,5(,R15)         MACHINE LENGTH FOR MOVE
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         SPACE 1
USER30   LA    R4,1(,R15)          ACTUAL RECORD LENGTH
         MVI   IDRTYPE,IDRUSER     USER IDR DATA IDENTIFIER
         MVC   IDRESDID,IDRUPREF   ADD THE CSECT ID
         LA    R1,IDRUPREF+2       YYDDD IDR RECORD CREATED
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SR    R15,R15
         IC    R15,IDRUPREF+5      LENGTH OF USER DATA AREA
         BCTR  R15,0
         STC   R15,IDRLDATA
         SPACE 2
USER40   BXLE  R3,R4,USERIDR
         EJECT
         SR    R3,R5
         BCTR  R3,0                ACTUAL REMAINING LENGTH
         LTR   R4,R3               CONTINUED DATA PORTION?
         BNP   NEXTIDR             NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRDATA+1(R14)  END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R4,1(,R4)           ACTUAL LENGTH OF LAST PORTION
         B     USER40
         SPACE 4
*  AMASPZAP IDR RECORDS:
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
*  0 - 1    2 - 4       5 - 12                                      *
*  ESDID    ZAP DATE    IDRDATA OR USER IDENTIFICATION              *
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
         SPACE
ZAPIDR   CLI   #ZAPOPT,X'01'       "ZAP"?
         BNE   NEXTIDR             NO, BRANCH
         SR    R4,R4
         NI    0(R3),X'3F'
         IC    R4,0(R3)            GET COUNT OF AMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R4,1(R4)            JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  BAL   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         MVC   IDRESDID,0(R3)      MOVE ESDID TO IDR RECORD
         LA    R1,2(R3)            ADDRESS OF DATE OF RECORD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SPACE
         MVC   IDRDATA,5(R3)       MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8          SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP      INDICATE AMASPZAP ENTRY
         LA    R3,13(R3)           JUMP DATA ADDRESS
         SPACE 1
ZAPIDR1  BCT   R4,ZAPIDR2          IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR             READ NEXT IDR RECORD
         EJECT
*  TRANSLATOR IDR RECORDS:
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*  0 - 1   2 - 3   N - N+1            0    (0 IS 1 TRANSLATOR)  
*  ESDID   ...     X'80'+LAST ESDID   FLAG (1 IS 2 TRANSLATORS) 
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
*    1 - 10        11 - 12    13 -15       16 - 30                 *
*    TRANSLATOR    VVMM       TRANS DATE   OPTIONAL, SAME AS 1-15  *
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
         SPACE
TRANIDR  CLI   #TRANOPT,X'01'      "TRAN"?
         BNE   NEXTIDR             NO, BRANCH
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH OK?
         BM    NEXTIDR             NO, DONE WITH THIS RECORD
         BAL   R14,GETIDR
         ST    R1,IDRLINK          CHAIN ON A
         LR    R2,R1                         NEW IDR RECORD
         SPACE 1
         MVI   IDRTYPE,IDRTRAN     TRANSLATOR TYPE RECORD
         LTR   R4,R4               MACHINE LENGTH OK?
         BP    TRAN20              YES, NOT SPLIT
         MVC   IDRESDID(1),0(R3)   NO, ONLY ONE ESDID BYTE
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         MVC   IDRESDID+1(1),0(R3) SECOND BYTE OF ESDID
         BCTR  R3,0                BACK UP THE INPUT POINTER BY ONE
         B     TRAN30
         SPACE 2
TRAN20   MVC   IDRESDID(2),0(R3)   ESDID FIELD
         SPACE 1
TRAN30   MVC   IDRUPREF(2),IDRESDID  ORIGINAL ESDID
         NI    IDRESDID,FF-X'80'   TURN OFF THE HIGH-ORDER BIT
         TM    IDRUPREF,X'80'      LAST ESDID?
         BO    TRAN50              YES, BRANCH
         LA    R4,2                ESDID WIDTH
         BXLE  R3,R4,TRANIDR       CONTINUE FOR ALL ESDID'S
         B     NEXTIDR             GET THE NEXT RECORD
         SPACE 1
TRAN50   LA    R3,2(,R3)
         LR    R4,R5
         SR    R4,R3               FLAG BYTE INPUT?
         BNM   TRAN60              YES, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
TRAN60   LA    R1,14               ASSUME ONE TRANSLATOR
         CLI   0(R3),0             CORRECT?
         BE    *+8                 YES, BRANCH
         LA    R1,29               NO, TWO TRANSLATORS
         MVC   IDRTDATA(30),1(R3)  MOVE IN THE MAXIMUM LENGTH
         STC   R1,IDRLDATA         MACHINE LENGTH
         LA    R4,2(,R1)           ACTUAL SEGMENT LENGTH
TRAN70   BXLE  R3,R4,TRAN80
         EJECT
         LR    R4,R3
         SR    R4,R5               BYTES LEFT +1
         S     R4,=F'1'            ANY ACTUAL REMAINING BYTES?
         BNP   TRAN80              NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRTDATA+1(R14) END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R3,1(R3,R4)         POSITION FOR THE NEXT SEGMENT
         SPACE 2
TRAN80   LA    R1,IDRTDATA+12      ADDRESS OF CREATION YYDDD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         LA    R1,#IDRPTR          SCAN IDR CHAIN
         SPACE 1
TRAN90   ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)
         BZ    TRANIDR                        DONE, BRANCH
         CLI   IDRTYPE-IDRENTRY(R1),IDRTRAN   TRANSLATOR TYPE?
         BNE   TRAN90                         NO, BRANCH
         CLI   IDRDATE-IDRENTRY(R1),0         ANY DATE YET?
         BNE   TRAN90                         YES, BRANCH
         MVC   IDRDATE-IDRENTRY(3+6+1+30,R1),IDRDATE
         B     TRAN90
         DROP  R2
         SPACE 3
*  LINKAGE EDITOR IDR RECORDS:
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
*  0 - 9                  10 - 11    12 - 14                        *
*  LINKAGE EDITOR NAME    VVMM       LINKAGE EDIT DATE              *
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
         SPACE
LKEDIDR  OI    FLAGSCC,FIDR
         LA    R1,12(R3)           ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         MVC   LKEDNAME(10),0(R3)  LINKAGE EDITOR NAME
         MVC   LKEDVVMM(2),10(R3)  VV.MM IN HEX
         SPACE 2
NEXTIDR  LA    R6,IDRNORML         NOT A CONTINUED RECORD
         SPACE 1
IDRCONT  B     IDREXCP             CONTINUED RECORD
         EJECT
LASTIDR  MVC   RLDCOUNT(1),3(R15)    SAVE THE FIRST RLD/CONTROL AMOUNT
         OC    #ZAPLEN(3),#ZAPLEN    ANY IDR FILTERING?
         BZ    LASTIDRZ              NO, BRANCH
         SPACE 2
         USING IDRENTRY,R3
         SR    R4,R4
         LA    R3,#IDRPTR            SCAN IDR CHAIN
IDRFIL10 LR    R2,R3                 PREVIOUS ENTRY
         ICM   R3,B'0111',IDRLINK+1  CURRENT ENTRY?
         BZ    LASTIDRZ              NO, BRANCH
         ICM   R4,B'0111',IDRLINK+1  NEXT ENTRY
         SPACE 2
         SR    R15,R15               COMPARE STRING LENGTH
         CLI   IDRTYPE,IDRTRAN       IDR TYPE:
         BH    IDRFIL40                USER, BRANCH
         BL    IDRFIL30                ZAP, BRANCH
         LA    R1,#TRANTXT             TRANSLATOR
         SPACE 1
         ICM   R15,B'0001',#TRANLEN  ANY TRANSLATOR FILTERING?
         BZ    IDRFIL10              NO, BRANCH
         CLI   IDRLDATA,15           ONE TRANSLATOR ONLY?
         BL    IDRFIL60              YES, BRANCH
         BCTR  R15,0                 COMPARE MACHINE LENGTH
         CLC   IDRTDATA+15(*-*),#TRANTXT  <<EXECUTED>>
         EX    R15,*-6                    THIS PL/S TRANSLATOR?
         BE    IDRFIL10                   YES, BRANCH
         IC    R15,#TRANLEN          COMPARE STRING LENGTH (AGAIN)
         B     IDRFIL60              CHECK THE FIRST TRANSLATOR TOO
         SPACE 2
IDRFIL30 IC    R15,#ZAPLEN           ZAP IDR RECORD
         LA    R1,#ZAPTXT
         B     IDRFIL60
         SPACE 2
IDRFIL40 IC    R15,#USERLEN          USER IDR RECORD
         LA    R1,#USERTXT
         SPACE 2
IDRFIL60 S     R15,=F'1'             VALID MACHINE LENGTH?
         BM    IDRFIL10              NO, BRANCH
         SPACE 1
         CLC   IDRDATA(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6               PARTIAL NAME MATCH?
         BE    IDRFIL10              YES, BRANCH
         SPACE 1
         LR    R3,R2                 NO, UNCHAIN THE
         ST    R4,IDRLINK                           CURRENT ENTRY
         B     IDRFIL10
         EJECT
LASTIDRZ LA    R3,#IDRPTR          PERFORM AN IDR RECORD SORT
         SPACE 2
SORTIDR  ICM   R3,B'1111',IDRLINK
         BZ    SORTIDRZ
         LR    R1,R3
         SPACE 1
SORTIDR2 ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)    GET THE NEXT ENTRY
         BZ    SORTIDR
         SPACE 1
         ICM   R0,B'1110',IDRDATE   ==> SORT BY DATE (DESCENDING ORDER)
         CLM   R0,B'0010',IDRDATE-IDRENTRY+2(R1)  THIRD BYTE:THIRD BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'1000',IDRDATE-IDRENTRY+0(R1)  FIRST BYTE:FIRST BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'0100',IDRDATE-IDRENTRY+1(R1)  SECONDBYTE:SECONDBYTE
         BNL   SORTIDR2                           HIGH/EQUAL - BRANCH
         SPACE 1
SORTIDR3 XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         B     SORTIDR2
         SPACE 1
SORTIDRZ LM    R14,R12,ESDIDRSV    RESTORE REGISTERS
         TM    FLAGSCC,FIDR        ANY IDR RECORDS?
         BZR   R14                 NO, RETURN AT +0
         B     4(,R14)             YES, RETURN AT +4
         DROP  R3
         SPACE 2
GETIDR   CLI   ##SUBCOM,C'A'         ATTRIB SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         MVI   SUBPOOLT,21
         LA    R0,LENIDR
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENIDR,R1),0(R1)
         OI    FLAGSCC,FIDR          IDR DATA EXISTS
         BR    R14
         TITLE 'P D S  --  PDS IF                       02/06/84'
***********************************************************************
***      IF SUBCOMMAND         ADDED BY BRUCE LELAND -- JAN., 1983  ***
***********************************************************************
*
         SPACE 1
IF       CSECT
         USING *,R8
         OC    #ACTIONT(8),#ACTIONT   ANY THEN OR ELSE?
         BNZ   *+10                   YES, BRANCH
         MVC   #ACTIONT(4),THENTBL+4  DEFAULT THEN ACTION IS ATTRIB
         SPACE 1
         TM    #OPTOPT,X'80'        PARSE INVOKED?
         BNO   IF996                NO, DONE
         CLC   #ATTRLO(3),DIRTTR    IN TTR LOWER BOUND?
         BH    IF997                NO, DO ANY ELSE
         CLC   #ATTRHI(3),DIRTTR    IN TTR UPPER BOUND?
         BL    IF997                NO, DO ANY ELSE
         SPACE 1
         TM    FLAGSCC,RECFMU       A LOAD LIBRARY?
         BO    IF006                YES, BRANCH
         LA    R1,MSGNLOAD          ASSUME LINKEDIT ATTRIBUTES
         TM    #OPTOPT,X'01'        CORRECT?
         BO    MSGNEWXX             YES, ERROR
         B     IF016
         SPACE 2
IF006    MVC   MODESAVE(1),DIRATTR3 RMODE/AMODE BYTE
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   MODESAVE,0           NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,MODESAVE
         SRA   R15,2                AMODE FOR ALIAS
         TM    DIRFLAG,X'80'        ALIAS?
         BNO   *+8                  NO, BRANCH
         STC   R15,MODESAVE         YES, USE ALIAS AMODE BITS
         NI    MODESAVE,DIRAM24+DIRAM31 TURN OFF OTHER BITS
         SPACE 3
IF016    CLI   #RMODE,X'01'         ANY RMODE SELECTION?
         BL    IF036                NO, BRANCH
         MVC   DOUBLE(1),DIRATTR3   RMODE/AMODE BYTE
         NI    DOUBLE,DIRRMANY      TURN OFF OTHER BITS
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   DOUBLE,0             NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,#RMODE
         SLA   R15,2                #RMODE*4
         B     *(R15)               CHECK FOR:
         B     IF020                  RMODE=24
         B     IF030                  RMODE=ANY
         B     IF030                  NO RMODE=24
         B     IF020                  NO RMODE=ANY
IF020    CLI   DOUBLE,0             RMODE=24?
         BNE   IF997                NO, DO ANY ELSE
         B     IF036                YES, BRANCH
IF030    CLI   DOUBLE,0             RMODE=24?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF036    CLI   #AMODE,X'01'         ANY AMODE SELECTION?
         BL    IF102                NO, BRANCH
         SR    R15,R15
         IC    R15,#AMODE
         SLA   R15,2                #AMODE*4
         B     *(R15)               CHECK FOR:
         B     IF040                  AMODE=24
         B     IF050                  AMODE=31
         B     IF060                  AMODE=ANY
         B     IF070                  NO AMODE=24
         B     IF080                  NO AMODE=31
         B     IF090                  NO AMODE=ANY
IF040    CLI   MODESAVE,DIRAM24     AMODE=24?
         BH    IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF050    CLI   MODESAVE,DIRAM31     AMODE=31?
         BNE   IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF060    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BNE   IF997                     NO, DO ANY ELSE
         B     IF102                     YES, BRANCH
IF070    CLI   MODESAVE,DIRAM24     AMODE=24?
         BNH   IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF080    CLI   MODESAVE,DIRAM31     AMODE=31?
         BE    IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF090    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BE    IF997                     YES, DO ANY ELSE
         SPACE 3
IF102    CLI   #ALIAOPT,X'01'       "ALIAS" OR "NOALIAS"?
         BL    IF106                NEITHER, BRANCH
         BH    IF104                "NOALIAS", BRANCH
         TM    DIRFLAG,X'80'        "ALIAS" -- CORRECT?
         BO    IF106                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF104    TM    DIRFLAG,X'80'        ALIAS?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF106    CLI   #VSLKED,X'01'        "VSLKED" OR "NOVSLKED"?
         BL    IF110                NEITHER, BRANCH
         BH    IF108                "NOVSLKED", BRANCH
         TM    DIRATTR2,DIRAOSLE    "VSLKED" -- CORRECT?
         BO    IF110                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF108    TM    DIRATTR2,DIRAOSLE    VSLKED?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF110    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"?
         BL    IF113                NEITHER, BRANCH
         LA    R2,ZERO              ASSUME NOT AUTHORIZED
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF111                       NO, NOT AUTHORIZED
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS ENTRY?
         BNO   *+8                  NO, BRANCH
         LA    R2,11(,R2)           YES, ELEVEN MORE BYTES
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF111                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF111    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"
         BH    IF112                "NOAUTH", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              "AUTH" -- CORRECT?
         BH    IF113                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF112    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF113                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              AUTHORIZED?
         BH    IF997                YES, DO ANY ELSE
         SPACE 3
IF113    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"?
         BL    IF116                NEITHER, BRANCH
         LA    R2,=X'01010101'      ASSUME NO APF ERROR
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF114                       NO, NO APF ERROR
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS ENTRY?
         BNO   *+8                  NO, BRANCH
         LA    R2,11(,R2)           YES, ELEVEN MORE BYTES
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF114                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF114    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"
         BH    IF115                "NOAPFERR", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BE    IF997                YES, DO ANY ELSE
         B     IF116                NO, BRANCH
IF115    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, DO ANY ELSE
         SPACE 3
IF116    CLI   #PAGEOPT,X'01'       "PAGE" OR "NOPAGE"?
         BL    IF120                NEITHER, BRANCH
         BH    IF118                "NOPAGE", BRANCH
         TM    DIRATTR2,DIR2PAGA    "PAGE" -- CORRECT?
         BO    IF120                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF118    TM    DIRATTR2,DIR2PAGA    PAGE?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF120    CLI   #SSIOPT,X'01'        "SSI" OR "NOSSI"?
         BL    IF130                NEITHER, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF122                YES, BRANCH
         LA    R2,ZERO              ASSUME NO SSI
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BO    IF124                YES, NO SSI INFORMATION
         LA    R2,DIRUSER           POINT TO SSI INFORMATION
         B     IF124                CHECK FOR ANY SSI
IF122    LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS ENTRY?
         BNO   *+8                  NO, BRANCH
         LA    R2,11(,R2)           YES, ELEVEN MORE BYTES
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BNO   IF124                NO, BRANCH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF128                NO, BRANCH
IF124    LA    R2,1(,R2)            ROUND TO START
         N     R2,=F'-2'                          OF NEXT HALFWORD
         CLC   ZERO,0(R2)           ZERO?
         BE    IF128                YES, NO SSI
         CLC   =F'-1',0(R2)         ALL ONES?
         BE    IF128                YES, NO SSI
IF126    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BE    IF130                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF128    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF130    TM    FLAGSCC,RECFMU       RECFM=U?
         BNO   IF140                NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#ATTRYES  ANY YES+0 ATTRIBUTES?
         BZ    IF132                NO, BRANCH
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF132    ICM   R1,B'0001',#ATTRYES+1 ANY YES+1 ATTRIBUTES?
         BZ    IF134                NO, BRANCH
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF134    ICM   R1,B'0001',#ATTRNO   NO+0 ATTRIBUTES
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 1
         ICM   R1,B'0001',#ATTRNO+1 NO+1 ATTRIBUTES
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 3
IF140    CLI   #USERIDO,X'01'       USERID, NOUSERID OR NOTUSERID?
         BL    IF144                NO, BRANCH
         LA    R1,MSGILOAD          ASSUME DATA SET IS A LOAD MODULE
         TM    FLAGSCC,RECFMU       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BNO   IF143                NO, BRANCH
         CLI   #USERIDO,X'02'       "NOUSERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SR    R1,R1
         ICM   R1,B'0001',#USERIDL  ANY USERID FILTERING?
         BP    IF141                YES, BRANCH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF144                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
         SPACE 1
IF141    BCTR  R1,0                 MACHINE LENGTH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BNE   IF142                NO, BRANCH
         EX    R1,IF142             PARTIAL USERID MATCH?
         BNE   IF997                NO, DO ANY ELSE
         B     IF144                YES, BRANCH
         SPACE 1
IF142    CLC   #USERIDT(*-*),DIRSPFID <<EXECUTED>>
         EX    R1,*-6               PARTIAL USERID MATCH?
         BE    IF997                YES, DO ANY ELSE
         B     IF144                NO, BRANCH
         SPACE 1
IF143    CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF144    TM    #OPTOPT,X'02'        NEED ANY MAP OR HISTORY DATA?
         BNO   IF150                NO, BRANCH
         MVI   RLDCOUNT,X'FF'       ASSUME NO RLD/CONTROL DATA FOUND
         SPACE 1
         L     R15,=A(READESD)
         BALR  R14,R15              ANY ESD DATA?
         NOP   0                    NO, IGNORE
         SPACE 1
         ICM   R2,B'1110',#ZAPOPT       SAVE ZAP/USER/TRAN
         MVC   #ZAPOPT(3),=X'01010101'  CHECK ZAP/USER/TRAN
         L     R15,=A(READIDR)
         BALR  R14,R15              ANY IDR DATA?
         B     IF145                NO, BRANCH
         SPACE 2
         MVI   #LKEDOPT,0           ASSUME LINKAGE-EDITOR FOUND
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN  ANY LKED NAME CHECK?
         BZ    IF145                NO, BRANCH
         BCTR  R1,0                 MACHINE COMPARE LENGTH
         CLC   LKEDNAME(*-*),#LKEDTXT  <<EXECUTED>>
         EX    R1,*-6               THIS PARTIAL LKED NAME?
         BE    IF145                YES, BRANCH
         MVI   #LKEDOPT,1           NO, LINKAGE-EDITOR NOT FOUND
         SPACE 2
IF145    STCM  R2,B'1110',#ZAPOPT   RESTORE ZAP/USER/TRAN
         USING ESDENTRY,R2
         LA    R2,#ESDPTR           SCAN ESD CHAIN
         SPACE 1
IF146    ICM   R2,B'1111',ESDLINK   ANY MORE ESD ENTRIES?
         BZ    IF150                NO, BRANCH
         CLI   ESDTYPE,CODESD       CSECT ESD ENTRY?
         BNE   IF146                NO, BRANCH
         SPACE 1
         USING IDRENTRY,R3
         LA    R3,#IDRPTR           SCAN IDR CHAIN
         SPACE 1
IF148    ICM   R3,B'1111',IDRLINK   ANY MORE IDR ENTRIES?
         BZ    IF146                NO, BRANCH
         CLC   ESDID,IDRESDID       THIS CSECT IDENTIFIER?
         BNE   IF148                NO, BRANCH
         SPACE 1
         MVI   #MODOPT,0            YES, MODULE FOUND
         CLI   IDRTYPE,IDRTRAN      IDR RECORD TYPE:
         BNL   *+8
         MVI   #ZAPOPT,0               **ZAP
         BNH   *+8
         MVI   #USEROPT,0              **USER
         BNE   *+8
         MVI   #TRANOPT,0              **TRANSLATOR
         B     IF148
         DROP  R2,R3
         SPACE 1
IF150    OC    #ZAPOPT(5),#ZAPOPT   ZAP/USER/TRAN/LKED/MODULE FOUND?
         BNZ   IF997                NO, DO ANY ELSE
         SPACE 3
         TM    DIRATTR2,DIRAOSLE    OS/VS LINKAGE EDITOR?
         BO    *+10                 YES, BRANCH
         MVC   RLDCOUNT(1),DIRATTR4 NO, WANT MATCHING RLD/CONTROL
         CLI   #RLDERR,X'01'        "RLDERR" OR "NORLDERR"
         BL    IF160                NEITHER, BRANCH
         BH    IF152                "NORLDERR", BRANCH
         CLC   DIRATTR4(1),RLDCOUNT "RLDERR" -- CORRECT?
         BNE   IF160                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF152    CLC   DIRATTR4(1),RLDCOUNT "NORLDERR" -- CORRECT?
         BNE   IF997                YES, DO ANY ELSE
         SPACE 3
IF160    NI    #IFABOVE,FF-X'01'    ASSUME SIZE IS NOT AVAILABLE
         TM    #IFABOVE,X'80'       ANY ABOVE CODED?
         BO    IF164                YES, BRANCH
         TM    #IFBELOW,X'80'       ANY BELOW CODED?
         BZ    IF170                NO, BRANCH
         SPACE 1
IF164    SR    R1,R1
         ICM   R1,B'0111',DIRSTORE  MODULE SIZE
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF168                YES, BRANCH
         TM    DIRFLAG,X'0F'        SPF STATISTICS PRESENT?
         BNO   IF180                NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ   RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   IF180                NO, BRANCH
         CLI   DIRSPFCD,0           00 OF OTHER 00YYDDDF ZERO?
         BNZ   IF180                NO, BRANCH
         LH    R1,DIRSPFSI
         SPACE 1
IF168    OI    #IFABOVE,X'01'       SIZE IS AVAILABLE
         ST    R1,#IFLEN            LENGTH OF THE MEMBER
         SPACE 1
IF170    TM    #OPTOPT,X'04'        NEED TO READ THE MEMBER?
         BNO   IF300                NO, BRANCH
         SPACE 1
IF180    SR    R6,R6                COUNT OF INPUT LOGICAL RECORDS
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BNL   IF242                YES, DONE
         OI    ##ADRPA#,@N          QUIET MODE READ IS REQUIRED
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         SPACE 3
IF204    BAL   R14,EXCP
         B     *+4(R15)             PROCESS RETURN CODE
         B     IF208                  00 - SUCCESSFUL READ
         B     IF240                  04 - END OF MEMBER
         B     IF240                  08 - END OF DATA SET
         B     IF242                  12 - I/O ERROR
         SPACE 2
IF208    LR    R3,R0                BUFFER START
         L     R5,LS                BLOCK LENGTH
         CH    R5,BLKSI             BLKSIZE VALID?
         BNH   IF220                YES, BRANCH
         OI    #OPTOPT,X'20'        NO, INDICATE A BLOCKSIZE ERROR
         SPACE 1
IF220    TM    FLAGSCC,RECFMF       FIXED FORMAT?
         BNO   IF222                NO, BRANCH
         LH    R14,LRECL            CURRENT LRECL
         LR    R0,R5                BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14               BLKSIZE/LRECL
         LTR   R0,R0                ANY REMAINDER?
         BZ    IF222                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (BLKSIZE/LRECL)
         SPACE 1
IF222    LR    R1,R5
         AR    R5,R3                END OF BUFFER
         BCTR  R5,0                 END OF BUFFER -1
         TM    FLAGSCC,RECFMF       RECFM=F?
         LH    R4,LRECL
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         LR    R4,R1
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, ASSUME RECFM=U
         AH    R3,=H'4'             SKIP BLOCK LENGTH WORD
         B     IF226
         SPACE 3
IF224    BXLE  R3,R4,IF226          GET THE NEXT LOGICAL RECORD
         B     IF204                END OF BLOCK, BRANCH
         SPACE 2
IF226    TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)     RECORD LENGTH +4
         LR    R14,R4               RECORD LENGTH +4
         AH    R3,=H'4'             SKIP RECORD LENGTH WORD
         SH    R4,=H'4'             RECORD LENGTH VALID?
         BNM   IF228                YES, BRANCH
         SR    R4,R4                NO, USE LENGTH 0
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (SHORT RECORD)
         B     IF224                SKIP THE RECORD
         SPACE 2
IF228    LA    R6,1(,R6)            ANOTHER OUTPUT RECORD
         CH    R14,LRECL            VALID LRECL VALUE?
         BNH   IF224                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (LONG RECORD)
         B     IF224
         SPACE 2
IF240    TM    DIRFLAG,DIR2TTR+DIR1TTR    ANY USER TTR?
         BZ    IF244                      NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',DIRSTART        TTR ACTUALLY INPUT?
         BNH   IF242                      NO, I/O ERROR
         TM    DIRFLAG,DIR2TTR            A SECOND TTR TOO?
         BZ    IF244                      NO, BRANCH
         CLM   R0,B'1110',DIRNOTE         TTR ACTUALLY INPUT?
         BH    IF244                      YES, BRANCH
         SPACE 1
IF242    OI    #OPTOPT,X'40'        PERMANENT I/O ERROR
         SPACE 2
IF244    LTR   R6,R6                ANY RECORDS?
         BNZ   IF246                YES, BRANCH
         OI    #OPTOPT,X'08'        NO, MARK AS NULL
         SPACE 3
IF246    NI    ##ADRPA#,FF-@N       END OF QUIET MODE READ
         CLI   #IOERROR,X'01'       "IOERROR" OR "NOIOERROR"?
         BL    IF260                NEITHER, BRANCH
         BH    IF248                "NOIOERROR", BRANCH
         TM    #OPTOPT,X'40'        "IOERROR" -- CORRECT?
         BO    IF260                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF248    TM    #OPTOPT,X'40'        IOERROR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF260    CLI   #BLOCKER,X'01'       "BLOCKERR" OR "NOBLOCKERR"?
         BL    IF266                NEITHER, BRANCH
         BH    IF262                "NOBLOCKERR", BRANCH
         TM    #OPTOPT,X'20'        "BLOCKERR" -- CORRECT?
         BO    IF266                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF262    TM    #OPTOPT,X'20'        BLOCKERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF266    CLI   #LRECLER,X'01'       "LRECLERR" OR "NOLRECLERR"?
         BL    IF280                NEITHER, BRANCH
         BH    IF268                "NOLRECLERR", BRANCH
         TM    #OPTOPT,X'10'        "LRECLERR" -- CORRECT?
         BO    IF280                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF268    TM    #OPTOPT,X'10'        LRECLERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF280    CLI   #NULL,X'01'          "NULL" OR "NONULL"?
         BL    IF300                NEITHER, BRANCH
         BH    IF282                "NONULL", BRANCH
         TM    #OPTOPT,X'08'        "NULL" -- CORRECT?
         BO    IF300                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF282    TM    #OPTOPT,X'08'        NULL?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF300    TM    #IFABOVE,X'01'         SIZE AVAILABLE?
         BNO   *+8                    NO, BRANCH
         L     R6,#IFLEN              YES, USE THAT LENGTH
         SPACE 1
         TM    #IFABOVE,X'80'         ABOVE CHECK?
         BNO   IF320                  NO, BRANCH
         CLM   R6,B'0111',#IFABOVE+1  ABOVE THE MINIMUM VALUE?
         BNH   IF997                  NO, DO ANY ELSE
         SPACE 1
IF320    TM    #IFBELOW,X'80'         BELOW CHECK?
         BNO   IF360                  NO, BRANCH
         CLM   R6,B'0111',#IFBELOW+1  BELOW THE MAXIMUM VALUE?
         BNL   IF997                  NO, DO ANY ELSE
         SPACE 3
IF360    CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NO, BRANCH
         SPACE 1
         SR    R3,R3                RESULT REGISTER
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),DIRNAME
         SPACE 1
IF382    BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,IF382                                  NON-BLANK
         SPACE 1
         CLI   DIRNAME,C'0'        NUMERIC FIRST CHARACTER?
         BL    *+8                 NO, BRANCH
         LA    R3,1                YES, NON-STANDARD NAME
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         BZ    *+8                 YES, BRANCH
         LA    R3,1                NO, NON-STANDARD NAME
         SPACE 1
         CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NEITHER, BRANCH
         BH    IF384                "NONAMEERR", BRANCH
         LTR   R3,R3                "NAMEERR" -- CORRECT?
         BP    IF390                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF384    LTR   R3,R3                NAMEERR?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK EACH MODULE FOR LINKAGE EDIT ATTRIBUTE CONFLICTS IF REQUIRED
IF390    CLI   #LKEDERR,X'01'              CONFLICT CHECK REQUESTED?
         BL    IF400                       NO, BRANCH
         SPACE 1
         TM    DIRATTR,ATTRRENT            REENTRANT?
         BNO   IF392                       NO, BRANCH
         TM    DIRATTR,ATTRREUS            REUSABLE?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF392    TM    DIRATTR+1,ATTRSYMS+ATTRNE   TEST AND NOT EDIT
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTRREUS+ATTRSCTR   REUSABLE AND SCATTER
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTROVLY            OVERLAY ATTRIBUTE?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR,ATTRRENT++ATTRREUS+ATTRSCTR  RENT/REUS/SCTR?
         BNZ   IF399                                YES, CONFLICT
         TM    DIRATTR+1,ATTRREFR          REFRESHABLE?
         BO    IF399                       YES, CONFLICT FOUND
         TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 24?
         BNZ   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF394    TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF397                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   IF397                       NO, BRANCH
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 31?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF397    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BE    IF997                       YES, DO ANY ELSE
         B     IF400                       NO, NONE FOUND
         SPACE 1
IF399    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BNE   IF997                       NO, DO ANY ELSE
         SPACE 3
* LOAD EACH MODULE IF REQUIRED
IF400    CLI   #LOADERR,X'01'      LOAD CHECK REQUESTED?
         BL    IF420               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,@N         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,IF402            RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         B     IF404
         SPACE 1
IF402    LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   RECOVER,1           LOAD DID NOT WORK
         SPACE 1
IF404    NI    ##ADRPA#,FF-@N       END OF QUIET MODE LOAD
         CLI   #LOADERR,X'01'       "LOADERR" OR "NOLOADERR"?
         BL    IF420                NEITHER, BRANCH
         BH    IF406                "NOLOADERR", BRANCH
         CLI   RECOVER,1            "LOADERR" -- CORRECT?
         BE    IF420                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF406    CLI   RECOVER,1            LOAD ERROR?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
IF420    CLI   #SPFEDIT,X'01'      INUSE CHECK REQUESTED?
         BL    IF440               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,@N         QUIET MODE SPF USE CHECK
         SR    R3,R3               ASSUME NOT IN USE
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         LA    R3,1                YES, MARK AS SUCH
         NI    ##ADRPA#,FF-@N      END OF QUIET MODE
         SPACE 1
         CLI   #SPFEDIT,X'01'       "SPFEDIT" OR "NOSPFEDIT"?
         BL    IF440                NEITHER, BRANCH
         BH    IF422                "NOSPFEDIT", BRANCH
         LTR   R3,R3                "SPFEDIT" -- CORRECT?
         BP    IF440                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF422    LTR   R3,R3                SPFEDIT?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF440    CLI   #AALIAS,X'01'        APPARENT ALIAS MEMBER CHECK?
         BL    IF460                NO, BRANCH
         SR    R3,R3                ASSUME NOT AN APPARENT ALIAS
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF444                YES, CANNOT BE AN APPARENT ALIAS
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF442    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF444                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF442                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF442                NO, BRANCH
         CLC   DIRNAME,MEMNAME      SAME MEMBER?
         BE    IF442                YES, BRANCH
         LA    R3,1                 AN APPARENT ALIAS MEMBER
         SPACE 1
IF444    CLI   #AALIAS,X'01'        "APPARENTALIAS" OR "NOAPPAREN.."?
         BL    IF460                NEITHER, BRANCH
         BH    IF446                "NOAPPARENTALIAS", BRANCH
         LTR   R3,R3                "APPARENTALIAS" -- CORRECT?
         BP    IF460                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF446    LTR   R3,R3                APPARENT ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF460    CLI   #ORPHAN,X'01'        ORPHAN MEMBER CHECK?
         BL    IF470                NO, BRANCH
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BNO   IF463                NO, CANNOT BE AN ORPHAN
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         LA    R3,1                 ASSUME IT IS AN ORPHAN
         SPACE 1
IF462    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF464                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF462                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF462                NO, BRANCH
IF463    SR    R3,R3                NOT AN ORPHAN
         SPACE 1
IF464    CLI   #ORPHAN,X'01'        "ORPHAN" OR "NOORPHAN"?
         BL    IF470                NEITHER, BRANCH
         BH    IF466                "NOORPHAN", BRANCH
         LTR   R3,R3                "ORPHAN" -- CORRECT?
         BP    IF470                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF466    LTR   R3,R3                ORPHAN?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF470    CLI   #RLD0,X'01'          "RLDZERO" OR "NORLDZERO"
         BL    IF480                NEITHER, BRANCH
         BH    IF472                "NORLDZERO", BRANCH
         CLI   DIRATTR4,0           "RLDZERO" -- CORRECT?
         BE    IF480                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF472    CLI   DIRATTR4,0           "NORLDZERO" -- CORRECT?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF480    DS    0H
         SPACE 1
IF996    MVC   FINDMEMQ+25(4),=C'THEN'
         L     R2,#ACTIONT          THEN ACTION
         B     IF998
         SPACE 1
IF997    MVC   FINDMEMQ+25(4),=C'ELSE'
         L     R2,#ACTIONE          ELSE ACTION
         SPACE 1
IF998    LTR   R2,R2                ANY ASSOCIATED ACTION?
         BNP   NEWCMD               NO, BRANCH
         MESSAGE MSGBLANK
         TM    FLAGSAA,FINDMSG      ANY HEADER MESSAGE?
         BNO   IF999                NO, BRANCH
         XI    FLAGSAA,FINDMSG      NO MORE MESSAGE
         MVC   FINDMEMQ+30(8),0(R2) SUBCOMMAND NAME
         MESSAGE FINDMEMQ           OUTPUT THE HEADER MESSAGE
         SPACE 1
IF999    MVC   ##ANSWER(LISUBS),ISUBS INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(20),0(R2)     CHANGE THE SUBCOMMAND
         B     CALLCMDZ               CALL THE SECONDARY SUBCOMMAND
         TITLE 'P D S  --  PDS LIST, DIRENTRY, FIND           02/06/84'
***********************************************************************
***      LIST SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      DIRENTRY SUBCOMMAND   ADDED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      FIND SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***********************************************************************
*
LIST     CSECT
         USING *,R8
         B     LIST010
*
         SPACE 1
DIRENTRY L     R8,LISTLIT+8        BASE REGISTER FOR LIST
         LA    R1,MSGNLIB          ASSUME IT IS NOT PARTITIONED
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, ERROR
         SPACE 1
         IC    R1,DIRFLAG
         N     R1,=XL4'0000001F'   NUMBER OF HALFWORDS
         LA    R5,12(R1,R1)        LENGTH IN BYTES
         LA    R6,DIRNAME          START OF DIRECTORY ENTRY
         LA    R4,16               DUMP FORMAT, WIDTH=16
         MVC   MSGTEXT2,MSGDUMP
         CVD   R5,DOUBLE
         ED    MSGTEXT2+MSGDUMPL-MSGDUMP(7),DOUBLE+5
         AR    R5,R6
         BCTR  R5,0
         MVC   MSGTEXT2+MSGDUMPB-MSGDUMP(23),DUMPDIR
         LH    R1,MSGTEXT2
         SH    R1,=H'12'
         STH   R1,MSGTEXT2
         LA    R3,LIST610          PROCESSING ROUTINE
         BR    R3
         SPACE 2
*
FIND     L     R8,LISTLIT+8        BASE REGISTER FOR LIST
         CLI   FINDLTH+1,0         STRING EVER CODED?
         LA    R1,MSGFINDN
         BZ    MSGNEWXX            NO, TERMINATE THIS GROUP
         SPACE 2
LIST010  MVC   FLAGSDD(1),HFLAGSDD INITIALIZE FLAGSDD
         OC    #MAXIN,#MAXIN       ANY INPUT DESIRED?
         BZ    LIST910             NO, BRANCH
         SPACE 2
         TM    DSORG,DS1DSGPO        DSORG=PO?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSCC,RECFMU        LOAD MODULE?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BM    LIST020                          YES, BRANCH
         MVI   FLAGSDD,LDUMP                    NO, USE LDUMP
LIST020  TM    FLAGSDD,LBLOCK+LDUMP  LBLOCK OR LDUMP FORMAT?
         BZ    LIST090               NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD LIST
         LA    R1,MSGTEXT1         ASSUME NO SYMBOLS FOUND
         L     R15,=A(READESD)     ESD SYMBOL ROUTINE
         BALR  R14,R15             ANY SYMBOLS?
         B     *+8                 NO, "NO EXTERNAL SYMBOLS"
         B     LIST030             YES, BRANCH
         SPACE 2
         MVC   4(8,R1),DIRNAME     MEMBER NAME IN ERROR
         TR    4(8,R1),TRLINE      CONVERT TO PRINTABLE CHARACTERS
         MESSAGE (R1)              OUTPUT AN ERROR MESSAGE
         B     LIST090             CONTINUE, BUT NO CSECT IDENTIFIERS
         SPACE 2
         USING ESDENTRY,R2
*  LOOPS TO SET MAXIMUM LENGTH FIELDS FOR LABEL REFERENCE ENTRIES
*  AND TO ASSOCIATE ENTRY RECORDS WITH THE APPROPRIATE CSECT NAMES
LIST030  OI    #LISTFLG,X'01'      CSECT OPERATION FLAG
         ICM   R2,B'1111',ESDLINK  ANY MORE ENTRIES?
         BZ    LIST060             NO, BRANCH
         MVI   ESDSEG#,0           CLEAR THE SEGMENT NUMBER
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   LIST030             NO, BRANCH
         LR    R1,R2
         SPACE 1
LIST040  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BZ    LIST060
         CLI   ESDTYPE-ESDENTRY(R1),CODELR  ASSOCIATED EXTERNAL LABEL?
         BNE   LIST030                      NO, BRANCH
         MVI   ESDSEG#-ESDENTRY(R1),0       CLEAR THE SEGMENT NUMBER
         ST    R2,ESDCSECT-ESDENTRY(R1)     SAVE POINTER TO NAME
         SR    R14,R14
         LH    R0,ESDID                     RELATIVE ESDID OF CSECT
         STH   R0,ESDID-ESDENTRY(R1)        SAVE FOR ENTRY
         L     R0,ESDADDR-1                 BASE START ADDRESS
         ST    R0,ESDMAIN-ESDENTRY(R1)      SAVE MAIN OFFSET
         A     R0,ESDLEN-1                  BASE MAXIMUM REFERENCE
         ICM   R14,B'0111',ESDADDR-ESDENTRY(R1)
         SR    R0,R14
         ST    R0,ESDLEN-ESDENTRY-1(R1)     MAXIMUM SYMBOL LENGTH
         B     LIST040
         SPACE 2
*  LOOP TO FILTER THE SYMBOL NAMES
LIST060  LA    R2,#ESDPTR          START OF ESD LIST
         SR    R3,R3
         ICM   R3,B'0001',#MODLEN  ANY FILTERING?
         BZ    LIST090             NO, BRANCH
         BCTR  R3,0                MACHINE LENGTH
         SPACE 1
LIST070  LR    R1,R2
         ICM   R2,B'1111',ESDLINK  END OF LIST?
         BZ    LIST090             YES, BRANCH
         CLC   ESDNAME(*-*),#MODTXT  <<EXECUTED>>
         EX    R3,*-6              MATCH THE PARTIAL NAME?
         BE    LIST070             YES, BRANCH
         MVC   ESDLINK-ESDENTRY(4,R1),ESDLINK
         LR    R2,R1                    DROP THIS SYMBOL
         B     LIST070
         SPACE 1
LIST090  MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
LIST100  BAL   R14,EXCP
         B     *+4(R15)                 PROCESS RETURN CODE
         B     LIST120                    00 - GOOD READ
         B     LIST110                    04 - END OF MEMBER
         B     LIST900                    08 - END OF DATA SET
         B     LIST910                    12 - I/O ERROR
         SPACE 1
LIST110  TM    DSORG,DS1DSGIS           ISAM DATA SET?
         BNO   LIST900                  NO, BRANCH
         MESSAGE MSGEOF                 YES, OUTPUT A MESSAGE
         B     LIST100                  CONTINUE
         SPACE 1
LIST120  NI    #LISTFLG,FF-X'20'        TURN OFF THE CSECT DATA FLAG
         L     R5,LS                    CURRENT LENGTH
         LR    R6,R0                    START OF BUFFER
         LR    R1,R5                    SAVE BLOCKSIZE FOR LATER
         LM    R14,R15,#LISTN0          SAVE IF NOT PHYSICAL BLOCKS
         L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R5                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         TM    FLAGSDD,BLOCK            BLOCK OUTPUT DESIRED?
         BO    LIST400                  YES, BRANCH
         TM    FLAGSDD,DUMP             DUMP OUTPUT DESIRED?
         BO    LIST420                  YES, BRANCH
         STM   R14,R15,#LISTN0          NOT PHYSICAL BLOCKS
         AR    R5,R6                    ADDRESS OF END OF BUFFER
         BCTR  R5,0                     END OF BUFFER ADDRESS
         LA    R3,LIST200               ASSUME FIXED OR UNDEFINED
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL                 GET RECORD LENGTH
         BOR   R3                       YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1                    BLOCK LENGTH
         BOR   R3                       YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNOR  R3                       NO, PROCESS LIKE RECFM=U
         SPACE 1                        SET END OF BUFFER ADDRESS
         AH    R6,=H'4'                 RECFM=V; START OF LRECL'S
         LA    R3,LIST150               VARIABLE LEN RECORD PROCESSOR
         SPACE 2
LIST150  SR    R4,R4
         ICM   R4,B'0011',0(R6)         RECORD LENGTH +4
         AH    R6,=H'4'                 SKIP OVER HEADER
         SH    R4,=H'4'                 SUBTRACT HEADER LENGTH
         BP    LIST200                  BRANCH IF RECORD EXISTS
         SR    R4,R4                    NO, RESET LENGTH TO ZERO
         SPACE 1
LIST160  LR    R0,R4                    PREVIOUS RECORD LENGTH
         A     R0,#LISTN1               OFFSET IN CURRENT RECORD
         ST    R0,#LISTN1               UPDATE
         BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         CLI   ##SUBCOM,C'D'            DIRENTRY SUBCOMMAND?
         BE    NEWCMD                   YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BZ    LIST100                  NO, GET A NEW BLOCK
         LM    R3,R6,#LFSAVE4           RESTORE R3-R6
         BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         B     LIST100                  REAL END OF BLOCK
         SPACE 3
LIST200  L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R4                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         STM   R3,R6,#LFSAVE4             SAVE FOR LBLOCK/LDUMP
         L     R0,#SKIPREC
         S     R0,=F'1'                   SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST160                    YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                   SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                    YES, BRANCH
LIST210  LR    R1,R4                      LENGTH OF DATA AREA
         TM    FLAGSDD,LBLOCK             LBLOCK?
         BO    LIST400                    YES, BRANCH
         TM    FLAGSDD,LDUMP              LDUMP?
         BO    LIST420                    YES, BRANCH
         LA    R0,8                       WIDTH OF LINE NUMBER FIELD
         LR    R15,R6                     MOVE START POSITION
         LR    R14,R15                    LINE NUMBER FOR RECFM=V OR U
         A     R15,#SKIPCOL               ADJUST START FOR START COLUMN
         S     R1,#SKIPCOL                ADJUST LEN FOR START COLUMN
         TM    FLAGSDD,NONUM+DONONUM      NONUM FORMAT?
         BNZ   LIST330                    YES, BRANCH
         AR    R15,R0                     ADJUST START FOR RECFM=V OR U
         SR    R1,R0                      ADJUST LENGTH, RECFM=V U OR F
         TM    FLAGSCC,RECFMF             RECFM=F?
         BNO   LIST230                    NO, BRANCH
         SR    R15,R0                     CORRECT DATA START POINTER
         LA    R14,0(R4,R6)               END OF LINE
         SR    R14,R0                     START OF LINE NUMBER
LIST230  TM    FLAGSDD,SNUM               SNUM FORMAT?
         BO    LIST330                    YES, BRANCH
         MVC   MSGTEXT1+4(8),0(R14)       NUM FORMAT -- GET LINE NUMBER
         NC    MSGTEXT1+4(8),=C'00000000' DROP NUMERIC PART
         CLC   MSGTEXT1+4(8),=C'00000000' ZONE DIGITS FOR ALL BYTES?
         BE    LIST240                    YES, BRANCH
         OI    FLAGSDD,DONONUM            CONTINUE WITH NONUM FORMAT
         B     LIST210
         SPACE 1
LIST240  MVC   MSGTEXT1+4(6),0(R14)       SPF LINE NUMBER FIELD
         TM    DIRFLAG,X'0F'              SPF ATTRIBUTES STORED?
         BO    LIST320                    YES, BRANCH
         LA    R0,5                       MAXIMUM OF 5 DIGITS TO BLANK
         LA    R2,MSGTEXT1+3              START OF FIELD TO BLANK
         MVC   MSGTEXT1+4(6),2(R14)       LINE NUMBER FIELD
LIST310  LA    R2,1(,R2)
         CLI   0(R2),C'0'                 BLANK LEADING ZEROES IN THE
         BNE   LIST320                       LINE NUMBER FIELD
         MVI   0(R2),X'40'
         BCT   R0,LIST310                 DO UP TO 5 DIGITS
LIST320  LA    R0,249                     MAXIMUM ALLOWED DATA LENGTH
         MVI   MSGTEXT1+10,X'40'          BLANK AFTER LINE NUMBER
         LA    R14,MSGTEXT1+11            WHERE TO MOVE DATA
         B     LIST340
         SPACE 2
LIST330  LA    R0,256                   MAXIMUM LRECL TO DISPLAY
         LA    R14,MSGTEXT1+4           WHERE TO MOVE OUTPUT DATA
         SPACE 1
LIST340  CR    R1,R0                    LENGTH>MAXIMUM ALLOWED?
         BL    *+6                      NO, BRANCH
         LR    R1,R0                    YES, USE MAXIMUM
         S     R1,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST360                  NO, BRANCH
         MVC   0(*-*,R14),0(R15)        <<EXECUTED>>
         EX    R1,*-6                   MOVE DATA INTO LINE
         SPACE 1
LIST360  LR    R2,R1                    MACHINE LENGTH
         LA    R1,MSGTEXT1+4            START OF DATA
         CR    R14,R1                   NUM MODE?
         BE    *+8                      NO, BRANCH
         AH    R2,=H'7'                 YES, ACCOUNT FOR LINE NO, BLANK
         A     R2,=F'1'                 ACTUAL DATA LENGTH
         C     R2,#MAXLEN               LENGTH>#MAXLEN?
         BL    *+8                      NO, BRANCH
         L     R2,#MAXLEN               YES, USE #MAXLEN INSTEAD
         S     R2,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST160                  LENGTH=0, BRANCH
         LA    R0,5(,R2)                ACTUAL LINE LENGTH + HALFWORDS
         SLL   R0,16
         ST    R0,MSGTEXT1              CREATE OUTPUT LINE HEADER
         CLI   ##SUBCOM,C'F'            FIND OPERATION?
         BNE   LIST730                  NO, LIST -- BRANCH
         SPACE 1
         LA    R14,1(R1,R2)             END OF DATA
         LH    R15,FINDLTH              STRING LENGTH
         BCTR  R15,0                    MACHINE LENGTH
LIST370  EX    R2,LIST380               FIRST CHARACTER FOUND?
         BZ    LIST160                  NO, NOT IN STRING, QUIT
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BNL   LIST160                  NO, QUIT
         EX    R15,LIST390              ENTIRE STRING FOUND?
         BE    LIST730                  YES, OUTPUT THE LINE
         LA    R1,1(,R1)                NO, TRY NEXT CHARACTER
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BM    LIST160                  YES, NOT IN STRING, QUIT
         B     LIST370                  CONTINUE SEARCHING
         SPACE 2
LIST380  TRT   0(*-*,R1),FINDTBL        <<EXECUTED>> -- FIRST BYTE
LIST390  CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         SPACE 3
LIST400  LA    R4,64                    BLOCK FORMAT, WIDTH=64
         LA    R3,LIST600               PROCESSING ROUTINE
         B     LIST430                  FORMAT HEADER
         SPACE 1
LIST420  LA    R4,16                    DUMP FORMAT, WIDTH=16
         LA    R3,LIST610               PROCESSING ROUTINE
         SPACE 1
LIST430  L     R0,#SKIPCOL              CHARACTERS TO SKIP
         ST    R0,#LISTN2               UPDATE BLOCK OFFSET
         A     R0,#LISTN1               CURRENT DATA
         ST    R0,#LISTN1                           DISPLACEMENT
         MVC   MSGTEXT2,MSGDUMP         HEADER FOR BLOCK OR DUMP
         NI    FLAGSDD,FF-OVERLAP       TURN OFF THE OVERLAP INDICATOR
         LR    R5,R1                    DATA LENGTH
         C     R5,#MAXLEN               RESTRICT DATA LENGTH?
         BL    *+8                      NO, BRANCH
         L     R5,#MAXLEN               YES, USE MAX LENGTH
         AR    R5,R6                    COMPUTE NEW END-OF-BLOCK
         BCTR  R5,0                     COMPUTE NEW END-OF-BLOCK -1
         A     R6,#SKIPCOL              ADJUST FOR START COLUMN
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST450                  YES, BRANCH
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         CVD   R0,DOUBLE
         ED    MSGTEXT2+MSGDUMPR-MSGDUMP(10),DOUBLE+4
         L     R0,#SKIPREC
         S     R0,=F'1'                 SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST100                  YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                 SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                  YES, BRANCH
         CVD   R1,DOUBLE                  RECORD LENGTH
         ED    MSGTEXT2+MSGDUMPL-MSGDUMP(7),DOUBLE+5
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         ST    R0,DOUBLE                SAVE RESULTING TTR
         UNPK  MSGTEXT2+MSGDUMPT-MSGDUMP(7),DOUBLE(4)
         TR    MSGTEXT2+MSGDUMPT-MSGDUMP(6),TRTABLE
         MVI   MSGTEXT2+MSGDUMPT-MSGDUMP+6,X'40'
         TM    FLAGSDD,DUMP
         BNOR  R3
         MVC   MSGTEXT2+MSGDUMPB-MSGDUMP(5),DUMPDIR
         BR    R3
         SPACE 2
LIST450  TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BNOR  R3                       NO, BRANCH
         OI    #LISTFLG,X'08'           NEED A HEADER CHANGE
         ST    R4,#LISTWD               WIDTH OF EACH LINE
         L     R6,EXCPBUFF              BUFFER START
         L     R5,LS                    BUFFER LENGTH
         AR    R5,R6                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    #LISTFLG,X'40'           ANY CSECT ADDRESS YET?
         BO    LIST460                  YES, BRANCH
         TM    0(R6),X'F0'              TEST, SCATTER, CESD OR IDR?
         BM    LIST100                  YES, BRANCH
LIST460  TM    #LISTFLG,X'C0'           ANY CURRENT ESD RECORD?
         BO    LIST470                  YES, THIS IS A DATA RECORD
         CLI   8(R6),X'06'              AN ESD RECORD?
         BNE   LIST100                  NO, BRANCH
         OI    #LISTFLG,X'C0'           FLAGS FOR ESD RECORD INPUT
         MVC   #LISTVIR+1(3),9(R6)      SAVE THE OFFSET ADDRESS
         LH    R15,6(R6)                OFFSET TO ESDID
         LA    R5,16(R15,R6)            FIRST ESDID FOR THIS BLOCK
         LH    R6,4(R6)                 ESDID ENTRIES *4
         SRL   R6,2                     ESDID ENTRIES
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         SPACE 2
*  LOOP TO MARK ENTRIES FOR THE NEXT PHYSICAL BLOCK
LIST464  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST100                  NO, DONE
         MVI   ESDIDIN,0                TURN OFF FIRST
         LR    R14,R5                   START OF ESDID LIST
         LR    R15,R6                   NUMBER OF ESDID ENTRIES
         SPACE 1
LIST468  CLC   0(2,R14),ESDID           THIS ENTRY?
         BNE   *+8                      NO, BRANCH
         MVI   ESDIDIN,1                YES, MARK IT
         LA    R14,4(,R14)              NEXT ESDID ENTRY
         BCT   R15,LIST468              MARK ALL ENTRIES THAT MATCH
         B     LIST464                  CHECK ALL ESD ENTRIES
         SPACE 3
LIST470  XI    #LISTFLG,X'A0'           OFF CURRENT ESD; ON CSECT DATA
         L     R0,#LISTVIR              BLOCK OFFSET
         L     R14,#LISTOFF             OFFSET
         ST    R14,#LISTN2              BLOCK OFFSET
         A     R14,#LISTN1              DATA
         ST    R14,#LISTN1                  DISPLACEMENT
         ICM   R14,B'1111',#LISTOFF     ANY OFFSET?
         BP    *+6                      YES, BRANCH
         LR    R14,R0                   NO, USE THE BLOCK OFFSET
         SR    R6,R0
         ST    R6,#LISTVIR              VIRTUAL BUFFER START ADDRESS
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         AR    R6,R14                   OFFSET --> ACTUAL ADDRESS
         SPACE 2
*  LOOP TO FIND A START ENTRY
LIST480  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST100                  NO, DONE
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR      START ADDRESS OF SYMBOL
         LR    R15,R14                  CSECT START OFFSET
         A     R14,#LISTVIR             OFFSET --> ADDR OF CSECT START
         A     R15,ESDLEN-1             CSECT END OFFSET
         A     R15,#LISTVIR             OFFSET --> ADDRESS OF CSECT END
         CR    R6,R15                   CURRENT:CSECT END
         BNL   LIST480                    HIGH OR EQUAL, LOOP FOR NEXT
         CR    R5,R14                   END:CSECT START
         BL    LIST480                    LOW, LOOP FOR NEXT
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST480                  NO, LOOP FOR NEXT
         MVC   #LISTID(2),ESDID         YES, MARK THE CURRENT ESDID
         ST    R2,#LISTENT              SAVE THIS ENTRY START
         LR    R1,R2
         SPACE 1
LIST490  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)  NEXT ENTRY?
         BZR   R3                               NO, DONE
         CLC   #LISTID(2),ESDID-ESDENTRY(R1)    SAME ESDID ON NEXT ONE?
         BNER  R3                               NO, DONE
         ICM   R15,B'0111',ESDADDR-ESDENTRY(R1) NEXT ENTRY START
         A     R15,#LISTVIR                     OFFSET --> NEXT START
         CR    R14,R15                          CSECT START:NEXT ENTRY
         BE    LIST490                            SAME, IGNORE ENTRY
         CR    R15,R6                           NEXT START:CURRENT
         BNH   LIST480                            NOT ABOVE, USE NEXT
         BR    R3
         SPACE 3
LIST600  BAL   R14,LIST800              PERFORM ANY CSECT POSITIONING
         LR    R2,R4                    DATA LENGTH
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    LIST160                  NO, BRANCH
         CR    R2,R14
         BNH   *+8
         LA    R2,1(,R14)               DATA LENGTH FOR THIS SEGMENT
         BCTR  R2,0                     MACHINE LENGTH FOR THE SEGMENT
         MVI   MSGTEXT1+17,C'*'         BEGINNING *
         MVC   MSGTEXT1+18(*-*),0(R6)   <<EXECUTED>>
         EX    R2,*-6                   MOVE IN TEXT
         LA    R1,MSGTEXT1+18+1(R2)     POSITION OF FINAL ASTERISK
         MVI   0(R1),C'*'               FINAL *
         UNPK  MSGTEXT1+04(7),#LISTN1+1(4)  UNPACK ADDRESS
         TR    MSGTEXT1+04(6),TRTABLE       CONVERT TO HEXADECIMAL
         MVI   MSGTEXT1+10,X'40'            BLANK THE FOLLOWING BYTE
         UNPK  DOUBLE(7),#LISTN2+1(4)       UNPACK ADDRESS
         TR    DOUBLE(7),TRTABLE            CONVERT TO HEXADECIMAL
         MVC   MSGTEXT1+11(4),DOUBLE+2      MOVE IN ADDRESS
         MVI   MSGTEXT1+15,X'40'            BLANK THE FOLLOWING BYTE
         CLI   DOUBLE+1,C'0'                LESS THAN 64K
         BE    *+10                         YES, BRANCH
         MVC   MSGTEXT1+11(5),DOUBLE+1      MOVE IN ADDRESS
         MVI   MSGTEXT1+16,X'40'            BLANK ANOTHER BYTE
         L     R1,#LISTN2                   CSECT
         LA    R1,1(R1,R2)                       BYTE
         ST    R1,#LISTN2                            DISPLACEMENT
         LA    R1,20(,R2)               OUTPUT LENGTH
         B     LIST640                  FINISH FIND PROCESSING
         SPACE 2
LIST610  BAL   R14,LIST800              PERFORM ANY CSECT POSITIONING
         CLEAR MSGTEXT1
         LR    R2,R4                    DATA LENGTH
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    LIST160                  NO, BRANCH
         CR    R2,R14
         BNH   *+8
         LA    R2,1(,R14)               DATA LENGTH FOR THIS RECORD
         BCTR  R2,0                     MACHINE LENGTH FOR THE RECORD
         LA    R15,MSGTEXT1+11          PRINT AREA ADDR
         MVI   46(R15),C'*'             BEGINNING *
         MVC   47(*-*,R15),0(R6)        <<EXECUTED>>
         EX    R2,*-6                   MOVE IN TEXT
         LA    R1,47+1(R15,R2)
         MVI   0(R1),C'*'               FINAL *
         UNPK  MSGTEXT1+06(7),#LISTN1+1(4)  UNPACK ADDRESS
         TR    MSGTEXT1+06(6),TRTABLE       CONVERT TO HEXADECIMAL
         MVI   MSGTEXT1+12,X'40'            BLANK THE FOLLOWING BYTE
         CLI   ##SUBCOM,C'D'                DIRENTRY SUBCOMMAND?
         BNE   *+10                         NO, BRANCH
         MVC   MSGTEXT1+06(6),BLANKS        BLANK THE COUNT FIELD
         UNPK  DOUBLE(7),#LISTN2+1(4)       UNPACK ADDRESS
         TR    DOUBLE(7),TRTABLE            CONVERT TO HEXADECIMAL
         MVC   MSGTEXT1+13(4),DOUBLE+2      MOVE IN ADDRESS
         CLI   DOUBLE+1,C'0'                LESS THAN 64K
         BE    *+10                         YES, BRANCH
         MVC   MSGTEXT1+13(5),DOUBLE+1      MOVE IN ADDRESS
         L     R1,#LISTN2                   CSECT
         LA    R1,1(R1,R2)                       BYTE
         ST    R1,#LISTN2                            DISPLACEMENT
         LR    R1,R6                    CURRENT DATA POINTER
         LA    R14,1(,R2)               NUMBER OF BYTES TO FORMAT
         SR    R0,R0                    CURRENT FORMATTED CHARACTER
         SPACE 2
LIST620  MVC   DOUBLE(1),0(R1)          AVOID 0C5 FOR LAST BUFFER BYTE
         UNPK  8(3,R15),DOUBLE(2)       UNPACK DATA
         TR    8(2,R15),TRTABLE         CONVERT TO HEXADECIMAL
         MVI   10(R15),X'40'            CLEAR FOLLOWING BYTE
         LA    R15,2(,R15)              INCR INTO PRINT AREA
         A     R1,=F'1'                 INCREMENT INTO DATA
         A     R0,=F'1'                 NEXT FORMATTED CHARACTER
         ST    R0,DOUBLE
         TM    DOUBLE+3,X'03'           4, 8, 12, 16, ...?
         BNZ   LIST630                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
         TM    DOUBLE+3,X'07'           8, 16, 24, 32, ...?
         BNZ   LIST630                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
LIST630  BCT   R14,LIST620              GO FORMAT
         LA    R1,62(,R2)               OUTPUT LENGTH
         SPACE 1
LIST640  SLL   R1,16                    CREATE OUTPUT LINE HEADER
         ST    R1,MSGTEXT1
         MVC   DOUBLE,DOUBLE
         CLI   ##SUBCOM,C'F'            FIND OPERATION?
         BNE   LIST700                  NO, LIST -- BRANCH
         XC    DOUBLE,DOUBLE
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LH    R15,FINDLTH              STRING LENGTH
         ST    R14,DOUBLE               END OF DISPLAYED PART
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    MACHINE LENGTH OF STRING
         CR    R14,R5                   BEYOND RECORD END?
         BL    LIST660                  NO, BRANCH
         LR    R14,R5                   YES, USE END OF RECORD -1
         SPACE 1
LIST660  EX    R2,LIST380               FIRST CHARACTER FOUND?
         BZ    LIST670                  NO, NOT IN STRING
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BH    LIST670                  NOT IN STRING
         EX    R15,LIST390              ENTIRE STRING FOUND?
         BNE   *+8                      NO, BRANCH
         ST    R1,DOUBLE+4              YES, SAVE THE LAST ADDRESS
         LA    R1,1(,R1)                NO, TRY NEXT CHARACTER
         L     R2,DOUBLE                LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   LIST660                  NO, CONTINUE SEARCHING
         SPACE 2
LIST670  ICM   R1,B'1111',DOUBLE+4      ANY STRING FOUND?
         BZ    LIST680                  NO, BRANCH
         NI    FLAGSDD,FF-OVERLAP       CLEAR THE OVERLAP FLAG
         SR    R1,R6                    DISPLACEMENT INTO DISPLAY
         AH    R1,FINDLTH               LENGTH AT END OF COMPARE
         CR    R1,R4                    BEYOND DISPLAYED LENGTH?
         BNH   LIST700                  NO, BRANCH
         OI    FLAGSDD,OVERLAP          YES, MARK AS AN OVERLAP
         B     LIST700
LIST680  TM    FLAGSDD,OVERLAP          NO STRING FOUND -- OVERLAP SET?
         BNO   LIST160                  NO, BRANCH
         XI    FLAGSDD,OVERLAP          YES, TURN OF THE OVERLAP FLAG
         SPACE 3
LIST700  CLI   MSGTEXT2,X'FF'           ANY HEADER?
         BE    LIST760                  NO, BRANCH
         CLI   ##SUBCOM,C'D'            DIRENTRY SUBCOMMAND?
         BE    LIST720                  YES, BRANCH
         ICM   R0,B'1111',#MAXOUT       ANY MORE OUTPUT?
         BNP   LIST910                  NO, ALL DONE
         CLI   ##SUBCOM+3,C'Y'          ANY OUTPUT YET?
         BNE   LIST710                  NO, BRANCH
         LA    R15,MSGBLANK
         BAL   R14,LIST770              OUTPUT A BLANK LINE
LIST710  TM    #LISTFLG,X'01'           CSECT OUTPUT?
         BO    LIST720                  YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST730                  YES, BRANCH
LIST720  LA    R15,MSGTEXT2             OUTPUT THE HEADER LINE
         BAL   R14,LIST770
LIST730  MVI   MSGTEXT2,X'FF'           TURN OFF THE HEADER
         L     R0,#MAXOUT               DECREMENT
         S     R0,=F'1'                          THE #MAXOUT
         ST    R0,#MAXOUT                                   COUNTER
         BM    LIST910                  NEGATIVE, ALL DONE
LIST760  TR    MSGTEXT1+4(256),TRLINE   UNPRINTABLES --> PERIODS
         LA    R15,MSGTEXT1             OUTPUT THE NEXT LINE
         BAL   R14,LIST770              OUTPUT THE NEXT LINE
         TM    ##ADRPA#,#FIND1          FINDLIST MODIFICATION?
         BNO   *+8                      NO, BRANCH
         MVI   ##SUBCOM,C'1'            YES, NOW LIST THE REST
         B     LIST160                  YES, CONTINUE
         SPACE 1
LIST770  STM   R14,R15,#LFSAVE2         SAVE REGISTERS 14 AND 15
         CLI   ##SUBCOM+3,C'D'          FIND AND NO OUTPUT YET?
         BNE   LIST780                  NO, BRANCH
         SPACE 1
         LA    R1,=C'THEN'
         ICM   R2,B'1111',#ACTIONT      ANY THEN ACTION?
         BP    LIST980                  YES, BRANCH
         SPACE 1
         ICM   R2,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    NEWCMD                   YES, IGNORE THIS MEMBER
         SPACE 1
         TM    FLAGSAA,FINDMSG          ANY HEADER MESSAGE?
         BNO   LIST780                  NO, BRANCH
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         MESSAGE FINDMEMQ               OUTPUT THE HEADER LINE
         NI    FLAGSAA,FF-FINDMSG       TURN OFF THE HEADER LINE
         SPACE 1
LIST780  L     R1,#LFSAVE2+4            POINT TO MESSAGE
         MVI   ##SUBCOM+3,C'Y'          OUTPUT WAS GENERATED
         MESSAGE (R1)                   OUTPUT THE MESSAGE
         L     R14,#LFSAVE2             RESTORE REGISTER 14
         BR    R14
         SPACE 3
LIST800  TM    #LISTFLG,X'60'           CSECT DATA?
         BNOR  R14                      NO, RETURN
         TM    #LISTFLG,X'08'           NEED ADJUSTMENT?
         BNO   LIST830                  NO, BRANCH
         XI    #LISTFLG,X'08'           RESET THE ADJUSTMENT FLAG
LIST804  L     R4,#LISTWD               DISPLAY CHARACTER WIDTH
         ICM   R2,B'1111',#LISTENT      END OF CSECT ENTRIES?
         BZ    LIST100                  YES, BRANCH
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST100                  NO, GET THE NEXT INPUT BLOCK
         SPACE 1
*  ADJUST TO BEGINNING BOUNDARY
         SR    R0,R0
         ICM   R0,B'0111',ESDADDR       CURRENT OFFSET
         A     R0,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         CR    R0,R6                    VALID ADJUSTMENT?
         BNH   *+6                      NO, BRANCH
         LR    R6,R0                    YES, POSITION TO SYMBOL START
         LR    R1,R6
         BCTR  R1,0
         CR    R1,R5                    IN THIS DATA RECORD?
         BNL   LIST100                  NO, IGNORE THIS RECORD
         XC    #LISTOFF(4),#LISTOFF     MODULE OFFSET WAS HONORED
         MVC   #LISTID(2),ESDID         SAVE THE CURRENT ESD ID
         MVC   MSGTEXT2,MSGLDUMP        "AT 123456  CSECT 12345678 .."
         UNPK  MSGTEXT2+7(7),ESDADDR(4)
         TR    MSGTEXT2+7(6),TRTABLE    OFFSET OF CSECT OR ENTRY
         MVI   MSGTEXT2+7+6,X'40'
         MVC   MSGTEXT2+21(8),ESDNAME   PRIVATE, COMMON OR CSECT NAME
         UNPK  MSGTEXT2+38(7),ESDLEN(4)
         TR    MSGTEXT2+38(6),TRTABLE   LENGTH OF CSECT
         MVI   MSGTEXT2+38+6,X'40'
         LR    R0,R6
         S     R0,#LISTVIR              ACTUAL ADDRESS --> OFFSET
         ST    R0,#LISTN1               OFFSET IN LOAD MODULE
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR       ENTRY ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         CLI   ESDTYPE,CODESD           CSECT ENTRY?
         BE    LIST810                  YES, BRANCH
         TM    ESDTYPE,CODEPC           PRIVATE CODE OR OVERLAY?
         BO    LIST810                  YES, BRANCH
         L     R15,ESDCSECT             ADDRESS OF MAIN ENTRY
         MVC   MSGTEXT2+21(8),4(R15)    CSECT NAME
         MVC   MSGTEXT2+31(6),=C' ENTRY' ENTRY WITHIN A CSECT
         MVC   MSGTEXT2+38(8),ESDNAME
         AR    R0,R1
         ICM   R1,B'0111',ESDMAIN+1     CSECT START ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         SPACE 2
LIST810  ICM   R0,B'0111',ESDADDR       ENTRY ADDRESS
         LR    R15,R0                   SAVE THE ENTRY ADDRESS
         A     R0,ESDLEN-1              LAST ADDRESS TO BE ACCESSED
LIST814  LTR   R2,R2                    AT END OF LIST?
         BZ    LIST820                  YES, BRANCH
         ICM   R2,B'1111',ESDLINK       ANOTHER ESD ENTRY?
         ST    R2,#LISTENT
         BZ    LIST820                  NO, BRANCH
         CLC   #LISTID(2),ESDID         ENTRY FOR THIS CSECT?
         BNE   LIST820                  NO, USE THE CURRENT ENTRY
         CLM   R15,B'0111',ESDADDR      START:NEXT START
         BNL   LIST814                    HIGH OR EQUAL, DISCARD IT
         CLM   R0,B'0111',ESDADDR       END:NEXT START
         BNH   LIST820                    LOW OR EQUAL, USE THIS LIMIT
         ICM   R0,B'0111',ESDADDR         HIGH, USE THE LOWER LIMIT
         SPACE 1
LIST820  LR    R1,R0                    CSECT OR ENTRY END
         SR    R1,R15                   CSECT OR ENTRY LENGTH
         C     R1,#MAXLEN               ABOVE MAXIMUM CSECT LENGTH?
         BL    *+8                      NO, BRANCH
         L     R1,#MAXLEN               YES, USE THE REQUESTED MAXIMUM
         AR    R1,R15                   END OF CSECT OR ENTRY
         A     R1,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         ST    R1,#LISTE1               SAVE END ADDRESS
         SPACE 2
*  CHECK FOR END OF CSECT OR EXTERNAL LABEL
LIST830  LR    R1,R4                    DATA LENGTH
         LR    R15,R5
         SR    R15,R6                   MACHINE LENGTH VALID?
         BM    LIST100                  NO, IGNORE THIS RECORD
         CR    R1,R15
         BNH   *+8
         LA    R1,1(,R15)               R1=MIN(16 OR 64,LENGTH LEFT)
         AR    R1,R6                    END OF THIS LINE
         C     R1,#LISTE1               END ON THIS LINE?
         BLR   R14                      NO, RETURN
         OI    #LISTFLG,X'08'           NEW HEADER IS NEEDED
         L     R4,#LISTE1               MAXIMUM ADDRESS OF CSECT
         SR    R4,R6                    DISPLAY LENGTH VALID?
         BPR   R14                      YES, BRANCH
         LTR   R2,R2                    AT END OF LIST?
         BZ    LIST100                  YES, IGNORE THIS MEMBER
         B     LIST804                  NO, TRY THE NEXT CSECT
         SPACE 3
LIST900  MVI   ##SUBCOM+4,X'FF'         END OF FILE ENCOUNTERED
LIST910  CLI   ##SUBCOM+3,C'D'          FIND AND NO OUTPUT?
         BNE   LIST920                  NO, BRANCH
         LA    R1,=C'ELSE'
         ICM   R2,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    LIST980                  YES, BRANCH
         TM    FLAGSAA,FMEM#MEM         MEMBER GROUP IN PROGRESS?
         BO    NEWCMD                   YES, DONE
         B     LIST940                  NO, BRANCH
         SPACE 1
LIST920  TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP SEGMENT FORMAT?
         BZ    LIST940                         NO, BRANCH
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         SPACE 1
LIST940  CLI   ##SUBCOM+4,X'FF'         END OF FILE ENCOUNTERED?
         BNE   NEWCMD                   NO, DONE
         MVC   MSGTEXT1,MSGBLK
         L     R1,#BLKCNT
         CVD   R1,DOUBLE
         ED    MSGTEXT1+MSGBLKC-MSGBLK(10),DOUBLE+4
         LA    R1,MSGTEXT1
         TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BO    LIST960                  YES, BRANCH
         TM    FLAGSDD,DUMP+BLOCK
         BNZ   LIST960
         MVC   MSGTEXT1+MSGBLKL-MSGBLK(6),=C' LINES'
LIST960  TM    DSORG,DS1DSGPO
         BO    MSGNEW
         MVC   MSGTEXT1+MSGDATAS-MSGBLK(8),=C'DATA SET'
         B     MSGNEW
         SPACE 2
LIST980  MVC   FINDMEMQ+25(4),0(R1)     ADD "THEN" OR "ELSE"
         MESSAGE MSGBLANK               OUTPUT A BLANK LINE
         TM    FLAGSAA,FINDMSG          ANY MESSAGE TO OUTPUT?
         BNO   LIST990                  NO, BRANCH
         XI    FLAGSAA,FINDMSG          NO MORE MESSAGE
         MVC   FINDMEMQ+30(8),0(R2)     SUBCOMMAND TO EXECUTE
         MESSAGE FINDMEMQ               IDENTIFICATION LINE
         SPACE 1
LIST990  MVC   ##ANSWER(LISUBS),ISUBS   INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(20),0(R2)       CHANGE THE SUBCOMMAND
         B     CALLCMDZ                 CALL THE SECONDARY SUBCOMMAND
         SPACE 3
         PRINT NOGEN
MSGFINDN MSG   'THE FIRST FIND SUBCOMMAND MUST CONTAIN A STRING'
MSGDUMP  MSG   'BLOCK  RECORD 1,234,567   LENGTH 12,345   TTR 123456 '
MSGDUMPB EQU   MSGDUMP+4,5         "DUMP "
         ORG   MSGDUMP+17
MSGDUMPR DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   MSGDUMP+36
MSGDUMPL DC    X'4020206B202120'         EDIT PATTERN
MSGDUMPT EQU   MSGDUMP+50,6              TTR OUTPUT
         ORG   ,
MSGLDUMP MSG   'AT 123456  CSECT 12345678  LENGTH 123456  '
MSGBLK   MSG   ' -- 1,234,567 BLOCKS IN THIS MEMBER  '
         ORG   MSGBLK+7
MSGBLKC  DC    X'40206B2020206B202120'   EDIT PATTERN
MSGBLKL  EQU   MSGBLK+18,6               " LINES"
MSGDATAS EQU   MSGBLK+33,7               "DATA SET"
         ORG   ,
         PRINT GEN
         TITLE 'P D S  --  PDS MAP                            02/06/84'
***********************************************************************
***      MAP SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
MAP      CSECT
         USING *,R8
         LA    R1,MSGNLIB       ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         SPACE 1
         LA    R1,MSGNLOAD      ASSUME NOT A LOAD LIBRARY
         TM    FLAGSCC,RECFMU   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         MVI   ENTRYPT,C'?'     NO ENTRY POINT YET
         SPACE 1
         L     R15,=A(READESD)  FORMAT THE ESD DATA
         BALR  R14,R15          ANY ESD DATA?
         B     MAPNOESD         NO, BRANCH
         SPACE 1
         LA    R4,#ESDPTR       ESD TABLE CHAIN POINTER
         USING ESDENTRY,R4      BASE FOR TABLE
         TM    FLAGSAA,FINDMSG  ANY GROUP HEADER?
         BNO   MAPESD           NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE FINDMEMQ
         NI    FLAGSAA,FF-FINDMSG
         SPACE 2
MAPESD   CLEAR MSGTEXT1
         ICM   R4,B'1111',ESDLINK    END OF CHAIN?
         BZ    MAPLAST               YES, BRANCH
         SPACE
         CLI   ESDTYPE,CODELR        EXTERNAL REFERENCE?
         BE    MAPLR                 YES
         SPACE 2
         CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+4(8),ESDNAME
         UNPK  MSGTEXT1+14(7),ESDADDR(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVI   MSGTEXT1+20,X'40'     CLEAR GARBAGE BYTE
         UNPK  MSGTEXT1+22(7),ESDLEN(4)
         TR    MSGTEXT1+22(6),TRTABLE
         MVI   MSGTEXT1+28,X'40'     CLEAR GARBAGE BYTE
         LA    R1,50
         SPACE 1
         TM    DIRATTR,ATTROVLY      OVERLAY PROGRAM?
         BZ    MAPNOVLY              NO, BRANCH
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
         B     MAPPRINT
         SPACE 1
MAPNOVLY CLI   ESDXAFLG,AMODE24          RMODE 24 AND AMODE 24?
         BNH   MAPPRINT                  NO, BRANCH
         MVC   MSGTEXT1+30(5),=C'RMODE'
         MVC   MSGTEXT1+36(3),=C'ANY'
         TM    ESDXAFLG,RMODEANY         RMODE ANY?
         BO    *+10                      YES, BRANCH
         MVC   MSGTEXT1+36(3),=C'24 '    RMODE 24
         MVC   MSGTEXT1+40(5),=C'AMODE'
         MVC   MSGTEXT1+46(3),=C'24 '
         TM    ESDXAFLG,AMODE31          AMODE 31?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'31 '    AMODE 31
         TM    ESDXAFLG,AMODE24+AMODE31  AMODE ANY?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'ANY'    AMODE ANY
         B     MAPPRINT
         SPACE 2
MAPLR    CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+56(8),ESDNAME
         UNPK  MSGTEXT1+66(7),ESDADDR(4)
         TR    MSGTEXT1+66(6),TRTABLE
         LA    R1,72
         SPACE
MAPPRINT SLL   R1,16
         ST    R1,MSGTEXT1
         MESSAGE MSGTEXT1
         B     MAPESD
         SPACE 2
MAPLAST  MVC   MSGTEXT1(8),MSGBLANK
MAPNOESD TM    FLAGSAA,FMEM#MEM+FINDMSG  GROUP HEADER TO BE WRITTEN?
         BNO   MAPNOMSG                  NO, BRANCH
         CLI   #MODLEN,0                 ANY FILTERING?
         BH    NEWCMD                    NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE FINDMEMQ
         SPACE 1
MAPNOMSG MESSAGE MSGTEXT1
         MVC   MSGTEXT1,MSGENTRY   ENTRY POINT ADDRESS AND SYMBOL
         UNPK  MSGTEXT1+MSGENTR1-MSGENTRY(7),DIREPA(4)
         TR    MSGTEXT1+MSGENTR1-MSGENTRY(6),TRTABLE
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         MVC   0(6,R14),=C'  --  '
         MVC   6(8,R14),ENTRYPT    NAME OF THE ENTRY POINT
         CLI   ENTRYPT,C'?'        ANY FOUND?
         BE    *+8                 NO, BRANCH
         LA    R15,14(,R15)        YES, SHOW ENTRY SYMBOL
         STH   R15,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE 1
         MVC   MSGTEXT1,MSGLEN
         UNPK  MSGTEXT1+MSGLEN1-MSGLEN(7),DIRSTORE(4)
         TR    MSGTEXT1+MSGLEN1-MSGLEN(6),TRTABLE
         MVI   MSGTEXT1+MSGLEN1-MSGLEN+6,X'40'
         SPACE 1
         ICM   R1,B'0111',DIRSTORE ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)        NEXT HIGHER
         SRL   R1,10                          1K VALUE
         CVD   R1,DOUBLE
         ED    MSGTEXT1+MSGLEN2-MSGLEN(6),DOUBLE+5
         MESSAGE MSGTEXT1
         SPACE
         TM    DIRFLAG,X'80'      IS MODULE AN ALIAS?
         BZ    NEWCMD             NO, NO ALIAS INFORMATION
         CLI   #MAPOPT,X'01'      SHORT OR ENTRY POINT MAP?
         BH    NEWCMD             YES, DONE
         TM    FLAGSGG,FALINCON   NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD             YES, DONE
         EJECT
*        FIND THE CORRESPONDING REAL MODULE
         MVI   STARTTR+2,X'01'    TTR=000001 (START OF DIRECTORY)
         SPACE 1
MAPAREAL BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAPNREAL           LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    MAPAREAL           YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAPAREAL           NO, BRANCH
         SPACE
         MVC   MSGTEXT1,MSGREAL
         MVC   MSGTEXT1+MSGREAL1-MSGREAL(8),MEMNAME
         TR    MSGTEXT1+MSGREAL1-MSGREAL(8),TRLINE
         MESSAGE MSGTEXT1
         B     MAPAREAL
         SPACE 2
MAPNREAL CLC   MSGTEXT1(20),MSGREAL  ANY REAL(S) FOUND?
         BE    NEWCMD                YES, QUIT
         MESSAGE MSGNOMOD
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   MSGTEXT1,MSGXREAL  DIRECTORY NAME OF REAL MODULE
         LA    R1,MSGTEXT1
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREALS
         TR    MSGTEXT1+MSGXREA1-MSGXREAL(8),TRLINE
         B     MSGNEW
         SPACE
         DROP  R4
         TITLE 'P D S  --  PDS MAP ESD SCAN SUBROUTINE        02/06/84'
*
*        ESD SCAN SUBROUTINE
*
         SPACE 2
READESD  STM   R14,R12,ESDIDRSV   SAVE REGISTERS FOR RETURN
         LR    R8,R15             ENTRY POINT ADDRESS
         USING READESD,R8         SUBROUTINE BASE REGISTER
         LA    R2,#ESDPTR         ROOT OF ESD CHAIN
         MVC   STARTTR(3),DIRTTR  FIRST TTR
         SPACE 1
ESDEXCP  BAL   R14,EXCP
         B     *+4(R15)           PROCESS RETURN CODE
         B     ESDEXCP1             00 - GOOD READ
         B     ESDLAST              04 - END OF MEMBER
         B     ESDLAST              08 - END OF DATA SET
         B     NEWCMD               12 - I/O ERROR
         SPACE
ESDEXCP1 LR    R3,R0              START OF RECORD
         CLI   0(R3),X'40'        TEST SYM RECORD?
         BE    ESDEXCP            YES, SKIP RECORD
         CLI   0(R3),X'20'        CESD RECORD?
         BNE   ESDLAST            NO, BRANCH
         SPACE
         LH    R6,4(,R3)          RELATIVE # OF 1ST ESD ID
         LH    R5,6(,R3)          LENGTH OF DATA IN BUFFER
         LA    R3,8(,R3)          START OF ESD DATA
         AR    R5,R3
         LA    R4,16              LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
         USING ESDNAME,R3
ESDSCAN  IC    R0,ESDTYPE
         LA    R1,CODESEG         CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   #MAPOPT,X'03'      ENTRY POINT ONLY?
         BE    ESDSCAN2           YES, BRANCH
         CLI   ##SUBCOM,C'A'      ALIAS SEARCH REQUEST?
         BNE   ESDMAP             NO, BRANCH
ESDSCAN2 CLI   ESDTYPE,CODESD     VALID EXTERNAL SYMBOL?
         BE    ESDCHK             YES, BRANCH
         CLI   ESDTYPE,CODELR     ANOTHER VALID ENTRY
         BNE   NEXTESD            NO, GET THE NEXT ESD
         SPACE 1
ESDCHK   TM    DIRATTR,ATTROVLY   OVERLAY ATTRIBUTE?
         BNO   ESDCHK1            NO, BRANCH
         CLI   ESDSEG#,1          SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD            NO, IGNORE
ESDCHK1  CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT ADDRESS?
         BNE   ESDCHK2            NO, BRANCH
         MVC   ENTRYPT,ESDNAME    YES, ENTRY POINT NAME IF NO ESD MATCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    ESDUSE3            YES, BRANCH
ESDCHK2  CLC   ESDNAME,DIRNAME    THIS NAME?
         BNE   NEXTESD            NO, BRANCH
         IC    R0,ESDXAFLG        GET MVS/XA ESD FLAGS
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR GET SYMBOL OFFSET
         ST    R6,ESDIDRSV+4      RETURN ESDID IN REG 15
         STM   R0,R1,ESDIDRSV+4+4 RETURN ESD FLAGS, OFFSET IN REGS 0,1
         MVC   ENTRYPT,ESDNAME    USE THIS ESDNAME FOR THE ENTRY POINT
         B     ESDOUT             FOUND THE SYMBOL
         SPACE
ESDMAP   CR    R0,R1              THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1            YES, BRANCH
         CLI   ESDTYPE,CODESD     SD ENTRY?
         BE    ESDUSE2            YES, BRANCH
         CLI   ESDTYPE,CODECM     COMMON BLOCK CODE?
         BE    ESDUSE2            YES, BRANCH
         CLI   ESDTYPE,CODELR     LR ENTRY?
         BNE   ESDMAPPC           NO, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BL    ESDUSE2            NO, BRANCH
         CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT?
         BE    ESDUSE2            NO, BRANCH
ESDMAPPC CLI   ESDTYPE,CODEPC     PC ENTRY?
         BNE   NEXTESD            NO, BRANCH
         MVC   ESDNAME,=CL8'$PRIVATE'  PRIVATE CODE
         B     ESDUSE2
         SPACE 1
ESDUSE1  STC   R0,ESDTYPE                          RESTORE OVERLAY TYPE
         MVC   ESDNAME(8),=CL8'$SEGTAB'            ASSUME SEGMENT TABLE
         OC    ESDADDR(3),ESDADDR                  CORRECT?
         BZ    ESDUSE2                             YES, BRANCH
         MVC   ESDNAME(8),=CL8'$ENTAB'             NO, ENTRY TABLE
ESDUSE2  SR    R1,R1
         CLI   ##SUBCOM,C'L'                       LIST SUBCOMMAND?
         BE    ESDUSE3                             YES, BRANCH
         ICM   R1,B'0001',#MODLEN                  ANY MODULE FILTER?
         BZ    ESDUSE3                             NO, BRANCH
         BCTR  R1,0                                MACHINE LENGTH
         CLC   ESDNAME(*-*),#MODTXT                <<EXECUTED>>
         EX    R1,*-6                              THIS PARTIAL NAME?
         BNE   NEXTESD                             NO, BRANCH
ESDUSE3  BAL   R14,GETESD
         ST    R1,ESDLINK-ESDENTRY(R2)             SAVE LINK POINTER
         STH   R6,ESDID-ESDENTRY(R1)               SAVE ESD ID
         MVC   ESDNAME-ESDENTRY(LENESD1,R1),0(R3)  SAVE ESD ENTRY
         LR    R2,R1                               CHAIN TO NEXT ONE
NEXTESD  LA    R6,1(R6)
         BXLE  R3,R4,ESDSCAN
         B     ESDEXCP            GET NEXT ESD RECORD
         DROP  R3
         EJECT
         USING ESDENTRY,R2
ESDLAST  MVC   MSGTEXT1,MSGNOESM
         L     R14,ESDIDRSV
         LA    R2,#ESDPTR
         OC    #ESDPTR,#ESDPTR    ANY ESD DATA AVAILABLE?
         BNZ   ESDSORT            YES, BRANCH
         CLI   #MODLEN,0          ANY ESD FILTERING?
         BH    ESDEXIT            YES, BRANCH
         MVC   MSGTEXT1,MSGNOESD
         B     ESDEXIT
         SPACE 1
ESDSORT  L     R2,ESDLINK
         ICM   R1,B'1111',ESDLINK END OF ESD ENTRIES?
         BZ    ESDOUT             YES, BRANCH
         SPACE
ESDSORT2 TM    DIRATTR,ATTROVLY             OVERLAY ATTRIBUTE?
         BNO   ESDNOSEG
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE
ESDNOSEG CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE
ESDSWAP  XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE
ESDSORT3 ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BNZ   ESDSORT2
         B     ESDSORT
         DROP  R2
         SPACE 2
GETESD   MVI   SUBPOOLT,21
         LA    R0,LENESD
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 1
ESDOUT   L     R14,ESDIDRSV
         LA    R14,4(,R14)         ADJUST EXIT ADDRESS
ESDEXIT  LM    R15,R12,ESDIDRSV+4
         BR    R14                 EXIT
         TITLE 'P D S  --  PDS OPTIONS                        02/06/84'
***********************************************************************
***      OPTIONS SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 2
OPTIONS  CSECT
         USING *,R8
         LA    R1,MSGAVAIL            AVAILABLE OPTIONS HEADER
         LTR   R3,R2                  RETURN=0?
         BNZ   *+8                    NO, BRANCH
         LA    R1,MSGUNKN             YES, INVALID COMMAND
         MESSAGE (R1)
         SPACE 1
         LA    R2,OPTNTBL             START OF OPTIONS TABLE
         LTR   R3,R3                  RETURN=0?
         BNZ   OPTIONS2               NO, BRANCH
         LA    R2,MSGCMDSL            START OF AVAILABLE COMMAND LIST
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BO    OPTIONS2               YES, BRANCH
         LA    R2,MSGCMDSS            NO, USE A DIFFERENT LIST
         B     OPTIONS2
         SPACE
OPTIONS2 CLI   0(R2),C'S'             SHORT OPTIONS TABLE MESSAGE?
         BE    OPTIONS4               YES, BRANCH
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   OPTIONS6               NO, BRANCH
         BCTR  R2,0                   YES, PREPARE FOR NEXT LA
OPTIONS4 LA    R2,1(,R2)              NEXT OPTION
         MESSAGE (R2)
OPTIONS6 LH    R1,0(,R2)              NEXT OPTION
         AR    R2,R1                  NEXT OPTION
         CLI   0(R2),X'FF'            DONE?
         BNE   OPTIONS2               NO, BRANCH
         B     NEWCMD
         TITLE 'P D S  --  PDS OPTIONS OUTPUT MESSAGES        02/06/84'
         PRINT NOGEN
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF200
MSGAVAIL MSG   'THE FOLLOWING OPTIONS ARE AVAILABLE:'
OPTNTBL  MSG   'ATTRIB   - LIST ATTRIBUTES OF A MEMBER'
         MSG   'ALIAS    - ASSIGN AN ALIAS TO A MEMBER'
         MSG   'BROWSE   - BROWSE USING SPF',S
         MSG   'CHANGE   - SELECT A NEW DATA SET',S
         MSG   'CONTROL  - SET PROGRAM CONTROLS',S
         MSG   'DISPLAY  - DISPLAY DIRECTORY NAMES'
         MSG   'DIRENTRY - DUMP A DIRECTORY ENTRY'
         MSG   'DELETE   - DELETE A MEMBER'
         MSG   'EDIT     - EDIT USING TSO EDIT',S
         MSG   'END      - STOP THE PROGRAM',S
         MSG   'EXEC     - EXECUTE PDS SUBCOMMANDS FROM A CLIST',S
         MSG   'FIND     - LIST LINES CONTAINING A STRING',S
         MSG   'FIXPDS   - MODIFY DATA SET ATTRIBUTES'
         MSG   'FSE      - EDIT USING FSE',S
         MSG   'HELP     - DISPLAY PDS SUBCOMMAND HELP',S
         MSG   'HISTORY  - LIST HISTORY OF A LOAD MODULE'
         MSG   'IF       - CONDITIONALLY EXECUTE A PDS SUBCOMMAND'
         MSG   'LIST     - DISPLAY A MEMBER',S
         MSG   'MAP      - MAP A LOAD MODULE'
         MSG   'MEMBERS  - DISPLAY A MEMBER GROUP'
         MSG   'OPTIONS  - DISPLAY THIS MENU',S
         MSG   'OUTCOPY  - OUTPUT IEBCOPY MEMBER SELECT STATEMENTS'
         MSG   'PATTERN  - DISPLAY DIRECTORY NAMES MATCHING A PATTERN'
         MSG   'PRINTOFF - PRINT A HARDCOPY',S
         MSG   'RECALL   - DISPLAY THE PREVIOUS SUBCOMMAND',S
         MSG   'RENAME   - RENAME A MEMBER'
         MSG   'RESTORE  - RESURRECT A PREVIOUSLY DELETED MEMBER'
         MSG   'REVIEW   - BROWSE DATA',S
         MSG   'SPFEDIT  - EDIT USING SPF',S
         MSG   'SUBMIT   - SUBMIT JCL',S
         MSG   'TSO      - INVOKE A TSO COMMAND',S
         MSG   'TSOLIST  - LIST DATA WITH TSO LIST',S
         MSG   'USAGE    - LIST DATA SET STATISTICS',S
         MSG   'VERIFY   - VALIDITY CHECK THE DATA SET',S
         DC    X'FF'
         EJECT
MSGUNKN  MSG 'INVALID OPTION, ENTER "HELP" OR "OPTIONS" FOR HELP'
MSGCMDSL MSG 'SUBCOMMANDS:  ATTRIB   ALIAS    BROWSE   CHANGE   CONTROLX
                 DISPLAY  DIRENTRY'
         MSG '              DELETE   EDIT     END      EXEC     FIND   X
                 FIXPDS   FSE'
         MSG '              HELP     HISTORY  IF       LIST     MAP    X
                 MEMBERS  OPTIONS'
         MSG '              OUTCOPY  PATTERN  PRINTOFF RECALL   RENAME X
                 RESTORE  REVIEW'
         MSG '              SPFEDIT  SUBMIT   TSO      TSOLIST  USAGE  X
                 VERIFY'
         MSG '  '
         DC  X'FF'
         SPACE 3
MSGCMDSS MSG 'SUBCOMMANDS:  BROWSE   CHANGE   CONTROL  EDIT     END    X
                 EXEC     FIND',S
         MSG '              FSE      HELP     LIST     OPTIONS  PRINTOFX
               F RECALL   REVIEW',S
         MSG '              SPFEDIT  SUBMIT   TSO      TSOLIST  USAGE  X
                 VERIFY',S
         MSG '  ',S
         DC  X'FF'
         AGO   .YESSPFC
.NSPF200 ANOP
MSGAVAIL MSG   'THE FOLLOWING OPTIONS ARE AVAILABLE:'
OPTNTBL  MSG   'ATTRIB   - LIST ATTRIBUTES OF A MEMBER'
         MSG   'ALIAS    - ASSIGN AN ALIAS TO A MEMBER'
         MSG   'CHANGE   - SELECT A NEW DATA SET',S
         MSG   'CONTROL  - SET PROGRAM CONTROLS',S
         MSG   'DISPLAY  - DISPLAY DIRECTORY NAMES'
         MSG   'DIRENTRY - DUMP A DIRECTORY ENTRY'
         MSG   'DELETE   - DELETE A MEMBER'
         MSG   'EDIT     - EDIT USING TSO EDIT',S
         MSG   'END      - STOP THE PROGRAM',S
         MSG   'EXEC     - EXECUTE PDS SUBCOMMANDS FROM A CLIST',S
         MSG   'FIND     - LIST LINES CONTAINING A STRING',S
         MSG   'FIXPDS   - MODIFY DATA SET ATTRIBUTES'
         MSG   'FSE      - EDIT USING FSE',S
         MSG   'HELP     - DISPLAY PDS SUBCOMMAND HELP',S
         MSG   'HISTORY  - LIST HISTORY OF A LOAD MODULE'
         MSG   'IF       - CONDITIONALLY EXECUTE A PDS SUBCOMMAND'
         MSG   'LIST     - DISPLAY A MEMBER',S
         MSG   'MAP      - MAP A LOAD MODULE'
         MSG   'MEMBERS  - DISPLAY A MEMBER GROUP'
         MSG   'OPTIONS  - DISPLAY THIS MENU',S
         MSG   'OUTCOPY  - OUTPUT IEBCOPY MEMBER SELECT STATEMENTS'
         MSG   'PATTERN  - DISPLAY DIRECTORY NAMES MATCHING A PATTERN'
         MSG   'PRINTOFF - PRINT A HARDCOPY',S
         MSG   'RECALL   - DISPLAY THE PREVIOUS SUBCOMMAND',S
         MSG   'RENAME   - RENAME A MEMBER'
         MSG   'RESTORE  - RESURRECT A PREVIOUSLY DELETED MEMBER'
         MSG   'REVIEW   - BROWSE DATA',S
         MSG   'SUBMIT   - SUBMIT JCL',S
         MSG   'TSO      - INVOKE A TSO COMMAND',S
         MSG   'TSOLIST  - LIST DATA WITH TSO LIST',S
         MSG   'USAGE    - LIST DATA SET STATISTICS',S
         MSG   'VERIFY   - VALIDITY CHECK THE DATA SET',S
         DC    X'FF'
         SPACE 1
MSGUNKN  MSG 'INVALID OPTION, ENTER "HELP" OR "OPTIONS" FOR HELP'
MSGCMDSL MSG 'SUBCOMMANDS: ATTRIB, ALIAS, CHANGE, CONTROL, DISPLAY,'
         MSG '  DIRENTRY, DELETE, EDIT, END, EXEC, FIND, FIXPDS,'
         MSG '  FSE, HELP, HISTORY, IF, LIST, MAP, MEMBERS, OPTIONS,'
         MSG '  OUTCOPY, PATTERN, PRINTOFF, RECALL, RENAME, RESTORE,'
         MSG '  REVIEW, SUBMIT, TSO, TSOLIST, USAGE AND VERIFY'
         DC  X'FF'
         SPACE 1
MSGCMDSS MSG 'SUBCOMMANDS: CHANGE, CONTROL, EDIT, END, EXEC, FIND,',S
         MSG '   FSE, HELP, LIST, OPTIONS, PRINTOFF, RECALL, REVIEW,',S
         MSG '   SUBMIT, TSO, TSOLIST, USAGE AND VERIFY',S
         DC    X'FF'
.YESSPFC ANOP
         PRINT GEN
         TITLE 'P D S  --  PDS OUTCOPY                        02/06/84'
***********************************************************************
***      OUT SUBCOMMAND        ADDED BY BRUCE LELAND -- JULY, 1983  ***
***********************************************************************
*
         SPACE 1
OUTCOPY  CSECT
         USING *,R8
         LA    R1,MSGNLIB                    ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO                CORRECT?
         BZ    MSGNEW                        YES, BRANCH
         CLI   #OUT+1,2                      NORECORD?
         BNE   *+8                           NO, BRANCH
         NI    FLAGSFF,FF-FOUTSAVE           YES, TURN OFF FLAG
         TM    #OUT,1                        CLOSE FORCED?
         BO    OUT84                         YES, BRANCH
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT ALREADY OPEN?
         BO    OUT10                         YES, BRANCH
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF250
         TM    FLAGSEE,FBKGRND               BACKGROUND MODE?
         BO    OUT04                         YES, BRANCH
         TM    FLAGSFF,FSPFOPT6+FSPFERR+FSPFCALL+FSPFDIAL  CHANGE SPF?
         BNZ   OUT04                                       NO, BRANCH
         OI    FLAGSFF,FSPFERR               SPF ERROR FLAG
         MVI   ##ADRCM#,CONTINUE+EDITOR      RECURSIVE CALL FROM ISPF
         BAL   R2,CLOSEIT                    NEED TO CLOSE BEFORE SPF
         BAL   R2,DEALLDCB                   AND FREE THE DATA SET
         MVC   ISPFTEXT+4(L'ISPF),ISPF       ISPF   PGM(PDS45678) P...
         MVC   ISPFTEXT+ISPFPGM+4(8),PDSNAME PDS COMMAND NAME
         CVD   R7,ISPFTEXT+4+ISPFPARM        ADD STORAGE ADDRESS
         LA    R1,L'ISPF+4                   LENGTH OF STRING
         SLL   R1,16                         CLEAR BOTTOM TWO BYTES
         ST    R1,ISPFTEXT                   SAVE STRING TOTAL LENGTH
*        MESSAGE ISPFTEXT                    DELETE * TO OUTPUT COMMAND
         SPACE 1
         MVI   ISPFTEXT+3,8                  OFFSET OF PGM(...
         LA    R1,ISPFTEXT                   START OF PARAMETER LIST
         ST    R1,ADDRTEXT                   COMMAND ADDRESS
         LOAD  EP=ISPLINK                    ADDRESS OF SPF INTERFACE
         ST    R0,ISPLINK                    SAVE ADDRESS FOR LATER
         SPACE 1
         XI    FLAGSFF,FSPFERR+FSPFCALL      SPF ERROR OFF; SPF CALL ON
         LA    R1,ADDRTEXT                   START OF CPPL PARAMETERS
         ATTACH EPLOC=ISPF,ECB=SPFECB,SZERO=NO,SHSPV=78,               X
               SF=(E,ATTACHS1)
         ST    R1,SPFTCB                     SUBTASK TCB ADDRESS
         WAIT  ECB=SPFECB                    WAIT FOR COMPLETION
         DETACH SPFTCB                       REMOVE THIS SUBTASK
         NI    FLAGSFF,FF-FSPFCALL           SPF FLAG TURNED OFF
         SR    R5,R5
         CLC   ##SUBCOM(8),ENDLIT            END SUBCOMMAND?
         BE    RETURN8                       YES, DONE
         OI    FLAGSFF,FSPFERR               SPF ERROR
         B     RESTART                       REALLOCATE AND REOPEN
         SPACE 1
.NSPF250 ANOP
OUT04    ESTAE 0                             CANCEL THE ESTAE ADDRESS
         MVC   STAEPARM(4),STAELIST          RESET THE ESTAE ADDRESS
         ESTAE STAEEXIT,CT,PARAM=(R7),MF=(E,STAEPARM)
         SPACE 1
         MVC   QAMDCB(LQSAMDCB),QSAMDCB      CONSTRUCT A DCB
         MVI   OPENLIST,X'80'                END OF LIST
         OPEN  (QAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         LA    R1,MSGQOPEN                   ASSUME OPEN ERROR
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT OPEN?
         BNO   MSGNEWXX                      NO, ERROR
         CLI   #OUT,1                        CORRECT DCB ATTRIBUTES?
         BNE   OUT84                         NO, BRANCH
         OI    FLAGSFF,FOUTNO37              OUTPUT CAN OCCUR NOW
         SPACE 2
OUT10    CLI   #OUT+1,1                      RECORD STATUS:
         BL    OUT20                            NO CHANGE, BRANCH
         BH    NEWCMD                           NORECORD, DONE
         OI    FLAGSFF,FOUTSAVE                 RECORD, SET FLAG
         B     NEWCMD
         SPACE 2
OUT20    MVI   #OUT,X'40'
         TM    FLAGSFF,FOUTNO37              PREVIOUS SPACE ABEND?
         BNO   OUT84                         YES, CLOSE THE DATA SET
         L     R2,RECOVER                    PREVIOUS RECOVERY ADDRESS
         LA    R1,OUT80                      RESUME ADDRESS
         ST    R1,RECOVER                    IN-LINE RECOVERY
         STM   R2,R8,#OUTREGS                SAVE REGISTERS
         TM    FLAGSFF,FCHANGE               CHANGED DATA SET?
         BNZ   OUT50                         NO, BRANCH
         OI    FLAGSFF,FCHANGE               YES, GOT A COPY STATEMENT
         MVC   #OUT,OUTCOPYS                 " COPY OUTPUT=OUTDD,IN..."
         SR    R1,R1                         MACHINE LENGTH
         LA    R3,DSNAME-2
         AH    R3,DSNLEN
         LA    R0,8                          MAX LOOPS +1
         CH    R0,DSNLEN                     MIN (8, NAME LENGTH)
         BH    *+8
         LH    R0,DSNLEN
OUT30    S     R0,=F'1'                      ONE CHARACTER?
         BZ    OUT40                         YES, BRANCH
         CLI   0(R3),C'.'                    PERIOD?
         BE    OUT40                         YES, BRANCH
         LA    R1,1(,R1)                     MACHINE LENGTH
         BCT   R3,OUT30                      BACK UP ONE BYTE
         SPACE 1
OUT40    MVC   #OUT+33(*-*),1(R3)            <<EXECUTED>>
         EX    R1,*-6                        MOVE LOW-LEVEL QUALIFIER
         PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         LA    R3,OUTMEMBS                   START OF DUPLICATE LIST
         TM    FLAGSFF,FOUTECHO              OUTPUT TO THE TERMINAL?
         BNO   OUT42                         NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE 1
OUT42    ICM   R3,B'0111',1(R3)              NEXT DUPLICATE STRUCTURE
         BZ    OUT50                         END, BRANCH
         MVI   6(R3),0                       CLEAR THE
         MVI   7(R3),1                                CURRENT COUNT
         B     OUT42                         LOOP FOR ALL STRUCTURES
         SPACE 1
OUT50    LA    R4,OUTMEMBS                   ROOT OF DUP STRUCTURES
OUT54    LR    R3,R4
         ICM   R4,B'0111',1(R4)              NEXT LIST ENTRY?
         BNZ   OUT60                         YES, BRANCH
         LA    R5,256                        256 -1 MEMBERS
         LR    R0,R5
         SLL   R0,3                          ELEMENT WIDTH IS 8
         MVI   OUTMEMBS,22                   SUBPOOL IS 22
         ICM   R0,B'1000',OUTMEMBS           GET THE SUBPOOL
         GETMAIN R,LV=(0)
         LR    R4,R1
         STCM  R4,B'0111',1(R3)              CHAIN TO LAST STUCTURE
         XC    0(8,R4),0(R4)                 NULL LINK POINTERS
         STH   R5,4(,R4)                     MAXIMUM ELEMENTS/STRUCTURE
         MVI   7(R4),1                       ONE ITEM CURRENTLY
         B     OUT70
         SPACE 2
OUT60    LH    R5,6(R4)                      NUMBER OF ELEMENTS
         LR    R1,R4
OUT64    S     R5,=F'1'                      ANY MORE ITEMS?
         BZ    OUT70                         NO, BRANCH
         LA    R1,8(,R1)                     NEXT ELEMENT
         CLC   0(8,R1),DIRNAME               THIS ELEMENT?
         BNE   OUT64                         NO, BRANCH
         ST    R2,RECOVER                    YES, IGNORE DUPLICATE
         B     NEWCMD
         SPACE 1
OUT70    CLC   6(2,R4),4(R4)                 CURRENT:MAXIMUM
         BE    OUT54                         EQUAL - GET NEXT STRUCTURE
         MVC   8(8,R1),DIRNAME               ADD THIS ITEM
         LH    R5,6(R4)                      ADD
         LA    R5,1(,R5)                        1 TO
         STH   R5,6(R4)                             NUMBER OF ELEMENTS
         MVC   #OUT,OUTSEL                   "          S M=12345678  "
         MVC   #OUT+14(8),DIRNAME            MEMBER NAME
         LA    R1,#OUT+14                    POINT TO EQUALS
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         MVI   1(R1),C','                    ADD BLANK AND COMMA
         MVC   2(8,R1),DIRNAME               MEMBER NAME AGAIN
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         MVI   0(R1),C')'                    ADD ONE PARENTHESIS
         MVI   1(R1),C')'                    ADD SECOND PARENTHESIS
         PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         ST    R2,RECOVER                    RESET RECOVERY ADDRESS
         TM    FLAGSFF,FOUTECHO              OUTPUT TO THE TERMINAL?
         BNO   NEWCMD                        NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         MESSAGE MSGTEXT1
         B     NEWCMD
         SPACE 2
OUT80    LM    R2,R8,#OUTREGS                RESET REGISTERS
         ST    R2,RECOVER                    RESTORE RECOVERY ADDRESS
         SPACE 1
OUT84    MVI   OPENLIST,X'80'                END OF LIST
         CLOSE (QAMDCB),MF=(E,OPENLIST)      CLOSE OUT DATA SET
         NI    FLAGSFF,FF-FCHANGE-FOUTSAVE   NEED A COPY STATEMENT
         LA    R1,MSGQDCB                    OUT DCB IS NOT FB, 80
         CLI   #OUT,0                        ERROR CLOSE?
         BE    MSGNEWXX                      YES, OUTPUT A MESSAGE
         LA    R1,MSGQCLOS                   DATA SET IS NOW CLOSED
         B     MSGNEWXX                      NOW CLOSED
         SPACE 16
         USING IHADCB,R1
OUT90    CLC   DCBDSORG(2),ZERO              ANY DSORG?
         BNE   *+8                           YES, BRANCH
         OI    DCBDSORG,DS1DSGPS             NO, USE DSORG=PS
         TM    DCBDSORG,DS1DSGPS             DSORG=PS?
         BNOR  R14                           NO, ERROR
         CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+8                           YES, BRANCH
         MVI   DCBRECFM,DCBRECF+DCBRECBR     NO, USE RECFM=FB
         TM    DCBRECFM,DCBRECF              RECFM=F.?
         BNOR  R14                           NO, ERROR
         LA    R0,80                         DEFAULT LRECL
         CLC   DCBLRECL(2),ZERO              ANY LRECL?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBLRECL                   NO, USE LRECL=80
         CH    R0,DCBLRECL                   LRECL=80?
         BNER  R14                           NO, ERROR
         LA    R0,3120                       DEFAULT BLKSIZE
         CLC   DCBBLKSI(2),ZERO              ANY BLKSIZE?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBBLKSI                   NO, USE BLKSIZE=3120
         MVI   #OUT,1                        ALL ATTRIBUTES ARE CORRECT
         BR    R14
         DROP  R1
         SPACE 2
         PRINT NOGEN
QSAMDCB  DCB   DSORG=PS,DDNAME=PDSOUT,MACRF=(PM),EXLST=OUT99
LQSAMDCB EQU   *-QSAMDCB
OUT99    DC    0F'0',X'85',AL3(OUT90)        OPEN EXIT ONLY
MSGQOPEN MSG   'OUTCOPY DCB OPEN ERROR; REALLOCATE AND TRY AGAIN'
MSGQDCB  MSG   'OUTCOPY DCB ATTRIBUTES CONFLICT - USE SEQUENTIAL WITH RX
               ECFM=F OR FB AND LRECL=80'
MSGQCLOS MSG   'NOTE: OUTCOPY DCB IS CLOSED'
         PRINT GEN
         SPACE 2
OUTCOPYS DC    CL80'          COPY OUTDD=OUTPUT,INDD=1'
OUTSEL   DC    CL80'          S M=12345678            '
         TITLE 'P D S  --  PDS PRINTOFF/REVIEW/SUBMIT/TSOLIST 02/06/84'
***********************************************************************
***    PRINTOFF SUBCOMMAND     ADDED BY BRUCE LELAND -- JULY, 1981  ***
***                                                                 ***
***    REVIEW SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1984   ***
***                                                                 ***
***    SUBMIT SUBCOMMAND       ADDED BY BRUCE LELAND -- NOV, 1977   ***
***                                                                 ***
***    TSOLIST SUBCOMMAND      ADDED BY BRUCE LELAND -- JULY, 1981  ***
***********************************************************************
*
         SPACE 1
PRINTOFF CSECT
         USING *,R8
         MVC   MSGTEXT1+4(8),0(R3)   **  PRINTOFF/REVIEW/SUBMIT/TSOLIST
         MVI   MSGTEXT1+12,C''''          ADD A QUOTE
         MVC   MSGTEXT1+13(44),DSNAME     ADD THE DATA SET NAME
         LH    R15,DSNLEN                 DSNAME ACTUAL LENGTH
         LA    R4,13(,R15)                LENGTH OF "PRINTOFF'"
         LA    R3,MSGTEXT1+12(R15)        POINT TO CURRENT BYTE -1
         TM    DSORG,DS1DSGPO             NON-PARTITIONED DATA SET?
         BZ    PRINT6                     YES, BRANCH
         SPACE 1
         MVI   1(R3),C'('                 MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R4,2+8(,R4)              LENGTH OF MEMBER +1
         LA    R2,8                     MAXIMUM MACHINE LENGTH+1
         SPACE 1
PRINT3   BCTR  R3,0                     SCAN
         BCTR  R4,0                         BACKWARDS
         CLI   0(R3),X'40'                           FOR
         BNE   *+8                                       FIRST
         BCT   R2,PRINT3                                      NON-BLANK
         SPACE 1
         BCTR  R2,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R2,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
PRINT6   MVI   1(R3),C''''                ADD A QUOTE
         MVI   2(R3),X'40'                ADD A BLANK
         LA    R4,3(,R4)                  ACCOUNT FOR ")' "
         LA    R2,COMMDSTR                START OF ANY ADDED TEXT
         SR    R2,R4                      WHERE TO START MESSAGE
         LR    R14,R4                     CURRENT MESSAGE LENGTH
         BCTR  R14,0                      MESSAGE MACHINE LENGTH
         MVC   0(*-*,R2),MSGTEXT1         <<EXECUTED>>
         EX    R14,*-6                    MOVE IN MESSAGE TEXT
         AH    R4,#COMMDSZ                ADD REMAINING LENGTH
         SLL   R4,16                      CLEAR BOTTOM TWO BYTES
         STCM  R4,B'1111',0(R2)           SAVE STRING TOTAL LENGTH
*        MESSAGE (R2)                     DELETE * TO OUTPUT COMMAND
         MVI   3(R2),8                    OPERAND OFFSET
         LA    R3,##SUBCOM                ASSUME LINK MEMBER NAME
         CLI   ##SUBCOM,C'T'              CORRECT?
         BNE   *+8                        YES, BRANCH
         LA    R3,LISTLIT                 NO, USE LIST
         SPACE 1
         ST    R2,ADDRTEXT                COMMAND ADDRESS
         LA    R1,ADDRTEXT                ADDRESS OF THE CPPL
         SPACE 1
         BAL   R2,ATTACH                  CLEAR THE ATTENTION ECB
         B     NEWSTAX                    DO THE NEXT COMMAND
         TITLE 'P D S  --  PDS RENAME                         02/06/84'
***********************************************************************
***      RENAME SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
RENAME   CSECT
         USING *,R8
         CLI   #RENRANG,1              RENAME GROUP?
         BE    RENAME30                YES, BRANCH
         SPACE 1
         MVC   MEMBERD+1(2),LMEMBER2   CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         SPACE 1
         MVC   DSNMEMQ(8),MEMBER1      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         LA    R6,NEWCMD               ASSUME NOT A SWAP REQUEST
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BNO   RENAME20                NO, BRANCH
         SPACE 1
*** RENAME WITH SWAP OPERAND *** *** *** *** *** *** *** *** *** *** **
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME10                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
RENAME10 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     NEWCMD
         SPACE 3
*** RENAME WITH NO OPERANDS  *** *** *** *** *** *** *** *** *** *** **
RENAME20 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP OPERAND ** *** *** *** *** *** *** *** *** *** **
RENAME30 NI    FLAGSBB,FF-FLINESET     NO OUTPUT IN PROGRESS YET
         MVI   SUBPOOLT,21             TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'         READ THE DIRECTORY
         LA    R5,256/2                256 MEMBERS INITIALLY
         SPACE 1
RENAME40 SLL   R5,1                    MEMBERS*2 FOR EACH LOOP
         SR    R2,R2
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                    MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)              ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1                   NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR      ANY PREVIOUS MEMBERS?
         BZ    RENAME42                NO, BRANCH
         SRL   R3,1                    YES, USE SECOND HALF OF TABLE
         LR    R14,R4                  NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                   MEMBERS*8 IS OLD TABLE SIZE
         LR    R2,R15                  DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0                  PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R2                   LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
RENAME42 ST    R4,#MEMPTR              MEMBER TABLE BASE
         AR    R4,R2                   WHERE TO ADD MEMBERS
         SPACE 3
RENAME44 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     RENAME52                END OF MEMBERS, BRANCH
         SPACE 1
         LH    R1,LMEMBER1             MEMBER NAME LENGTH
         CLC   MEMNAME(*-*),MEMBER1    <<EXECUTED>>
         EX    R1,*-6                  MEMBER:GROUP
         BL    RENAME44                  LOW, LOOP
         BH    RENAME52                  HIGH, DONE WITH FIRST PHASE
         MVC   DSNMEMQ(8),MEMNAME        EQUAL, CHECK FOR ENQUEUED
         BAL   R2,ENQMTEST
         NOP   0                       OUTPUT AN ERROR MESSAGE ONLY
         SPACE 2
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BO    RENAME46                YES, BRANCH
         OI    FLAGSBB,FLINESET        LINE NOW IN PROGRESS
         MVC   MSGTEXT2,MSGGRPR        MEMBERS TO BE RENAMED ARE:
         LH    R1,MSGTEXT2
         LA    R6,MSGTEXT2(R1)         POINT TO END OF MESSAGE
         SPACE 1
RENAME46 CLI   0(R2),X'40'             SCAN FOR
         BNE   *+8                             LAST
         BCT   R2,RENAME46                         NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGTEXT2
         SR    R15,R1
         C     R15,LINESIZE
         BNH   RENAME50
         SR    R6,R1
         STH   R6,MSGTEXT2
         MESSAGE (R1)
         MVC   MSGTEXT2,MSGBLANK
         LH    R1,MSGTEXT2
         LA    R6,MSGTEXT2-1(R1)       INDENT FOR BLANKS
         SPACE
RENAME50 MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME        CURRENT MEMBER NAME
         LA    R4,16(,R4)              NEXT MEMBER NAME
         S     R3,=F'1'                ANY HOLD POSITIONS LEFT?
         BP    RENAME44                YES, BRANCH
         B     RENAME40                NO, REALLOCATE
         SPACE 2
RENAME52 LA    R1,MSGNODSP             ASSUME NO MEMBERS FOUND
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BZ    MSGNEW                  NO, EXIT
         LA    R1,MSGTEXT2
         SR    R6,R1
         BCTR  R6,0                    DELETE FINAL COMMA
         STH   R6,MSGTEXT2
         MESSAGE (R1)
         MESSAGE MSGBLANK
         SPACE 1
         LA    R1,MSGRENOK             VERIFY THAT YOU WISH TO RENAME..
         BAL   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, BRANCH
         SPACE 2
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUE
         B     NEWCMD                  OPEN FAILED -- ERROR
         SPACE 3
         L     R3,#MEMPTR              FIRST MEMBER POINTER
         LA    R6,RENAME60             WHERE TO GO
         MVI   #RENDEFT,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         MVC   #RENDEFT+1(2+8+8+2),LMEMBER1  LENGTH1, MEMBER1, M2, L2
         SPACE 1
RENAME60 MVC   MEMBERD(1+2+8+8+2),#RENDEFT    FLAG, LEN1, MEM1, M2, L2
         MVC   MEMBERD+1(2),#RENDEFT+1+2+8+8  REPEAT LEN2 FOR LEN1
         MVC   MEMBERD+1+2(8),#RENDEFT+1+2+8  REPEAT MEM2 FOR MEM1
         CR    R3,R4                   DONE?
         BE    NEWCMD                  YES, BRANCH
         MVC   DIRNAME(16),0(R3)       CURRENT MEMBER NAME
         LA    R3,16(,R3)              NEXT MEMBER NAME
         MVC   MEMBER1(8),DIRNAME
         MVC   MEMBER2(8),DIRNAME
         LH    R1,LMEMBER2
         MVC   MEMBER2(*-*),#RENDEFT+1+2+8  <<EXECUTED>>
         EX    R1,*-6                       MOVE IN FIRST FEW BYTES
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BO    RENAME70                YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         EX    0,*                       08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP AND SWAP OPERAND   *** *** *** *** *** *** *** **
RENAME70 XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         SPACE 1
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         LTR   R15,R15                 ANY PROBLEM?
         BNZ   IOERROR                 YES, ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME80                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
RENAME80 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     RENAME60                PROCESS OTHER MEMBERS IN GROUP
         SPACE 3
** UPDATE ANY ALIAS ENTRIES FOR LOAD MODULES
RENAME90 MVC   MSGTEXT1,MSGRENAM       MEMBER RENAMED MESSAGE
         MVC   MSGTEXT1+4(8),MEMBER1
         TR    MSGTEXT1+4(8),TRLINE    MAKE PRINTABLE
         MESSAGE MSGTEXT1
         SPACE 1
         TM    FLAGSCC,RECFMU          LOAD MODULE?
         BNOR  R6                      NO, BRANCH
         TM    DIRFLAG,DIRALIAS        AN ALIAS?
         BOR   R6                      YES, BRANCH
         TM    FLAGSGG,FALUPCON        NO UPDATE OF ALIASES?
         BOR   R6                      YES, BRANCH
         SPACE 2
         MVC   MSGTEXT1,MSGALIUP       ALIAS UPDATED: (MEMBER)
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
RENAME95 BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     0(R6)                   LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   RENAME95                NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   RENAME95                NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         LA    R14,DIRREAL             CORRESPONDING REAL ENTRY NAME
         TM    DIRATTR,ATTRSCTR        SCATTER LOADED?
         BNO   *+8                     NO, BRANCH
         LA    R14,DIRREALS            YES, USE THIS NAME
         CLC   0(8,R14),MEMBER2        ALIAS NAME POINTER=MEMBER NAME?
         BE    RENAME95                YES, BRANCH
         SPACE 1
         MVC   0(8,R14),MEMBER2        CHANGE THE MEMBER POINTER
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   MSGTEXT1+MSGALIUN(8),DIRNAME  MEMBER NAME
         TR    MSGTEXT1+MSGALIUN(8),TRLINE   MAKE PRINTABLE
         MESSAGE MSGTEXT1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     RENAME95                CONTINUE FOR ALL ALIASES
         SPACE 3
         PRINT NOGEN
MSGRENAM MSG   '(MEMBER) RENAMED'
MSGGRPR  MSG   'MEMBERS TO BE RENAMED ARE: '
MSGRENOK MSG   ' DO YOU WANT TO RENAME THESE MEMBERS - ENTER Y (FOR YESX
               ) OR N (FOR NO):'
         PRINT GEN
         TITLE 'P D S  --  PDS RESTORE                        02/06/84'
***********************************************************************
***   RESTORE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***********************************************************************
*
         SPACE 1
RESTORE  CSECT
         USING *,R8
         ICM   R1,B'1111',MEMBER2       TTR TO BE USED
         CLI   LMEMBER2+1,1             COMPARE LENGTH:1
         BH    REST010                    HIGH (DO NOT ADJUST)
         SRL   R1,8
         BE    REST010                    EQUAL (ADJUSTED ONE TIME)
         SRL   R1,8                       LOW (ADJUSTED TWICE)
         SPACE 1
REST010  STCM  R1,B'1111',MEMBER2       SAVE THE UPDATED ADDRESS
         STCM  R1,B'1111',DIRTTR        SAVE THE UPDATED ADDRESS
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST020                  NO, BRANCH
         MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         MVI   #RESTDIR+9,ATTREXEC      EXECUTABLE
         MVI   #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         MVI   #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST020  CLI   #RESTLIK,0               ANY LIKE OPERAND?
         BE    REST080                  NO, BRANCH
         XC    DIRNAME(74),DIRNAME
         MVC   DIRNAME,#RESTLIK         LIKE NAME
         BLDL  INDCB,BLDLLIST           GET MEMBER DIRECTORY
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST030                    00 - FOUND
         B     NOMEMBER                   04 - NOT FOUND
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
REST030  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  BLDL ADDS THINGS
         MVC   #RESTDIR,DIRFLAG         SAVE USER DATA
         NI    #RESTDIR,FF-X'80'        TURN OFF ANY ALIAS BIT
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST080                  NO, BRANCH
         XC    #RESTDIR,#RESTDIR        CLEAR THE USER DATA
         LA    R2,DIRAPF                POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR         SCATTER LOAD?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, POSITION OVER FIELDS
         TM    DIRFLAG,X'80'            ALIAS ENTRY?
         BNO   *+8                      NO, BRANCH
         LA    R2,11(,R2)               YES, POSITION OVER FIELDS
         TM    DIRATTR2,DIRAOSLE        O/S MVS LINKAGE EDITOR?
         BNO   REST040                  NO, BRANCH
         TM    DIRATTR2,DIR2SSI         ANY SSI FIELD?
         BNO   REST050                  NO, BRANCH
REST040  LA    R1,1(,R2)                ADD FOR ROUNDING TO HALFWORD
         N     R1,=F'-2'                ROUND TO HALFWORD
         CLC   ZERO,0(R1)               ZERO?
         BE    REST050                  YES, BRANCH
         CLC   =F'-1',0(R1)             HIGH VALUES?
         BE    REST050                  YES, BRANCH
         MVC   #RESTSSI,0(R1)           SAVE THE SSI VALUE
         LA    R2,4(,R1)                POSITION AFTER SSI
         SPACE 1
REST050  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  VS LKED AND APF DATA GOOD?
         BO    REST060                     YES, BRANCH
         XC    DIRATTR2(3),DIRATTR2        NO, CLEAR NEW FLAGS
         B     REST070
         SPACE 1
REST060  CLI   0(R2),X'01'              VALID APF LENGTH?
         BNE   REST070                  NO, BRANCH
         MVC   #RESTAPF,1(R2)           YES, SAVE APF DATA
         SPACE 1
REST070  MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         NI    DIRATTR,X'CA'            RENT, REUS, ONLY LOAD, EXEC.
         NI    DIRATTR+1,X'09'          NOT EDIT, REFR
         MVC   #RESTDIR+9(2),DIRATTR    COPY INTO THE HOLD AREA
         OI    #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         NI    DIRATTR2,DIR2PAGA        PAGE BOUNDARY FLAG
         NI    DIRATTR2+1,X'1F'         RMODE, AMODE FLAGS
         MVC   #RESTDIR+19(2),DIRATTR2  COPY INTO THE HOLD AREA
         OI    #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST080  MVC   DIRNAME,MEMBER1          MEMBER NAME TO BE RESTORED
         MVC   DIRTTR(3),MEMBER2        TTR OF MEMBER TO BE RESTORED
         SPACE 2
         LA    R1,MSGTOBIG
         CLC   DIRTTR(3),DS1LSTAR       IN DATA SET BOUNDS?
         BNL   MSGNEW                   NO, ERROR
         SPACE 1
         TM    #RESFLAG,#RESFREP+#RESFDIS  REPEAT, DISPLAY OR PROMPT?
         BZ    RESTOR03                    NO, BRANCH
         SPACE 1
         ICM   R1,B'1110',DIRTTR        T T R X  ZERO?
         BZ    RESTSC08                 YES, BRANCH
         SRL   R1,8                     0 T T R
         S     R1,=F'1'                 0 T T R - 1 = 0?
         BZ    RESTSC08                 YES, BRANCH
         SLL   R1,8                     T T R 0 (WHERE R IS REDUCED)
         STCM  R1,B'1110',DIRTTR        T T R
         CLI   MEMBER2+2,X'01'          R OF TTR 1 OR LESS?
         BH    RESTSC08                 YES, BRANCH
         SRL   R1,16                    0 0 T T
         BCTR  R1,0                     0 0 T T - 1
         SLL   R1,16                    T T 0 0 (WHERE TT IS REDUCED)
         STCM  R1,B'1100',DIRTTR        T T  UPDATED
         MVI   DIRTTR+2,X'01'           RESET R TO 1 OF PREVIOUS TRACK
         SPACE 1
RESTSC08 TM    #RESFLAG,#RESFREP        REPEAT?
         BZ    RESTSC09                 NO, BRANCH
         SPACE 2
         LA    R1,7                     MACHINE LENGTH
         LA    R2,DIRNAME+7             LAST CHARACTER
         SPACE 1
         CLI   0(R2),X'40'              BLANK?
         BNE   *+10                     NO, BRANCH
         BCTR  R2,0                     PREVIOUS CHARACTER
         BCT   R1,*-10                  TRY ALL BYTES
         SPACE 1
         ST    R1,#RESTLEN                          NAME LENGTH
         SPACE 3
RESTSC09 MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE IN USE
         LA    R5,1024                  1024 MEMBERS INITIALLY
         SPACE 1
RESTSC10 LR    R0,R5
         SLL   R0,2                     MEMBERS*4 FOR TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         ST    R1,#MEMPTR               START OF TTR LIST
         LR    R4,R1
         XC    0(4,R4),0(R4)            DUMMY FIRST ELEMENT
         LR    R3,R5                    NUMBER OF ELEMENTS
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
RESTSC12 BAL   R14,READDIR              GET THE NEXT MEMBER
         B     RESTSC16                 END OF DIRECTORY, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR START<TTR OF THIS MEMBER?
         BNL   RESTSC12                 NO, IGNORE
         S     R3,=F'1'                 WILL THIS FIT?
         BNP   RESTSC14                 NO, BRANCH
         LA    R4,4(,R4)                NEXT TTR TABLE POSITION
         MVC   0(3,R4),MEMTTR           SAVE THE MEMBER START TTR
         MVI   3(R4),0                  CLEAR THE END FLAG
         B     RESTSC12                 GET THE NEXT ENTRY
         SPACE 1
RESTSC14 SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL TO FREE
         FREEMAIN R,SP=(0)
         SLL   R5,1                     DOUBLE THE NUMBER OF ENTRIES
         B     RESTSC10
         SPACE 2
RESTSC16 MVI   3(R4),X'FF'              MARK THE END OF THE TTR LIST
         BAL   R2,CLEAR                 CLEAR THE SCREEN
RESTSC17 TM    #RESFLAG,#RESFREP        REPEAT RESTORE?
         BNO   RESTSC18                 NO, BRANCH
         L     R1,#RESTSEQ              THIS MEMBER NUMBER
         LA    R1,1(,R1)
         ST    R1,#RESTSEQ              NEW MEMBER NUMBER
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'           CORRECT SIGN
         L     R1,#RESTLEN              MACHINE LENGTH
         UNPK  DIRNAME(8),DOUBLE(8)     CONVERT THE SEQUENTIAL NAME
         MVC   DIRNAME(*-*),MEMBER1     <<EXECUTED>>
         EX    R1,*-6                   ADD THE CODED NAME PORTION
         B     RESTSC20                 SKIP THIS CLEAR
         SPACE 1
RESTSC18 BAL   R2,CLEAR                 CLEAR THE SCREEN
         SPACE 3
RESTSC20 MESSAGE MSGBLANK               ONE BLANK LINE
         MVC   STARTTR(3),DIRTTR        FIRST DISK ADDRESS TO READ
         MVI   #RESTERR+1,3             ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 2
RESTSC22 BAL   R14,EXCP
         LA    R1,MSGEOFDS              ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RESTSC22                   00 - SUCCESSFUL READ
         B     RESTSC24                   04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     RESTSC23                   12 - I/O ERROR
         SPACE 1
*  PERMANENT I/O ERROR
*    A.  IF TOO MANY ERRORS FOR THIS MEMBER, TERMINATE
*    B.  SIMULATE AN END OF MEMBER CONDITION
*    C.  CONTINUE ON THE NEXT TRACK
RESTSC23 LA    R1,MSGRQUIT              ASSUME TOO MANY ERRORS
         LH    R0,#RESTERR
         S     R0,=F'1'                 CORRECT?
         BM    MSGNEW                   YES, BRANCH
         STH   R0,#RESTERR
         MESSAGE MSGNXTRK
         STM   R2,R12,28(R13)
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRL   R0,16                    TT OF LAST INPUT TTRN
         A     R0,=F'1'
         STH   R0,STARTTR               TT OF NEXT TRACK
         MVI   STARTTR+2,X'01'          R OF TTR
         SPACE 2
*  END OF MEMBER, GET THE NEXT DATA BLOCK
RESTSC24 BAL   R14,EXCP
         LA    R1,MSGEOFDS              ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RESTSC26                   00 - SUCCESSFUL READ
         B     RESTSC24                   04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     RESTSC23                   12 - I/O ERROR
         SPACE 1
RESTSC26 STM   R2,R12,28(R13)           CONVERT MBBCCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         STCM  R0,B'1110',DIRTTR        TTR OF THIS MEMBER
         CLM   R0,B'1110',MEMBER2       AT OR AFTER THE START POINT?
         BL    RESTSC22                 NO, BRANCH
         SPACE 1
         L     R4,#MEMPTR               START OF TTR TABLE
RESTSC28 CLC   DIRTTR(3),0(R4)          IN THE TABLE?
         BE    RESTSC22                 YES, IGNORE THIS MEMBER
         CLI   3(R4),X'FF'              END OF TTR TABLE?
         LA    R4,4(,R4)
         BNE   RESTSC28                 NO, BRANCH
         SPACE 1
         MVC   MSGTEXT1,MSGDELMB
         UNPK  MSGTEXT1+32(7),DIRTTR(4) TTR OF THIS DELETED MEMBER
         TR    MSGTEXT1+32(6),TRTABLE   MAKE PRINTABLE
         MESSAGE MSGTEXT1
         TM    #RESFLAG,#RESFDIS        DISPLAY OPTION?
         BNO   RESTSC90                 NO, BRANCH
         MESSAGE MSGBLANK
         SR    R5,R5                    LINE IN PROGRESS POINTER
         SR    R6,R6                    OUTPUT COUNTER
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    RESTSC36                 YES, BRANCH
         B     RESTSC62                 NO, MUST BE V OR F
         SPACE 3
*  RECORD FORMAT U (LOAD MODULE)
RESTSC30 BAL   R14,EXCP
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RESTSC36                   00 - SUCCESSFUL READ
         B     RESTSC32                   04 - END OF MEMBER
         B     RESTSC32                   08 - END OF DATA SET
         B     RESTSC23                   12 - I/O ERROR
         SPACE 1
RESTSC32 LTR   R5,R5                    LINE IN PROGRESS?
         BNP   RESTSC34                 NO, BRANCH
         LA    R1,MSGTEXT1
         SR    R5,R1                    LENGTH OF MESSAGE SO FAR
         BCTR  R5,0                     DELETE THE FINAL COMMA
         STH   R5,MSGTEXT1
         MESSAGE (R1)
         B     RESTSC90
         SPACE 1
RESTSC34 MESSAGE MSGIDRSD               NO CESD OR LKED IDR RECORDS
         MVC   STARTTR(3),DIRTTR        RE-READ THE MEMBER
         B     RESTSC60                 OUTPUT WITH CHARACTER OUTPUT
         SPACE 2
RESTSC36 L     R3,EXCPBUFF              BUFFER ADDRESS
         CLI   0(R3),X'20'              CESD RECORD?
         BNE   RESTSC50                 NO, CHECK FOR LINKAGE-EDIT IDR
         CH    R6,=H'5'                 NEED TO BUILD MORE CSECT LINES?
         BNL   RESTSC30                 NO, BRANCH
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     LENGTH/16=NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 1
         USING ESDNAME,R3
RESTSC38 TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   RESTSC46                 NO, BRANCH
         CLI   ESDNAME,X'40'            BLANK CSECT NAME?
         BE    RESTSC46                 YES, IGNORE
         LA    R2,ESDNAME+7
         LTR   R5,R5                    LINE IN PROGRESS?
         BP    RESTSC40                 YES, BRANCH
         MVC   MSGTEXT1,MSGCSECT        'CSECTS ARE: '
         LH    R1,MSGTEXT1
         LA    R5,MSGTEXT1(R1)          CURRENT OUTPUT LINE POSITION
         SPACE 2
RESTSC40 CLI   0(R2),X'40'              SCAN BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R2,RESTSC40                                   NON-BLANK
         SPACE 1
         LA    R0,ESDNAME
         SR    R2,R0                    MACHINE LENGTH OF NAME
         LA    R15,2(R2,R5)             CURRENT + NAME-1 + BLANK + 1
         LA    R1,MSGTEXT1
         SR    R15,R1                   DISPLACEMENT INTO THE LINE
         C     R15,LINESIZE             PAST END OF LINE?
         BNH   RESTSC44                 NO, BRANCH
         SPACE 1
         LA    R6,1(,R6)                ONE MORE OUTPUT LINE
         CH    R6,=H'5'                 ENOUGH LINES?
         BL    RESTSC42                 NO, BRANCH
         MVC   0(4,R5),=C' ...'         INDICATE MORE CSECTS EXIST
         LA    R5,5(,R5)                INCREMENT TO END +1
         B     RESTSC30
         SPACE 2
RESTSC42 LA    R1,MSGTEXT1
         SR    R5,R1                    MESSAGE LENGTH
         STH   R5,MSGTEXT1
         MESSAGE (R1)
         MVC   MSGTEXT1,MSGBLANK        BLANKS INITIALLY
         LH    R1,MSGTEXT1
         LA    R5,MSGTEXT1(R1)          INCREMENT TO END
         SPACE 1
RESTSC44 MVI   0(R5),X'40'              BLANK BEFORE CSECT NAME
         MVC   1(8,R5),ESDNAME          CSECT NAME
         TR    1(8,R5),TRLINE           MAKE PRINTABLE
         LA    R5,2(R2,R5)              POSITION TO FOLLOWING COMMA
         MVI   0(R5),C','               ADD THE COMMA
         LA    R5,1(,R5)                POSITION AFTER THE COMMA
         SPACE 1
RESTSC46 LA    R3,16(,R3)               NEXT ESD ENTRY
         BCT   R4,RESTSC38              LOOP FOR ALL ENTRIES
         B     RESTSC30                 GET THE NEXT RECORD
         DROP  R3
         SPACE 3
RESTSC50 CLI   0(R3),X'80'              IDR RECORD?
         BNE   RESTSC30                 NO, CONTINUE INPUT
         TM    2(R3),IDRLKED            LINKAGE EDITOR IDR RECORD?
         BNO   RESTSC30                 NO, CONTINUE INPUT
         LTR   R5,R5                    LINE IN PROGRESS?
         BNP   RESTSC52                 NO, BRANCH
         LA    R1,MSGTEXT1
         SR    R5,R1                    LENGTH OF MESSAGE SO FAR
         BCTR  R5,0                     DELETE THE FINAL COMMA
         STH   R5,MSGTEXT1
         MESSAGE (R1)
         SPACE 1
RESTSC52 MESSAGE MSGBLANK
         LA    R1,12+3(,R3)             ADDRESS OF YYDDD FORMAT DATE
         LA    R15,LKEDDATE             ADDRESS OF YYMMDD FORMAT OUTPUT
         BAL   R14,CONVDATE             CONVERT TO MMDDYY FORMAT
         MVC   MSGTEXT1,MSGHISTD        LAST LINKEDIT DATE MESSAGE
         ED    MSGTEXT1+DATEMASK-MSGHISTD(L'DATEMASK),LKEDDATE
         MESSAGE MSGTEXT1               OUTPUT
         B     RESTSC90                 PROMPT FOR RESTORE
         SPACE 2
*  RECORD FORMAT V, F OR U
RESTSC60 BAL   R14,EXCP
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RESTSC62                   00 - SUCCESSFUL READ
         B     RESTSC78                   04 - END OF MEMBER
         B     RESTSC78                   08 - END OF DATA SET
         B     RESTSC23                   12 - I/O ERROR
         SPACE 1
RESTSC62 L     R5,LS                    BLOCK LENGTH
         L     R2,EXCPBUFF              BUFFER START
         LR    R1,R5
         AR    R5,R2                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL
         BO    RESTSC68                 YES, BRANCH
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1
         BO    RESTSC68                 YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   RESTSC68                 NO, ASSUME RECFM=U
         AH    R2,=H'4'                 SKIP BLOCK LENGTH WORD
         B     RESTSC66
         SPACE 2
RESTSC64 BXLE  R2,R4,RESTSC66           GET THE NEXT LOGICAL RECORD
         B     RESTSC60                 END OF BLOCK, BRANCH
         SPACE 2
RESTSC66 TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   RESTSC68                 NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R2)         RECORD LENGTH +4
         AH    R2,=H'4'                 SKIP RECORD LENGTH WORD
         SH    R4,=H'4'                 RECORD LENGTH VALID?
         BP    RESTSC68                 YES, BRANCH
         SR    R4,R4                    NO, USE LENGTH ZERO
         B     RESTSC64                 IGNORE THE RECORD
         SPACE 1
RESTSC68 LA    R6,1(,R6)                ANOTHER OUTPUT RECORD
         CH    R6,=H'5'                 ENOUGH OUTPUT?
         BH    RESTSC78                 YES, BRANCH
         MVC   MSGTEXT1,MSGDATAL        DATA LINE X:
         LA    R0,240(,R6)              PRINTABLE COUNT CHARACTER
         STC   R0,MSGTEXT1+14
         MESSAGE MSGTEXT1
         LR    R1,R4                    CURRENT RECORD LENGTH
         CH    R1,=H'256'
         BNH   *+8
         LH    R1,=H'256'               MAXIMUM DISPLAY LENGTH
         BCTR  R1,0                     MACHINE LENGTH
         MVC   MSGTEXT1+4(*-*),0(R2)    <<EXECUTED>>
         EX    R1,*-6                   MOVE IN THE DISPLAY TEXT
         TR    MSGTEXT1+4(256),TRLINE   CONVERT TO PRINTABLES
         LA    R1,4+1(,R1)              MESSAGE LENGTH
         CH    R4,=H'256'               TRUNCATED RECORD MESSAGE?
         BNH   *+14                     NO, BRANCH
         MVC   MSGTEXT1+4+256(4),=C' ...'  INDICATE THAT MORE EXISTS
         LA    R1,4(,R1)                ADD TO THE MESSAGE LENGTH
         STH   R1,MSGTEXT1
         MESSAGE MSGTEXT1
         C     R4,LINESIZE              LRECL EXACTLY MATCH LINESIZE?
         BE    RESTSC64                 YES, A BLANK LINE ALREADY (+1)
         MESSAGE MSGBLANK
         B     RESTSC64
         SPACE 1
RESTSC78 TM    FLAGSCC,RECFMU           RECFM=U?
         BO    RESTSC20                 YES, INVALID LOAD MODULE
RESTSC90 MESSAGE MSGBLANK
         TM    #RESFLAG,#RESFPRO        PROMPT OPTION?
         BO    RESTSC94                 YES, BRANCH
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BNZ   RESTOR05                        YES, BRANCH
RESTSC94 TM    FLAGSEE,FBKGRND+FNOTTERM INPUT FROM THE TERMINAL?
         BNZ   RESTOR05                 NO, RESTORE THIS MEMBER
         LA    R1,MSGMEMOK              CORRECT MEMBER MESSAGE
         BAL   R2,YESNO1                RESPONSE "YES" OR "NO"?
         B     RESTSC18                 NO, CONTINUE SEARCHING
         BAL   R2,CLEAR                 YES, CLEAR THE SCREEN
         B     RESTOR05                      NO DIRECTORY CHECK NEEDED
         SPACE 3
RESTOR03 MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
RESTOR04 BAL   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     RESTOR05                 LAST MEMBER PROCESSED
         SPACE
         CLC   DIRTTR(3),MEMTTR         TTR'S MATCH?
         BNE   RESTOR04                 NO, BRANCH
         SPACE
         MVC   MSGTEXT1,MSGNORES        MEMBER ALREADY EXISTS AT TTR
         MVC   MSGTEXT1+MSGNORE1-MSGNORES(8),MEMNAME
         TR    MSGTEXT1+MSGNORE1-MSGNORES(8),TRLINE
         TM    MEMFLAG,X'80'            AN ALIAS?
         BNO   *+10                     NO, BRANCH
         MVC   MSGTEXT1+4(8),=C'AN ALIAS'
         MESSAGE MSGTEXT1
         SPACE 1
         TM    MEMFLAG,X'80'            ALIAS ENTRY?
         BO    RESTOR04                 YES, CONTINUE SEARCHING
         SPACE 1
         CLC   =C'TEMP',DIRNAME         ALLOW IT ANYWAY?
         BNE   NEWCMD                   NO, BRANCH
         SPACE 2
RESTOR05 TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BNO   RESTOR96            NO, STOW AND QUIT
         XC    DIRUSER,DIRUSER     CLEAR THE ADDITIONAL FIELDS
         MVC   DIRFLAG(33),#RESTDIR  INITIALIZE THE USER AREA
         SPACE 1
         MVI   #RESTESD,AMODE24    AMODE 24 IS THE DEFAULT
         MVI   #RESTMOD,RMODEANY   SET RMODE ANY SUMMARY START
         XC    #BLKSI(4),#BLKSI    CLEAR THE MAXIMUM MEMBER BLOCKSIZE
         LA    R2,RESTOR10         WHERE-TO-GO
         MVC   STARTTR(3),DIRTTR   START ADDRESS
         SPACE 3
RESTOR06 BAL   R14,EXCP
         B     *+4(R15)                 PROCESS RETURN CODE
         B     RESTOR08                   00 - GOOD READ
         B     RESTOR90                   04 - END OF MEMBER
         B     RESTOR90                   08 - END OF DATA SET
         B     NEWCMD                     12 - I/O ERROR
         SPACE 2
RESTOR08 L     R5,LS                    CURRENT LENGTH
         LR    R4,R0                    START OF BUFFER
         C     R5,#BLKSI                BLKSIZE>MAX?
         BNHR  R2                       NO, BRANCH
         ST    R5,#BLKSI                YES, SAVE NEW MAXIMUM
         BR    R2                       DECODE THE RECORD
         SPACE 3
RESTOR10 MVI   ENTRYPT,C'?'             NO ENTRY POINT NAME YET
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BNE   RESTOR12                 NO, BRANCH
         OI    DIRATTR,ATTRTEST         TEST SYM FLAG
         OI    DIRATTR+1,ATTRSYMS       SYMBOLS ARE AVAILABLE
         SPACE 1
RESTOR12 LA    R2,RESTOR12              WHERE-TO-GO NEXT
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BE    RESTOR06                 YES, GET NEXT RECORD
         SPACE
         CLI   0(R4),X'20'              CESD RECORD?
         BNE   RESTOR20                 NO, BRANCH
         SPACE
         LH    R6,4(,R4)                CURRENT ESDID
         LA    R3,8(,R4)                START OF ESD DATA
         USING ESDNAME,R3
         LH    R5,6(,R4)                LENGTH OF DATA IN BUFFER
         AR    R5,R3                    END OF DATA
         LA    R4,16                    LENGTH OF ONE ENTRY
         SR    R5,R4                    END OF DATA -(ONE ENTRY LENGTH)
         SPACE 3
RESTOR13 IC    R0,ESDTYPE
         LA    R1,CODESEG          CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   ESDTYPE,CODESD      VALID EXTERNAL SYMBOL?
         BE    RESTOR14            YES, BRANCH
         CLI   ESDTYPE,CODELR      EXTERNAL REFERENCE?
         BNE   RESTOR17            NO, BRANCH
         SPACE
RESTOR14 CLC   ESDNAME,DIRNAME     THIS MEMBER NAME?
         BNE   RESTOR15            NO, BRANCH
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR  ENTRY ADDRESS ZERO?
         BZ    *+8                  YES, BRANCH
         NI    DIRATTR+1,FF-ATTREP0 NO, INSURE ATTR FLAG OFF
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
         SPACE
RESTOR15 OC    ESDADDR(3),ESDADDR  ZERO OFFSET?
         BNZ   RESTOR17            NO, BRANCH
         CLI   ENTRYPT,C'?'        ANY SYMBOL NAME YET?
         BNE   RESTOR17            YES, BRANCH
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
RESTOR17 CR    R0,R1               THIS SEGTAB/ENTAB ENTRY?
         BE    RESTOR18            YES, BRANCH
         CLI   ESDTYPE,CODESD      EXTERNAL DEFINITION?
         BE    RESTOR18            YES, BRANCH
         CLI   ESDTYPE,CODEPC      PRIVATE CODE?
         BE    RESTOR18            YES, BRANCH
         CLI   ESDTYPE,CODECM      COMMON STORAGE?
         BNE   RESTOR19            NO, BRANCH
         SPACE 1
RESTOR18 L     R1,ESDADDR-1        RELATIVE OFFSET
         L     R14,ESDLEN-1        LENGTH OF ESD ENTRY
         LA    R14,7(R1,R14)       MAXIMUM DISPLACEMENT +7
         N     R14,=X'FFFFFFF8'    ROUND TO NEXT DOUBLE WORD
         CLM   R14,B'0111',DIRSTORE SMALLER THAN PREVIOUS HIGH?
         BL    *+8                 YES, BRANCH
         STCM  R14,B'0111',DIRSTORE NO, UPDATE MODULE LENGTH
         NC    #RESTMOD,ESDXAFLG   SUMMARIZE ALL RMODE ENTRIES
         OC    ESDADDR,ESDADDR     ORIGIN AT LOCATION ZERO?
         BNZ   RESTOR19            NO, BRANCH
         OC    ESDLEN,ESDLEN       NULL LENGTH BLOCK?
         BZ    RESTOR19            YES, BRANCH
         STCM  R6,B'0011',DIRSCET  SAVE THE FIRST TEXT ESDID
         SPACE 1
RESTOR19 A     R6,=F'1'            NEXT ESDID
         BXLE  R3,R4,RESTOR13
         B     RESTOR06            GET NEXT RECORD
         SPACE 3
         DROP  R3
RESTOR20 LA    R2,RESTOR20         WHERE-TO-GO NEXT
         CLI   0(R4),X'80'         IDR RECORD?
         BE    RESTOR06            YES, GET NEXT RECORD
         SPACE
         LA    R2,RESTOR30         WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'         FILLER RECORD?
         BNE   RESTOR30            NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD  NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT    ASSUME ONLY ONE TEXT RECORD TOO
         SPACE
RESTOR30 CLI   0(R4),X'0D'         FILLER RECORD?
         BE    RESTOR06            YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   RESTOR31            NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     RESTOR06
         SPACE 1
RESTOR31 TM    DIRATTR+1,ATTRNRLD  NO RLD MARKED?
         BNO   RESTOR33            NO, BRANCH
         CLM   R5,B'0111',DIRSTORE FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    RESTOR33            YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 1
RESTOR33 STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLI   0(R4),X'10'         SCATTER LOADED?
         BE    RESTOR60            YES, BRANCH
         STCM  R0,B'1110',DIRSTART RESULT TTR
         STCM  R5,B'0011',DIRTEXTL SAVE LENGTH OF FIRST TEXT RECORD
         LA    R2,RESTOR06         WHERE-TO-GO NEXT TIME
         CLI   0(R4),X'00'         OVERLAY DEFINITION RECORD?
         BNER  R2                  NO, LOOP UNTIL END OF MEMBER
         LA    R2,RESTOR40         WHERE-TO-GO NEXT
         OI    DIRATTR,ATTROVLY    OVERLAY MODULE
         XI    DIRFLAG,X'40'+X'20' TWO USER TTR'S NOW
         LA    R14,7(,R5)
         N     R14,=X'FFFFFFF8'    ROUND TO NEXT DOUBLE WORD
         OC    DIREPA(3),DIREPA    NON-ZERO ENTRY POINT?
         BNZ   RESTOR06            YES, BRANCH
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         NI    DIRATTR+1,FF-ATTREP0  INSURE ATTR FLAG OFF
         MVI   ENTRYPT,C'?'        ENTRY POINT ADDRESS IS NOT KNOWN
         MVI   #RESTESD,AMODE24    AMODE 24 AGAIN
         MVI   #RESTMOD,0          RMODE 24 FOR SUMMARY
         B     RESTOR06
         SPACE 2
RESTOR40 SRL   R5,2                LENGTH/4
         STC   R5,DIRNOTE#         NUMBER OF ENTRIES
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRNOTE  SAVE OVERLAY TABLE TTR
         B     RESTOR06            CONTINUE UNTIL END-OF-MEMBER
         SPACE 1
RESTOR60 STCM  R0,B'1110',DIRNOTE  START OF SCATTER LIST TABLE
         LA    R2,RESTOR63         WHERE-TO-GO NEXT
         MVI   DIRFLAG,X'40'+16    2 TEXT TTRS, 16 HALFWORDS
         OI    DIRATTR,ATTRSCTR    SCATTER LOADED MODULE
         SR    R3,R3               ACCUMULATE LENGTH OF OVERLAY RECORDS
         SPACE 1
RESTOR63 CLI   0(R4),X'10'           SCATTER DEFINITION RECORD?
         BNE   RESTOR65              NO, BRANCH
         AR    R3,R5                 ACCUMULATE SCATTER RECORD LENGTH
         SH    R3,=H'4'              LESS THE HEADER AMOUNT
         B     RESTOR06              CONTINUE FOR ALL SCATTER RECORDS
         SPACE 1
RESTOR65 LA    R2,RESTOR67           WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'           FILLER RECORD?
         BNE   RESTOR67              NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD    NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT      ASSUME ONLY ONE TEXT RECORD TOO
         SPACE
RESTOR67 CLI   0(R4),X'0D'         FILLER RECORD?
         BE    RESTOR06            YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   RESTOR72            NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     RESTOR06
         SPACE 1
RESTOR72 TM    DIRATTR+1,ATTRNRLD    NO RLD MARKED?
         BNO   RESTOR76              NO, BRANCH
         CLM   R5,B'0111',DIRSTORE   FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    RESTOR76              YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT   NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 2
RESTOR76 STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRSTART   START OF TEXT
         STCM  R5,B'0011',DIRTEXTL   LENGTH OF FIRST TEXT RECORD
         LA    R6,2(R6,R6)           (#CSECTS+1)*2+2
         SRL   R6,2                  ((#CSECTS+1)*2+2)/4
         SLL   R6,2                  ROUNDED TO NEXT FULLWORD*2
         STCM  R6,B'0011',DIRSCTL    SAVE LENGTH OF TRANSLATE TABLE
         SR    R3,R6
         STCM  R3,B'0011',DIRSCLL    SAVE LENGTH OF SCATTER LIST
         LA    R2,RESTOR06           WHERE-TO-GO NEXT TIME
         BR    R2                    SCAN UNTIL END-OF-MEMBER
         SPACE 1
RESTOR90 MVC   MSGTEXT1,MSGNOTXT
         OC    DIRSTART(3),DIRSTART     ANY TEXT TTR?
         BZ    RESTOR98                 NO, ERROR
         SPACE 1
         MVC   MSGTEXT1,MSGNOESD
         OC    DIRSTORE(3),DIRSTORE     STORAGE SIZE AVAILABLE?
         BZ    RESTOR98                 NO, ERROR
         SPACE 1
         CLC   #BLKSI+2(2),=H'1024'     LARGEST BLOCK=1024?
         BNE   *+8                      NO, PROBABLY NOT DC
         NI    DIRATTR+1,FF-ATTRNODC    YES, TURN OFF NO DC
         SPACE 1
         LA    R2,DIRAPF                APF FLAG
         TM    DIRATTR,ATTRSCTR         SCATTER LOADED?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, SKIP OVER SCATTER DATA
         OC    #RESTSSI,#RESTSSI        ANY SSI DATA?
         BZ    RESTOR93                 NO, BRANCH
         LA    R2,1(,R2)                PREPARE FOR HALFWORD ROUNDING
         N     R2,=F'-2'                ROUND TO HALFWORD
         OI    DIRATTR2,DIR2SSI         SSI DATA IS PRESENT
         OI    DIRFLAG,X'02'            ADD TWO MORE HALFWORDS
         MVC   0(4,R2),#RESTSSI         ADD THE SSI DATA
         LA    R2,4(,R2)                POSITION OVER SSI DATA
         SPACE 1
RESTOR93 MVI   0(R2),X'01'              APF LENGTH IS 1
         MVC   1(1,R2),#RESTAPF         ADD APF DATA
         CLI   #RESTLIK,0               ANY LIKE MEMBER?
         BH    RESTOR96                 YES, BRANCH (RMODE, AMODE SET)
         SPACE 1
         TM    #RESTMOD,RMODEANY        ALL CSECTS RMODE ANY?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRRMANY        YES, SET RMODE ANY
         TM    #RESTESD,AMODE24         LOW-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM24         YES, SET AMODE 24 ON
         TM    #RESTESD,AMODE31         HIGH-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM31         YES, SET AMODE 31 ON
         SPACE 1
RESTOR96 BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     NEWCMD              COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A   ADD AT THE GIVEN TTR
         LR    R5,R15              SAVE RETURN CODE
         BAL   R2,SHUTSTOW         CLOSE STOW, RELEASE ENQUEUES
         SPACE
         B     *+4(R5)             PROCESS RETURN CODE
         B     RESTOR97              00 - ADDED CORRECTLY
         B     MEMEXIST              04 - NAME ALREADY IN THE DIRECTORY
         EX    0,*                   08 - SHOULD NOT HAPPEN WITH ADD
         B     FULLDIR               12 - DIRECTORY IS ALREADY FULL
         B     IOERROR               16 - I/O ERROR IN THE DIRECTORY
         SPACE
RESTOR97 MVC   MSGTEXT1,MSGRESTO     RESTORED MESSAGE
         MVC   MSGTEXT1+4(8),DIRNAME
         MESSAGE MSGTEXT1
         SPACE 1
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1          DEFAULT MEMBER1
         MVC   MEMBERD+1+2+8(8),LMEMBER1+2          DEFAULT MEMBER2
         MVC   MEMBERD+1+2+8+8(2),LMEMBER1          DEFAULT LENGTH
         MVI   MEMBERD,FMEMBER1                     ASSUME ONE MEMBER
         TM    #RESFLAG,#RESFREP                    REPEAT?
         BZ    *+8                                  NO, BRANCH
         OI    MEMBERD,FMEMBER2+FMEMRANG+FMEM#MEM   MEMBER RANGE
         SPACE 2
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BNO   RESTOR99            NO, DONE WITH THIS MEMBER
         SPACE 1
         MVC   MSGTEXT1,MSGENTRY   ENTRY POINT ADDRESS AND SYMBOL
         UNPK  MSGTEXT1+MSGENTR1-MSGENTRY(7),DIREPA(4)
         TR    MSGTEXT1+MSGENTR1-MSGENTRY(6),TRTABLE
         LH    R15,MSGTEXT1
         LA    R14,MSGTEXT1(R15)
         MVC   0(6,R14),=C'  --  '
         MVC   6(8,R14),ENTRYPT    NAME OF THE ENTRY POINT
         CLI   ENTRYPT,C'?'        ANY FOUND?
         BE    *+8                 NO, BRANCH
         LA    R15,14(,R15)        YES, SHOW ENTRY SYMBOL
         STH   R15,MSGTEXT1
         MESSAGE MSGTEXT1
         SPACE 1
         MVC   MSGTEXT1,MSGLEN
         UNPK  MSGTEXT1+MSGLEN1-MSGLEN(7),DIRSTORE(4)
         TR    MSGTEXT1+MSGLEN1-MSGLEN(6),TRTABLE
         MVI   MSGTEXT1+MSGLEN1-MSGLEN+6,X'40'
         SPACE 1
         ICM   R1,B'0111',DIRSTORE ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)        NEXT HIGHER
         SRL   R1,10                          1K VALUE
         CVD   R1,DOUBLE
         ED    MSGTEXT1+MSGLEN2-MSGLEN(6),DOUBLE+5
         SPACE 1
RESTOR98 MESSAGE MSGTEXT1
         SPACE 2
RESTOR99 TM    #RESFLAG,#RESFREP   REPEAT OPTION?
         BNO   NEWCMD              NO, DONE
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BZ    RESTSC17                        NO, BRANCH
         TM    #RESFLAG,#RESFPRO   PROMPT OPTION?
         BO    RESTSC17            YES, PROCESS THE NEXT DELETED MEMBER
         MESSAGE MSGBLANK          ONE BLANK LINE
         MESSAGE MSGBLANK          ONE BLANK LINE
         B     RESTSC17            PROCESS THE NEXT DELETED MEMBER
         SPACE 3
         PRINT NOGEN
MSGRESTO MSG   '(MEMBER) RESTORED'
MSGNOTXT MSG   'NO TEXT FOUND FOR MEMBER'
MSGNXTRK MSG   'END OF MEMBER SIMULATED -- REMAINDER OF TRACK SKIPPED'
MSGRQUIT MSG   'RESTORE ABANDONED'
MSGIDRSD MSG   'INVALID LOAD MODULE DATA'
MSGCSECT MSG   'CSECTS ARE: '
MSGDELMB MSG   'DELETED MEMBER FOUND AT TTR 123456'
MSGDATAL MSG   'DATA LINE X:'
MSGMEMOK MSG   ' SHOULD THIS MEMBER BE RESTORED? -- ENTER Y (FOR YES) OX
               R N (FOR NO):'
MSGNORES MSG   'A MEMBER ALREADY EXISTS AT THIS TTR: (MEMBER)'
MSGNORE1 EQU   MSGNORES+41,8
         PRINT GEN
         TITLE 'P D S  --  PDS TSO                            02/06/84'
***********************************************************************
***      TSO   SUBCOMMAND      ADDED BY BRUCE LELAND -- AUG., 1982  ***
***********************************************************************
*
         SPACE 1
TSO      CSECT
         USING *,R8
         LA    R1,PARMLIST                 USE THE SAME PARAMETER LIST
         L     R15,ADDRSCAN                SCAN START ADDRESS
         BALR  R14,R15                     INVOKE COMMAND SCAN
         SPACE 1
         LA    R5,SCANANSR                       ADDR OF ANSWER AREA
         TM    CSOAFLG-CSOA(R5),CSOANOC          EMPTY BUFFER?
         BO    NEWCMD                            YES, NO TSO COMMAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP+CSOAVNP  VALID COMMAND?
         LA    R1,MSGTSOXX                       ASSUME NOT
         BZ    MSGNEW                            NO, BRANCH
         L     R2,CSOACNM-CSOA(,R5)              ADDR OF COMMAND NAME
         LH    R3,CSOALNM-CSOA(,R5)              LENGTH OF NAME
         BCTR  R3,0                              MACHINE LENGTH
         SPACE 1
         MVC   DIRNAME,BLANKS              INITIALIZE WITH BLANKS
         MVC   DIRNAME(*-*),0(R2)          <<EXECUTED>>
         EX    R3,*-6                      MOVE IN THE COMMAND NAME
         SPACE 1
         L     R1,ADDRCBUF                 COMMAND BUFFER ADDRESS
         ST    R1,ADDRTEXT                 COMMAND ADDRESS
         L     R4,ADDRECT                  ECT ADDRESS
         MVC   ECTPCMD-ECT(8,R4),DIRNAME   COMMAND NAME IS CHANGED
         OI    ECTSWS-ECT(R4),ECTNOPD      ASSUME NO OPERAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP    ANY OPERAND?
         BNO   *+8                         NO, BRANCH
         NI    ECTSWS-ECT(R4),FF-ECTNOPD   YES, ZAP THE NO OPERAND FLAG
         SPACE 1
         CLC   DIRNAME(5),=C'TIME '        TIME REQUEST?
         BNE   *+10                        NO, BRANCH
         MVC   DIRNAME(8),=C'IKJEFT25'     YES, USE IKJEFT25 INSTEAD
         SPACE 1
         CLC   DIRNAME(2),=C'H '           HELP REQUEST?
         BE    TSO010                      YES, BRANCH
         CLC   DIRNAME(8),HELPLIT          HELP REQUEST?
         BNE   TSO020                      NO, BRANCH
TSO010   MVC   DDNAMEH,DDNAME              SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP              DDNAME TO FREE
         BAL   R2,DEALLOCZ                 FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH              RESTORE ACTUAL DDNAME
         SPACE 1
TSO020   LA    R1,ADDRTEXT                 CPPL START
         MVI   ATTNECB,0                   CLEAR ATTENTION ECB
         LA    R3,DIRNAME                  PDS EXTERNAL SUBCOMMAND
         BAL   R2,ATTACH                   GO ATTACH IT
         B     NEWSTAX                     ISSUE STAX; GET NEXT COMMAND
         TITLE 'P D S  --  PDS USAGE                          02/06/84'
***********************************************************************
***      USAGE SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
USAGE    CSECT
         USING *,R8
         BAL   R2,DSN                   FORMAT THE ALLOCATION STATUS
         TM    DSORG,DS1DSGPO           DSORG=PO?
         BNO   USEDEB                   NO, BRANCH
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 1
         BAL   R14,READDIR              SCAN THROUGH THE DIRECTORY
         SPACE 1
         LA    R2,MSGUSEB               MSG - TOTAL BLOCKS ALLOCATED
         LH    R1,TOTBLOCK              GET COUNTER
         BAL   R4,USEFMT                FORMAT MESSAGE
         SPACE
         LA    R2,MSGUSED               MSG - TOTAL BLOCKS USED
         LH    R1,TOTUSED               GET COUNTER
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGTRKS               MSG - TOTAL TRACKS IN DIRECTORY
         LH    R1,TOTTRKS               GET COUNTER
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGMAXM               MSG - MAX. MEMBERS/DIRECTORY
         LH    R1,TOTMAXM               GET COUNTER
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSER               MSG - TOTAL MEMBERS
         LH    R1,TOTMEMR               GET COUNTER
         AH    R1,TOTMEMA
         BAL   R4,USEFMT
         SPACE
         LA    R2,MSGUSEA               MSG - TOTAL ALIASES
         LH    R1,TOTMEMA
         LTR   R1,R1                    ANY ALIASES?
         BZ    *+8                      NO
         BAL   R4,USEFMT
         SPACE 1
USEDEB   L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
USEXTNT  AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,USEXTNT              IF MORE EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         EJECT
         LH    R1,DSNTOTAL              TOTAL SPACE USED
         LA    R2,MSGUSESI              MSG FOR TOTAL SPACE
         BAL   R4,USEFMT
         SPACE
         LH    R1,DSNEMPTY              TOTAL FREE SPACE
         LA    R2,MSGUSEMP
         BAL   R4,USEFMT
         SPACE
         LH    R1,NUMEXT                TOTAL EXTENTS
         LA    R2,MSGUSEXT
         BAL   R4,USEFMT
         B     NEWCMD
         SPACE 2
USEFMT   LTR   R1,R1                    VALID NUMBER?
         BNM   *+6                      YES, BRANCH
         SR    R1,R1                    NO, USE ZERO
         CVD   R1,DOUBLE                CONVERT COUNTER TO DECIMAL
         SR    R15,R15
USEFMT1  SR    R0,R0
         D     R0,=F'10'                COMPUTE NUMBER OF SIGIF. DIGITS
         LTR   R1,R1                    RESIDUAL FACTOR ZERO?
         LA    R15,1(,R15)
         BNZ   USEFMT1                  NO, KEEP GOING
         LA    R1,MSGTEXT1+4(R15)       YES, WHERE OUTPUT TEXT GOES
         BCTR  R15,0                    EXECUTE LENGTH
         SLL   R15,4                    SHIFT FOR EXECUTED UNPACK
         OI    DOUBLE+7,X'0F'           CORRECT SIGN FOR EXECUTE
         UNPK  MSGTEXT1+4(*-*),DOUBLE   <<EXECUTED>>
         EX    R15,*-6                  UNPACK THIS NUMBER
         LH    R15,0(,R2)
         LA    R0,5
         SR    R15,R0
         MVC   0(*-*,R1),4(R2)          <<EXECUTED>>
         EX    R15,*-6
         LA    R15,1(R1,R15)
         LA    R0,MSGTEXT1
         SR    R15,R0
         SLL   R15,16
         ST    R15,MSGTEXT1
         MESSAGE MSGTEXT1
         BR    R4                       RETURN
         SPACE 1
         PRINT NOGEN
MSGUSEB  MSG   ' DIRECTORY BLOCKS ALLOCATED'
MSGUSED  MSG   ' DIRECTORY BLOCKS IN USE'
MSGTRKS  MSG   ' TRACKS USED FOR DIRECTORY BLOCKS'
MSGMAXM  MSG   ' MAXIMUM ENTRIES IN A DIRECTORY BLOCK'
MSGUSER  MSG   ' MEMBERS EXIST'
MSGUSEA  MSG   ' OF THEM ARE ALIASES'
MSGUSESI MSG   ' TRACKS ALLOCATED'
MSGUSEMP MSG   ' TRACKS UNUSED'
MSGUSEXT MSG   ' EXTENTS USED'
         PRINT GEN
         TITLE 'P D S  --  PDS VERIFY                   02/06/84'
***********************************************************************
***      VERIFY SUBCOMMAND     ADDED BY BRUCE LELAND --  MAY, 1983  ***
***********************************************************************
*
         SPACE 1
VERIFY   CSECT
         USING *,R8
         TM    FLAGSCC,RECFMF      FIXED FORMAT RECORDS?
         BNO   VERP00              NO, BRANCH
         LH    R15,BLKSI           BLKSIZE
         SR    R14,R14
         LH    R1,LRECL            LRECL
         LTR   R1,R1               ANY LRECL?
         BZ    VERP00              NO, AVOID DIVIDE ERROR
         DR    R14,R1              BLKSIZE/LRECL
         LTR   R14,R14             INTEGRAL NUMBER OF LRECL RECORDS?
         BZ    VERP00              YES, BRANCH
         MESSAGE MSGDIVER          NO, ERROR
         SPACE 2
VERP00   TM    DSORG,DS1DSGPO      PARTITIONED DATA SET?
         BNO   VERS00              NO, PERFORM OTHER GROUP OF CHECKS
         SPACE 1
         LA    R1,MSGNOTDR         ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO    CORRECT?
         BE    MSGNEW              YES, BRANCH
         TM    #VERUPDT,2          NOUPDATE SPECIFIED?
         BO    VERP09              YES, DO NOT OPEN STOW
         BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     VERP08              COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   MSGTEXT2,X'F9'          HIGH IN COLLATING SEQUENCE
         MVC   MSGTEXT2+1(7),FIXLIT    TEMPORARY STOW AND DELETE NAME
         MVI   MSGTEXT2+11,B'1001111'  ALIAS/MAX LENGTH ENTRY
         STOW  STOWDCB,MSGTEXT2,A      ADD THIS MEMBER
         SPACE 1
         LA    R1,MSGNOADD
         B     *+4(R15)                PROCESS RETURN CODE
         B     VERP06                    00 - SUCCESSFUL
         B     VERP06                    04 - MEMBER ALREADY EXISTS
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     VERP04                    12 - FULL DIRECTORY
         MESSAGE MSGIODIR                16 - I/O ERROR IN DIRECTORY
         B     VERP08
         SPACE 1
VERP04   MESSAGE MSGNODIR              DIRECTORY IS FULL
         B     VERP08                    12 - FULL DIRECTORY
         SPACE 2
VERP06   STOW  STOWDCB,MSGTEXT2,D      DELETE THIS MEMBER
         SPACE 1
         LTR   R15,R15                 ANY ERROR?
         BNP   VERP08                  NO, BRANCH
         MESSAGE MSGIODIR              YES, I/O ERROR IN DIRECTORY
VERP08   BAL   R2,SHUTSTOW         CLOSE THE STOW DCB; DEQUEUE
         SPACE 2
VERP09   MVI   SUBPOOLT,21         TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'     READ THE DIRECTORY
         LA    R5,1024/2           1024 MEMBERS INITIALLY
         SPACE 1
VERP10   SLL   R5,1                MEMBERS*2 FOR EACH LOOP
         SR    R6,R6
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)          ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1               NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR  ANY PREVIOUS MEMBERS?
         BZ    VERP12              NO, BRANCH
         SRL   R3,1                YES, USE SECOND HALF OF TABLE
         LR    R14,R4              NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3               MEMBERS*8 IS OLD TABLE SIZE
         LR    R6,R15              DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0              PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R6               LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
VERP12   ST    R4,#MEMPTR          MEMBER TABLE BASE
         AR    R4,R6               WHERE TO ADD MEMBERS
         SPACE 2
VERP14   BAL   R14,READDIR         GET THE NEXT MEMBER
         B     VERP50              END OF MEMBERS, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         SPACE 3
* CHECK FOR STANDARD MEMBER NAMES IF REQUIRED
         TM    #VERSTND,X'02'      STANDARD NAME CHECK?
         BNZ   VERP18              NO, BRANCH
         SPACE 1
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),MEMNAME
         SPACE 1
VERP16   BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERP16                                 NON-BLANK
         SPACE 1
         LA    R2,VERP18           EXIT ADDRESS
         CLI   MEMNAME,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR              YES, NON-STANDARD
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         LA    R2,VERP18
         BNZ   VERMBR              NO, BRANCH
         SPACE 3
* CHECK FOR CONFLICTING LOAD MODULE ATTRIBUTES
VERP18   L     R14,DIRPTRS                 CURRENT MEMBER
         MVC   DIRNAME(12),0(R14)          MOVE IN NAME, FIRST TTR
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BZ    VERP28                      NO, BRANCH
         MVC   DIRNAME(40),0(R14)          MOVE IN NAME, LKED FLAGS
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BNO   VERP20                      NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BZ    VERP20                      NO, BRANCH
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    CORRECT?
         BO    VERP19                      YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    VERP20                      YES, BRANCH
         LA    R1,=C'24 '                  AMODE MUST BE 24
VERP19   MVC   MSGTEXT1,MSGRESAD
         MVC   MSGTEXT1+MSGRESAM(3),0(R1)  MOVE IN THE AMODE TEXT
         BAL   R2,VERMBR                   OUTPUT AN ERROR MESSAGE
         SPACE 3
VERP20   TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   VERP22                          NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    VERP22                          YES, BRANCH
         MVC   MSGTEXT1,MSGRENTR               RENT AND NOT REUS
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
VERP22   MVC   MSGTEXT1,MSGTSTNE               TEST AND NOT EDIT
         TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   MSGTEXT1,MSGREUSC               REUSABLE AND SCATTER
         TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   VERP24                          NO, BRANCH
         MVC   MSGTEXT1,MSGOVCON               OVERLAY AND REENTRANT
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),REUSTXT+3  OVERLAY AND REUSABLE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),REFRTXT+3  OVERLAY AND REFRESHABLE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   MSGTEXT1+MSGOVCOD(4),SCTRTXT+3  OVERLAY AND SCATTER
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   VERP24                          NO, BRANCH
         MVC   MSGTEXT1+MSGOVCOD(5),=C'RMODE'
         MVC   MSGTEXT1+MSGOVCOD+6(3),=C'ANY'  RMODE ANY
         TM    DIRATTR3,DIRRMANY               RMODE ANY?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVI   MSGTEXT1+MSGOVCOD,C'A'          AMODE ANY
         SR    R1,R1
         IC    R1,DIRATTR3                     AMODE BITS
         TM    DIRFLAG,X'80'                   ALIAS?
         BNO   *+8                             NO, BRANCH
         SRL   R1,2                            YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                     SAVE CURRENT AMODE
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    VERP24                          YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    *+10                            YES, BRANCH
         MVC   MSGTEXT1+MSGOVCOD+6(3),=C'31 '  NO, AMODE 31
         BAL   R2,VERMBR                       ERROR MESSAGE
         SPACE 3
* LOAD EACH MEMBER IF REQUIRED AND THIS IS A LOAD LIBRARY
VERP24   TM    #VERLOAD,X'02'      LOAD CHECK REQUESTED?
         BNZ   VERP28              NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BZ    VERP28              NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,@N         ENTER QUIET MODE PROCESSING
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,VERP26           RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,#SAVEREG      SAVE REGISTERS
         LOAD   EPLOC=MEMNAME,DCB=INDCB
         DELETE EPLOC=MEMNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         B     VERP28
         SPACE 1
VERP26   LM    R2,R8,#SAVEREG      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVC   MSGTEXT1+MSGABETX(32),=CL32'LOADING THIS MODULE'
         BAL   R2,VERMBR           OUTPUT AN ABENDED MESSAGE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
VERP28   OI    ##ADRPA#,@N         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         BAL   R2,VERMBR           YES, OUTPUT AN IN-USE MESSAGE
         SPACE 3
* CHECK THAT THE PDS DIRECTORY MEMBERS ARE IN CORRECT COLLATING ORDER
VERP30   NI    ##ADRPA#,FF-@N      LEAVE QUIET MODE PROCESSING
         CLC   #VERMBR(8),MEMNAME  PREVIOUS:CURRENT
         BH    VERP32                HIGH  -- ERROR, OUT OF ORDER
         MVC   #VERMBR(8),MEMNAME
         BL    VERP34                LOW   -- VALID
         MVC   MSGTEXT1,MSGDUPMB     EQUAL -- ERROR, DUPLICATE
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 1
VERP32   MVC   MSGTEXT1,MSGORDER
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 3
* SELECT THE HIGHEST TTR POINTERS FOR THIS MEMBER
VERP34   MVC   0(8+4,R4),DIRNAME   MOVE IN THE NAME, FIRST TTR
         XC    12(4,R4),12(R4)     CLEAR THE SECOND TTR ADDRESS
         TM    MEMTTR+3,DIR1TTR+DIR2TTR  ANY OTHER TTR'S?
         BZ    VERP35                    NO, BRANCH
         SPACE 1
         MVC   12(3,R4),DIRNAME+12 ADD FIRST USER TTR
         TM    MEMTTR+3,DIR2TTR    SECOND TTR?
         BZ    VERP35              NO, BRANCH
         SPACE 1
         CLC   12(3,R4),DIRNAME+16 FIRST TTR > SECOND TTR?
         BH    *+10                YES, BRANCH
         MVC   12(3,R4),DIRNAME+16 NO, USE SECOND TTR
         SPACE 1
VERP35   NI    11(R4),X'80'        SAVE ONLY THE ALIAS BIT
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         MVC   MSGTEXT1,MSGTTRHI
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         B     VERP38
         SPACE 1
VERP36   CLC   12(3,R4),DS1LSTAR   IN DATA SET BOUNDS?
         BNH   VERP37              YES, BRANCH
         MVC   MSGTEXT1,MSGTOBIG
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         SPACE 1
VERP37   MVI   15(R4),X'FF'        ASSUME NOT AN OS/VS LINKAGE-EDITOR
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP38              NO, BRANCH
         TM    DIRATTR2,DIRAOSLE   OS/VS LINKAGE-EDITOR?
         BNO   VERP38              NO, BRANCH
         NI    DIRATTR3,DIRRMANY+DIRAM31+DIRAM24
         OC    11(1,R4),DIRATTR3   SAVE THE RMODE/AMODE CHARACTER
         MVC   15(1,R4),DIRATTR4   SAVE THE RLD/CONTROL COUNT
         SPACE 3
* PERFORM AN INSERTION SORT OF THIS ENTRY
VERP38   MVC   DIRNAME(16),0(R4)   SAVE ENTRY FOR INSERTION SORT
         LR    R14,R4              LAST SORT POSITION
         L     R1,#MEMPTR          FIRST SORT POSTION
         SH    R1,=H'16'           ADJUST FOR NEXT LA
         SPACE 1
VERP40   LA    R1,16(,R1)          NEXT TABLE ENTRY
         CLC   8(4,R1),DIRTTR      TABLE TTR:NEW TTR
         BL    VERP40                LOW   --  ITERATE
         BH    VERP42                HIGH  --  INSERT
         CLC   0(8,R1),DIRNAME       EQUAL --  TABLE NAME:NEW NAME
         BL    VERP40                            LOW   -- ITERATE
         BE    VERP44                            EQUAL -- IN PLACE
         SPACE 1
VERP42   SH    R14,=H'16'          PREVIOUS TABLE ENTRY
         MVC   16(16,R14),0(R14)   SLIDE DOWN TO MAKE ROOM
         CR    R14,R1              DONE WITH SHIFTING?
         BNL   VERP42              NO, BRANCH
         MVC   0(16,R1),DIRNAME    INSERT NEW ENTRY TO ITS POSITION
         SPACE 2
VERP44   LA    R4,16(,R4)          NEXT TABLE ENTRY
         S     R3,=F'1'            ANY MORE ENTRIES FIT?
         BP    VERP14              YES, BRANCH
         B     VERP10              NO, REALLOCATE THE MEMBER TABLE
         SPACE 3
* DIRECTORY TTR SORT COMPLETE -- CHECK FOR ORPHAN MEMBERS, APPARENT
* ALIAS MEMBERS AND ALIAS MEMBERS WITH INCORRECT REAL MEMBER POINTERS
VERP50   LA    R1,MSGEMPTY
         ST    R4,#MEMLAST         SAVE THE END POINTER
         ICM   R3,B'1111',#MEMPTR  FIRST MEMBER
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         OI    #VERSTND,X'02'      NO MORE MEMBER NAME CHECKING
         LR    R5,R3
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BO    VERP54              YES, BRANCH
         B     VERP58+4
         SPACE 1
VERP52   LA    R3,16(,R5)          NEXT TABLE ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R3,R4               DONE?
         BE    VERP66              YES, BRANCH
         LR    R5,R3
         SPACE 1
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BNO   VERP58+4            NO, BRANCH
VERP54   MVC   MSGTEXT1,MSGNOMOD   YES, THIS IS AN ORPHAN
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         NI    8+3(R3),X'FF'-X'80' TURN OFF THE ALIAS BIT (ORPHAN)
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP52              NO, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,0(R3)       MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         MVC   MSGTEXT1,MSGXREAL  DIRECTORY NAME OF REAL MODULE
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   MSGTEXT1+MSGXREA1-MSGXREAL(8),DIRREALS
         TR    MSGTEXT1+MSGXREA1-MSGXREAL(8),TRLINE
         MESSAGE MSGTEXT1
         B     VERP52
         SPACE 2
VERP58   LA    R5,16(,R5)          NEXT POTENTIAL ALIAS ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R5,R4               DONE?
         BE    VERP66              YES, BRANCH
         CLC   8(3,R3),16+8(R5)    TTR FOR AN ALIAS?
         BNE   VERP52              NO, BRANCH
         TM    16+8+3(R5),X'80'    ALIAS FLAG SET?
         BNO   VERP64              NO, APPARENT ALIAS
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP58              NO, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,16(R5)      MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         LA    R1,DIRREAL          NAME OF CORRESPONDING REAL
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRREALS         YES, USE THIS NAME INSTEAD
         SPACE 1
         CLC   0(8,R1),0(R3)       CORRECT NAME RECORDED?
         BE    VERP60              YES, BRANCH
         MVC   MSGTEXT1,MSGALIAB   NO, ALIAS POINTER ERROR
         MVC   MSGTEXT1+MSGALIA1-MSGALIAB(8),0(R3)
         TR    MSGTEXT1+MSGALIA1-MSGALIAB(8),TRLINE
         MVC   MSGTEXT1+MSGALIA2-MSGALIAB(8),0(R1)
         TR    MSGTEXT1+MSGALIA2-MSGALIAB(8),TRLINE
         MVC   MEMNAME(8),16(R5)
         BAL   R2,VERMBR
         SPACE 1
VERP60   CLC   15(1,R3),16+15(R5)  MATCHING RLD/CONTROL COUNTS?
         BE    VERP62              YES, BRANCH
         MVC   MSGTEXT1,MSGRLDAL   NO, RLD/CONTROL MISMATCH
         MVC   MSGTEXT1+4(8),16(R5)
         TR    MSGTEXT1+4(8),TRLINE
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         SPACE 1
VERP62   MVI   DOUBLE,DIRRMANY       BIT TO TEST
         MVI   DOUBLE+1,DIRRMANY     BIT TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS RMODE BIT
         NC    DOUBLE+1(1),11(R3)    MAIN RMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    RMODE BITS EQUAL?
         BE    VERP63                YES, BRANCH
         MVC   MSGTEXT1,MSGRMODX
         MVC   MSGTEXT1+4(8),16(R5)
         TR    MSGTEXT1+4(8),TRLINE
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         SPACE 1
VERP63   MVI   DOUBLE,DIRAM31+DIRAM24    BITS TO TEST
         MVI   DOUBLE+1,DIRAM31+DIRAM24  BITS TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS AMODE BITS
         NC    DOUBLE+1(1),11(R3)    MAIN AMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    AMODE BITS EQUAL?
         BE    VERP58                YES, BRANCH
         MVC   MSGTEXT1,MSGAMODX
         MVC   MSGTEXT1+4(8),16(R5)
         TR    MSGTEXT1+4(8),TRLINE
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         B     VERP58
         SPACE 1
VERP64   MVC   MSGTEXT1,MSGAPPAR   APPARENT ALIAS
         MVC   MSGTEXT1+MSGAPPMB-MSGAPPAR(8),16(R5)
         TR    MSGTEXT1+MSGAPPMB-MSGAPPAR(8),TRLINE
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         OI    16+8+3(R5),X'80'    TURN ON ALIAS BIT (APPARENT ALIAS)
         B     VERP58
         SPACE 3
* END OF DIRECTORY CHECK, NOW PERFORM ANY INPUT CHECK
VERP66   MVI   STARTTR+2,X'01'     TTR=000001 (START OF DIRECTORY)
         SPACE 1
VERP68   BAL   R14,EXCP            READ THE DIRECTORY RECORDS
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP70                00 - SUCCESSFUL READ
         B     VERP72                04 - END OF MEMBER
         B     VERP72                08 - END OF DATA SET
         B     IOERROR               12 - I/O ERROR
         SPACE 2
VERP70   L     R0,LS               LENGTH OF RECORD (DD OF RKDD)
         ICM   R0,B'0100',=X'08'   KEY LENGTH OF RECORD (K OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         B     VERP68              READ ALL DIRECTORY BLOCKS
         SPACE 1
VERP72   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OI    ##ADRPA#,@N         ENTER QUIET MODE PROCESSING
         CLI   CURMBB,0            MULTI-EXTENT DIRECTORY?
         BE    VERP73              NO, BRANCH
         MESSAGE MSGBLANK          YES, ERROR MESSAGE
         MESSAGE MSGBLANK
         MESSAGE MSGDIRXT
         SPACE 1
VERP73   ICM   R6,B'1111',#MEMPTR  FIRST MEMBER
         CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERP99              YES, BRANCH
         B     VERP74+4
         SPACE 2
VERP74   LA    R6,16(,R6)          NEXT TABLE ENTRY
         MVI   RLDCOUNT,X'FF'      ASSUME NO RLD/CONTROL IS FOUND
         C     R6,#MEMLAST         DONE?
         BE    VERP97              YES, BRANCH
         TM    8+3(R6),X'80'       ALIAS ENTRY?
         BO    VERP74              YES, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         MVC   MEMNAME(8),0(R6)    INITIALIZE THE MEMBER NAME
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         LA    R5,3                ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 1
         CLC   8(3,R6),DS1LSTAR    BEYOND END OF DATA MARKER?
         BNL   VERP90              YES, DONE
         MVC   STARTTR(3),8(R6)    NO, FIRST DISK ADDRESS TO READ
         SPACE 3
VERP76   BAL   R14,EXCP
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP80                00 - SUCCESSFUL READ
         B     VERP92                04 - END OF MEMBER
         B     VERP90                08 - END OF DATA SET
         B     VERP78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF MEMBER
VERP78   BAL   R2,VERMBR           OUTPUT THE I/O ERROR MESSAGE
         B     VERP92
         SPACE 2
VERP80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         LR    R0,R5               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERP81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP82              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         MVC   MSGTEXT1,MSGBLKSI   ERROR MESSAGE
         CVD   R5,DOUBLE
         ED    MSGTEXT1+MSGBLKS#-MSGBLKSI(7),DOUBLE+5
         BAL   R2,VERMBR
         SPACE 1
VERP81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERP82              NO, BRANCH
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERP82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         LH    R14,LRECL           CURRENT LRECL
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERP82              YES, BRANCH
         MVC   MSGTEXT1,MSGBLKDV   NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 1
VERP82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERP88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERP87              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERP86
         SPACE 3
VERP84   BXLE  R3,R4,VERP86        GET THE NEXT LOGICAL RECORD
         B     VERP76              END OF BLOCK, BRANCH
         SPACE 2
VERP86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BNM   VERP88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERP76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVC   MSGTEXT1,MSGLREC0   ERROR MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,MSGTEXT1+27     PLACE IN THE MESSAGE
         BAL   R2,VERMBR
         B     VERP76
         SPACE 2
VERP87   CLI   RLDCOUNT,X'FF'      ANY RLD/CONTROL RECORD YET?
         BNE   VERP88              YES, BRANCH
         TM    0(R3),X'F0'         TEST, ESD, IDR OR SCATTER RECORD?
         BM    VERP88              YES, BRANCH
         MVC   RLDCOUNT(1),3(R3)   NO, FIRST RLD/CONTROL ENTRY
         SPACE 2
VERP88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERP84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERP84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         MVC   MSGTEXT1,MSGLRECL   ERROR MESSAGE
         CVD   R14,DOUBLE
         ED    MSGTEXT1+MSGLREC#-MSGLRECL(7),DOUBLE+5
         BAL   R2,VERMBR
         B     VERP84
         SPACE 2
VERP90   MVI   #MEMCNT,X'FF'              END OF DATA SET
         SPACE 2
VERP92   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OC    12(3,R6),12(R6)            ANY USER TTR?
         BZ    VERP93                     NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',12(R6)          TTR ACTUALLY INPUT?
         BH    VERP93                     YES, BRANCH
         MVC   MSGTEXT1,MSGTTRUP          NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 1
VERP93   OC    #MEMCNT+1(3),#MEMCNT+1  ANY RECORDS?
         BNZ   VERP94                  YES, BRANCH
         MVC   MSGTEXT1,MSGNULLM   NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 2
VERP94   CLI   15(R6),X'FF'        OS/VS LINKAGE EDITOR?
         BE    VERP96              NO, BRANCH
         CLC   15(1,R6),RLDCOUNT   RLD/CONTROL COUNTS MATCH?
         BE    VERP96              YES, BRANCH
         MVC   MSGTEXT1,MSGRLDCN   NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 2
VERP96   CLI   #MEMCNT,X'FF'       END OF DATA SET?
         BNE   VERP74              NO, BRANCH
         MESSAGE MSGBLANK
         MESSAGE MSGEOFDS          YES, STATUS MESSAGE
         SPACE 2
VERP97   MESSAGE MSGBLANK
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   MSGTEXT1,MSGCLOG
         ED    MSGTEXT1+MSGCLOGE-MSGCLOG(10),DOUBLE+4
         MESSAGE MSGTEXT1
         SPACE 1
VERP98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   MSGTEXT1,MSGCBLK
         ED    MSGTEXT1+MSGCBLKE-MSGCBLK(10),DOUBLE+4
         MESSAGE MSGTEXT1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERP98B             NO, BRANCH
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   MSGTEXT1,MSGCBLC
         ED    MSGTEXT1+MSGCBLCE-MSGCBLC(10),DOUBLE+4
         MESSAGE MSGTEXT1
         SPACE 1
VERP98B  LH    R0,DS1LSTAR         GET TT OF LAST TRACK USED
         S     R0,#VERCTRK         LESS THE AMOUNT AFTER THE COMPRESS
         BNM   *+6                 ZERO OR MORE, BRANCH
         SR    R0,R0               NEGATIVE, ERROR (USE ZERO)
         CVD   R0,DOUBLE           NUMBER OF WHOLE COMPRESSED TRACKS
         MVC   MSGTEXT1,MSGCOMP
         ED    MSGTEXT1+MSGCOMPE-MSGCOMP(10),DOUBLE+4
         MESSAGE MSGTEXT1
         B     VERP99B
         SPACE 2
VERP99   MESSAGE MSGBLANK
         SPACE 1
VERP99B  L     R1,#MEMLAST         LAST MEMBER POINTER
         L     R2,#MEMPTR          START OF MEMBER LIST
         LA    R2,0(,R2)           CLEAR HIGH-ORDER DIGIT
         SR    R1,R2               (END+1 - START) * 16
         SRL   R1,4                DIVIDE BY 16
         MVC   MSGTEXT1,MSGCOUNT
         CVD   R1,DOUBLE
         ED    MSGTEXT1+MSGCOUN#-MSGCOUNT(10),DOUBLE+4
         MESSAGE MSGTEXT1
         MESSAGE MSGBLANK
         B     NEWCMD
         SPACE 2
VERMBR   ST    R2,#SAVEREG         SAVE REGISTER
         CLC   #VERMEM(8),MEMNAME  MEMBER NAME DISPLAYED?
         BE    VERMBR20            YES, BRANCH
         MVC   #VERMEM(8),MEMNAME  MEMBER NAME DISPLAYED
         SPACE 1
         MVC   MSGTEXT2,MSGMEMGP
         MVC   MSGTEXT2+4+3(8),##SUBCOM
         TM    DSORG,DS1DSGPO      PARTITIONED?
         BNO   VERMBR10            NO, BRANCH
         MESSAGE MSGBLANK          ONE BLANK LINE
         MVC   MSGTEXT2+4+3+8+1(8),MEMNAME
         TR    MSGTEXT2+4+3+8+1(8),TRLINE
         MVC   DOUBLE(8),MEMNAME
         LA    R1,7
         LA    R14,DOUBLE+8
         SPACE 1
VERMBR02 BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERMBR02                               NON-BLANK
         SPACE 1
         CLI   MEMNAME,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR04            YES, NON-STANDARD
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              VALID CHARACTERS?
         BZ    VERMBR10            YES, BRANCH
         SPACE 1
VERMBR04 MVC   MSGTEXT2+4+3+8+1+8+2(2),=C'X'''
         UNPK  MSGTEXT2+4+3+8+1+8+2+2(9),MEMNAME(5)
         UNPK  MSGTEXT2+4+3+8+1+8+2+2+8(9),MEMNAME+4(5)
         TR    MSGTEXT2+4+3+8+1+8+2+2(8+8),TRTABLE
         MVI   MSGTEXT2+4+3+8+1+8+2+2+8+8,C''''
         TM    #VERSTND,X'02'          STANDARD NAME CHECKING?
         BNZ   VERMBR10                NO, BRANCH
         MVC   MSGTEXT2+48(24),=CL24'NON-STANDARD MEMBER NAME'
         MVC   MSGTEXT1,MSGTEXT2
         B     VERMBR20
         SPACE 1
VERMBR10 MESSAGE MSGTEXT2          MEMBER HEADER MESSAGE
         SPACE 1
VERMBR20 MESSAGE MSGTEXT1          WARNING TEXT
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 2
VERCOMP  ST    R0,#VERCREG
         SPACE 1
VERCOMP2 L     R0,#VERCREG
         L     R1,#VERCREC
         A     R1,=F'1'            CURRENT COMPRESS RECORD
         ST    R1,#VERCREC
         ICM   R0,B'1000',#VERCREC+3  RECORD NUMBER IN RKDD (R OF RKDD)
         SPACE 1
         TRKCALC FUNCTN=TRKBAL,TYPE=BYTEUCB+1,REGSAVE=YES,             X
               BALANCE=#VERCBAL+2,RKDD=(0),MF=(E,PARMLIST)
         SPACE 1
         LTR   R15,R15             FIT ON CURRENT TRACK?
         BZ    VERCOMPX            YES, BRANCH
         SPACE 1
         L     R1,#VERCTRK
         A     R1,=F'1'            START A NEW TRACK IN OUTPUT
         ST    R1,#VERCTRK
         MVI   #VERCREC+3,0        RECORD ZERO
         B     VERCOMP2            TRY AGAIN
         SPACE 1
VERCOMPX ST    R0,#VERCBAL         SAVE THE TRACK BALANCE
         BR    R2
         EJECT
VERS00   OI    ##ADRPA#,@N         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   MEMNAME(8),BLANKS   INITIALIZE THE MEMBER NAME
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         B     *+8                 YES, BRANCH
         B     VERS40              NO, BRANCH
         MVC   MSGTEXT1+4(8),=C'DATA SET'
         MESSAGE MSGTEXT1             DATA SET IN USE MESSAGE
         SPACE 2
VERS40   CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERS99              YES, BRANCH
         SPACE 2
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         MVI   STARTTR+2,X'01'     TTR=000001 (START OF DATA SET)
         SPACE 2
VERS76   BAL   R14,EXCP
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERS80                00 - SUCCESSFUL READ
         B     VERS90                04 - END OF FILE
         B     VERS92                08 - END OF DATA SET
         B     VERS78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF DATA SET
VERS78   MESSAGE MSGTEXT1          OUTPUT THE I/O ERROR MESSAGE
         B     VERS97
         SPACE 2
VERS80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERS81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS82              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         MVC   MSGTEXT1,MSGBLKSI   ERROR MESSAGE
         CVD   R5,DOUBLE
         ED    MSGTEXT1+MSGBLKS#-MSGBLKSI(7),DOUBLE+5
         MESSAGE MSGTEXT1
         SPACE 1
VERS81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERS82              NO, BRANCH
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERS82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         LH    R14,LRECL           CURRENT LRECL
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERS82              YES, BRANCH
         MVC   MSGTEXT1,MSGBLKDV   NO, ERROR MESSAGE
         MESSAGE MSGTEXT1
         SPACE 1
VERS82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERS86
         SPACE 3
VERS84   BXLE  R3,R4,VERS86        GET THE NEXT LOGICAL RECORD
         B     VERS76              END OF BLOCK, BRANCH
         SPACE 2
VERS86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BP    VERS88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERS76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVC   MSGTEXT1,MSGLREC0   ERROR MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,MSGTEXT1+27     PLACE IN THE MESSAGE
         MESSAGE MSGTEXT1
         B     VERS76              YES, BRANCH
         SPACE 2
VERS88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERS84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERS84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         MVC   MSGTEXT1,MSGLRECL   ERROR MESSAGE
         CVD   R14,DOUBLE
         ED    MSGTEXT1+MSGLREC#-MSGLRECL(7),DOUBLE+5
         MESSAGE MSGTEXT1
         B     VERS84
         SPACE 2
VERS90   MESSAGE MSGBLANK
         MESSAGE MSGEOF                   END OF FILE
         MESSAGE MSGBLANK
         B     VERS76                     LOOP
         SPACE 2
VERS92   MESSAGE MSGBLANK
         MESSAGE MSGEOFDS                 END OF DATA SET
         SPACE 2
VERS97   TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   MSGTEXT1,MSGCLOG
         ED    MSGTEXT1+MSGCLOGE-MSGCLOG(10),DOUBLE+4
         MESSAGE MSGTEXT1
         SPACE 1
VERS98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   MSGTEXT1,MSGCBLK
         ED    MSGTEXT1+MSGCBLKE-MSGCBLK(10),DOUBLE+4
         MESSAGE MSGTEXT1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERS99              NO, BRANCH
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   MSGTEXT1,MSGCBLC
         ED    MSGTEXT1+MSGCBLCE-MSGCBLC(10),DOUBLE+4
         MESSAGE MSGTEXT1
         B     NEWCMD
         SPACE 2
VERS99   MESSAGE MSGBLANK
         LA    R1,MSGVOVER
         B     MSGNEW
         SPACE 3
         PRINT NOGEN
MSGRLDAL MSG   '12345678 RLD/CONTROL COUNTS DO NOT CORRESPOND'
MSGDUPMB MSG   'DUPLICATE MEMBER NAME, IGNORED'
MSGORDER MSG   'MEMBER NAME IS OUT OF SEQUENCE, IGNORED'
MSGNULLM MSG   'NOTE: THIS IS A NULL MEMBER'
MSGVOVER MSG   'DATA SET WAS CHECKED'
MSGBLKDV MSG   'THE INPUT RECORD SIZE IS NOT DIVISIBLE BY THE RECORD LEX
               NGTH'
MSGCOUNT MSG   ' 1,234,567 MEMBERS WERE CHECKED'
         ORG   MSGCOUNT+4
MSGCOUN# DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
         SPACE 1
MSGBLKSI MSG   'BLOCK LENGTH OF 12,345 EXCEEDS THE DATA SET BLKSIZE'
         ORG   MSGBLKSI+19
MSGBLKS# DC    X'4020206B202120'         EDIT PATTERN
         ORG   ,
MSGLREC0 MSG   'WARNING: RECORD LENGTH X IS INVALID'
MSGLRECL MSG   'RECORD LENGTH OF 12,345 EXCEEDS THE MAXIMUM LRECL'
         ORG   MSGLRECL+20
MSGLREC# DC    X'4020206B202120'         EDIT PATTERN
         ORG   ,
MSGCOMP  MSG   ' 1,234,567 TRACKS COULD BE REGAINED BY COMPRESSING THISX
                DATA SET'
         ORG   MSGCOMP+4
MSGCOMPE DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGCLOG  MSG   ' 1,234,567 LOGICAL RECORDS WERE INPUT'
         ORG   MSGCLOG+4
MSGCLOGE DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGCBLK  MSG   ' 1,234,567 PHYSICAL BLOCKS WERE INPUT'
         ORG   MSGCBLK+4
MSGCBLKE DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGCBLC  MSG   ' 1,234,567 CHARACTERS PER AVERAGE PHYSICAL BLOCK'
         ORG   MSGCBLC+4
MSGCBLCE DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGALIAB MSG   'THIS MEMBER IS AN ALIAS FOR (MEMBER) BUT IT POINTS TO (X
               MEMBER) '
MSGALIA1 EQU   MSGALIAB+32,8
MSGALIA2 EQU   MSGALIAB+58,8
MSGRMODX MSG   '(MEMBER) RMODE ENTRIES DO NOT CORRESPOND'
MSGAMODX MSG   '(MEMBER) MAIN AMODE ENTRIES DO NOT CORRESPOND'
         PRINT GEN
PDS61    CSECT
         TITLE 'P D S  --  PDS CHANGE                         02/06/84'
***********************************************************************
***      CHANGE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
CHANGE   LA    R1,DSN                   ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         SPACE 2
CHANGE2  NI    FLAGSFF,FF-FCHANGE       DATA SET WAS CHANGED
         LA    R0,RESTART               ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAL   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET
         TM    FLAGSAA,FOPTIONS         ANY OPERANDS?
         BO    RESTART                  YES, BRANCH
         SPACE 2
         CLI   SAVDSN+1+2,0             ANY OTHER DSNAME?
         BE    RESTART                  NO, DONE
         SPACE 2
         XC    SAVDSN+1(SAVLEN),DSNLEN  EXCHANGE
         XC    DSNLEN(SAVLEN),SAVDSN+1          OLD AND NEW
         XC    SAVDSN+1(SAVLEN),DSNLEN                     DSNAMES
         MVC   DSPALLOC,SAVDSN          GET PREVIOUS DISPOSITION
         MVC   SAVDSN(1),DSPREQST       SAVE REQUESTED DISPOSITION
         MVC   DSPREQST,DSPALLOC        GET PREVIOUS DISPOSITION
         B     RESTART                  BRANCH TO REALLOCATE
         TITLE 'P D S  --  PDS KLEAR                          02/06/84'
***********************************************************************
***      KLEAR   SUBCOMMAND  (SCREEN CLEAR FOR 3270 SCREENS)        ***
***********************************************************************
*
         SPACE 1
KLEAR    LA    R2,NEWCMD           A K OR KLEAR COMMAND WAS ENTERED
CLEAR    ICM   R0,B'1111',PAGESIZE 3270 DISPLAY SCREEN?
         BZ    CLEAR2              NO, JUST WRITE A BLANK LINE
         SPACE 1
         STFSMODE ON               TURN ON FULLSCREEN MODE
         SPACE 1
         LA    R0,12               SET SIZE FOR TPUT
         LA    R1,=X'F1115D7E114040133C404000'
         ICM   R1,B'1000',=X'03'   FULLSCR OPTION BITS
         TPUT  (1),(0),R           FULL SCREEN TPUT TO CLEAR THE SCREEN
CLEAR1   STLINENO LINE=1,MODE=OFF  TURN OFF FULL SCREEN MODE
         SPACE 1
         TCLEARQ INPUT             CLEAR INPUT QUEUE FOR SYNCRONIZATION
         BR    R2
         SPACE 1
CLEAR2   MESSAGE MSGBLANK          WRITE A BLANK LINE ONLY
         BR    R2
         TITLE 'P D S  --  PDS DSN                            02/06/84'
***********************************************************************
***      DSN SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
DSN      MVC   MSGTEXT1(24),MSGDSNDD     ADD "//12345678  DD  DSN="
         MVC   MSGTEXT1+4+2(8),DDNAME    MOVE IN THE DDNAME
         LA    R1,MSGTEXT1+4+20          DSNAME START POSITION
         MVC   0(44,R1),DSNAME           MOVE IN THE DSNAME
         AH    R1,DSNLEN                 LENGTH OF THE DSNAME
         MVC   0(15,R1),DISPTXT          ADD ",DISP=SHR,UNIT="
         CLI   DSPALLOC,DA08SHR          DISP=SHR?
         BE    *+10                      YES, BRANCH
         MVC   6(3,R1),=C'OLD'           NO, DISP=OLD
         LH    R15,BYTEUCB               UCBBYTE
         MH    R15,=H'7'                 INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)          DEVICE UNIT TYPE
         MVC   15(7,R1),0(R15)           ADD TO THE MESSAGE
         LA    R1,15+7(,R1)              BYTES ADDED TO THE MESSAGE
         LA    R15,MSGTEXT1              MESSAGE START
         SR    R1,R15                    LENGTH OF MESSAGE
         STH   R1,MSGTEXT1               UPDATE MESSAGE LENGTH
         MESSAGE MSGTEXT1
         MVC   MSGTEXT1(24),MSGDCB       "// DCB=(RECFM=, ..."
         IC    R5,FLAGSCC                RECFM BITS
         SLL   R5,29                     DROP TOP BITS
         SRL   R5,29                     REPOSITION
         IC    R5,RECFMTYP(R5)           RECORD FORMAT
         STC   R5,MSGTEXT1+19            MOVE INTO MESSAGE
         LA    R1,MSGTEXT1+20
         TM    RECFM,DCBRECBR            RECFM=.B?
         BNO   *+12                      NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    RECFM,DCBRECSB            RECFM=.S?
         BNO   *+12                      NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    RECFM,DCBRECTO            RECFM=.T?
         BNO   *+12                      NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCA            RECFM=.A?
         BNO   *+12                      NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCM            RECFM=.M?
         BNO   *+12                      NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         TM    FLAGSCC,RECFMU            RECFM=U?
         BO    DSNBLK                    YES, NO LRECL
         MVC   0(7,R1),LRECLTXT          MOVE IN LRECL TEXT
         LA    R1,7(R1)
         LH    R4,LABLRECL
         BAL   R14,DSNCONV
         SPACE 1
DSNBLK   MVC   0(9,R1),BLKSITXT
         LA    R1,9(R1)
         LH    R4,BLKSI
         BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0001',DS1KEYL        ANY KEY LENGTH?
         BZ    DSNDSORG                  NO, BRANCH
         MVC   0(8,R1),KEYLENTX          ADD ,KEYLEN=
         LA    R1,8(R1)
         BAL   R14,DSNCONV
         SPACE 1
         MVC   0(5,R1),RKPTXT            ADD ,RKP=
         LA    R1,5(R1)
         SR    R4,R4
         ICM   R4,B'0011',DS1RKP         RELATIVE KEY POSITION
         BAL   R14,DSNCONV
         SPACE 1
DSNDSORG TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSNVOL                   NO, BRANCH
         MVC   0(7,R1),DSORGTXT         ADD ,DSORG=
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   7(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   7(2,R1),=C'VS'
         LA    R1,9(,R1)                SKIP OVER DSORG TEXT
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSNVOL                   NO, BRANCH
         MVI   0(R1),C'U'               ADD U
         LA    R1,1(,R1)                SKIP OVER U
         SPACE 1
DSNVOL   MVC   0(10,R1),VOLSERTX        ADD ),VOL=SER=
         MVC   10(6,R1),VOLALLOC        ADD THE SERIAL NUMBER
         LA    R4,16(,R1)               INCREMENT THE MESSAGE LENGTH
         LA    R1,MSGTEXT1
         SR    R4,R1
         STH   R4,0(R1)
         MESSAGE (R1)                   OUTPUT THE DCB LINE
         BR    R2
         EJECT
DSNCONV  CVD   R4,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R4,DOUBLE-1
         LA    R4,1(R4)
         CLI   0(R4),C'0'
         BE    *-8
         OI    DOUBLE+4,X'F0'
DSNMOVE  MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         TM    1(R4),X'F0'
         LA    R4,1(R4)
         BO    DSNMOVE
         BR    R14
         SPACE 1
         PRINT NOGEN
MSGDSNDD MSG   '//12345678  DD  DSN=,DISP=SHR,UNIT='
DISPTXT  EQU   MSGDSNDD+4+20,15
MSGDCB   MSG   '//  DCB=(RECFM=,LRECL=,BLKSIZE=,DSORG=**,KEYLEN=,RKP=),X
               VOL=SER='
LRECLTXT EQU   MSGDCB+19,7
BLKSITXT EQU   MSGDCB+19+7,9
DSORGTXT EQU   MSGDCB+19+7+9,9
KEYLENTX EQU   MSGDCB+19+7+9+9,8
RKPTXT   EQU   MSGDCB+19+7+9+9+8,5
VOLSERTX EQU   MSGDCB+19+7+9+9+8+5,10
         PRINT GEN
         TITLE 'P D S  --  PDS HEXC -- HEXADECIMAL CONVERSION 02/06/84'
*
*        ROUTINE TO HEXADECIMAL INPUT TO BINARY
*
         SPACE 1
*R5          START OF STRING
*R1          LENGTH OF STRING
*MSGTEXT1    PROMPTING MESSAGE FOR ANY ERROR
*MSGTEXT2+0  LENGTH OF RESULT
*MSGTEXT2+1  MAXIMUM LENGTH OF RESULT
*MSGTEXT2+4  RESULTANT STRING
*R0          RESULT IN BINARY (UP TO FOUR BYTES)
*R2          RETURN REGISTER
         SPACE 1
HEXC     LA    R3,1(,R1)            STRING LENGTH +1
         N     R3,=F'-2'            NEXT EVEN NUMBER
         SR    R15,R15              ZERO
         LA    R4,MSGTEXT2+4        WHERE OUTPUT GOES
         LA    R0,2                 PAIRWISE COUNTER
         BCTR  R5,0                 PREPARE FOR THE FIRST ADD
         CR    R1,R3                EVEN LENGTH ORIGINALLY?
         SRL   R3,1
         STC   R3,MSGTEXT2          (STRING LENGTH +1)/2
         BNE   HEXC20               NO, BRANCH
         SPACE 1
HEXC10   LA    R5,1(,R5)            NEXT CHARACTER
         IC    R15,0(,R5)           GET A BYTE TO CONVERT
         CLI   0(R5),C'0'
         BL    *+12                 0-9?
         CLI   0(R5),C'9'
         BNH   HEXC20               YES, BRANCH
         SPACE 1
         LA    R15,9(,R15)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'           BELOW AN A?
         BL    HEXC50               YES, INVALID
         CLI   0(R5),C'F'           ABOVE AN F?
         BH    HEXC50               YES, INVALID
         SPACE 1
HEXC20   SLL   R15,28               SHIFT
         SLDL  R14,4                GET A GOOD DIGIT
         BCT   R0,HEXC10            DO THE NEXT DIGIT OF A PAIR
         LA    R0,2                 PAIRWISE COUNTER
         STC   R14,0(,R4)           SAVE AN OUTPUT CHARACTER
         LA    R4,1(,R4)            ANOTHER OUTPUT CHARACTER
         BCT   R3,HEXC10            DO ALL DIGITS
         SPACE 2
         CLC   MSGTEXT2(1),MSGTEXT2+1  TOO MANY DIGITS?
         BH    HEXC40                  YES, INVALID
         ICM   R0,B'1111',MSGTEXT2+4   RESULT REGISTER
         CLI   MSGTEXT2,3              LENGTH:3
         BH    4(R2)                     ABOVE, DONE
         SRL   R0,8
         BE    4(R2)                     EQUAL, RETURN
         SRL   R0,8
         CLI   MSGTEXT2,1              LENGTH:1
         BH    4(R2)                     ABOVE, DONE
         SRL   R0,8
         B     4(R2)                     EQUAL, RETURN
         SPACE 3
HEXC40   MESSAGE MSGHEXLN           STRING IS TOO LONG
         B     HEXC60
         SPACE 1
HEXC50   MESSAGE MSGHEXBD           INVALID HEXADECIMAL DIGITS
         SPACE 1
HEXC60  $PUTGET MSGTEXT1,ATTN=0(R2) SECOND PART OF MESSAGE
         L     R5,ADDRCBUF          ENTERED DATA
         LH    R1,0(,R5)            LENGTH OF STRING
         LA    R5,4(,R5)            START OF DATA IN THE STRING
         SH    R1,=H'5'             VALID MACHINE LENGTH?
         LA    R3,0(R5,R1)          LAST BYTE OF INPUT STRING
         BZ    HEXC90               MAYBE, CHECK FURTHER
         BNP   HEXC60               NO, NULL STRING -- PROMPT AGAIN
         SPACE 1
HEXC70   CLI   0(R3),X'40'          SCAN
         BNE   HEXC80                   BACKWARDS
         BCTR  R3,0                              FOR A
         BCT   R1,HEXC70                               NON-BLANK
         B     HEXC90               POSSIBLE STRING -- CHECK
         SPACE 1
HEXC80   CLI   0(R5),X'40'          SCAN
         BNE   HEXC90                   FORWARDS
         LA    R5,1(,R5)                        FOR A
         BCT   R1,HEXC80                              NON-BLANK
         SPACE 1
HEXC90   CLI   0(R5),X'40'          REAL STRING?
         BE    HEXC60               NO, PROMPT AGAIN
         MVI   MSGTEXT2+4,X'40'     BLANKS FOR
         MVC   MSGTEXT2+5(40),MSGTEXT2+4      UPPER-CASING INPUTS
         OC    0(*-*,R5),MSGTEXT2+4 <<EXECUTED>>
         EX    R1,*-6               UPPER-CASE THE INPUT DATA
         LA    R1,1(,R1)            ACTUAL STRING LENGTH
         B     HEXC                 PARSE THIS NEW STRING
         SPACE 3
         PRINT NOGEN
MSGHEXLN MSG   'INVALID HEXADECIMAL STRING LENGTH'
MSGHEXBD MSG   'INVALID HEXADECIMAL CHARACTERS'
         PRINT GEN
         TITLE 'P D S  --  PDS YES/NO PROMPTING ROUTINE       02/06/84'
*
*        ROUTINE TO PROMPT FOR A YES OR NO ANSWER
*
         SPACE 1
YESNO    TM    FLAGSGG,FPROMCON          PROMPTING TURNED OFF?
         BO    4(R2)                     YES, SIMULATE A YES RESPONSE
YESNO1   TM    FLAGSEE,FBKGRND+FNOTTERM  INPUT FROM THE TERMINAL?
         BNZ   4(R2)                     NO, SIMULATE A YES RESPONSE
         TM    FLAGSBB,FONESHOT          ONE SUBCOMMAND ONLY?
         BO    4(R2)                     YES, ALL DONE
         ST    R1,YESNOSV                PROMPTING MESSAGE ADDRESS
         SPACE 1
YESNO2   L     R1,YESNOSV              PROMPTING MESSAGE ADDRESS
        $PUTGET (R1),ATTN=NEWCMD       PROMPT FOR DESIRED ACTION
         TM    FLAGSEE,FNOTTERM        INPUT FROM THE TERMINAL?
         BZ    YESNO4                  YES, BRANCH
         OI    FLAGSBB,FCMD            NO, PDS SUBCOMMAND AVAILABLE
         B     4(R2)                   SIMULATE A YES RESPONSE
         SPACE 1
YESNO4   TM    FLAGSBB,FNULL           NULL REPLY?
         BO    YESNO2                  YES, TRY AGAIN
         L     R1,ADDRCBUF
         LH    R14,0(,R1)              TOTAL NUMBER OF BYTES IN MESSAGE
         SH    R14,=H'4'               LESS LENGTH OF COUNT FIELDS
         SPACE 1
YESNO6   CLI   4(R1),X'40'             BLANK?
         BNE   YESNO8                  NO, BRANCH
         LA    R1,1(,R1)               TRY THE NEXT BYTE
         BCT   R14,YESNO6              ANY NON-BLANK?
         B     YESNO2                  NO, TRY AGAIN
         SPACE 1
YESNO8   OI    4(R1),X'40'             UPPER-CASE THE RESULT
         CLI   4(R1),C'N'              CHECK REPLY
         BER   R2                      "N", RETURN AT +0
         SPACE 1
         CLI   4(R1),C'Y'              VALID REPLY?
         BNE   YESNO2                  NO, ASK AGAIN
         B     4(,R2)                  "Y", RETURN AT +4
         TITLE 'P D S  --  PDS DATE CONVERSION ROUTINE        02/06/84'
*
*        DATE CONVERSION ROUTINE -- CHANGES FORMAT YYDDDF TO MMDDYY
*
         SPACE
CONVDATE ST    R14,R14SAVE
         MVC   DAYTABLE,DAYMONTH
         XC    DOUBLE,DOUBLE
         SR    R14,R14             SETUP FOR LEAP YEAR TEST
         IC    R14,0(,R1)
         SLL   R14,4
         ST    R14,DOUBLE+4
         OI    DOUBLE+7,X'0F'
         LR    R14,R1
         CVB   R0,DOUBLE           GET YEAR IN BINARY
         SR    R1,R1
         SRDL  R0,2                SHIFT OFF REMAINDER OF YEAR/4
         LTR   R1,R1               THIS A LEAP YEAR?
         BNZ   *+8                 NO, BRANCH
         MVI   DAYTABLE+1,29       YES, FEB HAS 29 DAYS
         SPACE
         MVC   2(1,R15),0(R14)     YEAR RECORD CREATED
         MVC   DOUBLE+6(2),1(R14)
         CLI   DOUBLE+7,0          BAD DATE?
         BE    DAYSKIP             YES, DON'T 0C7
         CVB   R1,DOUBLE           GET RELATIVE DAYS IN BINARY
         SPACE
         LA    R14,DAYTABLE
         SR    R0,R0
DAYLOOP  IC    R0,0(R14)           DAYS THIS MONTH
         SR    R1,R0               WITHIN THIS MONTH?
         BNP   DAYNOW
         LA    R14,1(R14)
         B     DAYLOOP
         SPACE 1
DAYNOW   AR    R1,R0               GET DAY OF THE MONTH
         LA    R0,10
         MR    R0,R0
         CVD   R1,DOUBLE
         MVC   1(1,R15),DOUBLE+6
         LA    R1,DAYTABLE-1
         SR    R14,R1
         LA    R1,10
         MR    R0,R14
         CVD   R1,DOUBLE
         MVC   0(1,R15),DOUBLE+6
         SPACE
DAYSKIP  L     R14,R14SAVE
         BR    R14
         TITLE 'P D S  --  PDS ATTACH EXTERNAL SUBCOMMANDS    02/06/84'
*
*        ATTACH EXTERNAL PDS SUBCOMMANDS
*
         SPACE 3
ATTACH   LA    R0,NEWSTAX                 ABEND RECOVERY (EXCEPT EDIT)
         ST    R0,RECOVER
         SPACE 1
ATTACH2  OI    ##ADRCM#,EXTERNAL          AN EXTERNAL SUBCOMMAND
         MVI   SUBSECB,0                  CLEAR THE ATTENTION ECB
         SPACE 2
         ATTACH EPLOC=(3),ECB=SUBSECB,SZERO=NO,SHSPV=78,              XX
               TERM=YES,ESTAI=(STAEEXIT,(7)),SF=(E,ATTACHS2)
         SPACE 2
         ST    R1,SUBSTCB                 SUBTASK TCB ADDRESS
         WAIT  ECB=SUBSECB                WAIT FOR COMPLETION
         SPACE 2
         NI    ##ADRCM#,FF-EXTERNAL       BACK IN PDS NOW
         LA    R1,M99RBPTR                MARK ITS DATA SETS FOR REUSE
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBRI           REMOVE IN-USE VERB
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,M99TUNIT
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO TEXT UNITS
         OI    S99TUPTR,S99TUPLN          MARK END OF LIST
         MVI   S99TUKEY+1,1               KEY=1
         MVI   S99TUNUM+1,1               NUMBER=1
         MVI   S99TULNG+1,4               LENGTH OF TEXT=4
         MVC   S99TUPAR(4),SUBSTCB        SUBTASK TCB ADDRESS
         SPACE 1
         DYNALLOC
         SPACE 2
         ICM   R1,B'1111',SUBSTCB         NEED TO BE DETACHED?
         BZR   R2                         NO, DONE
         MVC   FULLWORD(4),SUBSTCB        PREPARE FOR THE XC
         XC    SUBSTCB(4),SUBSTCB         DO NOT DETACH AGAIN
         DETACH FULLWORD                  REMOVE THIS SUBTASK
         BR    R2                         DONE
         DROP  R1,R3,R4,R5
         TITLE 'P D S  --  PDS ATTENTION EXIT ROUTINE         02/06/84'
***
***      ATTENTION EXIT ROUTINE
***
         SPACE 1
ATTNEXIT ENTER ATTNEXIT
         NI    FLAGSAA,FF-FMEM#MEM     TERMINATE ANY MEMBER GROUP
         TM    ATTNECB,2+1             PARSE OR "ENTER OPTIONS"?
         BO    ATTNPOST                YES, "ENTER OPTIONS" -- BRANCH
         SPACE 2
         TM    ##ADRPC#,@I             SPF EDIT OR BROWSE SUBCOMMAND?
         BNO   ATTNPTGT                NO, BRANCH
         STAX  ,                       CANCEL THE ATTENTION EXIT
         B     ATTNDONE
         SPACE 3
ATTNPTGT TM    ATTNECB,X'40'           PREVIOUS ATTENTION CLEARED?
         BNZ   ATTNLOOP                NO, BRANCH
         L     R2,R14PTGT              $PUTGET IS NOT RECURSIVE
        $PUTGET MSGENTDS               PROMPT WITH "ENTER OPTIONS"
         ST    R2,R14PTGT              $PUTGET IS NOT RECURSIVE
         TM    FLAGSBB,FNULL           NULL LINE ENTERED?
         BO    ATTNDONE                YES, JUST LEAVE
         OI    FLAGSBB,FCMD            NO, INDICATE COMMAND AVAILABLE
         SPACE 6
ATTNPOST LA    R1,SUBSECB              ASSUME THIS IS EXTERNAL TO PDS
         TM    ##ADRCM#,EXTERNAL       CORRECT?
         BO    *+8                     YES, BRANCH
         LA    R1,ATTNECB              NO, MAIN ATTENTION ECB
         POST  (1)                     NOTIFY OF ATTENTION ENTERED
         SPACE 2
ATTNDONE SR    R15,R15                 ATTENTION PROCESSED
         EXIT
         SPACE 2
ATTNLOOP MVI   ATTNECB,0               CLEAR THE ATTENTION ECB FOR NOW
         MESSAGE MSGLOOP               POSSIBLE LOOP MESSAGE
         OI    ##ADRPA#,@N             NO ABEND MSG, QUIET MODE
*                                      *** S0C1 ABEND TO            ***
         DC    H'0'                    ***            RESTART PDS   ***
         TITLE 'P D S  --  PDS ESTAE EXIT ROUTINE             02/06/84'
*
*        ESTAE EXIT
*
         SPACE 1
         USING *,R15                    TEMPORARY BASE
STAEEXIT CH    R0,STAECD                WORK AREA PROVIDED?
         BNE   STAE10                   YES, BRANCH
         SR    R15,R15                  NO RETRY TO BE ATTEMPTED
         BR    R14                      RETURN
         DROP  R15
STAECD   DC    H'20'
         SPACE 2
STAE10   STM   R14,R12,12(R13)          SAVE REGISTERS
         LA    R15,STAESAVE             STAE SAVE AREA
         ST    R13,4(,R15)
         ST    R15,8(,R13)
         LR    R13,R15
         LR    R5,R1                    SDWA POINTER
         USING SDWA,R5
         L     R7,SDWAPARM              WORK AREA ADDRESS
         LM    R9,R12,BASES
         MVC   MSGTEXT1,MSGABE          ABEND MESSAGE
         L     R0,SDWAABCC              ABEND CODE
         SRDL  R0,12                    SHIFT USER ABEND TO R1
         ST    R0,FULLWORD              SYSTEM ABEND CODE
         UNPK  MSGTEXT1+MSGABESY(5),FULLWORD+2(3)
         TR    MSGTEXT1+MSGABESY(4),TRTABLE
         MVI   MSGTEXT1+MSGABESY,C'S'
         MVI   MSGTEXT1+MSGABESY+4,X'40'
         SRL   R1,20                    USER ABEND
         CVD   R1,DOUBLE
         UNPK  MSGTEXT1+MSGABEUS(5),DOUBLE+5(3)
         MVI   MSGTEXT1+MSGABEUS+4,X'40'
         MVC   MSGTEXT1+MSGABEPG(8),SDWANAME  PROGRAM NAME
         TR    MSGTEXT1+MSGABEPG(8),TRLINE    MAKE DISPLAYABLE
         SR    R0,R0
         SR    R1,R1
         ICM   R0,B'0111',SDWANXTP            NEXT INSTRUCTION
         ICM   R1,B'0111',SDWAEPA+1           ENTRY POINT ADDRESS
         SR    R0,R1                          OFFSET TO ERROR
         CLI   MSGTEXT1+MSGABEPG,C'.'         UNPRINTABLE NAME?
         BNE   STAE20                         NO, BRANCH
         MVC   MSGTEXT1+MSGABEAT-1(27),MSGTEXT1+MSGABEAT-2
         SR    R0,R1                          YES, USE ERROR LOCATION
         SPACE 1
STAE20   ST    R0,FULLWORD
         UNPK  MSGTEXT1+MSGABEAT(7),FULLWORD+1(4)
         TR    MSGTEXT1+MSGABEAT(6),TRTABLE
         MVI   MSGTEXT1+MSGABEAT+6,X'40'
         CLC   MSGTEXT1+MSGABESY+2(2),=C'37'  S*37 ABEND?
         BNE   *+8                            NO, BRANCH
         NI    FLAGSFF,FF-FOUTNO37            CLOSE OUTPUT DATA SET NOW
         CLC   MSGTEXT1+MSGABESY+2(2),=C'3E'  S*3E ABEND?
         BE    STAE60                         YES, IGNORE THIS ABEND
         TM    ##ADRPA#,@N                    QUIET MODE?
         BO    STAE30                         YES, BRANCH
         MESSAGE MSGTEXT1                     NO, OUTPUT A MESSAGE
         NI    FLAGSAA,FF-FMEM#MEM            TURN OFF ANY MEMBER GROUP
         SPACE 2
STAE30   LR    R1,R5                    SDWA START
         STM   R0,R15,SDWASRSV          RESET THE RETURN REGISTERS
         LR    R0,R7                    MAIN SAVE AREA ADDRESS
         TM    FLAGSFF,FSPFCALL         SPF INITIALIZED?
         BNO   *+8                      NO, BRANCH
         LA    R0,SPFMAIN               SAVEAREA WHEN PDS IS A DIALOGUE
         ST    R0,SDWASR13              RESET REGISTER 13
         SPACE 1
         ICM   R2,B'1111',RECOVER       ANY ABEND RECOVERY ADDRESS?
         BZ    STAE60                   NO, BRANCH
         TM    FLAGSGG,FRECVCON         STAE RECOVERY DISALLOWED?
         BO    STAE60                   YES, BRANCH
         SPACE 1
         TM    ##ADRCM#,EXTERNAL        EXTERNAL SUBCOMMAND?
         BO    STAE40                   YES, BRANCH
         NI    ##ADRCM#,FF-CONTINUE     TURN OFF ANY CONTINUATION
         LR    R1,R5                    NO, PDS ABENDED !!!
         L     R13,4(,R13)
         SETRP REGS=(14,12),RC=4,RETADDR=(2),RETREGS=YES,FRESDWA=YES
         SPACE 2
STAE40   POST  SUBSECB                  NOTIFY OF ATTENTION ENTERED
         SPACE 1
STAE60   NI    FLAGSAA,FF-FMEM#MEM      TURN OFF ANY MEMBER GROUP
         L     R13,4(,R13)
         LR    R1,R5                    SDWA POINTER
         SETRP REGS=(14,12),RC=0        NO RECOVERY
         TITLE 'P D S  --  PDS MEMBER IN USE CHECKING         02/06/84'
*
*        MEMBER IN USE CHECKS
*
         SPACE 2
ENQMTEST MVC   PARMLIST(4),ENQTEST     INITIALIZE ENQUEUE PARAMTERS
         ENQ   (SPFEDIT,DSNAME,E,52,SYSTEMS),RET=TEST,MF=(E,PARMLIST)
         LTR   R15,R15                 MEMBER IN USE?
         BZ    4(,R2)                  NO, SKIP ONE INSTRUCTION
         SPACE 1
         MVC   MSGTEXT1,MSGMUSER       MEMBER IN USE MESSAGE
         MVC   MSGTEXT1+4(8),DSNMEMQ   MEMBER IN USE
         TR    MSGTEXT1+4(8),TRLINE    MAKE PRINTABLE
         TM    ##ADRPA#,@N             QUIET MODE CHECK?
         BOR   R2                      YES, RETURN
         LA    R1,MSGTEXT1             ERROR MESSAGE
         MESSAGE (R1)                  YES, MESSAGE
         BR    R2                                  AND ERROR RETURN
         TITLE 'P D S  --  PDS STANDARD MESSAGES              02/06/84'
*
*        SUBCOMMAND MESSAGE SUBROUTINES
*
         SPACE 3
NOMEMBER MVC   MSGTEXT1,MSGNONE       MSG - (MEMBER) NOT FOUND
         B     MEMFORM
         SPACE 3
BADMEMB  MVC   MSGTEXT1,MSGNAME       MSG - (MEMBER) IS AN INVALID NAME
         B     MEMFORM
         SPACE 3
MEMEXIST MVC   MSGTEXT1,MSGEXIST      MSG - (MEMBER) EXISTS
         SPACE 1
MEMFORM  MVC   MSGTEXT1+4(8),DIRNAME
         TR    MSGTEXT1+4(8),TRLINE
         LA    R1,MSGTEXT1
         B     MSGNEWXX
         SPACE 3
IOERROR  LA    R1,MSGIODIR            MSG - I/O ERROR IN DIRECTORY
         B     MSGNEWXX
         SPACE 3
FULLDIR  LA    R1,MSGNODIR            MSG - DIRECTORY IS FULL
         SPACE 2
MSGNEWXX NI    FLAGSAA,FF-FMEM#MEM    TURN OFF ANY MEMBER GROUP
         SPACE 2
MSGNEW   MESSAGE (R1)                 OUTPUT THE MESSAGE POINTED TO
         B     NEWCMD                   AND GET A NEW SUBCOMMAND
         TITLE 'P D S  --  PDS MESSAGE/PUTLINE PROCESSING     02/06/84'
*
*        MESSAGE PROCESSING (MULTILEVEL MESSAGES HAVE BEEN DISABLED)
*
         SPACE 1
MESSTXT1 LA    R1,MSGTEXT1              OUTPUT MSGTEXT1
         SPACE 1
MESSAGE0 ST    R14,R14PUTM              SAVE LINKAGE REGISTER
        $PUTLINE (R1),ATTN=NEWCMD       DEFAULT INTERRUPT ADDRESS
         L     R14,R14PUTM              RESTORE LINKAGE REGISTER
         BR    R14                      RETURN
         SPACE 3
*
*        PUTLINE PROCESSING
*
         SPACE 1
$PUTLINE TM    ATTNECB,X'40'          ATTN OCCUR?
         BOR   R14                    YES, IMMEDIATE RETURN
         ST    R14,R14PUTL
         SPACE
         LR    R0,R1                  SAVE DATA LINE POINTER
         LA    R1,PARMLIST
         USING IOPL,R1
         L     R14,ADDRUPT
         L     R15,ADDRECT
         STM   R14,R15,IOPLUPT       INITIALIZE IOPLUPT AND IOPLECT
         SPACE
         LA    R15,ATTNECB
         ST    R15,IOPLECB           INITIALIZE IOPLECB
         DROP  R1
         SPACE
         L     R15,ADDRPUTL
         SPACE 2
         PUTLINE PARM=PUTLPARM,ENTRY=(15),MF=(E,(1)),                  X
               OUTPUT=((0),TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK)
         SPACE 2
         TM    FLAGSFF,FOUTSAVE+FOUTNO37  RECORD MODE AND VALID?
         BNO   PUTLINE9                   NO, BRANCH
         ST    R15,PUTLPARM+12            SAVE RETURN CODE
         L     R1,PUTLPARM+4              OUTPUT MESSAGE
         MVI   MSGLAST,C'*'               FILL WITH
         MVC   MSGLAST+1(79),MSGLAST               ASTERISKS
         LH    R14,0(,R1)                 LENGTH OF MESSAGE
         SH    R14,=H'5'                  MACHINE LENGTH VALID?
         BM    PUTLINE9                   NO, BRANCH
         CH    R14,=H'78'
         BNH   *+8                        LINE LENGTH=MIN(79,ACTUAL)
         LH    R14,=H'78'
         MVC   MSGLAST+1(*-*),4(R1)       <<EXECUTED>>
         EX    R14,*-6                    MOVE IN THE MESSAGE
         PUT   QAMDCB,MSGLAST             PUT THE MESSAGE TO DISK
         L     R15,PUTLPARM+12            RESTORE RETURN CODE
         SPACE 2
PUTLINE9 L     R14,R14PUTL           RETURN ADDRESS (+0 OR +4)
         TM    ATTNECB,X'40'         ATTN OCCUR?
         BOR   R14                   YES, RETURN AT +0
         SPACE 2
         CH    R15,=H'4'             PUTLINE ERROR SIGNALLED?
         BNH   4(R14)                NO, RETURN AT +4
         SPACE 2
         LA    R1,PUTLINER           ERROR MESSAGE
         LA    R0,L'PUTLINER         ERROR MESSAGE LENGTH
         TPUT  (1),(0),R             PUTLINE FAILED
         SPACE 2
         L     R1,PUTLPARM+4         START OF OUTPUT MESSAGE
         LA    R1,4(,R1)             LESS LENGTH CODE
         LA    R0,79                 FIRST 79 BYTES
         TPUT  (1),(0),R             INVALID OUTPUT TEXT
         SPACE 2
         NOP   NEWCMD                LOCATION FOR TEST COMMAND
         DC    H'0'                  FORCE AN ABEND CLEANUP
         SPACE 2
PUTLINER DC    C'*** PUTLINE FAILED, RC > 4; DATA WAS:'
         TITLE 'P D S  --  PDS PUTGET PROCESSING              02/06/84'
*
*        PUTGET PROCESSING
*
         SPACE 1
$PUTGET  TM    ATTNECB,X'40'         ATTN OCCUR?
         BOR   R14                   YES, IMMEDIATE RETURN
         ST    R14,R14PTGT
         NI    FLAGSBB,FF-FNULL
         SPACE
         LA    R0,1                  ONLY ONE SEGMENT
         STM   R0,R1,PUTOLD1
         SPACE 2
PUTGET0  LA    R1,PARMLIST
         L     R14,ADDRUPT
         L     R15,ADDRECT
         LA    R0,ATTNECB
         STM   R14,R0,IOPLUPT-IOPL(R1)  INITIALIZE IOPLUPT, IOPLECT,
*                                                  AND IOPLECB
         L     R15,ADDRPTGT
         SPACE
         PUTGET PARM=PTGTPARM,MF=(E,(1)),ENTRY=(15),                   X
               OUTPUT=(PUTOLD1,SINGLE,MODE),                           X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),                     X
               TERMGET=(EDIT,WAIT)
         SPACE
         STC   R15,PARMLIST          SAVE RETURN CODE
         NI    FLAGSEE,FF-FNOTTERM   ASSUME INPUT WAS FROM THE TERMINAL
         CLI   PARMLIST,4            CORRECT?
         BNE   *+8                   YES, BRANCH
         OI    FLAGSEE,FNOTTERM      NO, NOT FROM THE TERMINAL
         SPACE
         CLI   PARMLIST,12           PUTGET STATUS:
         BH    EXIT8N                  ERROR -- BRANCH
         BL    PUTGET1                 SUCCESSFUL, STACK OR ATTENTION
         L     R1,ADDRECT              STACK, 2ND-LEVEL MSG., NO PAUSE
         OI    ECTMSGF-ECT(R1),X'80' IGNORE SECOND LEVEL MESSAGES AND
         B     PUTGET0               GET THE DATA FROM THE STACK
         EJECT
PUTGET1  SR    R15,R15               ASSUME RETURN AT +0
         TM    ATTNECB,X'40'         ATTN OCCUR?
         BO    PUTGET8               YES, BRANCH
         SPACE
         LA    R1,PTGTPARM PUTGET    PARM AREA START
         L     R1,PGPBIBUF-PGPB(R1)  INPUT BUFFER ADDRESS
         SPACE 2
***
*** PFK SUPPORT WILL BE PROVIDED IN THE NEAR FUTURE --
***
***  EARLY TESTS WITH PUTGET ASIS INPUT UNCOVERED THREE PROBLEMS WITH
***  PUTGET SUPPORT MODULES IKJEFT45 AND IKJEFT55 (APAR OZ65820):
***   A.  ? PROCESSING DOES NOT WORK (IKJEFT45)
***   B.  PAUSE PROCESSING DOES NOT WORK (IKJEFT45)
***   C.  A PF KEY OR NULL LINE ENTERED ON A 3270 IN THE FIRST COLUMN
***       OF LINES 3, 7, 11, 15, ... IS NOT PROVIDED TO THE PUTGET
***       REQUESTOR.  SINCE ITS LAST SCREEN ADDRESS CHARACTER IS AN
***       X'60' OR MINUS, THIS LINE IS CONCATENATED WITH THE NEXT
***       LINE ENTERED BY THE USER.
***
         SPACE 2
         LH    R14,0(,R1)            SUBCOMMAND LENGTH
         CH    R14,=H'256'           MORE THAN PDS'S LIMIT?
         BNH   *+8                   NO, BRANCH
         LH    R14,=H'256'           YES, USE THE CURRENT LIMIT
         BCTR  R14,0                 MACHINE LENGTH OF THIS LINE
         MVC   PUTGETBF(*-*),0(R1)   <<EXECUTED>>
         EX    R14,*-6               SAVE THE ENTERED LINE
         SPACE 2
         LH    R0,0(R1)              BUFFER LENGTH
         ICM   R0,B'1000',=X'01'     SUBPOOL IS 1
         SPACE
         FREEMAIN R,LV=(0),A=(1)     RELEASE THE INPUT BUFFER
         SPACE 2
         LA    R1,PUTGETBF           ADDRESS OF INPUT LINE
         ST    R1,ADDRCBUF           SAVE THE LINE ADDRESS
         TM    FLAGSFF,FOUTSAVE+FOUTNO37  RECORD MODE AND VALID?
         BNO   PUTGET7                    NO, BRANCH
         L     R1,PUTOLD1+4               PROMPT MESSAGE
         MVI   MSGLAST,C'-'               FILL WITH
         MVC   MSGLAST+1(79),MSGLAST               MINUSES
         LH    R14,0(,R1)                 LENGTH OF MESSAGE
         SH    R14,=H'5'                  MACHINE LENGTH VALID?
         BM    PUTGET6                    NO, BRANCH
         CH    R14,=H'78'
         BNH   *+8                        LINE LENGTH=MIN(79,ACTUAL)
         LH    R14,=H'78'
         MVC   MSGLAST+1(*-*),4(R1)       <<EXECUTED>>
         EX    R14,*-6                    MOVE IN THE PROMPT
         PUT   QAMDCB,MSGLAST             PUT THE MESSAGE TO DISK
         SPACE 2
PUTGET6  L     R1,ADDRCBUF                USER RESPONSE
         MVI   MSGLAST,C'+'               FILL WITH
         MVC   MSGLAST+1(79),MSGLAST               PLUSES
         LH    R14,0(,R1)                 LENGTH OF MESSAGE
         SH    R14,=H'5'                  MACHINE LENGTH VALID?
         BM    PUTGET7                    NO, BRANCH
         CH    R14,=H'78'
         BNH   *+8                        LINE LENGTH=MIN(79,ACTUAL)
         LH    R14,=H'78'
         MVC   MSGLAST+1(*-*),4(R1)       <<EXECUTED>>
         EX    R14,*-6                    MOVE IN THE PROMPT
         PUT   QAMDCB,MSGLAST             PUT THE MESSAGE TO DISK
         SPACE 2
PUTGET7  LA    R15,4                 ASSUME RETURN AT +4
         CH    R15,PUTGETBF          A NULL LINE?
         BL    PUTGET8               NO, BRANCH
         OI    FLAGSBB,FNULL         YES, INDICATE A NULL LINE
         SPACE 2
PUTGET8  L     R14,R14PTGT
         B     0(R14,R15)
         TITLE 'P D S  --  PDS DATA SET ALLOCATION            02/06/84'
*
*        DATA SET ALLOCATION SUBROUTINE
*
         SPACE 2
ALLOCATE CSECT
         USING *,R8
         LR    R8,R15
         ST    R14,R14SAVE             SAVE LINKAGE REGISTER
         NI    FLAGSCC,FF-FRECOVER     CLEAR RECOVERY PROMPT FLAG
         SPACE 2
RETRY    NI    FLAGSCC,FF-FPERMDS      CLEAR PERMANENT STATUS
         SPACE 2
         LA    R6,DAIR08               POINT TO THE DAIR BLOCK
         XC    DAIR08,DAIR08
         USING DAPB08,R6
         MVI   DA08CD+1,X'08'          DATA SET ALLOCATION CODE
         SPACE
         LA    R0,DSNLEN               ADDR OF DSNAME FIELD
         ST    R0,DA08PDSN
         SPACE
         MVC   DA08PSWD,PASSWORD       PASSWORD
         MVC   DA08DDN,BLANKS          NO DDNAME
         MVC   DA08SER,VOLUME          VOLUME SPECIFIED (OR BLANKS)
         MVC   DA08MNM,BLANKS          NO MEMBER NAME
         MVC   DA08UNIT,BLANKS         NO UNIT REQUESTED
         CLI   VOLUME,X'40'            VOLUME SERIAL SPECIFIED?
         BE    *+10                    NO, BRANCH (CATALOG LOOKUP)
         MVC   DA08UNIT,=C'SYSALLDA'   YES, DEFAULT UNIT IS SYSALLDA
         SPACE
*                                      DISP=(SHR,KEEP,KEEP)
         MVC   DA08DSP1,DSPALLOC         OR DISP=(OLD,KEEP,KEEP)
         MVI   DA08DPS2,DA08KEEP
         MVI   DA08DPS3,DA08KEP
         OI    DA08CTL,DA08PERM         MUST BE SPECIFICALLY UNALLOC.
         SPACE 2
         LA    R1,PARMLIST              DYNALLOC PARAMETER LIST
         USING DAPL,R1
         SPACE
         L     R2,ADDRUPT
         L     R3,ADDRECT
         LA    R4,ATTNECB
         L     R5,ADDRPSCB
***      LA    R6,DAIR08
         STM   R2,R6,DAPLUPT            SAVE DAPLUPT, DAPLECT, DAPLECB,
*                                            DAPLPSCB AND DAPLDAPB
         SPACE 2
         MVI   ATTNECB,0
         L     R15,ADDRDAIR
         BALR  R14,R15
         EJECT
         LTR   R15,R15                  SUCCESSFUL?
         BNZ   DAIRFAIL                 NO, ERROR
         MVC   DDNAME,DA08DDN           SAVE DDNAME
         SPACE
         CLC   DDNAME(3),=C'SYS00000'   TEMPORARY TYPE DDNAME?
         BNE   NOTTEMP                  NO, BRANCH
         NC    DA08DDN(8),=C'SYS00000'  TURN OFF NUMERIC DIGITS
         CLC   DA08DDN(8),=C'SYS00000'  TEMPORARY TYPE DDNAME?
         BE    TEMPDSN                  YES, BRANCH
NOTTEMP  OI    FLAGSCC,FPERMDS          MARK AS "PERMANENT"
         SPACE
TEMPDSN  MVC   DSORG(1),DA08DSO         SAVE DSORG
         TM    DA08DSO,DS1DSGPO         PARTITIONED?
         BO    GOTALLOC                 YES, DONE
         DROP  R1,R6
         SPACE 2
         LA    R1,X'100'                TTR=1
         ST    R1,DIRTTR                SAVE FOR LATTER
         TM    ##ADRCM#,EDITOR          SPFEDIT, BROWSE, EDIT OR FSE?
         BO    GOTALLOC                 YES, BRANCH
         MESSAGE MSGNLIB                OUTPUT A WARNING MESSAGE
         SPACE 2
GOTALLOC L     R14,R14SAVE              RETURN AT
         B     4(R14)                            +4 (SUCCESSFUL)
         SPACE 2
DAIRFAIL CLC   ENDLIT(3),DSNAMEH        WAS "END" ENTERED BY THE USER?
         BNE   DAIRR15                  NO, BRANCH
         TM    DSNAMEH+3,X'FF'-X'40'    WITH A FOLLOWING BLANK OR NULL?
         BZ    NOTALLOC                 YES, TERMINATE THE PROGRAM
         SPACE 1
DAIRR15  ST    R15,DAIRRC               SAVE DAIR RETURN CODE
         LA    R14,PARMLIST             POINTER TO DAIR PARAMETER LIST
         LA    R15,DAIRRC               POINTER TO RETURN CODE
         LA    R0,ADDRFF02              POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFDAIR)          POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL              POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP           INITIALIZE DFDAPLP, DFDRCP,
*                                         DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                  PROBLEM WITH IKJEFF18?
         BZ    FAILBAD2                 NO, BRANCH
         EJECT
FAILBAD  CVD   R15,DOUBLE
         MVC   MSGTEXT1,MSGDAIRX        DAIRFAIL ROUTINE FAILED
         ED    MSGTEXT1+MSGDAIR1-MSGDAIRX(4),DOUBLE+6
         MVI   MSGTEXT1+MSGDAIR1-MSGDAIRX,C'='
         MESSAGE MSGTEXT1
         SPACE 1
FAILBAD2 CLI   DSPREQST,DA08OLD         DISP=OLD REQUESTED?
         BE    DSREALLO                 YES, ATTEMPT RETRY
         CLI   DSPALLOC,DA08OLD         OLD ALLOCATION ATTEMPT?
         BNE   DSREALLO                 NO, REALLOCATE
         SPACE 1
         MVI   DSPALLOC,DA08SHR         YES, RETRY WITH DISP=SHR
         NI    FLAGSAA,FF-FMEM#MEM      TURN OFF MEMBER GROUPS
         NI    ##ADRCM#,FF-CONTINUE     TURN OFF THE CONTINUED FLAG
         B     RETRY
         SPACE 2
DSREALLO OI    FLAGSCC,FRECOVER         DATA SET RECOVERY ROUTINE
        $PUTGET MSGRETRY,ATTN=NOTALLOC
         SPACE 2
         TM    FLAGSBB,FNULL            USER WANT ANOTHER DATA SET?
         BO    DSREALLO                 NULL REPLY, ASK AGAIN
         SPACE
         MVC   FULLWORD(4),##ADRCMD     SAVE CONTINUATION INFORMATION
         MVC   ##SUBCOM(20),MAINPCL     PCL FOR SUBCOMMAND NAME
         L     R15,=A(PARSE)            PARSE ADDRESS
         BALR  R14,R15                  INVOKE PARSE
         B     NOTALLOC                 EXIT - PARSE FAILURE
         MVC   ##ADRCMD(4),FULLWORD     CONTINUATION INFORMATION
         B     RETRY                    RETRY ALLOCATION
         SPACE 2
NOTALLOC L     R14,R14SAVE              ERROR EXIT - NOT ALLOCATED
         BR    R14
PDS61    CSECT
         TITLE 'P D S  --  PDS DATA SET UNALLOCATION          02/06/84'
*
*        FREE DATA SET NO LONGER REQUIRED
*
         SPACE 2
DEALLDCB DS    0H                 ** CONDITIONAL FREE OF FILE
         TM    FLAGSCC,FPERMDS    DATA SET PERMANENTLY ALLOCATED?
         BOR   R2                 YES, SKIP EXPLICIT FREE
         SPACE 1
DEALLOCZ LA    R6,DAIR18          ** ALWAYS FREE (EXCEPT SYSHELP AND
         XC    DAIR18,DAIR18                             SYSPROC)
         USING DAPB18,R6
         MVI   DA18CD+1,X'18'
         MVC   DA18DDN,DDNAME          DEALLOCATE THE DATA SET
         CLC   DDNAME(8),SYSHELP       SYSHELP DDNAME?
         BE    *+8                     YES, BRANCH
         OI    DA18CTL,DA18PERM        DEALLOCATE "PERM" ALLOCATED TOO
         CLC   DDNAME(8),SYSPROC       SYSPROC DDNAME?
         BER   R2                      YES, BRANCH
         SPACE
         LA    R1,PARMLIST             DYNALLOC PARAMETER LIST
         USING DAPL,R1
         SPACE
         L     R3,ADDRUPT
         ST    R3,DAPLUPT              SAVE DAPLUPT
         SPACE
         L     R3,ADDRECT
         LA    R4,ATTNECB
         L     R5,ADDRPSCB
***      LA    R6,DAIR18
         STM   R3,R6,DAPLECT           SAVE DAPLECT, DAPLECB,
*                                           DAPLPSCB AND DAPLDAPB
         SPACE
         MVI   ATTNECB,0
         DROP  R1,R6
         SPACE 2
         L     R15,ADDRDAIR
         BALR  R14,R15                 CALL DAIR TO FREE THE FILE
         SPACE 2
         BR    R2
         TITLE 'P D S  --  PDS OPEN/CLOSE STOW ROUTINES       02/06/84'
*
*        OPEN/CLOSE STOW SUBROUTINES
*
         SPACE 3
OPENSTOW NI    FLAGSCC,FF-FBLDLOK                BLDL IS INVALID NOW
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BO    4(R2)                             YES, EXIT
         SPACE 1
         BAL   R6,RESERVE                        RESERVE THE DEVICE
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT NOW
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BO    4(R2)                             YES, BRANCH
         SPACE 1
         MESSAGE MSGNOPEN                        STOW DID NOT OPEN
         BR    R2                                ERROR RETURN
         SPACE 3
SHUTSTOW TM    FLAGSEE,FBKGRND                   BACKGROUND MODE?
         BOR   R2                                YES, RESERVE IS OK
         SPACE 2
SHUTSTW2 TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         BNO   DEQ                               NO, BRANCH
         SPACE 1
         CLI   DSPALLOC,DA08OLD                  EXCLUSIVE USE?
         BE    DEQ                               YES, BRANCH
         SPACE 1
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)         CLOSE IT NOW
         SPACE 2
         B     DEQ                               RELEASE ANY ENQUEUES
         TITLE 'P D S  --  PDS RESERVE ROUTINE                02/06/84'
*
*        UCB RESERVE ROUTINE
*
         SPACE 1
RESERVE  CLI   DSPALLOC,DA08OLD            EXCLUSIVE USE?
         BER   R6                          YES, RETURN
         SPACE 1
         CLI   RESERVET,C'R'               ALREADY RESERVED?
         BER   R6                          YES, RETURN
         MVI   RESERVET,C'R'               MARK AS RESERVED
         SPACE 2
         MVC   PARMLIST(4),RESERVEL        LIST FORM OF RESERVE
         RESERVE (SPFDSN,DSNAME,E,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,   XX
               MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNOR  R6                          NO, RETURN
         SPACE 2
         L     R1,UCBADDR                  UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   RESERVE2                    NO, BRANCH
         MVC   PARMLIST(4),RESERVEL        LIST FORM OF RESERVE
         RESERVE (SYSIEWLP,DSNAME,E,44,SYSTEMS),RET=HAVE,UCB=UCBADDR, XX
               MF=(E,PARMLIST)
         BR    R6
         SPACE 5
RESERVE2 MVC   PARMLIST(4),ENQHAVE         LIST FORM OF RESERVE
         ENQ   (SYSIEWLP,DSNAME,E,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
         BR    R6
         TITLE 'P D S  --  PDS DEQ ROUTINE                    02/06/84'
*
*        UCB DEQ ROUTINE
*
         SPACE 1
DEQ      CLI   DSPALLOC,DA08OLD            EXCLUSIVE USE?
         BER   R2                          YES, RETURN
         SPACE 1
         CLI   RESERVET,C'R'               RESERVED?
         BNER  R2                          NO, RETURN
         MVI   RESERVET,0                  NO LONGER RESERVED
         SPACE 2
         MVC   PARMLIST(4),DEQSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (SPFDSN,DSNAME,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,        X
               MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNOR  R2                          NO, RETURN
         SPACE 2
         L     R1,UCBADDR                  UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   DEQ2                        NO, BRANCH
         MVC   PARMLIST(4),DEQSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (SYSIEWLP,DSNAME,44,SYSTEMS),RET=HAVE,UCB=UCBADDR,      X
               MF=(E,PARMLIST)
         BR    R2
         SPACE 5
DEQ2     MVC   PARMLIST(4),DEQSYS          LIST FORM, DEQ "SYSTEM"
         DEQ   (SYSIEWLP,DSNAME,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
         BR    R2
         TITLE 'P D S  --  PDS OPEN EXIT                      02/06/84'
         USING IHADCB,R1
OPENEXIT TM    ##ADRCM#,FIXOPEN                FIXPDS DCB MODIFICATION?
         BNO   OPENEXT2                        NO, BRANCH
         MVC   DCBRECFM(1),#RECFM+3            NEW RECORD FORMAT
         MVC   DCBLRECL(2),#LRECL+2            NEW LRECL
         MVC   DCBBLKSI(2),#BLKSI+2            NEW BLKSIZE
         MVI   DCBKEYLE,0                      RESET THE KEY LENGTH
         SPACE 1
OPENEXT2 MVC   RECFM(1),DCBRECFM               SAVE RECORD FORMAT BYTE
         NI    FLAGSCC,FF-RECFMU-RECFMV-RECFMF CLEAR TEST BITS
         XI    FLAGSCC,RECFMU                  ASSUME RECFM=U
         TM    DCBRECFM,DCBRECU                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMU+RECFMV           ASSUME RECFM=V
         TM    DCBRECFM,DCBRECV                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMV+RECFMF           ASSUME RECFM=F
         NI    DCBRECFM,FF-DCBRECSB            DROP ANY S OF RECFM=FBS
         TM    DCBRECFM,DCBRECF                CORRECT?
         BO    OPENEXT4                        YES, BRANCH
         XI    FLAGSCC,RECFMV                  NO, INDICATE UNKNOWN
         SPACE
OPENEXT4 ICM   R0,B'1111',BUFFADDR             BUFFER PRESENT?
         BNZR  R14                             YES, DONE
         SPACE 1
         LH    R2,DCBBLKSI                     GET SIZE OF BUFFER
         MVC   LRECL,DCBLRECL                  SAVE LRECL
         STH   R2,BLKSI                        SAVE BLOCKSIZE
         LR    R3,R14                          SAVE R14
         CH    R2,=H'256'                      BUFFSIZE>256?
         BH    *+8                             YES, BRANCH
         LH    R2,=H'256'                      NO, USE 256 AS A MINIMUM
         TM    DCBRECFM,DCBRECTO               RECFM=.T?
         BNO   *+8                             NO, BRANCH
         LH    R2,=H'32760'                    YES, USE THE MAXIMUM
         STH   R2,BUFFSIZE                     UPDATE BUFFER SIZE
         AR    R2,R2                           DOUBLE BUFFERING
         SPACE 1
         GETMAIN EC,LV=(R2),SP=0,A=BUFFADDR,BNDRY=PAGE,MF=(E,PARMLIST)
         SPACE 1
         LR    R14,R3                          RESTORE R14
         LTR   R15,R15                         SUCCESSFUL?
         BZR   R14                             YES, EXIT
         XC    BUFFADDR(6),BUFFADDR            BUFFADDR AND BUFFSIZE
         BR    R14
         TITLE 'P D S  --  PDS DIRECTORY READ SUBROUTINE      02/06/84'
*
*        DIRECTORY READ SUBROUTINE
*
         SPACE 1
READDIR  ST    R14,R14READD
         NI    FLAGSEE,FF-FDIREND      END OF DIRECTORY TO COME YET
         CLI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)?
         BNE   DEBLOCK                 NO, BRANCH
         SPACE
         XC    TOTBLOCK(LTOT),TOTBLOCK CLEAR THE TOTALS AREA
         LA    R1,MSGNOTDR             ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO        CORRECT?
         BE    READDIRE                YES, ERROR EXIT
         SPACE 2
READDIR1 BAL   R14,EXCP
         B     *+4(R15)                PROCESS RETURN CODE
         B     READDIR2                 00 - GOOD READ
         B     READDIR4                 04 - END OF MEMBER
         B     READDIR4                 08 - END OF DATA SET
         B     IOERROR                  12 - I/O ERROR IN DIRECTORY
         SPACE 2
READDIR2 LA    R1,MSGNO256             ASSUME INVALID DIRECTORY BLOCK
         CLC   LS+2(2),=H'256'         CORRECT?
         BNE   READDIRE                YES, ERROR EXIT
         LH    R14,TOTTRKS
         LA    R14,1(,R14)
         CLC   TOTCCHH,CURMBB+3        SWITCH TRACKS?
         BE    *+8                     NO, BRANCH
         STH   R14,TOTTRKS             YES, UPDATE TRACK COUNT
         MVC   TOTCCHH,CURMBB+3        NEW CCHH QUANTITY
         SPACE
         LH    R14,TOTBLOCK
         LA    R14,1(R14)
         STH   R14,TOTBLOCK
         TM    FLAGSEE,FDIREND         USAGE AND LAST MEMBER FOUND?
         BO    READDIR1                YES, BRANCH
         SPACE
         LH    R14,TOTUSED
         LA    R14,1(R14)
         STH   R14,TOTUSED
         MVI   TOTTHISM+1,0
         SPACE
         LR    R15,R0                   BUFFER ADDRESS
         LA    R0,2
         LH    R1,0(,R15)
         BCTR  R1,0
         LA    R1,0(R1,R15)
         STM   R15,R1,DIRPTRS
         SPACE 8
DEBLOCK  LM    R15,R1,DIRPTRS
         BXH   R15,R0,READDIR1         INDEX TO NEXT MEMBER NAME
         STM   R15,R1,DIRPTRS
         SPACE
         CLI   0(R15),X'FF'            LAST MEMBER?
         BE    READDIR3                YES
         SPACE
         MVC   MEMNAME(12),0(R15)      SAVE FIRST 12 BYTES OF ENTRY
         SPACE 1
         LH    R0,TOTTHISM
         A     R0,=F'1'
         STH   R0,TOTTHISM
         CH    R0,TOTMAXM
         BNH   *+8
         STH   R0,TOTMAXM              MAX DIRECTORY ENTRIES IN A BLOCK
         SPACE
         IC    R14,11(,R15)
         N     R14,=XL4'0000001F'
         LA    R0,12(R14,R14)
         STM   R15,R1,DIRPTRS
         SPACE 2
         TM    MEMFLAG,X'80'           ALIAS?
         LA    R14,TOTMEMR             ASSUME NOT
         BZ    *+8                     CORRECT?
         LA    R14,TOTMEMA             NO, ALIAS
         SPACE
         LH    R15,0(,R14)
         LA    R15,1(,R15)
         STH   R15,0(,R14)
         SPACE
         CLI   ##SUBCOM,C'U'           USAGE SUBCOMMAND?
         BE    DEBLOCK                 YES, CONTINUE TO DEBLOCK
         LA    R15,4                   NO, GET THE EXIT OFFSET
         B     READDIR5
         SPACE 2
READDIR3 OI    FLAGSEE,FDIREND         FLAG LAST MEMBER FOUND
         CLI   ##SUBCOM,C'U'           USAGE SUBCOMMAND?
         BE    READDIR1                YES, CONTINUE READING
         B     READDIR4                NO, EXIT WITH LAST MEMBER
         SPACE 2
READDIRE MESSAGE (R1)                  DIRECTORY ERROR
         MVI   STARTTR+2,X'00'         RESET THE START TTR
         SPACE 2
READDIR4 SR    R15,R15                 END OF DIRECTORY OR LAST MEMBER
         SPACE 1
READDIR5 L     R14,R14READD
         B     0(R14,R15)
         TITLE 'P D S  --  PDS EXCP READ SUBROUTINE           02/06/84'
*
*        EXCP SUBROUTINE
*
         SPACE 1
EXCP     ST    R14,R14EXCP
         LH    R1,IODONE                    ANOTHER
         LA    R1,1(,R1)                           INPUT
         STH   R1,IODONE                                ENTRY
         ICM   R0,B'1111',STARTTR           NEW TTR ADDRESS?
         BNZ   EXCPNTTR                     YES, BRANCH
         TM    FLAGSII,FIOPEND              ANY BUFFER PENDING?
         BO    EXCPIN                       YES, BRANCH
         TM    FLAGSII,FDIRGRP+FDIRNEXT     DIR. GROUP ALREADY INPUT?
         BO    EXCPDONE                     YES, BRANCH
         TM    FLAGSII,FDIRGRP              DIR. GROUP TO BE INPUT?
         BO    EXCPIN1                      YES, BRANCH
         B     EXCPCONT                     NO, BRANCH
         SPACE 1
EXCPDONE MVC   IOECB(1),IODIR2+28           RESTORE IOECB STATUS
         MVC   IOBCSW+4(1),IODIR2+28+1      RESTORE IOBCSW+4 STATUS
         NI    FLAGSII,FF-FDIRNEXT          USE THE GROUP RECORD NOW
         B     EXCPIN2
         SPACE 2
EXCPIN   WAIT  ECB=IOECB
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   EXCPIN2                      NO, BRANCH
         TM    FLAGSII,FIOPDIR              DIRECTORY GROUP PENDING?
         BO    EXCPIN2                      YES, BRANCH
         LH    R1,IODODIS2                  ANOTHER
         LA    R1,1(,R1)                           DISCARDED
         STH   R1,IODODIS2                                  RECORD
         NI    FLAGSII,FF-FIOPDIR-FIOPEND   TURN OFF FLAGS
         SPACE 1
EXCPIN1  MVC   IOBSEEK(LIOBDIR),IODIR1+4+12 RESTORE INPUT STATUS
         MVC   IOBCSW+4(1),IOECB+1          RESTORE IOBCSW+4 STATUS
         MVI   IOECB+1,0                    RESET I/O STATUS BYTE +1
         B     EXCPCONT
         SPACE 2
*  SETUP BUFFER CONTROLS
EXCPIN2  LH    R1,IODOUSE                   ANOTHER
         LA    R1,1(,R1)                           USEFUL
         STH   R1,IODOUSE                                INPUT RECORD
         LA    R14,IOBUFF1                  BUFFER 1 CONTROLS
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+8                          NO, BRANCH
         LA    R14,IODIR1                   YES, DIRECTORY 1 CONTROLS
         NI    FLAGSII,FF-FIOPDIR-FIOPEND   TURN OFF FLAGS
         L     R15,0(,R14)                  EXCHANGE
         L     R1,36(,R14)                         BUFFER 1
         ST    R15,36(,R14)                             AND BUFFER 2
         ST    R1,0(,R14)                                     ADDRESSES
         ST    R1,EXCPBUFF                  SET INPUT BUFFER ADDRESS
         SPACE 1
         MVC   CURMBB(8),36+4+12(R14)       SET FOR LAST CCHHR
         MVC   LS+2(2),36+4+12+9(R14)       SET FOR LAST DATA LENGTH
         MVC   IOBSEEK(12),36+4(R14)        SET CURRENT READ DATA
         MVC   CUREXT(4),36+32(R14)         SET CURRENT EXTENT
         B     EXCP42
         SPACE 3
EXCPNTTR TM    FLAGSII,FIOPEND              ANY PENDING I/O?
         BNO   EXCPNEW                      NO, BRANCH
         WAIT  ECB=IOECB
         TM    FLAGSII,FIOPDIR              FOR DIRECTORY GROUP?
         BO    EXCPDIR                      YES, BRANCH
         LH    R1,IODODIS2                  ANOTHER
         LA    R1,1(,R1)                           DISCARDED
         STH   R1,IODODIS2                                  RECORD
         MVI   IOECB,0                      RESET I/O STATUS
         B     EXCPNEW
         SPACE 1
EXCPDIR  OI    FLAGSII,FDIRNEXT             NEXT DIRECTORY RECORD IS IN
         MVC   IODIR2+28(1),IOECB           SAVE THE I/O STATUS
         MVC   IODIR2+28+1(1),IOBCSW+4      SAVE IOBCSW+4 STATUS
         MVI   IOECB,0                      RESET I/O STATUS
         SPACE 1
EXCPNEW  NI    FLAGSII,FF-FIOPEND-FIOPDIR   RESET THE PENDING FLAGS
         LH    R1,IODONEW                   ANOTHER
         LA    R1,1(,R1)                           TTR
         STH   R1,IODONEW                             CHANGE
         L     R0,STARTTR
         SRL   R0,8
         BCTR  R0,0                         REDUCE INITIAL TTR BY ONE
         SLL   R0,8
         SPACE 1
         STM   R15,R12,12+4(R13)            CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)   DEB ADDRESS
         LA    R2,IOBSEEK                   RETURN CCHHR
         L     R15,ADDRCNVT                 TTR TO MBBCCHHR CONVERTER
         LR    R3,R13                       SAVE ADDRESS OF SAVE AREA
         BALR  R14,R15
         LR    R13,R3                       RESTORE SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)            RESTORE REGISTERS
         SPACE 1
         XC    STARTTR,STARTTR              ZERO THE TTR
         CLC   IOBSEEK(8),LSTARMBB          LESS THAN DS1LSTAR?
         BL    EXCP1                        YES, BRANCH
         TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCP1                        YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGIS               ISAM DATA SET?
         BO    EXCP1                        YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGPO               PARTITIONED DATA SET?
         BNO   EXCPEND8                     NO, NO WARNING MESSAGE
         MESSAGE MSGTTRHI                   OUTPUT A WARNING MESSAGE
         B     EXCPEND8                     TERMINATE I/O FOR THIS
         SPACE 1
EXCP1    L     R1,INDCB+(DCBDEBAD-IHADCB)   DEB ADDRESS
         LA    R1,16(,R1)                   CURRENT EXTENT POINTER - 16
         SR    R15,R15
EXCP2    LA    R1,16(,R1)                   NEXT EXTENT POINTER
         CLM   R15,B'0001',IOBSEEK          THIS EXTENT?
         LA    R15,1(,R15)
         BNE   EXCP2                        NO, BRANCH
         B     EXCPINIT                     YES, DO THE FIRST READ
         SPACE 2
*    INITIALIZE CCHHR, SECTOR NUMBER AND READ IN THE LENGTH OF RECORD 1
EXCPXTNT SR    R1,R1                        AFTER THE FIRST EXTENT
         IC    R1,IOBSEEK
         A     R1,=F'1'
         STC   R1,IOBSEEK                   INCREMENT EXTENT NUMBER
         CH    R1,NUMEXT                    WITHIN THE EXTENTS?
         BNL   EXCPEND8                     NO, NO FINAL EOF RECORDED
         L     R1,CUREXT
         LA    R1,16(,R1)                   CURRENT EXTENT ENTRY
         MVC   IOBSEEK+3(4),6(R1)           INITIAL CCHH
         MVI   IOBSEEK+7,0                  INITIAL R=0
         SPACE 3
EXCPINIT ST    R1,CUREXT                    UPDATE FOR LATER
         MVI   SNO,0                        SET SECTOR NUMBER=ZERO
         SPACE 1
EXCPREST LA    1,CCW1A
         ST    1,IOBCCW                     CHANNEL PROGRAM ADDRESS
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         MVC   IOBCCW,EXCPCCWI              CHANNEL PROGRAM ADDRESS
         CLI   IOECB,X'7F'                  GOOD READ?
         BNE   EXCPERRA                     NO, INITIAL READ FAILED
         SPACE 2
EXCPCONT TM    ATTNECB,X'40'                ATTENTION SIGNALLED?
         BO    NEWCMD                       YES, BRANCH
         SPACE 1
         CLI   IOECB,X'42'                  EXTENT VIOLATION LAST TIME?
         BE    EXCPXTNT                     YES, DO THE NEXT EXTENT
         CLI   IOECB,X'7F'                  PREVIOUS READ OK?
         BNE   EXCPREST                     NO, RETRY WITH OTHER EXCP
         MVC   CURMBB(8),IOBSEEK            DISK ADDRESS OF THIS RECORD
         MVC   LS+2(2),DATALN               BLOCK LENGTH FOR THIS READ
         LH    R0,BUFFSIZE
         C     R0,LS                        WILL THIS RECORD FIT?
         BNL   EXCPEXCP                     YES, BRANCH
         ICM   R1,B'1111',BUFFADDR          ANY BUFFER?
         BNP   EXCPGETM                     NO, BRANCH
         AR    R0,R0                        DOUBLE BUFFERING
         FREEMAIN R,LV=(0),A=(1)            YES, FREE IT AND REALLOCATE
         SPACE 2
EXCPGETM L     R0,LS                        MAY NEED EXTRA ISAM BUFFER
         TM    FLAGSCC,RECFMF               RECFM=F..?
         BNO   *+8                          NO, BRANCH
         AH    R0,LRECL                     YES, PROTECT AGAINST S0C4
         CH    R0,=H'32760'                 ABOVE MAXIMUM BUFFER SIZE?
         BNH   *+8                          NO, BRANCH
         LH    R0,=H'32760'                 YES, USE THE MAXIMUM
         STH   R0,BUFFSIZE                  GETMAIN SIZE
         AR    R0,R0                        DOUBLE BUFFERING
         GETMAIN R,LV=(0)                   GET THE STORAGE
         ST    R1,BUFFADDR                  SAVE START ADDRESS
         ST    R1,IOBUFF1                   FIRST BUFFER ADDRESS
         AH    R1,BUFFSIZE
         ST    R1,IOBUFF2                   SECOND BUFFER ADDRESS
         SPACE 3
EXCPEXCP L     R1,IOBUFF1                   STANDARD BUFFER ADDRESS
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+8                          NO, BRANCH
         L     R1,IODIR1                    DIRECTORY BUFFER ADDRESS
         STCM  R1,B'0111',CCW3+1            UPDATE READ BUFFER ADDRESS
         MVC   CCW3+6(2),DATALN             UPDATE READ LENGTH
         OI    CCW3+7,X'08'                 MINIMUM OF EIGHT BYTES
         ST    R1,EXCPBUFF                  EXCP BUFFER ADDRESS
         LA    R0,IOBSEEK+3                 STANDARD IOBSEEK
         STCM  R0,B'0111',CCW1+1
         STCM  R0,B'0111',CCW4+1
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         SPACE 1
EXCP42   TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   EXCP42C                      NO, BRANCH
         MVC   IODIR1+4+12(LIOBDIR),IOBSEEK SAVE IOBSEEK INFORMATION
         MVC   IODIR1+4+28+1(1),IOBCSW+4    SAVE IOBCSW+4 TOO
EXCP42C  CLI   IOECB,X'42'                  EXTENT PROBLEM (NEXT TIME)?
         BE    EXCPEND0                     YES, QUIT
         TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCP7F                       YES, IGNORE DS1LSTAR
         TM    DSORG,DS1DSGIS               ISAM DATA SET?
         BO    EXCP7F                       YES, IGNORE DS1LSTAR
         CLC   CURMBB(8),LSTARMBB           CURRENT:DS1LSTAR
         BH    EXCPEND8                       HIGH, END OF DATA SET
         BL    EXCP7F                         LOW, GOOD READ
         TM    DSORG,DS1DSGPO                 EQUAL, PARTITIONED?
         BO    EXCPEND8                              YES, END OF DATA
         SPACE 1
EXCP7F   CLI   IOECB,X'7F'                  GOOD READ?
         BNE   EXCP41                       NO, BRANCH
         SPACE 2
*  CHECK FOR POSSIBILITY OF DOUBLE BUFFERING
         TM    RECFM,DCBRECTO               TRACK OVERFLOW DATA SET?
         BO    EXCPOV                       YES, BRANCH
         TM    FLAGSGG,FDOUBCON             CONTROL NODOUBLE?
         BO    EXCPEND0                     YES, BRANCH
         TM    ##ADRCM#,@S                  SINGLE BUFFERING REQUIRED?
         BO    EXCPEND0                     YES, BRANCH
         TM    FLAGSII,FSINGLE              THEN OR ELSE(DELETE)?
         BO    EXCPEND0                     YES, BRANCH
         CLC   DATALN(2),BUFFSIZE           EXPANSION NEEDED?
         BH    EXCPEND0                     YES, BRANCH
         SPACE 1
         LH    R1,IODOUBLE                  ANOTHER
         LA    R1,1(,R1)                           DOUBLE
         STH   R1,IODOUBLE                               BUFFER USED
         OI    FLAGSII,FIOPEND              I/O OPERATION PENDING
         LA    R14,IOBUFF2                  ALTERNATE BUFFER CONTROL
         TM    FLAGSII,FDIRGRP              DIRECTORY GROUP INPUT?
         BNO   *+12                         NO, BRANCH
         LA    R14,IODIR2                   ALTERNATE DIRECTORY CONTROL
         OI    FLAGSII,FIOPDIR              DIRECTORY GROUP PENDING
         SPACE 1
         MVC   4(12,R14),IOBSEEK            INITIALIZE FOR THIS READ
         MVC   4+12(LIOBDIR,R14),IOBSEEK    I/O SUPPORT INFORMATION
         MVC   CCW3+1(3),1(R14)             UPDATE READ BUFFER ADDRESS
         MVC   CCW3+6(2),DATALN             UPDATE READ LENGTH
         OI    CCW3+7,X'08'                 MINIMUM OF EIGHT BYTES
         LA    R15,4+3(,R14)                ALTERNATE IOBSEEK
         STCM  R15,B'0111',CCW1+1
         STCM  R15,B'0111',CCW4+1
         MVI   IOECB,0                      CLEAR I/O FLAGS
         EXCP  IOB                          INITIATE SECOND INPUT
         B     EXCPEND0
         SPACE 2
EXCPOV   CLI   IOBSEEK+7,2                  NEXT R OF CCHHR = 2?
         BNE   EXCPEND0                     NO, BRANCH
         CLI   CURMBB+7,1                   THIS R OF CCHHR = 1?
         BE    EXCPEND0                     YES, NO ADJUSTMENT
*  TRACK OVERFLOW RECORDS READ OK; HOWEVER, THE LENGTH OF AN OVERFLOW
*    RECORD IS INCORRECT (IT EQUALS THE FIRST PART OF THE RECORD).
         SPACE 1
         MVC   LS(2),DATALN                 SAVE NEXT DATA LENGTH
         MVI   IOBSEEK+7,0                  WANT TO GET R=1 LENGTH
         LA    1,CCW1A
         ST    1,IOBCCW                     CHANNEL PROGRAM ADDRESS
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         MVC   IOBCCW,EXCPCCWI              CHANNEL PROGRAM ADDRESS
         CLI   IOECB,X'7F'                  GOOD READ?
         BNE   EXCPERRA                     NO, INITIAL READ FAILED
         SR    R1,R1
         ICM   R1,B'0011',DATALN            DATA LENGTH OF R=1
         AH    R1,LS+2                      ADDED TO PREVIOUS PART
         MVC   DATALN(2),LS                 RESTORE NEXT LENGTH
         ST    R1,LS                        FIX RECORD LENGTH
         MVI   IOBSEEK+7,2                  NEXT R OF CCHHR IS 2
         B     EXCPEND0
         SPACE 2
EXCP41   CLI   IOECB,X'41'                  PERMANENT ERROR?
         BNE   EXCPERR                      NO, SOME OTHER DISK ERROR
         SPACE 1
         TM    IOBCSW+4,X'01'               ACTUALLY EOF?
         BNO   EXCPERR                      NO, ERROR
         SPACE 1
         TM    DSORG,DS1DSGPS+DS1DSGDA      DSORG=PS OR DA?
         BM    EXCPEND8                     YES, END OF DATA SET
         TM    DSORG+1,DS1ACBM              VSAM DATA SET?
         BO    EXCPEND8                     YES, END OF DATA SET
         SPACE 1
         ICM   R0,B'0011',DATALN            NEXT RECORD LENGTH=ZERO?
         BNZ   EXCPEND4                     NO, BRANCH
         LA    1,CCW1A
         ST    1,IOBCCW                     CHANNEL PROGRAM ADDRESS
         MVI   IOECB,0                      ZERO THE ECB
         EXCP  IOB
         WAIT  ECB=IOECB
         MVC   IOBCCW,EXCPCCWI              CHANNEL PROGRAM ADDRESS
         CLI   IOECB,X'7F'                  GOOD READ?
         BE    EXCPEND4                     YES, BRANCH
         CLI   IOECB,X'42'                  EXTENT VIOLATION NEXT TIME?
         BE    EXCPEND4                     YES, BRANCH
         TM    IOECB,X'41'                  PERMANENT I/O ERROR?
         BNE   EXCPERRA                     NO, SOME OTHER ERROR
         TM    IOBCSW+4,X'01'               ANOTHER EOF?
         BO    EXCPEND4                     YES, BRANCH
         SPACE 2
EXCPERRA SR    R0,R0                        FIRST READ OF EXTENT ERROR
         IC    R0,IOBSEEK+7                 R OF MBBCCHHR
         A     R0,=F'1'                     NEXT R
         STC   R0,IOBSEEK+7
         TM    ##ADRPA#,@N                  QUIET MODE I/O?
         BO    EXCPERR                      YES, BRANCH
         MESSAGE MSGBLANK
         MESSAGE MSGPERM#                   INITIALIZATION READ FAILED
         SPACE 1
EXCPERR  STM   R2,R12,28(R13)               PERMANENT I/O ERROR
         LA    R2,IOBSEEK
         L     R1,INDCB+(DCBDEBAD-IHADCB)   CONVERT MBBCCHHR TO TTR
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         MVC   MSGTEXT1(48),MSGPERMI        PERMANENT I/O ERROR
         STCM  R0,B'1110',MSGTEXT1+50       TTR OF THIS ERROR
         UNPK  MSGTEXT1+MSGPERMT(7),MSGTEXT1+50(4)
         TR    MSGTEXT1+MSGPERMT(6),TRTABLE
         LA    R15,12                   *** PERMANENT I/O ERROR
         TM    ##ADRPA#,@N                  QUIET MODE I/O?
         BO    EXCPENDZ                     YES, BRANCH
         MESSAGE MSGBLANK
         MESSAGE MSGTEXT1
         LA    R15,12                   *** PERMANENT I/O ERROR
         B     EXCPENDZ
         SPACE 1
EXCPEND8 LA    R15,8                    *** END OF DATA SET
         B     EXCPENDZ
EXCPEND4 LA    R15,4                    *** END OF MEMBER OR ISAM AREA
         B     EXCPENDZ
EXCPEND0 SR    R15,R15                  *** SUCCESSFUL READ
         L     R0,EXCPBUFF              EXCP BUFFER ADDRESS
         SPACE 1
EXCPENDZ L     R14,R14EXCP
         BR    R14
         TITLE 'P D S  --  PDS CLOSE DCB ROUTINE              02/06/84'
*
*        CLOSE DCB'S AND RELEASE BUFFER SPACE
*
CLOSEIT  TM    FLAGSII,FIOPEND         ANY I/O PENDING?
         BNO   CLOSEIT2                NO, BRANCH
         LH    R1,IODODIS3             ONE MORE
         LA    R1,1(,R1)                       DISCARDED
         STH   R1,IODODIS3                              INPUT RECORD
         WAIT  ECB=IOECB               WAIT FOR INPUT COMPLETION
         MVI   IOECB,0                 CLEAR THE I/O FLAG
         SPACE 1
CLOSEIT2 LH    R1,IODODIS3
         LA    R1,1(,R1)
         TM    FLAGSII,FDIRNEXT        DIRECTORY READ READY?
         BNO   *+8                     NO, BRANCH
         STH   R1,IODODIS3             ONE MORE DISCARDED INPUT RECORD
         MVI   FLAGSII,0               CLEAR THE I/O FLAGS
         SPACE 1
         ICM   R1,B'1111',BUFFADDR
         BZ    FREENONE
         XC    BUFFADDR,BUFFADDR
         LH    R0,BUFFSIZE
         AR    R0,R0                    DOUBLE BUFFERING
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
FREENONE MVI   OPENLIST,0
         MVI   OPENLIST+4,X'80'
         CLOSE (INDCB,,STOWDCB),MF=(E,OPENLIST)
         SPACE 1
         BR    R2
         TITLE 'P D S  --  PDS PARSE PROCESSING ROUTINE       02/06/84'
*
*        PARSE INTERFACE SUBROUTINE
*
         SPACE 1
PARSE    CSECT
         USING *,R8
         ST    R8,R8PARSE
         ST    R14,R14PARSE
         LR    R8,R15
         CLC   ##ADRPCL+1(3),MAINPCL+13 PARSE FOR NEW DATA SET?
         BNE   PARSE2                   NO, BRANCH
         STAX  ,                        YES, CANCEL ANY PREVIOUS STAX
PARSE2   BAL   R2,FREEPDL               FREE ANY PREVIOUS PARSE STORAGE
         SPACE 1
         LA    R15,PARMLIST             AREA FOR PARSE PARAMETERS
         USING PPL,R15                  BASE FOR PARSE PARAMETER LIST
         SPACE 1
         L     R1,ADDRUPT
         L     R2,ADDRECT
         LA    R3,ATTNECB
         L     R4,##ADRPCL
         LA    R5,ADDRANSR
         L     R6,ADDRCBUF
***      LA    R7,MAINSAVE
         SPACE 1
         TM    ##ADRPC#,@S+@G       NON-PARTITIONED AND MEMBER NEEDED?
         BNO   PARSE4               NO, BRANCH
         SPACE 1
*    THE FOLLOWING 6 LINES OF CODE ADD A MEMBER PARAMETER WHEN
*      REQUIRED FOR NON-PARTITIONED DATA SET SUBCOMMAND PARSE.
         LH    R14,2(,R6)           OFFSET FOR OPERANDS
         A     R14,=F'-2'           LESS TWO
         STH   R14,2(,R6)           UPDATE THE OFFSET
         LA    R14,0(R6,R14)        POINT TO THE NEW OPERAND
         MVI   4(R14),C'Z'          DUMMY MEMBER NAME
         MVI   5(R14),X'40'         SEPARATOR CHARACTER
         SPACE 1
PARSE4   STM   R1,R7,PPLUPT         INITIALIZE PPLUPT, PPLECT, PPLECB
*                                   PPLPCL, PPLANS, PPLCBUF AND PPLUWA
         DROP  R15
         LR    R1,R15
         MVI   ATTNECB,1            FLAG -- CAN BE ATTENTIONED
         SPACE 2
         L     R15,ADDRPARS         INVOKE
         BALR  R14,R15                    PARSE
         STC   R15,R14PARSE
         MVI   ATTNECB,0            CLEAR ATTENTION ECB
         L     R6,ADDRANSR          PARSE RESULT BASE REGISTER
         EJECT
         LTR   R15,R15                 GOOD PARSE?
         BNZ   PARSECHK                NO, BRANCH
         SPACE 1
         ICM   R15,B'0111',##ADRPAR+1  ANY PROCESSING ROUTINE?
         BNZR  R15                     YES, BRANCH
         B     PARSECHK                NO, NO ADDITIONAL PROCESSING
         SPACE 2
         USING PDLEDIT,R6
ZEDIT    MVC   #EDITOLD(1),PCLEDOP+1   SAVE SPECIFIED OLD/NEW OPTION
         CLI   PCLEDOP+1,X'02'         "OLD"?
         BNE   ZEDIT2                  NO, BRANCH
         OI    ##ADRPC#,@B             YES, MUST EXIST
ZEDIT2   MVC   #EDITNON(1),PCLNON+1    NONUM OPTION
         MVC   #EDITASI(1),PCLASIS+1   ASIS OPTION
         MVC   #EDITTYP+2(2),PCLTYPE   EDIT TYPE FIELD
         B     PARSECHK
         SPACE 2
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF300
         USING PDLSPFED,R6
ZSPFEDIT MVC   #SPFOLD(1),SPFNEWOP+1    SAVE ANY OLD/NEW OPTION
         CLI   SPFNEWOP+1,X'02'         "OLD"?
         BNE   PARSECHK                 NO, BRANCH
         OI    ##ADRPC#,@B              YES, MUST EXIST
         B     PARSECHK
.NSPF300 ANOP
         SPACE 2
         USING PDLPRINT,R6          NOTE: MULTIPLE USE PDL
ZPRINTOF DS    0H
ZREVIEW  DS    0H
ZSUBMIT  DS    0H
ZTSOLIST TM    PCLPRSTR+6,X'80'     STRING CODED?
         BNO   PARSECHK             NO, DONE
         LH    R2,PCLPRSTR+4        LENGTH OF STRING
         LTR   R2,R2                NULL STRING CODED?
         BZ    PARSECHK             YES, DONE
         CH    R2,=H'256'           LENGTH(STRING)>=256?
         BL    *+8                  NO, BRANCH
         LH    R2,=H'256'           YES, USE 256 AS A MAXIMUM LENGTH
         STH   R2,#COMMDSZ          SAVE ACTUAL LENGTH
         L     R1,PCLPRSTR          NEW STRING ADDRESS
         BCTR  R2,0                 MACHINE LENGTH
         MVC   COMMDSTR(*-*),0(R1)  <<EXECUTED>>
         EX    R2,*-6               MOVE IN ANY OTHER OPERANDS
         B     PARSECHK
         EJECT
         USING PDLIFTTR,R6
ZIF      MVC   #ALIAOPT(1),IFLKALIA+1   ALIAS/NOALIAS
         MVC   #SSIOPT(1),IFLKSSI+1     SSI/NOSSI
         MVC   #AUTHOPT(1),IFLKAUTH+1   AUTH/NOAUTH
         MVC   #PAGEOPT(1),IFLKPAGE+1   PAGE/NOPAGE
         MVC   #VSLKED(1),IFLKVSLE+1    VSLKED/NOVSLKED
         MVC   #RMODE(1),IFRMODE+1      RMODE24/RMODEANY
         MVC   #AMODE(1),IFAMODE+1      AMODE24/AMODE31/AMODEANY/NOA...
         MVC   #USERIDO(1),IFTUSERK+1   USERID/NOUSERID/NOTUSERID
         MVC   #LOADERR(1),IFLOADER+1   LOADERR/NOLOADERR
         MVC   #LKEDERR(1),IFLKEDER+1   LKEDERR/NOLKEDERR
         MVC   #IOERROR(1),IFIOERR+1    IOERR/NOIOERR
         MVC   #BLOCKER(1),IFBLKERR+1   BLOCKERR/NOBLOCKERR
         MVC   #LRECLER(1),IFLRECLE+1   LRECLERR/NOLRECLERR
         MVC   #NULL(1),IFNULL+1        NULL/NONULL
         MVC   #AALIAS(1),IFAALIAS+1    APPARENTALIAS/NOAPPARENTALIAS
         MVC   #ORPHAN(1),IFORPHAN+1    ORPHAN/NOORPHAN
         MVC   #SPFEDIT(1),IFSPFEDT+1   SPFEDIT/NOSPFEDIT
         MVC   #NAMEERR(1),IFNAMERR+1   NAMEERR/NONAMEERR
         MVC   #ATTRHI(3),=F'-1'        INITIALIZE THE SECOND TTR VALUE
         SPACE 1
ZIF380   CLI   IFLKREUS+1,X'01'       REUSABLE ATTRIBUTE STATUS:
         BL    ZIF384                   NONE, BRANCH
         BH    ZIF382                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRREUS        "REUS"
         B     ZIF384
ZIF382   OI    #ATTRNO,ATTRREUS         "NOREUS"
         SPACE 1
ZIF384   CLI   IFLKREFR+1,X'01'       REFRESHABLE ATTRIBUTE STATUS:
         BL    ZIF400                   NONE, BRANCH
         BH    ZIF386                   "NO" FORM, BRANCH
         OI    #ATTRYES+1,ATTRREFR      "REFR"
         B     ZIF400
ZIF386   OI    #ATTRNO+1,ATTRREFR       "NOREFR"
         SPACE 1
ZIF400   CLI   IFLKRENT+1,X'01'       REENTRANT ATTRIBUTE STATUS:
         BL    ZIF404                   NONE, BRANCH
         BH    ZIF402                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRRENT        "RENT"
         B     ZIF404
ZIF402   OI    #ATTRNO,ATTRRENT         "NORENT"
         SPACE 1
ZIF404   CLI   IFLKEXEC+1,X'01'       EXECUTABLE ATTRIBUTE STATUS:
         BL    ZIF420                   NONE, BRANCH
         BH    ZIF406                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTREXEC        "EXEC"
         B     ZIF420
ZIF406   OI    #ATTRNO,ATTREXEC         "NOEXEC"
         SPACE 1
ZIF420   CLI   IFLKLOAD+1,X'01'       LOADABLE ATTRIBUTE STATUS:
         BL    ZIF424                   NONE, BRANCH
         BH    ZIF422                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRLOAD        "LOAD"
         B     ZIF424
ZIF422   OI    #ATTRNO,ATTRLOAD         "NOLOAD"
         SPACE 1
ZIF424   CLI   IFLKEDIT+1,X'01'       NOT EDITABLE ATTRIBUTE STATUS:
         BL    ZIF440                   NONE, BRANCH
         BH    ZIF426                   "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNE        "EDIT"  -- BIT MUST BE REVERSED
         B     ZIF440
ZIF426   OI    #ATTRYES+1,ATTRNE        "NOEDIT"
         SPACE 1
ZIF440   CLI   IFLKLKED+1,X'01'       LINKAGE EDITOR ATTRIBUTE:
         BL    ZIF444                   NEITHER, BRANCH
         BH    ZIF442                   NULL FORM, BRANCH
         OI    #ATTRYES+1,ATTRFLEV      "FLEVEL"
         B     ZIF444
ZIF442   OI    #ATTRNO+1,ATTRFLEV       "NOFLEVEL"
         SPACE 1
ZIF444   CLI   IFLKOVER+1,X'01'       OVERLAY ATTRIBUTE STATUS:
         BL    ZIF460                   NONE, BRANCH
         BH    ZIF446                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTROVLY        "OVERLAY"
         B     ZIF460
ZIF446   OI    #ATTRNO,ATTROVLY         "NOOVERLAY"
         SPACE 1
ZIF460   CLI   IFLKTEST+1,X'01'       TEST ATTRIBUTE STATUS:
         BL    ZIF464                   NONE, BRANCH
         BH    ZIF462                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRTEST        "TEST"
         B     ZIF464
ZIF462   OI    #ATTRNO,ATTRTEST         "NOTEST"
         SPACE 1
ZIF464   CLI   IFLKSCTR+1,X'01'       SCATTER ATTRIBUTE STATUS:
         BL    ZIF480                   NONE, BRANCH
         BH    ZIF466                   "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRSCTR        "SCTR"
         B     ZIF480
ZIF466   OI    #ATTRNO,ATTRSCTR         "NOSCTR"
         SPACE 1
ZIF480   CLI   IFLKDC+1,X'01'         DOWNWARD COMPATIBLE ATTRIBUTE:
         BL    ZIF484                   NONE, BRANCH
         BH    ZIF482                   "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNODC       "DC" -- BIT IS REVERSED
         B     ZIF484
ZIF482   OI    #ATTRYES+1,ATTRNODC      "NODC"
         SPACE 1
ZIF484   TM    IFTUSER+6,X'80'        ANY USERID NAME FILTERING?
         BNO   ZIF500                 NO, BRANCH
         LH    R15,IFTUSER+4          LENGTH OF USERID NAME
         STC   R15,#USERIDL           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF500                 NO, BRANCH
         L     R1,IFTUSER             PARTIAL MODULE NAME
         MVC   #USERIDT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF500   MVC   #ZAPOPT(1),IFTLZAPK+1   SAVE ZAP/NOZAP
         MVC   #USEROPT(1),IFTLUSRK+1  SAVE USER/NOUSER
         MVC   #TRANOPT(1),IFTLTRNK+1  SAVE TRAN/NOTRAN
         MVC   #LKEDOPT(1),IFTLKEDK+1  SAVE LKED/NOLKED
         MVC   #MODOPT(1),IFTLMODK+1   SAVE MODULE/NOMODULE
         MVC   #RLDERR(1),IFRLDERR+1   SAVE RLDERR/NORLDERR
         MVC   #RLD0(1),IFRLD0+1       SAVE RLDZERO/NORLDZERO
         MVC   #APFERR(1),IFAPFERR+1   SAVE APFERR/NOAPFERR
         SPACE 1
         TM    IFTLKED+6,X'80'        ANY LINKAGE EDITOR FILTERING?
         BNO   ZIF502                 NO, BRANCH
         LH    R15,IFTLKED+4          LENGTH OF LKED NAME
         STC   R15,#LKEDLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF502                 NO, BRANCH
         L     R1,IFTLKED             PARTIAL MODULE NAME
         MVC   #LKEDTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF502   TM    IFTLMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   ZIF504                 NO, BRANCH
         LH    R15,IFTLMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF504                 NO, BRANCH
         L     R1,IFTLMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF504   TM    IFTLTRN+6,X'80'        ANY TRANSLATOR NAME FILTERING?
         BNO   ZIF506                 NO, BRANCH
         LH    R15,IFTLTRN+4          LENGTH OF NAME
         STC   R15,#TRANLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF506                 NO, BRANCH
         L     R1,IFTLTRN             PARTIAL TRANSLATOR NAME
         MVC   #TRANTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF506   TM    IFTLZAP+6,X'80'        ANY ZAP IDRDATA NAME FILTERING?
         BNO   ZIF508                 NO, BRANCH
         LH    R15,IFTLZAP+4          LENGTH OF NAME
         STC   R15,#ZAPLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF508                 NO, BRANCH
         L     R1,IFTLZAP             PARTIAL IDRDATA DATA
         MVC   #ZAPTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZIF508   TM    IFTLUSR+6,X'80'        ANY USER IDRDATA FILTERING?
         BNO   ZIF540                 NO, BRANCH
         LH    R15,IFTLUSR+4          LENGTH OF NAME
         STC   R15,#USERLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZIF540                 NO, BRANCH
         L     R1,IFTLUSR             PARTIAL IDRDATA DATA
         MVC   #USERTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 3
ZIF540   TM    IFTTTR+6,X'80'       STRING CODED?
         BNO   ZIF560               NO, DONE
         L     R5,IFTTTR            NEW STRING ADDRESS
         LH    R1,IFTTTR+4          LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZIF560               YES, DONE
         MVI   MSGTEXT2+1,3         MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,MSGTTRN1    FIRST TTR MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     FREEPDL              ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#ATTRLO   SAVE THE RESULTING TTR
         SPACE 2
         TM    IFTTTR+8+6,X'80'     STRING CODED?
         BNO   ZIF560               NO, DONE
         L     R5,IFTTTR+8          NEW STRING ADDRESS
         LH    R1,IFTTTR+8+4        LENGTH OF STRING
         LTR   R1,R1                NULL STRING CODED?
         BNP   ZIF560               YES, DONE
         MVC   MSGTEXT1,MSGTTRN2    SECOND TTR MESSAGE
         BAL   R2,HEXC              CONVERT TO BINARY
         B     FREEPDL              ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#ATTRHI   SAVE THE RESULTING TTR
         SPACE 2
ZIF560   MVI   #OPTOPT,X'80'          PARSE WAS CALLED FOR IF
         LA    R1,IFABOSUB            ABOVE(COUNT)
         BAL   R14,PACKMAX            ANY RESULT?
         B     ZIF600                 YES, BRANCH
         B     ZIF620                 NO, BRANCH
ZIF600   ST    R1,#IFABOVE            SAVE RESULT
         OI    #IFABOVE,X'80'         MARK AS ENTERED
         SPACE 1
ZIF620   LA    R1,IFBELSUB            BELOW(COUNT)
         BAL   R14,PACKMAX            ANY RESULT?
         B     ZIF640                 YES, BRANCH
         B     ZIF660                 NO, BRANCH
ZIF640   ST    R1,#IFBELOW            SAVE RESULT
         OI    #IFBELOW,X'80'         MARK AS ENTERED
         SPACE 1
ZIF660   L     R6,ADDRANSR            RESET THE PDL BASE ADDRESS
         OC    #ATTRYES(13),#ATTRYES  ANY LINKAGE EDITOR ATTRIBUTES?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'01'          YES, SET A FLAG FOR LATER
         OC    #ZAPOPT(6),#ZAPOPT     ZAP/USER/TRAN/LKED/MODULE/RLDERR?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'02'+X'01'    YES, SET A FLAG FOR LATER
         OC    #IOERROR(4),#IOERROR   IOERROR/BLOCKERR/LRECLERR/NULL?
         BZ    *+8                    NO, BRANCH
         OI    #OPTOPT,X'04'          YES, SET A FLAG FOR LATER
         SPACE 1
         LH    R1,IFTHENCC            THEN ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         THEN ACTION POINTER
         ST    R1,#ACTIONT            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONT    SET POSSIBLE FSINGLE FLAG
         SPACE 1
         LH    R1,IFELSECC            ELSE ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         ELSE ACTION POINTER
         ST    R1,#ACTIONE            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONE    SET POSSIBLE FSINGLE FLAG
         B     PARSECHK
         SPACE 2
         USING PDLVERIF,R6
ZVERIFY  MVC   #VERLOAD(1),VERLOAD+1    SAVE LOAD/NOLOAD OPTION
         MVC   #VERSTND(1),VERNAME+1    SAVE NAME/NONAME OPTION
         MVC   #VERREAD(1),VERINPUT+1   SAVE INPUT/NOINPUT OPTION
         MVC   #VERUPDT(1),VERUPDT+1    SAVE UPDATE/NOUPDATE OPTION
         B     PARSECHK
         SPACE 2
         USING PDLOUT,R6
ZOUTCOPY CLI   OUTCLOS+1,1               CLOSE REQUESTED?
         BNE   *+8                       NO, BRANCH
         MVI   #OUT,1                    YES, FLAG FOR LATER
         MVC   #OUT+1(1),OUTSAVE+1       SAVE RECORD/NORECORD
         CLI   OUTECHO+1,1               ECHO/NOECHO?
         BL    PARSECHK                  NO, BRANCH
         OI    FLAGSFF,FOUTECHO          ASSUME ECHO MODE IS DESIRED
         CLI   OUTECHO+1,1               CORRECT?
         BE    PARSECHK                  YES, BRANCH
         NI    FLAGSFF,FF-FOUTECHO       NO, NOECHO IS DESIRED
         B     PARSECHK
         EJECT
         USING PDLATTR,R6
ZATTRIB  MVC   #ALIAOPT(1),INFOALIA+1     SAVE ALIASINFO/NOALIASINFO
         MVC   #LKEDOPT(1),INFOLKED+1     SAVE LKEDDATE/NOLKEDDATE
         MVC   #RLDERR(1),LKEDRLDF+1      SAVE RLDFIX/NORLDFIX
         CLI   LKEDNONE+1,X'01'           NONE ATTRIBUTE USED?
         BNE   ZATTR10                    NO, BRANCH
         MVI   #AUTHOPT,2                 SIMULATE "NOAUTH"
         MVI   #PAGEOPT,2                 SIMULATE "NOPAGE"
         MVI   #RMODE,1                   SIMULATE "RMODE24"
         MVI   #AMODE,1                   SIMULATE "AMODE24"
         OI    #ATTRNO,ATTRREUS+ATTRRENT+ATTRLOAD  "NO" REUS/RENT/LOAD
         OI    #ATTRNO+1,ATTRREFR+ATTRNE  "NOREFR" AND "EDIT"
         OI    #ATTRYES,ATTREXEC          "EXEC"
         OI    #ATTRYES+1,ATTRNODC        "NODC"
         SPACE 2
ZATTR10  ICM   R1,B'0001',LKEDAUTH+1    AUTH/NOAUTH
         BZ    *+8
         STCM  R1,B'0001',#AUTHOPT
         ICM   R1,B'0001',LKEDPAGE+1    PAGE/NOPAGE
         BZ    *+8
         STCM  R1,B'0001',#PAGEOPT
         ICM   R1,B'0001',LKEDRMOD+1    RMODE24/RMODEANY
         BZ    *+8
         STCM  R1,B'0001',#RMODE
         ICM   R1,B'0001',LKEDAMOD+1    AMODE24/AMODE31/AMODEANY
         BZ    *+8
         STCM  R1,B'0001',#AMODE
         SPACE 2
ZATTR30  CLI   LKEDREUS+1,X'01'       REUSABLE ATTRIBUTE STATUS:
         BL    ZATTR34                  NONE, BRANCH
         BH    ZATTR32                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRREUS        "REUS"
         NI    #ATTRNO,FF-ATTRREUS      MAKE SURE NEGATIVE IS OFF
         B     ZATTR34
ZATTR32  OI    #ATTRNO,ATTRREUS         "NOREUS"
         SPACE 1
ZATTR34  CLI   LKEDREFR+1,X'01'       REFRESHABLE ATTRIBUTE STATUS:
         BL    ZATTR40                  NONE, BRANCH
         BH    ZATTR36                  "NO" FORM, BRANCH
         OI    #ATTRYES+1,ATTRREFR      "REFR"
         NI    #ATTRNO+1,FF-ATTRREFR    MAKE SURE NEGATIVE IS OFF
         B     ZATTR40
ZATTR36  OI    #ATTRNO+1,ATTRREFR       "NOREFR"
         SPACE 1
ZATTR40  CLI   LKEDRENT+1,X'01'       REENTRANT ATTRIBUTE STATUS:
         BL    ZATTR44                  NONE, BRANCH
         BH    ZATTR42                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRRENT        "RENT"
         NI    #ATTRNO,FF-ATTRRENT      MAKE SURE NEGATIVE IS OFF
         B     ZATTR44
ZATTR42  OI    #ATTRNO,ATTRRENT         "NORENT"
         SPACE 1
ZATTR44  CLI   LKEDEXEC+1,X'01'       EXECUTABLE ATTRIBUTE STATUS:
         BL    ZATTR50                  NONE, BRANCH
         BH    ZATTR46                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTREXEC        "EXEC"
         NI    #ATTRNO,FF-ATTREXEC      MAKE SURE NEGATIVE IS OFF
         B     ZATTR50
ZATTR46  OI    #ATTRNO,ATTREXEC         "NOEXEC"
         SPACE 1
ZATTR50  CLI   LKEDLOAD+1,X'01'       LOADABLE ATTRIBUTE STATUS:
         BL    ZATTR54                  NONE, BRANCH
         BH    ZATTR52                  "NO" FORM, BRANCH
         OI    #ATTRYES,ATTRLOAD        "LOAD"
         NI    #ATTRNO,FF-ATTRLOAD      MAKE SURE NOLOAD IS OFF
         B     ZATTR54
ZATTR52  OI    #ATTRNO,ATTRLOAD         "NOLOAD"
         SPACE 1
ZATTR54  CLI   LKEDEDIT+1,X'01'       NOT EDITABLE ATTRIBUTE STATUS:
         BL    ZATTR60                  NONE, BRANCH
         BH    ZATTR56                  "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNE        "EDIT"  -- BIT MUST BE REVERSED
         B     ZATTR60
ZATTR56  OI    #ATTRYES+1,ATTRNE        "NOEDIT"
         NI    #ATTRNO+1,FF-ATTRNE     MAKE SURE EDIT IS OFF
         SPACE 1
ZATTR60  CLI   LKEDDC+1,X'01'         DC ATTRIBUTE STATUS:
         BL    ZATTR99                  NONE, BRANCH
         BH    ZATTR62                  "NO" FORM, BRANCH
         OI    #ATTRNO+1,ATTRNODC       "DC"
         NI    #ATTRYES+1,FF-ATTRNODC   MAKE SURE NODC IS OFF
         B     ZATTR99
ZATTR62  OI    #ATTRYES+1,ATTRNODC      "NODC"
         SPACE 1
ZATTR99  OC    #ZAPOPT(14),#ZAPOPT    ANY LINKAGE EDITOR ATTRIBUTES?
         BZ    *+8                    NO, BRANCH
         MVI   #OPTOPT,FF             YES, SET A FLAG FOR LATER
         XC    #ATTRNO(2),=F'-1'      INVERT THE #ATTRNO BITS
         B     PARSECHK
         SPACE 2
         USING PDLHIST,R6
ZHISTORY MVC   #ZAPOPT(1),PCLHZAPK+1    SAVE THE ZAP OPTION
         MVC   #USEROPT(1),PCLHUSRK+1   SAVE THE USER OPTION
         MVC   #TRANOPT(1),PCLHTRNK+1   SAVE THE TRAN OPTION
         MVC   #LKEDOPT(1),PCLHLKDK+1   SAVE THE LKED OPTION
         SPACE 1
         TM    PCLHMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   ZHIST10                NO, BRANCH
         LH    R15,PCLHMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST10                NO, BRANCH
         L     R1,PCLHMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 3
ZHIST10  TM    PCLHTRN+6,X'80'        ANY TRANSLATOR NAME FILTERING?
         BNO   ZHIST20                NO, BRANCH
         LH    R15,PCLHTRN+4          LENGTH OF NAME
         STC   R15,#TRANLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST20                NO, BRANCH
         L     R1,PCLHTRN             PARTIAL TRANSLATOR NAME
         MVC   #TRANTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST20  TM    PCLHZAP+6,X'80'        ANY ZAP IDRDATA NAME FILTERING?
         BNO   ZHIST30                NO, BRANCH
         LH    R15,PCLHZAP+4          LENGTH OF NAME
         STC   R15,#ZAPLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST30                NO, BRANCH
         L     R1,PCLHZAP             PARTIAL IDRDATA DATA
         MVC   #ZAPTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST30  TM    PCLHUSR+6,X'80'        ANY USER IDRDATA FILTERING?
         BNO   ZHIST40                NO, BRANCH
         LH    R15,PCLHUSR+4          LENGTH OF NAME
         STC   R15,#USERLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    ZHIST40                NO, BRANCH
         L     R1,PCLHUSR             PARTIAL IDRDATA DATA
         MVC   #USERTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         SPACE 1
ZHIST40  TM    PCLHLKD+6,X'80'        ANY LKED IDRDATA FILTERING?
         BNO   PARSECHK               NO, BRANCH
         LH    R15,PCLHLKD+4          LENGTH OF NAME
         STC   R15,#LKEDLEN           SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    PARSECHK               NO, BRANCH
         L     R1,PCLHLKD             PARTIAL IDRDATA DATA
         MVC   #LKEDTXT(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         B     PARSECHK
         SPACE 3
         USING PDLRENAM,R6
ZRENAME  MVC   #RENSWAP(1),PCLRENSW+1    SAVE SWAP/NOSWAP
         MVC   #RENRANG(1),PCLRENGP+1    SAVE GROUP/NOGROUP
         CLI   #RENRANG,1                GROUP OPTION?
         BNE   PARSECHK                  NO, BRANCH
         NI    ##ADRPC#,FF-@B            YES, NO BLDL REQUIRED
         B     PARSECHK
         EJECT
         USING PDLDELET,R6
ZDELETE  MVC   #DELFLAG(1),PCLDELAL+1
         B     PARSECHK
         SPACE 3
         USING PDLMAP,R6
ZMAP     MVC   #MAPOPT(1),PCLMAPOP+1  SAVE ANY PROCESSING OPTIONS
         TM    PCLMMOD+6,X'80'        ANY MODULE NAME FILTERING?
         BNO   PARSECHK               NO, BRANCH
         LH    R15,PCLMMOD+4          LENGTH OF MODULE NAME
         STC   R15,#MODLEN            SAVE FOR LATER
         S     R15,=F'1'              MACHINE LENGTH VALID?
         BM    PARSECHK               NO, BRANCH
         L     R1,PCLMMOD             PARTIAL MODULE NAME
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                MOVE INTO STORAGE
         B     PARSECHK
         SPACE 3
         USING PDLRESTO,R6
ZRESTORE TM    FLAGSGG,FPROMCON            PROMPTING OFF?
         BNO   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFPRO*4         YES, SET A FLAG
         CLI   PCLRSREP+1,X'01'            REPEAT?
         BNE   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFREP+#RESFDIS  YES, SET FLAGS
         CLI   PCLRSDIS+1,X'01'            DISPLAY?
         BNE   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFDIS           YES, SET A FLAG
         CLI   PCLRSPRO+1,X'01'            PROMPT?
         BNE   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFPRO+#RESFDIS  YES, SET FLAGS
         SPACE 1
         CLI   PCLRSDIS+1,X'02'            NODISPLAY?
         BNE   *+8                         NO, BRANCH
         NI    #RESFLAG,FF-#RESFDIS        YES, TURN IT OFF
         CLI   PCLRSPRO+1,X'02'            NOPROMPT?
         BNE   *+8                         NO, BRANCH
         OI    #RESFLAG,#RESFPRO*2         YES, SET A FLAG
         SPACE 1
         TM    PCLREST+6,X'80'             ANY RESTORE LIKE OPERAND?
         BNO   PARSECHK                    NO, BRANCH
         LH    R15,PCLREST+4               LENGTH OF NAME
         S     R15,=F'1'                   MACHINE LENGTH VALID?
         BM    PARSECHK                    NO, BRANCH
         MVC   #RESTLIK,BLANKS             INITIALIZE THE MEMBER NAME
         L     R1,PCLREST                  PARTIAL IDRDATA DATA
         MVC   #RESTLIK(*-*),0(R1)         <<EXECUTED>>
         EX    R15,*-6                     MOVE INTO STORAGE
         B     PARSECHK
         EJECT
         USING PDLFIX,R6
ZFIXPDS  MVC   #FIXOPT(1),PCLFIXOP+1  SAVE FIXPDS MAIN KEYWORD OPTION
         SPACE 1
         SR    R1,R1
         ICM   R1,B'0001',RTYPE+1   ANY GENERAL DATA TYPE?
         BZ    ZFIX010              NO, BRANCH
         MH    R1,=H'22'            INDEX VALUE
         LA    R3,RTABLE-22-22(R1)  CURRENT ENTRY -22
         MVC   #RECFM+3(1),22+1(R3) RECFM
         MVC   #LRECL+2(2),22+2(R3) LRECL
         IC    R1,BYTEUCB+1         DEVICE INDEX
         LA    R0,X'0000000E'       MAXIMUM DEFINED INDEX
         CR    R1,R0                ABOVE 3380?
         BNH   *+6                  NO, BRANCH
         LR    R1,R0                YES, USE 3380 DATA
         AR    R1,R1                DEVICE INDEX * 2 (FOR TABLE ENTRY)
         LA    R3,22+4-12(R3,R1)    POINT TO DEVICE BLOCKSIZE
         MVC   #BLKSI+2(2),0(R3)    BLKSIZE
         SPACE 1
ZFIX010  LH    R1,RRECFMZI          RECORD FORMAT INDEX (0 IF NONE)
         LA    R1,RECFMVAL(R1)      INDEX TO CORRESPONDING DCBRECFM
         CLI   0(R1),X'00'          DEFAULT RECFM VALUE?
         BE    *+10                 YES, BRANCH
         MVC   #RECFM+3(1),0(R1)    NO, NEW RECORD FORMAT
         SPACE 1
         LA    R1,RDIRZI            NUMBER OF DIRECTORY BLOCKS
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#NEWDIR           SAVE RESULT IF CODED
         SPACE 1
         LA    R1,RLRECLZI          LRECL DESIRED
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#LRECL            SAVE RESULT IF CODED
         SPACE 1
         LA    R1,RBLKSIZI          BLOCKSIZE DESIRED
         BAL   R14,PACKMAX          CONVERT TO BINARY
         ST    R1,#BLKSI            SAVE RESULT IF CODED
         B     PARSECHK
         SPACE 3
         USING PDLCONT,R6
ZCONTROL MVC   #OUT+0(1),PALINCON+1
         MVC   #OUT+1(1),PALUPCON+1
         MVC   #OUT+2(1),PLKEDCON+1
         MVC   #OUT+3(1),PPROMCON+1
         MVC   #OUT+4(1),PRECVCON+1
         MVC   #OUT+5(1),PDOUBCON+1
         MVC   #OUT+7(1),PIMMCON+1
         B     PARSECHK
         EJECT
         USING PDLFIND,R6
ZFIND    TM    FINDSTRG+6,X'80'     STRING CODED?
         BNO   PARSECHK             NO, ASSUME OK
         L     R5,FINDSTRG          NEW STRING ADDRESS
         LH    R2,FINDSTRG+4        LENGTH OF STRING
FINDTEST LTR   R2,R2                NULL STRING CODED?
         BNP   FINDCHK              YES, CHECK FORMAT
         BCTR  R5,0                 BACK UP ONE
         OI    0(R5),X'40'          UPPER CASE THE DELIMITER
         CLI   0(R5),C'X'           POSSIBLE HEX STRING?
         BNE   FINDCHAR             NO, BRANCH
         LA    R1,MSGHEXBD          BAD HEX STRING MESSAGE
         LA    R3,1(,R2)            STRING LENGTH +1
         N     R3,=F'-2'            NEXT EVEN NUMBER
         SR    R15,R15              ZERO
         LA    R4,MSGTEXT2+4        WHERE OUTPUT GOES
         LA    R0,2                 PAIRWISE COUNTER
         CR    R2,R3                EVEN LENGTH ORIGINALLY?
         SRL   R3,1                 (STRING LENGTH +1)/2
         ST    R3,MSGTEXT2          (STRING LENGTH +1)/2
         BNE   FINDHEX2             NO, BRANCH
         SPACE 1
FINDHEX1 LA    R5,1(,R5)            NEXT CHARACTER
         IC    R15,0(,R5)           GET A BYTE TO CONVERT
         CLI   0(R5),C'0'
         BL    *+12                 0-9?
         CLI   0(R5),C'9'
         BNH   FINDHEX2             YES, BRANCH
         LA    R15,9(,R15)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'           BELOW AN A?
         BL    FINDNEWS             YES, INVALID
         CLI   0(R5),C'F'           ABOVE AN F?
         BH    FINDNEWS             YES, INVALID
         SPACE
FINDHEX2 SLL   R15,28               SHIFT
         SLDL  R14,4                GET A GOOD DIGIT
         BCT   R0,FINDHEX1          DO THE NEXT DIGIT OF A PAIR
         LA    R0,2                 PAIRWISE COUNTER
         STC   R14,0(,R4)           SAVE AN OUTPUT CHARACTER
         LA    R4,1(,R4)            ANOTHER OUTPUT CHARACTER
         BCT   R3,FINDHEX1          DO ALL DIGITS
         SPACE
         LA    R5,MSGTEXT2+4-1      START OF CONVERTED STRING -1
         L     R2,MSGTEXT2          LENGTH OF CONVERTED STRING
         CH    R2,=H'16'            VALID LENGTH?
         BH    FINDNEWS             NO, BRANCH
         EJECT
FINDCHAR LA    R1,MSGCHRBD          INVALID LENGTH MESSAGE
         CH    R2,=H'16'            VALID LENGTH?
         BH    FINDNEWS             NO, BRANCH
         STH   R2,FINDLTH           SAVE ACTUAL LENGTH
         BCTR  R2,0                 MACHINE LENGTH
         MVC   FINDSTR(*-*),1(R5)   <<EXECUTED>>
         EX    R2,*-6               SAVE THE NEW STRING
         XC    FINDTBL,FINDTBL      RESET THE TRT TABLE
         SR    R15,R15
         IC    R15,FINDSTR          FIRST CHARACTER OF NEW STRING
         LA    R1,FINDTBL(R15)      ADDRESS OF NEW TRT BYTE
         MVI   0(R1),X'77'          SET TRT BYTE TO NON-ZERO
         B     FINDCHK              TERMINATE
         SPACE 2
FINDNEWS MESSAGE (R1)               INVALID LENGTH STRING -- REENTER
FINDNEW0 LA    R2,NEWCMD            EXIT ADDRESS FOR ATTENTION
        $PUTGET MSGRESTR,ATTN=FREEPDL  SECOND PART OF MESSAGE
         L     R5,ADDRCBUF          ENTERED DATA
         LH    R2,0(,R5)            LENGTH OF STRING
         SH    R2,=H'5'             VALID MACHINE LENGTH?
         LA    R1,4(R5,R2)          LAST BYTE OF INPUT STRING
         BZ    FINDNEW3             MAYBE, CHECK FURTHER
         BNP   FINDNEW0             NO, NULL STRING -- PROMPT AGAIN
         SPACE 1
FINDNEW1 CLI   0(R1),X'40'          SCAN
         BNE   FINDNEW2                 BACKWARDS
         BCTR  R1,0                              FOR A
         BCT   R2,FINDNEW1                             NON-BLANK
         B     FINDNEW3             POSSIBLE STRING -- CHECK
         SPACE 1
FINDNEW2 CLI   4(R5),X'40'          SCAN
         BNE   FINDNEW3                 FORWARDS
         LA    R5,1(,R5)                        FOR A
         BCT   R2,FINDNEW2                            NON-BLANK
         SPACE 1
FINDNEW3 CLI   4(R5),X'40'          REAL STRING?
         BE    FINDNEW0             NO, PROMPT AGAIN
         MVI   MSGTEXT1,X'40'       BLANKS FOR
         MVC   MSGTEXT1+1(40),MSGTEXT1        UPPER-CASING INPUTS
         OC    4(*-*,R5),MSGTEXT1   <<EXECUTED>>
         EX    R2,*-6               UPPER-CASE DATA AND DELIMITERS
         CLC   0(1,R1),4(R5)        SAME DELIMITERS?
         BNE   *+6                  NO, BRANCH
         BCTR  R2,0                 YES, ALLOW FOR LAST DELIMITER
         LA    R5,5(,R5)            FIRST CHARACTER OF STRING
         B     FINDTEST             PARSE THIS NEW STRING
         EJECT
FINDCHK  CLI   FINDLIST+1,1           FINDLIST SPECIFIED?
         BNE   FINDCHK1               NO, BRANCH
         OI    ##ADRPA#,#FIND1        YES, TURN ON THE FLAG
         SPACE 1
FINDCHK1 CLI   FINDFMT+1,0            WAS A FORMAT SPECIFIED?
         BE    FINDCHK2               NO, BRANCH
         NI    HFLAGSDD,FF-NONUM-SNUM-BLOCK-DUMP-LBLOCK-LDUMP
         LH    R15,FINDFMT            FORMAT REQUESTED
         A     R15,=F'-2'             LESS 2 -- NEGATIVE?
         BM    FINDCHK2               YES, BRANCH
         LA    R1,1                   SHIFT VALUE
         SLL   R1,0(R15)              SHIFT INTO POSITION
         OI    HFLAGSDD,*-*           <<EXECUTED>>
         EX    R1,*-4                 MOVE INTO FLAG BYTE
         SPACE 1
FINDCHK2 LA    R1,FMAXLENC            MAXLEN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXLEN             SAVE RESULT
         SPACE 1
         LA    R1,FMAXINC             MAXIN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXIN              SAVE RESULT
         SPACE 1
         LA    R1,FMAXOUTC            MAXOUT
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXOUT             SAVE RESULT
         SPACE 1
         LA    R1,FSKIPRC             SKIPREC
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPREC            SAVE RESULT
         SPACE 1
         LA    R1,FSKIPCC             SKIPCOL
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPCOL            SAVE RESULT
         SPACE 1
         LH    R1,FTHENCC             THEN ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         THEN ACTION POINTER
         ST    R1,#ACTIONT            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONT    SET POSSIBLE FSINGLE FLAG
         SPACE 1
FINDCHK3 LH    R1,FELSECC             ELSE ACTION NUMBER
         SLA   R1,2                   ACTION SPECIFIED
         L     R1,THENTBL(R1)         ELSE ACTION POINTER
         ST    R1,#ACTIONE            NOTE FOR LATER
         OC    FLAGSII(1),#ACTIONE    SET POSSIBLE FSINGLE FLAG
         SPACE 1
         TM    FINDOFF+6,X'80'        OFFSET CODED?
         BNO   FINDCHK4               NO, DONE
         L     R5,FINDOFF             OFFSET ADDRESS
         LH    R1,FINDOFF+4           LENGTH OF OFFSET
         LTR   R1,R1                  NULL STRING CODED?
         BNP   FINDCHK4               YES, DONE
         MVI   MSGTEXT2+1,3           MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,MSGLISTO      OFFSET INVALID MESSAGE
         BAL   R2,HEXC                CONVERT TO BINARY
         B     FREEPDL                ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#LISTOFF+1  SAVE THE RESULTING OFFSET
         SPACE 1
FINDCHK4 LH    R15,FDUMPCS+4          LENGTH OF BLOCK/DUMP STRING
         STC   R15,#MODLEN
         S     R15,=F'1'              ANY STRING?
         BM    PARSECHK               NO, BRANCH
         L     R1,FDUMPCS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         B     PARSECHK
         SPACE 3
         USING PDLLIST,R6
ZLIST    CLI   LISTFMT+1,0            WAS A FORMAT SPECIFIED?
         BE    LISTCHK2               NO, QUIT
         NI    HFLAGSDD,FF-NONUM-SNUM-BLOCK-DUMP-LBLOCK-LDUMP
         LH    R15,LISTFMT            FORMAT REQUESTED
         A     R15,=F'-2'             LESS 2 -- NEGATIVE?
         BM    LISTCHK2               YES, BRANCH
         LA    R1,1                   SHIFT VALUE
         SLL   R1,0(R15)              SHIFT INTO POSITION
         OI    HFLAGSDD,*-*           <<EXECUTED>>
         EX    R1,*-4                 OR INTO FLAG BYTE
         SPACE 1
LISTCHK2 LA    R1,LMAXLENC            MAXLEN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXLEN             SAVE RESULT
         SPACE 1
         LA    R1,LMAXINC             MAXIN
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXIN              SAVE RESULT
         SPACE 1
         LA    R1,LMAXOUTC            MAXOUT
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#MAXOUT             SAVE RESULT
         SPACE 1
         LA    R1,LSKIPRC             SKIPREC
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPREC            SAVE RESULT
         SPACE 1
         LA    R1,LSKIPCC             SKIPCOL
         BAL   R14,PACKMAX            CONVERT TO BINARY
         ST    R1,#SKIPCOL            SAVE RESULT
         SPACE 1
         TM    LISTOFF+6,X'80'        OFFSET CODED?
         BNO   LISTCHK3               NO, DONE
         L     R5,LISTOFF             OFFSET ADDRESS
         LH    R1,LISTOFF+4           LENGTH OF OFFSET
         LTR   R1,R1                  NULL STRING CODED?
         BNP   LISTCHK3               YES, DONE
         MVI   MSGTEXT2+1,3           MAXIMUM OF 3 DIGITS
         MVC   MSGTEXT1,MSGLISTO      OFFSET INVALID MESSAGE
         BAL   R2,HEXC                CONVERT TO BINARY
         B     FREEPDL                ATTENTION -- IGNORE THE REST
         STCM  R0,B'0111',#LISTOFF+1  SAVE THE RESULTING OFFSET
         SPACE 1
LISTCHK3 LH    R15,LDUMPCS+4          LENGTH OF BLOCK/DUMP STRING
         STC   R15,#MODLEN
         S     R15,=F'1'              ANY STRING?
         BM    PARSECHK               NO, BRANCH
         L     R1,LDUMPCS             ADDRESS OF BLOCK/DUMP STRING
         MVC   #MODTXT(*-*),0(R1)     <<EXECUTED>>
         EX    R15,*-6                SAVE THE STRING FOR LATER
         B     PARSECHK
         SPACE
PACKMAX  LH    R15,4(,R1)             NUMBER OF DIGITS
         S     R15,=F'1'              ANY CODED?
         BM    4(R14)                 NO, RETURN AT +4
         L     R1,0(,R1)              POINT TO INPUT STRING
         EX    R15,PACKNUMB           PACK ALL THE DIGITS
         CVB   R1,DOUBLE              CONVERT TO BINARY
         BR    R14                    RETURN AT +0
         SPACE
PACKNUMB PACK  DOUBLE(8),0(*-*,R1)    <<EXECUTED>>
         EJECT
         USING MAINPDL,R6
ZMAINPDL MVC   VOLUME,BLANKS        INITIALIZE VOLUME NAME
         TM    VOLNAME+6,X'80'      ANY NAME REQUESTED?
         BNO   MAINPDL2             NO, BRANCH
         LH    R15,VOLNAME+4        LENGTH OF NAME
         S     R15,=F'1'            VALID?
         BM    MAINPDL2             NO, BRANCH
         L     R1,VOLNAME           ADDRESS OF NAME
         MVC   VOLUME(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6              MOVE IN THE NAME
         SPACE 1
MAINPDL2 MVI   DSPALLOC,DA08SHR     ASSUME DISP=SHR
         MVI   DSPREQST,DA08SHR     ASSUME DISP=SHR
         CLI   OPTDISP+1,1          CORRECT?
         BNE   MAINPDL4             NO, BRANCH
         MVI   DSPALLOC,DA08OLD     DISP=OLD
         MVI   DSPREQST,DA08OLD     DISP=OLD
         DROP  R6
         SPACE 1
MAINPDL4 TM    FLAGSBB,FQUOTE       QUOTED DATA SET NAME?
         BO    PARSECHK             YES, BRANCH
         CLI   VOLUME,X'40'         ANY VOLUME NAME SPECIFIED?
         BNE   PARSECHK             YES, ASSUME NO CATALOG USE
*  NEED TO FULLY QUALIFY THE DATA SET NAME
         USING DFPL,R6
         LA    R6,PARMLIST          START OF DFPL PARAMETER LIST
         L     R1,ADDRUPT
         L     R2,ADDRECT
         LA    R3,ATTNECB
         LA    R4,PARMLIST+16
         STM   R1,R4,DFPLUPT        INITIALIZE DFPLUPT, DFPLECT,
*                                              DFPLECB AND DFPLDFPB
         USING DFPB,R6
         LR    R6,R4
         LA    R1,DSNLEN            DSNAME LENGTH, DSNAME
         L     R2,ADDRPSCB          PSCB
         LA    R3,DOUBLE            BLANK DEFAULT QUALIFIER
         SR    R4,R4                NO USER CATALOG
         LA    R5,PASSWORD
         STM   R1,R5,DFPBDSN        INITIALIZE DFPBDSN, DFPBPSCB,
*                                     DFPBQUAL, DFBCAT AND DFPBPSWD
         MVI   DFPBCNTL,DFPBRET     ADD LOW-LEVEL QUALIFIER
         MVI   DFPBCODE,DFPB04      ENTRY CODE X'04'
         MVC   DOUBLE(8),BLANKS     BLANK DEFAULT QUALIFIER
         DROP  R6
         SPACE 1
         LA    R1,PARMLIST          ADDRESS OF PARM LIST
         L     R15,ADDRDEFT         DEFAULT ROUTINE ADDRESS
         BALR  R14,R15
         EJECT
PARSECHK SR    R15,R15
         IC    R15,R14PARSE
         SPACE 1
         LA    R14,MAXPARSE         RETURN CODE LIMIT
         CR    R15,R14              RETURN CODE WITHIN LIMITS?
         BH    PARSEBAD             NO, ERROR
         SPACE 1
         LA    R3,4                 OFFSET OF RETURN IF SUCCESSFUL
         B     *+4(R15)             PROCESS RETURN CODE
PARSERET B     PARSEOK                00 - SUCCESSFUL
         B     EXIT8N                 04 - PARSE UNABLE TO PROMPT
         B     PARSERR                08 - USER ENTERED ATTENTION
         B     PARSEBAD               12 - INVALID PARAMETERS
         B     PARSEBAD               16 - PARSE INTERNAL FAILURE
         B     EXIT8N                 20 - VALIDITY CHECK ERROR
MAXPARSE EQU   *-PARSERET
         SPACE 2
PARSEBAD LA    R1,GFPARMS
         ST    R1,GFPARMP           SAVE ADDRESS OF PARMLIST
         L     R0,ADDRFF02          ADDRESS OF IKJEFF02
         STM   R15,R0,GFRCODE       SAVE GFRCODE AND GF02PTR
         LA    R1,GFPARSE
         STH   R1,GFCALLID          SAVE CALLER-ID FOR PARSE
         L     R0,ADDRCPPL          CPPL
         LA    R1,ATTNECB           ATTENTION ECB
         STM   R0,R1,GFCPPLP        SAVE GFCPPLP AND GFECBP
         MVI   ATTNECB,0            CLEAR THE ECB
         LINK  EP=IKJEFF19,MF=(E,GFPARMP)
         CLI   R14PARSE,X'4'        USER ENTER ATTENTION?
         BE    PARSERR              YES, BRANCH -- IGNORE COMMAND
         LTR   R15,R15              ERROR MESSAGE OUTPUT?
         BZ    EXIT8N               YES, BRANCH
         CVD   R15,DOUBLE
         MVC   MSGTEXT1,MSGGENF     GENERAL FAIL ROUTINE FAILED
         ED    MSGTEXT1+MSGGENF1-MSGGENF(4),DOUBLE+6
         MVI   MSGTEXT1+MSGGENF1-MSGGENF,C'='
         LA    R1,MSGTEXT1
         B     EXIT8M
         SPACE 1
PARSERR  SR    R3,R3                RETURN AT OFFSET 0
         SPACE 1
PARSEOK  BAL   R2,FREEPDL           FREE PARSE STORAGE
         L     R8,R8PARSE           ORIGINAL BASE REGISTER
         L     R14,R14PARSE         EXIT BASE ADDRESS
         B     0(R3,R14)            EXIT FROM PARSE AT +0 OR +4
         EJECT
*
*        PARSE CLEANUP ROUTINE
*
         SPACE 1
FREEPDL  IKJRLSA ADDRANSR            RELEASE THE STORAGE
         SPACE
         XC    ADDRANSR,ADDRANSR
         BR    R2
         TITLE 'P D S  --  PDS PARSE VALIDITY CHECKING        02/06/84'
*
*
*        INPUT DATA SET VALIDITY CHECK EXIT
*
*
         SPACE 3
VERDSN   ENTER VALCHECK
         TM    FLAGSCC,FRECOVER         RECOVERY PROMPT?
         BO    VERDSN2                  YES, BRANCH
         MVC   SAVDSN+1(SAVLEN),DSNLEN  SAVE PREVIOUS DSNAME
         MVC   SAVDSN(1),DSPREQST       SAVE DISPOSITION FOR LATER
         SPACE 1
VERDSN2  CLEAR DSNAME
         MVC   PASSWORD,BLANKS
         SPACE 1
         L     R15,0(R6)                ADDR OF DSNAME
         LH    R14,4(R6)                LENGTH OF DSNAME
         STH   R14,DSNLEN
         BCTR  R14,0
         MVC   DSNAME(*-*),0(R15)       <<EXECUTED>>
         EX    R14,*-6                  MOVE DSNAME TO AREA
         L     R1,ADDRUPT               ADDRESS OF THE UPT
         SR    R14,R14
         IC    R14,UPTPREFL-UPT(R1)     LENGTH OF USERID
         MVC   DSNAMEH(4),DSNAME        FIRST FOUR BYTES OF DSNAME
         BCTR  R14,0                    MACHINE LENGTH
         CLC   DSNAME(*-*),UPTPREFX-UPT(R1) <<EXECUTED>>
         EX    R14,*-6                      START WITH USERID?
         LA    R1,2(R14,R15)                DATA AFTER PERIOD
         BNE   *+10                         NO, BRANCH
         MVC   DSNAMEH(4),0(R1)         FIRST FOUR BYTES OF DSNAME
         SPACE 10
         TM    14(R6),X'80'             MEMBER SUPPLIED?
         BZ    NOMEMB                   NO, BRANCH
         SPACE
         L     R15,8(,R6)               POINTER TO MEMBER NAME
         LH    R14,12(,R6)              LENGTH OF MEMBER NAME
         BCTR  R14,0
         MVC   MEMBERD+3(8),BLANKS      MEMBER NAME DEFAULT AREA
         MVC   MEMBERD+3(*-*),0(R15)    <<EXECUTED>>
         EX    R14,*-6                  MOVE MEMBER TO AREA
         STCM  R14,B'0011',MEMBERD+1    MACHINE LENGTH OF MEMBER NAME
         MVI   MEMBERD,FMEMBER1         JUST ONE MEMBER NAME DEFAULT
         SPACE 2
NOMEMB   TM    22(R6),X'80'             PASSWORD SUPPLIED?
         BZ    NOPASS                   NO, BRANCH
         SPACE
         L     R15,16(,R6)              POINTER TO PASSWORD
         LH    R14,20(,R6)              PASSWORD LENGTH
         BCTR  R14,0
         MVC   PASSWORD(*-*),0(R15)     <<EXECUTED>>
         EX    R14,*-6                  MOVE PASSWORD TO AREA
         SPACE 3
NOPASS   OI    FLAGSBB,FQUOTE           ASSUME DSNAME QUOTED
         TM    6(R6),X'40'              CORRECT?
         BO    *+8                      YES, BRANCH
         XI    FLAGSBB,FQUOTE           NO, MUST BE QUALIFIED
         SPACE 3
         SR    R15,R15
         SPACE
         EXIT
         EJECT
*
*        MEMBER NAME VALIDITY CHECK SUBROUTINE
*
         SPACE 1
VERMEMBR ENTER VALCHECK
         SR    R15,R15            ASSUME NOT PARTITIONED
         TM    ##ADRPC#,@S        CORRECT?
         BO    VERMEMX            YES, EXIT
         SPACE 1
         L     R15,0(R6)          ADDR OF MEMBER NAME
         LH    R14,4(R6)          LENGTH OF NAME
VERMEMS  BCTR  R14,0              EXECUTE LENGTH
         SPACE 1
         OI    FLAGSAA,FMEMBER2
         TM    FLAGSAA,FMEMBER1   FIRST MEMBER PROCESSED?
         LA    R6,MEMBER2
         LA    R2,LMEMBER2
         BO    MEM2ND             YES, DO THE SECOND MEMBER NAME
         XI    FLAGSAA,FMEMBER1+FMEMBER2
         LA    R6,MEMBER1
         LA    R2,LMEMBER1
MEM2ND   MVC   0(8,R6),BLANKS     BLANK THE MEMBER NAME
         SPACE 1
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER NAME?
         BO    VERSOUT            YES, BRANCH
         TM    ##ADRPC#,@G        DEFAULT AND MEMBER GROUPS ALLOWED?
         BO    VERM190            YES, BRANCH
         TM    ##ADRPA#,@C        PATTERN OR MEMBER SUBCOMMAND?
         BNO   VERSOUT            NO, BRANCH
VERM190  LA    R0,1(,R14)         ACTUAL MEMBER NAME LENGTH
         LR    R5,R15             START OF MEMBER NAME
         LTR   R14,R14            SINGLE CHARACTER NAME?
         BNZ   VERM210            NO, BRANCH
         CLI   0(R5),C'/'         MEMBER PATTERN REQUEST?
         BE    VERBAD             YES, INVALID
         CLI   0(R5),C'*'         MEMBER PLACE-HOLDER?
         BE    VERM200            YES, BRANCH
         CLI   0(R5),C':'         MEMBER RANGE REQUEST?
         BNE   VERSOUT            NO, BRANCH
         NI    FLAGSAA,FF-FMEMBER1        ENTIRE DIRECTORY DESIRED
         OI    FLAGSAA,FMEM#MEM+FMEMRANG  MEMBER RANGE GROUP DESIRED
         B     VERGOOD
         EJECT
VERM200  OI    FLAGSAA,FMEMSTAR   MEMBER NAME STAR (*) NOTATION
         CLI   MEMBERD,X'00'      ANY MEMBER DEFAULT YET?
         BE    VERSOUT            NO, JUST USE *
         NI    FLAGSAA,FF-FOPTIONS    SET FLAGS FOR NO OPERAND
         MVC   LMEMBER1(2+8+8+2),MEMBERD+1
         NI    FLAGSAA,FF-FMEMBER1-FMEMBER2-FMEM#MEM-FMEMRANG
         OC    FLAGSAA,MEMBERD    RESET THE FLAG BITS
         B     VERGOOD
         SPACE 1
VERM210  CLI   0(R5),C':'         MEMBER RANGE DESIRED?
         BE    VERM220            YES, BRANCH
         CLI   0(R5),C'/'         MEMBER PATTERN DESIRED?
         BE    VERM222            YES, BRANCH
         LA    R5,1(,R5)          NEXT MEMBER CHARACTER
         BCT   R0,VERM210         CHECK ALL MEMBER CHARACTERS
         B     VERSOUT            NOT FOUND -- NORMAL MEMBER NAME
         SPACE 1
VERM220  OI    FLAGSAA,FMEMRANG   MEMBER RANGE DESIRED
VERM222  OI    FLAGSAA,FMEM#MEM   MEMBER GROUP DESIRED
         CR    R15,R5             FIRST CHARACTER OF NAME?
         BNE   VERM230            NO, BRANCH
         LA    R15,1(,R15)        SKIP OVER : OR /
         XC    LMEMBER2,LMEMBER2  NO MEMBER 2 YET
         TM    FLAGSAA,FMEMRANG   MEMBER RANGE DESIRED?
         BNO   VERM240            NO, BRANCH
         MVI   LMEMBER1+1,0       MACHINE LENGTH IS ZERO
         MVI   MEMBER1,X'00'      DATA IS X'00'
         B     VERMEMS            CONTINE WITH MEMBER2
         SPACE 1
VERM230  S     R0,=F'1'           ACTUAL LENGTH OF MEMBER2
         STH   R0,LMEMBER2        SAVE FOR LATER
         SR    R14,R0             ACTUAL LENGTH OF MEMBER1
         LA    R5,1(,R5)          SKIP OVER : OR +
         ST    R5,FULLWORD        SAVE FOR LATTER
         SPACE 1
VERM240  BCTR  R14,0              REDUCE MACHINE LENGTH BY ONE
         SPACE 2
VERSOUT  STH   R14,0(,R2)         STASH LENGTH
         SPACE
         LA    R5,0(R14,R15)      POINT TO LAST CHARACTER OF MEMBER
         LM    R0,R1,BLANKS       HEX MEMBER NAME PADDING
         SPACE
         CLC   =C'X''',0(R15)     BEGINS WITH X'?
         BE    VERHEX             YES, MUST BE A HEX MEMBER NAME
         SPACE
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER?
         BNO   MEMLEN             NO, BRANCH
         CLC   ##SUBCOM,RESTOLIT  RESTORE SUBCOMMAND?
         BNE   MEMLEN             NO, BRANCH
         BCTR  R15,0              BACK UP R15
         SR    R0,R0              ZERO MEMBER NAME PADDING
         SR    R1,R1              ZERO MEMBER NAME PADDING
         B     VERNULL            CHECK FOR A NULL
         EJECT
MEMLEN   CLI   1(R2),7            COMPARE LENGTH > 7?
         BNH   VERCHAR            NO, BRANCH
         B     VERBAD             YES, INVALID MEMBER NAME
         SPACE 2
VERHEX   CLI   0(R5),C''''        A FINAL QUOTE?
         BNE   VERBAD             NO, INVALID HEXADECIMAL
         BCTR  R5,0               POINT TO PRECEDING CHARACTER
         LA    R15,1(,R15)        POINT TO FIRST QUOTE
         SPACE
VERNULL  CR    R5,R15             NULL HEX STRING?
         BNH   VERBAD             YES, INVALID SYNTAX
         SR    R4,R4              COUNT OF DIGITS
         L     R14,=F'-1'         BXH INCREMENT
         SPACE
VERHLOOP IC    R3,0(,R5)          NEXT CHARACTER (REVERSE SCAN)
         CLI   0(R5),C'0'
         BL    *+12               0-9?
         CLI   0(R5),C'9'
         BNH   VER0TOF            YES, BRANCH
         LA    R3,9(,R3)          NO, ADJUST LOW DIGIT 1-5 TO A-F
         CLI   0(R5),C'A'
         BL    VERBAD             A-F?
         CLI   0(R5),C'F'
         BH    VERBAD             NO, BRANCH
         SPACE
VER0TOF  SLL   R3,28              GET DIGIT IN TOP DIGIT
         SRDL  R0,4               CREATE ROOM IN R0,R1
         OR    R0,R3              ADD TO R0,R1
         LA    R4,1(,R4)          ANOTHER DIGIT
         BXH   R5,R14,VERHLOOP    DO ALL DIGITS
         SPACE
         LA    R14,1(,R4)         DIGITS+1
         SRL   R14,1              (DIGITS+1)/2    -- TRUNCATED
         BCTR  R14,0              MACHINE COMPARE LENGTH
         STH   R14,0(,R2)         MACHINE LENGTH AFTER CONVERSION
         LA    R14,2(R14,R14)     (COMPARE LENGTH+1)*2
         CR    R14,R4             EVEN NUMBER OF DIGITS?
         BE    *+8                YES, BRANCH
         SRDL  R0,4               NO, ADJUST BY ONE MORE DIGIT
         SPACE
         STM   R0,R1,DOUBLE       SAVE CREATED MEMBER NAME
         LA    R14,7              FULL LENGTH MEMBER NAME
         LA    R15,DOUBLE         START OF MOVE ADDRESS
         SPACE 2
VERCHAR  MVC   0(*-*,R6),0(R15)   <<EXECUTED>>
         EX    R14,*-6            MOVE IN MEMBER NAME
         EJECT
         TM    FLAGSAA,FMEM#MEM   MEMBER GROUP PROCESSING?
         BNO   VERGOOD            NO, BRANCH
         TM    FLAGSAA,FMEMBER2   SECOND MEMBER PROCESSED?
         BO    VERGOOD            YES, BRANCH
         LH    R14,LMEMBER2       ACTUAL LENGTH OF MEMBER2
         L     R15,FULLWORD       START OF MEMBER2
         LTR   R14,R14            ANY MEMBER2?
         BP    VERMEMS            YES, BRANCH
         SPACE 1
VERGOOD  SR    R15,R15
         TM    ##ADRPC#,@G        NEW DEFAULT MEMBER NAME?
         BNO   VERGOOD2           NO, BRANCH
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1  SAVE CURRENT MEMBER NAMES
         MVC   MEMBERD(1),FLAGSAA           SAVE CURRENT FLAGS
         NI    MEMBERD,FF-FOPTIONS-FINDMSG-FMEMSTAR-FHEAD
         B     VERMEMX            TERMINATE
         SPACE 2
VERGOOD2 TM    ##ADRPA#,@C        DISPLAY OR PATTERN?
         BNO   VERGOOD4           NO, BRANCH
         TM    FLAGSAA,FMEM#MEM   MEMBER GROUP TO DISPLAY?
         BNO   VERGOOD4           NO, BRANCH
         XI    FLAGSAA,FMEM#MEM   TURN OFF THE MEMBER GROUP FLAG
         L     R1,DISPLIT+8       ASSUME DISPLAY
         TM    FLAGSAA,FMEMRANG   CORRECT?
         BO    *+8                YES, BRANCH
         L     R1,PATTLIT+8       NO, PATTERN
         ST    R1,##ADRCMD        UPDATE THE PROCESSING ADDRESS
         SPACE 2
VERGOOD4 CLC   ##SUBCOM,PATTLIT   NEW DEFAULT PATTERN?
         BNE   VERMEMX            NO, BRANCH
         MVC   MEMBERP+1(2+8+8+2),LMEMBER1  SAVE CURRENT MEMBER NAMES
         MVC   MEMBERP(1),FLAGSAA           SAVE CURRENT FLAGS
         NI    MEMBERP,FF-FOPTIONS-FINDMSG-FMEMSTAR-FHEAD
         B     VERMEMX            TERMINATE
         SPACE 2
VERBAD   LA    R15,4               INVALID MEMBER NAME -- TRY AGAIN
         TM    FLAGSAA,FMEMBER2    PROCESSING MEMBER2?
         BO    *+8                 YES, BRANCH
         NI    FLAGSAA,FF-FMEMBER1 NO, SAY NO MEMBER1 YET
         NI    FLAGSAA,FF-FMEMBER2 ALWAYS SAY NOT MEMBER2 NOW
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP REQUEST?
         BNO   VERMEMX             NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM-FMEMBER1-FMEMBER2-FMEMRANG
VERMEMX  EXIT
PDS61    CSECT
         TITLE 'P D S  --  PDS END                            02/06/84'
EXIT12O  LA    R1,MSGNOPEN              DATA SET DID NOT OPEN
EXIT12M  MESSAGE (R1)                   OUTPUT THE ERROR MESSAGE
EXIT12N  MVI   RETURNCD+3,12            ERROR CODE 12 (NO CLOSE & FREE)
         MVC   ##SUBCOM(8),ENDLIT       SIMULATE AN END SUBCOMMAND
         B     RETURN2
         SPACE
EXIT8M   MESSAGE (R1)                   OUTPUT THE ERROR MESSAGE
EXIT8N   MVI   RETURNCD+3,8             ERROR CODE 8
         MVC   ##SUBCOM(8),ENDLIT       SIMULATE AN END SUBCOMMAND
         SPACE
RETURN   BAL   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET TOO
         SPACE 1
RETURN2  ICM   R5,B'1111',RETURNCD      NORMAL EXIT?
         BZ    RETURN4                  YES, BRANCH
         SPACE 2
         LA    R1,PARMLIST              AREA FOR STACK PARM LIST
         USING IOPL,R1
         SPACE
         L     R2,ADDRUPT
         L     R3,ADDRECT
         LA    R4,ATTNECB
         STM   R2,R4,IOPLUPT            INITIALIZE IOPLUPT, IOPLECT,
         DROP  R1
         SPACE 2
         MVI   ATTNECB,0
         L     R15,ADDRSTCK             STACK ROUTINE
         STACK PARM=PARMLIST+16,DELETE=ALL,MF=(E,(1)),ENTRY=(15)
         SPACE 2
         TCLEARQ INPUT                  CLEAR INPUT BUFFERS
         SPACE 2
RETURN4  STAX  ,                        CANCEL ANY OUTSTANDING STAX
         ESTAE 0                        CANCEL THE ESTAE EXIT
         SPACE 2
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10' OUT OPEN?
         BNO   RETURN6                      NO, BRANCH
         MVI   OPENLIST,X'80'               END OF LIST
         CLOSE (QAMDCB),MF=(E,OPENLIST)     CLOSE THE OUT DATA SET
         SPACE 2
RETURN6  TM    FLAGSFF,FSPFCALL         TERMINATE SPF?
         BNO   RETURN8                  NO, BRANCH
         NI    FLAGSFF,FF-FSPFCALL      YES, TERMINATE SPF TOO
         LA    R1,RETURN8               END COMMAND FINAL CODE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         L     R13,4(,R13)              PREVIOUS SAVE AREA
         LM    R14,R12,12(R13)          SAVE REGISTERS
         LA    R15,4                    RC=4 -- SIMULATES RETURN KEY
         BR    R14                      RETURN TO SPF
         SPACE 2
RETURN8  TM    FLAGSFF,FSPFDIAL         PDS CALLED BY SPF AS A DIALOG?
         BNO   RETURN12                 NO, BRANCH
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'         SECOND PARAMETER
         LA    R2,=CL8'REFRESH'         THIRD PARAMETER
         STM   R0,R2,MSGTEXT2           SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'         LAST
         LA    R1,MSGTEXT2                  PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15                  LET SPF KNOW
         SPACE 1
         L     R3,SUBPDIAL              START OF GETMAINED AREA
         CLC   16+96+64+8(2,R3),=H'32'  EXPANDED I/O WORK AREA?
         BE    RETURN10                 NO, BRANCH
         L     R1,16+96+64(,R3)         START OF NEW I/O WORK AREA
         LH    R0,16+96+64+8(,R3)       SIZE OF NEW I/O WORK AREA
         ICM   R0,B'1000',SUBPDIAL      SUBPOOL OF NEW I/O WORK AREA
         FREEMAIN R,LV=(0),A=(1)
         SPACE 2
RETURN10 LA    R0,16+96+64+16           LENGTH OF CREATED AREAS
         CLC   16+96+64+8(2,R3),=H'32'  I/O WORK AREA RELEASE TOO?
         BNE   *+8                      NO, BRANCH
         LA    R0,16+96+64+16+32        LENGTH OF CREATED AREAS
         ICM   R0,B'1000',SUBPDIAL      SUBPOOL OF CREATED WORK AREAS
         FREEMAIN R,LV=(0),A=(3)
         SPACE 2
RETURN12 SR    R0,R0
         ICM   R0,B'1000',OUTMEMBS      ANY SUBPOOL 22 DATA?
         BZ    RETURN20                 NO, BRANCH
         FREEMAIN R,SP=(0)
         SPACE 2
RETURN20 L     R1,AREA2                 START OF AREA 2
         LA    R0,256*2                 SIZE OF AREA 2
         ICM   R0,B'1000',WORKSIZE      SUBPOOL OF AREA 2
         FREEMAIN R,LV=(0),A=(1)        FREE AREA 2
         SPACE 2
         LA    R2,ADDRPUTL              FIRST SERVICE ROUTINE ADDRESS
         LA    R3,MODULEN               FIRST LOAD ADDRESS
         SPACE 1
RETURN30 CLI   0(R2),X'D6'              DELETE REQUIRED?
         BNE   RETURN40                 NO, BRANCH
         DELETE  EPLOC=2(R3)
RETURN40 LA    R2,4(,R2)                NEXT SERVICE ADDRESS
         LA    R3,10(,R3)               NEXT MODULES ADDRESS
         CLI   0(R3),X'FF'              END OF MODULES?
         BNE   RETURN30                 NO, BRANCH
         SPACE 2
         LR    R13,R7                   ADDRESS OF THE FIRST SAVE AREA
         LR    R15,R5                   RETURN CODE
         L     R0,WORKSIZE
         SPACE
         EXIT  LV=(0)
         TITLE 'P D S  --  PDS PARSE DEFINITIONS              02/06/84'
*
*        P A R S E   C O N T R O L   L I S T
*
         PRINT NOGEN
         SPACE 3
PCLMAIN  IKJPARM DSECT=MAINPDL
INPUTDSN IKJPOSIT DSNAME,USID,VALIDCK=VERDSN,                          $
               PROMPT='NAME OF DATA SET',                              $
               HELP=('NAME OF THE DATA SET TO BE PROCESSED')
OPTVOL   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=$VOLUME
OPTDISP  IKJKEYWD
         IKJNAME 'OLD'
         IKJNAME 'SHR'
$VOLUME  IKJSUBF
VOLNAME  IKJIDENT 'VOLUME NAME',MAXLNTH=6,FIRST=ALPHANUM,              $
               OTHER=ALPHANUM,PROMPT='VOLUME NAME',                    $
               HELP=('NAME OF THE VOLUME WHERE THE DATA SET RESIDES')
         IKJENDP
PRIMOPTN DC    CL2'OL'      "OLD"
         DC    CL2'SH'      "SHR"
         DC    CL2'VO'      "VOLUME"
         DC    X'FF'        END OF MAIN COMMAND TABLE
         SPACE 3
PCLALIAS IKJPARM DSECT=PDLALIAS
PCLAL1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='CURRENT MEMBER NAME',VALIDCK=VERMEMBR,          $
               HELP=('NAME OF THE MEMBER TO BE ASSIGNED AN ALIAS')
PCLAL2   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='ALIAS NAME',VALIDCK=VERMEMBR,                   $
               HELP=('NAME TO BE ASSIGNED AS AN ALIAS')
         IKJENDP
         SPACE 2
PCLATTR  IKJPARM DSECT=PDLATTR
PCLAT1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER WHOSE ATTRIBUTES ARE TO BE LIS$
               TED'),VALIDCK=VERMEMBR
LKEDNONE IKJKEYWD
         IKJNAME  'NONE'
LKEDREUS IKJKEYWD
         IKJNAME  'REUS'
         IKJNAME  'NOREUS'
LKEDREFR IKJKEYWD
         IKJNAME  'REFR'
         IKJNAME  'NOREFR'
LKEDRENT IKJKEYWD
         IKJNAME  'RENT'
         IKJNAME  'NORENT'
LKEDEXEC IKJKEYWD
         IKJNAME  'EXEC'
         IKJNAME  'NOEXEC'
LKEDLOAD IKJKEYWD
         IKJNAME  'LOADONLY'
         IKJNAME  'NOLOADONLY'
LKEDEDIT IKJKEYWD
         IKJNAME  'EDIT'
         IKJNAME  'NOEDIT'
LKEDDC   IKJKEYWD
         IKJNAME  'DC'
         IKJNAME  'NODC'
LKEDAUTH IKJKEYWD
         IKJNAME  'AUTH'
         IKJNAME  'NOAUTH'
LKEDPAGE IKJKEYWD
         IKJNAME  'PAGE'
         IKJNAME  'NOPAGE'
LKEDRLDF IKJKEYWD
         IKJNAME  'RLDFIX'
         IKJNAME  'NORLDFIX'
LKEDRMOD IKJKEYWD
         IKJNAME  'RMODE24'
         IKJNAME  'RMODEANY'
LKEDAMOD IKJKEYWD
         IKJNAME  'AMODE24'
         IKJNAME  'AMODE31'
         IKJNAME  'AMODEANY'
INFOALIA IKJKEYWD
         IKJNAME  'A',ALIAS=('ALIASINFO')
         IKJNAME  'NOA',ALIAS=('NOALIASINFO')
INFOLKED IKJKEYWD
         IKJNAME  'LKEDDATE'
         IKJNAME  'NOLKEDDATE'
         IKJENDP
         SPACE 2
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF350
PCLBROWS IKJPARM DSECT=PDLBROWS
PCLBR1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO BE BROWSED',VALIDCK=VERMEMBR, $
               HELP=('NAME OF THE MEMBER FOR THE SPF BROWSE COMMAND')
         IKJENDP
.NSPF350 ANOP
         SPACE 2
PCLCONT  IKJPARM DSECT=PDLCONT
PALINCON IKJKEYWD
         IKJNAME  'ALIASINFO'
         IKJNAME  'NOALIASINFO'
PALUPCON IKJKEYWD
         IKJNAME  'UPDATEALIAS'
         IKJNAME  'NOUPDATEALIAS'
PLKEDCON IKJKEYWD
         IKJNAME  'LKEDDATE'
         IKJNAME  'NOLKEDDATE'
PPROMCON IKJKEYWD
         IKJNAME  'PROMPT'
         IKJNAME  'NOPROMPT'
PRECVCON IKJKEYWD
         IKJNAME  'RECOVER'
         IKJNAME  'NORECOVER'
PDOUBCON IKJKEYWD
         IKJNAME  'DOUBLE'
         IKJNAME  'NODOUBLE'
PIMMCON  IKJKEYWD
         IKJNAME  'CPULOOP'
         IKJNAME  'ABEND'
         IKJNAME  'OUTLOOP'
         IKJNAME  'IOSTATS'
         IKJENDP
         SPACE 2
PCLDELET IKJPARM DSECT=PDLDELET
PCLS1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO BE DELETED',VALIDCK=VERMEMBR, $
               HELP=('NAME OF THE MEMBER TO BE SCRATCHED')
PCLDELAL IKJKEYWD
         IKJNAME  'ALIAS'
         IKJNAME  'NOALIAS'
         IKJENDP
         SPACE 2
PCLDIR   IKJPARM DSECT=PDLDIR
PCLDIR1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',VALIDCK=VERMEMBR,HELP=('NAME OF $
               THE MEMBER WHOSE DIRECTORY ENTRY IS TO BE DUMPED')
         IKJENDP
         SPACE 2
PCLDSPLY IKJPARM DSECT=PDL1MEM
PCLD1    IKJIDENT 'MEMBER NAME',MAXLNTH=39,                            $
               FIRST=ANY,OTHER=ANY,VALIDCK=VERMEMBR
PCLD2    IKJIDENT 'MEMBER NAME',MAXLNTH=19,                            $
               FIRST=ANY,OTHER=ANY,VALIDCK=VERMEMBR
         IKJENDP
         SPACE 2
PCLEDIT  IKJPARM DSECT=PDLEDIT
PCLEDIT1 IKJIDENT 'MEMBER NAME',MAXLNTH=39,FIRST=ANY,OTHER=ANY,        $
               PROMPT='NAME OF MEMBER TO EDIT',VALIDCK=VERMEMBR,       $
               HELP=('NAME OF THE MEMBER FOR THE EDIT COMMAND')
PCLNON   IKJKEYWD
         IKJNAME  'NONUM'
PCLASIS  IKJKEYWD
         IKJNAME  'ASIS'
PCLEDOP  IKJKEYWD
         IKJNAME  'NEW'
         IKJNAME  'OLD'
         SPACE 2
**    THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH EDITTYPE BELOW
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
PCLTYPE  IKJKEYWD
         IKJNAME  'DATA'
** INSERT INSTALLATION-ADDED DATA SET TYPES HERE AND IN EDITTYPE BELOW
         IKJNAME  'LIST'        ADDED LOCALLY AS A EDIT DATA SET TYPE
         IKJNAME  'VSBASIC'     NEW VS BASIC
         IKJNAME  'BASIC'
         IKJNAME  'IPLI'
         IKJNAME  'TEXT'
         IKJNAME  'COBOL'
         IKJNAME  'ASM'
         IKJNAME  'CLIST'
         IKJNAME  'CNTL'
         IKJNAME  'PLI'          $  -- DATA TYPE .PLI
* END OF VALID DATA SET TYPES
         IKJNAME  'PLIF'         $        (DEFAULT EDIT TYPE IS PLI)
         IKJNAME  'FORTE'    *
         IKJNAME  'FORTG'    *
         IKJNAME  'FORTGI'   *  -- DATA TYPE .FORT
         IKJNAME  'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
         IKJNAME  'GOFORT'   *
         IKJENDP
         SPACE 2
**  THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH PCLTYPE ABOVE
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
EDITTYPE DC    CL7'DATA'
** INSERT INSTALLATION-ADDED EDIT DATA TYPES HERE AND IN PCLTYPE ABOVE
         DC    CL7'LIST'      ADDED LOCALLY AS A VALID DATA SET TYPE
         DC    CL7'VSBASIC'   NEW VS BASIC
         DC    CL7'BASIC'
         DC    CL7'IPLI'
         DC    CL7'TEXT'
         DC    CL7'COBOL'
         DC    CL7'ASM'
         DC    CL7'CLIST'
         DC    CL7'CNTL'
         DC    CL7'PLI'
* END OF VALID DATA SET TYPES
PLIF     DC    CL7'PLIF'
         DC    CL7'FORTE'    *
         DC    CL7'FORTG'    *
         DC    CL7'FORTGI'   *  -- DATA TYPE .FORT
         DC    CL7'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
GOFORT   DC    CL7'GOFORT'   *
         SPACE 2
PCLFIND  IKJPARM DSECT=PDLFIND
PCLFIND1 IKJIDENT 'MEMBER NAME',MAXLNTH=39,FIRST=ANY,OTHER=ANY,        $
               PROMPT='NAME OF MEMBER',VALIDCK=VERMEMBR,               $
               HELP=('NAME OF THE MEMBER TO BE SEARCHED')
FINDDLM1 IKJPOSIT DELIMITER
FINDSTRG IKJPOSIT STRING
FINDFMT  IKJKEYWD
         IKJNAME  'NUM'                   FLAGSDD=X'00'
         IKJNAME  'NONUM'                 FLAGSDD=X'01'
         IKJNAME  'SNUM'                  FLAGSDD=X'02'
         IKJNAME  'LBLOCK'                FLAGSDD=X'04'
         IKJNAME  'LDUMP'                 FLAGSDD=X'08'
         IKJNAME  'BLOCK'                 FLAGSDD=X'10'
         IKJNAME  'DUMP'                  FLAGSDD=X'20'
FINDSEC  IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=FDUMPC
FINDOFFT IKJKEYWD
         IKJNAME  'OFFSET',SUBFLD=FINDOFFS
FINDLIST IKJKEYWD
         IKJNAME  'FIRST'
         IKJNAME  'NOFIRST'
FMAXLEN  IKJKEYWD
         IKJNAME  'MAXLEN',SUBFLD=FMAXLENS
FMAXIN   IKJKEYWD
         IKJNAME  'MAXIN',SUBFLD=FMAXINS
FMAXOUT  IKJKEYWD
         IKJNAME  'MAXOUT',SUBFLD=FMAXOUTS
FSKIPR   IKJKEYWD
         IKJNAME  'SKIPREC',SUBFLD=FSKIPRS
FSKIPC   IKJKEYWD
         IKJNAME  'SKIPCOL',SUBFLD=FSKIPCS
FTHENC   IKJKEYWD
         IKJNAME  'THEN',SUBFLD=FTHENCS
FELSEC   IKJKEYWD
         IKJNAME  'ELSE',SUBFLD=FELSECS
FDUMPC   IKJSUBF
FDUMPCS  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
FINDOFFS IKJSUBF
FINDOFF  IKJIDENT 'MODULE OFFSET',FIRST=ALPHANUM,OTHER=ALPHANUM,       $
               MAXLNTH=6,PROMPT='HEXADECIMAL OFFSET',HELP=('A ONE TO SI$
               X DIGIT HEXADECIMAL OFFSET INTO THE LOAD MODULE')
FMAXLENS IKJSUBF
FMAXLENC IKJIDENT 'MAXIMUM LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM LENGTH',MAXLNTH=5,                      $
               HELP=('MAXIMUM LENGTH OF EACH INPUT RECORD')
FMAXINS  IKJSUBF
FMAXINC  IKJIDENT 'MAXIMUM INPUT',FIRST=NUMERIC,OTHER=NUMERIC,         $
               PROMPT='MAXIMUM INPUT COUNT',MAXLNTH=7,                 $
               HELP=('MAXIMUM NUMBER OF RECORDS TO INPUT')
FMAXOUTS IKJSUBF
FMAXOUTC IKJIDENT 'MAXIMUM OUTPUT',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM OUTPUT RECORDS',MAXLNTH=7,              $
               HELP=('MAXIMUM NUMBER OF LINES OR PARTIAL RECORDS TO DIS$
               PLAY')
FSKIPRS  IKJSUBF
FSKIPRC  IKJIDENT 'SKIP COUNT',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='RECORDS TO SKIP',MAXLNTH=7,                     $
               HELP=('NUMBER OF INPUT RECORDS TO IGNORE')
FSKIPCS  IKJSUBF
FSKIPCC  IKJIDENT 'SKIP COLUMNS',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='COLUMNS TO SKIP',MAXLNTH=5,                     $
               HELP=('THE NUMBER OF BEGINNING COLUMNS TO IGNORE')
FTHENCS  IKJSUBF
FTHENCC  IKJKEYWD
         IKJNAME 'ATTRIB'
         IKJNAME 'DIRENTRY'
         IKJNAME 'DELETE'
         IKJNAME 'EDIT'
         IKJNAME 'END'
         IKJNAME 'FIND'
         IKJNAME 'FSE'
         IKJNAME 'HISTORY'
         IKJNAME 'LIST'
         IKJNAME 'MAP'
         IKJNAME 'MEMBERS'
         IKJNAME 'OUTCOPY'
         IKJNAME 'PRINTOFF'
         IKJNAME 'REVIEW'
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF400
         IKJNAME 'BROWSE'
         IKJNAME 'SPFEDIT'
.NSPF400 ANOP
         IKJNAME 'SUBMIT'
         IKJNAME 'TSOLIST'
         SPACE 1
FELSECS  IKJSUBF
FELSECC  IKJKEYWD
         IKJNAME 'ATTRIB'
         IKJNAME 'DIRENTRY'
         IKJNAME 'DELETE'
         IKJNAME 'EDIT'
         IKJNAME 'END'
         IKJNAME 'FIND'
         IKJNAME 'FSE'
         IKJNAME 'HISTORY'
         IKJNAME 'LIST'
         IKJNAME 'MAP'
         IKJNAME 'MEMBERS'
         IKJNAME 'OUTCOPY'
         IKJNAME 'PRINTOFF'
         IKJNAME 'REVIEW'
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF450
         IKJNAME 'BROWSE'
         IKJNAME 'SPFEDIT'
.NSPF450 ANOP
         IKJNAME 'SUBMIT'
         IKJNAME 'TSOLIST'
         IKJENDP
         SPACE 2
THENTBL  DC    A(0)                        NO ASSOCIATED THEN/ELSE
         DC    A(CMDTABLE)                 ATTRIB
         DC    A(DIRLIT)                   DIRENTRY
         DC    AL1(FSINGLE),AL3(DELLIT)    DELETE
         DC    A(EDITLIT)                  EDIT
         DC    A(ENDLIT)                   END
         DC    A(FINDLIT)                  FIND
         DC    A(FSELIT)                   FSE
         DC    A(HISTLIT)                  HISTORY
         DC    A(LISTLIT)                  LIST
         DC    A(MAPLIT)                   MAP
         DC    A(MEMBLIT)                  MEMBERS
         DC    A(OUTLIT)                   OUTCOPY
         DC    A(PRINTLIT)                 PRINTOFF
         DC    A(REVLIT)                   REVIEW
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF500
         DC    A(BROWSLIT)                 BROWSE
         DC    A(SPFLIT)                   SPFEDIT
.NSPF500 ANOP
         DC    A(SUBLIT)                   SUBMIT
         DC    A(TSOLLIT)                  TSOLIST
         SPACE 2
PCLFIX   IKJPARM DSECT=PDLFIX
PCLFIXOP IKJKEYWD
         IKJNAME  'DCB'
         IKJNAME  'EXPANDDIR',SUBFLD=RDIRZ
         IKJNAME  'RESETDIR',SUBFLD=RDIRZ
RTYPE    IKJKEYWD
         IKJNAME  'LOAD'
         IKJNAME  'OBJECT'
         IKJNAME  'CLIST'
         IKJNAME  'CNTL'
         IKJNAME  'ASM'
RLRECL   IKJKEYWD
         IKJNAME  'LRECL',SUBFLD=RLRECLZ
RBLKSI   IKJKEYWD
         IKJNAME  'BLKSIZE',SUBFLD=RBLKSIZ,                            $
               ALIAS=('BLOCKSIZE','B','BL')
RRECFM   IKJKEYWD
         IKJNAME  'RECFM',SUBFLD=RRECFMZ
RDIRZ    IKJSUBF
RDIRZI   IKJIDENT 'DIRECTORY BLOCKS',FIRST=NUMERIC,OTHER=NUMERIC,      $
               HELP=('NUMBER OF DIRECTORY BLOCKS'),MAXLNTH=5
RLRECLZ  IKJSUBF
RLRECLZI IKJIDENT 'LOGICAL RECORD LENGTH',FIRST=NUMERIC,OTHER=NUMERIC, $
               PROMPT='LOGICAL RECORD LENGTH',MAXLNTH=5,               $
               HELP=('DATA SET LOGICAL RECORD LENGTH DESIRED')
RBLKSIZ  IKJSUBF
RBLKSIZI IKJIDENT 'BLOCK SIZE',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='BLOCK SIZE',MAXLNTH=5,                          $
               HELP=('DATA SET BLOCKSIZE DESIRED')
RRECFMZ  IKJSUBF
RRECFMZI IKJKEYWD
         IKJNAME  'F'       X'80'     B'1000 0000'
         IKJNAME  'FB'      X'90'     B'1001 0000'
         IKJNAME  'FT'      X'A0'     B'1010 0000'
         IKJNAME  'FBT'     X'B0'     B'1011 0000'
         IKJNAME  'FA'      X'84'     B'1000 0100'
         IKJNAME  'FBA'     X'94'     B'1001 0100'
         IKJNAME  'FTA'     X'A4'     B'1010 0100'
         IKJNAME  'FBTA'    X'B4'     B'1011 0100'
         IKJNAME  'FM'      X'82'     B'1000 0010'
         IKJNAME  'FBM'     X'92'     B'1001 0010'
         IKJNAME  'FTM'     X'A2'     B'1010 0010'
         IKJNAME  'FBTM'    X'B2'     B'1011 0010'
         IKJNAME  'V'       X'40'     B'0100 0000'
         IKJNAME  'VB'      X'50'     B'0101 0000'
         IKJNAME  'VT'      X'60'     B'0110 0000'
         IKJNAME  'VBT'     X'70'     B'0111 0000'
         IKJNAME  'VA'      X'44'     B'0100 0100'
         IKJNAME  'VBA'     X'54'     B'0101 0100'
         IKJNAME  'VTA'     X'64'     B'0110 0100'
         IKJNAME  'VBTA'    X'74'     B'0111 0100'
         IKJNAME  'VM'      X'42'     B'0100 0010'
         IKJNAME  'VBM'     X'52'     B'0101 0010'
         IKJNAME  'VTM'     X'62'     B'0110 0010'
         IKJNAME  'VBTM'    X'72'     B'0111 0010'
         IKJNAME  'U'       X'C0'     B'1100 0000'
         IKJNAME  'UT'      X'E0'     B'1110 0000'
         IKJNAME  'UA'      X'C4'     B'1100 0100'
         IKJNAME  'UM'      X'C2'     B'1100 0010'
         IKJNAME  'UTA'     X'E4'     B'1110 0100'
         IKJNAME  'UTM'     X'E2'     B'1110 0010'
         IKJENDP
         SPACE 2
RECFMVAL DC    X'00',X'80',X'90',X'A0',X'B0',X'84',X'94',X'A4',X'B4'
         DC    X'82',X'92',X'A2',X'B2'  RECFM(F) THROUGH RECFM(FBTM)
         DC          X'40',X'50',X'60',X'70',X'44',X'54',X'64',X'74'
         DC    X'42',X'52',X'62',X'72'  RECFM(V) THROUGH RECFM(VBTM)
         DC          X'C0',X'E0',X'C4',X'C2',X'E4',X'E2'
         SPACE 2
PCLHIST  IKJPARM DSECT=PDLHIST
PCLH1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',VALIDCK=VERMEMBR,HELP=('NAME OF $
               THE LOAD MODULE WHOSE HISTORY IS TO BE LISTED')
PCLHTRNK IKJKEYWD
         IKJNAME  'TRAN',SUBFLD=PCLHTRNS
PCLHZAPK IKJKEYWD
         IKJNAME  'ZAP',SUBFLD=PCLHZAPS
PCLHUSRK IKJKEYWD
         IKJNAME  'USER',SUBFLD=PCLHUSRS
PCLHLKDK IKJKEYWD
         IKJNAME  'LKED',SUBFLD=PCLHLKDS
PCLHMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=PCLHMODS
PCLHMODS IKJSUBF
PCLHMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
PCLHUSRS IKJSUBF
PCLHUSR  IKJIDENT 'IDR USER DATA',FIRST=ANY,OTHER=ANY,MAXLNTH=8,       $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF IDR USER DATA')
PCLHLKDS IKJSUBF
PCLHLKD  IKJIDENT 'LINKAGE EDITOR',FIRST=ANY,OTHER=ANY,                $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A LINKA$
               GE EDITOR NAME')
PCLHZAPS IKJSUBF
PCLHZAP  IKJIDENT 'ZAP IDRDATA NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,    $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF ZAP IDRDATA')
PCLHTRNS IKJSUBF
PCLHTRN  IKJIDENT 'TRANSLATOR NAME',FIRST=ANY,OTHER=ANY,               $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A TRANS$
               LATOR (ASSEMBLER OR COMPILER) NAME')
         IKJENDP
         SPACE 2
PCLIFTTR IKJPARM DSECT=PDLIFTTR
PCLIFT1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER WHOSE ATTRIBUTES ARE TO BE CHE$
               CKED'),VALIDCK=VERMEMBR
IFLKREUS IKJKEYWD
         IKJNAME  'REUS'
         IKJNAME  'NOREUS'
IFLKREFR IKJKEYWD
         IKJNAME  'REFR'
         IKJNAME  'NOREFR'
IFLKRENT IKJKEYWD
         IKJNAME  'RENT'
         IKJNAME  'NORENT'
IFLKEXEC IKJKEYWD
         IKJNAME  'EXEC'
         IKJNAME  'NOEXEC'
IFLKLOAD IKJKEYWD
         IKJNAME  'LOADONLY'
         IKJNAME  'NOLOADONLY'
IFLKEDIT IKJKEYWD
         IKJNAME  'EDIT'
         IKJNAME  'NOEDIT'
IFLKAUTH IKJKEYWD
         IKJNAME  'AUTH'
         IKJNAME  'NOAUTH'
IFLKPAGE IKJKEYWD
         IKJNAME  'PAGE'
         IKJNAME  'NOPAGE'
IFLKOVER IKJKEYWD
         IKJNAME  'OVERLAY'
         IKJNAME  'NOOVERLAY'
IFLKTEST IKJKEYWD
         IKJNAME  'TEST'
         IKJNAME  'NOTEST'
IFRLD0   IKJKEYWD
         IKJNAME  'RLDZERO'
         IKJNAME  'NORLDZERO'
IFAPFERR IKJKEYWD
         IKJNAME  'APFERR'
         IKJNAME  'NOAPFERR'
IFRLDERR IKJKEYWD
         IKJNAME  'RLDERR'
         IKJNAME  'NORLDERR'
IFLKSCTR IKJKEYWD
         IKJNAME  'SCTR'
         IKJNAME  'NOSCTR'
IFLKLKED IKJKEYWD
         IKJNAME  'FLEVEL'
         IKJNAME  'NOFLEVEL'
IFLKVSLE IKJKEYWD
         IKJNAME  'VSLKED'
         IKJNAME  'NOVSLKED'
IFLKDC   IKJKEYWD
         IKJNAME  'DC'
         IKJNAME  'NODC'
IFLKSSI  IKJKEYWD
         IKJNAME  'SSI'
         IKJNAME  'NOSSI'
IFLKALIA IKJKEYWD
         IKJNAME  'ALIAS'
         IKJNAME  'NOALIAS'
IFLOADER IKJKEYWD
         IKJNAME  'LOADERR'
         IKJNAME  'NOLOADERR'
IFLKEDER IKJKEYWD
         IKJNAME  'LKEDERR'
         IKJNAME  'NOLKEDERR'
IFIOERR  IKJKEYWD
         IKJNAME  'IOERR'
         IKJNAME  'NOIOERR'
IFBLKERR IKJKEYWD
         IKJNAME  'BLOCKERR'
         IKJNAME  'NOBLOCKERR'
IFLRECLE IKJKEYWD
         IKJNAME  'LRECLERR'
         IKJNAME  'NOLRECLERR'
IFNULL   IKJKEYWD
         IKJNAME  'NULL'
         IKJNAME  'NONULL'
IFAALIAS IKJKEYWD
         IKJNAME  'APPARENTALIAS'
         IKJNAME  'NOAPPARENTALIAS'
IFORPHAN IKJKEYWD
         IKJNAME  'ORPHAN'
         IKJNAME  'NOORPHAN'
IFSPFEDT IKJKEYWD
         IKJNAME  'SPFEDIT'
         IKJNAME  'NOSPFEDIT'
IFNAMERR IKJKEYWD
         IKJNAME  'NAMEERR'
         IKJNAME  'NONAMEERR'
IFRLDF   IKJKEYWD
         IKJNAME  'RLDFIX'
         IKJNAME  'NORLDFIX'
IFRMODE  IKJKEYWD
         IKJNAME  'RMODE24'
         IKJNAME  'RMODEANY'
         IKJNAME  'NORMODE24'
         IKJNAME  'NORMODEANY'
IFAMODE  IKJKEYWD
         IKJNAME  'AMODE24'
         IKJNAME  'AMODE31'
         IKJNAME  'AMODEANY'
         IKJNAME  'NOAMODE24'
         IKJNAME  'NOAMODE31'
         IKJNAME  'NOAMODEANY'
IFTUSERK IKJKEYWD
         IKJNAME  'USERID',SUBFLD=IFTUSERS
         IKJNAME  'NOUSERID'
         IKJNAME  'NOTUSERID',SUBFLD=IFTUSERS
IFTHENC  IKJKEYWD
         IKJNAME  'THEN',SUBFLD=IFTHENCS
IFELSEC  IKJKEYWD
         IKJNAME  'ELSE',SUBFLD=IFELSECS
IFTLTRNK IKJKEYWD
         IKJNAME  'TRAN',SUBFLD=IFTLTRNS
IFTLZAPK IKJKEYWD
         IKJNAME  'ZAP',SUBFLD=IFTLZAPS
IFTLUSRK IKJKEYWD
         IKJNAME  'USER',SUBFLD=IFTLUSRS
IFTLMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=IFTLMODS
IFTLKEDK IKJKEYWD
         IKJNAME  'LKED',SUBFLD=IFTLKEDS
IFTTTRK  IKJKEYWD
         IKJNAME  'TTR',SUBFLD=IFTTTRS
IFABOKY  IKJKEYWD
         IKJNAME  'ABOVE',SUBFLD=IFABOSB
IFBELKY  IKJKEYWD
         IKJNAME  'BELOW',SUBFLD=IFBELSB
IFABOSB  IKJSUBF
IFABOSUB IKJIDENT 'MINIMUM SIZE',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='MINIMUM SIZE',MAXLNTH=9,                        $
               HELP=('MINIMUM SIZE OF A DESIRED MEMBER')
IFBELSB  IKJSUBF
IFBELSUB IKJIDENT 'MAXIMUM SIZE',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='MAXIMUM SIZE',MAXLNTH=9,                        $
               HELP=('MAXIMUM SIZE OF A DESIRED MEMBER')
IFTTTRS  IKJSUBF
IFTTTR   IKJIDENT 'TTR RANGE',RANGE,FIRST=ALPHANUM,OTHER=ALPHANUM,     $
               MAXLNTH=6,PROMPT='HEXADECIMAL TTR',HELP=('THE LOW:HIGH H$
               ADECIMAL TTR ADDRESS RANGE OF THE MEMBER ENTRIES')
IFTLKEDS IKJSUBF
IFTLKED  IKJIDENT 'LINKAGE EDITOR',FIRST=ANY,OTHER=ANY,                $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A LINKA$
               GE EDITOR NAME')
IFTLMODS IKJSUBF
IFTLMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
IFTLUSRS IKJSUBF
IFTLUSR  IKJIDENT 'IDR USER DATA',FIRST=ANY,OTHER=ANY,MAXLNTH=8,       $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF IDR USER DATA')
IFTLZAPS IKJSUBF
IFTLZAP  IKJIDENT 'ZAP IDRDATA NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=8,    $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF ZAP IDRDATA')
IFTLTRNS IKJSUBF
IFTLTRN  IKJIDENT 'TRANSLATOR NAME',FIRST=ANY,OTHER=ANY,               $
               MAXLNTH=10,HELP=('FIRST ONE TO TEN CHARACTERS OF A TRANS$
               LATOR (ASSEMBLER OR COMPILER) NAME')
IFTUSERS IKJSUBF
IFTUSER  IKJIDENT 'TSO USERID',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=7,   $
               HELP=('FIRST ONE TO SEVEN CHARACTERS OF A TSO USERID')
IFTHENCS IKJSUBF
IFTHENCC IKJKEYWD
         IKJNAME 'ATTRIB'
         IKJNAME 'DIRENTRY'
         IKJNAME 'DELETE'
         IKJNAME 'EDIT'
         IKJNAME 'END'
         IKJNAME 'FIND'
         IKJNAME 'FSE'
         IKJNAME 'HISTORY'
         IKJNAME 'LIST'
         IKJNAME 'MAP'
         IKJNAME 'MEMBERS'
         IKJNAME 'OUTCOPY'
         IKJNAME 'PRINTOFF'
         IKJNAME 'REVIEW'
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF550
         IKJNAME 'BROWSE'
         IKJNAME 'SPFEDIT'
.NSPF550 ANOP
         IKJNAME 'SUBMIT'
         IKJNAME 'TSOLIST'
         SPACE 1
IFELSECS IKJSUBF
IFELSECC IKJKEYWD
         IKJNAME 'ATTRIB'
         IKJNAME 'DIRENTRY'
         IKJNAME 'DELETE'
         IKJNAME 'EDIT'
         IKJNAME 'END'
         IKJNAME 'FIND'
         IKJNAME 'FSE'
         IKJNAME 'HISTORY'
         IKJNAME 'LIST'
         IKJNAME 'MAP'
         IKJNAME 'MEMBERS'
         IKJNAME 'OUTCOPY'
         IKJNAME 'PRINTOFF'
         IKJNAME 'REVIEW'
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF600
         IKJNAME 'BROWSE'
         IKJNAME 'SPFEDIT'
.NSPF600 ANOP
         IKJNAME 'SUBMIT'
         IKJNAME 'TSOLIST'
         IKJENDP
         SPACE 2
PCLLIST  IKJPARM DSECT=PDLLIST
PCLL1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',                                $
               HELP=('NAME OF THE MEMBER TO BE LISTED'),               $
               VALIDCK=VERMEMBR
LISTFMT  IKJKEYWD
         IKJNAME  'NUM'                   FLAGSDD=X'00'
         IKJNAME  'NONUM'                 FLAGSDD=X'01'
         IKJNAME  'SNUM'                  FLAGSDD=X'02'
         IKJNAME  'LBLOCK'                FLAGSDD=X'04'
         IKJNAME  'LDUMP'                 FLAGSDD=X'08'
         IKJNAME  'BLOCK'                 FLAGSDD=X'10'
         IKJNAME  'DUMP'                  FLAGSDD=X'20'
LISTSEC  IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=LDUMPC
LISTOFFT IKJKEYWD
         IKJNAME  'OFFSET',SUBFLD=LISTOFFS
LMAXLEN  IKJKEYWD
         IKJNAME  'MAXLEN',SUBFLD=LMAXLENS
LMAXIN   IKJKEYWD
         IKJNAME  'MAXIN',SUBFLD=LMAXINS
LMAXOUT  IKJKEYWD
         IKJNAME  'MAXOUT',SUBFLD=LMAXOUTS
LSKIPR   IKJKEYWD
         IKJNAME  'SKIPREC',SUBFLD=LSKIPRS
LSKIPC   IKJKEYWD
         IKJNAME  'SKIPCOL',SUBFLD=LSKIPCS
LDUMPC   IKJSUBF
LDUMPCS  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
LISTOFFS IKJSUBF
LISTOFF  IKJIDENT 'MODULE OFFSET',FIRST=ALPHANUM,OTHER=ALPHANUM,       $
               MAXLNTH=6,PROMPT='HEXADECIMAL OFFSET',HELP=('A ONE TO SI$
               X DIGIT HEXADECIMAL OFFSET INTO THE LOAD MODULE')
LMAXLENS IKJSUBF
LMAXLENC IKJIDENT 'MAXIMUM LENGTH',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM LENGTH',MAXLNTH=5,                      $
               HELP=('MAXIMUM LENGTH OF EACH INPUT RECORD')
LMAXINS  IKJSUBF
LMAXINC  IKJIDENT 'MAXIMUM INPUT',FIRST=NUMERIC,OTHER=NUMERIC,         $
               PROMPT='MAXIMUM INPUT COUNT',MAXLNTH=7,                 $
               HELP=('MAXIMUM NUMBER OF RECORDS TO INPUT')
LMAXOUTS IKJSUBF
LMAXOUTC IKJIDENT 'MAXIMUM OUTPUT',FIRST=NUMERIC,OTHER=NUMERIC,        $
               PROMPT='MAXIMUM OUTPUT RECORDS',MAXLNTH=7,              $
               HELP=('MAXIMUM NUMBER OF RECORDS TO DISPLAY')
LSKIPRS  IKJSUBF
LSKIPRC  IKJIDENT 'SKIP COUNT',FIRST=NUMERIC,OTHER=NUMERIC,            $
               PROMPT='RECORDS TO SKIP',MAXLNTH=7,                     $
               HELP=('NUMBER OF INPUT RECORDS TO IGNORE')
LSKIPCS  IKJSUBF
LSKIPCC  IKJIDENT 'SKIP COLUMNS',FIRST=NUMERIC,OTHER=NUMERIC,          $
               PROMPT='COLUMNS TO SKIP',MAXLNTH=5,                     $
               HELP=('THE NUMBER OF BEGINNING COLUMNS TO IGNORE')
         IKJENDP
         SPACE 2
PCLMAP   IKJPARM DSECT=PDLMAP
PCLM1    IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO BE MAPPED',                   $
               HELP=('NAME OF THE MEMBER WHOSE LOAD MODULE MAP IS TO BE$
                PRODUCED'),VALIDCK=VERMEMBR
PCLMAPOP IKJKEYWD
         IKJNAME  'FULL'
         IKJNAME  'SHORT'
         IKJNAME  'ENTRY'
PCLMMODK IKJKEYWD
         IKJNAME  'MODULE',SUBFLD=PCLMMODS
PCLMMODS IKJSUBF
PCLMMOD  IKJIDENT 'MODULE NAME',MAXLNTH=8,FIRST=ANY,OTHER=ANY,         $
               HELP=('FIRST ONE TO EIGHT CHARACTERS OF A MODULE NAME')
         IKJENDP
         SPACE 2
PCLMEMB  IKJPARM DSECT=PDLMEMB
PCLMEM   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='MEMBER GROUP',VALIDCK=VERMEMBR,HELP=('THE MEMBER$
                GROUP TO BE DISPLAYED')
         IKJENDP
         SPACE 2
PCLOUT   IKJPARM DSECT=PDLOUT
PCLOUT1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER',VALIDCK=VERMEMBR,HELP=('NAME OF $
               THE MEMBER TO OUTPUT TO FILE(PDSOUT)')
OUTCLOS  IKJKEYWD
         IKJNAME  'CLOSE'
         IKJNAME  'NOCLOSE'
OUTECHO  IKJKEYWD
         IKJNAME  'ECHO'
         IKJNAME  'NOECHO'
OUTSAVE  IKJKEYWD
         IKJNAME  'RECORD'
         IKJNAME  'NORECORD'
         IKJENDP
         SPACE 2
PCLPATTR IKJPARM DSECT=PDL2MEM
PCLPAT1  IKJIDENT 'PATTERN NAME',MAXLNTH=39,VALIDCK=VERMEMBR,          $
               HELP=('PATTERN CHARACTERS FOR A DIRECTORY SEARCH'),     $
               FIRST=ANY,OTHER=ANY,PROMPT='PATTERN NAME'
PCLPAT2  IKJIDENT 'PATTERN NAME',MAXLNTH=19,VALIDCK=VERMEMBR,          $
               FIRST=ANY,OTHER=ANY
         IKJENDP
         SPACE 2
PCLPRINT IKJPARM DSECT=PDLPRINT
PCLPR1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO PRINT',VALIDCK=VERMEMBR,      $
               HELP=('NAME OF THE MEMBER FOR THE PRINTOFF COMMAND')
PCLPR2   IKJPOSIT SPACE
PCLPRSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLRENAM IKJPARM DSECT=PDLRENAM
PCLREN1  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='CURRENT MEMBER NAME',VALIDCK=VERMEMBR,          $
               HELP=('NAME OF THE MEMBER TO BE RENAMED')
PCLREN2  IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='NEW MEMBER NAME',VALIDCK=VERMEMBR,              $
               HELP=('A NEW NAME FOR THE MEMBER')
PCLRENSW IKJKEYWD
         IKJNAME  'SWAP'
         IKJNAME  'NOSWAP'
PCLRENGP IKJKEYWD
         IKJNAME  'GROUP'
         IKJNAME  'NOGROUP'
         IKJENDP
         SPACE 2
PCLRESTO IKJPARM DSECT=PDLRESTO
PCLRS1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=19,        $
               PROMPT='NAME OF MEMBER TO RESTORE',VALIDCK=VERMEMBR,    $
               HELP=('NAME OF THE MEMBER TO BE RESURRECTED')
PCLRS2   IKJIDENT 'TTR ADDRESS',FIRST=ALPHANUM,OTHER=ALPHANUM,         $
               PROMPT='HEXADECIMAL TTR',MAXLNTH=6,VALIDCK=VERMEMBR,    $
               HELP=('THE START ADDRESS OF THE MEMBER TO BE RESTORED')
PCLRSREP IKJKEYWD
         IKJNAME  'REPEAT'
         IKJNAME  'NOREPEAT'
PCLRSDIS IKJKEYWD
         IKJNAME  'DISPLAY'
         IKJNAME  'NODISPLAY'
PCLRSPRO IKJKEYWD
         IKJNAME  'PROMPT'
         IKJNAME  'NOPROMPT'
PCLRESTK IKJKEYWD
         IKJNAME  'LIKE',SUBFLD=PCLRESTS
PCLRESTS IKJSUBF
PCLREST  IKJIDENT 'MEMBER NAME',FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=8,  $
               PROMPT='MEMBER NAME',HELP=('NAME OF A MODEL MEMBER')
         IKJENDP
         SPACE 2
PCLREVIE IKJPARM DSECT=PDLREVIE
PCLRE1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO REVIEW',VALIDCK=VERMEMBR,     $
               HELP=('NAME OF THE MEMBER FOR THE REVIEW COMMAND')
PCLRE2   IKJPOSIT SPACE
PCLRESTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF650
PCLSPFED IKJPARM DSECT=PDLSPFED
PCLSP1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO BE EDITED',VALIDCK=VERMEMBR,  $
               HELP=('NAME OF THE MEMBER FOR THE SPF EDIT COMMAND')
SPFNEWOP IKJKEYWD
         IKJNAME  'NEW'
         IKJNAME  'OLD'
         IKJENDP
.NSPF650 ANOP
         SPACE 2
PCLSUBMI IKJPARM DSECT=PDLSUBMI
PCLSU1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO SUBMIT',VALIDCK=VERMEMBR,     $
               HELP=('NAME OF THE MEMBER FOR THE SUBMIT COMMAND')
PCLSU2   IKJPOSIT SPACE
PCLSUSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLTSOLI IKJPARM DSECT=PDLTSOLI
PCLTL1   IKJIDENT 'MEMBER NAME',FIRST=ANY,OTHER=ANY,MAXLNTH=39,        $
               PROMPT='NAME OF MEMBER TO LIST',VALIDCK=VERMEMBR,       $
               HELP=('NAME OF THE MEMBER FOR THE TSO LIST COMMAND')
PCLTL2   IKJPOSIT SPACE
PCLTLSTR IKJPOSIT STRING
         IKJENDP
         SPACE 2
PCLVERIF IKJPARM DSECT=PDLVERIF
VERLOAD  IKJKEYWD
         IKJNAME  'LOAD'
         IKJNAME  'NOLOAD'
VERINPUT IKJKEYWD
         IKJNAME  'INPUT',ALIAS=('READ')
         IKJNAME  'NOINPUT',ALIAS=('NOREAD')
VERNAME  IKJKEYWD
         IKJNAME  'NAME'
         IKJNAME  'NONAME'
VERUPDT  IKJKEYWD
         IKJNAME  'UPDATE'
         IKJNAME  'NOUPDATE'
         IKJENDP
         TITLE 'P D S  --  PDS CONSTANTS                      02/06/84'
FF       EQU   X'FF'        IMMEDIATE FLAG MASK (FOR NI INSTRUCTIONS)
         SPACE 2
DEBNMEXT EQU   16           OFFSET IN DEB FOR NUMBER OF EXTENTS
DEBUCBAD EQU   32           OFFSET IN DEB FOR UCB ADDRESS
UCBTYP   EQU   16           OFFSET IN UCB FOR UNIT TYPE WORD
UCBRR    EQU   X'20'        UCB IS ON A SHARED DASDI DEVICE
UCBVOLI  EQU   28           OFFSET IN UCB FOR DISK VOLUME NAME
         SPACE 3
EXCPDCB  DCB   DEVD=DA,DDNAME=X,MACRF=E,EXLST=EXLST
LEXCPDCB EQU   *-EXCPDCB
SAMDCB   DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
LSAMDCB  EQU   *-SAMDCB
DIRDCB   DCB   DSORG=PS,RECFM=FB,LRECL=256,BLKSIZE=256,DDNAME=X,       X
               MACRF=(W),KEYLEN=8,DEVD=DA
LDIRDCB  EQU   *-DIRDCB
EXLST    DC    X'85',AL3(OPENEXIT)
         PRINT GEN
         SPACE 2
BLDLPARM DC    AL2(1,74)
DUMPDIR  DC    C'DUMP DIRECTORY ENTRY   '
RECFMTYP DC    C'*FV?U'                   RECFM=. TEXT
DSORGTYP DC    C'**DAPS??IS'              DSORG=.. TEXT
         SPACE 2
DAYMONTH DC    AL1(31,28,31,30,31,30,31,31,30,31,30,31)
TRTABLE  EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
         SPACE 2
SYSHELP  DC    CL8'SYSHELP'               FOR DDNAME FREE IN HELP
SYSPROC  DC    CL8'SYSPROC'               FOR NO DDNAME FREE IN DEALLOC
SPFUSER  DC    CL8'SPFUSER'               FOR SPF OPTION 6 ENQ TEST
SPFEDIT  DC    CL8'SPFEDIT'               FOR SPF MEMBER ENQ TEST
SPFDSN   DC    CL8'SPFDSN'                FOR SPF DATA SET RESERVE
SYSIEWLP DC    CL8'SYSIEWLP'              FOR LINKAGE-EDITOR RESERVE
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF700
         AIF ('&SPFGEN' EQ 'ISPF').FORISPF
ISPF     DC    C'ISPF    PGM(12345678)    PARM(++12345678)             X
                    '      COULD ZAP TO CHANGE "NEWAPPL(ISR)" OR "TEST"
         AGO   .FORBOTH
.FORISPF ANOP
ISPF     DC    C'ISPF    PGM(12345678)    PARM(++12345678) NEWAPPL(ISR)X
                    '      COULD ZAP TO CHANGE "NEWAPPL(ISR)" OR "TEST"
.FORBOTH ANOP
ISPFPGM  EQU   12,8                       DISPLACEMENT TO PROGRAM NAME
ISPFPARM EQU   32,8                       DISPLACEMENT TO PARM PASSED
.NSPF700 ANOP
         EJECT
WORKSIZE DC    0F'0',AL1(1),AL3(LENWORK)  SUBPOOL, DYNAMIC STORAGE SIZE
BLANKS   DC    0F'0',8X'40'
         SPACE 2
ISUBS    DC    30F'0'           INITIAL PDL AREA
         ORG   *-16
ZERO     DC    F'0'
         DC    F'99999'         INITIALIZE #MAXLEN
         DC    F'9999999'       INITIALIZE #MAXIN
         DC    F'9999999'       INITIALIZE #MAXOUT
LISUBS   EQU   *-ISUBS          INITIALIZATION LENGTH
         SPACE 2
RTABLE   DC  XL2'C0',AL2(0)      ...LOAD
******           ***** *****  ****  ****  ****  ****  **** *****  ****
******           23051 23052  2314  3330  3340  3350  3375 33301  3380
******           ***** *****  ****  ****  ****  ****  **** *****  ****
         DC  AL2(14136,14660,+7294,13030,+8368,19069,17600,13030,23476)
         DC  XL2'90',AL2(80)     ...OBJECT
         DC  AL2(+3200,+2720,+2240,+3120,+2640,+2960,+3200,+3120,+3120)
         DC  XL2'50',AL2(255)    ...CLIST
         DC  AL2(+4424,+4754,+2296,+4252,+1964,+6232,11616,+4252,15476)
         DC  XL2'90',AL2(80)     ...CNTL
         DC  AL2(+6800,+7200,+3520,+6400,+4080,+9440,17600,+6400,23440)
         DC  XL2'90',AL2(80)     ...ASM
         DC  AL2(+6800,+7200,+3520,+6400,+4080,+9440,17600,+6400,23440)
         SPACE 2
*  NOTE - THESE CCWS ARE USED FOR ALL READS
CCWINIT  CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
         CCW   X'08',*-8-(*-8),X'60',1          TIC
         CCW   X'92',IOBSEEK-IOBSEEK+3,X'20',8  MT READ NEXT COUNT
*
         CCW   X'23',SNO-SNO,X'60',1            SET SECTOR
         CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
         CCW   X'08',*-8-(*-8),X'60',1          TIC
         CCW   X'06',*-*,X'60',32760            READ DATA
         CCW   X'92',IOBSEEK-IOBSEEK+3,X'60',8  MT READ NEXT COUNT
         CCW   X'22',SNO-SNO,X'20',1            READ SECTOR NUMBER
         SPACE 2
KEYDATA  DC    XL8'FFFFFFFFFFFFFFFF',XL2'000E',XL8'FFFFFFFFFFFFFFFF'
         SPACE 3
UNITTBL  DC    CL7'????,  ',CL7'????,  ',CL7'????,  ',CL7'????,  '
         DC    CL7'????,  ',CL7'????,  ',CL7'2305-1,',CL7'2305-2,'
         DC    CL7'2314,  ',CL7'3330,  ',CL7'3340,  ',CL7'3350,  '
         DC    CL7'3375,  ',CL7'3330-1,',CL7'3380,  ',CL7'????,  '
         EJECT
OBTAIN   CAMLST SEARCH,0,0,0
         ORG   OBTAIN+4
         SPACE 3
ENQSTEP  ENQ   (*-*,*-*,E,7,STEP),RET=TEST,MF=L
         ORG   ENQSTEP+4
         SPACE 3
ENQTEST  ENQ   (*-*,*-*,E,*-*,SYSTEMS),RET=TEST,MF=L
         ORG   ENQTEST+4
         SPACE 3
ENQHAVE  ENQ   (*-*,*-*,E,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   ENQHAVE+4
         SPACE 7
RESERVEL RESERVE (*-*,*-*,E,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   RESERVEL+4
         SPACE 3
DEQSYSS  DEQ   (*-*,*-*,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   DEQSYSS+4
         SPACE 3
DEQSYS   DEQ   (*-*,*-*,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   DEQSYS+4
         SPACE 3
STAELIST ESTAE *-*,PARAM=*-*,MF=L
         ORG   STAELIST+4
         EJECT
***BITS FOR DIRATTR+0:
ATTRRENT EQU   X'80'          REENTRANT
ATTRREUS EQU   X'40'          REUSABLE
ATTROVLY EQU   X'20'          OVERLAY STRUCTURE
ATTRTEST EQU   X'10'          TEST SYMBOLS ARE AVAILABLE
ATTRLOAD EQU   X'08'          ONLY LOADABLE
ATTRSCTR EQU   X'04'          SCATTER LOADED
ATTREXEC EQU   X'02'          EXECUTABLE
ATTR1TXT EQU   X'01'          ONE TEXT RECORD ONLY AND NO RLD ITEMS
         SPACE
***BITS FOR DIRATTR+1:
ATTRNODC EQU   X'80'          NOT DOWNWARD-COMPATABLE
ATTRZORG EQU   X'40'          FIRST TEXT BLOCK HAS A ZERO ORIGIN
ATTREP0  EQU   X'20'          ENTRY POINT IS AT OFFSET ZERO
ATTRNRLD EQU   X'10'          NO RLD ITEMS
ATTRNE   EQU   X'08'          CANNOT BE REPROCESSED BY LINKAGE-EDITOR
ATTRSYMS EQU   X'04'          CONTAINS TEST SYMBOLS
ATTRFLEV EQU   X'02'          CREATED BY F LEVEL LINKAGE-EDITOR
ATTRREFR EQU   X'01'          REFRESHABLE
         SPACE 3
TBLATTR  DS    0H                                    PDS PROCESSING
*                                                    ==============
         DC    AL1(ATTROVLY,0,00006),CL9'OVERLAY'       LIST ONLY
         DC    AL1(ATTRTEST,0,00003),CL9'TEST'          LIST ONLY
         DC    AL1(0,ATTRFLEV,128+6),CL9'E-LEVEL'       LIST ONLY
SCTRTXT  DC    AL1(ATTRSCTR,0,00003),CL9'SCTR'          LIST ONLY
RENTTXT  DC    AL1(ATTRRENT,0,00003),CL9'RENT'       CHANGE AND LIST
REUSTXT  DC    AL1(ATTRREUS,0,00003),CL9'REUS'       CHANGE AND LIST
REFRTXT  DC    AL1(0,ATTRREFR,00003),CL9'REFR'       CHANGE AND LIST
         DC    AL1(ATTRLOAD,0,00008),CL9'LOAD ONLY'  CHANGE AND LIST
         DC    AL1(0,ATTRNE+0,00007),CL9'NOT EDIT'   CHANGE AND LIST
         DC    AL1(ATTREXEC,0,128+7),CL9'NOT EXEC'   CHANGE AND LIST
         DC    AL1(0,ATTRNODC,128+1),CL9'DC'         CHANGE AND LIST
         DC    X'FFFF'
         SPACE 3
MODULEN  DC    AL2(CVTPUTL-CVTMAP),CL8'IKJPUTL '
         DC    AL2(CVTPARS-CVTMAP),CL8'IKJPARS '
         DC    AL2(CVTPTGT-CVTMAP),CL8'IKJPTGT '
         DC    AL2(CVTSCAN-CVTMAP),CL8'IKJSCAN '
         DC    AL2(CVTDAIR-CVTMAP),CL8'IKJDAIR '
         DC    AL2(CVTSTCK-CVTMAP),CL8'IKJSTCK '
         DC    AL2(CVTEHDEF-CVTMAP),CL8'IKJEHDEF'
         DC    AL2(CVTEFF02-CVTMAP),CL8'IKJEFF02'
         DC    AL2(CVTPCNVT-CVTMAP),CL8'CVTPCNVT'
         DC    AL2(CVTPRLTV-CVTMAP),CL8'CVTPRLTV'
         DC    X'FFFF'
         PRINT NOGEN
         TITLE 'P D S  --  PDS SUBCOMMAND TABLE               02/06/84'
@Q       EQU   X'80'     QUIET MODE FOR SUBCOMMAND
@I       EQU   X'40'+@Q  SPFEDIT OR BROWSE ALLOW COLON NOTATION
@N       EQU   X'20'     OPERAND IS IGNORED IF PRESENT
@R       EQU   X'10'     OPERAND REQUIRED, PARSE IS ALWAYS CALLED
@S       EQU   X'08'     SUBCOMMAND WORKS FOR NON-PARTITIONED DATA SETS
@G       EQU   X'04'     MEMBER GROUP AND MEMBER DEFAULT ALLOWED
@B       EQU   X'02'     BLDL REQUIRED TO VERIFY FIRST MEMBER EXISTS
@C       EQU   X'01'     SUBCOMMAND INVALIDATES BLDL; PATTERN/DISPLAY
         SPACE 1
** LABEL      SUBCOMMAND,PF,ROUTINE ,  FLAGS    ,  PCL   , POST-PARSE
********      ********** ** ******** *********** ******** ***********
CMDTABLE PRSTBL   ATTRIB,07,++ATTRIB,00+@Q+@G+@B,+PCLATTR,@Q,ZATTRIB
         PRSTBL    ALIAS,00,+++ALIAS,00+00+@R+@B,PCLALIAS,00,00000000
         AIF ('&SPFGEN' EQ 'NO SPF').NSPF750
BROWSLIT PRSTBL   BROWSE,0B,++BROWSE,@S+@I+@G+@B,PCLBROWS,@Q,00000000
SPFLIT   PRSTBL  SPFEDIT,0C,++BROWSE,@S+@I+@G+@C,PCLSPFED,@Q,ZSPFEDIT
.NSPF750 ANOP
MAINPCL  PRSTBL   CHANGE,06,++CHANGE,@S+00+00+@C,+PCLMAIN,00,ZMAINPDL
         PRSTBL  CONTROL,00,+CONTROL,@S+00+00+@C,+PCLCONT,00,ZCONTROL
DISPLIT  PRSTBL  DISPLAY,00,+DISPLAY,00+00+00+00,PCLDSPLY,@C,00000000
DIRLIT   PRSTBL DIRENTRY,00,DIRENTRY,00+00+@G+@B,++PCLDIR,00,00000000
DELLIT   PRSTBL   DELETE,00,++DELETE,00+@Q+@G+@B,PCLDELET,00,ZDELETE
         PRSTBL  DSNAMES,00,+++++DSN,@S+00+@N+00,00000000,00,00000000
EDITLIT  PRSTBL     EDIT,00,++++EDIT,@S+00+@G+@C,+PCLEDIT,00,ZEDIT
ENDLIT   PRSTBL      END,03,++RETURN,@S+00+@N+00,00000000,00,00000000
EXECLIT  PRSTBL     EXEC,00,++++EXEC,@S+00+@N+00,00000000,00,00000000
FINDLIT  PRSTBL     FIND,05,++++FIND,@S+@Q+@G+@B,+PCLFIND,00,ZFIND
FIXLIT   PRSTBL FIXPDS**,00,++FIXPDS,00+00+00+@C,0+PCLFIX,00,ZFIXPDS
FSELIT   PRSTBL      FSE,00,++++EDIT,@S+@Q+@G+@C,+PCLEDIT,@Q,ZEDIT
         PRSTBL    KLEAR,00,+++KLEAR,@S+00+@N+00,00000000,00,00000000
HELPLIT  PRSTBL     HELP,01,++++HELP,@S+00+@N+00,00000000,00,00000000
HISTLIT  PRSTBL  HISTORY,00,+HISTORY,00+@Q+@G+@B,+PCLHIST,00,ZHISTORY
         PRSTBL       IF,05,++++++IF,00+@Q+@G+@B,PCLIFTTR,00,ZIF
LISTLIT  PRSTBL     LIST,02,++++LIST,@S+00+@G+@B,+PCLLIST,00,ZLIST
MAPLIT   PRSTBL      MAP,08,+++++MAP,00+@Q+@G+@B,++PCLMAP,00,ZMAP
MEMBLIT  PRSTBL  MEMBERS,0A,++NEWCMD,00+00+@G+@B,+PCLMEMB,00,00000000
OPTLIT   PRSTBL  OPTIONS,00,+OPTIONS,@S+00+@N+00,00000000,00,00000000
OUTLIT   PRSTBL  OUTCOPY,00,+OUTCOPY,00+@Q+@G+@B,++PCLOUT,00,ZOUTCOPY
PATTLIT  PRSTBL  PATTERN,00,+PATTERN,00+00+00+00,PCLPATTR,@C,00000000
         PRSTBL  PDSNEW2,00,++CHANGE,@S+00+00+@C,+PCLMAIN,00,ZMAINPDL
PRINTLIT PRSTBL PRINTOFF,04,PRINTOFF,@S+@Q+@G+@B,PCLPRINT,@Q,ZPRINTOF
RECALLIT PRSTBL   RECALL,00,00000000,@S+00+@N+00,00000000,00,00000000
         PRSTBL   RENAME,00,++RENAME,00+00+@R+@B,PCLRENAM,00,ZRENAME
RESTOLIT PRSTBL  RESTORE,00,+RESTORE,00+00+@R+@C,PCLRESTO,00,ZRESTORE
REVLIT   PRSTBL   REVIEW,00,PRINTOFF,@S+@Q+@G+@B,PCLREVIE,@Q,ZREVIEW
SUBLIT   PRSTBL   SUBMIT,00,PRINTOFF,@S+00+@G+@B,PCLSUBMI,00,ZSUBMIT
         PRSTBL  SCRATCH,00,++DELETE,00+@Q+@G+@B,PCLDELET,00,ZDELETE
         PRSTBL      TSO,00,+++++TSO,@S+00+@N+@C,00000000,00,00000000
TSOLLIT  PRSTBL  TSOLIST,00,PRINTOFF,@S+00+@G+@B,PCLTSOLI,00,ZTSOLIST
         PRSTBL    USAGE,09,+++USAGE,@S+00+@N+00,00000000,00,00000000
         PRSTBL   VERIFY,00,++VERIFY,@S+00+00+@C,PCLVERIF,00,ZVERIFY
         PRSTBL        W,00,++NEWCMD,@S+00+@N+00,00000000,00,00000000
         DC X'FF'
         TITLE 'P D S  --  PDS PROGRAM MESSAGES               02/06/84'
MSGPDS61 MSG   'PDS45678 --  VERSION 6.1    '
MSGBLANK MSG   '  '
MSGNDSCB MSG   'OBTAIN ERROR'
MSGIODIR MSG   'I/O ERROR IN DIRECTORY'
MSGMUSER MSG   '(MEMBER) IS BEING UPDATED BY ANOTHER USER'
MSGNLIB  MSG   'NOTE: THIS DATA SET IS NOT PARTITIONED'
MSGNLOAD MSG   'DATA SET IS NOT A LOAD LIBRARY'
MSGILOAD MSG   'DATA SET IS A LOAD LIBRARY'
MSGNOPEN MSG   'UNABLE TO OPEN DATA SET'
MSGEOF   MSG   'END OF FILE'
         SPACE 1
         DS    0X
MSGEOFDS MSG   'END OF DATA SET'
MSGNODIR MSG   'THE DATA SET DIRECTORY IS FULL (MEMBERS CANNOT BE ADDEDX
               )'
MSGEMPTY MSG   'NO MEMBERS EXIST'
MSGRANGE MSG   'INVALID NAME RANGE'
MSGNODSP MSG   'NO MEMBER NAMES ARE IN THIS RANGE'
MSGNOPAT MSG   'NO MEMBER NAMES MATCH THIS PATTERN'
MSGCHRBD MSG   'SEARCH STRING IS TOO LONG'
MSGEXIST MSG   '(MEMBER) ALREADY EXISTS'
MSGNONE  MSG   '(MEMBER) NOT FOUND'
MSGNAME  MSG   '(MEMBER) IS AN INVALID MEMBER NAME'
         SPACE 1
         DS    0X
MSGRLDCN MSG   'DIRECTORY RLD/CONTROL COUNT DOES NOT MATCH THE FIRST RLX
               D ENTRY'
MSGREUSC MSG   'REUSABLE ATTRIBUTE CONFLICTS WITH SCATTER'
MSGTSTNE MSG   'TEST ATTRIBUTE CONFLICTS WITH NOT EDIT'
MSGRENTR MSG   'REENTRANT ATTRIBUTE CONFLICTS WITH NOT REUSABLE'
MSGNOESD MSG   '**MODULE HAS NO EXTERNAL SYMBOLS'
MSGNOESM MSG   'MODULE HAS NO MATCHING EXTERNAL SYMBOLS'
MSGNOADD MSG   'DUMMY MEMBER ADD FAILED, RC=8'
MSGNODEL MSG   'DUMMY MEMBER DELETE FAILED, RC=8'
MSGRESTR MSG   ' REENTER FIND STRING WITH DELIMITERS:'
MSGENTER MSG   ' ENTER OPTION -- DSN=1234567890123456789012345678901234X
               5678901234                '
MSGRETRY MSG   ' RE-ENTER DATA SET NAME, VOLUME AND DISPOSITION:'
         SPACE 1
         DS    0X
MSGSPFHD MSG   ' SPF STATS:    VER.MOD  CREATED   LAST MODIFIED  SIZE  X
               INIT   MOD   ID     '
MSGTTRHI MSG   'WARNING: THIS MEMBER IS AFTER THE DATA SET END-OF-FILE'
MSGTOBIG MSG  'WARNING: TTR IS BEYOND THE USED PORTION OF THE DATA SET'
MSGINVLD MSG   'INVALID SUBCOMMAND'
MSGLOOP  MSG   'WARNING: POSSIBLE PROGRAM LOOP'
MSGTSOXX MSG   'TSO COMMAND NOT FOUND'
MSGMEMGP MSG   '** SUBCOMND (MEMBER)                                   X
                            '
         DS    0X
MSGDIVER MSG   'NOTE: BLOCKSIZE SHOULD BE EVENLY DIVISIBLE BY LRECL'
MSGTTRUP MSG   'WARNING: A TTR FOR THIS MEMBER WAS NOT FOUND'
MSGABE   MSG   '*** ABEND S123 U1234 AT +123456 IN PROGRAM 12345678'
MSGABESY EQU   14                SYSTEM ERROR CODE
MSGABEUS EQU   MSGABESY+6        USER ERROR CODE
MSGABETX EQU   MSGABESY+6+5      =CL32'LOADING THIS MODULE'
MSGABEAT EQU   MSGABEUS+9        OFFSET TO ERROR
MSGABEPG EQU   MSGABEAT+18       PROGRAM NAME
         SPACE 1
         DS    0X
MSGDIRXT MSG   'NOTE: PDS DIRECTORIES MUST BE CONTAINED IN ONE EXTENT'
MSGPERM# MSG   '*** INITIALIZATION READ FAILED'
MSGPERMI MSG   '*** PERMANENT I/O ERROR -- TTR=123456'
MSGPERMT EQU   35
MSGNOTDR MSG   'WARNING: THIS DATA SET HAS NO DIRECTORY BLOCKS'
MSGNO256 MSG   'WARNING: A DIRECTORY RECORD HAS AN INCORRECT LENGTH (NOX
               T 256 CHARACTERS)'
MSGTTRN1 MSG   ' REENTER THE FIRST TTR:'
MSGTTRN2 MSG   ' REENTER THE SECOND TTR:'
MSGLISTO MSG   ' REENTER THE HEX OFFSET:'
         SPACE 1
         DS    0X
MSGRESAD MSG   'RESIDENCE MODE ANY AND ADDRESSING MODE 123 ARE INCOMPATX
               IBLE'
MSGRESAM EQU   43
MSGOVCON MSG   'OVERLAY ATTRIBUTE CONFLICTS WITH RENT     '
MSGOVCOD EQU   37
MSGNOMOD MSG   'MEMBER IS AN ALIAS BUT NO REAL MEMBER EXISTS'
MSGALIUP MSG   'ALIAS UPDATED: (MEMBER)'
MSGALIUN EQU   19
MSGREAL  MSG   'MEMBER IS AN ALIAS FOR (MEMBER)'
MSGREAL1 EQU   MSGREAL+27,8
MSGAPPAR MSG   'NOTE: (MEMBER) IS AN APPARENT ALIAS FOR THIS MEMBER'
MSGAPPMB EQU   MSGAPPAR+10,8
MSGXREAL MSG   'THE ALIAS DIRECTORY ENTRY NOTES THE REAL ENTRY NAME AS X
               (MEMBER)'
MSGXREA1 EQU   MSGXREAL+59,8
         SPACE 2
MSGDAIRX MSG   'ERROR IN DAIRFAIL SERVICE ROUTINE;  R15=123'
         ORG   MSGDAIRX+43
MSGDAIR1 DC    X'40202120'
MSGGENF  MSG   'ERROR IN GENERAL FAIL SERVICE ROUTINE;  R15=123'
         ORG   MSGGENF+47
MSGGENF1 DC    X'40202120'
MSGENTRY MSG   'ENTRY POINT AT 123456' -- ENTRYPOINTNAME
MSGENTR1 EQU   MSGENTRY+19,6
MSGLEN   MSG   'MODULE LENGTH  123456  -- 12345K'
MSGLEN1  EQU   MSGLEN+19,6
         ORG   MSGLEN+29
MSGLEN2  DC    X'402020202120D2'
MSGHISTD MSG   'LAST LINK-EDITED ON 12/34/56'
         ORG   MSGHISTD+23
DATEMASK DC    X'402120612020612020'
MSGDEFM  MSG   'DEFAULT MEMBER - 12345678'
MSGDEFM1 EQU   MSGDEFM+21,8
         PRINT GEN
         TITLE 'P D S  --  PDS LITERALS                       02/06/84'
         LTORG
         TITLE 'P D S  --  PDS TRANSLATE TABLES               02/06/84'
PATCH    B     PATCH
         DC    18H'0'
         SPACE 3
***  TRANSLATE AND TEST TABLE TO FIND INVALID MEMBER NAMES
TRTMEM   DC    256X'FF'           ASSUME ALL INVALID FIRST
         ORG   TRTMEM+C'#'
         DC    1X'00'             # IS OK
         ORG   TRTMEM+C'$'
         DC    1X'00'             $ IS OK
         ORG   TRTMEM+C'@'
         DC    1X'00'             @ IS OK
         ORG   TRTMEM+C'A'
         DC    9X'00'             ABCDEFGHI  ARE OK
         ORG   TRTMEM+C'J'
         DC    9X'00'             JKLMNOPQR  ARE OK
         ORG   TRTMEM+C'S'
         DC    8X'00'             STUVWXYZ  ARE OK
         ORG   TRTMEM+C'0'
         DC    10X'00'            0123456789  ARE OK
         ORG   ,
         SPACE 3
***  TRANSLATE TABLE TO CONVERT UNPRINTABLES TO PERIODS
TRLINE   DC    256C'.'            ASSUME ALL UNPRINTABLE FIRST
         ORG   TRLINE+C' '
         DC    1X'40'             BLANK IS OK
         ORG   TRLINE+C''
         DC    C'.<(+&&'        .<(+&  ARE OK
         ORG   TRLINE+C'!'
         DC    C'!$*);^-/'        !$*);^-/  ARE OK
         ORG   TRLINE+C','
         DC    C',%_>?'           ,%_>?  ARE OK
         ORG   TRLINE+C':'
         DC    C':#@''="'         :#@'="  ARE OK
         ORG   TRLINE+X'81'       LOWER-CASE ABCDEFGHI
         DC    X'818283848586878889'
         ORG   TRLINE+X'91'       LOWER-CASE JKLMONPQR
         DC    X'919293949596979899'
         ORG   TRLINE+X'A2'       LOWER-CASE STUVWXYZ
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   TRLINE+C'A'
         DC    C'ABCDEFGHI'       ABCDEFGHI  ARE OK
         ORG   TRLINE+C'J'
         DC    C'JKLMNOPQR'       JKLMNOPQR  ARE OK
         ORG   TRLINE+C'S'
         DC    C'STUVWXYZ'        STUVWXYZ  ARE OK
         ORG   TRLINE+C'0'
         DC    C'0123456789'      0123456789  ARE OK
         ORG   ,
         TITLE 'P D S  --  PDS DYNAMIC AREA                   02/06/84'
WORKAREA DSECT
MAINSAVE DS    18A
         SPACE 2
VALSAVE  DS    0A             SAVE AREA FOR VALIDITY CHECKING ROUTINES
ESDIDRSV DS    18A            SAVE AREA FOR READESD AND READIDR
ATTNSAVE DS    18A            SAVE AREA FOR ATTENTION EXIT
STAESAVE DS    18A            SAVE AREA FOR ESTAE EXIT
SPFMAIN  DS    18A            SECONDARY SAVE AREA FOR SPF
         SPACE
R14SAVE  DS    A              FIRST LEVEL SAVE AREA
R8PARSE  DS    A              BASE REGISTER SAVE AREA
R14PARSE DS    A              SECOND LEVEL SAVE AREA
R14PUTM  DS    A              THIRD LEVEL SAVE AREA
R14PUTL  DS    A              THIRD LEVEL SAVE AREA
R14PTGT  DS    A              THIRD LEVEL SAVE AREA
R14EXCP  DS    A              THIRD LEVEL SAVE AREA
R14READD DS    A              THIRD LEVEL SAVE AREA
         SPACE
BASES    DS    4A             PROGRAM BASE ADDRESSES
         SPACE
***      NOTE: KEEP THE FOLLOWING STATEMENTS TOGETHER AND IN THE
***            SAME PHYSICAL ORDER (FOR STM INSTRUCTIONS).
***
ADDRTEXT DS    A        * *    ADDRESS OF TEXT PASSED
ADDRUPT  DS    A        * *    ADDRESS OF THE UPT
ADDRPSCB DS    A        * *    ADDRESS OF THE PSCB
ADDRECT  DS    A        * *    ADDRESS OF THE ECT
ADDRCBUF DS    A        * *    ADDRESS OF THE COMMAND BUFFER
ADDRCPPL DS    A        * *    ADDRESS OF THE CPPL
ADDRPUTL DS    A        * *    ADDRESS OF IKJPUTL
ADDRPARS DS    A        * *    ADDRESS OF IKJPARS
ADDRPTGT DS    A        * *    ADDRESS OF IKJPTGT
ADDRSCAN DS    A        * *    ADDRESS OF IKJSCAN
ADDRDAIR DS    A        * *    ADDRESS OF IKJDAIR
ADDRSTCK DS    A        * *    ADDRESS OF IKJSTCK
ADDRDEFT DS    A        * *    ADDRESS OF IKJEHDEF
ADDRFF02 DS    A        * *    ADDRESS OF IKJEFF02
ADDRCNVT DS    A        * *    TTR --> MBBCCHHR CONVERT ROUTINE
ADDRRLTV DS    A        * *    MBBCCHHR --> TTR CONVERT ROUTINE
***
         SPACE 2
ADDRANSR DS    A
OPENLIST DS    2A
PARMLIST DS    8A             GENERAL PARAMETER LIST
PUTLPARM DS    4A             $PUTLINE PARAMETER AREA
PTGTPARM DS    4A             $PUTGET PARAMETER AREA
YESNOSV  DS    A              SAVED MESSAGE ADDRESS FOR YES/NO PROMPTS
SCANANSR DS    XL8
         SPACE 1
PUTOLD1  DS    3F
PAGESIZE DS    F              LINES/SCREEN
LINESIZE DS    F              CHARACTERS/LINE
PDSENTRY DS    F              NUMBER OF ENTRIES TO PDS AFTER FIRST
         EJECT
SUBCMNDP DS    A              SUBCOMMAND ENTERED ON PRIMARY LINE
ATTNECB  DS    F
SUBSECB  DS    F
SUBSTCB  DS    F
RECOVER  DS    A
SPFECB   DS    F
SPFTCB   DS    A
ISPLINK  DS    A              ADDRESS OF THE SPF INTERFACE ROUTINE
DAIRRC   DS    F              DAIR RETURN CODE FROM REGISTER 15
RETURNCD DS    F              PROGRAM RETURN CODE
OUTMEMBS DS    A              ROOT OF DUPLICATE MEMBER NAME LIST
SUBPDIAL DS    A              SUBPOOL AND ADDRESS OF SP 78 DATA
         SPACE
UCBADDR  DS    A
EXCPBUFF DS    A                  ADDRESS OF LAST RECORD READ
BUFFADDR DS    A        **        ADDRESS OF THE TWO MAIN BUFFERS
BUFFSIZE DS    H        **        SIZE OF EACH OF THE MAIN BUFFERS
DSORG    DS    H
LRECL    DS    H
BLKSI    DS    H
LABLRECL DS    H
         SPACE 1
TOTBLOCK DS    H        **  --   THIS AREA IS CLEARED
TOTUSED  DS    H        **  --     VIA AN XC INSTRUCTION
TOTMEMR  DS    H        **  --     (LENGTH IS LTOT).
TOTMEMA  DS    H        **  --
TOTMAXM  DS    H        **  --
TOTTHISM DS    H        **  --
TOTTRKS  DS    H        **  --
TOTCCHH  DS    XL4      **  --
LTOT     EQU   *-TOTBLOCK        LENGTH OF XC AREA
DSNTOTAL DS    H
DSNEMPTY DS    H
NUMEXT   DS    H              NUMBER OF EXTENTS
BYTEUCB  DS    H              UCB TYPE IS THE SECOND BYTE
         SPACE
LMEMBER1 DS    H              LENGTH OF MEMBER NAME ONE
MEMBER1  DS    CL8            FIRST MEMBER NAME   *** TOGETHER (RENAME)
MEMBER2  DS    CL8            SECOND MEMBER NAME  *** TOGETHER (RENAME)
LMEMBER2 DS    H              LENGTH OF MEMBER NAME TWO
         SPACE
MEMBERD  DS    XL1,XL2,CL8,CL8,XL2
***      SAVE  FLAGSAA,LMEMBER1,MEMBER1,MEMBER2,LMEMBER2 FOR (GROUP)
MEMBERP  DS    XL1,XL2,CL8,CL8,XL2
***      SAVE  FLAGSAA,LMEMBER1,MEMBER1,MEMBER2,LMEMBER2 FOR PATTERN
         SPACE 2
HFLAGSDD DS    X              HOLD AREA FOR FLAGSDD
DSPALLOC DS    X              CURRENT DISPOSITION (SHARE OR OLD)
DSPREQST DS    X              INITIAL DISPOSITION (SHARE OR OLD)
RECFM    DS    X              DCBRECFM FOR THIS DATA SET
SUBPOOLT DS    X              TEMPORARY SUBPOOL FOR A PDS SUBCOMMAND
RESERVET DS    X              "R" WHEN A SPDSN RESERVE IS OUTSTANDING
DOUBLE   DS    D
         TITLE 'P D S  --  PDS DYNAMIC AREA -- CONTROL FLAGS  02/06/84'
FLAGSAA  DS    XL1   FLAG BYTE 1 (CLEARED FOR EACH SUBCOMMAND)
FOPTIONS EQU   X'80'          OPTIONS PRESENT WITH COMMAND
FINDMSG  EQU   X'40'          FINDMEMQ MESSAGE IS AVAILABLE
FMEMSTAR EQU   X'20'          * NOTATION USED FOR THE MEMBER NAME
FHEAD    EQU   X'10'          HEADER HAS BEEN WRITTEN
FMEMBER1 EQU   X'08'          FIRST MEMBER FLAG
FMEMBER2 EQU   X'04'          SECOND MEMBER FLAG
FMEMRANG EQU   X'02'          MEMBER RANGE DESIRED
FMEM#MEM EQU   X'01'          MEMBER GROUP IS IN PROGRESS
         SPACE 2
FLAGSBB  DS    XL1   FLAG BYTE 2
FONESHOT EQU   X'40'          SUBCOMMAND ENTERED FROM PRIMARY LINE
FLINESET EQU   X'20'          OUTPUT LINE IN PROGRESS
FEXIST   EQU   X'10'          MEMBERS EXIST IN DATA SET
FRANPAT  EQU   X'08'          MEMBERS EXIST IN RANGE OR PATTERN
FCMD     EQU   X'04'          SUBCOMMAND BUFFER AVAILABLE
FQUOTE   EQU   X'02'          DATA SET NAME IS QUOTED
FNULL    EQU   X'01'          NULL LINE ENTERED
         SPACE 2
FLAGSCC  DS    XL1   FLAG BYTE 3
FPERMDS  EQU   X'80'          "PERM" DATA SET
FRECOVER EQU   X'40'          DSNAME PARSE FOR DATA SET RECOVERY
FIDR     EQU   X'20'          IDR RECORDS EXIST
F1IDR    EQU   X'10'          IDR TITLE PROCESSED
FBLDLOK  EQU   X'08'          PREVIOUS BLDL IS STILL VALID
RECFMU   EQU   X'04'          RECORD FORMAT U
RECFMV   EQU   X'02'          RECORD FORMAT V
RECFMF   EQU   X'01'          RECORD FORMAT F
         SPACE 2
FLAGSDD  DS    XL1   FLAG BYTE 4
OVERLAP  EQU   X'80'          OUTPUT THE NEXT LINE (OVERLAPPED STRING)
DONONUM  EQU   X'40'          NUM --> NONUM FOR THE REST OF THE MEMBER
DUMP     EQU   X'20'          ** DUMP OUTPUT FORMAT
BLOCK    EQU   X'10'          ** BLOCK OUTPUT FORMAT
LDUMP    EQU   X'08'          ** LDUMP OUTPUT FORMAT
LBLOCK   EQU   X'04'          ** LBLOCK OUTPUT FORMAT
SNUM     EQU   X'02'          ** SNUM OUTPUT FORMAT
NONUM    EQU   X'01'          ** NONUM OUTPUT FORMAT
*NUM     EQU   X'00'          ** NUM (DEFAULT) IF NOT SNUM, NONUM,
*                                    LBLOCK, LDUMP, BLOCK OR DUMP
         EJECT
FLAGSEE  DS    XL1   FLAG BYTE 5
FBKGRND  EQU   X'08'          PDS IS EXECUTING IN BACKGROUND MODE
FNOTTERM EQU   X'04'          LAST RESPONSE WAS NOT FROM THE TERMINAL
FDIREND  EQU   X'01'          END OF MEMBERS IN DIRECTORY REACHED
         SPACE 2
FLAGSFF  DS    XL1   FLAG BYTE 6
FSPFDIAL EQU   X'80'          PDS WAS INVOKED BY SPF AS A DIALOG
FSPFERR  EQU   X'40'          SPF INITIALIZATION FAILED
FSPFOPT6 EQU   X'20'          EXECUTING UNDER SPF OPTION 6
FSPFCALL EQU   X'10'          SET AFTER SPF HAS BEEN CALLED ONCE
FCHANGE  EQU   X'08'          SET OFF WHEN DATA SET IS CHANGED
FOUTSAVE EQU   X'04'          SET ON IF RECORDING IS REQUESTED
FOUTNO37 EQU   X'02'          SET ON UNTIL A S*37 ABEND IS NOTED
FOUTECHO EQU   X'01'          SET ON  FOR "ECHO" MODE IN OUTCOPY
         SPACE 2
FLAGSGG  DS    XL1   FLAG BYTE 7
FALINCON EQU   X'80'          NOALIASINFO (ATTRIB AND MAP)
FALUPCON EQU   X'40'          NOUPDATEALIAS (ATTRIB AND RENAME)
FLKEDCON EQU   X'20'          NOLKEDDATE (ATTRIB)
FPROMCON EQU   X'10'          NOPROMPT
FRECVCON EQU   X'08'          NORECOVER
FDOUBCON EQU   X'04'          NODOUBLE
         SPACE 2
FLAGSII  DS    XL1   FLAG BYTE 8 (CLEARED FOR EACH SUBCOMMAND)
FDIRGRP  EQU   X'80'          DIRECTORY READ FOR GROUP OPERATIONS
FSINGLE  EQU   X'40'          FORCE SINGLE BUFFERING FOR SUBCOMMAND
FDIRNEXT EQU   X'20'          NEXT DIRECTORY RECORD IS ALREADY INPUT
FIOPEND  EQU   X'10'          INPUT HAS BEEN INITIATED AND IS PENDING
FIOPDIR  EQU   X'08'          INPUT IS PENDING FOR A DIRECTORY GROUP
         TITLE 'P D S  --  PDS DYNAMIC AREA                   02/06/84'
DSNLEN   DS    H
DSNAME   DS    CL44
DSNMEMQ  DS    CL8
PASSWORD DS    CL8
VOLUME   DS    CL8
SAVLEN   EQU   *-DSNLEN
         SPACE 1
SAVDSN   DS    XL1,XL2,XL44,XL8,XL8,XL8
*        DSN DISPOSITION, DSN LENGTH, DSN, ENQ MEMBER, PASSWORD, VOLUME
         SPACE 1
PDSNAME  DS    CL8                      PDS ACTUAL COMMAND NAME
DDNAME   DS    CL8
DDNAMEH  DS    CL8                      DDNAME HOLD
DSNAMEH  DS    CL4                      FIRST FOUR DSNAME CHARACTERS
VOLALLOC DS    CL6                      ALLOCATED DISK VOLUME NAME
MODESAVE DS    CL1                      RMODE/AMODE WORK AREA
MSGENTDS DS    CL85                     ENTER OPTION MESSAGE
         SPACE 1
         DS    0F
MSGTEXT1 DS    XL124,XL136       4 BYTE COUNT AND 256 BYTE DISPLAY
MSGTEXT2 DS    XL124
         SPACE 1
COMMDSTR EQU   MSGTEXT1+124,256  OPERAND REVIEW/SUBMIT/PRINTO/TSOLIST
MSGLAST  DS    XL80              PUTLINE AND PUTGET RECORD I/O BUFFER
         SPACE 2
FULLWORD DS    0D,F
ISPFTEXT DC    C'ISPF    PGM(12345678)    PARM(++12345678) NEWAPPL(ISR)X
                    '      COULD ZAP TO CHANGE "NEWAPPL(ISR)" OR "TEST"
         SPACE 1
RLDCOUNT DS    X               RLD/CONTROL RECORDS AFTER FIRST TEXT
DAYTABLE DS    XL12
FINDMEMQ DS    CL90
         SPACE 1
M99AREA  DS    0F              AREA FOR SVC 99
M99RBPTR DS    F                 REQUEST BLOCK POINTER
M99RB    DS    5F                DYNALLOC REQUEST BLOCK
M99TUPL  DS    F                 TEXT UNIT POINTER
M99TUNIT DS    3F                TEXT UNIT
         SPACE 1
FINDLTH  DS    H               ACTUAL STRING LENGTH
FINDSTR  DS    CL16            ACTUAL FIND STRING
FINDTBL  DS    XL256           TRT TABLE (FIND FIRST BYTE OF STRING)
PUTGETBF DS    XL256           $PUTGET INPUT LINE STORAGE
RECALLST DS    XL256           RECALL TEXT AREA
         SPACE 2
         PRINT NOGEN
INDCB    DCB   DEVD=DA,DDNAME=X,MACRF=E,EXLST=EXLST
STOWDCB  DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
BAMDCB   DCB   DDNAME=RESET,DSORG=PS,RECFM=FB,LRECL=256,BLKSIZE=256,   X
               MACRF=(W),KEYLEN=8,DEVD=DA
QAMDCB   DCB   DSORG=PS,DDNAME=PDSOUT,MACRF=(PM),EXLST=OUT99
EXLSTX   DS    A
         PRINT GEN
         TITLE 'P D S  --  PDS DYNAMIC AREA - POST-PROCESSING 02/06/84'
##SUBCOM DS    0F,CL8    ***  CURRENT FULL SUBCOMMAND NAME
##ADRCM# EQU   *,1            PFK FOR THIS SUBCOMMAND, FLAGS
CONTINUE EQU   X'80'          SUBCOMMAND CONTINUES AFTER DATA SET OPEN
FIXOPEN  EQU   X'40'          SUBCOMMAND IS FIXPDS (SPECIAL OPEN FLAG)
EDITOR   EQU   X'20'          SUBCOMMAND - SPFEDIT, BROWSE, EDIT OR FSE
EXTERNAL EQU   X'10'          SUBCOMMAND IS EXTERNAL TO PDS
##ADRCMD DS    A         ***  SUBCOMMAND PROCESSING ROUTINE
##ADRPC# EQU   *,1            SUBCOMMAND FLAGS
##ADRPCL DS    A         ***  PCL FOR THE SUBCOMMAND PARSE
##ADRPA# EQU   *,1            ADDITIONAL SUBCOMMAND FLAGS
#FIND1   EQU   X'10'          @Q - QUIET; @N - NO MSG; @C - DISPLAY
##ADRPAR DS    A         ***  POST-PARSE PROCESSING ROUTINE
##ANSWER DS    XL120          PARSE CSECT WORKAREA
         SPACE 2
** ALIAS, CONTROL, DELETE, DIRENTRY, FIND, LIST, OUT,
**   PRINTOFF, RESTORE, REVIEW, SUBMIT AND TSOLIST SUBCOMMAND AREA:
#OUT     EQU   ##ANSWER+0,80    OUT LOGICAL RECORD AREA
#DELFLAG EQU   ##ANSWER+0,1     DELETE: ALIAS/NOALIAS
#RESFLAG EQU   ##ANSWER+0,1     RESTORE: REPEAT/DISPLAY/PROMPT
#RESFREP EQU     X'01'          RESTORE: REPEAT
#RESFDIS EQU     X'02'          RESTORE: DISPLAY
#RESFPRO EQU     X'04'          RESTORE: PROMPT
#LISTN0  EQU   ##ANSWER+12,4    LIST -- NEXT MEMBER DISPLACEMENT
#LISTN1  EQU   ##ANSWER+16,4    LIST -- DISPLACEMENT IN LOAD MODULE
#LISTN2  EQU   ##ANSWER+20,4    LIST -- DISPLACEMENT IN CSECT
*#ESDPTR  EQU  ##ANSWER+24,4    ADDRESS OF ESD DATA CHAIN
*#ACTIONT EQU  ##ANSWER+32,4    ACTION AFTER A SUCCESSFUL FIND
#RESTAPF EQU   ##ANSWER+34,1    RESTORE: APF DATA FROM LIKE
#RESTDIR EQU   ##ANSWER+35,33   RESTORE: DIRECTORY USER DATA
*#ACTIONE EQU  ##ANSWER+36,4    ACTION AFTER AN UNSUCCESSFUL FIND
#LFSAVE2 EQU   ##ANSWER+40,8    LIST/FIND SAVE AREA FOR R14 AND R15
#LFSAVE4 EQU   ##ANSWER+48,16   LIST/FIND SAVE AREA FOR R3, R4, R5 & R6
#SKIPCOL EQU   ##ANSWER+64,4    COLUMNS TO SKIP FOR EACH RECORD
#SKIPREC EQU   ##ANSWER+68,4    RECORDS TO SKIP AT THE BEGINNING
#RESTSSI EQU   ##ANSWER+68,4    RESTORE: LOAD MODULE SSI DATA
#RESTLIK EQU   ##ANSWER+72,8    RESTORE: LIKE MODEL MEMBER NAME
#BLKCNT  EQU   ##ANSWER+72,4    LIST RECORD/BLOCK COUNTER
#COMMDSZ EQU   ##ANSWER+76,2    EXTERNAL SUBCOMMAND OPERAND LENGTH
#LISTOFF EQU   ##ANSWER+76,4    LIST OFFSET
#OUTREGS EQU   ##ANSWER+80,28   REGISTER R2:R8 SAVE AREA
#RESTSEQ EQU   ##ANSWER+80,4    SEQUENTIAL MEMBER NAME COUNT
#RESTLEN EQU   ##ANSWER+84,4    MACHINE LENGTH OF ENTERED NAME
#LISTID  EQU   ##ANSWER+84,2    LIST ESDID
#LISTFLG EQU   ##ANSWER+88,1    LIST CSECT FLAG
#ALIAESD EQU   ##ANSWER+89,1    ALIAS   MVS/XA RMODE/AMODE ESD FLAGS
#RESTESD EQU   ##ANSWER+90,1    RESTORE MVS/XA RMODE/AMODE ESD FLAGS
#RESTMOD EQU   ##ANSWER+91,1    RESTORE MVS/XA RMODE ESD SUMMARY AREA
#LISTVIR EQU   ##ANSWER+92,4    VIRTUAL LOAD MODULE START ADDRESS
#LISTENT EQU   ##ANSWER+96,4    CURRENT ESD ENTRY
#LISTWD  EQU   ##ANSWER+100,4   WIDTH (16 OR 64)
#LISTE1  EQU   ##ANSWER+104,4   END OF CSECT OR EXTERNAL SYMBOL
#MAXLEN  EQU   ##ANSWER+108,4   MAXIMUM RECORD LENGTH
#MAXIN   EQU   ##ANSWER+112,4   MAXIMUM RECORDS TO READ
#MAXOUT  EQU   ##ANSWER+116,4   MAXIMUM DATA LINES TO OUTPUT
         EJECT
** ATTRIB, IF, HISTORY AND MAP SUBCOMMAND AREA:
#ZAPOPT  EQU   ##ANSWER+0,1  ** ZAP(PARTNAME)             (LOAD ONLY)
#USEROPT EQU   ##ANSWER+1,1  ** USER(PARTNAME)            (LOAD ONLY)
#TRANOPT EQU   ##ANSWER+2,1  ** TRAN(PARTNAME)            (LOAD ONLY)
#LKEDOPT EQU   ##ANSWER+3,1  ** LKED(PARTNAME)            (LOAD ONLY)
#MODOPT  EQU   ##ANSWER+4,1  ** MODULE(PARTNAME)          (LOAD ONLY)
#RLDERR  EQU   ##ANSWER+5,1  ** RLDERR/NORLDERR           (LOAD ONLY)
#ATTRYES EQU   ##ANSWER+6,2  ** BITS ON FOR ATTR/IF       (LOAD ONLY)
#ATTRNO  EQU   ##ANSWER+8,2  ** BITS OFF FOR ATTR/IF      (LOAD ONLY)
#AUTHOPT EQU   ##ANSWER+10,1 ** AUTH/NOAUTH               (LOAD ONLY)
#PAGEOPT EQU   ##ANSWER+11,1 ** PAGE/NOPAGE               (LOAD ONLY)
#RMODE   EQU   ##ANSWER+12,1 ** RMODE24/RMODEANY          (LOAD ONLY)
#AMODE   EQU   ##ANSWER+13,1 ** AMODE24/AMODE31/AMODEANY  (LOAD ONLY)
#VSLKED  EQU   ##ANSWER+14,1 ** VSLKED/NOVSLKED           (LOAD ONLY)
#LOADERR EQU   ##ANSWER+15,1 ** LOADERR/NOLOADERR         (LOAD ONLY)
#LKEDERR EQU   ##ANSWER+16,1 ** CONFLICT/NOCONFLICT       (LOAD ONLY)
#RLD0    EQU   ##ANSWER+17,1 ** RLDZERO/NORLDZERO         (LOAD ONLY)
#APFERR  EQU   ##ANSWER+18,1 ** APFERR/NOAPFERR           (LOAD ONLY)
#MODLEN  EQU   ##ANSWER+19,1    PARTNAME LENGTH FOR MODULE(..
#MAPOPT  EQU   ##ANSWER+20,1    SHORT/FULL/ENTRY
#ALIAOPT EQU   ##ANSWER+21,1    ALIAS/NOALIAS
#USERIDO EQU   ##ANSWER+22,1    USERID(PUSER)/NOTUSERID(PUSER)/NOUSERID
#OPTOPT  EQU   ##ANSWER+23,1    PROCESSING OPTIONS SUMMARY
#ESDPTR  EQU   ##ANSWER+24,4    ADDRESS OF ESD DATA CHAIN
#IDRPTR  EQU   ##ANSWER+28,4    ADDRESS OF IDR DATA CHAIN
#ACTIONT EQU   ##ANSWER+32,4    ACTION AFTER A SUCCESSFUL FIND
#ACTIONE EQU   ##ANSWER+36,4    ACTION AFTER AN UNSUCCESSFUL FIND
#ATTRLO  EQU   ##ANSWER+40,3    LOWER TTR LIMIT FROM TTR(LOW:HIGH)
#ATTRHI  EQU   ##ANSWER+43,3    UPPER TTR LIMIT FROM TTR(LOW:HIGH)
#ZAPLEN  EQU   ##ANSWER+46,1 ** PARTNAME LENGTH FOR ZAP(..
#USERLEN EQU   ##ANSWER+47,1 ** PARTNAME LENGTH FOR USER(..
#TRANLEN EQU   ##ANSWER+48,1 ** PARTNAME LENGTH FOR TRAN(..
#LKEDLEN EQU   ##ANSWER+49,1    PARTNAME LENGTH FOR LKED(..
*** AVAILABLE  ##ANSWER+50,1
#SSIOPT  EQU   ##ANSWER+51,1    SSI/NOSSI
#IFABOVE EQU   ##ANSWER+52,4 ** ABOVE(COUNT)           (MAYBE READ)
#IFBELOW EQU   ##ANSWER+56,4 ** BELOW(COUNT)           (MAYBE READ)
#IOERROR EQU   ##ANSWER+60,1 ** IOERROR/NOIOERROR      (IMPLIES READ)
#BLOCKER EQU   ##ANSWER+61,1 ** BLOCKERR/NOBLOCKERR    (IMPLIES READ)
#LRECLER EQU   ##ANSWER+62,1 ** LRECLERR/NOLRECLERR    (IMPLIES READ)
#NULL    EQU   ##ANSWER+63,1 ** NULL/NONULL            (IMPLIES READ)
#AALIAS  EQU   ##ANSWER+64,1    APPARENTALIAS/NOAPPARENTALIAS
#ORPHAN  EQU   ##ANSWER+65,1    ORPHAN/NOORPHAN
#SPFEDIT EQU   ##ANSWER+66,1    SPFEDIT/NOSPFEDIT
#NAMEERR EQU   ##ANSWER+67,1    NAMEERR/NONAMEERR
#LKEDTXT EQU   ##ANSWER+68,10   LKED(NAME) TEXT
#IFLEN   EQU   ##ANSWER+68,4    LENGTH OF MODULE OR NUMBER OF LINES
#TRANTXT EQU   ##ANSWER+78,10   TRAN(NAME) TEXT
#USERIDL EQU   ##ANSWER+88,1    USERID(NAME) LENGTH
#USERIDT EQU   ##ANSWER+89,7    USERID(NAME) TEXT
#MODTXT  EQU   ##ANSWER+96,8    MODULE(NAME) TEXT
#ZAPTXT  EQU   ##ANSWER+104,8   ZAP(NAME) TEXT
#USERTXT EQU   ##ANSWER+112,8   USER(NAME) TEXT
         EJECT
** EDIT, FIXPDS, FSE, RENAME, SPFEDIT AND VERIFY SUBCOMMAND AREA:
#FIXOPT  EQU   ##ANSWER+0,1     EXPANDDIR/RESETDIR/DCB
#EDITOLD EQU   ##ANSWER+0,1     OLD/NEW
#SPFOLD  EQU   ##ANSWER+0,1     OLD/NEW
#VERLOAD EQU   ##ANSWER+0,1     LOAD/NOLOAD
#VERUPDT EQU   ##ANSWER+1,1     UPDATE/NOUPDATE
#EDITNON EQU   ##ANSWER+1,1     NONUM
#EDITASI EQU   ##ANSWER+2,1     ASIS
#VERREAD EQU   ##ANSWER+2,1     READ/NOREAD
#RESTERR EQU   ##ANSWER+2,2     CONSECUTIVE I/O ERRORS
#VERSTND EQU   ##ANSWER+3,1     STANDARD/NOSTANDARD
#EDITTYP EQU   ##ANSWER+4,4     EDIT TYPE (CNTL, ASM, OR ...)
#VERMBR  EQU   ##ANSWER+8,8     MEMBER NAME FOR VERIFY
#RECFM   EQU   ##ANSWER+8,4     RECORD FORMAT DESIRED FOR FIXPDS
#LRECL   EQU   ##ANSWER+12,4    LRECL DESIRED FOR FIXPDS
#BLKSI   EQU   ##ANSWER+16,4    BLKSIZE DESIRED FOR FIXPDS
#NEWDIR  EQU   ##ANSWER+20,4    DIRECTORY BLOCKS DESIRED FOR FIXPDS
#MEMPTR  EQU   ##ANSWER+24,4    BASE FOR THE MEMBER NAME LIST
#MEMBLKE EQU     X'08'             BLKSIZE TOO LARGE
#MEMDIVE EQU     X'04'             BLKSIZE/LRECL NOT INTEGRAL
#MEMLRLE EQU     X'02'             LRECL TOO LARGE
#MEMLRL0 EQU     X'01'             LRECL NEGATIVE OR ZERO
#MEMLAST EQU   ##ANSWER+28,4    END OF MEMBER LIST POINTER
#NOTEPTR EQU   ##ANSWER+28,4    BASE FOR THE NOTELIST
#RENSWAP EQU   ##ANSWER+28,1    SWAP/NOSWAP FOR RENAME
#RENRANG EQU   ##ANSWER+29,1    RANGE/NORANGE FOR RENAME
#RENDEFT EQU   ##ANSWER+30,21   FLAGS, L1, M1, M2, L2 FOR RENAME
#MEMCNT  EQU   ##ANSWER+32,4    COUNT OF RECORDS IN THIS MEMBER
#FINDTTR EQU   ##ANSWER+32,4    LAST MEMBER TTR
#SAVEREG EQU   ##ANSWER+36,4*7  REGISTER SAVE AREA
#FIXTTRI EQU   ##ANSWER+36,4    LAST INPUT TTR
#FIXTTRO EQU   ##ANSWER+40,4    LAST OUTPUT TTR
#EOF1TTR EQU   ##ANSWER+44,4    TTR OF ORIGINAL DIRECTORY EOF
#EOF2TT  EQU   ##ANSWER+48,4    TT OF NEW DIRECTORY EOF
#DIRFIX  EQU   ##ANSWER+52,12   TEMPORARY STOW AND DELETE MEMBER
#DIRBTRK EQU   ##ANSWER+64,4    NUMBER OF DIRECTORY BLOCKS PER TRACK
#VERCREC EQU   ##ANSWER+64,4    VERIFY COMPRESS TRACK RECORD NUMBER
#VERCTRK EQU   ##ANSWER+68,4    VERIFY COMPRESS TRACK NUMBER
#VERCBAL EQU   ##ANSWER+72,4    VERIFY COMPRESS TRACK BALANCE
#VERCREG EQU   ##ANSWER+76,4    VERIFY COMPRESS REGISTER SAVE
#VERCLOG EQU   ##ANSWER+80,4    VERIFY COUNT LOGICAL RECORDS
#VERCBLK EQU   ##ANSWER+84,4    VERIFY COUNT PHYSICAL BLOCKS
#VERCBLC EQU   ##ANSWER+88,4    VERIFY COUNT PHYSICAL CHARACTERS
#VERMEM  EQU   ##ANSWER+92,8    VERIFY LAST MEMBER NAME CHANGED
         SPACE 1
#LSUB    EQU   *-##SUBCOM     RESET LENGTH FOR SUBCOMMANDS
         SPACE 3
#SUBHOLD DS    CL8,3AL4,CL120   RESET SAVE AREA FOR SUBCOMMANDS
         TITLE 'P D S  --  PDS DYNAMIC AREA                   02/06/84'
MEMNAME  DS    CL8            DIRECTORY NAME FOR READDIR
MEMTTR   DS    XL3            TTR ADDRESS FOR READDIR
MEMFLAG  DS    X              FLAG BYTE FOR READDIR
         SPACE 2
CCWS     DS    0D
CCW1A    CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
CCW2A    CCW   X'08',*-8-(*-8),X'60',1          TIC
CCW3A    CCW   X'92',IOBSEEK-IOBSEEK+3,X'20',8  MT READ NEXT COUNT
*
CCW0     CCW   X'23',SNO-SNO,X'60',1            SET SECTOR
CCW1     CCW   X'31',IOBSEEK-IOBSEEK+3,X'60',5  SEARCH ID EQUAL (CCHHR)
CCW2     CCW   X'08',*-8-(*-8),X'60',1          TIC
CCW3     CCW   X'06',*-*,X'60',32760            READ DATA
CCW4     CCW   X'92',IOBSEEK-IOBSEEK+3,X'60',8  MT READ NEXT COUNT
CCW5     CCW   X'22',SNO-SNO,X'20',1            READ SECTOR NUMBER
LCCWINIT EQU   *-CCWS
         SPACE 2
IOB      DC    X'C2000000'
IOBECB   DC    A(IOECB)
IOBCSW   DC    2A(0)
IOBCCW   DC    A(CCW0)
IOBDCB   DC    A(INDCB)
         DC    2A(0)
IOBSEEK  DC    2A(0)       *** NEXT MBBCCHHR ADDRESS
KEYLN    DS    X           *** NEXT KEYLENGTH
DATALN   DS    2X          *** NEXT RECORD LENGTH
SNO      DS    X           *** NEXT SECTOR NUMBER
IOECB    DS    F           *** ECB FOR EXCP
CUREXT   DS    F           *** CURRENT DATA SET EXTENT
LIOBDIR  EQU   *-IOBSEEK
         SPACE 2
IODIR1   DS    A,12C,12C,2A    PRIMARY DIRECTORY GROUP INPUT CONTROL
IODIR2   DS    A,12C,12C,2A    SECONDARY DIRECTORY GROUP INPUT CONTROL
IOBUFF1  DS    A,12C,12C,2A    PRIMARY INPUT BUFFER CONTROL
IOBUFF2  DS    A,12C,12C,2A    SECONDARY INPUT BUFFER CONTROL CONTROL
         SPACE 1
AREA2    DS    A               START OF SECONDARY DYNAMIC AREA
DIRPTRS  DS    3A              READDIR DIRECTORY POINTERS
DIRPTRS2 DS    3A              READDIR DIRECTORY POINTERS FOR GROUPS
CURMBB   DS    XL8             CURRENT MBBCCHHR ADDRESS
CURDIR   DS    XL8             LAST DIRECTORY ADDRESS FOR A GROUP
LSTARMBB DS    XL8             DS1LSTAR IN MBBCCHHR FORMAT
EXCPCCWI DS    A               INITIAL CCW (CCW0 OR CCW1)
STARTTR  DS    F               INITIAL TTR TO READ
LS       DS    F               CURRENT RECORD LENGTH
         SPACE 2
ENTRYPT  DS    CL8            MODULE ENTRY POINT NAME
LKEDDATE DS    XL3
LKEDNAME DS    XL10
LKEDVVMM DS    XL4
         TITLE 'P D S  --  PDS DYNAMIC AREA -- MF=L MACROS    02/06/84'
STAEPARM ESTAE *-*,CT,PARAM=*-*,MF=L
         SPACE 1
STAXPARM STAX  *-*,REPLACE=YES,IBUF=0,OBUF=0,MF=L
         SPACE 1
         WRITE FIXPDSD,SF,*-*,*-*,'S',MF=L
         SPACE 1
*INKSFL  LINK   EPLOC=*-*,SF=L
         SPACE 1
ATTACHS1 ATTACH EPLOC=*-*,ECB=*-*,SZERO=NO,SHSPV=78,SF=L
         SPACE 1
ATTACHS2 ATTACH EPLOC=*-*,ECB=*-*,SZERO=NO,SHSPV=78,                  XX
               TERM=YES,ESTAI=(*-*,*-*),SF=L
         SPACE 3
IODONE   DS    H             **  INPUT ROUTINE ENTRIES
IODOUBLE DS    H             **  INITIATED SECOND BUFFER USES
IODOUSE  DS    H             **  USABLE SECOND READS
IODODIS1 DS    H             **  DISCARDED SECOND READ - NEW SUBCOMMAND
IODODIS2 DS    H             **  DISCARDED SECOND READ - NEW TTR
IODODIS3 DS    H             **  DISCARDED SECOND READ - CLOSE
IODONEW  DS    H             **  NUMBER OF TTR CHANGES
         TITLE 'P D S  --  PDS DYNAMIC AREA                   02/06/84'
DIRLINK  DS    0F             ***   DIRECTORY AREA MAPPING  ***
BLDLLIST DS    XL4
DIRNAME  DS    CL8
DIRTTR   DS    XL3
DIRFLAG  DS    X
DIRALIAS EQU   X'80'          MEMBER IS AN ALIAS
DIR2TTR  EQU   X'40'          TWO TTR'S TO UPDATE
DIR1TTR  EQU   X'20'          ONE TTR TO UPDATE
DIRUSER  DS    XL62
         ORG   DIRUSER  FOLLOWING FOR LOAD MODULES
DIRSTART DS    XL4            TTR OF FIRST TEXT BLOCK
DIRNOTE  DS    XL3            TTR OF NOTELIST
DIRNOTE# DS    X              NUMBER OF NOTELIST ENTRIES
DIRATTR  DS    XL2            MEMBER ATTRIBUTES
DIRSTORE DS    XL3            SIZE OF LOAD MODULE
DIRTEXTL DS    XL2            LENGTH OF 1ST TEXT RECORD
DIREPA   DS    XL3            ENTRY POINT ADDRESS
DIRATTR2 DS    X          ADDITIONAL ATTRIBUTE BYTES:
DIRAOSLE EQU   X'80'          VS LINKAGE EDITOR CREATED THIS MODULE
DIR2PAGA EQU   X'20'          PAGE ALIGNMENT REQUIRED FOR THIS MODULE
DIR2SSI  EQU   X'10'          SSI INFORMATION IS PRESENT FOR MODULE
DIRAPFLG EQU   X'08'          APF INFORMATION FOR THIS MODULE IS VALID
DIRATTR3 DS    X          ADDITIONAL ATTRIBUTE BYTES:
DIRRMANY EQU   X'10'          RMODE=ANY
DIRAA31  EQU   X'08'          AMODE=31 (ALIAS ENTRY)
DIRAA24  EQU   X'04'          AMODE=24 (ALIAS ENTRY)
DIRAM31  EQU   X'02'          AMODE=31 (MAIN ENTRY)
DIRAM24  EQU   X'01'          AMODE=24 (MAIN ENTRY)
DIRATTR4 DS    X              COUNT OF RLD ENTRIES AFTER FIRST TEXT
         SPACE 1
DIRAPF   DS    0XL2           APF (IF NOT SCAT, SSI OR ALIAS)
DIREP    DS    XL3            ENTRY POINT (REAL MEMBER)
DIRREAL  DS    CL8            REAL NAME OF MEMBER
DIRAPF2  DS    XL2            APF INFORMATION (ALIAS, NO SCAT OR SSI)
DIREND2  EQU   *              END OF ALIAS SECTION
         ORG   DIRAPF    FOLLOWING FOR SCATTER LOADED MEMBERS
DIRSCLL  DS    XL2            LENGTH OF SCATTER LIST
DIRSCTL  DS    XL2            LENGTH OF TRANSLATE TABLE
DIRSCET  DS    XL2            ESDID OF FIRST TEXT RECORD
DIRSCEP  DS    XL2            ESDID OF ENTRY POINT
DIRAPF3  DS    0XL2           APF (IF SCAT, NO SSI AND NO ALIAS)
DIREPSC  DS    XL3            ENTRY POINT (REAL MEMBER)
DIRREALS DS    CL8            REAL NAME OF MEMBER
DIRAPF4  DS    XL2            APF INFORMATION (SCAT, NO SSI AND ALIAS)
DIREND3  EQU   *              END OF SCATTER, ALIAS SECTION
*
         ORG   DIRUSER   FOLLOWING FOR MODULES SAVED BY SPF EDIT
DIRSPFV  DS    XL1            VERSION NUMBER IN BINARY
DIRSPFR  DS    XL1            REVISION NUMBER IN BINARY
DIRSPFZ  DS    XL2            NOT CURRENTLY USED
DIRSPFCR DS    XL4            CREATION DATE IN FORMAT 00YYDDDF
DIRSPFCD DS    XL4            LAST CHANGE DATE IN FORMAT 00YYDDDF
DIRSPFCT DS    XL2            LAST CHANGE TIME IN FORMAT HHMM
DIRSPFSI DS    XL2            CURRENT SIZE IN BINARY
DIRSPFIN DS    XL2            INITIAL SIZE IN BINARY
DIRSPFMD DS    XL2            MODIFIED LINES IN BINARY
DIRSPFID DS    CL8            USERID OF LAST PERSON TO UPDATE
*                             END OF SPF SECTION
         ORG   ,
DIREND   EQU   *              END OF DIRECTORY INFORMATION
         DS    XL2 (74 BYTES)  PADDING FOR:    MVC    DIRFLAG,DIRFLAG+2
         SPACE 3
         DS    0F             ***   DAIR CONTROL BLOCKS  ***
DAIR08   DS    XL88,XL12      DAIR ALLOCATION BLOCK
DAIR18   DS    XL40           DAIR FREE BLOCK
         ORG   DAIR08-44
         IECSDSL1 (1)         FORMAT 1 DSCB
         ORG   ,
         TITLE 'P D S  --  PDS DYNAMIC AREA -- GENERAL FAIL   02/06/84'
GFPARMP  DS    F              POINTER TO START OF GENERAL FAIL PARMS
         IKJEFFGF
         TITLE 'P D S  --  PDS DYNAMIC AREA -- DEFAULT RTN.   02/06/84'
         IKJEFFDF
         ORG   DFBUFS         NO CALLER-SUPPLIED BUFFERS NEEDED
         SPACE 3
LENWORK  EQU   *-WORKAREA
         TITLE 'P D S  --  PDS ESD, IDR DSECTS                02/06/84'
*
*        ESD TABLE FORMAT
*
ESDENTRY DSECT
ESDLINK  DS    F
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINITION
CODEER   EQU   X'02'          ENTERNAL REFERENCE
CODELR   EQU   X'03'          LABEL REFERENCE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODECM   EQU   X'05'          COMMON BLOCK
CODEPR   EQU   X'06'          PSEUDO REGISTER
CODENL   EQU   X'07'          NULL
CODEWK   EQU   X'0A'          WEAK EXTERNAL
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDXAFLG EQU   ESDSEG#        FLAGS FOR MVS/XA
RMODEANY EQU   X'04'          RMODE=ANY
AMODE24  EQU   X'01'          AMODE=24
AMODE31  EQU   X'02'          AMODE=31
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
ESDID    DS    XL2            RELATIVE ESD ID #
ESDIDIN  DS    XL2            ESD IS IN THIS PHYSICAL RECORD
ESDCSECT DS    XL4            LIST ONLY -- POINTER TO CSECT ENTRY
ESDMAIN  DS    XL4            LIST ONLY -- ENTRY POINT OF CSECT ENTRY
LENESD2  EQU   *-ESDNAME      LENGTH OF ESD DATA
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY
         SPACE 3
*
*        IDR TABLE FORMAT
*
IDRENTRY DSECT
IDRLINK  DS    A
IDRSTART EQU   *
IDRESDID DS    XL2
IDRTYPE  DS    X
IDRZAP   EQU   X'01'
IDRLKED  EQU   X'02'
IDRTRAN  EQU   X'04'
IDRUSER  EQU   X'08'
IDRDATE  DS    XL3
IDRLDATA DS    X
IDRUPREF DS    XL6            USER RECORD PREFIX
IDRZDATA DS    0CL8
IDRTDATA DS    0CL30
IDRDATA  DS    CL40
LENIDR1  EQU   *-IDRSTART
LENIDR   EQU   *-IDRENTRY
         TITLE 'P D S  --  PDS SDWA DSECT                     02/06/84'
         IHASDWA  VRAMAP=NO
         TITLE 'P D S  --  PDS SVC 99 PARAMETER LIST          02/06/84'
         IEFZB4D0
         TITLE 'P D S  --  PDS PPL, CSPL, CSOA DSECTS         02/06/84'
         IKJPPL
         SPACE 3
         IKJCSPL
         SPACE 2
         IKJCSOA
         TITLE 'P D S  --  PDS IOPL, PGPB DSECTS              02/06/84'
         IKJIOPL
         SPACE 5
         IKJPGPB
**       TITLE 'P D S  --  PDS GTPB DSECT                     02/06/84'
**       IKJGTPB
         TITLE 'P D S  --  PDS UPT DSECT                      02/06/84'
         IKJUPT
         TITLE 'P D S  --  PDS PSCB DSECT                     02/06/84'
         IKJPSCB
         TITLE 'P D S  --  PDS ECT DSECT                      02/06/84'
         IKJECT
         TITLE 'P D S  --  PDS CPPL DSECT                     02/06/84'
         IKJCPPL
         TITLE 'P D S  --  PDS DAPL DSECT                     02/06/84'
         IKJDAPL
**       TITLE 'P D S  --  PDS DAIR00 DSECT                   02/06/84'
**       IKJDAP00
**       TITLE 'P D S  --  PDS DAIR04 DSECT                   02/06/84'
**       IKJDAP04
         TITLE 'P D S  --  PDS DAIR08 DSECT                   02/06/84'
         IKJDAP08
         TITLE 'P D S  --  PDS DAIR18 DSECT                   02/06/84'
         IKJDAP18
         SPACE 2
**       IKJDAP0C
**0CDDN  DS    XL8 START OF DDNAME LIST
**       TITLE 'P D S  --  PDS DAIR10 DSECT                   02/06/84'
**       IKJDAP10
**       TITLE 'P D S  --  PDS DAIR14 DSECT                   02/06/84'
**       IKJDAP14
**       TITLE 'P D S  --  PDS DAIR1C DSECT                   02/06/84'
**       IKJDAP1C
**       TITLE 'P D S  --  PDS DAIR24 DSECT                   02/06/84'
**       IKJDAP24
**       TITLE 'P D S  --  PDS DAIR28 DSECT                   02/06/84'
**       IKJDAP28
**       TITLE 'P D S  --  PDS DAIR2C DSECT                   02/06/84'
**       IKJDAP2C
**       TITLE 'P D S  --  PDS DAIR30 DSECT                   02/06/84'
**       IKJDAP30
         TITLE 'P D S  --  PDS DFPL AND DFPB DSECTS           02/06/84'
         IKJDFPL
         SPACE 3
         IKJDFPB
         TITLE 'P D S  --  PDS DCBD AND CVT DSECTS            02/06/84'
         PRINT NOGEN
         DCBD  DSORG=PO
         SPACE 5
         CVT   DSECT=YES
         SPACE 5
.NOGEN   END  PDS61
