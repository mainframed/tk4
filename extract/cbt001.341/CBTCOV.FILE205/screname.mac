SCRE     TITLE '                        SCRATCH OR RENAME A MEMBER'
SCRENAME CSECT
         PRIME
         L     R10,0(R1)                LOAD POINTER TO PASSAREA
         USING PASSAREA,R10             ESTABLISH ADDRESSABILITY
         L     R9,DCBADDR               GET DCB ADDRESS
         OPEN  (PDS,OUTPUT)             OPEN FOR OUTPUT FOR STOW
         MVC   DSNAME(44),PDSDSN        MOVE LIB NAME TO MESSAGE
         TM    OPBITS,SCRTCHOP          CHECK IF A SCRATCH
         BO    SCRATCH                  YES - GO SCRATCH THE MEMBER
         TM    OPBITS,RENAMEOP          CHECK IF A RENAME
         BO    RENAME                   YES - GO RENAME THE MEMBER
         MVI CONDCODE+3,X'10'           SET CONDION CODE TO 16
         B     EOJ                      TERMINATE
         SPACE 2
SCRATCH  DS    0H                       DO THE SCRATCH
         STOW  PDS,NAME,D               DO A DELETE STOW
         LTR   R15,R15                  DID IT WORK?
         BZ    SCROK                    YES - ALL WAS OKAY
         CLM   R15,1,=X'08'             WAS IT NOT FOUND
         BE    NOTFOUND                 YES - IT WAS NOT THERE
         B     ERR2                     ELSE I/O ERROR
         SPACE 2
RENAME   DS    0H                       RENAME A MEMBER
         MVC   OLDMEMBR,NAME            CREATE LIST FOR STOW
         MVC   NEWMEMBR,NEWNAME         MOVE NEW NAME TO LIST
         STOW  PDS,OLDMEMBR,C           ISSUE CHANGE STOW
         LTR   R15,R15                  DID IT WORK?
         BZ    RENAMOK                  YES - ALL OKAY
         CLM   R15,1,=X'08'             WAS IT NOT FOUND
         BE    NOTFOUND                 YES - IT WAS NOT FOUND
         CLM   R15,1,=X'04'             DID 'NEWNAME' ALREADY EXIST
         BE    DUPLCATE                 YES GIVE MESSAGE
         B     ERR2                     ELSE I/O ERROR
         SPACE 2
SCROK    DS    0H                       SCRATCH WAS DONE OKAY
         MVC   SCRNAME,NAME             MOVE NAME TO MESSAGE
         MVC   PRTLINE(L'SCROKMSG),SCROKMSG  MOVE MESSAGE
         BAL   R3,PUTLINE               GO PRINT THE MESSAGE
         B     EOJ                      GET OUT
RENAMOK  DS    0H                       RENAME WAS OKAY
         MVC   RENAMNM,NAME             MOVE NAME TO MESSAGE
         MVC   RENAMNEW,NEWNAME         MOVE NEW NAME
         MVC   RENAMLIB,DSNAME          MOVE LIBRARY NAME
         MVC   PRTLINE(L'RENAMMS),RENAMMS  MOVE MESSAGE TO PRINT
         BAL   R3,PUTLINE
         B     EOJ                      TERMINATE
NOTFOUND DS    0H                       MEMBER NAME WAS NOT FOUND
         ST    R15,CONDCODE             SAVE CONDITION CODE
         MVC   NOTFNDLB,DSNAME          MOVE DSNAME TO MESSAGE
         MVC   NOTFNDNM,NAME            MOVE NAME TO MESSAGE
         MVC   PRTLINE(L'NOTFNDMS),NOTFNDMS   MOVE MESSAGE TO PRINT
         BAL   R3,PUTLINE               GO PRINT IT
         B     EOJ                      FINISHED
EOJ      DS    0H                       FINISHED
         CLOSE PDS                      CLOSE THE DCB
         L     R15,CONDCODE             RESTORE RETURN CODE
         TERME
PUTLINE  DS    0H                       WRITE A MESSAGE
         PUT   (R9),PRTLINE             PUT THE MESSAGE
         MVI   PRTLINE,X'40'            MOVE ONE BLANK
         MVC   PRTLINE+1(L'PRTLINE-1),PRTLINE    BLANK THE LINE
         BR    R3                       RETURN
ERR2     DS    0H                       GIVE ERROR MESSAGE
         ST    R15,CONDCODE             SAVE RETURN CODE
         MVC   ERR2DSN,DSNAME           MOVE LIBRARY NAME
         MVC   PRTLINE(L'ERR2MSG),ERR2MSG      MOVE TO PRINT MESSAGE
         BAL   R3,PUTLINE               GO PRINT IT
         B     EOJ                      TERMINATE
DUPLCATE DS    0H                       NEWNAME EXISTS
         ST    R15,CONDCODE             STORE THE CONDITION CODE
         MVC   DUPNEWNM,NEWNAME         MOVE NEWNAME
         MVC   DUPDSN,DSNAME            MOVE THE DSNAME
         MVC   PRTLINE(L'DUPMESG),DUPMESG  MOVE TO PRINT
         BAL   R3,PUTLINE               GO PRINT IT
         B     EOJ                      GO TERMINATE
         EJECT
PDSEXLST DC    0F'0',X'87',AL3(DSNAME)
CONDCODE DC    F'0'
PRTLINE  DC    CL121' '
         DS    0F                       TO MAKE JFCB DSN ALIGN
         DS    CL3                      TO MAKE JFCB DSN ALIGN
SCROKMSG DS    0CL89
         DC    C'0IZG020I MEMBER NAME XXXXXXXX SCRATCHED FROM '
         ORG   SCROKMSG+21
SCRNAME  DS    CL8
         ORG
DSNAME   DS    0CL44
SCRLIB   DS    11F                      NAME PART OF JFCB
OLDMEMBR DC    CL8' '
NEWMEMBR DC    CL8' '
NOTFNDMS DS    0CL99
         DC    C'0IZG022I MEMBER NAME XXXXXXXX NOT FOUND ON DIRECTORY O*
               F '
         ORG   NOTFNDMS+21
NOTFNDNM DS    CL8
         ORG
NOTFNDLB DC    CL44' '
RENAMMS  DS    0CL103
         DC    C'0IZG021I MEMBER NAME XXXXXXXX HAS BEEN RENAMED XXXXXXX*
               X ON '
         ORG   RENAMMS+21
RENAMNM  DS    CL8
         ORG
         ORG   RENAMMS+47
RENAMNEW DS    CL8
         ORG
RENAMLIB DC    CL44' '
ERR2MSG  DC    C'0IZG023I I/O ERROR ATTEMPTING TO UPDATE DIRECTORY OF '
ERR2DSN  DC    CL44' '
DUPMESG  DC    C'0IZG024I NEWNAME XXXXXXXX PRESENTLY EXISTS ON DIRECTOR*
               Y OF '
         ORG   DUPMESG+17
DUPNEWNM DS    CL8
         ORG
DUPDSN   DC    CL44' '
PDS      DCB   DDNAME=PDS,                                             *
               DSORG=PO,                                               *
               MACRF=W,                                                *
               EXLST=PDSEXLST
PASSAREA PASSAREA
         END
