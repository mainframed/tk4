HEADING  CSECT
         PRIME ,                        INITIALIZE PGM
         L     R10,0(R1)                LOAD PASSAREA ADDRESS
         USING PASSAREA,R10
         L     R2,DCBADDR               GET DCB ADDRESS
BRANCH   NOP   BYPASS                   BYPASS CALL AFTER FIRST
         CALL  SYSDATE,DATE             GET THE DATE
         MVI   BRANCH+1,X'F0'           MAKE NOP A BRANCH
BYPASS   DS    0H
         AP    PAGENO,=PL1'1'           ADD ONE TO THE PAGE COUNT
         MVC   PAGECNT-1(L'EDMASK),EDMASK    MOVE THE EDIT MASK
         ED    PAGECNT-1(L'EDMASK),PAGENO EDIT THE PAGE COUNT
         LR    R1,R2                    GET DCB ADDRESS
         PUT   (1),HDR1                 PUT THE HEADING
         LR    R1,R2                    GET DCB ADDR
         PUT   (1),BLANK                PUT A BLANK LINE
         LA    R15,50                   PUT NEW LINECOUNT IN R15
         TERME
         EJECT
WORK     DS    F
EDMASK   DC    X'40202120'
HDR1     DS    0CL121
         DC    CL1'1',CL3' '
DATE     DC    CL23' '
         DC    CL17' '
         DC    C'COMPRESSED SOURCE LIBRARY SYSTEM'
         DC    CL25' '
         DC    CL10' 2.4 SEP78'         MVS - AUTHORIZE EVERYTHING
         DC    CL2' '
         DC    C'PAGE '
PAGECNT  DC    CL3'  1'
BLANK    DC    CL121'0'
PASSAREA PASSAREA
         END
