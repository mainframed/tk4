SYSDATE  CSECT
         PRIME
         USING PASSAREA,R10            PROVIDE BASE FOR DSECT
         L     R10,0(R1)               LOAD FOR DSECT BASE
         MVI   PASSAREA,X'40'          BLANK THE USER
         MVC   PASSAREA+1(22),PASSAREA PASS AREA
         MVI   COMMA1,C','             MOVE COMMA AFTER WEEK DAY
         MVI   COMMA2,C','             MOVE COMMA AFTER MONTH DAY
         MVC   PA19(2),=C'19'          MOVE THE CENTURY
         TIME  DEC                     GET THE DATE IN DECIMAL
         ST    R1,DATE                 STORE THE DATE
         EJECT
**********************************************************************
*                                                                     *
*          * * * D A Y  O F  T H E  W E E K  S E C T I O N * * *      *
*                                                                     *
**********************************************************************
         SRL   R1,12                   SHIFT OUT THE DAY
         ST    R1,WORK+4               STORE THE YEAR
         OI    WORK+7,X'0F'            OR FOR PLUS SIGN
         CVB   R1,WORK                 CONVERT THE YEAR TO BINARY
         ST    R1,WORK+4               STORE THE BINARY YEAR
         LA    R2,71                   PUT A 71 IN R2
         SR    R1,R2                   SUBTRACT 71 FROM THE YEAR
         LR    R5,R1                   PUT RESULT IN R5
         LA    R5,2(R5)                ADD TWO TO R5
         SR    R4,R4                   CLEAR R4 FOR THE DIVISION
         LA    R3,4                    PUT A 4 IN R3
         DR    R4,R3                   DIVIDE YEAR - 71 + 2 BY 4
         AR    R1,R5                   ADD QUOTIENT TO YEAR - 71
         L     R3,DATE                 LOAD THE DATE IN R3
         SLL   R3,16                   SHIFT OUT THE YEAR
         SRL   R3,16                   RESTORE TO LOW ORDER
         ST    R3,WORK2+4              STORE THE DAY
         CVB   R3,WORK2                CONVERT THE DAY TO BINARY
         ST    R3,WORK2+4              STORE THE BINARY DAY
         AR    R3,R1                   ADD R1 TO JULIAN DAY
         LA    R4,7                    PUT A 7 IN R4
         SR    R2,R2                   CLEAR R2 FOR DIVISION
         DR    R2,R4                   DIV ADJUSTED JULIAN DAYS BY 7
         SR    R4,R4                   CLEAR R4 FOR MULTIPLICATION
         LA    R5,9                    PUT A 9 IN R5
         MR    R4,R2                   MULTIPLY REMAINDER OF DIV BY 9
         LA    R4,DAYTABLE             LOAD ADDR OF TABLE
         LA    R2,0(R5,R4)             USE R5 AS INDEX FOR TABLE
         MVC   WEEKDAY(9),0(R2)        MOVE THE DAY OF THE WEEK
      EJECT
**********************************************************************
*                                                                     *
*          * * * A L P H A  M O N T H  S E C T I O N * * *            *
*                                                                     *
**********************************************************************
         TM    WORK+7,X'03'            TEST IF LEAP YEAR (MULT OF FOUR)
         BNZ   NOLEAP                  BRANCH IF NOT A LEAP YEAR
         MVI   TABLE+2,X'1D'           MAKE FEB 29 IF LEAP
NOLEAP   DS    0H                      TABLE SEARCH ROUTINE
         LA    R7,1                    PUT A 1 IN R7
         LA    R4,TABLE                LOAD R4 WITH ADDR OF TABLE
         L     R1,WORK2+4              LOAD BINARY DAY IN R1
         SR    R6,R6                   CLEAR R6
LOOP     DS    0H                      SEARCH THRU TABLE
         IC    R6,0(R7,R4)             INSERT DAY IN R6
         SR    R1,R6                   SUBTRACT FROM JULIAN DAYS
         BNP   FOUND                   BRANCH IF NOT PLUS
         LA    R7,1(R7)                ADD ONE TO R7
         B     LOOP                    LOOP THRU TABLE
FOUND    DS    0H                      FOUND THE MONTH
         AR    R1,R6                   ADD LAST MONTH BACK TO JULIAN
         CVD   R1,WORK2                CONVERT THE DAY TO DECIMAL
         MVC   PADAY-2(4),EDITMASK     MOVE THE EDIT FIELD
         ED    PADAY-2(4),WORK2+6      EDIT THE DAY
         LA    R4,MOTABLE              LOAD ADDR OF MONTH TABLE IN R4
         SLL   R7,2                    MULTIPLY MONTH NO. BY FOUR
         AR    R4,R7                   ADD INCR TO TABLE ADDR
         MVC   PAMONTH(4),0(R4)        MOVE ALPHA MONTH TO USER AREA
         IC    R7,DATE+1               INSERT YEAR IN R7
         SLL   R7,4                    SHIFT LEFT 4 BITS FOR SIGN
         ST    R7,WORK                 STORE THE YEAR
         OI    WORK+3,X'0F'            OR FOR A PLUS SIGN
         UNPK  PAYEAR(2),WORK+2(2)     UNPAK THE YEAR
         TERME                         TERMINATE THE PROGRAM
         EJECT
***********************************************************************
*                                                                     *
*           * * * C O N S T A N T S  A N D  T A B L E S * * *         *
*                                                                     *
***********************************************************************
         DS    0D
WORK     DC    2F'0'                   WORK AREA USED FOR THE YEAR
WORK2    DC    2F'0'                   WORK AREA USED FOR THE DAY
DATE     DC    F'00'                   STORAGE FOR THE DATE IN JULIAN
EDITMASK DC    X'40202120'             DAY OF THE MONTH EDIT FIELD
         SPACE 2
TABLE    DC    X'001F1C1F1E1F1E1F1F1E1F1E1F' DAYS IN A MONTH TABLE
         SPACE 2
*          * * * A L P H A  M O N T H  T A B L E * * *                *
         SPACE 2
MOTABLE  DC    C'LDH '                 ID - LA DEPT OF HWYS
         DC    C'JAN '                 JANUARY
         DC    C'FEB '                 FEBRUARY
         DC    C'MAR '                 MARCH
         DC    C'APR '                 APRIL
         DC    C'MAY '                 MAY
         DC    C'JUN '                 JUNE
         DC    C'JUL '                 JULY
         DC    C'AUG '                 AUGUST
         DC    C'SEP '                 SEPTEMBER
         DC    C'OCT '                 OCTOBER
         DC    C'NOV '                 NOVEMBER
         DC    C'DEC '                 DECEMBER
         SPACE 2
*          * * * T A B L E  O F  D A Y S  O F  T H E  W E E K * * *   *
         SPACE 2
DAYTABLE DC    C' THURSDAY'
         DC    C'   FRIDAY'
         DC    C' SATURDAY'
         DC    C'   SUNDAY'
         DC    C'   MONDAY'
         DC    C'  TUESDAY'
         DC    C'WEDNESDAY'
         EJECT
***********************************************************************
*                                                                     *
*          * * * U S E R ' S  P A S S  A R E A * * *                  *
*                                                                     *
***********************************************************************
PASSAREA DSECT                         USER'S PASS AREA - DUMMY SECT
WEEKDAY  DS    9C                      THE DAY OF THE WEEK
COMMA1   DS    1C                      CONSTANT - COMMA
         DS    1C
PAMONTH  DS    3C                      THE THREE CHAR ALPHA MONTH
         DS    1C
PADAY    DS    2C                      THE DAY OF THE MONTH
COMMA2   DS    1C                      CONSTANT - COMMA
         DS    1C
PA19     DS    2C                      CONSTANT - 19
PAYEAR   DS    2C                      THE YEAR
         END
