         COPY  PRTGBL
         SPACE 3
         PRTOPT
         PRTLST
PRTA     TITLE '* * OS PRINT UTILITY - PRINTALL VER &VER MOD &MOD * *'
***********************************************************************
*                                                                     *
*       CSECT NAME -- PRINTALL                                        *
*       OTHER ENTRY POINTS -- NONE PRESENTLY                          *
*       LANGUAGE -- IBM 370 ASSEMBLER F                               *
*                   (NOTE) CAN BE MODIFIED FOR OS/360                 *
*       ATTRIBUTES -- REENTRANT, REUSABLE, REFRESHABLE                *
*       SUBROUTINES USED -- NONE                                      *
*       ATTACH, LINK, LOAD, OR XCTL TO -- NONE PRESENTLY              *
*       OPERATING SYSTEM -- OS/360 MVT OR MFT REL 21.6                *
*                           OS/370 SVS REL 1.7H                       *
*                           OS/370 MVS REL 3.8A                       *
*       DATE WRITTEN -- 4/20/73                                       *
*       AUTHOR -- AUGUST TREUBIG                                      *
*       INSTALLATION -- MIDDLE SOUTH SERVICES                         *
*                       200 WEST BANK EXPRESSWAY                      *
*                       GRETNA, LOUISIANA 70053                       *
*       SPECIAL CONSIDERATIONS -- THE FOLLOWING 'SPECIAL' MACROS ARE  *
*                                 USED IN THIS ASSEMBLY               *
*                                 PRIME - INITILIZATION MACRO         *
*                                 TERME - TERMINATION MACRO           *
*                                 IEFJFCBN - JFCB MAPPING MACRO       *
*                                 IECSDSL1 - DSCB MAPPING MACRO       *
*                                 PRTGBL - GEN GLOBALS COPY           *
*                                 PRTOPT - GEN OPTIONS SETUP MACRO    *
*                                 PRTLST - GEN OPTIONS LIST MACRO     *
*                                 MESSG - MESSAGE FORMAT MACRO        *
*                                                                     *
***********************************************************************
        SPACE 2
***********************************************************************
*                                                                     *
*       USER ABEND CODES -- NONE                                      *
*       RETURN CODES --                                               *
*             0  - NORMAL END OF JOB                                  *
*             8  - UNABLE TO OPEN SYSPRINT DCB                        *
*             16 - I/O ERROR ON SYSUT1 OR INSUFFICIENT CORE TO BUFFER *
*                  SYSUT1                                             *
*                                                                     *
*       SAMPLE JCL --                                                 *
*             //PRINT EXEC PGM=PRINTALL,PARM='NUMBER,SKIP'            *
*             //SYSPRINT DD SYSOUT=A                                  *
*             //SYSUT1 DD DSNAME=USER.FILE.TO.PRINT,DISP=SHR          *
*                                                                     *
***********************************************************************
        EJECT
***********************************************************************
*                                                                     *
*       VERSION 2 PRINTALL IS A REWRITE OF AN EXISTING UTILITY AT     *
*       MIDDLE SOUTH SERVICES.  THE ORIGINAL VERSION WAS WRITTEN      *
*       DURING IBM'S PCP ERA.  THE PROGRAM WAS LIMITED TO RECORDS     *
*       SHORTER THAN 7300 BYTES AND DID NOT FUNCTION AT ALL FOR       *
*       ISAM.                                                         *
*                                                                     *
*       EXECUTION LOGIC.                                              *
*             PARAMETERS ARE SCANNED AND DECODED.  THE FIRST PARAMETER*
*             IS THE NUMBER OF RECORDS TO BE PRINTED OF 'ALL'.        *
*             THE SECORD PARAMETER IS THE NUMBER OF RECORDS TO BE     *
*             SKIPPED BEFORE PRINTING.  THE NUMBERS MAY BE UP TO 15   *
*             DIGITS LONG.  IF ANY ERRORS IN THE PARAMETERS ARE       *
*             DETECTED, IE., NON-NUMERIC PARMS,  DEFAULT PARAMETERS   *
*             ARE ASSUMED.                                            *
*             DEFAULT PARAMETERS ARE DETERMINED BY THE USER AT        *
*             PRINTALL GENERATION.  CONSULT THE EXPANSION OF THE      *
*             MACRO 'PRTLST'.                                         *
*             THE HEADERS ARE SET UP TO BE PRINTED.  THE JFCB IS READ *
*             TO GET THE DSNAME AND VOLSER OF THE FILE.  A DEVICE     *
*             TYPE MACRO IS ISSUED TO DETERMINE THE TYPE OF I/O       *
*             DEVICE.  IF IT IS NOT A DISC THE FILE IS ASSUMED TO BE  *
*             SEQUENTIAL.  IF IT IS A DISC AN OBTAIN IS ISSUED TO GET *
*             THE FORMAT 1 DSCB AND THE DSORG IS MOVED TO THE DCB.    *
*             IF THE DSORG IS 'IS' THE BSAM DCB IS OVERLAID BY A      *
*             QISAM DCB.                                              *
*             THE SYSUT1 DCB IS THEN OPENED.  THE FILE INFORMATION    *
*             IN THE DCB IS THEN PLACED IT THE HEADER.  IF THE DSORG  *
*             IS NOT 'IS' THEN A 'VC' TYPE GETMAIN IS ISSUED FOR      *
*             BETWEEN THE LENGTH OF ONE AND TWO TIMES THE BLOCKSIZE.  *
*             THIS IS USED FOR BUFFER AREA.                           *
*             THE REQUIRED NUMBER OF RECORDS ARE SKIPPED.  RECORDS    *
*             ARE READ BY ISSUING CALLS TO THE 'DEBLOCK' ROUTINE.     *
*             THE CHARACTER, ZONE, AND NUMERIC REPRESENTATIONS OF THE *
*             INPUT RECORD ARE PRINTED.                               *
*             AFTER THE REQUIRED NUMBER OF RECORDS HAS BEEN PRINTED OR*
*             END OF FILE HAS BEEN REACHED, THE FILES ARE CLOSED AND  *
*             CONTROL IS RETURNED TO OS.                              *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*       DEBLOCK ROUTINE LOGIC.                                        *
*             THE DEBLOCK ROUTINE WORKS IN A GET-LOCATE MODE OF       *
*             OPERATION.  IF THE DSORG IS 'IS' THEN A QISAM GET-LOCATE*
*             IS ISSUED AND THE ADDRESS OF THE RECORD IS RETURNED IN  *
*             R1.                                                     *
*             OTHERWISE, THE BLOCK POINTER IS UPDATED BY ADDING IN THE*
*             THE CURRENT RECORD SIZE.  IF IT IS THE END OF THE BLOCK *
*             THEN IF THER ARE TWO BUFFERS A CHECK IS ISSUED AND THE  *
*             BUFFER ADDRESSES ARE SWAPPED AND A READ IS ISSUED.  IF  *
*             THER IS ONLY ONE BUFFER THEN A READ AND A CHECK ARE     *
*             ISSUED.  AFTER EITHER CHECK IS ISSHED THE RESIDUAL      *
*             COUNT FROM THE CSW IN THE SYSUT1 IOB IS SUBTRACTED FROM *
*             THE BLOCKSIZE TO FIGURE THE ACTUAL BLOCK LENGTH (IN CASE*
*             OF A SHORT BLOCK).  THE ROUTINE THE RETURNS THE RECORD  *
*             ADDRESS IN R1 AND THE LENGTH OF THE RECORD IN R0.       *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        MODIFICATION 1.                                              *
*              ADD MESSAGE AZB008I AND ASSOCIATED LOGIC.  CHECKS FOR  *
*              DATA SETS WHICH ARE ALLOCATED ON DIRECT ACCESS, BUT    *
*              HAVE NEVER BEEN OPENED FOR OUTPUT.                     *
*                                                                     *
*        MODIFICATION 2.                                              *
*              CORRECTS AN ERROR IN 'DEBLOCK' WHICH CAUSES AN S0C5 ON *
*              RECFM OF 'U'.                                          *
*              ADDITION OF LOGIC TO CHECK FOR INVALID RECORD          *
*              DESCRIPTOR WORDS IN VARIABLE LENGTH FILES. MESSAGE     *
*              AZB009I ADDED.  RELIEVES AN S0C5 CONDITION.            *
*              ADDITION OF MESSAGE AZB010I.  CHANGE OUTPUT TO PRINT   *
*              'CHARACTER RREC' ON DIRECT ACCESS FILES, (DSORG = DA). *
*                                                                     *
*        MODIFICATION 3.                                              *
*              REMOVAL OF SUBPOOL GETMAINS. ALL GETMAINS ARE NOW FOR  *
*              SUBPOOL 0.                                             *
*              ADDITION OF FREEMAINS AT PROGRAM TERMINATION TO FREE   *
*              ALL GETMAINED STORAGE.                                 *
*              ADDITION OF A DCB EXIT FOR SYSPRINT TO ALLOW THE USER  *
*              TO SPECIFY A BLOCKING FACTOR. (MULTIPLE OF 133)        *
*              ADDITION OF A DCB ABEND EXIT FOR SYSUT1 TO STOP        *
*              UNNEEDED ABENDS AND DUMPS. MESSAGE AZB011I ADDED.      *
*              ADDITION OF PARAMETER ERROR LOGIC. MESSAGE AZB012I     *
*              ADDED.                                                 *
*              ADDITION OF STSUT1 DCB EXIT TO ALLOW PRINTING OF NL    *
*              TAPES WITHOUT SPECIFING A RECFM OR BLKSIZE.            *
*                                                                     *
*        MODIFICATION 4.                                              *
*              REASSEMBLY OF PRINTALL FOR SVS                         *
*        MODIFICATION 5.                                              *
*              FIX PROBLEMS IN DCB EXITS WITH BASE REGISTERS AND SVS  *
*        MODIFICATION 5.                                              *
*              CHANGE MESSAGE ID FROM ITA (CONFLICT WITH MVS) TO AZB. *
*              CHANGE MAXIMUM BLKSIZE FROM 32767 TO 32760.            *
***********************************************************************
         EJECT
PRINTALL CSECT
         PRIME REG=(12,11)
***********************************************************************
*        DECODE PARAMETERS PASSED FROM USER                           *
***********************************************************************
         SPACE
         SR    R9,R9                    CLEAR R9
         L     R3,0(R1)                 PICK UP PARM ADDR
         LH    R4,0(R3)                 PICK UP PARM LENGTH
         LA    R3,2(R3)                 POINT AT PARM
         GETMAIN R,LV=WORKLEN           GETMAIN FOR WORK AREAS
         LR    R10,R1                   MOVE ADDR OF CORE TO BASE
         USING WORKAREA,R10             AND MAKE ADDRESSABLE
         LTR   R4,R4                    ARE THERE ANY PARMS
         BZ    DEFAULT                  NO, SET DEFAULT
         MVI   PARMTBL,255              SET UP PARM TRT TABLE
         MVC   PARMTBL+1(255),PARMTBL
         MVI   PARMCOMA,C','
         XC    PARMZERO,PARMZERO
         CLC   0(3,R3),=C'ALL'          IS IT 'ALL'
         BNE   FURTHER                  NO, PROCESS FURTHER
         LA    R5,1                     YES, SET TO MINUS 1
         LNR   R5,R5
         SH    R4,=H'3'                 SUBTRACT 3 FROM LENGTH
         BNP   DEFAULTS                 DEFAULT SKIP PARM
         CLI   3(R3),C','               IS THERE A COMMA
         BNE   PARMERR                  NO, SIGNIFY PARM ERROR
         LA    R3,4(R3)                 ADD 4 TO PARM ADDR
         B     TESTSKP                  GO TO TEST FOR SKIP
         SPACE
FURTHER  DS    0H
         LA    R15,COMATST              LOAD ERROR PTR
         BAL   R14,PACKIT               PACK NUMBER
         CVB   R5,AREA                  LOAD INTO R5
         B     DEFAULTS                 ASSUME DEFAULT SKIP
         SPACE
PACKIT   DS    0H
         SR    R2,R2                    CLEAR R2
         LR    R6,R4                    LOAD R6 WITH LENGTH
         BCTR  R6,0                     BUMP DOWN BY 1
         EX    R6,TRT                   TEST FOR NUMERIC
         BCR   4,R15                    NO, POSSIBLE ERROR
         EX    R6,PACKER                OK, PACK NUMBER
         BR    R14                      RETURN
         EJECT
COMATST  DS    0H
         AIF   (&NOT370).NOCLM
         CLM   R2,1,=C','               WAS IT A COMMA
         AGO   .NEXT1
.NOCLM   ANOP
         N     R2,=F'-256'              CLEAR GARBAGE
         CL    R2,=X'0000006B'          WAS IT A COMMA
.NEXT1   ANOP
         BNE   PARMERR
         LR    R6,R1                    PUT ADDR OF COMMA IN WORK
         SR    R6,R3                    FIGURE LENGTH (INCL ',')
         SR    R4,R6                    SUBTRACT FROM LENGTH
         BCTR  R6,0                     BUMP 1 OFF LENGTH
         EX    R6,PACKER                PACK NUMBER
         CVB   R5,AREA                  PUT IN R5
         LA    R3,1(R1)                 POINT AT NEXT PARM
         SPACE
TESTSKP  DS    0H
         LTR   R4,R4                    ARE THERE ANY MORE BYTES
         BNP   PARMERR                  NO, SIGNIFY PARM ERROR
         BCTR  R4,0                     BUMP 1 OFF LENGTH
         LA    R15,PARMERR              ERROR RETURN
         BAL   R14,PACKIT
         CVB   R7,AREA                  SAVE NO REC TO SKIP
         B     CONTINUE
         SPACE
TRT      TRT   0(0,R3),PARMTBL
PACKER   PACK  AREA,0(0,R3)
         SPACE
PARMERR  DS    0H
         LA    R9,1                     SET R9 TO
         LCR   R9,R9                    INDICATE AN ERROR
         SPACE
DEFAULT  DS    0H
         AIF   (&NUM GE 0).DEF1
         LA    R5,1                     SET DEFAULT NUMBER
         LCR   R5,R5                    TO 'ALL'
         AGO   .DEF3
.DEF1    ANOP
         AIF   (&NUM GT 4095).DEF2
         LA    R5,&NUM                  SET DEFAULT NUMBER
         AGO   .DEF3
.DEF2    ANOP
         LH    R5,=H'&NUM'              SET DEFAULT NUMBER
.DEF3    ANOP
         SPACE
DEFAULTS DS    0H
         AIF   (&SKIP NE 0).DEF4
         SR    R7,R7                    SET DEFAULT SKIP VALUE
         AGO   .DEF5
.DEF4    ANOP
         LA    R7,&SKIP                 SET DEFAULT SKIP VALUE
.DEF5    ANOP
         EJECT
***********************************************************************
*        BUILD HEADERS, FIGURE TIME AND DATE, AND OPEN SYSPRINT       *
***********************************************************************
         SPACE 2
CONTINUE DS    0H
         DROP  R10                      DROP OLD WORK AREA
         USING MAINWORK,R10             AND PICK UP NEW WORKAREA
         ST    R13,OSSAVE               SAVE ADDRESS OF MY SAVE
         STC   R9,ERRBYTE
         NI    ERRBYTE,255-BADPARM      CLEAR REST OF SWITCHES
         ST    R5,RECNO
         LA    R7,1(R7)
         ST    R7,SKIPNO
         MVI   HEAD1,C' '               CLEAR PRINT LINES
         MVC   HEAD1+1(133),HEAD1
         MVC   HEAD2+1(133),HEAD2
         MVC   PRINTLNE+1(132),PRINTLNE
         ZAP   PAGE,ZERO                ZERO PAGE COUNT
         MVI   FIL1,C'1'
         MVC   HEADREL(L'HEADTITL),HEADTITL BUILD HEADERS
         MVC   FIX1,=C'TIME - '
         MVC   FIX2,=C'DATE - '
         MVC   FIX3,=C'PAGE'
         MVI   CC1,C'0'                 SET TO DOUBLE SPACE
         MVC   DSNEQ,=C' DSNAME - '
         MVC   FIL2,=C'RECFM - '
         MVC   FIL3,=C'LRECL - '
         MVC   FIL4,=C'BLKSIZE - '
         MVC   FIL5,=C'DSORG - '
         MVC   SYSUT1,DUMUT1            MOVE DUMMY DCBS INTO
         MVC   SYSPRINT,DUMPRT          WORK AREAS
         OPEN  (SYSPRINT,OUTPUT),MF=(E,PRTOPEN)
         TM    SYSPRINT+48,X'10'        DID SYSPRINT OPEN OK
         BNO   BADPRT                   NO, BRANCH -- ERROR
         GETMAIN R,LV=TEMPLEN           GETMAIN FOR TEMP WORK
         LR    R9,R1                    LOAD ADDR OF IT IN R9
         USING JFDSECT,R9               AND MAKE ADDRESSABLE
         EJECT
         TIME  DEC                      GET TIME AND DATE
         ST    R0,ZWORK                 STORE TIME
         MVI   ZDATE,C' '               CLEAR WORK
         MVC   ZDATE+1(16),ZDATE        AREAS
         MVI   ZTIME+2,C':'             AND SET UP
         MVI   ZTIME+5,C':'             TIME AREA
         UNPK  ZWORK(7),ZWORK(4)        CHANGE TIME TO CHARACTER
         MVC   ZTIME(2),ZWORK
         MVC   ZTIME+3(2),ZWORK+2
         MVC   ZTIME+6(2),ZWORK+4
         SR    R0,R0                    CLEAR R0
         STM   R0,R1,ZWORK              PUT YR AND DATE IN WORK AREA
         UNPK  ZDATE+7(3),ZWORK+5(2)    CONVERT YR TO CHARACTER
         MVC   ZTAB(60),TEMPTAB         MOVE MONTH TABLE TO WORK AREA
         CVB   R1,ZWORK                 CONVERT TO BINARY
         D     R0,=F'1000'              YR IN R1, DAYS IN R0
         BCTR  R1,0                     YR-1
         N     R1,=F'3'                 (YR-1) MOD 4
         IC    R1,ZFEBTAB(R1)           28 OR 29 DAYS IN FEB
         STC   R1,ZTAB+4
         LA    R1,ZTAB-4                POINT AT MONTH TABLE
         SR    R15,R15                  CLEAR R15
         SPACE
ZLOOP    DS    0H
         LA    R1,4(R1)                 POINT AT NEXT MONTH
         IC    R15,0(R1)                PICK UP NO DAYS IN MONTH
         SR    R0,R15                   SUBTRACT
         BH    ZLOOP                    DONE IF 0 OR NEGATIVE
         SPACE
         AR    R0,R15                   ADD BACK LAST MONTH
         CVD   R0,ZWORK                 CONVERT DAYS TO DECIMAL
         MVC   ZDATE-2(4),=X'40202120'  AND EDIT
         ED    ZDATE-2(4),ZWORK+6       DATE INTO
         MVC   ZDATE+3(3),1(R1)         THE WORK AREA
         MVC   HTIME,ZTIME              PUT TIME INTO PRINT LINE
         MVC   HDATE,ZDATE              PUT DATE IN PRINT LINE
         EJECT
***********************************************************************
*        READ JFCB AND ISSUE A DEVTYPE TO DETERMINE THE TYPE OF       *
*        DEVICE THAT THE FILE RESIDES ON AND OPEN SYSUT1              *
***********************************************************************
         SPACE 2
         LA    R1,JFCB                  BUILD JFCB
         LA    R2,UT1EXIT               AND DCB EXIT
         LA    R3,UT1ABEND              AND DCB ABEND EXIT
         AIF   (&NOT370).NOICM1
         ICM   R1,8,=X'07'              EXIT LIST FOR
         ICM   R2,8,=X'05'
         ICM   R3,8,=X'91'
         AGO   .NEXT2
.NOICM1  ANOP
         O     R1,=X'07000000'          EXIT LIST FOR
         O     R2,=X'05000000'
         O     R3,=X'91000000'
.NEXT2   ANOP
         STM   R1,R3,EXLIST             SYSUT1 DCB
         LA    R8,SYSUT1                MOVE ADDRESS
         USING IHADCB,R8                OF EXIT LIST
         LA    R1,EXLIST                INTO THE
         ST    R1,DCBEXLST              SYSUT1 DCB
         RDJFCB (SYSUT1,INPUT),MF=(E,UT1OPEN)
         LTR   R15,R15                  * * VALID ONLY IN REL 21
         BNZ   ERR01                    UNABLE TO READ UT1 JFCB
         MVC   DSNAME(44),INFMJFCB      MOVE DSNAME TO PRINT LINE
         DEVTYPE DCBDDNAM,FMT1CAM       FIND OUT WHAT UT1 IS
         CLI   FMT1CAM+2,X'20'          IS IT A DASD DEVICE
         BNE   SEQDEV                   NO, IT MUST BE SEQUENTIAL
         EJECT
***********************************************************************
*        DETERMINE DSORG AND RECORD FORMAT FOR PRINT LINE             *
*                                                                     *
*        * * * (NOTE): THIS LOGIC CONTAINS LOCATION DEPENDENT CODE    *
***********************************************************************
         SPACE 2
         MVC   FMT1CAM,DUMCAM           MOVE DUMMY INTO WORK AREA
         LA    R1,JFCBDSNM              SET UP
         LA    R2,JFCBVOLS              CAMLST
         LA    R3,DS1FMTID              FOR OBTAIN
         STM   R1,R3,FMT1CAM+4
         OBTAIN FMT1CAM                 OBTAIN FORMAT 1 DSCB
         LTR   R15,R15                  WAS THE DSCB IN THE VTOC
         BNZ   ERR02                    NO, BRANCH -- ERROR
         SR    R1,R1                     CLEAR R1
         IC    R1,=X'80'                SET R1 TO INITIAL MASK
         LA    R3,DSORGTAB              LOAD PTR TO STARTING VALUE
         LA    R4,2                      LOAD INCREMENT
         LA    R5,DSORGLEN(R0,R3)        LOAD TEST VALUE
         SPACE
MASKLOOP DS    0H
         EX    R1,TESTMASK              TEST THE CURRENT BIT
         BO    GOTDSORG                 BRANCH ON A HIT
         SRL   R1,1                     NO HIT, SHIF TEST BIT
         BXLE  R3,R4,MASKLOOP           IF NO DSORG, FALL THRU
         SPACE
         MVC   DSOR,=C'UND'             ASSUME DSORG IS UNDEFINED
         B     DSORGEND                 CONTINUE PROCESSING
         SPACE
TESTMASK TM    DS1DSORG,0               EXECUTED TEST MASK
         SPACE
GOTDSORG DS    0H
         MVC   DSOR(2),0(R3)            MOVE IN ALPHA DSORG
         SPACE
DSORGEND DS    0H
         TM    DS1DSORG,X'01'           IS DS UNMOVABLE
         BNO   *+8
         MVI   DSOR+2,C'U'
         TM    DS1DSORG,X'20'           IS DSORG = DA
         BO    *+10                     YES, LEAVE DSORG = PS
         MVC   DCBDSORG,DS1DSORG        PUT DSORG IN UT1 DCB
         MVC   DSO,DS1DSORG             MOVE DSORG FROM DSCB
         TM    DCBDSORG,X'80'           IS DSORG = IS
         BNO   *+10                     NO, BRANCH
         MVC   SYSUT1,ISAMUT1           BUILD ISAM DCB
         B     NOTSEQ
         EJECT
SEQDEV   DS    0H
         MVC   DSO,DCBDSORG             MOVE DSORG FROM DCB
         MVC   DSOR(2),=C'PS'           PUT 'PS' IN PRINT LINE
         TM    JFCBLTYP,JFCBLP+JFCNL    IS THIS BLP OR NL PROCESSING
         BZ    NOTSEQ                   NO, BRANCH
         OI    ERRBYTE,NOLABL           YSE, SET NO-LABEL SWITCH
         SPACE
NOTSEQ   DS    0H
         OPEN  MF=(E,UT1OPEN)           OPEN UT DCB
         TM    ERRBYTE,ABEND            DID A SYSUT1 ABEND OCCUR
         BO    FINAL                    YES, PUNT ----->
         TM    SYSUT1+48,X'10'          DID UT1 OPEN
         BNO   ERR03                    NO, BRANCH -- ERROR
         LA    R1,REC
         TM    DCBRECFM,X'C0'           IS IT UNDEFINED
         BO    UNDEF                    YES, BRANCH
         TM    DCBRECFM,X'80'           IS IT FIXED
         BNO   *+16
         MVI   0(R1),C'F'
         LA    R1,1(R1)
         B     BLKTST
         TM    DCBRECFM,X'40'           IS IT VARIABLE
         BNO   *+16
         MVI   0(R1),C'V'
         LA    R1,1(R1)
         B     BLKTST
         SPACE
UNDEF    DS    0H
         MVI   0(R1),C'U'               ASSUME UNDEFINED
         B     FINTST
         SPACE
BLKTST   DS    0H
         TM    DCBRECFM,X'10'           IS IT BLOCKED
         BNO   *+12
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    DCBRECFM,X'08'           IS IT STANDARD
         BNO   *+12
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    DCBRECFM,X'04'           IS CC = ASA
         BNO   *+12
         MVI   0(R1),C'A'
         B     FINTST
         TM    DCBRECFM,X'02'           IS CC = MACHINE
         BNO   *+8
         MVI   0(R1),C'M'
         EJECT
FINTST   DS    0H
         LH    R1,DCBLRECL              PICK UP LRECL
         CVD   R1,WRKA                  CONVERT TO DECIMAL
         MVC   LREC-1(6),=X'402020202120'   MOVE EDIT MASK
         ED    LREC-1(6),WRKA+5         EDIT THE LRECL
         LH    R1,DCBBLKSI              PICK UP BLKSIZE
         CVD   R1,WRKA                  CONVERT TO DECIMAL
         MVC   BLKS-1(6),=X'402020202120'  MOVE THE EDIT MASK
         ED    BLKS-1(6),WRKA+5         EDIT THE BLKSIZE
         MVC   BLK,DCBBLKSI             SAVE DCB
         MVC   RECS,DCBLRECL            PARAMETERS
         MVC   SAVERFM,DCBRECFM         SAVE RECFM
         NI    SAVERFM,X'C0'            CLEAR ALL BUT FECORD FORMAT
         DROP  R8                       FORGET UT1 DCB
         FREEMAIN R,LV=TEMPLEN,A=(R9)   FREE TEMP WORK
         DROP  R9
         BAL   R14,HEADING              PUT OUT HEADERS
         STH   R1,LINECT                INITIALIZE LINE COUNT
         ZAP   RECCNT,ZERO              ZERO PAGE COUNT
         CLC   DSOR(3),=C'UND'          IS DSORG UNDEFINED
         BNE   NOTUNDEF                 NO, BRANCH
         LA    R1,AZB8                  YES, WARN USER
         BAL   R14,MSGWTR
         SPACE
NOTUNDEF DS    0H
         TM    ERRBYTE,BADPARM          WAS THERE A PARM ERROR
         BZ    GOODPARM                 NO, BRANCH
         LA    R1,AZB12                 YES, TELL USER HE GOOFED
         BAL   R14,MSGWTR
         SPACE
GOODPARM DS    0H
         TM    DSO,X'80'                IS DSORG = IS
         BO    GOON2                    YES, BRANCH
         TM    DSO,X'20'                IS DSORG = 'DA'
         BNO   NOTDA                    NO, BRANCH
         SP    RECCNT,=P'1'             MAKE RECCNT RELATIVE
         LA    R1,AZB10                 PRINT WARNING TO USER
         BAL   R14,MSGWTR
         EJECT
***********************************************************************
*        GET CORE FOR BUFFERS IF FILE IS NOT ISAM AND ISSUE FIRST     *
*        READ                                                         *
***********************************************************************
         SPACE 2
NOTDA    DS    0H
         XC    LREAD(20),LREAD          CLEAR DECB
         MVC   BLKWRK,BLK               FORCE CHECK AND READ LATER
         LH    R1,BLK                   PICK UP BLOCK SIZE
         LA    R1,7(R0,R1)              ADD 7 TO BLKSIZE
         N     R1,=F'-8'                AND ROUND TO DOUBLE WORD
         LR    R8,R1                    SAVE ROUNDED BLKSIZE FOR LATER
         ST    R1,CORE                  STORE IN MINIMUM NEEDED
         AR    R1,R1                    DOUBLE IT
         ST    R1,MAX                   AND STORE IT IN MAX NEEDED
         GETMAIN VC,LA=CORE,A=ANSW,SP=0,MF=(E,GETMLIST)
         LTR   R15,R15                  DID OS HAVE ENOUGH FOR ONE BUFF
         BNZ   ERR04                    NO, BRANCH -- ERROR
         L     R1,LEN                   YES, PROCESS
         L     R2,MAX                   WAS THERE ENOUGH
         CR    R1,R2                    FOR TWO BUFFERS
         BNL   TWOBUFF                  YES, BRANCH
         FREEMAIN V,MF=(E,GETMLIST)     FREE OLD BUFFER
         ST    R8,MAX                   SET MAX = CORE
         GETMAIN VC,MF=(E,GETMLIST)     GET ONE BUFFER
         LTR   R15,R15                  DID GETMAIN SUCEED
         BNZ   ERR04                    NO, BRANCH
         L     R8,ANSW                  MAKE BOTH R7 & R8
         LR    R7,R8                    POINT TO THE SAME BUFFER
         LA    R1,AZB5
         BAL   R14,MSGWTR
         B     GOON2
         SPACE
TWOBUFF  DS    0H
         L     R7,ANSW                  POINT R7 AT FIRST
         AR    R8,R7                    R8 AT THE SECOND
         SPACE
GOON     DS    0H
         READ  LREAD,SF,SYSUT1,(R8),'S',MF=E
         EJECT
***********************************************************************
*        PROCESS SKIP AND PRINT REQUIRED RECORDS                      *
***********************************************************************
         SPACE 2
GOON2    DS    0H
         L     R5,SKIPNO                GET NO RECORDS TO SKIP
         BAL   R14,DEBLOCK              SKIP A RECORD
         BCT   R5,*-4                   BUMP AND BRANCH
         SPACE
MAINLOOP DS    0H
         LR    R2,R0                    MOVE LRECL TO R2
         LR    R3,R1                    MOVE ADDRESS OF BUFFER TO R3
         ZAP   OFFSET,ZERO              ZERO OFFSET
         SPACE
NEXTLINE DS    0H
         CH    R2,=H'100'               IS LENGTH GT 100
         BH    *+10                     YES, BRANCH
         LR    R4,R2                    LOAD REMAINING LENGTH IN R4
         B     *+8                      SKIP NEXT INST
         LA    R4,100                   SET LENGTH TO 100
         BCTR  R4,0                     BUMP OFF 1 TO SET UP FOR EX
         LH    R1,LINECT                LOAD LINE COUNT
         BCT   R1,*+8                   BUMP DOWN AND TEST
         BAL   R14,HEADING
         STH   R1,LINECT                SAVE LINE COUNT
         EX    R4,PRTMVC                MOVE DATA TO PRINT LINE
         LA    R1,AREATAB               CLEAN UP
         EX    R4,TRINST                CHARACTER PRINT
         MVC   FILL1,RECMES             MOVE MESSAGE TO PRINT LINE
         TM    DSO,X'20'                IS DSORG = 'DA'
         BNO   *+8                      NO, SKIP MVI BELOW
         MVI   FILL1+11,C'R'            MAKE LREC NOW RREC
         UNPK  LREC1,RECCNT             UNPACK RECORD NUMBER
         OI    LREC1+5,X'F0'            INTO THE PRINT LINE
         LA    R0,PRINTLNE              PRINT CHARACTER
         BAL   R14,PUTPRT               LINE
         EX    R4,PRTMVC                MOVE DATA TO PRINT LINE
         LA    R1,HEX1TAB               CONVERT TO ZONES
         EX    R4,TRINST                IN PRINTABLE FORMAT
         MVC   FILL1,=CL16' HEX ZONES'
         MVC   LREC1(6),LREC1-1
         LA    R0,PRINTLNE              PRINT HEX1
         BAL   R14,PUTPRT               LINE
         EX    R4,PRTMVC                MOVE DATA TO PRINT LINE
         LA    R1,HEX2TAB               CONVERT TO NUMERICS
         EX    R4,TRINST                IN PRINTABLE FORMAT
         MVC   FILL1,=CL16' HEX DIGIT '
         LA    R0,PRINTLNE              PRINT HEX2
         BAL   R14,PUTPRT               LINE
         MVC   PRINTLNE+1(15),PRINTLNE  BLANK FIRST 16 BYTES
         MVC   FILL2(6),=X'402020202020' PUT OFFSET INTO PRINT LINE
         ED    FILL2(6),OFFSET
         AP    OFFSET,=P'100'           BUMP OFFSET BY 100
         MVC   MAIN(104),GARBAGE        MOVE SUB TITLE TO PRINT LINE
         LA    R0,PRINTLNE              PRINT LAST
         BAL   R14,PUTPRT               LINE
         MVC   PRINTLNE+1(132),PRINTLNE BLANK PRINT LINE
         LA    R3,1(R3,R4)              BUMP UP RECORD POINTER
         SH    R2,=H'100'               SUBTRACT 100 FROM LRECL
         BP    NEXTLINE                 IS IT STILL POSITIVE
         L     R1,RECNO                 LOAD RECORD COUNT
         BCT   R1,*+8                   BUMP DOWN AND TEST
         B     WRAPUP2                  FINISHDE
         ST    R1,RECNO                 SAVE CURRENT REC COUNT
         BAL   R14,DEBLOCK              GET A RECORD
         B     MAINLOOP                 AND CONTINUE
         SPACE
WRAPUP2  DS    0H
         LA    R1,AZB6
         BAL   R14,MSGWTR
         B     CLOSEF
         EJECT
***********************************************************************
*        DEBLOCK ROUTINE FOR DEBLOCKING BLOCKED RECORDS               *
***********************************************************************
         SPACE 2
DEBLOCK  DS    0H
         ST    R14,SAVE14               SAVE R14
         TM    DSO,X'80'                IS DSORG = IS
         BO    ISAMREAD                 YES, BRANCH
         LH    R1,BLKWRK                PICK UP BLOCK POINTER
         AH    R1,RECS                  ADD IN RECORD SIZE
         CH    R1,BLK                   IS IT THE IND OF THE BLOCK
         BL    NOREAD                   NO, PROCESS
         LA    R6,LREAD                 YES, ISSUE NEXT READ
         SPACE
CKREAD   DS    0H
         CR    R7,R8                    IS THERE ONLY ONE BUFFER
         BE    READ                     YES, BRANCH
         CHECK (R6)                     NO, CHECK LAST READ
         BAL   R14,BLKCK                FIGURE BLOCKSIZE
         XR    R7,R8                    SWAP BUFFER
         XR    R8,R7                    ADDRESSES
         XR    R7,R8                    IN R7 AND R8
         SPACE
READ     DS    0H
         READ  (R6),SF,SYSUT1,(R8),'S',MF=E
         CR    R7,R8                    IS THERE ONLY ONE BUFFER
         BNE   RECCK                    NO, CLEAR BLOCK COUNT
         CHECK (R6)                     YES, CHECK LAST READ
         BAL   R14,BLKCK                FIGURE BLOCKSIZE
         EJECT
RECCK    DS    0H
         CLI   SAVERFM,X'40'            IS RECFM = V
         BNE   CLEAR                    NO, BRANCH
         TM    SYSUT1+(DCBRECFM-IHADCB),X'10' IS RECFM = VB
         BZ    CLEAR                    NO, JUST V, BRANCH
         LA    R1,4                     YES, MAKE BLOCK POINTER = 4
         B     NOREAD                   CONTINUE
         SPACE
CLEAR    DS    0H
         SR    R1,R1                    ZERO BLOCK POINTER
         SPACE
NOREAD   DS    0H
         STH   R1,BLKWRK                SAVE UPDATED BLOCK POINTER
         AR    R1,R7                    POINT R1 AT NEW RECORD
         SPACE
ISAMRET  DS    0H
         CLI   SAVERFM,X'40'            IS RECFM = V
         BNE   NOTVAR                   NO, BRANCH
         AIF   (&NOT370).NOLH
         LH    R0,0(R1)                 PICK UP REC LEN FROM RECORD
         STH   R0,RECS                  STORE RECORD LENGTH
         AGO   .NEXT4
.NOLH    ANOP
         MVC   RECS,0(R1)               MOVE RECORD LENGTH TO TEMP
         LH    R0,RECS                  LOAD IN R0
.NEXT4   ANOP
         SH    R0,=H'4'                 SUBTRACT 4 FROM REC LENGTH
         LTR   R0,R0                    IS LENGTH VALID FOR TIS REC
         BNH   BADRDW                   NO, BRANCH
         LA    R1,4(R1)                 BUMP OVER RECORD LENGTH
         B     VARRET
         SPACE
NOTVAR   DS    0H
         LH    R0,RECS                  R0 HAS LRECL
         SPACE
VARRET   DS    0H
         AP    RECCNT,=P'1'
         L     R14,SAVE14               RESTORE R14
         BR    R14                      RETURN
         SPACE
ISAMREAD DS    0H
         GET   SYSUT1                   GET A RECORD FROM ISAM
         B     ISAMRET
         SPACE
BADRDW   DS    0H
         LA    R1,AZB9                  PRINT BAD RDW MESSAGE
         BAL   R14,MSGWTR
         B     CKREAD
         EJECT
***********************************************************************
*        FIGURE LENGTH OF ACTUAL BLOCK READ                           *
***********************************************************************
         SPACE 2
BLKCK    DS    0H
         TM    ERRBYTE,ABEND            DID A SYSUT1 ABEND OCCUR
         BO    FINAL                    YES, PUNT ----->
         L     R2,SYSUT1+(DCBIOBA-IHADCB) LOAD IOB PREFIX ADDRESS
         LH    R1,SYSUT1+(DCBBLKSI-IHADCB) LOAD BLOCKSIZE
         SH    R1,22(R0,R2)             SUBTRACT RESIDUAL COUNT
         STH   R1,BLK                   AND STORE IN BLOCK LENGTH
         TM    SAVERFM,X'C0'            IS RECFM = 'U'
         BNOR  R14                      NO, RETURN
         STH   R1,RECS                  SET RECORD LENGTH
         BR    R14                      RETURN
         EJECT
***********************************************************************
*        HEADINGS AND SYNAD EXIT ROUTINES                             *
***********************************************************************
         SPACE
         SPACE 2
HEADING  DS    0H
         ST    R14,SAVE14               SAVE R14
         AP    PAGE,=PL3'1'             BUMP PAGE COUNT
         MVC   PAGNO(6),=X'402020202021'
         ED    PAGNO(6),PAGE            EDIT PAGE NO INTO PRINT LINE
         LA    R0,HEAD1                 BOINT AT HEAD1 BUFFER
         BAL   R14,PUTPRT               PRINT THE LINE
         LA    R0,HEAD2                 POINT AT HEAD2 BUFFER
         BAL   R14,PUTPRT               PRINT THE LINE
         LA    R1,11                    RESTORE THE LINE COUNT
         L     R14,SAVE14               RESTORE R14
         BR    R14                      RETURN
         SPACE
PUTPRT   DS    0H
         ST    R14,SAVE14A              SAVE R14
         PUT   SYSPRINT                 PRINT THE CALLERS LINE
         L     R14,SAVE14A              RESTORE R14
         BR    R14                      RETURN
         SPACE
SYNADX   DS    0H
         SYNADAF ACSMETH=BSAM           GET SYNAD MESSAGE
         B     COMSYNAD
         SPACE
ISAMSYN  DS    0H
         SYNADAF ACSMETH=QISAM
         EJECT
COMSYNAD DS    0H
         MVC   PRINTLNE(19),=C'-AZB098I I/O ERROR '
         MVC   PRINTLNE+19(62),67(R1)   SET UP PRINT LINE
         LA    R0,PRINTLNE
         BAL   R14,PUTPRT               PUT PRINT LINE
         SYNADRLS                       RELEASE SYNAD BUFFER
         LA    R15,16                   RETURN CODE = 16
         B     FINAL
         EJECT
***********************************************************************
*        DCB EXIT FOR SYSPRINT AND ABEND EXIT FOR SYSUT1              *
***********************************************************************
         SPACE
PRTEXIT  DS    0H
         USING *,R15                    ADDRESS EXIT
         USING IHADCB,R1
         CLC   DCBBLKSI,=AL2(0)         DID USER SUPPLY A BLKSIZE
         BNER  R14                      YES, RETURN
         MVC   DCBBLKSI,=AL2(1729)      NO, SET TO OPTIMUM FOR 3330'S
         BR    R14                      RETURN
         DROP  R1,R15
         SPACE
UT1ABEND DS    0H
         USING *,R15                    ADDRESS EXIT
         UNPK  WRKA,0(4,R1)             UNPACK ABEND CODE
         MVC   AZBMESS,AZB11            MOVE MESSAGE TO WORK AREA
         TR    WRKA,HEX2TAB             TRANSLATE ABEND CODE
         MVC   AZBMESS+16(3),WRKA+1     MOVE ABEND CODE TO WORK AREA
         MVC   AZBMESS+20(2),WRKA+5     MOVE RC TO MESSAGE
         MVI   3(R1),4                  SET TO IGNORE THE ABEND
         ST    R1,SAVE1                 SAVE R1
         ST    R14,SAVE14B              SAVE R14
         CP    PAGE,ZERO                HAVE WE PUT OUT A HEADER
         BNE   NOHEAD                   YES, BRANCH
         BAL   R14,HEADING
         SPACE
NOHEAD   DS    0H
         OI    ERRBYTE,ABEND            INDICATE AN ABEND IS OCCURING
         LA    R1,AZBMESS               PRINT ABEND
         BAL   R14,MSGWTR               MESSAGE
         L     R1,SAVE1                 RESTORE R1
         L     R14,SAVE14B              RESTORE R14
         BR    R14                      RETURN
         DROP  R15
         SPACE
***********************************************************************
*        SYSUT1 DCB EXIT TO CHECK FOR NL OR BLP TAPES                 *
***********************************************************************
         SPACE
UT1EXIT  DS    0H
         USING *,R15                    ADDRESS EXIT
         USING IHADCB,R1
         TM    ERRBYTE,NOLABL           IS THIS NO-LABEL PROCESSING
         BER   R14                      NO, RETURN
         CLI   DCBRECFM,0               IS THERE A RECFM NOW
         BNER  R14                      YES, RETURN
         MVI   DCBRECFM,JFCUND          NO, SET RECFM = 'U'
         MVC   DCBBLKSI,=AL2(32760)     SET MAXIMUM TAPE BLKSIZE
         BR    R14                      RETURN
         DROP  R1,R15
         EJECT
***********************************************************************
*        MESSAGE AND ERROR PRINT ROUTINES                             *
***********************************************************************
         SPACE 2
BADPRT   WTO   'AZB099I UNABLE TO OPEN SYSPRINT DATA SET',ROUTCDE=11
         LA    R15,8
         B     FINAL
         SPACE
ERR01    DS    0H
         LA    R1,AZB1
         B     MESSROUT
         SPACE
ERR02    DS    0H
         LA    R1,AZB2
         B     MESSROUT
         SPACE
ERR03    DS    0H
         LA    R1,AZB3
         B     MESSROUT
         SPACE
ERR04    DS    0H
         LA    R1,AZB4
         B     MESSROUT
         EJECT
MESSROUT DS    0H
         BAL   R14,MSGWTR
         LA    R15,16
         B     FINAL
         SPACE
MSGWTR   DS    0H
         ST    R14,SAVE14A              SAVE R14
         MVC   PRINTLNE(8),=C'0AZBXXXI' MOVE IN DUMMY MESSAGE NUMBER
         XR    R0,R0                    CLEAR R0
         IC    R0,0(R1)                 INSERT MESSAGE NUMBER
         CVD   R0,WRKA                  CONVERT TO DECIMAL
         UNPK  PRINTLNE+4(3),WRKA+6(2)  AND PUT INTO
         OI    PRINTLNE+6,X'F0'         PRINT LINE
         XR    R2,R2                    CLEAR R2
         IC    R2,1(R1)                 INSERT LENGTH
         EX    R2,WTMVC                 MOVE MESSAGE TO PRINT LINE
         PUT   SYSPRINT,PRINTLNE        PRINT MESSAGE
         MVI   PRINTLNE,C' '            BLANK OUT PRINT
         MVC   PRINTLNE+1(132),PRINTLNE LINE AFTER ISSUING MESSAGE
         L     R14,SAVE14A             RESTORE R14
         BR    R14                      RETURN
         EJECT
***********************************************************************
*        WRAPUP AND FINAL PHASE ROUTINE                               *
***********************************************************************
         SPACE 2
WRAPUP   DS    0H
         LA    R1,AZB7
         BAL   R14,MSGWTR
         SPACE
CLOSEF   DS    0H
         CLOSE (,REWIND),MF=(E,UT1OPEN)
         CLOSE (SYSPRINT,LEAVE),MF=(E,PRTOPEN)
         TM    DSO,X'80'                WAS THE DSORG = 'IS'
         BO    NOFREE                   YES, DON'T ISSUE FREMAIN
         FREEMAIN V,MF=(E,GETMLIST)     FREE BUFFERS
         SPACE
NOFREE   DS    0H
         FREEPOOL SYSPRINT              FREE PRINT BUFFERS
         SR    R15,R15                  SET RC = 0
         EJECT
FINAL    DS    0H
         L     R13,OSSAVE               RESTORE MY SAVE AREA
         FREEMAIN R,LV=WORKLEN,A=(R10)  FREE WORK AREA
         TERME                          RETURN TO O/S WITH A SMILE
         SPACE
PRTMVC   MVC   MAIN(0),0(R3)
         SPACE
TRINST   TR    MAIN(0),0(R1)
         EJECT
***********************************************************************
*        DUMMY DCBS FOR OPENS                                         *
***********************************************************************
         SPACE 2
DUMUT1   DCB   DDNAME=SYSUT1,                                          $
               DSORG=PS,                                               $
               MACRF=(R),                                              $
               EODAD=WRAPUP,                                           $
               SYNAD=SYNADX
         EJECT
ISAMUT1  DCB   DDNAME=SYSUT1,                                          $
               DSORG=IS,                                               $
               MACRF=(GL),                                             $
               EODAD=WRAPUP,                                           $
               SYNAD=ISAMSYN
         EJECT
DUMPRT   DCB   DDNAME=SYSPRINT,                                        $
               DSORG=PS,                                               $
               MACRF=(PM),                                             $
               EROPT=ACC,                                              $
               RECFM=FBA,                                              $
               LRECL=133,                                              $
               EXLST=PRTLIST
DUMEQU   EQU   *
         EJECT
***********************************************************************
*        ERROR MESSAGES AND CONSTANTS                                 *
*                                                                     *
*        * * * (NOTE): THE ORDER OF THESE MESSAGES DETERMINE THE      *
*              MESSAGE NUMBER, ADD NEW MESSAGES AT THE END            *
***********************************************************************
         SPACE 2
         MESSG 'UNABLE TO READ SYSUT1 JFCB'
         MESSG 'DATA SET NOT ON VOLUME'
         MESSG 'UNABLE TO OPEN SYSUT1 DATA SET'
         MESSG 'UNABLE TO ALLOCATE SYSUT1 BUFFERS'
         MESSG 'UNABLE TO ALLOCATE MORE THAN ONE SYSUT1 BUFFER'
         MESSG 'REQUIRED NUMBER OF RECORDS HAS BEEN PRINTED'
         MESSG 'END OF FILE ON SDS OR MEMBER'
         MESSG 'WARNING, DATA SET ALLOCATED BUT NOT USED'
         MESSG 'RDW INVALID, REST OF BLOCK SKIPPED'
         MESSG 'RECORD COUNT IS RELATIVE, NOT LOGICAL'
         MESSG 'SYSUT1 ABEND SXXX-XX'
AZB11LEN EQU   *-AZB11
         MESSG 'PARM ERROR, DEFAULT ASSUMED'
         EJECT
WTMVC    MVC   PRINTLNE+9(0),2(R1)
ZERO     DC    P'0'
HEADTITL DC    C'* *   OS PRINT UTILITY   VER &VER MOD &MOD   * *'
DUMCAM   CAMLST SEARCH,0,0,0
         ORG   DUMCAM+4
PRTLIST  DC    X'85',AL3(PRTEXIT)       SYSPRINT EXIT LIST
TEMPTAB  DC    AL1(31),C'JAN'
         DC    AL1(28),C'FEB'
         DC    AL1(31),C'MAR'
         DC    AL1(30),C'APR'
         DC    AL1(31),C'MAY'
         DC    AL1(30),C'JUN'
         DC    AL1(31),C'JUL'
         DC    AL1(31),C'AUG'
         DC    AL1(30),C'SEP'
         DC    AL1(31),C'OCT'
         DC    AL1(30),C'NOV'
         DC    AL1(31),C'DEC'
ZFEBTAB  DC    AL1(28,28,28,29)
DSORGTAB DC    0H'0',C'ISPSDACXCQMQPO'
DSORGLEN EQU   *-DSORGTAB
HEX1TAB  DC    16CL1'0'
         DC    16CL1'1'
         DC    16CL1'2'
         DC    16CL1'3'
         DC    16CL1'4'
         DC    16CL1'5'
         DC    16CL1'6'
         DC    16CL1'7'
         DC    16CL1'8'
         DC    16CL1'9'
         DC    16CL1'A'
         DC    16CL1'B'
         DC    16CL1'C'
         DC    16CL1'D'
         DC    16CL1'E'
         DC    16CL1'F'
HEX2TAB  DC    16CL16'0123456789ABCDEF'
AREATAB  DC    74CL1' '
         DC    CL7'Ö.<(+×&&'
         DC    9CL1' '
         DC    CL8'!$*);^-/'
         DC    CL9' '
         DC    CL5',%_>?'
         DC    CL10' '
         DC    CL6':#@''="'
         DC    CL65' '
         DC    CL9'ABCDEFGHI'
         DC    CL7' '
         DC    CL9'JKLMNOPQR'
         DC    CL8' '
         DC    CL8'STUVWXYZ'
         DC    CL6' '
         DC    CL10'0123456789'
         DC    CL6' '
GARBAGE  DC    CL50'.1  .5   .10  .15  .20  .25  .30  .35  .40  .45  .'
         DC    CL50'50  .55  .60  .65  .70  .75  .80  .85  .90  .95  .'
         DC    C'100 '
RECMES   DC    C'0CHARACTER LREC '
         LTORG
         EJECT
***********************************************************************
*        WORK AREA DSECTS USED FOR ADDRESSABILITY                     *
***********************************************************************
         SPACE 2
WORKAREA DSECT
PARMTBL  DS    0XL256
         DS    107X
PARMCOMA DS    X
         DS    132X
PARMZERO DS    XL10
         DS    6X
AREA     DS    PL8
         EJECT
MAINWORK DSECT
WRKA     DS    D
CORE     DS    F
MAX      DS    F
ANSW     DS    F
LEN      DS    F
RECNO    DS    F
SKIPNO   DS    F
SAVE1    DS    F
SAVE14B  DS    F
SAVE14A  DS    F
SAVE14   DS    F
OSSAVE   DS    F
UT1OPEN  DS    A
PRTOPEN  DS    A
EXLIST   DS    A                        JFCB EXIT
         DS    A                        DCB OPEN EXIT
         DS    A                        DCB ABEND EXIT
         READ  LREAD,SF,MF=L
GETMLIST GETMAIN VC,SP=4,MF=L
RECS     DS    H
BLK      DS    H
LINECT   DS    H
BLKWRK   DS    H
DSO      DS    X
SAVERFM  DS    X
ERRBYTE  DS    C
NOLABL   EQU   X'20'                    NO-LABEL PROCESSING SWITCH
ABEND    EQU   X'40'                    ABEND SWITCH
BADPARM  EQU   X'80'                    INVALID PARM SWITCH
PAGE     DS    PL3
RECCNT   DS    PL3
OFFSET   DS    PL3
HEAD1    DS    0CL133
FIL1     DS    C
         DS    CL10
HEADREL  DS    CL42
         DS    CL16
FIX1     DS    CL7
HTIME    DS    CL8
         DS    CL6
FIX2     DS    CL7
HDATE    DS    CL9
         DS    CL8
FIX3     DS    CL4
PAGNO    DS    CL6
         DS    CL9
HEAD2    DS    0CL133
CC1      DS    C
DSNEQ    DS    CL10
DSNAME   DS    CL44
         DS    CL4
FIL2     DS    CL8
REC      DS    CL8
FIL3     DS    CL8
LREC     DS    CL5
         DS    CL4
FIL4     DS    CL10
BLKS     DS    CL5
         DS    CL4
FIL5     DS    CL8
DSOR     DS    CL3
         DS    CL11
PRINTLNE DS    0CL133
FILL1    DS    CL16
LREC1    DS    CL6
FILL2    DS    CL7
MAIN     DS    CL100
FILL3    DS    CL4
         DS    0F
SYSUT1   DS    XL(DUMPRT-ISAMUT1)
         DS    0F
SYSPRINT DS    XL(DUMEQU-DUMPRT)
AZBMESS  DS    CL(AZB11LEN)
WORKLEN  EQU   (((*-MAINWORK)/8)+1)*8
         EJECT
JFDSECT  DSECT
ZWORK    DS    D
FMT1CAM  DS    4F
JFCB     EQU   *
         IEFJFCBN
         ORG   *-44
         IECSDSL1 (1)
         DS    CL5
ZDATE    DS    CL9
         DS    X
ZTIME    DS    CL8
ZTAB     DS    12CL5
TEMPLEN  EQU   (((*-JFDSECT)/8)+1)*8
         EJECT
         DCBD  DEVD=(DA),DSORG=(PS)
         END   PRINTALL
