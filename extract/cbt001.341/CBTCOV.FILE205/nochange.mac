NOCH     TITLE '                       PREPARE DATA FOR A COMPILE ONLY'
NOCHANGE CSECT
         PRIME
         ST    R12,BASE                 STORE BASE ADDR
         XC    SAVEAREA(72),SAVEAREA    CLEAR SAVEAREA
         L     R10,0(R1)                LOAD ADDR OF PASSAREA
         USING PASSAREA,R10
         ST    R10,LINKLIST             SAVE ADDR OF PASSAREA IN LIST
         TM    INDBITS,LISTIND          CHECK IF A LIST REQUESTED
         BNO   NOLIST                   NO - BYPASS
         MVI   EXIT4B+1,X'00'           MAKE BRANCH A NOP
         LOAD  EP=HEADING               LOAD HEADING ROUTINE
         ST    R0,HEADING               STORE ENTRY POINT ADDR
         LA    R7,45                    LOAD INITIAL LINE COUNT
         ST    R7,LINECNT               SAVE LINECOUNT
NOLIST   DS    0H                       BYPASS
         OPEN  (INPUT,OUTPUT)           OPEN THE OUTPUT DATA SET
         LA    R1,LINKLIST              GET ADDR OF LINKLIST
         LINK  EP=EXPAND,                                              *
               MF=(E,(1))               PARM ADDR IN R1
         ST    R15,CONDCODE             STORE RETURN CODE
         CLOSE INPUT                    CLOSE DATA SET
         TM    INDBITS,LISTIND          CHECK IF A LIST REQUESTED
         BNO   EOJ                      NO - DON'T DELETE PGM
        DELETE EP=HEADING               DELETE HEADING MODULE
EOJ      DS    0H                       END OF JOB
         L     R15,CONDCODE             PICK UP RETURN CODE
         TERME
         EJECT
EXIT4    DS    0H                       EXIT TO EXPAND
         STM   R14,R12,12(R13)          SAVE REGISTERS
         USING EXIT4,R15
         L     R12,BASE                 GET BASE REGISTER ADDRESS
         DROP  R15
         LA    R2,SAVEAREA              GET SAVEAREA ADDR
         ST    R2,8(R13)                CHAIN
         ST    R13,4(R2)                     SAVEAREAS
         LR    R13,R2                   GET NEW SAVEAREA ADDR
         L     R10,LINKLIST             LOAD PASSAREA ADDR
         LTR   R1,R1                    CHECK REGISTER ONE
         BZ    EXIT4END                 IF ZERO - FINISHED
         LR    R3,R1                    SAVE R1
         LR    R0,R1                    LOAD POINTER TO CARD
         PUT   INPUT                    PUT ONE RECORD
EXIT4B   B     EXIT4END                 DO NOT PRINT LIST
         L     R7,LINECNT               GET LINECOUNT
         BCT   R7,NOHEAD                KEEP LINE COUNT
         LA    R1,LINKLIST              GET ADDR OF PASS LIST
         L     R15,HEADING              GET ENTRY POINT ADDR
         BALR  R14,R15                  LINK TO HEADING ROUTINE
         LR    R7,R15                   GET NEW LINE COUNT
NOHEAD   DS    0H                       HEADING BYPASS
         ST    R7,LINECNT               STORE NEW LINE COUNT
         MVC   CARDIMAG+41(80),0(R3)    MOVE THE CARD
         L     R1,DCBADDR               LOAD DCB ADDR
         PUT   (1),CARDIMAG             PUT THE RECORD
EXIT4END DS    0H                       FINISHED
         L     R13,4(R13)               GET SAVE AREA ADDR
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         BR    R14                      RETURN
         USING IHADCB,R1
DCBEXITI DS    0H                       DCB EXIT FOR INPUT
         CLC   DCBBLKSI,=2X'00'         CHECK IF ZERO
         BNER  R14                      RETURN IF NOT ZERO
         MVC   DCBBLKSI,=H'400'         MAKE BLKSIZE 400
         BR    R14                      RETURN
         DROP  R1
         SPACE 2
         EJECT
BASE     DS    F
HEADING  DS    F
LINECNT  DS    F
CONDCODE DC    F'0'
SAVEAREA DS    18F
EXLSTIN  DC    0F'0',X'85',AL3(DCBEXITI)
LINKLIST DS    F                        PASSAREA ADDR
         DC    A(EXIT4)
CARDIMAG DC    CL133' '
INPUT    DCB   DDNAME=INPUT,                                           *
               DSORG=PS,                                               *
               MACRF=PM,                                               *
               EXLST=EXLSTIN,                                          *
               RECFM=FB,                                               *
               LRECL=80
PASSAREA PASSAREA
         DCBD  DSORG=PS,DEVD=DA
         END
