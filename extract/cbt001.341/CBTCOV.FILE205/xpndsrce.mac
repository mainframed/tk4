XPND     TITLE 'EXPAND COMPRESSED SOURCE TO ORIGINAL FORM'
EXPNDSRC START 0
********       NOTE:  THIS ROUTINE IS NOT REUSABLE ********
********       SYSUT1 IS ENQUEUED UPON FOR SHARED CONTROL
********       SYSUT2 IS ENQUEUED UPON FOR EXCLUSIVE CONTROL
***********************************************************************
R0       EQU   0                  SYMBOLIC REGISTER DEFINITION
R1       EQU   1                  SYMBOLIC REGISTER DEFINITION
R2       EQU   2                  SYMBOLIC REGISTER DEFINITION
R3       EQU   3                  SYMBOLIC REGISTER DEFINITION
R4       EQU   4                  SYMBOLIC REGISTER DEFINITION
R5       EQU   5                  SYMBOLIC REGISTER DEFINITION
R6       EQU   6                  SYMBOLIC REGISTER DEFINITION
R7       EQU   7                  SYMBOLIC REGISTER DEFINITION
R8       EQU   8                  SYMBOLIC REGISTER DEFINITION
R9       EQU   9                  SYMBOLIC REGISTER DEFINITION
RA       EQU   10                 SYMBOLIC REGISTER DEFINITION
RB       EQU   11                 SYMBOLIC REGISTER DEFINITION
RC       EQU   12                 SYMBOLIC REGISTER DEFINITION
RD       EQU   13                 SYMBOLIC REGISTER DEFINITION
RE       EQU   14                 SYMBOLIC REGISTER DEFINITION
RF       EQU   15                 SYMBOLIC REGISTER DEFINITION
***********************************************************************
         USING *,RF           TEMPORARY ADDRESSABILITY
         SAVE  (14,12)        SAVE ALL THE REGISTERS
         CNOP  0,4            FORCE TO WORD BOUNDARY
         BAL   R2,SKIPSAVE    SAVEAREA + BASE ADDRESS IN REG. 2
         DROP  RF         END TEMPORARY ADDRESSABILITY
SAVEAREA DS    18F            SAVE AREA MUST FOLLOW BAL IMMEDIATELY
***********************************************************************
SCTRTABL DC    AL1(0,1,2,0,3,4,5,0,6,7,8,0,9,10,11,0,12,13,14,0)
         DC    AL1(15,16,17,0,18,19,20,0,21,22,23,0,24,25,26,0)
         DC    AL1(27,28,29,0,30,31,32,0,33,34,35,0,36,37,38,0)
         DC    AL1(39,40,41,0,42,43,44,0,45,46,47,0,48,49,50,0)
         DC    AL1(51,52,53,0,54,55,56,0,57,58,59,0,60,61,62,0)
         DC    AL1(63,64,65,0,66,67,68,0,69,70,71,0,72,73,74,0)
         DC    AL1(75,76,77,0,78,79,80,0,81,82,83,0,84,85,86,0)
         DC    AL1(87,88,89,0,90,91,92,0,93,94,95,0,96,97,98,0)
         DC    AL1(99,100,101,0,102,103,104,0,105,106,107,0)
         DC    AL1(108,109,110,0,111,112,113,0,114,115,116,0)
         DC    AL1(117,118,119,0,120,121,122,0,123,124,125,0)
         DC    AL1(126,127,128,0,129,130,131,0,132,133,134,0)
         DC    AL1(135,136,137,0,138,139,140,0,141,142,143,0)
         DC    AL1(144,145,146,0,147,148,149,0,150,151,152,0)
         DC    AL1(153,154,155,0,156,157,158,0,159,160,161,0)
         DC    AL1(162,163,164,0,165,166,167,0,168,169,170,0)
         DC    AL1(171,172,173,0,174,175,176,0,177,178,179,0)
         DC    AL1(180,181,182,0,183,184,185,0,186,187,188,0)
         DC    AL1(189,190,191,0)  END OF SCATTER TABLE
***********************************************************************
TBSCNFLG DC    62AL1(0),AL1(HITFLAST,HITFLBLK)   TRT TABLE FOR SCAN
***********************************************************************
INTBLANK EQU   0                   INTERNAL REPRESENTATION OF BLANK
INTASTER EQU   1                   INTERNAL REPRESENTATION OF ASTERISK
MAXREGCH EQU   61                  MAXIMUM INTERNAL REP IN REGULAR SET
INTSTAST EQU   62        INTERNAL REPRESENTATION OF STRING OF ASTERISKS
INTSTBLN EQU   63        INTERNAL REPRESENTATION OF STRING OF BLANKS
***********************************************************************
TBINTEXT EQU   *         TABLE TO TRANSLATE FROM SPECIAL CODE TO EBCDIC
         DC    AL1(064)  EXTERNAL REPRESENTATION                    000
         DC    AL1(092)  EXTERNAL REPRESENTATION                    001
         DC    AL1(075)  EXTERNAL REPRESENTATION                    002
         DC    AL1(076)  EXTERNAL REPRESENTATION                    003
         DC    AL1(077)  EXTERNAL REPRESENTATION                    004
         DC    AL1(078)  EXTERNAL REPRESENTATION                    005
         DC    AL1(079)  EXTERNAL REPRESENTATION                    006
         DC    AL1(080)  EXTERNAL REPRESENTATION                    007
         DC    AL1(091)  EXTERNAL REPRESENTATION                    008
         DC    AL1(093)  EXTERNAL REPRESENTATION                    009
         DC    AL1(094)  EXTERNAL REPRESENTATION                    010
         DC    AL1(095)  EXTERNAL REPRESENTATION                    011
         DC    AL1(096)  EXTERNAL REPRESENTATION                    012
         DC    AL1(097)  EXTERNAL REPRESENTATION                    013
         DC    AL1(107)  EXTERNAL REPRESENTATION                    014
         DC    AL1(108)  EXTERNAL REPRESENTATION                    015
         DC    AL1(109)  EXTERNAL REPRESENTATION                    016
         DC    AL1(110)  EXTERNAL REPRESENTATION                    017
         DC    AL1(111)  EXTERNAL REPRESENTATION                    018
         DC    AL1(122)  EXTERNAL REPRESENTATION                    019
         DC    AL1(123)  EXTERNAL REPRESENTATION                    020
         DC    AL1(124)  EXTERNAL REPRESENTATION                    021
         DC    AL1(125)  EXTERNAL REPRESENTATION                    022
         DC    AL1(126)  EXTERNAL REPRESENTATION                    023
         DC    AL1(127)  EXTERNAL REPRESENTATION                    024
         DC    AL1(193)  EXTERNAL REPRESENTATION                    025
         DC    AL1(194)  EXTERNAL REPRESENTATION                    026
         DC    AL1(195)  EXTERNAL REPRESENTATION                    027
         DC    AL1(196)  EXTERNAL REPRESENTATION                    028
         DC    AL1(197)  EXTERNAL REPRESENTATION                    029
         DC    AL1(198)  EXTERNAL REPRESENTATION                    030
         DC    AL1(199)  EXTERNAL REPRESENTATION                    031
         DC    AL1(200)  EXTERNAL REPRESENTATION                    032
         DC    AL1(201)  EXTERNAL REPRESENTATION                    033
         DC    AL1(209)  EXTERNAL REPRESENTATION                    034
         DC    AL1(210)  EXTERNAL REPRESENTATION                    035
         DC    AL1(211)  EXTERNAL REPRESENTATION                    036
         DC    AL1(212)  EXTERNAL REPRESENTATION                    037
         DC    AL1(213)  EXTERNAL REPRESENTATION                    038
         DC    AL1(214)  EXTERNAL REPRESENTATION                    039
         DC    AL1(215)  EXTERNAL REPRESENTATION                    040
         DC    AL1(216)  EXTERNAL REPRESENTATION                    041
         DC    AL1(217)  EXTERNAL REPRESENTATION                    042
         DC    AL1(226)  EXTERNAL REPRESENTATION                    043
         DC    AL1(227)  EXTERNAL REPRESENTATION                    044
         DC    AL1(228)  EXTERNAL REPRESENTATION                    045
         DC    AL1(229)  EXTERNAL REPRESENTATION                    046
         DC    AL1(230)  EXTERNAL REPRESENTATION                    047
         DC    AL1(231)  EXTERNAL REPRESENTATION                    048
         DC    AL1(232)  EXTERNAL REPRESENTATION                    049
         DC    AL1(233)  EXTERNAL REPRESENTATION                    050
         DC    AL1(240)  EXTERNAL REPRESENTATION                    051
         DC    AL1(241)  EXTERNAL REPRESENTATION                    052
         DC    AL1(242)  EXTERNAL REPRESENTATION                    053
         DC    AL1(243)  EXTERNAL REPRESENTATION                    054
         DC    AL1(244)  EXTERNAL REPRESENTATION                    055
         DC    AL1(245)  EXTERNAL REPRESENTATION                    056
         DC    AL1(246)  EXTERNAL REPRESENTATION                    057
         DC    AL1(247)  EXTERNAL REPRESENTATION                    058
         DC    AL1(248)  EXTERNAL REPRESENTATION                    059
         DC    AL1(249)  EXTERNAL REPRESENTATION                    060
         DC    AL1(255)  EXTERNAL REPRESENTATION                    061
         DC    AL1(253)  EXTERNAL REPRESENTATION                    062
         DC    AL1(254)  EXTERNAL REPRESENTATION                    063
         DC    AL1(000)  EXTERNAL REPRESENTATION                    064
         DC    AL1(001)  EXTERNAL REPRESENTATION                    065
         DC    AL1(002)  EXTERNAL REPRESENTATION                    066
         DC    AL1(003)  EXTERNAL REPRESENTATION                    067
         DC    AL1(004)  EXTERNAL REPRESENTATION                    068
         DC    AL1(005)  EXTERNAL REPRESENTATION                    069
         DC    AL1(006)  EXTERNAL REPRESENTATION                    070
         DC    AL1(007)  EXTERNAL REPRESENTATION                    071
         DC    AL1(008)  EXTERNAL REPRESENTATION                    072
         DC    AL1(009)  EXTERNAL REPRESENTATION                    073
         DC    AL1(010)  EXTERNAL REPRESENTATION                    074
         DC    AL1(011)  EXTERNAL REPRESENTATION                    075
         DC    AL1(012)  EXTERNAL REPRESENTATION                    076
         DC    AL1(013)  EXTERNAL REPRESENTATION                    077
         DC    AL1(014)  EXTERNAL REPRESENTATION                    078
         DC    AL1(015)  EXTERNAL REPRESENTATION                    079
         DC    AL1(016)  EXTERNAL REPRESENTATION                    080
         DC    AL1(017)  EXTERNAL REPRESENTATION                    081
         DC    AL1(018)  EXTERNAL REPRESENTATION                    082
         DC    AL1(019)  EXTERNAL REPRESENTATION                    083
         DC    AL1(020)  EXTERNAL REPRESENTATION                    084
         DC    AL1(021)  EXTERNAL REPRESENTATION                    085
         DC    AL1(022)  EXTERNAL REPRESENTATION                    086
         DC    AL1(023)  EXTERNAL REPRESENTATION                    087
         DC    AL1(024)  EXTERNAL REPRESENTATION                    088
         DC    AL1(025)  EXTERNAL REPRESENTATION                    089
         DC    AL1(026)  EXTERNAL REPRESENTATION                    090
         DC    AL1(027)  EXTERNAL REPRESENTATION                    091
         DC    AL1(028)  EXTERNAL REPRESENTATION                    092
         DC    AL1(029)  EXTERNAL REPRESENTATION                    093
         DC    AL1(030)  EXTERNAL REPRESENTATION                    094
         DC    AL1(031)  EXTERNAL REPRESENTATION                    095
         DC    AL1(032)  EXTERNAL REPRESENTATION                    096
         DC    AL1(033)  EXTERNAL REPRESENTATION                    097
         DC    AL1(034)  EXTERNAL REPRESENTATION                    098
         DC    AL1(035)  EXTERNAL REPRESENTATION                    099
         DC    AL1(036)  EXTERNAL REPRESENTATION                    100
         DC    AL1(037)  EXTERNAL REPRESENTATION                    101
         DC    AL1(038)  EXTERNAL REPRESENTATION                    102
         DC    AL1(039)  EXTERNAL REPRESENTATION                    103
         DC    AL1(040)  EXTERNAL REPRESENTATION                    104
         DC    AL1(041)  EXTERNAL REPRESENTATION                    105
         DC    AL1(042)  EXTERNAL REPRESENTATION                    106
         DC    AL1(043)  EXTERNAL REPRESENTATION                    107
         DC    AL1(044)  EXTERNAL REPRESENTATION                    108
         DC    AL1(045)  EXTERNAL REPRESENTATION                    109
         DC    AL1(046)  EXTERNAL REPRESENTATION                    110
         DC    AL1(047)  EXTERNAL REPRESENTATION                    111
         DC    AL1(048)  EXTERNAL REPRESENTATION                    112
         DC    AL1(049)  EXTERNAL REPRESENTATION                    113
         DC    AL1(050)  EXTERNAL REPRESENTATION                    114
         DC    AL1(051)  EXTERNAL REPRESENTATION                    115
         DC    AL1(052)  EXTERNAL REPRESENTATION                    116
         DC    AL1(053)  EXTERNAL REPRESENTATION                    117
         DC    AL1(054)  EXTERNAL REPRESENTATION                    118
         DC    AL1(055)  EXTERNAL REPRESENTATION                    119
         DC    AL1(056)  EXTERNAL REPRESENTATION                    120
         DC    AL1(057)  EXTERNAL REPRESENTATION                    121
         DC    AL1(058)  EXTERNAL REPRESENTATION                    122
         DC    AL1(059)  EXTERNAL REPRESENTATION                    123
         DC    AL1(060)  EXTERNAL REPRESENTATION                    124
         DC    AL1(061)  EXTERNAL REPRESENTATION                    125
         DC    AL1(062)  EXTERNAL REPRESENTATION                    126
         DC    AL1(063)  EXTERNAL REPRESENTATION                    127
         DC    AL1(065)  EXTERNAL REPRESENTATION                    128
         DC    AL1(066)  EXTERNAL REPRESENTATION                    129
         DC    AL1(067)  EXTERNAL REPRESENTATION                    130
         DC    AL1(068)  EXTERNAL REPRESENTATION                    131
         DC    AL1(069)  EXTERNAL REPRESENTATION                    132
         DC    AL1(070)  EXTERNAL REPRESENTATION                    133
         DC    AL1(071)  EXTERNAL REPRESENTATION                    134
         DC    AL1(072)  EXTERNAL REPRESENTATION                    135
         DC    AL1(073)  EXTERNAL REPRESENTATION                    136
         DC    AL1(074)  EXTERNAL REPRESENTATION                    137
         DC    AL1(081)  EXTERNAL REPRESENTATION                    138
         DC    AL1(082)  EXTERNAL REPRESENTATION                    139
         DC    AL1(083)  EXTERNAL REPRESENTATION                    140
         DC    AL1(084)  EXTERNAL REPRESENTATION                    141
         DC    AL1(085)  EXTERNAL REPRESENTATION                    142
         DC    AL1(086)  EXTERNAL REPRESENTATION                    143
         DC    AL1(087)  EXTERNAL REPRESENTATION                    144
         DC    AL1(088)  EXTERNAL REPRESENTATION                    145
         DC    AL1(089)  EXTERNAL REPRESENTATION                    146
         DC    AL1(090)  EXTERNAL REPRESENTATION                    147
         DC    AL1(098)  EXTERNAL REPRESENTATION                    148
         DC    AL1(099)  EXTERNAL REPRESENTATION                    149
         DC    AL1(100)  EXTERNAL REPRESENTATION                    150
         DC    AL1(101)  EXTERNAL REPRESENTATION                    151
         DC    AL1(102)  EXTERNAL REPRESENTATION                    152
         DC    AL1(103)  EXTERNAL REPRESENTATION                    153
         DC    AL1(104)  EXTERNAL REPRESENTATION                    154
         DC    AL1(105)  EXTERNAL REPRESENTATION                    155
         DC    AL1(106)  EXTERNAL REPRESENTATION                    156
         DC    AL1(112)  EXTERNAL REPRESENTATION                    157
         DC    AL1(113)  EXTERNAL REPRESENTATION                    158
         DC    AL1(114)  EXTERNAL REPRESENTATION                    159
         DC    AL1(115)  EXTERNAL REPRESENTATION                    160
         DC    AL1(116)  EXTERNAL REPRESENTATION                    161
         DC    AL1(117)  EXTERNAL REPRESENTATION                    162
         DC    AL1(118)  EXTERNAL REPRESENTATION                    163
         DC    AL1(119)  EXTERNAL REPRESENTATION                    164
         DC    AL1(120)  EXTERNAL REPRESENTATION                    165
         DC    AL1(121)  EXTERNAL REPRESENTATION                    166
         DC    AL1(128)  EXTERNAL REPRESENTATION                    167
         DC    AL1(129)  EXTERNAL REPRESENTATION                    168
         DC    AL1(130)  EXTERNAL REPRESENTATION                    169
         DC    AL1(131)  EXTERNAL REPRESENTATION                    170
         DC    AL1(132)  EXTERNAL REPRESENTATION                    171
         DC    AL1(133)  EXTERNAL REPRESENTATION                    172
         DC    AL1(134)  EXTERNAL REPRESENTATION                    173
         DC    AL1(135)  EXTERNAL REPRESENTATION                    174
         DC    AL1(136)  EXTERNAL REPRESENTATION                    175
         DC    AL1(137)  EXTERNAL REPRESENTATION                    176
         DC    AL1(138)  EXTERNAL REPRESENTATION                    177
         DC    AL1(139)  EXTERNAL REPRESENTATION                    178
         DC    AL1(140)  EXTERNAL REPRESENTATION                    179
         DC    AL1(141)  EXTERNAL REPRESENTATION                    180
         DC    AL1(142)  EXTERNAL REPRESENTATION                    181
         DC    AL1(143)  EXTERNAL REPRESENTATION                    182
         DC    AL1(144)  EXTERNAL REPRESENTATION                    183
         DC    AL1(145)  EXTERNAL REPRESENTATION                    184
         DC    AL1(146)  EXTERNAL REPRESENTATION                    185
         DC    AL1(147)  EXTERNAL REPRESENTATION                    186
         DC    AL1(148)  EXTERNAL REPRESENTATION                    187
         DC    AL1(149)  EXTERNAL REPRESENTATION                    188
         DC    AL1(150)  EXTERNAL REPRESENTATION                    189
         DC    AL1(151)  EXTERNAL REPRESENTATION                    190
         DC    AL1(152)  EXTERNAL REPRESENTATION                    191
         DC    AL1(153)  EXTERNAL REPRESENTATION                    192
         DC    AL1(154)  EXTERNAL REPRESENTATION                    193
         DC    AL1(155)  EXTERNAL REPRESENTATION                    194
         DC    AL1(156)  EXTERNAL REPRESENTATION                    195
         DC    AL1(157)  EXTERNAL REPRESENTATION                    196
         DC    AL1(158)  EXTERNAL REPRESENTATION                    197
         DC    AL1(159)  EXTERNAL REPRESENTATION                    198
         DC    AL1(160)  EXTERNAL REPRESENTATION                    199
         DC    AL1(161)  EXTERNAL REPRESENTATION                    200
         DC    AL1(162)  EXTERNAL REPRESENTATION                    201
         DC    AL1(163)  EXTERNAL REPRESENTATION                    202
         DC    AL1(164)  EXTERNAL REPRESENTATION                    203
         DC    AL1(165)  EXTERNAL REPRESENTATION                    204
         DC    AL1(166)  EXTERNAL REPRESENTATION                    205
         DC    AL1(167)  EXTERNAL REPRESENTATION                    206
         DC    AL1(168)  EXTERNAL REPRESENTATION                    207
         DC    AL1(169)  EXTERNAL REPRESENTATION                    208
         DC    AL1(170)  EXTERNAL REPRESENTATION                    209
         DC    AL1(171)  EXTERNAL REPRESENTATION                    210
         DC    AL1(172)  EXTERNAL REPRESENTATION                    211
         DC    AL1(173)  EXTERNAL REPRESENTATION                    212
         DC    AL1(174)  EXTERNAL REPRESENTATION                    213
         DC    AL1(175)  EXTERNAL REPRESENTATION                    214
         DC    AL1(176)  EXTERNAL REPRESENTATION                    215
         DC    AL1(177)  EXTERNAL REPRESENTATION                    216
         DC    AL1(178)  EXTERNAL REPRESENTATION                    217
         DC    AL1(179)  EXTERNAL REPRESENTATION                    218
         DC    AL1(180)  EXTERNAL REPRESENTATION                    219
         DC    AL1(181)  EXTERNAL REPRESENTATION                    220
         DC    AL1(182)  EXTERNAL REPRESENTATION                    221
         DC    AL1(183)  EXTERNAL REPRESENTATION                    222
         DC    AL1(184)  EXTERNAL REPRESENTATION                    223
         DC    AL1(185)  EXTERNAL REPRESENTATION                    224
         DC    AL1(186)  EXTERNAL REPRESENTATION                    225
         DC    AL1(187)  EXTERNAL REPRESENTATION                    226
         DC    AL1(188)  EXTERNAL REPRESENTATION                    227
         DC    AL1(189)  EXTERNAL REPRESENTATION                    228
         DC    AL1(190)  EXTERNAL REPRESENTATION                    229
         DC    AL1(191)  EXTERNAL REPRESENTATION                    230
         DC    AL1(192)  EXTERNAL REPRESENTATION                    231
         DC    AL1(202)  EXTERNAL REPRESENTATION                    232
         DC    AL1(203)  EXTERNAL REPRESENTATION                    233
         DC    AL1(204)  EXTERNAL REPRESENTATION                    234
         DC    AL1(205)  EXTERNAL REPRESENTATION                    235
         DC    AL1(206)  EXTERNAL REPRESENTATION                    236
         DC    AL1(207)  EXTERNAL REPRESENTATION                    237
         DC    AL1(208)  EXTERNAL REPRESENTATION                    238
         DC    AL1(218)  EXTERNAL REPRESENTATION                    239
         DC    AL1(219)  EXTERNAL REPRESENTATION                    240
         DC    AL1(220)  EXTERNAL REPRESENTATION                    241
         DC    AL1(221)  EXTERNAL REPRESENTATION                    242
         DC    AL1(222)  EXTERNAL REPRESENTATION                    243
         DC    AL1(223)  EXTERNAL REPRESENTATION                    244
         DC    AL1(224)  EXTERNAL REPRESENTATION                    245
         DC    AL1(225)  EXTERNAL REPRESENTATION                    246
         DC    AL1(234)  EXTERNAL REPRESENTATION                    247
         DC    AL1(235)  EXTERNAL REPRESENTATION                    248
         DC    AL1(236)  EXTERNAL REPRESENTATION                    249
         DC    AL1(237)  EXTERNAL REPRESENTATION                    250
         DC    AL1(238)  EXTERNAL REPRESENTATION                    251
         DC    AL1(239)  EXTERNAL REPRESENTATION                    252
         DC    AL1(250)  EXTERNAL REPRESENTATION                    253
         DC    AL1(251)  EXTERNAL REPRESENTATION                    254
         DC    AL1(252)  EXTERNAL REPRESENTATION                    255
***********************************************************************
MAJOR    DC    C'COPYDATA'    QNAME
ROCLIST  OPEN  (BSAMIN,(INPUT),QSAMOUT,(OUTPUT)),MF=L L.RDJFCB,OPEN,CLO
***********************************************************************
QLIST    ENQ  (MAJOR,MINORIN,S,,SYSTEM,MAJOR,MINOROUT,E,,SYSTEM),MF=L
***********************************************************************
F3       DC    F'3'                FULLWORD BINARY 3
F63      DC    F'63'          FULLWORD BINARY 64 FOR COMPARE
FM4      DC    F'-4'          FULLWORD BINARY MINUS 4 CONSTANT
DUMPCODE DC    X'80000000'    REQUEST DUMP WITH ABEND FLAG WORD-ALIGNED
BASERGRD DC    H'-1',AL2(RD*4096)    USED TO OR IN BASE RD - FULLWORD
EXTLSTIN DC    XL1'87',AL3(MINORIN)    RDJFCB EXIT LIST INPUT DATA SET
EXTLSTOT DC    XL1'87',AL3(MINOROUT)   RDJFCB EXIT LIST OUTPUT DATA SET
***********************************************************************
         USING SAVEAREA,RD    SAVE AREA POINTER USED AS BASE
SKIPUNPK B     PASTUNPK            MOVED TO ADD2SEQ# IF NO SEQ #
BRLASTCK B     LASTCHEK            MOVED TO INBFLOOP ON EOF
BRGTINC4 B     GATHINC4            MOVED TO LAORBRAN - FIFTH CHAR INCR
BRGTINC3 B     GATHINC3            MOVED TO LAORBRAN - HANDLE REST INCR
BRSPLSPC B     SPLSPECL            MOVED TO LAORBRAN
BRFIXSPC B     FIXSPECL            MOVED TO LAORBRAN
FIXBRAN  B     MORELAST            MOVED TO LAORBRAN
FIXLA    LA    R4,EXPNDWRK         STARTING ADDRESS FOR MOVE
MVRESTIN MVC   0(0,RE),EXPNDWRK    MOVE REST OF INCREMENT
MOVESCAT MVC   0(0,RE),0(R3)       MOVE TO SCATTER AREA
SCANFLAG TRT   2(0,R1),TBSCNFLG    SCAN FOR FLAGS
MOVBYTES MVC   0(0,RA),0(R4)       MOVE TO CARD IMAGE
PRPAGATE MVC   1(0,RA),0(RA)       PROPAGATE CHARACTER
***********************************************************************
SKIPSAVE ST    RD,4(,R2)           BACKWARD CHAIN
         ST    R2,8(,RD)           FORWARD CHAIN
         LR    RD,R2          COPY MY SAVE AREA ADDRESS TO REG 13
***********************************************************************
        RDJFCB ,MF=(E,ROCLIST)         USE REMOTE LIST
         CLI   MINORIN,C' '   SEE IF RDJFCB WORKED OK
         BE    TILT1          IF RDJFCB FAILED, GO ABEND
         CLI   MINOROUT,C' '  SEE IF RDJFCB WORKED OK
         BE    TILT2          IF RDJFCB FAILED, GO ABEND
         ENQ   ,MF=(E,QLIST)  ENQUEUE UPON INPUT + OUTPUT DATA SETS
         OPEN  ,MF=(E,ROCLIST),TYPE=J  USE REMOTE LIST, + HAVE JFCB
         TM    OOFLGS+BSAMIN,X'10'   SEE IF OPEN WAS SUCCESSFUL
         BZ    TILT1          ABEND IF INPUT OPEN WAS NOT SUCCESSFUL
         TM    OOFLGS+QSAMOUT,X'10'  SEE IF OPEN WAS SUCCESSFUL
         BZ    TILT2          ABEND IF OUTPUT OPEN WAS NOT SUCCESSFUL
         LH    R8,OBLKSI+BSAMIN    BLOCK, I.E. BUFFER, SIZE
         LA    R5,3(,R8)           FULLWORD MULTIPLE FOR BLKSIZE
         N     R5,FM4              FULLWORD MULTIPLE FOR BLKSIZE
         LR    R0,R5          COPY FULLWORD MULTIPLE FOR BLKSIZE
         ALR   R0,R0           2 * FULLWORD MULTIPLE FOR BLKSIZE
         ST    R0,BUFSSIZE    SIZE OF AREA FOR BUFFERS
         GETMAIN R,LV=(0)     GET CORE FOR BUFFERS
         LA    R7,DECB1       INITIALIZE REGS. FOR DECBS + BUFFERS
         ST    R1,ODECBUFR+DECB1   STORE ADDRESS OF 1ST BUFFER
         ALR   R1,R5               ADDRESS OF 2ND BUFFER ON FULLWD BND.
         LA    R6,DECB2       INITIALIZE REGS. FOR DECBS + BUFFERS
         ST    R1,ODECBUFR+DECB2   STORE ADDRESS OF 2ND BUFFER
         READ  (R6),SF,MF=E        INITIATE READ
         XR    R6,R7          LOGICAL DIFFERENCE TO FLIP BETWEEN DECBS
         SLR   R2,R2          CLEAR R2 FOR TRT TABLE INST. OFFSETS
         LA    R9,EXPNDEND-2       LIMIT FOR LOOP
         LA    RA,CARDWORK         START OF CARD IMAGE
BUFREMPT LA    R4,SCATNONE         SCATNONE WHERE TO GO ON END FILE
         READ  (R7),SF,MF=E        INITIATE READ FOR BUFFER JUST EMPTY
         XR    R7,R6          FLIP FROM ONE DECB + BUFFER TO OTHER
         L     R3,ODECBUFR(,R7)    ADDRESS OF BUFFER
         LR    RC,R3          COPY ADDRESS OF BUFFER
         CHECK (R7)           WAIT FOR COMPLETION,  CHECK
         AH    RC,0(,R3)           ADDRESS JUST PAST BUFFER
         LA    R3,8(,R3)           ADDRESS OF BEGIN RECORD IN BLOCK
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
         EJECT
***********************************************************************
SCATPART SR    RC,R3               # OF BYTES LEFT IN BUFFER
         BNP   BUFREMPT            BRANCH IF BUFFER EMPTY
         BCTR  RC,0                ONE LESS FOR MOVE
         LA    RE,SCATWORK         ADDRESS OF SCATTER WORK AREA
         EX    RC,MOVESCAT         MOVE REST OF BUFFER TO SCATTER WORK
         LA    R4,SCATREST         WHERE TO GO ON ENDFILE
         READ  (R7),SF,MF=E        INITIATE READ FOR BUFFER JUST EMPTY
         XR    R7,R6          FLIP FROM ONE DECB + BUFFER TO OTHER
         L     R3,ODECBUFR(,R7)    ADDRESS OF BUFFER
         CHECK (R7)           WAIT FOR COMPLETION,  CHECK
         LA    RE,SCATWORK+1(RC)   NEXT MOVE LOCATION IN SCATWORK
         LA    RF,192-2            TO CALC MOVE LENGTH -1
         SLR   RF,RC               # LEFT TO MOVE -1
         LR    RC,R3          COPY ADDRESS OF BUFFER
         AH    RC,0(,R3)           ADDRESS JUST PAST BUFFER
         LA    R3,8(,R3)           ADDRESS OF BEGIN RECORD IN BLOCK
         EX    RF,MOVESCAT         MOVE TO REST OF SCATTER WORK
         LA    R3,1(RF,R3)         INCREMENT BUFFER ADDRESS
         CLR   R3,RC          HAS MOVE EXCEEDED BUFFER DATA?
         BH    SCATLAST       IF SO MUST BE SHORT LAST BLOCK
         MVC   EXPNDWRK,SCTRTABL   MOVE SCATTER TABLE
         TR    EXPNDWRK,SCATWORK   SCATTER 3 TO 4
         B     BGEXPAND            GO CONVERT 6 TO 8-BIT
***********************************************************************
         EJECT
***********************************************************************
MOV2CARD EQU   *                   MOVE STUFF TO CARD(S) ROUTINE
         SR    R5,R4               CALC # LEFT IN EXPNDWRK TO MOVE -1
         BCR   4,R8           BM - RETURN IF NOTHING TO MOVE
LACRDEND LA    RB,CARDWORK+71      LAST BYTE BEFORE SEQ #
         SLR   RB,RA               CALC ROOM LEFT IN CARD IMAGE -1
WHCHLESS CR    R5,RB               SEE WHICH SHORTEST
         BNL   MORECARD            IF NOT ALL FIT IN CARD, BRANCH
         EX    R5,MOVBYTES         MOVE REST OF WORK AREA
         LA    RA,1(R5,RA)         INCR ADDRESS IN CARD IMAGE
         LA    R4,1(R5,R4)         INCREMENT ADDR IN EXPNDWRK
         BR    R8                  RETURN TO AFTER BAL R8,MOV2CARD
***********************************************************************
MORECARD EX    RB,MOVBYTES         FILL UP CARD IMAGE
         LA    R4,1(RB,R4)         INCREMENT ADDR IN EXPNDWRK
TRANCARD TR    CARDWORK,TBINTEXT   TRANSLATE TO EBCDIC
ADD2SEQ# AP    PKEDSEQ#,INCREMEN   GENERATE NEXT SEQUENCE #
         OI    PKEDSEQ#+4,X'0F'    MAKE SEQUENCE # UNSIGNED
UNPKSEQ# UNPK  CARDSEQ#,PKEDSEQ#   UNPACK SEQUENCE #
PASTUNPK EQU   *                   INSTRUCTION JUST PAST UNPK
         LA    RA,CARDWORK         START OF CARD IMAGE
         PUT   QSAMOUT,(RA)        PUT CARD IMAGE
         SR    R5,RB               CALC. # FOR NEXT CARD
         BCR   13,R8          BNP -IF NONE FOR NEXT, BYPASS
LACRDLFT LA    RB,71               # OF BYTES BEFORE SEQ # -1
         BCT   R5,WHCHLESS         ONE LESS THAN LENGTH FOR MOVE
         B     WHCHLESS            BRANCH IN CASE FELL THRU BCT
***********************************************************************
         EJECT
***********************************************************************
SCNOHIT  EQU   *                   HERE IF NO HIT DURING SCAN
         LA    R5,2(,R9)           LAST BYTE OF EXPNDWRK TO MOVE/EXPAND
         BAL   R8,MOV2CARD         GO MOVE AS IS STUFF TO CARD
INBFLOOP EQU   *                   LOOP THRU INPUT BUFFER
         LA    R0,192(,R3)         MAXIMUM CAN SCATTER 3 TO 4
         CLR   R0,RC               CAN WE SCATTER MAXIMUM?
         BH    SCATPART            IF NOT, BRANCH
         MVC   EXPNDWRK,SCTRTABL   MOVE SCATTER TABLE
         TR    EXPNDWRK,0(R3)      SCATTER 3 TO 4
         LR    R3,R0               UPDATE ADDR IN INPUT BUFFER
BGEXPAND EQU   *              EXPAND 6 BIT IN BUFFER TO 8-BIT IN WKAREA
         LA    R8,4                INCREMENT FOR LOOP
         LA    R5,EXPNDWRK-2       INITIALIZE 6-BIT TO 8-BIT LOOP
         USING LOOPWORD-2,R5       AVOIDS DIAGNOSTIC
EXPNLOOP L     RF,EXPNDWRD         WORD INTO REGISTER  4*6+8 JUNK BITS
         SLR   RE,RE               CLEAR 2 BITS IN RE
         SLDL  RE,6                SHIFT 6 BITS INTO RE
         SLL   RE,2                SHIFT 2 ZERO-BITS INTO RE
         SLDL  RE,6                SHIFT 6 BITS INTO RE
         SLL   RE,2                SHIFT 2 ZERO-BITS INTO RE
         SLDL  RE,6                SHIFT 6 BITS INTO RE
         SLL   RE,2                SHIFT 2 ZERO-BITS INTO RE
         SLDL  RE,6                SHIFT 6 BITS INTO RE
         ST    RE,EXPNDWRD         4*8 BIT WORD STORE
         BXLE  R5,R8,EXPNLOOP      LOOP UNTIL THRU EXPNDWRK
         DROP  R5                  END ADDRESSABILITY OF EXPNDWRD
LAORBRAN LA    R4,EXPNDWRK         STARTING ADDRESS FOR MOVE
         LA    R1,EXPNDWRK-2       STARTING ADDRESS FOR SCAN -2
SCANLOOP LR    RF,R9               ADDR END OF SCAN -2
         SR    RF,R1               CALC LENGTH (-1) OF SCAN
         BM    INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
         EX    RF,SCANFLAG         SCAN FOR SPECIAL HANDLING
         BZ    SCNOHIT             BRANCH IF NO HIT DURING SCAN
         LR    R5,R1               LAST BYTE OF EXPNDWRK TO MOVE
         BCTR  R5,0                LAST BYTE OF EXPNDWRK TO MOVE
         BAL   R8,MOV2CARD         GO MOVE AS IS STUFF TO CARD
         SPM   R8                  RESTORE CONDITION CODE AFTER TRT
BRANBASE EQU   *              BASE FOR TRT TABLE INSTRUCTION OFFSETS
         B     BRANBASE(R2)        BRANCH TO ROUTINE TO HANDLE FLAG
***********************************************************************
HITFLBLK EQU   *-BRANBASE          INSTRUCTION OFFSET FOR TRT TABLE
         MVI   0(RA),INTBLANK      1 BLANK IN CARD IMAGE
         BH    LASTBYTE            BRANCH TO RTN TO HANDLE LAST BYTE FL
GETCOUNT IC    R2,1(,R4)           GET COUNT FOR MOVE
         LTR   R2,R2               SEE IF SPECIAL CHAR
         BZ    SPECCHAR            BRANCH IF SPECIAL
         CL    R2,F63              CHECK FOR REST OF CARD FLAG
         BE    LSTBFRS#            GO FILL CARD UP TO SEQUENCE #
         EX    R2,PRPAGATE         PROPAGATE CHARACTER
         LA    RA,2(R2,RA)         INCREMENT CARD WORK ADDR
CHEKFULL LA    RB,CARDWORK+71      LAST BYTE OF CARD BEFORE SEQUENCE #
         CLR   RA,RB               HAVE WE FILLED CARD?
         BH    CARDFULL            IF SO, GO PROCESS CARD
         LR    R1,R4               ADDRESS TO START NEXT SCAN (-2)
         LA    R4,2(,R4)           ADDRESS TO START NEXT MOVE
         B     SCANLOOP            CONTINUE SCANNING EXPNDWRK WORK AREA
***********************************************************************
LSTBFRS# LA    R2,CARDWORK+71      LAST OF CARD BEFORE SEQUENCE #
         SLR   R2,RA               # BYTES LEFT -1
         EX    R2,PRPAGATE         PROPAGATE CHARACTER
CARDFULL EQU   *              PROPAGATED CHAR HAS FILLED CARD
         LR    R5,RB               FORCE ZERO IN TRANCARD SR INSTR.
         BAL   R8,TRANCARD         GO TRANSLATE + OUTPUT CARD IMAGE
         LR    R1,R4               ADDRESS TO START NEXT SCAN (-2)
         LA    R4,2(,R4)           ADDRESS TO START NEXT MOVE
         B     SCANLOOP            CONTINUE SCANNING EXPNDWRK WORK AREA
***********************************************************************
HITFLAST EQU   *-BRANBASE          INSTRUCTION OFFSET FOR TRT TABLE
         MVI   0(RA),INTASTER      1 ASTERISK IN CARD IMAGE
         BNH   GETCOUNT       BRANCH IF NOT LAST BYTE
LASTBYTE MVC   LAORBRAN,FIXBRAN    CHANGE LA TO BRANCH
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
MORELAST LA    R4,EXPNDWRK-1       FAKE OUT ADDRESS
         MVC   LAORBRAN,FIXLA      RESTORE LA INSTRUCTION
         B     GETCOUNT            GO GET COUNT OF REPETITIVE STRING
***********************************************************************
SPECCHAR CLR   R4,R9               ARE WE AT END OF EXPNDWORK?
         BNL   CHEKSPEC            GO REFILL WORKAREA, ETC.
TESTSPEC TM    2(R4),B'00110000'   SEE IF CHANGE SEQ # INCR OR SPL CHAR
         BNZ   TSEQINCR            HANDLE CHANGED SEQ # LENGTH OR INCR
         PACK  0(2,RA),2(3,R4)     PACK SPECIAL CHAR + 1 BYTE JUNK
         LA    R4,2(,R4)           INCREMENT EXPNDWRK SCAN + MOVE ADDR
INCRCDSP LA    RA,1(,RA)           INCREMENT CARD WORK ADDRESS
         TM    1(R4),B'00110000'   SEE IF ANOTHER UNPKD SPEC CHAR NEXT?
         BO    SPECCHAR            IF SO GO PROCESS IT
         B     CHEKFULL            GO SEE IF CARD IS FULL
***********************************************************************
CHEKSPEC BE    SPLTSPEC            GO HANDLE SPLIT SPECIAL CHARACTER
         MVC   LAORBRAN,BRFIXSPC   CHANGE LA TO BRANCH
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
FIXSPECL EQU   *                   COMPLETE SPECIAL CHARACTER PROCESSI
         MVC   LAORBRAN,FIXLA      RESTORE LA INSTRUCTION
         LA    R4,EXPNDWRK-2       FAKE OUT ADDRESS
         B     TESTSPEC            GO SEE IF SPECIAL CHAR OR INCR CHNGE
***********************************************************************
SPLTSPEC TM    2(R4),B'00110000'   SEE IF 1 BYTE SEQ # LENGTH CODE
         BO    SEQ#LGTH            IF SO, WE HAVE WHAT WE NEED
         BM    GATHINCR            MUST GATHER INCREMENT
         MVC   LAORBRAN,BRSPLSPC   CHANGE LA TO BRANCH TO SPLSPECL
         MVC   PACKWORK+0(1),2(R4)   SAVE 1ST HALF OF SPECIAL CHAR
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
SPLSPECL EQU   *                   COMPLETE SPECIAL CHARACTER PROCESSI
         MVC   LAORBRAN,FIXLA      RESTORE LA INSTRUCTION
         MVC   PACKWORK+1(1),EXPNDWRK  SAVE 2ND HALF OF SPECIAL CHAR
         PACK  0(2,RA),PACKWORK    RECONSTRUCT SPECIAL CHARACTER
         LA    R4,EXPNDWRK-1       FAKE OUT ADDRESS FOR SCAN + MOVE
         B     INCRCDSP            GO COMPLETE PROCESSING UNUSUAL CHAR
***********************************************************************
TSEQINCR BO    SEQ#LGTH            HANDLE CHANGED SEQ # LENGTH
********                           HANDLE CHANGED SEQ # INCREMENT
         MVC   INCREMEN(4),2(R4)   MOVE TO WORD BOUNDARY
         LA    R0,EXPNDEND-4       6 BYTES LEFT INCLUDING THIS BYTE
         CLR   R4,R0               6 BYTES LEFT INCLUDING THIS BYTE?
         BNL   GATHINC2            IF NOT MUST GATHER INCREMENT
         LA    R1,4(,R4)           INCREMENT PAST FLAGS + SEQ INCREMENT
         LA    R4,6(,R4)           INCREMENT PAST FLAGS + SEQ INCREMENT
LOADINCR L     RE,INCREMEN         LOAD PADDED BINARY INCREMENT/DECR
         SRDL  RE,6                SHIFT OUT 6 BITS OF GOOD DATA
         SRL   RE,2                SHIFT OUT 2 BITS OF PADDING
         SRDL  RE,6                SHIFT OUT 6 BITS OF GOOD DATA
         SRL   RE,2                SHIFT OUT 2 BITS OF PADDING
         SRDL  RE,6                SHIFT OUT 6 BITS OF GOOD DATA
         SRL   RE,2                SHIFT OUT 2 BITS OF PADDING
         SRDL  RE,4                SHIFT OUT 4 BITS OF GOOD DATA
         TM    INCREMEN,B'00100000'   SEE IF 4 OR 5 CHAR NEW INCREMENT
         BZ    FIVECHIN            IF 5-CHAR GO HANDLE EXTRA CHAR
         SRA   RF,10               SHIFT IN 10 SIGN BITS TO GET #
CNVINCRE CVD   RF,INCREMEN         CONVERT TO DECIMAL THE INCR/DECR
         B     SCANLOOP            CONTINUE SCANNING EXPNDWRK WORK AREA
***********************************************************************
FIVECHIN LA    R0,EXPNDEND         LAST BYTE OF EXPAND WORK AREA
         CLR   R4,R0               SEE IF FIFTH CHAR. IN EXPNOWRK
         BH    FIFTHINC            IF NOT HERE GO GET FIFTH CH. OF INCR
IC5THCHR IC    RE,0(,R4)           HIGH ORDER BITS OF BINARY INCREMENT
         SRDL  RE,6                SHIFT OUT 6 BITS OF GOOD DATA
         SRA   RF,4                SHIFT IN 4 SIGN BITS TO GET #
         LA    R4,1(,R4)           BUMP MOVE ADDR PAST 5TH CHAR OF INCR
         LA    R1,1(,R1)           BUMP SCAN ADDR PAST 5TH CHAR OF INCR
         B     CNVINCRE            GO CONVERT INCREMENT TO DECIMAL
***********************************************************************
GATHINCR LA    R0,EXPNDEND-4       LAST BYTE OF EXPNDWRK -4
         MVC   INCREMEN(1),2(R4)   SAVE 1ST BYTE OF INCREMENT
GATHINC2 SLR   R4,R0               NO OF BYTES OF INCR LEFT TO MOVE -1
         ST    R4,INCREMEN+4  SAVE NO OF BYTES OF INCR LEFT TO MOVE -1
         MVC   LAORBRAN,BRGTINC3   CHANGE LA TO BRANCH TO GATHINC3
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
GATHINC3 MVC   LAORBRAN,FIXLA      RESTORE LA INSTRUCTION
         LA    RE,INCREMEN+3       ENDING BYTE OF MOVE
         L     RF,INCREMEN+4       LENGTH -1 OF MOVE
         SLR   RE,RF               STARTING BYTE OF MOVE
         EX    RF,MVRESTIN         MOVE REST OF INCREMENT
         LA    R1,EXPNDWRK-1(RF)   START NEXT SCAN
         LA    R4,EXPNDWRK+1(RF)   START NEXT MOVE
         B     LOADINCR            GO CONVERT INCREMENT
***********************************************************************
FIFTHINC MVC   LAORBRAN,BRGTINC4   CHANGE LA TO BRANCH TO GATHINC4
         B     INBFLOOP            GO EXPAND MORE FROM BUFFER TO WRKAR
***********************************************************************
GATHINC4 MVC   LAORBRAN,FIXLA      RESTORE LA INSTRUCTION
         LA    R4,EXPNDWRK         START MOVE
         LA    R1,EXPNDWRK-2       START SCAN
         B     IC5THCHR            GO PROCESS FIFTH CHAR.
***********************************************************************
SEQ#LGTH TM    2(R4),B'00001111'   SEE IF NO SEQUENCE #
         BO    NOSEQUEN            BRANCH IF NONE
         MVO   UNPKSEQ#+1(1),2(1,R4)  MOVE LENGTH-1 TO UNPACK INST.
         IC    R2,2(,R4)           LENGTH +47 INTO REGISTER
         LA    RF,79+47            USED TO CALCULATE TRANSLATE LENGTH
         SLR   RF,R2               TRANSLATE LENGTH -1
STCTRLEN STC   RF,TRANCARD+1       TRANSLATE LENGTH -1
         STH   RF,LACRDLFT+2       STORE # OF BYTES BEFORE SEQ # -1
         LA    R0,CARDWORK-SAVEAREA+1(,RF)  OFFSET OF SEQ # FROM BASE
         O     R0,BASERGRD         OR IN BASE REGISTER - RD
         STH   R0,UNPKSEQ#+2       STORE BASE, DISPLACEMENT
         BCTR  R0,0                CALC. DISPLACEMENT LAST BYTE BEF SEQ
         STH   R0,LACRDEND+2       STORE BASE, DISPLACEMENT
         STH   R0,LSTBFRS#+2       STORE BASE, DISPLACEMENT
         STH   R0,CHEKFULL+2       STORE BASE, DISPLACEMENT
         LA    R1,1(,R4)           ADDR TO START SCAN -2
         LA    R4,3(,R4)           ADDR TO START NEXT MOVE
         B     SCANLOOP            CONTINUE SCANNING EXPNDWRK WORK AREA
***********************************************************************
NOSEQUEN MVC   ADD2SEQ#(4),SKIPUNPK  MOVE BRANCH TO BYPASS SEQ # LOGIC
         LA    RF,79               TRANSLATE LENGTH -1
         B     STCTRLEN            GO ADJUST VARIOUS INSTRUCTIONS
***********************************************************************
         EJECT
***********************************************************************
ENDDATA  BR    R4    SCATREST OR SCATNONE, EOF ON INPUT -COMPRESSED SRC
***********************************************************************
SCATLAST LA    RC,192-1(,RC)  USED TO CALC. # COMPR. BYTES IN EXPNDWRK
         SLR   RC,R3          CALC. # IN EXPNDWRK -1
SCATREST MVC   EXPNDWRK,SCTRTABL   MOVE SCATTER TABLE
         TR    EXPNDWRK,SCATWORK   SCATTER 3 TO 4
         LA    R5,2(RC,RC)         2 * # COMPRESSED BYTES LEFT
         ALR   R5,R5               4 * # COMPRESSED BYTES LEFT
         SLR   R4,R4               CLEAR HIGH ORDER DIVIDEND
         D     R4,F3               DIVIDE BY 3
         LA    R9,EXPNDWRK-3(R5)   LIMIT FOR SHORT LOOP
         MVC   INBFLOOP(4),BRLASTCK  CHANGE INSTRUCTION TO BRANCH
         B     BGEXPAND            GO EXPAND 6 TO 8 SHORT LAST EXPNDWRK
***********************************************************************
LASTCHEK LTR   R5,R5               SEE IF R5 ZERO AS SHOULD BE
         BNZ   TILT8
SCATNONE EQU   *                   EVERYTHING SEEMS OK
********                      CLOSE DATA SETS, DEQUEUE AND RETURN
         CLOSE ,MF=(E,ROCLIST)         USE REMOTE LIST
         DEQ   ,MF=(E,QLIST)  DEQUEUE FROM INPUT + OUTPUT DATA SETS
         L     RD,4(,RD)           RESTORE CALLER SAVE AREA POINTER
         LM    RE,RC,12(RD)   RESTORE CALLERS REGISTERS
         SLR   RF,RF          ZERO RETURN CODE
         BR    RE             RETURN TO CALLER
***********************************************************************
TILT1    LA    R1,100         COMPLETION CODE FOR ABEND DUMP IN R1
         B     TILT           GO OR IN DUMP BIT AND ISSUE ABEND SVC
TILT2    LA    R1,200         COMPLETION CODE FOR ABEND DUMP IN R1
         B     TILT           GO OR IN DUMP BIT AND ISSUE ABEND SVC
TILT8    LA    R1,800         COMPLETION CODE FOR ABEND DUMP IN R1
TILT     O     R1,DUMPCODE    REQUEST DUMP WITH ABEND
         ABEND (1)            GENERATE ABEND SVC
***********************************************************************
         EJECT
***********************************************************************
QSAMOUT  DCB   DDNAME=SYSUT2,      OUTPUT DDNAME                       C
               DSORG=PS,           PHYSICAL SEQUENTIAL                 C
               BFTEK=S,            SIMPLE BUFFERING                    C
               BFALN=D,            DOUBLEWORD BUFFER ALIGNMENT         C
               LRECL=80,           LOGICAL RECORD LENGTH OF CARD IMAGE C
               EXLST=EXTLSTOT,     EXIT LIST FOR READ DATA SET JFCB    C
               MACRF=PM            PUT MOVE
***********************************************************************
         EJECT
***********************************************************************
BSAMIN   DCB   DDNAME=SYSUT1,      INPUT COMPRESSED SOURCE             C
               DSORG=PS,           PHYSICAL SEQUENTIAL                 C
               NCP=2,              MAX # OF OUTSTANDING READS          C
               BUFNO=0,       DO OUR OWN BUFFERING                     C
               EXLST=EXTLSTIN,     EXIT LIST FOR READ DATA SET JFCB    C
               RECFM=V,       VARIABLE LENGTH RECORDS                  C
               EODAD=ENDDATA,      END OF FILE ROUTINE                 C
               MACRF=R             READ
***********************************************************************
         EJECT
***********************************************************************
         READ  DECB1,SF,BSAMIN,MF=L    FIRST OF TWO DECB'S
DECB2    DC    X'7F00000000200000',A(BSAMIN,0,0)     POSTED COMPL. DECB
ODECBUFR EQU   12             OFFSET OF BUFFER ADDRESS IN DECB
***********************************************************************
BUFSSIZE DS    F              SIZE OF AREA FOR BUFFERS
***********************************************************************
MINORIN  DC    CL44' '        DSNAME IN JFCB - INITIALLY BLANK
         DS    CL(176-44)     REST OF JFCB READ-IN AREA
***********************************************************************
MINOROUT DC    CL44' '        DSNAME IN JFCB - INITIALLY BLANK
         DS    CL(176-44)     REST OF JFCB READ-IN AREA
***********************************************************************
SCATWORK DS    24D                 USED TO COLLECT PIECES OF BUFFERS
***********************************************************************
INCREMEN DC    PL8'100' DOUBLEWORD PACKED CARD SEQUENCE # INCREMENT
***********************************************************************
EXPNDWRK DS    CL256               WORKAREA FOR 6 TO 8-BIT CONVERSION
EXPNDEND EQU   *-1                 LAST BYTE OF WORKAREA
***********************************************************************
CARDWORK DS    CL72                NORMAL CARD DATA AREA, DBLWD ALIGNED
CARDSEQ# DS    D                   NORMAL CARD SEQUENCE # AREA
         DS    CL1                 AREA FOR POSSIBLE 1-BYTE PACK JUNK
PACKWORK DS    XL3                 WORKAREA TO BUILD SPLIT SPECIAL CHAR
PKEDSEQ# DC    PL5'0'              PACKED CARD SEQUENCE # AREA
***********************************************************************
LOOPWORD DSECT                     AVOIDS DIAGNOSTIC
EXPNDWRD DS    F                   AVOIDS DIAGNOSTIC
***********************************************************************
         EJECT
***********************************************************************
         DCBD  DSORG=QS,DEVD=DA    DUMMY SECTION FOR DCB FIELDS
***********************************************************************
OOFLGS   EQU   DCBOFLGS-IHADCB     OFFSET IN DCB OF DCBOFLGS
ORECFM   EQU   DCBRECFM-IHADCB     OFFSET IN DCB OF RECORD FORMAT
OBLKSI   EQU   DCBBLKSI-IHADCB     OFFSET IN DCB OF BLOCK SIZE
OLRECL   EQU   DCBLRECL-IHADCB     OFFSET IN DCB OF RECORD LENGTH
***********************************************************************
         END   EXPNDSRC            ENTRY POINT
