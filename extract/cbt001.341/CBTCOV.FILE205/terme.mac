         MACRO
&NAME    TERME
         GBLB  &PRIME
.**********************************************************************
.*       MODIFIED 7/11/75 - MIDDLE SOUTH SERVICES, GRETNA, LA. BY AGT *
.*       UNDER VS/2 (BOTH MVS AND SVS) FREEMAIN SETS REG 15 TO ZERO   *
.*       TO INDICATE SUCESSFUL COMPLETION. THIS DISTROYS ANY RETURN   *
.*       CODE PLACED IN REG 15.                                       *
.*       TERME HAS BEEN MODIFIED TO SAVE AND RESTORE REG 15.          *
.**********************************************************************
         AIF   (&PRIME).NOMSG       IF GLOBAL SET DO NOT GIVE MSG
***********************************************************************
*
         MNOTE 8,'''PRIME'' MACRO SHOULD BE USED TO START PROGRAM '
         MNOTE *,'WHEN ''TERME'' MACRO IS USED TO TERMINATE.'
*
***********************************************************************
.NOMSG   ANOP
&NAME    L     13,4(13) .              GET POINTER TO PREVIOUS SAVEAREA
         STM   15,1,16(13) .           TEMPORARY SAVE OF R15 - R1  VS/2
         L     1,8(13) .               GET USER SAVE AREA ADDRESS
         LA    0,72 .                  GET LENGTH FOR FREEMAIN
         LA    1,0(1) .                CLEAR THE HIGH ORDER BYTE
         SVC   10 .                    ISSUE FREEMAIN SVC
         L     14,16(13) .             LOAD R14 TO
         SPM   14 .                         RESET PGM MASK
         LM    14,12,12(13) .          RESTORE REGISTERS           VS/2
         XC    8(4,13),8(13) .         DEQUE SAVE AREA
         BR    14 .                    RETURN TO CALLER
         MEND
