         MACRO
&NAME    PRIME &HELP,                   DOES USER WANT EXPLANATION     *
               &REG=12,                DEFAULT BASE REG IS R12         *
               &ID=CSECT,              PROVIDE MODULE ID               *
               &EQUATES=YES,           PROVIDE REGISTER EQUATES        *
               &RENT=YES               GENERATE REENTERABLE CODE
.**********************************************************************
.*
.*     SYSGRP  16 MAY 1972
.*
.*     THIS MACRO WILL GENERATE REENTERABLE CODE TO SAVE REGISTERS,
.*     PROVIDE A USER SAVE AREA BY DYNAMICALLY ALLOCATING 18
.*     FULLWORDS (72 BYTES) OF MAIN STORAGE, SET A BASE REGISTER,
.*     CHAIN SAVE AREAS, AND OPTIONALLY PROVIDE MODULE ID AND REGISTER
.*     EQUATES.
.*
.**********************************************************************
         GBLB  &PRIME
         LCLA  &K,&I,&NREG,&CHARCNT,&BRCNT
         LCLC  &A(16),&CHAR
***********************************************************************
*                                                                     *
*        THIS MACRO PROVIDED COURTESY OF                              *
*             SYSTEM MAINTENANCE GROUP                                *
*                  LOUISIANA DEPARTMENT OF HIGHWAYS                   *
*                                                                     *
         AIF   ('&HELP' EQ '').NOHELP   DON'T HELP UNLESS ASKED
*        PRIME                                                        *
*                                                                     *
*              HELP  --  CODE ANYWHERE IN MACRO LIST TO GET THIS      *
*                        EXPLANATION.                                 *
*              REG=  --  ONE OR MORE SYMBOLS REPRESENTING BASE        *
*                        REGISTERS.  IF MORE THAN ONE IS USED THE     *
*                        LIST MUST BE ENCLOSED IN PARANTHESES.        *
*                        SUCCESSIVE REGISTERS ARE LOADED WITH 4096    *
*                        OFF THE PREVIOUS BASE.  DEFAULT IS BASE      *
*                        REGISTER 12.                                 *
*              ID=  --   DETERMINES THE TYPE OF MODULE IDENTIFICATION.*
*                        IF ID=CSECT IS CODED A                       *
*                            DC    CL8'&SYSECT'                       *
*                        IS GENERATED AT THE BEGINNING OF THE PGM.    *
*                        IF ID=NAME IS CODED THE NAME FIELD ON THE    *
*                        MACRO CALL IS USED AS AN IDENTIFER.          *
*                        A CHARACTER STRING OF UP TO 255 CHARACTERS   *
*                        MAY BE SPECIFIED AND WILL BE USED AS THE     *
*                        IDENTIFIER.                                  *
*                        IF A NULL STRING IS SPECIFIED NO MODULE      *
*                        IDENTIFICATION WILL BE GENERATED.            *
*                        DEFAULT IS ID=CSECT.                         *
*              EQUATES=  SPECIFIES IF REGISTER EQUATES ARE TO BE      *
*                        GENERATED.                                   *
*                        EQUATES=YES CAUSES GENERATION OF EQUATES.    *
*                        EQUATES=NO CAUSES GENERATION OF THE EQUATES  *
*                        TO BE SUPRESSED.                             *
*                        DEFAULT IS EQUATES=YES.                      *
*              RENT= --  SPECIFIES IF REENTERABLE CODE IS TO BE       *
*                        GENERATED.                                   *
*                        RENT=YES CAUSES REENTERABLE CODE TO BE       *
*                        GENERATED.                                   *
*                        RENT=NO CAUSES AN INLINE SAVE AREA TO BE     *
*                        GENERATED AND USED.                          *
*                        DEFAULT IS RENT=YES.                         *
         AGO   .ENDHELP
.NOHELP  ANOP
*        CODE 'HELP' ANYWHERE IN MACRO LIST FOR EXPLANATION OF        *
*        MACRO PARAMETERS.                                            *
.ENDHELP ANOP
*                                                                     *
***********************************************************************
&NAME    DS    0H .                    BOUNDARY ALIGNMENT
         BALR  15,0  LOAD R15 WITH LAST 32 BITS OF PSW (SAVE PGM MASK)
&NREG    SETA  N'&REG                  SET TO NUMBER OF SUBPARMS
         AIF   (N'&REG LE 16).NREGOK   MORE THAN 16 REGS SUPPLIED
         MNOTE 8,'MORE THAN 16 BASE REGISTERS SPECIFIED.  ONLY FIRST '
         MNOTE *,'16 USED.'
&NREG    SETA  16
.NREGOK  ANOP
.LOOP    ANOP
&K       SETA  &K+1
&A(&K)   SETC  ',&REG(&K)'
         AIF   (&NREG EQ &K).END
         AGO   .LOOP
.END     ANOP
         USING *&A(1)&A(2)&A(3)&A(4)&A(5)&A(6)&A(7)&A(8)&A(9)&A(10)&A(1*
               1)&A(12)&A(13)&A(14)&A(15)&A(16) .                  ESTA*
               BLISH ADDRESSABILITY
         AIF   ('&ID' EQ '').NONAME     NO ID TO BE PROVIDED
         AIF   ('&ID' EQ 'CSECT').CSECT USE CSECT NAME AS ID
         AIF   ('&ID' EQ 'NAME').NAME  USE MACRO NAME AS ID
&CHARCNT SETA  K'&ID+8                  ADD LEN OF &SYSDATE
&CHAR    SETC  '&ID'
         AIF   ('&ID'(1,1) NE '''').NOQUOTE CHK IF ENCLOSED IN QUOTES
&CHAR    SETC  '&ID'(2,(K'&ID-2))      REMOVE QUOTES
&CHARCNT SETA  K'&ID+6                  MAKE TWO LESS
.NOQUOTE ANOP
&BRCNT   SETA  ((&CHARCNT+2)/2)*2+4     ROUND TO HALFWORD + 4 FOR B
         AIF   (&CHARCNT LE 255).CNTOK  CNT OK IF 255 OR LESS
&CHARCNT SETA  255                      ONLY 255 IF MORE
.CNTOK   ANOP
         B     &BRCNT.(15)             BRANCH AROUND ID
         DC    AL1(&CHARCNT)
         DC    C'&CHAR'
         DC    CL8'&SYSDATE'           DATE OF ASSEMBLY
         AGO   .NONAME
.*
.CSECT   ANOP
         AIF   ('&SYSECT' EQ '').NULLSET NO VALUE FOR &SYSECT
         B     22(15)                  BRANCH AROUND ID
         DC    AL1(17),CL9'&SYSECT' .  MODULE ID
         DC    CL8'&SYSDATE'           DATE OF ASSEMBLY
         AGO   .NONAME
.NULLSET ANOP
         MNOTE 4,'NO CONTROL SECTION ASSOCIATED WITH THIS MACRO.'
         AGO   .DUMMY
.*
.NAME    ANOP
         AIF   ('&NAME' EQ '').NULLNME CHECK IF A NAME SUPPLIED
         B     22(15)                  BRANCH AROUND ID
         DC    AL1(17),CL9'&NAME'      MODULE ID
         DC    CL8'&SYSDATE'           DATE OF ASSEMBLY
         AGO   .NONAME
.NULLNME ANOP
         MNOTE 4,'ID=NAME WAS SPECIFIED BUT NO NAME WAS SUPPLIED FOR '
         MNOTE *,'THIS MACRO.'
.DUMMY   ANOP
         B     22(15)                  BRANCH AROUND ID
         DC    AL1(17),CL9'PATCH'      INSERTED FOR PATCHING
         DC    CL8'&SYSDATE'           DATE OF ASSEMBLY
.NONAME  ANOP
         STM   14,12,12(13) .          SAVE REGS IN CALLERS AREA
         LR    &REG(1),15 .                 LOAD FIRST BASE REGISTER
&K       SETA  1
.LOOP2   ANOP
&K       SETA  &K+1
&I       SETA  &K-1
         AIF   (&K GT &NREG).END2
         LA    &REG(&K),4095(&REG(&I)) .           LOAD BASE REGISTER
         LA    &REG(&K),1(&REG(&K)) .              ADD ONE
         AIF   (&NREG EQ &K).END2
         AGO   .LOOP2
.END2    ANOP
         AIF   ('&RENT' NE 'NO').RENTCDE  GEN REENTERABLE CODE DEFAULT
         CNOP  0,4                     FULLWORD BOUNDRY
         BAL   1,*+76                  STEP OVER SAVEAREA
         DC    18F'0'                  SAVEAREA
         AGO   .CHAIN                  GO CHAIN SAVEAREAS
.RENTCDE ANOP
         LA    0,72 .                  LOAD R0 FOR GETMAIN
         BAL   1,*+4 .                 INDICATE GETMAIN
         SVC   10 .                    ISSUE GETMAIN SVC
         XC    0(72,1),0(1) .          CLEAR SAVE AREA
.CHAIN   ANOP
         ST    1,8(13) .               CHAIN FORWARD
         ST    13,4(1) .               CHAIN BACKWARD
         LM    0,1,20(13) .            RESTORE R0 AND R1
         L     13,8(13) .              SET SAVE AREA REGISTER
         AIF   ('&EQUATES' EQ 'NO').EXIT
         AIF   (&PRIME).EXIT           DON'T GEN EQUATES TWICE
         EJECT               LISTING CONTROL CARD - EJECT
***********************************************************************
*                                                                     *
*              * * * R E G I S T E R  E Q U A T E S * * *             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT               LISTING CONTROL CARD - EJECT
.EXIT    ANOP
&PRIME   SETB  1             SET GLOBAL FOR TERME MACRO
         MEND
