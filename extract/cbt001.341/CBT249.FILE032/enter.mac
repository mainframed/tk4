         MACRO
&LABEL   ENTER &NOCSECT,                                               X
               &VERSION=,                                              X
               &LEVEL=,                                                X
               &REGNAME=(R,EQU),                                       X
               &LABCHAR=$,                                             X
               &ENTRIES=,                                              X
               &CPYRITE=,                                              X
               &SAVEREG=,                                              X
               &BASEREG=,                                              X
               &SAVAREA=,                                              X
               &GOTO=,                                                 X
               &LINKTO=,                                               X
               &EXIT=,                                                 X
               &RESTREG=,                                              X
               &RETCODE=,                                              X
               &FREE=,                                                 X
               &TITLE=,                                                X
               &PATCHES=NO
.*
.* L O C A L   A R I T H M E T I C   V A R I A B L E S
.*
         LCLA  &IDLNGTH,&CNT1,&CNT2,&CNT3,&BR1,&BRCNT,&DSAVRCT,&PATCHNO
.*
.*             &IDLNGTH IS USED TO ACCUMULATE THE LENGTH OF THE
.*                  MODULE NAME FIELD.
.*
.*             &CNT1, &CNT2 AND &CNT3 ARE WORK COUNTERS.
.*
.*             &BR1 IS THE INITIAL BASE REGISTER.  IT IS INITIAL-
.*                  IZED AS 13 AND MAY BE ALTERED BY SPECIFICATION
.*                  OF THE BASEREG= PARAMETER.
&BR1     SETA  13
.*
.*             &BRCNT IS A COUNT OF THE BASE REGISTERS SPECIFIED
.*                  BY BASEREG= IF ANY.
.*
.*             &DSAVRCT IS A COUNT OF BASE REGS SPECIFIED FOR THE
.*                  DYNAMIC SAVE/WORK AREA.  IT IS USED TO INDEX
.*                  AND THEN LIMIT ACCESS TO THE &DSAVREG STACK.
.*
.*             &PATCHNO IS BOTH A SWITCH AND AN INDICATOR OF THE
.*                  SIZE OF THE PATCH AREA TO BE GENERATED IF ANY.
.*                  IT IS INITIALIZED AT SIZE OF 50 HALFWORDS.
.*                  ZERO VALUE INDICATES NO PATCHE AREA WANTED.
&PATCHNO SETA  50
.*
.* L O C A L   B I N A R Y   V A R I A B L E S
.*
         LCLB  &ERR,&REGEQU,&CPYRTSW,&DSAVINC,&GENEXIT
.*
.*             &ERR IS USED BY THE VALIDATION PHASE OF THE MACRO
.*                  TO INDICATE THAT ONE OR MORE ERRORS HAVE BEEN
.*                  FOUND.  IF IT IS NON-ZERO AT THE END OF VAL-
.*                  IDATION, NO CODE IS GENERATED.
.*
.*             &REGEQU IS A SWITCH USED TO INDICATE WHETHER OR
.*                  NOT REGISTER EQUATES ARE TO BE GENERATED.
.*
.*             &CPYRTSW IS A SWITCH USED TO INDICATE WHETHER OR
.*                  NOT A COPYRIGHT NOTICE IS TO BE GENERATED.
.*                  0 = NO; 1 = YES.
.*
.*             &DSAVINC IS A SWITCH USED TO INDICATE WHETHER OR
.*                  NOT THE SIZE SPECIFIED BY THE FIRST SUB-PARA-
.*                  METER OF SAVAREA= INCLUDES A REGISTER SAVE
.*                  AREA.  0 = YES.
.*
.*             &GENEXIT IS A SWITCH USED TO INDICATE WHETHER OR NOT
.*                  EXIT CODE TO RETURN CONTROL TO THE CALLER IS TO
.*                  BE PRODUCED.  '1' = YES, '0' = NO.
.*
.*             &NOSAVAR IS A SWITCH USED TO INDICATE WHETHER OR NOT
.*                  A SAVE AREA AND ASSOCIATED CODE IS TO BE PRODUCED.
.*                  '1' = NO, '0' = YES.
.*
.* L O C A L   C H A R A C T E R   V A R I A B L E S
.*
         LCLC  &IDFLD,&WORKC,&CPYTEXT,&BASES,&DSAVREG(10),&DSAVUSE,&RES*
               T,&FR,&LABHLD
.*
.*             &IDFLD IS USED TO BUILD THE MODULE NAME FIELD
.*                  WHICH IS COMPOSED OF THE CSECT NAME (INSTRUC-
.*                  TION LABEL), VERSION IF ANY, LEVEL IF ANY,
.*                  SYSTEM DATE AND SYSTEM TIME.
.*
.*             &WORKC IS A WORK AREA
.*
.*             &CPYTEXT IS USED TO BUILD THE TEXT OF THE OPTIONAL
.*                  COPYRIGHT NOTIFICATION.
.*
.*             &BASES IS USED TO CONTAIN BASE REGISTERS SPECIFIED
.*                  AFTER THE FIRST FOR USE IN USING STATEMENT.
.*
.*             &DSAVREG IS USED TO FIFO STACK BASE REGS SPECIFIED
.*                  FOR THE DYNAMIC SAVE/WORK AREA.  &DSAVRCT IS
.*                  THE LIMIT COUNT FOR THE STACK.
.*
.*             &DSAVUSE IS USED TO CONTAIN SAVE/WORK AREA BASE
.*                  REGISTERS IN USING STATEMENT FORMAT.
.*
.*             &REST IS USED AS A TEMPORARY HOLD AREA FOR THE
.*                  RESTREG= PARAMETER SO IT OR ITS DEFAULT,
.*                  &SAVEREG=, MAY BE PASSED TO THE EXIT MACRO.
.*
.*             &FR IS USED AS A TEMPORARY HOLD AREA FOR THE FREE=
.*                  PARAMETER SO IT OR A DEFAULT VALUE MAY BE
.*                  PASSED TO THE EXIT MACRO.
.*
.*             &LABHLD IS USED TO HOLD AN INSTRUCTION LABEL FOR
.*                  FUTURE USE WHEN A NEW LABEL MUST BE GENED.
.*
.* G L O B A L   A R I T H M E T I C   V A R I A B L E S
.*
         GBLA  &LC,&LH,&LABERR,&SLCNT
.*
.*             &LC   IS A NUMERIC LABEL QUALIFIER WHICH IS SUFFIXED
.*                  TO THE CHARACTER VARIABLE '&L' AND NUMERIC
.*                  VARIABLE '&LH' TO FORM LABELS WHERE REQUIRED
.*                  FOR THE GENERATED CODE.  ITS INITIAL VALUE IS
.*                  SET BELOW; IT IS INCREMENTED BY 10 AFTER EACH
.*                  USE.  INCREMENTING IS PERFORMED BY INNER MACRO
.*                  INSTRUCTION 'INCRLAB#'.  SEE ALSO '&LH'.
&LC      SETA  10
.*
.*             &LH  IS THE HIGH-ORDER DIGIT OF THE 3 DIGIT LABEL
.*                  QUALIFIER FORMED BY CONCATENATING '&LH&LC'.
.*                  ITS INITIAL VALUE IS ZERO; IT IS INCREMENTED
.*                  BY 1 BY 'INCRLAB#' WHENEVER '&LC' BECOMES
.*                  GREATER THAN 90.  SEE ALSO '&LC'.
&LH      SETA  0
.*
.*             &LABERR IS THE ERROR INDICATOR USED BY INNER MACRO
.*                  INSTRUCTION 'VALAB' TO INDICATE AN ERROR.
.*                  POSSIBLE VALUES ARE:  '1' = THE LABEL IS IN-
.*                  VALID
.*                  OR '0' = THE LABEL IS VALID.
.*
.*             &SLCNT IS USED BY INNER MACRO SUBLIST TO RETURN
.*                  THE NUMBER OF ELEMENTS IN THE SUBLIST IT HAS
.*                  EVALUATED.
.*
.* G L O B A L   B I N A R Y   V A R I A B L E S
.*
         GBLB  &DSAVE,&SLERR,&EXITERR,&ENTERR,&NOSAVAR
.*
.*             &DSAVE IS A SWITCH USED TO INDICATE WHETHER OR NOT
.*                  A DYNAMIC SAVE AREA IS REQUIRED.  1 = YES.
&DSAVE   SETB  0
.*
.*             &SLERR IS AN ERROR SWITCH USED BY INNER MACRO
.*                  SUBLIST.
.*
.*             &EXITERR IS AN ERR SWITCH USED BY THE EXIT MACRO
.*                  WHEN USED AS AN INNER MACRO MACRO.
.*
.*             &ENTERR IS THE ERROR INDICATOR USED BY THE ENTRIES
.*                  MACRO INSTRUCTION TO INDICATE AN ERROR.
.*                  POSSIBLE VALUES ARE: '0' = NO ERROR
.*                  VALIDATION ERROR
.*
.*             &NOSAVAR IS A SWITCH USED TO INDICATE WHETHER OR NOT
.*                  A SAVE AREA AND ASSOCIATED CODE IS TO BE PRODUCED.
.*                  '1' = NO, '0' = YES.
&NOSAVAR SETB  0
.*
.*
.* G L O B A L   C H A R A C T E R   V A R I A B L E S
.*
         GBLC  &L,&R,&DSAVSIZ,&DSAVSP,&SLPARMS(100),&REGNMPX,&CSECT
.*
.*             &L   IS THE FIRST CHARACTER(S), UP TO 3 ARE
.*                  PERMITTED BUT ONE IS PREFERRED, OF LABELS
.*                  GENERATED BY THIS MACRO.  ITS VALUE IS SET
.*                  FROM THE INPUT PARAMETER '&LABCHAR' AFTER
.*                  THAT PARAMETER IS VALIDATED.
.*
.*             &R   IS THE CHARACTER(S) USED TO NAME REGISTERS
.*                  (MINIMUM OF ONE, MAXIMUM OF SIX) SUFFIXED BY
.*                  THE ACTUAL REGISTER NUMBER.  ITS VALUE IS SET
.*                  FROM THE INPUT PARAMETER '&REGNAME' IF IT IS
.*                  SPECIFIED; OTHERWISE '&R' IS NULL.
&R       SETC  ''
.*
.*             &REGNMPX IS THE CHARACTER(S) USED TO GENERATE
.*                  SYMBOLIC REGISTER NAMES STORED FOR REFERENCE
.*                  BY OTHER MACRO INSTRUCTIONS.  ITS VALUE IS SET
.*                  FROM THE INPUT PARAMETER '&REGNAME' IF IT IS
.*                  SPECIFIED; OTHERWISE IT IS NULL.
&REGNMPX SETC  ''
.*
.*             &DSAVSIZ IS THE SIZE OF THE DYNAMIC SAVE/WORK AREA
.*                  REQUIRED.  ITS VALUE IS INITIALIZED AT 72 BUT
.*                  MAY BE OVERRIDDEN BY THE FIRST SUB-PARAMETER
.*                  OF THE SAVAREA= PARAMETER.
&DSAVSIZ SETC  '72'
.*
.*             &DSAVSP IS THE SUBPOOL REQUESTED FOR THE DYNAMIC
.*                  SAVE/WORK AREA.  ITS VALUE IS PRESET TO ZERO
.*                  BUT MAY BE OVERRIDDEN BY THE THIRD SUB-PARA-
.*                  METER OF THE SAVAREA= PARAMETER.
&DSAVSP  SETC  '0'
.*
.*             &SLPARMS IS USED BY INNER MACRO SUBLIST TO PASS
.*                  BACK THE INDIVIDUAL SUBLIST PARAMETERS.
.*                  &SLCNT IS THE NUMBER OF ELEMENTS STACKED IN
.*                  &SLPARMS.
.*
.*             &CSECT IS USED TO PASS THE MODULE CSECT NAME TO
.*                  OTHER MACROS.
.*
.*
.* V A L I D A T E   L A B E L   P A R A M E T E R
.*
         VALAB &LABEL,NAME=LABEL
         AIF   (&LABERR EQ 0).A002
&ERR     SETB  1
         AGO   .A010
.A002    ANOP
&IDFLD   SETC  '&LABEL'
&IDLNGTH SETA  K'&LABEL
.*
.* V A L I D A T E   I N S T R U C T I O N   L A B E L   C H A R S
.*
.A010    ANOP
         VALAB &LABCHAR,MAXLEN=3,NAME=LABCHAR
         AIF   (&LABERR EQ 0).A020
&ERR     SETB  1
         AGO   .B010
.A020    ANOP
&L       SETC  '&LABCHAR'
.*
.* V A L I D A T E   R E G N A M E   P A R A M E T E R
.*
.B010    ANOP
         AIF   (T'&REGNAME EQ 'O').C010
         AIF   (N'&REGNAME LE 2).B020
         MNOTE 4,'MAXIMUM REGNAME SUBFIELDS IS 2'
.B020    ANOP
         AIF   (T'&REGNAME(2) EQ 'O').B040
         AIF   ('&REGNAME(2)' EQ 'EQU').B030
         MNOTE 4,'REGNAME SUBFIELD 2 MUST BE ''EQU'' - ASSUMED'
.B030    ANOP
&REGEQU  SETB  1
.B040    ANOP
         VALAB &REGNAME(1),MAXLEN=6,NAME=REGNAME
         AIF   (&LABERR EQ 0).B050
&ERR     SETB  1
         AGO   .C010
.B050    ANOP
&R       SETC  '&REGNAME(1)'
&REGNMPX SETC  '&R'
.*
.* V A L I D A T E   E N T R I E S   P A R A M E T E R
.*
.C010    ANOP
         AIF   (T'&ENTRIES EQ 'O').D010
         ENTRIES &ENTRIES,MF=V
         AIF   (&ENTERR EQ 0).D010
&ERR     SETB  1
.*
.* C H E C K   F O R   V E R S I O N
.*
.D010    ANOP
         AIF   (T'&VERSION EQ 'O').E010
&WORKC   SETC ' V&VERSION'
&IDFLD   SETC  '&IDFLD.&WORKC'
&IDLNGTH SETA  &IDLNGTH+K'&VERSION+2
.*
.* C H E C K   F O R   L E V E L
.*
.E010    ANOP
         AIF   (T'&LEVEL EQ 'O').F010
&WORKC   SETC '.&LEVEL'
&IDFLD   SETC  '&IDFLD.&WORKC'
&IDLNGTH SETA  &IDLNGTH+K'&LEVEL+1
.*
.* A D D   D A T E   A N D   T I M E   T O   M O D U L E   N A M E
.*
.F010    ANOP
&IDFLD   SETC  '&IDFLD &SYSDATE &SYSTIME'
&IDLNGTH SETA  &IDLNGTH+15
.*
.* C H E C K   F O R   C O P Y R I G H T   R E Q U E S T
.*
         AIF   (T'&CPYRITE EQ 'O').G010
&CPYRTSW SETB  1
         AIF   ('&CPYRITE' NE 'YES').F020
&CPYTEXT SETC ' COPYRIGHT ROHM AND HAAS COMPANY 19'
&IDLNGTH SETA  &IDLNGTH+35
         AGO   .F040
.F020    ANOP
&CNT1    SETA  1
&CNT3    SETA  K'&CPYRITE
&WORKC   SETC  '&CPYRITE'(1,1)
         AIF   ('&WORKC' NE '''').F030
&CNT1    SETA  2
&CNT3    SETA  &CNT3-1
&WORKC   SETC  '&CPYRITE'(K'&CPYRITE,1)
         AIF   ('&WORKC' NE '''').F030
&CNT3    SETA  &CNT3-1
.F030    ANOP
&CPYTEXT SETC  'COPYRIGHT '
&WORKC   SETC  '&CPYRITE'(&CNT1,&CNT3)
&CPYTEXT SETC  '&CPYTEXT.&WORKC'
&WORKC   SETC  ' 19'
&CPYTEXT SETC  '&CPYTEXT.&WORKC'
&IDLNGTH SETA  &IDLNGTH+&CNT3+13
.F040    ANOP
&WORKC   SETC  '&SYSDATE'(7,2)
&IDLNGTH SETA  &IDLNGTH+2
&CPYTEXT SETC  '&CPYTEXT.&WORKC'
.*
.* V A L I D A T E   B A S E R E G   P A R A M E T E R
.*
.G010    ANOP
         AIF   (T'&BASEREG EQ 'O').H010
&BRCNT   SETA  N'&BASEREG
&CNT1    SETA  1
.G020    ANOP
         AIF   (&BRCNT LT &CNT1).H010
         AIF   (T'&BASEREG(&CNT1) EQ 'N').G030
       MNOTE 8,'BASE REGISTERS SPECIFIED MUST BE NUMERIC'
&ERR     SETB  1
         AGO   .G060
.G030    ANOP
         AIF  ((&BASEREG(&CNT1) GT 2) AND (&BASEREG(&CNT1) LT 14)).G040
         MNOTE 8,'BASE REGS SPECIFIED MUST BE > 2 AND < 14'
&ERR     SETB  1
         AGO   .G060
.G040    ANOP
         AIF   (&CNT1 GT 1).G050
&BR1     SETA  &BASEREG(&CNT1)
         AGO   .G060
.G050    ANOP
&WORKC   SETC  '&R&BASEREG(&CNT1)'
&BASES   SETC  '&BASES.,&WORKC'
.G060    ANOP
&CNT1    SETA  &CNT1+1
         AGO   .G020
.*
.* V A L I D A T E   S A V A R E A =   P A R A M E T E R
.*
.H010    ANOP
         AIF   (T'&SAVAREA EQ 'O').I010
         AIF   (T'&BASEREG NE 'O').H015
         AIF   ('&SAVAREA(1)' EQ 'NONE').H016
         MNOTE 8,'BASEREG= MUST BE SPECIFIED WITH DYNAMIC SAVE AREA'
&ERR     SETB  1
.H015    ANOP
         AIF   ('&SAVAREA(1)' NE 'NONE').H017
.H016    ANOP
&NOSAVAR SETB  1
         AGO   .I010
.H017    ANOP
         AIF   (&BR1 NE 13).H018
         MNOTE 8,'REG 13 MAY NOT BE BASE WITH DYNAMIC SAVE AREA'
&ERR     SETB  1
.H018    ANOP
&DSAVE   SETB  1
         AIF   (T'&SAVAREA(1) EQ 'O').H020
         AIF   ('&SAVAREA(1)' EQ 'DYNAM').H020
&DSAVSIZ SETC  '&SAVAREA(1)'
.H020    ANOP
         AIF   (T'&SAVAREA(3) EQ 'O').H030
&DSAVSP  SETC  '&SAVAREA(3)'
.H030    ANOP
         AIF   (T'&SAVAREA(2) EQ 'O').I010
         AIF   ('&SAVAREA(2)' EQ 'INCL').I010
         SUBLIST &SAVAREA(2),10,NAME=WORK/SAVE-AREA-BASE-REGS
         AIF   (&SLERR EQ 0).H040
&ERR     SETB  1
         AGO   .I010
.H040    ANOP
&CNT1    SETA  0
.H050    ANOP
&CNT1    SETA  &CNT1+1
         AIF   (&SLCNT LT &CNT1).I010
         AIF   ('&SLPARMS(&CNT1)' EQ '').H050
         AIF   ('&SLPARMS(&CNT1)' NE '13').H060
         AIF   (&CNT1 EQ 1).H050
         MNOTE 8,'REG 13 IS VALID BASE ONLY FOR FIRST EXTENT OF'
         MNOTE 8,'       DYNAMIC SAVE/WORK AREA'
&ERR     SETB  1
         AGO   .H050
.H060    ANOP
         AIF   (&CNT1 NE 1).H070
&DSAVINC SETB  1
&DSAVSIZ SETC  '&DSAVSIZ.+72'
.H070    ANOP
         AIF   (('&SLPARMS(&CNT1)' GE '2') AND ('&SLPARMS(&CNT1)' LE '1X
               2')).H080
         MNOTE 8,'&SLPARMS(&CNT1) NOT VALID SAVE/WORK AREA BASE REG'
&ERR     SETB  1
         AGO   .H050
.H080    ANOP
&DSAVRCT SETA  &DSAVRCT+1
&DSAVREG(&DSAVRCT) SETC '&SLPARMS(&CNT1)'
&DSAVUSE SETC  '&DSAVUSE,&R&SLPARMS(&CNT1)'
         AGO   .H050
.*
.* V A L I D A T E   G O T O   P A R A M E T E R
.*
.I010    ANOP
         AIF   (T'&GOTO EQ 'O').J010
         VALAB &GOTO,NAME=GOTO
         AIF   (&LABERR EQ 0).J010
&ERR     SETB  1
         AGO   .J010
.*
.* V A L I D A T E   L I N K T O   P A R A M E T E R
.*
.J010    ANOP
         AIF   (T'&LINKTO EQ 'O').K010
         AIF   (T'&GOTO EQ 'O').J020
         MNOTE 4,'LINKTO AND GOTO MUTUALLY EXCLUSIVE; GOTO IGNORED'
.J020    ANOP
         VALAB &LINKTO,NAME=LINKTO
         AIF   (&LABERR EQ 0).K010
&ERR     SETB  1
         AGO   .K010
.*
.* V A L I D A T E   E X I T   G E N   P A R M S
.*
.K010    ANOP
         AIF   (T'&EXIT EQ 'O').L010
         AIF   ('&EXIT' EQ 'NO').L010
         AIF   (T'&LINKTO NE 'O').K020
         MNOTE 8,'LINKTO REQUIRED WITH EXIT'
&ERR     SETB  1
.K020    ANOP
         AIF   ('&EXIT' EQ 'YES').K030
         MNOTE 8,'EXIT= MUST BE ''YES'', ''NO'' OR OMITTED'
&ERR     SETB  1
         AGO   .L010
.K030    ANOP
         AIF   (T'&RESTREG EQ 'O').K040
&REST    SETC  '&RESTREG'
         AGO   .K050
.K040    ANOP
&REST    SETC  '&SAVEREG'
.K050    ANOP
         AIF   (T'&FREE EQ 'O').K060
&FR      SETC  '&FREE'
         AGO   .K080
.K060    ANOP
         AIF   (&DSAVE EQ 0).K070
&FR      SETC  'YES'
         AGO   .K080
.K070    ANOP
&FR      SETC  'NO'
.K080    ANOP
         EXIT  FREE=&FR,RESTREG=&REST,RETCODE=&RETCODE,MF=V
&GENEXIT SETB  1
         AIF   (&EXITERR EQ 0).L010
&ERR     SETB  1
.*
.* V A L I D A T E   P A T C H E S   P A R A M E T E R
.*
.L010    ANOP
         AIF   (T'&PATCHES EQ 'O').M010
         AIF   ('&PATCHES' EQ 'YES').M010
&PATCHNO SETA  0
         AIF   ('&PATCHES' EQ 'NO').N010
         AIF   (T'&PATCHES NE 'N').L020
&PATCHNO SETA  &PATCHES
         AGO   .M010
.L020    ANOP
         MNOTE 8,'PATCHES= MUST BE ''YES'', ''NO'' OR NUMERIC'
&ERR     SETB  1
.*
.*
.*
.M010    ANOP
         AIF   ((T'&GOTO NE 'O') OR (&GENEXIT EQ 1)).N010
         MNOTE 8,'GOTO= OR EXIT=YES MUST BE SPECIFIED WITH PATCH AREA'
&ERR     SETB  1
.N010    ANOP
         AGO   .ERRCHEK
.*
.* C H E C K   F O R   V A L I D A T I O N   E R R O R S
.*
.ERRCHEK ANOP
         AIF   (&ERR EQ 1).Z010
         AGO   .GEN010
.*
.* G E N E R A T E   P R I M A R Y   E N T R Y   P O I N T   C O D E
.*
.GEN010  ANOP
         AIF   (T'&TITLE EQ 'O').GEN011
&LABEL   TITLE &TITLE
.GEN011  ANOP
         AIF   (T'&NOCSECT NE 'O').GEN012
&LABEL   CSECT
&CSECT   SETC  '&LABEL'
         SPACE 3
*
* A C C E P T   C O N T R O L   F R O M   C A L L E R
*
         AIF   (T'&ENTRIES EQ 'O').GEN015
*        PRIMARY ENTRY POINT
*
         AGO   .GEN015
.GEN012  ANOP
&LABEL   DS    0H
.GEN015  ANOP
         USING *,&R.15
         AIF   (T'&ENTRIES EQ 'O').GEN017
         LA    &R.1,0(&R.1)        INDICATE PRIMARY ENTRY POINT
.GEN017  ANOP
         B     &L&LH&LC            BRANCH AROUND MODULE NAME
         DC    AL1(&IDLNGTH)            LENGTH OF NAME
         DC    C'&IDFLD'
         AIF   (&CPYRTSW EQ 0).GEN020
         DC    C'&CPYTEXT'
.*
.* G E N   A L T E R N A T E   E N T R I E S   I F   A N Y
.*
.GEN020  ANOP
         AIF   (T'&ENTRIES EQ 'O').GEN030
         ENTRIES ,&L&LH&LC,MF=G
.*
.* G E N E R A T E   C O M M O N   R E G I S T E R   S A V E
.*
.GEN030  ANOP
&L&LH&LC DS    0H
         INCRLAB#
         STM   &R.14,&R.12,12(&R.13) SAVE CALLER'S REGISTERS
         AIF   (T'&ENTRIES EQ 'O').GEN040
         BALR  &R.15,0             SET COMMON BASE ADDR UNTIL OURS
         USING *,&R.15             IS ESTABLISHED
.*
.*
.*
.GEN040  ANOP
         AIF   (&DSAVE EQ 0).GEN050
.*       LR    &R.2,&R.13
         AGO   .GEN060
.GEN050  ANOP
         LR    &R.14,&R.13          SAVE CALLER'S SAVE AREA ADDR
.*
.* G E N   B A S E   A D D R   F O R   N O N - 1 3   O R   E N T R Y S
.*
.GEN060  ANOP
.*       AIF   (T'&ENTRIES NE 'O').GEN070
         AIF   (&BR1 EQ 13).GEN100
.GEN070  ANOP
         LR    &R.&BR1,&R.15       LOAD INITIAL BASE ADDRESS
         USING &LABEL,&R.&BR1.&BASES ESTABLISH ADDRESSABILITY
         DROP  &R.15
&CNT2    SETA  1
&CNT1    SETA  2
.GEN080  ANOP
         AIF   (&BRCNT LT &CNT1).GEN090
         LA    &R.&BASEREG(&CNT1),4095(&R.&BASEREG(&CNT2))
         LA    &R.&BASEREG(&CNT1),1(&R.&BASEREG(&CNT1))
&CNT1    SETA  &CNT1+1
&CNT2    SETA  &CNT2+1
         AGO   .GEN080
.*
.* G E N E R A T E   I N - L I N E   S A V E   A R E A
.*
.GEN090  ANOP
&BASES   SETC  ' '
.GEN100  ANOP
         AIF   (&NOSAVAR EQ 1).GEN170
         AIF   (&DSAVE EQ 1).GEN110
         CNOP  0,4
         BAL   &R.13,&L&LH&LC      BRANCH AROUND SAVE AREA
         AIF   (&BR1 NE 13).GEN105
         USING *,&R.13&BASES       AND ESTABLISH ADDRESSABILITY
         DROP  &R.15
.GEN105  ANOP
         DC    18F'0'                  SAVE AREA
&L&LH&LC EQU   *
         INCRLAB#
         AIF   (&BR1 NE 13).GEN109
&CNT2    SETA  1
&CNT1    SETA  2
.GEN107  ANOP
         AIF   (&BRCNT LT &CNT1).GEN109
         LA    &R.&BASEREG(&CNT1),4095(&R.&BASEREG(&CNT2))
         LA    &R.&BASEREG(&CNT1),1(&R.&BASEREG(&CNT1))
&CNT1    SETA  &CNT1+1
&CNT2    SETA  &CNT2+1
         AGO   .GEN107
.GEN109  ANOP
         ST    &R.14,4(&R.13)      LINK SAVE AREAS
         ST    &R.13,8(&R.14)
         AGO   .GEN170
.*
.* G E N E R A T E   D Y N A M I C   S A V E   A R E A   C O D E
.*
.GEN110  ANOP
&LABHLD  SETC  '&L&LH&LC'
         INCRLAB#
         CNOP  0,4
         B     &L&LH&LC            BRANCH AROUND CONSTANTS
&LABHLD  DC    AL1(&DSAVSP)        SUBPOOL OF WORKAREA
         DC    AL3(&DSAVSIZ)       SIZE OF WORKAREA
&L&LH&LC DS    0H
         INCRLAB#
         L     &R.0,&LABHLD        LOAD PARAMETER REGISTER
.*       LA    &R.0,&DSAVSIZ       LOAD SIZE OF AREA REQUESTED
.*       AIF   ('&DSAVSP' EQ '0').GEN112
.*       ICM   &R.0,8,=AL1(&DSAVSP) INDICATE SUBPOOL REQUESTED
.GEN112  ANOP
         GETMAIN R,LV=(0)          ALLOCATE DYNAMIC SAVE/WORK AREA
         ST    &R.13,4(&R.1)       LINK SAVE AREAS
         ST    &R.1,8(&R.13)
         LR    &R.13,&R.1          POINT TO CURRENT SAVE AREA
&CNT1    SETA  0
&CNT2    SETA  0
.GEN120  ANOP
&CNT1    SETA  &CNT2
&CNT2    SETA  &CNT2+1
         AIF   (&DSAVRCT LT &CNT2).GEN160
         AIF   (&CNT2 NE 1).GEN140
         AIF   (&DSAVINC EQ 0).GEN130
         LA    &R.&DSAVREG(&CNT2),72(&R.13) SET WORK AREA ADDR
         AGO   .GEN120
.GEN130  ANOP
         LA    &R.&DSAVREG(&CNT2),4095(&R.13)
         AGO   .GEN150
.GEN140  ANOP
         LA    &R.&DSAVREG(&CNT2),4095(&R.&DSAVREG(&CNT1))
.GEN150  ANOP
         LA    &R.&DSAVREG(&CNT2),1(&R.&DSAVREG(&CNT2))
         AGO   .GEN120
.GEN160  ANOP
         L     &R.1,4(&R.13)       RESTORE REG 0 AND 1 CONTENT AT ENTRY
         LM    &R.0,&R.1,20(&R.1)
.*
.* G E N E R A T E   B A L   T O   M A I N L I N E
.*
.GEN170  ANOP
         AIF   (T'&LINKTO EQ 'O').GEN180
         BAL   &R.14,&LINKTO       PERFORM MAINLINE PROCESSING
         AGO   .GEN190
.*
.* G E N E R A T E   B R A N C H   T O   M A I N L I N E
.*
.GEN180  ANOP
         AIF   (T'&GOTO EQ 'O').GEN190
         B     &GOTO
.*
.* G E N E R A T E   E X I T   I F   R E Q U E S T E D
.*
.GEN190  ANOP
         AIF   (&GENEXIT EQ 0).GEN200
         EXIT  MF=G
.*
.* G E N E R A T E   P A T C H   A R E A
.*
.GEN200  ANOP
         AIF   (&PATCHNO EQ 0).GEN210
         EJECT
*
* M O D U L E   P A T C H   A R E A
*
         PUSH  PRINT
         PRINT DATA
PATCHES  DC    &PATCHNO.S(*)
         POP   PRINT
         AGO   .GEN210
.*
.* G E N E R A T E   R E G I S T E R   E Q U A T E S
.*
.GEN210  ANOP
         AIF   (&REGEQU EQ 0).GEN230
         EJECT
*
* R E G I S T E R   N A M E   A S S I G N M E N T S
*
&CNT1    SETA  0
.GEN220  ANOP
         AIF   (&CNT1 GT 15).GEN230
&R.&CNT1 EQU   &CNT1
&CNT1    SETA  &CNT1+1
         AGO   .GEN220
.*
.*
.*
.GEN230  ANOP
         AGO   .Z010
.Z010    ANOP
         MEND
         EJECT
