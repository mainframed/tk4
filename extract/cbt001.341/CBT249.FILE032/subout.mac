         MACRO
&LABEL   SUBOUT
.*
.*       VERSION 01.00 - 8/79 - J.G.KINKER
.*
.*       SUBOUT IS USED IN CONJUNCTION WITH SUBIN.  IT IS CODED
.*       WITHOUT ANY PARAMETERS.  SEE SUBIN FOR DIRECTIONS ON USE
.*       OF THE TWO MACROS.
.*
         GBLA  &SUBINCT
         GBLC  &SUBINRG(16),&SUBINRA(16),&SUBINRB(16),&SUBINRC(16),&R
         LCLA  &CNT
         LCLC  &OP,&COMMA
         AIF   (T'&LABEL EQ 'O').A010
&LABEL   EQU   *
.A010    ANOP
         AIF   (&SUBINCT EQ 0).B010
&CNT     SETA  0
.A020    ANOP
&CNT     SETA  &CNT+1
         AIF   (&CNT GT &SUBINCT).B010
&OP      SETC  '&SUBINRG(&CNT)'
         AIF   ('&SUBINRC(&CNT)' EQ ' ').B000
&COMMA   SETC  ','
         AGO   .B005
.B000    ANOP
&COMMA   SETC  ' '
.B005    ANOP
         &OP   &SUBINRA(&CNT),&SUBINRB(&CNT)&COMMA&SUBINRC(&CNT)
         AGO   .A020
.B010    ANOP
         BR    &R.14
         MEND
