         MACRO
         ENTRIES &ENTPARM,&BRANCH,&REGNAME=,&MF=
.*
.*       THE ENTRIES MACRO INSTRUCTION GENERATES ENTRY CODE FOR
.*       ONE OR MORE ALTERNATE PROGRAM ENTRY POINTS IN THE FORM:
.*
.*                       SPACE 3
.*                       ENTRY ENTRY-POINT-NAMES
.*              ENTRYNM1 DS    0H
.*                       USING *,15
.*                       B     ICM
.*                       DC    X'L'ENTRYNM1'
.*                       DC    C'ENTRYNM1'
.*                       ICM   R1,8,=AL1(FLAG-VALUE)
.*                       B     BRANCH-POINT
.*                       SPACE 3
.*              ENTRYNM2 DS    0H
.*                       ETC.
.*
.*       IT IS INVOKED BY CODING:
.*
.*                              --      --
.*             ENTRIES ((ENTRYNM×,FLAGVAL×)....),BRANCHNAME
.*                              --      --
.*                     --                -- --       --
.*                     ×,REGNAME=REGPREFIX× ×,MF=G × V×
.*                     --                -- --       --
.*
.*             ENTRYNM IS REPLACED BY A VALID ENTRY POINT LABEL.
.*
.*             FLAGVAL IF SUPPLIED IS REPLACED BY A NUMERIC VALUE
.*                  WHICH WILL BE INSERTED INTO THE HIGH-ORDER 8
.*                  BITS OF REGISTER ONE.  IF IT IS NOT SUPPLIED,
.*                  A VALUE OF FOUR IS SUPPLIED FOR THE FIRST
.*                  ENTRY, EIGHT FOR THE SECOND, ETC.
.*
.*             BRANCHNAME IS REPLACED BY A VALID BRANCH LABEL TO
.*                  WHICH CONTROL WILL BE PASSED FROM THE ENTRY.
.*
.*             REGPREFIX IS REPLACED BY A VALID ASSEMBLER LABEL
.*                  UP TO 6 CHARACTERS LONG WHICH WILL PREFIX
.*                  REGISTER NUMBERS WHERE THEY ARE USED.  IF
.*                  REGNAME=R IS SPECIFIED, THE RESULTING USING
.*                  WOULD BE 'USING *,R15'.
.*
.*             USE OF THE MF= OPERAND PERMITS USE OF THE ENTRIES
.*             MACRO INSTRUCTION AS AN INNER MACRO.  WHEN MF=V IS
.*             CODED, ONLY VALIDATION OF ENTRYNM, FLAGVAL AND
.*             REGPREFIX IS PERFORMED.  ERRORS ARE INDICATED BY A
.*             1 IN GLOBAL BINARY VARIABLE &ENTERR.  WHEN MF=G IS
.*             CODED, ONLY BRANCHNAME NEED BE SUPPLIED.  IT IS
.*             VALIDATED AND THE ENTRY CODE IS GENERATED FROM
.*             PREVIOUSLY SUPPLIED PARAMETERS STORED IN GLOBAL
.*             VARIABLES.
.*
.*             IF THE CALLING MACRO SUPPORTS A 'REGPREFIX', THE
.*             REGNAME= PARAMETER NEED NOT BE SPECIFIED IF A
.*             GLOBAL CHARACTER VARIABLE NAMED '&R' IS USED TO
.*             CONTAIN THE PREFIX.
.*
.* G L O B A L   A R I T H M E T I C   V A R I A B L E S
.*
         GBLA  &ENTCNT,&ENTNMSZ(256),&ENTFLGS(256),&SLCNT,&OPSIZE,&LABEX
               RR
.*
.*             &ENTCNT IS USED TO COUNT THE NUMBER OF ENTRY POINTS
.*                  AND INDEX THROUGH THE DIMENSIONED GLOBAL
.*                  VARIABLES DURING VALIDATION.  DURING GENERATION
.*                  IT SERVES AS LOOP CONTROL.
.*
.*             &ENTNMSZ IS USED TO STORE THE LENGTH OF EACH ENTRY
.*                  POINT NAME.
.*
.*             &ENTFLGS IS USED TO STORE THE FLAG VALUE ASSOCIATED
.*                  WITH EACH ENTRY POINT.
.*
.*             &SLCNT IS USED TO RETURN THE NUMBER OF ITEMS IN A
.*                  SUBLIST FROM THE SUBLIST INNER MACRO.
.*
.*             &OPSIZE IS USED TO RETURN THE SIZE OF AN OPERAND
.*                  FROM THE OPSIZE INNER MACRO.
.*
.*             &LABERR IS A SWITCH USED TO INDICATE DETECTION OF
.*                  AN ERROR BY THE VALAB INNER MACRO.
.*
.* G L O B A L   B I N A R Y   V A R I A B L E S
.*
         GBLB  &ENTERR,&ENTVAL,&SLERR
.*
.*             &ENTERR IS A SWITCH USED TO INDICATE A VALIDATION
.*                  ERROR IN THIS MACRO. 0 = OK; 1 = ERR.
.*
.*             &ENTVAL IS A SWITCH USED TO INDICATE THAT AN MF=V
.*                  INVOCATION HAS PRECEEDED AN MF=G INVOCATION.
.*
.*             &SLERR IS A SWITCH USED TO INDICATE AN ERROR DETEC-
.*                  TED BY THE SUBLIST INNER MACRO.
.*
.* G L O B A L   C H A R A C T E R   V A R I A B L E S
.*
         GBLC  &ENTRY,&ENTNMS(256),&SLPARMS(100),&R
.*
.*             &ENTRY IS USED TO BUILD THE STRING OF ENTRY POINT
.*                  NAMES SEPARATED BY COMMAS WHICH IS  THE OPER-
.*                  AND OF THE ENTRY STATEMENT.
.*
.*             &ENTNMS IS USED TO STORE THE NAME OF EACH ENTRY
.*                  POINT.
.*
.*             &SLPARMS IS USED TO PASS THE DECODED OPERANDS BACK
.*                  FROM THE SUBLIST INNER MACRO.
.*
.*             &R IS USED TO STORE OR RECEIVE FROM A HIGHER LEVEL
.*                  MACRO THE REGISTER NAME PREFIX.
.*
.* L O C A L   A R I T H M E T I C   V A R I A B L E S
.*
         LCLA  &CNT1,&CNT2,&ES,&EF
.*
.*             &CNT1 IS USED AS AN INDEX.
.*
.*             &CNT2 IS USED TO ADJUST ENTRY NAME LENGTH TO A
.*                  MULTIPLE OF TWO
.*
.*             &ES IS USED TO CONTAIN AN ENTRY NAME LENGTH DURING
.*                  GENERATION.
.*
.*             &EF IS USED TO CONTAIN A FLAG VALUE DURING GENERAT-
.*                  ION.
.*
.* L O C A L   C H A R A C T E R   V A R I A B L E S
.*
         LCLC  &EN
.*
.*             &EN IS USED TO CONTAIN AN ENTRY NAME DURING GENER-
.*                  ATION.
.*
.* V A L I D A T E   M F =   P A R A M E T E R
.*
.A010    ANOP
         AIF   (T'&MF EQ 'O').B020
         AIF   ('&MF' EQ 'G').A020
         AIF   ('&MF' EQ 'V').B010
         MNOTE 8,'ENTRIES MF= PARM MUST BE ''G'', ''V'' OR OMITTED'
&ENTERR  SETB  1
         AGO   .B010
.A020    ANOP
         AIF   (&ENTVAL EQ 1).D010
         MNOTE 8,'MF=G MUST BE PRECEDED BY MF=V INVOCATION OF ENTRIES'
&ENTERR  SETB  1
         AGO   .Z010
.*
.* V A L I D A T E   R E G N A M E =   P A R A M E T E R
.*
.B010    ANOP
&ENTVAL  SETB  1                        INDICATE VALIDATE PASS DONE
         AGO   .B025
.B020    ANOP
&ENTVAL  SETB  0
.B025    ANOP
&ENTCNT  SETA  0
&ENTERR  SETB  0
&ENTRY   SETC  ''
.*
.*
.*
         AIF   (T'&REGNAME EQ 'O').C010
         VALAB &REGNAME,6,NAME=REGNAME
         AIF   (&LABERR EQ 0).B030
&ENTERR  SETB  1
         AGO   .C010
.B030    ANOP
&R       SETC  '&REGNAME'
.*
.* V A L I D A T E   E N T R Y   P A R A M E T E R S
.*
.C010    ANOP
&CNT1    SETA  0
         AIF   (T'&ENTPARM NE 'O').C020
         MNOTE 8,'AT LEAST 1 ENTRY POINT MUST BE SPECIFIED'
&ENTERR  SETB  1
         AGO   .D010
.C020    ANOP
&CNT1    SETA  &CNT1+1
         AIF   (&CNT1 GT N'&ENTPARM).D010
         SUBLIST &ENTPARM(&CNT1),2,NAME=ENTRY(&CNT1)
         AIF   (&SLERR EQ 0).C030
         MNOTE 8,'ERROR EVALUATING ENTPARM(&CNT1)'
&ENTERR  SETB  1
         AGO   .C020
.*
.* S A V E   E N T R Y   N A M E   I F   V A L I D
.*
.C030    ANOP
         AIF   (&SLCNT EQ 0).C020
         AIF   ('&SLPARMS(1)' NE '').C050
         MNOTE 8,'&ENTPARM(&CNT1) ENTRY NAME MUST BE SUPPLIED'
&ENTERR  SETB  1
         AGO   .C090
.C050    ANOP
&ENTCNT  SETA  &ENTCNT+1
         VALAB &SLPARMS(1),NAME=ENTRY-NAME
         AIF   (&LABERR EQ 0).C060
&ENTERR  SETB  1
.C060    ANOP
&ENTNMS(&ENTCNT) SETC '&SLPARMS(1)'
         AIF   (&ENTCNT GT 1).C070
&ENTRY   SETC  '&SLPARMS(1)'
         AGO   .C080
.C070    ANOP
&ENTRY   SETC  '&ENTRY,&SLPARMS(1)'
.*
.* G E T   L E N G T H   O F   E N T R Y   N A M E
.*
.C080    ANOP
         OPSIZE &SLPARMS(1)
&ENTNMSZ(&ENTCNT) SETA &OPSIZE
.*
.* S A V E   E N T R Y   F L A G   V A L U E
.*
.C090    ANOP
         AIF   ('&SLPARMS(2)' EQ '').C120
         AIF   (&SLCNT LT 2).C120
.C100    ANOP
         AGO   .C110
         AIF   (T'&SLPARMS(2) EQ 'N').C110
         MNOTE 8,'&ENTPARM(&CNT1) ENTRY FLAG MUST BE NUMERIC'
&ENTERR  SETB  1
         AGO   .C020
.C110    ANOP
&ENTFLGS(&ENTCNT) SETA &SLPARMS(2)
         AGO   .C020
.*
.*       NONE SUPPLIED - GENERATE ONE
.*
.C120    ANOP
&ENTFLGS(&ENTCNT) SETA &ENTCNT*4
         AGO   .C020
.*
.* V A L I D A T E   B R A N C H   P O I N T
.*
.D010    ANOP
         AIF   ('&MF' EQ 'V').Z010
         VALAB &BRANCH,NAME=BRANCH-POINT
         AIF   (&LABERR EQ 0).E010
&ENTERR  SETB  1
.*
.* C H E C K   F O R   V A L I D A T I O N   E R R O R S
.*
.E010    ANOP
         AIF   (&ENTERR EQ 1).Z010
.*
.* G E N E R A T E   E N T R Y   P O I N T S
.*
         SPACE 3
         ENTRY &ENTRY
&CNT1    SETA  0
.GEN010  ANOP
&CNT1    SETA  &CNT1+1
&EN      SETC  '&ENTNMS(&CNT1)'
&EF      SETA  &ENTFLGS(&CNT1)
&ES      SETA  &ENTNMSZ(&CNT1)
&CNT2    SETA  &ES+6
&CNT2    SETA  &CNT2/2
&CNT2    SETA  &CNT2*2
&EN      DS    0H
         USING *,&R.15
         B     *+&CNT2             BRANCH AROUND ENTRY POINT NAME
         DC    X'&ES'              LENGTH OF NAME FIELD
         DC    C'&EN'              NAME FIELD
         ICM   &R.1,8,=AL1(&EF)    SET ENTRY FLAG IN REG ONE
         B     &BRANCH             BRANCH
         AIF   (&CNT1 GE &ENTCNT).GEN020
         SPACE 3
         AGO   .GEN010
.GEN020  ANOP
         LTORG
         SPACE 3
.*
.* M A C R O   E X I T
.*
.Z010    MEND
         EJECT
