         SPACE 3
***********************************************************************
*                                                                     *
*                         D S I E X 1 4                               *
*                                                                     *
*         N C C F   N O R M A L   L O G O F F   E X I T               *
*                                                                     *
*                                                                     *
*              J . G . K I N K E R   -   0 4 / 8 2                    *
*                                                                     *
***********************************************************************
*                                                                     *
*        FUNCTION:                                                    *
*              THIS IS AN NCCF NORMAL LOGOFF EXIT.  IT IS INVOKED     *
*              WHEN AN OST OR NNT SUBTASK IS ABOUT TO TERMINATE       *
*              NORMALLY.  ITS PURPOSE IS TO FREE THE PRIVATE DATA     *
*              AREA ACQUIRED BY MODULE M99A0N, THE NCCF/ACF2 LOGON    *
*              SECURITY MODULE.                                       *
*                                                                     *
*        INPUT TO THIS MODULE IS:                                     *
*              A STANDARD OS PARAMETER LIST AS DEFINED BY DSECT       *
*              DSIUSE.                                                *
*                                                                     *
*        OUTPUT OF THIS MODULE IS:                                    *
*              NONE.                                                  *
*                                                                     *
*        OPERATION:                                                   *
*              THE ENTRY LINKAGE USED BY NCCF AND THIS MODULE IS      *
*              STANDARD EXCEPT THAT NCCF SUPPLIES A SAVE AREA AND     *
*              256 BYTE WORK AREA FOR THE EXIT'S USE IN AN AREA       *
*              MAPPED BY DSECT DSISWB AND POINTED TO BY THE INPUT     *
*              PARAMETER LIST.  THIS MODULE IS THEREFORE REENTRANT    *
*              WITHOUT THE GETMAIN/FREEMAIN OVERHEAD NORMALLY         *
*              REQUIRED TO GET AND FREE SAVEAREAS AND WORKING         *
*              STORAGE.                                               *
*                                                                     *
*        RETURN CODES ISSUED:                                         *
*              ZERO - SUCCESSFULL EXECUTION                           *
*                                                                     *
*        EXTERNAL REFERENCES:                                         *
*              NONE                                                   *
*                                                                     *
*        RELATED MODULES:                                             *
*              M99A0N - NCCF/ACF2 LOGON SECURITY MODULE - OBTAINS     *
*                       THE AREA FREED BY THIS MODULE.  REVIEW FOR    *
*                       IMPACT OF CHANGES TO THIS MODULE.  SOURCE     *
*                       IS IN PANVALET.                               *
*                                                                     *
*        JCL TO ASSEMBLE AND LINK:                                    *
*             //S1     EXEC CP0102,LMOD=DSIEX14,                      *
*             //            MAC3='MAD.SYS.ACF.ACFMAC',                *
*             //            LPARM=RENT,                               *
*             //            PARM.ASSEM=RENT                           *
*             //ASSEM.SYSIN DD DSN=MAD.SYS.VTAM.MSOURCE(DSIEX14),     *
*             //            DISP=SHR                                  *
*                                                                     *
*        THIS MODULE RESIDES IN MVS.UTILLIB.                          *
*                                                                     *
*        MAINTENANCE ACTIVITY:                                        *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
DSIEX14  ENTER VERSION=01,LEVEL=00,SAVAREA=NONE,BASEREG=(12),          *
               TITLE='NCCF NORMAL LOGOFF EXIT - 04/82 - J.G.KINKER'
         SPACE 6
*
* G L O B A L   A S S E M B L Y   V A R I A B L E S
*
         GBLC  &SCN,&RSVPFX,&ERT,&EST,&CSECT,&SCNDLM
&SCN     SETC  'S'       SCAN SUBROUTINE IDENTIFIER - 1 OR 2 CHARS
&SCNDLM  SETC  'VODLMCHK' SCAN SUBRTN DELIMITER STORAGE FIELD, OPTIONAL
&RSVPFX  SETC  'VA'      REGISTER SAVE AREA PREFIX - 2 CHARS
&EST     SETC  'VO'      ERROR ROUTINE STORAGE AREA PREFIX - 2 CHARS
&ERT     SETC  'Z'       ERROR ROUTINE IDENTIFIER - 1 OR 2 CHARS
         SPACE 6
*
* G L O B A L   R E G I S T E R   U S E   A S S I G N M E N T S
*
         USING ACCVT,R3       ADDR'G ACF2 CVT                         *
         USING ACTRM,R4       ADDR'G ACF2 CONTROL BLOCK AREA          *
         USING W2,R5          ADDR'G PRIVATE DATA AREA
         USING W1,R6          ADDR'G OUR WORK AREA
         USING DSITIB,R7      ADDR'G TASK INFORMATION BLOCK (TIB)
         USING DSITVB,R8      ADDR'G TASK VECTOR BLOCK (TVB)
         USING DSIMVT,R9      ADDR'G MAIN VECTOR TABLE (MVT)
         USING DSISWB,R10     ADDR'G SERVICE ROUTINE WORK BLOCK (SWB)
         USING DSIUSE,R11     ADDR'G INPUT PARAMETER LIST
*              R12            MODULE BASE REGISTER
*              R13            SAVE AREA ADDRESS
*              R14            SUBROUTINE LINKAGE
*              R15            WORK AND RETURN CODES
*
         EJECT
***********************************************************************
*                                                                     *
*    COMPLETE ENTRY LINKAGE AND ESTABLISH DATA ADDRESSABILITY         *
*                                                                     *
***********************************************************************
         LR    R11,R1              SAVE PTR TO INPUT PARAMETER LIST
         L     R10,USERSWB         SET PTR TO SWB
         LA    R13,SWBSAVEA        SET PTR TO OUR SAVE AREA IN SWB
         ST    R13,8(R14)          COMPLETE STANDARD LINKAGE
         ST    R14,4(R13)
         LA    R6,SWBADATD         SET PTR TO AREA WE CAN USE FOR WORK
         L     R7,SWBTIB           SET PTR TO TIB
         L     R8,TIBTVB           SET PTR TO TVB
         L     R9,TVBMVT           SET PTR TO TVB
         CLI   TVBCBH+CBHTYPE-DSICBH,CBHOST IS THIS AN OST TERMINATING
         BNE   A900                B IF NOT TO EXIT
         SPACE 6
***********************************************************************
*                                                                     *
*    FREE STORAGE CHAINED FROM TVBUFLD                                *
*                                                                     *
***********************************************************************
         ICM   R5,B'1111',TVBUFLD  GET STORAGE PTR FROM TVBUFLD
         BZ    A900                IF NO PTR, EXIT
         CLC   =C'NCVT',W2IDENT    IS THE AREA WHAT WE EXPECT
         BNE   A900                B IF NOT
         L     R4,W2AACF2          SET PTR TO ACF2 CONTROL BLOCK AREA
         MVI   ACTFCN,X'05'        SET FREE RESOURCES FUNCTION CODE
         MVI   ACTSFCN,ACTSMINI    SET SUB FUNCTION CODE
         LA    R14,W2MSGBUF        SET PTR TO MESSAGE BUFFER
         ST    R14,ACTMSG
         L     R14,W2AACMCB        SET PTR TO ACMCB
         ST    R14,ACTUCB
         ACFINCVT R3,NONE=A100     GET PTR TO ACF2 CVT
         ACFSVC ACVALD,TYPE=A,NONE=A100,CVT=HAVE CALL ACF2 TO FREE
A100     TAG
         L     R2,W2SIZE           GET LENGTH OF THE AREA
*
*   NOTE THAT THE EXIT PARAMETER ON DSIFRE IS SPECIFIED AS 'NO' EVEN
*   THOUGH WE ARE AN EXIT.  THIS IS BECAUSE THE STORAGE WE ARE TRYING
*   TO FREE WAS QUEUED BY A NON-EXIT.  THE EXIT PARAMETER TELLS NCCF
*   WHICH STORAGE QUEUE THE AREA TO BE FREED IS ON.
*
         DSIFRE LV=(R2),           FREE THE STORAGE                    *
               A=TVBUFLD,LISTA=W1FREWRK,Q=YES,TASKA=(R8),EXIT=NO
***********************************************************************
*                                                                     *
*   EXIT WITH ZERO RETURN CODE                                        *
*                                                                     *
***********************************************************************
A900     TAG
         LA    R15,USERASIS        SET CONTINUE RETURN CODE
         L     R13,4(R13)
         EXIT  RETCODE=(15)
         EJECT
***********************************************************************
*                                                                     *
*   V E N D O R   S U P P L I E D   D S E C T S                       *
*                                                                     *
***********************************************************************
         ACCVT
         EJECT
         ACTRM
&CSECT   CSECT
         EJECT
         DSICBS DSIMVT,DSITVB,DSITIB,DSISVL,DSISWB,DSIUSE,EJECT=YES
         EJECT
***********************************************************************
*                                                                     *
*        D S E C T   F O R   W O R K I N G   S T O R A G E            *
*                                                                     *
***********************************************************************
W1       DSECT
W1WORK   DS    0CL256    MAXIMUM LENGTH OF WORK AREA
         DS    0F             FREEMAIN PARAMETER LIST ALIGNMENT
W1FREWRK DS    CL14           AREA FOR FREEMAIN PARAMETER LIST
&CSECT   CSECT
         EJECT
***********************************************************************
*                                                                     *
*   D S E C T   F O R   P R I V A T E   W O R K   A R E A             *
*                                                                     *
***********************************************************************
W2       DSECT
W2WORK   DS    0D
W2IDENT  DS    CL4       'NCVT' AREA IDENTIFIER
W2SIZE   DS    F         SIZE OF THIS AREA - USED TO FREE IT AT LOGOFF
W2CVT    DS    0D        PRIVATE COMMUNICATIONS VECTOR TABLE
W2AACF2  DS    F              PTR TO ACF2 CONTROL BLOCK AREA
W2AACMCB DS    F              PTR TO ACF2 ACMCB CONTROL BLOCK
W2ALID   DS    F              PTR TO ACF2 LOGONID RECORD
W2CVTEND DS    0D             END OF PRIVATE CVT
W2CVTSIZ EQU   *-W2CVT        SIZE OF CVT AREA
W2ACF2   DS    0D        ACF2 CONTROL BLOCK AREA
         ACVALD DSECT=NO      ACF2 VALIDATION SVC PARAMETER LIST
W2MSGBUF DS    CL132          BUFFER FOR ACF2 MESSAGES
W2ACFEND DS    0D             END OF ACF2 CONTROL BLOCK AREA
W2ACFSIZ EQU   *-W2ACF2       SIZE OF ACF2 CONTROL BLOCK AREA
W2WRKSIZ EQU   *-W2WORK  LENGTH OF THE PRIVATE WORK AREA
&CSECT   CSECT
         EJECT
         END   &CSECT
