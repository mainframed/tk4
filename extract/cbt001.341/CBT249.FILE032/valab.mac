         MACRO
         VALAB &VALABEL,&MINLEN=1,&MAXLEN=8,&CHARS=ANN,&NAME=
.*
.*             THIS MACRO VALIDATES AN ASSEMBLER LABEL PARAMETER.
.*             THE CALLER MAY SPECIFY: MINIMUM AND MAXIMUM LENGTH
.*             (DEFAULT IS 1 AND 8 RESPECTIVELY) AND THE ACCEPT-
.*             ABLE CHARACTERS (DEFAULT IS ASSEMBLER STANDARD
.*             SPECIFIED AS 'ANN'; OTHER VALUES ARE ALPHANUMERIC
.*             BUT NO NATIONAL, 'AN'; AND ALPHABETIC ONLY, 'A').
.*             THIS MACRO ISSUES APPROPRIATE ERROR MESSAGES AND
.*             INDICATES AN ERROR THRU THE GLOBAL ARITH  VARIABLE
.*             'LABERR'.
.*
         GBLA  &LABERR
&LABERR  SETA  0
         LCLA  &CNT
         LCLC  &CHECK
.*
.* V A L I D A T E   M I N L E N   P A R A M E T E R
.*
         AIF   (T'&MINLEN EQ 'O').A010
         AIF   (T'&MINLEN NE 'N').A010
         AIF   ((&MINLEN GE 1) AND (&MINLEN LE 8)).B010
.A010    MNOTE 16,'VALAB ERR; CONTACT SYSTEMS PROGRAMMING'
&LABERR  SETA  2
.*
.* V A L I D A T E   M A X L E N   P A R A M E T E R
.*
.B010    AIF   (T'&MAXLEN EQ 'O').B020
         AIF   (T'&MAXLEN NE 'N').B020
         AIF   ((&MAXLEN GE 1) AND (&MAXLEN LE 8)).C010
.B020    MNOTE 20,'VALAB ERR; CONTACT SYSTEMS PROGRAMMING'
&LABERR  SETA  2
         AGO   .D010
.*
.* V A L I D A T E   M A X L E N   V S   M I N L E N
.*
.C010    AIF   (&MAXLEN GE &MINLEN).D010
         MNOTE 24,'VALAB ERR; CONTACT SYSTEMS PROGRAMMING'
&LABERR  SETA  2
.*
.* V A L I D A T E   F I E L D   N A M E   P A R A M E T E R
.*
.D010    AIF   (T'&NAME NE 'O').E010
         MNOTE 28,'VALAB ERROR; CONTACT SYSTEMS PROGRAMMING'
&LABERR  SETA  2
.*
.* V A L I D A T E   L A B E L   P R E S E N C E
.*
.E010    AIF   (T'&VALABEL NE 'O').F010
         MNOTE 08,'&NAME MUST BE SPECIFIED'
&LABERR  SETA  1
.*
.* V A L I D A T E   C H A R S   P A R A M E T E R
.*
.F010    AIF   (T'&CHARS EQ 'O').F020
         AIF   ('&CHARS' EQ 'A').H010
         AIF   ('&CHARS' EQ 'AN').H010
         AIF   ('&CHARS' EQ 'ANN').H010
.F020    MNOTE 36,'VALAB ERROR; CONTACT SYSTEMS PROGRAMMING'
&LABERR  SETA  2
         AGO   .H010
.*
.* C H E C K   F O R   I N P U T   E R R O R S
.*
.H010    AIF   (&LABERR GT 0).Z010
.*
.* C H E C K   L E N G T H   O F   L A B E L
.*
&CNT     SETA  1
         AIF   ((K'&VALABEL GE &MINLEN) AND (K'&VALABEL LE &MAXLEN)).I0+
               10
         MNOTE 8,'&NAME > OR < PROPER LENGTH OR OMITTED'
&LABERR  SETA  1
.*
.* S E T   L A B E L   C H A R A C T E R   T O   C H E C K
.*
.I010    ANOP
&CHECK   SETC  '&VALABEL'(&CNT,1)
.*
.* C H E C K   F O R   A L P H A B E T I C S
.*
         AIF   (('&CHECK' GE 'A') AND ('&CHECK' LE 'Z')).I070
.*
.* C H E C K   F O R   N U M E R I C S
.*
         AIF   (('&CHECK' LT '0') OR  ('&CHECK' GT '9')).I050
         AIF   ('&CHARS' NE 'A').I030
         MNOTE 8,'&NAME MUST BE ALL ALPHABETIC (&CNT)'
&LABERR  SETA  1
         AGO   .I070
.I030    ANOP
         AIF   (&CNT NE 1).I070
         MNOTE 8,'FIRST CHARACTER OF &NAME MAY NOT BE NUMERIC'
&LABERR  SETA  1
         AGO   .I070
.*
.* C H E C K   F O R   N A T I O N A L   C H A R A C T E R S
.*
.I050    ANOP
         AIF   (('&CHECK' EQ '$') OR ('&CHECK' EQ '#') OR ('&CHECK' EQ +
               '@')).I060
         MNOTE 8,'&NAME CONTAINS AN ILLEGAL CHARACTER (&CHECK, &CNT)'
&LABERR  SETA  1
         AGO   .I070
.I060    ANOP
         AIF   ('&CHARS' EQ 'ANN').I070
         MNOTE 8,'&NAME MAY CONTAIN ONLY ALPHA OR NUMERICS (&CNT)'
&LABERR  SETA  1
         AGO   .I070
.*
.* V A L I D A T I O N   L O O P   C O N T R O L
.*
.I070    ANOP
&CNT     SETA  &CNT+1
         AIF   (K'&VALABEL GE &CNT).I010
.*
.* E N D   O F   M A C R O
.*
.Z010    MEND
