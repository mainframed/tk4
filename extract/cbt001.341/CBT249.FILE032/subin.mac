         MACRO
&LABEL   SUBIN &A,&B,&MF=
.*
.*       VERSION 01.01 - 8/79 - J.G.KINKER
.*
.*       THIS MACRO GENERATES INTERNAL SUBROUTINE ENTRY CODE AS
.*       FOLLOWS:
.*                LABEL  DS    0H
.*                       ST    R14,PRE.SV14
.*                       ST(M) USER-REGS,PRE.SV.REGNUM  (OPTIONAL)
.*       IT IS CODED:
.*                           --    -- --          -- --   --
.*             LABEL   SUBIN ×PREFIX× ×,(USER-REGS)× ×,MF=L×
.*                           --    -- --          -- --   --
.*       LABEL IS REQUIRED AND IS REPLACED BY A VALID ASSEMBLER
.*             LABEL.  THE FIRST TWO NON-NUMERIC CHARACTERS OF THIS
.*             LABEL ARE USED TO MAKE GENERATED DATA NAMES UNIQUE.
.*
.*       PREFIX IS REPLACED BY TWO CHARACTERS WHICH FORM THE FIRST
.*             TWO CHARACTERS OF GENERATED DATA NAMES.  THIS PARAMETER
.*             MAY BE OMITTED IF THESE TWO CHARACTERS ARE PRESET BY
.*             ANOTHER SUBIN MACRO OR BY THE USER IN A GLOBAL
.*             CHARACTER VARIABLE NAMED '&RSVPFX'.
.*
.*       USER-REGS IS REPLACED BY THE NUMBER OF ONE OR MORE REGISTERS
.*             TO BE SAVED BY THE MACRO IN ADDITION TO REGISTER 14
.*             WHICH IS ALWAYS SAVED.  THE REGISTERS MAY NOT BE
.*             SPECIFIED SYMBOLICALLY.
.*
.*       MF=L IS CODED TO CAUSE GENERATION OF ALL DATA AREAS REQUIRED
.*             BY SUBIN MACRO INSTRUCTIONS CODED IN THE PROGRAM.  THE
.*             SUBIN MACRO WITH THIS PARAMETER SHOULD BE PLACED AT THE
.*             END OF THE PROGRAM.  NO OTHER PARAMETERS ARE NECESSARY
.*             WHEN MF=L IS CODED.
.*
.*       DATA NAMES GENERATED BY THIS MACRO HAVE THE FOLLOWING FORM
.*             PREFIX.LABEL-CHARACTERS.SV.REGISTER-NUMBER
.*       OR FOR:
.*             A010 SUBIN VA,(2,3):
.*                           VAASV14, VAASV2 AND VAASV3
.*             BD010 SUBIN W1,(2,3):
.*                           W1BDSV14, W1BDSV2 AND W1BDSV3
.*       IN GENERAL THERE SHOULD BE NO NEED FOR THE USER TO REFER TO
.*       REGISTER SAVE AREAS GENERATED BY THE MACRO.  A SISTER MACRO
.*       INSTRUCTION, SUBOUT, IS AVAILABLE TO RELOAD ALL REGISTERS
.*       SAVED BY SUBIN.  TO USE THESE TWO MACRO INSTRUCTIONS YOU CODE
.*             BAL R14,A010       TO PASS CONTROL TO A010
.*             .
.*             .
.*       A010  SUBIN VA,(OPTIONAL-REGISTERS)
.*             .
.*             .
.*             SUB-ROUTINE INSTRUCTIONS
.*             .
.*             .
.*             SUBOUT
.*
.*       SUBIN SAVES REGISTER 14 AND ANY OPTIONAL REGISTERS YOU
.*       SPECIFY.  SUBOUT RESTORES THE REGISTERS SAVED BY SUBIN AND
.*       GENERATES A BRANCH TO REGISTER 14.
.*
.*
         GBLA  &SUBINCT,&SUBINSC
         GBLC  &SUBINRG(16),&SUBINRA(16),&SUBINRB(16),&SUBINRC(16),&SUB*
               INSL(500),&REGNMPX,&RSVPFX
         LCLC  &LB,&CHECK,&R,&L
         LCLA  &CNT,&PNTR,&NUMBR1,&NUMBR2,&FIRST,&LAST
&R       SETC  '&REGNMPX'
&L       SETC  '&RSVPFX'
&CNT     SETA  0
&SUBINCT SETA  0
         AIF   ('&MF' EQ 'L').C010
         AIF   (T'&A NE 'O').A005
         AIF   ('&L' NE '').A010
         MNOTE 8,'REG SAVE AREA LABEL PREFIX MUST BE SPECIFIED'
         AGO   .Z010
.A005    ANOP
&L       SETC  '&A'
         AIF   ('&RSVPFX' NE '').A010
&RSVPFX  SETC  '&L'
.A010    ANOP
&CNT     SETA  &CNT+1
&CHECK   SETC  '&LABEL'(&CNT,1)
         AIF   (('&CHECK' LT 'A') OR ('&CHECK' GT 'Z')).A020
&LB      SETC  '&LB.&CHECK'
.A020    ANOP
         AIF   (&CNT LT 2).A010
&LABEL   DS    0H
         ST    &R.14,&L&LB.SV14
&PNTR    SETA  1
&SUBINRG(&PNTR) SETC 'L'
&SUBINRA(&PNTR) SETC '&R.14'
&SUBINRB(&PNTR) SETC '&L&LB.SV14'
&SUBINRC(&PNTR) SETC ' '
&SUBINSC SETA  &SUBINSC+1
&SUBINSL(&SUBINSC) SETC '&L&LB.SV14'
         AIF   (T'&B EQ 'O').B070
&CNT     SETA  0
.A110    ANOP
&CNT     SETA  &CNT+1
         AIF   (T'&B(&CNT) EQ 'O').A200
&SUBINSC SETA  &SUBINSC+1
&SUBINSL(&SUBINSC) SETC '&L&LB.SV&B(&CNT)'
         AGO   .A110
.A200    ANOP
&CNT     SETA  0
.B010    ANOP
&CNT     SETA  &CNT+1
         AIF   (&NUMBR1 NE 0).B020
&NUMBR1  SETA  &B(&CNT)+1
&FIRST   SETA  &CNT
         AGO   .B010
.B020    ANOP
         AIF   (T'&B(&CNT) EQ 'O').B030
         AIF   (&B(&CNT) NE &NUMBR1).B030
&LAST    SETA  &CNT
&NUMBR1  SETA  &NUMBR1+1
         AGO   .B010
.B025    ANOP
&FIRST   SETA  &CNT
&CNT     SETA  &CNT+1
.B030    ANOP
&NUMBR2  SETA  &FIRST+1
&PNTR    SETA  &PNTR+1
         AIF   (&NUMBR2 LT &CNT).B040
         ST    &R&B(&FIRST),&L&LB.SV&B(&FIRST)
&SUBINRG(&PNTR) SETC 'L'
&SUBINRA(&PNTR) SETC '&R&B(&FIRST)'
&SUBINRB(&PNTR) SETC '&L&LB.SV&B(&FIRST)'
&SUBINRC(&PNTR) SETC ' '
         AIF   (N'&B EQ &CNT).B025
         AIF   (N'&B LT &CNT).B070
         AGO   .B050
.B040    ANOP
         STM   &R&B(&FIRST),&R&B(&LAST),&L&LB.SV&B(&FIRST)
&SUBINRG(&PNTR) SETC 'LM'
&SUBINRA(&PNTR) SETC '&R&B(&FIRST)'
&SUBINRB(&PNTR) SETC '&R&B(&LAST)'
&SUBINRC(&PNTR) SETC '&L&LB.SV&B(&FIRST)'
         AIF   (N'&B EQ &CNT).B025
         AIF   (N'&B LT &CNT).B070
.B050    ANOP
&FIRST   SETA  &CNT
&NUMBR1  SETA  &B(&CNT)+1
.B060    ANOP
         AIF   (N'&B GT &CNT).B010
         AIF   (N'&B EQ &CNT).B030
.B070    ANOP
&SUBINCT SETA  &PNTR
         AGO   .Z010
.C010    ANOP
*
*        SUBROUTINE REGISTER SAVE AREAS
*
&CNT     SETA  0
.C020    ANOP
&CNT     SETA  &CNT+1
         AIF   (&CNT GT &SUBINSC).Z010
&LB      SETC  '&SUBINSL(&CNT)'
&LB      DC    F'0'
         AGO   .C020
.Z010    MEND
