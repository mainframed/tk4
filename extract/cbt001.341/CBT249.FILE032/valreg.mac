         MACRO
         VALREG &REG,&LIMITS,&ERR1=NO,&NAME=
.*
.*       VERSION 01.00 - J.G.KINKER - 12/77
.*
.*       THE VALREG MACRO INSTRUCTION IS A INNER MACRO WHICH VAL-
.*       IDATES A PARAMETER WHICH IS SUPPOSED TO BE A REGISTER
.*       NUMBER.  THE PARAMETER IS CONSIDERED VALID IF IT IS EN-
.*       CLOSED IN PARENTHESIS AND
.*            1. IS NUMERIC WITH A VALUE BETWEEN 0 AND 15 INCLUS-
.*               IVE OR BETWEEN SOME USER SPECIFIED LIMITS.
.*            2. IS A VALID ASSEMBLER LABEL IN WHICH CASE IT IS
.*               ASSUMED TO BE A SYMBOLIC REGISTER NAME.
.*       IF THE PARAMETER IS A VALID NUMERIC IT IS CONCATENATED
.*       WITH THE REGPREFIX IF ONE EXISTS.  IN ALL CASES THE
.*       VALID REGISTER NAME IS RETURNED TO THE USER IN GLOBAL
.*       CHARACTER VARIABLE &VALREG.  IF THE PARAMETER IS NOT
.*       VALID AN ERROR CODE IS PLACED IN &VREGERR WHICH IS A
.*       GLOBAL ARITHMETIC VARIABLE AND FOR ERRORS 2 AND 3 AN
.*       MNOTE IS ISSUED.  THE POSSIBLE VALUES OF &VREGERR AND
.*       THEIR MEANINGS ARE:
.*            0 - THE PARAMETER IS VALID AND IS IN &VALREG.
.*
.*            1 - THE PARAMETER IS NOT ENCLOSED IN PARENS.  THE
.*                USER MAY REQUEST AN MNOTE FOR THIS CONDITION BY
.*                CODING 'ERR1=YES' IN THE MACRO CALL.
.*            2 - THE PARAMETER IS NUMERIC AND NOT WITHIN THE
.*                ACCEPTABLE RANGE.
.*            3 - AN ERROR HAS BEEN FOUND IN THE CONTROL PARA-
.*                METERS PASSED TO THIS MACRO.  IT IS UNABLE TO
.*                PROCEED WITH EVALUATION OF THE REGISTER PARA-
.*                METER.
.*            4 - THE PARAMETER IS NOT NUMERIC AND IS NOT A VALID
.*                  LABEL.
.*       THE MACRO IS CODED:
.*
.*                            --        --     -- -- --       --
.*             VALREG REGPARM ×,(LIMIT1 ×,LIMIT2× )× ×,ERR1=YES×
.*                            --        --     -- -- --       --
.*                    ,NAME=PARMNAME
.*
.*             REGPARM IS REPLACED BY THE PARAMETER TO BE VALID-
.*                  ATED.  IT IS REQUIRED.
.*
.*             PARMNAME IS REPLACED BY THE NAME OF THE PARAMETER
.*                  TO BE VALIDATED FOR USE IN MNOTES.  IT IS
.*                  REQUIRED.
.*
.*             ERR1=YES IS CODED AS SHOWN TO REQUEST THE ERROR
.*                  TYPE 1 MNOTE.  IT IS OPTIONAL.
.*             LIMIT1 IS REPLACED BY THE LOWER LIMIT ACCEPTABLE
.*                  AS VALID.  IT IS OPTIONAL AND DEFAULTS TO
.*                  ZERO.
.*
.*             LIMIT2 IS REPLACED BY THE UPPER LIMIT ACCEPTABLE
.*                  AS VALID.  IT IS OPTIONAL AND DEFAULTS TO 15.
.*                  IF LIMIT1 IS SPECIFIED AND LIMIT2 IS OMITTED,
.*                  LIMIT1 IS USED FOR BOTH.  THIS CONSTRAINS A
.*                  VALID PARAMETER TO A SINGLE VALUE.
.*
.*       THIS MACRO SUPPORTS A REGPREFIX STORED IN A GLOBAL CHAR-
.*       ACTER VARIABLE NAMED &R.
.*
.*
.* G L O B A L   A R I T H M E T I C   V A R I A B L E S
.*
         GBLA  &VREGERR,&LABERR
.*
.*             &VREGERR IS USED TO PASS ERROR CODES TO THE USER.
&VREGERR SETA  0
.*
.*             &LABERR IS USED TO RECEIVE ERROR CODES FROM THE
.*                  VALAB MACRO INSTRUCTION.
.*
.*
.* G L O B A L   C H A R A C T E R   V A R I A B L E S
.*
         GBLC  &VALREG,&R,&VALREGN
.*
.*             &VALREG IS USED TO PASS A VALID REGISTER PARAMETER
.*                  QUALIFIED WITH THE REGPREFIX BACK TO THE USER.
.*
.*             &VALREGN IS USED TO PASS A VALID REGISTER PARAMETER
.*                  WITHOUT THE REGPREFIX QUALIFICATION BACK TO THE
.*                  USER.
.*
.*             &R IS USED TO RECEIVE THE REGPREFIX IF ONE IS IN
.*                  EFFECT.  THE ENTER MACRO INSTRUCTION NORMALLY
.*                  SETS THIS VALUE.
.*
.* L O C A L   A R I T H M E T I C   V A R I A B L E S
.*
         LCLA  &LOWLIM,&HIGHLIM,&LENGTH,&NUMCHK
.*
.*             &LOWLIM IS THE LOWEST ACCEPTABLE VALUE OF A REG-
.*                  ISTER PARAMETER.  DEFAULT IS ZERO.
&LOWLIM  SETA  0
.*
.*             &HIGHLIM IS THE HIGHEST ACCEPTABLE VALUE OF A REG-
.*                  ISTER PARAMETER.  DEFAULT IS 15.
&HIGHLIM SETA  15
.*
.*             &LENGTH IS USED FOR THE LENGTH OF &REG IN SUB-
.*                  STRING OPERATIONS.
.*
.*             &NUMCHK IS USED TO CHECK &REG FOR NUMERIC.
.*
.*
.* L O C A L   B I N A R Y   V A R I A B L E S
.*
         LCLB  &ERR1SW
.*
.*             &ERR1SW IS USED TO INDICATE WHETHER OR NOT AN MNOTE
.*                  IS DESIRED FOR ERROR 1.  1 = YES.
&ERR1SW  SETB  0
.*
.*
.* V A L I D A T E   N A M E =   P A R A M E T E R
.*
         AIF   (T'&NAME NE 'O').A010
         MNOTE 16,'NAME PARAMETER REQUIRED FOR VALREG'
&VREGERR SETA  3
.*
.* V A L I D A T E   E R R 1 =   P A R A M E T E R
.*
.A010    ANOP
         AIF   ((T'&ERR1 EQ 'O') OR ('&ERR1' EQ 'NO')).B010
         AIF   ('&ERR1' EQ 'YES').A020
         MNOTE 4,'ERR= MUST BE ''YES'', ''NO'' OR OMITTED; ''YES'' ASSU*
               MED'
.A020    ANOP
&ERR1SW  SETB  1
.*
.* V A L I D A T E   L I M I T S   P A R A M E T E R
.*
.B010    ANOP
         AIF   (T'&LIMITS EQ 'O').C010
         AIF   (T'&LIMITS(1) EQ 'O').B040
         AIF   (T'&LIMITS(1) EQ 'N').B020
         MNOTE 4,'LIMIT 1 IS NOT NUMERIC - IGNORED; DEFAULT VALUE USED'
         AGO   .B040
.B020    ANOP
         AIF   ((&LIMITS(1) GE 0) AND (&LIMITS(1) LE 15)).B030
         MNOTE 4,'LIMIT 1 NOT WITHIN ACCEPTABLE RANGE - IGNORED; DEFAUL*
               T VALUE USED'
         AGO   .B040
.B030    ANOP
&LOWLIM  SETA  &LIMITS(1)
         AIF   (T'&LIMITS(2) NE 'O').B050
&HIGHLIM SETA  &LIMITS(1)
         AGO   .C010
.B040    ANOP
         AIF   (T'&LIMITS(2) EQ 'O').C010
.B050    ANOP
         AIF   (T'&LIMITS(2) EQ 'N').B060
         MNOTE 4,'LIMIT 2 IS NOT NUMERIC - IGNORED; DEFAULT VALUE USED'
         AGO   .C010
.B060    ANOP
         AIF   ((&LIMITS(2) GE 0) AND (&LIMITS(2) LE 15)).B070
         MNOTE 4,'LIMIT 2 NOT WITHIN ACCEPTABLE RANGE - IGNORED; DEFAUL*
               T VALUE USED'
         AGO   .C010
.B070    ANOP
         AIF   (&LIMITS(2) GE &LOWLIM).B080
         MNOTE 4,'LIMIT 2 LESS THAN LIMIT 1 OR DEFAULT - IGNORED; DEFAU*
               LT VALUE USED'
         AGO   .C010
.B080    ANOP
&HIGHLIM SETA  &LIMITS(2)
.*
.* V A L I D A T E   T H E   R E G   P A R A M E T E R
.*
.C010    ANOP
         AIF   (&VREGERR GT 0).Z010
         AIF   (T'&REG NE 'O').C020
         MNOTE 8,'&NAME OMITTED, &NAME MUST BE SUPPLIED'
&VREGERR SETA  3
         AGO   .Z010
.C020    ANOP
         AIF   ('&REG'(1,1) NE '(').C060
&LENGTH  SETA  K'&REG
         AIF   ('&REG'(&LENGTH,1) EQ ')').C030
         MNOTE 4,'UNBALANCED PARENTHESIS IN &NAME OPERAND; ASSUMED'
&LENGTH  SETA  &LENGTH+1
.C030    ANOP
&VALREG  SETC  '&REG'(2,&LENGTH-2)
&VALREGN SETC  '&VALREG'
         AIF  (('&VALREG'(1,1) LT '0') OR ('&VALREG'(1,1) GT '9')).C050
&NUMCHK  SETA  &VALREG
         AIF   ((&NUMCHK GE &LOWLIM) AND (&NUMCHK LE &HIGHLIM)).C040
         MNOTE 8,'&NAME NOT WITHIN SPECIFIED RANGE'
&VREGERR SETA  2
         AGO   .Z010
.C040    ANOP
&VALREG  SETC  '&R.&VALREG'
         AGO   .Z010
.*
.* R E G   I S   N O T   N U M E R I C
.*
.C050    ANOP
         VALAB &VALREG,NAME=&NAME
         AIF   (&LABERR EQ 0).Z010
&VREGERR SETA  4
         AGO   .Z010
.*
.* R E G   I S   N O T   I N   P A R E N S
.*
.C060    ANOP
&VREGERR SETA  1
.*
.* E X I T
.*
.Z010    ANOP
         MEND
