         MACRO
         SUBLIST &LIST,&MAXCNT,&NAME=
.*
.*       INNER MACRO INSTRUCTION: SUBLIST
.*
.*       FORMAT: SUBLIST SUBLIST,MAXCOUNT,NAME='LISTNAME(POSITION)'
.*
.*       PURPOSE:
.*             IT IS VERY DIFFICULT TO GET TO THE CONTENTS OF A
.*             SUB-LIST WITHIN A SUB-LIST AS ILLUSTRATED BY:
.*                  ((A,B,C),(D,,E),(F,G),H,(,,I))
.*             BECAUSE THE ASSEMBLER PERMITS ONLY SINGLE LEVEL
.*             SUBSCRIPTING.  THIS MACRO RECEIVES AN INNER EXPRES-
.*             SION AS A SIMPLE SUBLIST AND CAN THEREFORE EASILY
.*             ATTACK ITS COMPONENT PARAMETERS.  THE VALUES OF THE
.*             COMPONENT PARAMETERS ARE ASSIGNED TO ELEMENTS OF
.*             THE DIMENSIONED GLOBAL CHARACTER VARIABLE &SLPARMS
.*             IN THE ORDER OF THEIR OCCURRENCE FOR EASE OF PRO-
.*             CESSING BY THE CALLER.  ON RETURN FROM SUBLIST, THE
.*             GLOBAL ARITHMETIC VARIABLE &5LCNT WILL HAVE THE
.*             VALUE:
.*                  &SLCNT EQ NUM OF COMPONENTS LE MAXCOUNT.
.*             COMPONENT PARAMETERS EXCEEDING MAXCOUNT ARE IGNORED
.*             BUT A LEVEL 4 MNOTE IS ISSUED INDICATING THEIR
.*             PRESENCE.  THE LIST NAME PARAMETER IS REQUIRED FOR
.*             THIS MNOTE.
.*
.*       EXAMPLE:
.*             TYPICAL CODE TO EVALUATE THE ABOVE SUBLIST WOULD
.*             BE:
.*                  GBLA &SLCNT
.*                  GBLB &SLPARMS
.*                  GBLC &SLERR
.*                  LCLA &CNT
.*                    .
.*                    .
.*             .L1  ANOP
.*             &CNT SETA &CNT+1
.*                  AIF  (K'&LIST LT &CNT).END
.*                  SUBLIST &LIST(&CNT),3,NAME='LIST(&CNT)'
.*                  AIF  (&SLERR EQ 1).ERROR
.*                    .
.*                    .
.*                  PICKUP && VALIDATE CONTENT OF &SLPARMS USING
.*                       &SLCNT TO CONTROL PROCESS
.*                    .
.*                  AGO  .L1
.*
.*
.* G L O B A L   A R I T H M E T I C   V A R I A B L E S
.*
         GBLA  &SLCNT
.*
.*             &SLCNT IS USED TO PASS THE NUMBER OF SUB-PARAMETERS
.*                  IN THE LIST (OMITTED OR NOT) TO THE CALLER AND
.*                  TO STEP THROUGH THE LIST WITHIN THIS MACRO.  A
.*                  NULL INPUT LIST IS INDICATED BY &SLCNT = ZERO.
.*
.*
.* G L O B A L   B I N A R Y   V A R I A B L E S
.*
         GBLB  &SLERR
.*
.*             &SLERR IS USED TO INDICATE A SUBLIST MACRO CALL
.*                  ERROR CONDITION TO THE CALLER.  '0' = OK; '1'
.*                  = ERROR IN CALL.
.*
.*
.* G L O B A L   C H A R A C T E R   V A R I A B L E S
.*
         GBLC  &SLPARMS(100)
.*
.*             &SLPARMS IS USED TO PASS THE INDIVIDUAL SUB-PARA-
.*                  METERS FROM THE INPUT SUBLIST BACK TO THE
.*                  CALLER.  NULL SUB-PARAMETERS IN THE SUBLIST
.*                  ARE REPRESENTED BY NULL ELEMENTS IN &SLPARMS.
.*
.*
.* L O C A L   A R I T H M E T I C   V A R I A B L E S
.*
         LCLA  &CNT1,&PCNT
.*
.*             &CNT1 IS USED TO INDEX THROUGH THE CONTENT OF &LIST
.*                  CHARACTER BY CHARACTER.
.*
.*             &PCNT IS USED TO COUNT PAIRED PARENTHESIS.
.*
.*
.* L O C A L   C H A R A C T E R   V A R I A B L E S
.*
         LCLC  &WORK1,&WORK2
.*
.*             &WORK1 IS USED TO HOLD A NON-DELIMETER CHARACTER
.*                  FROM STRING &LIST SO THAT IT MAY BE CONCATENATED
.*                  WITH PRIOR CHARACTERS HELD IN &WORK2.
.*
.*             &WORK2 IS USED TO CONSTRUCT A PARAMETER VALUE PRIOR
.*                  TO INSERTING IT INTO &SLPARMS INDEXED BY &SLCNT.
.*
.*
.* I N I T I A L I Z E   V A R I A B L E S
.*
&SLCNT   SETA  0
&SLERR   SETB  0
&PCNT    SETA  0
&CNT1    SETA  0
&WORK1   SETC  ' '
.*
.* V A L I D A T E   T H E   M A X C N T   P A R A M E T E R
.*
.MC010   ANOP
         AIF   (T'&MAXCNT EQ 'N').MC020
         MNOTE 16,'SUBLIST MAX CNT ERR; CALL SYSTEMS PROGRAMMING'
&SLERR   SETB  1
         AGO   .MC030
.MC020   ANOP
         AIF  ((&MAXCNT GE 1) AND (&MAXCNT LE 100)).MC030
         MNOTE 16,'SUBLIST MAX CNT NOT WITHIN LIMITS;'
         MNOTE 16,'      CALL SYSTEMS PROGRAMMING'
&SLERR   SETB  1
.MC030   AGO   .NM010
.*
.* V A L I D A T E   T H E   N A M E   P A R A M E T E R
.*
.NM010   ANOP
         AIF   (T'&NAME NE '0').NM020
         MNOTE 16,'SUBLIST NAME MUST BE SUPPLIED;'
         MNOTE 16,'      CALL SYSTEMS PROGRAMMING'
&SLERR   SETB  1
.NM020   AGO   .ER010
.*
.* C H E C K   F O R   P A R A M E T E R   E R R O R   I N   C A L L
.*
.ER010   ANOP
         AIF   (&SLERR EQ 1).Z010
.*
.* C H E C K   L I S T ;   O M I T T E D ,   E X I T
.*
         AIF   (T'&LIST EQ 'O').Z010
.LS010   ANOP
&SLCNT   SETA  &SLCNT+1
&WORK2   SETC  ''
.LS020   ANOP
&CNT1    SETA  &CNT1+1
         AIF   (K'&LIST LT &CNT1).LS030
         AIF   ('&LIST'(&CNT1,1) EQ '(').LS0201
         AIF   ('&LIST'(&CNT1,1) EQ ',').LS0203
         AIF   ('&LIST'(&CNT1,1) EQ ')').LS0202
         AGO   .LS0205
.LS0201  ANOP
&PCNT    SETA  &PCNT+1
         AGO   .LS0203
.LS0202  ANOP
&PCNT    SETA  &PCNT-1
.LS0203  ANOP
         AIF   (&PCNT GT 1).LS0205
         AIF   ('&LIST'(&CNT1,1) EQ ',').LS030
         AIF   (('&LIST'(&CNT1,1) EQ ')') AND (&PCNT EQ 1)).LS0205
         AIF   ('&LIST'(&CNT1,1) EQ ')').LS030
         AGO   .LS020
.LS0205  ANOP
&WORK1   SETC  '&LIST'(&CNT1,1)
&WORK2   SETC  '&WORK2.&WORK1'
         AGO   .LS020
.LS030   ANOP
&SLPARMS(&SLCNT) SETC '&WORK2'
         AIF   (K'&LIST LT &CNT1).Z010
         AIF   ('&LIST'(&CNT1,1) EQ ')').Z010
         AIF   (&SLCNT LT &MAXCNT).LS010
         MNOTE 4,'SUB-PARAMETERS OF &NAME EXCEED MAXIMUM ALLOWED'
         MNOTE 4,'ADDITIONAL PARAMETERS IGNORED'
.*
.* E X I T
.*
.Z010    MEND
