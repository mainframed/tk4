         PRINT DATA
IRBMFDWP CSECT
         USING IRBMFDWP+X'1C',12
ZAPPED   EQU   IRBMFDWP+X'210'
PATCH    EQU   IRBMFDWP+X'8A8'
WAMTPTR  EQU   X'B8'
*ZAP CARD ++PTF(HG00101)
*ZAP CARD   /* MODIFICATION TO MF1 WORKLOAD INTERVAL REPORTING
*ZAP CARD      TO COPY WAMT TO STATDIE PAGABLE WAMT BUFFER IF
*ZAP CARD      STATDIE IS ACTIVE.
*ZAP CARD      THIS CODE ZAPS THE ASSEMBLER LANGUAGE PROLOGUE
*ZAP CARD      TO THE THIRD ENTRY POINT IN IRBMFDWP. IT USES
*ZAP CARD      THE LINK PACK AREA SEARCH ROUTINE TO LOCATE
*ZAP CARD      'STATWAMT' WHICH IS A POINTER TO THE ONE PAGE
*ZAP CARD      BUFFER OBTAINED BY STATDIE TO HOLD THE LAST
*ZAP CARD      WAMT FOR DISPLAY BY THE 'WORKLOAD' TSO COMMAND
*ZAP CARD      OR TRACING BY OURMF. */ .
*ZAP CARD ++VER(Z037) PRE(UZ80700).
*ZAP CARD ++ZAP(IRBMFDWP).
*ZAP CARD  NAME IRBMFDWP
*ZAP CARD  EXPAND IRBMFDWP(90)
*ZAP START VER
         ORG   ZAPPED
         LM    2,12,28(1)
ZAPPED2  EQU   *
         ST    13,4(,1)
         ORG   PATCH
         DC    XL32'00'
*ZAP START REP
         ORG   ZAPPED2
         B     PATCH
RET      EQU   *
         ORG   PATCH
* THERE HAS TO BE SOMETHING EXCEPTIONAL ABOUT A GOOD ZAP
* THE NICE THINGS ABOUNT THIS ONE ARE:
*  1) WE HAVE TWO SAVE AREAS, ONE POINTED TO BY R13 THAT
*     HAS BEEN JUST STORED INTO, AND ONE POINTED TO BY
*     R1 WHICH R2-12 ARE BEING RESTORED FROM.  SO THERE
*     ARE LOTS OF REGISTERS TO USE.
*  2) IT IS JUST AFTER AN INTERNAL ENTRY POINT IN THE
*     MODULE AND WILL BE EASY TO RELOCATE AFTER MAINTENANCE
*  3) THIS ZAP HITS ONLY GENNED ASSEMBLER STATEMENTS, AN
*     ISLAND OF SANITY IN A SEA OF PLS.
         LR    10,1               SAVE FOR LATER
         LM    0,1,NAME           PARM TO LPA SRCH
         L     3,CVTPTR
         L     15,CVTLPDSR-CVT(3) ADDR OF LPASRCH
         BALR  14,15              CALL IEAVVMSR
         B     FOUND              NAME FOUND
         B     RTNR               NAME NOT FOUND
FOUND    LR    1,0                GET LPDE ADDR
         L     1,LPDENTP-LPDE(1) GET ENTRY POINT
         L     0,0(1)             GET STATWAMT VALUE
         LTR   0,0                IS STATDIE UP?
         BZ    RTNR               NO, QUIT
         LA    1,4095             STATDIE GETS 4K BUF
         L     2,WAMTPTR(11)      GET LOCAL WAMT ADDR
*                                 FROM GETMAIN DATA AREA
         L     3,4(2)             GET ITS SIZE
         MVCL  0,2                COPY WAMT
RTNR     LR    1,10               RESTORE
         LM    2,12,28(1)         RELOAD REGS
         ST    13,4(,1)
         B     RET                GO BACK
NAME     DC    CL8'STATWAMT'
*ZAP STOP
         CVT   DSECT=YES
         IHALPDE
         END
