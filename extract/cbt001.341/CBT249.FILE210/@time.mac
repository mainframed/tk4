@TOD     TITLE '@TOD - TIME OF DAY CLOCK CONVERSION ROUTINE'
         MACRO
         @ENTER   &NAME,&CS=YES,&LDATA=0
@&NAME  CSECT
         USING *,R15
         B     *+12           BYPASS NAME LOC
         DC    CL8'@&NAME'  NAME OF MODULE
         STM   R14,R11,12(R13)
         LTR   R1,R1 .             MAKE SURE WE HAVE THE POINTER
         BNZ   SAFE&SYSNDX
         ABEND 100,DUMP .          U0100 IF MISSING
SAFE&SYSNDX DS    0H
         LR    R2,R1 .        SAVE R1
         LA    R0,((107+&LDATA)/8)*8 .    LENGTH OF DSA
         L     R1,76(R13)          NEXT AVAIL DSA ADDR
         ALR   R0,R1               POINT TO MY END
         CL    R0,12(R12)          GREATER THAN REST OF ISA
         LA    R14,OK&SYSNDX       GET THE BASE
         BNH   OK&SYSNDX           NO: OK
         L     R15,116(R12)        ADDR OF NEW ISA CODE
         BALR  R14,R15             GET NEW ISA
         USING *,R15
OK&SYSNDX      DS                  0H
         LR    R15,R14             SET UP BASE
         ST    R0,76(R1)           NEXT AVAIL DSA
         ST    R13,4(R1)           POINT TO THIS DSA
         MVC   72(4,R1),72(R13)    POINT TO LIBRARY WORKSP
         LR    R13,R1              MY DSA
         MVI   86(R13),X'91'       SPIE
         MVI   87(R13),X'C0'        SUPPORT
         MVI   0(R13),X'80'        LABEL AS DSA
         LR    R1,R2               RESTORE PARM REG
         L     R1,4(R13)
         L     R1,24(R1) RESTORE R1
         BALR  R11,0
         DROP  R15
         USING *,R11
***********************************************************************
*                                                                     *
*        R11 -> BASE                                                  *
*        R12 -> PL/I TCA                                              *
*        R13 -> PL/I DSA (X'00'-X'48' IS SAVEAREA)                    *
*                                                                     *
***********************************************************************
         MEND
         MACRO
         @EXIT &OK=,&BAD=
&OK      SR    R15,R15 .           CLEAN RETURN
&BAD     DS    0H
         L     R13,4(R13)          GET RID OF DSA
         LM    R14,R11,12(R13)     RESTORE REGS
         BR    R14 .                RETURN
         MEND
         EJECT
         @ENTER TIME,LDATA=128
         USING PLIWORK,R13
         L     R3,0(R1)            GET POINTER TO DOPE VECTOR
         L     R5,0(R3)            GET POINTER TO ANSWER FIELD
         MVI   ANSWER,X'40'
         MVC   ANSWER+1(30),ANSWER     BLANK OUTPUT AREA
*
         STCK  CLOCK
*
         LA    R1,TIMEZONE
         ST    R1,PARMLIST
         LA    R1,CLOCK
         ST    R1,PARMLIST+4
         LA    R1,ANSWER
         ST    R1,PARMLIST+8
         LA    R1,PARMLIST
         LA    R2,1
         L     R10,=V(TODCNVRT)
         BALR  R14,R10
         MVC   0(22,R5),ANSWER
FINISHED EQU   *
         @EXIT OK=GOOD,BAD=BAD
         LTORG
PLIWORK  DSECT
SAVDSA   DS    108X
PARMLIST DC    A(TIMEZONE)
         DC    A(CLOCK)
         DC    A(ANSWER)
TIMEZONE DC    F'0'
         DC    2F'0'
CLOCK    DC    D'0'
         DC    2F'0'
ANSWER   DC    30X'00'
         DC    2F'0'
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
