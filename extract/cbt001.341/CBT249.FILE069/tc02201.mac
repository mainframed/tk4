//MGG0004 JOB (GTE4,136),'TC02201',TIME=8
//**********************************************************
//*                       TC02201                          *
//*          ASSEMBLY OF LOGON PREPROMPT EXIT IKJEFLD      *
//*         TO PERFORM ERASE OR ERASE ALT AT LOGON FOR     *
//*            SUPPORT OF 43 LINE SCREEN SUPPORT           *
//*         IPO 6/MVS 3.8 WITHOUT TCAM LEVEL-SET PTF       *
//*                                                        *
//*               MARGARET GARDNER, GTE LABORATORIES, INC. *
//**********************************************************
//ASM    EXEC ASMFC,PARM='LIST,XREF,NODECK,LOAD'
//ASM.SYSGO  DD DSN=SYS1.GTEOBJ(IKJEFLD),DISP=OLD
//ASM.SYSIN DD  *
IKJEFLD   CSECT
*        EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R12      EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
         EJECT
         USING *,R12              REG 12 IS BASE
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            ADDRESSABILITY
         B     BEGIN              BRANCH AROUND ID
ID       DC    CL8'IKJEFLD '
         DC    CL4'TC22'
BEGIN    GTSIZE
         LTR   R15,R15            RC
         BNZ   ERR                SET UP FOR WRITE/ERROR
         LA    R2,ERALT           SET FOR MOD 4
         CL    R0,MOD4            IS TERMINAL 3270 MOD 4?
         BE    TPUT               IF SO, TPUT WRITE/ERASE ALT
         CL    R0,MOD2            IS IT A MOD 2 3270?
         BNE   RETURN             MUST BE NON-3270, DON'T BOTHER
         LA    R2,ERASE           SET FOR PLAIN WRITE/ERASE
TPUT     TPUT  (R2),3,FULLSCR     TPUT INITIAL SCREEN SETTING
RETURN   LM    R14,R12,12(R13)    RESTORE REGISTERS
         BR    R14                RETURN
ERALT    DC    X'277E4040'        WRITE/ERASE ALTERNATE COMMAND
ERASE    DC    X'27F54040'        WRITE/ERASE COMMAND
MSG1     DC    X'27F14040'        INDICATION OF ERROR
MOD4     DC    F'43'              MOD 4 3270 NO OF LINES
MOD2     DC    F'24'              MOD 2 3270 NO OF LINES
ERR      LA    R2,MSG1            SET UP ERROR MSG
         B     TPUT               HAS UNIT PREFIX BEEN FILLED?
         END
/*
//SMP   EXEC  SMPPROC
//SMPOBJ DD  DSN=SYS1.GTEOBJ,DISP=SHR
//SMPPTFIN DD DATA,DLM=##
++USERMOD(TC02201).
++VER(Z038) FMID(ETC0108).
++MOD(IKJEFLD) TXLIB(SMPOBJ) DISTLIB(AGTELIB) LEPARM(RENT,REUS)
               LMOD(IKJEFLA).
##
//SMPCNTL  DD  *
 RECEIVE S(TC02201).
 APPLY S(TC02201).
/*
