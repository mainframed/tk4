//$SYS6JCL JOB (8205,3919),'6-PANSPOOL - JCLARCH',CLASS=Z,MSGCLASS=X,
//     NOTIFY=$SYS6
/*JOBPARM Q=F,I,O=JCLARCH
//PLIOPT   EXEC PGM=IEL0AA,
//         PARM='NAG,NGN,NOMAP,STMT,NSTG,OBJECT,NODECK'
//SYSPRINT  DD  SYSOUT=*
//SYSLIN    DD  UNIT=SYSDA,SPACE=(CYL,(1,1)),DSN=&&OBJMOD,
//     DISP=(MOD,PASS)
//SYSUT1    DD  UNIT=VIO,SPACE=(CYL,10),DCB=BLKSIZE=1024
//SYSIN     DD  *
 /* PANSPOOL - JES2 SYSMSG SPOOLING SYSTEM:  A PANVALET EXTERNAL WTR */
 /* **************************************************************** */
 /*                                                                  */
 /*        DATA SET JCLARCH    AT LEVEL 001 AS OF 05/19/78           */
 /*        DATA SET INTFC1082G AT LEVEL 001 AS OF 01/14/80           */
 /*                                                                  */
 /* **************************************************************** */
 /*                                                                  */
 /*  SOURCE:   CANADIAN PACIFIC INFORMATION SYSTEMS                  */
 /*            WINDSOR STATION                                       */
 /*            910 PEEL STREET                                       */
 /*            ROOM E-40                                             */
 /*            MONTREAL, QUEBEC, CANADA                              */
 /*            H3C 3E4                                               */
 /*            (514) 395-7880                                        */
 /*  CONTACT:  ESTELLE ROTHER                                        */
 /*                                                                  */
 /* **************************************************************** */
 /*                                                                  */
 /*  VERSION 1.0                                                     */
 /*  -----------                                                     */
 /*  INSTALLATION NOTE:                                              */
 /*     GETPAN2 AND GETJFCB MUST BE ASSEMBLED AND LINKEDITED PRI-    */
 /*     OR TO COMPILING THIS PROGRAM; OTHERWISE, UNRESOLVED EX-      */
 /*     TERNAL REFERENCES WILL RESULT.                               */
 /*                                                                  */
 /*  UPDATED 26FEB80, WILLIAM SMITH, MEMOREX CORPORATION,            */
 /*    DEPT. 8205, MS 10-35, SAN TOMAS AT CENTRAL EXPRESSWAY,        */
 /*    SANTA CLARA, CALIFORNIA, 95052  (408)-987-3919                */
 /*     .  MVS 3.8/JES2 4.1 - SE 1.1 - ACF/VTAM R2                   */
 /*     .  PANVALET/V10 W/TSO OPTION                                 */
 /*                                                                  */
 /*  UPDATED 28FEB80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  CHANGED ALL REFERENCES FROM SYSOUT TO 'PANARCH' TO        */
 /*        ELIMINATE THE 'CONCATENATION' OF THE PANVALET ++PRINT     */
 /*        2-UP DIRECTORY REPORT WITH THE SORT SYSOUT MESSAGES       */
 /*     .  MOVED THE SPECIFICATION OF THE 'ON ENDFILE' CONDITION     */
 /*        FOR FILE2 DIRECTLY AFTER THE OPEN TO ELIMINATE THE        */
 /*        OCCURRENCE OF U2000 ABEND WHEN A NULLFILE CONDITION       */
 /*        EXISTS                                                    */
 /*                                                                  */
 /*  UPDATED 11MAR80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  REPORT FORMATTING - ADDED TIME, DATE, SUBSTR, INDEX       */
 /*        FUNCTIONS AND THE ADDITION OF A SEVERAL TITLES            */
 /*                                                                  */
 /*  UPDATED 26MAR80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  PROVIDED FOR MORE MEANINGFUL IDENTIFIERS AND LABELS:      */
 /*           CC       CARRIAGE_CONTROL                              */
 /*           RDPOUT   READ_PANVALET_OUTPUT                          */
 /*           BADPANRC BAD_PANVALET_RC                               */
 /*           BADPAN   GETPAN#2_ERROR_MSG                            */
 /*           RD2UP    READ_PANVALET_2UP                             */
 /*           RDERR    READ_ERROR                                    */
 /*           DUPMSG   DUPLICATE_MESSAGE                             */
 /*           ERRORMSG SORT_RDJFCB_ERROR_MSG                         */
 /*           ERRTEXT  ERROR_TXT                                     */
 /*           TNAME1   TEMP_NAME1                                    */
 /*           TNAME2   TEMP_NAME2                                    */
 /*           PRTEND   DELETE_RUN_PRINT_ANALYSIS                     */
 /*                                                                  */
 /*     .  ADDITION OF NUMEROUS COMMENTS FOR CLARITY, READABILITY    */
 /*                                                                  */
 /*  VERSION 2.0                                                     */
 /*  -----------                                                     */
 /*  UPDATED 24APR80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  CHANGED THE REPORT TITLES                                 */
 /*     .  CHANGED THE REPORT HEADINGS - NEW COMPANY DIVISION NAME   */
 /*     .  FIXED AN ERROR IN THE PAGE-EJECT ROUTINE AND ADDED A      */
 /*        PAGE COUNT, SEPARATE ERROR ANALYSIS REPORT                */
 /*     .  CHANGED ALL STOPS TO RETURNS - NO MORE RC = 1000          */
 /*     .  ADDITION OF A NEW PRINT SUBROUTINE TO GENERATE A SEPAR-   */
 /*        ATE REPORT FOR THE ERROR MESSAGES - EASIER TO ISOLATE     */
 /*        ANY SUSPECTED ERROR CONDITIONS FROM SORT, DUPLICATE       */
 /*        ENTRIES, ARCHIVAL ERRORS, ETC.                            */
 /*                                                                  */
 /*  VERSION 2.1                                                     */
 /*  -----------                                                     */
 /*  UPDATED 20MAY80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  CHANGED ALL REFERENCES FROM WEEKLY TO MONTHLY PROCESSING  */
 /*        DUE TO AN IN-HOUSE REQUIREMENT OF RETAINING LISTINGS FOR  */
 /*        2 YEARS - TOO MANY TAPES WERE BEING USED BY PANSPOOL      */
 /*                                                                  */
 /*  VERSION 2.2                                                     */
 /*  -----------                                                     */
 /*  UPDATED 27JUN80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  CHANGED THE REPORT HEADINGS - NEW COMPANY DIVISION NAME   */
 /*     .  CHANGED THE MESSAGE 'NUMBER OF DATASETS ARCHIVED NOT      */
 /*        EQUAL TO NUMBER IN VTOC' TO READ 'NUMBER OF DATASETS      */
 /*        ARCHIVED NOT EQUAL TO NUMBER IN ARCHIVE DIRECTORY'        */
 /*     .  SUBSTANTIAL CHANGES TO THE MIGRATION JCL TO PLACE THE     */
 /*        DIRECTORY ON-LINE RATHER THAN AS FILE 1 OF THE ARCHIVE    */
 /*        TAPE - GREATER ACCESSABILITY BY MORE USERS/OPER PERSONNEL */
 /*     .  CORRESPONDS TO LEVEL 80-02 OF THE PANSPOOL USER'S GUIDE   */
 /*                                                                  */
 /*  VERSION 2.3                                                     */
 /*  -----------                                                     */
 /*  UPDATED 02SEP80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  SOURCE CODE UPDATES AS RECOMMENDED BY RICH STEHN OF USER  */
 /*        SERVICES TO HELP ELIMINATE A RANDOMLY OCCURRING ERROR IN  */
 /*        THE DAILY ARCHIVAL RUN - GARBAGE IN THE VOLUME SERIAL     */
 /*        NUMBER FIELD AND A REPETITIVELY PRINTED ENTRY AT THE END  */
 /*        OF THE ARCHIVAL LISTING                                   */
 /*     .  SUMMARY STATISTICS FOR THE ARCHIVAL RUN NOW APPEAR AT     */
 /*        THE END OF THE LIST OF ARCHIVED MODULES (PANVALET GENER-  */
 /*        ATED)                                                     */
 /*                                                                  */
 /*  UPDATED 09SEP80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  CORRECTED AN INCORRECT COUNT IN A SUBSTRING FUNCTION WHEN */
 /*        OVERLAYING THE LITERAL 'SYNC/SORT' WITH 'READ-JFCB' IN    */
 /*        THE SORT_RDJFCB_ERROR_MSG TEXT                            */
 /*                                                                  */
 /*     .  CORRECTED AN ERROR IN THE SUBSTRING FUNCTION WHICH        */
 /*        EXTRACTS FROM THE 2-UP LISTING THE NUMBER OF MODULES IN   */
 /*        CHARACTER FORMAT WHICH ARE TO BE ARCHIVED; IT WAS         */
 /*        DEFINED AS 'SUBSTR(REC,I+4,4)' WHICH INCLUDED A TRAILING  */
 /*        BLANK IN T#DEL; IT CAUSED NO ERROR SINCE DATA CONVER-     */
 /*        SION IS DONE IN THE FOLLOWING 'GET'                       */
 /*                                                                  */
 /*     .  ADDED CODE TO TEST FOR THE CONDITION OF HAVING NO JOBS    */
 /*        TO MIGRATE FROM THE PANSPOOL TO THE ARCHIVAL TAPE (I.E.   */
 /*        NO JOBS WERE EXTRACTED FROM THE PAN#2 '2-UP' LISTING);    */
 /*        IF NO JOBS ARE FOUND, CLOSE FILE2 AND EXIT GRACEFULLY     */
 /*        WITH A MESSAGE INDICATING A POSSIBLE ERROR                */
 /*                                                                  */
 /*     .  CHANGED DDNAME 'TEMP' TO 'TEMPFILE'                       */
 /*     .  ADDED INITIALIZATION TO THE DECLARE FOR TEMP_NAME2        */
 /*     .  CORRESPONDS TO LEVEL 80-03 OF THE PANSPOOL USER'S GUIDE   */
 /*                                                                  */
 /*  UPDATED 12SEP80, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  ADDED SKELETAL (MINIMAL) PARM FIELD VERIFICATION - ONLY   */
 /*        CHECK TO SEE THAT A NUMERIC VALUE WAS SPECIFIED; IF NOT,  */
 /*        ISSUE MESSAGE AND TERMINATE ARCHIVAL RUN                  */
 /*     .  ADDED NEW REPORT COLUMN HEADERS; REPORT NOW CENTERED      */
 /*     .  CHANGED FORMAT OF THE DATE ON THE REPORT TO MM/DD/YY      */
 /*        FORMAT                                                    */
 /*     .  ADDED LABEL 'REC-OUTPUT-ADJUST' TO PROVIDE SUPPORT FOR    */
 /*        CENTERING THE STATISTICS UNDER THE DETAIL PORTION OF THE  */
 /*        REPORT AFTER THE THIRD CALL TO PAN#2                      */
 /*                                                                  */
 /*  UPDATED 11MAR81, WILLIAM SMITH, MEMOREX CORPORATION             */
 /*     .  MVS 3.8/JES2 4.1 - SP 1.1 - ACF/VTAM R2;                  */
 /*        SUCCESSFULLY TESTED AT THE 8009+ PUT LEVEL                */
 /*                                                                  */
 /* ================================================================ */
 /*                                                                  */
 /*     PARM FIELD SPECIFICATION IS OF THE FORM 'MMDDYY,X' WHERE     */
 /*     'MMYY' IS THE CURRENT MONTH AND YEAR; 'DD' IS THE CURRENT    */
 /*     DAY MINUS '3' (LISTINGS ARE ONLY ARCHIVED AFTER REMAINING    */
 /*     ON THE LIBRARY FOR 3 DAYS); 'X' IS THE PROCESS OPTION OF     */
 /*     EITHER 'DAILY' OR 'MONTHLY' (MAY BE ABBREVIATED TO EITHER    */
 /*     'M' OR 'D').  NOTE THAT THE SPECIFICATION OF 'DD' IS AN      */
 /*     INSTALLATION DEFINED CRITERIA AND IS NOT VERIFIED BY THE     */
 /*     PROCESSING PROGRAM.                                          */
 /*                                                                  */
 /* ================================================================ */
 /*                                                                  */
 /*     OPTIONAL SPECIFICATION OF PANVALET DDNAMES IS BEING USED     */
 /*     BY THE ARCHIVAL UTILITY IN PLACE OF STANDARD DDNAMES.        */
 /*     ALTERNATE DDNAMES HAVE BEEN SPECIFIED THROUGH THE PARM       */
 /*     PARAMETER IN THE CALL TO PAN#2.  (FURTHER EXPLANATION OF     */
 /*     THIS FEATURE MAY BE FOUND IN SECTION 'D' OF THE PANVALET     */
 /*     'SYSTEM MANAGEMENT MANUAL', UNDER THE TOPIC 'OPTIONS AT      */
 /*     EXECUTION TIME'.)                                            */
 /*                                                                  */
 /*     THE MAPPING OF DDNAMES IS AS FOLLOWS:                        */
 /*         PANPARM1 MAPS  ----------------------                    */
 /*                        SORTWK01 ---> SYSIN                       */
 /*                        SORTWK02 ---> SYSPRINT                    */
 /*                                                                  */
 /*         PANPARM2 MAPS  ----------------------                    */
 /*                        SORTWK01 ---> SYSIN                       */
 /*                        SORTWK02 ---> SYSPRINT                    */
 /*                        SORTWK01 ---> SYSPUNCH                    */
 /*                                                                  */
 /*         PANPARM3 MAPS  ----------------------                    */
 /*                        SORTWK01 ---> SYSIN                       */
 /*                        TEMPFILE ---> SYSPRINT                    */
 /*                                                                  */
 /* ================================================================ */
 /*                                                                  */
 /*     SORT CONTROL-CARD STATEMENT LAYOUT - RECORD INPUT IS A 2-UP  */
 /*     PANVALET LISTING.                                            */
 /*                                                                  */
 /*        SORT FIELDS=(63,8,CH,A,87,10,CH,A),SIZE=E5000             */
 /*        RECORD TYPE=F,LENGTH=121                                  */
 /*                                                                  */
 /*     * COLUMN 63 CONTAINS THE JCL-SPECIFIED JOB NAME (PART OF     */
 /*       THE ++COMMENT CARD BUILT BY JCLLIST AT THE TIME THE        */
 /*       JOB WAS INTERCEPTED FOR ADDITION TO THE PANSPOOL)          */
 /*     * COLUMN 87 CONTAINS THE IEF375 JCLLIST-EXTRACTED START      */
 /*       TIME                                                       */
 /*                                                                  */
 /* ================================================================ */
 /*                                                                  */
 /*  REFERENCES:                                                     */
 /*  -----------                                                     */
 /*     PANVALET - THE PROGRAM MANAGEMENT AND SECURITY SYSTEM        */
 /*           A.)  "SYSTEM MANAGEMENT MANUAL"                        */
 /*                   - MAINTAINING A MATCH-MERGE OR HISTORICAL      */
 /*                     DELETE FILE                                  */
 /*                   - EXECUTING THE PANVALET PROGRAMS              */
 /*           B.)  "USER REFERENCE MANUAL OS"                        */
 /*           C.)  "EXTENDED FEATURES & ADVANCED TECHNIQUES"         */
 /*                   - PANVALET GROUP PROCESSOR                     */
 /*           D.)  "TSO OPTION"                                      */
 /*                   - USER REFERENCE MANUAL                        */
 /*                   - SYSTEM MANAGEMENT MANUAL                     */
 /*                                                                  */
 /* **************************************************************** */
 %PAGE;
 JCLARCH:
   PROC(PARM) OPTIONS(MAIN) REORDER;
   DCL
      (DATE, TIME, INDEX, PLISRTA ,SUBSTR, STRING, VERIFY) BUILTIN;
   DCL
      (P1, P2) PTR;
   DCL
      PARM CHAR(100) VARYING;
   DCL
      (GETPAN2, GETJFCB) ENTRY EXTERNAL OPTIONS(INTER, ASSEMBLER);
   DCL
      FILE1    RECORD       ENV(FB, RECSIZE(LRECL), BLKSIZE(BLKSIZE)),
      FILE2    RECORD INPUT ENV(FB, RECSIZE(0), BLKSIZE(0), LEAVE),
      PANARCH  RECORD OUTPUT,
      PANERRS  RECORD OUTPUT;
   DCL
      (LRECL,BLKSIZE) FIXED BIN(31) STATIC INIT(80);
   DCL
      REC CHAR(121) BASED(P1);
   DCL
      1 REC_OUTPUT_AREA,
         2 REC_OUT_CARRIAGE_CONTROL CHAR(1)   INIT(' '),
         2 FILLER1                  CHAR(10)  INIT((10)' '),
         2 REC_OUT                  CHAR(121) INIT((121)' '),
         2 FILLER2                  CHAR(1)   INIT((1)' ');
   DCL
      1 REC_OUTPUT_ADJUST,
         2 REC_OUT_ADJUST_CC        CHAR(1)   INIT(' '),
         2 FILLER1                  CHAR(5)   INIT((5)' '),
         2 REC_OUT_ADJUST_LINE      CHAR(121) INIT((121)' '),
         2 FILLER2                  CHAR(6)   INIT((6)' ');
   DCL
      1 VREC        BASED(P1),
         2 CARRIAGE_CONTROL         CHAR(1),
         2 PART1                    CHAR(59),
         2 ASTERISK                 CHAR(1),
         2 PART2                    CHAR(1),
         2 JOBNAME                  CHAR(8),
         2 PART3                    CHAR(51);
   DCL
      1 DREC        BASED(P1),
         2 PART1                    CHAR(61),
         2 PANNAME                  CHAR(10),
         2 PART2                    CHAR(15),
         2 SLASH                    CHAR(1),
         2 PART3                    CHAR(10),
         2 VERSION                  CHAR(1),
         2 PART4                    CHAR(23);
 %SKIP;
   DCL
      1 PANPARM1,
         2 PLENGTH  FIXED    BIN(15) INIT(17),
         2 PDDNAMES CHAR(17) INIT('SORTWK01,SORTWK02'),
      1 PANPARM2,
         2 PLENGTH  FIXED    BIN(15) INIT(29),
         2 PDDNAMES CHAR(29) INIT('SORTWK01,SORTWK02,,,,SORTWK01'),
      1 PANPARM3,
         2 PLENGTH  FIXED    BIN(15) INIT(17),
         2 PDDNAMES CHAR(17) INIT('SORTWK01,TEMPFILE');
   DCL
      PPRT   CHAR(80) INIT('++PRINT 2-UP,COMMENT,DATEM=010101,MMDDYY');
   DCL
      UPTYPE CHAR(1)  DEFINED PPRT POS(9);    /* MODIFY THE UP-TYPE */
   DCL
      PANMM  CHAR(2)  DEFINED PPRT POS(35),
      PANDD  CHAR(2)  DEFINED PPRT POS(37),
      PANYY  CHAR(2)  DEFINED PPRT POS(39);
   DCL
      PUSE   CHAR(80) INIT('++USING PGP');    /* INVOKE PGP FUNCTNS */
   DCL
      PDEL   CHAR(80) INIT('+-DELETE NAME='); /* PGP MODEL COMMANDS */
   DCL
      POPT   CHAR(80) INIT('++OPTION INPUT'); /* EXTENDED FACILITY  */
 %SKIP;
   DCL
      REPORT_DATE CHAR(6);                    /*  GET CURRENT DATE  */
   DCL
      REPORT_TIME CHAR(9);                    /*  TOD WHEN JOB RUNS */
   DCL
      PAGE_COUNT  BIN FIXED(15,0) STATIC INIT(0);
   DCL
      EJECT       BIT(1) INIT('1'B);          /* PAGENATION FOR THE */
   DCL
      NOEJECT     BIT(1) INIT('0'B);          /* NEW PERIOD MESSAGE */
 %SKIP;
   DCL
      TDATE       CHAR(6);
   DCL
      1 TD DEFINED TDATE,    /* SUBSTITUTED ON ++PRINT CONTROL CARD */
         2 MM PIC'99',       /*     CURRENT MONTH, DAY-3, YEAR      */
         2 DD PIC'99',       /*   EXTRACTED FROM PARM INFO ON EXEC  */
         2 YY PIC'99';       /*            JCL STATEMENT            */
   DCL
      MONTHLY BIT(1);        /* IF '1' BIT ---> THEN MONTHLY BACKUP */
 %SKIP;
   DCL
      1 REPORT_TITLE1,
         2 LINE1A CHAR(1)  INIT('1'),
         2 LINE1B CHAR(19) INIT('MEMOREX CORPORATION'),
         2 LINE1C CHAR(38) INIT((38)' '),
         2 LINE1D CHAR(15) INIT('P A N S P O O L'),
         2 LINE1E CHAR(52) INIT((52)' '),
         2 LINE1F,
            5 REPORT_MONTH CHAR(2),
            5 SLASH1       CHAR(1)  INIT('/'),
            5 REPORT_DAY   CHAR(2),
            5 SLASH2       CHAR(1)  INIT('/'),
            5 REPORT_YEAR  CHAR(2);
   DCL
      1 REPORT_TITLE2,
         2 LINE2A CHAR(1)  INIT(' '),
         2 LINE2B CHAR(28) INIT('SSG DATA PROCESSING SERVICES'),
         2 LINE2C CHAR(96) INIT((96)' '),
         2 LINE2D,
            5 REPORT_HOUR  CHAR(2),
            5 SLASH1       CHAR(1)   INIT(':'),
            5 REPORT_MINUT CHAR(2),
            5 SLASH2       CHAR(1)   INIT(':'),
            5 REPORT_SEC   CHAR(2);
   DCL
      1 REPORT_TITLE3,
         2 LINE3A CHAR(1)  INIT(' '),
         2 LINE3B CHAR(18) INIT('PRODUCTION CONTROL'),
         2 LINE3C CHAR(29) INIT((29)' '),
         2 LINE3D CHAR(36) INIT('PANVALET/JES2 SYSMSG SPOOLING SYSTEM'),
         2 LINE3E CHAR(41) INIT((41)' '),
         2 LINE3F,
            5 PAGE_LABEL   CHAR(5)   INIT('PAGE '),
            5 PAGE_NUMBER  PIC 'ZZ9' INIT(0);
   DCL
      1 REPORT_TITLE4,
         2 LINE4A CHAR(1)  INIT(' '),
         2 LINE4B CHAR(17) INIT((17)' '),
         2 LINE4C CHAR(97) INIT('* * *  S P O O L  F I L E  A R C H I V
 A L  &  M I G R A T I O N  D E T A I L  R E P O R T  * * *'),
         2 LINE4D CHAR(18) INIT((18)' ');
   DCL
      1 REPORT_TITLE5,
         2 LINE5A CHAR(1)  INIT(' '),
         2 LINE5B CHAR(31) INIT((31)' '),
         2 LINE5C CHAR(70) INIT('* * *  A R C H I V A L  E R R O R  A N
 A L Y S I S  R E P O R T  * * *'),
         2 LINE5D CHAR(31) INIT((31)' ');
   DCL
      1 REPORT_TITLE6,
         2 LINE6A CHAR(12) INIT((12)' '),
         2 LINE6B CHAR(29) INIT(' PANVALET    LVL   JCL  TYPE '),
         2 LINE6C CHAR(31) INIT('STAT ACCESSED      NO OF ACTION'),
         2 LINE6D CHAR(31) INIT(' JOBNAME    JES2      JOB START'),
         2 LINE6E CHAR(15) INIT('        JOB END'),
         2 FILLER CHAR(15) INIT((15)' ');
   DCL
      1 REPORT_TITLE7,
         2 LINE8A CHAR(12) INIT((12)' '),
         2 LINE8B CHAR(29) INIT('  MODULE          ERROR      '),
         2 LINE8C CHAR(31) INIT('                  STMNTS       '),
         2 LINE8D CHAR(31) INIT('             NO         TIME   '),
         2 LINE8E CHAR(15) INIT('          TIME '),
         2 FILLER CHAR(15) INIT((15)' ');
   DCL
      BLANK_LINE  CHAR(133) INIT((133)' ');
   DCL
      1 DASHES,                    /* PRINT DASHES FOR HEADINGS     */
         2 DASH1 CHAR(1)   INIT(' '),
         2 DASH2 CHAR(132) INIT((132)'-');
   DCL
      1 END_OLD_PERIOD,            /* PRINT 20 TIMES - W/WEEKLY RUN */
         2 EOMSG1 CHAR(1)  INIT('0'),
         2 EOMSG2 CHAR(24) INIT((24)' '),
         2 EOMSG3 CHAR(33) INIT('*** START OF NEW PERIOD FOLLOWS. '),
         2 EOMSG4 CHAR(24) INIT('RETAIN PREVIOUS LISTING '),
         2 EOMSG5 CHAR(39) INIT('FOR FUTURE REFERENCE. ***'),
         2 FILLER CHAR(12) INIT((12)' ');
   DCL
      1 NOTHING_TO_ARCHIVE,        /* ISSUED AFTER 1ST CALL TO PAN2 */
         2 NOARC1 CHAR(1)  INIT('-'),
         2 NOARC2 CHAR(3)  INIT((3)' '),
         2 NOARC3 CHAR(34) INIT('*** NO JOBS ARCHIVED FOR SPECIFIED'),
         2 NOARC4 CHAR(33) INIT(' TIME PERIOD.  POSSIBLE ERROR IN '),
         2 NOARC5 CHAR(27) INIT('PARM FIELD OR NO JOBS MEET '),
         2 NOARC6 CHAR(32) INIT('ARCHIVAL SELECTION CRITERIA. ***'),
         2 FILLER CHAR(3)  INIT((3)' ');
   DCL
      1 JFCB,                      /* SUPPLIED BY CALL TO 'GETJFCB' */
         2 PART1       CHAR(117),
         2 JFCBNVOL    BIT(8),     /* NUMBER OF OUTPUT TAPE VOLUMES */
         2 JFCBVOLS(5) CHAR(6),    /*  OUTPUT TAPE VOLSER FROM JFCB */
         2 PART2       CHAR(21);
   DCL
      1 VOLUME_RECORD,
         2 PART1    CHAR(36) INIT('-*** OUTPUT VOLUME SERIAL NUMBER(S)='
 ),
         2 VOLSERS  CHAR(85) INIT((85)' '),
         2 FILLER   CHAR(12) INIT((12)' ');
   DCL
      1 DUPLICATE_MESSAGE,
         2 PART1    CHAR(33) INIT('0*** DUPLICATE ENTRIES FOUND FOR '),
         2 DUPNAME  CHAR(10),
         2 PART2    CHAR(78) INIT(' ***'),
         2 FILLER   CHAR(12) INIT((12)' ');
   DCL
      1 DELREC      BASED(P2),
         2 DPART1   CHAR(16),
         2 NAME     CHAR(10),
         2 DPART3   CHAR(54);
   DCL
      DELTABLE(#DEL) CHAR(10) CONTROLLED;
   DCL
      TEMP_NAME1    CHAR(8)  INIT(' '),
      TEMP_NAME2    CHAR(10) INIT(' ');
   DCL
      (RC INIT(1), #DEL INIT(0), #REC, I, J, K) FIXED BIN(31);
 %SKIP;
   DCL
      SCD CHAR(80) INIT(
   ' SORT FIELDS=(63,8,CH,A,87,10,CH,A),SIZE=E5000 ');
   DCL
      RCD CHAR(80) INIT(' RECORD TYPE=F,LENGTH=121 ');
   DCL
      CORE FIXED BIN(31) INIT(100000);
 %SKIP;
   DCL
      1 PARM_FIELD_ERROR_MSG,
         2 ERROR_TXT1 CHAR(35)
                      INIT('-*** USER SPECIFIED PARM FIELD NOT '),
         2 ERROR_TXT2 CHAR(40)
                      INIT('NUMERIC.  CHECK PARM FIELD SPECIFICATION'),
         2 ERROR_TXT3 CHAR(31)
                      INIT(' AND RESUBMIT ARCHIVAL RUN. ***'),
         2 FILLER     CHAR(27)  INIT((27)' ');
 %SKIP;
   DCL
      1 SORT_RDJFCB_ERROR_MSG,
         2 ERROR_TXT  CHAR(32)
                      INIT('-*** SYNC/SORT UNSUCCESSFUL. RC='),
         2 MSGRC      PIC'9999',
         2 PART2      CHAR(90) INIT(' ***'),
         2 FILLER     CHAR(7)  INIT((7)' ');
 %SKIP;
   DCL
      1 GETPAN#2_ERROR_MSG,
         2 PART1   CHAR(5)  INIT('-*** '),
         2 CALL#   CHAR(4)  INIT('1ST'),
         2 PART2   CHAR(31) INIT('CALL TO PAN#2 UNSUCCESSFUL. RC='),
         2 PANRC   PIC'9999',
         2 PART3   CHAR(77) INIT('. PAN OUTPUT FOLLOWS. ***'),
         2 FILLER  CHAR(12) INIT((12)' ');
   DCL
      PANERRDD     CHAR(8)  INIT('SORTWK02');
   DCL
      T#DEL        CHAR(10);  /* TEMP COUNT - # MODULES TO ARCHIVE */
   DCL
      1 NOMATCH_ERROR_MSG,
         2 MATCH3  CHAR(79) INIT('0*** NUMBER OF DATA SETS ARCHIVED NOT
 EQUAL TO NUMBER IN ARCHIVAL DIRECTORY ***'),
         2 FILLER  CHAR(54) INIT((54)' ');
 %PAGE;
      REPORT_DATE=DATE;                       /* JOB EXECUTION DATE */
      REPORT_YEAR=SUBSTR(REPORT_DATE,1,2);
      REPORT_MONTH=SUBSTR(REPORT_DATE,3,2);
      REPORT_DAY=SUBSTR(REPORT_DATE,5,2);
      REPORT_TIME=TIME;                       /* JOB EXECUTION TIME */
      REPORT_HOUR=SUBSTR(REPORT_TIME,1,2);
      REPORT_MINUT=SUBSTR(REPORT_TIME,3,2);
      REPORT_SEC=SUBSTR(REPORT_TIME,5,2);

      TDATE=PARM;                   /* ARCHIVE DATE ----> PARM CARD */
      IF (VERIFY(TDATE,'0123456789')^=0)  /* USER SPECIFIED NUMBRS? */
         THEN DO;                         /* IF NOT, MSG AND RETURN */
                 CALL ERROR_DISPLAY
                      (STRING(PARM_FIELD_ERROR_MSG));
                 RETURN;
         END;
      PANMM=MM;                     /*  BREAK DOWN TO PLACE ON THE  */
      PANDD=DD;                     /*       ++PRINT STATEMENT      */
      PANYY=YY;
      MONTHLY=(INDEX(PARM,'M')^=0); /* 'M' ON PARM ? ---> MONTHLY ! */

      OPEN  FILE(FILE1) OUTPUT TITLE('SORTWK01');
      WRITE FILE(FILE1) FROM(PPRT);    /* SUPPLY A ++PRINT 2-UP CRD */
      CLOSE FILE(FILE1);
      CALL GETPAN2(PANPARM1, RC);      /* LINK TO PAN#2 - ASSEMBLER */
      IF RC^=0 THEN                    /* PROBS WITH PAN#2 ? YES -> */
         GO TO BAD_PANVALET_RC;        /* PRINT A MESSAGE ON REPORT */
      IF MONTHLY                       /* MONTHLY RUN ? BIT=1 YES-> */
         THEN DO;                      /* PRT 20 LINES - NEW PERIOD */
                 DO I=1 TO 19;
                    CALL ARCHIVE_DISPLAY
                         (STRING(END_OLD_PERIOD),NOEJECT);
                 END;
                 CALL ARCHIVE_DISPLAY (STRING(END_OLD_PERIOD),EJECT);
              END;
      LRECL=121;
      BLKSIZE=3146;
      OPEN FILE(FILE2) TITLE('SORTWK02'), FILE(FILE1) OUTPUT
      TITLE('TEMPFILE');
 %PAGE;
 READ_PANVALET_2UP:
      READ FILE(FILE2) SET(P1);             /* LET'S LOOK AT -> 2-UP */
      IF SUBSTR(REC,2,12)='SELECTED*** ' THEN /* ON THE RIGHT LINE ? */
         DO;
            I=INDEX(REC,'DATA SETS')-8;     /* GET POSN. OF THE NUMB */
            T#DEL=SUBSTR(REC,I,3)××SUBSTR(REC,I+4,3); /* NO COMMAS ! */
            GET STRING(T#DEL) LIST(#DEL);   /* CONVERT CHAR-> BINARY */
            IF #DEL<=0 THEN                 /* NOTHING TO ARCHIVE... */
               DO;
                  CALL ERROR_DISPLAY (STRING(NOTHING_TO_ARCHIVE));
                  GO TO EOF;                /* CLOSE ALL FILES--EXIT */
               END;
            GO TO SORT_MERGE_ROUTINE;       /* SORT THE JOBS TO ARCH */
         END;
      IF ASTERISK=' ' THEN                  /* AT EOF ON THE 2-UP ?  */
         GO TO READ_PANVALET_2UP;           /* NO, SO CONTINUE SERCH */
      CARRIAGE_CONTROL=' ';                 /* CHANGE TO SINGLE SPAC */
      WRITE FILE(FILE1) FROM(REC);
      GO TO READ_PANVALET_2UP;
 %PAGE;
 SORT_MERGE_ROUTINE:
      CLOSE FILE(FILE2), FILE(FILE1);  /* READY TO SORT NEW DIRECTRY */
      CALL PLISRTA(SCD,RCD,CORE,RC);   /* CALL OS/VS SORT-MERGE PROG */
      IF RC^=0 THEN
         DO;                           /* BAD EXECUTION - SORT/MERGE */
            MSGRC=RC;                  /* PLACE RETURN CODE IN MESAG */
            CALL ERROR_DISPLAY (STRING(SORT_RDJFCB_ERROR_MSG));
            RETURN;
         END;
      OPEN FILE(FILE2) TITLE('SORTOUT'); /* NEWLY SORTED DIRECT RECS */
      ON ENDFILE(FILE2)
         GO TO EOPRINT;
      DO I=1 BY 1;                     /* PROCESS RECS & PRINT REPRT */
         READ FILE(FILE2) SET(P1);
         IF JOBNAME=TEMP_NAME1 THEN    /* BLANK OUT REPETITIVE JOB-  */
            JOBNAME=' ';               /* NAMES ON PAN COMMENT CARDS */
                                       /* IN NEWLY CREATED DIRECTRY  */
         ELSE
            TEMP_NAME1=JOBNAME;
         REC_OUT=REC;                  /*  MOVE PRINT RECS TO OUTPUT */
         CALL ARCHIVE_DISPLAY (STRING(REC_OUTPUT_AREA),NOEJECT);
      END;
 %PAGE;
 EOPRINT:
      #REC=I-1;
      CLOSE FILE(FILE2);
      REVERT ENDFILE(FILE2);          /* NEGATE THE ON-UNIT CONDTION */
      LRECL=80;                       /*  MODIFY THE FILE1 DCB INFO  */
      BLKSIZE=3120;
      OPEN  FILE(FILE1) OUTPUT TITLE('SORTWK01');
      WRITE FILE(FILE1) FROM(PUSE);   /* ++USING PGP CONTROL CARD    */
      WRITE FILE(FILE1) FROM(PDEL);   /* +-DELETE NAME = MODEL CARD  */
      UPTYPE='0';                     /* PGP REQUIRES A '0-UP' CARD  */
      WRITE FILE(FILE1) FROM(PPRT);   /* ++PRINT 0-UP,COMMENT CARD   */
      CLOSE FILE(FILE1);              /* SORTWK01 MAPPED TO SYSPUNCH */
      CALL GETPAN2(PANPARM2, RC);     /* ALL SORTWK DD'S TO PANVALET */
      IF RC^=0 THEN                   /* PROBS WITH PAN#2 ?  YES --> */
         DO;                          /* BAD RETURN CODE FROM PAN-#2 */
            CALL#='2ND';              /* MOVE '2ND' TO MESSAGE TEXT  */
            PANRC=RC;                 /* MOVE RETURN CODE TO MESSAGE */
            GO TO BAD_PANVALET_RC;    /* ISSUE A MESSAGE OF PAN PROB */
         END;
      OPEN  FILE(FILE1) INPUT TITLE('SORTWK01');
      ON ENDFILE(FILE1)               /* ESTABLISH END-FILE CONDITON */
         GO TO DELETE_CLOSE;          /* ADDED PER RICH STEHN 02-SEP */
      ALLOCATE DELTABLE;              /* ACQUIRE STORAGE FOR DELTABL */
      DO I=1 TO #DEL;
         READ FILE(FILE1) SET(P2);    /* FILL UP TABLE WITH MODULE   */
         DELTABLE(I)=NAME;            /* NAMES TO SCRATCH OFF PANLIB */
      END;
 %SKIP;
 DELETE_CLOSE:
      CLOSE FILE(FILE1);
      REVERT ENDFILE(FILE1);          /* ADDED PER RICH STEHN 02-SEP */
      DO I=1 TO #DEL-1;               /*    A SIMPLE EXCHANGE SORT   */
         DO J=I+1 TO #DEL;
            IF DELTABLE(I)>DELTABLE(J) THEN
               DO;
                  TEMP_NAME2=DELTABLE(I);
                  DELTABLE(I)=DELTABLE(J);
                  DELTABLE(J)=TEMP_NAME2;
               END;
         END;
      END;
      OPEN FILE(FILE1) OUTPUT TITLE('SORTWK01');
      IF MONTHLY THEN
         DO;                          /*  CREATE AN ADDITIONAL TAPE  */
            OPEN FILE(FILE2) TITLE('PANDD3');
            CLOSE FILE(FILE2);
         END;
      ELSE
         WRITE FILE(FILE1) FROM(POPT);/* SUPPLY ++OPTION TO PANVALET */
      ALLOCATE DELREC;
      DPART1='++DELETE   NAME=';
      DPART3=' ';                     /*  BLANK OUT THE RECORD AREA  */
      DO I=1 TO #DEL;
         NAME=DELTABLE(I);            /* LIST ALL JOBS TO BE ARCH'ED */
         WRITE FILE(FILE1) FROM(DELREC);
      END;
      CLOSE FILE(FILE1);
      CALL GETPAN2(PANPARM3, RC);
      IF RC^=0 THEN                   /* PROBS WITH PAN#2 ?  YES --> */
         DO;                          /* BAD RETURN CODE FROM PAN-#2 */
            CALL#='3RD';              /* MOVE '3RD' TO MESSAGE TEXT  */
            PANRC=RC;                 /* MOVE RETURN CODE TO MESSAGE */
            PANERRDD='TEMPFILE';      /* CHANGE THE NAME OF DD CARD  */
            GO TO BAD_PANVALET_RC;    /* ISSUE A MESSAGE OF PAN PROB */
         END;
      OPEN FILE(FILE2) TITLE('TEMPFILE');
      ON ENDFILE(FILE2)
         GO TO EOJ;
 %PAGE;
 READ_PANVALET_OUTPUT:
      READ FILE(FILE2) SET(P1);
      IF SLASH='/' THEN
         IF VERSION>'1' THEN
            DO;
               DUPNAME=PANNAME; /* SAME NAME - DIFFERENT VERS */
               CALL ERROR_DISPLAY (STRING(DUPLICATE_MESSAGE));
            END;
      IF SUBSTR(REC,2,3)^='***' THEN
         GO TO READ_PANVALET_OUTPUT;   /* LOOKING FOR '***' IN STATS */
      IF MONTHLY THEN
         K=1;
      ELSE
         K=2;
      REC_OUT_ADJUST_CC='-';           /*        TRIPLE SPACE        */
      CARRIAGE_CONTROL=' ';
 %PAGE;
 DELETE_RUN_PRINT_ANALYSIS:
      DO I=1 TO K;                     /* M -> K=1, THEN ONLY 1 TIME */
         REC_OUT_ADJUST_LINE=REC;      /* ADDED PER RICH STEHN 02SEP */
         CALL ARCHIVE_DISPLAY (STRING(REC_OUTPUT_ADJUST),NOEJECT);
         REC_OUT_ADJUST_CC=' ';        /*        SINGLE SPACE        */
         READ FILE(FILE2) SET(P1);     /* READS ALL SYSPRINT RECORDS */
      END;
      T#DEL=SUBSTR(REC,115,3)××SUBSTR(REC,119,3);  /* STATS FROM DEL */
      GET STRING(T#DEL) LIST(J);                   /* CONVERT TO NO. */
      K=99999;                                     /* FINISH UP ANAL */
      GO TO DELETE_RUN_PRINT_ANALYSIS;             /* GET LAST LINES */
 %PAGE;
 BAD_PANVALET_RC:
      OPEN FILE(FILE2) TITLE(PANERRDD);            /* PAN2 ERROR MSG */
      ON ENDFILE(FILE2)
         GO TO EOF;
      CALL ERROR_DISPLAY (STRING(GETPAN#2_ERROR_MSG));

 READ_ERROR:
      READ FILE(FILE2) SET(P1);
      REC_OUT=REC;                     /* ADDED PER RICH STEHN 02SEP */
      CALL ERROR_DISPLAY (STRING(REC_OUTPUT_AREA));
      GO TO READ_ERROR;
 %PAGE;
 EOF:
      CLOSE FILE(FILE2);
      RETURN;

 EOJ:
      CLOSE FILE(FILE2);
      CALL GETJFCB('PANDD2  ',JFCB,RC);  /* GET # VOLS AND VOL-SER'S */
      IF RC^=0 THEN
         DO;                             /* INFORM USER OF JFCB PROB */
            SUBSTR(ERROR_TXT,6,9)='READ-JFCB';  /* OVERLAY SORT TEXT */
            MSGRC=RC;                           /* STUFF THE JFCB RC */
            CALL ERROR_DISPLAY (STRING(SORT_RDJFCB_ERROR_MSG));
            RETURN;
         END;

      PUT STRING(VOLSERS) EDIT((JFCBVOLS(I) DO I=1 TO JFCBNVOL),'***')
         (A,X(1));                       /* FORMAT JFCB VOLSER-VOLNO */
      CALL ARCHIVE_DISPLAY (STRING(VOLUME_RECORD),NOEJECT);

 FINISH:
      IF J^=#REC THEN                    /* NO. JOBS ^= NO. RECORDS  */
         DO;
            CALL ERROR_DISPLAY (STRING(NOMATCH_ERROR_MSG));
            RETURN;
         END;
      CLOSE FILE(PANARCH);
 %PAGE;
 ARCHIVE_DISPLAY: PROC(ARCHIVE_STMNT,PAGENATION);

 DCL LINE_COUNT BIN FIXED(15,0) STATIC INIT(60);
 DCL PAGE_COUNT BIN FIXED(15,0) STATIC INIT(0);

 DCL ARCHIVE_STMNT CHAR(133);            /* 133-BYTE ARC MESSAGE TXT */
 DCL PAGENATION    BIT(1);               /* PAGENATION - MONTHLY RUN */


 LINE_COUNT=LINE_COUNT+1;                /* ARCHIVE STMNTS USE 1 LNE */
 IF LINE_COUNT >= 61 THEN                /* ARE WE AT END OF A PAGE? */
    DO;                                  /* YES ! -> UP LINECNT - 12 */
       LINE_COUNT=12;                    /* RESET CNTR: 12 LINE HEDR */
       LINE_COUNT=LINE_COUNT+1;          /* INCREMENT THE LINE COUNT */
       PAGE_COUNT=PAGE_COUNT+1;          /* INCREMENT THE PAGE COUNT */
       REPORT_TITLE3.PAGE_NUMBER = PAGE_COUNT;     /* MOVE TO TITLE3 */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE1);    /*    HEADINGS    */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE2);    /*      FOR       */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE3);    /*    12 LINES    */
       WRITE FILE(PANARCH) FROM(BLANK_LINE);       /*   BLANK LINE   */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE4);    /* ARCHIVE HEADER */
       DO I=1 TO 2;
          WRITE FILE(PANARCH) FROM(BLANK_LINE);    /*    2 BLANKS    */
       END;
       WRITE FILE(PANARCH) FROM(DASHES);           /* LINE OF DASHES */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE6);    /* COLUMN HEADER1 */
       WRITE FILE(PANARCH) FROM(REPORT_TITLE7);    /* COLUMN HEADER2 */
       WRITE FILE(PANARCH) FROM(DASHES);           /* LINE OF DASHES */
       WRITE FILE(PANARCH) FROM(BLANK_LINE);       /*   BLANK LINE   */
    END;

 WRITE FILE(PANARCH) FROM(ARCHIVE_STMNT);

 IF PAGENATION THEN LINE_COUNT=61;       /* PAGE EJECTS - NEW CYCLES */

 END;
 %PAGE;
 ERROR_DISPLAY: PROC(ERROR_STMNT);       /* FORMAT: ERROR MSG REPORT */

 DCL LINE_COUNT  BIN FIXED(15,0) STATIC INIT(60);
 DCL PAGE_COUNT  BIN FIXED(15,0) STATIC INIT(0);

 DCL ERROR_STMNT CHAR(133);              /* 133-BYTE ERR MESSAGE TXT */

 LINE_COUNT=LINE_COUNT+2;                /* ERROR MESSAGES USE 2 LNE */
 IF LINE_COUNT >= 61 THEN                /* ARE WE AT END OF A PAGE? */
    DO;                                  /* YES ! -> UP LINECNT BY 8 */
       LINE_COUNT=8;                     /* RESET CNTR: 8 LINE HEADR */
       LINE_COUNT=LINE_COUNT+1;          /* INCREMENT THE LINE COUNT */
       PAGE_COUNT=PAGE_COUNT+1;          /* INCREMENT THE PAGE COUNT */
       REPORT_TITLE3.PAGE_NUMBER = PAGE_COUNT;     /* MOVE TO TITLE3 */
       WRITE FILE(PANERRS) FROM(REPORT_TITLE1);    /*    HEADINGS    */
       WRITE FILE(PANERRS) FROM(REPORT_TITLE2);    /*      FOR       */
       WRITE FILE(PANERRS) FROM(REPORT_TITLE3);    /*     8 LNES     */
       WRITE FILE(PANERRS) FROM(BLANK_LINE);       /*   BLANK LINE   */
       WRITE FILE(PANERRS) FROM(REPORT_TITLE5);    /*  ERROR TITLE   */
       DO I=1 TO 2;                                /*    2 BLANKS    */
          WRITE FILE(PANERRS) FROM(BLANK_LINE);
       END;
    END;

 WRITE FILE(PANERRS) FROM(ERROR_STMNT);

 END;
 %PAGE;
    END; /* J C L A R C H */
/*
//LINKEDIT EXEC PGM=IEWL,PARM='LIST,LET,XREF',COND=(9,LT)
//SYSPRINT  DD  SYSOUT=*
//SYSUT1    DD  UNIT=SYSDA,SPACE=(CYL,(3,1))
//SYSLIB    DD  DISP=SHR,DSN=SYS1.PLIBASE
//          DD  DISP=SHR,DSN=SYS2.LINKLIB
//SYSLMOD   DD  DISP=SHR,DSN=SYS2.LINKLIB
//SYSLIN    DD  DISP=(OLD,DELETE),DSN=&&OBJMOD
//          DD  *
 SETSSI   80000912
 IDENTIFY JCLARCH1('PANVALET/V10-JES2 SYSMSG SPOOLING SYSTEM')
 NAME     JCLARCH(R)
/*
//
