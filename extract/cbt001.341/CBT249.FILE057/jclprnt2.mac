CONTROL MAIN PROMPT NOMSG
/* ================================================================= */
/*                                                                   */
/*  PANSPOOL - JES2 SYSMSG SPOOLING SYSTEM:  A PANVALET EXTERNAL WTR */
/*                                                                   */
/* ================================================================= */
/*                                                                   */
/*  DEVELOPED BY:  CANADIAN PACIFIC INFORMATION SYSTEMS              */
/*                 WINDSOR STATION                                   */
/*                 910 PEEL STREET                                   */
/*                 ROOM E-40                                         */
/*                 MONTREAL, QUEBEC, CANADA                          */
/*                 (514) 395-7880                                    */
/*  CONTACT:       ESTELLE ROTHER                                    */
/*                                                                   */
/*  SYMBOLIC PARAMETERS:                                             */
/*  -------------------                                              */
/*     &CTYPE   - THE 'CHANGE TYPE' FOR MODIFYING THE STATUS OF A    */
/*                GIVEN PANVALET MODULE (INACTIVE/DISABLED)          */
/*     &PTYPE   - THE 'PRINT TYPE' FOR MODIFYING THE USER FIELD OF   */
/*                A GIVEN PANVALET MODULE ('1' IMPLIES FORCED PRINT- */
/*                ING (SYSOUT=A) DUE TO JOB ERROR)                   */
/*     &OLDFLAG - DETERMINES IF IN '.OLD' OR '.NEW' MODE             */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  11MAR81                                                          */
/*  .  MVS 3.8/JES2 4.1 - SP 1.1 - ACF/VTAM R2; SUCCESSFULLY TESTED  */
/*     AT THE 8011+ PUT LEVEL                                        */
/*  .  FUTURE PLANS INVOLVE INCORPORATION OF ISPF DIALOG MANAGEMENT  */
/*     SERVICES WITHIN THE JCLPRINT CLIST;  THIS WILL ENABLE THE     */
/*     USER TO BROWSE THE SYSMSG OUTPUT PREVIOUSLY RETRIEVED FROM    */
/*     THE PANSPOOL IN FULL-SCREEN MODE (IBM PP 5668-009)            */
/*  .  INCORPORATION OF MRXPRINT/DSPRINT TYPE FUNCTIONS ARE ALSO     */
/*     ANTICIPATED (MRX 2089/IBM 328X DEVICE SUPPORT)                */
/*                                                                   */
/*  UPDATED:  RICH STEHN, MRX USER SERVICES                          */
/*  08OCT80                                                          */
/*  .  CHANGED THE PROCESS FOR RESTORING A PANSPOOL MEMBER TO        */
/*     'PROD.TAPELIST'-----THE VOLUME SERIAL NUMBER OF THE ARCHIVAL  */
/*     BACKUP TAPE DOES NOT NEED TO BE SPECIFIED; THE CURRENT        */
/*     GENERATION OF THE ARCHIVAL BACKUP TAPE WILL BE USED FOR A     */
/*     RESTORE                                                       */
/*          NOTE: THIS IS NECESSARY BECAUSE OF THE MANNER IN WHICH   */
/*                A BACKUP TAPE FILE OF A PANVALET LIBRARY IS        */
/*                CREATED ---                                        */
/*                FOR A MULTI-VOLUME BACKUP FILE OF A PANVALET       */
/*                LIBRARY, THE RESTORE PROCESS MUST BEGIN WITH THE   */
/*                FIRST VOLUME ---                                   */
/*                AN ATTEMPT TO RESTORE A MEMBER BEGINNING WITH      */
/*                OTHER THAN THE FIRST VOLUME WILL ABORT !           */
/*                                                                   */
/*  UPDATED:  RICH STEHN, MRX USER SERVICES                          */
/*  17SEP80                                                          */
/*  .  CHANGED TEXT BEING PRINTED ON THE TERMINAL SCREEN TO REFER TO */
/*     'PANSPOOL' RATHER THAN 'PANVALET'                             */
/*     UNDER LABEL 'GETJNUM', CHANGED UNCONDITIONAL BRANCH FROM      */
/*     'GOTO NOJNUM' TO 'GOTO JNUM'; THE LABEL 'NOJNUM' DID NOT      */
/*     EXIST                                                         */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  17SEP80                                                          */
/*  .  ADDED DELETION OF PANTEMP.DATA TO ELIMINATE PROBLEMS OCCURR-  */
/*     ING WHEN CLIST IS INTERRUPTED OR SYSTEM FAILURE AND DATASET   */
/*     IS LEFT ON DISC                                               */
/*  .  NOTE THAT A MODULE RESTORED TO PROD.TAPELIST (OLD MODE) WILL  */
/*     NOT HAVE ITS STATUS CHANGED TO DISABLED UNLESS IT IS PRINTED  */
/*     ON HARDCOPY; FROM TIME-TO-TIME IT MAY BE NECESSARY TO MANU-   */
/*     ALLY DISABLE MODULES TO PREVENT OUT-OF-SPACE CONDITIONS ON    */
/*     PROD.TAPELIST                                                 */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  25JUL80                                                          */
/*  .  CHANGED THE RESTORE JCL AND JCLPRINT CLIST TO SUPPLY THE TSO  */
/*     USERID AS PART OF THE SUBMITTED RESTORE JOBNAME - EASIER TO   */
/*     ROUTE LISTING TO USER SUBMITTING THE RESTORE JOB; TSO SUBMIT  */
/*     COMMAND WILL PROMPT FOR 'USERID PLUS ONE CHARACTER'           */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  09JUL80                                                          */
/*  . CORRECTED AN ATTENTION-EXIT LOOP WHICH OCCURRED WHEN A USER    */
/*    INTERRUPTED A RETRIEVAL FROM ARCHIVES; LABEL 'ABORT' SPECIFIED */
/*    A PCF 'X ECHO' COMMAND WHICH IS NOT PRESENT IN THIS SHOP;      */
/*    ISSUED A WRITE INFORMING USER THAT THE RESTORE PROCESS HAD     */
/*    BEEN TERMINATED                                                */
/*  . COMPLETELY RE-DOCUMENTED THE PROCEDURE BY WHICH A USER IS TO   */
/*    SPECIFY THE PANVALET MODULE NAMES NECESSARY TO COMPLETE A RE-  */
/*    STORE FROM ARCHIVES TO PROD.TAPELIST                           */
/*  . IF HARDCOPY PRINT IS REQUESTED FOR A JOB ON PROD.PANSPOOL, THE */
/*    STATUS INDICATOR IS CHANGED TO 'INACTIVE'; IF HARDCOPY PRINT   */
/*    IS REQUESTED FOR A JOB ON PROD.TAPELIST, THE STATUS INDICATOR  */
/*    IS CHANGED TO 'DISABLED' FOR SUBSEQUENT DELETION DURING THE    */
/*    NEXT DAILY/MONTHLY ARCHIVE                                     */
/*  . THE DEFAULT REMOTE IS REMOTE '0' (RMT0)                        */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  29MAY80                                                          */
/*  . CHANGED THE SPACE ALLOCATION FOR PANTEMP.DATA FROM SP(5,5)     */
/*    TRACKS TO SP(2,1) CYL TO PREVENT B37-04 ON LARGE (40+ BLKS -   */
/*    6100 LINES OF JCL) SPOOLED MEMBER                              */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  23MAY80                                                          */
/*  . CHANGED THE SYSOUT ALLOCATION OF PSPLOUT FROM 'X' (HONEYWELL   */
/*    PAGE PRINTING SYSTEM) TO 'I' (DOCUMATION 3211 IMPACT PRINTER)  */
/*    TO HELP EXPEDITE FOREGROUND RETRIEVALS FROM PROD.PANSPOOL      */
/*                                                                   */
/*  UPDATED:  WILLIAM SMITH, SYSTEMS PROGRAMMER                      */
/*  24APR80   MEMOREX CORPORATION                                    */
/*            DEPT. 8205  MS/10-35                                   */
/*            SAN TOMAS AT CENTRAL EXPRESSWAYS                       */
/*            SANTA CLARA, CALIFORNIA 95052                          */
/*            (408)-987-3919                                         */
/*                                                                   */
/*  . GENERAL CLEANUP OF CLISTS - ALIGNMENT AND REMOVAL OF PAN       */
/*    COMMAND ABBREVIATIONS -                                        */
/*  . MVS 3.8/JES2 4.1 - SE 1.1 - ACF/VTAM R2                        */
/*  . PANVALET/V10 W/TSO OPTION                                      */
/*  . ADDITION OF SEVERAL FULL-SCREEN 3270 CLEARS; MORE FORMALIZED   */
/*    AND READABLE HELP INFORMATION                                  */
/*                                                                   */
/* ================================================================= */
CONTROL NOMSG
ERROR  OFF
ATTN   GOTO EOJ
FREE   FI(JCLLIST TAPELIST)
DELETE PANTEMP.DATA
CONTROL MSG
ERROR  EXIT
ALLOC  FI(JCLLIST)  DA('PROD.PANSPOOL')   SHR
ALLOC  FI(TAPELIST) DA('PROD.TAPELIST')   SHR
ALLOC  FI(PSPLIN)   DA(PANTEMP.DATA)      SP(2,1)  CYL
SET    &DDNAME=JCLLIST
SET    &OLDFLAG=0
SET    &CTYPE=STATUS(INACTIVE)
CLR3270
START: ERROR GOTO START
ATTN   GOTO START
SET    &JOBNAME=
SET    &JOBNUMBER=
WRITE
WRITE **** PANSPOOL/JES2 SYSMSG SPOOLING SYSTEM ****
WRITE FOR HELP INFORMATION ENTER A QUESTION MARK (?)
WRITE
WRITENR ENTER JOBNAME, PANNAME, 'RESTORE', OR .KEYWORD(+):
GETJNAME: READ &JOBNAME
SET  &L=&LENGTH(&STR(&JOBNAME))
IF   &L=0                               THEN GOTO START
IF   &DATATYPE(&SUBSTR(1,&JOBNAME))=NUM THEN DO
WRITE
WRITE THE JOBNAME/PANNAME REQUESTED MUST BE A VALID PANSPOOL NAME
WRITE BEGINNING WITH AN ALPHABETIC CHARACTER
WRITE PLEASE RESPECIFY FOR JOBNAME, PANNAME, 'RESTORE', OR .KEYWORD
WRITE
GOTO GETJNAME
END
IF   &JOBNAME=RESTORE                   THEN DO
ERROR  GOTO ABORT
ATTN   GOTO ABORT
GETRSTR: WRITE
WRITE  DO YOU WISH TO RESTORE A PANSPOOL MEMBER FOR THE CURRENT MONTH?
WRITENR REPLY 'Y' FOR YES, OR 'N' FOR NO ===>
READ &REPLY
IF   &SUBSTR(1,&REPLY)=N                THEN DO
WRITE
WRITE RESTORATION OF A PANSPOOL MEMBER FOR OTHER THAN THE CURRENT
WRITE MONTH SHOULD BE REQUESTED THROUGH THE 'USER SUPPORT' GROUP AT
WRITE THE DATA CENTER (EXT. 7-1781 OR 7-0888)
WRITE
WRITE HIT ENTER TO CONTINUE ...
READ NOTHING
CLR3270
GOTO START
END
IF   &SUBSTR(1,&REPLY)^=Y               THEN DO
WRITE
WRITE PLEASE SPECIFY EITHER YES OR NO FOR YOUR REPLY
GOTO GETRSTR
END
CLR3270
E 'SYS2.PANSPOOL.CNTL(JCLREST)' CNTL OLD
WRITE *----------------------------------------------------------------*
WRITE ×    ===> PANSPOOL:  JOB/JCL RETRIEVAL FROM ARCHIVE TAPE <===    ×
WRITE *----------------------------------------------------------------*
WRITE
WRITE -> ENTER THE 10-CHARACTER, PANSPOOL NAME(S) OF THE JOB(S) YOU
WRITE    WISH TO RETRIEVE, ONE NAME PER LINE, IN ASCENDING, COLLATING
WRITE    SEQUENCE.  RETRIEVED LISTINGS WILL BE PLACED ON PROD.TAPELIST.
WRITE
WRITE -> NOTE:  IF AN OUT-OF-SEQUENCE OCCURS, ONLY THE OUT-OF-SEQUENCE
WRITE    STATEMENTS WILL BE BYPASSED AND FLAGGED.  PROCESSING WILL
WRITE    CONTINUE IF THE SEQUENCE CAN BE RESTORED.  A LISTING WILL BE
WRITE    PRODUCED TO INDICATE ANY DATASETS THAT COULD NOT BE RESTORED
WRITE    AND ANY DATASETS SPECIFICALLY NAMED FOR RESTORE.  THE USER
WRITE    SUBMITTING THE RESTORE REQUEST WILL BE NOTIFIED VIA TSO OF
WRITE    THE RESTORE JOB'S COMPLETION.
WRITE
WRITE -> HIT 'ENTER' TO TERMINATE INPUT AFTER ENTERING LAST NAME
WRITE
C * 2 /?SYSUID/&SYSUID/ ALL
I NOPROMPT
TOP
F /SYSIN/ 3
CONTROL NOMSG
DOWN
C * 99999 //++RESTORE / ALL
CONTROL MSG
99999 &STR(/*)
SUBMIT
GOTO   EOEDIT
ABORT: WRITE -
× WARNING:  ---> ARCHIVAL-TAPE RETRIEVAL REQUEST HAS BEEN ABORTED <--- ×
EOEDIT: DATA
END N
ENDDATA
GOTO START
END
IF   &SUBSTR(1,&JOBNAME)=. THEN DO
IF   &L<2                         THEN SET &PTYPE=USER(0,1)
ELSE IF &SUBSTR(2,&JOBNAME)=F     THEN SET &PTYPE=USER(1)
ELSE IF &SUBSTR(2,&JOBNAME)=A     THEN SET &PTYPE=USER(0,1)
ELSE IF &SUBSTR(2,&JOBNAME)=N     THEN DO
SET  &PTYPE=STATUS(ACTIVE)
IF   &OLDFLAG=1 THEN DO
SET  &DDNAME=JCLLIST
SET  &OLDFLAG=0
SET  &CTYPE=STATUS(INACTIVE)
WRITE
WRITE ===> MODE SWITCH:  NOW USING PROD.PANSPOOL IN '.NEW' MODE <===
END
END
ELSE IF &SUBSTR(2,&JOBNAME)=O THEN DO
SET   &DDNAME=TAPELIST
SET   &PTYPE=USER(0,1)
SET   &CTYPE=STATUS(DISABLE)
SET   &OLDFLAG=1
WRITE
WRITE ===> MODE SWITCH:  NOW USING PROD.TAPELIST IN '.OLD' MODE <===
END
ELSE IF &SUBSTR(2,&JOBNAME)=E THEN GOTO EOJ
ELSE DO
WRITE
WRITE INVALID KEYWORD '&JOBNAME'
GOTO START
END
WRITE
WRITE HIT ENTER TO CONTINUE ...
READ NOTHING
CLR3270
WRITE *----------------------------------------------------------------*
WRITE ×    ===> PANSPOOL:  PAN 'PRINT' PROCESSING IN PROGRESS <===     ×
WRITE *----------------------------------------------------------------*
/* DEPRESS THE 'ENTER' KEY TO INITIATE A PAN/TSO 'PRINT' OPERATION: */
/* READ */
WRITE
PAN PRINT &PTYPE CON(0) COM DDNAME(&DDNAME)
GOTO START
END
IF &JOBNAME=? THEN DO
CLR3270
WRITE *----------------------------------------------------------------*
WRITE ×       ===> PANSPOOL/JCLPRINT CLIST HELP INFORMATION <===       ×
WRITE *----------------------------------------------------------------*
WRITE
WRITE ENTER ONE OF THE FOLLOWING:
WRITE
WRITE  -> NAME OF REQUIRED JOB (8 OR LESS CHAR., 1ST NOT NUMERIC),  OR
WRITE
WRITE  -> PANSPOOL NAME OF REQUIRED JOB (10 CHAR., LAST 4 NUMERIC), OR
WRITE
WRITE  -> 'RESTORE' FOR JOB RETRIEVAL                               OR
WRITE
WRITE *----------------------------------------*
WRITE ×  ONE OF THE FOLLOWING VALID KEY WORDS: ×
WRITE *----------------------------------------*
WRITE
WRITE  ->  '.NEW'  - TO LIST ALL JOBS NOT YET EXAMINED (PANSPOOL)
WRITE  ->  '.OLD'  - TO LIST ALL JOBS PREVIOUSLY RESTORED FROM TAPE
WRITE  ->  '.ALL'  - TO LIST ALL JOBS (PANSPOOL OR TAPELIST DEPENDING
WRITE                ON MODE OF CLIST OPERATION)
WRITE  ->  '.FAIL' - TO LIST ALL JOBS THAT HAD PRINTED (PANSPOOL OR
WRITE                TAPELIST)
WRITENR  ->  '.END'  - TO TERMINATE AND EXIT THE JCLPRINT CLIST:
GOTO GETJNAME
END
IF  &L=10 THEN DO
IF  &DATATYPE(&SUBSTR(7:10,&JOBNAME))^=NUM THEN DO
WRITENR JOBNAME ENTERED TOO LONG OR INVALID PANNAME - REENTER(+):
GOTO GETJNAME
END
SET &PANNAME=&JOBNAME
GOTO JNUMOK
END
IF  &L>8 THEN DO
WRITENR JOBNAME ENTERED WAS LONGER THAN 8 CHARACTERS - REENTER(+):
GOTO GETJNAME
END
SET &OJNAME=&JOBNAME
IF  &L<6                  THEN SET &JOBNAME=&JOBNAME.0000000
SET &L=&LENGTH(&JOBNAME)
IF  &L>6                  THEN SET &JOBNAME=&SUBSTR(1:6,&JOBNAME)
SET &I=1
DO WHILE &I<=6
SET &JCHAR=&SUBSTR(&I,&JOBNAME)
IF  &JCHAR>=A THEN GOTO EOL
SET &J=1
DO WHILE &J<=3
IF  &JCHAR=&SUBSTR(&J,#@$) THEN DO
IF  &I>1 THEN SET &PART1=&SUBSTR(1:&I-1,&JOBNAME)
ELSE SET &PART1=
IF  &I<6 THEN SET &PART2=&SUBSTR(&I+1:6,&JOBNAME)
ELSE SET &PART2=
SET &JOBNAME=&PART1&SUBSTR(&J,NAD)&PART2
GOTO EOL
END
SET &J=&J+1
END
EOL: SET &I=&I+1
END
JNUM: CLR3270
WRITE *----------------------------------------------------------------*
WRITE ×    ===> PANSPOOL:  PAN 'PRINT' PROCESSING IN PROGRESS <===     ×
WRITE *----------------------------------------------------------------*
/* DEPRESS THE 'ENTER' KEY TO INITIATE A PAN/TSO 'PRINT' OPERATION: */
/* READ */
WRITE
PAN PRINT N(&JOBNAME) CON(0) COM DDNAME(&DDNAME)
WRITE
WRITENR ENTER THE 4-DIGIT JES2 JOB NO. OF THE PANSPOOL JOB DESIRED(+):
GETJNUM: READ &JOBNUMBER
IF  &JOBNUMBER=? THEN DO
WRITE
WRITE DID YOU JUST RECEIVE A '0 DATASET(S) SELECTED' MESSAGE?
WRITENR REPLY 'Y' FOR YES, OR 'N' FOR NO ===>
READ &REPLY
IF   &SUBSTR(1,&REPLY)=Y THEN DO
WRITE
WRITE NO ENTRIES WERE FOUND FOR JOB &OJNAME.
GOTO START
END
WRITE
WRITE FROM THE FOLLOWING LIST DETERMINE THE ENTRY CORRESPONDING TO THE
WRITE JOB YOU WISH TO EXAMINE AND ENTER THE LAST FOUR DIGITS OF THE
WRITE PANSPOOL JOB (USUALLY THE SAME AS THE JOB NUMBER).
WRITE
WRITE YOU MAY ALSO ENTER '.END' TO RESTART THE JCLPRINT PROCESS.
WRITE
WRITE HIT ENTER TO CONTINUE ...
READ NOTHING
GOTO JNUM
END
SET &L=&LENGTH(&STR(&JOBNUMBER))
IF  &L=0                           THEN GOTO JNUMBAD
IF  &SUBSTR(1,&JOBNUMBER)=.        THEN GOTO START
IF  &DATATYPE(&JOBNUMBER)^=NUM     THEN GOTO JNUMBAD
IF  &JOBNUMBER<1                   THEN GOTO JNUMBAD
IF  &L=4                           THEN GOTO JNUMOK
IF  &L<4                           THEN DO
JOBNUMBER=&SUBSTR(1:4-&L,000)&JOBNUMBER
WRITE ASSUMING LEADING ZERO(S) - JOBNUMBER=&JOBNUMBER
GOTO JNUMOK
END
JNUMBAD: WRITENR JOBNUMBER &JOBNUMBER INVALID - REENTER(+):
GOTO GETJNUM
JNUMOK: ERROR OFF
SET &PANNAME=&JOBNAME&JOBNUMBER
/* SET &TSONAME=&SUBSTR(1:8,&PANNAME) */
ATTN GOTO EODS
PAN RETRIEVE &PANNAME DDNAME(&DDNAME) DA(PANTEMP.DATA)
IF  &LASTCC^=0 THEN GOTO START
ERROR GOTO EODS
SET &PRTFLAG=0
TSOHARD: WRITE
WRITENR DO YOU WANT A HARD COPY LISTING OF THE JOB(+)?
READ &REPLY
WRITE
IF  &LENGTH(&REPLY)=0   THEN GOTO WHAT
IF  &REPLY=?            THEN DO
IF  &PRTFLAG=0          THEN +
WRITE ENTER 'NO' IF YOU WISH TO EXAMINE THE JOB ON THE SCREEN FIRST.
ELSE WRITE ENTER 'NO' IF YOU ARE FINISHED EXAMINING THIS JOB.
WRITE ENTER 'YES' TO PRINT JOB ON LOCAL PRINTER, OR ENTER A
WRITENR REMOTE NUMBER WHERE THE PRINTOUT IS TO BE ROUTED ===>
READ &REPLY
WRITE
END
IF  &DATATYPE(&REPLY)=NUM THEN DO
SET &RMT=&REPLY
SET &REPLY=Y
END
ELSE SET &RMT=0
IF  &SUBSTR(1,&REPLY)=N THEN DO
IF  &PRTFLAG=1 THEN GOTO EODS
CLR3270
WRITE *----------------------------------------------------------------*
WRITE ×    ===> PANSPOOL:  PAN 'PRINT' PROCESSING IN PROGRESS <===     ×
WRITE *----------------------------------------------------------------*
/* DEPRESS THE 'ENTER' KEY TO INITIATE TSO 'LIST' PROCESSING: */
/* READ */
/* LIST &TSONAME..DATA 2 99999 NONUM COL(2:80) */
LIST PANTEMP.DATA 2 99999 NONUM COL(2:80)
SET &PRTFLAG=1
ERROR OFF
PAN CHANGE &PANNAME &CTYPE CON(0) DDNAME(&DDNAME)
IF  &LASTCC^=0 THEN WRITE *** IGNORE ABOVE MESSAGE (PV028) ***
ERROR GOTO EODS
GOTO  TSOHARD
END
IF  &SUBSTR(1,&REPLY)=Y THEN DO
CONTROL NOMSG
ERROR   OFF
FREE    FI(/* PSPLIN */ PSPLOUT)
CONTROL MSG
ERROR   GOTO EOHCOPY
ATTN    GOTO EOHCOPY
/* ALLOC DD(PSPLIN) DS(&TSONAME..DATA) */
ALLOC   FI(PSPLOUT) SYSOUT(I) DEST(R&RMT)   /* CHANGED SYSOUT=X TO I */
CALL 'SYS2.LINKLIB(PANPRT)'                 /*  HONEYWELL TO IMPACT  */
EOHCOPY: ERROR OFF                          /*  PRINTER WJS-23MAY79  */
FREE    FI(PSPLOUT)
IF  &PRTFLAG=0 THEN PAN CHANGE &PANNAME &CTYPE CON(0) DDNAME(&DDNAME)
IF  &LASTCC^=0 THEN WRITE *** IGNORE ABOVE MESSAGE (PV028) ***
GOTO    EODS
END
WHAT: WRITE '&REPLY' INVALID. ONLY 'YES', 'NO', OR RMT NUMBER ACCEPTABLE
GOTO    TSOHARD
EODS: CONTROL NOMSG
ERROR OFF
/* DELETE &TSONAME..DATA */
CONTROL MSG
GOTO START
EOJ: CONTROL NOMSG
ERROR OFF
FREE FI(JCLLIST TAPELIST)
DELETE PANTEMP.DATA
EXIT
