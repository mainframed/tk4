VSM00300 CSECT
         TITLE 'VSM00300 => CSA PROTECT KEY MONITOR'
***********************************************************************
*                                                                     *
*    SYSTEM -           VIRTUAL STORAGE MONITOR                       *
*                                                                     *
*    VERSION -          1.1                                           *
*                                                                     *
*    MODNAME -          VSM00300                                      *
*                                                                     *
*    AUTHOR -           R.P. SHANNON                                  *
*                       HALLMARK CARDS                                *
*                       ENFIELD DISTRIBUTION CENTER                   *
*                       ENFIELD CT.                                   *
*                                                                     *
*    DATE -             9/1/82                                        *
*                                                                     *
*    FUNCTION -         SHOW CSA SUBPOOL UTILIZATION BY PROTECT KEY   *
*                                                                     *
*    ATTRIBUTES -       REUS                                          *
*                                                                     *
*    REGS AT INPUT-     R1   - INTER MODULE LINKAGE                   *
*                       R14  - RETURN ADDRESS                         *
*                       R15  - EPA                                    *
*                                                                     *
*    REG USAGE     -    R0-R7 - WORK REGS                             *
*                       R8   - GDA POINTER                            *
*                       R9   - INTERNAL BAL REG                      *
*                       R10  - LINKAGE AREA POINTER                  *
*                       R11  - BASE REG                               *
*                       R13  - SAVEAREA POINTER                       *
*                                                                     *
*    REGS AT EXIT -     R14  - RETURN ADDRESS                         *
*                       R15  - RETURN CODE                            *
*                                                                     *
*    LABELS -           SOME OF THE FIELD USAGE IN THIS MODULE CAN    *
*                       BE DETERMINED BY THE FIRST CHARACTED OF THE   *
*                       FIELD. THE KEYS ARE:                          *
*                            @ - FIELD IS IN INTER MODULE LINKAGE     *
*                                AREA                                 *
*                            # - FIELD IS PART OF THE OUTPUT SCREEN   *
*                            $ - DSECT                                *
*                                                                     *
*    MACROS -           $DQE       => RYO DQESECT                     *
*                       $GDA       => RYO GDA                         *
*                       $LNKSECT   => INTER MODULE LINKAGE DSCET      *
*                       $SPQE      => RYO SPQESECT                    *
*                       HMAP       => SCREEN BUILDER                  *
*                       PKEY       => AUTHORIZATION MACRO             *
*                                                                     *
*    MODS -                                                           *
*          VERSION 1.0  - BASE                                        *
*                                                                     *
*          VERSION 1.1  - 1. REMOVED AUTOMATIC UPSHIFT OF KEYBOARDS   *
*                            FOR TERMINALS WITH NUM OPTION            *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                  INTER-MODULE LINKAGE DSECT
R11      EQU   11                  BASE REG
R12      EQU   12
R13      EQU   13                  GETMAINED ADDRESSABILITY
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  EPA/RC
         EJECT
***********************************************************************
*        STANDARD OS LINKAGE                                          *
***********************************************************************
         SPACE
         PRINT NOGEN
         SPACE
         SAVE  (14,12),,VSM00300+&SYSDATE&SYSTIME  SAVE CALLER'S REGS
         LR    R11,R15             LOAD EPA
         USING VSM00300,R11        GET ADDRESSABILITY
         LA    R2,SAVEAREA         CREATE SAVE AREA CHAIN
         ST    R2,8(R13)           FORWARD CHAIN POINTER
         ST    R13,SAVEAREA+4      BACKWARD CHAIN POINTER
         LR    R13,R2              POINT TO MY SAVEAREA
         ST    R13,8(R2)           FORWARD CHAIN POINTER
         SPACE
***********************************************************************
*        FIND ALL NECESSARY ADDRESSES AND POINTERS                    *
***********************************************************************
P000100  DS    0H
         LR    R10,R1              POINT TO LINKAGE AREA
         USING $LNKSECT,R10        DSECT ADDRESSABILITY
         SPACE
         SR    R0,R0               CLEAR R0
         SR    R2,R2               CLEAR R2
         SR    R3,R3               CLEAR R3
         SR    R4,R4               CLEAR R4
         SR    R5,R5               CLEAR R5
         SPACE
         LA    R2,CLRAREA          CLEAR
         LA    R3,CLEARLEN         WORK FIELDS
         LR    R4,R2               FIELDS
         LA    R5,0                WITH
         MVCL  R2,R4               ZEROS
         SPACE
         L     R8,@GDA             GDA POINTER
         USING $GDA,R8             GDA DSECT ADDRESSABILITY
         SPACE
***********************************************************************
*             MAINLINE PROCESSING                                     *
***********************************************************************
P000200  DS    0H
*        PKEY  S                   GET INTO KEY ZERO         CBT-AXC
         SR    1,1                 GET INTO CBT KEY ZERO     CBT-AXC
         SVC   254                 GET INTO CBT KEY ZERO     CBT-AXC
         MODESET KEY=ZERO,MODE=SUP KEY ZERO SUPERVISOR STATE CBT-AXC
         L     R3,CSASPQEP         CSA SPQE POINTER
         BAL   R9,SPQELOOP         PERFORM SUBPOOL ANALYSIS
*        PKEY  R                   GET OUT OF KEY ZERO       CBT-AXC
         LA    1,4                 GET OUT OF CBT KEY ZERO   CBT-AXC
         SVC   254                 GET OUT OF CBT KEY ZERO   CBT-AXC
 MODESET KEY=NZERO,MODE=PROB ORIG PROTECT KEY, PROBLEM STATE CBT-AXC
         SPACE
P000300  DS    0H
         MVC   TOTCSA,ZERO         MOVE ZERO TO CSA TOTAL
         MVC   TOT227,ZERO         MOVE ZERO TO SP227 TOTAL
         MVC   TOT228,ZERO         MOVE ZERO TO SP228 TOTAL
         MVC   TOT231,ZERO         MOVE ZERO TO SP231 TOTAL
         MVC   TOT239,ZERO         MOVE ZERO TO SP239 TOTAL
         MVC   TOT241,ZERO         MOVE ZERO TO SP241 TOTAL
         SPACE
         LA    R6,XPWORK           POINT TO THE XPOZ WORKAREA
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         SPACE
         MVC   #00CSA,#TOTCSA
         MVC   #00SP227,#TOT227
         MVC   #00SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #00SP231,#TOT231
         MVC   #00SP239,#TOT239
         MVC   #00SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORKAREA
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #01CSA,#TOTCSA
         MVC   #01SP227,#TOT227
         MVC   #01SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #01SP231,#TOT231
         MVC   #01SP239,#TOT239
         MVC   #01SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORKAREA
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #02CSA,#TOTCSA
         MVC   #02SP227,#TOT227
         MVC   #02SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #02SP231,#TOT231
         MVC   #02SP239,#TOT239
         MVC   #02SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #03CSA,#TOTCSA
         MVC   #03SP227,#TOT227
         MVC   #03SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #03SP231,#TOT231
         MVC   #03SP239,#TOT239
         MVC   #03SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #04CSA,#TOTCSA
         MVC   #04SP227,#TOT227
         MVC   #04SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #04SP231,#TOT231
         MVC   #04SP239,#TOT239
         MVC   #04SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #05CSA,#TOTCSA
         MVC   #05SP227,#TOT227
         MVC   #05SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #05SP231,#TOT231
         MVC   #05SP239,#TOT239
         MVC   #05SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #06CSA,#TOTCSA
         MVC   #06SP227,#TOT227
         MVC   #06SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #06SP231,#TOT231
         MVC   #06SP239,#TOT239
         MVC   #06SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #07CSA,#TOTCSA
         MVC   #07SP227,#TOT227
         MVC   #07SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #07SP231,#TOT231
         MVC   #07SP239,#TOT239
         MVC   #07SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT DATA FOR OUTPUT
         MVC   #08CSA,#TOTCSA
         MVC   #08SP227,#TOT227
         MVC   #08SP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #08SP231,#TOT231
         MVC   #08SP239,#TOT239
         MVC   #08SP241,#TOT241
         SPACE
         LA    R6,20(R6)           OFFSET INTO XPWORK
         BAL   R9,FORMAT           GO FORMAT V=R DATA FOR OUTPUT
         MVC   #VRCSA,#TOTCSA
         MVC   #VRSP227,#TOT227
         MVC   #VRSP228,#TOT228    MOVE OUTPUT TO CORRECT LINE
         MVC   #VRSP231,#TOT231
         MVC   #VRSP239,#TOT239
         MVC   #VRSP241,#TOT241
         EJECT
***********************************************************************
*        COMPUTE TOTAL CSA USAGE FOR ALL PROTECT KEYS                 *
***********************************************************************
P000400  DS    0H
         L     R3,TOT227           LOAD TOTAL SP227 SIZE INTO R3
         LR    R0,R3               ADD TO CSA TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT227,EDIT1       MOVE IN PATTERN
         ED    #TOT227,DBLEWORD+5  EDIT
         SPACE
         L     R3,TOT228           LOAD TOTAL SP228 SIZE INTO R3
         AR    R0,R3               ADD TO CSA TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT228,EDIT1       MOVE IN PATTERN
         ED    #TOT228,DBLEWORD+5  EDIT
         SPACE
         L     R3,TOT231           LOAD TOTAL SP231 SIZE INTO R3
         AR    R0,R3               ADD TO CSA TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT231,EDIT1       MOVE IN PATTERN
         ED    #TOT231,DBLEWORD+5  EDIT
         SPACE
         L     R3,TOT239           LOAD TOTAL SP239 SIZE INTO R3
         AR    R0,R3               ADD TO CSA TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT239,EDIT1       MOVE IN PATTERN
         ED    #TOT239,DBLEWORD+5  EDIT
         SPACE
         L     R3,TOT241           LOAD TOTAL SP241 SIZE INTO R3
         AR    R0,R3               ADD TO CSA TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT241,EDIT1       MOVE IN PATTERN
         ED    #TOT241,DBLEWORD+5  EDIT
         SPACE
         LR    R3,R0               LOAD TOTAL CSA SIZE INTO R3
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOTCSA,EDIT1       MOVE IN PATTERN
         ED    #TOTCSA,DBLEWORD+5  EDIT
         EJECT
***********************************************************************
*        OUTPUT THE SCREEN                                            *
***********************************************************************
P000500  DS    0H
         MVI   @RSPONSE,C' '         MOVE BLANK TO RESPONSE
         LA    R0,#SCREEN            LOAD SCREEN ADDR INTO R0
         ST    R0,@PUTAREA           STORE IN LINKAGE AREA
         LA    R0,#SCRNLEN           LOAD SCREEN LENGTH INTO R0
         ST    R0,@PUTLEN            STORE LENGTH IN LINKAGE AREA
         LR    R1,R10                POINT R1 TO LINKAGE AREA
         L     R15,@IOMOD            LOAD ADDR OF TERMINAL I/O MODULE
         BALR  R14,R15               GO DO TERMINAL I/O
         CLI   @RSPONSE,C'R'         REFRESH SCREEN ?
         BE    P000100               BIF YES
         SPACE 3
P999999  DS   0H
         L    R13,SAVEAREA+4         PREPARE TO BOOGIE
         RETURN (14,12)              B O O G I E
         EJECT
**********************************************************************
*                                                                    *
*       => SPQELOOP <=                                               *
*                                                                    *
*       COMPUTE SUB-POOL SIZE BY FOLLOWING THE DQE CHAINS            *
*                                                                    *
*                                                                    *
*       LOOP STRUCTURE FOR THE FOLLOWING CODE:                       *
*                                                                    *
*        => SPQELOOP                                                 *
*       ×                                                            *
*       ×       => DQELOOP                                           *
*       ×      ×                                                     *
*       ×      ×                                                     *
*       ×       => DQEEND                                            *
*       ×                                                            *
*        => SPQEEND                                                  *
*                                                                    *
*                                                                    *
*       REGISTER USAGE FOR THIS SUBROUTINE:                          *
*                                                                    *
*       R0 =>   TEMPORARY WORK REGISTER                              *
*       R1 =>   SUB-POOL SIZE ACCUMULATOR;CLEARED BEFORE ENTRY       *
*       R2 =>   SUB-POOL FREE SPACE ACCUMULATOR;CLEARED BEFORE ENTRY *
*       R3 =>   SPQE POINTER                                         *
*       R4 =>   FQE POINTER                                          *
*       R5 =>   FQE  POINTER                                         *
*       R6 =>   SUBPOOL INFO AREA POINTER                            *
*       R9 =>   RETURN ADDRESS                                       *
*                                                                    *
**********************************************************************
SPQELOOP DS    0H
         LA    R3,0(R3)            CLEAR HIGH ORDER BYTE
         LTR   R3,R3               ANY (MORE) SPQE'S ?
         BZ    ENDSPQE             BIF NO
         USING $SPQE,R3            SPQE DSECT ADDRESSABILITY
         SR    R5,R5               CLEAR R5
         SR    R6,R6               CLEAR R6
         SR    R7,R7               CLEAR R7
         IC    R5,SPQEKEY          LOAD PROTECT KEY INTO R5
         TM    SPQEKEY,X'80'       KEY 0 - 7 ?
         BZ    SPQE01              BIF YES
         TM    SPQEKEY,X'70'       KEY 8 ?
         BZ    SPQE01              BIF YES
         MVC   HOLDKEY,SPQEKEY     MOVE KEY TO WORK FIELD
         OI    HOLDKEY,X'90'       TREAT ALL V=R KEYS AS KEY 9 SO
*                                  THAT 1 WORK AREA IS USED FOR ALL
         IC    R5,HOLDKEY          LOAD PROTECT KEY INTO R5
         SPACE
SPQE01   LA    R7,20(R7)           LOAD R6 WITH OFFSET
         SRA   R5,4                SHIFT PROTECT KEY RIGHT 1 NIBBLE
         MR    R6,R5               COMPUTE OFFSET INTO XP WORK AREA
         LR    R6,R7               STORE RESULT IN R6
         LA    R7,XPWORK           LOAD ADDRESS OF XP WORK
         LA    R7,0(R7)            CLEAR HI-ORDER
         AR    R6,R7               POINT TO WHERE WE WANT TO BE
         CLC   SPQEID,SP239        SUBPOOL 239 ?
         BE    SPQE239             BRANCH IF YES
         BH    SPQE241             BRANCH IF HIGH
         CLC   SPQEID,SP228        SUBPOOL 228 ?
         BE    SPQE228             BRANCH IF YES
         BH    SPQE231             BRANCH IF HIGH
         SPACE
SPQE227  DS    0H
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE FOR SP 227
         B     SPQEREST            KEEP GOING
         SPACE
SPQE228  DS    0H
         LA    R6,4(R6)            OFFSET FOR SUB-POOL 228
         B     SPQEREST            KEEP GOING
         SPACE
SPQE231  DS    0H
         LA    R6,8(R6)            OFFSET FOR SUB-POOL 231
         B     SPQEREST            KEEP GOING
         SPACE
SPQE239  DS    0H
         LA    R6,12(R6)           OFFSET FOR SUBPOOL 239
         B     SPQEREST            KEEP GOING
         SPACE
SPQE241  DS    0H
         LA    R6,16(R6)           OFFSET FOR SUBPOOL 241
         B     SPQEREST            CONTINUE
         SPACE
SPQEREST DS    0H
         L     R4,SPDQEPTR         LOAD DQE ADDR INTO R4
         L     R1,0(R6)            LOAD ACCUMULATION INTO R1
         SPACE
DQELOOP  DS    0H
         LTR   R4,R4               ANY (MORE) DQE'S ?
         BZ    ENDDQE              BRANCH IF NO
         USING $DQE,R4             CSA DQE ADDRESSABILITY
         A     R1,DQELNTH          ACCUMULATE SUB-POOL LENGTH
         L     R0,DQEPTR           LOAD POINTER TO NEXT DQE
         DROP  R4                  DROP DQE DSECT ADDRESSABILITY
         LR    R4,R0               LOAD NEXT DQE ADDR INTO R4
         B     DQELOOP             GO DO DQE LOOP AGAIN
         SPACE
ENDDQE   DS    0H
         ST    R1,0(R6)            STORE SUB-POOL SIZE
         SR    R1,R1               CLEAR R1
         TM    SPQEFLGS,LASTSPQE   LAST SPQE ON THE CHAIN ?
         BO    ENDSPQE             BRANCH IF YES
         L     R0,SPQEAD           LOAD POINTER TO NEXT SPQE
         DROP  R3                  DROP SPQE DSECT ADDRESSABILITY
         LR    R3,R0               LOAD NEXT SPQE ADDR INTO R3
         B     SPQELOOP            GO DO SPQE LOOP AGAIN
         SPACE
ENDSPQE  DS    0H
         BR    R9                  RETURN
         EJECT
***********************************************************************
*        FORMAT THE DATA FOR OUTPUT                                   *
***********************************************************************
FORMAT   DS    0H
         ST    R9,SAVEREG9         SAVE RETURN ADDRESS
         USING $WRKSECT,R6         DSECT ADDRESSABILITY
         SPACE
         SR    R1,R1               CLEAR R1
         L     R0,TOT227           LOAD TOTAL SP227 SIZE INTO R0
         L     R3,WRK227           LOAD PROTECT KEY SP227 SIZE INTO R3
         AR    R0,R3               ADD TO SP 227 TOTAL
         AR    R1,R3               ADD TO PROTECT KEY TOTAL
         ST    R0,TOT227           STORE SP 227 TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT227,EDIT1       MOVE IN PATTERN
         ED    #TOT227,DBLEWORD+5  EDIT
         SPACE
         L     R0,TOT228           LOAD TOTAL SP228 SIZE INTO R0
         L     R3,WRK228           LOAD PROTECT KEY SP228 SIZE INTO R3
         AR    R0,R3               ADD TO SP228 TOTAL
         AR    R1,R3               ADD TO PROTECT KEY TOTAL
         ST    R0,TOT228           STORE SP228 TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT228,EDIT1       MOVE IN PATTERN
         ED    #TOT228,DBLEWORD+5  EDIT
         SPACE
         L     R0,TOT231           LOAD TOTAL SP231 SIZE INTO R0
         L     R3,WRK231           LOAD PROTECT KEY SP231 SIZE INTO R3
         AR    R0,R3               ADD TO SP231 TOTAL
         AR    R1,R3               ADD TO PROTECT KEY TOTAL
         ST    R0,TOT231           STORE SP231 TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT231,EDIT1       MOVE IN PATTERN
         ED    #TOT231,DBLEWORD+5  EDIT
         SPACE
         L     R0,TOT239           LOAD TOTAL SP239 SIZE INTO R0
         L     R3,WRK239           LOAD PROTECT KEY SP239 SIZE INTO R3
         AR    R0,R3               ADD TO SP239 TOTAL
         AR    R1,R3               ADD TO PROTECT KEY TOTAL
         ST    R0,TOT239           STORE SP239 TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT239,EDIT1       MOVE IN PATTERN
         ED    #TOT239,DBLEWORD+5  EDIT
         SPACE
         L     R3,WRK241           LOAD SP241 SIZE INTO R3
         L     R0,TOT241           LOAD TOTAL SP239 SIZE INTO R0
         AR    R0,R3               ADD TO SP239 TOTAL
         AR    R1,R3               ADD TO PROTECT KEY TOTAL
         ST    R0,TOT241           STORE TOTAL
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOT241,EDIT1       MOVE IN PATTERN
         ED    #TOT241,DBLEWORD+5  EDIT
         SPACE
         LR    R3,R1               LOAD CSA SIZE INTO R3
         BAL   R9,FORMAT2          GO COMPUTE K
         MVC   #TOTCSA,EDIT1       MOVE IN PATTERN
         ED    #TOTCSA,DBLEWORD+5  EDIT
         SPACE
         DROP  R6                  DROP DSECT ADDRESSABILITY
         L     R9,SAVEREG9         LOAD MAINLINE RETURN ADDRESS
         BR    R9                  RETURN
         SPACE
FORMAT2  DS    0H
         SR    R2,R2               CLEAR R2
         SR    R4,R4               CLEAR R4
         SRA   R3,10               DIVIDE SIZE BY 1024
         CVD   R3,DBLEWORD         CONVERT TO DECIMAL
         BR    R9
         EJECT
SAVEAREA DS   18F                 MY SAVE AREA
CLRAREA  EQU  *
SAVEREG9 DS   F
DBLEWORD DS   D                   DOUBLE WORD WORK AREA
TOTCSA   DS   F
TOT227   DS   F
TOT228   DS   F
TOT231   DS   F
TOT239   DS   F
TOT241   DS   F
XPWORK   DS   CL448
CLEARLEN EQU  *-CLRAREA
TEN      DC   X'0000000A'         TEN IN HEX
ONEMEG   DC   X'00100000'         ONE MEGABYTE
ZERO     DC   F'0'
EDIT1    DC   X'402020202120'     EDIT PATTERN FOR SIZE IN K
HOLDKEY  DC   X'00'               HOLD PROTECT KEY
SP228    DC   X'E4'               SUBPOOL 228
SP239    DC   X'EF'               SUBPOOL 239
SP241    DC   X'F1'               SUBPOOL 241
         DS   0F
#SCREEN  DS   0CL1639
         DC   X'C31140403C404000'
         HMAP R=1,C=1,A=(ASKIP,BRT)
         DC   C'SELECT OPTION => '
         HMAP R=1,C=18,A=(UNPROT,FSET,IC)
         DS   CL1
         HMAP R=3,C=25,A=(ASKIP,BRT)
         DC   C'CSA UTILIZATION BY PROTECT KEY'
         HMAP R=5,C=4,A=(ASKIP,NORM)
         DC   C'KEY'
         HMAP R=5,C=15,A=(ASKIP,NORM)
         DC   C'NAME'
         HMAP R=5,C=26,A=(ASKIP,NORM)
         DC   C'CSA'
         HMAP R=5,C=34,A=(ASKIP,NORM)
         DC   C'SP 227'
         HMAP R=5,C=43,A=(ASKIP,NORM)
         DC   C'SP 228'
         HMAP R=5,C=52,A=(ASKIP,NORM)
         DC   C'SP 231'
         HMAP R=5,C=61,A=(ASKIP,NORM)
         DC   C'SP 239'
         HMAP R=5,C=70,A=(ASKIP,NORM)
         DC   C'SP 241'
         SPACE
         HMAP R=6,C=4,A=(ASKIP,NORM)
#00KEY   DC   CL4' 0  '
         HMAP R=6,C=12,A=(ASKIP,NORM)
#00NAME  DC   C'SUPERVISOR'
         HMAP R=6,C=23,A=(ASKIP,NORM)
#00CSA   DS   CL6
         HMAP R=6,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=6,C=32,A=(ASKIP,NORM)
#00SP227 DS   CL6
         HMAP R=6,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=6,C=41,A=(ASKIP,NORM)
#00SP228 DS   CL6
         HMAP R=6,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=6,C=50,A=(ASKIP,NORM)
#00SP231 DS   CL6
         HMAP R=6,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=6,C=59,A=(ASKIP,NORM)
#00SP239 DS   CL6
         HMAP R=6,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=6,C=68,A=(ASKIP,NORM)
#00SP241 DS   CL6
         HMAP R=6,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=7,C=4,A=(ASKIP,NORM)
#01KEY   DC   CL4' 1  '
         HMAP R=7,C=12,A=(ASKIP,NORM)
#01NAME  DC   C'SCHDLR/JES'
         HMAP R=7,C=23,A=(ASKIP,NORM)
#01CSA   DS   CL6
         HMAP R=7,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=7,C=32,A=(ASKIP,NORM)
#01SP227 DS   CL6
         HMAP R=7,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=7,C=41,A=(ASKIP,NORM)
#01SP228 DS   CL6
         HMAP R=7,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=7,C=50,A=(ASKIP,NORM)
#01SP231 DS   CL6
         HMAP R=7,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=7,C=59,A=(ASKIP,NORM)
#01SP239 DS   CL6
         HMAP R=7,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=7,C=68,A=(ASKIP,NORM)
#01SP241 DS   CL6
         HMAP R=7,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=8,C=4,A=(ASKIP,NORM)
#02KEY   DC   CL4' 2  '
         HMAP R=8,C=12,A=(ASKIP,NORM)
#02NAME  DC   C'          '
         HMAP R=8,C=23,A=(ASKIP,NORM)
#02CSA   DS   CL6
         HMAP R=8,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=32,A=(ASKIP,NORM)
#02SP227 DS   CL6
         HMAP R=8,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=41,A=(ASKIP,NORM)
#02SP228 DS   CL6
         HMAP R=8,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=50,A=(ASKIP,NORM)
#02SP231 DS   CL6
         HMAP R=8,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=59,A=(ASKIP,NORM)
#02SP239 DS   CL6
         HMAP R=8,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=8,C=68,A=(ASKIP,NORM)
#02SP241 DS   CL6
         HMAP R=8,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=9,C=4,A=(ASKIP,NORM)
#03KEY   DC   CL4' 3  '
         HMAP R=9,C=12,A=(ASKIP,NORM)
#03NAME  DC   C'          '
         HMAP R=9,C=23,A=(ASKIP,NORM)
#03CSA   DS   CL6
         HMAP R=9,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=32,A=(ASKIP,NORM)
#03SP227 DS   CL6
         HMAP R=9,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=41,A=(ASKIP,NORM)
#03SP228 DS   CL6
         HMAP R=9,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=50,A=(ASKIP,NORM)
#03SP231 DS   CL6
         HMAP R=9,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=59,A=(ASKIP,NORM)
#03SP239 DS   CL6
         HMAP R=9,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=9,C=68,A=(ASKIP,NORM)
#03SP241 DS   CL6
         HMAP R=9,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=10,C=4,A=(ASKIP,NORM)
#04KEY   DC   CL4' 4  '
         HMAP R=10,C=12,A=(ASKIP,NORM)
#04NAME  DC   C'          '
         HMAP R=10,C=23,A=(ASKIP,NORM)
#04CSA   DS   CL6
         HMAP R=10,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=10,C=32,A=(ASKIP,NORM)
#04SP227 DS   CL6
         HMAP R=10,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=10,C=41,A=(ASKIP,NORM)
#04SP228 DS   CL6
         HMAP R=10,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=10,C=50,A=(ASKIP,NORM)
#04SP231 DS   CL6
         HMAP R=10,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=10,C=59,A=(ASKIP,NORM)
#04SP239 DS   CL6
         HMAP R=10,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=10,C=68,A=(ASKIP,NORM)
#04SP241 DS CL6
         HMAP R=10,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=11,C=4,A=(ASKIP,NORM)
#05KEY   DC   CL4' 5  '
         HMAP R=11,C=12,A=(ASKIP,NORM)
#05NAME  DC   C'DATA MNGT '
         HMAP R=11,C=23,A=(ASKIP,NORM)
#05CSA   DS   CL6
         HMAP R=11,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=32,A=(ASKIP,NORM)
#05SP227 DS   CL6
         HMAP R=11,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=41,A=(ASKIP,NORM)
#05SP228 DS   CL6
         HMAP R=11,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=50,A=(ASKIP,NORM)
#05SP231 DS   CL6
         HMAP R=11,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=59,A=(ASKIP,NORM)
#05SP239 DS   CL6
         HMAP R=11,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=11,C=68,A=(ASKIP,NORM)
#05SP241 DS   CL6
         HMAP R=11,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=12,C=4,A=(ASKIP,NORM)
#06KEY   DC   CL4' 6  '
         HMAP R=12,C=12,A=(ASKIP,NORM)
*06NAME  DC   C'ACF/VCAM  '                      CBT-AXC
#06NAME  DC   C'TCAM/VTAM '                      CBT-AXC
         HMAP R=12,C=23,A=(ASKIP,NORM)
#06CSA   DS   CL6
         HMAP R=12,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=32,A=(ASKIP,NORM)
#06SP227 DS   CL6
         HMAP R=12,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=41,A=(ASKIP,NORM)
#06SP228 DS   CL6
         HMAP R=12,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=50,A=(ASKIP,NORM)
#06SP231 DS   CL6
         HMAP R=12,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=59,A=(ASKIP,NORM)
#06SP239 DS   CL6
         HMAP R=12,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=12,C=68,A=(ASKIP,NORM)
#06SP241 DS   CL6
         HMAP R=12,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=13,C=4,A=(ASKIP,NORM)
#07KEY   DC   CL4' 7  '
         HMAP R=13,C=12,A=(ASKIP,NORM)
#07NAME  DC   C'IMS       '                    CBT-AXC
         HMAP R=13,C=23,A=(ASKIP,NORM)
#07CSA   DS   CL6
         HMAP R=13,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=32,A=(ASKIP,NORM)
#07SP227 DS   CL6
         HMAP R=13,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=41,A=(ASKIP,NORM)
#07SP228 DS   CL6
         HMAP R=13,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=50,A=(ASKIP,NORM)
#07SP231 DS   CL6
         HMAP R=13,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=59,A=(ASKIP,NORM)
#07SP239 DS   CL6
         HMAP R=13,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=13,C=68,A=(ASKIP,NORM)
#07SP241 DS   CL6
         HMAP R=13,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=14,C=4,A=(ASKIP,NORM)
#08KEY   DC   CL4' 8  '
         HMAP R=14,C=12,A=(ASKIP,NORM)
#08NAME  DC   C'V=V JOBS  '
         HMAP R=14,C=23,A=(ASKIP,NORM)
#08CSA   DS   CL6
         HMAP R=14,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=32,A=(ASKIP,NORM)
#08SP227 DC   CL6' '
         HMAP R=14,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=41,A=(ASKIP,NORM)
#08SP228 DS   CL6
         HMAP R=14,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=50,A=(ASKIP,NORM)
#08SP231 DS   CL6
         HMAP R=14,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=59,A=(ASKIP,NORM)
#08SP239 DS   CL6
         HMAP R=14,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=14,C=68,A=(ASKIP,NORM)
#08SP241 DS   CL6
         HMAP R=14,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=15,C=4,A=(ASKIP,NORM)
#VRKEY   DC   CL4'9-15'
         HMAP R=15,C=12,A=(ASKIP,NORM)
#VRNAME  DC   C'V=R JOBS  '
         HMAP R=15,C=23,A=(ASKIP,NORM)
#VRCSA   DS   CL6
         HMAP R=15,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=32,A=(ASKIP,NORM)
#VRSP227 DS   CL6
         HMAP R=15,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=41,A=(ASKIP,NORM)
#VRSP228 DS   CL6
         HMAP R=15,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=50,A=(ASKIP,NORM)
#VRSP231 DS   CL6
         HMAP R=15,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=59,A=(ASKIP,NORM)
#VRSP239 DS   CL6
         HMAP R=15,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=15,C=68,A=(ASKIP,NORM)
#VRSP241 DS   CL6
         HMAP R=15,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
         HMAP R=16,C=4,A=(ASKIP,NORM)
         DC   72C'-'
         SPACE
         HMAP R=17,C=4,A=(ASKIP,NORM)
#TOTKEY  DC   CL4'0-15'
         HMAP R=17,C=12,A=(ASKIP,NORM)
#TOTNAME DC   C'ALL KEYS  '
         HMAP R=17,C=23,A=(ASKIP,NORM)
#TOTCSA  DS   CL6
         HMAP R=17,C=30,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=17,C=32,A=(ASKIP,NORM)
#TOT227  DS   CL6
         HMAP R=17,C=39,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=17,C=41,A=(ASKIP,NORM)
#TOT228  DS   CL6
         HMAP R=17,C=48,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=17,C=50,A=(ASKIP,NORM)
#TOT231  DS   CL6
         HMAP R=17,C=57,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=17,C=59,A=(ASKIP,NORM)
#TOT239  DS   CL6
         HMAP R=17,C=66,A=(ASKIP,NORM)
         DC   C'K'
         HMAP R=17,C=68,A=(ASKIP,NORM)
#TOT241  DS   CL6
         HMAP R=17,C=75,A=(ASKIP,NORM)
         DC   C'K'
         SPACE
#SCRNLEN EQU  *-#SCREEN
         LTORG
$WRKSECT DSECT
WRK227   DS   F
WRK228   DS   F
WRK231   DS   F
WRK239   DS   F
WRK241   DS   F
         $DQE                     DESCRIPTOR QUEUE ELEMENT DSECT
         $GDA                     GENERAL DATA AREA DSECT
         $LNKSECT                 INTER MODULE LINKGAGE AREA DSECT
         $SPQE                    SUBPOOL QUEUE ELEMENT DSECT
         END
