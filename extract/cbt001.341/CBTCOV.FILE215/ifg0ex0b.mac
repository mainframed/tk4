***********************************************************************
         TITLE 'IFG0EX0B - OPEN EXIT'
***********************************************************************
*                                                                     *
*                                                                     *
*    WRITTEN BY KAREN KAWABATA                                        *
*               FRANK JUZWIK                                          *
*                                                                     *
*              AMERICAN HOSPITAL SUPPLY CORPORATION                   *
*              INFORMATION SERVICES                                   *
*              1400 WAUKEGAN RD                                       *
*              MCGAW PARK, ILL 60085                                  *
*              (312) - 473-1400                                       *
*                                                                     *
* THIS PROGRAM HAS BEEN TESTED IN BOTH AN MVS SP1.3 AND MVS X/A 2.1   *
* ENVIORNMENT. NO WARRANTY IS MADE TO THE ACCURACY OF THIS PROGRAM    *
*                                                                     *
*  MODULE:    IFG0EX0B                                                *
*                                                                     *
*  FUNCTION:  THIS EXIT IS GIVEN CONTROL WHENEVER OPEN PROCESSES A    *
*             DCB.  THE DCB WILL BE OPENED UNDER THE FOLLOWING RULES: *
*                                                                     *
*                1) NO RESTRICTIONS ON:                               *
*                   A) TEMPORARY DATA SETS                            *
*                   B) VIO DATA SETS                                  *
*                   C) COMMON, TEST, SYSCTLG, CATALOG, IMSVS OR CICS  *
*                      DATA SETS WHICH RESIDE ON DISK                 *
*                   D) NOT-LABELLED OR BYPASS-LABEL-PROCESSING TAPES  *
*                2) OPERATIONS AND SOFTWARE SUPPORT CAN OPEN ANYTHING *
*                3) 'C' USERS CAN OPEN ANYTHING                       *
*                3) SYSTEM DATA SETS:                                 *
*                   A) SYS1.UADS - MUST BE OPENED WITH DISP=SHR       *
*                   B) SYS2.SYSTEM - DITTO                            *
*                   C) SYS1.BRODCAST - DITTO                          *
*                   D) ALL OTHER SYS1, SYS2 AND SYS3 DATA SETS MUST   *
*                      BE OPENED FOR INPUT WITH DISP=SHR              *
*                4) PRODUCTION JOBS                                   *
*                   A) DISK DATA SETS NOT LISTED UNDER 1 OR 4 MUST    *
*                      HAVE A PRODUCTION DSN                          *
*                   B) TAPE DATA SETS (NOT NL OR BLP) MUST HAVE A     *
*                      PRODUCTION DSN                                 *
*                5) TEST JOBS                                         *
*                   A) FOR DISK DATA SETS NOT LISTED UNDER 1 OR 4     *
*                      - IF THE DATA SET DOES NOT HAVE A TEST DSN     *
*                           THEN IT CAN ONLY BE OPENED FOR INPUT      *
*                        ELSE (IT DOES HAVE A TEST DSN)               *
*                           IF THE DSN HAS THE SAME USER CLASS        *
*                               THEN THE DATA SET WILL BE OPENED      *
*                                                                     *
*  INPUT:     FOR THE FORMAT OF THE INPUT PARAMETER LIST, SEE:        *
*                   DATA FACILITY/DEVICE SUPPORT                      *
*                   USER'S GUIDE AND REFERENCE                        *
*                   (SC26-3952-0)                                     *
*                   CHAPTER 6: EXIT ROUTINES                          *
*                                                                     *
*  OUTPUT:    VIOLATIONS OF THE ABOVE COMMANDMENTS WILL RESULT IN     *
*             IN THE FOLLOWING MESSAGES:                              *
*                                                                     *
*             REG 15            MESSAGE                               *
*                                                                     *
*               0    -   DISP=SHR NOT REQUESTED FOR DSN=XXX           *
*               0    -   UNAUTHORIZED USE OF SYSTEM DSN=XXX           *
*               0    -   WRONG CLASS USER, DSN=XXX, JOB=YYY           *
*               0    -   UNAUTHORIZED USE OF DSN=XXX                  *
*               0    -   TEST DSN=XXX USED BY JOB=YYY                 *
*                                                                     *
*                        XXX = DSN; YYY = JOB NAME                    *
*                                                                     *
*  REGISTERS:                                                         *
*             R2  - PARAMETER LIST                                    *
*             R3  - CALLER'S TIOT                                     *
*             R4  - CALLER'S JFCB                                     *
*             R5  - CALLER'S DCB                                      *
*             R6  - CALLER'S UCB                                      *
*             R7  - CALLER'S JOBNAME                                  *
*             R11 - ERROR CODE                                        *
*                                                                     *
*  EXTERNAL REFERENCES:  NONE                                         *
*                                                                     *
***********************************************************************
IFG0EX0B CSECT
         PRINT NOGEN
*        EQUATES
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12)                 SAVE ALL REGISTERS
         USING IFG0EX0B,R12            SET UP ADDRESSABILITY
         LR    R12,R15
         LR    R2,R1                   SAVE POINTER TO PARMS
         USING OIEXL,R2                ADDRESS PARM LIST
*
         L     R4,OIEXJFCB             GET ADDRESS OF JFCB
*                                      FROM PARM LIST
         USING INFMJFCB,R4             ADDRESS JFCB
*
         TM    JFCBIND2,JFCTEMP        IF TEMPORARY DATA SET
         BO    ZERO#RC                     SET RC = 0
         TM    JFCFLGS1,JFCVRDS        ELSE IF VIO DATA SET
         BO    ZERO#RC                     SET RC = 0
*
*                                      GET ADDRESS OF TIOT
         L     R3,16                   LA OF CVT
         L     R3,0(R3)                LA OF TCB WORDS
         L     R3,4(R3)                LA OF TCB
         L     R3,12(R3)               LA OF TIOT
         USING TIOT1,R3                ADDRESS TIOT
         CLC   TIOCNJOB(4),=C'INIT'    IF THE EXIT WAS ENTERED AT
         BE    STEPNAME                   STEP INIT/TERM, THE JOB
*                                         NAME FIELD OF THE TIOT =
*                                         'INIT' AND THE STEP NAME
*                                         FIELD HAS THE JOB NAME;
*                                         THEREFORE, GO CHECK STEP
*                                         NAME FIELD
         LA    R7,TIOCNJOB                SAVE ADDR OF JOBNAME
         B     CHK#USER                   AND CHECK OUT USER CLASS
*
STEPNAME EQU   *
         LA    R7,TIOCSTEP+8              SAVE ADDR OF JOBNAME
CHK#USER EQU   *
         CLI   0(R7),C'S'              IF CLASS 1 USER
         BE    ZERO#RC                   (1ST CHARACTER OF JOBNAME
         CLI   0(R7),C'R'                 = S OR R OR O OR C) THEN
         BE    ZERO#RC                    LET THESE PRIVILEGED
         CLI   0(R7),C'O'                 CHARACTERS DO ANYTHING BY
         BE    ZERO#RC                    SETTING THE RETURN CODE
         CLI   0(R7),C'C'                 TO ZERO
         BE    ZERO#RC
         CLI   0(R7),C'D'              ELSE IF 'D' USER
         BE    D#USER                     CHECK OUT THE D'S
DEVICE   EQU   *
*                                      CHECK OUT NON-CLASS 1 USERS
         L     R6,OIEXUCB              GET ADDRESS OF UCB
*                                      FROM PARM LIST
         USING UCBOB,R6                ADDRESS UCB
         TM    UCBTBYT3,UCB3DACC       IF DISK DATA SET
         BO    ANYDISK                    CHECK OUT DISK DATA SETS
         TM    UCBTBYT3,UCB3TAPE       ELSE IF TAPE DATA SET
         BO    TAPE                       CHECK OUT TAPE DATA SETS
         B     ZERO#RC                    ELSE SET RC = 0
         TITLE 'IFG0EX0B - DISK DATA SETS'
ANYDISK  EQU   *
*                                      CHECK THE FOLLOWING DATA SETS
*                                           FOR DISP = SHR
*                                           1) SYS1.UADS
         CLC   JFCBDSNM(9),=C'SYS1.UADS'
         BE    DISP                         2) SYS2.SYSTEM
         CLC   JFCBDSNM(11),=C'SYS2.SYSTEM'
         BE    DISP                         3) SYS1.BRODCAST
         CLC   JFCBDSNM(13),=C'SYS1.BRODCAST'
         BE    DISP
         CLC   JFCBDSNM(4),=C'SYS1'    IF A SYSTEM CLASS 1 DATA SET
         BE    INPUT                      (SYS1 OR
         CLC   JFCBDSNM(4),=C'SYS2'        SYS2 OR
         BE    INPUT                       SYS3) THEN
         CLC   JFCBDSNM(4),=C'SYS3'        CHECK IF
         BE    INPUT                       OPENED FOR INPUT ONLY
         CLC   JFCBDSNM(6),=C'COMMON'  ELSE IF SYSTEM CLASS 2 D.S.
         BE    ZERO#RC                    (COMMON OR
         CLC   JFCBDSNM(4),=C'TEST'        TEST OR
         BE    ZERO#RC                     SYSCTLG OR
         CLC   JFCBDSNM(7),=C'SYSCTLG'     CATALOG)
         BE    ZERO#RC                     THEN
         CLC   JFCBDSNM(7),=C'CATALOG'     SET RC = 0
         BE    ZERO#RC
         CLC   JFCBDSNM(5),=C'IMSVS'   ELSE IF SYSTEM CLASS 3 D.S.
         BE    ZERO#RC                    (IMSVS OR
         CLC   JFCBDSNM(4),=C'CICS'        CICS) THEN
         BE    ZERO#RC                     SET RC = 0 AND EXIT
         CLI   0(R7),C'P'              ELSE IF THIS IS A PROD JOB
         BE    PRODDISK                   CHECK OUT PROD DISK D.S.
         B     TESTDISK                ELSE CHECK OUT TEST DISK
*                                         DATA SETS
INPUT    EQU   *
         TM    OIEXOOPT,X'0F'          IF OPENED FOR INPUT
         BZ    DISP                       CHECK IF DISP=SHR
         LA    R11,2                   ELSE  SET ERROR CODE = 2
         B     ERROR                      GO TO ERROR ROUTINE
DISP     EQU   *
         TM    JFCBIND2,JFCSHARE       IF DISP = SHR
         BO    ZERO#RC                    SET RETURN CODE = 0
         LA    R11,1                   ELSE  SET ERROR CODE = 1
         B     ERROR                      GO TO ERROR ROUTINE
         TITLE 'IFG0EX0B - TEST DISK DATA SETS'
TESTDISK EQU   *
         CLI   JFCBDSNM,C'T'           IF A TEST DATA SET (1ST
         BE    CHKUSER                    (CHARACTER = T, V OR U)
         CLI   JFCBDSNM,C'U'              THEN CHECK FOR MATCHING
         BE    CHKUSER                    USER CLASS
         CLI   JFCBDSNM,C'V'
         BE    CHKUSER                 ELSE
         TM    OIEXOOPT,X'0F'             IF OPENED FOR INPUT
         BZ    ZERO#RC                       SET RC = 0
*                                         ELSE
         LA    R11,4                         SET ERROR CODE = 4
         B     ERROR                         GO TO ERROR ROUTINE
*
CHKUSER  EQU   *
         CLC   JFCBDSNM(3),0(R7)       IF THE SAME USER CLASS
         BE    ZERO#RC                    (1ST 3 CHARACTERS OF DSN
*                                         = 1ST 3 CHAR. OF JOBNAME)
*                                         THEN SET RC = 0
*                                      ELSE
         LA    R11,3                      SET ERROR CODE = 3
         B     ERROR                      GO TO ERROR ROUTINE
*
         TITLE 'IFG0EX0B - PRODUCTION DISK DATA SETS'
PRODDISK EQU   *
         CLI   JFCBDSNM,C'P'           IF PROD JOB WANTS TO OPEN
         BE    ZERO#RC                     PROD DATA SET THEN SET
*                                          RC = 0 AND
         LA    R11,5                   ELSE SET ERROR CODE = 5
         B     ERROR                       AND GO TO ERROR ROUTINE
         TITLE 'IFG0EX0B - "D" USERS'
D#USER   EQU   *
         CLC   JFCBDSNM(3),=C'IMS'     IF THEY WANT TO OPEN AN 'IMS'
         BE    ZERO#RC                    OR 'D' DATA SET, THEN
         CLI   JFCBDSNM,C'D'              SET THE RETURN CODE = 0
         BE    ZERO#RC                 ELSE
         LA    R11,4                      SET ERROR CODE = 4
         B     ERROR                      AND GO TO ERROR ROUTINE
         TITLE 'IFG0EX0B - TAPE DATA SETS'
TAPE     EQU   *
         TM    JFCBLTYP,JFCNL          IF NOT LABELLED TAPE
         BO    ZERO#RC                     SET RC = 0
         TM    JFCBLTYP,JFCBLP         IF BYPASS-LABEL-PROCESSING
         BO    ZERO#RC                     SET RC = O
         CLI   0(R7),C'P'              IF TEST JOB
         BNE   TESTTAPE                    CHECK OUT TEST TAPE D.S.
         CLI   JFCBDSNM,C'P'           ELSE IF PROD JOB WANTS
*                                          PROD DATA SET THEN
         BE    ZERO#RC                     SET RC = 0
         LA    R11,5                       ELSE SET ERROR CODE = 5
         B     ERROR                       AND GO TO ERROR ROUTINE
*
TESTTAPE EQU   *
         CLI   JFCBDSNM,C'T'           IF TEST JOB WANTS A TEST
         BE    ZERO#RC                    DATA SET (1ST CHARACTER =
         CLI   JFCBDSNM,C'U'              T, V OR U) THEN
         BE    ZERO#RC                    SET RC = 0
         CLI   JFCBDSNM,C'V'
         BE    ZERO#RC                 ELSE
         LA    R11,4                      SET ERROR CODE = 4
         TITLE 'IFG0EX0B - ERROR ROUTINE'
ERROR    EQU   *
         GETMAIN R,LV=200              GET SAVE AREA
*                                      18 WORDS FOR REGISTER SAVE
*                                      26 WORDS FOR WTO PARM LIST
         ST    R13,4(R1)               STORE CALLER'S SAVE ARE ADDR
         ST    R1,8(R13)               STORE OUR SAVE AREA ADDR
         LR    R13,R1                  OUR SAVE AREA ADDR
*
         LA    R8,72(R13)              POINT TO WORK AREA
         MVC   0(WTOLEN,R8),WTOLIST    MOVE LIST FORM OF WTO TO
*                                      WORK AREA, AND OVERLAY THIS LIST
*                                      FORM WITH APPROPRIATE MSG
         USING WTODSECT,R8
         LA    R9,WTOTEXT              THIS IS OVERLAYABLE TEXT AREA
         C     R11,=F'1'               IF ERROR CODE = 1
         BE    SENDERR1                   SEND ERROR MESSAGE 1
         C     R11,=F'2'               AND SO ON ...
         BE    SENDERR2
         C     R11,=F'3'
         BE    SENDERR3
         C     R11,=F'4'
         BE    SENDERR4
*
SENDERR5 EQU   *
         MVC   WTOTEXT(94),MSG5        OVERLAY WITH MESSAGE
         USING FORM5,R9                ADDRESS TEXT AREA
         MVC   DSN5(44),JFCBDSNM       MOVE DSN
         MVC   JOB5(8),0(R7)           MOVE JOB NAME
         B     SENDMSG                 SEND MESSAGE
         SPACE 1
SENDERR1 EQU   *
         MVC   WTOTEXT(94),MSG1        OVERLAY WITH MESSAGE
         USING FORM1,R9                ADDRESS TEXT AREA
         MVC   DSN1(44),JFCBDSNM       MOVE DSN
         B     SENDMSG                 SEND MESSAGE
         SPACE 1
SENDERR2 EQU   *
         MVC   WTOTEXT(94),MSG2        OVERLAY WITH MESSAGE
         USING FORM2,R9                ADDRESS TEXT AREA
         MVC   DSN2(44),JFCBDSNM       MOVE DSN
         B     SENDMSG
         SPACE 1
SENDERR3 EQU   *
         MVC   WTOTEXT(94),MSG3        OVERLAY WITH MESSAGE
         USING FORM3,R9                ADDRESS TEXT AREA
         MVC   DSN3(44),JFCBDSNM       MOVE DSN
         MVC   JOB3(8),0(R7)           MOVE JOB NAME
         B     SENDMSG                 SEND MESSAGE
         SPACE 1
SENDERR4 EQU   *
         MVC   WTOTEXT(94),MSG4        OVERLAY WITH MESSAGE
         USING FORM4,R9
         MVC   DSN4(44),JFCBDSNM       MOVE DSN
         SPACE 2
SENDMSG  EQU   *
         WTO   MF=(E,(R8))             ISSUE MESSAGE
         C     R11,=F'5'               IF ERROR CODE = 5
         BE    CLEANUP                    FREEMAIN WORK AREA
*                                      ELSE
*                                         BLOW UP OFFENDING TASK
CLEANUP  EQU   *
         LR    R1,R13                  LOAD GETMAINED ADDRESS
         L     R13,4(R13)              POINT TO CALLER'S SAVE AREA
         FREEMAIN R,LV=200,A=(1)       FREE GETMAINED STORAGE
*                                      LEAVE THE EXIT
         TITLE 'IFG0EX0B - RETURN'
ZERO#RC  EQU   *
         LA    R15,0                   SET RETURN CODE TO ZERO
EXIT     EQU   *
         RETURN (14,12),RC=(15)        GO BACK
         LTORG
         TITLE 'IFG0EX0B - CONSTANTS AND MESSAGES'
*
*        THE WTO HAS A MESSAGE TEXT AREA OF 94 CHARACTERS'
*
WTOLIST  WTO  '1-------10--------20--------30--------40--------50------X
               --60--------70--------80--------90--94',ROUTCDE=(11),   X
               MF=L
WTOLEN   EQU   *-WTOLIST               LENGTH OF WTO PARM LIST
         SPACE 3
*        ERROR MESSAGES
*
MSG1     DC    CL94' '
         ORG   MSG1
         DC    CL10'MSCOP01-I'
         DC    CL38'DISP=SHR NOT REQUESTED FOR SYSTEM DSN='
         DS    CL44
         ORG
         SPACE 3
MSG2     DC    CL94' '
         ORG   MSG2
         DC    CL10'MSCOP01-I'
         DC    CL32'UNAUTHORIZED USE OF SYSTEM  DSN='
         DS    CL44
         ORG
         SPACE 3
MSG3     DC    CL94' '
         ORG   MSG3
         DC    CL10'MSCOP03-I'
         DC    CL22'PROJECT MISMATCH: JOB='
         DS    CL8
         DC    CL6'  DSN='
         DS    CL44
         ORG
         SPACE 3
MSG4     DC    CL94' '
         ORG   MSG4
         DC    CL10'MSCOP04-I'
         DC    CL24'UNAUTHORIZED USE OF DSN='
         DS    CL44
         ORG
         SPACE 3
MSG5     DC    CL94' '
         ORG   MSG5
         DC    CL10'MSCOP05-I'
         DC    CL4'JOB='
         DS    CL8
         DC    CL17'  USING TEST DSN='
         DS    CL44
         ORG
         TITLE 'IFG0EX0B - DSECTS'
WTODSECT DSECT
         DS    AL2,BL2                 WTO CONTROL FIELDS
WTOTEXT  DS    CL94                    ERROR MESSAGE
         SPACE 3
FORM1    DSECT                         DSECT FOR MESSAGE 1
         DS    CL48
DSN1     DS    CL44
         SPACE 3
FORM2    DSECT                         DSECT FOR MESSAGE 2
         DS    CL42
DSN2     DS    CL44
         SPACE 3
FORM3    DSECT                         DSECT FOR MESSAGE 3
         DS    CL32
JOB3     DS    CL8
         DS    CL6
DSN3     DS    CL44
         SPACE 3
FORM4    DSECT                         DSECT FOR MESSAGE 4
         DS    CL34
DSN4     DS    CL44
         SPACE 3
FORM5    DSECT                         DSECT FOR MESSAGE 5
         DS    CL14
JOB5     DS    CL8
         DS    CL17
DSN5     DS    CL44
         SPACE 3
*UCB     DSECT                         MACRO GENERATES A LABEL OF
*                                      UCBOB
         IEFUCBOB LIST=YES,PREFIX=YES
         SPACE 3
*                                      INPUT PARAMETER LIST
         IECOIEXL
         SPACE 3
TIOT     DSECT                         MACRO GENERATES A LABEL OF
*                                      TIOT1
         IEFTIOT1
         SPACE 3
JFCB     DSECT                         MACRO GENERATES A LABEL OF
*                                      INFMJFCB
         IEFJFCBN
         END   IFG0EX0B
