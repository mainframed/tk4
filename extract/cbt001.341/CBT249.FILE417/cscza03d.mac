         TITLE 'IEEZA03D -   ##A  ACTIVITY MONITOR'
***********************************************************************
***********************************************************************
***                                                                 ***
***                                                                 ***
***                                                                 ***
***                     COMMAND  SUB-SYSTEM                         ***
***                  -------------------------                      ***
***                                                                 ***
***                                                                 ***
***       SE2ZA03D -       ##A  ACTIVITY MONITOR                    ***
***                                                                 ***
***       VERSION -        2.0                                      ***
***                                                                 ***
***       MVS LEVEL -      MVS/XA 2.1.1                             ***
***                                                                 ***
***       UPDATED -        04/01/80                                 ***
***                        08/10/81  JCC-BCB TEST FOR END OF ASCBS  ***
***                        02/07/84  JCC-SNET ADD 31-BIT CODING     ***
***                                      FOR XA.                    ***
***                                                                 ***
***                                                                 ***
***********************************************************************
***********************************************************************
         EJECT
IEEZA03D CSECT
IEEZA03D AMODE 24
IEEZA03D RMODE 24
         SPLEVEL SET=2
         LR    R12,R15
         USING IEEZA03D,R12
         L     R0,SIZE             LOAD GETMAIN AREA SIZE AND SUBPOOL
         GETMAIN R,LV=(0)
         LR    R13,R1
         USING WORK,R13
         ST    R14,SAVRET          SAVE RETURN ADDR
         ST    R2,XSASAVE          SAVE XSA ADDR
         USING XSA,R2
         LR    R10,R13             LOAD SAVE AREA ADDR
         L     R9,AMODE31                                   -JAL/XA-
         BSM   0,R9                                         -JAL/XA-
AMODE31  DC    A(CONT31+X'80000000')                        -JAL/XA-
CONT31   DS    0H                                           -JAL/XA-
         B     ESTAEBYP            BYPASS ESTAE LIST
ESTAEOVL ESTAE STAEXIT,CT,MF=L
ESTAEBYP MVC   ESTAELST(20),ESTAEOVL
         LA    R1,ESTAELST
         ESTAE PARAM=(10),MF=(E,(1))
         MVI   TSOSW,C' '
         XC    JOBTOT2(8),JOBTOT2
         XC    DMNTAB(200),DMNTAB
         MVC   DMNTAB+200(200),DMNTAB
         MVC   DMNTAB+400(200),DMNTAB
         MVC   DMNTAB+600(200),DMNTAB
         MVC   DMNTAB+800(200),DMNTAB
         MVC   DMNTAB+1000(200),DMNTAB
         MVC   DMNTAB+1200(200),DMNTAB
         MVC   DMNTAB+1400(200),DMNTAB
         MVC   DMNTAB+1600(200),DMNTAB
         MVC   DMNTAB+1800(200),DMNTAB
         MVC   DMNTAB+2000(200),DMNTAB
         MVC   DMNTAB+2200(200),DMNTAB
         CLC   TSTSVC35(2),=X'0A23'   TSO OR STANDALONE EXEC
         BE    *+16                   NO
         MVC   HDRIA(26),=CL26'ASID DSP DMN PGN SWC FLAGS'
         MVC   HDROA(26),=CL26'ASID DSP DMN PGN SWC FLAGS'
         L     R1,XAT              LOAD WAITTIME ADDR
         L     R15,8(0,R1)         LOAD HEX WAITTIME
         LTR   R15,R15             TIME NOT SPECIFIED
         BNZ   *+10                NO
         MVC   0(8,R1),=CL8'00003000'   30 SECONDS DEFAULT
         L     R3,XAL              PARM LIST ADDR
         LA    R3,0(0,R3)
         LTR   R3,R3               ANY PARM
         BZ    PARM2               NO
         MVC   TSOSW(2),0(R3)      SAVE PARM
         CLI   TSOSW,C'D'          DOMAIN LIST
         BE    PARM2               YES, CONTINUE
         CLI   TSOSW,C'T'          TSO DISPLAY
         BE    PARM2               YES, CONTINUE
         CLI   TSOSW,C'J'          JOB DISPLAY
         BE    PARM1               YES, CONTINUE
         MVC   DOUBLE(2),=C'00'
         MVZ   DOUBLE(2),TSOSW
         CLC   DOUBLE(2),=C'00'    DOMAIN DISPLAY
         BNE   PARM1
         PACK  DOUBLE(8),TSOSW(2)
         CVB   R1,DOUBLE
         STC   R1,DOMAIN           SAVE DOMAIN
         MVI   TSOSW,C'X'
         B     PARM2
         SPACE 1
PARM1    MVI   TSOSW,C' '
         SPACE 1
PARM2    MVC   WTOLIST(4),=X'00540100'
         MVI   SCANRET,C' '        SET FOUND
         MVI   SCANTYPE,C'I'       SET IN QUEUE SCAN
         EJECT
SETINIT  L     R4,16               CVT
         L     R4,556(0,R4)        ASVT
         L     R8,516(0,R4)        MAX # OF ASIDS                JCC
         MH    R8,=H'4'            MULT BY LENGTH                JCC
         LA    R8,528(R4,R8)       FIND END OF ASID LIST         JCC
         ST    R8,ASVTEND          STORE THAT ADDR               JCC
         LA    R4,524(0,R4)        ASCB SLOT ADDR
         LA    R8,JOBTABLE         ASCB/OUCB INFO STORAGE
         SPACE 1
SETINIT1 XC    0(60,R8),0(R8)
         SPACE 1
SETINIT2 LA    R4,4(0,R4)          INCR INTO ASVT
         C     R4,ASVTEND          END OF ASVT                   JCC
         BNL   SETINIT4            YES, EXIT                     JCC
         TM    0(R4),X'80'         AVAILABLE SLOT
         BO    SETINIT2            YES
         L     R5,0(0,R4)          ASCB ADDR
         LA    R5,0(0,R5)
         L     R7,144(0,R5)        OUCB ADDR
         ST    R5,0(0,R8)          ENTRY ID
         LM    R14,R15,64(R5)      TCB TIME
         SRDL  R14,22
         LR    R1,R15
         LM    R14,R15,200(R5)     SRB TIME
         SRDL  R14,22
         AR    R15,R1
         ST    R15,4(0,R8)         SAVE PREVIOUS CPU TIME
         SR    R15,R15
         ICM   R15,3,40(R5)        EXCP COUNT
         A     R15,108(0,R7)       OUCB EXCP COUNT
         ST    R15,16(0,R8)        SAVE TOTAL EXCPS
         L     R1,148(0,R5)        OUXB ADDR
         LA    R1,0(0,R1)
         LTR   R1,R1               VALID ADDR
         BZ    SETINIT3            NO
         L     R14,16(0,R1)        CURR PAGEIN
         ST    R14,28(0,R8)        SAVE CURR PAGEIN
         L     R14,20(0,R1)        CURR PAGEOUT
         ST    R14,32(0,R8)        SAVE CURR PAGEOUT
         L     R14,24(0,R1)        CURR PAGE RECLAIM
         ST    R14,36(0,R8)        SAVE CURR PAGE RECLAIM
         L     R14,28(0,R1)        CURR VIO PAGEIN
         ST    R14,40(0,R8)        SAVE CURR VIO PAGEIN
         L     R14,32(0,R1)        CURR VIO PAGEOUT
         ST    R14,44(0,R8)        SAVE CURR VIO PAGEOUT
         L     R14,36(0,R1)        CURR VIO RECLAIM
         ST    R14,48(0,R8)        SAVE CURR VIO RECLAIM
         SPACE 1
SETINIT3 TIME  BIN
         ST    R0,52(0,R8)
         LA    R8,56(0,R8)         NEXT ENTRY
         B     SETINIT1
         SPACE 1
SETINIT4 L     R3,XAT              LOAD WAITTIME ADDR
         STIMER WAIT,DINTVL=(R3)
         LA    R1,HDRI             IN QUEUE HDR
         CLI   TSOSW,C'D'          DOMAIN LIST
         BE    JOBOUT              YES, CONTINUE
         BAL   R11,MSGRTN1
         EJECT
JOBOUT   L     R4,16               CVT ADDR
         L     R4,556(0,R4)        ASVT ADDR
         L     R5,516(0,R4)        MAX # OF ASIDS                JCC
         MH    R5,=H'4'            MULT BY LENGTH                JCC
         LA    R5,528(R4,R5)       FIND END OF ASID LIST         JCC
         ST    R5,ASVTEND          STORE THAT ADDR               JCC
         LA    R4,524(0,R4)        ASCB SLOT ADDR
         B     JOBOUT4
         SPACE 1
JOBOUT1  L     R5,0(0,R4)          ASCB ADDR
         LA    R5,0(0,R5)
         L     R6,56(0,R5)         CSCB ADDR
         L     R7,144(0,R5)        LOAD OUCB ADDR
         CLI   SCANTYPE,C'I'       IN QUEUE SCAN
         BNE   JOBOUT2             NO
         CLI   41(R7),X'00'        SRC PRESENT
         BNE   JOBOUT6             YES, GO CHECK DET WAIT
         CLI   38(R5),X'7F'        LOGICAL SWAP
         BE    JOBOUT4             YES, OUT
         B     JOBOUT3             CONTINUE
         SPACE 1
JOBOUT2  CLI   38(R5),X'7F'        LOGICAL SWAP
         BE    JOBOUT3             YES, OUT QUEUE
         CLI   41(R7),X'00'        SWAP REASON
         BE    JOBOUT4             NO, IN QUEUE
         CLI   41(R7),X'06'        DETECTED WAIT
         BNE   JOBOUT3             NO
         TM    21(R7),X'01'        MSO DET WAIT BIT ON
         BZ    JOBOUT4             NO, IGNORE SRC
         SPACE 1
JOBOUT3  MVI   JOBMSG,C' '
         MVC   JOBMSG+1(79),JOBMSG
         BAL   R11,ASCBFORM        GO FORMAT ASCB INFO
         L     R6,144(0,R5)        OUCB ADDR
         L     R3,60(0,R5)         TSB ADDR
         L     R5,56(0,R5)         CSCB ADDR
         BAL   R11,CSCBFORM        GO FORMAT CSCB INFO
         CLI   TSOSW,C'D'          DOMAIN LIST
         BE    JOBOUT4             YES, CONTINUE
         LA    R1,WTOLIST          MSG AREA
         BAL   R11,MSGRTN          GO PRINT MSG
         SPACE 1
JOBOUT4  LA    R4,4(0,R4)          INCR INTO ASVT
         C     R4,ASVTEND          END OF ASVT                   JCC
         BNL   JOBOUT5             YES, RETURN                   JCC
         TM    0(R4),X'80'         AVAILABLE ENTRY
         BO    JOBOUT4             YES, CHECK NEXT
         B     JOBOUT1             GO USE IT
         SPACE 1
JOBOUT5  CLI   SCANTYPE,C'O'       OUT QUEUE SCAN
         BE    DMNCHK              YES, EXIT
         MVI   SCANRET,C' '        INIT OUTPUT SWITCH
         MVI   SCANTYPE,C'O'       SET OUT QUEUE SCAN NEXT
         CLI   TSOSW,C'D'          DOMAIN LIST
         BE    JOBOUT              YES, CONTINUE
         BAL   R11,JOBTOTL         TOTAL INPUT QUEUE
         LA    R1,WTOLIST          MSG AREA
         BAL   R11,MSGRTN          GO PRINT MSG
         LA    R1,HDRO             OUT QUEUE HDR
         BAL   R11,MSGRTN1
         B     JOBOUT              NEXT PASS
         SPACE 1
JOBOUT6  CLI   41(R7),X'06'        DETECTED WAIT
         BNE   JOBOUT4             NO
         TM    21(R7),X'01'        MSO DET WAIT BIT ON
         BZ    JOBOUT3             NO, IGNORE SRC
         B     JOBOUT4
         EJECT
ASCBFORM LA    R8,JOBTABLE         JOBTABLE ADDR
         UNPK  DOUBLE(5),36(3,R5)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   ASID(4),DOUBLE      SAVE ASID
         CLI   HDRIA,C' '          TSO OR STANDALONE EXEC
         BE    ASCBFRM1            NO, SHORTEN CONSOLE MESSAGE
         L     R7,144(0,R5)        OUCB ADDR
         UNPK  DOUBLE(5),36(3,R5)
         NC    DOUBLE(4),=4X'0F'
         TR    DOUBLE(4),=C'0123456789ABCDEF'
         MVC   ASIDOUT(4),DOUBLE   ASID
         SR    R1,R1
         IC    R1,43(0,R5)         DPRTY TO MSG
         CVD   R1,DOUBLE
         UNPK  DSP(3),DOUBLE+6(2)
         OI    DSP+2,X'F0'
         LH    R1,42(0,R7)         SWAP COUNT          MVS/SE      WJB
         CVD   R1,DOUBLE
         MVC   SWC-1(4),=X'40202120'
         ED    SWC-1(4),DOUBLE+6
         SR    R1,R1
         IC    R1,40(0,R7)         DOMAIN NUMBER       MVS/SE      WJB
         CVD   R1,DOUBLE
         MVC   DMN-1(4),=X'40202120'
         ED    DMN-1(4),DOUBLE+6
         SR    R1,R1
         IC    R1,185(0,R7)        PERF GROUP NUMBER   MVS/SE2
         CVD   R1,DOUBLE
         MVC   PGN-1(4),=X'40202120'
         ED    PGN-1(4),DOUBLE+6
         CLI   SCANTYPE,C'I'       IN QUEUE PASS
         BNE   ASCBFRM0            NO
         LH    R1,38(0,R5)         DISP POSITION
         CVD   R1,DOUBLE
         MVC   FLAGS(5),=CL5'NS-'
         TM    17(R7),X'80'        NON-SWAPPABLE
         BO    *+10                YES
         MVC   FLAGS(5),=CL5'IN-'
         UNPK  FLAGS+3(2),DOUBLE+6(2)
         OI    FLAGS+4,X'F0'
         B     ASCBFRM1
         SPACE 1
ASCBFRM0 MVC   FLAGS(5),=CL5'OU-'
         UNPK  FLAGS+3(3),41(2,R7)
         NC    FLAGS+3(2),=2X'0F'
         TR    FLAGS+3(2),=C'0123456789ABCDEF'
         MVI   FLAGS+5,C' '
         CLI   41(R7),X'00'        SRC = 0
         BNE   ASCBFRM1            NO
         CLI   38(R5),X'7F'        LOGICAL SWAP
         BNE   ASCBFRM1            NO
         MVC   FLAGS+3(2),=C'LG'
         SPACE 1
ASCBFRM1 CLC   0(4,R8),=F'0'       JOBTABLE END
         BE    ASCBFRM2            YES
         C     R5,0(0,R8)          ASCB MATCH
         BE    ASCBFRM2            YES
         LA    R8,56(0,R8)         NEXT ENTRY
         B     ASCBFRM1
         EJECT
ASCBFRM2 LM    R14,R15,64(R5)      TCB TIME
         SRDL  R14,22
         LR    R1,R15
         LM    R14,R15,200(R5)     SRB TIME
         L     R7,144(0,R5)        OUCB ADDR
         SRDL  R14,22
         AR    R1,R15
         L     R15,4(0,R8)         LOAD PREVIOUS CPU TIME
         LTR   R15,R15             ANY PREVIOUS
         BNZ   *+6                 NO
         LR    R15,R1              PREVIOUS = CURRENT
         ST    R15,8(0,R8)         SAVE PREVIOUS CPU TIME
         ST    R1,4(0,R8)          SAVE TOTAL CPU TIME
         S     R1,8(0,R8)          SUB PREVIOUS CPU TIME
         ST    R1,12(0,R8)         SAVE DIFFERENCE
         SR    R1,R1
         ICM   R1,3,40(R5)         EXCP COUNT
         A     R1,108(0,R7)        OUCB EXCP COUNT
         L     R15,16(0,R8)        LOAD PREVIOUS EXCP COUNT
         LTR   R15,R15             ANY PREVIOUS
         BNZ   *+6                 NO
         LR    R15,R1              PREVIOUS = CURRENT
         ST    R15,20(0,R8)        SAVE PREVIOUS CPU TIME
         ST    R1,16(0,R8)         SAVE TOTAL CPU TIME
         S     R1,20(0,R8)         SUB PREVIOUS CPU TIME
         ST    R1,24(0,R8)         SAVE DIFFERENCE
         TIME  BIN
         L     R1,52(0,R8)         OLD TIME
         LTR   R1,R1               ANY OLD TIME
         BNZ   *+8                 YES
         ST    R0,52(0,R8)         OLD TIME = NEW TIME
         LR    R1,R0               BINARY TIME
         S     R1,52(0,R8)         INTERVAL TIME
         ST    R0,52(0,R8)         NEW TIME BEGIN
         ST    R1,TIMEDIFF
         LTR   R1,R1               TIME EQUAL 0
         BZ    ASCBFRMY            YES, NO DIVIDE
         L     R15,24(0,R8)        LOAD EXCP DIFFERENCE
         LTR   R15,R15             NEGATIVE
         BM    ASCBFRMX            YES
         L     R15,12(0,R8)        LOAD CPU DIFFERENCE
         LTR   R15,R15             NEGATIVE
         BM    ASCBFRMX            YES
         SR    R14,R14
         M     R14,=F'10'          PERCENT
         SR    R14,R14
         DR    R14,R1              PERCENT UTIL
         ST    R15,JOBTOT1+4       SAVE JOB TOTAL PCT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   PCT(7),=X'402020204B2020'
         ED    PCT(4),DOUBLE+6
         SRDL  R14,32
         M     R14,=F'100'
         DR    R14,R1              PERCENT UTIL  (HUNDREDTHS)
         ST    R15,JOBTOT1         SAVE JOB TOTAL HUNDREDTHS
         CVD   R15,DOUBLE          PERCENT TO PRINT  (HUNDREDTHS)
         UNPK  PCT+5(2),DOUBLE+6(2)
         OI    PCT+6,X'F0'
         EJECT
         L     R15,24(0,R8)        LOAD EXCP DIFFERENCE
         SR    R14,R14
         M     R14,=F'100'         SET FOR DIVIDE
         SR    R14,R14
         DR    R14,R1              EXCP PER SEC
         ST    R15,JOBTOT1+12      SAVE JOB TOTAL EXCP
         CVD   R15,DOUBLE
         MVC   SIO(9),=X'4020202020204B2020'
         ED    SIO(6),DOUBLE+5
         SRDL  R14,32
         M     R14,=F'100'
         DR    R14,R1              SIO PER SEC  (HUNDREDTHS)
         ST    R15,JOBTOT1+8       SAVE JOB TOTAL HUNDREDTHS
         CVD   R15,DOUBLE
         UNPK  SIO+7(2),DOUBLE+6(2)
         OI    SIO+8,X'F0'
         EJECT
         L     R1,148(0,R5)        OUXB ADDR
         LA    R1,0(0,R1)
         LTR   R1,R1               VALID ADDR
         BCR   8,R11               NO
         L     R14,16(0,R1)        CURR PAGEIN
         S     R14,28(0,R8)        SUB PREV PAGEIN
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         LR    R15,R14             ACCUMULATE
         L     R14,20(0,R1)        CURR PAGEOUT
         S     R14,32(0,R8)        SUB PREV PAGEOUT
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         AR    R15,R14             ACCUMULATE
         L     R14,24(0,R1)        CURR PAGE RECLAIM
         S     R14,36(0,R8)        SUB PREV PAGE RECLAIM
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         SR    R15,R14             ACCUMULATE
         L     R14,28(0,R1)        CURR VIO PAGEIN
         S     R14,40(0,R8)        SUB PREV VIO PAGEIN
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         AR    R15,R14             ACCUMULATE
         L     R14,32(0,R1)        CURR VIO PAGEOUT
         S     R14,44(0,R8)        SUB PREV VIO PAGEOUT
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         AR    R15,R14             ACCUMULATE
         L     R14,36(0,R1)        CURR VIO RECLAIM
         S     R14,48(0,R8)        SUB PREV VIO RECLAIM
         LTR   R14,R14             OVERFLOW
         BM    ASCBFRM3            YES
         SR    R15,R14             ACCUMULATE
         SR    R14,R14
         M     R14,=F'100'
         D     R14,TIMEDIFF        PAGES PER SEC
         ST    R15,JOBTOT1+20
         CVD   R15,DOUBLE
         LR    R1,R15
         MVC   PAG(7),=X'402020214B2020'
         ED    PAG(4),DOUBLE+6
         SRDL  R14,32
         M     R14,=F'100'
         D     R14,TIMEDIFF        HUNDREDTHS
         ST    R15,JOBTOT1+16
         CVD   R15,DOUBLE
         UNPK  PAG+5(2),DOUBLE+6(2)
         OI    PAG+6,X'F0'
         BAL   R10,CHKFRM
         BR    R11
         SPACE 1
ASCBFRM3 MVC   PAG(7),=CL7'**OVF**'
         BAL   R10,CHKFRM
         BR    R11
         SPACE 1
ASCBFRMX MVC   PCT(7),=C'**CHG**'
         BAL   R10,CHKFRM
         BR    R11
         SPACE 1
ASCBFRMY MVC   PCT(7),=C'**ERR**'
         BAL   R10,CHKFRM
         BR    R11
         EJECT
CHKFRM   L     R1,16              LOAD CVT ADDR
         L     R1,356(0,R1)       LOAD PVT ADDR
         L     R15,12(0,R1)       PFT APPARENT ADDR
         AH    R15,16(0,R1)       PFT ACTUAL ADDR
         SR    R14,R14             INDEX
         IC    R14,18(0,R1)           TO
         SLL   R14,8                      END OF
         IC    R14,19(0,R1)                 PFT
         SH    R14,16(0,R1)
         SRL   R14,4               DIVIDE BY 16
         SR    R1,R1               CLEAR FRAME COUNT
         SPACE 1
CHKFRM1  CLC   0(2,R15),36(R5)     ASID MATCH
         BNE   CHKFRM2             NO
         LA    R1,1(0,R1)          ADD 1 TO FRAME COUNT
         SPACE 1
CHKFRM2  LA    R15,16(0,R15)       NEXT PFT
         BCT   R14,CHKFRM1         PROCESS NEXT
         SLL   R1,2                CALC  'K'
         CVD   R1,DOUBLE
         MVC   FRAME(7),=X'402020202120D2'
         ED    FRAME(6),DOUBLE+5
         BR    R10
         EJECT
JOBTOTL  MVC   WTOLIST+4(18),=CL18'## IN Q TOTL ##'
         SR    R14,R14
         L     R15,JOBTOT2
         D     R14,=F'100'
         A     R15,JOBTOT2+4       TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   PCT(7),=X'402020204B2020'
         ED    PCT(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  PCT+5(2),DOUBLE+6(2)
         OI    PCT+6,X'F0'
         SR    R14,R14
         L     R15,JOBTOT2+8
         D     R14,=F'100'
         A     R15,JOBTOT2+12      TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   SIO(9),=X'4020202020204B2020'
         ED    SIO(6),DOUBLE+5
         CVD   R14,DOUBLE
         UNPK  SIO+7(2),DOUBLE+6(2)
         OI    SIO+8,X'F0'
         SR    R14,R14
         L     R15,JOBTOT2+16
         D     R14,=F'100'
         A     R15,JOBTOT2+20      TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   PAG(7),=X'402020204B2020'
         ED    PAG(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  PAG+5(2),DOUBLE+6(2)
         OI    PAG+6,X'F0'
         MVC   FRAME(35),=CL35' '
         BR    R11
         EJECT
DMNCHK   MVC   DMNLIST(4),=X'00540100'
         CLI   TSOSW,C'D'          DOMAIN LIST
         BNE   RETURN              NO
         LA    R1,HDRD             OUT QUEUE HDR
         BAL   R11,MSGRTN1
         LA    R4,DMNTAB-24
         SR    R5,R5
         SPACE 1
DMNCHK1  CVD   R5,DOUBLE           DOMAIN NUMBER
         MVC   DMNLIST+4(80),=CL80'DOMAIN XX'
         UNPK  DMNNBR(2),DOUBLE+6(2)
         OI    DMNNBR+1,X'F0'
         LA    R5,1(0,R5)          NEXT DOMAIN
         LA    R4,24(0,R4)         NEXT TABLE ENTRY
         C     R5,=F'51'           END OF DOMAINS
         BE    DMNCHK2             YES, TOTAL
         CLC   0(24,R4),=6F'0'     ANY TIME IN THIS DOMAIN
         BE    DMNCHK1             NO
         SR    R14,R14
         L     R15,0(0,R4)
         D     R14,=F'100'
         A     R15,4(0,R4)         TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNPCT(7),=X'402020204B2020'
         ED    DMNPCT(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  DMNPCT+5(2),DOUBLE+6(2)
         OI    DMNPCT+6,X'F0'
         SR    R14,R14
         L     R15,8(0,R4)
         D     R14,=F'100'
         A     R15,12(0,R4)         TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNSIO(9),=X'4020202020204B2020'
         ED    DMNSIO(6),DOUBLE+5
         CVD   R14,DOUBLE
         UNPK  DMNSIO+7(2),DOUBLE+6(2)
         OI    DMNSIO+8,X'F0'
         SR    R14,R14
         L     R15,16(0,R4)
         D     R14,=F'100'
         A     R15,20(0,R4)         TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNPAG(7),=X'402020204B2020'
         ED    DMNPAG(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  DMNPAG+5(2),DOUBLE+6(2)
         OI    DMNPAG+6,X'F0'
         LA    R1,DMNLIST          MSG AREA
         BAL   R11,MSGRTN          GO PRINT MSG
         B     DMNCHK1
         EJECT
DMNCHK2  MVC   DMNLIST+4(80),=CL80'TOTAL PCT'
         SR    R14,R14
         L     R15,JOBTOT2
         D     R14,=F'100'
         A     R15,JOBTOT2+4       TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNPCT(7),=X'402020204B2020'
         ED    DMNPCT(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  DMNPCT+5(2),DOUBLE+6(2)
         OI    DMNPCT+6,X'F0'
         SR    R14,R14
         L     R15,JOBTOT2+8
         D     R14,=F'100'
         A     R15,JOBTOT2+12      TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNSIO(9),=X'4020202020204B2020'
         ED    DMNSIO(6),DOUBLE+5
         CVD   R14,DOUBLE
         UNPK  DMNSIO+7(2),DOUBLE+6(2)
         OI    DMNSIO+8,X'F0'
         SR    R14,R14
         L     R15,JOBTOT2+16
         D     R14,=F'100'
         A     R15,JOBTOT2+20      TOTAL PERCENT
         CVD   R15,DOUBLE          PERCENT TO PRINT
         MVC   DMNPAG(7),=X'402020204B2020'
         ED    DMNPAG(4),DOUBLE+6
         CVD   R14,DOUBLE
         UNPK  DMNPAG+5(2),DOUBLE+6(2)
         OI    DMNPAG+6,X'F0'
         LA    R1,DMNLIST          MSG AREA
         BAL   R11,MSGRTN          GO PRINT MSG
         B     RETURN
         EJECT
CSCBFORM CLI   TSOSW,C'T'          TSO USER DISPLAY
         BNE   CSCBFRM0            NO
         CLI   28(R5),X'01'        TSO USER
         BNE   JOBOUT4             NO, BYPASS
         B     CSCBFRM6
         SPACE 1
CSCBFRM0 CLI   TSOSW,C'D'          DOMAIN LIST
         BE    *+12                YES, CONTINUE
         CLI   28(R5),X'01'        TSO USER
         BE    JOBOUT4             YES, DONT PROCESS
         CLI   28(R5),X'03'        INIT
         BNE   CSCBFRM3            NO, GO HANDLE
         LA    R9,0(0,R5)          SAVE CSCB ADDR
         SR    R10,R10             INSERT
         IC    R10,30(0,R5)
         SLL   R10,8               ASID
         IC    R10,31(0,R5)
         SPACE 1
CSCBFRM1 L     R5,0(0,R5)          NEXT CSCB
         LA    R5,0(0,R5)          CLEAR HIGH ORDER
         LTR   R5,R5               END OF CSCBS
         BNZ   *+10                NO, CONTINUE
         LR    R5,R9               LOAD CSCB ADDR
         B     JOBOUT4             NO GOOD
         CR    R5,R9               SAME CSCB
         BE    JOBOUT4             YES, NO GOOD
         CH    R10,30(0,R5)        SAME ASID
         BNE   CSCBFRM1            NO, CHECK NEXT
         SPACE 1
CSCBFRM2 MVC   JOBNAME(8),8(R5)
         MVC   STEPNAME(8),64(R5)
         B     CSCBFRM4
         SPACE 1
CSCBFRM3 MVC   JOBNAME(8),16(R5)
         MVC   STEPNAME(8),8(R5)
         SPACE 1
CSCBFRM4 CLI   TSOSW,C'X'          DOMAIN DISPLAY
         BNE   CSCBFRM5            NO
         CLC   DOMAIN(1),40(R6)    DOMAIN MATCH
         BNE   JOBOUT4
         SPACE 1
CSCBFRM5 LM    R14,R15,JOBTOT1
         A     R14,JOBTOT2
         A     R15,JOBTOT2+4
         STM   R14,R15,JOBTOT2
         LM    R14,R15,JOBTOT1+8
         A     R14,JOBTOT2+8
         A     R15,JOBTOT2+12
         STM   R14,R15,JOBTOT2+8
         LM    R14,R15,JOBTOT1+16
         A     R14,JOBTOT2+16
         A     R15,JOBTOT2+20
         STM   R14,R15,JOBTOT2+16
         CLI   TSOSW,C'D'          DOMAIN LIST
         BE    CSCBFRM7            YES
         CLC   ASID(4),=C'0001'
         BCR   7,R11
         MVC   JOBNAME(20),=CL20'MASTER'
         BR    R11
         SPACE 1
CSCBFRM6 MVC   JOBNAME(8),8(R5)
         MVC   STEPNAME(8),16(R5)
         B     CSCBFRM5
         SPACE 1
CSCBFRM7 SR    R1,R1
         IC    R1,40(0,R6)         DOMAIN NUMBER
         MH    R1,=H'24'
         LA    R1,DMNTAB(R1)       DOMAIN TABLE SLOT
         LM    R14,R15,JOBTOT1
         A     R14,0(0,R1)
         A     R15,4(0,R1)
         STM   R14,R15,0(R1)       SAVE DOMAIN TOTALS
         LM    R14,R15,JOBTOT1+8
         A     R14,8(0,R1)
         A     R15,12(0,R1)
         STM   R14,R15,8(R1)       SAVE DOMAIN TOTALS
         LM    R14,R15,JOBTOT1+16
         A     R14,16(0,R1)
         A     R15,20(0,R1)
         STM   R14,R15,16(R1)       SAVE DOMAIN TOTALS
         B     JOBOUT4
         EJECT
         USING *,R15
STAEXIT  CH    R0,=H'12'           SDWA GOTTEN
         BE    STAEXIT1            NO, FORCE RETURN
         L     R2,=A(STAERTRY)     LOAD RETRY ADDR
         SETRP RC=4,RETADDR=(2),FRESDWA=YES,DUMP=NO
         SPACE 3
STAEXIT1 L     R0,=A(STAERTRY)     LOAD RETRY ADDR
         LA    R15,4
         BR    R14
         DROP  R15
         SPACE 3
         USING *,R15
STAERTRY L     R12,=A(IEEZA03D)    LOAD ENTRY POINT ADDR
         LR    R13,R1              RELOAD WORK AREA ADDR
         DROP  R15
         LA    R1,STAEMSG          ADDRESSING ERROR
         L     R2,XSASAVE          RELOAD XSA ADDR
         LA    R11,RETURN          LOAD RET ADDR
         EJECT
MSGRTN   MVI   SCANRET,C'P'
         SPACE 1
MSGRTN1  SR    R0,R0
         IC    R0,XAU              INSERT MCS ID
TSTSVC35 SVC   35                  ISSUE WTO
         BR    R11
         SPACE 3
RETURN   ESTAE 0
         L     R0,SIZE             LOAD GETMAIN SIZE AND SUBPOOL
         LR    R1,R13              LOAD GETMAIN AREA ADDR
         L     R14,SAVRET          RETURN ADDR
         FREEMAIN R,LV=(0),A=(1)
         LA    R8,AMODE24                                  -JAL/XA-
         BSM   0,R8                                        -JAL/XA-
AMODE24  DS    0H                                          -JAL/XA-
         SR    R15,R15
         BR    R14
         EJECT
         LTORG
         SPACE 3
         DS    0F
SIZE     DC    X'F1',AL3(WORKEND-WORKSTRT)
         SPACE 3
         DS    0F
STAEMSG  DC    X'00540100'
         DC    CL80'ERROR ENCOUNTERED IN CONTROL BLOCKS, RETRY COMMAND'
         SPACE 1
         DS    0F
NFNDMSG  DC    X'00540100'
         DC    CL80'NO JOBS ON OUT QUEUE'
         SPACE 1
HDRD     DC    X'00540100'
         DC    CL18'**  DOMAINS  **'
         DC    CL62'CPU PCT  SIO/SEC  PAG/SEC'
         SPACE 1
HDRI     DC    X'00540100'
         DC    CL18'** IN  QUEUE **'
         DC    CL36'CPU PCT  SIO/SEC  PAG/SEC  WORKSET'
HDRIA    DC    CL26' '
         SPACE 1
HDRO     DC    X'00540100'
         DC    CL18'** OUT QUEUE **'
         DC    CL36'CPU PCT  SIO/SEC  PAG/SEC  WORKSET'
HDROA    DC    CL26' '
         EJECT
WORK     DSECT
WORKSTRT DS    0D
SAVRET   DS    18F
DOUBLE   DS    2F
JOBTOT1  DS    6F
JOBTOT2  DS    6F
XSASAVE  DS    F
ASID     DS    F
TIMEDIFF DS    F
ASVTEND  DS    F                                                 JCC
SCANRET  DS    C
SCANTYPE DS    C
TSOSW    DS    C
DOMAIN   DS    C
         SPACE 5
         DS    0F
WTOLIST  DS    F
JOBMSG   DS    0CL80
JOBNAME  DS    CL8
         DS    CL1
STEPNAME DS    CL8
         DS    CL1
PCT      DS    CL7
SIO      DS    CL9
         DS    CL2
PAG      DS    CL7
         DS    CL2
FRAME    DS    CL7
         DS    CL2
ASIDOUT  DS    CL4
         DS    CL1
DSP      DS    CL3
         DS    CL1
DMN      DS    CL3
         DS    CL1
PGN      DS    CL3
         DS    CL1
SWC      DS    CL3
         DS    CL1
FLAGS    DS    CL5
         DS    CL1
         SPACE 5
         DS    0F
DMNLIST  DS    F
DMNMSG   DS    0CL80
         DS    CL7
DMNNBR   DS    CL2
         DS    CL9
DMNPCT   DS    CL7
DMNSIO   DS    CL9
         DS    CL2
DMNPAG   DS    CL7
         DS    CL37
         SPACE 2
ESTAELST DS    5F
DMNTAB   DS    100CL24
         SPACE 5
JOBTABLE DS    0D
JOBTAB   DS    200CL48
WORKEND  DS    0F
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAT      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
XAA      DS    F
         EJECT
         IHASDWA
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
