*              /* LEVEL=0002                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXQSRT1: /* QUICKSORT */
   PROC(KEYS,BOUND) OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*  XXQSRT1 IS A NON-RECURSIVE VERSION OF QUICKSORT, INVENTED BY    */
 /*  C A R HOARE.  FOR FURTHER DETAILS SEE COMPUTER JOURNAL VOL 5,   */
 /*  (1962), NO 1, PP 10-15.                                         */
 /********************************************************************/
 /*   LEVL(0002)  6 APRIL 1979.  CHANGES:                            */
 /*   CORRECTION OF PARTITION ALGORITHM SO THAT IT WOULD COPE WITH   */
 /*   ITEMS WITH EQUAL KEYS AND ALL EQUAL ITEMS.                     */
 /*   FIRST VERSION COULD NOT COPE WITH THIS AND COULD GIVE          */
 /*   EITHER LOOPING OR RETURN FROM THE PROCEDURE WITHOUT HAVING     */
 /*   SORTED THE ARRAY IN CERTAIN PATHOLOGICAL CASES.                */
 /********************************************************************/
 /*   LEVL(0001)  10 MARCH 1979.  CHANGES:                           */
 /*   REMOVAL OF SORT OF ITEMS AS ONLY THE KEYS NEED TO BE SORTED    */
 /*   IN THIS PARTICULAR APPLICATION.                                */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
   DEFAULT
      RANGE(*) FIXED;
 %SKIP(4);
 /*PARAMETERS*/
 %SKIP;
   DCL
      BOUND FIXED BIN(15);             /* UPPER BOUND OF ITEMS, KEYS */
   DCL
      KEYS(*) FIXED BIN(15) CONN;      /* KEYS FOR WHICH SORTING IS TO
                                       BE PERFORMED */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      TEMPKEY FIXED BIN(15);           /* TEMPORARY USED IN EXCHANGING
                                       KEYS */
 %SKIP(4);
   DCL
      STACKL(0:20) FIXED BIN(15),
      STACKR(0:20) FIXED BIN(15),      /* STACKS FOR LEFT, RIGHT
                                       PARTITIONS - 20 WILL BE AMPLY
                                       LARGE TO HOLD THE STACKS */
      (LEFT,
      RIGHT,
      MIDDLE,
      L1,
      R1) FIXED BIN(15),               /* POINTERS TO LEFT AND RIGHT
                                       ENDS OF SUB-PARTITIONS */
      COMP FIXED BIN(15),              /* TEMPORARY TO HOLD KEY FOR
                                       COMPARISON */
      STACKINDEX FIXED BIN(15);        /* INDEX TO STACKS */
   DCL
      (ORD,                            /* FLAG - ON => ONE OR OTHER OF
                                       THE SCANS THROUGH THE ARRAY HAS
                                       REACHED THE PARTITION ELEMENT */
      DUPLEFTKEYS,                     /* FLAG - ON => THAT THERE ARE
                                       ITEMS WITH KEYS THE SAME AS THE
                                       COMPARE ITEM IN THE LEFT HAND
                                       PART OF THE ARRAY */
      DUPRIGHTKEYS,                    /* FLAG - ON => THAT THERE ARE
                                       ITEMS WITH KEYS THE SAME AS THE
                                       COMPARE ITEM IN THE RIGHT HAND
                                       PART OF THE ARRAY */
      FORNEXT,                         /* TEMPORARY FLAG TO STORE
                                       STATUS OF WHETHER AN ITEM HAS
                                       KEYS THE SAME AS THE COMPARE
                                       ITEM */
      LEFTEQMIDDLE,                    /* FLAG - ON => THAT THE LEFT
                                       HAND SCAN HAS REACHED THE
                                       MIDDLE */
      RIGHTEQMIDDLE) BIT(1) ALIGNED;   /* FLAG - ON => THAT THE RIGHT
                                       HAND SCAN HAS REACHED THE
                                       MIDDLE */
   DCL
      (SUB_PARTITION_L,                /* INDEX TO LEFT SUB-PARTITION */
      SUB_PARTITION_R) FIXED BIN(15);  /* INDEX TO RIGHT SUB-PARTITION
                                       */
   DCL
      SPACE FIXED BIN(15);             /* TEMPORARY TO HOLD NUMBER OF
                                       ITEMS IN THE MIDDLE OF THE
                                       ARRAY - IE THE NUMBER OF ITEMS
                                       NOT SCANNED YET */
   DCL
      TEMPXCH FIXED BIN(15);           /* TEMPORARY FOR EXCHANGING
                                       PARTITION COMPARE ITEM */
 %SKIP(4);
      STACKINDEX=0;                    /* PUT FIRST REQUEST ON STACK -
                                       IE SORT WHOLE ARRAY */
      STACKL(0)=0;
      STACKR(0)=BOUND;
 %PAGE;
      DO UNTIL(STACKINDEX = -1);       /* WHEN ALL ITEMS ARE REMOVED
                                       FROM THE STACK THEN WE'RE DONE */
         LEFT=STACKL(STACKINDEX);      /* TAKE TOP REQUEST FROM STACK */
         RIGHT=STACKR(STACKINDEX);
         STACKINDEX=STACKINDEX-1;
 %SKIP(2);
         DO UNTIL(LEFT >= RIGHT);      /* THIS LOOP REPEATS THE
                                       PARTITION STEP UNTIL THE TWO
                                       POINTERS MEET IN THE MIDDLE,
                                       BEFORE TAKING ELEMENTS FROM THE
                                       TOP OF THE STACK */
 %SKIP(4);
 /* PARTITION LOOP */
 %SKIP(2);
            L1=LEFT;                   /* SET SCAN INDICES TO THE ENDS
                                       OF THE SUBARRAY */
            R1=RIGHT;
            MIDDLE=R1-L1;              /* DIFFERENCE BETWEEN THE
                                       BEGNINNING AND THE ENDS OF THE
                                       PARTITION */
            IF MIDDLE < 3 THEN
               DO;                     /* THREE OR LESS ENTRIES - SORT
                                       THEM DIRECTLYAND NOT BOTHER
                                       WITH THE PARTITION */
                  SELECT (MIDDLE);
                     WHEN (2)
                        DO;            /* THREE ITEMS IN SORT */
                           MIDDLE=R1-1;/* INDEX OF MIDDLE ITEM */
                           IF KEYS(MIDDLE) < KEYS(L1) THEN
                              DO;      /* IF MIDDLE LESS   THEN LEFT */
                                 TEMPKEY=KEYS(MIDDLE);
                                 KEYS(MIDDLE)=KEYS(LEFT);
                                 KEYS(LEFT)=TEMPKEY;
                              END;
                           IF KEYS(R1) < KEYS(MIDDLE) THEN
                              DO;      /* MIDDLE IS GREATER THAN
                                       RIGHT, SO EXCHANGE THEM */
                                 TEMPKEY=KEYS(R1);
                                 KEYS(R1)=KEYS(MIDDLE);
                                 KEYS(MIDDLE)=TEMPKEY;
                              END;
                        END;
                     WHEN(1)
                        DO;            /* TWO ITEMS IN SORT */
                           MIDDLE=R1;  /* SET INDEX OF RIGHT HAND ONE
                                       INTO MIDDLE SO THAT THIRD
                                       COMPARE FOR THREE ITEM SORT
                                       WILL WORK AS THE SINGLE COMPARE
                                       REQUIRED FOR A TWO ITEM SORT */
                        END;
                     OTHER
                        GOTO NOITEMS;  /* ONLY 1 ITEM TO SORT - THIS
                                       SHOULD ONLY BE CAUSED BY
                                       CALLING THE PROCEDURE WITH A
                                       DEGENERATE ARRAY OF ONLY 1 ITEM
                                       */
                  END;                 /* OF SELECT */
                  IF KEYS(MIDDLE) < KEYS(L1) THEN
                     DO;               /* LAST COMPARE - MIDDLE AND L1
                                       AGAIN */
                        TEMPKEY=KEYS(L1);
                        KEYS(L1)=KEYS(MIDDLE);
                        KEYS(MIDDLE)=TEMPKEY;
                     END;
 NOITEMS:
                  LEFT=RIGHT;          /* FOOL MAIN QUICKSORT INTO
                                       THINKING THAT IT HAS REACHED
                                       THE MIDDLE OF THE ARRAY */
               END;
            ELSE
               DO;                     /* OTHERWISE DO THE PARTITION */
                  MIDDLE=(L1+R1)/2;    /* USE THE MIDDLE VALUE AS THE
                                       KEY ABOUT WHICH THE SUB-ARRAY
                                       IS TO BE PARTITIONED */
                  COMP=KEYS(MIDDLE);
                  DUPLEFTKEYS,DUPRIGHTKEYS='0'B;/* HAVE NOT FOUND
                                       DUPLICATE KEYS ON THIS SCAN YET
                                       */
 SCAN_LOOP:
                  DO WHILE('1'B);      /* KEEP DOING THIS LOOP UNTIL
                                       BOTH POINTERS REACH THE
                                       'MIDDLE' OF THE ARRAY - NOTE
                                       THAT THE VALUE OF THE 'MIDDLE'
                                       WILL CHANGE IF THERE ARE MORE
                                       RECORDS WITH KEYS LESS THAN THE
                                       KEY OF THE MIDDLE RECORD */
                     LEFTEQMIDDLE,RIGHTEQMIDDLE='0'B;/* NEITHER OF THE
                                       SCANS HAS REACHED THE MIDDLE
                                       YET */
                     ORD='1'B;         /* ORD IS A FLAG AS TO WHETHER
                                       ONE OR OTHER OF THE POINTERS
                                       HAS REACHED THE MIDDLE VALUE
                                       YET AND SO THEREFORE WHETHER
                                       SPECIAL ACTION NEEDS TO BE
                                       TAKEN */
                     FORNEXT='0'B;     /* SET EQUAL KEY FLAG OFF */
                     DO L1 = L1 BY 1 TO MIDDLE;/* SCAN LEFT HAND PART
                                       OF THE ARRAY FROM THE OUTSIDE
                                       INWARDS */
                        DUPLEFTKEYS=DUPLEFTKEYS×FORNEXT;/* AS THE
                                       ARRAY IS SCANNED, A TEST IS
                                       MADE TO SEE IF THERE ARE ANY
                                       MORE RECORDS WITH KEYS EQUAL TO
                                       THE PARTITION ITEM'S KEY.
                                       HOWEVER, THE COUNT NEEDS TO BE
                                       KEPT ONE BEHIND AS THE MIDDLE
                                       VALUE WHICH IS THE ITEM WHOSE
                                       KEY IS BEING USED FOR
                                       COMPARISON WILL OBVIOUSLY HAVE
                                       AN EQUAL KEY, WHICH WOULD MEAN
                                       THAT THE DUPLICATE KEY ROUTINE,
                                       WHICH IS EXECUTED AFTER THE
                                       PARTITION TO CLUMP ALL THE
                                       DUPLICATE KEYS INTO THE MIDDLE
                                       WOULD BE CALLED EVERY TIME NOT
                                       JUST WHEN THERE ARE EQUAL KEYS */
                        FORNEXT=(KEYS(L1)=COMP);/* SET FLAG TO
                                       INDICATE WHETHER THESE ITEMS
                                       HAVE DUPLICATE COMPARISON KEYS
                                       - SEE ABOVE */
                        IF KEYS(L1) > COMP THEN
                           GOTO SCANRIGHT;/* FOUND AN ELEMENT LARGER
                                       THAN THE COMPARAND ELEMENT */
                     END;
                     LEFTEQMIDDLE='1'B;/* OTHERWISE, IF WE DROP OUT OF
                                       THE BOTTOM OF THE LOOP THEN
                                       WE'VE REACHED THE MIDDLE OF THE
                                       ARRAY */
                     L1=MIDDLE;        /* THE VALUE OF L1 WHEN WE DROP
                                       OUT OF THE BOTTOM WILL BE
                                       MIDDLE+1. RESET */
                     ORD='0'B;         /* WE'VE REACHED THE MIDDLE OF
                                       THE ARRAY WITH THE LEFT HAND
                                       SCAN, SO SET FLAG TO INDICATE
                                       THAT THE SPECIAL ACTION WILL
                                       HAVE TO BE TAKEN */
 SCANRIGHT:
                     FORNEXT='0'B;     /* TURN DUPLICATE KEY FLAG OFF */
                     DO R1 = R1 BY -1 TO MIDDLE;/* SCAN RIGHT HAND
                                       PART OF THE ARRAY FROM THE
                                       OUTSIDE INWARDS */
                        DUPRIGHTKEYS=DUPRIGHTKEYS×FORNEXT;/* SET
                                       DUPLICATE KEY FLAG A ACCORDING
                                       TO LAST TIME ROUND (SEE
                                       EXPLANATION IN LEFT HAND SCAN
                                       COMMENTS) */
                        FORNEXT=(KEYS(R1)=COMP);/* SET IT FOR NEXT
                                       TIME */
                        IF KEYS(R1) < COMP THEN
                           GOTO POSXCHNGE;/* FOUND AN ITEM SMALLERTHAN
                                       THE COMPARISON ITEM */
                     END;
                     RIGHTEQMIDDLE='1'B;/* OTHERWISE, IF WE DROP OUT
                                       OF THE BOTTOM OF THE LOOP THEN
                                       WE'VE REACHED THE MIDDLE OF THE
                                       ARRAY */
                     R1=MIDDLE;
                     ORD='0'B;         /* WE'VE REACHED THE MIDDLE */
 %SKIP(2);
 POSXCHNGE:
                     IF (LEFTEQMIDDLE & RIGHTEQMIDDLE) THEN
                        LEAVE;         /* BOTH LEFT AND RIGHT HAVE
                                       REACHED THE CENTRE OF THE ARRAY
                                       SO WE'RE DONE */
                     TEMPKEY=KEYS(L1); /* OTHERWISE EXCHANGE THE ITEMS
                                       */
                     KEYS(L1)=KEYS(R1);
                     KEYS(R1)=TEMPKEY;
                     IF ORD THEN
                        DO;            /* HAVEN'T REACHED THE MIDDLE
                                       WITH EITHER OF THE SCANS YET,
                                       SO KEEP ON GOING */
                           L1=L1+1;
                           R1=R1-1;
                        END;
                     ELSE
                        DO;            /* HAVE REACHED THE MIDDLE, SO
                                       SHIFT THE MIDDLE ITEM */
                           SPACE=R1-L1;/* DIFFERENCE BETWEEN INDICES
                                       OF ITEMS */
                           IF SPACE=1 THEN
                              DO;      /* DIFFERENCE IS 1, ITEMS ARE
                                       THEREFORE NEXT TO EACH OTHER
                                       AND SO WILL BE IN THE RIGHT
                                       ORDER ALREADY */
                                 IF LEFTEQMIDDLE THEN
                                    MIDDLE,L1=R1;/* LEFT HAND SCAN
                                       REACHED MIDDLE. MIDDLE ITEM WAS
                                       XCHANGED WITH RIGHT HAND VALUE.
                                       MIDDLE ITEM (THE COMPARAND TEM
                                       IS NOW AT THE R1 POSITION, SO
                                       SET ALL THE INDICES TO THE
                                       IDDLE POSITION TO INDICATE THAT
                                       THE PARTITION SCANS ARE DONE */
                                 ELSE
                                    MIDDLE,R1=L1;/* OTHERWISE THE
                                       REVERSE HAPPENED AND RIGHT HAND
                                       SCAN HAS REACHED THE MIDDLE.
                                       MIDDLE VALUE IS NOW AT THE L1
                                       POSITION */
                                 LEAVE SCAN_LOOP;/* FINISHED THE SCANS
                                       */
                              END;
                           IF LEFTEQMIDDLE THEN
                              DO;      /* LEFT SCAN HAS REACHED THE
                                       MIDDLE. EXCHANGE HAS TAKEN
                                       MIDDLE ELEMENT AND PUT IT IN
                                       THE R1 POSITION. EXCHANGE ITEMS
                                       R1 AND (L1+R1)/2 AND UPDATE
                                       MIDDLE POINTER */
                                 TEMPXCH=R1;
                              END;
                           ELSE
                              DO;      /* MUST BE RIGHT SCAN REACHING
                                       THE MIDDLE - EXCHANGE HAS
                                       TRANSFERRED MIDDLE ELEMENT TO
                                       INDEX L1 - SO SET TEMPXCH TO L1
                                       TO EXCHANGE IT WITH THE ELEMENT
                                       MIDWAY BETWEEN L1 AND R1 */
                                 TEMPXCH=L1;
                              END;
                           MIDDLE=(L1+R1)/2;/* NEW MIDDLE VALUE */
                           TEMPKEY=KEYS(TEMPXCH);
                           KEYS(TEMPXCH)=KEYS(MIDDLE);
                           KEYS(MIDDLE)=TEMPKEY;
                           LEFTEQMIDDLE,RIGHTEQMIDDLE,ORD=
                           '0'B;
                        END;
                  END;                 /* OF OUTERLOOP */
                  SUB_PARTITION_L=MIDDLE-1;/* SET DEFAULT VALUES FOR
                                       TOP AND BOTTOM INDICES OF THE
                                       SUB-PARTITIONS */
                  SUB_PARTITION_R=MIDDLE+1;
                  IF DUPLEFTKEYS THEN
                     DO;               /* DUPLICATE KEYS IN LEFT HAND
                                       SCAN, SO SORT THEM OUT */
                        DO L1 = MIDDLE BY -1 TO LEFT;/* FIRST FIND OUT
                                       IF ALL THE KEYS ARE THE SAME */
                           IF KEYS(L1) ^= COMP THEN
                              GOTO XLEFT;/* FOUND A NON-DUPLICATE KEY */
                        END;
                        L1=LEFT;       /* SET L1 TO BOTTOM INDEX */
                        LEAVE;         /* ALL DUPLICATE LEFT KEYS */
 XLEFT:
                        SUB_PARTITION_L=L1;/* NEXT SPACE AVAILABLE FOR
                                       SWAPPING KEYS */
                        DO L1 = SUB_PARTITION_L TO LEFT BY -1;
                           IF KEYS(L1) = COMP THEN
                              DO;      /* DUPLICATE KEYS, EXCHANGE */
                                 TEMPKEY=KEYS(SUB_PARTITION_L);
                                 KEYS(SUB_PARTITION_L)=KEYS(L1);
                                 KEYS(L1)=TEMPKEY;
                                 SUB_PARTITION_L=
                                 SUB_PARTITION_L-1;
                              END;
                        END;
                     END;
                  IF DUPRIGHTKEYS THEN
                     DO;               /* DUPLICATE KEYS IN RIGHT HAND
                                       SCAN, SO SORT THEM OUT */
                        DO R1 = MIDDLE BY 1 TO RIGHT;/* FIRST FIND OUT
                                       IF ALL THE KEYS ARE THE SAME */
                           IF KEYS(R1) ^= COMP THEN
                              GOTO XRIGHT;/* FOUND A NON-DUPLICATE KEY
                                       */
                        END;
                        R1=RIGHT;      /* RESET R1 */
                        LEAVE;         /* ALL DUPLICATE RIGHT KEYS */
 XRIGHT:
                        SUB_PARTITION_R=R1;/* NEXT SPACE AVAILABLE FOR
                                       SWAPPING KEYS */
                        DO R1 = SUB_PARTITION_R+1 TO RIGHT BY 1;
                           IF KEYS(R1) = COMP THEN
                              DO;      /* DUPLICATE KEYS, EXCHANGE */
                                 TEMPKEY=KEYS(SUB_PARTITION_R);
                                 KEYS(SUB_PARTITION_R)=KEYS(R1);
                                 KEYS(R1)=TEMPKEY;
                                 SUB_PARTITION_R=
                                 SUB_PARTITION_R+1;
                              END;
                        END;
                     END;
 %SKIP(4);
                  IF (SUB_PARTITION_L-LEFT) < (RIGHT-SUB_PARTITION_R)
                  THEN
                     DO;               /* STACK REQUEST FOR SORTING
                                       LARGER PARTITION AND CONTINUE
                                       SORTING THE SMALLER PARTITION */
                        IF SUB_PARTITION_R < RIGHT THEN
                           DO;         /* STACK REQUEST FOR SORTING
                                       RIGHT PARTITION */
                              STACKINDEX=STACKINDEX+1;
                              STACKL(STACKINDEX)=SUB_PARTITION_R;
                              STACKR(STACKINDEX)=RIGHT;
                           END;
                        RIGHT=SUB_PARTITION_L;/* CONTINUE SORTING LEFT
                                       PARTITION */
                     END;
                  ELSE
                     DO;
                        IF LEFT < SUB_PARTITION_L THEN
                           DO;         /* STACK REQUEST FOR SORTING
                                       LEFT PART */
                              STACKINDEX=STACKINDEX+1;
                              STACKL(STACKINDEX)=LEFT;
                              STACKR(STACKINDEX)=SUB_PARTITION_L;
                           END;
                        LEFT=SUB_PARTITION_R;/* CONTINUE SORTING RIGHT
                                       PARTITION */
                     END;
               END;                    /* OF PARTITION LOOP */
         END;
      END;
      RETURN;
   END;                                /* OF XXQSRT1 */
