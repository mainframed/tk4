*              /* LEVEL=0002                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXEXPCN: /*CONVERTS NUMBER TO GRAPHIC FOR PLOTTING*/
   PROC(INNUMBER) REORDER RETURNS(CHAR(1)) OPTIONS(REENTRANT);
 %SKIP(4);
 /********************************************************************/
 /*   XXEXPCN CONVERTS A BINARY NUMBER INTO A CHARACTER FOR          */
 /*   PLOTTING AS FOLLOWS:         ×  THE METHOD USED IS A MODIFIED  */
 /*   0-9 MAP INTO BLANK,1,...,9   ×  BINARY SEARCH WHICH WILL       */
 /*   10-19 MAP INTO 'A'.          ×  TAKE A MAXIMUM OF 5 ATTEMPTS   */
 /*   20-39 MAP INTO 'B' ETC.      ×  TO FIND THE REQUIRED ENTRY.    */
 /********************************************************************/
 /*   LEVL(0002)  30 JAN 1979.  CHANGES:                             */
 /*   CORRECTION OF VALUE RANGES TABLE.                              */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
   DEFAULT
      RANGE(*) FIXED;
 %SKIP(4);
 /*PARAMETERS*/
 %SKIP;
   DCL
      INNUMBER BIN(31);                /* ARGUMENT TO THE PROCEDURE */
 %SKIP(4);
 /*CONSTANTS*/
 %SKIP;
   DCL
      MAXNOS(0:38) BIN(31) STATIC INIT(-2147483648,0,1,2,3,4, 5,6, 7,8,
   9,19,39,79,159,319,639,1279,2559,5119,10239,20479,40959, 81919,
   163839 , 327679, 655359, 1310719, 2621439, 5242879, 10485759,
   20971519, 41943039, 83886079, 167772159, 335544319, 671088640,
   1342177279, 2147483647);            /* UPPER BOUNDS OF VALUE RANGES
                                       */
   DCL
      EXPCHARSET CHAR(37) STATIC INIT(
   ' 123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ$') ;/* CHARACTERS TO BE
                                       RETURNED FOR EACH VALUE RANGE */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      (CURRENT,                        /* CURRENT INDEX TO MAXNOS */
      INCREMENT,                       /* CURRENT INCREMENT IN BINARY
                                       SEARCH */
      TEMPINDEX) BIN(15);              /* TEMPORARY INDEX TO MAXNOS */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      (MIN,
      SUBSTR) BUILTIN;
 %SKIP(4);
      CURRENT=0;                       /* FIRST SEARCH INDEX */
      INCREMENT=32;                    /* HIGHEST POWER OF TWO THAT
                                       WILL FIT */
      DO UNTIL(INCREMENT=0);
         TEMPINDEX=MIN(CURRENT+INCREMENT,37);/* MAKE SURE THAT IT WILL
                                       NOT GO OUTSIDE SUBSCRIPTRANGE */
         IF INNUMBER > MAXNOS(TEMPINDEX) THEN
            CURRENT=TEMPINDEX;         /* TO BIG */
         ELSE
            IF INNUMBER > MAXNOS(TEMPINDEX-1) THEN
               LEAVE;                  /* IN RIGHT RANGE */
         INCREMENT=INCREMENT/2;        /* DIVIDE INCREMENT RANGE */
      END;
      RETURN(SUBSTR(EXPCHARSET,TEMPINDEX,1));
   END;                                /* OF XXEXPCN */
