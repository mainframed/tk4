*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXMAXEL:
   PROC(FROM_ARRAY, TO_LIST, ROW_MAX, COL_MAX, SIZ)
   OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*   XXMAXEL WILL RETURN IN ROW_MAX AND COL_MAX THE MAXIMUM         */
 /*   ELEMENTS OF THE ROWS AND COLUMNS RESPECTIVELY OF THE           */
 /*   SPARSE MATRIX DESCRIBED BY FROM_ARRAY AND TO_LIST.             */
 /*   FROM_ARRAY IS A AN ARRAY OF INDICES.  EACH INDEX POINTS TO     */
 /*   THE ELEMENT IN TO_LIST WHICH IS THE HEAD OF THE ROW. IF THERE  */
 /*   ARE NO ELEMENTS IN THE ROW, THEN THE CORRESPONDING ELEMENT     */
 /*   OF FROM_ARRAY IS -1.  IN TO_LIST, THE COLUMN NUMBERS ARE       */
 /*   GIVEN BY COLUMN_INDEX, THE VALUE OF EACH ENTRY BY              */
 /*   COLUMN VALUE, AND THE POINTER TO THE NEXT ENTRY IN THE         */
 /*   ROW BY NEXT_COLUMN.  AT THE END OF THE CHAIN, THE              */
 /*   NEXT_COLUMN ENTRY IS -1 AS A FLAG.                             */
 /********************************************************************/
 /*   LEVL(0001) 5 MARCH 1979.  CHANGES:                             */
 /*   COMPLETELY REWRITTEN TO USE REVISED STORAGE ALLOCATION         */
 /*   OUTLINED ABOVE.                                                */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
   DEFAULT
      RANGE(*) FIXED;
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      FROM_ARRAY (0:827) BIN(15) CONN; /* ARRAY OF INDICES TO THE
                                       ENTRIES IN TO_LIST
                                       CORRESPONDING TO THE BEGINNING
                                       OF THE ROWS OF THE VIRTUAL
                                       ARRAY */
   DCL
      1 TO_LIST(*) CONN,               /* ENTRIES IN THE ARRAY */
         2 COLUMN_INDEX BIN(15),       /* COLUMN NUMBER */
         2 COLUMN_VALUE BIN(15),       /* VALUE OF ENTRY */
         2 NEXT_COLUMN BIN(15);        /* INDEX OF NEXT VALUE IN CHAIN
                                       */
   DCL
      ROW_MAX(*) BIN(15) CONN;         /* USED TO RETURN MAXIMUM ROW
                                       VALUES */
   DCL
      COL_MAX(*) BIN(15) CONN;         /* USED TO RETURN MAXIMUM
                                       COLUMN VALUES */
   DCL
      SIZ BIN(15);                     /* UPPER BOUND OF ROW_MAX AND
                                       COL_MAX */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      LOOP FIXED BIN(15);              /* LOOP COUNTER */
   DCL
      CURRENT_ENTRY BIN(15);           /* INDEX TO CURRENT ENTRY IN
                                       TO_LIST */
   DCL
      CURRENT_MAX BIN(15);             /* TEMPORARY FOR CURRENT ROW
                                       MAXIMUM */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      MAX BUILTIN;
 %SKIP(4);
      COL_MAX=0;                       /* SMALLEST POSSIBLE VALUES */
      DO LOOP = 0 TO SIZ;              /* LOOP DOWN THE ROWS */
         CURRENT_MAX=0;                /* SMALLEST POSSIBLE VALUE */
         CURRENT_ENTRY=FROM_ARRAY(LOOP);/* FIRST COLUMN ENTRY */
         DO WHILE(CURRENT_ENTRY ^= -1);/* CHAIN DOWN TO END */
            CURRENT_MAX=
            MAX(CURRENT_MAX,COLUMN_VALUE(CURRENT_ENTRY));/*
                                       CURRENT_MAX HOLDS THE TEMPORARY
                                       ROW MAXIMUM */
            COL_MAX(COLUMN_INDEX(CURRENT_ENTRY))=
            MAX(COL_MAX(COLUMN_INDEX(CURRENT_ENTRY)),
             COLUMN_VALUE(CURRENT_ENTRY));/* UPDATE THE RELEVANT
                                       COLUMN MAXIMUM */
            CURRENT_ENTRY=NEXT_COLUMN(CURRENT_ENTRY);/* CHAIN ALONG TO
                                       NEXT ELEMENT IN THE ROW */
         END;
         ROW_MAX(LOOP)=CURRENT_MAX;    /* STORE CURRENT ROW MAXIMUM -
                                       OR ZERO IF NO ENTRIES FOUND IN
                                       ROW */
      END;
      RETURN;
   END;                                /* OF XXMAXEL */
