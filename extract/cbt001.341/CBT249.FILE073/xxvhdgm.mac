*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXVHDGM: /* HEADING FOR ARRAYS */
   PROC(INBINARY,OUTCHARS,HIGHEST) OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*  XXVHDGM TAKES THE ELEMENTS OF THE ARRAY INBINARY AND CONVERTS   */
 /*  THEM INTO VERTICAL FORMAT SUITABLE FOR PRINTING AS THE TOP      */
 /*  LABEL OF AN ARRY IE IN THE FORMAT       1 1 2  ETC              */
 /*  IN THE ARRAY OUTCHARS.              1 2 0 0 1                   */
 /*                                    0 0 0 0 5 5                   */
 /********************************************************************/
 /*   LEVL(0001)  6 MARCH 1979.  CHANGES:                            */
 /*   CHAINGING OF THE BOUNDS OF THE ARRAY INBINARY FROM             */
 /*   FIXED TO VARIABLE.                                             */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      INBINARY(*) FIXED BIN(15) CONN,  /* INPUT ARRAY */
      OUTCHARS(3) CHAR(100) CONN,      /* OUTPUT STRINGS */
      HIGHEST FIXED BIN(15);           /* HIGHEST INDEX IN INBINARY
                                       THAT IS TO BE CONVERTED */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      (LOOP,
      LOOPA) FIXED BIN(15),            /* LOOP CONTROL VARIABLES */
      DECEDIT PIC 'ZZ9',
      DECCHAR(3) CHAR(1) DEFINED DECEDIT;/* USED IN EDITING VALUES */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      SUBSTR BUILTIN;
 %SKIP(4);
      OUTCHARS='';                     /* BLANK OUT STRINGS */
      DO LOOP = 0 TO HIGHEST;
         DECEDIT =INBINARY(LOOP);      /* EDIT VALUE INTO TEMPORARY
                                       PICTURE VARIABLE */
         DO LOOPA = 1 TO 3;
            SUBSTR(OUTCHARS(LOOPA),(LOOP*2)+2,1)=DECCHAR(LOOPA);/*
                                       PICK OFF INDIVIDUAL CHARACTERS
                                       AND PUT THEM IN OUTCHARS */
         END;
      END;
      RETURN;
   END;                                /* OF XXVDHGM */
