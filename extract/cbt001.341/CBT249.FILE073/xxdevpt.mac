*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXDEVPT: /* PRINT DEVICE DATA */
   PROC OPTIONS(MAIN,REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*   JLPDEVS TESTS THE ACCURACY OF A DEVICE INFORMATION TABLE AS    */
 /*   USED IN XXDISKMP BY PRINTING THE INFORMATION IN THE TABLE      */
 /*   SO THAT IT CAN BE COMPARED WITH THE ORIGINAL.                  */
 /*                                                                  */
 /*   THE FORMAT OF THE TABLE IS:                                    */
 /*   AN ARRAY (0:4095) FIXED BIN(15) WHERE THE ENTRIES HAVE         */
 /*   THREE SEPARATE MEANINGS DEPENDING ON WHETHER THEY ARE          */
 /*   POSITIVE, ZERO OR NEGATIVE.                                    */
 /*                                                                  */
 /*   POSITIVE:  THE DEVICE TYPE IS                                  */
 /*              1 - 3330 MODEL I                                    */
 /*              2 - 3330 MODEL II                                   */
 /*              3 - 3350                                            */
 /*              4 - 3350 FIXED HEADS OVER CYLINDERS 1 & 2           */
 /*                                                                  */
 /*   ZERO:      THERE IS NO DEVICE ATTACHED AT THIS ADDRESS         */
 /*              OR THE DEVICE ATTACHED AT THIS ADDRESS IS NOT       */
 /*              A DIRECT ACCESS DEVICE.  IN ANY CASE THE            */
 /*              PROGRAM IS NOT INTERESTED IN ANY ACTIONS            */
 /*              CONCERNING THIS DEVICE.                             */
 /*                                                                  */
 /*   NEGATIVE:  THIS ADDRESS IS THE ALTERNATE PATH FOR THE          */
 /*              DEVICE WHICH HAS A MAIN PATH AT                     */
 /*              -(THE ENTRY VALUE).                                 */
 /*                                                                  */
 /*   THE PROGRAM SCANS THROUGH THE DEVICE TABLE AND PRODUCES        */
 /*   A PRINTED REPORT SIMILAR TO THAT PRODUCED BY XXHEXPT,          */
 /*   EXCEPT THAT THE DEVICE TYPE IS FILLED IN, WHERE A DEVICE       */
 /*   EXISTS.  IF THE ENTRY IS ILLEGAL (IE >4) A STRING OF           */
 /*   ASTERISKS IS PRINTED INSTEAD OF THE DEVICE TYPE.               */
 /*   IF THE ENTRY IS AN ALTERNATE PATH, THEN THE DEVICE TYPE ON     */
 /*   THE MAIN PATH IS PRINTED, OR ASTERISKS IF THE ENTRY FOR THE    */
 /*   MAIN PATH IS EITHER ZERO, >4, OR NEGATIVE SIGNIFYING ANOTHER   */
 /*   ALTERNATE PATH.                                                */
 /********************************************************************/
 /*   LEVL(0001) 12 MARCH 1979.  CHANGES:                            */
 /*   REVISED AND REWRITTEN TO LOSE ONE EXTERNAL PROCEDURE           */
 /*   XXNWPGF.                                                       */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* CONSTANTS */
 %SKIP;
   DCL
      DEVCHAR(4) STATIC CHAR(7) INIT( '3330   ','3330-II', '3350   ',
   '3350-FH');                         /* DEVICE TYPES IN CHARACTERS */
 %SKIP(4);
 /* FILE */
 %SKIP;
   DCL
      PRINT FILE EXT STREAM OUTPUT PRINT;/* PRINTER FILE */
 %SKIP(4);
 /* ENTRY */
 %SKIP;
   DCL
      XXCONVH ENTRY(FIXED BIN(15)) RETURNS(CHAR(3)) REDUCIBLE;/*
                                       CONVERTS BINARY NUMBER
                                       CONTAINING DEVICE ADDRESS TO
                                       PRINTABLE HEX CHARACTERS */
   DCL
      XXGOODM ENTRY() RETURNS(POINTER);/* PROCEDURE CONTAINING THE
                                       DEVICE TABLE TO BE PRINTED */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      (YLINE,
      ZLINE) CHAR(132),                /* PRINT LINE BUFFERS */
      (LOOP1,
      LOOP2,
      LOOP3) FIXED BIN(15),            /* LOOP COUNTERS */
      (ADDR,                           /* TEMPORARY FOR CURRENT
                                       ADDRESS */
      TEMPBIN) FIXED BIN(15);          /* BINARY TEMPORARY */
   DCL
      DEVPTR POINTER;                  /* POINTER TO DEVICE
                                       INFORMATION */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      (MOD,
      SUBSTR) BUILTIN;
 %SKIP(4);
 /* BASED VARIABLE TEMPLATES */
 %SKIP;
   DCL
      DEVSIN(0:4095) FIXED BIN(15) BASED(DEVPTR);/* DEVICE TABLE
                                       ALLOCATED BY XXGOODM */
 %SKIP(4);
      ADDR=-1;                         /* ADDR COUNTS UP ALONG THE
                                       ADDRESSES, SET IT TO -1 AS
                                       INITIAL VALUE */
      DEVPTR=XXGOODM();                /* GET DEVICE INFORMATION TO
                                       BE PRINTED */
      OPEN FILE(PRINT) PAGESIZE(99) LINESIZE(132);/* MAKE SURE PRINT
                                       IS OPENED WITH A LINESIZE BIG
                                       ENOUGH AND A PAGESIZE SO LARGE
                                       THAT ENDPAGE IS NEVER RAISED */
 %SKIP;
      DO LOOP1 = 0 TO 15;              /* COUNT UP CHANNELS */
         PUT FILE(PRINT) EDIT (
          'DEVICE PRINT FOR NWB DISK ACTIVITY MAPPER', 'PAGE ',
         LOOP1+1, '=========================================') (
          PAGE, A(41), COL(120), A(5), F(3), SKIP, A(41) );/* OUTPUT
                                       PAGE HEADING */
         PUT FILE(PRINT)
         EDIT('CHANNEL # ',SUBSTR('0123456789ABCDEF',(LOOP1+1),1)) (
          SKIP(2),A(10),A(1));         /* CHANNEL NUMBER AT TOP */
 %SKIP;
         DO LOOP2 = 0 TO 15;           /* COUNT FOR SECOND DIGIT */
            YLINE,ZLINE='';            /* BLANK OUT BUFFERS */
 %SKIP;
            DO LOOP3 = 0 TO 15;        /* FOR EACH ADDRESS ACROSS THE
                                       PAGE */
               ADDR=ADDR+1;
               SUBSTR(YLINE,(LOOP3*8+1),3)=XXCONVH(ADDR);/* EDIT IN
                                       CHANNEL ADDRESS */
               TEMPBIN=DEVSIN(ADDR);   /* GET INFO FOR ADDRESS */
               IF TEMPBIN ^= 0 THEN
                  DO;                  /* THERE IS INFO FOR THIS
                                       DEVICE */
                     IF TEMPBIN > 0 THEN
                        DO;            /* THIS IS THE MAIN PATH FOR
                                       THE DEVICE */
                           IF TEMPBIN > 4 THEN
                              SUBSTR(ZLINE,(LOOP3*8+1),7)=((7)'*');/*
                                       INVALID DEVICE TYPE */
                           ELSE
                              SUBSTR(ZLINE,(LOOP3*8+1),7)=
                              DEVCHAR(TEMPBIN);/* ELSE OK, SO PUT IN
                                       DEVICE TYPE */
                        END;
                     ELSE
                        DO;            /* ALTERNATE PATH ADDRESS */
                           TEMPBIN=-TEMPBIN;/* GET MAIN PATH ADDRESS */
                           IF TEMPBIN > 4095 THEN
                              SUBSTR(YLINE,LOOP3*8+5,3)='***';/*
                                       INVALID MAIN PATH ADDRESS */
                           ELSE
                              DO;      /* VALID MAIN PATH ADDRESS */
                                 SUBSTR(YLINE,LOOP3*8+5,3)=
                                 XXCONVH(TEMPBIN);
                                 TEMPBIN=DEVSIN(TEMPBIN);/* GET DATA
                                       FOR MAIN PATH */
                                 IF (TEMPBIN <= 0)×(TEMPBIN > 4) THEN
                                    SUBSTR(ZLINE,LOOP3*8+1,7)=(
                                    (7)'*');/* INVALID MAIN PATH DATA */
                                 ELSE
                                    SUBSTR(ZLINE,LOOP3*8+1,7)=
                                    DEVCHAR(TEMPBIN);/* VALID MAIN
                                       PATH */
                              END;
                        END;
                  END;
            END;
            PUT FILE(PRINT) EDIT(YLINE,ZLINE) (
             SKIP(2),A(132),SKIP,A(132));
         END;
      END;
      RETURN;
   END;                                /* OF XXDEVPT */
