*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXHEXPT:
   PROC(UPPER_BOUND) OPTIONS(MAIN,REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*   XXHEXPT IS A PROGRAM WRITTEN TO HELP PREPARE DEVICE TABLES     */
 /*   XXDISKMP.  IT PRODUCES A TABLE OF ALL POSSIBLE DEVICE          */
 /*   ADDRESSES UP TO AND INCLUDING THE CHANNEL NUMBER SPECIFIED AS  */
 /*   AS THE EXEC PARM.  THIS PRINTOUT HAS ONE CHANNEL PER PAGE      */
 /*   AND IS USED TO COMPLETE A LIST OF DIRECT ACCESS DEVICES        */
 /*   TOGETHER WITH THEIR ALTERNATE PATHS.                           */
 /*                                                                  */
 /*   THE CHANNEL ID AS THE UPPER BOUND IN THE MAIN PARM SHOULD      */
 /*   BE ONE OF 1, 2, 3, 4, 5, 6, 7, 8, 9, A, B, C, D, E, F.         */
 /*   IF IT IS NOT OR THERE IS NO PARM, THE VALUE F WILL BE USED.    */
 /*                                                                  */
 /*   THE FIRST CHARACTER ONLY OF THE INPUT PARM STRING IS USED      */
 /*   ALL OTHERS, IF PRESENT, ARE IGNORED.                           */
 /********************************************************************/
 /*   LEVL(0001)  12 MARCH 1979.  CHANGES:                           */
 /*   RECOMMENTED.                                                   */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* EXECUTION TIME OPTIONS */
 %SKIP;
   DCL
      PLIXOPT CHAR(100) VARYING STATIC EXTERNAL INITIAL(
   'NOREPORT,ISASIZE(3K)');
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      UPPER_BOUND CHAR(100) VARYING;   /* EXEC PARM. FIRST CHARACTER
                                       IS USED AS THE CHANNEL ID OF
                                       THE HIGHEST CHANNEL IT IS
                                       DESIRED TO PRINT */
 %SKIP(4);
 /* FILES */
 %SKIP;
   DCL
      PRINT FILE OUTPUT STREAM PRINT;  /* OUTPUT FILE */
 %SKIP(4);
 /* CONSTANTS */
   DCL
      CHANNELS CHAR(16) STATIC INITIAL('0123456789ABCDEF');/* CHANNEL
                                       IDS */
   DCL
      CHANNEL_CHARS(0:15) CHAR(1) DEF(CHANNELS);/* CHANNEL IDS AS
                                       SEPARATE CHARACTERS */
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      (BUFFER1,
      BUFFER2) CHAR(132);              /* OUTPUT BUFFERS */
   DCL
      1 HEXLINE(16) DEF BUFFER1,       /* FIELDS ON FIRST BUFFER FOR
                                       HEX CHANNEL ADDRESSES PRINT */
         2 PADDING CHAR(5),            /* PADDING */
         2 CHANNEL_ADDRESS(3) CHAR(1); /* SPACE FOR CHANNEL ADDRESS */
   DCL
      1 DECLINE(16) DEF BUFFER2,       /* FIELDS ON SECOND BUFFER FOR
                                       PRINTING DECIMAL VALUES OF
                                       CHANNEL ADDRESS */
         2 PADDING CHAR(3),            /* PADDING */
         2 DECIMAL_VALUE PIC 'ZZZZ9' ; /* SPACE FOR DECIMAL VALUE OF
                                       CHANNEL ADDRESS TO MAKE
                                       ALTERNATE PATHS' CALCULATION OF
                                       MAIN PATHS EASY */
   DCL
      (LOOP1,
      LOOP2,
      LOOP3) FIXED BIN(15);            /* LOOP COUNTERS */
   DCL
      CURRENT_ADDRESS FIXED DEC(5) INIT(-1);/* CURRENT ADDRESS */
   DCL
      TOP_BIN FIXED BIN(15);           /* CHANNEL NUMBER OF HIGHEST
                                       CHANNEL TO BE PRINTED */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      (LENGTH,
      INDEX,
      SUBSTR) BUILTIN;
 %SKIP(4);
      IF LENGTH(UPPER_BOUND) = 0 THEN
         TOP_BIN=15;                   /* DEFAULT IF NO PARM OF ALL
                                       CHANNELS */
      ELSE
         DO;
            TOP_BIN=INDEX(CHANNELS,SUBSTR(UPPER_BOUND,1,1))-1;/* FIND
                                       CHANNEL NUMBER OF PARM */
            IF TOP_BIN <= 0 THEN
               TOP_BIN=15;             /* IF TOP_BIN <= 0 THEN AN
                                       INVALID PARM WAS SPECIFIED, SO
                                       SET DEFAULT TO ALL CHANNELS */
         END;
 %SKIP(4);
      OPEN FILE(PRINT) PAGESIZE(999) LINESIZE(132);/* OPEN THE PRINT
                                       FILE, FORCING THE LINESIZE TO
                                       132, AND SETTING THE PAGESIZE
                                       TO AN ARTIFICIALLY HIGH VALUE
                                       SO THAT ENDPAGE WILL NEVER BE
                                       RAISED */
 %SKIP(4);
      DO LOOP1 = 0 TO TOP_BIN;         /* THIS LOOP PRINTS ONE PAGE
                                       PER CHANNEL */
         PUT FILE(PRINT) EDIT('CHANNEL ', CHANNEL_CHARS(LOOP1) ) (
          PAGE, A(8), A(1) );          /* PAGE HEADING */
         DO LOOP2 = 1 TO 16;           /* THIS LOOP PRINTS TWO LINES
                                       GOING THROUGH EACH POSSIBLE
                                       SECOND DIGIT */
            BUFFER1,BUFFER2='';        /* CLEAR PRINT BUFFERS */
            DO LOOP3 = 1 TO 16;        /* THIS LOOP FILLS THE PRINT
                                       BUFFERS */
               CURRENT_ADDRESS=CURRENT_ADDRESS+1;/* STEP
                                       CURRENT_ADDRESS ON TO NEXT
                                       VALUE */
               CHANNEL_ADDRESS(LOOP3,1)=
               CHANNEL_CHARS(LOOP1);   /* FIRST HEX DIGIT OF CHANNEL
                                       ADDRESS */
               CHANNEL_ADDRESS(LOOP3,2)=
               CHANNEL_CHARS(LOOP2-1); /* SECOND HEX DIGIT OF CHANNEL
                                       ADDRESS */
               CHANNEL_ADDRESS(LOOP3,3)=
               CHANNEL_CHARS(LOOP3-1); /* THIRS HEX DIGIT OF CHANNEL
                                       ADDRESS */
               DECIMAL_VALUE(LOOP3)=CURRENT_ADDRESS;/* DECIMAL VALUE
                                       OF CURRENT ADDRESS */
            END;
            PUT FILE(PRINT) EDIT(BUFFER1,BUFFER2) (
             SKIP(2), A(132), SKIP, A(132) );/* OUTPUT ASSEMBLED LINE */
         END;
      END;
      RETURN;
   END;
