*              /* LEVEL=0000                                         */
*PROCESS X(S),A(S),AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(TIME),OF;
 XXCONVT: /* CONVERT A BIT(16) STRING TO 4 HEX CHARACTERS */
   PROC(BITIN) RETURNS(CHAR(4)) OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*  XXCONVT CONVERTS A BIT STRING OF LENGTH 16 INTO ITS             */
 /*  CHARACTER REPRESENTATION, AS HEX DIGITS                         */
 /********************************************************************/
 /*   LEVL(0000)  26 JANUARY 1979.                                   */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      BITIN BIT(16) ALIGNED;
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      LOOP FIXED BIN(15),              /* LOOP COUNTER */
      TEMPBIN FIXED BIN(31),           /* INTERMEDIATE VALUE */
      TEMPBIT BIT(32) ALIGNED,         /* INTERMEDIATE VALUE */
      CHARSOUT CHAR(4);                /* TEMPORARY TO HOLD RESULT OF
                                       FUNCTION */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      (SUBSTR,
      UNSPEC,
      MOD) BUILTIN;
 %SKIP(4);
 /* FIRST CONVERT BIT STRING TO BINARY */
 %SKIP;
      TEMPBIT='0'B;
      SUBSTR(TEMPBIT,17,16)=BITIN;
      UNSPEC(TEMPBIN)=TEMPBIT;
 %SKIP(4);
 /* NOW CONVERT INDIVIDUAL HEX CHARACTERS */
 %SKIP;
      DO LOOP = 4 TO 1 BY -1;
         SUBSTR(CHARSOUT,LOOP,1) =
         SUBSTR('0123456789ABCDEF',MOD(TEMPBIN,16)+1,1);
         TEMPBIN=TEMPBIN/16;
      END;
      RETURN(CHARSOUT);
   END;                                /* OF XXCONVT */
