*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXMAXND:
   PROC(FROM_ARRAY, TO_LIST, ROW_MAX, COL_MAX, SIZ)
   OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*   XXMAXND WILL RETURN IN ROW_MAX AND COL_MAX THE MAXIMUM         */
 /*   IN THE ROWS AND COLUMNS RESPECTIVELY OF THE SPARSE ARRAY       */
 /*   DESCRIBED BY FROM_ARRAY AND TO_LIST, EXCEPT THAT DIAGONAL      */
 /*   ELEMENTS OF THE ARRAY WILL BE IGNORED IN THE COMPARISON.       */
 /*   FROM_ARRAY IS AN ARRAY OF INDICES, EACH ONE POINTING TO THE    */
 /*   ELEMENT IN TO_LIST WHICH IS THE HEAD OF THE ENTRIES FOR        */
 /*   THAT ROW.  THE COLUMN NUMBER FOR THE ENTRY IS GIVEN BY         */
 /*   THE COLUMN_INDEX PART OF THE ENTRY, THE VALUE BY THE           */
 /*   COLUMN VALUE PART OF THE ENTRY, AND THE NEXT COLUMN ENTRY      */
 /*   ENTRY IN THE CHAIN BY NEXT_COLUMN.  THE LAST COLUMN ENTRY      */
 /*   IN CHAIN HAS A NEXT_COLUMN FIELD OF -1.                        */
 /********************************************************************/
 /*   LEVL(0001)  5 MARCH 1979.  CHANGES:                            */
 /*   COMPLETELY REWRITTEN TO CATER FOR THE REVISED STORAGE          */
 /*   ALLOCATION SCHEME FOR THE SEEK ANALYSIS TABLE                  */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
   DEFAULT
      RANGE(*) FIXED;
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      FROM_ARRAY (0:827) BIN(15) CONN; /* ARRAY OF INDICES TO THE
                                       ENTRIES IN TO_LIST
                                       CORRESPONDING TO THE BEGINNING
                                       OF THE ROWS OF THE VIRTUAL
                                       ARRAY */
   DCL
      1 TO_LIST(*) CONN,               /* ENTRIES IN THE ARRAY */
         2 COLUMN_INDEX BIN(15),       /* COLUMN NUMBER */
         2 COLUMN_VALUE BIN(15),       /* VALUE OF ENTRY */
         2 NEXT_COLUMN BIN(15);        /* INDEX OF NEXT VALUE IN CHAIN
                                       */
   DCL
      ROW_MAX(*) BIN(15) CONN;         /* USED TO RETURN MAXIMUM ROW
                                       VALUES */
   DCL
      COL_MAX(*) BIN(15) CONN;         /* USED TO RETURN MAXIMUM
                                       COLUMN VALUES */
   DCL
      SIZ BIN(15);                     /* UPPER BOUND OF ROW_MAX AND
                                       COL_MAX */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      LOOP FIXED BIN(15);              /* LOOP COUNTER */
   DCL
      CURRENT_ENTRY BIN(15);           /* INDEX TO CURRENT ENTRY IN
                                       TO_LIST */
   DCL
      CURRENT_MAX BIN(15);             /* TEMPORARY FOR CURRENT ROW
                                       MAXIMUM */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      MAX BUILTIN;
 %SKIP(4);
      COL_MAX=0;                       /* SMALLEST POSSIBLE VALUES */
      DO LOOP = 0 TO SIZ;              /* LOOP DOWN THE ROWS */
         CURRENT_MAX=0;                /* SMALLEST POSSIBLE VALUE */
         CURRENT_ENTRY=FROM_ARRAY(LOOP);/* FIRST COLUMN ENTRY */
         DO WHILE(CURRENT_ENTRY ^= -1);/* CHAIN DOWN TO END */
            IF COLUMN_INDEX(CURRENT_ENTRY) ^= LOOP THEN
               DO;                     /* OK - THE ELEMENT IS NOT ON A
                                       DIAGONAL */
                  CURRENT_MAX=
                  MAX(CURRENT_MAX,COLUMN_VALUE(CURRENT_ENTRY));/*
                                       UPDATE CURRENT ROW MAXIMUM */
                  COL_MAX(COLUMN_INDEX(CURRENT_ENTRY))=
                  MAX(COL_MAX(COLUMN_INDEX(CURRENT_ENTRY)),
                   COLUMN_VALUE(CURRENT_ENTRY));/* UPDATE REQUISITE
                                       COLUMN MAXIMUM */
               END;
            CURRENT_ENTRY=NEXT_COLUMN(CURRENT_ENTRY);/* CHAIN DOWN */
         END;
         ROW_MAX(LOOP)=CURRENT_MAX;    /* STORE ROW MAXIMUM OR ZERO IF
                                       NO ELEMENTS FOUND IN THE ROW */
      END;
      RETURN;
   END;                                /* OF XXMAXND */
