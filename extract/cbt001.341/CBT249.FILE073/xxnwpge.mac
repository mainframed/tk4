*              /* LEVEL=0007                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXNWPGE: /* START NEW PAGE AND PRINT HEADING */
   PROC OPTIONS(REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*  XXNWPGE PRINTS A HEADING ON THE TOP OF EACH PAGE AND KEEPS A    */
 /*  COUNT OF THE PAGE NUMBER.  ENTRY XXNWPG1 SETS UP THE HEADINGS   */
 /*  AND PERFORMS GENERAL INITIALISATION.                            */
 /********************************************************************/
 /*   LEVL(0007)  25 APRIL 1979.  CHANGES:                           */
 /*   VERSION NUMBER CHANGE TO 2.1.0.                                */
 /********************************************************************/
 /*   LEVL(0006)  13 MARCH 1979.  CHANGES:                           */
 /*   VERSION NUMBER CHANGE TO 2.0.0.                                */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
 /* OF ENTRY XXNWPG1 */
 %SKIP;
   DCL
      MEASURE_DATE CHAR(9),            /* DATE ON FIRST TIMESTAMP
                                       RECORD */
      MEASURE_TIME CHAR(12),           /* TIME ON FIRST TIMESTAMP
                                       RECORD */
      CENTRE FIXED BIN(15);            /* CENTRE AT WHICH MEASUREMENTS
                                       WERE MADE - 1=GOODMANS,
                                       2=KEGWORTH */
 %SKIP(4);
 /* CONSTANTS */
 %SKIP;
   DCL
      MONTHS(12) CHAR(3) INIT('JAN', 'FEB', 'MAR', 'APR', 'MAY', 'JUN',
   'JUL', 'AUG', 'SEP', 'OCT', 'NOV', 'DEC');/* CHARACTERS FOR EDITING
                                       MONTHS */
 %SKIP(4);
 /* PRIVATE */
 %SKIP;
   DCL
      1 HEADING CTL,                   /* SKELETON HEADINGS FOR PAGES */
         2 HEADING1 CHAR(129) INIT( (
   'NWB   D I S K   A C T I V I T Y   M A P P E R  ' ××
   'MEASUREMENT DATE DD MMM YY     ANALYSIS DATE DD MMM YY ' ××
   '      KEGWORTH        PAGE ')),
         2 HEADING2 CHAR(132) INIT(( (45)'=' ××
   '  FIRST RECRD TIME HH.MM.SS.TTT           TIME ' ××
   'HH.MM.SS.TTT     CENTRE    VERSION 2.1.0'));
 %SKIP(4);
 /* EXTERNAL */
 %SKIP;
   DCL
      1 PRINTFL CTL EXT,               /* INFORMATION ABOUT CURRENT
                                       STATE OF FILE PRINT */
         2 LINES FIXED BIN(15) INIT(0), /* LINE NUMBER IN CURRENT PAGE
                                       */
         2 MAXLINES FIXED BIN(15) INIT(59), /* MAXIMUM NUMBER OF LINES
                                       PER PAGE */
         2 PAGE# FIXED DEC(3) INIT(1); /* CURRENT PAGE NUMBER */
 %SKIP(4);
 /* FILE */
 %SKIP;
   DCL
      PRINT FILE EXTERNAL STREAM PRINT;/* MAIN OUTPUT FILE */
 %SKIP(4);
      PAGE#=PAGE#+1;
      PUT FILE(PRINT) EDIT(HEADING1, PAGE#, HEADING2) (
       PAGE,A(129),P'ZZ9',SKIP,A(132));
      LINES=3;                         /* RESET LINE NUMBER IN NEW
                                       PAGE */
      RETURN;
 %PAGE;
 XXNWPG1: /* ENTRY TO SET UP HEADINGS */
      ENTRY(MEASURE_DATE,MEASURE_TIME,CENTRE);
 %SKIP(4);
      BEGIN;                           /* NOTHING BETTER THAN A NICE
                                       BEGIN BLOCK TO GET SOME
                                       VARIABLES THAT WE ONLY NEED FOR
                                       THIS ENTRY */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      TIMECHAR CHAR(12),
      TIMEDIT PIC '99.99.99.999' DEF TIMECHAR;/* USED TO EDIT TIME OF
                                       FIRST RECORD INTO HEADING */
   DCL
      TIMEIN CHAR(9),
      TIMEOUT PIC '999999999' DEF TIMEIN;/* USED TO CONVERT TIME TO
                                       DECIMAL WITHOUT CALLING LIBRARY
                                       ROUTINE */
   DCL
      DATEIN CHAR(6) DEF TIMECHAR;     /* USED TO RECEIVE UNEDITED
                                       DATE FIELD. STORAGE CAN BE
                                       REUSED AS EDITING OF TIME AND
                                       DATE FROM PLI FORM INTO
                                       REQUIRED FORM DOES NOT TAKE
                                       PLACE SIMULTANEOUSLY */
   DCL
      YEARIN CHAR(2) DEF TIMECHAR POS(1), /* PICK OFF YEAR FIELD */
      MONTHIN PIC '99' DEF TIMECHAR POS(3), /* PICK OFF MONTH VALUE */
      DAYSIN PIC '99' DEF TIMECHAR POS(5);/* PICK OFF DAYS VALUE */
   DCL
      DAYOUT PIC 'Z9' DEF TIMEIN POS(1), /* EDIT IN DAYS */
      MONTHOUT CHAR(3) DEF TIMEIN POS(4), /* EDIT IN MONTHS */
      YEAROUT CHAR(2) DEF TIMEIN POS(8);/* EDIT IN YEARS */
   DCL
      TIMEDEC FIXED DECIMAL(9);        /* INTERMEDIATE VALUE */
 %SKIP(2);
 /* BUILT IN FUNCTIONS */
   DCL
      (TIME,
      DATE,
      SUBSTR) BUILTIN;
 %SKIP(2);
         ALLOCATE HEADING, PRINTFL;
         IF CENTRE=1 THEN
            SUBSTR(HEADING1, 109,8)='GOODMANS';/* KEGWORTH VALUE IS
                                       THE INITIAL VALUE */
         SUBSTR(HEADING1,65,9)=MEASURE_DATE;/* MEASUREMENT DATE */
         SUBSTR(HEADING2, 65,12)=MEASURE_TIME;/* MEASUREMENT TIME */
         TIMEIN=TIME();                /* ANALYSIS TIME */
         TIMEDEC=TIMEOUT;              /* CONVERT TO DECIMAL */
         TIMEDIT=TIMEDEC;              /* EDIT INTO PATTERN */
         SUBSTR(HEADING2, 93,12)=TIMECHAR;/* EDIT INTO HEADING */
         DATEIN=DATE();                /* GET TODAY'S DATE */
         TIMEIN='';                    /* CLEAR FIELD BEFORE EDITING
                                       DATE */
         YEAROUT=YEARIN;               /* EDIT IN YEAR */
         DAYOUT=DAYSIN;                /*  EDIT IN MONTHS */
         MONTHOUT=MONTHS(MONTHIN);     /* EDIT IN MONTHS */
         SUBSTR(HEADING1, 93,9)= TIMEIN;/* INSERT INTO HEADING */
         PUT FILE(PRINT) EDIT(HEADING1, PAGE#, HEADING2) (
          A(129),P 'ZZ9', SKIP,A(132));/* PUT FIRST HEADING OUT */
         LINES=3;
         RETURN;
      END;                             /* OF BEGIN BLOCK */
   END;                                /* OF XXNWPGE */
