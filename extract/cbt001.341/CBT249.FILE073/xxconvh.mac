*              /* LEVEL=0000                                         */
*PROCESS X(S),A(S),AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(TIME),OF;
 XXCONVH: /* CONVERT ADDRESSES TO HEX */
   PROC(INBIN) RETURNS(CHAR(3)) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*  XXCONVH CONVERTS A BINARY (ASSUMED TO BE BETWEEN 0 AND 4095     */
 /*  DECIMAL INTO A HEX NUMBER IN CHARACTER FORM SUITABLE FOR        */
 /*  PRINTING.                                                       */
 /********************************************************************/
 /*   LEVL(0000)  26 JANUARY 1979.                                   */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      INBIN FIXED BIN(15);
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      (TEMPBIN,                        /* TEMPORARY FOR EVALUATING
                                       INTERMEDIATE VALUES */
      LOOP) FIXED BIN(15);             /* LOOP COUNTER */
   DCL
      CHAR3 CHAR(3);                   /* USED TO ACCUMULATE OUTPUT
                                       VALUE */
 %SKIP(4);
 /* BUILT IN FUNCTIONS */
 %SKIP;
   DCL
      (MOD,
      SUBSTR) BUILTIN;
 %SKIP(4);
      TEMPBIN=INBIN;
      DO LOOP = 3 TO 1 BY -1;
         SUBSTR(CHAR3,LOOP,1)=
         SUBSTR('0123456789ABCDEF',MOD(TEMPBIN,16)+1,1);
         TEMPBIN=TEMPBIN/16;
      END;
      RETURN(CHAR3);
   END;                                /* OF XXCONVH */
