*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXGETRM: /* INPUT ROUTINE FOR XXDEVCM */
   PROC(GTIME, GTYPE, GINFO) OPTIONS(REENTRANT)
   RETURNS(BIT(1) ALIGNED) REORDER;
 %SKIP(2);
 /********************************************************************/
 /*   XXGETRM IS THE INPUT ROUTINE FOR XXDEVCM. IT READS IN THE      */
 /*   RECORDS AND SORTS OUT THE RECORD TYPES, PROCESSING THE         */
 /*   TIME-STAMP FIELD FOR EACH ONE.                                 */
 /********************************************************************/
 /*   LEVL(0001)  5 MARCH 79.  CHANGES:                              */
 /*   NEW CODE TO DISTINGUISH SENSE START IOS FROM ORDINARY START    */
 /*   BY A SEEK ADDRESS OF CYLINDER 0, TRACK 0, RECORD 0.            */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(2);
   DEFAULT
      RANGE(*) FIXED;                  /*NO FLOATS PLEASE*/
 %SKIP(2);
 /*PARAMETERS*/
   DCL
      GTIME BIN(31),                   /*RETURNS RELATIVE TIME OF THIS
                                       RECORD. (WRITE)*/
      GTYPE BIN(15),                   /*RETURNS TYPE OF RECORD: (1)
                                       TIMESTAMP, (2) LOST BLOCK LOST
                                       EVENT BUFFER FULL RECORDS, (3)
                                       START IO TRACE RECORD, (4) IO
                                       TRACE RECORD, (5) UNDEFINED OR
                                       UNSUPPORTED RECORD, (6) IO
                                       TRACE RECORD WITHOUT A VALID
                                       UCB, (7) PCI TRACE RECORD, (8)
                                       END OF SENSE TRACE RECORD. (9)
                                       SENSE START IO RECORD,
                                       IDENTIFIED BY A SEEK TO
                                       CYLINDER 0 HEAD 0. THESE (1-4)
                                       CAUSE ACTIVE PROCESSING, (5-8)
                                       MERELY INCREMENT COUNTERS*/
      1 GINFO,                         /*INFORMATION ABOUT THE RECORD*/
         2 GSUB_TYPE BIN(15),          /*RECORD SUB-TYPE: USED FOR
                                       LOST BLOCK &C RECORDS: (1)
                                       BUFFER FULL, (2) LOST EVENT(S),
                                       (3) LOST BLOCK(S)*/
         2 GJOB_NAME CHAR(8),          /*JOB NAME FIELD ON THOSE
                                       RECORDS WHICH HAVE IT*/
         2 G_FLAG8(4) BIT(8) ALIGNED,  /*FLAGS - USED FOR TOPTIONS
                                       FIELD OF TIMESTAMP RECORD,
                                       G_FLAGS(1) IS USED FOR SIO
                                       CONDITION CODES, (SEE SIO_TRACE
                                       DECLARATION)*/
         2 G_FLAG16 BIT(16) ALIGNED,   /*USED TO RETURN STATUS FROM
                                       SIO RECORDS FOR DETERMINING THE
                                       REASONS FOR SIO CC1*/
         2 DEV_ADDR BIN(15),           /*DEVICE ADDRESS*/
         2 SEEK_ADDR BIN(15),          /*SEEK ADDRESS*/
         2 LOST_COUNT BIN(31);         /*LOST EVENT COUNT FOR LOST
                                       BLOCK &C RECORDS*/
 %SKIP;
   DCL
      CHTIME CHAR(12),                 /*USED TO RETURN TIME AS
                                       HH.MM.SS.TTT*/
      CHDATE CHAR(9);                  /*USED TO RETURN DATE AS DD MMM
                                       YY*/
 %PAGE;
 /* INPUT RECORD TEMPLATES */
   DCL
      1 LOST_BLOCK UNALIGNED BASED(FILEPTR),
         2 LRECIDEN BIT(8) ALIGNED,
         2 LFORMIDEN BIT(8) ALIGNED,
         2 LTIMEZONE FIXED BINARY(31),
         2 LTIMESTMP BIT(64) ALIGNED,
         2 LLOSTCOUNT FIXED BINARY(31);
 /*LOST_BLOCK  FORMAT OF LOST BLOCK & LOST EVENT RECORDS.      */
 /*LRECIDEN    RECORD IDENTIFIER  X'00' CONTROL RECORD         */
 /*LFORMIDEN   RECORD FORMAT IDENTIFIER  X'03', X'05' OR X'02  */
 /*            X'03' - LOST BLOCK.                             */
 /*            X'02' - LOST EVENT.                             */
 /*            X'05' - BUFFER FULL.                            */
 /*LTIMEZONE   TIME ZONE VALUE.                                */
 /*LTIMESTMP   TIME OF DAY CLOCK VALUE.                        */
 /*LLOSTCOUNT  LOST EVENT COUNT                                */
 %PAGE;
   DCL
      1 TIMESTAMP UNALIGNED BASED(FILEPTR),
         2 TRECIDEN BIT(8) ALIGNED,
         2 TFORMIDEN BIT(8) ALIGNED,
         2 TTIMEZONE FIXED BIN(31),
         2 TTIMESTAMP BIT(64) ALIGNED,
         2 TOPTIONS(4) BIT(8) ALIGNED;
 /*TIMESTAMP   FORMAT OF TIMESTAMP RECORDS.                    */
 /*TRECIDEN    RECORD IDENTIFIER. X'00' CONTROL RECORD.        */
 /*TFORMIDEN   FORMAT IDENTIFIER. X'01' TIMESTAMP RECORD.      */
 /*TTIMEZONE   TIME ZONE VALUE                                 */
 /*TTIMESTAMP  TIME OF DAY CLOCK VALUE.                        */
 /*TOPTIONS    4 OPTION BYTES.                                 */
 /*                                                            */
 /*TOPTIONS(1)                                                 */
 /*1.......    SYSM IN EFFECT.                                 */
 /*.1......    SYSP IN EFFECT.                                 */
 /*..1.....    SYS IN EFFECT..                                 */
 /*...1....    USR IN EFFECT.                                  */
 /*....1...    TRC IN EFFECT.                                  */
 /*.....1..    DSP IN EFFECT.                                  */
 /*......X.    RESERVED.                                       */
 /*.......1    PCI IN EFFECT.                                  */
 /*                                                            */
 /*TOPTIONS(2)                                                 */
 /*X.......    SVC IN EFFECT.                                  */
 /*.1......    SVCP IN EFFECT.                                 */
 /*..1.....    SIO IN EFFECT.                                  */
 /*...1....    SIOP IN EFFECT.                                 */
 /*....1...    PI IN EFFECT.                                   */
 /*.....1..    PIP IN EFFECT.                                  */
 /*......1.    IO IN EFFECT.                                   */
 /*.......1    IOP IN EFFECT.                                  */
 /*                                                            */
 /*TOPTIONS(3)                                                 */
 /*1.......    EXT IN EFFECT.                                  */
 /*.1......    RNIO IN EFFECT.                                 */
 /*..1.....    SRM IN EFFECT.                                  */
 /*...1....    FRR IN EFFECT.                                  */
 /*....XXX.    RESERVED.                                       */
 /*.......1    IO=SIO - DEVICES TRACED ARE THE SAME.           */
 /*                                                            */
 /*TOPTIONS(4)                                                 */
 /*XXXXXX..    RESERVED.                                       */
 /*......1.    TOD CLOCK IN SYSTEM.                            */
 /*.......1    USER TIMESTAMP IN EACH RECORD.                  */
 %PAGE;
   DCL
      1 IO_TRACE UNALIGNED BASED(FILEPTR),
         2 IRECIDEN BIT(8) ALIGNED,
         2 IFORMIDEN BIT(8) ALIGNED,
         2 ITIMESTMP BIT(64) ALIGNED,
         2 IEVENTID BINARY(15),
         2 IASCB BINARY(31),
         2 ICPUID BINARY(15),
         2 IJOBN CHAR(8),
         2 IIOOLDPSW BIT(64) ALIGNED,
         2 IRQETCB FIXED BINARY(31),
         2 ICSW BIT(64) ALIGNED,
         2 ICPRADD BIN(31),
         2 ICPVADD BIN(31),
         2 ISENSE BIT(16) ALIGNED,
         2 IDEVADDR BIN(15),
         2 ICTBADD BIN(31),
         2 IFLAGS CHAR(9);
 /*IO_TRACE    FORMAT OF IO COMPREHENSIVE TRACE RECORDS.       */
 /*IRECIDEN    RECORD IDENTIFIER - X'FF' TRACE RECORD.         */
 /*IFORMIDEN   FORMAT IDENTIFIER - X'00'.                      */
 /*ITIMESTMP   TIME OF DAY CLOCK VALUE.                        */
 /*IEVENTID    EVENT IDENTIFIER                                */
 /*            X'2100' PCI INTERRUPTION.                       */
 /*            X'5101' EOS INTERRUPTION.                       */
 /*            X'5201' IO INTERRUPTION - NO VALID UCB.         */
 /*            X'5200' IO INTERRUPTION - VALID UCB.            */
 /*IASCB       ADDRESS SPACE CONTROL BLOCK.                    */
 /*ICPUID      CPU ID.                                         */
 /*IOOLDPSW    I/O OLD PSW.                                    */
 /*IRQETCB     RQE TCB.                                        */
 /*ICSW        CHANNEL STATUS WORD.                            */
 /*ICPRADD     REAL ADDRESS OF CHANNEL PROGRAM.                */
 /*ICPVADD     VIRTUAL ADDRESS OF CHANNEL PROGRAM.             */
 /*ISENSE      SENSE BYTES.                                    */
 /*IDEVADDR    DEVICE ADDRESS.                                 */
 /*ICTBADD     ADDRESS OF PURGE CONTROL BLOCK.                 */
 /*IFLAGS      FLAGS                                           */
 %PAGE;
   DCL
      1 SIO_TRACE UNALIGNED BASED(FILEPTR),
         2 SRECIDEN BIT(8) ALIGNED,
         2 SFORMIDEN BIT(8) ALIGNED,
         2 STIMESTMP BIT(64) ALIGNED,
         2 SEVENTID BINARY(15),
         2 SASCB BINARY(31),
         2 SCPUID BINARY(15),
         2 SJOBN CHAR(8),
         2 SCPRADD BINARY(31),
         2 SCPVADD BINARY(31),
         2 SCTBADD BINARY(31),
         2 SSIOCC BIT(8) ALIGNED,
         2 SDEVADDR BIN(15),
         2 SCAW BIN(31),
         2 SSTATUS BIT(16) ALIGNED,
         2 SSEEK,
            3 MMM BIT(8) ALIGNED,
            3 BBB BIT(16) ALIGNED,
            3 CCC BIT(16) ALIGNED,
            3 HHH BIT(16) ALIGNED,
            3 RRR BIT(8) ALIGNED,
         2 SFLAGS CHAR(6);
 /*SIO_TRACE   FORMAT OF SIOTRACE RECORDS                      */
 /*SRECIDEN    RECORD IDENTIFIER - X'FF' TRACE RECORD.         */
 /*SFORMIDEN   FORMAT IDENTIFIER - X'00'                       */
 /*STIMESTMP   TMIE OF DAY CLOCL VALUE.                        */
 /*SEVENTID    EVENT IDENTIFIER X'5100' SIO.                   */
 /*SASCB       ADDRESS SPACE CONTROL BLOCK.                    */
 /*SCPUID      CPU ID.                                         */
 /*SJOBN       JOB NAME.                                       */
 /*SCPRADD     REAL ADDRESS OF CHANNEL PROGRAM.                */
 /*SCPVADD     VIRTUAL ADDRESS OF CHANNEL PROGRAM.             */
 /*SCTBADD     ADDRESS OF PURGE CONTROL BLOCK.                 */
 /*SSIOCC      SIO CONDITION CODES.                            */
 /*..00....    SUCCESSFUL SIO.                                 */
 /*..01....    CSW STORED.                                     */
 /*..10....    SUBCHANNEL OR CHANNEL BUSY.                     */
 /*..11....    NOT OPERATIONAL.                                */
 /*SDEVADDR    DEVICE ADDRESS.                                 */
 /*SCAW        CHANNEL ADDRESS WORD.                           */
 /*SSTATUS     STATUS.                                         */
 /*SSEEK       SEEK ADDRESS:                                   */
 /*MMM         EXTENT # WITHIN DEB.                            */
 /*BBB         BIN/CELL #.                                     */
 /*CCC         CYLINDER NUMBER.                                */
 /*HHH         HEAD NUMBER. (TRACK ADDRESS).                   */
 /*RRR         RECORD # WITHIN TRACK.                          */
 /*SFLAGS      FLAGS.                                          */
 %PAGE;
 /* PRIVATE */
   DCL
      1 PERM_INFO CONTROLLED,
         2 FILEPTR POINTER,            /* POINTER TO INPUT RECORD IN
                                       BUFFER */
         2 STORE_TIME BIN(31) INIT(0), /* KEEP TRACK OF RELATIVE TIME */
         2 DOREAD BIT(1) INIT('1'B);   /* FLAG TO STOP READING AT END
                                       OF FILE */
 %SKIP(2);
 /* LOCAL VARIABLES */
   DCL
      HEAD_ADDR FIXED BIN(15);         /* USED AS A TEMPORARY TO HOLD
                                       HEAD ADDRESS FOR START IO CC 0,
                                       SO THAT BEGINNING OF SENSE
                                       RECORDS CAN BE IDENTIFIED */
 %SKIP(2);
 /* FILE */
   DCL
      INFILE FILE BUFFERED INPUT;      /* INPUT FILE */
 %SKIP(2);
 /* BUILTIN FUNCTIONS */
   DCL
      UNSPEC BUILTIN;
 %SKIP(2);
 /* ENTRY */
   DCL
      XXTODHM ENTRY(BIT(64) ALIGNED,CHAR(9)) RETURNS(CHAR (12)),
      XXTODH2 ENTRY(BIT(64) ALIGNED) RETURNS(FIXED BIN(31));
 /*XXTODHM     TIME OF DAY CLOCK STAMP HANDLING ROUTINES.      */
 /*            ENTRY TODH1 TAKES THE TOD VALUE AND RETURNS THE */
 /*            TIME AS HH.MM.SS.TTT SETS THE SECOND PARAMETER  */
 /*            TO DD.MMM YY AND SETS THE RELATIVE TIME 0 TO    */
 /*            THE VALUE OF PARM1.                             */
 /*XXTODH2     TAKES AS ARGUMENT THE TOD CLOCK AND RETURNS     */
 /*            A BINARY NO GIVING RELATIVE TIME IN MSEC.       */
 %PAGE;
      ON ENDFILE(INFILE)
         GOTO EODAD;
 %SKIP(2);
      ON TRANSMIT(INFILE)
         GOTO END_BAD_DATA;
 %SKIP(2);
      IF DOREAD THEN
         READ FILE(INFILE) SET(FILEPTR);/*SECOND & SUBSEQUENT TIMES AT
                                       THIS ENTRY, READ A NEW RECORD.
                                       FIRST TIME WILL HAVE FIRST
                                       RECORD ALREADY IN PLACE*/
      DOREAD='1'B;
      IF SRECIDEN='00000000'B THEN
         SELECT(SFORMIDEN);            /*RECORD IDENTIFIER IS X'00'
                                       FOR A CONTROL RECORD OR X'FF'
                                       FOR TRACE RECORD*/
 /*SORT CONTROL RECORDS ACCORDING TO FORMAT IDENTIFIER*/
            WHEN('00000001'B)
               DO;                     /* X'01' TIMESTAMP RECORD*/
                  G_FLAG8=TOPTIONS;    /*USER OPTIONS IN EFFECT*/
                  GTIME=XXTODH2(TTIMESTAMP);/*TOD STAMP*/
                  GTYPE=1;
                  GOTO TEST_AND_RETURN;/* NOT REACHED END OF FILE */
               END;
            WHEN('00000010'B,'00000101'B,'00000011'B)
               DO;                     /*LOST DATA RECORDS- X'02' -
                                       LOST EVENT, *'03' - LOST BLOCK,
                                       X'05' - BUFFER FULL*/
                  GTYPE=2;             /* LOST DATA RECORD */
                  LOST_COUNT=LLOSTCOUNT;/*# OF EVENTS LOST*/
                  GTIME=XXTODH2(LTIMESTMP);/*TIME*/
                  SELECT(SFORMIDEN);   /*SELECT ACCORDING TO WHICH IT
                                       IS FOR SUB-TYPE*/
                     WHEN('00000010'B)
                        GSUB_TYPE=2;   /*LOST EVENT*/
                     WHEN('00000011'B)
                        GSUB_TYPE=3;   /*LOST BLOCK*/
                     WHEN('00000101'B)
                        GSUB_TYPE=1;   /*BUFFER FULL*/
                  END;
                  GOTO TEST_AND_RETURN;/* NOT REACHED END OF FILE */
               END;
            OTHERWISE
               GOTO DONT_KNOW;         /*ELSE WE CAN'T IDENTIFY IT*/
         END;
      ELSE
         IF SRECIDEN = '11111111'B THEN
            IF SFORMIDEN = '00000000'B THEN
               SELECT(SEVENTID);       /*OTHERWISE RECORD IDENT SHOULD
                                       BE X'FF' AND FORMAT IDENT
                                       SHOULD BE X'00'. SELECT FURTHER
                                       ON EVENT ID*/
                  WHEN(20993)
                     DO;               /*X'5201' IO TRACE RECORD - IO
                                       INTERRUPT WITHOUT A VALID UCB*/
                        GTIME=XXTODH2(ITIMESTMP);/* TIME */
                        GJOB_NAME=IJOBN;/* INSERT JOB NAME */
                        GTYPE=6;
                        GOTO TEST_AND_RETURN;/* NOT END OF FILE YET */
                     END;
                  WHEN(20992)
                     DO;               /*X'5200' IO INTERRUPT - VALID
                                       UCB*/
                        DEV_ADDR=IDEVADDR;/*DEVICE ADDRESS*/
                        GJOB_NAME=IJOBN;/*JOB NAME*/
                        GTIME=XXTODH2(ITIMESTMP);/*RELATIVE TIME*/
                        GTYPE=4;
                        GOTO TEST_AND_RETURN;/* NOT END OF FILE YET */
                     END;
                  WHEN(20736)
                     DO;               /*X'5100' SIO TRACE RECORD*/
                        GTYPE=3;
                        GJOB_NAME=IJOBN;/* INSERT JOB NAME */
                        DEV_ADDR=SDEVADDR;/*DEVICE ADDRESS*/
                        UNSPEC(SEEK_ADDR)=SSEEK.CCC;/*SEEK ADDRESS*/
                        G_FLAG8(1)=SSIOCC;/*SIO CONDITION CODE*/
                        G_FLAG16=SSTATUS;/*DEVICE STATUS FOR CC1*/
                        GTIME=XXTODH2(STIMESTMP);/*TOD CLOCK*/
                        IF (SSIOCC&'00110000'B) THEN
                           ;           /* NOT CONDITION CODE 0 */
                        ELSE
                           IF (SEEK_ADDR=0) THEN
                              DO;
                                 UNSPEC(HEAD_ADDR)=SSEEK.HHH;/*
                                       EXTRACT HEAD ADDRESS */
                                 IF HEAD_ADDR=0 THEN
                                    GTYPE=9;/* IF START IO CC0 AND
                                       SEEK ADDRESS IS TO CYLINDER 0
                                       HEAD 0 THEN ASSUME THAT IT IS A
                                       SENSE COMMAND */
                              END;
                        GOTO TEST_AND_RETURN;/* NOT END OF FILE YET */
                     END;
                  WHEN(20737)
                     DO;               /*X'5101' END OF SENSE
                                       INTERRUPT - MERELY COUNTED */
                        GTYPE=8;
                        GTIME=XXTODH2(ITIMESTMP);/* REL TIME */
                        GOTO TEST_AND_RETURN;/* NOT END OF FILE YET */
                     END;
                  WHEN(8448)
                     DO;               /*X'2100 PCI INTERRUPT - MERELY
                                       COUNTED*/
                        GTYPE=7;
                        GTIME=XXTODH2(ITIMESTMP);/*REL TIME */
                        GOTO TEST_AND_RETURN;/* NOT END OF FILE YET */
                     END;
                  OTHERWISE
                     GOTO DONT_KNOW;   /*ELSE RECORD TYPE IS NOT
                                       IDENTIFIED*/
               END;
 %SKIP(2);
 DONT_KNOW: /*CANT IDENTIFY RECORD TYPE*/
      GTYPE=5;
      RETURN('0'B);                    /*NOT  EOF YET*/
 %SKIP(4);
 TEST_AND_RETURN: /* TEST WHETHER TIME IS OUT OF RANGE OR NOT AND THEN
 RETURN */
      IF GTIME ^= -2147483647 THEN
         DO;
            STORE_TIME=GTIME;          /* SAVE RELATIVE TIME */
            RETURN('0'B);              /* IN RANGE */
         END;
      ELSE
         DO;                           /* TIME OUT OF RANGE */
            GTYPE=3;
            GTIME=STORE_TIME;          /* RETURN RELATIVE TIME OF LAST
                                       RECORD */
            RETURN('1'B);
         END;
 %SKIP(2);
 EODAD: /*END OF FILE RAISED*/
      GTIME=STORE_TIME;                /* TIME ON LAST RECORD OF
                                       IDENTIFIED TYPE */
      GTYPE=1;                         /* NORMAL TERMINATION */
      CLOSE FILE(INFILE);              /*CLOSE THE FILE*/
      DOREAD='0'B;                     /* SET FLAG TO INDICATE EOF */
      RETURN('1'B);                    /*WE HAVE REACHED END OF FILE*/
 %SKIP(2);
 END_BAD_DATA: /* TRANSMIT RAISED */
      GTIME=STORE_TIME;                /* TIME ON LAST RECORD OF
                                       IDENTIFIED TYPE */
      GTYPE=2;                         /* ABNORMAL TERMINATION */
      CLOSE FILE(INFILE);              /* CLOSE THE FILE */
      DOREAD='0'B;                     /* SET FLAG TO INDICATE EOF */
      RETURN('1'B);                    /* SET TERMINATOR FLAG */
 %PAGE;
 XXGETR1: /*ENTRY FOR SETTING UP & INITIALISATION*/
      ENTRY(CHTIME,CHDATE);
      ALLOCATE PERM_INFO;              /*ALLOCATE INFORMATION FIELDS
                                       THAT ARE USED TO MAINTAIN
                                       CONTROL INFORMATION*/
      DO UNTIL((TRECIDEN='00000000'B)&(TFORMIDEN='00000001'B));
         READ FILE(INFILE) SET(FILEPTR);/*READ RECORDS UNTIL WE GET A
                                       TIMESTAMP RECORD TO SIGNAL THE
                                       BEGINNING OF A BLOCK*/
      END;
      CHTIME=XXTODHM(TTIMESTAMP,CHDATE);/*CALL TODM TO SET UP BASE FOR
                                       RELATIVE TIMES AND RETURN TIME
                                       & DATE TO CALLING PROCEDURE*/
      DOREAD='0'B;                     /* THIS RECORD WILL DO FOR NEXT
                                       TIME TOO */
      RETURN;                          /*TO CALLING PROCEDURE*/
   END;                                /*OF XXGETRM*/
