*              /* LEVEL=0001                                         */
*PROCESS X,A,AG,S,STG,LIST,NEST,F(I),LMSG,MAP,ESD,OPT(2),OF;
 XXDEVMN: /* MAIN ROUTINE OF DEVICE ACTIVITY MAPPER */
   PROC(PARMSTRING) OPTIONS(MAIN,REENTRANT) REORDER;
 %SKIP(4);
 /********************************************************************/
 /*   XXDEVMN IS THE MAIN CONTROL PROGRAM OF THE DEVICE ACTIVITY     */
 /*   MAPPER.  IT FIRST CALLS XXPARSM IF THE PARM STRING PASSED TO   */
 /*   IT IS NOT NULL.                                                */
 /*   IT THEN CALLS XXGETRM AT ENTRY XXGETR1 TO SET UP RELATIVE      */
 /*   TIME USAGE AND ACQUIRE DATE AND TIME ON FIRST TIMESTAMP        */
 /*   RECORD.                                                        */
 /*   WITH THIS INFORMATION IT THEN CALLS XXNWPGE AT ENTRY XXNWPG1   */
 /*   TO OPEN PRINT FILE AND INITIALISE HEADING LINES.               */
 /*   THIS COMPLETES INITIALISATION.                                 */
 /*   THE MAIN ANALYSIS IS COMPLETED BY CALLING XXMAINL WHICH        */
 /*   READS THE INPUT FILE INFILE TO EXHAUSTION,PERFORMING THE       */
 /*   ANALYSIS AND PRINTING FINALLY RETURNING CONTROL TO THIS        */
 /*   MODULE, WHICH THEN RETURNS TO OS.                              */
 /********************************************************************/
 /*   LEVL(0001)  13 MARCH 1979.  CHANGES:                           */
 /*   INCLUSION OF PLIXOPT EXECUTION TIME OPTIONS.                   */
 /********************************************************************/
 /*   LEVL(0000)  26 JAN 1979.                                       */
 /********************************************************************/
 /* WRITTEN BY    JOHN H LISTER,                                     */
 /*               TECHNICAL SERVICES GROUP,                          */
 /*               DATA PROCESSING DEPARTMENT,                        */
 /*               NATIONAL WESTMINSTER BANK,                         */
 /*               GOODMANS FIELDS COMPUTER CENTRE,                   */
 /*               74 ALIE STREET, LONDON   E1 8HL.                   */
 /*                                                                  */
 /*               TELEPHONE: 01-488 0707 EXT 2766.                   */
 /********************************************************************/
 %SKIP(4);
 /* PARAMETERS */
 %SKIP;
   DCL
      PARMSTRING CHAR(100) VARYING;
 %SKIP(4);
 /* EXECUTION TIME OPTIONS */
 %SKIP;
   DCL
      PLIXOPT CHAR(100) VARYING STATIC INITIAL('NOREPORT,ISASIZE(155K)')
      ;                                /* NOTE THAT IT IS IMPORTANT
                                       THAT IF THE DEFAULT NUMBER OF
                                       DEVICES IS EXCEEDED THEN THE
                                       EXEC PARM SHOULD SPECIFY A
                                       LARGER ISASIZE, AS THIS CAN BE
                                       CRUCIAL TO THE PERFORMANCE OF
                                       THE PLI STORAGE ALLOCATION
                                       SCHEME. THE ISASIZE SHOULD BE
                                       25K PLUS 65K PER DEVICE TO BE
                                       MAPPED */
 %SKIP(4);
 /* ENTRY */
 %SKIP;
   DCL
      XXPARSM ENTRY(CHAR(100) VARYING,FIXED BIN(15),FIXED BIN(15), FIXED
       BIN(15),POINTER);               /* PARSE ROUTINE */
   DCL
      XXMAINL ENTRY(FIXED BIN(15),FIXED BIN(15), POINTER,CHAR(12));/*
                                       MAIN PROCESSING LOOP */
   DCL
      XXNWPG1 ENTRY(CHAR(9),CHAR(12), FIXED BIN(15));/* ENTRY TO
                                       INITIALISE HEADINGS IN XXNWPGE */
   DCL
      XXGETR1 ENTRY(CHAR(12),CHAR(9)); /* INITIALISATION ENTRY -
                                       RETURNS TIME AND DATE ON FIRST
                                       RECORD */
 %SKIP(2);
 /* FILES */
 %SKIP;
   DCL
      INFILE FILE BUFFERED INPUT EXTERNAL;/* INPUT GTF RECORDS */
   DCL
      PRINT FILE EXT STREAM OUTPUT PRINT;/* FILE TO PRINT RESULTS */
 %SKIP(4);
 /* LOCAL VARIABLES */
 %SKIP;
   DCL
      MAXDEVICES FIXED BIN(15),        /* MAXIMUM NUMBER OF DEVICES */
      CENTRE FIXED BIN(15),            /* CENTRE MEASUREMENTS OBTAINED
                                       1 = GOODMANS, 2 = KEGWORTH */
      METHOD FIXED BIN(15),            /* METHOD USED TO EVALUATE
                                       WHICH METHOD IS TO BE USED TO
                                       EVALUATE THE 50 MOST
                                       SIGNIFICANT ROWS: 1 = ROWS, 2 =
                                       MAXIMUM, 3 = NODIAG (SEE
                                       XXMAINL FOR DETAILS) */
      DEVICEPTR POINTER,               /* POINTER TO DATA ABOUT
                                       VARIOUS DEVICES */
      MTIME CHAR(12),                  /* TIME OF FIRST RECORD */
      MDATE CHAR(9);                   /* DATE ON FIRST RECORD */
 %SKIP(4);
      OPEN FILE(INFILE), FILE(PRINT) LINESIZE(132) PAGESIZE(99);/*
                                       OPEN THE FILES, PRINT WITH A
                                       PAGESIZE SO LARGE THAT ENDPAGE
                                       WILL NEVER BE RAISED FOR IT */
      CALL XXPARSM(PARMSTRING,MAXDEVICES,CENTRE,METHOD,DEVICEPTR);/*
                                       PARSE EXEC PARMSTRING */
      CALL XXGETR1(MTIME,MDATE);       /* INITIALISE INPUT ROUTINE */
      CALL XXNWPG1(MDATE,MTIME,CENTRE);/* INITIALISE PAGE HEADING
                                       ROUTINE AND PUT HEADING ON
                                       FIRST PAGE */
      CALL XXMAINL(METHOD,MAXDEVICES,DEVICEPTR,MTIME);/* CALL MAIN
                                       PROCESS PROCEDURE */
      RETURN;                          /* DONE - RETURN TO OS */
   END;                                /* OF XXDEVMN */
