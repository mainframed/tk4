.im syspub pl=66 pagenum=top ll=72 layout=manual dark=yes
.ad 0
.ce 2
Abend 522 Protection from JES3 Global Down
.ur &sysdate
.sp 2
.ce 2
Author: Lionel Dyck
Rockwell International
.sp
.pp
On the rare occasion when the JES3 Global processor is down there is the
potential for any address space on either the Global or Local processor
to exceed the SMF wait time
(JWT) value.  This happens because the address space requires a JES3
service, such as more sysout space, a sysin file, dynamic allocation
or any number of other reasons.
When this happens the address space is
terminated with a 522 abend.  This can be very frustrating to the user
who has his/her job or TSO session terminated because of JES3.
.pp
The solution to this problem is to test in the SMF Time Limit Exit to
determine if the reason the exit was entered is because the
wait time limit
was reached due to a JES3 outage.
.pp
Logic to add to IEFUTL.
.cp begin
.point begin
On entry check register 0 for a code of 8.  This indicates that
the entry reason is a wait time exceeded problem.
See example in figure below.
.boxon
.fo off
         C     R0,=F'8'    SEE IF WAIT TIME EXCEEDED
         BE    WAITTIME    YES....GO DO ANALYSIS FOR WAIT
.fo on
.boxoff
.cp end
.cp begin
.point
Next check to determine if you a running on the Global processor.
See example in figure below.
.boxon
.fo off
         L     R1,CVTPTR       -> CVT
         USING CVT,R1
         L     R1,CVTJESCT     -> JESCT
         USING JESCT,R1
         L     R1,JESSSCT      -> SSCT FOR JES3 (1ST ONE)
         USING SSCT,R1
         L     R2,SSCTSSVT     -> SSVT
         USING SSVT,R2
         TM    SVTFLAG1,SVTGLOBL   ARE WE ON GLOBAL ?
         BO    GLOBAL              YES
         B     LOCAL               NO - MUST BE LOCAL
.fo on
.boxoff
.cp end
.cp begin
.point 2
If you are on the global then you will need to determine if the Global
is active or not.      See example in figure below.
.boxon
.fo off
GLOBAL   DS    0H                 ON GLOBAL
         TM    SVTFLAG1,SVTABFG   GLOBAL IS DOWN ?
         BZ    CANCEL        NO - CANCEL THE SUCKER
         B     EXTEND        ELSE EXTEND HIS TIME
.fo on
.boxoff
.cp end
.cp begin
.point 1
If you are not on the Global then you will need to check the JES3
Main Processor Control (MPC) table for the status of the Global.
To do this you must find the MPC for the Global processor.
See example in figure below to see how to find the Global MPC when
running on a Local.
.boxon
.fo off
LOCAL    L     R3,SVTMPCDA      -> MPC TABLES
         USING MPCSTART,R3
MPCLOOP  DS    0H
         TM    MPSYSTEM,MPGLBL  IS THIS THE GLOBAL MPC ?
         BO    MPCGLBL          HAVE IT
         L     R3,MPNEXT        -> NEXT ENTRY
         LTR   R3,R3            IS THERE A NEXT ?
         BZ    EXTEND           -EXTEND IT NO GLOBAL
         B     MPCLOOP
.fo on
.boxoff
.cp end
.cp begin
.point 2
After finding the Global MPC you need to test the CTC flags to
determine the status of the JES3 Global processor. See example in
figure below to see how to do this.
.fo off
.boxon
MPCGLBL  DS    0H               FOUND GLOBAL MPC
         CLI   CTCFLG2,x'00'    ANY CTC ERROR ON
         BNE   EXTEND           YES - EXTEND THE USER
         TM    CTCFLG3,CTCRECN  CTC ENABLED FOR RECON
         BO    EXTEND           YES - EXTEND THE USER
         TM    CTCFLG4,CTCTMOT  TIME OUT DETECTED ON CTC ?
         BO    EXTEND           YES - EXTEND THE USER
         TM    CTCSAPF2,CTCSLOW IN SLOW DOWN MODE (PSEUDO DOWN)
         BO    EXTEND           YES - EXTEND THE USER
         B     CANCEL           ELSE - CANCEL THE TURKEY
.boxoff
.fo on
.pp
The CTCFLG2 flag is set to indicate any I/O error on the CTC.
.pp
The CTCRECN flag is set prior to JES3 connect and after replying NO to
message IAT2076 and indicates that only CONNECT SIO's are allowed on
the CTC.
.pp
The CTCFLG4 is set by IATSSCM when the delta between the current time
and the time of the last CTC I/O is greater than the value in CTCTMVAL,
to indicate that the CTC connection may be out or the Global may be
down.  This does not cause message IAT2076 to be issued,  this is
caused by a hard CTC error or entry by the MIH.
.pp
The CTCSLOW flag is set to indicate that slow down mode
is in effect.
.cp end
.cp begin
.point 1
If the JES3 Global is down then you will want to extend the wait time
limit by some value.      See example in figure below to see an example
on how to extend the wait time by 5 minutes.
.boxon
.fo off
EXTEND   DS    0H          EXTEND THE USERS TIME
         L     R1,=F'300'  5 MINUTE EXTENSION
         LA    R2,8        SET RETURN CODE, R1 = SECONDS
         B     RETURN      AND RETURN
.fo on
.boxoff
.cp end
.point 1
You will have to add the following macros to IEFUTL for a clean
assembly.
.sp
.boxon
.fo off
         CVT   DSECT=YES
         IATYFCT
         IATYMPC
         IATYSVT
         IEFJESCT
         IEFJSCVT
.fo on
.boxoff
.point end
