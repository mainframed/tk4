         TITLE 'S P Y --  MVS CONSOLE SPY PROGRAM  --  VERSION 2.0'
***********************************************************************
*                                                                     *
*                                 S P Y                               *
*                                                                     *
*                               05/22/79                              *
*                                                                     *
*                       OPERATOR CONSOLE MONITOR                      *
*                                                                     *
*     THIS PROGRAM DISPLAYS ALL ACTIVE OPERATORS CONSOLES ON          *
*     A 3270 TSO TERMINAL. SINCE THIS BUFFER CAN BE 35 LINES LONG,    *
*     IT MUST BE DISPLAYED IN TWO 'PAGES'. VARIOUS CONTROL            *
*     COMMANDS ARE AVAILABLE AND ARE DESCRIBED BELOW.                 *
*                                                                     *
*     COMMAND      DESCRIPTION                                        *
*                                                                     *
*        ?         DISPLAYS HELP FOR SPY                              *
*        1         SHIFT TO DISPLAY MODE 1                            *
*        2         SHIFT TO DISPLAY MODE 2                            *
*        B         BYE; END THE PROGRAM                               *
*        E         END; END THE PROGRAM                               *
*        F         FREEZE DISPLAY ON THE CURRENT PAGE                 *
*        R         RELEASE DISPLAY; SHOW ALTERNATING PAGES            *
*        WXX       AUTOMATICALLY REFRESH THE SCREEN                   *
*                  XX TIMES, THEN RETURN TO NORMAL MODE.              *
*                  IF XX IS NOT ENTERED, 30 ITERATIONS IS ASSUMED.    *
*                  IF XX = 0, TIMER WILL CONTINUE UNTIL INTERRUPT     *
*                  IS PRESSED.                                        *
*        CXX       SWITCH DISPLAY TO SYSTEM CONSOLE XX                *
*                  IF XX IS NOT A VALID CONSOLE, '1' IS ASSUMED       *
*        DXX       SET DELAY TIME BETWEEN REFRESHES TO XX TENTHS      *
*                  OF A SECOND.                                       *
*        L         LINK TO SWAP PROGRAM (MUST BE IN SAME LOAD MODULE  *
*                  AS SPY)                                            *
*     DISPLAY MODE 1 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 12 LINES                       *
*     DISPLAY MODE 2 - PAGE 1 = TOP 22 LINES                          *
*                      PAGE 2 = BOTTOM 22 LINES                       *
*                                                                     *
*     HITTING THE ATTENTION KEY WHILE IN TIMER MODE WILL CAUSE        *
*     THE TIMER TO BE RESET TO ZERO AND WAIT MODE TERMINATED.         *
*                                                                     *
*     SPY GIVES A TSO USER THE CAPABILITY TO ENTER SYSTEM AND JES     *
*     OPERATOR COMMANDS. THIS FACILITY IS PROTECTED BY A SIMPLE       *
*     3 CHARACTER PASSWORD (IN VARIABLE VIPWORD) TO PREVENT YOU       *
*     FROM ACCIDENTALLY ENTERING AN OPERATOR COMMAND. THE SYNTAX IS   *
*                                                                     *
*     JES COMMAND:  $...  ANY JES COMMAND ...                         *
*     OPER COMMAND: /...  ANY OPER COMMAND ...                        *
*                                                                     *
*     TYPING THE 3-LETTER VIP PASSWORD 'TOGGLES' THE VIP FLAG.        *
*     THE AUTHORIZATION LEVEL OF COMMANDS YOU CAN ISSUE DEPENDS ON    *
*     HOW YOUR INSTALLATION HAS SYSGENED ITS INTERNAL READERS.        *
*     THIS FACILITY WORKS BY DYNAMICALLY ALLOCATING AN INTERNAL       *
*     READER AND THROWING THE COMMAND THROUGH IT.                     *
*                                                                     *
*     THIS PROGRAM WILL PROBABLY REQUIRE BOTH THE SYS1.AMODGEN AND    *
*     SYS1.APVTMACS MACRO LIBRARIES TO ASSEMBLE PROPERLY.             *
*     SPY WAS DEVELOPED ON A 370/168 MVS RELEASE 3.7; IT WILL         *
*     PROBABLY *NOT* WORK ON ANYTHING BUT MVS...                      *
*                                                                     *
*                        STEVE LANGLEY                                *
*                        SOUTHERN CALIF. EDISON                       *
*                        ROSEMEAD, CALIF   91770                      *
*                        213-572-3521                                 *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 4
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SPY      CSECT
         B     100(R15)           BRANCH AROUND SAVE AREAS
         DC    CL9'SPY'           IDENTIFIER
         DC    CL9'&SYSDATE'
         DC    CL6'&SYSTIME'
SAVE     DC    18F'0'             SAVE AREA
         STM   R14,R12,12(R13)    SAVE REGISTERS
         LR    R12,R15            R12 = ADDR OF ENTRY POINT
         USING SPY,R12,R11        ADDRESABILITY TO CSECT
         LA    R11,SAVE           R11 = ADDR OF OUR SAVE AREA
         ST    R13,SAVE+4         SAVE POINTER TO CALLERS SAVE AREA
         ST    R11,8(R13)         SAVE PTR TO OUR SAVE AREA IN CALLER'S
         LR    R13,R11            R13 = ADDR OF OUR SAVE AREA
         LA    R11,4095(R12)      R11 WILL BE
         LA    R11,1(R11)         SECOND BASE REGISTER
         SPACE 3
***********************************************************************
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
***********************************************************************
         LA    R3,STAXLIST        R3 = ADDRESS OF STAX LIST MACRO
         STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         SPACE 2
         GTSIZE
         LTR   R0,R0              R0 = NUMBER OF LINES PER SCREEN
         BZ    HARDCOPY           IF NONZERO ASSUME A CRT IS IN USE
         SPACE
CRT      STH   R0,LPSCREEN        R0 = LINES PER SCREEN
         STH   R1,CPLINE          R1 = CHARACTERS PER LINE
         CH    R0,=H'24'          IS USER ON A 3277?
         BE    FLSCREEN           YES, JUST CONTINUE
         MVI   MOD4FLG,X'FF'      SET 3278-4 FLAG ON
         MVC   CMDCTRL(3),R41C1   ROW 41, COL 1
         MVC   PHEADING(3),R42C1  ROW 42, COL 1
         MVC   R24C1(3),R43C1     ROW 43, COL 1
FLSCREEN STFSMODE ON,INITIAL=YES  TURN ON VTAM FULL SCREEN MODE
         B     BLDUCMS
         SPACE
HARDCOPY STSIZE SIZE=80           OTHERWISE, HARDCOPY; SET LSIZE=80
         MVI   CRTFLAG,X'00'      WE ARE USING A HARDCOPY
         MVC   CMDCTRL(6),BLANKS  ZAP OUT 3277 CNTRL CHARS
         MVC   PHEADING(6),BLANKS ZAP OUT 3277 CNTRL CHARS
         MVC   HELP(14),BLANKS    ZAP OUT 3277 CNTRL CHARS
         SPACE 3
***********************************************************************
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
***********************************************************************
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R10,R10            R10 = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(R8)           SAVE UCM ADDRESS IN UCMTAB
         LA    R10,1(R10)         R10 = R10 + 1  (ONE MORE UCM)
         LA    R8,4(R8)           R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R10,NUMUCMS        SAVE NUMBER OF UCMS FOUND
         DROP  R4
         SPACE 3
***********************************************************************
*                                                                     *
*                            TOP OF LOOP                              *
*              LOCATE SCREEN BUFFER AND PREPARE TO TPUT               *
*                                                                     *
***********************************************************************
NEXTPAGE CLI   ATTNFLG,X'00'      WAS ATTN HIT?
         BE    NOATTN             NO
*                                 ATTENTION KEY HIT; PROCESS IT
         MVI   ATTNFLG,X'00'      YES, RESET FLAG
         MVC   TIME(3),BLANKS     BLANK OUT TIMER FIELD
         MVI   WAITFLG,X'00'      TURN OFF WAIT FLAG
         XC    TIMER,TIMER        SET TIMER TO 0
NOATTN   EQU   *
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         L     R4,CONSOLE         R4 = CONSOLE TO BE DISPLAYED
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
         MVC   ERROR(26),ERRMSG1  CONSOLE NUMBER INVALID
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(R5)           R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING DCMTSRT,R6
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         MVC   ERROR(26),ERRMSG4  NON-GRAPHIC CONSOLE
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   CTYPE(28),BLANKS   BLANK OUT CONSOLE TYPE FIELD
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH               NO
         MVC   MASTER(6),=CL6'MASTER' YES
AUTH     TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   SYS(3),=CL3'SYS'   YES
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   IO(3),=CL3'I/O'    YES
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CONS(4),=CL4'CONS' YES
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         MVC   UNIT(3),13(R7)     MOVE UNIT ADDR INTO LINE
         L     R7,DCMADTRN        R7 = ADDR OF TDCM
         USING DCMSTRT,R7
         SPACE
***********************************************************************
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
***********************************************************************
         L     R3,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R3,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         MVC   LASTLINE(79),DCMINPUT   MOVE IN INPUT BUFFER LINE
         MVI   MODE+1,C' '        BLANK OUT VIP INDICATOR
         CLI   VIPFLG,X'FF'       IS THE VIP FLAG ON?
         BNE   CHKINTEG           NOPE
         MVI   MODE+1,C'*'        TURN ON VIP INDICATOR
         SPACE 1
***********************************************************************
*                                                                     *
*                    CHECK FOR INTEGRATED CONSOLE                     *
*                                                                     *
*     IF THIS IS A INTEGRATED CONSOLE, CHECK IF THE USER HAS A        *
*     327X-2 OR THE LARGE-SCREEN 327X-4.                              *
*                                                                     *
***********************************************************************
CHKINTEG MVI   INTEGFLG,X'FF'     TURN ON INTEGRATED CONSOLE FLAG
         CLC   DCMMSGAL(2),=H'30'  IS THIS REALLY INTEGRATED CONSOLE?
         BE    CHKMODEL           YES
         CLC   DCMMSGAL(2),=H'19'  IS THIS A TYPE 3277 ?
         BE    CONTXXXX           YES
         ABEND 777                WHAT IS IT
CONTXXXX MVI   INTEGFLG,X'00'     NO; TURN OFF INTEGRATED CONSOLE FLAG
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD2TPUT SET TPUT LENGTH FOR SHORT SCREEN
         B     MOD2               AND TREAT AS MOD2 FOR NOW
         SPACE 1
CHKMODEL CLI   MOD4FLG,X'FF'      IS THIS A 3278-4?
         BNE   MOD2               NO - MUST BE A MOD2
         MVI   FREEZE,C'F'        FREEZE DISPLAY
         MVI   PAGE,C'1'          ON PAGE 1
         MVC   TPUTLEN(4),MOD4TPUT SET LENGTH FOR FULL 3278-4 SCREEN
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN BUFFER
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M4BUFLEN        R5 = 3278-4 BUFFER LEN (35 LINES)
         LA    R9,M4BUFLEN        R9 = 3278-4 BUFFER LEN (35 LINES)
         B     MOVEBUFF
         SPACE 2
MOD2     CLI   PAGE,C'1'          ARE WE ON PAGE 1?
         BNE   ONTWO              NO, SO WE MUST BE ON 2
         CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 1?
         BNE   PAGE2              NO, SO DISPLAY PAGE 2
         B     PAGE1              YES, SO DISPLAY PAGE 1
ONTWO    CLI   FREEZE,C'F'        ARE WE FROZEN ON PAGE 2?
         BE    PAGE2              YES, SO DISPLAY PAGE 2
PAGE1    MVI   PAGE,C'1'          PAGE = 1
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN IMAGE BUFFER
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUF (21 LINES)
         LA    R9,M2BUFLEN        R9 = CONSOLE BUFFER LEN   (21 LINES)
         B     MVETRAIL           GO MOVE THE BUFFER
PAGE2    MVI   PAGE,C'2'          PAGE = 2
         L     R8,DCMASCRN        R8 = ADDR OF SCREEN IMAGE BUFFER
         CLI   MODE,C'2'          ARE WE IN DISPLAY MODE 2?
         BE    DMODE2             YES, BRANCH TO DMODE2
DMODE1   LA    R8,LEN22(R8)       MOVE POINTER DOWN 23 LINES
         LA    R9,LEN13           R9 = LENGTH OF LAST 12 LINES
         B     CONTINUE           JUMP AROUND MODE 2 DISPLAY
DMODE2   LA    R8,LEN9(R8)        MOVE POINTER DOWN 9 LINES
         LA    R9,LEN21           R9 = LENGTH OF SOURCE BUFFER
CONTINUE LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M2BUFLEN        R5 = LENGTH OF OUTPUT BUFFER
MVETRAIL MVC   ENDMOD2(TRAILEN),CMDCTRL MOVE IN TRAILER
MOVEBUFF ICM   R9,8,PAD           MAKE BLANK THE PAD CHARACTER
         MVCL  R4,R8              MOVE CONSOLE BUFFER TO OUTPUT BUFFER
         DROP  R5,R6,R7
         CLI   CRTFLAG,X'FF'      IS THIS A CRT?
         BE    TPUTCRT            YES
         SPACE 5
***********************************************************************
*                                                                     *
*      DISPLAY THE OPERATOR'S SCREEN ON A TSO HARDCOPY TERMINAL       *
*                                                                     *
***********************************************************************
         XR    R8,R8              R8 = COUNTER = 0
         LA    R1,BUF             SET POINTER TO FIRST LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         L     R0,=F'78'          R0 LENGTH OF OUTPUT LINE
NEXTL    LR    R3,R1              SAVE R1 SINCE TPUT ZAPS IT
         TPUT  (1),(0),R          PRINT ONE LINE ON HARDCOPY
         LA    R8,1(R8)           ADD 1 TO COUNTER
         C     R8,=F'21'          HAVE WE PRINTED LAST LINE?
         BE    DOLAST2            YES, CONTINUE
         LA    R1,80(R3)          NOPE, POINT TO NEXT LINE
         CLI   INTEGFLG,X'FF'     IS THIS AN INTEGRATED CONSOLE?
         BE    NOT3270            YES
         MVC   0(5,R1),BLANKS     BLANK OUT 3270 CTRL CHARS
         LA    R1,4(R1)           ADD 4 EXTRA BYTES TO SKIP CTRL CHARS
NOT3270  L     R0,=F'78'          LOAD LENGTH OF LINE
         ICM   R1,8,EDITFLG       EDIT MODE
         B     NEXTL              PRINT NEXT LINE
DOLAST2  TPUT  LASTLINE,79,EDIT   DISPLAY OPER CMD LINE
         TPUT  HEADING,79,EDIT    DISPLAY CONSOLE STATUS LINE
         TPUT  USERLINE,79,EDIT   DISPLAY USER CMD LINE
         B     INPUT
         SPACE 5
         EJECT
***********************************************************************
*                                                                     *
*               DISPLAY THE OPERATORS SCREEN ON A 3277                *
*                                                                     *
***********************************************************************
TPUTCRT  LA    R1,CLEAR           R1 = ADDR OF OUTPUT STREAM
         L     R0,TPUTLEN         R0 = LENGTH OF TPUT
         ICM   R1,8,FULLSCR       SET ASIS TYPE FOR TPUT
         TPUT  (1),(0),R          DISPLAY ENTIRE SCREEN
INPUT    MVC   ERROR(32),BLANKS   BLANK OUT ERROR FIELD
         CLI   WAITFLG,X'FF'      IS THE WAIT FLAG ON?
         BNE   READCHAR           NO, SO GO GET A COMMAND
         STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         L     R2,TIMER           R2 = CURRENT VALUE OF TIMER
         BCTR  R2,0               TIMER = TIMER - 1
         ST    R2,TIMER           STORE NEW VALUE OF TIMER
         CVD   R2,WORK            CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         LTR   R2,R2              HAS TIMER HIT ZERO?
         BNZ   NEXTPAGE           NO, CONTINUE TO COUNT
         MVC   TIME(3),BLANKS     CLEAR COUNTER FIELD
         XI    WAITFLG,X'FF'      TOGGLE WAIT FLAG OFF
         B     NEXTPAGE           AND GO ON AS IF NOTHING HAPPENED..
         EJECT
***********************************************************************
*                                                                     *
*                       READ COMMAND FROM USER                        *
*                                                                     *
***********************************************************************
READCHAR TGET  REPLY,79           GET 79 CHARACTERS FROM TERMINAL
         CLI   REPLY,X'6E'        IS THIS A RESHOW? (VTAM ONLY)
         BE    NEXTPAGE           YES, JUST GO REFRESH
         CLI   REPLY,C' '         JUST A BLANK?
         BE    NEXTPAGE           YES, JUST GO REFRESH
         OC    REPLY(79),BLANKS   CONVERT CHARS TO UPPER CASE
***********************************************************************
*                                                                     *
*                       W  --  ENTER WAIT MODE                        *
*                                                                     *
***********************************************************************
         CLI   REPLY,C'W'         DO WE SHIFT TO WAIT MODE?
         BNE   CDELAY             NO, SO CONTINUE
         XI    WAITFLG,X'FF'      TURN ON WAIT FLAG
         LA    R2,30              SET DEFAULT VALUE = 30
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1  MOVE TIME LEFT INTO PLACE
         ST    R2,TIMER           STORE STARTING TIMER VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*            D  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
***********************************************************************
CDELAY   CLI   REPLY,C'D'         ARE WE CHANGING THE TIME DELAY?
         BNE   CCONSOLE           NO, SO CONTINUE
         LA    R2,10              SET DEFAULT VALUE = 10 TENTHS SECOND
         LA    R15,CONVBIN        BRANCH TO CONVERSION RTN
         BALR  R14,R15            EBCDIC TO BINARY
         MVC   SCRATCH(5),DPATTRN MOVE IN EDIT PATTERN
         ED    SCRATCH(5),WORK+6  EDIT IN DELAY TIME
         MVC   PAUSE(3),SCRATCH+2 MOVE TIME LEFT INTO PLACE
         MH    R2,=H'10'          CONVERT TO 100THS OF A SECOND
         ST    R2,DELAY           STORE WAIT DELAY VALUE
         B     NEXTPAGE           ALL SET - GO DISPLAY NEXT PAGE
***********************************************************************
*                                                                     *
*                    C  --  SET CONSOLE NUMBER                        *
*                                                                     *
***********************************************************************
CCONSOLE CLI   REPLY,C'C'         DO WE CHANGE CONSOLES?
         BNE   BYE                NO, SO CONTINUE
         L     R2,CONSOLE         SET DEFAULT CONSOLE
         ST    R2,OLDCONS         SAVE OLD CONSOLE #
         LA    R15,CONVBIN        BRANCH TO
         BALR  R14,R15            EBCDIC->BINARY CONVERTOR
         ST    R2,CONSOLE         STORE R2 AWAY AS CONSOLE NUMBER
         B     NEXTPAGE           CONTINUE
         EJECT
***********************************************************************
*                                                                     *
*                        B  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
BYE      CLI   REPLY,C'B'         IS IT A 'B'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*                        E  --  TERMINATE SPY                         *
*                                                                     *
***********************************************************************
END      CLI   REPLY,C'E'         IS IT AN 'E'?
         BE    DONE               YES, SO QUIT
***********************************************************************
*                                                                     *
*               F  --  FREEZE DISPLAY ON CURRENT PAGE                 *
*                                                                     *
***********************************************************************
F        CLI   REPLY,C'F'         IS IT AN 'F'?
         BNE   R                  NO, SO CONTINUE ON
         MVI   FREEZE,C'F'        TURN ON FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*           R  --  RELEASE FREEZE ON CURRENT PAGE DISPLAY             *
*                                                                     *
***********************************************************************
R        CLI   REPLY,C'R'         IS IT AN 'R'?
         BNE   MODE1              NO, SO CONTINUE ON
         MVI   FREEZE,C'R'        TURN OFF FREEZE INDICATOR
         B     NEXTPAGE           CONTINUE
***********************************************************************
*                                                                     *
*                1  --  SHIFT TO MODE 1 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE1    CLI   REPLY,C'1'         DO WE SHIFT TO MODE 1 DISPLAY?
         BNE   MODE2              NO, SO CONTINUE
         MVI   MODE,C'1'          SET MODE INDICATOR
         B     NEXTPAGE
***********************************************************************
*                                                                     *
*                2  --  SHIFT TO MODE 2 TYPE DISPLAY                  *
*                                                                     *
***********************************************************************
MODE2    CLI   REPLY,C'2'         DO WE SHIFT TO MODE 2 DISPLAY?
         BNE   GETHELP            NO, SO CONTINUE
         MVI   MODE,C'2'          SET MODE INDICATOR
         B     NEXTPAGE
         EJECT
***********************************************************************
*                                                                     *
*           ?  --  LIST HELP FOR SPY COMMANDS ON TERMINAL             *
*                                                                     *
***********************************************************************
GETHELP  CLI   REPLY,C'?'         IS HE ASKING FOR HELP?
         BNE   SWAPLINK           NO,SO CONTINUE
         LA    R1,HELP            R1 = ADDR OF HELP PAGE
         LA    R0,HLENGTH         R0 = LENGTH OF HELP PAGE
         ICM   R1,8,FULLSCR       INSERT ASIS CNTL CHARS
         TPUT  (1),(0),R          DISPLAY HELP
         B     READCHAR
***********************************************************************
*                                                                     *
*                    L  --  LINK TO SWAP PROGRAM                      *
*                                                                     *
***********************************************************************
SWAPLINK CLI   REPLY,C'L'         SHOULD WE XCTL TO SWAP?
         BNE   VIP                NO, SO CONTINUE
         L     R13,SAVE+4         CALLERS SAVE AREA POINTER.
         XCTL  (2,12),EP=SWAP     XCTL TO SWAP
***********************************************************************
*                                                                     *
*                       CHECK FOR VIP PASSWORD                        *
*                                                                     *
***********************************************************************
VIP      CLC   REPLY(3),VIPWORD   WAS VIP PASSWORD ENTERED?
         BNE   JESOPER            NO, CONTINUE
         XI    VIPFLG,X'FF'       TOGGLE VIP FLAG
         B     NEXTPAGE
***********************************************************************
*                                                                     *
*                      SYSTEM OPERATOR COMMANDS                       *
*                                                                     *
***********************************************************************
JESOPER  CLI   VIPFLG,X'FF'       ARE WE IN VIP MODE?
         BNE   BADCMD             NO, SO CONTINUE
OPER     CLI   REPLY,C'/'         IS THIS AN OPERATOR COMMAND?
         BNE   JES                NO, CHECK FOR JES CMD
         LA    R1,REPLY+1         R1 = ADDR OF FIRST CHAR IN COMMAND
         LA    R7,OPERCMD         R7 = ADDR OF FIRST CHAR IN RECORD
         LA    R3,N1              R3 = ADDR OF COLUMN 72 IN RECORD
CHKCHAR  CLI   0(R1),C''''        IS THIS A TICK MARK?
         BNE   MOVECHAR           NOPE; GO ON
         MVI   0(R7),C''''        TUCK IN AN EXTRA TICK
         LA    R7,1(R7)           R7 = NEXT CHAR IN RECORD
MOVECHAR MVC   0(1,R7),0(R1)      MOVE ONE CHAR INTO BUFFER
         LA    R7,1(R7)           R7 = NEXT CHAR IN RECORD
         LA    R1,1(R1)           POINT TO NEXT CHAR IN BUFFER
         CR    R7,R3              ARE WE POINTING TO THE 'N'?
         BL    CHKCHAR            NO, GO GET ANOTHER CHAR
         MVC   REPLY(79),BLANKS   YES; QUIT AND BLANK OUT REPLY FIELD
NCHAR    BCTR  R3,0               R3 = R3 - 1
         CLI   0(R3),C' '         IS THIS CHAR A BLANK?
         BE    NCHAR              YES, BACK UP ANOTHER CHAR
         MVI   1(R3),C''''        MAKE A ' THE LAST CHAR IN THE CMD
         LA    R3,OPERCARD        R3 = ADDR OF OPER COMMAND
         B     DYNA               BRANCH TO DYNAMIC ALLOCATE
         EJECT
***********************************************************************
*                                                                     *
*                       JES OPERATOR COMMANDS                         *
*                                                                     *
***********************************************************************
JES      CLI   REPLY,C'$'         IS THIS A JES COMMAND?
         BNE   BADCMD             NO, CONTINUE
         MVC   JESCMD(69),REPLY   MOVE COMMAND INTO PLACE
         MVC   REPLY(79),BLANKS   BLANK OUT REPLY FIELD
         LA    R3,JESCARD         R3 = ADDR OF JES COMMAND
DYNA     LA    R1,RBPTR           R1 = ADDR OF RB POINTER
         DYNALLOC                 <<< DYNAMIC ALLOCATION >>>
         LA    R7,READER          R7 = ADDR OF READER DCB
         USING IHADCB,R7
         MVC   DCBDDNAM,DDNAME    MOVE DDNAME INTO DCB
         DROP  R7
         OPEN  (READER,OUTPUT)    OPEN INTRDR FOR OUTPUT
         PUT   READER,(3)         PUT OPER CMD TO INTRDR
         CLOSE READER             CLOSE AND FREE INTERNAL READER
         B     NEXTPAGE
***********************************************************************
*                                                                     *
*                      COMMAND WAS INVALID                            *
*                                                                     *
***********************************************************************
BADCMD   MVC   ERROR(26),ERRMSG3  COMMAND WAS INVALID
         B     NEXTPAGE
         SPACE 5
DONE     CLI   CRTFLAG,X'00'      IS THIS A HARDCOPY?
         BE    ALLDONE            YES
         TPUT  CLR,CLRLEN,FULLSCR NO, LETS CLEAR THE SCREEN FIRST
         STFSMODE OFF             TURN OFF FS MODE
ALLDONE  L     R13,SAVE+4         RESTORE POINTER TO CALLER'S SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         LA    R15,0
         BR    R14                RETURN TO SYSTEM
         EJECT
***********************************************************************
*                                                                     *
*            CONVERT EBCDIC NUMBERS FROM USER INTO BINARY             *
*                                                                     *
***********************************************************************
CONVBIN  CVD   R2,WORK            CONVERT TO DECIMAL.
         CLI   REPLY+1,C' '       DID HE ENTER A NUMBER?
         BE    RTRN               NO, USE THE DEFAULT
         CLI   REPLY+1,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+1,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(1) PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   REPLY+2,C' '       DID HE ENTER 2 DIGITS?
         BE    CVB                NO, DONT DO THE 2 DIGIT PACK
         CLI   REPLY+2,C'0'       IS THE HEX CODE < 'F0' ?
         BL    BADCHAR            YES, ERROR
         CLI   REPLY+2,C'9'       IS THE HEX CODE > 'F9' ?
         BH    BADCHAR            YES, ERROR
         PACK  WORK(8),REPLY+1(2) PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      CVB   R2,WORK            GET BINARY
RTRN     BR    R14                RETURN TO MAINLINE
BADCHAR  MVC   ERROR(26),ERRMSG2  CONSOLE NUMBER ERROR
         B     RTRN
         DROP  12
         EJECT
***********************************************************************
*                                                                     *
*                          A T T N E X I T                            *
*                                                                     *
*         TRAP USERS ATTENTION INTERRUPTS AND FLAG FOR RESET          *
*                                                                     *
***********************************************************************
ATTNEXIT LR    R7,R15             ESTABLISH
         USING ATTNEXIT,R7        ADDRESSABILITY.
         MVI   ATTNFLG,X'FF'      SET ATTN FLAG
         BR    R14                RETURN TO CALLER
         DROP  R7
         EJECT
***********************************************************************
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
***********************************************************************
         DS    0D
WORK     DS    D                   WORK AREA FOR PACKS
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP
OLDCONS  DC    F'1'                PREVIOUS CONSOLE NUMBER
CONSOLE  DC    F'1'                CONSOLE TO BE LOOKED AT
TPUTLEN  DC    A(MOD2LEN)          LENGTH OF MOD 2 TPUT
MOD4TPUT DC    A(MOD4LEN)          LENGTH OF MOD4 TPUT
MOD2TPUT DC    A(MOD2LEN)          LENGTH OF MOD2 TPUT
LPSCREEN DC    H'0'                LINES PER SCREEN
CPLINE   DC    H'0'                CHARACTERS PER LINE
TIMER    DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'100'              DELAY FOR 100 HUNDREDTHS OF A SECOND
MOD4FLG  DC    X'00'               X'FF' INDICATES 3278-4 IN USE
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
CRTFLAG  DC    X'FF'               X'FF' INDICATES CRT IN USE
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
VIPFLG   DC    X'00'               X'FF' INDICATES VIP MODE
INTEGFLG DC    X'00'               INTEGRATED CONSOLE FLAG
FULLSCR  DC    X'03'               TPUT ASIS FLAG
EDITFLG  DC    X'00'               TPUT EDIT FLAG
VIPWORD  DC    C'GAK'              VIP PASSWORD
NULLS    DC    80X'00'             JUST NULLS
R41C1    DC    X'11F240'           3278-4  --  ROW 41, COL 1
R42C1    DC    X'11F350'           3278-4  --  ROW 42, COL 1
R43C1    DC    X'11F460'           3278-4  --  ROW 43, COL 1
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
REPLY    DC    CL80' '             USERS COMMAND INPUT FIELD
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
STAXLIST STAX  ATTNEXIT,MF=L
BLANKS   DC    CL80' '
         SPACE 5
***********************************************************************
*                                                                     *
*        PARAMETERS FOR DYNAMIC ALLOCATION OF INTERNAL READER         *
*                                                                     *
***********************************************************************
         DS    0F
RBPTR    DC    X'80'
         DC    AL3(RB)
RB       DC    X'14'              RB LENGTH
         DC    X'01'              DSNAME ALLOCATION REQUESTED
         DC    X'1000'            FLAGS1
INFOCODE DC    H'0'               ERROR CODE
ERORCODE DC    H'0'               INFORMATION BYTES
         DC    A(TXTUNITS)        ADDR OF TEXT UNIT POINTERS
         DC    2F'0'
TXTUNITS DC    A(TU1)
         DC    A(TU2)
         DC    A(TU3)
         DC    X'80',AL3(TU4)
TU1      DC    X'0019'            SYSOUT INTRDR REQUESTED
         DC    X'0001'
         DC    X'0006'
         DC    CL8'INTRDR'
TU2      DC    X'001C'            FREE DDNAME AT CLOSE
         DC    X'0000'
TU3      DC    X'0018'            RETURN GENERATED DDNAME
         DC    X'0001'
         DC    X'0001'
         DC    CL1'A'             DDNAME RETURN  HERE
TU4      DC    X'0055'            RETURN GENERATED DDNAME
         DC    X'0001'
         DC    X'0008'
DDNAME   DC    CL8' '             DDNAME RETURN  HERE
         SPACE 2
***********************************************************************
*                                                                     *
*                      DCB AND COMMAND BUFFERS                        *
*                                                                     *
***********************************************************************
READER   DCB   DSORG=PS,MACRF=PM,RECFM=F,LRECL=80,BLKSIZE=80,DDNAME=X
         SPACE 2
JESCARD  DC    C'/*'
JESCMD   DC    CL69' '
N        DC    CL9'N'             SUPRESS ECHO OF COMMAND
         SPACE 2
OPERCARD DC    CL7'/*$VS,'''
OPERCMD  DC    CL64' '
N1       DC    CL9'N'             SUPRESS ECHO OF COMMAND
         SPACE 2
***********************************************************************
*                                                                     *
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*                                                                     *
***********************************************************************
CLR      DC    X'C1'              WCC - CLEAR SCREEN
         DC    X'115D7E'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'13'              INSERT CURSOR
CLRLEN   EQU   *-CLR
         SPACE 5
***********************************************************************
*                                                                     *
*                  DISPLAY SCREEN - HEADER SECTION                    *
*                                                                     *
***********************************************************************
HEADER   EQU   *
CLEAR    DC    X'C1'              WCC
         DC    X'115D7F'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'1D60'            ATTR BYTE - PROTECTED FIELD
         DC    X'40'
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - IMAGE BUFFER SECTION                 *
*                                                                     *
***********************************************************************
BUF      DC    21CL80' '          OPERATORS SCREEN BUFFER
         DC    14CL80' '          PLUS EXTRA FOR 3278-4
***********************************************************************
*                                                                     *
*               DISPLAY SCREEN - TRAILER SECTION                      *
*                                                                     *
***********************************************************************
TRAILER  EQU   *
CMDCTRL  DC    X'115A50'          SBA TO ROW 22, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
LASTLINE DC    CL79' '            OPERATORS COMMAND INPUT LINE
PHEADING DC    X'115B60'          SBA TO ROW 23, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
HEADING  DC    CL8'Console '
CONNUM   DC    CL2' 1'            CONSOLE NUMBER
CTYPE    DC    CL4' '
MASTER   DC    CL8' '             MASTER CONSOLE
SYS      DC    CL4' '             SYS  AUTHORIZATION
IO       DC    CL4' '             I/O  AUTHORIZATION
CONS     DC    CL5' '             CONS AUTHORIZATION
         DC    CL3' '
         DC    CL5'Unit'
UNIT     DC    CL4' '             UNIT ADDR OF CONSOLE
HEADING1 DC    CL7'Timer: '
TIME     DC    CL3' '             SECONDS REMAINING ON TIMER
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'           DELAY IN SECONDS
         DC    CL2' '
HEADING2 DC    CL6'Mode: '
FREEZE   DC    C'F'               FREEZE/RELEASE MODE
MODE     DC    CL1'2'             DISPLAY MODE 2/1
         DC    CL2' '
         DC    CL5'Page '
PAGE     DC    CL1'2'             PAGE NUMBER
R24C1    DC    X'115CF0'          SBA TO ROW 24, COL 1
         DC    X'1D40'            ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'              INSERT CURSOR
USERLINE DC    CL13' '            USERS COMMAND INPUT LINE
ERROR    DC    CL66' '            ERROR MSG FIELD
ENDTRAIL EQU   *
         SPACE 5
         LTORG
         SPACE 5
         DS    0F
UCMTAB   DS    F
         DS    20F                PROVIDE SPACE FOR 20 UCM ADDRESSES
UCMTABE  EQU   *
NUMUCMS  DS    H
         SPACE 5
ERRMSG1  DC    CL26'ERROR - Console not active'
ERRMSG2  DC    CL26'ERROR - Non-numeric value '
ERRMSG3  DC    CL26'ERROR - Invalid command   '
ERRMSG4  DC    CL26'ERROR - Non-CRT console   '
         SPACE 5
***********************************************************************
*                                                                     *
*                           USER HELP PAGE                            *
*                                                                     *
***********************************************************************
HELP     DC    X'C1'               WCC
         DC    X'115D7F'           SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040',X'1DE8',C'Command     Description'
         DC    X'11C150'
         DC    X'11C260',C'   B        End SPY'
         DC    X'11C3F0',C'   C        Switch monitor to console 1'
         DC    X'11C540',C'   CXX      Switch monitor to console XX'
         DC    X'11C650',C'   DXX      Set delay to XX tenths seconds'
         DC    X'11C760',C'   E        End SPY'
         DC    X'11C8F0',C'   F        Freeze display on current page'
         DC    X'114A40',C'   L        Link to SWAP program'
         DC    C' (if available)'
         DC    X'114B50',C'   R        Release Display'
         DC    X'114C60',C'   W        Start timer mode for 30 seconds'
         DC    X'114DF0',C'   WXX      Start timer mode for XX seconds'
         DC    X'114F40',C'   W0       Start timer mode until '
         DC    C'interrupt'
         DC    X'115050',C'   ?        Display this page'
         DC    X'11D160',C'   1        Display mode 1'
         DC    X'11D2F0',C'   2        Display mode 2'
         DC    X'11D440'
         DC    X'11D550',C'Hitting INTERRUPT will stop the wait timer'
         DC    X'11D660'
         DC    X'11D7F0'
         DC    X'11D940'
         DC    X'115A50'
         DC    X'115B60'
         DC    X'115CF0'        ROW 24, COL 1
         DC    C'Hit ENTER to continue'
         DC    X'115DC6'        ROW 24, COL 23
         DC    X'1D40'
         DC    X'1340'
         DC    X'1DE8'
MARKER1  EQU   *
HLENGTH  EQU   MARKER1-HELP     LENGTH OF HELP TPUT
         EJECT
***********************************************************************
*                                                                     *
*                           E Q U A T E S                             *
*                                                                     *
***********************************************************************
LEN9     EQU   9*80             NUMBER OF BYTES IN  9 LINES
LEN21    EQU   21*80            NUMBER OF BYTES IN 21 LINES
LEN22    EQU   22*80            NUMBER OF BYTES IN 22 LINES
LEN13    EQU   13*80            NUMBER OF BYTES IN 13 LINES
LEN14    EQU   14*80            NUMBER OF BYTES IN 14 LINES
M2BUFLEN EQU   21*80            LENGTH OF BUFFER 3278-2
M4BUFLEN EQU   35*80            LENGTH OF BUFFER 3278-4
HEADLEN  EQU   BUF-HEADER       LENGTH OF HEADER
TRAILEN  EQU   ENDTRAIL-TRAILER LENGTH OF TRAILER
MOD4LEN  EQU   ENDTRAIL-HEADER  LENGTH OF TPUT FOR MOD4
MOD2LEN  EQU   MOD4LEN-LEN14    LENGTH OF TPUT FOR MOD2
ENDMOD2  EQU   BUF+M2BUFLEN     ADDR OF TRAILER FOR 3278-2
         PRINT NOGEN
         EJECT
         DCBD  DSORG=BS,DEVD=DA
         SPACE 4
         CVT   DSECT=YES
         SPACE 4
         IEERDCM
         SPACE 4
         IEETDCM
         SPACE 4
         IEECUCM FORMAT=NEW
         END   SPY
