         MACRO ,                   GENERATE ERROR MESSAGES.
         @ERROR &N,&X1,&X2
         GBLB  &@FAIL              SET IF SEVERE ERROR.
         GBLA  &@NUMERR            NO. OF ERRORS (INIT 0).
&@NUMERR SETA  &@NUMERR+1          COUNT ERRORS.
         AIF   (&N EQ 1).L01
         AIF   (&N EQ 2).L02
         AIF   (&N EQ 3).L03
         AIF   (&N EQ 4).L04
         AIF   (&N EQ 5).L05
         AIF   (&N EQ 6).L06
         AIF   (&N EQ 7).L07
         AIF   (&N EQ 8).L08
         AIF   (&N EQ 9).L09
         AIF   (&N EQ 10).L10
         AIF   (&N EQ 11).L11
         AIF   (&N EQ 12).L12
         @ERROR 7,ERROR-CODE,&N    INVALID ERROR CODE (WILL SET &@FAIL)
         MEXIT
.L01     MNOTE 8,'OFL001 TOO MANY &X1 -- ONLY 1ST &X2 USED.'
         AGO   .FAIL
.L02 MNOTE *,'OFL002 IGNORE ABOVE REMOVALS.  ''&X2'' IGNORED SINCE'
         AGO   .L09
.L03     MNOTE 8,'OFL003 UNMATCHED ''&X1'' -- REMOVED'
         AGO   .FAIL
.L04     MNOTE 1,'OFL004 NO LABEL ALLOWED FOR ''&X1''.'
         AGO   .END
.L05     MNOTE 0,'OFL005 ''&X1'' STATEMENTS SHOULD BE COMMENTED.'
         AGO   .END
.L06     MNOTE 1,'OFL006 SLIGHTLY INVALID &X1:  &X2..'
         AGO   .END
.L07     MNOTE 8,'OFL007 INVALID &X1:  &X2..'
         AGO   .FAIL
.L08     MNOTE 8,'OFL008 DUPLICATE ''&X1'' -- IGNORED.'
         AGO   .FAIL
.L09  MNOTE 8,'OFL009 MATCHING ''&X1'' NOT FOUND IN CONTAINING BLOCK.'
         AGO   .FAIL
.L10     MNOTE 8,'OFL010 NO INTERVENING ''&X1''.'
         AGO   .FAIL
.L11     MNOTE 4,'OFL011 FINAL LEVEL IS NOT 0.'
         AGO   .END
.L12     MNOTE 4,'OFL012 NO INTERVENING ''&X1''.'
         AGO   .END
.FAIL    ANOP
&@FAIL   SETB  1
.END     MEND
