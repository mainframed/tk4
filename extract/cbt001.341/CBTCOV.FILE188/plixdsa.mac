         MACRO
&SA      PLIXDSA
.*
.* THIS MACRO IS USED TO MAP A DSA FOR USE WITH THE PLIXDENT MACRO,
.* SUCH THAT ADDITIONAL WORKAREA STORAGE CAN BE GOTTEN BY:
.*       PLIXDENT DSALEN=WORKEND-DSA
.* WHERE WORKEND IS THE END OF THE USER'S WORKAREA.
.* NOTE THAT THE FULL 120 BYTE DSA IS MAPPED HERE.
.* (PLIXDENT ONLY USES THE 1ST 92 BYTES).
.* &SA IS THE NAME ASSIGNED TO THIS DSECT. DEFAULT = DSA.
.* MACRO PLIXSET HAS THE NECESSARY EQUATES FOR PL/I DATA IN THE DSA.
.* APB - 08/29/79
.*
         LCLC  &SSS
&SSS     SETC  '&SA'
         AIF   ('&SSS' NE '').NOSET
&SSS     SETC  'DSA'
.NOSET   ANOP
&SSS     DSECT
         DS    0D                  DOUBLEWORD ALIGNMENT
         DC    X'8000'             FLAGS
         DC    H'0'                OFFSET OF ONCA (USED ONLY IN LWS)
         DC    F'0'                CHAIN BACK
         DC    F'0'                CHAIN FORWARD (NOT USED IN PL/I)
         DC    15F'0'              SAVE AREA FOR R14-R12
         DC    F'0'                A(LWS)
         DC    F'0'                NAB
         DC    F'0'                END OF PROLOGUE NAB
         DC    X'91C0'             BLOCK-ENABLE CELL
         DC    X'91C0'             CURRENT-ENABLE CELL
         DC    A(0)                STATIC BACKCHAIN
         DC    A(0)                A(1ST STATIC ONCB)
         DC    A(0)                A(MOST RECENT DYNAMIC ONCB)
         DC    3A(0)               PLIX RESERVED
         DC    A(0)                A(ONCELLS)
         DC    4X'00'              FLAGS
         MEND
