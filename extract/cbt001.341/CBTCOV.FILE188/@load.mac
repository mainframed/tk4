         MACRO
&N       @LOAD &REG,&W,&V
.*             IF &V = 'VAR' THEN &W MUST BE A VARIABLE ITEM.
         GBLB  &@FAIL
         LCLA  &L
         LCLC  &C
&@FAIL   SETB  0                   ASSUME SUCCESS.
         AIF   ('&W'(1,1) EQ '(').REG
         AIF   (T'&W EQ 'U').UNDEF
         AIF   (T'&W EQ 'N').SELF
         AIF   ('&V' EQ 'VAR').VAR SKIP CONSTANT KRUD.
         AIF   ('&W'(1,3) EQ '=A(' AND '&W'(K'&W,1) EQ ')').ADDR
.VAR     ANOP
&L       SETA  L'&W
         AIF   (&L EQ 4).L4
         AIF   (&L EQ 2).L2
         AIF   (&L EQ 1).L1
.ERR     ANOP
&@FAIL   SETB  1
         MEXIT
.REG     AIF   ('&W(1)' NE '&REG').REG1
         AIF   ('&N' EQ '').END
&N       DS    0H
         MEXIT
.REG1    ANOP
&N       LR    &REG,&W(1)
         MEXIT
.ADDR    AIF   (K'&W GT 12).ADDR1
&C       SETC  '&W'(4,K'&W-4)
&N       LA    &REG,&C
         MEXIT
.ADDR1   ANOP
&N       L     &REG,&W
         MEXIT
.SELF    AIF   ('&V' EQ 'VAR').ERR
         AIF   (&W EQ 0).ZERO
&N       LA    &REG,&W
         MEXIT
.ZERO    ANOP
&N       SR    &REG,&REG
         MEXIT
.UNDEF   AIF   ('&V' EQ 'VAR').ERR
&N       LA    &REG,&W.(,0)
         MEXIT
.L4      ANOP
&N       L     &REG,&W
         MEXIT
.L2      ANOP
&N       LH    &REG,&W
         MEXIT
.L1      ANOP
&N       SR    &REG,&REG
         IC    &REG,&W
.END     MEND
