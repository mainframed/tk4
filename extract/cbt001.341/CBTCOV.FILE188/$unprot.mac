         MACRO
&NME     $UNPROT &D,&MASK=FF,&SYS=MVS,&SYSAD=,&WORKREG=,&MF=
.*
.*
.*
.* LAST CHANGE DATE - JANUARY 11, 1977
.*                  - THIS MACRO CAN NOW FUNCTION IN EITHER MVT SYSTEMS
.*                    OR MVS SYSTEMS OR BOTH SYSTEMS.
.*                  - MAILING ADDRESS CHANGE.
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE. ANY QUESTIONS CONCERNING
.* IT MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
         GBLC  &$PROSYS
         GBLC  &$TESRET(20)
         LCLC  &#,&DONE,&N,&R
         AIF   ('&SYSAD' EQ '').SYSOK
&$PROSYS SETC  ''
         AIF   ('&SYSAD' EQ '*').SYSOK
&$PROSYS SETC  '&SYSAD'
.SYSOK   AIF   ('&MF' EQ 'INIT').MEND
&N       SETC  '&NME'
&#       SETC  '&SYSNDX'
&DONE    SETC  '*+4'
&R       SETC  '&WORKREG(1)'
         AIF   ('&WORKREG(1)' NE '').ROK
         $TEST PFIX=
&R       SETC  '&$TESRET(1)1'
.ROK     AIF   ('&SYS' EQ 'MVT').SYSMVT
         AIF   ('&SYS' EQ 'MVS').SYSMVS
&DONE    SETC  'U&#.END'
         AIF   ('&$PROSYS' NE '').LCLSYS1
&N       L     &R,CVTPTR           --> CVT
         TM    CVTDCB-CVTMAP(&R),CVT6DAT VIRTUAL SYSTEM?
         AGO   .LCLSYS2
.LCLSYS1 ANOP
&N       TM    &$PROSYS,CVT6DAT    VIRTUAL SYSTEM?
.LCLSYS2 ANOP
&N       SETC  ''
         BNZ   U&#.MVS             YES, GO ISSUE MODESET
.SYSMVT  ANOP
&N       LPSW  U&#.PSW             LOAD KEY-0 PSW
         DS    0D                  DWORD ALIGNMENT
U&#.PSW  DC    X'&MASK.040000',A(&DONE) KEY-0 PSW
         AIF   ('&SYS' EQ 'MVT').MEND
&N       SETC  'U&#.MVS'
.SYSMVS  ANOP
&N       SETC  '&N        '(1,8)
         MNOTE '&N MODESET EXTKEY=ZERO       SET KEY-0'
&N       MODESET EXTKEY=ZERO
         AIF   ('&SYS' EQ 'MVS').MEND
&DONE    DS    0H
.MEND    MEND
