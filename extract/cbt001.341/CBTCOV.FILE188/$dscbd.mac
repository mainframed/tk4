         MACRO
&N       $DSCBD &FORMAT
.*
.*
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MVS FIELDS ADDED.
.*                  - BIT NAMES ADDED.
.*                  - DS4FMTID NAME CHANGED TO DS4IDFMT FOR
.*                    COMPATIBILITY WITH THE IECSDSL1 MACRO.
.*                  - THE &FORMAT= KEYWORD OPERAND HAS BEEN REPLACED BY
.*                    THE &FORMAT POSITIONAL OPERAND.
.*                  - THE &PFIX= KEYWORD OPERAND HAS BEEN REPLACED BY
.*                    &N NAME FIELD OPERAND.
.*                  - MAILING ADDRESS CHANGE.
.*
.* LAST CHANGE DATE - NOVEMBER 1, 1973
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE.  ANY QUESTIONS CONCERNING
.* IT MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
         GBLB  &$DSCBSW(7)
         LCLA  &W1,&W2
         LCLB  &F(7)
         LCLC  &P,&Q,&L
&L       SETC  'L'''
&P       SETC  '&N'
         AIF   ('&P' NE '').GOTP
&P       SETC  'DS'
.GOTP    ANOP
&W1      SETA  0
.FMTLP1  AIF   (&W1 EQ N'&FORMAT).FMTOK1
&W1      SETA  &W1+1
         AIF   (&FORMAT(&W1) LE 6).FMTOK2
         MNOTE 8,'ERROR   - "&&FORMAT(&W1)=&FORMAT(&W1)" IS INVALID.'
         MNOTE *,'          TERM IGNORED.'
         AGO   .FMTLP1
.FMTOK2  ANOP
&F(&FORMAT(&W1)+1) SETB (1)
         AGO   .FMTLP1
.FMTOK1  AIF   (N'&FORMAT NE 0).FMTOK3
&W1      SETA  0
.FMTLP2  AIF   (&W1 EQ 7).FMTOK3
&W1      SETA  &W1+1
&F(&W1)  SETB  (1)
         AGO   .FMTLP2
.FMTOK3  ANOP
&P       DS    0F                  STARTING POINT
&W2      SETA  0
         AGO   .DSCBNXT
.DSCBLP  ANOP
&$DSCBSW(&W2) SETB (1)
.DSCBNXT AIF   (&W2 EQ 7).END
&W2      SETA  &W2+1
         AIF   (NOT &F(&W2)).DSCBNXT
&W1      SETA  &W2-1
         SPACE 3
         ORG   &P                  ORIGIN TO START
         SPACE 1
         MNOTE *,'FORMAT &W1 DSCB'
&Q       SETC  '&P&W1'
&Q       DS    0XL140              DSCB LENGTH
         AIF   (&W1 NE 0).NOTF0
         DS    XL44                XL44'00'
&Q.FMTID DS    X                   FORMAT ID: X'00'
         DS    XL95'00'            XL95'00'
         AGO   .DSCBLP
.NOTF0   AIF   (&W1 NE 1).NOTF1
&Q.DSNAM DS    CL44                DSN IN EBCDIC
&Q.FMTID DS    C                   FORMAT ID: C'1'
&Q.DSSN  DS    CL6                 ORIGINATING VOL SER #
&Q.VOLSQ DS    HL2                 VOLUME SEQUENCE NUMBER
&Q.CREDT DS    XL3                 CREATION DATE: YDD
&Q.EXPDT DS    XL3                 EXPIRATION DATE: YDD
&Q.NOEPV DS    FL1                 NUMBER OF EXTENTS
&Q.NOBDB DS    FL1                 BYTES USED IN LAST PDS DRCTY BLK
&Q.MODD  EQU   *,3                 LAST-MOD-DATE (YCC)
&Q.MODT  EQU   &Q.MODD+&L&Q.MODD,2 LAST-MOD-TIME (YCC)
         DS    X                   RESERVED
&Q.SYSCD DS    CL13                SYSTEM ID CODE: C'IBM',...
&Q.REFD  DS    XL3                 LAST-REF-DATE: YDD
         DS    XL4                 RESERVED
&Q.DSORG DS    BL2                 DATA SET ORGANIZATION
&Q.RECFM DS    B                   RECORD FORMAT
&Q.OPTCD DS    B                   OPTION CODE
&Q.BLKL  DS    H                   MAXIMUM BLOCK SIZE
&Q.LRECL DS    H                   (MAXIMUM) LOGICAL RECORD LENGTH
&Q.KEYL  DS    FL1                 KEY LENGTH
&Q.RKP   DS    HL2                 RELATIVE KEY POSITION
&Q.DSIND DS    B                   DATA SET INDICATORS
&Q.SCALO DS    0XL4                ALLOCATION PARAMETERS
         DS    B                   ALLOCATION TYPE FLAGS
         DS    FL3                 SECONDARY ALLOCATION QUANTITY
&Q.LSTAR DS    XL3                 LAST BLOCK POINTER: TTR
&Q.TRBAL DS    HL2                 REMAININT SPACE ON LAST USED TRACK:
*                                  LL
         DS    XL2                 RESERVED
&Q.EXT1  DS    0XL10               FIRST EXTENT DESCRIPTION
         DS    B                   EXTENT TYPE INDICATOR
         DS    FL1                 EXTENT SEQUENCE NUMBER: M
         DS    XL4                 EXTENT LOWER LIMIT: CCHH
         DS    XL4                 EXTENT UPPER LIMIT: CCHH
&Q.EXT2  DS    XL10                SECOND EXTENT DESCRIPTION
&Q.EXT3  DS    XL10                THIRD EXTENT DESCRIPTION
&Q.PTRDS DS    XL5                 PTR TO FORMAT 2 OR FORMAT 3 DSCB
*                                  (IF ANY)
         AGO   .DSCBLP
.NOTF1   AIF   (&W1 NE 2).NOTF2
         DS    X                   KEY FIELD CODE: X'02'
&Q.2MIND DS    XL7                 2ND LEVEL MASTER INDEX PTR: MBBCCHH
&Q.L2MEN DS    XL5                 LAST ENTRY IN 2ND LEVEL MASTER
*                                  INDEX: CCHHR
&Q.3MIND DS    XL7                 3RD LEVEL MASTER INDEX PTR: MBBCCHH
&Q.L3MIN DS    XL5                 LAST ENTRY IN 3RD LEVEL MASTER
*                                  INDEX: CCHHR
         DS    XL11                RESERVED
&Q.LPDT  DS    XL8                 LAST PRIME TRK @ LAST PRIME CYL
&Q.FMTID DS    C                   FORMAT ID: C'2'
&Q.NOLEV DS    FL1                 NUMBER OF INDEX LEVELS
&Q.DVIND DS    FL1                 # OF TRKS FOR MAKING MASTER INDEX
&Q.1RCYL DS    XL3                 PTR TO 1ST DATA RCD ON EA CYL: HHR
&Q.LTCYL DS    XL2                 PTR TO LAST DATA TRK ON EA CYL: HH
&Q.CYLOV DS    FL1                 # OF TRKS FOR CYL OVFL ON EA CYL
&Q.HIRIN DS    FL1                 LAST POSSIBLE RCD ON HIGH LEVEL
*                                  INDEX TRACKS: R
&Q.HIRPR DS    FL1                 LAST POSSIBLE FORMAT F RCD ON EACH
*                                  PRIME DATA TRACK: R
&Q.HIROV DS    FL1                 LAST POSSIBLE FORMAT F RCD ON EA
*                                  OVFL DATA TRACK: R
&Q.RSHTR DS    FL1                 LAST DATA RCD ON SHARED TRK: R
&Q.HIRTI DS    FL1                 LAST POSSIBLE RCD ON EA UNSHARED
*                                  TRK INDEX TRK: R
&Q.HIIOV DS    FL1                 LAST POSSIBLE FORMAT F RCD ON EA
*                                  IND OVFL DATA TRK: R
&Q.TAGDT DS    HL2                 # OF DELETED RCDS
&Q.RORG3 DS    FL3                 LAST USE OVFL ACCESS CNT
&Q.NOBYT DS    H                   TOTL LENGTH OF HIGHEST INDEX: BYTES
&Q.NOTRK DS    FL1                 # OF TRKS HOLDING HIGHEST INDEX
&Q.PRCTR DS    FL4                 # OF PRIME DATA RECORDS
&Q.STIND DS    B                   STATUS INDICATORS
&Q.CYLAD DS    XL7                 CYL INDEX PTR: MBBCCHH
&Q.ADLIN DS    XL7                 PTR TO LOWEST MASTER INDEX: MBBCCHH
&Q.ADHIN DS    XL7                 PTR TO HIGHEST MASTER INDEX: MBBCCHH
&Q.LPRAD DS    XL8                 LAST PRIME DATA RCD PTR: MBBCCHHR
&Q.LTRAD DS    XL5                 AN INDEX PTR: CCHHR
&Q.LCYAD DS    XL5                 PTR TO END OF CYL INDEX: CCHHR
&Q.LMSAD DS    XL5                 PTR TO END OF MASTER INDEX: CCHHR
&Q.LOVAD DS    XL8                 END OF IND OVFL AREA PTR: MBBCCHHR
&Q.BYOVL DS    H                   # OF UNUSED BYTES ON CURRENT IND
*                                  OVFL AREA TRK
&Q.RORG2 DS    H                   # OF UNUSED TRKS IN IND OVFL AREA
&Q.OVRCT DS    H                   RCD CNT IN OFVL AREA
&Q.RORG1 DS    H                   FULL CYL OVFL AREA CNT
&Q.NIRT  DS    XL3                 PTR TO DUMMY TRK INDEX ENTRY: HHR
&Q.PTRDS DS    XL5                 PTR TO FORMAT 3 DSCB (IF ANY)
         AGO   .DSCBLP
.NOTF2   AIF   (&W1 NE 3).NOTF3
         DS    XL4                 KEY FIELD CODE: 4X'03'
&Q.EXTNT DS    4XL10               4 MORE EXTENT DESCRIPTIONS
&Q.FMTID DS    C                   FORMAT ID: C'3'
&Q.ADEXT DS    9XL10               9 MORE EXTENT DESCRIPTIONS
&Q.PTRDS DS    XL5                 RESERVED
         AGO   .DSCBLP
.NOTF3   AIF   (&W1 NE 4).NOTF4
         DS    XL44                KEY FIELD CODE: 44X'04'
&Q.IDFMT DS    C                   FORMAT ID: C'4'
&Q.HPCHR DS    XL5                 FORMAT 1 HIGH WATER MARK: CCHHR
&Q.DSREC DS    H                   FORMAT 0 COUNT
&Q.HCCHH DS    XL4                 NEXT ALT TRK PTR: CCHH
&Q.NOATK DS    H                   REMAINING ALT TRK CNT
&Q.VTOCI DS    B                   VTOC INDICATORS
         AIF   (&$DSCBSW(5)).NOBITS1
DS4DOSBT EQU   B'10000000'         DOS BIT
DS4DSTKP EQU   B'00010000'         DOS STACKED PACK
DS4DOCVT EQU   B'00001000'         DOS CONVERTED VTOC
DS4DIRF  EQU   B'00000100'         DIRF BIT
DS4DICVT EQU   B'00000010'         DIRF RECLAIMED
.NOBITS1 ANOP
&Q.NOEXT DS    FL1                 VTOC EXTENT CNT: FL1(1)
         DS    XL2                 RESERVED
&Q.DEVCT DS    0XL24               DEVICE CONSTANTS
&Q.DEVSZ DS    0XL4                DEVICE SIZE
         DS    H                   # OF LOGICAL CYLS
         DS    H                   # OF TRKS / LOGICAL CYL
&Q.DEVTK DS    H                   TRACK LENGTH: BYTES
&Q.DEVOV DS    0H                  2305 ALL KEYED BLOCKS OVERHEAD VALUE
&Q.DEVI  DS    FL1                 KEYED BLOCK OVERHEAD VALUE
&Q.DEVL  DS    FL1                 KEYED LAST BLOCK OVERHEAD VALUE
&Q.DEVK  DS    FL1                 UNKEYED BLOCK UNDERHEAD
&Q.DEVFG DS    B                   OVERHEAD FLAG BYTE
&Q.DEVTL DS    H                   DEVICE TOLERANCE
&Q.DEVDT DS    FL1                 # DSCB'S / TRK
&Q.DEVDB DS    FL1                 # OF PDS DRCTY BLKS / TRK
&Q.AMTIM DS    XL8                 VSAM TIME STAMP
&Q.AMCAT DS    0XL3                VSAM CATALOG INDICATOR
&Q.VSIND DS    B                   VSAM INDICATORS
&Q.VSCRA DS    XL2                 RELATIVE TRACK (TT) OF THE CRA
&Q.R2TIM DS    XL8                 VSAM VOLUME/CATALOG MATCH TIME STAMP
         DS    XL5                 RESERVED
&Q.F6PTR DS    XL5                 FORMAT SIX CHAIN HEAD: CCHHR
&Q.VTOCE DS    XL10                VTOC EXTENT DESCRIPTION
         DS    XL25                RESERVED
         AGO   .DSCBLP
.NOTF4   AIF   (&W1 NE 5).NOTF5
&Q.KEYID DS    XL4                 KEY FIELD CODE: 4X'05'
&Q.AVEXT DS    0XL5                FIRST FREE EXTENT DESCRIPTION
         DS    XL2                 PTR TO 1ST TRK IN EXTENT: TT
         DS    HL2                 # OF EMPTY CYLS IN EXTENT
         DS    FL1                 # OF TRKS IN EXTENT NOT IN FULL CYLS
&Q.EXTAV DS    7XL5                7 MORE FREE AREA DESCRIPTIONS
&Q.FMTID DS    C                   FORMAT ID: C'5'
&Q.MAVET DS    18XL5               18 MORE FREE AREA DESCRIPTIONS
&Q.PTRDS DS    XL5                 PTR TO NEXT FORMAT 5 DSCB (IF ANY)
         AGO   .DSCBLP
.NOTF5   ANOP
&Q.KEYID DS    XL4                 KEY FIELD CODE: 4X'06'
&Q.AVEXT DS    0XL5                DESCRIPTION OF SHARED EXTENT
         DS    XL2                 PTR TO 1ST CYL IN EXTENT: TT
         DS    HL2                 # OF CYLS IN EXTENT
         DS    FL1                 # OF DATA SETS SHARING THE EXTENT
&Q.EXTAV DS    7XL5                7 MORE SHARED EXTENT DESCRIPTIONS
&Q.FMTID DS    C                   FORMAT ID: C'6'
&Q.MAVET DS    18XL5               18 MORE SHARED EXTENT DESCRIPTIONS
&Q.PTRDS DS    XL5                 PTR TO NEXT FORMAT 6 DSCB (IF ANY)
         AGO   .DSCBLP
.END     MEND
