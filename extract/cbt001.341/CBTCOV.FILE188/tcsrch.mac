         MACRO
&LABEL   TCSRCH  &R,&FIELD,&CHARS,&LEN=
         LCLA  &I
         LCLC  &KEY(4),&KS(4),&REGL,&PFX
&KEY(1)  SETC  'PORT'
&KEY(2)  SETC  'LINE'
&KEY(3)  SETC  'TERM'
&KEY(4)  SETC  'TYPE'
&KS(1)   SETC  'PT'
&KS(2)   SETC  'LN'
&KS(3)   SETC  'TM'
&KS(4)   SETC  'TY'
&I       SETA  1
.LP1     AIF ('&KEY(&I)' EQ '&FIELD'(1,4)).FOUND
&I       SETA  &I+1
         AIF   (&I LE 4).LP1
         MNOTE 12,'SEARCH FIELD INVALID'
         MEXIT
.FOUND   ANOP
&PFX     SETC  '&KEY(&I)'
         LM    15,1,TC&PS(&I).FST  GET 1ST ADDR,LEN,LAST
         AIF   ('&LEN' NE '').NNULL
TCSC&SYSNDX CLC &FIELD-TC&PFX.(,15),&CHARS
         AGO   .NEXT
.NNULL   AIF   ('&LEN'(1,1) EQ '(').REG
TCSC&SYSNDX CLC   &FIELD-TC&PFX.(&LEN,15),&CHARS
         AGO   .NEXT
.REG     ANOP
&REGL    SETC  '&LEN'(2,K'&LEN-2)
         BCTR  &REGL,0             DECREMENT FOR EXECUTE
         CLC   &FIELD-TC&PFX.(*-*,15),&CHARS  EXECUTED ONLY
TCSC&SYSNDX EX  &REGL,*-6
.NEXT    BE    *+8                 IF FOUND,ESCAPE
         BXLE  15,0,TCSC&SYSNDX
         AIF   ('&REGL' EQ '').NOEX
         LA    &REGL,1(&REGL)      RESTORE REGISTER
.NOEX    ANOP
*        NOTE THAT IF CONDITION 'E' THEN FOUND, PTR IN R15
*        IF 'NE' THEN NOT FOUND
         MEND
         MACRO
