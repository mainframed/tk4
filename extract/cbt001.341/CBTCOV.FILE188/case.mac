         MACRO ,                   GRAB -- CASE -- BARG
&N       CASE  &ITEM,&EXIT=
         COPY  @STACK
         LCLC  &SYMB
         @POP  GRAB,CASE,N=&N      FIND 'GRAB'.
         AIF   (&@FAIL).END
&SYMB    SETC  '&ITEM'
         AIF   (K'&ITEM LE 5).GOTSYMB
&SYMB    SETC  '&ITEM'(1,2).'&ITEM'(K'&ITEM-2,3)
.GOTSYMB AIF   (&@MODD(&@LEVEL) EQ 1).NOMULT MULT=1 -- NO SWEAT.
         AIF   ('&SYMB' EQ 'ELSE').ELSE
         DC    0S(&ITEM/&@MODD(&@LEVEL)*&@MODD(&@LEVEL)-&ITEM.(0))
.ELSE    ANOP
.NOMULT  AIF   ('&EXIT' NE '').EXIT
         AIF   (NOT &@MODB(&@LEVEL)).FIRST   1ST TIME.
         B     BARG&@MODA(&@LEVEL) .
.FIRST   ANOP
&@MODB(&@LEVEL) SETB 1             NOT 1ST TIME ANYMORE.
@&@MODC(&@LEVEL)&SYMB DC 0Y(*-@&@MODA(&@LEVEL)&SYMB)
.REPUSH  @PUSH SKIP=1
         MEXIT
.EXIT    @DEFER &EXIT,@&@MODC(&@LEVEL)&SYMB DEFERRED EXIT.
         DC    0Y(*-@&@MODA(&@LEVEL)&SYMB)
         AGO   .REPUSH
.END     MEND
