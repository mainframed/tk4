         MACRO
         PLICALL &RTN,&SAVE=,&SCRATCH=,&PARM=
         GBLC  &$ADDR
         LCLC  &TEMP
         AIF   ('&SAVE' EQ '').NOSTR
         AIF   ('&SCRATCH' EQ '').NOSCR
         AIF   (N'&SCRATCH GT 1).STRTWO
         $MAKEADD &SCRATCH
         STM   12,13,&$ADDR
         AGO   .ENDSTR
.STRTWO  ANOP
         LR    &SCRATCH(1),12
         LR    &SCRATCH(2),13
         AGO   .ENDSTR
.NOSTR   ANOP
         MNOTE 0,'NO SAVE-R12 AND R13 ASSUMED TO POINT TO TCA AND DSA'
         AGO   .ENDSTR
.NOSCR   ANOP
         MNOTE 0,'NO SCRATCH PROVIDED- R12 AND R13 WILL BE WIPED'
.ENDSTR ANOP
         L     15,=V(&RTN)
         AIF   ('&PARM'(1,1) EQ '(').REGPARM
         LA    1,&PARM
         AGO   .ENDPARM
.REGPARM AIF ('&PARM' EQ '(1)').ENDPARM
&TEMP    SETC  '&PARM'(2,L'&PARM-2)
         LR    1,&TEMP
.ENDPARM ANOP
         AIF   ('&SAVE' EQ '').ENDLM
         $MAKEADD &SAVE
         LM    12,13,&$ADDR
.ENDLM   ANOP
         BALR  14,15
         AIF   ('&SAVE' EQ '').ENDGET
         AIF   ('&SCRATCH' EQ '').ENDGET
         AIF   (N'&SCRATCH GT 1).GETTWO
         $MAKEADD &SCRATCH
         LM    12,13,&$ADDR
         AGO   .ENDGET
.GETTWO  ANOP
         LR    12,&SCRATCH(1)
         LR    13,&SCRATCH(2)
.ENDGET ANOP
         MEXIT
         MEND
