         MACRO
&L       SETSCRN &LIST=,&DDNAME=LOG,&DDLOC=,&TTABLE=,&UPDATE=
         LCLA  &CTR
         LCLA  &FLAG
         LCLB  &LOG,&TRANS
         LCLB  &SET1,&SET2
         LCLC  &REG
.LOOP1   ANOP
&CTR     SETA  &CTR+1
         AIF   (&CTR GT N'&SYSLIST).END1
         AIF   ('&SYSLIST(&CTR)' EQ 'GLOBAL').GLOBAL
         AIF   ('&SYSLIST(&CTR)' EQ 'CANCEL').CANCEL
         AIF   ('&SYSLIST(&CTR)' EQ 'TEST').TEST
         AIF   ('&SYSLIST(&CTR)' EQ 'LOG').LOG
         AIF   ('&SYSLIST(&CTR)' EQ 'NOLOG').NOLOG
         AIF   ('&SYSLIST(&CTR)' EQ 'NOTRANS').NOTRANS
         MNOTE 12,'INVALID OPTION(S) SPECIFIED'
         MEXIT
.GLOBAL  ANOP
&FLAG    SETA  &FLAG+128
         AGO   .LOOP1
.CANCEL  ANOP
&FLAG    SETA  &FLAG+64
         AIF   (&SET1).ERRSET1
&SET1    SETB  1
         AGO   .LOOP1
.TEST    ANOP
&FLAG    SETA  &FLAG+32
         AIF   (&SET1).ERRSET1
&SET1    SETB  1
         AGO   .LOOP1
.LOG     ANOP
&FLAG    SETA  &FLAG+16
&LOG     SETB  1
         AIF   (&SET2).ERRSET2
&SET2    SETB  1
         AGO   .LOOP1
.NOLOG   ANOP
&FLAG    SETA  &FLAG+8
         AIF   (&SET2).ERRSET2
&SET2    SETB  1
         AGO   .LOOP1
.NOTRANS ANOP
&FLAG    SETA  &FLAG+4
         AGO   .LOOP1
.END1    AIF   ('&LIST' EQ '').ERRNLST
         AIF ('&LIST'(1,1) EQ '(').RFORM
&L       LA    1,&LIST             GET ADDRESS OF LIST
         AGO   .COMM1
.RFORM   ANOP
&REG     SETC  '&LIST'(2,K'&LIST-2)
&L       LR    1,&REG              GET ADDRESS OF LIST
.COMM1   AIF   (NOT &LOG).COMM2
         AIF   ('&DDLOC' EQ '').NAMETYP
         AIF   ('&DDLOC'(1,1) EQ '(').RFORM1
         LA    15,&DDLOC           GET DDNAME LOCATION
         AGO   .COMM1A
.RFORM1  ANOP
&REG     SETC  '&DDLOC'(2,K'&DDLOC-2)
         MVC   4(8,1),0(&REG)      MOVE DDNAME TO LIST
         AGO   .COMM2
.NAMETYP BAL   15,*+12             SKIP OVER LOG NAME
         DC    CL8'&DDNAME'        NAME OF LOG FILE
.COMM1A  MVC   4(8,1),0(15)        MOVE NAME TO LIST
.COMM2   AIF   ('&TTABLE' EQ '').COMM3
&TRANS   SETB  1
&FLAG    SETA  &FLAG+1
         AIF   ('&TTABLE'(1,1) EQ '(').RFORM2
         LA    15,&TTABLE          GET ADDRESS OF TRANSLATION TABLE
         ST    15,0(,1)            PLACE IN LIST
         AGO   .COMM3
.RFORM2  ANOP
&REG     SETC  '&TTABLE'(2,K'&TTABLE-2)
         ST    &REG,0(,1)          PLACE TABLE ADDRESS IN LIST
.COMM3   AIF   ('&UPDATE' EQ '').COMM4
         AIF   (&TRANS).ERRSET3
&FLAG    SETA  &FLAG+2
         AIF   ('&UPDATE'(1,1) EQ '(').RFORM3
         LA    15,&UPDATE          GET ADDRESS OF UPDATE STRING
         ST    15,0(,1)            STORE IN PARAMETER LIST
         AGO   .COMM4
.RFORM3  ANOP
&REG     SETC  '&UPDATE'(2,K'&UPDATE-2)
         ST    &REG,0(,1)          STORE ADDRESS OF UPD. STR IN LIST
.COMM4   MVI   0(1),&FLAG          SET FLAGS
         SVC   245                 ISSUE SETSCRN SVC
         MEXIT
.ERRNLST MNOTE 12,'LIST ADDRESS NOT SPECIFIED'
         MEXIT
.ERRSET1 MNOTE 12,'CONFLICTING OPTIONS CANCEL AND TEST SPECIFIED'
         MEXIT
.ERRSET2 MNOTE 12,'CONFLICTING OPTIONS LOG AND NOLOG SPECIFIED'
         MEXIT
.ERRSET3 MNOTE 12,'CONFLICTING OPTIONS TTABLE AND UPDATE SPECIFIED'
         MEND
