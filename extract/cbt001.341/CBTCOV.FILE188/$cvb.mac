         MACRO
&NME     $CVB  &PFIX=,&SOURCE=,&LEN=,&WORK=,&RESULT=,&RETURN=14
.*
.*
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MAILING ADDRESS CHANGE.
.*
.* LAST CHANGE DATE - NOVEMBER 1, 1973
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE.  ANY QUESTIONS CONCERNING
.* IT MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
         GBLC  &$TESRET(20)
         LCLA  &R(5),&W1,&W2
         LCLC  &N,&LPFX
&N       SETC  '&NME'
         AIF   (K'&NME NE 0).NMEOK
&N       SETC  'CVB'
.NMEOK   $TEST PFIX=&PFIX,REGS=(&LEN(1),&RESULT(1),&SOURCE(1),&WORK(1),*
               &RETURN(1))
&LPFX    SETC  '&$TESRET(1)'
&W1      SETA  0
.LP1    AIF    (&W1 EQ 5).END1
&W1      SETA  &W1+1
&R(&W1)  SETA  &$TESRET(&W1+1)
         AGO   .LP1
.END1    ANOP
&W1      SETA  0
.LP2     AIF   (&W1 EQ 5).END2
&W1      SETA  &W1+1
&W2      SETA  &W1
.LP3    AIF    (&W2 EQ 5).LP2
&W2      SETA  &W2+1
         AIF   (&R(&W1) NE &R(&W2)).LP3
         MNOTE 8,'"&R(&W1)" MAY NOT BE MULTIPLY SPECIFIED.'
         AGO   .LP2
.END2    AIF   (&R(3) NE 0).AOK
         MNOTE 8,'"SOURCE=" MAY NOT SPECIFY REGISTER 0.'
.AOK     ANOP
&N       SR    &LPFX&R(2),&LPFX&R(2) CLEAR THE RESULT REGISTER
         SR    &LPFX&R(4),&LPFX&R(4) CLEAR THE WORK REGISTER FOR "IC"'S
&N.LOOP  CLI   0(&LPFX&R(3)),C'0' IS CHARACTER LOWER THAN A DIGIT?
         BCR   4,&LPFX&R(5) YES, ERROR OR END; RETURN (CC<0)
         CLI   0(&LPFX&R(3)),C'9' NO, IS CHARACTER HIGHER THAN A DIGIT?
         BCR   2,&LPFX&R(5) YES, ERROR OR END; RETURN (CC>0)
         NI    0(&LPFX&R(3)),X'0F' NO, CLEAR THE ZONE
         IC    &LPFX&R(4),0(,&LPFX&R(3)) GET THE DIGIT
         MH    &LPFX&R(2),=Y(10) SHIFT THE RESULT LEFTWARDS 1 DEC DIGIT
         BCR   1,&LPFX&R(5) OVERFLOW, ERROR; RETURN (CC OVERFLOW)
         AR    &LPFX&R(2),&LPFX&R(4) ADD IN THE NEW DIGIT
         BCR   1,&LPFX&R(5) OVERFLOW, ERROR; RETURN (CC OVERFLOW)
         LA    &LPFX&R(3),1(,&LPFX&R(3)) ADVANCE THE SOURCE POINTER
         BCT   &LPFX&R(1),&N.LOOP LOOP FOR THE NEXT DIGIT (IF ANY)
         SR    &LPFX&R(4),&LPFX&R(4) FINISHED; SET CC=0
         BR    &LPFX&R(5) RETURN (CC=0)
         SPACE 1
         MEND
