         MACRO
         ARGS  &N
.*       ARGS  &N   RETRIEVES &N .LE. 8 ARGUMENTS FROM A PL/I CALLING
.*       ROUTINE.  IT ASSUMES THAT R1 POINTS TO A LIST OF POINTERS
.*       TO DOPE VECTORS, AND IT RETURNS POINTERS TO ARG 1 IN R2, ...,
.*       TO ARG 8 IN R9.   JKS
         LCLA  &MAX,&R,&D
         LCLC  &A
&MAX     SETA  8
&R       SETA  1
         AIF   (&N GT &MAX).BAD
.LOOP    ANOP
&D       SETA  4*&R-4
&A       SETC  '&R'
&R       SETA  &R+1
         MNOTE *,'  ARGUMENT &A'
         L     R&R,&D.(R1)         A(DOPE VECTOR ARG)
         L     R&R,0(R&R)          A(ARGUMENT)
         LA    R&R,0(R&R)          ELIMINATE HIGH ORDER BYTE
         AIF   (&R LE &N).LOOP
         MEXIT
.BAD     MNOTE 1,'MORE ARGS THAN AVAILABLE REGISTERS'
         MEND
