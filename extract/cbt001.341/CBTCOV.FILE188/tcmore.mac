         MACRO
&LABEL   TCMORE  &R,&FIELD,&CHARS,&LEN=,&RLAST=15
         LCLA  &I
         LCLC  &KEY(4),&REGL,&PFX,&LENCAR
&KEY(1)  SETC  'PORT'
&KEY(2)  SETC  'LINE'
&KEY(3)  SETC  'TERM'
&KEY(4)  SETC  'TYPE'
&I       SETA  1
.LP1     AIF ('&KEY(&I)' EQ '&FIELD'(1,4)).FOUND
&I       SETA  &I+1
         AIF   (&I LE 4).LP1
         MNOTE 12,'SEARCH FIELD INVALID'
         MEXIT
.FOUND   ANOP
&PFX     SETC  '&KEY(&I)'
&I       SETA  (&I-1)*6
         LH    1,&I.(&R)            GET OFFSET
         AR    1,&R                GET LIST ADDRESS
         AH    1,&I+2(&R)          GET END ADDRESS
         LH    0,&I+4(&R)          GET INCREMENT
         AIF   ('&LEN' NE '').NNULL
&LENCAR  SETC  'L''&FIELD'
TCSC&SYSNDX CLC &FIELD-TC&PFX.(&LENCAR,&RLAST),&CHARS
         AGO   .NEXT
.NNULL   AIF   ('&LEN'(1,1) EQ '(').REG
TCSC&SYSNDX CLC   &FIELD-TC&PFX.(&LEN,&RLAST),&CHARS
         AGO   .NEXT
.REG     ANOP
&REGL    SETC  '&LEN'(2,K'&LEN-2)
         BCTR  &REGL,0             DECREMENT FOR EXECUTE
         CLC   &FIELD-TC&PFX.(*-*,&RLAST),&CHARS  EXECUTED ONLY
TCSC&SYSNDX  EX    &REGL,*-6
.NEXT    BE    *+8                 IF FOUND,ESCAPE
         BXLE  &RLAST,0,TCSC&SYSNDX
         AIF   ('&REGL' EQ '').NOEX
         LA    &REGL,1(&REGL)      RESTORE REGISTER
.NOEX    ANOP
         LR    &RLAST,15           PUT ANSWER IN R15
*        NOTE THAT IF CONDITION 'E' THEN FOUND, PTR IN &RLAST
*        IF 'NE' THEN NOT FOUND
         MEND
