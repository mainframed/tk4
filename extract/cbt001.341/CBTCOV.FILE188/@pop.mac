         MACRO ,                   ** CUT STACK POINTER.
         @POP  &NAME,&THIS,&N=
         COPY  @STACK
         GBLB  &@NEST
         LCLA  &I
&I       SETA  &@LEVEL             (IN CASE HAVE TO RESTORE).
.LOOP    ANOP
&@LEVEL  SETA  &@LEVEL-1           POP 1 LEVEL.
         AIF   ('&@NAME(&@LEVEL)' EQ '&NAME').OK
         @ERROR 3,&@NAME(&@LEVEL)
         AIF   (&@LEVEL GT 1).LOOP IF WE OVERSHOOT THE ORIGIN, THEN
&@LEVEL  SETA  &I                  RESTORE &@LEVEL.
         @ERROR 2,&NAME,&THIS      TELL USER NO MATCH (SETS &@FAIL).
         MEXIT
.OK      ANOP
&@FAIL   SETB  0
.******* AIF   (NOT &@NEST).NONEST
&I       SETA  &@LEVEL-2
         MNOTE *,'<<< LEVEL = &I..'
.NONEST  AIF   ('&N' NE '').ISNAME
         MEXIT
.ISNAME  @ERROR 4,&THIS
         MEND
