         MACRO
&N       $DIAG &R1,&R3,&D2
.*
.*
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MAILING ADDRESS CHANGE.
.*
.* LAST CHANGE DATE - FEBRUARY 3, 1976
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE. ANY QUESTIONS CONCERNING IT
.* MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
.*
.*   THIS MACRO GENERATES A DIAGNOSE MACHINE INSTRUCTION (OPCODE IS
.* X'83'). IF THE THIRD POSITIONAL OPERAND IS PRESENT, THEN AN RS-TYPE
.* INSTRUCTION FORMAT IS USED. IF THE THIRD POSITIONAL OPERAND IS
.* OMITTED, THEN AN RX-TYPE INSTRUCTION FORMAT IS USED.
.*
.*
.*
.* INNER MACROS USED - NONE
.*
         AIF   (K'&D2 EQ 0).RX
&N       LM    &R1,&R3,&D2 GENERATE AN RS-TYPE INSTRUCTION
         AGO   .OPCODE
.RX      ANOP
&N       L     &R1,&R3 GENERATE AN RX-TYPE INSTRUCTION
.OPCODE  ORG   *-4 LOCATE BACK TO CHANGE THE OPCODE
         DC    X'83' GENERATE THE DIAGNOSE OPCODE
         ORG   *+3 RELOCATE FORWARD
         MEND
