         MACRO
&NAME    RRETURN &RC=,&LENGTH=
         GBLC  &RSECTL
         LCLC  &RC1
         AIF   (T'&LENGTH EQ 'O').A
&RSECTL SETC '&LENGTH'
.A       ANOP
&NAME    LR    R1,R13              FREE SAVE AREA
         L     R13,4(R13)          BUT GET CALLER'S SAVE
         LA    R0,&RSECTL        LENGTH TO FREE
         AIF   (T'&RC EQ 'O').NORC
&RC1     SETC  '&RC(1)'
         AIF   ('&RC'(1,1) EQ '(').REGRC
         LA    R15,&RC             RETURN CODE
&RC1     SETC  'R15'
.REGRC   ST    &RC1,16(R13)         STORE RETURN CODE IN CALLER'S R15
.NORC    FREEMAIN R,LV=(0),A=(1)
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGS
         MVI   12(R13),X'FF'        FLAG RETURN
         BR    R14                 AND RETURN
         MEND
