         MACRO ,                   OPTIONAL TERM IN 'IF' BLOCK.
&LAB     ELSE  &DUMMY,&EXIT=
         COPY  @STACK
         @POP  IF,ELSE,N=&LAB
         AIF   (&@FAIL).END        PUNT IF NOT FOUND.
         AIF   (&@MODB(&@LEVEL)).DUP         ALREADY HAVE AN ELSE.
&@MODB(&@LEVEL) SETB 1                       INDICATE 'ELSE'.
         AIF   ('&EXIT' NE '').EXIT          PROCESS EXIT=.
.NORMAL  B     FI&@MODA(&@LEVEL) .            SKIP 'ELSE' SECTION.
ELSE&@MODA(&@LEVEL) EQU *
.REPUSH  @PUSH SKIP=1              RE-PUSH THE ORIGINAL 'IF'.
         MEXIT
.DUP     @ERROR 8,ELSE
         AGO   .REPUSH
.*
.*       'ELSE EXIT=ALPHA' IS BETTER THAN 'ELSE','EXIT ALPHA'
.*       SINCE IN THE FORMER CASE WE CAN ELIMINATE BOTH THE
.*       ELSE-GENERATED SKIP AROUND THE ELSE CODE & THE
.*       EXIT-GENERATED BRANCH.  WE WILL SET UP AN ENTRY SO
.*       THAT THE EXIT'S WONK WILL KNOW THAT 'ELSE...' SHOULD
.*       BE EQUATED TO 'EXIT---'.
.*
.EXIT    @DEFER &EXIT,ELSE&@MODA(&@LEVEL)    SET UP DEFERRED EXIT.
         AIF   (&@FAIL).NORMAL
         MNOTE *,'(''FI'' MUST FOLLOW IMMEDIATELY)'
         AGO   .REPUSH
.END     MEND
