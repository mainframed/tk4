         MACRO
&N       $ALD  &R,&A
.*
.*
.*
.* LAST CHANGE DATE - FEBRUARY 2, 1977
.*                  - MAILING ADDRESS CHANGE.
.*
.* LAST CHANGE DATE - FEBRUARY 3, 1976
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE. ANY QUESTIONS CONCERNING IT
.* MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CONN. 06511
.*
.*
.*
.*   THIS MACRO SIMULATES A HYPOTHETICAL "ADD LOGICAL DOUBLE" MACHINE
.* INSTRUCTION. ITS FUNCTION IS SIMILAR TO THE "AL" MACHINE INSTRUCTION
.* EXCEPT THAT IT OPERATES ON 64-BIT NUMBERS. WARNING, THE RESULTING
.* CONDITION CODE IS NOT AN ANALOGOUS EXTENSION FROM THE "AL"
.* INSTRUCTION.
.*
.* INNER MACROS USED - $TEST
.*
.*
.*
         GBLA  &$TESERR
         GBLC  &$TESRET(20)
         LCLA  &A1
         LCLC  &RODD
&RODD    SETC  '&R+1'
         $TEST REGS=&R
         AIF   (&$TESERR NE 0).REGUNKN
&A1      SETA  &$TESRET(1)+1
         AIF   (&A1 LE 15).GOTA1
&A1      SETA  0
.GOTA1   $TEST PFIX=
&RODD    SETC  '&$TESRET(1)&A1'
.REGUNKN ANOP
&N       AL    &RODD,4+&A LOW-ORDER SUM; HI-ORDER AFFECTED?
         BC    12,ALD&SYSNDX NO, SKIP
         AL    &R,=F'1' YES, ADJUST HI-ORDER
ALD&SYSNDX AL  &R,&A GET HI-ORDER SUM
         MEND
