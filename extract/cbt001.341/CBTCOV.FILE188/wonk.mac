         MACRO ,                   CLOSING 'KNOW'.
&N       WONK  &DUMMY
         COPY  @STACK
         LCLA  &I,&J               INDICES FOR 'ELSE EXIT=' LOOP.
         LCLC  &C
         @POP  KNOW,WONK,N=&N      FIND CORRESPONDING 'KNOW'.
         AIF   (&@FAIL).END
         AIF   (&@MODB(&@LEVEL)).ISONXIT     GO IF IS AN 'ONEXIT'.
EXIT&@MODA(&@LEVEL) DS 0H .                END OF 'KNOW' BLOCK.
         AIF   (&@MODD(&@LEVEL) GT 0).ELSE   DO 'ELSE EXIT=' STUFF.
         SPACE 1
         MEXIT
.ISONXIT ANOP
WONK&@MODA(&@LEVEL) DS 0H .                END OF 'KNOW' BLOCK.
         AIF   (&@MODD(&@LEVEL) GT 0).ELSE
         SPACE 1
         MEXIT
.*
.*       PROCESS KRUD SET UP BY 'ELSE EXIT=...'.  &@MODD = THE
.*       NUMBER OF SUCH THINGS EXITING ON OUR 'KNOW'.
.*
.ELSE    ANOP
&I       SETA  &@MODD(&@LEVEL)     COUNT DOWN THRU 'ELSE EXIT='S.
&J       SETA  0-1                 COUNT UP   THRU &@ELSEXT.
&C       SETC  '&@MODA(&@LEVEL)'
.L1      ANOP  ,
&J       SETA  &J+2
         AIF   ('&@ELSEXT(&J)' NE '&C').L1
&@ELSEXT(&J+1) EQU EXIT&C
&@ELSEXT(&J)   SETC ''
&I       SETA  &I-1
         AIF   (&I GT 0).L1
         SPACE 1
.END     MEND
