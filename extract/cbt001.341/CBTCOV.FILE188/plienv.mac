         MACRO
&LABEL   PLIENV &LEN=1,&SAVE=
.*  MACRO USED TO SET UP A PLI ENVIRONMENT FROM AN ASSEMBLER
.*  PROGRAM.
.*  GETMAINS YOUR "ISA" AND INITIALIZED APPROPRIATE FIELDS IN IT.
.*  PARAMETERS ARE:
.*     LEN=DECIMAL NUMBER      IS THE SIZE OF THE DSA IN K.
.*                   MINIMUM 1K, WHICH IS ENOUGH FOR ONE LEVEL
.*                   OF SUBROUTINE CALL WITH HAS NO WORK AREA
.*                   FIGURE 240+N*120 BYTES AS A MIN, WHERE N IS THE
.*                  DEEPEST LEVEL OF SUBROUTINE NESTING. GIVE IT A GOOD
.*                     DEAL MORE TO BE SAFE
.*     SAVE=ADDR        THE ADDRESS OF A DOUBLEWORD INTO WHICH
.*                      THE MACRO CAN WRITE
.*     THEN, WHEN YOU WANT TO START CALLING PLI ROUTINES,
.*       USE THE PLICALL MACRO
.*
.*   TO FREEMAIN THE CORE GOTTEN FOR THE PLI ENVIRONMENT DO:
.*    PLIDONE LEN=...,SAVE=.... WITH THE SAME PARAMETERS
         LCLA  &MINSP
         LCLC  &L,&#                                                DBC
         GBLC  &$ADDR
&#       SETC  '&SYSNDX'                                            DBC
&MINSP   SETA  1
         AIF   (&LEN LT &MINSP).NOTE1
&L       SETC  '&LABEL        '(1,8)                                DBC
         MNOTE '&L GETMAIN R,LV=&LEN.000'                           DBC
&L       GETMAIN R,LV=&LEN.000
         AIF   ('&SAVE' EQ 'NONE').NOSAV1A                          DBC
         AIF   ('&SAVE' EQ '').NOSAVE1
         $MAKEADD &SAVE
         ST    R1,&$ADDR            SAVE TCA ADDRESS
         AGO   .ENDSV1
.NOSAVE1 ANOP
         MNOTE 0,'NO SAVE AREA SPECIFIED. R12 AND R13 HAVE BEEN TAKEN OX
               VER AND SHOULD NOT BE USED UNTIL PLIDONE IS CALLED'
.NOSAV1A ANOP                                                       DBC
         LR    R12,R1
.ENDSV1  ANOP
         LA    R0,120(R1)          GET START OF DSA ADDR
         ST    R0,8(R1)            STORE IN TCA
         A     R0,=A(&LEN.000-120)                              JSA
         BCTR  R0,0                GET END OF DSA ADDRESS
         ST    R0,12(R1)           STORE IN TCA
         MVI   8(R1),X'FF'         FLAG THE ADDRESSES
         MVI   12(R1),X'FF'        WITH SEG#
         MVC   108(12,R1),PLIR&#   MOVE IN ADDRESSES OF ROUTINES
         LA    R1,120(R1)          ADDRESS DSA
         AIF   ('&SAVE' EQ 'NONE').NOSAVE2                          DBC
         AIF   ('&SAVE' EQ '').NOSAVE2
         $MAKEADD &SAVE
         ST    R1,&$ADDR+4          RECORD
         AGO   .ENDSV2
.NOSAVE2 ANOP
         LR    R13,R1              ESTABLISH DSA PTR
.ENDSV2  ANOP
         LA    R0,120(R1)          ADDRESS OF NEXT AVAIL DSA
         ST    R0,76(R1)           STORE IN NAB OF DSA
         MVI   76(R1),X'FF'        SET SEGMENT NUMBER           JSA
         B     PLIE&#              BRANCH AROUND ROUTINES AND DATA
PLIR&#   DC    A(PLIG&#,PLIF&#,PLIO&#)
PLIG&#   DS    0H
         USING *,R15
         MNOTE '         GETMAIN R,LV=(0)'                          DBC
         GETMAIN R,LV=(0)
         BR    R14
         DROP  R15
PLIF&#   DS    0H
         USING *,R15
         MNOTE '         FREEMAIN R,LV=(0),A=(1)'                   DBC
         FREEMAIN R,LV=(0),A=(1)
         BR    R14
         DROP  R15
         MNOTE 'PLIO&# ABEND 7'                                     DBC
PLIO&#   ABEND 7
PLIE&#   DS    0H
         MEXIT
.NOTE1   MNOTE 8,'NOT ENOUGH SPACE FOR PLI ISA. MINIMUM IS &MINSP '
         MEND
