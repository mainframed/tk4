* STATUS = VERSION 1 LEVEL 0
* CHANGED =  6/21/79  HKG  PERMIT "LEAVE" FROM EPILOG
         MACRO
&LAB     BLOCK &DUMMY,&EPILOG=NO
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         YCCPUSH NAME=&LAB,TYPE=BLOCK
         AIF   ('&EPILOG' NE 'YES').EXITOK
&YCCEPLG(&YCCLVL) SETB (1)
.EXITOK  ANOP
&LAB     DS    0H
ST&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
         SPACE 1
         MEND
         SPACE 5
         MACRO
         BEND  &BLKNM,&EPILOG=NO
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         AIF   (&YCCLVL GT 0).OKLVL
         MNOTE 8,'BEND OR EPILOG STATEMENT OUTSIDE OF BLOCK'
         SPACE 1
         MEXIT
.OKLVL   AIF   ('&BLKNM' NE '&YCCBLKN(&YCCLVL)').ERR1
         AIF   (&YCCTYPE(&YCCLVL) EQ 0).DCRMT
         AIF   (&YCCTYPE(&YCCLVL) EQ 1 AND &YCCEPLG(&YCCLVL) AND '&EPIL*
               OG' EQ 'NO').ERR2
         AIF   (&YCCTYPE(&YCCLVL) NE 2 AND &YCCTYPE(&YCCLVL) NE 3).NLP
         B     TS&YCCLVL.@&YCCBLK#(&YCCLVL)
EN&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
EL&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
         AGO   .DCRMT
.NLP     AIF   (&YCCTYPE(&YCCLVL) EQ 1 OR &YCCTYPE(&YCCLVL) EQ 5).NELS
EL&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
.NELS    ANOP
EN&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
.DCRMT   ANOP
         AIF   ('&EPILOG' EQ 'YES').EXIT
         YCCGRAF
&YCCLVL  SETA  &YCCLVL-1
         YCCGRAF
         SPACE 5
.EXIT    MEXIT
.ERR1    MNOTE 8,'BLOCK NAME OMITTED OR DOES NOT MATCH CURRENT BLOCK'
         SPACE 1
         MEXIT
.ERR2    MNOTE 8,'MISSING EPILOG FOR THIS BLOCK'
         SPACE 1
         MEND
         SPACE 5
         MACRO
&LAB     LEAVE &BLKNM
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         LCLA &LVL
&LVL     SETA  &YCCLVL
         AIF   ('&YCCBLKN(&LVL)' EQ '&BLKNM' AND &YCCTYPE(&LVL) EQ 0).E*
               RR2
.LP      AIF   (&LVL EQ 0).ERR
         AIF   ('&BLKNM' EQ '&YCCBLKN(&LVL)').OK
         AIF   (&YCCEPLG(&LVL)).ERREXIT
&LVL     SETA  &LVL-1
         AGO   .LP
.OK      ANOP
&LAB     B     EN&LVL.@&YCCBLK#(&LVL)
         SPACE 1
         MEXIT
.ERR     MNOTE 8,'ATTEMPT TO LEAVE UNRECOGNIZED BLOCK'
         SPACE 1
.ERREXIT MNOTE 8,'CANNOT EXIT BEYOND BLOCK &YCCBLKN(&LVL)'
         SPACE 1
         MEXIT
.ERR2    MNOTE 8,'YOU CANNOT LEAVE A BLOCK FROM ITS OWN EPILOG'
         MEND
         SPACE 5
         MACRO
&LAB     EPILOG &BLKNM
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
&LAB     DS    0H
         AIF   (&YCCTYPE(&YCCLVL) NE 1).ERR1
         BEND  &BLKNM,EPILOG=YES
&YCCTYPE(&YCCLVL) SETA 0
&YCCEPLG(&YCCLVL) SETB (0)
         MEXIT
.ERR1    MNOTE 8,'EPILOG APPEARS OUT OF CONTEXT'
         MEND
         SPACE 5
         MACRO
&LAB     IF    &DUMMY
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         LCLA  &I
&I       SETA  1
         YCCPUSH NAME=&LAB,TYPE=IF
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
.LP      AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1)
&I       SETA  &I+2
         AGO   .LP
.DONE    ANOP
ST&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
         SPACE 1
         MEND
         SPACE 5
         MACRO
&LAB     DO    &DUMMY
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         LCLA  &I
&I       SETA  2
         AIF   ('&DUMMY' NE 'WHILE' AND '&DUMMY' NE 'INFINITE').TIL
         YCCPUSH NAME=&LAB,TYPE=DOWILE
         AIF   ('&LAB' EQ '').NOLAB
&LAB     DS    0H
.NOLAB   ANOP
         AGO   .TESTS
.TIL     AIF   ('&DUMMY' NE 'UNTIL').ERR1
         YCCPUSH NAME=&LAB,TYPE=DOTIL
         AIF   ('&LAB' EQ '').NOLAB2
&LAB     DS    0H
.NOLAB2  ANOP
         B     ST&YCCLVL.@&YCCBLK#(&YCCLVL)
         AGO   .TESTS
.ERR1    MNOTE 8,'DO MUST BE (UNTIL×WHILE×INFINITE)'
.TESTS   ANOP
TS&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
.LP      AIF   ('&SYSLIST(&I)' EQ '').DONE
         YCCTEST &SYSLIST(&I),&SYSLIST(&I+1)
&I       SETA  &I+2
         AGO   .LP
.DONE    ANOP
ST&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
         SPACE 1
         MEND
         SPACE 5
         MACRO
         ELSE  &DUMMY
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         B     EN&YCCLVL.@&YCCBLK#(&YCCLVL)
         AIF   (&YCCTYPE(&YCCLVL) NE 4).ERR1
&YCCTYPE(&YCCLVL) SETA 5
EL&YCCLVL.@&YCCBLK#(&YCCLVL) YCCTAG
         SPACE 1
         MEXIT
.ERR1    MNOTE 8,'MISPLACED ELSE STATEMENT'
         SPACE 1
         MEND
         SPACE 5
         MACRO
&LAB     YCCTAG
&LAB     DS    0H
         MEND
         SPACE 5
         MACRO
         YCCTEST &A,&B
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         LCLC  &N
         AIF   ('&B' EQ 'AND' OR '&B' EQ 'OR' OR '&B' EQ '' OR '&B' EQ *
               'THEN').TCONJ
         MNOTE 8,'&B SHOULD BE (AND×OR×THEN)'
.TCONJ   AIF   (&YCCTYPE(&YCCLVL) EQ 2).TIL
         AIF   ('&B' NE 'OR').IFAND
&YCCGO   SETC  'ST'
&YCCOMP  SETB  (0)
         AGO   .OKCONJ
.IFAND   ANOP
&YCCGO   SETC  'EL'
&YCCOMP  SETB  (1)
         AGO   .OKCONJ
.TIL     AIF   ('&B' EQ 'AND').TILAND
&YCCGO   SETC  'EL'
&YCCOMP  SETB  (0)
         AGO   .OKCONJ
.TILAND  ANOP
&YCCGO   SETC  'ST'
&YCCOMP  SETB  (1)
.OKCONJ  AIF   (N'&A NE 1).COMPND
         YCCOND &A
         BC    &YCCBC,&YCCGO.&YCCLVL.@&YCCBLK#(&YCCLVL)
         MEXIT
.COMPND  AIF   ('&A(2)' NE 'BY').NBXH
         AIF   ('&YCCGO' EQ 'EL').OKBXH
         MNOTE 8,'ILLEGAL AND×OR AFTER INDEXED-BY IN UNTIL×WHILE STMT'
.OKBXH   BXH   &A(1),&A(3),EL&YCCLVL.@&YCCBLK#(&YCCLVL)
         MEXIT
.NBXH    ANOP
         YCCOND &A(2)
         AIF   ('&A(4)' EQ '').DFLT
         &A(4) &A(1),&A(3)
         BC    &YCCBC,&YCCGO.&YCCLVL.@&YCCBLK#(&YCCLVL)
         MEXIT
.DFLT    AIF   (&YCCTM).TM
         AIF   ('&A(3)' NE '0').NLTR
         LTR   &A(1),&A(1)
         AGO   .TST
.NLTR    AIF   (T'&A(3) NE 'N').NCH
         AIF   ('&A(3)'(K'&A(3),1) EQ '''').NCH
         CH    &A(1),=H'&A(3)'
         AGO   .TST
.NCH     C     &A(1),&A(3)
.TST     BC    &YCCBC,&YCCGO.&YCCLVL.@&YCCBLK#(&YCCLVL)
         MEXIT
.TM      TM    &A(1),&A(3)
         BC    &YCCBC,&YCCGO.&YCCLVL.@&YCCBLK#(&YCCLVL)
         MEND
         SPACE 5
         MACRO
         YCCOND &A
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         LCLC  &N
&N       SETC  '&A'
         AIF   ('&N'(1,1) NE 'N').POS
&N       SETC  '&N'(2,K'&N-1)
.POS     ANOP
&YCCTM   SETB  (0)
         AIF   ('&N' NE 'EQ' AND '&N' NE 'E' AND '&N' NE 'OFF').NEQ
&YCCBC  SETA  8
         AGO   .COMP
.NEQ     AIF   ('&N' NE 'LT' AND '&N' NE 'MIX').NLT
&YCCBC  SETA  4
         AGO   .COMP
.NLT     AIF   ('&N' NE 'GT').NGT
&YCCBC  SETA  2
         AGO   .COMP
.NGT     AIF   ('&N' NE 'LE').NLE
&YCCBC  SETA  8+4+1
         AGO   .COMP
.NLE     AIF   ('&N' NE 'GE').NGE
&YCCBC  SETA  8+2+1
         AGO   .COMP
.NGE     AIF   ('&N' NE 'ON').NON
&YCCBC  SETA  1
         AGO   .COMP
.NON     MNOTE 8,'RELATION NOT EQ,NE,LT,GT,LE,GE,ON,OFF,MIX,OR BY'
&YCCBC  SETA  0
.COMP    AIF   ('&A'(1,1) NE 'N').NC1
&YCCBC  SETA  15-&YCCBC
.NC1     AIF   (NOT &YCCOMP).NC2
&YCCBC  SETA  15-&YCCBC
.NC2     AIF   ('&N' NE 'ON' AND '&N' NE 'OFF' AND '&N' NE 'MIX').ARITH
&YCCTM   SETB  (1)
.ARITH   MEND
         SPACE 5
         MACRO
         YCCPUSH &NAME=,&TYPE=
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
         AIF   (&YCCLVL EQ 30).OVFLOW
         AIF   (&YCCLVL EQ 0).NAMEOK
         AIF   ('&YCCBLKN(&YCCLVL)' NE '').NAMEOK
         MNOTE 4,'BLOCK NESTED WITHIN OTHER UNNAMED BLOCK'
.NAMEOK  ANOP
         YCCGRAF
&YCCLVL  SETA  &YCCLVL+1
         YCCGRAF
&YCCBLK#(&YCCLVL) SETA  &YCCBLK#(&YCCLVL)+1
&YCCBLKN(&YCCLVL) SETC  '&NAME'
&YCCEPLG(&YCCLVL) SETB (0)
         AIF   ('&TYPE' NE 'BLOCK').NBLK
&YCCTYPE(&YCCLVL) SETA 1
         MEXIT
.NBLK    AIF ('&TYPE' NE 'DOTIL').NDOTIL
&YCCTYPE(&YCCLVL) SETA 2
         MEXIT
.NDOTIL   AIF   ('&TYPE' NE 'DOWILE').NDOWILE
&YCCTYPE(&YCCLVL) SETA 3
         MEXIT
.NDOWILE AIF   ('&TYPE' NE 'IF').NOTIF
&YCCTYPE(&YCCLVL) SETA 4
         MEXIT
.NOTIF   MNOTE 8,'UNRECOGNIZED BLOCK TYPE'
         MEXIT
.OVFLOW  MNOTE 16,'BLOCKS NESTED TOO DEEPLY FOR HAL MACROS'
         MEND
         SPACE 5
         MACRO
         YCCGRAF
         GBLA  &YCCBC,&YCCLVL,&YCCBLK#(30),&YCCTYPE(30)
         GBLC  &YCCGRAF,&YCCBLKN(30),&YCCGO
         GBLB  &YCCEPLG(30),&YCCOMP,&YCCTM
&YCCGRAF SETC  ' '
         AIF   (&YCCLVL GT 15 OR &YCCLVL EQ 0).NOGRAF
&YCCGRAF SETC  '--------------<'(15-&YCCLVL,&YCCLVL-1).'123456789ABCDEF*
               '(&YCCLVL,1)
.*       MNOTE '
.*             &YCCGRAF'
.NOGRAF  ANOP
         MEND
         SPACE 5
         MACRO
&NAME    DCL   &TYPE,&INIT,&CLASS,&DEFINED=
         GBLC  &YCCVN(400),&YCCVI(400),&YCCVT(400),&YCCVD(400)
         GBLB  &YCCST(400)
         GBLA  &YCCV#
         AIF   ('&TYPE' EQ '').ERR1
         AIF   ('&CLASS' NE 'STATIC' AND '&CLASS' NE '').ERR2
         AIF   ('&DEFINED' NE '' AND '&INIT' NE '' AND '&CLASS' NE 'STA*
               TIC').ERR3
         AIF   ('&INIT' EQ '').NOPCHK
         AIF   ('&INIT'(1,2) EQ '->' AND '&TYPE' NE 'A').ERR3
.NOPCHK  ANOP
&YCCV#   SETA  &YCCV#+1
&YCCVN(&YCCV#) SETC '&NAME'
&YCCVT(&YCCV#) SETC '&TYPE'
&YCCVI(&YCCV#) SETC '&INIT'
&YCCVD(&YCCV#) SETC '&DEFINED'
&YCCST(&YCCV#) SETB ('&CLASS' EQ 'STATIC')
         MEXIT
.ERR1    MNOTE 8,'MISSING PARAMETERS'
         MEXIT
.ERR2    MNOTE 8,'STORAGE CLASS NOT STATIC OR DEFAULT'
         MEXIT
.ERR3    MNOTE 8,'CANNOT INITIALIZE ''DEFINED'' AUTOMATIC VARIABLE'
         MEXIT
.ERR4    MNOTE 8,'-> INITIALIZATION VALID ONLY FOR ''A'' ADCONS'
         MEND
         SPACE 5
         MACRO
&NAME    PROC  &OPTIONS=,&BR=11
         GBLB  &YCCPLI
         GBLC  &YCCVN(400),&YCCVI(400),&YCCVT(400),&YCCVD(400)
         GBLB  &YCCST(400)
         GBLA  &YCCV#
         LCLA  &I
         LCLC  &N
&YCCPLI  SETB  ('&OPTIONS' EQ 'PLI')
&NAME.A  CSECT
         DC    CL7'&NAME'
         DC    AL1(7)
         ENTRY &NAME
**********************************************************************
*             STANDARD STORAGE ALLOCATION PROLOG                     *
**********************************************************************
&NAME    STM   14,12,12(13)
         BALR  &BR,0
         USING *,&BR
         AIF   (&YCCPLI).PLI
*        GETMAIN R,LV=@DSALEN
         GETMAIN R,LV=@DSALEN
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
         AGO   .COMEND
.PLI     ANOP
         LA    0,@DSALEN
         L     1,76(13)
         ALR   0,1
         CL    0,12(12)
         BNH   *+10
         L     15,116(12)
         BALR  14,15
         ST    0,76(1)
         ST    13,4(1)
         MVC   72(4,1),72(13)
         LR    13,1
         MVI   0(13),X'80'
         MVI   86(13),X'91'
         MVI   87(13),X'C0'
.COMEND  ANOP
         USING @DSA,13
         LA    2,22*4(13)
         L     3,=A(@DSALEN-22*4)
         LA    4,@INITVAL
         L     5,=A(@INITL)
         MVCL  2,4
&I       SETA  1
.LP      AIF   (&I GT &YCCV#).EL
         AIF   ('&YCCVI(&I)' EQ '').NX
         AIF   ('&YCCVI(&I)'(1,2) NE '->').NX
&N       SETC  '&YCCVI(&I)'(3,K'&YCCVI(&I)-2)
&YCCVI(&I) SETC ''
         LA    1,&N
         ST    1,&YCCVN(&I)
.NX      ANOP
&I       SETA  &I+1
         AGO   .LP
.EL      ANOP
         SPACE 5
         L     1,4(13)
         LM    1,5,24(1)
*    END OF STORAGE ALLOCATION PROLOG
         MEND
         SPACE 5
         MACRO
&NAME    PEND  &DSA=
         GBLC  &YCCVN(400),&YCCVI(400),&YCCVT(400),&YCCVD(400)
         GBLB  &YCCST(400),&YCCPLI
         GBLA  &YCCV#
         LCLC  &N
         LCLA  &I
&NAME    DS    0H
         L     13,4(13)
         STM   15,1,16(13)
         AIF   (&YCCPLI).PLI
         L     1,8(13)
*        FREEMAIN R,LV=@DSALEN,A=(1)
         FREEMAIN R,LV=@DSALEN,A=(1)
.PLI     ANOP
         LM    14,12,12(13)
         BR    14
         EJECT
**********************************************************************
*                   STATIC DATA AREA                                 *
**********************************************************************
&I       SETA  1
.LP#1    AIF   (&I GT &YCCV#).EL#1
         AIF   (NOT &YCCST(&I)).NX#1
&N       SETC  '&YCCVN(&I)'
         AIF   ('&YCCVD(&I)' NE '').VD#1
&N       DC    &YCCVT(&I)&YCCVI(&I)
         AGO   .NX#1
.VD#1    ANOP
         ORG   &YCCVD(&I)
         AIF   ('&YCCVI(&I)' NE '').DC
&N       DS    &YCCVT(&I)
         AGO   .ORG
.DC      ANOP
&N       DC    &YCCVT(&I)&YCCVI(&I)
.ORG     ANOP
         ORG
.NX#1    ANOP
&I       SETA  &I+1
         AGO   .LP#1
.EL#1    ANOP
         SPACE 5
**********************************************************************
*                  INITIALIZING CONSTANTS                            *
**********************************************************************
@INITVAL EQU   *
&I       SETA  1
.LP#2    AIF   (&I GT &YCCV#).EL#2
         AIF   (&YCCST(&I)).NX#2
         AIF   ('&YCCVI(&I)' EQ '').NX#2
         DC    &YCCVT(&I)&YCCVI(&I)
.NX#2    ANOP
&I       SETA  &I+1
         AGO   .LP#2
.EL#2    ANOP
@INITL   EQU   *-@INITVAL
         SPACE 5
         LTORG
         SPACE 5
**********************************************************************
*                    AUTOMATIC STORAGE                               *
**********************************************************************
@DSA     DSECT
         DS    22A
&I       SETA  1
.LP#3    AIF   (&I GT &YCCV#).EL#3
         AIF   (&YCCST(&I)).NX#3
&N       SETC  '&YCCVN(&I)'
         AIF   ('&YCCVI(&I)' EQ '').NX#3
         AIF   ('&YCCVI(&I)' NE '').DC2
&N       DS    &YCCVT(&I)
         AGO   .NX#3
.DC2     ANOP
&N       DC    &YCCVT(&I)&YCCVI(&I)
.NX#3    ANOP
&I       SETA  &I+1
         AGO   .LP#3
.EL#3    ANOP
*
* THE FOLLOWING VARIABLES HAVE NO EXPLICIT INITIAL VALUE OR ARE
* DEFINED ON PREVIOUS VARIABLES
*
&I       SETA  1
.LP#4    AIF   (&I GT &YCCV#).EL#4
         AIF   (&YCCST(&I)).NX#4
&N       SETC  '&YCCVN(&I)'
         AIF   ('&YCCVI(&I)' NE '').NX#4
         AIF   ('&YCCVD(&I)' NE '').VD#4
&N       DS    &YCCVT(&I)
         AGO   .NX#4
.VD#4    ANOP
         ORG   &YCCVD(&I)
&N       DS    &YCCVT(&I)
         ORG
.NX#4    ANOP
&I       SETA  &I+1
         AGO   .LP#4
.EL#4    ANOP
         AIF   ('&DSA' EQ 'EXTEND').END
         DS    0D
@DSALEN  EQU   *-@DSA
.END     MEND
