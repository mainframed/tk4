         MACRO
         @DEFER &EXIT,&LABEL
         COPY  @STACK
.*
.*       ASSOCIATE &LABEL WITH THE KNOW-BLOCK FOR &EXIT,
.*       SO THAT WONK WILL EQUATE IT TO 'EXIT....'
.*       @DEFER IS CALLED BY 'ELSE EXIT=...' AND 'CASE N,EXIT=...'
.*
         LCLA  &I                  INDEX FOR EXIT= PROCESSING.
         @EXIT &EXIT,IFTEST=0      FIND THE PROPER KNOW.
         AIF   (&@FAIL).END        PUNT IF NOT FOUND.
.*       PUT DATA IN KLUDGEY ASSOCIATIVE MEMORY --
&I       SETA  1
.L1      AIF   ('&@ELSEXT(&I)' EQ '').HIT    EMPTY SLOT.
&I       SETA  &I+2
         AGO   .L1
.HIT     AIF   (&I GT &@ELSEMX).IGNORE       OVER END.
&@ELSEXT(&I)   SETC '&@MODA(&@VALUE)'        KNOW'S NAME.
&@ELSEXT(&I+1) SETC '&LABEL'       TARGET NAME.
&@MODD(&@VALUE) SETA &@MODD(&@VALUE)+1       BUMP ITEM COUNT.
         MEXIT
.IGNORE  ANOP
&I       SETA  &@ELSEMX/2          MAX SIZE OF MEMORY.
         @ERROR 1,DEFERRED-EXITS,&I
.END     MEND
