*
*        BCDHEX: CONVERT EBCDIC TO HEX BINARY
*        ENTRY:  BAL R14,BCDHEX
*        INPUT:  R0 = NUMBER OF CHARACTERS, 1-8.
*                R1 = A(FIRST CHARACTER)
*                R13 = A(SAVE AREA)
*        OUTPUT: R1 = HEX NUMBER, RIGHT JUSTIFIED.
*                R15 = 0, CONVERSION OK.
*                R15 ^= 0, INPUT NOT ALL VALID HEX CHARACTERS,
*                          OR LENGTH NOT 1-8.
*
BCDHEX   STM   R14,R12,12(R13)
         LR    R15,R13             INSURE R15 ^= 0.
         CH    R0,=H'8'
         BH    BCDHEXBR            R0 = LENGTH, MUST BE <= 8,
         LTR   R0,R0
         BNH   BCDHEXBR            AND ALSO > 0.
         SR    R2,R2               INITIALIZE RESULT.
BCDHEXCK LA    R4,=C'0123456789ABCDEF'
         LA    R5,16               CHECK FOR 16 VALID HEX DIGITS.
BCDHEXLP CLC   0(1,R1),0(R4)
         BE    BCDHEXOK
         LA    R4,1(R4)
         BCT   R5,BCDHEXLP
         B     BCDHEXBR
BCDHEXOK IC    R3,0(R1)            CURRENT CHARACTER
         TM    0(R1),X'F0'         ALPHABETIC?
         BO    BCDHEXNM            NO, NUMERIC.
         LA    R3,9(R3)            INCREMENT BY 9.
BCDHEXNM SLL   R3,28               ISOLATE HEX DIGIT,
         SLDL  R2,4                AND ACCUMULATE.
         LA    R1,1(R1)            STEP CHAR POINTER.
         BCT   R0,BCDHEXCK         PROCESS IF MORE.
         SR    R15,R15             CLEAR RETURN REG.
         LR    R1,R2               LOAD RESULT REG.
BCDHEXBR LM    R2,R12,28(R13)      RESTORE REGISTERS.
         BR    R14                 RETURN.
