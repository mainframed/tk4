         MACRO
&NME     $PROT &D,&MASK=FF,&SYS=MVS,&KEYAD=,&SYSAD=,&WORKREG=,&MF=
.*
.*
.*
.* LAST CHANGE DATE - JANUARY 11, 1977
.*                  - THE MACRO CAN NOW FUNCTION ON EITHER MVT SYSTEMS
.*                    OR MVS SYSTEMS OR BOTH SYSTEMS.
.*                  - MAILING ADDRESS CHANGE.
.*
.* THIS MACRO WAS WRITTEN BY DAVID B. COLE. ANY QUESTIONS CONCERNING
.* IT MAY BE ADDRESSED TO:
.*       809 WHITNEY AVE.
.*       NEW HAVEN, CT. 06511
.*
.*
         GBLC  &$PROKEY,&$PROSYS
         GBLC  &$TESRET(20)
         LCLC  &#,&DONE,&N,&R,&R#
         AIF   ('&KEYAD' EQ '').KEYOK
&$PROKEY SETC  ''
         AIF   ('&KEYAD' EQ '*').KEYOK
&$PROKEY SETC  '&KEYAD'
.KEYOK   AIF   ('&SYSAD' EQ '').SYSOK
&$PROSYS SETC  ''
         AIF   ('&SYSAD' EQ '*').SYSOK
&$PROSYS SETC  '&SYSAD'
.SYSOK   AIF   ('&MF' EQ 'INIT').MEND
&N       SETC  '&NME'
&#       SETC  '&SYSNDX'
&DONE    SETC  '*+4'
&R       SETC  '&WORKREG(1)'
         AIF   ('&WORKREG(1)' NE '').ROK
         $TEST PFIX=
&R       SETC  '&$TESRET(1)1'
.ROK     AIF   ('&SYS' EQ 'MVT').SYSMVT
         AIF   ('&SYS' EQ 'MVS').SYSMVS
&DONE    SETC  'P&#.END'
         AIF   ('&$PROSYS' NE '').LCLSYS1
&N       L     &R,CVTPTR           --> CVT
         TM    CVTDCB-CVTMAP(&R),CVT6DAT VIRTUAL SYSTEM?
         AGO   .LCLSYS2
.LCLSYS1 ANOP
&N       TM    &$PROSYS,CVT6DAT    VIRTUAL SYSTEM?
.LCLSYS2 ANOP
&N       SETC  ''
         AIF   ('&$PROKEY' NE '').TCBKEY1
         AIF   ('&$PROSYS' EQ '').TCBKEY2
         L     &R,CVTPTR           (--> CVT)
.TCBKEY2 L     &R,CVTTCBP-CVTMAP(,&R) (--> TCB NEW/OLD PTRS)
         L     &R,4(,&R)           (--> OUR TCB)
.TCBKEY1 BNZ   P&#.MVS             YES, GO ISSUE MODESETS
.SYSMVT  AIF   ('&$PROKEY' NE '').MVTKEY1
         AIF   ('&SYS' NE 'MVT').MVTKEY2
&N       L     &R,CVTPTR           --> CVT
         L     &R,CVTTCBP-CVTMAP(,&R) --> TCB NEW/OLD PTRS
         L     &R,4(,&R)           --> OUR TCB
.MVTKEY2 MVZ   P&#.PSW+1(1),TCBPKF-TCB(&R) COPY P-KEY TO PSW
         AGO   .MVTKEY3
.MVTKEY1 ANOP
&N       MVZ   P&#.PSW+1(1),&$PROKEY COPY P-KEY TO PSW
.MVTKEY3 LPSW  P&#.PSW             LOAD PROTECTED PSW
         DS    0D                  DWORD ALIGNMENT
P&#.PSW  DC    X'&MASK.040000',A(&DONE) PSW SKELETON
         AIF   ('&SYS' EQ 'MVT').MEND
&N       SETC  'P&#.MVS'
.SYSMVS  $TEST REGS=&R
&R#      SETC  '&$TESRET(1)'
&N       SETC  '&N        '(1,8)
         AIF   ('&$PROKEY' NE '').MVSKEY1
         AIF   ('&SYS' NE 'MVS').MVSKEY2
&N       L     &R,CVTPTR           --> CVT
&N       SETC  '        '
         L     &R,CVTTCBP-CVTMAP(,&R) --> TCB NEW/OLD PTRS
         L     &R,4(,&R)           --> OUR TCB
.MVSKEY2 PUSH  USING               SAVE USING ENVIRONMENT
         DROP  ,                   RELEASE ALL PREVIOUS USINGS
         USING TCB,&R              DECLARE TCB BASE
         MNOTE '&N MODESET EXTKEY=TCB,WORKREG=&R# RESTORE P/P-KEY'
&N       MODESET EXTKEY=TCB,WORKREG=&R#
         POP   USING               RESTORE USING ENVIRONMENT
         AGO   .MVSKEY3
.MVSKEY1 MNOTE '&N MODESET KEYADDR=&$PROKEY,WORKREG=&R# RESTORE P/P-KEY*
               '
&N       MODESET KEYADDR=&$PROKEY,WORKREG=&R#
.MVSKEY3 AIF   ('&SYS' EQ 'MVS').MEND
&DONE    DS    0H
.MEND    MEND
