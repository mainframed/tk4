//PLI          EXEC PGM=IEL0AA,                                        C
//    PARM='NIS,OPT(2),LC(60),NAG,SIZE(MAX)',                          C
//             ACCT=(CHG-TIS8SI00,TYPE-TEST)
//STEPLIB      DD  DISP=SHR,DSNAME=SYS1.PL1LINK
//SYSPRINT     DD  SYSOUT=*
//SYSLIN       DD  DSNAME=&&LOADSET,DISP=(MOD,PASS), C
//             SPACE=(400,(100,200),RLSE), C
//             UNIT=SYSSQ,DCB=BLKSIZE=400
//SYSUT1       DD  UNIT=SYSDA,SPACE=(CYL,(9,1),,CONTIG),DCB=LRECL=4051
//SYSIN        DD  *
*PROCESS NOMAP,NOOFFSET,NUMBER,NOMACRO,N('IMSDBUT'),NX,NA,GONUMBER;
       /*                  I M S D B U T

            AN IMS DATABASE UTILITY FOR USE BY PL/I PROGRAMMERS


                            P R E F A C E

           THIS PROGRAM WAS WRITTEN DURING THE DEVELOPMENT OF A
           LARGE BATCH-MODE IMS SYSTEM TO PERMIT MAINTENANCE OF
           DATABASES PRIOR TO THE EXISTENCE OF UPDATE PROGRAMS.
           IT IS ALSO USED TO TEST CALLS TO THE DATABASE.

           ITS USE PRESUPPOSES A KNOWLEDGE OF PL/I DATA-DIRECTED
           INPUT, AND OF THE CODING OF IMS SEGMENT SEARCH ARGUMENTS
           WHICH, IN TURN, REQUIRES AN AWARENESS OF THE CAPABILITIES
           AND LIMITATIONS OF THE DBD'S AND THE PSB USED.

           THIS PROGRAM HAS BEEN RENDERED INDEPENDENT OF ANY PARTICULAR
           PSBGEN BY USE OF THE ACCOMPANYING ASSEMBLER ROUTINE,
           ARG@ WHICH IDENTIFIES TO THIS MODULE THE ADDRESS OF THE
           PROGRAM SPECIFICATION BLOCK PROVIDED BY DFSRRC00.

           THIS PROGRAMMING HAS BEEN COMPILED ON THE OPTIMIZING
           COMPILER VERSION 1 RELEASE 3.1 PTF 70, LINK EDITED
           USING DFSILNK0 WITH ENTRY PLICALLA, AND RUN UNDER
           DFSRRC00, IMS RELEASE 1.1.5 ON S/370 OS/MVS.

           INQUIRIES MAY BE DIRECTED TO:
                  G.KUNZ
                  THE B.F.GOODRICH CO.
                  500 SOUTH MAIN STREET
                  AKRON, OHIO 44318
                  PHONE:  (216) 374 3978

      */
 %PAGE;
     /*

                   F U N C T I O N   A N D   U S E


        THIS PROGRAM PERMITS ACCESS TO A DATABASE USING SIMPLY CODED
        INPUTS.  ANY CALL IS PERMITTED USING FROM 0 TO 5 SSAS
        HAVING NO MORE THAN 200 CHARACTERS EACH.  THE UPPER LIMIT
        ON THE LENGTH OF THE IO_AREA IS 2044 BYTES.

        INPUTS ARE READ FROM DDNAME=SYSIN IN A FREE 'DATA DIRECTED'
        FORMAT ((VARIABLE NAME)=(VALUE)) WHERE THE PARENTHESES ARE
        FOR READABILITY AND ARE NOT PART OF THE INPUT.  CHARACTER
        STRING VALUES MUST BE ENCLOSED IN TICS AND BLANKS ARE NOT
        PERMITTED ON EITHER SIDE OF THE EQUAL SIGN.  SUCCESSIVE INPUTS
        FOR ONE EXECUTION MUST BE SEPARATED BY COMMAS AND/OR BLANKS
        AND MUST END WITH A SEMI-COLON.  INPUTS MAY BE CONTINUED FROM
        ONE CARD TO THE NEXT TAKING DUE CARE NOT TO INTRODUCE EXTRAN-
        EOUS BLANKS.

        ANY CALL IS PERMITTED AND THE CONTENTS OF THE PCB, THE INPUT
        VALUES AND THE IO_AREA (LESS TRAILING BLANKS) ARE PRINTED
        AFTER EACH CALL.  THE IO_AREA IS PRINTED BOTH AS CHARACTER
        AND AS HEX.  SYSPRINT IS USED AS THE OUTPUT FILE.
        REPETITIVE CALLS ARE SUPPORTED SO THAT 'GET NEXT' TYPE
        CALLS THROUGH THE DATABASE CAN BE DONE EASILY.

        BECAUSE THIS PROGRAM CANNOT KNOW THE LENGTH OF THE SEGMENT
        RETRIEVED, THE LENGTH OF THE IO_AREA DATA PRINTED WILL
        REMAIN AS LONG AS THE LONGEST SEGMENT RETRIEVED UNTIL THE
        DATABASE REFERENCE IS CHANGED.  IT IS ANTICIPATED THAT THE
        USER IS SUFFICIENTLY FAMILIAR WITH HIS DATABASE SEGMENTS
        TO BE ABLE TO DISREGARD ANY RESIDUAL GARBAGE AT THE END OF
        THE PRINTOUT. IF THIS PROVES TO BE TROUBLESOME, THE
        IO_AREA CAN BE REINITIALIZED TO A NULL STRING.

        THE POSSIBLE INPUTS TO THE PROGRAM AND THEIR DEFAULTS
        ARE AS FOLLOWS: (ALTERNATES SHOWN BELOW ARE TO ACCOMMODATE
        FREQUENT SPELLING ERRORS)

             #_OF_CALLS      THE NUMBER OF TIMES EACH CALL IS TO BE MADE
                             DEFAULT IS 1.  PROCESSING OF A SET OF INPUT
                             DATA WILL END WHEN THE FIRST BYTE OF THE
                             STATUS CODE IS NOT BLANK OR 'G' OR THE
                             STATUS CODE IS 'GE' OR 'GB'.

             FUNC,FUNCTION   THE FUNCTION CALL. DEFAULT IS 'GN  '
                             A VALUE OF '    ' WILL NOT EXECUTE A CALL,
                             AND WILL THEREBY LEAVE THE IO AREA
                             UNCHANGED. THIS IS USEFUL WHEN NEEDED
                             REVISIONS TO THE IO_AREA REQUIRE MORE THAN
                             ONE SET OF INPUTS IN PREPARATION FOR
                             'ISRT' OR 'REPL' CALLS.

             D_B,DB          THE NAME OF THE DATABASE TO BE USED
                             IN SELECTING THE PCB AS IT APPEARS IN
                             THE PCB (8 CHARACTERS). ONLY THE FIRST PCB
                             IN THE PSB WHICH USES A PARTICULAR
                             DATABASE NAME CAN BE ACCESSED.

             SSA1,SSA2,      THESE ARE THE SEGMENT SEARCH ARGUMENTS.
             SSA3,SSA4,SSA5  THE DEFAULT IS A NULL STRING.  MAXIMUM
                             LENGTH IS 200 CHARACTERS.

             IO_AREA         THE I/O AREA.  IT IS A CHARACTER STRING
                             2044 BYTES LONG WHICH IS THE MAXIMUM
                             LENGTH OF DATA TO BE RETRIEVED OR
                             INSERTED WITHOUT PROGRAM MODIFICATION.

             CHARLOC         INTEGER LOCATION IN IO_AREA WHERE CHNG_CHAR
                             IS TO BE INSERTED.  THE INITIAL VALUE IS
                             ZERO.  INSERTION WILL OCCUR WHENEVER
                             ITS VALUE IS GREATER THAN ZERO.

             CHNG_CHAR       A VARYING CHARACTER STRING WHICH CAN BE
                             USED TO ALTER THE CONTENTS OF THE IO_AREA
                             STARTING IN POSITION CHARLOC.

             INTLOC          INTEGER LOCATION IN IO_AREA WHERE
                             A TWO-BYTE INTEGER 'INTEGER'' IS TO BE
                             INSERTED. INSERTION WILL OCCUR WHENEVER
                             ITS VALUE IS GREATER THAN ZERO.

             INTEGER         THE VALUE TO BE PLACED IN A TWO-BYTE
                             INTEGER FIELD IN THE IO_AREA.

             INTLOC4         INTEGER LOCATION IN IO_AREA WHERE
                             THE FOUR-BYTE INTEGER 'INTEGER4' IS TO BE
                             INSERTED. INSERTION WILL OCCUR WHENEVER
                             ITS VALUE IS GREATER THAN ZERO.

             INTEGER4        THE VALUE TO BE PLACED IN A FOUR-BYTE
                             INTEGER FIELD.

             FLTLOC          INTEGER LOCATION IN IO_AREA WHERE
                             THE FLOATING POINT NUMBER 'FLOAT' IS TO BE
                             INSERTED. INSERTION WILL OCCUR WHENEVER
                             ITS VALUE IS GREATER THAN ZERO.

             FLOAT           THE VALUE TO BE PLACED IN A FOUR-BYTE
                             FLOATING POINT FIELD.

             FB15_7_LOC      INTEGER LOCATION IN IO_AREA WHERE A
                             NUMBER WITH THE ATTRIBUTES  FIXED BIN(15,7)
                             IS TO BE INSERTED. INSERTION WILL OCCUR
                             WHENEVER ITS VALUE IS GREATER THAN ZERO.

             FB15_7_VALUE    THE VALUE TO BE PLACED IN A TWO-BYTE
                             FIXED BIN(15,7) FIELD.


        NOTA BENE:  INPUT IS THROUGH THE USE OF THE PL/I GET DATA
                    STATEMENT.  CONVENTIONS TO BE FOLLOWED ARE GIVEN
                    IN THE LANGUAGE REFERENCE MANUAL.  VALUES
                    UNCHANGED BY ASSIGNMENT(INPUT) REMAIN AS THEY
                    LAST WERE (EXCEPT FOR IO_AREA WHICH MAY BE
                    CHANGED BY A SUCCESSFUL RETRIEVAL CALL).  EACH
                    SET OF INPUTS MUST END WITH A SEMICOLON.


             EXAMPLE OF INPUT DATA WHICH WOULD RETRIEVE THE FIRST FOUR
             OCCURRENCES OF THE SEGMENT NAMED THISSEG1 FROM THE
             DATABASE NAMED MYDABA01:

               D_B='MYDABA01',SSA1='THISSEG1 ',#_OF_CALLS=1,FUNC='GU  ';
               #_OF_CALLS=3,FUNC='GN  ';

     */
 %PAGE;
     /*             F L O W C H A R T

                        *********
                       *  START  *
                        *********
                            *
                            *
                      *************
                      *   COUNT   *
                      *   PCB'S   *
                      *************
                            *
                            * <****************************************
                            *                                         *
                      *************                                   *
                      *   READ    *                                   *
                      *  INPUTS   *                                   *
                      *************                                   *
                            *                                         *
                            *                                         *
                            *                                         *
                         *     *                                      *
     ********  YES    *           *                                   *
    * RETURN * <** *   END OF FILE   *                                *
     ********         *     ?     *                                   *
                         *     *                                      *
                            *                                         *
                            *                                         *
                            *                                         *
                            *                                         *
                         *     *                                      *
                      *    PCB    *        NO    **************       *
                   *      VALID      ***********>*  FIND PCB  *       *
                      *     ?     *              **************       *
                         *     *                        *             *
                            *           YES          *     *     NO   *
                            *<******************* *   FOUND?  * *****>*
                            *                        *     *          *
                            *                           *             *
                         *     *                                      *
                      *   IO AREA  *    YES                           *
                   *   MODIFICATIONS  **************                  *
                      * REQUESTED? *               *                  *
                         *     *                   *                  *
                            *               *****************         *
                            *<***************   MODIFY IO   *         *
    **********************> *               *      AREA     *         *
    *                       *               *****************         *
    *              ********************                               *
    *              * *              * *                               *
    *              * *    CALL      * *                               *
    *              * *  ASMTDLI     * *                               *
    *              * *              * *                               *
    *              ********************                               *
    *                       *                                         *
    *                       *                                         *
    *                 ******************                              *
    *                *                *                               *
    *               *  PRINT OUTPUT  *                                *
    *              *                *                                 *
    *             ******************                                  *
    *                       *                                         *
    *                       *                                         *
    *                    *      *                                     *
    *                 *   MORE     *        NO                        *
    *              *    CALLS TO      *******************************>*
    *                 *    DO?     *                                  *
    *                    *      *                                     *
    *                       *                                         *
    *                       *                                         *
    *                       *                                         *
    *                    *      *                                     *
    *        YES      *   STATUS   *        NO                        *
    *<**************       CODE       *******************************>*
                      *    OK?     *
                         *      *
                            *

     */
 %PAGE;
 IMSDBUT:
 PROCEDURE OPTIONS (MAIN) REORDER;
   DECLARE ARG@ EXTERNAL ENTRY, /* THIS IS AN ASSEMBLER ROUTINE
                                   WHICH RETURNS THE CONTENTS OF
                                   REGISTER 1.                   */
           #_OF_PCBS  FIXED BIN(15) INIT(0) STATIC,
           #_OF_CALLS FIXED BIN(15) INIT(1) STATIC,
           FUNC CHAR(4) INIT('GN') STATIC,
           FUNCTION CHAR(4) DEFINED FUNC,
           D_B CHAR(8)  STATIC INIT(''),
           DB CHAR(8) DEFINED D_B,
           AREA_USED_TO_ALIGN_IO_AREA AREA(2048) INTERNAL
                           INIT(EMPTY),
           AREA_PTR PTR INIT(ADDR(AREA_USED_TO_ALIGN_IO_AREA)),
           IO_AREA CHAR(2044) BASED (AREA_PTR),
           IO_RAY (2044) CHAR(1) BASED (AREA_PTR),
           PLIDUMP BUILTIN,
           UNSPEC  BUILTIN,
           (I,I1,I2,I3,J,K,L) FIXED BIN(15) STATIC INIT(0),
           SYSIN EXTERNAL FILE STREAM INPUT,
           SYSPRINT EXTERNAL FILE PRINT,
           CHNG_CHAR CHAR(100) VARYING,
           CHARLOC FIXED BIN(15) INIT(0) STATIC,
           INTLOC  FIXED BIN(15) INIT(0) STATIC,
           INTLOC4 FIXED BIN(15) INIT(0) STATIC,
           FLTLOC  FIXED BIN(15) INIT(0) STATIC,
           INT2 FIXED BIN(15) BASED,
           FB15_7_LOC FIXED BIN(15) INIT(0) STATIC,
           INT4 FIXED BIN(31) BASED,
           INT_15_7 FIXED BIN(15,7) BASED,
           FLOT FLOAT BASED,
           INTEGER FIXED BIN(15) INIT(0) STATIC,
           INTEGER4 FIXED BIN(31) INIT(0) STATIC,
           FLOAT FIXED BIN(31,10) INIT(0) STATIC,
           FB15_7_VALUE FIXED BIN(15,7) INIT(0) STATIC,
          (SSA1,SSA2,SSA3,SSA4,SSA5) CHAR(200) VARYING INIT('') STATIC,
           ASMTDLI EXTERNAL ENTRY OPTIONS (ASSEMBLER INTER NOMAP),
           PLIXOPT CHAR(24) VARYING STATIC EXTERNAL
              INIT('ISASIZE(6K),NOREPORT/'),
          (LENGTH,EMPTY,NULL,SUBSTR) BUILTIN,
           PSB@ PTR INIT CALL ARG@(PSB@),
           PCB@ (17) PTR BASED(PSB@),
           TRUE BIT(1) STATIC INIT('1'B),
           BIT_CHECK (255,32) BIT(1) BASED (PSB@),
           CURRENT_PCB PTR INIT(NULL);
 %PAGE;
   DECLARE 1 PCB ALIGNED BASED(CURRENT_PCB),
             3 DBD_NAME CHAR(8),
             3 SEG_LEVEL CHAR(2),
             3 STATUS_CODE CHAR(2),
             3 PROC_OPTIONS CHAR(4),
             3 RESERVED_DLI FIXED BINARY(31),
             3 SEG_NAME CHAR(8),
             3 LENGTH_FB_KEY FIXED BINARY(31),
             3 #_SENSE_SEGS FIXED BINARY(31),
             3 KEY_FB_AREA CHAR(DUMMY REFER(PCB.LENGTH_FB_KEY));

  DECLARE
          WORK_PAIRS (1)      BIT (16)   BASED (AREA_PTR),
          HEX_DATA            CHAR(4088) STATIC INIT((4088)' '),
          OUTPUT_PTR          PTR INIT(ADDR(HEX_DATA)),
          HEX_BYTE (1)        CHAR(1) BASED(OUTPUT_PTR),
          ADDR                BUILTIN,
          MOD                 BUILTIN,
          HEX_DIGIT (0:15)    CHARACTER(1) STATIC INIT('0','1','2','3',
                                                       '4','5','6','7',
                                                       '8','9','A','B',
                                                       'C','D','E','F');

   ON ERROR SNAP
     BEGIN;
       DECLARE TEMPFYL FILE PRINT;
       ON UNDEFINEDFILE(TEMPFYL) GO TO EOJ;
       BIT_CHECK(#_OF_PCBS,1) = '1'B;
       OPEN FILE(TEMPFYL) TITLE('PLIDUMP');
       CALL PLIDUMP('TFBHC');
       CLOSE FILE(TEMPFYL) ;
       GO TO EOJ;
     END;
   ON NAME(SYSIN)
     BEGIN;
       DECLARE DATAFIELD BUILTIN;
       FUNC = '';
       PUT SKIP LIST('*****WARNING***** AN INVALID CONDITION HAS BEEN '
           ×× 'IDENTIFIED IN THE INPUT.  DATAFIELD = ',DATAFIELD);
       PUT SKIP LIST('                  INVALID INPUT WILL BE '
           ×× 'DISREGARDED, FUNCTION SET TO BLANKS, AND PROCESSING '
           ×× 'WILL CONTINUE.');
   END;
   ON ENDFILE (SYSIN) GO TO EOJ;
 %PAGE;
       /*  COUNT THE NO. OF PCB'S AVAILABLE TO A GIVEN EXECUTION
           AND TURN OFF THE FIRST BIT OF THE LAST POINTER PASSED.  */

   DO #_OF_PCBS = 1 BY 1 WHILE ( ^ BIT_CHECK(#_OF_PCBS,1));
   END;
   BIT_CHECK(#_OF_PCBS,1) = '0'B;




   DO WHILE (TRUE); /*  UNCONDITIONAL LOOP WHICH TERMINATES WITH
                        AN ENDFILE CONDITION FOR FILE SYSIN      */

            /*  READ A SET OF INPUT DATA  */

     GET DATA(#_OF_CALLS,FUNC,FUNCTION,D_B,DB,SSA1,SSA2,SSA3,SSA4,SSA5,
              IO_AREA,CHNG_CHAR,INTLOC,INTEGER,CHARLOC,INTEGER4,INTLOC4,
              FLTLOC,FLOAT,FB15_7_LOC,FB15_7_VALUE);

            /*  TEST PCB FOR CONSISTENCY WITH INPUT DATA  */

     IF CURRENT_PCB = NULL × DBD_NAME ^= D_B
       THEN /* THIS IS EITHER THE FIRST TIME THROUGH THE LOOP OR
            WE HAVE ASKED TO USE A DIFFERENT PCB FROM THE LAST TIME.*/

         DO; /*  FIND THE REQUIRED PCB IN THE PSB  */
           CURRENT_PCB = NULL;
           DO J = 1 TO #_OF_PCBS WHILE(CURRENT_PCB = NULL);
             IF PCB@(J)->DBD_NAME = D_B
               THEN    /*  THIS IS THE PCB WE ASKED FOR  */
                    CURRENT_PCB = PCB@(J);
           END;
           IF CURRENT_PCB = NULL
             THEN
               DO;
                 PUT SKIP EDIT('PCB NOT FOUND FOR DATABASE=',D_B)(A,A);
                 PUT DATA(FUNC,IO_AREA,SSA1,SSA2,SSA3,SSA4,SSA5);
                 GO TO END_LOOP;
               END;

                   /*  INITIALIZE IO_AREA  */
           IO_AREA = '';
         END;
 %PAGE;
          /*  DO REQUESTED MODIFICATIONS TO THE IO_AREA  */

     IF CHARLOC > 0 /*  WE ARE ASKING TO INSERT A CHARACTER STRING
                        INTO THE IO_AREA PRIOR TO AN IMS CALL.  */
       THEN SUBSTR(IO_AREA,CHARLOC,LENGTH(CHNG_CHAR)) = CHNG_CHAR;
     IF INTLOC > 0 /*  WE ARE ASKING TO INSERT A FIXED BIN(15) INTEGER
                       INTO THE IO_AREA PRIOR TO AN IMS CALL.  */
       THEN ADDR(IO_RAY(INTLOC)) -> INT2 = INTEGER;
     IF INTLOC4 > 0 /*  WE ARE ASKING TO INSERT A FIXED BIN(31) INTEGER
                        INTO THE IO_AREA PRIOR TO AN IMS CALL.  */
       THEN ADDR(IO_RAY(INTLOC4)) -> INT4 = INTEGER4;
     IF FLTLOC > 0 /*  WE ARE ASKING TO INSERT A FLOAT DECIMAL(6) VALUE
                       INTO THE IO_AREA PRIOR TO AN IMS CALL.   */
       THEN ADDR(IO_RAY(FLTLOC)) -> FLOT = FLOAT;
     IF FB15_7_LOC > 0 /*  WE ARE ASKING TO INSERT A FIXED BIN(15,7)
                           VALUE INTO THE IO_AREA PRIOR TO AN IMS CALL*/
       THEN ADDR(IO_RAY(FB15_7_LOC)) -> INT_15_7 = FB15_7_VALUE;

        /*  REPEAT SUBROUTINE CALL FOR THE REQUIRED NUMBER OF
            TIMES SO LONG AS THE STATUS CODE INDICATES A VALID
            CONDITION.   */

     DO J = 1,2 TO #_OF_CALLS WHILE
        (STATUS_CODE='  '× (STATUS_CODE^='GE' & SUBSTR(STATUS_CODE,1,1)
                   = 'G'  & STATUS_CODE ^= 'GB'));
       CALL FIX ((SSA1),(SSA2),(SSA3),(SSA4),(SSA5));
     END;
 END_LOOP:
   END;
 %PAGE;
 FIX:
   PROCEDURE (SSA1,SSA2,SSA3,SSA4,SSA5);
     DECLARE
             (SSA1,SSA2,SSA3,SSA4,SSA5) CHAR(*),
             (THREE INIT(3),FOUR INIT(4),FIVE INIT(5),SIX INIT(6),
              SEVEN INIT(7),EIGHT INIT(8)) FIXED BIN(31),
              J STATIC INIT(0),
              FULL_WORD_BIN FIXED BIN(31) STATIC INIT(0),
              LOW_BITS BIT(16) STATIC INIT((16)'0'B),
             1 HEX_STRUCT (2044) BASED (OUTPUT_PTR),
               2 TOP CHAR(1),
               2 BOT CHAR(1);
 %SKIP;
     IF FUNC ^= ''
       THEN /*  DO THE REQUIRED IMS CALL  */
         SELECT;
           WHEN (LENGTH(SSA5) > 0)
              CALL ASMTDLI
                     (EIGHT,FUNC,PCB,IO_AREA,SSA1,SSA2,SSA3,SSA4,SSA5);
           WHEN (LENGTH(SSA4) > 0)
              CALL ASMTDLI(SEVEN,FUNC,PCB,IO_AREA,SSA1,SSA2,SSA3,SSA4);
           WHEN (LENGTH(SSA3) > 0)
              CALL ASMTDLI(SIX,FUNC,PCB,IO_AREA,SSA1,SSA2,SSA3);
           WHEN (LENGTH(SSA2) > 0)
              CALL ASMTDLI(FIVE,FUNC,PCB,IO_AREA,SSA1,SSA2);
           WHEN (LENGTH(SSA1) > 0)
              CALL ASMTDLI(FOUR,FUNC,PCB,IO_AREA,SSA1);
           OTHERWISE
              CALL ASMTDLI(THREE,FUNC,PCB,IO_AREA);
         END;

     DO K = LENGTH(IO_AREA) TO 1 BY -1 WHILE (IO_RAY(K) = ' ');
     END; /*  THIS LOOP FINDS THE END OF SIGNIFICANT DATA  */
 %PAGE;
             /*  PRINT THE OUTPUT DATA  */

                 /*  PRINT THE PCB  */

     CALL PRINT_PCB(PCB);

                 /*  PRINT THE INPUT DATA  */

     PUT SKIP DATA(FUNC,D_B,SSA1,SSA2,SSA3,SSA4,SSA5);
     PUT SKIP DATA(CHNG_CHAR,CHARLOC,INTLOC,INTEGER,INTEGER4,INTLOC4,
               FLTLOC,FLOAT,#_OF_CALLS);

                 /*  FORMAT THE IO_AREA IN HEX  */

     DO J = 1 TO (2 * K) BY 4;
       UNSPEC(FULL_WORD_BIN) = LOW_BITS ×× WORK_PAIRS((J+3)/4);
       DO L = 3 TO 0 BY -1;            /*  MASK OFF 4 LOW-ORDER BITS  */
         HEX_BYTE(J + L) =             /*  (ONE HEX DIGIT).  PLACE    */
           HEX_DIGIT(MOD(FULL_WORD_BIN,/*  EBCDIC EQUIVALENT IN COR-  */
                  16));                /*  RESPONDING POSITION IN     */
                                       /*  RETURNED STRING.           */
         FULL_WORD_BIN = FULL_WORD_BIN / 16; /* ZERO HALF-BYTE JUST
                                                TRANSLATED.           */
       END;
     END;

            /*  PRINT THE IO_AREA IN BOTH EBCDIC AND HEX
                UNTIL ONLY BLANKS REMAIN AT THE END.      */

     PUT SKIP(2) EDIT('IO_AREA:')(A);
     DO I = 1 TO K/120+1;
       I2 = 120 * I;
       I1 = I2 - 119;
       IF I2 > K
         THEN
           DO;
             I2 = K;
             I1 = (I - 1) * 120 + 1;
           END;
       PUT SKIP EDIT(SUBSTR(IO_AREA,I1,I2 - I1 + 1))(A);
       PUT SKIP EDIT((TOP(I3) DO I3 = I1 TO I2))(A);
       PUT SKIP EDIT((BOT(I3) DO I3 = I1 TO I2))(A);
     END;
     PUT SKIP(3);
 %PAGE;
 PRINT_PCB:
   PROCEDURE(PCB);
     DECLARE 1 PCB ALIGNED,
               3 DBD_NAME CHAR(8),
               3 SEG_LEVEL CHAR(2),
               3 STATUS_CODE CHAR(2),
               3 PROC_OPTIONS CHAR(4),
               3 RESERVED_DLI FIXED BINARY(31),
               3 SEG_NAME CHAR(8),
               3 LENGTH_FB_KEY FIXED BINARY(31),
               3 #_SENSE_SEGS FIXED BINARY(31),
               3 KEY_FB_AREA CHAR(*);
     PUT SKIP DATA(PCB);
   END;
   END;
 EOJ:
          /*  RESTORE THE FIRST BIT OF THE POINTER TO THE LAST PCB  */
   BIT_CHECK(#_OF_PCBS,1) = '1'B;
 END IMSDBUT;
//ASM      EXEC  PGM=IFOX00,PARM='LOAD,NODECK,NOESD'
//SYSUT1   DD    DSN=&&SYSUT1,UNIT=VIO,SPACE=(1700,(600,100))
//SYSUT2   DD    DSN=&&SYSUT2,UNIT=VIO,SPACE=(1700,(300,50))
//SYSUT3   DD    DSN=&&SYSUT3,UNIT=VIO,SPACE=(1700,(300,50))
//SYSPRINT DD    SYSOUT=*,DCB=BLKSIZE=1089
//SYSPUNCH DD    DUMMY
//SYSGO    DD    DSN=&&ASMSET,SPACE=(400,(100,200),RLSE),
//         DISP=(MOD,PASS),UNIT=SYSSQ,DCB=BLKSIZE=400
//SYSIN    DD    *
ARG@     CSECT
         SPACE
*
*        DECLARE ARG@ EXTERNAL ENTRY (POINTER) OPTIONS(ASSEMBLER);
*        DECLARE P POINTER INITIAL CALL ARG@(P);
*
         SPACE
         STM   14,12,12(13)
         L     2,0(1)
         L     12,4(13)
         L     1,24(12)
         ST    1,0(2)
         LM    14,12,12(13)
         BR    14
         SPACE
*        THIS SUBROUTINE ENABLES PL/I PROGRAMMERS TO WRITE PROCEDURES
*        WITH A VARYING NUMBER OF ARGUMENTS.
*        FOR EXAMPLE, A PL/I PROGRAM RUNNING UNDER IMS IS
*        ESSENTIALLY A SUBROUTINE OF IMS WITH ONE ARGUMENT FOR
*        EACH PCB IN THE PSB.  IF THE PROGRAMMER WANTS TO TAKE
*        THE TIME TO MAKE HIS PROGRAM INDEPENDENT OF THE PSBGEN, HE
*        MAY USE THIS SUBROUTINE TO SET A POINTER EQUAL TO
*        REGISTER 1, WHICH POINTS TO THE ARGUMENT LIST.
*
         SPACE
*             /-----------^               /----------^
*   REG13 --> I           I      I------> I          I
*           +4I BACKCHAIN I -----I        I          I
*             I           I               I          I
*             I           I               I          I
*             I           I               I          I
*             I           I               I          I
*                                      +24I OLD REG1 I ------I
*             /-----------^               I          I       I
*   REG1 ---> I  A(PTR)   I -----I                           I
*             ------------/      I                           I
*                                I                           V
*                                I                      ARGUMENT
*                                I                        LIST
*                                V                      FOR CALLER
*
*                            PL/I POINTER VARIABLE
*
         SPACE
         END
//LKED         EXEC PGM=DFSILNK0,                                      C
//             COND=(11,LT,PLI),                                       C
//             PARM='LIST,LET,SIZE=(128K,18K),MAP',                    C
//             ACCT=(CHG-TIS8SI00,TYPE-TEST)
//STEPLIB      DD  DSNAME=IMSVS.RESLIB,DISP=SHR
//SYSLIB       DD  DISP=SHR,DSNAME=SYS1.PL1BASE
//             DD  DSNAME=IMSVS.RESLIB,DISP=SHR
//SYSLMOD      DD  DSNAME=&&SHARE(IMSDBUT),DISP=(NEW,PASS),UNIT=DISK,  C
//             SPACE=(CYL,(1,1,1),RLSE)
//SYSUT1       DD  UNIT=SYSSQ,SPACE=(CYL,(9,1))
//SYSPRINT     DD  DUMMY
//SYSLIN       DD  DDNAME=SYSIN
//             DD  DSNAME=&&ASMSET,DISP=(OLD,DELETE)
//             DD  DSNAME=&&LOADSET,DISP=(OLD,DELETE)
//SYSIN        DD *
     ENTRY PLICALLA
//* STEP1 EXEC PGM=DFSRRC00,ETC
