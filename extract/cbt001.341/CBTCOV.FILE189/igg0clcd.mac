IGG0CLCD CSECT
*ZAP CARD ++USERMOD(HG00011)
*ZAP CARD  /* INSERT PROJECT IN OS CVOL ENTRY FOR TAPE */ .
*ZAP CARD ++VER(Z038) FMID(EDM1102) PRE(UZ22031).
*ZAP CARD ++ZAP(IGG0CLCD) .
*ZAP CARD  EXPAND IGG0CLCD(160)
*ZAP CARD NAME IGG0CLCD
* CATLG TAPES WITH PROJECT AS DUMMY VOLUME
* THIS MOD IS NECESSITATED BY THE PECULIAR THING ONCE DONE TO THE
* CATALOG TO INCLUDE THE PROJECT AS A SECOND PSEUDO-VOLUME FOR
* ACCOUNTING PURPOSES. THE IMPORTANT ADDRESSES ARE:
ADDR0    EQU   IGG0CLCD+X'1DE'
ADDR1    EQU   IGG0CLCD+X'620'  THE OVERLAYED INSTRUCTION
ADDR2    EQU   IGG0CLCD+X'D28'  THE PATCH AREA
MOVE1    EQU   IGG0CLCD+X'C98'
MOVEVOLS EQU   MOVE1-12
*ZAP START
* ZAP TO IGG0CLCD TO REMOVE SPECIAL YCC CATALOG ENTRY FOR PROJECT
* OF TAPE DATASET. THIS ENTRY, WHICH LOOKS LIKE ANOTHER VOLUME, IS
* ADDED TO THE VOLUME LIST OF THE CATLG REQUEST
* THE OS CATALOG.
* OS VS2 RELEASE 3.0   MODIFICATION CURRENT TO 10/22/74 FICHE
* REGISTERS 1 AND 2 ARE USED AFTER EXECUTING THE
*  ZAPOVER INSTRUCTION BECAUSE THEY ARE NOT NEEDED
*  LOGICALLY AFTER THAT POINT
         USING IGG0CLCD+2,4
         USING WKAREA,6
*ZAP PAUSE
         B     BRANCH
         ORG   ADDR0
*ZAP START VER
         EX    1,MOVEVOLS
RET0     EQU   *
         ORG   ADDR1
         EX    2,MOVE1
         ORG   ADDR2
         DC    C'ZZZZZZZZ'
*ZAP START REP
         ORG   ADDR0
         B     PATCH2
         ORG   ADDR1
BRANCH   B     PATCH
RTN      EQU   *
         ORG   ADDR2
PATCH    EQU   *
         EX    2,MOVE1
         TM    DEVT+2,X'80'   IS THIS A TAPE
         BZ    RTN            NO
         CH    2,TOP          MORE THAN 4 VOLS
         BH    RTN            PUNT
         LA    2,VOLCNT+1(2)  ADDR PAST LAST VOL ENTRY
         GETPROJ
         CLC   0(6,1),YCCDM1
         BE    RTN
         MVC   0(4,2),DUMMY  MOVE DUMMY DEVTYPE
         MVC   4(6,2),0(1)   MOVE PROJECT
         XC    10(2,2),10(2) CLEAR SEQ NUM
         LH    2,VOLCNT        GET VOL CNT
         LA    2,1(2)           AND BUMP IT
         STH   2,VOLCNT         AND STORE
         IC    2,TYPE         NOW THE LENGTH IS KEPT
         LA    2,6(2)         IN TWO PLACES
         STC   2,TYPE         HERE IT IS IN HALFWORD UNITS
         B     RTN
PATCH2   EQU   *
         EX    1,MOVEVOLS
         LR    2,1
         TM    DEVT2+2,X'80'   IS THIS A TAPE
         BZ    RET0            NO
         CH    2,TOP          MORE THAN 4 VOLS
         BH    RET0            PUNT
         LA    2,VOLCNT2+1(2)  ADDR PAST LAST VOL ENTRY
         GETPROJ
         CLC   0(6,1),YCCDM1
         BE    RETX
         MVC   0(4,2),DUMMY  MOVE DUMMY DEVT2YPE
         MVC   4(6,2),0(1)   MOVE PROJECT
         XC    10(2,2),10(2) CLEAR SEQ NUM
         LH    2,VOLCNT2        GET VOL CNT
         LA    2,1(2)           AND BUMP IT
         STH   2,VOLCNT2         AND STORE
         IC    2,TYPE         NOW THE LENGTH IS KEPT
         LA    2,6(2)         IN TWO PLACES
         STC   2,TYPE         HERE IT IS IN HALFWORD UNITS
         LA    1,0(2,2)
         BCTR  1,0
         B     RET0
RETX     SR    1,1
         IC    1,TYPE
         LA    1,0(1,1)
         BCTR  1,0
         B     RET0
DUMMY    DC    X'FFFFFFFF'
YCCDM1   DC    C'YCCDM1'
TOP      DC    AL2(4*12+1)    BYTES FOR 4 VOLS-1
*ZAP CARD  IDRDATA HG00011
*ZAP STOP
WKAREA   DSECT
         DS    CL15
TYPE     DS    CL1
VOLCNT   DC    H'1'
DEVT     DC    XL4'00008002',C'ATAPEV',HL2'0'
         DC    CL12' '
         DS    18XL12
         ORG   WKAREA+X'31C'
VOLCNT2  DS    H
DEVT2    DS    XL4
         END
