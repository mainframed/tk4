         PRINT GEN,DATA
         $REGS
READPSWD CSECT
         USING READPSWD+2,R3           ORIGINAL ADDRESSIBILITY
LOC1     EQU   READPSWD+X'1D0'
LOC2     EQU   READPSWD+X'1E8'
PATCH    EQU   READPSWD+X'328'
*ZAP START
*ZAP CARD ++USERMOD(AK00001)
*ZAP CARD /*  BLINDING OF RESPONSE TO DATASET PASSWORD PROMPT  */ .
*ZAP CARD ++VER(Z038) FMID(EDM1102).
*ZAP CARD ++ZAP(READPSWD).
*ZAP CARD  EXPAND READPSWD(104)
*ZAP CARD NAME READPSWD
*ZAP START VER
         ORG   LOC1                    1ST PATCH LOCATION
         LA    R0,8                    (ORIGINAL INSTRUCTION)
         ORG   LOC2                    2ND PATCH LOCATION
         MVI   X'094'(R13),X'14'       (ORIGINAL INSTRUCTION)
         ORG   PATCH                   NEW CODE
         DC    XL104'00'               SHOULD BE ZEROES
*ZAP START REP
         ORG   LOC1                    1ST PATCH LOCATION
PATCH1   B     PRETPUT                 TPUT 1ST MASK
         ORG   LOC2                    2ND PATCH LOCATION
PATCH2   B     POSTTPUT                TPUT 2ND MASK
         ORG   PATCH                   NEW CODE BEGINS HERE
PRETPUT  LA    R0,PREMSKL              R0=TPUT LENGTH
         LA    R1,PREMSK               R1->TPUT BUFFER
         O     R1,CNTLMSK              SET TPUT CONTROL
         SVC   93                      TPUT
         LA    R0,8                    (ORIGINAL INSTRUCTION)
         B     PATCH1+4                RETURN TO MAINLINE
POSTTPUT LA    R0,POSTMSKL             R0=TPUT LENGTH
         LA    R1,POSTMSK              R1->TPUT BUFFER
         O     R1,CNTLMSK              SET TPUT CONTROL
         SVC   93                      TPUT
         MVI   X'094'(R13),X'14'       (ORIGINAL INSTRUCTION)
         B     PATCH2+4                RETURN TO MAINLINE
PREMSK   DC    X'15'                   <NL>
         DC    X'0D'                   <CR>
         DC    C'NNNNNNNNN'            N'S
         DC    X'0D'                   <CR>
         DC    C'IIIIIIIII'            I'S
         DC    X'0D'                   <CR>
         DC    C'HHHHHHHHH'            H'S
         DC    X'0D'                   <CR>
         DC    C'ZZZZZZZZZ'            Z'S
         DC    X'0D'                   <CR>
         DC    X'24'                   <BYP>
PREMSKL  EQU   *-PREMSK
POSTMSK  DC    X'16'                   <BS>
         DC    X'0D'                   <CR>
         DC    CL9' '                  BLANKS
         DC    X'0D'                   <CR>
POSTMSKL EQU   *-POSTMSK
         DS    0F                      FULLWORD BOUNDARY
CNTLMSK  DC    X'02000000'             TPUT CONTROL MASK
*ZAP CARD  IDRDATA AK00001
*ZAP STOP
         END   READPSWD
