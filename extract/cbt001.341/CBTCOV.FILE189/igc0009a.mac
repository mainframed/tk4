         PRINT DATA
         MACRO
         FMT   &POS,&LEN
         CVD   R0,WORKAREA
         UNPK  MSGAREA+&POS.(&LEN),WORKAREA(8)
         OI    MSGAREA+&POS+&LEN-1,X'F0'
         MEND
IGC0009A CSECT
ZAP      EQU   IGC0009A+X'244'
PATCHA   EQU   IGC0009A+X'935'
*ZAP CARD ++USERMOD(HG00086).
*ZAP CARD ++VER(Z038) FMID(EDS1102) PRE(UZ90079).
*ZAP CARD ++ZAP (IGC0009A) .
*ZAP CARD   NAME IGC0009A
*ZAP CARD   EXPAND IGC0009A(256)
         EQUREGS
         USING IGC0009A+2,R12
         USING WORKAREA,R11
         USING UCBMT,R6
         USING UCB-512,R10
         ORG   ZAP
*ZAP START VER
         SVC   76
         LR    R1,R11
RET      EQU   *
         ORG   PATCHA
         DC    X'0000000000000000'
*ZAP START REP
         ORG   ZAP
         B     PATCH
         ORG   PATCHA
PATCH    DS    0H
         SVC   76
         MVC   WTOAREA(WTOLEN),WTOMFL
         MVC   MSGAREA+10(6),UCBVOLI
         LH    R0,UCBSIO
         FMT   21,5
         LH    R0,UCBERG
         FMT   58,3
         SR    R0,R0
         IC    R0,UCBTR
         FMT   30,3
         IC    R0,UCBTW
         FMT   37,3
         IC    R0,UCBPR
         FMT   44,3
         IC    R0,UCBPW
         FMT   51,3
         WTO   MF=(E,WTOAREA)
         LR    R1,R11
         B     RET
WTOMFL   WTO   'TAPEINFO: AAAAAA,SIO=99999,TR=999,TW=999,PR=999,PW=999,*
               EG=999',MF=L,ROUTCDE=5
WTOLEN   EQU   *-WTOMFL
         LTORG
*ZAP CARD  IDRDATA HG00086
*ZAP STOP
WORKAREA DSECT
         DS    2F
WTOAREA  DS    F
MSGAREA  DS    0CL80
         PRINT NOGEN
UCB      DSECT
         IEFUCBOB
         END
