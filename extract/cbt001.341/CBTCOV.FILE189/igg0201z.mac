*ZAP CARD ++USERMOD (JA00004).
*ZAP CARD ++VER (Z038) FMID(JDM1112) PRE(UZ25962).
*ZAP CARD ++ZAP  (IGG0201Z).
*ZAP CARD *
*ZAP CARD *THIS ZAP CAUSES INFORMATION RESEMBLING THAT PROVIDED
*ZAP CARD *BY SPF TO BE PLACED IN THE DIRECTORY OF ALL PDS MEMBERS
*ZAP CARD *CREATED BY SEQUENTIAL OUTPUT (MEMBERNAME PROVIDED IN JCL
*ZAP CARD *FOR BSAM OR QSAM OUTPUT).  THE INFORMATION CONSISTS OF
*ZAP CARD *15 HALFWORDS OF USERDATA AS FOLLOWS:
*ZAP CARD * +0:  X'01000000'
*ZAP CARD * +4   CVTDATE
*ZAP CARD * +8   CVTDATE
*ZAP CARD * +C   TIME AS PACKED DECIMAL HHMM
*ZAP CARD * +E   XL6'00'
*ZAP CARD * +14: USERID
*ZAP CARD * +1C: CL2' '
*ZAP CARD *
*ZAP CARD * THE ZAP EXPLOITS THE FACT THAT A 72 BYTE FREE AREA
*ZAP CARD * IS USED TO BUILD THE DIRECTORY ENTRY.
*ZAP CARD *
*ZAP CARD  NAME IGG0201Z
*ZAP CARD  EXPAND IGG0201Z(64)
IGG0201Z DSECT
BASE     EQU   IGG0201Z+2
ZAP      EQU   IGG0201Z+X'2A6'
PATCH    EQU   IGG0201Z+X'460'
         PRINT DATA
         SPACE
         USING BASE,3
         USING STOW,9
         ORG   ZAP
*ZAP START VER
         MVI   STOCTTRN,0
         ORG   PATCH
         DC    25H'0'
PATCHEND EQU   *+64                NOTE: 64 IS CURRENT EXPAND VALUE
         ORG   ZAP
*ZAP START REP
         B     PATCH
         ORG   PATCH
         MVI   STOCTTRN,15
         MVI   STODATA,X'01'
         MVI   STODATA+1,X'00'
         MVC   STODATA+2(18),STODATA+1
         TIME  ,
         ST    1,STODATA+4
         ST    1,STODATA+8
         STCM  0,B'1100',STODATA+12
         L     1,PSAAOLD-PSA       --> ASCB
         LH    0,ASCBASID-ASCB(,1) HAVE ASID
         L     1,CVTPTR            --> CVT
         L     1,CVTUSER-CVT(,1)   --> XCVT
         L     1,XCVTXASV-XCVT(,1) --> XASVT
         LA    1,XASVENTY-XASVT-4(,1) --> XASCB VECTOR BASE
         SLL   0,2                 ASID TIMES 4
         ALR   1,0                 --> --> XASCB
         L     1,0(,1)             --> XASCB
         MVC   STODATA+20(8),XASCUID-XASCB(1)  MOVE USERID
         MVI   STODATA+28,C' '     BLANK
         MVI   STODATA+29,C' '     BLANK
         B     ZAP+4
*ZAP CARD  IDRDATA JA00004
*ZAP STOP
         LTORG
         DS    0XL(PATCHEND-*+1)  FLAGGED IF PATCH TOO LONG
         PRINT NOGEN
         IHAPSA
         CVT   DSECT=YES,LIST=YES
         XCVT  DSECT=YES,LIST=YES
         XASVT DSECT=YES,LIST=YES
         IHAASCB
         XASCB DSECT=YES,LIST=YES
         IHASTOW
         END
