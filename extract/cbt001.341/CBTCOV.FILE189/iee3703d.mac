 TITLE   'IEE3703D ZAP TO SUPPORT A=XXXX ON CANCEL AND FORCE CMDS'
         SPACE 3
*                                  JOSH AUERBACH, YCC
*                                  MARCH 1, 1979
         SPACE 3
*ZAP CARD ++USERMOD (JA00002).
*ZAP CARD ++VER (Z038) FMID(EBB1102) PRE(UZ27373).
*ZAP CARD ++ZAP (IEE3703D) .
*ZAP CARD  NAME IEE3703D
*ZAP CARD  EXPAND IEE3703D(70)
*                                  NOTE: EXPAND VALUE MUST BE CHECKED
*                                  AGAINST ZAPSIZE AND CURRENT MODULE
*                                  INFORMATION.  ABOVE VALUE IS NOT
*                                  'TIMELESS.'
         SPACE 3
IEE3703D DSECT ,                   START ADDESSABILITY
         YREGS
         USING IEE3703D+2,R12
         USING CMCHAIN,R11
         USING CMXSA,R2
         SPACE 3
*        THE FOLLOWING LABELS ARE IN THE ORIGINAL MODULE
         SPACE 1
ENDCK    EQU   IEE3703D+X'136'
CMNOCAN  EQU   IEE3703D+X'194'
CMNOGO   EQU   IEE3703D+X'1A4'
CANJOB   EQU   IEE3703D+X'1AC'
PATCH    EQU   IEE3703D+X'3AF'
         SPACE 3
*        THE FOLLOWING LABELS ARE FOR INSTRUCTIONS WHICH
*        ARE UNLABELLED IN THE ORIGINAL (SU 17) CODE
         SPACE 1
ZAP1     EQU   IEE3703D+X'108'
ZAP2     EQU   IEE3703D+X'164'
ZAP3     EQU   IEE3703D+X'16E'
         SPACE 3
*        VERIFY VARIOUS PRESUMED ADDRESS CONTENTS
         SPACE 1
         PRINT ON,GEN,DATA
*ZAP START VER
         ORG   ZAP1
         B     ENDCK
         ORG   ZAP2
         BNE   CMNOGO
         ORG   ZAP3
         BNE   CMNOGO
         ORG   CMNOGO
         L     R11,CHPTR
         ORG   PATCH
         DC    2CL25'** IEE3703D PATCH AREA **'
         ORG   ENDCK
         LA    0,41                (FIRST INS. OF SYSEVENT)
         ORG   CMNOCAN
         OI    XAE,X'10'
         ORG   CANJOB
         CLI   XAN,132
*ZAP PAUSE
         SPACE 3
*        IMPLEMENT PATCH
         SPACE 1
*ZAP START REP
         ORG   ZAP1
         B     PATCH1
         ORG   ZAP2
         BNE   PATCH2
         ORG   ZAP3
         BNE   PATCH2
         ORG   PATCH
PATCH1   DS    0H
         CLC   XAX(2),=C'A='       TEST FOR A=
         BNE   ENDCK               NO, SHORT OUT ZAP
         CLI   XAX+6,C' '          YES, TEST FOR REASONABLE LEN.
         BE    LENOK               REASONABLE, PROCEED
         MVI   XAX,C' '            UNREASONABLE, ASSURE NO MATCH
         B     ENDCK               AND LET THE BOZO TAKE IT
LENOK    LA    R15,XAX+2           --> FIRST DIGIT
         SLR   R0,R0               PREPARE TO ACCUMULATE
LOOP     CLI   0(R15),C' '         END OF DIGITS?
         BE    STORE               YES, STORE RESULT
         IC    R1,0(R15)           NO, GET DIGIT
         CLI   0(R15),C'0'         DECIMAL RANGE?
         BNL   DECRANG             YES, KEEP
         LA    R1,9(,R1)           ASSUME ALPH, ADJUST
DECRANG  SLL   R1,28               GET RIGHT NIBBLE
         SLDL  R0,4                ACCUMULATE
         LA    R15,1(,R15)         --> NEXT DIGIT
         B     LOOP                REPEAT
STORE    STCM  R0,B'0011',XAX+6    SAVE FOR LATER
         B     ENDCK               RETURN TO MAINLINE
PATCH2   CLC   XAX(2),=C'A='       TEST FOR SPECIAL
         BNE   CMNOGO              NO, RESUME MAINLINE
         CLC   CHASID,XAX+6        COMPARE ASID
         BNE   CMNOGO              NO MATCH, RESUME MAINLINE
         TM    CHACT,CHCL          MATCH, CANCELLABLE?
         BZ    CMNOCAN             NO, RESUME AT 'NONCANCEL' POINT
         B     CANJOB              YES, RESUME AT CANCEL POINT
         B     CANJOB              YES, RESUME, BYPASSING
*                                  FORGROUND BACKGROUND CHECK
         LTORG
*ZAP CARD  IDRDATA JA00002
*ZAP STOP
ZAPSIZE  EQU   *-PATCH
         EJECT
CMXSA    DSECT
         IEEXSA
         EJECT
CMCHAIN  DSECT
         IEECHAIN
         END
