IGG0     TITLE 'IGG019R0 -- ASMTOZAP SOURCE FOR TCAM LNE END APPENDAGE'
         SPACE
*ZAP CARD ++USERMOD (JA00005).
*ZAP CARD ++VER (Z038) FMID(ETC0108) PRE(UZ27380).
*ZAP CARD ++ZAP (IGG019R0).
*ZAP CARD NAME IGG019R0
*ZAP CARD *
*ZAP CARD * THIS ZAP TO THE TCAM LINE END APPENDAGE ALLOWS NULL
*ZAP CARD * LINES TO BE RECEIVED BY NON TSO APPLICATIONS AND PREVENTS
*ZAP CARD * DISCONNECT PROCESSING WHEN A NULL LINE IS ENTERED BEFORE
*ZAP CARD * LOGON IS RECOGNIZED.  IT CONSISTS OF TREATING ANY CHANNEL
*ZAP CARD * END-DEVICE-END DURING 'READ RESPONSE TO INVITATION' FOR A
*ZAP CARD * TWX DEVICE AS IF
*ZAP CARD * IT WERE COMING FROM AN 'AUDIO' DEVICE UNLESS BITS IN THE
*ZAP CARD * LCB INDICATE THAT TSO IS IN CONTROL.  THE EXCEPTION FOR
*ZAP CARD * TSO IS NECESSARY IN ORDER TO PERMIT ATTENTIONS INTENDED
*ZAP CARD * AS LINE DELETES TO BE DISTINGUISHED FROM ATTENTIONS
*ZAP CARD * INTENDED AS INTERRUPTIONS.
*ZAP CARD *
IGG019R0 DSECT ,
         YREGS
*ZAP START VER
BASE     EQU   IGG019R0+4
ZAP      EQU   IGG019R0+X'5FE'  LOCUS OF ZAP
AUDIO    EQU   IGG019R0+X'656'  SPECIAL CODE FOR 'AUDIO' DEVICE
TESTARU  EQU   IGG019R0+X'64A'  OLD BRANCH TARGET
         USING BASE,R15
         USING IEDQSCB,R14
         USING IEDQLCB,R2
*
*        VERIFY  OLD CONTENTS
*
         ORG   ZAP
         TM    SCBSTATE,SCBLCK1N
         BO    TESTARU
         L     R9,X'13C'(,R13)
         CLI   X'13'(R9),0
         BE    AUDIO
         ORG   AUDIO
         CLI   LCBCSW+6,2
*
*        REPLACE WITH NEW CONTENTS
*
*ZAP START REP
         ORG   ZAP
         L     R9,X'13C'(,R13)     --> UCB
         CLI   X'13'(R9),X'53'     TWX DEVICE?
         BNE   NOTTWX              NO, OLD CODE
         TM    LCBTSOB,LCBTSBUF+LCBINHBN   IS TWX.  LOOK LIKE TSO?
         BZ    AUDIO               NO, PRETEND WE'RE 'AUDIO'
NOTTWX   EQU   *
*ZAP CARD  IDRDATA JA00005
*ZAP STOP
         TLCBD
         TSCBD
         END
