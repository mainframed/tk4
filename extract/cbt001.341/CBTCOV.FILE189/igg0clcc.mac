IGG0CLCC CSECT
*ZAP CARD ++USERMOD(HG00002)
*ZAP CARD   /* ALLOW LOCATE OF YCC CATALOGED TAPES FROM OS CVOL */  .
*ZAP CARD ++VER(Z038) FMID(EDM1102) PRE(UZ22875).
*ZAP CARD ++ZAP(IGG0CLCC) .
*ZAP CARD NAME IGG0CLCC
* RETRIEVE CATALOGED TAPES FROM OS CATALOG UNDER VS2
* THIS MOD IS NECESSITATED BY THE PECULIAR THING ONCE DONE TO THE
* CATALOG TO INCLUDE THE PROJECT AS A SECOND PSEUDO-VOLUME FOR
* ACCOUNTING PURPOSES. THE IMPORTANT ADDRESSES ARE:
ADDR0    EQU   IGG0CLCC+X'818'
ADDR1    EQU   IGG0CLCC+X'A52'  THE OVERLAYED INSTRUCTION
ADDR2    EQU   IGG0CLCC+X'F90'  THE PATCH AREA
*ZAP START
* ZAP TO IGG0CLCC TO REMOVE SPECIAL YCC CATALOG ENTRY FOR PROJECT
* OF TAPE DATASET. THIS ENTRY, WHICH LOOKS LIKE ANOTHER VOLUME, IS
* STRIPPED OFF THE RETURNED INFORMATION FROM ANY LOCATE REFRENCING
* THE OS CATALOG.
* OS VS2 RELEASE 3.7   WITH DATA MGMT SU
* USES R0 AND R1 WHICH ARE ABOUT TO BE CLOBBERED ANYWAY
*ZAP PAUSE
* THE FOLLOWING INSTRUCTIONS ARE INCLUDED TO ALLOW THE ZAP TO BE
* TESTED BY ACTUALLY EXECUTING THE ASSEMBLER LANGUAGE
         USING *,15
         LA    4,10(15)
         L     6,=V(WKAREA)
         DROP  15
*ZAP START
         USING IGG0CLCC+10,4
         USING WKAREA,6
*ZAP PAUSE
         B     BRANCH
*ZAP START VER
         ORG   ADDR0
         MVC   WKAREA(X'3E'),12(10)
RET2     EQU   *
         ORG   ADDR1
         MVC   WKAREA(X'3E'),WKAREA+18
*ZAP PAUSE
         ORG   ADDR2
*ZAP START VER
         DC    C'ZZZZZZZZ'
*ZAP START REP
         ORG   ADDR0
         BAL   15,PATCH2
         NOPR  0
         ORG   ADDR1
BRANCH   BAL   15,PATCH
         BCR   0,0
RTN      EQU   *
         ORG   ADDR2
PATCH    EQU   *
         MVC   WKAREA(X'3E'),WKAREA+18
FIX      SR    0,0
         LH    1,VOLCNT
         BCTR  1,0
         M     0,TWELV
         LA    1,DEVT(1)
         CLC   0(4,1),DUMMY
         BNER  15
         LH    1,VOLCNT
         BCTR  1,0
         STH   1,VOLCNT
         BR    15
PATCH2   MVC   WKAREA(X'3E'),12(10)
         B     FIX
TWELV    DC    F'12'
DUMMY    DC    X'FFFFFFFF'
*ZAP CARD  IDRDATA HG00002
*ZAP STOP
         ORG   RTN
         ABEND 77
WKAREA   CSECT
VOLCNT   DC    H'2'
DEVT     DC    XL4'11111111',C'YCC055',HL2'0'
         DC    XL4'FFFFFFFF',C'YCC000',HL2'1'
         DS    18XL12
         END
