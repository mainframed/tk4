*          DATA SET Q28        AT LEVEL 001 AS OF 07/09/80
         PRINT NOGEN                                               FCI*
HISTORY  QSTART 'QUEUE COMMAND - PRINT JOB HISTORY/SOURCE    '     FCI*
         GBLB  &QACF2         ACF2 FLAG                            FCI*
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA       FCI*
         L     R10,QVDSPL     LOAD BASE REG                        FCI*
         USING WORK,R13       BASE REG FOR LOCAL WORK AREA         FCI*
***********************************************************************
*                                                                  FCI*
*   PROCESS THE HISTORY COMMAND                                    FCI*
*                                                                  FCI*
***********************************************************************
STARTIT  L     R15,=V(FINDJOB) GO FIND JOB/JCT/JQE                 FCI*
         BALR  R14,R15                                             FCI*
         L     R9,QCJCTA      GET ADDRESS OF JCT                   FCI*
         USING JCTDSECT,R9    ADDRESSABILITY                       FCI*
         SPACE 2                                                   FCI*
FORMATIT MVC   QDHLINE,HEADER                                      FCI*
         MVC   QDMSG,QBLANK   BLANK OUT LINE                       FCI*
         BAL   R7,DISPLAY     GO DO THE DISPLAY                    FCI*
*                                                                  FCI*
LINE3    MVC   QDMSG,HEAD3    MOVE IN LINE MASK                    FCI*
         MVC   JOBNUMB,JCTJOBID     JOB ID                         FCI*
         MVC   JOBNAME,JCTJNAME     JOBNAME                        FCI*
         MVC   JOBSCE,JCTINDEV      INPUT DEVICE                   FCI*
         MVC   BIN#,JCTROOMN        BIN NUMBER                     FCI*
         MVC   JOBCLASS,JCTJCLAS    JOB CLASS FROM JOBCARD         FCI*
         MVC   MSGCLASS,JCTMCLAS    MESSAGE CLASS                  FCI*
         MVC   NOTIFY,JCTTSUID      WHO TO CONTACT                 FCI*
         BAL   R7,DISPLAY           PUT OUT LINE3                  FCI*
*                                                                  FCI*
LINE4    BAL   R7,DISPBLNK       GO PUT OUT A BLANK LINE           FCI*
*                                                                  FCI*
LINE5    MVC   QDMSG,HEAD5        MOVE IN HEADING                  FCI*
         MVC   PROCDDN,JCTPROCN   PROCLIB DDNAME                   FCI*
         LA    R15,PRTDEST                                         FCI*
         LA    R1,JCTPROUT                                         FCI*
         BAL   R7,RMTORLCL       CONVERT PRT= FIELD                FCI*
         LA    R15,PUNDEST                                         FCI*
         LA    R1,JCTPUOUT                                         FCI*
         BAL   R7,RMTORLCL       CONVERT PUN= FIELD                FCI*
         LM    R0,R1,JCTRDRON    GET READER TIME/DATE              FCI*
         LTR   R1,R1             ANYBODY (DATE) HOME               FCI*
         BZ    NODATE5                                             FCI*
         BAL   R7,TIMECONV       FORMAT IT PRETTY                  FCI*
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD                 FCI*
NODATE5  BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
LINE6    MVC   QDMSG,HEAD6        MOVE IN HEADING                  FCI*
         MVC   PROGAMER,JCTPNAME  PROGRAMMER NAME                  FCI*
         LM    R0,R1,JCTXEQON    GET XEQ START TIME/DATE           FCI*
         LTR   R1,R1             ANYBODY (DATE) HOME               FCI*
         BZ    NODATE6                                             FCI*
         BAL   R7,TIMECONV       FORMAT IT PRETTY                  FCI*
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD                 FCI*
NODATE6  BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
LINE7    MVC   QDMSG,HEAD7        MOVE IN HEADING                  FCI*
         LA    R15,CARDSIN       POINT TO POSITION                 FCI*
         L     R1,JCTCARDS       GET VALUE                         FCI*
         BAL   R7,FITINUM        AND PUT IT AWAY                   FCI*
         LM    R0,R1,JCTXEQOF    GET XEQ STOP TIME/DATE            FCI*
         LTR   R1,R1             ANYBODY (DATE) HOME               FCI*
         BZ    NODATE7                                             FCI*
         BAL   R7,TIMECONV       FORMAT IT PRETTY                  FCI*
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD                 FCI*
NODATE7  BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
LINE8    MVC   QDMSG,HEAD8        MOVE IN HEADING                  FCI*
         LA    R15,LINESOUT      POINT TO POSITION                 FCI*
         L     R1,JCTLINES       GET VALUE                         FCI*
         BAL   R7,FITINUM        AND PUT IT AWAY                   FCI*
         LA    R15,CARDSOUT      POINT TO POSITION                 FCI*
         L     R1,JCTPUNCH       GET VALUE                         FCI*
         BAL   R7,FITINUM        AND PUT IT AWAY                   FCI*
         LM    R0,R1,JCTOUTON    GET PRT/PUN START TIME/DATE       FCI*
         LTR   R1,R1             ANYBODY (DATE) HOME               FCI*
         BZ    NODATE8                                             FCI*
         BAL   R7,TIMECONV       FORMAT IT PRETTY                  FCI*
         MVC   TIMESTMP,CDAYN    MOVE IN TO RECORD                 FCI*
NODATE8  BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
         AIF   (NOT &QACF2).$SKP1                                  FCI*
LINE911  MVC   QDMSG,QBLANK     BLANK IT OUT                       FCI*
         LA    R2,4             SAY DO IT 4 TIMES                  FCI*
BLNKS    BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
         BCT   R2,BLNKS                                            FCI*
*                                                                  FCI*
LINE12   MVC   QDMSG,HEAD12       MOVE IN HEADING                  FCI*
         BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
LINE13   BAL   R7,DISPBLNK       GO PUT OUT A BLANK LINE           FCI*
*                                                                  FCI*
LINE14   MVC   QDMSG,HEAD14       MOVE IN HEADING                  FCI*
         MVC   LOGONID,LIDLID    ACF2 LOGON ID                     FCI*
         MVC   GROUP,LIDGROUP    ACF2 GROUP (FCI FIELD)            FCI*
         MVC   NAME,LIDNAME      ACF2 USER NAME/DESCRIPT           FCI*
         MVC   PHONE,LIDPHONE    ACF2 PHONE NUMBER FOR USER        FCI*
         BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
*                                                                  FCI*
LINE15   BAL   R7,DISPBLNK       GO PUT OUT A BLANK LINE           FCI*
*                                                                  FCI*
LINE16   MVC   QDMSG,HEAD16       MOVE IN HEADING                  FCI*
         MVC   SOURCE,ACVSRCE    ACF2 LOGICAL SOURCE               FCI*
         MVC   SUBLID,ACVSLID    ACF2 SUBMITTING LID               FCI*
         MVC   SUBPROG,ACVAPROG  ACF2 SUBMITTING PROGRAM           FCI*
         MVC   SUBPATH,ACVPATH   ACF2 SUBMITTING PATH              FCI*
         BAL   R7,DISPLAY        GO SHOW IT OFF                    FCI*
.$SKP1   ANOP                                                      FCI*
         SPACE 2                                                   FCI*
RETURN   QSTOP                                                     FCI*
         SPACE 2                                                   FCI*
***********************************************************************
*   LOCAL SUBROUTINE TO PUT OUT LINE VIA Q DISPLAY                 FCI*
***********************************************************************
DISPBLNK MVC   QDMSG,QBLANK   CLEAR OUT MESSAGE                    FCI*
*                                                                  FCI*
DISPLAY  MVC   QDMLNG,=H'80'  SET LENGTH                           FCI*
         LA    R0,QDMSG       GET ADDRESS OF MESSAGE               FCI*
         ST    R0,QDMSGA      AND SAVE ADDRESS                     FCI*
         L     R15,=V(DISPLAY) GET ADDRESS OF MODULE FOR DISPLAY   FCI*
         BALR  R14,R15        CALL THE MOTHER                      FCI*
         BR    R7             RETURN TO MAINLINE...                FCI*
         EJECT ,                                                   FCI*
***********************************************************************
*                                                                  FCI*
* FORMAT DATE TIME  HEADING   ENTER WITH BAL R7,TIMECONV           FCI*
*   R0=TIME IN 100THS OF SECOND SINCE MIDNIGHT                     FCI*
*   R1=DATE IN 00YYDDDS FORMAT                                     FCI*
*                                                                  FCI*
* RETURNS WITH CDAYN(LEN) = DDMONYY (DDD) DAY HH:MM:SS.TH          FCI*
*                E.G.       07JUL80 (189) MON 13:45:24.56          FCI*
*                           123456789.123456789.123456789          FCI*
*                                                                  FCI*
***********************************************************************
TIMECONV MVC   MONMSK(MVCLEN),XMONMSK  INIT DATE/TIME WORK AREA    FCI*
         ST    R1,DATE                 SAVE DATE FOR LATER         FCI*
         CVD   R0,DOUBLEWD            GET TIME IN DEC 100TH/SEC    FCI*
         DP    DOUBLEWD(8),DEC6000(3) /6000 : REM IS SS.TH         FCI*
         DP    DOUBLEWD(5),DEC60(2)   /60   : REM IS MM            FCI*
         MVC   EDITWORK(L'HHMASK),HHMASK   SETUP MULT EDIT MASK    FCI*
         ED    EDITWORK(L'HHMASK),DOUBLEWD EDIT HH,MM,SS           FCI*
FINTIME  MVC   CHH(2),EDITWORK+4      MOVE IN HOURS                FCI*
         MVC   CMM(2),EDITWORK+8      MOVE IN MM                   FCI*
         MVC   CSS(5),EDITWORK+12     MOVE IN SS.TH                FCI*
*                                                                  FCI*
***FORMAT THE DATE INTO THE OUTPUT MSG                             FCI*
*                                                                  FCI*
         MVC   WORKWORD(4),DATE        MOVE DATE TO WORK AREA.     FCI*
         MVO   WORKWORD+1(3),WORKWORD(2)   MAKE 00YYDDDS INTO 00000YYS.
         UNPK  CYR(2),WORKWORD+2(2)    FORMAT YEAR.                FCI*
         XC    DOUBLEWD,DOUBLEWD                                   FCI*
         MVC   DOUBLEWD+4(4),WORKWORD  CNVT YEAR TO BIN            FCI*
         CVB   R0,DOUBLEWD                                         FCI*
         ST    R0,BINYEAR              AND SAVE                    FCI*
         TM    BINYEAR+3,X'03'         LEAP YEAR?                  FCI*
         BNZ   MON1                    NO.                         FCI*
         MVI   MONMSK+10,29            YES, CORRECT                FCI*
MON1     XC    DOUBLEWD,DOUBLEWD       CNVT DAYS TO BIN            FCI*
         MVC   DOUBLEWD+6(2),DATE+2                                FCI*
         CVB   R2,DOUBLEWD                                         FCI*
         ST    R2,BINDAYS              SAVE FOR LATER              FCI*
         XR    R1,R1                                               FCI*
         LA    R15,11                                              FCI*
MON2     IC    R1,MONMSK(R15)          COMPUTE MON & DAY           FCI*
         SR    R2,R1                                               FCI*
         BNP   MONOVR                                              FCI*
         BCT   R15,MON2                                            FCI*
         B     *+6                                                 FCI*
MONOVR   AR    R2,R1                   CORRECT OVERDRAW            FCI*
         CVD   R2,DOUBLEWD             FORMAT DAY                  FCI*
         UNPK  CDAYN(2),DOUBLEWD+6(2)                              FCI*
         OI    CDAYN+1,X'F0'           FIX UP SIGN                 FCI*
         MH    R15,=H'3'               GET TABLE OFFSET            FCI*
         LA    R15,MONTAB(R15)         AND POINT AT MONTH          FCI*
         MVC   CMON(3),0(R15)          MOVE IT TO THE MSG          FCI*
         EJECT ,                                                   FCI*
***DAY OF WEEK                                                     FCI*
         L     R15,BINYEAR                                         FCI*
         SH    R15,=H'69'                                          FCI*
         BNP   OUTT                    MUST BE GREATER OR BAD      FCI*
         XR    R14,R14                 CLEAR DAY REG               FCI*
         D     R14,=F'4'                                           FCI*
         MH    R15,=H'5'                                           FCI*
         AR    R14,R15                                             FCI*
         A     R14,BINDAYS                                         FCI*
         SRDA  R14,32                                              FCI*
         D     R14,=F'7'               MODULO 7 FOR WEEK           FCI*
         MH    R14,=H'3'                                           FCI*
         LA    R14,DAYTAB(R14)         POINT AT TODAY              FCI*
         MVC   CDAY(3),0(R14)          INSERT IN MSG               FCI*
***                                                                FCI*
MOVEJD   MVC   JDATE-1(7),=X'4020204B202020'                       FCI*
         ED    JDATE-1(7),DATE+1                                   FCI*
         MVC   CJDAY(3),JDATE+3         MOVE DAY TO MESSAGE        FCI*
*                                                                  FCI*
*                                                                  FCI*
DATERTN  BR    R7                      RETURN TO MAINLINE          FCI*
*                                                                  FCI*
OUTT     MVC   CDAY(3),=CL3'???'                                   FCI*
         B     MOVEJD                                              FCI*
         SPACE 2                                                   FCI*
         EJECT
***********************************************************************
*                                                                  FCI*
*       CONSTANTS FOR DATE ROUTINE USAGE                           FCI*
*                                                                  FCI*
***********************************************************************
         SPACE ,
HHMASK   DC    X'40202021202022212020222120204B2020'               FCI*
DEC60    DC    X'060C'                                             FCI*
DEC6000  DC    X'06000C'                                           FCI*
DEC50    DC    X'050C'                                             FCI*
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES       FCI*
*                                                                  FCI*
XMONMSK  DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )   FCI*
         DC    CL1' '                                          )   FCI*
         DC    C'XX.XXX'                           L           )   FCI*
*                                                  E  F        )   FCI*
         DC    CL2' '                              A  I  T     )   FCI*
         DC    CL3' '                              V  E  O  F  )   FCI*
         DC    CL2' ',C' ('                        E  L  G  O  )   FCI*
         DC    CL3' ',C') '                           D  E  R  )   FCI*
         DC    CL3' ',C' '                            S  T     )   FCI*
         DC    CL2' ',C':'                               H  I  )   FCI*
         DC    CL2' ',C':'                               E  N  )   FCI*
         DC    CL5'SS.TH'                                R  I  )   FCI*
*                                                           T  )   FCI*
MVCLEN   EQU   *-XMONMSK                                           FCI*
*                                                                  FCI*
***********************************************************************
DAYTAB   DC    C'TUEWEDTHRFRISATSUNMON'                            FCI*
MONTAB   DC    C'DECNOVOCTSEPAUGJULJUNMAYAPRMARFEBJAN'             FCI*
         EJECT
***********************************************************************
*RMTORLCL SUBROUTINE - DETERMINE REMOTE OR LOCAL DESTINATION       FCI*
* R1 POINTS TO PRTRT/PUNRT, R15 TO ASSEMBLY POINTER                FCI*
***********************************************************************
RMTORLCL CLI   1(R1),0        IS IT FOR REMOTE 00=LOCAL            FCI*
         BNE   RMTRMT         NO LUCK .. HAVE TO WORK FOR IT       FCI*
*                                                                  FCI*
         MVC   0(L'LOCAL,R15),LOCAL   MOVE IN LOCAL                FCI*
         LA    R15,L'LOCAL(R15)       BUMP POINTER AND             FCI*
         BR    R7                       RETURN                     FCI*
*                                                                  FCI*
RMTRMT   MVC   0(L'RMT,R15),RMT       MOVE IN 'RMT'                FCI*
         LA    R15,L'RMT(R15)  BUMP POINTER                        FCI*
         SR    R14,R14        LOAD RMT FLAGS                       FCI*
         IC    R14,1(R1)      FROM PRT/PUN                         FCI*
         CVD   R14,DOUBLEWD   RMT NUMBER TO DECIMAL                FCI*
         B     FITINUM1       FIT THE NUMBER IN RMT MESSAGE        FCI*
         SPACE 2
***********************************************************************
* FITINUM SUBROUTINE - CONVERT BIN NUMBER TO NICE FORMAT           FCI*
*                                                                  FCI*
***********************************************************************
FITINUM  CVD   R1,DOUBLEWD    CONVERT TO PACKED DECIMAL            FCI*
FITINUM1 MVC   EDITWORK(L'NORMAL),NORMAL INIT THE EDIT FORMAT      FCI*
         LA    R1,EDITWORK+SIGNORM IN CASE OF ZEROES               FCI*
         EDMK  EDITWORK(L'NORMAL),DOUBLEWD+2 CONVERT TO EBCDIC     FCI*
         LA    R14,EDITWORK+L'NORMAL-1 A(END OF CONVERTED NUMBER)  FCI*
         SLR   R14,R1         LENGTH OF THE CONVERTED NUMBER - 1   FCI*
         EX    R14,MVNUMBER   PUT THE NUMBER IN THE MSG            FCI*
         LA    R15,1(R14,R15) A(NEXT SPOT IN MSG)                  FCI*
         BR    R7             RETURN TO OUR CALLER                 FCI*
         SPACE 3                                                   FCI*
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED                       FCI*
         SPACE 2                                                   FCI*
NORMAL   DC    X'402020202020202020202120' EDIT MASK               FCI*
SIGNORM  EQU   11             OFFSET TO LAST DIGIT                 FCI*
RMT      DC    C'RMT'                                              FCI*
LOCAL    DC    C'LOCAL'                                            FCI*
         EJECT ,                                                   FCI*
         LTORG                                                     FCI*
         EJECT ,                                                   FCI*
***********************************************************************
*                                                                  FCI*
* LINE MAPPINGS                                                    FCI*
*                                                                  FCI*
***********************************************************************
*                   0123456789.123456789.123456789.123456789.123456789.
HEADER   DC    CL80'                           JOB HISTORY INFORMATION'
HEAD3    DC    CL80'XXXXXXXX  XXXXXXXX VIA=XXXXXXXX BIN=XXXX  CLASS=X M*
               SGCLASS=X  NOTIFY=XXXXXXX'                          FCI*
*              123456789.123456789.123456789
HEAD5    DC    CL80'PROCDDN=XXXXXXXX PRT=       PUN=        RDR TIME'
HEAD6    DC    CL80'PROGRAMMER=XXXXXXXXXXXXXXXXXXXX         XEQ START'
HEAD7    DC    CL80'CARDS  IN=                              XEQ STOP'
HEAD8    DC    CL80'LINES OUT=             CARDS OUT=       PRT START'
*
         AIF   (NOT &QACF2).$SKP2                                  FCI*
HEAD12   DC    CL80'                              ACF2 INFORMATION'
HEAD14   DC    CL80'LOGONID=XXXXXXXX GROUP=XXXXXXXX NAME=XXXXXXXXXXXXXX*
               XXXXXX PHONE=XXXXXXXXXXXX'                          FCI*
HEAD16   DC    CL80'SOURCE=XXXXXXXX SUBMITTED BY XXXXXXXX XXXXXXXX XXXX*
               XXXX'                                               FCI*
.$SKP2   ANOP  ,                                                   FCI*
         EJECT ,                                                   FCI*
JCT      EQU   0                                                   FCI*
BUFSTART EQU   0                                                   FCI*
BUFDSECT EQU   0                                                   FCI*
         $JCT                                                      FCI*
         EJECT  ,                                                  FCI*
         QCOMMON                                                   FCI*
         SPACE 2                                                   FCI*
*                            OVERLAY FOR HEAD3                     FCI*
JOBNAME  EQU   QDMSG+00,8                                          FCI*
JOBNUMB  EQU   QDMSG+10,8                                          FCI*
JOBSCE   EQU   QDMSG+23,8                                          FCI*
BIN#     EQU   QDMSG+36,4                                          FCI*
JOBCLASS EQU   QDMSG+48,1                                          FCI*
MSGCLASS EQU   QDMSG+59,1                                          FCI*
NOTIFY   EQU   QDMSG+69,7                                          FCI*
*                                                                  FCI*
*                            OVERLAY FOR HEAD5                     FCI*
PROCDDN  EQU   QDMSG+08,8                                          FCI*
PRTDEST  EQU   QDMSG+21,6                                          FCI*
PUNDEST  EQU   QDMSG+32,6                                          FCI*
TIMESTMP EQU   QDMSG+50,29     COMMON FOR READER/XEQ/PRT           FCI*
*                                                                  FCI*
*                            OVERLAY FOR HEAD6                     FCI*
PROGAMER EQU   QDMSG+11,20                                         FCI*
*                            OVERLAY FOR HEAD7                     FCI*
CARDSIN  EQU   QDMSG+10,8                                          FCI*
*                            OVERLAY FOR HEAD8                     FCI*
LINESOUT EQU   QDMSG+10,8                                          FCI*
CARDSOUT EQU   QDMSG+33,8                                          FCI*
         AIF   (NOT &QACF2).$SKP3                                  FCI*
*                            OVERLAY FOR HEAD14                    FCI*
LOGONID  EQU   QDMSG+08,8                                          FCI*
GROUP    EQU   QDMSG+23,8                                          FCI*
NAME     EQU   QDMSG+37,20                                         FCI*
PHONE    EQU   QDMSG+64,12                                         FCI*
*                            OVERLAY FOR HEAD16                    FCI*
SOURCE   EQU   QDMSG+07,8                                          FCI*
SUBLID   EQU   QDMSG+29,8                                          FCI*
SUBPROG  EQU   QDMSG+38,8                                          FCI*
SUBPATH  EQU   QDMSG+47,8                                          FCI*
.$SKP3   ANOP  ,                                                   FCI*
         EJECT ,                                                   FCI*
***********************************************************************
*                                                                  FCI*
* WORK AREA DSECT                                                  FCI*
*                                                                  FCI*
***********************************************************************
WORK     DSECT                                                     FCI*
         DS    CL72                                                FCI*
DOUBLEWD DC    D'0'                                                FCI*
BINYEAR  DC    F'0'                                                FCI*
BINDAYS  DC    F'0'                                                FCI*
DATE     DC    F'0'                                                FCI*
WORKWORD DC    2F'0'                                               FCI*
EDITWORK DC    12H'0'
***********************************************************************
* LEAVE FIELDS TOGETHER.. INITIALIZED BY ONE MVC WITH VALUES       FCI*
*                                                                  FCI*
MONMSK   DC    AL1(31,30,31,30,31,31,30,31,30,31,28,31)        )   FCI*
         DC    CL1' '                                          )   FCI*
*        0123456789.123456789.123456789.                       )   FCI*
*        DDMONYY (DDD) DAY HH:MM:SS.TH                         )   FCI*
*                                                              )   FCI*
JDATE    DC    C'XX.XXX'                           L           )   FCI*
*                                                  E  F        )   FCI*
CDAYN    DC    CL2' '                              A  I  T     )   FCI*
CMON     DC    CL3' '                              V  E  O  F  )   FCI*
CYR      DC    CL2' ',C' ('                        E  L  G  O  )   FCI*
CJDAY    DC    CL3' ',C') '                           D  E  R  )   FCI*
CDAY     DC    CL3' ',C' '                            S  T     )   FCI*
CHH      DC    CL2' ',C':'                               H  I  )   FCI*
CMM      DC    CL2' ',C':'                               E  N  )   FCI*
CSS      DC    CL5'SS.TH'                                R  I  )   FCI*
*                                                           T  )   FCI*
***********************************************************************
LEN      EQU   *-CDAYN                                             FCI*
         END  ,                                                    FCI*
