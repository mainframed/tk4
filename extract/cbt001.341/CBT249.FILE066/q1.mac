         PRINT NOGEN
QUEUE    QSTART 'Q1 - QUEUE COMMAND - MAINLINE MODULE',MAIN=YES
***********************************************************************
*                                                                     *
*   CALL - INITIALIZATION                                             *
*                                                                     *
***********************************************************************
         L     R15,=V(INIT)   ADDR OF INIT
         BALR  R14,R15        GO TO IT
         STFSMODE  ON,INITIAL=YES                                   BFG
         LA    R10,LOOP       INTERRUPTED RETURN ADDRESS
         USING QDISPLAY,R9    BASE REG FOR DISPLAY WORK AREA
         L     R9,QVDSPL      LOAD BASE REG
         L     R3,8(R2)       LOAD ADDRESS OF CPPL: PSCB           FCI*
         USING PSCB,R3                                             FCI*
CHKOPER  TM    PSCBATR1,PSCBCTRL DOES USER HAVE OPER PRIVILEDGE    FCI*
         BNO   CKPT           NO. SKIP AUTH SETUP              HWC FCI*
         DROP  R3                                                  FCI*
         OI    QXAUTH,X'80'   INDICATE OPER PRIV USER              FCI*
         B     CKPT                                                HWC
***********************************************************************
*                                                                     *
*   LOOK THRU TABLE FOR COMMANDS WHICH DONT NEED CHECKPOINT READS HWC *
*                                                                 HWC *
***********************************************************************
LOOP     EQU   *              CHECK FOR COMMANDS WHICH DONT       HWC
         LA    R15,QUICKTBL   NEED CHECKPOINT PROCESSING          HWC
         LA    R14,QDREPLY    POINT TO FIRST PLACE TO LOOK        BFG
         CLI   0(R14),C' '    IS IT BLANK?                        BFG
         BNE   LOOP1          NO, SO USE IT                       BFG
         LA    R14,QSUBNAME   SINCE BLANK, USE OTHER INPUT        BFG
LOOP1    CLC   0(2,R14),0(R15)                              BFG + HWC
         BE    PARSE                                              HWC
         LA    R15,2(,R15)                                        HWC
         CLI   0(R15),X'FF'                                       HWC
         BNE   LOOP1                                              HWC
***********************************************************************
*                                                                     *
*   CALL - READ JES2 CHECKPOINT FILE                                  *
*                                                                     *
***********************************************************************
CKPT     L     R15,=V(CKPT)   ADDR OF CKPT
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL - COMMAND LINE PARSE                                         *
*                                                                     *
***********************************************************************
PARSE    L     R15,=V(PARSE)  ADDR OF PARSE
         BALR  R14,R15        GO TO IT
         CLC   =C'E ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'EXIT ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'END ',QSUBNAME STOP?
         BE    EXIT           YES.
         CLC   =C'STOP ',QSUBNAME STOP?
         BE    EXIT           YES.
***********************************************************************
*                                                                     *
*   CALL - SUB-COMMAND MODULE SELECTED BY PARSE                       *
*                                                                     *
***********************************************************************
         MVC   QDHLINE,DUMMY  NO OUTPUT MESSAGE
         L     R15,QSUBCMD    ADDR OF SUBCMD FROM QCOMMON
         BALR  R14,R15        GO TO IT
         MVC   QDMLNG,=H'0'   ZERO OUT MESSAGE LENGTH
         L     R15,=V(DISPLAY) ADDR OF DISPLAY MODULE
         BALR  R14,R15        WRITE LAST SCREEN, GET NEXT INPUT
         B     LOOP           DO IT AGAIN
***********************************************************************
*                                                                     *
*   CLEAN UP AND GO HOME                                              *
*                                                                     *
***********************************************************************
EXIT     TPUT  CLEAR,L'CLEAR,FULLSCR,MF=(E,QTPUT) CLEAR SCREEN     PWF*
         STFSMODE  OFF                                              BFG
         USING QCKPT,R8 BASE REG FOR CKPT WORK AREA
         L     R8,QVCKPT      LOAD BASE REG
         CLOSE MF=(E,HOCKPT)
         CLOSE MF=(E,QCSPOOLS)
         TM    QPFLAG,HARDCPY         IS HARDCOPY INVOKED?         FCI*
         BNO   FREEUP                   NO..SPLIT THIS STUFF       FCI*
         L     R15,=V(PRINT)               INVOKE PRINT            FCI*
         MVC   QDREPLY,QBLANK                  TO                  FCI*
         MVC   QDREPLY(09),=C'PRINT OFF'          FREE UP          FCI*
         MVC   QDRLNG,=X'0009'                        HARDCOPY     FCI*
         BALR  R14,R15                                     OUTPUT  FCI*
         EJECT
***********************************************************************
*                                                                  FCI*
*   FREE HASPCKPT AND HASPACEN DDNAMES BEFORE LEAVING TO BE NEAT.. FCI*
*                                                                  FCI*
***********************************************************************
FREEUP   MVI   DAIRFLAG,X'18'    INDICATE FREE DDNAME(XXXXXXXX)    FCI*
         MVC   DA18DDN,HASPCKPT+40  GET DDNAME USED...             FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           GO FREE IT..                      FCI*
*
         LA    R2,QCSPOOLS       GET ADDR OF LIST OF HASPACE DCBS  FCI*
         LA    R4,35             MAX OF 35 PASSES THROUGH HERE..   FCI*
FREEUP1  L     R3,0(R2)          GET ADDRESS OF DCB TO WORK ON     FCI*
         LTR   R3,R3             ANYONE THERE?                     FCI*
         BZ    EXITQCK                                             FCI*
         MVC   DA18DDN,40(R3)    MOVE IN DDNAME FROM DCB           FCI*
         L     R15,=V(ALLOCATE)  GET ROUTINE NAME                  FCI*
         BALR  R14,R15           AND GO INVOKE DAIR TO FREE IT..   FCI*
         LA    R2,4(R2)          BUMP                              FCI*
         BCT   R4,FREEUP1                                          FCI*
***********************************************************************
*                                                                     *
*   FREE THE AREAS ACQUIRED IN INIT (Q3)                              *
*                                                                     *
***********************************************************************
EXITQCK  OI    QGETL3,X'80'   PREPARE FOR FREEMAIN             PWF FCI*
         FREEMAIN MF=(E,QFREE)
         QSTOP
***********************************************************************
*                                                                     *
*   CONSTANTS AND OTHER ODDITIES                                      *
*                                                                     *
***********************************************************************
         LTORG
CLEAR    DC    X'C1115D7E1140403C4040001DC813'  *** SPF TCAM
DUMMY    DC    CL80'    NO DATA IS AVAILABLE FOR YOUR REQUEST'
QUICKTBL DC    C'HEFAFTTOBOUPENSAXPXBCODCXIB C D E F U - @ + HBHFPFPB'
         DC    C'MDRMH h rmXCxc'                                   HWC
         DC    C'hefafttoboupensaxpxbcodcxib c d e f u hbhfpfpb'   HWC
         DC    X'FFFFFFFFFFFFFF' LEAVE SOME ZAPPING ROOM           HWC
         QCOMMON
         EJECT
         IKJPSCB                                                   FCI*
         END
