         PRINT NOGEN
SYSLOG   QSTART 'QUEUE COMMAND - PRINT THE SYSTEM LOG DATASET'
         USING QCKPT,R10      BASE REG FOR CKPT WORK AREA
         L     R10,QVCKPT     LOAD BASE REG
         USING WORK,R13       BASE REG FOR TEMP WORK
***********************************************************************
*                                                                     *
*   SUPPLY DEFAULT SYSLOG                                             *
*                                                                     *
***********************************************************************
         CLI   QPARM1,C' '           ?/SPECIFIC SYSLOG SPECIFIED
         BE    DEFAULT               NO, TAKE DEFAULT               BFG
         CLC   QPARM1(4),=C'IPO1'    SLOG FOR IPO1?                 BFG
         BE    IPO1                  YES, FILL IN FOR IPO1          BFG
         CLC   QPARM1(3),=C'168'     DID THEY SPECIFY 168?          BFG
         BE    IPO1                  YES, FILL IN FOR IPO1          BFG
         CLC   QPARM1(4),=C'IPO2'    DID THEY SPECIFY IPO2          BFG
         BE    IPO2                  YES, FILL IN FOR IPO2          BFG
         CLC   QPARM1(4),=C'3033'    DID THEY SPECIFY 3033          BFG
         BNE   CALLFJOB              NO, USE SPECIFIC REQUEST       BFG
IPO1     EQU   *                                                    BFG
         LA    R1,2                  LOAD CPU AFFINITY              BFG
         B     CONTINUE              AND CONTINUE                   BFG
IPO2     EQU   *                                                    BFG
         LA    R1,1                  LOAD CPU AFFINITY              BFG
         B     CONTINUE              AND CONTINUE                   BFG
DEFAULT  EQU   *                                                    BFG
         MVC   QPARM1,QLOGON         TSO USERID
         MVC   QLNG1,=H'8'           LENGTH OF QPARM1
         L     R15,=V(FINDJOB)       ADDRESS OF FINDJOB
         BALR  R14,R15               GO TO FINDJOB
         SR    R1,R1                 CLEAR REG
         L     R2,QCJQEA             A(JQE)
         USING JQEDSECT,R2           JQE ADDRESSABILITY
         IC    R1,JQEFLAGS           INSERT JQE FLAGS
         N     R1,=X'00000007'       TURN OFF USELESS BITS
CONTINUE EQU   *                                                    BFG
         MVC   QPARM1,=CL8'SYSLOG'   JOBNAME FOR JQE SEARCH
         MVC   QLNG1,=H'8'           LENGTH OF JOBNAME
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
CALLFJOB L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CHECK JOBNAME EQUAL SYSLOG                                        *
*                                                                     *
***********************************************************************
         L     R9,QCJCTA      ADDR OF IOAREA FOR JCT
         USING JCTDSECT,R9    BASE REG FOR JCT
         CLC   JCTJNAME,=CL8'SYSLOG' IS THIS SYSLOG?
         BNE   TILT           NO. TILT.
***********************************************************************
*                                                                     *
*   DETERMINE NUMBER OF DATASETS FROM CURRENT DATASET                 *
*                                                                     *
***********************************************************************
         LH    R3,JCTPDDBK    HIGHEST DATASET ID NUMBER
         LH    R2,QLNG2       LENGTH OF BACKUP PARM
         SH    R2,=H'1'       IS THE BACKUP PARM ZERO LENGTH?
         BM    CALLLIST       YES. SKIP.
         CLI   QPARM2,C'-'    IS THERE A MINUS SIGN?
         BNE   PLUS           NO. SKIP.
         MVI   QPARM2,C'0'    CHANGE MINUS TO ZERO
PLUS     MVC   QFZONES,QFZONE INITIALIZE NUMERIC TEST
         EX    R2,MVZ         MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE IS THE FIELD NUMERIC?
         BNE   TILT           NO. QUIT.
         EX    R2,PACK        PACK THE FIELD
         CVB   R2,CONVERT     CONVERT TO BINARY
         SR    R3,R2          BACK UP NUMBER OF DATASETS
         CH    R3,=H'101'     IS THE NUMBER LESS THAN 101?
         BL    TILT           YES. TILT.
***********************************************************************
*                                                                     *
*   CALL LISTDS TO LIST THE DATASET                                   *
*                                                                     *
***********************************************************************
CALLLIST STH   R3,QPDSID      STORE DATASET ID
         MVC   QPOFFSET,=H'0' PRINT OFFSET ZERO
         L     R15,=V(LISTDS) ADDR OF LISTDS MODULE
         BALR  R14,R15        GO TO IT
         QSTOP
TILT     QTILT '*** INVALID PARAMETER ***'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
MVZ      MVZ   QFZONES(1),QPARM2 CHECK FOR NUMERIC
PACK     PACK  CONVERT,QPARM2(1) CONVERT TO BINARY
         LTORG
JCT      EQU   0
BUFSTART EQU   0
BUFDSECT EQU   0
         $JCT
         $JQE
WORK     DSECT
         DS    72C
CONVERT  DS    D
         QCOMMON
         END
