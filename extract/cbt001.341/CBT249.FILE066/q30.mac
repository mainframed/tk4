CONSOLE  QSTART 'Q30 - ECHO CONSOLE ON USER SCREEN'                 BFG
         USING QDISPLAY,R9                                          BFG
         L     R9,QVDSPL                                            BFG
         TESTAUTH FCTN=1                                            BFG
         LTR   R15,R15               ARE WE AUTHORISED              BFG
         BZ    AUTHCODE              YES, SKIP SVC SET              BFG
* QTILT '***** FUNCTION NOT AUTHORISED *****'                       BFG
         L     R1,PSATOLD-PSA        GET TCB ADDRESS                BFG
         L     R0,0(,R1)             GET RB ADDRESS                 BFG
         USVC  GETKEY,KEY=0          GET INTO KEY 0                 BFG
         OI    FLAG1,F1AUTH          MARK WE ARE AUTHORIZED         BFG
*  USVC ALOWS US TO ENTER KEY ZERO                                  BFG
*  WITHOUT THE PROGRAM BEING AUTHORISED                             BFG
AUTHCODE EQU  *                                                     BFG
         L    R7,16                  GET CVT ADDRESS                BFG
         L    R7,100(,R7)            GET CVTCUCB A(IEECUCB) CONSOLE BFG
         L    R7,24(,R7)             WQE                            BFG
         MVC  QDMSG(80),TOP          MESSAGE TO SCREEN              BFG
         MVC  QDMLNG,=H'80'          SET THE LENGTH                 BFG
         LA   R0,QDMSG               GET THE ADDR                   BFG
         ST   R0,QDMSGA              SET THE ADDR                   BFG
         L    R15,=V(DISPLAY)                                       BFG
         BALR R14,R15                DISPLAY THE LINE               BFG
         MVC  QDHLINE,QBLANK         BLANK OUT THE HEADER LINE      BFG
         MVC  QDMSG,QBLANK           BLANK OUT THE MESSAGE          BFG
LOOP     EQU  *                                                     BFG
         LA   R7,0(R7)                                              BFG
         LTR  R7,R7                                                 BFG
         BZ   END                                                   BFG
         LRA  R0,0(R7)               SEE IF STARING ADDR EXISTS     BFG
         BNE  END                                                   BFG
         LRA  R0,158(R7)             SEE IF ENDING ADDR EXISTS      BFG
         BNE  END                                                   BFG
         MVC  QDMSG(80),13(R7)       MESSAGE TO SCREEN              BFG
         MVC  QDMLNG,=H'80'          SET THE LENGTH                 BFG
         LA   R0,QDMSG               GET THE ADDR                   BFG
         ST   R0,QDMSGA              SET THE ADDR                   BFG
         L    R15,=V(DISPLAY)                                       BFG
         BALR R14,R15                DISPLAY THE LINE               BFG
         LRA  R0,0(R7)               SEE IF ADDR EXISTS             BFG
         BNE  END                                                   BFG
         CLC  4(4,7),=F'62'          MESSAGE LONGER THAN 61?        BFG
         BNH  LOOP1                  NO, KEEP ON TRUCKING           BFG
         MVC  QDMSG(14),QBLANK       BLANK OUT THE FIRST PART       BFG
         MVC  QDMSG+14(66),93(R7)    MOVE REST OF MESSAGE TO SCREEN BFG
         MVC  QDMLNG,=H'80'          SET THE LENGTH                 BFG
         LA   R0,QDMSG               GET THE ADDR                   BFG
         ST   R0,QDMSGA              SET THE ADDR                   BFG
         L    R15,=V(DISPLAY)                                       BFG
         BALR R14,R15                DISPLAY THE LINE               BFG
LOOP1    EQU  *                                                     BFG
         LRA  R0,0(R7)               SEE IF ADDR EXISTS             BFG
         BNE  END                                                   BFG
         L    R7,0(,R7)             GET NEXT MESSAGE FROM OS        BFG
         B    LOOP                  AND GET THE NEXT LINE           BFG
END      EQU  *                                                     BFG
         MVC  QDMSG(80),BOTTOM       MESSAGE TO SCREEN              BFG
         MVC  QDMLNG,=H'80'          SET THE LENGTH                 BFG
         LA   R0,QDMSG               GET THE ADDR                   BFG
         ST   R0,QDMSGA              SET THE ADDR                   BFG
         L    R15,=V(DISPLAY)                                       BFG
         BALR R14,R15                DISPLAY THE LINE               BFG
         TM   FLAG1,F1AUTH          WERE WE AUTHORISED UPON CALL?   BFG
         BNO  ENDOFPGM              YES DON'T USE USVC              BFG
         L    R1,PSATOLD-PSA        GET TCB ADDRESS                 BFG
         L    R0,0(,R1)             GET RB ADDRESS                  BFG
         USVC RESTKEY               GET INTO KEY 8                  BFG
         NI   FLAG1,X'FF'-F1AUTH    TURN OFF THE FLAG               BFG
ENDOFPGM EQU  *                      CANCEL SPIE                    BFG
         QSTOP
TOP      DC   CL80'********************************* TOP OF DATA ******X
               ****************************'
BOTTOM   DC   CL80'******************************** BOTTOM OF DATA ****X
               ****************************'
         EJECT
         IHAPSA
         QCOMMON
         END
