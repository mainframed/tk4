         PRINT NOGEN
DISPLAY  QSTART 'Q5 - QUEUE COMMAND - 3270 DISPLAY ROUTINES'
         GBLB  &QPFK          PF-KEY OPTION. DEFINED BY QSTART     ICBC
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
         USING QCPRINT,R9     BASE REG FOR PRINT   WORK AREA       FCI*
         L     R9,QVPRINT     ADDR OF PRINT   WORK AREA            FCI*
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         LH    R4,QDMLNG      LOAD MSG LENGTH
         CH    R4,=H'1680'    IS THE MSG LENGTH MORE THAN 1680?
         BH    RETURN         YES. GO AWAY.
         MVC   QPRSAVE,QDTLINE        SAVE SUBTITLE LINE ON ENTRY  FCI*
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR         YES. WRITE SCREEN.
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         AH    R4,QDNEXT      ADD CURRENT LOCATION ON SCREEN
         CH    R4,=H'1680'    IS THERE ROOM ON THE SCREEN?
         BH    WRTSCR         NO. WRITE SCREEN.
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER
         LR    R1,R4          SAVE LINE NUMBER
         LA    R4,QDLINE1(R4) LOAD ADDRESS OF NEXT LINE
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE
         LA    R5,80          LENGTH MUST BE MULTIPLE OF 80
DSP3     CR    R5,R7          IS 5 NOT LESS THAN 7?
         BNL   DSP4           YES. GO DO IT.
         LA    R5,80(R5)      INCR BY 80
         B     DSP3           TRY AGAIN
DSP4     AR    R1,R5          UPDATE LINE NUMBER
         STH   R1,QDNEXT      STORE LINE NUMBER
         MVCL  R4,R6          MOVE THE MESSAGE TO THE SCREEN
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY                               *
*                                                                     *
***********************************************************************
WRTSCR   TR    QDLINE1(256),TABLE ENSURE PRINTABILITY
         TR    QDLINE1+256(256),TABLE
         TR    QDLINE1+512(256),TABLE
         TR    QDLINE1+768(256),TABLE
         TR    QDLINE1+1024(256),TABLE
         TR    QDLINE1+1280(256),TABLE
         TR    QDLINE1+1536(144),TABLE
TPUTSCRN EQU   *
         MVC   QATTRIB(2),=X'1DF0'    MOVE IN HEADER STUFF          BFG
         MVC   QCONST(2),=C'C='                                     BFG
         MVC   QDONST(3),=C' D='                                    BFG
         L     R1,16                  LOAD CVT ADDR                 BFG
         L     R1,604(R1)             LOAD ADDR OF RMCT             BFG
         L     R1,228(R1)             LOAD ADDR OF RCT              BFG
         LH    R15,64(R1)             LOAD RCTCPUA FIELD            BFG
         SRL   R15,4                  GET VALUE                     BFG
         CVD   R15,QDOUBLE                                          BFG
         UNPK  QCPU(3),QDOUBLE(8)                                   BFG
         OI    QCPU+2,X'F0'                                         BFG
         LH    R15,74(R1)                                           BFG
         CVD   R15,QDOUBLE                                          BFG
         UNPK  QDPR(3),QDOUBLE(8)                                   BFG
         OI    QDPR+2,X'F0'                                         BFG
         LA    R1,QDSCREEN    LOAD ADDRESS OF SCREEN BUFFER
         TPUT  QDSCREEN,QDSLNG,FULLSCR MF=(E,QTPUT)
         MVC   QDTLINE,QPRSAVE        RESTORE SUBTITLE INFO        FCI*
         AIF  (&QPFK).PFK1    SKIP NON-PFK CODE                    ICBC
         TGET  QDREPLY,63,EDIT,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?
         BNE   NOCLEAR        NO. CONTINUE.
         TCLEARQ INPUT        CLEAR THE QUEUE
NOCLEAR  STH   R1,QDRLNG      STORE LENGTH OF REPLY
         AGO   .PFK2                                               ICBC
.PFK1    ANOP                                                      ICBC
         LA    R6,QDREPLY                                          ICBC
         XC    PFREPLY,PFREPLY                                     ICBC
         XC    QDREPLY,QDREPLY                                     ICBC
         TGET  PFREPLY,69,ASIS,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?         ICBC
         BNE   NOCLEAR        NO. CONTINUE.                        ICBC
         TCLEARQ INPUT        CLEAR THE QUEUE                      ICBC
NOCLEAR  IC    R4,PFCODE                                           ICBC
         XR    R5,R5                                               ICBC
         N     R4,=X'0000000F'   EXTRACT PF-KEY NUMBER             ICBC
         CH    R4,=H'12'                                           ICBC
         BH    ENTKEY           "ENTER" KEY                        ICBC
         BCTR  R4,0                                                ICBC
         MH    R4,=H'5'                                            ICBC
         LA    R5,PFKTAB                                           ICBC
         LA    R5,0(R4,R5)                                         ICBC
         MVC   QDREPLY(5),0(R5)  MOVE PF-KEY VALUE                 ICBC
         LA    R5,5                                                ICBC
         LA    R6,3(,R6)                                           ICBC
ENTKEY   EQU   *                                                   ICBC
         SH    R1,=H'3'                                            ICBC
         BZ    NOTXT                                               ICBC
         MVC   0(60,R6),PFTXT                                      ICBC
         LA    R5,3                                                ICBC
         SH    R1,=H'3'                                            ICBC
NOTXT    LA    R1,0(R5,R1)                                         ICBC
         STH   R1,QDRLNG      STORE LENGTH OF REPLY                ICBC
.PFK2    ANOP                                                      ICBC
         OC    QDREPLY,QBLANK     UPPERCASE THE COMMAND            FCI*
         CLI   QDREPLY,C'$'       IS IT A JES2CMD?                  BFG
         BE    JES2CMD            YES, PROCESS IT                   BFG
         CLC   QDREPLY(2),=CL2'PR' POSSIBLE PRINT COMMAND      PWF FCI*
         BNE   CLSCRN             NOPE..SPLIT NORMALLY             FCI*
*                                                                  FCI*
         L     R15,=V(PRINT)  FETCH PRINT ENTRY ADDRESS            FCI*
         BALR  R14,R15        AND CALL HIM                         FCI*
         LTR   R15,R15        HOW IS HIS RETURN CODE               FCI*
         BNZ   CLSCRN         NOTHING TO REPORT                    FCI*
*                                                                  FCI*
         B     TPUTSCRN       GO REPOST SCREEN                     FCI*
CLSCRN   LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE           FCI*
         LA    R5,1680        LOAD LENGTH OF SCREEN                FCI*
         SR    R6,R6          NO SENDING FIELD NEEDED              FCI*
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER            FCI*
         SR    R7,R7          FILL SCREEN WITH NULLS               FCI*
         MVCL  R4,R6          CLEAR THE SCREEN                     FCI*
*
         LH    R1,QDRLNG      STORE LENGTH OF REPLY
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW
         B     DSP2           CONTINUE PROCESSING
INTER    EQU   *                                                    BFG
         TM    FLAG1,F1AUTH   ARE WE AUTHORIZED BY USVC?            BFG
         BNO   INTER1         NO, SKIP RESET                        BFG
         L     R1,PSATOLD-PSA LOAD ADDR OF TCB                      BFG
         L     R0,0(R1)       LOAD RB ADDR                          BFG
         USVC  RESTKEY                                              BFG
INTER1   L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
***********************************************************************
*                                                                     *
*   JES2CMDS THRU THE INTERNAL READER                                 *
*                                                                     *
***********************************************************************
JES2CMD  EQU   *                                                    BFG
         TM    QXAUTH,1       ARE WE AUTHORISED?                    BFG
         BNO   TPUTSCRN       NO, DON'T PROCESS THE COMMAND         BFG
         LA    R1,DYNINTR     LOAD ADDR OF INTERNAL READER PARMS    BFG
         TM    0(R1),X'80'    IS THE HIGH ORDER BIT ON?             BFG
         BNO   JES2CMD1       NO, DDCARD ALREADY ALLOCATED.         BFG
         DYNALLOC             DYNAMICALLY ALLOCATE THE DD CARD      BFG
         LTR   R15,R15        Q. DID IT ALLOCATE?                   BFG
         BZ    JES2CMD1       A. YES, SKIP QTILT                    BFG
         MVC   QDTLINE(31),=CL31'***DYNAMIC ALLOCATION FAILED***'   BFG
         B     JES2CMD4       AND RE-DISPLAY THE SCREEN             BFG
JES2CMD1 EQU   *                                                    BFG
         OPEN  (DCBINTR,OUTPUT),MF=(E,DYNOPEN)                      BFG
         LTR   R15,R15        Q. DID IT OPEN?                       BFG
         BZ    JES2CMD2       A. YES, SKIP QTILT                    BFG
         MVC   QDTLINE(17),=CL17'***OPEN FAILED***'                 BFG
         B     JES2CMD4       AND RE-DISPLAY THE SCREEN             BFG
JES2CMD2 EQU   *                                                    BFG
         MVI   DYNINTR,X'00'    DON'T ALLOCATE FILE AGAIN           BFG
         MVC   QINTCMD(80),QBLANK  BLANK OUT THE JES COMMAND AREA   BFG
         MVC   QINTCMD(2),=C'/*' PUT THE FIRST PART OF THE CMD      BFG
         MVI   QINTCMD+71,C'N'  DON'T DISPLAY ON MASTER CONSOLE     BFG
         MVC   QINTCMD+2(63),QDREPLY AND PUT IN THE REST OF IT      BFG
         PUT   DCBINTR,QINTCMD  ISSUE THE COMMAND                   BFG
         CLOSE DCBINTR,MF=(E,DYNCLOSE)                              BFG
JES2CMD4 EQU   *                                                    BFG
         XC    QDRLNG,QDRLNG  ZERO REPLY LENGTH                     BFG
         MVC   QDREPLY,QBLANK BLANK OUT THE FIELD                   BFG
         B     TPUTSCRN       AND RE-DISPLAY THE SCREEN             BFG
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
         AIF  (NOT &QPFK).PFK3                                     ICBC
* PF-KEY DEFINITIONS                                               ICBC
PFKTAB   DS    0CL60                                               ICBC
PF1      DC    CL5'H'                                              ICBC
PF2      DC    CL5'DA'                                       FCI*  ICBC
PF3      DC    CL5'E'                                              ICBC
PF4      DC    CL5'PRINT'                                    FCI*  ICBC
PF5      DC    CL5'F'                                              ICBC
PF6      DC    CL5'DI'                                             ICBC
PF7      DC    CL5'-  21'                                          ICBC
PF8      DC    CL5'+  21'                                          ICBC
PF9      DC    CL5'DO'                                             ICBC
PF10     DC    CL5'CO 1'                                           ICBC
PF11     DC    CL5'CO 41'                                          ICBC
PF12     DC    CL5'ST'                                             ICBC
.PFK3    ANOP                                                      ICBC
* TABLE OF PRINTABLE CHARACTERS
TABLE    DC    CL64' '
*     0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF0123456789ABCDEF
 DC C'          Ö.<(+×&&         !$*);^-/         ,%_>?          :#@'''
 DC C'="' CONTINUANCE OF PREVIOUS LINE BECAUSE OF DUPLICATION
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ                      '
 DC C' ABCDEFGHI       JKLMNOPQR        STUVWXYZ      0123456789      '
         QCOMMON
         IHAPSA
         END
