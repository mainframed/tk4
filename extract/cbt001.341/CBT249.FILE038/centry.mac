         MACRO
&NAME    CENTRY &BASE=12,&SIZE=72,&SAVE=
.*       CENTRY                        STANDARD LINKAGE ENTRY MACRO
.*                                     COMPANION MACRO: EXIT
.*       PARAMETERS:
.*                                     LABEL ON MACRO IS CSECT NAME
.*             BASE=                   BASE REGISTER(S)
.*                                       DEFAULT 12, MUST BE 3 - 12
.*                  N                    SINGLE BASE REGISTER
.*                  (N,N,...)            BASE REGISTER LIST
.*             SAVE=LABEL              SAVEAREA DEFINED IN PROGRAM
.*                                       18 FULLWORD REGISTER SAVE
.*               OR
.*             SIZE=N                  GETMAIN SAVEAREA SIZE &
.*                                       OPTIONAL WORKAREA (IF > 72)
.*                                       SAVEAREA IS FIRST 72 BYTES
.*                                       DEFAULT, MINIMUM 72
.*                                       FREED BY EXIT IF USED
.*             SIZE=LABEL              EQUATED SIZE OF SAVEAREA
         GBLA  &SVAREAM
         GBLC  &SVAREAE
         LCLC  &CSECT
         LCLC  &SVAREAN
         LCLA  &USER
         LCLA  &DISP
         LCLA  &COUNT
&SVAREAM SETA  72
&SVAREAE SETC  'SVAREAE'
&CSECT   SETC  'GO'
&USER    SETA  12
&DISP    SETA  0
&COUNT   SETA  1
         AIF   (T'&NAME EQ 'O').NONAME
&CSECT   SETC  '&NAME'
.NONAME  ANOP
&CSECT   CSECT
         SAVE  (14,12),,&NAME_&SYSDATE_&SYSTIME SAVE HIGHER LEVEL REGS
         AIF   (T'&BASE(1) NE 'N').BAS1ERR
         AIF   (&BASE(1) GE 3 AND &BASE(1) LE 12).SETBAS1
.BAS1ERR ANOP
         AIF   (N'&BASE GT 1).BASERR
         MNOTE 4,'    WARNING - BASE REGISTER NOT 3 - 12, 12 USED'
         AGO   .BASREG1
.SETBAS1 ANOP
&USER    SETA  &BASE(1)
.BASREG1 ANOP
         LR    &USER,15                SET BASE REGISTER
         USING &CSECT,&USER            INDICATE BASE REGISTER(S)
.USELOOP ANOP
&COUNT   SETA  &COUNT+1
&DISP    SETA  &DISP+4088
         AIF   (N'&BASE LT &COUNT).CHKSIZE
         AIF   (T'&BASE(&COUNT) NE 'N').BASERR
         AIF   (&BASE(&COUNT) GE 3 AND &BASE(&COUNT) LE 12).SETBASE
.BASERR  ANOP
         MNOTE 12,'    ERROR - BASE REGISTER NOT 3 - 12'
         MEXIT
.SETBASE ANOP
         LA    &BASE(&COUNT),4088(&BASE(&COUNT-1))
         USING &CSECT+&DISP,&BASE(&COUNT)
         AGO   .USELOOP
.CHKSIZE ANOP
         LR    2,1                     SAVE PARAMETER REGISTER
         AIF   (T'&SAVE NE 'O').SETSA
         AIF   (T'&SIZE EQ 'N').SIZELEN
&SVAREAM SETA  0
&SVAREAE SETC  '&SIZE'
         L     0,=A(&SVAREAE)          INDICATE SIZE
         AGO   .GETMAIN
.SETSA   ANOP
         LA    1,&SAVE
&SVAREAN SETC  '&SAVE'
&SVAREAE SETC  'SAVEAREA'
         AGO   .CHAINSA
.SIZELEN ANOP
         AIF   (&SIZE GE 72).SETSIZE
.SIZERR  ANOP
         MNOTE 4,'    WARNING - AREA SIZE INVALID, 72 USED'
         AGO   .GETSIZE
.SETSIZE ANOP
&SVAREAM SETA  &SIZE
.GETSIZE ANOP
         L     0,=F'&SVAREAM'          INDICATE SIZE
.GETMAIN ANOP
         BAL   1,G&SYSNDX              INDICATE GETMAIN
G&SYSNDX SVC   10                      ISSUE GETMAIN SVC
.CHAINSA ANOP
         ST    13,4(1)                 SAVE HIGHER SAVEAREA POINTER
         ST    1,8(13)                 SET POINTER THIS SAVEAREA
         LR    13,1                    SET SAVEAREA REGISTER
         LR    1,2                     RESTORE PARAMETER REGISTER
         MEND
