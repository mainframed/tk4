         MACRO
&NAME    EXIT  &RETCODE=,&SIZE=
.*       EXIT                          STANDARD LINKAGE EXIT MACRO
.*                                     COMPANION MACRO: CENTRY (ENTRY)
.*       PARAMETERS:
.*             SIZE=N                  SIZE OF AREA TO BE FREED
.*             SIZE=LABEL              EQUATED SIZE OF AREA TO BE FREED
.*                                     DO NOT USE IF CENTRY IS USED
.*                                       CENTRY GETMAIN SIZE USED
.*             RETCODE=                RETURN CODE SPECIFICATION
.*                                       DEFAULT 0
.*                     N               N = CODE
.*                     (R,N)           N = REGISTER CONTAINING CODE
.*                     LABEL           1 - 4 BYTE FIELD CONTAINING CODE
         GBLA  &SVAREAM
         GBLC  &SVAREAE
         AIF   ('&SVAREAE' EQ 'SAVEAREA').SA
         AIF   (&SVAREAM EQ 0).CHKSIZE
         AIF   (T'&SIZE EQ 'O').FREE
         MNOTE 4,'    COMMENT - SIZE IGNORED, CENTRY SIZE USED'
         AGO   .FREE
.CHKSIZE ANOP
         AIF   (T'&SIZE EQ 'N').SETSIZE
         AIF   (&SVAREAM EQ 0).LODSIZE
.NOSIZE  ANOP
         MNOTE 8,'    WARNING - NO SIZE SPECIFIED, NONE FREED'
.SA      ANOP
&NAME    L     13,4(13)                RESET REGISTER - HIGHER SAVEAREA
         AGO   .RETCODE
.LODSIZE ANOP
         AIF   ('&SVAREAE' EQ 'SAVEAREA').NOSIZE
&NAME    LR    1,13                    SET SAVEAREA POINTER
         L     13,4(13)                RESET REGISTER - HIGHER SAVEAREA
         L     0,=A(&SVAREAE)          INDICATE SIZE
         LA    1,0(0,1)                INDICATE FREEMAIN
         SVC   10                      ISSUE FREEMAIN SVC
         AGO   .RETCODE
.SETSIZE ANOP
&SVAREAM SETA  &SIZE
.FREE    ANOP
&NAME    LR    1,13                    SET SAVEAREA POINTER
         L     13,4(13)                RESET REGISTER - HIGHER SAVEAREA
         L     0,=F'&SVAREAM'          INDICATE SIZE
         LA    1,0(0,1)                INDICATE FREEMAIN
         SVC   10                      ISSUE FREEMAIN SVC
.RETCODE ANOP
         AIF   ('&RETCODE(1)' EQ 'R').SETCODE
         XC    16(4,13),16(13)         ZERO SAVEAREA RETURN CODE
         AIF   (T'&RETCODE EQ 'O').EXIT
         AIF   (T'&RETCODE EQ 'N').CHKCODE
         AIF   (L'&RETCODE GT 4).CODERR
         MVC   20-L'&RETCODE.(L'&RETCODE,13),&RETCODE       SET RETURN
         AGO   .EXIT
.SETCODE ANOP
         ST    &RETCODE(2),16(13)
         AGO   .EXIT
.CHKCODE ANOP
         AIF   (&RETCODE LT 0 OR &RETCODE GT 4095).CODERR
         LA    15,&RETCODE             SET SAVEAREA RETURN CODE
         ST    15,16(13)
         AGO   .EXIT
.CODERR  ANOP
         MNOTE 4,'    WARNING - INVALID RETCODE SPECIFIED, 0 USED'
.EXIT    ANOP
         LM    14,12,12(13)            RESTORE HIGHER LEVEL REGISTERS
         BR    14                      RETURN TO HIGHER LEVEL
&SVAREAM SETA  0
         MEND
