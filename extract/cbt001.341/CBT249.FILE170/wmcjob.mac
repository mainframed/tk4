WMC      TITLE ' WMCJOB TSO COMMAND  VERSION 2.0 '
*------------------------------------------------------------------*
*                                                                  *
* WMCJOB TSO COMMAND                                               *
* WRITTEN BY: DAN EWART                                            *
*             COMPUTER CENTER - JONES HALL                         *
*             THE COLLEGE OF WILLIAM AND MARY                      *
*             WILLIAMSBURG, VA.  23185                             *
*          PHONE (804) 253-4547                                    *
*------------------------------------------------------------------*
         MACRO
&BN      BEGIN &R,&LV=72,&REGS=YES,&RNAME=RETURN,&SP=0
         LCLC  &REG
&REG     SETC  '&REGS'
&BN      CSECT
*
***   ***   ***   ***   ***   ***   ***   ***   ***
*                                                 *
*      B E G I N   M A C R O                      *
* PARMS: R,LV=72,REGS=YES,RNAME=RETURN,SP=0       *
* R IS THE USER BASE REGISTER. IT SHOULD NOT BE   *
* ANY OF 0, 1, 13, 14, OR 15. THIS IS UP TO USER. *
* SETS UP REG EQUS IF REGS=YES (DEFAULT)  AND     *
* SETS UP SAVEAREA USING GETMAIN (LENGTH OF LV AND*
* SAVES R1 IN R14 TEMP).R0 CHANGED ON ENTRY.      *
* ALSO SETS UP RETURN LINKAGE AT SYMBOL OF        *
* RNAME USING FREEMAIN WITH USER RC IN R15.       *
* SP IS THE SUBPOOL NUMBER FOR THE GETMAINED AREA *
*  NOTE: REG EQUATES SHOULD BE DONE SOMEWHERE IN  *
*         THE ASSEMBLY SINCE THEY ARE USED IN THIS*
*         CODE. THE REGS PARM ALLOWS THE USER TO  *
*         PREVENT DUPLICATE SYMBOLS.  WITH THE LV *
*         PARM A USER MAY CREATE A WORKAREA       *
*         AND SUPPLY A USING STMT FOR R13 FOR IT. *
***   ***   ***   ***   ***   ***   ***   ***   ***
*
         AIF   ('&REGS' NE 'YES').NOR
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
.NOR     ANOP
         DS    0H
         USING &BN,R15 .          TEMP BASE REG
         B     BEGA&SYSNDX .      BRANCH OVER NAME CONST
         DC    XL1'08',CL8'&BN' . CONTROL SECTION NAME
         DS    0H .               ALIGN
BEGA&SYSNDX STM   R14,R12,12(R13) .  SAVE REGS IN HIGHER SA
         LR    &R,R15 .           SET REAL BASE REG
         DROP  R15 .              RID OF TEMP BASE
         USING &BN,&R .           SET REAL BASE TO ENTRY
         LR    R14,R1 .           SAVE PARM POINTER
         GETMAIN R,LV=&LV,SP=&SP .GET SAVEAREA + ANY WORK
         ST    R13,4(R1) .        @ HIGHER SA IN MINE
         ST    R1,8(R13) .        @ MY SA IN HIGHER SA
         LR    R13,R1 .           R13 BASE FOR SA + WORK
         LR    R1,R14 .           RESTORE PARM POINTER
         B     BEG&SYSNDX .       GO TO USER CODE
*
&RNAME   EQU   * .                USER NAMED RETURN CODE
         LR    R1,R13 .           @ MY SA
         L     R13,4(R1) .        @ HIGHER SA
         ST    R15,16(R13) .      TEMP STORE USER RC IN HIGHER SA
         FREEMAIN R,A=(1),LV=&LV,SP=&SP .
         LM    R14,R15,12(R13) .  RESTORE R14 AND RC IN R15
         LM    R0,R12,20(R13) .   RESTORE REGS 0-12
         BR    R14 .              RETURN CODE IN R15
*
BEG&SYSNDX EQU * .                START USER CODE
         EJECT
         MEND
GSTACK   BEGIN 12,LV=LWORK
*UPDATE  10/06/77  ----------------------------------------------------
*UPDATE  11/  /80 CONVERTED TO MVS - ALLOCATE USE INTRDR FOR SYSOUT
*        NOT RENT ***  LOCAL TSO COMMAND TO SUBMIT JOB FROM STACK
*
*        USES DAIR TO ALLOC A SYSOUT FILE FOR OUTPUT TO DDNAME STACKOUT
*
*        READS THE TSO STACK WITH THE GETLINE SERVICE ROUTINE
*        WRITES 80 BYTE RECORDS TO DDNAME STACKOUT DSORG=PS
*              DEFAULT BLKSIZE=80
*        STOPS AFTER READING A LINE WITH GSTACKSTOP AS THE 1ST 10 CHAR
*        OR AT THE END OF THE STACK. THEN FREES STACKOUT FILE TO
*        SYSOUT INTRDR. (SVS USED SYSOUT=C WITH AN OS WRITER TO
*        THE HASP INTRDR).  WMCJOB ADDS A /*EOF AS THE LAST
*        RECORD AND CHECKS THE FIRST FOR //# TO ENFORCE THE
*        LOCAL CONVENTION THAT SUBMITTED JOBS HAVE # AS THE
*        FIRST CHARACTER OF THE JOBNAME.
*
*----------------------------------------------------------------------
         LR    R2,R1               ADDRESS OF CPPL
         USING CPPL,R2
         USING WORKAREA,R13
         L     R3,CPPLUPT
         L     R4,CPPLECT
         LA    R5,MYECB
         L     R6,CPPLPSCB
         L     R7,=V(DAPL)
         STM   R3,R6,0(R7)    SETUP DAPL
         LA    R6,MYGTPB
         STM   R3,R6,IOPLA         SETUP IOPL
         L     R1,=V(ALIST)   SELECT DAPB
         CALL  DAIR
         LTR   R15,R15
         BNZ   RETURN
         OPEN  (OFILE,OUTPUT)
         TM    OFILE+48,X'10'  OPEN OK ?
         BNO   OER
         MVC   MYGTPB(8),MGTPB
*
         LOAD  EP=IKJGETL
*
         ST    R0,EPGL
GET      XC    MYECB,MYECB
         LA    R1,IOPLA
         L     R15,EPGL
         BALR  R14,R15
         CH    R15,=H'4'
         BH    ERROR
*
         L     R5,4(,R6)      GTPB BUFFER ADDRESS
         MVI   OAREA,X'40'
         MVC   OAREA+1(79),OAREA
         LH    R7,0(,R5)      LENGTH FROM 1ST HW OF BUFFER
         SH    R7,=H'5'      4 FOR RDW + 1 FOR EX
         BM    PUT           IF NULL CLIST LINE, PUT BLANK RECORD ???
         CH    R7,=H'79'   MAX CARD LENGTH
         BNH   XMVC        GO ON AND MOVE IT
         LH    R7,=H'79'   80 - 1 FOR EX IS MAX
XMVC     EX    R7,BMOVE      MVC OAREA(1),4(R5)
         CLC   OAREA(10),=CL10'GSTACKSTOP'
         BE    END
PUT      TM    FLG,X'80'      1ST CARD ?
         BO    OPUT           ALREADY HAD FIRST
         CLC   OAREA(3),=CL3'//#'   GOOD JOB NAME ?     (ONLY CHECK FOR
         BE    OPUT           IS OK.                     FIRST JOB).
         TPUT  BADJ,L'BADJ
         OI    FLG,X'40'      SHOW BAD
         B     CLOS
OPUT     OI    FLG,X'80'
         PUT   OFILE,OAREA
         LH    R8,0(,R5)     LENGTH BUFFER AREA
         O     R8,POOL      SET IN SUBPOOL NUMBER
         LR    R0,R8        AND GET POOL,LENGTH TO R0
         LR    R1,R5         BUFFER ADDRESS
         FREEMAIN R,LV=(0),A=(1)
         B     GET
*
OER      TPUT  OERM,L'OERM
         LA    R15,16
         B     RETURN
ERROR    EQU   *
END      TPUT  CMSG,L'CMSG
         PUT   OFILE,FINAL
CLOS     CLOSE (OFILE)
         L     R1,=V(FLIST)
         CALL  DAIR
         B     RETURN
*  OPEN EXIT FOR OFILE DCB  R1 -> DCB
         USING IHADCB,R1
OPENEXIT OC    DCBBLKSI,DCBBLKSI
         BNZ   0(R14)       RETURN TO OPEN IF NOT ZERO
         MVI   DCBBLKSI+1,80     DEFAULT BLKSIZE = 80
         BR    R14               RETURN TO OPEN
BMOVE    MVC   OAREA(1),4(R5)
OEXIT    DC    0F'0',X'85',AL3(OPENEXIT)
POOL     DC    X'01000000'
FLG      DC    X'00'
FINAL    DC    CL80'/*EOF'
CMSG     DC    C'JOB COMPLETE'
OERM     DC    C'OPEN ERROR FOR STACKOUT'
BADJ     DC   C'JOBNAME MISSING # AS 1ST CHARACTER - PROCESS ABENDING'
         EJECT
*        NOT RENT ***
*
OFILE    DCB   DSORG=PS,MACRF=PM,DDNAME=STACKOUT,RECFM=FB,LRECL=80,    X
               EXLST=OEXIT
*
MGTPB     GETLINE  MF=L
WORKAREA DSECT
REGS     DS    18F
MYECB    DS    F
EPGL     DS    F
MYGTPB   DS    D
IOPLA    DS    2D
OAREA    DS    CL80
*
LWORK    EQU   *-WORKAREA
         EJECT
         IKJCPPL
         SPACE 3
         IKJGTPB
         SPACE 3
         PRINT NOGEN
         DCBD  DSORG=PS
         PRINT GEN
         EJECT
ALIST    CSECT
D30CD    DC    XL2'0030'   ALLOC SYSOUT FILE
         DC    X'000000000000'  FLAGS,ERROR CODE, RESV
D30DSN   DC    A(DSNBUF)           @ FOR DSN BUFFER
D30DDNP  DC    CL8'STACKOUT'    DDNAME TO BE USED
D30UNIT  DC    CL8' '         ANY UNIT
D30VOL   DC    CL8' '         ANY VOL
D30BLK   DC    F'80'          80 BYTE BLOCKS
D30PQTY  DC    F'40'          40 BLOCKS PRIMARY SPACE
D30SQTY  DC    F'40'          40 BLOCKS SECONDARY
D30PGM   DC    CL8'INTRDR'    SYSOUT WTR NAME
D30FORM  DC    CL4' '         SPECIAL FORMS, NONE
D30CLS   DC    CL2'A '         ALLOC SYSOUT CLASS DEFAULT
         DC    X'00'          RESERVED
D30CTL   DC    X'58'          REC,RLSE,PERM
D30ALN   DC    CL8' '
         DS    0F
LEND30  EQU   *-D30CD
DSNBUF   DC    H'44'
         DC    CL44' '
         EJECT
FLIST    CSECT
D18CD    DC    XL2'0018'      FREE SYSOUT FILE LIST
         DC    X'000000000000'  FLAGS, ERROR CODE, CTRC
D18DSNP  DC    A(0)           @ DSN BUFFER
D18DDN   DC    CL8'STACKOUT'  DDNAME TO FREE
D18MNM   DC    CL8' '         MEMBER NONE
D18CLS   DC    C'C '          CLASS TO FREE TO FOR SYSOUT
D18DSP2  DC    X'08'          KEEP DISP 2
D18CTL   DC    X'10'          UNALLOC + NOT IN USE
D18JBNM  DC    CL8' '         USE TIOT FOR JOBNAME
         DS    0F
LEND18  EQU   *-D18CD
         EJECT
DAIR     BEGIN 12,REGS=NO,RNAME=ARETURN
         LR    R0,R1          DAPB ADDRESS
         L     R1,VDAPL
         ST    R0,16(R1)      SET DAPB @ IN DAPL
         LINK  EP=IKJEFD00    **  DAIR  **
         LTR   R15,R15
         BNZ   AERR
         B     ARETURN
AERR     LA    R2,AERMSG
         L     R3,AERL
         CVD   R15,WDWORD
         MVC   17(6,R2),ADMASK
         ED    17(6,R2),WDWORD+5
         TPUT  (2),(3)
         LA    R15,20
         B     ARETURN
AERMSG   DC    C'DAIR ERROR CODE =XXXXXX'
AERL     DC    F'23'
ADMASK    DC    X'402020202120'
WDWORD   DC    D'0'
VDAPL    DC    V(DAPL)
         SPACE 3
DAPL     CSECT
         DC    5F'0'
         SPACE 3
         END   GSTACK
