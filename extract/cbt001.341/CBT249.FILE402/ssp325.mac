       ID DIVISION.
       PROGRAM-ID.         SSP325.
       AUTHOR.
                 RON KOHL.
       REMARKS.
                 THIS PROGRAM IS USED TO ADD, CHANGE OR DELETE
                 DATA SET MANAGEMENT RECORDS.  THE USER IS PROMPTED
                 FOR A DATA SET NAME.  IF THE RECORD EXISTS ON THE VSAM
                 FILE, THE MAP IS SENT ASSUMING THAT A CHANGE WILL
                 OCCUR.  THE USER MAY CHANGE THE FUNCTION TO DELETE
                 IF DESIRED.  IF THE RECORD IS NOT FOUND, AN ADD IS
                 ASSUMED.  IF AN ASTERISK IS FOUND IN THE DSN FIELD,
                 THE DSN IS PASSED IN THE COMMAREA TO SSP326 FOR PRO-
                 CESSING AS A GENERIC BROWSE.  IN THAT PROGRAM, THE
                 USER MAY SELECT A RECORD FOR PROCESSING BY THIS PRO-
                 GRAM.
       SKIP1
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-370.
       OBJECT-COMPUTER. IBM-370.
       DATA DIVISION.
       WORKING-STORAGE SECTION.

       01  WS-BLANK        VALUE ' '          PIC X.

       01  ERROR-SW                           PIC X.

       01  WS-COMMAREA                        PIC X(44).

       01  DSN-OCCURS.
           03  DSN-EACH-BYTE  OCCURS 44 TIMES PIC X.

       01  SUB                      COMP      PIC S9(4).

       01  DEVICE-WORK              PIC X(4).
           88  VALID-DEVICE
               VALUE '3340' '3350' '3375' '3380' 'TAPE'.

       01  STRUCTURE-WORK           PIC X(4).
           88  VALID-STRUCTURE
               VALUE 'SEQ ' 'VSAM' 'BDAM' 'PDS ' 'IMS '.

       01  RECFM-WORK               PIC X(3).
           88  VALID-RECFM
               VALUE 'F  ' 'FB ' 'FBA' 'FBM' 'V  ' 'VB '.
           88  BLOCKED-RECFM
               VALUE 'FB ' 'FBA' 'FBM' 'VB '.

       01  DASD-PARAMETERS.
           03  3340-WORK.
             05  3340-BYTES-PER-TRACK  COMP-3  VALUE +8535  PIC S9(5).
             05  3340-OVHD-PER-BLOCK   COMP-3  VALUE +167   PIC S9(5).
             05  3340-OVHD-PER-SEGMENT COMP-3  VALUE +0     PIC S9(5).
             05  3340-SEGMENT-DIVISOR  COMP-3  VALUE +1     PIC S9(5).

           03  3350-WORK.
             05  3350-BYTES-PER-TRACK  COMP-3  VALUE +19254 PIC S9(5).
             05  3350-OVHD-PER-BLOCK   COMP-3  VALUE +185   PIC S9(5).
             05  3350-OVHD-PER-SEGMENT COMP-3  VALUE +0     PIC S9(5).
             05  3350-SEGMENT-DIVISOR  COMP-3  VALUE +1     PIC S9(5).

           03  3375-WORK.
             05  3375-SEGMENTS-PER-TRK COMP-3  VALUE +1125  PIC S9(5).
             05  3375-OVHD-PER-BLOCK   COMP-3  VALUE +12    PIC S9(5).
             05  3375-OVHD-PER-SEGMENT COMP-3  VALUE +12    PIC S9(5).
             05  3375-SEGMENT-DIVISOR  COMP-3  VALUE +32    PIC S9(5).

           03  3380-WORK.
             05  3380-SEGMENTS-PER-TRK COMP-3  VALUE +1499  PIC S9(5).
             05  3380-OVHD-PER-BLOCK   COMP-3  VALUE +15    PIC S9(5).
             05  3380-OVHD-PER-SEGMENT COMP-3  VALUE +12    PIC S9(5).
             05  3380-SEGMENT-DIVISOR  COMP-3  VALUE +32    PIC S9(5).

           03  DASD-WORK.
             05  DASD-BYTES-PER-TRACK  COMP-3               PIC S9(5).
             05  DASD-OVHD-PER-BLOCK   COMP-3               PIC S9(5).
             05  DASD-OVHD-PER-SEGMENT COMP-3               PIC S9(5).
             05  DASD-SEGMENT-DIVISOR  COMP-3               PIC S9(5).
             05  DASD-NUM-RECS         COMP-3               PIC S9(7).
             05  DASD-TOTAL-BLOCKS     COMP-3               PIC S9(7).
             05  DASD-RECS-PER-BLOCK   COMP-3               PIC S9(5).
             05  DASD-REC-LENGTH       COMP-3               PIC S9(5).
             05  DASD-BLKSIZE          COMP-3               PIC S9(5).
             05  DASD-TOT-SEGMENTS     COMP-3               PIC S9(5).
             05  DASD-BLKS-PER-TRK     COMP-3               PIC S9(5).
             05  DASD-TOT-TRKS         COMP-3               PIC S9(5).

       01  TODAY-MMDDYY            PIC X(6).

       01  DATE-AREA             COPY SSP301C1.

       01  DSM-RECORD.
           03  DSM-KEY               PIC X(44).
      *        VSAM KEY IS DATASET NAME
           03  DSM-CREATE-DATE       PIC 9(6).
           03  DSM-CHG-DATE          PIC 9(6).
           03  DSM-STRUCTURE         PIC X(4).
           03  DSM-SIZE              PIC 9(5).
           03  DSM-GROWTH            PIC 9(3).
           03  DSM-GDG               PIC 9(2).
           03  DSM-DEV-TYPE          PIC X(4).
           03  DSM-VOLSER            PIC X(6).
           03  DSM-RETAIN-DATE       PIC 9(6).
           03  DSM-RECFM             PIC X(3).
           03  DSM-LRECL             PIC 9(5).
           03  DSM-BLKSIZE           PIC 9(5).
           03  DSM-NUM-RECS          PIC 9(7).
           03  DSM-DESCR             PIC X(80).
           03  FILLER                PIC X(114).

       01  SSP325M               COPY SSP325M.

       01  SSP303-COMMAREA-DEF   COPY SSP303C1.

       01  ATTRIBUTES            COPY ATTRBYTE.

       01  DFHAID                COPY DFHAID.
           EJECT
       LINKAGE SECTION.

       01  DFHCOMMAREA             PIC X(44).
       PROCEDURE DIVISION.

       000-HANDLE.
           EXEC CICS
               HANDLE CONDITION MAPFAIL(920-END)
                                ERROR(910-ERROR)
           END-EXEC.
           EXEC CICS
               HANDLE AID CLEAR(920-END)
           END-EXEC.

       100-CALEN.
           IF EIBCALEN = 0
               MOVE LOW-VALUES TO SSP325M
               EXEC CICS
                   SEND MAP('SSP325M') FROM(SSP325M) ERASE FREEKB
               END-EXEC
               EXEC CICS
                   RETURN TRANSID('SSP2')
                          COMMAREA(WS-COMMAREA)
                          LENGTH(1)
               END-EXEC.
      ****************************************************************
      *  A CALEN OF 44 MEANS A DSN HAS BEEN PASSED FROM SSP326
      ****************************************************************
           IF EIBCALEN = 44
               MOVE DFHCOMMAREA TO DSNI
               GO TO 140-GET-RECORD.

       120-RECEIVE-MAP.
           EXEC CICS
               RECEIVE MAP('SSP325M') INTO(SSP325M)
           END-EXEC.

       130-EDIT-DSN.
           IF DSNI = SPACES OR LOW-VALUES
               GO TO 920-END.
           MOVE DSNI TO DSN-OCCURS.
      ****************************************************************
      *  AN ASTERISK IN THE DSN FIELD INDICATES THAT THE USER IS RE-
      *  QUESTING A GENERIC SEARCH.  IF AN ASTERISK IS FOUND THE DSN
      *  IS STRIPPED OF THE ASTERISK AND CONTROL IS PASSED TO SSP326
      ****************************************************************
           PERFORM 135-CHK-FOR-ASTERISK
               VARYING SUB FROM 1 BY 1 UNTIL SUB > 44.
      ****************************************************************
      *  A SUB VALUE > 48 (ACTUALLY 50) MEANS AN ASTERISK WAS FOUND
      ****************************************************************
           IF SUB > 48
               MOVE DSN-OCCURS TO WS-COMMAREA
               EXEC CICS
                   XCTL PROGRAM('SSP326')
                        COMMAREA(WS-COMMAREA)
                        LENGTH(44)
               END-EXEC.
           GO TO 140-GET-RECORD.

       135-CHK-FOR-ASTERISK.
           IF DSN-EACH-BYTE (SUB) = '*'
               MOVE LOW-VALUES TO DSN-EACH-BYTE (SUB)
               MOVE 50 TO SUB.

       140-GET-RECORD.
           CALL 'SSP330' USING EIBDATE DATE-AREA.
           MOVE GREGORIAN-MMDDYY TO TODAY-MMDDYY.
           EXEC CICS
               HANDLE CONDITION NOTFND(300-RECORD-ADD)
           END-EXEC.
           EXEC CICS
               READ INTO(DSM-RECORD)
                    DATASET('SSPDSM') RIDFLD(DSNI) UPDATE
           END-EXEC.

       145-DETERMINE-PROCESS.
           IF FUNCTNI = 'DEL'
               GO TO 600-RECORD-DELETE.
      ****************************************************************
      *  A CALEN OF 2 MEANS THAT THE RECORD HAS ALREADY BEEN DISPLAYED
      ****************************************************************
           IF EIBCALEN = 2
               GO TO 400-RECORD-CHANGE.

       200-FORMAT-SCREEN.
           MOVE LOW-VALUES TO SSP325M.
           MOVE 'CHG' TO FUNCTNO.
           MOVE DSM-KEY TO DSNO.
           MOVE DSM-CREATE-DATE TO CREDATO.
           MOVE DSM-CHG-DATE TO CHGDATO.
           MOVE DSM-STRUCTURE TO STRUCTRO.
           MOVE DSM-SIZE TO SIZEO.
           MOVE DSM-GROWTH TO GROWTHO.
           MOVE DSM-GDG TO GDGO.
           MOVE DSM-DEV-TYPE TO DEVICEO.
           MOVE DSM-VOLSER TO VOLSERO.
           MOVE DSM-RECFM TO RECFMO.
           MOVE DSM-LRECL TO LRECLO.
           MOVE DSM-BLKSIZE TO BLKO.
           MOVE DSM-NUM-RECS TO NRECO.
           MOVE DSM-DESCR TO DESCO.
           MOVE DSM-RETAIN-DATE TO RDATEO.
           EXEC CICS
               SEND MAP('SSP325M') FROM(SSP325M) ERASE FREEKB
           END-EXEC.
           EXEC CICS
               UNLOCK DATASET('SSPDSM')
           END-EXEC.
           EXEC CICS
               RETURN TRANSID('SSP2')
                      COMMAREA(WS-COMMAREA)
                      LENGTH(2)
           END-EXEC.

       300-RECORD-ADD.
           PERFORM 450-EDIT-DATA.
           PERFORM 500-COMPUTE-FILE-SIZE THRU 500-EXIT.
           IF ERROR-SW = '1'
               MOVE ATTR-PROT-DRK-OFF TO MSGA
               MOVE 'HIGHLIGHTED FIELDS ARE IN ERROR' TO ERRLNO
               EXEC CICS
                   SEND MAP('SSP325M') FROM(SSP325M) DATAONLY FREEKB
               END-EXEC
               EXEC CICS
                   RETURN TRANSID('SSP2')
                   COMMAREA(WS-COMMAREA)
                   LENGTH(1)
               END-EXEC.
           MOVE SPACES TO DSM-RECORD.
           MOVE DSNI TO DSM-KEY.
           MOVE TODAY-MMDDYY TO DSM-CHG-DATE DSM-CREATE-DATE.
           MOVE STRUCTRI TO DSM-STRUCTURE.
           MOVE SIZEI TO DSM-SIZE.
           MOVE GROWTHI TO DSM-GROWTH.
           MOVE GDGI TO DSM-GDG.
           MOVE DEVICEI TO DSM-DEV-TYPE.
           MOVE VOLSERI TO DSM-VOLSER.
           MOVE RECFMI TO DSM-RECFM.
           MOVE LRECLI TO DSM-LRECL.
           MOVE BLKI TO DSM-BLKSIZE.
           MOVE NRECI TO DSM-NUM-RECS.
           MOVE DESCI TO DSM-DESCR.
           MOVE RDATEI TO DSM-RETAIN-DATE.
           EXEC CICS
               WRITE FROM(DSM-RECORD)
                     DATASET('SSPDSM') RIDFLD(DSM-KEY)
           END-EXEC.
           MOVE 'RECORD ADDED====>' TO MSGO.
           MOVE DSM-KEY TO ERRLNO.
           MOVE ATTR-PROT-HIGH-OFF TO MSGA.
           MOVE ATTR-PROT-HIGH-OFF TO ERRLNA.
           MOVE ATTR-UNPROT-ON TO DSNA.
           MOVE ATTR-UNPROT-ON TO STRUCTRA.
           MOVE TODAY-MMDDYY TO CREDATO.
           MOVE ATTR-PROT-OFF TO CREDATA.
           MOVE ATTR-UNPROT-ON TO SIZEA.
           MOVE ATTR-PROT-DRK-OFF TO CHGDATA.
           MOVE ATTR-UNPROT-ON TO GROWTHA.
           MOVE ATTR-UNPROT-ON TO GDGA.
           MOVE ATTR-UNPROT-ON TO DEVICEA.
           MOVE ATTR-UNPROT-ON TO VOLSERA.
           MOVE ATTR-UNPROT-ON TO RECFMA.
           MOVE ATTR-UNPROT-ON TO LRECLA.
           MOVE ATTR-UNPROT-ON TO BLKA.
           MOVE ATTR-UNPROT-ON TO NRECA.
           MOVE ATTR-UNPROT-ON TO DESCA.
           MOVE ATTR-UNPROT-ON TO RDATEA.
           MOVE ATTR-PROT-HIGH-ON TO FUNCTNA.
           MOVE 'ADD' TO FUNCTNO.
           EXEC CICS
               SEND MAP('SSP325M') FROM(SSP325M) DATAONLY FREEKB
           END-EXEC.
           EXEC CICS
               RETURN TRANSID('SSP2')
                      COMMAREA(WS-COMMAREA)
                      LENGTH(1)
           END-EXEC.

       400-RECORD-CHANGE.
           PERFORM 450-EDIT-DATA.
           PERFORM 500-COMPUTE-FILE-SIZE THRU 500-EXIT.
           IF ERROR-SW = '1'
               MOVE ATTR-PROT-DRK-OFF TO MSGA
               MOVE 'HIGHLIGHTED FIELDS ARE IN ERROR' TO ERRLNO
               EXEC CICS
                   SEND MAP('SSP325M') FROM(SSP325M) DATAONLY FREEKB
               END-EXEC
               EXEC CICS
                   UNLOCK DATASET('SSPDSM')
               END-EXEC
               EXEC CICS
                   RETURN TRANSID('SSP2')
                   COMMAREA(WS-COMMAREA)
                   LENGTH(2)
               END-EXEC.
           MOVE DSNI TO DSM-KEY.
           MOVE TODAY-MMDDYY TO DSM-CHG-DATE.
           MOVE STRUCTRI TO DSM-STRUCTURE.
           MOVE SIZEI TO DSM-SIZE.
           MOVE GROWTHI TO DSM-GROWTH.
           MOVE GDGI TO DSM-GDG.
           MOVE DEVICEI TO DSM-DEV-TYPE.
           MOVE VOLSERI TO DSM-VOLSER.
           MOVE RECFMI TO DSM-RECFM.
           MOVE LRECLI TO DSM-LRECL.
           MOVE BLKI TO DSM-BLKSIZE.
           MOVE NRECI TO DSM-NUM-RECS.
           MOVE DESCI TO DSM-DESCR.
           MOVE RDATEI TO DSM-RETAIN-DATE.
           EXEC CICS
               REWRITE FROM(DSM-RECORD)
                       DATASET('SSPDSM')
           END-EXEC.
           MOVE LOW-VALUES TO SSP325M.
           MOVE 'RECORD CHANGED==>' TO MSGO.
           MOVE DSM-KEY TO DSNO ERRLNO.
           GO TO 800-SEND-AND-RETURN.

       450-EDIT-DATA.
           MOVE '0' TO ERROR-SW.
           MOVE ATTR-UNPROT-ON TO DSNA.
           MOVE STRUCTRI TO STRUCTURE-WORK.
           IF STRUCTRI = SPACES OR LOW-VALUES OR NOT VALID-STRUCTURE
               MOVE '****' TO STRUCTRO
               MOVE ATTR-UNPROT-HIGH-OFF TO STRUCTRA
               MOVE '1' TO ERROR-SW
           ELSE
               MOVE ATTR-UNPROT-ON TO STRUCTRA.
           IF STRUCTRI = 'VSAM' OR = 'IMS'
               IF SIZEI NOT NUMERIC
                   MOVE 0 TO SIZEO
                   MOVE ATTR-UNPROT-HIGH-OFF TO SIZEA
                   MOVE '1' TO ERROR-SW
               ELSE
                   MOVE ATTR-UNPROT-ON TO SIZEA.
           IF GROWTHI NOT NUMERIC
               MOVE 0 TO GROWTHI
               MOVE ATTR-UNPROT-ON TO GROWTHA.
           IF GDGI NOT NUMERIC
               MOVE 0 TO GDGI
               MOVE ATTR-UNPROT-ON TO GDGA.
           MOVE DEVICEI TO DEVICE-WORK.
           IF DEVICEI = SPACES OR LOW-VALUES OR NOT VALID-DEVICE
               MOVE '****' TO DEVICEO
               MOVE ATTR-UNPROT-HIGH-OFF TO DEVICEA
               MOVE '1' TO ERROR-SW
           ELSE
               MOVE ATTR-UNPROT-ON TO DEVICEA.
           IF DEVICEI = 'TAPE'
               MOVE SPACES TO VOLSERI
           ELSE
           IF VOLSERI = SPACES OR LOW-VALUES
               MOVE 'SYSPD ' TO VOLSERI
               MOVE ATTR-UNPROT-ON TO VOLSERA
           ELSE
               MOVE ATTR-UNPROT-ON TO VOLSERA.
           IF STRUCTRI = 'VSAM' OR = 'IMS'
               MOVE 'AMS' TO RECFMO
           ELSE
               IF RECFMI = SPACES OR LOW-VALUES
                   MOVE 'FB ' TO RECFMO RECFM-WORK
               ELSE
                   MOVE RECFMI TO RECFM-WORK
                   IF NOT VALID-RECFM
                       MOVE '***' TO RECFMO
                       MOVE ATTR-UNPROT-HIGH-OFF TO RECFMA
                       MOVE '1' TO ERROR-SW
                   ELSE
                       MOVE ATTR-UNPROT-ON TO RECFMA.
           IF LRECLI NOT NUMERIC OR LRECLI < 1
               MOVE 0 TO LRECLO
               MOVE ATTR-UNPROT-HIGH-OFF TO LRECLA
               MOVE '1' TO ERROR-SW
           ELSE
               MOVE ATTR-UNPROT-ON TO LRECLA.
           IF BLKI NOT NUMERIC OR BLKI < 1
               IF STRUCTRI = 'VSAM' OR = 'IMS'
                   MOVE 4096 TO BLKO
               ELSE
                   MOVE 0 TO BLKO
                   MOVE ATTR-UNPROT-HIGH-OFF TO BLKA
                   MOVE '1' TO ERROR-SW
           ELSE
               MOVE ATTR-UNPROT-ON TO BLKA.
           IF LRECLI NUMERIC AND BLKI NUMERIC
               IF LRECLI > BLKI
                   MOVE ATTR-UNPROT-HIGH-ON TO LRECLA
                   MOVE ATTR-UNPROT-HIGH-ON TO BLKA
                   MOVE '1' TO ERROR-SW
               ELSE
               IF BLOCKED-RECFM OR STRUCTRI = 'VSAM' OR = 'IMS'
                   IF BLKI NOT > LRECLI
                       MOVE ATTR-UNPROT-HIGH-ON TO RECFMA
                       MOVE ATTR-UNPROT-HIGH-ON TO LRECLA
                       MOVE ATTR-UNPROT-HIGH-ON TO BLKA
                       MOVE '1' TO ERROR-SW
                   ELSE
                       NEXT SENTENCE
               ELSE
                   IF BLKI NOT = LRECLI
                       MOVE ATTR-UNPROT-HIGH-ON TO RECFMA
                       MOVE ATTR-UNPROT-HIGH-ON TO LRECLA
                       MOVE ATTR-UNPROT-HIGH-ON TO BLKA
                       MOVE '1' TO ERROR-SW.
           IF NRECI NOT NUMERIC OR NRECI < 1
               MOVE 0 TO NRECO
               MOVE ATTR-UNPROT-HIGH-OFF TO NRECA
               MOVE '1' TO ERROR-SW
           ELSE
               MOVE ATTR-UNPROT-ON TO NRECA.
           IF DESCI = SPACES OR LOW-VALUES
               MOVE 'NONE' TO DESCO
           ELSE
               MOVE ATTR-UNPROT-ON TO DESCA.
            IF RDATEI NOT NUMERIC
                MOVE 999999 TO RDATEI.
            IF RDATEI NOT = 999999
                MOVE SPACES TO DATE-AREA
                MOVE 0 TO INCREMENT-MONTHS INCREMENT-DAYS
                MOVE RDATEI TO GREGORIAN-MMDDYY
                CALL 'SSP301' USING DATE-AREA
                IF ERROR-CODE NOT = '00'
                    MOVE ATTR-UNPROT-HIGH-OFF TO RDATEA
                    MOVE '1' TO ERROR-SW
               ELSE
                   MOVE ATTR-UNPROT-ON TO RDATEA.

       500-COMPUTE-FILE-SIZE.
           IF ERROR-SW = '1'
               GO TO 500-EXIT.
           IF STRUCTRI = 'VSAM' OR = 'IMS' OR DEVICEI = 'TAPE'
               GO TO 500-EXIT.
           IF DEVICEI = '3340'
               MOVE 3340-WORK TO DASD-WORK
           ELSE
           IF DEVICEI = '3350'
               MOVE 3350-WORK TO DASD-WORK
           ELSE
           IF DEVICEI = '3375'
               MOVE 3375-WORK TO DASD-WORK
           ELSE
           IF DEVICEI = '3380'
               MOVE 3380-WORK TO DASD-WORK
           ELSE
               MOVE 0 TO SIZEI
               GO TO 500-EXIT.
           MOVE NRECI TO DASD-NUM-RECS.
           MOVE BLKI TO DASD-BLKSIZE.
           MOVE LRECLI TO DASD-REC-LENGTH.
           COMPUTE DASD-RECS-PER-BLOCK =
               DASD-BLKSIZE / DASD-REC-LENGTH.
           COMPUTE DASD-TOTAL-BLOCKS =
               DASD-NUM-RECS / DASD-RECS-PER-BLOCK.
           COMPUTE DASD-TOT-SEGMENTS =
             (DASD-BLKSIZE + DASD-OVHD-PER-BLOCK
              + (DASD-SEGMENT-DIVISOR - 1)) /
              DASD-SEGMENT-DIVISOR.
           COMPUTE DASD-BLKS-PER-TRK =
               DASD-BYTES-PER-TRACK /
               (DASD-OVHD-PER-SEGMENT + DASD-TOT-SEGMENTS).
           IF DASD-BLKS-PER-TRK < 1
               MOVE ATTR-UNPROT-HIGH-ON TO DEVICEA
               MOVE ATTR-UNPROT-HIGH-ON TO BLKA
               MOVE '1' TO ERROR-SW
           ELSE
               COMPUTE DASD-TOT-TRKS =
                   (DASD-TOTAL-BLOCKS / DASD-BLKS-PER-TRK) + 1
               MOVE DASD-TOT-TRKS TO SIZEI.
       500-EXIT.
           EXIT.

       600-RECORD-DELETE.
           EXEC CICS
               DELETE DATASET('SSPDSM')
           END-EXEC.
           MOVE LOW-VALUES TO SSP325M.
           MOVE 'RECORD DELETED==>' TO MSGO.
           MOVE DSM-KEY TO ERRLNO.
           GO TO 800-SEND-AND-RETURN.

       800-SEND-AND-RETURN.
           EXEC CICS
               SEND MAP('SSP325M') FROM(SSP325M) ERASE FREEKB
           END-EXEC.
           EXEC CICS
               RETURN TRANSID('SSP2')
                      COMMAREA(WS-COMMAREA)
                      LENGTH(1)
           END-EXEC.

       910-ERROR.
           MOVE DFHEIBLK TO SSP303-DFHEIBLK.
           MOVE 'SSP325' TO SSP303-PROGRAM.
           EXEC CICS
               XCTL PROGRAM('SSP303')
                    COMMAREA(SSP303-COMMAREA)
                    LENGTH(SSP303-COMMAREA-LENGTH)
           END-EXEC.

       920-END.
           EXEC CICS
               SEND FROM(WS-BLANK) LENGTH(0) ERASE
           END-EXEC.
           EXEC CICS
               RETURN
           END-EXEC.
           GOBACK.
