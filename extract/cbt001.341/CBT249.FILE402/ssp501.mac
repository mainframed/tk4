000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.   SSP501.
000040 INSTALLATION. MOA.
000050     SKIP1
000060 ENVIRONMENT DIVISION.
000070 CONFIGURATION SECTION.
000080 SOURCE-COMPUTER.     IBM-370.
000090 OBJECT-COMPUTER.     IBM-370.
000100 SPECIAL-NAMES.
000110     C01 IS T-O-P.
000120     SKIP1
000130 INPUT-OUTPUT SECTION.
000140 FILE-CONTROL.
000150     SELECT EXTRACT-INPUT-FILE
000160         ASSIGN TO UT-S-IEXTRACT.
000170     SELECT DMS003-REPORT
000180         ASSIGN TO UT-S-ORPT001.
000190     SELECT SORT-FILE
000200         ASSIGN TO UT-S-SORTWK01.
000210     EJECT
000220 DATA DIVISION.
000230 FILE SECTION.
000240 FD  EXTRACT-INPUT-FILE
000250     RECORDING MODE IS F
000260     LABEL RECORDS ARE STANDARD
000270     RECORD CONTAINS 105 CHARACTERS
000280     BLOCK CONTAINS 0 RECORDS
000290     DATA RECORD IS EXTRACT-RECORD.
000300     SKIP1
000310 01  EXTRACT-RECORD              PIC X(105).
000320     SKIP2
000330 FD  DMS003-REPORT
000340     RECORDING MODE IS F
000350     LABEL RECORDS ARE STANDARD
000360     RECORD CONTAINS 133 CHARACTERS
000370     BLOCK CONTAINS 0 RECORDS
000380     DATA RECORD IS REPORT-RECORD.
000390     SKIP1
000400 01  REPORT-RECORD               PIC X(133).
000410     SKIP1
000420 SD  SORT-FILE
000430     RECORDING MODE IS F
000440     LABEL RECORDS ARE STANDARD
000450     RECORD CONTAINS 105 CHARACTERS
000460     BLOCK CONTAINS 0 RECORDS
000470     DATA RECORD IS SORT-REC.
000480     SKIP1
000490 01  SORT-REC.
000500     05  SORT-DSNAME             PIC X(44).
000510     05  FILLER  PIC X(61).
000520     EJECT
000530 WORKING-STORAGE SECTION.
000540 77  WS-PCT-FREE                 PIC S9(2)V999   COMP-3.
000550 77  LINE-COUNT                  PIC S9(3) COMP-3 VALUE +99.
000560 77  PAGE-COUNT                  PIC S9(3) COMP-3 VALUE ZEROS.
000570     SKIP1
000580 01  WS-EXT-RECORD.
000590     05  EXTRACT-DSN             PIC  X(44).
000600     05  EXTRACT-CREATE-DATE     PIC S9(7)  COMP-3.
000610     05  EXTRACT-EXPIR-DATE      PIC S9(7)  COMP-3.
000620     05  EXTRACT-NUM-EXTENTS     PIC  9(4)  COMP.
000630     05  EXTRACT-DSORG           PIC  X(2).
000640     05  EXTRACT-RECFM           PIC  X(3).
000650     05  EXTRACT-LRECL           PIC  9(4)  COMP.
000660     05  EXTRACT-BLKSIZE         PIC  9(4)  COMP.
000670     05  EXTRACT-KEYLEN          PIC  9(4)  COMP.
000680     05  EXTRACT-RKP             PIC  9(4)  COMP.
000690     05  EXTRACT-ALLOC-PRM       PIC  X(3).
000700     05  EXTRACT-RES-VOLSER      PIC  X(6).
000710     05  EXTRACT-RES-PACK-TYP    PIC  X(4).
000720     05  EXTRACT-SPACE-ALLOC     PIC S9(9)  COMP-3.
000730     05  EXTRACT-SPACE-USED      PIC S9(9)  COMP-3.
000740     05  EXTRACT-CVOL            PIC  X(6).
000750     05  EXTRACT-CAT-VOL         PIC  X(6).
000760     05  FILLER                  PIC  X(3).
000770 01  HOLD-DATE.
000780     05  HOLD-DATE-NUM           PIC 9(7).
000790     05  HOLD-DATE-RED  REDEFINES  HOLD-DATE-NUM.
000800         10  FILLER              PIC X.
000810         10  HOLD-DATE-MM        PIC 9(2).
000820         10  HOLD-DATE-DD        PIC 9(2).
000830         10  HOLD-DATE-YY        PIC 9(2).
000840     SKIP2
000850 01  PRINT-LINES.
000860     SKIP1
000870     05  HEADING-1.
000880         10  FILLER              PIC X(10)       VALUE SPACES.
000890         10  FILLER              PIC X(6)
000900                 VALUE 'PAGE: '.
000910         10  HDG1-PAGE-NO        PIC Z9.
000920         10  FILLER              PIC X(29)       VALUE SPACES.
000930         10  FILLER              PIC X(37)
000940                 VALUE 'F O R M A T T E D   L I S T   V T O C'.
000950         10  FILLER              PIC X(29)       VALUE SPACES.
000960         10  FILLER              PIC X(6)
000970                 VALUE 'DATE: '.
000980         10  HDG1-DATE           PIC X(8).
000990         10  FILLER              PIC X(5)        VALUE SPACES.
001000     SKIP1
001010     05 HEADING-2.
001020         10  FILLER              PIC X(59)       VALUE SPACES.
001030         10  FILLER              PIC X(8)
001040                 VALUE 'VOLUME: '.
001050         10  HDG2-VOLUME         PIC X(6).
001060         10  FILLER              PIC X(60)       VALUE SPACES.
001070     SKIP1
001080     05  DETAIL-1.
001090         10  FILLER              PIC X(4)        VALUE SPACES.
001100         10  FILLER              PIC X(5)
001110                 VALUE 'DSN: '.
001120         10  DET1-DSN            PIC X(44).
001130         10  FILLER              PIC X(3)       VALUE SPACES.
001140         10  FILLER              PIC X(13)
001150                 VALUE 'CREATE DATE: '.
001160         10  DET1-CREATE-DATE.
001170                 15  DET1-CREATE-MM PIC Z9.
001180                 15  FILLER      PIC X(1)   VALUE '/'.
001190                 15  DET1-CREATE-DD PIC 99.
001200                 15  FILLER      PIC X(1)  VALUE '/'.
001210                 15  DET1-CREATE-YY PIC 99.
001220         10  FILLER              PIC X(3)  VALUE SPACES.
001230         10  FILLER              PIC X(17)
001240                 VALUE 'EXPIRATION DATE: '.
001250         10  DET1-EXPIR-DATE.
001260                 15  DET1-EXPIR-MM  PIC Z9.
001270                 15  FILLER      PIC X(1)  VALUE '/'.
001280                 15  DET1-EXPIR-DD PIC 99.
001290                 15  FILLER      PIC X(1)  VALUE '/'.
001300                 15  DET1-EXPIR-YY PIC 99.
001310         10  FILLER              PIC X(28)  VALUE SPACES.
001320     SKIP1
001330     05  DETAIL-2.
001340         10  FILLER              PIC X(4)  VALUE SPACES.
001350         10  FILLER              PIC X(36)
001360                 VALUE 'SPACE INFORMATION:  NUM OF EXTENTS: '.
001370         10  DET2-NUM-EXTS       PIC Z9.
001380         10  FILLER              PIC X(2)  VALUE SPACES.
001390         10  FILLER              PIC X(14)
001400                 VALUE 'ALLOC: TRACKS-'.
001410         10  DET2-ALLOC-TRKS     PIC ZZZZ9.
001420         10  FILLER              PIC X(11)
001430                 VALUE ' CYLS/TRKS-'.
001440         10  DET2-ALLOC-CYLS     PIC ZZ9.
001450         10  FILLER              PIC X(1)   VALUE '/'.
001460         10  DET2-ALLOC-LO-TRKS  PIC Z9.
001470         10  FILLER              PIC X(5)  VALUE SPACES.
001480         10  FILLER              PIC X(13)
001490                 VALUE 'USED: TRACKS-'.
001500         10  DET2-USED-TRKS      PIC ZZZZ9.
001510         10  FILLER              PIC X(11)
001520                 VALUE ' CYLS/TRKS-'.
001530         10  DET2-USED-CYLS      PIC ZZ9.
001540         10  FILLER              PIC X(1)  VALUE '/'.
001550         10  DET2-USED-LO-TRKS   PIC Z9.
001560         10  FILLER              PIC X(2) VALUE SPACES.
001570         10  FILLER              PIC X(8)
001580                 VALUE '% FREE: '.
001590         10  DET2-PCT-FREE       PIC Z9.
001600         10  FILLER              PIC X(1)  VALUE SPACES.
001610     SKIP1
001620     05  DETAIL-3.
001630         10  FILLER              PIC X(4)  VALUE SPACES.
001640         10  FILLER              PIC X(26)
001650                 VALUE 'DCB INFORMATION:   DSORG: '.
001660         10  DET3-DSORG          PIC X(2).
001670         10  FILLER              PIC X(4)  VALUE SPACES.
001680         10  FILLER              PIC X(7)
001690                 VALUE 'RECFM: '.
001700         10  DET3-RECFM          PIC X(2).
001710         10  FILLER              PIC X(4)  VALUE SPACES.
001720         10  FILLER              PIC X(6)
001730                 VALUE 'LRECL:'.
001740         10  DET3-LRECL          PIC ZZZZ9.
001750         10  FILLER              PIC X(4)  VALUE SPACES.
001760         10  FILLER              PIC X(9)
001770                 VALUE 'BLKSIZE: '.
001780         10  DET3-BLKSIZE        PIC ZZZZ9.
001790         10  FILLER              PIC X(3)  VALUE SPACES.
001800         10  FILLER              PIC X(5)
001810                 VALUE 'RKP: '.
001820         10  DET3-RKP            PIC Z9.
001830         10  FILLER              PIC X(3)  VALUE SPACES.
001840         10  FILLER              PIC X(9)
001850                 VALUE 'KEY LEN: '.
001860         10  DET3-KEYLEN         PIC ZZ9.
001870         10  FILLER              PIC X(2)  VALUE SPACES.
001880         10  FILLER              PIC X(10)
001890                 VALUE 'CAT STAT: '.
001900         10  DET3-CATSTAT        PIC X.
001910         10  FILLER              PIC X(3)  VALUE SPACES.
001920         10  FILLER              PIC X(6)
001930                 VALUE 'CVOL: '.
001940         10  DET3-CVOL           PIC X(6).
001950         10  FILLER              PIC X(2)  VALUE SPACES.
001960 PROCEDURE DIVISION.
001970     SKIP1
001980 SORT-INPUT SECTION.
001990     SKIP1
002000     SORT SORT-FILE
002010         ASCENDING SORT-DSNAME
002020             USING EXTRACT-INPUT-FILE
002030             OUTPUT PROCEDURE REPORT-SECTION.
002040     SKIP1
002050     CLOSE DMS003-REPORT.
002060     SKIP1
002070     STOP RUN.
002080     SKIP1
002090 REPORT-SECTION SECTION.
002100 10-OPEN-PARA.
002110     OPEN OUTPUT DMS003-REPORT.
002120 20-READ-PROCESS.
002130     RETURN SORT-FILE  INTO WS-EXT-RECORD
002140         AT END
002150             GO TO 99-EXIT.
002160     MOVE EXTRACT-DSN           TO DET1-DSN.
002170     MOVE EXTRACT-CREATE-DATE   TO HOLD-DATE-NUM.
002180     MOVE HOLD-DATE-MM          TO DET1-CREATE-MM.
002190     MOVE HOLD-DATE-DD          TO DET1-CREATE-DD.
002200     MOVE HOLD-DATE-YY          TO DET1-CREATE-YY.
002210     IF EXTRACT-EXPIR-DATE = ZEROS
002220         MOVE 'NONE' TO DET1-EXPIR-DATE
002230     ELSE
002240         MOVE '  /  /  ' TO DET1-EXPIR-DATE
002250         MOVE EXTRACT-EXPIR-DATE    TO HOLD-DATE-NUM
002260         MOVE HOLD-DATE-MM          TO DET1-EXPIR-MM
002270         MOVE HOLD-DATE-DD          TO DET1-EXPIR-DD
002280         MOVE HOLD-DATE-YY          TO DET1-EXPIR-YY.
002290     MOVE EXTRACT-NUM-EXTENTS    TO DET2-NUM-EXTS.
002300     MOVE EXTRACT-SPACE-ALLOC    TO DET2-ALLOC-TRKS.
002301
002310     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3330'
002320         DIVIDE EXTRACT-SPACE-ALLOC BY 19
002330             GIVING DET2-ALLOC-CYLS
002340             REMAINDER DET2-ALLOC-LO-TRKS.
002341
002342     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3340'
002343         DIVIDE EXTRACT-SPACE-ALLOC BY 12
002344             GIVING DET2-ALLOC-CYLS
002345             REMAINDER DET2-ALLOC-LO-TRKS.
002346
002350     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3350'
002360         DIVIDE EXTRACT-SPACE-ALLOC BY 30
002370             GIVING DET2-ALLOC-CYLS
002380             REMAINDER DET2-ALLOC-LO-TRKS.
002381
002382     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3375'
002383         DIVIDE EXTRACT-SPACE-ALLOC BY 12
002384             GIVING DET2-ALLOC-CYLS
002385             REMAINDER DET2-ALLOC-LO-TRKS.
002386
002390     MOVE EXTRACT-SPACE-USED    TO DET2-USED-TRKS.
002391
002400     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3330'
002410         DIVIDE EXTRACT-SPACE-USED BY 19
002420             GIVING DET2-USED-CYLS
002430             REMAINDER DET2-USED-LO-TRKS.
002431
002433     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3340'
002434         DIVIDE EXTRACT-SPACE-USED BY 12
002435             GIVING DET2-USED-CYLS
002436             REMAINDER DET2-USED-LO-TRKS.
002437
002438     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3350'
002450         DIVIDE EXTRACT-SPACE-USED BY 30
002460             GIVING DET2-USED-CYLS
002470             REMAINDER DET2-USED-LO-TRKS.
002471
002472     IF EXTRACT-RES-PACK-TYP IS EQUAL TO '3375'
002473         DIVIDE EXTRACT-SPACE-USED BY 12
002474             GIVING DET2-USED-CYLS
002475             REMAINDER DET2-USED-LO-TRKS.
002476
002480     COMPUTE WS-PCT-FREE =
002490         (EXTRACT-SPACE-ALLOC - EXTRACT-SPACE-USED) /
002500                   EXTRACT-SPACE-ALLOC              *
002510                          100.
002520     MOVE WS-PCT-FREE           TO DET2-PCT-FREE.
002530     MOVE EXTRACT-DSORG         TO DET3-DSORG.
002540     MOVE EXTRACT-RECFM         TO DET3-RECFM.
002550     MOVE EXTRACT-LRECL         TO DET3-LRECL.
002560     MOVE EXTRACT-BLKSIZE       TO DET3-BLKSIZE.
002570     MOVE EXTRACT-RKP           TO DET3-RKP.
002580     MOVE EXTRACT-KEYLEN        TO DET3-KEYLEN.
002590     IF EXTRACT-CAT-VOL = 'NOTCAT'
002600         MOVE 'N'                  TO DET3-CATSTAT
002610     ELSE
002620         MOVE 'Y'                  TO DET3-CATSTAT.
002630     MOVE EXTRACT-CVOL          TO DET3-CVOL.
002640     PERFORM 30-WRITE-LINE.
002650     GO TO 20-READ-PROCESS.
002660     SKIP1
002670 30-WRITE-LINE.
002680     IF LINE-COUNT GREATER THAN 55
002690         PERFORM 50-HEADINGS.
002700     WRITE REPORT-RECORD FROM DETAIL-1  AFTER 2.
002710     WRITE REPORT-RECORD FROM DETAIL-2  AFTER 1.
002720     WRITE REPORT-RECORD FROM DETAIL-3  AFTER 1.
002730     ADD 4 TO LINE-COUNT.
002740     SKIP1
002750 50-HEADINGS.
002760     MOVE CURRENT-DATE          TO HDG1-DATE.
002770     ADD 1                      TO PAGE-COUNT.
002780     MOVE PAGE-COUNT            TO HDG1-PAGE-NO.
002790     WRITE REPORT-RECORD FROM HEADING-1 AFTER T-O-P.
002800     MOVE EXTRACT-RES-VOLSER    TO HDG2-VOLUME.
002810     WRITE REPORT-RECORD FROM HEADING-2 AFTER 1.
002820     MOVE ALL '-'               TO REPORT-RECORD.
002830     WRITE REPORT-RECORD AFTER 2.
002840     MOVE SPACES TO REPORT-RECORD.
002850     MOVE ZEROS TO LINE-COUNT.
002860     SKIP1
002870 99-EXIT.
002880     EXIT.
