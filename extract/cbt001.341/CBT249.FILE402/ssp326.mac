       ID DIVISION.
       PROGRAM-ID.         SSP326.
       AUTHOR.
                 RON KOHL.
       REMARKS.
                 THIS PROGRAM IS USED TO DISPLAY A SCREEN OF DATA-
                 SETS FROM THE DATA SET MANAGEMENT FILE.  IF THE
                 USER HAS ENTERED AN ASTERISK SOMEWHERE IN THE DSN
                 FIELD OF SSP325, THIS PROGRAM GAINS CONTROL AND
                 INITIATES A GENERIC SEARCH BEGINNING AT THE HIGH
                 LEVEL KEY. THE USER CAN PAGE FORWARD AND BACKWARD
                 UNDER PROGRAM CONTROL.  THE USER CAN OPTIONALLY
                 SELECT A DATASET FOR CHANGES.  IN THIS SITUATION,
                 CONTROL IS PASSED BACK TO SSP325 FOR PROCESSING.
                 FOR EACH RECORD DISPLAYED ON THE SCREEN, A TEMP
                 STORAGE QUEUE RECORD IS WRITTEN CONTAINING THE DSN.
                 IF THE USER SELECTS A DISPLAYED RECORD OR REQUESTS
                 A DELETE OF A DISPLAYED RECORD, THE QUEUE IS READ
                 FOR THE RECORD SELECTED BY ITS RELATIVE POSITION
                 ON THE SCREEN.  THIS QUEUE IS BUILT SO THE MAP
                 DOES NOT HAVE TO BE READ BACK IN.
       SKIP1
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-370.
       OBJECT-COMPUTER. IBM-370.
       DATA DIVISION.
       WORKING-STORAGE SECTION.
           SKIP1

       01  WS-BLANK        VALUE ' '          PIC X.

       01  WS-COMMAREA.
           03  DSN-NEXT                       PIC X(44).
           03  DSN-CURRENT                    PIC X(44).
           03  DSN-PRIOR                      PIC X(44).
           03  DSN-PRIOR-1                    PIC X(44).
           03  DSN-PRIOR-2                    PIC X(44).

       01  TS-QUEUE-DATA.
           03  TS-QUEUE-LENGTH   COMP   VALUE +44  PIC S9(4).
           03  TS-QUEUE-NAME.
               05  TS-TRNID                   PIC X(4).
               05  TS-TRMID                   PIC X(4).

       01  LINE-CTR                 COMP      PIC S9(4).

       01  DSM-RECORD.
           03  DSM-KEY               PIC X(44).
      *        VSAM KEY IS DATASET NAME
           03  DSM-CREATE-DATE       PIC 9(6).
           03  DSM-CHG-DATE          PIC 9(6).
           03  DSM-STRUCTURE         PIC X(4).
           03  DSM-SIZE              PIC 9(5).
           03  DSM-GROWTH            PIC 9(3).
           03  DSM-GDG               PIC 9(2).
           03  DSM-DEV-TYPE          PIC X(4).
           03  DSM-VOLSER            PIC X(6).
           03  DSM-RETAIN-DATE       PIC 9(6).
           03  DSM-RECFM             PIC X(3).
           03  DSM-LRECL             PIC 9(5).
           03  DSM-BLKSIZE           PIC 9(5).
           03  DSM-NUM-RECS          PIC 9(7).
           03  DSM-DESCR             PIC X(80).
           03  FILLER                PIC X(114).

       01  DTL-LINE.
           03  DTL-DSN         PIC X(44).
           03  FILLER          PIC X(2).
           03  DTL-CREATE      PIC X(6).
           03  FILLER          PIC X.
           03  DTL-STRUCTURE   PIC X(4).
           03  FILLER          PIC X.
           03  DTL-SIZE        PIC Z(4)9.
           03  FILLER          PIC X.
           03  DTL-GROWTH      PIC ZZ9.
           03  FILLER          PIC X.
           03  DTL-RETAIN      PIC X(6).

       01  SSP326M                     COPY SSP326M.

       01  SSP303-COMMAREA-DEF         COPY   SSP303C1.

      ******************************************************************
      *    B M S  ATTRIBUTE BYTES                                      *
      *        1)    ALPHANUMERIC UNLESS NUM (NUMERIC) SPECIFIED       *
      *        2)    INTENSITY = NORMAL UNLESS HIGH OR DRK SPECIFIED   *
      *        3)    MDT = ON OR OFF                                   *
      ******************************************************************
       01  ATTRIBUTES.
           05  ATTR-UNPROT-OFF           PIC X(01) VALUE ' '.
           05  ATTR-UNPROT-ON            PIC X(01) VALUE 'A'.
           05  ATTR-UNPROT-HIGH-OFF      PIC X(01) VALUE 'H'.
           05  ATTR-UNPROT-HIGH-ON       PIC X(01) VALUE 'I'.
           05  ATTR-UNPROT-DRK-OFF       PIC X     VALUE '<'.
           05  ATTR-UNPROT-DRK-ON        PIC X     VALUE '('.
           SKIP1
           05  ATTR-UNPROT-NUM-OFF       PIC X(01) VALUE '&'.
           05  ATTR-UNPROT-NUM-ON        PIC X(01) VALUE 'J'.
           05  ATTR-UNPROT-NUM-HIGH-OFF  PIC X(01) VALUE 'Q'.
           05  ATTR-UNPROT-NUM-HIGH-ON   PIC X(01) VALUE 'R'.
           05  ATTR-PROT-DRK-OFF         PIC X     VALUE '%'.
           05  ATTR-PROT-DRK-ON          PIC X     VALUE '_'.
           SKIP1
           05  ATTR-PROT-OFF             PIC X(01) VALUE '-'.
           05  ATTR-PROT-ON              PIC X(01) VALUE '/'.
           05  ATTR-PROT-HIGH-OFF        PIC X(01) VALUE 'Y'.
           05  ATTR-PROT-HIGH-ON         PIC X(01) VALUE 'Z'.
           05  ATTR-UNPROT-NUM-DRK-OFF   PIC X     VALUE '*'.
           05  ATTR-UNPROT-NUM-DRK-ON    PIC X     VALUE ')'.
           SKIP1
           05  ATTR-PROT-NUM-OFF         PIC X(01) VALUE '0'.
           05  ATTR-PROT-NUM-ON          PIC X(01) VALUE '1'.
           05  ATTR-PROT-NUM-HIGH-OFF    PIC X(01) VALUE '8'.
           05  ATTR-PROT-NUM-HIGH-ON     PIC X(01) VALUE '9'.

       01  DFHAID                      COPY DFHAID.
           EJECT
       LINKAGE SECTION.
           SKIP1
       01  DFHCOMMAREA             PIC X(220).
       PROCEDURE DIVISION.
           SKIP2
       000-HANDLE.
           EXEC CICS
               HANDLE CONDITION MAPFAIL(920-END)
                                ERROR(910-ERROR)
           END-EXEC.
           IF EIBAID = DFHCLEAR
               GO TO 920-END.

       100-CALEN.
      ****************************************************************
      * TEMPORARY STORAGE QUEUE ID =  TRANSID + TERMID FOR UNIQUENESS
      ****************************************************************
           MOVE 'SSP3' TO TS-TRNID.
           MOVE EIBTRMID TO TS-TRMID.
      ****************************************************************
      * CALEN OF 0 INDICATES THAT THE USER ENTERED 'SSP3' AS A TRANSID.
      * SINCE THIS PROGRAM CANNOT BE INVOKED DIRECTLY, XCTL TO SSP325
      ****************************************************************
           IF EIBCALEN = 0
               EXEC CICS
                   XCTL PROGRAM('SSP325')
               END-EXEC.
      ****************************************************************
      * CALEN OF 44 INDICATES THAT CONTROL WAS PASSED FROM SSP325.
      * THE DSN WAS PASSED IN ORDER TO INITIATE A GENERIC BROWSE.
      ****************************************************************
           IF EIBCALEN = 44
                MOVE LOW-VALUES TO WS-COMMAREA
                MOVE DFHCOMMAREA TO DSN-CURRENT DSN-NEXT
                GO TO 150-FORMAT-SCREEN.
      ****************************************************************
      *      DEFAULT CALEN IS 220 (5 KEYS)
      ****************************************************************
            MOVE DFHCOMMAREA TO WS-COMMAREA.

       120-RECEIVE-MAP.
           EXEC CICS
               RECEIVE MAP('SSP326M') INTO(SSP326M)
           END-EXEC.

       121-CHK-FOR-DELETE-AND-SELECT.
      ****************************************************************
      * THE MAP IS NOT REDEFINED AS IT HIGHLY ERROR PRONE
      *
      * PROCESS IS:  THE USER MAY DELETE AS MANY OF THE DISPLAYED DATA-
      * SETS AS DESIRED.  HOWEVER ONLY 1 DATASET MAY BE SELECTED FOR
      * PROCESSING BY SSP325.
      ****************************************************************
           IF SEL1I = 'D'
               MOVE 1 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL2I = 'D'
               MOVE 2 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL3I = 'D'
               MOVE 3 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL4I = 'D'
               MOVE 4 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL5I = 'D'
               MOVE 5 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL6I = 'D'
               MOVE 6 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL7I = 'D'
               MOVE 7 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL8I = 'D'
               MOVE 8 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL9I = 'D'
               MOVE 9 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL10I = 'D'
               MOVE 10 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL11I = 'D'
               MOVE 11 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL12I = 'D'
               MOVE 12 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL13I = 'D'
               MOVE 13 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL14I = 'D'
               MOVE 14 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL15I = 'D'
               MOVE 15 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL16I = 'D'
               MOVE 16 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL17I = 'D'
               MOVE 17 TO LINE-CTR
               PERFORM 700-DELETE.
           IF SEL1I = 'S'
               MOVE 1 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL2I = 'S'
               MOVE 2 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL3I = 'S'
               MOVE 3 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL4I = 'S'
               MOVE 4 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL5I = 'S'
               MOVE 5 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL6I = 'S'
               MOVE 6 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL7I = 'S'
               MOVE 7 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL8I = 'S'
               MOVE 8 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL9I = 'S'
               MOVE 9 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL10I = 'S'
               MOVE 10 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL11I = 'S'
               MOVE 11 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL12I = 'S'
               MOVE 12 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL13I = 'S'
               MOVE 13 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL14I = 'S'
               MOVE 14 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL15I = 'S'
               MOVE 15 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL16I = 'S'
               MOVE 16 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
           IF SEL17I = 'S'
               MOVE 17 TO LINE-CTR
               GO TO 800-XCTL-SSP325.
      ****************************************************************
      * ANY KEY WILL CAUSE PAGE FORWARD, ONLY 'B' WILL PAGE BACKWARD
      *                                     (SO WILL PF7 OR PF19)
      ****************************************************************
           IF PAGECMDI = 'B' OR (EIBAID = DFHPF7 OR = DFHPF19)
               GO TO 125-PAGE-BACKWARD.
           GO TO 130-PAGE-FORWARD.

       125-PAGE-BACKWARD.
           IF DSN-PRIOR = LOW-VALUES
               MOVE DSN-CURRENT TO DSN-NEXT
               MOVE LOW-VALUES TO SSP326M
               MOVE 'NO PREVIOUS PAGE EXISTS' TO ERRLINO
               EXEC CICS
                   SEND MAP('SSP326M') FROM(SSP326M) ERASE FREEKB
               END-EXEC
               EXEC CICS
                   RETURN TRANSID('SSP3')
                          COMMAREA(WS-COMMAREA)
                          LENGTH(220)
               END-EXEC.
           MOVE DSN-PRIOR TO DSN-NEXT DSN-CURRENT.
           MOVE DSN-PRIOR-1 TO DSN-PRIOR.
           MOVE DSN-PRIOR-2 TO DSN-PRIOR-1.
           MOVE LOW-VALUES TO DSN-PRIOR-2.
           GO TO 150-FORMAT-SCREEN.

       130-PAGE-FORWARD.
           MOVE DSN-PRIOR-1 TO DSN-PRIOR-2.
           MOVE DSN-PRIOR   TO DSN-PRIOR-1.
           MOVE DSN-CURRENT TO DSN-PRIOR.
           MOVE DSN-NEXT    TO DSN-CURRENT.

       150-FORMAT-SCREEN.
           MOVE 0 TO LINE-CTR.
           MOVE LOW-VALUES TO SSP326M.
           EXEC CICS
               HANDLE CONDITION ENDFILE(200-SEND-SCREEN)
           END-EXEC.
           EXEC CICS
               STARTBR DATASET('SSPDSM') RIDFLD(DSN-NEXT)
           END-EXEC.
           EXEC CICS
               HANDLE CONDITION QIDERR(160-READNEXT)
           END-EXEC.
           EXEC CICS
               DELETEQ TS QUEUE(TS-QUEUE-NAME)
           END-EXEC.

       160-READNEXT.
           EXEC CICS
               READNEXT INTO(DSM-RECORD)
               DATASET('SSPDSM') RIDFLD(DSN-NEXT)
           END-EXEC.
           ADD 1 TO LINE-CTR.
      ****************************************************************
      * EACH DISPLAYED DATASET NAME IS WRITTEN TO A TEMPORARY STORAGE
      * QUEUE IN ORDER TO FACILITATE THE SELECTION OR DELETION OF
      * DISPLAYED RECORDS.  THE RECORD NUMBER IN THE QUEUE IS THE SAME
      * AS THE RELATIVE POSITION ON THE SCREEN.
      ****************************************************************
           EXEC CICS
               WRITEQ TS QUEUE(TS-QUEUE-NAME)
                         FROM(DSM-KEY)
                         LENGTH(44)
           END-EXEC.

       170-FORMAT-SCREEN.
           MOVE SPACES TO DTL-LINE.
           MOVE DSM-KEY TO DTL-DSN.
           MOVE DSM-CREATE-DATE TO DTL-CREATE.
           MOVE DSM-STRUCTURE TO DTL-STRUCTURE.
           MOVE DSM-SIZE TO DTL-SIZE.
           MOVE DSM-GROWTH TO DTL-GROWTH.
           MOVE DSM-RETAIN-DATE TO DTL-RETAIN.
           IF LINE-CTR = 1
               MOVE DTL-LINE TO LINE1O
               GO TO 160-READNEXT.
           IF LINE-CTR = 2
               MOVE DTL-LINE TO LINE2O
               GO TO 160-READNEXT.
           IF LINE-CTR = 3
               MOVE DTL-LINE TO LINE3O
               GO TO 160-READNEXT.
           IF LINE-CTR = 4
               MOVE DTL-LINE TO LINE4O
               GO TO 160-READNEXT.
           IF LINE-CTR = 5
               MOVE DTL-LINE TO LINE5O
               GO TO 160-READNEXT.
           IF LINE-CTR = 6
               MOVE DTL-LINE TO LINE6O
               GO TO 160-READNEXT.
           IF LINE-CTR = 7
               MOVE DTL-LINE TO LINE7O
               GO TO 160-READNEXT.
           IF LINE-CTR = 8
               MOVE DTL-LINE TO LINE8O
               GO TO 160-READNEXT.
           IF LINE-CTR = 9
               MOVE DTL-LINE TO LINE9O
               GO TO 160-READNEXT.
           IF LINE-CTR = 10
               MOVE DTL-LINE TO LINE10O
               GO TO 160-READNEXT.
           IF LINE-CTR = 11
               MOVE DTL-LINE TO LINE11O
               GO TO 160-READNEXT.
           IF LINE-CTR = 12
               MOVE DTL-LINE TO LINE12O
               GO TO 160-READNEXT.
           IF LINE-CTR = 13
               MOVE DTL-LINE TO LINE13O
               GO TO 160-READNEXT.
           IF LINE-CTR = 14
               MOVE DTL-LINE TO LINE14O
               GO TO 160-READNEXT.
           IF LINE-CTR = 15
               MOVE DTL-LINE TO LINE15O
               GO TO 160-READNEXT.
           IF LINE-CTR = 16
               MOVE DTL-LINE TO LINE16O
               GO TO 160-READNEXT.
           MOVE DTL-LINE TO LINE17O.

       200-SEND-SCREEN.
           MOVE DSM-KEY TO DSN-NEXT.
           EXEC CICS
               SEND MAP('SSP326M') FROM(SSP326M) ERASE FREEKB
           END-EXEC.
           EXEC CICS
               RETURN TRANSID('SSP3')
                      COMMAREA(WS-COMMAREA)
                      LENGTH(220)
           END-EXEC.

       700-DELETE.
           EXEC CICS
               READQ TS QUEUE(TS-QUEUE-NAME)
                        INTO(DSM-KEY)
                        LENGTH(TS-QUEUE-LENGTH)
                        ITEM(LINE-CTR)
           END-EXEC.
           EXEC CICS
               DELETE DATASET('SSPDSM')
                      RIDFLD(DSM-KEY)
           END-EXEC.

       800-XCTL-SSP325.
           EXEC CICS
               READQ TS QUEUE(TS-QUEUE-NAME)
                        INTO(DSM-KEY)
                        LENGTH(TS-QUEUE-LENGTH)
                        ITEM(LINE-CTR)
           END-EXEC.
           EXEC CICS
               DELETEQ TS QUEUE(TS-QUEUE-NAME)
           END-EXEC.
           EXEC CICS
               XCTL PROGRAM('SSP325')
                    COMMAREA(DSM-KEY)
                    LENGTH(44)
           END-EXEC.

       910-ERROR.
           MOVE DFHEIBLK TO SSP303-DFHEIBLK.
           MOVE 'SSP326' TO SSP303-PROGRAM.
           EXEC CICS
               XCTL PROGRAM('SSP303')
                    COMMAREA(SSP303-COMMAREA)
                    LENGTH(SSP303-COMMAREA-LENGTH)
           END-EXEC.

       920-END.
           EXEC CICS
               HANDLE CONDITION QIDERR(950-ALL-DONE)
           END-EXEC.
           EXEC CICS
               DELETEQ TS QUEUE(TS-QUEUE-NAME)
           END-EXEC.

       950-ALL-DONE.
           EXEC CICS
               SEND FROM(WS-BLANK) LENGTH(0) ERASE
           END-EXEC.
           EXEC CICS
               RETURN
           END-EXEC.
           GOBACK.
