SSP531   TITLE 'ROUTINE TO PROVIDE INPUT TO IDCAMS FROM A CORE TABLE.'
         PUNCH '  ALIAS SSP531      '
         PUNCH '  ALIAS SSP531G   '
         PUNCH '  ALIAS SSP531L   '
SSP531   CSECT
*
*              THIS SUBPROGRAM IS CALLED FROM AN APPLICATION PROGRAM
*        TO INVOKE ACCESS METHOD SERVICES. INPUT IS PROVIDED TO
*        IDCAMS VIA AN IN CORE TABLE FROM THE APPLICATION PROGRAM.
*
*              AN EXAMPLE OF A CALL TO THIS ROUTINE IS:
*
*              CALL 'SSP531' USING SYSIPT-BUFFER.
*
*              WHERE:
*
*                   SYSIPT-BUFFER = 01  SYSIPT-BUFFER.
*                                       02  IDCAMS-RETURN-CD PIC S999
*                                                                COMP.
*                                       02  NUMBER-OF-CARDS  PIC S999
*                                                                COMP.
*                                       02  IDCAMS-CARDS PIC X(80)
*                                                 OCCURS N TIMES.
*
*                   WHERE:
*
*                        IDCAMS-RETURN-CD = HIGHEST SEVERITY CODE FROM
*                                           IDCAMS AFTER ITS EXECUTION.
*
*                        NUMBER-OF-CARDS  = THE NUMBER OF CARDS IN THE
*                                           TABLE THAT FOLLOWS THIS
*                                           DATA ITEM.
*
*                        IDCAMS-CARDS     = TABLE OF CARDS PASSED TO
*                                           IDCAMS AS ITS SYSIPT DATA
*                                           INPUT.
*
         EJECT
*
*        THIS PART OF THE ROUTINE GETS CONTROL FIRST. WE LOAD IDCAMS
*        AND SET UP ALL PARAMETERS TO CALL IT AND HAVE IT CALL THE
*        SECOND PART OF THIS ROUTINE TO PASS IT A CONTROL CARD FROM
*        THE APPLICATION PROGRAMS CONTROL CARD TABLE.
*
SSP531   TSENTRY ID='IDCAMS INTERFACE ROUTINE ',BASE=R12,SAVE=ASAVE
         LR    R3,R1               SAVE PARMS PASSED
*
*        LOAD IDCAMS
*
         L     R15,AIDCAMSA
         LTR   R15,R15
         BNZ   ACKPRMS
         LOAD  EP=IDCAMS
         LR    R15,R0
*
*        CHECK PARMS PASSED
*
ACKPRMS  L     R4,0(,R3)           POINT TO PARMS
         ST    R4,APRMPNTR         SAVE PARM POINTER
         LA    R1,4(,R4)           POINT TO CARDS
         ST    R1,ACURCARD         SAVE POINTER
         LH    R5,2(,R4)           LOAD NUMBER OF CARDS
         CH    R5,=H'256'          IS IT BIGGER THAN LIMIT
         BH    AERR200             YES, BRANCH
         LTR   R5,R5               IS IT LESS THAN ONE
         BNH   AERR200             YES, BRANCH
         MH    R5,=H'80'           POINT PAST TABLE
         AR    R1,R5                "
         ST    R1,ALSTCARD         SAVE POINTER
         ST    R15,AIDCAMSA
         NI    ASW,X80
         TM    ASW,X80
         BO    AEXIT
*
*        INVOKE IDCAMS
*
         CALL  (15),(AZERO,AZERO,AZERO,AIOLIST),VL
*
*        SAVE IDCAMS RETURN-CODE AND RETURN TO CALLER
*
         STH   R15,0(,R4)         SAVE RETURN CODE
AEXIT    TSEXIT RC=0              RETURN TO CALLER
         EJECT
*
*        ERROR LOADING IDCAMS
*
AERR100  LA    R15,100(,R15)
AERR100S STH   R15,ATABCD
ACALTAB  LR    R1,R15
         ABEND (1),DUMP,STEP
*
*        NUMBER-OF-CARDS OUT OF RANGE
*
AERR200  LA    R15,200
         B     AERR100S
*
*        LOGICAL ERROR, GET CALLED WITHOUT LST BEING CALLED
*
AERR300  LA    R15,300
         B     AERR100S
         EJECT
*
*        ENTRY TO TURN ON WRITING SYSLST TO DISK
*
SSP531L TSENTRY ID='OUTPUT SYSLST TO DISK ',BASE=R12,SAVE=ASAVE
*
         L     R2,=V(SSP531)
         IDENTIFY EP=SSP531,ENTRY=(R2)
         L     R2,=V(SSP531G)
         IDENTIFY EP=SSP531G,ENTRY=(R2)
         LA    R1,2
         ST    R1,AIOLIST
         OI    ASW,X80
*
         TSEXIT RC=0
*
         LTORG
         EJECT
SSP531G TSENTRY ID='RETURN LIST LINE TO CALLER ',BASE=R12,SAVE=ASAVE2
*
         MVC   ALINPNTR,0(R1)
         TM    ASW,X80
         BZ    AERR300
*
         TM    ASW,X40
         BO    AGMOVE
*
         OI    ASW,X40
         LA    R13,ASAVE
         MVC   ASAVE(72),ASAVE2
         L     R15,AIDCAMSA
         CALL  (15),(AZERO,AZERO,AZERO,AIOLIST),VL
         L     R14,APRMPNTR
         STH   R15,0(,R14)
         LA    R13,ASAVE2
         XC    ALSTPNTR,ALSTPNTR
         TSEXIT RC=0
AGMOVE   L     R5,ALSTPNTR
         L     R4,AFLGPNTR
         OI    ASW,X20
         USING TSIOINFO,R5
         USING TSIOFLGS,R4
         L     R8,ALINPNTR
         LTR   R5,R5
         BZ    AGEOF
         MVC   0(L'BIOFLGS3,R8),BIOFLGS3
         LA    R0,L'BIOFLGS3(,R8)
         LA    R1,123-L'BIOFLGS3
         L     R14,BRECADDR
         L     R15,BRECLEN
         ICM   R15,8,=C' '
         MVCL  R0,R14
         LA    R13,ASAVE3
         TSEXIT RC=0
AGEOF    MVC   0(2,R8),=H'-1'
         LA    R0,2(,R8)
         LA    R1,121
         XR    R15,R15
         ICM   R15,8,=C' '
         MVCL  R0,R14
         TSEXIT RC=0
         EJECT
SSP531PT TSENTRY ID='SYSLIST RECIEVER ',BASE=R12,SAVE=ASAVE3
*
         LM    R3,R5,0(R1)
         USING TSCRDPTR,R3
         USING TSIOFLGS,R4
         USING TSIOINFO,R5
*
         XR    R15,R15
         IC    R15,BIOFLGS1
         B     *+4(R15)
         B     ALOPEN
         B     ALCLOSE
         B     ALGET
         B     ALPUT
*
*        SAVE LIST LINE PNTR
*
ALPUT    ST    R5,ALSTPNTR
         ST    R4,AFLGPNTR
         TM    ASW,X20
         BZ    AGMOVE
         LA    R13,ASAVE2
ALOPEN   EQU   *
ALCLOSE  EQU   *
ALGET    EQU   *
         TSEXIT RC=0
         EJECT
         TSEQUR
         EJECT
*
*        SAVE AND WORK AREAS
*
ASAVE    DC    18F'0'
ASAVE2   DC    18F'0'
ASAVE3   DC    18F'0'
ATABCD   DC    H'0'
AZERO    DC    H'0'
ASW      DC    X'00'
         LTORG
*
*        PARM LIST FOR SYSIPT ROUTINE
*
AIOLIST  DC    F'1'
         DC    A(AIPTNAME)
         DC    V(SSP531RD)
         DC    A(ACURCARD)
         DC    A(ALSTNAME)
         DC    A(SSP531PT)
         DC    A(0)
AIPTNAME DC    CL10'DDSYSIN'
ALSTNAME DC    CL10'DDSYSPRINT'
*
*        POINTERS FOR CARD TABLE
*
ACURCARD DS    F
ALSTCARD DS    F
ALSTPNTR DC    F'0'
AFLGPNTR DC    F'0'
AIDCAMSA DC    F'0'
APRMPNTR DC    F'0'
ALINPNTR DC    F'0'
         TITLE 'READ/WRITE ROUTINE CALLED BY IDCAMS.'
SSP531RD CSECT
*
*        CONTROL IS GIVEN HERE TO PASS A CARD BACK TO IDCAMS
*
SSP531RD TSENTRY ID='GET ROUTINE FOR IDCAMS ',BASE=R12,SAVE=BSAVE
*
*        LOAD POINTERS AND SET ADDRESSABILITY
*
         LM    R3,R5,0(R1)
         USING TSCRDPTR,R3
         USING TSIOFLGS,R4
         USING TSIOINFO,R5
         EJECT
*
*        PROCESS ACORDING TO IOFLAGS1
*
         XR    R15,R15
         IC    R15,BIOFLGS1
         B     *+4(R15)
         B     BOPEN               OPEN
         B     BCLOSE              CLOSE
         B     BGET                GET
         B     BPUT                PUT
*
*        PASS POINTER IDCAMS FOR NEXT CARD
*
BGET     L     R1,BCURCARD         POINT TO CURRENT CARD
         C     R1,BLSTCARD         IS IT EOF
         BNL   BRETURN4            YES, BRANCH
         ST    R1,BRECADDR         SAVE POINTER
         LA    R1,80(,R1)          BUMP TO NEXT CARD
         ST    R1,BCURCARD         SAVE AS CURRENT CARD
         LA    R1,80               LOAD LEN
         ST    R1,BRECLEN          SAVE LEN
         B     BRETURN0
*
*        PUT SYSLST LINE OUT
*
BPUT     B     BRETURN0
         EJECT
*
*        OPEN SYSLST WHEN CALLED FOR
*
BOPEN    B     BRETURN0
*
*        CLOSE SYSLST WHEN CALLED FOR
*
BCLOSE   B     BRETURN0
         EJECT
*
*        SET RETURN-CODES AND RETURN TO IDCAMS
*
BRETURN0 XR    R15,R15
BRETURN  TSEXIT RC=(R15)
BRETURN4 LA    R15,4
         B     BRETURN
BRETURN8 LA    R15,8
         B     BRETURN
BRETURNC LA    R15,12
         B     BRETURN
         EJECT
*
*        SAVE AND WORK AREAS
*
BSAVE    DC    18F'0'
BPUTRECA DS    A
         LTORG
         EJECT
*
*        DSECT'S TO DESCRIBE DATA PASSED
*
TSCRDPTR DSECT
BCURCARD DS    F
BLSTCARD DS    F
*
TSIOFLGS DSECT
BIOFLGS1 DS    X
BIOFLGS2 DS    X
BIOFLGS3 DS    H
*
TSIOINFO DSECT
BDDNAME  DS    0CL8
BRECADDR DS    F
BRECLEN  DS    F
         SPACE 6
         END
