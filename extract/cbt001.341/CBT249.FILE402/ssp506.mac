         LOGO  CSECT=SSP506,                                           *
               TITLE='IEHPROGM PROCESSOR'
SSP506   CSECT
         USING SSP506,R2
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R2,R15                  SET BASE ADDR REG
         LA    R3,SAVEAREA             SET REG 3 TO POINT TO MY SAVE
         ST    R3,8(R13)               STORE MY SAVE AREA POINTER
         ST    R13,SAVEAREA+4          STORE REG 13 IN MY SAVE AREA
         LR    R13,R3                  POINT TO NEW SAVE AREA
         OPEN  (VATLIST,(INPUT))
         CALL  SSP632,(WORK,INIT),VL
VATLOOP  EQU   *
         GET   VATLIST,VATAREA
         MVC   PARM2VOL,VATVOL
         MVC   PARM2DDN,VATVOL
         MVC   PARM2UNT,VATUNIT
         CALL  SSP632,(WORK,ALLOC,PARM2),VL
         B     VATLOOP
VATEND   EQU   *
         CLOSE VATLIST
         LINK  EP=IEHPROGM,                                            *
               PARAM=(IEHPARMS,IEHDDNMS),                              *
               VL=1
         L      R13,SAVEAREA+4
         RETURN (14,12),RC=0           END-OF-JOB!!!
IEHPARMS DC    H'+0'                        *NULL PARM STRING
         DS    0F,H
IEHDDNMS DC    H'+48'                       *ALTERNATE DDNAME LIST
         DC    4XL8'00'
         DC    C'SYSIN   '                  *ALTERNATE SYSIN NAME
         DC    C'SYSPRINT'                  *ALTERNATE SYSPRINT NAME
SAVEAREA DS    18F
VATAREA  DS    0CL80
VATVOL   DS    CL6
         DS    CL1    ,
VATX1    DS    CL1
         DS    CL1    ,
VATX2    DS    CL1
         DS    CL1    ,
VATUNIT  DS    CL4
         DS    CL4
         DS    CL1    ,
VATX3    DS    CL1
         DS    CL60
ALLOC    DC    CL6'ALLOC '
INIT     DC    CL5'INIT '
PARM2    DS    0CL36
         DC    CL5'DD=DD'
PARM2DDN DC    CL6' '
         DC    CL1' '
         DC    CL7'VOLUME='
PARM2VOL DC    CL6' '
         DC    CL1' '
         DC    CL5'UNIT='
PARM2UNT DC    CL4' '
         DC    CL5' SHR;'
WORK     DC    F'300'
         DS    CL100
         DS    CL100
         DS    CL100
         EJECT
**********************************************************************
*                                                                    *
*           D A T A   C O N T R O L   B L O C K S                    *
*                                                                    *
**********************************************************************
         SPACE 1
VATLIST  DCB   DDNAME=VATLIST,                                         *
               DSORG=PS,                                               *
               EODAD=VATEND,                                           *
               MACRF=GM
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         LTORG
         SPACE 1
         END
