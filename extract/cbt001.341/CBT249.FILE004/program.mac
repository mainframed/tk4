*          DATA SET CBT1342    AT LEVEL 003 AS OF 06/14/82
*          DATA SET CBT1342    AT LEVEL 002 AS OF 06/09/82
*          DATA SET DED121     AT LEVEL 006 AS OF 06/09/82
*
*  MODIFIED 06/14/82 BY HNB VIA CBT TO INCREASE THE GETMAIN FOR THE
*  KEYWORD TABLE (FROM 4096 TO 16364)
*
         MACRO
&NAME    SAVEALL &RENT,&REG=3,&EQU=YES,&ID=NO
         GBLB  &SAVRENT
         GBLB  &REGSW
         GBLB  &TIME
         GBLC  &SAVNDX
&SAVNDX  SETC  '&SYSNDX'
&NAME    CSECT
         AIF   ('&SAVRENT' EQ '1').NOTFREE
         AIF   ('&REGSW' EQ '1').NOEQU
         AIF   ('&EQU' NE 'YES').NOEQU
&REGSW   SETB  1
         SPACE
***********************************************************************
********           DEFINITION OF GENERAL REGISTERS             ********
***********************************************************************
R0       EQU   0    GENERAL REGISTER ZERO
R1       EQU   1    GENERAL REGISTER ONE
R2       EQU   2    GENERAL REGISTER TWO
R3       EQU   3    GENERAL REGISTER THREE
R4       EQU   4    GENERAL REGISTER FOUR
R5       EQU   5    GENERAL REGISTER FIVE
R6       EQU   6    GENERAL REGISTER SIX
R7       EQU   7    GENERAL REGISTER SEVEN
R8       EQU   8    GENERAL REGISTER EIGHT
R9       EQU   9    GENERAL REGISTER NINE
R10      EQU   10   GENERAL REGISTER TEN
R11      EQU   11   GENERAL REGISTER ELEVEN
R12      EQU   12   GENERAL REGISTER TWELVE
R13      EQU   13   GENERAL REGISTER THIRTEEN
R14      EQU   14   GENERAL REGISTER FOURTEEN
R15      EQU   15   GENERAL REGISTER FIFTEEN
         SPACE
RA       EQU   10   GENERAL REGISTER TEN
RB       EQU   11   GENERAL REGISTER ELEVEN
RC       EQU   12   GENERAL REGISTER TWELVE
RD       EQU   13   GENERAL REGISTER THIRTEEN
RE       EQU   14   GENERAL REGISTER FOURTEEN
RF       EQU   15   GENERAL REGISTER FIFTEEN
***********************************************************************
********             END OF REGISTER DEFINITIONS               ********
***********************************************************************
         SPACE
.NOEQU   ANOP
         SAVE  (14,12)
         AIF   ('&REG' EQ '').MESS
         BALR  &REG(1),0
         AIF   (N'&REG EQ 1).A
         AIF   (N'&REG EQ 2).B
         AIF   (N'&REG EQ 3).C
         AIF   (N'&REG EQ 4).D
         AIF   (N'&REG EQ 5).E
         MNOTE *****,'EXCESSIVE BASE REGISTERS -- 5 ASSUMED *****'
         AGO   .E
.MESS    MNOTE 12,'***** NO BASE REGISTER SPECIFIED *****'
         MEXIT
.NOTFREE MNOTE 12,'***** ENDALL NOT ISSUED PRIOR TO NEW SAVEALL *****'
         MEXIT
.E       USING *,&REG(1),&REG(2),&REG(3),&REG(4),&REG(5)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         L     &REG(5),=A(Q&SYSNDX+16384)
         AGO   .END
.D       USING *,&REG(1),&REG(2),&REG(3),&REG(4)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         AGO   .END
.C       USING *,&REG(1),&REG(2),&REG(3)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         AGO   .END
.B       USING *,&REG(1),&REG(2)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTER
         AGO   .END
.A       USING *,&REG(1)
.END     ANOP
         AIF   ('&RENT' NE 'RENT').NORENT
&SAVRENT SETB  1
         GETMAIN R,LV=4096         GET STORAGE FOR SAVE AREA
         ST    13,4(1)             SET UP FORWARD CHAIN
         ST    1,8(13)             SET UP BACKWARD CHAIN
         L     0,20(0,13)          RESET REGISTER 0
         L     1,24(0,13)          RESET REGISTER 1
         L     13,8(0,13)          LOAD SAVE AREA ADDRESS
         B     J&SYSNDX           BRANCH AROUND LTORG
         AGO   .RENT
.NORENT  ANOP
&TIME    SETB  1
         ST    13,SAVE&SYSNDX+4    SET UP FORWARD CHAIN
         LA    1,SAVE&SYSNDX
         ST    1,8(0,13)           SET UP BACKWARD CHAIN
         L     1,24(0,13)          RESET REGISTER 1
         LA    13,SAVE&SYSNDX
         B     J&SYSNDX           BRANCH AROUND BASE REGISTER CONST
SAVE&SYSNDX DC 18F'0'
.RENT    ANOP
         LTORG
         AIF   ('&ID' NE 'YES').NOID
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
.NOID    ANOP
J&SYSNDX DS    0H
         MEND
         MACRO
&NAME    ENDALL &CODE=
         GBLB  &TIME
         GBLB  &SAVRENT
         GBLC  &SAVNDX
         LCLA  &LEN
         LCLC  &REG
         AIF   ('&CODE' GT '4095').INVRET
&NAME    DS    0H
         AIF   (K'&CODE LE 0).CHKRENT
         AIF   ('&CODE'(1,1) NE '(').CHKRENT
         AIF   ('&CODE' EQ '(13)' OR '&CODE' EQ '(14)').INVREG
         AIF   ('&CODE' EQ '(0)' OR '&CODE' EQ '(1)').INVREG
         AIF   ('&CODE' NE '(15)').CHKRENT
         LR    2,15               SAVE REG 15 RETURN CODE
.CHKRENT AIF   (NOT &SAVRENT).NOTRENT
&SAVRENT SETB  0
         LR    1,13               LOAD SAVE AREA ADDRESS
         L     13,4(0,13)         RE-LOAD REGISTER 13
         FREEMAIN R,LV=4096,A=(1) FREE SAVE AREA STORAGE
         AGO   .RENT
.NOTRENT ANOP
         AIF   (NOT &TIME).NOSAVE
&TIME    SETB  0
         L     13,SAVE&SAVNDX+4   RE-LOAD REGISTER 13
.RENT    ANOP
         AIF   ('&CODE' EQ '').COND1
         AIF   ('&CODE' NE '(15)').CHKREG
         LR    15,2               GET RETURN CODE BACK FROM R2
         AGO   .ENDIT
.CHKREG  AIF   ('&CODE'(1,1) NE '(').NOTREG
&LEN     SETA  K'&CODE-2
&REG     SETC  '&CODE'(2,&LEN)
         LR    15,&REG            GET RETURN CODE
         AGO   .ENDIT
.NOTREG  ANOP
         LA    15,&CODE           LOAD CODE TO REG 15
         AGO   .ENDIT
.INVRET  MNOTE 12,'***** RETURN CODE PASSED GREATER THAN 4095 *****'
         MEXIT
.NOSAVE  MNOTE 12,'***** SAVEALL NOT ISSUED PRIOR TO ENDALL *****'
         MEXIT
.INVREG  MNOTE 12,'***** INVALID REGISTER USED FOR RETURN CODE "&CODE"'
         MEXIT
.COND1   LA    15,0               LOAD ZERO (NO CODE) TO REG 15
.ENDIT   ANOP
         RETURN     (14,12),T,RC=(15)
         MEND
CBTINFO  SAVEALL REG=(3,12)
         EJECT
         OPEN  (SYSIN,(INPUT),FILEIN,(INPUT),FILEOUT,(OUTPUT))
         SPACE
         GETMAIN R,LV=16364             GET STOR FOR NON-KEYW TBL CBT
*        GETMAIN R,LV=4096              GET STOR FOR NON-KEYW TBL CBT
         SPACE
         LR    R4,R1                    POINT AT IT
         USING NOKWTBL,R4               ADDR TO IT
         ST    R4,SAVENOKW              SAVE PTR TO BEGINNING OF IT
         SPACE
         GETMAIN R,LV=98304             GET STOR FOR KEYW USED TBL
         SPACE
         LR    R6,R1                    POINT AT IT
         ST    R6,SAVETABL              AND SAVE ADDRESS
         ST    R6,NEXTENTR              AND SAVE ADDRESS
         USING KWTBL,R6                 ADDR TO IT
         EJECT
GETLP    GET   SYSIN,NOKW               GET A KEYWORD
         CLC   0(8,R4),ENDTABLE         END OF TABLE
         BE    ENDREAD                      YES...END OF KEYWORD DS
         LA    R4,30(R4)                    NO MAX KEYWORD LENGTH=30
         B     GETLP                        AND LOOP
         SPACE
ENDREAD  SR    R10,R10                  INIT REG
         AH    R10,ONE                  SET THIS IS FIRST TOPIC
         SPACE
STARTUP  MVC   INREC(255),BLANK255      CLEAN OUT ENTRY FIRST
         GET   FILEIN,INREC             GET A RECORD
         TR    INREC(255),TRTABLE       SET TO UPPER CASE
         CLC   INREC(12),SECTION        START OF A TOPIC?
         BNE   STARTUP                     NO..CONTINUE
         SPACE
FIRSTONE MVC   TOPIC(58),INREC+5        SAVE TOPIC NAME
         LA    R2,TABLE                 START OF TABLE
         MVC   0(253,R2),INREC+2        MOVE TO TABLE
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD
         SPACE
LOOP1    MVC   INREC(255),BLANK255      CLEAN OUT ENTRY FIRST
         GET   FILEIN,INREC             GET A RECORD
         TR    INREC(255),TRTABLE       SET TO UPPER CASE
         CLC   INREC(12),SECTION        START OF A TOPIC?
         BE    ISNEW                       YES
         CLC   INREC(9),NEWFILE         START OF A TOPIC?
         BE    ISNEW                       YES
         LA    R2,255(R2)               BUMP TABLE POINTER
         MVC   0(253,R2),INREC+2        MOVE TO TABLE
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD
         B     LOOP1
         SPACE
ISNEW    LA    R2,255(R2)               BUMP POINTER
         MVC   0(255,R2),BLANK255       CLEAN OUT ENTRY FIRST
         MVC   0(72,R2),$EOMLINE        MOVE $EOM TO END OF ENTRY
         CVD   R10,DWORK                CONVERT COUNTER TO DECIMAL
         UNPK  TOPIC#,DWORK             UNPACK IT
         OI    TOPIC#+5,X'F0'           FIX IT UP
         MVC   TTOPIC(58),TOPIC         MOVE TOPIC TO TITLE LINE
         MVC   KTOPIC(58),TOPIC         MOVE TOPIC TO KEYWORD LINE
         PUT   FILEOUT,HEADER           MOVE HEADER TO OUTPUT
         PUT   FILEOUT,TITLE            MOVE TITLE  TO OUTPUT
         PUT   FILEOUT,KEYWORD          MOVE KEYWORD TO OUTPUT
         LA    R2,TABLE                 POINT TO BEGINNING OF TABLE
         SR    R11,R11                  INIT REG
         LH    R11,=H'4'                INITIALIZE AS 4TH LINE
         SPACE
         B     DOKEYW                   GO DO KEYWORDING OF 'L' LINES
         EJECT
ENDKW    MVC   OUTREC(72),BLANKL        MOVE BLANKLINE TO OUTPUT
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE
         OI    OUTREC+11,X'F0'          FIX IT UP
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER
         LA    R2,TABLE                 POINT TO BEGINNING OF ENTRY TBL
         SPACE
LOOP2    CLC   0(4,R2),=C'$EOM'         IS IT END OF ENTRY?
         BE    ENDENTRY                      YES
         MVC   OUTREC+12(243),0(R2)     MOVE A LINE TO OUTPUT REC,
*                                       MAKE ROOM FOR SEQ # + RDW
         MVC   OUTREC(4),RDW            MOVE IN DEFAULT RDW
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE
         OI    OUTREC+11,X'F0'          FIX IT UP
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT
         LA    R2,255(R2)               BUMP POINTER
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER
         B     LOOP2                         AND LOOP
         SPACE
ENDENTRY MVC   OUTREC+12(243),0(R2)     MOVE A $EOM LINE TO OUTPUT AND
*                                       MAKE ROOM FOR SEQ # + RDW
         MVC   OUTREC(4),RDW            MOVE IN DEFAULT RDW
         MVC   OUTREC+4(8),BLANK255     INIT SEQ # WITH BLANKS
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL
         UNPK  OUTREC+4(8),DWORK        UNPACK IT INTO LINE
         OI    OUTREC+11,X'F0'          FIX IT UP
         PUT   FILEOUT,OUTREC           MOVE LINE TO OUTPUT
         CLI   ENDSW,C'1'               WAS THIS THE LAST ENTRY?
         BE    ENDIT                        YES DO FINAL PROCESSING
         LA    R2,TABLE                 POINT TO BEGINNING OF TABLE
         AH    R10,ONE                  SET NEXT TOPIC NUMBER
         MVC   0(253,R2),INREC+2        MOVE TO TABLE
         MVC   70(185,R2),BLANK255      MOVE BLANKS TO REST OF RECORD
         MVC   TOPIC(58),INREC+5        SAVE TOPIC NAME
         LA    R2,255(R2)               BUMP TABLE POINTER
         B     LOOP1                    AND CONTINUE
         EJECT
DOKEYW   LA    R2,TABLE                 POINT TO BEGINNING OF TABLE
         MVC   NEXTENTR,SAVETABL        RESET NEXT ENTRY POINTER
         SPACE
MAINLP   BAL   R14,FINDWORD             GO GET A WORD
         CLC   0(5,R7),=C'$EOM '        END OF ENTRY?
         BE    BLDL                         YES... BUILD 'L' KW LINES
         BAL   R14,TESTTBL              IS IN TBL OF BAD WORDS?
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   MAINLP                       IN TABLE, GET NEXT WORD
         BAL   R14,CHKWORD              ADD TO TABLE IF NOT USED
         B     MAINLP                   LOOP TILL END OF DOCUMENT
         EJECT
EOFILE   MVI   ENDSW,C'1'               THIS IS THE END
         B     ISNEW                    DO IT ONE MORE TIME
         SPACE
ENDIT    L     R4,SAVENOKW              ADDRESS OF NOKEYWORD TABLE
         SPACE
         FREEMAIN R,LV=16364,A=(R4)      FREE STORAGE     CBT
*        FREEMAIN R,LV=4096,A=(R4)       FREE STORAGE     CBT
         SPACE 2
         L     R6,SAVETABL              ADDRESS OF KEYWD USED TABLE
         SPACE
         FREEMAIN R,LV=98304,A=(R6)     FREE STORAGE
         EJECT
         CLOSE (FILEIN,,FILEOUT,,SYSIN)
         SPACE
         ENDALL
         EJECT
FINDWORD CLI   0(R2),C' '               IS IT A BLANK?
         BNE   FINDEND                       NO.... WE HAVE A SOMETHING
FINDBMP1 LA    R2,1(R2)                      BUMP
         B     FINDWORD                      AND LOOP
         SPACE
FINDEND  LR    R7,R2                    SAVE PTR TO BEGINNING OF WORD
         SR    R8,R8                    GET READY TO GET LENGTH OF WORD
         AH    R8,ONE                   SET LENGTH TO ONE
FINDBL   CLI   0(R2),C' '               BLANK?
         BER   R14                           YES.. HAVE END OF WORD
         LA    R2,1(R2)                      NO... BUMP
         AH    R8,ONE                        ADD 1 TO LENGTH
         B     FINDBL                        AND LOOP
         EJECT
TESTTBL  L     R4,SAVENOKW              PT TO BEGIN OF BAD KEYWORDS
         BCTR  R8,0                     DECREMENT FOR EX INSTR
         SR    R15,R15                  RC = 0
TESTLP   CLC   0(8,R4),ENDTABLE         IS THIS END OF TABLE?
         BER   R14                           YES.. CONTINUE
         EX    R8,CLC                   IN TBL OF WORDS NOT TO BE USED?
         BE    BADWORD                       YES.. DON'T USE IT
         LA    R4,30(R4)                     NO... BUMP
         B     TESTLP                              AND LOOP
         SPACE
BADWORD  LA    R15,4                    RC = 4..WORD IS N.G.
         BR    R14                      AND RETURN
         EJECT
CHKWORD  L     R6,SAVETABL              PT TO BEGIN OF USED KEYWORDS
CHKWDLP  C     R6,NEXTENTR              END OF USED ENTRIES?
         BNL   TBLADD                        YES.. ADD WORD TO TBL
         EX    R8,CLCUSED               WAS WORD USED YET?
         BER   R14                           YES.. DONT ADD
         LA    R6,30(R6)                BUMP
         B     CHKWDLP                  AND LOOP
         SPACE
TBLADD   MVC   0(30,R6),BLANK255        BLANK OUT SPOT IN TABLE FIRST
         EX    R8,MVC                   NEW WORD ADD TO TBL
         LA    R6,30(R6)                BUMP TABLE POINTER
         ST    R6,NEXTENTR              AND SAVE FOR LATER
         BR    R14                      RETURN TO MAINLINE
         EJECT
BLDL     L     R6,SAVETABL              PT TO BEGINNING OF USED WORDS
         SR    R8,R8                    CLEAR A REG
BLDLLP   C     R6,NEXTENTR              END OF TABLE?
         BE    CHKLASTL                      YES... SEE IF ONE MORE KW
         CH    R8,TWO                   TWO WORDS THIS LINE?
         BE    PUTLCARD                      YES... DONE THIS LINE
         CH    R8,ONE                   ONE WORD THIS LINE?
         BE    DOLKW2                        YES... DONE THIS LINE
LMOVE1   MVC   LKW1(28),0(R6)           MOVE KEYWORD TO LINE
LBUMP    AH    R8,ONE                   ADD ONE TO COUNTER
         LA    R6,30(R6)                     BUMP
         B     BLDLLP                        AND LOOP
DOLKW2   MVC   LKW2(28),0(R6)           MOVE IN 2ND KEYWORD THIS LINE
         B     LBUMP                         AND BUMP
         SPACE
PUTLCARD MVC   LSEQ(8),BLANK255         INIT SEQ # WITH BLANKS
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL
         UNPK  LSEQ(8),DWORK            UNPACK IT INTO LINE
         OI    LSEQ+7,X'F0'             FIX IT UP
         MVC   OUTREC(255),BLANK255     CLEAR OUT OUTPUT WORKAREA
         MVC   OUTREC(72),LLINE         MOVE LLINE TO OUTPUT
         PUT   FILEOUT,OUTREC           WRITE IT OUT
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER
         MVC   LKW1(28),BLANK255        CLEAR OUT 1ST KEYWORD
         MVC   LKW2(28),BLANK255        CLEAR OUT 2ND KEYWORD
         SR    R8,R8                    RESET COUNTER
         B     LMOVE1                   AND CONTINUE
         SPACE
CHKLASTL CLC   LKW1(28),BLANK255        IS THE KEYWORD BLANK?
         BE    ENDKW                          YES... DONE KWD'ING
         MVC   LSEQ(8),BLANK255         INIT SEQ # WITH BLANKS
         CVD   R11,DWORK                CONVERT SEQ # TO DECIMAL
         UNPK  LSEQ(8),DWORK            UNPACK IT INTO LINE
         OI    LSEQ+7,X'F0'             FIX IT UP
         MVC   OUTREC(255),BLANK255     CLEAR OUT OUTPUT WORKAREA
         MVC   OUTREC(72),LLINE         MOVE LLINE TO OUTPUT
         PUT   FILEOUT,OUTREC           WRITE IT OUT
         AH    R11,ONE                  ADD ONE TO SEQ NUMBER
         B     ENDKW                    NOW DONE KWD'ING
         EJECT
*
*   HEADER RECORD FORMAT
*
HEADER   DS    0CL72
HRDW     DC    X'00480000'
HSEQ     DC    C'00000001'
H        DC    C'H'
         DC    C' '
FILENAME DC    C'E'
TOPIC#   DS    CL6
         DC    C' S=MVS C=G'
         DC    CL59' '
*
*   TITLE LINE  FORMAT
*
TITLE    DS    0CL72
TRDW     DC    X'00480000'
TSEQ     DC    C'00000002'
T        DC    C'T'
         DC    C' '
TTOPIC   DS    CL58
         DC    CL20' '
         SPACE
*
*   KEYWORD LINE FORMAT
*
KEYWORD  DS    0CL72
KRDW     DC    X'00480000'
KSEQ     DC    C'00000003'
K        DC    C'K'
         DC    C' '
KTOPIC   DS    CL58
         DC    CL20' '
         SPACE
*
* L KEYWORD LINE FORMAT
*
LLINE    DS    0CL72
LRDW     DC    X'00480000'
LSEQ     DS    CL8
L        DC    C'L'
         DC    C' '
LKW1     DS    CL28
         DC    C' '
LKW2     DS    CL28
         DC    C' '
         SPACE
*
*   BLANK LINE FORMAT
*
BLANKL   DS    0CL72
BRDW     DC    X'00480000'
BSEQ     DS    CL8
         DC    CL70' '
*
*   $EOM LINE FORMAT
*
$EOMLINE DS    0CL72
         DC    CL4'$EOM'
         DC    CL68' '
         SPACE
         EJECT
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,EODAD=ENDREAD,                    X
               LRECL=80,                                               X
               MACRF=(GM),RECFM=FB
         EJECT
         SPACE
FILEIN   DCB   DDNAME=FILEIN,DSORG=PS,EODAD=EOFILE,                    X
               MACRF=(GM)
         EJECT
FILEOUT  DCB   BLKSIZE=3120,DDNAME=FILEOUT,DSORG=PS,                   X
               LRECL=255,                                              X
               MACRF=(PM),RECFM=VB
         EJECT
         LTORG
         EJECT
*
* INPUT OUTPUT WORK AREA
*
         DS   0D                        FORCE DOUBLE WORD ALIGNMENT
DWORK    DC    D'0'
SAVENOKW DC    F'0'
SAVETABL DC    F'0'
SAVEENTR DC    F'0'
NEXTENTR DC    F'0'
ONE      DC    H'1'
TWO      DC    H'2'
RDW      DC    X'00540000'              DEFAULT RDW 84 BYTE RECORD
ENDTABLE DC    C'ENDTABLE'
NEWFILE  DC    C'//***FILE'
SECTION  DC    C'//***SECTION'
BLANK255 DC    255C' '
         SPACE
CLC      CLC   0(1,R4),0(R7)            IS THIS WORD NOT TO BE USED?
CLCUSED  CLC   0(1,R6),0(R7)            WAS WORD USED YET?
MVC      MVC   0(1,R6),0(R7)            ADD WORD TO USED TABLE
         SPACE
ENDSW    DS    CL1
SAVEPARM DS    CL3
UNPKWORK DS    CL6
TOPIC    DS    CL58
INREC    DS    CL255
OUTREC   DS    CL255
         SPACE
TRTABLE  DC    CL255' '
         ORG   TRTABLE+X'40'
         DC    C'          Ö.<(+×&&         !$*);^-/        |,%_>?'
         ORG   TRTABLE+X'70'
         DC    C'         `:#@''=" '
         ORG   TRTABLE+X'81'
         DC    C'ABCDEFGHI       '
         ORG   TRTABLE+X'91'
         DC    C'JKLMNOPQR       ~'
         ORG   TRTABLE+X'A2'
         DC    C'STUVWXYZ                      {'
         ORG   TRTABLE+C'A'
         DC    C'ABCDEFGHI      }'
         ORG   TRTABLE+C'J'
         DC    C'JKLMNOPQR      \ '
         ORG   TRTABLE+C'S'
         DC    C'STUVWXYZ      '
         ORG   TRTABLE+C'0'
         DC    C'0123456789      '
         ORG
         SPACE
TABLE    DS    3000CL255                 MAX 3000 LINES PER ENTRY
         EJECT
NOKWTBL  DSECT
NOKW     DS    CL16364            ABOUT 800 KEYWORDS      CBT
*OKW     DS    CL4096             ABOUT 800 KEYWORDS      CBT
         EJECT
KWTBL    DSECT
KW       DS    CL49152
         END
