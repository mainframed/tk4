         PRINT NOGEN
LASTIPL  BEGIN AUTHOR=JAM,SAVE=(WORKAREA,WORKLENG,0)
         SPACE
         MVC   TIMCMSG(40),CMESSAGE    MOVE MESSAGE TO WORKAREA
         MVC   TIMIMSG(40),IMESSAGE    MOVE MESSAGE TO WORKAREA
         L     R2,X'10'                POINT AT CVT ADDRESS
         L     R2,X'CC'(0,R2)          LOAD CVTUSER ADDRESS
         LTR   R2,R2                   IF USERCVT EXISTS
         BNZ   CHKWCVT                    THEN CHECK FOR WSRCCCVT
         MVC   TIMIMSG(40),NOUSRCVT    MOVE IN NO UCVTMSG MESSAGE
         B     PUTITIME                GO ISSUE MESSAGE
         SPACE
CHKWCVT  EQU   *
         CLC   X'40'(8,R2),WSRCCCVT    IF WSRCCCVT INSTALLED
         BE    FMTITIME                   THEN FORMAT IPL MESSAGE
         MVC   TIMIMSG(40),NOWSRCVT    MOVE IN NO WSRCCCVT MESSAGE
         B     PUTITIME                GO ISSUE MESSAGE
         SPACE
FMTITIME EQU   *
         LA    R2,X'48'(0,R2)          POINT AT IPL TIME AND DATE
         MVC   IPLTIME(8),0(R2)        MOVE IPLTIME TO WORKAREA
         ED    TIMIMSG+MITOFF(12),IPLTIME       EDIT IN THE TIME
         ED    TIMIMSG+MIDOFF(08),IPLDATE+1     EDIT IN THE DATE
         SPACE
PUTITIME EQU   *
         TPUT  TIMIMSG,40              ISSUE THE MESSAGE TO USER
         LTR   R15,R15                 CHECK RETURN CODE
         BNZ   FINISH                  BRANCH IF NOT GOOD
         TIME  DEC                     GET CURRENT TIME
         STM   R0,R1,NOWTIME           PUT CURRENT TIME IN WORKAREA
         ED    TIMCMSG+MCTOFF(12),NOWTIME       EDIT IN THE TIME
         ED    TIMCMSG+MCDOFF(08),NOWDATE+1     EDIT IN THE DATE
         TPUT  TIMCMSG,40              ISSUE THE MESSAGE TO USER
         SPACE
FINISH   EQU   *
         FINISH RC=(R15)               RETURN TO CALLER
         EJECT
CMESSAGE DS    0CL40
         DC    CL18'CURRENT TIME-DATE:'
MSGCTME  DC    XL13'4021207A20207A20204B202040'
MSGCDTE  DC    XL09'4021204B2020204040'
MCTOFF   EQU   MSGCTME-CMESSAGE
MCDOFF   EQU   MSGCDTE-CMESSAGE
         SPACE
IMESSAGE DS    0CL40
         DC    CL18'THE LAST IPL WAS: '
MSGITME  DC    XL13'4021207A20207A20204B202040'
MSGIDTE  DC    XL09'4021204B2020204040'
MITOFF   EQU   MSGITME-IMESSAGE
MIDOFF   EQU   MSGIDTE-IMESSAGE
         SPACE
WSRCCCVT DC    CL08'WSRCCCVT'          WSRCC CVT LITERAL
NOUSRCVT DC    CL40'LAST IPL TIME NOT AVAILABLE, NO USER CVT'
NOWSRCVT DC    CL40'LAST IPL TIME NOT AVAILABLE, NO WSRCCCVT'
         SPACE 2
WORKAREA DSECT
SAVEAREA DS    18F                     OS SAVEAREA
IPLTIME  DS    F                       IPL TIME FROM USERCVT
IPLDATE  DS    F                       IPL DATE FROM USERCVT
NOWTIME  DS    F                       CURRENT TIME
NOWDATE  DS    F                       CURRENT DATE
TIMCMSG  DS    CL40                    CURRENT TIME & DATE MESSAGE
TIMIMSG  DS    CL40                    LAST IPL TIME & DATE MESSAGE
WORKLENG EQU   *-WORKAREA              LENGTH OF WORK AREA
         SPACE 2
         END
