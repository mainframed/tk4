         TITLE 'IFAUX1XA - MODULE COMMENTARY'
***************************************************************
*                                                             *
*        NAME      .IFAUX1XA VERSION 01.00                    *
*                                                             *
*        TITLE     .IFASMFDP USER1 EXIT                       *
*                                                             *
*        FUNCTION/ .PROVIDE A GENERAL PURPOSE USER1 EXIT      *
*        PURPOSE    FOR IFASMFDP TO SELECT ONLY THOSE         *
*                   INPUT RECORDS THAT WERE PRODUCED BY AN    *
*                   MVS/XA SYSTEM.  RECORDS PRODUCED BY AN    *
*                   MVS SYSTEM WHICH IS NOT XA ARE FILTERED   *
*                   OUT BY THIS ROUTINE SO THAT THEY ARE NOT  *
*                   PROCESSED BY IFASMFDP.                    *
*                                                             *
*        MODULE    .RENT,REUS,REFR,AC=0,AMODE(24),RMODE(24)   *
*        ATTRIBUTES                                           *
*                  .BECAUSE IFASMFDP IS AN AUTHORIZED PROGRAM,*
*                   THIS EXIT MUST RESIDE IN AN AUTHORIZED    *
*                   LIBRARY IN ORDER TO BE USED WITH          *
*                   IFASMFDP.  IF THIS EXIT IS NOT IN AN      *
*                   AUTHORIZED LIBRARY, AND USE IS ATTEMPTED  *
*                   WITH IFASMFDP RUNNING AUTHORIZED, AN      *
*                   S306-C ABEND WILL OCCUR.                  *
*                                                             *
*        METHOD                                               *
*          OF                                                 *
*        INVOCATION                                           *
*                  .IFASMFDP WILL NOT REFERENCE THIS EXIT     *
*                   UNLESS IT IS EXPLICITLY DIRECTED TO DO    *
*                   SO.  TO USE THIS EXIT WITH IFASMFDP,      *
*                   THE USER MUST SUPPLY A                    *
*                                                             *
*                          USER1(IFAUX1XA)                    *
*                                                             *
*                   CONTROL STATEMENT TO IFASMFDP.            *
*                                                             *
*                                                             *
*        ENTRY     .IFAUX1XA                                  *
*                                                             *
*        INPUT     .TWO AREAS IN MAIN STORAGE ARE PASSED      *
*                   TO THIS EXIT FROM IFASMFDP: A THREE-WORD  *
*                   USER EXIT PARAMETER LIST, AND A           *
*                   THREE-WORD USER EXIT WORK AREA:           *
*                                                             *
*                   ____________                              *
*                   ×          ×                              *
*                   ×  REG 1   ×                              *
*                   ×__________×                              *
*                        ×                                    *
*                        ×                                    *
*                        ---> THREE-WORD USER EXIT PARM LIST  *
*                   __________________________________        *
*                   ×          ×          ×          ×        *
*                   ×    ->    ×USER EXIT ×USER EXIT ×        *
*                   ×USER EXIT ×DEPENDENT ×DEPENDENT ×        *
*                   ×WORK AREA ×   DATA   ×   DATA   ×        *
*                   ×__________×__________×__________×        *
*                        ×                                    *
*                        ×                                    *
*                        ---> THREE-WORD USER EXIT WORK AREA  *
*                   __________________________________        *
*                   ×          ×          ×          ×        *
*                   × IFASMFDP × IFASMFDP ×   USER   ×        *
*                   ×   REG 0  ×   REG 1  ×   WORD   ×        *
*                   × ON ENTRY × ON ENTRY ×INIT ZEROS×        *
*                   ×__________×__________×__________×        *
*                                                             *
*                                                             *
*                  .REGISTERS ON ENTRY TO THIS EXIT ARE       *
*                   ASSUMED TO HAVE BEEN INITIALIZED AS       *
*                   FOLLOWS:                                  *
*                        R1   AS DESCRIBED ABOVE;             *
*                        R13  -> 18 WORD REGISTER SAVE AREA;  *
*                        R14  RETURN ADDRESS TO IFASMFDP;     *
*                        R15  ENTRY POINT ADDRESS OF THE EXIT.*
*                                                             *
*        OUTPUT    .THIS EXIT SAVES AND RESTORES ALL REGISTERS*
*                   EXCEPT FOR REGISTER 15 WHICH CONTAINS A   *
*                   RETURN CODE ON RETURN TO IFASMFDP.  THE   *
*                   RETURN CODE VALUES ARE DESCRIBED BELOW.   *
*                                                             *
*                                                             *
*        EXIT      .RETURN TO IFASMFDP VIA BR14.              *
*                                                             *
*                                                             *
*        RETURN    .00   ALLOW NORMAL IFASMFDP PROCESSING     *
*        CODES           TO CONTINUE;                         *
*                  .04   THE RECORD SHOULD NOT BE FURTHER     *
*                        PROCESSED BY IFASMFDP;               *
*                   ANY OTHER RETURN CODE INDICATES THAT A    *
*                   PROBLEM WAS ENCOUNTERED, AND IFASMFDP IS  *
*                   NOT TO INVOKE THIS EXIT AGAIN.  IF USE OF *
*                   THE EXIT IS DISCONTINUED, IFASMFDP ISSUES *
*                   MESSAGE IFA016I.                          *
*                                                             *
*                                                             *
*        RESTRICTIONS/                                        *
*        LIMITATIONS                                          *
*                  .THIS EXIT IS DESIGNED FOR USE WITH THE    *
*                   VERSION OF IFASMFDP THAT IS AVAILABLE     *
*                   WITH MVS/SE2 OR LATER LEVELS OF THE       *
*                   MVS OPERATING SYSTEM.  WE ARE NOT AWARE   *
*                   OF ANY APAR FIXES THAT NEED TO BE         *
*                   APPLIED TO IFASMFDP IN ORDER FOR THIS     *
*                   EXIT TO WORK CORRECTLY.                   *
*                                                             *
*        PACKAGING/                                           *
*        ASSEMBLY                                             *
*                  .THIS EXIT IS DESIGNED TO BE AN            *
*                   INDEPENDENT LOAD MODULE IN SYS1.LINKLIB.  *
*                   IT MUST RESIDE IN AN AUTHORIZED LIBRARY   *
*                   IN ORDER TO BE USED WITH IFASMFDP WHEN    *
*                   IFASMFDP IS RUNNING AS AN AUTHORIZED      *
*                   PROGRAM (IFASMFDP DOES NOT HAVE TO        *
*                   BE USING AN AUTHORIZED FUNCTION E.G.,     *
*                   CLEAR, IN ORDER TO BE RUNNING AS AN       *
*                   AUTHORIZED PROGRAM).                      *
*                                                             *
*        UPDATE    .EXIT DEVELOPED, WRITTEN, AND TESTED.      *
*        HISTORY    TO SUPPORT THE CONVERSION TO MVS/XA.      *
*                                                 04/05/84 TJA*
*                                                             *
*        FACILITIES/FEATURES                                  *
*                                                             *
*               THIS EXIT IS DESIGNED FOR USE WITH THE        *
*        IFASMFDP PROGRAM THAT IS AVAILABLE IN MVS/SE2 OR     *
*        LATER LEVELS OF MVS.  THIS EXIT IS INTENDED TO MAKE  *
*        THE IFASMFDP PROGRAM MORE USEFUL BY ALLOWING THE     *
*        USER OF IFASMFDP TO SELECT ONLY THOSE RECORDS THAT   *
*        WERE GENERATED BY AN MVS/XA SYSTEM, THUS EASING THE  *
*        COEXISTENCE OF BOTH TYPES OF SYSTEM DURING THE       *
*        CONVERSION PROCESS.                                  *
*                                                             *
*                                                             *
***************************************************************
***                                                         ***
***>>>>>>>>>>>>>>C U I C   D I S C L A I M E R<<<<<<<<<<<<<<***
***                                                         ***
***************************************************************
***            THE INFORMATION OR MATERIAL BEING PROVIDED   ***
***      BY COMMERCIAL UNION INSURANCE COMPANY (CUIC),      ***
***      WHETHER IN HARD COPY OR MACHINE READABLE FORM,     ***
***      HAS BEEN DEVELOPED BY CUIC FOR ITS OWN PURPOSE     ***
***      AND FOR USE ON ITS OWN EQUIPMENT AND WITHIN ITS    ***
***      OWN DATA PROCESSING SYSTEM.  CUIC MAKES NO         ***
***      REPRESENTATIONS OR WARRANTIES WHATSOEVER WITH      ***
***      RESPECT TO THE INFORMATION OR MATERIAL FURNISHED   ***
***      HEREUNDER, EXPRESSED OR IMPLIED, INCLUDING BUT     ***
***      NOT LIMITED TO ANY REPRESENTATION OR WARRANTY OF   ***
***      MERCHANTABILITY OR FITNESS FOR ANY PARTICULAR USE  ***
***      OR PURPOSE OR THAT THE USE OF THE INFORMATION OR   ***
***      MATERIAL WILL NOT INFRINGE ANY PATENT, COPYRIGHT,  ***
***      TRADEMARK, OR OTHER PROPRIETARY INTEREST.  YOU     ***
***      ARE, THEREFORE, ACCEPTING THIS INFORMATION OR      ***
***      MATERIAL ON AN "AS IS" BASIS AND WILL BE USING IT  ***
***      AT YOUR OWN RISK.  NEITHER CUIC NOR ANY OF ITS     ***
***      AFFILIATES SHALL BE LIABLE WITH RESPECT TO ANY     ***
***      CLAIM, ACTION, OR DEMAND BY ANY USER OR OTHER      ***
***      PARTY (INCLUDING ANY CLAIM, ACTION, OR DEMAND FOR  ***
***      CONSEQUENTIAL DAMAGES EVEN IF CUIC HAS BEEN        ***
***      ADVISED OF THE POSSIBILITY OF SUCH DAMAGES)        ***
***      ARISING FROM THE USE OF THE INFORMATION OR THE     ***
***      MATERIALS AND CONCEPTS RELATED THERETO.            ***
***      FURTHERMORE, CUIC WILL NOT MAINTAIN, CORRECT, OR   ***
***      UPDATE THIS INFORMATION OR MATERIAL IN THE         ***
***      FUTURE.                                            ***
***                                              01/26/83   ***
***************************************************************
*                                                             *
*              DIRECT INQUIRIES TO THE                        *
*        AUTHOR.....                                          *
*                                                             *
*                   THOMAS J. AUBREY                          *
*                   MIS                                       *
*                   COMMERCIAL UNION ASSURANCE COMPANIES      *
*                   ONE BEACON STREET                         *
*                   BOSTON, MASSACHUSETTS  02108              *
*                   TELEPHONE (617) 725-6208                  *
*                                                             *
*                                                             *
***************************************************************
         TITLE 'IFAUX1XA - MODULE EQUATES'
IFAUX1XA START 000000
IFAUX1XA AMODE 24
IFAUX1XA RMODE 24
*
*
*        PHYSICAL REGISTER EQUATES
R0       EQU   00
R1       EQU   01
R2       EQU   02
R3       EQU   03
R4       EQU   04
R5       EQU   05
R6       EQU   06
R7       EQU   07
R8       EQU   08
R9       EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*
*        LOGICAL REGISTER EQUATES
RWORK0   EQU   00                  SUBROUTINE PARM/WORK REGISTER
RWORK1   EQU   01                  SUBROUTINE PARM/WORK REGISTER
RWORK2   EQU   02                  SUBROUTINE PARM/WORK REGISTER
RWORK3   EQU   03                  SUBROUTINE PARM/WORK REGISTER
RWORK4   EQU   04                  SUBROUTINE PARM/WORK REGISTER
RWORK5   EQU   05                  SUBROUTINE PARM/WORK REGISTER
RWORK6   EQU   06                  SUBROUTINE PARM/WORK REGISTER
RWORK7   EQU   07                  SUBROUTINE PARM/WORK REGISTER
RBASE1   EQU   08                  MODULE BASE REGISTER # 1
RBASE2   EQU   09                  MODULE BASE REGISTER # 2
RUEPL    EQU   10                  -> USER EXIT PARAMETER LIST
RUEWA    EQU   11                  -> USER EXIT WORK AREA
RSMFAD   EQU   12                  -> SMF RECORD
RSAVE    EQU   13                  -> REGISTER SAVE AREA
RLINK    EQU   14                  SUBROUTINE LINKAGE REGISTER
RENTRY   EQU   15                  ENTRY ADDRESS REGISTER
*
*
*        IFASMFDP USER EXIT RETURN CODE EQUATES
IFARC00  EQU   00      (ALL EXITS) CONTINUE WITH NORMAL PROCESSING
IFARC04  EQU   04      (U1 & U2)   DON'T WRITE THE SMF RECORD OR       X
                       (U3)        DISCONTINUE USE OF EXIT FOR DATASET
IFARC08  EQU   08      (U1 & U2)   DISCONTINUE USE OF EXIT FOR DATASET
         TITLE 'IFAUX1XA - USER EXIT PARAMETER LIST MAPPINGS'
UEPLMAP  DSECT  ,                  USER EXIT PARAMETER LIST
***************************************************************
*                                                             *
*        USER EXIT PARAMETER LIST.                            *
*        THE ADDRESS OF THIS AREA IS PASSED IN REGISTER R1    *
*        TO EACH USER EXIT TAKEN FROM IFASMFDP.  THE FIRST    *
*        WORD CONTAINS THE ADDRESS OF A THREE-WORD USER       *
*        WORK AREA.  THE CONTENTS OF THE SECOND AND THIRD     *
*        WORDS DEPEND ON THE EXIT BEING TAKEN.                *
*                                                             *
***************************************************************
*
UEPL     DS   0F                   START OF PARAMETER LIST
*
*        MAP THE PARAMETER LIST PASSED TO USER EXIT # 1
         ORG   UEPL                MAPPING FOR EXIT # 1
U1UEWA   DS    A                   -> USER EXIT WORK AREA
U1SMFAD  DS    A                   -> SMF RECORD
U1INDDAD DS    A                   -> INDD DDNAME
*
*        MAP THE PARAMETER LIST PASSED TO USER EXIT # 2
         ORG   UEPL                MAPPING FOR EXIT # 2
U2UEWA   DS    A                   -> USER EXIT WORK AREA
U2SMFAD  DS    A                   -> SMF RECORD
U2OUDDAD DS    A                   -> OUTDD DDNAME
*
*        MAP THE PARAMETER LIST PASSED TO USER EXIT # 3
         ORG   UEPL                MAPPING FOR EXIT # 3
U3UEWA   DS    A                   -> USER EXIT WORK AREA
U3OUDCB  DS    A                   -> OUTPUT DATA SET DCB
U3OUDDAD DS    A                   -> OUTDD DDNAME
         SPACE 3
UEWAMAP  DSECT  ,                  USER EXIT WORK AREA
***************************************************************
*                                                             *
*        USER EXIT WORK AREA.                                 *
*        THIS AREA IS POINTED TO BY THE FIRST WORD OF THE     *
*        USER EXIT PARAMETER LIST PASSED TO ALL USER EXITS    *
*        TAKEN FROM IFASMFDP.                                 *
*                                                             *
***************************************************************
*
UEWA     DS   0F                   START OF WORK AREA
UEWAR0   DS    A                   R0 CONTENTS ON ENTRY TO IFASMFDP
UEWAR1   DS    A                   R1 CONTENTS ON ENTRY TO IFASMFDP
UEWAUSER DS    A                   USER WORD
         TITLE 'IFAUX1XA - SMF RECORD MAPPINGS'
SMFREC   DSECT  ,                  SMF RECORD DSECT
         ORG   SMFREC              START OF SMF RECORD
***************************************************************
*                                                             *
*        MAP THE STANDARD SMF RECORD HEADER                   *
*                                                             *
***************************************************************
*
*        SMF RECORD HEADER
SMFLEN   DS    YL2            RECORD LENGTH
SMFSEG   DS    YL2            SEGMENT DESCRIPTOR
SMFFLG   DS    BL1            SYSTEM INDICATOR
SMFRTY   DS    FL1            RECORD TYPE
SMFTME   DS    FL4            TIME IN 1/100 SEC REC WAS MOVED TO BFR
SMFDTE   DS    PL4            DATE REC WAS MOVED TO BFR
SMFSID   DS    CL4            SYSTEM IDENTIFICATION
         TITLE 'IFAUX1XA - EXIT ROUTINE MAINLINE ROUTINE'
*
*
*
IFAUX1XA CSECT  ,                  RESUME CSECT DEFINITION
         USING *,RENTRY            TEMPORARY ADDRESSABILITY
         B     BYID                BYPASS MODULE IDENTIFIER
         DC    AL1(NDID-*)         MODULE IDENTIFIER LENGTH
         DC    CL8'IFAUX1XA'       MODULE/CSECT NAME
         DC    C' '                     SPACE
         DC    C'VERSION 01.00'    VERSION/LEVEL NUMBER
         DC    C' '                     SPACE
         DC    C'&SYSDATE'         ASSEMBLY DATE
         DC    C' '                     SPACE
         DC    C'&SYSTIME'         ASSEMBLY TIME
         DC    C' '                     SPACE
         DC    C' '                FIX LEVEL
NDID     DS   0C                   END OF MODULE IDENTIFIER
BYID     DS   0H                   BYPASS MODULE IDENTIFIER
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    RWORK7,RSAVE        COPY SAVE AREA POINTER
         EJECT  ,
*
*
*        ESTABLISH MODULE ADDRESSABILITY
         LA    RBASE1,00(,RENTRY)  SET UP BASE REG # 1
         LA    RBASE2,2048         SET
         AR    RBASE2,RBASE2           UP
         AR    RBASE2,RBASE1              BASE REG # 2
         USING IFAUX1XA,RBASE1,RBASE2     ADDRESSABILITY
         DROP  RENTRY              DROP TEMPORARY BASE
*
*
*        ESTABLISH PARAMETER LIST ADDRESSABILITY
         LA    RUEPL,00(,R1)       COPY PARM LIST ADDRESS
         USING UEPLMAP,RUEPL       PARAMETER LIST ADDRESSABILITY
*
*
*        ESTABLISH WORK AREA ADDRESSABILITY
         L     RUEWA,U1UEWA        -> USER EXIT WORK AREA
         USING UEWAMAP,RUEWA       WORK AREA ADDRESSABILITY
*
*
*        ESTABLISH ADDRESSABILITY TO THE SMF RECORD.
         L     RSMFAD,U1SMFAD      -> SMF RECORD
         USING SMFREC,RSMFAD       SMF RECORD ADDRESSABILITY
*
*
*        IF THE RECORD WAS NOT GENERATED ON A VS2(MVS/370) SYSTEM,
*        OR WAS GENERATED ON AN MVS SYSTEM WHICH WAS NOT
*        AT XA LEVEL, DROP THE RECORD.
FILTER   DS   0H                   ADDRESS IF PARM LIST INVALID
         TM    SMFFLG,X'02'        RECORD GENERATED BY MVS(VS2) ??
         BZ    REJECT              DROP THE RECORD IF NOT
         TM    SMFFLG,X'04'        RECORD GENERATED BY MVS/XA   ??
         BZ    REJECT              DROP THE RECORD IF NOT
         B     ACCEPT              PROCESS THE RECORD
*
*
*        SET RETURN CODE TO REJECT THE RECORD:
REJECT   DS   0H
         LA    RENTRY,IFARC04      SET RC=04
         B     RETURN              ANDRETURN TO IFASMFDP
*
*
*        SET RETURN CODE TO ACCEPT THE RECORD:
ACCEPT   DS   0H
         LA    RENTRY,IFARC00      SET RC=00
         B     RETURN              ANDRETURN TO IFASMFDP
*
*
*        RESTORE REGISTERS AND RETURN TO THE CALLER.
RETURN   DS   0H                   ADDRESS IF PARM LIST INVALID
         L     R14,12(,R13)        RELOAD CALLER'S REGISTER  14
         LTR   R15,R15             SET CC FROM RC
         LM    R0,R12,20(R13)      RELOAD CALLER'S REGISTERS 00-12
         BR    R14                 RETURN TO CALLER
         SPACE 5
*
*        DEFINE THE MODULE'S LITERAL POOL
         LTORG  ,
         END    ,            END OF CSECT IFAUX1XA
