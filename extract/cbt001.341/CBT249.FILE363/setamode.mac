         MACRO
&LABEL   SETAMODE &REG1,&REG2, NON-ZERO WORK REGISTER(S)               X
               &AMODE=,        SPECIFY ADDRESSING MODE                 X
               &AMASK=,        ADDR OF FULLWORD X'80000000'            X
               &S370=MVS,      S/370 SYSTEM EXECUTION CAPABILITY       X
               &BALR=BALR,     SPECIFY OPCODE FOR BALR/BASR            X
               &ASMHV2=YES,    IF ASM H V2 IS BEING USED               X
               &CVTMAP=YES     IF MVS/XA CVT MAP IS IN PROGRAM
.**************************************************************
.*                                                            *
.*             S E T A M O D E   V E R S I O N   02.00        *
.*                                                            *
.*             THIS  IS A GENERAL-PURPOSE MACRO WRITTEN       *
.*       AT COMMERCIAL UNION TO FACILITATE SWITCHING          *
.*       BETWEEN 24-BIT AND 31-BIT ADDRESSING MODE IN         *
.*       MVS/XA.  THIS MACRO IS CAPABLE OF GENERATING         *
.*       CODE THAT CAN EXECUTE ON BOTH MVS/370 AND            *
.*       MVS/XA SYSTEMS, BUT IT CAN ALSO GENERATE A           *
.*       SHORT EXPANSION, NOT INCLUDING THE TEST FOR          *
.*       SYSTEM TYPE, WHICH WILL EXECUTE ONLY ON AN XA        *
.*       SYSTEM (IN EITHER 24-BIT OR 31-BIT MODE).            *
.*       ASSEMBLER H VERSION 2 IS REQUIRED TO ASSEMBLE        *
.*       THIS MACRO (UNLESS ASMHV2=NO IS SPECIFIED),          *
.*       BECAUSE THE EXPANSION INCLUDES A "BSM"               *
.*       INSTRUCTION WHICH IS NEW FOR XA AND IS               *
.*       RECOGNIZED ONLY BY ASSEMBLER H VERSION 2, OR A       *
.*       COMPARABLE ASSEMBLER.                                *
.*                                                            *
.*                                                            *
.*       WHEN &S370=NO IS SPECIFIED, &REG1 SPECIFIES A        *
.*       NON-ZERO WORK REGISTER THAT IS USED TO LOAD          *
.*       THE AMODE VALUE AND BRANCH ADDRESS FOR THE BSM       *
.*       INSTRUCTION.  &REG2 IS NOT USED WHEN &S370=NO,       *
.*       AND AN MNOTE(00) WARNING MESSAGE WILL BE             *
.*       GENERATED BY THIS MACRO IF &REG2 IS USED WITH        *
.*       &S370=NO.                                            *
.*                                                            *
.*       WHEN &S370=MVS, &REG1 AND &REG2 SPECIFY WORK         *
.*       REGISTERS (NOT ZERO) THAT ARE USED TO SET UP         *
.*       VALUES FOR THE BSM INSTRUCTION IN MVS/XA.  THE       *
.*       GENERATED BSM INSTRUCTION SAVES THE CURRENT          *
.*       AMODE VALUE IN THE HIGH-ORDER BIT OF &REG1.          *
.*       WHEN EXECUTING ON AN MVS/370 SYSTEM, A BALR          *
.*       INSTRUCTION IS EXECUTED INSTEAD OF BSM;              *
.*       BECAUSE THE ILC FOR BALR IS B'01', BALR CLEARS       *
.*       THE HIGH-ORDER BIT OF &REG1 (AS IF AMODE 24          *
.*       HAD BEEN IN EFFECT).  IF TWO REGISTERS AREN'T        *
.*       AVAILABLE, THE SECOND REGISTER CAN BE OMITTED;       *
.*       OMITTING &REG2 WILL INCREASE THE LENGTH OF THE       *
.*       GENERATED CODE, BUT THE RESULT WILL BE THE           *
.*       SAME.  WHEN TWO REGISTERS ARE SPECIFIED, THEY        *
.*       MUST DESIGNATE DIFFERENT, NON-ZERO, REGISTERS.       *
.*                                                            *
.*                                                            *
.*       THREE VALUES ARE SUPPORTED FOR &AMODE:               *
.*                                                            *
.*             -"24"      SETS AMODE 24, AND SAVES THE        *
.*                        CURRENT AMODE IN THE                *
.*                        HIGH-ORDER BIT OF &REG1;            *
.*                                                            *
.*             -"31"      SETS AMODE 31, AND SAVES THE        *
.*                        CURRENT AMODE IN THE                *
.*                        HIGH-ORDER BIT OF &REG1;            *
.*                                                            *
.*             -"RESET"   USES THE AMODE BIT THAT WAS         *
.*                        SAVED IN THE HIGH-ORDER BIT         *
.*                        OF &REG1 TO SET THE AMODE;          *
.*                        THE CURRENT AMODE IS SAVED IN       *
.*                        THE HIGH-ORDER BIT OF &REG1;        *
.*                                                            *
.*                                                            *
.*       THE CODE GENERATED BY THIS MACRO REQUIRES            *
.*       ACCESS TO A FULLWORD OF X'80000000' FOR              *
.*       EXTRACTING AND MANIPULATING THE AMODE BIT IN         *
.*       THE WORK REGISTERS.  IF THE USER DOES NOT            *
.*       SUPPLY THE ADDRESS OF SUCH A FULLWORD USING          *
.*       THE AMASK= KEYWORD, A FULLWORD CONSTANT IS           *
.*       GENERATED INLINE.  TO SAVE ON STORAGE, THE           *
.*       USER CAN SUPPLY THE ADDRESS OF SUCH A FULLWORD       *
.*       TO BE USED BY THE MACRO EXPANSION.  IF THE           *
.*       USER PROVIDES SUCH AN ADDRESS, USING THE             *
.*       AMASK= KEYWORD, THE INLINE FULLWORD IS NOT           *
.*       GENERATED, SAVING FOUR BYTES WITH EACH USE OF        *
.*       THIS MACRO.  THE SYMBOL SUPPLIED VIA AMASK=          *
.*       MUST DESIGNATE THE ADDRESS OF A FULLWORD IN          *
.*       STORAGE CONTAINING THE VALUE X'80000000', OR         *
.*       BE THE VALUE IN REGISTER NOTATION (DOUBLE            *
.*       ENCLOSING PARENTHESES) OF A GENERAL REGISTER         *
.*       CONTAINING THE VALUE X'80000000'.                    *
.*                                                            *
.*                                                            *
.*       WHEN S370=MVS, THE GENERATED CODE INTERROGATES       *
.*       THE CVT TO DETERMINE WHETHER IT IS EXECUTING         *
.*       ON MVS/370 OR MVS/XA.  IF THE MVS/XA CVT             *
.*       MAPPING MACRO IS NOT EXPANDED IN THE ASSEMBLY        *
.*       CONTAINING SETAMODE, THE USER SHOULD SPECIFY         *
.*       CVTMAP=NO ON THE SETAMODE MACRO.  THE                *
.*       GENERATED CODE WILL THEN USE HEXADECIMAL             *
.*       VALUES FOR "CVTDCB" AND "CVTMVSE" INSTEAD OF         *
.*       THE SYMBOLS FROM THE CVT MACRO.  THE SETAMODE        *
.*       DEFAULT IS TO USE THE SYMBOLS (&CVTMAP=YES) SO       *
.*       THAT THEY APPEAR ON THE ASSEMBLY CROSS               *
.*       REFERENCE LISTING.  THE CVT MAPPING MACRO IS         *
.*       IN SYS1.AMODGEN ON THE DLIB VOLUME, AND IS           *
.*       CALLED "CVT".                                        *
.*                                                            *
.*                                                            *
.**************************************************************
.**************************************************************
.**                                                         ***
.**>>>>>>>>>>>>>>C U I C   D I S C L A I M E R<<<<<<<<<<<<<<***
.**                                                         ***
.**************************************************************
.**            THE INFORMATION OR MATERIAL BEING PROVIDED   ***
.**      BY COMMERCIAL UNION INSURANCE COMPANY (CUIC),      ***
.**      WHETHER IN HARD COPY OR MACHINE READABLE FORM,     ***
.**      HAS BEEN DEVELOPED BY CUIC FOR ITS OWN PURPOSE     ***
.**      AND FOR USE ON ITS OWN EQUIPMENT AND WITHIN ITS    ***
.**      OWN DATA PROCESSING SYSTEM.  CUIC MAKES NO         ***
.**      REPRESENTATIONS OR WARRANTIES WHATSOEVER WITH      ***
.**      RESPECT TO THE INFORMATION OR MATERIAL FURNISHED   ***
.**      HEREUNDER, EXPRESSED OR IMPLIED, INCLUDING BUT     ***
.**      NOT LIMITED TO ANY REPRESENTATION OR WARRANTY OF   ***
.**      MERCHANTABILITY OR FITNESS FOR ANY PARTICULAR USE  ***
.**      OR PURPOSE OR THAT THE USE OF THE INFORMATION OR   ***
.**      MATERIAL WILL NOT INFRINGE ANY PATENT, COPYRIGHT,  ***
.**      TRADEMARK, OR OTHER PROPRIETARY INTEREST.  YOU     ***
.**      ARE, THEREFORE, ACCEPTING THIS INFORMATION OR      ***
.**      MATERIAL ON AN "AS IS" BASIS AND WILL BE USING IT  ***
.**      AT YOUR OWN RISK.  NEITHER CUIC NOR ANY OF ITS     ***
.**      AFFILIATES SHALL BE LIABLE WITH RESPECT TO ANY     ***
.**      CLAIM, ACTION, OR DEMAND BY ANY USER OR OTHER      ***
.**      PARTY (INCLUDING ANY CLAIM, ACTION, OR DEMAND FOR  ***
.**      CONSEQUENTIAL DAMAGES EVEN IF CUIC HAS BEEN        ***
.**      ADVISED OF THE POSSIBILITY OF SUCH DAMAGES)        ***
.**      ARISING FROM THE USE OF THE INFORMATION OR THE     ***
.**      MATERIALS AND CONCEPTS RELATED THERETO.            ***
.**      FURTHERMORE, CUIC WILL NOT MAINTAIN, CORRECT, OR   ***
.**      UPDATE THIS INFORMATION OR MATERIAL IN THE         ***
.**      FUTURE.                                            ***
.**                                              01/26/83   ***
.**************************************************************
.*                                                            *
.*              DIRECT INQUIRIES TO THE                       *
.*       AUTHOR........                                       *
.*                                                            *
.*                   THOMAS J. AUBREY                         *
.*                   C.U. AUTOMATION SERVICES                 *
.*                   COMMERCIAL UNION INSURANCE COMPANIES     *
.*                   ONE BEACON STREET - THIRTEENTH FLOOR     *
.*                   BOSTON, MASSACHUSETTS  02108             *
.*                   TELEPHONE: (617) 725-6208                *
.*                                                            *
.**************************************************************
.*************************************************
.*                                               *
.*       DEFINE THE SET SYMBOLS USED IN THIS     *
.*       MACRO, AND ASSIGN INITIAL VALUES        *
.*       TO THEM:                                *
.*                                               *
.*************************************************
.*
.*
         LCLB  &R2AVAIL        IF SECOND REGISTER AVAILABLE
         LCLB  &ACONGEN        IF ADDRESS CONSTANT IS TO BE GEN'ED
         LCLB  &AMSKGEN        IF X'80000000' IS TO BE INLINE
         LCLB  &OFSTGEN        IF Y(IHB&IX.S-IHB&IX.B) TOBE GEN'ED
         LCLC  &IX             INDEX VALUE FOR LABELS
         LCLC  &AMSKLBL        LABEL FOR FULLWORD X'80000000'
         LCLC  &AR             "R" IF X'80000000' IS IN A REGISTER
         LCLC  &R1,&R2         REGISTER VALUES
         LCLC  &LB,&LS         BALR/BSM LABEL VALUES
         LCLC  &LU             USER'S LABEL
         LCLC  &AMD            ADDRESSING MODE VALUE
         LCLC  &AMDHEX         ADDRESSING MODE FOR ACON
&IX      SETC  '&SYSNDX'       ASSIGN LABEL INDEX VALUE
&LB      SETC  'IHB&IX.B'      ASSIGN LABEL VALUE
&LS      SETC  'IHB&IX.S'      ASSIGN LABEL VALUE
&LU      SETC  '&LABEL'        ASSIGN LABEL VALUE
.*
.*       DETERMINE THE LABEL VALUE FOR &AMSKLBL:
&AMSKLBL SETC  'IHB&IX.M'      ASSIGN DEFAULT LABEL FOR AMASK
         AIF  ('&AMASK(2)' EQ '').NOAM12
&AMSKLBL SETC  '&AMASK(2)'     GEN IT NOW, USE IT LATER
.NOAM12  ANOP   ,
&AMSKGEN SETB  1               GENERATE CONSTANT INLINE
&AR      SETC  ''              CONSTANT IS IN STORAGE
         AIF  ('&AMASK(1)' EQ '').BYAM1    IF ADDR NOT SUPPL
&AMSKLBL SETC  '&AMASK(1)'     ASSIGN LABEL FOR AMASK
&AMSKGEN SETB  0               DON'T GENERATE CONSTANT INLINE
.*       IF &AMASK VALUE IS IN REGISTER NOTATION:
         AIF  ('&AMSKLBL'(1,1) NE '(' OR                               X
               '&AMSKLBL'(K'&AMSKLBL,1) NE ')').BYAM1  IF IN STORAGE
&AR      SETC  'R'                           CONSTANT IS IN A REGISTER
&AMSKLBL SETC  '&AMSKLBL'(2,K'&AMSKLBL-2)    DROP PARENTHESES
.BYAM1   ANOP   ,
.*
.*       DETERMINE THE REGISTER VALUES TO USE,
.*       AND SET &R2AVAIL TO INDICATE WHETHER
.*       A SECOND REGISTER IS AVAILABLE:
&R1      SETC  '&REG1(1)'      FIRST  REGISTER VALUE
&R2      SETC  '&R1'           USE FIRST REGISTER TWICE
&R2AVAIL SETB  0               ASSUME SECOND REGISTER NOT AVAILABLE
&OFSTGEN SETB  1               ASSUME OFFSET TO BE GENERATED
         AIF  ('&S370(1)' NE 'NO').TSR2
&OFSTGEN SETB  0               DON'T NEED OFFSET WHEN S370=NO
.TSR2    AIF  ('&REG2(1)' EQ '' OR '&REG2(1)' EQ '&R1').BYR2
         AIF  ('&S370(1)' NE 'NO').OKR2
         MNOTE 00,'REG2 SPECIFICATION ''&REG2(1)'' IS NOT USED WHEN S37X
               0=NO, REGISTER VALUE HAS BEEN IGNORED'
         AGO   .BYR2
.OKR2    ANOP   ,
&R2      SETC  '&REG2(1)'      SECOND REGISTER VALUE
&R2AVAIL SETB  1               SECOND REGISTER IS AVAILABLE
&OFSTGEN SETB  0               DON'T NEED OFFSET WHEN TWO REGS AVAIL
.BYR2    ANOP   ,
.*
.*
.*       VALIDATE THE AMODE SPECIFICATION, AND SET LOCAL SYMBOL
.*       VALUES BASED ON THE AMODE/S370 SPECIFICATIONS:
         AIF   ('&AMODE(1)' EQ '24').AMD24
         AIF   ('&AMODE(1)' EQ '31').AMD31
         AIF   ('&AMODE(1)' EQ 'RESET' OR '&AMODE(1)' EQ 'RS').AMDRS
         MNOTE 08,'AMODE SPECIFICATION ''&AMODE(1)'' INVALID, SUPPORTEDX
               VALUES ARE 24, 31, AND RESET'
         MNOTE *,'AMODE=31 HAS BEEN ASSUMED'
         AGO   .AMD31
.AMD24   ANOP   ,                    AMODE=24
&AMD     SETC  '24'
&AMDHEX  SETC  ''
&ACONGEN SETB  1               GENERATE ADDRESS CONSTANT
         AIF  ('&S370(1)' NE 'NO').AMDZZ
&LS      SETC  ''                    LABEL IS NOT NEEDED
&ACONGEN SETB  0               ADDRESS CONSTANT NOT NEEDED
         AGO   .AMDZZ
.AMD31   ANOP   ,                    AMODE=31
&AMD     SETC  '31'
&AMDHEX  SETC  ''
&ACONGEN SETB  1               GENERATE ADDRESS CONSTANT
         AIF  ('&S370(1)' NE 'NO').AMDZZ
&LS      SETC  ''                    LABEL IS NOT NEEDED
&AMDHEX  SETC  '+X''80000000'''
         AGO   .AMDZZ
.AMDRS   ANOP   ,                    AMODE=RESET
&AMD     SETC  'RS'
&AMDHEX  SETC  ''
&ACONGEN SETB  1               GENERATE ADDRESS CONSTANT
         AIF  ('&S370(1)' NE 'NO').AMDZZ
&LS      SETC  ''                    LABEL IS NOT NEEDED
         AGO   .AMDZZ
.*
.AMDZZ ANOP     ,
.*************************************************
.*                                               *
.*       GENERATE THE IN-LINE EXPANSION.         *
.*                                               *
.*************************************************
.*
.*
.*-----------------------------------------------------
.*                                                    ×
.*       DEPENDING ON THE &AMODE AND &S370            ×
.*       SPECIFICATIONS, GENERATE THE INSTRUCTIONS    ×
.*       TO PUT THE ADDRESSING MODE VALUE INTO THE    ×
.*       HIGH-ORDER BIT OF &REG1.  IF &S370=NO WAS    ×
.*       SPECIFIED, THE LOW-ORDER 31 BITS OF &REG1    ×
.*       ARE SET TO THE BRANCH ADDRESS FOR BSM.       ×
.*       IF &S370=MVS, THE LOW-ORDER 31 BITS OF       ×
.*       &REG1 ARE SET TO ZERO, SO THAT AN OFFSET     ×
.*       VALUE CAN BE PLACED THERE LATER IN THE       ×
.*       EXPANSION.                                   ×
.*                                                    ×
.*-----------------------------------------------------
         AIF   ('&AMD' EQ '24').GAMD24
         AIF   ('&AMD' EQ '31').GAMD31
         AIF   ('&AMD' EQ 'RS').GAMDRS
.GAMD24  AIF   ('&S370(1)' EQ 'NO').GAMD24X
         AGO   .GAMD24S
.GAMD31  AIF   ('&S370(1)' EQ 'NO').GAMD31X
         AGO   .GAMD31S
.GAMDRS  AIF   ('&S370(1)' EQ 'NO').GAMDRSX
         AGO   .GAMDRSS
.GAMD24S ANOP   ,        ----------->AMODE=24,S370=MVS<--------
&LU      XR    &R1,&R1              CLEAR ADDR/SET  BIT FOR AMODE=24
         AGO   .GAMDZZ
.GAMD24X ANOP   ,        ----------->AMODE=24,S370=NO<---------
&LU      LA    &R1,IHB&IX.N         LOAD  ADDR AND  BIT FOR AMODE=24
         AGO   .GAMDZZ
.GAMD31S ANOP   ,        ----------->AMODE=31,S370=MVS<--------
&LU      L&AR  &R1,&AMSKLBL         CLEAR ADDR/SET  BIT FOR AMODE=31
         AGO   .GAMDZZ
.GAMD31X ANOP   ,        ----------->AMODE=31,S370=NO<---------
&LU      L     &R1,IHB&IX.A         LOAD  ADDR AND  BIT FOR AMODE=31
         AGO   .GAMDZZ
.GAMDRSS ANOP   ,        ----------->AMODE=RESET,S370=MVS<-----
&LU      N&AR  &R1,&AMSKLBL         CLEAR ADDR/SAVE BIT FOR AMODE=RESET
         AGO   .GAMDZZ
.GAMDRSX ANOP   ,        ----------->AMODE=RESET,S370=NO<------
&LU      N&AR  &R1,&AMSKLBL         CLEAR ADDR/SAVE BIT FOR AMODE=RESET
         O     &R1,IHB&IX.A         PACK BR ADDR W/ AMODE BIT
         AGO   .GAMDZZ
.GAMDZZ  ANOP   ,
.*
.*-----------------------------------------------------
.*                                                    ×
.*       IF S370=NO WAS REQUESTED, PROCEED            ×
.*       IMMEDIATELY TO GENERATE THE BSM              ×
.*       INSTRUCTION:                                 ×
.*                                                    ×
.*-----------------------------------------------------
         AIF  ('&S370(1)' EQ 'NO').GENBSM
.*
.*
.*-----------------------------------------------------
.*                                                    ×
.*       IF MVS/370 EXECUTABILITY WAS REQUESTED,      ×
.*       DETERMINE THE TYPE OF SYSTEM ON WHICH WE     ×
.*       ARE EXECUTING BY TESTING THE CVTMVSE BIT     ×
.*       IN THE CVT.  THE RESULT OF THIS TEST WILL    ×
.*       BE THE PLACING OF AN OFFSET VALUE INTO       ×
.*       THE LOW-ORDER 31 BITS OF &REG1.    THE       ×
.*       OFFSET VALUE WILL BE USED FOR BRANCHING      ×
.*       TO THE MODE CHANGE INSTRUCTION DEPENDING     ×
.*       ON WHETHER THE EXECUTING SYSTEM IS           ×
.*       MVS/370 OR MVS/XA.                           ×
.*                                                    ×
.*-----------------------------------------------------
.*
         AIF   (&R2AVAIL).R2CVT
.R1CVT   AIF   ('&CVTMAP(1)' EQ 'NO').R1CVTN
         AGO   .R1CVTY
.R2CVT   AIF   ('&CVTMAP(1)' EQ 'NO').R2CVTN
         AGO   .R2CVTY
.*
.*       SINGLE REGISTER, CVT MAP AVAILABLE:
.R1CVTY  X     &R1,CVTPTR                  -> CVT                      X
                                           (POSSIBLY INVERT AMODE BIT)
         TM    CVTDCB-CVT(&R1),CVTMVSE     IN MVS/XA SYSTEM ??
         BZ    IHB&IX.Y                    BRANCH IF MVS/370
IHB&IX.X X     &R1,CVTPTR                  CLEAR CVT ADDRESS,          X
                                           (AND RESTORE AMODE BIT)
         AH    &R1,IHB&IX.O         PACK OFFSET WITH AMODE BIT
         B     IHB&IX.Z                    W/ OFFSET IN REGISTER
IHB&IX.Y X     &R1,CVTPTR                  CLEAR CVT ADDRESS,          X
                                           (AND RESTORE AMODE BIT)
IHB&IX.Z DS   0H                    END OF SYSTEM TYPE DETERMINATION
         AGO   .CVTEND
.*
.*       SINGLE REGISTER, NO CVT MAP:
.R1CVTN  X     &R1,16                      -> CVT                      X
                                           (POSSIBLY INVERT AMODE BIT)
         TM    X'74'(&R1),X'80'            IN MVS/XA SYSTEM ??
         BZ    IHB&IX.Y                    BRANCH IF MVS/370
IHB&IX.X X     &R1,16                      CLEAR CVT ADDRESS,          X
                                           (AND RESTORE AMODE BIT)
         AH    &R1,IHB&IX.O         PACK OFFSET WITH AMODE BIT
         B     IHB&IX.Z                    OFFSET IS IN REGISTER
IHB&IX.Y X     &R1,16                      CLEAR CVT ADDRESS,          X
                                           (AND RESTORE AMODE BIT)
IHB&IX.Z DS   0H                    END OF SYSTEM TYPE DETERMINATION
         AGO   .CVTEND
.*
.*       DOUBLE REGISTER, CVT MAP AVAILABLE:
.R2CVTY  L     &R2,CVTPTR                  -> CVT
         TM    CVTDCB-CVT(&R2),CVTMVSE     IN MVS/XA SYSTEM ??
         LR    &R2,&R1                     COPY AMODE  VALUE
         BZ    IHB&IX.Z                    BRANCH IF MVS/370
         LA    &R1,IHB&IX.S-IHB&IX.B       OFFSET TO BSM INSTR
IHB&IX.Z DS   0H                    END OF SYSTEM TYPE DETERMINATION
         AGO   .CVTEND
.*
.*       DOUBLE REGISTER, NO CVT MAP:
.R2CVTN  L     &R2,16                      -> CVT
         TM    X'74'(&R2),X'80'            IN MVS/XA SYSTEM ??
         LR    &R2,&R1                     COPY AMODE  VALUE
         BZ    IHB&IX.Z                    BRANCH IF MVS/370
         LA    &R1,IHB&IX.S-IHB&IX.B       OFFSET TO BSM INSTR
IHB&IX.Z DS   0H                    END OF SYSTEM TYPE DETERMINATION
         AGO   .CVTEND
.CVTEND  ANOP   ,
.MVSEND  ANOP   ,
.*-----------------------------------------------------
.*                                                    ×
.*       IF ONLY ONE REGISTER IS AVAILABLE, &REG1     ×
.*       CONTAINS THE AMODE BIT VALUE TO BE SET,      ×
.*       AND AN OFFSET VALUE ITS THE LOW-ORDER 31     ×
.*       BITS.  IF TWO REGISTERS ARE AVAILABLE,       ×
.*       &REG1 CONTAINS THE OFFSET VALUE IN ITS       ×
.*       LOW-ORDER 31 BITS, AND &REG2 CONTAINS THE    ×
.*       AMODE BIT VALUE TO BE SET, WITH ITS          ×
.*       LOW-ORDER 31 BITS SET TO ZERO.  THE          ×
.*       OFFSET VALUE WILL BE USED TO SELECT THE      ×
.*       MODE CHANGE INSTRUCTION DYNAMICALLY.         ×
.*                                                    ×
.*                                                    ×
.*-----------------------------------------------------
.*                                                    ×
.*                                                    ×
.*       WE NOW SELECT THE MODE CHANGE INSTRUCTION    ×
.*       TO BE EXECUTED.  IF TWO REGISTERS ARE        ×
.*       AVAILABLE, PACK THE BRANCH ADDRESS INTO      ×
.*       THE LOW-ORDER 31 BITS OF &REG2, AND THEN     ×
.*       BRANCH TO THE MODE CHANGE INSTRUCTION        ×
.*       USING THE OFFSET VALUE IN &REG1.             ×
.*                                                    ×
.*       IF ONLY ONE REGISTER IS AVAILABLE, FIRST     ×
.*       BRANCH USING THE OFFSET VALUE IN &REG1;      ×
.*       THEN CLEAR THE OFFSET VALUE FROM &REG1       ×
.*       AND PACK THE BRANCH ADDRESS INTO &REG1       ×
.*       (THIS MEANS DUPLICATED CODE); AND EXECUTE    ×
.*       THE BALR OR BSM INSTRUCTION.                 ×
.*                                                    ×
.*-----------------------------------------------------
.*
.*       USE THE OFFSET IN &REG1 TO SELECT
.*       THE MODE CHANGE INSTRUCTION:
         AIF  (NOT &R2AVAIL).R2AD1  IF &REG2 NOT AVAILABLE
         O     &R2,IHB&IX.A         PACK BR ADDR W/ AMODE BIT
.R2AD1   ANOP  ,
         B     IHB&IX.B(&R1)        PROCEED WITH BALR OR BSM
.*
.*
.*       GENERATE THE SETUP SEQUENCE AND "BALR".
.*       BECAUSE THE OFFSET VALUE TO GET HERE IS ZERO,
.*       IT IS NOT NECESSARY TO CLEAR THE OFFSET VALUE FIRST:
         AIF  (&R2AVAIL).R2AD2      IF &REG2 AVAILABLE
&LB      O     &R1,IHB&IX.A         PACK BR ADDR W/ AMODE BIT          X
                                    (OFFSET VALUE WAS ZERO)
&LB      SETC  ''                   LABEL HAS BEEN USED
.R2AD2   ANOP  ,
&LB      &BALR &R1,&R2              JUST CONTINUE IF MVS/370
.*
.*
.*       GENERATE THE SETUP SEQUENCE FOR "BSM":
         AIF  (&R2AVAIL).R2AD3      IF &REG2 AVAILABLE
&LS      N&AR  &R1,&AMSKLBL         CLEAR OFFSET VALUE
         O     &R1,IHB&IX.A         PACK BR ADDR W/ AMODE BIT
&LS      SETC  ''                   LABEL HAS BEEN USED
.R2AD3   ANOP  ,
.*-----------------------------------------------------
.*                                                    ×
.*       GENERATE THE "BSM" INSTRUCTION:              ×
.*                                                    ×
.*-----------------------------------------------------
.GENBSM  AIF  ('&ASMHV2(1)' EQ 'NO').AHV2N
.AHV2Y   ANOP   ,                   ASM "H" V2 IS BEING USED
&LS      BSM   &R1,&R2              CHANGE/SAVE ADDRESSING MODE
         AGO   .AHV2Z
.AHV2N   ANOP   ,                   ASM "H" V2 ISN'T BEING USED
&LS      DC   0Y(0)                 CHANGE/SAVE ADDRESSING MODE
         DC    X'0B',AL.4(&R1,&R2)           "BSM" INSTRUCTION
.AHV2Z   ANOP   ,                   ASM "H" V2 IS BEING USED
.*
.*
.*       GENERATE THE INLINE CONSTANTS.  THE BALR
.*       OR BSM INSTRUCTION WILL BRANCH AROUND
.*       THESE CONSTANTS USING THE ADDRESS IN IHB&IX.A:
.*
         AIF  (NOT (&OFSTGEN OR &AMSKGEN OR &ACONGEN)).NOCONS
         AIF  (NOT &OFSTGEN).OFNO
.OFGN    CNOP  2,4                  BOUNDARY ALIGN
IHB&IX.O DC    Y(IHB&IX.S-IHB&IX.B) OFFSET TO BSM
         AGO   .OFZZ
.OFNO    CNOP  0,4                  BOUNDARY ALIGN
.OFZZ    ANOP   ,
.*
         AIF  (NOT &AMSKGEN).BYAM2  IF ADDR SUPPLIED
&AMSKLBL DC    A(X'80000000')       AMODE 31 BIT MASK
.BYAM2   ANOP   ,
.*
         AIF  (NOT &ACONGEN).BYAC2  IF ADDR SUPPLIED VIA "LA"
IHB&IX.A DC    A(IHB&IX.N&AMDHEX)       BRANCH ADDRESS FOR BSM
.BYAC2   ANOP   ,
.NOCONS  ANOP   ,
.*
.*
.*       GENERATE THE "N" INSTRUCTION TO CLEAR THE RESULT
.*       REGISTER AND SET THE CONDITION CODE:
IHB&IX.N N&AR  &R1,&AMSKLBL         CLEAR ADDR, PRESERVE AMODE BIT,    X
                                    AND SET CC FOR INVOKER
         MEND
