         MACRO
&LABEL   TSTAMODE &REG1,       WORK REGISTER (CAN BE ZERO)             X
               &AMASK=,        ADDR OF FULLWORD X'80000000'            X
               &BALR=BALR      SPECIFY OPCODE FOR BALR/BASR
.**************************************************************
.*                                                            *
.*             T S T A M O D E   V E R S I O N   01.00        *
.*                                                            *
.*             THIS MACRO IS A GENERAL-PURPOSE MACRO WRITTEN  *
.*       AT COMMERCIAL UNION TO COMPLEMENT THE SETAMODE       *
.*       MACRO AND FACILITATE TESTING OF THE CURRENT          *
.*       ADDRESSING MODE BIT IN MVS/XA.  THE CODE GENERATED   *
.*       BY THIS MACRO CAN BE EXECUTED ON BOTH MVS/370 AND    *
.*       MVS/XA SYSTEMS.                                      *
.*                                                            *
.*       THIS MACRO SETS THE CONDITION CODE TO REFLECT        *
.*       THE CURRENT ADDRESSING MODE.  CC=0 INDICATES         *
.*       S/370 OR XA 24-BIT ADDRESSING MODE; CC^=0            *
.*       INDICATES XA 31-BIT ADDRESSING MODE.  THE            *
.*       ADDRESSING MODE IS NOT CHANGED BY THIS MACRO.        *
.*                                                            *
.*       IN ADDITION TO SETTING THE CONDITION CODE,           *
.*       THIS MACRO PLACES THE ADDRESSING MODE IN THE         *
.*       SPECIFIED REGISTER.  THIS VALUE CAN BE SAVED,        *
.*       IF DESIRED, AND USED AS INPUT TO THE SETAMODE        *
.*       MACRO USING AMODE=RESET; OR CAN BE USED WITH         *
.*       THE BSM INSTRUCTION TO RESTORE THE AMODE.            *
.*                                                            *
.*       THE CODE GENERATED BY THIS MACRO REQUIRES            *
.*       ACCESS TO A FULLWORD OF X'80000000' FOR              *
.*       EXTRACTING AND MANIPULATING THE AMODE BIT IN         *
.*       THE WORK REGISTERS.  IF THE USER DOES NOT            *
.*       SUPPLY THE ADDRESS OF SUCH A FULLWORD USING          *
.*       THE AMASK= KEYWORD, A FULLWORD CONSTANT IS           *
.*       GENERATED INLINE.  TO SAVE ON STORAGE, THE           *
.*       USER CAN SUPPLY THE ADDRESS OF SUCH A FULLWORD       *
.*       TO BE USED BY THE MACRO EXPANSION.  IF THE           *
.*       USER PROVIDES SUCH AN ADDRESS, USING THE             *
.*       AMASK= KEYWORD, THE INLINE FULLWORD IS NOT           *
.*       GENERATED, SAVING EIGHT BYTES WITH EACH USE OF       *
.*       THIS MACRO.  THE SYMBOL SUPPLIED VIA AMASK=          *
.*       MUST DESIGNATE THE ADDRESS OF A FULLWORD IN          *
.*       STORAGE CONTAINING THE VALUE X'80000000', OR         *
.*       BE THE VALUE IN REGISTER NOTATION (DOUBLE            *
.*       ENCLOSING PARENTHESES) OF A GENERAL REGISTER         *
.*       CONTAINING THE VALUE X'80000000'.                    *
.*                                                            *
.*                                                            *
.**************************************************************
.**************************************************************
.**                                                         ***
.**>>>>>>>>>>>>>>C U I C   D I S C L A I M E R<<<<<<<<<<<<<<***
.**                                                         ***
.**************************************************************
.**            THE INFORMATION OR MATERIAL BEING PROVIDED   ***
.**      BY COMMERCIAL UNION INSURANCE COMPANY (CUIC),      ***
.**      WHETHER IN HARD COPY OR MACHINE READABLE FORM,     ***
.**      HAS BEEN DEVELOPED BY CUIC FOR ITS OWN PURPOSE     ***
.**      AND FOR USE ON ITS OWN EQUIPMENT AND WITHIN ITS    ***
.**      OWN DATA PROCESSING SYSTEM.  CUIC MAKES NO         ***
.**      REPRESENTATIONS OR WARRANTIES WHATSOEVER WITH      ***
.**      RESPECT TO THE INFORMATION OR MATERIAL FURNISHED   ***
.**      HEREUNDER, EXPRESSED OR IMPLIED, INCLUDING BUT     ***
.**      NOT LIMITED TO ANY REPRESENTATION OR WARRANTY OF   ***
.**      MERCHANTABILITY OR FITNESS FOR ANY PARTICULAR USE  ***
.**      OR PURPOSE OR THAT THE USE OF THE INFORMATION OR   ***
.**      MATERIAL WILL NOT INFRINGE ANY PATENT, COPYRIGHT,  ***
.**      TRADEMARK, OR OTHER PROPRIETARY INTEREST.  YOU     ***
.**      ARE, THEREFORE, ACCEPTING THIS INFORMATION OR      ***
.**      MATERIAL ON AN "AS IS" BASIS AND WILL BE USING IT  ***
.**      AT YOUR OWN RISK.  NEITHER CUIC NOR ANY OF ITS     ***
.**      AFFILIATES SHALL BE LIABLE WITH RESPECT TO ANY     ***
.**      CLAIM, ACTION, OR DEMAND BY ANY USER OR OTHER      ***
.**      PARTY (INCLUDING ANY CLAIM, ACTION, OR DEMAND FOR  ***
.**      CONSEQUENTIAL DAMAGES EVEN IF CUIC HAS BEEN        ***
.**      ADVISED OF THE POSSIBILITY OF SUCH DAMAGES)        ***
.**      ARISING FROM THE USE OF THE INFORMATION OR THE     ***
.**      MATERIALS AND CONCEPTS RELATED THERETO.            ***
.**      FURTHERMORE, CUIC WILL NOT MAINTAIN, CORRECT, OR   ***
.**      UPDATE THIS INFORMATION OR MATERIAL IN THE         ***
.**      FUTURE.                                            ***
.**                                              01/26/83   ***
.**************************************************************
.*                                                            *
.*              DIRECT INQUIRIES TO THE                       *
.*       AUTHOR........                                       *
.*                                                            *
.*                   THOMAS J. AUBREY                         *
.*                   C.U. AUTOMATION SERVICES                 *
.*                   COMMERCIAL UNION INSURANCE COMPANIES     *
.*                   ONE BEACON STREET - THIRTEENTH FLOOR     *
.*                   BOSTON, MASSACHUSETTS  02108             *
.*                   TELEPHONE: (617) 725-6208                *
.*                                                            *
.**************************************************************
.*
.*
.*       DEFINE THE LOCAL SET SYMBOLS USED IN THIS MACRO
.*       AND ASSIGN INITIAL VALUES TO THEM:
         LCLB  &AMSKGEN        IF X'80000000' IS TO BE INLINE
         LCLC  &IX             INDEX VALUE FOR LABELS
         LCLC  &AMSKLBL        LABEL FOR FULLWORD X'80000000'
         LCLC  &AR             "R" IF X'80000000' IS IN A REGISTER
         LCLC  &R1,&R2         REGISTER VALUES
         LCLC  &LU             USER'S LABEL
&IX      SETC  '&SYSNDX'       ASSIGN LABEL INDEX VALUE
&LU      SETC  '&LABEL'        ASSIGN LABEL VALUE
&R1      SETC  '&REG1(1)'      ASSIGN REGISTER VALUE
.*
.*
.*       DETERMINE THE LABEL VALUE FOR &AMSKLBL:
&AMSKLBL SETC  'IHB&IX.M'      ASSIGN DEFAULT LABEL FOR AMASK
         AIF  ('&AMASK(2)' EQ '').NOAM12
&AMSKLBL SETC  '&AMASK(2)'     GEN IT NOW, USE IT LATER
.NOAM12  ANOP   ,
&AMSKGEN SETB  1               GENERATE CONSTANT INLINE
&AR      SETC  ''              CONSTANT IS IN STORAGE
         AIF  ('&AMASK(1)' EQ '').BYAM1    IF ADDR NOT SUPPL
&AMSKLBL SETC  '&AMASK(1)'     ASSIGN LABEL FOR AMASK
&AMSKGEN SETB  0               DON'T GENERATE CONSTANT INLINE
.*       IF &AMASK VALUE IS IN REGISTER NOTATION:
         AIF  ('&AMSKLBL'(1,1) NE '(' OR                               X
               '&AMSKLBL'(K'&AMSKLBL,1) NE ')').BYAM1  IF IN STORAGE
&AR      SETC  'R'                           CONSTANT IS IN A REGISTER
&AMSKLBL SETC  '&AMSKLBL'(2,K'&AMSKLBL-2)    DROP PARENTHESES
.BYAM1   ANOP   ,
.*
.*
.*       THE FOLLOWING BALR (OR BASR) INSTRUCTION PUTS THE
.*       CURRENT AMODE BIT INTO A REGISTER.  WHEN EXECUTED
.*       IN S/370 MODE OR IN XA 24-BIT MODE, BALR CAUSES
.*       THE INSTRUCTION LENGTH CODE TO BE PLACED IN THE TOP
.*       TWO BITS OF THE REGISTER.  SINCE THE INSTRUCTION
.*       LENGTH CODE FOR A TWO-BYTE INSTRUCTION IS B'01',
.*       S/370 MODE AND XA 24-BIT MODE BOTH CAUSE THE
.*       HIGH-ORDER BIT OF THE REGISTER TO BE SET TO B'0'.
.*       IN XA 31-BIT MODE, BALR CAUSES THE RIGHT HALF
.*       OF THE PSW TO BE PLACED IN THE REGISTER, SETTING
.*       THE BIT TO B'1'.  THUS THE BALR INSTRUCTION PROVIDES
.*       A TRUE INDICATION OF THE ADDRESSING MODE, EVEN THOUGH
.*       BAL (BRANCH-AND-LINK), BECAUSE ITS ILC IS B'10',
.*       DOES NOT.
.*
.*       IF DESIRED, THE INVOKER CAN USE THE BASR
.*       INSTRUCTION (WHICH IS SUPPORTED IN S/370 ON 308X
.*       PROCESSORS) TO ACCOMPLISH THE SAME RESULT AS BALR.
.*       IN BOTH S/370 AND XA MODES, BASR CAUSES THE RIGHT
.*       HALF OF THE CURRENT PSW TO BE PLACED IN &REG1.
.*       THIS SETS THE HIGH-ORDER BIT OF THE REGISTER TO BE
.*       SET TO B'1' ONLY FOR XA 31-BIT MODE.
.*
.*
         AIF  (NOT &AMSKGEN).BYAM2
         AIF  ('&LU' NE '').BYLU2
&LU      SETC  'IHB&IX.U'           ASSIGN A LABEL VALUE
.BYLU2   CNOP  0,4                  BOUNDARY ALIGN
         B     &LU                  BRANCH AROUND CONSTANT
&AMSKLBL DC    A(X'80000000')       AMODE 31 BIT MASK
.BYAM2   ANOP   ,
&LU      &BALR &REG1,0              PUT AMODE BIT IN REGISTER
         N&AR  &REG1,&AMSKLBL       CLEAR ADDR, SET CC FROM AMODE BIT
         MEND   ,
