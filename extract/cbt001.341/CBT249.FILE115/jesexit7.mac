***********************************************************************
*                                                                     *
* MODULE NAME: HASPXJ07                                               *
*                                                                     *
* DESCRIPTION: MAIN TASK JCT READ/WRITE EXIT                          *
*              IF FOUND PROCESS THEM                                  *
*                                                                     *
*                                                                     *
*                                                                     *
*  ENTRY POINT:                                                       *
*      HASPXJ07                                                       *
*         CALLED FROM HASPNUC                                         *
*                                                                     *
*  PURPOSE:                                                           *
*      TO READ/WRITE PCT (PASSWORD CONTROL TABLE) WHEN JCT IO OCCURS  *
*                                                                     *
*  INPUT:                                                             *
*      R0  = CODE INDICATING WHETHER A JCT IS BEING READ OR WRITTEN   *
*                0 INDICATES JCT HAS BEEN READ FROM SPOOL             *
*                4 INDICATES JCT WILL BE WRITTEN TO SPOOL             *
*                4 INDICATES JCT IS ALREADY IN MAIN STORAGE           *
*      R1  = ADDRESS OF THE JCT                                       *
*      R11 = ADDRESS OF THE HCT                                       *
*      R13 = ADDRESS OF TE PCE                                        *
*      R14 = RETURN ADDRESS                                           *
*      R15 = ENTRY ADDRESS                                            *
*                                                                     *
*  OUTPUT:                                                            *
*      R0-R14 SAME AS ENTRY                                           *
*      R15   RETURN CODE                                              *
*      RETURN CODES HAVE THE FOLLOWING MEANINGS                       *
*        0 - TELLS JES2 THAT IF THERE ARE ANY ADDITIONAL EXIT ROUTINES*
*            ASSOCIATED WITH THIS EXIT, CALL THE NEXT CONSECUTIVE EXIT*
*            ROUTINE. IF THERE ARE NO ADDITIONAL EXIT ROUTINES ASSOCI-*
*            ATED WITH THIS EXIT, CONTINUE NORMAL PROCESSING.         *
*        4 - TELLS JES2 TO IGNORE OTHER EXIT ROUTINES ASSOCIATED WITH *
*            THIS EXIT AND TO CONTINUE NORMAL PROCESSING.             *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,NOGEN
         COPY  $HASPGBL
         EJECT ,
HASPXJ07 $MODULE NOTICE=NONE,                                          C
               TITLE='JESEXIT7 - MAIN TASK JCT READ/WRITE EXIT',       C
               $BUFFER,            GENERATE HASP BUFFER DSECT          C
               $HASPEQU,           GENERATE HASP EQUATES               C
               $JCT,                                                   C
               $JQE,                                                   C
               $PCE,                                                   C
               $CAT,                                                   C
               $HCT,                                                   C
               $IOT,                                                   C
               $MIT,                                                   C
               $MITETBL,                                               C
               $MODMAP,                                                C
               $RDRWORK,                                               C
               $TAB,                                                   C
               $USERCBS,                                               C
               $XECB,              GENERATE HASP XECB DSECT            C
               $XIT                GENERATE HASP XIT DSECT
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
UEXIT7  $ENTRY CSECT=YES,BASE=R12    EXIT ROUTINE ENTRY POINT
        $SAVE
         LR    R12,R15         LOAD BASE REGISTER
         LR    R10,R1          LOAD JCT ADDRESS
         LR    R2,R0           LOAD ENTRY CODE
         LA    R2,BRTBL(R2)    GET OFFSET INTO BRANCH TABLE
         BR    R2              BRANCH INTO BRANCH TABLE
         SPACE 3
BRTBL    DS    0H
         B     JCTREAD         BRANCH TO JCT READ
         B     JCTWRITE        BRANCH TO JCT WRITE
         B     JCTINSTG        BRANCH TO JCT STORAGE
         EJECT ,
***********************************************************************
*                                                                     *
*        JCT READ ROUTINE (NULL ROUNTINE)                             *
*                                                                     *
***********************************************************************
JCTREAD  DS    0H
         B     RET0            NO PROCESSING FOR JCT READ IN MAIN TASK
         EJECT ,
***********************************************************************
*                                                                     *
*        JCT WRITE ROUTINE (DO INITIAL CHECKING)                      *
*                                                                     *
***********************************************************************
JCTWRITE DS    0H
         L     R1,$JOBQPTR
         AL    R1,JCTJQE
         TM    JQETYPE-JQEDSECT(R1),$INPUT
         BNO   RET0
         TM    JCTUSER0,JCTPW  PASSWORDS SUPPLIED IN JCL ?
         BNO   RET0            NO, RETURN CODE 0
         CLC   JCTUSER1,=A(0)  ALREADY GOT TRACK SPACE ?
         BNE   RET0            YES, RETURN CODE 0
         CLC   JCTUSER2,=A(0)  IS THERE A PCT ?
         BE    RET0            NO, RETURN CODE 0
         SPACE 2
***********************************************************************
*                                                                     *
*        GET SPOOL SPACE FOR PCT                                      *
*                                                                     *
***********************************************************************
         L     R1,RDRIOT
         LA    R1,IOTMSTAB-IOTDSECT(,R1)
        $TRACK (R1),JQE=PCEJQE
         ST    R1,JCTUSER1
***********************************************************************
*                                                                     *
*        WRITE OUT PCT                                                *
*                                                                     *
***********************************************************************
         MVC   PCEUSER0(4),PCEBUFAD
         MVC   PCEUSER1(4),PCESEEK
         ST    R1,PCESEEK
         MVC   PCEBUFAD(4),JCTUSER2
         LA    R1,PCEDADCT
        $EXCP  (R1),WAIT=YES
         MVC   PCEBUFAD(4),PCEUSER0
         MVC   PCESEEK(4),PCEUSER1
***********************************************************************
*                                                                     *
*        FREE BUFFER FOR INCORE PCT                                   *
*                                                                     *
***********************************************************************
         L     R1,JCTUSER2
         XC    JCTUSER2,JCTUSER2
        $FREEBUF (R1)
         B     RET0
         EJECT ,
***********************************************************************
*                                                                     *
*        JCT IN STORAGE ROUTINE (NULL ROUTINE)                        *
*                                                                     *
***********************************************************************
JCTINSTG DS    0H
         B     RET0            NO PROCESSING FOR JCT IN STORAGE
         EJECT ,
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
RET0     DS    0H
         LA    R15,0           LOAD RETURN CODE 0
        $RETURN RC=(R15)       RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        LITERALS                                                     *
*                                                                     *
***********************************************************************
         LTORG ,
        $MODEND
         END
