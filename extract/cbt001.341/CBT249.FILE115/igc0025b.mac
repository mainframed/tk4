         TITLE 'IGC0025B - GET PASSWORD FROM JES2 - TYPE 4 SVC'
***********************************************************************
*                                                                     *
* MODULE NAME: IGC0025B (SVC 252)                                     *
*                                                                     *
* DESCRIPTION: ROUTINE TO GET PASSWORD FROM JCL                       *
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*  1) GET POINTER TO JCT                                              *
*         IF NOT FOUND RETURN WITH CODE 4                             *
*                                                                     *
*  2)     SCAN TABLE FOR DSN MATCH OR BLANK DSN (FOR DEFAULT PW)      *
*             IF NONE FOUND RETURN WITH CODE 4                        *
*                ELSE MOVE PSW AND RETURN WITH CODE 0                 *
*                                                                     *
*  ENTRY POINT:                                                       *
*      IGC0025B CALLED FROM READPSWD AND IGG0CLBM                     *
*                                                                     *
*  PURPOSE:                                                           *
*      TO GET PASSWORD FROM JES                                       *
*                                                                     *
*  INPUT:                                                             *
*      R0  = ADDRESS OF FIELD IN WHICH PASSWORD IS RETURNED           *
*      R1  = ADDRESS OF DSN                                           *
*      R6  = ENTRY POINT                                              *
*      R7  = ADDRESS OF ASCB                                          *
*      R13 =                                                          *
*      R14 = RETURN ADDRESS                                           *
*                                                                     *
*  OUTPUT:                                                            *
*      IF PASSWORD FOUND                                              *
*          PASSWORD AT ADDRESS PASSED IN R0                           *
*          RETURN CODE 0                                              *
*      ELSE                                                           *
*          RETURN CODE 4                                              *
*                                                                     *
*  REGISTER USEAGE:                                                   *
*       R0  - WORK REGISTER                                           *
*       R1  - WORK REGISTER                                           *
*       R2  - WORK                                                    *
*       R3  - WORK                                                    *
*       R4  - WORK                                                    *
*       R5  - WORK                                                    *
*       R6  - BASE REGISTER FOR PROGRAM                               *
*       R7  - BASE REGISTER FOR ASCB DSECT                            *
*       R8  - UNUSED                                                  *
*       R9  - UNUSED                                                  *
*       R10 - BASE REGISTER FOR JCT DSECT                             *
*       R11 - UNUSED                                                  *
*       R12 - UNUSED                                                  *
*       R13 - UNUSED                                                  *
*       R14 - RETURN ADDRESS                                          *
*       R15 - RETURN CODE                                             *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,NOGEN
         $HASPEQU
         $BUFFER
         $JCT
         $SJB
         $XECB
         $SCAT
         $SVT
         $USERCBS
         IECDSECS CVT,TCB,JSCB,PSCB,UPT,DSAB,TIOT,UCB,ASCB,            X
               DEB,PSA,JESCT,                                          X
               SECCORE,(MAIN,(IOB=NO)),WTG,PREFX,EXPAND=YES
         IEFJSCVT
         PRINT ON,GEN
         TITLE 'IGC0025B - GET PASSWORD FROM JES2 - TYPE 4 SVC'
IGC0025B CSECT
         USING PSA,0
         USING ASCB,R7
         USING IGC0025B,R6     SET ADDRESSABILITY
         SPACE 2
***********************************************************************
*                                                                     *
*        SAVE PARAMETERS                                              *
*                                                                     *
***********************************************************************
         LR    R4,R0            SAVE PASSWORD RETURN ADDRESS
         LR    R5,R1            SAVE DSN ADDRESS
         SPACE 2
***********************************************************************
*                                                                     *
*        GET POINTER TO JCT                                           *
*                                                                     *
***********************************************************************
         L     R1,ASCBJBNI              CHECK FOR INITIATED JOB
         LTR   R1,R1                    IS IT ?
         BZ    RET4                     NO, THEN LEAVE RET 4
         LH    R2,ASCBASID              GET ASID
         SLL   R2,2                     TIMES 4
         L     R1,FLCCVT                GET POINTER TO CVT
         L     R1,CVTJESCT-CVT(,R1)     GET POINTER TO JESCT
         L     R1,JESSSCT-JESCT(,R1)    GET POINTER TO SSCVT
SSCVTLP  DS    0H
         LR    R3,R1                    SAVE POINTER
         L     R1,SSCTSSVT-SSCT(,R1)    GET POINTER TO SSVT
         LTR   R1,R1                    IS THERE ONE ?
         BZ    SSCVTLP2                 NO, SKIP
         SPACE 2
         L     R1,$SVHAVT-SSVT(,R1)     GET POINTER TO HAVT
         LTR   R1,R1                    IS THERE ONE ?
         BZ    SSCVTLP2                 NO, SKIP
         SPACE 2
         LA    R1,0(R2,R1)              POINT TO ...
         ICM   R1,B'1111',0(R1)         ... FIRST SJB IN CHAIN
         BZ    SSCVTLP2                 SKIP IF NONE FOUND
         SPACE 2
SJBLOOP  DS    0H
         LR    R0,R1                    LOAD R0 WITH SJB ADDRESS
         TM    SJBFLG2-SJBDSECT(R1),SJB2INIT    TEST FOR BATCH JOB
         BE    GOTSJB                   GOT SJB BRANCH TO PROCESS
         ICM   R1,B'1111',SJBSJB-SJBDSECT(R1)  POINT TO NEXT SJB
         BNZ   SJBLOOP                  LOOP UNTILL END OF CHAIN
         B     SSCVTLP2                 IF NO JOB SJB FOUND ...
*                                       ... TRY NEXT SUB-SYSTEM
         SPACE 2
SSCVTLP2 DS    0H
         ICM   R1,B'1111',SSCTSCTA-SSCT(R3)    POINT TO NEXT SSCVT
         BZ    RET4                     RETURN CODE 4 IF NONE
         B     SSCVTLP                  ELSE LOOP
         EJECT ,
GOTSJB   DS    0H
         LR    R1,R0                    POINT TO LAST SJB
         ICM   R10,B'1111',SJBJCT-SJBDSECT(R1) GET POINTER TO JCT
         BZ    RET4                     RETURN CODE 4 IF NO JCT
         SPACE 2
***********************************************************************
*                                                                     *
*        CHECK PASSWORD MODE                                          *
*                                                                     *
***********************************************************************
         TM    JCTUSER0,JCTPW           PASSWORD SUPPLIED IN JCL ?
         BNO   RET4                     NO, RETURN CODE 4
         EJECT ,
***********************************************************************
*                                                                     *
*        PASSWORD PROCESSING ROUTINE                                  *
*                                                                     *
***********************************************************************
         USING PCTDSECT,R1
         L     R1,JCTUSER2              PTR TO PASSWORD CONTROL TABLE
         LTR   R1,R1                    IS THERE ONE ?
         BZ    RET4                     NO, RETURN CODE 4
         LH    R0,PCTPWCNT              GET COUNT
         LA    R1,PCTTABL               POINT TO FIRST ENTRY
         DROP  R1
         SPACE ,
PWDSRCH  DS    0H
         XC    8(44,R1),=52X'FF'        UNSCRAMBLE DSN
         CLC   8(44,R1),0(R5)           DATA SET NAMES COMPARE ?
         BE    PWDSRCH1                 YES, BR TO MOVE PW
         CLC   8(44,R1),=CL44' '        ENTRY FOR DEFAULT PASSWORD ?
         BNE   PWDSRCH2                 NO, BRANCH TO CONTINUE
         MVC   0(8,R4),0(R1)            MOVE IN PASSWORD
         XC    0(8,R4),=52X'FF'         UNSCRAMBLE PASSWORD
PWDSRCH2 DS    0H
         XC    8(44,R1),=52X'FF'        RESCRAMBLE DSN
         LA    R1,52(,R1)               BUMP TO NEXT ENTRY
         BCT   R0,PWDSRCH               LOOP TO END OF TABLE
         TM    JCTUSER0,JCTPWDEF        WAS THERE A DEFAULT PASSWORD ?
         BO    RET0                     YES, BRANCH RETURN CODE 0
         B     RET4                     NO, BR RET CODE 4 PW NOT FOUND
         SPACE ,
PWDSRCH1 DS    0H
         XC    8(44,R1),=52X'FF'        RESCRAMBLE DSN
         MVC   0(8,R4),0(R1)            MOVE IN PASSWORD
         XC    0(8,R4),=52X'FF'         UNSCRAMBLE PASSWORD
         B     RET0                     BRANCH TO RETURN CODE 0
         EJECT ,
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
RET0     DS    0H
         LA    R15,0                    LOAD RETURN CODE 0
         B     RETURN                   BR TO RETURN
RET4     DS    0H
         LA    R15,4                    LOAD RETURN CODE OF 4
         B     RETURN                   BR TO RETURN
RETURN   DS    0H
         BR    R14                      RETURN
         EJECT ,
***********************************************************************
*                                                                     *
*        LITERALS                                                     *
*                                                                     *
***********************************************************************
         LTORG ,
         END
