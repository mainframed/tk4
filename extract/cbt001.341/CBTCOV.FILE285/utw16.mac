*PROCESS;
 /****************************   UTW16    ******************************
 *                                                                     *
 * NAME - UTW16.                                                       *
 *                                                                     *
 * DATE OF LAST MODIFICATION - 01 FEB 81.                              *
 *                                                                     *
 * AUTHORS - M. ROBINSON.                                              *
 *                                                                     *
 * MODULES.                                                            *
 *         UTW16                                                       *
 *                                                                     *
 * FUNCTION.                                                           *
 *         THIS PROGRAM READS THROUGH OS LOAD LIBRARIES, OR LOAD       *
 *         MODULES, SCANNING THE LINK-EDIT INFORMATION TO PRODUCE      *
 *         LISTS OF:-                                                  *
 *              1) CSECT NAMES,                                        *
 *              2) ENTRY POINTS,                                       *
 *              3) CALLS (IE. XREF ENTRIES).                           *
 *         EACH LIST IS WRITTEN TO A SEPARATE FILE FOR LATER ANALYSIS. *
 *                                                                     *
 * BASIC  FLOW.                                                        *
 *         A CONTROL CARD FILE IS READ TO DETERMINE WHICH LIBRARIES    *
 *         AND MEMBERS TO SCAN.                                        *
 *         A TEMPORARY FILE OF CONTROL INFORMATION IS PRODUCED.        *
 *         THE REQUESTED LOAD MODULES ARE READ IN TURN AND THE         *
 *         REQUIRED DATA WRITTEN TO THE THREE OUTPUT FILES.            *
 *                                                                     *
 * ENTERED  FROM - JCL.                                                *
 *                                                                     *
 * INPUT.                                                              *
 *         CONTROL CARDS:-                                             *
 *         COL 1-8 = DDNAME TO BE SCANNED.                             *
 *         COLS 9-16, 17-24, ETC (OPTIONAL) MEMBERS TO BE SCANNED.     *
 *         .                                                           *
 *         THE CONTROL CARDS DETERMINE WHICH LIBRARIES ARE USED AS     *
 *         INPUT.                                                      *
 *                                                                     *
 * OUTPUT.                                                             *
 *         THERE ARE THREE OUTPUT FILES:-                              *
 *              1) CSECTS  A RECORD FOR EACH CSECT FOUND,              *
 *              2) ENTRIES A RECORD FOR EACH ENTRY POINT FOUND,        *
 *              3) XREFS A RECORD FOR CALL FROM ONE CSECT TO ANOTHER.  *
 *                                                                     *
 * EXTERNAL  REFERENCES.                                               *
 *         THE READJFCB AND OPENJ SUBROUTINES ARE USED.                *
 *                                                                     *
 * MACROS  REFERENCED.                                                 *
 *         THE PWRDJFCB INCLUDED TEXT IS A PL/I STRUCTURE MAPPING      *
 *         THE JFCB.                                                   *
 *                                                                     *
 * TABLES  REFERENCED.                                                 *
 *         NIL                                                         *
 *                                                                     *
 * EXITS.                                                              *
 *         TO JCL WHEN ALL MODULES HAVE BEEN SCANNED.                  *
 *                                                                     *
 * MESSAGES.                                                           *
 *         NIL.                                                        *
 *                                                                     *
 * ERROR  HANDLING.                                                    *
 *         NIL.                                                        *
 *                                                                     *
 * ENVIRONMENT - TESTED MVT 21.8 PL/I 3.1.                             *
 *                                                                     *
 * RESTRICTIONS - NONE.                                                *
 *                                                                     *
 *****************************   UTW16    *****************************/
 /********************************************************************/
 /*                                                                  */
 /*      SCAN LOAD MODULE(S) TO CROSS-REFERENCE CSECT USAGE          */
 /*                                                                  */
 /********************************************************************/
 UTW16:
   PROC OPTIONS(MAIN);
   DCL
      SYSIN FILE RECORD INPUT;
   DCL
      1 CARD,
         2 DDNAME CHAR(8),
         2 MODULE(9) CHAR(8);
   DCL
      SYSUT1 FILE RECORD INPUT;
   DCL
      1 SYM BASED(PTR),
         2 CODE BIT(8),
         2 F1 CHAR(1),
         2 COUNT FIXED BIN(15) UNALIGNED,
         2 ESD(15) CHAR(16);
   DCL
      1 CESD BASED(PTR),
         2 F1 CHAR(6),
         2 COUNT FIXED BIN(15) UNALIGNED,
         2 ESD(15) CHAR(16);
   DCL
      1 RLD BASED(PTR),
         2 F1 CHAR(6),
         2 COUNT FIXED BIN(15) UNALIGNED,
         2 SPARE CHAR(8),
         2 DATA CHAR(240);
   DCL
      1 RLD_DATA BASED(RLD_PTR) UNAL,
         2 R FIXED BIN(15),
         2 P FIXED BIN(15),
         2 F BIT(8);
   DCL
      RLD_INC(0:99) CHAR(1) BASED(RLD_PTR);
   DCL
      CSECTS FILE RECORD OUTPUT ENV(FB RECSIZE(80));
   DCL
      ENTRIES FILE RECORD OUTPUT ENV(FB RECSIZE(80));
   DCL
      XREFS FILE RECORD OUTPUT ENV(FB RECSIZE(80));
   DCL
      1 OUTPUT,
         2 DSNAME CHAR(44),
         2 LOADMOD CHAR(8),
         2 CSECT CHAR(8),
         2 CALLS CHAR(8),
         2 ENTRY CHAR(8),
         2 SIZE FIXED BIN(31);
   DCL
      STRING_OUTPUT CHAR(76) BASED(ADDR(OUTPUT));
   DCL
      SAVE_OUTPUT CHAR(76);
   DCL
      SYSUT3 FILE RECORD ENV(F RECSIZE(16) BLKSIZE(16));
   DCL
      1 ESD UNALIGNED,
         2 MEMBER CHAR(8),
         2 TYPE BIT(8),
         2 F2 CHAR(4),
         2 LL CHAR(1),
         2 ESDID FIXED BIN(15);
   DCL
      SYSUT4 FILE RECORD DIRECT INPUT ENV(REGIONAL(1) F RECSIZE(16)
   BLKSIZE(16));
   DCL
      TEMP FILE RECORD OUTPUT ENV(FB RECSIZE(80) BLKSIZE(3120) TOTAL);
   DCL
      DIRECT FILE RECORD INPUT ENV(U RECSIZE(300) BLKSIZE(300) TOTAL);
   DCL
      REC CHAR(300);
   DCL
      1 DIR_ENT BASED(DIR_PTR),
         2 PDS_MEMB CHAR(8),
         2 ENT_LENGTH FIXED BIN(31);
   DCL
      DIR_PTR_PLUS(0:2) CHAR(1) BASED(DIR_PTR);
   DCL
      BLK_LENGTH FIXED BIN(15) BASED(ADDR(REC));
   DCL
      (I,
      J,
      K,
      L,
      SAVE_LENGTH,
      WORK) FIXED BIN(31);
   DCL
      CSECT_COUNT FIXED BIN(31);
   DCL
      KEY PIC'99999999';
   DCL
      (ENDFILE_CARD,
      ENDFILE_MOD,
      ENDFILE_DIRECT,
      ENDFILE_WORK) BIT(1) INIT('0'B);
   DCL
      OPENJ ENTRY(CHAR(176),CHAR(8)) OPTIONS(ASSEMBLER,RETCODE);
 % INCLUDE PWRDJFCB;
 /********************************************************************/
 /*                                                                  */
 /*                            ON-UNITS                              */
 /*                                                                  */
 /********************************************************************/
      ON KEY(SYSUT4)
         BEGIN;
            MEMBER = '';
            ESDID = 0;
         END;
      ON ERROR
         CALL PLIDUMP('SHAFTB');
      ON ENDFILE(SYSIN)
         ENDFILE_CARD = '1'B;
      ON ENDFILE(SYSUT1)
         ENDFILE_MOD = '1'B;
      ON ENDFILE(SYSUT3)
         ENDFILE_WORK = '1'B;
      ON ENDFILE(DIRECT)
         ENDFILE_DIRECT = '1'B;
 /********************************************************************/
 /*                                                                  */
 /*     READ INPUT CARDS, EXPANDING IF MODULE NOT SPECIFIED          */
 /*                                                                  */
 /********************************************************************/
      OPEN FILE(SYSIN);
      OPEN FILE(TEMP);
      READ FILE(SYSIN) INTO(CARD);
      IF ENDFILE_CARD THEN
         DO;
            CLOSE FILE(SYSIN), FILE(TEMP);
            OPEN FILE(CSECTS),FILE(ENTRIES),FILE(XREFS);
            CLOSE FILE(CSECTS),FILE(ENTRIES),FILE(XREFS);
            RETURN;
         END;
      DO UNTIL(ENDFILE_CARD);
         IF DDNAME ^= '' THEN
            DO;
               IF MODULE(1) ^= '' THEN
                  WRITE FILE(TEMP) FROM(CARD);
               ELSE
                  DO;
                     I = 0;
                     OPEN FILE(DIRECT) TITLE(DDNAME);
                     READ FILE(DIRECT) INTO(REC);
                     DIR_PTR = ADDR(REC);
                     DIR_PTR = ADDR(DIR_PTR_PLUS(2));
                     DO WHILE(^ENDFILE_DIRECT & (PDS_MEMB^=HIGH(8)));
                        SAVE_LENGTH = BLK_LENGTH - 2;
                        DO
                        WHILE(PDS_MEMB ^= HIGH(8) & SAVE_LENGTH > 0);
                           I = I + 1;
                           MODULE(I) = PDS_MEMB;
                           IF I = 8 THEN
                              DO;
                                 I = 0;
                                 WRITE FILE(TEMP) FROM(CARD);
                                 MODULE(*) = '';
                              END;
                           WORK = 2 * MOD(ENT_LENGTH,'100000'B) + 12;
                           SAVE_LENGTH = SAVE_LENGTH - WORK;
                           DIR_PTR = ADDR(DIR_PTR_PLUS(WORK));
                        END;
                        READ FILE(DIRECT) INTO(REC);
                        DIR_PTR = ADDR(REC);
                        DIR_PTR = ADDR(DIR_PTR_PLUS(2));
                     END;
                     IF I ^= 0
                     THEN WRITE FILE(TEMP) FROM(CARD);
                     CLOSE FILE(DIRECT);
                  END;
            END;
         READ FILE(SYSIN) INTO(CARD);
      END;
      CLOSE FILE(SYSIN), FILE(TEMP);
 /********************************************************************/
 /*                                                                  */
 /*       NOW READ THE GENERATED CONTROL CARDS,                      */
 /*            AND GET ESD INFO FROM EACH MODULE SPECIFIED.          */
 /*                                                                  */
 /********************************************************************/
      ENDFILE_CARD = '0'B;
      OPEN FILE(SYSIN) TITLE('TEMP');
      READ FILE(SYSIN) INTO(CARD);
      DO UNTIL(ENDFILE_CARD);
         JFCB = RDJFCB(DDNAME);
         DSNAME=JFCBDSNM;
         DO K = 1 TO 8 WHILE(MODULE(K)^='');
            SIZE=0;
            JFCBELNM, LOADMOD = MODULE(K);
            JFCBIND1 = JFCBIND1 × '00000001'B;
            CALL OPENJ(JFCB,DDNAME);
            OPEN FILE(SYSUT1) TITLE(DDNAME);
            OPEN FILE(SYSUT3) TITLE('WORKFILE') OUTPUT;
            CSECT_COUNT = 0;
            READ FILE(SYSUT1) SET(PTR);
 MODULE_LOOP:
            DO UNTIL(ENDFILE_MOD);
               SELECT(CODE);
                  WHEN('00000001'B, '00000101'B, '00001101'B,
                   '00000011'B, '00000111'B, '00001111'B)
                     READ FILE(SYSUT1) SET(PTR);
                  WHEN('00100000'B)
                     DO;
                        J = MIN(CESD.COUNT/16,15);
                        DO I = 1 TO J;
                           WRITE FILE(SYSUT3) FROM(CESD.ESD(I));
                           CSECT_COUNT = CSECT_COUNT + 1;
                        END;
                     END;
                  WHEN('01000000'B)
                     DO;
                        J = MIN(SYM.COUNT/16,15);
                        DO I = 1 TO J;
                           WRITE FILE(SYSUT3) FROM(SYM.ESD(I));
                           CSECT_COUNT = CSECT_COUNT + 1;
                        END;
                     END;
                  WHEN('00000010'B, '00000110'B, '00001110'B)
                     LEAVE MODULE_LOOP;
                  WHEN('10000000'B)
                     ;
                  OTHER
                     ;
               END;
               READ FILE(SYSUT1) SET(PTR);
            END;
            CSECT_COUNT = CSECT_COUNT - 1;
            CLOSE FILE(SYSUT3);
 /********************************************************************/
 /*                                                                  */
 /*         NOW SCAN THROUGH OUR ESD'S TO OUTPUT THE DATA            */
 /*                                                                  */
 /********************************************************************/
            OPEN FILE(SYSUT3) TITLE('WORKFILE') INPUT;
            OPEN FILE(SYSUT4) TITLE('WORKFILE');
            READ FILE(SYSUT3) INTO(ESD);
            DO UNTIL(ENDFILE_WORK);
               SIZE = 0;
               SELECT(SUBSTR(TYPE,5,4));
                  WHEN('0000'B, '0100'B)
                     DO;
                        CSECT, ENTRY = MEMBER;
                        CALLS = '';
                        SIZE = ESDID;
                        WRITE FILE(CSECTS) FROM(OUTPUT);
                     END;
                  WHEN('0010'B, '0011'B)
                     DO;
                        CALLS, ENTRY = '';
                        IF SUBSTR(TYPE,5,4) = '0011'B THEN
                           ENTRY = MEMBER;
                        ELSE
                           CALLS = MEMBER;
                        IF ESDID <= CSECT_COUNT THEN
                           DO;
                              KEY = ESDID - 1;
                              READ FILE(SYSUT4) INTO(ESD) KEY(KEY);
                              CSECT = MEMBER;
                              WRITE FILE(ENTRIES) FROM(OUTPUT);
                           END;
                     END;
                  OTHER
                     ;
               END;
               READ FILE(SYSUT3) INTO(ESD);
            END;
 /********************************************************************/
 /*                                                                  */
 /*      FINALLY WE OUTPUT XREF INFO FROM RLD'S                      */
 /*                                                                  */
 /********************************************************************/
            DO UNTIL(ENDFILE_MOD);
               SELECT(CODE);
                  WHEN('00000010'B, '00000110'B, '00001110'B)
                     DO;
                        RLD_PTR = ADDR(RLD.DATA);
                        L = RLD.COUNT - 8;
                        DO UNTIL(L < 0);
                           IF ( (F & '11110000'B) ^= '00100000'B) & (
                            (F & '11110000'B) ^= '00110000'B) & (
                            P ^= R ) THEN
                              DO;
                                 SIZE = SUBSTR(F,1,4);
                                 KEY = P - 1;
                                 READ FILE(SYSUT4) INTO(ESD)
                                 KEY(KEY);
                                 CSECT = MEMBER;
                                 KEY = R - 1;
                                 READ FILE(SYSUT4) INTO(ESD)
                                 KEY(KEY);
                                 ENTRY = MEMBER;
                                 IF ^(F & '10000000'B) THEN
                                    IF SUBSTR(TYPE,5,4) ^= '0000'B &
                                    SUBSTR(TYPE,5,4) ^= '0100'B THEN
                                      DO;
                                      KEY = ESDID - 1;
                                      READ FILE(SYSUT4) INTO(ESD)
                                      KEY(KEY);
                                      CALLS = MEMBER;
                                      END;
                                    ELSE
                                      CALLS = MEMBER;
                                 ELSE
                                    CALLS = 'UNRESOLV';
                                 IF CALLS ^= '' THEN
                                    DO;
                                      IF SAVE_OUTPUT ^= STRING_OUTPUT
                                      THEN
                                      DO;
                                      WRITE FILE(XREFS)
                                      FROM(OUTPUT);
                                      SAVE_OUTPUT = STRING_OUTPUT;
                                      END;
                                    END;
                              END;
                           DO WHILE(F & '00000001'B);
                              L = L - 4;
                              RLD_PTR = ADDR(RLD_INC(4));
                           END;
                           L = L - 8;
                           RLD_PTR = ADDR(RLD_INC(8));
                        END;
                     END;
                  OTHER
                     ;
               END;
               READ FILE(SYSUT1) SET(PTR);
            END;
 /********************************************************************/
 /*                                                                  */
 /*            TIDY UP READY FOR ANOTHER MODULE                      */
 /*                                                                  */
 /********************************************************************/
            CLOSE FILE(SYSUT1);
            CLOSE FILE(SYSUT4);
            CLOSE FILE(SYSUT3);
            ENDFILE_MOD = '0'B;
            ENDFILE_WORK = '0'B;
         END;
         READ FILE(SYSIN) INTO(CARD);
      END;
   END;
