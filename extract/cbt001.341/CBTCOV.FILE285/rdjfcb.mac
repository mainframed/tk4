***********************************************************************
*
* NAME - RDJFCB (FUNCTION).
*
* FUNCTION.
*         TO ALLOW PL/I PROGRAMS TO USE OS SVC 64 ( READ JFCB ).
*
* INPUT.
*          DDNAME - 8 CHARACTER FIELD, CONTAINING DDNAME TO BE OPENED.
*
* OUTPUT.
*         OS SVC 64 RETURNS A 176-BYTE JFCB FROM THE OS JOB QUEUE.
*
* EXAMPLE.
*         % INCLUDE PWRDJFCB; /* DECLARES 176-BYTE JFCB AND RDJFCB
*                                                       ROUTINE */
*         JFCB = RDJFCB('DDNAME');
*         IF PLIRETV() ^= 0 THEN SIGNAL ERROR;
*         .... THE JFCB MAY BE INSPECTED NOW.
*                 THE PWRDJFCB MACRO DECLARES ALL THE JFCB FIELDS,
*                 WITH PL/I ATTRIBUTES, USING THE IBM FIELD NAMES
*                 WHICH CAN BE FOUND IN THE IBM SYSTEM CONTROL BLOCKS
*                 MANUAL. ....
*
* MESSAGES.
*         NO MESSAGES ARE ISSUED IN THIS MODULE.
*
* ERROR  HANDLING.
*         THE RETURN CODE FROM SVC 64 IS PASSED TO THE PL/I PLIRETV
*              BUILTIN FUNCTION.
*
***********************************************************************
RDJFCB   CSECT
         USING *,15
         STM   14,12,SAVE+12
         BALR  10,0
         USING *,10
         DROP  15
         L     2,0(1)              ADDR  OF  D_V
         L     2,0(2)              ADDR  OF  DDNAME
         MVC   DCB+40(8),0(2)      DDNAME
         L     3,4(1)              ADDR  OF  D_V
         L     3,0(3)              ADDR  OF  JFCB
         ST    3,EXLIST
         MVI   EXLIST,X'87'        SET  JFCB  EXIT  LIST  FLAG
         RDJFCB (DCB)              AND  READ  THE  JFCB
         LTR   15,15               R.C.  =  0  ?
         BZ    OKRETURN            YES   -  GOOD  RETURN
         MVC   118(6,3),=X'000000000000'  ELSE  ZERO  VOLSER
OKRETURN EQU *
         LM    14,12,SAVE+12
         BR    14
DCB      DCB   DDNAME=VSNERROR,EXLST=EXLIST,DSORG=PS,MACRF=(E)
EXLIST   DS    F
SAVE     DS    18F
         LTORG
         END
