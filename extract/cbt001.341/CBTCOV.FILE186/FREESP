00013450FREESP:  /*FREESPACE COMMAND*/
00013460         PROC(PARM) OPTIONS(MAIN)REORDER;
00013470%SKIP;
00013480         /*
00013490           THE FREESP COMMAND RETURNS A TABLE FOR ALL ONLINE DASD DEVICES
00013500               STATING THE VOLUME, USE STATUS, #TRKS IN LARGEST EXTENT,
00013510               #CYLS IN LARGEST CONTIG. CYL EXTENT, AND NUMBERS OF EXTENTS
00013520               CONTAINING MORE THAN 1, 5, 10, 20 TRKS AND MORE THAN
00013530               1, 5, 10, 20 CYLS.  THE CUTPOINTS FOR THE FREQUENCY
00013540               ANALYSIS ARE CONTROLLED BY THE ARRAYS TRKFREQ AND CYLFREQ.
00013550               .....
00013560              THE INCLUSION OF PRIVATE VOLUMES IS CONTROLLED BY
00013570              THE KEYWORD PARAMETER PRIVATE/NOPRIVATE (DEFAULT IS
00013580              NOPRIVATE).
00013590               JSA 5/16/78 LAST MOD 5/24/78
00013600               */
00013610%SKIP;
00013620         DCL 1 TRKFREQ(4),
00013630               2 COMPARAND FIXED BIN INIT(1,5,10,20),
00013640               2 COUNT FIXED BIN;
00013650         DCL 1 CYLFREQ(4) LIKE TRKFREQ;
00013660%SKIP;
00013670         /*THE FOLLOWING ARRAY IS FILLED IN BY GETDASD, A SEPARATE
00013680               ASSEMBLER MODULE*/
00013690         DCL 1 DASDLIST ,
00013700               2 N FIXED BIN INIT(200),
00013710               2 ENTRY(200),
00013720                   3 VOLUME CHAR(6),
00013730                   3 CCHH FIXED BIN(31),
00013740                   3 TPC FIXED BIN,
00013750                   3 USE_STATUS CHAR(4);
00013760         DCL GETDASD ENTRY(*);
00013770%SKIP;
00013780         /*THE FOLLOWING ARRAY IS USED BY GETFMT5, WHICH RETRIEVES
00013790               ALL VTOC INFORMATION FOR A SINGLE VOLUME*/
00013800         DCL 1 FMT5LIST ,
00013810               2 VOLUME CHAR(6),
00013820               2 CCHH FIXED BIN(31),
00013830               2 N FIXED BIN INIT(2080),
00013840               2 EXT(2080),
00013850                   3 TT FIXED BIN UNAL,
00013860                   3 CYL FIXED BIN UNAL,
00013870                   3 TRK BIT(8)ALIGNED;
00013880         DCL GETFMT5 ENTRY;
00013890%SKIP;
00013900         /*OUTPUT RELATED:*/
00013910         DCL TELL ENTRY;
00013920         DCL HEADING(2)CHAR(72) INIT(
00013930 '      |    | MOST |MOST |  # EXTENTS >= N TRKS  |  # EXTENTS >= N CYLS |',
00013940 'VOLUME| USE| TRKS |CYLS |N=1  |N=5  |N=10 |N=20 |N=1  |N=5  |N=10 |N=20|');
00013950         DCL OUTLINE CHAR(72),
00013960               1 OUTSTRUC DEF(OUTLINE),
00013970                   2 VOLUME CHAR(7),
00013980                   2 USE_STATUS CHAR(4),
00013990                   2 (MOSTTRK,MOSTCYL,TRKCOUNT(4),CYLCOUNT(4))
00014000                       PIC'ZZZZZZ',
00014010                   2 FINAL CHAR(1);
00014020%SKIP;
00014030    DCL PRIVATE BIT(1)ALIGNED;
00014040    DCL PARM FIXED BIN;
00014050    DCL FREPARS PTR EXT;/*PARSE TABLE*/
00014060%PAGE;
00014070%INCLUDE IKJPARS;
00014080%PAGE;
00014090         IF 0<PARSE(ADDR(PARM)->IKJBUF,FREPARS) THEN RETURN;
00014100         CALL GETPDE('MODE',PDEPTR);
00014110         PRIVATE=IKJKEYWD=1;
00014120%SKIP;
00014130         CALL TELL(HEADING(1));
00014140         CALL TELL(HEADING(2));
00014150         CALL GETDASD(DASDLIST);
00014160         IF DASDLIST.N<=0 THEN GO TO ERROR1;
00014170         DO I=1 TO DASDLIST.N;
00014180              IF PRIVATE|DASDLIST.ENTRY.USE_STATUS(I)^='PRV ' THEN DO;
00014190                   FMT5LIST=DASDLIST.ENTRY(I),BY NAME;
00014200                   FMT5LIST.N=2080;
00014210                   CALL GETFMT5(FMT5LIST);
00014220                   IF FMT5LIST.N<=0 THEN GO TO ERROR2;
00014230                   MAXTRK,MAXCYL=0;
00014240                   TRKFREQ.COUNT,CYLFREQ.COUNT=0;
00014250                   DO J=1 TO FMT5LIST.N;
00014260                        NCYL=FMT5LIST.EXT.CYL(J);
00014270                        NTRK=DASDLIST.ENTRY.TPC(I)*NCYL+
00014280                             BINARY(FMT5LIST.EXT.TRK(J),8);
00014290                        MAXCYL=MAX(NCYL,MAXCYL);
00014300                        MAXTRK=MAX(NTRK,MAXTRK);
00014310                        DO K=1 TO 4;
00014320                             IF TRKFREQ.COMPARAND(K)>NTRK THEN LEAVE;
00014330                             TRKFREQ.COUNT(K)=TRKFREQ.COUNT(K)+1;
00014340                             END;
00014350                        DO K=1 TO 4;
00014360                             IF CYLFREQ.COMPARAND(K)>NCYL THEN LEAVE;
00014370                             CYLFREQ.COUNT(K)=CYLFREQ.COUNT(K)+1;
00014380                             END;
00014390                        END;
00014400                   OUTSTRUC=DASDLIST.ENTRY(I),BY NAME;
00014410                   OUTSTRUC.MOSTCYL=MAXCYL;
00014420                   OUTSTRUC.MOSTTRK=MAXTRK;
00014430                   OUTSTRUC.TRKCOUNT=TRKFREQ.COUNT;
00014440                   OUTSTRUC.CYLCOUNT=CYLFREQ.COUNT;
00014450                   OUTSTRUC.FINAL=' ';
00014460                   CALL TELL(OUTLINE);
00014470                   END;
00014480              END;
00014490         RETURN;
00014500ERROR1:  CALL TELL('ERROR OBTAINING LIST OF ONLINE VOLUMES');
00014510         RETURN;
00014520ERROR2:  CALL TELL('ERROR PROCESSING FREESPACE INFORMATION ON VOLUME');
00014530         END FREESP;
