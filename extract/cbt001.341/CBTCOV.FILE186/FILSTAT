00005920FILSTAT: /*DETECTS VARIOUS COMMON JCL ERRORS FOR A FILE*/
00005930         PROC(DDN,JFCBRET)REORDER RETURNS(FIXED BIN);
00005940%SKIP;
00005950/****RETURN CODES:
00005960         0 -- USER IS USING AN OLD DATASET
00005970         4 -- USER IS USING A NEW DATASET WHICH IS OR WILL BE CATALOGUED
00005980               IN THIS STEP
00005990         8 -- USER IS USING A NEW DATASET AND HAS NOT REQUESTED CATLG
00006000         12-- USER IS USING A NEW DATASET WHICH DUPLICATES THE NAME OF AN EXISTING
00006010              CATALOGUED DATASET (CLASSIC NOT CATALOGUED 2 SITUATION)
00006020         16-- USER IS USING A NEW DATASET WHICH CANNOT BE CATALOGUED BECAUSE
00006030              IT WOULD CREATE INVALID CATALOG STRUCTURE
00006040         20-- DDNAME NOT ALLOCATED (MISSING DD CARD)
00006050         24-- SYSTEM OR PROGRAM ERROR
00006060THE JFCB ADDRESS IS ALSO RETURNED IN SECOND ARGUMENT TO AID IN FURTHER
00006070ERROR ANALYSIS AND DIAGNOSTICS
00006080               *******/
00006090%SKIP;
00006100/*****DECLARATIONS*****/
00006110         DCL DDN CHAR(8);/*DDNAME FOR WHICH ANALYSIS IS DESIRED*/
00006120         DCL JFCBRET PTR;/*FOR RETURN OF JFCB ADDRESS*/
00006130         DCL JFCBPTR ENTRY(CHAR(8))RETURNS(PTR);
00006140         DCL 1 JFCB BASED(JFCBRET),/*JOB FILE CONTROL BLOCK*/
00006150               2 JFCBDSNM CHAR(44),/*DATASET NAME*/
00006160               2 JFCBELNM CHAR(8),/*NOT USED, THIS ROUTINE*/
00006170               2 JFCBTSDM BIT(1)ALIGNED,/*FLAGS: FIRST BIT INDICATES THAT 
00006180                   DATASET IS CATALOGUED*/
00006190               2 PADDING1 CHAR(34),
00006200               2 JFCBIND2 BIT(2)ALIGNED;/*STATUS: '11'B INDICATES NEW*/
00006210         /*REMAINDER OF JFCB NOT USED, THIS ROUTINE*/
00006220         DCL DSN CHAR(44);
00006230         DCL LOCAT ENTRY(CHAR(44),*,FIXED BIN);
00006240         DCL CATWKAREA(34)FLOAT DEC(15);/*WORK AREA REQUIRED BY LOCAT*/
00006250%INCLUDE DAL,DALDISP,DALINFO;
00006260%PAGE;
00006270         JFCBRET=JFCBPTR(DDN);
00006280         IF JFCBRET=NULL() THEN RETURN(20);
00006290         IF JFCBIND2^='11'B THEN RETURN(0);
00006300         IF JFCBTSDM THEN RETURN(4);
00006310         DALDDNAME=DDN;
00006320         CALL DAL(INFORMATION,DDNAME,WHATDISP);
00006330         SELECT(DALWHATDISP);
00006340               WHEN(CATLG);/*USER REQUESTED DISP OF CATLG*/
00006350               OTHERWISE RETURN(8);/*SOME OTHER DISP*/
00006360               END;
00006370         DSN=JFCBDSNM;
00006380         CALL LOCAT(DSN,CATWKAREA,I);
00006390         SELECT(I);
00006400               WHEN(8)RETURN(4);
00006410               WHEN(0)RETURN(12);
00006420               WHEN(12,16)RETURN(16);
00006430               OTHERWISE RETURN(24);
00006440               END;
00006450         END FILSTAT;
00006460/*****JOSH AUERBACH, JANUARY 11, 1978*****/
00006470/*****LAST ALTERATION, MARCH 21, 1978*****/
