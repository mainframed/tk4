00006490FIX:PROC OPTIONS(MAIN)REORDER;
00006500    /**************************************************************
00006510         TO FIX BAD DATES IN THE VTOC (AFTER IPL WITH WRONG DATE,
00006520         FOR EXAMPLE), DO THE FOLLOWING.
00006530              1.  EDIT THE DCL FOR VOLUM SO THAT IT REFLECTS THE
00006540                   CORRECT LIST OF VOLUMES.  SET THE INIT VALUE FOR
00006550                   N TO THE NUMBER OF VOLUMES.
00006560              2.  EDIT THE INIT FOR CRIT SO THAT IT HAS THE BIT(24)
00006570                   VALUE OF THE BAD DATE.  BITS 1-8 SHOULD REPRESENT
00006580                   THE YEAR, BITS 9-24 THE DAY.
00006590              3.  EDIT THE INIT FOR BADDATE SO IT HAS THE HEX
00006600                   EQUIVALENT OF WHAT YOU GAVE FOR THE INIT OF CRIT.
00006610              4.  EDIT THE INIT FOR GOODDATE SO IT HAS THE HEX REPRESENTATION
00006620                   OF THE CORRECT DATE FOR SUBSTITUTION.
00006630              5.COMPILE THIS PROGRAM USING INCLUDE AND LIB('SSCLIB.PLIXMACS')
00006640              6.  ASSEMBLE SYSTEM.SOURCLIB.ASM(VTCREAD) IF AN ASSEMBLED
00006650                   VERSION IS NOT AVAILABLE.
00006660              7.  LINK THIS PROGRAM WITH VTCREAD USING PLIBASE AND
00006670                   LIB('SSCLIB.PLILIB')
00006680              8.  ALLOCATE A DATASET TO OUTFILE.
00006690              9.  RUN THIS PROGRAM.
00006700              10. EDIT THE RESULTING CNTL DATASET, ADDING A JOB CARD
00006710                   AND REMOVING STEPS WHICH HAVE NO SYSIN INPUT.
00006720              11. MAKE SURE EVERY VOLUME INVOLVED HAS A DATASET
00006730                   (A 1 TRACK UNCATALOGUED DATASET WILL DO) CALLED
00006740                   FORMAT4.DSCB.
00006750              12. SUBMIT THE JOB.
00006760              13. FROM THE CONSOLE, REPLY Y TO ALL MESSAGES REQUESTING
00006770                   PERMISSION TO UPDATE VTOCS.
00006780              14. REPEAT STEPS 9-13 SEVERAL HOURS LATER TO CATCH DATASETS
00006790                   WHICH MAY HAVE BEEN OPEN DURING THE EARLIER RUN.  SUCH
00006800                   DSCBS MAY HAVE BEEN REWRITTEN AT CLOSE, RESTORING THE
00006810                   INCORRECT DATE.
00006820    JSA 6/7/78
00006830*************************************************************************/
00006840    DCL VTCREAD ENTRY(FIXED BIN(31),CHAR(8),PTR,FIXED BIN(31));
00006850    DCL RETCODE FIXED BIN(31);
00006860    DCL 1 DSCB BASED(DSCBPTR),
00006870         2 CCHHR CHAR(5),
00006880         2 PAD1 CHAR(47),
00006890         2 FMT CHAR(1),
00006900         2 PAD2 CHAR(11),
00006910         2 DATE BIT(24)ALIGNED;
00006920    DCL CRIT BIT(24) STATIC ALIGNED INIT('010011100000000010111100'B);
00006930    DCL VOLUM (6) CHAR(6) INIT('USER21','USER22','USER23','USER24',
00006940         'USER25','VSAM01');
00006950    DCL N FIXED BIN INIT(6);
00006960    DCL OUTFILE OUTPUT SEQL ENV(FB,RECSIZE(80),BLKSIZE(6160),TOTAL);
00006970    DCL OUTREC CHAR(80)BASED(OPTR);
00006980    DCL HEX ENTRY(CHAR(*)VAR,CHAR(*)VAR);
00006990    DCL HEXSTR CHAR(10)VAR;
00007000    DCL BADDATE CHAR(6) INIT('4E00BC');
00007010    DCL GOODDATE CHAR(6) INIT('4E009E');
00007020%INCLUDE DAL,DALUNIT,DALRETRN;
00007030         DALSTATUS=SHR;
00007040         DO I=1 TO N;
00007050                   DALVOLSER=VOLUM(I);
00007060                   CALL DAL(ALLOCATE,VOLSER,RETDDNAME,STATUS);
00007070                   CALL VTCREAD(1,DALRETDDNAME,DSCBPTR,RETCODE);
00007080                   IF RETCODE^=0 THEN SIGNAL ERROR;
00007090                   LOCATE OUTREC FILE(OUTFILE);
00007100                   OUTREC='//'||VOLUM(I)||' EXEC PGM=AMASPZAP';
00007110                   LOCATE OUTREC FILE(OUTFILE);
00007120                   OUTREC='//SYSPRINT DD SYSOUT=*';
00007130                   LOCATE OUTREC FILE(OUTFILE);
00007140                   OUTREC=
00007150                       '//SYSLIB DD UNIT=DISK,DISP=SHR,DSN=FORMAT4.DSCB,VOL=SER='||VOLUM(I);
00007160                   DO WHILE(RETCODE=0);
00007170                        CALL VTCREAD(0,DALRETDDNAME,DSCBPTR,RETCODE);
00007180                        IF RETCODE>0 THEN LEAVE;
00007190                        IF FMT='1' & DATE=CRIT THEN DO;
00007200                             CALL HEX(CCHHR,HEXSTR);
00007210                             LOCATE OUTREC FILE(OUTFILE);
00007220                             OUTREC=' CCHHR '||HEXSTR;
00007230                             LOCATE OUTREC FILE(OUTFILE);
00007240                             OUTREC=' VER 35 '||BADDATE;
00007250                             LOCATE OUTREC FILE(OUTFILE);
00007260                             OUTREC=' REP 35 '||GOODDATE;
00007270                             LOCATE OUTREC FILE(OUTFILE);
00007280                             OUTREC=' CCHHR '||HEXSTR;
00007290                             LOCATE OUTREC FILE(OUTFILE);
00007300                             OUTREC=' VER 38 '||BADDATE;
00007310                             LOCATE OUTREC FILE(OUTFILE);
00007320                             OUTREC=' REP 38 '||GOODDATE;
00007330                             LOCATE OUTREC FILE(OUTFILE);
00007340                             OUTREC=' CCHHR '||HEXSTR;
00007350                             LOCATE OUTREC FILE(OUTFILE);
00007360                             OUTREC=' VER 3D '||BADDATE;
00007370                             LOCATE OUTREC FILE(OUTFILE);
00007380                             OUTREC=' REP 3D '||GOODDATE;
00007390                             LOCATE OUTREC FILE(OUTFILE);
00007400                             OUTREC=' CCHHR '||HEXSTR;
00007410                             LOCATE OUTREC FILE(OUTFILE);
00007420                             OUTREC=' VER 4B '||BADDATE;
00007430                             LOCATE OUTREC FILE(OUTFILE);
00007440                             OUTREC=' REP 4B '||GOODDATE;
00007450                             END;
00007460                        END;
00007470                   CALL VTCREAD(2,DALRETDDNAME,DSCBPTR,RETCODE);
00007480              END;
00007490         END FIX;
