00000020 ASZAP: PROC OPTIONS(MAIN) REORDER;                                     
00000030  /* THIS IS A UTILITY WHICH CONVERTS AN ASSEMBLER LISTING              
00000040     DATASET INTO A FORMATTED IMASPZAP INPUT DECK OR SMP PTF. IT        
00000050     IS USEFUL IN PREPARING, MAINTAINING, AND DOCUMENTING THOSE         
00000060     INSTALLATION MODIFICATIONS WHICH CANNOT BE INSTALLED EXECPT        
00000070     BY MODIFING IBM CODE. ONE CAN MAKE FREE USE OF MACROS,             
00000080     LITERALS, AND OTHER CONVENIENCE FEATURES IN PREPARING THE          
00000090     DECK. CONTROL CARDS IN SPECIAL ASSEMBLER COMMENT FORM              
00000100     PROVIDE FLEXABILITY IN MODIFICATION DESIGN AND CONTROL             
00000110     OVER OUTPUT.                                                       
00000120        ALL CONTROL CARDS ARE INCLUDED IN THE ASSEMBLER DECK            
00000130     BEGINNING WITH THE CHARACTERS '*ZAP'. THE PURPOSE OF THE           
00000140     CONTROL CARDS IS TO INTERRUPT AND RESUME GENERATION OF             
00000150     ZAP OUTPUT. STATEMENTS WHICH DO NOT GENERATE OBJECT CODE           
00000160     (COMMENTS,ORG,DS,EQU,ETC.) ARE TRANSFERRED TO THE OUTPUT AS        
00000170     ZAP COMMENT CARDS, UNLESS OUTPUT GENERATION IS CURRENTLY           
00000180     PAUSED. OTHER STATEMENTS (INSTRUCTIONS,DC,CCW,ETC.) ARE            
00000190     TRANSFERRED TO THE OUTPUT AS 'VER' OR 'REP' CARDS IF               
00000200     OUTPUT GENERATION HAS BEEN (RE)STARTED IN ONE OF THESE             
00000210     TWO MODES. THE CARDS GOVERNING THIS ARE                            
00000220          *ZAP START REP                                                
00000230          *ZAP START VER                                                
00000240          *ZAP PAUSE                                                    
00000250     ZAP GENERATION IS INITIALLY PAUSED. A '*ZAP START' CARD IS         
00000260     VALID AND (RE)STARTS GENERATION IN REP OR VER MODE, WHICHEVER      
00000270     WAS LAST ACTIVE, BUT IF IT IS THE FIRST START CARD IN THE          
00000280     DECK, IT CANNOT BE FOLLOWED BY OBJECT GENERATING STATEMENTS        
00000290     UNTIL MODE IS SPECIFIED ON A SUBSEQUENT '*ZAP START (VER/REP)'     
00000300     CARD.                                                              
00000310        WHEN THE ZAP IS DONE, A '*ZAP END' CARD WILL TERMINATE          
00000320     A SCAN OF THE LISTING AND CLOSE THE OUTPUT FILE. THIS NOT          
00000330     ONLY SAVES A FEW EXTRA MICROSECONDS OF CPU WHICH WOULD BE          
00000340     SPENT SCANNING THE MAPPING MACROS AND CROSS REFRENCE, BUT          
00000350     HELPS THE HUMAN READING THE ASSEMBLER TO KNOW WHERE THE            
00000360     ESSENTIAL CODE HAS STOPPED AND THE SUPPORT DEFINITIONS             
00000370     HAVE BEGUN. IT TAKES A LOT OF ZAP COMMENT CARDS TO DEFINE          
00000380     THE CVT DSECT.                                                     
00000390        THE '*ZAP CARD' CONTROL CARD ALLOWS ARBITRARY TEXT TO           
00000400     BE REPRODUCED INT THE OUTPUT. THE CHARACTERS UP TO THE             
00000410     SPACE AFTER 'CARD' ARE STRIPPED OFF AND THE REMAINDER OF           
00000420     THE CARD TO COLUMN 72 IS REPRODUCED IN THE OUTPUT DECK.            
00000430     THIS IS PRIMARILY FOR NAME,DUMP, AND SMP CONTROL CARDS.            
00000440          *ZAP CARD ++PTF(YU00001) /* TYPICAL PTF DECK * /.             
00000450          *ZAP CARD ++ZAP(IEXAMPLE).                                    
00000460          *ZAP CARD   EXPAND IEXAMPLE(50)                               
00000470          *ZAP CARD   NAME IEXAMPLE                                     
00000480          *ZAP START VER                                                
00000490     RESTRICTIONS:                                                      
00000500          YOU MUST SPECIFY 'PRINT DATA' IN THE ASSEMBLY IF YOU          
00000510             GENERATE CONSTANTS OR LITERAL IN EXCESS OF 8 BYTES         
00000520             LENGTH.                                                    
00000530          REMEMBER TO PUT THE 'LTORG' STATEMENT BEFORE THE              
00000540            '*ZAP END' CARD IF LITERALS ARE GENERATED.                  
00000550          NO RELOCATABLE ADDRESS CONSTANTS CAN BE CODED IN THE          
00000560            ZAP CARD.                                                   
00000570     IT IS NOT POSSIBLE AT THIS TIME TO DETECT VIOLATIONS OF THE        
00000580      RESTRICTIONS LISTED ABOVE. BE CAREFUL.                            
00000590     GOOD TECHNIQUE:                                                    
00000600        PUT ALL SENSITIVE ADDRESSES AT THE BEGINNING OF THE DECK AS     
00000610           EQU INSTRUCTIONS. THEY CAN BE EASILY MODIFIED WHEN HIT BY    
00000620           PTFS OR WHEN THE NEXT RELEASE IS DISTRIBUTED. CANDIDATES     
00000630           INCLUDE THE ZAPPED OVER INSTRUCTION, THE PATCH AREA, AND     
00000640           TARGETS OF CONDITIONAL BRANCH INSTRUCTIONS OTHER THAN THE    
00000650           NATURAL RETURN.                                              
00000660        IT IS SOMETIMES POSSIBLE TO IMBED THE ZAP INSTRUCTIONS IN       
00000670           ENOUGH SUPPORT CODE TO MAKE THE OBJECT DECK OUTPUT OF        
00000680           THE ASSEMBLY AN INDEPENDENTLY EXECUTABLE PROGRAM WHICH       
00000690           YOU CAN TSO TEST BEFORE INCORPORATING INTO THE SYSTEM.       
00000700           THIS SAVES A LITTLE SYSTEM TEST TIME TRACKING DOWN DATA      
00000710           EXCEPTIONS AND THE LIKE.                                     
00000720                                                                        
00000730     COMMENTS AND SUGGESTIONS SHOULD BE ADDRESSED TO                    
00000740          HOWARD GILBERT                                                
00000750          YALE COMPUTER CENTER                                          
00000760          175 WHITNEY AVE                                               
00000770          NEW HAVEN, CONN. 06520                                        
00000780                                                                        
00000790     DDNAMES USED: IN,OUT,SYSPRINT                                      
00000800  /*                                                                    
00000810  DCL IN INPUT FILE RECORD /* DS CONTAINING SYSPRINT FROM ASSEMBLY */;  
00000820  DCL OUT OUTPUT FILE STREAM ENV(FB RECSIZE(80) BLKSIZE(800));          
00000830     /* RECEIVES ZAP CARDS */                                           
00000840  DCL LINE CHAR(133) VAR; /* ONE LINE OF ASSEMBLER */                   
00000850  DCL LINE2 CHAR(133);                                                  
00000860  DCL ADDR CHAR(4) DEF LINE2 POS(4); /* OFFSET OR ADDRESS PART */       
00000870  DCL DATA CHAR(16) DEF LINE2 POS(9); /* HEX INSTRUCTION CODE OR DATA */
00000880          /* NOTE THAT SUBSTR(DATA,1,1) OVERLAPS LAST DIGIT OF          
00000890             HEX REPRESENTATION OF ADDRESS GENERATED BY EQU             
00000900             STATEMENT, SO TEST IS ON SUBSTR(DATA,2,1) FOR              
00000910             PRESENCE OF ANY REAL DATA */                               
00000920  DCL CARD CHAR(72) DEF LINE2 POS(42); /* ASM SOURCE PART */            
00000930  DCL (DATE,TIME,INDEX,VERIFY,SUBSTR) BUILTIN;                          
00000940  DCL WHAT CHAR(3) INIT(' '); /* BECOMES 'VER' OR 'REP' */              
00000950  DCL SKP BIT INIT('1'B); /* CONTROLS SKIPPING UNDER *ZAP PAUSE, ETC */ 
00000960  ON ERROR BEGIN;                                                       
00000970     ON ERROR SYSTEM;                                                   
00000980     PUT EDIT(' ERROR PROCESSING:',LINE)(SKIP,A);                       
00000990     END;                                                               
00001000  %PAGE;                                                                
00001010  /* LOGIC:                                                             
00001020     EACH LINE IS HANDLED SEPARATELY. THE ONLY INFORMATION WHICH        
00001030       EXTENDS ACROSS LINES IS THE MODE OF GENERATION IN 'WHAT'         
00001040       AND THE GENERATION (0) OR PAUSE (1) STATUS IN 'SKP'.             
00001050       FIRST ONE DETECTS AND PROCESSES CONTROL CARDS.                   
00001060       THEN ONE CHECKS FOR PAGE HEADER LINES AND BLANK LINES.           
00001070       ASSEMBLER COMMENT CARDS ARE REPRODUCED.                          
00001080       A COMMENT CARD IS PRODUCED FOR LABELS OR NON-OBJECT              
00001090         GENERATING STATEMENTS.                                         
00001100       A VER OR REP CARD IS CREATED FOR OBJECT GENERATING STATEMENTS.   
00001110    */                                                                  
00001120  GETLINE:                                                              
00001130     READ FILE(IN) INTO(LINE);                                          
00001140     LINE2=LINE;                                                        
00001150                                                                        
00001160                                                                        
00001170                                                                        
00001180     IF SUBSTR(CARD,1,4)='*ZAP' THEN                                    
00001190          DO /* ASMTOZAP CONTROL CARD PROCESSING */;                    
00001200          I=VERIFY(SUBSTR(CARD,5),' ') +4;                              
00001210          IF SUBSTR(CARD,I,5)='START' THEN                              
00001220               DO;                                                      
00001230               SKP='0'B;                                                
00001240               IF INDEX(CARD,'VER')>0 THEN WHAT='VER';                  
00001250               IF INDEX(CARD,'REP')>0 THEN WHAT='REP';                  
00001260               GO TO GETLINE;                                           
00001270               END;                                                     
00001280          IF SUBSTR(CARD,I,5)='PAUSE' THEN                              
00001290               DO;                                                      
00001300               SKP='1'B;                                                
00001310               GO TO GETLINE;                                           
00001320               END;                                                     
00001330          IF SUBSTR(CARD,I,4)='STOP'|SUBSTR(CARD,I,4)='END'             
00001340             THEN GO TO FINN;                                           
00001350          IF SUBSTR(CARD,I,4)='CARD' THEN                               
00001360               DO;                                                      
00001370               PUT FILE(OUT) EDIT(SUBSTR(CARD,I+5))(COL(1),A(72));      
00001380               GO TO GETLINE;                                           
00001390               END;                                                     
00001400          PUT EDIT ('***** UNRECOGNIZED CONTROL CARD ****',CARD)        
00001410               (SKIP(2),2 A);                                           
00001420          CALL PLIRETC(16);                                             
00001430          GO TO GETLINE;                                                
00001440          END /* OF CONTROL CARD PROCESSING */;                         
00001450     IF SKP THEN GO TO GETLINE;                                         
00001460  /* NO PROCESSING IS DONE IF GENERATION IS PAUSED */                   
00001470     IF VERIFY(SUBSTR(LINE2,2,23),' 0123456789ABCDEF')>0                
00001480        THEN GO TO GETLINE;                                             
00001490   /* IF THERE ARE NON HEX CHARACTERS IN THE ADDRESS AND DATA FIELDS    
00001500      THEN WE ARE PROBABLY LOOKING AT A PAGE TITLE LINE */              
00001510     IF CARD=' ' & DATA=' ' THEN GO TO GETLINE;                         
00001520          /* IF THESE FIELDS ARE BLANK, THEN WE ARE LOOKING AT BLANK    
00001530             LINES PRODUCED BY SPACE OR UNDER PAGE HEADER. */           
00001540     IF SUBSTR(CARD,1,1)='*' THEN                                       
00001550          DO /* REPRODUCE ASSEMBLER COMMENT CARD */;                    
00001560          PUT FILE(OUT) EDIT(CARD)(COL(1),A(72));                       
00001570          GO TO GETLINE;                                                
00001580          END;                                                          
00001590     IF SUBSTR(CARD,1,1)^=' ' & SUBSTR(DATA,2,1)^=' ' THEN              
00001600          DO /* WE HAVE A LABELLED OBJECT GENERATING STATEMENT.         
00001610            BREAK IT UP INTO TWO LINE, A COMMENTED 'LABEL EQU *' AND    
00001620            A VER OR REP LINE THIS ALLOWS LESS TRUNCATION OF THE        
00001630            COMMENT FIELD OF THE ASSEMBLER SOURCE, SINCE COLS 1-9 ARE   
00001640            NEVER REPRODUCED AS BLANKS. */;                             
00001650          PUT FILE(OUT) EDIT('*',SUBSTR(CARD,1,8),'EQU   *')            
00001660               (COL(1),A(1),COL(21),A(8),COL(30), A);                   
00001670          END;                                                          
00001680     IF SUBSTR(DATA,2,1)=' ' THEN                                       
00001690          DO /* NON-OBJECT-GENERATING STATEMENT */;                     
00001700          PUT FILE(OUT) EDIT('*',CARD)                                  
00001710               (COL(1),A(1),COL(21),A(60));                             
00001720          GO TO GETLINE;                                                
00001730          END /* IT IS PASSED AS A COMMENT */;                          
00001740     IF WHAT=' ' THEN                                                   
00001750          DO /* OBJECT GENERATOR HIT AFTER ONLY '*ZAP START' CARD */;   
00001760          PUT EDIT('**** NEITHER VER NOR REP HAS BEEN SPECIFIED ****',  
00001770               CARD)(SKIP, A);                                          
00001780          CALL PLIRETC(16);                                             
00001790          END;                                                          
00001800     /* ZAP IS WRITTEN IN TWO PIECES. FIRST VER OR REP AND OFFSET,      
00001810        NEXT DATA AND COLUMNS 10- ON ASSEMBLER SOURCE. */               
00001820     PUT FILE(OUT) EDIT(WHAT,ADDR,' ')(COL(1),A(3),X(1),A(4),A(1));     
00001830     I=1;                                                               
00001840               /* DATA GENERATING STATEMENTS ARE OF TWO KINDS:          
00001850                  MACHINE OPS GENERATE '0000 0000 0000' WITH            
00001860                  THE SECOND AND THIRD GROUP OPTIONAL.                  
00001870                  DC GENERATES A VARIABLE LENGTH                        
00001880                  '0000000000000000' WITH AN EVEN # DIGITS<17 */        
00001890          DO J=1 TO 4 WHILE(I<14);                                      
00001900          PUT FILE(OUT) EDIT(SUBSTR(DATA,I,4))(A(4));                   
00001910          IF SUBSTR(DATA,I+4,1)=' ' THEN I=I+1;                         
00001920          IF SUBSTR(DATA,I+4,1)^=' ' & I<10 THEN                        
00001930               PUT FILE(OUT) EDIT(',')(A(1));                           
00001940          I=I+4;                                                        
00001950          END;                                                          
00001960     PUT FILE(OUT) EDIT(SUBSTR(CARD,10))(COL(30),A(51));                
00001970     GO TO GETLINE;                                                     
00001980  FINN:                                                                 
00001990     PUT FILE(OUT) EDIT('* DECK PRODUCED BY THE ASMTOZAP UTILITY'       
00002000          ,DATE,TIME)(COL(1),A,X(1),P'99/99/99',X(1),A(6));             
00002010     END;                                                               
