POUND    TITLE 'COMMAND SUB-SYSTEM PROCESSOR INTERFACE FOR D C M S'
POUND    DCMSTART R,LV=65536
         MVC   LASTCMD(8),=CL8' '
         XC    JOBTABLE(240),JOBTABLE
         XC    JOBTABLE+240(124),JOBTABLE
         MVI   JOBEND,X'FF'
         XC   XSA(XSAEND-XSA),XSA
*
RESTART  EQU   *
         GETIMAGE #CMD,SCREEN
*
DOIO#1   EQU   *
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO
         NI    IOCBFLGC,255-IOCBCCLR+IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY K9=RESTART,K1=#HELP,K12=#CMDXIT,ENTER=#GOTCMD,          X
               DEF=RESTART
#GOTCMD  DS    0H
         LA   R0,TABSTART           .
         SR   R14,R14               . CLEAR
         SR   R15,R15               .         MESSAGE
         L    R1,=A(65500-AREASIZE) .                AREA
         ICM  R15,8,BLANKS          .                    AT
         MVCL R0,R14 .              .                      STARTUP
         XC    CURNUMS(4),CURNUMS
         LA    R1,TABSTART
         ST    R1,CONTROL
         ST    R1,CURRENT
         LA    R1,NUMENT
         ST    R1,NUMTABS
         LA   R0,TABSTART
         L    R1,=A(NUMENT*79)
         AR   R1,R0
         ST   R1,BOTTOM
*
         CLC   INPLINE(14),BLANKS
         BE    #HELP
         CLI   INPLINE,C'#'
         BNE   WRONGIN
         B     CMDSCAN#
WRONGIN  EQU   *
         MVC   SCREEN,BLANKS
         MVC   SCREEN(L'ERRMSG1),ERRMSG1
         MVC   INPLINE,BLANKS
         B     DOIO#1
#HELP    EQU   *
         MVC   INPLINE,=CL78'#H '
         B    #GOTCMD
CMDSCAN# EQU   *
         MVC   EPLOC,CMDNAME     SCAN
         CLI   INPLINE+1,C' '        FOR
         BE    #HELP                    LEGAL
         CLI   INPLINE+1,C'*'                COMMAND
         BE    #CMDXIT                              SYNTAX
         LA    R3,TABLE          POINT
         LA    R4,25                  TO
         SR    R5,R5                    SKELETON
         IC    R5,INPLINE+1                     TABLE
#SCAN1   EQU   *
         CLI   0(R3),X'FF'    SCAN
         BE    WRONGIN            TABLE
         CLI   0(R3),X'00'             FOR
         BE    WRONGIN                    LEGAL
         EX    R5,#TEST                        COMMAND
         BE    #FOUND                                 NAME
         LA    R3,4(R3)
         BCT   R4,#SCAN1
         EX    0,*           TABLE SCREWED UP
#TEST    CLI   0(R3),X'00'
#FOUND   EQU   *
         MVC   EPLOC+3(2),1(R3)  COPY IN MODULE NAME
         LA    R8,8
         LA    R7,INPLINE+2
#OPNDLP1 EQU   *
         CLI   0(R7),C' '
         BNE   OPERAN1
         LA    R7,1(R7)
         BCT   R8,#OPNDLP1
         XC    XAL(4),XAL
         B     #OPERAND
OPERAN1  EQU   *
         LA    R8,INPLINE+78
         SR    R8,R7
         CH    R8,=H'76'
         BL    #MOVEOPS
         LA    R8,77
#MOVEOPS EQU   *
         EX    R8,#MOVE
         LA    R1,COMMAND
         ST    R1,XAL
#OPERAND EQU   *
         B     #ISSUE
#MOVE    MVC   COMMAND(0),0(R7) .EXECUTED MOVE OF OPERANDS
#ISSUE   EQU   *
         MVC   WTOLIST(60),WTOOVLY
         MVC   UCMID(4),=CL4'DCMS'  UCM ID OF INPUT CONSOLE
         MVC   CMDID(1),0(R3)      COMMAND ID FROM TABLE
         L     R1,16               CVT
         L     R1,196(0,R1)        SMCA
         MVC   SYSID(4),16(R1)     SYSTEM ID
         TIME  DEC
         ST    R1,DOUBLE
         UNPK  YEAR(3),DOUBLE+1(2)
         MVI   YEAR+2,C'.'
         UNPK  DAY(3),DOUBLE+2(2)
         OI    DAY+2,X'F0'
         ST    R0,DOUBLE
         UNPK  HOUR(3),DOUBLE(2)
         MVI   HOUR+2,C'.'
         UNPK  MINUTE(3),DOUBLE+1(2)
         MVI   MINUTE+2,C' '
*
**
*
         LOAD EPLOC=EPLOC
         ST   R0,CMDADDR
         LTR  R15,R15
         BZ   #CMDLINK
         MVC  SCREEN(L'ERRMSG2),ERRMSG2
         MVC  INPLINE,BLANKS
         B    DOIO#1
#CMDLINK EQU  *
         L    R1,=A(#CMDIO)     POINT TO
         ST   R1,XSW                    IO/DRIVER
*        LA   R1,COMMAND      POINT TO
*        ST   R1,XAL                  SUB OPERAND STRING
*        LA   R8,8
*
*
*
*
*====>
         MODESET KEY=ZERO,MODE=SUP
         LA   R2,XSA          POINT TO COMMAND PARM AREA
         L    R15,CMDADDR
         STM  R3,R13,XXSAVE1
         BALR R14,R15
         USING  *,R14
         LM   R3,R13,XXSAVE1  RECOVER REGISTERS
         DROP R14
         B     CONTINU
XXSAVE1  DC   18F'0'
CONTINU  EQU  *
*====>
         MODESET KEY=NZERO,MODE=PROB
         EJECT
*
*
*        OUTPUT WORK AREA FILLED AT THIS POINT
*
*
         SPACE 3
PUTPAGE  EQU  *
         XC    FRAMES,FRAMES       INITIALIZE
         XC    CURFRAM,CURFRAM               CONTROLS
         XC    CUROUT,CUROUT                         FOR
         L     R1,CONTROL                               THE
         ST    R1,CUROUT                                   CURRENT
         L     R5,CURNUMS          COMMAND
         SR    R4,R4                      OUTPUT
         D     R4,=F'20'                        DISPLAY
         LTR   R4,R4
         BZ    *+8
         LA    R5,1(R5)
         ST    R5,FRAMES
*
*
*
DOIO#2   EQU  *
*        LA    R0,FORMAT      CLEAR
*        LA    R1,SCREEN           SCREEN
*        BAL   R14,@CLEAR                AND GET
         BAL   R14,GETPAGE                 A FRAME
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO
         NI    IOCBFLGC,255-IOCBCCLR+IOCBCRAO
         TRMIO IOCB,FORMAT=FORMAT,IMAGE=SCREEN
         PFKEY DEF=PFK8,K9=RESTART,ENTER=#GOTCMD,K7=PFK7,K8=PFK8,      X
               K12=#CMDXIT
PFK7     EQU   *
         LA    R1,1580        PAGE
         L     R2,CUROUT          BACKWARD
         SR    R2,R1                      UNLESS
         C     R2,CONTROL                        AT
         BL    PUTPAGE                            THE
         ST    R2,CUROUT                             TOP
         B     DOIO#2
PFK8     EQU   *
         LA    R1,1580        PAGE
         L     R2,CUROUT          FOREWARD
         AR    R2,R1                      UNLESS
         C     R2,BOTTOM                        AT
         BH    PUTPAGE                            BOTTOM:
         CLC   CURFRAM,FRAMES     OR
         BNL   PUTPAGE              RESTART
         ST    R2,CUROUT                   DISPLAY
         B     DOIO#2                             AT TOP
GETPAGE  DS    0H
*
*        MVCL  ROUTINE TO INIT A PAGE
*
*
         L     R1,CURFRAM
         LA    R1,1(R1)
         ST    R1,CURFRAM
         MVC   SCREEN(60),WTOLIST       TITLE AT TOP
*        LR    R2,R14
*        ST    R1,DECWORK1
*        BAL   R14,@DECIMAL
*        MVC   SCREEN+61(10),=CL10'FRAME'
*        MVC   SCREEN+67(4),DECWORK4
*        LR    R14,R2
         LA    R2,SCREEN+79             TWENTY USERS
         LA    R3,1580                  ON LINES
         LR    R5,R3                    TWO   THRU
         L     R4,CUROUT                TWENTY-ONE
         ICM   R5,8,=C' '
         MVCL  R2,R4                    DOIT
         BR    R14
         EJECT
         DS    0D
CMDNAME  DC    CL8'IEEXX13D'
TESTNAME DC    CL8'CMDTESTX'
TABLE    DC    C'N',C'SA',C'0'
         DC    C'L',C'SC',C'0'
         DC    C'D',C'SD',C'0'
         DC    C'A',C'SE',C'0'
         DC    C'F',C'SF',C'0'
         DC    C'R',C'SG',C'0'
         DC    C'S',C'SH',C'0'
         DC    C'V',C'SI',C'0'
         DC    C'I',C'SJ',C'0'
         DC    C'C',C'SK',C'0'
         DC    C'J',C'SL',C'0'
         DC    C'H',C'SM',C'0'
         DC    C'T',C'SN',C'0'
         DC    X'FF',C'SW',C'0'     THE IO ROUTINE MUST BE LAST.
         DC    X'00'
         EJECT
WTOOVLY  DS    0CL52
         DC    CL4' '
         DC    CL4'--  ',CL8'CMDSBSYS',CL3' (#'
         DC    CL4' )  ',CL8'L=    ',CL4' ',CL8' ',CL7' ',CL4'  --'
         DC    CL10' '
         SPACE 3
ERRMSG1  DS    0CL48
         DC    CL4' '
         DC    CL44'CMD SUBSYS -   COMMAND PARAMETER ERROR'
         SPACE 3
ERRMSG2  DS    0CL48
         DC    CL4' '
         DC    CL44'CMD SUBSYS -   COMMAND NOT SUPPORTED'
         EJECT
FORMAT   SFMT
SCREEN   FIELD 79,INTEN=HI
L2       FIELD 79
L3       FIELD 79
L4       FIELD 79
L5       FIELD 79
L6       FIELD 79
L7       FIELD 79
L8       FIELD 79
L9       FIELD 79
L10      FIELD 79
L11      FIELD 79
L12      FIELD 79
L13      FIELD 79
L14      FIELD 79
L15      FIELD 79
L16      FIELD 79
L17      FIELD 79
L18      FIELD 79
L19      FIELD 79
L20      FIELD 79
L21      FIELD 79
*22      FIELD 79
INPLINE  FIELD 79,ALPHA,CURSOR=YES,ROW=22,COLUMN=2,INTEN=HI
         SFEND
#CMDXIT  DCMSTOP SCREEN=NO
         EJECT
         DSGEN FORMAT
         DS    4CL79
CMDADDR  DS    F
EPLOC    DS    CL8
LASTCMD  DS    CL8
         SPACE 2
         DS    0D
USERWORK DS    0CL228
COMMAND  DS    CL80
INCONS   DS    CL2
         SPACE 2
         DS    0F
WTOLIST  DS    0CL60
         DS    CL19
CMDID    DS    C
         DS    CL6
UCMID    DS    CL2
         DS    CL3
SYSID    DS    CL4
         DS    CL2
YEAR     DS    CL2
         DS    C
DAY      DS    CL3
         DS    CL2
HOUR     DS    CL2
         DS    C
MINUTE   DS    CL2
         DS    CL4
PADDING  DS    CL21
         SPACE 2
         DS    0F
XSA      DS    0CL48
         DS    5F
XAL      DS    F
         DS    4F
XAU      DS    X
         DS    XL7
XSW      DS    18F               STANDARDS IO DRIVER AREA.
XSAEND   EQU   *
JOBTABLE DS    30CL12
JOBEND   DS    F
         SPACE 2
FRAMES   DS    F .               CURRENT NUMBER OF OUTPUT FRAMES
CURFRAM  DS    F .               CURRENT FRAME NUMBER
CUROUT   DS    F .               CURRENT OUTPUT START ADDRESS
BOTTOM   DS    F .               POINTER TO BOTTOM OUTPUT TABLE
CONTROL  DS    F .               POINTER TO TOP OF OUTPUT TABLE
CURRENT  DS    F .               POINTER TO CURRENT OUTPUT LINE
CURNUMS  DS    F .               CURRENT NUMBER OF OUTPUT LINES
NUMTABS  DS    F .               TOTAL NUMBER OF LINES IN ARRAY
TABSTART DS    F .    S T A R T  OF OUTPUT ARRAY TABLE
         SPACE 2
AREAEND  DS    0D
AREASIZE EQU   *-DCMSWORK
ENTRIEZ  EQU  (65500-AREASIZE)/79
NUMENT   EQU  ENTRIEZ-2
      TITLE '#CMDIO    -  #COMMAND COMMON TPUT/WTO ROUTINE.  '
#CMDIO   CSECT
***********************************************************************
*
*        IEESW13D -- COMMON IO ROUTINE.
*
*        DOES ALL TPUT/WTO FOR COMMAND SUBSYSTEM.
*
*        ON ENTRY:
*                 R1  = ADDRESS OF WTO FORMAT MASSAGE.
*                 R2  = XSA ADDRESS.
*
*        CSECT  NAME: IEESW13D
*        MODULE NAME: IEESW13D
*        ATTRIBUTES : REENTRANT,REUSABLE
*
*        NOTE: OUTPUT BUFFER IS SETUP IN WTO FORMAT PRIOR TO ENTRY.
*
***********************************************************************
         B     14(0,15)           BRANCH AROUND ID
         DC    AL1(8)
         DC    CL8'#CMDIO  '
         USING XSA,R2
         STM   R0,R15,XSW+8       EXTENDED SAVE AREA.
         USING #CMDIO,R12
         LR    R12,R15
         SPACE 3
*
*              R3 = TOP OF TABLE             (CONTROL)
*              R4 = CURRENT TABLE ENTRY      (CURRENT)
*              R5 = CURRENT USED ENTRIES     (CURNUMS)
*              R6 = MAX ENTRY  NUMBER        (NUMTABS)
*
         LM    R3,R6,CONTROL
         MVC   0(158,R4),MSGSPACE   BLANK CURRENT AND NEXT
         LA    R5,1(R5)             BUMP COUNT
         ST    R5,CURNUMS                     OF ENTRIES USED
         LA    R9,79(R4)            POINT TO NEXT
         ST    R9,CURRENT                        AVAIL ENTRY
         LH    R7,0(R1)             GET AND
         SH    R7,=H'5'                    ADJUST LENGTH
         EX    R7,OUTMOVE           COPY THE OUTPUT LINE
         CR    R5,R6
         BL    #IOEXIT              EXIT IF TABLE NOT FULL
*        XC    CURNUMS(4),CURNUMS        ELSE
         ST    R3,CURRENT           ADJUST TABLE POINTERS TO WRAP
#IOEXIT  EQU   *
         LM    R0,R15,XSW+8
         BR    R14
*
****
****
*
OUTMOVE  MVC   0(0,R4),4(R1)
         EJECT
***********************************************************************
*
*        CONSTANTS, TABLES, ETC.
*
***********************************************************************
         LTORG
MSGSPACE DC    180C' '
*        DSXSA
         END
