DOMAINS  TITLE 'DCMS DOMAIN TABLE SUBROUTINE'
#DMN    #STAKCB DSECT=YES
         EJECT
DOMAINS  DCMSTART R,LV=4096,IOCB=NO
         L     R1,0(R1)
         LR    R10,R1
         USING #DMN,R10
         L     R3,CVTPTR
         USING CVT,R3
         L     R11,CVTOPCTP
         DROP  R3
         USING RMCNS,R11
#DMN    #STAK  RESET
         MVI   @ZERO,X'00'
         LH    R7,RMCTDMNC        GET # OF DMN ENTRIES
         L     R6,RMCTDMDT        GET DMDT
         USING DMDT,R6
         L     R4,#SIZ#DMN         ***LIMIT OF DOMAINS DISPLAYED
         CR    R4,R7
         BNL   *+6
         LR    R7,R4
         L     R1,PARMADDR        GET PARM POINTER
         L     R1,4(R1)           POINT TO SECOND PARM
*        L     R1,0(R1)
         CLI   3(R1),C'Z'
         BNE   *+8
         MVI   @ZERO,X'FF'
         SR    R14,R14            CLEAR DMN # REG
         IC    R14,1(R1)          GET STARTING DMN #
         LTR   R14,R14
         BZ    DODOMAIN           SKIP IF START = 0
         CR    R14,R7
         BNL   DODOMAIN
*
         BCTR  R7,0
         SR    R15,R15
SSKIP    EQU   *
         LA    R6,DMDTLEN(R6)
         IC    R15,DMDTNO
         CR    R14,R15
         BE    DODOMAIN
         BCT   R7,SSKIP
         B     SRMEXIT
*
DODOMAIN EQU   *
         LA    R5,#TT1#DMN
         MVC   0(79,R5),TITLE#1
         LA    R5,#TT2#DMN
         MVC   0(79,R5),TITLE#2
         LH    R1,RMCTDMNC
         ST    R1,DECWORK1
         BAL   R14,@DECIMAL
         MVC   27(10,R5),=CL10'DOMAINS'
         MVC   23(3,R5),DECWORK4+1
         CLI   @ZERO,X'00'
         BE    NOSUPMSG
         MVC   39(39,R5),TITLE#2A
NOSUPMSG EQU   *
         LA    R5,#TT3#DMN
         MVC   0(79,R5),TITLE#3
         LA    R5,DOMAINZ
DMNLOOP  EQU   *
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         BAL   R8,DMNLINE
         LA    R6,DMDTLEN(R6)
         BCT   R7,DMNLOOP
         B     SRMEXIT
         EJECT
DMNLINE  EQU   *
         SR    R2,R2
         IC    R2,DMDTNO               DOMAIN #
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   4(3,R5),DECWORK4+1
         IC    R2,DMDTLO               MIN MPL
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   9(3,R5),DECWORK4+1
         IC    R2,DMDTHI              MAX MPL
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   13(3,R5),DECWORK4+1
         LH    R2,DMDTMPLT       MPL TARGET
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   17(3,R5),DECWORK4+1
         LH    R2,DMDTCMPL            CURRENT MPL
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   22(3,R5),DECWORK4+1
         LA    R2,8
         AH    R2,DMDTRUA             READY USER AVERAGE
         SRDA  R2,32
         D     R2,=F'16'
         ST    R3,DECWORK1
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   27(3,R5),DECWORK4+1
         LH    R2,DMDTINCU             INCORE  USERS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   31(3,R5),DECWORK4+1
         LH    R2,DMDTNSW              NONSWAPPABLE USERS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   35(3,R5),DECWORK4+1
         LH    R2,DMDTGOOU            GOING OUT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   39(3,R5),DECWORK4+1
         LH    R2,DMDTOUTU             OUTREADY USERS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   44(3,R5),DECWORK4+1
         L     R2,DMDTTWSR             WEIGHTED INTVL SERVICE
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   49(4,R5),DECWORK4
         MVC   54(4,R5),=CL4'????'
         SR    R2,R2
         IC    R2,DMDTWT              WEIGHT
         LTR   R2,R2
         BZ    XDOBJ
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   54(3,R5),DECWORK4+1
         MVI   57(R5),C'W'
         B     XCIDX
XDOBJ    EQU   *
         SR    R2,R2
         IC    R2,DMDTDOBJ            DOBJ
         LTR   R2,R2
         BZ    XAOBJ
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   54(3,R5),DECWORK4+1
         MVI   57(R5),C'D'
         B     XCIDX
XAOBJ    EQU   *
         SR    R2,R2
         IC    R2,DMDTAOBJ            AOBJ
         LTR   R2,R2
         BZ    XFOBJ
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   54(3,R5),DECWORK4+1
         MVI   57(R5),C'A'
         B     XCIDX
XFOBJ    EQU   *
         SR    R2,R2
         IC    R2,DMDTFWKL            FWKL
         LTR   R2,R2
         BZ    XCIDX
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   54(3,R5),DECWORK4+1
         MVI   57(R5),C'F'
XCIDX    EQU   *
         L     R2,DMDTCIDX             CONTENTION INDEX
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   62(4,R5),DECWORK4
*
*        END OF OPERATOR COMPATIBLE DISPLAY FIELDS
*
         LH    R2,DMDTRUMX             OUTREADY USERS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   68(3,R5),DECWORK4+1
         IC    R2,DMDTWT              WEIGHT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   72(3,R5),DECWORK4+1
         MVC   DECWORK4(4),=CL4'   0'
         IC    R1,DMDTWT        CURRENT WEIGHT
         MH    R1,DMDTRUA        TIMES
         SR    R0,R0             READY USER AVG
         LH    R2,DMDTMPLT       DIVIDED BY
         LTR   R2,R2
         BZ    CCINODR
         DR    R0,R2             MPL TARGET
         ST    R1,DECWORK1       EQUALS
         BAL   R14,@DECIMAL      CONTENTION INDEX
CCINODR  EQU   *
*        MVC   76(3,R5),DECWORK4+1
         CLI   @ZERO,X'00'             IF ZERO SUPPRESS:
         BE    PUT#OUT                 THEN
         CLC   DMDTGOOU(8),ZZEROS         SKIP
         BNE   PUT#OUT                        LINES
         CLC   DMDTCIDX(6),ZZEROS                  WITH
         BNE   PUT#OUT                                 ALL
         BR    R8                                         ZEROS
PUT#OUT  EQU   *
         #PUT  #DMN,DOMAINZ
         BR    R8
         EJECT
@BIN     DS    0H
         OC    DECWORK4(4),=XL4'F0F0F0F0'
         MVC   HEXWORK1(4),DECWORK4
         PACK  DOUBLE(8),DECWORK4(4)
         CVB   R1,DOUBLE
         ST    R1,DECWORK1
         BR    R14
         SPACE 3
@LIM     DS    0H
         L     R15,DECWORK1     GET VALUE TO BE LIMITED
         C     R15,0(R1)       SET
         BNL   @LIM2             TO
         L     R15,0(R1)            LOWER BOUND
         B     @LIM9          IF LT LOWER LIMIT
@LIM2    EQU   *
         C     R15,4(R1)       SET
         BNH   @LIM9             TO
         L     R15,4(R1)            UPPER BOUND
@LIM9    EQU   *
         ST    R15,DECWORK1    IF GT UPPER LIMIT
         LR    R1,R15          - OTHERWISE
         BR    R14                       LEAVE ALONE
LIMUIC   DC    F'001'
         DC    F'255'
LIMCPU   DC    F'001'
         DC    F'101'
LIMASM   DC    F'001'
         DC    F'255'
LIMPTR   DC    F'010'
         DC    F'150'
LIMLCH   DC    F'001'
         DC    F'100'
@DLIM0   DC    F'000'
         DC    F'255'
@DLIM1   DC    F'001'
         DC    F'255'
         EJECT
TITLE#1  DC CL79'   D C M S   DOMAIN TABLE DISPLAY  '
TITLE#2  DC CL79'             THERE ARE              '
TITLE#3  DC CL79'   DMNNO MIN MAX MPLT CMPL RUA INC NSW GOO OUTU  TWSR C
                TCTL   CIDX  RUM  WT    '
TITLE#2A DC CL39'--ZERO ACTIVITY DOMAINS SUPPRESSED--'
ZZEROS   DC 16X'00'
SRMEXIT  DCMSTOP RC=(15)
DOMAINZ  DC    CL79' '
PARMPTR  DC    F'0'
DMLINE   DS    F .
@A#      DS   X
@ALO     DS   F
@AHI     DS   F
@AWT     DS   F
@DLO     DS   F
@DHI     DS   F
@DWT     DS   F
@UICL    DC    F'0'
@UICH    DC    F'0'
@CPUL    DC    F'0'
@CPUH    DC    F'0'
@ASML   DC    F'0'
@ASMH   DC    F'0'
@PTRL   DC    F'0'
@PTRH   DC    F'0'
@LOW     DC   F'0'
@HI      DC   F'0'
@AV      DC   F'0'
@CT      DC   F'0'
@MODE    DS   X .
@AUTH    DS   X .
@ZERO    DS   X .
         EJECT
         CVT   DSECT=YES
         EJECT
RMCNS    RMCNS
         EJECT
DMDT     IRADMDT
         END
