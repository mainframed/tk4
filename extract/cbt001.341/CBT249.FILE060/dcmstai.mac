STAI TITLE 'TL/1 COMMAND AND PROGRAM STAI SUPPORT'
         TLIDATA
         EJECT
         TLIREGS
         EJECT
TLISTAI CSECT
         USING *,R15               START WITH BASE
         B     PAST INCORE
         DC    C'TLISTAI '
PAST     DS    0H
         C     R0,FULL             IF HAPPY DAYS
         BNE   YEH                  DOIT
         LA    R15,0              CONTINUE KILL
         BR    R14                  AND OUT
FULL     DC    F'12'
YEH      DS    0H                  CAN PLAY AROUND
         BALR  R11,0               GET A BASE
         USING *,R11                AND USE IT
         LR    R9,R1              SAVE STAI BLOCK
         LA    R0,72               SAVE AREA SIZE
         GETMAIN R,LV=(0)          GET IT
         STM   R14,R12,12(R13) SAVE REGS
         ST    R13,4(R1)           SET UP
         ST    R1,8(R13)            CHAIN
         LR    R13,R1              AND USE NEW SAVE AREA
         L     R10,0(R9)           GET TLIDATA ADDRESS
         USING TLIDATA,R10         USE TLIDATA
*        R9-> STAI CONTROL BLOCK
         LA    R2,16               GET KILL CODE
         ST    R2,TLIREGS          SAVE IT
*
*        FOLLOWING GOES ALL ABEND TESTING OR RECOVERY CODE
*
         CLC   5(2,R9),=X'13E0'    IF DETACH
         BE    U322                 THEN ISSUE U322
         CLC   5(2,R9),=X'33E0'    IF DETACH
         BNE   NOU322
U322     ABEND 322,DUMP             THEN ISSUE U322
NOU322   DS    0H
*
*        END OF ABEND TESTING OR RECOVERY CODE
*
         LR    R1,R13              GET SAVE ADDRESS
         L     R13,4(R13)          GET FORMER SAVE ADDRESS
         LA    R0,72               GET LENGTH
         FREEMAIN R,LV=(0),A=(1) FREE IT
         L     R15,TLIREGS         GET RETURN CODE
* 16=KILL,12=STAI RETRY ROUTINE (SEE MVT GUIDE FOR RETRY PARTICULARS
         LM    R0,R12,20(R13)     GET REGS
         L     R14,12(R13)         GET RETURN REG
         BR    R14                  AND OUT
******************************************************************R2344
*   PROJECT 4123 (TL/I DISTRIBUTION)                              R2344
*   SEQUENCING:NEW1=10000 INCR=10000                              R2344
******************************************************************R2344
         END
