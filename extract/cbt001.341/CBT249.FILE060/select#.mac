SELECT#  TITLE 'SELECT  --  DCMS SELECTOR'
US1NG    OPSYN USING
USING    OPSYN
DR0P     OPSYN DROP
DROP     OPSYN
         DCMSREGS
         EJECT
SELECT#  CSECT
* INITIALIZE THE SELECTOR
         SAVE  (14,12)                 SAVE SO TERMIO CAN FIND DCB
         L     R9,0(R1)                GET TDCB ADDRESS
         USING TDCB,R9
         LR    R11,R15        INITIALIZE
         L     R10,TDCBVECT
         L     R10,0(R10)
         USING SELECT#,R11
         USING DCDA,R10
         GETMAIN R,LV=8192,SP=3
         LA    R12,2048
         AR    R12,R12
         AR    R12,R1
         USING DCMSBUF,R12
         ST    R13,4(R1)               LINK SAVE AREAS
         ST    R1,8(R13)
         LR    R13,R1                 USE NEW SAVE AREA
         USING WORKAREA,R13
         INUSE
         OI    IOCBFLGD,IOCBDPK2
         L     R1,DCDALOAD
         ST    R1,TDCBLOAD
         EJECT
*                                                                    *
*                                                                    *
*        L O G O N  /  P A S S W O R D   P R O M P T I N G           *
*                                                                    *
*                                                                    *
         TM    TDCBCFLG,TDCBLOG        IS USER LOGGED ON
         BNZ   NOTFIRST                BR IF YES
*    ASK FOR PASSWORD IF FIRST TIME THRU. ELSE TELL COMPLETION CODE
         OI    IOCBFLGC,IOCBCTIM       SUPPRESS TIMESTAMP
         LA    R1,IOAREA               GET SCREEN IMAGE
         ST    R1,IOCBIMG              COPY
         GETIMAGE NEGO,TASKLINE
         LA    R1,SCR2                 POINT AT FORMAT WITH ENTRY OFF
         ST    R1,IOCBFMT
ASKPASS  MVC   MSGLINE,PASSLINE     ASK FOR PASSWORD
ASKPASS2 MVC   INPLINE(18),=CL18' '    CLEAR THE INPUT LINE
         BAL   R2,DISPLAY              DISPLAY THE MESSAGES
*
         L     R8,TDCBACCT             POINT AT THE TACCT
         USING TACCT,R8
         L      R1,TDCBVECT        POINT AT VECTOR
         L      R1,16(,R1)
         LTR    R1,R1
         BZ     ALLOWLGN
*        B      ALLOWLGN               SUPPRESS USE OF UADS
*        *
*       ***
*      *****
*     *******
*        *
*        * * * * * * UNCOMMENT THE UNCONDITIONAL BRANCH ABOVE
*                    TO SUPPRESS THE USE OF TSO FORMAT LOGON
*                    ID/PASSWORD AND SYS1.UADS
*
          EJECT
         LA    R2,ACCTDEPT             POINT TO FIRST RECEIVING FIELD
         LA    R1,2                    SET NUMBER OF FIELDS
         LA    R7,INPLINE              POINT AT INPUT
LOGSCAN0 SR    R5,R5                   SET NUMBER OF CHARACTERS OK
         LA    R6,9                    SET NUMBER TO EXAMINE
         LA    R3,8                    SET LENGTH OF RECEIVING FIELD
LOGSCAN1 CLI   0(R7),C'A'
         BL    LOGSCAN2                LOOKING FOR ALPHA NUMERIC
         LA    R5,1(R5)
         LA    R7,1(,R7)               POINT AT NEXT BYTE
         BCT   R6,LOGSCAN1
         B     ASKPASS                 TOO LONG A FIELD
         SPACE 2
LOGSCAN2 CH    R1,=H'1'                WHICH PASS IS IT?
         BE    LOGSCAN3                SECOND, MUST HAVE A BLANK
         CLI   0(R7),C'/'              ELSE LOOK FOR A SLASH
         BE    SEPOK
         B     ASKPASS
         SPACE 1
LOGSCAN3 CLI   0(R7),C' '              IS SEPARATOR RIGHT
         BNE   ASKPASS                 NO, TELL HIM TO TRY AGAIN
         SPACE 2
SEPOK    LTR   R5,R5                   WERE ANY CHARACTERS OK
         BZ    ASKPASS
         LR    R4,R7
         SR    R4,R5                   POINT AT START OF FIELD
         ICM   R5,8,=CL18' '           SET FILL CHARACTER
         MVCL  R2,R4                   MOVE THE FIELD ACROSS
         LA    R7,1(,R7)               SKIP OVER THE SEPARATOR
         BCT   R1,LOGSCAN0             GO DO ANOTHER FIELD
         LA    R5,ACCTDEPT+1           POINT AT FIRST MAYBE BLANK
ACCTBSCN CLI   0(R5),C' '              IS THIS A BLANK
         BE    ACCTBFND                YES, GO ZERO IT
         LA    R5,1(,R5)               MOVE TO NEXT
         B     ACCTBSCN
         SPACE 2
ACCTBFND MVI   0(R5),C'0'              APPEND A ZERO FOR UADS
         ENQ   MF=(E,UADSENQ)
         LTR   R15,R15
         BZ    GOTUADS
         EX    0,*                     LOOK AT R15
GOTUADS  L     R1,TDCBVECT
         L     R2,16(R1)               POINT AT UADS DCB
         FIND  (R2),ACCTDEPT,D
         LTR   R15,R15
         BZ    ACTFND
         DEQ   MF=(E,UADSENQ)
         B     BADPASS
ACTFND   LA    R3,UADSDATA
         READ  UADSDECB,SF,(R2),(R3),4000
         CHECK UADSDECB
         DEQ   MF=(E,UADSENQ)
         LH    R2,UADSDATA+26          POINT AT PASSWORD BLOCK
NEXTPASS L     R3,UADSDATA+8(R2)       POINT AT DATA BLOCK
         LA    R4,UADSDATA(R3)         GET DATA ADDRESS
         CLC   ACCTPROG(8),4(R4)       CHECK PASSWORD
         BE    GOTPASS
         LA    R4,UADSDATA(R2)         GET PASSWORD BLOCK ADDRESS
         TM    0(R4),X'80'             ARE THERE MORE
         BO    BADPASS                 NO, COMPLAIN
         L     R2,UADSDATA(R2)         MOVE TO NEXT PASSWORD
         B     NEXTPASS
         SPACE 3
GOTPASS  L     R3,UADSDATA+4(R2)       POINT AT ACCOUNT BLOCK
         L     R3,UADSDATA+8(R3)       POINT AT ACCOUNT NUMBER DATA
         LA    R3,UADSDATA(R3)         GET ADDRESS OF ACCOUNT NUMBER
         B     ALLOWLGN                ALLOW HIM ON
         SPACE 3
BADPASS  MVC   PROMPT,BADLGN
         B     ASKPASS2                GO ASK AGAIN
         EJECT
*                                                                    *
*                                                                    *
*        L O G O N    A C C E P T    P R O C E S S I N G             *
*                                                                    *
*                                                                    *
ALLOWLGN XC    ACCTSTAT,ACCTSTAT       CLEAR STATISTICS
         XC    ACCTTIME(8),ACCTTIME    CLEAR LOGON TIME AND DATE
         MVC   ACCTDEPT(16),=CL18' '
         MVI   ACCTDEPT,C'D'
         MVC   ACCTDEPT+1(3),45(R3)
         MVC   ACCTPROG(3),48(R3)      GET PROGRAMMER ID
         MVC   ACCTFLGS(1),UADSDATA+16 GET PRIVILEDGE FLAGS
         MVC   OPMSG(LDOPMSG),DOPMSG
         MVC   OPMSG+4(8),ACCTPROG
         DROP  R8
         L     R8,TDCBDUPT
         USING DUPT,R8
         L      R1,TDCBVECT        POINT AT VECTOR
         L      R1,16(,R1)
         LTR    R1,R1
         BZ     ALL0
*        WTO   MF=(E,OPMSG)
ALL0     EQU    *
         OI    TDCBCFLG,TDCBLOG        NOTE LOGON COMPLETE
*        GETIMAGE LOGO,L3                     LOGO
         L     R4,DCDALOGO                        SCREEN
         L     R3,=A(22*79)                             ONTO
         LA    R2,MSGLINE                                   BUFFER
         LR    R5,R3
         MVCL  R2,R4
         MVC   MSGLINE,LOGON           TELL HIM HE'S ON
*        MVC   PROMPT,=CL79' '       NO PENDING TASKS YET
*        MVC   TASKLINE,=CL79' '       NO PENDING TASKS YET
         LA    R1,SCR1                 RESTORE NORMAL INPUT LINE
         ST    R1,IOCBFMT
         MVC   PROMPT,SERVMSG          ASK FOR SERVICE NAME
         B     ASKSERV2                SEE WHAT WE CAN DO FOR THE USER
         EJECT
*
*                                                                    *
*                                                                    *
*        R E T U R N   C O D E  /  S E R V I C E   P R O M P T       *
*                                                                    *
*                                                                    *
NOTFIRST EQU   *
         OI    IOCBFLGC,IOCBCTIM       SUPPRESS TIMESTAMP
         LA    R1,SCR1                 GET NORMAL SCREEN FORMAT
         ST    R1,IOCBFMT
         LA    R1,IOAREA               GET SCREEN IMAGE
         ST    R1,IOCBIMG
         TM    TDCBCFLG,TDCBRETA       SEE IF FIRST ENTRY AFTER ABEND
         BO    RETRY                   BR IF YES
         CLC   =F'0',TDCBABND          SEE IF LAST SERVICE BAD COMP CO
         BNE   LASTABND                BR IF IT DID
         MVC   MSGLINE,SUCCESS         TELL USER IT WAS OK
         B     ASKSERV                 AND ASK FOR NEXT SERVICE
LASTABND MVC   MSGLINE,ABNDMSG         TELL USER THE BAD NEWS
         UNPK  TEMP(9),TDCBABND(5)     CONVER TO HEX ABEND CODE
         TR    TEMP(8),HEXTAB
         MVC   MSGLINE+36(8),TEMP      MOVE CODE TO OUTPUT LINE
         B     ASKSERV                 ASK AGAIN
RETRY    NI    TDCBCFLG,255-TDCBRETA   TURN OFF RETRY ENTRY FLAG
         MVC   MSGLINE,RETRYMSG        TELL USER ABOUT RETRY OPTION
         MVC   TASKLINE,=CL79' '       DON'T TELL ABOUT PENDING TASKS
         MVC   PROMPT,RETRYMS2         TELL USER OPTIONS
         L     R2,TDCBPEND             FAILED TASKS TDCB ADDR
         L     R3,TDCBSTWA-TDCB(R2)    GET STAE WORK AREA PNTR
         UNPK  TEMP(9),4(5,R3)         GET ABEND CODE
         TR    TEMP(8),HEXTAB          CONVERT TO HEX
         MVC   MSGLINE+22(8),TEMP      MOVE INTO MSG
         MVC   INPLINE(18),=CL18' '    CLEAR THE INPUT LINE
         BAL   R2,DISPLAY              ASK USER WHAT TO DO
         CLC   =C'YES',INPLINE         SAVE TASK STATUS
         BE    ASKSERV                 YES, JUST ASK FOR NEXT PGM
         CLC   =C'NO',INPLINE          CANCEL DYING TASK
         BNE   RETRY                   IF NOT, ASK AGAIN
         L     R2,TDCBPEND             GET STOPPED TASKS TDCB ADDR
         POST  TDCBSECB-TDCB(R2)       LET STOPPED TASK CONTINUE
         MVC   MSGLINE,FIRSTMSG
         B ASKSERV                     ASK FOR NEXT SERVICE
         EJECT
* ASK USER FOR SERVICE NAME
         INUSE
ASKSERV  EQU   *                       ASK FOR SERVICE NAME
         MVC   PROMPT,SERVMSG          ASK FOR SERVICE NAME
         MVC   TASKLINE,=CL79' '       ASSUME NO PENDING TASKS
         MVC   L3,=CL79' '             CLEAR CONFIGURATOR
         MVC   L4(158),MACHINE               OUTPUT
         MVC   L4+8(4),DCDASMCA
         MVC   L4+28(4),DCDASYS
         MVC   L4+53(7),DCDAVERS+1
         MVC   L4+69(4),DCDAJES
         MVC   L5+8(12),DCDACPU0
         MVC   L5+23(12),DCDACPU1
         CLI   DCDACPU1,X'40'
         BNE   *+8
         MVI   L5+21,C' '
         MVC   L6,=CL79' '
         L     R4,DCDASTAT             STATISTICS SCREEN
         L     R3,=A(18*79)                             ONTO
         LA    R2,L7                                        BUFFER
         L     R5,=A(14*79)            FROM DCDA
         ICM   R5,8,=C' '
         MVCL  R2,R4
ASKSERV2 SR    R1,R1                   PENDING TASK COUNTER
         LR    R15,R9                  INIT PEND COUNT LOOP
PENDLOOP ICM   R0,15,TDCBPEND-TDCB(R15) SEE IF PEND TASK EXISTS
         BZ    TASKNO                  IF NO MORE, INSPECT COUNT
         LA    R1,1(R1)                UP PENDING TASK COUNTER
         LR    R15,R0                  CONTINUE DOWN CHAIN
         B     PENDLOOP                CONTINUE COUNTING
TASKNO   LTR   R1,R1                   ANY PENDING TASKS
         BZ    NOPEND                  BR IF NONE
         CVD   R1,DWORD                CONVERT PEND COUNT
         MVC   TASKLINE,TASKMSG        PEND TASK MSG TO BUFFER
         ED    TASKLINE(4),DWORD+6     PUT PEND COUNT IN MSG
         BCT   R1,NOPEND               BR IF PLURAL MSG OK
         MVI   TASKLINE+9,C' '         ELSE REMOVE THE S
NOPEND   MVC   INPLINE(18),=CL18' '    CLEAR THE INPUT LINE
         BAL   R2,DISPLAY              DISPLAY AND READ TERMINAL
         CLC   INPLINE(4),=C'IBMB'     IS IT A TRANSIENT LIB MODULE
         BE    BADNAME                 YES, DON'T LOAD IT
         CLC   INPLINE(8),=CL8' '      IS IT A TRANSIENT LIB MODULE
         BE    ASKSERV                 YES, DON'T LOAD IT
         LA    R2,8                    MAX NAME LENGTH
         LA    R3,INPLINE              ADDR OF ANSWER
LOOP1    CLI   0(R3),C' '              LOOK FOR ENDING BLANK
         BE    BLANKOUT                BR IF FOUND
         CLI   0(R3),C'A'              SEE IF LEGAL CHAR
         BL    BADNAME                 MUST NOT BE SPECAIAL CHAR
         LA    R3,1(R3)                POINT TO NEXT BYTE
         BCT   R2,LOOP1                CHECK ALL 8 BYTES
         EJECT
* NOW SEE IF THE SERVICE EXISTS
BLDL     MVC   BLDLLIST(4),BLDLDATA    PARM LIST HEADER
         MVC   BLDLLIST+4(8),INPLINE   COPY SERVICE NAME
         BLDL  0,BLDLLIST              SEE IF SERVICE EXISTS
         LTR   R15,R15                 CHECK BLDL RETURN CODE
         BNZ   BADNAME                 NON-ZEOR MEANS NO MODULE
         CLI   BLDLLIST+16,2           WHICH LIBRARY WAS IT IN
         BL    BADNAME                 LINKLIB - WRONG PLACE
* COPY MOD NAME, FEEE WORK AREA, AND GO TO DESIRED SERVICE
         L    R1,SAVE+4               PREVIOUS SAVE AREA ADDR
         MVC   0(8,R1),INPLINE        COPY MODULE NAME TO SAVE AREA
         LR   R12,R13
         L    R13,SAVE+4
         FREEMAIN R,LV=8192,SP=3,A=(R12)
         LM    14,12,12(R13)           RESTORE ORIGINAL REGS FOR FRITZ
         XC    8(8,R13),8(R13)         CLEAR XCTL PARM LIST
         XCTL  EPLOC=0(R13),DCB=0,SF=(E,8(R13))
         EJECT
* CLEAR TRAILING GARBAGE FROM NAME
LOOP2    MVI   0(R3),C' '              BLANK OUT NEXT BYTE
BLANKOUT LA    R3,1(R3)                POINT TO NEXT BYTE
         BCT   R2,LOOP2                CLEAR REST OF NAME
         B     BLDL                    GO LOOK FOR MODULE
BADNAME  MVC   MSGLINE,BADMSG          TELL USER NAME IS BAD
         MVC   MSGLINE(8),INPLINE      PUT NAME IN MESSAGE
         B     ASKSERV                 AND TRY AGAIN
         EJECT
* DISPLAY MSG, READ ANSWER, AND CHECK FOR END CONDITIONS
DISPLAY  TRMIO IOCB
         CLI   IOCBAID,AIDPA1          DID USER HIT PA1
         BE    ENDIT                   IF YES, LOGOFF
         CLI   IOCBAID,AIDPF2
         BNE   DISPOKEY
         MVC   INPLINE(8),=CL8'DCMSPROF'
         BR    R2
*
DISPOKEY EQU   *
         CLI   IOCBAID,AIDPF12
         BE    ENDIT
         CLI   IOCBAID,AIDENT          DID HE HIT ENTER
         BNE   DISPLAY                 IF NOT, DISPLAY AGAIN
*
*        SELECT - PF KEY SUPPORT 01/21/78
*
         CLC   =C'END ',INPLINE        DID USER LOG OFF
         BNER  R2                      IF NOT, RETURN TO CALLER
         EJECT
ENDIT    TM    TDCBCFLG,TDCBLOG        IS HE LOGGED OFF
         BZ    DISPLAY                 YES, THAT'S NICE
         NI    TDCBCFLG,255-TDCBLOG    NO, BUT HE IS NOW
         OC    TDCBPEND,TDCBPEND       IS THIS THE LAST ONE
         BNZ   NOLOGOFF                NO, NO MESSAGE
*---------------------------------------------------------------
* IF THE USER SPECIFIED 'PARM=NOUADS' DONT DO THE WTO..
*---------------------------------------------------------------
         L     R1,TDCBVECT        POINT AT VECTOR
         L     R1,16(,R1)
         LTR   R1,R1
         BZ    NOLOGOFF            NO WTO NECESSARY
         MVC   OPMSG(LDOPMSG),DOPMSG   INITIALIZE WTO AREA
         L     R1,TDCBACCT
         USING TACCT,R1
         MVC   OPMSG+4(8),ACCTPROG
         MVC   OPMSG+13(3),=C'OFF'
         DROP  R1
         WTO   MF=(E,OPMSG)            NOTIFY THE OPERATOR
NOLOGOFF EQU  *
         LR   R10,R13
         L    R13,SAVE+4               PREVIOUS SAVE AREA ADDR
         FREEMAIN R,LV=8192,SP=3,A=(R10)
         RETURN (14,12),RC=0
         EJECT
BLDLDATA DC    H'1,58'                 BLDL PARM LIST HEADR
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'     HEX CONVERSION TABLE
FIRSTMSG DC    CL80' '
PASSLINE DC    CL80'ENTER YOUR DCMS LOGON CODE:'
SYSUADS  DC    CL8'SYSUADS'
UADSENQ  ENQ   (SYSUADS,SYSUADS,E,8,STEP),MF=L
BADLGN   DC    CL80'INVALID USERID/PASSWORD.'
SUCCESS  ZB   'DCMS   SELECTOR'
LOGON    ZB   'WELCOME TO DCMS'
RETRYMSG DC    CL79'SERVICE ABENDED CODE='
RETRYMS2 DC    CL79'SHOULD THE TASK BE PRESERVED? (YES OR NO)'
MACHINE  DS    0CL158
M1       DC    CL40'SYSTEM  ____  IS  A  MODEL  XXXX '
M2       DC    CL39'MVS  RELEASE XXXXXXX  USING  JES? '
M3       DC    CL40'SERIAL: 123456789012 / 123456789012 '
M4       DC    CL39' '
ABNDMSG  DC    CL80'SERVICE TERMINATED ABNORMALLY. CODE='
SERVMSG  DS    0CL80
         DC    CL40'ENTER NAME OF SERVICE DESIRED:'
         DC    CL40'IF YOU DON''T KNOW, ENTER "HELP".'
TASKMSG  DC    X'40202020',CL75' TASKS SUSPENDED'
BADMSG   DC    CL80'         IS NOT A VALID SERVICE NAME'
DOPMSG   WTO   '         ON  DCMS',ROUTCDE=8,DESC=6,MF=L
LDOPMSG  EQU   *-DOPMSG
* SCREEN FORMAT
SCR1     SFMT
MSGLINE  FIELD 79
TASKLINE FIELD 79
PROMPT   FIELD 79
L3       FIELD 79
L4       FIELD 79
L5       FIELD 79
L6       FIELD 79
L7       FIELD 79
L8       FIELD 79
L9       FIELD 79
L10      FIELD 79
L11      FIELD 79
L12      FIELD 79
L13      FIELD 79
L14      FIELD 79
L15      FIELD 79
L16      FIELD 79
L17      FIELD 79
L18      FIELD 79
L19      FIELD 79
L20      FIELD 79
L21      FIELD 79
INPLINE  FIELD 18,ALPHA,CURSOR=YES,ROW=23,COLUMN=2,INTEN=HI
         SFEND
SCR2     SFMT
XSGLINE  FIELD 79
XASKLINE FIELD 79
XROMPT   FIELD 79
X3       FIELD 79
X4       FIELD 79
X5       FIELD 79
X6       FIELD 79
X7       FIELD 79
X8       FIELD 79
X9       FIELD 79
X10      FIELD 79
X11      FIELD 79
X12      FIELD 79
X13      FIELD 79
X14      FIELD 79
X15      FIELD 79
X16      FIELD 79
X17      FIELD 79
X18      FIELD 79
X19      FIELD 79
X20      FIELD 79
X21      FIELD 79
XNPLINE  FIELD 18,ALPHA,CURSOR=YES,ROW=23,COLUMN=2,INTEN=OFF
         SFEND
         LTORG
         EJECT
WORKAREA DSECT
SAVE     DS    18F
IOCB     IOCB
DWORD    DS    D                       CVB WORK AREA
BLDLLIST DS    31H                     BLDL PARM LIST AREA
TEMP     DS    CL9
@MODE    DS    X .
OPMSG    WTO   '12345678 ON  DCMS',ROUTCDE=8,DESC=6,MF=L
UADSDATA DS    1000F
WORKLEN  EQU   *-WORKAREA
DCMSBUF  DSECT
IOAREA   EQU   *
         DSGEN SCR1                    SCR2 IS THE SAME
         TDCB
         TACCT
         DCMSDATA
         DCMSUPT
         END
