CHANNEL  TITLE 'C H A N N E L  LOGICAL CHANNEL ACTIVITY MONITOR'
         SPACE 4
*.
* SCAMP
*
* THIS MONITOR IS WRITTEN TO DISPLAY  ON AN INTERVAL ( CURRENTLY 1 SEC)
* CHANNEL UTILIZATION FOR ALL LOGICAL CHANNELS
*
* THIS PROGRAM IS A BIT ON THE DEFFICIENT SIDE IN THAT IT MONITORS ONLY
* THE FIRST 20 LOGICAL CHANNEL ENTRIES. I PLAN TO ALLOW THE USER TO
* EXCLUDE CERTAIN CHANNELS WITH A COMMAND, BUT HAVEN'T GOTTEN AROUND TO
* IT YET.
*
* THE FOLLOWING PF KEYS ARE DEFINED FOR THIS PROGRAM..
*
*
*    .  -----
*      × PA1 ×   LEAVE SCAMP, BACK TO SELECTOR
*      ×     ×
*       -----
*    .  -----
*      × PF1 ×   HELP KEY, DESCRIBES WHAT YOU ARE SEEING
*      ×     ×
*       -----
*
*    .  -----
*      × PF2 ×   DEFINE SCREEN UPDATE TIME. DEFAULT = ONE SECOND
*      ×     ×
*       -----
*
*    .  -----
*      × PF3 ×   CHANGE OUTPUT MODE. CAN BE EITHER PERCENT OF CHANNEL
*      ×     ×   BUSY (P) OR SIO REQUEST RATE PER SECOND (R)
*       -----
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*   PHONE:
*     (408) 735 4011
*
*.
CHANNEL  DCMSTART R,LV=4096,STAE=NO
         L     R1,0(R1)
         ST    R1,TDCBADDR
         L     R6,CVTPTR           FIND THE RMCT
         L     R6,CVTOPCTP-CVT(R6) ...
         USING RMCT,R6             ...
         BAL   R9,INISH            SET ORIGINAL SCREEN VALUES
         BAL   R9,WRITE            WRITE THE SCREEN
         SPACE
*
*  TIME  LOOP  BEGINS HERE
*
         SPACE
SCAMP0   EQU   *
         LA    R7,SCR1LNE0+9       FIRST LINE PLEASE
         L     R2,RMCTRLCT         PICK UP THE FIRST LOGICAL CHAN
         L     R3,RMCTICT          POINTER TO I/O CONTROL TABLE
*
*  ONLY THE FIRST TWENTY LOGICAL CHANNELS WILL FIT ON A SCREEN
*  SO... THATS ALL THAT CURRENTLY GOES ON
*
         LA    R9,19               SET A LIMIT FOR NOW
         USING ICT,R3
SCAMP1   EQU   *                   IS THIS THE END ?
         C     R2,ICCLCLST         IS THIS THE END ?
         BH    SCAMP5              YES, I'M AFRAID SO...
         MVC   0(70,R7),BLANKS     CLEAR THIS ENTRY
         USING RLCT,R2
         LR    R1,R7               BACK UP A MINUTE HERE
         SH    R1,=H'9'            ...
         TM    RLCTFLGS,RLCTOVER   OVER UTILIZED ?
         BZ    *+8
         MVI   6(R1),C'O'
         TM    RLCTFLGS,RLCTUNDR   UNDER UTILIZED ?
         BZ    *+8
         MVI   6(R1),C'U'
*
* THE FOLLOWING IS USELESS AFTER SU7 IN THAT THE SRM HAS NO CLUE
* WHERE THE PAGING CHANNELS ARE
*
         TM    RLCTFLGS,RLCTPAGE   PAGING CHANNEL ?
         BZ    *+8                 NO.
         MVI   7(R1),C'P'
         LH    R1,RLCTUTIL         GET THE USAGE FIGURE
         CLI   MODE,MODEPCT        PCT MONITOR ?
         BE    SCAMP2              YUP...
         LH    R1,RLCTRQRT         OTHERWISE GET RATE
SCAMP2   EQU   *
         C     R1,F69              WELL ?
         BNH   SCAMP3              ITS ALL RIGHT
*
* THIS CODE WILL OVERLAY THE ORIGINAL BAR WITH AN OVERFLOW CHARACTER
* WHEN THE UTILIZATION IS OVER 70 (THE LENGTH OF THE BAR).
* EXAMPLE:
* IF THE GRAPH IS TEN CHARS LONG AND THE AMOUNT WE ARE PLOTTING IS 15
* THE GRAPH WILL LOOK LIKE THIS;
*
* 0....+....1....+
* ×>>>>>**********
*
* WHERE '>' IS THE OVERLAY CHARACTER
*
* THANKS TO KEITH SOPER OF RAND FOR THE IDEA.
*
         MVC   0(70,R7),STARS      FIRST FIELD
         SL    R1,F69
         CL    R1,F69              STILL OVERFLOWED ?
         BH    NOHOPE               HELL.. WHATS HAPPENING HERE  ?
         EX    R1,STRIPEM          MOVE THE STRIPES
         B     SCAMP4
NOHOPE   EQU   *
         MVC   0(69,R7),OVERFLOW
         B     SCAMP4
SCAMP3   EQU   *
         EX    R1,STARMOVE         MOVE EM BOSS
SCAMP4   EQU   *
         LA    R1,RLCTSIZE
         ALR   R2,R1               GET THE NEXT DUDE
         LA    R7,79(,R7)
         BCT   R9,SCAMP1          ...
         SPACE
SCAMP5   EQU   *
         OI    IOCBFLGC,IOCBCRAO
         OI    IOCBFLGD,IOCBDYON
         TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=IOAREA
         EJECT
         PFKEY DEF=SCAMP0,K1=HELP,K3=MODESET,K9=SCAMP0,                X
               K12=SCAMP6,ATTN=SCAMP6,ENTER=SCAMP0,K7=FREEZE
         EJECT
FREEZE   EQU   *
         BAL   R9,WRITE
         B     SCAMP0
         SPACE 4
INISH    EQU   *
         LA    R2,IOAREA
         LA    R3,IOLEN
         LA    R5,X'40'
         STC   R5,EDITBLNK
         SLL   R5,24               PAD TO   BLANKS
         MVCL  R2,R4
         XC    CHANMASK(16),CHANMASK CLEAR CHANNEL MASK & COUNTS
         MVI   MODE,MODEPCT        USE PERCENT
         MVC   SCR1SCAL(9),FIELDNM
         MVC   SCR1SCA2(9),FIELDNM
         MVC   SCR1SCAL+9(69),SCALE
         MVC   SCR1SCA2+9(69),SCALE
         MVC   SCR1MSG(L'ENTERMSG),ENTERMSG
         MVC   SCR1HDR(L'MODEHDR+2),MODEHDR
         MVC   SCR1HDR+32(L'HEADER),HEADER
         LA    R2,NAMETBL          MOVE NAMES TO FRONT OF LINE
         LA    R3,SCR1LNE0
         LA    R8,20               THIS MANY NAMES
         L     R1,CVTPTR
         L     R1,CVTILCH-CVT(,R1) LOGICAL CHANNEL ENTRIES
         USING LCH,R1
         L     R4,RMCTICT
         LH    R0,ICCLCHN-ICT(,R4) GET THE NUMBER OF LOGICAL CHANLS
*
* PUT THE NUMBER OF LOGICAL CHANNELS ON THE SCREEN SO THE USER KNOWS
* WHAT HIS CONFIGURATION LOOKS LIKE, AND SO HE CAN SEE IF THERE ARE
* ANY UN-DISPLAYABLE CHANNELS (MORE THAN 20)
*
         CVD   R0,CVDWORK          KILL IT
         MVC   EDITWORK,NUMEDIT
         ED    EDITBLNK(5),CVDWORK+6
         MVC   SCR1HDR+L'MODEHDR(3),EDITWORK+1
         LA    R5,LCHEL
*
* BUILD THE NAME ENTRIES FOR THE SCREEN, THEY CONTAIN:
* LOGICAL CHANNEL #       1 - BYTE
* PHYSICAL CHANNELS #'S   N - BYTES
*
* INPUT:
*  R0 HAS COUNT OF LOGICAL CHANNEL ENTRIES
*  R8 HAS NUMBER OF LINES LEFT ON THE SCREEN
*  R1 HAS POINTER TO THE FIRST LOGICAL CHANNEL ENTRY
*  R5 HAS THE LENGTH OF A LOGICAL CHANNEL ENTRY
*
INI0     EQU   *                   NAME MOVE LOOP
         MVC   0(9,R3),0(R2)        MOVE THE NAME
         LA    R7,2(R3)
         L     R15,LCHTCH          GET THE TCH ENTRY
INI1     EQU   *
         XR    R14,R14             ...
         IC    R14,0(R15)          ...
         IC    R14,HEXTAB(R14)     ...
         STC   R14,0(R7)
         LA    R15,2(,R15)         GET THE NEXT TCH ENTRY
         LA    R7,1(,R7)           ...
         CLI   0(R15),X'FF'        LAST ?
         BNE   INI1
         LA    R2,9(,R2)           INCREMENT COUNTERS
         LA    R3,79(,R3)          ...
         ALR   R1,R5               ADDRESS NEXT LCH ENTRY
         BCTR  R0,0                IS THIS THE END ?
         LTR   R0,R0               OF THE LOGICAL CHANNELS
         BZ    *+8                 YES
         BCT   R8,INI0
         BR    R9
         SPACE 4
WRITE    EQU   *
         TRMIO IOCB,FORMAT=SCR1,IMAGE=IOAREA,BEEP=YES
         L     R1,TDCBADDR         RESET THE
         MVI   TDCBAID-TDCB(R1),0   CHEAP INPUT AREA
         BR    R9
         SPACE 4
TIMEOUT  EQU   *
         STIMER WAIT,BINTVL==F'100'
         BR    R9
         SPACE 4
HELP     EQU   *
         TRMIO IOCB,FORMAT=HELPFMT,IMAGE=HELPAREA,BEEP=YES
         BAL   R9,WRITE            REWRITE
         B     SCAMP0
         SPACE 4
MODESET  DS    0H                  CHANGE MODE
         MVC   SCR1MSG,BLANKS
         MVC   SCR1MSG(L'MODETXT),MODETXT
         BAL   R9,WRITE            WRITE THE SCREEN
* VALIDATE INPUT
         LA    R1,SCR1MSG
         AH    R1,MODEANS          ANSWER IS HERE
         CLI   0(R1),MODEPCT       PCT WANTED ?
         BNE   MODE1               NO..
         MVI   MODE,MODEPCT        INDICATE PCT
         MVC   SCR1HDR+5(4),=CL4'PCT'
         B     MODEEXIT
MODE1    EQU   *
         MVI   MODE,MODERATE       THIS JOKER GETS RATE
         MVC   SCR1HDR+5(4),=CL4'RATE'
MODEEXIT EQU   *
         MVC   SCR1MSG,BLANKS
         MVC   SCR1MSG(L'ENTERMSG),ENTERMSG    RESET
         B     SCAMP0
MODEPCT  EQU   C'P'
MODERATE EQU   C'R'
*  SCR1MSG +     0....+....1....+..2....+....3...+...4....+....5..
MODETXT  DC    C'ENTER NEW MODE ''P'' FOR PCT OR ''R'' FOR RATE ==>'
MODEANS  DC    Y(*-MODETXT)
         TITLE ' D C M S - DATA  AREAS, DSECTS AND THE LIKE'
         SPACE 3
STARMOVE MVC   0(0,R7),STARS       <<< EXECUTED >>>
STRIPEM  MVC   0(0,R7),STRIPES     <<< EXECUTED >>>
         SPACE
STARS    DC    70C'*'              BICENTENNIAL
STRIPES  DC    70C'>'                 MADNESS
         SPACE
ENTERMSG DC    C'PRESS ''PA1'' TO STOP, ''PF1'' FOR HELP ==>'
MODEHDR  DC    C'MODE=PCT   #CHANNELS='
* SCR1HDR +     32..+....4....+....5....+....6....+....7....+
HEADER   DC    C'-C H A N N E L-   LOGICAL CHANNEL USAGE       '
ZLANKS   DC    CL70' '
MORE     DC    CL10' '
HEXTAB   DC    C'0123456789ABCDEF'
NUMEDIT  DC    X'40212020202020'
OVERFLOW DC    7CL10'*OVERFLOW*'
FLIPPED  DC    7CL10'*FLIPOVER*'
FIELDNM  DC    CL9'CHANNEL-×'
NAMETBL  DC    CL9'0-      ×'
         DC    CL9'1-      ×'
         DC    CL9'2-      ×'
         DC    CL9'3-      ×'
         DC    CL9'4-      ×'
         DC    CL9'5-      ×'
         DC    CL9'6-      ×'
         DC    CL9'7-      ×'
         DC    CL9'8-      ×'
         DC    CL9'9-      ×'
         DC    CL9'A-      ×'
         DC    CL9'B-      ×'
         DC    CL9'C-      ×'
         DC    CL9'D-      ×'
         DC    CL9'E-      ×'
         DC    CL9'F-      ×'
         DC    CL9'G-      ×'
         DC    CL9'H-      ×'
         DC    CL9'I-      ×'
         DC    CL9'J-      ×'
F69      DC    F'69'
         SPACE 4
HELPFMT  SFMT
HELPAREA FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         SFEND
HELPAREA EQU   *
 DC CL79'                              -C H A N N E L-               '
 DC CL79'                    LOGICAL CHANNEL ACTIVITY MONITOR'
 DC 79C'_'                                                           '
 DC CL79' THIS MONITOR DISPLAYS UTILIZATION OF LOGICAL CHANNELS ON AN'
 DC CL79' INTERVAL SPECIFIED  BY  THE  USER. THE UTILIZATION  CAN  BE'
 DC CL79' DISPLAYED IN ONE OF TWO FORMATS, UTILIZATION BY PERCENTAGE,'
 DC CL79' OR REQUEST RATE. BOTH FIGURES ARE CALCULATED  BY THE SRM ON'
 DC CL79' A ONE SECOND INTERVAL.  FOR  DETAILED INFORMATION ON HOW HE'
 DC CL79' ARRIVES AT  THESE FIGURES, THE MODULE IRARMIOM IN COMPONENT'
 DC CL79' SC1-CX SHOULD PROVE USEFUL.                                '
 DC CL79' '
 DC CL79' PF KAY DEFINITIONS'
 DC CL79' '
 DC CL79'  -----                -----             ----- '
 DC CL79' × PF1 × HELP KEY     × PF2 × CHANGE    × PF3 × CHANGE '
 DC CL79' ×     ×              ×     × UPDATE    ×     × DISPLAY'
 DC CL79'  -----                -----  TIME       -----  MODES  '
 DC 79C'_'                                                           '
         SPACE 4
SCR1     SFMT
SCR1HDR  FIELD 79
SCR1MSG  FIELD 79,ALPHA,INTEN=HI,CURSOR=AFTER
SCR1SCAL FIELD 79
SCR1LNE0 FIELD 79,INTEN=HI
SCR1LNE1 FIELD 79,INTEN=LO
SCR1LNE2 FIELD 79,INTEN=HI
SCR1LNE3 FIELD 79,INTEN=LO
SCR1LNE4 FIELD 79,INTEN=HI
SCR1LNE5 FIELD 79,INTEN=LO
SCR1LNE6 FIELD 79,INTEN=HI
SCR1LNE7 FIELD 79,INTEN=LO
SCR1LNE8 FIELD 79,INTEN=HI
SCR1LNE9 FIELD 79,INTEN=LO
SCR1LNEA FIELD 79,INTEN=HI
SCR1LNEB FIELD 79,INTEN=LO
SCR1LNEC FIELD 79,INTEN=HI
SCR1LNED FIELD 79,INTEN=LO
SCR1LNEE FIELD 79,INTEN=HI
SCR1LNEG FIELD 79,INTEN=LO
SCR1LNEH FIELD 79,INTEN=HI
SCR1LNEI FIELD 79,INTEN=LO
SCR1LNEJ FIELD 79,INTEN=HI
SCR1SCA2 FIELD 79,INTEN=HI
         SFEND
SCALE    DC    CL70'0....+....1....+....2....+....3....+....4....+....5X
               ....+....6....+...9'
SCAMP6   DCMSTOP SCREEN=NO
IOAREA   EQU   *
         DSGEN SCR1
IOLEN    EQU   *-IOAREA
CVTA     DC    D'0'
MODE     DC    C'P'                DEFAULT UTILIZATION
CHANMASK DC    D'0'                MASK OF CHANNELS BEING MONITO RED
CVDWORK  DS    D                   CONVERT WORK AREA
EDITBLNK DS    C
EDITWORK DS    XL8                 DECIMAL WORK AREA
         CVT   DSECT=YES
         TDCB
         RLCT
         EJECT
         IRARMCT
         EJECT
         IRAICT
         EJECT
         IECDLCH
         END
