DIDOCS   TITLE 'D I D O C S - DISPLAY CONSOLE SUPPORT FOR DCMS '
         SPACE
*.
* DIDOCS
*
* THIS PROGRAM WILL DO CONSOLE SIMULATION FOR A DCMS TERMINAL.
* IT GOES OUT AND FINDS A GRAPHICS (3277) ACTIVE CONSOLE AND USES
* THAT CONSOLES SCREEN IMAGE FOR OUTPUT AND UCM-ID FOR SVC 34.
* THIS PROGRAM REQUIRES AUTHORIZATION TO USE THE SVC 34 INTERFACE.
*
* TO USE IT SIMPLY SAY.. DIDOCS
*
* ERROR MESSAGES -
*  NO ACCEPTABLE (3277) CONSOLES FOUND
*
*
* A NULL INPUT CAUSES THE SCREEN TO BE UPDATED. (SO YOU CAN
* USE IT UN-AUTHORIZED SO LONK AS YOU DONT TYPE ANYTHING IN).
*
* COMMANDS ARE NOT BROADCAST TO THE CONSOLE BEING USED SO YOU CAN
* HAVE SOME FUN WITH YOUR OPERATORS. THE COMMAND ENTERED IS PLACED
* ON THE LINE BELOW THE INPUT LINE.
*
*
*    .  -----
*      × PA1 ×   TERMINATES DIDOCS
*      ×     ×
*       -----
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*  PHONE:
*     (408) - 735 - 4011
*
*.
CONS     XSTART ,
         EJECT
         L     R1,CVTPTR           AND IN THE BEGINNING...
         L     R1,CVTCUCB-CVT(R1)  ...
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME
*                                   R4 CONTAINS UCME LENGTH
*                                    R5 -> TO THE LASST UCME
DID0     EQU   *                   SEARCH FOR SOMETHING USEFUL
         USING UCMLIST,R3
***
         L     R1,UCMUCB           DEBUGGING
         MVC   UNITXXX(3),13(R1)   ...
***
         TM    UCMATR,UCMUF        IS IT ACTIVE ?
         BZ    DID1                NO.. UNUSEABLE
         L     R10,UCMXB           IS IT GRAPHICS ?
         LTR   R10,R10             ...
         BZ    DID1                NO.. USELESS
         USING DCMTSRT,R10         POINT TO THE DCM
         L     R6,DCMADTRN         FIND  THE PAGEABLE DCM (TDCM)
         LTR   R6,R6               DOES IT EXIST ?
         BZ    DID1                NO...
         USING DCMSTRT,R6          A LITTLE
         CLI   DCMIONDX,X'10'      IS IT A 3270 ?
         MVC   XUCMID,UCMID        SAVE THIS
         BE    GOTONE              YES BY DINGLES !
*                                      DINGLES ?
DID1     EQU   *
         BXLE  R3,R4,DID0          GET THE NEXT ENTRY
         MVC   SCREEN(L'ERMSG),ERMSG NO ACCEPTABLE CONSOLES ACTIVE
         BAL   R9,WRITE            WRITE THE SCREEN
         B     END1                FINI
         EJECT
*
* A CONSOLE HAS BEEN FOUND COPY THE SCREEN AND DISPLAY IT FER THE USER
*        R3 ->  UCME
*        R10 -> RDCM
*        R6 ->  TDCM
*
GOTONE   EQU   *
         LH    R1,DCMMSGAL         NUMBER  OF LINES IN MSG AREA
         L     R7,DCMASCRN         POINT TO  THE  FIRST INPUT LINE
         LA    R8,SCREEN           OUTPUT LINE
GOT1     EQU   *
         MVC   0(74,R8),4(R7)      MOVE A LINE
         TR    0(74,R8),BLANKTAB   GET IT READABLE
         LA    R8,79(,R8)          OUR SIZE
         LA    R7,84(,R7)          THEIRS
         BCT   R1,GOT1             GET EM ALL
         L     R1,DCMAINS
         LA    R8,79(,R8)          SKIP PFK LINE
         MVC   0(UNITLEN,R8),UNITMSG MOVE OUR INSTRUCTION LINE
         LA    R8,(3*79)(,R8)      SKIP INPUT LINE
         MVC   0(L'DCMSWARN,R8),DCMSWARN
         BAL   R9,WRITE            DOIT JACK !!!
         CLI   IOCBAID,AIDPA1      WELL ?
         BE    END1
         CLI   ENTRLNE,C' '        NULL INPUT ?
         BE    GOTONE
         MVI   CMNDBUF,C' '
         MVC   CMNDBUF+1(79),CMNDBUF
         MVC   CMNDBUF(79),ENTRLNE
         MVI   ENTRLNE,C' '
         MVC   ENTRLNE+1(79),ENTRLNE RESET INPUT
         OC    CMNDBUF(79),ENTRLNE
         MVC   DCMSWARN(L'DCMSWARN),CMNDBUF
         MODESET KEY=ZERO
         LA    R1,CMND             WELL ?
         XR    R0,R0
         IC    R0,XUCMID           OUR MAN IN THE COMM TASK
         SVC   34                  FIRE HIM THRU
         LR    R2,R15              SAVE RETURN CODE
         MODESET KEY=NZERO
         LTR   R15,R2
         BZ    GOTONE              FIND OUT WUTS HAPPENIN
         SPACE  1
***
* SEE IF  WE ARE HERE BY A PREVIOUS MARRIGAE ...
*
         SPACE 1
         MVC   ENTRLNE(LNOTAUTH),NOTAUTH
         LR    R1,R15              SHOW R/C
         BAL   R9,CVTPRT           DO IT
         STCM  R15,B'1111',NOTR15  ...
         BAL   R9,WRITE            ...
         MVI   ENTRLNE,C' '
         MVC   ENTRLNE+1(79),ENTRLNE RESET INPUT
         B     GOTONE
END1     EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0      RETURN TO XCNTRL
         EJECT
CVTPRT   EQU   *
         ST    R1,CVTB
         UNPK  CVTA(9),CVTB(5)
         TR    CVTA(8),HEXTAB
         LM    R14,R15,CVTA
         BR    R9
         SPACE 2
WRITE    EQU   *
         TRMIO IOCB,FORMAT=SCR1,IMAGE=SCREEN BEEP=YES
         BR    R9
         TITLE 'D I D O C S - DATA AREAS AND THE LIKE'
IOCB     IOCB
SCR1     SFMT
SCREEN   FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79
         FIELD 79,ALPHA,CURSOR=AFTER
         FIELD 79
         FIELD 79
         SFEND
SCREEN   DC    24CL79' '           THISS IS THE MAN
ENTRLNE  EQU   SCREEN+(79*21)      WOOPS
CVTA     DC    D'0',X'0'               PLUS A GARBAGE BYTE
CVTB     DC    F'0'
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
ERMSG    DC    C'>>> NO ACCEPTABLE (3277)  CONSOLES FOUND'
DCMSWARN DC    C'IEE163I MODE= DCMS'
         SPACE 1
UNITMSG  DC    C'DCMS SIMULATED CONSOLE USING DIDOCS CONSOLE AT-'
UNITXXX  DC    CL4' '
UNITLEN  EQU   *-UNITMSG
         SPACE 1
NOTAUTH  DC    C'*** POSSIBLY NOT AUTHORIZED SVC 34 R/C = '
NOTR15   DC    CL4' '
LNOTAUTH EQU   *-NOTAUTH
         SPACE 1
CMND     DC    H'44',H'0'
CMNDBUF  DC    CL80' '
CMNDLEN  EQU   *-CMND
XUCMID   DC    X'0'
         LTORG
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'Ö'
         DC    C'Ö.<(+×',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);^-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
         TITLE 'MVS UCM DEFINITION'
         IEECUCM FORMAT=NEW
         TITLE 'MVS RESIDENT DCM DEFINITION (RDCM)'
         IEERDCM
         TITLE 'MVS TRANSIENT DCM DEFINITION (TDCM)'
         IEETDCM
         TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         END
