LOG$     TITLE 'PFK 10 PROCESSOR FOR HARD COPY LOG FUNCTION'
DCMSLOG$ DCMSTART  R,LV=DCMSLEN
         XC    IOCB(IOCBEND-IOCB),IOCB  CLEAR IOCB
         MVC   IOCBNAME(8),DCMS$NAM   RESET THE PROCESSOR NAME
         L     R11,0(R1)           SET TDCB ADDR
         USING TDCB,R11
         LTR   R11,R11             IF NO TDCB
         BZ    EXIT                  THEN QUIT
         LA    R2,IOCBTDCB-IOCB    WE KNOW THAT R1 POINTS INTO IOCB
         SR    R1,R2               GET ADDR OF OLD IOCB
         LR    R8,R1
         SPACE 5
*
*        CLEAR SCREEN BUFFER AND FORMAT IT
*
         DCMSNULL FORMAT=FORMAT,IMAGE=SCREEN
         SPACE 5
         MVC   SCREEN(17),=C'PRINT LOG SUPPORT'
         MVC   L4(14),=C'CURRENT STATUS'
         MVC   L6(12),=C'CURRENT MODE'
         MVC   L8(5),=C'CLASS'
         MVC   L10(4),=C'DEST'
*
*        NOW DETERMINE CURRENT STATE
*
         MVC   STATUS,=C'UNALC'    DEFAULT TO UNALLOC
         CLC   TDCBLGDC,=F'0'      IS IT OPEN
         BNE   ALCSTATE            YES SO DISPLAY
         MVI   L8+20,C'A'          SET DEFAULT CLASS
         MVC   L10+20(8),=CL8'LOCAL'  AND DEFAULT DEST
         B     ENDSTATE
ALCSTATE DS    0H                  FILE ALLOCATED OR BEING ALLOCATED
         MVC   STATUS,=C'ALLOC'    YES OPEN SO ALLOC
         MVC   L8(5),=CL5' '       IF ALLOC THEN NO NEED FOR CLASS
         MVC   L10(4),=CL5' '      OR DEST
ENDSTATE MVC   L4+20(5),STATUS     SET STATUS
         SPACE 2
*
*        NOW DETERMINE THE CURRENT MODE
*
         MVC   MODE,=CL6'OFF'      DEFAULT STATUS TO OFF
         TM    IOCBFLGL-IOCB(R8),IOCBLALL+IOCBLNOW   ANY BITS ON
         BZ    ENDMODE             NOPE
         MVC   MODE,=CL6'ON'       SET MODE TO ON
         TM    IOCBFLGL-IOCB(R8),IOCBLALL   IS IT ALL
         BO    ENDMODE             YES
         MVC   MODE,=CL6'SINGLE'
ENDMODE  MVC   L6+20(6),MODE       SET MODE
         SPACE 5
*
*        NOW DISPLAY CURRENT STATUS
*
         TRMIO IOCB,IMAGE=SCREEN,FORMAT=FORMAT
         PFKEY DEF=EXIT,K12=EXIT,K9=DCMSOVER,ENTER=CHANGES
         EJECT
*
*        NOW IDENTIFY CHANGES TO LOG SCREEN
*
CHANGES  CLC   L4+20(5),STATUS     HAS STATUS CHANGED
         BE    CKMODE              NOPE SO TRY MODE
         CLC   L4+20(5),=C'ALLOC'  DO WE WANT TO ALLOC
         BE    OPEN                IF SO THEN OPEN REQUEST
         CLC   L4+20(5),=C'UNALC'  DO WE WANT TO CLOSE
         BE    CLOSE               IF SO THEN CLOSE REQUEST
         SPACE 3
CKMODE   CLC   L6+20(6),MODE       IS IT THE SAME
         BE    DCMSOVER            IF SO THEN REDISLAY
         CLC   L6+20(6),=CL6'OFF'  WANT IT OFF
         BE    TURNOFF             YES SO TURN OFF
         CLC   L6+20(6),=CL6'ON'   TURN IT ON
         BE    TURNON
         CLC   L6+20(6),=CL6'SINGLE'    ONLY ONE
         BE    TURNSING            YES SO SET SINGLE BIT
         B     DCMSOVER
         EJECT
*
*        FUNCTION ROUTINES TO MODIFY OLD IOCB FLAGS
*
TURNON   EX    0,CLEARBIT          CLEAR THE FLAGS
         OI    IOCBFLGL-IOCB(R8),IOCBLALL
         B     DCMSOVER
         SPACE 5
TURNOFF  EX    0,CLEARBIT
         B     DCMSOVER
         SPACE 5
TURNSING EX    0,CLEARBIT
         OI    IOCBFLGL-IOCB(R8),IOCBLNOW   SET SINGLE BIT
         B     DCMSOVER
CLEARBIT NI    IOCBFLGL-IOCB(R8),255-IOCBLALL-IOCBLNOW
         EJECT
*
*        FUNCTIONS TO TURN LOGING ON AND OFF
*
CLOSE    LA    R4,LOGECLOS         SET CLOSE REQUEST
         LA    R5,16          LENGTH OF CLOSE
         EX    0,CLEARBIT
         B     OPCLCMN             GO TO COMMON CODE
         SPACE 3
OPEN     LA    R4,LOGEOPEN         SET OPEN REQUESET
         LA    R5,16+9        LENGTH OF OPEN
OPCLCMN  CLC   TDCBLGST,=F'0'      IS LOG FACILITY UP
         BZ    DCMSOVER            NOPE    SHOULD GIVE MSG
         GETMAIN R,LV=(R5)
         XC    0(16,R1),0(R1)      CLEAR THE AREA
         USING LOGELEM,R1
         ST    R4,LOGEFUNC         SAVE FUNCTION CODE
         ST    R5,LOGELEN          SET LENGTH OF AREA
         ST    R11,LOGETDCB        SAVE ADDR OF TDCB
         MVI   TDCBLGDC,X'FF'      FLAG AS OPENING
         C     R4,=A(LOGEOPEN)     IS THIS OPEN
         BNE   NOTOPEN             NOT OPEN
         MVC   LOGEDEST,L10+20
         MVC   LOGECLS,L8+20
NOTOPEN  DS    0H
*
*        NOW QUEUE ON FUNCTION QUEUE AND POST
*
         L     R3,TDCBLGST         POINT AT STACK HEAD
         L     R2,0(R2)            GET CURRENT VALUE
         ST    R2,LOGELINK         LINK TOGETHER
         CS    R2,R1,0(R3)         TRY TO QUEUE
         BNE   *-8                 IF NO GOOD TRY AGAIN
         L     R1,TDCBLGEC         POINT AT ECB
         POST  (R1)                POST  IT
         C     R4,=A(LOGEOPEN)     WAS THIS AN OPEN
         BE    CKMODE              IF SO THEN PROCESS MODE ALSO
         B     DCMSOVER
         EJECT
FORMAT   SFMT
SCREEN   FIELD 79,ALPHA,INTEN=HI
L2       FIELD 79,ALPHA
L3       FIELD 79,ALPHA
L4       FIELD 79,ALPHA,CURSOR=AFTER
L5       FIELD 79,ALPHA
L6       FIELD 79,ALPHA
L7       FIELD 79,ALPHA
L8       FIELD 79,ALPHA
L9       FIELD 79,ALPHA
L10      FIELD 79,ALPHA
L11      FIELD 79,ALPHA
L12      FIELD 79,ALPHA
L13      FIELD 79,ALPHA
L14      FIELD 79,ALPHA
L15      FIELD 79,ALPHA
L16      FIELD 79,ALPHA
L17      FIELD 79,ALPHA
L18      FIELD 79,ALPHA
L19      FIELD 79,ALPHA
L20      FIELD 79,ALPHA
L21      FIELD 79,ALPHA
         SFEND
         EJECT
*
*        DONE SO EXIT
*
EXIT     DCMSTOP SCREEN=NO
         DSGEN FORMAT
STATUS   DS    CL5                 CURRENT STATUS
MODE     DS    CL6                 CURRENT MODE
DCMSLEN  EQU   *-DCMSWORK
         EJECT
         TDCB
         EJECT
         LOGELEM
         END
