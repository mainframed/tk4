MERLIN   TITLE 'DCMS SRM CONTROL INTERFACE ROUTINE'
MERLIN   DCMSTART R,LV=4096,IOCB=YES
         MVI   @AUTH,X'F0'
         MVI   @MODE,X'7F'
         TESTAUTH FCTN=1
         LTR   R15,R15           FLAG
         BNZ   NOAUTH                AUTHORITY
         MVI   @AUTH,X'00'                    OF SESSION
NOAUTH   EQU   *
         L     R3,CVTPTR
         USING CVT,R3
         L     R11,CVTOPCTP
         DROP  R3
         USING RMCNS,R11
*                                     DO STATIC DISPLAY ITEMS  LINE 1
         LA    R5,STATLINE
         MVC   0(79,R5),BLANKS
         L     R2,RMCTWMST
         LA    R2,WMSTID-WMST(R2)      SET
         MVC   0(L'IPSMSG,R5),IPSMSG      CURRENT
         MVC   L'IPSMSG(2,R5),0(R2)              IPS
*                                     DO STATIC DISPLAY ITEMS  LINE 2
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
         MVC   0(79,R5),ERVTITL
         L     R2,RMPTCPU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   5(3,R5),DECWORK4+1
         L     R2,RMPTIOC
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   13(3,R5),DECWORK4+1
         L     R2,RMPTERV
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   21(4,R5),DECWORK4
*                                     DO STATIC DISPLAY ITEMS  LINE 3
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
         MVC   0(79,R5),RFCTITL
         L     R10,RMCTWMST
         USING WMST,R10
         MVC   07(4,R5),WMSTIPC
         MVC   18(4,R5),WMSTIPI
         MVC   29(4,R5),WMSTIPM
*                                                          LINE 4
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
*
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
*                                                          LINE 5
         CLI   @MODE,X'0F'
         BE    RUNSRMS
DOFLASH  MVI   @MODE,X'7F'
         B     RUNSRMS
STARTALT MVI   @MODE,X'0F'
         B     RUNSRMS
RUNSRMS  EQU   *
         XC    IOCBWAIT(4),IOCBWAIT
         LA    R0,SCREEN
         LA    R1,DOMAINZ
         BAL   R14,@CLEAR
         LA    R5,LINE17           COPY
         LA    R2,5                    STATIC
         LA    R6,STATLINE                   LINES
         CLI   @MODE,X'0F'
         BE    STATNONE
STATLOOP EQU   *
         MVC   0(79,R5),0(R6)      ONTO
         LA    R5,79(R5)               CURRENT
         LA    R6,79(R6)                      SCREEN
         BCT   R2,STATLOOP
*
**
STATNONE EQU   *
         LH    R7,RMCTDMNC        GET # OF DMN ENTRIES
         L     R6,RMCTDMDT        GET DMDT
         LA    R6,DMDTLEN(R6)             AND SKIP DMN=0
         BCTR  R7,0                       ADJUSTING # OF DOMAINS
         LA    R5,DOMAINZ
         MVC   0(79,R5),PAGETITL
         ST    R7,DECWORK1
         BAL   R14,@DECIMAL
         MVC   26(3,R5),DECWORK4+1
         LA    R4,12               ***LIMIT OF DOMAINS DISPLAYED
         CR    R4,R7
         BNL   DODOMAIN
         LR    R7,R4
DODOMAIN EQU   *
         LA    R5,79(R5)
         MVC   0(79,R5),DMNTITL
DMNLOOP  EQU   *
         LA    R5,79(R5)
         BAL   R15,DMNLINE
         LA    R6,DMDTLEN(R6)
         BCT   R7,DMNLOOP
         LA    R5,79(R5)
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         LA    R4,1(R4)
         ST    R4,DMLINE
         LA    R5,DOMAINZ+158
         LA    R6,REASONS
         LA    R3,11
         LA    R4,RMCASRC
         CLI   @MODE,X'0F'
         BNE   SWAPLOOP
         LA    R5,LINE15
         B     NOSWAPS
*
SWAPLOOP EQU   *
         L     R2,0(R4)
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   48(22,R5),0(R6)
         MVC   72(4,R5),DECWORK4
         LA    R6,22(R6)
         LA    R4,4(R4)
         LA    R5,79(R5)
         BCT   R3,SWAPLOOP
         SH    R5,=H'79'        BACKUP OVER LAMONT CRANSTON
         MVC   48(22,R5),0(R6)
         LH    R2,RMCAINUS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   72(4,R5),DECWORK4
         LA    R5,79(R5)
         LA    R6,22(R6)
         MVC   48(22,R5),0(R6)
         LH    R2,RMCACIUS
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   72(4,R5),DECWORK4
         LA    R5,079(R5)
NOSWAPS  EQU   *
         MVC   48(28,R5),RCTTITL
         LA    R5,79(R5)
         MVC   48(28,R5),RCDTITL
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         SR    R2,R2
RCTLOOP  EQU   *
         LH    R2,RCCUICTL
         ST    R2,@LOW
         LH    R2,RCCUICTH
         ST    R2,@HI
         LH    R2,RCVUICA
         ST    R2,@AV
         L     R2,RCVUICC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),=CL8'U I C'
*
*
*
*
*
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCCPUTL
         SRL   R2,4
         ST    R2,@LOW
         LH    R2,RCCCPUTH
         SRL   R2,4
         ST    R2,@HI
*
**
***
**
*
         LH    R2,RCVCPUA
         SRL   R2,4
         ST    R2,@AV
         L     R2,RCVCPUC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),=CL8'C P U'
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCASMTL
         ST    R2,@LOW
         LH    R2,RCCASMTH
         ST    R2,@HI
         LH    R2,RCVASMQA
         ST    R2,@AV
         L     R2,RCVASMQ
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),=CL8'A S M Q'
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCPTRTL
         ST    R2,@LOW
         LH    R2,RCCPTRTH
         ST    R2,@HI
         LH    R2,RCVPTR
         ST    R2,@AV
         L     R2,RCVBPTCT
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),=CL8'P A G E'
         LA    R5,79(R5)
         LH    R2,RCCTOTUT
         ST    R2,@LOW
         LH    R2,RCCLCHUT
         ST    R2,@HI
         LH    R2,RCCLCHRR
         ST    R2,@AV
         LH    R2,RCVTOTDF
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),=CL8'L C H '
         EJECT
         CLI   @MODE,X'7F' .       IS IT FLASH MODE?
         BNE   SRMPUT1 .           IF NOT, GO TO R/W
SRMPUT0  EQU   *
         MVI   @MODE,X'7F'
         MVC   LINE23(79),MODEDISP
         CLI   @AUTH,X'00'
         BE    DONLY
         MVC   LINE22+16(L'OONLY),OONLY
DONLY    EQU   *
         MVI   IOCBFLGC,X'00'
         MVI   IOCBFLGD,X'00'
         MVC   IOCBWAIT(4),INITINT
         OI    IOCBFLGD,IOCBDYON
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO+IOCBCFRA
         TRMIO IOCB,WRITE,FORMAT=SCREEN,IMAGE=DOMAINZ
         PFKEY DEF=NO,ATTN=SRMEXIT,ENTER=DOFLASH,                      X
               K9=DOFLASH,K12=SRMEXIT,K3=STARTALT
         B     DOFLASH
SRMPUT1  EQU   *
         MVI   @MODE,X'0F'
         MVC   LINE23(79),MODEALTR
*
*
         LA    R14,ALTCOPY     MOVE
         L     R15,=A(79*23)       SCREEN
         LR    R1,R15                    TO
         LA    R1,0(R1)                    COMPARISON
         LA    R0,DOMAINZ                            AREA
*****>   MVCL  R14,R0
*
SRMPUT2  EQU   *
         MVI   IOCBFLGC,X'00'
         MVI   IOCBFLGD,X'00'
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO
         TRMIO IOCB,FORMAT=ALTERD,IMAGE=DOMAINZ
         PFKEY DEF=DCMSOVER,ATTN=SRMEXIT,ENTER=ALTERMS,                X
               K9=DCMSOVER,K12=SRMEXIT,K3=DOFLASH
         EJECT
*                                                                     *
*                                                                     *
*        S R M    C O N S T A N T   A L T E R   S U P P O R T         *
*                                                                     *
*                                                                     *
ALTERMS  DS    0H
         LA    R6,LINE17   POINT
         LA    R4,DOMAINZ        TO
         SR    R6,R4               RCT
         LA    R5,ALTCOPY             DATA
         AR    R5,R6                      IN
         LA    R6,LINE17                    BOTH
*                                               COPIES
TESTUIC  CLC   56(09,R6),56(R5)        ====>UIC
         B     FIXUIC
TESTCPU  LA    R5,79(R5)
         LA    R6,79(R6)
*
         CLC   56(09,R6),56(R5)        ====>CPU
         B     FIXCPU
TESTASMQ LA    R5,79(R5)
         LA    R6,79(R6)
*
         CLC   56(09,R6),56(R5)        ====>ASMQ
         B     FIXASMQ
TESTPAGE LA    R5,79(R5)
         LA    R6,79(R6)
*
         CLC   56(09,R6),56(R5)        ====>PAGE
         B     FIXPAGE
TESTLCH  LA    R5,79(R5)
         LA    R6,79(R6)
*
         CLC   56(09,R6),56(R5)        ====>LCH
         B     FIXLCH
         LA    R5,79(R5)
         LA    R6,79(R6)
*
ALTNEXT EQU   *
         L     R7,RMCTDMNC        POINT TO
         L     R6,RMCTDMDT                 DOMAIN TABLE
         LA    R6,DMDTLEN(R6)     AND SKIP DOMAIN
         BCTR  R7,0                               ZERO ENTRY
         LA    R5,LINE3           POINT TO INPUT AREA
         LA    R4,12              ENFORCE
         CR    R4,R7                      LIMIT
         BNL   *+6                             OF TWELVE
         LR    R7,R4                                    DOMAINS
CKNEXTDM EQU   *
         B     CHEKDMN
CKDOMAIN LA    R6,DMDTLEN(R6)
         LA    R5,79(R5)
         BCT   R7,CKNEXTDM
*
*
*
ALTGONE  EQU   *
         B     SRMPUT2
         EJECT
FIXUIC   EQU   *
         LH    R1,RCCUICTL         SAVE
         ST    R1,@UICL                OLD
         LH    R1,RCCUICTH                UIC
         ST    R1,@UICH                      VALUES
*
         MVC   DECWORK4(4),56(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   66(4,R6),HEXWORK1          LOW
         LA    R1,LIMUIC                       LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@LOW                    SAVE
         MVC   DECWORK4(4),61(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   71(4,R6),HEXWORK1          HIGH
         LA    R1,LIMUIC                      LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@HI                     SAVE
         CLC   @LOW(8),@UICL       IF NEW AND OLD ARE THE SAME
         BE    FIZUIC                 SKIP THE UPDATE
         MODESET KEY=ZERO
         L     R0,@LOW             PUT
         L     R1,@HI                 NEW
         STH   R0,RCCUICTL               VALUES
         STH   R1,RCCUICTH                     IN RCT
         MODESET KEY=NZERO
FIZUIC   EQU   *
*
         B     TESTCPU
FIXCPU   EQU   *
         LH    R1,RCCCPUTL         SAVE
         ST    R1,@CPUL                OLD
         LH    R1,RCCCPUTH                CPU
         ST    R1,@CPUH                      VALUES
*
         MVC   DECWORK4(4),56(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   66(4,R6),HEXWORK1          LOW
         LA    R1,LIMCPU                       LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@LOW                    SAVE
         MVC   DECWORK4(4),61(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   71(4,R6),HEXWORK1          HIGH
         LA    R1,LIMCPU                      LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@HI                     SAVE
         CLC   @LOW(8),@CPUL       IF NEW AND OLD ARE THE SAME
         BE    FIZCPU                 SKIP THE UPDATE
         MODESET KEY=ZERO
         L     R0,@LOW             PUT
         L     R1,@HI                 NEW
         SLL   R0,4
         SLL   R1,4
         STH   R0,RCCCPUTL               VALUES
         STH   R1,RCCCPUTH                     IN RCT
         MODESET KEY=NZERO
FIZCPU   EQU   *
*
         B     TESTASMQ
FIXASMQ  EQU   *
         LH    R1,RCCASMTL         SAVE
         ST    R1,@ASML                OLD
         LH    R1,RCCASMTH                ASM
         ST    R1,@ASMH                      VALUES
*
         MVC   DECWORK4(4),56(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   66(4,R6),HEXWORK1          LOW
         LA    R1,LIMASM                       LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@LOW                    SAVE
         MVC   DECWORK4(4),61(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   71(4,R6),HEXWORK1          HIGH
         LA    R1,LIMASM                      LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@HI                     SAVE
         CLC   @LOW(8),@ASML       IF NEW AND OLD ARE THE SAME
         BE    FIZASM                 SKIP THE UPDATE
         MODESET KEY=ZERO
         L     R0,@LOW             PUT
         L     R1,@HI                 NEW
         STH   R0,RCCASMTL               VALUES
         STH   R1,RCCASMTH                     IN RCT
         MODESET KEY=NZERO
FIZASM   EQU   *
*
         B     TESTPAGE
FIXPAGE  EQU   *
         LH    R1,RCCPTRTL         SAVE
         ST    R1,@PTRL                OLD
         LH    R1,RCCPTRTH                PTR
         ST    R1,@PTRH                      VALUES
*
         MVC   DECWORK4(4),56(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   66(4,R6),HEXWORK1          LOW
         LA    R1,LIMPTR                       LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@LOW                    SAVE
         MVC   DECWORK4(4),61(R6)   GET
         BAL   R14,@BIN                NEW
         MVC   71(4,R6),HEXWORK1          HIGH
         LA    R1,LIMPTR                      LIMIT
         BAL   R14,@LIM                AND
         ST    R1,@HI                     SAVE
         CLC   @LOW(8),@PTRL       IF NEW AND OLD ARE THE SAME
         BE    FIZPTR                 SKIP THE UPDATE
         MODESET KEY=ZERO
         L     R0,@LOW             PUT
         L     R1,@HI                 NEW
         STH   R0,RCCPTRTL               VALUES
         STH   R1,RCCPTRTH                     IN RCT
         MODESET KEY=NZERO
FIZPTR   EQU   *
*
         B     TESTLCH
FIXLCH   EQU   *
         B     ALTNEXT
         EJECT
CHEKDMN  EQU   *
         USING DMDT,R6
         SR    R2,R2         SAVE
         IC    R2,DMDTLO         OLD
         ST    R2,@DLO              VALUES
         IC    R2,DMDTHI                  FOR
         ST    R2,@DHI                       THIS
         IC    R2,DMDTWT                         DOMAIN
         ST    R2,@DWT                                 ENTRY
*
         MVC   DECWORK4+1(3),0(R5)
         MVI   DECWORK4,C' '
         BAL   R14,@BIN
         STC   R1,@A#
         CLC   @A#(1),DMDTNO
         BNE   ALTGONE
*
         MVI   DECWORK4,C' '           OBTAIN
         MVC   DECWORK4+1(3),4(R5)           LOWER
         BAL   R14,@BIN                           LIMIT
         MVC   20(4,R5),HEXWORK1
         LA    R1,@DLIM0
         BAL   R14,@LIM
         ST    R1,@ALO
*
         MVI   DECWORK4,C' '           OBTAIN
         MVC   DECWORK4+1(3),8(R5)           HIGHER
         BAL   R14,@BIN                            LIMIT
         MVC   24(4,R5),HEXWORK1
         LA    R1,@DLIM0
         BAL   R14,@LIM
         ST    R1,@AHI
*
         MVI   DECWORK4,C' '           OBTAIN
         MVC   DECWORK4+1(3),12(R5)          DOMAIN
         BAL   R14,@BIN                            WEIGHT
         MVC   28(4,R5),HEXWORK1
         LA    R1,@DLIM1
         BAL   R14,@LIM
         ST    R1,@AWT
*
         CLC   @DLO(12),@ALO
         BE    CHEKDMNX
         MODESET KEY=ZERO
         SR    R2,R2         SET
         SR    R1,R1            NEW
         SR    R0,R0               DOMAIN
         IC    R2,@ALO+3                 CONSTRAINTS
         IC    R1,@AHI+3     INTO
         IC    R0,@AWT+3         DOMAIN
         STC   R2,DMDTLO               TABLE
         STC   R1,DMDTHI                    ENTRY
         STC   R0,DMDTWT
         MODESET KEY=NZERO
CHEKDMNX EQU   *
         B     CKDOMAIN
         EJECT
DMNLINE  EQU   *
*        USING DMDT,R6
         SR    R2,R2
         IC    R2,DMDTNO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   0(3,R5),DECWORK4+1
         IC    R2,DMDTLO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   4(3,R5),DECWORK4+1
         IC    R2,DMDTHI
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   08(3,R5),DECWORK4+1
         IC    R2,DMDTWT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   12(3,R5),DECWORK4+1
         CLI   @MODE,X'0F'
         BE    DMNLINEX
         LH    R2,DMDTMPLT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   16(3,R5),DECWORK4+1
         LH    R2,DMDTRUA
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   20(3,R5),DECWORK4+1
         LH    R2,DMDTGOOU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   24(3,R5),DECWORK4+1
         LH    R2,DMDTCMPL
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   28(3,R5),DECWORK4+1
         LH    R2,DMDTOUTU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   32(3,R5),DECWORK4+1
         LH    R2,DMDTINCU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   36(3,R5),DECWORK4+1
         MVC   DECWORK4(4),=CL4'   0'
         IC    R1,DMDTWT        CURRENT WEIGHT
         MH    R1,DMDTRUA        TIMES
         SR    R0,R0             READY USER AVG
         LH    R2,DMDTMPLT       DIVIDED BY
         LTR   R2,R2
         BZ    CCINODR
         DR    R0,R2             MPL TARGET
         ST    R1,DECWORK1       EQUALS
         BAL   R14,@DECIMAL      CONTENTION INDEX
CCINODR  MVC   40(3,R5),DECWORK4+1
         MVI   45(R5),C'×'
DMNLINEX EQU   *
         BR    R15
         EJECT
XLINE    EQU   *
         L     R2,@LOW
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   56(4,R5),DECWORK4
         L     R2,@HI
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   61(4,R5),DECWORK4
         CLI   @MODE,X'0F'
         BE    XLINEX
         L     R2,@AV
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   66(4,R5),DECWORK4
         L     R2,@CT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   72(4,R5),DECWORK4
XLINEX   EQU   *
         BR    R15
         EJECT
@BIN     DS    0H
         OC    DECWORK4(4),=XL4'F0F0F0F0'
         MVC   HEXWORK1(4),DECWORK4
         PACK  DOUBLE(8),DECWORK4(4)
         CVB   R1,DOUBLE
         ST    R1,DECWORK1
         BR    R14
         SPACE 3
@LIM     DS    0H
         L     R15,DECWORK1     GET VALUE TO BE LIMITED
         C     R15,0(R1)       SET
         BNL   @LIM2             TO
         L     R15,0(R1)            LOWER BOUND
         B     @LIM9          IF LT LOWER LIMIT
@LIM2    EQU   *
         C     R15,4(R1)       SET
         BNH   @LIM9             TO
         L     R15,4(R1)            UPPER BOUND
@LIM9    EQU   *
         ST    R15,DECWORK1    IF GT UPPER LIMIT
         LR    R1,R15          - OTHERWISE
         BR    R14                       LEAVE ALONE
LIMUIC   DC    F'001'
         DC    F'255'
LIMCPU   DC    F'001'
         DC    F'101'
LIMASM   DC    F'001'
         DC    F'255'
LIMPTR   DC    F'010'
         DC    F'150'
LIMLCH   DC    F'001'
         DC    F'100'
@DLIM0   DC    F'000'
         DC    F'255'
@DLIM1   DC    F'001'
         DC    F'255'
         EJECT
MODEDISP DC    CL79' MODE = DISPLAY '
MODEALTR DC    CL79' MODE = ALTER   '
OONLY    DC    C' (ONLY) '
IPSMSG   DC    C' CURRENT IPS =  '
INITINT  DC  F'500'      .      FIVE SECOND INTERVAL
SCREEN   SFMT
DOMAINZ  FIELD 79
LINE2    FIELD 79
LINE3    FIELD 79
LINE4    FIELD 79
LINE5    FIELD 79
LINE6    FIELD 79
LINE7    FIELD 79
LINE8    FIELD 79
LINE9    FIELD 79
LINE10   FIELD 79
LINE11   FIELD 79
LINE12   FIELD 79
LINE13   FIELD 79
LINE14   FIELD 79
LINE15   FIELD 79
LINE16   FIELD 79
LINE17   FIELD 79
LINE18   FIELD 79
LINE19   FIELD 79
LINE20   FIELD 79
LINE21   FIELD 79
LINE22   FIELD 79
LINE23   FIELD 79,INTEN=LO,CURSOR=AFTER
         SFEND
ALTERD   SFMT
L1    FIELD 79,ALPHA,INTEN=HI
L2    FIELD 79,ALPHA,INTEN=HI
L3    FIELD 79,ALPHA
L4    FIELD 79,ALPHA
L5    FIELD 79,ALPHA
L6    FIELD 79,ALPHA
L7    FIELD 79,ALPHA
L8    FIELD 79,ALPHA
L9    FIELD 79,ALPHA
L10   FIELD 79,ALPHA
L11   FIELD 79,ALPHA
L12   FIELD 79,ALPHA
L13   FIELD 79,ALPHA
L14   FIELD 79,ALPHA
L15   FIELD 79,ALPHA,INTEN=HI
L16   FIELD 79,ALPHA,INTEN=HI
L17   FIELD 79,ALPHA
L18   FIELD 79,ALPHA
L19   FIELD 79,ALPHA
L20   FIELD 79,ALPHA
L21   FIELD 79,ALPHA
L22   FIELD 79,ALPHA
L23   FIELD 79,ALPHA,INTEN=HI,CURSOR=AFTER
         SFEND
PAGETITL DS    0CL79
         DC    CL40'DOMAIN TABLE (DMDT)       NNN DOMAINS   '
         DC    CL40'     ×  SWAP ANALYSIS DATA    (RMCA)    '
DMNTITL  DS    0CL79
         DC    CL40'DMN MIN MAX WT MPLT RUA GOU CMP OUT INC '
         DC    CL40'CCI  ×  SWAP REASONS          COUNTS  '
ERVTITL  DS    0CL79
         DC    CL79' CPU=NNN I/O=NNN ENQ=NNN   '
RFCTITL  DS    0CL79
         DC    CL79' CPU = NN.N I/O = NN.N MSO = NN.N '
RCDTITL  DS    0CL30
         DC    CL30'VARIABLE LOW HIGH AVG  COUNT  '
RCTTITL  DS    0CL30
         DC    CL30'RESOURCE CONTROL TABLE (RCT) '
REASONS  DS    0C
         DC    CL22'TERMINAL INPUT    (01)'
         DC    CL22'TERMINAL OUTPUT   (02)'
         DC    CL22'LONG WAIT         (03)'
         DC    CL22'AUX STG SHORTAGE  (04)'
         DC    CL22'REAL STG SHORTAGE (05)'
         DC    CL22'DETECTED WAIT     (06)'
         DC    CL22'REQUESTED         (07)'
         DC    CL22'CAP: ENQ EXCHANGE (08)'
         DC    CL22'CAP: WLM EXCHANGE (09)'
         DC    CL22'CAP: UNILATERAL   (0A)'
         DC    CL22'ONLY THE SHADOW KNOWS '
         DC    CL22'IN CORE USERS         '
         DC    CL22'OUTREADY USERS        '
SRMEXIT  DCMSTOP RC=(15)
         DSGEN  SCREEN
DMLINE   DS    F .
         DS   4F .
@A#      DS   X
@ALO     DS   F
@AHI     DS   F
@AWT     DS   F
@DLO     DS   F
@DHI     DS   F
@DWT     DS   F
@UICL    DC    F'0'
@UICH    DC    F'0'
@CPUL    DC    F'0'
@CPUH    DC    F'0'
@ASML   DC    F'0'
@ASMH   DC    F'0'
@PTRL   DC    F'0'
@PTRH   DC    F'0'
@LOW     DC   F'0'
@HI      DC   F'0'
@AV      DC   F'0'
@CT      DC   F'0'
@MODE    DS   X .
@AUTH    DS   X .
STATLINE DS    5CL79
ALTCOPY  DS    24CL79 .      COMPARISON COPY OF SCREEN FOR ALTER MODE
         EJECT
         CVT   DSECT=YES
         EJECT
RMCNS    RMCNS
         EJECT
DMDT     IRADMDT
         EJECT
WAMT     IRAWAMT
         EJECT
WMST     IRAWMST
         END
