         TITLE 'JH#PDF8 - JH ISPF BACKGROUND JOBS DRIVER'
*---------------------------------------------------------------------*
*                                                                     *
*   JOHN HANCOCK MUTUAL LIFE INSURANCE COMPANY, INC.                  *
*   TECHNICAL SERVICES, B-3                                           *
*   P. O. BOX 111                                                     *
*   BOSTON, MASSACHUSETTS 02117                                       *
*                                                                     *
*   JH#PDF8      SEPTEMBER 17, 1982      RON MURA, TECHNICAL SERVICES *
*   REENTERANT, REUSABLE, REFRESHABLE.                                *
*                                                                     *
*   THIS PROGRAM IS AN ISPF DIALOG FUNCTION. IT IS INVOKED VIA ISPF   *
*   SELECT SERVICE AND IN TURN INVOKES OTHER ISPF SERVICES.           *
*   THIS MODULE CONTROLS THE JOHN HANCOCK BATCH JOBS OPTION OF ISPF.  *
*                                                                     *
*   PARM - NAME OF INITIAL BATCH SELECTION MENU (DEFAULT ZPDF8A).     *
*                                                                     *
*   NOTE: ZB@A REFERS TO THE INITIAL SELECTION MENU AND ZB@B REFERS   *
*         TO THE TERMINATION SELECTION MENU WHEN THEY APPEAR BELOW.   *
*                                                                     *
*   THE FOLLOWING IS HOW THIS PROGRAM MAY BE INVOKED FROM A           *
*   SELECTION PANEL (E.G., ISR@PRIM):                                 *
*                                                                     *
*         &ZSEL = TRANS( TRUNC (&ZCMD,'.')                            *
*                       8,'PGM(JH#PDF8) PARM(ZB@A) NOCHECK'           *
*         &ZTRAIL = .TRAIL                                            *
*                                                                     *
*   "NOCHECK" AND &ZTRAIL ARE REQUIRED.                               *
*                                                                     *
*   THE FOLLOWING VARIABLES MAY BE SET TO PASS INFORMATION TO THIS    *
*   PROGRAM:                                                          *
*                                                                     *
*       NAME      WHERE SET                PURPOSE                    *
*       ----      ---------                -------                    *
*       TERMMENU  ZB@A                     TERMINATION SELECTION MENU *
*       PANEL     ZB@A/ZB@B                SUBOPTION PANEL            *
*       JOBSKEL   ZB@A                     JOB JCL SKELETON           *
*                                           (INITIAL SKELETON)        *
*       SUBMSG    ZB@A/ZB@B                JOB SUBMITTED LOG MSG      *
*       CANMSG    ZB@A/ZB@B                JOB CANCELED LOG MSG       *
*       EDITMSG   ZB@A/ZB@B                JOB JCL EDITED LOG MSG     *
*       SKEL      ZB@A/ZB@B OR             SUBOPTION SKELETON         *
*                  IN SUBOPTION PANEL                                 *
*       LOGMSG    ZB@A/ZB@B OR             MSG ID FOR LOG             *
*                  IN SUBOPTION PANEL                                 *
*       NXTPNL    IN SUBOPTION PANEL ONLY  NEXT SUBOPT PANEL          *
*       LASTSKEL  ZB@A/ZB@B OR             LAST SKELETON              *
*                  IN SUBOPTION PANEL                                 *
*                                                                     *
*   PANEL IS REQUIRED.  SKEL DEFAULTS TO PANEL NAME IF NOT SET.       *
*   TERMMENU DEFAULTS TO INITIAL MENU IF NOT SET.                     *
*                                                                     *
*   VARIABLE PRIORSKL IS SET BY THIS PROGRAM.  IT MAY BE TESTED IN    *
*   SKELETONS (FOR EXAMPLE) TO SEE IF IT IS NECESSARY TO GENERATE     *
*   THE ENTIRE STEP JCL OR JUST ADDITIONAL CONTROL CARDS.             *
*                                                                     *
*   NOTE THAT ZB@A AND ZB@B ARE NOT NORMAL ISPF SELECTION             *
*   MENUS (THEY ARE NOT DISPLAYED BY THE SELECT SERVICE).  THIS IS    *
*   BECAUSE WHEN THE END KEY IS PRESSED (WHICH CAUSES THE SELECT      *
*   SERVICE TO REVERT TO A HIGHER FUNCTION), FURTHER PROCESSING       *
*   (ESPECIALLY JOB SUBMISSION) MAY BE REQUIRED.  THIS PROGRAM        *
*   CONTROLS THE DISPLAYING OF ZB@A/ZB@B AND OF THE SUBOPTION         *
*   PANELS.                                                           *
*                                                                     *
*   IF A USER ENTERS 8.SUBOPT, ZB@A IS PROCESSED IN                   *
*   NONDISPLAY-ENTER MODE AND ZB@B IN NONDISPLAY-END MODE.            *
*   THIS ALLOWS VALIDATION OF JOB JCL FIELDS IN ZB@A AND END-OF-JCL   *
*   PROCESSING IN ZB@B (E.G., INCREMENTING A JOB CHARACTER).          *
*   ZB@A MAY SET &PANEL TO ? AND CONTROL VARIABLE .RESP TO END IF THE *
*   OPTION (&ZCMD) IS INVALID AND THE USER ENTERED 8.SUBOPT           *
*   (I.E., &ZTRAIL IS NONBLANK).  THIS WILL CAUSE TERMINATION         *
*   FOLLOWING A SETMSG OF AN 'INVALID SUBOPTION' MSG (ZB@003).        *
*                                                                     *
*   PANEL AND JOB SUBMISSION FLOW ARE THE SAME (TO THE END-USER) AS   *
*   FOR THE IBM-PROVIDED BACKGROUND OPTIONS (SEE ISPF TUTORIAL).      *
*   THERE IS THE ADDITIONAL OPTION, HOWEVER, OF ENTERING "E" ON ZB@B  *
*   TO LEAVE BY WAY OF EDITING THE ISPF TEMPORARY JCL DATASET INSTEAD *
*   OF SUBMITTING.  WHEN INVOKED WITH A SUBOPTION (WHICH CAUSES ZB@B  *
*   TO BE BYPASSED), USE THE FORM "8.SUBOPT/E".                       *
*                                                                     *
*   CHANGED 3/22/83 - MURA - DEFAULT INITIAL SELECTION PANEL NAME     *
*      CHANGED FROM ZB@A TO ZPDF8A.  VARIABLE PRIORSKL SET FOR        *
*      TESTING IN SKELETONS.                                          *
*   CHANGED 9/01/83 - MURA - SET RC=20 IF &PANEL SET TO ?, INDICATING *
*      AN INVALID SUBOPTION.  ISPF WILL THEN ISSUE ITS OWN INVALID    *
*      OPTION MESSAGE, WITHOUT ERASING THE ENTERED OPTION.  THIS ALSO *
*      MEANS THAT &INVALMSG IS NO LONGER USED.                        *
*   CHANGED 01/10/84 MURA - GET &ZCMD FIRST, BEFORE &PANEL, TO PREVENT*
*      ANY TRUNCATION ERROR IF USER ENTERS CANCEL/EDIT ON ZB@A.       *
*   CHANGED 04/25/84 MURA - IF ISPF VERSION 2 (ZTEMPN IS SET TO       *
*      JCL DDNAME), THEY USE LM SERVICES TO EDIT TEMPORARY FILE.      *
*      THIS WILL ALLOW VIO DATA SET EDITING.                          *
*   CHANGED 08/28/84 MURA - USE PANEL JHAEFR01 WITH EDIT SERVICE.     *
*                                                                     *
*   MACROS:                                                           *
*   ENTER                                                             *
*   LEAVE                                                             *
*   SCANLINE                                                          *
*   ISPCALL                                                           *
*   REQUS                                                             *
*   MACROS ON SYS1.MACLIB                                             *
*                                                                     *
*   REGISTERS:                                                        *
*   R2  - WORK                                                        *
*   R3  - WORK                                                        *
*   R4  - WORK                                                        *
*   R5  - WORK                                                        *
*   R6  - (NOT USED)                                                  *
*   R7  - (NOT USED)                                                  *
*   R8  - (NOT USED)                                                  *
*   R9  - RETURN CODE                                         09/01/83*
*   R10 - (NOT USED)                                                  *
*   R11 - 2ND BASE (IF NECESSARY)                                     *
*   R12 - 1ST BASE                                                    *
*   R13 - DSECT BASE (1ST 18F FOR SAVEAREA)                           *
*                                                                     *
*---------------------------------------------------------------------*
JH#PDF8  ENTER RENT=YES,WKBYTES=WKBYTES    ,BASE2=R11
         USING DSECT,R13
         XR    R9,R9                      INIT RETURN CODE    09/01/83
*---------------------------------------------------------------------*
*  HANDLE PARM (NAME OF INITIAL SELECTION MENU):                      *
*---------------------------------------------------------------------*
         L     R3,0(R1)                   GET PARM ADDRESS
         LH    R4,0(R3)                   LOAD PARM LENGTH
         MVC   USERPARM,=CL8'ZPDF8A'      DEFAULT INIT MENU     3/22/83
         LTR   R4,R4                      ANY USER PARM PROVIDED?
         BZ    INITFLDS                   NO - JUST USE DEFAULT
         MVC   USERPARM,BLANKS
         CH    R4,=H'8'
         BNH   USRPRMOK
         LH    R4,=H'8'                   MAX. ALLOWABLE PARM LNTH IS 8
USRPRMOK BCTR  R4,0                       SUBTRACT 1 FOR EX
         EX    R4,MVC$1                   MOVE IN USER PARM
         B     INITFLDS
MVC$1    MVC   USERPARM(1),2(R3)          MOVE USER PARM (EXECUTED)
*---------------------------------------------------------------------*
*  INITIALIZE FIELDS.  INITIALLY DEFINE ALL FUNCTION VARIABLES AS     *
*  BLANK:                                                             *
*---------------------------------------------------------------------*
INITFLDS MVC   ZCMD,BLANKS
         MVI   SWITCHES,X'00'             INITIALIZE
         ISPCALL ('VREPLACE','(TERMMENU PANEL JOBSKEL SUBMSG CANMSG    X
               EDITMSG SKEL LOGMSG NXTPNL LASTSKEL PRIORSKL)', 09/01/83X
               VREPARAY,BLANKS),WKAREA=WKAREA                   3/22/83
*---------------------------------------------------------------------*
*  GET CONTENTS OF ZTRAIL TO SEE IF A SUBOPTION WAS ENTERED.          *
*  (TELL ISPF NOT TO QUIT WITH DIALOG ERROR ON VCOPY IN CASE USER     *
*  ENTERS A LONG--AND INVALID--SUBOPTION ON ISP@PRIM, WHICH WOULD     *
*  CAUSE A TRUNCATION ERROR ON VCOPY SERVICE.)                        *
*---------------------------------------------------------------------*
         ISPCALL ('CONTROL','ERRORS','RETURN'),                        X
               WKAREA=WKAREA              DON'T QUIT IF BAD ERROR
         MVC   ZTRAIL,BLANKS
         ISPCALL ('VCOPY','ZTRAIL',16,ZTRAIL,'MOVE')
         ISPCALL ('CONTROL','ERRORS','CANCEL') RESTORE TO DEFAULT
         CLI   ZTRAIL,X'40'               ANY SUBOPTION ENTERED?
         BE    DISPZB@A                   NO - JUST DISPLAY ZB@A
*---------------------------------------------------------------------*
*  A SUBOPTION WAS ENTERED, SO BYPASS JH BG SELECTION MENU            *
*  (ZB@A/ZB@B), SET VARIABLE ZCMD TO SUBOPTION VALUE, AND PROCESS     *
*  PANEL ZB@A IN NON-DISPLAY MODE TO ENSURE THAT THE SUBOPTION AND    *
*  JOBCARD PARAMETERS ARE VALID:                                      *
*---------------------------------------------------------------------*
         SCANLINE '/',RECADDR=ZTRAIL,ENDCOL=15,NOFIND=NOSLASH
         LR    R4,R1
         LA    R5,ZTRAIL
         SR    R1,R5                      GET LENGTH BEFORE SLASH
         CH    R1,=H'1'                   WATCH OUT FOR '8./E'
         BL    DISPZB@A                   TREAT AS NO SUBOPT
         BCTR  R1,0                       DEDUCT 1 FOR MVC
         EX    R1,MVC$3                   MOVE SUBOPT INTO ZCMD
         CLI   1(R4),C'E'                 "/E" ENTERED?
         BE    SETEDIT                       YES - SET SW FOR EDIT
         CLI   1(R4),C'C'                 "/C" ENTERED?
         BNE   NOSLASH                       NO - NOTHING SPECIAL
         OI    SWITCHES,CANCEL               YES - SET SW FOR CANCEL
ZCMDDONE ISPCALL ('VREPLACE','ZCMD',16,ZCMD) MAKE ZCMD AVAIL TO FUNCT
         OI    SWITCHES,SKIPSEL           SKIP SELECTION PANEL
         ISPCALL ('CONTROL','NONDISPL','ENTER') SET NONDISPL FOR ZB@A
         B     DISPZB@A                   GO "DISPLAY" ZB@A
NOSLASH  MVC   ZCMD,ZTRAIL
         B     ZCMDDONE
SETEDIT  OI    SWITCHES,EDIT              SET SWITCH FOR EDIT
         B     ZCMDDONE
MVC$3    MVC   ZCMD(1),ZTRAIL
*---------------------------------------------------------------------*
*  DISPLAY INITIAL SELECTION MENU, WITH SIMULATED "ENTER"             *
*  IF 8.SUBOPT WAS ENTERED.  GET SUBOPTION (ZCMD) AND THE SUBOPTION   *
*  PANEL NAME:                                                        *
*---------------------------------------------------------------------*
DISPZB@A ISPCALL ('DISPLAY',USERPARM)     DISPLAY INIT SELECTION PANEL
         LR    R4,R15                     SAVE SERVICE RETURN CODE
*  GET &ZCMD BEFORE &PANEL TO CHECK FOR CANCEL/EDIT.           01/10/84
         BAL   R5,GETZCMD                 GET &ZCMD FROM SELECTION PNL
         CLI   ZCMD,C'C'                  CANCEL ENTERED?
         BE    FINISH
         CLI   ZCMD,C'E'                  EDIT ENTERED?
         BE    FINISH                     YES - NOTHING TO EDIT
         MVC   PANEL,BLANKS
         ISPCALL ('VCOPY','PANEL',8,PANEL,'MOVE') GET PNL FROM ZB@A
         CH    R4,=H'8'                   END COMMAND ENTERED?
         BL    ZB@ACONT                   NO - CONTINUE ON
         CLI   PANEL,C'?'                 ? RETURNED AS PANEL NAME?
         BE    BADZCMD                    YES - INDICATES SUBOPT ERROR
         B     FINISH                     NO - SIMPLY QUIT
*---------------------------------------------------------------------*
*  OPEN THE TEMPORARY JCL FILE.   GET THE                             *
*  DSN OF THE TEMPORARY FILE AND DO SOME PREPARATION THAT WILL BE     *
*  REQUIRED FOR EITHER SUBMITTING THE JOB OR EDITING THE TEMP. FILE:  *
*---------------------------------------------------------------------*
ZB@ACONT MVC   CMDBUFF,=C'CMD(SUB '''
         ISPCALL ('FTOPEN','TEMP')        OPEN TEMP JCL FILE
         MVC   ZTEMPN,BLANKS                                   04/25/84
         ISPCALL ('VCOPY','ZTEMPN',8,ZTEMPN,'MOVE')            04/25/84
         MVC   ZTEMPF,BLANKS
         ISPCALL ('VCOPY','ZTEMPF',44,ZTEMPF,'MOVE')
         SCANLINE ' ',RECADDR=ZTEMPF      FIND END OF ZTEMPF DSN
         MVI   0(R1),C''''                ADD ENDING QUOTE
*---------------------------------------------------------------------*
*  RESET VARIABLE &NXTPNL BEFORE EACH SUBOPTION PANEL DISPLAY.        *
*  DISPLAY THE SUBOPTION PANEL:                                       *
*---------------------------------------------------------------------*
DSPLSOPT ISPCALL ('VREPLACE','NXTPNL',1,BLANKS)  ALWAYS SET TO BLANK
         ISPCALL ('DISPLAY',PANEL)        DISPLAY PANEL FOR OPTION
         LR    R4,R15                     SAVE RETURN CODE
         CH    R4,=H'8'                   END KEY PRESSED?
         BL    DISPLOK                    NO - GO GENERATE JCL
*---------------------------------------------------------------------*
*  END KEY PRESSED - IF WE ARE BYPASSING THE JH BG SELECTION MENU,    *
*  THEN GO ON TO QUIT; OTHERWISE, SHOW TERMINATION SELECTION          *
*  MENU TO ALLOW ANOTHER SUBOPTION TO BE SELECTED:                    *
*---------------------------------------------------------------------*
         TM    SWITCHES,SKIPSEL           BYPASS SELECTION MENU?
         BO    FINNOSUB
         MVI   ZCMD,C' '                  RESET ZCMD TO BLANKS
         B     DISPZB@B
*---------------------------------------------------------------------*
*  END KEY NOT PRESSED ON SUBOPTION - GENERATE JOB CARDS IF &JOBSKEL  *
*  IS NONBLANK AND THIS IS THE FIRST SUBOPTION PROCESSED.  GENERATE   *
*  JCL FOR THE SUBOPTION.  LOG A MSG IF VARIABLE &LOGMSG HAS BEEN     *
*  SET.  CHECK TO SEE IF WE ARE BYPASSING THE SELECTION MENU AND,     *
*  IF SO, IF THE USER ENTERED "/E" OR "/C" AFTER THE SELECTION:       *
*---------------------------------------------------------------------*
DISPLOK  MVC   NXTPNL,BLANKS
         ISPCALL ('VCOPY','NXTPNL',8,NXTPNL,'MOVE') GET NXTPNL
         CLI   NXTPNL,C' '                ANOTHER PANEL?
         BE    VCOPSKEL                   NO - GET SKELETON NAME
         MVC   PANEL,NXTPNL               YES - SET NEW PANEL NAME
         B     DSPLSOPT                   GO DISPLAY NEXT PANEL
VCOPSKEL MVC   SKEL,BLANKS
         ISPCALL ('VCOPY','SKEL',8,SKEL,'MOVE')   GET SKELETON NAME
         CLI   SKEL,X'40'                 WAS SKEL SET?
         BNE   CHKJBJCL
         MVC   SKEL,PANEL                 ASSUME SKEL NAME = PNL NAME
CHKJBJCL TM    SWITCHES,JCLGEND           ANY JCL WRITTEN OUT YET?
         BO    INCLSKEL                   YES - SKIP JOB CARDS
         MVC   JOBSKEL,BLANKS
         ISPCALL ('VCOPY','JOBSKEL',8,JOBSKEL,'MOVE')
         CLI   JOBSKEL,X'40'              IS &JOBSKEL NONBLANK?
         BE    INCLSKEL                   YES - THEN NO JOB CARDS
         ISPCALL ('FTINCL',JOBSKEL)       NO - WRITE JOB CARDS
INCLSKEL ISPCALL ('FTINCL',SKEL)          WRITE JCL FOR SUBOPTION
         ISPCALL ('VREPLACE','PRIORSKL',8,SKEL)                 3/22/83
         MVC   LOGMSG,BLANKS
         ISPCALL ('VCOPY','LOGMSG',8,LOGMSG,'MOVE') GET LOG MSG ID
         CLI   LOGMSG,X'40'               ANY LOG MSG ID?
         BE    DISPLOK2                   NO - SKIP LOGGING
         ISPCALL ('LOG',LOGMSG)           LOG MSG FOR SUBOPTION
DISPLOK2 OI    SWITCHES,JCLGEND           JCL HAS BEEN GENERATED
         MVI   ZCMD,C' '                  RESET ZCMD TO BLANKS
         TM    SWITCHES,SKIPSEL           SELECTION PANEL SKIPPED?
         BZ    DISPZB@B                   NO - GO DISPLAY ZB@B
         TM    SWITCHES,EDIT              "/E" ENTERED?
         BO    SETZCMDE
         TM    SWITCHES,CANCEL            "/C" ENTERED?
         BZ    NONDISPL
         MVI   ZCMD,C'C'                  SET ZCMD TO 'C'
         B     NONDISPL
SETZCMDE MVI   ZCMD,C'E'                  SET ZCMD TO 'E'
NONDISPL ISPCALL ('CONTROL','NONDISPL','END')  PROCESS ZB@B IN NONDISPL
*---------------------------------------------------------------------*
*  REPLACE ZCMD (BLANK, "C", OR "E").                                 *
*  RESET THE VARIABLES THAT CHANGE WITH EACH SUBOPTION.               *
*  DISPLAY TERMINATION SELECTION MENU TO ALLOW ADDITIONAL JCL TO BE   *
*  GENERATED.  IF CANCEL (C) ENTERED, LOG A "CANCELLED" MSG (IF       *
*  VARIABLE &CANMSG IS DEFINED) AND QUIT.                             *
*  IF EDIT (E) ENTERED, BRANCH TO CLOSE TEMP. FILE BEFORE INVOKING    *
*  EDIT SERVICE:                                                      *
*---------------------------------------------------------------------*
DISPZB@B ISPCALL ('VREPLACE','ZCMD',1,ZCMD)
         ISPCALL ('VREPLACE','(PANEL SKEL LOGMSG)',VREPARAY,BLANKS)
         MVC   TERMMENU,BLANKS
         ISPCALL ('VCOPY','TERMMENU',8,TERMMENU,'MOVE')
         CLI   TERMMENU,X'40'             TERMMENU BLANK?
         BNE   DSPZB@B2
         MVC   TERMMENU,USERPARM          YES - DEFAULT IS INIT MENU
DSPZB@B2 ISPCALL ('DISPLAY',TERMMENU)     DISPLAY SELECTION PANEL
         LR    R4,R15
         MVC   ZCMD,BLANKS
         BAL   R5,GETZCMD                 GET ZCMD FROM SELECTION PNL
         CLI   ZCMD,C'E'                  EDIT ENTERED?
         BE    FINNOSUB
         CLI   ZCMD,C'C'                  CANCEL ENTERED?
         BE    CANCELJB
         CH    R4,=H'8'                   END KEY PRESSED?
         BNL   JOBBEND
         MVC   PANEL,BLANKS
         ISPCALL ('VCOPY','PANEL',8,PANEL,'MOVE') GET PNL FROM ZB@B
         B     DSPLSOPT
CANCELJB MVC   CANMSG,BLANKS
         ISPCALL ('VCOPY','CANMSG',8,CANMSG,'MOVE')
         CLI   CANMSG,X'40'
         BE    FINNOSUB                   NO LOG MSG IF &CANMSG NOT SET
         ISPCALL ('LOG',CANMSG)           WRITE CANCELED MSG ON LOG
         B     FINNOSUB
*---------------------------------------------------------------------*
*  END KEY PRESSED ON SELECTION MENU ZB@B - IF ANY JCL HAS BEEN       *
*  GENERATED, GO ON TO SUBMIT THE JOB; OTHERWISE, JUST QUIT:          *
*---------------------------------------------------------------------*
JOBBEND  TM    SWITCHES,JCLGEND           DON'T SUB IF NO JCL GEN'D
         BZ    FINNOSUB
*---------------------------------------------------------------------*
*  PREPARE TO SUBMIT -                                                *
*  CLOSE THE FILE AND PREPARE FOR LINE OUTPUT:                        *
*---------------------------------------------------------------------*
         BAL     R2,FTCLOSE$              CLOSE TEMP JCL FILE
         ISPCALL ('CONTROL','DISPLAY','LINE',19)
*---------------------------------------------------------------------*
*  SUBMIT THE GENERATED JCL VIA SELECT SERVICE:                       *
*---------------------------------------------------------------------*
         ISPCALL ('SELECT',54,CMDBUFF)    SUBMIT THE JOB
         MVC   SUBMSG,BLANKS
         ISPCALL ('VCOPY','SUBMSG',8,SUBMSG,'MOVE')
         CLI   SUBMSG,X'40'
         BE    FINISH                     NO LOG MSG IF &SUBMSG NOT SET
         ISPCALL ('LOG',SUBMSG)           WRITE SUBMITTED MSG ON LOG
FINISH   LEAVE RC=(R9)                                        09/01/83
*---------------------------------------------------------------------*
*  QUIT WITHOUT SUBMITTING JOB - CLOSE THE TEMPORARY FILE (OTHERWISE  *
*  THE NEXT INVOCATION WILL ADD TO DATASET INSTEAD OF STARTING CLEAN),*
*  THEN WE CAN QUIT:                                                  *
*---------------------------------------------------------------------*
FINNOSUB BAL   R2,FTCLOSE$
         CLI   ZCMD,C'E'                  EDIT ENTERED?
         BNE   FINISH                        NO - JUST LEAVE
EDITJCL  MVC   EDITMSG,BLANKS
         ISPCALL ('VCOPY','EDITMSG',8,EDITMSG,'MOVE')
         CLI   EDITMSG,X'40'
         BE    EDITJCL2                  NO LOG MSG IF &EDITMSG NOT SET
         ISPCALL ('LOG',EDITMSG)
EDITJCL2 CLI   ZTEMPN,C' '                ANY TEMP. DDNAME?    04/25/84
         BE    EDITJCL3                   NO - (ISPF V1)       04/25/84
*                                         FOLLOWING LINE ADDED 08/28/84
         ISPCALL ('VDEFINE','(JHTITLE)',JHTITLE,'CHAR',=A(L'JHTITLE))
         ISPCALL 'LMINIT DATAID(JHPDF8ED) DDNAME(&&ZTEMPN)'
         ISPCALL 'EDIT DATAID(&&JHPDF8ED) PANEL(JHAEFR01)'     08/28/84
         ISPCALL 'LMFREE DATAID(&&JHPDF8ED)'
         B     FINISH
EDITJCL3 ISPCALL ('EDIT',ZTEMPF-1)        EDIT TEMP. FILE
         B     FINISH
*---------------------------------------------------------------------*
*  ROUTINE TO FINISH UP AND CLOSE TEMPORARY JCL:                      *
*---------------------------------------------------------------------*
FTCLOSE$ MVC   LASTSKEL,BLANKS
         ISPCALL ('VCOPY','LASTSKEL',8,LASTSKEL,'MOVE')
         CLI   LASTSKEL,X'40'             IS &LASTSKEL NONBLANK?
         BE    FTCLOSE
         ISPCALL ('FTINCL',LASTSKEL)
FTCLOSE  ISPCALL ('FTCLOSE')
         BR    R2
*---------------------------------------------------------------------*
*  HANDLE BAD SUBOPTION (ENTERED ONLY IF USER HAS ENTERED 8.SUBOPT    *
*  AND ZB@A HAS SET &PANEL TO ? TO INDICATE ERROR):                   *
*---------------------------------------------------------------------*
BADZCMD  LA    R9,20                      SET RET CODE FOR ISPF 9/01/83
         B     FINISH                     THEN LEAVE
*---------------------------------------------------------------------*
*  GET VARIABLE &ZCMD FROM SELECTION MENU, THEN IMMEDIATELY RESET:    *
*---------------------------------------------------------------------*
GETZCMD  MVC   ZCMD,BLANKS
         ISPCALL ('VCOPY','ZCMD',16,ZCMD,'MOVE')
         ISPCALL ('VREPLACE','ZCMD',1,BLANKS)  RESET TO BLANK
         BR    R5
*---------------------------------------------------------------------*
*  DATA AREAS:                                                        *
*---------------------------------------------------------------------*
BLANKS   DC    CL48' '
VREPARAY DC    11F'1'                     LENGTHS FOR VREPLACE 09/01/83
JHTITLE  DC    C'EDIT - TEMPORARY CONTROL DATA SET'            08/28/84
         LTORG
DSECT    DSECT
SAVEAREA DS    18F
WKAREA   DS    3D                         1ST F FOR NUMERICS IN ISPCALL
USERPARM DS    CL8
SWITCHES DS    XL1
JCLGEND  EQU   B'00000001'   JCL FOR A SUBOPTION HAS BEEN GEN'D
SKIPSEL  EQU   B'00000010'   BYPASS "SELECTION" MENU (ZB@B)
EDIT     EQU   B'00000100'   EDIT INSTEAD OF SUB ("/E")
CANCEL   EQU   B'00001000'   CANCEL INSTEAD OF SUB ("/C")
CMDBUFF  DS    CL9                        "CMD(SUB '"
ZTEMPF   DS    CL45                       MUST FOLLOW CMDBUFF FIELD
         DS    0F
ZTEMPN   DS    CL8                                             04/25/84
CANMSG   DS    CL8                        JOB CANCELED LOG MSG ID
EDITMSG  DS    CL8                        JOB JCL EDITED LOG MSG ID
JOBSKEL  DS    CL8                        JOB JCL (1ST) SKELETON NAME
LASTSKEL DS    CL8                        SKEL INCLUDED BEFORE FTCLOSE
LOGMSG   DS    CL8                        LOG MSG ID
NXTPNL   DS    CL8                        NEXT PANEL NAME
PANEL    DS    CL8                        SUBOPT PANEL NAME
SKEL     DS    CL8                        SUBOPT SKELETON NAME
SUBMSG   DS    CL8                        JOB SUBMITTED LOG MSG ID
TERMMENU DS    CL8                        TERMINATION MENU NAME
ZCMD     DS    CL16                       SUBOPTION (FROM ZB@A/ZB@B)
ZTRAIL   DS    CL16                       SET IN ISR@PRIM (INVOKNG PNL)
WKBYTES  EQU   *-WKAREA
         REQUS
         END
