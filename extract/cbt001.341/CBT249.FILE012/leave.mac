         MACRO
&LABEL   LEAVE &RC=0,       RETURN CODE (VALUE OR REGISTER)            X
               &LABEL2=     LABEL FOR REGISTER RESTORES
.*                                                      RON MURA, TPD
.*                 NO PARAMETERS REQUIRED
         MNOTE *,'*** LEAVE - STANDARD OS LINKAGE ***'
         GBLB  &RENTSW1              1 = REENTERANT SAVEAREA IN ENTER
         GBLC  &WKLNTH
         LCLB  &RCSW                 1 = MUST RESTORE REG 15
&RCSW    SETB  0                     INSURE OFF TO START
&LABEL   CNOP  0,4
         AIF   (&RENTSW1 EQ 1).REENT
         L     13,4(13)              RESTORE REG 13 (POINT TO HSA)
.*
.COMMON  ANOP       COMMON CODE FOR RENT AND NORENT:
&LABEL2  L     14,12(13)             RESTORE REGISTER
         AIF   ('&RC'(1,1) EQ '(').REGRC    CHECK FOR REGISTER NOTATION
         LA    15,&RC                LOAD RETURN CODE
         AGO   .L14
.REGRC   AIF   (&RCSW EQ 0).REGRC2
         LR    15,5                  RESTORE RETURN CODE
         AGO   .L14
.REGRC2  AIF   ('&RC(1)' EQ '15' OR '&RC(1)' EQ 'R15').L14
         LR    15,&RC(1)             LOAD RETURN CODE
.L14     ANOP
         LM    0,12,20(13)           RESTORE REGISTERS
         MVI   12(13),X'FF'          SET RETURN INDICATION IN CALLER SA
         BR    14
.*       LTORG
         MEXIT
.REENT   ANOP
         LR    1,13                  LOAD REG 1 FOR FREEMAIN
         AIF   ('&RC'(1,1) NE '(').R2
         AIF   ('&RC(1)' NE '15' AND '&RC(1)' NE 'R15').R2
         LR    5,&RC(1)              SAVE RETURN CODE
&RCSW    SETB  1
.R2      ANOP
         L     13,4(13)              RESTORE REG 13 (POINT TO HSA)
         MNOTE '         FREEMAIN R,LV=72+&WKLNTH,A=(1) '
         FREEMAIN R,LV=72+&WKLNTH,A=(1)
         AGO  .COMMON
         MEND
