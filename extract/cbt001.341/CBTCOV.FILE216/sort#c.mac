PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      ISPF/PDF CLIST FOR DOING INTERACTIVE SORTS        */
/*--------------------------------------------------------*/
CONTROL  NOMSG
FREE FI(SYSPRINT SYSIN SYSOUT SORTIN SORTOUT    +
        SORTWK01 SORTWK02 SORTWK03)
ALLOC FI(SYSOUT) DUMMY
ALLOC FI(SORTWK01) UNIT(WORK80) SPACE(1) CYL
ALLOC FI(SORTWK02) UNIT(WORK80) SPACE(9) CYL
ALLOC FI(SORTWK03) UNIT(WORK80) SPACE(9) CYL
ISPEXEC VGET (SDSNIN,SDSNOUT,CC1)
ALLOC FI(SORTIN) DA(&SDSNIN) SHR
ALLOC FI(SORTOUT) DA(&SDSNOUT) SHR
ALLOC FI(SYSPRINT) DA(*)
 ISPEXEC FTOPEN TEMP
 ISPEXEC FTINCL SORT#S
 ISPEXEC FTCLOSE
 ISPEXEC VGET   ZTEMPF
 ALLOC FI(SYSIN) DA('&ZTEMPF') SHR
WRITE
WRITE     *-------------------------*
WRITE     *                         *
WRITE     *   SORT BEING INVOKED    *
SET &LASTCC = 0
CALL 'SYS1.PPLINK(SORT)'
IF &LASTCC  = 0 THEN  +
 DO
 WRITE     *  SORT WAS SUCCESSFUL    *
 END
ELSE +
 DO
 WRITE     *  SORT WAS UNSUCCESSFUL  *
 END
WRITE     *      SORT ENDED         *
WRITE     *-------------------------*
WRITE
FREE FI(SYSPRINT SYSOUT SORTIN SORTOUT    +
        SORTWK01 SORTWK02 SORTWK03 SYSIN)
ALLOC FI(SYSIN)    DA(*)
ALLOC FI(SYSPRINT) DA(*)
