IEFUTL   TITLE 'WELLS FARGO BANK SMF TIME LIMIT EXIT'
***********************************************************************
         PRINT NOGEN                                                  *
*             MODULE NAME:  IEFUTL                                    *
*                                                                     *
*             DESCRIPTIVE NAME:  WELLS FARGO BANK SMF TIME LIMIT EXIT *
*                                                                     *
*             STATUS:  MVS RELEASE SP1.3.                             *
*                                                                     *
*             FUNCTION:                                               *
*                                                                     *
*              . VALIDATE ACF2 AUTHORIZATION (VIA LOGON RECORD)       *
*                TO DIFFERENTIATE NORMAL TEST JOBS & USERS            *
*                FROM 'PRIVILEGED' (I.E. SUBMITTOR HAS OPER AUTH.     *
*                (TP01 LEVEL MODIFICATION)                            *
*              . ALLOW TEST (Z) JOBS TO BE CANCELLED IF THE CPU       *
*                TIME LIMIT IS EXCEEDED (UNLESS PRIVILEGED USER).     *
*              . ALLOW TSO SESSIONS TO BE EXTENDED INDEFINETELY       *
*                IF THE CPU TIME LIMIT IS EXCEEDED.                   *
*              . ALLOW PRODUCTION & PRIVILEGED JOBS TO BE EXTENDED    *
*                5 MINUTES 6 TIMES IF THE CPU TIME LIMIT              *
*                IS EXCEEDED (I.E. 30  MINUTES)                       *
*              . ALLOW TSO SESSIONS TO BE CANCELLED IF THE WAIT       *
*                TIME LIMIT IS EXCEEDED.                              *
*              . ALLOW TEST (Z) JOBS TO BE CANCELLED IF THE WAIT      *
*                TIME LIMIT IS EXCEEDED (UNLESS PRIVILEGED USER).     *
*              . ALLOW PRODUCTION & PRIEVLEDGED JOBS TO BE EXTENDED   *
*                5 MINUTES UP TO 6 TIMES IF THE WAIT TIME LIMIT       *
*                IS EXCEEDED (I.E. 3.5 HOURS).                        *
*                                                                     *
*             OPERATION:                                              *
*              . ANALYZE SMF COMMON EXIT PARAMETER AREA.              *
*              . CANCEL JOB IF WFB SMF PARAMETER LIST IS MISSING.     *
*              . IF CPU TIME IS EXPIRED:                              *
*                   EXTEND TIME BY 5 MINUTES IF TSO SESSION.          *
*                   CANCEL JOB IMMEDIATELY IF Z JOB (NON-PRIVILEGED)  *
*                   EXTEND TIME BY 5 MINUTES UP TO 6 TIMES FOR A      *
*                      PRODUCTION OR PRIVILEGED Z JOB.                *
*              . IF WAIT TIME IS EXPIRED:                             *
*                   CONDITIONALLY TERMINATE SESSION IF NON-PRIVILEGED *
*                   (I.E. NO OPER AUTHORITY) USER.                    *
*                   EXTEND TIME ONCE FOR 30 MINUTES IF PRIVILEGED     *
*                   USER, CONDITIONALLY TERMINATE IF WAIT PERSISTS.   *
*                ** CONDITIONAL TERMINATION ALLOWS LOGON RECONNECT    *
*                   CANCEL IF TEST (Z) JOB SUBMITTED BY NON-PRIVILEGED*
*                   USER.                                             *
*                   EXTEND TIME BY 30 MINUTES UP TO 6 TIMES IF A      *
*                      PRODUCTION OR PRIVILEGED JOB.                  *
*              . NUMBER OF TIMES IEFUTL IS ENTERED FOR WAIT AND CPU   *
*                TIME IS LOGGED IN THE WFB SMF PARAMETER LIST. IT     *
*                IS POINTED TO BY SMFUSER IN THE COMMON SMF PARAMETER *
*                LIST (THE JMR - JOB MANAGEMENT RECORD).              *
*                                                                     *
*             DEPENDENCIES:  CHARACTER SET IS EBCDIC.  REASSEMBLE     *
*                IF A DIFFERENT CHARACTER SET IS NEEDED.              *
*                                                                     *
*             RESTRICTIONS:  NONE                                     *
*                                                                     *
*             REGISTER CONVENTIONS:  STANDARD CONVENTIONS.            *
*                   R0  - PARM/WORK REGISTER                          *
*                   R1  - PARM/WORK REGISTER                          *
*                   R2  - IEFUTL ENTRY TYPE INDICATOR                 *
*                       - TEMP RETURN CODE REGISTER                   *
*                   R3  - TEMP TIME EXTENSION IN SECONDS              *
*                   R4  - ACF2 ACCVT & SVC34 AREA                     *
*                   R5  - ACF2 ACUCB & LIDREC, & AUTHORIZATION CODE   *
*                   R6  - R7  ARE UNUSED                              *
*                   R8  - POINTER TO SVC34 MESSAGE AREA               *
*                   R9  - POINTER TO WFB SMFLIST                      *
*                   R10 - POINTER TO GETMAINED WTO AREA               *
*                   R11 - POINTER TO COMMON EXIT PARAMETER AREA       *
*                   R12 - BASE REGISTER                               *
*                   R13 - UNUSED (REMAINS CALLERS SAVE AREA PTR)      *
*                   R14 - RETURN REGISTER                             *
*                   R15 - RETURN CODE REGISTER                        *
*                                                                     *
*             PATCH LABEL: NO PATCH AREA PROVIDED                     *
*                                                                     *
*             MODULE TYPE:  PROCEDURE                                 *
*                                                                     *
*                PROCESSOR:  ASM                                      *
*                                                                     *
*                MODULE SIZE:  600 BYTES                              *
*                                                                     *
*                ATTRIBUTES:  SCHEDULER KEY 0, REENTRANT,             *
*                   SUPERVISOR STATE, ENABLED                         *
*                                                                     *
*             ENTRY POINTS:  IEFUTL (ONLY ENTRY POINT)                *
*                                                                     *
*             LINKAGE:  FROM INITIATOR VIA MODULE IEATLEXT            *
*                                                                     *
*             INPUT:  REGISTER 0 HAS REASON FOR ENTRY CODE            *
*                       0 - JOB CPU TIME EXCEEDED                     *
*                       4 - STEP CPU TIME EXCEEDED                    *
*                       8 - JOB WAIT TIME EXCEEDED                    *
*                     REGISTER 1 POINTS TO A LIST OF FULL WORDS       *
*                     THE FIRST OF WHICH POINTS TO THE SMF COMMON     *
*                     EXIT PARAMETER AREA MAPPED BY SMFCEPA DSECT     *
*                                                                     *
*             EXIT - NORMAL:  AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*             OUTPUT: REGISTER 1 TIME LIMIT EXTENSION IN SECS (IF ANY)*
*                     REGISTER 15 HAS EXIT CODE                       *
*                       0 - CANCEL JOB                                *
*                       8 - EXTEND TIME BY VALUE IN REGISTER 1        *
*                     WFB SMFLIST HAS UPDATED EXTENTION COUNT         *
*                                                                     *
*             EXIT - ERROR:  NONE                                     *
*                                                                     *
*             EXTERNAL REFERENCES:  NONE                              *
*                                                                     *
*             CONTROL BLOCKS: NONE                                    *
*                                                                     *
*             TABLES:  NONE                                           *
*                                                                     *
*             MACROS & SVC'S: SAVE, GETMAIN, FREEMAIN, TPUT, WTO      *
*                                                                     *
*             CHANGE ACTIVITY:  NONE                                  *
*                                                                     *
*             MESSAGES: SEE CONSTANTS AREA                            *
*                                                                     *
*             ABEND CODES:  NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
*
*   E  Q  U  A  T  E  S
*
D0       EQU   0              USED FOR OFFSET OF ZERO
D12      EQU   12             USED FOR OFFSET OF TWELVE
D28      EQU   28             USED FOR OFFSET OF TWENTY EIGHT
WTOSVC   EQU   35             WRITE TO OPERATOR SVC
WTOAREAL EQU   144            LENGTH OF GETMAIN FOR WTO WORK AREA
*
*  R  E  G  I  S  T  E  R     E  Q  U  A  T  E  S
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
IEFUTL   CSECT
         SAVE  (14,12),,IEFUTL.MVS.&SYSTIME_&SYSDATE SAVE REGISTERS
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY
         LR    R2,R0                   SAVE ENTRY TYPE INDICATOR
         L     R11,D0(R1)              LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
*
*  GET AREA FOR MESSAGES FOR REENTRANTABILITY
*
         GETMAIN R,LV=WTOAREAL,SP=241  GET STORAGE FOR WTO WORK AREA
         LR    R10,R1                  SAVE ADDRESS OF WTO WORK AREA
         GETMAIN R,LV=SVC34GM,SP=241   GET SVC34 BUFFER AREA      TP01
         LR    R8,R1                   USE R8 FOR SVC34 WORKAREA  TP01
         USING SVC34,R8                MAP SVC34 PARM AREA        TP01
         LA    R1,SVC34GM              SET UP SVC34 PARM LIST     TP01
         STH   R1,SVC34L               PLUG IN MESSAGE LENGTH     TP01
         MVC   SVC34M,TERMMSG          COPY VTAM TERM MSG TO LIST TP01
*
*  INSURE WFB SMFLIST IS PRESENT
*
         L     R9,SMFUSER              GET WFB SMFLIST ADDR
         LA    R9,0(R9)                CLEAR HIGH ORDER BYTE
         LTR   R9,R9                   IS THERE AN ADDR?
         BP    WFBSMFOK                YES - CONTINUE
         USING SMFLIST,R9              ADDR WFB SMFLIST
         MVC   0(ERRMSGL,R10),ERRMSG   MOVE ERROR MESSAGE TO WORK AREA
         LR    R1,R10                  POINT TO MESSAGE
         SVC   WTOSVC                  LET THE OPERATOR KNOW
         B     CANCEL                  DO NOT CONTINUE
         EJECT
WFBSMFOK DS    0H
*--------------------------------------------------------------------*
*        VALIDATE SUBMITTORS ACF2 AUTHORITY.  OBTAIN LOGON ID RECORD *
*              VIA ACF2 CONTROL BLOCK CHASE AND SET REGISTER 5 TO    *
*              1 IF SUBMITTOR HAS OPER  AUTHORITY, 0 IF NOT.         *
*                                                                    *
*--------------------------------------------------------------------*
         USING ACCVT,R4                REQUIRED FOR CB CHASE    TP01
         LA    R5,1                    SET AUTH IN CASE NO ACF2 TP01
         ACFGACVT R4,                  GET ACCVT ADDR IN R4     TP01   X
               NONE=NOTAUTH             ACF2 ISN'T UP...        TP01
         XR    R5,R5                   RESET TO NOT AUTH IF ACF2TP01
         ACFGUCB R4,                   GET ACF2 UCB ADDR IN R4  TP01   X
               NONE=NOTAUTH,           DEFAULT TO NO OPER       TP01   X
               INLINE=YES,             NO EXTERNAL CALL         TP01   X
               SYS=AOS2                GENERATE MVS CODE ONLY   TP01
         DROP  R4                                               TP01
         USING ACUCB,R4                MAP THE ACF2 UCB         TP01
         MVC   SVC34LU,ACUTIDNT        VTAM LU NAME INTO SVC34  TP01
         L     R4,ACULRECP             POINT TO LOGON ID RECORD TP01
         DROP  R4                                               TP01
         USING LIDREC,R4               MAP THE LOGON ID RECORD  TP01
         TM    LIDTFLG3,LIDT3OPR       SUBMITTOR HAVE ACF OPER ?TP01
         BNO   NOTAUTH                   NOPE                   TP01
         LA    R5,1                      ELSE SET AUTHORIZED FLGTP01
         DROP  R4
NOTAUTH  DS    0H
         SPACE 2
*
*  EVALUATE REGISTER 0 = 0 - JOB CPU TIME EXCEEDED
*                      = 4 - STEP CPU TIME EXCEEDED
*                      = 8 - JOB WAIT TIME EXCEEDED
*
         C     R2,FULL4                SEE IF JOB/STEP TIME EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
         EJECT
*
*  PROCESS CPU TIME LIMIT EXCEEDED
*
         MVC   0(TIMEMSGL,R10),TIMEMSG MOVE MESSAGE LIST TO WORK AREA
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   PCPU                    NO.....MUST BE BACKGROUND JOB
         LTR   R5,R5                   AUTHORIZED USER ?          TP01
         BNZ   CPUEXTND                 YES, EXTEND W/O TPUT      TP01
         TPUT  TSOMSG1,L'TSOMSG1       PUT OUT MESSAGE TO USER    TP01
         B     CPUEXTND                 SKIP THE WTO              TP01
PCPU     DS    0H
         MVC   TIMENAME(8,R10),SMFJOBN MOVE JOBNAME TO MESSAGE
         MVC   TIMETYPE(4,R10),=C' CPU' INDICATE CPU TIME EXCEEDED
         LR    R1,R10                  POINT TO MESSAGE
         SVC   WTOSVC                  LET THE OPERATOR KNOW
         CLI   SMFJOBN,C'Z'   IS THIS A TEST JOB?
         BNE   NOCANCEL                NO, DON'T CANCEL IT.       TP01
         LTR   R5,R5                   AUTHORIZED USER (R5 =1 )?  TP01
         BZ    CANCEL                   NOT AUTH, SO CANCEL JOB   TP01
NOCANCEL DS    0H                                                 TP01
         SLR   R1,R1          CLEAR WORK REGISTER
         IC    R1,SMFCPUE     GET CURRENT NUMBER OF CPU EXTENSIONS.
         LA    R1,1(R1)       UPDATE NUMBER OF CPU TIME EXTENSIONS.
         STC   R1,SMFCPUE     SAVE IN WFB SMF PARMLIST.
         C     R1,SIX         HAVE WE EXTENDED THIS JOB 6 TIMES?
         BH    CANCEL         YES - GO CANCEL IT.
CPUEXTND DS    0H
         LA    R2,8           SET RETURN CODE TO 8.
         LA    R3,60*5        EXTEND CPU TIME FOR 5 MINUTES
         B     RETURN         GO RETURN TO CALLER
         EJECT
*
*  PROCESS WAIT TIME LIMIT EXCEEDED
*
WAITTIME DS    0H
         SLR   R3,R3          CLEAR THE TIME EXTENSION REGISTER
         MVC   0(TIMEMSGL,R10),TIMEMSG MOVE MESSAGE TO WORK AREA
         TM    SMFOPT,SMFTSO  IS THIS A TIME SHARING USER
         BNO   PWAIT          NO - GO PROCESS BACKGROUND JOBS
         LTR   R5,R5                   AUTHORIZED USER (R5 = 1)? TP01
         BNZ   AUTHWAIT                YES, DON'T ABORT SESSION  TP01
         TPUT  TSOMSG3,L'TSOMSG3  TELL TSO USER WHAT'S HAPPENING
         B     CANCEL                  AND CANCEL SESSION        TP01
PWAIT    DS    0H
         CLI   SMFJOBN,C'Z'            IS THIS A TEST JOB ?      TP01
         BNE   JOBWAIT                 NO, DON'T CANCEL          TP01
         LTR   R5,R5                   AUTHORIZED SUBMITTOR ?    TP01
         BZ    CANCEL                    NOT A CHANCE, WASTE IT. TP01
         B     JOBWAIT                 PRIVILEDED BATCH JOB      TP01
AUTHWAIT DS    0H
         SLR   R1,R1                   INITIALIZE R1             TP01
         IC    R1,SMFWAITE             GET WAIT EXTENTION COUNT  TP01
         LA    R1,1(0,R1)              BUMP COUNTER              TP01
         STC   R1,SMFWAITE             SAVE IN SMF EXIT RECORD   TP01
         C     R1,=F'1'                EXTENDED ONCE ALREADY ?   TP01
         BH    CANCEL                  YES, ISSUE VTAM TERM      TP01
         B     WTEXTND                 ADD 30 MINUTES AND RETURN TP01
JOBWAIT  DS    0H
         SLR   R1,R1          CLEAR WORK REGISTER
         IC    R1,SMFWAITE    GET SMFWAITE COUNT
         LA    R1,1(R1)       UP COUNT BY 1
         STC   R1,SMFWAITE    SAVE IN WFB'S SMF RECORD
         C     R1,SIX         HAVE WE EXTENDED THIS JOB 6 TIMES?
         BH    CANCEL         YES - GO CANCEL IT.
WTEXTND  DS    0H
         LA    R2,8           SET THE RETURN CODE TO 8 (EXTEND JOB)
         LA    R3,60*30       EXTEND WAIT TIME 30 MINUTES
         B     RETURN         AND RETURN TO CALLER
         EJECT
*
*  PROCESSING FOR JOB CANCELLATION
*
CANCEL   DS    0H
         TM    SMFOPT,SMFTSO  IS THIS A TIME SHARING USER
         BNO   NOTTSU         NO - CONTINUE
         MVC   0(DISCMSGL,R10),DISCMSG     COPY DISCONNECT MSG    TP01
         MVC   DISCNAME(8,R10),SMFJOBN     PLUG IN USERID         TP01
         LR    R1,R10                      POINT TO MSG.          TP01
         SVC   WTOSVC                      ISSUE SVC              TP01
         LTR   R9,R9                   IS SMFLIST PRESENT ?       TP01
         BNP   NOTTSU                  NO, ALLOW CANCEL TO STAND  TP01
         CLC   SVC34LU,=C'XXXX'        VALID LU OBTAINED FROM ACF?TP01
         BE    NOTTSU                  NO, ALLOW CANCEL TO STAND  TP01
         XR    R0,R0                   CLEAR R0 (SET CONSOLE ID)  TP01
         STC   R0,SMFWAITE             CLEAR WAIT EXTENTION COUNT TP01
         LR    R1,R8                   POINT TO SVC34 PARM LIST   TP01
         SVC   34
         LA    R2,8                    SET EXTEND RETURN CODE     TP01
         LA    R3,16*60                 & EXTEND FOR 16 MINUTES   TP01
         B     RETURN                   (I.E. RESET JWT FOR AFTER TP01
*                                       LOGON REONNECT).          TP01
NOTTSU   DS    0H
         MVC   0(CANMSGL,R10),CANMSG  MOVE MESSAGE TO WORK AREA
         MVC   CANNAME(8,R10),SMFJOBN INDICATE JOBNAME
         LR    R1,R10         POINT TO MESSAGE
         SVC   WTOSVC         TELL THE OPERATOR
         SLR   R2,R2          0 INDICATES CANCEL JOB/TSU
*
*        NORMAL END PROCESSING
*
RETURN   DS    0H
         FREEMAIN R,LV=WTOAREAL,A=(R10),SP=241
         FREEMAIN R,LV=SVC34GM,A=(R8),SP=241
         L     R14,D12(,R13)           LOAD ADDRESS FOR RETURN
         LR    R15,R2                  SET THE RETURN CODE
         LR    R1,R3                   SET THE EXTENSION (IF ANY)
         LM    R2,R12,D28(R13)         RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
         EJECT
*
*  C O N S T A N T S   A N D   M E S S A G E S
*
FULL4    DC    F'4'
SIX      DC    F'6'
         SPACE 2
TSOMSG1  DC    C'IEFUTL - CPU TIME EXPIRED, TSO SESSION EXTENDED'
TSOMSG2  DC    C'IEFUTL - WAIT TIME EXPIRED, TSO SESSION EXTENDED'
TSOMSG3  DC    C'IEFUTL - WAIT TIME EXPIRED, TSO SESSION CANCELED '
TERMMSG  DC    C'V NET,TERM,NOTIFY=NO,TYPE=COND,SLU=XXXXXXXX'
         SPACE 2
***********************************************************************
*                                                                     *
*        ROUTCDE=2 WAS USED SO THAT ENQ RET=TEST WAS UNNECESSARY      *
*        ENQ RET=TEST IS NECESSARY FOR ROUTCDE=11 (WTP) WHETHER       *
*           OR NOT THAT ROUTING OCCURS BY DEFAULT                     *
*        WTP CAUSES AN ENQ OF THE FOLLOWING FORM:                     *
*           MAJORNAME: SYSSJWTP                                       *
*           MINORNAME: RPL + ASID (LENGTH OF 6)                       *
*              RPL IS THE 4 BYTE REQUEST PARAMETER LIST POINTER       *
*                 LOCATED AT OFFSET JSCBSMLR (X'128') IN THE JSCB     *
*              ASID IS THE 2 BYTE ADDRESS SPACE IDENTIFIER            *
*                 LOCATED AT OFFSET ASCBASID (X'24') IN THE ASCB      *
*                                                                     *
***********************************************************************
         SPACE
ERRMSG   WTO   'IEFUTL - WFB SMFLIST MISSING',ROUTCDE=(2,9),DESC=6,MF=L
ERRMSGL  EQU   *-ERRMSG
         SPACE
TIMEMSG  WTO   'IEFUTL - JOB JOBNAMEX WAIT TIME EXPIRED',              X
               ROUTCDE=(2,9),DESC=(6),MF=L
TIMEJOB  EQU   13              OFFSET TO JOB/TSU FIELD
TIMENAME EQU   17              OFFSET TO JOBNAME FIELD
TIMETYPE EQU   26              OFFSET TO WAIT/CPU FIELD
TIMEMSGL EQU   *-TIMEMSG
         SPACE
CANMSG   WTO   'IEFUTL - JOB JOBNAMEX CANCELLED',                      X
               ROUTCDE=(2,9),DESC=(6),MF=L
CANJOB   EQU   13              OFFSET TO JOB/TSU FIELD
CANNAME  EQU   17              OFFSET TO JOBNAME FIELD
CANMSGL  EQU   *-CANMSG
         SPACE
DISCMSG  WTO   'IEFUTL - TSU JOBNAMEX DISCONNECTED',                   X
               ROUTCDE=(2,9),DESC=(6),MF=L
DISCNAME EQU   17              OFFSET TO JOBNAME FIELD
DISCMSGL EQU   *-DISCMSG
         SPACE
         LTORG
         EJECT
*
*  D S E C T S   (M A P P I N G   M A C R O S)
*
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         SPACE 3
SMFDSECT SMFLIST
         SPACE 3
SVC34    DSECT                         PARM LIST FOR SVC34
SVC34L   DS    F                       LENGTH (INCLUDING LLBB)
SVC34M   DS    C'V NET,TERM,NOTIFY=NO,TYPE=COND,SLU=XXXXXXXX'
         ORG   *-8
SVC34LU  DS    CL8                     TARGET LU NAME
SVC34GM  EQU   *-SVC34
         SPACE 3
         ACCVT                         ACF2 CVT MAP
         ACFASVT                       ACF2 SUBSYSTEM VECTOR TABLE
         IHAPSA                        MVS: PREFIX AREA
         IHAASCB                       MVS: ASCB
         ACUCB                         ACF2 USER CONTROL BLOCK
         LIDREC                        ACF2 LOGON ID RECORD
         END   IEFUTL
