//WFH0TS29 JOB (GTE4),TIME=10
/*JOBPARM Q=F,I,L=100
//AA EXEC ASMFC,MAC1='SYS1.AMODGEN'
//ASM.SYSGO DD DSN=SYS1.GTEOBJ(DSPACE),DISP=SHR
//ASM.SYSIN DD *
********************************************************************
*
*      PROGRAM TO PRINTOUT AVAILABLE SPACE ON A DISK DRIVE
*      USES LSPACE (SVC 78)  FUNCTION
*      ALSO DETERMINES IF PACK HAS INDEXED VTOC
*      PRODUCED FROM DISASEMBLY OF ORIGINAL OBJECT MODULE
*      W F HARTMAN MAY 1981
*
*
*
*
********************************************************************
DSPACE   STM   R14,R12,12(R13)             SAVE REGS
         BALR  R12,R0                      ADDRESS SET
         USING *,R12                       TELL ASSEMBLER
         LR    R2,R1                       SAVE R1
         GETMAIN R,LV=168                  GET WORK AREA
         ST    R13,4(R1)                   BCHAIN
         ST    R1,8(R13)                   FCHAIN
         LR    R13,R1                      ADD OF GETMAINED AREA
         LR    R11,R13                     GETMAINED AREA
         LR    R1,R2                       ORIG R1
         USING WORK,R11                ADDRESSABILITY OF WORK AREA
         LA    R2,PPL                  GET ADDRESS OF PPL
         USING PPL,R2                  TELL ASSEMBLER
         MVC   UPT,4(R1)               COPY  UPT
         MVC   ECT,12(R1)              COPY  ECT
         LA    R0,ECB                  GET ADDRESS OF NEW ECB
         ST    R0,ECBP                 SAVE ECB ADDRESS
         XC    ECB,ECB                 ZERO ECB
         MVC   PCL,ADDPCL              GET ADD OF PCL
         LA    R0,ANS                  GET ADD OF ANSWER WORD
         ST    R0,ANSW                 PUT IN PPL
         MVC   COMBUF,0(R1)            PUT ADD OF CBUFF IN PPL
         XC    WK,WK                   ZERO USER WORK AREA
         LA    R1,PPL                  GET ADD OF PPL
         CALLTSSR EP=IKJPARS           GO PARSE IT
         DROP  R2
         LTR   R15,R15                 TEST  RETURN CODE
         BNZ   ERROR                   ERROR GO TELL USER
         L     R6,ANS                  GET ADDRESS OF ANSWER AREA
         LA    R6,8(,R6)               PDL  DSECT
         TM    6(R6),X'80'             IS IT REQUST FOR SPEC VOL
         BO    GETVOL                  YES GO PROCESS
         SR    R6,R6                   NO ALL VOLUMES
         B     GETCVT                  SKIP
GETVOL   MVI   VOLSER,C' '
         MVC   VOLSER+1(5),VOLSER
         L     R7,0(R6)          GET ADDRESS OF VOLUME
         LH    R8,4(R6)          GET LENGTH OF VOLUME
         BCTR  R8,R0             MACHINE LENGTH
         LA    R9,VOLSER         ADDRESS OF MESSAGE
         EX    R8,EXMVC          MOVE VOLUME IN
GETCVT   L     R7,16                       CVT ADDRESS
         L     R7,40(,R7)       IECILK1  UCB LOOKUP
UCBLOOP  LH    R8,0(R7)         GET UCB ADD
         LTR   R8,R8            TEST VALIDITY
         BM    UCBEND           MINUS END OF CHAIN
         BC    8,UCBSKP
         CLI   2(R8),X'FF'      IS ADDRESS VALID
         BNE   UCBSKP           NO
         CLI   18(R8),X'20'    IS IT DASDI
         BNE   UCBSKP          NO   SKIP
         TM    3(R8),X'80'     IS IT ONLINE
         BNO   UCBSKP          NO  SKIP
         TM    3(R8),X'40'     IS IT CHANGING STATUS
         BO    UCBSKP          YES SKIP
         LTR   R6,R6           TEST IF ONLY ONE VOLUME WANTED
         BZ    SNGVOL          NO GO PROCESS
         CLC   VOLSER(6),28(R8)     YES TEST VOLSER
         BNE   UCBSKP          DO NOT WANT
SNGVOL   LR    R0,R8
         MVC   INFLAG,=C'  '
         MVC   UNIT,13(R8)
         MVC   VOLSER,28(R8)
         LA    R1,WORKA
         SVC   78                ISSUE LSPACE
         MVC   OUTMESS,VOLMESS
         MVC   OUTMESS+4(6),VOLSER
         MVC   OUTMESS+16(3),UNIT
         CVAFTST  UCB=(R8)       TEST  VTOC
         B     BRTBL(R15)            TEST  RETURN CODE
BRTBL    B     RC0
         B     RC4
         B     RC8
         B     RC12
         B     RC16
RC8      MVC   INFLAG,=C'I '
RC4      DS    0H
RC0      DS    0H
         LA    R0,52
         LA    R1,OUTMESS
         TPUT (R1),(R0)  TPUT MESSAGE
TESTONE  LTR   R6,R6
         BNZ   SECVOL
UCBSKP   LA    R7,2(R7)    INCREMENT BY 2
         B     UCBLOOP     CONTINUE SEARCHING SVC TABLE
UCBEND   LTR   R6,R6        IS IT ALL
         BZ    DONE         YES
         MVC   OUTMESS(25),NOVOL
         MVC   OUTMESS+7(6),VOLSER
         LA    R0,25
         LA    R1,OUTMESS   VOL NOT FOUND
         TPUT (R1),(R0)  TPUT MESSAGE
SECVOL   L     R6,8(R6)
         LA    R6,0(R6)
         LTR   R6,R6 IS THERE A SECOND VOLUME
         BNZ   GETVOL
DONE     IKJRLSA   ANS      FREE PARSE AREA
DONE1    LR    R1,R11       GET ADDRESS TO FREE
         L     R13,4(R13)   GO TO ORG SAVE AREA
         FREEMAIN R,LV=168,A=(R1)           FREE WORK AREA
         XC    16(4,R13),16(R13)           ZERO RETURN CODE
         LM    R14,R12,12(R13)             RESTORE REGS
         BR    R14                         EXIT
RC12     TPUT  NOVIB,12
         B     TESTONE
RC16     TPUT  NOTDA,12
         B     TESTONE
ERROR    TPUT  ERRMESS,12                  TGET/TPUT
         B     DONE1
EXMVC    MVC   0(0,9),0(7)
ADDPCL   DC    V(PARSEPCL)
         DC    H'0016'
NOVIB    DC    C'VIB NOT INIT'
NOTDA    DC    C'NOT DASDI   '
ERRMESS  DC    C'PARSE FAILED'
VOLMESS  DC    C'VOL VVVVVV '
UNITMESS DC    C'UNIT UUU '
NOVOL    DC    C'VOLUME VVVVVV NOT MOUNTED'
PARSEPCL IKJPARM  DSECT=IKJPARMD
VOL      IKJIDENT 'VOLUME',UPPERCASE,MAXLNTH=6,FIRST=ALPHANUM,       XXX
               OTHER=ALPHANUM,LIST
         IKJENDP
R0      EQU   0              .
R1      EQU   1              .
R2      EQU   2              .
R3      EQU   3              .
R4      EQU   4              .
R5      EQU   5              .
R6      EQU   6              .
R7      EQU   7              .
R8      EQU   8              .
R9      EQU   9              .
R10     EQU   10             .
R11     EQU   11             .
R12     EQU   12             .
R13     EQU   13             .
R14     EQU   14             .
R15     EQU   15             .
RA      EQU   10             .
RB      EQU   11             .
RC      EQU   12             .
RD      EQU   13             .
RE      EQU   14             .
RF      EQU   15             .
         CVT  DSECT=YES
WORKZ    DSECT
*        WORK AREA FOR  DSPACE CP
WORK     DS   0F        NAME FOR USING
SAVE1    DS   XL72      SAVE AREA
ANS      DS   F
ECB      DS   F
PPL      DS   0F
UPT      DS   F
ECT      DS   F
ECBP     DS   F
PCL      DS   F
ANSW     DS   F
COMBUF   DS   F
WK       DS   F
OUTMESS  DS   CL20      MESSAGE AREA
INFLAG   DS   CL2       INDEXED VTOC FLAG AREA
WORKA    DS   CL30      AREA FOR LSPACE RETURN
VOLSER   DS   CL6       VOLSER
UNIT     DS   CL3       UNIT
         END  DSPACE
//SMP EXEC SMPPROC
//SMPCNTL DD *
 RECEIVE S(TS02902).
 APPLY S (TS02902) DIS(WRITE).
//SMPPTFIN DD DATA,DLM='??'
++USERMOD(TS02902)  /* DSPACE    COMMAND */.
++VER(Z038) FMID(GTS1102) SUP(TS02901).
++JCLIN .
//DSPACE EXEC PGM=IEWL,PARM='LET,LIST,NCAL,REFR,RENT,REUS,XREF'
//AGTELIB DD DSN=SYS1.AGTELIB,DISP=SHR
//SYSLMOD DD DSN=SYS1.GTELIB,DISP=SHR
 INCLUDE AGTELIB(DSPACE)
 NAME DSPACE(R)
++MOD(DSPACE)  LEPARM(STD) DISTLIB(AGTELIB) TXLIB(GTEOBJ).
++MAC(DSPACE)  DISTLIB(AGTEHELP) SYSLIB(GTEHELP).
)F FUNCTION -
   DSPACE displays the free space available on all online disk volumes.
          It also can display the free space on a specific volume or
          list of disk volumes.
)X SYNTAX -
   DSPACE vol-ser

   REQUIRED - none
   DEFAULTS - none (all online disk volumes are processed)
   NOTE: free space information is reported in the following format:
              cylinders,tracks,extents/max-cyl,max-trk
         Where:
         CYLINDERS - total number of full cylinders available.
         TRACKS    - total number of tracks available,excluding
                     cylinders counted above.
         EXTENTS   - number of extents of free space.
         MAX-CYL   - maximum single full cylinder space available.
         MAX-TRK   - maximum single track extent available, not included
                     in MAX-CYL.
)O OPERANDS -
))VOL-SER       - The volume identification of the disk(s) to be
                  reported on. It can be a list, enclosed in parens.
                  Each vol-ser can be up to 6 characters in length.
??
//
