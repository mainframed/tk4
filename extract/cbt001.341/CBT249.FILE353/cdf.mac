//BCOOKCDF  JOB (X0002,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=BCOOK
/*JOBPARM L=999
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.HASPSRC,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         TITLE 'CONSOLE DISPLAY FACILITY (CDF)'
***********************************************************************
*                                                                     *
*                                                                     *
*                 CONSOLE DISPLAY FACILITY (CDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY MVS MASTER CONSOLE ACTIVITY AND PROCESS MVS       *
*           AND JES COMMANDS.                                         *
*                                                                     *
*                                                                     *
* FUNCTION: CDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A CONSOLE DISPLAY        *
*           SIMILAR TO JESOPER, DIDOCS,ETC.,        BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           CDF LOOKS AT VARIOUS MVS SP 1.3.0 CONTROL BLOCKS.         *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* CHANGE LOG:                                                         *
*                                                                     *
*   9/21/83  REVISED SCREEN DISPLAY TO PUT REQUESTS FOR REPLY, MOUNTS,*
*    BCOOK   AND INTERVENTION REQUIRED AT TOP OF DISPLAY.             *
*                                                                     *
*   9/28/83  HIGHLIGHT MESSAGES WHERE APPROPRIATE.                    *
*    BCOOK                                                            *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
CDF      CSECT
         USING *,13,12,11,10
         B     72(,15)
         DC    17F'0'
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
*
SETBASES DS    0H
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
*---------------------------------------------------------------------*
*                                                                     *
*                       PROGRAM INITIALIZATION                        *
*                                                                     *
*---------------------------------------------------------------------*
*
SHOWINIT DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,U8,USERID,SPFMOVE),                        X
               VL,MF=(E,ISPARMS)
*
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
SHOWAGIN DS    0H
         BAL   R7,SHOWCONS    DISPLAY THE MASTER CONSOLE
*
SHOWDONE DS    0H
         CLI   ATABFL,C'0'        NO ACTIVE TABLE
         BE    SHOWRTRN
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               JCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CONLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
*
SHOWRTRN DS    0H
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
L4096    DC    F'4096'
*
         TITLE 'CDF   START OF SHOW CONSOLE LOOP'
SHOWCONS DS    0H
         ST    R7,CDF7HOLD SAVE THE RETURN ADDRESS
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   ATABFL,C'0'        DO I HAVE AN ACTIVE TABLE?
         BE    ANOTAB             NOPE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               JCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CONLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
         TITLE 'CDF   DEFINE THE CONSOLE TABLE  '
ANOTAB   DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               CONLINEP,TABAREA,CHAR,L79),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               JCMD,CMDLINE,CHAR,L42),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               CDFTABLE,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
*
         TITLE 'CDF   BUILD UCM TABLE           '
TAB1LOOP DS    0H
*
*---------------------------------------------------------------------*
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
*---------------------------------------------------------------------*
BLDUCMS  L     R4,16              R4 = ADDR OF CVT
         USING CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         DROP  R4
         USING UCM,R4
         L     R5,UCMVEA          R5 = ADDR OF FIRST UCM ENTRY
         L     R6,UCMVEZ          R6 = LENGTH OF EACH UCM ENTRY
         L     R7,UCMVEL          R7 = ADDR OF LAST UCM ENTRY
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R2,R2              R2  = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(0,R8)         SAVE UCM ADDRESS IN UCMTAB
         LA    R2,1(0,R2)         R2  = R2  + 1  (ONE MORE UCM)
         LA    R8,4(0,R8)         R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R2,NUMUCMS         SAVE NUMBER OF UCMS FOUND
         DROP  R4
*---------------------------------------------------------------------*
*        SCAN THE UCM FOR THE MASTER CONSOLE AS THE DEFAULT        GMM*
*        CONSOLE WHEN ENTERING THE "JESOPER" COMMAND.              GMM*
*---------------------------------------------------------------------*
SETMAST  LA    R4,1               SET DEFAULT TO FIRST CONSOLE     JL1
MASTSCAN LA    R5,UCMTAB          SET POINTER TO UCM ADDRESS TABLE GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?              GMM
         BNH   MASTCONT           NO, CONTINUE                     GMM
         ABEND 1                  IMPOSSIBLE .. NO MASTER CONSOLE ?GMM
MASTCONT SLL   R4,2               MAKE CONSOLE NUMBER TABLE INDEX  GMM
         LA    R5,0(R5,R4)        SET POINTER TO UCM ADDRESS SLOT  GMM
         L     R5,0(0,R5)         SET POINTER TO UCM               GMM
         USING UCMLIST,R5         TELL ASSEMBLER ABOUT UCM         GMM
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?        GMM
         BO    MASTFND            YES ? FOUND THE MASTER CONSOLE ..GMM
         SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE GMM
         LA    R4,1(0,R4)         SET POINTER TO NEXT CONSOLE      GMM
         B     MASTSCAN           CONTINUE MASTER CONSOLE SCAN LOOPGMM
MASTFND  SRL   R4,2               RESET CONSOLE NUMBER TO RELATIVE GMM
         ST    R4,OLDCONS         SET VALID OLD CONSOLE NUMBER     GMM
         ST    R4,CONSOLE         SET VALID CONSOLE NUMBER (MASTER)GMM
         DROP  R5                 DONT NEED UCM ADDRESSABILITY NOW GMM
         TITLE 'CDF   PICK A CONSOLE            '
*---------------------------------------------------------------------*
*                                                                     *
*                            TOP OF LOOP                              *
*                                                                     *
*---------------------------------------------------------------------*
NEXTPAGE DS    0H
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         ICM   R4,15,CONSOLE      R4 = CONSOLE TO BE DISPLAYED
         BZ    WRTERR4            WE HAVE NO CONSOLE ZERO (MASTER) GMM
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BNH   GETUCM             NO, CONTINUE
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
RESETCN  L     R4,OLDCONS         RESET TO OLD CONSOLE
         ST    R4,CONSOLE         AND SAVE IT
GETUCM   SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(0,R5)         R5 = ADDR OF UCM
         USING UCMLIST,R5
         L     R6,UCMXB           R6 = ADDR OF RDCM
         USING IEERDCM,R6                                          GMM
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BP    GRAPHICS           YES
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
WRTERR4  DS    0H
         MVI   CLEAR,X'C5'        TURN ON BELL BIT IN WCC          JL2
         B     RESETCN            RESET THE CONSOLE NUMBER
         SPACE
GRAPHICS EQU   *
         MVC   CTYPE(28),BLANKS   BLANK OUT CONSOLE TYPE FIELD
         TM    UCMDISP1,UCMDISPA  IS THIS A MASTER CONSOLE?
         BNO   AUTH0              NO
         MVC   MASTER(6),=CL6'MASTER' YES
AUTH0    TM    UCMAUTHA,UCMAUTH1  IS THIS CONSOLE SYSTEM AUTHORIZED?
         BNO   AUTH1              NO
         MVC   SYS(3),=CL3'SYS'   YES
AUTH1    TM    UCMAUTHA,UCMAUTH2  IS IT I/O AUTHOZRIZED?
         BNO   AUTH2              NO
         MVC   IO(3),=CL3'I/O'    YES
AUTH2    TM    UCMAUTHA,UCMAUTH3  IS IT CONS AUTHORIZED?
         BNO   AUTHDONE           NO
         MVC   CONS(4),=CL4'CONS' YES
AUTHDONE EQU   *
         L     R7,UCMUCB          R7 = ADDR OF UCB
         MVC   UNIT(3),13(R7)     MOVE UNIT ADDR INTO LINE
         L     R7,DCMADTRN        GET ADDRESS OF TDCM              GMM
         USING STRTDCM,7
         SPACE 2
         TITLE 'CDF   CROSS MEMORY SERVICES     '
*---------------------------------------------------------------------*
*                                                                     *
*                 FILLIN TRAILER AT BOTTOM OF SCREEN                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         L     R3,CONSOLE         LOAD THE CONSOLE NUMBER
         CVD   R3,WORK            CONVERT TO DECIMAL IN WORK
         MVC   SCRATCH(4),PATTERN MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6  EDIT IN CONSOLE NUMBER
         MVC   CONNUM(2),SCRATCH+2 MOVE CONSOLE NUMBER INTO PLACE
         LA    R0,1                    REQUEST AUTHORIZATION ON    JL1
         SVC   235                     SET AUTHORIZATION ON        JL1
         MODESET MF=(E,SUPRMOD)   GET INTO SUPV STATE AND KEY ZERO JPB
*
*                                      SET UP FOR CROSS MEMORY MOVE
*
         AXRES AXLIST=AXLIST           RESERVE AN AX               JPB
         SPACE 2
         LA    R1,CONSASID             'CONSOLE' ASID IS 5         JPB
         AXEXT ASID=(1)                EXTRACT AX OF 'CONSOLE'IN R0JPB
         SPACE 2
         AXSET AX=(0)                  SET OUT AX TO THAT OF CONSOLEPB
         SPACE 2
         LA    R1,CONSASID             'CONSOLE' ASID IS 5         JPB
         SSAR  R1                      SET 'CONSOLE' AS SECONDARY  JPB
         SPACE 2
         PRINT ON,GEN
*
*                                      PICK UP LAST LINE
*
         SLR   R0,R0                   SECONDARY KEY
         LA    R1,L'LASTLINE           TRUE LENGTH
         MVCP  LASTLINE(R1),DCMINPUT-STRTDCM(R7),R0
*
*                                      PICK UP START ADDRESS
*
         LA    R1,CONSASID
         SSAR  R1
         LA    R1,4               SET TRUE LENGTH                  JPB
         SLR   R0,R0              SET STORAGE KEY                  JPB
         MVCP  SIBPTR(R1),DCMASCRN-STRTDCM(R7),R0 PT TO SCRN ADRJPB
*
*                                      SET UP FOR XMEM MOVE LOOP
*
         L     R8,SIBPTR          R8 = ADDR OF SCREEN IMAGE
         LA    R4,BUF             R4 = ADDR OF OUTPUT BUFFER
         LA    R5,M4BUFLEN        R5 = 3278-4 BUFFER LEN (35 LINES)
         LA    R9,M4BUFLEN        R9 = 3278-4 BUFFER LEN (35 LINES)
MOVEBUFF DS    0H
         SH    R8,=H'6'
         SLR   R0,R0              RESET THE KEY
*
MVCPLOOP DS    0H
         MVCP  0(R5,R4),0(R8),R0  LOOP THRU THE CONSOLE BUFFER
*
         BZ    RESETSAR
         AL    R8,=F'256'         MAX LEN ALLOWED
         AL    R4,=F'256'         MAX LEN ALLOWED
         SL    R5,=F'256'         MAX LEN ALLOWED
         B     MVCPLOOP
*
RESETSAR DS    0H
*---------------------------------------------------------------------*
*                                                                     *
*        IN ORDER TO ISSUE SVCS THE PRIMARY AND                       *
*        SECONDARY ADDRSPC MUST BE THE SAME                           *
*                                                                     *
*---------------------------------------------------------------------*
*
         EPAR  R1                 EXTRACT PRIMARY ASN
         SSAR  R1
         MODESET KEY=NZERO                                         GMM
         SR    R0,R0                   REQUEST AUTHORIZATION OFF   JL1
         SVC   235                     SET AUTHORIZATION OFF       JL1
*
         DROP  R5,R6,R7                                            GMM
*
GOTITALL DS    0H
         LA    R1,1                    SPF LINE OVERHEAD
         ST    R1,OUTLINES             INITIALIZE IT
*
         B     SCANEM    GO SCAN THE SCREEN FOR REPLIES AND MOUNTS
*
         TITLE 'CDF   SCAN FOR REPLIES AND MOUNTS'
SCANEM   DS    0H
*
         XR    R8,R8              R8 = COUNTER = 0
         LA    R3,BUF             SET POINTER TO FIRST LINE
*
SCNEXT   DS    0H
         CLC   IEEMSG(3),6(R3)   END OF SCREEN
         BE    SCLAST            DISPLAY THE TABLE
         CLC   BLANKS(70),5(R3)  ALL SPACES
         BE    SCBUMP            BUMP TO NEXT LINE
         MVI   TABAREA,X'03'     HIGHLIGHT
         CLI   09(R3),C'*'       MOUNT OR REPLY PENDING
         BE    SCKEEP            BUMP TO NEXT LINE
         CLI   19(R3),C'*'       MOUNT OR REPLY SATISFIED
         BNE   SCBUMP            BUMP TO NEXT LINE
         CLC   20(7,R3),IEAMSG   INTERVENTION REQUIRED, SATISFIED
         BE    SCBUMP            BUMP TO NEXT LINE
         MVI   TABAREA,X'00'    PREVENT ISPF DROPPING LEADING BLANKS
SCKEEP   DS    0H
         LA    R8,1(0,R8)         ADD 1 TO COUNTER
         MVC   TABAREA+1(77),8(R3)  MOVE IT TO SPF AREA
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
         MVC   5(70,R3),BLANKS    PREVENT DUPLICATE MESSAGES ON SCREEN
*
SCBUMP   DS    0H
         LA    R3,84(0,R3)        POINT TO NEXT LINE
*
         B     SCNEXT             CHECK IT OUT
*
SCLAST   DS    0H
*        MVC   TABAREA(79),BLANKS
*        MVI   TABAREA,X'00'    PREVENT ISPF DROPPING LEADING BLANKS
*        BAL   R7,OUTMSG
*        MVC   TABAREA(79),MSG2
*        BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*        MVC   TABAREA(79),BLANKS
*        MVI   TABAREA,X'00'    PREVENT ISPF DROPPING LEADING BLANKS
*        BAL   R7,OUTMSG
*
         B     COUNTEM           DISPLAY THE TABLE
*
IEEMSG   DC    C'IEE152I'        BOTTOM MESSAGE ON OPER CONSOLE
IEAMSG   DC    C'IEA000A'        INTERVENTION REQUIRED
         TITLE 'CDF   COUNT LINES TO BE DISPLAYED'
COUNTEM  DS    0H
*
*
*
*
         XR    R8,R8              R8 = COUNTER = 0
         LA    R3,BUF             SET POINTER TO FIRST LINE
*
COUNTLP  DS    0H
         CLC   IEEMSG(3),6(R3)   END OF SCREEN
         BE    COUNTED            CHECK COUNT
         CLC   BLANKS(70),5(R3)   BLANK LINE
         BE    COUNTUP1           CHECK COUNT
         C     R8,=F'44'          MASTER CONSOLE MAX LINES
         BNL   COUNTED            COUNT DONE
         LA    R8,1(0,R8)         ADD 1 TO COUNTER
COUNTUP1 DS    0H
         LA    R3,84(0,R3)        NOPE, POINT TO NEXT LINE
         B     COUNTLP            LOOP
COUNTED  DS    0H
*
         LA    R1,23              PICK UP MAXIMUM LINES FOR 3278-2
         C     R1,OUTLINES        DO I ALREADY HAVE MORE THAN THAT?
         BL    SHOWSCRN         GO DISPLAY THE TABLE
         S     R1,OUTLINES        SUBTRACT NUMBER ALREADY DISPLAYED
         LA    R3,BUF             SET POINTER TO FIRST LINE
*
         CR    R8,R1               278-2 CAN SHOW MAX OF 19 LINES
         BL    NEXTL              GO DISPLAY THE LINES
*
         SR    R8,R1              SET R8 = LINES TO BUMP
         LR    R1,R8              SET R8 = LINES TO BUMP
         SR    R0,0               CLEAR EVEN REG OF EVEN/ODD PAIR
         LA    R3,84              MULTIPLIER
         MR    R0,R3              MULTIPLY NUMBER OF EXTRA LINES BY 84
         LA    R3,BUF(R1)         USE PRODUCT AS INDEX REG
*
         B     NEXTL
         TITLE 'CDF   BUILD SPF TABLE           '
NEXTL    DS    0H
         CLC   BLANKS(70),5(R3)  BLANK LINE
         BE    BUMPL1             CHECK COUNT
         CLC   IEEMSG(3),6(R3)   END OF SCREEN
         BE    LASTL            GO DISPLAY THE TABLE
         MVC   TABAREA(78),7(R3)  MOVE IT TO SPF AREA
         MVI   TABAREA,X'00'    PREVENT ISPF DROPPING LEADING BLANKS
*
***********************************************************************
*
*   ADD THE DETAIL LINE TO THE TABLE
*
***********************************************************************
*
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
BUMPL1   DS    0H
         LA    R3,84(0,R3)        NOPE, POINT TO NEXT LINE
*
         B     NEXTL              PRINT NEXT LINE
LASTL    DS    0H
         MVC   TABAREA(77),5(R3)  MOVE IT TO SPF AREA
         MVI   TABAREA,X'00'    PREVENT ISPF DROPPING LEADING BLANKS
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
         B     SHOWSCRN         GO DISPLAY THE TABLE
*
         TITLE 'CDF  DISPLAY SPF TABLE          '
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
SHOWSCRN DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               CDFTABLE,                                               X
               CDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
         C     R15,F8         END KEY
         BE    DUNWITIT       YUP
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BNE   DUNWITIT       NOPE
GOTSTUFF DS    0H
         TR    CMDLINE,CAPSONLY  UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDLINE,C' '   IF COMMAND AREA IS SPACES
         BE    SHOWAGIN       THEN A COMMAND WAS NOT ENTERED
*
         CLC   CMDLINE(3),=C'REP'      IS IT REPEAT,
         BE    SHOWAGIN                  THEN GO REPEAT IT
*
         CLC   CMDLINE(3),=C'RPT'      IS IT REPEAT,
         BE    SHOWAGIN                  THEN GO REPEAT IT
*
         CLC   CMDLINE(2),=C'D '       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   CMDLINE(2),=C'$D'       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   CMDLINE(2),=C'$L'       IS IT A DISPLAY COMMAND
         BE    GOTCMD                    THEN GO DO IT
*
TSTAUTH  DS    0H
*
         CLC   USERID(2),=C'AS' SYSTEMS PROGRAMMER ID
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   USERID(2),=C'BP' PRODUCTION SERVICES ID
         BE    GOTCMD                    THEN GO DO IT
*
         CLC   USERID(3),=C'MVS' OPERATIONS ID - MVSOPER
         BE    GOTCMD                    THEN GO DO IT
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               CDF001),                                                X
               VL,MF=(E,ISPARMS)
*
         B     SHOWAGIN
*
DUNWITIT DS    0H
         L     R7,CDF7HOLD     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
*
GOTCMD   DS    0H
*
*        LA    R1,76            LENGTH OF COMMAND
*        STCM  R1,3,CMDISMF     STICK IT IN THERE
*
*        CLC   CMDLINE(2),=C'R '  AM I REPLYING TO SOMETHING
*        BNE   GETAUTH            NOPE
*
         LA    R1,42            LENGTH OF REPLY COMMAND
         STCM  R1,3,CMDISMF     STICK IT IN THERE
*
GETAUTH  DS    0H
*
         LA    R15,1            SET FUNCTION CODE = 1
         LA    R1,CMDISMF             GET ADDRESS OF COMMAND I SMF
         SVC   242                     ISSUE COMMAND VIA SVC 34
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(41),CMDLINE
*
         B     SHOWAGIN
*
CMDISMF  DC    Y(76),Y(00)
CMDLINE  DC    CL42'     ' COMMAND SVC 34 INPUT
         DC    CL19' COMMAND ISSUED BY '
USERID   DC    CL11'     '
*
INITMSG  MVI   TABAREA,C' '   BLANK OUT THE
         MVC   TABAREA+1(79),TABAREA MESSAGE BUFFER
         BR    R7             RETURN TO OUR CALLER
         SPACE 5
         EJECT
*
***********************************************************************
*
*   ADD A LINE TO THE TABLE
*
***********************************************************************
*
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R7,OUTLINES             PICK UP THE OUTPUT LINE COUNT
         LA    R7,1(,7)                ADD 1
         ST    R7,OUTLINES             SAVE THE OUTPUT LINE COUNT
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               CDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   TABAREA,C' '
         MVC   TABAREA+1(79),TABAREA
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'MISCELLANEOUS CONSTANTS'
         LTORG
*
         DS    0F
OUTLINES DC    F'5'
OUTMAX   DC    F'22'        MAX LINES ON 3278-2
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
CDF7HOLD DS    F
OUT7HOLD DS    F
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
CDFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(CDFOPT)
SELECTR  DC    CL8'SELECT  '
CDFOPT   DC    CL8'CDFOPT  '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
L3       DC    F'3'
L79      DC    F'79'
L20      DC    F'20'
L60      DC    F'60'
L42      DC    F'42'
L15      DC    F'15'
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             CDF NAME LIST
         DC    C'CONLINE )'     JOB INFO
CONLINEP DC    C'(CONLINE )'    JOB INFO
JCMD     DC    C'(JCMD    )'    SELECTION CODE
*
SPACES   DC    C'        '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
CDFDATAB DC    C'CDFDATAB'
CDFTABLE DC    C'CDFTABLE'
*
CMDDATAB DC    C'CMDDATAB'
CMDTABLE DC    C'CMDTABLE'
*
*---------------------------------------------------------------------*
*                                                                     *
*                         C O N S T A N T S                           *
*                                                                     *
*---------------------------------------------------------------------*
SCRATCH  DS    D                   SCRATCH AREA FOR CHAR. MANIP
OLDCONS  DC    F'1'                PREVIOUS CONSOLE NUMBER
CONSOLE  DC    F'1'                CONSOLE TO BE LOOKED AT
LOGON    DC    CL8' '              LOGON USER ID                   GMM
PROFID   DC    CL8' '              USER ID SET VIA PROFILE COMMAND GMM
PROFLEN  DC    X'00'               LENGTH OF USER SET PROFILE      GMM
AUTH     DC    X'00'           JESOPER COMMAND AUTHORIZATION BYTE  GMM
AUTHOPR  EQU   X'80'               TSO OPERATOR PRIVILEGE          GMM
*        JES AND VS COMMAND BUFFER AND LENGTH                      GMM
PCOMMAND EQU   *                   JES OR VS COMMAND BLOCK         GMM
PCMDLEN  DC    H'0'                LENGTH OF COMMAND BLOCK         GMM
         DC    H'0'                UNUSED NIBBLE                   GMM
PCMD     DC    CL80' '             "DWR" OPERATOR COMMAND          GMM
TPUTLEN  DC    A(MOD2LEN)          LENGTH OF MOD 2 TPUT
MOD4TPUT DC    A(MOD4LEN)          LENGTH OF MOD4 TPUT
MOD2TPUT DC    A(MOD2LEN)          LENGTH OF MOD2 TPUT
LPSCREEN DC    H'0'                LINES PER SCREEN
CPLINE   DC    H'0'                CHARACTERS PER LINE
TIMER    DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'100'              DELAY FOR 100 HUNDREDTHS OF A SECOND
MOD4FLG  DC    X'00'               X'FF' INDICATES 3278-4 IN USE
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
CRTFLAG  DC    X'FF'               X'FF' INDICATES CRT IN USE
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
INTEGFLG DC    X'00'               INTEGRATED CONSOLE FLAG
FULLSCR  DC    X'03'               TPUT ASIS FLAG
EDITFLG  DC    X'00'               TPUT EDIT FLAG
NULLS    DC    80X'00'             JUST NULLS
R41C1    DC    X'11F240'           3278-4  --  ROW 41, COL 1
R42C1    DC    X'11F350'           3278-4  --  ROW 42, COL 1
R43C1    DC    X'11F460'           3278-4  --  ROW 43, COL 1
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
REPLYCDE DC    CL1' '              USERS COMMAND PREFIX FIELD
REPLY    DC    CL80' '             USERS COMMAND INPUT FIELD
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
DCMSECED DC    CL80' '
BLANKS   DC    CL80' '
IBUFFER  DC    CL80' '             INPUT BUFFER FOR PFK PROCESSING GMM
PASSWORD DC    CL8'JBS001'         PASSWORD FOR COMMAND AUTHORIZATION
USPSWD   DC    CL8' '                                              JL1
PSWDFG   DC    X'00'                                               JL1
OKAUTH   EQU   X'01'                                               JL1
PGMPSWD  EQU   X'02'                                               JL1
RMFPTR   DS    F                   ENTRY POINT TO RMFMON           JL2
CNTLFG   DC    X'00'               CONTROL FLAG                    JL2
LOAD     EQU   X'80'               RMFMON WAS LOADED               JL2
*---------------------------------------------------------------------*
*                                                                     *
*        FOLLOWING IS IN SUPPORT OF C M S                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
SIBPTR   DS    F                   PRT TO SCREEN IMAGE BUFFER
AXLIST   DC    H'1',H'0'           AXRES PARMLIST
CONSASID EQU   5                   'CONSOLE' ASID FOR CMS
         SPACE 2
         CNOP  0,8
*---------------------------------------------------------------------*
*                                                                     *
*               327X SCREEN CLEAR CONTROL CHARACTERS                  *
*                                                                     *
*---------------------------------------------------------------------*
CLR      DC    X'C1'              WCC - CLEAR SCREEN
         DC    X'115D7E'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'13'              INSERT CURSOR
CLRLEN   EQU   *-CLR
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*                  DISPLAY SCREEN - HEADER SECTION                    *
*                                                                     *
*---------------------------------------------------------------------*
HEADER   EQU   *
CLEAR    DC    X'C1'              WCC
         DC    X'115D7F'          SBA TO ROW 24, COL 80 (FSE 5.0)
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'3C404000'        FILL SCREEN WITH NULLS
         DC    X'114040'          SBA TO ROW 1, COL 1
         DC    X'1D60'            ATTR BYTE - PROTECTED FIELD
         DC    X'40'
*---------------------------------------------------------------------*
*                                                                     *
*               DISPLAY SCREEN - IMAGE BUFFER SECTION                 *
*                                                                     *
*---------------------------------------------------------------------*
         DC    CL40'BUF  THIS IS THE START OF THE BUFFER  =>'
BUF      DC    21CL80' '          OPERATORS SCREEN BUFFER
         DC    14CL80' '          PLUS EXTRA FOR 3278-4
         DC    CL40'<= BUF  THIS IS THE END   OF THE BUFFER '
*---------------------------------------------------------------------*
*                                                                     *
*               DISPLAY SCREEN - TRAILER SECTION                      *
*                                                                     *
*---------------------------------------------------------------------*
TRAILER  EQU   *
CMDCTRL  DC    X'C1115A50'        SBA TO ROW 22, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
LASTLINE DC    CL79' '            OPERATORS COMMAND INPUT LINE
PHEADING DC    X'115B60'          SBA TO ROW 23, COL 1
         DC    X'1DE8'            ATTR BYTE - PROTECTED, HIGH INTENSITY
CONNUM   DC    CL2' 1'            CONSOLE NUMBER
CTYPE    DC    CL4' '
MASTER   DC    CL8' '             MASTER CONSOLE
SYS      DC    CL4' '             SYS  AUTHORIZATION
IO       DC    CL4' '             I/O  AUTHORIZATION
CONS     DC    CL5' '             CONS AUTHORIZATION
         DC    CL3' '
UNIT     DC    CL4' '             UNIT ADDR OF CONSOLE
TIME     DC    CL3' '             SECONDS REMAINING ON TIMER
SLASH    DC    CL1'/'
PAUSE    DC    CL3'1.0'           DELAY IN SECONDS
         DC    CL2' '
FREEZE   DC    C'F'               FREEZE/RELEASE MODE
MODE     DC    CL1'2'             DISPLAY MODE 2/1
         DC    CL2' '
PAGE     DC    CL1'2'             PAGE NUMBER
R24C1    DC    X'115CF0'          SBA TO ROW 24, COL 1
         DC    X'1D40'            ATTR BYTE - UNPROTECTED, LOW INTENS.
         DC    X'13'              INSERT CURSOR
USERLINE DC    CL13' '            USERS COMMAND INPUT LINE
ERROR    DC    CL66' '            ERROR MSG FIELD
ENDTRAIL EQU   *
         LTORG
         DS    0F
UCMTAB   DS    F
         DS    35F                PROVIDE SPACE FOR 20 UCM ADDRESSES
UCMTABE  EQU   *
NUMUCMS  DS    H
         SPACE 5
*---------------------------------------------------------------------*
*                                                                     *
*                   COMMAND VALIDATION TABLE                          *
*                                                                     *
*---------------------------------------------------------------------*
CMDTAB   DC    X'08',C'DISPLAY '                                   JL1
         DC    X'02',C'D '                                         JL1
         DC    X'06',C'START '                                     JL1
         DC    X'02',C'S '                                         JL1
         DC    X'03',C'$AJ'                                        JL1
         DC    X'03',C'$A'''                                       JL1
         DC    X'03',C'$DJ'                                        JL1
         DC    X'03',C'$D'''                                       JL1
         DC    X'02',C'$R'                                         JL1
         DC    X'03',C'$T'''                                       JL1
         DC    X'03',C'$TJ'                                        JL1
         DC    X'06',C'$LSYS '                                     JL1
         DC    X'00'                  END OF TABLE                 JL1
*
         TITLE 'EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
LEN9     EQU   9*80             NUMBER OF BYTES IN  9 LINES
LEN21    EQU   21*80            NUMBER OF BYTES IN 21 LINES
LEN22    EQU   22*80            NUMBER OF BYTES IN 22 LINES
LEN13    EQU   13*80            NUMBER OF BYTES IN 13 LINES
LEN14    EQU   14*80            NUMBER OF BYTES IN 14 LINES
M2BUFLEN EQU   21*80            LENGTH OF BUFFER 3278-2
M4BUFLEN EQU   35*80            LENGTH OF BUFFER 3278-4
HEADLEN  EQU   BUF-HEADER       LENGTH OF HEADER
TRAILEN  EQU   ENDTRAIL-TRAILER LENGTH OF TRAILER
MOD4LEN  EQU   ENDTRAIL-HEADER  LENGTH OF TPUT FOR MOD4
MOD2LEN  EQU   MOD4LEN-LEN14    LENGTH OF TPUT FOR MOD2
ENDMOD2  EQU   BUF+M2BUFLEN     ADDR OF TRAILER FOR 3278-2
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L                            GMM
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L                          GMM
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
TABAREA  DS    0CL79
JOB      DC    CL8' '
         DC    C' '
PSTEP    DC    C' '
STEP     DC    CL8' '
         DC    C' '
WHERE    DC    CL3' '
         DC    C' '
STATUS   DC    CL4' '
         DC    C' '
SIZE     DC    CL5' '
         DC    CL2' '
LINEWSS  DC    CL5' '
         DC    CL2' '
TYPE     DC    C' '
         DC    C' '
DP       DC    CL2' '
         DC    C' '
LINEPGN  DC    CL3' '
         DC    C' '
LINESWC  DC    CL3' '
         DC    C' '
         DC    C' '
         DC    C' '
LINEWMR  DC    CL5' '
         DC    C' '
CPUTIME  DC    CL8' '
         DC    C' '
         DC    CL8' '
         DC    C' '
*
LINEWMS  DC    CL7' '
*
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
JOBNAMS  DS    CL8            JOBNAME SAVE AREA
JOBTYPE  DS    CL3            JOBTYPE SAVE AREA
*
*
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHSWAP'
*                ×...×...×...×...×...×...×...×...×...×...×...
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'Ö.<(+×&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         SPACE 2
         EJECT
HEADLIN1 DS    0CL77
         DC    CL4'CPU='         CPU UTILIZATION
HDRCPU   DC    CL3'XXX',C'%'
         DC    CL5' AFC='        AVERAGE AVAILABLE FRAME COUNT
HDRAFC   DC    CL4'XXXX'
         DC    CL5' PFR='        PAGE FAULT RATE
HDRAPR   DC    CL4'XXXX'
         DC    CL5' ASQ='        AUX STORAGE MANAGER QUE LENGTH
HDRASQ   DC    CL4'XXXX'
         DC    CL5' UIC='        UNIREFERENCED INTERVAL COUNT
HDRUIC   DC    CL4'XXXX'
         DC    CL5' DIO='        DEFERRED IO REQUESTS
HDRDIO   DC    CL4'XXXX'
         DC    CL8' DMNS='       # OF DOMAINS IN ENTRIES
HDRDMN   DC    3CL6' MT:RU'      MULTI-PROGRAMMING LEVEL TARGET
*
HEADLIN2 DC CL77' JOB      STEP   .STATUS.  MEMORY   WSS  T DP  PG SWAPX
               S   WMR    CPU'
*
CDF001   DC    CL8'CDF001 '
*
SETMSG   DC    CL8'SETMSG  '
*
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
*
         LTORG
         EJECT
         PRINT NOGEN
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
TCBWORDS DSECT                                                     JL1
TCBNEXT  DS    F                                                   JL1
TCBPTR   DS    F                                                   JL1
ASCBNEXT DS    F                                                   JL1
ASCBPTR  DS    F                                                   JL1
         TITLE '*** PROTECTED STEP CONTROL BLOCK DSECT ***'        GMM
         IKJPSCB                   COPY THE PSCB FORMAT DSECT      GMM
         TITLE '*** COMMAND PROCESSOR PARAMETER LIST DSECT ***'    GMM
         IKJCPPL                   COMMAND PROCESSOR PARM LIST     GMM
         TITLE '*** TSO USER PROFILE TABLE DSECT ***'              GMM
         IKJUPT                    TSO USER PROFILE TABLE          GMM
         TITLE '*** COMMUNICATION VECTOR TABLE ***'                GMM
         CVT   DSECT=YES                                           GMM
         TITLE '*** RDCM DSECT ***'                                GMM
IEERDCM  DSECT ,                                                   GMM
         IEECRDCM                                                  GMM
         TITLE '*** TDCM DSECT ***'                                GMM
IEETDCM  DSECT ,                                                   GMM
         IEECDCM                                                   GMM
         TITLE '*** MULTIPLE CONSOLE SUPPORT (MCS) UCM PREFIX ***' GMM
         IEECUCM FORMAT=NEW
         TITLE '*** ADDRESS SPACE CONTROL BLOCK ***'               JL1
         IHAASCB                                                   JL1
         TITLE '*** TSB DSECT ***'                                 JL1
         IKJTSB LIST=YES                                           JL1
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.SERV.LOAD
//         DD   DISP=SHR,DSN=TECH.FIXDHEAD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.LOAD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  NAME CDF(R)
/*
