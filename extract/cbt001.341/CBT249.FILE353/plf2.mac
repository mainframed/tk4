//BCOOKPLF  JOB (X9999,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=X,NOTIFY=BCOOK
/*JOBPARM L=99
//* *****************************************************************
//* *****************************************************************
//*                                                               ***
//* TECH.SERV.BCOOK (PLF2)                                        ***
//*                                                               ***
//* THIS IS THE PRE-ISPF/PDF VERSION                              ***
//*                                                               ***
//* *****************************************************************
//* *****************************************************************
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,
//  UNIT=SYSDA,VOL=SER=TSTRES
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//  UNIT=SYSDA,VOL=SER=TSTRES
//         DD  DSN=SYS1.HASPSRC,DISP=SHR
//         DD  DSN=SYS2.MACLIB,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSGO    DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
***********************************************************************
*                                                                     *
*                                                                     *
*               PAN LIBRARY FACILITY (PLF)                            *
*                                                                     *
*                                                                     *
* ABSTRACT: EDIT, BROWSE AND UTILITY FUNCTIONS FOR PAN LIBRARIES.     *
*                                                                     *
*                                                                     *
* FUNCTION: PLF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A DIRECTORY MENU OF      *
*           A PRE-ALLOCATED PAN LIBRARY (DDNAME=PANDD1) AND ALLOWS    *
*           LINE SELECTION COMMANDS EDIT, BROWSE, RENAME, DELETE,     *
*           ETC. IT DYNAMICALLY INVOKED THE SPF EDIT AND BROWSE       *
*           FUNCTIONS, AND REQUIRES A PRE-ALLOCATED SYSIN FILE        *
*           WITH THE NAME &USERID.PAN.DATA, AND A PRE-ALLOCATED FILE  *
*           WITH THE NAME &USERID.PAN.LIST. SINCE IT                  *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCES: ISPLINK, PAM                                   *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
PLF      CSECT
         USING *,13,12,11,10
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         ST    R2,8(R13)      LINK SAVE AREAS
         LA    R13,0(,R2)     SET SAVE AREA AND BASE 1
         B     SETBASES
         DC    12F'0'
*
SETBASES DS    0H
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
         XC    0(4,R13),0(13) CLEAR WORD 1 OF SAVE AREA
*
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
         MVI   POPEN,C'0'     PAN NOT OPEN
*
         L     R2,0(,R1)      PICK UP PARM
         LH    R3,0(,R2)      PICK UP PARM LENGTH
         LTR   R3,3           TEST PARM LENGTH
         BZ    EOJ
         BCTR  R3,0           MINUS 1
         EX    R3,PARMOVE
*
*
         BAL   R7,INITFLDS    INITIALIZATION
*
SHOWAGIN DS    0H
         BAL   R7,SHOWDIR     BUILD THE DIRECTORY TABLE
*
         BAL   R7,SHOWSCRN    DISPLAY THE DIRECTORY
*
SHOWDONE DS    0H
*
         CLI   ATABFL,C'0'        NO ACTIVE TABLE
         BE    TSTPOPEN           CHECK PAN OPEN STATUS
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANS),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               OLDNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NEWNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
TSTPOPEN DS    0H
         CLI   POPEN,C'0'     PAN NOT OPEN
         BE    EOJ
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   POPEN,C'0'     PAN NOT OPEN
*
EOJ      DS    0H
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
L4096    DC    F'4096'
PARMSAVE DC    CL79' '
PSPOOL   DC    C'0'
PARMOVE  MVC   PARMSAVE(1),2(R2)
*
         EJECT
INITFLDS DS    0H
         CLC   PARMSAVE(7),=C'P.SPOOL'    IS IT P.SPOOL
         BNE   *+8                        NOPE
         MVI   PSPOOL,C'1'                SET TO PSPOOL
*
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
         XC    ISPARMS(40),ISPARMS
*
* OPEN  PANVALET PROCESSING FOR THIS DDN                              *
*
         SPACE
         CLI   POPEN,C'1'     PAN OPEN
         BNE   OPENPAN        NO, JUST OPEN IT
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   POPEN,C'0'     PAN CLOSED
OPENPAN  DS    0H
         XC    ACTION,ACTION
         L     R15,=V(POPEN)                                        FCI
         LA    R1,@POPEN                                            FCI
         BALR  R14,R15               INVOKE OPEN PANVALET FILE      FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'
         BE    GOODOPN               OK
ABEND1   ABEND 1,DUMP,STEP                                          FCI
GOODOPN  DS    0H
         MVI   POPEN,C'1'     PAN NOW OPEN
         BR    R7
         EJECT
DIR7SAVE DS    F
SHOWDIR  DS    0H
         ST    R7,DIR7SAVE
         CLI   ATABFL,C'0'        NO ACTIVE TABLE
         BE    TABOPENP
*
DLTTABL  DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANS),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               OLDNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NEWNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
TABOPENP DS    0H
*
         MVI   SELCODE,C' '       SET SELECTION CODE TO SPACES
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PCMD,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANS,SELCODE,CHAR,L1),                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               OLDNAMEP,OLDNAME,CHAR,L10),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               NEWNAMEP,NEWNAME,CHAR,L10),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANLINEP,TABAREA,CHAR,L50),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               PLFTABLE,                                               X
               NAMEKEY,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
*
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
DTAILOOP DS    0H
         XC    DIRNUM,DIRNUM
*
         CLI   POPEN,C'1'         IS IT OPEN
         BE    LD#LOOP            YUP
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)                                        FCI
         LA    R1,@POPEN                                            FCI
         BALR  R14,R15               INVOKE OPEN PANVALET FILE      FCI
         MVI   POPEN,C'1'         IS IT OPEN
*
LD#LOOP  DS    0H
         XC    ACTION,ACTION
         MVC   COMMENT,NOENTRY               WE WANT NO COMMENTS
         MVC   SUBSET,NOENTRY                BUT NO SUBSET JUNK
*  SEARCH
         L     R15,=V(PSRCH)
         LA    R1,@PSRCH
         BALR  R14,R15               INVOKE DIR SEARCH FUNCTION
         SPACE 1
         CLC   ACTION,=F'0'        RETURN CODE SHOULD BE ZERO
         BE    TDIREND
ABEND2   ABEND 2,DUMP,STEP
TDIREND  DS    0H
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    LD#EOF              THATS ALL..EXIT
*
         L     R15,DIRNUM          PICK UP THE CURRENT COUNT
         LA    R15,1(,15)          BUMP
         ST    R15,DIRNUM          STORE IT
*
         CLC   DIRNUM,DIRLIM       TEST FOR ENOUGH DIR ENTRIES
         BNL   LD#LIM              THATS ALL..EXIT
*
*  MOVE/FORMAT DIRECTORY FIELDS FOR / DIR DISPLAY                     *
*
         MVC   OLDNAME(10),DNAME
         MVC   PLEVEL(3),DLEVEL
         MVC   PUSER(4),DUSER
         MVC   PTYPE(5),DTYPE
         MVC   PSTATUS(3),DSTATUS
         MVC   PDATEM(8),DDATEM
         MVC   PDATEA(8),DDATEA
         PACK  PACKWK(5),DSTMTS(8)
         MVC   PSTMTS(8),EDIT8
         ED    PSTMTS(8),PACKWK+1
         MVC   PLASTACT(4),DLASTACT
***********************************************************************
*
*   ADD THE DETAIL LINE TO THE TABLE
*
***********************************************************************
*
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         B     LD#LOOP         GO GET NEXT LINE
LD#EOF   DS    0H
*
         MVC   NAME1(10),=CL10'A '   START WITH THIS NAME
         B     LD#RTRN               RETURN
*
LD#LIM   DS    0H
*
         MVC   NAME1(10),DNAME NEXT TIME THRU, START WITH THIS NAME
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE1'
         MVC   TABAREA(77),SPACES   BLANK LINE
         MVI   TABAREA,C'*'
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE2'
         MVC   TABAREA(77),ASTERS   ASTERISK LINE
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE3'
         MVC   TABAREA(77),SPACES   BLANK LINE
         MVI   TABAREA,C'*'
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE4'
         MVC   TABAREA(77),FORMORE  ENTER MORE
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE5'
         MVC   TABAREA(77),SPACES   BLANK LINE
         MVI   TABAREA,C'*'
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         MVC   OLDNAME(10),=CL10'Z99 NOTE6'
         MVC   TABAREA(77),ASTERS   ASTERISK LINE
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
LD#RTRN  DS    0H
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   POPEN,C'0'     PAN CLOSED
         L     R7,DIR7SAVE
         BR    R7               RETURN
DIRNUM   DS    F
DIRLIM   DC    F'100'
ASTERS   DC    77C'*'
FORMORE  DC    CL77' FOR MORE DIRECTORY ENTRIES, ENTER "MORE"'
         EJECT
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
*
SCR7SAVE DS    F
SHOWSCRN DS    0H
         ST    R7,SCR7SAVE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
SHOWTBDS DS    0H
         SR    R15,15
         C     R15,TABCOUNT  ANY LINES ADDED?
         ST    R15,TABCOUNT  SET TO ZERO IN ANY CASE
         BNE   SHOWTABL      YUP, GO SHOW THEM
*
         MVC   OLDNAME(10),=CL10'NO ENTRY '
         MVC   TABAREA(77),ASTERS   ASTERISK LINE
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         SR    R15,15
         ST    R15,TABCOUNT  SET TO ZERO IN ANY CASE
SHOWTABL DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFISP),                                                X
               VL,MF=(E,ISPARMS)
AFTRTBDS DS    0H
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BNE   DUNWITIT       NOPE
GOTSTUFF DS    0H
         MVC   SELSAVE(1),SELCODE    SAVE SEL CODE
         MVC   DNAME(10),OLDNAME     SAVE MEMBER NAME
         MVC   NAME(10),OLDNAME     SAVE MEMBER NAME
         MVI   SELCODE,C' '       SET SELECTION CODE TO SPACES
*
         TR    NEWNAME(10),CAPSONLY    UPPER-CASE ALL INPUT FIELDS
         OI    SELSAVE,C' '        UPPER-CASE IT
*
         CLI   SELSAVE,C' '       IF A SELECTION CODE WAS NOT ENTERED
         BE    GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'B'   IS IT A BROWSE
         BE    PBROWSE        GO DO IT
*
         CLI   SELSAVE,C'S'   IS IT "SELECT"
         BNE   *+12           NOPE
         MVI   SELSAVE,C'B'   SET IT TO BROWSE
         B     PBROWSE        GO DO IT
*
         CLI   SELSAVE,C'E'   IS IT EDIT
         BE    PEDIT          GO DO IT
*
         CLI   SELSAVE,C'D'   IS IT DELETE
         BE    PDELETE        GO DO IT
*
         CLI   SELSAVE,C'F'   IS IT LOCATE
         BE    PLOCATE        GO DO IT
*
         CLI   SELSAVE,C'L'   IS IT LOCATE
         BE    PLOCATE        GO DO IT
*
         CLI   SELSAVE,C'R'   IS IT RENAME
         BE    PRENAME        GO DO IT
*
         CLI   SELSAVE,C'C'   IS IT COPY
         BE    PCOPY          GO DO IT
*
         CLI   SELSAVE,C'U'   IS IT CHANGE USER ID
         BE    PNEWUSER       GO DO IT
*
* ADD TESTS FOR OTHER LINE SELECTION CODES HERE
*
         B     SHOWTBDS       GO REPEAT THE DISPLAY
         TITLE 'PLF - CHANGE USER ID'
PNEWUSER DS    0H
*
         MVC   ULINE+7(10),OLDNAME
         LA    R15,ULINE+8
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12         YUP
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
*
         OC    PUSER(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   1(4,R15),PUSER     MOVE IT IN
         MVI   5(R15),C','  SET THE COMMA
*
         OC    NEWNAME(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   6(4,R15),NEWNAME   MOVE IT IN
*
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTULINE
         PUT   SYSIN,CLINE
*
PUTULINE DS    0H
*
         PUT   SYSIN,ULINE
*
         CLOSE SYSIN
*
         LA    R1,PAN1PARM
         LINK  EP=PAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO REPEAT THE DISPLAY
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFISP,PLF001),                                         X
               VL,MF=(E,ISPARMS)
*
         B     AFTRTBDS       USER ERROR
ULINE    DC    CL80'++USER '
         EJECT
PEDIT    DS    0H
*
         MVC   ENAME(10),OLDNAME
*
         BAL   R7,READPAN
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (EDIT,DSNOTLST),VL,MF=(E,ISPARMS)
*
         OPEN  (CARDIN,(INPUT),PANLIST,(OUTPUT))
*
COPYOUT  DS    0H
         GET   CARDIN,BUFF133
         PUT   PANLIST,BUFF133
         B     COPYOUT
*
COPYBACK DS    0H
*
         CLOSE CARDIN
         CLOSE PANLIST
*
         OPEN  (SYSIN,(OUTPUT),LISTIN,(INPUT))
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTELINE
         PUT   SYSIN,CLINE
*
PUTELINE DS    0H
         PUT   SYSIN,ELINE
*
COPYAGIN DS    0H
         GET   LISTIN,BUFF133
         PUT   SYSIN,BUFF133
         B     COPYAGIN
*
COPYDONE DS    0H
*
         CLOSE LISTIN
         CLOSE SYSIN
*
         LA    R1,PAN1PARM
         LINK  EP=PAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODEDT        GO REPEAT THE DISPLAY
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFISP,PLF004),                                         X
               VL,MF=(E,ISPARMS)
*
         B     AFTRTBDS       USER ERROR
*
GOODEDT  DS    0H
         MVC   NAME1(10),ENAME
         B     DLTTABL
*
BUFF133  DS    CL133
*
ENAME    DC    CL10' '
*
NULLPARM DC    X'80',AL3(H0)
         EJECT
PLOCATE  DS    0H
*
         MVC   NAME1(10),CMDDATA+2
         B     DLTTABL
*
         TITLE 'PLF - RENAME A PAN MEMBER'
PRENAME  DS    0H
*
         MVC   RLINE(9),=CL9'++RENAME'
PRBLANKS DS    0H
         MVC   RLINE+9(10),OLDNAME
         LA    R15,RLINE+10
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
         MVC   1(10,R15),NEWNAME  MOVE IT IN
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTRLINE
         PUT   SYSIN,CLINE
*
PUTRLINE DS    0H
*
         PUT   SYSIN,RLINE
         CLOSE SYSIN
*
         LA    R1,PAN1PARM
         LINK  EP=PAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO REPEAT THE DISPLAY
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFISP,PLF005),                                         X
               VL,MF=(E,ISPARMS)
*
         B     AFTRTBDS       USER ERROR
RLINE    DC    CL80'++RENAME '
         EJECT
PCOPY    DS    0H
*
         MVC   RLINE(9),=CL9'++COPY  '
*
         B     PRBLANKS       GO REPEAT THE DISPLAY
         EJECT
PDELETE  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         MVC   DLINE+14(10),OLDNAME
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTDLINE
         PUT   SYSIN,CLINE
*
PUTDLINE DS    0H
*
         PUT   SYSIN,DLINE
*
         CLOSE SYSIN
*
         LA    R1,PAN2PARM
         LINK  EP=PAN#2
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO RESET THE DIR LIST
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFISP,PLF002),                                         X
               VL,MF=(E,ISPARMS)
*
         B     AFTRTBDS       USER ERROR
*
GOODNAM  DS    0H
         MVC   NAME1(10),OLDNAME
         MVI   NAME1+10,C' '   SET A BLANK
*
         LA    R15,NAME1
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         BCTR  R15,0        BACK UP 1
         BCTR  R15,0        SUBTRACT 1
         MVI   0(R15),C' '  SET A BLANK
         MVI   1(R15),C' '  SET A BLANK
         B     DLTTABL
*
PAN1PARM DC    X'80',AL3(HPAN1)
HPAN1    DC    H'14',C'PANIN,PANPRINT'
*
PAN2PARM DC    X'80',AL3(HPAN2)
HPAN2    DC    H'23',C'PANIN,PANPRINT,,PANDD20'
*
H0       DC    H'0'
*
DLINE    DC    CL80'++DELETE NAME='
*
         EJECT
PBROWSE  DS    0H
*
         BAL   R7,READPAN
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         B     SHOWTABL       GO REPEAT THE DISPLAY
*
*
         EJECT
*
DUNWITIT DS    0H
         L     R7,SCR7SAVE     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         EJECT
*
*    THE COMMAND ENTERED IS IN "CMDDATA"
*
GOTCMD   DS    0H
*
         TR    CMDDATA,CAPSONLY   UPPER-CASE IT
*
         CLI   CMDDATA,C' ' ANY DATA IN COMMAND LINE
         BE    DUNWITIT     NOPE
*
         CLC   CMDDATA(4),=C'MORE'  MORE DIRECTORY ENTRIES
         BE    DLTTABL
*
         CLI   CMDDATA,C'L' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'F' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'S' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'C' CONTROL
         BE    PCONTROL     YUP
*
*     PUT ADDITIONAL COMMAND TESTS HERE
*
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         XC    ISPARMS(40),ISPARMS
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
PCONTROL DS    0H
         MVC   CONTROL(4),CMDDATA+1
         B     SHOWTABL       GO REPEAT THE DISPLAY
         SPACE 5
         EJECT
INITMSG  MVI   TABAREA,C' '   BLANK OUT THE
         MVC   TABAREA+1(79),TABAREA MESSAGE BUFFER
         BR    R7             RETURN TO OUR CALLER
         SPACE 5
         EJECT
*
*  READ THE PAN MEMBER
*
         SPACE
READPAN  DS    0H
*
         ST    R7,KEEP7PAN
*
         CLI   SELSAVE,C'B'        IS IT BROWSE
         BE    RDBROWSE            YUP, NO SPECIAL HANDLING
*
         CLI   SELSAVE,C'E'        IS IT EDIT
         BNE   DUNWITIT            NOPE, I SHOULDN'T BE HERE
*
         XC    ACTION,ACTION
         MVC   NAME(10),DNAME      MOVE SEARCHED NAME TO READ NAME
         LA    R7,SYSIN           DCB ADDRESS
         ST    R7,ADCBADD
*
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BNO   DUNWITIT            NOPE, FORGET IT
*
         MVC   ELINE+9(40),SPACES  CLEAR IT
         MVC   ELINE+9(10),DNAME   MOVE SEARCHED NAME TO UPDATE CARD
         LA    R14,ELINE+10        SET START OF LOOP
         LA    R15,9               SET LOOP CONTROL
*
UPDLOOP  DS    0H
         CLI   0(R14),C' '  GOT A BLANK
         BE    UPDMOVE      MOVE IN THE STUFF
         LA    R14,1(,14)   BUMP
         BCT   R15,UPDLOOP  LOOP
*
UPDMOVE  DS    0H
*
         MVI   0(R14),C','
         MVC   1(3,R14),PLEVEL       MOVE IN OLD LEVEL NUMBER
         MVC   4(4,R14),=C',ALL'
*
*
         B     READAGN
*
ELINE    DC    CL80'++UPDATE '
*
RDBROWSE DS    0H
*
         XC    ACTION,ACTION
         MVC   NAME(10),DNAME      MOVE SEARCHED NAME TO READ NAME
         LA    R7,SYSIN           DCB ADDRESS
         MVC   DSNOTLST+4(4),=CL4'DATA'
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    OPENOUT             JUST OPEN IT
         LA    R7,PANLIST         DCB ADDRESS
         MVC   DSNOTLST+4(4),=CL4'LIST'
OPENOUT  DS    0H
         ST    R7,ADCBADD
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BO    READAGN            YUP
ABEND3   ABEND 3,DUMP,STEP
READAGN  DS    0H
         CLI   POPEN,C'1'         IS IT OPEN
         BE    READAPAN           YUP
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)                                        FCI
         LA    R1,@POPEN                                            FCI
         BALR  R14,R15               INVOKE OPEN PANVALET FILE      FCI
         MVI   POPEN,C'1'     PAN NOW OPEN
*
         CLC   ACTION,=F'0'
         BE    READAPAN            RETURN CODE SHOULD BE ZERO
ABEND4   ABEND 4,DUMP,STEP
*
READAPAN DS    0H
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES FCI
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS FCI
         L     R15,=V(PREAD)                                        FCI
         LA    R1,@PREAD                                            FCI
         BALR  R14,R15             GO READ A RECORD VIA PAM         FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'
         BE    GOODREAD            RETURN CODE SHOULD BE ZERO
         CLC   ACTION,=F'4'        4 IS PROBABLY "INCLUDE NOT FOUND"
         BE    GOODREAD
ABEND5   ABEND 5,DUMP,STEP
GOODREAD DS    0H
         CLC   RECORD(2),=C'$*'    TEST FOR END OF DATA
         BE    ENDREAD
*
*PROCESS A RECORD
*
         CLI   PSPOOL,C'1'         PANSPOOL FILE
         BE    PUTSPL              READ AGAIN
         PUT   (7),RECORD
         B     READAGN
PUTSPL   DS    0H
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   PSPREC(80),RECORD
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES FCI
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS FCI
         L     R15,=V(PREAD)                                        FCI
         LA    R1,@PREAD                                            FCI
         BALR  R14,R15             GO READ A RECORD VIA PAM         FCI
         SPACE 1                                                    FCI
         CLC   ACTION,=F'0'
         BE    GOTPSPL             RETURN CODE SHOULD BE ZERO
ABEND6   ABEND 6,DUMP,STEP
GOTPSPL  DS    0H
         MVC   PSPREC+80(53),RECORD+27
         PUT   (7),PSPREC
         B     READAGN
PSPREC   DC    CL133' '
ENDREAD  DS    0H
         CLOSE ((7))
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         CLC   ACTION,=F'0'
         BE    ENDREAD2            RETURN CODE SHOULD BE ZERO
ABEND7   ABEND 7,DUMP,STEP
ENDREAD2 DS    0H
         MVI   POPEN,C'0'     PAN NOT OPEN
*
         L     R7,KEEP7PAN
         BR    R7
DSNOTLST DC    CL44'PAN.DATA'
KEEP7PAN DS    F
ADCBADD  DS    F
         EJECT
*
***********************************************************************
*
*   ADD A LINE TO THE TABLE
*
***********************************************************************
*
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   TABAREA,C' '
         MVC   TABAREA+1(79),TABAREA
         L     R15,TABCOUNT  PICK UP TABLE ENTRY COUNT
         LA    R15,1(,15)    ADD 1
         ST    R15,TABCOUNT  SET IT
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'MISCELLANEOUS CONSTANTS'
         LTORG
*
         DS    0F
TABCOUNT DC    F'0'
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
OUT7HOLD DS    F
L1       DC    F'1'
L10      DC    F'10'
L50      DC    F'50'
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
PACKWK   DS    PL5
POPEN    DC    C'0'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CLINE    DC    CL10'++CONTROL '
CONTROL  DC    CL70'          '
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
PLFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(PLFOPT)
SELECTR  DC    CL8'SELECT  '
PLFOPT   DC    CL8'PLFOPT  '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
L3       DC    F'3'
L77      DC    F'77'
L20      DC    F'20'
L40      DC    F'40'
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
BROWSE   DC    C'BROWSE   '
EDIT     DC    C'EDIT     '
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSCAN   DC    C'TBSCAN   '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
         DC    C'PANS     '     SELECTION CODE
         DC    C'OLDNAME '      MEMBER NAME
         DC    C'NEWNAME '      BLANK
         DC    C'PANLINE )'     JOB INFO
PANS     DC    C'(PANS    )'    SELECTION CODE
NAMEKEY  DS    0CL10            OLD NAME IS USED AS A KEY
OLDNAMEP DC    C'(OLDNAME )'    MEMBER NAME
NEWNAMEP DC    C'(NEWNAME )'    NEW NAME FOR RENAME OPTION
PANLINEP DC    C'(PANLINE )'    MEMBER INFO
*
PCMD     DC    C'(PCMD    )'    MEMBER INFO
*
SPACES   DC    CL80' '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
PLFISP   DC    C'PLFISP  '
PLFTABLE DC    C'PLFTABLE'
*
         DS    0D
         DC    C'COMMAND '
CMDDATA  DC    CL40' '
*
OLDNAME  DC    CL10' '
NEWNAME  DC    CL10' '
*
PLF001   DC    CL80'PLF001 '
PLF002   DC    CL8'PLF002'
PLF003   DC    CL8'PLF003 '
PLF004   DC    CL8'PLF004 '
PLF005   DC    CL8'PLF005 '
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
TABAREA  DS    0CL77
PLEVEL   DC    CL3' '
         DC    C' '
PUSER    DC    CL4' '
         DC    C' '
PTYPE    DC    CL5' '
         DC    C' '
PSTATUS  DC    CL3' '
         DC    C' '
PDATEM   DC    CL8' '
         DC    C' '
PDATEA   DC    CL8' '
         DC    C' '
PSTMTS   DC    CL8' '
         DC    C' '
PLASTACT DC    CL4' '
         DC    CL30' '  PAD OUT TO 80 BYTES
*
*
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
*.....................................................................*
*  LIST FORM OF MACROS                                                *
*.....................................................................*
         SPACE
@POPEN   CALL  ,(ACTION,DDNAME,BACKUP),VL,MF=L                      FCI
*
*  POPEN PARAMETERS
*
DDNAME   DC    CL8'PANDD1  '     DEFAULT
BACKUP   DC    CL8'NO-ENTRY'     MUST BE CL8'BACKUP' IF PROTECTION FIL
         SPACE
@PSRCH   CALL ,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL,MF=L FCI
         SPACE
@PREAD   CALL  ,(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL,MF=L       FCI
*
*  SHARED PARAMETER
         DS    0D
         DC    CL8'ACTION'
ACTION   DC    F'0'         RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
         DS    0D
         DC    CL8'RECORD'
RECORD   DS    CL80                STATMENT WILL BE RETURNED HERE
         DS    0D
         DC    CL8'NAME  '
NAME     DC    CL22'A          '   PREAD NAME
         DS    0D
         DC    CL8'INCLUDES'
INCLUDES DS    CL8
         DS    0D
         DC    CL8'COMMENT '
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
*
@PCLOSE  CALL  ,(ACTION),VL,MF=L                                    FCI
         EJECT
*.....................................................................*
*  CONSTRUCTS FOR PANVALET                                            *
*.....................................................................*
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    CL3                 LEVEL NUMBER
DUSER    DS    CL4                 USER CODE
DSECURE  DS    CL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    CL5                 NO. OF BLOCKS
DSTMTS   DS    CL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD
DBYTES   DS    CL2                 NO. BYTES PER STATMENT
DSUBSET  DS    CL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
         DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
         EJECT
NAME1    DC    CL22'A          '   SEARCH NAME1
NAME2    DC    CL11'Z999999999 '   SEARCH NAME2
**
* NOTE  COMMENTS WILL BE PROCEDED AND ENDED WITH AN *
**
SUBSET   DC    CL27'NO-ENTRY'      NO SUBSETS WANTED, ELSE='SUBSET'
**
*  NOTE  IF SUBSETS ARE REQUESTED THE FOLLOWING WILL BE A DEFINITION OF
*        THE SUBSET DIRECTORY ENTRY
**
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    CL5                 # OF STMTS IN SUBSET
         SPACE 2                                                    FCI
**
* SPECIAL RETURN CODES FROM EACH CALL TO PAM WILL BE RETNED IN ACTION
*   2    NAME NOT ALPHANUMERIC (SAME AS PV002)
*   4    ++INCLUDE NOT FOUND (PREAD ONLY)
*   7    PARAMETER TO LONG (SAME AS PV007)
*   23   NAME NOT FOUND (SAME AS PV023)
*   245  PARAMETER LIST ERROR IN CALL TO PAM
**
NOENTRY  DC    CL92'NO-ENTRY'            NO-ENTRY FOR PANVALET      FCI
BLANKS   EQU   NOENTRY+8,80              BLANKS FOR US              FCI
         EJECT
*.....................................................................*
*  OVERLAY MAPPING OF DIRECTORY LINE FORMAT ON SCREEN                 *
*.....................................................................*
         SPACE
MDIR     DS    0CL80     MAP FOR NICE FORMATTED DIR ENTRY           FCI
MDNAME   DS    CL10,CL1                                             FCI
SCANSTRT EQU   *                                                    FCI
MDLEVEL  DS    CL3,CL1                                              FCI
MDTYPE   DS    CL5,CL1                                              FCI
MDUSER   DS    CL4,CL1                                              FCI
MDSECURE DS    CL1,CL1                                              FCI
MDSTATUS DS    CL3,CL1                                              FCI
MDMAINT  DS    CL8,CL1                                              FCI
MDACCES  DS    CL8,CL1                                              FCI
MDSTMTS  DS    CL8,CL2                                              FCI
MDBLOCKS DS    CL5,CL1                                              FCI
SCANLEN  EQU   *-SCANSTRT                                           FCI
MDLSTPRD DS    CL1                                                  FCI
MDLSTACT DS    CL8                                                  FCI
CAPSONLY DC    CL64' '
         DC    CL10' ',C'Ö.<(+×&&',CL9' ',C'!$*);^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         EJECT
PANLIST  DCB   DDNAME=PANLIST,DSORG=PS,MACRF=PM
LISTIN   DCB   DDNAME=PANLIST,DSORG=PS,MACRF=GM,EODAD=COPYDONE
SYSIN    DCB   DDNAME=PANIN,DSORG=PS,MACRF=PM
CARDIN   DCB   DDNAME=PANIN,DSORG=PS,MACRF=GM,EODAD=COPYBACK
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.SERV.LOAD
//         DD   DISP=SHR,DSN=P.FIXDHEAD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=SYS2.LINKLIB
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  INCLUDE SYSLIB(PAM)
  NAME PLF(R)
/*
