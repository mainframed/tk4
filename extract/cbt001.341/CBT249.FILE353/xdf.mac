//BCOOKXDF  JOB (X0002,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=T,NOTIFY=BCOOK
/*JOBPARM L=999
//*******************************************************************
//*                                                                **
//* REF TECH.SERV.BCOOK(XDF)                                       **
//*                                                                **
//*******************************************************************
//ASM1    EXEC PGM=IEV90,REGION=1024K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR,
//  UNIT=SYSDA,VOL=SER=TSTRES
//         DD  DSN=SYS1.AMODGEN,DISP=SHR,
//  UNIT=SYSDA,VOL=SER=TSTRES
//         DD  DSN=SYS1.HASPSRC,DISP=SHR,
//  UNIT=SYSDA,VOL=SER=TSTRES
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
***********************************************************************
*                                                                     *
*                                                                     *
*               EXECUTION DISPLAY FACILITY (XDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY STATUS OF JES2 JOBS, LOGONS, & STARTED TASKS.     *
*                                                                     *
*                                                                     *
* FUNCTION: XDF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A DISPLAY OF THE         *
*           ACTIVITY IN THE SYSTEM, SIMILAR TO RMF, BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           XDF LOOKS AT VARIOUS MVS SP 1.3.0 CONTROL BLOCKS.         *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
*  GLOSSARY OF TERMS FOR OUTPUT DISPLAY:
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS WILL ALWAYS BE INIT.
*              FOR TSO JOBS AND STARTED TASKS WILL BE ACTUAL STEP NAME.
*    P         REASON CODE FOR SWAP OUT FROM OUCBPFL.
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*                U  0A MEMORY WAS UNILATERALLY SWAPPED
*                X  09 MEMORY WAS EXCHANGE SWAPPED
*                E  08 AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY
*                S  07 MEMORY REQUESTED SWAP OUT
*                D  06 INVOLUNTARY SWAPOUT DUE TO DETECTED WAIT
*                R  05 SWAPOUT CAUSED BY REAL STORAGE SHORTAGE
*                A  04 SWAPOUT CAUSED BY AUX. STORAGE SHORTAGE
*                L  03 MEMORY SWAPPED OUT BY MS6 LONG WAIT DETECTION
*                I  02 MEMORY IS IN INPUT TERMINAL WAIT
*                T  01 MEMORY IS IN OUTPUT TERMINAL WAIT
*                ?     UNABLE TO DETERMINE SWAPOUT REASON.
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    T         TYPE OF MEMORY (S=STC OR BATCH, T=TSO USER)
*    LOC       CURRENT LOCATION OF THIS MEMORY:
*                IN  SWAPPED IN AND ELIGIBLE TO RUN.
*                OUT SWAPPED OUT BUT READY TO RUN.
*                WT  SWAPPED OUT AND NOT READY TO RUN.
*                N/S SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.
*                <-> TRANSITIONING BETWEEN STATES.
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    WMR       CURRENT SRM WORKLOAD MANAGER RECOMMENDATION VALUE.
*              (ACTUALLY THE WORKLOAD LEVEL AT WHICH THIS MEMORY IS
*               OPERATING).
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
*
*
*
*
***********************************************************************
         EJECT
***********************************************************************
*
*  CHANGE LOG
*
*  9/13/83  ADDED RCT DSECT TO ACCESS SP1.3 SRM FIELDS FOR DISPLAY
*           HEADING. - DJS
*
*  9/18/83  ADD PROCSTEP TO DISPLAY, ADDED PROVISION TO HIGH-LIGHT   BC
*           DETAIL LINES. (MOVE X'03' TO "HIGHLITE".)
*
*  9/21/83  COMBINED MVS/SP1.3 SYSTEM TASKS WITH THE *MASTER* TASK.
*           HIGHLIGHTED SWAPPED IN TASKS. - DJS
*
*  3/22/84  ADDED OPTIONS "ALL", "BAT", "STC", "TSO" IN THE COMMAND
*           LINE TO LIMIT/EXPAND DISPLAY. - BC
*
*  3/23/84  ADDED ASID, DELETED WORKLOAD MANAGER RECOMMENDATION, ADDED
*           CPU TIME CHANGED FIELD. ADDED JOB-LEVEL COMMANDS FOR
*           "CANCEL", "FORCE", AND "RESET".                          BC
*
***********************************************************************
         EJECT
XDF      CSECT
         USING *,13,12,11,10
         B     72(,15)
         DC    17F'0'
         SAVE  (14,12)
         LA    R2,0(,R15)     A(XDFST)
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
         XC    ISPARMS(40),ISPARMS
         MVC   XDFCMD(3),=C'ALL'    INITIAL COMMAND - SHOW EVERYTHING
         MVC   SELCMD(3),=C'ALL'    INITIAL COMMAND - SHOW EVERYTHING
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,U8,USERID,SPFMOVE),                        X
               VL,MF=(E,ISPARMS)
*
SHOWAGIN DS    0H
         BAL   R7,SHOWRMF     DISPLAY THE RESOURCE STUFF
*
SHOWDONE DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               ACTLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CMDLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PIKP),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         RETURN (14,12),RC=0  RETURN TO OUR CALLER
*
L4096    DC    F'4096'
*
         EJECT
SHOWRMF  DS    0H
         ST    R7,XDF7HOLD SAVE THE RETURN ADDRESS
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   ATABFL,C'0'        DO I HAVE AN ACTIVE TABLE?
         BE    ANOTAB             NOPE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               ACTLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               CMDLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PIKP),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
ANOTAB   DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               ACTLINEP,TABAREA,CHAR,L78),                             X
               VL,MF=(E,ISPARMS)
*
         MVC   XDFCMD(3),SELCMD      SET COMMAND
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               CMDLINEP,XDFCMD,CHAR,L40),                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PIKP,SELCODE,CHAR,L1),                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               XDFTABLE,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
*
FINDCVT  DS    0H
*
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
*
BUILDHDR DS    0H
         L     R4,16           ---> CVT...
         L     R7,356(R4)      ---> PVT...
***********************************************************************
*
*   FILL IN SYSTEM CPU UTILIZATION
*
***********************************************************************
         L     R4,604(R4)      ---> RMCT... 1ST MODULE OF IRARMCNS
         L     R5,4(R4)        ---> IRACCT
         LH    R6,102(R5)         CPU UTILIZATION
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRCPU,WORK+6(2)     ...AND EDIT
         SPACE
***********************************************************************
*
*   FILL IN AVERAGE AVAILABLE FRAME COUNT
*
***********************************************************************
         LH    R6,2(R7)           AVERAGE AVAILABLE FRAME COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAFC,WORK+6(2)     ...AND EDIT
         SPACE
***
*   THE FOLLOWING WAS ADDED TO PROVIDE ADDRESSABILITY TO MVS/SP1.3
*   SRM RCT FIELDS
***
         ST    R4,R4SAV           SAVE R4
         USING RMCT,R4            ADDRESS RMCT
         L     R4,RMCTRCT         LOAD RCT ADDRESS
         USING RCTDSECT,R4        ADDRESS RCT
***
*
***
***********************************************************************
*
*   FILL IN PAGE FAULT RATE
*
***********************************************************************
         LH    R6,RCVDPR          PAGE FAULT RATE
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRAPR,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,RCVASMQA        AUX. STORAGE MANAGER QUE LENGTH
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRASQ,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,RCVUICA         UNREFERENCED INTERVAL COUNT
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRUIC,WORK+6(2)     ...AND EDIT
         SPACE
         LH    R6,RCVAVQC         DEFERRED IO REQUESTS
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  HDRDIO,WORK+6(2)     ...AND EDIT
         L     R4,R4SAV        RESTORE R4
         SPACE
         L     R7,RMCTDMDT(,R4) ---> DMDT..     R4--> RMCT
         SR    R15,R15         CLEAR A REG
         LA    R5,3            NUMBER OF DOMAIN ENTRIES TO PROCESS
         LA    R14,HDRDMN      POINT TO FIRST DOMAIN ENTRY
         USING DMDT,R7
         LA    R7,DMDTLEN(R7)  BUMP PAST FIRST DOMAIN
DODMNS   DS    0H
*        IC    R15,0(R7)       GET DOMAIN NUMBER
*        CVD   R15,WORK        CHANGE IT TO DECIMAL
*        OI    WORK+7,X'0F'    FIX THE SIGN
*        UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LH    R15,DMDTMPLT    GET MULTI-PROGRAMMING LEVEL TARGET
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
*        LH    R15,DMDTRUA     GET NUMBER OF READY USERS
         LH    R15,DMDTCMPL    GET CURRENT MPL
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  4(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LA    R14,6(R14)     POINT TO NEXT DOMAIN ENTRY
         LA    R7,DMDTLEN(R7)  BUMP PAST THIS DOMAIN
         BCT   R5,DODMNS       GO PROCESS ANOTHER
         SPACE
         MVC   TABAREA(78),SYSHDR   MOVE IN SCREEN HEADING
         BAL   R7,OUTMSG            WRITE IT
         MVC   TABAREA(78),HEAD1    MOVE IN SCREEN HEADING
         BAL   R7,OUTMSG            WRITE IT
         MVI   WHICH,C'S'       DO STARTED TASKS FIRST
TOPASCB  DS    0H
         L     R2,16            LOAD PTR TO CVT.
         L     R2,556(,R2)      TO ASVT
         L     R3,516(,R2)      MAXIMUM NUMBER OF ENTRIES
         LA    R4,524(,R2)      FIRST ENTRY MINUS 4.
         SPACE
ASCBLOOP DS    0H
         LA    R4,4(,R4)        BUMP BY FOUR.
         ICM   R5,B'1111',0(R4) LOAD THE ASCB POINTER
***********************************************************************
*
*   SET R5 = ASCB
*
         USING ASCB,5
***********************************************************************
         BM    NOGOOD           BRANCH IF BAD PTR.
*
         LH    R1,ASCBASID      PICK UP ASID
         CVD   R1,DOUBLEWD
         MVC   WORK6(6),EDIT6
         ED    WORK6(6),DOUBLEWD+5
*
*        TM    ASCBRCTF,ASCBOUT TEST FOR SWAPPED OUT.
         L     R6,ASCBOUCB       LOAD PTR TO OUCB FOR QUICK TSO CHK
***********************************************************************
*
*   SET R6 = OUCB
*
         USING OUCB,6
***********************************************************************
         TM    OUCBYFL,OUCBLOG  CHECK FOR TSO.
         BO    ITISATSO         YES, IT IS TSO.
         CLC   ASCBJBNI(4),FZEROS  JOBNAME POINTER FOR BATCH JOBS
         BNE   ITISABAT         BRANCH IF ZERO POINTER.
         CLC   ASCBJBNS(4),FZEROS  JOBNAME POINTER FOR STARTED TASKS
         BNE   ITISASTC         BRANCH IF STARTED TASK.
         B     NOGOOD           NO, SO SKIP THIS.
ITISABAT DS    0H
         MVC   ASID(4),WORK6+2  SET ASID
         CLI   WHICH,C'B'       AM I DOING BATCH
         BNE   NOGOOD           NO, SO SKIP THIS.
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   SELCMD(3),=C'BAT'     DISPLAY BATCH
         BE    DISPALL          YES
         B     NOGOOD           NO, SO SKIP THIS.
ITISATSO DS    0H
         MVC   PSTEP(8),=CL8'TSU'   SET DEFAULT TSO PROCSTEPNAME
         CLI   WHICH,C'T'       AM I DOING TSO
         BNE   NOGOOD           NO, SO SKIP THIS.
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPTSO          YES
         CLC   SELCMD(3),=C'TSU'     DISPLAY TSO
         BE    DISPTSO          YES
         B     NOGOOD           NO, SO SKIP THIS.
ITISASTC DS    0H
         CLI   WHICH,C'S'       AM I DOING STARTED TASKS
         BNE   NOGOOD           NO, SKIP THIS
         CLC   SELCMD(3),=C'ALL'     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   SELCMD(3),=C'STC'     DISPLAY STARTED TASKS
         BE    DISPALL          YES
         B     NOGOOD           NO, SO SKIP THIS.
DISPALL  DS    0H
         MVC   PSTEP(8),SPACES
DISPTSO  DS    0H
         MVC   STEP(8),SPACES
         SPACE
         MVC   JOB(8),=CL8'STARTING' TO BYPASS GARBAGE
         ICM   R6,B'1111',172(R5) LOAD PTR TO JOBNAME.
         BZ    NOTJOB           BRANCH IF ZERO POINTER.
         MVC   JOB,0(R6)        OTHERWISE, MOVE IN JOBNAME.
         MVC   PSTEP(8),X'18'(R6)  MOVE IN PROC STEP NAME       KMT
         MVC   STEP(8),X'38'(R6)  MOVE IN STEP NAME (NO PSTEP)  KMT
         B     STEPOK                                           KMT
NOTJOB   ICM   R6,B'1111',176(R5) PTR IF S/M/L.
         BZ    JOBOK            BYPASS THIS MOVE IF POINTER IS ZERO.
***
*  THE FOLLOWING WAS ADDED TO COMBINE MVS/SP1.3 SYSTEM TASKS WITH
*  THE *MASTER* TASK
***
MMASTER  CLC   =CL8'*MASTER*',0(R6)  *MASTER* TASK
         BNE   MPCAUTH          NO - CHECK NEXT
         MVC   ASID(4),WORK6+2  SET ASID
         XC    MASTFLG,MASTFLG  CLEAR MASTFLG
         XC    MASTFMCT,MASTFMCT CLEAR FRAME COUNT ACCUMULATOR
         XC    MASTWSS,MASTWSS  CLEAR WORKING SET SIZE ACCUMULATOR
         XC    MASTCPU,MASTCPU  CLEAR CPU ACCUMULATOR
         OI    MASTFLG,X'01'    TURN ON *MASTER* PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MPCAUTH  CLC   =CL8'PCAUTH  ',0(R6)  PCAUTH TASK
         BNE   MGRS             NO - CHECK NEXT
         OI    MASTFLG,X'02'    TURN ON PCAUTH PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MGRS     CLC   =CL8'GRS     ',0(R6)  GRS TASK
         BNE   MDUMPSRV         NO - CHECK NEXT
         OI    MASTFLG,X'04'    TURN ON GRS PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MDUMPSRV CLC   =CL8'DUMPSRV ',0(R6)  DUMPSRV TASK
         BNE   MALLOCAS         NO - CHECK NEXT
         OI    MASTFLG,X'08'    TURN ON DUMPSRV PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MALLOCAS CLC   =CL8'ALLOCAS ',0(R6)  ALLOCAS TASK
         BNE   MCONSOLE         NO - CHECK NEXT
         OI    MASTFLG,X'10'    TURN ON ALLOCAS PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MCONSOLE CLC   =CL8'CONSOLE ',0(R6)  CONSOLE TASK
         BNE   NOTMAST          NO - NOT SYSTEM TASK
         OI    MASTFLG,X'20'    TURN ON CONSOLE PROCESSED FLAG
         B     MPROCESS         PROCESS FOR SYSTEM TASK
MPROCESS ST    R4,R4SAV         SAVE ASCB POINTER
         BAL   R4,MASTPROC      PROCESS SYSTEM TASKS
         L     R4,R4SAV         RESTORE ASCB POINTER
         BCT   R3,ASCBLOOP      PROCESS NEXT ASCB
         B     GOTSOMEJ         TABLE DISPLAY IF DONE
NOTMAST  EQU   *
***
*
***
         MVC   ASID(4),WORK6+2  SET ASID
         MVC   JOB,0(R6)        MOVE IN JOBNAME FOR S/M/L MEMORIES.
JOBOK    DS    0H
         SPACE
         ICM   R7,B'1111',ASCBCSCB    POINTER TO THE TOP CSCB.
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         CLI   4(R7),X'04'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         CLI   4(R7),X'0C'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         MVC   STEP(8),16(R7)   PICK UP CHCLS.
         B     STEPOK           BYPASS FURTHER CSCB PROCESSING.
STORMT   MVC   STEP(8),8(R7)    PICK UP CHKEY.
STEPOK   DS    0H
         CLC   JOB(8),=CL8'INIT' IS IT A 'DRY' INIT??           KMT
         BE    NOGOOD           YEP...KISS OFF                  KMT
         SPACE
         LH    R6,ASCBFMCT       LOAD ALLOC FRAME COUNT.
         SLL   R6,2             MULTIPLY BY FOUR.
         CVD   R6,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
         SPACE
***********************************************************************
*
*   SET R6 = OUCB, AGAIN
*
***********************************************************************
         SPACE
***********************************************************************
*
*   FILL IN WORKING SET SIZE
*
***********************************************************************
         L     R6,ASCBOUCB      PTR TO OUCB.
         LH    R7,OUCBWSS        LOAD OUCBWSS
*        SLL   R7,2             TIMES FOUR FOR K.
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWSS(6),=X'202020212040' MOVE IN THE EDIT MASK.
         ED    LINEWSS-1(6),WORK+5 EDIT THE RESULT.
         SPACE
         TM    18(R6),X'20'     TEST FOR TSO MEMORY
         BZ    NOTTSO           BRANCH IF NOT TSO.
         MVI   TYPE,C'T'        MOVE IN TSO MEMORY INDICATION.
         B     TYPEOK           BYPASS REST OF TYPE PROCESSING.
NOTTSO   MVI   TYPE,C'S'        MOVE IN S/M/L INDICATION.
TYPEOK   DS    0H
         SPACE
         MVC   WHERE,=CL3'IN'   DEFAULT MEMORY POSITION IS IN.
         MVI   HIGHLITE,X'03'   HIGHLIGHT AS SWAPPED IN
         TM    17(R6),X'80'     TEST FOR NON-SWAP
         BZ    TRYWAIT          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  DS    0H
         TM    16(R6),X'08'     CHECK FOR WAIT
         BZ    TRYOUT           BRANCH IF NOT.
         MVC   WHERE,=CL3'WT'   MOVE IN WAIT POSITION INDICATION.
         MVI   HIGHLITE,X'40'   TURN OFF HIGHLIGHT
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    16(R6),X'04'     TRY OUT QUEUE
         BZ    TRYTRIV          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         MVI   HIGHLITE,X'40'   TURN OFF HIGHLIGHT
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYTRIV  TM    16(R6),X'02'     CHECK FOR TRIVIAL QUEUE POSITION.
         BZ    WHEREOK          IF NOT, EXIT POSITION PROCESSING.
         MVC   WHERE,=CL3'TRV'  MOVE IN TRIVIAL QUEUE POSITION.
WHEREOK  DS    0H
         TM    16(R6),X'E0'     CHECK FOR TRANSITIONING STATUS
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
NOTTRANS DS    0H
         SPACE
         SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,43(,R5)       LOAD ASCBDP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,43(,R5)       LOAD THE DISPATCHING PRIORITY AGN.
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
         SPACE
***********************************************************************
*
*   FILL IN PERFORMANCE GROUP NUMBER
*
***********************************************************************
         SR    R7,R7            PREPARE FOR THE IC.
         IC    R7,OUCBSPG+1     NEW PGN  ???
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINEPGN(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINEPGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         SPACE
***********************************************************************
*
*   FILL IN SWAP COUNT
*
***********************************************************************
         LH    R7,OUCBSWC       LOAD OUCBSWC (SWAP COUNT)
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINESWC(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINESWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         SPACE
***********************************************************************
*
*   FILL IN SWAP REASON CODE
*
***********************************************************************
         TM    OUCBQFL,OUCBLSW  SEE IF OUT SOMEHOW
         BZ    PFLDONE          MUST BE IN.
CHKSRC   DS    0H
         CLI   OUCBSRC,X'0A'    OUCBSRC (SWAP REASON CODE)
         BH    UNKNOWN          TOO BIG REASON UNKNOWN
         SR    R7,R7            CLEAR OUT WORK REGISTER        KMT
         IC    R7,OUCBSRC       GET OUCBSRC (SWAP REASON CODE) KMT
         SLL   R7,2             MULTIPLY BY 4 TO GET INDEX     KMT
         LA    R7,REASONS(R7)   GET ADDRESS OF REASON CODE     KMT
         MVC   STATUS,0(R7)     MOVE IT INTO THE MESSAGE       KMT
         B     PFLDONE                                         KMT
UNKNOWN  MVC   STATUS,=CL4'????'       SORRY..DONT KNOW        KMT
PFLDONE  DS    0H
         SPACE
***********************************************************************
*
*   FILL IN SERVICE UNITS
*
***********************************************************************
         L     R7,OUCBWMS       OUCBWMS LOAD SERVICE UNITS THIS XACTION
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWMS(7),=X'20202020202120' MOVE IN THE EDIT MASK.
         ED    LINEWMS-1(8),WORK+4 EDIT THE RESULT.
         SPACE
***********************************************************************
*
*   FILL IN CPU TIME
*
***********************************************************************
NOWMS    LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET HUNDREDTHS AND MICROS
         LM    10,R11,200(R5)   LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS AND MICROS
         LTR   R9,R9            TEST JST CPU TIME
         BP    TESTSRB          IF ITS POISTIVE GO ON
         NOPR  R9               DUMMY INSTR FOR TSO TEST TO STOP ON
TESTSRB  LTR   R11,R11          TEST SRB CPU TIME
         BP    ADSRBJST         IF ITS OK GO ADD SRB AND JST CPU TIME
         NOPR  R9               ANOTHER DUMMY INSTR.
ADSRBJST AR    R9,R11           ADD TOGETHER.
         CVD   R9,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
*
         MVI   CPUFLAG,C' '     DEFAULT
*
*
*  NOW LOOP, CHECKING FOR THE JOBS WHOSE CPU TIME HAS INCREASED SINCE
*            THE LAST XDF DISPLAY
*
         LA    R7,CPUTAB        START OF TABLE
CPULOOP1 DS    0H
         CLI   0(R7),X'FF'      END OF TABLE
         BE    JUSTOUT1         YES, IGNORE
*
         CLC   0(33,R7),SPACES  UNUSED ENTRY?
         BE    CPUADD1          YES, ADD THIS JOB TO TABLE
*
         CLC   0(31,R7),ASID    MATCH ON ASID, JOBNAME, STEPNAME, PSTEP
         BE    CPUMATCH         YES, COMPARE CURRENT CPU TIME VS LAST
*
         LA    R7,33(,7)        BUMP TO NEXT ENTRY
         B     CPULOOP1         KEEP LOOPING
*
CPUMATCH DS    0H
         CLC   31(2,R7),CPUTIME+3       CHECK CPU WHOLE SECONDS
         BE    JUSTOUT1         IF THEY ARE THE SAME, DON'T SET FLAG
         MVC   31(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
         MVI   CPUFLAG,C'*'     INDICATE CPU TIME HAS INCREASED
         B     JUSTOUT1         ADD THE JOB TO THE SPF TABLE
*
CPUADD1  DS    0H
         MVC   0(31,R7),ASID    MOVE IN ASID, JOBNAME, STEPNAME, PSTEP
         MVC   31(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
*
JUSTOUT1 DS    0H
         BAL   R7,OUTMSG        ADD THE LINE TO THE SPF TABLE.
*
NOGOOD   BCT   R3,ASCBLOOP      BRANCH BACK TO LOOK AT NEXT MEMORY.
*
         CLI   WHICH,C'T'       FINISHED TSO?
         BE    GOTSOMEJ         YES, GO DISPLAY THE TABLE
*
         CLI   WHICH,C'S'       FINISHED STARTED TASKS
         MVI   WHICH,C'B'       SET DO BATCH NEXT
         BE    TOPASCB          YES, GO RE-LOOP THROUGH THE ASCB'S
*
         MVI   WHICH,C'T'       IF I'M NOT DOING STARTED TASKS OR TSO,
         B     TOPASCB          GO RE-LOOP THROUGH THE ASCB'S
*
         EJECT
EODAD    DS    0H
BYEBY    DS    0H
*
GOTSOMEJ DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               XDFTABLE,                                               X
               XDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
         MVC   TABWORK(80),TABAREA   SAVE REPLY FIELDS
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BE    GOTSTUFF       YUP, GO PROCESS IT
         B     DUNWITIT       ALL DONE
GOTSTUFF DS    0H
         MVC   SELSAVE(3),SELCODE    SAVE SEL CODE
         OC    SELSAVE(3),SPACES   UPPER-CASE IT
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
         MVC   JOBASID(4),TABWORK+1  SAVE ASID
         MVC   JOBNAMS(8),TABWORK+6  SAVE JOB NAME
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBGET,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVC   TABGOTN(80),TABAREA   SAVE REPLY FIELDS
         C     R15,F8         RC=8 MEANS CRP IS AT TOP
         BE    GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'C'   IS IT A JOB CANCEL
         BE    JOBCAN         GO DO IT
*
         CLI   SELSAVE,C'F'   IS IT A JOB CANCEL-FORCE
         BE    JOBFOR         GO DO IT
*
         CLI   SELSAVE,C'E'   IS IT RESET PERFORMANCE GROUP
         BE    JOBRESET       GO DO IT
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
         EJECT
JOBCAN   DS    0H
*
         CLC   USERID(5),JOBNAMS
         BE    JOBCANIT
         CLC   USERID(2),=C'AS'
         BE    JOBCANIT
         CLC   USERID(2),=C'AP'
         BE    JOBCANIT
         CLC   USERID(2),=C'BP'
         BE    JOBCANIT
         CLC   USERID(3),=C'MVS'
         BE    JOBCANIT
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
JOBCANIT DS    0H
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVC   CMDLINE(7),=C'CANCEL '
         MVC   CMDLINE+8(8),JOBNAMS    JOB NAME
*
         LA    R0,1             SET AUTH ON
         SVC   235
*
         MODESET KEY=ZERO,MODE=SUP
         SLR   R0,0                    CLEAR REG 0
         LA    R1,CMDRESET            GET ADDRESS OF COMMAND
         SVC   34                      ISSUE COMMAND VIA SVC 34
         MODESET KEY=NZERO,MODE=PROB
*
         SR    R0,0             SET AUTH OFF
         SVC   235
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
JOBFOR   DS    0H  CANCEL JOB WITH CC=333
         CLC   USERID(2),=C'AS'
         BE    JOBFORCE
         CLC   USERID(2),=C'AP'
         BE    JOBFORCE
         CLC   USERID(2),=C'BP'
         BE    JOBFORCE
         CLC   USERID(3),=C'MVS'
         BE    JOBFORCE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
JOBFORCE DS    0H  CANCEL JOB WITH CC=333
*
         PACK  DOUBLEWD(8),JOBASID(4)
         CVB   R2,DOUBLEWD
*
         STAX  DEFER=YES
*
         LA    R0,1             SET AUTH ON
         SVC   235
         MODESET KEY=ZERO,MODE=SUP
*
         LR    R7,R13
         LA    R13,RTMWORK
       CALLRTM TYPE=MEMTERM,COMPCOD=819,ASID=(R2)
         LR    R13,R7
*
         MODESET KEY=NZERO,MODE=PROB
         SR    R0,0             SET AUTH OFF
         SVC   235
*
         STAX  DEFER=NO
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
RTMWORK  DS    18F
         EJECT
*
JOBRESET DS    0H
*
         CLC   USERID(2),=C'AS'
         BE    ELEVATE
         CLC   USERID(2),=C'AP'
         BE    ELEVATE
         CLC   USERID(2),=C'BP'
         BE    ELEVATE
         CLC   USERID(3),=C'MVS'
         BE    ELEVATE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
ELEVATE  DS    0H  CHANGE THE PERFORMANCE GROUP TO 3
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVC   CMDLINE(2),=C'E '
         MVC   CMDLINE+3(8),JOBNAMS    JES NUMBER
*
         LA    R15,CMDLINE+4           FIND A BLANK
*
         CLI   0(R15),C' '             GOT A BLANK?
         BE    *+12                    YUP
         LA    R15,1(,15)              BUMP POINTER
         B     *-12                    LOOP
*
         MVC   0(10,R15),=C',PERFORM=3'    TOP PERFORMANCE
*
         LA    R0,1             SET AUTH ON
         SVC   235
*
         MODESET KEY=ZERO,MODE=SUP
         SLR   R0,0                    CLEAR REG 0
         LA    R1,CMDRESET            GET ADDRESS OF COMMAND
         SVC   34                      ISSUE COMMAND VIA SVC 34
         MODESET KEY=NZERO,MODE=PROB
*
         SR    R0,0             SET AUTH OFF
         SVC   235
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
CMDRESET DC    Y(76),Y(00)
CMDLINE  DC    CL72'E JOB' COMMAND SVC 34 INPUT
*
*
         EJECT
*
DUNWITIT DS    0H
         L     R7,XDF7HOLD     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         EJECT
*
*    ONLY COMMAND SUPPORTED IS "END", ASIDE FROM MODE SWITCH
*
GOTCMD   DS    0H
         CLC   XDFCMD(3),=C'ALL'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'TSU'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'BAT'
         BE    GOODCMD
         CLC   XDFCMD(3),=C'STC'
         BE    GOODCMD
         B     DUNWITIT
*
GOODCMD  DS    0H
         MVC   SELCMD(3),XDFCMD
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         XC    ISPARMS(40),ISPARMS
         B     SHOWAGIN       GO REPEAT THE SCREEN
         EJECT
INITMSG  MVI   TABAREA,C' '   BLANK OUT THE
         MVC   TABAREA+1(79),TABAREA MESSAGE BUFFER
         BR    R7             RETURN TO OUR CALLER
         SPACE 5
         EJECT
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               XDFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   TABAREA,C' '
         MVC   TABAREA+1(79),TABAREA
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'EXECUTED INSTRUCTIONS'
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED
         TITLE 'SYSTEM TASK PROCESSING'
***
*  THE FOLLOWING WAS ADDED TO COMBINE MVS/SP1.3 SYSTEM TASKS WITH
*  THE *MASTER* TASK
***
MASTPROC EQU   *
         ST    R4,R4SAV2        SAVE R4
         L     R4,R4SAV         RESTORE ASCB POINTER
         CLC   =CL8'*MASTER*',0(R6) *MASTER* TASK
         BNE   MASTLOOP         NO - PROCESS OTHER SYSTEM TASK
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
         MVI   HIGHLITE,X'03'   HIGHLIGHT AS SWAPPED IN
         L     R6,ASCBOUCB      LOAD OUCB POINTER
*  PERFORMANCE GROUP
         SR    R7,R7            PREPARE FOR THE IC.
         IC    R7,OUCBSPG+1     PERFORMANCE GROUP NUMBER
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINEPGN(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINEPGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.
*  DISPATCHING PRIORITY
         SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,43(,R5)       LOAD ASCBDP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,43(,R5)       LOAD THE DISPATCHING PRIORITY AGN.
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
         LH    R7,OUCBSWC       LOAD OUCBSWC (SWAP COUNT)
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINESWC(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINESWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.
         MVI   TYPE,C'S'         STARTED TASK TYPE
*  ALLOCATED FRAME COUNT (MEMORY)
MASTLOOP LH    R7,ASCBFMCT       LOAD ALLOC FRAME COUNT.
         SLL   R7,2             MULTIPLY BY FOUR.
         A     R7,MASTFMCT       ADD TO SYSTEM ACCUMULATION
         ST    R7,MASTFMCT       STORE FOR ACCUMULATION
*  WORKING SET SIZE
         L     R6,ASCBOUCB      LOAD OUCB POINTER
         LH    R7,OUCBWSS        LOAD OUCBWSS
         A     R7,MASTWSS        ADD TO SYSTEM ACCUMULATION
         ST    R7,MASTWSS        STORE FOR ACCUMULATION
*  CPU TIME
         LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,=F'10000'     TO GET HUNDREDTHS AND MICROS
         LM    10,R11,200(R5)   LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,=F'10000'    TO GET HUNDREDTHS AND MICROS
         LTR   R9,R9            TEST JST CPU TIME
         BP    MASTSRB          IF ITS POISTIVE GO ON
MASTSRB  LTR   R11,R11          TEST SRB CPU TIME
         BP    MASTBJST         IF ITS OK GO ADD SRB AND JST CPU TIME
MASTBJST AR    R9,R11           ADD TOGETHER.
         A     R9,MASTCPU       ADD TO SYSTEM ACCUMULATION
         ST    R9,MASTCPU       STORE FOR ACCUMULATION
         TM    MASTFLG,X'3F'    TEST ALL SYSTEM TASK BITS
         BNO   MASTEXIT         PROCESS NEXT ASCB IF NOT ALL ON
*  ACCUMULATED ALLOCATED FRAME COUNT (MEMORY)
         L     R7,MASTFMCT      LOAD FRAME COUNT ACCUMULATION
         CVD   R7,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
*  ACCUMULATED WORKING SET SIZE
         L     R7,MASTWSS       LOAD WORKING SET SIZE ACCUMULATION
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWSS(6),=X'202020212040' MOVE IN THE EDIT MASK.
         ED    LINEWSS-1(6),WORK+5 EDIT THE RESULT.
*  ACCUMULATED CPU TIME
         L     R7,MASTCPU       LOAD CPU TIME ACCUMULATION
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
         MVC   JOB,=CL8'*MASTER*'  SIGNIFY *MASTER* TASKS
         BAL   R7,OUTMSG        MOVE THE LINE TO THE 3270 BUFFER.
MASTEXIT ST    R4,R4SAV         SAVE ASCB POINTER
         L     R4,R4SAV2        RESTORE RETURN ADDRESS
         BR    R4               RETURN
R4SAV2   DS    F                REG. 4 SAVE AREA
MASTFMCT DS    F                FRAME COUNT ACCUMULATOR
MASTWSS  DS    F                WORKING SET SIZE ACCUMULATOR
MASTCPU  DS    F                CPU TIME ACCUMULATOR
MASTFLG  DS    X                SYSTEM TASK PROCESSED FLAGS
***
*
***
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT NOGEN
         LTORG
*
         DS    0F
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
XDF7HOLD DS    F
OUT7HOLD DS    F
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
XDFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(XDFOPT)
SELECTR  DC    CL8'SELECT  '
XDFOPT   DC    CL8'XDFOPT  '
*
L78      DC    F'78'
L40      DC    F'40'
FZEROS   DC    F'0'
F8       DC    F'8'
L1       DC    F'1'
L20      DC    F'20'
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
USERID   DC    CL8' '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
*
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'PIKA     '     SELECTION CODE
ACTLINE  DC    C'ACTLINE )'     JOB INFO
ACTLINE2 DC    C'ACTLINE  '     JOB INFO
ACTLINEP DC    C'(ACTLINE )'    JOB INFO
CMDLINEP DC    C'(XCMD    )'    JOB INFO
PIKP     DC    C'(PIKA    )'    SELECTION CODE
*
XDFCMD   DC    CL40' '
SELCMD   DC    CL40' '
*
SPACES   DC    CL80' '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
XDFDATAB DC    C'XDFDATAB'
XDFTABLE DC    C'XDFTABLE'
*
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
WHICH    DS    CL1
*
WORK6    DS    XL6            PARM LIST FOR ISPLINK
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
         DS    0F
         DC    C'TABWORK '
TABWORK  DS    CL80
         DS    0F
         DC    C'TABGOTN '
TABGOTN  DS    CL80
         DS    0F
         DC    C'TABAREA '
TABAREA  DS    0CL78
HIGHLITE DC    C' '             SET THIS TO X'03' TO HIGHLIGHT A LINE
ASID     DC    CL4' '
         DC    C' '
JOB      DC    CL8' '
         DC    C' '
PSTEP    DC    CL8' '
         DC    C' '
STEP     DC    CL8' '
         DC    C' '
WHERE    DC    CL3' '
         DC    C' '
STATUS   DC    CL4' '
         DC    C' '
SIZE     DC    CL5' '
         DC    CL2' '
LINEWSS  DC    CL5' '
         DC    C' '
TYPE     DC    C' '
         DC    C' '
DP       DC    CL2' '
         DC    C' '
LINEPGN  DC    CL3' '
         DC    C' '
LINESWC  DC    CL3' '
         DC    C' '
CPUTIME  DC    CL8' '
CPUFLAG  DC    C' '
         DC    CL8' '
         DC    C' '
*
LINEWMS  DC    CL7' '
*
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
*
         DS    0F
         DC    C'SELECTED ASID '
JOBASID  DC    CL4' '
*
         DS    0F
         DC    C'SELECTED JOBNAME'
*
JOBNAMS  DS    CL8            JOBNAME SAVE AREA
JOBTYPE  DS    CL3            JOBTYPE SAVE AREA
*
MESSAGE  EQU   *              OUTPUT MESSAGE BUFFER
JOEINIT  DS    C    0=JQEINIT, 1=JOEINIT
MSGJBTYP DS    CL3            JOB OR TSU OR STC
MSGJOB#  DS    CL5            JOB NUMBER
         DS    C
MSGJNME  DS    CL8            JOB NAME
         DS    C
MSGSTYPE DS    CL42           JOB STATUS
         DS    C
MSGSPCST DS    CL9            SPECIAL STATUS
         DS    CL9
LMESSAGE EQU   *-MESSAGE      LENGTH OF THE MESSAGE BUFFER
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHSWAP'
*                ×...×...×...×...×...×...×...×...×...×...×...
HEAD1   DC     X'40'                      HIGHLIGHT IS X'03'
        DC     C'ASID   JOB    PSTEP    STEP    '
        DC     C'.STATUS.  '
        DC     C'MEMORY   WSS T DP  PG SWP'
        DC     C'      CPU '
*
         SPACE 2
         LTORG
         EJECT
SYSHDR   DS    0CL77
         DC    X'40'                      HIGHLIGHT IS X'03'
         DC    CL4'CPU='         CPU UTILIZATION
HDRCPU   DC    CL3'XXX',C'%'
         DC    CL5' AFC='        AVERAGE AVAILABLE FRAME COUNT
HDRAFC   DC    CL4'XXXX'
         DC    CL5' PFR='        PAGE FAULT RATE
HDRAPR   DC    CL4'XXXX'
         DC    CL5' ASQ='        AUX STORAGE MANAGER QUE LENGTH
HDRASQ   DC    CL4'XXXX'
         DC    CL5' UIC='        UNIREFERENCED INTERVAL COUNT
HDRUIC   DC    CL4'XXXX'
         DC    CL5' DIO='        DEFERRED IO REQUESTS
HDRDIO   DC    CL4'XXXX'
         DC    CL6' DMNS='       # OF DOMAINS IN ENTRIES
HDRDMN   DC    3CL6' MT:RU'      MULTI-PROGRAMMING LEVEL TARGET
R4SAV    DS    F                 REG. 4 SAVE AREA
CPUTAB   DC    100CL33' '        CPUTABLE SAVE AREA
         DC    X'FF'             END OF CPUTABLE
         ORG   CPUTAB
CLC26    DS    CL31              ASID, JOB, PROCSTEP, STEP
CLCPU    DS    CL2               CPU SECONDS
         CNOP  0,8
         CVT   DSECT=YES
***
*  THE FOLLOWING WAS ADDED TO PROVIDE ADDRESSABILITY TO MVS/SP1.3
*  SRM RESOURCE CONTROL TABLE VARIABLES (RCT)
***
RMCT     DSECT ,                                                  NERDC
         ORG   RMCT+228                                           NERDC
RMCTRCT  DS    F                   POINTER TO RCT                 NERDC
         ORG   ,                                                  NERDC
         SPACE 3                                                  NERDC
RCTDSECT DSECT ,                   SRM RES CNTL TABLE             NERDC
RCTRCT   DS    CL4                 'RCT'                          NERDC
RCCUICTL DS    H                   UIC THRESHOLD LOW              NERDC
RCCUICTH DS    H                   UIC THRESHOLD HIGH             NERDC
RCCCPUTL DS    H                   CPU THRESHOLD LOW              NERDC
RCCCPUTH DS    H                   CPU THRESHOLD HIGH             NERDC
RCCPTRTL DS    H                   PAGING THRESHOLD LOW           NERDC
RCCPTRTH DS    H                   PAGING THRESHOLD HIGH          NERDC
RCCASMTL DS    H                   ASM Q REQUEST THRESHOLD LOW    NERDC
RCCASMTH DS    H                   ASM Q REQUEST THRESHOLD HIGH   NERDC
RCCDPRTL DS    H                   DEMAND PAGE RATE LOW THRESH    NERDC
RCCDPRTH DS    H                   DEMAND PAGE RATE HIGH THRESH   NERDC
RCCCPUPL DS    H                   CPU UTIL W/ DEMAND PAGING LOW  NERDC
RCCCPUPH DS    H                   CPU UTIL W/ DEMAND PAGING HI   NERDC
RCCMSPTL DS    H                   PAGE DELAY W/ DEMAND PAGING LO NERDC
RCCMSPTH DS    H                   PAGE DELAY W/ DEMAND PAGING HI NERDC
RCCPDLTL DS    H                   PAGE DELAY TIME (MS) LOW       NERDC
RCCPDLTH DS    H                   PAGE DELAY TIME (MS) HIGH      NERDC
         ORG   RCTDSECT+60                                        NERDC
RCVCTMC  DS    H                   SAMPLE INTERVALS COUNT         NERDC
RCVUICA  DS    H                   UIC AVERAGE                    NERDC
RCVCPUA  DS    H                   CPU USAGE AVERAGE              NERDC
RCVAVQC  DS    H                   AVQ LOWW COUNT                 NERDC
RCVAFQA  DS    H                   AVG AVAIL FRAMES               NERDC
RCVASMQA DS    H                   ASM Q LENGTH AVERAGE           NERDC
RCVPTR   DS    H                   PAGING RATE                    NERDC
RCVDPR   DS    H                   DEMAND PAGING RATE             NERDC
RCVPAGRT DS    H                   TOTAL PAGING RATE              NERDC
RCVMSPP  DS    H                   PAGE DELAY TIME (MS)           NERDC
***
*
***
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR ASCB
*.....................................................................*
         IHAASCB
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
         DS    9F
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.SERV.LOAD
//         DD   DISP=SHR,DSN=TECH.FIXDHEAD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.ASQCCLD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  NAME XDF(R)
/*
