//ASQCCVDF  JOB (X0002,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=T,NOTIFY=BCOOK
/*JOBPARM L=99
//* *****************************************************************
//* *****************************************************************
//*                                                               ***
//* TECH.SERV.BCOOK (VDF2                                         ***
//*                                                               ***
//* *****************************************************************
//* *****************************************************************
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
***********************************************************************
*                                                                     *
*                                                                     *
*                    VTOC DISPLAY FACILITY (VDF)                      *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY ONLINE DISK VTOC'S AND DATASETS.                  *
*                                                                     *
*                                                                     *
* FUNCTION: VDF2 IS DESIGNED TO OPERATE UNDER ISPF/PDF USING DIRECT   *
*           CALLS TO ISPSUBS.    IT PROVIDES A DISPLAY OF THE         *
*           ACTIVITY IN THE SYSTEM, SIMILAR TO RMF, BUT SINCE IT      *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*           VDF LOOKS AT VARIOUS MVS SP 1.3.0 CONTROL BLOCKS.         *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
VDF      CSECT
         USING *,13,12,11,10
         B     72(,15)
         DC    17F'0'
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
*
SETBASES DS    0H
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
         ST    R9,ISPFTLDA        SAVE TLD POINTER
         CLC   0(3,R9),=C'TLD'   DOES R9 POINT TO TLD?
         BE    INITFLDS           YES, KEEP GOING
         DC    H'0'               NO, ABEND
*
ISPFTLDA DS    F
ISPFAREA DS    F
ISPFWORK DS    F
ISPFSIZE DS    F
MAXLINES DS    F
ISPFEPA  DS    F
*
INITFLDS DS    0H
*
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
         MVI   PRTLINE,C' '   CLEAR TAB ENTRY AREA
         MVC   PRTLINE+1(79),PRTLINE
         MVI   PRTATTR1,X'1F'
         MVI   PRTATTR2,X'05'
         XC    ISPARMS(40),ISPARMS
         MVC   VOLPARM(3),=C'DEF'
*
         L     R1,16                        CVT ADDRESS
         USING CVT,R1
         MVC   UCBTOP(4),CVTILK2            UCB LOOKUP TABLE
         MVC   UCBNXT(4),CVTILK2            UCB LOOKUP TABLE
         DROP  R1
*
SHOWAGIN DS    0H
         MVC   UCBNXT(4),UCBTOP
         B     SHOWDASD    DISPLAY THE VTOC'S
*
SHOWDONE DS    0H
*                                  DELETE THE TABLE VARIABLES
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
L4096    DC    F'4096'
UCBTOP   DS    F
UCBNXT   DS    F
*
         TITLE 'EXTRACT UCB INFORMATION'
SHOWDASD DS    0H
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
*
         L     R9,ISPFTLDA
         USING ISPFTLD,9
         MVC   ISPFAREA(4),SPFSCRN    SCREEN AREA
         MVC   ISPFSIZE(4),SPFNUMLN   SCREEN SIZE
         MVC   MAXLINES(4),SPFSPLIT   NUMBER OF LINES ON SCREEN
*
         L     R8,SPFSUBPT
         USING SPFSUBRS,8
         MVC   ISPFEPA(4),SPFDISPL    DISPLAY ROUTINE EPA
*
         L     R15,ISPFAREA
         MVC   0(80,R15),HDR1
         LA    R15,80(,R15)
         MVC   0(80,R15),HDR2
         LA    R15,80(,R15)
         MVC   0(80,R15),HDR3
         LA    R15,80(,R15)
         ST    R15,ISPFWORK
         LA    R15,CURSOR
         ST    R15,SPFCURSR
*
         LA    R15,3
         ST    R15,ISPFNUMB         INITIALIZE COUNTER
         MVC   SPFHELP(8),=CL8'XDFHELP'     NAME OF HELP SCREEN
         XC    SPFFUNC,SPFFUNC     TURN OFF PRIOR FUNCTION BITS
         OC    SPFENABL+1(3),=XL3'58C0FC' SET ENABLE BITS
         MVC   SPFDLINE,SPFMAXLN   SET NUMBER OF DISP LINES
         MVC   SPFDLLEN,SPFMXLEN   SET LINE LENGTH
         MVC   SPFAPPID,=CL8'VDFKEY'     SAVE KEY NAME
         MVI   SPFPREQ,0           TURN OFF PROCESS REQUESTS
         MVI   SPFPSTAT,0          TURN OFF PROCESS STATUS
         XC    SPFSCROL,SPFSCROL   ZERO SCROLL INPUT BYTE PTR
*
         L     R6,UCBNXT
*
         SR    R9,R9                        CLEAR BACK PTR
LOOP     LH    R8,0(0,R6)                   NEXT UCB FROM TABLE
         LTR   R8,R8                        TEST ENTRY TYPE
         BM    DONEUCBS                     MINUS, STOP SCANNING
         BP    GOTUCB                       IF NON-ZERO GO ON
NXTUCB   LA    R6,2(0,R6)                   NEXT ONE
         B     LOOP                         CONTINUE SCANNING
         USING UCBCMSEG,R8
GOTUCB   DS    0H
         TM    UCBTBYT3,UCB3DACC            DIRECT ACCESS?
         BZ    NXTUCB                       NO, OMIT
         CR    R8,R9                        ALTERNATE CHANNEL?
         BNH   NXTUCB                       IF SO SKIP IT
         LR    R9,R8                        SAVE THIS ONE AS DONE
         CLC   UCBTBYT3(2),=X'2005'         DATA CELL?
         BE    NXTUCB                       IF SO SKIP IT
*
         CLC   VOLPARM(3),=C'OFF'           SHOW OFFLINE AS WELL?
         BE    USEUCB
*
         TM    UCBVOLI,X'FF'                ANY VOLUME MOUNTED?
         BZ    NXTUCB                       NO, IGNORE IT
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   NXTUCB                       NO, IGNORE DEVICE
*
         CLC   VOLPARM(3),=C'DEF'           DEFAULT?
         BE    DEFTST
*
         CLC   VOLPARM(3),=C'ALL'           SHOW ALL?
         BE    USEUCB
*
         CLC   VOLPARM(3),UCBVOLI           THIS ONE SELECTED
         BE    USEUCB
*
         CLC   VOLPARM(3),=C'SYS'           SYSTEM PACK
         BNE   NXTUCB
*
         CLC   UCBVOLI(3),=C'TST'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'MVS'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'PAG'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'SPO'           SYS  PACK
         BE    USEUCB
         CLC   UCBVOLI(3),=C'SPA'           SYS  PACK
         BE    USEUCB
*
         B     NXTUCB
*
DEFTST   DS    0H
         CLC   UCBVOLI(2),=C'VM'            VM PACK?
         BE    NXTUCB
*
         CLC   UCBVOLI(5),=C'SPARE'         SPARE PACK?
         BE    NXTUCB
*
USEUCB   DS    0H
         MVC   VOLNAME,UCBVOLI              PRINT VOLUME NAME
         MVC   UNITADDR,UCBNAME             GET UNIT ADDRESS
*
         MVC   UNITTYPE(4),=C'3350'
         CLI   UCBUNTYP,X'0B'     IS IT 3350
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3380'
         CLI   UCBUNTYP,X'0E'     IS IT 3380
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3330'
         CLI   UCBUNTYP,X'09'     IS IT 3330
         BE    TSTACT             YUP
*
         MVC   UNITTYPE(4),=C'3331'
         CLI   UCBUNTYP,X'0D'     IS IT 3330 MOD 11
         BE    TSTACT             YUP
*
TSTACT   DS    0H
         MVC   USTATUS(4),=C'ACT '
*
         TM    UCBSTAT,UCBALOC    IS IT ALLOCATED
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'RESV'
*
         TM    UCBSTAT,UCBRESV    IS IT RESERVED
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'OFFP'
*
         TM    UCBSTAT,UCBUNLD    IS IT OFFLINE-PENDING
         BO    TSTTYPE            YUP
*
         MVC   USTATUS(4),=C'OFF '
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   SETOFFLN                     NO, ISSUE MSG
*
         MVC   USTATUS(4),=C'ONLN'
*
TSTTYPE  DS    0H
         MVC   USAGE(4),=C'PRIV'
*
         TM    UCBSTAB,UCBBPRV    IS IT A PRIVATE VOLUME
         BO    SETUSERS           YUP
*
         MVC   USAGE(4),=C'STOR'
*
         TM    UCBSTAB,UCBBSTR    IS IT A STORAGE VOLUME
         BO    SETUSERS           YUP
*
         MVC   USAGE(4),=C'PUB '
*
SETUSERS DS    0H
         SR    R1,R1                        CLEAR A REG
         IC    R1,UCBUSER                   GET USE COUNT
         CVD   R1,DBLW                      CONVERT TO PRINT
         UNPK  USECNT,DBLW+6(2)             PLACE IT ON THE LINE
         OI    USECNT+2,X'F0'               FIX SIGN BITS
*
         TM    UCBFLA,UCBSAP      IS IT IN USE BY STANDALONE PROCESS
         BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
         TM    UCBFLB,UCBRESVH    IS IT RESERVED
         BO    SETRSVD            YES, SET MSG  AND SKIP LSPACE
         TM    UCBFL5,UCBNALOC    IS IT IN USE BY ICKDSF
         BZ    DOLSPACE           NO, GO DO LSPACE
*
SETRSVD  DS    0H
         MVC  TOTCYL(27),SYSTUSE
         B     ADD1LINE
*
SETOFFLN DS    0H
         MVC  TOTCYL(27),SYSOFF
         B     ADD1LINE
*
         TITLE 'EXTRACT SPACE INFORMATION USING LSPACE SVC'
DOLSPACE DS    0H
         LR    R0,R8                        POINT TO UCB
         LA    R1,LSPACE                    MONITOR SPACE MESSAGE AREA
*  R1 -> MONITOR SPACE MESSAGE AREA
*  R0 -> UCB, HIGH ORDER BYTE INDICATES NO SMF RECORD (TYPE 19)
         SVC   78                           GET SPACE INFO
         LTR   R15,R15                      DID IT WORK?
         BZ    SPCOK                        YES
         B     NXTUCB                       NOW GO ON
*
*  LSPACE RETURNS ITS INFORMATION IN THE FORM:
*        SPACE=CCCC,TTTT,AAAA/EEEE,KKKK
*  WHERE
*        CCCC = TOTAL FREE CYLINDERS
*        TTTT = TOTAL FREE TRACKS
*        AAAA = TOTAL FREE AREAS
*        EEEE = NUMBER OF CYLINDERS IN LARGEST FREE EXTENT
*        KKKK = NUMBER OF TRACKS IN LARGEST FREE EXTENT
*
SPCOK    DS    0H
         MVC   TOTCYL,CYLTOTAL+1            CYL (TOTAL)
         MVC   TOTTRK,TRKTOTAL+1            TRK (TOTAL)
         MVC   TOTAREA,AREATOTL+1           NUM (OF AREAS)
         MVC   LARGECYL,CYLEXT+1            CYL (LARGEST)
         MVC   LARGETRK,TRKEXT+1            TRK (LARGEST)
*
* PRTLINE IS FORMATTED WITH THE VOL DISPLAY LINE
*
ADD1LINE DS    0H
*
         L     R15,ISPFWORK
         MVC   0(80,R15),PRTLINE
         LA    R15,80(,R15)
         ST    R15,ISPFWORK
         L     R15,ISPFNUMB
         LA    R15,1(,R15)
         ST    R15,ISPFNUMB
         C     R15,MAXLINES
         BNL   SHOWVTAB
*
         ST    R6,UCBNXT
         MVI   PRTLINE,C' '
         MVC   PRTLINE+1(79),PRTLINE
         MVI   PRTATTR1,X'1F'
         MVI   PRTATTR2,X'05'
*
         B     NXTUCB                       NOW GO ON
ISPFNUMB DS    F
         TITLE 'VDF-SHOW VTOC TABLE'
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
DONEUCBS DS    0H
*
         MVC   UCBNXT(4),UCBTOP
         L     R14,ISPFNUMB
         C     R14,MAXLINES
         BNL   SHOWVTAB
*
         L     R15,ISPFWORK
         MVC   0(80,R15),ENDOFDAT
         LA    R15,80(,R15)
         LA    R14,1(,R14)
*
SPACUCBS DS    0H
         C     R14,MAXLINES
         BNL   SHOWVTAB
         MVI   0(R15),C' '
         MVC   1(79,R15),0(R15)
         LA    R15,80(,R15)
         LA    R14,1(,R14)
         B     SPACUCBS
SHOWVTAB DS    0H
         L     R15,ISPFEPA         DISP ADDRESS
         LA    R1,ISPFSAVE         POINT TO SAVE AREA
         L     R9,ISPFTLDA         SET PARAMETER
         ST    R9,0(,R1)           SET PARAMETER
         STM   R1,R13,SAVE13       SAVE OLD SAVE AREA POINTER
         LA    R13,ISPFSAVE        SET  NEW SAVE AREA POINTER
         BALR  R14,R15
*
         CNOP  2,4
         BALR  R14,0
         LM    R1,R13,8(R14)
         B     60(,R14)
SAVE13   DS    13F
         L     R9,ISPFTLDA         SET PARAMETER
         MVC   SPFAID,SPFFUNC4     MOVE AID TO WORK AREA
*                              NOTE SPF AID CHARACTER IS NON-STANDARD
*
         TM    SPFFUNC2,HELP       WAS HELP KEY HIT ?
         BNO   *+12                BRANCH IF NOT
         MVI   SPFAID,X'01'        INDICATE HELP KEY
         B     SHOWDONE            ALL DONE
*
         TM    SPFFUNC2,RETURN     WAS RETURN KEY HIT ?
         BNO   CKENDK              BRANCH IF NOT
         B     ENDK                INDICATE END KEY HIT
CKENDK   DS    0H
         TM    SPFFUNC3,ENDKEY     WAS END KEY HIT ?
         BNO   GOTSTUFF            BRANCH IF NOT
ENDK     DS    0H
         MVI   SPFAID,X'03'        INDICATE END KEY
         B     SHOWDONE            ALL DONE
*
*
SPFAID   DS    X
*
ENDOFDAT DC    X'03'
         DC    30C'*'
         DC    CL19'     END OF DATA   '
         DC    30C'*'
*
ISPFSAVE DS    18F
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
GOTSTUFF DS    0H
*
         L     R1,SPFSCRN    SCREEN AREA
         MVC   CMDBGN(60),CURSOR(R1)    COMMAND DATA
*
         CLI   SPFAID,DOWN    SCROLL DOWN
         BE    SHOWDASD       GO REPEAT THE DISPLAY
*
GOTCMD   DS    0H
         TR    CMDBGN,CAPSONLY   UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDBGN,C' '    IF COMMAND AREA IS SPACES
         BE    SHOWAGIN       THEN A COMMAND WAS NOT ENTERED
*
         CLC   CMDBGN(2),=C'V='  IS IT VOLUME SELECTION?
         BE    SETNVOL            RESET DEFAULT
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
SETNVOL  DS    0H
*
         MVC   VOLPARM(3),CMDBGN+2
SETTUCB  DS    0H
         MVC   UCBNXT(4),UCBTOP
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT NOGEN
         LTORG
*
         DS    0F
EPNAME   DC    CL8' '
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
VDF7HOLD DS    F
OUT7HOLD DS    F
*
H0PTR    DC    X'80',AL3(H0)
H0       DC    H'0'
*
VOLPARM  DC    CL3'   '
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
JOBN     DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
VDFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(VDFOPT)
SELECTR  DC    CL8'SELECT  '
VDFOPT   DC    CL8'VDFOPT  '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
L3       DC    F'3'
L77      DC    F'77'
L40      DC    F'40'
*
SELSAVE  DC    CL3' '
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'VSLA     '     SELECTION CODE
VTOLINE  DC    C'VTOLINE )'     JOB INFO
VTOLINE2 DC    C'VTOLINE  '     JOB INFO
VTOLINEP DC    C'(VTOLINE )'    JOB INFO
VSLP     DC    C'(VSLA    )'    SELECTION CODE
VCMD     DC    C'(VCMD    )'    SELECTION CODE
*
SPACES   DC    C'        '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
VDFDATAB DC    C'VDFDATAB'
VDFTABLE DC    C'VDFTABLE'
*
*
         TITLE 'SCREEN HEADER LINES'
***********************************************************************
*                                                                     *
*        SCREEN HEADER LINES                                          *
*                                                                     *
***********************************************************************
HDR1     DS    0CL80
         DC    X'07'
         DC    19C'-'
         DC    CL40'    VTOC DISPLAY FACILITY    (VDF)    '
         DC    20C'-'
HDR2     DS    0CL80
         DC    X'07'
         DC    CL12'COMMAND ==> '
         DC    X'1F'
CURSOR   EQU   *-HDR1
CMDDATA  DC    CL67'       '
HDR3     DS    0CL80
         DC    X'05'
         DC    CL47'SEL  VOLSER ADR  TYPE STATUS USAGE USERS CYLS  '
         DC    CL32'TRKS  EXTS    CYL  TRK          '
CMDBGN   DC    CL67'       '
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
         TITLE 'VDF-WORK AREAS      '
         LTORG
*
DBLW     DS    D                           CVD WORK AREA
*
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'Ö.<(+×&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
SYSTUSE  DC   X'03',CL26'IN USE BY SYSTEM'
SYSOFF   DC   X'03',CL26'UCB IS OFFLINE  '
*
         TITLE 'VDF-LSPACE WORK AREA'
*
LSPACE   DS    0CL1
SPACEMSG DS    CL6                          'SPACE='
CYLTOTAL DS    CL4                          TOTAL FREE CYLINDERS
COMMA1   DS    CL1
TRKTOTAL DS    CL4                          TOTAL FREE TRACKS
COMMA2   DS    CL1
AREATOTL DS    CL4                          TOTAL FREE AREAS
SLASH    DS    CL1
CYLEXT   DS    CL4                          CYLS IN LARGEST FREE EXTENT
COMMA3   DS    CL1
TRKEXT   DS    CL4                          TRKS IN LARGEST FREE EXTENT
         DC   CL40' '
         TITLE 'VDF-VTOC LINE FORMAT'
VDFHEAD  DC    CL40' NAME  ADR CNT CYL TRK NUM CYL/TRK'
PRTLINE  DS    0CL77
PRTATTR1 DS   X
SELCODE  DS   CL4
PRTATTR2 DS   X
VOLNAME  DS   CL6                           VOLUME NAME
DIRFFLG  DS   C                             DIRF FLAG
UNITADDR DS   CL3                           UNIT ADDRESS
         ORG  PRTLINE+17
         DS   CL1
UNITTYPE DS   CL4
         DS   CL2
USTATUS  DS   CL4
         DS   CL2
USAGE    DS   CL4
         DS   CL3
USECNT   DS   CL3                           # OF CURRENT ALLOCATIONS
         DS   CL3
TOTCYL   DS   CL3                           TOTAL FREE CYLINDERS
         DS   CL3
TOTTRK   DS   CL3                           TOTAL FREE TRACKS
         DS   CL3
TOTAREA  DS   CL3                           TOTAL FREE AREAS
         DS   CL4
LARGECYL DS   CL3                           # CYLS IN LARGEST FREE EXT
         DS   CL2
LARGETRK DS   CL3                           # TRKS IN LARGEST FREE EXT
         DS   XL256
         DS   XL256
         EJECT
*.....................................................................*
*        MAPPING DSECT FOR ASCB
*.....................................................................*
         IHAASCB
*.....................................................................*
*        MAPPING DSECT FOR SRM USER CONTROL BLOCK
*.....................................................................*
         IRAOUCB
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
         SPACE
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
DMDTLEN  EQU   *-DMDT
         SPACE 2
*
* EQUATES....
*
RMCTDMDT EQU   X'B4'           POINTER TO DOMAIN TABLE
RMCTDMNC EQU   X'BC'           COUNT OF NUMBER OF DOMAINS
RMCTWMST EQU   X'18'           ADDRESS OF WLM SPEC TABLE
         SPACE 2
         PRINT GEN
         TITLE 'COMMUNICATION VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'UNIT CONTROL BLOCK'
         IEFUCBOB
         TITLE 'DATA EXTENT BLOCK'
         IEZDEB
         TITLE 'FMT4 DSCB'
DSCB4    DSECT
         ORG   DSCB4+44                     GET PAST KEY (44X'04')
         IECSDSL1 (4)
         TITLE 'TLD  - ISPF/PDF  TERMINAL DATA CONTROL BLOCK '
ISPFTLD DSECT
         DS    CL4        EYE-CATCHER "TLD0" (OR TLD1, ETC)
         DS    4F
THISTLD  DS    F           THIS TLD POINTER
         DS    7F
SPFSUBPT DS    F           SPF SUBS POINTER
         DS    10F
SPFSCRN  DS    F           ADDRESS OF LOGICAL SCREEN
         DS    11F
SPFFUNC  DS    0XL8
SPFFUNC1 DS    X
SPFFUNC2 DS    X
HELP     EQU   X'10'        PF1 = HELP
RETURN   EQU   X'08'        PF3 = END
SPFFUNC3 DS    X
ENDKEY   EQU   X'80'        PF3 = END
SPFFUNC4 DS    X                   SCROLL KEY BYTE
ENTER    EQU   X'00'        ENTER
FIND     EQU   X'08'        PF5 = FIND
CHANGE   EQU   X'04'        PF6 = CHANGE
UP       EQU   X'80'        PF7 = SCROLL UP
DOWN     EQU   X'40'        PF8 = SCROLL DOWN
LEFT     EQU   X'10'        PF10= SCROLL LEFT
RIGHT    EQU   X'20'        PF11= SCROLL RIGHT
         DS    XL4
SPFENABL DS    XL8                 ENABLE FUNCTIONS BITS
         DS    F
SPFCURSR DS    F                   RELATIVE CURSOR POSITION
         DS    2F
SPFNUMLN DS    F                   MAXIMUN SCREEN SIZE
SPFMAXLN DS    F                   MAXIMUM NUMBER OF LINES ON TERMINAL
SPFMXLEN DS    F                   MAXIMUN LINE WIDTH ON TERMINAL
SPFDLINE DS    F                   CURRENT NUMBER OF LINES FOR SPFDISP
SPFDLLEN DS    F                   CURRENT LINE WIDTH FOR SPFDISP
         DS    2F
SPFSPLIT DS    F                   MAX LINES IF IN SPLIT SCREEN
         DS    11F
SPFSCROL DS    F                   SCROLL INPUT BYTE POINTER
SPFAPPID DS    CL8                 APP KEYWORD FOR SAVING
         DS    3F
SPFPREQ  DS    X                   REQUEST BITS
         DS    3X
         DS    X
SPFPSTAT DS    X                   STATUS FLAGS
         DS    2X
         DS    19F
SPFHELP  DS    CL8                 HELP MENU NAME
*
*                         FIELD ATTRIBUTE BYTES FOR SPF DISPLAY
*
ATTRONON EQU   X'04'               OUTPUT NON-DISPLAY
ATTROLOW EQU   X'05'               OUTPUT LOW INTENSITY
ATTROHI  EQU   X'07'               OUTPUT HIGH INTENSITY
ATTRIAN  EQU   X'10'               INPUT ASIS NON-DISPLAY
ATTRIAL  EQU   X'11'               INPUT ASIS LOW INTENSITY
ATTRIAH  EQU   X'13'               INPUT ASIS HIGH INTENSITY
ATTRICN  EQU   X'1C'               INPUT CAPS (BLANK-PAD) NON-DISPLAY
ATTRICL  EQU   X'1D'               INPUT CAPS (BLANK-PAD) LOW INTENSITY
ATTRICH  EQU   X'1F'               INPUT CAPS (BLANK-PAD) HIGH INT
*
SPFSUBRS DSECT
         DS    24F
SPFDISPL DS    F                   ISPF/PDF DISPLAY ROUTINE
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.PROD.ISPLOAD
//         DD   DISP=SHR,DSN=TECH.SERV.LOAD
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.ASQCCLD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  NAME VDF(R)
/*
