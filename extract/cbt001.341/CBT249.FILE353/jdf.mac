//BCOOKJDF  JOB (X0002,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=T,NOTIFY=BCOOK
/*JOBPARM L=999
//*
//*  TECH.SERV.BCOOK(JDF)
//*
//ASM1    EXEC PGM=IEV90,REGION=1024K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL),BATCH'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.HASPSRC,DISP=SHR
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
           TITLE 'JDF - JES2 DISPLAY FACILITY '
***********************************************************************
*                                                                     *
*                                                                     *
*                   JES2 DISPLAY FACILITY (JDF)                       *
*                                                                     *
*                                                                     *
* ABSTRACT: DISPLAY STATUS OF JES2 JOBS, LOGONS, & STARTED TASKS.     *
*                                                                     *
*                                                                     *
* FUNCTION: JDF IS A SET OF ENTRY POINTS IN A SINGLE CALLED PROGRAM,  *
*           DESIGNED TO BE INVOKED FROM ISPF PANELS. THE PROGRAM IS   *
*           INTENDED TO BE USED IN TSO AND CMS ENVIRONMENTS WITH      *
*           MINIMAL SOURCE CHANGES.                                   *
*                                                                     *
*           JDF READS THE JES2 CHECKPOINT AND SPOOL DATASETS.         *
*                                                                     *
*           THE JES2 JOB QUEUE ELEMENTS (JQES) FROM HASPCKPT          *
*           ARE USED TO DISPLAY INFORMATION ON JOBS ON INPUT, WAIT-   *
*           ING FOR EXECUTION, OR EXECUTING.                          *
*                                                                     *
*                                                                     *
*  NOTE: FOR CMS/SPF INSTALLATION, DO THE FOLLOWING:                  *
*                                                                     *
*   1. YOU MUST CHANGE THE CMS FLAG:                                  *
*                                                                     *
*      TSO FORMAT ==>          SETCMS   MVI   CMSFLAG,C'0'            *
*                                                                     *
*      CMS FORMAT ==>          SETCMS   MVI   CMSFLAG,C'1'            *
*                                                                     *
*                                                                     *
*   2. DELETE THE "MODESET" MACROS (NOT USEABLE UNDER CMS).           *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
* INPUT: R1 -> STANDARD PARAMETER LIST:                               *
*        R1 ==> (PARM ADDR)                                           *
*      PARM ==> H'LENGTH',PARMDATA                                    *
*                                                                     *
* EXTERNAL REFERENCE: JDFSRB                                          *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              ABEND                                                  *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES: REENTRANT,RESIDENT                                      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* CHANGE LOG                                                          *
*                                                                     *
*  ??????? REMOVED "RENT" LOGIC                                   BC  *
*                                                                     *
*  1/05/83 CHANGED CHECK INPUT FROM CLI JQEFLAGS,0:BO JQEINPUT        *
*                                TO CLI JQETYPE,67:BE JQEINPUT    BC  *
*                                                                     *
*  1/05/83 ADDED "RDR" COMMAND FOR CMS OUTPUT ROUTING             BC  *
*                                                                     *
*  1/06/83 CHANGED THE DSTABLE PROCESSING ("S" OPTION) TO CREATE  BC  *
*          A TABLE OF 1000 RECORDS ONLY AND ADDED A "MORE" SUB-CMD    *
*                                                                     *
*  5/03/83 ADDED "=OM?" COMMAND, WHERE "?" IS THE 3270 MODEL      BC  *
*          NUMBER. THIS MATCHES A CLIST ON SYS1.CMDPROC.              *
*          CHANGED OUTPUT DISPLAY TO DECREMENT PRINT LINES TO SHOW    *
*          LINES REMAINING. ALSO ADDED OUTPUT DEVICE.                 *
*                                                                     *
*  5/11/83 CHANGED "R4.NT0" TO "R4 QUE" FOR OUTPUTS ON REMOTE     BC  *
*          QUEUES.                                                    *
*                                                                     *
*  6/15/83 CHANGED THE DSTABLE PROCESSING ("S" OPTION) TO SKIP    BC  *
*          THE SECOND LINE IF LENGTH=80 AND LAST 2 BYTES ARE ZEROS    *
*          OR SPACES.                                                 *
*                                                                     *
*  6/22/83 REVISED REQUEUE LOGIC TO AVOID "PRSYSOUT" STATUS       BC  *
*                                                                     *
*  6/24/83 ADDED "BURST" INDICATION TO JOB AND DSID DISPLAYS.     BC  *
*                                                                     *
*  6/29/83 CHANGED THE JOBS DISPLAY TO HAVE A SEPARATE COLUMN     BC  *
*          FOR "SYSOUT=S", "SYSOUT=P (OR C)", AND ALL ELSE IS         *
*          SHOWN AS "SYSOUT=A". THIS WILL PROVIDE OPERATIONS          *
*          ADDITIONAL REQUESTED INFORMATION.                          *
*                                                                     *
*  7/15/83 ADDED AUTHORIZATION CHECKING FOR USERID "FFTCS" TO     BC  *
*          ALLOW BROWSE OF JOBS BEGINNING WITH "ITP".                 *
*                                                                     *
*  7/15/83 FOR NEW SPF, CHANGED "=" FOR RESET COMMAND TO "Z"      BC  *
*          BECAUSE SPF USES "=" FOR JUMP FUNCTION.                    *
*                                                                     *
*  8/22/83 FOR NEW SPF, CHANGED "Z" FOR RESET COMMAND TO "D"      BC  *
*          AND CHANGED "I" TO "Q"; CHANGED "N" TO "I".                *
*                                                                     *
*  8/23/83 ADDED "DL" TO DISPLAY SYSLOG; ALSO CAN BE DISPLAYED    BC  *
*          FROM STATUS OPTION.                                        *
*                                                                     *
*  9/07/83 TABLE DISPLAY FOR DSID DATASETS COPIES AN EXTRA BYTE   BC  *
*          TO SECOND LINE.                                            *
*                                                                     *
*  9/29/83 ADDED "DR" COMMAND TO DISPLAY REMOTE QUEUES, AND       BC  *
*          REMOVED REMOTE QUEUES FROM "DO" DISPLAY. ALSO ADDED        *
*          "DT" COMMAND TO DISPLAY REMOTE NODE QUEUE'S.               *
*                                                                     *
*  2/12/84 CHANGED TO USE JES2 INTERFACE SVC 242 FOR CHECKPOINT,  BC  *
*          SPOOL ACCESS. ALSO FOR SUB-SYSTEM REQUESTS.                *
*                                                                     *
*  3/09/84 ADDED SPECIAL "BROWSE" FUNCTION, AND CHANGED SYSLOG    BC  *
*          HANDLING DUE TO MAS. ALSO ADDED SYSTEM-ID FOR ACTIVE       *
*          AND EXECUTING TASKS/JOBS. (EXTRA JCT READ)                 *
*                                                                     *
*  3/15/84 ADDED TEST OF CKPT JOE "RECORDS ALREADY PRINTED"       BC  *
*                                                                     *
*  4/10/84 ADDED STAX                  FOR XMEM SERVICES.         BC  *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'JDF - ENTRY POINT LOGIC'
         PRINT NOGEN
JDF      CSECT                DISPLAY STATUS OF JES2 JOBS
         USING *,13,12,11,10
JDFBGN   EQU   *
         DC    18F'0'
JDFST    EQU   *
         ENTRY JDFST
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFST)
         LA    R3,JDFST-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,STATREQ     INDICATE STATUS
         B     SHOWBGIN
*                                              COMMAND
JDFDQ    EQU   *              DISPLAY INPUT QUEUES
         ENTRY JDFDQ
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDQ)
         LA    R3,JDFDQ-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DQREQ       INDICATE DISPLAY INPUT QUEUES
         B     SHOWBGIN
*
JDFDE    EQU   *              DISPLAY EVERYTHING
         ENTRY JDFDE
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDE-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DEREQ       INDICATE DISPLAY EVERYTHING
         B     SHOWBGIN
*
JDFDA    EQU   *
         ENTRY JDFDA
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDA-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DAREQ       INDICATE DISPLAY ACTIVE
         B     SHOWBGIN
*
JDFDH    EQU   *
         ENTRY JDFDH
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDH-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DHREQ       INDICATE DISPLAY HELD JOBS
         B     SHOWBGIN
*
JDFDL    EQU   *
         ENTRY JDFDL
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDL-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DLREQ       INDICATE DISPLAY OUTPUT JOBS
         B     SHOWBGIN
*
JDFDO    EQU   *
         ENTRY JDFDO
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDO-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DOREQ       INDICATE DISPLAY OUTPUT JOBS
         B     SHOWBGIN
*
JDFDR    EQU   *
         ENTRY JDFDR
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDR-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DRREQ       INDICATE DISPLAY OUTPUT JOBS
         B     SHOWBGIN
*
JDFDT    EQU   *
         ENTRY JDFDT
         SAVE  (14,12)
         LA    R2,0(,R15)     A(JDFDE)
         LA    R3,JDFDT-JDFBGN
         SR    R2,R3
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         LR    R13,R2         SET SAVE AREA
         LR    R12,R2         SET BASE 1
         LA    R5,DTREQ       INDICATE DISPLAY OUTPUT JOBS
         B     SHOWBGIN
*
L4096    DC    F'4096'
*
STATREQ  DC    C'ST'
DQREQ    DC    C'DQ'
DEREQ    DC    C'DE'
DAREQ    DC    C'DA'
DHREQ    DC    C'DH'
DLREQ    DC    C'DL'
DOREQ    DC    C'DO'
DRREQ    DC    C'DR'
DTREQ    DC    C'DT'
*
         TITLE 'JDF - MAINLINE PROCESSING'
SHOWBGIN DS    0H             DISPLAY STATUS OF JES2 JOBS
*
         A     R12,L4096      SET BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
*
         MVC   COMMAND(2),0(R5)    SAVE THE COMMAND ENTERED
         MVI   GOTANYST,C'0'  SET TO "NO STATUS MATCHES"
         MVI   ATABFL,C'0'    NO ACTIVE TABLE
SETCMS   MVI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         MVI   DTABFL,C'0'    NO DD NAME TABLE
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
         XC    ISPARMS(40),ISPARMS
         CLI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         BE    SHOWINIT
         MVC   DSNOTLST(44),CMSDSN1   BROWSE DATASET
         MVC   DSNEDIT(44),CMSDSN2    EDIT   DATASET
*
SHOWINIT DS    0H             DISPLAY STATUS OF JES2 JOBS
         BAL   R7,DOINIT      BUILD THE JOB NAME PARAMETER LIST
*
         B     SHOWAGN2       BEGIN DISPLAY LOOP
*
SHOWAGIN DS    0H
SHOWAGN2 DS    0H             DISPLAY STATUS OF JES2 JOBS
         BAL   R7,DOPENS      OPEN CHECKPOINT AND SPOOL FILES
*
         BAL   R7,GETJQES     READ CKPT FOR JQE'S AND JOE'S
*
         BAL   R7,OUTJQES     FILL IN THE SPF TABLE
*
         BAL   R7,SHOWISP     DISPLAY THE SPF TABLE
*
SHOWDONE DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DATABLE1,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DDTABLE1,                                               X
               NULLENT,                                                X
               DDNAMLST,                                               X
               NOWRITE,                                                X
               REPLACE),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DSTABLE1,                                               X
               NULLENT,                                                X
               DSNAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               DATABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               DDTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
JUSTGOUT DS    0H
*        CLOSE JDFLOG
         TM    HASPCKPT+48,X'10'   IS IT OPEN
         BZ    TSTSPACE            NOPE
         CLOSE HASPCKPT
TSTSPACE DS    0H
         TM    HASPACE+48,X'10'   IS IT OPEN
         BZ    JDFRTRN             NOPE
         CLOSE HASPACE
JDFRTRN  DS    0H
         L     R13,4(R13)     RESTORE THE CALLER'S R13
*
         RETURN (14,12),RC=0  RETURN TO OUR CALLER
         TITLE 'JDF - INITIALIZATION'
DOINIT   DS    0H
*        OPEN  (JDFLOG,(OUTPUT))
         MVC   CKPTREAD(20),CREAD
*
         MVC   HASPCKPT(88),DCB1 MOVE IN CHECKPOINT DCB
         MVC   HASPACE(88),DCB2  MOVE IN SPOOL DCB
         MVC   OUTLIST(96),DCB3  MOVE IN OUTLIST DCB
         MVC   JDFCNTL(96),DCB4  MOVE IN JCL DCB
*
         L     R14,4(,R13)   ORIGINAL SAVE AREA
         L     R14,24(,14)   ORIGINAL R1
         L     R14,0(,14)    R1 PARAMETER
         LH    R15,0(,R14)   PARM LENGTH
         LTR   R15,15        GOT ANY PARMS?
         BZ    JUSTGOUT       NO, DON'T DO ANYTHING
*
         BCTR  R15,0         SUBTRACT 1
         XC    USRJNAME(8),USRJNAME
         EX    R15,JOBNMOVE
         OC    USRJNAME(8),SPACES UPPER-CASE IT
         STC   R15,USRJLENG   SET THE JOB LENGTH
*
         LA    R1,256        TT=0, R=1
         ST    R1,JQEPOINT
         ST    R1,JOEPOINT
         XC    READMTTR(4),READMTTR
         XC    READAREA(4),READAREA
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               JOBLINEP,TABAREA,CHAR,L76),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PIKP,SELCODE,CHAR,L3),                                  X
               VL,MF=(E,ISPARMS)
*
         MVI   CMDDATA,C' '   CLEAR TAB ENTRY AREA
         MVC   CMDDATA+1(39),CMDDATA
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               CMDP,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               ZCMD,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               DCMD,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               DDNLINEP,TABAREA,CHAR,L76),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               DDSL,SELCODE,CHAR,L3),                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               DSLN,MESSAGE,CHAR,L78),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DATABLE1,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DDTABLE1,                                               X
               NULLENT,                                                X
               DDNAMLST,                                               X
               NOWRITE,                                                X
               REPLACE),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DSTABLE1,                                               X
               NULLENT,                                                X
               DSNAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,U8,USERID,SPFMOVE),                        X
               VL,MF=(E,ISPARMS)
*
         LTR   R15,15          IF RETURN CODE IS ZERO,
         BZ    JOBNDONE        OK
*
ABEND12  ABEND 12,DUMP,STEP
*
JOBNDONE DS    0H
         MVC   MSGJNAME(8),USRJNAME   SAVE FOR MESSAGE
*
         MVC   JOBC1NME(5),USERID     FILL IN JOB CARD
         MVC   JOBC1USR(3),USERID+2   FILL IN JOB CARD
         MVC   JOBC1UID(5),USERID     FILL IN JOB CARD
         MVC   JOBC2UID(5),USERID     FILL IN JOB CARD
*
         BR    R7             RETURN TO OUR CALLER
*
*
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
USERID   DC    CL8' '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
JQEPOINT DS    F
JOEPOINT DS    F
         TITLE 'JDF - OPEN SPOOL AND CHECKPOINT'
DOPENS   DS    0H
         CLI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         BER   R7             RETURN TO OUR CALLER
*
         TM    HASPCKPT+48,X'10'  IS IT OPEN
         BZ    DOPENCPT            NOPE
*
         LA    R1,256        TT=0, R=1
         ST    R1,DDIOTRAK
*
         POINT HASPCKPT,DDIOTRAK
         B     TOPENSPC       GO CHECK SYS1.HASPACE
*
DOPENCPT DS    0H
         OPEN  (HASPCKPT,(INPUT))
*
         TM    HASPCKPT+48,X'10'   GOOD OPEN
         BZ    JUSTGOUT            NOPE
*
TOPENSPC DS    0H
         TM    HASPACE+48,X'10'  IS IT OPEN
         BZ    DOPENSPC            NOPE
*
         LA    R1,256        TT=0, R=1
         ST    R1,DDIOTRAK
*
         POINT HASPACE,DDIOTRAK
         BR    R7             RETURN TO OUR CALLER
*
DOPENSPC DS    0H
         OPEN  (HASPACE,(INPUT))
*
         TM    HASPACE+48,X'10'   GOOD OPEN
         BZ    JUSTGOUT            NOPE
*
         BR    R7             RETURN TO OUR CALLER
         TITLE 'JDF - READ JQE S  AND JOE S'
*
GETJQES  DS    0H
         ST    R7,JQESHOLD SAVE THE RETURN ADDRESS
*
         CLI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         BE    XMEMJQES       DON'T DO I/O, USE CROSS-MEMORY SERVICES
*
         L     R9,JQEPTR
         LA    R6,52(,9)      FIRST JQE
         USING JQE,6          ADDRESSABILITY
*
         LA    R8,HASPCKPT
         LA    R7,CKPTECB
         XC    CKPTECB(4),CKPTECB
*
         POINT HASPCKPT,JQEPOINT
*
READCKJQ DS    0H
         READ  (7),SF,(8),(9),'S',MF=(E,CKPTREAD)
         CHECK (7)
         CLC   0(8,R9),=C'JES2 NJE' DO I HAVE THE JQE'S
         BNE   READCKJQ             NOPE
*
         LA    R5,9                 LOOP CONTROL, READ 10 JQE BLOCKS
         LA    R1,256               FIRST TIME THROUGH
         C     R1,JQEPOINT
         BNE   READNXJQ             NOPE, SKIP THE "NOTE"
*
         NOTE  HASPCKPT
         ST    R1,JQEPOINT
*
READNXJQ DS    0H
         A     R9,L4096
         READ  (7),SF,(8),(9),'S',MF=(E,CKPTREAD)
         CHECK (7)
*
         BCT   R5,READNXJQ          READ IN  RECORDS
*
READJQDN DS    0H
         L     R9,JOEPTR
*
         LA    R1,256               FIRST TIME THROUGH
         C     R1,JOEPOINT
         BE    READCKJO             YES, DON'T DO THE POINT
*
         POINT HASPCKPT,JOEPOINT
*
READCKJO DS    0H
         READ  (7),SF,(8),(9),'S',MF=(E,CKPTREAD)
         CHECK (7)
         CLC   0(4,R9),=C'JOT '     DO I HAVE THE JOE'S
         BNE   READCKJO             NOPE
*
         LA    R5,9                 READ IN 10
         LA    R1,256               FIRST TIME THROUGH
         C     R1,JOEPOINT
         BNE   READNXJO             NOPE, SKIP THE "NOTE"
*
         NOTE  HASPCKPT
         ST    R1,JOEPOINT
*
*
READNXJO DS    0H
         A     R9,L4096
         READ  (7),SF,(8),(9),'S',MF=(E,CKPTREAD)
         CHECK (7)
         BCT   R5,READNXJO
*
ENDGET   DS    0H
*
         L     R7,JQESHOLD SAVE THE RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
*
XMEMJQES DS    0H
*
         STAX  DEFER=YES
*
         L     R1,JQEPTR
         L     R0,JOEPTR
         LA    R15,2
         SVC   242
*
         STAX  DEFER=NO
*
         BR    R7
         TITLE 'JDF - BUILD JQE DISPLAY'
OUTJQES  DS    0H
         L     R9,JQEPTR
         ST    R7,JQESHOLD SAVE THE RETURN ADDRESS
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
         MVI   GOTANYST,C'0'      DID I FIND ANY ENTRIES?
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DATABLE1,                                               X
               NULLENT,                                                X
               DANAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               DATABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         MVI   ATABFL,C'1'        AN ACTIVE TABLE
*
         L     R9,JQEPTR
         CLC   0(8,R9),=C'JES2 NJE' DO I HAVE THE JQE'S
         BNE   SHOWDONE             NOPE
*
         LA    R6,52(,9)      FIRST JQE
*        USING JQE,6          ADDRESSABILITY
         LA    R9,786         MAXIMUM NUMBER OF JQE'S
CHECKJQE DS    0H
         MVI   BURSTY,C'0'    NO BURST OUTPUT
         CLI   JQEJNAME,C' '  NEVER USED
         BE    JQEDONE        ALL DONE WITH JQE'S
         CLI   JQEJNAME,X'00' NEVER USED
         BE    JQEDONE        ALL DONE WITH JQE'S
         CLI   JQETYPE,$FREE  FREE QUEUE
         BE    NEXTJQE        GET ANOTHER JQE
         CLC   COMMAND(2),DEREQ    DE REQUEST
         BE    LOOKYJQE      YES => GO DISPLAY EVERYTHING
         CLC   COMMAND(2),STATREQ  STATUS REQUEST
         BE    TSTSTAT        YES, DISPLAY EVERY JOB, TSO, STC
         CLC   COMMAND(2),DLREQ    SYSLOG REQUEST
         BE    TSTLOG         YES, GO CHECK IT
         CLI   JQETYPE,$PURGE JOB HAS BEEN PURGED
         BE    NEXTJQE        GET ANOTHER JQE
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    LOOKYJQE      YES => GO DISPLAY ALL JOBS IN THE SYSTEM
         CLC   COMMAND(2),DRREQ    DO REQUEST
         BE    LOOKYJQE      YES => GO DISPLAY ALL JOBS IN THE SYSTEM
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    LOOKYJQE      YES => GO DISPLAY ALL JOBS IN THE SYSTEM
         TM    JQEFLAG3,QUEJOB IS IT A BATCH JOB
         BNZ   NEXTJQE    NO, GET ANOTHER JQE
         B     CHECKINP      YES => SEE IF IT'S A "DI" REQUEST
*
TSTSTAT  DS    0H
         SR    R15,15
         IC    R15,USRJLENG   SET THE JOB LENGTH
         EX    R15,NAMECLC1   MATCH
         BE    LOOKYJQE       GO PRINT THIS JQE
         EX    R15,NAMECLC2   MATCH
         BE    LOOKYJQE       GO PRINT THIS JQE
         B     NEXTJQE        GET ANOTHER JQE
*
TSTLOG   DS    0H
         CLC   JQEJNAME(8),=C'SYSLOG  '
         BE    LOOKYJQE       GO PRINT THIS JQE
         B     NEXTJQE        GET ANOTHER JQE
*
CHECKINP DS    0H
         CLC   COMMAND(2),DQREQ    DI REQUEST
         BE    LOOKYJQE       GO PRINT THIS JQE
*
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    *+24                IS IT DISPLAY ACTIVE
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    *+14                IS IT DISPLAY ACTIVE
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BNE   CHECKDAR            IS IT DISPLAY ACTIVE
         CLC   JQEJOEB(3),FZEROS   ANY JOE'S???
         BNE   LOOKYJQE        YES, GO PRINT IT
         B     NEXTJQE         NO, SKIP IT
CHECKDAR DS    0H
         CLC   COMMAND(2),DAREQ    DA REQUEST
         BNE   CHECKHJR         GO SEE IF IT IS A "HELD JOBS" REQUEST
         CLC   JQEJNAME(5),=C'INIT '   IS IT AN INITIATOR??
         BE    NEXTJQE         YES, GO GET NEXT ONE
         TM    JQEFLAGS,QUEBUSY EXECUTING??
         BZ    NEXTJQE          NO, GO GET NEXT ONE
         B     LOOKYJQE      YES => GO PRINT IT
CHECKHJR DS    0H
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BNE   JQEDONE        INVALID COMMAND
         TM    JQETYPE,$HARDCPY JOB ON PRINTER/AWAITING PRINT?
         BZ    NEXTJQE          NO
         SR    R1,1
         ICM   R1,3,JQEHLDCT
         SRL   R1,4
         LTR   R1,1
         BZ    NEXTJQE
*
         CLC   JQEJNAME(2),=C'AS'
         BNE   NOTASHJR
         CLC   USERID(2),JQEJNAME
         BNE   NEXTJQE
*
NOTASHJR DS    0H
         CLC   USERID(6),=CL6'BPSCAN'
         BNE   INITAHJR
*                                     IF USER ID IS BPSCAN
         CLI   JQEJCLAS,C'S'          LOOK AT CLASS "S" JOBS
         BE    INITAHJR
*
         CLI   JQEJNAME+7,C'0'        AND PRODUCTION
         BL    NEXTJQE
*
INITAHJR DS    0H
         BAL   R7,INITMSG
         B     JQEOUTSH
*
LOOKYJQE DS    0H
         BAL   R7,INITMSG     INITIALIZE THE OUTPUT MESSAGE
         CLI   MSGJOB#+4,C' ' IF JOB NUMBER IS ZEROS
         BE    JQEDONE        ALL DONE
         CLI   JQETYPE,$PURGE JOB HAS BEEN PURGED
         BE    JQEPURGE       GO PRINT IT
         TM    JQEFLAGS,QUEBUSY   JOB EXECUTING?
         BZ    JQEQINP        NO, SEE IF IT IS IN INPUT
         B     JQEXEQ         YES
JQEQINP  DS    0H
         CLI   JQETYPE,X'43'  JOB AWAITING EXECUTION?
         BE    JQEINPUT       YES
         TM    JQETYPE,$XMIT  JOB ON TRANSMISSION QUEUE
         BO    JQEXMIT        YES
         TM    JQETYPE,$RECEIVE JOB ON SYSOUT RECEIVER?
         BO    JQEINPUT       YES
         TM    JQETYPE,$DUMPQ JOB ON SPOOL OFFLOAD DUMP QUEUE?
         BO    JQEDUMP        YES
         TM    JQETYPE,$OUTPUT JOB ON OUTPUT QUEUE?
         BO    JQEINPUT       YES
         TM    JQETYPE,$HARDCPY JOB ON PRINTER?
         BO    JQEOUTP        YES
         B     NEXTJQE        GET ANOTHER JQE
JQEXEQ   DS    0H
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
*
         CLI   JQEFLAG3,QUETSU  TSO USER ?
         BE    LOGDUSER       YES => SET THE MSG
         CLI   JQEFLAG3,QUESTC  STARTED TASK?
         BNE   EXECJOB         NO => MUST BE BATCH JOB
         MVC   MSGSTYPE(L'ACTIVE),ACTIVE MUST BE A STARTED TASK
         B     GETSYSID        FIND OUT WHAT SYSTEM IT IS RUNNING ON
LOGDUSER MVC   MSGSTYPE(L'LOGGEDON),LOGGEDON TSUSER
         B     GETSYSID        FIND OUT WHAT SYSTEM IT IS RUNNING ON
EXECJOB  MVC   MSGSTYPE(L'EXECUTNG),EXECUTNG EXECUTING JOB
         MVC   MSGQTYPE,JQEJCLAS SET THE JOB CLASS
         OI    MSGQTYPE,HIGHBIT MAKE THE JOB CLASS PRINTABLE
         B     GETSYSID        FIND OUT WHAT SYSTEM IT IS RUNNING ON
GETSYSID DS    0H
*
         L     R7,JCTPTR
         USING JCTSTART,7
         CLC   JCTID(4),=C'JCT '
         BNE   GETSYSIJ
         PACK  DOUBLEWD(8),JCTJOBID+3(5)       JQE JOB NUMBER
         CVB   R1,DOUBLEWD
         CLM   R1,3,JQEJOBNO
         BE    GETSYSI2
*
*
GETSYSIJ DS    0H
         MVC   READMTTR,JQETRAK
         MVC   READAREA,JCTPTR
         BAL   R7,READSPAC
*
         L     R7,JCTPTR
GETSYSI2 DS    0H
         MVC   MSGSTYPE+20(4),JCTEXSID
         DROP  7
*
         B     MSGTEST        GO PRINT THE MESSAGE
*
JQEINPUT DS    0H
*
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
*
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         MVC   MSGSTYPE(L'INPQUE),INPQUE   ON INPUT QUEUE
         MVC   MSGQTYPE,JQEJCLAS SET THE JOB CLASS
         OI    MSGQTYPE,HIGHBIT MAKE THE JOB CLASS PRINTABLE
         B     MSGTEST        GO PRINT THE MESSAGE
JQEXMIT  DS    0H
*
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
*
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         MVC   MSGSTYPE(L'INPQUE),INPQUE   ON INPUT QUEUE
         MVC   MSGQTYPE,JQEJCLAS SET THE JOB CLASS
         OI    MSGQTYPE,HIGHBIT MAKE THE JOB CLASS PRINTABLE
         B     MSGTEST        GO PRINT THE MESSAGE
JQEPURGE DS    0H
*
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
*
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         MVC   MSGSTYPE(L'PURQUE),PURQUE   ON PURGE QUEUE
         B     MSGTEST        GO PRINT THE MESSAGE
JQERECV  DS    0H
*
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
*
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         MVC   MSGSTYPE(L'SYSREC),SYSREC   ON SYSOUT RECEIVER
         B     MSGTEST        GO PRINT THE MESSAGE
JQEDUMP  DS    0H
         CLC   COMMAND(2),DHREQ    HJ REQUEST
         BE    NEXTJQE        INVALID COMMAND
*
         MVC   MSGSTYPE(L'DMPQUE),DMPQUE   ON $DUMP QUEUE
         B     MSGTEST        GO PRINT THE MESSAGE
JQEOUTP  DS    0H
         CLC   USRJNAME(6),=C'BPSCAN'  AM I PROCESSING BPSCAN
         BNE   JQEOUTSH                NO, GO SHOW THE JQE
*
         CLC   JQEJNAME(4),=C'CICS'    IS THIS CICS
         BE    JQEOUTSH                YUP
*
         CLI   JQEJCLAS,C'S'           SPECIAL SCHEDULED JOB
         BE    JQEOUTSH                YUP
*
         CLI   JQEJCLAS,C'0'           PRODUCTION JOB?
         BL    NEXTJQE                 NO, SKIP THE JQE
*
JQEOUTSH DS    0H
         MVC   MSGSTYPE(L'OUTQUE),OUTQUE   ON OUTPUTQUEUE
         CLC   JQEJOEB(3),FZEROS  IS THERE A WORK JOE?
         BE    JQENOWRK           NOPE
         CLC   JQEJOEB(3),BLOCKS4+1 IS THE OFFSET MORE THAN 4 BLOCKS
         BH    JQENOWRK           YUP, SKIP THIS
         SR    R14,14
         XC    ACOUNT(4),ACOUNT       RECORD COUNT
         XC    PCOUNT(4),PCOUNT       RECORD COUNT
         XC    SCOUNT(4),SCOUNT       RECORD COUNT
         ICM   R14,7,JQEJOEB
         A     R14,JOEPTR     PICK UP FIRST WORK JOE POINTER
         ST    R14,12(,R13)   SAVE JOE POINTER
         USING JOEDSECT,14
         TM    JQEFLAGS,X'E0'     IS THE JOB HELD
         BZ    JQEJOENH           NO. CONTINUE CHECKING
         MVC   MSGSTYPE(L'OUTQUE),SPACES   CLEAR THE FIELD
         MVC   MSGSTYPE(L'HOLD),HOLD       HELD
         B     JQEOUTLP
JQEJOENH DS    0H
         TM    JOEDEVID,X'0F'     IS THIS AN XWTR
         BNO   NOTXWTR            NO. CONTINUE CHECKING
         MVC   MSGSTYPE+3(12),SPACES
         MVC   MSGSTYPE+3(4),=C'XWTR' YES.
         B     JQEOUTLP
NOTXWTR  DS    0H
         CLC   JOEREMOT,=H'0'  REMOTE DEVICE?
         BNE   RMTDEV          YES => OUTPUT IT
         SR    R1,R1            FOR THE INSERT CHARACTER
         IC    R1,JOEDEVID      DEVICE TYPE
         SRL   R1,4             RIGHT JUSTIFIED
         MH    R1,DEVTYPEL      TYPE * LENGTH OF A DEVICE ENTRY
         LA    R1,DEVTABLE(R1)   A(DEVICE TYPE)
         MVC   MSGSTYPE+3(12),SPACES
         MVC   MSGSTYPE+3(8),1(R1)      PUT IN THE DEVICE TYPE
         CLI   JOEDEVID,0        INTERNAL READER?
         BE    JQEOUTLP          YES => GIVE THE USER THE INFO
         SR    R15,R15           FOR THE INSERT CHARACTER
         IC    R15,JOEDEVID+2    DEVICE NUMBER
         CVD   R15,DOUBLEWD      IN PACKED DECIMAL
         IC    R15,0(R1)         OFFSET TO WHERE THE DEV # GOES
         LA    R15,MSGSTYPE+3(R15) A(WHERE THE DEV # GOES)
         MVC   1(L'DIGITS3,R15),DIGITS3 SET UP THE EDIT OF 3 DIGITS
         EDMK  0(L'DIGITS3+1,R15),DOUBLEWD+6 DEV # IN EBCDIC
         MVC   0(L'DIGITS3+1,R15),0(R1) ADJUST FOR BLANKS
*
*
         B     JQEOUTLP       GO EXIT
*
RMTDEV   DS    0H
*
         SR    R0,R0          NOT REALLY NEEDED NOW
         LH    R0,JOEREMOT      REMOTE NUMBER
         CVD   R0,DOUBLEWD    IN PACKED DECIMAL
         MVC   MSGSTYPE+3(12),SPACES
         MVI   MSGSTYPE+3,C'R'  INDICATE A REMOTE DEVICE
         MVC   MSGSTYPE+5(L'THREEPT),THREEPT SET UP THE EDIT MASK
         EDMK  MSGSTYPE+4(L'THREEPT),DOUBLEWD+6 CONVERT TO EBCDIC
         MVC   MSGSTYPE+4(L'THREEPT),0(R1) ADJUST FOR BLANKS
         LA    R1,MSGSTYPE+4    A(SPOT JUST BEFORE POSSIBLE SEP)
FINDPT   LA    R1,1(R1)       A(NEXT BYTE)
         CLI   0(R1),C'.'     FOUND THE SEPARATOR?
         BNE   FINDPT         NO => KEEP LOOKING
         SR    R15,R15        FOR THE INSERT CHARACTER
         IC    R15,JOEDEVID     DEVICE TYPE INDICATOR
         SRL   R15,3          RIGHT JUSTIFIED
         LA    R15,RMTDEVS-HIGHBIT/8(R15) A(DEVICE TYPE)
         MVC   1(2,R1),0(R15) PUT IN THE DEVICE TYPE
         MVC   3(1,R1),JOEDEVID     THE DEVICE NUMBER
         OI    3(R1),C'0'     MAKE THE DEVICE NUMBER PRINTABLE
         CLC   0(4,R1),=C'.NT0'
         BNE   JQEOUTLP       GO EXIT
         MVC   0(4,R1),=C' QUE'
         B     JQEOUTLP       GO EXIT
***********************************************************************
*   DEVICE TABLES FOR LOCAL AND REMOTE DEVICES                        *
***********************************************************************
         DS    0H
DEVTABLE DC    AL1(0),CL8'OUTPUT Q',AL1(6),CL8'READER Q'
         DC    AL1(7),CL8'PRINTER',AL1(5),CL8'PUNCH  Q'
DEVTYPEL DC    AL2((*-DEVTABLE)/4)
RMTDEVS  DC    C'**',C'RD',C'PR',C'PU'
         DC    C'012345678L1.ST1  ABCDEFGHIJKLMNOP'
         EJECT
JQEOUTLP DS    0H
         L     R14,12(,R13)   RESTORE WORK JOE POINTER
         XC    ACOUNT(4),ACOUNT    RESET RECORD COUNT
         XC    PCOUNT(4),PCOUNT    RESET RECORD COUNT
         XC    SCOUNT(4),SCOUNT    RESET RECORD COUNT
JQEOUTLN DS    0H
         MVI   JOERECCT,X'00'      WIPE OUT LEFT BYTE
         SR    R1,1
         C     R1,JOERECCT     ANY LINES
         BE    JOERECLP        NOPE,   GET NEXT WORK JOE
*
*  FIRST SET THE RECORD COUNTER BASED ON SYSOUT CLASS
*
         LA    R3,SCOUNT      SET DEFAULT
         L     R4,SCOUNT      SET DEFAULT
         CLI   JOECURCL,C'S'  IS THIS OK?
         BE    JQECKPTJ       GO DO IT
*
         LA    R3,PCOUNT      SET DEFAULT
         L     R4,PCOUNT      SET DEFAULT
         CLI   JOECURCL,C'C'  IS THIS OK?
         BE    JQECKPTJ       GO DO IT
         CLI   JOECURCL,C'P'  IS THIS OK?
         BE    JQECKPTJ       GO DO IT
*
         LA    R3,ACOUNT      SET DEFAULT
         L     R4,ACOUNT      SET DEFAULT
*
JQECKPTJ DS    0H
*
*  PICK UP CHECKPOINT JOE FOR LINES ALREADY PRINTED
*
*  NOTE: I ADDED A TEST FOR CHPT "RECORDS ALREADY PRINTED" BIGGER THAN
*        RECORD COUNT BECAUSE I WAS GETTING SOME BOGUS NUMBERS.
*        I WONDER IF THE CKPT JOE HAS RESIDUAL DATA OR SOMETHING.
*
         ICM   R1,7,JOECKPTB  OFFSET CHECKPOINT JOE
         LTR   R1,1           POINTER INVALID,
         BZ    JQEJOENC       SKIP THIS
         L     R15,JOERECCT   TOTAL RECORD COUNT FROM WORK JOE
         LR    R14,R1         CLOBBER WORK JOE BASE
         A     R14,JOEPTR     ADD BASE ADDR OF JOT TO GET CKPT JOE
         C     R15,JOECRECN   COMPARE TOTAL VS ALREADY PRINTED
         BL    *+8            IF TOTAL IS LESS, AVOID NEGATIVE
         S     R15,JOECRECN SUBTRACT RECORDS ALREADY PRINTED FROM TTL
         A     R15,0(,R3)     ACCUM   RECORD COUNT
         ST    R15,0(,R3)     SAVE    RECORD COUNT
         B     JQEJOECH       GO GET CHAR JOE
*
JQEJOENC DS    0H IF INVALID CHECKPOINT JOE
*
         L     R15,0(,R3)     RESTORE RECORD COUNT
         A     R15,JOERECCT
         ST    R15,0(,R3)     SAVE    RECORD COUNT
*
JQEJOECH DS    0H
*
*  PICK UP CHARACTERISTIC JOE FOR FORM NUMBER, BURST FLAG
*
         L     R14,12(,R13)   RESTORE WORK JOE POINTER
         SR    R1,1
         ICM   R1,7,JOECHARB  OFFSET CHARACTERISTIC JOE
         LTR   R1,1           POINTER INVALID,
         BZ    JOERECLP       SKIP THIS.
         LR    R14,R1
         A     R14,JOEPTR     ADD BASE ADDR OF JOT
*
         TM    JOECFLAG,$JOEBRST BURST OUTPUT?
         BZ    *+8            NO BURST OUTPUT
         MVI   BURSTY,C'1'  YES, BURST OUTPUT
*
         CLC   JOEFORM(4),FORMS1       IS IT A DUP OF FORM
         BE    JOERECLP                GET NEXT WORK JOE
         CLC   JOEFORM(4),FORMS2       IS IT A DUP OF FORM
         BE    JOERECLP                GET NEXT WORK JOE
         CLC   JOEFORM(4),FORMS3        S IT A DUP OF FORM
         BE    JOERECLP                GET NEXT WORK JOE
*
         CLC   FORMS1(4),=C'STD1'     IS IT STD1
         BNE   *+14                   NO, CAN'T USE THIS ONE
         MVC   FORMS1(4),JOEFORM      MOVE IT IN
         B     JOERECLP                GET NEXT WORK JOE
*
         CLC   FORMS2(4),=C'STD1'     IS IT STD1
         BNE   *+14                   NO, CAN'T USE THIS ONE
         MVC   FORMS2(4),JOEFORM      MOVE IT IN
         B     JOERECLP                GET NEXT WORK JOE
*
         CLC   FORMS3(4),=C'STD1'     IS IT STD1
         BNE   *+14                   NO, CAN'T USE THIS ONE
         MVC   FORMS3(4),JOEFORM      MOVE IT IN
*
JOERECLP DS    0H
         L     R14,12(,R13)   RESTORE WORK JOE POINTER
         CLC   JOEJQNXB(3),FZEROS  IS THERE ANOTHER ONE
         BE    JQEOUTMS           NOPE
         CLC   JOEJQNXB(3),BLOCKS4+1 IS THE OFFSET MORE THAN 4 BLOCKS
         BH    JQEOUTMS            YUP, SKIP THIS
*
*  NOW POINT TO NEXT WORK JOE
*
         SR    R1,1
         ICM   R1,7,JOEJQNXB
         LR    R14,R1
         A     R14,JOEPTR     PICK UP NEXT WORK JOE POINTER
         ST    R14,12(,R13)   SAVE    WORK JOE POINTER
         B     JQEOUTLN       LOOP TO ADD IN RECORD COUNT
         EJECT
JQEOUTMS DS    0H
         L     R15,ACOUNT     SYSOUT CLASS A LINE COUNT
         A     R15,PCOUNT     SYSOUT CLASS C LINE COUNT
         A     R15,SCOUNT     SYSOUT CLASS S LINE COUNT
         LTR   R15,15             ANY LINES
         BZ    JQENOWRK           NOPE
         L     R15,ACOUNT     RESTORE RECORD COUNT
         XC    ACOUNT(4),ACOUNT    RESET RECORD COUNT
         CVD   R15,DOUBLEWD        RECORD COUNT
         MVC   MSGALINE(10),EDIT10
         ED    MSGALINE,DOUBLEWD+4
*
         L     R15,PCOUNT     SYSOUT CLASS C LINE COUNT
         XC    PCOUNT(4),PCOUNT    RESET RECORD COUNT
         CVD   R15,DOUBLEWD        RECORD COUNT
         MVC   MSGPLINE(10),EDIT10
         ED    MSGPLINE,DOUBLEWD+4
*
         L     R15,SCOUNT     SYSOUT CLASS S LINE COUNT
         XC    SCOUNT(4),SCOUNT    RESET RECORD COUNT
         CVD   R15,DOUBLEWD        RECORD COUNT
         MVC   MSGSLINE(10),EDIT10
         ED    MSGSLINE,DOUBLEWD+4
         B     JQETNODE            CHECK FOR RMT PRINT NODE
JQENOWRK DS    0H
         MVC   MSGSTYPE+8(17),=CL17', NO READY OUTPUT'
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    NEXTJQE       YES => SKIP THIS, ONLY SHOW READY OUTPT
JQETNODE DS    0H
*
         LH    R15,JQEPRNOD  PICK UP PRINT NODE
         CH    R15,JQEXEQND  COMPARE EXECUTION NODE (ME)
         BE    MSGTEST       IT'S THE SAME, SO SKIP IT
JQESNODE DS    0H
         CVD   R15,DOUBLEWD     NODE NUMBER FOR PRINTED OUTPUT
         OI    DOUBLEWD+7,X'0F' MAKE PRINTABLE
         MVI   MSGSPCST,C'N'
         UNPK  MSGSPCST+1(1),DOUBLEWD+7(1)
         CLC   MSGSPCST(2),=C'N1'  LOCAL NODE
         BNE   MSGTEST        GO PRINT THE MESSAGE
         MVC   MSGSPCST(2),SPACES  LOCAL NODE
MSGTEST  DS    0H
         CLC   COMMAND(2),DQREQ    DI REQUEST
         BNE   MSGOTREQ            NO, TEST FOR "DO"
         CLC   MSGSTYPE(L'INPQUE),INPQUE   ON INPUT QUEUE
         BNE   NEXTJQE        NO, SKIP IT
         B     MSGNEXT        GO PRINT THIS JQE
*
MSGOTREQ DS    0H
         CLC   COMMAND(2),DTREQ    DT REQUEST
         BE    MSGDTREQ            YES, GO TEST FOR OUTPUT QUEUE TYPE
         CLC   COMMAND(2),DRREQ    DR REQUEST
         BE    MSGDRREQ            YES, GO TEST FOR OUTPUT QUEUE TYPE
         CLC   COMMAND(2),DOREQ    DO REQUEST
         BE    MSGDOREQ            YES, GO TEST FOR OUTPUT QUEUE TYPE
         B     MSGNEXT        GO PRINT THIS JQE
*
MSGDRREQ DS    0H
         CLI   MSGSPCST,C'N'  REMOTE NODE?
         BE    NEXTJQE       YES, SKIP IT
         CLI   MSGSTYPE+3,C'R' REMOTE PRINT/PUNCH?
         BE    MSGNEXT        GO PRINT THIS JQE
         B     NEXTJQE        NO, SKIP IT
*
MSGDOREQ DS    0H
         CLI   MSGSPCST,C'N'  REMOTE NODE?
         BE    NEXTJQE       YES, SKIP IT
         CLI   MSGSTYPE+3,C'O' OUTPUT - LOCAL PRINT QUEUE
         BE    MSGNEXT        GO PRINT THIS JQE
         CLI   MSGSTYPE+3,C'P' OUTPUT - ACTUALLY PRINTING
         BE    MSGNEXT        GO PRINT THIS JQE
         CLI   MSGSTYPE,C'H' OUTPUT - JOB HAS BEEN HELD
         BE    MSGNEXT        GO PRINT THIS JQE
         B     NEXTJQE        NO, SKIP IT
*
MSGDTREQ DS    0H
         CLI   MSGSPCST,C'N'
         BNE   NEXTJQE        NO, SKIP IT
         B     MSGNEXT        GO PRINT THIS JQE
*
MSGNEXT  DS    0H
         LA    R7,DANAMLST    NAME LIST
         ST    R7,DNMADDRH    SAVE FOR OUTMSG
         LA    R7,DATABLE1    TABLE NAME
         ST    R7,TABADDRH    SAVE FOR OUTMSG
*
         CLC   FORMS1(4),=C'STD1'     IS IT STD1
         BE    *+10                   DON'T DISPLAY IT
         MVC   MSGFORM(4),FORMS1      MOVE IT IN
*
         CLI   BURSTY,C'0'    ANY BURST OUTPUT
         BE    *+8            NOPE
         MVI   MSGBURST,C'Y'  SET BURST FLAG
         MVI   BURSTY,C'0'    NO BURST OUTPUT
*
         BAL   R7,OUTMSG      OUTPUT THE MESSAGE
NEXTJQE  DS    0H
*
         MVC   FORMS1(4),=C'STD1'    SET IT STD1
         MVC   FORMS2(4),=C'STD1'    SET IT STD1
         MVC   FORMS3(4),=C'STD1'    SET IT STD1
*
         LA    R6,JQELNGTH(6) A(NEXT JQE)
         BCT   R9,CHECKJQE CHECKED ALL RETURNED JQES?
JQEDONE  DS    0H
         L     R7,JQESHOLD YES => RETURN
         BR    R7             TO OUR CALLER
BURSTY   DS    CL1
FORMS1   DC    CL4'STD1'
FORMS2   DC    CL4'STD1'
FORMS3   DC    CL4'STD1'
BLOCKS4  DC    F'16000'  LIMIT FOR JOE OFFSETS
         TITLE 'JDF -  JQE DISPLAY USING TABLE MGMT SERVICES'
SHOWISP  DS    0H
         ST    R7,JQESHOLD     SAVE     RETURN ADDRESS
         CLI   GOTANYST,C'0'
         BNE   GOTSOMEJ
*
GOTNONE  DS    0H
         MVC   MESSAGE(44),NOJOBMSG
         LA    R7,DANAMLST    NAME LIST
         ST    R7,DNMADDRH    SAVE FOR OUTMSG
         LA    R7,DATABLE1    TABLE NAME
         ST    R7,TABADDRH    SAVE FOR OUTMSG
         BAL   R7,OUTMSG
*
GOTSOMEJ DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               DATABLE1),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DATABLE1,                                               X
               JDFDATAB),                                              X
               VL,MF=(E,ISPARMS)
         B     TSTRTRNC        TEST RETURN CODES
DNOTAUTH DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DATABLE1,                                               X
               JDFDATAB,JDFMSG2),                                      X
               VL,MF=(E,ISPARMS)
TSTRTRNC DS    0H
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BE    GOTSTUFF       YUP, GO PROCESS IT
DUNWITIT DS    0H
         L     R7,JQESHOLD     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'JDF -  COMMAND INPUT PROCESSING'
*
GOTSTUFF DS    0H
         MVC   SELSAVE(3),SELCODE    SAVE SEL CODE
         OC    SELSAVE(3),SPACES   UPPER-CASE IT
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
         MVC   JOBNUMS(5),TABAREA+3  SAVE JES JOB NUMBER
         OC    JOBNUMS(5),CZEROS     SET LEADING ZEROS
         PACK  JOBNUMP(8),JOBNUMS
         CVB   R1,JOBNUMP
         STH   R1,JOBNUMH
         MVC   JOBNAMS(8),TABAREA+9  SAVE JOB NAME
         MVC   BROJOB(8),TABAREA+9  SAVE JOB NAME
         MVC   JOBTYPE(3),TABAREA    SAVE JOB TYPE, STC, TSU, OR JOB
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBGET,                                                 X
               DATABLE1),                                              X
               VL,MF=(E,ISPARMS)
         C     R15,F8         RC=8 MEANS CRP IS AT TOP
         BE    GOTCOMND       THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   JOBTYPE,C' '   IF NO JOB TYPE,
         BE    SHOWAGIN           MUST BE A MISTAKE
*
         B     USERAUTH       GO CHECK AUTHORITY
*
         TITLE 'JDF -  CHECK USER COMMAND AUTHORITY'
USERAUTH DS    0H
*
* CHECK USER FOR AUTHORITY
*
*
         CLC   JOBNAMS(6),=C'SYSLOG' ANYONE CAN LOOK AT SYSLOG
         BE    IAMAUTH          YUP
*
         CLC   USERID(2),=C'AS'      TECHSERV USER-ID
         BE    IAMAUTH          YUP
*
         CLC   USERID(3),=C'MVS'     OPERATIONS
         BE    IAMAUTH          YUP
*
         CLC   USERID(2),=C'BP'      PRODSERV USER-ID
         BE    IAMAUTH          YUP
*
         CLC   SELSAVE(3),=C'PUP'    BUMP PRINT PRIORITY
         BE    NOTAUTH            DON'T DO IT
*
         CLC   USERID(5),=C'FFTCS'   IS THIS SPECIAL VM CICS USER
         BNE   USERJNAM              NO, JUST TEST JOBNAME VS USERID
*
         CLC   JOBNAMS(3),=C'ITP'    IS THIS SPECIAL VM CICS JOB
         BE    IAMAUTH          YUP
*
USERJNAM DS    0H
         CLC   USERID(5),JOBNAMS    DOES USERID MATCH JOBNAME
         BE    IAMAUTH          YUP
*
         CLC   USERID(5),JOBNAMS+3  DOES USERID MATCH JOBNAME
         BE    IAMAUTH          YUP
*
* SINCE THERE IS NO JOB NAME CONVENTION THAT TIES IN THE USERID,
* WE MAY HAVE TO READ IN THE JCT.
*
*
         L     R9,JQEPTR            PICK UP JQE BUFFER
         LA    R6,52(,9)      FIRST JQE
*        USING JQE,6          ADDRESSABILITY
         LA    R9,786         MAXIMUM NUMBER OF JQE'S
JCGETJQE DS    0H
         LH    R7,JOBNUMH     JES JOB NUMBER OF JOB SELECTED
         CH    R7,JQEJOBNO    MATCH?
         BE    JCTPOINT       YUP
         LA    R6,JQELNGTH(,6) BUMP
         BCT   R9,JCGETJQE     LOOP
*
         B     SHOWAGIN
*
JCTPOINT DS    0H
         CLC   FZEROS(4),JQETRAK    JCT DISK ADDRESS
         BE    SHOWAGIN
*
*   IF I'VE ALREADY READ THE JCT, DON'T READ IT AGAIN
*
         L     R9,JCTPTR
         USING JCTSTART,9
         CLC   JCTID(4),=C'JCT '
         BNE   JCTPOIN1
         PACK  DOUBLEWD(8),JCTJOBID+3(5)       JQE JOB NUMBER
         CVB   R1,DOUBLEWD
         CLM   R1,3,JQEJOBNO
         BE    JCTPOIN2
*
JCTPOIN1 DS    0H
         MVC   READMTTR(4),JQETRAK    JCT DISK ADDRESS
         MVC   READAREA(4),JCTPTR
         BAL   R7,READSPAC
*
         CLC   JCTID(4),=C'JCT '
         BNE   SHOWAGIN
*
JCTPOIN2 DS    0H
         LA    R9,JCTNJHDR
         USING NJHDSECT,9
         CLC   NJHGUSID(5),USERID   SUBMITTING USER-ID VS MINE
         BE    IAMAUTH          YUP
         DROP  9
*
         B     NOTAUTH            DON'T DO IT
IAMAUTH  DS    0H
*
         CLI   SELSAVE,C'S'   IS IT SHOWJOB
         BE    SHOWJOB        THEN GO DO IT
*
         CLI   SELSAVE,C'A'   IS IT A "$AJ" COMMAND
         BE    ACTAJOB        THEN GO DO IT
*
         CLC   SELSAVE(3),=CL3'RDR'   IS IT ROUTE TO CMS SPOOL
         BE    JOBRDR         THEN GO DO IT
*
         CLI   SELSAVE,C'B'   IS IT A BPSCAN
         BE    SHOWJOB        THEN GO DO IT
*
         CLI   SELSAVE,C'C'   IS IT A CANCEL
         BE    JOBCAN         THEN GO DO IT
*
         CLI   SELSAVE,C'H'   IS IT A "$HJ"
         BE    HOLDAJOB       THEN GO DO IT
*
         CLC   SELSAVE(3),=C'PUP'    BUMP PRINT PRIORITY
         BE    PRTPRIO               GO DO IT
*
         CLI   SELSAVE,C'P'   IS IT A PURGE
         BE    JOBPUR         THEN GO DO IT
*
         CLC   SELSAVE(3),=CL3'RLS'   IS IT RELEASE FROM HELD STATUS
         BE    JOBRLS         THEN GO DO IT
*
         CLI   SELSAVE,C'R'   IS IT A REQUEUE
         BE    JOBREQ         THEN GO DO IT
*
         CLI   SELSAVE,C'T'   IS IT A "$TJ" COMMAND
         BE    JOBTJ          THEN GO DO IT
*
         MVI   SELSAVE,C'S'  SET IT SHOWJOB
         B     SHOWJOB        THEN GO DO IT
*
NOTAUTH  DS    0H
         B     DNOTAUTH       ALL DONE
*
         TITLE 'JDF -  INVOKE XDF (EXECUTION DISPLAY)'
*
SHOWXDF  DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHOWAGIN       NOPE
*
         LINK EP=XDF
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  INVOKE XDF (EXECUTION DISPLAY)'
*
SHOWDIF  DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHOWAGIN       NOPE
*
         LINK EP=DIF
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  INVOKE CDF (CONSOLE DISPLAY)'
*
SHOWCONS DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHOWAGIN       NOPE
*
         LINK EP=CDF
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  PERFORM $TJ PROCESSING'
*
JOBTJ    DS    0H
*
         CLI   SELSAVE+1,C'C' IS IT A "$TJ000,C=X" COMMAND
         BE    JOBTJC         THEN GO DO IT
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
JOBTJC   DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'T'          ALTER
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
         MVC   CMDLINE+8(3),COMMAC     SYNTAX
         MVC   CMDLINE+11(1),SELSAVE+2 NEW CLASS
*
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  INCREASE PRIORITY'
*
PRTPRIO  DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHOWAGIN       NOPE
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'T'          ALTER
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
         MVC   CMDLINE+8(6),PUPDATA    P=99
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  HOLD A JOB'
*
HOLDAJOB DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'H'          HOLD
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
*
*
HOLDAJBC DS    0H
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  RELEASE A HELD JOB'
*
ACTAJOB  DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'A'          HOLD
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
*
*
ACTAJOBC DS    0H
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
         TITLE 'JDF -  JOB CANCEL    PROCESSING'
JOBCAN   DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'C'        CANCEL
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
*
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
         B     SHOWAGIN       GO REPEAT THE SCREEN
SSREQRC  DS    F
ABEND5   ABEND 5,DUMP,STEP
ABEND6   ABEND 6,DUMP,STEP
         TITLE 'JDF -  JOB PURGE     PROCESSING'
JOBPUR   DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'P'          PURGE
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
*
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  RELEASE HELD OUTPUT FOR A JOB'
JOBRLS   DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   JOBRLCMS       NOPE, MUST BE CMS, SO GO DO IT
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DATABLE1,                                               X
               JDFDATAB,JDFMSG3),                                      X
               VL,MF=(E,ISPARMS)
         B     SHOWAGIN       GO REPEAT THE SCREEN
JOBRLCMS DS    0H
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVI   CMDLINE+1,C'O'          RELEASE
         MVC   CMDLINE+2(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+3(5),JOBNUMS    JES NUMBER
*
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
        TITLE 'JDF -  ROUTE A JOBS OUTPUT TO A CMS USERS READER QUEUE'
JOBRDR   DS    0H
*
         MVI   CMDLINE,C' '
         MVC   CMDLINE+1(71),CMDLINE
         MVI   CMDLINE,C'$'
         MVC   CMDLINE+1(7),=C'RALL,J=' ELEASE
         MVC   CMDLINE+8(1),JOBTYPE    TYPE, "JOB", "STC", OR "TSU"
         MVC   CMDLINE+9(5),JOBNUMS    JES NUMBER
         MVC   CMDLINE+14(6),=C',D=CIS'
*
         BAL   R7,ISSUECMD         ISSUE JES COMMAND VIA SVC 34
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  JOB REQUEUE   SUBROUTINE'
JOBREQ   DS    0H
*
         MVC   RDEST(5),SPACES       SET FLAG
*
         CLI   SELSAVE+1,C'Q'         IS THIS "RQ"
         BE    RQ1BYTE                YUP, ONE-BYTE FORMAT
*
RQ2BYTE  DS    0H
*   RE-QUEUE COMMAND WAS CODED AS "R00", WHERE "00" = RMT#
         MVC   RDEST(2),SELSAVE+1     SAVE THE DEST
         MVI   SELSAVE+2,C'A'         SET NEW CLASS
         B     CLSREQ                 GO DO IT
*
RQ1BYTE  DS    0H
*   RE-QUEUE COMMAND WAS CODED AS "RQ0", WHERE "0" = RMT# OR CLASS
*
         CLI   SELSAVE+2,C'0'        AM I CHANGING THE CLASS?
         BL    CLSREQ               NOT NUMERIC, JUST CHANGE THE CLASS
*
*  I AM RE-QUEUEING TO A NUMERIC VALUE, SO IT MUST BE A REMOTE DEST.
*
         MVC   RDEST(1),SELSAVE+2     SAVE THE DEST
         MVI   SELSAVE+2,C'A'         SET NEW CLASS
*
CLSREQ   DS    0H
*
         CLI   SELSAVE+2,X'C0'         ALPHANUMERIC CLASS
         BH    *+8                     YUP
         MVI   SELSAVE+2,C'A'          NOPE, SO SET IT TO DEFAULT
         MVC   TOCMDCLS(1),SELSAVE+2
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BE    SETSSOB        YUP
*
         MVC   TOCMDJOB(5),JOBNUMS
         MVC   CMDLINE(17),TOCMD
*
         CLI   RDEST,C' '     IS THERE A REMOTE DEST
         BE    *+10           NOPE
         MVC   CMDLINE(28),TOCMD
*
         BAL   R7,ISSUENJE    SUBMIT COMMMAND VIA NJE/RSCS INTERFACE
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
TOCMD    DC    CL4'$TOJ'
TOCMDJOB DS    CL5
         DC    CL7',ALL,Q='
TOCMDCLS DS    CL1
         DC    CL3',D='
RQRMT    DC    CL3'N1R'
RDEST    DS    CL1
         DC    CL4' '
*
SETSSOB  DS    0H
***                                                                 ***
*   SET UP SSOB TO ISSUE IEFSSREQ TO REQUEUE A JOB                    *
***                                                                 ***
*
         XC    SSSOBGN(SSSOSIZE),SSSOBGN
         MVC   SSOBFUNC(2),H1        SET FUNCTION TO SYSOUT
         MVC   SSSOPGMN,SPACES     INITIALIZE WRITER NAME TO BLANKS
         MVC   SSSOLEN(2),=AL2(SSSOSIZE)
         OI    SSSOUFLG,SSSOSETC
         OI    SSSOUFLG,SSSORLSE
         OI    SSSOFLG1,SSSOHLD
         OI    SSSOFLG1,SSSOSJBN
         OI    SSSOFLG1,SSSOSJBI
         OI    SSSOFLG1,SSSOSCLS  INCLUDE CLASS
         MVC   SSSOCLSL(8),=CL8'X '   SET CLASS TO "X" ONLY
         OI    SSSOFLG2,SSSOCTRL
         MVC   SSSOJOBN(8),JOBNAMS JOB NAME
         MVC   SSSOJOBI+3(5),JOBNUMS JOB ID
         MVC   SSSOJOBI(3),JOBTYPE JOB TYPE, "JOB", "STC", OR "TSU"
         MVC   SSSOCLAS(1),SELSAVE+2 SET NEW CLASS
***                                                                 ***
**       NOW CHECK TO SEE WHETHER I AM CHANGING THE CLASS OR DEST
***                                                                 ***
         CLI   RDEST,C' '     IS THERE A REMOTE DEST
         BE    *+14           NOPE
         MVC   SSSODEST(8),RQRMT
         OI    SSSOUFLG,SSSOROUT  USE DESTINATION
*
***                                                                 ***
**     IEFSSREQ                                                      **
***                                                                 ***
         LA    R1,SSOBPTR
         LA    R15,4               SSREQ FLAG
         SVC   242
*
         ST    R15,SSREQRC         SAVE RETURN CODE
*
         SR    R15,15              CLEAR REGISTER
         C     R15,SSREQRC         DID I HAV RC=0 FROM SSREQ?
         BNE   BADREQ              NOPE
         C     R15,SSOBRETN        DID I HAV RC=0 FROM SSREQ?
         BNE   BADREQ              NOPE
         B     SHOWAGIN       GO REPEAT THE SCREEN
         B     ENDREQ         GO REPEAT THE SCREEN
*
BADREQ   DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DATABLE1,                                               X
               JDFDATAB,JDFMSG1),                                      X
               VL,MF=(E,ISPARMS)
         B     SHOWAGIN       GO REPEAT THE SCREEN
ENDREQ   DS    0H
         B     SHOWAGIN       GO REPEAT THE SCREEN
*
JDFMSG1  DC    CL8'JDF001'
JDFMSG2  DC    CL8'JDF002'
JDFMSG3  DC    CL8'JDF003'
*
GOTCOMND DS    0H
         TR    CMDDATA(40),CAPSONLY  UPPER-CASE COMMAND INPUT
         CLC   CMDDATA(4),=C'DUMP'   IS IT A DUMP REQUEST
         BE    S0C1                  YUP, JUST GET A S0C1
*
         CLI   CMDDATA,C'R'          IS IT A "R" FOR REPEAT
         BE    RPTCOMND              YUP, JUST REPEAT
*
         MVC   SELSAVE(40),CMDDATA   MOVE IT IN
RESETCMD DS    0H
*
         CLC   SELSAVE(2),=C'DC'
         BE    SHOWCONS       GO SELECT THE SCREEN
*
         CLC   SELSAVE(2),=C'DI'
         BE    SHOWDIF        GO SELECT THE SCREEN
*
         CLC   SELSAVE(2),=C'DX'
         BE    SHOWXDF        GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DAREQ
         CLC   SELSAVE(2),DAREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DEREQ
         CLC   SELSAVE(2),DEREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DHREQ
         CLC   SELSAVE(2),DHREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DQREQ
         CLC   SELSAVE(2),DQREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DOREQ
         CLC   SELSAVE(2),DOREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DRREQ
         CLC   SELSAVE(2),DRREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DTREQ
         CLC   SELSAVE(2),DTREQ
         BE    SHOWAGIN       GO SELECT THE SCREEN
*
         MVC   COMMAND(2),DLREQ
         CLC   SELSAVE(2),DLREQ
         BE    SHOWAGIN       RESHOW THE SCREEN
*
         MVC   COMMAND(2),STATREQ
         B     SHOWAGIN       GO SELECT THE SCREEN
*
S0C1     DC    H'0'                  SOC1
*
RPTCOMND DS    0H
         MVI   TABAREA,C' '   CLEAR TAB ENTRY AREA
         MVC   TABAREA+1(79),TABAREA
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               DATABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ATABFL,C'0'        NO ACTIVE TABLE
*
         XC    ISPARMS(40),ISPARMS
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'JDF -  BUILD DDNAME TABLE'
SHOWJOB  DS    0H
*
*              INITIALIZE CONSTANTS FOR A NEW JOB
*
*
         LA    R7,DDNAMLST    NAME LIST
         ST    R7,DNMADDRH    SAVE FOR OUTMSG
         LA    R7,DDTABLE1    TABLE NAME
         ST    R7,TABADDRH    SAVE FOR OUTMSG
*
         XC    IXTJOBID,IXTJOBID
*
         MVC   CONDZERO(3),P3ZERO INITIALIZE COUNTERS
         MVC   CONDNUM(3),P3ZERO  INITIALIZE COUNTERS
         MVC   CATNUM(12),CONDNUM                     FOR BPSCAN
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DDTABLE1,                                               X
               NULLENT,                                                X
               DDNAMLST,                                               X
               NOWRITE,                                                X
               REPLACE),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               DDTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   DTABFL,C'1'        AN ACTIVE TABLE
*
*
*  OK,  NOW NEED TO LOCATE THE JQE
*  THAT HAS BEEN SELECTED SO I CAN PICK UP THE JCT, ETC.
*
         L     R9,JQEPTR            PICK UP JQE BUFFER
         CLC   0(8,R9),=C'JES2 NJE' DO I HAVE THE JQE'S
         BE    GOTJES2N
*
         MVC   MESSAGE(44),NOTJQEFN
         BAL   R7,OUTMSG
         B     SHODDTAB
REPLACE  DC    CL8'REPLACE'
GOTJES2N DS    0H
         LA    R6,52(,9)      FIRST JQE
*        USING JQE,6          ADDRESSABILITY
         LA    R9,786         MAXIMUM NUMBER OF JQE'S
DDGETJQE DS    0H
         LH    R7,JOBNUMH     JES JOB NUMBER OF JOB SELECTED
         CH    R7,JQEJOBNO    MATCH?
         BE    HASPOINT       YUP
         LA    R6,JQELNGTH(,6) BUMP
         BCT   R9,DDGETJQE     LOOP
*
         MVC   MESSAGE(44),NOTJQENO
         BAL   R7,OUTMSG
         B     SHODDTAB
*
HASPOINT DS    0H
         CLC   FZEROS(4),JQETRAK    JCT DISK ADDRESS
         BE    ABADJCT
         L     R9,JCTPTR
         USING JCTSTART,9
         CLC   JCTID(4),=C'JCT '
         BNE   GETAJCT
         PACK  DOUBLEWD(8),JCTJOBID+3(5)       JQE JOB NUMBER
         CVB   R1,DOUBLEWD
         CLM   R1,3,JQEJOBNO
*        BE    GOTAJCT
*
GETAJCT  DS    0H
         MVC   READMTTR(4),JQETRAK    JCT DISK ADDRESS
         MVC   READAREA(4),JCTPTR
         BAL   R7,READSPAC
*
         CLC   0(4,R9),=C'JCT '
         BE    GOTAJCT
*
ABADJCT  DS    0H
         MVC   MESSAGE(44),NOTAJCT
         BAL   R7,OUTMSG
         B     SHODDTAB
*
GOTAJCT  DS    0H
*
         CLC   FZEROS(4),JCTIOT     IOT DISK ADDRESS
         BE    ABADIOT
         MVC   READMTTR(4),JCTIOT     IOT DISK ADDRESS
         MVC   NORMIOT(4),JCTIOT     IOT DISK ADDRESS
         MVC   SPINIOT(4),JCTSPIOT   IOT DISK ADDRESS
*
         CLC   USERID(2),=C'BP'      BPSCAN
         BE    USEAJCT        YUP
         CLC   USERID(2),=C'AS'      TECH SERV USERID
         BE    USEAJCT        YUP
         CLC   USERID(3),=C'MVS'     OPERATIONS
         BE    USEAJCT        YUP
         CLC   USERID(5),JQEJNAME  NAME MATCH
         BE    USEAJCT        YUP
         CLC   JQEJNAME(6),=C'SYSLOG'   SYSLOG
         BE    USEAJCT        YUP
*
         CLC   USERID(5),=C'FFTCS'   IS THIS SPECIAL VM CICS USER
         BNE   USERNODE              NO, JUST TEST NODE
*
         CLC   JOBNAMS(3),=C'ITP'    IS THIS SPECIAL VM CICS JOB
         BE    USEAJCT        YUP
*
USERNODE DS    0H
         CLC   USERID(5),824(R9)      JCT REMOTE SUBMITTING USER ID
         BNE   DNOTAUTH
*
USEAJCT  DS    0H
         DROP  9
         MVC   READAREA(4),IOTPTR
*
*
GETPDDBS DS    0H                  READ JES2 JCT
*
*                                                                 JSVC
         MVC   IXTJOBID(4),FZEROS
*                                                                 JSVC
         CLC   FZEROS(4),NORMIOT    IOT DISK ADDRESS
         BE    ABADIOT
         MVC   READMTTR(4),NORMIOT  IOT DISK ADDRESS
         MVC   READAREA(4),IOTPTR   IOT DISK ADDRESS
*
*
         BAL   R7,READSPAC
*
         L     R7,IOTPTR
         CLC   0(4,R7),=C'IOT '
         BE    GOTAIOT
*
ABADIOT  DS    0H
         WTO   'NOT A IOT',ROUTCDE=11
         DC    H'0'
*
SPINIOT  DS    F
NORMIOT  DS    F
DDSDSID  DS    CL3
*
GOTAIOT  DS    0H
*
         USING IOTSTART,7
*
*
* OK, GOT THE IOT, NOW PICK UP THE PDDB'S
*
         L     R6,IOTPDDBP  END OF PDDB'S OFFSET
         LA    R6,0(6,R7)   END OF PDDB'S ADDRESS
         MVC   NORMIOT(4),IOTIOTTR    NEXT IOT DISK ADDRESS
         DROP  7
PDDB1    EQU   464            OFFSET OF FIRST PDDB
TABLNGTH EQU   12             THIS IS FROM SYS1.HASPSRC($TAB)
         LA    R7,PDDB1(,7)   OFFSET OF FIRST PDDB
         USING PDBDSECT,7
*
*
         L     R8,DDAREA           PICK UP PARMS
         USING DDLINE,8            OUTPUT AREA
*
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   STEPNAME(17),JOBTYPE  JOB NAME AND NUMBER
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'OPR'    INPUT JCL
         MVC   DDN(9),=C'BPSCAN   '  INPUT JCL
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'CTL'    INPUT JCL
         MVC   DDN(9),=C'BLOCKS   '  INPUT JCL
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'001'    INPUT JCL
         MVC   DDN(9),=C'INPUT JCL'  INPUT JCL
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'002'    JOB LOG
         MVC   DDN(7),=C'JOB LOG'
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'003'    JCL
         MVC   DDN(3),=C'JCL'
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'004'    JOB MESSAGES
         MVC   DDN(8),=C'JOB MSGS'
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
*
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDLDSID(3),=C'005'
         MVC   DDN(8),=C'INT TEXT'   INTERNAL TEXT
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVI   0(R8),X'FF'           END OF TABLE ENTRIES
*
IXTPDDB  DS    0H
*
*                                                                 JSVC
         CLC   PDBDSKEY(2),=X'0005'
         BH    GOTALLDD
         BNE   SKIPTHIS
         MVC   IXTMTTR(4),PDBMTTR
         B     GETINTXT              GO GET IT
SKIPTHIS DS    0H
         LA    R7,PDBLENG(,7)        BUMP TO NEXT PDDB
         CR    R7,R6
         BL    IXTPDDB
         B     GOTALLDD
*
GETINTXT DS    0H
*
         LA    R5,IXTMTTR             SET FOR LOOP
         MVC   STEPNAME(8),SPACES
         MVC   DDN(8),SPACES
*
***********************************************************************
*                                                                     *
*   PROCESS INTERNAL TEXT                                             *
*                                                                     *
***********************************************************************
NEXTBLK  L     R4,0(R5)       DISK ADDR OF NEXT BLOCK
FIRST    LTR   R4,R4          IS THE DISK ADDR ZERO?
         BZ    GOTALLDD       YES. END OF DATASET.
         ST    R4,NORMIOT             SET FOR LOOP
*
         ST    R4,READMTTR          IXT DISK ADDRESS
         MVC   READAREA(4),IXTPTR   IXT DISK ADDRESS
         BAL   R7,READSPAC
*
         L     R5,IXTPTR    INTERNAL TEXT INPUT AREA
         CLC   IXTJOBID(4),FZEROS        FIRST TIME THROUGH?
         BNE   *+10
         MVC   IXTJOBID(4),4(R5) SET JOB ID
         CLC   IXTJOBID(4),4(R5) DOES THE JOBID MATCH?
         BNE   GOTALLDD       NO. END OF DATASET.
         CLC   =H'5',8(R5)    IS THE DSID 5?
         BNE   GOTALLDD       NO. END OF DATASET.
         LA    R4,10(R5)      ADDR OF FIRST RECORD IN BLOCK
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
NEXTREC  CLI   0(R4),X'FF'    IS LENGTH BYTE FF?
         BE    NEXTBLK        YES. END OF BLOCK.
         TM    1(R4),X'10'    IS THIS A SPANNED RECORD?
         BO    SPAN           YES. SKIP IT.
         SR    R6,R6          ZERO OUT REG
         IC    R6,0(R4)       INSERT LENGTH
         TM    5(R4),2        IS THIS AN EXEC RECORD?
         BO    EXEC           YES. PROCESS IT.
         TM    5(R4),4        IS THIS A DD RECORD?
         BO    DD             YES. PROCESS IT.
SKIPREC  LA    R4,3(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPAN     LH    R6,2(R4)       LENGTH OF SEGMENT
         TM    1(R4),X'08'    IS THIS THE FIRST SEGMENT?
         BO    SPANFRST       YES. USE HEADER LENGTH OF 6.
         LA    R4,4(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
SPANFRST LA    R4,6(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTREC        PROCESS NEXT RECORD
***********************************************************************
*                                                                     *
*   PROCESS AN EXEC RECORD                                            *
*                                                                     *
***********************************************************************
EXEC     MVC   STEPNAME(8),SPACES
         CLI   7(R4),X'94'    IS THERE A STEPNAME?
         BNE   SKIPREC        NO. SKIP THE REST.
         SR    R1,R1          ZERO OUT R1
         IC    R1,9(R4)       LENGTH OF STEPNAME
         SH    R1,=H'1'       DECREMENT BY 1
         BM    SKIPREC        STEPNAME WAS ZERO LENGTH.
         EX    R1,MVCSTEP     MOVE THE STEPNAME
         B     SKIPREC        CONTINUE PROCESSING
MVCSTEP  MVC   STEPNAME(1),10(R4)
***********************************************************************
*                                                                     *
*   PROCESS DD RECORDS                                                *
*                                                                     *
***********************************************************************
DD       TM    6(R4),X'30'    IS THIS A SYSIN OR SYSOUT DD?
         BZ    SKIPREC        NO. SKIP THE RECORD.
         LA    R7,7(R4)       ADDR OF FIRST KEY
         LR    R3,R6          REMAINING LENGTH OF RECORD
         SR    R15,15         ZERO OUT R15
         SR    R14,R14        ZERO OUT R14
         SR    R1,R1          ZERO OUT R1
TRYFLD   CLI   0(R7),X'6E'    IS THIS THE DDNAME?
         BE    DDKEY          YES. PROCESS IT.
         CLI   0(R7),X'4A'    IS THIS THE DSNAME?
         BNE   NEXTFLD        NO. CHECK NEXT FIELD
         B     DSKEY          YES. PROCESS IT.
         CLC   3(3,R7),=C'JES' YES. IS THIS TRULY A SYSIN/SYSOUT DS?
         BE    DSKEY          YES. PROCESS IT.
NEXTFLD  IC    R1,1(R7)       NUMBER OF SUBFIELDS
         LA    R7,2(R7)       UPDATE LOCATION
         SH    R3,=H'2'       REMAINING COUNT
         SR    R3,R1          REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
         LTR   R1,R1          ARE THERE ANY SUBFIELDS?
         BZ    TRYFLD         NO. TRY NEXT FIELD.
LOOPFLD  TM    0(R7),X'80'    IS THIS A SUB-SUB-FIELD
         BZ    NOSUB          NO. CONTINUE.
         NI    0(R7),X'7F'    CLEAR THE HEX 80 BIT
         IC    R14,0(R7)      NUMBER OF SUB-SUB-FIELDS
         LA    R7,1(R7)       UPDATE LOCATION
         SH    R3,=H'1'       REMAINING COUNT
         SR    R3,R14         REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
         AR    R1,R14         INCREASE NUMBER OF SUBFIELDS
         B     YESSUB         DECREMENT AND TRY AGAIN
NOSUB    IC    R15,0(R7)      SUBFIELD LENGTH
         LA    R7,1(R15,R7)   ADD TO LOCATION
         SR    R3,R15         REMAINING COUNT
         BNP   SKIPREC        RECORD IS EXHAUSTED
YESSUB   BCT   R1,LOOPFLD     DO NEXT SUBFIELD
         B     TRYFLD         TRY NEXT FIELD
DDKEY    IC    R1,2(R7)       LENGTH OF DDNAME
         LTR   R1,R1          IS THE LENGTH ZERO?
         BZ    NEXTFLD        YES. SKIP THE FIELD.
         BCTR  R1,0           DECREMENT BY 1
         MVC   DDN(8),SPACES
         EX    R1,MVCDDN      MOVE THE DDNAME
         B     NEXTFLD        PROCESS NEXT FIELD
MVCDDN   MVC   DDN(1),3(R7)      MOVE DDNAME
*
*   GOT A SYSOUT FILE, NOW GO LOCATE THE PDDB TABLE ENTRY
*
DSKEY    DS    0H
         MVC   DDLDSID(3),20(R7)      SET DSID NUMBER
         LA    R8,DDLINESZ(,R8)       NEXT DDLINE
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVI   0(R8),X'FF'            END OF DDLINES
         B     SKIPREC        PROCESS NEXT RECORD
*
GOTALLDD DS    0H
*
         L     R7,IOTPTR
         B     GOTIOT2
*
*
PDDBLOOP DS    0H
         MVC   READAREA,IOTPTR
         BAL   R7,READSPAC
*
         L     R7,IOTPTR
         CLC   0(4,R7),=C'IOT '
         BNE   SHOWDONE
*
GOTIOT2  DS    0H
*
         USING IOTSTART,7
*
*
* OK, CREATED ALL THE DDLINE ENTRIES, IN ORDER, WITH STEP AND DDNAMES,
* NOW EXTRACT THE DATA FROM THE PDDB'S.
*
         L     R6,IOTPDDBP  END OF PDDB'S OFFSET
         LA    R6,0(6,R7)   END OF PDDB'S ADDRESS
         MVC   NORMIOT(4),IOTIOTTR    NEXT IOT DISK ADDRESS
         MVC   READMTTR(4),IOTIOTTR   NEXT IOT DISK ADDRESS
         DROP  7
         LA    R7,PDDB1(,7)   OFFSET OF FIRST PDDB
         USING PDBDSECT,7
*
NEXTPDDB DS    0H
*
*
         L     R8,DDAREA           PICK UP PARMS
*
*                                                                 JSVC
         CLC   PDBDSKEY(2),=X'0006'    IF "MYSTERY" PDDB
         BE    SKIPPDDB                SKIP THIS
         SR    R1,1
         LH    R1,PDBDSKEY
         C     R1,FZEROS               IF DSKEY ZERO
         BE    SKIPPDDB                SKIP THIS
         CVD   R1,DOUBLEWD
         OI    DOUBLEWD+7,X'0F'
         UNPK  TXTDSID(3),DOUBLEWD+6(2)
*                                                                 JSVC
DDLNLOOP DS    0H
*                                                                 JSVC
         CLI   0(R8),X'FF'           NO MATCH??????????
         BE    NOMATCH              WH-A-A-A-A-AT??????
         CLC   TXTDSID(3),DDLDSID    MATCH
         BE    GOTDDN
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
         B     DDLNLOOP
*                                                                 JSVC
TXTDSID  DS    CL3
*                                                                 JSVC
DDIDUPL  DS    0H
*                                                                 JSVC
         CLI   0(R8),X'FF'           NO MATCH??????????
         BE    DDIDUPL2
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
         B     DDIDUPL
*                                                                 JSVC
DDIDUPL2 DS    0H
*                                                                 JSVC
         AP    DOUBLEWD(8),=PL2'+800'
         OI    DOUBLEWD+7,X'0F'
         UNPK  TXTDSID(3),DOUBLEWD+6(2)
*                                                                 JSVC
NOMATCH  DS    0H
*                                                                 JSVC
         MVI   DDLINE,C' '      CLEAR OUTPUT AREA
         MVC   DDLINE+1(DDLINESZ-1),DDLINE
         MVC   DDN(8),=C'--SPIN--'
         MVI   DDLINESZ(R8),X'FF'    RESET END OF LIST FLAG
         MVC   DDLDSID(3),TXTDSID    SET THE DSID
         CLC   JOBNAMS(8),=C'SYSLOG  '  SYSLOG
         BNE   GOTDDNOW
*                                                                 JSVC
         TM    PDBFLAG1,PDB1NULL
         BO    GOTDDNOW
         CLC   PDBNAME(4),=X'00000000'
         BNE   GOTDDNOW
         CLC   PDBMTTR(4),=X'00000000'
         BE    GOTDDNOW
         MVI   STEPNAME-1,X'03'          HIGHLIGHT
         MVC   STEPNAME(6),=C'ACTIVE'    HIGHLIGHT
         B     GOTDDNOW
*                                                                 JSVC
GOTDDN   DS    0H
         CLC   DDN(8),=C'--SPIN--'
         BE    DDIDUPL
*                                                                 JSVC
GOTDDNOW DS    0H
         MVC   DDCLASS(1),PDBCLASS
         SR    14,14
         IC    R14,PDBCOPYS
         CVD   R14,DOUBLEWD
         MVC   DDCOPIES-1(4),EDIT4
         ED    DDCOPIES-1(4),DOUBLEWD+6
         TM    PDBFLAG3,PDB3BRST        IS IT BURST
         BZ    *+16                     NOPE
         TM    PDBFLAG2,PDB2BRST        IS IT BURST
         BZ    *+8                      NOPE
         MVI   DDBURST,C'B'
*
         L     R14,PDBRECCT   LINES
         CVD   R14,DOUBLEWD
         MVC   DDLINES(8),EDIT8
         ED    DDLINES,DOUBLEWD+4
*
         MVC   DDMTTR(4),PDBMTTR
         PACK  DDDDMTTR(1),PDBMTTR(1)
         NI    DDDDMTTR,X'0F'
         MVC   DDDDMTTR+1(1),PDBMTTR
         NI    DDDDMTTR+1,X'0F'
         PACK  DDDDMTTR+2(1),PDBMTTR+1(1)
         NI    DDDDMTTR+2,X'0F'
         MVC   DDDDMTTR+3(1),PDBMTTR+1
         NI    DDDDMTTR+3,X'0F'
         PACK  DDDDMTTR+4(1),PDBMTTR+2(1)
         NI    DDDDMTTR+4,X'0F'
         MVC   DDDDMTTR+5(1),PDBMTTR+2
         NI    DDDDMTTR+5,X'0F'
         PACK  DDDDMTTR+6(1),PDBMTTR+3(1)
         NI    DDDDMTTR+6,X'0F'
         MVC   DDDDMTTR+7(1),PDBMTTR+3
         NI    DDDDMTTR+7,X'0F'
         TR    DDDDMTTR(8),HEXTABLE
*
         MVC   DDFORM(4),PDBFORMS
         OC    DDFORM(4),SPACES        CHANGE ZEROS TO BLANKS
         MVC   DDFCB(4),PDBFCB
         MVC   DDUCS(4),PDBUCS
         MVC   DDLRECL(2),PDBLRECL
*
         SR    R14,R14
         ICM   R14,3,PDBLRECL
         CVD   R14,DOUBLEWD
         MVC   DDDRECL(6),EDIT6
         ED    DDDRECL,DOUBLEWD+5
*
         MVC   DDRECFM(1),PDBRECFM
         TM    DDRECFM,X'C0'
         BO    PDDBRECU
         BZ    PDDBRECU
*
         TM    DDRECFM,X'80'
         BO    PDDBRECF
         MVI   DDDRECF,C'V'         SET RECFM "V"
         B     PDDBLOCK             SEE IF IT'S BLOCKED
*
HEXTABLE DC    C'0123456789ABCDEF'
PDDBRECU DS    0H
*
         MVI   DDDRECF,C'U'         SET RECFM "U"
         B     PDDBBBMP             GO BUMP
*
PDDBRECF DS    0H
*
         MVI   DDDRECF,C'F'         SET RECFM "F"
*
PDDBLOCK DS    0H
*
         TM    DDRECFM,X'10'
         BZ    PDDBCCCC             NOT BLOCKED, CHECK FOR CARR CTL
         MVI   DDDRECF+1,C'B'       SET BLOCKED
*
PDDBCCCC DS    0H
*
         TM    DDRECFM,X'06'        ANY CARRIAGE CONTROL
         BZ    PDDBBBMP             NO, JUST BUMP
         BO    PDDBBBMP             NO, JUST BUMP
*
         TM    DDRECFM,X'04'        ASA CARRIAGE CONTROL
         BO    PDDBASAC             YES, SET IT
*
         CLI   DDDRECF+1,C' '       BLANK
         BNE   *+12                 NO, MUST BE BLOCKED
         MVI   DDDRECF+1,C'M'       SET IT
         B     PDDBBBMP             GO BUMP
         MVI   DDDRECF+2,C'M'       SET IT
         B     PDDBBBMP             GO BUMP
*
PDDBASAC DS    0H
*
         CLI   DDDRECF+1,C' '       BLANK
         BNE   *+12                 NO, MUST BE BLOCKED
         MVI   DDDRECF+1,C'A'       SET IT
         B     *+8                  GO BUMP
         MVI   DDDRECF+2,C'A'       SET IT
*
PDDBBBMP DS    0H
         LA    R8,DDLINESZ(,R8)      BUMP TO NEXT ENTRY
SKIPPDDB DS    0H
         LA    R7,PDBLENG(,7)        BUMP TO NEXT PDDB
         CR    R7,R6
         BL    NEXTPDDB
         L     R1,NORMIOT
         LTR   R1,1
         BNZ   PDDBLOOP
         L     R1,SPINIOT
         LTR   R1,1
         BZ    DODDTAB
         MVC   NORMIOT(4),SPINIOT     IOT DISK ADDRESS
         MVC   READMTTR(4),SPINIOT    IOT DISK ADDRESS
         XC    SPINIOT(4),SPINIOT     IOT DISK ADDRESS
         B     PDDBLOOP
*
         TITLE 'JDF -  DISPLAY DDNAME TABLE USING TABLE MGMT SERVICES'
DODDTAB  DS    0H
*
         L     R8,DDAREA           PICK UP PARMS
*
DODDLOOP DS    0H
*
         CLI   0(R8),X'FF'        END OF ENTRIES
         BE    DODDS1ST           YES, SHOW TABLE
*
         MVC   MESSAGE(DDLINSHO),0(R8)
         BAL   R7,OUTMSG
         LA    R8,DDLINESZ(,R8)    BUMP TO NEXT
         B     DODDLOOP
*
DODDS1ST DS    0H
         LM    R4,R8,SAVE5        RESTORE THE WORK REGISTERS
         MVI   COPYFLAG,0     FIRST TIME THROUGH
*
SHODDTAB DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               DDTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         MVI   DSIDEOF,0      NOT EOF
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHODDTBS       NOPE
*
         CLC   USRJNAME(6),=C'BPSCAN'  USERID IS BPSCAN??
         BNE   SHODDTBS      DISPLAY THE DD TABLE
*
         CLC   COMMAND(2),DHREQ        HELD JOB DISPLAY??
         BNE   SHODDTBS      DISPLAY THE DD TABLE
*
         CLI   SELSAVE,C'B'            BPSCAN SELECT??
         BNE   SHODDTBS      DISPLAY THE DD TABLE
*
         B     GOTDSOPR       GO DO THE BPSCAN DISPLAY
*
SHODDTBS DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DDTABLE1,                                               X
               JDFDDTAB),                                              X
               VL,MF=(E,ISPARMS)
         B     DDTSTRTN       YUP, GO PROCESS IT
DDMSG2   DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DDTABLE1,                                               X
               JDFDDTAB,JDFMSG2),                                      X
               VL,MF=(E,ISPARMS)
DDTSTRTN DS    0H  TEST RETURN CODE FROM DISPLAY
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BE    GOTDDSEL       YUP, GO PROCESS IT
DDALLDON DS    0H
*
*    DONE PROCESSING THE DDNAME MENU, SO
*    RE-SHOW THE PRIMARY OPTION SELECTED
*
*
         CLI   COPYFLAG,0     ANY DSID'S SELECTED FOR REPRINTING?
         BE    SHOWAGIN       NOPE
*
         TM    JDFCNTL+48,X'10'         GOOD OPEN
         BZ    SHOWAGIN                 NOPE
*
         CLOSE JDFCNTL
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (EDIT,                                                  X
               DSNEDIT),                                               X
               VL,MF=(E,ISPARMS)
*
         B     SHOWAGIN       ALL DONE
         TITLE 'JDF -  PROCESS DDNAME TABLE SELECTION OPTIONS'
GOTDDSEL DS    0H
*
         MVI   READ1ST,0          SET FIRST-TIME-THROUGH
*
         MVC   SELSAVE(3),SELCODE    SAVE SELECTION OPTION
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
         OC    SELSAVE(3),SPACES     SAVE SELECTION OPTION
         CLC   SELSAVE(3),SPACES     NOTHING ENTERED
         BE    SHODDTAB       REPEAT THE DD TABLE
         MVC   DDSDSID(3),TABAREA    SAVE JES DSID NUMBER
         MVC   BRODSID(3),DDSDSID        SAVE IT FOR BROWSE
         MVC   BRODDN(8),TABAREA+14  SAVE DD  NAME
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBGET,                                                 X
               DDTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         C     R15,F8         RC=8 MEANS CRP IS AT TOP
         BE    GOTDDCMD       THEN A COMMAND WAS ENTERED
*
         CLC   DDLDSID(3),=C'CTL'    CONTROL BLOCK DISPLAY?
         BE    GOTDSCTL
*
         CLC   DDLDSID(3),=C'OPR'    BPSCAN DISPLAY?
         BE    GOTDSOPR
APDDBDD  DS    0H
         BAL   R7,PDDBLOCA
         CLC   DDLDSID(3),DDSDSID        DSID MATCH?
         BNE   DDALLDON                  NOPE
         ST    R8,BRODDLN                SAVE DDLINE POINTER
         LA    R5,DDMTTR                POINT TO THE BLOCK
         MVC   READAREA(4),IXTPTR        MOVE IN THE READ BUFFER
         MVC   READMTTR(4),DDMTTR        MOVE IN THE READ MTTR
*
         CLI   SELSAVE,C'R'              WAS ROUTE REQUESTED?
         BE    ROUTDS                    YUP, GO DO IT
*
*
*
*    A DISPLAY OF A PARTICULAR DSID HAS BEEN REQUESTED, SO CHECK AUTH
*
         CLC   USERID(2),=C'AS'      TECHSERV USER-ID
         BE    DDAUTHOK         YUP
         CLC   USERID(5),=C'FFTCS'   IS THIS SPECIAL VM CICS USER
         BNE   USERTECH              NO, JUST TEST FOR TECHSERV JOB
*
         CLC   JOBNAMS(3),=C'ITP'    IS THIS SPECIAL VM CICS JOB
         BE    DDAUTHOK         YUP
*
USERTECH DS    0H
         CLC   JOBNAMS(2),=C'AS'         TECHSERV JOB
         BNE   DDAUTH2                   NOPE
         CLC   USERID(2),=C'AS'      TECHSERV USER-ID
         BE    DDAUTHOK         YUP
*
         B     DDMSG2  AUTH CHECK FAILED, DISPLAY THE MESSAGE
*
DDAUTH2  DS    0H
         CLI   JOBNAMS+7,C'0'            IS IT PRODUCTION
         BL    DDAUTHOK                  NOPE
*
         CLI   DDLDSID,C'1'            IS JES DSID NUMBER < 100
         BL    DDAUTHOK                  OK TO LOOK AT
*
         CLC   DDN(3),=C'SYS'          IS IT SYSOUT, SYSPRINT, ETC
         BE    DDAUTHOK                  OK TO LOOK AT
*
         CLC   DDN(3),=C'CPX'          IS IT CAPEX
         BE    DDAUTHOK                  OK TO LOOK AT
*
         CLC   DDN(8),=C'SORTMSGS'     IS IT SORT MESSAGES
         BE    DDAUTHOK                  OK TO LOOK AT
*
DDAUTHNO DS    0H
         CLC   USERID(2),=C'AS'      TECHSERV USER-ID
         BE    DDAUTHOK         YUP
*
         CLC   USERID(6),=C'BPSCAN'  PRODUCTION SERVICES
         BE    DDAUTHOK         YUP
*
         B     DDMSG2  AUTH CHECK FAILED, DISPLAY THE MESSAGE
*
DDAUTHOK DS    0H
*
*    IF AUTH OK, DEFINE THE DATASET TABLE
*
         XC    GOTSOME(2),GOTSOME NO RECORDS WRITTEN
*
         CLI   SELSAVE,C'B'              WAS BROWSE REQUESTED?
         BE    OPBROWSE                  YUP, OPEN OUTLIST FILE
*
         CLI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         BNE   DDSTABLE
*
         LA    R1,BROPARMS
         CALL  JDFBRO
*
         B     SHODDTBS
*
BROPARMS DS    0F
BRODDLN  DS    F
         DC    A(BRODSN)
         DC    A(BRO24K)
*
BRODSN   DC    H'30',CL5'JES2.'
BROJOB   DC    CL8'        '
         DC    CL1'.'
BRODDN   DC    CL8'        '
         DC    CL5'.DSID'
BRODSID  DC    CL4'000 '
DDSTABLE DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               DSTABLE1,                                               X
               NULLENT,                                                X
               DSNAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         B     DOREAD1A
*
OPBROWSE DS    0H
         OPEN  (OUTLIST,(OUTPUT))
         TM    OUTLIST+48,X'10'   OPEN OK?
         BZ    NOBROWST           YUP
         B     DOREAD1A           YUP
NOBROWST DS    0H
         MVI   SELSAVE,C'S'       NO, SO SET IT TO TABLE SELECT
         B     DDSTABLE           GO DO IT
*
DOREAD1A DS    0H
*
         XC    GOTSOME(2),GOTSOME NO RECORDS WRITTEN
         MVI   READ1ST,0          SET FIRST-TIME-THROUGH
         MVC   READAREA(4),IXTPTR
         XC    IXTJOBID,IXTJOBID
*
DOREAD1B DS    0H
         BAL   R7,READ1REC        GET A RECORD FROM THE SPOOL
         LTR   R15,15             DID I GET A RECORD
         BNZ   EOFDSID            NOPE, EOF
*
         LH    R1,GOTSOME       PICK UP RECORD COUNT
         LA    R1,1(,1)         ADD 1
         STH   R1,GOTSOME       SAVE RECORD COUNT
*
         CLI   SELSAVE,C'B'              WAS BROWSE REQUESTED?
         BNE   DSTAB1                    NOPE, PUT IT IN THE TABLE
         PUT   OUTLIST,DSNLINE
         B     DOREAD1B
*
DSTAB1   DS    0H
         CLI   CCFLAG,0       IS THERE CARRIAGE CONTROL?
         BE    *+14           NOPE
         MVC   MESSAGE(78),DSNLINE+1
         B     *+10
         MVC   MESSAGE(78),DSNLINE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DSNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(79),MESSAGE
*
         CH    R7,=H'79'      IS THE RECORD BIGGER THAN LINE WIDTH
         BL    DSTSTLIM
*
         CH    R7,=H'79'      IS THE RECORD BIGGER THAN LINE WIDTH
         BH    DSLNE2
*
         CLC   DSNLINE+78(2),=C'00'
         BE    DSTSTLIM
         CLC   DSNLINE+78(2),=C'  '
         BE    DSTSTLIM
*
DSLNE2   DS    0H
         MVC   MESSAGE+24(54),DSNLINE+79                   9/07/83 BC
         MVI   MESSAGE,C'+'
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DSNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(79),MESSAGE
DSTSTLIM DS    0H
         LA    R15,1000         TABLIMIT
         LH    R1,GOTSOME       AMOUNT OF LINES
         CR    R15,R1           COMPARE LIMIT VS CURRENT RECORD COUNT
         BL    DSTABLIM         IF 500 LINES, GO DISPLAY THEM
         B     DOREAD1B
*
DSTABLIM DS    0H
 MVC MESSAGE(78),=CL78' ***** TO VIEW MORE, ENTER MORE ON COMMAND LINE'
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DSNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(79),MESSAGE
*
         B     DSTABSHO                  GO DISPLAY THE TABLE
*
EOFDSID  DS    0H
*
         CLI   SELSAVE,C'B'              WAS BROWSE REQUESTED?
         BNE   DSTABSHO                  NOPE, DISPLAY THE TABLE
*
BROWSEDS DS    0H
*
* WRITE A LINE OF ASTERISKS TO AVOID EMPTY FILE
*
         SR    R1,1
         CH    R1,GOTSOME         NO RECORDS WRITTEN
         BNE   BROWSCLS           NOPE, JUST SHOW THE TABLE
*
         MVC   DSNLINE(133),=CL133'*** FILE IS EMPTY'
         PUT   OUTLIST,DSNLINE
BROWSCLS DS    0H
         CLOSE OUTLIST
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,                                                X
               DSNOTLST),                                              X
               VL,MF=(E,ISPARMS)
*
         CLC   JOBNAMS(6),=C'SYSLOG'       SYSLOG?
         BE    SHOWAGIN                    YES
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   SHODDTAB       NOPE
*
         CLC   SELSAVE(3),=C'BPS'    DOING SPECIAL BPSCAN DISPLAY?
         BNE   SHODDTAB       REPEAT THE DD TABLE
*
         CLC   USRJNAME(6),=C'BPSCAN'  USERID IS BPSCAN??
         BNE   SHODDTAB       REPEAT THE DD TABLE
*
         CLC   COMMAND(2),DHREQ        HELD JOB DISPLAY??
         BNE   SHODDTAB       REPEAT THE DD TABLE
*
         MVC   SELSAVE(3),=C'RQW'    SET REQUEUE TO CLASS "W"
         B     JOBREQ                CHANGE OUTPUT CLASS OF JOB
*
DSTABSHO DS    0H
*
* WRITE A LINE OF ASTERISKS TO AVOID EMPTY FILE
*
         SR    R1,1
         CH    R1,GOTSOME         NO RECORDS WRITTEN
         BNE   DSTABSH2           NOPE, JUST SHOW THE TABLE
*
         MVC   MESSAGE(78),=CL78'*** FILE IS EMPTY'
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DSNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
DSTABSH2 DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               DSTABLE1,                                               X
               JDFDSTAB),                                              X
               VL,MF=(E,ISPARMS)
*
         CLI   DSIDEOF,0      IS IT EOF
         BNE   SHODDTAB   YUP,REPEAT THE DD TABLE
         CLI   CMDDATA,C' '    NO COMMAND ENTERED
         BE    DSTABDLT
*
         B     DDSTABLE
*
DSTABDLT DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               DSTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         B     SHODDTAB       REPEAT THE DD TABLE
GOTDSCTL DS    0H
         B     SHODDTAB       REPEAT THE DD TABLE
         TITLE 'JDF -  PERFORM REROUTE FOR A SELECTED DSID'
*
ROUTDS   DS    0H
         CLI   COPYFLAG,0     FIRST TIME THROUGH
         BNE   REPRTHIS       NOPE
         MVI   COPYFLAG,1     RESET IT
*
         OPEN  (JDFCNTL,(OUTPUT))
         TM    JDFCNTL+48,X'10'         GOOD OPEN
         BZ    ROUTDONE                 NOPE
*
         PUT   JDFCNTL,JOBC1
         PUT   JDFCNTL,JOBC2
         PUT   JDFCNTL,JOBC3
*
*
REPRTHIS DS    0H             JUST REPRINT THIS ONE
         MVC   JOBC4DS(3),DDLDSID     SET STEPNAME
         MVC   JOBC4JOB(4),JOBNUMS+1  SET JES JOB NUMBER
         MVC   JOBC4DS2(3),DDLDSID    SET DSID
         MVI   JOBC4CLS,C'A'          SET DEFAULT SYSOUT CLASS
         MVC   JOBC4RMT(7),=C'CISMVS '    DEFAULT NODE IS LOCAL
         CLI   SELSAVE+1,C' '         NO SYSOUT CLASS SPECIFIED
         BE    REPRTPUT               USE DEFAULT "A"
         CLI   SELSAVE+1,X'EF'        IF NUMERIC,
         BH    REPRNODE               CHECK NODE
         MVC   JOBC4CLS(1),SELSAVE+1  SET IT AS SPECIFIED
         B     REPRTPUT               GO WRITE OUT THE JCL CARD
*
REPRNODE DS    0H
         MVC   JOBC4RMT(7),=C'N1R    '    SET NODE TO LOCAL
         MVC   JOBC4RMT+3(2),SELSAVE+1    SET RMT NUMBERS
*
REPRTPUT DS    0H             JUST REPRINT THIS ONE
         PUT   JDFCNTL,JOBC4
ROUTDONE DS    0H
         B     SHODDTBS       REPEAT THE DD TABLE
*
         TITLE 'JDF -  PERFORM SPECIAL FORMAT BPSCAN DISPLAY'
GOTDSOPR DS    0H
         OPEN  (OUTLIST,(OUTPUT))
         TM    OUTLIST+48,X'10'   OPEN OK?
         BO    DOBPSCAN           YUP
         B     SHOWAGIN       ALL DONE
*
***********************************************************************
*                                                                     *
*   SET UP SPECIAL FORMAT BPSCAN DISPLAY                              *
*                                                                     *
***********************************************************************
DOBPSCAN DS    0H
         PUT   OUTLIST,BPSFORM
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         PUT   OUTLIST,DSNLINE
         MVC   LABEL1(13),EXPECTED
*
*  FIRST LOCATE THE PDDB FOR DSID 1 TO PICK UP THE COND COMMENT CARD
*
         MVC   DDSDSID(3),=C'001'        EXECUTION JCL
         BAL   R7,PDDBLOCA
         CLC   DDLDSID(3),DDSDSID        DSID MATCH?
         BNE   SHODDTAB                  NOPE
         LA    R5,DDMTTR                 POINT TO THE BLOCK
         MVC   READAREA(4),IXTPTR        MOVE IN THE READ BUFFER
         MVC   READMTTR(4),DDMTTR        MOVE IN THE READ MTTR
*
         MVI   READ1ST,0          SET FIRST-TIME-THROUGH
*
         MVI   BOPR1ST,1          SET FIRST-TIME-THROUGH
         XC    GOTSOME(2),GOTSOME NO RECORDS WRITTEN
*
DOREAD1C DS    0H
         BAL   R7,READ1REC        GET A RECORD FROM THE SPOOL
         LTR   R15,15             DID I GET A RECORD
         BNZ   BGNJMREC           NOPE, EOF
*
         LA    R1,DSNLINE
*
BPCOMMLP DS    0H
*
         CLC   0(3,R1),=C'//*'   COMMENT LINE?
         BNE   DOREAD1C          NO, SKIP IT
*
         LA    R1,3(,1)          BUMP
         LA    R7,65             MAXIMUM ITERATIONS
*
BPCOMML1 DS    0H
         CLC   0(4,R1),=C'COND'  CONDITION CODE LINE
         BE    BPCOMMPT          YUP
         LA    R1,1(,1)
         BCT   R7,BPCOMML1
         B     DOREAD1C
*
BPCOMMPT DS    0H
         MVC   CMDLINE(67),DSNLINE+3
         MVC   DSNLINE(13),LABEL1
         MVI   LABEL1,C' '
         MVC   LABEL1+1(12),LABEL1
         MVC   DSNLINE+13(67),CMDLINE
         PUT   OUTLIST,DSNLINE
*
         B     DOREAD1C
*
BGNJMREC DS    0H
*
*
GETJMREC DS    0H
*
*
*    NOW LOCATE THE PDDB FOR DSID 4 TO PICK UP THE COND CODES, ETC.
*
         MVC   DDSDSID(3),=C'004'        JOB MESSAGES
         BAL   R7,PDDBLOCA
         CLC   DDLDSID(3),DDSDSID        DSID MATCH?
         BNE   SHODDTAB                  NOPE
         LA    R5,DDMTTR                 POINT TO THE BLOCK
         MVC   READAREA(4),IXTPTR        MOVE IN THE READ BUFFER
         MVC   READMTTR(4),DDMTTR        MOVE IN THE READ MTTR
*
         MVI   READ1ST,0          SET FIRST-TIME-THROUGH
*
GETJMLOP DS    0H
         BAL   R7,READ1REC        GET A RECORD FROM THE SPOOL
         LTR   R15,15             DID I GET A RECORD
         BNZ   SHOWBP       NOPE, END OF DATASET.
*
         LA    R1,DSNLINE+1
*
*
         CLC   0(7,R1),=C'IEF142I'     COND LINE
         BE    BPGOTCON                YUP
         CLC   0(7,R1),=C'IEF472I'     COND LINE
         BE    BPGOTCON                YUP
*
         CLC   0(7,R1),=C'IEF285I'  CAT/UNCAT REC?
         BNE   BPUNCAT              NO
*
         CLC   55(3,R1),=C'CAT'  CATALOGED REC?
         BE    BPCATED              YUP
*
         CLC   55(3,R1),=C'UNC'  UNCATALOGED REC?
         BNE   GETJMLOP          NOPE
*
         CLI   BOPR1ST,2            IF SECOND TIME AROUND
         BE    GETJMLOP             SKIP IT
*
         AP    UNCATNUM(3),P1(1)
         B     GETJMLOP            GO GET THE NEXT ONE
*
BPUNCAT  DS    0H
         CLC   0(7,R1),=C'IEF287I'  "NOT CATALOGED" REC?
         BNE   GETJMLOP             NO
*
         CLC   55(3,R1),=C'NOT'     ACTUAL RECORD?
         BNE   GETJMLOP          NOPE
*
         CLI   BOPR1ST,2            IF SECOND TIME AROUND
         BNE   BPUNADD              GO WRITE IT OUT
*
         PUT   OUTLIST,DSNLINE
         BAL   R7,READ1REC        GET A RECORD FROM THE SPOOL
         LTR   R15,15             DID I GET A RECORD
         BNZ   SHOWBP       NOPE, END OF DATASET.
         B     BPPUT1        YUP, GO WRITE IT OUT
*
BPUNADD  DS    0H
         AP    NCATNUM(3),P1(1)
         B     GETJMLOP            GO GET THE NEXT ONE
*
BPGOTCON DS    0H
         LA    R1,DSNLINE+35       BEGIN TEST FOR COND CODE 0
         LA    R15,80
BPTSTCON DS    0H
         CLC   0(10,R1),=C'COND CODE '
         BE    BPGOTCND
         CLC   0(15,R1),=C'COMPLETION CODE' FOR ABEND LINE
         BE    BPGOTCND
         LA    R1,1(,1)            BUMP
         BCT   R15,BPTSTCON        LOOP
BPGOTCND DS    0H
*
         CLI   BOPR1ST,2            IF SECOND TIME AROUND
         BE    BPTSTBAD             GO TEST FOR NON-ZERO
*
         AP    CONDNUM(3),P1(1)
         CLC   10(4,R1),=C'0000'   CONDITION CODE ZERO
         BNE   GETJMLOP
*
         AP    CONDZERO(3),P1(1)
         B     GETJMLOP            GO GET THE NEXT ONE
*
BPTSTBAD DS    0H
         CLC   10(4,R1),=C'0000'   CONDITION CODE ZERO
         BE    GETJMLOP            GO GET THE NEXT ONE
*
         PUT   OUTLIST,DSNLINE
         B     GETJMLOP            GO GET THE NEXT ONE
*
BPCATED  DS    0H
*
         CLI   BOPR1ST,2            IF SECOND TIME AROUND
         BE    GETJMLOP             GO GET THE NEXT ONE
*
         AP    CATNUM(3),P1(1)
         B     GETJMLOP            GO GET THE NEXT ONE
*
BPPUT1   DS    0H
         PUT   OUTLIST,DSNLINE
         B     GETJMLOP            GO GET THE NEXT ONE
*
SHOWBP   DS    0H
         CLI   BOPR1ST,2            IF SECOND TIME THROUGH
         BE    DOSHOWBP             ALL DONE
         MVI   BOPR1ST,2
*
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         MVC   DSNLINE(13),FOUND
*
*
* COND CODES
         MVC   DSNLINE+13(16),CONDMS
         MVC   DSNLINE+29(6),EDIT6   DO EDIT
         ED    DSNLINE+29(6),CONDNUM DO EDIT
* DATA SETS CATALOGED
         MVC   DSNLINE+36(9),CATSMS
         MVC   DSNLINE+45(6),EDIT6   DO EDIT
         ED    DSNLINE+45(6),CATNUM DO EDIT
* DATA SETS UNCATALOGED
         MVC   DSNLINE+52(11),UNCATMS
         MVC   DSNLINE+63(6),EDIT6   DO EDIT
         ED    DSNLINE+63(6),UNCATNUM
         PUT   OUTLIST,DSNLINE
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         PUT   OUTLIST,DSNLINE
* ZERO COND CODES
         CP    CONDNUM,CONDZERO      IF ALL CONDITION CODES ZERO
         BE    ALLCND0               ISSUE THE MESSAGE
         SP    CONDNUM,CONDZERO      IF ALL CONDITION CODES ZERO
         MVC   DSNLINE+13(20),CONDMSNZ
         MVC   DSNLINE+33(6),EDIT6   DO EDIT
         ED    DSNLINE+33(6),CONDNUM
         PUT   OUTLIST,DSNLINE
         B     SHOWBPFR              ISSUE THE MESSAGE
ALLCND0  DS    0H
         MVC   DSNLINE+13(24),CONDMS0
         PUT   OUTLIST,DSNLINE
SHOWBPFR DS    0H
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         PUT   OUTLIST,DSNLINE
         PUT   OUTLIST,BPSFORM
         PUT   OUTLIST,DSNLINE
*
         B     GETJMREC             GO DO IT AGAIN
*
CONDMS0  DC    CL24'ALL CONDITION CODES 0000'
CONDMSNZ DC    CL20'NON-ZERO COND CODES '
CONDZERO DC    PL3'+0'
*
DOSHOWBP DS    0H
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         PUT   OUTLIST,DSNLINE
         PUT   OUTLIST,BPSFORM
         PUT   OUTLIST,DSNLINE
         MVC   DDLDSID(3),=C'004'  JOB MESSAGES
         MVC   SELSAVE(3),=C'BPS'    DOING SPECIAL BPSCAN DISPLAY
*
         B     APDDBDD
*
GOTDDCMD DS    0H
         B     SHODDTAB       REPEAT THE DD TABLE
         TITLE 'JDF -  PDDB LOCATE SUBROUTINE'
PDDBLOCA DS    0H
         L     R8,DDAREA
PDDBLOC1 DS    0H
         CLI   0(R8),X'FF'       END OF TABLE AND NO MATCH
         BER   R7                SKIP THIS
         CLC   DDLDSID(3),DDSDSID        DSID MATCH?
         BER   R7                GOODY
         LA    R8,DDLINESZ(,8)   BUMP TO NEXT TABLE ENTRY
         B     PDDBLOC1          LOOP
         TITLE 'JDF -  DSID READ 1 RECORD SUBROUTINE'
READ1REC DS    0H
         ST    R7,RD1SAVE7
         CLI   READ1ST,0      FIRST TIME THROUGH?
         BE    READ1OPN       YUP
         CLI   DSIDEOF,0      IS IT EOF
         BNE   READ1EOF       YUP
         LM    R4,R7,RD1SAVE
         B     SKIP1REC
READ1OPN DS    0H
         MVI   READ1ST,1      TURN OFF SWITCH
         MVI   DSIDEOF,0      NOT EOF
READ1NXT DS    0H
***********************************************************************
*                                                                     *
*   PROCESS DATASET                                                   *
*                                                                     *
***********************************************************************
NEXTDSA  L     R4,0(R5)       DISK ADDR OF NEXT BLOCK
         LTR   R4,R4          IS THE DISK ADDR ZERO?
         BZ    READ1EOF       YUP END OF DATASET.
         L     R5,IXTPTR                 POINT TO THE BLOCK
         ST    R4,READMTTR    STORE DISK ADDR IN TABLE
         BAL   R7,READSPAC               READ IN THE BLOCK
         CLC   IXTJOBID(4),FZEROS     FIRST TIME THROUGH
         BNE   *+10
         MVC   IXTJOBID(4),4(R5) SETS THE JOBID
         CLC   IXTJOBID(4),4(R5) DOES THE JOBID MATCH?
         BNE   READ1EOF       NO. END OF DATASET.
         LA    R4,10(R5)      ADDR OF FIRST RECORD IN BLOCK
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
NEXTDSR  CLI   0(R4),X'FF'    IS LENGTH BYTE FF?
         BE    NEXTDSA        YES. END OF BLOCK.
         TM    1(R4),X'10'    IS THIS A SPANNED RECORD?
         BO    SPANSKIP       YES. SKIP IT.
         SR    R6,R6          ZERO OUT REG
         IC    R6,0(R4)       INSERT LENGTH
         LR    R7,R6          SAVE RECORD LENGTH
         LR    R1,R4          SAVE RECORD LOCATION
         MVI   CCFLAG,0       NO CARRIAGE CONTROL
         TM    1(R4),X'80'    IS THERE CARRIAGE CONTROL?
         BZ    *+12           NO.
         LA    R1,1(,1)       BUMP 1
         MVI   CCFLAG,1       THERE IS CARRIAGE CONTROL
         TM    1(R4),X'08'    IS THIS RECORD TO BE IGNORED?
         LR    R4,R1          UPDATE RECORD POINTER
         BNZ   SKIP1REC       YES. SKIP IT.
         LTR   R7,R7          LENGTH ZERO
         BZ    SKIP1REC       YES. SKIP IT.
         BCTR  R7,0           MINUS 1
         N     R7,ZEROFF      LOP OFF LEFT BYTES
         CH    R7,=H'133'     IS THE RECORD BIGGER THAN LINE WIDTHLBDMC
         BNH   *+8            NO. USE RECORD LENGTH.
         LH    R7,=H'133'          YES. USE LINE WIDTH            LBDMC
         LA    R1,3(R1)       OFFSET PAST REC HDR
         MVI   DSNLINE,C' '
         MVC   DSNLINE+1(132),DSNLINE
         CLI   CCFLAG,0       IS THERE CARRIAGE CONTROL?
         BE    *+10           NOPE
         BCTR  R1,0           BACK UP 1 TO PICK UP CC
         LA    R7,1(,7)       AND ADD 1 TO LENGTH
         EX    R7,MOVEDATA
         STM   R4,R7,RD1SAVE
         SR    R15,15         GOT A RECORD
READ1RTN DS    0H
         L     R14,RD1SAVE7
         BR    R14
READ1EOF DS    0H
         LA    R15,4          EOF
         MVI   READ1ST,0      FIRST TIME THROUGH IS FORCED
         MVI   DSIDEOF,1      SET EOF
         B     READ1RTN
*
*
SKIP1REC LA    R4,3(R6,R4)    INCREMENT TO NEXT RECORD
         B     NEXTDSR        PROCESS NEXT RECORD
SPANSKIP LH    R6,2(R4)       LENGTH OF SEGMENT
         TM    1(R4),X'08'    IS THIS THE FIRST SEGMENT?
         BO    SPAN1STR       YES. USE HEADER LENGTH OF 6.
         LA    R4,4(R6,R4)    UPDATE RECORD POSITION
         B     NEXTDSR        PROCESS NEXT RECORD
SPAN1STR LA    R4,6(R6,R4)    UPDATE RECORD POSITION
         B     NEXTDSR        PROCESS NEXT RECORD
         TITLE 'JDF -  MVS COMMAND   SUBROUTINE'
ISSUECMD DS    0H
*
         CLI   CMSFLAG,C'0'   AM I RUNNING UNDER TSO?
         BNE   ISSUENJE       NOPE
*
         SLR   R0,0                    CLEAR REG 0
         LA    R1,CMDISMF             GET ADDRESS OF COMMAND I SMF
         LA    R15,1
         SVC   242                     ISSUE COMMAND VIA SVC 34
         BR    R7
*
*  I'M RUNNING UNDER CMS, SO CREATE SMSG TO TRANSMIT TO JES2 VIA RSCS
*
*
ISSUENJE DS    0H
         MVC   NJECMD(40),CMDLINE
         LA    R4,CMDEND1-CMDBEG1
         LA    R3,CMDBEG1             COMMAND
         DC    X'83340008'            VIRTUAL CONSOLE FUNCTION
*
ISSUEOUT DS    0H
         BR    R7
*
CMDISMF  DC    Y(76),Y(00)
CMDLINE  DC    CL72'I SMF' COMMAND SVC 34 INPUT
*
CMDBEG1  EQU   *
         DC    C'SMSG BSJESCMD CMD CISMVS '
NJECMD   DC    CL40' '
CMDEND1  EQU   *
*
         TITLE 'JDF -  READ 1 BLOCK FROM THE SPOOL'
READSPAC DS    0H
         ST    R7,READSPA7
         CLI   CMSFLAG,C'0'   "0" INDICATES TSO, "1" INDICATES CMS
         BE    SPOOLSVC       USE SVC FOR SPOOL I/O
*
         CLC   FZEROS(2),READMTTR+1      ZERO TRACK ADDRESS
         BE    ABEND7                    YUP
         MVC   DDIOTRAK(3),READMTTR+1    DISK ADDRESS
         MVI   DDIOTRAK+3,0
         SR    R1,1
         ICM   R1,3,DDIOTRAK   PICK UP ABSOLUTE TT
         LA    R7,90
*
         CR    R1,R7         IS TRACK < 90
         BL    ABEND8                    YUP
*
         SR    R1,R7
         STH   R1,DDIOTRAK   SET RELATIVE TT
*
         C     R1,TTMAX      IS IT TOO BIG?
         BH    ABEND9                    YUP
*
         POINT HASPACE,DDIOTRAK
*
         L     R7,READAREA
         XC    CKPTECB(4),CKPTECB
*
         READ  CKPTECB,SF,HASPACE,(7),'S',MF=(E,CKPTREAD)
         CHECK CKPTECB
*
         CLI   CKPTECB,X'7F'             GOOD RETURN CODE
         BNE   ABEND10                   NOPE
*
READSPAE DS    0H
*        TM    JDFLOG+48,X'10'  FILE OPEN
*        BO    JDFLOGIT
*        OPEN  (JDFLOG,(OUTPUT))
*        TM    JDFLOG+48,X'10'  FILE OPEN
*        BZ    READRTN7
JDFLOGIT DS    0H
*        L     R7,READAREA
*        PUT   JDFLOG,(R7)
READRTN7 DS    0H
         L     R7,READSPA7
         BR    R7
SPOOLSVC DS    0H
         L     R1,READMTTR
         L     R0,READAREA
         S     R0,BACK88
         LA    R15,3
         SVC   242
         B     READSPAE
*
BACK88   DC    F'88'
TTMAX    DC    F'14999'                  MAX TRACKS IN HASPACE
JDFLOG   DCB   DDNAME=JDFLOG,DSORG=PS,MACRF=PM
ABEND7   ABEND 7,DUMP,STEP
ABEND8   ABEND 8,DUMP,STEP
ABEND9   ABEND 9,DUMP,STEP
ABEND10  ABEND 10,DUMP,STEP
ABEND11  ABEND 11,DUMP,STEP
         TITLE 'JDF -  INITIALIZE MESSAGE SUBROUTINE'
INITMSG  MVI   MESSAGE,C' '   BLANK OUT THE
         MVC   MESSAGE+1(79),MESSAGE MESSAGE BUFFER
         MVC   MSGJNME,JQEJNAME PUT IN THE JOB NAME
         LH    R1,JQEJOBNO   JOB'S NUMBER
         TM    JQEFLAG3,QUEJOB  BATCH JOB IF ZEROS
         BNZ   STCORTSU     NO => TRY STC OR TSU
         MVC   MSGJBTYP,JOB   INDICATE IT'S A JOB
         B     CONVERT#       PUT IN THE JOB NUMBER
STCORTSU DS    0H
         CLI   JQEFLAG3,QUETSU  TSO USER ?
         BNE   ITSTC          NO => IT'S STC
         MVC   MSGJBTYP,TSU   INDICATE TSU
         B     CONVERT#       PUT IN THE TSU NUMBER
ITSTC    MVC   MSGJBTYP,STC   INDICATE STC
CONVERT# CVD   R1,DOUBLEWD    CONVERT TO PACKED DECIMAL
         UNPK  MSGJOB#,DOUBLEWD+5(3) CONVERT TO ZONED DECIMAL
         OI    MSGJOB#+L'MSGJOB#-1,C'0' CONVERT TO EBCDIC
         CLI   MSGJOB#,C'0'
         BNE   NUMDONE
         MVI   MSGJOB#,C' '
         CLI   MSGJOB#+1,C'0'
         BNE   NUMDONE
         MVI   MSGJOB#+1,C' '
         CLI   MSGJOB#+2,C'0'
         BNE   NUMDONE
         MVI   MSGJOB#+2,C' '
         CLI   MSGJOB#+3,C'0'
         BNE   NUMDONE
         MVI   MSGJOB#+3,C' '
         CLI   MSGJOB#+4,C'0'
         BNE   NUMDONE
         MVI   MSGJOB#+4,C' '
NUMDONE  DS    0H
         TM    JQEFLAGS,QUEHOLD2+QUEPURGE+QUEOPCAN
         BZR   R7             NO SPECIAL FLAGS
         MVC   MSGSPCST(L'PURGE),PURGE ASSUME THE JOB'S TO BE PURGED
         TM    JQEFLAGS,QUEPURGE JOB TO BE PURGED?
         BOR   R7             YES => RETURN TO OUR CALLER
         MVC   MSGSPCST(L'CANCEL),CANCEL ASSUME JOB CANCELLED
         TM    JQEFLAGS,QUEOPCAN JOB CANCELLED?
         BOR   R7             YES => RETURN TO OUR CALLER
         MVC   MSGSPCST(L'DUPLICAT),DUPLICAT MUST BE DUPLICATE JOB
         BR    R7             RETURN TO OUR CALLER
         TITLE 'JDF -  ADD 1 TABLE ENTRY  SUBROUTINE'
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R7,DNMADDRH    ADDRESS OF NAME LIST
         MVC   TABAREA(80),MESSAGE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R7,TABADDRH    ADDRESS OF CURRENT TABLE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               (7)),                                                   X
               VL,MF=(E,ISPARMS)
         MVI   GOTANYST,C'1'  GOT ONE
         MVI   MESSAGE,C' '
         MVC   MESSAGE+1(79),MESSAGE
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'EXECUTED INSTRUCTIONS'
MVNUMBER MVC   0(0,R15),0(R1) TO BE EXECUTED
JOBNMOVE MVC   USRJNAME(1),2(R14) MOVE BUFFER TO MY DSECT
NAMECLC1 CLC   USRJNAME(1),JQEJNAME    JOBNAME FIRST 5 BYTES
NAMECLC2 CLC   USRJNAME(1),JQEJNAME+3  JOBNAME  LAST 5 BYTES
MOVEDATA MVC   DSNLINE(1),0(R1)   MOVE IN LINE
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT NOGEN
         LTORG
*
         DS    0F
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
CATSMS   DC    CL9'CATALOGS='
UNCATMS  DC    CL11'UNCATALOGS='
NOTCATMS DC    CL24'NOT CATALOGED ERRORS    '
CONDMS   DC    CL16'CONDITION CODES='
NZCONDMS DC    CL24'NON-ZERO CONDITION CODES'
EXPECTED DC    CL13'  EXPECTED:  '
LABEL1   DC    CL13'             '
FOUND    DC    CL13'     FOUND:  '
BOPR1ST  DC    X'01'                   FIRST-TIME-THROUGH SW
DSIDEOF  DS    XL1                     EOF FLAG = 1
GOTSOME  DS    H                       OUTPUT COUNT
*
PDDBSAVE DS    F                       SAVE AREA FOR "DISPLAY LOG"
*
BROWSE   DC    CL8'BROWSE'
DSNOTLST DC    CL44'JOB.OUTLIST'
*
CMSDSN1  DC    CL44'(JOB OUTLIST)'
CMSDSN2  DC    CL44'(JDF CNTL)'
*
EDIT     DC    CL8'EDIT  '
DSNEDIT  DC    CL44'JDF.CNTL   '
*
NOTJQEFN DC    CL44'  JQES NOT FOUND ON CHKPOINT FILE'
NOTJQENO DC    CL44'  NO JQE FOUND FOR THIS JOB NUMBER'
NOTAJCT  DC    CL44'  NO JCT FOUND FOR THIS JOB NUMBER'
NOTAIOT  DC    CL44'  NO IOT FOUND FOR THIS JOB'
NOTPDDB1 DC    CL70'  COULD NOT FIND PDDB FOR DSID 1, CHECK EQU'
NOJOBMSG DC    CL44'     NO JOBS FOUND '
NODDNTAB DC    CL44'     NO DDNAME TABLE FOUND FOR'
WATOUTOF DC    C'AWAITING OUTPUT OF'
HLDSMSG  DC    C'HELD SYSOUT(S)'
AWAITXEQ DC    C'AWAITING EXECUTION CLASS ?,'
HIGHBIT  EQU   X'80'          MAKES JOB CLASS PRINTABLE
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
EDIT10   DC    X'40206B2020206B202120'
*
JOB      DC    C'JOB'
TSU      DC    C'TSU'
STC      DC    C'STC'
HOLD     DC    C'HOLD'
PURGE    DC    C'PURG'
CANCEL   DC    C'CANC'
DUPLICAT DC    C'DUPL'
ACTIVE   DC    C'ACTIVE'      STARTED TASK
LOGGEDON DC    C'LOGGED ON'   TSO USER
EXECUTNG DC    C'EXECUTING CLASS' JOB
INPQUE   DC    C'ON INPUT  CLASS' JOB
PURQUE   DC    C'ON PURGE Q'
SYSREC   DC    C'ON SYS RECV'
DMPQUE   DC    C'ON $DUMP Q'
OUTQUE   DC    C'ON OUT Q'
*
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
JDFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(JDFOPT)
SELECTR  DC    CL8'SELECT  '
JDFOPT   DC    CL8'JDFOPT  '
         SPACE 5
DCB1 DCB DDNAME=HASPCKPT,DSORG=PS,RECFM=U,BLKSIZE=4096,MACRF=(RCP),    X
               LRECL=4096
DCB2 DCB DDNAME=HASPACE,DSORG=PS,RECFM=F,BLKSIZE=3664,MACRF=(RCP),     X
               LRECL=3664
DCB3 DCB DDNAME=OUTLIST,DSORG=PS,MACRF=(PM)
DCB4 DCB DDNAME=JDFCNTL,DSORG=PS,MACRF=(PM)
DCBL     EQU   *-DCB1
CREAD READ  ECB1,SF,DCB1,DUMM,'S',MF=L
LREAD    EQU   *-CREAD
DUMM     DS    0H
*
L78      DC    F'78'
L7       DC    F'7'
FZEROS   DC    F'0'
F8       DC    F'8'
L3       DC    F'3'
L76      DC    F'76'
L20      DC    F'20'
L40      DC    F'40'
*
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
SELECT   DC    C'SELECT   '
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'TSEL     '     SELECTION CODE
JOBLINE  DC    C'JOBLINE )'     JOB INFO
JOBLINE2 DC    C'JOBLINE  '     JOB INFO
JOBLINEP DC    C'(JOBLINE )'    JOB INFO
PIKP     DC    C'(TSEL    )'    SELECTION CODE
*
CMDP     DC    C'(JCMD    )'    COMMAND LINE  FOR JDFDATAB
ZCMD     DC    C'(ZCMD    )'    COMMAND LINE  FOR JDFDSTAB
DCMD     DC    C'(DCMD    )'    COMMAND LINE  FOR JDFDDTAB
CMDDATA  DS    CL40
*
DSNAMLST DC    C'(DSLN   )'     DISPLAY DATA SET NAME LIST
DSLN     DC    C'(DSLN    )'    DATA LINE
*
DDNAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
         DC    C'DSEL     '     SELECTION CODE
         DC    C'DDNLINE )'     JOB INFO
         DC    C'DDNLINE  '     JOB INFO
DDNLINEP DC    C'(DDNLINE )'    JOB INFO
DDSL     DC    C'(DSEL    )'    SELECTION CODE
*
ACOUNT   DS    F
PCOUNT   DS    F
SCOUNT   DS    F
*
SPACES   DC    CL16' '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
DSTABLE1 DC    C'DSTABLE1'
JDFDSTAB DC    C'JDFDSTAB'
DATABLE1 DC    C'DATABLE1'
JDFDATAB DC    C'JDFDATAB'
JDFDDTAB DC    C'JDFDDTAB'
DDTABLE1 DC    C'DDTABLE1'
RETLEN   DC    AL2(2*4096)    LENGTH OF THE RETURN AREA
*
COMMAC   DC    C',C='
PUPDATA  DC    C',P=+99'
*
JCTPTR   DC    A(JCTAREA)
JOEPTR   DC    A(JOEHOLD)
JQEPTR   DC    A(JQEHOLD)
IOTPTR   DC    A(IOTAREA)
DDAREA   DC    A(DDLNTABL)  DDLNTABLE
IXTPTR   DC    A(IXTAREA)   INTERNAL TEXT BUFFER
*
JOBC1    DS    0CL80        JOB CARD
         DC    C'//'        JOB CARD
JOBC1NME DS    CL5          CMS USER ID
         DC    CL17'RTE  JOB  (X0002,'
JOBC1USR DS    CL3          USER CODE
         DC    C','
JOBC1UID DS    CL5          USER ID
         DC    CL47'),''1-YOUR NAME        '','
*
JOBC2    DC    CL31'//   MSGCLASS=Z,CLASS=V,NOTIFY='
JOBC2UID DS    CL5          USER ID
         DC    CL44' '
*
JOBC3    DC    CL80'/*ROUTE PRINT CISMVS           '
*
JOBC4    DC    CL5'//RTR'
JOBC4DS  DS    CL3
         DC    CL15'   EXEC  RTR,J='
JOBC4JOB DS    CL4
         DC    CL3',D='
JOBC4DS2 DS    CL3
         DC    CL3',C='
JOBC4CLS DC    C'A'
         DC    CL25',FORMAT=FBA,REC=133,NODE='
JOBC4RMT DC    CL18'CISMVS    00000000'
*
JOBC5    DC    CL31'//JDFCMD EXEC PGM=JDFCMD,PARM='''
JOBC5CMD DS    CL29
         DC    CL20''' '
*
JOBC6    DC    CL31'//JDFREQ EXEC PGM=JDFREQ,PARM='''
JOBC6JES DS    CL4
         DC    CL45''' '
*
CAPSONLY DC    CL64' '
         DC    CL10' '
         DC    C'.<(+&&'
         DC    CL9' '
         DC    C'!$*)'
         DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
         DC    C'^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
JOBNUMP  DS    D
CKPTECB  DS    F
ISPARMS  DS    9F
RD1SAVE  DS    4F
RD1SAVE7 DS    F
DDIOTRAK DS    F
DDJCTRAK DS    F
READSPA7 DS    F
READMTTR DS    F
SLOGMTTR DS    F
IXTMTTR  DS    XL4                 STARTING TRACK ADDR
IXTJOBID DS    XL4
SAVE5    DS    5F
READAREA DS    F
JQESHOLD DS    A              RETURN ADDRESS HOLD FOR OUTJQES
JOESHOLD DS    A              RETURN ADDRESS HOLD FOR OUTJOES
OUT7HOLD DS    A              RETURN ADDRESS HOLD FOR OUTMSG
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
JOBNUMH  DS    H
*
GOTANYST DS    XL1     DID I FIND ANY MATCHES, Y=1, N=0
READ1ST  DS    X
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
ATABFL   DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
DTABFL   DS    XL1     DO I HAVE A CURRENT DD NAME TABLE
*
CMSFLAG  DS    CL1     "0" INDICATES TSO, "1" INDICATES CMS
*
USRJLENG DS    CL1
*
USRJNAME DS    CL8
USRJCT   DS    CL8
*
COMMAND  DS    CL2
ENTCOMD  DS    CL2
MSGJNAME DS    CL8     SAVE FOR MESSAGE
*
TABAREA  DS    0CL80
TABSEL   DS    CL1
TABDATA  DS    CL79
*
CCFLAG   DS    XL1
*
COPYFLAG DS    XL1
*
DSNLINE  DS    CL133
*
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL40           LINE SELECTION CODE
*
BPSFORM  DC    CL45'  ************************* BPSCAN DATA  FOR '
JOBTYPE  DS    CL3            SAVE JOB TYPE, STC, TSU, OR JOB
JOBNUMS  DS    CL5            SAVE JES JOB NUMBER
         DC    C' '
JOBNAMS  DS    CL8            SAVE JOB NAME
         DC    CL74'  *************************'
*
*                           NOTE THESE FIELDS ARE POSITION-DEPENDENT
CONDNUM  DS    PL3
CATNUM   DS    PL3
UNCATNUM DS    PL3
NCATNUM  DS    PL3
NZCONDS  DS    PL3
*
MESSAGE  EQU   *              OUTPUT MESSAGE BUFFER
MSGJBTYP DS    CL3            JOB OR TSU OR STC
MSGJOB#  DS    CL5            JOB NUMBER
         DS    C
MSGJNME  DS    CL8            JOB NAME
         DS    C
MSGSTYPE DS    CL12           JOB STATUS
MSGSPCST DS    CL4            SPECIAL STATUS
MSGQTYPE DS    CL1            JOB CLASS
         DS    C
MSGALINE DS    CL10
MSGPLINE DS    CL10
MSGSLINE DS    CL10
         DS    C
MSGFORM  DS    CL4            JOB CLASS
         DS    C
MSGBURST DS    CL1            JOB CLASS
         DS    C
         DS    CL2            PAD OUT TO 80
LMESSAGE EQU   *-MESSAGE      LENGTH OF THE MESSAGE BUFFER
*
HASPCKPT DCB DDNAME=HASPCKPT,DSORG=PS,RECFM=U,MACRF=(RCP)
HASPACE  DCB DDNAME=HASPACE,DSORG=PS,RECFM=F,MACRF=(RCP)
OUTLIST  DCB DDNAME=OUTLIST,DSORG=PS,MACRF=(PM)
JDFCNTL  DCB DDNAME=JDFCNTL,DSORG=PS,MACRF=(PM)
CKPTREAD READ  ECB2,SF,DCB1,DUMM,'S',MF=L
         TITLE 'SUB-SYSTEM OUTPUT BLOCK (SSOB)'
         DS    0F
SSOBPTR  DC    X'80',AL3(SSOB)
SSOB     DS    0F
SSOBEGIN EQU   *
SSOBID   DC    CL4'SSOB'          CONTROL BLOCK IDENTIFIER
SSOBLEN  DC    AL2(SSOBHSIZ)      LENGTH OF SSOB HEADER
SSOBFUNC DC    H'1'               FUNCTION ID
SSOBSSIB DC    A(0)               ADDRESS OF SSIB OR ZERO
SSOBRETN DS    F                  RETURN CODE FROM SUBSYSTEM
*
*  THE FOLLOWING RETURN CODES WILL BE RETURNED IN REGISTER 15
*  TO THE ISSUER OF THE IEFSSREQ MACRO -
*
*  SSOBRETN CONTAINS FUNCTION-RELATED RETURN CODES
*    (DEFINED IN EACH FUNCTION EXTENSION)
*
SSRTOK   EQU   0                  SUCCESSFUL COMPLETION - REQUEST WENT
*                                 TO A SUBSYSTEM.
SSRTNSUP EQU   4                  SUBSYSTEM DOES NOT SUPPORT THIS
*                                 FUNCTION
SSRTNTUP EQU   8                  SUBSYSTEM EXISTS, BUT IS NOT UP
SSRTNOSS EQU   12                 SUBSYSTEM DOES NOT EXIST
SSRTDIST EQU   16                 FUNCTION NOT COMPLETED-DISASTROUS
*                                 ERROR
SSRTLERR EQU   20                 LOGICAL ERROR (BAD SSOB FORMAT,
*                                 INCORRECT LENGTH,...)
*
SSOBINDV DC    A(*+4)             FUNCTION DEPENDENT AREA POINTER
SSOBHSIZ EQU   *-SSOBEGIN         SSOB HEADER LENGTH
SSOBGN   EQU   *                  START OF OPTIONS DATA
*
*  PROCESS SYSOUT DATA SETS RETURN CODES (SSOBRETN)
*
SSSORTOK EQU   0                  EVERYTHING IS OK
SSSOEODS EQU   4                  NO MORE DATA SETS TO SELECT
SSSONJOB EQU   8                  JOB NOT FOUND
SSSOINVA EQU   12                 INVALID SEARCH ARGUMENTS
SSSOUNAV EQU   16                 UNABLE TO PROCESS NOW
SSSODUPJ EQU   20                 DUPLICATE JOBNAMES
SSSOINVJ EQU   24                 INVALID JOBNAME/JOBID COMBINATION
SSSOIDST EQU   28                 INVALID DESTINATION SPECIFIED
*
         ORG   SSOBGN
SSSOBGN  EQU   *
SSSOLEN  DC    AL2(SSSOSIZE)      SYSOUT EXTENSION LENGTH
SSSOUFLG DS    B                  USER SELECTION OPTIONS CLASS
*                                 ROUTING AND DISPOSITION FLAGS
SSSOSETC EQU   X'80'              USE SSSOCLAS AS DISPOSITION
SSSODELC EQU   X'40'              DELETE SELECTED DATA SET
SSSOROUT EQU   X'20'              REROUTE SELECTED DATA SET TO
*                                 DESTINATION IN SSSODEST
SSSOHOLD EQU   X'10'              HOLD ALL SELECTED DATA SETS
SSSORLSE EQU   X'08'              RELEASE ALL SELECTED DATA SETS
*        EQU   X'07'              RESERVED FLAGS
SSSORESV DS    X                  RESERVED
SSSOFLG1 DS    B                  DATA SET SELECTION CONTROL FLAGS
SSSOHLD  EQU   X'80'              SELECTION SHOULD INCLUDE HELD
*                                 SYSOUT DATA SETS
SSSOSCLS EQU   X'40'              USE CLASS
SSSODST  EQU   X'20'              USE REMOTE DESTINATION
SSSOSJBN EQU   X'10'              USE JOB NAME
SSSOSJBI EQU   X'08'              USE JOB ID
SSSOSPGM EQU   X'04'              USE USER WRITER PROGRAM NAME
SSSOSFRM EQU   X'02'              USE FORM NUMBER
SSSORSV2 EQU   X'01'              RESERVED
*
SSSOFLG2 DS    B                  CURRENT DATA SET DISPOSITION FLAGS
SSSOCTRL EQU   X'80'              1 - PROCESSING COMPLETED
*                                 0 - RETURN DATA SET NAME
SSSOCHKP EQU   X'40'              USE SSSORBA TO CHECKPOINT RBA
*                                 OF CURRENT DATA SET IN CLASS
SSSORSV3 EQU   X'3F'              RESERVED FLAGS
SSSOCOPY DS    H                  NUMBER OF COPIES
SSSOJOBN DS    CL8                JOB NAME
SSSOJOBI DS    CL8                JOB ID
SSSOCLAS DS    CL1                NAME OF DESTINATION CLASS SPECIFIED
*                                 VIA THE NEWCLASS PARAMETER
SSSORSV5 DS    CL3                RESERVED
SSSODEST DS    CL8                REMOTE DESTINATION SPECIFIED
*                                 VIA THE DEST PARAMETER
SSSOPGMN DS    CL8                USER WRITER NAME
SSSORBA  DS    CL8                RBA OF SYSOUT DATA SET
SSSODSN  DS    CL44               SYSOUT DATA SET NAME
SSSOFORM DS    CL4                FORM NUMBER
*
*  SSSOCLSL WILL CONTAIN 1-8 CLASSES WHEN USED FOR REROUTING OR DELETE
*  FUNCTIONS AND WILL CONTAIN ONLY ONE CLASS WHEN USED FOR PRINTING.
*
SSSOCLSL DS    CL8                CLASS SELECTION LIST FOR
*                                 DATA SET SELECTION
SSSOWTRC DS    A                  A POINTER TO A COMMUNICATION  YM02726
*                                 AREA FOR THE USER WRITTEN     YM02726
*                                 WRITER                        YM02726
SSSODSID DS    CL8                DATA SET ID TO PLACE SYSOUT  @Z30OPSD
*                                 ON EXTERNAL DEVICES          @Z30OPSD
*
SSSOSIZE EQU   *-SSSOBGN          SYSOUT EXTENSION LENGTH
SSOBLEN1 EQU   SSOBHSIZ+SSSOSIZE  SSOB LENGTH=HEADER + SYSOUT EXTENSION
         TITLE 'SS CANCEL CONTROL BLOCK'
SSOBCANC EQU   2                  CANCEL FUNCTION ID (SSOBFUNC)
SSOBSTAT EQU   3                  JOB STATUS FUNCTION ID (SSOBFUNC)
*
*  CANCEL/STATUS RETURN CODES (SSOBRETN)
*
SSCSRTOK EQU   0                  CANCEL/STATUS COMPLETED
SSCSNOJB EQU   4                  JOB NAME NOT FOUND
SSCSBADI EQU   8                  INVALID JOBNAME/JOB ID COMBINATION
SSCSNCAN EQU   12                 JOB NOT CANCELLED - DUPLICATE
*                                 JOBNAMES AND NO JOB ID GIVEN
SSCSMALL EQU   16                 STATUS ARRAY TOO SMALL
SSCSOUTP EQU   20                 JOB NOT CANCELLED-JOB ON OUTPUT QUEUE
SSCSYNTX EQU   24                 JOBID WITH INVALID SYNTAX FOR
*                                 SYBSYSTEM                     YM06023
SSCSICAN EQU   28                 INVALID CANCEL REQUEST - CANNOT
*                                 CANCEL AN ACTIVE TSO USER OR STARTED
*                                 TASK / TSO USER MAY NOT CANCEL THE
*                                 ABOVE JOBS UNLESS THEY ARE ON AN
*                                 OUTPUT QUEUE.                 YM06036
*
         ORG   SSOBGN
SSCSBGN  EQU   *
SSCSLEN  DC    AL2(SSCSIZE)       CANCEL/STATUS EXTENSION LENGTH
SSCSFLGS DS    B                  USER SELECTION FLAGS
SSCSUSID EQU   X'80'              USERID IS IN JOBNAME FIELD
SSCSCOUT EQU   X'40'              CANCEL THE JOBS OUTPUT         Y02886
*        EQU   X'3F'              RESERVED FLAGS
SSCSULEN DS    X                  USERID LENGTH
SSCSJOBN DS    CL8                JOB NAME
SSCSJOBI DS    CL8                JOB ID OR BLANKS
SSCSDIMP DS    H                  SET BY CALLER TO INDICATE SIZE
*                                 OF ARRAY AVAILABLE TO SUBSYSTEM
*                                 TO STORE RESULTS IN
SSCSDIMR DS    H                  SET BY SUBSYSTEM TO INDICATE SIZE
*                                 OF ARRAY USED, OR NEEDED IF NOT
*                                 ENOUGH IS AVAILABLE
         SPACE
*
*  SSCSARAY MAPS AN ELEMENT OF AN ARRAY GOTTEN BY THE CALLER FOR
*  THE SUBSYSTEM TO RETURN RESULTS IN.  IF MORE THAN ONE ELEMENT
*  EXISTS, ADDRESSABILITY TO THIS ARRAY MUST BE UPDATED BY THE
*  ELEMENT SIZE (SSCSELSZ). THE TOTAL ARRAY SPACE USED FOR JOB
*  STATUS REPLIES FROM THE SUBSYSTEM(ARRAY ELEMENT SIZE IN BYTES
*  TIMES THE NUMBER OF ELEMENTS) MUST BE INDICATED IN SSCSDIMR.
*  MESSAGES MUST FOLLOW THE LAST SSCSARAY ELEMENT USED FOR JOB
*  STATUS.
*
SSCSARAY DS    0CL16
SSCSARBG EQU   *
SSCSARID DS    CL8                JOB IDENTIFIER
SSCSFLG1 DS    B                  FLAGS SET BY SUBSYSTEM
SSCSJACT EQU   X'80'              JOB IS CURRENTLY ACTIVE (EXECUTING
*                                 AFTER BEING GIVEN CONTROL BY THE
*                                 INITIATOR)
SSCSEXCQ EQU   X'40'              JOB IS WAITING FOR EXECUTION (ON A
*                                 PRE-EXECUTION QUEUE)
SSCSOUTQ EQU   X'20'              JOB IS ON OUTPUT QUEUE
SSCSHOLD EQU   X'10'              JOB IS HELD IN ITS CURRENT QUEUE
SSCSSECL EQU   X'08'              JOB HAS A SECOND LEVEL MESSAGE
SSCSNJEA EQU   X'04'                    JOB ACTIVE IN NJE    @AZ59113
*        EQU   X'03'              RESERVED FLAGS
SSCSUJOB DS    CL1                JOBNAME CHARACTER RETURNED
*                                 BY SUBSYSTEM FOR USERID AS JOBNAME
SSCSRSV2 DS    CL2                RESERVED
SSCSMPTR DS    A                  POINTER TO MESSAGE RETURNED IN ARRAY
*
*  THE SECOND LEVEL MESSAGE AREA IS MAPPED BY A MULTI-LEVEL PUTLINE
*  OUTPUT LINE DESCRIPTOR (OLD).  THE FIRST 9 BYTES OF THE FIRST OR
*  ONLY MESSAGE SEGMENT ARE RESERVED FOR THE INSERTION OF A MESSAGE
*  ID BY THE CALLER.  ONE TO 62 BYTES OF MESSAGE TEXT MAY BE PROVIDED
*  BY THE SUBSYSTEM. A MAPPING OF THE OLD FORMAT NEEDED FOLLOWS.
*
*  SSCSMPTR -> ONE TSO PUTLINE OUTPUT LINE DESCRIPTOR (OLD)
*     +0 0 (NO OTHER OLD)
*     +4 NUMBER OF MESSAGE SEGMENTS
*     +8 PTR TO FIRST MESSAGE SEGMENT
*     +. PTR TO NTH MESSAGE SEGMENT
*     ........ MESSAGE SEGMENT FORMAT......
*     +0 TOTAL LENGTH OF MSG SEGMENT (INCLUDING THIS FIELD)
*     +2 0 IF FIRST SEGMENT, OR OFFSET FOR INSERT IN FIRST
*     +4 10 BLANKS FOR MSG ID (ONLY IN FIRST SEGMENT)
*     +D MESSAGE TEXT (1-62 BYTES TOTAL FOR ALL SEGMENTS)
*
SSCSELSZ EQU   *-SSCSARAY         ARRAY ELEMENT SIZE
SSCSIZE  EQU   *-SSCSBGN          CANCEL/STATUS EXTENSION LENGTH
SSOBLEN2 EQU   SSOBHSIZ+SSCSIZE   TOTAL SSOB LENGTH
         TITLE 'BUFFER AREAS'
         DS    0D
         DC    C'JQE HOLD'
JQEHOLD  DS    XL40960        HOLD 10 RECORDS
BRO24K   DS    XL24576
         DS    0D
         DC    C'JCT HOLD'
         DS    XL88
JCTAREA  DS    XL4096         JCT
         DS    0D
         DC    C'IOT AREA'
         DS    XL88
IOTAREA  DS    XL4096         IOT
         DS    0D
         DC    C'PDDB TAB'
DDLNTABL DS    XL8192
         DS    0D
         DC    C'INT TEXT'
         DS    XL88
IXTAREA  DS    XL4096         INTERNAL TEXT INPUT AREA
         DC    C'JOE HOLD'
JOEHOLD  DS    XL40960        HOLD 10 RECORDS
         TITLE 'DD LINE DSECT'
DDLINE   DSECT
DDLDSID  DS    CL3
         DS    CL1
         DS    CL1
STEPNAME DS    CL8
         DS    CL1
DDN      DS    CL8
         DS    CL1
DDLINES  DS    CL8
         DS    CL2
DDCLASS  DS    CL1
         DS    CL2
DDFORM   DS    CL4
         DS    CL1
DDFCB    DS    CL4
         DS    CL1
DDUCS    DS    CL4
         DS    CL1
DDCOPIES DS    CL3
DDBURST  DS    CL1
DDDRECL  DS    CL6
         DS    CL1
DDDRECF  DS    CL3
         DS    CL1
DDDDMTTR DS    CL8
DDLINSHO EQU   *-DDLINE
DDMTTR   DS    CL4
DDLRECL  DS    XL2                    PDDB LRECL
DDRECFM  DS    X                      PDDB RECFM
DDLINESZ EQU   *-DDLINE
*
JDF      CSECT
*
         $JQE  DOC=YES
         $JOE  DOC=YES
         $PDDB DOC=YES
BUFDSECT EQU   0
BUFSTART EQU   88
         $IOT  DOC=YES
JCT      EQU   1
         $JCT  DOC=YES
         $NHD  DOC=YES
         END
************************************************************
*                                                          *
*         PHASE 2 INITIALIZATION                           *
*                                                          *
************************************************************
         SPACE
*
*         FROM THIS POINT ON, PROCESSING IS THE SAME
*         FOR BOTH DSNAME AND DDNAME OPTIONS.
*
*        BRDSNAME  - CONTAINS THE FULLY QUALIFIED DSNAME,
*                    FROM THE USER OR FROM THE JFCB.
*         $MEMBER  - CONTAINS THE MEMBER NAME, IF SPECIFIED,
*                    FROM THE USER OR FROM THE JFCB.
*         $DDNAME  - CONTAINS THE DDNAME, FROM IKJDAIR OR
*                    FROM THE USER.
*         $UCBAD   - CONTAINS THE UCB ADDRESS,
*                    FROM THE TIOT ENTRY.
*         $VOLSER  - CONTAINS THE VOLUME, FROM THE UCB POINTED
*                    TO BY THE TIOT ENTRY.
*
*         IF THE USER SPECIFIED A DSNAME (DID NOT SPECIFY THE FILE
*         KEYWORD) THEN THE STALLOC BIT IS SET ON SO THE DDNAME
*         WILL BE UNALLOCATED WHEN THE COMMAND IS FINISHED WITH IT.
*
JDFBRO   CSECT
         USING *,R10,R11,R12
         STM   R14,R12,12(R13)        SAVE CALLING PROGRAM'S REGISTERS
         LR    R10,R15             RESET BASE REGISTER
         LH    R15,FOURK
         LA    R11,0(R15,R10)   BASE
         LA    R12,0(R15,R11)   BASE
*
         LM    R2,R4,0(R1)             PICK UP PARMS
*                                      R2 POINTS TO PDDB TABLE ENTRY
*                                      R3 POINTS TO DSNAME
*                                      R4 POINTS TO 4K WORK AREA
         LR    R15,R13                SAVE CALLING PROGRAM'S SAVE
         LR    R13,R4
         USING @DATA,13
         ST    R15,4(,R13)             SAVE HIGHER SAVE AREA IN MINE
         ST    R13,8(,R15)             SAVE MY SAVE AREA IN HIGHER
*
         MVC   BRDSNAME(30),2(R3)      SET DSNAME
         USING DDLINE,2
         MVC   JESMTTR(4),DDMTTR      SAVE DSID START
         MVC   TOPMTTR(4),DDMTTR      SAVE DSID START
         MVC   JESLRECL(2),DDLRECL    SAVE LRECL
         MVC   WRKRECFM,DDRECFM       SAVE RECFM
         MVC   TSTRECFM,DDRECFM       SAVE RECFM
*
         MVC   IXTJOBID(4),FZEROS  FIRST READ
*
         TM    DDRECFM,X'C0'      RECFM U
         BNO   SHOWINIT            NO, BRANCH
         NI    TSTRECFM,255-X'C0'  YES, SET BOTH BITS OFF
         B     SHOWINIT
*
FOURK    DC    H'4096'
ACTAB    DC    C'0'       0=NO ACTIVE TABLE, 1=ACTIVE TABLE DEFINED
         EJECT
SHOWINIT DS    0H
*
         LA    R1,20
         STH   R1,SCROWS1
*
OKOPEN   DS    0H
************************************************************
*                                                          *
*        SET UP THE BUFFER AREA                            *
*                                                          *
************************************************************
         SPACE
         LH    R0,FOURK
         AH    R0,=H'7'            ROUND
         N     R0,=A(X'FFFFFFF8')   UPWARDS TO MULTIPLE OF 8
         ST    R0,SAVBLKSI         SAVE FOR PARTITIONING
         SPACE
         SLR   R0,R0
         TM    TSTRECFM,X'48'      VS OR VBS
         BNO   NOTSPAN             NO, BRANCH
         LH    R0,JESLRECL         YES, GET LRECL
         AH    R0,=H'7'            ROUND
         N     R0,=A(X'FFFFFFF8')   UPWARDS TO MULTIPLE OF 8
         LTR   R0,R0               WAS LRECL ZERO
         BNZ   *+8                 NO, SKIP NEXT INSTR
         L     R0,SAVBLKSI         YES, USE BLKSIZE ROUNDED
         A     R0,=A(2048)         ADD 2K IN CASE LRECL IS WRONG
NOTSPAN  ST    R0,SAVSPANL         SAVE FOR PARTITIONING
         SPACE
         LH    R0,JESLRECL         YES, GET LRECL
         LTR   R0,R0
         BNZ   *+8
         LH    R0,FOURK            JES BLKSIZE
         STH   R0,RECSIZE
         AH    R0,=H'7'            ROUND
         N     R0,=A(X'FFFFFFF8')   UPWARDS TO MULTIPLE OF 8
         CH    R0,=H'1000'         IS LRECL GREATER THAN MAX
         BNH   *+8                 NO, SKIP NEXT INSTR
         LH    R0,=H'1000'         YES, HELD RECORDS MUST BE TRUNCATED
         ST    R0,SAVLRECL         SAVE FOR HOLD AREA DIVISION
         MH    R0,SCROWS1
         ST    R0,SAVHOLDL         SAVE FOR PARTITIONING
         SPACE
         A     R0,SAVSPANL         COMBINE LENGTHS FOR GETMAIN
         A     R0,SAVBLKSI         COMBINE LENGTHS FOR GETMAIN
         A     R0,=A(20*200)       PLUS ROOM FOR 200 CHECKPOINTS
         LA    R7,RANGE
         ST    R0,0(,R7)
         A     R0,=A(20*824)       OPTIONAL ROOM FOR 1024 CHECKPOINTS
*        L     R0,=A(16*1024*1024-8) 16 MEG
         ST    R0,4(,R7)
         LA    R8,ANSWER
         MVC   GMVUW(GMVUL),GMVU
         GETMAIN VU,LA=(R7),A=(R8),MF=(E,GMVUW)
         OI    STATUS,STGMVU
*        L     R0,ANSWER+4         LENGTH
*        L     R1,ANSWER           ADDRESS
*        FREEMAIN R,LV=(0),A=(1)
         L     R1,ANSWER
         LR    R0,R1
         A     R0,ANSWER+4
         ST    R0,ENDPTR           END OF GETMAINED AREA
         ST    R1,BLOCKPTR
         A     R1,SAVBLKSI         POINT PAST BLOCK AREA
         ST    R1,SPANPTR
         A     R1,SAVSPANL         POINT PAST SPANNED RECORD AREA
         ST    R1,HOLDPTR
         A     R1,SAVHOLDL         POINT PAST HOLD AREA
         ST    R1,CHKPTTOP
         XC    0(16,R1),0(R1)      FIRST ENTRY IN TABLE
         XC    DEBLOCKS(12),DEBLOCKS
         XC    OFFSET,OFFSET
         LH    R0,SCROWS1          LINES PER PAGE
         ST    R0,SCROLL
         SLR   R0,R0
         ST    R0,FNDNUM
         STH   R0,FNDOFF
         ST    R0,CHKPTBOT
         MVC   PERIODS,CAPST
*
         LA    R2,HEXFOX           INDICATE END OF BLOCK
         SR    R1,1
         SR    R0,0
         STM   R0,R2,DEBLOCKS
*
************************************************************
*                                                          *
*         INITIALIZE HOLD AREA DIRECTORY                   *
*                                                          *
************************************************************
HOLDINIT DS    0H
         LA    R7,HOLDDIR
         SLR   R0,R0
         L     R15,HOLDPTR
         LH    R8,SCROWS1          LINES PER PAGE
HOLDLOOP ST    R0,DIRNUM(,R7)      PUT ZERO IN RECORD LENGTH
         ST    R0,DIRLEN(,R7)      PUT ZERO IN RECORD NUMBER
         ST    R15,DIRREC(,R7)     STORE ADDRESS OF HELD RECORD
         A     R15,SAVLRECL        POINT TO NEXT HELD RECORD
         LA    R1,DIRSIZ(,R7)      POINT TO NEXT ENTRY
         ST    R1,DIRNXT(,R7)      STORE ITS ADDRESS IN THIS ENTRY
         LR    R14,R7              SAVE LAST ENTRY
         LR    R7,R1               MAKE NEXT ENTRY THIS ENTRY
         BCT   R8,HOLDLOOP         DO IT FOR ALL BUT LAST ENTRY
         LA    R1,HOLDDIR          ADDRESS OF FIRST ENTRY
         ST    R1,DIRNXT(,R14)     CLOSE THE CIRCLE
         ST    R1,HOLDTOP          START WITH FIRST AS TOP
*
         BAL   R14,FILLHOLD
         B     FILLSCR
         SPACE
************************************************************
*                                                          *
*         READ ENOUGH RECORDS TO FILL HOLD AREA            *
*                                                          *
************************************************************
         SPACE
FILLHOLD ST    R14,HOLDR
         L     R6,HOLDPTR
         LR    R0,R6
         L     R1,SAVHOLDL
         SLR   R14,R14
         L     R15,=A(X'40000000')
         MVCL  R0,R14              FILL HOLD AREA WITH BLANKS
         L     R7,HOLDTOP          POINT TO HOLD AREA DIRECTORY
         LH    R8,SCROWS1          NUMBER OF DATA LINES PER SCREEN
HILLOOP  EQU   *
         BAL   R14,GET
         LA    R0,1                COMPUTE
         A     R0,COUNT             THE
         ST    R0,COUNT              RECORD NUMBER
         ST    R0,DIRNUM(,R7)      STORE THE RECORD NUMBER
         ST    R1,DIRLEN(,R7)      LENGTH IN DIRECTORY
         LTR   R1,R1               END OF FILE
         BM    HILLED              YES BRANCH
         C     R1,SAVLRECL         IS RECORD LONGER THAN MAX
         BNH   *+8                 NO, SKIP NEXT INSTR
         L     R1,SAVLRECL         YES, TRUNCATE TO MAX
         LR    R0,R2               ADDRESS TO MOVE FROM
         L     R14,DIRREC(,R7)     ADDRESS TO MOVE TO
         LR    R15,R1              LENGTH FOR MOVE
         MVCL  R14,R0
         LR    R1,R7               SAVE PTR TO MOST CURRENT ENTRY
         L     R7,DIRNXT(,R7)      POINT TO NEXT DIRECTORY ENTRY
         BCT   R8,HILLOOP
         LR    R7,R1               GET MOST CURRENT ENTRY
HILLED   EQU   *
         ST    R7,HOLDEND          SAVE LAST RECORD ENTRY
         L     R14,HOLDR
         BR    R14
SAV3     DS    3F
         SPACE
************************************************************
*                                                          *
*         SET UP THE SCREEN HEADER PRIOR TO DISPLAY        *
*                                                          *
************************************************************
         SPACE
FILLSCR  DS    0H
************************************************************
*                                                          *
*         FILL IN THE DATA AREA OF THE SCREEN              *
*                                                          *
************************************************************
*
         CLI   ACTAB,C'0'         IS THERE AN ACTIVE TABLE
         BE    TABDEF             NO, GO DEFINE IT
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               BRLINEP),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               BRCMD),                                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
TABDEF   DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               BRLINEP,LINEAREA,CHAR,L80),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               BRCMD,BCMD,CHAR,L48),                                   X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               BRTABLE1,                                               X
               NULLENT,                                                X
               BRNAMLST,                                               X
               NOWRITE,REPLACE),                                       X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         MVI   ACTAB,C'1'         THERE IS AN ACTIVE TABLE
*
*
         MVC   BCMD(48),CMDSAVE
*
         L     R7,HOLDTOP
         MVC   JESNAME+56(4),=C'LINE'
         L     R1,DIRNUM(,R7)      GET NUMBER OF FIRST LINE
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  JESNAME+60(7),DOUBLE+4(4)
         MVI   JESNAME+60,C' '
         MVC   LINEAREA(80),JESNAME
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               BRNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         SR    R5,R5
         MVC   JESNAME+68(10),SPACES
         LH    R8,SCROWS1          NUMBER OF DATA LINES
FILLOOP  EQU   *
         LM    R1,R2,DIRLEN(R7)    GET LENGTH AND ADDRESS
         LTR   R1,R1
         BM    SHOWTABL
         MVC   LINEAREA(80),SPACES              BLANKS
         BZ    FILNUL              BRANCH IF R1 ZERO
         SH    R1,OFFSET           IS OFFSET BEYOND END OF RECORD
         BNP   FILLNEXT            YES, LEAVE LINE BLANK
         AH    R2,OFFSET
         TM    MODE,MODEX
         BO    FILLHEX
         CH    R1,=H'80'
         BNH   *+8
         LA    R1,80
         BCTR  R1,0
         B     *+10
         MVC   LINEAREA(1),0(R2)
         EX    R1,*-6
         TR    LINEAREA(80),PERIODS
         B     FILLNEXT
FILLHEX  CH    R1,=H'40'
         BNH   *+8
         LA    R1,40
         LR    R0,R1
         LR    R1,R2
         LR    R15,R6
         BAL   R14,HEX
         B     FILLNEXT
FILNUL   MVC   0(6,R6),=C'(NULL)'
*
FILLNEXT LA    R5,01(,R5)          COUNT LINES FILLED IN
*
ADDALINE DS    0H
*
         MVC   BCMD(48),CMDSAVE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               BRNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R7,DIRNXT(,R7)
         BCT   R8,FILLOOP
         B     SHOWTABL
*
SHOWTABL DS    0H
*
*
         MVC   LINEAREA(80),LASTLINE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               BRNAMLST),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
*
JUSTSHOW DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               BRTABLE1,                                               X
               JDFBRTAB),                                              X
               VL,MF=(E,ISPARMS)
*
         CLI   BCMD,C' '      NO COMMAND ENTERED
         BE    SHOWDONE
*
         CLI   BCMD,C'L'
         BE    GOTLCMD
         CLI   BCMD,C'R'
         BE    GOTRCMD
         MVC   CMDSAVE(48),BCMD
*
         B     GOTACMD
*
GOTLCMD  DS    0H
         MVC   CMDSAVE(1),CMDSAV1
         B     GOTACMD
*
GOTRCMD  DS    0H
         MVC   CMDSAV1(1),CMDSAVE
         MVI   CMDSAVE,C'L'   IF "R" WAS ENTERED, "L" WILL
         B     GOTACMD         PROBABLY BE THE NEXT, SO PLUG IT IN
*
CMDSAVE  DC    CL48' '
CMDSAV1  DC    C' '
*
LASTLINE DC    X'03'
         DC    CL50' D = DOWN, U = UP, R = RIGHT, L = LEFT, T = TOP, B'
         DC    CL29' = BOTTOM, F = FIND, E = END '
*
GOTACMD  DS    0H
*
************************************************************
*                                                          *
*         PARSE THE COMMAND OPERANDS, IF ANY               *
*                                                          *
************************************************************
*
         XC    OPD1(4),OPD1              ZERO IT
         CLC   BCMD+1(47),SPACES         ANY OPERANDS
         BE    CMDSCAN                   NOPE
*
         LA    R1,BCMD+1                 DEFAULT LOCATION
         CLI   0(R1),C' '                GOT STUFF
         BNE   GOTOPND1                  YUP
         LA    R1,1(,1)                  BUMP 1
         CLI   0(R1),C' '                GOT STUFF
         BE    CMDSCAN                   NO, FORGET IT
*
GOTOPND1 DS    0H
*
         ST    R1,OPD1                   SAVE IT
*
         SR    R14,14                    LENGTH COUNTER
         LA    R15,46                    MAXIMUM LENGTH
*
LUPOPND1 DS    0H
*
         LA    R1,0(R1,R15)              BUMP
         CLI   0(R1),C' '                END OF PARM
         BNE   GOTOPLNG                  YES
         LA    R14,1(,14)                BUMP COUNTER
         BCT   R15,LUPOPND1              LOOP
*
GOTOPLNG DS    0H
*
         LA    R14,1(,14)                BUMP COUNTER
         STH   R14,OPD1+4                SAVE THE LENGTH
*
************************************************************
*                                                          *
*         PARSE THE COMMAND AND OPERANDS                   *
*                                                          *
************************************************************
         SPACE
CMDSCAN  EQU   *
         CLI   BCMD,C'L'
         BE    LEFT
         CLI   BCMD,C'R'
         BE    RIGHT
         CLI   BCMD,C'U'
         BE    UP
         CLI   BCMD,C'D'
         BE    DOWN
         CLI   BCMD,C'T'
         BE    TOP
         CLI   BCMD,C'B'
         BE    BOTTOM
         CLI   BCMD,C'F'
         BE    FIND
         CLI   BCMD,C'/'
         BE    FIND
*        CLI   BCMD,C'S'
*        BE    SETSCRN
         CLI   BCMD,C'E'
         BE    END
         CLI   BCMD,C'Q'
         BE    END
         LA    R1,BCMD
         CLC   0(5,R1),=C'ASIS '
         BE    ASIS
         CLC   0(5,R1),=C'CAPS '
         BE    CAPS
         CLC   0(4,R1),=C'HEX '
         BE    HEXMODE
         CLC   0(5,R1),=C'SNAP '
         BE    SNAP
         B     JUSTSHOW
         SPACE
************************************************************
*                                                          *
*         RIGHT                                            *
*                                                          *
************************************************************
         SPACE
SETSCRN  DS    0H
         L     R14,OPD1            ARE THERE ANY OPERANDS
         LTR   R14,R14
         BZ    JUSTSHOW
         LH    R1,OPD1+4           GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    ERRINV              YES, ERROR
         CH    R1,=H'7'            IS LENGTH MORE THAN 7
         BH    ERRINV              YES, ERROR
         BCTR  R1,0
         B     *+10
         TRT   0(0,R14),NUMERIC
         EX    R1,*-6              IS IT NUMERIC
         BNZ   ERRINV              NO, ERROR
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LTR   R15,R1
         BZ    JUSTSHOW
         STH   R15,SCROLL
         B     FILLSCR
*
************************************************************
*                                                          *
*         RIGHT                                            *
*                                                          *
************************************************************
         SPACE
RIGHT    EQU   *
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    RIGHTDEF            NO, USE DEFAULT
         LH    R1,4(,R15)          GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    ERRINV              YES, ERROR
         CH    R1,=H'7'            IS LENGTH MORE THAN 7
         BH    ERRINV              YES, ERROR
         L     R14,0(,R15)         GET ADDRESS OF DATA
         BCTR  R1,0
         B     *+10
         TRT   0(0,R14),NUMERIC
         EX    R1,*-6              IS IT NUMERIC
         BNZ   ERRINV              NO, ERROR
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LTR   R15,R1
         BZ    JUSTSHOW
         B     RIGHTR15
RIGHTDEF LA    R15,80
         TM    MODE,MODEX
         BZ    *+8
         LA    R15,40
RIGHTR15 LH    R0,OFFSET
         AR    R0,R15
         LH    R1,RECSIZE
         CH    R1,=H'1000'
         BNH   *+8
         LH    R1,=H'1000'
         SR    R1,R15              GET RECSIZE-80
         BNM   *+6                 IF RECSIZE IS LESS THAN 80
         SLR   R1,R1                  THEN RECSIZE-80 IS ZERO
         CR    R0,R1               IF OFFSET+80 GT RECSIZE-80
         BNH   *+6                    THEN
         LR    R0,R1                  USE RECSIZE-80
         STH   R0,OFFSET
         B     FILLSCR
         SPACE
************************************************************
*                                                          *
*         LEFT                                             *
*                                                          *
************************************************************
         SPACE
LEFT     EQU   *
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    LEFTDEF             NO, USE DEFAULT
         TM    6(R15),QUOTED       IS OPERAND QUOTED
         BO    ERRINV              YES, INVALID
         LH    R1,4(,R15)          GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    ERRINV              YES, ERROR
         CH    R1,=H'7'            IS LENGTH MORE THAN 7
         BH    ERRINV              YES, ERROR
         L     R14,0(,R15)         GET ADDRESS OF DATA
         BCTR  R1,0
         B     *+10
         TRT   0(0,R14),NUMERIC
         EX    R1,*-6              IS IT NUMERIC
         BNZ   ERRINV              NO, ERROR
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LTR   R15,R1
         BZ    JUSTSHOW
         B     LEFTR15
LEFTDEF  LA    R15,80
         TM    MODE,MODEX
         BZ    *+8
         LA    R15,40
LEFTR15  LH    R0,OFFSET
         SR    R0,R15              REDUCE OFFSET BY 80
         BNM   *+6                 IF RESULT IS NEGATIVE
         SLR   R0,R0                  THEN MAKE IT ZERO
         STH   R0,OFFSET
         B     FILLSCR
         SPACE
************************************************************
*                                                          *
*         DOWN                                             *
*                                                          *
************************************************************
*
DOWN     EQU   *
         L     R0,SCROLL
*
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    DOWNRGE             NO, USE RANGE
         TM    6(R15),QUOTED       IS OPERAND QUOTED
         BO    ERRINV              YES, INVALID
         LH    R1,4(,R15)          GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    ERRINV              YES, ERROR
         CH    R1,=H'7'            IS LENGTH MORE THAN 7
         BH    ERRINV              YES, ERROR
         L     R14,0(,R15)         GET ADDRESS OF DATA
         BCTR  R1,0
         B     *+10
         TRT   0(0,R14),NUMERIC
         EX    R1,*-6              IS IT NUMERIC
         BNZ   ERRINV              NO, ERROR
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LTR   R0,R1
         BZ    JUSTSHOW
DOWNRGE  ST    R0,DOWNAMT
         BAL   R14,DOWNER
         LTR   R15,R15             ACTION TAKEN
         BNZ   JUSTSHOW            NO, BRANCH
         BNZ   JUSTSHOW
         B     FILLSCR             YES, BRANCH
DOWNER   EQU   *
*
*              SEE IF EOF IS ON SCREEN AND WOULD BE FORCED OFF
*
         LA    R15,4
         L     R7,HOLDEND          POINT TO ENTRY FOR LAST RECORD
         TM    DIRLEN(R7),X'80'    IS EOF ON SCREEN
         BZ    DOWNNEOF            NO, DOWN IS POSSIBLE
         L     R7,HOLDTOP
         L     R0,DOWNAMT          GET DOWN AMOUNT
         CH    R0,SCROWS1          OR SCREEN SIZE IF SMALLER
         BNH   *+8
         LH    R0,SCROWS1
DOWNTEST TM    DIRLEN(R7),X'80'    IS THIS END OF DATA
         BO    0(R14)              YES, CANT GO DOWN
         L     R7,DIRNXT(,R7)      GET ENTRY FOR NEXT RECORD
         BCT   R0,DOWNTEST
         L     R7,HOLDEND          POINT TO ENTRY FOR LAST RECORD
DOWNNEOF ST    R14,DOWNR
         L     R8,DOWNAMT          GET NUMBER OF LINES TO GO DOWN
DOWNLOOP L     R15,HOLDTOP         POINT TO TOP ENTRY
         L     R15,DIRNXT(,R15)    GET ADDRESS OF ENTRY AFTER TOP
         ST    R15,HOLDTOP         MAKE IT NEW TOP
         TM    DIRLEN(R7),X'80'    IS EOF ON SCREEN
         BO    DOWNNXT             YES, BYPASS GET
         L     R7,DIRNXT(,R7)      POINT TO NEXT ENTRY
         BAL   R14,GET
         LA    R0,1                COMPUTE
         A     R0,COUNT             THE
         ST    R0,COUNT              RECORD NUMBER
         ST    R0,DIRNUM(,R7)      STORE THE RECORD NUMBER
         ST    R1,DIRLEN(,R7)      STORE LENGTH
         LTR   R1,R1               END OF FILE
         BM    DOWNX               YES BRANCH
         C     R1,SAVLRECL         IS RECORD LONGER THAN MAX
         BNH   *+8                 NO, SKIP NEXT INSTR
         L     R1,SAVLRECL         YES, TRUNCATE TO MAX
         LR    R0,R2               ADDRESS TO MOVE FROM
         L     R14,DIRREC(,R7)     ADDRESS TO MOVE TO
         L     R15,SAVLRECL        LENGTH OF RECEIVING FIELD
         O     R1,=A(X'40000000')  PAD WITH BLANKS
         MVCL  R14,R0
DOWNNXT  BCT   R8,DOWNLOOP
DOWNX    ST    R7,HOLDEND          NEW END POINTER
         SLR   R15,R15
         L     R14,DOWNR
         BR    R14
         SPACE
************************************************************
*                                                          *
*         UP                                               *
*                                                          *
************************************************************
         SPACE
UP       EQU   *
         L     R0,SCROLL
*
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    UPRANGE             NO, USE RANGE
         TM    6(R15),QUOTED       IS OPERAND QUOTED
         BO    ERRINV              YES, INVALID
         LH    R1,4(,R15)          GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    ERRINV              YES, ERROR
         CH    R1,=H'7'            IS LENGTH MORE THAN 7
         BH    ERRINV              YES, ERROR
         L     R14,0(,R15)         GET ADDRESS OF DATA
         BCTR  R1,0
         B     *+10
         TRT   0(0,R14),NUMERIC
         EX    R1,*-6              IS IT NUMERIC
         BNZ   ERRINV              NO, ERROR
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LTR   R0,R1
         BZ    JUSTSHOW
UPRANGE  ST    R0,DOWNAMT
         L     R7,HOLDTOP
*
*  "HOLDTOP" SOMEHOW GETTING SET TO ZEROS, DON'T KNOW WHERE
*
         LTR   R7,7                IS IT ZEROS
         BNZ   UPTOPOK             NO, IT'S OK
         L     R7,HOLDEND          NO, IT'S OK
         LA    R7,16(,7)           BUMP TO NEXT ENTRY
         ST    R7,HOLDTOP          RESET IT
*
UPTOPOK  DS    0H
         CLC   DIRNUM(4,R7),=F'1'  ARE WE AT TOP ALREADY
         BNH   JUSTSHOW            YES, BRANCH
         TM    DIRLEN(R7),X'80'    IS FIRST LINE EOF
         BZ    UPTOP               NO, BRANCH
         NC    COUNT,COUNT         IS DATA SET EMPTY
         BZ    JUSTSHOW            YES, BRANCH
UPTOP    EQU   *
         L     R0,DIRNUM(,R7)      GET RECORD NUMBER OF TOP LINE
         S     R0,DOWNAMT          GET RECORD NUMBER TO GO BACK TO
         BP    *+8                 IF NEGATIVE
         LA    R0,1                   THEN MAKE IT 1
         B     LISTAT
*
************************************************************
*                                                          *
*         TOP                                              *
*                                                          *
************************************************************
         SPACE
TOP      EQU   *
         L     R1,CHKPTTOP
TOPJES   DS    0H
         MVC   JESMTTR(4),TOPMTTR  TTR OF FIRST BLOCK
         LA    R2,HEXFOX           INDICATE END OF BLOCK
         SR    R1,1
         SR    R0,0
         STM   R0,R2,DEBLOCKS
TOPGOT   DS    0H
         XC    COUNT,COUNT
         L     R14,SPANPTR
         SLR   R0,R0
         STH   R0,0(,R14)          RESET SPAN BUFFER LENGTH
         ST    R0,FNDNUM           RESET LAST-FOUND NUMBER
         STH   R0,FNDOFF           RESET LAST-FOUND OFFSET
         BAL   R14,FILLHOLD
         B     FILLSCR
HEXFOX   DC    X'FF'
         SPACE
************************************************************
*                                                          *
*         BOTTOM                                           *
*                                                          *
************************************************************
         SPACE
BOTTOM   DS    0H
         L     R0,BOTNINES
         B     LISTAT
BOTRET   L     R0,SCROLL
         B     UPRANGE
         SPACE
*        MVC   DOWNAMT,=F'99999999'
*        BAL   R14,DOWNER
*        B     FILLSCR
*
LISTAT   ST    R0,LISTNUM
         AH    R0,SCROWS1          GET RECORD NUMBER TO READ UP TO
         BCTR  R0,0
         ST    R0,AIMFOR           SAVE FOR DOWNTO
*
         LA    R15,20              LENGTH OF CHKPT ENTRY
         L     R1,CHKPTBOT
         TM    0(R1),X'80'         IS IT EOF
         BZ    LISTNB
         C     R1,CHKPTTOP         IS DATA SET EMPTY
         BE    SHOWTABL            YES
         SR    R1,R15              NO, BACK UP ONE ENTRY
LISTNB   L     R0,LISTNUM
LISTCK   C     R0,0(,R1)           DOES THIS BLOCK PRECEDE OUR RECORD
         BH    LISTPNT             YES, GO POINT TO IT
         SR    R1,R15              NO, BACK UP ONE BLOCK ENTRY
         B     LISTCK
LISTPNT  MVC   COUNT,0(R1)
         MVC   DEBLOCKS(12),8(R1)
         MVC   TTR,4(R1)           TTR OF FIRST BLOCK
         MVC   TTRZ(3),TTR
*
         CLC   4(4,R1),FZEROS     IS THIS A VALID BLOCK MTTR?
         BNE   *+14               ASSUME SO
         MVC   JESMTTR(4),TOPMTTR ELSE RESET TO TOP OF DSID
         B     *+10               AND SKIP CHECKPOINT MTTR
         MVC   JESMTTR(4),4(R1)   TTR OF FIRST BLOCK
*
         LA    R2,HEXFOX           INDICATE END OF BLOCK
         SR    R1,1
         SR    R0,0
         STM   R0,R2,DEBLOCKS
LISTNJES DS    0H
         L     R14,SPANPTR
         SLR   R0,R0
         STH   R0,0(,R14)          RESET SPAN BUFFER LENGTH
         ST    R0,FNDNUM           RESET LAST-FOUND NUMBER
         STH   R0,FNDOFF           RESET LAST-FOUND OFFSET
*
         BAL   R14,FILLHOLD        READ NEXT 20 RECORDS
*
LISTFINE L     R7,HOLDTOP          GET ENTRY FOR TOP OF HOLD AREA
         TM    DIRLEN(R7),X'80'    IS EOF ON TOP OF SCREEN
         BO    LISTRDY             YES, WE ARE FINISHED
         CLC   LISTNUM,DIRNUM(R7)  IS REQUESTED NUMBER AT TOP
         BNH   FILLSCR             YES, BRANCH
         MVC   DOWNAMT,=F'1'       DOWN 1
         BAL   R14,DOWNER
         B     LISTFINE
LISTRDY  CLC   LISTNUM,BOTNINES    WAS THIS A LIST 99999999
         BE    BOTRET              YES, BRANCH
         B     FILLSCR
ERRMISS  DS    0H          MSG35
         B     SHOWTABL
         SPACE
************************************************************
*                                                          *
*         FIND                                             *
*                                                          *
************************************************************
         SPACE
FIND     EQU   *
         MVI   FINDSW,0
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    FINDSAME            NO, USE PREVIOUS STRING
         XC    STRING,STRING       ERASE OLD STRING
         SLR   R0,R0
         ST    R0,FNDNUM           RESET LAST-FOUND NUMBER
         STH   R0,FNDOFF           RESET LAST-FOUND OFFSET
         STH   R0,FINDCOL          RESET COLUMN
         LH    R1,4(,R15)          GET LENGTH
         LTR   R1,R1               IS IT NULL STRING
         BZ    FINDNULL            YES, ERROR
         L     R14,0(,R15)         GET ADDRESS OF DATA
         BCTR  R1,0
         STH   R1,STRINGL          SAVE LENGTH CODE
         B     *+10
         MVC   STRING(0),0(R14)
         EX    R1,*-6
FINDSAME EQU   *
         NC    STRING,STRING       HAS A STRING BEEN ENTERED
         BZ    FINDNULL            NO, ERROR
         LH    R6,FINDCOL
         L     R7,HOLDTOP          GET INFO FOR FIRST HELD RECORD
         L     R0,DIRNUM(,R7)      GET RECORD NUMBER OF FIRST HELD
         L     R1,FNDNUM           GET RECORD NUMBER WHERE LAST FOUND
         LTR   R1,R1               HAS IT BEEN FOUND
         BNP   FINDSCR             NO, START WITH HELD RECORDS
         CR    R0,R1               ARE WE PAST LAST FOUND REC
         BH    FINDSCR             YES, START WITH HELD RECORDS
         L     R14,HOLDEND
         L     R15,DIRNUM(,R14)    GET RECORD NUMBER OF LAST HELD
         CR    R1,R15              IS LAST FOUND REC ON SCREEN
         BH    FINDDOWN            NO, BRANCH
FINDPREV C     R1,DIRNUM(,R7)      IS THIS RECORD WHERE LAST FOUND
         BE    FINDPCOL            YES, BRANCH
         C     R7,HOLDEND          IS THIS LAST RECORD ON SCREEN
         BE    FINDDOWN            SHOULD NOT HAPPEN
         L     R7,DIRNXT(,R7)      POINT TO NEXT RECORD
         B     FINDPREV
FINDPCOL L     R15,DIRREC(,R7)     POINT TO RECORD
         LTR   R6,R6               WAS A COLUMN SPECIFIED
         BZ    FINDPOFF            NO, LOOK AT SAME RECORD
         C     R7,HOLDEND          IS THIS LAST RECORD ON SCREEN
         BE    FINDDOWN            YES, BRANCH
         L     R7,DIRNXT(,R7)      NO, POINT TO NEXT RECORD
         B     FINDSCR             GO EXAMINE RECORD
FINDPOFF AH    R15,FNDOFF          POINT TO LAST FOUND STRING
         LA    R15,1(,R15)         POINT PAST LAST FOUND STRING
         B     FINDSTR
FINDCLC  CLC   0(0,R15),STRING     (EXECUTED)
FINDSCR  TM    DIRLEN(R7),X'80'    ARE WE AT EOF
         BO    FINDBOT             YES, BRANCH
         L     R15,DIRREC(,R7)     POINT TO FIRST BYTE TO EXAMINE
         LTR   R6,R6               WAS A COLUMN SPECIFIED
         BZ    FINDSTR             NO, BRANCH
         AR    R15,R6              YES, POINT TO COLUMN PLUS 1
         BCTR  R15,0               POINT TO COLUMN
         LH    R14,STRINGL
         EX    R14,FINDCLC
         BE    FOUND
         B     FINDNEXT
FINDSTR  L     R0,DIRLEN(,R7)      GET LENGTH OF RECORD
         C     R0,SAVLRECL         IS RECORD TRUNCATED
         BNH   *+8                 NO, SKIP NEXT INSTR
         L     R0,SAVLRECL         YES, USE TRUNCATED LENGTH
         L     R1,DIRREC(,R7)      GET ADDRESS OF RECORD
         AR    R0,R1               POINT PAST LAST BYTE OF RECORD
         LH    R14,STRINGL         GET LENGTH CODE OF STRING
         AR    R15,R14             POINT TO LAST BYTE TO BE COMPARED
         SR    R0,R15              GET NUMBER OF COMPARISONS
         BNP   FINDNEXT            STRING TOO LONG FOR REMAINING TEXT
         SR    R15,R14             PUT STRING ADDRESS BACK
FINDCOMP EX    R14,FINDCLC         COMPARE STRING TO DATA
         BE    FOUND
         LA    R15,1(,R15)         INCREMENT DATA POINTER
         BCT   R0,FINDCOMP         GO COMPARE AGAIN
FINDNEXT C     R7,HOLDEND          WAS THAT LAST HELD RECORD
         BE    FINDDOWN            YES, BRANCH
         L     R7,DIRNXT(,R7)      POINT TO NEXT RECORD
         B     FINDSCR             GO PROCESS NEXT RECORD
FINDBOT  EQU   *
*                          MSG32
         B     FILLSCR
FINDDOWN EQU   *
         LA    R1,1
         ST    R1,DOWNAMT
         BAL   R14,DOWNER
         L     R7,HOLDEND
         OI    FINDSW,1
         B     FINDSCR
FINDNULL DS    0H          MSG33
         B     SHOWTABL
ERRINV   DS    0H          MSG34
         B     SHOWTABL
FOUND    DS    0H          MSG31
         BCTR  R15,0
         MVI   0(R15),X'03'        HIGHLIGHT
         LA    R15,1(,R15)
         MVC   FNDNUM,DIRNUM(R7)   SAVE RECORD NUMBER
         S     R15,DIRREC(,R7)     GET OFFSET TO FOUND LOCATION
         STH   R15,FNDOFF          SAVE OFFSET
         LA    R15,1(,R15)         MAKE IT COLUMN NUMBER
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         MVC   JESNAME+68(6),=C'AT COL'
         UNPK  JESNAME+74(3),DOUBLE+6(2)
         MVI   JESNAME+74,C' '
         L     R15,FNDNUM
         CVD   R15,DOUBLE
         OI    DOUBLE+7,X'0F'
         LA    R15,MSG+16
         UNPK  0(5,R15),DOUBLE+5(3)
         TM    FINDSW,1
         BZ    FILLSCR
         LH    R6,SCROWS1
         SH    R6,=H'2'
FINDLN2  LA    R1,1
         ST    R1,DOWNAMT
         BAL   R14,DOWNER
         BCT   R6,FINDLN2
         B     FILLSCR
************************************************************
*                                                          *
*         HEX                                              *
*                                                          *
************************************************************
         SPACE
HEXMODE  EQU   *
         LA    R15,OPD1            GET FIRST OPERAND ENTRY
         L     R1,OPD1             ARE THERE ANY OPERANDS
         LTR   R1,1
         BZ    HEXFLIP             NO, FLIP FLOP
         L     R14,0(,R15)         POINT TO OPERAND
         CLI   5(R15),2            IS LENGTH 2
         BNE   HEXOFF              NO, TRY OFF
         CLC   0(2,R14),=C'ON'     YES, IS IT 'ON'
         BNE   ERRKW               NO, INVALID OPERAND
         OI    MODE,MODEX
         B     FILLSCR
HEXOFF   CLI   5(R15),3            IS LENGTH 3
         BNE   ERRKW
         CLC   0(3,R14),=C'OFF'
         BNE   ERRKW
         NI    MODE,255-MODEX
         B     FILLSCR
HEXFLIP  XI    MODE,MODEX          FLIP FLOP THE SWITCH
         B     FILLSCR
ERRKW    DS    0H          MSG34
         B     SHOWTABL
*
         SPACE
************************************************************
*                                                          *
*         CAPS / ASIS                                      *
*                                                          *
************************************************************
         SPACE
CAPS     MVC   PERIODS,CAPST
         B     FILLSCR
         SPACE
ASIS     MVC   PERIODS,ASIST
         B     FILLSCR
************************************************************
*                                                          *
*         SNAP                                             *
*                                                          *
************************************************************
         SPACE
SNAP     EQU   *
         DC    H'0'
         SPACE
************************************************************
*                                                          *
*         SUBROUTINE TO CONVERT DATA TO HEX                *
*                                                          *
************************************************************
         SPACE
HEX      MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         BR    R14                 RETURN TO CALLER
         SPACE
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATE TABLE
         EJECT
************************************************************
*                                                          *
*         SUBROUTINE TO GET A LOGICAL RECORD               *
*                                                          *
************************************************************
         SPACE
*
*               INPUT
*                R4 DCB ADDRESS
*                CHKPTTOP  -  TOP OF CHECKPOINT TABLE
*                CHKPTBOT  -  BOTTOM OF CHECKPOINT TABLE, ZERO 1ST TIME
*                COUNT     -  LAST LOGICAL RECORD NUMBER READ
*                BLOCKPTR  -  ADDRESS OF BUFFER
*                DEBLOCKS  -  DEBLOCKING INFO (ZEROS FIRST TIME)
*                SPANPTR   -  ADDRESS OF AREA TO COMBINE
*                             SPANNED RECORD SEGMENTS
*
*               OUTPUT
*                 R1 CONTAINS LENGTH OF RECORD (OR -1 IF END OF FILE)
*                 R2 CONTAINS ADDRESS OF RECORD (OR 0 IF END OF FILE)
*
GET      ST    R14,READR
         STM   R6,R7,JESRSAV1 I AM GOING TO CLOBBER THESE REGS
         LM    R0,R2,DEBLOCKS      GET DEBLOCKING STATUS
READJB   DS    0H
         LA    R2,0(R1,R2)    BUMP PAST FLAGS
         CLI   0(R2),X'FF'    IS LENGTH BYTE FF?
         BE    JES2RDI        YES. END OF BLOCK.
         TM    1(R2),X'10'    IS THIS A SPANNED RECORD?
         BO    SPANSKIP       YES. SKIP IT.
         SR    R6,R6          ZERO OUT REG
         IC    R6,0(R2)       INSERT LENGTH
         LR    R7,R6          SAVE RECORD LENGTH
         LR    R1,R2          SAVE RECORD LOCATION
         TM    1(R2),X'80'    IS THERE CARRIAGE CONTROL?
         BZ    *+8            NO.
         LA    R1,1(,1)       BUMP 1
         TM    1(R2),X'08'    IS THIS RECORD TO BE IGNORED?
         LR    R2,R1          UPDATE RECORD POINTER
         BNZ   SKIP1REC       YES. SKIP IT.
         LTR   R7,R7          LENGTH ZERO
         BZ    SKIP1REC       YES. SKIP IT.
         LR    R1,R7          RECORD LENGTH
READ1RTN DS    0H
         LA    R2,3(,R2)      BUMP PAST FLAGS
         STM   R1,R2,DEBLOCKS+4    SAVE STATUS INFO
         B     READX
SKIP1REC DS    0H
         LA    R2,3(R6,R2)    INCREMENT TO NEXT RECORD
         SR    R1,1           ZERO LENGTH CODE
         B     READJB         PROCESS NEXT RECORD
SPANSKIP LH    R6,2(R2)       LENGTH OF SEGMENT
         TM    1(R2),X'08'    IS THIS THE FIRST SEGMENT?
         BO    SPAN1STR       YES. USE HEADER LENGTH OF 6.
         LA    R2,4(R6,R2)    UPDATE RECORD POSITION
         SR    R1,1           ZERO LENGTH CODE
         B     READJB         PROCESS NEXT RECORD
SPAN1STR LA    R2,6(R6,R2)    UPDATE RECORD POSITION
         SR    R1,1           ZERO LENGTH CODE
         B     READJB         PROCESS NEXT RECORD
*
*
*
*
JES2RDI  DS    0H
         L     R5,CHKPTBOT         GET LAST CHECKPOINT
         LTR   R5,R5               IS THIS FIRST READ
         BNZ   JES2NF              BRANCH IF NOT FIRST
         L     R5,CHKPTTOP
         B     JES2NEW             READING A RECORD NOT READ BEFORE
JES2NF   L     R1,0(,R5)           GET NUMBER OF HIGHEST RECORD READ
         LTR   R1,R1               WAS IT EOF
         BM    JES2OLD             YES, BRANCH
         C     R1,COUNT            HAVE WE READ THIS RECORD BEFORE
         BNH   JES2ADD             NO, BRANCH
JES2OLD  LA    R5,CHKPTDUM         YES, DONT CHANGE CHKPT TABLE
         B     JES2RNEW
JES2ADD  LA    R5,20(,R5)          ADD AN ENTRY TO THE CHKPT TABLE
         C     R5,ENDPTR           IS TABLE FILLED UP
         BL    *+8                 NO, SKIP NEXT INSTR
         BAL   R14,READHALF        YES, HALVE THE TABLE
JES2NEW  DS    0H
         ST    R5,CHKPTBOT         SAVE NEW CURRENT CHECKPOINT POINTER
         MVC   0(4,R5),COUNT
JES2RNEW DS    0H
         L     R2,BLOCKPTR
         CLC   JESMTTR(4),FZEROS   EOF
         BE    JES2EOD
         LR    R0,R2
         S     R0,F88
         L     R1,JESMTTR
         LA    R15,3
         SVC   242
         MVC   4(4,R5),JESMTTR     SET MTTR OF THIS BLOCK
         MVC   JESMTTR(4),0(R2)    SET MTTR OF NEXT BLOCK
         CLC   IXTJOBID(4),FZEROS  FIRST READ
         BNE   *+10
         MVC   IXTJOBID(4),4(R2)  SET THE JOBID
         CLC   IXTJOBID(4),4(R2) DOES THE JOBID MATCH?
         BNE   JES2EOD        NO. END OF DATASET.
         SR    R1,1
         LA    R2,10(,2)           BUMP TO FIRST RECORD
         LA    R0,4086(,R2)        END OF BLOCK
         STM   R0,R2,DEBLOCKS
         MVC   8(12,R5),DEBLOCKS
         B     READJB              CONTINUE
*
F88      DC    F'88'
JES2EOD  DS    0H
         L     R1,=F'-1'           EOF
         ST    R1,0(,R5)           SAVE EOF IN CHKPT TABLE
         SLR   R2,R2
READX    DS    0H
         LM    R6,R7,JESRSAV1 RESTORE THESE REGS
         L     R14,READR
         BR    R14
FZEROS   DC    F'0'
         EJECT
DYNEOD   EQU   *
         L     R1,=F'-1'           EOF
         ST    R1,0(,R5)           SAVE EOF IN CHKPT TABLE
         SLR   R2,R2
         B     READX
         SPACE
READHALF LA    R0,20               LENGTH OF EACH ENTRY
         L     R15,CHKPTTOP        POINT TO FIRST ENTRY
         AR    R15,R0              POINT TO SECOND ENTRY
         LR    R1,R15
         AR    R1,R0               POINT TO THIRD ENTRY
READHMOV MVC   0(20,R15),0(R1)     MOVE 3RD TO 2ND
*                                       5TH TO 3RD
*                                       7TH TO 4TH, ETC
         AR    R15,R0              RECEIVING FIELD DOWN 1
         AR    R1,R0               SENDING FIELD DOWN 1
         AR    R1,R0               SENDING FIELD DOWN 2
         CR    R1,R5               ARE WE PAST THE LAST ENTRY
         BL    READHMOV            NO, BRANCH
         LR    R5,R15              YES, NEW CURRENT POINTER
         BR    R14                 RETURN
         SPACE
************************************************************
*                                                          *
*         END OF PROGRAM                                   *
*                                                          *
************************************************************
         SPACE
END      DS    0H
*
SHOWDONE DS    0H
*
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               BRLINEP),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               BRCMD),                                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               BRTABLE1),                                              X
               VL,MF=(E,ISPARMS)
         MVI   ACTAB,C'0'         NO ACTIVE TABLE
         MVC   BCMD(48),SPACES
         MVC   CMDSAVE(48),SPACES
*
         LA    R15,0
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14                 RETURN TO PHASE 1
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
ISPLINK  DC    V(ISPLINK)
*
L80      DC    F'80'
L2       DC    F'2'
L48      DC    F'48'
L44      DC    F'44'
*
SELECT   DC    C'SELECT   '
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
REPLACE  DC    C'REPLACE  '
*
*
NULLENT  DC    C'()'
*
CHAR     DC    C'CHAR    '
*
BRTABLE1 DC    C'BRTABLE1'
JDFBRTAB DC    C'JDFBRTAB'
*
LINEAREA DS    CL80
JESNAME  DC    X'03'
BRDSNAME DC    CL48' '
         DC    CL31' '
BCMD     DC    CL48' '
SPACES   DC    CL80' '
*
BRLINEP  DC    C'(BRLINE  )'    JOB INFO
BRCMD    DC    C'(BRCMD   )'    JOB INFO
*
BRNAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
         DC    C'BRCMD    '     COMMAND
         DC    C'BRLINE  )'     JES2 DSID DATA LINE
*
         SPACE
BOTNINES DC    F'99999999'
GMVU     GETMAIN VU,MF=L
GMVUL    EQU   *-GMVU
         SPACE
ASIST    DS    0F
         DC    3X'4B',X'03'
         DC    60X'4B',X'40',9X'4B'
         DC    X'4A4B4C4D4E4F'       CENT,PERIOD,LESS,LPAREN,PLUS,BAR
         DC    X'50',9X'4B'          AMPERSAND
         DC    X'5A5B5C5D5E5F'       EXCL,$,DOT,RPAREN,SEMI,NOT
         DC    X'6061',8X'4B'        HYPHEN,SLASH
         DC    X'6A6B6C6D6E6F'       WHAT,COMMA,PERCENT,UNDLN,GT,QM
         DC    9X'4B',X'79'          70-78
         DC    X'7A7B7C7D7E7F'       COLON,POUND,AT,APOST,EQ,DBLQUOTE
         DC    X'4B'
         DC    X'818283848586878889',7X'4B'
         DC    X'919293949596979899',8X'4B'
         DC    X'A2A3A4A5A6A7A8A9',23X'4B'
         DC    C'ABCDEFGHI',7X'4B'
         DC    C'JKLMNOPQR',8X'4B'
         DC    C'STUVWXYZ',06X'4B'
         DC    C'0123456789',6X'4B'
CAPST    DS    0F
         DC    3X'4B',X'03'
         DC    60X'4B',X'40',9X'4B'
         DC    X'4A4B4C4D4E4F'       CENT,PERIOD,LESS,LPAREN,PLUS,BAR
         DC    X'50',9X'4B'          AMPERSAND
         DC    X'5A5B5C5D5E5F'       EXCL,$,DOT,RPAREN,SEMI,NOT
         DC    X'6061',8X'4B'        HYPHEN,SLASH
         DC    X'6A6B6C6D6E6F'       WHAT,COMMA,PERCENT,UNDLN,GT,QM
         DC    9X'4B',X'79'          70-78
         DC    X'7A7B7C7D7E7F'       COLON,POUND,AT,APOST,EQ,DBLQUOTE
         DC    X'4B'
         DC    C'ABCDEFGHI',7X'4B'
         DC    C'JKLMNOPQR',8X'4B'
         DC    C'STUVWXYZ',23X'4B'
         DC    C'ABCDEFGHI',7X'4B'
         DC    C'JKLMNOPQR',8X'4B'
         DC    C'STUVWXYZ',06X'4B'
         DC    C'0123456789',6X'4B'
SCREEN   DC    CL80' '
SCRDSN   DC    55C'-'
         DC    C' LINE '
SCRLINE  DC    C'00000'
         DC    C' COL '
SCRCOL   DC    C'001 080 '
SCREENL  EQU   *-SCREEN
SCRDATA  EQU   *
*
*               TRANSLATE TABLES
*
FINDSBA  DC    17X'00',X'11',238X'00'
NUMERIC  DC    240X'FF',10X'00',6X'FF'
HEXDATA  DC    193X'FF',6X'00',41X'FF',10X'00',6X'FF'
         SPACE
TABNONBL DC    64X'FF'
         DC    X'00'               BLANK
         DC    42X'FF'
         DC    X'FF'               COMMA
         DC    148X'FF'
TABBLANK DC    64X'00'
         DC    X'40'               BLANK
         DC    42X'00'
         DC    X'00'               COMMA
         DC    148X'00'
TABQUOTE DC    125X'00',X'7D',130X'00'
         SPACE
KAPS     DC    129AL1(*-KAPS)      00-80
         DC    9AL1(*-KAPS+X'40')  81-89 BECOME C1-C9
         DC    7AL1(*-KAPS)        8A-90
         DC    9AL1(*-KAPS+X'40')  91-99 BECOME D1-D9
         DC    8AL1(*-KAPS)        9A-A1
         DC    8AL1(*-KAPS+X'40')  A2-A9 BECOME E2-E9
         DC    86AL1(*-KAPS)       AA-FF
         DC    0D'0'               END OF CSECT
         TITLE 'DD LINE DSECT'
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
DDLINE   DSECT
DDLDSID  DS    CL3
         DS    CL1
         DS    CL1
STEPNAME DS    CL8
         DS    CL1
DDN      DS    CL8
         DS    CL1
DDLINES  DS    CL8
         DS    CL2
DDCLASS  DS    CL1
         DS    CL2
DDFORM   DS    CL4
         DS    CL1
DDFCB    DS    CL4
         DS    CL1
DDUCS    DS    CL4
         DS    CL1
DDCOPIES DS    CL3
DDBURST  DS    CL1
DDDRECL  DS    CL6
         DS    CL1
DDDRECF  DS    CL3
         DS    CL1
DDDDMTTR DS    CL8
DDLINSHO EQU   *-DDLINE
DDMTTR   DS    CL4
DDLRECL  DS    XL2                    PDDB LRECL
DDRECFM  DS    X                      PDDB RECFM
DDLINESZ EQU   *-DDLINE
*
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
ISPARMS  DS    8F                  PARMS FOR ISPLINK
SIZE     DS    F                   SIZE OF THIS AREA
CPPLPTR  DS    F
LINKAREA DS    2F
BASE1    DS    4F
BASE2    DS    3F
RET1     DS    F
DWORK    DS    D
SCROWS1  DS    H                   LINES PER SCREEN
*
JESPARM  DS    4F
JAMWORK  DS    XL8
JESMTTR  DS    F
TOPMTTR  DS    F
JESMTTR2 DS    F
JOBNUM   DS    H
JOBDSID  DS    H
JESLRECL DS    H
WRKRECFM DS    X
*
COLUMNS  DS    H                   COLS  PER SCREEN
MYPPL    DS    7F
MYANS    DS    F
MYECB    DS    F                   USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                  USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                  USED BY PUTLINE ROUTINE
MYOLD    DS    2F                  USED BY PUTLINE ROUTINE
PUTLINS  DS    4F                  USED BY PUTLINE ROUTINE
MYPUTLEP DS    F                   ADDRESS OF IKJPUTL
MYSTPB   DS    0F                  5 WORDS USED BY STACK DELETE
MYDAPL   DS    5F
MYDAPB   DS    21F
MYDFPB   DS    5F
*
$MEMBER  DS    CL8
$PASSWRD DS    CL8
$DDNAML  DS    H                   THESE
$DDNAME  DS    CL8                  THREE
$CONCAT  DS    CL36                  TOGETHER
$VOLSER  DS    CL6
$UCBAD   DS    F
$UNIT    DS    CL8
LOCATEW  DS    0F
OBTAINW  DS    4F
LOCBUF   DS    0D                  USES NEXT 265 BYTES
MSG      DS    CL128
STATUS   DS    X
STALLOC  EQU   X'80'
STOPEN   EQU   X'40'
STABEND  EQU   X'20'
STLOCAT  EQU   X'10'
STGMVU   EQU   X'08'
STRECV   EQU   X'04'
STEOF    EQU   X'02'
STNOMEM  EQU   X'01'
MODE     DS    X
MODEX    EQU   X'80'
CCFLAG   DS    X
ITISJES  EQU   X'00'
JESRSAV1 DS    2F
IXTJOBID DS    XL4
SMFSW    DS    X
SMFBIT   EQU   X'80'
DSORG    DS    X
TSTRECFM DS    X
FINDSW   DS    X
RC       DS    H
FILEKV   DS    H
QUICKV   DS    H
MYDFPARM DS    5F  USED BY DAIRFAIL
MYDFREGS DS    F   USED BY DAIRFAIL
MYDFRC   DS    F   USED BY DAIRFAIL
MYJEFF02 DS    F   USED BY DAIRFAIL
MYDFID   DS    H   USED BY DAIRFAIL
DOUBLE   DS    D
COLNUM   DS    CL6
         DS    CL2
DAIRREGS DS    F
OPEND    DS    0F
CLOSED   DS    F
DYNEXLST DS    2F
KOUNT    DS    F
RANGE    DS    2F
ANSWER   DS    2F
GMVUW    DS    0F,XL(GMVUL)
SAVSPANL DS    F
SAVLRECL DS    F
SAVBLKSI DS    F
SAVHOLDL DS    F
BLOCKPTR DS    F
SPANPTR  DS    F
HOLDPTR  DS    F
CHKPTTOP DS    F
CHKPTBOT DS    F
CHKPTDUM DS    5F
ENDPTR   DS    F
HOLDTOP  DS    F
HOLDEND  DS    F
HOLDDIR  DS    XL704
DIRNUM   EQU   0
DIRLEN   EQU   4
DIRREC   EQU   8
DIRNXT   EQU   12
DIRSIZ   EQU   16
OFFSET   DS    H
RECSIZE  DS    H
COUNT    DS    F
TTR      DS    F
TTRZ     DS    F                   TTR + 0 FOR POINT
TTRK     DS    F                   TTR + CONCAT FOR FIND
SCROLL   DS    F
DOWNAMT  DS    F
LISTNUM  DS    F
AIMFOR   DS    F
FLDPTR   DS    F
FLDLEN   DS    F
CMDPTR   DS    F
CMDLEN   DS    F
CMDAREA  DS    CL63
MSGDSN   DS    CL55
SBASAVE  DS    4F
FINDCOL  DS    H
FNDNUM   DS    F
FNDOFF   DS    H
STRINGL  DS    H
STRING   DS    CL64
OPDL     DS    0F                  OPERAND DESCRIPTOR LIST
OPD1     DS    2F                  OPERAND DESCRIPTOR 1
PRESENT  EQU   X'80'
QUOTED   EQU   X'40'
HOLDR    DS    F
READR    DS    F
DOWNR    DS    F
DEBLOCKS DS    3F
SCREENF  DS    F
TGETREGS DS    3F
SYNADSW  DS    F
SYNADMSG DS    CL78
DEVDATA  DS    2F
JFCB     DS    0F,CL176
POOLSIZ  DS    F
POOLLEN  DS    2F
         DS    0D
PERIODS  DS    CL256
@DATAEND EQU   *
@DATAL   EQU   *-@DATA
         SPACE
IHADCB   DSECT
         DS    32XL1
DCBBFTEK DS    XL1
DCBEODAD DS    AL3
DCBRECFM DS    X
DCBEXLSA DS    AL3
DCBDDNAM DS    CL8
DCBOFLGS DS    X
         DS    7XL1
         DS    X
DCBSYNAD DS    AL3
DCBBLKSI EQU   IHADCB+62,2
DCBLRECL EQU   IHADCB+82,2
         SPACE
         IKJCPPL
         SPACE 3
         IKJPPL
         SPACE
         IKJDFPB
         SPACE 2
         IKJUPT
         SPACE 2
         IKJIOPL
         SPACE 2
         IKJDAPL
         SPACE 2
         IKJDAP08
         SPACE 2
         IKJDAP18
         SPACE 2
         IKJPSCB
         SPACE 2
QUOTE    EQU   X'7D'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
$MAXDA   EQU   32
         END
/*
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.SERV.LOAD
//         DD   DISP=SHR,DSN=TECH.FIXDHEAD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,
//         DSN=TECH.SERV.ASQCCLD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
    ALIAS JDFDA
    ALIAS JDFDE
    ALIAS JDFDH
    ALIAS JDFDL
    ALIAS JDFDO
    ALIAS JDFDQ
    ALIAS JDFDR
    ALIAS JDFDT
    ALIAS JDFST
  NAME JDF(R)
/*
