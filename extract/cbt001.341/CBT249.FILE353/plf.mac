//BCOOKJOB  JOB (X0002,QCC,BCOOK),'X-BRIAN COOK',
//   MSGCLASS=X,
//   MSGLEVEL=(1,1),CLASS=T,NOTIFY=BCOOK
/*JOBPARM L=99
//* *****************************************************************
//* *****************************************************************
//*                                                               ***
//* TECH.SERV.BCOOK (PLF2)                                        ***
//*                                                               ***
//* *****************************************************************
//* *****************************************************************
//ASM1    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DISP=SHR,DSN=SYS1.AMODGEN
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
***********************************************************************
*                                                                     *
*                                                                     *
*               PAN LIBRARY FACILITY (PLF)                            *
*                                                                     *
*                                                                     *
* ABSTRACT: EDIT, BROWSE AND UTILITY FUNCTIONS FOR PAN LIBRARIES.     *
*                                                                     *
*                                                                     *
* FUNCTION: PLF IS DESIGNED TO OPERATE UNDER ISPF USING DISPLAY       *
*           MANAGEMENT SERVICES. IT PROVIDES A DIRECTORY MENU OF      *
*           A PRE-ALLOCATED PAN LIBRARY (DDNAME=PANDD1) AND ALLOWS    *
*           LINE SELECTION COMMANDS EDIT, BROWSE, RENAME, DELETE,     *
*           ETC. IT DYNAMICALLY INVOKED THE SPF EDIT AND BROWSE       *
*           FUNCTIONS, AND REQUIRES A PRE-ALLOCATED SYSIN FILE        *
*           WITH THE NAME &USERID.PAN.DATA, AND A PRE-ALLOCATED FILE  *
*           WITH THE NAME &USERID.PAN.LIST. SINCE IT                  *
*           RUNS UNDER ISPF, IT CAN BE INVOKED WITH SPLIT-SCREEN.     *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCES: ISPLINK, PAM                                   *
*                                                                     *
*                                                                     *
* MACROS USED: SAVE                                                   *
*              RETURN                                                 *
*              GETMAIN                                                *
*              STAX                                                   *
*              ENQ                                                    *
*              DEQ                                                    *
*              TPUT                                                   *
*              FREEMAIN                                               *
*              $JQE                                                   *
*              $JOE                                                   *
*              CVT                                                    *
*              CVTUSERS (USER)                                        *
*              TCBWORK  (USER)                                        *
*                                                                     *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         TITLE 'PLF - PROGRAM INITIALIZATION'
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
PLF      CSECT
         PRINT NOGEN
         USING *,13,12,11,10,9
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R13,4(R2)      SAVE THE CALLER'S R13
         ST    R2,8(R13)      LINK SAVE AREAS
         LA    R13,0(,R2)     SET SAVE AREA AND BASE 1
         B     SETBASES
         DC    12F'0'
*
SETBASES DS    0H
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R10,R11        SET
         A     R10,L4096          BASE 4
         LR    R9,R10         SET
         A     R9,L4096           BASE 5
*
         XC    0(4,R13),0(13) CLEAR WORD 1 OF SAVE AREA
*
         MVI   PLFTABFL,C'0'  NO ACTIVE TABLE
         MVI   PLFPOPEN,C'0'  PAN NOT OPEN
*
         BAL   R7,INITFLDS    INITIALIZATION
*
         TITLE 'PLF - MAIN PROCESSING LOOP'
SHOWAGIN DS    0H
*
         MVI   DONEFLAG,C'0'  NOT ALL DONE
         BAL   R7,SHOWMENU    PROMPT FOR INPUT
         CLI   DONEFLAG,C'1'  AM I ALL DONE
         BE    PLFEOJ         YUP
*
         BAL   R7,PLFDYNAM    ALLOCATE THE FILES NEEDED
*
         CLI   LIBTYPE,C'I'   IS IT A PDS
         BE    SHOWPDS
*
         CLI   LIBTYPE,C'P'   IS IT A PAN LIBRARY
         BE    SHOWPAN
*
         CLI   LIBTYPE,C'S'   IS IT A SEQUENTIAL FILE
         BE    SHOWSEQ
*
         B     PLFEOJ         THAT'S ALL
*
SHOWPDS  DS    0H
         BAL   R7,PLFAPDS     PROCESS THE LIBRARY
         B     SHOWAGIN       RE-SHOW THE MENU
*
SHOWPAN  DS    0H
         BAL   R7,PLFAPAN     PROCESS THE LIBRARY
         B     SHOWAGIN       RE-SHOW THE MENU
*
SHOWSEQ  DS    0H
         BAL   R7,PLFASEQ     PROCESS SEQUENTIAL FILE
         B     SHOWAGIN       RE-SHOW THE MENU
*
PLFEOJ   DS    0H
*
         BAL   R7,PLFDONE
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
*
DONEFLAG DC    C'0'
*
L4096    DC    F'4096'
*
         TITLE 'TERMINATION LOGIC'
DONE7    DS    F
PLFDONE DS     0H
*
         ST    R7,DONE7
*
         CLI   PLFTABFL,C'0'      NO ACTIVE TABLE
         BE    PLFTOPEN        CHECK PAN OPEN STATUS
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANS),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NAMEPANP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NEWNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   PLFTABFL,C'0'      NO ACTIVE TABLE
*
PLFTOPEN DS 0H
         CLI   PLFPOPEN,C'0'  PAN NOT OPEN
         BE    EOJ
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   PLFPOPEN,C'0'  PAN NOT OPEN
*
EOJ      DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANSPFN),                                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               VCTL),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,PLFVARS,PROFILE),                                 X
               VL,MF=(E,ISPARMS)
*
         CLI   PANZ,C'Q'           CLEAR UP ENQ'S
         BE    *+12
         CLI   ENQFLAG,C'1'
         BNE   DEQ2
         DEQ   (PLFZUSER,PLFDD1S,,STEP),RET=HAVE
         CLI   PANZ,C'Q'           CLEAR UP ENQ'S
         BNE   DEQDONE
*
DEQ2     DS    0H
         CLI   PANZ,C'Q'           CLEAR UP ENQ'S
         BE    *+12
         CLI   ENQFLAG,C'2'
         BNE   DEQDONE
         DEQ   (PLFZUSER,PLFDD1S,,STEP),RET=HAVE
*
DEQDONE  DS    0H
*
         BAL   R14,REDYNAM
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),DDLIB
         BAL   R14,DYNAM
*
         MVC   DDNAME(8),DDMEM
         BAL   R14,DYNAM
*
         MVC   DDNAME(8),DDSIN
         BAL   R14,DYNAM
*
         MVC   DDNAME(8),DDSYSP
         BAL   R14,DYNAM
*
         MVC   DDNAME(8),DDPAN2
         BAL   R14,DYNAM
*
         MVC   DDNAME(8),DDPAN3
         BAL   R14,DYNAM
*
DONEDONE DS    0H
         L     R7,DONE7
         BR    R7             RETURN
*
         TITLE 'PLF - INITIALIZATION'
INIT7    DC    F'0'
INITFLDS DS    0H
         ST    R7,INIT7
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VCOPY,ZUSER,U8,USERID,SPFMOVE),                        X
               VL,MF=(E,ISPARMS)
*
         LTR   R15,15          IF RETURN CODE IS ZERO,
         BZ    GOTZUSER        OK
*
         WTO   'PLF001 - UNABLE TO RETRIEVE ZUSER',ROUTCDE=11
ABEND1   ABEND 1,DUMP,STEP
GOTZUSER DS    0H
*
         MVC   PLFZUSER(8),USERID
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANWORK,PANACTN,CHAR,L7),                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANSPFN,NAME,CHAR,L10),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               VCTL,CONTROL,CHAR,L4),                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               VCTL),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFZ,PANZ,CHAR,L1),                                     X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFPROJ,PANPROJ,CHAR,L8),                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFLIBR,PANLIBR,CHAR,L8),                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFTYPE,PANTYPE,CHAR,L8),                               X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFMEMB,PANMEMB,CHAR,L10),                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFNEWN,PANNEWN,CHAR,L10),                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFDSN2,PANDSN2,CHAR,L44),                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFMEM2,PANMEM2,CHAR,L10),                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFVOL,PANVOL,CHAR,L6),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFPASSW,PANPASSW,CHAR,L8),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFCONTR,PANCONTR,CHAR,L4),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFU,PANU,CHAR,L4),                                     X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFL,PANL,CHAR,L6),                                     X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PLFM,PANM,CHAR,L10),                                    X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VGET,PLFVARS,ASIS),                                    X
               VL,MF=(E,ISPARMS)
*
*
*  NOW I HAVE TO DETERMINE WHAT DDNAMES TO USE. SINCE I COULD BE IN
*  SPLIT-SCREEN, WITH PLF ALREADY BEING INVOKED ON THE OTHER SCREEN,
*  I HAVE TO USE A DIFFERENT SET OF DDNAMES. THE DDNAMES I NEED ARE:
*
*    SET 1       SET 2       USAGE
*
*    PLFDD10     PLFDD20     LIBRARY
*    PLFDD11     PLFDD21     FOR A PDS, LIBRARY MEMBER
*    PLFDD12     PLFDD22     "SYSIN" CARD-IMAGE FILE
*    PLFDD13     PLFDD23     "SYSPRINT" FILE
*    PLFDD14     PLFDD24     FOR PAN, PANDD2 FILE
*    PLFDD15     PLFDD25     FOR PAN, PANDD3 FILE
*
ENQLIB1  DS    0H
         MVC   PLFDDS(48),PLFDD1                       DEFAULT
         MVC   PANPARMS(4),PAN1PARM                    DEFAULT
         MVI   ENQFLAG,C'1'                            DEFAULT
         ENQ   (PLFZUSER,PLFDD1S,E,,STEP),RET=TEST
         LTR   R15,15                                  CAN I GET IT?
         BNZ   ENQLIB2                                 NOPE, TRY 2
*
         ENQ   (PLFZUSER,PLFDD1S,E,,STEP),RET=HAVE
         LTR   R15,15                                  GOT IT?
         BZ    INITDDNS                                YUP
         B     PLFRRENQ                                NOPE
*
ENQLIB2  DS    0H
         MVC   PLFDDS(48),PLFDD2                       DEFAULT
         MVC   PANPARMS(4),PAN2PARM                    DEFAULT
         MVI   ENQFLAG,C'2'                            DEFAULT
         ENQ   (PLFZUSER,PLFDD2S,E,,STEP),RET=TEST
         LTR   R15,15                                  CAN I GET IT?
         BNZ   PLFRRENQ                                NOPE
*
         ENQ   (PLFZUSER,PLFDD2S,E,,STEP),RET=HAVE
         LTR   R15,15                                  GOT IT?
         BNZ   PLFRRENQ                                YUP
*
INITDDNS DS    0H
         MVC   PANLIST+40(8),DDSYSP
         MVC   SYSIN+40(8),DDSIN
         MVC   LIBRDCB+40(4),DDLIB
         MVC   MEMBRDCB+40(8),DDMEM
*
INITDONE DS    0H
         L     R7,INIT7
         BR    R7
*
         TITLE 'PLF - DISPLAY THE MENU'
MENU7    DS    F
PLFPF3   DS    0H
         MVI   DONEFLAG,C'1'  I AM ALL DONE
MENUDONE DS    0H
         L     R7,MENU7
         BR    R7
SHOWMENU DS    0H
         ST    R7,MENU7
DISPLAY1 DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (DISPLAY,                                               X
               PLFMENU1),                                              X
               VL,MF=(E,ISPARMS)
*
         C     R15,F8             IF END KEY ENTERED
         BE    PLFPF3
*
         C     R15,FZEROS         IF ENTER KEY PRESSED
         BNE   PLFBADRC
*
TESTINPT DS    0H
         OI    PANZ,C' '                 UPPER-CASE IT
         CLI   PANZ,C'Z'                  END
         BE    PLFEOJ                     YUP
         CLI   PANZ,C'Q'                  CLEAN UP ENQ'S
         BE    PLFEOJ                     YUP
*
         TR    PANPROJ(8),CAPSONLY       UPPER-CASE IT
         TR    PANLIBR(8),CAPSONLY       UPPER-CASE IT
         TR    PANTYPE(8),CAPSONLY       UPPER-CASE IT
         TR    PANMEMB(10),CAPSONLY      UPPER-CASE IT
         TR    PANNEWN(10),CAPSONLY      UPPER-CASE IT
         TR    PANDSN2(44),CAPSONLY      UPPER-CASE IT
         TR    PANMEM2(10),CAPSONLY      UPPER-CASE IT
         TR    PANVOL(6),CAPSONLY        UPPER-CASE IT
         TR    PANPASSW(8),CAPSONLY      UPPER-CASE IT
         TR    PANL(8),CAPSONLY          UPPER-CASE IT
         TR    PANM(8),CAPSONLY          UPPER-CASE IT
         TR    PANU(8),CAPSONLY          UPPER-CASE IT
*
         CLI   PANCONTR,C' '             CONTROL WORD ENTERED?
         BE    PLFSETDS                  NOPE
         MVC   CONTROL(4),PANCONTR       YES, SET IT
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               VCTL),                                                  X
               VL,MF=(E,ISPARMS)
*
         TITLE 'PLF - SET DSNAME'
PLFSETDS DS    0H
*
*  PICK UP DSNAME
*
         MVI   DSNNOQ,C' '
         MVC   DSNNOQ+1(43),DSNNOQ
         MVI   DSNQUO,C' '
         MVC   DSNQUO+1(43),DSNQUO
*
         CLI   PANDSN2,C' '               WAS IT ENTERED THERE
         BNE   CHKDSNF                    YUP
         CLI   PANPROJ,C' '               WAS IT ENTERED IN SPF FMT
         BE    PLFRRDSN                   NOPE
*
         MVC   DSNNOQ(8),PANPROJ          MOVE IN HIGH-LEVEL OF INDEX
         LA    R1,DSNNOQ+1                SET ADDRESS FOR SCAN
*
         CLI   0(R1),C' '
         BE    *+12
         LA    R1,1(,1)
         B     *-12
         MVI   0(R1),C'.'
         LA    R1,1(,1)
*
         LA    R15,PANLIBR                SET ADDRESS FOR SCAN
*
         BAL   R14,MOVENAME               DO THE MOVE
*
         MVI   0(R1),C'.'
         LA    R1,1(,1)                   BUMP FOR NEXT LOCATION
         LA    R15,PANTYPE                SET ADDRESS FOR SCAN
*
         BAL   R14,MOVENAME               DO THE MOVE
*
DSNFQUOT DS    0H
         MVI   DSNQUO,X'7D'               STARTS WITH SINGLE QUOTE
         MVC   DSNQUO+1(26),DSNNOQ        MOVE MAX LENGTH
         LA    R1,26                      LOOP CONTROL
         LA    R14,DSNQUO+2               START OF LOOP
*
*
         CLI   0(R14),C' '                FOUND BLANK
         BE    *+12                       WIPE IT OUT
         LA    R14,1(,14)                 BUMP
         BCT   R1,*-12                    KEEP LOOPING
*
         MVI   0(R14),X'7D'               SET QUOTE
         B     CHKPSPL
*
CHKDSNF  DS    0H
         CLI   PANDSN2,X'7D'              STARTS WITH SINGLE QUOTE
         BNE   DSNFNQ                     NOPE
         MVC   DSNQUO(44),PANDSN2         SAVE DSN WITH QUOTES
         MVC   DSNNOQ(42),PANDSN2+1       STRIP OFF LEADING QUOTE
         LA    R1,42                      LOOP CONTROL
         LA    R14,DSNNOQ+1               START OF SCAN
*
         CLI   0(R14),X'7D'               FOUND QUOTE
         BE    *+16                       WIPE IT OUT
         LA    R14,1(,14)                 BUMP
         BCT   R1,*-12                    KEEP LOOPING
         B     PLFRRSN                    NFG
*
         MVI   0(R14),C' '                KILL QUOTE
         B     CHKPSPL
*
DSNFNQ   DS    0H
*
* OKAY, THE DSN WAS ENTERED ON LINE2, BUT NOT IN QUOTES. THIS MEANS
* THAT IT HAS TO BE PREFIXED BY THE USERID TO GET THE FULL DSN.
*
         MVC   DSNNOQ(8),USERID           MOVE IN USERID AS HIGH
*
         LA    R1,DSNNOQ+1                SET ADDRESS FOR SCAN
         CLI   0(R1),C' '                 CHECK FOR SPACE
         BE    *+12                       GOT IT
         LA    R1,1(,1)                   BUMP ADDRESS
         B     *-12                       KEEP LOOPING
         MVI   0(R1),C'.'                 SET A PERIOD
         LA    R1,1(,1)                   BUMP ADDRESS
*
         MVC   0(35,R1),PANDSN2           MOVE THE REST OF IT
         B     DSNFQUOT
*
CHKPSPL  DS    0H
         MVI   PSPOOL,C'0'                NOT PSPOOL
         CLC   DSNNOQ(7),=C'P.SPOOL'     IS IT P.SPOOL
         BNE   *+8                        NOPE
         MVI   PSPOOL,C'1'                SET TO PSPOOL
*
*        B     TSTTYPE
*
         TITLE 'PLF - SHOWMENU - ISSUE LOCATE FOR THIS DSN'
TSTTYPE  DS    0H
*
* NOW DETERMINE LIBRARY TYPE
*
         CLI   PANVOL,C' '        WAS VOLSER PROVIDED?
         BNE   PLSETORG           YUP
*
         XC    CAT(16),CAT        CLEAR CAMLST AREA
         MVI   CAT,X'44'          SET FLAGS
         LA    R14,DSNNOQ         SET DSN
         ST    R14,CAT+4
         LA    R14,CATINFO        SET OUTPUT AREA
         ST    R14,CAT+12
*
         LOCATE CAT
*
         LTR   R15,15             TEST RETURN CODE
         BNZ   PLFRRLOC           IF NO GOOD, SET MESSAGE
*
         MVC   PANVOL(6),CATINFO+6   SET THE VOLSER
*
*        B     PLSETORG           GOT IT
         TITLE 'PLF - SHOWMENU - READ THE VTOC AND CHECK LRECL, DSORG'
PLSETORG DS    0H
*
*  OKAY, GOT THE VOLSER, NOW FIND OUT WHAT THE DSORG IS.
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
*
         XC    VTOC(16),VTOC      CLEAR CAMLST AREA
         MVI   VTOC,X'C1'         SET FLAGS
         LA    R14,DSNNOQ         SET DSN
         ST    R14,VTOC+4
         LA    R14,PANVOL         SET VOLSER
         ST    R14,VTOC+8
         LA    R14,VTOCINFO       SET OUTPUT AREA
         ST    R14,VTOC+12
*
         OBTAIN VTOC
*
         LTR   R15,15             CHECK RETURN CODE
         BNZ   PLFRROBT           IF NO GOOD, SET MESSAGE
*
         LA    R14,VTOCINFO       PICK UP VTOCINFO
         USING DS1FMTID,14
         SR    R15,15             CLEAR REGISTER
         ICM   R15,3,DS1LRECL     SAVE THE LRECL
         LTR   R15,15             IS LRECL 0?
         BNZ   *+12               NOPE
         MVI   DS1DSORG,X'20'     SET DSORG TO DA IF LRECL=0
         ICM   R15,3,DS1BLKL      USE THE BLOCK SIZE AS LRECL
*
         STCM  R15,3,PLFLRECL     SAVE THE LRECL
*
         MVI   LIBTYPE,C'I'         IBM PDS
         CLI   DS1DSORG,X'02'         IS IT PO
         BE    MENUDONE               ALL DONE WITH THIS
         CLI   DS1DSORG,X'03'         IS IT POU
         BE    MENUDONE               ALL DONE WITH THIS
*
         MVI   LIBTYPE,C'V'         VSAM
         CLI   DS1DSORG+1,X'08'       IS IT VSAM
         BE    PLFRRORG               NOT SUPPORTED AT THIS TIME
*
         MVI   LIBTYPE,C'P'   DEFAULT IS PAN LIBRARY
         CLI   PSPOOL,C'1'          IS IT PANSPOOL
         BE    MENUDONE               ALL DONE WITH THIS
         CLI   DS1DSORG,X'20'         IS IT DA (MUST BE PAN)
         BE    MENUDONE               ALL DONE WITH THIS
         CLI   DS1DSORG,X'21'         IS IT DAU (MUST BE PAN)
         BE    MENUDONE               ALL DONE WITH THIS
*
         CLI   DS1DSORG,X'41'         IS IT SEQUENTIAL, UNMOV
         BE    *+12                   SEE IF IT REALLY IS
         CLI   DS1DSORG,X'40'         IS IT SEQUENTIAL
         BNE   PLFRRORG           IF NO GOOD, SET MESSAGE
*
         DROP  14
*
* A PAN LIBRARY MIGHT HAVE THE DSCB1 SET TO INDICATE SEQUENTIAL,
* SO I CHECK THE LRECL. IF THE LRECL IS GREATER THAN 1024, IT MUST
* BE A PAN LIBRARY, IN SPITE OF THE DSORG.
*
         LA    R14,1024             SET TO 1K
         SR    R15,15
         ICM   R15,3,PLFLRECL       PICK UP DSCB LRECL
         CR    R15,R14
         BH    MENUDONE               ALL DONE WITH THIS
*
         MVI   LIBTYPE,C'S'         SET IT SEQ
         B     MENUDONE             MUST BE SEQUENTIAL FILE
*
         TITLE 'PLF - PERFORM THE DYNAMIC ALLOCATIONS'
*
DYNAM7   DS    F
PLFDYDUN DS    0H
         L     R7,DYNAM7
         BR    R7
PLFDYNAM DS    0H
         ST    R7,DYNAM7
*
*
         CLI   DATANAME,C' '         FIRST TIME THROUGH
         BNE   PLFALLO4              NOPE
*
* ALLOCATE THE SEQUENTIAL FILE FOR PAN SYSPRINT
*
         BAL   R14,REDYNAM
*
         MVC   DSNAME(8),USERID
         LA    R1,DSNAME+1
         LA    R14,8
*                                   SET USERID AS HIGH-LEVEL INDEX
         CLI   0(R1),C' '
         BE    *+16
         LA    R1,1(,1)
         BCT   R14,*-12
         B     PLFBADUS
*                                   SET SECOND LEVEL = DDNAME
         MVI   0(R1),C'.'
         MVC   1(7,R1),DDMEM        PDS MEMBER
         MVC   8(5,R1),=C'.DATA'
         MVC   MEMBNAME(44),DSNAME  SAVE THIS FOR LATER
         MVC   MEMBISPF(44),1(R1)   SAVE THIS FOR LATER
         MVC   1(7,R1),DDSIN        SYSIN/PAN MEMBER FILE
         MVC   DATANAME(44),DSNAME  SAVE THIS FOR LATER
         MVC   DATAISPF(44),1(R1)   SAVE THIS FOR LATER
         MVC   1(7,R1),DDSYSP       PAN SYSPRINT FILE
         MVC   LISTNAME(44),DSNAME  SAVE THIS FOR LATER
         MVC   LISTISPF(44),1(R1)   SAVE THIS FOR LATER
*
         MVC   DDNAME(8),DDSYSP
         MVC   DSSTATUS(3),=C'OLD'
*
         BAL   R14,DYNAM            FIRST TRY TO ALLOCATE EXISTING
         LTR   R15,15               IF OK,
         BZ    PLFALLO2             GO DO THE NEXT ONE
*
         MVC   DSSTATUS(3),=C'NEW'
         MVC   DSNDISP(6),=C'CATLG '
         MVC   DSADISP(6),=C'DELETE'
         MVC   DSUNIT(5),=C'SYSDA'
         MVC   DSALLOC(3),=C'CYL'
         MVC   DSPRI(3),=C'001'
         MVC   DSSEC(3),=C'001'
         MVC   DSLRECL(5),=C'00133'
         MVC   DSBLKSI(5),=C'01330'
         MVC   DSORG(2),=C'PS'
         MVC   DSRECFM(2),=C'FB'
         MVI   DSRECFM5,C'A'
*
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE NEW
*
         BAL   R14,REDYNAM
         MVC   DDNAME(8),DDSYSP
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         BAL   R14,DYNAM            FREE THE FILE TO CATALOG IT
*
         MVC   DSSTATUS(3),=C'OLD'
         MVC   DSNAME(44),LISTNAME
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE IT AGAIN
         LTR   R15,15               IF NOT OK,
         BNZ   PLFRRAL1             DISPLAY THE MESSAGE
*
PLFALLO2 DS    0H
*
* ALLOCATE THE SEQUENTIAL FILE FOR PDS MEMBER EDIT/BROWSE
*
         BAL   R14,REDYNAM
*
*
         MVC   DSNAME(44),MEMBNAME
         MVC   DDNAME(8),DDMEM
         MVC   DSSTATUS(3),=C'OLD'
*
         BAL   R14,DYNAM            FIRST TRY TO ALLOCATE EXISTING
         LTR   R15,15               IF OK,
         BZ    PLFALLO3             GO DO THE NEXT ONE
*
         MVC   DSSTATUS(3),=C'NEW'
         MVC   DSNDISP(6),=C'CATLG '
         MVC   DSADISP(6),=C'DELETE'
         MVC   DSUNIT(5),=C'SYSDA'
         MVC   DSALLOC(3),=C'CYL'
         MVC   DSPRI(3),=C'001'
         MVC   DSSEC(3),=C'001'
         MVC   DSLRECL(5),=C'00080'
         MVC   DSBLKSI(5),=C'03120'
         MVC   DSORG(2),=C'PS'
         MVC   DSRECFM(2),=C'FB'
*
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE NEW
*
         BAL   R14,REDYNAM
         MVC   DDNAME(8),DDMEM
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         BAL   R14,DYNAM            FREE THE FILE TO CATALOG IT
*
         MVC   DSSTATUS(3),=C'OLD'
         MVC   DSNAME(44),MEMBNAME
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE IT AGAIN
         LTR   R15,15               IF NOW OK,
         BNZ   PLFRRAL2             DISPLAY THE MESSAGE
*
PLFALLO3 DS    0H
*
* ALLOCATE THE SEQUENTIAL FILE FOR PAN MEMBER EDIT/BROWSE
*
         BAL   R14,REDYNAM
*
*
         MVC   DSNAME(44),DATANAME
         MVC   DDNAME(8),DDSIN
         MVC   DSSTATUS(3),=C'OLD'
*
         BAL   R14,DYNAM            FIRST TRY TO ALLOCATE EXISTING
         LTR   R15,15               IF OK,
         BZ    PLFALLO4             GO DO THE NEXT ONE
*
         MVC   DSSTATUS(3),=C'NEW'
         MVC   DSNDISP(6),=C'CATLG '
         MVC   DSADISP(6),=C'DELETE'
         MVC   DSUNIT(5),=C'SYSDA'
         MVC   DSALLOC(3),=C'CYL'
         MVC   DSPRI(3),=C'001'
         MVC   DSSEC(3),=C'001'
         MVC   DSLRECL(5),=C'00080'
         MVC   DSBLKSI(5),=C'03120'
         MVC   DSORG(2),=C'PS'
         MVC   DSRECFM(2),=C'FB'
*
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE NEW
*
         BAL   R14,REDYNAM
         MVC   DDNAME(8),DDSIN
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         BAL   R14,DYNAM            FREE THE FILE TO CATALOG IT
*
         MVC   DSSTATUS(3),=C'OLD'
         MVC   DSNAME(44),DATANAME
         MVC   DDNAME(8),DDSIN
         BAL   R14,DYNAM            NOW TRY TO ALLOCATE IT AGAIN
         LTR   R15,15               IF NOT OK,
         BNZ   PLFRRAL3             DISPLAY THE MESSAGE
*
PLFALLO4 DS    0H
*
* ALLOCATE THE PANDD2 AND PANDD3 FILES DUMMY
*
         BAL   R7,PLFDD23
*
PLFDYLIB DS    0H
*
*
         BAL   R14,REDYNAM
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),DDLIB
         BAL   R14,DYNAM
*
*
         BAL   R14,REDYNAM
         MVC   DSNAME(44),DSNNOQ
         MVC   DDNAME(8),DDLIB
         MVC   DSSTATUS(3),=C'SHR'
         MVC   DSUNIT(5),=C'SYSDA'
         MVC   DSVOLSER(6),PANVOL
*
         CLI   PANPASSW,C' '
         BE    *+10
         MVC   PASSWORD(6),PANPASSW
*
         BAL   R14,DYNAM
*
         B     PLFDYDUN
PLFPANL  DS    0H
DATANAME DC    CL44' '
LISTNAME DC    CL44' '
MEMBNAME DC    CL44' '
DATAISPF DC    CL44' '
LISTISPF DC    CL44' '
MEMBISPF DC    CL44' '
*
PLFDD237 DS    F
PLFDD23  DS    0H
         ST    R7,PLFDD237
*
* ALLOCATE THE PANDD2 AND PANDD3 FILES DUMMY
*
         BAL   R14,REDYNAM
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),DDPAN2
         BAL   R14,DYNAM
         MVI   DSNAME,C' '
         MVC   DSNAME+1(43),DSNAME
         MVC   DSNAME(8),=C'NULLFILE'
         BAL   R14,DYNAM
*
         LTR   R15,15                   ALLOCATE OK
         BZ    PLFDD3                   YUP
         LR    R2,R15                   SAVE THE REGISTER
         LR    R3,R0                    SAVE THE REGISTER
         WTO   'PLF002 - UNABLE TO ALLOCATE PANDD2',ROUTCDE=11
         DC    H'0'
         B     PLFEOJ
*
PLFDD3   DS    0H
         BAL   R14,REDYNAM
         MVI   DSNAME,X'00'
         MVC   DSNAME+1(43),DSNAME
         MVC   DDNAME(8),DDPAN3
         BAL   R14,DYNAM
         MVI   DSNAME,C' '
         MVC   DSNAME+1(43),DSNAME
         MVC   DSNAME(8),=C'NULLFILE'
         BAL   R14,DYNAM
*
         L     R7,PLFDD237
         LTR   R15,15                   ALLOCATE OK
         BZR   R7
         LR    R2,R15                   SAVE THE REGISTER
         WTO   'PLF003 - UNABLE TO ALLOCATE PANDD3',ROUTCDE=11
         DC    H'0'
         B     PLFEOJ
*
         TITLE 'PLF - PDS LIBRARY PROCESSOR '
APDS7    DS    F
PLFEPDS  DS    0H
         L     R7,APDS7
         BR    R7
*
PLFAPDS  DS    0H
         ST    R7,APDS7
*
         CLI   LIBTYPE,C'I'         PDS LIBRARY
         BNE   PLFRRORG             WHAT IS IT???????
*
         CLI   PANZ,C'E'
         BE    PLFEDPDS             GO EDIT PDS
*
         CLI   PANZ,C' '
         BE    PLFEDPDS             GO EDIT PDS
*
         CLI   PANZ,C'B'
         BE    PLFBRPDS             GO BROWSE PDS
*
         CLI   PANZ,C'C'
         BE    PLFCOMPS             GO COMPRESS PDS
*
         B     PLFBADOP             INVALID OPTION
*
PLFEDPDS DS    0H
*
         MVC   DSNOTLST(44),DSNQUO  SET DSN IN QUOTES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (EDIT,DSNOTLST,PANVOL),VL,MF=(E,ISPARMS)
*
         B     PLFEPDS
*
PLFBRPDS DS    0H
*
         MVC   DSNOTLST(44),DSNQUO  SET DSN IN QUOTES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST,PANVOL),VL,MF=(E,ISPARMS)
*
         B     PLFEPDS
*
PLFCOMPS DS    0H
*
         CLI   LIBTYPE,C'I'
         BNE   PLFRRORG           IF NO GOOD, SET MESSAGE
*
         MVC   BUFFLIB1(7),DDLIB
         MVC   BUFFLIB2(7),DDLIB
         MVC   PLFCPLIB(7),DDLIB
         MVC   PLFCPRNT(7),DDSYSP
         MVC   PLFCPCTL(7),DDSIN
*
         OPEN  (SYSIN,(OUTPUT))
*
         PUT   SYSIN,BUFF80
*
         CLOSE SYSIN
*
         LINK  EP=SPFCOPY,PARAM=(OPTLIST,DDNMELST),VL=1
*
         LTR   R15,15
         BZ    PLFCOMP0             SUCESSFUL COMPRESS
*
         MVC   DSNOTLST(44),LISTISPF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST,PANVOL),VL,MF=(E,ISPARMS)
*
*
         B     PLFRCOMP             RESHOW THE SCREEN
*
PLFCOMP0 DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF001O),                                               X
               VL,MF=(E,ISPARMS)
*
         B     SHOWAGIN
*
         TITLE 'PLF - SEQUENTIAL FILE PROCESSOR '
ASEQ7    DS    F
PLFESEQ  DS    0H
         L     R7,ASEQ7
         BR    R7
*
PLFASEQ  DS    0H
         ST    R7,ASEQ7
*
         CLI   LIBTYPE,C'S'         SEQUENTIAL FILE
         BNE   PLFRRORG             WHAT IS IT???????
*
         CLI   PANZ,C'E'
         BE    PLFEDSEQ             GO EDIT SEQUENTIAL
*
         CLI   PANZ,C' '
         BE    PLFEDSEQ             GO EDIT SEQUENTIAL
*
         CLI   PANZ,C'B'
         BE    PLFBRSEQ             GO BROWSE SEQUENTIAL
*
         B     PLFBADOP             INVALID OPTION
*
PLFEDSEQ DS    0H
*
         MVC   DSNOTLST(44),DSNQUO  SET DSN IN QUOTES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (EDIT,DSNOTLST,PANVOL),VL,MF=(E,ISPARMS)
*
         B     PLFESEQ
*
PLFBRSEQ DS    0H
*
         MVC   DSNOTLST(44),DSNQUO  SET DSN IN QUOTES
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST,PANVOL),VL,MF=(E,ISPARMS)
*
         B     PLFESEQ
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR '
APAN7    DS    F
PLFEPAN  DS    0H
         L     R7,APAN7
         BR    R7
*
PLFAPAN  DS    0H
         ST    R7,APAN7
*
         MVI   REBUILD,C'0'
         MVI   POSITION,C' '
*
         CLI   LIBTYPE,C'P'         PAN LIBRARY
         BNE   PLFRRORG             WHAT IS IT???????
*
* OPEN  PANVALET PROCESSING FOR THIS DDN                              *
*
         CLI   PLFPOPEN,C'1'  PAN OPEN
         BNE   OPENPAN        NO, JUST OPEN IT
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   PLFPOPEN,C'0'  PAN CLOSED
*
OPENPAN  DS    0H
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         SPACE 1
         CLC   ACTION,=F'0'
         BNE   PLFRRPOP              NO GOOD
*
         MVI   PLFPOPEN,C'1'  PAN OPEN
         CLI   PLFTABFL,C'0'      NO ACTIVE TABLE
         BE    TABOPENP
*
DLTTABL  DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PCMD),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANS),                                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               PANLINEP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NAMEPANP),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDELETE,                                               X
               NEWNAMEP),                                              X
               VL,MF=(E,ISPARMS)
*
*                                  DELETE THE TABLE ITSELF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBEND,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   PLFTABFL,C'0'      NO ACTIVE TABLE
*
TABOPENP DS    0H
*
         MVI   SELCODE,C' '       SET SELECTION CODE TO SPACES
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PCMD,CMDDATA,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               SCMD,CMDEDIT,CHAR,L40),                                 X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANS,SELCODE,CHAR,L1),                                  X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               NAMEPANP,NAMEPAN,CHAR,L10),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               NEWNAMEP,NEWNAME,CHAR,L10),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VDEFINE,                                               X
               PANLINEP,TABAREA,CHAR,L50),                             X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBCREATE,                                              X
               PLFTABLE,                                               X
               NAMEKEY,                                                X
               DANAMLST,                                               X
               NOWRITE),                                               X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBVCLEAR,                                              X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
*
         MVI   PLFTABFL,C'1'      AN ACTIVE TABLE
*
DTAILOOP DS    0H
*
         CLI   PLFPOPEN,C'1'      IS IT OPEN
         BE    LD#LOOP            YUP
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         MVI   PLFPOPEN,C'1'      IS IT OPEN
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - READ DIRECTORY LOOP'
LD#LOOP  DS    0H
         XC    ACTION,ACTION
         MVC   COMMENT,NOENTRY               WE WANT NO COMMENTS
         MVC   SUBSET,NOENTRY                BUT NO SUBSET JUNK
*  SEARCH
         L     R15,=V(PSRCH)
         LA    R1,@PSRCH
         BALR  R14,R15               INVOKE DIR SEARCH FUNCTION
         SPACE 1
         CLC   ACTION,=F'0'        RETURN CODE SHOULD BE ZERO
         BE    TDIREND
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF007),                                                X
               VL,MF=(E,ISPARMS)
*
         B     PLFEPAN
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - FORMAT DIRECTORY ENTRY'
TDIREND  DS    0H
         CLC   DIRENTRY(2),=C'$*'  TEST FOR NO MORE DIR ENTRIES
         BE    LD#EOF              THATS ALL..EXIT
*
*  NOW TEST TO SEE WHETHER THE DIRECTORY MEMBER DISPLAY IS TO BE  P   *
*  LIMITED BASED ON USERID, LANGUAGE TYPE, OR MEMBER NAME PREFIX.     *
*
TDIRLIMT DS    0H
*
         CLI   PANU,C' '       USER ID LIMIT REQUESTED?
         BNE   TDIRULIM        YES
         CLI   PANM,C' '       MEMBER NAME LIMIT REQUESTED?
         BNE   TDIRPFX         YES
         CLI   PANL,C' '       LANGUAGE TYPE LIMIT REQUESTED?
         BNE   TDIRLANG        YES
*
         B     DIRFORM
*
TDIRULIM DS    0H
         CLI   PANU,C' '       USER ID LIMIT REQUESTED?
         BE    TDIRPFX         NOPE, GO CHECK FOR MEM NAME PREFIX
*
*  THE LIBRARY SEARCH WAS LIMITED TO SPECIFIC USER ID'S, SO PICK UP   *
*  THE LENGTH OF THE USERID ENTERED, AND COMPARE IT TO THE USERID     *
*       IN THE DIRECTORY ENTRY JUST RETRIEVED.                        *
*
         SR    R1,1            BYTE COUNTER
         LA    R14,7           LIMIT
         LA    R15,PANU        START
*
         CLI   0(R15),C' '     END OF PREFIX
         BE    DOUSRCLC        YES, DO THE COMPARE
*
         LA    R1,1(,1)        BUMP THE COUNTER
         LA    R15,1(,15)      BUMP THE ADDRESS
         BCT   R14,*-16        LOOP
*
DOUSRCLC DS    0H
         BCTR  R1,0
         LTR   R1,1
         BZ    TDIRPFX
         EX    R1,USRCLC
         BE    DIRFORM
         B     TDIRPFX
*
USRCLC   CLC   PANU(1),DUSER
*
TDIRPFX  DS    0H
         CLI   PANM,C' '       MEMBER PREFIX REQUESTED?
         BE    TDIRLANG        NOPE, GO CHECK FOR LANGUAGE TYPE
*
*  THE LIBRARY SEARCH WAS LIMITED BY MEMBER NAME PREFIX, SO PICK UP   *
*  THE LENGTH OF THE PREFIX ENTERED, AND COMPARE IT TO THE MEMBER     *
*  NAME IN THE DIRECTORY ENTRY JUST RETRIEVED.                        *
*
         SR    R1,1            BYTE COUNTER
         LA    R14,7           LIMIT
         LA    R15,PANM        START
*
         CLI   0(R15),C' '     END OF PREFIX
         BE    DOPFXCLC        YES, DO THE COMPARE
*
         LA    R1,1(,1)        BUMP THE COUNTER
         LA    R15,1(,15)      BUMP THE ADDRESS
         BCT   R14,*-16        LOOP
*
DOPFXCLC DS    0H
         BCTR  R1,0
         LTR   R1,1
         BZ    DIRFORM
         EX    R1,PFXCLC
         BE    DIRFORM
         B     TDIRLANG
*
PFXCLC   CLC   PANM(1),DNAME
*
TDIRLANG DS    0H
*
         CLI   PANL,C' '       LANGUAGE TYPE LIMIT REQUESTED?
         BE    LD#LOOP             NO, JUST GO OUT
*
*  THE LIBRARY SEARCH WAS LIMITED BY LANGUAGE TYPE,      SO PICK UP   *
*  THE LENGTH OF THE TYPE   ENTERED, AND COMPARE IT TO THE MEMBER     *
*  TYPE IN THE DIRECTORY ENTRY JUST RETRIEVED.                        *
*
         SR    R1,1            BYTE COUNTER
         LA    R14,7           LIMIT
         LA    R15,PANL        START
*
         CLI   0(R15),C' '     END OF PREFIX
         BE    DOLANGLC        YES, DO THE COMPARE
*
         LA    R1,1(,1)        BUMP THE COUNTER
         LA    R15,1(,15)      BUMP THE ADDRESS
         BCT   R14,*-16        LOOP
*
DOLANGLC DS    0H
         BCTR  R1,0
         LTR   R1,1
         BZ    DIRFORM
         EX    R1,LANGCLC
         BE    DIRFORM
*
         CLC   PANL(3),=C'COB'     WAS COBOL REQUESTED
         BNE   TLANGPLI            NO, SEE IF IT IS PLI
*
         CLC   DTYPE(3),=C'COB'    IS THIS SOME KIND OF COBOL
         BE    DIRFORM
*
         CLC   DTYPE(5),=C'ANSCB'  IS THIS SOME KIND OF COBOL
         BE    DIRFORM
*
         B     LD#LOOP         GO GET NEXT LINE
*
TLANGPLI DS    0H
*
         CLC   PANL(3),=C'PL'      WAS PL/1 REQUESTED
         BNE   LD#LOOP             NO, JUST GO OUT
*
         CLC   DTYPE(3),=C'PL'     IS THIS SOME KIND OF PL/1?
         BE    DIRFORM
*
         B     LD#LOOP         GO GET NEXT LINE
*
LANGCLC  CLC   PANL(1),DTYPE
*
DIRFORM  DS    0H
*
*  MOVE/FORMAT DIRECTORY FIELDS FOR / DIR DISPLAY                     *
*
         MVC   NAMEPAN(10),DNAME
         MVC   PLEVEL(3),DLEVEL
         MVC   PUSER(4),DUSER
         MVC   PTYPE(5),DTYPE
         MVC   PSTATUS(3),DSTATUS
         MVC   PDATEM(8),DDATEM
         MVC   PDATEA(8),DDATEA
         PACK  PACKWK(5),DSTMTS(8)
         MVC   PSTMTS(8),EDIT8
         ED    PSTMTS(8),PACKWK+1
         MVC   PLASTACT(4),DLASTACT
***********************************************************************
*
*   ADD THE DETAIL LINE TO THE TABLE
*
***********************************************************************
*
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         B     LD#LOOP         GO GET NEXT LINE
         TITLE 'PLF - PAN LIBRARY PROCESSOR - EOF ON DIRECTORY'
LD#EOF   DS    0H
*
         MVC   NAME1(10),=CL10'A '   START WITH THIS NAME
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
         MVI   PLFPOPEN,C'0'  PAN CLOSED
*
* POSITION TO THE TOP OF THE TABLE
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
SHOWTBDS DS    0H
         SR    R15,15
         C     R15,TABCOUNT  ANY LINES ADDED?
         ST    R15,TABCOUNT  SET TO ZERO IN ANY CASE
         BNE   SHOWTABL      YUP, GO SHOW THEM
*
         MVC   NAMEPAN(10),=CL10'NO ENTRY '
         MVC   TABAREA(77),ASTERS   ASTERISK LINE
         BAL   R7,OUTMSG        ADD THE ENTRY TO THE TABLE
*
         SR    R15,15
         ST    R15,TABCOUNT  SET TO ZERO IN ANY CASE
         TITLE 'PLF - PAN LIBRARY PROCESSOR - DISPLAY DIRECTORY'
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
SHOWTABL DS    0H
*
* FIRST CHECK TO SEE WHETHER ANY DISPLAY POSITIONING IS NEEDED
*
         CLI   POSITION,C' '             ANY NAME FIELD ENTERED?
         BE    SHOWATAB                  NOPE, SHOW START OF TABLE
*
         MVC   NAMEPAN(10),POSITION
         MVI   POSITION,C' '
         MVC   POSITION+1(9),POSITION
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBTOP,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBSCAN,                                                X
               PLFTABLE,                                               X
               NAMEPANP),                                              X
               VL,MF=(E,ISPARMS)
         B     SHOWATAB
*
NEXTLINE DS    0H
         L     R15,DISPLRC          GET THE LAST PREVIOUS RETURN CODE
         LTR   R15,15               ALL DONE?
         BNZ   GETNXTNT             NO, RETRIEVE THE NEXT TABLE ENTRY
*
* I HAVE PROCESSED ALL THE TABLE ENTRIES THAT WERE SELECTED, NOW I
* HAVE TO FIND OUT WHETHER TO REBUILD THE DIRECTORY OR NOT.
*
         CLI   REBUILD,C'0'         DO I HAVE TO REBUILD
         MVI   REBUILD,C'0'
         BE    SHOWTABL             RESHOW THE TABLE
         B     DLTTABL              REBUILD THE TABLE
*
GETNXTNT DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         ST    R15,DISPLRC          SAVE THE RETURN CODE
         B     AFTRTBDL             GO PROCESS IT
*
SHOWATAB DS    0H
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBDISPL,                                               X
               PLFTABLE,                                               X
               PLFMENU2),                                              X
               VL,MF=(E,ISPARMS)
         ST    R15,DISPLRC          SAVE THE RETURN CODE
AFTRTBDL DS    0H
*
***********************************************************************
*
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
*
***********************************************************************
*
         C     R15,F4         COMMAND ENTERED/LINE MODIFIED??
         BE    GOTSTUFF       YUP
         C     R15,FZEROS     COMMAND ENTERED/LINE MODIFIED??
         BNE   PLFEPAN        NOPE
GOTSTUFF DS    0H
         MVC   SELSAVE(1),SELCODE    SAVE SEL CODE
         MVC   DNAME(10),NAMEPAN     SAVE MEMBER NAME
         MVC   NAME(10),NAMEPAN     SAVE MEMBER NAME
*                                   COPY NAME TO VARIABLE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANSPFN),                                               X
               VL,MF=(E,ISPARMS)
*
         MVI   SELCODE,C' '       SET SELECTION CODE TO SPACES
*
         TR    NEWNAME(10),CAPSONLY    UPPER-CASE ALL INPUT FIELDS
         OI    SELSAVE,C' '        UPPER-CASE IT
*
         CLI   SELSAVE,C' '       IF A SELECTION CODE WAS NOT ENTERED
         BE    GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'B'   IS IT A BROWSE
         BE    PBROWSE        GO DO IT
*
         CLI   SELSAVE,C'S'   IS IT "SELECT"
         BNE   CMDCHKS        NOPE
         MVI   SELSAVE,C'B'   SET IT TO BROWSE
         CLI   PSPOOL,C'1'    IS IT PANSPOOL
         BE    PBROWSE        GO DO IT
         MVI   SELSAVE,C'E'   IT IS EDIT
         B     PEDIT          GO DO IT
*
CMDCHKS  DS    0H
         CLI   SELSAVE,C'E'   IS IT EDIT
         BE    PEDIT          GO DO IT
*
         CLI   SELSAVE,C'D'   IS IT DELETE
         BE    PDELETE        GO DO IT
*
         CLI   SELSAVE,C'K'   IS IT DELETE
         BE    PDELETE        GO DO IT
*
         CLI   SELSAVE,C'F'   IS IT LOCATE
         BE    PLOCATE        GO DO IT
*
         CLI   SELSAVE,C'L'   IS IT LOCATE
         BE    PLOCATE        GO DO IT
*
         CLI   SELSAVE,C'N'   IS IT STATUS ENABLE
         BE    PENABLE        GO DO IT
*
         CLI   SELSAVE,C'R'   IS IT RENAME
         BE    PRENAME        GO DO IT
*
         CLI   SELSAVE,C'C'   IS IT COPY
         BE    PCOPY          GO DO IT
*
         CLI   SELSAVE,C'U'   IS IT CHANGE USER ID
         BE    PNEWUSER       GO DO IT
*
* ADD TESTS FOR OTHER LINE SELECTION CODES HERE
*
         B     SHOWTBDS       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - CHANGE USER ID'
PNEWUSER DS    0H
*
         MVC   ULINE+7(10),NAMEPAN
         LA    R15,ULINE+8
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12         YUP
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
*
         OC    PUSER(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   1(4,R15),PUSER     MOVE IT IN
         MVI   5(R15),C','  SET THE COMMA
*
         OC    NEWNAME(4),=C'0000'  SET BLANKS TO ZEROS
         MVC   6(4,R15),NEWNAME   MOVE IT IN
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTULINE
         CLC   CONTROL(4),=C'0000' ONTROL WORD ENTERED
         BE    PUTULINE
         PUT   SYSIN,CLINE
PUTULINE DS    0H
         PUT   SYSIN,ULINE
         CLOSE SYSIN
*
         MVC   PANACTN(7),=C'PANUSER'
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         BAL   R7,PLFDD23
*
         LA    R1,PANPARMS
         LINK  EP=PLFPAN#1
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO REPEAT THE DISPLAY
*
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR EDIT
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF001),                                                X
               VL,MF=(E,ISPARMS)
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - EDIT A PAN MEMBER'
PEDIT    DS    0H
*
         MVC   ENAME(10),NAMEPAN
*
         BAL   R7,READPAN
*
         MVC   DSNOTLST(44),DATAISPF   SET DSN FOR EDIT
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (EDIT,DSNOTLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (DISPLAY,                                               X
               PLFMENU3),                                              X
               VL,MF=(E,ISPARMS)
*
         OI    CMDEDIT,C' '     UPPER-CASE IT
         CLI   CMDEDIT,C'S'     IS IT SAVE
         BNE   GOODEDT          NOPE, DON'T SAVE ANYTHING
*
*
         MVC   PANACTN(7),=C'PANUPD '
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         BAL   R7,PLFDD23
         LA    R1,PANPARMS
         LINK  EP=PLFPAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODEDT        GO REPEAT THE DISPLAY
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF004),                                                X
               VL,MF=(E,ISPARMS)
*
         B     NEXTLINE
*
GOODEDT  DS    0H
         MVI   REBUILD,C'1'         DIRECTORY TABLE MUST BE REBUILT
         MVI   NAME1,C'A'
         MVI   NAME1+1,C' '
         MVC   NAME1+2(20),NAME1+1
         MVC   POSITION(10),ENAME
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - LOCATE A PAN MEMBER'
PLOCATE  DS    0H
*
         MVC   POSITION(10),CMDDATA+2
*
         LA    R1,POSITION+1
         LA    R14,9
*
         CLI   0(R1),C' '     SEARCH FOR FIRST BLANK
         BE    *+16           SET AN ASTERISK
         LA    R1,1(,1)       BUMP POINTER
         BCT   R14,*-12       LOOP
         B     *+8            FULL 10 CHARACTERS PROVIDED
         MVI   0(R1),C'*'     GENERIC SEARCH
*
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - RENAME A PAN MEMBER'
PRENAME  DS    0H
*
         MVC   RLINE(9),=CL9'++RENAME'
PRBLANKS DS    0H
         MVC   POSITION(10),NAMEPAN
         MVC   RLINE+9(10),NAMEPAN
         LA    R15,RLINE+10
*
         CLI   0(R15),C' '  GOT A BLANK?
         BE    *+12
         LA    R15,1(,15)   NO, BUMP 1
         B     *-12
*
         MVI   0(R15),C','  SET THE COMMA
         MVC   1(10,R15),NEWNAME  MOVE IT IN
*
         OPEN  (SYSIN,(OUTPUT))
*
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTRLINE
         CLC   CONTROL(4),=C'0000' ONTROL WORD ENTERED
         BE    PUTRLINE
         PUT   SYSIN,CLINE
*
PUTRLINE DS    0H
*
         PUT   SYSIN,RLINE
         CLOSE SYSIN
*
*
         MVC   PANACTN(7),=C'PANREN '
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         BAL   R7,PLFDD23
         LA    R1,PANPARMS
         LINK  EP=PLFPAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODRNAM       GO REPEAT THE DISPLAY
*
*
         MVC   DSNOTLST(44),LISTISPF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF005),                                                X
               VL,MF=(E,ISPARMS)
         B     NEXTLINE
*
GOODRNAM DS    0H
         MVI   REBUILD,C'1'         DIRECTORY TABLE MUST BE REBUILT
         MVC   POSITION(10),NEWNAME    SET POSITION
         B     NEXTLINE
         TITLE 'PLF - PAN LIBRARY PROCESSOR - COPY A PAN MEMBER'
PCOPY    DS    0H
*
         MVC   RLINE(9),=CL9'++COPY  '
         MVC   PANACTN(7),=C'PANCOPY'
*
         B     PRBLANKS       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - DELETE A PAN MEMBER'
MINUS1   DC    F'-1'
PDELETE  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         MVC   KLINE+14(10),NAMEPAN SAVE THIS NAME
*
         MVC   DLINE+9(70),DLINE+8
         MVC   DLINE+9(10),NAMEPAN  SAVE THIS NAME
         LA    R15,DLINE+10
         CLI   0(R15),C' '
         BE    *+12
         LA    R15,1(,15)
         B     *-12
         MVC   0(8,R15),=C',DISABLE'
*
         CLI   POSITION,C' '        POSITIONING SPECIFIED ALREADY?
         BNE   PDELCTL              YES, JUST LET IT GO
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBSKIP,PLFTABLE,                                       X
               MINUS1),                                                X
               VL,MF=(E,ISPARMS)
*
         LTR   R15,15
         BNZ   PDELCTL
*
         MVC   POSITION(10),NAMEPAN SAVE THIS NAME
*
PDELCTL  DS    0H
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTDLINE
         CLC   CONTROL(4),=C'0000' ONTROL WORD ENTERED
         BE    PUTDLINE
         PUT   SYSIN,CLINE
*
PUTDLINE DS    0H
*
         CLI   SELSAVE,C'K'   IS IT DELETE
         BE    PUTKLINE       YES
*
         PUT   SYSIN,DLINE
*
         CLOSE SYSIN
*
         BAL   R7,PLFDD23
*
         MVC   PANACTN(7),=C'PANSTAT'
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         LA    R1,PANPARMS
         LINK  EP=PLFPAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO RESET THE DIR LIST
         B     NOTDLTED       GO DISPLAY THE SYSPRINT FILE MSGS
*
PUTKLINE DS    0H
*
         PUT   SYSIN,KLINE
*
         CLOSE SYSIN
*
         BAL   R7,PLFDD23
         LA    R1,PANPARMS
         LINK  EP=PAN#2
*
         LTR   R15,15         IF RC=0
         BZ    GOODNAM        GO RESET THE DIR LIST
         B     NOTDLTED       GO DISPLAY THE SYSPRINT FILE MSGS
*
*
NOTDLTED DS    0H
         MVC   DSNOTLST(44),LISTISPF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF002),                                                X
               VL,MF=(E,ISPARMS)
*
         B     NEXTLINE
*
GOODNAM  DS    0H
         MVI   REBUILD,C'1'         DIRECTORY TABLE MUST BE REBUILT
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - ENABLE A PAN MEMBER'
PENABLE  DS    0H
*
         OPEN  (SYSIN,(OUTPUT))
*
         MVC   DLINE+9(70),DLINE+8
         MVC   DLINE+9(10),NAMEPAN  SAVE THIS NAME
         LA    R15,DLINE+10
         CLI   0(R15),C' '
         BE    *+12
         LA    R15,1(,15)
         B     *-12
         MVC   0(8,R15),=C',ENABLE '
*
         CLI   POSITION,C' '        POSITIONING SPECIFIED ALREADY?
         BNE   PNABCTL              YES, JUST LET IT GO
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBSKIP,PLFTABLE,                                       X
               MINUS1),                                                X
               VL,MF=(E,ISPARMS)
*
         LTR   R15,15
         BNZ   PNABCTL
*
         MVC   POSITION(10),NAMEPAN SAVE THIS NAME
*
PNABCTL  DS    0H
         CLI   CONTROL,C' '       CONTROL WORD ENTERED
         BE    PUTNLINE
         CLC   CONTROL(4),=C'0000' ONTROL WORD ENTERED
         BE    PUTNLINE
         PUT   SYSIN,CLINE
*
PUTNLINE DS    0H
*
         PUT   SYSIN,DLINE
*
         CLOSE SYSIN
*
         BAL   R7,PLFDD23
*
         MVC   PANACTN(7),=C'PANOTHR'
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               PANWORK),                                               X
               VL,MF=(E,ISPARMS)
*
         LA    R1,PANPARMS
         LINK  EP=PLFPAN#1
*
         LTR   R15,15         IF RC=0
         BZ    GOODNABL       GO RESET THE DIR LIST
*
         MVC   DSNOTLST(44),LISTISPF
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF002),                                                X
               VL,MF=(E,ISPARMS)
*
         B     NEXTLINE
*
GOODNABL DS    0H
         MVI   REBUILD,C'1'         DIRECTORY TABLE MUST BE REBUILT
         B     NEXTLINE
*
         TITLE 'PLF - PAN LIBRARY PROCESSOR - BROWSE A PAN MEMBER'
PBROWSE  DS    0H
*
         BAL   R7,READPAN
*
         MVC   DSNOTLST(44),DATAISPF   SET DSN FOR PAN MEMB BROWSE
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    *+10                JUST DO THE BROWSE
         MVC   DSNOTLST(44),LISTISPF   SET DSN FOR PANSPOOL BROWSE
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (BROWSE,DSNOTLST),VL,MF=(E,ISPARMS)
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - PROCESS A COMMAND'
*
*    THE COMMAND ENTERED IS IN "CMDDATA"
*
GOTCMD   DS    0H
*
         TR    CMDDATA,CAPSONLY   UPPER-CASE IT
*
         CLI   CMDDATA,C' ' ANY DATA IN COMMAND LINE
         BE    PLFEPAN      NOPE
*
         CLI   CMDDATA,C'L' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'F' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'S' LOCATE
         BE    PLOCATE      YUP
         CLI   CMDDATA,C'C' CONTROL
         BE    PCONTROL     YUP
*
*     PUT ADDITIONAL COMMAND TESTS HERE
*
         B     PLFBADCM
         TITLE 'PLF - PAN LIBRARY PROCESSOR - PROCESS A CONTROL WORD'
PCONTROL DS    0H
         MVC   CONTROL(4),CMDDATA+1
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               VCTL),                                                  X
               VL,MF=(E,ISPARMS)
*
         B     NEXTLINE       GO REPEAT THE DISPLAY
         TITLE 'PLF - PAN LIBRARY PROCESSOR - READ A PAN MEMBER'
*
*  READ THE PAN MEMBER
*
         SPACE
READPAN  DS    0H
*
         ST    R7,KEEP7PAN
*
         CLI   SELSAVE,C'B'        IS IT BROWSE
         BE    RDBROWSE            YUP, NO SPECIAL HANDLING
*
         CLI   SELSAVE,C'E'        IS IT EDIT
         BNE   PLFEPAN             NOPE, I SHOULDN'T BE HERE
*
         XC    ACTION,ACTION
         MVC   NAME(10),DNAME      MOVE SEARCHED NAME TO READ NAME
         LA    R7,SYSIN           DCB ADDRESS
         ST    R7,ADCBADD
*
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BNO   PLFEPAN             NOPE, FORGET IT
*
         MVC   ELINE+9(40),SPACES  CLEAR IT
         MVC   ELINE+9(10),DNAME   MOVE SEARCHED NAME TO UPDATE CARD
         LA    R14,ELINE+10        SET START OF LOOP
         LA    R15,9               SET LOOP CONTROL
*
UPDLOOP  DS    0H
         CLI   0(R14),C' '  GOT A BLANK
         BE    UPDMOVE      MOVE IN THE STUFF
         LA    R14,1(,14)   BUMP
         BCT   R15,UPDLOOP  LOOP
*
UPDMOVE  DS    0H
*
         MVI   0(R14),C','
         MVC   1(3,R14),PLEVEL       MOVE IN OLD LEVEL NUMBER
         MVC   4(4,R14),=C',ALL'
*
*
         B     READAGN
*
RDBROWSE DS    0H
*
         XC    ACTION,ACTION
         MVC   NAME(10),DNAME      MOVE SEARCHED NAME TO READ NAME
         LA    R7,SYSIN           DCB ADDRESS
         CLI   PSPOOL,C'0'         NOT PANSPOOL FILE
         BE    OPENOUT             JUST OPEN IT
         LA    R7,PANLIST         DCB ADDRESS
OPENOUT  DS    0H
         ST    R7,ADCBADD
         OPEN  ((7),(OUTPUT))
         TM    48(R7),X'10'       IS IT OPEN?
         BO    READAGN            YUP
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF008),                                                X
               VL,MF=(E,ISPARMS)
*
         B     PLFEPAN
*
         EJECT
READAGN  DS    0H
         CLI   PLFPOPEN,C'1'      IS IT OPEN
         BE    READAPAN           YUP
*
         XC    ACTION,ACTION
         L     R15,=V(POPEN)
         LA    R1,@POPEN
         BALR  R14,R15               INVOKE OPEN PANVALET FILE
         MVI   PLFPOPEN,C'1'  PAN NOW OPEN
*
         CLC   ACTION,=F'0'
         BE    READAPAN            RETURN CODE SHOULD BE ZERO
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF009),                                                X
               VL,MF=(E,ISPARMS)
*
         B     PLFEPAN
*
*
READAPAN DS    0H
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS
         L     R15,=V(PREAD)
         LA    R1,@PREAD
         BALR  R14,R15             GO READ A RECORD VIA PAM
         SPACE 1
         CLC   ACTION,=F'0'
         BE    GOODREAD            RETURN CODE SHOULD BE ZERO
         CLC   ACTION,=F'4'        4 IS PROBABLY "INCLUDE NOT FOUND"
         BE    GOODREAD
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF009),                                                X
               VL,MF=(E,ISPARMS)
*
         B     PLFEPAN
*
GOODREAD DS    0H
         CLC   RECORD(2),=C'$*'    TEST FOR END OF DATA
         BE    ENDREAD
*
*PROCESS A RECORD
*
         CLI   PSPOOL,C'1'         PANSPOOL FILE
         BE    PUTSPL              READ AGAIN
         L     R7,ADCBADD
         PUT   (7),RECORD
         B     READAGN
PUTSPL   DS    0H
         XC    ACTION,ACTION                SET TO BINARY ZEROS
         MVC   PSPREC(80),RECORD
         MVC   INCLUDES,NOENTRY             DO NOT PROCESS INCLUDES
         MVC   COMMENT,NOENTRY              DO NOT PROCESS COMMENTS
         L     R15,=V(PREAD)
         LA    R1,@PREAD
         BALR  R14,R15             GO READ A RECORD VIA PAM
         SPACE 1
         CLC   ACTION,=F'0'
         BE    GOTPSPL             RETURN CODE SHOULD BE ZERO
*
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               PLF009),                                                X
               VL,MF=(E,ISPARMS)
*
         B     PLFEPAN
*
GOTPSPL  DS    0H
         MVC   PSPREC+80(53),RECORD+27
         L     R7,ADCBADD
         PUT   (7),PSPREC
         B     READAGN
ENDREAD  DS    0H
         L     R7,ADCBADD
         CLOSE ((7))
*
         XC    ACTION,ACTION       CLEAR ACTION WORD
         L     R15,=V(PCLOSE)
         LA    R1,@PCLOSE
         BALR  R14,R15             GO CLOSE OUT PANVALET FILE
*
         CLC   ACTION,=F'0'
         BNE   PLFRRPRC            RETURN CODE SHOULD BE ZERO
*
ENDREAD2 DS    0H
         MVI   PLFPOPEN,C'0'  PAN NOT OPEN
*
         L     R7,KEEP7PAN
         BR    R7
         TITLE 'PLF - OUTMSG ROUTINE'
*
***********************************************************************
*
*   ADD A LINE TO THE TABLE
*
***********************************************************************
*
OUTMSG   DS    0H
         ST    R7,OUT7HOLD    RETURN ADDRESS
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (VPUT,                                                  X
               DANAMLST),                                              X
               VL,MF=(E,ISPARMS)
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (TBADD,                                                 X
               PLFTABLE),                                              X
               VL,MF=(E,ISPARMS)
         MVI   TABAREA,C' '
         MVC   TABAREA+1(79),TABAREA
         L     R15,TABCOUNT  PICK UP TABLE ENTRY COUNT
         LA    R15,1(,15)    ADD 1
         ST    R15,TABCOUNT  SET IT
         L     R7,OUT7HOLD    RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
         TITLE 'PERFORMED ROUTINES'
MOVENAME DS    0H
*
         CLI   0(R15),C' '                END OF THIS NAME
         BE    MOVEDONE                   YUP
*
         MVC   0(1,R1),0(R15)             END OF THIS NAME
         LA    R1,1(,1)                   BUMP " TO " FIELD ADDRESS
         LA    R15,1(,15)                 BUMP "FROM" FIELD ADDRESS
         B     MOVENAME                   LOOP
*
MOVEDONE DS    0H
*
         BR    R14                        YUP
*
REDYNAM  DS    0H
         MVI   PARMLIST,C' '
         MVC   PARMLIST+001(256),PARMLIST
         MVC   PARMLIST+256(256),PARMLIST+255
         MVI   WORKAREA,X'00'
         MVC   WORKAREA+001(256),WORKAREA
         MVC   WORKAREA+256(256),WORKAREA+255
         MVC   WORKAREA+512(256),WORKAREA+511
         BR    R14
         TITLE 'PLF - ERROR MESSAGE ROUTINES  '
PLFSETMS DS    0H
         L     R15,ISPLINK
         CALL  (15),                                                   X
               (SETMSG,                                                X
               (2)),                                                   X
               VL,MF=(E,ISPARMS)
*
         BR    R3
*
PLFRRPOP DS    0H
         LA    R2,PLF006
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRPRC DS    0H
         LA    R2,PLF001A
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRDSN DS    0H
         LA    R2,PLF001B
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRENQ DS    0H
         MVI   ENQFLAG,C'0'                            NOT GOT ANY
         LA    R2,PLF001C
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRORG DS    0H
         LA    R2,PLF001D
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRROBT DS    0H
         LA    R2,PLF001E
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRLOC DS    0H
         LA    R2,PLF001F
         LA    R3,SHOWAGIN
         LA    R14,DSNNOQ         SET DSN
         DC    H'0'
         B     PLFSETMS
*
PLFRRSN  DS    0H
         LA    R2,PLF001G
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFBADUS DS    0H
         LA    R2,PLF001H
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRAL1 DS    0H
         LA    R2,PLF001I
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRAL2 DS    0H
         LA    R2,PLF001J
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRRAL3 DS    0H
         LA    R2,PLF001K
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFBADRC DS    0H
         LA    R2,PLF001L
         LA    R3,SHOWAGIN
         B     PLFEOJ
*
PLFBADOP DS    0H
         LA    R2,PLF001M
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFRCOMP DS    0H
         LA    R2,PLF001N
         LA    R3,SHOWAGIN
         B     PLFSETMS
*
PLFBADCM DS    0H
         LA    R2,PLF001P
         LA    R3,NEXTLINE
         B     PLFSETMS
*
         LTORG
         EJECT
         TITLE 'DYNAMIC ALLOCATION INTERFACE'
DYNAM    DS    0H
         STM   R1,R14,SAVE14       SAVE RETURN POINT
         LA    R6,DYNWORK
         USING S99RB,R6
         ST    R6,REQBKPTR         POINT TO REQUEST BLOCK.
         OI    REQBKPTR,X'80'
         LA    R1,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK.
         STC   R1,S99RBLN
         CLC   DSNAME,NULLS        CHECK FOR UNALLOCATION REQUEST.
         BNE   *+12
         MVI   S99VERB,S99VRBUN    REQUEST UNALLOCATION.
         B     *+12
         MVI   S99VERB,S99VRBAL    REQUEST ALLOCATION.
         OI    S99FLG11,S99NOCNV+S99NOMNT
         LA    R8,S99RBEND         PICK UP ADDRESS FOR TEXT POINTERS
         ST    R8,S99TXTPP
         L     R5,TXPTRSPC         PICK UP SPACE RESERVED FOR TEXT PTRS
         ALR   R5,R8               POINT TO FIRST TEXT UNIT.
         USING S99TUNIT,R5
         SPACE 1
* DDNAME **************************
         SPACE 1
         LA    R7,DDNAME-PARMLIST  INIT 'OFFSET' REGISTER.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BNE   *+12
         LA    R1,DUNDDNAM
         B     *+8
         LA    R1,DALDDNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         LA    R14,DDNAME          FIND OUT ACTUAL LENGTH OF DDNAME.
         LA    R15,L'DDNAME
         BAL   R1,PP000
         LTR   R15,R15
         BNP   QQ000
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DDNAME.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         CLI   S99VERB,S99VRBUN    HAS UNALLOCATION BEEN REQUESTED?
         BE    MM000               GO DO REQUEST.
         SPACE 1
* DSNAME **************************
         SPACE 1
         LA    R7,DSNAME-PARMLIST  OFFSET
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BNE   AA100
         LA    R1,DALDUMMY
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
         B     AA190
AA100    EQU   *
         LA    R14,DSNAME
         LA    R15,L'DSNAME
         BAL   R1,PP000            GET DSNAME LENGTH.
         LTR   R15,R15
         BNP   AA110
         LA    R1,DALDSNAM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSNAME.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSMEMBER ************************
         SPACE 1
AA110    EQU   *
         LA    R7,DSMEMBER-PARMLIST
         LA    R14,DSMEMBER
         LA    R15,L'DSMEMBER
         BAL   R1,PP000            GET DSMEMBER LENGTH.
         LTR   R15,R15
         BNP   AA120
         LA    R1,DALMEMBR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSMEMBER.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* PASSWORD ************************
         SPACE 1
AA120    EQU   *
         LA    R7,PASSWORD-PARMLIST
         LA    R14,PASSWORD
         LA    R15,L'PASSWORD
         BAL   R1,PP000            GET PASSWORD LENGTH
         LTR   R15,R15
         BNP   AA130
         LA    R1,DALPASSW
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE PASSWORD.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSSTATUS ************************
         SPACE 1
AA130    EQU   *
         LA    R7,DSSTATUS-PARMLIST
         LA    R14,DSSTATUS
         LA    R15,L'DSSTATUS
         BAL   R1,PP000            GET DSSTATUS LENGTH
         LTR   R15,R15
         BNP   AA140
         LA    R1,DALSTATS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSSTATUS,=CL8'OLD '
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA135
         CLC   DSSTATUS,=CL8'MOD '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA135
         CLC   DSSTATUS,=CL8'NEW '
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA135
         CLC   DSSTATUS,=CL8'SHR '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA135    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSNDISP *************************
         SPACE 1
AA140    EQU   *
         LA    R7,DSNDISP-PARMLIST
         LA    R14,DSNDISP
         LA    R15,L'DSNDISP
         BAL   R1,PP000            GET DSNDISP LENGTH
         LTR   R15,R15
         BNP   AA150
         LA    R1,DALNDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSNDISP,=CL8'UNCATLG '
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA145
         CLC   DSNDISP,=CL8'CATLG   '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA145
         CLC   DSNDISP,=CL8'DELETE  '
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA145
         CLC   DSNDISP,=CL8'KEEP    '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA145    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSADISP *************************
         SPACE 1
AA150    EQU   *
         LA    R7,DSADISP-PARMLIST
         LA    R14,DSADISP
         LA    R15,L'DSADISP
         BAL   R1,PP000            GET DSADISP LENGTH
         LTR   R15,R15
         BNP   AA160
         LA    R1,DALCDISP
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSADISP,=CL8'UNCATLG '
         BNE   *+12
         MVI   S99TUPAR,X'01'
         B     AA155
         CLC   DSADISP,=CL8'CATLG   '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA155
         CLC   DSADISP,=CL8'DELETE  '
         BNE   *+12
         MVI   S99TUPAR,X'04'
         B     AA155
         CLC   DSADISP,=CL8'KEEP    '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA155    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSUNIT **************************
         SPACE 1
AA160    EQU   *
         LA    R7,DSUNIT-PARMLIST
         LA    R14,DSUNIT
         LA    R15,L'DSUNIT
         BAL   R1,PP000            GET DSUNIT LENGTH
         LTR   R15,R15
         BNP   AA170
         LA    R1,DALUNIT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSUNIT.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV1 ************************
         SPACE 1
AA170    EQU   *
         LA    R7,$RESERV1-PARMLIST
         CLC   $RESERV1,BLANKS
         BNE   QQ000
         SPACE 1
* DSVOLSER ************************
         SPACE 1
AA170A   EQU   *
         LA    R7,DSVOLSER-PARMLIST
         CLC   DSVOLSER,NULLS
         BNE   AA172
         LA    R1,DALRTVOL         SET UP TO HAVE VOLSER RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,6
         MVC   S99TUPAR(6),BLANKS
         LA    R1,S99TUPAR
         ST    R1,WKRTNVOL         SAVE ADDRESS OF PARAMETER FIELD.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+6       NEXT TEXT UNIT.
         B     AA180
AA172    EQU   *
         LA    R14,DSVOLSER
         LA    R15,L'DSVOLSER
         BAL   R1,PP000            GET DSVOLSER LENGTH.
         LTR   R15,R15
         BNP   AA180
         LA    R1,DALVLSER
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLSER.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV2 ************************
         SPACE 1
AA180    EQU   *
         LA    R7,$RESERV2-PARMLIST
         CLC   $RESERV2,BLANKS
         BNE   QQ000
         SPACE 1
* DSVOLREF ************************
         SPACE 1
AA180A   EQU   *
         LA    R7,DSVOLREF-PARMLIST
         LA    R14,DSVOLREF
         LA    R15,L'DSVOLREF
         BAL   R1,PP000            GET DSVOLREF LENGTH.
         LTR   R15,R15
         BNP   AA190
         CLC   DSVOLSER,BLANKS     CHECK TO SEE THAT DSVOLREF DOES NOT
         BE    AA184                   CONFLICT WITH DSVOLSER.
         CLC   DSVOLSER,NULLS
         BNE   QQ000
AA184    EQU   *
         LA    R1,DALVLRDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE DSVOLREF.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* DSFREE **************************
         SPACE 1
AA190    EQU   *
         LA    R7,DSFREE-PARMLIST
         LA    R14,DSFREE
         LA    R15,L'DSFREE
         BAL   R1,PP000            GET DSFREE LENGTH.
         LTR   R15,R15
         BNP   AA198
         CLC   DSFREE,=CL8'END   '
         BE    AA198
         CLC   DSFREE,=CL8'CLOSE '
         BNE   QQ000
         LA    R1,DALCLOSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
AA198    EQU   *
         CLC   DSNAME,=CL44'NULLFILE ' CHECK FOR DUMMY ALLOCATION.
         BE    CC100               GO DO DCB PARAMETERS.
         SPACE 1
* DSLABEL *************************
         SPACE 1
AA200    EQU   *
         LA    R7,DSLABEL-PARMLIST
         LA    R14,DSLABEL
         LA    R15,L'DSLABEL
         BAL   R1,PP000            GET DSLABEL LENGTH.
         LTR   R15,R15
         BNP   AA210
         LA    R1,DALLABEL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSLABEL,=CL4'SL  '
         BNE   *+12
         MVI   S99TUPAR,X'02'
         B     AA205
         CLC   DSLABEL,=CL4'SUL '
         BNE   QQ000
         MVI   S99TUPAR,X'08'
AA205    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSINOUT *************************
         SPACE 1
AA210    EQU   *
         LA    R7,DSINOUT-PARMLIST
         LA    R14,DSINOUT
         LA    R15,L'DSINOUT
         BAL   R1,PP000            GET DSINOUT LENGTH
         LTR   R15,R15
         BNP   AA220
         LA    R1,DALINOUT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSINOUT,=CL4'IN  '
         BNE   *+12
         MVI   S99TUPAR,X'80'
         B     AA215
         CLC   DSINOUT,=CL4'OUT '
         BNE   QQ000
         MVI   S99TUPAR,X'40'
AA215    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* $RESERV3 ************************
         SPACE 1
AA220    EQU   *
         LA    R7,$RESERV3-PARMLIST
         CLC   $RESERV3,BLANKS
         BNE   QQ000
         SPACE 3
         CLC   DSSTATUS,BLANKS     CHECK FOR NEW ALLOCATION.
         BE    BB100
         CLC   DSSTATUS,=CL8'NEW '
         BNE   CC100               GO DO DCB PARAMETERS.
         EJECT
* DSPWDLBL ************************
         SPACE 1
BB100    EQU   *
         LA    R7,DSPWDLBL-PARMLIST
         LA    R14,DSPWDLBL
         LA    R15,L'DSPWDLBL
         BAL   R1,PP000            GET DSPWDLBL LENGTH.
         LTR   R15,R15
         BNP   BB110
         LA    R1,DALPASPR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSPWDLBL,=CL8'PASSWORD'
         BNE   *+12
         MVI   S99TUPAR,X'10'
         B     BB105
         CLC   DSPWDLBL,=CL8'NOPWREAD'
         BNE   QQ000
         MVI   S99TUPAR,X'30'
BB105    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDATE **************************
         SPACE 1
BB110    EQU   *
         LA    R7,DSDATE-PARMLIST
         LA    R14,DSDATE
         LA    R15,L'DSDATE
         BAL   R1,PP000            GET DSDATE LENGTH.
         LTR   R15,R15
         BNP   BB120
         CLC   DSDATE(6),=C'EXPDT=' CHECK FOR DATE TYPE.
         BNE   BB114
         SH    R15,=H'6'
         CH    R15,=H'5'           CHECK FOR VALID LENGTH.
         BNE   QQ000
         LA    R14,6(0,R14)        BUMP POINTER FOR MVCPARM
         LA    R1,DALEXPDT
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM         MOVE EXPDT DATE.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         B     BB120
         SPACE 1
BB114    EQU   *
         CLC   DSDATE(6),=C'RETPD='
         BNE   QQ000               ERROR
         SH    R15,=H'6'
         BNP   QQ000
         CH    R15,=H'4'
         BH    QQ000
         LA    R14,6(0,R14)        BUMP POINTER TO START OF NUMBER.
         BAL   R1,PP100            EDIT AND CONVERT RETPD.
         LA    R1,DALRETPD
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR        PLACE RETPD VALUE IN TEXT UNIT.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSALLOC *************************
         SPACE 1
BB120    EQU   *
         LA    R7,DSALLOC-PARMLIST
         LA    R14,DSALLOC
         LA    R15,L'DSALLOC
         BAL   R1,PP000            GET DSALLOC LENGTH.
         LTR   R15,R15
         BNP   QQ000               ERROR
         CLC   DSALLOC,=CL5'TRK '  CHECK FOR TRACK ALLOCATION.
         BNE   BB122
         LA    R1,DALTRK
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB122    EQU   *
         CLC   DSALLOC,=CL5'CYL '  CHECK FOR CYLINDER ALLOCATION.
         BNE   BB124
         LA    R1,DALCYL
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
         B     BB130
         SPACE 1
BB124    EQU   *
         BAL   R1,PP100            CHECK FOR AVERAGE BLOCK ALLOCATION.
         LA    R1,DALBLKLN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSPRI ***************************
         SPACE 1
BB130    EQU   *
         LA    R7,DSPRI-PARMLIST
         LA    R14,DSPRI
         LA    R15,L'DSPRI
         BAL   R1,PP000            GET DSPRI LENGTH.
         LTR   R15,R15
         BNP   QQ000
         BAL   R1,PP100            CHECK FOR PRIMARY QUANTITY.
         LA    R1,DALPRIME
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSSEC ***************************
         SPACE 1
BB140    EQU   *
         LA    R7,DSSEC-PARMLIST
         LA    R14,DSSEC
         LA    R15,L'DSSEC
         BAL   R1,PP000            GET DSSEC LENGTH.
         LTR   R15,R15
         BNP   BB150
         BAL   R1,PP100            CHECK FOR SECONDARY QUANTITY.
         LA    R1,DALSECND
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSDIR ***************************
         SPACE 1
BB150    EQU   *
         LA    R7,DSDIR-PARMLIST
         LA    R14,DSDIR
         LA    R15,L'DSDIR
         BAL   R1,PP000            GET DSDIR LENGTH.
         LTR   R15,R15
         BNP   BB160
         BAL   R1,PP100            CHECK FOR DIRECTORY QUANTITY.
         LA    R1,DALDIR
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,3
         STCM  R15,B'0111',S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+3       NEXT TEXT UNIT.
         SPACE 1
* DSRLSE **************************
         SPACE 1
BB160    EQU   *
         LA    R7,DSRLSE-PARMLIST
         LA    R14,DSRLSE
         LA    R15,L'DSRLSE
         BAL   R1,PP000            GET DSRLSE LENGTH.
         LTR   R15,R15
         BNP   BB170
         CLC   DSRLSE,=CL8'RLSE '
         BNE   QQ000
         LA    R1,DALRLSE
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* DSCONTIG ************************
         SPACE 1
BB170    EQU   *
         LA    R7,DSCONTIG-PARMLIST
         LA    R14,DSCONTIG
         LA    R15,L'DSCONTIG
         BAL   R1,PP000            GET DSCONTIG LENGTH.
         LTR   R15,R15
         BNP   BB180
         LA    R1,DALSPFRM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLC   DSCONTIG,=CL8'CONTIG '
         BNE   QQ000
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSROUND *************************
         SPACE 1
BB180    EQU   *
         LA    R7,DSROUND-PARMLIST
         LA    R14,DSROUND
         LA    R15,L'DSROUND
         BAL   R1,PP000            GET DSROUND LENGTH.
         LTR   R15,R15
         BNP   BB190
         CLC   DSROUND,=CL8'ROUND '
         BNE   QQ000
         LA    R1,DALROUND
         STH   R1,S99TUKEY
*                                  S99TUNUM SET TO X'0000' BY WORKAREA
*                                      CLEAR.
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUENT         NEXT TEXT UNIT.
         SPACE 1
* $RESERV4 ************************
         SPACE 1
BB190    EQU   *
         LA    R7,$RESERV4-PARMLIST
         CLC   $RESERV4,BLANKS
         BNE   QQ000
         EJECT
* DSBLKSI *************************
         SPACE 1
CC100    EQU   *
         LA    R7,DSBLKSI-PARMLIST
         LA    R14,DSBLKSI
         LA    R15,L'DSBLKSI
         BAL   R1,PP000            GET DSBLKSI LENGTH.
         LTR   R15,R15
         BNP   CC110
         BAL   R1,PP100            CHECK FOR BLKSIZE VALUE.
         CH    R15,=H'32767'       CHECK FOR MAXIMUM BLOCKSIZE.
         BH    QQ000               ERROR.
         LA    R1,DALBLKSZ
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         STH   R15,S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSORG ***************************
         SPACE 1
CC110    EQU   *
         LA    R7,DSORG-PARMLIST
         CLC   DSORG,NULLS
         BNE   CC112
         LA    R1,DALRTORG         SET UP TO HAVE DSORG RETURNED TO US
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         MVC   S99TUPAR(2),NULLS
         LA    R1,S99TUPAR
         ST    R1,WKRTDSRG         SAVE ADDRESS OF PARAMETER FIELD
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+2       NEXT TEXT UNIT.
         B     CC120
CC112    EQU   *
         LA    R14,DSORG
         LA    R15,L'DSORG
         BAL   R1,PP000            GET DSORG LENGTH
         LTR   R15,R15
         BNP   CC120
         LA    R1,DALDSORG
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSORG,=CL8'VSAM '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0008'
         B     CC115
         CLC   DSORG,=CL8'PO   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0200'
         B     CC115
         CLC   DSORG,=CL8'POU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'0300'
         B     CC115
         CLC   DSORG,=CL8'DA   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2000'
         B     CC115
         CLC   DSORG,=CL8'DAU  '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'2100'
         B     CC115
         CLC   DSORG,=CL8'PS   '
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'4000'
         B     CC115
         CLC   DSORG,=CL8'PSU  '
         BNE   QQ000               ERROR
         MVC   S99TUPAR(2),=XL2'4100'
CC115    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSKEYLEN ************************
         SPACE 1
CC120    EQU   *
         LA    R7,DSKEYLEN-PARMLIST
         LA    R14,DSKEYLEN
         LA    R15,L'DSKEYLEN
         BAL   R1,PP000            GET DSKEYLEN LENGTH.
         LTR   R15,R15
         BNP   CC130
         BAL   R1,PP100            CHECK FOR KEYLENGTH QUANTITY.
         CH    R15,=H'255'         CHECK FOR MAX KEYLENGTH.
         BH    QQ000               ERROR
         LA    R1,DALKYLEN
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         STC   R15,S99TUPAR
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSLRECL *************************
         SPACE 1
CC130    EQU   *
         LA    R7,DSLRECL-PARMLIST
         LA    R14,DSLRECL
         LA    R15,L'DSLRECL
         BAL   R1,PP000            GET DSLRECL LENGTH.
         LTR   R15,R15
         BNP   CC140
         LA    R1,DALLRECL
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,2
         CLC   DSLRECL,=CL5'X '    CHECK FOR SPANNED RECORDS.
         BNE   *+14
         MVC   S99TUPAR(2),=XL2'8000'
         B     CC134
         BAL   R1,PP100            CHECK FOR LRECL QUANTITY.
         CH    R15,=H'32767'
         BH    QQ000
         STH   R15,S99TUPAR
CC134    EQU   *
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+2       NEXT TEXT UNIT.
         SPACE 1
* DSRECFM1 THRU DSRECFM8 **********
         SPACE 1
CC140    EQU   *
         LA    R7,DSRECFM1-PARMLIST
         CLC   DSRECFM,BLANKS      CHECK FOR NO INFORMATION
         BE    CC180
         LA    R1,DALRECFM
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         MVI   S99TULNG+1,1
         CLI   DSRECFM1,C' '
         BE    CC144
         CLI   DSRECFM1,C'V'
         BNE   *+12
         OI    S99TUPAR,X'40'
         B     CC144
         CLI   DSRECFM1,C'F'
         BNE   *+12
         OI    S99TUPAR,X'80'
         B     CC144
         CLI   DSRECFM1,C'U'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'C0'
         SPACE 1
CC144    EQU   *
         LA    R7,DSRECFM2-PARMLIST
         CLI   DSRECFM2,C' '
         BE    CC148
         CLI   DSRECFM2,C'B'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'10'
         SPACE 1
CC148    EQU   *
         LA    R7,DSRECFM3-PARMLIST
         CLI   DSRECFM3,C' '
         BE    CC152
         CLI   DSRECFM3,C'S'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'08'
         SPACE 1
CC152    EQU   *
         LA    R7,DSRECFM4-PARMLIST
         CLI   DSRECFM4,C' '
         BE    CC156
         CLI   DSRECFM4,C'T'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'20'
         SPACE 1
CC156    EQU   *
         LA    R7,DSRECFM5-PARMLIST
         CLI   DSRECFM5,C' '
         BE    CC160
         CLI   DSRECFM5,C'M'
         BNE   *+12
         OI    S99TUPAR,X'02'
         B     CC160
         CLI   DSRECFM5,C'A'
         BNE   QQ000               ERROR
         OI    S99TUPAR,X'04'
         SPACE 1
CC160    EQU   *
         LA    R7,DSRECFM6-PARMLIST
         CLI   DSRECFM6,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC164    EQU   *
         LA    R7,DSRECFM7-PARMLIST
         CLI   DSRECFM7,C' '
         BNE   QQ000               ERROR
         SPACE 1
CC168    EQU   *
         LA    R7,DSRECFM8-PARMLIST
         CLI   DSRECFM8,C' '
         BNE   QQ000               ERROR
         SPACE 1
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1       NEXT TEXT UNIT.
         SPACE 1
* DSDCBDS *************************
         SPACE 1
CC180    EQU   *
         LA    R7,DSDCBDS-PARMLIST
         LA    R14,DSDCBDS
         LA    R15,L'DSDCBDS
         BAL   R1,PP000            GET DSDCBDS LENGTH.
         LTR   R15,R15
         BNP   CC190
         LA    R1,DALDCBDS
         STH   R1,S99TUKEY
         MVI   S99TUNUM+1,1
         STH   R15,S99TULNG
         BCTR  R15,0
         EX    R15,MVCPARM
         ST    R5,0(0,R8)          SAVE THIS TEXT UNIT ADDRESS.
         LA    R8,4(0,R8)          NEXT TEXT POINTER.
         LA    R5,S99TUPAR+1(R15)  NEXT TEXT UNIT.
         SPACE 1
* $RESERV5 ************************
         SPACE 1
CC190    EQU   *
         LA    R7,$RESERV5-PARMLIST
         CLC   $RESERV5,BLANKS
         BNE   QQ000
         B     MM000               GO DO REQUEST
         SPACE 3
MVCPARM  MVC   S99TUPAR(0),0(R14)
         EJECT
MM000    EQU   *
         SH    R8,=H'4'            BACK UP TO LAST USED TEXT POINTER.
         OI    0(R8),X'80'         INDICATE END OF LIST.
         LA    R1,REQBKPTR         GET THE BEGINNING OF THIS MESS.
         DYNALLOC
         L     R0,S99RSC           PICK UP REASON CODES.
         LM    R1,R14,SAVE14
         BR    R14
         LTR   R15,R15             CHECK FOR ERRORS.
         BNZ   ZZ000
         L     R1,WKRTNVOL         CHECK FOR VOLSER RETURN REQUEST.
         LTR   R1,R1
         BZ    MM010
         MVC   DSVOLSER,0(R1)      GIVE CALLER INFO.
MM010    EQU   *
         L     R1,WKRTDSRG         CHECK FOR DSORG RETURN REQUEST.
         LTR   R1,R1
         BZ    MM020
         CLC   0(2,R1),=XL2'0008'
         BNE   *+14
         MVC   DSORG,=CL8'VSAM '
         B     MM020
         CLC   0(2,R1),=XL2'0200'
         BNE   *+14
         MVC   DSORG,=CL8'PO   '
         B     MM020
         CLC   0(2,R1),=XL2'0300'
         BNE   *+14
         MVC   DSORG,=CL8'POU  '
         B     MM020
         CLC   0(2,R1),=XL2'2000'
         BNE   *+14
         MVC   DSORG,=CL8'DA   '
         B     MM020
         CLC   0(2,R1),=XL2'2100'
         BNE   *+14
         MVC   DSORG,=CL8'DAU  '
         B     MM020
         CLC   0(2,R1),=XL2'4000'
         BNE   *+14
         MVC   DSORG,=CL8'PS   '
         B     MM020
         CLC   0(2,R1),=XL2'4100'
         BNE   *+14
         MVC   DSORG,=CL8'PSU  '
         B     MM020
         CLC   0(2,R1),=XL2'8000'
         BNE   *+14
         MVC   DSORG,=CL8'IS   '
         B     MM020
         CLC   0(2,R1),=XL2'8100'
         BNE   MM020
         MVC   DSORG,=CL8'ISU  '
MM020    EQU   *
         B     ZZ000
         EJECT
PP000    EQU   *                   ROUTINE TO DETERMINE LENGTH OF
         LTR   R15,R15                 NON-BLANK CHARACTERS IN FIELD.
         BNP   PP030                   AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS FIELD
PP010    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF FIELD.  AT EXIT R15 WILL BE
         CLI   0(R15),C' '             ALTERED TO CONTAIN LENGTH OF
         BNE   PP020                   NON-BLANK CHARACTERS IN FIELD.
         CLR   R15,R14
         BH    PP010
         SR    R15,R15
         B     PP030
PP020    EQU   *
         SLR   R15,R14
         LA    R15,1(0,R15)
PP030    EQU   *
         BR    R1
         SPACE 3
PP100    EQU   *                   ROUTINE TO EDIT EBCDIC NUMERIC
         LTR   R15,R15                 DATA AND, IF VALID, TO CONVERT
         BNP   PP120                   IT TO BINARY.
         ST    R15,DBLWORD             AT ENTRY R1 CONTAINS THE RETURN
         ALR   R15,R14                 ADDRESS.  R14 CONTAINS DATA
PP110    EQU   *                       ADDRESS.  R15 CONTAINS LENGTH
         BCTR  R15,0                   OF DATA.  AT NORMAL EXIT, R15
         CLI   0(R15),C'0'             WILL BE ALTERED TO CONTAIN THE
         BL    QQ000                   BINARY EQUIVALENT OF THE NUMBER.
         CLI   0(R15),C'9'
         BH    QQ000                   AN ABNORMAL EXIT WILL BE TAKEN
         CLR   R15,R14                 TO THE COMMON ERROR ROUTINE IF
         BH    PP110                   DATA IS NOT NUMERIC.
         L     R15,DBLWORD
         BCTR  R15,0
         EX    R15,PACKNUM
         CVB   R15,DBLWORD
PP120    EQU   *
         BR    R1
         SPACE 1
PACKNUM  PACK  DBLWORD,0(0,R14)
         EJECT
QQ000    EQU   *
ZZ000    EQU   *
ZZ010    EQU   *
FREE1    DS    0H
         LM    R1,R14,SAVE14
         BR    R14
SAVE14   DS    14F
WORKLEN  DC    F'1024'
TXPTRSPC DC    A(4*40)             RESERVE SPACE FOR 40 TEXT POINTERS.
NULLS    DC    XL44'00'
         LTORG
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
*
         DS    0F
TABCOUNT DC    F'0'
ZEROFF   DC    X'000000FF'
P1       DC    PL1'+1'
P3ZERO   DC    PL3'+0'
OUT7HOLD DS    F
L1       DC    F'1'
L10      DC    F'10'
L6       DC    F'6'
L8       DC    F'8'
L44      DC    F'44'
L50      DC    F'50'
*
DIGITS3  DC    X'202020'
THREEPT  DC    X'2020204B'
EDIT4    DC    X'40202120'
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202120'
*
PSPOOL   DC    C'0'
VCOPY    DC    CL8'VCOPY '
ZUSER    DC    CL8'ZUSER '
USERID   DC    CL8' '
SPFMOVE  DC    CL8'MOVE   '
U8       DC    F'8'
*
DSNOTLST DC    CL44'PAN.DATA'
KEEP7PAN DS    F
ADCBADD  DS    F
PACKWK   DS    PL5
PLFPOPEN DC    C'0'
PSPREC   DC    CL133' '
*
PLFZUSER DC    CL8' '
PLFDD1S  DC    C'PLFDD1S'
PLFDD2S  DC    C'PLFDD2S'
ENQFLAG  DC    C'0'
PLFDDS   DS    0H
DDLIB    DS    CL8
DDMEM    DS    CL8
DDSIN    DS    CL8
DDSYSP   DS    CL8
DDPAN2   DS    CL8
DDPAN3   DS    CL8
*
PLFDD1   DS    0H
         DC    CL8'PLFDD10'
         DC    CL8'PLFDD11'
         DC    CL8'PLFDD12'
         DC    CL8'PLFDD13'
         DC    CL8'PLFDD14'
         DC    CL8'PLFDD15'
*
PLFDD2   DS    0H
         DC    CL8'PLFDD20'
         DC    CL8'PLFDD21'
         DC    CL8'PLFDD22'
         DC    CL8'PLFDD23'
         DC    CL8'PLFDD24'
         DC    CL8'PLFDD25'
*
LIBTYPE  DC    C'P'       P=PAN, I=PDS, S=SEQ
REBUILD  DC    C'0'
DSNQUO   DC    CL45' '
DSNNOQ   DC    CL45' '
*
PLFLRECL DC    H'0'       LRECL
*
CAT      DS    4F
CATINFO  DS    CL256
VTOC     DS    4F
VTOCINFO DS    CL140
*
DIRLIM   DC    F'100'
ASTERS   DC    77C'*'
FORMORE  DC    CL77' FOR MORE DIRECTORY ENTRIES, ENTER "MORE"'
ULINE    DC    CL80'++USER '
BUFF80   DC    CL8' COPY I='
BUFFLIB1 DC    CL10'       ,O='
BUFFLIB2 DC    CL62'       '
*
ENAME    DC    CL10' '
*
PANPARMS DS    F
*
PAN1PARM DC    X'80',AL3(HPAN1)
HPAN1    DC    H'39'
         DC    CL7'PLFDD12'
         DC    CL8',PLFDD13'
         DC    CL8',PLFDD10'
         DC    CL8',PLFDD14'
         DC    CL8',PLFDD15'
*
PAN2PARM DC    X'80',AL3(HPAN2)
HPAN2    DC    H'39'
         DC    CL7'PLFDD22'
         DC    CL8',PLFDD23'
         DC    CL8',PLFDD20'
         DC    CL8',PLFDD24'
         DC    CL8',PLFDD25'
*
         CNOP  2,4
OPTLIST  DC    H'8',CL8'COMPRESS'
         CNOP  2,4
DDNMELST DC    H'64',X'0000000000000000'
         DC    X'0000000000000000'
         DC    X'0000000000000000'
         DC    X'0000000000000000'
PLFCPCTL DC    CL8' '
PLFCPRNT DC    CL8' '
         DC    X'0000000000000000'
PLFCPLIB DC    CL8' '
*
H0       DC    H'0'
*
DLINE    DC    CL80'++STATUS      '
*
KLINE    DC    CL80'++DELETE NAME='
*
ELINE    DC    CL80'++UPDATE '
*
RLINE    DC    CL80'++RENAME '
*
NULLPARM DC    X'80',AL3(H0)
*
CLINE    DC    CL10'++CONTROL '
CONTROL  DC    CL70'0000      '
CTAB     DS    0H
ISPLINK  DC    V(ISPLINK)
PLFOPTP  DC    A(SELECTR)
         DC    X'80',AL3(PLFOPT)
SELECTR  DC    CL8'SELECT  '
PLFOPT   DC    CL8'PLFOPT  '
*
L78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
F4       DC    F'4'
L3       DC    F'3'
L77      DC    F'77'
L20      DC    F'20'
L40      DC    F'40'
L4       DC    F'4'
*
DISPLRC  DS    F          RETURN CODE FROM TBDISPL
H1       DC    H'1'
H2       DC    H'2'
H8       DC    H'8'
*
BROWSE   DC    C'BROWSE   '
DISPLAY  DC    C'DISPLAY  '
EDIT     DC    C'EDIT     '
TBADD    DC    C'TBADD    '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL  '
TBEND    DC    C'TBEND    '
TBGET    DC    C'TBGET    '
TBQUERY  DC    C'TBQUERY  '
TBSCAN   DC    C'TBSCAN   '
TBSKIP   DC    C'TBSKIP   '
TBTOP    DC    C'TBTOP    '
TBVCLEAR DC    C'TBVCLEAR '
VDEFINE  DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE  '
VGET     DC    C'VGET     '
VPUT     DC    C'VPUT     '
VREPLACE DC    C'VREPLACE '
*
NOWRITE  DC    C'NOWRITE  '
*
NULLENT  DC    C'()'
*
PROFILE  DC    CL8'PROFILE'
ASIS     DC    CL8'ASIS   '
SHARED   DC    CL8'SHARED '
PLFVARS  DC    C'(PLFPROJ,PLFLIBR,PLFTYPE,'
         DC    C'PLFU,PLFL,PLFM)'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
         DC    C'PANS     '     SELECTION CODE
         DC    C'NAMEPAN '      MEMBER NAME
         DC    C'NEWNAME '      BLANK
         DC    C'PANLINE )'     JOB INFO
         EJECT
*
*              V D E F I N E    KEYWORDS
*
PANS     DC    C'(PANS    )'    SELECTION CODE FOR VDEFINE
NAMEKEY  DS    0CL10            OLD NAME IS USED AS A KEY
NAMEPANP DC    C'(NAMEPAN )'    MEMBER NAME
NEWNAMEP DC    C'(NEWNAME )'    NEW NAME FOR RENAME OPTION
PANLINEP DC    C'(PANLINE )'    MEMBER INFO
PANSPFN  DC    C'(PANSPFN )'    PAN LIB MEMBER NAME
VCTL     DC    C'(VCTL    )'    PAN LIB MEMBER NAME
PANWORK  DC    C'(PANWORK )'    ACTION INFO
PANACT   DC    C'PANWORK   '    ACTION INFO
PANACTN  DC    C'PANUPD    '    ACTION INFO
PCMD     DC    C'(PCMD    )'    MEMBER INFO
SCMD     DC    C'(SCMD    )'    SAVE OR END
*
PLFZ     DC    C'(PLFZ    )'    MENU SELECTION CHARACTER
PLFPROJ  DC    C'(PLFPROJ )'    HIGH LEVEL OF INDEX OF PAN LIB
PLFLIBR  DC    C'(PLFLIBR )'    2 ND LEVEL OF INDEX OF PAN LIB
PLFTYPE  DC    C'(PLFTYPE )'    3 RD LEVEL OF INDEX OF PAN LIB
PLFMEMB  DC    C'(PLFMEMB )'    MEMBER NAME 1
PLFNEWN  DC    C'(PLFNEWN )'    NEWNAME FOR RENAME
PLFDSN2  DC    C'(PLFDSN2 )'    DSN ALL ON ONE LINE
PLFMEM2  DC    C'(PLFMEM2 )'    MEMBER ON ONE LINE
PLFVOL   DC    C'(PLFVOL  )'    VOLSER, IF NOT CATALOGED
PLFPASSW DC    C'(PLFPASSW)'    PASSWORD
PLFCONTR DC    C'(PLFCONTR)'    CONTROL WORD
PLFU     DC    C'(PLFU    )'    LIMIT BY USER CODE
PLFL     DC    C'(PLFL    )'    LIMIT BY LANGUAGE TYPE
PLFM     DC    C'(PLFM    )'    LIMIT BY MEMBER NAME PREFIX
*
         EJECT
*
*              V D E F I N E    STORAGE AREAS
*
SPACES   DC    CL80' '
CZEROS   DC    CL8'00000000'
*
CHAR     DC    C'CHAR    '
*
POSITION DC    CL11' '
*
PLFMENU2 DC    C'PLFMENU2 '
PLFMENU3 DC    C'PLFMENU3 '
PLFMENU1 DC    C'PLFMENU1 '
PLFTABLE DC    C'PLFTABLE'
*
         DS    0D
         DC    C'COMMAND '
CMDDATA  DC    CL40' '
CMDEDIT  DC    CL40' '
*
NAMEPAN  DC    CL10' '
NEWNAME  DC    CL10' '
*
*
PANZ     DC    C' '             MENU SELECTION CHARACTER
PANPROJ  DC    CL8' '           HIGH LEVEL OF INDEX OF PAN LIB
PANLIBR  DC    CL8' '           2 ND LEVEL OF INDEX OF PAN LIB
PANTYPE  DC    CL8'  '          3 RD LEVEL OF INDEX OF PAN LIB
PANMEMB  DC    CL10' '          MEMBER NAME 1
PANNEWN  DC    CL10'  '         NEWNAME FOR RENAME
PANDSN2  DC    CL44' '          DSN ALL ON ONE LINE
PANMEM2  DC    CL10' '          MEMBER ON ONE LINE
PANVOL   DC    CL6' '           VOLSER, IF NOT CATALOGED
PANPASSW DC    CL8' '           PASSWORD
PANCONTR DC    CL4'    '        CONTROL WORD
PANU     DC    CL4' '           LIMIT BY USER CODE
PANL     DC    CL6'  '          LIMIT BY LANGUAGE TYPE
PANM     DC    CL10'       '    LIMIT BY MEMBER NAME PREFIX
*
PLF001   DC    CL8'PLF001 '
PLF002   DC    CL8'PLF002'
PLF003   DC    CL8'PLF003 '
PLF004   DC    CL8'PLF004 '
PLF005   DC    CL8'PLF005 '
PLF006   DC    CL8'PLF006  '
PLF007   DC    CL8'PLF007  '
PLF008   DC    CL8'PLF008  '
PLF009   DC    CL8'PLF009  '
PLF001A  DC    CL8'PLF001A  '
PLF001B  DC    CL8'PLF001B  '
PLF001C  DC    CL8'PLF001C  '
PLF001D  DC    CL8'PLF001D  '
PLF001E  DC    CL8'PLF001E  '
PLF001F  DC    CL8'PLF001F  '
PLF001G  DC    CL8'PLF001G  '
PLF001H  DC    CL8'PLF001H  '
PLF001I  DC    CL8'PLF001I  '
PLF001J  DC    CL8'PLF001J  '
PLF001K  DC    CL8'PLF001K  '
PLF001L  DC    CL8'PLF001L  '
PLF001M  DC    CL8'PLF001M  '
PLF001N  DC    CL8'PLF001N  '
PLF001O  DC    CL8'PLF001O  '
PLF001P  DC    CL8'PLF001P  '
*
SETMSG   DC    CL8'SETMSG  '
*
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
L7       DC    F'7'           ADDRESS OF TABLE TO USE FOR OUTMSG
TABADDRH DS    A              ADDRESS OF TABLE TO USE FOR OUTMSG
DNMADDRH DS    A              ADDRESS OF NAMES TO USE FOR OUTMSG
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
PLFTABFL DS    XL1     DO I HAVE A CURRENT "DISPLAY ACTIVE" TABLE
*
*
TABAREA  DS    0CL77
PLEVEL   DC    CL3' '
         DC    C' '
PUSER    DC    CL4' '
         DC    C' '
PTYPE    DC    CL5' '
         DC    C' '
PSTATUS  DC    CL3' '
         DC    C' '
PDATEM   DC    CL8' '
         DC    C' '
PDATEA   DC    CL8' '
         DC    C' '
PSTMTS   DC    CL8' '
         DC    C' '
PLASTACT DC    CL4' '
         DC    CL30' '  PAD OUT TO 80 BYTES
*
*
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
*
*
WORK     DS    D
HEX      DC    C'0123456789ABCDEF'
*.....................................................................*
*  LIST FORM OF MACROS                                                *
*.....................................................................*
         SPACE
@POPEN CALL ,(ACTION,DDLIB,BACKUP),VL,MF=L
*
*     POPEN PARAMETERS
*
BACKUP   DC    CL8'NO-ENTRY'     MUST BE CL8'BACKUP' IF PROTECTION FIL
         SPACE
@PSRCH   CALL ,(ACTION,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL,MF=L
         SPACE
@PREAD   CALL  ,(ACTION,RECORD,NAME,INCLUDES,COMMENT),VL,MF=L
*
*  SHARED PARAMETER
         DS    0D
         DC    CL8'ACTION'
ACTION   DC    F'0'         RETURN CODE FROM PAN, SHOULD BE SET TO ZERO
         DS    0D
         DC    CL8'RECORD'
RECORD   DS    CL80                STATMENT WILL BE RETURNED HERE
         DS    0D
         DC    CL8'NAME  '
NAME     DC    CL22'A          '   PREAD NAME
         DS    0D
         DC    CL8'INCLUDES'
INCLUDES DS    CL8
         DS    0D
         DC    CL8'COMMENT '
COMMENT  DC    CL52'NO-ENTRY'      NO COMMENTS WANTED, ELSE='COMMENT'
*
@PCLOSE  CALL  ,(ACTION),VL,MF=L
         EJECT
*.....................................................................*
*  CONSTRUCTS FOR PANVALET                                            *
*.....................................................................*
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80               DIR ENTRY RETURNED IN 0-UP FORMAT
DNAME    DS    CL10                NAME LEFT JUSTIFIED
DLEVEL   DS    CL3                 LEVEL NUMBER
DUSER    DS    CL4                 USER CODE
DSECURE  DS    CL1                 SECURITY CODE
DTYPE    DS    CL5                 LANGUAGE TYPE
DSTATUS  DS    0CL3                3 CHAR STATUS
DPRODT   DS    CL1                 P-PROD  T-TEST
DEORDD   DS    CL1                 E-ENABLE  D-DISABLE
DAORDI   DS    CL1                 A-ACTIVE  I-INACTIVE
DDATEM   DS    CL8                 DATE OF LAST MAINTENANCE MM/DD/YY
DDATEA   DS    CL8                 DATE OF LAST ACCESS  MM/DD/YY
DBLOCKS  DS    CL5                 NO. OF BLOCKS
DSTMTS   DS    CL8                 NO. OF STATEMENTS
DLASTACT DS    CL4                 LAST ACTION, 1ST POS. IS * IF PROD
DBYTES   DS    CL2                 NO. BYTES PER STATMENT
DSUBSET  DS    CL4                 NO. OF SUBSETS(SUPERSETS ONLY)
DNAME2   DS    CL10                NAME RIGHT JUSTIFIED
         DS    CL1                 NOT USED
         DS    CL1                 N=NOFORMAT, T=TSO, ELSE BLANK
DINC     DS    CL1                 I=INCLUDE BEING EXPND (EXIT ONLY)
DVERSION DS    XL2                 VER. #(FOR PROTECTION FILES ONLY)
         EJECT
NAME1    DC    CL22'A          '   SEARCH NAME1
NAME2    DC    CL11'Z999999999 '   SEARCH NAME2
**
* NOTE  COMMENTS WILL BE PROCEDED AND ENDED WITH AN *
**
SUBSET   DC    CL27'NO-ENTRY'      NO SUBSETS WANTED, ELSE='SUBSET'
**
*  NOTE  IF SUBSETS ARE REQUESTED THE FOLLOWING WILL BE A DEFINITION OF
*        THE SUBSET DIRECTORY ENTRY
**
SUBENTRY DS    0CL27
SUBNAME  DS    CL11                FORMAT .XXXXXXXXXX X'S = SUBSET NME
         DS    CL1                 BLANK
SUBDATE  DS    CL8                 DATE ATTACHED IN FORM MM/DD/YY
         DS    CL2                 BLANKS
SUBSTMT  DS    CL5                 # OF STMTS IN SUBSET
         SPACE 2
**
* SPECIAL RETURN CODES FROM EACH CALL TO PAM WILL BE RETNED IN ACTION
*   2    NAME NOT ALPHANUMERIC (SAME AS PV002)
*   4    ++INCLUDE NOT FOUND (PREAD ONLY)
*   7    PARAMETER TO LONG (SAME AS PV007)
*   23   NAME NOT FOUND (SAME AS PV023)
*   245  PARAMETER LIST ERROR IN CALL TO PAM
**
NOENTRY  DC    CL92'NO-ENTRY'            NO-ENTRY FOR PANVALET
BLANKS   EQU   NOENTRY+8,80              BLANKS FOR US
         EJECT
*.....................................................................*
*  OVERLAY MAPPING OF DIRECTORY LINE FORMAT ON SCREEN                 *
*.....................................................................*
         SPACE
MDIR     DS    0CL80     MAP FOR NICE FORMATTED DIR ENTRY
MDNAME   DS    CL10,CL1
SCANSTRT EQU   *
MDLEVEL  DS    CL3,CL1
MDTYPE   DS    CL5,CL1
MDUSER   DS    CL4,CL1
MDSECURE DS    CL1,CL1
MDSTATUS DS    CL3,CL1
MDMAINT  DS    CL8,CL1
MDACCES  DS    CL8,CL1
MDSTMTS  DS    CL8,CL2
MDBLOCKS DS    CL5,CL1
SCANLEN  EQU   *-SCANSTRT
MDLSTPRD DS    CL1
MDLSTACT DS    CL8
CAPSONLY DC    CL64' '
         DC    CL10' ',C'.<(+&&',CL9' ',C'!$*);^-/'
         DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'                '
         DC    CL16' ABCDEFGHI      '
         DC    CL16' JKLMNOPQR      '
         DC    CL16'  STUVWXYZ      '
         DC    CL16'0123456789      '
         TITLE 'PLF - DCB S'
PANLIST  DCB   DDNAME=PANLIST,DSORG=PS,MACRF=(GM,PM)
SYSIN    DCB   DDNAME=PANIN,DSORG=PS,MACRF=(GM,PM)
LIBRDCB  DCB   DDNAME=PANIN,DSORG=PS,MACRF=(GM,PM)
MEMBRDCB DCB   DDNAME=PANIN,DSORG=PS,MACRF=(GM,PM)
         TITLE 'PLF - DYNAMIC ALLOCATION WORK AREA'
PARMLIST DS    0H
DDNAME   DS    CL8
DSNAME   DS    CL44
DSMEMBER DS    CL8
PASSWORD DS    CL8
DSSTATUS DS    CL8
DSNDISP  DS    CL8
DSADISP  DS    CL8
DSUNIT   DS    CL8
$RESERV1 DS    CL8
DSVOLSER DS    CL6
$RESERV2 DS    CL40
DSVOLREF DS    CL44
DSFREE   DS    CL8
DSLABEL  DS    CL4
DSINOUT  DS    CL4
$RESERV3 DS    CL16
DSPWDLBL DS    CL8
DSDATE   DS    CL12
DSALLOC  DS    CL5
DSPRI    DS    CL6
DSSEC    DS    CL6
DSDIR    DS    CL5
DSRLSE   DS    CL8
DSCONTIG DS    CL8
DSROUND  DS    CL8
$RESERV4 DS    CL24
DSBLKSI  DS    CL5
DSORG    DS    CL8
DSKEYLEN DS    CL3
DSLRECL  DS    CL5
DSRECFM  DS    0CL8
DSRECFM1 DS    C
DSRECFM2 DS    C
DSRECFM3 DS    C
DSRECFM4 DS    C
DSRECFM5 DS    C
DSRECFM6 DS    C
DSRECFM7 DS    C
DSRECFM8 DS    C
DSDCBDS  DS    CL44
$RESERV5 DS    CL24
PARMEND  EQU   *
         SPACE 3
WORKAREA DS    0H
         DS    18F
REQBKPTR DS    F
WKRTNVOL DS    F
WKRTDSRG DS    F
         DS    F
DBLWORD  DS    D
         DS    4F
DYNWORK  DS    XL1024
         TITLE 'PLF - IBM DSECTS'
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
DSCB1    DSECT
IECSDSL1 EQU   *                   FORMAT 1 DSCB
IECSDSF1 EQU   IECSDSL1
DS1DSNAM DS    CL44                DATA SET NAME
DS1FMTID DS    CL1                 FORMAT IDENTIFIER
DS1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
DS1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER
DS1CREDT DS    XL3                 CREATION DATE
DS1EXPDT DS    XL3                 EXPIRATION DATE
DS1NOEPV DS    XL1                 NUMBER OF EXTENTS ON VOLUME
DS1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
DS1SYSCD DS    CL13                SYSTEM CODE
DS1REFD  DS    XL3                 DATE LAST REFERENCED           @01C
         DS    XL4                 RESERVED                   @G60ASBJ
DS1DSORG DS    XL2                 DATA SET ORGANIZATION
*
*                    FIRST BYTE OF DS1DSORG
DS1DSGIS EQU   X'80'               IS - INDEXED SEQUENTIAL         @L1A
*                                  ORGANIZATION
DS1DSGPS EQU   X'40'               PS - PHYSICAL SEQUENTIAL        @L1A
*                                  ORGANIZATION
DS1DSGDA EQU   X'20'               DA - DIRECT ORGANIZATION        @L1A
DS1DSGCX EQU   X'10'               CX - BTAM OR QTAM LINE GROUP    @L1A
*        EQU   X'08'               RESERVED                        @L1A
*        EQU   X'04'               RESERVED                        @L1A
DS1DSGPO EQU   X'02'               PO - PARTITIONED ORGANIZATION   @L1A
DS1DSGU  EQU   X'01'               U - UNMOVABLE, THE DATA         @L1A
*                                  CONTAINS LOCATION DEPENDENT
*                                  INFORMATION
*
*                     SECOND BYTE OF DS1DSORG
DS1DSGGS EQU   X'80'               GS - GRAPHICS ORGANIZATION      @L1A
DS1DSGTX EQU   X'40'               TX - TCAM LINE GROUP            @L1A
DS1DSGTQ EQU   X'20'               TQ - TCAM MESSAGE QUEUE         @L1A
*        EQU   X'10'               RESERVED                        @L1A
DS1ACBM  EQU   X'08'               ACCESS METHOD CONTROL BLOCK     @L1A
DS1DSGTR EQU   X'04'               TR - TCAM 3705                  @L1A
*        EQU   X'02'               RESERVED                        @L1A
*        EQU   X'01'               RESERVED                        @L1A
*                                  RESERVE THE VSAM DSORG BIT:    @L2A
DS1ORGAM EQU   X'08'               DSORG+1: 08 - VSAM DATA SET    @L2A
DS1RECFM DS    XL1                 RECORD FORMAT
DS1OPTCD DS    XL1                 OPTION CODE
*                                  THE FOLLOWING SETTINGS APPLY   @L2A
*                                  ONLY WHEN DS1ORGAM IS ON:      @L2A
*                                  -VSAM OPTCD FIELD ASSIGNMENTS- @L2A
DS1OPTIC EQU   X'80'               OPTCD: 80 - D/S IN AN ICF CTLG @L2A
DS1OPTBC EQU   X'40'               OPTCD: 40 - ICF CATALOG        @L2A
*                                  OPTCD: 20-01 RESERVED, UNUSED  @L2A
*                                                                 @L2A
DS1BLKL  DS    XL2                 BLOCK LENGTH
DS1LRECL DS    XL2                 RECORD LENGTH
DS1KEYL  DS    XL1                 KEY LENGTH
DS1RKP   DS    XL2                 RELATIVE KEY POSITION
DS1DSIND DS    XL1                 DATA SET INDICATORS
DS1IND80 EQU   X'80'               LAST VOLUME ON WHICH A DATA@G60ASBJ
*                                  SET RESIDES                @G60ASBJ
DS1IND40 EQU   X'40'               DATA SET IS RACF DEFINED   @G60ASBJ
DS1IND20 EQU   X'20'               BLOCK LENGTH IS A MULTIPLE @G60ASBJ
*                                  OF 8 BYTES                 @G60ASBJ
DS1IND10 EQU   X'10'               PASSWORD IS REQUIRED TO    @G60ASBJ
*                                  READ OR WRITE OR BOTH-SEE  @G60ASBJ
*                                  DS1IND04                   @G60ASBJ
DS1IND08 EQU   X'08'               RESERVED                   @G60ASBJ
DS1IND04 EQU   X'04'               IF DS1IND10 IS 1 THEN IF   @G60ASBJ
*                                  DS1IND04 IS . . .          @G60ASBJ
*                                  1-PASSWORD REQUIRED TO     @G60ASBJ
*                                  WRITE BUT NOT TO READ      @G60ASBJ
*                                  0-PASSWORD REQUIRED TO     @G60ASBJ
*                                  WRITE AND TO READ          @G60ASBJ
DS1IND02 EQU   X'02'               DATASET OPENED FOR OTHER   @G60ASBJ
*                                  THAN INPUT SINCE LAST      @G60ASBJ
*                                  BACKUP COPY MADE.          @G60ASBJ
DS1DSCHA EQU   DS1IND02            SAME USE AS BIT DS1IND02   @G60ASBJ
DS1IND01 EQU   X'01'               SECURE CHECKPOINT DATA SET     @01C
DS1CHKPT EQU   DS1DSIND            SAME AS DS1IND01               @01A
DS1SCALO DS    XL4                 SECONDARY ALLOCATION
DS1LSTAR DS    XL3                 LAST USED TRACK AND BLOCK ON TRACK
DS1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
DS1EXT1  DS    XL10                FIRST EXTENT DESCRIPTION
*        FIRST BYTE                EXTENT TYPE INDICATOR
*        SECOND BYTE               EXTENT SEQUENCE NUMBER
*        THIRD - SIXTH BYTES       LOWER LIMIT
*        SEVENTH - TENTH BYTES     UPPER LIMIT
DS1EXT2  DS    XL10                SECOND EXTENT DESCRIPTION
DS1EXT3  DS    XL10                THIRD EXTENT DESCRIPTION
DS1PTRDS DS    XL5                 POSSIBLE PTR TO A FORMAT 2 OR 3 DSCB
DS1END   EQU   *
         END
//LKED1    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM1),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.PROD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.ASQCCLD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  INCLUDE SYSLIB(PAM,ISPLINK)
  NAME PLF(R)
/*
//ASM2    EXEC PGM=IEV90,REGION=2048K,
//        PARM='OBJECT,NODECK,TERM,XREF(FULL)'
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DISP=SHR,DSN=SYS1.AMODGEN
//SYSUT1   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT2   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSUT3   DD  SPACE=(CYL,(5,5)),
//             UNIT=SYSDA
//SYSTERM  DD  SYSOUT=*
//SYSPRINT DD  SYSOUT=*
//SYSPUNCH DD  DUMMY
//SYSLIN   DD   DSN=&&OBJSET,UNIT=SYSDA,SPACE=(CYL,(5,5)),
//             DISP=(MOD,PASS)
//SYSIN    DD  *
         PRINT NOGEN
*
*   GENERAL PURPOSE SPF PANVALET EXIT
*   ORIGINALLY   PANAEXIT5 ON PANVALET
*
*   UPDATED BY J. OLEKSIW FOR SPF 09/81
*
*   DOCUMENTATION:   PANVALET EXTENDED FEATURES AND ADVANCED TECHNIQUES
*                    PAGES 1-11
*
PANEXIT  CSECT
         USING *,12,6
         STM   R14,R12,12(R13)    SAVE INPUT REGS
         LR    R12,R15            SET BASE REGISTER
         LA    R2,1
         LA    R6,4095(2,12)      SET SECOND BASE
         LR    R3,R1              SAVE PARM REGISTER
         GETMAIN R,LV=80          GET MY SAVE AREA ADDRESS
         LR    R11,R1             SAVE GETMAIN POINTER
         ST    R13,4(R11)         STORE BACKWARDS POINTER
         ST    R11,8(R13)         STORE FORWARD POINTER
         LR    R13,R11
*
*                 GET ADDRESSIBILITY TO PANVALET IO AREAS
*
         USING AIOAREA,R9        IOAREA
         USING AIOCODE,R10       IOCODE
         USING ADIRECT,R11
START1   LM    R9,R11,0(R3)   R1 -> ADDR(PARAMETER LIST)
*
         CLI   PROGRAM,C'1'   IS THIS PAN#1?
         BNE   EXIT           NO, SKIP THE EXIT
*
         CLI   IOCODE,C'1'    CODE 1: ISSUED BEFORE A READ
         BNE   EXIT           EXIT IF NOT EQUAL
*
*********************************************************************
*       IOCODE = 1  ... HE TRIES TO FIGURE OUT THE ACTION REQUIRED  *
*********************************************************************
*
         CLI   FLAG,X'FF'     HAVE WE BEEN THROUGH HERE BEFORE?
         BE    EXIT           YES, GO BYE-BYE, NO ACTION
*
         CLI   FLAG,X'01'     SECOND CARD
         BE    PANUPDLY       YES
*
         MVI   FLAG,X'FF'     SET FLAG .. WE HAVE BEEN HERE
*
*        DETERMINE THE ACTION CODE
*
         CALL  ISPLINK,(VGET,PANWORK,SHARED),VL
         LTR   15,15        GET ACTION: ADD-UPDATE-RETRIEVE-ETC
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,PANACT,F10,ACTION,MOVE),VL
         LTR   R15,R15    COPY IN CURRENT VALUES
         BNZ   ERROR0
*
         CLC   ACTION,=C'PANUPD '      ++UPDATE   0,ALL?
         BE    PANUPD
*
*    "PANUPD" IS THE ONLY ACTION CODE SUPPORTED
*
         B     EXIT
*
         CLC   ACTION,=C'PANADD '      ++ADD?
         BE    PANADD
         CLC   ACTION,=C'PANFORM'      ++FORMAT?
         BE    PANFORMT
         CLC   ACTION,=C'PANRET '      ++WRITE WORK,??
         BE    PANRET
         CLC   ACTION,=C'PANSTAT'      ++STATUS   N,N??
         BE    PANSTAT
         CLC   ACTION,=C'PANREN '      ++RENAME   N,N
         BE    PANREN
         CLC   ACTION,=C'PANOTHR'      GENERAL UNSPECIFIC INTERFACE
         BE    PANOTHER
         CLC   ACTION,=C'PANUSER'      ++USER   N,Y??
         BE    PANUSER
         CLC   ACTION,=C'PANPRNT'      ++WRITE PRINT?
         BE    PANPRINT
         CLC   ACTION,=C'PANCOM '      ++COMMENT  ?
         BE    PANCOM
         LA    R7,ACTERROR             GIVE ERROR MESSAGE
         BAL   R8,SPFPUT
         B     FIN
*     PANVALET NAME OBTAIN
*     REGISTER 8 = BAL REGISTER
*
GETNAME  EQU   *
*   OBTAIN THE PANVALET NAME FROM THE MENU
         CALL  ISPLINK,(VGET,PANNAMEL,SHARED),VL
         LTR   R15,R15       VGET SUCCESSFUL?
         BNZ   ERROR0        IF NOT, GIVE THE USER A MESSAGE
         CALL  ISPLINK,(VCOPY,NAMEPAN,LENNAME,PANNAME,MOVE),VL
         LTR   R15,R15       VCOPY SUCCESSFUL?
         BNZ   ERROR1        IF NOT, TELL USER
         BR    R8            RETURN ... MINI ROUTINE ENDED
*
*     PANVALET RETRIEVE ... GENERATE A ++WRITE WORK COMMAND
*
PANRET   EQU   *
         BAL   R8,GETNAME   GET THE PANVALET NAME
         MVC   BUFFER(13),=C'++WRITE WORK,'
         MVC   BUFFER+13(10),PANNAME    PUT IN NAME
         MVC   IOAREA(30),BUFFER        COPY TO PANWORK AREA
         MVI   COMMFLAG,X'FF'
         MVI   IOCODE,C'A'              SET FOR WRITE WORK
         B     EXIT                     ALL DONE
*
*        PANVALET UPDATE ... GENERATE A ++UPDATE  NAME,0,ALL LINE
*
PANUPD   EQU   *
*
*        FIRST TEST FOR A CONTROL WORD
*
         CALL  ISPLINK,(VGET,VCTL,SHARED),VL
         LTR   R15,R15       VGET SUCCESSFUL?
         BNZ   ERROR0        IF NOT, GIVE THE USER A MESSAGE
         CALL  ISPLINK,(VCOPY,NAMECTL,LENCTL,CONTROL,MOVE),VL
         LTR   R15,R15       VCOPY SUCCESSFUL?
         BNZ   ERROR1        IF NOT, TELL USER
*
         CLC   CONTROL(4),=C'0000'   NO CONTROL WORD?
         BE    PANUPDLY              NOPE, JUST DO THE UPDATE
*
         MVC    IOAREA(10),=C'++CONTROL ' INTO PANVALET WORK AREA
         MVC    IOAREA+10(4),CONTROL      CONTROL WORD VALUE
         MVI   COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         MVI   FLAG,X'01'     INDICATE UPDATE CARD GENERATION
         B      EXIT                     ALL DONE
*
PANUPDLY DS    0H   DO UPDATE ONLY
         BAL    R8,GETNAME            OBTAIN THE PANVALET NAME
         MVC    BUFFER(9),=C'++UPDATE '
         MVC    BUFFER+9(10),PANNAME  INSERT NAME
         LA     R2,BUFFER+9           PUT IN ',0,ALL'
         A      R2,LENNAME
         MVC    0(12,R2),=C',0,ALL       '
         MVC    IOAREA(40),BUFFER     PUT INTO PANVALET WORK AREA
         MVI   FLAG,X'FF'     SET FLAG .. WE HAVE BEEN HERE
         MVI   COMMFLAG,X'FF'
*        MVI    COMMFLAG,X'0F'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET STATUS     GENERATE A ++STATUS  NAME,ENABLE  LINE
*
PANSTAT  EQU   *
         BAL    R8,GETNAME            FIND OUT NAME
         CALL   ISPLINK,(VGET,PANSTATL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,STATPAN,LENSTAT,PANSTATS,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++STATUS '
         MVC    BUFFER+9(10),PANNAME
         LA     R2,BUFFER+9
         A      R2,LENNAME
         MVI    0(R2),C','            INSERT A COMMA
         MVC    1(10,R2),PANSTATS     PUT IN STATUS CODE
         MVC    IOAREA(40),BUFFER     PUT INTO PANVALET WORK AREA
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET RENAME     GENERATE A ++RENAME  OLDNAME,NEWNAME LINE
*
PANREN   EQU   *
         BAL    R8,GETNAME   GETNAME
         CALL   ISPLINK,(VGET,NEWNAMEL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,NEWNAME,LENNEW,PANNEWN,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++RENAME '
         MVC    BUFFER+9(10),PANNAME
         LA     R2,BUFFER+9
         A      R2,LENNAME               FIND WHERE TO PUT NEW NAME
         MVI    0(R2),C','               COMMA BETWEEN NAMES
         MVC    1(10,R2),PANNEWN         NEW NAME
         MVC    IOAREA(50),BUFFER        COPY INTO BUFFER
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET FORMAT     GENERATE A ++FORMAT  NAME,FORMAT
*
PANFORMT EQU   *
         BAL    R8,GETNAME   GETNAME
         CALL   ISPLINK,(VGET,FORMATL,SHARED),VL
         LTR    R15,R15
         BNZ    ERROR0
         CALL   ISPLINK,(VCOPY,PANFORM,F10,FORMAT,MOVE),VL
         LTR    R15,R15
         BNZ    ERROR1
         MVC    BUFFER(9),=C'++FORMAT '
         MVC    BUFFER+9(10),PANNAME
         LA     R2,BUFFER+9
         A      R2,LENNAME               FIND WHERE TO PUT NEW NAME
         MVI    0(R2),C','               COMMA BETWEEN NAMES
         MVC    1(10,R2),FORMAT          NEW NAME
         MVC    IOAREA(50),BUFFER        COPY INTO BUFFER
         MVI    COMMFLAG,X'FF'
         MVI    IOCODE,C'A'              SET FOR UPDATE
         B      EXIT                     ALL DONE
*
*        PANVALET COMMENT    GENERATE A ++COMMENT NAME,COMMENT
*
PANCOM   EQU   *
         BAL   R8,GETNAME    GET THE NAME
         CALL  ISPLINK,(VGET,PANCOML,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         B     PUTCOMM
*
*        PANVALET USER       GENERATE A ++USER OLDUSER,NEWUSER
*
PANUSER  EQU   *
         BAL   R8,GETNAME     GET THE NAME FOR ++USER
         CALL  ISPLINK,(VGET,PANUSERL,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,NUMBPAN,LENNUMB,PANNUMB,MOVE),VL
         LTR   R15,R15       ... GET THE OLD USER NUMBER ...
         BNZ   ERROR3
         CALL  ISPLINK,(VCOPY,NUMBNEW,F10,PANUSERN,MOVE),VL
         LTR   R15,R15       ... GET THE OLD USER NUMBER ...
         BNZ   ERROR3
         MVC   BUFFER(7),=C'++USER '
         MVC   BUFFER+7(10),PANNAME   INSERT NAME
         LA    R2,BUFFER+7     WHERE DO YOU PUT THE NEW USER?
         A     R2,LENNAME
         MVI   0(R2),C','      COMMA BETWEEN USER NOS.
         MVC   1(5,R2),PANNUMB PUT IN OLD NUMBER
         LA    R2,1(R2)        WHERE DO YOU PUT THE NEW USER?
         A     R2,LENNUMB
         MVI   0(R2),C','      COMMA BETWEEN USER NOS.
         MVC   1(5,R2),PANUSERN    INSERT NEW USER NO.
         MVI   COMMFLAG,X'FF'
         MVC   IOAREA(40),BUFFER
         MVI   IOCODE,C'A'
         B     EXIT
*
*        PANVALET PRINT      GENERATE A ++WRITE PRINT,DSNAME
*                            SET FLAGS TO ALLOW PRINT
*
PANPRINT EQU   *
         BAL   R8,GETNAME     GET THE NAME FOR ++USER
         MVC   BUFFER(14),=C'++WRITE PRINT,'
         MVC   BUFFER+14(10),PANNAME
         MVI   IOCODE,C'A'      SET FOR PRINT
         MVI   PRINTFLG,X'FF'   SET PRINT FLAG
         MVC   IOAREA(30),BUFFER
         MVI   COMMFLAG,X'FF'
         B     EXIT
*
*        PANVALET OTHER      JUST GIVE THE DATA LINE TO PANVALET
*
PANOTHER  EQU  *
         CALL   ISPLINK,(VGET,PANOTHRL,SHARED),VL
         LTR   R15,R15
         BNZ   ERROR0
         CALL  ISPLINK,(VCOPY,OTHRPAN,F80,BUFFER,MOVE),VL
         LTR   R15,R15
         BNZ   ERROR3
         MVC   IOAREA(65),BUFFER
         MVI   COMMFLAG,X'FF'
         MVI   IOCODE,C'A'
         B     EXIT
*
*        PANVALET ADD
*
PANADD   EQU   *
*
*    0.  CREATE ADDRESSIBILITY TO SPF VARIABLES
*
         CALL  ISPLINK,(VGET,NAMELIST,SHARED),VL
         LTR   R15,R15
         BNZ     ERROR0      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    1.  GET PANVALET NAME
*
         CALL  ISPLINK,(VCOPY,NAMEPAN,LENNAME,PANNAME,MOVE),VL
         LTR   R15,R15
         BNZ     ERROR1      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    2.  GET PANVALET TYPE
*
         CALL  ISPLINK,(VCOPY,TYPEPAN,LENTYPE,PANTYPE,MOVE),VL
         LTR   R15,R15
         BNZ   ERROR2      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*    3.  GET PANVALET NUMBER
*
         CALL  ISPLINK,(VCOPY,NUMBPAN,LENNUMB,PANNUMB,MOVE),VL
         LTR   R15,R15
         BNZ     ERROR3      NON-NORMAL RETURN CODE ... GO BYE-BYE
*
*  GENERATE THE '++ADD NAME,TYPE,NUMBER ' LINE
*
         MVI   IOAREA,C' '              BLANKOUT IO AREA
         MVC   IOAREA+1(79),IOAREA
*
         MVC   BUILDER(10),PANNAME   PUT IN PANVALET NAME
*
*  R2 WILL POINT TO THE PLACE WHERE THE DATA WILL BE PLACED
*
         LA    R2,BUILDER        FIND END OF NAME
         A     R2,LENNAME        NAME CAN BE UP TO 10 BYTES LONG
*
*        *   GET PANVALET TYPE
*
GETTYPE  EQU   *
*
*        ALLOW TYPES TEXT, CLIST, VSBASIC, AND APL ... CHANGE TO DATA
*
         CLC   PANTYPE,=C'TEXT    '   TEXT?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'CLIST   '   CLIST?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'VSBASIC '   VSBASIC?
         BE    CHANGED                IF SO, CHANGE TO DATA
         CLC   PANTYPE,=C'APL     '   APL?
         BNE   NOCHANGE               IF SO, CHANGE TO DATA
CHANGED  EQU   *
         MVC   PANTYPE,=C'DATA    '   CHANGE TO DATA
         MVC   LENTYPE,=F'4'          RESET LENGTH
NOCHANGE EQU   *
         MVI   0(R2),C','        PUT IN A COMMA
         LA    R2,1(R2)       NOW PUT IN THE DATA TYPE NEXT BYTE
         MVC   0(8,R2),PANTYPE    MOVE TYPE
         A     R2,LENTYPE     PAN TYPE CAN BE UP TO 8 BYTES LONG
ENDTYPE  EQU   *
         MVI   0(R2),C','         PUT IN COMMA AFTER NAME
         LA    R2,1(R2)       BUMP UP POINTER
         CLC   PANTYPE,=C'DATA    '   DO WE PUT ON 'TSO' PARAMETER
         BE    GETNUM
         CLC   PANTYPE,=C'OBJECT  '   DO WE PUT ON 'TSO' PARAMETER
         BE    GETNUM
         MVC   0(4,R2),=C'TSO,'
         LA    R2,4(R2)
GETNUM   EQU   *
         MVC   0(4,R2),PANNUMB      PUT IN PANVALET NUMBER
END      EQU   *
         MVC   IOAREA(41),CARD
         MVI   IOCODE,C'A'
*
EXIT     DS    0H
         LR    R11,R13      SAVE MY SAVE AREA ADDRESS
         L     R13,4(,R13)  PICK UP CALLING SAVE AREA
         FREEMAIN R,LV=80,A=(11)
         LM    R14,R12,12(R13)
         SR    R15,15
         BR    R14
*
CHEKCOM  CLI   COMMFLAG,X'FF'  EXIT IF COMMENT HAS BENN ENTERED
         BE    FIN
         B     EXIT
*
*
PUTCOMM  EQU   *     ENTER A COMMENT IF ONE EXISTS
*
*    4.  GET PANVALET COMMENT
*
         CLI   COMMFLAG,X'FF'    SET FLAG ... DON'T COME BACK
         BE    FIN
         CLC   ACTION,=C'PANUPD '
         BE    EXIT
         CALL  ISPLINK,(VCOPY,COMMPAN,LENCOMM,PANCOMM,MOVE),VL
         LTR   R15,R15
         BNZ   MESSCOMM      NON-NORMAL RETURN CODE ... GO BYE-BYE
         CLC   PANCOMM(2),=C'  '                               '
         BE    MESSCOMM   NO COMMENT,NO WORK
         MVC   BUFFER(10),=C'--COMMENT '   GENERATE ++COMMENT
         MVC   BUFFER+10(10),PANNAME
         LA    R2,BUFFER+10       FIND OUT WHERE TO PUT COMMENT
         A     R2,LENNAME         LENGTH OF PANVALET NAME
         MVI   0(R2),C','         --COMMENT NAME,BLABLB
         MVC   1(52,R2),PANCOMM   INSERT ACTUAL COMMENT
         MVC   IOAREA(65),BUFFER
         MVI   COMMFLAG,X'FF'    SET FLAG ... DON'T COME BACK
         MVI   IOCODE,C'A'
         B     EXIT
*
DISPLAY  EQU   *
         CLC   ACTION,=C'PANADD '
         BE    PANAPR
         CLC   ACTION,=C'PANPRNT'
         BE    DISQUIT
         CLC   ACTION,=C'PANRET '
         BE    PANRPR
         CLC   ACTION,=C'PANUPD '
         BE    PANUPR
         CALL  ISPLINK,(VDISPLAY,SCREEN4),VL
         B     DISQUIT
PANRPR   EQU   *
         CALL  ISPLINK,(VDISPLAY,SCREEN3),VL
         B     DISQUIT
PANUPR   EQU   *
         CALL  ISPLINK,(VDISPLAY,SCREEN2),VL
         B     DISQUIT
PANAPR   EQU   *
         CALL  ISPLINK,(VDISPLAY,SCREEN1),VL
DISQUIT  CALL  ISPLINK,(VDELETE,TEXTLIST),VL
         B     EXIT
*
PRINT    EQU   *
         MVC   MESS5,IOAREA
         LA    R7,MESS5      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'F'
         B     EXIT
*
ERROR0   EQU   *
         C     R15,=F'16'
         BNE   ER0
         MVC   MESSVGET,=C' XLATION ERROR'
         B     ER9
ER0      C     R15,=F'20'
         BNE   ER1
         MVC   MESSVGET,=C'SEVERE ERROR'
         B     ER9
ER1      MVC   MESSVGET,=C'UNKNOWN ERROR'
ER9      LA    R7,MESS0      SET UP FOR PRINT ROUTINE
         TPUT  MESS0,50
FIN      CLI   IOCODE,C'8'     NO ACTION AT END OF JOB
         BE    EXIT
         MVI   IOCODE,C'G'
         B     EXIT
*
ERROR1   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS1A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSNAME,PANNAME
         TPUT  MESS1,50
*        LA    R7,MESS1      SET UP FOR PRINT ROUTINE
*        BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
ERROR2   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS2A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSTYPE,PANTYPE
         LA    R7,MESS2      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
ERROR3   EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESS3A,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSNUMB,PANNUMB
         LA    R7,MESS3      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
MESSCOMM EQU   *
         LA    R1,ERRNUMBR
         AR    R1,R15
         MVC   MESSCA,0(R1)       MOVE IN ERROR FLAG
         MVC   MESSCOMT,PANCOMM
         LA    R7,MESSC      SET UP FOR PRINT ROUTINE
         BAL   R8,SPFPUT
         MVI   IOCODE,C'G'
         B     EXIT
*
SPFPUT   EQU   *
         CLI   PRINTFLG,X'FF'     IS THIS A PRINT REQUEST?
         BE    EXIT               NO CHANGE IF IT IS.
         CLI   TEXT1F,C'1'        HAVE WE INITIALIZED?
         BE    PUT2
PUT1     EQU   *                  PRINT OUT FIRST LINE
         MVI   TEXT1F,C'1'        SET FLAG
*   ESTABLISH ADDRESSIBILITY
         MVC   TEXT1(50),0(R7)
*   COPY CURRENT VALUES
         CALL  ISPLINK,(VDEFINE,NAMETEX1,TEXT1,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX2,TEXT2,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX3,TEXT3,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX4,TEXT4,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         CALL  ISPLINK,(VDEFINE,NAMETEX5,TEXT5,CHAR,F50),VL
         LTR   R15,R15
         BNZ   ERROR1
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT2     CLI   TEXT2F,C'2'    HAVE WE BEEN HERE BEFORE?
         BE    PUT3
         MVI   TEXT2F,C'2'    SET FLAG FOR HERE
         MVC   TEXT2(50),0(R7)
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT3     CLI   TEXT3F,C'3'    HAVE WE BEEN HERE BEFORE?
         BE    PUT4
         MVI   TEXT3F,C'3'    SET FLAG FOR HERE
         MVC   TEXT3(50),0(R7)
         BR    R8            RETURN
*   COPY OUTPUT TEXT INTO BUFFER
PUT4     CLI   TEXT4F,C'4'    HAVE WE BEEN HERE BEFORE?
         BE    PUT5
         MVI   TEXT4F,C'4'    SET FLAG FOR HERE
         MVC   TEXT4(50),0(R7)
         BR    R8            RETURN
         EJECT
PUT5     EQU   *
         MVC   TEXT5(50),0(R7)
         BR    R8            RETURN
         LTORG
TEXT1F   DC    X'0'
TEXT2F   DC    X'0'
TEXT3F   DC    X'0'
TEXT4F   DC    X'0'
*MASK    DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*        DC    X'40404040404040404040'
*
*======================================================================
*      SPF PARAMETERS : DOCUMENTED IN SPF DIALOG MANAGEMENT SVCS      
*      SC34-2036-1                                                    
VREPLACE DC    CL8'VREPLACE'    SPF SERVICE NAME                      
VDELETE  DC    CL8'VDELETE '    SPF SERVICE NAME                      
VDEFINE  DC    CL8'VDEFINE '    SPF SERVICE NAME                      
VDISPLAY DC    CL8'DISPLAY '    SPF SERVICE NAME                      
SCREEN1  DC    CL8'PANXXXAZ'    SPF SCREEN  NAME                      
SCREEN2  DC    CL8'PANXXXUZ'    SPF SCREEN  NAME                      
SCREEN3  DC    CL8'PANXXXRZ'    SPF SCREEN  NAME                      
SCREEN4  DC    CL8'PANOTHRZ'    SPF SCREEN  NAME                      
VCOPY    DC    CL8'VCOPY'       SPF SERVICE NAME                      
VGET     DC    CL8'VGET'        SPF SERVICE NAME                      
CHAR     DC    CL8'CHAR'        TYPE OF DATA FOR VDEFINE              
MOVE     DC    CL8'MOVE  '      MOVE MODE, NOT LOCATE                 
SHARED   DC    CL8'SHARED'      GET VARIABLE FROM SHARED VARIABLE POOL
*
*        VARIABLES FOR CONTROL WORD LOGIC
*
VCTL     DC    CL8'(VCTL)'
NAMECTL  DC    CL8'VCTL'
LENCTL   DC    F'4'
CONTROL  DC    C'0000'
*
*        VARIABLES FOR OTHER USES
*
PANOTHRL DC    C'(OTHRPAN) '    LIST TYPE                             
PANUSERL DC    C'(NUMBPAN,NUMBNEW) '                                  
NEWNAMEL DC    C'(NEWNAME) '                                          
PANNAMEL DC    C'(PANSPFN) '                                          
PANSTATL DC    C'(STATPAN) '                                          
PANCOML  DC    C'(COMMPAN) '                                          
STATPAN  DC    CL8'STATPAN'                                           
NEWNAME  DC    CL8'NEWNAME'                                           
NUMBNEW  DC    CL8'NUMBNEW'                                           
OTHRPAN  DC    CL8'OTHRPAN'                                           
LENSTAT  DC    F'10'                                                  
F80      DC    F'80'                                                  
PANUSERN DC    CL10'          '                                       
PANNEWN  DC    CL10'          '                                       
PANSTATS DC    CL10'          '                                       
NAMEPAN  DC    CL8'PANSPFN'     SPF VARIABLE NAME OF PAN DATASET      
COMMPAN  DC    CL8'COMMPAN'     SPF VAR NAME OF COMMENT               
TYPEPAN  DC    CL8'TYPEPAN'     SPF VAR NAME OF TYPE                  
NUMBPAN  DC    CL8'NUMBPAN'     SPF VAR NAME OF USER NUMBER           
PANFORM  DC    CL8'PANFORM'     SPF VAR NAME OF USER NUMBER           
NAMELIST DC    CL35'(NAMEPAN,COMMPAN,TYPEPAN,NUMBPAN)'                
TEXTLIST DC    CL35'(TEXT1,TEXT2,TEXT3,TEXT4,TEXT5)  '                
NAMETEX1 DC    CL8'(TEXT1) '    OUTPUT VARIABLE NAME                  
NAMETEX2 DC    CL8'(TEXT2) '    OUTPUT VARIABLE NAME                  
NAMETEX3 DC    CL8'(TEXT3) '    OUTPUT VARIABLE NAME                  
NAMETEX4 DC    CL8'(TEXT4) '    OUTPUT VARIABLE NAME                  
NAMETEX5 DC    CL8'(TEXT5) '    OUTPUT VARIABLE NAME                  
PANWORK  DC    C'(PANWORK)'     VGET PARAMETER                        
FORMATL  DC    C'(PANFORM)'     VGET PARAMETER                        
PANACT   DC    CL8'PANWORK'     VCOPY PARAMETER                       
ACTION   DC    CL7'       '     PANVALET ACTION REQUESTED             
TEXT1    DC    50CL1' '         FIRST LINE OF OUTPUT TEXT             
TEXT2    DC    50CL1' '         2ND   LINE OF OUTPUT TEXT             
TEXT3    DC    50CL1' '         3RD   LINE OF OUTPUT TEXT             
TEXT4    DC    50CL1' '         4TH   LINE OF OUTPUT TEXT             
TEXT5    DC    50CL1' '         5TH   LINE OF OUTPUT TEXT             
F10      DC    F'10'            TEN                                   
F50      DC    F'50'            FIFTY                                 
LENNAME  DC    F'10'            LENGTH OF PANNAME                     
LENNEW   DC    F'10'            LENGTH OF PANNAME                     
LENCOMM  DC    F'52'            LENGTH OF COMMENT                     
LENTYPE  DC    F'8'             LENGTH OF TYPE                        
LENNUMB  DC    F'4'             LENGTH OF USER NUMBER                 
FORMAT   DC    CL10'         '  NEW FORMAT CODE                       
PANNUMB  DS    CL4              PANVALET USER NUMBER                  
         DC    C' '             POSITIONAL                            
PANCOMM  DS    CL52             PANVALET COMMENT                      
         DC    C' '             POSITIONAL                            
PANTYPE  DC    CL8'        '    PANVALET TYPE                         
         DC    C' '             POSITIONAL                            
PANNAME  DS    CL10             PANVALET NAME                         
         DC    C' '             POSITIONAL                            
ERRNUMBR DC   C'0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ'                 
*======================================================================
*
FLAG     DC    X'00'        ERROR FLAG
PRINTFLG DC    X'00'        ERROR FLAG
COMMFLAG DC    X'00'        ERROR FLAG
CARD     DC    C'++ADD '
BUILDER  DC    CL35'                                 '
BUFFER   DC    CL80'                                                  '
*
ACTERROR DC    C'ERROR - PAN ACTION UNKNOWN '
MESS0    DC    C'SPF VAR SVCS ERROR CODE:'
MESSVGET DS    CL20
         DC     5CL1' '
*
*
MESS1    DC    C'SPF DSN MISSING:CODE'
MESS1A   DC   2CL1' '
MESSNAME DS    CL10
         DC     5CL1' '
*
MESS2    DC    C'SPF DA TYPE MISSING:CODE'
MESS2A   DC   2CL1' '
MESSTYPE DS    CL10
         DC     5CL1' '
*
MESS3    DC    C'SPF PAN NO. MISSING:CODE'
MESS3A   DC   2CL1' '
MESSNUMB DS    CL4
         DC     5CL1' '
*
*
MESSC    DC    C'MISSING PAN COMMENT:CODE'
MESSCA   DC   2CL1' '
MESSCOMT DS    CL10
         DC     5CL1' '
*
MESS4    DC    C'UNKNOWN SPF-PANVALET ERROR  '
MESS5    DS    CL50
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  1ST BASE REG
R13      EQU   13                  WORK AREA
R14      EQU   14
R15      EQU   15
*
AIOCODE  DSECT
IOCODE   DS    C
PROGRAM  DS    C
RETURN   DS    C
ADIRECT  DSECT
DIRENT   DS    CL80
AIOAREA  DSECT
IOAREA   DS    CL121
         END   PANEXIT
/*
//LKED2    EXEC PGM=HEWLF064,PARM=(XREF,LET,LIST,TERM),
//            COND=(4,LT,ASM2),REGION=1024K
//SYSLIN   DD   DSN=&&OBJSET,DISP=(OLD,DELETE)
//         DD   DDNAME=SYSIN
//SYSLIB   DD   DSN=SYS2.LINKLIB,DISP=SHR
//         DD   DISP=SHR,DSN=TECH.PROD.ISPLOAD
//SYSLMOD  DD   DISP=SHR,DSN=TECH.SERV.ASQCCLD
//SYSUT1   DD   DSN=&&SYSUT1,UNIT=VIO,SPACE=(1024,(50,20))
//SYSTERM  DD   SYSOUT=*
//SYSPRINT DD   SYSOUT=*
//SYSIN    DD   *
  INCLUDE SYSLIB(PAN#1)
  ENTRY   PANO111
  NAME PLFPAN#1(R)
/*
