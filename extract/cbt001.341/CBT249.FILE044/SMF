00000100PROC 0 TYPE(JOB)    /* DOING JOBS UNLESS TYPE(SYS)
00000200 /******************************************************************/
00000300 /*                                                                */
00000400 /* REFER:  SYS2.CMDPROC(SMF)                                      */
00000500 /*                                                                */
00000600 /* DOC:                                                           */
00000700 /*                                                                */
00000800 /*   SMF CLIST EXTRACTS JOB INFORMATION OR SYSTEM INFORMATION     */
00000900 /*   FROM SMF DATASETS AND DISPLAYS SAME.                         */
00001000 /*                                                                */
00001100 /*   CALLING OPTIONS:                                             */
00001200 /*                                                                */
00001300 /*   (EX) SMF 'TYPE(JOB)' OR 'TYPE(J)'   (IS DEFAULT)             */
00001400 /*                                                                */
00001500 /*        PROMPTS FOR WHEN; SYS IF TODAY; DATE(IF TODAY)/TIME;    */
00001600 /*        JOBNAME(S).  IF NO JOBNAMES YOU GET EVERYTHING.         */
00001700 /*                                                                */
00001800 /*   (EX) SMF 'TYPE(SYS)' OR 'TYPE(S)'                            */
00001900 /*                                                                */
00002000 /*        PROMPTS FOR WHEN; SYSTEM (IF TODAY); DATE(IF TODAY)/    */
00002100 /*        TIME; SUPPRESSES ALL JOB-RELATED RECORDS.               */
00002200 /*                                                                */
00002300 /*   (EX) SMF 'TYPE(ANYTHINGELSE)'                                */
00002400 /*                                                                */
00002500 /*        LIKE (JOB) EXCEPT USER IS PROMPTED FOR SUPPRESSION      */
00002600 /*        STRING (SMF RECORDS NOT TO DISPLAY).                    */
00002700 /*                                                                */
00002800 /*   D. THEWLIS, FEBRUARY 13, 1979.                               */
00002810 /*   B. LUM, REVISED FEBRUARY 26, 1979                            */
00002900 /*                                                                */
00003000 /******************************************************************/
00003100 
00003200CONTROL NOMSG NOLIST MAIN
00003300 
00003400 /*   USE SMFBROWSE PROGRAM TO EXTRACT ALL JOB-RELATED SMF RECORDS */
00003500 /*   FROM CURRENT (MVS.XXXX.SMFDATA, SYS1.MANX, SYS1.MANY);       */
00003600 /*   YESTERDAY'S (MVS.SMFDAILY);  WEEKLY (MVS.SMFDATAW);          */
00003610 /*   OR LAST WEEK'S (MVS.SMFLASTW)  SMF COLLECTION / HOLDING      */
00003700 /*   DATASET.                                                     */
00003800 
00003900 /*    ATTN EXIT:                                    */
00004000 
00004100ATTN  +
00004200   GOTO DONE
00004300 
00004400 /*   SELECT TODAY, YESTERDAY, WEEKLY, OR LASTWEEK ACCUMULATION    */
00004500 
00004600GETWHEN: +
00004700WRITE TODAY, YESTERDAY, WEEK, OR LASTWEEK?
00004800READ &WHEN
00004900IF &WHEN NE TODAY AND &WHEN NE YESTERDAY AND &WHEN NE WEEK +
00004910   AND &WHEN NE LASTWEEK AND &WHEN NE T AND &WHEN NE Y +
00005000   AND &WHEN NE W AND &WHEN NE L +
00005100   THEN GOTO GETWHEN
00005200 
00005300 /*   ONLY NEED SYSTEM ID IF DOING CURRENT (TODAY'S) DATA          */
00005400 /*   TO GENERATE MVS.XXXX.SMFDATA AND VOLSER OF PAGE1X            */
00005500 
00005600IF &WHEN NE TODAY AND &WHEN NE T +
00005700   THEN GOTO GETPARM
00005800GETSYS: +
00005900WRITE SYSTEM A158 OR B3031?
00006000READ &SYS
00006100IF &SYS NE A158 AND &SYS NE B3031 +
00006200   THEN GOTO GETSYS
00006300 
00006400 /*   SET UP FILE 'SYSIN' TO CONTAIN PARAMETER CARDS FOR $SMFBRWS  */
00006500 
00006600GETPARM: +
00006700FREE F(SYSIN SMFTPE) A(A)
00006800ATTR A RECFM(F) LRECL(80) BLKSIZE(80)
00006900ALLOC F(SYSIN) NEW SP(1) TRACKS UNIT(SYSVIO) USING(A)
00007000OPENFILE SYSIN OUTPUT
00007100 
00007200 /*   GET TIME AND DATE RANGE AND CONSTRUCT/WRITE PARAMETER CARD   */
00007300 
00007400WRITE ENTER TIME RANGE HHMMHHMM LOW-HIGH OR NULL
00007500READ &TIME
00007600 
00007700 /*   UNLESS DOING WEEKLY OR LASTWEEK, NO NEED FOR DATE            */
00007800 
00007900SET &DATE EQ &STR()
00008000IF &WHEN NE WEEK AND &WHEN NE W +
00008010   AND &WHEN NE LASTWEEK AND &WHEN NE L +
00008100   THEN GOTO SETONLY
00008200 
00008300WRITE ENTER DATE RANGE YYDDDYYDDD LOW-HIGH OR NULL
00008400READ &DATE
00008500 
00008600SETONLY: +
00008700IF &TIME EQ &STR() THEN SET &TIME EQ &STR(00002400)
00008800SET &SYSIN EQ &STR(&TIME &DATE)
00008900PUTFILE SYSIN
00009000 
00009100 /*   IF TYPE(SYS) SPECIFIED, SUPPRESS JOB-RELATED RECORDS AND     */
00009200 /*   BYPASS JOBNAME ACQUISITION.                                  */
00009300 
00009400IF &TYPE NE SYS AND &TYPE NE S +
00009500     THEN GOTO SETQRY
00009600 
00009700SET &SYSIN EQ +
00009800    &STR(04 05 06 14 15 17 18 20 26 34 35 40 62 63 64 67 68 69 255)
00009900PUTFILE SYSIN
00010000GOTO SYSDN
00010100 
00010200 /*   IF TYPE NOT (JOB) ASSUME IT WAS REQUEST FOR SUPPRESSION      */
00010300 /*   CONTROL.  READ SUPPRESSION STRING FROM USER.                 */
00010400 
00010500SETQRY: +
00010600IF &TYPE EQ JOB OR &TYPE EQ J +
00010700    THEN GOTO SETJOB
00010800 
00010900WRITE SPECIFY SUPPRESSION STRING:
00011000READ
00011100SET &SYSIN EQ &STR(&SYSDVAL)
00011200PUTFILE SYSIN
00011300GOTO GETJOB
00011400 
00011500 /*   WRITE NULL TYPE-SUPPRESSION STRING.  IF USER SPECIFIES       */
00011600 /*   JOB NAME, NON-JOB-RELATED RECORDS WILL BE SUPPRESSED         */
00011700 /*   ANYHOW.  IF NOT, GIVE USER EVERYTHING IN RANGE.              */
00011800 
00011900SETJOB: +
00012000SET &SYSIN EQ +
00012100    &STR(01)
00012200PUTFILE SYSIN
00012300 
00012400 /*   GET JOBNAME OR PARTIAL JOB NAME IF ANY AND WRITE STRING.     */
00012500 
00012600GETJOB: +
00012700WRITE ENTER JOB/USER NAME; NAME.... FOR ALL STARTING "NAME"; OR NULL
00012800READ &SYSIN
00012900PUTFILE SYSIN
00013000 
00013100 /*   PROCESSING RECORDS BUILT FOR SMF BROWSE PROGRAM.  NOW        */
00013200 /*   CLOSE IT'S SYSIN FILE AND SETUP THE APPROPRIATE DATASET.     */
00013300 
00013400SYSDN: +
00013500CLOSFILE SYSIN
00013600 
00013700IF &WHEN EQ TODAY OR &WHEN EQ T +
00013800   THEN GOTO SETCUR
00013900IF &WHEN EQ YESTERDAY OR &WHEN EQ Y +
00014000   THEN GOTO SETYES
00014010 
00014020IF &WHEN EQ WEEKLY OR &WHEN EQ W +
00014030   THEN GOTO SETWEEK
00014040 
00014050ALLOC F(SMFTPE) DA('MVS.SMFLASTW') SHR     /* LAST WEEK'S DATA */
00014060GOTO SETCALL
00014070 
00014100SETWEEK: +
00014200ALLOC F(SMFTPE) DA('MVS.SMFDATAW') SHR      /* WEEK'S DATA */
00014300GOTO SETCALL
00014400 
00014500SETYES: +
00014600ALLOC F(SMFTPE) DA('MVS.SMFDAILY') SHR      /* YESTERDAY'S DATA */
00014700GOTO SETCALL
00014800 
00014900SETCUR: +
00015000ALLOC F(SMFTPE) DA('MVS.&SYS..SMFDATA') SHR /* TODAY'S DATA (START) */
00015100 
00015200 /*   NOW INVOKE THE BROWSE PROGRAM:                               */
00015300 
00015400SETCALL: +
00015500CALL 'SYS2.LINKLIB($SMFBRWS)' 'NOMSG'
00015600 
00015700 /*   IF NOT DOING TODAY'S DATA, WE ARE DONE.  IF WE ARE DOING     */
00015800 /*   TODAY'S DATA, WE HAVE TO DO SYS1.MANX AND SYS1.MANY ALSO.    */
00015900 /*   SINCE WE CANNOT CONCATENATE ALLOC'S WITH THE VOL PARAMETER   */
00016000 /*   WE WILL INDIVIDUALLY ALLOCATE MANX AND MANY AND INVOKE THE   */
00016100 /*   BROWSE PROGRAM.  (NOTE THAT THE NON-CURRENT MANX/Y DATASET   */
00016200 /*   IS ALWAYS EMPTY AT THIS SHOP ANYHOW SO THE ORDER DOESN'T     */
00016300 /*   MATTER.                                                      */
00016400 
00016500IF &WHEN NE TODAY AND &WHEN NE T +
00016600   THEN GOTO DONE
00016700 
00016800SET &SYSCHAR EQ &SUBSTR(1:1,&SYS)
00016900 
00017000FREE F(SMFTPE)
00017100ALLOC F(SMFTPE) DA('SYS1.MANX') VOL(PAGE1&SYSCHAR) UNIT(SYSALLDA) SHR
00017200CALL 'SYS2.LINKLIB($SMFBRWS)' 'NOMSG'
00017300 
00017400FREE F(SMFTPE)
00017500ALLOC F(SMFTPE) DA('SYS1.MANY') VOL(PAGE1&SYSCHAR) UNIT(SYSALLDA) SHR
00017600CALL 'SYS2.LINKLIB($SMFBRWS)' 'NOMSG'
00017700 
00017800 /*   ALL DONE AT LAST.  FREE FILES; REALLOCATE SYSIN; TERMINATE.  */
00017900 
00018000DONE: +
00018100FREE F(SYSIN SMFTPE) A(A)
00018200ALLOC F(SYSIN) DA(*)
00018300 
00018400ATTN OFF
