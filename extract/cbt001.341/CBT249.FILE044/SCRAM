00000100PROC 0 RESTART()
00000200WRITE DO YOU WISH A TUTORIAL? Y/N
00000300READ &TUT
00000400IF &TUT EQ N THEN GOTO NOTUT
00000500WRITE SCRAM IS A DATASET DELETION AID.
00000600WRITE IT WILL PROMPT YOU WITH: "PLEASE ENTER THE DATASET PREFIX YOU WISH TO SCAN."
00000700WRITE ENTER THE DATSET PREFIX YOU ARE INTERESTED IN SCANNING FOR.
00000800WRITE THE CLIST WILL THEN WRITE THE NAMES OF ALL CATALOGED DATASETS
00000900WRITE HAVING THAT PREFIX FOLLOWED BY THE MESSAGE: DELETE? Y/N/M
00001000WRITE
00001100WRITE IF YOU RESPOND "Y", THE DATASET WILL BE DELETED. IF IT IS ON A
00001200WRITE VOLUME THAT IS NOT CURRENTLY MOUNTED, ITS NAME WILL BE SCRATCHED
00001300WRITE FROM THE CATALOG. (SYS1 AND SYS2 PREFIXES WILL NOT BE DELETED.)
00001400WRITE
00001500WRITE IF YOU RESPOND "N", THE DATASET WILL NOT BE DELETED.
00001600WRITE
00001700WRITE IF YOU RESPOND "M", THE CLIST WILL DO A "LISTDS" ON THE DATASET
00001800WRITE SO THAT YOU CAN FIND OUT ITS' CREATION DATE, THE VOLUME
00001900WRITE THAT IT RESIDES ON, AND ITS' MEMBERS (IF IT IS A PDS).
00002000WRITE IF IT IS A SEQUENTIAL DATASET, SCRAM WILL DO A "LIST" ON THE
00002100WRITE DATASET. (IF YOU WANT TO INTERRUPT THE "LIST" FUNCTION, HIT PA1.)
00002200WRITE
00002300WRITE IF YOU WANT TO INTERRUPT "SCRAM" (IE, RETURN TO "READY"),
00002400WRITE KEY IN "ENDEND" AT THE TIME YOU GET THE "DELETE? Y/N/M" MESSAGE.
00002500WRITE THE MESSAGE: "CURRENT DATASET *** DATASET NAME ***" WILL APPEAR.
00002600WRITE FOLLOWED BY "READY".
00002700WRITE
00002800WRITE TO RESTART, KEY: SCRAM RESTART(FULLY.QUALIFIED.DATASET.NAME)
00002900WRITE
00003000NOTUT: WRITE PLEASE ENTER THE DATASET PREFIX YOU WISH TO SCAN.
00003100CONTROL NOMSG MAIN
00003200READ &LEVEL
00003300FREE F(IN)
00003400FREE ATTR(A)
00003500ATTR A RECFM(V B A) LRECL(255) BL(3120)
00003600ALLOC FI(IN) SHR NEW SP(1 1) TRACKS BL(255) USING(A) UNIT(SYSVIO)
00003700LISTCAT OUTFILE(IN) L(&LEVEL)
00003800ATTN +
00003900GOTO RETRY
00004000ERROR +
00004100DO
00004200IF &LASTCC EQ 400 THEN +
00004300  DO
00004400    CLOSFILE IN
00004500    EXIT
00004600  END
00004700ELSE +
00004800IF &LASTCC EQ 8 THEN +
00004900  DO
00004950    CONTROL NOMSG
00005000    DEL '&IN' NOSCRATCH
00005100    WRITE &IN REMOVED FROM CATALOG. PACK NOT MOUNTED.
00005200    GOTO GET
00005250    CONTROL MSG
00005300  END
00005400ELSE +
00005500  DO
00005600    ELIST &LASTCC
00005700    CLOSFILE IN
00005800    EXIT
00005900  END
00006000END
00006100SET &RSW EQ NO
00006200IF &RESTART EQ &STR() THEN SET &RSW EQ YES
00006300CONTROL NOFLUSH NOMSG
00006400OPENFILE IN
00006500GET: +
00006600GETFILE IN
00006700IF &SUBSTR(18:&EVAL(&LENGTH(&LEVEL)+17),&IN) NE &STR(&LEVEL) THEN GOTO GET
00006800SET IN EQ &SUBSTR(18:&LENGTH(&IN),&IN)
00006900DO WHILE &RSW NE YES
00007000IF &IN NE &RESTART THEN GOTO GET
00007100SET &RSW EQ YES
00007200END
00007300RETRY: +
00007400WRITE *** &IN ***
00007500WRITE DELETE? Y/N/M. ENTER "ENDEND" TO TERMINATE PROCESSING.
00007600READ &ANS
00007700IF &ANS EQ ENDEND THEN GOTO LAST
00007800IF &ANS EQ N THEN GOTO GET
00007900IF &ANS EQ M THEN +
00008000DO
00008050CONTROL MSG
00008100LISTDS '&IN' HISTORY MEMBERS
00008200CONTROL NOMSG
00008300L '&IN'
00008400CONTROL MSG
00008500GOTO RETRY
00008600END
00008700IF &ANS NE Y THEN +
00008800DO
00008900WRITE INVALID REPLY: &ANS
00009000GOTO RETRY
00009100END
00009200IF &SUBSTR(1:4,&LEVEL) EQ SYS1 OR &SUBSTR(1:4,&LEVEL) EQ SYS2 THEN +
00009300DO
00009400WRITE DELETION OF DATASETS WITH PREFIX OF &SUBSTR(1:4,&LEVEL) NOT SUPPORTED
00009500GOTO GET
00009600END
00009700WRITE DATASET &IN ABOUT TO BE DELETED. ENTER "D" TO DELETE.
00009800READ &DEL
00009900IF &DEL NE D THEN GOTO GET
00010000DEL '&IN'
00010100GOTO GET
00010200LAST: WRITE CURRENT DATASET *** &IN ***
00010300CLOSFILE IN
00010400EXIT
