************************************************************************
*     BY  BOB WIDNEY, COUNTY OF LOS ANGELES, DOWNEY CA.                *
*                   (213) 771-5421                                     *
*   RMF REDUCTION OF TYPE79 FOR WORKING SET STATISTICS EVALUATION      *
************************************************************************
* THIS SAS PROGRAM WAS BORN OF A NEED TO FACILITATE THE EVALUATION OF  *
* AN ONLINE ENVIRONMENT NEEDING STORAGE ISOLATION AND THEN TO HELP IN  *
* THE EVALUATION OF THE STORAGE ISOLATION PARAMETERS.                  *
*                                                                      *
* THE FOLLOWING REPORTS AND PLOTS ARE PRODUCED FOR EACH JOBNAME SPECI- *
* FIED IN THE 'JOBS' DD STREAM: (SEE ALSO JNUMJOB MACRO DISCUSSION)    *
*                                                                      *
*  - LIST OF AVG REAL STORAGE (AVG_RS) AND PAGEIN RATE (PINRT) BY TIME.*
*  - MIN/MAX/MEAN/STDEV OF AVG_RS AND PINRT.                           *
*  - PLOT OF TIME VS. AVG_RS AND PINRT.                                *
*  - PLOT OF SPAG DATA: TIME VS. TOTAL PAGE RATE, DEMAND PAGE RATE,    *
*    UIC AND AFC.                                                      *
*                                                                      *
* IN ORDER TO MAKE THE PLOTS COME OUT OK, SOME RULES MUST BE FOLLOWED  *
* IN GENERATING THE RMF INPUT DATA.                                    *
*                                                                      *
*   1 - THE MONITOR II SESSION MUST BE STARTED ON A 10 MINUTE BOUNDARY.*
*   2 - SINTV(600S), ARD, SPAG, AND RECORD MUST BE SPECIFIED           *
*   3 - IF THE SESSION >MUST< BE MODIFIED LATER, DO SO ON A 10 MINUTE  *
*       BOUNDARY. EVEN THEN THE Y-AXIS ON THE PLOTS MAY BE DISRUPTED,  *
*       BETTER TO NOT MODIFY THE SESSION IF POSSIBLE.                  *
* I HANDLE THIS BY AN AUTOMATIC COMMAND AT OR BEFORE 7:50 AM:          *
*      $TANNN,T=HH.00,                                                 *
*        '$VS,''F RMF,S XX,ARD,SPAG,SINTV(600S),RECORD,STOP(9H)'''     *
*                                                                      *
* OTHER CONSIDERATIONS INCLUDE:
* - //SMFIN  DD STMT:                                                  *
*       RMF TYPE 79 DATA STREAM IN TIME SEQUENCE. >>ONE DATE ONLY<<    *
* - //JOBS   DD *     SPECIFY JOBNAMES DESIRED (UP TO 10)              *
*     JOBNAME1 JOBNAME2 JOBNAME3 ........JOBNAMEA                      *
* - MACRO RMFDATE = DATE OF RMF DATA IN FORMAT WISHED ON REPORTS/PLOTS *
* - MACRO JNUMJOB = J## WHERE ## IS THE NUMBER OF JOBNAMES TO BE       *
*   PICKED OFF THE 'JOBS' DD STREAM. BE SURE TO HAVE AT LEAST THIS     *
*   NUMBER OF JOBNAMES IN THE DATA STREAM. MAX = 10.                   *
* - MACRO SYSID   = SMFID OF RMF DATA DESIRED. NEEDED IN A MULTI-      *
*   SYSTEM ENVIRONMENT AND ALSO FOR THE REPORT/PLOT TITLES.            *
*                                                                      *
* NOTE: TO MINIMIZE THE NUMBER OF OBSERVATIONS OUT OF THE TIME RANGE   *
*       OF THE PLOTS (THE PLTVAXIS MACRO), I ONLY INPUT RMF DATA FROM  *
*       ABOUT 0740 TO 1620. (THIS WILL JUST FILL A PAGE ON THE LIST OF *
*       TIME VS. AVG_RS AND PINRT).                                   *;
    PAGE;
  MACRO RMFDATE 'WEDNESDAY, 16 DECEMBER 1981'%
  MACRO JNUMJOB  J02 %
  MACRO SYSID   '1733'%
  MACRO PLTVAXIS
    VAXIS = 0800 TO 0850 BY 10
            0900 TO 0950 BY 10
            1000 TO 1050 BY 10
            1100 TO 1150 BY 10
            1200 TO 1250 BY 10
            1300 TO 1350 BY 10
            1400 TO 1450 BY 10
            1500 TO 1550 BY 10
            1600%
  SKIP 1;
  OPTIONS LS=132 PS=60 NODATE;
  DATA ARD79  (KEEP = JOBNAME TIME AVG_RS PINRT)
       SPAG79 (KEEP = TIME DMD_PGRT TOT_PGRT AFC UIC);
  ARRAY  LASTEJST(IXJOB) LASTE1-LASTE10;
  ARRAY  LASTPIN (IXJOB) LASTP1-LASTP10;
  ARRAY  LASTPSS (IXJOB) LASTS1-LASTS10;
  ARRAY  JOBNAMES(IXJOB) $ 8 J1-JNUMJOB;
  RETAIN J1-JNUMJOB '.';
  RETAIN LASTE1-LASTE10 0  LASTP1-LASTP10 0  LASTS1-LASTS10 0;
  RETAIN LASTDPGE 0  LASTTPGE 0;
  IF _N_ = 1 THEN
  DO;
    INFILE JOBS;
    INPUT J1-JNUMJOB $;
  END;
  SKIP 1;
  INFILE SMFIN;
  INPUT @2 SMF79RTY PIB1. @;
  IF SMF79RTY ^= 79 THEN DELETE;
  INPUT @11  SMF79SID $4.
        @17  SMF79IST PD4.
        @71  R79RECST PIB2.
        @153 R79NELM  PIB2.
        @155 R79SELM  PIB2. @;
  IF  SMF79SID ^= SYSID  THEN DELETE;
  IF  R79RECST  = 2  THEN GOTO SUBTYPE2;
  IF  R79RECST  = 4  THEN GOTO SUBTYPE4;
  DELETE;
  PAGE;
SUBTYPE2:     ***********  PROCESS SUBTYPE-2 (ARD) RECORDS  **********;
  PTR = 157;
  DO IXSUB = 1 TO R79NELM;
    INPUT @PTR+02 R792JBN  $8.
          @PTR+28 R792ARS  PIB2.
          @PTR+42 R792PSS2 PIB4.
          @PTR+46 R792EJST PIB4.
          @PTR+94 R792PIN  PIB4. @;
    DO OVER JOBNAMES;
      IF R792JBN = JOBNAMES THEN GOTO FOUNDJOB;
      IF JOBNAMES = '.' THEN GOTO BUMP;
    END;
    GOTO BUMP;
FOUNDJOB:
    JOBNAME = JOBNAMES;
    DPIN  = R792PIN  - LASTPIN;
    DEJST = R792EJST - LASTEJST;
    DPSS  = R792PSS2 - LASTPSS;
    IF (DEJST > 0  &  DPSS >= 0)
      THEN AVG_RS = DPSS / DEJST;
      ELSE AVG_RS = 0;
    PINRT = DPIN * 100 / (DEJST * 100 / 976);
    FORMAT AVG_RS PINRT COMMA6.0;
    LASTPIN  = R792PIN;
    LASTEJST = R792EJST;
    LASTPSS  = R792PSS2;
    TIME = SMF79IST / 100;
    FORMAT TIME Z4.0;
    OUTPUT ARD79;
BUMP:
    PTR = PTR + R79SELM;
  END;
  RETURN;

SUBTYPE4:     **********  PROCESS SUBTYPE-4 (SPAG) RECORDS  *********;
  PTR = 157;
  INPUT @PTR    R794CMNI PIB4.
        @PTR+04 R794CMNO PIB4.
        @PTR+16 R794PSPI PIB4.
        @PTR+20 R794PSPO PIB4.
        @PTR+24 R794PRVI PIB4.
        @PTR+28 R794PRVO PIB4.
        @PTR+36 R794VIO  PIB4.
        @PTR+40 R794AFC  PIB2.
        @PTR+42 R794CRI  PIB2. @;
  DPAGE = R794CMNI + R794PRVI;
  TPAGE = DPAGE + R794CMNO + R794PSPI + R794PSPO + R794PRVO + R794VIO;
  DMD_PGRT = (DPAGE - LASTDPGE) / 600;
  TOT_PGRT = (TPAGE - LASTTPGE) / 600;
  LASTDPGE = DPAGE;
  LASTTPGE = TPAGE;
  AFC = R794AFC;
  UIC = R794CRI;
  TIME = SMF79IST / 100;
  FORMAT TIME Z4.0;
  OUTPUT SPAG79;
  RETURN;
  PAGE;
**********************************************************************;
  PROC SORT DATA=ARD79;
       BY JOBNAME TIME;
**********************************************************************;
  PROC PRINT DATA=ARD79 PAGE;
       BY JOBNAME;
       ID TIME;
       VAR   AVG_RS PINRT;
   TITLE RMFDATE          W O R K I N G   S E T    S T A T I S T I C S;
**********************************************************************;
  PROC SUMMARY DATA=ARD79;
       BY JOBNAME;
       VAR AVG_RS PINRT;
       OUTPUT OUT=SUMMARY
       MIN(AVG_RS)  =  MIN_ARS
       MAX(AVG_RS)  =  MAX_ARS
       MEAN(AVG_RS) =  MEAN_ARS
       STD(AVG_RS)  =  STDV_ARS
       MIN(PINRT)  =  MIN_PIN
       MAX(PINRT)  =  MAX_PIN
       MEAN(PINRT) =  MEAN_PIN
       STD(PINRT)  =  STDV_PIN;
**********************************************************************;
  PROC PRINT DATA=SUMMARY;
       BY JOBNAME;
   TITLE1 WORKING SET STATISTICS:  MIN/MAX/MEAN/STDV OF ARS AND PINRT;
   TITLE2 RMFDATE;
**********************************************************************;
  PROC PLOT  DATA=ARD79 NOLEGEND;
       BY JOBNAME;
   TITLE1 W O R K I N G   S E T   S T A T I S T I C S   P L O T
                   A = AVG_RS (FRAMES)       P = PINRT  (PAGES/SEC);
   TITLE2 RMFDATE;
    PLOT TIME * AVG_RS  = 'A'
         TIME * PINRT   = 'P' / OVERLAY
         PLTVAXIS
         HAXIS = 0 TO 600 BY 50
         VREVERSE
         VREF = 0800 0900 1000 1100 1200 1300 1400 1500
         VREFCHAR = '-'
         HREF =  50 100 150 200 250 300 350 400 450 500 550 600
         HREFCHAR = '.';
**********************************************************************;
  PROC PLOT  DATA=SPAG79 NOLEGEND;
    TITLE1 S P A G  PLOT FOR SYSTEM:  SYSID
              U = UIC    A = AFC    D = DMD_PGRT    T = TOT_PGRT;
    TITLE2 RMFDATE;
    PLOT TIME * TOT_PGRT  = 'T'
         TIME * DMD_PGRT  = 'D'
         TIME * UIC       = 'U'
         TIME * AFC       = 'A' / OVERLAY
         PLTVAXIS
         HAXIS = 0 TO 120 BY 10
         VREVERSE
         VREF = 0800 0900 1000 1100 1200 1300 1400 1500
         VREFCHAR = '-'
         HREF =   10  20  30  40  50  60  70  80  90  100  110  120
         HREFCHAR = '.';
