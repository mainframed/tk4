         TITLE 'PDF - FRONTEND FOR ISPF/PDF'
         PRINT NOGEN
**********************************************************************
***                   PDF (ALIAS SPF AND ISPFPROF) COMMAND      TS04002
***   DESC:                                                        ***
***     THIS COMMAND IS A FRONTEND FOR ISPF/PDF.                   ***
***                                                                ***
***   FORMAT:                                                      ***
***        PDF  OPTION  TEST/TESTX/TRACE/TRACEX                    ***
***                                                                ***
***   FUNCTION:                                                    ***
***                                                                ***
***     PDF FIRST FREES DD(ISPPROF), THEN TRIES TO ALLOCATE        ***
***     DD(ISPPROF) TO DSN('&SYSUID..ISPF.PROFILE') WITH        TS04002
***     DISP=(OLD,KEEP,KEEP).  IF IT CANNOT ALLOCATE BECAUSE OF    ***
***     LOCATE ERROR 1708 (NOT FOUND IN CATALOG), IT ALLOCATES THE ***
***     DATASET WITH DISP=(NEW,CATLG,CATLG), UNIT=SYSTSO,          ***
***     DCB=(RECFM=FB,LRECL=80,BLKSIZE=3120,DSORG=PO),             ***
***     SPACE=(TRK,(1,2,10)).  FOR ANY OTHER ALLOCATION ERROR      ***
***     CODE, THE PROGRAM TERMINATES AND GIVES THE NORMAL IKJEFF18 ***
***     ERROR MESSAGES.                                            ***
***                                                                ***
***     PDF MAKES A NEW COPY OF THE COMMAND BUFFER (CBUF) AND THE  ***
***     COMMAND PROCESSOR PARAMETER LIST (CPPL), POINTING THE NEW  ***
***     CPPL TO THE NEW CBUF.  IF THE USER USED 'SPF' RATHER THAN  ***
***     'PDF,' PDF WILL PUT OUT AN INFO MESSAGE SAYING THAT 'SPF'  ***
***     HAS BEEN REPLACED BY 'PDF.'  PDF THEN PLACES THE LITERAL   ***
***     'ISP' INTO THE NEW CBUF BEGINNING AT THE LOCATION OF THE   ***
***     FIRST NONBLANK CHARACTER OF THE COMMAND TEXT.  IF DATASET  ***
***     '&SYSUID..ISPF.PROFILE' WAS ALLOCATED NEW, MESSAGES     TS04002
***     CONCERNING THE NEW DATASET ARE WRITTEN TO THE TERMINAL.    ***
***                                                                ***
***     PDF THEN POINTS R1 TO THE NEW CPPL, AND ATTACHES ISRPCP.   ***
***     IT THEN WAITS FOR ISRPCP TO COMPLETE, DETACHES ISRPCP,     ***
***     FREES STORAGE AND TERMINATES.  IF ALIAS ISPFPROF HAS    TS04002
***     HAS BEEN USED, ISRPCP IS BYPASSED.                      TS04002
***                                                                ***
***   RETURN CODES:                                                ***
***     IF ISP EXECUTES, THE CODE RETURNED IS THE CODE FROM ISP.   ***
***     OTHERWISE THE RETURN CODE IS 12.                           ***
***                                                                ***
***   REGISTER USAGE:                                              ***
***     R0-R3    - USED BY MACROS                                  ***
***     R4-R5    - WORK REGISTERS                                  ***
***     R6-R9    - (UNUSED)                                        ***
***     R10      - LINKAGE REGISTER USED WITH BAL                  ***
***     R11      - BASE OF SP0A1 WORK AREA IN SP-000               ***
***     R12      - BASE REGISTER FOR PROGRAM PDF                   ***
***     R13      - BASE REGISTER FOR AREA OBTAINED IN LINKSAVE,    ***
***                AREA BEGINS WITH REGISTER SAVE AREA             ***
***     R14      - USED BY MACROS                                  ***
***     R15      - USED BY MACROS, RETURN CODE AT END              ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***   MACROS:                                                      ***
***                                                                ***
***     IBM:                                                       ***
***       IKJUPT    - MAP OF THE USER PROFILE TABLE                ***
***       IKJCPPL   - MAP OF THE COMMAND PROCESSOR PARM LIST       ***
***       IEFZB4D0  - MAP OF SVC-99 CONTROL BLOCKS                 ***
***       IKJEFFDF  - MAP OF IKJEFF18 (DAIRFAIL) CONTROL BLOCKS    ***
***       ATTACH    - ATTACH A COMMAND                             ***
***       WAIT      - WAIT FOR AN ECB TO BE POSTED                 ***
***       DETACH    - DETACH A COMMAND                             ***
***       GETMAIN   - GET STORAGE IN SUBPOOL 0                     ***
***       FREEMAIN  - FREE STORAGE IN SUBPOOL 0                    ***
***       TPUT      - WRITE TO THE TERMINAL                        ***
***       TGET      - READ FROM THE TERMINAL                       ***
***                                                                ***
***     GTEL:                                                      ***
***       LINKSAVE  - LINKAGE CONVENTIONS UPON ENTRY, AND OBTAIN   ***
***                   A SMALL AMOUNT OF WORKING STORAGE.           ***
***       EQ$R      - REGISTER EQUATES                             ***
***       LINKBACK  - THE OPPOSITE OF LINKSAVE                     ***
***       GTEDADAT  - CREATE SVC-99 AND IKJEFF18 CONTROL BLOCKS    ***
***       GTEDASET  - INITIALIZE SVC-99 AND IKJEFF18 CONTROL BLKS  ***
***       GTEDAALC  - EXECUTE SVC-99 AND IKJEFF18                  ***
***                                                                ***
***   ASSEMBLER OPTIONS:      RENT                                 ***
***   LINKAGE EDITOR OPTIONS: RENT, REUS                           ***
***                                                                ***
***   CULPRIT:                                                     ***
***     CHUCK HOFFMAN, SYSTEMS PROGRAMMING,                        ***
***     GTE LABORATORIES TECHNICAL COMPUTING CENTER,               ***
***     WALTHAM, MASSACHUSETTS.  617/466-2131.  12/29/82           ***
***                                                                ***
***   MAINTENANCE HISTORY:                                         ***
***                                                                ***
***   1.  USE &SYSUID INSTEAD OF &SYSPREF IN DATASET NAME.      TS04002
***                                                             TS04002
***   2.  IF NAME ISPFPROF IS USED, BYPASS GOING INTO ISPF.     TS04002
***                                                             TS04002
**********************************************************************
         EJECT
**********************************************************************
***  31 JANUARY 1986 - UP ON MVS/SP1.3.3 WITH TSO-E. HAVE PROBLEM  ***
***                    WITH OPT 3.8, DUPLICATE JOBNAMES ON STATUS  ***
***                    AND ENCOUNTER A S30-A ABEND. SUSPECT THIS   ***
***                    CODE. WILL TRY TO FIX ASAP. JIM MARSHALL    ***
***                                                                ***
***                                                                ***
***          MAINLINE                                              ***
***                                                                ***
**********************************************************************
*
PDF      LINKSAVE BASE=12,GETMAIN=YES   LINKAGES PLUS A LITTLE STORAGE
         EQ$R                           REGISTER EQUATES
         USING SP0A0,R13                R13 BASE OF GETMAIN AREA
         ST    R1,CPPLADR               SAVE ADDRESS OF CPPL
*
         BAL   R10,GETSTRGE             GET MORE STORAGE
         USING SP0A1,R11                R11 BASE OF NEW AREA
         MVI   SWITCH1,X'00'            INITIALIZE SWITCH1      TS04002
*
         BAL   R10,CHNGECMD             CREATE ISP COMMAND FROM INPUT
*
         BAL   R10,BILDSN               BILD DSN &SYSUID..ISPF. TS04002
*
         BAL   R10,OLDALLOC                     ALLOCATE DATASET OLD
         CLC   RETCDE,HWZERO                    IF OKAY
         BE    ISP                                GO TO PERFORM ISPF
         LA    R4,OLDRB                         ELSE
         CLC   (S99ERROR-S99RB)(2,R4),LOCERR08  IF NOT 'NOT IN CATLG'
         BNE   RETCDE12                           BRANCH TO END OF PGM
*
         BAL   R10,NEWALLOC                     ELSE ALLOCATE NEW
         CLC   RETCDE,HWZERO                    IF NOT OKAY
         BNE   RETCDE12                           BRANCH TO END OF PGM
         BAL   R10,ALLOCMSG                     WRITE PROFILE MESSAGES
*
ISP      TM    SWITCH1,X'80'            IF 'ISPFPROF' NOT USED  TS04002
         BO    *+8                        THEN                  TS04002
         BAL   R10,ATTCHISP                 PERFORM ISPF PROC   TS04002
         B     ENDING                   GO TO ENDING
*
RETCDE12 MVC   COMPCDE(4),FW12          IF BAD ALLOCATION, SET RC=12
*
ENDING   FREEMAIN R,LV=SP0A1LEN,A=(11)  FREE MAIN STORAGE
         L     R15,COMPCDE              LOAD COMPLETION CODE TO R15
         LINKBACK                       RETURN TO CALLER
         EJECT
**********************************************************************
***                                                                ***
***  GET MAIN STORAGE FOR WORK AREA.                               ***
***                                                                ***
**********************************************************************
GETSTRGE ST    R10,GER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         GETMAIN R,LV=SP0A1LEN        , GET MAIN STORAGE
         LR    R11,R1                   R11 POINTS TO NEW AREA
*
GEEND    L     R10,GER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   CREATE NEW ISP COMMAND BUFFER FROM OLD PDF COMMAND           ***
***                                                                ***
**********************************************************************
CHNGECMD ST    R10,CHR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R4,CPPLADR               R4 POINTS TO CPPL
         MVC   NCPPL(16),0(R4)          COPY CPPL TO NEW CPPL
         LA    R4,NCPPL                 SAVE ADDR OF
         ST    R4,NCPPLADR                OF NEW CPPL
         LA    R4,NCBUF                 PLACE ADDR OF NEW CMMD
         ST    R4,NCPPL+(CPPLCBUF-CPPL)   INTO NEW CPPL
*
         L     R4,CPPLADR               R4 POINTS TO CPPL
         L     R4,(CPPLCBUF-CPPL)(0,R4) R4 POINTS TO OLD CBUF
         LH    R5,0(0,R4)               R5 IS LEN OF OLD CBUF
         BCTR  R5,0                       MINUS ONE FOR EXEC
         EX    R5,CHMVC1                MOVE OLD BUFFER TO NEW
*
         LA    R4,NCBUFTXT-1            R4 PNTS 1 CHAR BEFORE NCBUFTXT
CHLOOP1  LA    R4,1(0,R4)               R4 POINTS TO NEXT CHAR IN TEXT
         CLI   0(R4),X'40'              IF BLANK,
         BE    CHLOOP1                    STAY IN LOOP
*
         CLC   0(8,R4),CHISPROF         IF USER USED 'ISPFPROF' TS04002
         BNE   *+8                        THEN                  TS04002
         OI    SWITCH1,X'80'                TURN ON SWITCH1-0   TS04002
*                                                               TS04002
         CLC   0(3,R4),CHSPF            IF USER DID NOT USE 'SPF'
         BNE   CHOVRLAY                   BRANCH AROUND
         ST    R4,CHR4SAV               ELSE SAVE WORK REGISTER
         BAL   R10,SPFMSG                 PUT OUT SPF INFO MESSAGE
         L     R4,CHR4SAV                 RESTORE WORK REGISTER
CHOVRLAY MVC   0(3,R4),CHISP            MOVE 'ISP' OVER 'PDF' OR 'SPF'
*
CHEND    L     R10,CHR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
*
CHMVC1   MVC   NCBUF(0),0(R4)           MOVE OLD BUFFER TO NEW
         EJECT
**********************************************************************
***                                                                ***
***   BUILD DATASET NAME '&SYSUID..ISPF.PROFILE'                TS04002
***                                                                ***
**********************************************************************
BILDSN   ST    R10,BIR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   PROFDSN(44),BLANK44      CLEAR DSN FIELD
         XR    R4,R4                    CLEAR R4 FOR IC
         L     R5,CPPLADR               ADDR OF CPPL TO R5
         L     R5,(CPPLPSCB-CPPL)(0,R5) R5 POINTS TO PSCB       TS04002
         IC    R4,(PSCBUSRL-PSCB)(0,R5) R4 IS LENGTH OF USERID  TS04002
         BCTR  R4,0                       MINUS ONE FOR EXEC
         EX    R4,BIMVC1                MOVE &SYSUID TO PROFILE TS04002
*
         LA    R5,PROFDSN               R5 IS ADDR OF PROFILE DSN
         AR    R5,R4                    R5 IS ADDR OF LAST CHAR OF PREF
         MVI   1(R5),C'.'               PUT DOT INTO NAME
         MVC   2(12,R5),LOWDSN          MOVE LOW END OF DSN TO NAME
*
         LA    R4,14(0,R4)              R4 IS LENGTH OF PROFILE DSN
         STH   R4,PROFDSLN              SAVE THE LENGTH
*
BIEND    L     R10,BIR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
*
*
BIMVC1   MVC   PROFDSN(0),(PSCBUSER-PSCB)(R5) MOVE &SYSUID TO P TS04002
         EJECT
**********************************************************************
***                                                                ***
*** TRY TO ALLOCATE '&SYSUID..ISPF.PROFILE'   DISP=(OLD,KEEP,KE TS04002
***                                                                ***
**********************************************************************
OLDALLOC ST    R10,OLR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   FRE(@FRLEN),@FR          COPY DATA TO WORK AREA
         GTEDASET FRE,CPPLPTR=NCPPLADR  INITIALIZE WORK DATA
         GTEDAALC FRE,VERB=UN           FREE DD(ISPPROF)
*
         MVC   OLD(@OLLEN),@OL          COPY DATA TO WORK AREA
         GTEDASET OLD,CPPLPTR=NCPPLADR  INITIALIZE WORK DATA
         MVC   OLDDSNAM(44),PROFDSN     MOVE DSN TO WORK DATA
         GTEDAALC OLD,VERB=AL,ERRMSG=NO TRY TO ALLOCATE OLD (NOMSG)
         MVC   RETCDE(2),OLDS99RC+2     MOVE SVC-99 RC TO PROC RETCDE
*
OLEND    L     R10,OLR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
*** TRY TO ALLOCATE '&SYSUID..ISPF.PROFILE' DISP=(NEW,CATLG,CAT TS04002
***                                                                ***
**********************************************************************
NEWALLOC ST    R10,NER10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         GTEDAALC FRE,VERB=UN           FREE DD(ISPPROF)
*
         MVC   NEW(256),@NE             COPY DATA TO WORK AREA
         MVC   NEW+256(@NELEN-256),@NE+256 COPY THE REST OF IT
         GTEDASET NEW,CPPLPTR=NCPPLADR  INITIALIZE WORK DATA
         MVC   NEWDSNAM(44),PROFDSN     MOVE DSN TO WORK DATA
         GTEDAALC NEW,VERB=AL           TRY TO ALLOCATE NEW
         MVC   RETCDE(2),NEWS99RC+2     MOVE SVC-99 RC TO PROC RETCDE
*
NEEND    L     R10,NER10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   PUT OUT INFORMATION MESSAGE ABOUT CREATING NEW PROFILE.      ***
***                                                                ***
**********************************************************************
ALLOCMSG ST    R10,ALR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R4,ALMSG-80              R4 POINTS TO -1 MESSAGE
         LA    R5,4(0,0)                R5 IS LOOP COUNTER
ALLOOP1  LA    R4,80(0,R4)              POINT TO NEXT MESSAGE
         TPUT  (R4),80                  WRITE MESSAGE
         BCT   R5,ALLOOP1               LOOP BACK UP IF MSGS REMAIN
*
         MVC   WKMSGB(80),ALMSGB1       START MESSAGE LINE WITH DSN
         MVC   WKMSGB+23(44),PROFDSN    ADD DSN TO MESSAGE LINE
         LA    R4,WKMSGB+23             R4 POINTS
         AH    R4,PROFDSLN                JUST BEYOND DSN IN MESSAGE
         MVC   0(36,R4),ALMSGB2         COMPLETE MESSAGE LINE WITH DSN
         TPUT  WKMSGB,80                WRITE THE MESSAGE
*
         LA    R4,ALMSGC-80             R4 POINTS TO -1 MESSAGE
         LA    R5,7(0,0)                R5 IS LOOP COUNTER
ALLOOP2  LA    R4,80(0,R4)              POINT TO NEXT MESSAGE
         TPUT  (R4),80                  WRITE MESSAGE
         BCT   R5,ALLOOP2               LOOP BACK UP IF MSGS REMAIN
*
         TGET  ALENTER,1                REQUIRE ENTER KEY
*
ALEND    L     R10,ALR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***   PUT OUT INFO MESSAGE ABOUT SPF NO LONGER BEING SUPPORTED     ***
***                                                                ***
**********************************************************************
SPFMSG   ST    R10,SPR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R4,SPMSGE-80             R4 POINTS TO -1 MESSAGE
         LA    R5,7(0,0)                R5 IS LOOP COUNTER
SPLOOP1  LA    R4,80(0,R4)              POINT TO NEXT MESSAGE
         TPUT  (R4),80                  WRITE MESSAGE
         BCT   R5,SPLOOP1               LOOP BACK UP IF MSGS REMAIN
*
         TGET  SPENTER,1                REQUIRE ENTER KEY
*
SPEND    L     R10,SPR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***                                                                ***
***     ATTACH ISRPCP COMMAND PROCESSOR                            ***
***                                                                ***
**********************************************************************
ATTCHISP ST    R10,ATR10SAV             SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         L     R1,NCPPLADR              LOAD POINTER TO NEW CPPL
         ATTACH EP=ISRPCP,ECB=ISPECB,SF=(E,LISTATT) ATTACH ISRPCP
         ST    R1,ISPTCBAD              SAVE TCB ADDRESS
         WAIT  ECB=ISPECB             , WAIT FOR ISPF TO COMPLETE
         ST    R15,COMPCDE              SAVE COMPLETION CODE
         DETACH ISPTCBAD              , DETACH ISPF
*
ATEND    L     R10,ATR10SAV             RESTORE RETURN ADDRESS
         BR    R10                      RETURN
         EJECT
**********************************************************************
***      DATA CONSTANTS                                            ***
**********************************************************************
CONSTNTS DS    0F
BLANK44  DC    CL44' '                  CLEAR DSN
LOWDSN   DC    CL12'ISPF.PROFILE'       LOWER END OF DSN
@FR      GTEDADAT DDNAM=ISPPROF,UNALC=YES
@OL      GTEDADAT DDNAM=ISPPROF,DSNAM=00,STATS=OLD,NDISP=KEEP,         X
               CDISP=KEEP,CNVRT=YES
@NE      GTEDADAT DDNAM=ISPPROF,DSNAM=00,STATS=NEW,NDISP=CATLG,        X
               CDISP=CATLG,CNVRT=YES,TRK=YES,PRIME=1,SECND=2,DIR=10,   X
               UNIT=SYSTSO,BLKSZ=3120,DSORG=PO,LRECL=80,               X
               RECFM=FB
CHISP    DC    CL3'ISP'                 NEW COMMAND NAME
CHSPF    DC    CL3'SPF'                 SPF COMMAND NAME
CHISPROF DC    CL8'ISPFPROF'            ISPFPROF COMMAND NAME   TS04002
HWZERO   DC    H'0'                     ZERO ERROR CODE
LOCERR08 DC    X'1708'                  LOCATE ERR CODE 08 (NOT CATLG)
FW12     DC    F'12'                    COMPLETION CODE 12
ALMSG    DC    CL80' '
         DC    CL80'                         YOUR ISPF/PDF PROFILE'
         DC    CL80' '
         DC    CL80'No standard ISPF/PDF profile dataset was found for X
               your user id, so one was'
ALMSGB1  DC    CL80'created.  It is named '''
ALMSGB2  DC    CL36''', and is allocated to file ISPPROF.'
ALMSGC   DC    CL80' '
         DC    CL80'Since this is a new profile, you may want to changeX
                some of your ISPF/PDF'
         DC    CL80'parameters (in Option 0).'
         DC    CL80' '
         DC    CL80'***************************************************X
               ***************************'
         DC    CL40'                    Type in a NULL LINE '   TS04002
         DC    CL40'to continue.'                               TS04002
         DC    CL80'***************************************************X
               ***************************'
SPMSGE   DC    CL80'***************************************************X
               ***************************'
         DC    CL80' '
         DC    CL80'       ''SPF'' is no longer supported.  ''PDF'' hasX
                been invoked instead.'
         DC    CL80' '
         DC    CL80'             Type in a NULL LINE to continue into IX
               SPF/PDF'
         DC    CL80' '
         DC    CL80'***************************************************X
               ***************************'
         EJECT
**********************************************************************
***     AREA OBTAINED WITH LINKSAVE GETMAIN=YES,  ADDRESSED BY R13 ***
**********************************************************************
SP0A0    DSECT
MYSAVE   DS    18F                      REGISTER SAVE AREA
CPPLADR DS     F                        ADDRESS OF CPPL
GER10SAV DS    F                        RETURN ADDR SAVE AREA
COMPCDE  DS    F                        FINAL COMPLETION CODE
RETCDE   DS    H                        RETURN CODE FROM PROC BLOCKS
         DS    CL50                     UNUSED
         EJECT
**********************************************************************
***      WORK STORAGE AREA, ADDRESSED BY R11                       ***
**********************************************************************
SP0A1    DSECT
         DS    0D
NCBUF    DS    0CL255                   NEW COMMAND BUFFER
NCBUFLN  DS    H                          LENGTH
NCBUFOFF DS    H                          OFFSET
NCBUFTXT DS    CL251                      TEXT
NCPPLADR DS    A
NCPPL    DS    4F                       NEW COMMAND CPPL
LISTATT  ATTACH EP=ISRPCP,ECB=ISPECB,SF=L  ATTACH LIST FOR ISRPCP
ISPECB   DS    F                        ECB FOR ATTACHED ISPF
ISPTCBAD DS    F                        ISPF TCB ADDRESS
CHR4SAV  DS    F                        WORK REGISTER SAVE AREA
CHR10SAV DS    F                        RETURN ADDRESS SAVE AREA
BIR10SAV DS    F                        RETURN ADDRESS SAVE AREA
OLR10SAV DS    F                        RETURN ADDRESS SAVE AREA
NER10SAV DS    F                        RETURN ADDRESS SAVE AREA
ADR10SAV DS    F                        RETURN ADDRESS SAVE AREA
ALR10SAV DS    F                        RETURN ADDRESS SAVE AREA
ATR10SAV DS    F                        RETURN ADDRESS SAVE AREA
SPR10SAV DS    F                        RETURN ADDRESS SAVE AREA
WKMSGB   DS    CL102                    MESSAGE WITH DSN IN IT
PROFDSLN DS    H                        LENGTH OF DSN OF PROFILE DS
PROFDSN  DS    CL44                     DSN OF PROFILE DATASET
ALENTER  DS    CL1                      RESPONSE FROM ENTER KEY
SPENTER  DS    CL1                      RESPONSE FROM ENTER KEY
FRE      GTEDADAT DDNAM=ISPPROF,UNALC=YES,MAP=ONLY
OLD      GTEDADAT DDNAM=ISPPROF,DSNAM=00,STATS=OLD,NDISP=KEEP,         X
               CDISP=KEEP,CNVRT=YES,MAP=ONLY
NEW      GTEDADAT DDNAM=ISPPROF,DSNAM=00,STATS=NEW,NDISP=CATLG,        X
               CDISP=CATLG,CNVRT=YES,TRK=YES,PRIME=1,SECND=2,DIR=10,   X
               UNIT=SYSTSO,BLKSZ=3120,DSORG=PO,LRECL=80,               X
               RECFM=FB,MAP=ONLY
SWITCH1  DC    B'00000000'              SWITCH1 AREA            TS04002
*                1.......                 0: ISPFPROF IN USE    TS04002
*                .1111111               1-7: NOT USED           TS04002
*                                                               TS04002
SP0A1END DS    0D                       D/W BOUNDRY FOR GETMAIN
SP0A1LEN EQU   SP0A1END-SP0A1           EQUATE FOR LENGTH
         EJECT
**********************************************************************
***      MAPPING DSECTS                                            ***
**********************************************************************
         PRINT NOGEN
         SPACE 3
PSCB     IKJPSCB                        PROT STEP CONTROL BLOCK TS04002
         SPACE 3
CPPL     IKJCPPL                        COMMAND PROCESSOR PARM LIST
         SPACE 3
         IEFZB4D0
         SPACE 3
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES
         END
