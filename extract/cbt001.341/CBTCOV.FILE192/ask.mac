ASK  TITLE 'GSF-ASK TSO CLIST COMMAND PROCESSOR'
*
*        07MAY74, GENE CZARCINSKI, NASA/GSFC, CODE 531.
*
         MACRO
&NAME    PROLOG &SHORT
         LCLC  &NM
&NM      SETC  '&NAME'
         AIF   ('&SHORT' EQ '').CONT
&NM      SETC  '&SHORT'
.CONT    ANOP
&NAME    CSECT                    DEFINE CONTROL SECTION
         SAVE  (14,12),,*         SAVE REGISTERS
         USING &NAME,R15          DEFINE BASE REGISTER
         LR    R2,R13             CHAIN SAVE AREAS
         LA    R13,&NM.SAVE
         ST    R2,4(R13)
         ST    R13,8(R2)
         DROP  R15                RE-DEFINE BASE REGISTERS
         USING &NM.SAVE,R13
         B     &NM.SAVE+72         BRANCH AROUND SAVE AREA
&NM.SAVE DC    18F'0'         SAVE AREA
         MEND
         MACRO
&NAME    TSOBLKS &DSECTS,&CPPL=,&IOPL=,&PPL=,&DAPL=,&ECB=
.*********************************************************************
.*                                                                   *
.*                      SYMBOLIC PARAMETERS                          *
.*                                                                   *
.*     &DSECTS       - IF DSECT SPECIFIED, TSO DSECTS WILL BE        *
.*                     GENERATED                                     *
.*     &CPPL(1,2)    - 1- REGISTER TO BE USED AS CPPL BASE REGISTER  *
.*                     2- REGISTER CONTAINING CPPL ADDRESS           *
.*     &IOPL(1,2)    - 1- REGISTER TO BE USED AS IOPL BASE REGISTER  *
.*                     2- SYMBOLIC LABEL OF IOPL                     *
.*     &PPL(1,2,3,4) - 1- REGISTER TO BE USED AS PPL BASE REGISTER   *
.*                     2- SYMBOLIC LABEL OF PPL                      *
.*                     3- SYMBOLIC LABEL OF PCL CSECT (NAME FIELD OF *
.*                        IKJPARM MACRO)                             *
.*                     4- SYMBOLIC LABEL WHERE ANSWER LIST ADDRESS   *
.*                        WILL BE PLACED                             *
.*     &DAPL(1,2)      1- REGISTER TO BE USED AS DAPL BASE REGISTER  *
.*                   - 2- SYMBOLIC LABEL OF DAPL                     *
.*     &ECB          - SYMBOLIC LABEL OF ECB                         *
.*                                                                   *
.*********************************************************************
         LCLA  &IER           ERROR INDICTOR
&IER     SETA  0              INITIALIZE ERROR INDICATOR
         AIF   (N'&CPPL EQ 2).CKIOPL
         MNOTE 8,'*** CPPL SUBPARAMETERS INCORRECT'
&IER     SETA  1              SET ERROR INDICATOR
.CKIOPL  AIF   (N'&IOPL EQ 0 OR N'&IOPL EQ 2).CKPPL
         MNOTE 8,'*** IOPL SUBPARAMETERS INCORRECT'
&IER     SETA  1              SET ERROR INDICATOR
.CKPPL   AIF   (N'&PPL EQ 0 OR N'&PPL EQ 4).CKDAPL
         MNOTE 8,'*** CPPL SUBPARAMETERS INCORRECT'
&IER     SETA  1              SET ERROR INDICATOR
.CKDAPL  AIF   (N'&DAPL EQ 0 OR N'&DAPL EQ 2).CKECB
         MNOTE 8,'*** DAPL SUBPARAMETERS INCORRECT'
&IER     SETA  1              SET ERROR INDICATOR
.CKECB   AIF   ('&ECB' NE '').CKERR
         MNOTE 8,'*** ECB PARAMETER MISSING'
&IER     SETA  1              SET ERROR INDICATOR
.CKERR   AIF   (&IER EQ 0).PARMSOK
         MEXIT
.PARMSOK AIF   ('&DSECTS' NE 'DSECT').USING
         IKJCPPL
         SPACE 4
         AIF   ('&IOPL' EQ '').IKJPPL
         IKJIOPL
         SPACE 4
.IKJPPL  AIF   ('&PPL' EQ '').IKJDAPL
         IKJPPL
         SPACE 4
.IKJDAPL AIF   ('&DAPL' EQ '').EJECT
         IKJDAPL
.EJECT   EJECT
&SYSECT  CSECT                CONTINUE USER CSECT
.USING   ANOP
***********************************************************************
*                                                                     *
*                     SET UP TSO CONTROL BLOCKS                       *
*                                                                     *
***********************************************************************
&NAME    LR    &CPPL(1),&CPPL(2)        LOAD CPPL ADDRESS
         USING CPPL,&CPPL(1)            DEFINE CPPL BASE REGISTER
         AIF   ('&IOPL' EQ '').UPPL    SEE IF IOPL REQUESTED
         LA    &IOPL(1),&IOPL(2)
         USING IOPL,&IOPL(1)
.UPPL    AIF   ('&PPL' EQ '').UDAPL
         LA    &PPL(1),&PPL(2)
         USING PPL,&PPL(1)
.UDAPL   AIF   ('&DAPL' EQ '').SETUP
         LA    &DAPL(1),&DAPL(2)
         USING DAPL,&DAPL(1)
.SETUP   ANOP
*--------SET UP UPT, ECT, AND ECB ADDRESSES
         L     R1,CPPLUPT
         AIF   ('&IOPL' EQ '').S1
         ST    R1,IOPLUPT
.S1      AIF   ('&PPL' EQ '').S2
         ST    R1,PPLUPT
.S2      AIF   ('&DAPL' EQ '').S3
         ST    R1,DAPLUPT
.S3      L     R1,CPPLECT
         AIF   ('&IOPL' EQ '').S4
         ST    R1,IOPLECT
.S4      AIF   ('&PPL' EQ '').S5
         ST    R1,PPLECT
.S5      AIF   ('&DAPL' EQ '').S6
         ST    R1,DAPLECT
.S6      LA    R1,&ECB
         AIF   ('&IOPL' EQ '').S7
         ST    R1,IOPLECB
.S7      AIF   ('&PPL' EQ '').S8
         ST    R1,PPLECB
.S8      AIF   ('&DAPL' EQ '').S9
         ST    R1,DAPLECB
.S9      AIF   ('&PPL' EQ '').S10
*--------COMPLETE PPL
         L     R1,=V(&PPL(3))
         ST    R1,PPLPCL
         LA    R1,&PPL(4)
         ST    R1,PPLANS
         L     R1,CPPLCBUF
         ST    R1,PPLCBUF
.*-------DONE
.S10     MEND
         TITLE 'ASK A QUESTION AND GET A YES OR NO REPLY'
ASK      PROLOG
         TSOBLKS DSECT,CPPL=(R12,R1),PPL=(R10,ASKPPL,ASKPCL,ASKANS),   X
               ECB=ASKECB
         LA    R1,ASKPPL          LOAD PARSE CONTROL LIST ADDRESS
         LINK  EP=IKJPARS         PARSE THE OPERANDS
         LTR   R15,R15            TEST RETURN CODE
         BNZ   ERRPARS
         L     R10,ASKANS         LOAD ADDRESS OF ANSWER LIST
         USING ASKPDL,R10         DEFINE BASE REGISTER
WRITE    LH    R0,QUESTION+4      LOAD LENGTH OF STRING
         LTR   R0,R0              TEST FOR ZERO
         BZ    READ               IF ZERO, SKIP WRITE
         L     R1,QUESTION        LOAD ADDRESS OF STRING
         TPUT  (1),(0)            PUT STRING
READ     TGET  ANSWER,4           GET ANSWER
         OC    ANSWER(4),=C'    '    CONVERT TO UPPER CASE
         CLC   ANSWER,=C'YES '    TEST FOR YES
         BE    YES
         CLC   ANSWER,=C'NO  '    TEST FOR NO
         BNE   WRITE
*--------ANSWER IS NO - RC = 4
         LA    R2,4               LOAD RETURN CODE
         B     RLSE
*--------ANSWER IS YES - RC = 0
YES      LA    R2,0               LOAD RETURN CODE
         B     RLSE
*--------PARSE ERROR - RC = 16
ERRPARS  TPUT  ERROR,32           PUT MESSAGE
         LA    R2,16              LOAD RETURN CODE
         B     DONE
*--------RELEASE PARSE STORAGE AND RETURN
RLSE     IKJRLSA ASKANS           RELEASE ANSWER LIST STORAGE
DONE     LR    R15,R2             LOAD RETURN CODE TO REGISTER 15
         L     R13,4(R13)         RESTORE OLD SAVE AREA ADDRESS
         RETURN (14,12),T,RC=(15) RETURN
*--------DATA
         LTORG
ERROR    DC    CL32'UNABLE TO INTERPRET OPERANDS'
ANSWER   DC    CL4' '
ASKECB   DC    F'0'
ASKANS   DC    A(0)
ASKPPL   DC    7A(0)
         REGS
ASKPCL   IKJPARM DSECT=ASKPDL
QUESTION IKJPOSIT QSTRING
         IKJENDP
         END   ASK
