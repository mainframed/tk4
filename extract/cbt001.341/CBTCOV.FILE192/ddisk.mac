         TITLE '     T S O   C O M M A N D   -   D D I S K   '
**********************************************************
*                                                        *
*           P.R.C. TSO COMMAND 'DDISK'                   *
*                                                        *
**********************************************************
DDISK    CSECT
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
         PRINT NOGEN
         SAVE  (14,12),,*
         LR    12,15
         USING DDISK,12
         LA    11,SAVE2
         ST    11,8(13)
         ST    13,4(11)
         LR    13,11
*        OPEN  (PRINT,(OUTPUT))
         L     6,16
         L     6,X'C4'(6)
*        MVC   HDR1+35(1),16(6)
*        MVC   HDR1+27(9),=CL9'MODEL 158'
*        PUT   PRINT,HDR1
         TPUT  HDR1+1,23
         CALL  DATEV,(MDY,HMS)
*        PUT   PRINT,HDR5
*        PUT   PRINT,HDR3
         TPUT  HDR5+1,23
         TPUT  HDR3+1,31
         L     6,16           A(CVT)
         L     6,40(6)        POINTER TO UCB ADDR TABLE
NXTUCB   LH    5,0(6)         GET UCB ADDRESS
         LTR   5,5            IS THIS A HOLE IN THE UCB TABLE
         BZ    UCBHOLE        YES
         CH    5,=X'FFFF'     ANY MORE UCB'S
         BE    FINIS          NO - THAT'S ALL FOLKS
         CLI   18(5),X'20'    IS IT DIRECT ACCESS STORAGE
         BNE   UCBHOLE        NO
         SR    4,4            ZERO THE REGISTER
         IC    4,19(5)        GET THE UNIT TYPE
         MH    4,=H'6'
         LA    4,UNITTYPE(4)  ADDRESS IN TABLE
         MVC   PRUNIT,0(4)    MOVE UNIT TYPE TO PRINT LINE
         CLC   PRADDR(2),13(5)
         BE    *+8
         MVI   LINE,C'0'
         MVC   PRADDR,13(5)   MOVE UNIT ADDRESS TO PRINT LINE
         MVC   PRSER,28(5)    MOVE VOLUME SERIAL TO PRINT LINE
         CLC   PRSER,ZEROES
         BE    UCBHOLE
         SR    7,7
COMPLIST CH    7,LCT
         BNL   ADDLIST
         CH    5,LIST(7)
         BE    UCBHOLE
         LA    7,2(7)
         B     COMPLIST
ADDLIST  LH    7,LCT
         CH    7,LMAX
         BNL   FULL
         STH   5,LIST(7)
         LA    7,2(7)
         STH   7,LCT
FULL     EQU   *
*        PUT   PRINT,LINE     OUTPUT
         TPUT  LINE+1,31
         MVI   LINE,C' '
UCBHOLE  LA    6,2(6)         INCREMENT POINTER
         B     NXTUCB         GET NEXT UCB
FINIS    EQU   *
         B     NOSORT     FORGET THE SORT  (NOW REALLY!)    CCITSO
         MVI   LINE,C' '
*         SORT THE DISKS BY VOLUME SERIAL AND PRINT THEM
         SR    3,3
SORTLOOP LH    4,LIST(3)
         LR    5,3
INLOOP   LH    6,LIST(5)
         CLC   28(6,4),28(6)     COMPARE VOL SER
         BNP   NOSWITCH
         STH   4,LIST(5)
         STH   6,LIST(3)
         LR    4,6
NOSWITCH LA    5,2(5)
         CH    5,LCT
         BL    INLOOP
         LA    3,2(3)
         CH    3,LCT
         BL    SORTLOOP
         LA    2,2
SPACE    EQU   *
*        PUT   PRINT,HDR4
         BCT   2,SPACE
*        PUT   PRINT,HDR2
*        PUT   PRINT,HDR3
         SR    3,3
PR2LOOP  SR    4,4
         LH    5,LIST(3)
         IC    4,19(5)
         SLA   4,2
         LA    4,UNITTYPE(4)
         MVC   PRUNIT,0(4)    MOVE UNIT TYPE TO PRINT LINE
         MVC   PRADDR,13(5)   MOVE UNIT ADDRESS TO PRINT LINE
         MVC   PRSER,28(5)    MOVE VOLUME SERIAL TO PRINT LINE
*        PUT   PRINT,LINE
         LA    3,2(3)
         CH    3,LCT
         BL    PR2LOOP
NOSORT   EQU   *                                             CCITSO
*        CLOSE PRINT
         L    13,4(13)
         SR    15,15
         RETURN (14,12),T,RC=(15)
* PRINT  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,
*              RECFM=FBA,LRECL=121,BLKSIZE=968
LINE     DC    CL4' '
PRSER    DC    CL6' '
         DC    CL6' '
PRADDR   DC    CL3' '
         DC    CL6' '
PRUNIT   DC    CL6' '
BLANK    DC    CL90' '
UNITTYPE DC    CL6'    '      00
         DC    CL6'2311'      01
         DC    CL6'2301'      02
         DC    CL6'2303'      03
         DC    CL6'2302'      04
         DC    CL6'2321'      05
         DC    CL6'2305'      06
         DC    CL6'    '      07
         DC    CL6'2314'      08
         DC    CL6'3330'      09
         DC    CL18'   '      0A - 0C
         DC    CL6'3330-1'    0D
HDR1     DC    CL121'1  DIRECT ACCESS VOLUMES - MACHINE '
HDR2     DC    CL121'1  ALPHABETICAL LISTING'
HDR3     DC    CL121'0  VOL SER     ADDR      UNIT'
HDR4     DC    CL121'0'
         DS    0F
HDR5     DC    CL4' '
MDY      DS    CL8
         DC    CL4' '
HMS      DS    CL8
         DC    CL100' '
ZEROES   DC    2F'0'
LCT      DC    H'0'
LMAX     DC    H'2000'
LIST     DS    1000H
SAVE2    DC    18F'0'
         DS    H
         LTORG
       TITLE   'DATEV - A ROUTINE TO GET THE DATE AND TIME'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*       THIS SUBROUTINE OUTPUTS DATE AND TIME OF DAY WHEN             *
*       CALLED BY THE STATEMENT                                       *
*                                                                     *
*            DATEV (MDY,HMS)                                          *
*                                                                     *
*            MDY - THE FIRST OUTPUT ARGUMENT CONTAINING DATE IN       *
*                   MM/DD/YY FORMAT, WHERE                            *
*                        MM - MONTH OF THE YEAR (2 DECIMAL DIGITS)    *
*                        DD - DAY OF THE MONTH                        *
*                        YY - LAST TWO DIGITS OF THE YEAR             *
*                                                                     *
*            HMS - THE SECOND OUTPUT ARGUMENT CONTAINING TIME IN      *
*                   HH.MM.SS FORMAT, WHERE                            *
*                        HH - HOUR OF THE DAY (2 DECIMAL DIGITS)      *
*                        MM - MINUTES                                 *
*                        SS - SECONDS                                 *
*                                                                     *
*       NOTE - ARGUMENTS MUST EACH BE ABLE TO CONTAIN 8 CHARACTERS    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE  5
DATEV    CSECT
         USING  *,15
         SAVE   (14,12)
       LR    4,1                       ARGLIST ADDRS NOW IN R4
         LA     12,SAVE
         ST     13,4(12)
         ST     12,8(13)
         LR     13,12
         LR     12,15
         DROP   15
         USING  DATEV,12
         TIME  DEC                      MACRO-INST GIVES DATE IN REG1,
*                                       TIME IN REGO FORMATTED 00YYDDDC
*                                       AND HHMMSSTH RESPECTIVELY
         LM     2,3,0(4)                SAVE ARGS
         ST    0,TIMET
         OI    TIMET+3,X'0F'            INSERT DECIMAL SIGN
         MVC   WKAREA(9),PTTRN          SET UP FOR EDITING
         ED    WKAREA(9),TIMET
         MVC   0(8,3),WKAREA+1          SEND TIME TO CALLER
         ST    1,TIMET
         MVO   TEMP,TIMET+1(1)          PREPARE FOR CONVERSION
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK  DATEN+6(2),TEMP+6(2)     FORMAT YY IN DATEN
         CVB    9,TEMP                  CONVERT YY TO BINARY
         STC    9,YEAR
         TM    YEAR,X'03'               LEAP YEAR
         BC      5,*+10                 NO
         MVC   TABLE+4,=H'29'           MODIFY TABLE
         MVC   TEMP,=D'0'               ZERO TEMP
         MVC   TEMP+6(2),TIMET+2        PREPARE TO CONVERT DDD
         CVB   11,TEMP                  CONVERT DDD TO BINARY
         LA    10,2                     INITIALIZE INDEX REG OF TABLE
LOOPDATE SH    11,TABLE(10)             SUBTRACT DAYS OF MONTH(INDEXED)
         BC    12,CHECK                 RESULT ZRO OR NEGATIVE
         LA    10,2(0,10)               INCREMENT INDEX BY 2
         CH    10,=H'26'                ERROR
         BC    12,LOOPDATE              NO
         B     DATERROR
CHECK    AH    11,TABLE(10)
         CVD   11,TEMP                  DD NOW IN PACKED DECIMAL
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK  DATEN+3(2),TEMP+6(2)     DD IN BCD - PLACE IN DATEN
         SRA   10,1                     MONTH NOW IN REG 10
         CVD   10,TEMP                  CONVERT MONTH TO PACKED DECIMAL
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK    DATEN(2),TEMP+6(2)     MM IN BCD - PLACE IN DATEN
         MVC   0(8,2),DATEN             SEND DATE TO CALLER
         B     RESTORE
DATERROR MVC   DATEN(8),=C'ER/R0/R '
RESTORE  L      13,4(13)
         MVI   12(13),X'FF'
         RETURN (14,12)
         LTORG
*
TEMP     DS    D
TIMET    DS    F
TABLE    DS    H
         DC    H'31'
         DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
SAVE     DS     18F
WKAREA   DS    CL9
PTTRN    DC    X'4021214B21214B2121'
DATEN    DC    C'  /  /  '
         DS     C
YEAR     DS    C
         END
