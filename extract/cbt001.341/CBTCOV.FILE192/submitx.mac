         TITLE '   S U B M I T X   '
*$DOC@*****************************************************************
*                                                                     *
*        'SUBMITX' - TSO COMMAND TO SUBMIT TO SECONDARY JES.          *
*                    ALSO STATUSX, OUTPUTX, CANCELX                   *
*                    FOR SECONDARY JES.                               *
*                                                                     *
***********************************************************************
         SPACE
*  WRITTEN BY. BILL GODFREY, PLANNING RESEARCH CORPORATION, MCLEAN VA.
*  INSTALLATION. AIR FORCE DATA SERVICES CENTER, PENTAGON.
*  DATE WRITTEN. JUNE 7 1982.
*  DATE UPDATED. JUNE 8 1982.
*  ATTRIBUTES. RE-ENTRANT.
*   MUST BE LINK-EDITED WITH AC=1 IN AUTHORIZED LIBRARY.
*  DESCRIPTION.
*   THIS TSO COMMAND ALTERS ITS ENVIRONMENT TO LOOK LIKE IT IS
*   RUNNING UNDER 'JESX' (A SECONDARY JES2), THEN INVOKES THE
*   'SUBMIT' COMMAND (OR 'STATUS', 'OUTPUT', OR 'CANCEL'),
*   THEN PUTS ITS ENVIRONMENT BACK THE WAY IT WAS (PRIMARY JES2).
*   THE NET RESULT IS THE SUBMITX COMMAND PUTS THE JOB INTO AN
*   INTERNAL READER ON 'JESX', THE STATUSX COMMAND RETRIEVES
*   JOB STATUS INFO FROM 'JESX', OUTPUTX RETRIEVES SYSOUT FROM
*   'JESX', AND CANCELX CANCELS JOBS ON 'JESX'.
*
*   THIS COMMAND SHOULD BE NAMED 'SUBMITX' WITH ALIASES 'CANCELX'
*   'OUTPUTX', AND 'STATUSX'.  THE COMMAND WILL DETERMINE WHICH NAME
*   IT IS RUNNING UNDER BY LOOKING AT THE ECTPCMD FIELD IN THE ECT.
*   IT USES THE FIRST 2 LETTERS TO DECIDE WHICH TSO COMMAND TO
*   INVOKE, SO YOU COULD GIVE THIS COMMAND OTHER ALIASES SUCH AS
*   'SUBX' AND 'OUTX' AND 'STX' TOO.
*
*   A NOTE ABOUT HOW THIS COMMAND WORKS.
*   THIS COMMAND REQUESTS A JOB ID FROM THE SECONDARY JES BY
*   ISSUING THE IEFSSREQ MACRO.  JESX ASSIGNS A STARTED TASK
*   NUMBER AND THE NAME 'SYSLOG' TO YOUR SESSION.  AFTER THIS
*   COMMAND INVOKES THE 'SUBMIT' COMMAND (OR WHATEVER), IT
*   RETURNS THE JOB ID TO JESX, THUS TELLING JESX THAT IT IS
*   ENDING EXECUTION.  THIS PROCESS IS ALMOST INVISIBLE BECAUSE
*   USUALLY THERE ARE NO MESSAGES FROM JESX ABOUT THE TASK.
*   HOWEVER, YOU WILL NOTICE THAT JESX SEEMS TO BE SKIPPING
*   JOB NUMBERS.  ONE JOB SUBMITTED WILL GET JOB ID 23, AND THE
*   NEXT MAY GET 25.  WHERE DID 24 GO? IT WAS THE SYSLOG TASK
*   NUMBER ASSIGNED TO YOUR SESSION WHILE JOB 25 WAS SUBMITTED.
*
*  ********** WARNING **********
*   THERE IS CURRENTLY A PROBLEM WITH THIS COMMAND.  IT IS GETTING
*   A S0C4 ABEND IN JESXSSSM (THE SECONDARY HASPSSSM) ON THE
*   VERSION OF JES2 THAT COMES WITH THE SP1.3 IPO.  THE S0C4
*   WAS OCCURRING FROM THE IEFSSREQ MACRO IN RETURNING THE JOB ID.
*   WE HOPE THE PROBLEM WILL BE FIXED WHEN WE BRING THAT JES2
*   UP TO CURRENT MAINTENANCE LEVEL. OUR PRIMARY JES IS PRE-SP JES2.
*   IN THE MEANTIME YOU COULD ZAP THE 'BAL TO RETJOBID' TO A NOP.
*   THE RESULT IS THAT THE SECONDARY JES DOES NOT LET GO OF THE
*   SYSLOG JOBID UNTIL END-OF-MEMORY (LOGOFF). FREQUENT USE
*   OF THIS COMMAND WILL BUILD UP A LOT OF JOB ID'S IN USE ON THE
*   SECONDARY JES.  AT END-OF-MEMORY, JESX WILL ISSUE THE MESSAGE
*   'SYSLOG TERMINATED AT END OF MEMORY' FOR EACH JOB ID.
*
*  ********** WARNING **********
*   THIS COMMAND IS STILL IN THE TESTING STAGE AND MAY PROVE TO BE
*   UNWORKABLE EXCEPT WHEN USED WITH EXTREME CAUTION AND VERY SELDOM.
*   IT HAS SUCCESSFULLY BEEN USED TO SUBMIT JOBS TO A SECONDARY JES2
*   BUT IT HAS ALSO SOMETIMES PUT THE ISSUER'S SESSION IN A WAIT.
*   IT DOES THAT IF THE SECONDARY JES IS STARTED BUT A $S (OR @S
*   REALLY) HAS NOT BEEN ENTERED FOR IT BY THE OPERATOR.
*   WE SOMETIMES ARE UNABLE TO STOP THE SECONDARY JES, PERHAPS
*   BECAUSE END-OF-MEMORY FOR A SUBMITTING SESSION HAS NOT OCCURRED.
*   WE ARE NOT SURE THAT IT IS SAFE TO DO WHAT THIS CODE DOES, I.E.,
*   SWITCH FROM ONE JES TO ANOTHER BY CHANGING THE SSIB POINTER.
*   WE ARE WONDERING IF THE JSCB AND MAYBE SOME OTHER CONTROL BLOCKS
*   CAN BE ALTERED BY JESX AND THROW JES2 OFF, OR VICE VERSA.
*   MORE RESEARCH NEEDS TO BE DONE SO PLEASE BE AWARE OF THIS
*   WHEN DECIDING WHETHER TO INSTALL THIS COMMAND.
*
*   NOTE: 3 JAN 1983.
*         JIM MARSHALL, SADSC, (512) 828-0201
*         HAD A CALL FROM RAY DANT, PYA/MONARCH, (803) 269-3140 X294
*         WHO IS ON MVS/SP1.3 AND IS GETTING ASSEMBLER ERRORS
*         ON THE IEFSSREQ MACRO. SOMETHING ABOUT SYSTEMS ARE
*         NOT COMPATIBLE. SORRY, I CAN NOT HELP. I AM ON MVS/SP1.3
*         AND BILL IS NOT AROUND. A PERSON CAN CALL HIM DIRECT ON
*         (202) 395-3290 IN WASH DC.
*
*         1 MAR 83: TALKED TO BILL GODFREY, HE SAYS ANY SP1.3
*         PROBLEMS ARE PROBABLY CAUSED BY MACROS IMBEDDED IN THE
*         SOURCE. SO I REMOVED AS MUCH AS I COULD SEE THAT SHOULD
*         BE REMOVED (IHASDWA ADDED TO REPLACE IN-LINE CODE)
*
*
*   ESTAE EXIT NOTES.
*   THERE IS AN ESTAE EXIT WHOSE MAIN PURPOSE IS TO RESTORE THE
*   JSCBSSIB FIELD TO POINT TO THE PRIMARY JES2 SSIB IN CASE
*   THE USER HITS THE ATTENTION KEY DURING THIS COMMAND.
*   WE TRIED TO HAVE THE ESTAE EXIT POINT TO A RETRY ROUTINE
*   BUT WE DISCOVERED THAT RETRY IS NOT PERMITTED FOR THE TYPE
*   OF ABEND CAUSED BY THE ATTENTION KEY (S33E ABEND).
*   SO WE RESET THE JSCBSSIB IN THE ESTAE EXIT ITSELF INSTEAD
*   OF IN A RETRY ROUTINE.
*
*  LOG OF CHANGES.
*   08JUN82 - ESTAE EXIT ADDED TO RESTORE JSCBSSIB IF NECESSARY.
*   11JUN82 - COMMENTS ADDED.
*
***********************************************************************
         SPACE
SUBMITX  START
         USING *,R11,R12
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'SUBMITX'
         DC    CL16' &SYSDATE &SYSTIME '
SUBPOOL  DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(13)
         LR    R11,R15
         LA    R15,1
         LA    R12,4095(R15,R11)
         LR    R2,R1
         SPACE
         L     R0,SUBPOOL
         GETMAIN R,LV=(0)
         SPACE
         LR    R9,R1
         USING @DATA,R9
         XC    @DATA(@DATAL1),@DATA
         SPACE
         ST    R13,4(,R9)          PUT OLD ADDRESS IN NEW SAVE AREA
         ST    R9,8(,R13)          PUT NEW ADDRESS IN OLD SAVE AREA
         LR    R13,R9              NEW SAVEAREA POINTER
         SR    R15,R15
         ST    R15,RC
         ST    R2,CPPLPTR
         XC    LINKAREA(8),LINKAREA
         SPACE
************************************************************
*                                                          *
*        IF NOT AUTHORIZED, ISSUE MESSAGE AND QUIT         *
*                                                          *
************************************************************
         SPACE
         TESTAUTH FCTN=1
         LTR   R15,R15
         BZ    AUTHX
         LA    R1,MSAU             ENVIRONMENT IS NOT APF AUTHORIZED
         LA    R0,L'MSAU
         SVC   93                  TPUT
         LR    R1,R13              AREA TO BE FREEMAINED
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         L     R0,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)
         LA    R15,12              RETURN CODE
         LM    0,12,20(R13)
         L     14,12(,R13)
         BR    14
AUTHX    EQU   *
         SPACE
************************************************************
*                                                          *
*        GO INTO KEY 0                                     *
*                                                          *
************************************************************
         SPACE
         MODESET KEY=ZERO
         OI    STATUS,KEYZERO
         SPACE
************************************************************
*                                                          *
*        SET UP ESTAE                                      *
*                                                          *
************************************************************
         SPACE
         STM   R11,R12,BASES       SAVE REGS FOR ESTAE EXIT
         LA    R3,ESTAEXIT
         LA    R1,ESTAEWRK
         MVC   ESTAEWRK(ESTAEWKL),ESTAEMFL
         MVI   ESTAEOPT,255        WANT ESTAE EXIT TO ATTEMPT RECOVER
         ESTAE (R3),PARAM=(R9),TERM=YES,MF=(E,(1))
         OI    STATUS,STATEST
         SPACE
************************************************************
*                                                          *
*        SAVE THE NAME OF THIS COMMAND                     *
*                                                          *
************************************************************
         SPACE
         L     R1,CPPLPTR
         USING CPPL,R1
         L     R15,CPPLECT
         USING ECT,R15
         MVC   OURNAME,ECTPCMD
         DROP  R15                 ECT
         DROP  R1                  CPPL
         SPACE
************************************************************
*                                                          *
*        IF NAME IS ATTNX THEN DO TGET TO TEST ATTN KEY.   *
*        IF NAME IS ABENDX THEN FORCE A S0C1 ABEND.        *
*                                                          *
************************************************************
         SPACE
         CLC   OURNAME(2),=C'AT'   IS NAME ATTNX
         BNE   NOTATTN
         LA    R1,MSGWK
         LA    R0,80
         O     R1,=A(X'80000000')  SET TGET BIT ON
         SVC   93                  TGET
         B     EXIT12
NOTATTN  CLC   OURNAME(2),=C'AB'   IS NAME ABENDX
         BNE   *+6
         DC    H'0'
         SPACE
************************************************************
*                                                          *
*        FIND OUR SSIB                                     *
*                                                          *
************************************************************
         SPACE
         L     R2,540              PSATOLD-PSA
         L     R2,180(,R2)         TCBJSCB-TCB(,R2)
         L     R2,X'15C'(,R2)      JSCBSACT-JSCB(,R2)
         L     R2,X'13C'(,R2)      JSCBSSIB-JSCB(,R2)
         USING SSIB,R2
         ST    R2,OLDSSIB
         SPACE
************************************************************
*                                                          *
*         BUILD A NEW SSIB                                 *
*                                                          *
************************************************************
         SPACE
*              FOR OTHERS      SSIBSSNM IS JES2, SSIBJBID IS STC NNNN
         SPACE
         LA    R0,36               LENGTH OF SSIB
         O     R0,=A(X'FF000000')  SUBPOOL 255
         GETMAIN R,LV=(0)
         ST    R1,NEWSSIB
         OI    STATUS,STATGOT
         MVC   0(36,R1),MASKSSIB
         SPACE
************************************************************
*                                                          *
*         GET A JOB ID FROM JESX                           *
*                                                          *
************************************************************
         SPACE
         L     R3,16               CVTPTR
         USING CVT,R3
         L     R4,CVTJESCT         JES CONTROL TABLE
         USING JESCT,R4
         SPACE
************************************************************
*                                                          *
*        REQUEST JOB ID FROM PRIMARY SUBSYSTEM             *
*                                                          *
************************************************************
         SPACE
REQJOBID EQU   *
         LA    R5,LCLSSOB
         USING SSOB,R5
         MVC   SSOBID(4),=C'SSOB'
         LA    R15,SSOBHSIZ
         STH   R15,SSOBLEN
         LA    R15,SSOBRQST
         STH   R15,SSOBFUNC
         L     R15,NEWSSIB
         ST    R15,SSOBSSIB
         LA    R15,SSRRBGN
         ST    R15,SSOBINDV
         LA    R1,PTRSSOB
         ST    R5,0(,R1)
         OI    0(R1),X'80'
         LA    R15,8
         STH   R15,SSRRLEN
         LA    R15,LCLRSECB
         ST    R15,SSRRSECB
         DROP  R5                  SSOB
         SPACE
         LR    R5,R1
         MODESET MODE=SUP
         LR    R1,R5
         SPACE
         IEFSSREQ
         SPACE
         LR    R5,R15
         MODESET MODE=PROB
         LR    R15,R5
         SPACE
         LTR   R15,R15
         BZ    OKREQ
         MVC   WTOW(MS01L),MS01
         MVC   WTOW+21(4),MASKSSIB+8
         MVC   WTOW+4(8),OURNAME
         LA    R1,WTOW             JOBID REQUEST FAILED
         LH    R0,0(,R1)
         SH    R0,=H'4'
         LA    R1,4(,R1)
         SVC   93                  TPUT
         B     EXIT12
         SPACE
OKREQ    L     R1,NEWSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         OI    STATUS,STATSIB      RUNNING UNDER NEW SSIB
         SPACE
************************************************************
*                                                          *
*         DETERMINE NAME OF COMMAND TO INVOKE              *
*         BASED ON FIRST 2 LETTERS OF COMMAND NAME         *
*                                                          *
************************************************************
         SPACE
SUBJOB   EQU   *
         LA    R14,OURNAME
         LA    R2,=CL8'SUBMIT'
         CLC   0(2,R14),=CL8'SUBMIT'   SU
         BE    LINK
         LA    R2,=CL8'OUTPUT'
         CLC   0(2,R14),=CL8'OUTPUT'   OU
         BE    LINK
         LA    R2,=CL8'CANCEL'
         CLC   0(2,R14),=CL8'CANCEL'   CA
         BE    LINK
         LA    R2,=CL8'STATUS'
         CLC   0(2,R14),=CL8'STATUS'   ST
         BE    LINK
         MVC   WTOW(MS02L),MS02
         MVC   WTOW+4(8),OURNAME
         LA    R1,WTOW
         LH    R0,0(,R1)
         SH    R0,=H'4'
         LA    R1,4(,R1)
         SVC   93                  TPUT
         B     EXIT12
         SPACE
************************************************************
*                                                          *
*         INVOKE SUBMIT, CANCEL, OUTPUT, OR STATUS         *
*                                                          *
************************************************************
         SPACE
LINK     EQU   *
         MODESET KEY=NZERO
         NI    STATUS,255-KEYZERO
         L     R1,CPPLPTR
         SPACE
         LINK  EPLOC=(R2),SF=(E,LINKAREA)
         SPACE
         ST    R15,RC
         MODESET KEY=ZERO
         OI    STATUS,KEYZERO
         SPACE
************************************************************
*                                                          *
*        RETURN JOB ID TO PRIMARY SUBSYSTEM                *
*                                                          *
************************************************************
         SPACE
RETID    EQU   *
ZAPTHIS  BAL   R14,RETJOBID        VER 45E0 REP 4700
         B     EXIT
         SPACE
************************************************************
*                                                          *
*        RETURN JOB ID TO PRIMARY SUBSYSTEM                *
*                                                          *
************************************************************
         SPACE
*              IF THIS IS NOT DONE, JES2 ISSUES MESSAGE
*              $HASP310 SYSLOG  TERMINATED AT END OF MEMORY
         SPACE
RETJOBID EQU   *
         LR    R6,R14              SAVE RETURN ADDRESS
         SPACE
         L     R1,OLDSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         NI    STATUS,255-STATSIB
         SPACE
         LA    R5,LCLSSOB          SSOB
         USING SSOB,R5
         LA    R15,SSOBRTRN        RETURN JOB ID
         STH   R15,SSOBFUNC
         L     R15,NEWSSIB
         ST    R15,SSOBSSIB
         DROP  R5                  SSOB
         PRINT GEN
         SPACE
         MODESET MODE=SUP
         SPACE
         LA    R1,PTRSSOB
         SPACE
         IEFSSREQ
         SPACE
         MODESET MODE=PROB
         SPACE
         PRINT GEN
         LR    R14,R6              RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         SPACE
************************************************************
*                                                          *
*         TERMINATE                                        *
*                                                          *
************************************************************
         SPACE
EXIT12   LA    R15,12
         ST    R15,RC
EXIT     EQU   *
         TM    STATUS,KEYZERO      ARE WE IN KEY ZERO
         BO    EXITKEY             YES, STAY THAT WAY
         MODESET KEY=ZERO
         OI    STATUS,KEYZERO
EXITKEY  EQU   *
         TM    STATUS,STATSIB      ARE WE RUNNING UNDER NEW SSIB
         BZ    EXITSIB             NO, BRANCH
         L     R1,OLDSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         NI    STATUS,255-STATSIB
EXITSIB  EQU   *
         TM    STATUS,STATGOT
         BZ    EXITGOT
         LA    R0,36               LENGTH OF SSIB
         O     R0,=A(X'FF000000')  SUBPOOL 255
         L     R1,NEWSSIB
         FREEMAIN R,LV=(0),A=(1)
         NI    STATUS,255-STATGOT
EXITGOT  EQU   *
         TM    STATUS,STATEST
         BZ    EXITX1
         ESTAE 0
         NI    STATUS,255-STATEST
EXITX1   EQU   *
         L     R2,RC
         LR    R3,R13              SAVE R13 FOR FREEMAIN
         L     R13,4(,R13)         POINT TO PREVIOUS SAVE AREA
         SPACE
         MODESET KEY=NZERO
         NI    STATUS,255-KEYZERO
         SPACE
         LR    R1,R3               AREA TO BE FREEMAINED
         L     R0,SUBPOOL
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2              RETURN CODE
         LM    0,12,20(R13)
         L     14,12(,R13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        ESTAE EXIT                                        *
*                                                          *
************************************************************
         SPACE
         CNOP  0,8
         USING *,R15
ESTAEXIT EQU   *
         CH    R0,STAE12  SDWA (SYSTEM DIAGNOSTIC WORK AREA)
         BNE   STAEXWA             YES - BRANCH
         LR    R0,R9               SAVE R9
         LR    R9,R2               RESTORE WORK AREA BASE
         STM   R0,R15,ESTAESVE     SAVE ALL REGS
         ST    R0,ESTAESVE+36      STORE ORIGINAL VALUE OF R9
         LA    R0,12               RESTORE R0
         ST    R0,ESTAESVE         AND SAVE ITS ORIGINAL VALUE
         LM    R11,R12,BASES       RESTORE PROGRAM BASES
         DROP  R15                 DROP TEMPORARY BASE REG
         ST    R1,ABEND            SAVE ABEND COMPLETION CODE
         BAL   R14,ESTAEFIX        PUT THE PRIMARY SSIB BACK
         CLI   ESTAEOPT,0          DO WE WANT TO RECOVER
         BE    STAERC0             NO, BRANCH
         LA    R0,STAERETR         POINT TO RETRY ROUTINE
         LA    R15,4               INDICATE RETRY
         LM    R1,R14,ESTAESVE     RESTORE OTHER REGS
         BR    R14                 RETURN TO SYSTEM
         SPACE
STAERC0  LM    R0,R14,ESTAESVE
         SR    R15,R15             CONTINUE TERMINATION
         BR    R14
         SPACE
STAE12   DC    H'12'
         SPACE
         CNOP  0,8
         USING SDWA,R1
STAEXWA  STM   14,12,12(13)        SAVE REGISTERS
         L     R9,SDWAPARM         RESTORE WORK AREA BASE
         LM    R11,R12,BASES       RESTORE PROGRAM BASES
         MVC   ABEND(4),SDWACMPC-1 MOVE ABEND CODE
         L     R15,SDWAGR15        GET ABEND RC
         ST    R15,ABENDRC         SAVE IT
         BAL   R14,ESTAEFIX        PUT THE PRIMARY SSIB BACK
         CLI   ESTAEOPT,0          DO WE WANT TO RECOVER
         BNE   STAERW4             YES, BRANCH
         SPACE
STAERW0  SETRP RC=0,REGS=(14,12)
         SPACE
STAERW4  ST    R9,SDWAGR09         SET UP FOR SETRP RETREGS
         STM   R11,R12,SDWAGR11    SET UP FOR SETRP RETREGS
         SETRP RC=4,DUMP=NO,RETADDR=STAERETY,                          +
               RETREGS=YES,REGS=(14,12)
         SPACE
************************************************************
*                                                          *
*         ESTAE EXIT SUBROUTINE, SDWA OR NOT               *
*                                                          *
************************************************************
         SPACE
ESTAEFIX STM   R14,R1,ESTAEFXR
         TM    STATUS,KEYZERO      ARE WE IN KEY ZERO
         BO    ESTAKEY             YES, STAY THAT WAY
         MODESET KEY=ZERO
         OI    STATUS,KEYZERO
ESTAKEY  EQU   *
         TM    STATUS,STATSIB      ARE WE RUNNING UNDER NEW SSIB
         BZ    ESTASIB             NO, BRANCH
         L     R1,OLDSSIB          SSIB
         L     R15,540             PSATOLD-PSA
         L     R15,180(,R15)       TCBJSCB-TCB(,R15)
         L     R15,X'15C'(,R15)    JSCBSACT-JSCB(,R15)
         STCM  R1,7,X'13D'(R15)    JSCBSSIB-JSCB(,R15)
         NI    STATUS,255-STATSIB
ESTASIB  EQU   *
         TM    STATUS,STATGOT      IS SSIB GETMAINED
         BZ    ESTAGOT             NO, BRANCH
         LA    R0,36               LENGTH OF SSIB
         O     R0,=A(X'FF000000')  SUBPOOL 255
         L     R1,NEWSSIB          ADDRESS TO BE FREED
         FREEMAIN R,LV=(0),A=(1)   FREE SSIB FROM LSQA
         NI    STATUS,255-STATGOT
ESTAGOT  EQU   *
         MVI   MSGWK,C' '
         MVC   MSGWK+1(79),MSGWK
         MVC   MSGWK+00(8),OURNAME
         MVC   MSGWK+09(8),=C'ABEND (S'
         L     R1,ABEND
         N     R1,=A(X'00FFF000')
         BZ    STAERETU
         UNPK  MSGWK+17(5),ABEND+1(3)   ABCXYZ TO FAFBFCFXZY
         TR    MSGWK+17(3),HEXTR-240    FAFBFC TO C1C2C3
         MVC   MSGWK+20(2),=C') '
         CLC   MSGWK+17(3),=C'33E'
         BNE   STAERETM
         MVC   MSGWK+22(L'MSGATTN),MSGATTN
         B     STAERETM
STAERETU MVI   MSGWK+16,C'U'
         L     R1,ABEND
         N     R1,=A(X'00000FFF')
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGWK+17(4),DOUBLE+5(3)
         MVI   MSGWK+21,C')'
STAERETM LA    R1,MSGWK
         LA    R0,52
         SVC   93                      TPUT
ESTAEFXX LM    R14,R1,ESTAEFXR
         BR    R14
         SPACE
************************************************************
*                                                          *
*        STAE RETRY ROUTINE                                *
*                                                          *
************************************************************
         SPACE
         CNOP  0,8
STAERETR EQU   *                   RETRY ENTRY POINT IF NO SDWA
         L     R9,0(,R1)
         LM    R10,R12,BASES
STAERETY EQU   *                   RETRY ENTRY POINT IF SDWA PRESENT
         MVI   ESTAEOPT,0          DONT WANT TO RECOVER MORE THAN ONCE
         B     EXIT12
         SPACE
*               ABENDS MOST LIKELY TO HAPPEN
*               33E   THE TMP DETACHED THIS COMMAND BEFORE IT
*                     ENDED (BECAUSE THE ATTENTION KEY WAS HIT)
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
HEXTR    DC    C'0123456789ABCDEF'
ESTAEMFL ESTAE ESTAEDUM,TERM=YES,MF=L
ESTAEWKL EQU   *-ESTAEMFL
ESTAEDUM EQU   1
MSAU     DC    C'ENVIRONMENT IS NOT APF AUTHORIZED'
         SPACE
MS01     WTO   'XXXXXXX FAILED - JESX IS NOT ACTIVE',                  X
               ROUTCDE=(2),MF=L
MS01L    EQU   *-MS01
         SPACE
MS02     WTO   'XXXXXXXX DOES NOT RECOGNIZE THE COMMAND NAME IN THE ECTX
               ',ROUTCDE=(2),MF=L
MS02L    EQU   *-MS02
         SPACE
MSGATTN  DC    C'(ATTENTION KEY)'
         DC    0F'0'
MASKSSIB DC    C'SSIB',AL2(36,0),C'JESX',CL16' ',XL8'00'
         DC    0D'0'
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
CPPLPTR  DS    F
RC       DS    F
LINKAREA DS    2F
OURNAME  DS    CL8
BASES    DS    2F
STATUS   DS    X
STATEST  EQU   X'80'
STATSIB  EQU   X'40'
STATGOT  EQU   X'20'
KEYZERO  EQU   X'10'
PTRSSOB  DS    F
LCLSSOB  DS    5F,7F
LCLRSECB DS    F
NEWSSIB  DS    F
OLDSSIB  DS    F
@DATAL1  EQU   *-@DATA
ALLOCR   DS    7F
WTOW     DS    20F
MSGWK    DS    20F
ESTAEWRK DS    0F,(ESTAEWKL)X
ESTAESVE DS    16F                 REGISTERS 0-15
ESTAEFXR DS    4F                  REGISTERS 14,15,0,1
ABEND    DS    F
ABENDRC  DS    F
ESTAEOPT DS    C
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
************************************************************
*                                                          *
*         MAP OF CVT                                       *
*                                                          *
************************************************************
         SPACE
CVTPTR   EQU   16
CVT      DSECT
CVTMAP   EQU   *
         ORG   CVT+X'128'
CVTJESCT DS    A
         SPACE
************************************************************
*                                                          *
*         MAPPING MACROS                                   *
*                                                          *
************************************************************
         SPACE
         IKJCPPL
         SPACE 3
         IKJECT
         SPACE
         IEFJESCT
         SPACE 3
         IEFJSCVT
         SPACE 3
         IEFJSSIB
         SPACE 3
         IEFJSSOB (RR),CONTIG=YES
         SPACE 3
         IHASDWA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
