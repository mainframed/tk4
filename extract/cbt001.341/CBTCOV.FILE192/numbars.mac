         TITLE '   N U M B A R S '
************************************************************
*        'NUMBARS'
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        DATE WRITTEN. AUGUST 19 1977.
*        DATE UPDATED.
*        ATTRIBUTES. RE-ENTRANT.
*        REMARKS.
*            THIS TSO COMMAND DISPLAYS A LIST OF NUMBERS
*            IN THE FORM OF A BAR GRAPH.
*            FOR EXAMPLE:
*
*            NUMBARS (15 14 8 9 13 16)
*
*            WOULD PRODUCE THE FOLLOWING:
*
*               15 XXXXXXXXXXXXXXX
*               14 XXXXXXXXXXXXXX
*                8 XXXXXXXX
*                9 XXXXXXXXX
*               13 XXXXXXXXXXXXX
*               16 XXXXXXXXXXXXXXXX
         SPACE
NUMBARS  START
         USING *,R12
         B     32(,15)
         DC    AL1(11),CL11'NUMBARS'
         DC    CL16' &SYSDATE &SYSTIME '
         STM   14,12,12(13)
         LR    R12,15
         GETMAIN R,LV=MYDSECTL
         ST    13,4(,1)
         ST    1,8(,13)
         LR    14,13
         LR    13,1
         L     1,24(,14) RESTORE R1
         USING MYDSECT,13
         SPACE 1
         LR    R2,R1
         USING CPPL,R2
         SPACE
************************************************************
*                                                          *
*        SET UP IOPL FOR PUTLINE                           *
*                                                          *
************************************************************
         SPACE
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
         SPACE
         L     R15,16              LOAD CVT POINTER
         TM    444(R15),X'80'      IS PUTLINE LOADED? (VS2)
         BNO   PUTLOAD             NO - BRANCH TO LOAD
         L     R15,444(,R15)       YES - USE CVTPUTL
         B     PUTLODED            BRANCH AROUND LOAD
PUTLOAD  LA    R0,=CL8'IKJPUTL '
         LOAD  EPLOC=(0)
         LR    R15,R0              GET ENTRY ADDRESS
         LA    R15,0(,R15)         CLEAR HI BYTE FOR DELETE ROUTINE
PUTLODED ST    R15,MYPUTLEP        SAVE PUTLINE ENTRY ADDRESS
         SPACE
************************************************************
*                                                          *
*        SET UP PPL FOR PARSE                              *
*                                                          *
************************************************************
         SPACE
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
         L     R0,=A(NUMPCL)
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF
         LA    R0,MYUWA
         ST    R0,PPLUWA
         DROP  R15
         SPACE 1
************************************************************
*                                                          *
*        CALL THE PARSE SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE 1
         LR    R1,R15              POINT TO PPL
         L     R15,16              CVTPTR
         TM    X'020C'(R15),X'80'  IF HI ORDER BIT NOT ON
         BNO   PARSELNK               THEN DO LINK, NOT CALL
         L     R15,X'020C'(,R15)   CVTPARS
         BALR  R14,R15             CALL IKJPARS
         B     PARSEEXT            SKIP AROUND LINK
PARSELNK EQU   *
         LINK  EP=IKJPARS
PARSEEXT EQU   *
         SPACE 1
         LTR   R15,R15
         BZ    PARSEOK
         LA    R1,MSG01
         LA    R0,L'MSG01
         BAL   R14,PUTMSG
         B     EXIT12
PARSEOK  EQU   *
         SPACE 1
         SLR   R15,R15
         ST    R15,HIGHEST
         MVI   LOWEST,X'7F'
         SPACE
         L     R3,MYANS
         USING IKJPARMD,R3
         SPACE
************************************************************
*                                                          *
*        CONVERT NUMBERS TO BINARY                         *
*                                                          *
************************************************************
         SPACE
         LA    R4,NUMBERS
         LA    R5,TABLE
         SLR   R6,R6
         LA    R7,100              MAXIMUM NUMBERS
         B     FIRSTNUM
         SPACE
NEXTNUM  CLI   8(R4),X'FF'
         BE    NUMSDONE
         L     R4,8(,R4)           POINT TO NEXT ENTRY
FIRSTNUM TM    6(R4),X'80'         OMITTED?
         BZ    NEXTNUM             YES - IGNORE
         LH    R1,4(,R4)           GET LENGTH
         LTR   R1,R1               LENGTH ZERO?
         BZ    NEXTNUM             YES - IGNORE
         L     R15,0(,R4)          POINT TO NUMBER
         BCTR  R1,0
         B     *+10
         PACK  DOUBLE(8),0(0,R15)  PACK THE NUMBER
         EX    R1,*-6              EXECUTE THE ABOVE
         CVB   R1,DOUBLE
         SPACE
         LA    R6,1(,R6)
         CR    R6,R7
         BH    TOOMANY
         STH   R1,0(,R5)
         STH   R1,2(,R5)
         LA    R5,4(,R5)
         SPACE
         C     R1,LOWEST
         BNL   *+8
         ST    R1,LOWEST
         C     R1,HIGHEST
         BNH   *+8
         ST    R1,HIGHEST
         SPACE
         B     NEXTNUM
         SPACE
TOOMANY  EQU   *
         LA    R1,MSG02
         LA    R0,L'MSG02
         BAL   R14,PUTMSG
         B     EXIT12
         SPACE
NUMSDONE EQU   *
         ST    R6,TABLESIZ
         SPACE
************************************************************
*                                                          *
*        PROCESS 'BASE'                                    *
*                                                          *
************************************************************
         SPACE
         LA    R15,0
         LA    R4,BASE
         TM    6(R4),X'80'
         BZ    RBASE
         LH    R1,4(,R4)
         LTR   R1,R1
         BZ    RBASE
         L     R15,LOWEST
         LTR   R15,R15
         BZ    *+6
         BCTR  R15,0
         L     R14,0(,R4)
         CLI   0(R14),C'*'
         BE    RBASE
         BCTR  R1,0
         B     *+10
         PACK  DOUBLE(8),0(0,R14)
         EX    R1,*-6
         CVB   R15,DOUBLE
RBASE    STH   R15,OPTBAS
         LH    R15,OPTBAS
         LTR   R15,R15             ANY BASE?
         BZ    XBASE               NO - BRANCH
         LA    R5,TABLE
         L     R6,TABLESIZ
LBASE    LH    R1,0(,R5)
         SR    R1,R15              SUBTRACT BASE
         BNM   *+6                 BRANCH IF NOT MINUS
         SLR   R1,R1               ZERO IF MINUS
         STH   R1,2(,R5)
         LA    R5,4(,R5)
         BCT   R6,LBASE
         L     R1,HIGHEST
         SR    R1,R15
         BNM   *+6
         SLR   R1,R1
         ST    R1,HIGHEST
         L     R1,LOWEST
         SR    R1,R15
         BNM   *+6
         SLR   R1,R1
         ST    R1,LOWEST
XBASE    EQU   *
         SPACE
************************************************************
*                                                          *
*        PROCESS 'SIZE'                                    *
*                                                          *
************************************************************
         SPACE
         LA    R15,64
         STH   R15,OPTSIZ
         LA    R4,SIZE
         TM    6(R4),X'80'
         BZ    NOSIZ
         LH    R1,4(,R4)
         LTR   R1,R1
         BZ    NOSIZ
         L     R15,0(,R4)
         BCTR  R1,0
         B     *+10
         PACK  DOUBLE(8),0(0,R15)
         EX    R1,*-6
         CVB   R1,DOUBLE
         LA    R15,120            MAXIMUM SIZE
         CR    R1,R15             TOO LARGE?
         BNH   *+6                NO - BRANCH
         LR    R1,R15             YES - SET BACK TO MAXIMUM
         STH   R1,OPTSIZ
NOSIZ    EQU   *
         SPACE
         L     R15,HIGHEST
         CH    R15,OPTSIZ
         BNH   NOADJ
         SPACE
         LA    R5,TABLE
         L     R6,TABLESIZ
         L     R15,HIGHEST
         LTR   R15,R15            IS HIGHEST ZERO?
         BZ    NOADJ              YES - BRANCH
         LH    R14,OPTSIZ
ADJUST   EQU   *
         LH    R1,2(,R5)
         MR    R0,R14             MULTIPLY R1 BY OPTSIZ
         DR    R0,R15             DIVIDE PRODUCT BY HIGHEST
         STH   R1,2(,R5)          STORE QUOTIENT
         LA    R5,4(,R5)
         BCT   R6,ADJUST
NOADJ    EQU   *
         SPACE
         MVI   OPTDEC,C'0'
         LA    R4,DEC
         TM    6(R4),X'80'
         BZ    NODEC
         L     R15,0(,R4)
         CLI   0(R15),C'3'
         BH    NODEC
         MVC   OPTDEC,0(R15)
NODEC    EQU   *
         SPACE
************************************************************
*                                                          *
*        PROCESS 'CHAR'                                    *
*                                                          *
************************************************************
         SPACE
         LA    R4,CHAR
         MVI   OPTCHR,C'X'
         TM    6(R4),X'80'
         BZ    NOCHAR
         L     R14,0(,R4)
         MVC   OPTCHR(1),0(R14)
NOCHAR   EQU   *
         MVC   MSGV(1),OPTCHR
         MVC   MSGV+1(255),MSGV
         MVC   MSGV+256(256),MSGV
         MVC   MSGV+512(256),MSGV
         MVC   MSGV+768(256),MSGV
         MVI   MSGV+7,C' '
         LA    R5,TABLE
         L     R6,TABLESIZ
OUTLOOP  LH    R1,2(,R5)
         LA    R0,6(,R1)
         MVC   MSGV(7),=X'40402020202120'
         CLI   OPTDEC,C'1'
         BNE   *+10
         MVC   MSGV(7),=X'40202020214B20'
         CLI   OPTDEC,C'2'
         BNE   *+10
         MVC   MSGV(7),=X'402020214B2020'
         CLI   OPTDEC,C'3'
         BNE   *+10
         MVC   MSGV(7),=X'4020214B202020'
         LH    R1,0(,R5)
         CVD   R1,DOUBLE
         ED    MSGV(7),DOUBLE+5
         LA    R1,MSGV+2
         BAL   R14,PUTLINE
         LA    R5,4(,R5)
         BCT   R6,OUTLOOP
         SPACE
         B     EXIT0
         SPACE
************************************************************
*                                                          *
*        PUTMSG ROUTINE                                    *
*                                                          *
************************************************************
         SPACE
PUTMSG   STM   R14,R1,PUTLINS
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'     IS DATA PRECEEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE
************************************************************
*                                                          *
*        PUTLINE ROUTINE                                   *
*                                                          *
************************************************************
         SPACE
PUTLINE  STM   R14,R1,PUTLINS
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTLINS
         BR    R14
         SPACE
EXIT12   LA    R15,12
         B     EXIT
EXIT0    SR    15,15
EXIT     LR    1,13
         L     13,4(,13)
         ST    15,16(,13)
         FREEMAIN R,A=(1),LV=MYDSECTL
         LM    14,12,12(13)
         BR    14
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         SPACE
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
MSG01    DC    C'PARSE ERROR'
MSG02    DC    C'MAXIMUM OF 100 NUMBERS EXCEEDED'
         LTORG
         SPACE
************************************************************
*                                                          *
*        PARSE PPL                                         *
*                                                          *
************************************************************
         SPACE
         PRINT NOGEN
NUMPCL   IKJPARM
NUMBERS  IKJIDENT 'NUMBER (NOT TO EXCEED 4 DIGITS)',LIST,              +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=4,                  +
               PROMPT='NUMBER TO BE REPRESENTED IN BAR GRAPH'
SIZEKW   IKJKEYWD
         IKJNAME 'SIZE',SUBFLD=SIZESF
DECKW    IKJKEYWD
         IKJNAME 'DECIMAL',SUBFLD=DECSF
BASKW    IKJKEYWD
         IKJNAME 'BASE',SUBFLD=BASSF
CHARKW   IKJKEYWD
         IKJNAME 'CHAR',SUBFLD=CHARSF
SIZESF   IKJSUBF
SIZE     IKJIDENT 'SIZE',                                              +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=3,                  +
               PROMPT='SIZE OF LINE'
DECSF    IKJSUBF
DEC      IKJIDENT 'DECIMAL PLACES',                                    +
               FIRST=NUMERIC,MAXLNTH=1,                                +
               PROMPT='DECIMAL PLACES'
BASSF    IKJSUBF
BASE     IKJIDENT 'BASE VALUE',ASTERISK,                               +
               FIRST=NUMERIC,OTHER=NUMERIC,MAXLNTH=4,                  +
               PROMPT='BASE VALUE'
CHARSF   IKJSUBF
CHAR     IKJIDENT 'CHARACTER',                                         +
               FIRST=ANY,MAXLNTH=1,                                    +
               PROMPT='CHARACTER TO BE USED IN BAR GRAPH'
         IKJENDP
         PRINT GEN
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
MYDSECT  DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D
MYPPL    DS    7F
MYANS    DS    F
MYUWA    DS    F
MYECB    DS    F                  USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                 USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                 USED BY PUTLINE ROUTINE
MYPUTLEP DS    F                  USED BY PUTLINE ROUTINE
MYOLD    DS    2F                 USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL256           USED BY PUTLINE ROUTINE
PUTLINS  DS    4F                 USED BY PUTLINE ROUTINE
OPTDEC   DS    H
OPTSIZ   DS    H
OPTBAS   DS    H
OPTCHR   DS    H
LOWEST   DS    F
HIGHEST  DS    F
TABLESIZ DS    F
TABLE    DS    100F
MSGV     DS    1024C
         DS    0D
MYDSECTL EQU   *-MYDSECT
         SPACE
         IKJCPPL
         SPACE
         IKJIOPL
         SPACE
         IKJPPL
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
