TPRI TITLE 'GSF-TPRINT  OUTPUT MESSAGE TO TERMINAL (FROM CLIST)'
TPRINT   CSECT
         SAVE  (14,12),,GSF-TPRINT-07MAY74
         BALR  12,0
         USING *,12
         LR    R11,R1
         USING CPPL,R11
         L     R6,CPPLCBUF             LOAD BUFFER ADDRESS
         CLI   1(R6),X'0E'             BUFFER LENGHT MUST BE
         BL    ERROR                   AT LEAST 14 BYTES
         CLI   3(R6),X'07'             AND THE OFFSET 7 BYTES.
         BL    ERROR
         LH    R4,2(R6)                LOAD OFFSET
         LA    R5,4(R6)                POINT TO FIRST CHARACTER
         AR    R5,R4                   AFTER COMMAND TPRINT
         CLI   0(R5),C''''
         BNE   ERROR
         LH    R3,0(R6)                FIND THE NUMBER
         AR    R3,R6                   OF CHARACTERS
         SR    R3,R5
         LA    R8,1(R5)                SAVE ADDRESS FOR TPUT
         SR    R2,R2
         BCT   R3,TP1
TP1      LA    R5,1(R5)                POINT TO NEXT CHARACTER
         OI    0(R5),X'40'             CONVERT TO UPPER CASE
         CLI   0(R5),C''''
         BNE   TP2
         LR    R2,R5                   SAVE ADDRESS OF '
TP2      BCT   R3,TP1
         LTR   R2,R2                   CHECK FOR 2ND '
         BZ    ERROR
         LR    R4,R2                   FIND THE NUMBER OF
         SR    R4,R8                   CHARACTERS FOR TPUT
SEND     TPUT  (R8),(R4)
         SR    R15,R15
         RETURN (14,12),RC=(15)
ERROR    LA    R4,21
         LA    R8,MSG
         B     SEND
MSG      DC    C'ERROR IN MESSAGE TEXT'
         IKJCPPL
         REGS
         END   TPRINT
