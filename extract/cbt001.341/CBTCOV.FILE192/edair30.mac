         TITLE '   E D A I R 3 0  '
***********************************************************************
*                                                                     *
*        'EDAIR30' SUBPROGRAM                                         *
*                                                                     *
***********************************************************************
*
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER, INC.  MCLEAN VA
*        DATE WRITTEN. MARCH 4 1981.
*        DATE UPDATED. MARCH 4 1981.
*        ATTRIBUTES. RE-ENTRANT.
*        MACROS. GETMAIN FREEMAIN LINK IEFZB4D0 IEFZB4D2 IKJEFFDF.
*         IKJCPPL, IKJUPT, IKJDAPL, IKJDAP30.
*
*         THIS SUBPROGRAM IS AN ENHANCED VERSION OF IKJDAIR
*         FOR ALLOCATING SYSOUT DATA SETS.
*
*         IT DOES NOT REPLACE IKJDAIR, BUT CAN BE CALLED INSTEAD
*         OF IKJDAIR WHEN ALLOCATING A SYSOUT DATA SET.
*
*         WHEN MODIFYING AN EXISTING TSO COMMAND TO GIVE
*         IT THE ABILITY TO DYNAMICALLY ALLOCATE A GREATER
*         VARIETY OF DATA SETS THAN 'IKJDAIR' ALLOWS, IT
*         IS INCONVENIENT TO CONVERT THE CODE FROM 'IKJDAIR'
*         ORIENTED PARAMETER LISTS TO 'SVC-99' ORIENTED
*         PARAMETER LISTS.  THIS SUBPROGRAM WAS WRITTEN TO
*         SIMPLIFY THAT TASK BY ALLOWING THE 'IKJDAIR' LOGIC
*         TO BE LEFT UNCHANGED, WHILE INCORPORATING THE NEW
*         SVC-99 FUNCTIONS IN THE FORM OF NEW FIELDS IN THE
*         DAIR PARAMETER BLOCKS.
*
*         TO CONVERT A COMMAND, SIMPLY ADD YOUR NEW KEYWORDS
*         AND OPERANDS, ADD CODE TO INTERPRET THEM INTO THE NEW
*         FIELDS, AND CALL 'EDAIR30' INSTEAD OF 'IKJDAIR'.
*         BE SURE TO CLEAR THE NEW FIELDS YOU ARE NOT USING.
*
*         THIS SUBPROGRAM ACCEPTS A PARAMETER LIST SIMILAR
*         TO THAT PASSED TO IKJDAIR, AND EITHER PASSES IT ON
*         TO IKJDAIR OR TRIES TO PERFORM THE REQUESTED FUNCTION
*         ITSELF.  IT DOES THE LATTER WHENEVER A FUNCTION IS
*         REQUESTED WHICH IKJDAIR DOES NOT SUPPORT, SUCH AS
*
*             ALLOCATE A SYSOUT FILE SPECIFYING CLASS, DEST, HOLD,
*             AND COPIES.  DAIR IGNORES CLASS AND DOES NOT SUPPORT
*             THE OTHERS.
*
*         THESE FUNCTIONS ARE REQUESTED BY ADDING NEW FIELDS TO
*         THE DA30 PARAMETER AREA, AND CALLING THIS SUBPROGRAM
*         INSTEAD OF IKJDAIR.
*
*         DIFFERENCES BETWEEN THIS PROGRAM AND IKJDAIR.
*
*          A) SUPPORT CLASS, DEST, HOLD, COPIES, FREE=CLOSE, FCB.
*          B) DA30 PARAMETER AREA IS LARGER.
*          C) THIS PROGRAM DOES NOT HAVE THE SAME DEGREE OF
*             VALIDITY CHECKING THAT IKJDAIR DOES.  SOME INVALID
*             DATA IN THE OLD FIELDS WHICH IKJDAIR WOULD REJECT
*             MAY GO UNNOTICED HERE.
*          D) SPACE INFORMATION IS IGNORED.
*          E) UNIT INFORMATION IS IGNORED.
*          F) THE DUMMY BIT IS IGNORED.
*          G) FREE=CLOSE IS AUTOMATIC IF PROGRAM NAME IS INTRDR.
*          H) IF SVC 99 FAILS TO ALLOCATE, THIS PROGRAM CAN ISSUE
*             AN EXPLANATORY MESSAGE ITSELF, IF REQUESTED TO.
*             THIS MAY PRODUCE MORE INFORMATIVE MESSAGES.
*
*         THE OPERATING SYSTEM MUST BE OS/VS2 MVS.
*
*         AT ENTRY REGISTER 1 POINTS TO A 'DAPL', MAPPED BY
*         THE STANDARD 'IKJDAPL' MACRO.
*
*         UPON RETURN TO CALLER, REGISTER 15 IS ZERO IF THE
*         ALLOCATION WAS SUCCESSFUL.  IF REGISTER 15 IS 4,
*         THE DAPB CONTAINS AN INVALID PARAMETER, AND NO
*         MESSAGE IS ISSUED EVEN IF A MESSAGE WAS REQUESTED.
*         REGISTER 15 MAY CONTAIN OTHER RETURN CODES, ALL
*         OFF THEM COMPATIBLE WITH IKJDAIR.
*
*$DOC$*****************************************************************
         SPACE
EDAIR30  START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'EDAIR30'
         DC    CL16' &SYSDATE &SYSTIME '
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15
         LR    R2,R1               SAVE PARAMETER LIST ADDRESS
         USING DAPL,R2
         L     R3,DAPLDAPB
         CLC   0(2,R3),=X'0030'    SYSOUT
         BE    IN30                YES, BRANCH
         EJECT
CALDAIR  L     R15,16              CVTPTR
         TM    732(R15),X'80'      IS IKJDAIR IN LPA
         BNO   LNKDAIR             NO, BRANCH
         L     R15,732(,R15)       YES, GET IKJDAIR ENTRY ADDRESS
         LM    0,12,20(R13)        RESTORE ALL REGISTERS
         BR    R15                 DAIR WILL RETURN TO OUR CALLER
         SPACE
LNKDAIR  LA    R0,80               LENGTH OF SAVEAREA PLUS LINK SF
         GETMAIN R,LV=(0)          GET A SAVEAREA
         ST    R13,4(,R1)          PUT OLD ADDRESS IN NEW SAVEAREA
         ST    R1,8(,R13)          PUT NEW ADDRESS IN OLD SAVEAREA
         LR    R13,R1              TRANSFER SAVEAREA BASE
         LA    R15,72(,R13)        POINT TO LINK PARAMETER AREA
         XC    0(8,R15),0(R15)     CLEAR LINK PARAMETER AREA
         LR    R1,R2               RESTORE ORIGINAL R1
         LINK  EP=IKJDAIR,SF=(E,(R15))
         LR    R1,R13              SET UP R1 FOR FREEMAIN
         LA    R0,80               SET UP R0 FOR FREEMAIN
         L     R13,4(,R13)         RESTORE ORIGINAL SAVEAREA BASE
         LR    R2,R15              SAVE R15 ACROSS FREEMAIN
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2              RESTORE R15 (DAIR RETURN CODE)
         LM    0,12,20(R13)        RESTORE OTHER REGISTERS
         L     R14,12(,R13)        RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         EJECT
***********************************************************************
*
*         DAIR 30 - SYSOUT ALLOCATION
*
***********************************************************************
         SPACE
IN30     EQU   *
         USING DAPB30,R3
         SPACE
         GETMAIN R,LV=@DATAL
         ST    R13,4(,R1)          PUT OLD ADDRESS IN NEW SAVEAREA
         ST    R1,8(,R13)          PUT NEW ADDRESS IN OLD SAVEAREA
         LR    R13,R1              TRANSFER SAVEAREA BASE
         USING @DATA,R13
         SPACE
         TM    DA30CTL,DA30ATRL    ATTRIBUTE NAME PRESENT
         BZ    IN30ATOK            NO, BRANCH
         CLI   DA30ALN,C' '        YES, IS IT BLANK
         BE    EXIT4               YES, ERROR
IN30ATOK EQU   *
         SPACE
         MVI   MYDSRET,0
         L     R14,DA30PDSN        POINTER TO DSNAME
         LTR   R14,R14             IS DSNAME PRESENT
         BZ    IN30DSOK            NO, BRANCH
         CLC   0(2,R14),=H'44'     YES, IS ITS LENGTH 44
         BNE   EXIT4               NO, ERROR
         CLI   2(R14),X'40'        IS IT BLANK
         BNE   EXIT4               NO, ERROR
         MVI   MYDSRET,C'R'        YES, INDICATE RETURN DSNAME
IN30DSOK EQU   *
         SPACE
         CLI   DA30UNIT,0
         BE    EXIT4
         SPACE
         LA    R1,MY99RB
         ST    R1,MY99RBP
         OI    MY99RBP,X'80'
         XC    MY99RB(20),MY99RB
         LA    R14,MY99RB
         USING S99RB,R14
         MVI   S99RBLN,20
         MVI   S99VERB,S99VRBAL
         LA    R1,MY99TUPL
         ST    R1,S99TXTPP
         DROP  R14                 S99RB
         SPACE
         XC    MY99TUPL(TUPL$LEN),MY99TUPL
         OI    MY99TUPL+TUPL$LEN-4,X'80'
         USING S99TUNIT,R4
*
*              PROCESS RETURN-DSNAME
*
         CLI   MYDSRET,C'R'
         BNE   IN30NODS
         LA    R4,MY99TXDS
         ST    R4,MY99TUDS
         LA    R15,DALRTDSN
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,44
         STH   R15,S99TULNG
         MVI   S99TUPAR,C' '
         MVC   S99TUPAR+1(43),S99TUPAR
IN30NODS EQU   *
*
*              PROCESS DDNAME
*
         MVI   MYDDRET,0
         LA    R4,MY99TXDD
         ST    R4,MY99TUDD
         CLI   DA30DDN,X'40'       DD TO BE RETURNED?
         BE    IN30DDR             YES - BRANCH
*
*              DDNAME IS SPECIFIED
*
         LA    R15,DALDDNAM
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),DA30DDN
         B     IN30SO
*
*              DDNAME NOT SPECIFIED
*
IN30DDR  LA    R15,DALRTDDN
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),=CL8' '
         MVI   MYDDRET,C'R'
*
*              PROCESS SYSOUT CLASS
*
IN30SO   LA    R4,MY99TXSO
         ST    R4,MY99TUSO
         LA    R15,DALSYSOU
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,1
         STH   R15,S99TULNG
         MVC   S99TUPAR(1),DA30OCLS
         CLI   DA30OCLS,X'40'      CLASS SPECIFIED?
         BE    IN30NOSO            NO - BRANCH
         CLI   DA30OCLS,X'00'      CLASS SPECIFIED?
         BNE   IN30SOX             YES - BRANCH
IN30NOSO SR    R15,R15             INDICATE NULL ENTRY
         STH   R15,S99TUNUM         FOR THIS KEY
IN30SOX  EQU   *
*
*              PROCESS PROGRAM NAME OR INTRDR
*
         CLI   DA30PGNM,X'40'
         BE    IN30NOPG
         CLI   DA30PGNM,X'00'
         BE    IN30NOPG
         LA    R4,MY99TXPG
         ST    R4,MY99TUPG
         LA    R15,DALSPGNM
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8               SET LENGTH INITIALLY TO 8
         LA    R1,DA30PGNM+7 POINT TO 8TH CHAR OF NAME
IN30PGLP CLI   0(R1),X'40'         BLANK?
         BNE   IN30PGLN            NO - BRANCH TO STORE LENGTH
         BCTR  R15,0               YES - DECREMENT LENGTH BY 1
         BCT   R1,IN30PGLP         POINT BACK 1 CHAR AND BRANCH
IN30PGLN STH   R15,S99TULNG
         MVC   S99TUPAR(8),DA30PGNM
IN30NOPG EQU   *
*
*              PROCESS FORM
*
         CLI   DA30FORM,X'40'      FORM?
         BE    IN30NOFO            NO - BRANCH
         CLI   DA30FORM,X'00'      FORM?
         BE    IN30NOFO
         LA    R4,MY99TXFO
         ST    R4,MY99TUFO
         LA    R15,DALSFMNO
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,4
         STH   R15,S99TULNG
         MVC   S99TUPAR(4),DA30FORM
IN30NOFO EQU   *
*
*              PROCESS PERMANENTLY ALLOC
*
         TM    DA30CTL,DA30PERM
         BZ    IN30NOPE
         LA    R4,MY99TXPE
         ST    R4,MY99TUPE
         LA    R15,DALPERMA
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
IN30NOPE EQU   *
*
*              PROCESS ATTRIBUTE LIST NAME
*
         TM    DA30CTL,DA30ATRL    IS ATTRIBUTE NAME PRESENT
         BZ    IN30NOAT            NO, BRANCH
         LA    R4,MY99TXAT
         ST    R4,MY99TUAT
         LA    R15,DALDCBDD
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8               SET LENGTH INITIALLY TO 8
         LA    R1,DA30ALN+7 POINT TO 8TH CHAR OF NAME
IN30ATLP CLI   0(R1),X'40'         BLANK?
         BNE   IN30ATLN            NO - BRANCH TO STORE LENGTH
         BCTR  R15,0               YES - DECREMENT LENGTH BY 1
         BCT   R1,IN30ATLP         POINT BACK 1 CHAR AND BRANCH
IN30ATLN STH   R15,S99TULNG
         MVC   S99TUPAR(8),DA30ALN
IN30NOAT EQU   *
*
*              PROCESS COPIES
*
         CLI   DA30CPYS,1          IS COPIES ZERO OR 1
         BNH   IN30NOCO            YES, DO NOT SPECIFY COPIES
         LA    R4,MY99TXCO
         ST    R4,MY99TUCO
         LA    R15,DALCOPYS
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,1               SVC99 ONLY ALLOWS 1 BYTE.
         STH   R15,S99TULNG
         MVC   S99TUPAR(1),DA30CPYS
IN30NOCO EQU   *
*
*              PROCESS DEST
*
         CLI   DA30DEST,X'40'      DEST?
         BE    IN30NODE            NO - BRANCH
         CLI   DA30DEST,X'00'      DEST?
         BE    IN30NODE            NO - BRANCH
         LA    R4,MY99TXDE
         ST    R4,MY99TUDE
         LA    R15,DALSUSER
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(8),DA30DEST
IN30NODE EQU   *
*
*              PROCESS HOLD
*
         TM    DA30CTLX,DA30HOLD   HOLD?
         BZ    IN30NOHO            NO - BRANCH
         LA    R4,MY99TXHO
         ST    R4,MY99TUHO
         LA    R15,DALSHOLD
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
IN30NOHO EQU   *
*
*              PROCESS FREE=CLOSE
*
         CLC   DA30PGNM,=CL8'INTRDR' IF INTERNAL READER
         BE    IN30FR              THEN FORCE FREE=CLOSE
         TM    DA30CTLX,DA30FC     FREE=CLOSE REQUESTED?
         BZ    IN30NOFR            NO - BRANCH
IN30FR   LA    R4,MY99TXFR
         ST    R4,MY99TUFR
         LA    R15,DALCLOSE
         STH   R15,S99TUKEY
         LA    R15,0
         STH   R15,S99TUNUM
IN30NOFR EQU   *
*
*              PROCESS FCB
*
         CLI   DA30FCB,X'40'       FCB?
         BE    IN30NOFC            NO - BRANCH
         CLI   DA30FCB,X'00'       FCB?
         BE    IN30NOFC            NO - BRANCH
         LA    R4,MY99TXFC
         ST    R4,MY99TUFC
         LA    R15,DALFCBIM
         STH   R15,S99TUKEY
         LA    R15,1
         STH   R15,S99TUNUM
         LA    R15,8
         STH   R15,S99TULNG
         MVC   S99TUPAR(4),DA30FCB
IN30NOFC EQU   *
*
*              PROCESS OTHER THINGS
*
         SPACE
*              UNLIKE IKJDAIR, THIS PROGRAM IGNORES THE
*              DA30UNIT FIELD, ALL FIELDS RELATED TO SPACE
*              (PRIMARY, SECONDARY, TRKS, ABKL, RLSE)
*              AND THE DUMMY FLAG.
         SPACE
*
*              ALLOCATE
*
ALLOC    LA    R1,MY99RBP
         SVC   99
*
*              TEST RETURN CODE
*
         LTR   R15,R15             WAS IT SUCCESSFUL
         BNZ   FAILED              NO, BRANCH
         SPACE
         CLI   MYDSRET,C'R'        DSNAME TO BE RETURNED?
         BNE   IN30RDSX            NO - BRANCH
         LA    R4,MY99TXDS
         L     R15,DA30PDSN        OR DA08PDSN
         LH    R14,S99TULNG
         BCTR  R14,0               LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   2(0,R15),S99TUPAR   (EXECUTED)
         EX    R14,*-6             MOVE DSNAME
         SR    R15,R15
IN30RDSX EQU   *
         SPACE
         CLI   MYDDRET,C'R'        DDNAME TO BE RETURNED?
         BNE   EXIT                NO - BRANCH
         LA    R4,MY99TXDD
         MVC   DA30DDN,=CL8' '     OR DA08DDN
         LH    R14,S99TULNG
         BCTR  R14,0               LENGTH MINUS 1 FOR EX
         B     *+10
         MVC   DA30DDN(0),S99TUPAR (EXECUTED)
         EX    R14,*-6             MOVE DDNAME
         B     EXIT
*
*               CONVERT ERROR CODES TO DAIR CODES
*
FAILED   TM    DA30CTLX,DA30MSG    AUTOMATIC MESSAGE
         BZ    *+8                 NO, BRANCH
         BAL   R8,PUTMSG           YES, CALL IKJEFF18
         LA    R14,MY99RB
         USING S99RB,R14
FAIL04   CH    R15,=H'4'           UNABLE TO ALLOCATE
         BNE   FAIL08
FAIL04A  CLC   S99ERROR,=X'0410'   RCDDUNAV
         BNE   FAIL04B
         LA    R15,20              RETURN CODE 20
         B     FAIL04X
FAIL04B  CLC   S99ERROR,=X'043C'   RCNTUNLC, DYNAMNBR TOO SMALL
         BE    FAIL04B1
         CLC   S99ERROR,=X'0450'   RCLIMITS, MORE THAN 1635
         BNE   FAIL04C
FAIL04B1 LA    R15,16              RETURN CODE 16
         B     FAIL04X
FAIL04C  CLC   S99ERROR,=X'0454'   RCDCBRNF, ATTRIB DD NOT FOUND
         BNE   FAIL04D
         LA    R15,28              RETURN CODE 28
         B     FAIL04X
FAIL04D  MVC   DA30DARC,S99ERROR   DARC SAME AS S99ERROR
         LA    R15,12              RETURN CODE 12
FAIL04X  B     EXIT
         SPACE
FAIL08   CH    R15,=H'8'           REQUEST DENIED BY EXIT
         BNE   FAIL12
         LA    R15,52              REQUEST DENIED
         B     EXIT
         SPACE
FAIL12   CH    R15,=H'12'          INVALID PARAMETER
         BNE   *+4
         CLC   S99ERROR,=X'035C'   RCINPARM
         BNE   FAIL12S             NO, BRANCH
FAIL12A  CLC   S99INFO,=AL2(DALDDNAM)
         BNE   FAIL12B
         MVC   DA30DARC,=X'0318'   DRDDNINV, INVALID DDNAME
         B     FAIL12X
FAIL12B  CLC   S99INFO,=AL2(DALSPGNM)
         BNE   FAIL12C
         MVC   DA30DARC,=X'0324'   DRSYSPGM, INVALID SYSOUT PGM
         B     FAIL12X
FAIL12C  CLC   S99INFO,=AL2(DALSFMNO)
         BNE   FAIL12D
         MVC   DA30DARC,=X'0328'   DRSYSFRM, INVALID FORM
         B     FAIL12X
FAIL12D  CLC   S99INFO,=AL2(DALSYSOU) (THIS TEST NOT IN IKJDAIR)
         BNE   FAIL12S
         MVC   DA30DARC,=X'032C'   DR      , INVALID CLASS
         B     FAIL12X
FAIL12S  MVC   DA30DARC,S99ERROR   DARC SAME AS S99ERROR
FAIL12X  LA    R15,12
         B     EXIT
         DROP  R14                 S99RB
*
*               ISSUE APPROPRIATE ERROR MESSAGE
*
PUTMSG   LA    R1,OURCPPL
         USING CPPL,R1
         MVC   CPPLUPT(4),DAPLUPT
         MVC   CPPLPSCB(4),DAPLPSCB
         MVC   CPPLECT(4),DAPLECT
         DROP  R1                  CPPL
         LA    R1,MYDFAREA
         USING DFDSECTD,R1
         ST    R15,MYDFRC          STORE SVC 99 RETURN CODE
         LA    R15,MYDFRC
         ST    R15,DFRCP           STORE ADDRESS OF RETURN CODE
         LA    R15,MY99RB
         ST    R15,DFS99RBP        STORE ADDRESS OF SVC 99 REQUEST BLK
         SR    R15,R15
         ST    R15,MYJEFF02        STORE ZERO IN IKJEFF02 ADDRESS
         LA    R15,MYJEFF02
         ST    R15,DFJEFF02        STORE ADDRESS OF ZERO
         LA    R15,DFSVC99
         STH   R15,MYDFID          STORE TYPE OF REQUEST (SVC 99)
         LA    R15,MYDFID
         ST    R15,DFIDP           STORE ADDRESS OF TYPE
         LA    R15,OURCPPL
         ST    R15,DFCPPLP         STORE CPPL ADDRESS OR ZERO
*        CLC   DFCPPLP+1(3),=AL3(0) CPPL ADDRESS SUPPLIED
*        BNE   *+8                 YES - USE PUTLINE
*        OI    MYDFID,DFWTP        NO - USE WRITE-TO-PROGRAMMER
         XC    LINKAREA,LINKAREA
         LINK  EP=IKJEFF18,SF=(E,LINKAREA)
         L     R15,MYDFRC          RETURN SVC 99 RETURN CODE
         BR    R8
         SPACE
*
*         RETURN TO CALLER
*
EXIT4    LA    R15,4               RETURN CODE 4
         B     EXIT
EXIT0    SR    15,15               RETURN CODE ZERO
EXIT     LR    R1,R13              AREA TO BE FREED
         L     R13,4(,R13)         RESTORE OLD R13
         LR    R2,R15              SAVE RETURN CODE
         FREEMAIN R,A=(1),LV=@DATAL
         LR    R15,R2              RESTORE RETURN CODE
         LM    0,12,20(R13)        RESTORE OLD REGISTERS
         L     R14,12(,R13)        RESTORE RETURN ADDRESS
         BR    R14                 RETURN
         SPACE 3
         LTORG
         DC    0D'0'
         SPACE 3
*
*         WORKSPACE
*
@DATA    DSECT
         DS    18F
MY99RBP  DS    A                   SVC 99 RB POINTER
MY99RB   DS    5A                  SVC 99 REQUEST BLOCK
*
* TEXT UNIT POINTERS
*
MY99TUPL DS    0A                  SVC 99 TEXT LIST POINTERS
MY99TUDS DS    A                   TEXT UNIT POINTER, DSNAME
MY99TUDD DS    A                   TEXT UNIT POINTER, DDNAME
MY99TUSO DS    A                   TEXT UNIT POINTER, SYSOUT CLASS
MY99TUCO DS    A                   TEXT UNIT POINTER, COPIES
MY99TUDE DS    A                   TEXT UNIT POINTER, DEST
MY99TUHO DS    A                   TEXT UNIT POINTER, HOLD
MY99TUPG DS    A                   TEXT UNIT POINTER, PGM NAME
MY99TUPE DS    A                   TEXT UNIT POINTER, PERM
MY99TUFR DS    A                   TEXT UNIT POINTER, FREE=CLOSE
MY99TUFC DS    A                   TEXT UNIT POINTER, FCB
MY99TUFO DS    A                   TEXT UNIT POINTER, USING DD REF
MY99TUAT DS    A                   TEXT UNIT POINTER, ATTR LIST NAME
         DS    A                   HI ORDER BIT ON
TUPL$LEN EQU   *-MY99TUPL
*
* TEXT UNITS
*
MY99TXDS DS    3H,CL44             TEXT UNIT, DSNAME
MY99TXDD DS    3H,CL8              TEXT UNIT, DDNAME
MY99TXSO DS    3H,CL1              TEXT UNIT, SYSOUT CLASS
MY99TXCO DS    3H,H                TEXT UNIT, COPIES
MY99TXDE DS    3H,CL8              TEXT UNIT, DEST
MY99TXHO DS    3H                  TEXT UNIT, HOLD=YES
MY99TXPG DS    3H,CL8              TEXT UNIT, PROGRAM NAME
MY99TXPE DS    3H                  TEXT UNIT, PERM
MY99TXFR DS    3H                  TEXT UNIT, FREE=CLOSE
MY99TXFC DS    3H,CL4              TEXT UNIT, FCB
MY99TXFO DS    3H,CL4              TEXT UNIT, FORM NUMBER
MY99TXAT DS    3H,CL8              TEXT UNIT, ATTRIBUTE LIST NAME
*
*
*
MYDFAREA DS    5F                  IKJEFF18 PARAMETER BLOCK
MYDFRC   DS    F                   SVC 99 RETURN CODE
MYJEFF02 DS    F                   MESSAGE ROUTINE (ZERO)
MYDFID   DS    H                   TYPE OF FAILURE (SVC 99)
MYDSRET  DS    C                   SWITCH
MYDDRET  DS    C                   SWITCH
LINKAREA DS    2F
OURCPPL  DS    4F
         DS    0D
@DATAL   EQU   *-@DATA
         EJECT
         IEFZB4D0
         EJECT
         IEFZB4D2
         EJECT
         IKJEFFDF DFDSECT=YES
         SPACE 3
         IKJCPPL
         SPACE 3
         IKJUPT
         SPACE 3
         IKJDAPL
         SPACE 3
         IKJDAP30
DA30DEST DS    CL8      DEST
DA30CTLX DS    X        FLAGS
DA30MSG  EQU   X'80'    CALL IKJEFF18 IF IT FAILS
DA30HOLD EQU   X'40'    HOLD
DA30FC   EQU   X'20'    FREE AT CLOSE
DA30CPYS DS    X        COPIES
DA30FCB  DS    CL4      FCB
DA30DCB  DS    XL10     RESERVED FOR FUTURE USE (DCB,3800)
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
