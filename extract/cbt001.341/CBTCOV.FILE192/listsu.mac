         TITLE '   L I S T S U    '
************************************************************
*                                                          *
*        'LISTSU'                                          *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        DATE WRITTEN. JUNE 7 1979.
*        DATE UPDATED.
*        ATTRIBUTES. RE-ENTRANT.
*        COMMENTS.
*            THIS PROGRAM PRINTS A LIST OF THE SELECTABLE UNITS
*            THAT ARE INSTALLED IN THE SYSTEM.
*
*            OUTPUT IS TO DDNAME SYSPRINT, UNLESS EXECUTED FROM
*            A TSO SESSION, WHEN OUTPUT IS TO TERMINAL VIA 'TPUT'.
         SPACE
LISTSU   START
         USING *,R12
         B     @BEGIN-*(,15)
         DC    AL1(11),CL11'LISTSU '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@BEGIN   STM   14,12,12(R13)       SAVE REGISTERS
         LR    R12,R15             LOAD BASE REGISTER
         LR    R2,R1               PARM POINTER
         L     R0,@SIZE            WORKAREA SUBPOOL AND LENGTH
         GETMAIN R,LV=(0)
         LR    R9,R1               INITIALIZE WORKAREA POINTER
         USING @DATA,R9
         SPACE 1
         ST    R13,4(,R9)          CHAIN SAVEAREA
         ST    R9,8(,R13)          CHAIN SAVEAREA
         LR    R13,R9              UPDATE SAVEAREA POINTER
         SPACE 1
         MVI   STATUS,0
         MVI   LINE-1,C' '
         L     R2,0(,R2)           POINT TO PARM
         CLI   1(R2),0             IS THERE A PARM
         BE    TSOTEST             NO, BRANCH
         CLI   2(R2),C'P'          PARM=P
         BE    PRINT               YES, USE SYSPRINT EVEN IF TSO
TSOTEST  L     R1,X'224'           PSAAOLD, CURRENT ASCB
         L     R1,X'3C'(,R1)       ASCBTSB
         LTR   R1,R1               IS THIS A TSO SESSION
         BZ    PRINT               NO, USE SYSPRINT
         MVI   TSO,C'T'            YES
         B     NOPRINT             BYPASS OPEN
PRINT    MVI   TSO,0               USING SYSPRINT
         LA    R3,PRTDCBW          POINT R3 TO DCB IN WORK AREA
         MVC   0(PRTDCBL,R3),PRTDCB
         LA    R1,OPEN
         MVI   0(R1),X'80'
         OPEN  ((R3),OUTPUT),MF=(E,(1))
         TM    OFLGS(R3),X'10'     WAS OPEN SUCCESSFUL?
         BO    OKPRT               YES
         LA    R1,MSG01            NO, ISSUE MESSAGE
         WTO   MF=(E,(1))
         B     EXIT12
OKPRT    EQU   *
         OI    STATUS,X'80'        SYSPRINT IS OPEN
NOPRINT  EQU   *
         SPACE 1
         MVC   LINE,LINE-1
         MVI   LINE,C'1'           TOP OF PAGE
         MVC   LINE+1(L'TITLE),TITLE
         BAL   R2,PUTLINE
         L     R1,16               CVTPTR
         L     R4,X'414'(,R1)      CVTIHASU
         LA    R5,13               NUMBER OF BYTES
         SLR   R6,R6               CURRENT BIT NUMBER
         LA    R2,LINE+1
         MVC   LINE,LINE-1         FILL LINE WITH BLANKS
         LA    R11,LINE+71
BYTELOOP IC    R7,0(,R4)           GET A BYTE
         SLL   R7,23               SHIFT BYTE LEFT
         LA    R8,8                NUMBER OF BITS THIS BYTE
BITLOOP  SLL   R7,1                GET NEXT BIT INTO SIGN
         LTR   R7,R7               IS SIGN BIT ON
         BNM   NEXTBIT             NO, BRANCH
         LTR   R6,R6               IS IT FIRST BIT OF IST BYTE
         BZ    NEXTBIT             YES, THERE IS NO SU ZERO
         CR    R2,R11              IS THE LINE FULL
         BL    NEXTCVD             NO, BRANCH
         BAL   R2,PUTLINE
         MVC   LINE,LINE-1         FILL LINE WITH BLANKS
         LA    R2,LINE+1
NEXTCVD  CVD   R6,DOUBLE           CONVERT CURRENT BIT NUMBER
         MVC   0(4,R2),=X'40202120'
         ED    0(4,R2),DOUBLE+6
         LA    R2,4(,R2)
NEXTBIT  LA    R6,1(,R6)           UPDATE CURRENT BIT NUMBER
         BCT   R8,BITLOOP
         LA    R4,1(,R4)           UPDATE CURRENT BYTE ADDRESS
         BCT   R5,BYTELOOP
         BAL   R2,PUTLINE
         SPACE 1
EXIT0    SR    R15,R15             RETURN CODE ZERO
         B     EXIT
PUTLINE  CLI   TSO,C'T'
         BE    TPUT
         PUT   (R3),LINE
         BR    R2
TPUT     LA    R1,LINE+1
         LA    R0,120
         TPUT  (1),(0),R
         BR    R2
EXIT12   LA    R15,12              RETURN CODE 12
EXIT     LR    R2,R15              SAVE RETURN CODE
         TM    STATUS,X'80'        IS SYSPRINT OPEN?
         BZ    EXITCPRT            NO
         LA    R3,PRTDCBW
         LA    R1,CLOSE
         MVI   0(R1),X'80'
         CLOSE ((R3)),MF=(E,(1))
EXITCPRT EQU   *
EXITX    LR    R15,R2              RESTORE RETURN CODE
         LR    R1,R13              POINT R1 TO AREA TO BE FREED
         L     R0,@SIZE            SUBPOOL AND LENGTH
         L     R13,4(,R13)         RESTORE PREVIOUS SAVEAREA
         ST    R15,16(,R13)        STORE RETURN CODE FOR LM
         FREEMAIN R,A=(1),LV=(0)
         LM    14,12,12(R13)       LOAD RETURN ADDRESS AND RC
         BR    14                  RETURN
         SPACE
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
         LTORG
TITLE    DC    C'THE FOLLOWING SELECTABLE UNIT (SU) NUMBERS ARE INSTALL+
               ED'
         SPACE
MSG01    WTO   'LISTSU UNABLE TO OPEN SYSPRINT',                       +
               ROUTCDE=(11),MF=L
         SPACE 1
         PRINT NOGEN
         SPACE 1
PRTDCB   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      +
               RECFM=FBA,LRECL=121,BLKSIZE=121
PRTDCBL  EQU   *-PRTDCB
         SPACE 1
         PRINT GEN
         SPACE 1
         DC    0D'0'
OFLGS    EQU   48                  DCB OFFSET
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F                 REGISTER SAVEAREA
DOUBLE   DS    D                   DOUBLEWORD WORK AREA
OPEN     EQU   DOUBLE,4            OPEN LIST
CLOSE    EQU   DOUBLE,4            CLOSE LIST
TSO      DS    C
STATUS   DS    C
PRTDCBW  DS    0D,(PRTDCBL)X       SYSPRINT DCB
LINEB    DS    CL122               LINE-1
LINE     EQU   LINEB+1,121
@DATAL   EQU   *-@DATA
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
