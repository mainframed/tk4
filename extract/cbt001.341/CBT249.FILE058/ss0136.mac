            TITLE 'S0136 DISPLAY FORMS CONTROL BUFFER'
**********************************************************************
**********************************************************************
**  PROGRAM NAME : SS0136                            C.P. 02/25/76  **
**  SOURCE NAME : S0136                                             **
**  SOURCE LOCATION : T1.SRCLIB                                     **
**  FUNCTION : TO DISPLAY IN REPORT FORMAT ALL FCB'S OR ONLY THE    **
**             FCB'S LISTED ON PARM CARD.                           **
**                                                                  **
**********************************************************************
**********************************************************************
*        R0    STANDARD LINKAGE
*        R1    STANDARD LINKAGE
*        R2    WORK
*        R3    PROGRAM BASE
*        R4    WORK
*        R5    WORK
*        R6    WORK
*        R7    WORK
*        R8
*        R9
*        R10
*        R11
*        R12   SUBROUTINE LINKAGE
*        R13
*        R14   STANDARD LINKAGE
*        R15   STANDARD LINKAGE
S0136    $ENTER
         ST    R1,PARMSAVE    SAVE POINTER TO PARM
         OPEN  (FCBLIST,OUTPUT) OPEN OUTPUT REPORT FILE
         OI    SW,2           SET INDICATOR TO SHOW FILE OPENED
         L     R1,PARMSAVE    PICK UP PARM ADDR
         LTR   R1,R1          VALID ADDRESS ?
         BZ    PARMERR        NO
         L     R2,0(R1)       POINT TO PARM
         LH    R12,0(R2)      PICK UP PARM LENGTH
         LTR   R12,R12        IS IT ZERO ?
         BZ    PARMERR        YES
         BAL   R12,BLDFCBS    BUILD TABLE OF ALL POSSIBLE FCB NAMES
         LA    R2,2(,R2)      BUMP PAST LENGTH
         CLC   0(3,R2),ALL    PRINT ALL FCB'S ?
         BE    ALLLOOP        YES
         CLC   0(3,R2),=C'LIST'  SEE IF NEW LIST FORMAT NEEDED
         BNE   CKFCB
         MVI   NEWLSTFG,C'Y'   SET SW FOR NEW LIST FORMAT
         LA    R2,5(R2)        BUMP PAST LIST KEY WORD
         CLC   0(3,R2),ALL    PRINT ALL FCB'S ?
         BE    ALLLOOP        YES
CKFCB    EQU   *
         MVC   FCBNAME+4(4),0(R2) MOVE FCB NAME TO WORK AREA
         MVC   FCBNAME(4),=C'FCB2' SET UP PREFIX
         BAL   R12,FCBSRCH    IS FCB IN TABLE ?
         BNE   FCBNTFND       NO
         CLI   NEWLSTFG,C'Y'  SET WHICH PRINT ROUTINE ACTIVE
         BE    DONEW1         DO THE NEW ONE
         BAL   R12,PRTFCB     GO PRINT REGULAR
         B     RTNA           SKIP BY NEW
DONEW1   BAL   R12,PRTFCBR    DO THE NEW PRINT ROUNTINE
RTNA     CLI   4(R2),107      MORE FCB'S THIS RUN ?
         BNE   ALLDONE        NO
         LA    R2,5(R2)       BUMP TO NEXT FCB
         B     CKFCB          LOOP
ALLDONE  EQU   *
         TM    SW,1           ILIB OPEN ?
         BZ    CLOS2          NO
         CLOSE ILIB
CLOS2    EQU   *
         TM    SW,2          PRINT FILE OPEN ?
         BZ    CLOS3         NO
         CLOSE FCBLIST
CLOS3    EQU   *
         FREEMAIN R,LV=2000,A=GETCORE
         FREEMAIN R,LV=8640,A=PRTTABAD
         L     R15,RTNCODE    SET UP RETURN CODE
         $RTRN RC=(R15)
**********************************************************************
****** BUILD TABLE OF ALL FCB NAMES FORM SYS1.IMAGELIB          ******
**********************************************************************
BLDFCBS  EQU   *
         OPEN  (DILIB,INPUT)  OPEN DIRECTORY
         GETMAIN R,LV=8640    CORE FOR TABLE OF CHAN PRINT LINES
*                2000 FOR FCB NAMES & 8640 FOR CHAN PRINT LINES
         ST    R1,PRTTABAD    SAVE TABLE ADDR.
         GETMAIN R,LV=2000    CORE FOR TABLE OF FCB NAMES
         ST    R1,FCBTABAD    SAVE TABLE ADDR.
         ST    R1,GETCORE     SAVE TABLE ADDR.
         LR    R4,R1          BASE TABLE
         SR    R8,R8          CLEAR COUNTER
DIRREAD  EQU   *
         READ  DIRDECB,SF,DILIB,IOAREA,256
         CHECK DIRDECB        TEST FOR COMPLETION
         LA    R1,IOAREA+8    START OF RECORD
         LH    R5,0(R1)       PICK UP COUNT
         LA    R1,2(,R1)      POINT TO MEMBER
DIRMOV   EQU   *
         CLC   0(4,R1),=C'FCB2' IS THIS FCB OR UCS
         BNE   NOFCB            UCS
         MVC   0(8,R4),0(R1)  MOVE MEMBER TO TABLE
         LA    R8,1(,R8)      BUMP COUNT
         LA    R4,8(,R4)      BUMP TO NEXT SLOT
NOFCB    CLC   0(8,R4),=8X'FF' LAST ITEM?
         BE    ENDDIR         YES
         NI    11(R1),X'1F'   TURN OFF BIT NOT IN COUNT
         SR    R6,R6          CLEAR
         IC    R6,11(R1)      PICK UP COUNT USER DATA
         SLL   R6,1           DOUBLE
         LA    R6,12(,R6)     PLUS MEMBER FIXED AREA
         AR    R1,R6          BUMP TO NEXT MEMBER
         SR    R5,R6          SUB FROM COUNT
         BP    DIRMOV         IF POS MOVE NEXT MEMBER
         B     DIRREAD        ELSE READ NEXT RECORD
ENDDIR   EQU   *
         STH   R8,FCBCNT      SAVE MEMBER COUNT
         CLOSE DILIB          CLOSE IMAGELIB
         BR    R12            RETURN
***********************************************************************
****** THIS ROUTINE WILL PRINT ALL FCB'S IN SYS1.IMAGELIB        ******
***********************************************************************
ALLLOOP  EQU   *
         L     R8,FCBTABAD    PICK UP START OF TABLE
         LH    R7,FCBCNT      FCB COUNT
         LTR   R7,R7          ANY MORE ?
         BZ    ALLDONE        NO
         BCTR  R7,0           DECREMENT COUNT
         STH   R7,FCBCNT      SAVE NEW COUNT
         MVC   FCBNAME,0(R8)  MOVE NAME TO FCBNAME
         LA    R8,8(,R8)      BUMP TO NEXT MEMBER
         ST    R8,FCBTABAD    SAVE NEW START OF TABLE
         CLI   NEWLSTFG,C'Y'  SET WHICH PRINT ROUTINE ACTIVE
         BE    DONEW2         DO THE NEW ONE
         BAL   R12,PRTFCB     GO PRINT REGULAR
         B     RTNB           SKIP BY NEW
DONEW2   BAL   R12,PRTFCBR    DO THE NEW PRINT ROUNTINE
RTNB     B     ALLLOOP        LOOP
**********************************************************************
**       ERROR   CONDITIONS                                         **
**********************************************************************
PARMERR  EQU   *
         LA    R15,4          SET RTN CODE
         ST    R15,RTNCODE    SAVE TILL EOJ
         CNOP  0,4            ALIGN
         BAL   R12,PUT        WRITE ERROR # 1 TO PRINT FILE
         DC    A(ERR1,FCBLIST)
         B     ALLDONE        GO WRAP UP JOB
FCBNTFND EQU   *
         LA    R15,8          SET RTN CODE
         ST    R15,RTNCODE    SAVE TILL EOJ
         MVC   ERR2NAM,FCBNAME NAME TO PRINT
         CNOP  0,4            ALIGN
         BAL   R12,PUT        WRITE MSG TO PRINT FILE
         DC    A(ERR2,FCBLIST)
         B     ALLDONE        WRAP UP JOB
FCBIOERR EQU   *
         LA    R15,12         SET RTN CODE
         ST    R15,RTNCODE    SAVE TILL EOJ
         CNOP  0,4
         BAL   R12,PUT
         DC    A(ERR3,FCBLIST)
         B     ALLDONE
FCBERR   EQU   *
         LA    R15,16         SET RTN CODE
         ST    R15,RTNCODE    SAVE TILL EOJ
         CNOP  0,4
         BAL   R12,PUT
         DC    A(ERR4,FCBLIST)
         B     ALLDONE
**********************************************************************
**       SUBRROUTINES                                               **
**********************************************************************
* PRINT FCB REPORT FOR ONE FCB
PRTFCB   EQU   *
*   FCB NAME IS IN FIELD FCBNAME 8 CHAR WITH RIGHT SPACES
*   RETURNIS VIA R12
         ST    R12,PRTRTN     SAVE RETURN ADDR
         TM    SW,1           IS ILIB OPEN ?
         BO    ILIBOPEN       YES
         OPEN  (ILIB,INPUT)   OPEN ILIB
         OI    SW,1
ILIBOPEN EQU   *
         LOAD  EPLOC=FCBNAME,DCB=ILIB     LOAD FCB
         ST    R0,REG0SAV     SAVE POINTER TO FCB
FCBLN1   EQU   *
         TM    SW,2
         BO    LISTOPEN
         OPEN  (FCBLIST,OUTPUT)
LISTOPEN EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 1ST HEADING LINE
         DC    A(HL1,FCBLIST)
         MVC   L2NAME(4),FCBNAME+4 LAST HALF NAME TO PRINT
FCBLN2   EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 2ND LINE
         DC    A(LN2,FCBLIST)
FCBLN3   EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 3RD LINE
         DC    A(LN3,FCBLIST)
FCBLN4   EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 4TH LINE
         DC    A(LN4,FCBLIST)
         L     R6,REG0SAV     POINT TO FCB
FCBLN5   EQU   *
         CLI   0(R6),X'80'    DEFAULT IMAGE ?
         BE    DFLTIMG        YES
         CNOP  0,4
         BAL   R12,PUT        PRINT 5TH LINE WITH NO DEFAULT
         DC    A(LN5A,FCBLIST)
         B     FCBLN6
DFLTIMG  EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 5TH LINE WITH DEFAULT
         DC    A(LN5B,FCBLIST)
FCBLN6   EQU   *
         LA    R6,1(,R6)      BUMP TO SECOND BYTE
         SR    R7,R7          CLEAR
         IC    R7,0(R6)       PICK UP COUNT
         TM    1(R6),X'C0'    IS THERE PRT POS INDEX?
         BZ    CONV           NO
         BCTR  R7,0           DEC CNT FOR PRT POS INDEX
CONV     CVD   R7,DWORK         *
         UNPK  L6CNT,DWORK+6(2) **  CONVERT LINE COUNT
         OI    L6CNT+2,X'F0'    *
         CNOP  0,4
         BAL   R12,PUT        PRINT 6TH LINE
         DC    A(LN6,FCBLIST)
FCBLN7   EQU   *
         LA    R6,1(,R6)      BUMP TO THIRD BYTE
         TM    0(R6),X'C0'    IS PRT POS INDEX PRESENT?
         BZ    FCBLN8A
         TM    0(R6),X'80'    IS IT TO RIGHT?
         BZ    PPLEFT         NO
         MVC   PPDIR,=C'RIGHT' SET DIR
PPLEFT   EQU   *
         NI    0(R6),X'3F'     TURN OFF DIR INDICATORS
         SR    R5,R5           CLEAR
         IC    R5,0(R6)        PICK UP OFFSET
         CVD   R5,DWORK        CONVERT
         UNPK  PPCNT,DWORK+6(2)
         OI    PPCNT+2,X'F0'   SET SIGN BITS
         CNOP  0,4             ALIGN
         BAL   R12,PUT         PRINT POS LINE
         DC    A(PPLN,FCBLIST)
         LA    R6,1(,R6)      BUMP TO FORTH BYTE
FCBLN8A  EQU   *
         TM    0(R6),X'10'    EIGHT LINES/INCH ?
         BZ    FCBLN8B        NO
         CNOP  0,4
         BAL   R12,PUT        PRINT 8TH LINE EIGHT PER INCH
         DC    A(LN7A,FCBLIST)
         B     FCBLN9
FCBLN8B  EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 8TH LINE SIX PER INCH
         DC    A(LN7B,FCBLIST)
FCBLN9   EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 9TH LINE
         DC    A(LN8,FCBLIST)
FCBLN10  EQU   *
         CNOP  0,4
         BAL   R12,PUT        PRINT 10TH LINE
         DC    A(LN9,FCBLIST)
         LA    R9,1           START LINE COUNT
FCBDET   EQU   *
         MVC   SAVBYTE,0(R6)  SAVE CURRENT BYTE
         NI    SAVBYTE,X'0F'  TURN OFF ANY HIGH BITS
         SR    R8,R8          CLEAR
         IC    R8,SAVBYTE     PICK UP CHANNEL
         LTR   R8,R8
         BZ    NODET
         CVD   R8,DWORK
         UNPK  DETCA,DWORK+6(2)
         OI    DETCA+2,X'F0'
         MVI   DETCA,C' '
         CVD   R9,DWORK
         UNPK  DETLN,DWORK+6(2)
         OI    DETLN+2,X'F0'
         CNOP  0,4
         BAL   R12,PUT
         DC    A(DETLINE,FCBLIST)
NODET    EQU   *
         LA    R9,1(,R9)      BUMP PAGE COUNT
         LA    R6,1(,R6)      BUMP TO NEXT BYTE
         BCT   R7,FCBDET
         BCTR  R6,0           SET COUNT BACK TO CHECK LAST CHAR
         TM    0(R6),X'10'
         BO    FCBRET
         CNOP  0,4
         BAL   R12,PUT
         DC    A(ERR3,FCBLIST)
FCBRET   EQU   *
         L     R12,PRTRTN
         BR    R12
**********************************************************************
**       SUBRROUTINE FOR PRINT ALL                                  **
**********************************************************************
* PRINT FCB REPORT FOR ONE FCB
PRTFCBR  EQU   *
*   FCB NAME IS IN FIELD FCBNAME 8 CHAR WITH RIGHT SPACES
*   RETURNIS VIA R12
         ST    R12,PRTRTN     SAVE RETURN ADDR
         TM    SW,1           IS ILIB OPEN ?
         BO    ILIBOPN       YES
         OPEN  (ILIB,INPUT)   OPEN ILIB
         CALL  GDATE,(RPTDTE),VL   GET SYSTEM DATE
         MVC   HDRDTE,RPTDTE
         TIME
         ST    R0,REG0SAV
         MVC   HDRTIME,=X'4020207A20207A2020'
         ED    HDRTIME,REG0SAV    PUT TIME IN HEADER PRINT LINE
         OI    SW,1
ILIBOPN  EQU   *
         LOAD  EPLOC=FCBNAME,DCB=ILIB     LOAD FCB
         ST    R0,REG0SAV     SAVE POINTER TO FCB
FCBLNE   EQU   *
         TM    SW,2
         BO    CKFSTPRT
         OPEN  (FCBLIST,OUTPUT)
CKFSTPRT CLI   FSTPRT,C'1'    SEE IF THIS IS FIRST TIME
         BE    LSTOPEN       NOT FIRST TIME GO TO LISTOPEN
         MVI   FSTPRT,C'1'    SET FIRST TIME
         ZAP   LINECNT,=P'60' FORCE LINE COUNT TO MAX SO HDRS COME OUT
         L     R14,PRTTABAD   GET ADDR OF PRINT LINE CHAN INFO
         L     R15,=F'8640'   LENGHT OF WORK STORAGE FOR CHAN INFO
         LM    R0,R1,LONGFM   SET UP FOR CLEAR ACTION ON MOVE LONG
         MVCL  R14,R0         CLEAR USING MOVE LONG
         B     LSTOPEN
         CNOP  0,4
LONGFM   DC    A(*+4)         SOURCE OF SPACE FOR MOVE LONG
         DC    X'40',AL3(1)   CLEARING ACTION
LSTOPEN  EQU   *
         CP    LINECNT,=P'56' CHECK FOR MAX LINES PER PAGE
         BL    SKIPHDR        IF NOT LOW GO TO REGULAR PRINT
         CNOP  0,4
         BAL   R12,PUT        PRINT 1ST HEADING LINE
         DC    A(RH1,FCBLIST)
         ZAP   LINECNT,=P'0'   CLEAR LINE COUNT
         CNOP  0,4
         BAL   R12,PUT        PRINT 2ND LINE
         DC    A(HL2,FCBLIST)
         CNOP  0,4
         BAL   R12,PUT        PRINT 3RD LINE
         DC    A(HL3,FCBLIST)
SKIPHDR  EQU   *
         MVI   PRTCAR,C'0'
         MVI   PRTLNE,C' '
         MVC   PRTLNE+1(119),PRTLNE   SET PRINT LINE TO SPACES
         MVC   RPTFCB,FCBNAME+4       FCB NAME TO PRINT
         MVC   PRTDEF,=CL3'NO '       DEFAULT TO NO
         L     R6,REG0SAV     POINT TO FCB
         CLI   0(R6),X'00'    DEFAULT IMAGE ?
         BE    DFLTMGE       ZERO IS DO NOT USE AS DEFAULT IMAGE
         MVC   PRTDEF,=CL3'YES'
DFLTMGE EQU   *
         LA    R6,1(,R6)      BUMP TO SECOND BYTE
         SR    R7,R7          CLEAR
         IC    R7,0(R6)       PICK UP COUNT
         TM    1(R6),X'C0'    IS THERE PRT POS INDEX?
         BZ    CONV7          NO
         BCTR  R7,0           DEC CNT FOR PRT POS INDEX
CONV7    CVD   R7,DWORK         *
         MVC   PRTLEN,=X'40202020'
         ED    PRTLEN,DWORK+6    **  CONVERT LINE COUNT
         LA    R6,1(,R6)      BUMP TO THIRD BYTE
         TM    0(R6),X'C0'    IS PRT POS INDEX PRESENT?
         BZ    DOLPI
         MVI   PRTLOR,C'L'   SET TO LEFT
         TM    0(R6),X'80'    IS IT TO RIGHT?
         BZ    TOLEFT         NO
         MVI   PRTLOR,C'R'    SET TO RIGHT
TOLEFT   EQU   *
         NI    0(R6),X'3F'     TURN OFF DIR INDICATORS
         SR    R5,R5           CLEAR
         IC    R5,0(R6)        PICK UP OFFSET
         CVD   R5,DWORK        CONVERT
         MVC   PRTIDX,=X'40202020'
         ED    PRTIDX,DWORK+6
         LA    R6,1(,R6)      BUMP TO FORTH BYTE
DOLPI    MVI   PRTLPI,C'6'    SET TO SIX LINES PER INCH
         TM    0(R6),X'10'    EIGHT LINES/INCH ?
         BZ    DOCHANS        NO
         MVI   PRTLPI,C'8'    SET TO EIGHT LINES
DOCHANS  ZAP   LNECHAN,=P'1'  START LINE COUNT
         L     R9,PRTTABAD    GET GETMAIN ADDR
FCBDETL  EQU   *
         MVC   SAVBYTE,0(R6)  SAVE CURRENT BYTE
         NI    SAVBYTE,X'0F'  TURN OFF ANY HIGH BITS
         SR    R8,R8          CLEAR
         IC    R8,SAVBYTE     PICK UP CHANNEL
         LTR   R8,R8
         BZ    NODETL
         BCTR  R8,0           ADJ CHAN FOR PRINT ARRAY POSITION
         SLL   R8,2           MULT CHAN NUM BY 4
         LA    R10,0(R9,R8)   FORM ADDR OF CHAN FOR 1ST GROUP
FINDSP   CLC   0(4,R10),=CL4' '  CHECK FOR BLANKS
         BE    HAVSLOT        BLANK MEANS USE TABLE POSITION
         LA    R10,48(R10)    BUMP TO NEXT TABLE LINE
         B     FINDSP
HAVSLOT  MVC   0(4,R10),=X'40202020'
         ED    0(4,R10),LNECHAN    PUT CHAN LINE IN TABLE
NODETL   EQU   *
         AP    LNECHAN,=P'1'  BUMP CHANNEL LINE COUNT
         LA    R6,1(,R6)      BUMP TO NEXT BYTE
         BCT   R7,FCBDETL
         BCTR  R6,0           SET COUNT BACK TO CHECK LAST CHAR
         TM    0(R6),X'10'
         BO    MORCHAN
         CNOP  0,4
         BAL   R12,PUT
         DC    A(ERR3,FCBLIST)
MORCHAN  MVC   PRTCHAN,0(R9)      MOVE 1ST GROUP
         CNOP  0,4
         BAL   R12,PUT
         DC    A(OUTLNE,FCBLIST)
         MVC   0(48,R9),=CL48' '   CLEAR TABLE AREA
         CLI   PRTCAR,C' '         SEE IF SINGLE SPACING
         BE    ADVONE
         AP    LINECNT,=P'1'       BUMP LINE COUNT
ADVONE   AP    LINECNT,=P'1'       BUMP LINE COUNT
         MVI   PRTCAR,C' '         SET TO BLANKS
         MVC   PRTLNE(120),PRTCAR  CLEAR LINE TO BLANKS
         LA    R9,48(R9)           BUMP TO NEXT POSITION IN TABLE
         CLC   0(48,R9),=CL48' '   CHECK FOR MORE INFO IN TABLE
         BNE   MORCHAN
         L     R12,PRTRTN
         BR    R12
***********************************************************************
**********************************************************************
*
*  COMMON OUTPUT
*
PUT      EQU   *
         LM    R0,R1,0(R12)   PICK UP DCB AND I/O AREA
         PUT   (R1),(R0)      PUT IT OUT TO FILE
         B     8(0,R12)       RETURN
**********************************************************************
* SEARCH TABLE OF FCB NAMES TO VALIDATE PARM CARD
FCBSRCH  EQU   *
         L     R8,FCBTABAD    ADDR OF TABLE
         LH    R9,FCBCNT      NUMBER ITEMS IN TABLE
FSLOOP   CLC   FCBNAME,0(R8)  IS NAME IN TABLE ?
         BER   R12            YES
         LA    R8,8(,R8)      BUMP TO NEXT ITEM
         BCT   R9,FSLOOP      LOOP
         BR    R12            RETURN
***********************************************************************
*  CONSTANTS
SW       DC    X'00'          OPEN FILE SW.
ALL      DC    CL3'ALL'       INDICATOR TO PRINT ALL FCB'S
FCB2     DC    CL4'FCB2'      PREFIX TO FCB NAME
PARMSAVE DC    F'0'           POINTER TO PARM FIELD
RTNCODE  DC    F'0'           RETURN CODE
PRTRTN   DC    F'0'           RETURN ADDR FROM PRTFCB
FCBTABAD DC    F'0'           ADDR OF FCB TABLE
FCBCNT   DC    H'0'           COUNT OF ITEM IN FCB TABLE
IOAREA   DS    CL300          IOAREA FOR READ OF DIR.
         DS    0D             ALIGNMENT
FCBNAME  DC    CL8' '         NAME OF FCB TO BE PRINTED
REG0SAV  DC    F'0'           I/O AREA
         DS    0D
DWORK    DC    8X'00'         FOR CONVERTING
SAVBYTE  DC    X'00'          SAVE AREA FOR CURRENT BYTE
FSTPRT   DC    C'0'           IND TO SHOW FIRST PRINT OCCURED
PRTTABAD DC    F'0'           ADDR OF PRTINT TABLE
GETCORE  DC    F'0'           ADDR OF CORE FROM GET MAIN
LINECNT  DC    PL2'0'         PRINT LINE COUNT
NEWLSTFG DC    C'N'    USE NEW LISTING FORMAT FLAG Y = USE NEW
LNECHAN  DC    PL2'0'         LINE NUMBER FOR CHANNELS
RPTDTE   DC    CL12' '
***********************************************************************
******  DCB FOR PRINT FILE                                       ******
***********************************************************************
FCBLIST  DCB   DSORG=PS,                                               X
               DDNAME=W013600,                                         X
               MACRF=PM,                                               X
               LRECL=125,                                              X
               RECFM=VBA
***********************************************************************
******  DCB FOR SYS1.IMAGELIB TO LOAD FCB AS PROGRAMS            ******
***********************************************************************
ILIB     DCB   DSORG=PO,                                               X
               DDNAME=R013601,                                         X
               MACRF=R,                                                X
               EODAD=FCBLN1,                                           X
               RECFM=U,                                                X
               BLKSIZE=1024
***********************************************************************
******   DCB TO READ DIRECTORY OF SYS1.IMAGELIB          **************
***********************************************************************
DILIB    DCB   DDNAME=R013602,                                         X
               DSORG=PS,                                               X
               EODAD=ENDDIR,                                           X
               LRECL=256,                                              X
               RECFM=U,                                                X
               KEYLEN=8,                                               X
               MACRF=R,                                                X
               BLKSIZE=264
HL1      DC    Y(HL1END-*)
         DC    X'0000F1'
         DC    CL7' '
         DC    C'F O R M S    C O N T R O L    B U F F E R'
HL1END   EQU   *
OUTLNE   DC    Y(ENDPRT-*)
         DC    X'0000'
PRTCAR   DC    C'0'
PRTLNE   DS    0CL(120)
RPTFCB   DS    CL4           FCB NAME
         DS    CL3
PRTDEF   DS    CL3           DEFAULT YES OR NO
         DS    CL4
PRTLPI   DS    CL1           6 OR 8 LINES PER INCH
PRTIDX   DS    CL4           UP TO 32 POSITIONS OFFSET
         DS    CL1
PRTLOR   DS    CL1       L=LEFT OR R=RIGHT FOR INDEX OFFSET POSITION
         DS    CL2
PRTLEN   DS    CL4           LENGHT OF FORM FOLD IN LINES
         DS    CL2
PRTCHAN  DS    CL48          CHANNEL LINES GROUP
         DS    CL43
ENDPRT   EQU   *
RH1      DC    Y(RH1END-*)
         DC    X'0000F1'
         DC    CL10'LIBRARY = '
HDRLIB   DC    CL16'SYS1.IMAGELIB '
         DC    CL55'F O R M S    C O N T R O L    B U F F E R'
         DC    CL3'ON '
HDRDTE   DC    CL8'        '
         DC    CL4' AT '
HDRTIME  DC    CL9'        '
RH1END   EQU   *
         LTORG
***********************************************************************
HL2      DC    Y(HL2END-*)
         DC    X'0000'
         DC    C'0'
         DC    C'FCB  DEFAULT LPI INDEX LINES      C H A N N E L  AT'
         DC    C'   F O R M   L I N E S '
HL2END   EQU   *
HL3      DC    Y(HL3END-*)
         DC    X'000040'
         DC    C'NAME  ALLOW      POSIT LONG     1   2   3   4   5   6'
         DC    C'   7   8   9  10  11  12  '
HL3END   EQU   *
LN2      DC    Y(LN2END-*)
         DC    X'0000'
         DC    C'-'
         DC    C'    FCB NAME: '
L2NAME   DC    CL8' '
LN2END   EQU   *
LN3      DC    Y(LN3END-*)
         DC    X'0000F0'
         DC    C'    SYS1.IMAGELIB'
LN3END   EQU   *
LN4      DC    Y(LN4END-*)
         DC    X'000040'
         DC    C'   ---------------'
LN4END   EQU   *
LN5A     DC    Y(LN5AEND-*)
         DC    X'0000F0'
         DC    C'   THIS IS NOT A DEFAULT IMAGE'
LN5AEND  EQU   *
LN5B     DC    Y(LN5BEND-*)
         DC    X'0000F0'
         DC    C'   THIS IS A DEFAULT IMAGE'
LN5BEND  EQU   *
LN6      DC    Y(LN6END-*)
         DC    X'0000F0'
         DC    C'   NUMBER OF LINES: '
L6CNT    DC    CL3' '
LN6END   EQU   *
LN7A     DC    Y(LN7AEND-*)
         DC    X'0000F0'
         DC    C'   EIGHT LINES PER INCH'
LN7AEND  EQU   *
LN7B     DC    Y(LN7BEND-*)
         DC    X'0000F0'
         DC    C'   SIX LINES PER INCH'
LN7BEND  EQU   *
PPLN     DC    Y(PPLNEND-*)   PRINT POS LINE
         DC    X'0000F0'
         DC    C'   PRINT POS INDEX IS '
PPCNT    DC    CL3' '
         DC    C' '
PPDIR    DC    C'LEFT '
PPLNEND  EQU   *
LN8      DC    Y(LN8END-*)
         DC    X'0000F0'
         DC    C'   FOLLOWING LINES HAVE CHANNELS:'
LN8END   EQU   *
LN9      DC    Y(LN9END-*)
         DC    X'0000F0'
         DC    C'    LINE NO      CHANNEL'
LN9END   EQU   *
DETLINE  DC    Y(DETEND-*)
         DC    X'000040'
         DC    CL6' '
DETLN    DC    CL3' '
         DC    CL10' '
DETCA    DC    CL3' '
DETEND   EQU   *
*
ERR1     DC    Y(ERR1END-*)   ERROR IN PARM CARD
         DC    X'0000F1'      NEW PAGE
         DC    C'  THERE IS AN ERROR IN YOUR PARM CARD '
         DC    C'CORRECT AND RERUN JOB'
ERR1END  EQU   *
*
ERR2     DC    Y(ERR2END-*)   FCB NOT FOUND ERROR
         DC    X'0000F1'      NEW PAGE
         DC    CL3' '         FILLER
ERR2NAM  DC    CL8' '         NAME OF FCB NOT FOUND
         DC    C'   WAS NOT FOUND ON SYS1.IMAGELIB'
ERR2END  EQU   *
*
ERR3     DC    Y(ERR3END-*)
         DC    X'0000F1'
         DC    C'   LAST LINE NOT FLAGED OR INVLD LINE COUNT'
ERR3END  EQU   *
*
ERR4     DC    Y(ERR4END-*)
         DC    X'0000F1'
         DC    C'   I/O ERROR READING SYS1.IMAGELIB'
ERR4END  EQU   *
         LTORG
         END
