         TITLE 'DISCLAIMER - INSTALLATION INSTRUCTIONS'
***********************************************************************
*                                                                     *
*     THIS PROGRAM, DEVELOPED AT FPL OR SUPPLIED BY OTHER USERS       *
*     ON A NON-RESTRICTIVE BASIS, IS OF GENERAL INTEREST              *
*     SUBMITTED FOR UNRESTRICTED DISTRIBUTION.  THIS PROGRAM          *
*     HAS MET A BASIC SET OF PROGRAMMING AND DOCUMENTATION            *
*     STANDARDS, BUT MAY NOT HAVE BEEN PROGRAM TESTED IN ANY          *
*     FORMAL FASHION BY FPL.  THE USER IS EXPECTED TO MAKE THE        *
*     FINAL EVALUATION AS TO THE USEFULLNESS IN HIS OWN               *
*     ENVIRONMENT.                                                    *
*                                                                     *
*     FPL MAKES NO WARRANTY, EXPRESSED OR IMPLIED, INCLUDING, BUT     *
*     NOT LIMITED TO, THE IMPLIED WARRANTIES OR MERCHANTABILITY AND   *
*     FITNESS FOR A PARTICULAR PUTPOSE AS TO THE DOCUMENTATION,       *
*     FUNCTION, OR PERFORMANCE OF THESE PROGRAMS.                     *
*                                                                     *
*     ACCEPTANCE AND USE OF THIS PROGRAM CONSTITUTES A RELEASE        *
*     FROM LIABILITY OF FPL FOR ANY PROBLEMS USE OF THE PROGRAM       *
*     MAY CAUSE AT THE USER'S INSTALLATION.                           *
*                                                                     *
*     USERS ARE INVITED TO SUBMIT SUGGESTIONS OR ERROR DOCUMENTATION  *
*     TO FPL, HOWEVER, NO PROMISE CAN BE MADE THAT SUCH SUGGESTIONS   *
*     WILL BE IMPLEMENTED OR ERRORS CORRECTED.  SUBMIT COMMENTS TO:   *
*              COORDINATOR OF TECHNICAL SYSTEMS                       *
*              COMPUTER OPERATIONS DEPARTMENT                         *
*              FLORIDA POWER & LIGHT COMPANY - GENERAL OFFICE         *
*              P. O. BOX  529100                                      *
*              MIAMI, FLORIDA  33152                                  *
*                                                                     *
*     THIS PROGRAM IS MADE AVAILABLE BY FPL WITHOUT CHARGE OR         *
*     CONSIDERATION.  RECIPIENTS ARE FREE TO MAKE THIS PROGRAM        *
*     AVAILABLE TO OTHERS IN LIKE MANNER.  IT MAY NOT BE SOLD.        *
*                                                                     *
***********************************************************************
TAPEDUP  TITLE 'TAPE DUPLICATION PROGRAM'
***********************************************************************
*                                                                     *
* NAME         SS0278 ALIAS TAPEDUP                                   *
*                                                                     *
* FUNCTION     DUPLICATE A STANDARD LABEL OR NON-LABELLED TAPE.  A    *
*              TAPE MAP LISTING IS ALSO PRODUCED, WHICH SHOWS:        *
*                                                                     *
*               1.  FILE NUMBER.                                      *
*               2.  DATA SET NAME (SL ONLY).                          *
*               3.  RECORD FORMAT.                                    *
*               4.  RECORD LENGTH (SL ONLY).                          *
*               5.  BLOCK SIZE.                                       *
*               6.  NUMBER OF BLOCKS.                                 *
*               7.  USER LABEL COUNT (SL ONLY).                       *
*               8.  RECORDING DENSITY.                                *
*               9.  LENGTH OF FILE (IN FEET).                         *
*              10.  CUMULATIVE TAPE LENGTH (IN FEET).                 *
*              11.  FILE CREATION DATE (SL ONLY).                     *
*              12.  FILE EXPIRATION DATE (SL ONLY).                   *
*              13.  NAME OF JOB WHICH CREATED FILE (SL ONLY).         *
*              14.  NAME OF STEP WHICH CREATED FILE (SL ONLY).        *
*              15.  SECURITY CODE (SL ONLY).                          *
*              16.  VOLUME SEQUENCE NUMBER (SL ONLY).                 *
*                                                                     *
*              ITEMS INDICATED WITH SL ONLY ARE DISPLAYED ONLY FOR    *
*              STANDARD LABEL TAPES.                                  *
*                                                                     *
* DESCRIPTION  BOTH THE INPUT AND OUTPUT TAPES ARE PROCESSED BY THE   *
*              PROGRAM AS BLP.  IF THE FIRST RECORD OF THE INPUT TAPE *
*              IS A VOL1 HEADER, THE TAPE IS CONSIDERED STANDARD      *
*              LABEL, OTHERWISE IT IS CONSIDERED NON-LABELLED.        *
*                                                                     *
*              THE OUTPUT TAPE WILL BE WRITTEN WITH THE SAME LABEL    *
*              TYPE AS THE INPUT TAPE REGARDLESS OF ITS ORIGINAL      *
*              LABEL TYPE.  FIELDS IN THE LABELS OF STANDARD LABEL    *
*              TAPE ARE UPDATED.  THOSE FIELDS ARE:                   *
*                                                                     *
*              VOL1 - VOLUME SERIAL (UNLESS CPYVOLID=YES IS           *
*                        SPECIFIED.                                   *
*                                                                     *
*              HDR1                                                   *
*              EOF1                                                   *
*              EOV1 - CREATION DATE (SET TO CURRENT DATE)             *
*                     VOLUME SERIAL (IF VOLUME SEQUENCE NUMBER IS 1)  *
*                     FILE SEQUENCE NUMBER (IF VOLUME SEQUENCE NUMBER *
*                        IS 1 AND THE FILE NUMBER ON THE INPUT TAPE   *
*                        IS INCORRECT).                               *
*                                                                     *
*              HDR2                                                   *
*              EOF2                                                   *
*              EOV2 - JOBNAME/STEPNAME (SET TO COPY OF /XXXXXX WHERE  *
*                        XXXXXX IS THE OUTPUT VOLUME SERIAL).         *
*                     DENSITY CODE (SET TO OUTPUT TAPE DENSITY).      *
*                                                                     *
*              THE VOLUME SERIAL OF THE OUTPUT TAPE IS TAKEN FROM THE *
*              DD STATEMENT UNLESS CPYVOLID=YES IS SPECIFIED.  THE    *
*              OUTPUT TAPE DENSITY IS DETERMINED BY THE DCB DEN       *
*              SPECIFICATION IN THE JCL OR THE DEFAULT TAPE DENSITY.  *
*                                                                     *
*              FOR STANDARD LABEL TAPES, ALL INFORMATION DISPLAYED    *
*              FOR EACH FILE IS TAKEN FROM THE LABELS.  THE VOLUME    *
*              SERIAL IS TAKEN FROM THE VOL1 HEADER RECORD, AND THE   *
*              DENSITY DISPLAYED IN THE HEADER IS THE TRUE TAPE       *
*              RECORDING DENSITY.  THE DATA SET NAME FIELD CONTAINS   *
*              ONLY 17 CHARACTERS.  THEY ARE THE LAST 17 CHARACTERS   *
*              OF THE DATA SET NAME SPECIFIED WHEN THE TAPE WAS       *
*              CREATED.                                               *
*                                                                     *
*              FOR NON-LABELLED TAPES, THE PROGRAM ATTEMPTS TO        *
*              IDENTIFY THE RECORD FORMAT AND BLKSIZE AS FOLLOWS:     *
*                                                                     *
*                   THE RECORD FORMAT IS DISPLAYED AS V IF THE FIRST  *
*                   HALFWORD OF EACH BLOCK IS EQUAL TO THE LENGTH OF  *
*                   THE BLOCK.  IF NOT, BUT ALL RECORDS ARE THE SAME  *
*                   LENGTH, OR ONLY THE LAST BLOCK IS SHORT, THE      *
*                   RECORD FORMAT IS INDICATED AS F (THIS IS ACTUALLY *
*                   FS FORMAT).  THE RECORD FORMAT IS DISPLAYED AS U  *
*                   IF THE ABOVE CONTIDIONS ARE NOT MET.              *
*                                                                     *
*                   BLKSIZE IS GIVEN AS THE LARGEST BLOCK READ.       *
*                                                                     *
*              FOR BOTH STANDARD AND NON-LABELLED TAPES, THE FOOTAGE  *
*              CALCULATIONS ARE BASED ON THE MAXIMUM BLOCK SIZE,      *
*              NOMINAL INTERBLOCK GAP LENGTH, AND THE TRUE TAPE       *
*              DENSITY.                                               *
*                                                                     *
*              PROCESSING IS TERMINATED BY A DOUBLE TAPE MARK.        *
*                                                                     *
* ENVIRONMENT  OS/VS2, OS/MVT                                         *
*              APF AUTHORIZED (MVS)                                   *
*              BACKGROUND                                             *
*                                                                     *
* INPUT        THE ONLY INPUT SOURCE IS THE TAPE TO BE COPIED.        *
*                                                                     *
* OUTPUT       OUTPUT CONSISTS OF A TAPE COPY OF THE INPUT TAPE WHICH *
*              IS IDENTICAL TO THE INPUT TAPE EXCEPT FOR:             *
*                   TAPE DENSITY                                      *
*                   CERTAIN FIELDS OF STANDARD LABELS                 *
*              AND A TAPE MAP:                                        *
*                                                                     *
*              FIELD DESCRIPTIONS (HEADER)                            *
*                                                                     *
*                   VOLUME SERIAL - THE VOLUME SERIAL OF THE OUTPUT   *
*                        TAPE.  THE VOLUME SERIAL IS OBTAINED FROM    *
*                        THE SYSUT2 DD STATEMENT UNLESS CPYVOLID=YES  *
*                        IS SPECIFIED AND THE INPUT TAPE IS STANDARD  *
*                        LABEL.  IN THAT CASE, THE OUTPUT TAPE WILL   *
*                        HAVE THE SAME VOLUME SERIAL AS THE INPUT     *
*                        TAPE.                                        *
*                                                                     *
*                   COPY OF - THE VOLUME SERIAL OF THE INPUT TAPE.    *
*                        FOR STANDARD LABEL TAPES, THE SERIAL IS      *
*                        OBTAINED FROM THE VOL1 HEADER RECORD.  FOR   *
*                        NON-LABELLED TAPES, IT IS THE SERIAL NUMBER  *
*                        SPECIFIED IN THE SYSUT1 DD STATEMENT.        *
*                                                                     *
*                   DENSITY - THIS IS THE TRUE TAPE RECORDING DENSITY *
*                        EXPRESSED AS BOTH A CODE (0-4) AND AS BPI.   *
*                                                                     *
*                   LABEL TYPE - DEFINES THE LABEL TYPE OF THE TAPE.  *
*                        IT IS EITHER STANDARD, NON-LABELLED, OR LTM. *
*                        LTM INDICATES A NON-LABELLED TAPE WITH A     *
*                        LEADING TAPE MARK.                           *
*                                                                     *
*                   OWNER ID - THE OWNER ID IS TAKEN FROM THE VOL1    *
*                        HEADER OF A STANDARD LABEL TAPE.  IT IS      *
*                        BLANK FOR A NON-LABELLED TAPE.               *
*                                                                     *
*                   PAGE - PAGE NUMBER.                               *
*                                                                     *
*              FIELD DESCRIPTIONS (DATA LINES)                        *
*                                                                     *
*                   FILE NUMBER - THE FILE SEQUENCE NUMBER.  FOR      *
*                        STANDARD LABEL TAPES IT IS OBTAINED FROM THE *
*                        HDR1 LABEL.                                  *
*                                                                     *
*                   DATA SET NAME - THE LAST 17 CHARACTERS OF THE     *
*                        DATA SET NAME SPECIFIED WHEN THE DATA SET    *
*                        WAS CREATED.  IT IS OBTAINED FROM THE HDR1   *
*                        RECORD OF A STANDARD LABEL TAPE AND IS NOT   *
*                        PRINTED FOR NON-LABELLED TAPES.              *
*                                                                     *
*                   RECFM - RECORD FORMAT.  FOR STANDARD LABEL TAPES, *
*                        THIS VALUE IS OBTAINED FROM THE HDR2 LABEL.  *
*                        FOR NON-LABELLED TAPES, IT IS V IF THE FIRST *
*                        HALFWORD OR EACH RECORD EQUALS THE LENGTH OF *
*                        THE RECORD.  IF NOT, IT IS F IF ALL BLOCKS   *
*                        ARE THE SAME LENGTH OR ALL BLOCKS ARE THE    *
*                        SAME LENGTH EXCEPT THE LAST BLOCK WHICH IS   *
*                        SHORTER.  IT IS U IF THE ABOVE CONDITIONS    *
*                        ARE NOT MET.                                 *
*                                                                     *
*                   LRECL - LOGICAL RECORD LENGTH.  FOR STANDARD      *
*                        LABEL TAPES THIS VALUE IS OBTAINED FROM THE  *
*                        HDR2 LABEL.  IT IS NOT PROVIDED FOR          *
*                        NON-LABELLED TAPES.                          *
*                                                                     *
*                   BLKSIZE - MAXIMUM BLOCK (PHYSICAL RECORD) SIZE.   *
*                        FOR STANDARD LABEL TAPES, THIS VALUE IS      *
*                        OBTAINED FROM THE HDR2 LABEL.  FOR           *
*                        NON-LABELLED TAPES, IT IS THE SIZE OF THE    *
*                        LARGEST BLOCK READ.                          *
*                                                                     *
*                   BLOCKS - THE NUMBER OF BLOCKS IN THE FILE.  FOR   *
*                        STANDARD LABEL TAPES, THIS VALUE IS OBTAINED *
*                        FROM THE EOF1 OR EOV1 LABEL.  FOR            *
*                        NON-LABELLED TAPES, IT IS THE NUMBER OF      *
*                        BLOCKS READ.                                 *
*                                                                     *
*                   UL - USER LABEL COUNT.  THIS VALUE IS NOT PRINTED *
*                        FOR NON-LABELLED TAPES.                      *
*                                                                     *
*                   DEN - TAPE DENSITY.  THIS VALUE IS DETERMINED BY  *
*                        THE DCB DEN PARAMETER ON THE SYSUT2 DD       *
*                        STATEMENT OR THE DEFAULT TAPE DENSITY.       *
*                                                                     *
*                   F.  FT.  - NUMBER OF FEET IN THE FILE.  THIS      *
*                        VALUE IS BASED ON THE TRUE DENSITY, BLOCK    *
*                        COUNT, MAXIMUM BLOCK SIZE, AND NOMINAL       *
*                        INTERBLOCK GAP SIZES.                        *
*                                                                     *
*                   R.  FT.  - ACCUMULATED REEL FOOTAGE.              *
*                                                                     *
*                   CR.  DATE - THIS VALUE WILL BE THE CURRENT DATE   *
*                        FOR STANDARD LABEL TAPES AND IS NOT PROVIDED *
*                        FOR NON-LABELLED TAPES.                      *
*                                                                     *
*                   EX.  DATE - THE EXPIRATION DATE.  THIS VALUE IS   *
*                        OBTAINED FROM THE HDR1 LABEL AND IS NOT      *
*                        PROVIDED FOR NON-LABELLED TAPES.             *
*                                                                     *
*                   JOBNAME - THIS VALUE IS SET TO "COPY OF" FOR      *
*                        STANDARD LABEL TAPES AND IS NOT PROVIDED FOR *
*                        NON-LABELLED TAPES.                          *
*                                                                     *
*                   STEPNAME - THIS VALUE IS SET TO THE VOLUME SERIAL *
*                        OF THE INPUT TAPE FOR STANDARD LABEL TAPES,  *
*                        AND IS NOT PROVIDED FOR NON-LABELLED TAPES.  *
*                                                                     *
*                   SEC - SECURITY INDICATOR.  THIS VALUE IS OBTAINED *
*                        FROM THE HDR1 LABEL AND IS NOT PROVIDED FOR  *
*                        NON-LABELLED TAPES.THE MEANING OF THE CODES  *
*                        ARE:                                         *
*                        0 - NO SECURITY.                             *
*                        1 - READ, WRITE, DELETE SECURITY.            *
*                        3 - WRITE AND DELETE SECURITY.               *
*                                                                     *
*                   VSEQ - VOLUME SEQUENCE NUMBER.  THIS VALUE IS     *
*                        OBTAINED FROM THE HDR1 LABEL AND IS THE      *
*                        ORDER OF THE VOLUME WITHIN A MULTI-VOLUME    *
*                        GROUP CREATED AT THE SAME TIME.              *
*                                                                     *
* CONSIDERATIONS                                                      *
*                                                                     *
*              ABEND CODES:                                           *
*                                                                     *
*                   U1000 - SYSPRINT DD STATEMENT MISSING (NO DUMP).  *
*                                                                     *
*                   U1001 - PROCESSING ERROR.  THIS ABEND WILL BE     *
*                        PRECEEDED BY AN ERROR MESSAGE TO SYSPRINT    *
*                        AND WILL PRODUCE A DUMP.                     *
*                                                                     *
*              ERROR MESSAGES:                                        *
*                                                                     *
*                   UNABLE TO OPEN SYSUTX DD STATEMENT.  CODE=16.     *
*                        THE SYSUTX DD STATEMENT FAILED TO OPEN.  THE *
*                        RUN TERMINATES WITH CONDITION CODE 16.  NO   *
*                        DUMP IS PRODUCED.                            *
*                                                                     *
*                   PERMANENT I/O ERROR:  XXXXXXXXXX                  *
*                        A PERMANENT I/O ERROR WAS DETECTED.  THE     *
*                        DATA REPRESENTED BY XXXXXXXXXX IS PROVIDED   *
*                        BY THE SYNAD ROUTINE.  SEE THE SYNADAF MACRO *
*                        DESCRIPTION FOR A DESCRIPTION OF THIS DATA.  *
*                        THIS MESSAGE IS FOLLOWED BY A U1001 ABEND.   *
*                                                                     *
*                   TAPE MARK EXPECTED, NOT FOUND                     *
*                        A TAPE MARK WAS EXPECTED AT THE END OF THE   *
*                        DATA FILE, BUT WAS NOT DETECTED.  THIS       *
*                        MESSAGE IS FOLLOWED BY A U1001 ABEND.        *
*                                                                     *
*                   UNEXPECTED TAPE MARK ENCOUNTERED ATTEMPTING TO    *
*                   READ XXXX RECORD                                  *
*                        DURING PROCESSING OF A STANDARD LABEL TAPE,A *
*                        TAPE MARK WAS DETECTED DURING AN ATTEMPT TO  *
*                        READ THE RECORD TYPE DEFINED BY XXXX.  THIS  *
*                        MESSAGE IS FOLLOWED BY A U1001 ABEND.        *
*                                                                     *
*                   XXXX RECORD TYPE EXPECTED, NOT FOUND              *
*                        DURING PROCESSING OF A STANDARD LABEL TAPE,  *
*                        RECORD TYPE XXXX WAS EXPECTED, BUT WAS NOT   *
*                        FOUND.  THIS MESSAGE IS FOLLOWED BY A U1001  *
*                        ABEND.                                       *
*                                                                     *
*                   VOLUME SERIAL OF INPUT TAPE XXXXXX DOES NOT MATCH *
*                   VOLUME SERIAL IN DD YYYYYY.                       *
*                        THE VOLUME SERIAL OF THE INPUT TAPE          *
*                        (STANDARD LABEL) DOES NOT MATCH THE VOLUME   *
*                        SERIAL SPECIFIED ON THE SYSUT1 DD STATEMENT. *
*                        THIS IS A WARNING MESSAGE.                   *
*                                                                     *
*                   VOLUME SERIALS ARE REVERSED.  RUN TERMINATED,     *
*                   CODE=16.                                          *
*                        THE VOLUME SERIAL OF THE INPUT TAPE          *
*                        (STANDARD LABEL) MATCHES THE VOLUME SERIAL   *
*                        SPECIFIED ON THE SYSUT2 DD STATEMENT.  THE   *
*                        VOLUMES HAVE PROBABLY BEEN SWITCHED WHEN     *
*                        MOUNTED AND THE RUN IS TERMINATED TO AVOID   *
*                        DESTROYING THE TAPE.                         *
*                                                                     *
*                   OUTPUT I/O ERROR                                  *
*                        AN I/O ERROR OCCURRED WRITING THE OUTPUT     *
*                        TAPE.  THIS MESSAGE IS FOLLOWED BY A U1001   *
*                        ABEND.                                       *
*                                                                     *
* PARAMETERS   THE PARAMETER FIELD MAY BE USED TO INDICATE THAT THE   *
*              VOLUME SERIAL IS TO BE COPIED FOR STANDARD LABEL       *
*              TAPES.  IF CPYVOLID=YES IS SPECIFIED AND THE INPUT     *
*              TAPE IS STANDARD LABEL, THE OUTPUT TAPE WILL RETAIN    *
*              THE VOLUME SERIAL OF THE INPUT TAPE.                   *
*                                                                     *
* EXECUTION    DDNAMES                                                *
*                                                                     *
*              SYSUT1   DEFINES THE TAPE TO BE COPIED.  ONLY UNIT AND *
*                       VOLUME SERIAL ARE REQUIRED.  THE VOLUME       *
*                       SERIAL IS USED IN THE MESSAGE TO THE OPERATOR *
*                       TO MOUNT THE TAPE.  COPYING ALWAYS BEGINS AT  *
*                       FILE 1.                                       *
*                                                                     *
*              SYSUT2   DEFINES THE OUTPUT TAPE.  ONLY UNIT AND       *
*                       VOLUME SERIAL ARE REQUIRED.  THE DCB DEN      *
*                       PARAMETER MAY BE USED TO SPECIFY THE DENSITY  *
*                       OF THE OUTPUT TAPE.                           *
*                                                                     *
*              SYSPRINT DEFINES THE OUTPUT LISTING DATA SET.          *
*                                                                     *
* RESTRICTIONS AND LIMITATIONS                                        *
*                                                                     *
*              IF THE SERIAL OF THE OUTPUT TAPE IS UNKNOWN AT THE     *
*              TIME THE JOB IS SUBMITTED (THE OPERATOR WILL MOUNT A   *
*              SCRATCH TAPE) AN IEBGENER STEP SHOULD BE RUN PRIOR TO  *
*              THE COPY STEP TO CAUSE A SCRATCH TAPE TO BE MOUNTED.   *
*              THE COPY STEP CAN THEN OBTAIN THE SERIAL OF THE        *
*              SCRATCH TAPE BY DOING A REFER BACK TO THE IEBGENER     *
*              STEP.  THIS STEP IS ALSO NECESSARY SO THAT THE TAPE    *
*              LIBRARY SYSTEM WILL BE ABLE TO IDENTIFY THE OUTPUT     *
*              TAPE (IT CANNOT IDENTIFY A TAPE WHEN BYPASS LABEL      *
*              PROCESSING IS USED).                                   *
*                                                                     *
*              THE EXCP COUNT SUPPLIED BY SMF FOR THE OUTPUT TAPE     *
*              DOES NOT INDICATE THE ACTUAL BLOCK COUNT.  THE PROGRAM *
*              QUEUES UP DATA AND USES COMMAND CHAINING TO WRITE      *
*              MULTIPLE BLOCKS WITH A SINGLE EXCP.  THE COUNT         *
*              SUPPLIED BY SMF IS THE NUMBER OF TIMES EXCP WAS        *
*              ISSUED, NOT THE ACTUAL BLOCK COUNT.                    *
*                                                                     *
* EXAMPLES                                                            *
*                                                                     *
*              //DUP    EXEC  PGM=TAPEDUP                             *
*              //SYSPRINT DD  SYSOUT=*                                *
*              //SYSUT1   DD  UNIT=TAPE,VOL=SER=XXXXXX                *
*              //SYSUT2   DD  UNIT=TAPE,VOL=SER=YYYYYY                *
*              //SYSUDUMP DD  SYSOUT=*                                *
*                                                                     *
*              THE OUTPUT VOLUME SERIAL IS NOT KNOWN WHEN THE JOB IS  *
*              SUBMITTED.  THE OPERATOR WILL MOUNT A SCRATCH VOLUME.  *
*              AN IEBGENER STEP IS USED SO THAT THE TAPEDUP PROGRAM   *
*              AND THE TAPE LIBRARY SYSTEM WILL KNOW THE VOLUME       *
*              SERIAL OF THE SCRATCH TAPE.                            *
*                                                                     *
*              //GENER  EXEC  PGM=IEBGENER                            *
*              //SYSPRINT DD  DUMMY                                   *
*              //SYSIN    DD  DUMMY                                   *
*              //SYSUT1   DD  DUMMY,DCB=(RECFM=F,LRECL=80,BLKSIZE=80) *
*              //SYSUT2   DD  DISP=(NEW,PASS),                        *
*              //             DSNAME=X,                               *
*              //             LABEL=(1,SL),                           *
*              //             UNIT=TAPE                               *
*              //DUP    EXEC  PGM=TAPEDUP                             *
*              //SYSPRINT DD  SYSOUT=*                                *
*              //SYSUT1   DD  UNIT=TAPE,VOL=SER=XXXXXX                *
*              //SYSUT2   DD  UNIT=TAPE,                              *
*              //             VOL=REF=*.GENER.SYSUT2                  *
*              //SYSUDUMP DD  SYSOUT=*                                *
*                                                                     *
* FPL MACROS USED  (IN MAINLINE OR SUBROUTINES)                       *
*              $ENTER   - ENTRY LINKAGE                               *
*              $RTRN    - RETURN LINKAGE                              *
*              $CALL    - INVOKE A SUBROUTINE                         *
*                                                                     *
* IBM MACROS USED  (IN MAINLINE OR SUBROUTINES)                       *
*              ABEND    - ABNORMAL TERMINATE                          *
*              CHECK    - WAIT FOR I/O COMPLETION                     *
*              CLOSE    - CLOSE A DATA FILE                           *
*              DCB      - DEFINE A DATA CONTROL BLOCK                 *
*              EXCP     - EXECUTE CHANNEL PROGRAM (PHYSICAL I/O)      *
*              IEFJFCBN - MAP JOB FILE CONTROL BLOCK                  *
*              OPEN     - OPEN A DATA FILE                            *
*              PUT      - PRINT A DATA RECORD                         *
*              RDJFCB   - READ JOB FILE CONTROL BLOCK                 *
*              READ     - READ A BLOCK FROM TAPE                      *
*              SYNADAF  - ANALYZE SYNAD ERROR                         *
*              SYNALRLS - RELEASE SYNAD WORK AREA                     *
*              TIME     - TIME                                        *
*              WAIT     - WAIT                                        *
*                                                                     *
* AUTHOR       THIS PROGRAM WAS BASED ON A PROGRAM WRITTEN BY         *
*              C. L. TURNER (UMSL) AND RECEIVED FROM LSU.             *
*              THE ORIGINAL PROGRAM INCLUDED MODIFICATIONS BY         *
*              R. NESTOR,  P. J. CAUDLL,  E. R. VANCE, (UMSL),        *
*              AND ERIC STRATMANN (LSU DEPT OF EXP. STAT.).           *
*                                                                     *
*              IT WAS RE-WRITTEN BY GORDON P. WEST OF FLORIDA         *
*              POWER & LIGHT COMPANY TO INCLUDE ADDITIONAL COMMENTS,  *
*              MNEMONIC REGISTER NOTATION, 6250 BPI SUPPORT,          *
*              LTM SUPPORT, REVISED OUTPUT FORMAT, CONSOLIDATION OF   *
*              NL AND SL SUBROUTINES, AUTOMATIC BLP SPECIFICATION     *
*              (IF NOT SPECIFIED IN JCL), AND A FEW OTHER MINOR       *
*              GOODIES.                                               *
*                                                                     *
*              THE TAPE MAPPING PROGRAM WAS USED AS A BASIS FOR THIS  *
*              TAPE DUPLICATION PROGRAM BY ADDING THE OUTPUT CODING   *
*              AND MODIFICATIONS FOR STANDARD LABEL UPDATING.         *
*                                                                     *
* REGISTER USAGE                                                      *
*                                                                     *
*              R0  - LINKAGE                                          *
*              R1  - LINKAGE                                          *
*              R2  - FILE COUNTER                                     *
*              R3  - LINE COUNTER                                     *
*              R4  - NUMBER OF BLOCK SIZE CHANGES (NL TAPES)          *
*              R5  - BLOCK COUNT (NL TAPES) / USER LABEL COUNT (SL)   *
*              R6  - RECORD BASE ADDRESS                              *
*              R7  - ACCUMULATED DATA LENGTH                          *
*              R8  - CCW COUNT                                        *
*              R9  - INTERNAL LINKAGE REGISTER                        *
*              R10 - LINE INCREMENT                                   *
*              R11 - LINE LIMIT                                       *
*              R12 - PROGRAM BASE REGISTER                            *
*              R13 - SAVE AREA ADDRESS - DATA BASE REGISTER           *
*              R14 - LINKAGE                                          *
*              R15 - LINKAGE                                          *
*                                                                     *
***********************************************************************
         SPACE 200009500
***********************************************************************
*                                                                     *
*        THE FOLLOWING PUNCH STATEMENT(S) CAUSE LINKAGE EDITOR        *
*        CONTROL STATEMENTS TO BE INCLUDED IN THE OBJECT OUTPUT.      *
*                                                                     *
*        IF MESSAGE IEW0731 IS GENERATED (ALIAS NAME MATCHES MEMBER   *
*        NAME) IT MAY BE IGNORED'                                     *
*                                                                     *
***********************************************************************
*
         PUNCH '  ALIAS TAPEDUP  &SYSDATE  &SYSTIME     IGNORE IEW0731'
         PUNCH '  SETCODE AC(1)            REMOVE IF NOT MVS'
         TITLE 'ENTRY LINKAGE'
SS0278   $ENTER BASE=R12,                                              +
               SAVE=SAVE
         USING SAVE,R13
         MVI   FLAGS,X'00'             CLEAR INDICATORS
         L     R2,0(R1)                LOAD PARM ADDRESS
         CLC   =H'12',0(R2)            TEST PARM LENGTH
         BNE   OPEN
         CLC   =C'CPYVOLID=YES',2(R2)  TEST PARM VALUE
         BNE   OPEN
         OI    FLAGS,$COPYSER          INDICATE COPY SERIAL
         TITLE 'OPEN FILES'
***********************************************************************
*                                                                     *
*        OPEN FILES                                                   *
*                                                                     *
*        LABEL IS FORCED TO 1,BLP.  UNDER MVS, A PROGRAM MUST         *
*        BE APF AUTHORIZED TO CHANGE THE LABEL TYPE TO BLP.           *
*                                                                     *
***********************************************************************
         SPACE
OPEN     OPEN  (SYSPRINT,OUTPUT)       OPEN PRINT FILE
         TM    SYSPRINT+48,X'10'       TEST FOR SUCCESSFUL OPEN
         BO    OPENTAPE
         ABEND 1000
OPENTAPE RDJFCB (INTAPE,,              READ JFCB'S                     +
               OUTTAPE,OUTPUT)
         USING JFCBMAP,R1              JFCB ADDRESSABILITY
         LA    R1,INJFCB               INPUT JFCB ADDRESS
         MVC   JFCBFLSQ,=H'1'          SET FILE NUMBER TO 1
         MVI   JFCBLTYP,JFCBLP         SET BLP  (APF AUTHORIZATION)
         OI    JFCBMASK,X'80'          REWRITE JFCB
         MVC   INSER,JFCBVOLS          COPY INPUT VOLUME SERIAL
         SPACE
         LA    R1,OUTJFCB              OUTPUT JFCB ADDRESS
         MVC   JFCBFLSQ,=H'1'          SET FILE NUMBER TO 1
         MVI   JFCBLTYP,JFCBLP         SET BLP  (APF AUTHORIZATION)
         OI    JFCBMASK,X'80'          REWRITE JFCB
         MVC   OUTVOL,JFCBVOLS         COPY OUTPUT VOLUME SERIAL
         DROP  R1
         SPACE
         OPEN  (INTAPE,,               OPEN TAPES                      +
               OUTTAPE,OUTPUT),                                        +
               TYPE=J
         TM    INTAPE+48,X'10'         TEST FOR SUCCESSFUL OPEN
         BO    CHECKOUT
         MVI   SYSUTX,C'1'
         PUT   SYSPRINT,NOTAPE
         OI    FLAGS,$OPENERR
CHECKOUT TM    OUTTAPE+48,X'10'        TEST FOR SUCCESSFUL OPEN
         BO    CHECKERR
         MVI   SYSUTX,C'2'
         PUT   SYSPRINT,NOTAPE
         OI    FLAGS,$OPENERR
CHECKERR TM    FLAGS,$OPENERR          TEST FOR OPEN ERRORS
         BO    RC16
         LA    R1,DENTABL2             GET OUTPUT TAPE DENSITY
GETDEN   CLI   0(R1),X'00'             TEST FOR END OF TABLE
         BE    GOTDEN
         CLC   0(1,R1),OUTTAPE+18      COMPARE DENSITY CODE IN DCB
         BE    GOTDEN
         LA    R1,2(R1)                GO TO NEXT ENTRY
         B     GETDEN
GOTDEN   MVC   DENSITY,1(R1)           COPY DENSITY CODE FROM TABLE
         TITLE 'INITIALIZATION'
***********************************************************************
*                                                                     *
*        INITIALIZATION                                               *
*                                                                     *
***********************************************************************
         SPACE
         LA    R3,99                   LINE COUNT - FORCE NEW PAGE
         LA    R10,1                   LINE COUNT INCREMENT
         LA    R11,50                  DATA LINE LIMIT
         LH    R2,FILENO               INITIALIZE FILE NUMBER
         $CALL EP=WKDATE,              GET CURRENT DATE AND TIME       +
               PARAM=OUTDATE
         XC    CUMTOTAL,CUMTOTAL       ZERO OUT CUMTOTAL
         XC    USERLBLS,USERLBLS       ZERO OUT USER LABEL COUNT
         MVC   DEN,DENSITY             COPY DENSITY TO HEADER
         SR    R1,R1                   SET BPI CODE
         IC    R1,DENSITY
         N     R1,=X'0000000F'         ISOLATE DIGIT
         SLA   R1,2                    MULTIPLY BY 4
         LA    R1,DENTABLE(R1)         POINT TO DENSITY IN BPI
         MVC   BPI,0(R1)               COPY INTO TITLE
         LA    R1,DATA                 INITIALIZE RECORD POINTER
*
         ST    R1,RECADDR
         XC    CCWCOUNT,CCWCOUNT       INITIALIZE CCW COUNT
         XC    DATALEN,DATALEN         ZERO DATA LENGTH
         TITLE 'VOL1 HEADER PROCESSING'
***********************************************************************
*                                                                     *
*        VOL1 HEADER PROCESSING                                       *
*                                                                     *
*             VOLUME SERIAL                                           *
*             OWNER ID                                                *
*                                                                     *
*             UPDATE VOLUME SERIAL WITH OUTPUT VOLUME SERIAL.         *
*                                                                     *
*             IF CPYVOLID=YES IS SPECIFIED, NEW VOLUME SERIAL IS      *
*             OLD VOLUME SERIAL.                                      *
*                                                                     *
***********************************************************************
         SPACE
         MVC   EODAD,=AL3(LTM)         SET EOD ADDRESS
         BAL   R9,READREC              READ VOL1 HEADER
         USING VOL1REC,R6              VOL1 RECORD ADDRESSABILITY
         LH    R1,DCBLRECL             LOAD RECORD LENGTH
         ST    R1,RECLEN               SAVE IT
         CLC   RECLEN,=F'80'           TEST FOR 80 BYTE RECORD
         BNE   NONLABEL
         CLC   VOLID,=C'VOL1'          TEST FOR VOL1 HEADER
         BNE   NONLABEL
         BAL   R9,DEBUG                PRINT VOL1 BEFORE MODIFICATION
         MVI   LABELTYP,C'S'           INDICATE STANDARD LABEL
         MVC   LABEL,=CL12'  STANDARD  '
         MVC   OUTOWNER,VOLOWNER       COPY OWNER ID
         CLC   INSER,VOLSER            COMPARE JFCB AND LABEL SERIALS
         BE    GETTIME
         MVC   NOMATCH+29(6),VOLSER    COPY SERIAL FROM LABEL
         MVC   NOMATCH+71(6),INSER     COPY SERIAL FROM JFCB
         PUT   SYSPRINT,NOMATCH        PRINT MESSAGE
         MVC   INSER,VOLSER            SET INSER TO SERIAL FROM LABEL
         CLC   OUTVOL,VOLSER           SEE IF TAPES REVERSED
         BNE   GETTIME
         PUT   SYSPRINT,SWITCHED       PRINT MESSAGE
         B     RC16
GETTIME  TIME  BIN                     GET CURRENT DATE
         ST    R1,WORK
         UNPK  NEWDATE,WORK+1(3)       UNPACK DATE
         OI    NEWDATE+4,X'F0'         SET SIGN
         TM    FLAGS,$COPYSER          SEE IF VOL SER TO BE COPIED
         BZ    COPYSER
         MVC   OUTVOL,INSER
COPYSER  MVC   VOLSER,OUTVOL           MOVE IN OUTPUT VOLUME SERIAL
         BAL   R9,DEBUG                PRINT VOL1 AFTER MODIFICATION
         TITLE 'HDR1 PROCESSING'
***********************************************************************
*                                                                     *
*        HDR1 PROCESSING                                              *
*                                                                     *
*             FILE SEQUENCE NUMBER                                    *
*             DSNAME                                                  *
*             VOLUME SEQUENCE NUMBER                                  *
*             CREATION DATE                                           *
*             EXPIRATION DATE                                         *
*             SECURITY CODE                                           *
*             VOLUME SEQUENCE NUMBER                                  *
*                                                                     *
*             UPDATE CREATION DATE WITH CURRENT DATE                  *
*                                                                     *
*             UPDATE VOLUME SERIAL UNLESS VOLUME SEQUENCE NUMBER      *
*             IS NOT 0001                                             *
*                                                                     *
*             UPDATE FILE SEQUENCE NUMBER IF VOLUME SEQUENCE NUMBER   *
*             IS 0001 AND THE FILE SEQUENCE NUMBER OF THE INPUT       *
*             TAPE IS INCORRECT                                       *
*                                                                     *
***********************************************************************
         SPACE
         MVC   EODAD,=AL3(UTM)         SET EOD ADDRESS
READHDR1 MVC   RECTYPE,=C'HDR1'        SET RECORD TYPE EXPECTED
         BAL   R9,READREC              READ A RECORD
         USING HDR1REC,R6              RECORD ADDESSABILITY
         MVC   EODAD,=AL3(UTM)         RESET EOD ADDRESS
         CLC   HDRID,RECTYPE           TEST FOR HDR1
         BNE   BADREC
*
         BAL   R9,DEBUG                PRINT HDR1 BEFORE MODIFICATION
         CLC   VOLSEQ,=C'0001'         IF FIRST TAPE, UPDATE SERIAL
         BNE   CPYDATE1
         MVC   HDR1SER,OUTVOL          MOVE OUTPUT SERIAL INTO LABEL
CPYDATE1 MVC   CRDATE+1(5),NEWDATE     REPLACE CREATION DATE
*
         MVC   OUTDSN,DSNAME           MOVE THE DSN
         MVC   OUTVSEQN,VOLSEQ         MOVE VOL.SEQ. #
         PACK  WORK,DSSEQ
         CVB   R1,DWORK                CONVERT FILE NUMBER TO BINARY
         STH   R1,FILENO               STORE IN FOOTAGE WORK AREA
         MVC   VORK,PATTRN
         ED    VORK,WORK
         MVC   OUTFILE,VORK+4          MOVE FILE #
         CR    R1,R2                   COMPARE FILE # IN LABEL TO COUNT
         BE    GETCRDAT
*
         CLC   VOLSEQ,=C'0001'         TEST VOLUME SEQUENCE NUMBER
         BNE   GETCRDAT                NO CHANGE IF NOT 0001
         CVD   R2,DWORK                FILE NUMBER INCORRECT, FIX IT
         UNPK  VORK,DWORK+4
         OI    VORK+7,X'F0'
         MVC   DSSEQ,VORK+4
*
GETCRDAT PACK  DATEWORK,CRDATE+1(5)    PACK CREATION DATE
         BAL   R14,GDATE               CONVERT TO MM/DD/YY
         MVC   OUTCRDAT,MMDDYY         COPY TO OUTPUT LINE
         PACK  DATEWORK,EXDATE+1(5)    PACK EXPIRATION DATE
         BAL   R14,GDATE               CONVERT TO MM/DD/YY
         MVC   OUTEXDAT,MMDDYY         COPY TO OUTPUT LINE
         MVC   OUTSEC,SECURITY         COPY SECURITY INDICATOR
*
         MVC   FILE#,DSSEQ             SAVE FILE NUMBER
         BAL   R9,DEBUG                PRINT HDR1 AFTER MODIFICATION
         TITLE 'HDR2 PROCESSING'
***********************************************************************
*                                                                     *
*        HDR2 PROCESSING                                              *
*                                                                     *
*             RECFM                                                   *
*             LRECL                                                   *
*             BLKSIZE                                                 *
*             JOBNAME                                                 *
*             STEPNAME                                                *
*                                                                     *
*             UPDATE JOBNAME/STEPNAME FIELD                           *
*                                                                     *
*             UPDATE DENSITY FIELD                                    *
*                                                                     *
***********************************************************************
         SPACE
         MVC   RECTYPE,=C'HDR2'        INDICATE RECORD TYPE EXPECTED
         BAL   R9,READREC              READ A RECOD
         USING HDR2REC,R6              RECORD ADDRESSABILITY
         CLC   HDR2ID,RECTYPE          TEST FOR HDR2
         BNE   BADREC
*
         BAL   R9,DEBUG                PRINT HDR2 BEFORE MODIFICATION
         MVC   JOBNAME(17),=C'COPY OF /XXXXXX  '
         MVC   STEPNAME(6),INSER
         MVC   DENLABEL,DENSITY
*
         MVC   OUTDEN,DENLABEL         COPY DENSITY FROM LABEL
PACKBLK  PACK  WORK,BLKSIZE            BLOCK SIZE
         CVB   R1,DWORK                CONVERT TO BINARY
         ST    R1,BLKSZE               SAVE IN FOOTAGE WORK AREA
         MVC   VORK,PATTRN
         ED    VORK,WORK
         MVC   OUTBLOCK,VORK+3         MOVE BLOCKSIZE
         PACK  WORK,LRECL              LRECL
         MVC   VORK,PATTRN
         ED    VORK,WORK
         MVC   OUTLRECL,VORK+3         MOVE LRECL
         MVC   OUTJOBN,JOBNAME         COPY JOBNAME
         MVC   OUTSTEP,STEPNAME        COPY STEPNAME
         SPACE
         LA    R1,OUTRECFM             LOAD THE ADDR OF RECFM
         MVC   0(1,R1),RECFM           MOVE IN THE FORMAT
         LA    R1,1(R1)                INCREMENT OUTPUT POINTER
         CLI   BLOCKING,C'B'           TEST FOR BLOCKED
         BNE   GBS
         MVI   0(R1),C'B'              INDICATE BLOCKED
         LA    R1,1(R1)                INCREMENT OUTPUT POINTER
         B     GBA
GBS      CLI   BLOCKING,C'S'           TEST FOR SPANNED OR STANDARD
         BNE   GBR
         MVI   0(R1),C'S'              INDICATE SPANNED OR STANDARD
         LA    R1,1(R1)                INCREMENT OUTPUT POINTER
         B     GBA
GBR      CLI   BLOCKING,C'R'           TEST FOR B AND S
         BNE   GBA
         MVC   0(2,R1),=C'BS'          INDICATE BS
         LA    R1,2(R1)                INCREMENT OUTPUT POINTER
GBA      MVC   0(1,R1),CC              CONTROL CHAR TYPE, IF ANY
         BAL   R9,DEBUG                PRINT HDR2 AFTER MODIFICATION
         TITLE 'READ PAST USER HEADER LABELS IF ANY'
***********************************************************************
*                                                                     *
*        READ THROUGH USER LABELS IF ANY ARE PRESENT                  *
*                                                                     *
***********************************************************************
         SPACE
         MVC   EODAD,=AL3(CNTRL)       RESET EOD ADDRESS
         SR    R5,R5                   ZERO USER LABEL COUNT
READUHL  BAL   R9,READREC              READ A RECORD
         LA    R5,1(R5)                COUNT A USER LABEL
         B     READUHL
         TITLE 'READ DATA RECORDS'
***********************************************************************
*                                                                     *
*        READ DATA RECORDS                                            *
*                                                                     *
***********************************************************************
         SPACE
*
CNTRL    BAL   R9,WRITETM              WRITE TAPE MARK
*
         ST    R5,USERLBLS             SAVE USER LABEL COUNT
         CVD   R5,DWORK                CONVERT TO DECIMAL
         MVC   VORK,PATTRN             MOVE IN EDIT PATTERN
         ED    VORK,WORK               EDIT UL COUNT
         MVC   OUTUL,VORK+7            COPY TO OUTPUT LINE
         MVC   EODAD,=AL3(READEOF)     RESET EOD ADDRESS
READDATA BAL   R9,READREC              READ A DATA RECORD
         B     READDATA
         TITLE 'COMPLETE OUTPUT LINE AND PRINT'
***********************************************************************
*                                                                     *
*        COMPLETE OUTPUT LINE AND PRINT                               *
*                                                                     *
*        OBTAIN NUMBER OF BLOCKS FROM EOF1 OR EOV1 LABEL              *
*                                                                     *
*        CALCULATE FOOTAGE AND PRINT LINE                             *
*                                                                     *
*        UPDATE FIELDS IN EOF AND EOV RECORDS AS THEY WERE            *
*        UPDATED IN HDR RECORDS                                       *
*                                                                     *
***********************************************************************
         SPACE
READEOF  BAL   R9,WRITETM              WRITE A TAPE MARK
         MVC   EODAD,=AL3(UTM)         RESET EOD ADDRESS
         MVC   RECTYPE,=C'EOF1'        INDICATE RECORD EXPECTED
         BAL   R9,READREC              READ A RECORD
         USING HDR1REC,R6              RECORD ADDRESSABILITY
         CLC   HDRID,RECTYPE           TEST FOR EOF1
         BE    EOF1REC
         CLC   HDRID,=C'EOV1'          IT MIGHT BE AN EOV INSTEAD
         BNE   BADREC
*
EOF1REC  BAL   R9,DEBUG                PRINT EOF1 BEFORE MODIFICATION
         MVC   DSSEQ,FILE#             MOVE IN FILE NUMBER
         CLC   VOLSEQ,=C'0001'         IF FIRST VOL, UPDATE SERIAL
         BNE   CPYDATE2
         MVC   HDR1SER,OUTVOL          MOVE OUTPUT SERIAL INTO LABEL
CPYDATE2 MVC   CRDATE+1(5),NEWDATE     REPLACE CREATION DATE
*
         PACK  WORK,BLKCOUNT           CONVERT NUMBER OF BLOCKS
         CVB   R1,DWORK
         ST    R1,NUMBLKS              SAVE BLOCK COUNT
         MVC   VORK,PATTRN
         ED    VORK,WORK
         MVC   OUTNUMBL,VORK+2         COPY TO OUTPUT LINE
         BAL   R9,DEBUG                PRINT EOF1 AFTER MODIFICATION
         $CALL EP=FOOTAGE,             GET FILE AND REEL FOOTAGE       +
               PARAM=LABELTYP
         MVC   OUTFFT,FILEFT+2         COPY IN FILE FOOTAGE
         MVC   OUTRFT,REELFT+2         COPY IN REEL FOOTAGE
         BXLE  R3,R10,PUTLINE          INCREMENT LINE COUNT AND TEST
         AP    PAGENO,=P'1'            INCREMENT PAGE NUMBER
         MVC   PAGE,=X'40202120'       MOVE IN EDIT MASK
         ED    PAGE,PAGENO             EDIT IN PATE NUMBER
         PUT   SYSPRINT,HEADER1        PRINT HEADERS
         PUT   SYSPRINT,HEADER2
         PUT   SYSPRINT,LINE1
         PUT   SYSPRINT,HEADER3
         LA    R3,1                    RESET LINE COUNT
         MVI   NULINE,C'0'             MAKE IT DOUBLE SPACE FIRST LINE
PUTLINE  PUT   SYSPRINT,NULINE         WRITE OUT LINE W/ TAPE FILE INFO
         MVI   NULINE,C' '             BLANK OUT OUTPUT LINE
         MVC   NULINE+1(132),NULINE
         LA    R2,1(R2)                INCREMENT FILE COUNTER
         CLC   DATA(4),=C'EOV1'        IS LABEL EOV1 ?
         BNE   READEOF2                IF NOT THEN READ NEW RECORD
         PUT   SYSPRINT,MULTVOL        IF SO THEN TELL THE USER
         TITLE 'PROCESS EOF2 OR EOV2 LABEL'
***********************************************************************
*                                                                     *
*        EOF2 OR EOV2                                                 *
*                                                                     *
***********************************************************************
         SPACE 1
READEOF2 MVC   EODAD,=AL3(UTM)         RESET EOD ADDRESS
         MVC   RECTYPE,=C'EOF2'        INDICATE RECORD TYPE EXPECTED
         BAL   R9,READREC              READ A RECOD
         USING HDR2REC,R6              RECORD ADDRESSABILITY
         CLC   HDR2ID,RECTYPE          TEST FOR HDR2
         BE    UPDTJBNM
         CLC   HDR2ID,=C'EOV2'
         BNE   BADREC
*
UPDTJBNM BAL   R9,DEBUG                PRINT EOF2 BEFORE MODIFICATION
         MVC   JOBNAME(17),=C'COPY OF /XXXXXX  '
         MVC   STEPNAME(6),INSER
         MVC   DENLABEL,DENSITY
         BAL   R9,DEBUG                PRINT EOF2 AFTER MODIFICATION
         TITLE 'PASS THROUGH TRAILER RECORDS'
***********************************************************************
*                                                                     *
*        PASS THROUGH TRAILER RECORDS                                 *
*                                                                     *
***********************************************************************
         SPACE
         MVC   EODAD,=AL3(EOF)         RESET EOD ADDRESS
READEND  BAL   R9,READREC              READ A RECORD
         B     READEND
         SPACE
EOF      BAL   R9,WRITETM              WRITE TAPE MARK
         MVC   EODAD,=AL3(EOV)         RESET EOD ADDRESS
         B     READHDR1                GO TO READ HDR1
         TITLE 'LEADING TAPE MARK PROCESSING'
***********************************************************************
*                                                                     *
*        LEADING TAPE MARK                                            *
*                                                                     *
*        FILE 1 HAS NO RECORDS, TREAT AS NON-LABELLED                 *
*                                                                     *
***********************************************************************
         SPACE
LTM      BAL   R9,WRITETM              WRITE TAPE MARK
         MVI   LABELTYP,C'N'           TREAT AS NON-LABELLED
         MVC   LABEL,=CL12'    LTM     '
         SR    R4,R4                   NO BLOCK SIZE CHANGES
         SR    R5,R5                   BLOCK COUNT
         XC    BLKSZE,BLKSZE           BLOCK SIZE
         B     ENDFILE                 JOIN NON-LABELLED PROCESSING
         TITLE 'NON-LABELLED TAPE PROCESSING'
***********************************************************************
*                                                                     *
*        NON-LABELLED TAPE PROCESSING                                 *
*                                                                     *
***********************************************************************
         SPACE 2
NONLABEL MVI   LABELTYP,C'N'           INDICATE NON LABELLED
         MVC   LABEL,=CL12'NON-LABELLED'
         B     SKIPREC1                RECORD 1 ALREADY READ
         SPACE
NEXTFILE BAL   R9,READREC              READ A BLOCK
         LH    R1,DCBLRECL             LOAD BLOCK LENGTH
         ST    R1,RECLEN
         SPACE
SKIPREC1 MVC   BLKSZE,RECLEN           SET UP RECORD LENGTH
         SR    R4,R4                   ZERO CHANGE COUNTER
         LA    R5,1                    INITIALIZE BLOCK COUNTER
         MVC   EODAD,=AL3(ENDFILE)     SET UP EOD ADDRESS
         MVI   VAR,C'Y'                INDICATE POSSIBLE V RECFM
         CLC   DCBLRECL,0(R6)          SEE IF FIRST HALFWORD = LENGTH
         BE    LOOP
         MVI   VAR,C'N'                CAN'T BE VARIABLE
LOOP     BAL   R9,READREC              READ A BLOCK
         LH    R1,DCBLRECL             LOAD RECORD LENGTH
         ST    R1,RECLEN
         LA    R5,1(R5)                COUNT RECORD
         CLC   DCBLRECL,0(R6)          SEE IF FIRST HALFWORD = LENGTH
         BE    TESTF
         MVI   VAR,C'N'                IF NOT, CAN'T BE VARIABLE
TESTF    MVI   LAST,C'N'
         CLC   BLKSZE,RECLEN           COMPARE RECORD LENGTH
         BE    LOOP
         LA    R4,1(R4)                COUNT A BLOCK LENGTH CHANGE
         MVI   LAST,C'Y'
         BH    LOOP
         MVI   LAST,C'N'
         MVC   BLKSZE,RECLEN           SAVE NEW MAX BLOCK LENGTH
         B     LOOP
         TITLE 'END OF DATA'
***********************************************************************
*                                                                     *
*        END OF DATA - SET UP OUTPUT                                  *
*                                                                     *
***********************************************************************
         SPACE 2
ENDFILE  BAL   R9,WRITETM              WRITE A TAPE MARK
         STH   R2,FILENO               SAVE FILE NUMBER
         CVD   R2,DWORK                CONVERT FILE NUMBER
         MVC   VORK,PATTRN             MOVE IN EDIT PATTERN
         ED    VORK,WORK               EDIT FILE NUMBER
         MVC   OUTFILE,VORK+4          COPY FILE NUMBER
         MVC   OUTDEN,DENSITY          COPY DENSITY
         ST    R5,NUMBLKS              SAVE BLOCK COUNT
         CVD   R5,DWORK                CONVERT BLOCK COUNT
         MVC   VORK,PATTRN             MOVE IN EDIT PATTERN
         ED    VORK,WORK               EDIT BLOCK COUNT
         MVC   OUTNUMBL,VORK+2         COPY INTO OUTPUT LINE
         L     R5,BLKSZE               SET UP MAX BLOCK SIZE
         CVD   R5,DWORK                CONVERT BLOCK SIZE
         MVC   VORK,PATTRN             MOVE IN EDIT PATTERN
         ED    VORK,WORK               EDIT BLOCK SIZE
         MVC   OUTBLOCK,VORK+3         COPY INTO OUTPUT LINE
         MVI   OUTRECFM,C'V'           SET UP RECFM
         CLI   VAR,C'Y'                SEE IF VARIABLE OK
         BE    GETFEET2
         MVI   OUTRECFM,C'F'           SET UP RECFM
         C     R4,=F'1'                TEST NUMBER OF BLKSIZE CHANGES
         BL    GETFEET2                0  - RECFM=F
         BH    RECFMU                  >1 - RECFM=U
         CLI   LAST,C'Y'               =1 - WAS LAST RECORD SHORT?
         BE    GETFEET2                YES - RECFM=F
RECFMU   MVI   OUTRECFM,C'U'           NO  - RECFM=U
GETFEET2 $CALL EP=FOOTAGE,             GET FOOTAGE                     +
               PARAM=LABELTYP
         MVC   OUTFFT,FILEFT+2         COPY FOOTAGE TO OUTPUT LINE
         MVC   OUTRFT,REELFT+2
         TITLE 'PRINT OUTPUT LINE'
***********************************************************************
*                                                                     *
*        PRINT OUTPUT LINE                                            *
*                                                                     *
***********************************************************************
         SPACE
         BXLE  R3,R10,PUTLINE2         TEST LINE COUNT
         AP    PAGENO,=P'1'            INCREMENT PAGE NUMBER
         MVC   PAGE,=X'40202120'       MOVE IN EDIT MASK
         ED    PAGE,PAGENO             EDIT IN PATE NUMBER
         PUT   SYSPRINT,HEADER1        PRINT HEADERS
         PUT   SYSPRINT,HEADER2
         PUT   SYSPRINT,LINE1
         PUT   SYSPRINT,HEADER3
         LA    R3,1                    RESET LINE COUNT
         MVI   NULINE,C'0'             DOUBLE SPACE FIRST LINE
PUTLINE2 PUT   SYSPRINT,NULINE         PRINT DATA LINE
         MVI   NULINE,C' '             BLANK OUT LINE
         MVC   NULINE+1(132),NULINE
         LA    R2,1(R2)                INCREMENT FILE NUMBER
         MVC   EODAD,=AL3(EOV)         RESET EOD ADDRESS
         B     NEXTFILE
         TITLE 'END OF TAPE'
***********************************************************************
*                                                                     *
*        END OF TAPE REACHED                                          *
*                                                                     *
***********************************************************************
         SPACE
EOV      BAL   R9,WRITETM              WRITE TAPE MARK
         XC    OUTECB,OUTECB           WRITE LAST TIME
         EXCP  OUTIOB
         WAIT  ECB=OUTECB
         CLI   OUTECB,X'7F'
         BNE   IOERR
         PUT   SYSPRINT,GOODBY
         CLOSE (INTAPE,DISP,OUTTAPE,DISP,SYSPRINT)
         SR    R15,R15
         SPACE 2
RETURN   $RTRN RC=(R15)
         SPACE 2
RC16     CLOSE (INTAPE,DISP,OUTTAPE,DISP,SYSPRINT)
         LA    R15,16
         B     RETURN
         TITLE 'ERROR ROUTINES'
***********************************************************************
*                                                                     *
*        ERROR ROUTINES                                               *
*                                                                     *
***********************************************************************
         SPACE
*--------UNEXPECTED TAPE MARK DETECTED
UTM      MVC   UTMMSG+53(4),RECTYPE    SET UP MESSAGE
         PUT   SYSPRINT,UTMMSG         PRINT MESSAGE
         B     ABEND
         SPACE
*--------WRONG RECORD READ
BADREC   MVC   BADRECMG+1(4),RECTYPE   SET UP MESSAGE
         PUT   SYSPRINT,BADRECMG       PRINT MESSAGE
         B     ABEND
         SPACE
*--------SYNAD ERROR
SYNAD    SYNADAF  ACSMETH=BSAM
         MVC   NL+38(78),50(R1)        MOVE MESSAGE TO PRINT AREA
         SYNADRLS
         PUT   SYSPRINT,NL
         B     ABEND
         SPACE
*--------I/O ERROR ON PUTPUT
IOERR    PUT   SYSPRINT,OUTIOERR
         SPACE
*--------TERMINATE WITH DUMP
ABEND    ABEND 1001,DUMP               GET A DUMP
         SPACE
*--------DCB ABEND ROUTINE
DCBABEND TM    3(R1),X'04'             TEST "IGNORE" OPTION BIT
         BZ    HOPE                    IF ZERO THEN ABEND
         MVI   3(R1),X'04'             IGNORE THE ABEND
HOPE     BR    R14                     RETURN & HOPE FOR THE BEST
         TITLE 'DATE CONVERSION SUBROUTINE'
***********************************************************************
*                                                                     *
*              CONVERT JULIAN DATE TO MM/DD/YY FORM                   *
*              ADAPTED FROM SUBROUTINE GDATE                          *
*              DATE IS RETURNED AS MM/DD/YYJJJB                       *
*                                                                     *
***********************************************************************
         SPACE
GDATE    LA    R1,MMDDYY               LOAD OUTPUT AREA ADDRESS
         USING OUTPUT,R1
         CP    DATE,=PL3'0'
         BE    NODATE
         UNPK  JULIAN,DATE             UNPACK DATE
         OI    JULDAY+2,X'F0'          SET ZONE
         MVI   BLANK,C' '              SET BLANK
         MVC   TARGET,=PL3'0'          00 00 0C
         MVO   YEAR2,YEAR              00 0Y YC
         DP    TARGET,=P'4'            0Q QC RC
         CP    REM,=P'0'               TEST REMAINDER
         BE    LEAP
         CP    DAY,=P'59'              TEST FOR BEFORE FEB 29
         BNH   LEAP
         AP    DAY,=P'1'               ADJUST TO LEAP YEAR
LEAP     LA    R15,TABLE               LOAD TABLE ADDRESS
         USING CALENDAR,R15            SET BASE ADDRESS
         ZAP   MONTHP,=P'1'            SET MONTH TO 1
LOOP1    CP    DAY,DAYS                SEE IF MORE DAYS THAN THIS MNTH
         BNH   DATERTRN
         SP    DAY,DAYS                SUBTRACT DAYS IN THIS MONTH
         AP    MONTHP,=P'1'            ADD 1 TO MONTH
         LA    R15,LEN(R15)            INCREMENT TO NEXT MONTH
         B     LOOP1
DATERTRN UNPK  MO,MONTHP               UNPACK MONTH
         OI    MO+1,X'F0'              SET ZONE
         UNPK  DA,DAY                  UNPACK DAY
         OI    DA+1,X'F0'              SET ZONE
         MVI   SLASH1,C'/'             SET SLASH
         MVI   SLASH2,C'/'             SET SLASH
         BR    R14                     RETURN
NODATE   MVC   MMDDYY,=CL12' '         BLANK DATE FIELD
         BR    R14                     RETURN
         SPACE
         DROP  R1,R15
         TITLE 'READ BLOCK SUBROUTINE'
***********************************************************************
*                                                                     *
*        READ A BLOCK OF DATA AND ADD A CCW TO THE CHAIN TO WRITE     *
*        IT TO THE OUTPUT TAPE                                        *
*                                                                     *
*        IF OVER 32760 BYTES OF DATA HAVE BEEN READ OR IF CHANNEL     *
*        PROGRAM ALREADY HAS 100 CCW'S, START CHANNEL PROGRAM         *
*        BEFORE ISSUING THE READ.                                     *
*                                                                     *
*        ADDRESS OF INPUT RECORD IS RETURNED IN REGISTER 6.           *
*                                                                     *
***********************************************************************
         SPACE 1
READREC  L     R6,RECADDR              LOAD INPUT ADDRESS
         L     R7,DATALEN              LOAD DATA LENGTH
         L     R8,CCWCOUNT             LOAD CCW COUNTER
         C     R7,=F'32760'            SEE IF ROOM FOR A BIG BLOCK
         BH    WRITE
         C     R8,=F'100'              SEE IF 100 CCW'S
         BL    READ
WRITE    LA    R6,DATA                 RESET DATA ADDRESS
         SR    R7,R7                   RESET DATA LENGTH
         SR    R8,R8                   RESET CCW COUNTER
         ST    R6,RECADDR              SAVE RESET VALUES
         ST    R7,DATALEN
         ST    R8,CCWCOUNT
         XC    OUTECB,OUTECB           CLEAR ECB
         EXCP  OUTIOB                  WRITE OUTPUT
         WAIT  ECB=OUTECB              WAIT FOR COMPLETION OF WRITE
         CLI   OUTECB,X'7F'            SEE IF SUCCESSFUL
         BNE   IOERR
READ     READ  TAPEIN,SF,INTAPE,(R6),MF=E   READ A BLOCK
         CHECK TAPEDECB                WAIT FOR COMPLETION
         LR    R15,R8                  CCW COUNT
         SLA   R15,3                   *8
         LA    R15,OUTCCWS-8(R15)      ADDRESS OF PREVIOUS CCW
         OI    4(R15),X'40'            TURN ON CMD CHAINING BIT
         MVC   8(8,R15),CCWMODEL       COPY IN MODEL CCW
         MVC   14(2,R15),DCBLRECL      COPY IN DATA LENGTH
         MVC   9(3,R15),RECADDR+1      COPY IN RECORD ADDRESS
         LH    R15,DCBLRECL            LOAD BLOCK LENGTH
         AR    R7,R15                  ADD TO CUMULATIVE LENGTH
         LA    R8,1(R8)                INCREMENT CCW COUNTER
         AR    R15,R6                  ADDRESS OF NEXT RECORD
         ST    R15,RECADDR             SAVE RECORD ADDRESS
         ST    R7,DATALEN              SAVE DATA LENGTH
         ST    R8,CCWCOUNT             SAVE CCW COUNT
         BR    R9                      RETURN
         TITLE 'ADD TAPE MARK CCW TO CHANNEL PGM'
***********************************************************************
*                                                                     *
*        ADD TAPE MARK CCW TO CHANNEL PROGRAM                         *
*                                                                     *
*        IF CHANNEL PROGRAM HAS 100 CCW'S ALREADY, ISSUE WRITE        *
*        BEFORE ADDING CCW.                                           *
*                                                                     *
***********************************************************************
         SPACE 1
WRITETM  L     R8,CCWCOUNT             LOAD CCW COUNT
         C     R8,=F'100'              SEE IF OVER 100
         BL    WRITE2
         SR    R8,R8                   RESET CCW COUNTER
         SR    R7,R7                   RESET DATA LENGTH
         ST    R7,DATALEN
         LA    R6,DATA                 RESET RECORD ADDDRESS
         ST    R6,RECADDR
         XC    OUTECB,OUTECB           CLEAR ECB
         EXCP  OUTIOB                  WRITE TAPE
         WAIT  ECB=OUTECB              WAIT FOR COMPLETION OF WRITE
         CLI   OUTECB,X'7F'            TEST FOR SUCCESSFUL
         BNE   IOERR
WRITE2   LR    R15,R8                  COPY CCW COUNT
         SLA   R15,3                   *8
         LA    R15,OUTCCWS-8(R15)      ADDRESS OF PREVIOUS CCW
         OI    4(R15),X'40'            TURN ON CHAINING
         MVC   8(8,R15),EOFCCW         COPY IN EOF CCW
         LA    R8,1(R8)                INCEMENT CCW COUNT
         ST    R8,CCWCOUNT             SAVE CCW COUNT
         BR    R9                      RETURN
         TITLE 'DEBUGGING ROUTINE'
***********************************************************************
*                                                                     *
*        PRINT LABELS BEFORE AND AFTER MODIFICATION                   *
*                                                                     *
*        ON ENTRY, R6 CONTAINS ADDRESS OF RECORD                      *
*                  R9 CONTAINS RETURN ADDRESS                         *
*                                                                     *
***********************************************************************
         SPACE 2
DEBUG    DS    0H
         BR    R9                      COMMENT THIS STATEMENT TO
*                                           ACTIVATE DEBUG PRINT
         MVC   LABELTXT,0(R6)          COPY LABEL TO OUTPUT AREA
         PUT   SYSPRINT,LABELREC       PRINT LABEL
         BR    R9                      RETURN
         TITLE 'DATA AREA'
***********************************************************************
*                                                                     *
*        DATA AREA                                                    *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE 3
SAVE     DS    18F                     SAVE AREA
PATTRN   DC    XL8'4020202020202120'
         DS    0D
DWORK    DC    F'0'
WORK     DS    1F
VORK     DS    1D
RECLEN   DS    F                       RECORD LENGTH
RECTYPE  DS    CL4                     RECORD TYPE TO BE READ
FILE#    DS    CL4                     FILE NUMBER FOR SL TAPES
DENTABLE DC    C' 200'                 0
         DC    C' 556'                 1
         DC    C' 800'                 2
         DC    C'1600'                 3
         DC    C'6250'                 4
*               MODE  CODE
DENTABL2 DC    X'03',C'0'
         DC    X'43',C'1'
         DC    X'83',C'2'
         DC    X'C3',C'3'
         DC    X'D3',C'4'
NEWDATE  DC    CL5' '                  CURRENT DATE FOR SL TAPES
LAST     DS    C
VAR      DS    C                       Y IF POSSIBLE RECFM = V
PAGENO   DC    PL2'0'                  PAGE NUMBER
FLAGS    DC    X'00'                   INDICATORS
$OPENERR EQU   X'80'                     ERROR DURING OPEN
$COPYSER EQU   X'40'                     COPY SERIAL FROM INPUT TAPE
         SPACE
TAPEIN   READ  TAPEDECB,               READ A TAPE RECORD              +
               SF,                                                     +
               INTAPE,                                                 +
               DATA,                                                   +
               'S',                                                    +
               MF=L
         EJECT
*--------FOLLOWING IS A WORK AREA FOR FOOTAGE ROUTINE
LABELTYP DS    C                       LABEL TYPE
DENSITY  DS    C                       DENSITY CODE
FILENO   DC    H'1'                    FILE SEQUENCE NUMBER IN BINARY
BLKSZE   DS    F                       BLOCK SIZE IN BINARY
NUMBLKS  DS    F                       NUMBER OF BLOCKS IN BINARY
USERLBLS DS    F                       USER LABELS IN BINARY
CUMTOTAL DS    F                       CUMULATIVE REEL TOTAL
FILEFT   DS    CL8                     FILE FOOTAGE IN EBCDIC
REELFT   DS    CL8                     REEL FOOTABE IN EBCDIC
*--------END OF FOOTAGE ROUTINE PARAMETERS
         SPACE 3
*--------MESSAGES
         SPACE
MULTVOL  DC    CL133'0       THIS DATASET IS CONTINUED ON ANOTHER TAPE'
         SPACE
HEADER1  DC    CL133'1                                            SYSTE+
               M SUPPORT UTILITIES - TAPE DUP PROGRAM'
         SPACE
HEADER2  DC    CL133'0VOLUME SERIAL  COPY OF  ---DENSITY--  -LABEL TYPE+
               -  -OWNER ID-  -------------------DATE------------------+
               -       PAGE'
         SPACE
LINE1    DC    CL133' '
         ORG   LINE1
         DC    CL4' '
OUTVOL   DC    CL6' '                  VOLUME SERIAL
         DC    CL6' '
INSER    DC    CL6' '                  INPUT SERIAL
         DC    CL3' '
DEN      DC    C' '                    DENSITY CODE
         DC    C' ('
BPI      DC    CL4' '                  BITS PER INCH
         DC    C' BPI)  '
LABEL    DC    CL12' '
         DC    CL2' '
OUTOWNER DC    CL10' '                 OWNER ID
         DC    CL2' '
OUTDATE  DC    CL42' '                 CURRENT DATE AND TIME
         DC    C'       '
PAGE     DC    CL4' '                  PAGE NUMBER
         ORG
         SPACE 2
HEADER3  DC    CL133'0 FILE  --DATA SET NAME-- RECFM LRECL BLKSIZE BLOC+
               KS UL DEN F. FT. R. FT. CR. DATE EX. DATE JOBNAME  STEPN+
               AME SEC VSEQ'
         SPACE 2
NULINE   DC    CL133' '
         ORG   NULINE
         DC    C'  '                   CARRIAGE CONTROL
OUTFILE  DC    CL4' '                  FILE SEQUENCE NUMBER
         DC    C'  '
OUTDSN   DC    CL17' '                 DATA SET NAME
         DC    CL2' '
OUTRECFM DC    CL3' '                  RECORD FORMAT
         DC    CL2' '
OUTLRECL DC    CL5' '                  LOGICAL RECORD LENGTH
         DC    CL2' '
OUTBLOCK DC    CL5' '                  BLOCK SIZE
         DC    CL2' '
OUTNUMBL DC    CL6' '                  NUMBER OF BLOCKS
         DC    CL2' '
OUTUL    DC    C' '                    NUMBER OF USER LABELS
         DC    CL2' '
OUTDEN   DC    C' '                    DENSITY FROM LABEL
         DC    CL2' '
OUTFFT   DC    CL6' '                  FILE FOOTAGE
         DC    C' '
OUTRFT   DC    CL6' '                  REEL FOOTAGE
         DC    C' '
OUTCRDAT DC    CL8' '                  CREATION DATE
         DC    C' '
OUTEXDAT DC    CL8' '                  EXPIRATION DATE
         DC    C' '
OUTJOBN  DC    CL8' '                  JOB NAME
         DC    C' '
OUTSTEP  DC    CL8' '                  STEP NAME
         DC    CL2' '
OUTSEC   DC    C' '                    SECURITY INDICATOR
         DC    CL2' '
OUTVSEQN DC    CL4' '                  VOUME SEQUENCE NUMBER
         ORG
         SPACE 2
GOODBY   DC    CL133'-       END OF UTILITY - TAPE IS COPIED'
NOTAPE   DC    CL133'-UNABLE TO OPEN SYSUT_.  RUN TERMINATED, CODE=16.'
SYSUTX   EQU   NOTAPE+21
NOMATCH  DC    CL133'-VOLUME SERIAL OF INPUT TAPE ______ DOES NOT MATCH+
                VOLUME SERIAL IN DD ______.'
SWITCHED DC    CL133'-VOLUME SERIALS ARE REVERSED.  RUN TERMINATED, COD+
               E=16.'
NL       DC    CL133'-PERMANENT I/O ERROR READING LABELS:'
NOTMK    DC    CL133'0TAPE MARK EXPECTED, NOT FOUND'
UTMMSG   DC    CL133'-UNEXPECTED TAPE MARK ENCOUNTERED ATTEMPTING TO RE+
               AD ____ RECORD.'
BADRECMG DC    CL133'-____ RECORD TYPE EXPECTED, NOT FOUND'
OUTIOERR DC    CL133'-OUTPUT I/O ERROR.'
LABELREC DC    CL133' '
         ORG   LABELREC
         DC    C' '                    CARRIAGE CONTROL
LABELTXT DC    CL80' '
         ORG
         EJECT
INTAPE   DCB   DDNAME=SYSUT1,          DDNAME                          +
               DSORG=PS,               DATA SET ORGANIZATION           +
               MACRF=R,                MACRO FORM                      +
               EODAD=LTM,              END OF DATA ADDRESS             +
               EXLST=INEXLST,          EXIT LIST ADDRESS               +
               RECFM=U,                RECORD FORMAT                   +
               BLKSIZE=32760,          MAXIMUM BLOCK SIZE              +
               DEVD=TA,                TAPE DATA SET                   +
               SYNAD=SYNAD,            SYNAD ROUTINE ADDRESS           +
               BUFNO=1                 ONE BUFFER
         SPACE
EODAD    EQU   INTAPE+33,3             END OF DATA ADDRESS
DCBLRECL EQU   INTAPE+82,2             RECORD LENGTH
         SPACE
INEXLST  DS    0F                      FULL WORD ALIGNMENT FOR EXLST
         DC    X'07',AL3(INJFCB)       JFCB AREA ADDRESS
         DC    X'11',AL3(DCBABEND)     ADDRESS OF OFF-THE-END ROUTINE
         DC    X'80000000'             END OF EXLST
         SPACE
INJFCB   DS    44F                     INPUT JFCB AREA
         EJECT
OUTTAPE  DCB   DDNAME=SYSUT2,          DDNAME                          +
               DSORG=PS,               DATA SET ORGANIZATION           +
               MACRF=E,                MACRO FORM                      +
               EXLST=OUTEXLST          EXIT LIST ADDRESS
         SPACE
OUTEXLST DS    0F                      FULL WORD ALIGNMENT FOR EXLST
         DC    X'07',AL3(OUTJFCB)      JFCB AREA ADDRESS
         DC    X'80000000'             END OF EXLST
         SPACE
OUTJFCB  DS    44F                     JFCB AREA
         SPACE
         DC    C'ECB>'                 SO I CAN FIND IT EASIER IN DUMPS
OUTECB   DC    F'0'
CCWCOUNT DC    F'0'
DATALEN  DC    F'0'
RECADDR  DC    A(DATA)
         SPACE
CCWMODEL CCW   X'01',0,X'20',0         MODEL OUTPUT CCW
EOFCCW   CCW   X'1F',0,X'00',1         WRITE TAPE MARK CCW
         SPACE
         DS    D
OUTCCWS  DS    100D                    CHANNEL PROGRAM FOR OUTPUT
         SPACE
OUTIOB   DC    X'42000000'
         DC    A(OUTECB)               ECB ADDRESS
         DC    XL8'00'
         DC    A(OUTCCWS)              CHANNEL PGM ADDRESS
         DC    A(OUTTAPE)              DCB ADDRESS
         DC    F'0'
         DC    X'00010000'
         EJECT
SYSPRINT DCB   DDNAME=SYSPRINT,                                        +
               DSORG=PS,                                               +
               MACRF=PM,                                               +
               RECFM=FBA,                                              +
               LRECL=133,                                              +
               BLKSIZE=133,
         EJECT
***********************************************************************
*                                                                     *
*        CALENDAR TABLE - NUMBER OF DAYS IN EACH MONTH (LEAP YEAR)    *
*                                                                     *
***********************************************************************
         SPACE
TABLE    DC    PL2'31'                  JANUARY
         DC    PL2'29'                  FEBRUARY
         DC    PL2'31'                  MARCH
         DC    PL2'30'                  APRIL
         DC    PL2'31'                  MAY
         DC    PL2'30'                  JUNE
         DC    PL2'31'                  JULY
         DC    PL2'31'                  AUGUST
         DC    PL2'30'                  SEPTEMBER
         DC    PL2'31'                  OCTOBER
         DC    PL2'30'                  NOVEMBER
         DC    PL2'31'                  DECEMBER
         SPACE 2
***********************************************************************
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
***********************************************************************
         SPACE
DATEWORK DS    0F                       DATE
         DS    C                        FILLER
DATE     DS    0CL3                     JULIAN DATE - PACKED DECIMAL
YEAR     DS    C                        YEAR - NO SIGN
DAY      DS    CL2                      DD DS
MONTHP   DS    PL2                      0M MS
TARGET   DS    0CL3                     00 0Y YS  -  0Q QS RS
         DS    C                        0Q         PART OF QUOTIENT
YEAR2    DS    0PL2                     0Y YS      YEAR AND SIGN
         DS    C                        QS         QUOTIENT AND SIGN
REM      DS    C                        RS         REMAINDER AND SIGN
         DS    0F                       FULLWORD ALIGNMENT
MMDDYY   DC    CL12' '                  WORK AREA FOR FINAL DATE
         EJECT
DATA     DS    CL32760
         DS    CL32760
         SPACE 2
VOL1REC  DSECT
VOLID    DS    CL4                     VOL1
VOLSER   DS    CL6                     VOLUME SERIAL
         DS    CL31
VOLOWNER DS    CL10                    OWNER ID
         SPACE 2
HDR1REC  DSECT
HDRID    DS    CL4                     HDR1, EOF1, OR EOV1
DSNAME   DS    CL17                    DATA SET NAME
HDR1SER  DS    CL6                     DATA SET SERIAL NUMBER
VOLSEQ   DS    CL4                     VOLUME SEQUENCE NUMBER
DSSEQ    DS    CL4                     DATA SET SEQUENCE NUMBER
         DS    CL4                     GENERATION NUMBER
         DS    CL2                     VERSION NUMBER
CRDATE   DS    CL6                     CREATION DATE
EXDATE   DS    CL6                     EXPIRATION DATE
SECURITY DS    CL1                     SECURITY CODE
BLKCOUNT DS    CL6                     BLOCK COUNT
         DS    CL13                    SYSTEM CODE
         DS    CL7                     RESERVED
         SPACE 2
HDR2REC  DSECT
HDR2ID   DS    CL4                     HDR2, EOF2, OR EOV2
RECFM    DS    CL1                     RECFM
BLKSIZE  DS    CL5                     BLKSIZE
LRECL    DS    CL5                     LRECL
DENLABEL DS    CL1                     DEN
         DS    CL1                     DATA SET POSITION
JOBNAME  DS    CL8                     JOBNAME
         DS    CL1                     /
STEPNAME DS    CL8                     STEPNAME
TECH     DS    CL2                     RECORDING TECHNIQUE
CC       DS    CL1                     CONTROL CHARACTER
         DS    CL1                     RESERVED
BLOCKING DS    CL1                     BLOCK ATTRIBUTE
         DS    CL41                    RESERVED
         EJECT
***********************************************************************
*                                                                     *
*        CALENDAR TABLE DSECT                                         *
*                                                                     *
***********************************************************************
         SPACE
CALENDAR DSECT
DAYS     DS    PL2                      DAYS IN MONTH
LEN      EQU   *-DAYS                   LENGTH OF ENTRY
         SPACE 2
***********************************************************************
*                                                                     *
*        OUTPUT AREA DSECT                                            *
*                                                                     *
***********************************************************************
         SPACE
OUTPUT   DSECT                          OUTPUT DSECT
MO       DS    CL2                      MONTH
SLASH1   DS    C                        SLASH
DA       DS    CL2                      DAY
SLASH2   DS    C                        SLASH
JULIAN   DS    0CL5                     JULIAN DATE - EBCDIC
YR       DS    CL2                      YEAR
JULDAY   DS    CL3                      JULIAN DAY
BLANK    DS    C                        BLANK
         EJECT
JFCBMAP  DSECT
         IEFJFCBN
         TITLE 'TAPEMAP - TAPE MAPPING PROGRAM - SUBROUTINE FOOTAGE'
***********************************************************************
*                                                                     *
* NAME             FOOTAGE                                            *
*                                                                     *
* FUNCTION         CALCULATE FOOTAGE FOR A FILE AND ACCUMULATE        *
*                  REEL FOOTAGE.                                      *
*                                                                     *
* DESCRIPTION      FOOTAGE IS CALLED FROM THE MAPPING ROUTINES AND    *
*                  IS PROVIDED WITH A WORK AREA.  THE FOOTAGE FOR     *
*                  THE FILE (BASED ON BLKSIZE, NUMBER OF BLOCKS,      *
*                  AND TAPE DENSITY IS CALCULATED AND MOVED INTO      *
*                  THE OUTPUT AREA.  TOTAL REEL LENGTH IS ALSO        *
*                  CALCULATED AND MOVED INTO THE OUTPUT AREA.         *
*                                                                     *
* PARAMETERS       FOOTAGE IS PASSED THE ADDRESS OF A WORK AREA       *
*                  WHICH CONTAINS INPUT DATA AND IS UPDATED BY        *
*                  FOOTAGE.  THE WORK AREA INCLUDES -                 *
*                                                                     *
*                  TAPE LABEL TYPE INDICATOR (STANDARD, NON-LABELLED) *
*                  DENSITY CODE                                       *
*                  RECORD LENGTH                                      *
*                  NUMBER OF RECORDS                                  *
*                  NUMBER OF USER LABELS                              *
*                  CUMULATIVE REEL COUNT   (UPDATED)                  *
*                  FILE FOOTAGE IN EBCDIC  (UPDATED)                  *
*                  REEL FOOTAGE IN EBCDIC  (UPDATED)                  *
*                                                                     *
* REGISTER USAGE   R0  - LINKAGE                                      *
*                  R1  - LINKAGE                                      *
*                  R2  - POINTER TO DENSITY CONSTANTS                 *
*                  R3  - NOT USED                                     *
*                  R4  - FOOTAGE CALCULATION WORK                     *
*                  R5  - FOOTAGE CALCULATION WORK                     *
*                  R6  - NOT USED                                     *
*                  R7  - NOT USED                                     *
*                  R8  - NOT USED                                     *
*                  R9  - NOT USED                                     *
*                  R10 - NOT USED                                     *
*                  R11 - WORK AREA BASE                               *
*                  R12 - PROGRAM BASE                                 *
*                  R13 - SAVE AREA                                    *
*                  R14 - LINKAGE                                      *
*                  R15 - LINKAGE                                      *
*                                                                     *
***********************************************************************
         TITLE 'ENTRY CODING'
FOOTAGE  $ENTER BASE=R12
         L     R11,0(R1)               LOAD WORK AREA ADDRESS
         USING LABELTYP,R11            WORK AREA ADDRESSABILITY
         CLI   DEN,C'4'                TEST FOR 6250 BPI
         BE    CALC6250
         CLI   DEN,C'3'                TEST FOR 1600 BPI
         BE    CALC1600
         CLI   DEN,C'2'                TEST FOR 800 BPI
         BNE   FOOTRTRN
         TITLE 'FOOTAGE CALCULATIONS FOR 800 BPI, NRZI'
***********************************************************************
*                                                                     *
*        FOOTAGE CALCULATIONS FOR 800 BPI, NRZI                       *
*                                                                     *
*        (BYTES/800 + .6IN)*BLOCKS = (BYTES+480)*BLOCKS/800           *
*                                                                     *
*        LABEL = ((UL+2)*80/800 + (UL+2)(.6IN))*2                     *
*              = (UL+2)*(1120)/800                                    *
*                                                                     *
*        VOLUME LABEL = 80/800 + .6IN = 560/800                       *
*                                                                     *
*        FULL REEL COUNT = 13,440,000                                 *
*                                                                     *
***********************************************************************
         SPACE
         SR    R4,R4                   CLEAR FOR DIVIDE
         L     R5,BLKSZE               LOAD BLOCKSIZE
         A     R5,=F'480'              ADD 480 FOR .6 IN IBG
         M     R4,NUMBLKS              MULTIPLY BY BLOCKSIZE
         CLI   LABELTYP,C'S'           TEST FOR STANDARD LABELS
         BNE   SAVE800
         L     R1,USERLBLS             LOAD USER LABEL COUNT
         LA    R1,2(R1)                ADD 2 FOR STANDARD LABELS
         MH    R1,=H'1120'             MULTIPLY BY 1120
         AR    R5,R1                   ADD TO COUNTER
         CLC   =H'1',FILENO            TEST FOR FIRST FILE
         BNE   SAVE800
         A     R5,=F'560'              ADD 560 FOR LABEL
SAVE800  LA    R2,DATA800              ADDRESS OF 800 BPI CONSTANTS
         B     FOOTEDIT
         TITLE 'FOOTAGE CALCULATIONS FOR 1600 BPI, PE'
***********************************************************************
*                                                                     *
*        FOOTAGE CALCULATIONS FOR 1600 BPI, PE                        *
*                                                                     *
*        ((BYTES+82)/1600 + .6IN)*BLOCKS = (BYTES+1042)*BLOCKS/1600   *
*                                                                     *
*        LABEL = ((UL+2)*162/1600 + (UL+2)(.6IN))*2                   *
*              = (UL+2)*(2244)/1600                                   *
*                                                                     *
*        VOLUME LABEL = (80+82)/1600 + .6IN = 1122/1600               *
*                                                                     *
*        FULL REEL COUNT = 26,880,000                                 *
*                                                                     *
***********************************************************************
         SPACE
CALC1600 SR    R4,R4                   CLEAR FOR DIVIDE
         L     R5,BLKSZE               LOAD BLOCKSIZE
         A     R5,=F'1042'             ADD 1042 FOR .6 IN IBG
         M     R4,NUMBLKS              MULTIPLY BY BLOCKSIZE
         CLI   LABELTYP,C'S'           TEST FOR STANDARD LABELS
         BNE   SAVE1600
         L     R1,USERLBLS             LOAD USER LABEL COUNT
         LA    R1,2(R1)                ADD 2 FOR STANDARD LABELS
         MH    R1,=H'2244'             MULTIPLY BY 2244
         AR    R5,R1                   ADD TO COUNTER
         CLC   =H'1',FILENO            TEST FOR FIRST FILE
         BNE   SAVE1600
         A     R5,=F'1122'             ADD 1122 FOR LABEL
SAVE1600 LA    R2,DATA1600             ADDRESS OF 1600 BPI CONSTANTS
         B     FOOTEDIT
         TITLE 'FOOTAGE CALCULATIONS FOR 6250 BPI, GCR'
***********************************************************************
*                                                                     *
*        FOOTAGE CALCULATIONS FOR 6250 BPI, GCR                       *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*        FULL REEL COUNT = 260,409,600                                *
*                                                                     *
***********************************************************************
         SPACE
CALC6250 SR    R4,R4                   CLEAR FOR DIVIDE
         L     R5,BLKSZE               LOAD BLOCKSIZE
         D     R4,=F'1106'             DIVIDE BY 1106
         LR    R1,R4                   SAVE REMAINDER
         M     R4,=F'1600'             FOR EVERY 1106, 1580 + 20 RESYNC
         SR    R0,R0
         D     R0,=F'7'                7 BYTE GROUPS
         LA    R1,1(R1)                ADD 1 TO QUOTIENT
         MH    R1,=H'10'               FOR EVERY 7, YOU GET 10
         AR    R5,R1                   ADD IT TO TOTAL
         A     R5,=F'2898'             ADD 2898 FOR IBG AND CONTROL
         M     R4,NUMBLKS              MULTIPLY BY NUMBER OF BLOCKS
         CLI   LABELTYP,C'S'           TEST FOR STANDARD LABELS
         BNE   SAVE6250
         L     R1,USERLBLS             LOAD USER LABEL COUNT
         LA    R1,2(R1)                ADD 2 FOR STANDARD LABELS
         MH    R1,=H'6036'             MULTIPLY BY 6036
         AR    R5,R1                   ADD TO TOTAL
         CLC   =H'1',FILENO            TEST FOR FIRST FILE
         BNE   SAVE6250
         A     R5,=F'3018'             ADD 3018 FOR VOL LABEL
SAVE6250 LA    R2,DATA6250             ADDRESS OF 6250 BPI CONSTANTS
         TITLE 'CONVERT THE BIT COUNTS TO FEET AND EDIT'
***********************************************************************
*                                                                     *
*        CONVERT THE BIT COUNTS TO FEET AND EDIT                      *
*                                                                     *
***********************************************************************
         SPACE
FOOTEDIT LR    R1,R5                   COPY
         A     R1,CUMTOTAL             ADD PREVIOUS TOTAL
         ST    R1,CUMTOTAL             STORE NEW TOTAL
         A     R5,0(R2)                ROUND OFF
         D     R4,4(R2)                DIVIDE BY (BPI/10)*12
         CVD   R5,PACKDEC              CONVERT TO DECIMAL
         MVC   FILEFT,=X'4040202021204B20'  MOVE IN MASK
         ED    FILEFT,PACKDEC+5        EDIT IT
         LR    R5,R1                   COPY CUMULATIVE TOTAL
         SR    R4,R4
         A     R5,0(R2)                ROUND OFF
         D     R4,4(R2)                DIVIDE BY (BPI/10)*12
         CVD   R5,PACKDEC              CONVERT TO DECIMAL
         MVC   REELFT,=X'4040202021204B20'  MOVE IN MASK
         ED    REELFT,PACKDEC+5
FOOTRTRN $RTRN RC=0                    RETURN
         TITLE 'DATA AREAS'
***********************************************************************
*                                                                     *
*        DATA AREAS                                                   *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
DATA800  DC    F'480'
         DC    F'960'                  800/10*12 (BITS PER FOOT/10)
DATA1600 DC    F'960'
         DC    F'1920'                 1600/10*12 (BITS PER FOOT/10)
DATA6250 DC    F'5425'
         DC    F'10850'                9042/10*12 (BITS PER FOOT/10)
PACKDEC  DS    D
         TITLE 'TAPE MAPPING PROGRAM - CURRENT DATE ROUTINE'
***********************************************************************
*                                                                     *
* NAME        WKDATE                                                  *
*                                                                     *
* FUNCTION    GET CURRENT DATE AND TIME AND RETURN AS DAY OF WEEK,    *
*             DAY, MONTH, YEAR, HOUR, MINUTE, SECOND, AM/PM.          *
*                                                                     *
* DESCRIPTION GREGORIAN CALENDAR -                                    *
*                                                                     *
*             EACH "COMMON" YEAR CONTAINS 365 DAYS, EACH "LEAP"       *
*             YEAR CONTAINS 366 DAYS.  YEARS DIVISIBLE BY 4 ARE       *
*             LEAP YEARS, EXCEPT CENTESIMAL YEARS WHICH ARE LEAP      *
*             YEARS IF DIVISIBLE BY 400.                              *
*                                                                     *
*             A YEAR FOLLOWING A COMMON YEAR WILL START ONE DAY       *
*             LATER THAN THE COMMON YEAR (IF A COMMON YEAR STARTS     *
*             ON MONDAY, THE FOLLOWING YEAR WILL START ON TUESDAY).   *
*             YEARS FOLLOWING LEAP YEARS WILL START 2 DAYS LATER.     *
*             365/7 = 52, REMAINDER 1.  366/7 = 52, REMAINDER 2.      *
*                                                                     *
*             EACH 400 YEAR GROUP CONTAINS 303 COMMON YEARS AND       *
*             97 LEAP YEARS - 303 * 1 + 97 * 2 = 497.  497/7 =        *
*             59, REMAINDER 0.  THEREFORE, CALENDARS CYCLE EVERY      *
*             400 YEARS.  MULTIPLES OF 400 YEARS CAN BE DISCARDED.    *
*                                                                     *
*             EACH 100 YEARS SHIFTS THE STARTING DAY 5 DAYS (24       *
*             LEAP YEARS, 76 COMMON YEARS - 76+2*24=124. 124/7 =      *
*             59, REMAINDER 5).                                       *
*                                                                     *
*             FOR EACH 4 YEARS IN A CENTURY, ADJUST THE STARTING      *
*             DAY 5 DAYS (3 FOR COMMON YEARS, 2 FOR THE LEAP YEAR).   *
*                                                                     *
*             REM1 = REMAINDER(YEAR/400)   (DISCARD QUOTIENT)         *
*                                                                     *
*             QOT2 = QUOTIENT(REM1/100)    (HUNDREDS - ADJUST 5)      *
*             REM2 = REMAINDER(REM1/100)                              *
*                                                                     *
*             QOT3 = QUOTIENT(REM2/4)      (FOURS - ADJUST 5)         *
*             REM3 = REMAINDER(REM2/4)     (SINGLES - ADJUST 1)       *
*                                                                     *
*             ADD 2 FOR QUADRACENTESIMAL YEAR (1600, 2000, ETC.)      *
*             FIRST DAY OF A QUADRACENTESIMAL YEAR IS A SATURDAY.     *
*                                                                     *
*             ADJUSTMENT = 5*QOT2 + 5*QOT3 + REM3 + 2                 *
*                                                                     *
*             INDEX = (ADJUSTMENT + (DAY-1))/7                        *
*                                                                     *
*             IF INDEX = 0, DAY IS SATURDAY                           *
*                        1         SUNDAY                             *
*                        2         MONDAY                             *
*                        3         TUESDAY                            *
*                        4         WEDNESDAY                          *
*                        5         THURSDAY                           *
*                        6         FRIDAY                             *
*                                                                     *
* PARAMETERS  ADDRESS OF A 42 BYTE AREA TO RECEIVE THE DATE AND TIME. *
*                                                                     *
* FPL MACROS USED - SEE COMMENTS IN MAINLINE                          *
*                                                                     *
* IBM MACROS USED - SEE COMMENTS IN MAINLINE                          *
*                                                                     *
* REGISTER USAGE                                                      *
*                                                                     *
*              R0  - LINKAGE                                          *
*              R1  - LINKAGE                                          *
*              R2  - DAY OF WEEK WORK REGISTER                        *
*              R3  - PARM LIST ADDRESS                                *
*              R4  - WORK - REMAINDER                                 *
*              R5  - WORK - QUOTIENT                                  *
*              R6  - NOT USED                                         *
*              R7  - NOT USED                                         *
*              R8  - NOT USED                                         *
*              R9  - NOT USED                                         *
*              R10 - NOT USED                                         *
*              R11 - CORRECTION ACCUMULATOR                           *
*              R12 - PROGRAM BASE                                     *
*              R13 - SAVE AREA                                        *
*              R14 - LINKAGE                                          *
*              R15 - LINKAGE                                          *
*                                                                     *
***********************************************************************
         TITLE 'ENTRY LINKAGE'
WKDATE   $ENTER BASE=R12
         L     R3,0(R1)                PARM FIELD ADDRESS
         USING PARMFLD,R3              PARM FIELD ADDRESSABILITY
         MVC   ZDAY(42),=CL42' '
*--------GET TIME
         TIME
         ST    R1,TIMEDATE             REG 1 CONTAINS JULIAN OOYYDDDS
         MVC   ZAPM,=C'AM'             REG 0 CONTAINS TIME HHMMSSTH
         C     R0,=X'12000000'         TEST FOR AFTER NOON
         BL    TEST00
         MVC   ZAPM,=C'PM'             INDICATE PM
         S     R0,=X'12000000'         SUBTRACT 12 HOURS
TEST00   C     R0,=X'01000000'         TEST FOR 00 HOURS
         BNL   CONVTIME
         A     R0,=X'12000000'         MAKE IT 12
CONVTIME ST    R0,TWORD                STORE ADJUSTED TIME
         MVC   ZTIME,=X'21207A20207A2020'  MOVE IN EDIT MASK
         ED    ZTIME-1(9),TWORD        EDIT TIME INTO OUTPUT AREA
*--------GET DAY, MONTH, YEAR
         CVB   R5,PACKDATE             CONVERT YEAR TO BINARY
         SR    R4,R4                   PREPARE FOR DIVIDE
         D     R4,=F'1000'             R0 = DAY, R1 = YEAR
         ST    R4,BINDAY               STORE DAY
         LA    R5,1900(R5)             MAKE IT 19XX
         ST    R5,BINYEAR              SAVE YEAR
         CVD   R5,PACKDATE             CONVERT YEAR TO DECIMAL
         MVC   ZYEAR-2(6),=X'402021202020'  MOVE IN EDIT MASK
         ED    ZYEAR-2(6),PACKDATE+5   EDIT IN YEAR
         LA    R5,TABLE1               ADDRESS OF DAYS IN MONTH TABLE
         LA    R2,1                    INITIALIZE MONTH COUNTER
         TM    BINYEAR+3,X'03'         SEE IF A LEAP YEAR
         BZ    FINDMNTH
         C     R4,=F'59'               NOT LEAP YEAR - SEE IF JAN, FEB
         BL    FINDMNTH
         LA    R4,1(R4)                ADD 1 TO JULIAN DAY
FINDMNTH CH    R4,0(R5)                TEST DAY
         BL    GOTMONTH
         SH    R4,0(R5)
         LA    R5,2(R5)                INCREMENT MONTH POINTER
         LA    R2,1(R2)                INCRENENT MONTH COUNTER
         B     FINDMNTH
GOTMONTH CVD   R4,PACKDATE             CONVERT DAY OF MONTH TO DEC
         MVC   TWORD,=X'40202120'      MOVE IN EDIT MASK
         ED    TWORD,PACKDATE+6        EDIT
         MVC   ZDATE,TWORD+2           MOVE INTO OUTPUT AREA
         SLA   R2,4                    MULTIPLY BY 16
         LA    R2,TABLE2-16(R2)        LOAD ADDRESS OF MONTH
         MVC   ZMONTH(9),0(R2)         MOVE MONTH INTO OUTPUT AREA
*--------GET DAY OF WEEK
         L     R5,BINYEAR              LOAD YEAR
         SR    R4,R4                   PREPARE FOR DIVIDE
         D     R4,=F'400'              THROW OUT 400 YEAR GROUPS
         LTR   R5,R4                   TEST REMAINDER
         BZ    ADDDAY
         BCTR  R5,0                    SUBTRACT 1 FROM REMAINDER
         SR    R4,R4                   PREPARE FIE DIVIDE
         D     R4,=F'100'              SPLIT OUT CENTURIES
         MH    R5,=H'5'                SHIFT 5 DAYS PER CENTURY
         LR    R2,R4                   COPY REMAINDER
         SRA   R2,2                    REMAINDER/4
         MH    R2,=H'5'                SHIFT 5 DAYS FOR 4 YEAR GROUPS
         N     R4,=X'00000003'         ONE DAY FOR EACH REMAINDER
         AR    R5,R4                   ADD CENTURY ADJUSTMENT
         AR    R5,R2                   4 YEAR GROUP ADJUSTMENT
         LA    R5,2(R5)                ADJUST 2 FOR QUADRACENTESIMAL YR
ADDDAY   A     R5,BINDAY               ADD DAY OF THIS YEAR
         BCTR  R5,0                    SUBTRACT 1
         SR    R4,R4                   PREPARE FOR DIVIDE
         D     R4,=F'7'                DIVIDE BY 7
         SLA   R4,4                    TIMES 16
         LA    R4,TABLE3(R4)           POINT TO DAY
         MVC   ZDAY,0(R4)              COPY IT TO OUTPUT AREA
*--------RETURN TO CALLING PROGRAM
         $RTRN RC=0
         SPACE
         DROP  R3
         EJECT
***********************************************************************
*                                                                     *
*        DATA AREAS                                                   *
*                                                                     *
***********************************************************************
         SPACE
         LTORG
         SPACE
***********************************************************************
*                                                                     *
*        DAYS IN MONTH TABLE                                          *
*                                                                     *
***********************************************************************
         SPACE
TABLE1   DC    H'31'                   JANUARY
         DC    H'29'                   FEBRUARY  (LEAP YEAR)
         DC    H'31'                   MARCH
         DC    H'30'                   APRIL
         DC    H'31'                   MAY
         DC    H'30'                   JUNE
         DC    H'31'                   JULY
         DC    H'31'                   AUGUST
         DC    H'30'                   SEPTEMBER
         DC    H'31'                   OCTOBER
         DC    H'30'                   NOVEMBER
         DC    H'31'                   DECEMBER
         SPACE
***********************************************************************
*                                                                     *
*        MONTH NAME TABLE                                             *
*                                                                     *
***********************************************************************
         SPACE
TABLE2   DC    CL16'JANUARY'
         DC    CL16'FEBRUARY'
         DC    CL16'MARCH'
         DC    CL16'APRIL'
         DC    CL16'MAY'
         DC    CL16'JUNE'
         DC    CL16'JULY'
         DC    CL16'AUGUST'
         DC    CL16'SEPTEMBER'
         DC    CL16'OCTOBER'
         DC    CL16'NOVEMBER'
         DC    CL16'DECEMBER'
         SPACE
***********************************************************************
*                                                                     *
*        DAY OF WEEK NAME TABLE                                       *
*                                                                     *
***********************************************************************
         SPACE
TABLE3   DC    CL16'SATURDAY,'         JAN 1, 0000 WOULD HAVE BEEN SAT
         DC    CL16'SUNDAY,'
         DC    CL16'MONDAY,'
         DC    CL16'TUESDAY,'
         DC    CL16'WEDNESDAY,'
         DC    CL16'THURSDAY,'
         DC    CL16'FRIDAY,'
         SPACE
PACKDATE DS    0D
         DC    XL4'00'
TIMEDATE DS    F
BINDAY   DS    F                       JULIAN DAY IN BINARY
BINYEAR  DS    F                       YEAR IN BINARY
TWORD    DS    F
         EJECT
***********************************************************************
*                                                                     *
*        PARAMETER LIST DESCRIPTION                                   *
*                                                                     *
***********************************************************************
         SPACE
PARMFLD  DSECT
ZDAY     DC    CL10' '                 DAY OF WEEK
         DC    CL2' '
ZDATE    DC    CL2' '                  DATE
         DC    CL1' '
ZMONTH   DC    CL9' '                  MONTH
         DC    CL1' '
ZYEAR    DC    CL4' '                  YEAR
         DC    CL2' '
ZTIME    DC    CL8' '                  TIME HH:MM:SS
         DC    CL1' '
ZAPM     DC    CL2' '                  AM/PM
         END
