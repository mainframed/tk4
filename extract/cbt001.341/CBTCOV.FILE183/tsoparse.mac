         MACRO
&NAME    TSOPARSE &X,&PCL=,&CBUF=,&MF=I,&USER=,&ENTRY=
         GBLC  &R
         LCLC  &N
&N       SETC  '&NAME'
         AIF   (('&MF(1)' NE 'I')AND('&MF(1)' NE 'E')AND('&MF(1)' NE 'L*
               ')).ERR1
         AIF   (('&MF(1)' EQ 'I')AND(('&PCL' EQ '')OR('&CBUF' EQ ''))).*
               ERR2
         AIF   ('&PCL' EQ '').GCBUF
         AIF   ('&PCL'(1,1) EQ '(').RPCL
&N       L     &R.0,&PCL .              GET ADDRESS
&N       SETC  ''
         ST    &R.0,PPLPCL .            SAVE IN PARM LIST
         AGO   .GCBUF
.RPCL    ANOP
&N       ST    &PCL(1),PPLPCL .         SAVE IN PARM LIST
&N       SETC  ''
.GCBUF   AIF   ('&CBUF' EQ '').GWORK
         AIF   ('&CBUF'(1,1) EQ '(').RCBUF
&N       L     &R.0,&CBUF .             LOAD ADDRESS OF BUFFER
&N       SETC  ''
         ST    &R.0,PPLCBUF .           SAVE IN PARM LIST
         AGO   .GWORK
.RCBUF   ANOP
&N       ST    &CBUF(1),PPLCBUF .       SAVE IN PARM LIST
&N       SETC  ''
.GWORK   AIF   ('&USER' EQ '').CALL
         AIF   ('&USER'(1,1) EQ '(').RWORK
&N       LA    &R.0,&USER(1) .          GET USER FIELD
         ST    &R.0,PPLUWA .            SAVE IN USER FIELD
&N       SETC  ''
         AGO   .CALL
.RWORK   ANOP
&N       ST    &USER(1),PPLUWA .        STORE USER FIELD
&N       SETC  ''
.CALL    AIF   ('&MF(1)' EQ 'L').MEND
&N       LA    &R.1,PPL .               LOAD ADDRESS OF PARM LIST
&N       SETC  ''
         AIF   ('&ENTRY' NE '').SPCALL  SPECIAL CALL
         CALLTSSR EP=IKJPARS                                *HMD 10/79*
.MEND    AIF   ('&N' EQ '').DONE
&N       NOPR  0 .                      DEFINE NAME
.DONE    MEXIT
.SPCALL  AIF   ('&ENTRY'(1,1) EQ '(').RSPCALL
         L     &R.15,&ENTRY(1) .        LOAD EPLOC
.CALLBAL BALR  &R.14,&R.15 .            CALL PARSE
         MEXIT
.RSPCALL AIF   (('&ENTRY(1)' EQ '15')OR('&ENTRY(1)' EQ 'R15')).CALLBAL
         LR    &R.15,&ENTRY(1) .        GET ADDRESS OF PARSE
         AGO   .CALLBAL
.ERR1    MNOTE 12,'MF=&MF IS INVALID'
         AGO   .MEND
.ERR2    MNOTE 12,'PCL= AND CBUF= REQUIRED WITH MF=I'
         AGO   .MEND
         MEND
