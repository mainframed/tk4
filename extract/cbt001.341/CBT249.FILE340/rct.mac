RCTT      CSECT
         SAVE  (14,12),,RCTT.&SYSDATE..&SYSTIME
         LR    12,15
         USING RCTT,12
         GETMAIN R,LV=WORKLEN-WORKAREA
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
         USING WORKAREA,13
         L     3,16               * LOAD ADDRESS OF CVT
         L     3,604(3)           * LOAD CVTOPCTP PTR FOR RMCT
         L     3,228(3)           * LOAD RMCTRCT PTR FOR RCT
         USING RCT,4              * SET UP ADDRESSABILITY
         LA    4,0(3)             * POINT TO RCT DATA
         TIME  DEC                * GET TIME
         ST    1,WRKDATE          * STORE DATE IN WORK AREA
         ST    0,WRKTIME          * STORE TIME IN WORK AREA
         UNPK  DATE,WRKDATE+1(3)  * MOVE DATE TO PRINT
         OI    DATE+4,X'F0'       * MAKE IT PRINTABLE
         OI    WRKTIME+3,X'0F'    * MAKE IT PRINTABLE
         ED    TIME,WRKTIME       * MOVE TIME TO PRINT
         CLC   RCTRCT,=C'RCT '    * IS THIS THE RCT
         BNE   NOTRCT             * NO, TELL HIM AND GET OUT
         XR    6,6                * CLEAR REGISTER 6 FOR WORK AREA
         LH    6,RCVUICA          * GET UICA VALUE
         ST    6,DECWORK1         * STORE UICA FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   AVGUIC,DECWORK4+1  * MOVE IT TO PRINT
         LH    6,RCVCPUA          * GET CPU VALUE
         ST    6,DECWORK1         * STORE CPUA FOR CONVERSION
         BAL   7,CNVTCPU          * GO MAKE IT PRINTABLE
         MVC   CPUUTIL,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVAFQA          * GET AFQA VALUE
         ST    6,DECWORK1         * STORE AFQA FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   AVGAVLFR,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVASMQA         * GET ASMQA FACTOR
         ST    6,DECWORK1         * STORE ASMQA FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   AVGASMQ,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVPTR           * GET PAGE FAULT RATE VALUE
         ST    6,DECWORK1         * STORE PAGE FAULT RATE FOR CONVERSN
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   PAGERATE,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVDPR           * GET DEMAND PAGE RATE
         ST    6,DECWORK1         * STORE DEMAND PAGE RATE FOR CONVERS
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMDPGRT,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVPAGRT         * GET TOTAL PAGE RATE VALUE
         ST    6,DECWORK1         * STORE TOT PAGE RATE FOR CONVERSN
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   TOTPGRT,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,RCVMSPP          * GET MSPP VALUE
         ST    6,DECWORK1         * STORE MSPP FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   MSPPG,DECWORK4+1   * MOVE IT TO PRINT
*
*    START WRITING REPORT
*
         TPUT  TITLE,40           * PRINT REPORT TITLE
         TPUT  CPU,40             * PRINT AVERAGE CPU LINE
         TPUT  UIC,40             * PRINT AVERAGE UIC LINE
         TPUT  AFQ,40             * PRINT AVERAGE AFQ LINE
         TPUT  ASMQ,40            * PRINT AVERAGE ASMQ LINE
         TPUT  PGRT,40            * PRINT PAGE FAULT RATE
         TPUT  DMD,40             * PRINT DEMAND PAGE RATE
         TPUT  TOTPG,40           * PRINT TOTAL PAGE RATE
         TPUT  MSPP,40            * PRINT MILLISECONDS PER PAGE DELAY
DONE     EQU   *
         LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=WORKLEN-WORKAREA,A=(1)
         RETURN (14,12),RC=0
NOTRCT   EQU   *
         TPUT  ERROR,33           * PUT OUT ERROR MESSAGE
         LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=WORKLEN-WORKAREA,A=(1)
         RETURN (14,12),RC=8
CONVERT  EQU   *
         XC    DECWORK3(8),DECWORK3 * CLEAR OUT WORK AREA
         L     2,DECWORK1         * LOAD VALUE TO BE CONVERTED
         CVD   2,DECWORK3         * CONVERT TO DECIMAL
         MVC   DECWORK4(4),EDITMASK * MOVE IN EDIT PATTERN
         ED    DECWORK4(4),DECWORK3+6 * MAKE IT PRINTABLE
         XC    DECWORK1(4),DECWORK1 * CLEAR OUT WORK AREA
         BR    7                  * RETURN TO CALLER
CNVTCPU  EQU   *
         XC    DECWORK3(8),DECWORK3 * CLEAR OUT WORK AREA
         SR    8,8                * CLEAR OUT WORK REGISTER
         L     9,DECWORK1         * LOAD VALUE TO BE CONVERTED
         LA    11,16              * LOAD 16 INTO WORK REG
         DR    8,11               * DIVIDE CPU BY 16 FOR AVERAGE CPU
         CVD   9,DECWORK3         * CONVERT TO DECIMAL
         MVC   DECWORK4(4),EDITMASK * MOVE IN EDIT PATTERN
         ED    DECWORK4(4),DECWORK3+6 * MAKE IT PRINTABLE
         XC    DECWORK1(4),DECWORK1 * CLEAR OUT WORK AREA
         BR    7                  * RETURN TO CALLER
TITLE    DS    0CL40
         DC    CL26'SRM VARIABLES'
DATE     DS    CL5
TIME     DC    X'4021207A20207A2020'
ERROR    DC    CL33' PROGRAM STRUCTURES ARE INCORRECT'
CPU      DS    0CL40
         DC    CL36' CPU UTILIZATION '
CPUUTIL  DS    CL4
UIC      DS    0CL40
         DC    CL36' AVERAGE UIC     '
AVGUIC   DS    CL4
AFQ      DS    0CL40
         DC    CL36' AVERAGE AVAILABLE FRAMES '
AVGAVLFR DS    CL4
ASMQ     DS    0CL40
         DC    CL36' AVERAGE ASM QUEUE '
AVGASMQ  DS    CL4
PGRT     DS    0CL40
         DC    CL36' PAGE FAULT RATE '
PAGERATE DS    CL4
DMD      DS    0CL40
         DC    CL36' DEMAND PAGING RATE '
DMDPGRT  DS    CL4
TOTPG    DS    0CL40
         DC    CL36' TOTAL PAGING RATE  '
TOTPGRT  DS    CL4
MSPP     DS    0CL40
         DC    CL36' PAGE DELAY TIME (MILLISECONDS) '
MSPPG    DS    CL4
EDITMASK DC    XL4'40202120'
WORKAREA DSECT
SAVEAREA DS    18F
DECWORK1 DS    XL2
DECWORK2 DS    XL2
DECWORK3 DS    D
DECWORK4 DS    CL4
WRKDATE  DS    F
WRKTIME  DS    F
WORKLEN  EQU   *
RCT      DSECT
RCTRCT   DS    CL4
         ORG   RCT+62
RCVUICA  DS    XL2
RCVCPUA  DS    XL2
         DS    XL2
RCVAFQA  DS    XL2
RCVASMQA DS    XL2
RCVPTR   DS    XL2
RCVDPR   DS    XL2
RCVPAGRT DS    XL2
RCVMSPP  DS    XL2
         DS    XL56
RCTEND   EQU   *
         END
