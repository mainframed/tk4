DMN      CSECT
         SAVE  (14,12),,DMN.&SYSDATE..&SYSTIME
         LR    12,15
         USING DMN,12
         GETMAIN R,LV=WORKLEN-WORKAREA
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
         USING WORKAREA,13
         L     3,16               * LOAD ADDRESS OF CVT
         L     3,604(3)           * LOAD CVTOPCTP PTR FOR RMCT
         L     4,180(3)           * LOAD RMCTDMDT PTR FOR DMNT
         LH    5,188(3)           * LOAD RMCTDMNC NUMBER OF DOMAINS
         LA    8,0(4)             * LOAD DOMAIN DATA
         USING DMDT,8             * SET UP ADDRESSABILITY
         TIME  DEC                * GET DATE AND TIME
         ST    1,WRKDATE          * STORE DATE IN WORK AREA
         ST    0,WRKTIME          * STORE TIME IN WORK AREA
         UNPK  DATE,WRKDATE+1(3)  * MOVE IT TO PRINT
         OI    DATE+4,X'F0'       * MAKE IT PRINTABLE
         OI    WRKTIME+3,X'0F'    * MAKE IT PRINTABLE
         ED    TIME,WRKTIME       * MOVE IT TO PRINT
         TPUT  TITLE,61           * PUT TITLE TO TERMINAL
         TPUT  HEADING,61         * PUT HEADINGS TO TERMINAL
DMNLOOP  EQU   *
         XR    6,6                * CLEAR REGISTER 6 FOR WORK AREA
         IC    6,DMDTNO           * GET DOMAIN NUMBER
         ST    6,DECWORK1         * STORE DOMAIN FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNNO,DECWORK4+1   * MOVE IT TO PRINT
         IC    6,DMDTLO           * GET MIN MPL VALUE
         ST    6,DECWORK1         * STORE MIN MPL FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNLO,DECWORK4+1   * MOVE IT TO PRINT
         IC    6,DMDTHI           * GET MAX MPL VALUE
         ST    6,DECWORK1         * STORE MAX MPL FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNHI,DECWORK4+1   * MOVE IT TO PRINT
         IC    6,DMDTWT           * GET WEIGHT FACTOR
         ST    6,DECWORK1         * STORE WEIGHT FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNWT,DECWORK4+1   * MOVE IT TO PRINT
         LH    6,DMDTMPLT         * GET CURRENT MPL TARGET
         ST    6,DECWORK1         * STORE MPL TARGET FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNMPLT,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTRUA          * GET AVG # READY USERS
         ST    6,DECWORK1         * STORE RUA FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNRUA,DECWORK4+1  * MOVE IT TO PRINT
         LH    6,DMDTGOOU         * GET # USERS GOING OUT
         ST    6,DECWORK1         * STORE GOOU FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNGOOU,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTCMPL         * GET CURRENT MPL
         ST    6,DECWORK1         * STORE CURRENT MPL FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNCMPL,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTOUTU         * GET # USERS ON OUT Q
         ST    6,DECWORK1         * STORE OUTQ FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNOUTQ,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTINCU         * GET  SWAPPABLE INCORE USERS
         ST    6,DECWORK1         * STORE INCU FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNINCU,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTNSW          * GET CURRENT # NSW IN USERS
         ST    6,DECWORK1         * STORE # NSW IN USERS FOR CONVERSION
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMN#NSW,DECWORK4+1 * MOVE IT TO PRINT
         LH    6,DMDTRUMX         * GET MAX # READY USERS IN INTERVAL
         ST    6,DECWORK1         * STORE MAX # READY USERS FOR CONVERS
         BAL   7,CONVERT          * GO MAKE IT PRINTABLE
         MVC   DMNRUMX,DECWORK4+1 * MOVE IT TO PRINT
         TPUT  DETAIL,61          * PUT DETAIL LINE TO TERMINAL
         LA    8,DMDTEND-DMDT(8)  * POINT TO NEXT ENTRY
         BCT   5,DMNLOOP          * LOOP THROUGH DOMAIN TABLE
         LR    1,13
         L     13,4(13)
         FREEMAIN R,LV=WORKLEN-WORKAREA,A=(1)
         RETURN (14,12)
CONVERT  EQU   *
         XC    DECWORK3(8),DECWORK3 * CLEAR OUT WORK AREA
         L     2,DECWORK1         * LOAD VALUE TO BE CONVERTED
         CVD   2,DECWORK3         * CONVERT TO DECIMAL
         MVC   DECWORK4(4),EDITMASK * MOVE IN EDIT PATTERN
         ED    DECWORK4(4),DECWORK3+6 * MAKE IT PRINTABLE
         XC    DECWORK1(4),DECWORK1 * CLEAR OUT WORK AREA
         BR    7                  * RETURN TO CALLER
DETAIL   DS    0CL51
         DS    CL2
DMNNO    DS    CL4
         DS    CL1
DMNLO    DS    CL4
         DS    CL1
DMNHI    DS    CL4
         DS    CL1
DMNWT    DS    CL4
         DS    CL1
DMNMPLT  DS    CL4
         DS    CL1
DMNRUA   DS    CL4
         DS    CL1
DMNGOOU  DS    CL4
         DS    CL1
DMNCMPL  DS    CL4
         DS    CL1
DMNOUTQ  DS    CL4
         DS    CL1
DMNINCU  DS    CL4
         DS    CL1
DMN#NSW  DS    CL4
         DS    CL1
DMNRUMX  DS    CL4
EDITMASK DC    XL4'40202120'
TITLE    DS    0CL50
         DC    CL47'    DOMAIN ACTIVITY TABLE MONITOR '
DATE     DS    CL5
TIME     DC    X'4021207A20207A2020'
HEADING  DC    C'    #  MIN  MAX   WT MPLT  RUA GOOU CMPL OUTQ INCU #NSX
               W RUMX '
WORKAREA DSECT
SAVEAREA DS    18F
DECWORK1 DS    XL2
DECWORK2 DS    XL2
DECWORK3 DS    D
DECWORK4 DS    CL4
WRKDATE  DS    F
WRKTIME  DS    F
WORKLEN  EQU   *
DMDT     DSECT
DMDTNO   DS    XL1
DMDTLO   DS    XL1
DMDTHI   DS    XL1
DMDTWT   DS    XL1
DMDTMPLT DS    XL2
DMDTRUA  DS    XL2
DMDTGOOU DS    XL2
DMDTCMPL DS    XL2
DMDTOUTU DS    XL2
DMDTINCU DS    XL2
         DS    XL18
DMDTNSW  DS    XL2
DMDTRUMX DS    XL2
         DS    XL18
DMDTEND  EQU   *
         END
