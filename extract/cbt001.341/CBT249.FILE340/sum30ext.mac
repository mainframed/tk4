SUM30    CSECT
         SAVE  (14,12),,SUM30.&SYSDATE..&SYSTIME
         LR    R12,R15
         USING SUM30,R12
         GETMAIN R,LV=WRKLEN-WRKAREA
         ST    R13,4(R1)
         ST    R1,8(R13)
         LR    R13,R1
         USING WRKAREA,R13
         USING SMFRCD30,R9
         OPEN  (SMFIN,,RECOUT,(OUTPUT))
         EJECT
READREC  EQU   *
         GET   SMFIN              * READ SMF RECORD
         LR    R9,R1              * LOAD ADDRESS OF RECORD INTO R9
         CLI   SMF30RTY,X'1E'     * IS THIS A TYPE 30 RECORD
         BNE   READREC            * NO - GO GET NEXT RECORD
         CLI   SMF30SON+1,1       * IS SUBSYSTEM SEGMENT THERE
         BL    READREC            * NO - GO GET NEXT RECORD
         CLC   SMF30WID(3),STC    * IS THIS RECORD FOR STARTED TASKS
         BE    READREC            * YES - IGNORE STARTED TASKS
         MVI   SMFWRK,X'40'       * CLEAR WORK AREA
         MVC   SMFWRK+1(175),SMFWRK * CLEAR WORK AREA
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30SOF        * POINT TO SUBSYSTEM SEGMENT
         USING SMF30PSS,R4
         CLC   SMF30TYP,R30STEP   * IS THIS A STEP TERMINATION RECORD
         BE    STEP               * YES - GO DO STEP PROCESSING
         CLC   SMF30TYP,R30JOB    * IS THIS A JOB TERMINATION RECORD
         BNE   READREC            * NO - GO GET NEXT RECORD
         MVI   RECTYPE,C'J'       * MOVE IN JOB INDICATOR
         B     PROCESS            * GO PROCESS DATA
STEP     EQU   *
         MVI   RECTYPE,C'S'       * MOVE IN STEP INDICATOR
         EJECT
PROCESS  EQU   *
         MVC   ENDTIME,SMF30TME   * GET ENDED TIME
         MVC   ENDDATE,SMF30DTE   * GET ENDED DATE
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30IOF        * POINT TO ID SEGMENT
         USING SMF30ID,R4
         CLI   SMF30ION+1,1       * IS ID SEGMENT THERE
         BL    IOACT              * NO - GO CHECK I/O ACTIVITY SEGMENT
         MVC   JOBNAME,SMF30JBN   * GET JOBNAME/SESSION NAME
         MVC   PGMNAME,SMF30PGM   * GET PROGRAME NAME
         MVC   STEPNAME,SMF30STM  * GET STEPNAME
         MVC   STEPNUMB,SMF30STN  * GET STEP NUMBER
         MVC   JOBCLASS,SMF30CLS  * GET JOB CLASS
         MVC   PGN,SMF30PGN       * GET PERFORMANCE GROUP NUMBER
         MVC   PPGMSTRT,SMF30PPS  * GET PROBLEM PROGRAM START TIME
         MVC   STEPINIT,SMF30SIT  * GET STEP INITIATION TIME
         MVC   STEPDATE,SMF30STD  * GET STEP INITIATION DATE
IOACT    EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30UOF        * POINT TO I/O ACTIVITY SEGMENT
         USING SMF30URA,R4
         CLI   SMF30UON+1,1       * IS I/O ACTIVITY SEGMENT THERE
         BL    CCODE              * NO - GO CHECK CONDITION CODE
         MVC   CARDCT,SMF30INP    * GET NUMBER OF SYSIN DD *,DATA IMAGE
         MVC   NUMTPUT,SMF30TPT   * GET NUMBER OF TPUTS
         MVC   NUMTGET,SMF30TGT   * GET NUMBER OF TGETS
         MVC   TOTEXCP,SMF30TEP   * GET TOTAL BLOCKS(EXCP) TRANSFERED
CCODE    EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30TOF        * POINT TO COMPLETION CODE SEGMENT
         USING SMF30CMP,R4
         CLI   SMF30TON+1,1       * IS COMPTETION CODE SEGMENT THERE
         BL    CPU                * NO - GO CHECK CPU SEGMENT
         MVC   COMPCODE,SMF30SCC  * GET COMPLETION CODE
         MVC   TERMINDC,SMF30STI  * GET TERMINATION INDICATOR
CPU      EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30COF        * POINT TO CPU SEGMENT
         USING SMF30CAS,R4
         CLI   SMF30CON+1,1       * IS CPU SEGMENT THERE
         BL    STORAGE            * NO - GO CHECK STORAGE SEGMENT
         MVC   DPRTY,SMF30PTY     * GET CPU DISPATCHING PRIORITY
         MVC   TCBTIME,SMF30CPT   * GET STEP TCB TIME
         MVC   SRBTIME,SMF30CPS   * GET STEP SRB TIME
         EJECT
STORAGE  EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30ROF        * POINT TO STORAGE SEGMENT
         USING SMF30SAP,R4
         CLI   SMF30RON+1,1       * IS STORAGE SEGMENT THERE
         BL    PERFORM            * NO - GO CHECK PERFORMANCE SEGMENT
         MVC   REGREQ,SMF30RGN    * GET REQUESTED REGION SIZE
         MVC   PVTSTG,SMF30PRV    * GET PVT VIRTUAL STG ALLOCATED
         MVC   SYSSTG,SMF30SYS    * GRT SYSTEM VIRTUAL STG ALLOCATED
         MVC   PAGEINS,SMF30PGI   * GET NUMBER OF PAGINS
         MVC   PAGEOUTS,SMF30PGO  * GET NUMBER OF PAGE OUTS
         MVC   STEPRECL,SMF30REC  * GET NUMBER OF STEP RECLAIMS
         MVC   SWAPS,SMF30NSW     * GET NUMBER OF SWAPS
         MVC   SWAPINS,SMF30PSI   * GET NUMBER OF SWAPINS
         MVC   SWAPOUTS,SMF30PSO  * GET NUMBER OF SWAP OUTS
         MVC   VIOPGIN,SMF30VPI   * GET NUMBER OF VIO PAGE INS
         MVC   VIOPGOUT,SMF30VPO  * GET NUMBER OF VIO PAGE OUTS
         MVC   VIOPGREC,SMF30VPR  * GET NUMBER OF VIO RECLAIMS
         MVC   CMNPGIN,SMF30CPI   * GET NUMBER OF COMMON PAGE INS
         MVC   CMNRECL,SMF30CPR   * GET NUMBER OF COMMON RECLAIMS
         MVC   PGSTOLEN,SMF30PST  * GET NUMBER OF PAGES STOLEN
         MVC   PAGESEC,SMF30PSC   * GET NUMBER OF PAGES PER SECOND
PERFORM  EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30POF        * POINT TO PERFORMANCE SEGMENT
         USING SMF30PRF,R4
         CLI   SMF30PON+1,1       * IS PERFORMANCE SEGMENT THERE
         BL    ENDPROC            * NO - GO DO END OF DATA PROCESSING
         MVC   SERVICE,SMF30SRV   * GET TOTAL SERVICE UNITS
         MVC   CPUSRV,SMF30CSU    * GET CPU SERVICE UNITS
         MVC   SRBSRV,SMF30SRB    * GET SRB SERVICE UNITS
         MVC   IOSRV,SMF30IO      * GET I/O SERVICE UNITS
         MVC   MSOSRV,SMF30MSO    * GET MSO SERVICE UNITS
         MVC   XACTTIME,SMF30TAT  * GET TRANSACTION TIME
         MVC   XACTRES,SMF30RES   * GET TRANSACTION RESIDENCY TIME
         MVC   NUMXACT,SMF30TRS   * GET NUMBER OF TRANSACTIONS
ENDPROC  EQU   *
         PUT   RECOUT,SMFWRK      * WRITE RECORD TO OUTPUT FILE
         B     READREC            * GO GET NEXT RECORD
         EJECT
DONE     EQU   *
         CLOSE (SMFIN,,RECOUT)
         LR    R1,R13
         L     R13,4(R13)
         FREEMAIN R,LV=WRKLEN-WRKAREA,A=(1)
         RETURN (14,12),RC=0
R30JOB   DC    H'5'               * JOB TERMINATION RECORD INDICATOR
R30STEP  DC    H'4'               * STEP TERMINATION RECORD INDICATOR
STC      DC    CL3'STC'           * STARTED TASK SUBSYSTEM INDICATOR
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=GL,EODAD=DONE
         EJECT
RECOUT   DCB   DDNAME=RECOUT,DSORG=PS,MACRF=PM
         EJECT
WRKAREA  DSECT
SAVEAREA DS    18F
SMFWRK   DS    0CL176
RECTYPE  DS    CL1
*   S = STEP DATA INDICATOR
*   J = JOB DATA INDICATOR
ENDTIME  DS    CL4                * USED FOR STEP RPT1,2,3 AND JOB
ENDDATE  DS    PL4                * USED FOR STEP RPT1,2,3 AND JOB
JOBNAME  DS    CL8                * USED FOR STEP RPT1,2,3 AND JOB
PGMNAME  DS    CL8                * USED FOR STEP RPT1
STEPNAME DS    CL8                * USED FOR STEP RPT1,2 AND 3
JOBCLASS DS    CL1                * USED FOR JOB REPORT
STEPNUMB DS    BL2                * USED FOR JOB REPORT
PGN      DS    BL2                * USED FOR STEP RPT2 AND JOB REPORT
TERMINDC DS    BL1                * USED FOR STEP RPT1 AND JOB REPORT
         DS    BL1                * FOR ALIGNMENT ONLY
PPGMSTRT DS    F                  * USED FOR STEP RPT2
STEPINIT DS    F                  * USED FOR STEP RPT2
STEPDATE DS    F                  *
CARDCT   DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
NUMTPUT  DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
NUMTGET  DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
TOTEXCP  DS    F                  * USED FOR STEP RPT2
COMPCODE DS    BL2                * USED FOR STEP RPT1 AND JOB REPORT
DPRTY    DS    BL2                * USED FOR STEP RPT1
TCBTIME  DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
SRBTIME  DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
REGREQ   DS    BL2                * USED FOR STEP RPT1
PVTSTG   DS    BL2                * USED FOR STEP RPT1
SYSSTG   DS    BL2                * USED FOR STEP RPT1
         DS    H                  * FOR ALIGNMENT ONLY
PAGEINS  DS    F                  * USED FOR STEP RPT1
PAGEOUTS DS    F                  * USED FOR STEP RPT1
STEPRECL DS    F                  * USED FOR STEP RPT3
SWAPS    DS    F                  * USED FOR STEP RPT1
SWAPINS  DS    F                  * USED FOR STEP RPT1
SWAPOUTS DS    F                  * USED FOR STEP RPT1
VIOPGIN  DS    F                  * USED FOR STEP RPT1
VIOPGOUT DS    F                  * USED FOR STEP RPT1
VIOPGREC DS    F                  * USED FOR STEP RPT3
CMNPGIN  DS    F                  * USED FOR STEP RPT3
CMNRECL  DS    F                  * USED FOR STEP RPT3
PGSTOLEN DS    F                  * USED FOR STEP RPT3
PAGESEC  DS    D                  * USED FOR STEP RPT3
SERVICE  DS    F                  * USED FOR STEP RPT2 AND JOB REPORT
CPUSRV   DS    F                  *
SRBSRV   DS    F                  *
IOSRV    DS    F                  *
MSOSRV   DS    F                  *
XACTTIME DS    F                  * USED FOR JOB REPORT
XACTRES  DS    F                  * USED FOR STEP RPT3 AND JOB REPORT
NUMXACT  DS    F                  * USED FOR JOB REPORT
WRKLEN   EQU   *
         EJECT
         IFASMFR 30
         END
