#V       TITLE 'IEESI03D - OPERATOR #V COMMAND'
IEESI03D CSECT
         GSB
         EJECT
         TVGIDE
         EJECT
         PRINT GEN
IEESI03D CSECT
         LR    R12,R15             ESTABLISH BASE
         USING IEESI03D,R12
         USING XSA,R2              'USE' XSA BASE
         USING WTO,R13             'USE' WTO AREA BASE
         L     R0,SIZE
         GETMAIN R,LV=(0)          GET WTO AREA
         LR    R13,R1              PUT WTO ADDR IN WTO BASE
         ST    R14,SAVRET          SAVE RETURN ADDR
         L     R7,XAL              GET PTR TO INPUT PARM
         LA    R7,0(R7)            CLEAR HI BYTE
         LTR   R7,R7               IS THERE A PARM ?
         BZ    NOTHING             NO
         MVC   BUFFER(6),0(R7)
         GSAMCALL READ,POOL=5,FILE=FILENAME,BUFFER=BUFFER,BUFFLEN=256, X
               OPTIONS=(INITIAL,DIR,NOUPDATE),MF=(E,WGSB)
         MVC   WTOCTL1(4+L'WTOTEXT),WOVL1    PATTERN WTO
         MVC   WTOTEXT(6),0(R7)   REQUESTED VOLSER
         LTR   R15,R15
         BNZ   MSGREADY            VOLUME (PROBABLY) NOT DEFINED
         CLC   BUFFER(6),0(R7)     CORRECT RECORD?
         BNE   MSGREADY            NO, REQUESTED VOL NOT DEFINED
         LA    R7,BUFFER
         USING TVGISECT,R7
         FORMAT (TVGIGRP,A,4),                                         *
               (TVGIADDR,4,99999),' ',(TVGIUSR,A,8),' ',               *
               (TVGIACCT,A,8),WKA=WKA,TO=WTOTEXT+7
         CLI   TVGIOUT,C' '
         BE    MSGREADY
         MVI   WTOTEXT+36,C'-'
         CLI   TVGIOUT,C'W'
         BNE   *+4+6+4
         MVC   WTOTEXT+38(9),=C'WITHDRAWN'
         B     MSGREADY
         MVC   WTOTEXT+38(7),=C'PENDING'
         CLI   TVGIOUT,C'Z'
         BNE   *+4+6+4
         MVC   WTOTEXT+46(8),=C'DELETION'
         B     MSGREADY
         CLI   TVGIOUT,C'V'
         BNE   *+4+6+4
         MVC   WTOTEXT+46(9),=C'WITHDRAWL'
         B     MSGREADY
         CLI   TVGIOUT,C'R'
         BNE   *+4+6+4
         MVC   WTOTEXT+46(6),=C'RETURN'
         B     MSGREADY
         CLI   TVGIOUT,C'A'
         BNE   *+4+6+4
         MVC   WTOTEXT+46(7),=C'ARRIVAL'
         B     MSGREADY
         MVC   WTOTEXT+38(21),=C'STATUS X UNRECOGNIZED'
         MVC   WTOTEXT+45(1),TVGIOUT
MSGREADY EQU   *
         BAL   R10,WRITEIT
NOTHING  DS    0H
EXIT     LR    R1,R13
         L     R14,SAVRET          RELOAD RETURN ADDR
         L     R0,SIZE
         FREEMAIN R,LV=(0),A=(1)          FREE WTO AREA
         SR    R15,R15             CLEAR RETURN CODE
         BR    R14                 EXIT
         EJECT
WRITEIT  SR    R0,R0               CLEAR R0
         LA    R1,WTOCTL1
         IC    R0,XAU              GET UCMID IF REQUESTING CONSOLE
         SVC   35
         BR    R10
         SPACE 5
         DS    0F
SIZE     DC    X'F1',AL3(WTOEND-WTO)
         SPACE 3
WOVL1    DC    X'003C0100'
         DC    CL60'XXXXXX NOT DEFINED'
         SPACE 3
FILENAME DC    CL8'TAPSER'
         LTORG
         SPACE 5
PATCH    DC    CL40' '
         EJECT
WTO      DSECT
WGSB     DS    (GSBLEN)X
BUFFER   DS    CL256
WKA      DS    0D
         DS    3A
WTOCTL1  DS    F
WTOTEXT  DS    CL60
SAVRET   DS    F
WTOEND   DS    0H
         EJECT
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F
XAN      DS    0C
XAL      DS    F
XAV      DS    D
XAS      DS    D
XAU      DS    C
         DS    C
XAJ      DS    H
         DS    F
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   IEESI03D
