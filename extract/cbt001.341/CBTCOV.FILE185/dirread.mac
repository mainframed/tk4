DIRREAD  CSECT
R1       EQU   1
R3       EQU   3
R2       EQU   2
R4       EQU   4
R5       EQU   5
R10      EQU   10
         SAVE  (14,11),,DIRREAD
         USING DIRREAD,R10
         LR    R10,15
         ST    13,SAVEAREA+4
         LA    13,SAVEAREA
         LM    R2,R5,0(R1)    GET PARAMETERS.
         XC    0(4,R5),0(R5)  CLEAR RETURN CODE.
         TM    PDS+48,X'10'
         BO    OK
         L     R2,0(R2)
         MVC   PDS+40(8),0(R2)  DDNAME INTO DCB.
         RDJFCB PDS
         L     R3,0(R3)
         MVC   JFCB(44),0(R3)  DSNAME INTO JFCB.
         OI    JFCB+52,X'08'  DON'T WRITE BACK JFCB.
         LM    2,3,=C'NOUSAGE '  DON'T UPDATE LAST-USED-DATE.
         OPEN  (PDS,(INPUT)),TYPE=J
         TM    PDS+48,X'10'
         BO    OK
         MVI   3(R5),X'08'    OPEN ERROR.
         B     RETURN
OK       EQU   *
         READ  PDSDECB,SF,PDS,DIRKEY,'S'
         CHECK PDSDECB
RETURN   EQU   *
         MVC   0(4,R4),DBADDR
         L     13,4(13)
         LM    14,11,12(13)
         BR    14
EOF      EQU   *
         CLOSE (PDS)
         MVI   3(R5),X'04'    EOF.
         B     RETURN
ERROR    EQU   *
         SYNADAF ACSMETH=BSAM  GET ERROR MESSAGE FROM OS.
         MVC   SYNADMSG(79),49(1)  SAVE IT.
         SYNADRLS             FREE THE MESSAGE BUFFER.
         WTO   MF=(E,MSG)
         MVI   3(R5),X'0C'    I/O ERROR.
         B     RETURN
DIRKEY   DS    D
DIRBLOCK DS    256C
DBADDR   DC    A(DIRBLOCK)
JFCB     DS    0D
         DS    CL176
JFCBADDR DS    0F
         DC    X'87'
         DC    AL3(JFCB)
MSG      DS    0F
         DC    H'83'          MSG LENGTH
         DC    X'8000'        MCSFLAGS
SYNADMSG DS    CL79
         DC    X'00000020'    ROUTCDE=11
PDS      DCB   DDNAME=LIST00,DSORG=PS,EODAD=EOF,SYNAD=ERROR, XXXXXXXXXXX
               RECFM=F,BLKSIZE=256,KEYLEN=8,MACRF=R,EXLST=JFCBADDR
SAVEAREA DS    18F
         END
