APF TITLE 'COMMAND TO ALLOW TSO EXECUTION OF APF AUTHORIZED PROGRAMS'
         YREGS
APF      CSECT ,
         SAVE  (14,12),,*
         LR    R8,R1               SAVE ARGUMENT REGISTER
         USING CPPL,R8             ADDRESS CPPL
         LR    R12,R15             PROGRAM BASE
         USING APF,R12
         USING PSA,0               ADDRESS PSA
*
*        TEST AUTHORIZATION
*
         L     R1,PSATOLD          GET TCB ADDRESS
         L     R1,TCBTCT-TCB(,R1)  -->TCT
         L     R1,TCTJMR-SMFTCT(,R1) -->JMR
         L     R1,JMRUCOM-JMR(,R1) -->JLSE (LOCAL DEFINITION)
         USING JLSDSECT,R1         ADDRESS JLSE
         CLC   =C'YCC',JLSPRJ      TEST FOR YCC PROJECT
         BE    OK                  IF OK, PROCEED
         ABEND X'B0A',,,SYSTEM     NOT OK, BLOW THE GUY OUT WITH A B0A
         DROP R1
OK       EQU   *
         GETMAIN R,LV=DSLEN,SP=1   REENTRANT LINKAGE
         ST    R1,8(R13)
         ST    R13,4(R1)
         LA    R13,0(R1)
         USING DS,R13              ADDRESS DYNAMIC STORAGE
*
*        SCAN OUT COMMAND NAME
*
         L     R2,CPPLUPT          GET UPT ADDRESS
         L     R3,CPPLECT          GET ECT ADDRESS
         LA    R4,CPECB            GET ECB ADDRESS
         XC    CPECB,CPECB         CLEAR ECB
         LA    R5,FLAG             GET FLAG ADDRESS
         XC    FLAG,FLAG           CLEAR FLAG
         LA    R6,OUTAREA          GET SCAN OUTPUT AREA ADDRESS
         L     R7,CPPLCBUF         GET COMMAND BUFFER ADDRESS
         LA    R1,CSPL             -->COMMAND SCAN PARM LIST
         STM   R2,R7,CSPL          PLACE ADDRESSES IN LIST
         CALLTSSR EP=IKJSCAN       CALL SCAN ROUTINE
         USING CSOA,R6             ADDRESS OUTPUT AREA
         TM    CSOAFLG,VALID       IS COMMAND VALID?
         BNZ   CMDOK               YES, PROCEED
         TPUT  INVALID,L'INVALID   NO, ISSUE MESSAGE
         B     EXIT
CMDOK    LH    R2,CSOALNM          GET NAME LENGTH
         BCTR  R2,0                USE WITH EX
         L     R3,CSOACNM          GET ADDRESS OF NAME
         MVI   EPLOC,C' '          BLANK OUT...
         MVC   EPLOC+1(7),EPLOC    ...EPLOC AREA
         MVC   EPLOC(0),0(R3)      (EXECUTED)
         EX    R2,*-6              MOVE COMMAND NAME
         LOAD  EPLOC=EPLOC         GET COMMAND
         LR    R15,R0              GET ENTRY POINT
         LR    R1,R8               RESTORE PARAMETERS
         BALR  R14,R15             CALL COMMAND
         LR    R2,R15              SAVE RETURN CODE
EXIT     LR    R1,R13              POINT TO DYNAMIC STORAGE
         L     R13,4(R13)          CHAIN BACK SAVE AREA
         FREEMAIN R,LV=DSLEN,A=(R1),SP=1 FREE DYNAMIC STORAGE
         L     R14,12(R13)         RESTORE RETURN REGISTER
         LR    R15,R2              SET RETURN CODE
         RETURN (2,12),T           RETURN TO SYSTEM
*
*        CONSTANTS
*
INVALID  DC    C'INVALID COMMAND NAME SUPPLIED'
VALID    EQU   X'C0'               TEST FOR VALID COMMAND FROM SCAN
         LTORG ,
 TITLE 'DYNAMIC STORAGE MAP FOR APF COMMAND'
DS       DSECT ,                   NAME FOR AREA
         DS    18F                 NORMAL SAVE AREA
CSPL     DS    6F                  PARM AREA FOR SCAN
OUTAREA  DS    2F                  ANSWER AREA FOR SCAN
CPECB    DS    1F                  ECB FOR SCAN INVOKATION
FLAG     DS    1F                  FLAG WORD
EPLOC    DS    CL8                 STORAGE FOR COMMAND NAME
DSLEN    EQU   *-DS                LENGTH OF AREA
 TITLE 'STANDARD MAPPING MACROS'
         SPACE 3
         PRINT NOGEN
         IKJTCB ,                  TCB
         SPACE 3
         IEFTCT ,                  TCT
         SPACE 3
         IEFJMR ,                  JMR
         SPACE 3
         IHAPSA ,                  PSA
         SPACE 3
APF      CSECT ,
         $JLSE ,                   JLSE (LOCAL DEFINITION)
         SPACE 3
         CVT DSECT=YES             CVT (REQUIRED BY CALLTSSR)
         SPACE 3
         IKJCPPL ,                 CPPL
         SPACE 3
         IKJCSOA ,                 COMMAND SCAN OUTPUT AREA
         END
