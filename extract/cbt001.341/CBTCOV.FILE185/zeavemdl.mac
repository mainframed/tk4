YMDL     TITLE 'ZEAVEMDL--PREFIX ROUTINE FOR IEAVEMDL'
***********************************************************************
*                                                                     *
*        NAME:     ZEAVEMDL                                           *
*                                                                     *
*        ENTRY:    SINCE THIS ROUTINE IS LINKED "IN FRONT"            *
*                  OF IEAVEMDL, THE PARAMETERS IT RECEIVES ARE THOSE  *
*                  MEANT FOR THAT ROUTINE.  R13, R14, AND R15 ARE USED*
*                  AS FOR STANDARD OS LINKAGE.  R1 POINTS TO A        *
*                  POINTER TO A PARAMETER LIST OF THE FORM:           *
*                     XL2          FLAGS                              *
*                     AL2(ASID)                                       *
*                     A(ASCB)                                         *
*                     A(TCB)                                          *
*                                                                     *
*        FUNCTION: THIS ROUTINE DELETES THE POINTER IN THE            *
*                  XASVT TO THE XASCB FOR THIS ADDRESS SPACE, THEN    *
*                  FREES THE XASCB.                                   *
*                                                                     *
*        EXIT:     THIS ROUTINE ALWAYS EXITS NORMALLY.                *
*                  IT DOES THIS BY PASSING CONTROL TO IEAVEMDL        *
*                  AS IF IT WERE XCTL'ING; I.E., CONTROL IS           *
*                  PASSED BACK FROM IEAVEMDL TO THIS ROUTINE'S        *
*                  CALLER.                                            *
*                                                                     *
*        ATTRIBUTES:  REENTRANT, REFRESHABLE                          *
*                                                                     *
*        VERSION:  1.0   DATE:  7/24/74   AUTHOR:  T. DAHLQUIST       *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         INITIAL
         EJECT
IEAVEMDL CSECT
         YREGS
         USING *,R15
         STM   R0,R3,12(R13)
         SPACE 1
         DO    GET -> XASCB FROM XASVT
         L     R2,CVTPTR      -> CVT
         USING CVT,R2
         L     R2,CVTUSER     -> XCVT
         USING XCVT,R2
         L     R2,XCVTXASV    -> XASVT
         USING XASVT,R2
         L     R1,0(,R1)      GET -> PARM LIST
         LH    R3,2(,R1)      GET ASID
*****
*****   NOTE THAT THE ABOVE LINE DEPENDS ON THE FORMAT
*****   OF THE PARM LIST FOR IEAVEMDL REMAINING THE SAME.
*****
         SLA   R3,2           CONVERT ASID TO FULLWORD OFFSET
         L     R1,XASVENTY-4(R3)   -> XASCB
         SR    R0,R0
         ST    R0,XASVENTY-4(R3)
         OD    XASVT ENTRY ZEROED, -> XASCB IN R1
         SPACE 1
         DO    FREE XASCB
         L     R0,GETXASC     SUBPOOL, LENGTH
         LR    R3,R15
         FREEMAIN R,LV=(0),A=(1)
         DROP  15
         USING IEAVEMDL,3
         OD    XASCB FREED.
         SPACE 1
         DO    PASS CONTROL TO IEAVEMDL
         L     R15,=V(YEAVEMDL)    GET ADDRESS TO BRANCH TO,
         LM    R0,R3,12(R13)  RESTORE REGS
         BR    R15            AND GO.
         OD
         FINAL
         TITLE 'CONSTANTS'
GETXASC  DS    0F
         DC    AL1(XASCSUBP),AL3(XASCBL)
         LTORG
         SPACE 1
         TITLE 'DSECTS'
         PRINT GEN
         CVT   DSECT=YES
         XCVT
         XASVT
         XASCB
         END
