MRGE     TITLE 'UPDMERGE -- INITIAL DEFINITIONS'
         GBLA  &BUFSPCE            APPROX BUF SPACE 4 EACH DS
         GBLA  &CARDSZE            IBM CARD LENGTH
         GBLA  &LINECT             SYSPRINT PAGE HEIGHT
         GBLA  &MAXBUF             MAX # BUFS FOR EACH DS
         GBLA  &MINBUF             MIN # BUFS FOR EACH DS
         GBLA  &TEMP               WORK VARIABLE
         SPACE 3
&BUFSPCE SETA  5*1024              APPROX BUF SPACE FOR EACH DS
&CARDSZE SETA  80                  IBM CARD LENGTH
&LINECT  SETA  58                  SYSPRINT PAGE HEIGHT
&MAXBUF  SETA  10                  MAX # BUFS FOR EACH DS
&MINBUF  SETA  3                   MIN # BUFS FOR EACH DS
         $PUT  SUBAD=PUTSYSP,MF=INIT
         SPACE 3
         $REGS ,
         TITLE 'UPDMERGE -- DSECTS'
         PRINT NOGEN
         SPACE 3
CVTDSECT DSECT
         CVT
         SPACE 3
OWADSECT DSECT
         ORG   *+100
         IEFJFCBN
         EJECT ,
DCBDSECT DSECT
         $DCBD DSORG=(PS,PO)
         SPACE 1
*                                  BSAM/BPAM DCB EXTENSION
         ORG   DCBDCB+88
DCBDECBC DS    A                   PTR TO CURRENT DECB
DCBRCDPT DS    A                   PTR TO CURRENT LOGICAL RECORD
DCBLKEND DS    A                   END PTR FOR CURRENT BLOCK
         SPACE 1
*                                  QSAM DCB EXTENSION
         ORG   DCBDCB+96
DCBMEMBR DS    CL8                 CURRENT PROCESSING MEMBER NAME
DCBDELET DS    CL8                 LAST SEQ # TO DELETE FROM THIS DCB
DCBFLAG  DS    X                   FLAG BYTE
DCBUPDAT DS    X                   CURRENT UPDATE FUNCTION
DCBID    DS    C                   SYSIN DCB ID
         SPACE 1
*        DCBFLAG BIT DEFINITIONS
ADDREPL  EQU   X'01'               DCB IS CURRENTLY ADDING / REPLACING
ONBIT    EQU   X'80'               4 SPCL COND CODE SETTING
         SPACE 1
*        DCBUPDAT BIT DEFINITIONS
*        THESE DEFINITIONS MUST BE IN ALPHA ORDER
ADD      EQU   X'01'               ./ ADD CARD
CHNGE    EQU   X'02'               ./ CHNGE CARD
DELET    EQU   X'04'               ./ DELET CARD
ENDUP    EQU   X'08'               ./ ENDUP CARD
REPL     EQU   X'10'               ./ REPL CARD
REPRO    EQU   X'20'               ./ REPRO CARD
RESEQ    EQU   X'40'               ./* RESEQ CARD
         EJECT ,
DECBDSCT DSECT ,
DECBECB  DS    A                   EVENT CONTROL BLOCK
DECBTYPE DS    2X                  DECB TYPE FLAGS
DECBDCB  DS    A                   DATA CONTROL BLOCK POINTER
DECBBUFR DS    A                   I/O BUFFER POINTER
DECBIOB  DS    A                   I/O BLOCK POINTER
DECBLINK DS    A                   LINK TO NEXT DECB
DECBLEN  EQU   *-DECBDSCT          LENGTH OF THE DECB
         SPACE 1
DECBIDLE EQU   X'3F'               LOCAL DECB IDLE/AVAILABLE FLAG
         SPACE 3
         PRINT GEN
         TITLE 'UPDMERGE -- MERGE IEBUPDAT CONTROL DECKS'
*        STANDARD ENTRY LINKAGE FOR THREE BASES
         SPACE 1
UPDMERGE $ENTER BASES=3
         SPACE 3
*        INSURE SERIAL REUSABILITY
         SPACE 1
         MVI   FLAG,0              CLEAR FLAG BYTE
         MVI   NORESEQ,0           CLEAR RESEQUENCE CONTROL
         MVI   RCD+1,RCD0          RESTORE PGM COMPLETION CODE
         XC    STAKMOST,STAKMOST   CLEAR MAX STACK USAGE COUNT
         OI    LINES2GO,X'80'      FORCE PAGE EJECT ON NEXT PRINT LINE
         ZAP   PAGE,=P'0'          INITIALIZE PAGE COUNTER
         MVI   BKSIERRM,C'-'       RESTORE ERR MSG CARAGE CTL CHAR
         MVC   TITLE+16(39),=C'(IEBUPDAT CONTROL DECK MERGING PROGRAM)'
         MVC   DDMISSNG+10(8),PUNDDNAM ASSUME PUNCH DD CARD MISSING
         OI    CDSTACK+4,X'80'     FLAG NO GETMAIN YET FOR STACK
         MVC   CDSTACK+8(24),CDSTACK+4 PROPAGATE TO OTHER DESCRIPTERS
         SPACE 3
*        EXAMINE THE PARM FIELD
         SPACE 1
         L     R1,0(,R1)           POINT TO THE PARM (IF ANY)
         CLC   0(6,R1),ONLYPARM    IS THE PARM FIELD GIVEN?
         BNE   SYSPOPEN            NO, SKIP
         OI    FLAG,ECHO           YES, ALLOW ECHO OUTPUT
         SPACE 3
*        OPEN VARIOUS DATA SETS
         SPACE 1
SYSPOPEN OPEN  (SYSPRINT,OUTPUT)   OPEN SYSPRINT
         TM    PRTOFLGS,X'10'      SYSPRINT OPENED?
         BNO   ABORTA              NO, TERMINATE
MISCOPEN OPEN  (SYSLIB,,SYSUT1,OUTIN,SYSPUNCH,OUTPUT)
         LA    R1,DDMISSNG-1       YES, ASSUME SYSPUNCH NOT OPEN
         TM    PUNOFLGS,X'10'      TRUE?
         BNO   ABORTC              YES, TERMINATE
         SPACE 3
*        DETERMINE HOW MANY INPUT DATA SETS ARE TO BE MERGED
         SPACE 1
         L     R1,CVTPTR           POINT TO THE CVT
         L     R1,CVTTCBP-CVTDSECT(,R1) PNT TO OLD/NEW TCB PTRS
         L     R1,4(,R1)           POINT TO OUR TCB
         L     R1,12(,R1)          POINT TO OUR TIOT
         LA    R2,24(,R1)          POINT TO 1ST DD CARD ENTRY
         LA    R3,C'0'             START AT SYSIN1 -1
TIOTLP1  LA    R3,1(,R3)           GET NEXT SYSIN DDNAME
         STC   R3,INXDDNAM+5       BUILD COMPLETE SYSIN DDNAME
         LR    R1,R2               POINT TO 1ST DD ENTRY IN TIOT
         SR    R0,R0               CLEAR DD ENTRY LENGTH REGISTER
TIOTLP2  AR    R1,R0               POINT TO "NEXT" DD ENTRY
         IC    R0,0(,R1)           SAVE ITS LENGTH
         LTR   R0,R0               ACTUALLY END OF TIOT?
         BZ    GOTALLDD            YES, GOT ALL SYSIN DDNAMES
         CLC   INXDDNAM,4(R1)      NO, IS THIS THE NEXT SYSIN DDNAME?
         BNE   TIOTLP2             NO, GO LOOK AT NEXT DD ENTRY
         B     TIOTLP1             YES, GO SEE IF THERE IS ANOTHER DD
GOTALLDD MVC   DDMISSNG+10(8),INXDDNAM ASSUME NO SYSIN DD CARDS
         LA    R1,DDMISSNG-1       EVEN POINT TO THE ERR MSG
         BCTR  R3,R0               GET NUMBER ID OF LAST SYSIN DDNAME
         STC   R3,INXCOUNT+1       SAVE IN MSG ICO AOK
         N     R3,=X'0000000F'     CONVERT CHAR TO BINARY; ANY SYSIN'S?
         BZ    ABORTC              NO, PRNT ERR MSG AND TERMINATE
         $PUT  INXCOUNT            YES, PRINT COUNT MSG
         SPACE 3
*        PRINT SYSLIB STATUS MESSAGE
         SPACE 1
         LA    R1,SYSLNAME-1       ASSUME SYSLIB EXISTS
         TM    LIBOFLGS,X'10'      TRUE?
         BO    LIBPTMSG            YES, GO PRINT THE MSG
         OI    NORESEQ,NOSYSLIB    NO, FLAG NO RESEQUENCING
         LA    R1,NOSYSLMS-1       PNT TO MSG
LIBPTMSG $PUT  (R1)                PRINT SAYING (NO) SYSLIB
         SPACE 3
*        GET AND INITIALIZE CORE FOR INPUT DCB'S
         SPACE 1
         STH   R3,WORKAREA         STORE SYSIN DD COUNT
         LA    R0,INXLEN+4         GET LEN OF 1 DCB + DCB PTR
         MH    R0,WORKAREA         GET CORE SIZE FOR INPUT DCB'S
         ST    R0,OPENLIST+4       SAVE FOR LATER FREEMAIN
         GETMAIN R,LV=(0),HIARCHY=1 GET THE CORE
         ST    R1,OPENLIST         SAVE PTR TO DCB POINTER LIST
         LR    R9,R3               GET # DCB'S
         SLL   R9,2                GET LEN OF DCB PTR LIST
         AR    R9,R1               POINT PAST PTR'S TO 1ST DCB PLACE
         USING DCBDSECT,R9         DCL DCB BASE FOR REST OF PROGRAM
         LA    R2,C'1'             GET ID FOR 1ST DCB (SYSIN1)
BUILDCBS ST    R9,0(,R1)           SAVE DCB PTR IN OPEN/POINTER LIST
         LA    R1,4(,R1)           ADVANCE TO NEXT POINTER PLACE
         STC   R2,INXDDNAM+5       COMPLETE THE INPUT DDNAME (SYSIN*)
         STC   R2,INXID            SAVE THE ID FOR THIS DCB
         LA    R2,1(,R2)           GET NEXT ID #
         MVC   DCBDCB(INXLEN),SYSINX GENERATE DCB FOR THIS SYSIN*
         LA    R9,INXLEN(,R9)      PNT TO NEXT DCB PLACE
         BCT   R3,BUILDCBS         LOOP TO BUILD IT
         SH    R1,=Y(4)            BACK UP TO LAST DCB PTR
         OI    0(R1),LASTDCB       FLAG IT AS LAST
         L     R1,OPENLIST         POINT TO START OF PTR LIST
         OPEN  MF=(E,(1))          OPEN THE INPUT DCB'S
         TM    FLAG,ABORTION       ANY PROBLEMS UP TO HERE?
         BO    ABORTD              YES, TERMINATE
         SPACE 3
*        GET SPACE FOR IN CORE CARD (RESEQUENCE) STACK
         SPACE 1
         GETMAIN VU,LA=GETMQTY,A=CDSTACK,HIARCHY=1 GET AS MUCH AS POSBL
         LM    R2,R3,CDSTACK       GET SPACE'S ADDRESS AND LENGTH
         LH    R4,=Y(4096)         GET LENGTH TO GIVE BACK TO OS
         FREEMAIN R,LV=(R4),A=(R2) GIVE OS SOME ELBOW ROOM
         AR    R2,R4               POINT TO NEW START OF STACK
         SR    R3,R4               GET ITS NEW LENGTH
         STM   R2,R3,CDSTACK       SAVE THE STACK'S DESCRIPTERS
         ST    R2,STACKPTR         EMPTY THE STACK
         SR    R2,R2               CLEAR FOR DEVIDE
         LA    R1,&CARDSZE 
         DR    R2,R1               GET # CARDS THAT CAN B STACKED
         ST    R3,STACKMAX         SAVE IT
         SPACE 3
*        FORCE EACH INPUT DATA SET TO BE SKIPPED TO ITS FIRST MEMBER
*        SELECTING CONTROL CARD
         SPACE 1
         L     R7,OPENLIST         POINT TO THE DCB POINTERS
SETSKIP  OI    0(R7),SKIPDCB       FORCE IT TO SKIP TO CONTROL CARD
         TM    0(R7),LASTDCB       END OF POINTER LIST?
         LA    R7,4(,R7)           ASSUME NOT
         BNO   SETSKIP             NO, LOOP FOR NEXT POINTER
         SPACE 3
         TM    FLAG,ECHO           ECHO OUTPUT?
         BZ    NEXTMEMB            NO, DON'T ALTER TITLE
         MVC   TITLE+16(39),TITLE+15 CLEAR FOR SECONDARY TITLES
         SPACE 3
*        MAIN PROCESSING LOOP
*        INSURE THAT ALL I/O IS COMPLETED ON SYSLIB
         SPACE 1
NEXTMEMB TM    FLAG,MEMBRFND       WAS SYSLIB USED? / IS CHECK LP DONE?
         BZ    CHCKDONE            NO/YES, SKIP OUT
         BAL   R14,CHECKLIB        YES/NO, GO CHECK OUTSTANDING READ
         B     NEXTMEMB            LOOP FOR NEXT I/O
CHCKDONE NI    FLAG,ECHO           CLEAR MOST FLAGS
         NI    NORESEQ,NOSYSLIB    RESET RESEQUENCING CONTROL
         MVC   LIBNEXT,=8X'FF'     INITIALIZE AS IF NO NEXT MEMBER
         SPACE 3
*        IDENTIFY BOTH THE NEXT MEMBER TO PROCESS AND THE FIRST INPUT
*        DCB THAT PROCESSES IT
         SPACE 1
         L     R7,OPENLIST         POINT TO THE DCB PTRS
         LR    R6,R7               SAVE "FIRST ACTIVE DCB" PTR
         L     R8,0(,R7)           POINT TO "1ST ACTV DCB"
FINDLWST L     R9,0(,R7)           POINT TO NEXT DCB
         TM    0(R7),SKIPDCB       SKIP TO NEXT CONTROL CARD?
         BZ    SKIPDONE            NO, CONTINUE
SKIPLOOP BAL   R14,GETCARD         YES, READ NEXT CARD
         BC    7,GETCERR           IF ^ CTL CD, THEN ERROR
SKIPDONE CLC   DCBMEMBR,DCBMEMBR-DCBDSECT(R8) LOOK 4 LOWEST NEXT MEMBER
         BNL   NOTLOWST            NOT THIS DCB; SKIP
         LR    R8,R9               ON THE OTHER HAND, SAVE THIS POINTER
         LR    R6,R7                AND THIS POINTER POINTER
NOTLOWST LA    R7,4(,R7)           POINT TO NEXT DCB PTR
         LTR   R9,R9               WAS THIS THE LAST DCB?
         BNM   FINDLWST            NO, GO SEE IF IT'S LOWER
         SPACE 3
         CLC   DCBMEMBR-DCBDSECT(,R8),=8X'FF' ALL FINISHED?
         BE    EOP                 YES, TERMINATE
         ST    R6,FRSTACTV         SAVE PTR 2 PTR 4 1ST ACTV DCB
         TM    FLAG,ECHO           ECHO OUTPUT?
         BZ    NOECHO2             NO, DON'T ALTER OR FORCE TITLE
         MVC   TITLE+39(8),DCBMEMBR-DCBDSECT(R8) MEMBR NAME TO TITLE
         OI    LINES2GO,X'80'      FORCE IMEDIATE PAGE EJECT
NOECHO2  MVC   IDMVC(2),NOPR       SET NULL MEMBER SEQ # ID
         SPACE 3
*        ATTEMPT TO LOCATE THE NAMED MEMBER IN SYSLIB
         SPACE 1
         LA    R1,MRGMSG-1         ASSUME NO SYSLIB
         TM    LIBOFLGS,X'10'      SYSLIB AVAILABLE?
         BZ    BLDLDONE            NO, SKIP
         FIND  SYSLIB,DCBMEMBR-DCBDSECT(,R8),D YES, FIND THE MEMBER
         B     *+4(R15)            WHAT HAPPENED WITH THE FIND?
         B     GOTMEMBR            MEMBER FOUND
         B     NOMEMBR             MEMBER NOT FOUND
         OI    NORESEQ,LIBIOERR    I/O ERR IN DIRECTORY; KILL RESEQ
         LA    R1,DCTYIOER-1       POINT TO ERROR MSG
         CLI   RCD+1,RCD8          CHK COMPLETION CODE
         BNL   BLDLDONE            ALREADY SET
         MVI   RCD+1,RCD8          RESET
         B     BLDLDONE            GO PRINT IT
NOMEMBR  LA    R1,MBRNEXST-1       PNT TO NO MEMBER MSG
         B     BLDLDONE            GO PRINT IT
GOTMEMBR OI    FLAG,MEMBRFND       REMEMBER THAT MEMBER FOUND
         LA    R1,MBREXIST-1       PNT TO MEMBR FOUND MSG
BLDLDONE MVC   2(8,R1),DCBMEMBR-DCBDSECT(R8) MBR NME TO MSG
         $PUT  (R1)                PRINT THE MSG
         SPACE 3
*        IDENTIFY ALL INPUT DCB'S THAT PROCESS THIS MEMBER
         SPACE 1
         LR    R7,R6               PNT TO 1ST ACTIVE DCB PTR
FINDSAME L     R9,0(,R7)           POINT TO "NEXT" DCB
         CLC   DCBMEMBR,DCBMEMBR-DCBDSECT(R8) SAME MEMBER?
         BNE   NOTSAME             NO, SKIP
         OI    0(R7),ACTVDCB       YES, FLAG IT AS ACTIVE
         XC    DCBDELET,DCBDELET   CLEAR DELET RANGE END
         MVI   DCBFLAG,ONBIT       INITIALIZE THE FLAG BYTE
         L     R5,DCBRECAD         POINT TO THE CONTROL CARD
         MVC   INPFNCTN+4(5),9(R5) PUT FUNCTION INTO MSG
         MVC   INPFNCTN+L'INPFNCTN-1(1),DCBID NAME THE SYSIN
         $PUT  INPFNCTN            PRINT INFO MSG
NOTSAME  LA    R7,4(,R7)           POINT TO NEXT POINTER
         LTR   R9,R9               IS THERE A NEXT POINTER?
         BNM   FINDSAME            YES, GO SEE IF IT'S 4 THE SAME MEMBR
         $PUT  BLNK1               NO, PRINT A BLNK LINE
         SPACE 3
*        RESOLVE THE OLD UPDATE FUNCTION AGAINST THE MEMBER'S
*        (NON-)EXISTANCE
         SPACE 1
         LR    R7,R6               POINT TO 1ST ACTIVE DCB PTR
         LR    R9,R8               POINT TO 1ST ACTIVE DCB
RESOLVE1 L     R5,DCBRECAD         POINT TO FUNCTION CARD
         TM    NORESEQ,NOSYSLIB+LIBIOERR "NO" SYSLIB?
         BNZ   RESOLVE2            YES, ASSUME RESOLUTION UNNECESSARY
         TM    FLAG,MEMBRFND       NO, SPECIFIED MEMBER EXIST?
         BZ    NOMEMBR2            NO, GO PROCESS DIFFERENTLY
         TM    DCBUPDAT,ADD        YES, "./ ADD"?
         BNO   RESOLVE2            NO, RESOLUTION COMPLETE
         MVC   9(4,R5),REPLVERB    YES, CHANGE TO "./ REPL"
         MVI   DCBUPDAT,REPL       CHANGE TO "./ REPL"
         B     RESOLVE2            RESOLUTION COMPLETE
NOMEMBR2 TM    DCBUPDAT,ADD        NO MEMBER; "./ ADD"?
         BO    RESOLVE2            YES, AOK; RESOLUTION COMPLETE
         TM    DCBUPDAT,REPL       NO, "./ REPL"?
         BNO   OMITIT              NO, ERROR
         MVC   9(4,R5),ADDVERB     YES, CHANGE TO "./ ADD"
         MVI   DCBUPDAT,ADD        CHANGE TO "./ ADD"
         B     RESOLVE2            RESOLUTION COMPLETE
OMITIT   XI    0(R7),ACTVDCB+SKIPDCB IMPOSSIBLE COMBO; KILL THIS DCB
         BAL   R14,FIND1ST         RESET 1ST ACTIVE DCB PTR
         BC    8,NEXTMEMB          NO ACTIVES LEFT; LOOP TO NEXT MEMBR
         LR    R7,R15              POINT TO 1ST ACTIVE DCB PTR
         L     R9,0(,R7)           POINT TO 1ST ACTIVE DCB
         B     RESOLVE1            YES, GO REDO THE 1ST RESOLUTION
         SPACE 3
*        RESOLVE THE OLDER UPDATE FUNCTIONS AGAINST THE NEWER UPDATE
*        FUNCTIONS
         SPACE 1
RESOLVE2 LR    R8,R9               SAVE OLDER DCB PTR
         LR    R6,R7               SAVE OLDER DCB PTR PTR
         LR    R4,R5               SAVE OLDER FUNCTION CARD
RESOLVE3 LTR   R9,R9               IS THIS THE LAST/NEWEST DCB?
         BM    RSLVDONE            YES, RESOLUTION IS COMPLETE
         LA    R7,4(,R7)           NO, POINT TO NEXT/NEWER DCB PTR
         L     R9,0(,R7)           POINT TO NEXT/NEWER DCB
         TM    0(R7),ACTVDCB       IS IT ACTIVE FOR THIS MEMBER?
         BNO   RESOLVE3            NO, LOOP FOR STILL NEWER DCB
         L     R5,DCBRECAD         YES, POINT TO ITS FUNCTION CARD
         TM    DCBUPDAT-DCBDSECT(R8),ADD IS OLDER "./ ADD"?
         BO    OLDADD              YES, GO PROCESS
         TM    DCBUPDAT,ADD+REPL   NO, IS NEWER ./ ADD OR REPL?
         BZ    NTNWREPL            NO, SKIP
         MVC   9(5,R5),REPLVERB    YES, CHANGE IT TO ./ REPL
         MVI   DCBUPDAT,REPL       CHANGE IT TO ./ REPL
ADRPLRPO L     R1,FRSTACTV         GET OLD 1ST ACTV PTR
         ST    R7,FRSTACTV         SET NEW 1ST ACTV PTR
KILLLOOP CR    R1,R7               ADVANCED TO NEW 1ST ACTV YET?
         BE    RESOLVE2            YES, ALL PREVIOUS DCB'S KILLED
         TM    0(R1),ACTVDCB       NO, WAS THIS ONE ACTIVE?
         BZ    KILLED              NO, CONTINUE
         XI    0(R1),ACTVDCB+SKIPDCB YES, KILL IT & SKIP IT 2 NEX FCTN
KILLED   LA    R1,4(,R1)           POINT TO NEXT DCB PTR
         B     KILLLOOP            SEE IF MORE KILLING IS TO BE DONE
NTNWREPL TM    DCBUPDAT-DCBDSECT(R8),REPRO IS OLD ./REPRO?
         BO    ADRPLRPO            YES, KILL IT
OLDADCHG MVC   15(&CARDSZE-15,R4),15(R5) NO, SET OLD FCTN TO NEW FCTN
         TM    DCBUPDAT,CHNGE      IS NEW FCTN ./CHNGE?
         BO    RESOLVE3            YES, THIS PAIR IS RESOLVED
         XI    0(R7),ACTVDCB+SKIPDCB NO, IT'S ./REPRO; KILL IT
         B     RESOLVE3            THIS PAIR IS RESOLVED
OLDADD   TM    DCBUPDAT,CHNGE+REPRO OLD=./ADD; IS NEW ./CHNGE OR REPRO?
         BNZ   OLDADCHG            YES, GO PROCESS
         MVC   9(4,R5),ADDVERB     NO, FORCE NEW TO ./ADD
         MVI   DCBUPDAT,ADD        FORCE NEW TO ./ADD
         B     ADRPLRPO            CONTINUE PROCESSING
RSLVDONE MVC   FCTNCARD,0(R4)      SAVE RESULTANT FUNCTION CARD
         SPACE 3
*        FOR ./ADD OR ./REPL CARDS, SET THE LIST FLAG TO "1";
*        OTHERWISE, SET IT TO "0".
         SPACE 1
         MVI   FCTNCARD+L'FCTNCARD-9,C' ' INSURE DELIMITING BLANK
         LA    R1,FCTNCARD+14      PNT TO B4 START OF OPERANDS
LISTSRCH LA    R1,1(,R1)           POINT TO NEXT COLUMN
         CLI   0(R1),C' '          END OF OPERANDS?
         BE    LISTNFND            YES, FORGET IT
         CLI   0(R1),C','          NO, END OF MEMBER NAME?
         BNE   LISTSRCH            NO, LOOP TO CHECK AGAIN
         MVI   6(R1),C'0'          YES, ASSUME LIST NOT WANTED
         TM    DCBUPDAT-DCBDSECT(R8),REPL+ADD CORRECT?
         BZ    LISTNFND            YES, DONE
         MVI   6(R1),C'1'          NO, SET LIST WANTED
         OI    DCBFLAG-DCBDSECT(R8),ADDREPL REMEMBER ADD / REPL SECTION
         NI    FLAG,255-MEMBRFND   SYSLIB MEMBER NOT NEEDED
LISTNFND EQU   *
         SPACE 3
*        SPECIAL HANDLING FOR ./REPRO CARDS
         SPACE 1
         TM    DCBUPDAT-DCBDSECT(R8),REPRO ./REPRO?
         BNO   NOTREPRO            NO, SKIP
         TM    NORESEQ,NOSYSLIB+LIBIOERR YES, RESEQUENCE?
         BNZ   PUTREPRO            NO, SKIP
         MVC   FCTNCARD+L'FCTNCARD-8(8),FCTNCARD CLEAR THE SEQUENCE FIE
PUTREPRO OI    FLAG,FCTNPUTD       PREVENT REDUNDANT PUTS
         LA    R1,FCTNCARD         POINT TO THE FUNCTION CARD
         BAL   R14,PUTCARD         PUNCH AND PRINT IT
         XI    0(R6),ACTVDCB+SKIPDCB KILL AND SKIP THE DCB
         B     NEXTMEMB            GO PROCESS NEXT MEMBER
NOTREPRO EQU   *
         SPACE 3
*        READ THE FIRST CARD AFTER THE FUNCTION CARD FOR EACH ACTIVE
*        DCB
         SPACE 1
         LR    R7,R6               POINT TO 1ST ACTV DCB PTR
GET1STCD L     R9,0(,R7)           POINT TO "NEXT" DCB
         TM    0(R7),ACTVDCB       IS IT ACTV FOR THIS MEMBER?
         BNO   SKIPGET1            NO, SKIP IT
         BAL   R14,GETCARD         YES, GO READ IN ITS 1ST CARD
SKIPGET1 LA    R7,4(,R7)           POINT TO NEXT PTR
         LTR   R9,R9               WAS THIS THE LAST DCB?
         BNM   GET1STCD            NO, LOOP TO GET ITS 1ST CARD
         L     R7,FRSTACTV         POINT TO 1ST ACTV DCB PTR AGAIN
         LTR   R7,R7               ALL NULL UPDATES?
         BZ    NEXTMEMB            YES, GO FOR NEXT MEMBER
         SPACE 3
*        IF THE SYSLIB IS TO BE USED, THEN PRIME ITS DECB'S
         SPACE 1
         L     R9,0(,R7)           POINT TO 1ST ACTV DCB
         TM    NORESEQ,NOSYSLIB+LIBIOERR SYSLIB AVAILABLE?
         BNZ   NORESEQ0            NO, SKIP
         TM    DCBFLAG,ADDREPL     YES, WILL IT BE USED?
         BNZ   NOPRIMEL            NO, SKIP
         L     R2,LIBDECBC         YES, POINT TO SYSLIB'S 1ST DECB
         USING DECBDSCT,R2         DCL DECB BASE
PRIMELIB L     R2,DECBLINK         ADVANCE TO NEXT DECB
         READ  (R2),SF,MF=E        READ WITH IT
         C     R2,LIBDECBC         ALL BUFFERS PRIMED?
         BNE   PRIMELIB            NO, GET READ AGAIN
         BAL   R14,CHECKLIB        YES, CHECK THE 1ST READ
         DROP  R2                  KILL DECB BASE
         L     R5,LIBRCDPT         POINT TO THE 1ST RECORD
         MVC   CARDNEXT,0(R5)      SAVE 1ST SYSLIB CARD
         LA    R1,&CARDSZE-8(,R5)  --> 1ST RCD'S SEQ #
         B     GETID               GO SCAN IT
NOPRIMEL EQU   *
         SPACE 3
*        IDENTIFY THE SEQUENCE FIELD NON-NUMERICS (IF ANY)
         SPACE 1
         L     R5,DCBRECAD         --> 1ST CARD AFTER FUNCTION CARD
         LA    R1,15(,R5)          ASSUME ./DELET; --> 1ST SEQ FLD
         TM    DCBUPDAT,DELET      ./DELET?
         BNZ   GETID               YES, PROCEED
         LA    R1,&CARDSZE-8(,R5)  NO, SEQ # IS IN COLS. 73-80
GETID    BAL   R14,GETSEQ#         ISOLATE THE SEQ # ID
         XC    LIBPREV,LIBPREV     CLEAR PREVIOUS SEQ #
         LA    R1,LIBPREV-(&CARDSZE-8) GET PTR FOR SEQ ID MVC
         EX    0,IDMVC             MOVE SEQ # IT TO "PREVIOUS" SEQ #
         MVC   FCTNCARD+L'FCTNCARD-8(8),MBRID MOVE THE ID TO THE FCTN C
NORESEQ0 EQU   *
         SPACE 3
*        PREPARE TO ENTER MAIN MERGING LOOP
         SPACE 1
         XC    DELEND,DELEND       CLEAR
         B     NEXTPASS            ENTER LOOP; ALL DCB'S ALREADY PRIMED
         EJECT ,
*        MAIN MERGING LOOP. THINGS GET VERY OBSCURE FROM HERE ON IN
         SPACE 1
NEXTCARD BAL   R14,GETCARD         REPRIME THIS DCB
         SPACE 3
*        ALL OF THE CURRENTLY ACTIVE DCB'S HAVE A NEXT CARD. DETERMINE
*        WHICH OF THESE NEXT CARDS IS TO BE PROCESSED NEXT.
         SPACE 1
NEXTPASS MVC   NEXTSEQ#,=8X'FF'    SET TO MAX VALUE FOR LOW CARD SRCH
         SR    R6,R6               CLR NEXT LOW DCB PTR
         L     R7,FRSTACTV         PNT TO 1ST ACTIVE DCB
         LTR   R7,R7               ANY DCB'S LEFT FOR THIS MEMBER?
         BZ    EOM1                NO, DO FINAL EOM PROCESSING
NEXTDCB1 L     R9,0(,R7)           YES, PNT TO THE DCB
         TM    0(R7),ACTVDCB       IS THIS DCB ACTIVE?
         BO    PNT2CARD            YES, GO POINT TO ITS NEXT CARD
NEXTDCB2 LA    R7,4(,R7)           NO, PNT TO NEXT DCB PTR
         LTR   R9,R9               WAS THIS THE LAST DCB?
         BNM   NEXTDCB1            NO, LOOP TO TRY NEXT DCB
         B     LOWFOUND            YES, NEXT UPDATE CARD FOUND
         SPACE 1
PNT2CARD L     R1,DCBRECAD         PNT TO THIS DCB'S NEXT CARD
TESTCARD TM    DCBUPDAT,DELET      IEBUPDAT CONTROL CARD?
         BNZ   DLTECARD            YES, GO HANDLE DIFFERENTLY
         LA    R2,&CARDSZE-8(,R1) 
         CLC   0(8,R2),DCBDELET    SHOULD THIS CARD BE DELETED?
         BH    LOWCHCK             NO, GO SEE IF IT'S NEXT UPDATE CARD
SKIPCARD BAL   R14,GETCARD         YES, GET THE NEXT CARD FOR THIS DCB
         BC    7,TESTCARD          GO ANALYZE THE CARD
         B     NEXTPASS            END OF UPD FROM THIS DCB 4 THIS MEMB
DLTECARD LA    R2,15(,R1)          IT'S ./DELET; PNT TO SEQ #
         CLC   15(8,R1),DCBDELET   OVERLAP WITH A CURRENT DELETION?
         BH    LOWCHCK             NO, GO SEE IF IT'S NEXT UPD CARD
         LR    R5,R1               YES, POINT TO THE CARD
         BAL   R14,SETDELET        GO RESET SOME DELETE RANGES
         B     SKIPCARD            GO GET NEXT CARD FROM THIS DCB
LOWCHCK  CLC   0(8,R2),NEXTSEQ#    CMPR THIS CARD WITH CURRENT LOW
         BH    NEXTDCB2            NOT NEW LOW? LOOP TO NEXT DCB
         BL    NEWLOW              NEW LOW; GO RESET SOME THINGS
         L     R8,0(,R6)           SAME #; PNT TO OLD LOW'S DCB
         TM    DCBUPDAT-DCBDSECT(R8),DELET WAS IT A DELETE CARD?
         BNZ   NEXTDCB2            YES, OLD LOW STANDS
         XR    R7,R6               NO, INTERCHANGE -
         XR    R6,R7                DCB -
         XR    R7,R6                 POINTERS
         LR    R9,R8               PNT TO OLD LOW'S DCB
         BAL   R14,GETCARD         DELETE (SKIP) THE OLD LOW CARD
         LR    R7,R6               PNT BACK TO CURRENT DCB PTR
         L     R9,0(,R7)           PNT BACK TO CURRENT DCB
         B     NEXTDCB2            LOOP FOR NEXT DCB
NEWLOW   MVC   NEXTSEQ#,0(R2)      RESET THE LOW SEQ #
         LR    R6,R7               RESET THE LOW'S DCB PTR
         B     NEXTDCB2            LOOP FOR NEXT DCB
         SPACE 1
LOWFOUND LR    R7,R6               PNT TO DCB OF NEXT UPDATE CARD
         L     R9,0(,R7)           PNT TO DCB OF NEXT UPDATE CARD
         L     R5,DCBRECAD         PNT TO THE NEXT UPDATE CARD
EOM1     EQU   *
         SPACE 3
         TM    FLAG,DELETE         IS A DELETION BEING RESOLVED?
         BZ    STAKCHCK            NO, JUST CHECK CURRENT CARD
         SPACE 3
*        A PRIOR OR CURRENT DELETION IS ACTIVE. SEE IF THE CURRENT CARD
*        FALLS WITHIN THE DELETION RANGE.
         SPACE 1
         CLC   NEXTSEQ#,DELEND     THIS CD OVERLAP CURRENT DELETION?
         BH    ENDELETE            NO, SKIP
         SPACE 1
*        A PRIOR OR CURRENT UPDATE HAS REQUESTED THAT THE CURRENT
*        SEQUENCE NUMBER RANGE BE DELETED. THE CURRENT UPDATE CARD
*        FALLS WITHIN THE DELETION RANGE AND SUPERCEDES.
         TM    DCBUPDAT,DELET      IS CURRENT CARD ALSO A DELETE?
         BNZ   INSERT              YES, GO EXTEND THE DELETION RANGE IF
*                                  NECESSARY.
         TM    DCBUPDAT,RESEQ      IS CRNT CARD ./RESEQ ON/OFF?
         BNZ   INTERUPT            YES, BREAK IN
         TM    FLAG,MEMBRFND       NO, CURRENT SYSLIB MEMBER AVAIL?
         BZ    INSERT              NO, JUST STACK THE DATA CARD
         SPACE 1
*        THE SYSLIB MEMBER IS AVAILABLE; THE CURRENT CARD IS A DATA
*        CARD; IT FALLS WITHIN THE RANGE OF PREVIOUSLY DELETED SYSLIB
*        CARDS. IF THIS CARD IS IDENTICAL TO ONE OF THE DELETED SYSLIB
*        CARDS, THEN THIS CARD IS A RESTORATION; ACCORDINGLY, THE
*        DELETION RANGE HAS TO BE FORSHORTENED OR SEGMENTED.
         MVC   NXTLESS1,NEXTSEQ#   GET THIS CARD'S SEQ #
         IC    R1,NXTLESS1+L'NXTLESS1-1 GET ITS LAST BYTE
         BCTR  R1,0                DECRIMENT IT. NOTE, THIS WON'T WORK
*                                  SO WELL IF IT IS ALREADY X'00', BUT
*                                  THAT'S NOT SUPPOSED TO HAPPEN.
         STC   R1,NXTLESS1+L'NXTLESS1-1 STORE DECRIMENTED VALUE
         LA    R1,NXTLESS1         --> ADJUSTED NUMBER
         BAL   R14,GETLIB          ADVANCE SYSLIB TO THE PROPER POINT
         CLC   CARDNEXT,0(R5)      IS THE DATA CARD IDENTICAL TO A
*                                  PREVIOUSLY DELETED SYSLIB CARD?
         BNE   INSERT              NO, JUST GO STACK THE CARD
INTERUPT OI    FLAG,DLINTREQ       YES, REMEMBER DELETION INTERRUPTED
         MVC   DELENDSV,DELEND     SAVE THE DELETION RANGE'S END
         MVC   DELEND,LIBPREV      SET THE END FOR THE FIRST DELETION
*                                  SEGMENT
         SPACE 3
*        THE CURRENT UPDATE CARD FALLS BEYOND A CURRENTLY ACTIVE
*        DELETION RANGE. ADVANCE SYSLIB TO THE END OF THE DELETION
*        RANGE, AND THEN FALL THROUGH TO SEE IF THE CARD STILL FALLS
*        WITHIN THE CURRENT INSERTION RANGE.
         SPACE 1
ENDELETE TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF RESEQUENCING?
         BNZ   DLTETEST            NO, SKIP RESEQ RANGE DETERMINATION
         LA    R1,DELEND           YES, PNT TO END OF DELETION
         BAL   R14,GETLIB          SKIP SYSLIB TO PAST THE DLTE
         SPACE 3
*        DETERMINE IF THE CURRENT UPDATE CARD FALLS WITHIN THE CURRENT
*        INSERTION RANGE.
         SPACE 1
STAKCHCK TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF SYSLIB AVAILABLE?
         BNZ   DLTETEST            NO, NO INSERTION RANGE ACTIVE
         MVC   DELEND,LIBPREV      YES, IF THE CURRENT UPDATE CARD
*                                  FALLS OUTSIDE OF THE CURRENT RANGE,
*                                  THEN THE SEQUENCE NUMBER IN LIBPREV
*                                  EITHER MARKS THE START OF THE
*                                  RESEQUENCE VALUES FOR THE INSERTION
*                                  RANGE OR (IF THE INSERTION RANGE
*                                  INCLUDES A NON-NULL DELETION) MARKS
*                                  THE END OF THE DELETION (IN WHICH
*                                  CASE, DELSTART MARKS THE START OF
*                                  THE RESEQUENCE VALUES).
         MVC   RESEQLIM,LIBNEXT    SAVE RANGE END VALUE
         LTR   R7,R7               END OF MEMBER?
         BZ    EOM2                YES, THEN DON'T BOTHER SYSLIB
         LA    R1,NEXTSEQ#         NO, PNT TO SEQ # ON CURRENT CARD
         BAL   R14,GETLIB          SKIP SYSLIB TO PAST THIS POINT
         BC    11,PASTWALL         SYSLIB MOVED; WE'RE PAST THE WALL
         TM    DCBUPDAT,RESEQ      DIDN'T MOVE; IS CRNT ./RESEQ?
         BZ    INSERT              NO, JUST ADD CARD TO INSERTION
         MVC   RESEQLIM,NEXTSEQ#   YES, TERMINATE THE INSERTION
PASTWALL TM    NORESEQ,LIBIOERR    WAS THERE AN I/O ERROR?
         BNZ   DLTETEST            YES, FORGET ABOUT RESEQUENCING
EOM2     CLC   DELSTART,DELEND     IS THE DELETION NULL?
         BNH   DNOTNULL            NO, CONTINUE
         NI    FLAG,255-DELETE     YES, FORGET ABOUT IT
DNOTNULL OC    STACKCNT,STACKCNT   IS THERE ANYTHING STACKED?
         BZ    DLTETEST            NO, SKIP RESEQUENCE RANGE SET UP
         LA    R1,RESEQLIM         YES, PNT 2 TOP OF RESEQ RANGE
         BAL   R14,GETSEQ#         CONVERT IT TO BINARY
         ST    R1,TOP              SAVE FOR UNSTACK
         MVC   WORKAREA+8(8),MBRID SAVE SEQ # NON-NUMERICS
         LA    R1,DELEND           GET POSSIBLE START OF RESEQ RANGE
         TM    FLAG,DELETE         DOES THE RANGE START W/ DELETION?
         BZ    GETBOTTM            NO, CONTINUE
         LA    R1,DELSTART         YES, GET ADJUSTED RESEQ RANGE START
GETBOTTM BAL   R14,GETSEQ#         CONVERT IT TO BINARY
         ST    R1,BOTTOM           SAVE IT
         CLC   MBRID,WORKAREA+8    HAVE THE NON-NUMERICS CHANGED?
         BE    DLTETEST            NO, AOK
         LA    R1,7                YES, GENERATE A MAXIMUM TOP
         SR    R1,R0                (R0 = # NON-NUMERICS - 1)
         LA    R0,1                 (CONVERT THIS NUMBER TO A POWER OF
         BNP   NODIGITS             10)
DECSHIFT MH    R0,=Y(10)
         BCT   R1,DECSHIFT
NODIGITS ST    R0,TOP              RESET RANGE'S TOP
         SPACE 3
*        IF A DELETION IS PENDING, THEN BUILD A ./DELET CARD
         SPACE 1
DLTETEST TM    FLAG,DELETE         GENERATE A ./DELET CARD?
         BZ    NODELETS            NO, SKIP
         MVC   CARD(9),=CL9'./'    YES, GET "./       "
         MVC   CARD+9(6),DELEVERB  GET "DELETE "
         MVC   CARD+15(8),DELSTART GET DELETE START NUMBER
         MVI   CARD+23,C','        GET SEPERATING COMMA
         MVC   CARD+24(8),DELEND   GET DELETE ENDING NUMBER
         MVI   CARD+32,C' '        SET TRAILING BLANK
         MVC   CARD+33(L'DLTECMNT),DLTECMNT SET CARD'S COMMENTS
         MVC   CARD+L'CARD-8(8),DELSTART SET SEQ # FIELD
         LA    R1,CARD             PNT TO THE CARD
         BAL   R14,PUTCARD         GO PRINT AND PUNCH IT
         NI    FLAG,255-DELETE     NO LONGER RESOLVING A DELETION
NODELETS EQU   *
         SPACE 3
*        PUNCH OUT THE INSERTION STACK (IF ANY)
         SPACE 1
         BAL   R14,UNSTACK         PUNCH OUT THE STACK
         LTR   R7,R7               END OF MEMBER?
         BZ    NEXTMEMB            YES, GO PROCESS NEXT MEMBER
         SPACE 3
*        RECOGNIZE IF THE CURRENT CARD IS A ./* RESEQ (UPDMERGE
*        CONTROL) CARD
         SPACE 1
         TM    DCBUPDAT,RESEQ      ./RESEQ CARD?
         BZ    NOTRESEQ            NO, SKIP
         MVC    LIBPREV,NEXTSEQ#   YES, PARTITION THE RANGE
         NI    NORESEQ,255-TURNDOFF ASSUME ./RESEQ ON
         CLI   16(R5),C'N'         RIGHT?
         BE    GOTRESEQ            YES, PROCEED
         OI    NORESEQ,TURNDOFF    NO, IT'S ./RESEQ OFF
GOTRESEQ LR    R1,R5               --> CARD IMAGE
         BAL   R14,PUTCARD         PUNCH THE CARD
         TM    FLAG,DLINTREQ       WAS A DELETION INTERRUPTED?
         BNZ   DLRESTAR            YES, GO RESUME IT
         B     NEXTCARD            NO, JUST LOOP FOR NEXT CARD
NOTRESEQ EQU   *
         SPACE 3
*        CHECK TO SEE IF THE CURRENT CARD IS A SYSLIB REPLACEMENT
         SPACE 1
         TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF NO, RESEQUENCING?
         BNZ   INSERT              NO, SIMPLY PUNCH THE CARD
         TM    DCBUPDAT,DELET      YES, IS CRNT CARD ./DELET?
         BNZ   INSERT              YES, GO "STACK" IT
         CLC   LIBPREV,&CARDSZE-8(R5) NO, REPLACEMENT?
         BNE   INSERT              NO, IT'S AN INSERTION; STACK IT
         SPACE 3
*        THE CURRENT DATA CARD IS A REPLACEMENT. DETERMINE IF IT IS A
*        RESTORATION OF AN ORIGINAL SYSLIB CARD. IF SO, THEN DROP IT
*        FROM THE MERGED UPDATE.
         SPACE 1
         CLC   CARDPREV,0(R5)      UPDATE IDENTICAL?
         BNE   DONTSTAK            NO, GO PUNCH UPD CARD
         TM    FLAG,DLINTREQ       YES, DID IT INTERRUPT A DELETION
*                                  RANGE?
         BZ    NEXTCARD            NO, JUST DROP THIS CARD
DLRESTAR XI    FLAG,DLINTREQ+DELETE YES, FLIP SOME FLAGS
         MVC   DELSTART,LIBNEXT    SET DELETION RESUME POINT
         MVC   DELEND,DELENDSV     RESTORE DELETION END POINT
         B     NEXTCARD            DROP CURRENT DATA CARD
         SPACE 3
*        THE CURRENT CARD IS AN INSERT
         SPACE 1
INSERT   TM    DCBUPDAT,DELET      YES, IS THIS A ./DELET CARD?
         BZ    STAKTEST            NO, GO STACK (DELAY PUNCHING) THE CD
         TM    FLAG,DELETE         YES, IS A DELETION BEING RESOLVED?
         BO    DLRELIMT            YES, GO EXTEND THE DELETE RANGE
         OI    FLAG,DELETE         NO, BUT ONE IS BEING RESOLVED NOW
         MVC   DLTECMNT,33(R5)     SAVE 1ST DELETE CARD COMMENTS
         MVC   DELSTART,15(R5)     SET THE DELET RANGE START
         TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF RESEQUENCING?
         BNZ   DLRELIMT            NO, GO SET END OF DELET RANGE
         CLC   LIBPREV,15(R5)      YES, DEL START = A LIB SEQ #?
         BE    DLRELIMT            YES, START VALUE IS OK
         MVC   DELSTART,LIBNEXT    NO, SET START TO NEXT LIB SEQ #
DLRELIMT BAL   R14,SETDELET        GO SET SOME DELETE RANGES
         B     NEXTCARD            GO SEARCH FOR NEXT UPDATE CARD
         SPACE 3
*        DETERMINE WHETHER OR NOT THE CURRENT CARD SHOULD BE STACKED OR
*        PUNCHED IMMEDIATELY
         SPACE 1
STAKTEST TM    FLAG,DELETE         DELETION ACTIVE?
         BNZ   STAKCARD            YES, STACK THE CARD
         TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF NO, RESEQUENCING?
         BZ    STAKCARD            YES, STACK THE CARD
DONTSTAK LR    R1,R5               NO, --> CARD
         BAL   R14,PUTCARD         PUNCH IT
         B     NEXTCARD            LOOP FOR NEXT CARD
         SPACE 1
STAKCARD BAL   R14,STACK           DELAY PUNCHING UNTIL LATER
         B     NEXTCARD            GO READ NEXT CARD
         TITLE 'UPDMERGE -- PROGRAM TERMINATION'
EOP      OI    FLAG,FCTNPUTD       PREVENT CONFUSION IN PUTCARD
         TM    FLAG,ECHO           ECHO OUTPUT?
         BZ    NOECHO3             NO, DON'T ALTER TITLE × FORCE EJECT
         MVC   TITLE+16(39),TITLE+15 YES, CLEAR THE TITLE
         MVI   LINES2GO,X'80'      FORCE IMMEDIATE EJECT
NOECHO3  MVC   CARD(9),=CL9'./'    BUILD A -
         MVC   CARD+9(6),ENDUVERB   ./ENDUP -
         MVC   CARD+15(L'CARD-15),CARD+14   CARD
         LA    R1,CARD             POINT TO THE CARD
         BAL   R14,PUTCARD         GO PREITN AND PUNCH IT
         $PUT  EOPMSG              PRINT EOP MSG
         LA    R1,COREDECR-1       POINT TO MAY DECREASE MSG
         L     R3,STACKMAX         GET INCORE STACK CAPACITY
         S     R3,STAKMOST         GET DIFFERENCE WITH MOST USED
         MH    R3,=Y(&CARDSZE) 
         BNM   COREFIGR            MAY DECREASE; SKIP
         LA    R1,COREINCR-1       SHOULD INCREASE; PNT TO OTHER MSG
         LPR   R3,R3               GET POSITIVE DIFFERENCE
         LA    R3,1023(,R3)        SETUP FOR ROUNF UP
COREFIGR SRA   R3,10               DEVIDE BY 1024; ZERO?
         BZ    EOPA                YES, CORE IS JUST RIGHT
         CVD   R3,WORKAREA         NO, CONVERT # TO DECIMAL
         MVC   38(5,R1),EDITMASK   GET EDIT MASK
         ED    37(6,R1),WORKAREA+5 EDIT VALUE INTO MSG
         $PUT  (R1)                PRINT A MSG
         B     EOPA                CONTINUE
         SPACE 3
ABORTD   L     R2,OPENLIST         POINT TO THE SYSIN DCB PTR'S
         BAL   R14,CLOSE           CLOSE IT AND FREE ITS BUFFER POOL
         B     ABORTB              SKIP
         SPACE 3
ABORTC   $PUT  (R1)                PRINT ABORTION ERROR MSG
ABORTB   $PUT  ABORTED             SAY PROGRAM ABORTED
ABORTA   MVI   RCD+1,RCD16         SET ABORTION COMPLETION CODE
         SPACE 3
EOPA     LA    R2,MISCOPEN+4       PNT TO MISC DCB PTRS
         BAL   R14,CLOSE           CLOSE IT & FREE ITS BUFFER POOL
         LA    R2,SYSPOPEN+4       PNT TO SYSPRINT'S OPEN LIST
         BAL   R14,CLOSE           CLOSE IT
         SPACE 3
         LA    R2,4                LOOP CONTROL
         LA    R3,CDSTACK          POINT TO CORE DESCRIPTER LIST
FREELOOP TM    4(R3),X'80'         WAS THIS AREA GETMAIN'ED?
         BO    NOFREE              NO, SKIP
         FREEMAIN V,A=(R3)         YES, FREE IT
NOFREE   LA    R3,8(,R3)           ADVANCE TO NEXT DESCRIPTERS
         BCT   R2,FREELOOP         LOOP TO FREE IT
         SPACE 3
         LH    R15,RCD             GET PROGRAM COMPLETION CODE
         $EXIT ((R14,R12)),RC=(R15) RETURN TO CALLER
         SPACE 3
UT1ERR   SYNADAF ACSMETH=BSAM      SYSUT1 SYNAD EXIT
         L     R13,4(,R13)         RESTORE BASE REGISTER
TEMPDIE  LA    R15,ABORTED-1       POINT TO TERMINATION MSG
         BAL   R14,SYNADMSG        PRINT I/O ERR MSG + TERM MSG
         CLOSE SYSPRINT
         SVC   3                   TERMINATE BUT GOOD
         SPACE 3
PUNERR   SYNADAF ACSMETH=QSAM      SYSPUNCH SYNAD EXIT
         L     R13,4(,R13)         RESTORE BASE REGISTER
         B     TEMPDIE             GO DIE-DIE
         TITLE 'UPDMERGE -- ANY LEVEL SUBROUTINES'
*        FIND1ST - RESET THE POINTER TO THE 1ST ACTIVE DCB POINTER
         SPACE 1
FIND1ST  L     R15,OPENLIST        POINT TO 1ST DCB PTR
ACTVSRCH TM    0(R15),ACTVDCB      IS THIS DCB ACTIVE?
         BO    ACTVRSET            YES, GO SET THE POINTER (CC^=0)
         TM    0(R15),LASTDCB      NO, END OF POINTER LIST?
         LA    R15,4(,R15)         ASSUME NOT
         BNO   ACTVSRCH            IT'S NOT; LOOP TO CHECK NEXT PTR
         SR    R15,R15             YES, SET A ZERO PTR (ALSO CC=0)
ACTVRSET ST    R15,FRSTACTV        RESET THE PTR TO 1ST ACTV DCB PTR
         BR    R14                 RETURN TO CALLER (CC IS SET)
         SPACE 6
*        GETSEQ# - CONVERT A SEQUENCE NUMBER FIELD TO BINARY AND
*        ISOLATE ITS NON-NUMERIC PART
         SPACE 1
GETSEQ#  MVC   MBRID,0(R1)         GET THE SEQ # FIELD
         MVC   WORKAREA(8),=8C'0'  CLEAR WORK AREA TO EBCDIC ZEROS
         LA    R1,MBRID+8          POINT PAST THE SEQ #
         LA    R15,WORKAREA+8      POINT PAST THE WORK AREA
         LA    R0,8                GET LOOP CONTROL
SEQ#LOOP BCTR  R1,0                ADVANCE THE SEQ # PTR
         BCTR  R15,0               ADVANCE THE WORK AREA PTR
         TM    0(R1),X'BF'         X'00' OR C' '?
         BNZ   SEQ#NOT0            NO, SKIP
         MVI   0(R1),C'0'          YES, SET TO C'0'
SEQ#NOT0 TM    0(R1),X'FF'         X'FF'?
         BNO   SEQ#NOT9            NO, SKIP
         MVI   0(R1),C'9'          YES, SET TO C'9'
SEQ#NOT9 CLI   0(R1),C'0'          IS IT A DIGIT?
         BL    SEQ#END             NO, FINISHED
         CLI   0(R1),C'9'          MAYBE, TEST AGAIN
         BH    SEQ#END             STILL NOT A DIGIT
         MVC   0(1,R15),0(R1)      IT'S A DIGIT; MOVE TO WRK AREA
         MVI   0(R1),C' '          CLR DIGIT FROM NON-NUMERIC PART
         BCT   R0,SEQ#LOOP         LOOP FOR NEXT DIGIT
         BCTR  R0,0                NULL SEQ ID; SET UP "MACHINE LENGTH"
         MVC   IDMVC(2),NOPR       ALL DIGITS; SETUP FOR NULL ID
         B     SEQ#CVB             GO CONVERT THE SEQ # TO BINARY
SEQ#END  MVI   IDMVC,X'D2'         CHANGE FROM "NOPR" TO "MVC"
         BCTR  R0,0                GET SEQ # ID'S MACHINE LENGTH
         STC   R0,IDMVC+1          SET MVC'S LENGTH FIELD
SEQ#CVB  PACK  WORKAREA(8),WORKAREA(8) PACK THE PURIFIED SEQ #
         CVB   R1,WORKAREA         CONVERT IT TO BINARY
         BR    R14                 RETURN TO CALLER
         TITLE 'UPDMERGE -- FIRST LEVEL SUBROUTINES'
*        CLOSE - LINK TO THE QSAM EOD ROUTINE TO CLOSE A LIST OF DCB'S
*        AND FREE THEIR BUFFERS
         SPACE 1
CLOSE    ST    R14,SAVER14A        SAVE THE RETURN ADDRESS
CLOSELP  L     R9,0(,R2)           POINT TO THE NEXT DCB
         TM    DCBOFLGS,X'10'      IS THE DCB OPEN?
         BNO   CLOSED              NO, SKIP THE CLOSE
         BAL   R14,EODA            YES, CLOSE IT AND FREE ITS BUFFERS
CLOSED   LA    R2,4(,R2)           POINT TO NEXT DCB PTR
         LTR   R9,R9               WAS THIS THE LAST DCB?
         BNM   CLOSELP             NO, LOOP TO CLOSE NEXT DCB
         L     R14,SAVER14A        YES, RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 6
*        BSAMEXIT - DCB OPEN EXIT FOR SYSLIB AND SYSUT1
         SPACE 1
BSAMEXIT ST    R14,SAVER14A        SAVE THE RETURN ADDRESS
         BAL   R14,QSAMEXIT        GO FIGURE OUT BLKSIZE, BUFL, & BUFNO
         LR    R9,R1               POINT TO THE DCB
         SR    R4,R4               CLEAR FOR "IC"
         IC    R4,DCBBUFNO         GET # BUFS
         MVI   DCBBUFNO,0          DON'T LET OPEN GET THEM
         STC   R4,DCBNCP           SET NCP = # BUFS
         TM    FLAG,ABORTION       WILL THE PROGRAM ABORT?
         BO    BSAMRET             YES, RETURN TO OPEN
         LA    R3,DECBLEN          NO, GET A DECB LENGTH
         LR    R1,R3               GET A DECB LENGTH
         AH    R1,DCBBUFL          ADD IN BUFFER LENGTH
         MR    R0,R4               GET SIZE NEEDED 4 BUFS & DECB'S
         LA    R5,LIBBUFS          PNT TO SYSLIB BUF CORE DESCRIPTER
         TM    DCBDSORG,X'02'      IS THIS SYSLIB?
         BO    SETDSCPT            YES, CONTINUE
         LA    R5,UT1BUFS          NO, PNT TO SAVE PLACE FOR SYSUT1
SETDSCPT ST    R1,4(,R5)           SAVE CORE LENGTH
         LR    R0,R1               GET SIZE FOR GETMAIN
         GETMAIN R,LV=(0),HIARCHY=1 GETMAIN THE CORE
         ST    R1,0(,R5)           SAVE START OF GOTTEN CORE
         ST    R1,DCBDECBC         SET INITIAL CURRENT DECB PTR
         MR    R2,R4               GET TOTAL LENGTH OF DECB'S
         AR    R3,R1               POINT PAST THE DECB'S
         ST    R3,DCBRCDPT         INITIALIZE 1ST RCD PTR
         LR    R0,R3               GET START OF BUFFER
         AH    R0,DCBBUFL          PNT TO END OF BUFFER
         ST    R0,DCBLKEND         INIT END OF BLOCK PTR
         ST    R9,DECB+(DECBDCB-DECBDSCT) SET DCB PTR IN DECB
DECBINIT LR    R2,R1               POINT TO NEXT DECB
         USING DECBDSCT,R2         DCL DECB BASE
         MVC   0(12,R2),DECB       GET DECB SKELETON
         ST    R3,DECBBUFR         SET BUFFER ADDRESS
         AH    R3,DCBBUFL          (POINT TO NEXT BUFFER)
         LA    R1,DECBLEN(,R2)     POINT TO NEXT DECB
         ST    R1,DECBLINK         SET FORWARD CHAIN POINTER
         BCT   R4,DECBINIT         LOOP FOR NEXT DECB
         MVC   DECBLINK,DCBDECBC   POINT LAST DECB TO 1ST DECB
         DROP  R2                  KILL DECB BASE
         TM    DCBDSORG,X'02'      IS THIS SYSLIB?
         BNO   BSAMRET             NO, RETURN TO OPEN
         L     R1,DCBDEBAD         POINT TO OPEN'S WORK AREA
         MVC   SYSLNAME+L'SYSLNAME-44(44),JFCBDSNM-OWADSECT(R1)
BSAMRET  L     R14,SAVER14A        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO OPEN
         SPACE 6
*        GETLIB READ THROUGH SYSLIB UP TO A NEXT SEQ #
         SPACE 1
GETLIB   CLC   0(8,R1),LIBNEXT     ALREADY THERE?
         BCR   4,R14               YES, RETURN TO CALLER (CC=-)
         STM   R14,R2,SAVER14A     NO, SAVE REGISTERS
         LR    R2,R1               PNT TO THE SEQ #
GETAGAIN L     R1,LIBDECBC         PNT TO THE CURRENT DECB
         USING DECBDSCT,R1         DCL THE DECB BASE
         CLI   DECBECB,DECBIDLE    IS A READ OUTSTANDING HERE?
         BE    GETRECD             NO, GET THE RECORD FROM THE BUFFER
         BAL   R14,CHECKLIB        YES, CHECK THE READ
GETRECD  L     R15,LIBRCDPT        POINT TO NEXT LOGICAL RECORD
         CLC   LIBPREV,LIBNEXT     SYSLIB SEQUENCING ERROR?
         BNL   LIBSQERR            YES, SAVE HIGHER NUMBER
         MVC   CARDPREV,CARDNEXT   NO, GET HIGHER NUMBER
LIBSQERR MVC   CARDNEXT,0(R15)     GET NEXT SET #
         LA    R15,&CARDSZE.(,R15) PNT TO NEXT CARD
         ST    R15,LIBRCDPT        SAVE THE PTR
         C     R15,LIBLKEND        PAST END OF BLOCK?
         BL    NOREAD              NO, CONTINUE
         READ  (1),SF,MF=E         YES, ISSUE READ FOR THSI BLOCK
NOREAD   CLC   0(8,R2),LIBNEXT     HAVE WE ARIVED?
         BNL   GETAGAIN            NO, GO LOOK AT NEXT CARD
*        WARNING - THIS PROGRAM WILL REALLY FUCK UP IF THE INPUT DECKS
*        CONTAIN A SEQUENCE FIELD EQUAL TO 8X'FF'
         SR    R0,R0               SET CC=0
         LM    R14,R2,SAVER14A     RESTORE REGISTERS
         DROP  R1                  KILL DECB BASE
         BR    R14                 RETURN TO CALLER
         SPACE 6
*        SETDELET - SET VARIOUS DELETION RANGES
         SPACE 1
         DROP  R9                  KILL PROGRAM DCB BASE
SETDELET ST    R14,SAVER14A        SAVE THE RETURN ADDRESS
         LA    R1,24(,R5)          PNT TO DEL END #
         CLC   15(8,R5),24(R5)     IS START ACTUALLY HIGHER?
         BNH   GOTDEND             NO, CONTINUE
         LA    R1,15(,R5)          YES, PNT TO HIGHEST NUMBER
GOTDEND  L     R14,FRSTACTV        PNT TO 1ST ACTIVE DCB
SETDELLP CR    R14,R7              UP TO CURRENT YET?
         BNL   RSETDEND            YES, FINISHED HERE
         TM    0(R14),ACTVDCB      NO, IS THIS DCB ACTIVE?
         BZ    SETNXDCB            NO, SKIP TO NEXT DCB
         L     R15,0(,R14)         YES, PNT TO THE DCB
         USING DCBDSECT,R15        DCL LOCAL DCB BASE
         CLC   0(8,R1),DCBDELET    IS NEW DELET END HIGHER FOR THIS DCB
         BNH   SETNXDCB            NO, SKIP TO NEXT DCB
         MVC   DCBDELET,0(R1)      YES, RESET DEL RANGE FOR THIS DCB
SETNXDCB LA    R14,4(,R14)         PNT TO NEXT DCB PTR
         B     SETDELLP            LOOW FOR NEXT DCB
RSETDEND L     R14,SAVER14A        GET THE RETURN ADDRESS
         CLC   0(8,R1),DELEND      ADJUST MASTER DELETE END?
         BCR   13,R14              NO, RETURN TO CALLER
         MVC   DELEND,0(R1)        YES, DO SO
         BR    R14                 RETURN TO CALLER
         DROP  R15                 KILL LOCAL DCB BASE
         USING DCBDSECT,R9         RESTORE PROGRAM DCB BASE
         SPACE 6
*        STACK - STACK CARDS THAT CANNOT BE PUNCHED YET EITHER BECAUSE
*        THEY NEED TO BE RESEQUENCED OR BECAUSE THEY FOLLOW A DELETION
*        WHOSE RANGE IS NOT YET DETERMINED
         SPACE 1
STACK    ST    R14,SAVER14A        SAVE THE RETURN ADDRESS
         LM    R14,R0,STACKPTR     LOAD STACKPTR, STACKCTR, STACKMAX
         LA    R15,1(,R15)         INCR THE COUNTER
         CR    R15,R0              MAX IN CORE STACK EXCEEDED?
         BH    SPILL               YES, GO WRITE THE CARD TO SYSUT1
         MVC   0(&CARDSZE,R14),0(R5) NO, STACK IT IN CORE
         LA    R14,&CARDSZE.(,R14) ADVANCE THE STACK POINTER
         STM   R14,R15,STACKPTR    STORE STACKPTR, STACKCTR
STACKRET L     R14,SAVER14A        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 1
SPILL    TM    UT1OFLGS,X'10'      IS SYSUT1 AVAILABLE
         BO    SPILLIT             YES, CONTINUE
NOUT1GAT NOP   STACKRET            EXCESS MSG REPEAT SUPPRESSION GATE
         OI    NOUT1GAT+1,X'F0'    CLOSE THE GATE
         $PUT  NOSPILL             PRINT ERROR MSG
         $PUT  BLNK1               PRINT A BLNK LINE
         CLI   RCD+1,RCD12         CHECK COMPLETION CODE
         BNL   STACKRET            ALREADY SET
         MVI   RCD+1,RCD12         RESET
         B     STACKRET            RETURN TO CALLER
SPILLIT  ST    R15,STACKCNT        RESET THE STACK TOTAL
         L     R1,UT1DECBC         POINT TO THE CURRENT DECB
         USING DECBDSCT,R1         DCL DECB BASE
         CLI   DECBECB,DECBIDLE    HAS A WRITE BEEN ISSUED TO IT?
         BE    BUFFERIT            NO, BUFFER IS AVAILABLE FOR USE
         L     R1,DECBLINK         YES, POINT TO NEXT DECB
         ST    R1,UT1DECBC         RESET THE CURRENT BUFFER PTR
         L     R0,DECBBUFR         PNT TO THE START OF THE BUFFER
         ST    R0,UT1RCDPT         SET 1ST RECORD PTR
         AH    R0,UT1BUFL          PNT TO THE END OF THE BUFFER
         ST    R0,UT1LKEND         SET END OF BLOCK PTR
         CLI   DECBECB,DECBIDLE    HAS BUFFER BEEN USED BEFORE?
         BE    BUFFERIT            NO, GO USE IT
         CHECK (1)                 YES, CHECK THE OUTSTANDING WRITE
         L     R1,UT1DECBC         RESTORE THE CURRENT DECB PTR
         MVI   DECBECB,DECBIDLE    FLAG NO OUTSTANDING READ ON THIS BUF
BUFFERIT L     R15,UT1RCDPT        GET NEXT SLOT IN BUFFER
         MVC   0(&CARDSZE,R15),0(R5) MOVE THE CARD TO THE BUFFER
         LA    R15,&CARDSZE.(,R15) ADVANCE THE SLOT POINTER
         ST    R15,UT1RCDPT        ASSUME BUF NOT FULL; STORE THE PTR
         C     R15,UT1LKEND        IS THE BUFFER FULL?
         BL    STACKRET            NO, RETURN TO CALLER
         WRITE (1),SF,MF=E         YES, WRITE IT OUT
         B     STACKRET            RETURN TO CALLER
         DROP  R1                  KILL DECB BASE
         SPACE 6
*        UNSTACK - COPY THE CARD STACK TO THE PUNCH AND RESEQUENCE THE
*        CARDS IN THE PROCESS
         SPACE 1
         DROP  R9                  KILL DCB BASE FOR THIS ROUTINE
UNSTACK  STM   R14,R9,SAVER14A     SAVE ALL NEEDED REGISTERS
         NI    NOUT1GAT+1,X'0F'    INSURE ERR MSG GATE IS OPEN
         LM    R7,R8,STACKCNT      GET STACKCNT & STACKMAX
         LTR   R7,R7               IS THE STACK EMPTY?
         BZ    UNSTAKRT            YES, GO RETURN TO CALLER
         C     R7,STAKMOST         CURRENT STACK MORE THAN MOST?
         BNH   GOTMOST             NO, SKIP
         ST    R7,STAKMOST         YES, STORE HIGHER VALUE
GOTMOST  TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF RESEQUENCING SUPPRESS
         BNZ   NORESEQ1            YES, SKIP
         L     R3,TOP              GET TOP OF RESEQUENCE RANGE
         L     R5,BOTTOM           GET BOTTOM OF RESEQUENCE RANGE
         SR    R3,R5               GET THE RESEQUENCE RANGE
         SR    R4,R4               GET REMAINDER ACCUMULATOR
         LA    R6,1(,R7)           GET SEQUENCE RANGE DEVISOR
         SR    R2,R2               CLEAR FOR DEVIDE
         DR    R2,R6               GET SEQUENCE INCREMENT AND REMAINDER
         LA    R1,1                GET 10**0
         LA    R15,10              GET MULTIPLIER
TENSLP   MR    R0,R15              GET NEXT POWER OF TEN
         CR    R1,R3               LARGER THAN INTERVAL YET?
         BL    TENSLP              NO, LOOP FOR NEXT POWER OF 10
         BE    TENSGOT             NO BUT EQUAL, GO SAVE
         DR    R0,R15              YES, BACK OFF 1 POWER OF TEN
TENSGOT  ST    R1,TENSOFF          SAVE SO THAT DURING INSERTION
*                                  SEQUENCING WE CAN DROP UNECESSARY
*                                  LOW ORDER DIGITS
         LTR   R3,R3               IS RANGE LARGE ENOUGH?
         BNZ   NORESEQ1            YES, AOK
         CVD   R5,WORKAREA         NO, GET STARTING SEQ NUMBER
         UNPK  DUPSEQ1(8),WORKAREA CNVRT TO EBCDIC
         OI    DUPSEQ1+7,X'F0'     FIX THE FUCKING SIGN
         LA    R1,DUPSEQ1-(&CARDSZE-8) SETUP FOR EXECUTED INSTRUCTION
         EX    0,IDMVC             INSERT NON-NUMERICS
         $PUT  DUPSEQ              PRINT THE ERROR MESSAGE
         CLI   RCD+1,RCD4          TEST THE COMPLETION CODE
         BNL   NORESEQ1            ALREADY HIGH; SKIP
         MVI   RCD+1,RCD4          RESET
NORESEQ1 CR    R7,R8               STACK TOTAL =< INCORE TOTAL?
         BNH   STCKONLY            YES, SKIP
         LR    R7,R8               NO, MAX STACK --> INCORE TOTAL
STCKONLY L     R8,CDSTACK          POINT TO START OF INCORE STACK
         ST    R8,STACKPTR         RESET STACK TOP PTR TO EMPTY
PUTSTAK1 LR    R1,R8               POINT TO NEXT CARD
         BAL   R14,RESEQNCE        (RESEQ) & PRINT & PUNCH THE CARD
         LA    R8,&CARDSZE.(,R8) 
         BCT   R7,PUTSTAK1         LOOP FOR NEXT CARD
         CLC   STACKCNT,STACKMAX   ANY CARDS SPILLED TO SYSUT1?
         ST    R7,STACKCNT         RESET STACK TOTAL IN ANY CASE
         BNH   UNSTAKRT            NO, GO RETURN TO CALLER
         SPACE 1
         L     R9,UT1DECBC         YES, POINT TO CURRENT DECB
         USING DECBDSCT,R9         DCL DECB BASE
         CLI   DECBECB,DECBIDLE    HAS IT BEEN WRITTEN YET?
         BNE   CHCKWRTE            YES, GO CHECK IT
         L     R1,UT1RCDPT         NO, POINT PAST LAST RCD
         S     R1,DECBBUFR         GET THE BLOCK'S LENGTH
         STH   R1,UT1BLKSI         SET UP TO WRITE SHORT BLOCK
         WRITE (R9),SF,MF=E        WRITE THE LAST BLOCK
         MVC   UT1BLKSI,UT1BUFL    RESTORE THE BLKSIZE
         L     R9,DECBLINK         POINT TO THE NEXT DECB
         ST    R9,UT1DECBC         RESET CURRENT DECB PTR
CHCKWRTE CLI   DECBECB,DECBIDLE    HAS THIS BUFFER BEEN USED?
         BE    BFUNUSED            NO, SKIP
         CHECK (R9)                YES, CHECK ITS WRITE
BFUNUSED L     R9,DECBLINK         POINT TO NEXT DECB
         C     R9,UT1DECBC         BACK TO CURRENT DECB?
         BNE   CHCKWRTE            NO, GO CHECK ITS WRITE
         CLOSE (SYSUT1,REREAD),TYPE=T YES, WRITE FILE MARK & REPOSITION
PRIMEUT1 READ  (R9),SF,MF=E        READ TO THIS DECB
         L     R9,DECBLINK         POINT TO NEXT DECB
         C     R9,UT1DECBC         PAST LAST DECB?
         BNE   PRIMEUT1            NO, GO READ TO IT
CHCKREAD CHECK (R9)                CHECK THE READ
         LH    R7,UT1BLKSI         GET THE MAX BLKSIZE
         L     R1,DECBIOB          POINT TO THE IOB
         SH    R7,14(,R1)          GET THE ACTUAL BLKSIZE
         BNP   READSPIL            "NULL" BLOCK; IGNORE
         L     R8,DECBBUFR         POINT TO THE BLOCK
         AR    R7,R8               POINT PAST THE BLOCK
PUTSTAK2 LR    R1,R8               POINT TO THE NEXT CARD
         BAL   R14,RESEQNCE        (RESEQ &) PRINT & PUNCH IT
         LA    R8,&CARDSZE.(,R8) 
         CR    R8,R7               PAST THE END OF THE BLOCK?
         BL    PUTSTAK2            NO, LOOP TO PUNCH THIS CARD
READSPIL READ  (R9),SF,MF=E        RE-ISSUE READ FOR THIS DECB
         L     R9,DECBLINK         POINT TO THE NEXT DECB
         B     CHCKREAD            GO CHECK NEXT READ
         SPACE 3
EODSPILL ST    R9,UT1DECBC         SET CURRENT = DECB REGISTER
         L     R0,DECBBUFR         POINT TO THE BUFFER
         ST    R0,UT1RCDPT         SET INITIAL RCD PTR
         AH    R0,UT1BUFL          PNT TO END OF BUFFER
         ST    R0,UT1LKEND         RESET END OF BLOCK PTR
SPILRSET MVI   DECBECB,DECBIDLE    FLAG NO OUTSTANDING WRITE
         L     R9,DECBLINK         POINT TO NEXT DECB
         C     R9,UT1DECBC         PAST LAST DECB?
         BNE   SPILRSET            NO, LOOP TO FLAG IT
         MVC   UT1FDAD,UT1START    POINT TO START OF WORK DATA SET
UNSTAKRT LM    R14,R9,SAVER14A     RESTORE ALL USED REGISTERS
         BR    R14                 RETURN TO CALLER
         DROP  R9                  KILL DECB BASE
         USING DCBDSECT,R9         RESTORE DCB BASE
         TITLE 'UPDMERGE -- SECOND LEVEL SUBROUTINES'
*        CHECKLIB - CHECK ALL READS TO SYSLIB AND HANDLE END OF FILE
*        CONDITIONS
         SPACE 1
CHECKLIB L     R1,LIBDECBC         POINT TO THE CURRENT DECB
         USING DECBDSCT,R1         DCL DECB BASE
         L     R1,DECBLINK         POINT TO THE NEXT DECB
         ST    R1,LIBDECBC         RESET THE CURRENT DECB PTR
         CLI   DECBECB,DECBIDLE    IS A READ OUTSTANDING HERE?
         BNE   CHECKIT             YES, CONTINUE
         NI    FLAG,255-MEMBRFND   NO, MUST BE FINISHED W/ MEMBER
         BR    R14                 RETURN TO CALLER
CHECKIT  ST    R14,SAVER14B        SAVE THE RETURN ADDRESS
         CHECK (1)                 CHECK THE READ
LIBRCSET L     R1,LIBDECBC         POINT TO THE DECB AGAIN
         MVI   DECBECB,DECBIDLE    FLAG NO OUTSTANDING READ
         L     R15,DECBBUFR        PNT TO THE BLOCK
         ST    R15,LIBRCDPT        SET 1ST LOGICAL RECORD PTR
         AH    R15,LIBBUFL         PNT TO END OF LONGEST POSSIBLE BLOCK
         L     R14,DECBIOB         POINT TO THE IOB
         SH    R15,14(,R14)        KNOCK OFF RESIDUAL COUNT FROM CSW
         ST    R15,LIBLKEND        SET END OF BLOCK PTR
CHKLRET  L     R14,SAVER14B        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 1
EODMEMBR L     R1,LIBDECBC         POINT TO THE 1ST DECB
KILLCHCK MVI   DECBECB,DECBIDLE    FLAG NO OUTSTANDING READ
         L     R1,DECBLINK         POINT TO THE NEXT DECB
         C     R1,LIBDECBC         PAST THE LAST DECB?
         BNE   KILLCHCK            NO, LOOP TO FLAG IT
         LA    R15,FAKEBLKS-14     SET UP FAKE IOB PTR -
         ST    R15,DECBIOB          FOR GETTING FAKE CSW REESIDUAL CNT
         L     R1,DECBBUFR         POINT TO ITS 1ST RECORD
         DROP  R1                  KILL DECB BASE
         MVC   &CARDSZE-8(8,R1),=8X'FF' BUILD A FAKE -
         EX    0,IDMVC               LAST -
         MVI   0(R1),C' '             CARD
         NI    FLAG,255-MEMBRFND   MEMBER NO LONGER NEEDED
         B     LIBRCSET            CONTINUE PROCESSING
         SPACE 1
LIBERR   SYNADAF ACSMETH=BPAM      SYSLIB SYNAD ROUTINE
         L     R13,4(,R13)         RESTORE BASE REGISTER
         LA    R15,SEQSUPR-1       POINT TO ACTION MSG
         BAL   R14,SYNADMSG        GO PRINT SYNAD AND ACTION MSG'S
         OI    NORESEQ,LIBIOERR    SUPPRESS FURTHER RESEQUENCING
         MVC   LIBNEXT,=8X'FF'     SIGNEL EO-PROCESSING FOR THIS MEMBER
         CLI   RCD+1,RCD8          CHECK THE COMPLETION CODE
         BNL   CHKLRET             ALREADY SET
         MVI   RCD+1,RCD8          RESET
         B     CHKLRET             GO RETURN TO CARREL
         SPACE 6
*        GETCARD - GET A CARD FROM ONE OF THE SYSIN DATA SETS; IDENTIFY
*        FUNCTION CARDS; DO END OF FUNCTION PROCESSING AS NECESSARY;
*        AND HANDLE END OF DATA CONDITIONS
*        RETURN CC=0 => FUNCTION CARD READ
         SPACE 1
GETCARD  ST    R14,SAVER14B        SAVE THE RETURN ADDRESS
         MVI   DCBUPDAT,0          SET CARD ID FLAG TO "DATA CARD"
GETCARDA GET   (R9)                GET THE NEXT CARD
         CLC   0(2,R1),=CL9'./'    IEBUPDAT/UPDMERGE CONTROL CARD?
         BE    IDENTIFY            YES, GO IDENTIFY IT
         SPACE 1
GETCRETM LTR   R14,R14             NO, FORCE CC^=0
GETCRET  L     R14,SAVER14B        RESTORE RETURN ADDR (CC SET)
         BR    R14                 RETURN TO CALLER
         SPACE 1
IDENTIFY LA    R0,1                YES, GET INITIAL FLAG BIT
         LA    R15,CTLLIST-6       PNT TO CTL CARD VERB LIST
CTLLOOP  LA    R15,6(,R15)         PNT TO NEXT LIST ENTRY
         CLI   0(R15),X'FF'        END OF LIST?
         BE    GETCERR             YES, BAD CTL CARD
         CLC   9(6,R1),0(R15)      NO, CURRENT VERB FOUND YET?
         BE    CTLCARD             YES, GO PROCESS
         BL    GETCERR             NO, INVALID
         AR    R0,R0               NOT YET; SHIFT THE FLAG BIT
         B     CTLLOOP             GO LOOK AGAIN
CTLCARD  STC   R0,DCBUPDAT         YES, SET FLAG BIT SAYING WHAT TYPE
         TM    DCBUPDAT,DELET+RESEQ DETAIL CARD?
         BZ    FUNCTION            NO, IT'S A FUNCTION CARD
         TM    DCBUPDAT,RESEQ      YES, UPDMERGE CONTROL CARD?
         BZ    DETAILCD            NO, IEBUPDAT DETAIL CARD
         CLC   =C'OFF ',15(R1)     YES, VALID OPERAND?
         BE    GETCRETM            YES, RETURN TO CALLER
         CLC   =C'ON ',15(R1)      DON'T KNOW, CHECK AGAIN
         BE    GETCRETM            YES, RETURN TO CALLER
         B     GETCERR             NO, ERROR
DETAILCD TM    DCBFLAG,ADDREPL+ONBIT YES, ./ADD OR ./REPL SECTION?
         BM    GETCRET             NO, CARD OK; RETURN (CC^=0)
         SPACE 3
*        SECONDARY ENTRY FOR DISCARDING AN INVALID CARD. FOR
*        ENVIRONMENT IT IS NECESSARY TO STORE THE RETURN ADDRESS INTO
*        SAVER14B.
         SPACE 1
GETCERR  MVC   CARDPRFX(16),=C'ILLEGAL CARD -->' YES, ERROR
         MVC   CARD,0(R1)          GET THE BAD CARD IMAGE
         $PUT  CARDMSG             SHIP THE MSG
         MVC   CARDPRFX,CARDPRFX-1 CLEAR
         CLI   RCD+1,RCD8          CHECK RETURN CODE
         BNL   GETCARDA            ALREADY SET; LP 4 NXT CARD
         MVI   RCD+1,RCD8          RESET
         B     GETCARDA            LOOP FOR NEXT CARD
         SPACE 1
FUNCTION TM    DCBUPDAT,ENDUP      ./ENDUP CARD?
         BO    EOD                 YES, GO PERFORM EOD PROCESSING
         NI    0(R7),255-ACTVDCB-SKIPDCB KILL ACTIVE & SKIP FLAGS
         BAL   R14,FIND1ST         RESET THE 1ST ACTIVE DCB PTR PTR
         MVC   DCBMEMBR,=CL8' '    CLEAR MEMBER NAME SAVE AREA
         LA    R0,8                GET NAME SCAN LOOP CONTROL
         LA    R15,DCBMEMBR        PNT TO MEMBER NAME SAVE AREA
MEMBRLP  CLI   15(R1),C','         END OF NAME?
         BE    MEMBREND            YES, SKIP OUT OF LOOP
         CLI   15(R1),C' '         MAYBE, TEST AGAIN
         BE    MEMBREND            YES, SKIP OUT OF SCAN LOOP
         MVC   0(1,R15),15(R1)     NO, SAVE THIS CHARACTER
         LA    R15,1(,R15)         ADVANCE SAVE AREA PTR
         LA    R1,1(,R1)           ADVANCE THE SOURCE SCANNER
         BCT   R0,MEMBRLP          LOOP TO CHECK NEXT CHARACTER
MEMBREND L     R1,DCBRECAD         RESTORE THE CARD POINTER
         SR    R0,R0               SET CC=0
         B     GETCRET             GO RETURN TO CALLER (CC=0)
*        SETUP FOR THE CLOSURE OF ANY DATA SET AND THE FREEING OF ITS
*        BUFFER POOL
EODA     ST    R14,SAVER14B        YES, SAVE THE RETURN ADDRESS
         SR    R7,R7               FLAG AS SIMPLE CLOSE
*        END OF FILE ROUTINE FOR ANY SYSIN DATA SET
EOD      CLOSE ((R9))              CLOSE THE DATA SET
         TM    DCBBUFCB+3,X'01'    DID THE DATA SET HAVE BUFFERS?
         BO    NOPOOL              NO, SKIP
         FREEPOOL (R9)             YES, FREE THEM
NOPOOL   LTR   R7,R7               SIMPLE CLOSE?
         BZ    GETCRET             YES, RETURN TO CALLER
         LA    R1,LASTCARD         NO, EOD ON SYSIN; PNT 2 FAKE LAST CD
         ST    R1,DCBRECAD         POINT THE DCB TO IT
         B     IDENTIFY            GO DO END OF FUNCTION PROCESSING
         SPACE 1
INXERR   SYNADAF ACSMETH=QSAM      SYSIN* SYNAD EXIT
         L     R13,4(,R13)         RESTORE BASE REGISTER
         LA    R15,KILLINX-1       POINT TO ACTION MSG
         BAL   R14,SYNADMSG        GO ISSUE IO ERR & ACTION MSG
         CLI   RCD+1,RCD12         CHECK THE COMPLETION CODE
         BNL   EOD                 ALREADY SET
         MVI   RCD+1,RCD12         RESET
         B     EOD                 GO TERMINATE THIS INPUT DCB
         SPACE 6
*        QSAMEXIT - DCB EXIT FOR SYSPUNCH AND ALL SYSIN DCB'S; ALSO A
*        SUBROUTINE USED BY BSAMEXIT
         SPACE 1
         DROP  R9                  KILL PROGRAM'S DCB BASE
         USING DCBDSECT,R1         DCL LOCAL DCB BASE
QSAMEXIT LH    R3,DCBBLKSI         GET BLKSIZE (IF ANY)
         LH    R4,DCBLRECL         GET LRECL
         LTR   R3,R3               WAS A BLKSIZE GIVEN?
         BZ    SETBLKSI            NO, SET BLKSIZE = LRECL
         CR    R4,R3               YES, IS FILE UNBLOCKED?
         BE    SETBUFL2            YES, GO SET BUFL = BLKSIZE
         OI    DCBRECFM,X'10'      NO, SET FILE BLOCKED FLAG
         SR    R2,R2               CLEAR FOR DEVIDE
         DR    R2,R4               GET BLOCKING FACTOR
         LTR   R2,R2               IS THE FACTOR INTEGRAL?
         BZ    SETBUFL             YES, GO SET BUFL = BLKSIZE
         ST    R14,SAVER14B        NO, SAVE THE RETURN ADDRESS
         MVC   SASAVE(60),12(R13)  SAVE THE SAVE AREA
         L     R2,CVTPTR           POINT TO THE CVT
         L     R2,CVTTCBP-CVTDSECT(,R2) POINT TO OLD/NEW TCB PTRS
         L     R2,4(,R2)           POINT TO OUR TCB
         L     R2,12(,R2)          POINT TO OUR TIOT
         AH    R2,DCBTIOT          POINT TO THIS DCB'S DD ENTRY
         MVC   BKSIERRM+26(8),4(R2) GET DDNAME INTO ERR MSG
         LR    R2,R1               SAVE THE DCB PTR
         $PUT  BKSIERRM            PRINT ERROR MSG
         L     R14,SAVER14B        RESTORE THE RETURN ADDRESS
         MVC   12(60,R13),SASAVE   RESTORE THE SAVE AREA
         LR    R1,R2               RESTORE THE DCB PTR
         OI    FLAG,ABORTION       REMEMBER TO ABORT AFTER OPEN
         MVI   DCBBUFNO,1          FORCE MINIMAL BUFFER SPACE
         MVI   BKSIERRM,C' '       DON'T SKIP LINES ON SIMILAR ERROR
SETBLKSI STH   R4,DCBBLKSI         RESET THE BLOCKSIZE
SETBUFL  LH    R4,DCBBLKSI         GET THE BLKSIZE
SETBUFL2 STH   R4,DCBBUFL          SET BUFL = BLKSIZE
         CLI   DCBBUFNO,0          BUFNO GIVEN?
         BCR   7,R14               YES, RETURN TO OPEN
         LH    R3,=Y(&BUFSPCE) 
         SR    R2,R2               CLEAR FOR DEVIDE
         DR    R2,R4               GET # BUFFERS TO ALLOCATE
         STC   R3,DCBBUFNO         SET BUFNO
         CLI   DCBBUFNO,&MINBUF 
         BNL   GOTMIN              NO, CONTINUE
         MVI   DCBBUFNO,&MINBUF 
GOTMIN   CLI   DCBBUFNO,&MAXBUF 
         BCR   13,R14              NO, RETURN TO OPEN
         MVI   DCBBUFNO,&MAXBUF 
         BR    R14                 RETURN TO CALLER
         DROP  R1                  KILL LOCAL DCB BASE
         USING DCBDSECT,R9         RE-ESTABLISH PROGRAM DCB BASE
         SPACE 6
*        RESEQNCE - RESEQUENCE AND PRINT AND PUNCH A CARD (R1 THROUGH
*        R6 ARE PRE-SET)
         SPACE 1
RESEQNCE ST    R14,SAVER14B        SAVE THE RETURN ADDRESS
         TM    NORESEQ,NOSYSLIB+LIBIOERR+TURNDOFF RESEQUENCING SUPPRESS
         BNZ   NORESEQ2            YES, SKIP
         AR    R5,R3               INCR THE SEQUENCE NUMBER
         AR    R4,R2               INCR THE SEQUENCE REMAINDER
         CR    R4,R6               SEQUENCE DEVISOR EXCEEDED?
         BL    NOROUND             NOT YET
         SR    R4,R6               YES, ADJ THE REMAINDER ACCUMULATOR
         AH    R5,=Y(1)            ADJUST THE SEQUENCE NUMBER
NOROUND  LR    R15,R5              GET THE SEQ NUMBER
         C     R15,BOTTOM          OFF THE GROUND YET?
         BNE   NOADJ               YES, PROCEED
         AH    R15,=Y(1)           NO, LIFT-OFF
NOADJ    SR    R14,R14             CLEAR FOR DEVIDE
         D     R14,TENSOFF         DROP UNECESSARY DIGITS
         M     R14,TENSOFF         FILL WITH DECIMAL ZEROS
         CVD   R15,WORKAREA        CONVERT SEQ # TO DECIMAL
         UNPK  &CARDSZE-8(8,R1),WORKAREA UNPACK THE NUMBER
         OI    &CARDSZE-1(R1),X'F0' FIX THE FUCKING SIGN
         EX    0,IDMVC             BRING IN THE SEQ # ID
NORESEQ2 BAL   R14,PUTCARD         GO PRINT AND PUNCH THE CARD
         L     R14,SAVER14B        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         TITLE 'UPDMERGE -- THIRD LEVEL SUBROUTINES'
*        PUTCARD - PRINT AND PUNCH AN UPDATE CARD
         SPACE 1
PUTCARD  TM    FLAG,FCTNPUTD       FUNCTION CARD ALREADY OUT?
         BNZ   PUTCARD2            YES, PROCEED
         OI    FLAG,FCTNPUTD       NO, WILL BE SOON
         XC    FCTNCARD,0(R1)      SAVE -
         XC    0(L'FCTNCARD,R1),FCTNCARD CURRENT -
         XC    FCTNCARD,0(R1)        CARD
         ST    R14,SAVER14D        SAVE THE RETURN ADDRESS
         BAL   R14,PUTCARD2        PUT OUT THE FUNCTION CARD
         L     R14,SAVER14D        RESTORE THE RETURN ADDRESS
         LA    R1,FCTNCARD         --> CURRENT CARD
PUTCARD2 ST    R14,SAVER14C        SAVE THE RETURN ADDRESS
         MVC   CARD,0(R1)          SAVE THE CARD
         LR    R0,R1               POINT TO THE CARD
         PUT   SYSPUNCH,(0)        PUNCH THE CARD
         TM    FLAG,ECHO           ECHO OUTPUT?
         BZ    PCRET               NO, SKIP
         $PUT  CARDMSG             YES, PRINT THE CARD
PCRET    L     R14,SAVER14C        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 6
*        SYNADMSG - PRINT SYNAD AND ADDITIONAL MESSAGES AND ISSUE
*        SYNADRLS
         SPACE 1
SYNADMSG STM   R14,R15,SAVER14C    SAVE RETURN ADDRESS AND MSG PTR
         MVC   55(13,R1),=C'¤0I/O ERROR - ' SYNAD MSG PREFIX
         LA    R1,55(,R1)          POINT TO THE MSG
         $PUT  (R1)                PRINT IT
         L     R1,SAVER15C         PNT TO ADDITIONAL MSG
         $PUT  (R1)                PRINT IT
         $PUT  BLNK1               PRINT A BLANK LINE
         SPACE 1
         L     R13,8(,R13)         POINT TO SYNAD'S SAVE AREA
         BAL   R15,*+4             SET R15 NEGETIVE
         SVC   68                  ISSUE SYNADRLSE
         L     R14,SAVER14C        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         TITLE 'UPDMERGE - FOURTH LEVEL SUBROUTINES'
*        PUTSYSP - PRINT ALL MESSAGES KEEPING TRACK OF LINE COUNT SO AS
*        TO INSERT A TITLE AT THE TOP OF EVERY PAGE
         SPACE 1
PUTSYSP  TM    PRTOFLGS,X'10'      IS SYSPRINT OPEN?
         BCR   14,R14              NO, RETURN TO CALLER
         ST    R14,SAVER14E        YES, SAVE THE RETURN ADDRESS
CCCRECHK LA    R15,CCC+L'CCC       POINT PAST CARAGE CTL CHAR LIST
         LA    R0,4                GET CCC IDENTIFICATION LOOP CTL
CCCLP    BCTR  R15,0               POINT TO NEXT CCC
         CLC   0(1,R15),1(R1)      IS THIS IT?
         BE    CCCFND              YES, GO PROCESS
         BCT   R0,CCCLP            NO, LOOP TO TEST AGAIN
         B     NEWPAGE             NOT FOUND; MUST BE EJECT
CCCFND   BCTR  R0,0                GET VALUE OF CCC
         LNR   R0,R0               GET NEG. FOR SUBTRACTION
         AH    R0,LINES2GO         SUBTRACT FROM LINES LEFT ON PAGE
         BNM   OLDPAGE             SAME OLD PAGE; CONTINUE
NEWPAGE  AP    PAGE,=P'1'          NEW PAGE; INCR PAGE NUMBER
         MVC   TITLE+L'TITLE-3(3),EDITMASK+2 GET THE EDIT MASK
         ED    TITLE+L'TITLE-4(4),PAGE EDIT PAGE # INTO TITLE
         LTR   R0,R0               AUTOMATIC EJECT?
         LA    R0,&LINECT-1 
         BNM   OLDPAGE             NO, FORCED BY MSG; GO PRINT IT
         ST    R1,SAVER1E          YES, SAVE THE MSG PTR
         MVI   PRTLRECL+1,L'TITLE  SET LRECL = TITLE LENGTH
         PUT   SYSPRINT,TITLE      PRINT THE TITLE
         MVI   PRTLRECL+1,L'BLNK1  SET LRECL = L'BLNK1
         PUT   SYSPRINT,BLNK1      PRINT IT ALSO
         L     R1,SAVER1E          RESTORE MSG PTR
         LA    R0,&LINECT-2 
         STH   R0,LINES2GO         RESET LINES LEFT ON PAGE
         B     CCCRECHK            GO RE-EVALUATE THE MSG'S CCC
OLDPAGE  STH   R0,LINES2GO         RESET LINES 2 GO ON PAGE
         MVC   PRTLRECL+1(1),0(R1) SET LRECL
         LA    R0,1(,R1)           POINT TO THE MESSAGE
         PUT   SYSPRINT,(0)        PRINT THE MESSAGE
         L     R14,SAVER14E        RESTORE THE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         TITLE 'UPDMERGE -- DATA - DCB''S'
         PRINT NOGEN               DCB'S ARE UGLY
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=UA,LRECL=121,   *
               BLKSIZE=121,BUFL=121,BUFNO=3,OPTCD=C,EROPT=ACC
         ORG   SYSPRINT
PRT      $DCBD DSORG=QS
         SPACE 3
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,RECFM=F,              *
               LRECL=&CARDSZE,OPTCD=C,EXLST=QSXLIST,SYNAD=PUNERR,      *
               EROPT=ACC
         ORG   SYSPUNCH
PUN      $DCBD DSORG=QS
         SPACE 3
&TEMP    SETA  1963/&CARDSZE*&CARDSZE
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(R,W),RECFM=F,             *
               LRECL=&CARDSZE,BLKSIZE=&TEMP,OPTCD=C,BUFNO=3,           *
               EXLST=BSXLIST,SYNAD=UT1ERR,EODAD=EODSPILL
         ORG   SYSUT1
UT1      $DCBD DSORG=BS,DEVD=DA
UT1DECBC DS    A                   PTR TO CURRENT DECB
UT1RCDPT DS    A                   PTR TO CURRENT LOGICAL RECORD ADDR
UT1LKEND DS    A                   END OF BLOCK PTR
         SPACE 3
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PO,MACRF=R,RECFM=F,LRECL=&CARDSZE,  *
               OPTCD=C,EXLST=BSXLIST,SYNAD=LIBERR,EODAD=EODMEMBR
         ORG   SYSLIB
LIB      $DCBD DSORG=BS,DEVD=DA
LIBDECBC DS    A                   PTR TO CURRENT DECB
LIBRCDPT DS    A                   PTR TO CURRENT LOGICAL RECORD
LIBLKEND DS    A                   END OF BLOCK PTR
         SPACE 3
SYSINX   DCB   DDNAME=SYSIN*,DSORG=PS,MACRF=GL,RECFM=F,LRECL=&CARDSZE, *
               OPTCD=C,EXLST=QSXLIST,SYNAD=INXERR,EROPT=ACC,EODAD=EOD
         ORG   SYSINX
INX      $DCBD DSORG=QS
INXMEMBR DS    CL8                 CURRENT UPDATE MEMBER NAME
INXDELET DS    CL8                 END OF DELETE RANGE FOR THIS DCB
INXFLAG  DS    X                   FLAG BYTE
INXUPDAT DS    X                   CURRENT UPDATE FUNCTION
INXID    DS    C                   SYSIN ID
         DS    0A                  ALIGN TO FULLWORD MULTIPLE LEN
         SPACE 1
INXLEN   EQU   *-SYSINX            DCB LENGTH
         SPACE 3
         PRINT GEN                 RESTORE MACRO EXPANSIONS
         TITLE 'UPDMERGE -- DATA - MISCELLANIOUS'
WORKAREA DS    2D                  WORK AREA
DELSTART DS    CL8                 DELETION START
DELEND   DS    CL8                 DELETION END
DELENDSV DS    CL8                 SAVE AREA FOR DELEND USED WHEN A
*                                  DELETION RANGE IS INTERRUPTED BY A
*                                  RESTORATION DATA CARD
NEXTSEQ# DS    CL8                 SEQ # OF NEXT UPDATE CARD
NXTLESS1 DS    CL8                 CRNT SEQ # - X'01'
RESEQLIM DS    CL8                 RESEQ RANGE UPPER LIMIT
MBRID    DS    CL8                 SEQ # ID SAVE AREA
         SPACE 1
CARDPREV DS    CL&CARDSZE -        PREVIOUS SYSLIB CARD
         ORG   *-8
LIBPREV  DS    CL8                 PREVIOUS SYSLIB SEQ #
         SPACE 1
CARDNEXT DS    CL&CARDSZE -        NEXT SYSLIB CARD
         ORG   *-8
LIBNEXT  DS    CL8                 NEXT SYSLIB SEQ #
         SPACE 1
FCTNCARD DS    CL&CARDSZE          FUNCTION CARD TEMP SAVE
         SPACE 1
SAVER14A DS    3A                × SAVE AREA
SAVER1A  DS    9A                V SAVE AREA
SAVER14B DS    A                   SAVE AREA
SAVER14C DS    A                 × SAVE AREA
SAVER15C DS    A                 V SAVE AREA
SAVER14D DS    A                   SAVE AREA
SAVER14E DS    A                   SAVE AREA
SAVER1E  DS    A                   SAVE AREA
         SPACE 1                 V SAVE AREA
CDSTACK  DS    2A                × CARD STACK FREEMAIN DESCRIPTION
OPENLIST DS    2A                × PTR TO SYSIN DCBS PTRS
LIBBUFS  DS    2A                × SYSLIB BUFS & DECB'S FREEMAIN DSCPTR
UT1BUFS  DS    2A                V SYSUT1 BUFS & DECB'S FREEMAIN DSCPTR
         SPACE 1
STACKPTR DS    A                 × IN CORE STACK TOP PTR
STACKCNT DC    A(0)              × CARD STACK COUNT
STACKMAX DS    A                 V MAX INCORE STACK SIZE
         SPACE 1
         READ  DECB,SF,*-*,*-*,MF=L A DECB
         ORG   DECB+(DECBECB-DECBDSCT) PNT TO THE ECB
         DC    AL1(DECBIDLE)       SET NO OUTSTANDING I/O FLAG
         ORG   DECB+(DECBBUFR-DECBDSCT) ALLOW OVERLAP OF UNUSED PART
         SPACE 1
SASAVE   DS    15A                 SAVE AREA'S SAVE AREA
STAKMOST DS    A                   MAX STACK DEPTH
BSXLIST  DC    X'85',AL3(BSAMEXIT) DCB EXIT LIST
QSXLIST  DC    X'85',AL3(QSAMEXIT) DCB EXIT LIST
GETMQTY  DC    A(4096+&CARDSZE,X'FFFFFF') FOR VARIABLE GETMAIN
FRSTACTV DS    A                   PTR TO 1ST CURRENTLY ACTIVE DCB
TOP      DS    A                   TOP OF RESEQUENCE RANGE
BOTTOM   DS    A                   BOTTOM OF RESEQUENCE RANGE
TENSOFF  DS    F                   TO KILL UNECESSARY SEQ DIGITS
CCC      DC    C'+ 0-'             CARRAGE CONTROL CHARACTERS
         SPACE 1
RCD      DC    Y(0)                PROGRAM COMPLETION CODE
RCD0     EQU   0                   EXECUTED WITHOUT ERRORS
RCD4     EQU   4                   DUPLICATE SEQUENCE NUMBERS GENERATED
RCD8     EQU   8                   ERRORS SUPPRESSED SOME RESEQUENCING
RCD12    EQU   12                  SOME CARDS LOST
RCD16    EQU   16                  PROGRAM ABORTED
         SPACE 1
FAKEBLKS DC    Y(0)                FAKE CSW RESIDUAL CNT FOR SYSLIB EOD
LINES2GO DS    Y                   PAGE LINES TO GO
IDMVC    MVC   &CARDSZE-8(*-*,R1),MBRID GET SEQ # ID INSTRUCTION
NOPR     NOPR  R0                  USED TO NULLIFY "IDMVC"
PAGE     DS    PL2                 SYSPRINT PAGE COUNTER
         SPACE 1
CTLLIST  DS    0C                  IEBUPDAT CONTROL VERB LIST
ADDVERB  DC    CL6'ADD'
CHNGVERB DC    CL6'CHNGE'
DELEVERB DC    CL6'DELET'
ENDUVERB DC    CL6'ENDUP'
REPLVERB DC    CL6'REPL'
REPRVERB DC    CL6'REPRO'
RSEQVERB DC    CL6'RESEQ'
         DC    X'FF'               END OF LIST FLAG
         SPACE 1
FLAG     DS    X                   FLAG BYTE
ABORTION EQU   X'04'               TERMINATE WITHOUT PROCESSING
MEMBRFND EQU   X'08'               MEMBER FOUND IN SYSLIB
DELETE   EQU   X'10'               DELETION RESOLUTION IN PROGRESS
ECHO     EQU   X'20'               GENERATE ECHO OUTPUT
DLINTREQ EQU   X'40'               A DELETION RANGE HAS BEEN
*                                  INTERRUPTED BY A RESTORE
FCTNPUTD EQU   X'80'               THE FUNCTION CARD (./ADD, ETC.) HAS
*                                  BEEN PUNCHED FOR THIS MEMBER
         SPACE 1
NORESEQ  DS    B                   RESEQUENCING CONTROL BYTE
NOSYSLIB EQU   B'10000000'         NO SYSLIB DATA SET PRESENT
LIBIOERR EQU   B'01000000'         I/O ERROR IN CURRENT SYSLIB MEMBER
TURNDOFF EQU   B'00100000'         ./*RESEQ OFF CARD ENCOUNTERED
         SPACE 1
*        THE FOLLOWING FLAGS ARE SET IN THE HIGH ORDER BYTE OF THE
*        ENTRIES IN THE DCB POINTER LIST
LASTDCB  EQU   X'80'               LAST DCB PTR IN LIST
ACTVDCB  EQU   X'40'               THIS DCB IS ACTIVE FOR THIS MEMBER
SKIPDCB  EQU   X'20'               SKIP THIS DCB TO ITS NEXT FUNCTION
         SPACE 1
UT1START DS    XL(L'UT1FDAD)       START OF SYSUT1 SAVE POINTER
LASTCARD DC    C'./       ADD   ',8X'FF' USED AT SYSIN EOD
DLTECMNT DS    CL(&CARDSZE-41) 
EDITMASK DC    X'2020202120'       EDIT MASK
ONLYPARM DC    YL2(4),C'ECHO'      ONLY VALID PARM FIELD
         SPACE 1
         LTORG ,
         TITLE 'UPDMERGE -- DATA - MESSAGES'
         DC    AL1(L'ABORTED)      RCD16 GENERATED
ABORTED  DC    C' PROGRAM ABORTED'
         SPACE 3
         DC    AL1(L'BKSIERRM)     RCD16 GENERATED
BKSIERRM DC    C'*ERROR--- THE BLKSIZE FOR ******** IS NOT A MULTIPLE O*
               F &CARDSZE'
         SPACE 3
         DC    AL1(L'BLNK1)
BLNK1    DC    C'  '
         SPACE 3
         DC    AL1(CARDMSGL)
CARDMSG  EQU   *
CARDMCCC DC    C' '
CARDPRFX DC    CL20' '
CARD     DS    CL(&CARDSZE)
CARDMSGL EQU   *-CARDMSG
         SPACE 3
         DC    AL1(L'COREDECR)
COREDECR DC    C'0 AVAILABLE CORE MAY BE DECREASED BY *****K BEFORE SYS*
               UT1 WOULD BE NEEDED'
         SPACE 3
         DC    AL1(L'COREINCR)
COREINCR DC    C'0IF AVAILABLE CORE WERE INCREASED BY *****K, THEN SYSU*
               T1 WOULD NOT BE NEEDED'
         SPACE 3
         DC    AL1(L'DCTYIOER)     RCD8 GENERATED
DCTYIOER DC    C'0******** NOT FOUND DUE TO I/O ERROR WHILE SEARCHING S*
               YSLIB DIRECTORY'
         SPACE 3
         DC    AL1(L'DDMISSNG)     RCD16 GENERATED
DDMISSNG DC    C'-ERROR--- SYSPUNCH DD CARD MISSING'
         SPACE 3
         DC    AL1(L'DUPSEQ+L'DUPSEQ1) RCD4 GENERATED
DUPSEQ   DC    C' WARNING - THE INSERTION BLOCK FOLLOWING '
DUPSEQ1  DC    C'******** CONTAINS DUPLICATE SEQUENCE NUMBERS'
         SPACE 3
         DC    AL1(L'EOPMSG)
EOPMSG   DC    C'-END OF PROGRAM'
         SPACE 3
         DC    AL1(L'INPFNCTN)
INPFNCTN DC    C' ./ ***** FROM SYSIN*'
         SPACE 3
         DC    AL1(L'INXCOUNT)
INXCOUNT DC    C' * DECK(S) WILL BE MERGED'
         SPACE 3
         DC    AL1(L'KILLINX)      RCD12 GENERATED
KILLINX  DC    C' INPUT FROM THIS DD CARD IS TERMINATED'
         SPACE 3
         DC    AL1(L'MBREXIST)
MBREXIST DC    C'0******** EXISTS IN SYSLIB'
         SPACE 3
         DC    AL1(L'MBRNEXST)
MBRNEXST DC    C'0******** DOES NOT EXIST IN SYSLIB'
         SPACE 3
         DC    AL1(L'MRGMSG)
MRGMSG   DC    C'0******** BEING PROCESSED'
         SPACE 3
         DC    AL1(L'NOSPILL)      RCD12 GENERATED
NOSPILL  DC    C'0ERROR--- SYSUT1 DD CARD OMITTED - CARDS LOST'
         SPACE 3
         DC    AL1(L'NOSYSLMS)
NOSYSLMS DC    C'0SYSLIB NOT SUPPLIED - RESEQUENCING SUPPRESSED'
         SPACE 3
         DC    AL1(L'SEQSUPR)      RCD8 GENERATED
SEQSUPR  DC    C' RESEQUENCING SUPPRESSED FOR THE REST OF THIS MEMBER'
         SPACE 3
         DC    AL1(L'SYSLNAME)
SYSLNAME DC    C'0THE DECKS TO BE MERGED ARE UPDATES FOR ***************
               ******************************'
         SPACE 3
         DC    AL1(L'TITLE)
TITLE    DC    CL121'1UPDMERGE'
         ORG   *-8
         DC    C'PAGE ***'
         ORG   ,
         SPACE 3
         END   UPDMERGE
