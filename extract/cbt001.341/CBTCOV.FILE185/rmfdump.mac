*          DATA SET CBT276     AT LEVEL 001 AS OF 02/25/75
         PRINT NOGEN
         MACRO
&NAME    SAVEALL    &RENT,&REG=3,&EQU=YES
         GBLB  &SAVRENT
         GBLB  &REGSW
         GBLB  &TIME
         GBLC  &SAVNDX
&SAVNDX  SETC  '&SYSNDX'
&NAME    CSECT
         AIF   ('&SAVRENT' EQ '1').NOTFREE
         AIF   ('&REGSW' EQ '1').NOEQU
         AIF   ('&EQU' NE 'YES').NOEQU
&REGSW   SETB  1
         SPACE 2
***********************************************************************
********           DEFINITION OF GENERAL REGISTERS             ********
***********************************************************************
R0       EQU   0    GENERAL REGISTER ZERO
R1       EQU   1    GENERAL REGISTER ONE
R2       EQU   2    GENERAL REGISTER TWO
R3       EQU   3    GENERAL REGISTER THREE
R4       EQU   4    GENERAL REGISTER FOUR
R5       EQU   5    GENERAL REGISTER FIVE
R6       EQU   6    GENERAL REGISTER SIX
R7       EQU   7    GENERAL REGISTER SEVEN
R8       EQU   8    GENERAL REGISTER EIGHT
R9       EQU   9    GENERAL REGISTER NINE
R10      EQU   10   GENERAL REGISTER TEN
R11      EQU   11   GENERAL REGISTER ELEVEN
R12      EQU   12   GENERAL REGISTER TWELVE
R13      EQU   13   GENERAL REGISTER THIRTEEN
R14      EQU   14   GENERAL REGISTER FOURTEEN
R15      EQU   15   GENERAL REGISTER FIFTEEN
         SPACE 1
RA       EQU   10   GENERAL REGISTER TEN
RB       EQU   11   GENERAL REGISTER ELEVEN
RC       EQU   12   GENERAL REGISTER TWELVE
RD       EQU   13   GENERAL REGISTER THIRTEEN
RE       EQU   14   GENERAL REGISTER FOURTEEN
RF       EQU   15   GENERAL REGISTER FIFTEEN
***********************************************************************
********             END OF REGISTER DEFINITIONS               ********
***********************************************************************
         SPACE 2
.NOEQU   ANOP
         SAVE  (14,12),,*
         AIF   ('&REG' EQ '').MESS
         BALR  &REG(1),0
         AIF   (N'&REG EQ 1).A
         AIF   (N'&REG EQ 2).B
         AIF   (N'&REG EQ 3).C
         AIF   (N'&REG EQ 4).D
         AIF   (N'&REG EQ 5).E
         MNOTE *****,'EXCESSIVE BASE REGISTERS -- 5 ASSUMED *****'
         AGO   .E
.MESS    MNOTE 12,'***** NO BASE REGISTER SPECIFIED *****'
         MEXIT
.NOTFREE MNOTE 12,'***** ENDALL NOT ISSUED PRIOR TO NEW SAVEALL *****'
         MEXIT
.E       USING *,&REG(1),&REG(2),&REG(3),&REG(4),&REG(5)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         L     &REG(5),=A(Q&SYSNDX+16384)
         AGO   .END
.D       USING *,&REG(1),&REG(2),&REG(3),&REG(4)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         L     &REG(4),=A(Q&SYSNDX+12288)
         AGO   .END
.C       USING *,&REG(1),&REG(2),&REG(3)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTERS
         L     &REG(3),=A(Q&SYSNDX+8192)
         AGO   .END
.B       USING *,&REG(1),&REG(2)
Q&SYSNDX L     &REG(2),=A(Q&SYSNDX+4096)     LOAD BASE REGISTER
         AGO   .END
.A       USING *,&REG(1)
.END     ANOP
         AIF   ('&RENT' NE 'RENT').NORENT
&SAVRENT SETB  1
         GETMAIN    R,LV=72        GET STORAGE FOR SAVE AREA
         ST    13,4(1)             SET UP FORWARD CHAIN
         ST    1,8(13)             SET UP BACKWARD CHAIN
         L     0,20(0,13)          RESET REGISTER 0
         L     1,24(0,13)          RESET REGISTER 1
         L     13,8(0,13)          LOAD SAVE AREA ADDRESS
         AGO   .RENT
.NORENT  ANOP
&TIME    SETB  1
         ST    13,SAVE&SYSNDX+4    SET UP FORWARD CHAIN
         LA    1,SAVE&SYSNDX
         ST    1,8(0,13)           SET UP BACKWARD CHAIN
         L     1,24(0,13)          RESET REGISTER 1
         LA    13,SAVE&SYSNDX
         B     J&SYSNDX           BRANCH AROUND BASE REGISTER CONST
SAVE&SYSNDX DC 18F'0'
.RENT    ANOP
         LTORG
J&SYSNDX DS    0H
         EJECT
         MEND
         MACRO
&NAME    ENDALL &CODE=
         GBLB  &TIME
         GBLB  &SAVRENT
         GBLC  &SAVNDX
         AIF   ('&CODE' GT '4095').MESS1
&NAME    DS    0H
         AIF   (NOT &SAVRENT).NOTRENT
&SAVRENT SETB  0
         LR    1,13                LOAD SAVE AREA ADDRESS
         L     13,4(0,13)          RE-LOAD REGISTER 13
         LA    0,72                LOAD NUMBER OF BYTES TO BE FREED
         FREEMAIN   R,LV=(0),A=(1) FREE SAVE AREA STORAGE
         AGO   .RENT
.NOTRENT ANOP
         AIF   (NOT &TIME).NOSAVE
&TIME    SETB  0
         L     13,SAVE&SAVNDX+4    RE-LOAD REGISTER 13
.RENT    ANOP
         AIF   ('&CODE' EQ '(15)').ENDIT
         AIF   ('&CODE' EQ '').COND1
         LA    15,&CODE           LOAD CODE TO REG 15
         AGO   .ENDIT
.MESS1   MNOTE 12,'***** RETURN CODE PASSED GREATER THAN 4095 *****'
         MEXIT
.NOSAVE  MNOTE 12,'***** SAVEALL NOT ISSUED PRIOR TO ENDALL *****'
         MEXIT
.COND1   LA    15,0               LOAD ZERO (NO CODE) TO REG 15
.ENDIT   ANOP
         RETURN     (14,12),T,RC=(15)
         MEND
       MACRO
       DISCLAIM
       EJECT
*********************************************************************
*                                                                    *
*                      D I S C L A I M E R                           *
*                                                                    *
*                                                                    *
*   THE PROGRAMS HAVE BEEN USED SUCCESSFULLY BY CBT AND TO THE BEST  *
*   OF OUR KNOWLEDGE THE SYSTEM IS OPERATIONAL, HOWEVER NO WARRANTY  *
*   IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND  *
*   NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR  *
*   INDIRECTLY  CAUSED BY THE USE OF THE PROGRAMS.  IT IS THE USERS  *
*   RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL  *
*   SUPPLIED.                                                        *
*                                                                    *
*   CBT DOES NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE  *
*   NOR DOES IT GUARANTEE TO PROVIDE ANY CORRECTIONS OR  EXTENSIONS  *
*   DESCRIBED BY THE USER OR  CORRECTIONS OR EXTENSIONS MADE IN THE  *
*   FUTURE BY CBT ITSELF.                                            *
*                                                                    *
*                                                                    *
*                                                                    *
*********************************************************************
       EJECT
       MEND
*          DATA SET CBT827     AT LEVEL 008 AS OF 02/05/78
RMFDUMP  SAVEALL REG=(11,3)
*
* THIS PROGRAM NEEDS 'SYS1.AMODGEN' AS PART OF SYSLIB
*
*        ESTAB SECND=11            SAVE REGS AND SET UP
*                                  ADDRESSABILITY.
         USING SMFRCD0,R5          COMMON RECORD SEGMENT.
         OPEN  SMFTP               OPEN THE TAPE FILE.
         OPEN  (OUT,OUTPUT)
         OPEN  INPUT
         PUT   OUT,MSG1
         GET   INPUT
         LR    R5,R1
         PACK  WORK,0(4,R5)
         PACK  WORK2,4(4,R5)
         CVB   R10,WORK
         MH    R10,=H'3600'
         ST    R10,STIME
         CVB   R10,WORK2
         MH    R10,=H'3600'
         ST    R10,QTIME
         PUT   OUT,MSG2
         PUT   OUT,MSG3
         PUT   OUT,MSG3A                                  AXC
         GET   INPUT
         LR    R5,R1
         LA    R9,BRANCHES+2
NEXLOOP  EQU   *
         CLI   0(R5),C'0'                                AXC
         BL    NEXEND                                    AXC
         PACK  WORK,0(2,R5)
         CVB   R10,WORK
         LTR   R10,R10
         BZ    NEXEND
         SLA   R10,2
         LA    R10,0(R10,R9)
         MVC   0(2,R10),NEXAD
         LA    R5,3(R5)
         B     NEXLOOP
NEXEND   EQU   *
         SPACE 2
         PUT   OUT,MSG4
         GET   INPUT
         LR    R5,R1
         CLI   0(R5),C' '
         BE    NAMEND
         MVC   JOBNAME,0(R5)
         MVI   FLAG,X'80'
NAMEND   EQU   *
         CLOSE INPUT
NEXREC   EQU   *
         GET   SMFTP               READ THE TAPE.
         LR    R5,R1               SAVE THE BUFFER ADDRESS.
*        CLC   SMF0SID,=C'H158'    CHECK FOR ACTUAL RECORD.
*        BNE   NEXREC              READ AGAIN IF NOT.
         SPACE
         CLI   SMF0RTY,X'02'       CHECK FOR HEADER REC.
         BE    NEXREC              IF SO, SKIP IT.
         CLI   SMF0RTY,X'03'       CHECK FOR TRAILER REC.
         BE    NEXREC              IF SO, SKIP IT.
         MVC   TIME,SMF0TME
         CLC   TIME,STIME
         BL    NEXREC
         CLC   TIME,QTIME
         BH    EOF
         SPACE 3
         SLR   R10,R10             CLEAR REG 10.
         IC    R10,SMF0RTY         LOAD RECTYP.
         C     R10,=F'100'         CHECK FOR TYPE GREATER THAN 100.
         BH    NEXREC              READ AGAIN IF SO.
         C     R10,=F'0'           CHECK FOR TYPE LESS THAN ZERO.
         BL    NEXREC              READ AGAIN IF SO.
         SLA   R10,2               MULTIPLY TYPE BY FOUR.
         LA    R9,BRANCHES         LOAD ADDRESS OF THE BRANCH TABLE.
         B     0(R10,R9)           BRANCH INTO TABLE.
         SPACE 3
EOF      EQU   *
         CLOSE SMFTP               CLOSE THE TAPE FILE.
         CLOSE OUT
*        EXIT  RC=0                ....AND EXIT.
         B     END
         EJECT
BRANCHES DS    0H
         B     IPLREC              0 IPL.
         B     NEXREC              1 DOES NOT EXIST.
         B     NEXREC              2 DUMP HEADER.
         B     NEXREC              3 DUMP TRAILER.
         B     STPREC              4 STEP TERMINATION.
         B     JOBREC              5 JOB TERMINATION.
         B     SYSOUT              6 OUTPUT WRITER.
         B     LOSTD               7 SMF DATA LOST.
         B     NEXREC              8 I/O CONFIGURATION.
         B     VONLINE             9 VARY ONLINE.
         B     ALLREC              10 ALLOCATION RECOVERY.
         B     VOFFLINE            11 VARY OFFLINE.
         B     NEXREC              12 DOES NOT EXIST.
         B     NEXREC              13 DOES NOT EXIST.
         B     DASIN               14 INPUT DATA SET ACTIVITY.
         B     DASOUT              15 OUTPUT DATA SET ACTIVITY.
         B     NEXREC              16 DOES NOT EXIST.
         B     DASCR               17 SCRATCH DATA SET STATUS.
         B     RENAME              18 RENAME DATA SET STATUS.
         B     NEXREC              19 DIRECT-ACCESS VOLUME.
         B     NUJOB               20 JOB COMMENCEMENT.
         B     NEXREC              21 ERROR STATISTICS BY VOLUME.
         B     NEXREC              22 CONFIGURATION.
         B     NEXREC              23 DOES NOT EXIST.
         B     NEXREC              24 DOES NOT EXIST.
         B     NEXREC              25 DOES NOT EXIST.
         B     PURGE               26 JES2 PURGE .
         B     NEXREC              27 DOES NOT EXIST.
         B     NEXREC              28 DOES NOT EXIST.
         B     NEXREC              29 DOES NOT EXIST.
         B     NEXREC              30 DOES NOT EXIST.
         B     TIOCIN              31 TIOC INITIALIZATION.
         B     NEXREC              32 DOES NOT EXIST.
         B     NEXREC              33 DOES NOT EXIST.
         B     TSOCOM              34 TS STEP TERMINATION.
         B     LOGOFF              35 TS JOB TERMINATION (LOGOFF).
         B     NEXREC              36 DOES NOT EXIST.
         B     NEXREC              37 DOES NOT EXIST.
         B     NEXREC              38 DOES NOT EXIST.
         B     NEXREC              39 DOES NOT EXIST.
         B     NEXREC              40 DYNAMIC DD.
         B     NEXREC              41 DOES NOT EXIST.
         B     NEXREC              42 DOES NOT EXIST.
         B     JES2ST              43 JES2 START.
         B     NEXREC              44 JES2 WITHDRAWAL.
         B     JESTOP              45 JES2 WITHDRAWAL.
         B     NEXREC              46 DOES NOT EXIST.
         B     RJEON               47 RJE SIGNON.
         B     RJEOFF              48 RJE SIGNOFF.
         B     NEXREC              49 JES2 INTEGRITY.
         B     NEXREC              50 DOES NOT EXIST.
         B     NEXREC              51 DOES NOT EXIST.
         B     NEXREC              52 DOES NOT EXIST.
         B     NEXREC              53 DOES NOT EXIST.
         B     NEXREC              54 DOES NOT EXIST.
         B     NEXREC              55 DOES NOT EXIST.
         B     NEXREC              56 DOES NOT EXIST.
         B     NEXREC              57 DOES NOT EXIST.
         B     NEXREC              58 DOES NOT EXIST.
         B     NEXREC              59 DOES NOT EXIST.
         B     NEXREC              60 DOES NOT EXIST.
         B     NEXREC              61 DOES NOT EXIST.
         B     VSAMOPEN            62 VSAM CLUSTER OR COMP OPENED.
         B     VSAMDEFN            63 VSAM CLUSTER OR COMP CATLGD.
         B     NEXREC              64 VSAM COMPONENT STATUS.
         B     NEXREC              65 DOES NOT EXIST.
         B     NEXREC              66 DOES NOT EXIST.
         B     VSAMDEL             67 VSAM ENTRY DELETED.
         B     VSAMRNAM            68 VSAM ENTRY RENAMED.
         B     NEXREC              69 VSAM DATA SPC DEFD OR DELD.
         B     NEXREC              70 CPU ACTIVITY.
         B     NEXREC              71 PAGING ACTIVITY.
         B     NEXREC              72 WORKLOAD ACTIVITY.
         B     NEXREC              73 CHANNEL ACTIVITY.
         B     NEXREC              74 DEVICE ACTIVITY.
         B     NEXREC              75 PAG/SWAP DASET ACTIVITY.
         B     NEXREC              76 TRACE ACTIVITY.
         B     NEXREC              77 ENQUEUE ACTIVITY.
         B     NEXREC              78 DOES NOT EXIST.
         B     NEXREC              79 MONITOR II ACTIVITY.
         EJECT
IPLREC   EQU   *
         B     NEXREC                                 AXC
         BAL   R14,TIMED
         MVC   MESG(15),=C'SYSTEM WAS IPLD'
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
STPREC   EQU   *
         USING SMFRCD4,R5
         TM    FLAG,X'80'
         BZ    STPA
         CLC   JOBNAME,SMF4JBN
         BNE   NEXREC
STPA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(30),=C'STEP XXX OF JOB XXXXXXXX ENDED'
         MVC   MESG+16(8),SMF4JBN
         SR    R10,R10
         IC    R10,SMF4STN
         CVD   R10,WORK
         MVC   MESG+4(4),PAT1
         ED    MESG+4(4),WORK+6
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
JOBREC   EQU   *
         USING SMFRCD5,R5
         TM    FLAG,X'80'
         BZ    JOBA
         CLC   JOBNAME,SMF5JBN
         BNE   NEXREC
JOBA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(22),=C'JOB XXXXXXXX HAS ENDED'
         MVC   MESG+4(8),SMF5JBN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
NUJOB    EQU   *
         USING SMF20HDR,R5
         TM    FLAG,X'80'
         BZ    NUJA
         CLC   JOBNAME,SMF20JBN
         BNE   NEXREC
NUJA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(24),=C'JOB XXXXXXXX HAS STARTED'
         MVC   MESG+4(8),SMF20JBN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
SYSOUT   EQU   *
         USING SMF6LEN,R5
         TM    FLAG,X'80'
         BZ    SYOA
         CLC   JOBNAME,SMF6JBN
         BNE   NEXREC
SYOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(34),=C'JOB XXXXXXXX HAS SYSOUT IN CLASS X'
         MVC   MESG+33(1),SMF6OWC
         MVC   MESG+4(8),SMF6JBN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
LOSTD    EQU   *
         USING SMF7LEN,R5
         BAL   R14,TIMED
         MVC   MESG(13),=C'SMF DATA LOST'
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VONLINE  EQU   *
         USING SMF9LEN,R5
         BAL   R14,TIMED
         MVC   MESG(26),=C'UNIT XXX WAS VARIED ONLINE'
         MVC   WORK+2(2),SMF9DVAD+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK+1
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
ALLREC   EQU   *
         USING SMF10LEN,R5
         TM    FLAG,X'80'
         BZ    ALLA
         CLC   JOBNAME,SMF10JBN
         BNE   NEXREC
ALLA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(42),=C'JOB XXXXXXXX IN ALLOC RECOVERY ON UNIT XXX'
         MVC   MESG+4(8),SMF10JBN
         MVC   WORK+2(2),SMF10DEV+2
         BAL   R14,CONV
         MVC   MESG+39(3),WORK+1
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VOFFLINE EQU   *
         USING SMF11LEN,R5
         BAL   R14,TIMED
         MVC   MESG(23),=C'UNIT XXX VARIED OFFLINE'
         MVC   WORK+2(2),SMF11DEV+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK+1
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
DASIN    EQU   *
         USING SMF14LEN,R5
         TM    FLAG,X'80'
         BZ    DASA
         CLC   JOBNAME,SMF14JBN
         BNE   NEXREC
DASA     EQU   *
         TM    SMF14RIN,X'10'
         BO    NEXREC
         BAL   R14,TIMED
         MVC   MESG(32),=C'JOB XXXXXXXX HAS READ FROM NAME='
         MVC   MESG+4(8),SMF14JBN
         MVC   MESG+32(44),SMFJFCB1
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
DASOUT   EQU   *
         TM    SMF14RIN,X'10'
         BO    NEXREC
         TM    FLAG,X'80'
         BZ    DAOA
         CLC   JOBNAME,SMF14JBN
         BNE   NEXREC
DAOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(33),=C'JOB XXXXXXXX HAS WRITTEN TO NAME='
         MVC   MESG+4(8),SMF14JBN
         MVC   MESG+33(44),SMFJFCB1
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
DASCR    EQU   *
         USING SMF17LEN,R5
         TM    FLAG,X'80'
         BZ    DACA
         CLC   JOBNAME,SMF17JBN
         BNE   NEXREC
DACA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(27),=C'JOB XXXXXXXX HAS SCRATCHED '
         MVC   MESG+4(8),SMF17JBN
         MVC   MESG+27(44),SMF17DSN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
RENAME   EQU   *
         USING SMF18LEN,R5
         TM    FLAG,X'80'
         BZ    RENA
         CLC   JOBNAME,SMF18JBN
         BNE   NEXREC
RENA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(22),=C'JOB XXXXXXXX, DATASET '
         MVC   MESG+4(8),SMF18JBN
         MVC   MESG+22(44),SMF18ODS
         LA    R10,MESG+22
RENLOOP  EQU   *
         CLI   0(R10),C' '
         BE    RENGOT
         LA    R10,1(R10)
         B     RENLOOP
RENGOT   EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'WAS RENAMED'
         MVC   12(44,R10),SMF18NDS
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
PURGE    EQU   *
         USING SMF26LEN,R5
         TM    FLAG,X'80'
         BZ    PURA
         CLC   JOBNAME,SMF26JBN
         BNE   NEXREC
PURA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(28),=C'XXXXXXXX XXXXXXXX HAS PURGED'
         MVC   MESG(8),SMF26JID
         MVC   MESG+9(8),SMF26JBN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
TIOCIN   EQU   *
         BAL   R14,TIMED
         MVC   MESG(15),=C'TSO WAS STARTED'
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
TSOCOM   EQU   *
         USING TIVRLEN,R5
         TM    FLAG,X'80'
         BZ    TSOA
         CLC   JOBNAME,TIVUIF
         BNE   NEXREC
TSOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(6),=C'TSOCOM'
         MVC   MESG+7(8),TIVUIF
         MVC   MESG+16(8),TIVPRGNM
         MVC   MESG+25(8),TIVINVNM
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
LOGOFF   EQU   *
         USING TLGRLEN,R5
         TM    FLAG,X'80'
         BZ    LOGA
         CLC   JOBNAME,TLGUIF
         BNE   NEXREC
LOGA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(10),=C'TSO LOGOFF'
         MVC   MESG+11(8),TLGUIF
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
JES2ST   EQU   *
         USING SMF43LEN,R5
         BAL   R14,TIMED
         MVC   MESG(12),=C'JES2 STARTED'
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
JESTOP   EQU   *
         USING SMF45LEN,R5
         BAL   R14,TIMED
         MVC   MESG(14),=C'JES2 WITHDRAWN'
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
RJEON    EQU   *
         USING SMF47LEN,R5
         BAL   R14,TIMED
         MVC   MESG(10),=C'RJE SIGNON'
         MVC   MESG+11(8),SMF47RMT
         MVC   MESG+20(8),SMF47LIN
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
RJEOFF   EQU   *
         USING SMF48LEN,R5
         BAL   R14,TIMED
         MVC   MESG(11),=C'RJE SIGNOFF'
         MVC   MESG+12(16),SMF48RMT
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VSAMOPEN EQU   *
         USING SMF62LEN,R5
         TM    FLAG,X'80'
         BZ    VSOA
         CLC   JOBNAME,SMF62JBN
         BNE   NEXREC
VSOA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(8),=C'VSAMOPEN'
         MVC   MESG+9(8),SMF62JBN
         MVC   MESG+18(44),SMF62DNM
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VSAMDEFN EQU   *
         USING SMF63LEN,R5
         TM    FLAG,X'80'
         BZ    VSDA
         CLC   JOBNAME,SMF63JBN
         BNE   NEXREC
VSDA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(10),=C'VSAMDEFINE'
         MVC   MESG+11(8),SMF63JBN
         MVC   MESG+20(44),SMF63ENM
         LA    R10,MESG+20
DEFNLOOP EQU   *
         CLI   0(R10),C' '
         BE    DEFNGOT
         LA    R10,1(R10)
         B     DEFNLOOP
DEFNGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'IN CATOLOG '
         MVC   12(44,R10),SMF63CNM
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VSAMDEL  EQU   *
         USING SMF67LEN,R5
         TM    FLAG,X'80'
         BZ    VSLA
         CLC   JOBNAME,SMF67JBN
         BNE   NEXREC
VSLA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(10),=C'VSAMDELETE'
         MVC   MESG+11(8),SMF67JBN
         MVC   MESG+20(44),SMF67DEN
         LA    R10,MESG+20
DELTLOOP EQU   *
         CLI   0(R10),C' '
         BE    DELTGOT
         LA    R10,1(R10)
         B     DELTLOOP
DELTGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'IN CATOLOG '
         MVC   12(44,R10),SMF67CNM
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
VSAMRNAM EQU   *
         USING SMF68LEN,R5
         TM    FLAG,X'80'
         BZ    VSRA
         CLC   JOBNAME,SMF68JBN
         BNE   NEXREC
VSRA     EQU   *
         BAL   R14,TIMED
         MVC   MESG(10),=C'VSAMRENAME'
         MVC   MESG+11(8),SMF68JBN
         MVC   MESG+20(44),SMF68ONM
         LA    R10,MESG+20
RNAMLOOP EQU   *
         CLI   0(R10),C' '
         BE    RNAMGOT
         LA    R10,1(R10)
         B     RNAMLOOP
RNAMGOT  EQU   *
         LA    R10,1(R10)
         MVC   0(11,R10),=C'WAS RENAMED'
         MVC   13(44,R10),SMF68NNM
         PUT   OUT,LINE
         B     NEXREC
         SPACE 3
TIMED    EQU   *
         L     R10,TIME
         CVD   R10,WORK
         DP    WORK,=P'100'
         DP    WORK(6),=P'3600'
         ZAP   WORK2,WORK(3)
         MP    WORK2,=P'10000'
         ZAP   WORK3,WORK+3(3)
         DP    WORK3,=P'60'
         ZAP   WORK,WORK3(6)
         MP    WORK,=P'100'
         AP    WORK2,WORK
         AP    WORK2,WORK3+6(2)
         ZAP   WORK,WORK2
         MVC   LTIME,PAT2
         ED    LTIME,WORK+4
         MVC   MESG(120),BLANKS
         BR    R14
         SPACE 3
CONV     EQU   *
         LA    R6,CHARS
         L     R5,WORK
         SLL   R5,20
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+1
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+2
         SR    R4,R4
         SLDA  R4,4
         IC    R10,0(R4,R6)
         STC   R10,WORK+3
         BR    R14
         SPACE 3
LINE     DS    0CL133
         DC    C' '
LTIME    DS    CL10
         DC    CL2' '
MESG     DS    CL120
         SPACE
WORK     DS    D
WORK2    DS    D
WORK3    DS    D
TIME     DS    F
STIME    DS    F
QTIME    DS    F
JOBNAME  DS    CL8
NEXAD    DC    S(NEXREC)
PAT1     DC    X'40202021'
PAT2     DC    X'402020217A20207A2020'
FLAG     DC    X'00'
CHARS    DC    C'0123456789ABCDEF'
MSG1     DC    CL133' ENTER TIMES, BBBBEEEE  (MILITARY TIME FORMAT)'
MSG2     DS    0CL133
         DC    C' RECS PROC''D ARE:0,4-7,9-11,14,15,'
         DC    C'17,18,20,26,31,34,35,43,45,47,48,'
         DC    C'62,63,67,68'
BLANKS   DC    CL120' '
BLANK    DC    CL13' '                                             AXC
MSG3     DC    CL133' TO SUPPRESS RECS ENTER NUMBERS SEP''D BY BLANKS'
MSG3A    DC    CL133' OR ENTER A SINGLE BLANK FOR NO SUPRESSION'   AXC
MSG4     DC    CL133' ENTER A SPECIFIC JOBNAME OR A SINGLE BLANK'
 LTORG
INPUT    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GL
OUT      DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FA,   *
               BLKSIZE=133
SMFTP    DCB   DDNAME=SMFTPE,DSORG=PS,MACRF=GL,EODAD=EOF,              X
               BFTEK=A
         SPACE 3
END      ENDALL
RECDEF   DSECT
RECS     IFASMFR (0,4,5,6,7,9,10,11,14,17,18,20,26,31,34,35)
         IFASMFR (43,45,47,48,62,63,64,67,68,69)
         END
