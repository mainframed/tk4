OBTAIN   TITLE 'PL/I CALLABLE ROUTINES TO OBTAIN DSCBS'
* OBTAIN HAS TWO ENTRIES:
* OBTSRCH READS THE DATA PORTION OF A FORMAT 1 (IDENTIFIER) DSCB
* SELECTED BY DATASET NAME.
* OBTSEEK READS ANY FULL DSCB SELECTED BY ITS TRACK ADDRESS (CCHHR).
         SPACE 2
* SRCH -> TO USE:   CALL OBTAIN(DSCBF1,VOLSER,RC)
* DSCBF1 IS A 184 CHARACTER FIXED LENGTH STRING.  THE 1ST 44 CHARACTERS
* CONTAIN THE FULLY QUALIFIED DATASET NAME (FILLED IN BEFORE THE CALL).
* THE NEXT 96 CHARACTERS ARE THE DATA PORTION OF A FORMAT 1 DSCB.
* THESE ARE RETURNED BY OBTAIN.
* THE LAST 44 CHARACTERS ARE A WORK AREA FOR OBTAIN.
* THE FIRST 5 CHARACTERS OF THE WORK AREA WILL CONTAIN THE ABSOLUTE
* TRACK ADDRESS (CCHHR) OF THE RETURNED DSCB.
* NOTE THAT THE 1ST 140 CHARACTERS ARE A FORMAT 1 DSCB,
* THEREFORE A STRUCTURE THAT MAPS THE DSCB SHOULD BE BASED ON THE
* BEGINNING OF THIS STRING (OR A MAPPING STRUCTURE THAT INCLUDES A
* 44 BYTE TRAILER MAY BE USED FOR THIS ARGUMENT).
* VOLSER IS THE 6 CHARACTER VOLUME SERIAL OF THE DISK THAT CONTAINS
* THE DATASET (FILLED IN BEFORE THE CALL).
* RC IS THE RETURN CODE:
*      0 - OK
*      4 - VOLUME IS NOT MOUNTED
*      8 - DSCB NOT FOUND ON VTOC FOR SPECIFIED DATASET/VOLUME
*     12 - I/O ERROR OR BAD F1 DSCB
*     16 - INVALID WORK AREA PTR
*
* NOTE - IF THE DSNAME IS SET TO 44X'04', THEN THE FORMAT 4 (VTOC) DSCB
* FOR THE SPECIFIED VOLUME WILL BE RETURNED.
         EJECT
* SEEK -> TO USE:  CALL OBTAIN(CCHHR,VOLSER,DSCB,RC)
* CCHHR IS THE 5 CHARACTER TRACK ADDRESS OF THE DSCB
* (FILLED IN BEFORE THE CALL).
* VOLSER IS THE 6 CHARACTER VOLUME SERIAL OF THE DISK THAT CONTAINS
* THE DATASET (FILLED IN BEFORE THE CALL).
* DSCB IS A 140 CHARACTER AREA INTO WHICH THE DSCB WILL BE RETURNED.
* A 140 BYTE MAPPING STRUCTURE MAY BE USED FOR THIS ARGUMENT.
* NOTE THAT FOR A FORMAT 1 DSCB, THE DSNAME IS RETURNED AS THE 1ST 44
* BYTES OF THE DSCB.
* RC IS THE RETURN CODE:
*      0 - OK
*      4 - VOLUME IS NOT MOUNTED
*      8 - DSCB NOT FOUND IN VTOC OF SPECIFIED VOLUME
*     12 - I/O ERROR OR BAD F4 DSCB
*     16 - INVALID WORK AREA PTR
*     20 - TRACK ADDR CCHH IS NOT WITHIN BOUNDS OF VTOC
         SPACE 2
* SEE SPL:  DATA MANAGMENT FOR INFORMATION.
*
* PL/I DECLARE:
* DCL OBTAIN   GENERIC
*     (OBTSRCH WHEN (*,*,*),
*      OBTSEEK WHEN (*,*,*,*));
* DCL OBTSRCH  EXT ENTRY(*,CHAR(6),FIXED BIN(15,0));
* DCL OBTSEEK  EXT ENTRY(CHAR(5),CHAR(6),*,FIXED BIN(15,0));
*
* ATTRIBUTES:  REENTRANT, REUSABLE, REFRESHABLE
*
* LOGIC FROM "OBTAINS" - COURTESY JSA
* APB - 08/29/79 */
         EJECT
OBTAIN   PLIXSET
         EJECT
OBTSRCH  PLIXDENT DSALEN=DSALEN    SEARCH ENTRY
         USING DSA,RDSA
         SPACE 2
         LM    R2,R4,0(R1)         GET PARMS
         MVC   CAMLIST,CAMSRCH     REENTRANT COPY
         MVC   CAM+4(4),0(R2)      ADDR(DSNAME) - FROM LOCATOR
         MVC   CAM+8(4),0(R3)      ADDR(VOLSER) - FROM LOCATOR
         L     R2,0(R2)            -> DSNAME
         LA    R2,44(R2)           INCREMENT PAST DSNAME
         ST    R2,CAM+12           -> DATA PORTION OF DSCB
         OBTAIN CAM                OBTAIN THE DSCB
         STH   R15,0(R4)           SET RETURN CODE
         SPACE 2
         PLIXEXT                   EXIT
         EJECT
OBTSEEK  PLIXDENT DSALEN=DSALEN    SEEK ENTRY
         USING DSA,RDSA
         SPACE 2
         LM    R2,R5,0(R1)         GET PARMS
         MVC   CAMLIST,CAMSEEK     REENTRANT COPY
         MVC   CAM+4(4),0(R2)      ADDR(CCHHR) -  FROM LOCATOR
         MVC   CAM+8(4),0(R3)      ADDR(VOLSER) - FROM LOCATOR
         MVC   CAM+12(4),0(R4)     ADDR(DSCB)   - FROM LOCATOR
         OBTAIN CAM                OBTAIN THE DSCB
         STH   R15,0(R5)           SET RETURN CODE
         SPACE 2
         PLIXEXT                   EXIT
         EJECT
CAMSRCH  CAMLST SEARCH,0,0,0       PROTOTYPE FOR SEARCH
         SPACE
CAMSEEK  CAMLST SEEK,0,0,0         PROTOTYPE FOR SEEK
         SPACE
         LTORG
         EJECT
         PLIXDSA
CAM      CAMLST SEARCH,0,0,0       CAM (FUNCTION IS DUMMY)
CAMLIST  EQU   CAM,*-CAM
DSALEN   EQU   *-DSA
         END   OBTAIN
