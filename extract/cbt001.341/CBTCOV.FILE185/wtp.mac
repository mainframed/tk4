WTP      TITLE 'ROUTINE TO ISSUE A WRITE TO PROGRAMMER MESSAGE'
* WTP IS USED TO ISSUE A 70 CHARACTER MESSAGE TO THE PROGRAMMER
* THIS IS ESPECIALLY USEFUL FOR PUTTING OUT MESSAGES IN BATCH WHEN
* THERE IS NO PRINT FILE AND NONE CAN BE ALLOCATED
* IF THE MESSAGE IS GOING TO A TSO USER, HE MUST HAVE THE "WTPMSG"
* OPTION SET IN HIS PROFILE
* WTP SHOULD BE USED JUDICIOUSLY SINCE ALL WTP MESSAGE ALSO GO TO
* THE OPERATOR'S CONSOLE (UNDER YCC'S CURRENT CONFIGURATION)
* NOTE THAT THE SUPPLIED MESSAGE MUST BE 70 CHARACTERS LONG
* (PADDED WITH BLANKS)
* THE RETURN CODE FROM THE WTO IS RETURNED IN REGISTER 15
* PL/I DECLARE:
* DCL WTP      EXT ENTRY (CHAR(70)) OPTIONS (ASM,INTER,RETCODE);
* ATTRIBUTES:  REFRESHABLE, REUSABLE, REENTRANT
* APB - 08/21/79
         SPACE 2
WTP      XSAVE OPT=CSECT,REEN=WTOLEN
         EJECT
         REGS
         EJECT
         L     R1,0(R1)                      -> 70 CHAR MESSAGE
         USING DSA,R13                       ADDRESS DSA
         MVC   WTOLIST(WTOLEN),WTOLST        MOVE LIST FORM TO DSA
         MVC   WTOLIST+4(70),0(R1)           MOVE IN MESSAGE
         WTO   MF=(E,WTOLIST)                PUT OUT MESSAGE
         SPACE 2
         XRETURN T=*,REEN=WTOLEN,RC=(R15)    RETURN
         EJECT
* PROTOTYPE WTO WITH 70 CHAR BLANK LINE
* ROUTCDE & DESC REQUEST A WRITE TO PROGRAMMER
WTOLST   WTO   '                                                       *
                              ',ROUTCDE=11,DESC=7,MF=L
         SPACE 2
         LTORG
         SPACE 2
DSA      DSECT                     DYNAMIC STORAGE
         DS    18F                 SAVEAREA
* SPACE FOR THE WTO LIST FORMAT
WTOLIST  WTO   '                                                       *
                              ',ROUTCDE=11,DESC=7,MF=L
WTOLEN   EQU   *-WTOLIST
         END   WTP
