         PRINT NOGEN
         INITIAL
         TITLE 'DAIR ERROR MESSAGE ROUTINE'
         DO    LINKAGE, ZERO DATA AREA
YCCDAEM  RSECT BASE=12,LENGTH=DSECTL
         USING DSECT,R13
         LM    R10,R11,0(R1)  PICK POINTERS,
         LA    R2,72(,R13)
         LA    R3,DSECTL-72
         SR    R5,R5
         MVCL  R2,R4
         OD    LINKAGE DONE.
         DO    PUT DAIR MESSAGE.
         LA    R1,DAIRMS      MS BODY,
         L     R2,0(,R10)     AND RET CODE,
         BAL   R8,PUTMS       PUT THEM OUT.
         OD    DAIR MS PUT.
         DO    PICK DAR AND/OR CAT RET CODES.
         LH    R8,0(,R11)     PICK UP DAPB CODE,
         GRAB  (R8),(8,12,28,48),MULT=4,CHECK=YES
         CASE  8
         USING DA08,R11
         LH    R9,DA08DARC
         LH    R8,DA08CTRC
         CASE  12
         USING DA0C,R11
         LH    R9,DA0CDARC
         CASE  28
         USING DA1C,R11
         LH    R9,DA1CDARC
         CASE  48
         USING DA30,R11
         LH    R9,DA30DARC
         CASE  ELSE
         SR    R8,R8
         SR    R9,R9
         BARG
         OD    CODES LOADED.
         DO    PUT OUT A DAR OR CAT MS IF NECESSARY.
         L     R2,0(,R10)     PICK UP R.C AGAIN,
         GRAB  (R2),(8,12),MULT=4,CHECK=YES
         CASE  8
         LA    R1,CTRCMS
         LR    R2,R8
         BAL   R8,PUTMS
         CASE  12
         LA    R1,DARCMS
         LR    R2,R9
         BAL   R8,PUTMS
         CASE  ELSE
         BARG
         OD    DARC OR CTRC MS OUT.
         RRETURN LENGTH=DSECTL
         TITLE 'INTERNAL SUBROUTINES'
         DO    PUT OUT A R.C. MS.
PUTMS    LH    R3,0(,R1)      MS LENGTH,
         EX    R3,MOVEMS
         LA    R5,MSG+1(R3)
         STH   R2,DECODE
         MVI   DECODE+2,X'0F'
         UNPK  0(5,R5),DECODE(3)
         TR    0(4,R5),HEXTRAN
         LA    R3,5(,R3)
         TPUT  MSG,(R3)
         BR    R8
MOVEMS   MVC   MSG(0),2(R1)
         OD    MS PUT.
         FINAL
         TITLE 'CONSTANTS'
DAIRMS   DC    AL2(DAIRMSL)
         DC    C'DAIR RETURN CODE '
DAIRMSL  EQU   *-(DAIRMS+3)
CTRCMS   DC    AL2(CTRCMSL)
         DC    C'CATALOG RETURN CODE '
CTRCMSL  EQU   *-(CTRCMS+3)
DARCMS   DC    AL2(DARCMSL)
         DC    C'DYNAMIC ALLOCATION RETURN CODE '
DARCMSL  EQU   *-(DARCMS+3)
HEXTRAN  EQU   *-X'F0'
         DC    C'0123456789ABCDEF'
         TITLE 'DSECTS'
DSECT    DSECT
SAVEAREA DS    18F
DECODE   DS    H
         DS    X
MSG      DS    CL(DARCMSL+5)
DSECTL   EQU   *-DSECT
         SPACE 2
DA08     DSECT
         COPY  DA08SECT
         SPACE 2
DA1C     DSECT
         COPY  DA1CSECT
         SPACE 2
DA0C     DSECT
         COPY  DA0CSECT
         SPACE 2
DA30     DSECT
         COPY  DA30SECT
         END
