SVC243   TITLE 'REGION SIZE SVC--ALTERS REGION IN LDA'
         SPACE 3
*                                  JOSH AUERBACH, YALE COMPUTER CENTER
*                                  LAST ALTERATION, OCTOBER 31, 1978
         SPACE 3
*****************************************************************
*                                                               *
*        SETS REGION SIZE IN LDA.                               *
*        AT ENTRY: R1 CONTAINS NEW 'SOFT LIMIT' (VVREGSZ)       *
*              R0  CONTAINS NEW 'FUDGE FACTOR' (DIFFERENCE      *
*                  BETWEEN VVREGSZ AND LDALIMIT)                *
*              IF EITHER VALUE IS NEGATIVE, NO CHANGE OCCURS    *
*                                                               *
*        AT RETURN, R1 AND R0 CONTAIN OLD VALUES FOR THEIR      *
*        RESPECTIVE QUANTITIES AND R15 CONTAINS 0 IF A CHANGE   *
*        WAS PERFORMED, 4 OTHERWISE.                            *
*                                                               *
*****************************************************************
         SPACE 3
IGC0024C CSECT
         PRINT NOGEN
         IHALDA
         IHAASCB
         EJECT
         PRINT GEN
IGC0024C CSECT
         SPACE 3
         YREGS
LDAREG   EQU   R2
         USING LDA,LDAREG
         USING ASCB,R7
         USING IGC0024C,R6
         SPACE 3
         L     LDAREG,ASCBLDA
         SRL   R1,11               2K MULTIPLE
         SLL   R1,11
         SRL   R0,11
         SLL   R0,11
         LTR   R1,R1
         BNP   NOCHANGE
         LTR   R0,R0
         BM    NOCHANGE
         AR    R0,R1
         L     R15,VVREGSZ
         ST    R1,VVREGSZ
         LR    R1,R15
         L     R15,LDALIMIT
         ST    R0,LDALIMIT
         LR    R0,R15
         SR    R0,R1
         SR    R15,R15
         BR    R14
NOCHANGE LA    R15,4
         L     R0,LDALIMIT
         L     R1,VVREGSZ
         SR    R0,R1
         BR    R14
         LTORG
         END
