  TITLE  'UNCATLG  --  TSO COMMAND TO UNCATALOG A DATASET'
         PRINT NOGEN
UNCATLG  CSECT
         SAVE  (14,12),,*
         LR    R2,R1          SAVE THE PARM REG
         USING CPPL,R2        ADDRESSABILITY FOR PARMS
         LR    R12,R15        SET UP ADDRESSABILITY
         USING UNCATLG,R12           FOR THIS CSECT
         LA    R1,SAVEAREA
         ST    R13,4(R1)      CHAIN
         ST    R1,8(R13)        SAVEAREAS
         LR    R13,R1         SET UP ADDRESSABILITY
*
*        SET UP PARSE PARAMETER LIST
*
         L     R3,CPPLUPT     UPT
         L     R4,CPPLECT     ECT
         LA    R5,ECB         ECB
         L     R6,=A(PCL)     PCL
         LA    R7,PARSEPDL    PDL
         L     R8,CPPLCBUF    CBUF
         SR    R9,R9          UWA = 0
         STM   R3,R9,PPLAREA  AND THAT'S THE PPL
*
*                   LINK TO PARSE AND CHECK RETURN CODES
*
         LA    R1,PPLAREA
         LINK  EP=IKJPARS
         LTR   R15,R15
         BZ    PARSOK         BR IF NO ERROR
         TPUT  MSG0,L'MSG0    TELL USER
         B     TERMNATE        AND LEAVE
         SPACE
MSG0     DC    C'PARSE ERROR'
         SPACE
PARSOK   DS    0H
         EJECT
*
*                   GET PARAMETERS FROM PARSE
*
         L     R10,PARSEPDL        ADDRESSABILITY
         USING IKJPARMD,R10           FOR PARSE ANSWER
*
*        SET UP FOR CHAINING THROUGH DSNAMES
*
         LA    R9,DSN       ADDRESSABILITY
         USING DSNPDE,R9        FOR DSN CHAIN
NEXTDSN  L     R8,PDEDSN      ADDRESS OF DSN
         LH    R7,PDELEN1      LENGTH
         SR    R6,R6          CLEAR REG - MAY IC LATER
         MVI   DSNAME,C' '         BLANK
         MVC   DSNAME+1(43),DSNAME   DATA SET NAME
         TM    PDEFLAG1,X'40'     IS DSN QUOTED
         BO    DSNQUOTE
*
*              GET THE USERID AND PREFIX THE DSNAME
*
         L     R5,CPPLPSCB    ADDRESS OF PSCB
         USING PSCB,R5           FOR USERID
         IC    R6,PSCBUSRL      LENGTH OF USERID
         MVC   DSNAME(7),PSCBUSER  MOVE USERID
         LA    R6,DSNAME(R6)    ADD BASE
         MVI   0(R6),C'.'     ADD PERIOD
         LA    R6,1(R6)         ITS LENGTH
         B     EXECUTE
DSNQUOTE LA    R6,DSNAME(R6)  ADD ON THE BASE
EXECUTE  BCTR  R7,0
         EX    R7,MOVEDSN
         DROP  R5
         DROP  R2
         EJECT
*
*              ALL SET - UNCATALOG IT, THEN CHECK
*
         CATALOG CAMLST
         LTR   R15,R15
         BNZ   ERROR
         TPUT  UNCTLGED,12     ISSUE A MESSAGE TO TERMINAL
*
*              NOW CHAIN TO ANY MORE DATA SETS
*
         L     R9,PDEDSNCH
         LA    R9,0(R9)
         LTR   R9,R9
         BNZ   NEXTDSN
         SR    R15,R15
*
*        RETURN
*
TERMNATE DS    0H
         L     R13,4(R13)
         RETURN (14,12),T
         EJECT
*
*        SEND ERROR MESSAGE TO CALLER
*
ERROR    DS    0H
         CH    R15,=H'8'      CHECK FOR RC=04,08
         BL    ERR4            RC = 4
         BE    ERR8            RC = 8
         CH    R15,=H'16'     CHECK FOR RC=12,16
         BL    ERR12           RC = 12
         BE    ERR16           RC = 16
         CH    R15,=H'24'     CHECK FOR RC=20,24,28
         BL    ERR20           RC = 20
         BE    ERR24           RC = 24
         BH    ERR28
*
ERR4     TPUT  MSG1,L'MSG1    VOLUME NOT MOUNTED OR NO CATALOG
         B     TERMNATE
MSG1     DC    C'VOLUME NOT MOUNTED OR DOES NOT CONTAIN A CATALOG'
*
ERR8     TPUT  MSG2,L'MSG2    INVALID CATALOG STRUCTURE
         B     TERMNATE
MSG2     DC    C'INVALID CATALOG STRUCTURE OR NAME NOT IN CATALOG'
*
ERR12    TPUT  MSG3,L'MSG3    SHOULD NOT OCCUR
         B     TERMNATE
MSG3     DC    C'UNKNOWN ERROR'
*
ERR16    EQU   ERR12          SHOULD NOT OCCUR
*
ERR20    TPUT  MSG4,L'MSG4    INSUFFICIENT SPACE
         B     TERMNATE
MSG4     DC    C'INSUFFICIENT SPACE IN THE CATALOG'
*
ERR24    TPUT  MSG5,L'MSG5    IMPROPER GDG
         B     TERMNATE
MSG5     DC    C'IMPROPERLY NAMED GENERATION DATA GROUP'
*
ERR28    TPUT  MSG6,L'MSG6    PERMANENT I/O ERROR
         B     TERMNATE
MSG6     DC    C'PERMANENT I/O ERROR IN THE CATALOG'
         EJECT
*
*                   DEFINED CONSTANTS
*
SAVEAREA DS    18F
CAMLST  CAMLST  UCATDX,DSNAME
MOVEDSN  MVC   0(1,R6),0(R8)
UNCTLGED DC    CL12'UNCATALOGUED'
PPLAREA  DS    7F             PARSE PARM LIST AREA
ECB      DC    F'0'
PARSEPDL DS    F
DSNAME   DC    CL44' '
         LTORG ,
         EJECT
*
*        COMMAND DESCRIPTION FOR PARSE
*
PCL      IKJPARM
         SPACE
DSN      IKJPOSIT DSNAME,LIST,PROMPT='DATA SET NAME',                  *
               HELP='THE NAME OF THE DATASET WHICH IS TO BE UNCATALOGUE*
               D'
         SPACE
         IKJENDP
         SPACE 3
DSNPDE   DSECT
PDEDSN   DS    A         POINTER TO DSNAME
PDELEN1  DS    H         LENGTH OF DSNAME
PDEFLAG1 DS    H         BIT 0 - PRESENT, BIT 1 - QUOTES
PDEMEM   DS    A         POINTER TO MEMBER NAME
PDELEN2  DS    H         LENGTH OF MEMBER NAME
PDEFLAG2 DS    H         BIT 0 - PRESENT
PDEPASS  DS    A         POINTER TO PASSWORD
PDEFLAG3 DS    H        BIT 0  - PRESENT
PDEDSNCH DS    A
         SPACE 3
         PRINT GEN
         IKJCPPL
         EJECT
         IKJPSCB
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
