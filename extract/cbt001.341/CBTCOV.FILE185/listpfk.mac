LISTPFK  $ENTER
         YREGS
         L     R1,CVTPTR           --> CVT
         L     R1,CVTCUCB-CVT(,R1) --> UCM BASE
         LM    R3,R5,UCMVEA-UCM(R1) HAVE BXLE DESCRIPTOR OF UCM ENTRIES
         LA    R6,C'1'            HAVE CONSOLE COUNTER
         SLR   R8,R8               FOR IC
CONSLOOP ICM   R2,15,UCMXB-UCMLIST(R3) --> DCM IF PRESENT
         BZ    NEXT                SKIP IF NOT
         STC   R6,CNNO             SET CONSOLE NUMBER
         TPUT  TITLE,LTITLE
         L     R7,DCMADPFK-RDCM(R2)
PFKLOOP  CLI   0(R7),X'64'         TEST FOR END
         BE    NEXT
         IC    R8,0(R7)            GET NUMBER
         CVD   R8,DWORK            CONVERT TO DECIMAL
         UNPK  PFKNO,DWORK+6(2)     ZONE
         MVI   PFKNO,C' '          PRETTY UP
         OI    PFKNO+2,C'0'        PRETTY UP
         MVI   CONV,C' '           SET NONCONV
         TM    1(R7),X'20'         CONVERSATIONAL?
         BNO   NOTCONV             NO, SKIP RESET
         MVI   CONV,C'C'           YES, INDICATE
NOTCONV  TM    1(R7),X'80'         ACTIVE?
         BO    ACTIVE              YES, DISPLAY
         MVC   TEXT(108),=CL108'(INACTIVE)'
         B     SHOW
ACTIVE   TM    1(R7),X'08'         NESTED?
         BO    NESTED              YES, HANDLE SPECIALLY
         MVC   TEXT(108),2(R7)     NO, MOVE TEXT
         B     SHOW
NESTED   MVI   TEXT,C' '           BLANK
         MVC   TEXT+1(107),TEXT      OUT TEXT AREA
         LA    1,TEXT              --> TEXT
         LA    R15,2(,R7)          --> SOURCE TEXT
NESTLOOP CLI   0(R15),C'/'         AT END?
         BE    SHOW                YES, DO DISPLAY
         IC    R8,0(R15)           NO, GET PFK NUMBER
         CVD   R8,DWORK           CONVERT TO DECIMAL
         UNPK  0(3,R1),DWORK+6(2)   CONVERT TO ZONED
         OI    2(R1),C'0'          SET VALID EBCDIC
         MVI   0(R1),C' '          BLANK FIRST CHARACTER
         LA    R1,3(,R1)           INCREMENT SINK PTR
         LA    R15,2(,R15)         INCREMENT SOURCE PTR
         B     NESTLOOP            AND REPEAT
SHOW     TPUT  LINE,LLINE          OUTPUT THE LINE
         LA    R7,110(,R7)
         B     PFKLOOP             REPEAT FOR OTHER PFKS
NEXT     LA    R6,1(,R6)           INCREMENT CONSOLE #
         BXLE  R3,R4,CONSLOOP
         $EXIT ((14,12)),RC=0
DWORK    DS    D
TITLE    DC    C'PFK DEFINITIONS FOR CN'
CNNO     DS    C
LTITLE   EQU   *-TITLE
LINE     DS    0C
PFKNO    DS    CL3
         DC    C' '
CONV     DS    C
         DC    C' '
TEXT     DS    CL108
LLINE    EQU   *-LINE
         LTORG
         PRINT NOGEN
         CVT   DSECT=YES,LIST=YES
         IEECUCM FORMAT=NEW
RDCM     DSECT
         IEECRDCM
         END
