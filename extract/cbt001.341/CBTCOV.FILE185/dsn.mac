DSN      CSECT
         SAVE  (14,12),,DSN
         BALR  R12,0
         USING *,R12
         LR    R11,R1
         USING CPPL,R11
         L     R2,CPPLCBUF             LOAD BUFFER ADDRESS
         CLI   1(R2),X'09'             BUFFER LENGTH MUST BE
         BL    ERROR                   AT LEAST 9 BYTES AND
         CLI   3(R2),X'04'             THE OFFSET 4 BYTES
         BL    ERROR
         LH    R3,2(R2)                LOAD OFFSET
         LA    R3,4(R3)
         LH    R5,0(R2)                LOAD BUFFER LENGTH
         SR    R5,R3                   FIND OPERAND LENGTH
         AR    R3,R2                   POINT TO FIRST CHARACTER
*                                      AFTER COMMAND DSN
         SPACE 2
         LR    R4,R3                   FIND LAST CHARACTER
DSN1     CLI   0(R4),X'40'             OF DSNAME
         BE    DSN2
         LA    R4,1(R4)
         BCT   R5,DSN1
         SPACE 2
DSN2     BCTR  R4,R0
         CLI   0(R3),C' '
         BE    ERROR
         CLI   0(R3),C''''             IF FIRST CHARACTER IS ',
         BNE   USERID                  DON'T ADD USERID TO DSNAME
         CLI   0(R4),C''''
         BNE   ERROR
         LA    R3,1(R3)
         SR    R4,R3                   FIND LENGTH OF DSNAME - 1
         LTR   R4,R4                   AND CHECK FOR ERRORS
         BNP   ERROR
         LR    R8,R4
         LA    R5,44
         CR    R8,R5
         BH    ERROR
         BCTR  R4,R0
         EX    R4,MOVEFQ
         B     SEARCH
USERID   L     R7,CPPLPSCB             LOAD ADDRESS OF PSCB
         SR    R8,R8
         IC    R8,7(R7)                LENGTH OF USERID
         BCTR  R8,R0
         EX    R8,MOVEU                MOVE USERID
         LA    R7,DSNAME
         AR    R7,R8
         LA    R7,1(R7)
         MVI   0(R7),C'.'
         SR    R4,R3                   DSNAME LENGTH - 1
         AR    R8,R4
         LA    R8,3(R8)
         LA    R5,44
         CR    R8,R5
         BH    ERROR
         EX    R4,MOVEDSN
         SPACE 4
SEARCH   LR    R7,R8                   CONVERT DSNAME TO
         LA    R2,DSNAME               UPPER CASE
CONV     OI    0(R2),X'40'
         LA    R2,1(R2)
         BCT   R7,CONV
         SPACE 2
         L     R2,16                   ADDRESS OF CVT
         L     R2,200(R2)              ADDRESS OF SECONDARY CVT
         L     R2,20(R2)               ADDRESS OF FIRST MAJOR QCB
QCBNEXT  LTR   R2,R2
         BZ    NOFIND
         CLC   12(8,R2),SYSDSN
         BE    FINDDSN
         L     R2,0(R2)                ADDRESS OF NEXT MAJOR QCB
         B     QCBNEXT
         SPACE 2
FINDDSN  L     R3,8(R2)                ADDRESS OF FIRST MINOR QCB
         SR    R4,R4
NEXTMIN  LTR   R3,R3
         BZ    NOFIND
         IC    R4,12(R3)               LENGTH OF QCB NAME
         CR    R4,R8
         BNE   INCR
         BCTR  R4,R0
         EX    R4,COMPDSN
         BE    GETQEL
INCR     L     R3,8(R3)                ADDRESS OF NEXT MINOR QCB
         B     NEXTMIN
         SPACE 2
GETQEL   LA    R4,58
         LA    R2,MSG
         LA    R5,MSG+6
NEXTQEL  L     R3,0(R3)                LOAD ADDRESS OF QEL
         LTR   R3,R3
         BZ    OUT
         MVI   PSW,X'00'               RESET PRINT SWITCH
         TM    4(R3),X'80'
         BO    SHR
         MVC   MSG(3),=C'OLD'
         LA    R5,MSG+6
         BAL   R6,FINDJN
PRINT    SR    R5,R2
         TPUT  (R2),(R5)
         B     RETURN
         SPACE 2
SHR      LR    R6,R5
         SR    R6,R2
         CR    R6,R4
         BL    SHR1
         BCTR  R6,R0
         TPUT  (R2),(R6)
         MVI   MSG+6,C' '
         MVC   MSG+7(63),MSG+6
         LA    R5,MSG+6
         MVI   PSW,X'FF'
SHR1     BAL   R6,FINDJN
         MVI   0(R5),C','
         LA    R5,1(R5)
         B     NEXTQEL
OUT      CLI   PSW,X'00'
         BNE   RETURN
         BCTR  R5,R0
         B     PRINT
ERROR    TPUT  ERMSG1,15
         B     RETURN
         SPACE 4
NOFIND   TPUT  ERMSG2,22
RETURN   RETURN (14,12),,RC=0
         EJECT
*        FINDJN    -    FINDS THE JOB NAME AND MOVES IT
*                       TO A SPECIFIED LOCATION
*
*        UPON ENTRY     REG. 3  -  ADDRESS OF QEL
*                       REG. 5  -  LOC. TO REC. JOB NAME OR USERID
*                       REG. 6  -  RETURN REG.
*
*        UPON EXIT      REG. 5  -  CONTAINS ADDRESS FOLLOWING
*                                  THE JOB NAME OR USERID
*
*                       REG. 7  -  WORK REG.
*                       REG. 8  -  WORK REG.
*                       REG. 9  -  WORK REG.
*                       REG. 10 -  WORK REG.
         SPACE 4
FINDJN   SR    R10,R10
         CLI   12(R3),X'00'
         BE    FJN1
         IC    R10,12(R3)              TJID
         B     GETID
         SPACE 2
FJN1     L     R7,8(R3)                LOAD TCB ADDRESS
         L     R8,180(R7)              LOAD JSCB ADDRESS
         CLI   269(R8),X'00'           CHECK FOR TSO USER
         BE    FJN2                    IN CORE
         IC    R10,269(R8)             TJID
         B     GETID
         SPACE 2
FJN2     MVC   0(2,R5),=C'J='
         LA    R5,2(R5)
         L     R7,12(R7)               LOAD TIOT ADDRESS
         MVC   0(8,R5),0(R7)           MOVE JOB NAME
GOHOME   LA    R7,8
LOOP     CLI   0(R5),C' '
         BE    0(R6)
         LA    R5,1(R5)
         BCT   R7,LOOP
         BR    R6
         SPACE 2
GETID    L     R7,16                   ADDRESS OF CVT
         L     R7,228(R7)              ADDRESS OF TSCVT
         LH    R9,26(R7)               SIZE OF TJB
         MR    R8,R10
         L     R8,0(R7)
         AR    R8,R9                   ADDRESS OF TJB FOR TJID
         MVC   0(2,R5),=C'U='
         LA    R5,2(R5)
         MVC   0(8,R5),28(R8)
         B     GOHOME
         EJECT
MOVEFQ   MVC   DSNAME(0),0(R3)
MOVEU    MVC   DSNAME(0),0(R7)
MOVEDSN  MVC   1(0,R7),0(R3)
COMPDSN  CLC   14(0,R3),DSNAME
DSNAME   DC    44C' '
MSG      DC    CL70'SHR -'
SYSDSN   DC    CL8'SYSDSN'
ERMSG1   DC    CL15'ERROR IN DSNAME'
ERMSG2   DC    CL22'DATA SET NOT ALLOCATED'
PSW      DC    X'00'
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 4
         IKJCPPL
         END
