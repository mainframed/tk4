YCCPRT   XSAVE OPT=CSECT,REEN=0
         EQUREGS
* THIS IMPLIMENTS THE EDITOR PRINT (P) COMMAND
* WRITTEN FOR OS VS2 R3.0  MAY,1975
* CODE IS REENTRANT AND ALL THAT GOOD STUFF
* TRANSFORMS THE P COMMAND INTO A L * ???? COMMAND
* AND PASSES THE NEW COMMAND BACK TO THE EDITOR
* FOR EXECUTION. USES INTERNAL EDITOR CONVENTIONS
* FOR LINKAGE AND COMMAND COMMUNICATION.
* UPON ENTRY, REGISTER 1 POINTS TO THE EDITOR
* COMMUNICATIONS AREA.
         LR    R8,R1
         USING IKJEBECA,8
* OBTAIN AND INITIALIZE AN NEW COMMAND BUFFER
         GETMAIN R,LV=16,SP=1
         ST    R1,CAPTNBFR    STORE ADDR FOR NXT CMD
         LR    R7,R1          SAVE FOR CHARACTER MOVES
         MVC   0(16,R7),INITIAL   INITIALIZE
* SET FLAG INDICATING I AM PASSING BACK A COMMAND
* IF NO OPERANDS, QUIT
         TM    CASCBFFL,CAOPERND
         BZ    RETN
* OTHERWISE WE DO A MINI PARSE.
* FFIND THE FIRST DIGIT IN THE COMMAND
*     IF THERE ARE NONE, 'L *'
*     IF IT IS 0, 'L * 100000'
*     ELSE MOVE THE REST OF THE INPUT COMMAND TO THE
*          NEW COMMAND
         L     R6,CAPTIBFR
         LH    R3,0(R6)      R3=LENGTH(COMMAND) +4
         LA    R6,4(R6)      R6-> COMMAND
         SH    R3,=H'5'
         EX    R3,TRTR       TRT OPERATION,R1-> 1ST DIGIT
         BZ    RETN          IF NO DIGITS, QUIT
         CLI   0(R1),C'0'    IF IT IS A ZERO, GO TO 100K
         BE    K100
         LA    R6,1(R6,R3)
         LA    R1,0(R1)       CLEAR HIGH BYTE
         SR    R6,R1         R6=LENGTH OF REST OF COMMAND
         CH    R6,=H'6'      OR 6, WHICHEVER IS LESS
         BL    *+8
         LA    R6,6
         BCTR  R6,0
         EX    R6,MOVER         MOVE REST OF COMMAND TO NEW COMMAND
         B     RETN
K100     MVC   8(6,R7),=C'100000'
RETN     XRETURN REEN=0,RC=0
TRTR     TRT   0(0,R6),TAB
MOVER    MVC   8(0,R7),0(R1)
INITIAL  DC    H'16',H'0',CL12'L *'
TAB      DC    240AL1(0),10AL1(4),6AL1(0)
         IKJEBECA
         END
