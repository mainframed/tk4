QTEMP    TITLE 'QUASITEMP -- DETERMINE WHETHER OR NOT A DATA SET IS ''Q*
               UASI-TEMPORARY'''
*************************************************************
*                                                           *
* MODULE NAME - QUASITMP                                    *
*                                                           *
* DESCRIPTIVE NAME - 'QUASI-TEMPORARY' DETERMINATION MODULE *
*                                                           *
* FUNCTION - GIVEN A DATA SET NAME, THIS MODULE RETURNS AN  *
*            INDICATION OF WHETHER OR NOT THE DATA SET IS   *
*            'QUASI-TEMPORARY'. SUCH DATA SETS NORMALLY ARE *
*            NOT BACKED UP AND ARE SCRATCHED IF THEY REMAIN *
*            UNREFERENCED FOR A PERIOD OF TIME (CURRENTLY   *
*            SEVEN DAYS).                                   *
*                                                           *
* AUTHOR - DAVE COLE                                        *
*                                                           *
* INPUT - R1 - --> A STANDARD PARAMETER LIST. THE FIRST AND *
*              ONLY ENTRY IN THE LIST POINTS TO THE DSNAME  *
*              TO BE TESTED.                                *
*         R13 - --> A STANDARD 72-BYTE REGISTER SAVE AREA.  *
*         R14 - --> THE RETURN ADDRESS.                     *
*         R15 - --> THIS ROUTINE'S ENTRY ADDRESS.           *
*                                                           *
* OUTPUT - R15 - CONTAINS A ZERO (0) IF THE DATA SET IS NOT *
*                QUASI-TEMPORARY; OTHERWISE, R15 CONTAINS A *
*                A FOUR (4) (I.E. IF THE DATA SET IS QUASI- *
*                TEMPORARY).                                *
*                                                           *
* ATTRIBUTES - THIS ROUTINE IS RE-ENTRANT.                  *
*                                                           *
*************************************************************
         SPACE 3
QUASITMP CSECT ,
         EJECT ,
         $DSA  ,                   DATA STORAGE AREA
DSAEND   EQU   *                   END OF STANDARD REGISTER SAVE AREA
         ORG   DSAR0               RELOCATE TO OVERLAY
DSADOTS  DC    C'..'               SCAN TERMINATERS
DSADSN   DS    CL44                DSN WORK AREA
         $TEST SIZE=(*,LE,DSAEND)  INSURE EVERYTHING FITS
         ORG   ,                   RELOCATE
DSALEN   EQU   *-DSA               DSA LENGTH
         EJECT ,
         $REGS R                   STANDARD REGISTER NAMES
         EJECT ,
         USING DSA,R13             DECLARE CALLER'S DSA BASE
         USING QUASITMP,R15        DECLARE LOCAL BASE
QUASITMP CSECT ,
         STM   R14,R15,DSAR14      STORE FOR DUMP DOCUMENTATION
*                                  PURPOSES ONLY
         MVC   DSADOTS,=C'..'      COPY SCAN TERMINATER
         L     R1,0(,R1)           --> GIVEN DSN
         MVC   DSADSN,0(R1)        COPY TO WORK AREA
         LA    R1,DSADSN+L'DSADSN  LOAD SCANNER
         SLR   R0,R0               ASSUME NOT QTEMP
         SPACE 1
BLNKLOOP BCTR  R1,0                SCAN BACK
         CLI   0(R1),C' '          STILL BLANK?
         BE    BLNKLOOP            YES, KEEP SCANNING
DOT1LOOP BCTR  R1,0                NO, SCAN BACK
         CLI   1(R1),C'.'          LAST DOT YET?
         BNE   DOT1LOOP            NO, KEEP SCANNING
         CLC   =C'TEMPLIST',2(R1)  YES, QTEMP?
         BE    QTEMP               YES
         CLC   =C'LOADLIST',2(R1)  MAYBE, QTEMP?
         BE    QTEMP               YES
         CLC   =C'LINKLIST',2(R1)  MAYBE, QTEMP?
         BE    QTEMP               YES
         CLC   =C'TESTLIST',2(R1)  MAYBE, QTEMP?
         BE    QTEMP               YES
         SPACE 1
DOT2LOOP BCTR  R1,0                MAYBE, SCAN BACK
         CLI   1(R1),C'.'          2ND-TO-LAST DOT?
         BNE   DOT2LOOP            NO, KEEP SCANNING
         CLC   =C'TEMP',2(R1)      QTEMP?
         BNE   NOTTEMP             NO, NORMAL
         SPACE 1
QTEMP    LA    R0,4                YES, SET SIGNEL
NOTTEMP  LR    R15,R0              LOAD RC FOR CALLER
         BR    R14                 RETURN TO CALLER
         DROP  ,
         SPACE 3
         LTORG ,
         SPACE 3
         END   ,
