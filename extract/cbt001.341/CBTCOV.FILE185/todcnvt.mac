* MODULE: TODCNVT
*
* FUNCTION: INTERFACE TO AMDPRSEG MODULE TO CONVERT A 64 BIT TIMESTAMP
*           OBTAINED BY STCK INTO A CHARACTER STRING GIVING YEAR,
*           JULIAN DATE AND TIME OF DAY
*
* ENTRY CONDITIONS:
*           DCL TODCNVT ENTRY(*) RETURNS(CHAR(24));
*
*   ARGUMENT CAN BE DECLARED CHAR(8) OR BIT(64) ALIGNED
*   VALUE RETURNED IS YYYYDDDHH.MM.SS.MMMMMM
*
* ATTRIBUTES: REENTRANT
*
* MACROS: PLISECT,PLIEND (ESTABLISH DSA)
 EJECT
         PRINT NOGEN
TODCNVT PLISECT DSA=DSALEN
         USING DSA,R13
         LM    R2,R3,0(R1)
         MVC   ARGL+4(4),0(R2)
         MVC   ARGL+8(4),0(R3)
         L     R2,CVTPTR
         LA    R2,CVTTZ-CVT(R2)
         ST    R2,ARGL
         LA    R1,ARGL
         LA    R2,1
         BALR  R3,0
         SR    R0,R0
         SPM   R0
* THE ENTRY CONDITIONS TO TODCNVRT ARE:
*   R1 POINTS TO AN ARGUMENT LIST WITH 3 ADCONS
*       THE FIRST ARGUMENT IS A CVTTZ VALUE FOR GMT ADJUSTMENT
*       THE SECOND ARGUMENT IS AN 8 BYTE TOD CLOCK VALUE FROM STCK
*       THE THIRD ARGUMENT IS A 24 CHARACTER RETURN AREA
*   R2 CONTAINS A F'1' MEANING 'RETURN FULL DATE'
*   R13 POINTS TO A SAVE AREA
*   THE PROGRAM MASK IS DISABLED FOR FIXED POINT OVERFLOW
         CALL  TODCNVRT
         SPM   R3
         PLIEND
DSA      DSECT
         DS    CL88
ARGL     DS    3A
DSALEN   EQU   *-DSA
         EQUREGS
         CVT   DSECT=YES
         END
