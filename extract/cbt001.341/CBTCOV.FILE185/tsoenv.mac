* PROGRAM TO DETERMINE IF THE CURRENT TASK IS RUNNING UNDER
* TSO OR BATCH.  TSOADDR WILL CONTAIN THE ADDRESS OF A BYTE
* IN WHICH THE HIGH ORDER BIT IS 1 IF THE TASK IS IN A TSO
* ADDRESS SPACE; 0 OTHERWISE.  (SEE SPL:  SUPERVISOR).
* NOTE - RUNNING THE TMP UNDER BATCH IS CONSIDERED BATCH.
* PREVIOUS VERSION OF PROGRAM SET RC=0 IF TSO; RC=4 OTHERWISE.
* CURRENT VERSION SETS A BIT FLAG TO THE VALUE OF THE BIT
* POINTED TO BY TSOADDR.
* THE FLAG IS THE 1ST BIT IN A PL/I ALIGNED BIT STRING.
* (COULD USE BIT LOC/DESC TO REFERENCE ANY BIT).
* PL/I DECLARATION:
* DCL TSOENV   EXT ENTRY RETURNS(BIT(1) ALIGNED);
* ATTRIBUTES:  REFR, REUS, RENT.
* APB  11/03/77  -  MOD  06/19/79
         SPACE 2
TSOENV   XSAVE OPT=CSECT,REEN=4
         EJECT
         REGS
         SPACE 2
         USING DSA,R13
*                                  R1 -> PARMLIST
         L     R1,0(R1)            R1 -> BIT STRING LOCATOR/DESCRIPTOR
         L     R3,0(R1)            R3 -> ADDR(BIT)
         OI    0(R3),X'80'         SET FLAG
         LA    R2,TSOADDR
         EXTRACT (R2),'S',FIELDS=(TSO)
         L     R2,TSOADDR
         TM    0(R2),X'80'
         BO    EXIT                TSO (FLAG IS SET)
         NI    0(R3),X'7F'         NOT TSO (CLEAR FLAG)
         EJECT
EXIT     XRETURN T=*,REEN=4,RC=0
         SPACE 2
DSA      DSECT
         DS    18F                 SAVEAREA
TSOADDR  DS    F
         END   TSOENV
