 TITLE 'IDIOT QSAM COPIER--PL/I CALLABLE'
*
*****JOSH AUERBACH
*****FEBRUARY 6, 1978
         PRINT NOGEN
IQCOPY1   PLIXSET   BASE=R2,LAST=R7
IQCOPY    PLIXDENT DSALEN=DSAEND-DSA
         USING DSA,RDSA
         USING IHADCB,R3
         MVC   INDCB(LINDCB),PINDCB    FILL IN DYNAMIC DCBS FROM THEIR
         MVC   OUTDCB(LOUTDCB),POUTDCB ...PROTOTYPES
         LA    R3,INDCB    R3->INDCB
         LA    R4,OUTDCB   R4->OUTDCB
         LM    R5,R7,0(R1) GET ARGUMENTS
         L     R5,0(R5)    SKIP STRING LOCATOR FOR FIRST DDNAME...
         MVC   DCBDDNAM(8),0(R5) ...AND PUT IN DCB
         L     R6,0(R6)    DO SAME FOR SECOND
         MVC   DCBDDNAM-IHADCB(8,R4),0(R6)
         OPEN  ((R3),(INPUT),(R4),(OUTPUT))
         TM    DCBOFLGS,DCBOFOPN
         BZ   OPENERR
         TM    DCBOFLGS-IHADCB(R4),DCBOFOPN
         BZ   OPENERR
*                      I. E., OPEN DATASET, CHECK FOR ERRORS.
*                      NOW, CHECK ATTRIBUTES
         TM    DCBRECFM,B'01001000'
*                      TEST FOR SPANNED RECORDS
         BO    ATTRERR
         TM    DCBRECFM-IHADCB(R4),B'01001000'
         BO    ATTRERR
         IC    R5,DCBRECFM  GET INPUT RECFM IN R5
         IC    R6,DCBRECFM-IHADCB(R4)  GET OUTPUT RECFM IN R6
         XR    R6,R5       COMPARE FOR EQUALITY....
         N     R6,=F'192'   CHECKING ONLY MAJOR RECFM BITS
         BNZ   ATTRERR
*                      NOW WE KNOW RECFMS ARE OK AND COMPATIBLE
         N     R5,=F'192'   ISOLATE MAJOR RECFM BITS
         SRL   R5,4    AND CONVERT TO BRANCH VECTOR INDEX
         B     BRNCHVEC(R5)
BRNCHVEC B     ATTRERR NO SUCH THING AS ZERO RECFM
         B     VCASE   VARIABLE RECORDS
         B     FCASE   FIXED RECORDS
*                      UNFORMATTED RECORDS
         LH    R5,DCBBLKSI  GET INPUT BLOCKSIZE
         CH    R5,DCBBLKSI-IHADCB(R4)  COMPARE TO OUTPUT BLOCKSIZE
         BH    ATTRERR                 INPUT BLKSIZE MAY NOT BE GREATER
         B     LOOP
VCASE    LH    R5,DCBLRECL  GET INPUT LRECL
         CH    R5,DCBLRECL-IHADCB(R4)  COMPARE TO OUTPUT LRECL
         BH    ATTRERR             INPUT MAY NOT BE GREATER
         B     LOOP
FCASE    LH    R5,DCBLRECL  GET INPUT LRECL
         CH    R5,DCBLRECL-IHADCB(R4)      COMPARE TO OUTPUT LRECL
         BNE   ATTRERR             MUST BE THE SAME
*
*        MAIN QSAM LOOP
*
LOOP     GET   (R3)   GET A RECORD FROM INPUT
         LR    R0,R1   PUT ITS ADDRESS IN R0 FOR PUT
         MVC   DCBLRECL-IHADCB(2,R4),DCBLRECL MOVE LRECL
         PUT   (R4)
         B     LOOP
*
*        VARIOUS EXITS
*
EOD      LA    R5,0
         STH   R5,0(R7)    RETURN ZERO TO INDICATE SUCCESS
         B     EXIT
IOERR    LA    R5,12
         STH   R5,0(R7)    RETURN 12 TO INDICATE IO ERROR
         B     EXIT
ATTRERR  LA    R5,8
         STH   R5,0(R7)    RETURN 8 TO INDICATE ATTRIBUTE ERROR
         B     EXIT
OPENERR  LA    R5,4
         STH   R5,0(R7)    RETURN 4 TO INDICATE OPEN ERROR
EXIT     CLOSE ((R3),,(R4))
         PLIXEXT   ,   RETURN TO CALLER
*
*        CONSTANTS
*
PINDCB   DCB   DSORG=PS,MACRF=GL,DDNAME=XXXXXXXX,EODAD=EOD,SYNAD=IOERR
LINDCB   EQU   *-PINDCB
POUTDCB  DCB   DSORG=PS,MACRF=PM,DDNAME=XXXXXXXX,EODAD=EOD,SYNAD=IOERR
LOUTDCB  EQU   *-POUTDCB
         LTORG
         PLIXDSA
INDCB    DS    XL(LINDCB)
OUTDCB   DS    XL(LOUTDCB)
DSAEND   EQU   *
         DCBD  DSORG=PS,DEVD=DA
         END
