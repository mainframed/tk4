CATLG    CSECT
         SAVE  (14,12),,*
         LR    R2,R1          SAVE THE PARM REG
         USING CPPL,R2        ADDRESSABILITY FOR PARMS
         LR    R12,R15        SET UP ADDRESSABILITY
         USING CATLG,R12           FOR THIS CSECT
         GETMAIN R,LV=WORKLEN GET WORKING CORE
         ST    R13,4(R1)      CHAIN
         ST    R1,8(R13)        SAVEAREAS
         LR    R13,R1         SET UP ADDRESSABILITY
         USING WORK,R13          FOR WORK CORE
*
*                   INITIALIZE WORK AREA
*
         LA    R3,PPLAREA          PARSE PARM LIST
         USING PPL,R3
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         LA    R8,ECB
         ST    R8,PPLECB
         XC    ECB,ECB
         MVC   PPLPCL,PCLADCON
         LA    R8,PARSEPDL
         ST    R8,PPLANS
         MVC   PPLCBUF,CPPLCBUF
         LA    R8,CONWORK
         ST    R8,PPLUWA
*
*                   LINK TO PARSE AND CHECK RETURN CODES
*
         LINK  EP=IKJPARS,MF=(E,(R3))
         LTR   R15,R15
         BNZ   ERROR
         EJECT
*              INITIALIZE CAMLST AND CAMLST PARMS
         MVC   CAMLST(CAMENDLN),CAMODEL  MODEL CAT CAMLST
         LA    R4,DSNAME           PARM 1
         LA    R5,CATVOL
         LA    R6,VOLIST                3
         STM   R4,R6,CAMLST+4      STORE THEM
         LA    R1,1
         STH   R1,VOLIST           VOLUME COUNTER
         SR    R1,R1
         STH   R1,VOLSEQ           ALWAYS ZERO FOR DASD
*
*                   GET PARAMETERS FROM PARSE
*
*              FIRST VOLSER AND UNIT
         L     R10,PARSEPDL        ADDRESSABILITY
         USING IKJPARMD,R10           FOR PARSE ANSWER
         L     R5,VOLSER           ADDRESS OF VOLSER
         LH    R6,VOLSER+4         LENGTH OF VOLSER
         BCTR  R6,0                MINUS 1 FOR EX
         MVC   CAMVOL,BLANKS  BLANK IT
         EX    R6,MOVEVOL          MOVE IT
         L     R5,UNIT             ADDRESS OF UNIT
         LH    R6,UNIT+4           LENGTH OF UNIT
         BCTR  R6,0                MINUS 1 FOR EX
         MVC   UNITNAME,BLANKS     BLANK ENTIRE FIELD
         EX    R6,MOVEUNIT
*
*              CONVERT UNITNAME TO DEVICE TYPE CODE FOR CAMLST
*
NOUNSPEC LA    R4,0           NUMBER OF DEVICES COUNTER
         LA    R6,DEVICES     MAXIMUM
DEVLOOP  LR    R5,R4           GET COUNTER
         MH    R5,H6             * LENGTH
         LA    R5,DEVCODES(R5)   + BASE
         CLC   UNITNAME,0(R5)  AND COMPARE TO INPUT DEVICE TYPE
         BE    DEVFOUND          IF THIS IT, GET OUT OF LOOP
         LA    R4,1(R4)        BUMP COUNTER
         CR    R4,R6           CHECK FOR MAXIMUM
         BL    DEVLOOP         NORMALLY LOOP
         LA    R15,2           *** ERROR CODE ***
         B     ERROR
DEVFOUND SLA   R4,2           FOUND THE DEVICE - * LENGTH
         LA    R4,DEVSCR(R4)  + BASE
         MVC   VOLDEVT,0(R4)  MOVE IT IN
*                  SET UP FOR CHAINING THROUGH DSNAMES
         LA    R9,DSN       ADDRESSABILITY
         USING DSNPDE,R9        FOR DSN CHAIN
NEXTDSN  L     R8,PDEDSN      ADDRESS OF DSN
         LH    R7,PDELEN1      LENGTH
         LA    R8,0(R8)       CLEAR TOP BYTE
         LA    R15,1          ERROR CODE
         LTR   R8,R8          ZERO
         BZ    ERROR          IF SO, IT'S AN ERROR
         MVI   DSNAME,C' '         BLANK
         MVC   DSNAME+1(43),DSNAME   DATA SET NAME
         SR    R6,R6          COUNTER FOR USERID PREFIX
         CLI   0(R8),C''''    IS THE FIRST CHAR A QUOTE
         BNE   NODSNBMP       IF NOT,SKIP BUMP
         LA    R8,1(R8)       SKIP THE QUOTE
NODSNBMP TM    PDEFLAG1,X'40'     IS DSN QUOTED
         BO    DSNQUOTE
*
*              GET THE USERID AND PREFIX THE DSNAME
*
         L     R5,CPPLPSCB    ADDRESS OF PSCB
         USING PSCB,R5           FOR USERID
         IC    R6,PSCBUSRL      LENGTH OF USERID
         MVC   DSNAME(7),PSCBUSER  MOVE USERID
         LA    R6,DSNAME(R6)    ADD BASE
         MVI   0(R6),C'.'     ADD PERIOD
         LA    R6,1(R6)         ITS LENGTH
         B     EXECUTE
DSNQUOTE LA    R6,DSNAME(R6)  ADD ON THE BASE
EXECUTE  BCTR  R7,0
         EX    R7,MOVEDSN
         DROP  R5
*
*              ALL SET - CATALOG IT, THEN CHECK
*
         CATALOG CAMLST
         LTR   R15,R15
         BNZ   ERROR
         TPUT  CATLOGED,9     ISSUE A MESSAGE TO TERMINAL
*
*              NOW CHAIN TO ANY MORE DATA SETS
*
CHAIN    L     R9,PDEDSNCH
         LA    R9,0(R9)
         LTR   R9,R9
         BNZ   NEXTDSN
         SR    R15,R15
TERMNATE LR    R2,R13
         L     R13,4(R13)
         STM   R15,R1,16(R13)
         FREEMAIN R,LV=WORKLEN,A=(R2)
         RETURN (14,12),T
         EJECT
ERROR    SR    R9,R9          CONVERT R15 TO HEX AND OUTPUT ERROR MSG
         MVC   MSGAREA(6),ERRORMSG
CONLOOP  LR    R10,R15        GET NUMBER
         LR    R11,R9         WHICH DIGIT
         SLL   R11,2          * 4 BITS / HEX DIGIT
         SLL   R10,0(R11)     SHIFT LEFT TO CLEAR BITS ABOVE
         SRL   R10,28         AND RIGHT TO JUSTIFY AND CLEAR BITS BELOW
         CH    R10,CONH10     0-9 OR A-F
         BL    CONNUM         0-9
         LA    R10,C'A'-10(R10)    ADD TO GET CORRECT CHARACTER
CONSTORE STC   R10,MSGAREA+6(R9)   STORE IT
         LA    R9,1(R9)       INCREMENT DIGIT COUNTER
         CH    R9,CONH8       COMPARE FOR END OF NUMBER
         BL    CONLOOP        LOOP IF NOT
         LA    R0,14          LENGTH OF MSG
         LA    R1,MSGAREA     ADDR. OF MSG
         SVC   TPUT           PUT IT OUT
         B     TERMNATE
CONNUM   LA    R10,C'0'(R10)  ADD FOR PROPER CHARACTER
         B     CONSTORE
         EJECT
*                   DEFINED CONSTANTS
CAMODEL  CAMLST CATBX,1,2,3
CAMENDLN EQU   *-CAMODEL
PCLADCON DC    V(PCL)              ADDRESS OF PARSE DEFINITION CSECT
CONH10   DC    H'10'               CONST 10 FOR ERROR HEX CONVERSION
CONH8    DC    H'8'                CONST  8 DITTO
H6       DC    H'6'
H8       DC    H'8'
H16      DC    H'16'
MOVEDSN  MVC   0(1,R6),0(R8)
MOVEUNIT MVC   UNITNAME(1),0(R5)
MOVEVOL  MVC   CAMVOL(1),0(R5)
BLANKS   DC    CL8' '
TSO      DC    CL3'TSO'
SYS3     DC    CL5'SYS3.'
INDEXERR DC    CL11'INDEX ERROR'
CATLOGED DC    CL9'CATALOGED'
ERRORMSG DC    CL6'ERROR'          FOR ERROR MSG
CATVOL   DC    C'USER01'      CATALOG VOLUME
         EJECT
*
*              DEVICE CODE TABLE - DIRECT ACCESS UNITS
*
DEVCODES DS    0F
         DC    CL6'2311'
         DC    CL6'2301'
         DC    CL6'2302'
         DC    CL6'2303'
         DC    CL6'2314'
         DC    CL6'2321'
         DC    CL6'2305-1'
         DC    CL6'2305-2'
         DC    CL6'3330'
DEVICES  EQU   9             NUMBER OF DEVICES IN TABLE
DEVSCR   DS    0F
         DC    X'30002001'
         DC    X'30402002'
         DC    X'30002004'
         DC    X'30002003'
         DC    X'30C02008'
         DC    X'30002005'
         DC    X'30002006'
         DC    X'30002007'
         DC    X'30502009'
         EJECT
PCL      IKJPARM
DSN      IKJPOSIT DSNAME,LIST,PROMPT='DATA SET NAME'
VOLSER   IKJIDENT 'VOLUME SERIAL',                                     X
               FIRST=ALPHANUM,OTHER=ALPHANUM,MAXLNTH=6,                X
               PROMPT='VOLUME SERIAL NUMBER'
UNIT     IKJIDENT 'UNIT TYPE',                                         X
               FIRST=NUMERIC,OTHER=ANY,MAXLNTH=6,                      X
               DEFAULT='3330'
         IKJENDP
         EJECT
WORK     DSECT
SAVEAREA DS    18F       ADDRESS FOR SAVE REGS
PPLAREA  DS    8F        PARSE PARM LIST AREA
MSGAREA  DS    CL16      AREA FOR OUTPUT MESSAGES
ECB      DS    F         ECB FOR THIS CP
CONWORK  DS    F         WORK AREA FOR USER PARSE
PARSEPDL DS    F         AREA FOR ADDRESS OF PDL
CAMLST   CAMLST CAT,DSNAME,0,VOLIST
DSNAME   DS    CL44
VOLIST   DS    H
VOLDEVT  DS    CL4
CAMVOL   DS    CL6
VOLSEQ   DS    H
UNITNAME DS    CL6
WORKEND  DS    0D
WORKLEN  EQU   WORKEND-WORK
         EJECT
DSNPDE   DSECT
PDEDSN   DS    A         POINTER TO DSNAME
PDELEN1  DS    H         LENGTH OF DSNAME
PDEFLAG1 DS    H         BIT 0 - PRESENT, BIT 1 - QUOTES
PDEMEM   DS    A         POINTER TO MEMBER NAME
PDELEN2  DS    H         LENGTH OF MEMBER NAME
PDEFLAG2 DS    H         BIT 0 - PRESENT
PDEPASS  DS    A         POINTER TO PASSWORD
PDEFLAG3 DS    H        BIT 0  - PRESENT
PDEDSNCH DS    A
         EJECT
         IKJPPL
         EJECT
         IKJCPPL
         EJECT
         IKJPSCB
         EJECT
TPUT     EQU   93
         YREGS
         END
