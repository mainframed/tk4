         TITLE 'IKJEFLD - LOGON PRE-PROMPT EXIT'
***********************************************************************
*                                                                     *
*        IMPORTANT! - TO CORRECTLY GENERATE IKJEFLD OBJECT CODE       *
*        YOU MUST CONCATENATE THE OUTPUT OF THE ASSEMBLIES OF         *
*        SYSTEM.SOURCLIB.ASM MEMBERS IKJEFLD,IKJEFLDP, AND IKJEFLDX.  *
*        SYSTEM.MISC.CNTL(IKJEFLD) WILL DO EXPLICITLY THIS AND        *
*        PUT OBJECT IN SYSTEM.SOURCLIB.OBJ(IKJEFLD).                  *
*                                                                     *
***********************************************************************
         SPACE 1
*FUNCTION: EXIT DESIGNED TO COUNT CURRENT USERS LOGGED ON, COMPARE
*THAT COUNT AGANIST THE CURRENT USER MAX THRESHOLD, IF COUNT IS <= THEN
*CONTINUE AS USUAL, ELSE SIGNAL "LOGON" PROCESSOR TO DISCONNECT USER.
*
*INPUT: R1->A LIST OF ADDRESSES CONSTITUING THE INTERFACE BETWEEN
*"LOGON" AND THE LOGON EXIT (IKJEFLD).
*
*MAPPINGS: CVT,ASCB,ASVT,XCVT
*
*OUTPUTS: NONE
*
*ALGORITHM: COUNT # OF TSO ADDR SPACES, ASCBTSB, COMPARE AGANIST
*
*XCVTMAXU, IF > SIGNAL LOGON PROCESSOR TO DISCONNECT
*
*PROPERTIES: REENTRANT,REFRESHABLE
*
*RESTRICTIONS: NONE
*
*BOB IRWIN, 4 MAR 77
*
         EJECT
***********************************************************************
*                                                                     *
*        CHANGES!  (AK1)                                              *
*                                                                     *
*        INTELLEGENT RECONNECT - THE ABILITY TO APPEND ' REC' TO      *
*        THE COMMAND BUFFER WHEN THE USERID HAS AN EXISING ASCB AND   *
*        A TSB WITH THE HUNG BIT ON.                                  *
*                                                                     *
*        REMEMBER THAT THIS ROUTINE IS ENTERED IN KEY 0.  WE          *
*        MODESET TO KEY 8 FOR NORMAL PROCESSING & GO BACK TO KEY 0    *
*        TO ACCESS THE TSB (FETCH PROTECTED KEY 6).                   *
*                                                                     *
*        NOTE THAT UPON RETURN FROM THE PARSE (IKJEFLDP), THERE       *
*        WILL BE A RETURN CODE IN R15 DESCRIBING KEYWORDS FOUND.      *
*        CURRENTLY RECONNECT=>4, PURGE×CLEAR=>8 THOUGH RC=8 IS NOT    *
*        IMPLEMENTED AS OF YET.                                       *
*                                                                     *
*                                          AL KARENKO   6-79          *
*                                                                     *
***********************************************************************
          SPACE 1
***********************************************************************
*                                                                     *
*        MORE CHANGES!  (AK2)                                         *
*                                                                     *
*        XASCB FIELD IS INITIALIZED (PREVIOUSLY DONE AS ZAP IN        *
*        IKJEFLE) IN ROUTINE IKJEFLDX.  ALSO TELENET VERIFICATION     *
*        DURING RECONNECT; IF HE TRIES TO RECONNECT ON TELENET        *
*        LINE AFTER BEING ON NON-TELENET LINE, WE GIVE HIM A          *
*        MESSAGE AND BLOW HIM OUT.  IF HE TRIES TO RECONNECT TO       *
*        A NON-TELENET LINE AFTER BEING ON A TELENET LINE, WE         *
*        GIVE HIM A WARNING THAT WE'LL BE CHARGING HIM AND LET        *
*        HIM SPEND HIS MONEY.                                         *
*                                                                     *
*        NOTE THAT IKJEFLDX SINCE IT IS INITIALIZING THE XASCB        *
*        MUST BE IN MODESET TO KEY 0 (WHICH IT IS).  MUST ALSO        *
*        BE KEY 0 FOR SOME OF THE TELENET TSB REFERENCES.             *
*                                                                     *
*        THE XASCB INITIALIZATION IS CURRENTLY INACTIVE; TO           *
*        ACTIVATE IT, DELETE THE BRANCH TO REST.  LOOK FOR            *
*        '** DEACTIVATE XASCB INIT **' TO FIND INSTRUCTIONS.          *
*        YOU WILL THEN NO LONGER NEED TNETINIT, THE CODE THAT         *
*        INITIALIZES JUST THE TELENET FLAG IN THE XASCB, AND          *
*        THESE 5 LINES CAN BE DELETED (SEE COMMENT).                  *
*                                                                     *
*        NOTE THAT A NEGATIVE RETURN CODE FROM THE PARSER             *
*        INDICATES A STAX/LOGOFF REQUEST AND IKJEFLD IS               *
*        EXITED IMMEDIATELY FOR LOGOFF PROCESSING.                    *
*                                                                     *
*                                           AL KARENKO   6-79         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*         YET MORE CHANGES   (AK3)                                    *
*                                                                     *
*         SINCE THE TERMINAL TABLE HAS GOTTEN MUCKED UP -             *
*         IN ORDER TO CONVERT TO R3.8 & IMPLEMENT TELENET             *
*         CHARGING, I AM GOING TO A DIRECT TSB LINE SEARCH            *
*         INSTEAD OF A TCTABL LOAD WITH SUBSEQUENT PORT,              *
*         LINE, ETC. SEARCHES.                                        *
*                                                                     *
*         ***  IMPORTANT  ***                                         *
*                                                                     *
*         THE VALID TELENET LINES AT PRESENT ARE:                     *
*           X'00DD', X'00DE', & X'00DF' -                             *
*         IF THESE ARE CHANGED, DELETED, OR ADDED, THIS ROUTINE       *
*         MUST BE MODIFIED TO REFLECT THE CHANGES.                    *
*                                                                     *
*                                               AL KARENKO  8-79      *
*                                                                     *
***********************************************************************
          SPACE 1
***********************************************************************
*                                                                     *
*         WHAT ELSE - CHANGES  (AK4)                                  *
*                                                                     *
*         LOGON PURGE IS FINALLY IMPLEMENTED - THIS ALLOWS A USER     *
*         TO CANCEL HIMSELF BY DROPPING HIS LINE AND LOGGING ON       *
*         USING THE 'PURGE' KEYWORD.  THE ACTUAL CANCEL IS DONE       *
*         BY BUILDING A 'CANCEL USER=...' BUFFER AND ISSUING          *
*         SVC 34, THEN WAITING FOR (CURRENTLY) 10 3 SECOND INTERVALS  *
*         AND CHECKING TO SEE IF THE OLD ADDRESS SPACE HAS LEFT       *
*         US - IF SO, WE LET LOGON CONTINUE; IF NOT, WE BLOW HIM      *
*         OUT AND GIVE HIM A TOUGH LUCK MESSAGE.                      *
*                                                                     *
*                                              AL KARENKO  9-79       *
*                                                                     *
***********************************************************************
 SPACE 1
***********************************************************************
*                                                                     *
*        AND I'VE SEEN CHANGES...  (AK5)                              *
*                                                                     *
*        THE (CURRENT) DEFINITIVE SOLUTION TO THE TELENET LINE        *
*        IDENTITY CRISIS IS TO CHECK TSBTRMID+5 (LENGTH OF 2) IN      *
*        THE TSB FOR 'TL'.  THIS IS ALSO EASIER TO MAINTAIN AND       *
*        THEREFORE SUPERCEDES THE AK3 CHANGES.  IT ALSO APPEARS       *
*        THAT THE ORIGINAL XASCINIT ROUTINE WILL JOIN THE DESOTO      *
*        IN THE ANNALS OF HISTORY (THOUGH THE REMNANTS STILL          *
*        REMAIN IN IKJEFLDX - THESE REMNANTS MAY HAVE SOME            *
*        AESTHETIC VALUE... IE. POMPEII).                             *
*                                                                     *
*                                             AL KARENKO  4-80        *
*                                                                     *
***********************************************************************
          EJECT
         PRINT NOGEN
         $REGS R
LINENORM EQU   X'00'                                                AK3
LINETNET EQU   X'FF'                                                AK3
IKJEFLD  CSECT                                                      AK1
         SAVE  (14,12),,*              SAVE REGS                    AK1
         USING IKJEFLD,R15             TEMP ADDRESSABILITY          AK1
         MODESET KEYADDR=KEY8,WORKREG=1 MODESET TO USER KEY         AK1
         GETMAIN R,LV=WKLEN            GET NEW SAVE AREA            AK1
         ST    R13,4(R1)               SAVE OLD PTR IN NEW AREA     AK1
         ST    R1,8(R13)               SAVE NEW PTR IN OLD AREA     AK1
         LR    R12,R1                  SAVE NEW SAVE PTR            AK1
         LM    0,1,20(R13)             RESTORE R0 & R1              AK1
         LR    R13,R12                 R13-> NEW SAVE AREA          AK1
         USING WKAREA,R13                                           AK1
         DROP  R15                     DON'T NEED R15 ANY MORE      AK1
         BALR  R12,0                   R12=BASE REG                 AK1
         USING *,R12                                                AK1
*
         LR    R9,R1              SAVE ARGUMENTS
         L     R15,PARSADDR            R15->PARSE MODULE (IKJEFLDP) AK1
         BALR  R14,R15                 PARSE INPUT BUFFER           AK1
         LTR   R15,R15                 STAX/LOGOFF?                 AK2
         BM    RET                     YES - THEN EXIT              AK2
         SR    R4,R4                   NEW USER, IN COUNT TO "1"
         LA    R4,1(,R4)
         L     R8,CVTPTR
         L     R3,CVTASVT-CVT(,R8)     R3->ASVT
         USING ASVT,R3
         L     R6,ASVTMAXU             R6<-CURR # OF ADDR SP'S
         LA    R7,ASVTENTY             R7 USED AS INDEX
*                                      INTO ASVTENTY VECTOR
         L     R5,ASVTENTY             R5->ASCB "POSSIBLY"
         USING ASCB,R5
         L     R2,8(R9)                R2->USERID DESCRIPTOR        AK1
         L     R2,0(R2)                R2->USERID (FROM PARSE)      AK1
*
*COUNT UP THE NUMBER OF TSO ADDR SPACES
*
NXTENTY  TM    0(R7),ASVTAVAL          ADDR SPACE ASSIGNED?
         BO    CHAIN                   NO, CHAIN TO NXT CANADATE
         L     R5,0(,R7)               LD BASE WITH ASCB ADDR
         CLC   ASCBTSB+1(3),=X'000000' TSO ADDR SPACE?
         BE    CHAIN                   NO, TRY NXT ENTY
         L     R10,ASCBJBNS            R10->JOBNAME (POSSIBLY)      AK1
         LTR   R10,R10                 DOES IT REALLY?              AK1
         BZ    CONT                    NO - CONTINUE                AK1
         CLC   0(7,R2),0(R10)          DOES PARSED USERID=JOBNAME?  AK1
         BE    FOUNDHIM(R15)           YES - TAKE REQUIRED ACTION   AK1
CONT     LA    R4,1(,R4)               BUMP USER CNTR
CHAIN    LA    R7,4(,R7)              POINT TO NXT ENTY
         BCT   R6,NXTENTY
         DROP  R3
         L     R3,CVTUSER-CVT(,R8)
         USING XCVT,R3                 R3->XCVT
         CH    R4,XCVTMAXU             IS CURR USER > LIMIT
*                                      SET BY OPER OR IPL
         BNH   TNETINIT                ** DEACTIVATE XASCB INIT **  AK2
         BNH   XASCBFLD                NO - INITIALIZE XASCB FLDS   AK2
*
*WRITE USER MAX EXECEEDED MSG AND SIGNAL LOGON PRC TO DISCONNECT
*
         TPUT  MSG1,44
*
*SIGNAL LOGON TO DISCONNECT USER ATTEMPTING TO LOGON, HE/SHE IS
*HAS EXCEEDED THE MAXIMUM ALLOWABLE NUMBER OF USERS
*
BLOWOUT  L     R2,0(,R9)               R9->DESCRIBTOR BLOCK
         L    R2,0(,R2)                R2->LOGON CONTROL SWITCHES
         OI    0(R2),X'10'             ARM DISCONNECT SWITCH
         SR    R15,15                  SET RET CODE AND RETURN
REST     MODESET KEYADDR=KEY0,WORKREG=1 MODESET BACK TO KEY 0       AK1
RET      SPKA  X'80'                   KEY 8 TO FREEMAIN            AK1
         LR    R11,R13                 R11->NEW SAVE AREA           AK1
         L     R13,4(R13)              R13->OLD SAVE AREA           AK1
         FREEMAIN R,LV=WKLEN,A=(R11)   FREE NEW SAVE AREA           AK1
         SPKA  X'00'                   BACK TO KEY 0 TO RETURN      AK1
         RETURN (14,12)                GO HOME (IN KEY 0)           AK1
         EJECT
FOUNDHIM B     TRYREC                  NO PARSED RECONNECT -        AK1
         B     TERMVER                 PARSED RECONNECT             AK2
         B     CANUSER                 PARSED PURGE                 AK4
         SPACE 1
TRYREC   MODESET KEYADDR=KEY0,WORKREG=1 MODESET TO KEY 0            AK1
         L     R10,ASCBTSB             R10->TSB                     AK1
         USING TSB,R10                                              AK1
         TM    TSBFLG4,TSBHUNG         IS IT A LINE DROP?           AK1
         BNO   RET                     NO - LET LOGON BLOW HIM OUT  AK1
         DROP  R10                                                  AK1
         MODESET KEYADDR=KEY8,WORKREG=1 BACK TO USER KEY            AK1
         L     R10,4(R9)               R10->BUFFER DESCRIPTOR       AK1
         LH    R11,6(R10)              R11=BUFFER LENGTH            AK1
         CH    R11,=H'248'             HAS HE USED UP HIS BUFFER?   AK1
         BH    REST                    YES - LET LOGON BLOW HIM OUT AK1
         L     R2,0(R10)               R2->BUFFER                   AK1
         AR    R2,R11                  R2->END OF BUFFER            AK1
         LA    R11,4(R11)              R11=NEW LENGTH (OLD+4)       AK1
         STH   R11,6(R10)              SAVE NEW BUFFER LENGTH       AK1
         MVC   0(4,R2),=C' REC'        APPEND ' REC'                AK1
         TPUT  MSG2,MSG2L              LET HIM KNOW WHATS HAPPENING AK1
TERMVER  LH    R10,ASCBASID            R10=ASID                     AK2
         L     R11,16                  R11->CVT                     AK2
         USING CVT,R11                                              AK2
         L     R11,CVTUSER             R11->XCVT                    AK2
         USING XCVT,R11                                             AK2
         L     R11,XCVTXASV            R11->XASVT                   AK2
         USING XASVT,R11                                            AK2
         SLL   R10,2                   SETUP R10 FOR ASID INDEX     AK2
         L     R10,XASVENTY-4(R10)     R10->XASCB OF OLD ADDR SP    AK2
         USING XASCB,R10                                            AK2
         L     R5,PSAAOLD-PSA          R5->ASCB OF LOGON            AK2
         MODESET KEYADDR=KEY0,WORKREG=1 MODESET TO KEY 0            AK2
         L     R11,ASCBTSB             R11->TSB                     AK2
         USING TSB,R11                                              AK2
         CLC   TSBTRMID+5(2),=CL2'TL'  IS HE ON A TELENET LINE?     AK5
         BE    TNETLINE                YES - MAKE VERIFICATIONS     AK3
         TM    XASCFLG1,XASTELNT       WAS HE ON TELENET?           AK2
         BNO   RET                     NO - EVERYTHING IS FINE      AK2
         B     TELNTOK                 YES - WARN HIM & LET HIM GO  AK2
TNETLINE TM    XASCFLG1,XASTELNT       WAS HE ON TELENET?           AK2
         BNO   TELNTNG                 NO - DISCREPENCY - KILL HIM  AK2
         B     RET                     YES - EVERYTHING IS FINE     AK2
         EJECT
*                                                                   AK2
*                                                                   AK2
*        THE NEXT 5 LINES ARE TEMPORARY TO INITIALIZE THE           AK2
*        TELENET FLAG BYTE IN THE XASCB - WHEN THE XASCB            AK2
*        INITIALIZATION ROUTINE IS ACTIVATED, THESE LINES           AK2
*        CAN BE DELETED (IN ADDITION THE THE 'BNH' TO IT)           AK2
*                                                                   AK2
*                                                                   AK2
TNETINIT L     R5,PSAAOLD-PSA          R5->ASCB OF LOGON            AK3
         LH    R10,ASCBASID            R10=ASID                     AK2
         L     R11,16                  R11->CVT                     AK2
         USING CVT,R11                                              AK2
         L     R11,CVTUSER             R11->XCVT                    AK2
         USING XCVT,R11                                             AK2
         L     R11,XCVTXASV            R11->XASVT                   AK2
         USING XASVT,R11                                            AK2
         SLL   R10,2                   SETUP R10 FOR ASID INDEX     AK2
         L     R10,XASVENTY-4(R10)     R10->XASCB                   AK2
         USING XASCB,R10                                            AK2
         MODESET KEYADDR=KEY0,WORKREG=1 MODESET TO KEY 0            AK2
         L     R11,ASCBTSB             R11->TSB                     AK2
         USING TSB,R11                                              AK2
         CLC   TSBTRMID+5(2),=CL2'TL'  IS HE ON A TELENET LINE?     AK5
         BNE   RET                     NO - RETURN                  AK3
         OI    XASCFLG1,XASTELNT       INITIALIZE XASCB TELENET FLD AK3
         B     RET                     RETURN                       AK3
         DROP  R5,R10,R11                                           AK3
         SPACE 1
TELNTNG  SPKA  X'80'                   GET INTO USER KEY            AK2
         TPUT  MSG3,MSG3L              LET HIM KNOW THAT HE'S       AK2
         TPUT  MSG4,MSG4L                ATTEMPTING A NO-NO...      AK2
         B     BLOWOUT                 THEN KILL HIM                AK2
         SPACE 1
TELNTOK  SPKA  X'80'                   GET INTO USER KEY            AK2
         TPUT  MSG5,MSG5L              LET HIM KNOW THAT WE'LL BE   AK2
         TPUT  MSG6,MSG6L                CHARGING HIM FOR AN ENTIRE AK2
         TPUT  MSG7,MSG7L                TELENET SESSION...         AK2
         B     RET                     LET HIM RETURN NORMALLY      AK2
         SPACE 1
XASCBFLD MODESET KEYADDR=KEY0,WORKREG=1 MODESET TO KEY 0            AK2
         L     R15,INITADDR            R15->XASCB INIT ROUTINE      AK2
         BALR  R14,R15                 INITIALIZE XASCB FIELDS      AK2
         B     RET                     GO HOME                      AK2
         EJECT
***********************************************************************
*                                                                     *
*        LOGON PURGE IMPLEMENTED HERE - NOTE THAT UPON ENTRY TO       *
*        THIS CODE THE FOLLOWING REGISTER USAGE HAS BEEN DETERMINED:  *
*                                                                     *
*            R2 -> PARSED USERID                                      *
*            R5 -> ASCB OF OLD USER (IE. USER TO BE CANCELLED)        *
*            R7 -> ASVTENTY OF OLD USER                               *
*                                                                     *
***********************************************************************
         SPACE 1
CANUSER  EQU   *
         USING ASCB,R5                 R5->OLD ASCB (SEE ABOVE)     AK4
         L     R6,ASCBTSB              R6->TSB OF OLD USER          AK4
         USING TSB,R6                                               AK4
         SPKA  X'00'                   MODESET TO KEY 0             AK4
         TM    TSBFLG4,TSBHUNG         IS OLD USER LINE DROPPED?    AK4
         BNO   INELIG                  NO - THEN IS INELIGIBLE      AK4
         SPKA  X'80'                   MODESET BACK TO KEY 8        AK4
         GETMAIN R,LV=CANBUFL          GETMAIN COMMAND BUFFER       AK4
         USING CANBUF,R1                                            AK4
         USING PSA,R0                                               AK4
         L     R3,PSAAOLD              R3->LOGON'S ASCB             AK4
         LH    R0,ASCBASID-ASCB(,R3)   R0=LOGON'S ASID              AK4
         O     R0,TSOFLAG              SET TSOUSER INDICATOR        AK4
         LR    R3,R1                   R3->BUFFER (FOR FREEMAIN)    AK4
         MVC   CANOPCMD,CANCMD         MOVE CANCEL COMMAND TO BUFFR AK4
         MVC   CANID(8),0(R2)          MOVE USERID TO COMMAND BUFFR AK4
         SPKA  X'00'                   MODESET TO KEY 0             AK4
         SVC   34                      MGCR SVC                     AK4
         SPKA  X'80'                   MODESET TO KEY 8             AK4
         LTR   R15,R15                 ZERO RETURN CODE?            AK4
         BNZ   BADRC                   NO - GIVE MSG & GIVE UP      AK4
         FREEMAIN R,LV=CANBUFL,A=(R3)  FREEMAIN COMMAND BUFFER      AK4
         DROP  R0,R1,R3,R6                                          AK4
         SPACE 1
         LA    R6,10                   DO TIMER LOOP 10 TIMES       AK4
TIMLOOP  STIMER WAIT,BINTVL=TIMINT     WAIT 3 SECONDS               AK4
         TM    0(R7),ASVTAVAL          ASCB STILL ASSIGNED?         AK4
         BO    CANNED                  NO - THEN HE WAS CANCELLED   AK4
         CLC   ASCBTSB+1(3),=X'000000' TSO ADDRESS SPACE? (ALA RTI) AK4
         BE    CANNED                  NO - THEN HE WAS CANCELLED   AK4
         L     R10,ASCBJBNS            R10->JOBNAME (POSSIBLY)      AK4
         LTR   R10,R10                 DOES IT REALLY?              AK4
         BNP   CANNED                  NO - THEN HE WAS CANCELLED   AK4
         CLC   0(7,R2),0(R10)          DOES USERID = OLD JOBNAME?   AK4
         BNE   CANNED                  NO - THEN HE WAS CANCELLED   AK4
         TPUT  BEEP,1,CONTROL          GIVE HIM A BEEP              AK4
         BCT   R6,TIMLOOP              DO TIMLOOP 10 TIMES          AK4
         TPUT  CANNG,CANNGL            GIVE UNABLE TO CANCEL MSG    AK4
         B     BLOWOUT                 GIVE UP                      AK4
         SPACE 1
BADRC    CVD   R15,RCPACKED            CONVERT RC TO PACKED         AK4
         UNPK  RCZONED,RCPACKED        UNPACK RETURN CODE           AK4
         OI    RCZONED+15,X'F0'        REMOVE ZONED SIGN            AK4
         FREEMAIN R,LV=CANBUFL,A=(R3)  FREEMAIN COMMAND BUFFER      AK4
         MVC   BADCAN+24(2),RCZONED+14 MOVE RETURN CODE TO MSG      AK4
         TPUT  BADCAN,BADCANL          GIVE BAD CANCEL MSG (RC)     AK4
         B     BLOWOUT                 GIVE UP                      AK4
         SPACE 1
INELIG   SPKA  X'80'                   GET INTO USER KEY            AK4
         TPUT  CANINEL,CANINELL        GIVE INELIGIBLE MSG          AK4
         B     BLOWOUT                 GIVE UP                      AK4
         SPACE 1
CANNED   TPUT  CANOK,CANOKL            GIVE CANCELLED MSG           AK4
         B     TNETINIT                ** DEACTIVATE XASCB INIT **  AK4
         B     XASCBFLD                CHECK FOR TELENET LINES      AK4
         DROP  R5                                                   AK4
         EJECT
*
*
*DATA AREAS
*
*
*
PARSADDR DC    V(IKJEFLDP)                                          AK1
INITADDR DC    V(IKJEFLDX)                                          AK2
TSOFLAG  DC    X'00008000'                                          AK4
TIMINT   DC    F'300'                                               AK4
CANCMD   DC    Y(CANCMDL)              COMMAND LENGTH               AK4
         DC    H'0'                                                 AK4
         DC    C'CANCEL U=XXXXXXXX'                                 AK4
CANCMDL  EQU   *-CANCMD                                             AK4
BEEP     DC    X'2F'                                                AK4
KEY0     DC    X'00'                                                AK1
KEY8     DC    X'80'                                                AK1
TELNT1   DC    XL2'00DD'                                            AK3
TELNT2   DC    XL2'00DE'                                            AK3
TELNT3   DC    XL2'00DF'                                            AK3
MSG1     DC    CL44'MAXIMUM ALLOWABLE USERS LOGGED ON, TRY LATER'
MSG2     DC    C'ATTEMPTING RECONNECT...'                           AK1
MSG2L    EQU   *-MSG2                                               AK1
MSG3     DC    C'ORIGINAL LOGON WAS NOT VIA TELENET LINE...'        AK2
MSG3L    EQU   *-MSG3                                               AK2
MSG4     DC    C'LOGON RECONNECT VIA TELENET NOT PERMITTED!'        AK2
MSG4L    EQU   *-MSG4                                               AK2
MSG5     DC    C'ORIGINAL LOGON WAS MADE VIA TELENET LINE...'       AK2
MSG5L    EQU   *-MSG5                                               AK2
MSG6     DC    C'LOGON RECONNECT WILL BE PERMITTED, BUT'            AK2
MSG6L    EQU   *-MSG6                                               AK2
MSG7     DC    C'ENTIRE SESSION WILL BE CHARGED FOR TELENET ACCESS' AK2
MSG7L    EQU   *-MSG7                                               AK2
CANOK    DC    C'CANCEL SUCCESSFUL - LOGON PROCEEDING...'           AK4
CANOKL   EQU   *-CANOK                                              AK4
CANNG    DC    C'UNABLE TO CANCEL - CONTACT OPERATIONS FOR ASSISTANCE'
CANNGL   EQU   *-CANNG                                              AK4
CANINEL  DC    C'INELIGIBLE TO USE LOGON PURGE'                     AK4
CANINELL EQU   *-CANINEL                                            AK4
BADCAN   DC    C'CANCEL NOT ACCEPTED (RC=XX)'                       AK4
BADCANL  EQU   *-BADCAN                                             AK4
         LTORG                         FOR ADDRESSABILITY
*                                      WITH R5 START LIT POOL HERE
         SPACE 1
WKAREA   DSECT                                                      AK2
         DS    18F                     REGISTER SAVE AREA           AK2
RCPACKED DS    PL8                     RETURN CODE (PACKED)         AK4
RCZONED  DS    ZL16                    RETURN CODE (ZONED)          AK4
WKLEN    EQU   *-WKAREA                WORK AREA LENGTH             AK2
         SPACE 1
CANBUF   DSECT                                                      AK4
CANOPCMD DS    CL(CANCMDL-8)           CANCEL COMMAND               AK4
CANID    DS    CL8                     CANCEL USERID                AK4
CANBUFL  EQU   *-CANBUF                                             AK4
         SPACE 1
*
*
         IHAASVT DSECT=YES
         IHAASCB DSECT=YES
         IKJTSB LIST=YES                                            AK1
         XASVT DSECT=YES,LIST=YES                                   AK2
         XASCB DSECT=YES,LIST=YES                                   AK2
         XCVT  LIST=YES,DSECT=YES
         CVT   DSECT=YES,LIST=YES
         IHAPSA
*
*
         END   IKJEFLD
