PFTSRCH  CSECT
         YREGS
PFTSRCH  $ENTER
         DEBUG
         L     R1,0(,R1)           -->PARM
         AH    R1,2(,R1)           ADD CURRENT OFFSET
         LA    R1,4(,R1)           ACCOUNT FOR PREFIX
TESTBLNK CLI   0(R1),C' '          BLANK?
         BNE   OKOK                OK, OK
         LA    R1,1(,R1)           INCREMENT
         B     TESTBLNK            TEST AGAIN
OKOK     NC    0(8,R1),KNOCK1      CONVERT TO 5 BIT CRAPSIDIC
         TR    0(8,R1),T1          TRANSLATE TO ZONED HEX
         PACK  PACKER(5),0(9,R1)   PACK
         MVC   MATCHER,PACKER      MOVE
         L     R1,CVTPTR
         L     R1,CVTPVTP-CVT(,R1) --> PVT
         L     R2,PVTPFTP-PVT(,R1) --> PFTE APPARENT ORIGIN
         SLR   R3,R3
         ICM   R3,3,PVTFPFN-PVT(R1) HAVE RBN OF FIRST PFTE
         SLR   R5,R5
         ICM   R5,3,PVTLPFN-PVT(R1) HAVE RBN OF LAST PFTE
         ALR   R3,R2               --> FIRST PFTE
         LA    R4,16               FOR BXLE
         ALR   R5,R2               --> LAST PFTE
LOOP     CLC   MATCHER,0(R3)       TEST PFTE
         BE    FOUND
         BXLE  R3,R4,LOOP
         TPUT  NF,L'NF
         B     FINI
FOUND    SLR   R3,R2               BACK TO RBN
         SLL   R3,8                LOOK LIKE ADDRESS
         ST    R3,PACKER
         UNPK  HEXWK(9),PACKER(5)  UNPACK
         NC    HEXWK(8),KNOCK2     CONVERT TO 4 BIT CRAPSIDIC
         TR    HEXWK(8),T2         CONVERT TO EBCDIC
         TPUT  HEXWK,8             REPORT
FINI     $EXIT ((14,12)),RC=0
KNOCK1   DC    X'1F1F1F1F1F1F1F1F'
T1       DC    X'F0FAFBFCFDFEFF',C'000000000'
         DC    C'0123456789000000'
NF       DC    C'REAL ADDRESS NOT FOUND'
PACKER   DS    5C
MATCHER  DS    CL4
KNOCK2   DC    X'0F0F0F0F0F0F0F0F'
T2       DC    C'0123456789ABCDEF'
HEXWK    DS    9C
         LTORG
         PRINT NOGEN
         CVT   DSECT=YES,LIST=YES
         IHAPVT
         END
