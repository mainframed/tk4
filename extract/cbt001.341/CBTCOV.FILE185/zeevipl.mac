YIPL     TITLE 'ZEEVIPL-- A YALE PREFIX ROUTINE FOR IEEVIPL'
***********************************************************************
*                                                                     *
*        NAME:     ZEEVIPL                                            *
*                                                                     *
*        FUNCTION: THIS MODULE IS CALLED AT IPL TIME TO               *
*                  INITIALIZE CERTAIN INSTALLATION CREATED CONTROL    *
*                  BLOCKS--                                           *
*                     XCVT (EXTENDED CVT)                             *
*                     XASVT (EXTENDED ASVT)                           *
*                     XASCB (EXTENDED ASCB) FOR ANY ADDRESS SPACES    *
*                       ALREADY CREATED.                              *
*                                                                     *
*        ENTRY:    THIS MODULE IS LINKEDITED "IN FRONT" OF THE        *
*                  MASTER SCHEDULER INITIALIZATION MODULE (IEEVIPL),  *
*                  THUS IS ENTERED AT IPL TIME FROM THE NIP EXIT      *
*                  PHASE.  THERE IS NO ENTRY DATA.                    *
*                                                                     *
*        EXIT:     EXIT IS MADE TO IEEVIPL.  NOTHING IS PASSED        *
*                  TO IT.  THERE IS CURRENTLY NO PROVISION FOR        *
*                  ABNORMAL EXITS.                                    *
*                                                                     *
*        SYSTEM LEVEL:  VS2, R2.0                                     *
*                                                                     *
*        VERSION:  1.0   AUTHOR:  T. DAHLQUIST   DATE:  7/17/74       *
*                                                                     *
***********************************************************************
         PRINT NOGEN
         INITIAL
         EJECT
IEEVIPL  CSECT
         YREGS
         SPACE 1
RBASE    EQU   R12
RCVT     EQU   R11
RXCVT    EQU   R10
RASVT    EQU   R9
RXASVT   EQU   RCVT
RXASCB   EQU   R8
RASCB    EQU   RXCVT
         BALR  RBASE,0
         USING *,RBASE
         SPACE 2
         DO    INITIALIZE XCVT
         L     RCVT,CVTPTR    -> CVT
         USING CVT,RCVT
         L     R0,XCVTGET     SP 245, L(XCVT)
         GETMAIN R,LV=(0)
         LR    RXCVT,R1
         USING XCVT,RXCVT
         ST    RXCVT,CVTUSER  PUT POINTER INTO CVT
         LR    R2,RXCVT       SETUP FOR CLEAR
         LA    R3,XCVTL
         SR    R4,R4
         LR    R5,R4
         MVCL  R2,R4          CLEAR
         MVC   XCVTXCVT,=C'XCVT'
         OD    XCVT SETUP
         SPACE 2
         DO    INITIALIZE XASVT
         L     RASVT,CVTASVT  ->ASVT
         USING ASVT,RASVT
         L     R7,ASVTMAXU MAX NO. OF ADDRESS SPACES
         LR    R3,R7
         SLA   R3,2           CONVERT TO FULLWORDS
         LA    R3,XASVENTY-XASVXASV(,R3) ADD IN LENGTH OF HEADER
         LR    R0,R3          FOR GETMAIN
         ICM   R0,B'1000',=AL1(245) SUBPOOL
         GETMAIN R,LV=(0)
         LR    R2,R1
         LR    RXASVT,R1      SAVE
         USING XASVT,RXASVT
         MVCL  R2,R4          CLEAR XASVT
         MVC   XASVXASV,=C'XASV'
         ST    R7,XASVMAXU
         ST    RXASVT,XCVTXASV     PUT ADDRESS IN XCVT
         OD    XASVT INITIALIZED.
         SPACE 2
         DO    CONSTRUCT XASCB'S FOR ANY EXTANT ADDRESS SPACES
         SR    R6,R6          USE R6 FOR AN INDEX.
         WHILE GO UP ASVT UNTIL A NOT IN USE ENTRY FOUND.
         DO CHECK NEXT ENTRY
         L     RASCB,ASVTENTY(R6)
         USING ASCB,RASCB
         LTR   RASCB,RASCB
         OD    READY FOR WTEST
         WTEST *,NMI
         DO    GET AND CLEAR AN XASCB.
         L     R0,XASCBGET
         GETMAIN R,LV=(0)
         LR    RXASCB,R1
         USING XASCB,RXASCB
         LR    R2,R1
         LA    R3,XASCBL
         MVCL  R2,R4          CLEAR
         ST    RXASCB,XASVENTY(R6) -> XASCB INTO XASVT
         OD    XASCB GOT.
         DO    INITIALIZE THE XASCB.
         MVC   XASCXASC,=C'XASC'    ACRONYM
         MVC   XASCASID,ASCBASID
         ST    RASCB,XASCASCB -> ASCB
         MVC   XASCPROJ,=CL8'YCC001'
         MVC   XASCUID,=CL8'SYSTEM'
         OD    XASCB INITIALIZED.
         LA    R6,4(,R6)      INCREMENT
         ELIHW ON ASVT LOOP.
         OD    XASCBS CREATED.
         SPACE 2
         DO    EXIT BY CALLING THE REAL IEEVIPL--AS YEEVIPL
         L     R15,=V(YEEVIPL)
         BR    R15
         OD
         FINAL
         TITLE 'CONSTANTS'
XCVTGET  DS    0F
         DC    AL1(245),AL3(XCVTL)
XASCBGET DS    0F
         DC    AL1(245),AL3(XASCBL)
         SPACE 3
         LTORG
         TITLE 'DSECTS'
         PRINT GEN
         CVT   DSECT=YES
         XCVT
         IHAASVT
         XASVT
         IHAASCB
         XASCB
         END
