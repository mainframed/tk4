BELIKE   CSECT
         SAVE  (14,12),,*
         YREGS
         CNOP  0,4
         BAL   R14,START-BELIKE(,R15)
SAVEAREA DS    18F
START    ST    R14,8(,R13)
         ST    R13,4(,R14)
         LR    R13,R14
         USING SAVEAREA,R13
         SPACE 3
         PRINT NOGEN
         IKJPPL
PPLEN    EQU   *-PPL
         IKJCPPL
         DCBD DSORG=PS,DEVD=DA
         CVT   DSECT=YES
         PRINT GEN
BELIKE   CSECT
         LA    R2,PPLSTOR
         USING PPL,R2
         USING CPPL,R1
         MVC   PPLCBUF,CPPLCBUF
         MVC   PPLUPT,CPPLUPT
         MVC   PPLECT,CPPLECT
         MVC   PPLPCL,=A(PCL)
         LA    R0,ANS
         ST    R0,PPLANS
         LA    R0,ECB
         ST    R0,PPLECB
         LR    R1,R2
         CALLTSSR EP=IKJPARS
         LTR   R15,R15
         BNE   PARSFAIL
         L     R2,ANS
         USING PARMS,R2
         L     R1,DDGIVEN
         LH    R3,DDGIVEN+4
         BCTR  R3,0
         MVC   DDNAME(0),0(R1)
         EX    R3,*-6
         L     R1,DSGIVEN
         LH    R3,DSGIVEN+4
         BCTR  R3,0
         MVC   DSNAME(0),0(R1)
         EX    R3,*-6
         IKJRLSA ANS
         LA    R1,DYNPARMS
         DYNALLOC
         LA    R2,DCB1             ASSUME DSORG=PS
         TM    DSORG,X'02'         PARTITIONED?
         BZ    ISPS                NO, PROCEED
         LA    R2,DCB2             YES, CHANGE
ISPS     DS    0H
         USING IHADCB,R2
         MVC   DCBDDNAM,DDNAME
         OPEN  ((R2))
         TM    DCBOFLGS,DCBOFOPN
         BNO   OPENFAIL
         CLOSE ((R2))
         B     EXIT
PARSFAIL TPUT  PFAIL,L'PFAIL
         B     EXIT
OPENFAIL TPUT  OFAIL,L'OFAIL
EXIT     L     R13,4(,R13)
         RETURN (14,12),T,RC=0
         LTORG
PPLSTOR  DS    0F
         DS    XL(PPLEN)
ANS      DS    A
ECB      DC    F'0'
DYNPARMS DS    0F
         DC    X'80'
         DC    AL3(RB)
RB       DS    0F
         DC    AL1(20),X'01',H'0',F'0'
         DC    A(TXTPTRS)
         DC    2F'0'
TXTPTRS  CALL  ,(DS,DD,SHR,PERM,RETDS),VL,MF=L
DS       DC    Y(2,1,44)
DSNAME   DC    CL44' '
DD       DC    Y(1,1,8)
DDNAME   DC    CL8' '
SHR      DC    Y(4,1,1),X'08'
PERM     DC    X'00520000'
RETDS    DC    X'005700010002'
DSORG    DS    XL2
DCB1     DCB   DDNAME=X,DSORG=PS,MACRF=E
DCB2     DCB   DDNAME=X,DSORG=PO,MACRF=R
PCL      IKJPARM DSECT=PARMS
DDGIVEN  IKJPOSIT DSNAME,DDNAM,PROMPT='FILE (DD) NAME'
DSGIVEN  IKJPOSIT DSNAME,USID,PROMPT='DATASET NAME'
         IKJENDP
PFAIL    DC    C'COULD NOT PARSE GIVEN COMMAND LINE'
OFAIL    DC    C'COULD NOT OPEN SPECIFIED DATASET'
         END
