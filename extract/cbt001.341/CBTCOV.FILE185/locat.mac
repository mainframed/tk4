LOCAT    TITLE 'PL/I ROUTINE TO SEARCH THE CATALOG FOR A DATASET ENTRY'
* TO USE:  CALL LOCAT(DSN,LWORK,RC)
* DSN   - 44 BYTE FULLY QUALIFIED DATASET NAME
* LWORK - 265 BYTE WORK AREA FOR LOCATE
*         (WILL CONTAIN THE VOLSER OF THE DATASET IF FOUND)
* RC    - HALFWORD RETURN CODE (FROM LOCATE)
*         (WE USE RC = 2 TO SIGNIFY THAT DSN IS AN ALIAS)
* SEE SPL:  DATA MANAGEMENT FOR INFO ON LOCATE WORK AREA & RC'S
* ATTRIBUTES:  REFRESHABLE, REUSABLE, REENTRANT
* APB - 08/30/79
         SPACE 1
LOCAT1   PLIXSET
         EJECT
LOCAT    PLIXDENT DSALEN=DSALEN
         USING DSA,RDSA
         EJECT
         LM    R2,R4,0(R1)         GET PARMS
         MVC   CAMLIST,PROTOCAM    REENTRANT COPY
         MVC   CAM+4(4),0(R2)      ADDR(DSNAME) - FROM LOCATOR
         MVC   CAM+12(4),0(R3)     ADDR(WORKAREA) - FROM LOCATOR
         L     R2,0(R2)            -> DSN
         MVC   ODSN,0(R2)          SAVE FOR ALIAS CHECK
         LOCATE CAM                LOCATE THE DATASET
         LTR   R15,R15             FOUND IT?
         BNZ   EXIT                NO, HEAD OUT
         CLC   ODSN,0(R2)          YES, WAS DSN CHANGED (ALIAS)?
         BE    EXIT                NO, ALL SET
         LA    R15,2               YES, DSN IS AN ALIAS, SET RC = 2
*                                  NOTE DSN IS NOW THE REAL ITEM
         SPACE 1
EXIT     STH   R15,0(R4)           SET RETURN CODE
         PLIXEXT                   RETURN
         SPACE 2
PROTOCAM CAMLST NAME,0,,0          PROTOTYPE FOR CAM
         SPACE 1
         LTORG
         EJECT
DSA      PLIXDSA
CAM      CAMLST NAME,0,,0          CAM FOR LOCATE
CAMLIST  EQU   CAM,*-CAM
ODSN     DS    CL44                DSN BEFORE LOCATE
DSALEN   EQU   *-DSA
         END   LOCAT
