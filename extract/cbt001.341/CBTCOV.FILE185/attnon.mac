ATTNON   CSECT ,
******************************************************************
*                                                                *
*         ATTNON: A FORTRAN SUBROUTINE TO TRAP ATTENTIONS      *
*        ATTNOF: SHUTOFF ATTN TRAP                             *
*                                                                *
*         INPUT: R1 -> A PARAMETER LIST OF TWO VARIABLES,        *
*         A COUNT INTEGER AND A CHAR STRING.                     *
*         THE COUNT IS USED AS A TEST VARIABLE BY THE CALLING PGM*
*         AND THE CHAR STRING IS ECHOED BACK TO USER WHEN HE     *
*         STRIKES THE ATTN KEY.                                  *
*                                                                *
*         OUTPUT: NONE.                                          *
*         EXITS: NONE.                                           *
*                                                                *
*        ATTNON:  THIS ENTRY SETS UP AN ATTN EXIT                *
*                                                                *
*        INPUT: 1). COUNT                                        *
*              2). OUTPUT STRING FOR DISPLAYING ON ATTN          *
*              3). LENGTH OF OUTPUT STRING                       *
*                                                                *
*              OUTPUTS: NONE                                     *
*                                                                *
*        ON ATTN ATTNEXT IS ENTERED AND THE COUNTER IS BUMPED    *
*        BY ONE. IT IS THE RESPONSIBILITY OF THE CALLING PGM     *
*        TO TEST COUNT AND TAKE WHATEVER ACTION NECESSARY WHEN   *
*        COUNT GOES GREATER THAN ZERO. THE CALLING PGM MUST SET  *
*        COUNT TO ZERO ON ENTRANCE TO ATTNEXT AND RESET IT AFTER *
*        ITS USE.                                                *
*                                                                *
*                                                                *
*         RTI JULY 77                                            *
******************************************************************
         SPACE 2
         $REGS R
         SAVE  (14,12),,*      SAVE REGS
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         LM    R2,R4,0(R1)        R2->COUNT, R3->OUTBUF, R4->LEN(OBUF)
         L     R4,0(,R4)           GET LENGTH
         STAX  ATTNCT,OBUF=((3),(4)),USADDR=(2)
         L     R13,SAVEAREA+4
         RETURN (14,12)
         SPACE 3
*ENTER HERE WHEN USER WISHES TO SHUT OFF ATTENTION TRAPING
         SPACE
*SHUT DOWN ATTENTION TRAPPING FUNCTION
         SPACE
         ENTRY ATTNOF
ATTNOF   SAVE  (14,12),,*          SAVE REGS
         BALR  R12,0               GET ADDRESSIBILITY
         USING *,R12
         ST    R13,SAVEAREA+4      SAVE SAVEAREA
         LA    R13,SAVEAREA        GET SAVE AREA
         STAX
         L     R13,SAVEAREA+4
         RETURN (14,12)                       RETURN CONTROL TO FORTRAN
         SPACE
ATTNCT   DS    0H                  ATTN ENTRY
         USING *,R15
         SAVE  (14,12)
         L     R6,8(,R1)           GET ADDRESS OF SAVE AREA
         LA    R5,1                GET ONE
         A     R5,0(,R6)          BUMP COUNTER
         ST    R5,0(,R6)          LOAD UPDATED VALUE WITH COUNTER VAR
         RETURN (14,12)
         SPACE
SAVEAREA DS    18F                 SAVE AREA
         END
