TERMIO   TITLE 'PL/I CALLABLE ROUTINES TO DO TERMINAL I/O'
* TERMIO:  PL/I CALLABLE ROUTINES TO DO TERMINAL I/O.
* ON ENTRY R1 POINTS TO A PARAMETER LIST WHICH CONTAINS ONE OR
* TWO ADDRESS(ES).  THE FIRST ADDRESS IS OF AN 8 BYTE PL/I STRING
* LOCATOR/DESCRIPTOR:  (SEE PL/I EXECUTION LOGIC MANUAL)
*        BYTES 0-3  CONTAIN THE STRING ADDRESS
*        BYTES 4-5  CONTAIN THE STRING LENGTH
*                   (OR THE MAXIMUM LENGTH IF THE STRING IS VARYING)
*        BYTE  6    BIT 0 IS '1' IF THE STRING IS OF VARYING LENGTH
* IF THE STRING IS VARYING, THEN THE 1ST TWO BYTES OF THE STRING ARE
* A PREFIX GIVING THE CURRENT ALLOCATED LENGTH.
* IF A FIXED STRING IS SHORTER THAN ITS ALLOCATED LENGTH, IT IS
* PADDED WITH BLANKS (OF COURSE, VARYING STRINGS NEED NO PADDING).
* THE OPTIONAL SECOND PARAMETER IS THE ADDRESS OF A HALFWORD INTO
* WHICH THE RETURN CODE FROM THE TPUT/TGET SVC MAY BE PLACED.
* RETURN CODES (TPUT):
*        R15 = 0  => OK
*            = 4  => NOWAIT SPECIFIED BUT NO OUTPUT BUFFER AVAILABLE
*            = 8  => ATTENTION INTERRUPT
*            = 12 => ASID SPECIFIED BUT USER HAS SPECIFIED NO MESSAGES
*            = 16 => INVALID PARAMETERS
*            = 20 => TERMINAL DISCONNECTED
* RETURN CODES (TGET):
*        R15 = 0  => OK
*            = 4  => NOWAIT SPECIFIED BUT NO INPUT BUFFER AVAILABLE
*            = 8  => ATTENTION INTERRUPT
*            = 12 => INPUT BUFFER IS TOO SMALL, SUBSEQUENT TGETS
*                    GET THE REST OF THE LINE
*            = 16 => INVALID PARAMETERS
*            = 20 => TERMINAL DISCONNECTED
*        R1  = LENGTH OF THE INPUT LINE
* SEE OS/VS2 GUIDE TO WRITING A TMP OR CP FOR FURTHER INFORMATION.
* THE NUMBER OF PARAMETERS PASSED CAN BE DETERMINED BY THE FACT THAT
* THE ADDRESS OF THE LAST PARAMETER CONTAINS A X'80' IN THE HIGH
* ORDER BYTE.
* ENTRY NAMES:
*        TGET - SPECIFIES INPUT
*        TPUT - SPECIFIES OUTPUT
*        F    - SPECIFIES FIXED STRING
*        V    - SPECIFIES VARYING STRING
*        A    - SPECIFIES ASIS MODE
*        C    - SPECIFIES CONTROL MODE
*        W    - SPECIFIES NOWAIT (TGET)
*        H    - SPECIFIES HOLD   (TPUT)
*        R    - SPECIFIES RETURN CODE REQUESTED
* IF ASIS OR CONTROL IS NOT SPECIFIED THEN EDIT MODE IS THE DEFAULT.
* THE CHOICE OF THE FIXED/VARYING ENTRIES AND THE NUMBER OR PARMS
* IS DETERMINED GENERICALLY BY PL/I.
* ATTRIBUTES:  REUSABLE, REFRESHABLE, REENTRANT.
* APB - 08/30/77
         EJECT
* TPUT HOLD ADDED - 10/11/79 - APB
* POTENTIAL MODS:
* DETERMINE WHETHER FIXED OR VARYING STRING VIA DESCRIPTOR,
* AND HAVE GETSTR MACRO DO ALL SETUPS (SAVES # EXTERNAL ENTRIES,
* AND CAN THEN GET RID OF ALL "R" ENTRIES - DO NOT NEED GENERIC
* SELECTION).
* CONSOLIDATE WITH TSEND (TPUT USERID).
         EJECT
         MACRO
         GETSTR
.* SETUP REGISTERS FOR A FIXED STRING INPUT
.*                               R1 -> PARAMETER LIST
         LR    PARMPTR,R1        -> PARAMETER LIST
         L     LDPTR,0(PARMPTR)  -> LOCATOR/DESCRIPTOR
         L     STRPTR,0(LDPTR)   -> STRING
         LR    TXTPTR,STRPTR     -> STRING
         LH    LENGTH,4(LDPTR)   = LENGTH
         MEND
         SPACE 3
         MACRO
         CLRSTR
.* PAD THE STRING WITH BLANKS IN CASE THE TERMINAL INPUT IS SHORTER
.* THAN THE STRING LENGTH (PL/I STRINGS ARE PADDED WITH BLANKS).
* NOTE THAT THIS WILL DESTROY THE CONTENTS OF LDPTR (R6).
         LR    R6,TXTPTR         STRING ADDRESS
         LR    R7,LENGTH         STRING LENGTH
         L     R9,PAD            SET PAD CHAR AND 0 LENGTH
         MVCL  R6,R8             MOVE PAD CHAR INTO STRING
         SPACE 1
         MEND
         SPACE 3
         MACRO
         REXIT
.* OPTIONALLY SET RETURN CODE AND EXIT
         TM    0(PARMPTR),X'80'  RETCODE REQUESTED?
         BO    EXIT              NO, EXIT
         L     R1,4(PARMPTR)     YES, GET ADDR OF HALFWORD
         STH   R15,0(R1)         SET RETURN CODE
         B     EXIT              EXIT
         MEND
         EJECT
TERMIO   PLIXSET
         SPACE 3
TXTPTR   EQU   R2                -> 1ST BYTE OF TEXT
STRPTR   EQU   R3                -> STRING
LENGTH   EQU   R4                =  CURRENT TEXT LENGTH
PARMPTR  EQU   R5                -> PARAMETER LIST
LDPTR    EQU   R6                -> LOCATOR/DESCRIPTOR
         EJECT
* INPUT A FIXED LENGTH CHARACTER STRING
TGETF    PLIXDENT
         SPACE 1
         ENTRY TGETFR
TGETFR   EQU   TGETF
         EJECT
         GETSTR
* NOTE - EDIT MODE TGET WILL PAD THE STRING FOR US (YAY)
         TGET  (2),(4)
         REXIT
         DROP  R11
         EJECT
* INPUT A VARYING LENGTH CHARACTER STRING
TGETV    PLIXDENT
         SPACE 1
         ENTRY TGETVR
TGETVR   EQU   TGETV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         TGET  (2),(4)
         STH   R1,0(STRPTR)      STORE CURRENT LENGTH IN PREFIX
         REXIT
         DROP  R11
         EJECT
* INPUT A FIXED LENGTH CHARACTER STRING IN ASIS MODE
TGETAF   PLIXDENT
         SPACE 1
         ENTRY TGETAFR
TGETAFR  EQU   TGETAF
         EJECT
         GETSTR
         CLRSTR                  PAD STRING WITH BLANKS
         TGET  (2),(4),ASIS
         REXIT
         DROP  R11
         EJECT
* INPUT A VARYING LENGTH CHARACTER STRING IN ASIS MODE
TGETAV   PLIXDENT
         SPACE 1
         ENTRY TGETAVR
TGETAVR  EQU   TGETAV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         TGET  (2),(4),ASIS
         STH   R1,0(STRPTR)      STORE CURRENT LENGTH IN PREFIX
         REXIT
         DROP  R11
         EJECT
* INPUT A FIXED LENGTH CHARACTER STRING WITH NOWAIT (EDIT MODE)
TGETWF   PLIXDENT
         SPACE 1
         ENTRY TGETWFR
TGETWFR  EQU   TGETWF
         EJECT
         GETSTR
* NOTE - EDIT MODE TGET WILL PAD THE STRING FOR US (YAY)
         TGET  (2),(4),NOWAIT
         REXIT
         DROP  R11
         EJECT
* INPUT A VARYING LENGTH CHARACTER STRING WITH NOWAIT (EDIT MODE)
TGETWV   PLIXDENT
         SPACE 1
         ENTRY TGETWVR
TGETWVR  EQU   TGETWV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         TGET  (2),(4),NOWAIT
         STH   R1,0(STRPTR)      STORE CURRENT LENGTH IN PREFIX
         REXIT
         DROP  R11
         EJECT
* OUTPUT A FIXED LENGTH CHARACTER STRING
TPUTF    PLIXDENT
         SPACE 1
         ENTRY TPUTFR
TPUTFR   EQU   TPUTF
         EJECT
         GETSTR
         TPUT  (2),(4)
         REXIT
         DROP  R11
         EJECT
* OUTPUT A VARYING LENGTH CHARACTER STRING
TPUTV    PLIXDENT
         SPACE 1
         ENTRY TPUTVR
TPUTVR   EQU   TPUTV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         LH    LENGTH,0(STRPTR)  SET TO CURRENT LENGTH
         TPUT  (2),(4)
         REXIT
         DROP  R11
         EJECT
* OUTPUT A FIXED LENGTH CHARACTER STRING IN ASIS MODE
TPUTAF   PLIXDENT
         SPACE 1
         ENTRY TPUTAFR
TPUTAFR  EQU   TPUTAF
         EJECT
         GETSTR
         TPUT  (2),(4),ASIS
         REXIT
         DROP  R11
         EJECT
* OUTPUT A VARYING LENGTH CHARACTER STRING IN ASIS MODE
TPUTAV   PLIXDENT
         SPACE 1
         ENTRY TPUTAVR
TPUTAVR  EQU   TPUTAV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         LH    LENGTH,0(STRPTR)  SET TO CURRENT LENGTH
         TPUT  (2),(4),ASIS
         REXIT
         DROP  R11
         EJECT
* OUTPUT A FIXED LENGTH CHARACTER STRING IN CONTROL MODE
TPUTCF   PLIXDENT
         SPACE 1
         ENTRY TPUTCFR
TPUTCFR  EQU   TPUTCF
         EJECT
         GETSTR
         TPUT  (2),(4),CONTROL
         REXIT
         DROP  R11
         EJECT
* OUTPUT A VARYING LENGTH CHARACTER STRING IN CONTROL MODE
TPUTCV   PLIXDENT
         SPACE 1
         ENTRY TPUTCVR
TPUTCVR  EQU   TPUTCV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)  SKIP 2 BYTE PREFIX, -> TEXT
         LH    LENGTH,0(STRPTR)  SET TO CURRENT LENGTH
         TPUT  (2),(4),CONTROL
         REXIT
         DROP  R11
         EJECT
* OUTPUT A FIXED LENGTH CHARACTER STRING (EDIT MODE)
* AND WAIT UNTIL THE USER HAS RECEIVED IT AT HIS TERMINAL.
TPUTHF   PLIXDENT
         SPACE 1
         ENTRY TPUTHFR
TPUTHFR  EQU   TPUTHF
         EJECT
         GETSTR
         TPUT  (2),(4),HOLD
         REXIT
         DROP  R11
         EJECT
* OUTPUT A VARYING LENGTH CHARACTER STRING (EDIT MODE)
* AND WAIT UNTIL THE USER HAS RECEIVED IT AT HIS TERMINAL.
TPUTHV   PLIXDENT
         SPACE 1
         ENTRY TPUTHVR
TPUTHVR  EQU   TPUTHV
         EJECT
         GETSTR
         LA    TXTPTR,2(TXTPTR)    SKIP 2 BYTE PREFIX, -> TEXT
         LH    LENGTH,0(STRPTR)    SET TO CURRENT LENGTH
         TPUT  (2),(4),HOLD
         REXIT
         EJECT
* EXIT TO PL/I CALLING ROUTINE
EXIT     PLIXEXT
         SPACE 3
         DS    0F
PAD      DC    CL1' ',XL3'000000'
         LTORG
         END   TERMIO
