CMDLIBER    XSAVE OPT=CSECT,REEN=LSECT
* CMDLIBER: FRONT END COMMAND PROCESSOR TO INVOKE OTHER CP'S
*           WITH A USER SPECIFIED TASKLIB.
* SYSTEM REQUIREMENTS: TSO
* ATTRIBUTES: RENT,REUS,REFR
* AUTHOR: HOWARD GILBERT   YALE COMPUTER CENTER 213 432-4080
* DATE OF THIS VERSION: 1/14/76
* INSTALLATION INSTRUCTIONS:
* LINK EDIT THIS CSECT WITH ALIASES CONSISTING OF ONE CHARACTER
*    PREFIXING THE NAME OF THE CP YOU WANT TO FRONTEND.
*     EXAMPLE:
*         ALIAS XPLI
*         ALIAS XFORT
*         ALIAS XLOAD
*         ALIAS XLIBC
*         ALIAS XEDIT
*         NAME CMDLIBER
*     HERE YOU ARE FRONTENDING THE PLI,FORT,LOAD,LIBC,AND EDIT
*     COMMANDS.
* RUNNING INSTRUCTIONS:
*     ALLOC FILE(CMDLIB) DA('SYS1.TEST.PLIX.COMPILER')
*     XPLI PGM
*     ALLOC FILE(CMDLIB) DA('SYS1.TEXT.PLIX.TRANSENT.SUBS')
*     XLOAD PGM LIB('SYS1.TEST.PLIBASE')
*  ETC
*  PLEASE NOTE: WHEREAS THE CALL COMMAND PROCESSOR USES A TASKLIB,
*  IT IS NOT POSSIBLE TO FRONT END CALL. THEREFORE, CONCATENATE
*  YOUR LOAD MODULE LIBRARY TO THE FILE YOU WOULD OTHERWISE USE
*  AS CMDLIB AND USE 'LIBC' OR A SIMILAR CP WHICH EFFECTS BATCH
*  TYPE LINKAGE TO LINKLIB MODULES FROM TSO.
*
*  METHOD OF OPERATION:
*  GET THE NAME OF THE COMMAND THAT INVOKED US FROM THE ECTPCMD
*    FIELD OF THE ECT
*  OPEN THE CMDLIB DD NAME
*  ATTACH THE MODULE WHOSE NAME IS THE LAST 7 CHARACTERS OF
*    ECTPCMD PASSING IT THE CURRENT CPPL.
*  CLOSE UP SHOP.
         USING DSASECT,13
         EQUREGS
         LR    R2,R1
         USING CPPL,2
         L     R5,CPPLECT
         USING ECT,5
         MVC   NAME,=CL8' '
         MVC   NAME(7),ECTPCMD+1
         MVC   ECTPCMD(7),ECTPCMD+1
         MVI   ECTPCMD+7,C' '
         LA    R10,TL
         MVC   TL,TLL
         MVC   OPL(4),OPLL
         OPEN  ((10),(INPUT)),MF=(E,OPL)
         TM    TL+48,X'10'
         BZ    ERROR
         XC    EC,EC
         LA    R3,EC
         MVC   ATTL(ATTLEN),ATTLL
         LA    R15,ATTL
         LR    R1,R2
         ATTACH EPLOC=NAME,TASKLIB=(10),DCB=(10),SHSPV=78,ECB=(3),     X
               MF=(E,(1)),SF=(E,(15))
         ST    R1,TCB
         WAIT  ECB=EC
         DETACH   TCB
         MVC   CLOSL(4),CLOSLL
         CLOSE ((10)),MF=(E,CLOSL)
         L     R15,EC
RET      LA    R15,0(R15)
         XRETURN REEN=LSECT,RC=(15)
ERROR    TPUT  MSG,L'MSG
         LA    R15,12
         B     RET
MSG      DC    C'UNABLE TO OPEN CMDLIB FILE'
TLL      DCB   DDNAME=CMDLIB,DSORG=PO,MACRF=(R)
OPLL     OPEN  (,),MF=L
         ORG   TLL+96
CLOSLL   CLOSE (,),MF=L
ATTLL    ATTACH               SF=L
ATTLEN   EQU   *-ATTLL
DSASECT  DSECT
         DS    18A
EC       DS    A
TCB      DS    A
TL       DS    CL96
ATTL     ATTACH   SF=L
OPL      OPEN  (,),MF=L
CLOSL    CLOSE (,),MF=L
NAME     DS    CL8
LSECT    EQU   *-EC
         IKJCPPL
         IKJECT
         END    CMDLIBER
