         MACRO
&N       #MSG  &MSG
         LCLC  &L,&M
&L       SETC  'L'''
&M       SETC  ''' '.'&MSG'(2,K'&MSG-1)
         DC    AL1(&L&N)
&N       DC    C&M
         MEND
         EJECT ,
CMPRSEQ  START
         SPACE 3
         $REGS R
         SPACE 3
PRTLEN   EQU   133                 PRINTER WIDTH
LINECT   EQU   60                  PAGE HEIGTH
         SPACE 3
         $PUT  SUBAD=PRINTER,MF=INIT
         EJECT ,
         PRINT NOGEN
         SPACE 3
JFCB     DSECT ,
         IEFJFCBN LIST=YES         JOB FILE CONTROL BLOCK              *
                                   BASE IS INFMJFCB
         SPACE 3
         PRINT GEN
         EJECT ,
         PRINT NOGEN
         SPACE 3
DATA     DSECT
*DATDCB  DS    0A
DAT      $DCBD DSORG=QS
         ORG   ,
DATSTART DS    A
DATEND   DS    A
DATDIF   DS    A
DATXLIST DS    A
         SPACE 1
DATLIST  DS    0F
DLS      $DCBD DSORG=QS
         ORG   ,
DATLCT   DS    H
DATP#    DS    PL3
DATNXCCC DS    C
DATTIT   DS    CL(PRTLEN)
DATNAME  EQU   DATTIT+1,3
DATDSN   EQU   DATTIT+7,L'JFCBDSNM+L'JFCBELNM+2
DATTITP# EQU   *-(2*L'DATP#),2*L'DATP#
         DS    CL(L'DATTITP#-1)
         SPACE 3
         PRINT GEN
         EJECT
CMPRSEQ  $ENTER BASES=3
         SPACE 3
         L     R1,0(,R1)
         CLC   2(4,R1),=C'FULL'
         BNE   NOFULL
         MVI   CMPRLEN,79
NOFULL   EQU   *
         SPACE 3
         OPEN  (OLD,,                                                  *
               NEW,,                                                   *
               IGNORE,,                                                *
               SYSPRINT,OUTPUT,                                        *
               OLDLIST,OUTPUT,                                         *
               NEWLIST,OUTPUT)
         SPACE 1
         MVC   OLDTITDS,OLDDSN
         MVC   NEWTITDS,NEWDSN
         SPACE 1
         MVC   CVDWA(1),PRTOFLGS
         OC    CVDWA(1),OLSOFLGS
         OC    CVDWA(1),NLSOFLGS
         TM    CVDWA,DCBOFOPN
         $DIE  Z,'NO OUTPUT FILES AVAILABLE'
         SPACE 1
         LA    R1,MNOOLD-1
         TM    OLDOFLGS,DCBOFOPN
         BZ    TERM16
         LA    R1,MNONEW-1
         TM    NEWOFLGS,DCBOFOPN
         BZ    TERM16
         SPACE 3
         GETMAIN VU,LA=GETMQTY,A=GETMADR
         LM    R0,R2,ELBOW
         SR    R2,R0
         ST    R2,GETMADR+4
         AR    R1,R2
         ST    R1,GETMEND
         FREEMAIN R,LV=(0),A=(1)
         SPACE 3
         L     R2,GETMADR
         ST    R2,IGNSTART
         TM    IGNOFLGS,DCBOFOPN
         BZ    IGNNONE
         SPACE 1
         L     R3,GETMEND
         SH    R3,=H'240'
IGNGET2  LA    R1,MIGNCOR-1
         CR    R2,R3
         BH    TERM16
         GET   IGNORE,(R2)
         LA    R2,80(,R2)
         B     IGNGET2
IGNEOD   CLOSE IGNORE
IGNNONE  EQU   *
         SPACE 1
         ST    R2,IGNEND
         ST    R2,OLDSTART
         ST    R2,OLDEND
         ST    R2,OLDDIF
         LA    R2,80(,R2)
         ST    R2,NEWSTART
         ST    R2,NEWEND
         ST    R2,NEWDIF
         LA    R8,OLDDATA
         LA    R9,NEWDATA
         USING DATA,R9
         SPACE 3
GETLOOP  XR    R9,R8
         XR    R8,R9
         XR    R9,R8
         L     R3,DATDIF
         C     R3,DATEND
         BL    NOGET
         TM    DATOFLGS,DCBOFOPN
         BZ    GETLOOP
         GET   DATDCB
         LA    R0,80(,R3)
         C     R0,GETMEND
         BH    COREOVFL
         MVC   0(80,R3),0(R1)
         LA    R0,160(,R3)
         ST    R0,DATEND
NOGET    L     R2,DATSTART-DATA(,R8)
         IC    R1,CMPRLEN
CMPRLOOP C     R2,DATDIF-DATA(,R8)
         BNL   SAVE
         CLC   0(*-*,R3),0(R2)
         EX    R1,*-6
         BE    SAME
         LA    R2,160(,R2)
         B     CMPRLOOP
SAVE     LA    R3,160(,R3)
         ST    R3,DATDIF
         B     GETLOOP
         SPACE 3
SAME     CLC   DATDIF,DATSTART
         BNE   DIFFS3
         ST    R2,DATDIF-DATA(,R8)
         B     NODIFFS
DIFFS3   L     R15,IGNSTART
IGNLP    C     R15,IGNEND
         BNL   DIFFS2
         CLC   0(*-*,R3),0(R15)
         EX    R1,*-6
         BE    SAVE
         LA    R15,80(,R15)
         B     IGNLP
DIFFS2   ST    R2,DATDIF-DATA(,R8)
         BAL   R10,PRTDIF
NODIFFS  BAL   R10,DATASHFT
         XR    R9,R8
         XR    R8,R9
         XR    R9,R8
         BAL   R10,DATASHFT
         B     GETLOOP
         SPACE 3
EOD      CLOSE ((R9))
         XR    R9,R8
         XR    R8,R9
         XR    R9,R8
         TM    DATOFLGS,DCBOFOPN
         BNZ   GETLOOP
         CLC   OLDSTART,OLDEND
         BNE   DIFFS
         CLC   NEWSTART,NEWEND
         BE    EOP
DIFFS    BAL   R10,PRTDIF
         B     EOP
         DROP  R9
         EJECT ,
         USING DATA,R9
DATASHFT L     R2,DATDIF
         TM    DLSOFLGS,DCBOFOPN
         BZ    DANODLS
         MVC   PRTCARD,0(R2)
         LA    R15,DLSDCB
         $PUT  PRTBUF
DANODLS  L     R1,DATSTART
         ST    R1,DATDIF
SHFTLOOP LA    R2,160(,R2)
         C     R2,DATEND
         BNL   SHFTDONE
         MVC   0(80,R1),0(R2)
         LA    R1,160(,R1)
         B     SHFTLOOP
SHFTDONE ST    R1,DATEND
         BR    R10
         DROP  R9
         EJECT
PRTDIF   MVI   RCD+1,4
         AP    COUNT,=P'1'
         MVC   CNTBUF,=X'40202020202060'
         $TEST SIZE=(L'CNTBUF,EQ,7)
         EDMK  CNTBUF(2*L'COUNT),COUNT
         $TEST SIZE=(L'CNTBUF,GE,2*L'COUNT)
         BCTR  R1,0
         MVI   0(R1),C'-'
         SPACE 1
         MVC   PRTCNT,CNTBUF
         MVC   PRTFRAM1,=C'---'
         MVC   PRTFRAM2,=C'---'
         MVI   NEXTCCC,C'-'
         LA    R3,OLDDATA
         USING DATA,R3
         SPACE 1
PRTLP1   MVC   PRTNAME1,DATNAME
         MVC   PRTNAME2,DATNAME
         L     R5,DATSTART
         SPACE 1
PRTLP2   C     R5,DATDIF
         BNL   PRTDONE2
         MVC   PRTCARD,0(R5)
         LA    R15,SYSPRINT
         $PUT  PRTBUF
         MVI   NEXTCCC,C' '
         C     R5,DATSTART
         BNE   PRTNONUM
         MVC   PRTCNT,CNTBUF
PRTNONUM LA    R15,DLSDCB
         $PUT  PRTBUF
         MVC   PRTCNT,CNTBLANK
         LA    R5,160(,R5)
         B     PRTLP2
         SPACE 1
PRTDONE2 LA    R1,NEWDATA
         CR    R3,R1
         BE    PRTDONE
         LR    R3,R1
         MVI   PRTNAME1,C'-'
         MVC   PRTNAME1+1(PRTBUFZ-(PRTNAME1+1)),PRTNAME1
         LA    R15,SYSPRINT
         $PUT  PRTBUF
         MVI   NEXTCCC,C' '
         SPACE 1
         MVC   PRTCNT,CNTBUF
         CLC   OLDSTART,OLDDIF
         BNE   NDSHOLD
         MVC   PRTNAME1,OLDNAME
         MVC   PRTNAME2,OLDNAME
         LA    R15,OLSDCB
         $PUT  PRTBUF
NDSHOLD  CLC   NEWSTART,NEWDIF
         BNE   NDSHNEW
         MVC   PRTNAME1,NEWNAME
         MVC   PRTNAME2,NEWNAME
         LA    R15,NLSDCB
         $PUT  PRTBUF
NDSHNEW  MVC   PRTCNT,CNTBLANK
         B     PRTLP1
         DROP  R3
         SPACE 1
PRTDONE  MVI   PRTBUF,C' '
         MVC   PRTBUF+1(PRTBUFLN-1),PRTBUF
         BR    R10
         EJECT ,
TERM16   LR    R2,R1
         MVI   NEXTCCC,C'-'
         LA    R1,MTERMER-1
         BAL   R14,BRODCAST
         MVI   NEXTCCC,C' '
         LR    R1,R2
         BAL   R14,BRODCAST
         MVI   RCD+1,16
         B     TERM
         SPACE 3
COREOVFL BAL   R10,PRTDIF
         LA    R1,MCOROFL-1
         B     TERM12
         SPACE 3
TERM12   MVI   NEXTCCC,C'-'
         BAL   R14,BRODCAST
         MVI   RCD+1,12
         B     TERM
         SPACE 3
EOP      ED    MCE(6),COUNT
         MVI   NEXTCCC,C'-'
         LA    R1,MCE-1
         BAL   R14,BRODCAST
         B     TERM
         SPACE 3
TERM     CLOSE (OLD,,NEW,,IGNORE,,SYSPRINT,,OLDLIST,,NEWLIST)
         SPACE 1
         LH    R15,RCD
         $EXIT ((R14,R12)),RC=(R15)
         EJECT ,
BRODCAST STM   R14,R15,SAVBRODC
         LA    R15,SYSPRINT
         $PUT  (R1)
         LA    R15,OLDLIST
         $PUT  (R1)
         LA    R15,NEWLIST
         $PUT  (R1)
         LM    R14,R15,SAVBRODC
         BR    R14
         EJECT ,
         USING DATLIST,R15
PRINTER  TM    DLSOFLGS,DCBOFOPN
         BZR   R14
         STM   R14,R1,SAVPRINT
         MVC   DATNXCCC,NEXTCCC
         CLI   DATNXCCC,C'1'
         BE    PEJECT
         LA    R14,CCC+L'CCC
         LA    R0,L'CCC
PCCCSCAN BCTR  R14,0
         CLC   DATNXCCC,0(R14)
         BE    PCCCGOTN
         BCT   R0,PCCCSCAN
         MVI   DATNXCCC,C' '
         LA    R0,2
PCCCGOTN BCTR  R0,0
         LNR   R0,R0
         AH    R0,DATLCT
         BNM   PRINT
         SPACE 3
PEJECT   AP    DATP#,=P'1'
         MVC   DATTITP#+1(L'DATTITP#-1),=(L'DATTITP#-1)X'20'
         EDMK  DATTITP#,DATP#
         $TEST SIZE=(L'DATTITP#,EQ,2*L'DATP#)
         MVC   DATTITP#+1(L'DATTITP#-1),0(R1)
         MVI   DLSLRECL+1,L'DATTIT
         PUT   DLSDCB,DATTIT
         DROP  R15
         LM    R14,R1,SAVPRINT
         USING DATLIST,R15
         MVI   DATNXCCC,C'-'
         LA    R0,LINECT-4
         SPACE 3
PRINT    STH   R0,DATLCT
         MVC   1(1,R1),DATNXCCC
         MVC   DLSLRECL+1(1),0(R1)
         LA    R0,1(,R1)
         PUT   DLSDCB,(0)
         DROP  R15
         LM    R14,R1,SAVPRINT
         BR    R14
         EJECT
         USING DATDCB,R1
OPNEXIT  L     R3,DATDEBAD
         USING INFMJFCB-100,R3
         MVC   DATDSN,DATDSN-1
         MVC   DATDSN(L'JFCBDSNM),JFCBDSNM
         CLI   JFCBELNM,C' '
         BE    DATEXIT
         LA    R2,DATDSN+L'JFCBDSNM
BLNKLP1  BCTR  R2,0
         CLI   0(R2),C' '
         BE    BLNKLP1
         MVI   1(R2),C'('
         MVC   2(L'JFCBELNM,R2),JFCBELNM
         LA    R2,L'JFCBELNM+2(,R2)
BLNKLP2  BCTR  R2,0
         CLI   0(R2),C' '
         BE    BLNKLP2
         MVI   1(R2),C')'
         DROP  R3
         SPACE 1
DATEXIT  LH    R3,DATBLKSI
         LH    R4,DATLRECL
         SR    R2,R2
         DR    R2,R4
         MR    R2,R4
         CR    R3,R4
         BH    SETBLKSI
         LR    R3,R4
         NI    DATRECFM,255-DCBRECBR
SETBLKSI STH   R3,DATBLKSI
         STH   R3,DATBUFL
         BR    R14
         DROP  R1
         EJECT
         PRINT NOGEN
         SPACE 3
OLDDATA  DS    0F
OLD      DCB   DDNAME=OLD,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,         *
               OPTCD=C,EXLST=OLDXLIST,EODAD=EOD,EROPT=ACC
         ORG   OLD
OLD      $DCBD DSORG=QS
         ORG   ,
OLDSTART DS    A
OLDEND   DS    A
OLDDIF   DS    A
OLDXLIST DC    0F'0',X'85',AL3(OPNEXIT)
         SPACE 3
OLDLIST  DCB   DDNAME=OLDLIST,DSORG=PS,MACRF=PM,RECFM=UA,LRECL=PRTLEN, *
               BLKSIZE=PRTLEN,OPTCD=C,EROPT=ACC
         ORG   OLDLIST
OLS      $DCBD DSORG=QS
         ORG   ,
         SPACE 1
OLDLCT   DC    H'0'
OLDP#    DC    PL3'0'
OLDNXCCC DS    C
OLDTIT   DS    0CL(PRTLEN)
         DC    C'1'
OLDNAME  DC    C'OLD',C' - '
OLDDSN   DC    CL(L'JFCBDSNM+L'JFCBELNM+2)' '
         DC    CL(L'OLDTIT-L'DATTITP#-4-(*-OLDTIT))' ',C'PAGE'
OLDTITP# DC    CL(L'DATTITP#)' '
         $TEST SIZE=(*-OLDTIT,EQ,L'OLDTIT)
         DC    CL(L'OLDTITP#-2)' '
         SPACE 3
         PRINT GEN
         EJECT
         PRINT NOGEN
         SPACE 3
NEWDATA  DS    0F
NEW      DCB   DDNAME=NEW,DSORG=PS,MACRF=GL,RECFM=FB,LRECL=80,         *
               OPTCD=C,EXLST=NEWXLIST,EODAD=EOD,EROPT=ACC
         ORG   NEW
NEW      $DCBD DSORG=QS
         ORG   ,
NEWSTART DS    A
NEWEND   DS    A
NEWDIF   DS    A
NEWXLIST DC    0F'0',X'85',AL3(OPNEXIT)
         SPACE 3
NEWLIST  DCB   DDNAME=NEWLIST,DSORG=PS,MACRF=PM,RECFM=UA,LRECL=PRTLEN, *
               BLKSIZE=PRTLEN,OPTCD=C,EROPT=ACC
         ORG   NEWLIST
NLS      $DCBD DSORG=QS
         ORG   ,
         SPACE 1
NEWLCT   DC    H'0'
NEWP#    DC    PL3'0'
NEWNXCCC DS    C
NEWTIT   DS    0CL(PRTLEN)
         DC    C'1'
NEWNAME  DC    C'NEW',C' - '
NEWDSN   DC    CL(L'JFCBDSNM+L'JFCBELNM+2)' '
         DC    CL(L'NEWTIT-L'DATTITP#-4-(*-NEWTIT))' ',C'PAGE'
NEWTITP# DC    CL(L'DATTITP#)' '
         $TEST SIZE=(*-NEWTIT,EQ,L'NEWTIT)
         DC    CL(L'NEWTITP#-2)' '
         SPACE 3
         PRINT GEN
         EJECT ,
         PRINT NOGEN
         SPACE 3
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=UA,LRECL=PRTLEN,*
               BLKSIZE=PRTLEN,OPTCD=C,EROPT=ACC
         ORG   SYSPRINT
PRT      $DCBD DSORG=QS
         ORG   ,
PRTLCT   DC    H'0'
PRTP#    DC    PL3'0'
PRTNXCCC DS    C
PRTTIT   DS    0CL(PRTLEN)
         DC    C'1OLD - '
OLDTITDS DC    CL(L'OLDDSN)' ',C' NEW - '
NEWTITDS DC    CL(L'NEWDSN)' '
         DC    CL(L'PRTTIT-L'DATTITP#-4-(*-PRTTIT))' ',C'PAGE'
PRTTITP# DC    CL(L'DATTITP#)' '
         $TEST SIZE=(*-PRTTIT,EQ,L'PRTTIT)
         DC    CL(L'DATTITP#-2)' '
         SPACE 3
         PRINT GEN
         EJECT ,
         PRINT NOGEN
         SPACE 3
IGNORE   DCB   DDNAME=IGNORE,DSORG=PS,MACRF=GM,RECFM=FB,LRECL=80,      *
               OPTCD=C,EXLST=IGNXLIST,EODAD=IGNEOD,EROPT=ACC
         ORG   IGNORE
IGN      $DCBD DSORG=QS
         ORG   ,
IGNSTART DS    A
IGNEND   DS    A
         DS    A
IGNXLIST DC    0F'0',X'85',AL3(DATEXIT)
         SPACE 3
         PRINT GEN
         EJECT
CVDWA    DS    D
RCD      DC    Y(0)
COUNT    DC    PL3'0'
CCC      DC    C'+ 0-'
NEXTCCC  DS    C
CMPRLEN  DC    AL1(71)
         SPACE 3
SAVBRODC DC    2A(0)
SAVPRINT DC    4A(0)
         SPACE 3
GETMQTY  DC    A(5*1024,X'800000')
ELBOW    DC    A(4*1024)
GETMADR  DC    2A(0)
GETMEND  DS    A
         SPACE 3
         DC    AL1(PRTBUFLN)
PRTBUF   DS    C
PRTCNT   DS    C'-#####-'
PRTNAME1 DS    C'OLD'
PRTFRAM1 DS    C'---'
PRTCARD  DS    CL80
PRTFRAM2 DS    C'---'
PRTNAME2 DS    C'OLD'
PRTBUFZ  EQU   *
PRTBUFLN EQU   PRTBUFZ-PRTBUF
         SPACE 1
         ORG   PRTBUF
         DC    CL(PRTBUFLN)' '
         SPACE 1
CNTBUF   DS    CL(L'PRTCNT)
CNTBLANK DC    CL(L'PRTCNT)' '
         SPACE 3
         LTORG
         SPACE 3
MTERMER  #MSG  '*** TERMINAL ERROR ***'
         SPACE 3
MNOOLD   #MSG  '*** OLD FILE NOT AVAILABLE ***'
         SPACE 3
MNONEW   #MSG  '*** NEW FILE NOT AVAILABLE ***'
         SPACE 3
MIGNCOR  #MSG  '*** INSUFFICIENT MEMORY TO HOLD THE IGNORE FILE ***'
         SPACE 3
MCOROFL  #MSG  '*** MEMORY OVERFLOW - COMPARE ABANDONED ***'
         SPACE 3
MCE      #MSG  '½½½À½ BLOCKS OF COMPARE ERRORS FOUND'
         SPACE 3
         END
