**********************************************************************
* PROCEDURE    IEFUTL                                                *
*                                                                    *
* FUNCTION     SMF TIME LIMIT EXIT                                   *
*                                                                    *
* VERSION      MAY 17, 1978                                          *
*                                                                    *
* AUTHOR       HOWARD GILBERT                                        *
*                                                                    *
* THIS EXIT GETS CONTROL UNDER AN IRB IN THE INITIATOR               *
*  TASK WHEN THE TIMER INTERRUPT HANDLER DISCOVERS THAT              *
*  A JOB HAS EXCEEDED ITS CPU JOBSTEP OR WAIT TIME LIMIT.            *
* ENTRY CONDITIONS ARE DOCUMENTED IN SPL: SMF GC28-0706.             *
*                                                                    *
* NOTE: WHAT SPL:SMF DOES NOT TELL YOU IS THAT THE CALLER            *
* OF THIS EXIT, IN ORDER TO MAKE THINGS EASY FOR US OR SOMETHING,    *
* HAS CLOBBERED OUR JSCBACT FIELD, SO IT IS NOT POSSIBLE TO          *
* LOCATE THE RUNNING PROJECT THROUGH NORMAL MEANS. IT JUST           *
* SO HAPPENS THAT HE KEEPS THE POINTER TO THE ACTIVE                 *
* JSCB IN R9 PRIOR TO CALLING US. SO, IN DESPAIRATION, I USE         *
* R9 IN THE CALCULATION OF THE PROJECT ADDRESSING.                   *
**********************************************************************
*                                                                    *
*                       C H A N G E S                                *
*                                                                    *
*   IEFUTL WAS CHANGED TO ALLOW BLIP CHARACTER AND BLIP INCREMENT    *
*   TO BE LOADED FROM RESPECTIVE XASCBLPC AND XASCCPUB FIELDS OF     *
*   THE XASCB - THESE FIELDS WERE SET/RESET BY THE BLIP COMMAND.     *
*                                                                    *
*                                        AL KARENKO   AUG 1978       *
**********************************************************************
         EJECT
         PRINT NOGEN
IEFUTL   CSECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING IEFUTL,R12
         SPACE
* IF BATCH, THEN CANCEL THE GUY
         L     R3,PSAAOLD-PSA
         L     R3,ASCBTSB-ASCB(R3)
         LTR   R3,R3
         BZ    CANCEL
         SPACE
* IF WAIT TIME EXCEEDED
         CH    R0,EIGHT
         BNE   CPULIMIT
*    THEN IF THIS IS A YCC PROJECT, GIVE HIM MORE TIME,
*    FOR OTHER PROJECTS, CANCEL HIM
         L     R3,260(R9)         GET OS JCT FROM PP JSCB
         L     R3,55(R3)          GET ACT FIELD
         CLC   49(3,R3),YCC      CHECK PROJECT
         BNE   CANCEL
         L     R1,MOREWAIT
         B     RESUME
         SPACE
* IF THE CPU LIMIT HAS BEEN EXCEEDED, THEN IN TSO THIS
*  MEANS THAT THE USER WANTS TO BE BLIPPED.
         SPACE
CPULIMIT L     R3,PSAAOLD-PSA              -> ASID            *AMK*
         LH    R3,ASCBASID-ASCB(R3)        R3 = ASID          *AMK*
         SLL   R3,2                        R3 = ASID * 4      *AMK*
         L     R4,CVTPTR                   -> CVT             *AMK*
         L     R4,CVTUSER-CVT(R4)          -> XCVT            *AMK*
         L     R4,XCVTXASV-XCVT(R4)        -> XASVT           *AMK*
         L     R4,XASVENTY-4-XASVT(R3,R4)  -> XASCB           *AMK*
         USING XASCB,R4                                       *AMK*
         LA    R5,XASCBLPC                 -> BLIP CHARACTER  *AMK*
         TPUT  (5),1,CONTROL               BLIP THE USER      *AMK*
         L     R1,XASCCPUB                 R1 = BLIP INTERVAL *AMK*
         DROP  R4                                             *AMK*
         SPACE
* THIS LABEL EXITS WITH R15=8, R1 CONTAINS TIME EXTENSION
RESUME   LA    R15,8
         L     R14,12(R13)
         RETURN (2,12),RC=(15)
         SPACE
* THIS LABEL EXITS WITH R15=0 AND THE GUY GOES DOWN THE TUBES
CANCEL   RETURN (14,12),RC=0
MOREWAIT DC    A(2000*60)         GIVE YCC A BIG EXTENSION
EIGHT    DC    H'8'
YCC      DC    CL3'YCC'
         IHAPSA
         IHAASCB
         CVT   DSECT=YES
         XCVT
         XASVT
         XASCB
         YREGS
         END
