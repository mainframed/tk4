         PRINT NOGEN
* THIS FRIENDLY LITTLE FELLOW WAS WRITTEN BEFORE I
* BECAME AN OFFICIAL MEMBER OF THE YCC FAMILY. I
* THEREFORE REFUSE TO OFFER ANY APOLOGY TO THOSE
* WHO FEEL IT LACKS APPROPRIATE SERIOUSNESS
TOD      XSAVE TR=NO,OPT=CSECT
         EQUREGS              REGISTER EQUATES
         TIME  DEC            NOW WE HAVE DONE ALL THE WORK
         ST    R0,WORK        BUT WE MUST PRINT OUT SOMETHING
         OI    WORK+3,X'0F'   POSITIVE DECIMAL NUMBER NOW
         CP    WORK,NOON      IS IT PM???????
         BNH   AM
         SP    WORK,NOON      KNOCK OFF 12 HOURS
         MVC   CD,=C'PM'      AND CHANGE THE CHARACTER TAG
AM       UNPK  WORK2,WORK
         MVC   HR,WORK2       PICK UP TWO DIGITS FOR HOURS
         MVC   MI,WORK2+2     AND THE TWO FOR MINUTES
         CLI   HR,C'0'        DUMB KIND OF MASKING
         BNE   *+8               WE JUST BLANK THE LEADING
         MVI   HR,C' '           ZERO
         CLC   HR,=C' 0'      NOW IF IT IS 00:00 OR 12:00 HOURS
         BNE   *+10              WE ARE VERY UNMILITARY
         MVC   HR,=C'12'         AND CALL IT 12 OCLOCK
         TPUT  LINE,NOON-LINE
         TPUT  BEL,2,CONTROL  DING,DONG  AVON CALLING
         XRETURN TR=NO,SA=*
WORK     DS    PL4
WORK2    DS    ZL7
LINE     DC    C'AT THE TONE, THE TIME WILL BE '
HR       DS    CL2
         DC    C':'
MI       DS    CL2
         DC    C' '
CD       DC    CL2'AM'
NOON     DC    PL4'1200000'
BEL      DC    X'2F2F'
         END   TOD
