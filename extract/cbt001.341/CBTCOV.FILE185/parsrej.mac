PARSREJ  TITLE 'PARSE TABLE FOR REPROJ COMMAND'
***********************************************************************
* THIS CSECT CONTAINS THE IKJPARS MACROS REQUIRED TO PARSE THE        *
* COMMAND STRING OF THE REPROJ COMMAND.                               *
* THE REPROJ COMMAND IS WRITTEN IN PL/I AND INTERFACES TO THESE       *
* MACROS VIA THE SUBROUTINE "PARSER".                                 *
* REFER TO 'YCC.DOC.TEXT(PARSER)' FOR INFO ON THE INTERFACE.          *
* THE REPROJ COMMAND IS USED TO CHANGE THE PROJECT TO WHICH A DISK    *
* DATASET IS CHARGED (FOR STORAGE).                                   *
* CHANGES TO THE REPROJ COMMAND SYNTAX MUST BE COORDINATED WITH       *
* CHANGES TO THESE MACROS (FOR PDE ORDERING).                         *
* APB    07/12/79                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
* THE SYNTAX IS:                                                      *
*                                                                     *
*     REPROJ  DSNAME/DSLIST  PROJECT  VOLUME(XXXXXX)  OLDPROJ(YYYYYY) *
*                                                                     *
* DSNAME IS THE DATASET NAME (WITH OR WITHOUT THE UPPER LEVEL         *
* QUALIFIER).                                                         *
* DSNAME MAY CONTAIN AN IMBEDDED ASTERISK TO REPRESENT A LEVEL OF     *
* QUALIFICATION (EG. 'JOE.*.DATA', TEST.*, AND * ARE ALL VALID,       *
* '*.TEST.PLI' AND '*' ARE NOT VALID - ASTERISK CANNOT REPRESENT      *
* USERIDS).                                                           *
* ANY DSNAME IS TREATED AS A POTENTIAL LEVEL (EG.  TEST.*  AND  TEST  *
* ARE EQUIVALENT - EITHER WILL RESOLVE  TEST.X  TEST.Y.Z  ETC.).      *
* DSLIST IS A LIST OF DSNAMES ENCLOSED IN PARENTHESES.                *
* PROJECT IS THE PROJECT TO WHICH THE DATASET IS TO BE CHARGED.       *
* AN ASTERISK FOR PROJECT IMPLIES THE USER'S CURRENT PROJECT.         *
* TO BE VALID A PROJECT MUST EXIST AND HAVE THE ACTIVE AND RUNABLE    *
* ATTRIBUTES.                                                         *
* THE USER RUNNING THE COMMAND MUST BE JOINED TO THE NEW PROJECT.     *
* YCC STAFF MAY REPROJECT A DATASET TO ANY VALID PROJECT.             *
* XXXXXX IS THE DISK VOLUME ON WHICH AN UNCATALOGUED DATASET RESIDES. *
* YYYYYY SPECIFIES THAT ONLY DATASETS CHARGED TO PROJECT YYYYYY BE    *
* CHANGED.  THIS IS ESPECIALLY USEFUL WHEN:  REPROJ * ...             *
* AN ASTERISK FOR OLDPROJ IMPLIES THE USER'S CURRENT PROJECT.         *
* VOLUME AND OLDPROJ ARE OPTIONAL (KEYWORD) PARAMETERS.               *
***********************************************************************
         EJECT
***********************************************************************
* PARSER HAS SIX ARGUMENTS:                                           *
* 1) COMMAND BUFFER (IN OS COMMAND PROCESSOR FORMAT)                  *
*    (USE PL/I STRUCTURE)                                             *
* 2) PARSE CONTROL LIST (PCL CSECT BELOW)                             *
*    (DCL PCL PTR STATIC EXTERNAL; PASS ADDR(PCL))                    *
* 3) VALIDITY CHECKING ROUTINES (PL/I VALCHK SUBROUTINES)             *
*    (USE ENTRY VARIABLE ARRAY)                                       *
*    (SEE BELOW FOR INFO ON VALCHK PARMS AND RETCODE)                 *
* 4) RETURN CODE (HALFWORD) FROM PARSER:                              *
*    (NOTE CODES 0 - 28 ARE IKJPARS CODES)                            *
*        0 - PARSE IS OK                                              *
*        4 - PARMS ARE INCOMPLETE AND PARSE COULD NOT PROMPT          *
*        8 - ATTN INTERRUPT                                           *
*       12 - INVALID PARSE PARAMETER LIST (PPL)                       *
*       16 - PARSE COULD NOT GETMAIN STORAGE                          *
*       20 - VALCHK ROUTINE REQUESTED TERMINATION                     *
*       24 - CONFLICTING PARMS IN (TERM, OPER, OR RSVWD) MACROS       *
*       28 - TERMINAL DISCONNECTED                                    *
*       32 - ERROR IN PARSER ROUTINE                                  *
* 5) PDE PTRS RETURNED FROM IKJPARS                                   *
*    (USE PL/I STRUCTURE OF PTRS)                                     *
* 6) PTR TO DYNAMIC STORAGE ACQUIRED BY IKJPARS (FOR PDES)            *
*    (USE THIS PTR AS THE ARGUMENT TO ROUTINE "PARSFRE" AFTER         *
*    PROCESSING THE PDES TO FREE THE STORAGE).                        *
***********************************************************************
         EJECT
PARSREJ  CSECT
* IKJPARS MACROS REQUIRE A CONTAINING CSECT
* (<PARSREJ> - WHICH IN THIS CASE IS NULL).
* "PCL" IS THE STATIC EXTERNAL NAME THAT WILL BE PASSED BY REPROJ
* TO PARSER TO GET THE PARSE CONTROL LIST (PCL).
         SPACE 2
***********************************************************************
******** BEGIN IKJPARS MACROS                                         *
PCL      IKJPARM DSECT=PDL
         EJECT
***********************************************************************
******** PARAMETERS                                                   *
***********************************************************************
* DATASET(S)
DSN      IKJPOSIT DSTHING,LIST,USID,                                   *
               PROMPT='NAME OF DATASET(S) TO BE REPROJECTED'
* NOTE USERID PREFIX (FROM UPT) WILL BE APPENDED WHERE NECESSARY
* (HOWEVER, DSNS BEGINNING WITH "'" OR "*" WILL NOT BE PREFIXED)
         SPACE 1
* NEW PROJECT
NEWPROJ  IKJIDENT 'NEW PROJECT',MAXLNTH=8,FIRST=NONATABC,              *
               OTHER=ALPHANUM,ASTERISK,VALIDCK=PROJCHK,                *
               PROMPT='PROJECT TO BE CHARGED FOR DATASET STORAGE'
* NOTE ENTERING "*" WILL IMPLY USING THE CURRENT PROJECT OF THE USER
         SPACE 1
* VOLUME SERIAL
VOLUME   IKJKEYWD
         IKJNAME  'VOLUME',SUBFLD=VOLSER,ALIAS=('VOLSER')
         SPACE 1
* OLD PROJECT
OLDPROJ  IKJKEYWD
         IKJNAME  'OLDPROJ',SUBFLD=OLDPRJ
         EJECT
***********************************************************************
******** SUBFIELDS                                                    *
***********************************************************************
* VOLUME SERIAL
VOLSER   IKJSUBF
VOL      IKJIDENT 'VOLUME',MAXLNTH=6,CHAR,                             *
               PROMPT='NAME OF DISK VOLUME THAT CONTAINS YOUR DATASET'
         SPACE 1
* OLD PROJECT
OLDPRJ   IKJSUBF
OPROJ    IKJIDENT 'OLD PROJECT',MAXLNTH=8,FIRST=NONATABC,              *
               OTHER=ALPHANUM,ASTERISK,                                *
               PROMPT='NAME OF OLD PROJECT FOR DATASET SELECTION'
* NOTE ENTERING "*" WILL IMPLY USING THE CURRENT PROJECT OF THE USER
         EJECT
         IKJENDP
******** END OF IKJPARS MACROS                                        *
***********************************************************************
         EJECT
***********************************************************************
******** INTERFACE TO PL/I VALIDATION ROUTINES                        *
***********************************************************************
* THE PLIVALID MACRO IS USED TO INTERFACE THE VALIDITY CHECKING
* ROUTINES REQUESTED IN THE IKJPARS MACROS TO THE ACTUAL VALIDATION
* ROUTINES (WHICH ARE PL/I SUBROUTINES WITHIN REPROJ).
* THE NUMERIC OPERAND TO "PLIVALID" IS THE ORDER OF THE REQUESTED
* VALIDATION ROUTINE IN THE LIST OF ROUTINES PASSED TO PARSER.
* NOTE THAT THE PARSE MACROS DO NOT NEED TO KNOW THE REAL NAME OF
* THE PL/I VALIDITY CHECKING ROUTINES.
* THE PL/I VALCHK ROUTINES HAVE THREE ARGUMENTS:
* 1) A PTR TO THE PDE,
* 2) A PTR TO PROMPTING TEXT THAT THE VALCHK ROUTINE WANTS IKJPARS
*    TO ISSUE (IF THE VALCHK ROUTINE DOES NOT SUPPLY A MESSAGE, THE
*    PTR REMAINS NULL),
* 3) RETURN CODE FROM THE VALCHK ROUTINE:
*        0 - PDE IS OK
*        4 - PARSE SHOULD ISSUE ERROR MESSAGE & PROMPT FOR REPLACEMENT
*        8 - VALCHK ISSUED MSG, PARSE SHOULD PROMPT FOR REPLACEMENT
*       12 - PARSE SHOULD RETURN IMMEDIATELY TO CALLER.
         SPACE 2
PROJCHK  PLIVALID 1                VERIFIES THAT NEWPROJ IS VALID      *
               THE USER MUST BE JOINED TO THE PROJECT AND IT MUST BE   *
               ACTIVE AND RUNABLE.  (YCC STAFF NEED NOT BE JOINED).
         SPACE 2
******** THAT'S ALL FOLKS
***********************************************************************
         END   PARSREJ
