* KARL A. SPIGULIS
* FEBRUARY 22, 1978
*
* FUNCTION:
*      UPPER RETURNS THE OPERAND WITH ALL ALPHABETIC CHARACTERS
* (LETTERS A THROUGH Z) FOLDED INTO THE UPPER CASE.  LOWER RETURNS
* THE ARGUMENT WITH EACH ALPHABETIC CHARACTER FOLDED INTO THE LOWER
* CASE.  ALL NON-ALPHABETIC CHARACTERS IN THE OPERAND REMAIN UNCHANGED.
*
* SYNTAX:
*     CALL UPPER('STRING')
*     CALL LOWER('STRING')
*
* OPERANDS:
*      'STRING' MAY BE ANY VALID PL1 CHARACTER STRING, OF EITHER FIXED
* OR VARYING LENGTH.  STRINGS MAY BE OF ANY LENGTH.  LENGTHS OF LESS
* THAN ONE WILL RESULT IN NO ACTION BY UPPER OR LOWER.
*
* THIS CODE IS BOTH REENTRANT AND REUSABLE
*
*
UPPER1   PLIXSET
UPPER    PLIXDENT DSALEN=SAME
RTBL     EQU    R7
         LA    RTBL,TBL1
         B     BODY
LOWER    PLIXDENT DSALEN=SAME
         LA    RTBL,TBL2
BODY     L     R2,0(R1)       R2 GETS ADDR OF L/D
         TM    6(R2),X'80'    IS STRING FIXED OR VARYING?
         BZ    FIXED
         L     R3,0(,R2)      R3 GETS ADDR OF VARYING STRING
         LH    R4,0(,R3)      R4 GETS LENGTH OF VARYING STRING
         LA    R3,2(R3)       R3 GETS ADDRESS OF VARYING STRING TEXT
         B     CHKLTH
FIXED    L     R3,0(,R2)      R3 GETS ADDR OF FIXED STRING
         LH    R4,4(,R2)      R4 GETS LENGTH OF FIXED STRING
CHKLTH   LTR   R4,R4
         BNP   DONE
LOOP     C     R4,=F'256'     IS STRING NOT LONGER THAN 256 BYTES?
         BNH   SHORT
         TR    0(256,R3),0(RTBL)   THIS LOOP EXECUTED IF STRING IS
         S     R4,=F'256'          LONGER THAN 256 BYTES
         LA    R3,256(R3)
         B     LOOP
TEST     TR    0(*-*,R3),0(RTBL)
SHORT    BCTR  R4,0
         EX    R4,TEST
DONE     PLIXEXT
TBL1     DC    129AL1(*-TBL1)
         DC    9AL1(*-TBL1+X'40'),7AL1(*-TBL1)
         DC    9AL1(*-TBL1+X'40'),8AL1(*-TBL1)
         DC    8AL1(*-TBL1+X'40'),86AL1(*-TBL1)
TBL2     DC    193AL1(*-TBL2)
         DC    9AL1(*-TBL2-X'40'),7AL1(*-TBL2)
         DC    9AL1(*-TBL2-X'40'),8AL1(*-TBL2)
         DC    8AL1(*-TBL2-X'40'),22AL1(*-TBL2)
DSA      PLIXDSA
SAME     EQU    *-DSA
         END
