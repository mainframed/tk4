 TITLE 'TIME STAMP ROUTINES...PL/I CALLABLE'
*
*                  TIMANAL IS CALLED WITH TWO ARGUMENTS.  THE FIRST
*                  IS A FULLWORD REPRESENTING THE FIRST WORD OF
*                  THE SYSTEM CLOCK.  THE SECOND IS AN
*                  ARRAY OF 6 HALFWORDS TO RECEIVE YEAR, MONTH,
*                  DAY, HOUR, MINUTE, SECOND.  GMT CORRECTION AND
*                  EDT CORRECTION, IF ANY, MUST BE APPLIED TO THE
*                  STAMP PRIOR TO CALL.
*
*                  TIMSYN IS THE INVERSE OF TIMANAL.  IT TAKES
*                  IDENTICAL ARGUMENTS, BUT COMPUTES THE STAMP
*                  FROM THE HALFWORDS. TIMSYN ALTERS THE HALFWORD LIST
*                  BY REPLACING THE MONTH ENTRY WITH JULIAN DAYS.
*
*                  JULIN IS CALLED WITH ONE ARGUMENT, AN ARRAY OF
*                  THREE HALFWORDS, THE FIRST CONTAINING A YEAR,
*                  THE SECOND A JULIAN DAY.  AT RETURN, THE SECOND
*                  AND THIRD HALFWORDS CONTAIN MONTH AND DAY.
*
*                  JULOUT PERFORMS THE INVERSE OPERATION TO JULIN.
*
*                  ALL YEARS ARE IN 'PLUS 1900 NOTATION' I. E.,
*                  1978 IS REPRESENTED AS 78.
*****JOSH AUERBACH
*****FEBRUARY 13, 1978
*****LAST ALTERATION FEBRUARY 14, 1978
         PRINT NOGEN
TIMANAL1 PLIXSET
TIMANAL  PLIXDENT
         LM    R2,R3,0(R1) GET ARGUMENTS
         L     R3,0(R3)    SKIP LOCATOR; R3->HALFWORD ARRAY
         SR    R5,R5   CLEAR R5 (WHICH WILL BE LOW WORD OF "CLOCK")
         L     R4,0(R2)    GET HIGH CLOCK WORD
         SRDL  R4,14   CONVERT TO DOUBLE REG. 4-MICROSEC. UNITS
         D     R4,=F'1000000'  CONVERT TO 4-SECOND UNITS
         SR    R4,R4   ELIMINATE ODD MICROSECONDS
         D     R4,=F'15'   CONVERT TO MINUTES AND 4-SEC. UNITS
         SLL   R4,2        GET BACK TO SECONDS SCALE
         STH   R4,10(R3)   STORE SECONDS
         SR    R4,R4       CLEAR SECONDS
         D     R4,=F'60'   CONVERT TO HOURS AND MINUTES
         STH   R4,8(R3)    STORE MINUTES
         SR    R4,R4       CLEAR MINUTES
         D     R4,=F'24'   CONVERT TO DAYS AND HOURS
         STH   R4,6(R3)    STORE HOURS
         SR    R4,R4       CLEAR HOURS
         C     R5,=F'59'   UNLESS THE DATE IS FEB. 28,1900
*                          OR EARLIER, WE WILL APPLY A CORRECTION
*                          FOR THE GREGORIAN CONVENTION THAT 1900
*                          IS NOT A LEAP YEAR.
         BL    NOGREG1
         LA    R5,1(R5)    ADJUST DAYS AS IF 1900 WAS A LEAP YEAR
NOGREG1  SLDL  R4,2        MULTIPLY DAYS BY 4....
         D     R4,=F'1461' ...AND DIVIDE BY 1461...EQUIVALENT TO
*                          DIVISION BY 365.25
         STH   R5,0(R3)    STORE YEARS
         SRL   R4,2        TRUNCATE DAYS
         LA    R4,1(R4)    JULIAN DAYS USE 1-ORIGIN, NOT 0-ORIGIN
         STH   R4,2(R3)    STORE JULIAN DAYS
         LA    R1,4(R1)    R1->ORGINAL SECOND ARGUMENT
         LA    R15,JULIN   CALL JULIN ROUTINE
         BALR  R14,R15
         PLIXEXT   ,   RETURN TO CALLER
         EJECT
JULIN    PLIXDENT
         L     R1,0(R1)    R1->LOCATOR
         L     R1,0(R1)    R1->HALFWORD ARRAY
         LH    R4,2(R1)    R4 HAS JULIAN DAYS
         LA    R3,REGTABL  R3-> REGULAR MONTH TABLE
         TM    1(R1),3     TEST YEARS FOR MULTIPLE OF FOUR
         BNZ   NOLEAP1     SKIP NEXT INS. IF  NOT LEAP YEAR
         LA    R3,LEAPTABL R3->LEAP YEAR TABLE
NOLEAP1  LM    R5,R7,BXLECTR   USE R5-R7 FOR BXLE INSTRUCTION
BXLELOOP CH    R4,2(R5,R3) COMPARE DAYS TO MONTH TABLE ENTRY
         BNH   MATCH       IF NOT GREATER, WE'VE FOUND THE RIGHT MO.
         BXLE  R5,R6,BXLELOOP  GO AROUND LOOP
*                          IF WE FALL THROUGH, ITS AN ERROR
         XC    0(6,R1),0(R1)  CLEAR ANSWER AREA TO ZEROS
         B     EXIT1        AND RETURN
MATCH    SH    R4,0(R5,R3) SUBTRACT NEXT LOWER MONTH TABL. ENTRY
         STH   R4,4(R1)    STORE DAYS
         SRL   R5,1    COMPUTE MONTHS FROM BXLE COUNTER
         LA    R5,1(R5)
         STH   R5,2(R1)    STORE MONTHS
EXIT1    PLIXEXT   ,   RETURN TO CALLER
         EJECT
TIMSYN   PLIXDENT
         LM    R2,R3,0(R1) GET ORIGINAL ARGUMENTS
         LA    R1,4(R1)    R1->SECOND ARGUMENT IN LIST
         LA    R15,JULOUT  CALL JULOUT TO CONVERT MONTHS AND DAYS
         BALR  R14,R15
*                      NOTE: THIS ALTERS ARGUMENT!
         L     R3,0(R3)    SKIP LOCATOR; R3->HALFWORD LIST
         SR    R4,R4       CLEAR R4 FOR MULTIPLICATION
         LH    R5,0(R3)    GET YEARS IN R5
         M     R4,=F'1461' FIND NUMBER OF QUARTER DAYS IN YEARS
         LA    R5,3(R5)    ROUND UP
         SRL   R5,2        AND CONVERT BACK TO DAYS
         AH    R5,2(R3)    ADD JULIAN DAYS TO GET TOTAL DAYS
         BCTR  R5,0        BUT SUBTRACT 1, BECAUSE JULIAN DAYS USE
*                          1-ORIGIN, NOT 0-ORIGIN
         C     R5,=F'60'   UNLESS DATE IS BEFORE MARCH 1, 1900,
*                          APPLY GREGORIAN 'DECORRECTION'
         BL    NOGREG2
         BCTR  R5,0        REMOVE SPURIOUS LEAP DAY
NOGREG2  M     R4,=F'24'   CONVERT TO HOURS
         AH    R5,6(R3)    ADD HOURS TO GET TOTAL HOURS
         M     R4,=F'60'   CONVERT TO MINUTES
         AH    R5,8(R3)    ADD MINUTES TO GET TOTAL MINUTES
         M     R4,=F'15'   CONVERT TO FOUR SECOND UNITS
         LH    R4,10(R3)   GET SECONDS
         SRL   R4,2        DIVIDE, TRUNCATING, TO FOUR SECOND UNITS
         AR    R5,R4       ADD TO GET TOTAL
         SR    R4,R4       CLEAR R4
         M     R4,=F'1000000'  MULTIPLY TO GET FOUR MICSEC UNITS
         SLDL  R4,14   SHIFT TO TIME-STAMP POSITION
         ST    R4,0(R2)    STORE
         PLIXEXT   ,   RETURN TO CALLER
         EJECT
JULOUT   PLIXDENT
         L     R1,0(R1)    R1->LOCATOR
         L     R1,0(R1)    R1->HALFWORD ARRAY
         LH    R2,2(R1)    R2 HAS MONTHS
         LH    R3,4(R1)    R3 HAS DAYS
         LA    R4,REGTABL  R4->REGULAR MONTH TABLE
         TM    1(R1),3     TEST WHETHER LEAP YEAR
         BNZ   NOLEAP2
         LA    R4,LEAPTABL IF SO, R4->LEAP YEAR TABLE
NOLEAP2  BCTR  R2,0    DECREMENT MONTHS
         SLL   R2,1        AND MULTIPLY BY TWO FOR USE AS INDEX
         AH    R3,0(R2,R4) ADD DAYS FROM MONTH TABLE TO GET JULIAN DAY
         STH   R3,2(R1)    STORE RESULT
         PLIXEXT   ,   AND RETURN
*  CONSTANTS
REGTABL  DC    H'0,31,59,90,120,151,181'
         DC    H'212,243,273,304,334,365'
LEAPTABL DC    H'0,31,60,91,121,152,182'
         DC    H'213,244,274,305,335,366'
BXLECTR  DC    F'0'    R5 INITIAL VALUE (COUNTER)
         DC    F'2'    R6 INITIAL VALUE (INCREMENT)
         DC    F'22'   R7 INITIAL VALUE (COMPARAND)
         LTORG
         END
