         TITLE 'COMMAND SUB-SYSTEM INITIALIZATION'
DATSBINT CSECT
         SAVE  (14,12)
         LR    R3,R15
         USING DATSBINT,R3
         ST    R13,SAVE+4
         LA    R13,SAVE
         SPACE 1
         MODESET KEY=ZERO,MODE=SUP
         SPACE 1
         L     R0,SIZE             LOAD SIZE AND SUBPOOL
         SPACE 1
         GETMAIN R,LV=(0)
         SPACE 1
         LR    R4,R1               SAVE ADDR OR SSVT AND WORK AREA
         USING AREA,R4
         XC    SSVT(4),SSVT        CLEAR SSVT 1ST 4 BYTES
         MVI   SSVTFNUM+1,X'03'    SET NO. OF SUPPORTED FUNCTIONS
         XC    SSVTFCOD(256),SSVTFCOD  CLEAR FUNCTION MATRIX
         MVI   SSVTFCOD+06,1      FREE
         MVI   SSVTFCOD+15,3      OPEN
         MVI   SSVTFCOD+16,3      CLOSE
         MVI   SSVTFCOD+17,1      CHECKPOINT
         MVI   SSVTFCOD+18,1      RESTART
         MVI   SSVTFCOD+37,1      CONVERTER EXIT
         MVI   SSVTFCOD+38,2      ALLOCATE GROUP
         EJECT
         LOAD  EP=DATSBSYS         LOAD DAT SUB-SYSTEM MODULE
         SPACE 1
         LR    R10,R0              LOAD FROM ADDR
         LA    R11,0(0,R1)         LOAD LENGTH
         SLL   R11,3               MULT NO. OF DOUBLEWORDS BY 8
         SPACE 1
         LOAD  EP=DATDUMMY         LOAD DUMMY FLPA MODULE ADDR
         SPACE 1
         LR    R8,R0               SAVE
         A     R8,0(0,R10)            FUNCTION 1
         ST    R8,SSVTFUN1               ADDR IN SSVT
         LR    R8,R0               SAVE
         A     R8,4(0,R10)            FUNCTION 2
         ST    R8,SSVTFUN2               ADDR IN SSVT
         LR    R8,R0               SAVE
         A     R8,8(0,R10)            FUNCTION 3
         ST    R8,SSVTFUN3               ADDR IN SSVT
         QUEUE START,HEAD=SSVTHEAD
         LR    R8,R0               LOAD TO ADDR
         LA    R9,1024             LOAD LENGTH
         MVCL  R8,R10              MOVE IN SUB-SYS MODULE
         L     R5,16               CVT
         L     R6,0(0,R5)          TCB WORDS
         L     R6,4(0,R6)          CURRENT TCB
         L     R6,12(0,R6)         TIOT
         L     R5,296(0,R5)        JESCT
         L     R5,24(0,R5)         1ST SSCVT ADDR
         EJECT
SSCVT1   CLC   8(4,R5),8(R6)       NAME MATCH
         BE    SSCVT2              YES, CONTINUE
         L     R5,4(0,R5)          NEXT SSCVT
         LTR   R5,R5               ZERO SSCVT
         BNZ   SSCVT1              NO, CONTINUE
         SPACE 1
         ABEND 77,DUMP             INIT ERROR
         SPACE 3
SSCVT2   ST    R4,16(0,R5)         SAVE SSVT ADDR IN SSCVT
         L     R13,SAVE+4
         SPACE 1
         RETURN (14,12),RC=0
         EJECT
         LTORG
         SPACE 5
SAVE     DS    18F
SIZE     DC    X'F1',AL3(AREAEND-AREA)
ONES     DC    4AL1(1)
         DS    0D
         EJECT
AREA     DSECT
SSVT     DS    H
SSVTFNUM DS    H
SSVTFCOD DS    XL256
SSVTFRTN DS    0F
SSVTFUNC DS    0F
SSVTFUN1 DS    F
SSVTFUN2 DS    F
SSVTFUN3 DS    F
TFUN4    DS    F
SSVTHEAD DS    2F
         DS    7F
AREAEND  DS    0D
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
