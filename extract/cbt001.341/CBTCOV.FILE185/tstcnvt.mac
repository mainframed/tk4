TSTC     TITLE 'TSTCNVT -- CONVERT AN SRM TRANSACTION START TIME TO STC*
               K VALUE'
TSTCNVT  CSECT
         SPACE 3
         PRINT NOGEN
         SPACE 3
         CVT   DSECT=YES,LIST=YES  COMMINUCATIONS VECTOR TABLE         *
                                   BASE IS CVTMAP
         SPACE 3
         $REGS R                   STANDARD REGISTER NAMES
         SPACE 3
         PRINT ON,GEN,NODATA
         EJECT ,
*************************************************************
*                                                           *
*        DSA -- LOCAL REGISTER SAVE AREA AND DYNAMIC        *
*        STORAGE AREA                                       *
*                                                           *
*************************************************************
         SPACE 1
         $DSA  ,
DSASTCK  DS    D                   STORE-CLOCK BUFFER
DSALEN   EQU   *-DSA               L'DSA
         EJECT ,
TSTCNVT  $ENTER SAVTYPE=(RENT,DSALEN) REENTRANT ENTRY
         USING DSA,R13             DCL DSA BASE
         SPACE 1
         L     R2,CVTPTR           --> CVT
         L     R2,CVTOPCTP-CVTMAP(,R2) --> RMCT
         L     R2,X'78'(,R2)       HAVE TIME BASE
         LM    R10,R11,0(R1)       --> ARGUMENTS
         L     R10,0(,R10)         HAVE TST VALUE
         SLR   R10,R2              HAVE BITS 10-41 OF STCK VALUE
         SRL   R10,10              HAVE BITS 10-31 IN PLACE
         STCK  DSASTCK             GET CURRENT CLOCK
         LM    R8,R9,DSASTCK       HAVE IN REGS
         SRDL  R8,22               BITS 0-9 IN R8, 10-31 IN R9
         SRL   R9,10               RIGHT POSITION
         CR    R9,R10              NOW CAN'T BE BEFORE THEN
         BNL   OK                  OUR FAITH IS CONFIRMED
         BCTR  R8,0                OOPS, OVERFLOW, ADJUST
OK       SLL   R8,22               BACK IN PLACE
         OR    R10,R8              PUT TOGETHER
         ST    R10,0(,R11)         GIVE ANSWER
         $EXIT ((R14,R12)),RC=0    RETURN TO CALLER
         SPACE 3
         LTORG ,
         SPACE 3
         END   ,
