*FUNCTION: OPERATOR START COMMAND, RUN INITIALLY AT IPL TIME
*TO LOAD CURRENT MAXLVL OF TSO USERS INTO XCVTMAXU FLD OF THE XCVT
*
*INPUT: PARM GIVING MAXTSO
*OUTPUT: NONE
*MAPINGS: CVT,XCVT
*ALGORITHM: PARSE OUT PARM GIVING MAXU, LOAD XCVTMAXU WITH IT
*PROPERTIES: REENTRANT, REFRESHABLE
*RESTRICTIONS: NONE
*
*BOB IRWIN 4 MAR 77
*SEE COMPANION MODULE WHICH TESTS XCVTMAXU FIELD, LOGON EXIT
*IKJEFLD
*
*
*
         $REGS R
MAXTSO   XSAVE OPT=CSECT,REEN=8,BR=R12
*
*
*PARM VALIDITY CHECKING
*
         L     R2,0(R1)                R2<-PARM PTR
*
*LEN CHECKING, 1=<LEN>=2, ALLOWABLE LENGTH OF PARM
*
         SR    R4,R4
         LA    R4,2                    INIT R4
         CH    R4,0(,R2)               LEN=2?
         BE    NUMTEST                 OK, DO NEXT CHECK
         LA    R4,1                    NOW CHECK FOR A "1"
         CH    R4,0(,R2)               LEN=1?
         BE    NUMTEST                 OK
         B     BADPARM                 LEN CHECK NOT SATISFIED, NUM>99
*
*NUM REQIRED TO BE NUMERIC CHAR, F0F0=<NUM>=F9F9
*
NUMTEST  CLI   2(R2),X'F0'             VERFIY THAT NUM IS WITHIN
*                                      ABOVE RANGE
         BL    BADPARM
         CLI   2(R2),X'F9'
         BH    BADPARM
         CLI   3(R2),X'F0'
         BL    BADPARM
         CLI   3(R2),X'F9'
         BH    BADPARM
*
*VALID LENGTH AND VALID TYPE, NOW PACK CONVERT AND STORE
*
         PACK  72(8,R13),2(2,R2)
         CVB   R3,72(,R13)             R3<-MAXTSO
         L     R4,CVTPTR
         L     R4,CVTUSER-CVT(,R4)     R4->XCVT
         USING XCVT,R4
*
         MODESET KEY=ZERO
*
         STH   R3,XCVTMAXU             LD FLD WITH PARM
*
*
         B     RET
*
*
BADPARM  WTO 'INVALID PARM, NUM IS AN INTEGER FROM ZERO                X
               TO NINETY NINE',ROUTCDE=(1,2),DESC=3
RET      MODESET KEY=NZERO
         XRETURN
*
*DATA AREA
*
         XCVT  DSECT=YES,LIST=YES
         CVT   DSECT=YES,LIST=YES
*
         END
