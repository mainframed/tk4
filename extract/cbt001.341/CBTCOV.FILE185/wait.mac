WAIT     TITLE 'PL/I-CALLABLE ROUTINES THAT WAIT'
* THIS PROGRAM WILL GO INTO WAIT STATE FOR A SPECIFIED TIME INTERVAL
* THE WAIT CAN BE TERMINATED AT ANY TIME BY TYPING AN ATTENTION
* PL/I DECLARE:
*     DCL WAITX    EXT ENTRY (FIXED BIN) OPTIONS (ASM,INTER,RETCODE);
* WHERE X SPECIFIES WHICH ENTRY YOU WANT
* INPUT IS A HALFWORD INTEGER SPECIFYING THE TIME TO WAIT IN:
*     WAITM - MINUTES
*     WAITS - SECONDS
*     WAITH - HUNDRETHS OF SECONDS
* RETURN CODE:
*     0  - WAIT COMPLETED NORMALLY
*     8  - WAIT TERMINATED BY ATTENTION INTERRUPT
*     12 - INVALID PARM SPECIFIED (NO WAITING DONE)
* ATTRIBUTES:  REUSABLE, REFRESHABLE, REENTRANT
* 09/09/77  -  APB
         SPACE 3
         MACRO
         CNVTH &FACADDR          CONVERT TO HUNDRETHS OF A SECOND
.* &FACADDR IS THE ADDRESS OF THE HALFWORD CONVERSION FACTOR
*                                R1 -> PARMLIST
         L     R2,0(R1)          R2 -> TIME
         L     R4,0(R2)          R4 =  TIME INTERVAL
         LTR   R4,R4             IF THE INTERVAL IS NON-POSITIVE
         BNP   BOGUS             THEN WE WILL NOT WAIT
         LH    R3,&FACADDR       R3 =  CONVERSION FACTOR
         MH    R3,0(R2)          CONVERT TO HUNDRETHS OF A SECOND
         ST    R3,HUND           STORE INTERVAL FOR STIMER
         B     GO
         MEND
         EJECT
WAIT     PLIXSET
         EJECT
WAITH    PLIXDENT DSALEN=DSALEN
         SPACE
         USING DSA,RDSA
         CNVTH TH                TIME IS IN HUNDRETHS OF A SECOND
         EJECT
WAITS    PLIXDENT DSALEN=DSALEN
         SPACE
         USING DSA,RDSA
         CNVTH TS                TIME IS IN SECONDS
         EJECT
WAITM    PLIXDENT DSALEN=DSALEN
         SPACE
         USING DSA,RDSA
         CNVTH TM                TIME IS IN MINUTES
         EJECT
GO       EQU   *                           PROCESS THE WAIT
         MVC   TIMEUP(TEXITLEN),TIMEXIT    COPY TIMER EXIT INTO DSA
         LA    R2,TIMEUP+ECBPOS            -> ECB IN DSA
* SETUP ATTENTION INTERRUPT EXIT
         STAX  STOP,REPLACE=NO,DEFER=NO,USADDR=(2)
         SPACE
         STIMER REAL,TIMEUP,BINTVL=HUND    START THE TIMER
         SPACE
         LA    R1,TIMEUP+ECBPOS            -> ECB IN DSA
         WAIT  1,ECB=(1)                   HANG OUT
         SPACE
         STAX  ,                           CANCEL ATTENTION EXIT
         EJECT
EXIT     LA    R15,TIMEUP+ECBPOS+2         -> RETCODE
         LH    R15,0(R15)                  SET RETURN CODE
         PLIXEXT                           EXIT
         SPACE
* THERE IS NO PROVISION FOR A PARAMETER TO BE PASSED TO THE STIMER
* EXIT.  SINCE WE WANT TO BE REENTRANT, WE COPY THE PROTOTYPE EXIT
* CODE BELOW INTO THE DSA SO THAT IT CAN USE ITS OWN BASE ADDRESS
* TO ACCESS THE ECB.  NOTE THAT WE ALSO COPY A PROTOTYPE ECB INTO
* THE DSA (THE ECB MUST BE ALIGNED ON A FULLWORD).  THANKS TO HKG.
TIMEXIT  DS    0F
         USING *,R15             ESTABLISH ADDRESSABILITY
         STM   R14,R1,12(R13)    SAVE CONTROL REGISTERS
         POST  WECB,0            STOP WAITING, SET RETCODE=0
         LM    R14,R1,12(R13)    RESTORE CONTROL REGISTERS
         BR    R14               RETURN
WECB     DC    F'0'              PROTOTYPE ECB FOR WAIT
*                                LOW ORDER HALFWORD HOLDS RETURN CODE
ECBPOS   EQU   WECB-TIMEXIT      OFFSET FROM EXIT TO ECB
TEXITLEN EQU   *-TIMEXIT         LENGTH OF EXIT CODE + ECB
         DROP  R15
         SPACE
STOP     EQU   *                 ATTENTION EXIT
         USING *,R15
         STM   R14,R1,12(R13)    SAVE CONTROL REGISTERS
         TTIMER CANCEL           CANCEL THE TIMER
         L     R1,24(R13)        R1 -> ATTENTION PARAMETER LIST
         L     R1,8(R1)          -> USER INFO (THE ECB)
         POST  (1),8             STOP WAITING, SET RETCODE=8
         LM    R14,R1,12(R13)    RESTORE CONTROL REGISTERS
         BR    R14               RETURN
         DROP  R15
         SPACE
* INVALID PARM EXIT
BOGUS    LA    R1,12
         LA    R2,TIMEUP+ECBPOS+2          -> RETCODE
         STH   R1,0(R2)                    SET RETCODE=12
         B     EXIT
         EJECT
* CONVERSION CONSTANTS
TH       DC    H'1'              HUNDRETHS OF SECONDS
TS       DC    H'100'            SECONDS
TM       DC    H'6000'           MINUTES
         LTORG
         SPACE
         PLIXDSA
HUND     DS    F                 TIME INTERVAL IN 1/100'S SECONDS
TIMEUP   DS    (TEXITLEN)X       STORAGE FOR THE STIMER EXIT AND ECB
DSALEN   EQU   *-DSA
         END WAIT
