./       ADD   NAME=OUT38C
PROC 0 DEBUG
CONTROL PROMPT NOMSG
SET &MSG EQ &STR()
SET &CC EQ &STR()
/********************************************/
/* DRIVER CLIST FOR THE USER-WRITTEN DIALOG */
/* REPLACING THE ISPF/PDF 3.8 OUTLIST       */
/* UTILITY.  PANELS ARE OUT38P,ISRUTIL AND  */
/* OUTLID IN SS.PROD.OUT38.ISPPLIB AND      */
/* MESSAGES IN MEMBER OUT00 OF LIBRARY      */
/* SS.PROD.OUT38.ISPMLIB.  05/20/84  TPB    */
/********************************************/
IF &DEBUG EQ &STR(DEBUG) THEN CONTROL CONLIST SYMLIST LIST MSG
/********************************************/
/* GET TSO USERID WHETHER USER HAS THE      */
/* 'PROFILE NOPREFIX' TSO ATTRIBUTE OR NOT. */
/********************************************/
ISPEXEC CONTROL NONDISPL
ISPEXEC DISPLAY PANEL(OUTLID)
SET &USERID EQ &STR(&ZUSER)
SET &OLDID EQ &STR(&SYSUID)
ISPEXEC CONTROL ERRORS RETURN
MAINP:ISPEXEC DISPLAY PANEL(OUT38P) CURSOR(ZCMD) &MSG
/********************************************/
/* IF PF3 OR END KEY ENTERED GO BACK.       */
/* (THIS IS ONLY EXIT POINT FOR THIS PANEL) */
/********************************************/
IF &LASTCC EQ 8 THEN EXIT
/********************************************/
/***     S T A T U S    R E Q U E S T     ***/
/********************************************/
IF &ZCMD EQ &STR(L) THEN +
 DO
   IF &JBID NE &STR() AND &SUBSTR(1,&JBID) NE &STR(J) THEN +
           DO
             SET &MSG EQ &STR(MSG(OUT002))
             GOTO MAINP
           END
    SET &MSG EQ &STR()
    IF &SUBSTR(1:6,&JNAME) NE &STR(&USERID) THEN +
       DO
          CONTROL MSG
          ISPEXEC SELECT CMD(STATUS &JNAME)
          CONTROL NOMSG
          SET &ZCMD EQ &STR()
          GOTO MAINP
       END
    CONTROL MSG
    ISPEXEC SELECT CMD(STATUS)
    CONTROL NOMSG
    SET &ZCMD EQ &STR()
    GOTO MAINP
  END
/********************************************/
/***     D E L E T E     R E Q U E S T    ***/
/********************************************/
IF &ZCMD EQ &STR(D) THEN +
  DO
     IF &JNAME EQ &STR() THEN +
        DO
           SET &MSG EQ &STR(MSG(OUT001))
           GOTO MAINP
        END
     IF &JBID EQ &STR() THEN +
        DO
         INVOKE JOBSTAT '&JNAME'
         SET &CC EQ &LASTCC
           IF &CC EQ 4 THEN GOTO CC4
           IF &CC EQ 8 THEN GOTO CC8
           IF &CC EQ 12 THEN GOTO CC12
           IF &CC EQ 16 THEN GOTO CC16
           IF &CC EQ 32 THEN GOTO CC32
           IF &CC EQ 36 THEN GOTO CC36
           IF &CC EQ 40 THEN GOTO CC40
           IF &CC EQ 44 THEN GOTO CC44
          IF &LASTCC EQ 0 THEN GOTO OUTDEL
           SET &ZCMD EQ &STR()
           SET &MSG EQ &STR(MSG(OUT005))
          GOTO MAINP
        END
     IF &JBID NE &STR() THEN +
        DO
        IF &SUBSTR(1,&JBID) NE &STR(J) THEN +
                DO
                  SET &MSG EQ &STR(MSG(OUT002))
                  GOTO MAINP
                END
           INVOKE JOBSTAT '&JNAME/&JBID'
           SET &CC EQ &LASTCC
           IF &CC EQ 4 THEN GOTO CC4
           IF &CC EQ 8 THEN GOTO CC8
           IF &CC EQ 12 THEN GOTO CC12
           IF &CC EQ 16 THEN GOTO CC16
           IF &CC EQ 32 THEN GOTO CC32
           IF &CC EQ 36 THEN GOTO CC36
           IF &CC EQ 40 THEN GOTO CC40
           IF &CC EQ 44 THEN GOTO CC44
           IF &CC EQ 0 THEN GOTO OUTDEL
           SET &ZCMD EQ &STR()
           SET &MSG EQ &STR(MSG(OUT005))
           GOTO MAINP
        END
    OUTDEL: SET &MSG EQ &STR()
    IF &JBID EQ &STR() THEN +
       DO
          CONTROL MSG
          ISPEXEC SELECT CMD(OUTPUT &JNAME DELETE)
          CONTROL NOMSG
            SET &ZCMD EQ &STR()
            SET &MSG EQ &STR(MSG(OUT005))
          GOTO MAINP
       END
    IF &JBID NE &STR() THEN +
       DO
          CONTROL MSG
          ISPEXEC SELECT CMD(OUTPUT &JNAME(&JBID) DELETE)
          CONTROL NOMSG
            SET &ZCMD EQ &STR()
            SET &MSG EQ &STR(MSG(OUT005))
          GOTO MAINP
       END
  END
/********************************************/
/***    R E Q U E U E    R E Q U E S T    ***/
/********************************************/
IF &ZCMD EQ &STR(R) THEN +
  DO
     IF &JNAME EQ &STR() THEN +
        DO
           SET &MSG EQ &STR(MSG(OUT001))
           GOTO MAINP
        END
     IF &JBID EQ &STR() THEN +
        DO
          INVOKE JOBSTAT '&JNAME'
          SET &CC EQ &LASTCC
           IF &CC EQ 4 THEN GOTO CC4
           IF &CC EQ 8 THEN GOTO CC8
           IF &CC EQ 12 THEN GOTO CC12
           IF &CC EQ 16 THEN GOTO CC16
           IF &CC EQ 32 THEN GOTO CC32
           IF &CC EQ 36 THEN GOTO CC36
           IF &CC EQ 40 THEN GOTO CC40
           IF &CC EQ 44 THEN GOTO CC44
        END
     SET &MSG EQ &STR()
 IF &NCLASS EQ &STR() THEN +
        DO
          SET &MSG EQ &STR(MSG(OUT004))
          GOTO MAINP
        END
 IF &NCLASS NE &STR() THEN +
        DO
          SET &NUCLASS EQ &STR(NEWCLASS(&NCLASS))
        END
/********************************************/
/* REQUEUE REQUEST: PROCESS WHEN JOBNAME    */
/* ONLY IS PROVIDED.                        */
/********************************************/
  IF &JBID NE &STR() THEN GOTO RJBID
        IF &RMT NE &STR() AND &SUBSTR(1,&RMT) NE &STR(R) THEN +
                DO
                  ISPEXEC CONTROL DISPLAY LINE
                  WRITE REMOTE ID MUST BEGIN WITH THE LETTER R
                  GOTO MAINP
                END
        IF &DEST EQ &STR() THEN SET &DEST EQ &STR(CICA)
        IF &DEST NE &STR(CICA) THEN +
           DO
               IF &RMT EQ &STR() THEN GOTO OUTPUT1
               GOTO ROUTE1
           END
        IF &DEST EQ &STR(CICA) AND &RMT EQ &STR() THEN GOTO OUTPUT1
        IF &DEST EQ &STR(CICA) AND &RMT NE &STR() THEN +
           DO
              INVOKE DESTCHK '&RMT'
              IF &LASTCC NE 0 THEN +
                 DO
                    SET &MSG EQ &STR(MSG(OUT003))
                    GOTO MAINP
                 END
              SET &DEST EQ &RMT
              GOTO OUTPUT1
           END
        ROUTE1: +
          DO
            SET &RMT EQ &STR(.&RMT)
            INVOKE ROUTE '$RALL,J=''&JNAME'',D=&DEST&RMT'
            ISPEXEC SELECT CMD(OUTPUT &JNAME &NUCLASS)
            GOTO NODEST1
          END
        OUTPUT1: ISPEXEC SELECT CMD(OUTPUT &JNAME &NUCLASS DEST(&DEST))
        NODEST1: +
        IF &SUBSTR(1,&DEST) EQ &STR(R) THEN SET &DEST EQ &STR(CICA)
        SET &CC EQ &LASTCC
        IF &CC NE 0 THEN GOTO MAINP
        SET &ZCMD EQ &STR()
        SET &MSG EQ &STR(MSG(OUT005))
        GOTO MAINP
/********************************************/
/* REQUEUE REQUEST: PROCESS WHEN JOBNAME/   */
/* JOBID COMBINATION IS PROVIDED.           */
/********************************************/
RJBID:  IF &SUBSTR(1,&JBID) NE &STR(J) THEN +
                DO
                  SET &MSG EQ &STR(MSG(OUT002))
                  GOTO MAINP
                END
        IF &RMT NE &STR() AND &SUBSTR(1,&RMT) NE &STR(R) THEN +
                DO
                  ISPEXEC CONTROL DISPLAY LINE
                  WRITE REMOTE ID MUST BEGIN WITH THE LETTER R
                  GOTO MAINP
                END
        IF &DEST EQ &STR() THEN SET &DEST EQ &STR(CICA)
        IF &DEST NE &STR(CICA) THEN +
           DO
               IF &RMT EQ &STR() THEN GOTO OUTPUT2
               GOTO ROUTE2
           END
        IF &DEST EQ &STR(CICA) AND &RMT EQ &STR() THEN GOTO OUTPUT2
        IF &DEST EQ &STR(CICA) AND &RMT NE &STR() THEN +
           DO
              INVOKE DESTCHK '&RMT'
              IF &LASTCC NE 0 THEN +
                 DO
                    SET &MSG EQ &STR(MSG(OUT003))
                    GOTO MAINP
                 END
              SET &DEST EQ &RMT
              GOTO OUTPUT2
           END
        ROUTE2: +
           DO
             SET &RMT EQ &STR(.&RMT)
             INVOKE ROUTE '$RALL,J=&JBID,D=&DEST&RMT'
             ISPEXEC SELECT CMD(OUTPUT &JNAME(&JBID) &NUCLASS)
             GOTO NODEST2
           END
        OUTPUT2:ISPEXEC SELECT CMD(OUTPUT &JNAME(&JBID) &NUCLASS +
               DEST(&DEST))
        NODEST2: +
        IF &SUBSTR(1,&DEST) EQ &STR(R) THEN SET &DEST EQ &STR(CICA)
        SET &CC EQ &LASTCC
        IF &CC NE 0 THEN GOTO MAINP
        SET &ZCMD EQ &STR()
        SET &MSG EQ &STR(MSG(OUT005))
        GOTO MAINP
  END
/********************************************/
/***    B R O W S E      R E Q U E S T    ***/
/********************************************/
IF &ZCMD EQ &STR() THEN +
 DO
     IF &JNAME EQ &STR() THEN +
        DO
           SET &MSG EQ &STR(MSG(OUT001))
           GOTO MAINP
        END
/********************************************/
/* BROWSE REQUEST: PROCESS WITH JOBNAME ONLY*/
/********************************************/
   IF &JBID NE &STR() THEN GOTO BJBID
   INVOKE JOBSTAT '&JNAME'
   SET &CC EQ &LASTCC
   IF &CC EQ 4 THEN GOTO CC4
   IF &CC EQ 8 THEN GOTO CC8
   IF &CC EQ 12 THEN GOTO CC12
   IF &CC EQ 16 THEN GOTO CC16
   IF &CC EQ 32 THEN GOTO CC32
   IF &CC EQ 36 THEN GOTO CC36
   IF &CC EQ 40 THEN GOTO CC40
   IF &CC EQ 44 THEN GOTO CC44
   SET &MSG EQ &STR()
   FREE ATTRLIST(OUT38)
   ATTRIB OUT38 LRECL(133) BLKSIZE(1330) RECFM(F B A)
   SET &TM EQ &STR(&SYSTIME)
   SET &TIME EQ &STR(T&SUBSTR(1:2,&TM)&SUBSTR(4:5,&TM)&SUBSTR(7:8,&TM))
   ALLOC DA('&USERID..&TIME..OUTLIST') +
   NEW SPA(75 15) TRA RELEASE ROUND USING(OUT38)
   ISPEXEC SELECT CMD(OUTPUT &JNAME +
   PRINT('&USERID..&TIME..OUTLIST') +
                KEEP HOLD CLASS(&CLASS))
           ISPEXEC BROWSE DATASET('&USERID..&TIME..OUTLIST')
           IF LASTCC GT 0 THEN +
              DO
                CONTROL MSG
                ISPEXEC SELECT CMD(STATUS)
                CONTROL NOMSG
                SET &ZCMD EQ &STR()
                GOTO MAINP
              END
           IF &LASTCC EQ 0 THEN +
              DO
                DELETE '&USERID..&TIME..OUTLIST'
              END
           SET &ZCMD EQ &STR()
           SET &MSG EQ &STR(MSG(OUT005))
           GOTO MAINP
   END
/********************************************/
/* BROWSE REQUEST: PROCESS WITH             */
/* JOBNAME/JOBID COMBINATION                */
/********************************************/
BJBID:  IF &SUBSTR(1,&JBID) NE &STR(J) THEN +
                DO
                  SET &MSG EQ &STR(MSG(OUT002))
                  GOTO MAINP
                END
   INVOKE JOBSTAT '&JNAME/&JBID'
   SET &CC EQ &LASTCC
   IF &CC EQ 4 THEN GOTO CC4
   IF &CC EQ 8 THEN GOTO CC8
   IF &CC EQ 12 THEN GOTO CC12
   IF &CC EQ 16 THEN GOTO CC16
   IF &CC EQ 32 THEN GOTO CC32
   IF &CC EQ 36 THEN GOTO CC36
   IF &CC EQ 40 THEN GOTO CC40
   IF &CC EQ 44 THEN GOTO CC44
   SET &MSG EQ &STR()
   FREE ATTRLIST(OUT38)
   ATTRIB OUT38 LRECL(133) BLKSIZE(1330) RECFM(F B A)
   SET &TM EQ &STR(&SYSTIME)
   SET &TIME EQ &STR(T&SUBSTR(1:2,&TM)&SUBSTR(4:5,&TM)&SUBSTR(7:8,&TM))
   ALLOC DA(&TIME..OUTLIST) +
   NEW SPA(75 15) TRA RELEASE ROUND USING(OUT38)
 ISPEXEC SELECT CMD(OUTPUT &JNAME(&JBID) +
 PRINT('&USERID..&TIME..OUTLIST') +
                KEEP HOLD CLASS(&CLASS)
           ISPEXEC BROWSE DATASET('&USERID..&TIME..OUTLIST')
              IF LASTCC GT 0 THEN +
                 DO
                   ISPEXEC CONTROL DISPLAY LINE
                   WRITE JOB &JNAME OUTPUT NOT HELD
                   ISPEXEC CONTROL DISPLAY LINE
                   WRITE OR DUPLICATE JOBNAMES WITH NO JOBID GIVEN
                   GOTO MAINP
                 END
              IF &LASTCC EQ 0 THEN +
                 DO
                   DELETE '&USERID..&TIME..OUTLIST'
                 END
           SET &ZCMD EQ &STR()
           SET &MSG EQ &STR(MSG(OUT005))
           GOTO MAINP
  END
/********************************************/
/* RETURN CODES FROM JOBSTAT PROGRAM TO     */
/* ISSUE STATUS REQUEST AND PUT CONDITION   */
/* CODE IN REGISTER 15.                     */
/********************************************/
 CC4:  +
             ISPEXEC CONTROL DISPLAY LINE
             WRITE JOBNAME &JNAME NOT FOUND ON OUTPUT QUEUE
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC8:  +
             ISPEXEC CONTROL DISPLAY LINE
             WRITE JOB &JNAME HAS INVALID JOBNAME/JOBID COMBINATION
             SET &ZCMD EQ &STR()
             SET &MSG EQ &STR(MSG(OUT006))
             GOTO MAINP
 CC12: +
             ISPEXEC CONTROL DISPLAY LINE
             WRITE JOB &JNAME IS A DUPLICATE JOBNAME WITH NO JOBID GIVEN
             CONTROL MSG
             STATUS &JNAME
             CONTROL NOMSG
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC16: +
             ISPEXEC CONTROL DISPLAY LINE
             WRITE JOB &JNAME IS A DUPLICATE JOBNAME WITH NO JOBID GIVEN
             CONTROL MSG
             STATUS &JNAME
             CONTROL NOMSG
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC32: +
             ISPEXEC CONTROL DISPLAY LINE
          WRITE JOB &JNAME CURRENTLY EXECUTING OR JOB OUTPUT NOT HELD
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC36: +
             ISPEXEC CONTROL DISPLAY LINE
             WRITE JOB &JNAME DOES NOT HAVE HELD OUTPUT
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC40: +
             ISPEXEC SELECT CMD(STATUS &JNAME)
             SET &ZCMD EQ &STR()
             GOTO MAINP
 CC44: +
             ISPEXEC CONTROL DISPLAY LINE
        WRITE JOB &JNAME IS ACTIVE IN NJE TRANSMISSION TO ANOTHER NODE
             SET &ZCMD EQ &STR()
             GOTO MAINP
