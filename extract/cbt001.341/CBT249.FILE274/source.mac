./       ADD   NAME=DESTCHK
DESTCHK  CSECT
 TITLE 'ISSUE SUBSYSTEM REQUEST TO VERIFY REMOTE DESTINATION'
**********************************************************************
* NAME       -   DESTCHK                                             *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   ISSUE SUBSYSTEM REQUEST TO JES2 TO VERIFY THE       *
*                EXISTENCE OF A REMOTE DESTINATION.                  *
*                                                                    *
* INPUT      -   PARAMETER OF DESTINATION.                           *
*                                                                    *
* OUTPUT     -   SET RETURN CODE IN REGISTER 15                      *
*                                                                    *
* SYSTEM     -   MVS SP1.3 - MVS/XA 1.0                              *
*                                                                    *
* DATE       -   03/09/84                                            *
*                                                                    *
* MACROS     -   IEFSSREQ,TPUT,REGISTER                              *
*                                                                    *
* DSECTS     -   IEFJESCT,IEFJSSOB                                   *
*                                                                    *
* REGISTERS  -      R1   INPUT PARM TO IEFSSREQ MACRO                *
*                   R2   SSIB ADDRESS                                *
*                   R3   PARAMETER LENGTH                            *
*                   R4   ADDRESS OF ACTUAL PARAMETER                 *
*                   R5   SSOB CONTROL BLOCK DSECT                    *
*                   R6   ADDRESS OF SSOB PLUS LEADING FULLWORD       *
*                   R7   SSI REQUEST RC AND SSOBRETN RC              *
*                   R8   **** UNUSED BY PROGRAM ****                 *
*                   R9   **** UNUSED BY PROGRAM ****                 *
*                   R10  **** UNUSED BY PROGRAM ****                 *
*                   R11  FIRST BASE REGISTER                         *
*                   R12  SECOND BASE REGISTER                        *
*                   R13  **** UNUSED BY PROGRAM ****                 *
*                   R14  RETURN ADDRESS TO MVS                       *
*                   R15  RETURN CODE FROM IEFSSREQ                   *
*                                                                    *
* SUBSYSTEM                                                          *
* RETURN CODES   -   4   INVALID DESTINATION                         *
* (SSOBRETN FIELD)   8   SUBSYSTEM COULD NOT COMPLETE VALIDITY       *
*                        CHECK                                       *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    SUPERVISOR STATE - KEY 0 PROCESSING                *
**********************************************************************
         STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R11,R15                      ENTRY POINT ADDRESS LOAD
         USING DESTCHK,R11,R12              SET ADDRESSABILITY
         LA    R12,4095(R11)                SET SECOND BASE REGISTER
         LR    R3,R13                       SAVE SYSTEM ADDRESS
         ST    R13,SAVEAREA+4               SYSTEM ADDR IN MY SAVEAREA
         LA    R13,SAVEAREA                 ADDRESS OF MY SAVEAREA
         ST    R13,8(R3)                    MY SAVEAREA PUT IN SYSTEM
         L     R1,0(R1)                     GET ADDRESS OF COMMAND
         LH    R3,0(R1)                     GET COMMAND LENGTH
         LTR   R3,R3                        ANY COMMAND?
         BZ    PARMNO                           NO, END NOW
         LA    R4,2(R1)                     ADDRESS OF ACTUAL COMMAND
******************************************
*     PROCESS DESTINATION PARAMETER.     *
******************************************
         CH    R3,EIGHT                     DESTID > 8 ?
         BNL   LONGPARM                     PARM TOO LONG - EXIT
         BCTR  R3,0                         DECREMENT FOR EXECUTE
         EX    R3,MOVEDEST                  MOVE IN DESTID FIELD
         OC    DESTID,CAPS                  FLIP TO UPPERCASE
******************************************
*  CREATE SSIB/SSOB CONTROL BLOCKS FOR   *
*  SUBSYSTEM REQUEST TO DESTINATION      *
*  VERIFICATION.                         *
******************************************
         USING SSOB,R5                      DSECT IT
         LA    R0,SSOBLEN8+4                SSOB TOTAL LENGTH+4
         GETMAIN R,LV=(0)                   GET THE STORAGE
         LA    R5,4(R1)                     SSIB ADDRESS POINTER+4
         MVC   SSOBID,SSOBCHAR              SET SSIB ID
         L     R1,=A(SSOBHSIZ)              SSOB SIZE
         STH   R1,SSOBLEN                   SET IT IN SSOBLEN
         LA    R1,SSOBUSER                  SET FUNCTION ID
         STH   R1,SSOBFUNC                  STORE IN IN SSOB FUNC
         SLR   R1,R1                        ZERO OUT R1
         ST    R1,SSOBSSIB                  SET TO JOB STEP SSIB
         MVC   SSUSUSER,DESTID              MOVE DESTID TO SSOB
         LA    R1,SSOBGN                    SSOB EXTENSION ADDRESS
         ST    R1,SSOBINDV
         LR    R1,R5                        GET SSOB ADDRESS
         SH    R1,FOUR                      BACK UP 4 BYTES
         ST    R5,0(R1)                     SSOB ADDRESS OFF R1
         OI    0(R1),X'80'                  TURN ON HIGH-ORDER BIT
         LR    R6,R1                        GET ADDRESS OF ADDRESS
         NI    0(R1),X'FF'                  TURN OFF HIGH-ORDER BIT
         LA    R0,1
         SVC   255                          GET AUTHORIZED
         MODESET MODE=SUP,KEY=ZERO
         LR    R1,R6                        SET R1 FOR REQUEST
         IEFSSREQ
         LR    R7,R15                       SAVE RETURN CODE IN REG15
         MODESET MODE=PROB,KEY=NZERO
         SLR   R0,R0
         SVC   255                          GET UNAUTHORIZED
******************************************
*  CHECK TO SEE IF SUBSYSTEM REQUEST WAS *
*  SUCCESSFULL.                          *
******************************************
         LR    R15,R7                       PUT IEFSSREQ BACK IN R15
         LTR   R15,R15                      WAS REQUEST ISSUED?
         BNZ   NOREQ                            NO, ISSUE ERRMSG
******************************************
*  SET SSOBRETN (SSOB RETURN CODE) IF IT *
*  WAS NON-ZERO.                         *
******************************************
         SLR   R15,R15                      CLEAR REGISTER 15
         L     R7,SSOBRETN                  IF SSOBRETN IS NE 0 THEN
         LA    R0,SSOBLEN8+4                STORAGE TO BE FREED
         FREEMAIN R,LV=(0),A=(6)            FREE GETMAINED STORAGE
         LR    R15,R7                       SET RETURN CODE
FINIS    L     R13,SAVEAREA+4               SYSTEM SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)             REG 15 HAS RETURN CODE
PARMNO   TPUT  NOPARM,L'NOPARM
         B     FINIS
NOREQ    TPUT  REQNO,L'REQNO
         B     FINIS
LONGPARM TPUT  PARMLONG,L'PARMLONG
         B     FINIS
MOVEDEST MVC   DESTID(0),0(R4)
PARMLONG DC    C'INPUT DESTINATION PARAMETER MUST BE 8 DIGITS OR LESS'
NOPARM   DC    C'INPUT DESTINATION PARAMETER MISSING'
REQNO    DC    C'DESTINATION VERIFICATION REQUEST DID NOT GO TO JES2'
SSOBCHAR DC    C'SSOB'
CAPS     DC    CL8' '
DESTID   DC    CL8' '
SAVEAREA DS    18F
FOUR     DC    H'4'
EIGHT    DC    H'8'
         REGISTER
         PRINT NOGEN
         IEFJESCT
         CVT DSECT=YES
         PRINT GEN
         IEFJSSOB (US),CONTIG=YES
         END   DESTCHK
./       ADD   NAME=JOBSTAT
JOBSTAT  CSECT
 TITLE 'ISSUE SUBSYSTEM REQUEST FOR JOB STATUS'
**********************************************************************
* NAME       -   JOBSTAT                                             *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   ISSUE SUBSYSTEM REQUEST TO JES2 TO GET JOB STATUS.  *
*                SET NON-ZERO RETURN CODE IF JOB NOT FOUND.          *
*                                                                    *
* INPUT      -   PARAMETER OF JOBNAME AND/OR JOBID.                  *
*                                                                    *
* OUTPUT     -   SET RETURN CODE IN REGISTER 15                      *
*                                                                    *
* SYSTEM     -   MVS SP1.3 - MVS/XA 1.0                              *
*                                                                    *
* DATE       -   02/27/84                                            *
*                                                                    *
* MACROS     -   IEFSSREQ,TPUT,REGISTER                              *
*                                                                    *
* DSECTS     -   IEFJESCT,IEFJSSOB                                   *
*                                                                    *
* REGISTERS  -      R1   INPUT PARM TO IEFSSREQ MACRO                *
*                   R2   SSIB ADDRESS                                *
*                   R3   PARAMETER LENGTH                            *
*                   R4   ADDRESS OF ACTUAL PARAMETER                 *
*                   R5   SSOB CONTROL BLOCK DSECT                    *
*                   R6   ADDRESS OF SSOB PLUS LEADING FULLWORD       *
*                   R7   SSI REQUEST RC AND SSOBRETN RC              *
*                   R8   **** UNUSED BY PROGRAM ****                 *
*                   R9   **** UNUSED BY PROGRAM ****                 *
*                   R10  **** UNUSED BY PROGRAM ****                 *
*                   R11  FIRST BASE REGISTER                         *
*                   R12  SECOND BASE REGISTER                        *
*                   R13  **** UNUSED BY PROGRAM ****                 *
*                   R14  RETURN ADDRESS TO MVS                       *
*                   R15  RETURN CODE FROM IEFSSREQ                   *
*                                                                    *
* SUBSYSTEM                                                          *
* RETURN CODES   -   4   JOBNAME NOT FOUND                           *
* (SSOBRETN FIELD)   8   INVALID JOBNAME/JOBID COMBINATION           *
*                   12   DUPLICATE JOBNAMES AND NO JOBID GIVEN       *
*                   16   STATUS ARRAY TO SMALL                       *
* USER-DEFINED                                                       *
* RETURN CODES   -  32   NON-OUTPUT JOBS                             *
* (SSCSFLG1 FIELD)  36   NON-HELD OUTPUT                             *
*                   40   CURRENTLY ACTIVE JOB                        *
*                   44   NJE ACTIVE JOB                              *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    SUPERVISOR STATE - KEY 0 PROCESSING                *
**********************************************************************
         STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R11,R15                      ENTRY POINT ADDRESS LOAD
         USING JOBSTAT,R11,R12              SET ADDRESSABILITY
         LA    R12,4095(R11)                SET SECOND BASE REGISTER
         LR    R3,R13                       SAVE SYSTEM ADDRESS
         ST    R13,SAVEAREA+4               SYSTEM ADDR IN MY SAVEAREA
         LA    R13,SAVEAREA                 ADDRESS OF MY SAVEAREA
         ST    R13,8(R3)                    MY SAVEAREA PUT IN SYSTEM
         L     R1,0(R1)                     GET ADDRESS OF COMMAND
         LH    R3,0(R1)                     GET COMMAND LENGTH
         LTR   R3,R3                        ANY COMMAND?
         BZ    PARMNO                           NO, END NOW
         LA    R4,2(R1)                     ADDRESS OF ACTUAL COMMAND
******************************************
*  PROCESS JOBNAME AND/OR JOBID COMBO.   *
******************************************
         MVC   JOBNAME(7),0(R4)             MOVE IN JOBNAME FIELD
         CH    R3,SEVEN                     JOBNAME > 7 ?
         BE    NOJOBID                      WE HAVE JOBNAME AND JOBID
         SH    R3,EIGHT                     LENGTH MINUS JOBNAME/
         BCTR  R3,0                         DECREMENT FOR EXECUTE
         EX    R3,MOVEID                    MOVE IN JOBID FIELD
NOJOBID  EQU   *
******************************************
*  CREATE SSIB/SSOB CONTROL BLOCKS FOR   *
*  SUBSYSTEM REQUEST TO GET JOBNAME      *
*  (/JOBID) STATUS.                      *
******************************************
         USING SSOB,R5                      DSECT IT
         LA    R0,SSOBLEN2+4                SSOB TOTAL LENGTH+4
         GETMAIN R,LV=(0)                   GET THE STORAGE
         LA    R5,4(R1)                     SSIB ADDRESS POINTER+4
         MVC   SSOBID,SSOBCHAR              SET SSIB ID
         L     R1,=A(SSOBHSIZ)              SSOB SIZE
         STH   R1,SSOBLEN                   SET IT IN SSOBLEN
         LA    R1,SSOBSTAT                  SET FUNCTION ID
         STH   R1,SSOBFUNC                  STORE IN IN SSOB FUNC
         SLR   R1,R1                        ZERO OUT R1
         ST    R1,SSOBSSIB                  SET TO JOB STEP SSIB
         MVC   SSCSJOBN,JOBNAME             MOVE JOBNAME TO SSOB
         MVC   SSCSJOBI,JOBID               MOVE JOBID TO SSOB
         LA    R1,SSOBGN                    SSOB EXTENSION ADDRESS
         ST    R1,SSOBINDV
         LA    R1,SSCSELSZ                  ARRAY ELEMENT SIZE
         STH   R1,SSCSDIMP                  SET BY CALLER
         LA    R1,SSCSARAY                  ARRAY FOR SUBSYSTEM
         ST    R1,SSCSMPTR                  STORE IN SSOB POINTER
         LR    R1,R5                        GET SSOB ADDRESS
         SH    R1,FOUR                      BACK UP 4 BYTES
         ST    R5,0(R1)                     SSOB ADDRESS OFF R1
         OI    0(R1),X'80'                  TURN ON HIGH-ORDER BIT
         LR    R6,R1                        GET ADDRESS OF ADDRESS
         NI    0(R1),X'FF'                  TURN OFF HIGH-ORDER BIT
         LA    R0,1
         SVC   255                          GET AUTHORIZED
         MODESET MODE=SUP,KEY=ZERO
         LR    R1,R6                        SET R1 FOR REQUEST
         IEFSSREQ
         LR    R7,R15                       SAVE RETURN CODE IN REG15
         MODESET MODE=PROB,KEY=NZERO
         SLR   R0,R0
         SVC   255                          GET UNAUTHORIZED
******************************************
*  CHECK TO SEE IF SUBSYSTEM REQUEST WAS *
*  SUCCESSFULL.                          *
******************************************
         LR    R15,R7                       PUT IEFSSREQ BACK IN R15
         LTR   R15,R15                      WAS REQUEST ISSUED?
         BNZ   NOREQ                            NO, ISSUE ERRMSG
******************************************
*  SET SSOBRETN (SSOB RETURN CODE) IF IT *
*  WAS NON-ZERO.  IF IT WAS ZERO CHECK   *
*  THE FOLLOWING:                        *
*                                        *
*  1.)  JOB OUTPUT ONLY                  *
*  2.)  HELP OUTPUT ONLY                 *
*  3.)  JOB ACTIVE IN NJE                *
*                                        *
*  IF SO THEN SET APPROPRIATE RETURN     *
*  CODE.                                 *
******************************************
         ICM   R7,15,SSOBRETN               IF SSOBRETN IS NE 0 THEN
         BNZ   FREEMAIN                         ISSUE FREEMAIN AND EXIT
         TM    SSCSFLG1,SSCSJACT            JOB CURRENTLY EXECUTING?
         BZ    CHKOUT                          YES, IS IT OUTPUT?
         LA    R7,40                        RC 40 FOR ACTIVE JOB
         B     FREEMAIN                     FREE GETMAINED STORAGE NOW
CHKOUT   TM    SSCSFLG1,SSCSOUTQ            IS THIS JOB OUTPUT ONLY?
         BO    CHKHOLD                         YES, IT IT HELD?
         LA    R7,32                        RC 32 FOR NON-OUTPUT JOBS
         B     FREEMAIN                     FREE GETMAINED STORAGE NOW
CHKHOLD  TM    SSCSFLG1,SSCSHOLD            IS THIS HELD JOB OUTPUT?
         BZ    CHKNJE                          YES, END NOW
         LA    R7,36                        RC 36 FOR NON-HELD OUTPUT
         B     FREEMAIN                     FREE GETMAINED STORAGE NOW
CHKNJE   TM    SSCSFLG1,SSCSNJEA            IS JOB ACTIVE IN NJE?
         BZ    FREEMAIN                        YES, END NOW
         LA    R7,44                        RC 40 FOR NJE ACTIVE JOB
FREEMAIN LA    R0,SSOBLEN2+4                STORAGE TO BE FREED
         FREEMAIN R,LV=(0),A=(6)            FREE GETMAINED STORAGE
         LR    R15,R7                       SET RETURN CODE
FINIS    L     R13,SAVEAREA+4               SYSTEM SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)             REG 15 HAS RETURN CODE
PARMNO   TPUT  NOPARM,L'NOPARM
         B     FINIS
NOREQ    TPUT  REQNO,L'REQNO
         B     FINIS
MOVEID   MVC   JOBID(0),8(R4)
NOPARM   DC    C'INPUT JOBNAME AND/OR JOBID PARAMETER MISSING'
REQNO    DC    C'STATUS REQUEST DID NOT GO TO JES2'
SSOBCHAR DC    C'SSOB'
JOBNAME  DC    CL8' '
JOBID    DC    CL8' '
SAVEAREA DS    18F
FOUR     DC    H'4'
SEVEN    DC    H'7'
EIGHT    DC    H'8'
         REGISTER
         PRINT NOGEN
         IEFJESCT
         CVT DSECT=YES
         PRINT GEN
         IEFJSSOB (CS),CONTIG=YES
         END   JOBSTAT
./       ADD   NAME=ROUTE
ROUTE    CSECT
 TITLE 'ISSUE OPERATOR JES2 ROUTE ($RALL) COMMAND'
**********************************************************************
* NAME       -   ROUTE                                               *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   PROGRAM ISSUED MVS/JES2 OPERATOR COMMAND FROM       *
*                PASSED PARAMETER.                                   *
*                                                                    *
* INPUT      -   INPUT PARAMETER IS OPERATOR COMMAND.                *
*                                                                    *
* OUTPUT     -   SET RETURN CODE IN REGISTER 15                      *
*                                                                    *
* SYSTEM     -   MVS SP1.3 - MVS/XA 1.0                              *
*                                                                    *
* DATE       -   05/10/84                                            *
*                                                                    *
* MACROS     -   MODESET                                             *
*                                                                    *
* REGISTERS  -      R0   ZERO FOR SVC 34 ISSUANCE                    *
*                   R1   PARM ADDRESS AND ADDRESS OF ACTUAL COMMAND  *
*                   R2   **** UNUSED BY PROGRAM ****                 *
*                   R3   USED IN ENTRY SAVE LOGIC                    *
*                   R4   INPUT PARAMETER COMMAND LENGTH              *
*                   R5   LENGTH OF COMMAND AREA MINUS 1 FOR EXECUTE  *
*                   R6   ACTUAL PARAMETER COMMAND ADDRESS            *
*                   R7   ADDRESS OF ACTUAL COMMAND                   *
*                   R8   RETURN CODE FROM SVC 34                     *
*                   R9   **** UNUSED BY PROGRAM ****                 *
*                   R10  **** UNUSED BY PROGRAM ****                 *
*                   R11  **** UNUSED BY PROGRAM ****                 *
*                   R12  BASE REGISTER                               *
*                   R13  **** UNUSED BY PROGRAM ****                 *
*                   R14  **** UNUSED BY PROGRAM ****                 *
*                   R15  RETURN CODE FROM SVC 34                     *
*                                                                    *
* LANGUAGE   -    OS/VS ASSEMBLER                                    *
*                                                                    *
* PRIVILEGES -    SUPERVISOR STATE - KEY 0 PROCESSING                *
*                                                                    *
**********************************************************************
         STM   R14,R12,12(R13)              SAVE REGISTERS
         LR    R12,R15                      ENTRY POINT ADDRESS LOAD
         USING ROUTE,R12                    SET ADDRESSABILITY
         LR    R3,R13                       SAVE SYSTEM ADDRESS
         ST    R13,SAVEAREA+4               SYSTEM ADDR IN MY SAVEAREA
         LA    R13,SAVEAREA                 ADDRESS OF MY SAVEAREA
         ST    R13,8(R3)                    MY SAVEAREA PUT IN SYSTEM
         L     R4,0(R1)                     LOAD DATA AREA ADDRESS
         CLC   0(2,R4),=H'0'                IS THERE A MESSAGE
         BE    FINIS                        NO, EXIT NOW
         LH    R5,0(0,R4)                   LOAD PARM LENGTH TO R5
         LA    R6,2(R4)                     LOAD PARM ADDRESS TO R6
         CLC   0(L'$RALL,R6),$RALL          IS THIS A ROUTE COMMAND
         BNE   FINIS                           NO SNEAK AWAY
         LA    R7,CMD                       GET COMMAND ADDRESS
         XC    0(80,R7),0(R7)               CLEAR THE COMMAND AREA
         BCTR  R5,0                         DECREMENT 1 FOR EXECUTE
         EX    R5,MOVECMD                   MOVE THE COMMAND
         OC    CMD(80),BLANKS               CAPITALIZE EVERYTHING
         LA    R0,1                         PREPARE TO GET
         SVC   255                          GET APF AUTHORIZED
         MODESET MODE=SUP,KEY=ZERO          SUPERVISOR STATE KEY 0
         SLR   R1,R1                        ZERO REGISTER 1
         LA    R1,MESSAGE                   ADDRESS OF MESSAGE
         SLR   R0,R0                        ZERO REGISTER 0
         SVC   34                           ISSUE OPERATOR COMMAND
         LR    R8,R15                       SAVE SVC 34 RETURN CODE
         MODESET MODE=PROB,KEY=NZERO        PROBLEM STATE KEY 8
         SLR   R0,R0                        PREPARE TO GET
         SVC   255                          GET UNAUTHORIZED
         LR    R15,R8                       SET RETURN CODE
FINIS    L     R13,SAVEAREA+4               SYSTEM SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)             RESTORE REGISTERS
MOVECMD  MVC   0(0,R7),0(R6)                MOVE MESSAGE
         REGISTER
SAVEAREA DS    18F
MESSAGE  DC    0D'0'
         DC    AL2(CMDL)                    COMMAND LENGTH
         DC    XL2'00'                      FILLER
CMD      DC    CL80' '
BLANKS   DC    CL80' '                      TO BLANK OUT CMD AREA
CMDL     EQU   L'CMD
$RALL    DC    C'$RALL'                     OPERATOR ROUTE CMD ONLY
         END   ROUTE
