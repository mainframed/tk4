BLDO     CSECT
* FUNCTION
         SPACE 1
*        BLDO BUILDS THE OCCURRENCE FILES WHICH ARE USED TO
*              CONSTRUCT THE QUANDRY MAP AND INDEX FILES.
         SPACE 1
* INPUT
*        FILE(ERIC)--THE DIRECT-ACCESS (DA) FILE OF ERIC CITATIONS
*              IN QUANDRY FORMAT, PROCESSED AS PHYSICAL SEQUENTIAL.
*              ON EACH PASS, THE AUTHOR, DESCRIPTOR, AND IDENTIFIER
*              FIELDS ARE SEARCHED FOR AUTHORS, ETC. WITHIN AN
*              ALPHABETIC INTERVAL.  THE FORMAT OF FILE(ERIC)
*              IS AS FOLLOWS:
*                   RECLEN   DS H   RECORD LENGTH EXCLUDING THIS FIELD
*                   DATALEN  DS H   LENGTH OF DATA PORTION OF RECORD
*                   TTRECNO  DS H   TTR TRACK OF RECORD IN FILE(ERIC)
*                   RRECNO   DS H   TTR RECORD OF RECORD IN FILE(ERIC)
*                   FILLER   DS 4H
*                   OFFSETS  DS 15H OFFSETS OF EACH FIELD INTO DATA
*                                   OFFSETS+28 IS IDENTIFIER OFFSET
*                                   OFFSETS+26 IS DESCRIPTOR OFFSET
*                                   OFFSETS+24 IS AUTHOR OFFSET
*                   DATA     DS CL(DATALEN) ERIC CITATION FIELDS
         SPACE 1
* OUTPUT
*        FILE(MAJOR)--OCCURRENCE RECORDS FOR EACH MAJOR DESCRIPTOR
*              AND IDENTIFIER (THOSE BEGINNING WITH AN ASTERISK).
*              ONE RECORD IS WRITTEN WHENEVER A MAJOR DESCRIPTOR
*              OR IDENTIFIER OCCURS IN A RECORD.  THE FORMAT OF
*              FILE(MAJOR) IS AS FOLLOWS:
*                   TTRECNO  DS H    TTR TRACK IN FILE(ERIC)
*                   RRECNO   DS H    TTR RECORD IN FILE(ERIC)
*                   DESCRIPT DS CL32 AUTHOR, DESC, OR IDENT
*                   ASTERISK DC C'*' INDICATES MAJOR
*        FILE(MINOR)--OCCURRENCE RECORDS FOR EACH MINOR DESCRIPTOR
*              AND IDENTIFIER (THOSE NOT BEGINNING WITH AN ASTERISK),
*              AND EACH AUTHOR.  (AUTHORS ARE NEVER STARRED.)  THE
*              FORMAT IS IDENTICAL TO FILE(MAJOR) EXCEPT THAT THE
*              ASTERISK IS REPLACED BY A BLANK.
*        FILE(SYSPRINT)--THE FOLLOWING STATISTICS ARE PRINTED:
*                   OCCURRENCES-NUMBER OF OCCURRENCE RECORDS WRITTEN
*                   RECORDS-----NUMBER OF RECORDS READ IN FILE(ERIC)
         EJECT
         REGS
         SPACE 1
         BEGIN
         OPEN  (MAJOR,OUTPUT,MINOR,OUTPUT,SYSPRINT,OUTPUT)
         OPEN  ERIC               OPEN INPUT FILE
         EJECT
LOOP     GET   ERIC               READ ERIC RECORD
         ST    R1,RECAD           SAVE RECORD ADDRESS
         AP    RECCT,=P'1'        INCREMENT RECORD COUNT
         MVC   RECNO(4),4(R1)     COPY RECORD NUMBER
         SPACE 1
         LR    R4,R1              COPY RECORD ADDRESS
         AH    R4,30(R1)          INDEX PAST IDENTIFIERS
         MVI   0(R4),X'00'        TERMINATE IDENTIFIERS
         AH    R1,28(R1)          INDEX TO IDENTIFIERS
         CR    R4,R1              TEST FOR PRESENCE OF IDENTIFIERS
         BNH   *+8                BRANCH IF NO IDENTIFIER FIELD
         BAL   R10,SCANFLD        GET IDENTIFIERS
         SPACE 1
         L     R1,RECAD           RESTORE RECORD ADDRESS
         LR    R4,R1              COPY RECORD ADDRESS
         AH    R4,28(R1)          INDEX PAST DESCRIPTORS
         MVI   0(R4),X'00'        TERMINATE DESCRIPTORS
         AH    R1,26(R1)          INDEX TO DESCRIPTORS
         BAL   R10,SCANFLD        GET DESCRIPTORS
         SPACE 1                                                     50
         L     R1,RECAD           RESTORE RECORD ADDRESS             50
         LR    R4,R1              COPY RECORD ADDRESS                50
         AH    R4,26(R1)          INDEX PAST AUTHORS                 50
         MVI   0(R4),X'00'        TERMINATE AUTHORS                  50
         AH    R1,24(R1)          INDEX TO AUTHORS                   50
         CR    R4,R1              TEST FOR PRESENCE OF AUTHORS       50
         BNH   *+8                BRANCH IF NO AUTHOR FIELD          50
         BAL   R10,SCANFLD        GET AUTHORS                        50
         B     LOOP               PROCESS NEXT RECORD
         EJECT
SCANFLD  LR    R3,R1              POINT TO FIRST FIELD
BLD      TRT   0(256,R3),TRTBL    TRT FOR SEMICOLON
         LR    R9,R1              SAVE ADDRESS OF DELIMITER
         MVI   PREFIX,X'40'       ASSUME MINOR
         CLI   0(R3),C'*'         TEST
         BNE   *+12               IF MINOR
         MVI   PREFIX,C'*'        FLAG AS MAJOR
         LA    R3,1(R3)           POINT PAST C'*'
         LR    R2,R9              LOAD ADDRESS OF DELIMITER
         SR    R2,R3              CALCULATE LENGTH OF DESCRIPTOR
         BZ    NEXTDESC           TO SKIP BAD RECORD
         BCTR  R2,0               REDUCE COUNT FOR EXECUTE
         C     R2,=A(DESCLEN-1)   TEST FOR EXCESSIVE ENTRY LENGTH
         BNH   *+8                BRANCH IF NOT
         LA    R2,DESCLEN-1       ELSE TRUNCATE DESCRIPTOR
         MVC   ENTRY(DESCLEN),=CL80' ' BLANK THE ENTRY
         EX    R2,MVCENTRY        MOVE TO WORK AREA
         MVI   ENTRY+DESCLEN-1,C' ' BLANK LAST BYTE                  31
         CLI   ENTRY,C'A'         TEST FOR                           50
         BNE   *+14                'AND OTHERS'                      50
         CLC   ENTRY(DESCLEN),ANDOTHRS IN THE AUTHOR FIELD           50
         BE    NEXTDESC                 AND SKIP IF SO               50
         AP    OCCURCT,=P'1'      COUNT OCCURRENCE
         LA    R1,MAJOR           ASSUME MAJOR
         CLI   PREFIX,C'*'       TEST ASSUMPTION
         BE    *+8                BRANCH IF SO
         LA    R1,MINOR           ELSE POINT TO MINOR DCB
         PUT   (1),RECNO          WRITE OCCURRENCE RECORD
NEXTDESC LA    R3,2(R9)           POINT TO NEXT DESCRIPTOR (PAST C'; ')
         CLI   0(R9),X'00'        TEST FOR END OF DESCRIPTORS
         BNE   BLD                PROCESS NEXT DESCRIPTOR IF NOT
         BR    R10                RETURN IF AT END
         EJECT
EODAD    LA    R2,OCCURCT         POINT TO FIRST TOTAL FIELD
PRINTSUM UNPK  PRINTNUM,0(4,R2) MOVE TOTAL
         OI    PRINTNUM+L'PRINTNUM-1,X'F0' TO PRINT LINE
         MVC   PRINTHDR,4(R2)     MOVE TITLE TO PRINT LINE
         PUT   SYSPRINT,PRINTHDR  WRITE LINE
         LA    R2,17(R2)          POINT TO NEXT STATISTIC
         CLI   0(R2),X'FF'        TEST AT END
         BNE   PRINTSUM           PROCESS IF NOT
         SPACE 1
         CLOSE (MAJOR,,MINOR,,ERIC,,SYSPRINT) CLOSE FILES
         FINISH
         SPACE 3
DESCLEN  EQU   32
         CNOP  4,8                ALIGN ENTRY ON A DOUBLEWORD
RECNO    DS    F'0',0CL(DESCLEN),0CL1
ENTRY    DC    XL(DESCLEN)'00'    AUTHOR, DESC, OR IDENT
PREFIX   DC    C' '               BLANK OR ASTERISK
         SPACE 1
OCCURCT  DC    PL4'0',CL13'OCCURRENCES'
RECCT    DC    PL4'0',CL13'RECORDS'
         DC    X'FF'
PRINTHDR DC    CL13' '
PRINTNUM DC    CL7'0000000'
MVCENTRY MVC   ENTRY(0),0(R3)
ANDOTHRS DC    CL(DESCLEN)'AND OTHERS'                               50
         EJECT
         PRINT NOGEN
ERIC     DCB   DDNAME=ERIC,DSORG=PS,MACRF=GL,EODAD=EODAD
MAJOR    DCB   DDNAME=MAJOR,DSORG=PS,MACRF=PM,RECFM=FBS,               X
               LRECL=4+DESCLEN+1,BLKSIZE=100*(4+DESCLEN+1)
MINOR    DCB   DDNAME=MINOR,DSORG=PS,MACRF=PM,RECFM=FBS,               X
               LRECL=4+DESCLEN+1,BLKSIZE=100*(4+DESCLEN+1)
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=F,BLKSIZE=20
TRTBL    DC    0D'0',XL256'00'
         ORG   TRTBL
         DC    X'08'
         ORG   TRTBL+C';'         SEMICOLON
         DC    X'04'
         ORG
         LTORG
RECAD    DS    A
         END   BLDO
