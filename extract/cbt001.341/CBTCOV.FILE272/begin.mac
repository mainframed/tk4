         MACRO
&NAME    BEGIN &TYPE=,&BASE=12,&STAE=NO
         LCLA  &N
         AIF   ('&STAE' EQ 'NO' OR '&STAE' EQ 'YES' OR '&STAE'         X
               EQ 'Y').OK1
         MNOTE 4,'INVALID STAE PARAMETER'
         MEXIT
.OK1     ANOP
&NAME    STM   14,12,12(13) "SAVE CALLER'S REGISTERS
         AIF   (T'&TYPE EQ 'O').B2   BRANCH IF 'TYPE' OMITTED
         AIF   ('&BASE' NE '13').OK3  MAKE SURE NOT USING REG 13
         MNOTE 4,'INVALID BASE REGISTER'
         MEXIT
.OK3     BALR  &BASE,0 "         ESTABLISH BASE
         USING *,&BASE
         AIF   ('&TYPE' EQ 'RENT').B1  BRANCH IF RE-ENTRANCY DESIRED
         LA    14,&TYPE "     LOAD ADDRESS OF SAVE AREA
         AGO   .B5                USE COMMON ENDING
.B1      ANOP
         GETMAIN R,LV=72          GET CORE FOR SAVE AREA
         LR    14,1 "        HOLD ADDRESS OF GOTTEN CORE
         LM    0,1,20(13) "  RESTORE REGS DESTROYED BY GETMAIN
         AGO   .B5                USE COMMON ENDING
.B2      ANOP
&N       SETA  80
         AIF   ('&STAE' EQ 'NO').B3 BRANCH IF NO STAE
&N       SETA  &N+8          ALLOW SPACE FOR REL 20 STAE PARM LIST
.B3      BAL   14,&N.(,15) " BRANCH AROUND SAVE AREA
         USING *,13 "        REGISTER 13 ASSUMED AS BASE
         DS    18F "         SAVE AREA
         AIF   ('&STAE' EQ 'NO').B5  BRANCH IF NO STAE
         DC    V(XDUMP) "    *RELEASE 20.6 STAE
         DC    A(0) "        *PARAMETER LIST
         ST    14,8(,13) "   LINK UP CALLER'S SAVE AREA
         ST    13,4(,14) "   WITH MY SAVE AREA
         LR    13,14
         LA    1,72(,13) "   POINT TO STAE PARM LIST
         AGO   .B6                USE COMMON ENDING
.B5      ST    14,8(,13) "   LINK UP CALLER'S SAVE AREA
         ST    13,4(,14) "   WITH MY SAVE AREA
         LR    13,14
         AIF   ('&STAE' EQ 'NO').END  EXIT IF NO STAE
         CNOP  0,4
         BAL   1,*+12 "      BRANCH AROUND PARM LIST
         DC    A(0) "        *PARAMETER LIST
         LA    1,0(,1) "     CLEAR HIGH-ORDER BYTE  (REL 20.6)
.B6      SR    0,0 "         INDICATE CREATE OPTION (REL 20.6)
         SVC   60 "          ISSUE STAE SVC         (REL 20.6)
.END     MEND
