         MACRO
&NAME    FINISH &SAVE=13,&TYPE=,&RC=
         AIF   (T'&TYPE EQ 'O').GO1 BRANCH IF PARAMETER OMITTED
         AIF   ('&TYPE' NE 'RENT').ERROR
&NAME    LR    1,&SAVE *          LOAD ADDRESS OF AREA TO BE FREED
         LA    0,72(0,0) *        LOAD LENGTH OF AREA
         FREEMAIN R,LV=(0),A=(1)
         L     13,4(&SAVE) *      LOAD REG 13 WITH CALLER'S SAVE AREA
         AGO   .GO2
.GO1     ANOP
&NAME    L     13,4(&SAVE) *         LOAD REG 13 WITH SAVE AREA ADDR
.GO2     ANOP
         AIF  (T'&RC EQ 'O').NORC
         AIF   ('&RC' EQ '(15)').RCSET
         LA    15,&RC.(0,0) *       LOAD REG 15 WITH RETURN CODE
.RCSET   LM    0,12,20(13) *      RESTORE REGS 0 THRU 12
         L     14,12(13) *        LOAD RETURN ADDRESS
         AGO   .GO3
.NORC    LM    14,12,12(13) *     RESTORE REGS 14 THRU 12
.GO3     BR    14 *               RETURN
         AGO   .END
.ERROR   MNOTE 12,'INVALID TYPE SPECIFICATION'
.END     MEND
