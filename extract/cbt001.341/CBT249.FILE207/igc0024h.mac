* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*   THIS SVC (248 TYPE 4) IS USED IN F.S.E. EDITOR TO
*    A) GIVE ISSUING CODE KEY ZERO SUPERVISOR MODE ABILITY
*       IN ORDER FOR "FSEPRTY" MODULE TO EXECUTE
*    B) WITH ENTRY REGISTER 1 =X'FFFFFFFF' TO EXECUTE
*       TCAM REPOSITIONING CODE TO BYPASS CURSOR JUMPING.
*       NOTE: IT IS ONLY ISSUED FOR THE LATTER FUNCTION
*       IN A NON-MVS ENVIRONMENT.(SEE REASONS IN PROLOGUE
*       IN MODULE "FSE")
*
*    THIS SVC (248) IS A TYPE 4 DISABLED.
*
*           LVL 1.00  01/21/80      BRUNO LALICATA CITIBANK
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
R0       EQU  0
R1       EQU  1
R2       EQU  2
R3       EQU  3
R4       EQU  4
R5       EQU  5
R6       EQU  6
R7       EQU  7
R8       EQU  8
R9       EQU  9
R10      EQU  10
R11      EQU  11
R12      EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
IGC0024H CSECT
         ENTRY IGC0024H
*
         USING IGC0024H,R6            R6 HAS ENTRY ADDRESS
         C     R1,FF                  TCAM FLAG?
         BE    TCAMCODE               YES, GO EXECUTE
         STC   R0,0(R15)              NO, ZAP JSCB
         BC    15,RETURN              RETURN TO ISSUER
TCAMCODE L     5,16                   A(CVT)
         L     5,0(5)                 A(OLD/NEW TCB)
         L     5,4(5)                 A(MYTCB)
         L     5,180(5)               A(MY JSCB)
         MVC   TJID(2),268(5)         SAVE TJID
         L     R2,16                  ADD(CVT)
         L     R2,X'F0'(R2)           ADD(ADD(TCAM AVT))
         L     R2,0(R2)               ADD(TCAM AVT)
         ICM   R2,15,424(R2)          ADD(TERMNAME TABLE)
         LH    R3,44(R2)              NUMBER OF TERMINALS GENNED IN MCP
         LA    R4,82(R2)              POINT TO OFFSET FOR ENTRIES: EACH
         XR    R5,R5                    ENTRY HAS NAME + ADDRESS OF QCB
         IC    R5,40(R2)              LENGTH OF EACH TERMINAL NAME
LOOP     LR    R7,R4                  SAVE BEGGINNING ADDRESS
         AR    R7,R5                  ADD THE LENGTH OF NAME
         BCTR  R7,0                   SUBTRACT FOR 4 BYTE LOAD
         L     R7,0(R7)               ADD(TERMINAL TABLE ENTRY)
         L     R7,0(R7)               ADD(DESTINATION QCB FOR CRT)
         CLC   30(2,R7),TJID          CHECK TO SEE IF THIS IS RIGTH
         BNE   GETNEXT                  TERMINAL BY MATCHING TJID"S
*                                     NO, GO GET NEXT DEST. QCB
*        TPUT  CLEARSCR,8,FULLSCR,HOLD  YES, CLEAR SCREEN
         MVI   21(R7),X'00'           MOVE 0 TO CARRIAGE COUNT BYTE
         BC    15,RETURN              GOOD WORK..RETURN TO BASE...
*
GETNEXT  AR    R4,R5                  ADD TERMINAL NAME LENGTH
         LA    R4,3(R4)               ADD 3 BYTES FOR QCB ADDRESS
         BCT   R3,LOOP                GO SCAN AGAIN FOR NEXT QCB
*
RETURN   BR    R14                    RETURN TO SVC ISSUER
*
CLEARSCR DC   X'C11140403C404000'     THIS IS DC FOR SCRREN CLEAR
TJID     DS   H
ID       DC   7C' '
         DS   0F
FF       DC   X'FFFFFFFF'
*
         END  IGC0024H
