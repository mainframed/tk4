* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*   THIS ROUTINE IS LINKED FROM MAIN DRIVER "FSE" WHEN A COMMAND
*   WAS ENTERED THAT WAS FOUND TO BE A NON-FSE SUBCOMMAND.
*   THE FIELD WHERE THE COMMAND WAS ENTERED IS SCANNED FOR VALIDITY
*   AND IF A VALID COMMAND NAME WAS ENTERED, A LINK IS ISSUED
*   FOR IT.  S806 ABENDS WILL BE PROCESSED BY THE "ABEND01" CSECT
*   IN "FSE" IF A NON-FOUND CONDITION IS TRUE.
*
*   AT ENTRY R7 POINTS TO THE FIELD WHERE THE COMMAND WAS ISSUED,
*   AND R8 POINTS TO C.P.P.L. LIST WHICH IS PASSED TO THE COMMAND
*   TO BE INVOKED VIA R1.
*
*   WHEN CONTROL IS RETURNED FROM THE LINKED -SUBCOMMAND- , THE
*   SCREEN IS CLEARED  AND A TGET IS ISSUED TO ALLOW USER TO
*   RETURN TO "FSE" BY PRESSING ENTER.
*
*   BRUNO LA LICATA         LVL 1.00     11 / 11 / 77
*   N.B.N.A.
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
FSETSO   CSECT
R0       EQU  0
R1       EQU  1
R2       EQU  2
R3       EQU  3
R4       EQU  4
R5       EQU  5
R6       EQU  6
R7       EQU  7
R8       EQU  8
R9       EQU  9
R10      EQU  10
R11      EQU  11
R12      EQU  12
R13      EQU  13
R14      EQU  14
R15      EQU  15
*
         STM  R14,R12,12(R13)     SAVE
         LR   R12,R15
         USING FSETSO,12              REGISTERS
         ST   R13,SAVE+4
         LA   R5,SAVE                       AND SET
         ST   R5,8(R13)
         LR   R13,R5                               ADDRESSABILITY
         MVC COMMAND(76),=76C' '      CLEAR COMMAND NAME
         MVC LINKEP(8),COMMAND        CLEAR MODULE NAME
*
*  R7 =ADDRESS(INPUT LINE AT START OF COMMAND)
*  R8 =ADDRESS(CPPL)
*
         TCLEARQ OUTPUT                   CLEAR OUT Q
         TPUT    CLEAR,8,FULLSCR,HOLD     CLEAR SCREEN
*
         LR   R9,R7     SAVE BEGINNING ADDR.
         LA   R10,76    MAX SCAN LENGTH
         XR   R4,R4
TSOLOOP1 CLI  0(R7),C' '                  SCAN FOR
         BE   CHECKLEN                      END
         LA   R7,1(R7)                         OF
         LA   R4,1(R4)                           COMMAND
         BCT  R10,TSOLOOP1                           NAME
CHECKLEN C    R4,=F'8'                 LENGTH LARGER THAN MAX?
         BNH  LENGTHOK                 NO, OK
         TPUT LENGTHMS,20              YES, ISSUE MESSAGE
         B    RETURN                        AND RETURN TO FSE
LENGTHOK BCTR R4,0                     MOVE
         EX   R4,MOVENAME                  COMMAND
         B    *+10                             NAME
MOVENAME MVC  LINKEP(0),0(R9)                     TO AREA
         LA   R4,1(R4)                 LOAD BACK FOR THE EX BCTR
         STH  R4,COMMLEN               SAVE LENGTH
         XR   R4,R4
TSOLOOP2 CLC  0(5,R7),=5C' '           SCAN FOR END OF PARM
         BE   NOSECOND                 IF SO, SKIP PROCESSING
         CLI  0(R7),C' '               BYPASS
         BNE  READPARM                    BLANKS
         LA   R7,1(R7)                       FOR A
         LA   R4,1(R4)                         LENGTH
         BCT  R10,TSOLOOP2                         OF < 5
NOSECOND MVC  BLANKLEN(2),=H'0'        ONLY COMMAND NAME SPECIFIED
         MVC  SUBPARML(2),BLANKLEN     SAVE LENGTHS OF ZEROES
         B    COMPUTE                  GO MAKE UP FAKE C.B.
READPARM STH  R4,BLANKLEN              SAVE LENGTH OF MIDDLE BLANKS
         XR   R4,R4
TSOLOOP3 CLC  0(5,R7),=5C' '           END OF PARMS?
         BE   ENDOFPRM                 YES
         LA   R7,1(R7)                 NO, KEEP LOOKING
         LA   R4,1(R4)                     AND ADDING
         BCT  R10,TSOLOOP3
ENDOFPRM STH  R4,SUBPARML              THIS SECTION WILL USE
COMPUTE  LH   R4,COMMLEN               THE  LENGTHS OF 1)COMMAND
         AH   R4,BLANKLEN              NAME 2)BLANKS FROM END OF
         STH  R4,PARMOFFS              COMMAND NAME TO START OF
         AH   R4,SUBPARML              SUBPARAMETERS IN ORDER TO
         LA   R4,4(R4)                 MOVE THE LENGTHS IN A FAKE
         STH  R4,TOTLEN                COMMAND BUFFER TO BE USED
         SH   R4,=H'4'                 IN PASSING C.P.P.L. VALUES
         BCTR R4,0                     TO PROGRAM.
         EX   R4,MOVEALL
         B    *+10
MOVEALL  MVC  COMMAND(0),0(R9)         MOVE FIELDS IN FAKE C.B.
         LM   R2,R5,0(R8)              STORE ALL C.P.P.L. VALUES
         STM  R2,R5,CB                 EXCEPT C.B.
         LA   R2,FAKECB                USE FAKE ONE
         ST   R2,CB                       TO STORE IT AS
         LA   R1,CPPL                         PARMS IN R1
         LINK EPLOC=LINKEP             LINK MODULE
         TPUT PROMPT,28
         TCLEARQ INPUT                 WAIT FOR USER TO PRESS
         TGET COMMAND,1                ENTER TO RETURN BACK
*
*
RETURN   L    R13,4(R13)               TO "FSE".
         LM   R14,R12,12(R13)
         BR   R14
PROMPT   DC   C'PRESS ENTER TO RETURN TO FSE'
         DS   0F
SAVE     DS   18F
CLEAR    DC   X'C11140403C404000'
FAKECB   DS   0F
TOTLEN   DS   H
PARMOFFS DS   H
COMMAND  DC   76C' '
         DS   0H
COMMLEN  DS   H
BLANKLEN DS   H
SUBPARML DS   H
LINKEP   DC   8C' '
LENGTHMS DC   C'INVALID COMMAND NAME'
CPPL     DS   0F
CB       DS   F
UPT      DS   F
PSCB     DS   F
ECT      DS   F
*
         END
