         MACRO
&NAME    RSAVE &SA,&BASE,&WORK
         GBLC  &SAVENAM,&MBASE
         LCLC  &MWORK
         AIF   ('&SA' NE '').AA
&SAVENAM SETC  '$SVE'.'&SYSNDX'
         AGO   .BB
.AA      ANOP
&SAVENAM SETC  '&SA'
.BB      ANOP
         AIF   ('&MBASE' EQ '').Q4
         DROP  &MBASE DROP OLD BASE FOR SAFETY
.Q4      ANOP
         AIF   ('&BASE' EQ '').CC
&MBASE   SETC  '&BASE'
         AGO   .DD
.CC      ANOP
&MBASE   SETC  '12'
.DD      ANOP
         AIF   ('&WORK' EQ '').EE
&MWORK   SETC  '&WORK'
         AGO   .FF
.EE      ANOP
&MWORK   SETC  '11'
.FF      ANOP
         AIF   ('&NAME' EQ '').Q2
         ENTRY &NAME MAKE NAME KNOWN EXTERNALLY
.Q2      ANOP
         CNOP  0,4
&NAME    STM   14,12,12(13) SAVE REGISTERS
         BALR  &MBASE,0 ESTABLISH BASE
         USING *,&MBASE ESTABLISH ADDRESSABILITY
         LA    &MWORK,&SAVENAM LOAD SAVE AREA ADDRESS
         ST    &MWORK,8(13) STORE IN CALLING PROGRAM
         ST    13,&SAVENAM+4 STORE ADDRESS OF CALLERS SAVE AREA
         LR    13,&MWORK ESTABLISH SAVE AREA POINTER
         B     *+76 BRANCH AROUND SAVE AREA
&SAVENAM DS    18F REGISTER SAVE AREA
         MEND
