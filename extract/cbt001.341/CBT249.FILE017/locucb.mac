         MACRO
&NAME    LOCUCB &UNIT=,&VOLUME=,&NOFIND=
         LCLC  &XX1,&XX2
&XX1     SETC  'IHB'.'&SYSNDX'.'1'
&XX2     SETC  'IHB'.'&SYSNDX'.'2'
         AIF   ('&UNIT' EQ '' AND '&VOLUME' EQ '').E1
         AIF   ('&NOFIND' EQ '').E2
         AIF   ('&NAME' EQ '').NONAM
&NAME    DS    0H
.NONAM   ANOP
         SR    R14,R14        SET TO ZERO
         BCTR  R14,0          LESS 1 MAKES ALL FOXES
         L     R1,16          CVT POINTER
         L     R1,40(,R1)     UCB LOOKUP TABLE
&XX1     DS    0H
         CH    R14,0(,R1)     END OF LOOKUP TABLE??
         BE    &NOFIND        YES, ERROR END
         LH    R15,0(,R1)     UCB ADDRESS
         AIF   ('&UNIT' EQ '').MVOL
         CLC   13(3,R15),&UNIT IS THIS IT??
         BE    &XX2           YES, PROCESS
.MVOL    AIF   ('&VOLUME' EQ '').CONT
         CLC   28(6,R15),&VOLUME IS THIS IT??
         BE    &XX2           YES, PROCESS
.CONT    ANOP
         LA    R1,2(,R1)      NEXT UCB ADDRESS
         B     &XX1           LOOP BACK
&XX2     DS    0H
         MEXIT
.E1      MNOTE 8,'*** ERROR, EITHER UNIT OR VOLUME MUST BE SPECIFIED'
         MEXIT
.E2      MNOTE 8,'*** ERROR, NOFIND ADDRESS MUST BE SPECIFIED'
         MEND
