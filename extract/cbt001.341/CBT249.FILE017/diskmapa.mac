DISKMAPA TITLE 'ALLOCATE ONLINE DASD AND INVOKE DISKMAP'
***********************************************************************
* PURPOSE: THIS ROUTINE ALLOCATES EACH ONLINE                         *
*          DASD VOLUME THEN INVOKES THE DISKMAP PROGRAM TO GET A MAP  *
*          OF THE VOLUME(S)                                           *
*                                                                     *
***********************************************************************
DISKMAPA CSECT
         USING *,R15
         SAVE (14,12),,DISKMAPA_&SYSDATE_&SYSTIME
         ST   R1,SAVER1
         ST   R13,SAVE+4
         LA   R2,SAVE
         ST   R2,8(,R13)
         LR   R13,R2
         LR   R12,R15
         USING DISKMAPA,R12
         DROP R15
         L     3,16
         L     4,40(3)
GOGO     LH    5,0(4)
         CH    5,FFFFS
         BE    MAPIT
         CH    5,ZEROS
         BE    UPCNT
         CLI   18(5),X'20'     SEE IF DA
         BNE   UPCNT
         CH    5,TEST
         BNH   UPCNT
         STH   5,TEST
         TM    3(5),B'10000000'
         BO    GOTONE
         TM    6(5),B'01000000'
         BO    UPCNT
GOTONE   EQU   *                 28(5) = VOLSER
         CLI   28(R5),C'A'       ENSURE VOLSER EXISTS
         BNH   UPCNT
         LH    R2,DDCOUNT
         LA    R2,1(,R2)
         STH   R2,DDCOUNT
         CVD   R2,WORK
         UNPK  DDNUM(3),WORK+6(2)
         OI    DDNUM+2,C'0'
         MVC   VOLSER,28(R5)
         LA    R1,PARMLIST
         L     R15,=V(DYNAM)
         BALR  R14,R15
         LTR   R15,R15
         BZ    UPCNT
         LR    R1,R15
         ABEND (1),DUMP
UPCNT    LA    4,2(4)
         B     GOGO
MAPIT    EQU   *
         L     R1,SAVER1
         LINK  EP=DISKMAP
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
         DC    H'0'
WORK     DS    D
SAVE     DS    18F
SAVER1   DS    F
TEMP     DS    F
ZEROS    DC    X'0000'
FFFFS    DC    X'FFFF'
TEST     DC    H'0'
DDCOUNT  DC    H'0'
PARMLIST DC    A(PARM1,PARM2),XL1'80',AL3(PARM3)
PARM1    DC    F'0'
PARM2    DC    C'ALLOC '
PARM3    DC    C'DDNAME=DD'
DDNUM    DC    C'000'
         DC    C' OLD VOLUME='
VOLSER   DC    C'XXXXXX'
         DC    C' UNIT=SYSALLDA ;'
         REGEQU
         END
