MBCHGDTE CSECT
         SAVE  (14,12),,*          SAVE THE WORLD
         BALR  10,0                LOAD BASE REGISTER
         USING *,10                ESTABLISH ADDRESSABILITY
         ST    13,MYSAVE+4         FORWARD
         LR    12,13               AND
         LA    13,MYSAVE           BACKWORD
         ST    13,8(12)            CHAINING
         L     3,0(0,1)   GET ADDRESS OF PARM FIELD
         LH    2,0(3)     MOVE 2 BT LENGTH INTO REGISTER
         LR    4,2                 SAVE LENGTH OF PARM FOR ERROR MSG
         CH    2,=H'0'  WAS PARM FIELD SPECIFIED?
         BH    CHKMAXL  YES-GO CHECK MAX LENGTH OF FIELD
         MVC   MESSAGE(40),=C'PARM FIELD IS MISSING-** NO ACTION TAKEN'
         B     WTLOG   PUT OUT ERROR MESSAGE
CHKMAXL  EQU   *
         CH    2,=H'11'  MAXIMUM LENGTH OF PARM FIELD
         BNH   NEXTEST             BRANCH IF EQUAL OR LESS
ERROR    EQU   *
         MVC   MESSAGE(27),=C'PARM FIELD TOO LONG-PARM IS'
ERROR1   EQU   *
         STC   4,*+5       STORE LENGTH OF PARM IN NEXT INSTRUCTION
         MVC   MESSAGE+40(*-*),2(3)  MOVE IN PARM
         MVC   MESSAGE+60(19),=C' ** NO ACTION TAKEN'
         B     WTLOG                 ISSUE WTL FOR HASP LOG AND RETURN
NEXTEST  EQU   *
         SH    2,SIX   SUBTRACT LENGTH OF KEYWORD AND = SIGN
         CLC   2(5,3),=C'EXPDT'  IS CHANGE FOR EXPIRATION DATE
         BE    EXPIRE              IF SO BRANCH TO HANDLE
         CLC   2(5,3),=C'RETPD'  IS CHANGE FOR RETENTION PERIOD
         BE    RETAIN              IF SO BRANCH TO HANDLE
         MVC   MESSAGE(35),=C'PARM MUST BE EXPDT OR RETPD-PARM IS'
         B     ERROR1              MUST BE IN ERROR--GO HANDLE
EXPIRE   EQU   *
         CH    2,FIVE   JULIAN DATE IS 5 POSITIONS
         BE    AROUND1  IF IT IS 5 BRANCH AROUND
         MVC   MESSAGE(39),=C'EXPDT MUST BE 5 POSITIONS-YYDDD-PARM IS'
         B     ERROR1  MUST BE WRONG--GO HANDLE
AROUND1  EQU   *
         MVC   EXPDT,8(3)   MOVE PARM TO WORK AREA
         NC    NUMTEST,EXPDT       IS IT NUMERIC?
         CLC   NUMTEST,=X'F0F0F0F0F0'
         BE    NEXTSTEP            YES- CONTINUE
         MVC   MESSAGE(35),=C'EXPIRATION DATE NOT NUMERIC-PARM IS'
         B     ERROR1              IF NOT BRANCH TO ERROR RTN
NEXTSTEP EQU   *
         CLC   DAY,=C'366'         IS THE DAY LOGICAL
         BNH   RDJFCB              IF SO GO READ JFCB
         MVC   MESSAGE(29),=C'ONLY 366 DAYS IN YEAR-PARM IS'
         B     ERROR1              IF NOT BRANCH TO ERROR RTN
RETAIN   EQU   *
         CH    2,THREE   RETPD IS 3 POSITIONS
         BE    AROUND2  IF SO BRANCH AROUND
         MVC   MESSAGE(37),=C'RETPD MUST BE 3 POSITIONS-DDD-PARM IS'
         B     ERROR1   MUST BE BAD  GO HANDLE
AROUND2  EQU   *
         MVC   RETPD,8(3)  MOVE PARM TO WORKAREA
         NC    NUMTEST+2(3),RETPD      IS IT
         CLC   NUMTEST,=X'F0F0F0F0F0'   NUMERIC?
         BE    AROUNDIT                YES-CONTINUE
         MVC   MESSAGE(36),=C'RETENTION PERIOD NOT NUMERIC-PARM IS'
         B     ERROR1              IF NOT BRANCH TO ERROR RTN
AROUNDIT EQU   *
         CLC   RETPD,=C'365'       IS THE PERIOD LOGICAL
         BNH   RDJFCB              YES GO READ JFCB
         MVC   MESSAGE(29),=C'ONLY 366 DAYS IN YEAR-PARM IS'
         B     ERROR1              IF NOT BRANCH TO ERROR RTN
RDJFCB   EQU   *
         RDJFCB  (DASD,,VTOC)  READ BOTH JFCB'S
         MVC   DSNAME,JFCB         SAVE DSN
         MVC   VOLUME,JFCB+118     SAVE VOL SER
         MVC   JFCB2+118(6),JFCB+118 MOVE VOL SER TO VTOC
         MVC   JFCB2(44),JFCB2-1  MOVE VTOC DSN FOR CORRECT DEB
         OI    JFCB2+76,X'80'  PUT VTOC JFCB BACK ON JOBQUEUE
OBTAINIT EQU   *
         OBTAIN DSCBLIST           GET DSCB FOR OUT VOLUME
         B     RETCODE(15)         INDEX ON R15 FOR RC FROM OBTAIN
RETCODE  EQU   *
         B     NOPROBLM            NO PROBLEM
         B     NOVOLUME            VOLUME NOT MOUNTED GO HANDLE
         B     NODSCB              DATA SET NOT ON VOLUME GO HANDLE
         B     IOERROR             I O ERROR GO HANDLE
         B     BADPNTR             BAD OBTAIN WORKAREA  GO HANDLE
         B     BADCCHH             BAD ADDRESS OF DSCB (NOT FOR US)
* OBTAIN IS IN SEARCH MODE NOT SEEK MODE - BAD ADDRESS WON'T APPLY
NOVOLUME EQU   *                   FORMAT NO VOL ERROR MSG
         MVC   MESSAGE(33),=C'VOLUME-      -FOR CHNGDATE WAS NOT'
         MVC   MESSAGE+7(6),VOLUME
         MVC   MESSAGE+34(27),=C' MOUNTED ** NO ACTION TAKEN'
         B     WTLOG
NODSCB   EQU   *                   FORMAT NO DATA SET ERROR MSG
         MVC   MESSAGE(9),=C'DATA SET-'
         MVC   MESSAGE+9(44),DSNAME
         MVC   MESSAGE+54(48),=C'CANNOT BE FOUND ON VOL-      -** NO ACX
               TION TAKEN'
         MVC   MESSAGE+77(6),VOLUME
         B     WTLOG
IOERROR  EQU   *                   FORMAT I O ERROR MSG
         MVC   MESSAGE(44),=C'IO ERROR ON VOLUME-      -** NO ACTION TAX
               KEN'
         MVC   MESSAGE+19(6),VOLUME
         B     WTLOG
BADPNTR  EQU   *                   FORMAT BAD WKAREA ERROR MSG
         MVC   MESSAGE(69),=C'BAD WORKAREA POINTER -CONTACT SYSTEMS PROX
               GRAMMING- ** NO ACTION TAKEN'
         B     WTLOG
BADCCHH  EQU   *                   FORMAT BAD DSCB ADDRESS ERROR MSG
         MVC   MESSAGE(72),=C'DSCB NOT IN VTOC EXTENT-CONTACT SYSTEMS PX
               ROGRAMMING- ** NO ACTION TAKEN'
         B     WTLOG
NOPROBLM EQU   *
         OPEN  (VTOC,(OUTPUT)),TYPE=J   OPEN THE VTOC
         TM    VTOC+48,X'10'       CHECK DCB FOR VTOC FOR GOOD OPEN
         BO    CONTINUE    GO ON IF OPEN WAS OK
         MVC   MESSAGE(22),=C'UNSUCCESSFUL OPEN FOR '  FORMAT MESSAGE
         MVC   MESSAGE+23(44),DSNAME                   MOVE IN DSN
         MVC   MESSAGE+68(18),=C'** NO ACTION TAKEN'   FORMAT SOME MORE
         B     WTLOG               PUT OUT MESSAGE AND RETURN
CONTINUE EQU   *
         MVC   OLDATE,WORKAREA+12  SAVE OLD EXPIRATION DATE FOR MSG
         MVC   MBBCCHHR+3(5),WORKAREA+96  MOVE IN CCHHR FROM OBTAIN
         CLI   2(3),C'E'     IS IT FOE EXPIRATION DATE?
         BNE   RETENTN                    NO  BRANCH FOR RET PERIOD
         PACK  DTRACKS+6(2),YR            PACK YEAR OF EXP DATE
         CVB   6,DTRACKS                  CONVERT TO BINARY
         STC   6,WORKAREA+12              STORE RESULT IN WRITE AREA
         PACK  DTRACKS+6(2),DAY           PACK DAY OF EXP DATE
         CVB   6,DTRACKS                  CONVERT IT TO BINARY
         STH   6,HWORD                    STORE RESULT
         MVC   WORKAREA+13(2),HWORD       MOVE NEW DAY INTO WRITE AREA
         B     XDAPER                     BRANCH TO WRITE NEW DATE
RETENTN  EQU   *
         PACK  DTRACKS+6(2),RETPD       PACK RET PERIOD
         CVB   6,DTRACKS                CONVERT IT TO BINARY
         MVC   HWORD,WORKAREA+13        MOVE OLD DATE TO WORK AREA
         AH    6,HWORD                  ADD CURRENT AND NEW RET PERIOD
         CH    6,=H'366'                IS IT BEYOND CURRENT YEAR
         BH    OVERYEAR                 YES GO HANDLE
         STH   6,HWORD                  STORE IN SAVE AREA
         MVC   WORKAREA+13(2),HWORD     MOVE IT TO WRITE WORK AREA
         B     XDAPER                   GO WRITE IT
OVERYEAR EQU   *
         SH    6,=H'366'                SUBTRACT ONE YEAR
         STH   6,HWORD                  SAVE DAYS
         MVC   WORKAREA+13(2),HWORD     STORE DAY IN WRITE BUFFER
         SR    6,6  CLEAR REGISTER 6 FOR INSERT CHAR INSTR
         IC    6,WORKAREA+12            GET OLD YEAR
         LA    6,1(6)                   ADD ONE YEAR
         STC   6,WORKAREA+12            PUT OLD YEAR + 1 IN WRITE BUFF
XDAPER   EQU   *
      ENQ   (QNAME,VOLUME,E,,SYSTEM)  GET CONTROL OF THE VTOC
         XDAP  EVENT,WK,VTOC,WORKAREA,96,(DSNAME,44),MBBCCHH WRITE IT
         WAIT  ,ECB=EVENT          WAIT FOR COMPLETION
         DEQ   (QNAME,VOLUME,,SYSTEM)  RETURN CONTROL OF THE VTOC
         TM    EVENT,X'7F'         WAS IT GOOD I-O
         BO    GOODBYE             YES GO TO USER
         MVC   MESSAGE(65),=C'IO ERROR WHILE UPDATING VTOC ON VOLUME-  X
                   - ** NO ACTION TAKEN'     FORMAT ERROR MSG
         MVC   MESSAGE+39(6),VOLUME
         B     WTLOG                         GO WRITE MSG
GOODBYE  EQU   *
         MVC   LOG(98),=C'*****DSN=                                    X
                        EXPIRATION DATE WAS-     -IS NOW-     -*****'
         MVC   LOG+9(44),DSNAME    PUT DATA SET NAME IN MSG
         SR    6,6  CLEAR REGISTER 6 FOR INSERT CHAR INSTR
         IC    6,OLDATE   GET OLD YEAR IN BINARY
         CVD   6,DTRACKS  CONVERT IT TO DECIMAL
         UNPK  LOG+74(2),DTRACKS+6(2)  UNPACK INTO MSG
         MVC   HWORD,OLDATE+1   MOVE OLD DAY TO HALFWORD
         LH    6,HWORD   PUT IT IN REG FOR CONVERT
         CVD   6,DTRACKS  CONVERT IT TO DECIMAL
         UNPK  LOG+76(3),DTRACKS+5(3)  UNPACK IT IN MSG
         OC    LOG+74(5),NUMTEST  INSURE VALID ZONES
         SR    6,6  CLEAR REGISTER 6 FOR INSERT CHAR INSTR
         IC    6,WORKAREA+12            GET NEW YR IN BINARY
         CVD   6,DTRACKS                COVERT TO DECIMAL
         UNPK  LOG+87(2),DTRACKS+6(2)   PUT IT IN END MSG
         MVC   HWORD,WORKAREA+13        GET NEW DAY IN BINARY
         LH    6,HWORD                  PUT IT IN REGISTER
         CVD   6,DTRACKS                COVERT IT TO BINARY
         UNPK  LOG+89(3),DTRACKS+5(3)   PUT IT IN END MSG
         OC    LOG+87(5),NUMTEST   INSURE VALID ZONES
         B     WRTOLOG   GO WRITE THE MESSAGE
WTLOG    DS    0F
         LA    1,MESSAGE
         LA    0,108
         TPUT  (1),(0),R
         BAL   11,CLOSEM
         L     13,4(13)            RESTORE SAVE AREA POINTER
         RETURN (14,12),RC=15      RETURN WITH RC=15 FOR ERROR
WRTOLOG  DS    0F
         LA    1,LOG
         LA    0,103
         TPUT  (1),(0),R
         BAL   11,CLOSEM
         L     13,4(13)            RESTORE SAVE AREA POINTER
         RETURN (14,12),RC=0       RETURN WITH RC=0
CLOSEM   EQU   *
         TM    VTOC+48,X'10'
         BZ    AR1
         CLOSE (VTOC)
AR1      EQU   *
AR2      BR    11
         DS    0D
MYSAVE   DC    18A(0)
SIX      DC    H'6'
FIVE     DC    H'5'
THREE    DC    H'3'
HWORD    DC    H'0'
EXPDT    DS    0CL5
YR       DS    CL2
DAY      DS    CL3
RETPD    DS    CL3
DSNAME   DC    CL44' '
VOLUME   DC    CL6' '
MBBCCHHR DS    0CL8
MBBCCHH  DS    0CL7
MBB      DC    X'000000'
CC       DS    CL2
HH       DS    CL2
R        DS    CL1
OLDATE   DS    CL3
DTRACKS  DC    D'0'
WORKAREA DS    CL350
NUMTEST  DC    X'F0F0F0F0F0'
QNAME    DC    CL8'SYSVTOC'
         DS    0F
LIST     DC    X'87'
         DC    AL3(JFCB)
JFCB     DS    CL176
         DS    0F
LIST2    DC    X'87'
         DC    AL3(JFCB2)
         DC    X'04040404'
JFCB2    DS    CL176
DSCBLIST CAMLST SEARCH,DSNAME,VOLUME,WORKAREA
LOG      DC    CL103' '
MESSAGE  DC    CL108' '
DASD     DCB   DDNAME=SYSDISK,MACRF=(E),EXLST=LIST
VTOC     DCB   DDNAME=SYSVTOC,MACRF=(E),EXLST=LIST2
         END   MBCHGDTE
