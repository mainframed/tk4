PROC 0 DEBUG FULL
/*-------------------------------------------------------------------*/
/* REFER    : AMS          - AMS FRONT END DRIVER.                   */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/* DOC      : FRONT END DRIVER FOR AMS PROCESS.  ALLOWS DIALOG       */
/*            MANAGER APPLICATION TO INVOKE ACCESS METHOD SERVICES.  */
/*                                                                   */
/*  FILES:   FILENAME(AUTHFILE)                                      */
/*           CONTAINS THE VAILD OPTIONS THE USER WILL BE ALLOWED ON  */
/*           PRIMARY MENU.                                           */
/*                                                                   */
/*           FILENAME(AUTHFILE)                                      */
/*           ALLOWS AN AUDIT TRAIL OF WHAT OPTIONS HAVE BEEN USED.   */
/*           NOT TOTALLY TESTED USE AT YOUR OWN RISK.                */
/*                                                                   */
/*-------------------------------------------------------------------*/
SETCNTL: +
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
    ISPEXEC VPUT DEBUG SHARED
INIT: +
    IF &FULL = &STR(FULL) THEN DO
        ISPEXEC DISPLAY PANEL(AMS@MSTR)
        IF &LASTCC = 8 THEN DO
            EXIT
        END DO
    END DO
    SET &USERID    = &SYSUID
    SET &AUDITUSE  = &STR(N)      /* Y - AUDIT FILE WILL BE UPDATED */
    ISPEXEC VPUT (USERID AUDITUSE)
    SET &AMSDELT = STR(D)
    ISPEXEC VPUT (AMSDELT) SHARED
    FREE F(AUTHFILE)
/* AUTHORIZATION FILE FOR AMS */
    ALLOC F(INFILE1) DA('USER.AMSFILE1.DATA') SHR
    IF &AUDITUSE = &STR(Y) THEN DO
        FREE F(AUDTFILE)
/* AUDIT TRAIL FILE FOR AMS */
        ALLOC F(INFILE2) DA('USER.AMSFILE2.DATA') SHR
    END DO
ALLOUTLS: +
    SET &WRKTIME = &SYSTIME
    SET &W1 = &SUBSTR(1:2,&STR(&WRKTIME))
    SET &W2 = &SUBSTR(4:5,&STR(&WRKTIME))
    SET &W3 = &SUBSTR(7:8,&STR(&WRKTIME))
    SET &AMSOUTLS = &STR(&SYSUID..T&W1&W2&W3..OUTLIST)
    ISPEXEC VPUT (AMSOUTLS) SHARED
    FREE F(AMSOUT)
    FREE ATTR(ATR1)
    ATTR ATR1 LRECL(125) BLKSIZE(629) RECFM(V B A)
    ALLOC F(AMSOUT)   DA('&AMSOUTLS')   NEW SP(5 2) TR -
        RELEASE  USING(ATR1)
    ISPEXEC SELECT PGM(AMSPGM)
    ISPEXEC VGET (AMSDELT AMSOUTLS) SHARED
    IF &AMSDELT = &STR(K) THEN GOTO EXITAMS
    DEL '&AMSOUTLS'
EXITAMS: +
    EXIT
