PROC 0
/*-------------------------------------------------------------------*/
/* REFER    : AMSHCPRT     - AMS CLIST TO INVOKE PRINTOFF COMMAND    */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/*                                                                   */
/* AMSRC    : RETURN CODE                                            */
/*  VALUES                                                           */
/*        0 : OUTLIST KEPT AND NOT PRINTED                           */
/*        1 : OUTLIST PRINTED                                        */
/*        2 : OUTLIST DELETED AND NOT PRINTED                        */
/*        3 : OUTLIST PRINTED AND DELETED                            */
/*-------------------------------------------------------------------*/
SETCNTL: +
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
INIT: +
    SET AMSRC = 0
    SET &P1 =
    SET &P2 =
    ISPEXEC VGET (AMSOUTLS) SHARED
    ISPEXEC DISPLAY PANEL(AMSHCPRT)
    IF &AMSSOUT ^= &STR( ) THEN DO
        SET &P1 = &STR(CLASS(&AMSSOUT))
    END DO
    IF &AMSPID ^= &STR( ) THEN DO
        SET &P2 = &STR(DEST(&AMSPID))
    END DO
CHKPRT: +
    IF &TMP1 = &STR(P) THEN DO
        PRINTOFF ('&AMSOUTLS') &P1 &P2
        SET &AMSRC = &AMSRC + 1
    END DO
CHKDEL: +
    IF &TMP1 = &STR(D) THEN DO
        DEL '&AMSOUTLS'
        GOTO ALLONEW
    END DO
    IF &TMP2 = &STR(D) THEN DO
        DEL '&AMSOUTLS'
        GOTO ALLONEW
    END DO
    ISPEXEC VPUT (AMSRC) SHARED
    EXIT
ALLONEW: +
    SET &AMSRC = &AMSRC + 2
    SET &WRKTIME = &SYSTIME
    SET &W1 = &SUBSTR(1:2,&STR(&WRKTIME))
    SET &W2 = &SUBSTR(4:5,&STR(&WRKTIME))
    SET &W3 = &SUBSTR(7:8,&STR(&WRKTIME))
    SET &AMSOUTLS = &STR(&SYSUID..T&W1&W2&W3..OUTLIST)
    ISPEXEC VPUT (AMSOUTLS AMSRC) SHARED
    FREE F(AMSOUT)
    FREE ATTR(ATR1)
    ATTR ATR1 LRECL(125) BLKSIZE(629) RECFM(V B A)
    ALLOC F(AMSOUT)   DA('&AMSOUTLS')   NEW SP(5 2) TR -
        RELEASE  USING(ATR1)
EXITHC: +
    EXIT
