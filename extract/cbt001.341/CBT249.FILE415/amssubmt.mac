PROC 0
/*-------------------------------------------------------------------*/
/* REFER    : AMSSUBMT     - AMS CLIST TO SUBMIT JOBS                */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/*                                                                   */
/* COMMENTS : THE SPF LOG AND LIST (0.2) JCL VARIABLES ARE USED TO   */
/*            SUBMIT THE BATCH PROCESS.  VARIABLES AMSP1 - AMSP6 ARE */
/*            USED TO PASS THE AMS PARAMETERS TO THIS PROCESS.       */
/*                                                                   */
/* AMSRC    : RETURN CODE VARIABLE (SHARED PROFILE)                  */
/*                                                                   */
/*        0 - SUCCESSFUL COMPLETION                                  */
/*        1 - JCL SAVED SUCESSFULLY                                  */
/*        8 - CANCEL SELECTED                                        */
/*       12 - SUBMIT FAILED                                          */
/*       16 - SAVE JCL FAILED                                        */
/*                                                                   */
/*-------------------------------------------------------------------*/
SETCNTL: +
    SET &ADSN =
    SET &AMSMBR =
    ISPEXEC VPUT (ADSN AMSMBR) SHARED
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
INIT: +
    SET &AMSRC  = 0
    SET &AMSMBR = ' '
    SET &ERRCNT = 0
    SET &SUBCNT = 0
GETAMS: +
    ISPEXEC VGET (AMSP1 AMSP2 AMSP3 AMSP4 AMSP5 AMSP6 -
    AMSFUNC) SHARED
    IF &LASTCC ^= &STR(0) THEN DO
        GOTO SUBERR
    END DO
GETJCL: +
    ISPEXEC VGET (ZLLGJOB1 ZLLGJOB2 ZLLGJOB3 ZLLGJOB4)
    IF &LASTCC ^= &STR(0) THEN DO
        GOTO SUBERR
    END DO
PROCESS: +
    SET &AMSJ1 = &STR(&ZLLGJOB1)
    SET &AMSJ2 = &STR(&ZLLGJOB2)
    SET &AMSJ3 = &STR(&ZLLGJOB3)
    SET &AMSJ4 = &STR(&ZLLGJOB4)
    SET &PARM1  = &STR(&AMSP1)
    SET &PARM2  = &STR(&AMSP2)
    SET &PARM3  = &STR(&AMSP3)
    SET &PARM4  = &STR(&AMSP4)
    SET &PARM5  = &STR(&AMSP5)
    SET &PARM6  = &STR(&AMSP6)
SHOWJCL: +
    ISPEXEC DISPLAY PANEL(AMSJCL)
    IF &AMSOPT = &STR(END) THEN GOTO SUBIT
    IF &AMSOPT = &STR(CANCEL) THEN DO
        SET &AMSRC = 8
        ISPEXEC VPUT (AMSRC) SHARED
        EXIT
    END DO
    IF &AMSOPT = &STR(SAVE) THEN DO
        ISPEXEC DISPLAY PANEL(AMSJCL01)
        IF LASTCC = 8 THEN DO
            SET &AMSRC = 8
            ISPEXEC VPUT (AMSRC) SHARED
            EXIT
        END DO
    END DO
    FREE F(ISPFILE)
    ALLOC F(ISPFILE) DA('&ADSN') SHR
STRJCL: +
    ISPEXEC VPUT (ADSN AMSMBR) SHARED
    ISPEXEC FTOPEN
    SET &AMSRC = &LASTCC
    IF &AMSRC = 0 THEN GOTO BLDJCL
    IF &AMSRC = 8 THEN DO
        ISPEXEC FTCLOSE
        SET &ERRCNT = &ERRCNT + 1
        IF &ERRCNT > 1 THEN DO
            SET &AMSRC = 16
            ISPEXEC VPUT (AMSRC) SHARED
            EXIT
        END DO
        GOTO STRJCL
    END DO
    SET &AMSRC = 16
    ISPEXEC VPUT (AMSRC) SHARED
    EXIT
BLDJCL: +
    ISPEXEC FTINCL AMSJCL01
    IF &LASTCC ^= 0 THEN DO
        SET &AMSRC = 16
        ISPEXEC VPUT (AMSRC) SHARED
        EXIT
    END DO
    ISPEXEC FTCLOSE NAME(&AMSMBR)
    IF &LASTCC ^= 0 THEN DO
        SET &AMSRC = 16
        ISPEXEC VPUT (AMSRC) SHARED
        EXIT
    END DO
    SET &AMSRC = 1
    ISPEXEC VPUT (AMSRC) SHARED
    EXIT
SUBIT: +
    ISPEXEC FTOPEN TEMP
    ISPEXEC FTINCL AMSJCL01
    ISPEXEC FTCLOSE
    ISPEXEC VGET ZTEMPF
    CONTROL MSG
    ISPEXEC CONTROL DISPLAY LINE START(20)
    SUBMIT '&ZTEMPF'
    IF &LASTCC ^= &STR(0) THEN DO
        SET &SUBCNT = &SUBCNT + 1
        IF &SUBCNT > 3 THEN DO
            GOTO SUBERR
        END DO
        GOTO SUBIT
    END DO
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
ENDGOOD: +
    SET &AMSRC = 0
    ISPEXEC VPUT (AMSRC) SHARED
    EXIT
SUBERR: +
    SET &AMSRC = 12
    ISPEXEC VPUT (AMSRC) SHARED
    ZTWCLR
    EXIT
