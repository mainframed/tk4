PROC 0 DEBUG
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
    END DO
/*====================================================================*/
/*  SITE:    BLUE CROSS BLUE SHIELD OF KANSAS                         */
/*  REFER:   USER.CMDPROC(CLEANUP)                                    */
/*  AUTHOR:  DAVE WEILERT                                             */
/*  DOC:     THIS PROCESS IS USED TO CLEANUP DATA SETS.  THIS CLIST   */
/*           WILL ALLOW THE USER TO SELECT THE DATA SET PREFIX AND    */
/*           PERFORM A LISTC.  WHEN THE LISTC IS PERFORMED THE OUT-   */
/*           PUT IS WRITTEN TO A DISK DATA SET.  THE OUTPUT DATA SET  */
/*           IS THEN READ AND ALL DISK DATA SETS ARE PLACED IN A      */
/*           TABLE.  THE TABLE CAN BE SCROLLED AND THE USER MAY       */
/*           SELECT EITHER TO BROWSE OR DELETE A DATA SET.  MULTIPULE */
/*           BROWSE OR DELETE OPTIONS MAY BE ENTERED.  THE OPTIONS    */
/*           WILL BE PROCESSED IN THE SEQUENCE AS THEY APPEAR ON THE  */
/*           SCREEN.                                                  */
/*                                                                    */
/*           THE WORK FILE NAME IS '&SYSUID..&SYSUID..CLEANUP.DATA'   */
/*           AND IS NOT LISTED.  THIS CLIST WILL DELETE THE WORK FILE.*/
/*                                                                    */
/*           IF THE VOLSER OF A DATA SET IS NUMERIC IT IS CONSIDERED  */
/*           TO BE A TAPE FILE AND IS BYPASSED.                       */
/*====================================================================*/
/*                                                                    */
                                               /*ALLOC WORK DATA SET*/
BLDDSN: +
    SET &ODSN = &STR(&SYSUID..&SYSUID..CLEANUP.DATA)
    FREE DA('&ODSN')
    FREE ATTR(ATR1)
    DEL '&ODSN'
    ATTR ATR1 LRECL(125) BLKSIZE(629) RECFM(V B A)
    ALLOC F(DSNFILE) DA('&SYSUID..&SYSUID..CLEANUP.DATA') -
        NEW SP(3 2) TR RELEASE UNIT (SYSDA)  USING(ATR1)
    IF &LASTCC ^= 0 THEN DO
        WRITE &STR(UNABLE TO ALLOCATE &ODSN FOR OUTPUT)
        WRITE &STR(PLEASE RETRY PROCESS               )
        EXIT
    END DO
    SET &DELCNT = 0                             /*DEL DATA SET COUNT*/
    SET &AMT    = &STR(PAGE)                    /*SCROLL VALUE      */
    SET &NEXT   = &STR(N)                       /*SCROLL TABLE Y/N  */
    SET &SROWNUM = 1                            /*START TAB ROW NUM */
PANEL1: +
    IF &DELCNT > 0 THEN DO
        SET &DELMSG = &STR(&DELCNT DATA SET(S) DELETED)
    END DO
    ELSE DO
        SET &DELMSG = &STR(   )
    END DO
    SET &DELCNT = 0
    ISPEXEC DISPLAY PANEL(CLEANUP)              /*DISPLAY PANEL     */
    IF &LASTCC = 8 THEN DO
        GOTO FREEALL
    END DO
    CONTROL MSG
    ISPEXEC CONTROL DISPLAY LINE START(15)
    WRITE &STR(   PROCESSING CATALOG FOR '&LISTCVAR' LEVEL DATA SETS)
    CONTROL NOMSG
    SET &NST = &LENGTH(&STR(&LISTCVAR))
    SET &NST = &NST+17
    LISTC L(&LISTCVAR) ALL OUTFILE(DSNFILE)     /*PERFORM LISTC     */
    SET &RCC = &LASTCC
    IF &RCC = 4 THEN DO
        WRITE &STR(                                               )
        WRITE &STR(   UNABLE TO LOCATE '&LISTCVAR' LEVEL DATA SETS)
        WRITE &STR(   RE-ENTER DATA SET LEVEL                     )
        GOTO PANEL1
    END DO
    IF &RCC > 4 THEN DO
        WRITE &STR(                                             )
        WRITE &STR(   ERROR IN PROCESSING CATALAOG FOR &LISTCVAR)
        WRITE &STR(   PLEASE RETRY PROCESS                      )
        GOTO PANEL1
    END DO
    CONTROL MSG
    ISPEXEC CONTROL DISPLAY LINE START(17)
    WRITE &STR(   CREATING TABLE FOR &LISTCVAR LEVEL DATA SETS)
    CONTROL NOMSG
    ISPEXEC CONTROL ERRORS RETURN
    ISPEXEC TBCLOSE CLEANUP
    IF &DEBUG = &STR(DEBUG) THEN DO
        ISPEXEC BROWSE DATASET('&ODSN')
    END DO
                                                /*CREATE TEMP TABLE */
    ISPEXEC TBCREATE CLEANUP -
        NAMES(ADSN AVOL)-
        NOWRITE
    IF &LASTCC ^= 0 THEN DO
        WRITE &STR(                                      )
        WRITE &STR(   UNABLE TO CREATE DATA SET TABLE    )
        WRITE &STR(   PLEASE RETRY PROCESS               )
        GOTO PANEL1
    END DO
    OPENFILE DSNFILE INPUT                      /*OPEN WORK FILE    */
    IF &LASTCC ^= 0 THEN DO
        WRITE &STR(                                      )
        WRITE &STR(   UNABLE TO OPEN DATA SET FILE       )
        WRITE &STR(   PLEASE RETRY PROCESS               )
        GOTO PANEL1
    END DO
LOOPDSN: +
    ERROR DO
      IF &LASTCC = 400 OR &MAXCC = 400 THEN DO  /*  CHECK FOR EOF   */
          ERROR OFF
          ISPEXEC TBTOP CLEANUP
          GOTO SHOWTABL
      END DO
    END DO
    GETFILE DSNFILE                             /*READ WORK FILE    */
    SET &ST = &LENGTH(&STR(&DSNFILE))
    IF &ST < 14  THEN GOTO LOOPDSN
    IF &SUBSTR(9:14,&STR(&DSNFILE))  = &STR(VOLSER) THEN GOTO SAVEVOL
    IF &ST < 21 THEN GOTO LOOPDSN
    IF &SUBSTR(18:&NST,&STR(&DSNFILE)) = &LISTCVAR THEN GOTO SAVENAME
    GOTO LOOPDSN
                                                /*GET DATA SET NAME */
SAVENAME: +
    SET &ADSN = &SUBSTR(18:&ST,&STR(&DSNFILE))
    GOTO LOOPDSN
SAVEVOL: +
    SET &AVOL = &SUBSTR(27:32,&STR(&DSNFILE))
    IF &ADSN  = &STR(&ODSN) THEN DO             /*CHECK FOR CLEANUP */
        SET &ADSN = &STR(  )                    /*WORK DATA SET WHEN*/
        SET &AVOL = &STR(  )                    /*FOUND BYPASS      */
        GOTO LOOPDSN
    END DO
    SET &CHKTYPE = &DATATYPE(&STR(&AVOL))
    IF &CHKTYPE  = &STR(NUM) THEN DO            /*IF VOLSER IS NUM  */
        SET &ADSN = &STR(  )                    /*DATA SET IS A TAPE*/
        SET &AVOL = &STR(  )                    /*BYPASS IT         */
        GOTO LOOPDSN
    END DO
    SET &AVOL = &STR(    )
    ISPEXEC TBADD CLEANUP
    IF &LASTCC ^= 0 THEN DO
        WRITE &STR(   DATA SET &ADSN NOT PLACED IN TABLE)
    END DO
    GOTO LOOPDSN
                                                /*DISPALY DATA SET  */
                                                /*TABLE             */
SHOWTABL: +
    SET &RCC = 0
    IF &NEXT = &STR(Y) THEN DO
        SET &FUNCTION = &STR(  )
    END DO
    ELSE DO
        SET &CHKROW = &STR(Y)
        SET &FUNCTION = &STR(PANEL(CLEANUP1)
    END DO
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC TBDISPL CLEANUP &FUNCTION
    SET &RCC = &LASTCC
    IF &RCC > 8 THEN DO
        WRITE &STR(                                     )
        WRITE &STR(   UNABLE TO DISPLAY DATA SET TABLE  )
        WRITE &STR(   RETRUN CODE : &RCC                )
        CLOSFILE DSNFILE
        GOTO PANEL1
    END DO
    IF &RCC = 8 THEN DO
        ISPEXEC TBEND CLEANUP
        CLOSFILE DSNFILE
        GOTO PANEL1
    END DO
    IF &RCC = 4 THEN DO
        SET &NEXT = &STR(Y)
        GOTO CHKOPT
    END DO
    SET &NEXT = &STR(N)
CHKOPT: +
    IF &O = &STR(B) OR &O = &STR(D) THEN GOTO CONTINU
    SET &O = &STR( )
    GOTO SHOWTABL
CONTINU: +
    IF &O = &STR(B) THEN DO
        ISPEXEC BROWSE DATASET('&ADSN')
        SET &O = &STR( )
        GOTO SHOWTABL
    END DO
    IF &O = &STR(D) THEN DO
        DEL '&ADSN'
        SET &RCC = &LASTCC
        IF &RCC = 0 THEN DO
            SET &AVOL = &STR(DELETED)
            ISPEXEC TBPUT CLEANUP
            SET &O = &STR( )
            SET &DELCNT = &DELCNT + 1
            GOTO SHOWTABL
        END DO
        SET &AVOL = &STR(FAILED)
        ISPEXEC TBPUT CLEANUP
        SET &O = &STR( )
        GOTO SHOWTABL
    END DO
    GOTO SHOWTABL
                                                /*EXIT CLIST        */
FREEALL: +
    DEL '&ODSN'
    EXIT
