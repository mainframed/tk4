PROC 0
/*-------------------------------------------------------------------*/
/* REFER    : AMSDEL       - AMS CLIST TO INVOKE THE LISTC FEATURE   */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/*-------------------------------------------------------------------*/
SETCNTL: +
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
    SET &MSGID =
INIT: +
    SET &C1 =
    SET &C2 =
    SET &C3 =
    SET &C4 =
    SET &C5 =
    SET &C6 =
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC DISPLAY PANEL(AMSLIS01) MSG(&MSGID)
    IF &LASTCC = &STR(8) THEN DO
        EXIT
    END DO
SETPWS: +
    SET &PW = &STR(')
    IF &AMSDPWRD ^= &STR( ) THEN DO
        SET &PW = &STR('/&AMSDPWRD)
    END DO
    SET &PW1 = &STR(')
    IF &AMSCPWRD ^= &STR( ) THEN DO
        SET &PW1 = &STR('/&AMSCPWRD)
    END DO
SETPARMS: +
    SET &X1 =
    IF &AMSDSN01 ^= &STR( ) THEN DO
        SET &X1 = &STR(LVL('&AMSDSN01&PW1))
    END DO
    SET &P1 =
    IF &AMSDSN02 ^= &STR( ) THEN DO
        SET &P1 = &STR(CATALOG('&AMSDSN02&PW1))
    END DO
    SET &O1 =
    IF &AMSENT01 ^= &STR( ) THEN DO
        SET &O1 = &STR(ALIAS)
    END DO
    SET &O2 =
    IF &AMSENT02 ^= &STR( ) THEN DO
        SET &O2 = &STR(AIX)
    END DO
    SET &O3 =
    IF &AMSENT03 ^= &STR( ) THEN DO
        SET &O3 = &STR(CL)
    END DO
    SET &O4 =
    IF &AMSENT04 ^= &STR( ) THEN DO
        SET &O4 = &STR(DATA)
    END DO
    SET &O5 =
    IF &AMSENT05 ^= &STR( ) THEN DO
        SET &O5 = &STR(GDG)
    END DO
    SET &O6 =
    IF &AMSENT06 ^= &STR( ) THEN DO
        SET &O6 = &STR(IX)
    END DO
    SET &O7 =
    IF &AMSENT07 ^= &STR( ) THEN DO
        SET &O7 = &STR(NVSAM)
    END DO
    SET &O8 =
    IF &AMSENT08 ^= &STR( ) THEN DO
        SET &O8 = &STR(PGSPC)
    END DO
    SET &O9 =
    IF &AMSENT09 ^= &STR( ) THEN DO
        SET &O9 = &STR(PATH)
    END DO
    SET &O10 =
    IF &AMSENT10 ^= &STR( ) THEN DO
        SET &O10 = &STR(SPC)
    END DO
    SET &O11 =
    IF &AMSENT11 ^= &STR( ) THEN DO
        SET &O11 = &STR(UCAT)
    END DO
    SET &P2 = &STR(&O1 &O2 &O3 &O4 &O5 &O6 &O7 &O8 &O9 &O10 &O11)
SET2: +
    SET W1 = &STR( )
    IF &AMSTYP01 ^= &STR( ) THEN DO
        SET &W1 = &STR(ALLOC)
    END DO
    SET W2 = &STR( )
    IF &AMSTYP02 ^= &STR( ) THEN DO
        SET &W2 = &STR(HIST)
    END DO
    SET W3 = &STR( )
    IF &AMSTYP03 ^= &STR( ) THEN DO
        SET &W3 = &STR(NAME)
    END DO
    SET W4 = &STR( )
    IF &AMSTYP04 ^= &STR( ) THEN DO
        SET &W4 = &STR(VOL)
    END DO
    SET &P3 = &STR(&W1 &W2 &W3 &W4)
SET3: +
    SET &OTH01 =
    IF &AMSOPT01 ^= &STR( ) THEN DO
        SET OTH01 = &STR(CREAT(&AMSOPT01))
    END DO
    SET &OTH02 =
    IF &AMSOPT02 ^= &STR( ) THEN DO
        SET OTH02 = &STR(EXPIR(&AMSOPT02))
    END DO
    SET &OTH03 =
    IF &AMSOPT03 ^= &STR( ) THEN DO
        SET OTH03 = &STR(NUS)
    END DO
    SET &P4 = &STR(&OTH01 &OTH02 &OTH03)
PROCESS: +
    IF &AMSMODE = &STR(B) THEN DO
        GOTO BATCH
    END DO
    CONTROL MSG
    SET &AMSFUNC = &STR(LISTCAT)
    LISTC &X1 &P1 &P2 &P3 &P4 OUTFILE(AMSOUT)
    SET &AMSRC = &LASTCC
    IF &AMSRC ^= 0 THEN DO
        SET &MSGID = &STR(AMS026)
        GOTO INIT
    END DO
    ISPEXEC VGET (AMSOUTLS) SHARED
    ISPEXEC BROWSE DATASET('&AMSOUTLS')
    %AMSHCPRT
    ISPEXEC VGET (AMSRC) SHARED
    IF &AMSRC = 0  THEN SET &MSGID = &STR(AMS022)
    IF &AMSRC = 1  THEN SET &MSGID = &STR(AMS023)
    IF &AMSRC = 2  THEN SET &MSGID = &STR(AMS024)
    IF &AMSRC = 3  THEN SET &MSGID = &STR(AMS025)
    GOTO INIT
BATCH: +
    IF &STR(&P4) = &STR( ) THEN GOTO CHK1
    SET &C3 = &STR( -)
    SET &C2 = &STR( -)
    SET &C1 = &STR( -)
    GOTO PASSPARM
CHK1: +
    IF &STR(&P3) = &STR( ) THEN GOTO CHK2
    SET &C2 = &STR( -)
    SET &C1 = &STR( -)
    GOTO PASSPARM
CHK2: +
    IF &STR(&P2) = &STR( ) THEN GOTO PASSPARM
    SET &C1 = &STR( -)
PASSPARM: +
    SET &AMSP1 = &STR(LISTC &X1 -)
    SET &AMSP2 = &STR(&P1&C1)
    SET &AMSP3 = &STR(&P2&C2)
    SET &AMSP4 = &STR(&P3&C3)
    SET &AMSP5 = &STR(   )
    SET &AMSP6 = &STR(   )
    SET &AMSFUNC = &STR(LISTCAT)
    SET &AMSFILE = &STR(//*  )
    ISPEXEC VPUT (AMSP1 AMSP2 AMSP3 AMSP4 AMSP5 AMSP6 -
    AMSFUNC AMSFILE) SHARED
    %AMSSUBMT
    ISPEXEC VGET (AMSRC ADSN AMSMBR) SHARED
    IF &AMSRC = 0  THEN SET &MSGID = &STR(      )
    IF &AMSRC = 1  THEN SET &MSGID = &STR(AMS020)
    IF &AMSRC = 8  THEN SET &MSGID = &STR(AMS016)
    IF &AMSRC = 12 THEN SET &MSGID = &STR(AMS017)
    IF &AMSRC = 16 THEN SET &MSGID = &STR(AMS018)
    GOTO INIT
