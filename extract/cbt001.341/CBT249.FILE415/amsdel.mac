PROC 0
/*-------------------------------------------------------------------*/
/* REFER    : AMSDEL       - AMS CLIST TO INVOKE THE DELETE FEATURE  */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/*-------------------------------------------------------------------*/
SETCNTL: +
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
    SET &MSGID =
INIT: +
    SET &C1 =
    SET &C2 =
    SET &C3 =
    SET &C4 =
    SET &C5 =
    SET &C6 =
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC DISPLAY PANEL(AMSDEL01) MSG(&MSGID)
    IF &LASTCC = &STR(8) THEN DO
        EXIT
    END DO
    SET &PW = &STR(')
    IF &AMSDPWRD ^= &STR( ) THEN DO
        SET &PW = &STR('/&AMSDPWRD)
    END DO
SETPARMS: +
    SET &P1 =
    SET &PW1 = &STR(')
    IF &AMSCPWRD ^= &STR( ) THEN DO
        SET &PW1 = &STR('/&AMSCPWRD)
    END DO
    IF &AMSDSN02 ^= &STR( ) THEN DO
        SET &P1 = &STR(CATALOG('&AMSDSN02&PW1))
    END DO
    IF &AMST1 ^= &STR( ) THEN DO
        SET &P2 = &STR(ALIAS)
        GOTO SET2
    END DO
    IF &AMST2 ^= &STR( ) THEN DO
        SET &P2 = &STR(AIX)
        GOTO SET2
    END DO
    IF &AMST3 ^= &STR( ) THEN DO
        SET &P2 = &STR(CL)
        GOTO SET2
    END DO
    IF &AMST4 ^= &STR( ) THEN DO
        SET &P2 = &STR(GDG)
        GOTO SET2
    END DO
    IF &AMST5 ^= &STR( ) THEN DO
        SET &P2 = &STR(NVSAM)
        GOTO SET2
    END DO
    IF &AMST6 ^= &STR( ) THEN DO
        SET &P2 = &STR(PGSPC)
        GOTO SET2
    END DO
    IF &AMST7 ^= &STR( ) THEN DO
        SET &P2 = &STR(PATH)
        GOTO SET2
    END DO
    IF &AMST8 ^= &STR( ) THEN DO
        SET &P2 = &STR(SPC)
        GOTO SET2
    END DO
    IF &AMST9 ^= &STR( ) THEN DO
        SET &P2 = &STR(UCAT)
        GOTO SET2
    END DO
    SET &P2 = &STR(  )
SET2: +
    SET W1 = &STR( )
    SET W3 = &STR( )
    SET W2 = &STR( )
    SET W4 = &STR( )
    IF &AMSOPT1 = &STR(Y) THEN DO
        SET &W1 = &STR(ERASE)
    END DO
    IF &AMSOPT1 = &STR(N) THEN DO
        SET &W1 = &STR(NOERASE)
    END DO
    IF &AMSOPT2 = &STR(Y) THEN DO
        SET &W2 = &STR(PURGE)
    END DO
    IF &AMSOPT2 = &STR(N) THEN DO
        SET &W2 = &STR(NOPURGE)
    END DO
    IF &AMSOPT3 = &STR(Y) THEN DO
        SET &W3 = &STR(FORCE)
    END DO
    IF &AMSOPT3 = &STR(N) THEN DO
        SET &W3 = &STR(NOFORCE)
    END DO
    IF &AMSOPT4 = &STR(Y) THEN DO
        SET &W4 = &STR(SCRATCH)
    END DO
    IF &AMSOPT4 = &STR(N) THEN DO
        SET &W4 = &STR(NOSCRATCH)
    END DO
    SET &P3 = &STR(&W1 &W2 &W3 &W4)
PROCESS: +
    IF &AMSMODE = &STR(B) THEN DO
        GOTO BATCH
    END DO
    CONTROL MSG
    DELETE ('&AMSDSN01&PW) &P1 &P2 &P3
    GOTO INIT
BATCH: +
    IF &STR(&P3) = &STR( ) THEN GOTO CHK1
    SET &C2 = &STR( -)
    SET &C1 = &STR( -)
    GOTO PASSPARM
CHK1: +
    IF &STR(&P2) = &STR( ) THEN GOTO PASSPARM
    SET &C1 = &STR( -)
PASSPARM: +
    SET &AMSP1 = &STR(DELETE ('&AMSDSN01&PW) -)
    SET &AMSP2 = &STR(&P1&C1)
    SET &AMSP3 = &STR(&P2&C2)
    SET &AMSP4 = &STR(&P3)
    SET &AMSP5 = &STR(   )
    SET &AMSP6 = &STR(   )
    SET &AMSFUNC = &STR(DELETE)
    SET &AMSFILE = &STR(//*  )
    ISPEXEC VPUT (AMSP1 AMSP2 AMSP3 AMSP4 AMSP5 AMSP6 -
    AMSFUNC AMSFILE) SHARED
    %AMSSUBMT
    ISPEXEC VGET (AMSRC ADSN AMSMBR) SHARED
    IF &AMSRC = 0  THEN SET &MSGID = &STR(      )
    IF &AMSRC = 1  THEN SET &MSGID = &STR(AMS020)
    IF &AMSRC = 8  THEN SET &MSGID = &STR(AMS016)
    IF &AMSRC = 12 THEN SET &MSGID = &STR(AMS017)
    IF &AMSRC = 16 THEN SET &MSGID = &STR(AMS018)
    GOTO INIT
