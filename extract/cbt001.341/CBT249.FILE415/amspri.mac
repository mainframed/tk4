PROC 0
/*-------------------------------------------------------------------*/
/* REFER    : AMSPRT       - AMS CLIST TO INVOKE THE PRINT FEATURE   */
/* DATE     : DEC 1983                                               */
/* AUTHOR   : DAVE WEILERT                                           */
/*-------------------------------------------------------------------*/
SETCNTL: +
    ISPEXEC VGET DEBUG SHARED
    IF &DEBUG = &STR(DEBUG) THEN DO
        CONTROL MSG NOFLUSH LIST CONLIST SYMLIST PROMPT MAIN
    END DO
    ELSE DO
        CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST PROMPT MAIN
    END DO
    SET &MSGID =
INIT: +
    SET &C1 =
    SET &C2 =
    ISPEXEC CONTROL DISPLAY REFRESH
    ISPEXEC DISPLAY PANEL(AMSPRT01) MSG(&MSGID)
    IF &LASTCC = &STR(8) THEN DO
        EXIT
    END DO
SETPARMS: +
    IF &AMSFORMT = &STR(C) THEN DO
        SET &P1 = &STR(CHAR)
    END DO
    IF &AMSFORMT = &STR(D) THEN DO
        SET &P1 = &STR(DUMP)
    END DO
    IF &AMSFORMT = &STR(H) THEN DO
        SET &P1 = &STR(HEX)
    END DO

    SET &P2 = &STR( )
    IF &STR(&AMSFRKEY) ^= &STR( ) THEN DO  /*SET FROM PARM          */
        SET &P2 = &STR(FKEY(&AMSFRKEY))
    END DO
    IF &STR(&AMSFRADR) ^= &STR( ) THEN DO
        SET &P2 = &STR(FADDR(&AMSFRADR))
    END DO
    IF &STR(&AMSFRNUM) ^= &STR( ) THEN DO
        SET &P2 = &STR(FNUM(&AMSFRNUM))
    END DO
    IF &STR(&AMSFRSKP) ^= &STR( ) THEN DO
        SET &P2 = &STR(SKIP(&AMSFRSKP))
    END DO

    SET &P3 = &STR(  )
    IF &STR(&AMSTOKEY) ^= &STR( ) THEN DO  /*SET TO PARM           */
        SET &P3 = &STR(TOKEY(&AMSTOKEY))
    END DO
    IF &STR(&AMSTOADR) ^= &STR( ) THEN DO
        SET &P3 = &STR(TADDR(&AMSTOADR))
    END DO
    IF &STR(&AMSTONUM) ^= &STR( ) THEN DO
        SET &P3 = &STR(TNUM(&AMSTONUM))
    END DO
    IF &STR(&AMSTOCNT) ^= &STR( ) THEN DO
        SET &P3 = &STR(COUNT(&AMSTOCNT))
    END DO
PROCESS: +
    IF &AMSMODE = &STR(B) THEN DO
        GOTO BATCH
    END DO
    CONTROL MSG
    PRINT IDS('&AMSDSN01') &P1 &P2 &P3 OUTFILE(AMSOUT)
    IF &LASTCC ^= 0 THEN GOTO INIT
    ISPEXEC VGET (AMSOUTLS) SHARED
    ISPEXEC BROWSE DATASET('&AMSOUTLS')
    %AMSHCPRT
    ISPEXEC VGET (AMSRC) SHARED
    IF &AMSRC = 0  THEN SET &MSGID = &STR(AMS022)
    IF &AMSRC = 1  THEN SET &MSGID = &STR(AMS023)
    IF &AMSRC = 2  THEN SET &MSGID = &STR(AMS024)
    IF &AMSRC = 3  THEN SET &MSGID = &STR(AMS025)
    GOTO INIT
BATCH: +
    IF &STR(&P3) = &STR( ) THEN GOTO CHK1
    SET &C2 = &STR( -)
    SET &C1 = &STR( -)
    GOTO PASSPARM
CHK1: +
    IF &STR(&P2) = &STR( ) THEN GOTO PASSPARM
    SET &C1 = &STR( -)
PASSPARM: +
    SET &AMSP1 = &STR(PRINT IDS('&AMSDSN01') -)
    SET &AMSP2 = &STR(&P1&C1)
    SET &AMSP3 = &STR(&P2&C2)
    SET &AMSP4 = &STR(&P3)
    SET &AMSP5 = &STR(   )
    SET &AMSP6 = &STR(   )
    SET &AMSFUNC = &STR(PRINT)
    ISPEXEC VPUT (AMSP1 AMSP2 AMSP3 AMSP4 AMSP5 AMSP6 -
    AMSFUNC) SHARED
    %AMSSUBMT
    ISPEXEC VGET (AMSRC ADSN AMSMBR) SHARED
    IF &AMSRC = 0  THEN SET &MSGID = &STR(      )
    IF &AMSRC = 1  THEN SET &MSGID = &STR(AMS020)
    IF &AMSRC = 8  THEN SET &MSGID = &STR(AMS016)
    IF &AMSRC = 12 THEN SET &MSGID = &STR(AMS017)
    IF &AMSRC = 16 THEN SET &MSGID = &STR(AMS018)
    GOTO INIT
