JULGREG TITLE '                  CONVERT JULIAN-DATE TO GREGORIAN FORM'
JULGREG  CSECT
         USING *,15
         B     SAVEREG
         DC    CL8'JULGREG'
         DC    CL9'&SYSDATE'
         DC    CL7'&SYSTIME'
SAVEREG  SAVE  (5,7)
*
*  THIS SUBROUTINE CONVERTS JULIAN DATE INTO GREGORIAN DATE
*
*  ON ENTRY R1 POINTS TO 2 ARGUMENTS
*     ARGUMENT 1- THE JULIAN DATE AS A PL3 FIELD YYDDDS
*     ARGUMENT 2- AN 8-BYTE RECEIVING FIELD FOR THE GREG DATE MM/DD/YY
*
*  NO VERIFICATION OF ARGUMENT FIELDS IS DONE
*  SUBROUTINE ASSUMES DATE BETWEEN 1901 AND 1999
*
*  PROGRAM IS SERIALLY-REUSABLE
*
*  REGISTER USAGE -
*
*        1     POINTS TO ARGUMENT ADDRESS LIST  (SINCE NO MACROS
*                  ARE USED IN THE MAINLINE, R1 IS NOT RESET BY THE SYS
*        5     ARGUMENT POINTER
*        6     UTILITY
*        7     UTILITY
*       15     BASE
*
         L     R5,0(R1)            R5 POINTS TO JULIAN DATE
         UNPK  YYY,0(2,R5)         PREP YEAR
         MVC   DAY,1(R5)           NOTE- NO CHECK FOR VALID PACKED FLD
         LA    R7,DAYTBL
         TM    0(R5),X'03'         LEAPYEAR  Q
         BNZ   NOLEAP              NO
         LA    R7,LEAPTBL          YES, USE LEAPYEAR TABLE
NOLEAP   LA    R6,12
         ZAP   MO,=P'1'
FINDMO   SP    DAY,0(2,R7)
         BNP   NEGDAY
         AP    MO,=P'1'
         LA    R7,2(R7)
         BCT   R6,FINDMO
         SP    MO,=P'1'            ADJUST IF DECEMBER
         B     MOVEMO
NEGDAY   AP    DAY,0(2,R7)
MOVEMO   UNPK  MMM,MO
         OI    MMM+2,X'F0'
         UNPK  DDD,DAY
         OI    DDD+2,X'F0'
         L     R5,4(R1)            R5 NOW POINTS TO RECV FLD
         MVC   0(2,R5),MMM+1
         MVI   2(R5),C'/'
         MVC   3(2,R5),DDD+1
         MVI   5(R5),C'/'
         MVC   6(2,R5),YYY
         RETURN (5,7),RC=0
         SPACE 2
DAYTBL   DC    P'31,28,31,30,31,30,31,31,30,31,30,31'
LEAPTBL  DC    P'31,29,31,30,31,30,31,31,30,31,30,31'
YYY      DS    CL3
MMM      DS    CL3
DDD      DS    CL3
DAY      DS    PL2
MO       DS    PL2
         SPACE
         LTORG
         SPACE
         REGEQU
         SPACE
         END
