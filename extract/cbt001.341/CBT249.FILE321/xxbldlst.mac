XXBLDLST TITLE 'PROGRAM TO LIST OUT DYNAMIC BLDL TABLES'
*              /* LEVEL=0004 */
XXBLDLST START
*
*        CODE TO PRINT DYNAM-BLDL STATISTICS
*
*
*   DYNAM-BLDL LEVEL  CODE  DEPENDENCIES
*     DEPENDS ON LAYOUT OF TABLE BEING SAME AS MODULE IN NUC
*     DEPENDS ON ADDRESS AND TIME WORDS BEING CORRECT IN NUC MODULE
*     DEPENDS ON LAYOUT OF BLDL TABLE HEADER BEING CORRECT
*     STORAGE ALLOCATED FOR BLDL TABLE OF 500 ENTRIES MAX
*
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT NOGEN
         B     26(R15)                  BRANCH AROUND ID
         DC    AL1(21),CL8'XXBLDLST'    PROGRAM NAME
         DC    CL8'&SYSDATE'            DATE
         DC    CL5'&SYSTIME'            TIME
         SAVE  (14,12)                  SAVE REGS IN HI SAVE AREA
         LR    R11,R15                  SET UP BASE
         USING XXBLDLST,R11             OF R12
         LR    R12,R13                   SAVE CALLERS SAVE AREA ADDR
         LA    R13,SAVEAREA             SET SAVEAREA
         ST    R12,SAVEAREA+4           SAVE CALLERS SAVE AREA ADDR
         ST    R13,8(,R12)              SAVE OURS IN CALLERS
         SPACE 4
*
**
***      CHECK OUT PARM FIELD PASSED
**
*
         L     R2,0(R1)                 PARM FIELD PTR
         LH    R3,0(R2)       PARM LENGTH
         LTR   R3,R3
         BZ    NOPARM         NO PARM TO DO
         LA    R2,1(R3,R2)    ADDR LAST BYTE OF PARM
T1       CLI   0(R2),C'S'     SUMMARY ONLY REQUESTED
         BNE   T2
         OI    CTLFLAG,X'80'  YES,FLAG AS SUMMARY ONLY
T2       DS    0H             NEXT PARM TEST......
         BCTR  R2,0           ADDR PREVIOUS CHAR
         BCT   R3,T1          PROCESS IT IF ANY..
         SPACE 4
*
**
***      FIND IGC018XX IN CORE AND CHECK CODE LEVEL
**
*
NOPARM   DS    0H
         L     R3,X'10'       CVT
         L     R3,X'C8'(,R3)  SSCVT
         L     R3,X'84'(,R3)  SVCTABLE
         LA    R3,X'90'(,R3)  BLDL ENTRY
         L     R2,0(R3)                 CODE
         CLC   =C'10-20-81',12(R2) Q.IS THIS SUPPORTED LEVEL
         BNE   UNSUPORT       NO ,TELL USER & ABEND
         MVC   STTIME,X'20'(R2)     MOVE OUT START TIME
         SPACE 4
*
**
***      MOVE TABLE FROM IGC018XX TO OUR AREA
**
*
         L     R2,20(R2)            GET TBL POINTER FROM PGM HEADER
         LA    R3,LINKTAB
         L     R4,SORT-LINKTAB(R2)
         ST    R4,SORT
         L     R4,HITS-LINKTAB(R2)
         ST    R4,HITS
*
         LM    R5,R6,TABSTAT
         L     R9,TABSTAT-LINKTAB(,R2)
         LR    R1,R5
         SR    R1,R9               R1 HOLDS OFFSET OF TBL-COPY FROM TBL
         L     R7,TABEND-LINKTAB(,R2)
         AR    R7,R1               R7 POINTS TO END OF TBL-COPY
         ST    R7,TABEND
MOVEDATA MVC   0(64,R5),0(R9)
         AR    R9,R6
         BXLE  R5,R6,MOVEDATA
         SPACE 4
*
**
***      MAIN PROCESSING
**
*
         OPEN  (SYSPRINT,(OUTPUT))
         USING ENTRY,R5
         LM    R5,R7,TABSTAT
         TM    CTLFLAG,X'80'  SUMMARY ONLY
         BO    PRINTIT
         TIME  DEC
         ST    R0,FTIME
         ST    R1,FDATE
         ED    TOTIME,FTIME
         CLC   STTIME,ZEROS
         BNE   EDST
         MVC   PSTTM,IPL
         B     TITLDATE
EDST     ED    PSTTM,STTIME
TITLDATE CALL  JULGREG,(DATE3,TDATE)
         PUT   SYSPRINT,HDR1
         PUT   SYSPRINT,BLANKS
         PUT   SYSPRINT,HDR2
         PUT   SYSPRINT,HDR3
         PUT   SYSPRINT,HDR4
         PUT   SYSPRINT,BLANKS
PRINTIT  MVC   OUTLINE+1(79),OUTLINE CLEAR OUT LINE
         L     R9,RECENT      Q.ANY ACTION
         LTR   R9,R9          ELSE DONT PRINT LINE
         BZ    PRINTED
         TM    CTLFLAG,X'80'  SUMMARY ONLY
         BO    PRINTED
         LH    R9,RECENT
         LTR   R9,R9
         BZ    PRECENT
         CVD   R9,DWD
         UNPK  OUTLINE+3(5),DWD+5(3)
         OI    OUTLINE+7,X'F0'
PRECENT  DS    0H
         LH    R9,TOTAL MG
         LTR   R9,R9
         BZ    PTOTAL
         CVD   R9,DWD
         UNPK  OUTLINE+9(5),DWD+5(3)
         OI    OUTLINE+13,X'F0'
PTOTAL   DS    0H
         MVC   OUTLINE+16(8),DATA
         SR    R9,R9
         IC    R9,DATA+59     CONCAT NO
         IC    R9,CHARTABL(R9)
         STC   R9,OUTLINE+26
         UNPK  OUTLINE+35(7),DATA+8(4)
         UNPK  OUTLINE+41(7),DATA+11(4)
         UNPK  OUTLINE+47(7),DATA+14(4)
         UNPK  OUTLINE+53(7),DATA+17(4)
         UNPK  OUTLINE+59(7),DATA+20(4)
         UNPK  OUTLINE+65(7),DATA+23(4)
         UNPK  OUTLINE+71(7),DATA+26(4)
         MVI   OUTLINE+75,C' '
         NC    OUTLINE+35(40),=40X'0F'
         TR    OUTLINE+35(40),CHARTABL
         PUT   SYSPRINT,OUTLINE
PRINTED  BXLE  R5,R6,PRINTIT
         PUT SYSPRINT,BLANKS
         LH    R9,SORT
         CVD   R9,DWD
         UNPK  SUMMARY+10(5),DWD+5(3)
         OI    SUMMARY+14,X'F0'
         LH    R9,HITS
         LR    R5,R9
         LR    R7,R9             R7 HOLDS COUNT OF HITS
         CVD   R9,DWD
         UNPK  SUMMARY+20(5),DWD+5(3)
         OI    SUMMARY+24,X'F0'
         LH    R9,MISSES
         AR    R5,R9             R5 HOLDS HITS+MISSES
         CVD   R9,DWD
         UNPK  SUMMARY+31(5),DWD+5(3)
         OI    SUMMARY+35,X'F0'
         MH    R7,=H'1000'
         SR    R6,R6
         DR    R6,R5
         CVD   R7,DWD
         AP    DWD,=P'.5'          ROUND
         ED    SUMPCT,DWD+5
         PUT   SYSPRINT,SUMMARY
         SPACE 4
*
**
***      FREE DYNAMIC SAVEAREA AND RETURN
**
*
RET      DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
         SPACE 4
*
**
***      WTO WRONG LEVEL IN IGC018XX CODE
**
*
UNSUPORT DS    0H
         WTO   'XXBLDLST - WRONG LEVEL OF IGC018XX CODE'
         B     RET                      HOME
         SPACE 4
         LTORG
SAVEAREA DS    18F
DWD      DC    D'0'
FTIME    DS    F
FDATE    DS    0F
         DS    CL1
DATE3    DS    CL3
STTIME   DS    F
ZEROS    DC    F'0'
BLANKS   DC    CL80' '
         SPACE
HDR1     DS    0CL80
         DC    X'09'
         DC    CL28'DYNAMIC BLDL LIST'
TDATE    DS    CL8
         DC    CL10' '
         DC    CL4'FROM'
PSTTM    DC    XL9'4021207A20207A2020'
         DC    CL3' TO'
TOTIME   DC    XL9'4021207A20207A2020'
         DC    CL8' '
         SPACE
HDR2     DC    X'09'
         DC    CL79'         LONG'
HDR3     DC    X'09'
         DC    CL79' RECENT  TERM'
HDR4     DC    X'09'
         DC    CL79'  USES   USES  MODULE   CONCAT     DIRECTORY DUMP'
         SPACE
SUMMARY  DS    0CL80
         DC    X'09'
         DC    CL35'   SORTS=     ,HIT=     ,MISS=                   '
         DC    CL6' '
SUMPCT   DC    XL5'4020202120'
         DC    CL33' PERCENT HITS'
         SPACE
OUTLINE  DC    CL80' '
CHARTABL DC    C'0123456789ABCDEF'
CTLFLAG  DS    X'00'          CONTROL FLAG FROM PARMS
*                             X'80' - PRINT SUMMARY ONLY
IPL      DC    CL9'   IPL'
         SPACE 4
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=80,             X
               RECFM=FA,BLKSIZE=80
         SPACE 4
ENT      EQU   500
ENTX     EQU   37
LINKTAB  DS    0D
TABSTAT  DC    A(START)
TABSIZE  DC    F'64'
TABEND   DC    A(LASTENT)
TABLAST  DC    A(LASTENT)
TABSLOT  DC    A(START+ENTX*64)
SORT     DC    H'0'
ENTRIES  DC    AL2(ENT)
HITS     DC    H'0'
MISSES   DC    H'0'
START    DC    ((ENT-1)*64)X'0'
LASTENT  DC    64X'0'
LINKEND  DS    0F
         SPACE 4
*
**
***      TABLE ENTRY DSECT
**
*
ENTRY    DSECT
RECENT   DS    H
TOTAL    DS    H
DATA     DS    0F
         SPACE 4
         END
