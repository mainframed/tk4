 TITLE 'ACF2 PRE-VALIDATION EXIT FOR TEST DATASET ALLOCATIONS'
VLDEXIT  $PROLOG 12
**********************************************************************
* NAME       -   VLDEXIT                                             *
*                                                                    *
* AUTHOR     -   T. BRYANT                                           *
*                                                                    *
* FUNCTION   -   ACF2 PRE-VALIDATION EXIT TO CONTROL                 *
*                ALLOCATE/RENAME/SCRATCH DATASET OPERATIONS ON       *
*                TEST (MIDDLE-INDEX DATASET NAME) DATASETS           *
*                ON NON-TEST VOLUMES. (STM3** ARE TEST VOLUMES).     *
*                                                                    *
*                                                                    *
* INPUT     R1 --> ACF$USER PARMLIST                                 *
*           R0 --> ADDRESS OF LOGON ID RECORD                        *
*                                                                    *
**********************************************************************
         EJECT
**********************************************************************
* OUTPUT    R15 =  0, RESUME STANDARD ACF2 VALIDATION                *
*               =  4, BYPASS ACF2 VALDATION, PERMIT ACCESS, NO LOG   *
*               =  8, TERMINATE ACCESS, NO LOG                       *
*               = 12, BYPASS VALIDATION, PERMIT ACCESS AND LOG       *
*               = 16, TERMINATE ACCESS AND LOG                       *
*               = 20, TERMINATE ONLY IF IN ABORT MODE                *
*               = 24, RETRY THE ENTIRE SVC REQUEST                   *
*               = 28, INVOKE VALIDATION EXIT                         *
*               = 32, INVOKE DSNGEN EXIT                             *
*               = 36, ALLOW ACCESS, ISSUE WARN MSGS, LOG             *
*               = 40, TAKE SVC DUMP AND CONTINUE(RC=0)               *
*               = 44, SUPPRESS WTO, USE RC=12 RETURN                 *
*               = 48, SUPPRESS WTO, USE RC=16 RETURN                 *
*               = 52, SUPPRESS WTO, USE RC= 0 RETURN                 *
*               =255, TAKE AN SVC DUMP, ABORT                        *
*                                                                    *
* MACROS     -  RETURN,WTO,$PROLOG,GETMAIN,FREEMAIN                  *
*                                                                    *
* DSECTS     -  ACSXP,ACUCB                                          *
*                                                                    *
* REGISTERS  - R0  -  ACF2 POINTER TO LIDREC                         *
*              R1  -  NOT USED BY PROGRAM                            *
*              R2  -  ADDRESS OF 2ND LEVEL INDEX NAME                *
*              R3  -  ADDRESS OF PERIOD FOLLOWING 2ND LEVEL INDEX NM *
*              R4  -  LENGTH OF 2ND LEVEL INDEX NAME                 *
*              R5  -  ADDRESS OF 6 CHARACTER VOLUME NAME             *
*              R6  -  INDEX MAX DIGIT VALUE OF 8                     *
*              R7  -  LENGTH OF ACTUAL DATASET NAME                  *
*              R8  -  POINTER TO DATASET WTO OUTPUT FIELD            *
*              R9  -  POINTER TO LIDREC (COPIED FROM R0)             *
*              R10 -  POINTER TO GETMAIN'D AREA FOR WTOMSG           *
*              R11 -  USER PARMLIST ADDRESS                          *
*              R13 -  SAVEAREA ADDRESS                               *
*              R14 -  RETURN ADDRESS                                 *
*              R15 -  ENTRY ADDRESS,  RETURN CODE AT EXIT            *
*                                                                    *
* LANGUAGE   - ASSEMBLER H                                           *
*                                                                    *
* ATTRIBUTES - AUTHORIZED, KEY 0 AND RE-ENTRANT                      *
**********************************************************************
         EJECT
**********************************************************************
* PSUEDOCODE - TAPE OPEN/CLOSE/EOV REQUEST WILL CAUSE A RETURN       *
*              ACF2 AND NORMAL DATASET VALIDATION.                   *
*                                                                    *
*              IF LOGONID MAKING THIS ACCESS IS NON-CNCL OR          *
*              PREFIX(********) RESUME STANDARD ACF2 VALIDATION.     *
*                                                                    *
*              READ THE MODIFIABLE DATASET NAME(ACCUDSN)             *
*              IF MIDDLE-INDEX NAME = 'TEST' AND THE                 *
*              ACCESS ATTEMPT IS AN ALLOCATE/SCRATCH/RENAME          *
*              THEN                                                  *
*                DO                                                  *
*                   READ THE MODIFIABLE DATASET VOLUME(ACCUVOL)      *
*                   IF FIRST 4 CHARACTERS OF ACCUVOL = 'STM3' THEN   *
*                     DO                                             *
*                         IF THE ACUCB LOGONID FIRST 2 DIGITS        *
*                         ARE SS FOR SYSTEM SUPPORT THEN             *
*                             DO                                     *
*                               SET REGISTER 15 WITH CONDITION CODE  *
*                               12 INDICATING BYPASS ACF2 VALIDATION *
*                               PERMIT ACCESS AND LOG                *
*                             END                                    *
*                       SET REGISTER 15 WITH CONDITION CODE 48       *
*                       (TERMINATE ACCESS AND LOG)                   *
*                       ISSUE A WRITE TO PROGRAMMER MESSAGE          *
*                     END                                            *
*                END                                                 *
*                                                                    *
*  TOM BRYANT                                                        *
*  COMBINED INSURANCE COMPANY OF AMERICA                             *
*  5050 NORTH BROADWAY AVENUE                                        *
*  CHICAGO,ILLINOIS 60640  (312) 275-8000 X8414                      *
**********************************************************************
         SLR   R10,R10                      ZERO OUT R10
         LR    R9,R0                        GET ADDRESS OF LIDREC
         USING LIDREC,R9                    ADDRESSABILITY TO LIDREC
         TM    LIDMFLG,LIDMNCNL             NON-CNCL USER?
         BO    RC0                          NO, PERFORM NORMAL VALD
         CLC   LIDPFX(8),SOFTMASK           PREFIX(********)?
         BE    RC0                          NO, PERFORM NORMAL VALD
         SLR   R15,R15                      RETURN CODE REGISTER
         LR    R11,R1                       SAVE PARMLIST ADDRESS
         USING ACSXP,R11                    SET ADDRESSABILITY
         CLI   ACUUFLG3,ACUU3VSM            VSAM OPEN REQUEST?
         BE    PROCESS                       YES, PROCESS
         CLI   ACUUFLG3,ACUU3CMS            CATLG MGMT FUNCTION?
         BE    PROCESS                       YES, PROCESS
         CLI   ACUUFLG3,ACUU3DSD            NEW DATASET ALLOCATION?
         BE    PROCESS                       YES, PROCESS
         CLI   ACUUFLG3,ACUU3REN            RENAME FUNCTION?
         BE    PROCESS                       YES, PROCESS
         CLI   ACUUFLG3,ACUU3SCR            SCRATCH FUNCTION?
         BE    PROCESS                       YES, PROCESS
         B     RC0                          NO, PERFORM NORMAL VALD
PROCESS  L     R7,ACUUDSN                   GET DSNAME ADDRESS
         LR    R2,R7
DSNLONG  CLC   0(1,R7),ZERO                 BINARY ZERO
         BE    GOTLNTH
         CLC   0(1,R7),BLANK                OR BLANK DLM?
         BE    GOTLNTH
         LA    R7,1(R7)                     INCREMENT BY 1
         B     DSNLONG
GOTLNTH  SR    R7,R2                        LENGTH OF DSN IN R7
         LA    R6,8                         SET MAXIMUM INDEX LENGTH
INDEX#1  CLC   0(1,R2),PERIOD               FIND PERIOD AFTER 1ST INDEX
         BE    FOUND#1                        YES, PROCESS
         LA    R2,1(R2)                     INCREMENT BY 1
         BCT   R6,INDEX#1                   GO LOOK FOR PERIOD DLM
FOUND#1  LA    R6,8                         SET MAXIMUM INDEX LENGTH
         LA    R2,1(R2)                     POINT TO SECOND INDEX NAME
         LR    R3,R2                        USE REG 3 FOR HIGH DLM
INDEX#2  CLC   0(1,R3),PERIOD               FIND PERIOD AFTER 2ND INDEX
         BE    FOUND#2                        YES, PROCESS
         LA    R3,1(R3)                     INCREMENT BY 1
         BCT   R6,INDEX#2                   GO LOOK FOR PERIOD DLM
FOUND#2  LR    R4,R3                        SAVE HIGH ADDRESS
         SLR   R4,R2                        GET LENGTH OF 2ND INDEX
         CH    R4,FOUR                      MIDDLE INDEX NAME 4 CHARS
         BNE   RC0
         CLC   0(R4,R2),TEST                IS MIDDLE NAME 'TEST'?
         BNE   RC0                           NO, PERFORM NORMAL VALD
         LA    R0,WTOMSGL                   WORKAREA FOR WTO ROUT 11
         GETMAIN R,LV=(0)
         LR    R10,R1                       SAVE FOR LATER DSECT
         USING WTODSECT,R10                 SET ADDRESSABILITY TO IT
         MVC   0(WTOMSGL,R10),WTOMSG        INITIALIZE WTO AREA
         L     R2,ACUUDSN                   GET BEGINNING OF DATASET
         LA    R8,DSNAME-WTOMSG(R10)        POINT TO OUTPUT FIELD
         CH    R7,H22                       IS DSN > 22 CHARACTERS
         BNH   DSNOK                         IT IS LESS, PROCESS
         LA    R7,22                        SET MAX DSNAME ALLOWED
DSNOK    BCTR  R7,0                         SUBTRACT 1 FOR EXECUTE
         EX    R7,MOVEDSN                   SAVE FIRST 22 CHARS OF DSN
         L     R5,ACUUVOL                   GET VOLUME NAME
         MVC   DSVOLME,0(R5)                GET VOLUME NAME IN WTO
         CLC   0(L'WORKVOL,R5),WORKVOL      IS IT A WORK VOLUME???
         BE    RC0                           YES, PERFORM NORMAL VALD
         CLI   ACUUFLG3,ACUU3VSM            VSAM OPEN ALLOCATION
         BE    VSAMOPEN                       YES, SET WTO MSG
         CLI   ACUUFLG3,ACUU3CMS            CATLG MGMT FUNCTION
         BE    CTLGFUNC                       YES, SET WTO MSG
         CLI   ACUUFLG3,ACUU3DSD            NEW DATASET ALLOCATION
         BE    NEWDSD                         YES, SET WTO MSG
         CLI   ACUUFLG3,ACUU3REN            RENAME DSNAME FUNCTION
         BE    RENFUNC                        YES, SET WTO MSG
         CLI   ACUUFLG3,ACUU3SCR            SCRATCH DSNAME FUNCTION
         BE    SCRFUNC                        YES, SET WTO MSG
         B     RC0                          PERFORM NORMAL VALD
VSAMOPEN MVC   DSFUNC,VSAM#                 VSAM OPEN MESSAGE
         B     FAIL
CTLGFUNC MVC   DSFUNC,CTLG#                 CATLG MGMT MESSAGE
         B     FAIL
NEWDSD   MVC   DSFUNC,ALLOC#                ALLOCATION MESSAGE
         B     FAIL
RENFUNC  MVC   DSFUNC,RENAME#               RENAME MESSAGE
         B     FAIL
SCRFUNC  MVC   DSFUNC,SCRATCH#              SCRATCH MESSAGE
FAIL     WTO   MF=(E,(R10))                 ISSUE MSG FOR USER
         LA    R0,WTOMSGL                   GETMAIN LENGTH
         FREEMAIN R,LV=(0),A=(10)           FREE WTOMSG WORKAREA
         $EPILOG 48                         TERMINATE,LOG, NO ACF2 WTO
RC0      SLR   R15,R15                      ZERO REG 15
FINIS    LTR   R10,R10                      DID WE ISSUE WTOMSG?
         BZ    ENDPGM
         LA    R0,WTOMSGL                   GETMAIN LENGTH
         FREEMAIN R,LV=(0),A=(10)           FREE WTOMSG WORKAREA
ENDPGM   $EPILOG
WTOMSG   DS    0F
         DC    AL2(WTOMSGL-4)
MCSFLAG  DC    B'1000000000000000'
FUNCTION DC    CL30'                             '
DSNAME   DC    CL22'                      '
         DC    C' ON VOLUME '
VOLUME   DS    CL6
         DC    C' DENIED BY INSTALLATION EXIT'
DESCFLG  DC    B'0000000000000000'
ROUTFLG  DC    B'0000000000100000'   ROUTCDE=11
WTOMSGL  EQU   *-WTOMSG
VSAM#    DC    C'      DATASET VSAM OPEN FOR'
CTLG#    DC    C'DATASET CATALOG REQUEST FOR'
ALLOC#   DC    C'       DATASET ALLOCATE FOR'
RENAME#  DC    C'         DATASET RENAME FOR'
SCRATCH# DC    C'        DATASET SCRATCH FOR'
WORKVOL  DC    C'STM3'
TEST     DC    C'TEST'
FOUR     DC    H'4'
BLANK    DC    C' '
PERIOD   DC    C'.'
ZERO     DC    X'00'
H22      DC    H'22'
MOVEDSN  MVC   0(0,R8),0(R2)
SOFTMASK DC    XL8'00'       PREFIX(********) REALLY BINARY ZEROES!
SS       DC    C'SS'         SYSTEM SUPPORT PREFIX LOGONID
         LTORG
WTODSECT DSECT
         DS    CL4     WTOMSGL AND BIT FLAGS
DSFUNC   DS    CL30    DATASET FUNCTION MESSAGE
DSDSN    DS    CL22    FIRST 22 BYTES OF DATASET
         DS    CL11    ON VOLUME
DSVOLME  DS    CL6        VOLSER
         DS    CL28    DENIED BY INSTALLATION EXIT
         LIDREC DSECT=YES
         ACSXP DSECT=YES
         END VLDEXIT
