         MACRO
&NAME    REG   &ID,&ADDBASE,&ADDBAS2
         SPACE
***********************************************************************
*                                                                     *
*      REG MACRO VERSION 1.1 REVISED 12/23/80 BY CDW TO ADD COMMENTS  *
*                        1.2 REVISED 05/07/82 BY CJM TO ADD THIRD     *
*                                                       BASE REGISTER *
*          FORMAT OF USE                                              *
*                                                                     *
*     NAME   REG  ID,BASE1,BASE2                                      *
*                                                                     *
*      NAME IS THE NAME OF THE FIRST CSECT IN PROGRAM.                *
*      REG IS THE MACRO.                                              *
*      ID IS A STRING PASSED TO THE SAVE MACRO FOR IDENTIFYING MODULE.*
*      BASE1 IS THE REGISTER TO USE AS THE SECOND BASE REG.           *
*      BASE2 IS THE REGISTER TO USE AS THE THIRD BASE REG.            *
*      IF ONLY ID IS CODED THERE WILL BE ONE BASE REGISTER            *
*      USE RETURN72 TO FREE SAVE AREA - DO NOT CODE 'L  R13,4(R13)'   *
*                                                                     *
***********************************************************************
*                                                                     *
**                    ****   E Q U A T E S   ****                    **
*                                                                     *
***********************************************************************
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
RETREG   EQU   14
COMBASE  EQU   12
BASE1    EQU   11
BASE2    EQU   10
&NAME    CSECT
         USING *,15
         SAVE  (14,12),,&ID
         BALR  COMBASE,0
         AIF   (K'&ADDBASE EQ 0).ONEBAS
         USING *,COMBASE,&ADDBASE
PBASE    EQU   *
         LA    &ADDBASE,4095(COMBASE)
         LA    &ADDBASE,1(&ADDBASE)
         AIF   (K'&ADDBAS2 EQ 0).SKIPUSE
         USING PBASE+8192,&ADDBAS2
         LA    &ADDBAS2,4095(&ADDBASE)
         LA    &ADDBAS2,1(&ADDBAS2)
         AGO   .SKIPUSE
.ONEBAS  ANOP
         USING *,COMBASE
PBASE    EQU   *
.SKIPUSE ANOP
         DROP  R15
         LA    R0,72                   GET A
         BAL   R1,*+4                     SAVE
         SVC   10                            AREA
         ST    R13,4(,R1)              BACK CHAIN SAVE AREAS
         ST    R1,8(,R13)              FORWARD CHAIN SAVE AREAS
         LR    R13,R1                  SET R13 TO NEW SAVE AREA
         L     R1,4(,R13)              SET R1 TO SAVE AREA AT ENTRY
         L     R1,24(,R1)              RESTORE R1 FROM SAVE AREA
         SPACE
         MEND
