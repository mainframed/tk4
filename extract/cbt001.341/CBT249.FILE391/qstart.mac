         MACRO
&NAME    QSTART &MAIN=NO,&TYPE=NORMAL                             ONL01
         GBLB  &QPFK          PF-KEY OPTION
         GBLB  &QACF2         ACF2 AUTH CHECKING                   FCI*
         GBLB  &QSP           MVS/SP LEVEL OF JES2.               UF020
         GBLB  &QGEN          FORCE PRINT GEN OF MACRO EXPANSIONS UF019
         GBLB  &QBGEN         FORCE PRINT GEN OF CNTL BLOCK MACROSONL01
         GBLB  &QJTIP         JTIP PRODUCT INSTALLED              UF025
         GBLB  &QDBC          DBC  PRODUCT INSTALLED              UF024
         GBLB  &QRNB          ENABLES RNB-SPECIFIC CODE           RNB05
         GBLB  &QRACF         RACF CHECKING                       RNB03
         GBLC  &QRACUSR       NEW RACF USERID                     RNB03
         GBLB  &QNERDC        ENABLES NERDC SPECIFIC CODE         UF032
         GBLB  &QONL          ENABLES ONL SPECIFIC CODE           ONL06
         GBLC  &QDSPRFX       DEFINES HASPCKPT/HASPACE DSN PREFIX ONL09
         GBLC  &QCKPTV        VOLSER OF CHECKPOINT VOLUME         UF042
         GBLC  &QSIDS(7)      SMF ID'S OF SHARED CPU'S            UF042
         GBLC  &Q1CMD         DEFAULT FIRST COMMAND               UF042
         GBLC  &QPF(24)       PFK DEFINITIONS                     UF042
.*                                                                UF019
.* QPFK=0 SELECTS NO PF-KEY SUPPORT
.* QPFK=1 SELECTS PF=KEY SUPPORT
.*QACF2=0 SELECTS NO ACF2 CHECKING (SHOPS WITHOUT ACF2)            FCI*
.*QACF2=1 SELECTS ACF2 AUTH TO SYSOUT VIA DSN='SYSOUT.LID.JOBNAME' FCI*
.*  QSP=0 SELECTS PRE-SP SUPPORT                                  UF020
.*  QSP=1 SELECTS SP SUPPORT                                      UF020
.* QGEN=0 SELECTS PRINT NOGEN OPTION                              UF019
.* QGEN=1 SELECTS PRINT GEN OPTION                                UF019
.*QBGEN=0 SELECTS PRINT NOGEN OPTION FOR CONTROL BLOCK MACROS     ONL01
.*QBGEN=1 SELECTS PRINT GEN OPTION FOR CONTROL BLOCK MACROS       ONL01
.*QJTIP=0 SELECTS NO JTIP SUPPORT.                                UF025
.*QJTIP=1 SELECTS SHOPS WITH JTIP INSTALLED                       UF025
.* QDBC=0 SELECTS NO DBC SUPPORT.                                 UF024
.* QDBC=1 SELECTS SHOPS WITH DBC INSTALLED                        UF024
.*QRACF=0 SELECTS NO RACF SUPPORT                                 RNB03
.*QRACF=1 SELECTS RACF SUPPORT FOR ACCESS TO THE SPOOL/CHECKPOINT RNB03
.*        AND FOR THE XP AND XDS COMMANDS. DEFINE TWO RACF        RNB03
.*        RESOURCES IN CLASS(APPL): QUEUEXP WILL CONTROL THE XP   RNB03
.*        COMMAND AND QUEUEXDS WILL PROVIDE FURTHER CONTROL OVER  RNB03
.*        THE XDS COMMAND. WHEN THE RACHECK FOR THE XDS COMMAND   RNB03
.*        IS DONE, THE JOBNAME WILL BE SPECIFIED AS THE APPLID.   RNB03
.*        THUS, IF YOU SPECIFY AUDIT(ALL) FOR APPL-QUEUEXDS YOU   RNB03
.*        WILL KNOW WHAT DATA YOUR PRIVILEGED USERS HAVE BEEN     RNB03
.*        EXAMINING (AUDITORS LIKE THIS). QRACF IS NOT SUPPORTED  RNB03
.*        VIA THE SYSPARM OPTION.                                 RNB03
.*QRACUSR IS USED IF YOUR SPOOL AND CHECKPOINT ARE DEFINED TO     RNB03
.*        RACF WITH UACC=NONE. IT SPECIFIES A USERID THAT HAS     RNB03
.*        ACCESS TO THE SPOOL AND CHECKPOINT WITH READ AUTHORITY. RNB03
.*        DURING INITIALIZATION, THE SPECIFIED USERID WILL BE     RNB03
.*        SUBSTITUTED INTO THE RACF ACEE SO THE USER OF QUEUE HAS RNB03
.*        ACCESS TO THE SPOOL AND CHECKPOINT DATA SETS ONLY WHILE RNB03
.*        THE QUEUE COMMAND IS IN PROGRESS. IF QRACF=1, THEN      RNB03
.*        QRACUSR MUST BE SPECIFIED. IF YOUR SPOOL AND CHECKPOINT RNB03
.*        HAVE A UACC OF >= READ, SPECIFY QRACUSR AS NULL ('') TO RNB03
.*        BYPASS CHANGING THE ACEE USERID. QRACUSR IS NOT         RNB03
.*        SUPPORTED VIA THE SYSPARM OPTION.                       RNB03
.*                                                                UF019
&QPFK    SETB  1
&QACF2   SETB  0                                                   FCI*
&QSP     SETB  1                                                  UF020
&QGEN    SETB  1                                                  ONL01
&QBGEN   SETB  0                                                  ONL01
&QJTIP   SETB  0                                                  UF025
&QDBC    SETB  0                                                  UF024
&QRNB    SETB  0                                                  RNB05
&QRACF   SETB  0                                                  RNB03
&QRACUSR SETC  'QCMD'                                             RNB03
&QNERDC  SETB  1                                                  UF032
&QONL    SETB  0                                                  ONL06
&QDSPRFX SETC  'SYS1'                                             ONL09
&QCKPTV  SETC  'SPOOL1'                                           UF042
&QSIDS(1) SETC 'NER1'                                             UF042
&QSIDS(2) SETC 'NER2'                                             UF042
&QSIDS(3) SETC 'NER3'                                             UF042
&QSIDS(4) SETC 'NER4'                                             UF042
&QSIDS(5) SETC 'NER5'                                             UF042
&QSIDS(6) SETC 'NER6'                                             UF042
&QSIDS(7) SETC 'NER7'                                             UF042
&Q1CMD    SETC 'HELP'                                             UF042
&QPF(1)  SETC  'HELP'                                             UF043
&QPF(2)  SETC  'DA'                                               UF043
&QPF(3)  SETC  'END'                                              UF043
&QPF(4)  SETC  'PRINT'                                            UF043
&QPF(5)  SETC  'FIND'                                             UF043
&QPF(6)  SETC  'DI'                                               UF043
&QPF(7)  SETC  '-     21'                                         UF043
&QPF(8)  SETC  '+     21'                                         UF043
&QPF(9)  SETC  'DO'                                               UF043
&QPF(10) SETC  'CO    1'                                          UF043
&QPF(11) SETC  'CO    41'                                         UF043
&QPF(12) SETC  'ST'                                               UF043
.*                                                                UF019
         LCLA  &CNT,&CTR,&STRNG,&LNTH,&SUB                        UF019
         AIF   (K'&SYSPARM EQ 0).SYSPEND                          UF019
         MNOTE *,'SYSPARM IS ''&SYSPARM'' '                       UF019
         AIF   ('&SYSPARM'(1,1) EQ '(').MORE                      UF019
  MNOTE 2,'SYSPARM SYNTAX ERROR--MUST BE ENCLOSED IN PARENS'      UF019
         AGO   .SYSPEND                                           UF019
.MORE    ANOP                                                     UF019
&CNT     SETA  K'&SYSPARM                                         UF019
&CTR     SETA  1                                                  UF019
&STRNG   SETA  &CTR+1                                             UF019
.LOOP    AIF   ('&SYSPARM'(&CTR,1) EQ ',' OR &CTR EQ &CNT).FOUND  UF019
&CTR     SETA  &CTR+1                                             UF019
         AGO   .LOOP                                              UF019
.FOUND   ANOP                                                     UF019
&LNTH    SETA  &CTR-&STRNG                                        UF019
         AIF   (&LNTH EQ 0).NULL                                  UF019
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'PFK').SPNPFK         UF019
&QPFK    SETB  1                                                  UF019
.SPNPFK  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOPFK').SPNPFK2      UF019
&QPFK    SETB  0                                                  UF019
.SPNPFK2 AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'ACF2').SPNACF        UF019
&QACF2   SETB  1                                                  UF019
.SPNACF  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOACF2').SPNACF2     UF019
&QACF2   SETB  0                                                  UF019
.SPNACF2 AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'SP').SPNSP           UF019
&QSP     SETB  1                                                  UF019
.SPNSP   AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOSP').SPNSP2        UF019
&QSP     SETB  0                                                  UF019
.SPNSP2  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'GEN').SPNGEN         UF019
&QGEN    SETB  1                                                  UF019
.SPNGEN  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOGEN').SPNGEN2      UF019
&QGEN    SETB  0                                                  UF019
.SPNGEN2 ANOP                                                     UF019
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'BGEN').BGENN1        ONL01
&QBGEN   SETB  1                                                  ONL01
.BGENN1  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOBGEN').BGENN0      ONL01
&QBGEN   SETB  0                                                  ONL01
.BGENN0  ANOP  ,                                                  ONL01
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'JTIP').SPNJTIP       UF019
&QJTIP   SETB  1                                                  UF019
.SPNJTIP AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOJTIP').SPNJTP2     UF019
&QJTIP   SETB  0                                                  UF019
.SPNJTP2 ANOP                                                     UF019
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'DBC').SPNDBC         UF019
&QDBC    SETB  1                                                  UF019
.SPNDBC  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NODBC').SPNDBC2      UF019
&QDBC    SETB  0                                                  UF019
.SPNDBC2 ANOP                                                     UF019
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NERDC').SPNNER       UF032
&QNERDC  SETB  1                                                  UF032
&QPF(2)  SETC  'L *    +'                                         UF043
&QPF(9)  SETC  'HELP'                                             UF043
.SPNNER  AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NONERDC').SPNNER2    UF032
&QNERDC  SETB  0                                                  UF032
.SPNNER2 ANOP                                                     UF032
         AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'ONL').ONLN1          ONL06
&QONL    SETB  1                                                  ONL06
.ONLN1   AIF   ('&SYSPARM'(&STRNG,&LNTH) NE 'NOONL').ONLN0        ONL06
&QONL    SETB  0                                                  ONL06
.ONLN0   ANOP  ,                                                  ONL06
.NULL    ANOP                                                     UF019
         AIF   (&CTR EQ &CNT).SYSPEND                             UF019
&CTR     SETA  &CTR+1                                             UF019
&STRNG   SETA  &CTR                                               UF019
         AGO   .LOOP                                              UF019
.SYSPEND ANOP                                                     UF019
         AIF   ('&TYPE'  EQ 'GLOBAL').GEN  FORCE PRINT GEN FOR QCOMMON
         AIF   (&QGEN).GEN                                        UF019
         PRINT NOGEN                                              UF019
         AGO   .START                                             UF019
.GEN     PRINT GEN                                                UF019
.START   ANOP                                                     UF019
         AIF   (NOT &QONL).NOTONL                                 ONL06
&QPFK    SETB  1                   THESE                          ONL06
&QACF2   SETB  0                    PARAMETERS                    ONL06
&QSP     SETB  1                     ARE                          ONL06
&QGEN    SETB  1                      FORCED                      ONL06
&QBGEN   SETB  0                       IF                         ONL06
&QJTIP   SETB  0                        &QONL=1                   ONL06
&QDBC    SETB  0                         .                        ONL06
&QRNB    SETB  0                          .                       ONL06
&QRACF   SETB  0                           .                      ONL06
&QNERDC  SETB  0                            .                     ONL06
&QDSPRFX SETC  'JES2'                        .                    ONL06
&QSIDS(1) SETC 'X10A'                         .                   ONL06
&QSIDS(2) SETC 'X10B'                          .                  ONL06
&QSIDS(3) SETC 'X10C'                           .                 ONL06
&QSIDS(4) SETC 'X10D'                            .                ONL06
&QPF(2)  SETC  'L *    +'                         .               ONL06
&QPF(6)  SETC  'FA /42I/'                          .              ONL06
&QPF(9)  SETC  'DD *'                               .             ONL06
.NOTONL  ANOP  ,                                                  ONL06
***********************************************************************
 MNOTE *,'PFK=&QPFK, ACF2=&QACF2, SP=&QSP, GEN=&QGEN, BGEN=&QBGEN, JTIP+
               =&QJTIP, DBC=&QDBC, RACF=&QRACF'                   ONL01
 MNOTE *,'RACUSR=&QRACUSR, NERDC=&QNERDC, ONL=&QONL, DSPRFX=&QDSPRFX, S+
               POOL=&QCKPTV'                                      ONL01
***********************************************************************
         SPACE 1                                                  ONL01
&NAME    START 0
         AIF   ('&TYPE' EQ 'GLOBAL' OR '&MAIN' EQ 'YES').PRTSKP1  ONL01
         AIF   (&QBGEN).PRTSKP1                                   ONL01
         PUSH  PRINT                                              ONL01
         PRINT OFF                                                ONL01
.PRTSKP1 ANOP  ,                                                  ONL01
         SPACE 1                                                  ONL01
***********************************************************************
*                                                                     *
*   THE QUEUE COMMAND WAS WRITTEN FOR YOUR AMUSEMENT AND AMAZEMENT BY *
*     THE INTERGALACTIC MESSIANIC INDIVIDUAL GROUP THERAPY DIVISION   *
*     OF TRW SYSTEMS, 1 SPACE PARK, REDONDO BEACH, CA. 90278          *
*   THE ORIGINAL PROGRAMMING WAS DONE BY ANDY ZIDE, CHIEF PROGRAMMER  *
*     AND RESIDENT FLAKE WHO HAS SINCE DEPARTED TRW TO PLAY WITH      *
*     MICROCODE.                                                      *
*   PLEASE ADDRESS ANY COMMENTS, SUGGESTIONS, COMPLAINTS, OR THREATS  *
*     TO STEVE ANDERSON (R3/1028)   (213) 535-0682   OR               *
*        PAUL FELIX     (R3/1028)   (213) 535-0682                    *
*                                                                     *
*   STEVE ANDERSON HAS LEFT TRW.  PLEASE ADDRESS ANY COMMENTS OR      *
*   FIXES TO JACK SCHUDEL AT THE ADDRESS LISTED BELOW.                *
*                                                                     *
*   THE FOLLOWING INDIVIDUALS HAVE MADE MODIFICATIONS TO QUEUE WHICH  *
*     HAVE BEEN INCORPORATED INTO THIS CURRENT VERSION.               *
*                                                                     *
*        VILKO MACEK                                                  *
*        INSURANCE CORPORATION OF BRITISH COLUMBIA                    *
*        MODIFICATION: PFK SUPPORT                                    *
*                                                                     *
*        TRW ISD                                                      *
*        ANAHEIM, CALIFORNIA                                          *
*        MODIFICATION: DISPLAY CPU TIME FOR BATCH, STC AND TSO        *
*                      DISPLAY INITIATORS                             *
*                                                                     *
*        KEN TRUE                                                     *
*        FAIRCHILD CAMERA AND INSTRUMENT                              *
*        MOUNTAIN VIEW, CALIFORNIA                                    *
*        MODIFICATION: ACF2 SUPPORT                                   *
*                      PRINT SCREEN SUPPORT                           *
*                                                                     *
*        JACK SCHUDEL                                                 *
*        NORTHEAST REGIONAL DATA CENTER                               *
*        233 SSRB, UNIVERSITY OF FLORIDA                              *
*        GAINESVILLE, FLORIDA  32611                                  *
*        (904) 392-4601                                               *
*        MODIFICATIONS:  SEE MEMBER $UFDOC                            *
*                                                                     *
*        WALT FARRELL                                                 *
*        RAINIER NATIONAL BANK                                        *
*        P. O. BOX C34030                                             *
*        SEATTLE, WASHINGTON  98124                                   *
*        (206) 433-7467                                               *
*        MODIFICATIONS:  SEE MEMBER $RNBDOC                           *
*                                                                     *
*        ROBERT M. JINKINS                                            *
*        OAK RIDGE NATIONAL LABORATORY                                *
*        BLDG. 4500-N  MS-255                                         *
*        P.O. BOX X                                                   *
*        OAK RIDGE, TENN.  37830                                      *
*        (615) 574-7208/5300                                          *
*          FTS 624-7208/5300                                          *
*        SHARE INSTALLATION CODE:  ONL                                *
*        MODIFICATIONS:  SEE MEMBER $ONLDOC                           *
*                                                                     *
***********************************************************************
         SPACE 2                                                  ONL01
*                                                                 ONL01
*  REGISTER EQUATES                                               ONL01
*                                                                 ONL01
         SPACE 1                                                  ONL01
R0       EQU   0              TEMP WORK
R1       EQU   1              TEMP WORK
R2       EQU   2              WORK REG
R3       EQU   3              WORK REG
R4       EQU   4              WORK REG
R5       EQU   5              WORK REG
R6       EQU   6              WORK REG
R7       EQU   7              WORK REG
R8       EQU   8              WORK REG
R9       EQU   9              ADDRESS OF COMMON SUB-AREA
R10      EQU   10             ADDRESS OF COMMON SUB-AREA
R11      EQU   11             ADDRESS OF COMMON VECTOR TABLE
R12      EQU   12             BASE REGISTER
R13      EQU   13             SAVE AREA AND TEMPORARY WORK AREA
R14      EQU   14             TEMP WORK
R15      EQU   15             TEMP WORK
         AIF   ('&TYPE' EQ 'GLOBAL' OR '&MAIN' EQ 'YES').PRTSKP2  ONL01
         AIF   (&QBGEN).PRTSKP2                                   ONL01
         SPACE 1                                                  ONL01
         POP   PRINT                                              ONL01
.PRTSKP2 ANOP  ,                                                  ONL01
         AIF   ('&TYPE' EQ 'NORMAL').GO
         MEXIT
.GO      ANOP
         SPACE 1                                                  ONL01
         USING *,R12               BASE REGISTER
         USING QCOMMON,R11         ACCESS TO COMMON VECTOR TABLE
         STM   R14,R12,12(R13)     STANDARD REGISTER SAVE
         LR    R12,R15             LOAD BASE REG
         B     *+28                BRANCH AROUND IDENTIFIER
         DC    CL8'&NAME'          MODULE IDENTIFIER
         DC    CL8'&SYSDATE'       ASSEMBLY DATE
         DC    CL8' &SYSTIME'      ASSEMBLY TIME
         AIF   ('&MAIN' EQ 'YES').MAINYES
         LR    R15,R13             RETAIN SAVE AREA ADDR
         LA    R13,72(R13)         POINT TO NEXT SAVE AREA
         AGO   .MAINNO
.MAINYES ANOP
         LR    R2,R1               SAVE PARAMETER REGISTER
         GETMAIN R,LV=4096         GET STORAGE FOR SAVEAREA
         ST    R1,8(R13)           FORWARD POINTER
         ST    R13,4(R1)           BACKWARD POINTER
         LR    R13,R1              MOVE ADDR TO R1
         MEXIT
.MAINNO  ANOP
         ST    R13,8(R15)          FORWARD POINTER
         ST    R15,4(R13)          BACKWARD POINTER
         MEND
