TWA      TITLE 'COMPARE THE TABLES FOR COMPARE PROCESS '
***********************************************************************
*                                                                     *
*        COMPTBL                                                      *
*        -------                                                      *
*                                                                     *
*           COMPARES THE ATTRIBUTE TABLES GENERATED BY                *
*           PROCESSING 2 COPIES OF A LOAD MODULE.                     *
*                                                                     *
*           OUTPUT IS A TABLE OF THE DIFFERENCES FOUND                *
*           WHICH WILL BE PRINTED BY THE MAINLINE PROGRAM             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        ABENDS                                                       *
*         USER CODE   REASON                                          *
*          U0200      NUMBER OF PARAMETER RECORDS NOT POSITIVE        *
*          U0201      PRINT MESSGE TABLE OVERFLOWED                   *
*                                                                     *
***********************************************************************
         EJECT
COMPTBL  CENTER TYPE=C
         SYMREGS
         EJECT
***********************************************************************
*                                                                     *
*        REGISTER USAGE                                               *
*                                                                     *
*           R2    RECORD POINTER TO MESSAGE TABLE ROUTINE             *
*           R3    PLIST # 1           BASE                            *
*           R4    PLIST # 2           BASE                            *
*           R5    MESSAGE TABLE AREA  BASE                            *
*           R6    POINTS TO CURRENT ENTRY IN PLIST # 1                *
*           R7    POINTS TO CURRENT ENTRY IN PLIST # 2                *
*           R8    POINTS TO CURRENT ENTRY IN MESSAGE TABLE AREA       *
*           R9    DECREASING COUNT OF MESSAGES TO PROCESS IN # 1      *
*           R10   CURRENT COUNT OF MESSAGES IN MESSAGE TABLE AREA     *
*           R11   U N U S E D                                         *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PARAMETERS PASSED BY THE CALLER                              *
*                                                                     *
*           #1    PLIST # 1           BASE                            *
*           #2    PLIST # 2           BASE                            *
*           #3    MESSAGE TABLE AREA  BASE                            *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         LM    R3,R5,0(R1)          PICK UP PASSED PARAMETERS
         USING PLIST,R3                PLIST DSECT (COPY # 1)
*                    R4 ALSO POINTS TO PLIST DSECT (COPY # 2)
         USING MTABL,R5
         L     R9,PRECCT            GET CURRENT RECORDS IN PLIST #1
         BCTR  R9,0                 ADJUST FOR HEADER RECORD
         LA    R6,PREC              POINT TO THE FIRST SLOT
         LTR   R9,R9                JUST A BIT OF VALIDITY CHECK
         BP    OK
         ABEND 200,DUMP             IN CASE OF TRASH
OK       DS    0H
         LA    R8,MSGA              FIRST AVAILABLE MESSAGE SLOT
         SR    R10,R10              CLEAR THE COUNT OF MSG IN USE
         ST    R10,MSGCCT           STORE IN THE TABLE
*
*        ALL INITIALIZATION REGS HAVE BEEN LOADED
*
         EJECT
PL1      DS    0H           ENTER HERE TILL FIRST LIST EXHASUTED
         LA    R6,L'PREC(,R6)   GET THE NEXT TABLE #1 RECORD
         LA    R2,00(,R6)           POINT TO CURRENT RECORD
         BAL   R14,PRINTR           PUT IT IN THE TABLE
         LA    R7,PREC-PLIST(,R4)   POINT TO FIRST RECORD IN # 2
         LA    R7,L'PREC(,R7)       SKIP THE HEADER RECORD IN #2
         L     R11,PRECCT-PLIST(,R4) GET COUNT OF ENTRIES IN # 2
         BCTR  R11,0                ADJUST FOR THE HEADER
PLL2     CLC   0(17,R6),0(R7)       COMPARE NAME PORTION
         BE    GOTIT                    WE FOUND THE ONE WE WANT
         LA    R7,L'PREC(,R7)       NOT FOUND, ADDRESS THE NEXT
         BCT   R11,PLL2              TO NEXT # 2 SLOT IF SOME LEFT
*        NAME NOT FOUND IN TABLE # 2, MUST BE NEW
         LA    R2,NEWM              POINT TO NEW CSECT MESSAGE
         BAL   R14,PRINTR           PUT IT OUT
         XC    00(L'PREC,R6),00(R6) WIPE OUT THE USED ENTRY
         B     PLNEXT               DONE WITH THIS ONE
GOTIT    DS    0H                   NOW DO FULL COMPARE
         CLC   00(L'PREC,R6),00(R7) SEE IF EQUAL
         BNE   DIFFER               NO, THEY DIFFER SOMEHOW
         LA    R2,NOCHGM            POINT TO NO CHANGE MESSAGE
         BAL   R14,PRINTR           AND OUTPUT IT
*        TEST COULD BE PLACED HERE TO VALIDATE STANDARDS
PL1A     XC    00(L'PREC,R6),00(R6)
         XC    00(L'PREC,R7),00(R7)
PLNEXT   DS    0H
         BCT   R9,PL1               IF SOME ARE LEFT
*      CHECK IF ANY TABLE 2 RECORDS ARE LEFT, THIS MEANS DROPPED CSECT
*        SCAN TABLE # 2 FOR NON-ZERO MESSAGES
*        THESE HAVE NOT BEEN MATCHED
         LA    R7,PREC-PLIST(,R4)   FIRST SLOT IN TABLE
*            NOTE THAT WE WILL SKIP THE HEADER RECORD
         L     R11,PRECCT-PLIST(,R4) COUNT OF TOTAL ENTRIES
         BCTR  R11,0                 REMOVE 1 FOR HEADER
NXT2     LA    R7,L'PREC(,R7)        TO THE NEXT RECORD
         OC    0(L'PREC,R7),0(R7)    SEE IF FIELD IS ALL BIN 0'S
         BNZ   UNMATCH               IF NOT, THIS ONE WAS NOT MATCHED
NXT3     BCT   R11,NXT2              STEP THRU TABLE
         B     GOBACK
UNMATCH  DS    0H    TABLE 2 RECORD NOT MATCHED BY TABLE 1 RECORD
         LA    R2,0(,R7)            POINT TO THE RECORD
         BAL   R14,PRINTR
         LA    R2,GONEM
         BAL   R14,PRINTR
         XC    0(L'PREC,R7),0(R7)
         B     NXT3
DIFFER   DS    0H                   HANDLE CHANGES
         LA    R2,00(R7)            POINT TO OLD INFO
         BAL   R14,PRINTR
         LA    R2,CHGM              SET UP AND ISSUE CHANGED MESSAGE
         BAL   R14,PRINTR
         B     PL1A                 DONE, DO CLEANUP
         EJECT
GOBACK   RETRN  RC=0
         EJECT
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        PRINT LINE LOAD ROUTINE                                      *
*           R2 POINTS TO THE RECORD TO STORE                          *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
PRINTR   DS    0H
         MVC   0(133,R8),0(R2) COPY THE RECORD
         LA    R8,133(,R8)     INCREMENT THE RECORD POINTER
         ST    R8,MSGCA        AND STORE IT BACK
         LA    R10,1(,R10)     INCREMENT COUNT
         C     R10,MSGMAX      CHECK AGAINST MAX
         BL    PRINT1          OK TO CONTINUE
         ABEND 201,DUMP        WHO KNOWS WHAT HIT US
PRINT1   DS    0H
         ST    R10,MSGCCT      STORE NEW MESSAGE COUNT FOR LATER
         BR    R14
         EJECT
         PRINT GEN
NOCHGM   DC    CL133'         NO CHANGE IN ABOVE CSECT'
NEWM     DC    CL133'         ABOVE CSECT HAS BEEN ADDED'
GONEM    DC    CL133'         ABOVE CSECT HAS BEEN DELETED'
CHGM     DC    CL133'         ABOVE CSECT HAS BEEN CHANGED'
         LTORG
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE MESSAGE BUILD AREA  1.00                       *
*                                                                     *
***********************************************************************
MTABL    DSECT
MSGCA    DS    A         ADDR OF CURRENT SLOT
MSGCCT   DS    F         NUMBER OF RECORDS IN USE
MSGMAX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
MSGMAXX  EQU   255       COUNT IN THE NEXT DS
MSGA     DS    255CL133  SPACE FOR RECORDS
MTABLL   EQU   *-MTABL
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE PARAMETER AREA 1.02                            *
*                                                                     *
***********************************************************************
PLIST    DSECT
CNAME    DS    CL8       MEMBER NAME TO PROCESS
JFCB     DC    0D'0',176X'00'      AREA TO READ THE JFCB INTO
PLISTCA  DS    A         ADDR OF CURRENT SLOT
PRECCT   DS    F         NUMBER OF RECORDS IN USE
PRECMX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
PLISTC   EQU   255       COUNT IN THE NEXT DS
PREC     DS    255CL133  SPACE FOR RECORDS
PLISTL   EQU   *-PLIST
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE DETAIL PRINT LINE                              *
*                                                                     *
***********************************************************************
DETLINE  DSECT
         DS    CL1 -
DETMEM   DS    CL8 -               MEMBER NAME
         DS    CL1 -
DETTTRC  DS    XL8 -               TTRC
         DS    CL1 -
DETATTR  DS    XL4 -               MODULE ATTRIBUTES
         DS    CL1 -
DETSIZE  DS    CL6 -               MODULE SIZE
         DS    CL1 -
DETEP    DS    CL6 -               MODULE ENTRY POINT
         DS    CL1 -
DETDTL   DS    CL6 -               DATE LINKED
         DS    CL1 -
DETMSG   EQU   *
FLAG1    DS    CL8 -               SYMDUMP
FLAG2    DS    CL8 -               FLOW
FLAG3    DS    CL8 -               STATE
FLAG4    DS    CL8 -               TESTM
FLAG5    DS    CL8 -               RES
FLAG6    DS    CL8 -               ENDJOB
FLAG7    DS    CL8 -               OBJ370
FLAG8    DS    CL8 -               CAPEX
FLAGA    DS    CL2 -               VS OR V4
         DS    C
FLAGB    DS    CL5 -               COUNT FOR VS
         DS    C
FLAGC    DS    CL5 -               TRACE FOR VS
         DS    C
FLAGD    DS    CL5 -               DTECT FOR CAPEX
LNLGTH   EQU   *-DETLINE
         END
