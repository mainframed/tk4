TWA      TITLE 'COMPARE TWO LOAD MODULES FOR DIFFERENCES '
***********************************************************************
*                                                                     *
*        COMPARE                                                      *
*        -------                                                      *
*                                                                     *
*        PROCESS PAIRS OF COPIES OF A PROGRAM AND DETECT WHICH        *
*        CSECTS ARE                                                   *
*                  1.  DIFFERENT                                      *
*                  2.  WITHIN INSTALLATION STANDARDS                  *
*                                                                     *
*        THIS VERSION SUPPORTS THE FOLLOWING COBOL COMPILERS          *
*             VERSION 4, COBOL VS.                                    *
*                                                                     *
*        FOR EACH MEMBER, THE FOLLOWING IS EXAMINED -                 *
*                                                                     *
*               IS PROGRAM MARKED EXECUTABLE                          *
*               IS IT COBOL?                                          *
*               IS IT OBJ360 OR OBJ370                                *
*               IS COBOL /TEST/ OPTION SPECIFIED                      *
*               IS COBOL /FLOW/ OPTION SPECIFIED                      *
*               IS COBOL /STATE/ OPTION SPECIFIED                     *
*               IS COBOL /SYMDUMP/ OPTION SPECIFIED                   *
*               IS COBOL /RESIDENT/ OPTION SPECIFIED    3/79          *
*               IS COBOL /ENDJOB/ OPTION SPECIFIED                    *
*               WAS COBOL /OPTIMIZE/ OPTION SPECIFIED - V4 ONLY       *
*               WAS CAPEX OPTIMIZE SPECIFIED                          *
*               WAS CAPEX DTECT SPECIFIED             - CAPEX ONLY    *
*               WAS COBOL V4 OR COBOL VS UTILIZED FOR COMPILE         *
*               WAS COBOL /COUNT/ OPTION SPECIFIED    - VS ONLY       *
*               WAS COBOL /TRACE/ VERB SPECIFIED      - VS ONLY       *
*               DATE LINKEDITED                                       *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        JCL / DD-CARD REQUIREMENTS                                   *
*                                                                     *
*        SAMPLE JOBSTREAM                                             *
*                                                                     *
*          //S1 EXEC PGM=COMPARE                                      *
*          //SYSPRINT  DD  SYSOUT=A      FOR THE REPORT               *
*          //PDS   DD  DSN=SYPP.PGMLIB,DISP=SHR                       *
*          //*         THE PDS TO ANALYZE NEW PGMS                    *
*          //PDS2  DD  DSN=SYPP.XGMLIB,DISP=SHR                       *
*          //*         THE PDS TO ANALYZE OLD PGMS                    *
*          //SYSIN  DD  *     MEMBER NAMES TO COMPARE                 *
*          NAME1                                                      *
*          NAME2                                                      *
*          NAME3                                                      *
*          NAMEN                                                      *
*          /*                                                         *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        ABENDS                                                       *
*          USER CODE    REASON                                        *
*           U0010       NO INPUT IN SYSIN                             *
*           U0014       OPEN FAILED FOR SYSIN                         *
*           U0015       CONTROL STATEMENT INVALID                     *
*           U0006       OPEN FAILED FOR SYSPRINT                      *
*           U0011       NON-ZERO RETURN CODE FROM COMPDS - FIRST CALL *
*           U0012       NON-ZERO RETURN CODE FROM COMPDS - 2ND CALL   *
*                       NOW REPLACED BY MESSAGE - NO ABEND            *
*                       ALSO WILL SET RC=4                            *
*           U0006       NON-ZERO RETURN CODE FROM COMPTBL             *
*                                                                     *
***********************************************************************
         EJECT
COMPARE  CENTER TYPE=C
         SYMREGS
         EJECT
         OPEN  (SYSPRINT,(OUTPUT)) OPEN SYSPRINT
         TM    SYSPRINT+48,X'10'   DID IT OPEN O.K.  ?
         BZ    ERROR6              NO - BRANCH
         OPEN  (SYSIN,(INPUT))     OPEN SYSIN
         TM    SYSIN+48,X'10'      DID IT OPEN O.K.  ?
         BZ    ERROR14             NO - BRANCH
         SPACE 1
LOOP     GET   SYSIN               READ CONTROL STMT
         OI    MSW,INPUT                SHOW THAT WE READ SOMETHING
         CLI   0(R1),C' '          IS SOMETHING THERE
         BE    ERROR15              NO, DIE
         MVC   REALNAME,0(R1)      MOVE NAME TO HOLD AREA
         MVC   MNAME,REALNAME      FOR POSSIBLE MSG USAGE
         GETMAIN R,LV=PLISTL,SP=3  GET AREA FOR FIRST LIBRARY
         ST    R1,PLIST1
         ST    R1,PLIST31
         ST    R1,PLIST41
         OI    PLIST1,X'80'        SET END OF PARM LIST
         USING PLIST,R1
         LA    R0,PLISTC           GET MAX NUMBER OF RECORDS
         ST    R0,PRECMX
         LA    R0,JFCB             GET ADDRESS OF JFCB WORK AREA
         ST    R0,EXLST1A          PUT IT IN DCB#1 EXIT LIST
         OI    EXLST1A,X'87'       MARK ENTRY AS ACTIVE, JFCB, LAST
         MVC   CNAME,REALNAME
         DROP  R1
         GETMAIN R,LV=PLISTL,SP=4  GET AREA FOR SECOND LIBRARY
         ST    R1,PLIST2
         ST    R1,PLIST32
         ST    R1,PLIST42
         OI    PLIST2,X'80'        SET END OF PARM LIST
         USING PLIST,R1
         LA    R0,PLISTC           GET MAX NUMBER OF RECORDS
         ST    R0,PRECMX
         LA    R0,JFCB             GET ADDRESS OF JFCB WORK AREA
         ST    R0,EXLST2A          PUT IT IN DCB#2 EXIT LIST
         OI    EXLST2A,X'87'       MARK ENTRY AS ACTIVE, JFCB, LAST
         MVC   CNAME,REALNAME
         DROP  R1
*
*        SET UP AND CALL THE PGM ANALYZER FOR THE FIRST LIBRARY
*
         LA    R1,PARM1            FIRST PARM LIST POINTER
         L     R15,=V(COMPDS)      POINT TO THE ROUTINE
         BALR  R14,R15             CALL THE ROUTINE
         LTR   R15,R15             CHECK ITS RETURN CODE
         BNZ   ERR01                CANNOT CONTINUE WITH THIS MEMBER
*
*        SET UP AND CALL THE PGM ANALYZER FOR THE SECOND LIBRARY
*
         LA    R1,PARM2            FIRST PARM LIST POINTER
         L     R15,=V(COMPDS)      POINT TO THE ROUTINE
         BALR  R14,R15             CALL THE ROUTINE
         LTR   R15,R15             CHECK ITS RETURN CODE
         BNZ   ERR02                CANNOT CONTINUE WITH THIS MEMBER
*
*        SET UP AND CALL THE ACTUAL COMPARE ROUTINE
*
         TM    MSW,TEST            ARE WE TEST MODE
         BZ    NOTEST1
*        L     R15,=V(SNAP)  TEMP  POINT TO THE ROUTINE
*        BALR  R14,R15             CALL THE ROUTINE
NOTEST1  DS    0H
         GETMAIN R,LV=MTABLL,SP=5  GET AREA FOR MESSAGE TABLE
         ST    R1,MTABL3
         ST    R1,MTABL4
         OI    MTABL3,X'80'        SET END OF PARM LIST
         USING MTABL,R1
         LA    R15,MSGA
         ST    R15,MSGCA           SET CURRENT SLOT TO FIRST
         XC    MSGCCT,MSGCCT       SET IN USE COUNT TO ZERO
         LA    R15,MSGMAXX         GET MAXIMUM SLOTS IN TABLE
         ST    R15,MSGMAX          SET IT IN THE TABLE
         DROP  R1
         LA    R1,PARM3            FIRST PARM LIST POINTER
         L     R15,=V(COMPTBL)     POINT TO THE ROUTINE
         BALR  R14,R15             CALL THE ROUTINE
         LTR   R15,R15             CHECK ITS RETURN CODE
         BNZ   ERR03                CANNOT CONTINUE WITH THIS MEMBER
         TM    MSW,TEST            ARE WE TEST MODE
         BZ    NOTEST2
*        L     R15,=V(SNAP)  TEMP  POINT TO THE ROUTINE
*        BALR  R14,R15             CALL THE ROUTINE
NOTEST2  DS    0H
         LA    R1,PARM4
         L     R15,=V(PRTTBL)      POINT TO THE ROUTINE
         BALR  R14,R15             CALL THE ROUTINE
         L     R2,MTABL3                PREPARE FOR FREEMAIN
         FREEMAIN R,LV=MTABLL,SP=5,A=(R2) FREE UP A TABLE
CONTINUE L     R2,PLIST1                PREPARE FOR FREEMAIN
         FREEMAIN R,LV=PLISTL,SP=3,A=(R2) FREE UP A TABLE
         L     R2,PLIST2                PREPARE FOR FREEMAIN
         FREEMAIN R,LV=PLISTL,SP=4,A=(R2) FREE UP A TABLE
         B     LOOP                     DO IT AGAIN, SAM
DONE     CLOSE (SYSPRINT,,SYSIN)   CLOSE SYSPRINT AND SYSIN
         SPACE 1
         FREEPOOL SYSPRINT         FREE THE SYSPRINT BUFFER POOL
         FREEPOOL SYSIN            FREE THE SYSIN BUFFER POOL
         SPACE 1
         TM    MSW,INPUT                DID WE HAVE SOME INPUT
         BZ    DONEX                     NO, ABEND
GOBACK   RETRN  RC=0
         EJECT
***********************************************************************
*                                                                     *
*        ERROR CONDITIONS                                             *
*                                                                     *
***********************************************************************
         SPACE 1
*        NO INPUT
DONEX    ABEND 10,DUMP             NO INPUT PRESENT
         SPACE 1
*        UNABLE TO OPEN SYSIN     -
ERROR14  LA    R2,SYSIN            POINT TO SYSIN    DCB
         ABEND 14,DUMP             ABEND
         SPACE 1
*        BLANK CONTROL STMT
ERROR15  LA    R2,0(,R1)           POINT TO BAD CONTROL RECORD
         ABEND 15,DUMP             ABEND
         SPACE 1
*        UNABLE TO OPEN SYSPRINT  -
ERROR6   LA    R2,SYSPRINT         POINT TO SYSPRINT DCB
         ABEND 6,DUMP              ABEND
ERR01    ABEND 11,DUMP             ABEND
ERR03    ABEND 13,DUMP             ABEND
         EJECT
ERR02    DS    0H      BAD RETURN CODE WHEN READING /OLD/ MODULE
*                      RETURN CODE =16 -> MESSAGE CANNOT LOAD MODULE
*                      RETURN CODE =8 -> MESSAGE MODULE NOT THERE
         C     R15,=F'16'  WAS IT A LOAD FAILURE
         BE    CANTLOAD    PUT OUT THE MSG
         C     R15,=F'8'   WAS IT A MODULE NOT FOUND
         BE    NOTHOME     PUT OUT THE MSG
         LR    R2,R15      WHO KNOWS WHAT HIT US
         ABEND 12,DUMP     SHOULD NEVER OCCUR
SPLAT    LA    R9,5        COUNT
PL0      PUT   SYSPRINT,SPLATMSG
         BCT   R9,PL0
         BR    R10         BACK TO CALLER
NOTHOME  BAL   R10,SPLAT   PUT OUT ATTENTION GETTER
         LA    R9,2        COUNT
PL2      PUT   SYSPRINT,NOTMSG
         BCT   R9,PL2
         BAL   R10,SPLAT
         B     CONTINUE
CANTLOAD BAL   R10,SPLAT   PUT OUT ATTENTION GETTER
         LA    R9,2        COUNT
PL1      PUT   SYSPRINT,CANTMSG
         BCT   R9,PL1
         BAL   R10,SPLAT
         B     CONTINUE
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
*                                                                     *
*        C O N S T A N T S                                            *
*                                                                     *
*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
REALNAME DC    CL8'TEMPCOB'   PLACE FOR NAME FROM CTL RECORD
MSW      DC    X'00'               GENERAL PURPOSE MASTER SWITCH
TEST     EQU   X'80'                    WAS TEST DUMP REQUESTED
INPUT    EQU   X'40'               WAS  INPUT PRESENT
PARM1    DC    0F'0'               PARAMETER LIST TO S/R
         DC    A(DIRDCB)
         DC    A(MEMDCB)
PLIST1   DC    X'80',AL3(0)        PLIST POINTER
PARM2    DC    0F'0'               PARAMETER LIST TO S/R
         DC    A(DIRDCB2)
         DC    A(MEMDCB2)
PLIST2   DC    X'80',AL3(0)        PLIST POINTER
PARM3    DC    0F'0'               PARAMETER LIST TO S/R
PLIST31  DC    X'00',AL3(0)        PLIST1 POINTER
PLIST32  DC    X'00',AL3(0)        PLIST2 POINTER
MTABL3   DC    X'80',AL3(0)        MESSAGETABLE POINTER
PARM4    DC    0F'0'               PARAMETER LIST TO S/R
PLIST41  DC    X'00',AL3(0)        PLIST1 POINTER
PLIST42  DC    X'00',AL3(0)        PLIST2 POINTER
MTABL4   DC    X'00',AL3(0)        MESSAGETABLE POINTER
         DC    X'80',AL3(SYSPRINT) SYSPRINT DCB POINTER
         EJECT
*        D C B S  -
         PRINT NOGEN
*        SYSIN  DCB - USED TO READ NAMES OF MEMBERS TO PROCESS -
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL), .......................
               EODAD=DONE,LRECL=80
         SPACE 1
*        MEMBER DCB - USED TO READ EACH MEMBER OF THE PDS  -
MEMDCB   DCB   DDNAME=PDS,DSORG=PO,MACRF=R,RECFM=U,EXLST=EXLST1
*              EODAD FILLED IN BY USER
         SPACE 1
*        DIRECTORY DCB - USED TO READ THE DIRECTORY OF THE PDS  -
DIRDCB   DCB   DDNAME=PDS,DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=256,       X
               LRECL=256,BUFNO=100
         SPACE 1
*        MEMBER DCB - USED TO READ EACH MEMBER OF THE PDS  -
MEMDCB2  DCB   DDNAME=PDS2,DSORG=PO,MACRF=R,RECFM=U,EXLST=EXLST2
*              EODAD FILLED IN BY USER
         SPACE 1
*        DIRECTORY DCB - USED TO READ THE DIRECTORY OF THE PDS  -
DIRDCB2  DCB   DDNAME=PDS2,DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=256,      X
               LRECL=256,BUFNO=100
         SPACE 1
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA,  X
               BLKSIZE=1995
EXLST1   DC    0F'0'         MEMDCB EXIT LIST
EXLST1A  DC    F'0'          FILLED IN WITH X'87',AL3(JFCB) WORK #1
EXLST2   DC    0F'0'         MEMDCB2 EXIT LIST
EXLST2A  DC    F'0'          FILLED IN WITH X'87',AL3(JFCB) WORK#2
         EJECT
         PRINT GEN
         LTORG
SPLATMSG DC    CL40'0  * * *    W A R N I N G   * * * '
         DC    CL33'  COMPARE OPERATION FAILED FOR ->'
MNAME    DC    CL8'XXXXXXXX'
         DC    CL52' '
NOTMSG   DC    CL133'-    MODULE NOT FOUND IN PDS2 (OLD MODULE)'
CANTMSG  DC    CL133'-    MODULE IN PDS2 (OLD MODULE) WOULD NOT LOAD'
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE MESSAGE BUILD AREA  1.00                       *
*                                                                     *
***********************************************************************
MTABL    DSECT
MSGCA    DS    A         ADDR OF CURRENT SLOT
MSGCCT   DS    F         NUMBER OF RECORDS IN USE
MSGMAX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
MSGMAXX  EQU   255       COUNT IN THE NEXT DS
MSGA     DS    255CL133  SPACE FOR RECORDS
MTABLL   EQU   *-MTABL
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE PARAMETER AREA 1.02                            *
*                                                                     *
***********************************************************************
PLIST    DSECT
CNAME    DS    CL8       MEMBER NAME TO PROCESS
JFCB     DC    0D'0',176X'00'      AREA TO READ THE JFCB INTO
PLISTCA  DS    A         ADDR OF CURRENT SLOT
PRECCT   DS    F         NUMBER OF RECORDS IN USE
PRECMX   DS    F         MAX NUMBER OF RECORDS AVAILABLE
PLISTC   EQU   255       COUNT IN THE NEXT DS
PREC     DS    255CL133  SPACE FOR RECORDS
PLISTL   EQU   *-PLIST
         EJECT
***********************************************************************
*                                                                     *
*        FORMAT OF THE DETAIL PRINT LINE                              *
*                                                                     *
***********************************************************************
DETLINE  DSECT
         DS    CL1 -
DETMEM   DS    CL8 -               MEMBER NAME
         DS    CL1 -
DETTTRC  DS    XL8 -               TTRC
         DS    CL1 -
DETATTR  DS    XL4 -               MODULE ATTRIBUTES
         DS    CL1 -
DETSIZE  DS    CL6 -               MODULE SIZE
         DS    CL1 -
DETEP    DS    CL6 -               MODULE ENTRY POINT
         DS    CL1 -
DETDTL   DS    CL6 -               DATE LINKED
         DS    CL1 -
DETMSG   EQU   *
FLAG1    DS    CL8 -               SYMDUMP
FLAG2    DS    CL8 -               FLOW
FLAG3    DS    CL8 -               STATE
FLAG4    DS    CL8 -               TESTM
FLAG5    DS    CL8 -               RES
FLAG6    DS    CL8 -               ENDJOB
FLAG7    DS    CL8 -               OBJ370
FLAG8    DS    CL8 -               CAPEX
FLAGA    DS    CL2 -               VS OR V4
         DS    C
FLAGB    DS    CL5 -               COUNT FOR VS
         DS    C
FLAGC    DS    CL5 -               TRACE FOR VS
         DS    C
FLAGD    DS    CL5 -               DTECT FOR CAPEX
LNLGTH   EQU   *-DETLINE
         END   COMPARE
