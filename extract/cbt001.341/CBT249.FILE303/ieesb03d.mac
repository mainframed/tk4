         TITLE 'LOC - EXTENDED ACTIVE DISPLAY  -  DOCUMENTATION '
***********************************************************************
*                                                                     *
*               EXTENDED  ACTIVE  DISPLAY  ATTACH  MODULE             *
*                                                                     *
*                             DOCUMENTATION                           *
*                                                                     *
*                                                    J.MIHOVETZ       *
***********************************************************************
*                                                                     *
*         THIS SUBSYSTEM MODULE WILL ATTACH AS A SUB-TASK MODULE      *
*    'IEESB04D' TO DO THE ACTUAL PROCESSING. THIS IS DONE BECAUSE     *
*    OF THE AMOUNT OF TIME REQUIRED TO CHAIN-CHASE THE NEEDED         *
*    CONTROL BLOCKS OF THE OTHER TASK'S IN THE SYSTEM VIA THE         *
*    CROSS MEMORY MOVE 'SVC' (SVC 248).                    AXC        *
*                                                                     *
*         BEFORE ATTACHING THE PROCESSING MODULE NEEDED STORAGE       *
*    IS OBTAINED FOR THE PARAMETER PASS AND WORK AREA'S.              *
*                                                                     *
***********************************************************************
         TITLE 'LOC - EXTENDED ACTIVE DISPLAY  -  ATTACH PROCESSING '
***********************************************************************
*                                                                     *
*                  A T T A C H   P R O C E S S I N G                  *
*                                                                     *
***********************************************************************
         SPACE 2
*                                                                     *
*              ESTABLISH  BASE                                        *
*                                                                     *
IEESB03D CSECT
         LR    R12,R15             LOAD BASE
         LR    R11,R14             SAVE RETURN ADDRESS
         USING IEESB03D,R12        ISSUE USING
         L     R0,SIZE             LOAD STORAGE SIZE
         GETMAIN R,LV=(0)          GET STORAGE
         USING XSA,R1              ESTABLISH BASE
         MVC   XCORE,SIZE          PASS SUB-POOL # AND ADDRESS
         MVC   XAP(XSIZE),0(R2)   PASS XSA AREA
         ATTACH EP=IEESB04D        ATTACH PROCESSOR
         SR    R15,R15             SET RETURN CODE
         LR    R14,R11             SET RETURN ADDRESS
         BR    R14                 RETURN
         TITLE 'LOC - EXTENDED ACTIVE DISPLAY  -  REGISTERS '
***********************************************************************
*                                                                     *
*                        R E G I S T E R S                            *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                   GETMAIN STORAGE SIZE
R1       EQU   1                   PARAMETER PASS REGISTER
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11                  RETURN ADDRESS SAVE AREA
R12      EQU   12
R13      EQU   13
R14      EQU   14                  RETURN ADDRESS
R15      EQU   15                  RETURN CODE
         TITLE 'LOC - EXTENDED ACTIVE DISPLAY  -  CONSTANTS'
***********************************************************************
*                                                                     *
*                       C O N S T A N T S                             *
*                                                                     *
***********************************************************************
         SPACE 2
SIZE     DC    A(WAEND-XSA)
         TITLE 'LOC - EXTENDED ACTIVE DISPLAY  -  WRK AREA AND CTL BLK'
***********************************************************************
*                                                                     *
*     W O R K   A R E A   &   C O N T R O L   B L K   D S E C T       *
*                                                                     *
***********************************************************************
         SPACE 2
*                                                                     *
*              PARAMETER  PASS  AREA                                  *
*                                                                     *
XSA      DSECT
XAP      DS    F
XAD      DS    F
XAX      DS    D
XAE      DS    0C
XAR      DS    F                   POINTER TO PARAMETER LENGTH
XAN      DS    0C
XAL      DS    F                   SUB-PARAMETER ADDRESS
XAV      DS    D
XAS      DS    D
XAU      DS    C                   CONSOLE UCM ID
         DS    C
XAJ      DS    H
         DS    F
XEND     EQU   *
*
XCORE    DS    F
*
XSIZE    EQU   XEND-XAP
RETADDR  DS    F
*                                                                     *
*              WORK  AREA                                             *
*                                                                     *
WA       EQU   *
WASTRT   DS    0D
DBL      DS    D
ZERO     DS    F
YYAU     DS    F
ID       DS    CL8
*
TYPE     DS    C                   TYPE BREAKDOWN:
*                                      .... ...1 = DISPLAY ALL
*                                      .... ..1. = DISPLAY BY JOB #
*                                      .... .1.. = DISPLAY BY JOB NAME
*                                      .... 1 .. = FOUND BY JOB #
*                                      ...1 .... = FOUND BY JOB NAME
*                                      ..1. .... = ERROR DETECTED
*                                      .1.. .... = MASTER/JES2/ROSWTR
*                                      1... .... = JOB LINE ONLY
WORK     DS    D
SAVJLIM  DS    F
SAVSCPU  DS    F
SAVSSRB  DS    F
SAVJSRB  DS    F
*                                                                     *
*              JOB  DISPLAY  AREA                                     *
*                                                                     *
         DS    0F
JOBMSG   DS    0CL78
JOBLIST  DS    CL4
JOBNBR   DS    CL4
         DS    C
*
JOBERR   DS    0CL8
*
JOBNAME  DS    CL8
         DS    C
JOBSTEP  DS    CL2
         DS    C
JOBSTRT  DS    CL8
         DS    C
JOBELAP  DS    CL8
         DS    C
JOBLIMIT DS    CL7
         DS    C
JOBCPU   DS    CL7
         DS    C
JOBREM   DS    CL7
         DS    C
DPRTY    DS    CL3
         DS    C
ASID     DS    CL2
         DS    C
INITATOR DS    CL2
         DS    C
CLASS    DS    C
JOBTRLR  DS    CL4
*                                                                     *
*              STEP  DISPLAY  AREA                                    *
*                                                                     *
         DS    0F
STPMSG   DS    0CL78
STPLIST  DS    CL4
         DS    CL5
STEPNAME DS    CL8
         DS    C
STEP#    DS    CL2
         DS    C
STPSTRT  DS    CL8
         DS    C
STPELAP  DS    CL8
         DS    C
STPLIMIT DS    CL7
         DS    C
STEPCPU  DS    CL7
         DS    C
STEPREM  DS    CL7
STPFILLR DS    CL12
STPTRLR  DS    CL4
*                                                                     *
*              XMM  PARAMETER   AREA                                  *
*                                                                     *
WAPARM   DS    0F
WAASID   DS    F
WAFROM   DS    A
WATO     DS    A
WASIZE   DS    A
*                                                                     *
*              CONTROL  BLK  AREA                                     *
*                                                                     *
WACB     DS    CL380
*
WAEND    DS    0H
*
*
*
         END
