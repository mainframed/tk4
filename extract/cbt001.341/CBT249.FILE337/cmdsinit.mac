*          DATA SET XC00033    AT LEVEL 001 AS OF 01/28/80
CMDSINIT START 0                   START THIS MESS
         TITLE '*****  C O M M A N D   S U B S Y S T E M   I N I T I A X
               L I Z A T I O N   P R O G R A M  *****'
         SPACE 2
         SAVE  (14,12),,*          SAVE THE REGS
         LR    R10,R15             SET UP A BASE REG
         USING CMDSINIT,R10        THEN USE IT
         L     R0,SP2              LOAD S.P.# & LENGTH FOR REGSAVE AREA
         GETMAIN  R,LV=(R0)        GO GET THE MAIN
         LTR   R15,R15             TEST FOR GOOD GET
         BNE   FORGETIT            BR IF NO MAIN GOTTEN
         ST    R13,4(,R1)          SAVE THE SAVE AREA
         ST    R1,8(,R13)          SAVE OUR SAVE AREA
         LR    R13,R1              POINT TO OUR SAVE AREA
         L     R0,SP1              S.P. AND LENGTH FOR SSVT AREA
         GETMAIN  R,LV=(R0)        GET THE MAIN FOR THE SSVT
         LTR   R15,R15             TEST FOR GOOD GET
         BNE   NOSP241             BR IF NOT GOOD
         LR    R11,R1              R11 POINTS TO THE SSVT
         MVI   0(R11),0            ZERO OUT
         MVC   1(255,R11),0(R11)   THE SSVT
         MVC   256(255,R11),0(R11) GOTTEN AREA
         L     R2,16               ... CVT
         L     R2,X'128'(,R2)      ... JESCT
         L     R2,X'18'(,R2)       ...1ST SSCVT
SSCVTLP  CLC   8(4,R2),=C'CMDS'    TEST FOR CMDS SSCVT
         BE    FOUND               BR IF FOUND
         L     R2,4(,R2)           ELSE POINT TO THE NEXT SSCVT
         LTR   R2,R2               TEST FOR END OF THE LINE
         BE    NOSUBSYS            BR IF NO SUB-SYSTEM FOUND
         B     SSCVTLP             ELSE LOOP TO NEXT
FOUND    LR    R12,R2              R12 POINTS TO THE SSCVT
         LA    R5,X'110'(,R11)     R5 POINTS TO THE ECB IN THE SSVT
         LOAD  EP=CMDSROUT         LOAD THE COMMAND ROUTER MODULE
         LR    R9,R0               R9 POINTS TO THE ROUTER
         MVC   0(4,R11),=F'1'      INDICATE ONE FUNCTION IN THE SSVT
         MVI   13(R11),X'01'       INDICATE COMMAND FUNCTION
         ST    R9,X'104'(,R11)     STORE ROUTER ADDRESS IN SSVT
         L     R2,X'224'           GET THE CURRENT ASCB (ME)
         ST    R2,X'118'(R11)      SAVE ASCB ADDR IN SSVT
         ST    R5,X'114'(,R11)     SAVE ECB ADDRESS IN SSVT
         LA    R5,X'120'(,R11)     POINT R5 TO THE LIST IN THE SSVT
         ST    R5,20(,R12)         SAVE LIST ADDRESS IN THE SSCVT
         SPACE 1
******************************************************************
*        ATTACH THE CMDSMAIN CSECT TO PROCESS MESSAGES           *
******************************************************************
         SPACE 1
         ATTACH  EP=CMDSMAIN,JSTCB=YES,SVAREA=YES,SM=SUPV,KEY=ZERO
         SPACE 1
*******************************************************************
* THE NEXT INSTRUCTION CAUSES THE SUB-SYSTEM TO BECOME OPERATIONAL*
*******************************************************************
         SPACE 1
         ST    R11,16(,R12)        OPEN CMDSBSYS FOR BUSINESS
         SPACE 1
         WTO   'CMDSBSYS --- COMMAND SUB-SYSTEM INITIALIZATION COMPLETEX
               .'
         SPACE 5
GETOUT   LR    R2,R13              SAVE POINTER TO OUR SAVE AREA
         L     R13,4(,R13)         RESTORE CALLERS R13 INTO R13
         L     R0,SP2              GET THE S.P. NO. & LENGTH
         FREEMAIN  R,LV=(R0),A=(R2)  FREE THE REG SAVE AREA
         LM    R14,R12,12(R13)     RESTORE THE REGS
         SR    R15,R15             SET THE COMPLETION CODE
         BR    R14                 AND GET OUT
         EJECT
FORGETIT WTO   'CMDSBSYS --- REGSAVE GETMAIN FAILED. - NO CMDSBSYS'
         LM    R14,R12,12(R13)     RESTORE THE REGS
         LA    R15,4               SET CONDITION CODE
         BR    R14                 AND RETURN
         SPACE 2
NOSP241  WTO   'CMDSBSYS --- SSVT GETMAIN FAILED. - NO CMDSBSYS'
         B     GETOUT              RETURN TO CALLER
         SPACE 2
NOATTACH WTO   'CMDSBSYS --- ATTACH OF CMDSMAIN FAILED. - NO CMDSBSYS'
RLSE241  L     R0,SP1              POINT TO THE GETMAINED SSVT
         FREEMAIN  R,LV=(R0),A=(R12)  FREE THE SSVT
         B     GETOUT              RETURN TO CALLER
         SPACE 2
NOSUBSYS WTO   'CMDSBSYS --- CMDS SUB-SYSTEM NOT FOUND; TERMINATED'
         B     RLSE241             GO FREEMAIN AMD GET OUT
         EJECT
SP1      DS    0F
         DC    AL1(241)       GETMAIN IN SP 241
         DC    AL3(512)       FOR 512 BYTES
SP2      DC    AL1(241)       GETMAIN IN SP 241
         DC    AL3(144)       FOR 144 BYTES
         LTORG
         REGS
         END
