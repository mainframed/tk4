*          DATA SET XC00047    AT LEVEL 010 AS OF 09/04/81
IEESK03D START 0                   START THE RCT COMMAND
         TITLE '*****  C O M M A N D   S U B - S Y S T E M  -  : K   P X
               R O C E S S O R  *****'
         SAVE  (14,12),,*          SAVE S0ME REGS
         LR    R10,R15             SET UP A BASE REG
         USING IEESK03D,R10        THEN USE IT
         L     R0,GETPOOL          GET THE SP & LGTH
         GETMAIN R,LV=(R0)         AND SOME MAIN
         ST    R13,4(,R1)          SAVE HIS R13
         ST    R1,8(,R13)          AND, EFFECTIVELY, OUR R13
         LR    R13,R1              SET UP THE SAVE AREA
         USING WORKAREA,R13        THEN USE IT
         MVC   UCMID(2),2(R8)      SAVE THE CONSOLE IDENT
         MVC   MSGHDR(4),=X'00400100'  INSERT COUNT & MCS INSTS.
         MODESET KEY=ZERO,MODE=SUP INSURE SUP & KEY ZERO
         STNSM ENABLE+1,X'FC'      DISABLE THIS MOOSE
         L     R1,X'10'            ... CVT
         L     R1,X'25C'(,R1)      ...CVTOCTP (PTR TO SRM'S TABLES)
         L     R1,X'E4'(,R1)       LOAD THE PTR TO THE 'RCT'
         MVC   UICA(4),X'3E'(R1)   MOVE IN UIC + CPU AVG.'S
         MVC   ASMQA(10),X'46'(R1)  ASMQ + PAGE RATE + DEMAND PAGING
*                                  TOTAL PAGE RATE + PAGE DELAY TIME
         L     R4,16               CVT
         L     R4,604(,R4)         RMCT
         L     R5,88(,R4)          WAMT
         LTR   R5,R5               TEST FOR WAMT EXISTANCE
         BNZ   SEEMSOK1            BR IF NOT ZERO
         LA    R3,1                ELSE PREVERT A 0C9
         B     SAVER31             AND GO ON
SEEMSOK1 LA    R3,2                PERF 2
         SLL   R3,2                TIMES 4
         L     R6,80(R3,R5)        WAMP OFFSET
         AR    R5,R6               WAMP ADDR
         L     R3,0(,R5)           TOTAL ENDED TRANS
SAVER31  ST    R3,ENDED2           SAVE IT
         L     R3,12(,R5)          TOTAL ELAPSED TIME FOR ENDED TRANS
         ST    R3,TIME2            SAVE IT
         L     R5,88(,R4)          WAMT
         LTR   R5,R5               TEST FOR WAMT EXISTANCE
         BNZ   SEEMSOK2            BR IF NOT ZERO
         LA    R3,1                ELSE PREVERT A 0C9
         B     SAVER32             AND GO ON
SEEMSOK2 LA    R3,9                PERF 9
         SLL   R3,2                TIMES 4
         L     R6,80(R3,R5)        WAMP OFFSET
         AR    R5,R6               WAMP ADDR
         L     R3,0(,R5)           TOTAL ENDED TRANS
SAVER32  ST    R3,ENDED9           SAVE IT
         L     R3,12(,R5)          TOTAL ELAPSED TIME FOR ENDED TRANS
         ST    R3,TIME9            SAVE IT
ENABLE   STOSM SAVEIT,*-*          RE-ENABLE
         LA    R1,MSG1             POINT TO A MESSAGE
         BAL R9,WRITEIT+4                  WRITE THE LINE
         MVC   MESSAGE(60),CPU     MOVE IN THE CPU MESSAGE
         SR    R2,R2               ZAP A REG
         LH    R3,CPUA             GET THE CPU TIME
         D     R2,=F'16'           DIVIDE BY 16
         CVD   3,DBLWD             CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+33(3),DBLWD  ... UNPK
         MH    R2,=H'100'          REMAINDER TIMES 100
         LR    R3,R2               POSITION THE REM
         SR    R2,R2               ZAP A REG
         D     R2,=F'16'           DIVIDE TO GET HUNDREDS
         CVD   R3,DBLWD            CONVERT
         OI    DBLWD+7,15          INSURE SIGN
         UNPK  MESSAGE+37(2),DBLWD   MOVE IT IN
         CLI   MESSAGE+33,C'0'     TEST FOR A LEADING ZERO
         BNE   NOTZERO             BR IF NOT
         MVI   MESSAGE+33,C' '     ELSE BLANK IT
         CLI   MESSAGE+34,C'0'     TEST FOR ANOTHER ZERO
         BNE   NOTZERO             BR IF NOT
         MVI   MESSAGE+34,C' '     ELSE BLANK IT
NOTZERO  BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),ASMQ    MOVE IN THE ASMQ MESSAGE
         LH    R2,ASMQA            GET THE ASMQ LENGTH
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(2),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),PAGES   MOVE IN THE PAGE MESSAGE
         LH    R2,RCVPTR           GET THE PAGE COUNT
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(2),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),UIC     MOVE IN THE UIC MESSAGE
         LH    R2,UICA             GET THE UIC COUNT
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(3),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),DEMAND  MOVE IN THE DEMAND PAGE MESSAGE
         LH    R2,DEMPAGE          GET THE PAGE COUNT
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(2),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),TOTPGEM MOVE IN THE TOTAL PAGE MESSAGE
         LH    R2,TOTPAGE          GET THE TOTAL PAGING COUNT
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(3),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         MVC   MESSAGE(60),PGEDLYM   MOVE IN THE PAGE DELAY MESSAGE
         LH    R2,PAGEDLY          GET THE TOTAL DELAY TIME (MS)
         CVD   R2,DBLWD            CONVERT IT
         OI    DBLWD+7,15          INSURE CORRECT SIGN
         UNPK  MESSAGE+34(3),DBLWD  UNPK
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         SR    R2,R2               ZAP IT
         L     R3,ENDED2           # OF TRANS .. PERF2
         LTR   R3,R3               TEST FOR ZERO
         BZ    NONE2               YES, BYPASS 0C9
         L     R3,TIME2            ELAPSED TIME .. PERF2
         D     R2,ENDED2           DIVIDE BY ENDED TRANS
         SR    R2,R2
         D     R2,=F'1000'         DIVIDE TO GET SECONDS
NONE2    CVD   R3,DBL              AVG SEC RESPONSE TIME
         SRDL  R2,32               MOVE INTO R3
         D     R2,=F'10'           DIVIDE TO GET .10
         CVD   R3,DBLR             TENTHS
         MVC   MESSAGE(60),RESPON2 MOVE IN MESSAGE
         MVC   MESSAGE+31(7),=X'402021204B2020'
         ED    MESSAGE+31(4),DBL+6
         UNPK  MESSAGE+36(2),DBLR+6(2)
         OI    MESSAGE+37,X'F0'
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         SR    R2,R2               ZAP IT
         L     R3,ENDED9           # OF TRANS .. PERF 9
         LTR   R3,R3               TEST FOR ZERO
         BZ    NONE9               YES, BYPASS 0C9
         L     R3,TIME9            ELAPSED TIME .. PERF 9
         D     R2,ENDED9           DIVIDE BY ENDED TRANS
         SR    R2,R2
         D     R2,=F'1000'         DIVIDE TO GET SECONDS
NONE9    CVD   R3,DBL              AVG SEC RESPONSE TIME
         SRDL  R2,32               MOVE INTO R3
         D     R2,=F'10'           DIVIDE TO GET .10
         CVD   R3,DBLR             TENTHS
         MVC   MESSAGE(60),RESPON9 MOVE IN MESSAGE
         MVC   MESSAGE+31(7),=X'402021204B2020'
         ED    MESSAGE+31(4),DBL+6
         UNPK  MESSAGE+36(2),DBLR+6(2)
         OI    MESSAGE+37,X'F0'
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
         SR    R4,R4               ZAP IT
         L     R5,ENDED2           # OF TRANS .. PERF 2
         L     R6,ENDED9           # OF TRANS .. PERF 9
         AR    R5,R6               ADD TO GET TOTAL TRANS
         LTR   R5,R5               TEST FOR ZERO
         BZ    NONET               YES, BYPASS 0C9
         ST    R5,TEND             SAVE IT
         L     R5,TIME2            ELAPSED TIME .. PERF 2
         L     R6,TIME9            ELAPSED TIME .. PERF 9
         AR    R5,R6               ADD TO GET TOTAL ELAPSED TIME
         D     R4,TEND             DIVIDE BY TOTAL TRANS
         SR    R4,R4               ZAP IT
         D     R4,=F'1000'         DIVIDE TO GET SECONDS
NONET    CVD   R5,DBL
         SRDL  R4,32               MOVE TO REG 5
         D     R4,=F'10'           TENTHS
         CVD   R5,DBLR
         MVC   MESSAGE(60),RESPONT MOVE IN MESSAGE
         MVC   MESSAGE+31(7),=X'402021204B2020'
         ED    MESSAGE+31(4),DBL+6
         UNPK  MESSAGE+36(2),DBLR+6(2)
         OI    MESSAGE+37,X'F0'
         BAL   R9,WRITEIT          GO WRITE THE LINE
********************************************************************
RETURN   DS    0H
         CLI   0(R13),C'T'
         BNE   JUSTQUIT
         MODESET MODE=PROB,KEY=NZERO  RESTORE SANITY
JUSTQUIT DS    0H
         LR    R1,R13              SAVE R13 FOR FREEMAIN
         L     R13,4(,R13)         RESTORE THE CALLER'S REG
         L     R0,GETPOOL          GET THE SP & LGTH
         FREEMAIN R,LV=(R0),A=(R1)  FREE THE MAIN
         LM    R14,R12,12(R13)     RESTORE THE REGS
         SR    R15,R15             ZAP THE CC REG
         BR    R14                 RETURN TO CALLER
         EJECT
***************************************************************
WRITEIT  LA    R1,MSGHDR           POINT TO THE MESSAGE
         L     R15,X'0224'         ASCB
         L     R15,X'3C'(,R15)     ASCBTSB  ( TSO???)
         LTR   R15,R15             TSO????
         BZ    NOTTSO
         MVI   0(R13),C'T'         *** SET TSO FLAG ***
         LH    R0,0(R1)            LENGTH
         SH    R0,=H'4'            SKIP HEADER
         LA    R1,4(,R1)           SKIP OVER HEADER
         TPUT  (R1),(R0)           WRITE IT
         BR    R9                  AND RETURN
NOTTSO   DS    0H
         LH    R0,UCMID            GET THE CONSOLE IDENT
         SVC   35                  WRITE THE MESSAGE
         BR    R9                  AND RETURN
         SPACE 2
DATAAREA DS    0D
CPU      DC    CL60'CMDSBSYS --- C P U  UTILIZATION : XX.XX%'
MSG1     DC    X'003C0100'
         DC    CL60'CMDSBSYS --- SRM VARIABLES :'
ASMQ     DC    CL60'CMDSBSYS --- AVG ASM QUEUE :'
PAGES    DC    CL60'CMDSBSYS --- PAGE RATE :'
UIC      DC    CL60'CMDSBSYS --- AVG UIC :'
DEMAND   DC    CL60'CMDSBSYS --- DEMAND PAGE RATE :'
TOTPGEM  DC    CL60'CMDSBSYS --- TOTAL PAGE RATE :'
PGEDLYM  DC    CL60'CMDSBSYS --- PAGE DLY TIME(MS):'
RESPON2  DC    CL60'CMDSBSYS --- AVG RESP PERF(2) :'
RESPON9  DC    CL60'CMDSBSYS --- AVG RESP PERF(9) :'
RESPONT  DC    CL60'CMDSBSYS --- AVG RESP TOTAL :'
GETPOOL  DS    0D
         DC    AL1(241)            SUB POOL
         DC    AL3(244)            LENGTH
         SPACE 2
         LTORG
         EJECT
WORKAREA DSECT
REGSAVE  DS    18F
DBLWD    DS    D
TEND     DS    F
ENDED2   DS    F
ENDED9   DS    F
TIME2    DS    F
TIME9    DS    F
DBL      DS    D
DBLR     DS    D
UICA     DS    H                   UNUSED INTERVAL COUNT
CPUA     DS    H                   CPU TIME(TIMES 16)
AVQC     DS    H                   AVG LOW QUEUE
ASMQA    DS    H                   ASM Q LENGTH
RCVPTR   DS    H                   PAGES PER SECOND
DEMPAGE  DS    H                   DEMAND PAGE RATE
TOTPAGE  DS    H                   TOTAL PAGE RATE
PAGEDLY  DS    H                   PAGE DELAY TIME (MS)
UCMID    DS    H                   CONSOLE ID
SAVEIT   DS    X                   MASK SAVE AREA
MSGHDR   DS    XL4
MESSAGE  DS    CL70
IEESK03D CSECT
         EJECT
         REGS
         END
