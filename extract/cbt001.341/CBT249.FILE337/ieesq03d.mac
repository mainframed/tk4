*          DATA SET XC00038    AT LEVEL 008 AS OF 09/02/81
IEESQ03D START 0                   START THE CONFLICT/RESERVE ROUTINE
         TITLE '*****  C O M M A N D   S U B - S Y S T E M  -  : Q   P X
               R O C E S S O R  *****'
         SAVE  (14,12),,*          SAVE THE REGS
         LR    R10,R15             ESTABLISH A BASE
         USING IEESQ03D,R10        THEN USE IT
         L     R0,GETWORK          LOAD THE SP & LGTH OF THE SAVE AREA
         GETMAIN  R,LV=(R0)        GET SOME MAIN
         LTR   R15,R15             TEST FOR GOOD GET
         BNE   NOMAIN              BR IF NO MAIN TO GET
         ST    R13,4(,R1)          SAVE HIS SAVE AREA POINTER
         ST    R1,8(,R13)          SAVE OUR SAVE AREA POINTER
         LR    R13,R1              POINT TO OUR SAVE AREA
         USING WORKAREA,R13        USE R13 AS A BASE
         LA    R11,WORKEND         POINT TO THE END OF THE WORK AREA
         LA    R2,DBLWD            POINT TO THE SAVE AREA
         L     R5,=A(WORKEND-DBLWD-4)   GET A LENGTH-4 FOR SAFETY
         LR    R4,R2               POINT TO AREA
         SR    R3,R3               SET THE 2ND LENGTH TO ZERO
         MVCL  R4,R2               CLEAR THE WORK AREA
         ST    R11,CSAEND          SAVE THE WORK AREA END PTR
         L     R2,0(,R8)           GET THE UCMID
         ST    R2,UCMID            AND SAVE THE ID
         L     R3,X'0224'          ASCB
         ICM   R3,15,ASCBTSB(R3)   ASCBTSB
         BZ    ITSCOLON
         L     R1,4(,R13)          RELOAD PARM REG
         L     R1,24(,R1)          RELOAD PARM REG
         L     R8,0(,R1)           R8 --> HWLEN,HWOFF,CMD
         LA    R8,7(,R8)           R8 --> REMAINDER OF COMMAND
         B     ITSCOLON+4          SKIP THE REGULAR LOAD
ITSCOLON DS    0H
         LA    R8,11(,R8)          POINT PAST THE ':Q'
         OI    0(R8),C' '          TRANSLATE TO UPPERCASE
         CLI   0(R8),C'R'          TEST FOR A RESERVE REQUEST
         BNE   CONFLICT            IF NOT, ASSUME CONFLICTS
RESERVE  BAL   R9,DISABLE          GO DISABLE THIS MOOSE
         LA    R5,RESVTBL          POINT TO THE RESERVE TABLE
RESVLP   BAL   R9,GETQCB           GO GET THE QCB POINTERS
         LTR   R2,R2               TEST FOR END OF QCB CHAIN
         BE    ENDRESV             IF YES, GO END THIS MESS
         TM    12(R4),X'10'        TEST FOR A RESERVE
         BZ    RESVLP              LOOP IF NOT
         OI    RESVIND,255         INDICATE A RESERVE IS PRESENT
         BAL   R9,JOBNAME          GO GET THW JOB NAME
         MVC   0(8,R5),JOBN        SAVE THE JOBNAME IN THE TABLE
         L     R6,24(,R4)          GET THE UCB ADDRESS
         MVC   8(3,R5),13(R6)      MOVE IN THE UNIT NUMBER
         MVC   11(6,R5),28(R6)     MOVE IN THE VOLID
         MVC   17(8,R5),16(R2)     MOVE IN THE MAJOR QUEUE NAME
         MVC   25(8,R5),FFS        TERMINATE THE ENTRY
         LA    R5,25(,R5)          POINT TO THE NEXT ENTRY
         B     RESVLP              AND LOOP 'TIL DONE
         EJECT
CONFLICT BAL   R9,DISABLE          GO DISABLE THIS TURKEY
CONFLP   TM    CONFLIND,255        SEE IF WE'VE ALREADY FOUND A CONFL
         BO    CONFL2              IF SO, LEAVE R5 ALONE
         LA    R5,CONFLTBL         ELSE POINT TO THE CONFLICT TABLE
         B     CONFL3              AND CONTINUE
CONFL2   TM    CONF2IND,255        TEST FOR SECOND CONFLICT
         BO    CONFL3              BR IF ANOTHER CONFLICT
         LR    R5,R1               ELSE RESET R5
         MVC   0(12,R5),FFS        RE-TERMINATE THE TABLE
CONFL3   BAL   R9,GETQCB           GO GET THE QCB POINTERS
         LTR   R2,R2               TEST FOR THE END OF THE QCB'S
         BE    ENDCONFL            BR IF THE END
         LRA   R11,0(,R4)          SEE IF ADDRESS IS REAL
         BNE   BADCHAIN            BR IF NOT
         TM    12(R4),X'80'        TEST FOR EXCLUSIVE ENQUEUE
         BO    CONFLP              BR IF NOT EXCLUSIVE
         LR    R1,R5               SAVE THE CURRENT OUTAREA PTR
         NI    CONF2IND,0          ZAP THE SECONDARY CONFL IND.
         L     R4,8(,R3)           POINT BACK TO THE TOP OF THE QUEUE
         BAL   R9,JOBNAME          GO GET THE ENQ JOBNAME
         MVC   0(8,R5),JOBN        MOVE JOBNAME TO TABLE
         MVC   8(8,R5),16(R2)      MOVE IN THE MAJOR NAME
         SR    R6,R6               ZAP A REG
         IC    R6,16(,R3)          GET THE MINOR NAME LENGTH
         BCTR  R6,0                DECREMENT FOR THE MVC
         LA    R5,16(,R5)          POINT R5 T0 THE MINOR ENTRY
         C     R5,CSAEND           TEST FOR END OF WORK AREA
         BNL   TOOMUCH             BR IF TOO MUCH DATA
         STC   R6,0(,R5)           SAVE THE MINOR NAME LENGTH
         TM    12(R4),X'80'        TEST FOR TYPE OF ENQ
         OI    0(R5),X'80'         ELSE INDICATE SHR
EXCL     DS    0H
         EX    R6,MVEMINOR         MOVE MINOR NAME TO THE TABLE
         LA    R5,2(R6,R5)         POINT TO THE NEXT TABLE ENTRY
         C     R5,CSAEND           TEST FOR END OF WORK AREA
         BNL   TOOMUCH             BR IF TOO MUCH DATA
         MVC   0(12,R5),FFS        TERMINATE THE ENTRY
CONFLP2  LTR   R4,R4               TEST FOR END OF QEL
         BNE   CONTCONF            BR IF NOT END
         LA    R5,8(,R5)           IF END, POINT PAST FF'S
         C     R5,CSAEND           TEST FOR END OF WORK AREA
         BNL   TOOMUCH             BR IF TOO MUCH DATA
         B     CONFLP              AND LOOP TO THE TOP
CONTCONF BAL   R9,JOBNAME          GO GET A JOB NAME
         CLC   0(8,R1),JOBN        SEE IF THE IS THE 1ST NAME
         BE    ITISFND             BR IF SAME JOB
         OI    CONFLIND,255        INDICATE A CONFLICT EXISTS
         OI    CONF2IND,255        INDICATE A POSSIBLE 2ND CONFLICT
         MVC   0(8,R5),JOBN        MOVE JOBNAME TO TABLE
         TM    12(R4),X'80'        TEST FOR TYPE OF ENQ
         BNO   EXCL2               BR IF EXCLUSIVE
         NI    0(R5),X'BF'         INDICATE SHR
EXCL2    DS    0H
         MVC   8(12,R5),FFS        TERMINATE THE ENTRY
         LA    R5,8(,R5)           POINT TO THE NEXT ENTRY
         C     R5,CSAEND           TEST FOR END OF WORK AREA
         BNL   TOOMUCH             BR IF TOO MUCH DATA
ITISFND  L     R4,0(,R4)           POINT TO THE NEXT QEL
         B     CONFLP2             LOOP TO NEXT QEL
         SPACE 5
ENDRESV  BAL   R9,ENABLE           GO ENABLE EL-MOOSEO
         TM    RESVIND,255         TEST FOR RESERVES FOUND
         BZ    NORESVS             BR IF NONE FOUND
         LA    R5,RESVTBL          ELSE POINT TO THE TABLE
         MVC   MESSAGE(80),RESVMSG MOVE MESSAGE PATTERN TO WORG AREA
RESVLP2  MVC   RSVJOB(8),0(R5)     MOVE JOBNAME TO MESSAGE
         MVC   RSVUNIT(3),8(R5)    MOVE THE UNIT ADDRESS
         MVC   RSVVOL(6),11(R5)    MOVE THE VOLID
         MVC   RSVMAJOR(8),17(R5)  MOVE THE MAJOR NAME
         LA    R1,MESSAGE          POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         LA    R5,25(,R5)          POINT TO THE NEXT MESSAGE
         CLC   0(8,R5),FFS         TEST FOR END OF MESSAGES
         BNE   RESVLP2             LOOP IF NOT THE END
         B     RETURN              ELSE GO TERMINATE THIS EXECUTION
         SPACE 5
ENDCONFL BAL   R9,ENABLE           GO ENABLE THE CONFUSER
         TM    CONFLIND,255        SEE IF ANY CONFLICTS EXIST
         BZ    NOCONFL             BR IF NOT
         LA    R5,CONFLTBL         POINT TO THE CONFLICT TABLERN
CONFLOOP MVC   MESSAGE(80),CONFLMSG  MOVE IN THE MESSAGE PATTERN
         MVC   CONFLJOB(8),0(R5)   MOVE IN THE ENQ JOBNAMER MSGS
         MVC   SVCONFJ(8),0(R5)    SAVE THAT NAME FOR OTHER MSGS
         MVC   CONFLMAJ(8),8(R5)   MOVE IN THE MAJOR QUE NAME
         SR    R2,R2               ZAP A REG
         LA    R5,16(,R5)          POINT TO THE MINOR NAME
         TM    0(R5),X'80'         TEST FOR TYPE
         BO    SHR                 BR IF SHR
         MVC   SHREXC(5),=C'(EXC)' INDICATE EXCLUSIVE
         B     UNDOIT              TURN OFF INDICATOR
SHR      MVC   SHREXC(5),=C'(SHR)' INDICATE SHR
UNDOIT   NI    0(R5),X'7F'         TURN OFF THE INDICATOR
         IC    R2,0(,R5)           GET THE MINOR COUNT
         CH    R2,=H'7'            TEST FOR GR. MAX LGTH
         BNH   ITSOK               BR IF 8 OR LESS
         LA    R1,7                ELSE SET TO MAX
         B     MOVEITIN            AND BR AROUND
ITSOK    LR    R1,R2               SET UP THE EX REG
MOVEITIN MVC   CONFLMIN(8),=CL8' '  BLANK THE MINOR NAME FIELD
         EX    R1,MVCMINAM         MOVE IN THE MINOR NAME
         LA    R5,2(R2,R5)         POINT PAST THE MINOR NAME
         LA    R1,MESSAGE          POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         LA    R12,20              SET A MAX WRITE COUNT
ENDLOOP  CLC   0(12,R5),FFS        TEST FOR THE ABSOLUTE END
         BE    RETURN              BR IF END
         CLC   0(8,R5),FFS         TEST FOR ENTRY END
         BNE   DOJOBS              DO JOBS IF NOT ENTRY END
         LA    R5,8(,R5)           ELSE POINT TO THE NEXT ENTRY
         B     CONFLOOP            AND GO DO NEXT CONFLICT
DOJOBS   MVC   MESSAGE(80),JOBCONFL  MOVE IN THE MESSAGE PATTERN
         MVC   CONFJOB(8),0(R5)    MOVE IN THE CONFLICTING JOBNAME
         OI    CONFJOB,X'40'       MAKE 1ST CHAR A CAPITAL
         CLI   0(R5),C'A'          TEST THE TYPE
         BL    SHRX                BR IF SHR
         MVC   SHREXC(5),=C'(EXC)' INDICATE EXCLUSIVE
         B     NOTSHR              AND BR AROUND THE OTHER STUFF
SHRX     MVC   SHREXC(5),=C'(SHR)' ELSE INDICATE SHARED
         OI    0(R5),X'40'         RESET THE BIT
NOTSHR   DS    0H
         MVC   MAINJOB(8),SVCONFJ  MOVE IN THE MAIN CONFLICTOR
         LA    R1,MESSAGE          POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         LA    R5,8(,R5)           POINT TO THE NEXT NAME
         BCT   R12,ENDLOOP         LOOP FOR 20 TIMES
         LA    R1,QUITMSG          POINT TO THE 'TOO MANY' MSG
         BAL   R11,PRINTIT
         B     RETURN              AND GET OUT
         EJECT
DISABLE  MODESET  KEY=ZERO,MODE=SUP  INSURE SUPER/ZERO
         STNSM ENABLE+1,X'FC'      DISABLE THIS MOOSE
         BR    R9                  AND RETURN TO CALLER
         SPACE 2
ENABLE   STOSM SAVEIT,*-*          RE- ENABLE EL MOOSE-O
         L     R15,X'0224'         ASCB
         ICM   R15,15,ASCBTSB(R15) ASCBTSB
         BZR   R9
         MODESET KEY=NZERO,MODE=PROB   RESTORE SANITY ---
         BR    R9                  AND RETURN TO CALLER
         SPACE 2
GETQCB   TM    DOING,255           TEST FOR 1ST TIME HERE
         BO    NEXTQEL             BR IF NOT 1ST
         OI    DOING,255           INDICATE 1ST TIME DONE
         L     R2,X'10'            ... CVT
         L     R2,640(,R2)         ... 1ST MAJOR QCB
MAJOR    L     R3,8(,R2)           ...MINOR QCB
MINOR    L     R4,8(,R3)           ...QEL
QEL      BR    R9                  RET WITH MAJOR=R2,MINOR=R3,QEL=R4
NEXTQEL  LTR   R4,R4               SEE IF CONFLICTS SCREWED UP R4
         BE    ENDQEL              BR IF IT DID
         L     R4,0(,R4)           ELSE GET NEXT QEL
         LTR   R4,R4               TEST FOR QEL END
         BE    ENDQEL              BR IF QEL END
         LRA   R11,0(,R4)          TEST FOR QUE BARFED UP
         BNZ   BADCHAIN            BR IF CHAIN IS BAD
         B     QEL                 ELSE GO BACK TO CALLER
ENDQEL   L     R3,0(,R3)           GET NEXT MINOR QCB
         LTR   R3,R3               TEST FOR END OF MINOR
         BE    ENDMINOR            BR IF END OF MINOR
         LRA   R11,0(,R3)          TEST AGAIN FOR BAD CHAIN
         BNZ   BADCHAIN            AND BR IF BAD
         B     MINOR               AND LOOP BACK
ENDMINOR L     R2,0(,R2)           GET THE NEXT MAJOR
         LTR   R2,R2               TEST FOR LAST MAJOR
         BER   R9                  RETURN IF TRUE
         LRA   R11,0(,R2)          TEST FOR BAD CHAIN
         BNZ   BADCHAIN            BR IF BAD
         B     MAJOR               ELSE LOOP BACK
         EJECT
JOBNAME  SR    R6,R6               ZAP A REG
         IC    R6,15(,R4)          GET THE QEL ASIDTRY
         SLL   R6,2                TIMES 4THE TOP OF THE QEL'S
         L     R7,X'10'            ... CVT
         L     R7,X'22C'(,R7)      ... ASVT
         L     R7,X'20C'(R6,R7)    ... THE ASCB ENTRY
         L     R8,X'AC'(,R7)       GET BATCH JOB NAME
         LTR   R8,R8               TEST FOR BATCH JOB
         BNE   GOTIT               BR IF BATCH JOB
         L     R8,X'B0'(,R7)       GET TSO/STARTED JOBNAME PTR
         LTR   R8,R8               TEST FOR TSO/STARTED
         BNE   GOTIT               BR IF TSO/STARTED
BADJOB   MVC   JOBN(8),=CL8'UNKNOWN'   ELSE MOVE IN 'UNKNOWN'
         BR    R9                  AND RETURN TO CALLER
GOTIT    LRA    R11,0(,R8)         GET FOR REAL JOBNAME POINTER
         BNZ    BADJOB             BR IF POINTER IS BAD
         MVC   JOBN(8),0(R8)       MOVE IN THE JOBNAME
         BR    R9                  AND RETURN TO CALLER
         SPACE 5
NORESVS  LA    R1,NRESVMSG         POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         B     RETURN              AND GET OUT
         SPACE 2
NOCONFL  LA    R1,NCONFLMG         POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         SPACE 2
RETURN   LR    R1,R13              SAVE THE WORK AREA POINTER
         L     R13,4(,R13)         RELOAD THE CALLERS R13
         L     R0,GETWORK          GET THE SP & LENGTH
         FREEMAIN  R,LV=(R0),A=(R1)  FREE THE WORK AREA
         LM    R14,R12,12(R13)     RELOAD THE CALLER'S REGS
         SR    R15,R15             ZERO THE CC REG
         BR    R14                 AND RETURN TO CALLER
         SPACE 2
BADJOBX  ST    R1,SAVER1           SAVE R1 JUST IN CASE
         LA    R1,BJOBMSG          POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         L     R1,SAVER1           RESTORE R1
         BR    R9                  AND RETURN TO CALLER
         SPACE 2
BADCHAIN BAL   R9,ENABLE           RE-ENABLE THIS MOOSE
         LA    R1,BCHAINM          POINT TO THE BAD CHAIN MESSAGE
         BAL   R11,PRINTIT
         B     RETURN              AND GET THE HELL OUT OF HERE
         SPACE 2
TOOMUCH  BAL   R9,ENABLE           GO RE-ENABLE THE MOOSE
         LA    R1,IS2MUCH          POINT TO THE MESSAGE
         BAL   R11,PRINTIT
         B     RETURN              AND GET THE HELL OUT
         SPACE 3
* ***************************************************************
PRINTIT  DS    0H
         LH    R14,0(,R1)          LEN FOR TRANSLATE
         BCTR  R14,0               DECR
         EX    R14,MVCOUT          MOVE MSG TO DSECT AREA
         LA    R1,OUTBUF           POINT AT IT
         SH    R14,=H'4'           DECR 1 FOR TR INST AND 4 FOR HDR
         EX    R14,TRANS
CHKTSO   DS    0H                  TSO OUTPUT ROUTINE
         L     R15,X'0224'         CUR ASCB
         ICM   R15,15,ASCBTSB(R15)  ASCBTSB
ASCBTSB  EQU   X'3C'
         BZ    NOTTSO
         LA    R0,1(,R14)          PUT 1 BACK IN LEN FOR TPUT
         LA    R1,4(,R1)           BUMP PAST THE HEADER
         TPUT  (1),(0)             SEND THE MSG
         BR    R11                  RETURN
MVCOUT   MVC   OUTBUF(1),0(R1)          MOVE MSG TO BUFFER
TRANS    TR    4(1,R1),TRTBLE     TRANSLATE SPECIAL CHARACTERS
*   THIS IS TO PROTECT 3279'S FROM PROG470 ABENDS(AND LOGOFF)
************************
NOTTSO   DS    0H
         L     R0,UCMID            GET UCM ID
         SVC   35                  ISSUE WTO
         BR    R11
****************************************************************
         SPACE 3
         EJECT
*******************************************************************
*                                                                 *
*        THIS IS THE CONSTANT AREA FOR :Q                         *
*                                                                 *
*******************************************************************
         SPACE 1
TRTBLE   DC    CL256' '
         ORG   TRTBLE+C'A'
         DC    C'ABCDEFGHI'
         ORG   TRTBLE+C'J'
         DC    C'JKLMNOPQR'
         ORG   TRTBLE+C'S'
         DC    C'STUVWXYZ'
         ORG   TRTBLE+C'0'
         DC    C'0123456789'
         ORG   TRTBLE+C'-'
         DC    C'-'
         ORG   TRTBLE+C'('
         DC    C'('
         ORG   TRTBLE+C')'
         DC    C')'
         ORG   TRTBLE+C'#'
         DC    C'#'
         ORG   TRTBLE+C'$'
         DC    C'$'
         ORG   TRTBLE+C'@'
         DC    C'@'
         ORG   TRTBLE+C'*'
         DC    C'*'
         ORG   TRTBLE+C'.'
         DC    C'.'
         ORG   TRTBLE+C','
         DC    C','
         ORG   TRTBLE+C'='
         DC    C'='
         ORG   TRTBLE+C'/'
         DC    C'/'
         ORG   TRTBLE+C'Ï'
         DC    C'Ï'
         ORG
         DS    0D
GETWORK  DC    AL1(241)            GETMAIN SUB-POOL NUMBER
         DC    AL3(WORKEND-WORKSTRT)
MVEMINOR MVC   1(1,R5),20(R3)       MOVE THE MINOR NAME
MVCMINAM MVC   CONFLMIN(1),1(R5)    MOVE THE MINOR NAME
FFS      DC    X'FFFFFFFFFFFFFFFFFFFFFFFF'  TERMINATOR DATA
SAVER1   DC    F'0'                R1 SAVE AREA
IS2MUCH  DC    X'004C0100'
         DC    CL76'CMDSBSYS --- TOO MANY ENQUEUE CONFLICTS! HELP I''M X
               A PRISONER IN HERE!!!'
BCHAINM  DC    X'004C0100'
         DC    CL76'CMDSBSYS --- THE SYSTEM ENQUEUE CHAIN IS BROKE!  I X
                 Q U I T !!!'
BJOBMSG  DC    X'004C0100'
         DC    CL76'CMDSBSYS --- BAD JOB NAME POINTER FOUND BY :Q; CONTX
               INUING.'
RESVMSG  DC    X'004C0100'
         DC    CL76'CMDSBSYS --- JJJJJJJJ HAS UUU(VVVVVV) RESERVED, NAMX
               E = XXXXXXXX'
         SPACE 2
NRESVMSG DC    X'002A0100'
         DC    CL76'CMDSBSYS --- NO OUTSTANDING RESERVES.'
         SPACE 2
NCONFLMG DC    X'00260100'
         DC    CL76'CMDSBSYS --- NO ENQUEUE CONFLICTS.'
         SPACE 2
CONFLMSG DC    X'004C0100'
         DC    CL76'CMDSBSYS --- JOB JJJJJJJJ IS ENQUEUED ON QQQQQQQQ/MX
               MMMMMMM'
         SPACE 1
JOBCONFL DC    X'004A0100'
         DC    CL76'CMDSBSYS --- JOB JJJJJJJJ IS WAITING ON JOB XXXXXXXX
               X.'
         SPACE 1
QUITMSG  DC    X'004A0100'
         DC    CL70'CMDSBSYS --- TOO MANY MESSAGES. I   Q U I T !!!'
         SPACE 1
NOMAIN   WTO   'CMDSBSYS --- NO MAIN STORAGE AVAILABLE FOR :Q, TRY AGAIX
               N LATER.'
         LM    R14,R12,12(R13)     RESTORE THE CALLER'S REGS
         SR    R15,R15             ZAP THE CC REG
         BR    R14                 AND RETURN
         SPACE 2
         LTORG
         EJECT
********************************************************************
*                                                                  *
*        THIS IS THE DSECT WORK AREA OF THE PROGRAM                *
*                                                                  *
********************************************************************
         SPACE 1
WORKAREA DSECT
WORKSTRT DS    0D
RGSAVE   DS    18F                 REG SAVE AREA
DBLWD    DS    D
OUTBUF   DS    CL120
JOBN     DS    D
SVCONFJ  DS    D
UCMID    DS    F
CSAEND   DS    F
RESVIND  DS    X
CONFLIND DS    X
CONF2IND DS    X
DOING    DS    X
SAVEIT   DS    X
MESSAGE  DS    CL120
RSVJOB   EQU   MESSAGE+17
RSVUNIT  EQU   MESSAGE+30
RSVVOL   EQU   MESSAGE+34
RSVMAJOR EQU   MESSAGE+59
CONFLJOB EQU   MESSAGE+21
CONFLMAJ EQU   MESSAGE+45
CONFLMIN EQU   MESSAGE+54
CONFJOB  EQU   MESSAGE+21
MAINJOB  EQU   MESSAGE+48
SHREXC   EQU   MESSAGE+63
AREA     DS    400D
RESVTBL  EQU   AREA
CONFLTBL EQU   AREA
WORKEND  EQU   *
         EJECT
         REGS
         END
