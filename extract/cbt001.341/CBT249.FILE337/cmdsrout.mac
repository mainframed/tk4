*          DATA SET XC00032    AT LEVEL 005 AS OF 01/11/82
CMDSROUT START 0                  START THE CMDSBSYS ROUTER
         TITLE '    **********  C O M M A N D   S U B S Y S T E M   R OX
                U T E R   M O D U L E   **********'
         SAVE  (14,12),,*          SAVE CALLER'S REGS
         LR    R5,R1               POINT R5 TO THE SSOB
         USING SSOB,R5             USE R5 AS THE SSOB POINTER
         LR    R10,R15             SET UP A BASE REG
         USING CMDSROUT,R10        THEN USE IT
         L     R0,SAVESP           LOAD THE REG SP # & LENGTH
         GETMAIN R,LV=(R0)         GO GET THE MAIN
         LTR   R15,R15             TEST FOR GOOD GET
         BNE   WRONGO              BR IF N.G.
         ST    R13,4(,R1)          SAVE CALLER'S R13
         ST    R1,8(,R13)          SAVE OUR SAVE AREA
         LR    R13,R1              LOAD OUR SAVE POINTER
         LA    R6,72(,R13)         POINT ESTAE SAVE ARE TO 2ND HALF
         ESTAE ELBOMBO,RECORD=YES,SVEAREA=(R6)
*                                  SET UP FOR A POSSIBLE FAILURE
         L     R2,SSCMBUFF         GET COMMAND BUFF ADDR FROM SSOB
         LA    R2,4(,R2)           POINT PAST LENGTH
         CLI   0(R2),C':'          TEST FOR OUR COMMAND ( A : )
         BE    IWILLTRY            BR IF IT IS FOR US
         MVI   SSOBRETN+3,SSCMSCMD   TELL MVS IT'S NOT FOR US
         B     ENDIT               AND EVACUATE THE PREMISES
IWILLTRY LH    R4,NUMBCMDS         GET THE NO. OF COMMANDS
         LA    R3,LIST             NOW POINT TO THE COMMAND LIST
LOOKLOOP CLC   1(1,R2),0(R3)       TEST FOR GOOD COMMAND
         BE    FOUND               BR IF GOOD ONE FOUND
         LA    R3,1(,R3)           ELSE POINT TO THE NEXT ONE
         BCT   R4,LOOKLOOP         AND LOOP 'TIL DONE
         MVI   SSOBRETN+3,SSCMIMSG   TELL MVS THIS COMMAND STINKS
         B     NOTFOUND            BR IF NOT A GOOD COMMAND
FOUND    L     R7,16               ... CVT
         L     R7,X'128'(,R7)      ... JESCT
         L     R7,X'18'(,R7)       ... 1ST SSCVT
         USING SSCT,R7
SSCVTLP  CLC   SSCTSNAM,=C'CMDS'   TEST FOR COMMAND SUBSYSTEM
         BE    FNDSSCVT            BR IF WE FOUND IT
         L     R7,SSCTSCTA         ELSE POINT TO THE NEXT SSCVT
         B     SSCVTLP             AND GO SEARCH SOME MORE
FNDSSCVT L     R4,SSCTSUSE         GET ADDRESS OF COMMAND LIST
         LA    R6,20               SET THE ENTRY COUNTER
ENTLOOP  CLI   0(R4),0             TEST FOR AN EMPTY SLOT
         BE    OKENTRY             BR IF A SLOT IS EMPTY
         LA    R4,4(,R4)           ELSE POINT TO THE NEXT
         BCT   R6,ENTLOOP          AND LOOP 'TIL END
         B     ENTOFLO             BR IF OVER FLOW
OKENTRY  L     R0,SPENT            GET THE S.P. & LENGTH
         GETMAIN  R,LV=(R0)        GET SOME MAIN FOR THE MESSAGE
         LTR   R15,R15             TEST FOR A GOOD GET
         BNE   NOMAIN              BR IF NONE GOTTEN
         LR    R11,R1              SAVE THE BUFFER ADDRESS
         LA    R11,0(,R11)         CLEAR THE HI ORDER BYTE
         ST    R11,0(,R4)          SAVE ADDR IN THE LIST
         OI    0(R4),X'FF'         INDICATE A GOOD MESSAGE PRESENT
         MVC   0(4,R11),SSCMSCID   SAVE THE SOURCE ADDRESS(UCMID)
         L     R2,SSCMBUFF         GET THE BUFFER ADDRESS
         LH    R3,0(,R2)           GET THE MESSAGE LENGTH
         LA    R3,3(,R3)           ADD LENGTH OF HDR - 1
         EX    R3,MVC1             MOVE MESSAGE TO OUR BUFFER
         L     R9,SSCTSSVT         POINT TO THE SSVT
         DROP  R7                  DROP THE SSCVT POINTER
         L     R8,X'118'(,R9)      LOAD THE ASCB ADDRESS
         L     R7,X'114'(,R9)      LOAD THE ECB ADDRESS
         POST  (R7),ASCB=(R8),ERRET=PSTERR   POST CMDSMAIN
         SPACE 1
         LTR   R15,R15             TEST POST COMPLETION CODE
         B     GOODCMD             THIS WAS A BAD TEST SO IT'S NO-OPED
         MVI   SSOBRETN+3,SSCMIMSG   INDICATE COMMAND NO GOOD
         B     ENDIT               THEN GET OUT
         SPACE 1
GOODCMD  MVI   SSOBRETN+3,SSCMSUBC  TELL MVS I'M COOL, MACHO, ETC.
         SPACE 1
ENDIT    ESTAE 0
         LR    R2,R13              SAVE OUR SAVE AREA POINTER
         L     R13,4(,R13)         RESTORE THE CALLER'S R13
         L     R0,SAVESP           LOAD THE S.P. & LENGTH
         FREEMAIN  R,LV=(R0),A=(R2)  FREE THE REGSAVE AREA
         LM    R14,R12,12(R13)     RELOAD THE CALLER'S REGS
         SR    R15,R15             INDICATE ZERO COMP. CODE
         BR    R14                 AND RETURN TO CALLER
         EJECT
WRONGO   WTO   'CMDSBSYS --- UNABLE TO GETMAIN A SAVE AREA, TRY AGAIN',X
               ROUTCDE=(2),DESC=(4)
         LM    R14,R12,12(R13)     RESTORE THE REGS
         LA    R15,4               INDICATE COND = 4
         BR    R14                 AND RETURN TO CALLER
         SPACE 2
NOMAIN   WTO   'CMDSBSYS --- UNABLE TO GETMAIN A MESSAGE BUFFER; TRY AGX
               AIN',ROUTCDE=(2),DESC=(4)
         SPACE 2
NOTFOUND WTO   'CMDSBSYS --- COMMAND NOT FOUND',ROUTCDE=(2),DESC=(4)
         B     ENDIT               SO GET OUT
         SPACE 2
ENTOFLO  WTO   'CMDSBSYS --- MESSAGE BUFFER FULL; TRY AGAIN LATER',    X
               ROUTCDE=(2),DESC=(4)
         B     ENDIT               GET OUT
         DROP  R10
         SPACE 2
         USING *,R15               USE THE BR REG
PSTERR   WTO   'CMDSBSYS --- ERROR POSTING CMDSMAIN; TRY AGAIN',       X
               ROUTCDE=(2),DESC=(4)
         LA    R15,4               SET AN ERROR CODE
         BR    R14                 AND RETURN TO POST ROUTINE
         DROP  R15
         EJECT
ELBOMBO  STM   R0,R6,12(R13)       SAVE SOME REGS
         BALR  R6,0                SET UP ANOTHER BASE
         USING *,R6                USE THE NEW BASE REG
         CH    R0,=H'12'           TEST FOR NO SDWA
         BE    NOSDWA              BR IF NONE
         LR    R5,R1               SAVE SDWA POINTER
         USING SDWA,R5
         MVO   56(4,R13),SDWANXT1  GET THE FAILING ADDRESS
         OI    59(R13),X'0F'       INSURE CORRECT SIGN
         UNPK  48(6,R13),56(4,R13)  UNPK THE ADDRESS
         TR    48(6,R13),TRT       TRANSLATE THE ADDRESS
         MVC   ABNDWTO+85(6),48(R13)  MOVE IN FAILING ADDRESS
         CLC   SDWAINC1,=H'17'      TEST FOR CODE > PAGE FAULT
         BH    JUSTDOIT             BR IF NOT 0CX
         MVC   ABNDWTO+122(2),=C'0C' ELSE SET UP FOR OCX
         CLC   SDWAINC1,=H'16'      TEST FOR A PAGE FAULT
         BL    OCOTHER              BR IF JUST A 0X WHERE X < 10
         MVI   ABNDWTO+124,C'4'     INDICATE A 0C4
         B     ABNDWTO              THEN GO OUTPUT THE MESSAGE
OCOTHER  MVC   ABNDWTO+124(1),SDWAINC1+1  MOVE IN THE ABEND CODE
         OI    ABNDWTO+124,X'F0'    INSURE GOOD CHARACTER
         B     ABNDWTO             GO OUTPUT THE MESSAGE
JUSTDOIT MVO   56(2,R13),SDWAINC1  MOVE THE ABEND CODE
SHORTABN OI    57(R13),X'0F'       INSURE CORRECT SIGN
         UNPK  48(4,R13),56(2,R13)  UNPK THE COMP. CODE
         TR    48(4,R13),TRT       TRANSLATE THE COMP. CODE
         MVC   ABNDWTO+122(3),49(R13)  MOVE IN THE ABEND CODE
ABNDWTO  WTO   ('CMDSBSYS --- ROUTER MODULE FAILED:'),('CMDSBSYS --- FAX
               ILING ADDRESS = XXXXXX'),('CMDSBSYS --- ABEND CODE = SXXX
               X'),ROUTCDE=(2),DESC=(4)
         SPACE 1
******************************************************************
*        TAKE A DUMP FOR POSTERITY                               *
******************************************************************
         SPACE 1
         SDUMP HDR='CMDSSBSYS --- CMDSROUT FAILURE',                   X
               SDATA=(NUC,CSA,SQA,RGN,LSQA,LPA),QUIESCE=YES
         SPACE 1
         L     R2,16               ... CVT
         L     R2,X'128'(,R2)      ... JESCT
         L     R2,X'18'(,R2)       ... 1ST SSCVT
         USING SSCT,R2
SSCVTLP3 CLC   SSCTSNAM,=C'CMDS'   TEST FOR COMMAND SUS-SYSTEM
         BE    FNDCDS              BR WHEN FOUND
         L     R2,SSCTSCTA         ELSE POINT TO NEXT SSCVT
         B     SSCVTLP3            AND LOOP 'TIL FOUND
FNDCDS   MVC   SSCTSSVT,=F'0'      TURN OFF THE SUB-SYSTEM
         DROP  R2,R5               DROP THE SSCVT & SDWA POINTERS
         WTO   'CMDSBSYS --- COMMAND SUB-SYSTEM FAILED - TERMINATED',  X
               ROUTCDE=(2),DESC=(4)
         L     R0,SP1              GET THE SSVT S.P. & LENGTH
         L     R2,16(,R2)          AND THE SSVT ADDRESS
         FREEMAIN  R,LV=(R0),A=(R2)  FREE THE SSVT CORE
         SETRP WKAREA=(R5),DUMP=NO,COMPCOD=(599,SYSTEM)
         LM    R0,R6,12(R13)       RESTORE THE SAVED REGS
         BR    R14                 AND RETURN TO CALLER
         SPACE 5
NOSDWA   SRL   R1,12               POSITION COMP. CODE
         ST    R1,48(,R13)         SAVE THE CODE
         MVO   56(2,R13),48(2,R13)  OFFSET THE CODE
         B     SHORTABN            AND GO COMPLETE THE ABEND
         EJECT
SAVESP   DS    0F
         DC    AL1(241)            SUBPOOL 241
         DC    AL3(144)            FOR A LENGTH OF 156
SP1      DC    AL1(241)            SUBPOOL 241
         DC    AL3(512)            FOR A LENGTH OF 512
SPENT    DC    AL1(241)            S.P. 241
         DC    AL3(120)            AND L = 120
MVC1     MVC   4(1,R11),0(R2)      OBJECT OF EX INST
NUMBCMDS DC    H'35'               NUMBER OF VALID COMMANDS (12/24/79)
TRT      EQU   *-240
         DC    C'0123456789ABCDEF'   ADDRESS TRANSLATE TABLE
LIST     DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ123456789'  VALID COMMANDS
*              LIST COMMANDS CHANGED 8/28/80  J.W.
         LTORG
         EJECT
         IHASDWA
         EJECT
         IEFJESCT
         EJECT
         IEFJSCVT
         EJECT
         CVT
         EJECT
         IEFJSSOB (CM)
         EJECT
         REGS
         END
