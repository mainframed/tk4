TSSOSS10 CSECT
***********************************************************************
* THIS IS THE TSSO COMMAND INTERCEPT ROUTINE. CURRENT FUNCTION IS TO  *
* CHECK FOR A "#" AND PASS TO TSSO.                                   *
*                                                                     *
* NOTE: ALL MESSAGES START WITH TSSAXXXS WHERE 301 <= XXX <= 350      *
*       LAST MESSAGE NUMBER = 309
***********************************************************************
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSO
         USING  TSSOSS10,R12       GET ADDRESSIBILITY
         B      ARINIT1
         DC     CL8'&SYSDATE'      GET DATE
         DC     CL8'&SYSTIME'      AND TIME FOR QVERS COMMAND
ARINIT1  DS     0H
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR     R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=1024
         LR     R11,R1
         USING  SS10DATD,R11
         LR     R10,R8
         USING  SSOB,R10
*
         MVC    SAWTO1D(SAWTO1CE-SAWTO1C),SAWTO1C
*
         GETCVT R8                 FIND TSSO CVT
         USING TSSOCVT,R8
*****************************************************************
* IF TSSO IS TERMINATED, UNDER NO CIRCUMSTANCES DO ANYTHING !
*****************************************************************
         LTR    R8,R8              ANY CVT ?
         BO    S34EXIT             NO, DONT DO A THING ...
*
         TM    TSSOFLG1,TSSOGONE   TERMINATING?
         BO    S34OEXIT            YES - IGNORE
*
*
*
         L     R9,SSOBINDV         POINT TO FUNCTION AREA
         USING SSCMBGN,R9          ADDRESS IT.
         L     R2,SSCMBUFF         POINT TO COMMAND BUFFER.
         LH    R5,0(,R2)           GET LENGTH
* COMMAND BUFFER FORMAT IS AS FOLLOWS:
* 2 BYTES: LENGTH
* 2 BYTES: WHO KNOWS
* 140 BYTES: COMMAND
         CH    R5,=H'140'          OUTRAGEOUS LENGTH?
         BH    S34OEXIT            YES - IGNORE IT
         LTR   R5,R5               NEGATIVE LENGTH ?
         BNP   S34OEXIT            YES - IGNORE
*
***********************************************************************
* HERE, WE HAVE PRETTY MUCH DETERMINED THAT WE HAVE AN OPERATOR       *
* COMMAND. WE MUST CHECK THE SECURITY LEVEL. IF SOMEONE HAS BEEN BOLD *
* ENOUGH TO TRY OUT SECLEVEL=MAX, THEN WE WILL CHECK IF THE OPERATOR  *
* HAS SIGNED ON. IF NOT, REJECT THE COMMAND.                          *
***********************************************************************
         CLC   SECLEVEL(4),=CL4'MAXI'   MAXIMAL SECURITY ?
         BNE   OPCMDOK                  NO, ALWAYS PERMIT COMMAND
*
         CLC   4(1,R2),TSSOCHAR          IS THIS FOR TSSO ?
         BE    OPCMDOK                   YES - ALLOW IT !
*
         CLC   SSCMSCID(3),=XL4'000000'  LOOKS LIKE A CONSOLE ID?
         BNE   OPCMDOK                   WELL.. ALLOW THE COMMAND FOR
*                                        NOW, BECAUSE WE DONT KNOW
*
         CLC   SSCMSCID(4),=XL4'00000000'  IS THIS CONSOLE "0" ??
         BE    OPCMDOK                   WELL.. DONT WANT TO BLOCK MVS
*
         MVC   CONSID(4),SSCMSCID
***********************************************************************
* WE WISH TO ALLOW SUBSYSTEM CONSOLES TO ISSUE ANY COMMAND THEY LIKE. *
* TO DO THIS, WE MUS CHECK IF THE CONSOLE ISSUING THIS COMMAND IS A   *
* SUBSYSTEM ALLOCATABLE CONSOLE.                                      *
***********************************************************************
         L     R2,16             GET CVT
         USING CVT,R2
         L     R2,CVTCUCB        GET UCM BASE
         USING UCM,R2
         LM    R3,R5,UCMVEA      GET FIRST, LENGTH AND LAST UCME'S
         DROP  R2
         USING UCMLIST,R3
UCMSLOOP DS    0H
         CLC   CONSID+3(1),UCMID      IS THIS THE CONSOLE ?
         BE    UCMFOUND
         BXLE  R3,R4,UCMSLOOP
*
         WTO   'TSSA308S SEVERE ERROR LOCATING UCM BITS-> CONTACT TSSO X
               SUPPORT',DESC=(1,11)
         B     OPCMDOK
*
UCMFOUND DS    0H
         TM    UCMDISP2,UCMDISPL     SUBSYSTEM ALLOCATABLE CONSOLE ?
         BO    OPCMDOK
*
         DROP  R3
         L     R7,SECARRAY        GET SECURITY ARRAY
         LTR   R7,R7
         BZ    SS10LERR           LOGICAL ERROR IF NO ARRAY..
         LA    R3,20              MAXIMUM ENTRIES IN TABLE
SS10LOOP DS    0H
         USING SECENTRY,R7
         TM    SECFLAGS,X'80'     ENTRY IN USE ?
         BNO   NEXTENTR
         CLC   SECCONS(2),CONSID+2     IS THIS THE CORRECT CONSOLE
         BE    CONSLOG                 YUP !
         B     NEXTENTR
*
NEXTENTR DS    0H
         LA    R7,SECRLGNT(R7)    TRY NEXT ENTRY
         BCT   R3,SS10LOOP
*
***********************************************************************
* IF WE COME HERE, WE HAVE DETERMINED THAT THE CONSOLE IS NOT LOGGED  *
* ON TO TSSO...                                                       *
***********************************************************************
NOTLOGED DS    0H
         LH    R1,CONSID+2        GET CONSOLE FOR MESSAGE
         ST    R10,SAVER10
         LA    R10,WORKAREA
         CALL  JCECVTBD
         STCM  R1,B'0011',SAWTO1D+40
         L     R10,SAVER10
         WTO   MF=(E,SAWTO1D)       (MSG 306)
         B     S34BUSY            TELL MVS WE COULD NOT PROCESS THE
*                                 COMMAND.
CONSLOG  DS    0H
***********************************************************************
* WE COME HERE WHEN WE HAVE DETERMINED THAT THE CONSOLE IS LOGGED ON  *
* TO TSSO. WE WILL CHECK TO SEE IF THE CONSOLE HAS "TIMED OUT". IF SO *
* WE WILL ISSUE A LOGOFF COMMAND FROM THAT CONSOLE, AND REJECT THIS   *
* COMMAND.                                                            *
***********************************************************************
         TIME  BIN
         CLC   SECRDATE(4),=XL4'00000000'   FIRST COMMAND ?
         BE    NOTMEOUT                     YUP, NO TIME OUT THIS TIME
         C     R1,SECRDATE           SAME DATE AS LAST COMMAND ?
         BE    SAMEDATE              YUP, NO NEED TO ADJUST TIME
         A     R0,=F'8640000'        ADJUST TIME IN REG1
SAMEDATE DS    0H
         S     R0,SECRTIME           R1 NOW HAS DIFFERENCE IN .01 SECS
         L     R3,SECTMEOT           GET TIMEOUT VALUE IN MINUTES FROM
*                                    TSSO CVT
         M     R2,=F'6000'           CONVERT TO .01 SECONDS
         CR    R0,R3                 COMPARE THE TIMEOUT VALUE...
         BH    TIMEDOUT              CONSOLE TIMED OUT...
NOTMEOUT DS    0H
         TIME  BIN
         ST    R1,SECRDATE
         ST    R0,SECRTIME
         B     OPCMDOK
TIMEDOUT DS    0H
***********************************************************************
* HERE, WE HAVE DETERMINED THAT THE CONSOLE HAS TIMED OUT. WE WILL    *
* ISSUE A TSSO .LOGOFF COMMAND AND REJECT THIS COMMAND                *
*                                                                     *
***********************************************************************
         LA    R1,MVSCMD
         MVC   MVSCMD(2),=H'12'      4 + 8(#.LOGOFF)
         MVC   MVSCMD+2(2),=H'0'
         MVI   MVSCMD+4,X'40'
         MVC   MVSCMD+5(80),MVSCMD+4
         MVC   MVSCMD+4(1),TSSOCHAR      MOVE IN SEC
         MVC   MVSCMD+5(7),=CL7'.LOGOFF'
         LH    R0,CONSID+2
         SVC   34
         LH    R0,CONSID+2
         WTO   'TSSA309A CONSOLE HAS TIMED OUT - PLEASE LOGON WITH THE X
               .LOGON TSSO CONTROL COMMAND',MCSFLAG=(REG0)
         B     NOTLOGED
*
         DROP  R7
OPCMDOK  DS    0H
***********************************************************************
* HERE, WE HAVE DETERMINED THAT THE OPERATOR IS SIGNED ON TO TSSO.    *
* WE WILL ALLOW (AT LEAST IN THIS RELEASE) ALL OPERATOR COMMANDS.     *
***********************************************************************
*
         TM    TSSOFLG2,TSSOMON    MONITORING?
         BNO   NM1
         WTO   'TSSA301A TSSO COMMAND INTERCEPT ROUTINE ACTIVE'
*****************************************************************
* TSSO SHOULD NOT INTERFERE WITH THIS OS COMMAND                *
*****************************************************************
NM1      DS    0H
         CLC   4(1,R2),TSSOCHAR    MINE?
         BNE   S34OEXIT            NO - IGNORE
CMDPROC  DS    0H
         TM    TSSOFLG2,TSSOMON    MONITORING?
         BNO   NM2
         WTO   'TSSA302A TSSO WILL PROCESS THIS COMMAND'
*****************************************************************
* HERE, WE HAVE DETERMINED THAT THE OS COMMAND SHOULD BE HANDLED*
* BY THE TSSO MAINLINE MODULE, IF TSSO IS AVAILABLE.            *
*****************************************************************
NM2      DS    0H
         TM    TSSOFLG1,TSSOGONE   TERMINATING?
         BNO   STAT2               YES - IGNORE
         WTO   'TSSA303A TSSO IS IN SHUTDOWN STATUS- CMD REJECTED'
         B     S34OEXIT            IGNORE COMMAND
STAT2    DS    0H
         L     R3,NEXTCMD          ADDRESS OF NEXT AVAILABLE
         USING TSSOCMND,R3         ADDRESS RING POSITION
*                                  COMMAND BUFFER
         TM    CMNDFLAG,X'80'      IS THIS BUFFER STILL BUSY.
         BNO   STAT3               YES - BRANCH
     WTO   'TSSA304A TSSO IS BUSY WITH PREVIOUS COMMNANDS'
     WTO   'TSSA304A (CONT) THIS MAY INDICATE A TSSO SYSTEM PROBLEM'
         B     S34BUSY
STAT3    DS    0H
         MVI   CMNDCMD,X'40'
         MVC   CMNDCMD+1(139),CMNDCMD  BLANK THE COMMAND AREA
         OI    CMNDFLAG,X'80'      TURN ON COMMAND FLAG
         BCTR  R5,0                LENGTH MINUS 1 FOR EX
         EX    R5,MOVECMD          COPY THE COMMAND
         B     AR1                 BRANCH AROUND MVC
MOVECMD  MVC   CMNDCMD(0),4(R2)    (EXECUTED)
AR1      DS    0H
         MVC   CMNDLENG(4),0(R2)   COPY THE LENGTH
*****************************************************************
* CONSOLE ID WORKS AS FOLLOWS:
* IF COMMAND HAS COME FROM A CONSOLE, THEN THE FIRST 3 BYTES OF THIS
* FIELD ARE ZEROES, AND THE LAST IS THE CONSOLE ID.
*****************************************************************
         CLC   SSCMSCID(3),=XL6'000000'  LOOKS LIKE A CONSOLE ID?
         BE    STAT4                     YUP, LOOKEN GOOD.
* HERE, WE HAVE FOUND A COMMAND THAT WAS NOT ISSUED FROM A CONSOLE
* WE WILL INVALIDATE THE BUFFER BY RESETTING THE FLAG, AND
* ISSUE A MESSAGE TO THE CONSOLE, AND THE PROGRAMMER
         NI    CMNDFLAG,255-X'80'  INVALIDATE THE COMMAND
         B     S34EXITM            EXIT WITH A MESSAGE
STAT4    DS    0H
         L     R0,SSCMSCID
         ST    R0,CMNDCONS         SAVE IT IN THIS TSSO COMMAND TOKEN.
         L     R2,CMNDNEXT
         ST    R2,NEXTCMD          DO THIS FOR NEXT TIME.
         TM    TSSOFLG2,TSSOOKFL   IS 'OK' TO BE SUPPRESSED?
         BO    S34NOACK            YES - BRANCH
         WTO   '# OK',MCSFLAG=(REG0)
S34NOACK DS    0H
*****************************************************************
* NOW, THE IDEA IS TO POST THE TSSO MAINLINE ROUTINE, ONLY IF IT
* NEEDS TO BE POSTED. IF IT NEEDS TO BE POSTED, THEN THE WAIT
* BIT IN THE ECB SHOULD BE ON. WE WILL USE COMPARE AND SWAP
* FOR TWO REASONS. THE FIRST, IS THAT I NEVER USED A COMPARE AND
* SWAP, THE SECOND, IS THAT WE WANT TO ENSURE THAT TSSO DOESNT
* TURN THE WAIT BUT ON AFTER WE DO THE TM THAT WOULD BE REQUIRED, AND
* THEREFORE MISS THE POST.
*****************************************************************
         L     R2,$SVPOSTE         GET THE TSSO MAINLINE ECB
         L     R3,=X'40000000'
         TM    $SVPOSTE,X'80'      WAIT BIT ON ?
         BO    DOPOST              IF YES, DO THE POST.
         N     R2,=X'7FFFFFFF'    COMPARE OPERAND WITH WAIT BIT OFF
         CS    R2,R3,$SVPOSTE      ** COMPARE AND SWAP **
         BE    POSTDONE            COMPARE AND SWAP DID THE POST.
DOPOST   DS    0H
         LA    R5,$SVPOSTE          GET ECB TO POST
         L     R6,TSSOASCB          GET ADDRESS SPACE OF TSSO
         LA    R7,ENDPROG1          GET ERROR ROUTINE FOR XMEM POST
         LA    R1,$SVPOSTL          GET DSECT FOR XMEM POST
         SPACE
         POST  (R5),X'111',ASCB=(R6),ERRET=(R7),MF=(E,(1))
POSTDONE DS    0H
         B     S34MEXIT            INDICATE COMMAND IS MINE
*
S34BUSY  LA    R15,SSCMIMSG        COULD NOT EXECUTE
         B     S34EXIT             ISSUE IEE707I 'TEXT' NOT EXECUTED
*
S34MEXIT LA    R15,SSCMSUBC        MINE
         B     S34EXIT
*
S34OEXIT DS    0H
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
*
S34EXITM DS    0H                  NOT MINE, EXIT WITH MESSAGE
         WTO   'TSSA305E TSSO COMMANDS MAY NOT BE ISSUED FROM THIS ENVIX
               RONMENT',ROUTCDE=(2,11)
         LA    R15,SSCMSCMD        NOT MINE
         B     S34EXIT
SS10LERR DS    0H
***********************************************************************
* WE COME HERE IF WE HAVE A LOGICAL ERROR IN TSSOSS10. WE WILL ISSUE  *
* A MESSAGE, DENY EVERYTHING, AND TERMINATE.                          *
***********************************************************************
         XR    R15,R15
         WTO   'TSSA307S SEVERE ERROR IN TSSOSS10 - CONTACT TSSO SYSTEMX
               S PROGRAMMER'
S34EXIT  DS    0H
         ST    R15,SSOBRETN
         FREEMAIN R,LV=1024,A=(R11)
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         XR    R15,R15
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
***********************************************************************
* TSSOSS10 CONSTANTS AREA.                                            *
*                                                                     *
***********************************************************************
SAWTO1C  WTO   'TSSA306S COMMAND REJECTED - CONSOLE XX NOT LOGGED ON TOX
                TSSO',MF=L
SAWTO1CE EQU   *
***********************************************************************
* TSSOSS10 DATA AREA.                                                 *
*                                                                     *
***********************************************************************
SS10DATD DSECT
WORKAREA DS    CL200
SAWTO1D  WTO   'TSSA306S COMMAND REJECTED - CONSOLE XX NOT LOGGED ON TOX
                TSSO',MF=L
CONSID   DS    F                 CONSOLE ID ISSUING THIS COMMAND
SAVER10  DS    F                 REGISTER 10 WHEN USING JCE ROUTINE
MVSCMD   DS    CL100
         CVT   DSECT=YES
         IEFJESCT
         IEFJSSVT
         IEFJSCVT
         IEFJSSOB (CM,WT),CONTIG=NO
UCM      DSECT
         IEECUCM
         TSSOCVT
         TSSOCMND
         TSSOSECR
         IHAWQE
         IHAORE
         EQUATES
         END   TSSOSS10
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOSS10(''TSSO VERSION 4.2 &SYSDATE &SYSTIME'')'
 PUNCH ' INCLUDE  SYSLMOD(JCECVT)'
 PUNCH ' ENTRY    TSSOSS10'
 PUNCH ' NAME     TSSOSS10(R)'
         END
