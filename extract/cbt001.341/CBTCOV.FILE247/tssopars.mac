TSSOPARS CSECT
         EQUATES
         STM    R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR     R12,R15            LOAD ENTRY POINT OF TSSOPARS
         USING  TSSOPARS,R12       GET ADDRESSIBILITY
         LR     R8,R1
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         ST     R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST     R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR     R13,R1             KEEP A COPY IN R13
***********************************************************************
***********************************************************************
* THIS IS A SUBROUTINE USED TO PARSE    ALMOST ANY STRING. THE INPUT
* IS AS FOLLOWS:
*
* 0(R1)- POINTS TO THE STRING TO BE DECIPHERED.
* 4(R1)- A FULLWORD CONTAINING THE LENGTH OF THE STRING
* 8(R1)  AN ARRAY OF SIXTEEN BYTE ENTRIES. THE FIRST 2  BYTES OF THE
*        ARRAY CONTAIN A COUNT OF THE MAXIMUM NUMBER OF ENTRIES IN THE
*        ARRAY
*
* 12(R1) A 4 BYTE STRING OF SEPERATOR CHARACTERS. ANY OF THE CHARACTERS
*        IN THE STRING MAY BE USED AS THE SEPARATOR.
*
*
*
* EXIT CONDITIONS:
* R15 = 0: TSSOPARS COMPLETED NORMALLY. ARRAY HAS BEEN COMPLETED.
* R15 = 4: TSSOPARS COMPLETED ABNORMALLY, ARRAY HAS FILLED UP.
* R15 = 8: TSSOPARS COMPLETED ABNORMALLY, ARRAY PROBABLY NOT VALID
* 0(ARRAY): TSSOPARS WILL RETURN THE NUMBER OF ENTRIES IN THE ARRAY IN
*           THE FIRST TWO BYTES.
***********************************************************************
         GETMAIN R,LV=200
         LR    R11,R1
         USING DATA,R11
         LR    R1,R8
         L     R2,0(R1)              GET ADDRESS OF STRING
         L     R7,8(R1)              GET ADDRESS OF ARRAY
         ST    R7,ARRAY              PUT IT AWAY FOR LATER
         LH    R3,0(R7)              GET MAXIMUM 16 BYTE ENTRIES
         STH   R3,MAXCOUNT
         LA    R7,2(R7)              NOW POINT TO START OF ARRAY
         L     R4,4(R1)              GET LENGTH OF STRING
         LA    R4,0(R2,R4)
         L     R3,12(R1)
         MVC   SEPCHARS(4),0(R3)
         XC    ARRCOUNT(4),ARRCOUNT   ZERO OUT ARRCOUNT
*
PRSELOOP DS    0H
***********************************************************************
* R7 - NEXT POSITION IN ARRAY
* R2 - CURRENT POSITION IN STRING
***********************************************************************
         BAL   R14,FNDNBLNK           FIND NEXT NON BLANK
         LTR   R15,R15                 WHAT HAPPENED ?
         BNZ   ALLBLANK              IT WAS ALL BLANK !
* R2 POINTS TO FIRST NON BLANK CHARACTER IN REST OF STRING
         LR    R5,R2                 REMEMBER (SPOCK TO MCCOY (ST:TWOK)
         BAL   R14,FNDBLANK          GET NEXT BLANK CHARACTER
         CH    R15,=H'0'             WHAT HAPPENED THIS TIME ?
         BE    GOTBLNK               GOT A BLANK
         CH    R15,=H'4'             HOW ABOUT NOW ?
         BE    NOBLANKS              NO MORE BLANKS LEFT !
         CH    R15,=H'8'             WHAT ABOUT THIS ?
         BE    TOBIG                 MORE THEN SIXTEEN TOKENS !
*
GOTBLNK  DS    0H
* R2 PTS TO ENDING BLANK
* R5 PTS TO START OF STRING
* R3 HAS COUNT OF CHARS
* R7 HAS CURRENT TOKEN POSITION
         BCTR  R3,0
         EX    R3,MOVEIT
         B     AR1
MOVEIT   MVC   0(0,R7),0(R5)         ** EXECUTED **
AR1      DS    0H
         LA    R7,16(R7)             POINT TO NEXT TOKEN POSITION
         L     R3,ARRCOUNT
         LA    R3,1(R3)
         ST    R3,ARRCOUNT
         B     NEXTTOKE              CHECK FOR NEXT TOKEN...
*
NOBLANKS DS    0H
* NO BLANKS FOUND AT END OF STRING.. PROCESS THIS LAST TOKEN AND EXIT
* R2 - END OF STRING
* R3 - LENGTH
* R5 - START OF STRING
* R7 - CURRENT TOKEN POSITION
         BCTR  R3,0
         EX    R3,MOVEIT
         L     R3,ARRCOUNT
         LA    R3,1(R3)
         ST    R3,ARRCOUNT
         B     PARSEND
*
ALLBLANK DS    0H
         B     LASTTOKE
*
TOBIG    DS    0H
         MVC   0(16,R7),0(R5)
         LA    R2,1(R2)              POINT TO NEXT CHARACTER
         LA    R7,16(R7)
         L     R3,ARRCOUNT
         LA    R3,1(R3)
         ST    R3,ARRCOUNT
         B     NEXTTOKE
*
NEXTTOKE DS    0H
         L     R3,ARRCOUNT           GET ARRCOUNT
         CH    R3,MAXCOUNT           ARE WE AT END OF ROPE ?
         BNL   LASTTOKE
         B     PRSELOOP              GO AROUND AGAIN
LASTTOKE DS    0H
         B     PARSEND
PARSEND  DS    0H
         L     R1,ARRCOUNT
         L     R2,ARRAY
         STH   R1,0(R2)         PUT NUMBER OF TOKENS AWAY
         FREEMAIN R,LV=200,A=(R11)
ENDPROG  DS   0H
         LR     R1,R13              GET ADDRESS OF CORE IN R1
         L      R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
         L      R14,12(R13)         RELOAD RETURN POINT
         XR     R15,R15
         LM     R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR     R14
*
*
FNDBLANK DS    0H
* R2- A NON BLANK
* R4- A(LAST CHARACTER IN STRING)
         LA    R3,1             INCLUDE FIRST CHARACTER FOUND
BLNKLOOP DS    0H
         LA    R2,1(R2)         SCAN NEXT CHARACTER
         CLC   0(1,R2),SEPCHARS   GOT A SEPARATOR ?
         BE    FNDBLK           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+1    GOT A SEPARATOR ?
         BE    FNDBLK           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+2    GOT A SEPARATOR ?
         BE    FNDBLK           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+3    GOT A SEPARATOR ?
         BE    FNDBLK           YUP- GOT ONE
         CLI   0(R2),X'00'      IS THIS ZEROS ?
         BE    FNDBLK
         CR    R2,R4            AT END OF STRING
         BNL   LASTWD           YUP, LAST WORD...
         LA    R3,1(R3)         INCREMENT COUNT
         CH    R3,=H'16'        TOKEN TO BIG ?
         BE    WORD16           YUP- QUITE SAD..
         B     BLNKLOOP
*
FNDBLK   DS    0H
         XR    R15,R15
         BR    R14
*
LASTWD   DS    0H
         LA    R15,4
         BR    R14
*
WORD16   DS    0H
         LA    R15,8
         BR    R14
*
*
FNDNBLNK DS    0H
* FIND NEXT NON BLANK CHARACTER IN STRING
* R2- POINTS TO CURRENT POSITION IN STRING
* R4- POINTS TO END OF STRING
         CLC   0(1,R2),SEPCHARS   GOT A SEPARATOR ?
         BE    NGOTIT           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+1    GOT A SEPARATOR ?
         BE    NGOTIT           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+2    GOT A SEPARATOR ?
         BE    NGOTIT           YUP- GOT ONE
         CLC   0(1,R2),SEPCHARS+3    GOT A SEPARATOR ?
         BE    NGOTIT           YUP- GOT ONE
         CLI   0(R2),X'00'      ZEROS COUNT AS BLANKS...
         BE    NGOTIT
         B     GOTIT             NO, GOT NEXT NON-BLANK
NGOTIT   DS    0H
         LA    R2,1(R2)          INCREMENT
         CR    R2,R4             AT END OF STRING ?
         BNL   ENDOSTR           YUP- QUITE SAD.
         B     FNDNBLNK
GOTIT    DS    0H
         XR    R15,R15
         BR    R14
ENDOSTR  DS    0H
         LA    R15,4
         BR    R14
*
DATA     DSECT
SEPCHARS DS    CL4               SEPARATOR CHARACTERS
ARRCOUNT DS    F
MAXCOUNT DS    H
ARRAY    DS    A                 ADDRESS OF ARRAY
DUMMY    DS    F
         END   TSSOPARS
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOPARS(''V 1.1 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    TSSOPARS'
 PUNCH ' NAME     TSSOPARS(R)'
         END
