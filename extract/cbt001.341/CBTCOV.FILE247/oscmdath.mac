         TITLE ' T S O   A U T H O R I Z A T I O N   R O U T I N E'
OSCMDATH CSECT
***********************************************************************
* THIS IS THE TSSO AUTHORIZATION ROUTINE. IT IS CALLED BY OSCMD    TO *
* DETERMINE THE AUTHORITY LEVEL OF THE TSO USER TO ISSUE MVS COMMANDS.*
*                                                                     *
* THE AUTHORIZATION BYTE RETURNED BY THIS MODULE SHOULD BE IN THE FORM*
* REQUIRED BY THE TSSOPCMD ROUTINE. TO ALLOW FOR THE CASE WHERE NO    *
* ACCESS IS RETURNED, A RETURN CODE 4 ISRETURNED TO OSCMD. THIS MODULE*
* CAN BE REPLACED, AND THEN RELINKED INTO OSCMD    AS DESIRED BY THE  *
* INSTALLATION.                                                       *
*                                                                     *
*                                                                     *
* ARGUMENTS: 0(R1) - ADDRESS OF A 1 BYTE FIELD CONTAINING AUTH BYTE   *
*
* RETURN CODES:
*
* 0: OSCMDATH SUCCESSFULL: AUTHORITY BYTE RETURNED IN PARM LIST.      *
* 4: OSCMDATH SUCCESSFULL: NO ACCESS TO OSCMDATH IS PERMITTED.        *
*
* ANYTHING ELSE: OSCMDATH UNSUCCESSFULL - ABORT COMMAND.
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF OSCMDATH
         LR    R8,R1              SAVE INPUT PARMS.
         USING OSCMDATH,R12
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         GETMAIN R,LV=72          WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                  SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=4096         GETMAIN SOME PRIVATE STORAGE...
         LR    R11,R1
         USING AUTHDATD,R11
         MVC   EXTRACTD(EXTRACTE-EXTRACTC),EXTRACTC
*
***********************************************************************
* THE ALGORITHM WE USE AT BELL COMMUNICATIONS RESEARCH IS VERY EASY.  *
* WE CHECK THE PSCB BYTES, AND IF THE FIRST TWO BITS OF BYTE 1 ARE ON,*
* YOUR AUTHORIZED FOR EVERYTHING. IF JUST THE FIRST BIT IS ON, YOU    *
* GET I/O AUTHORITY, AND IF NO BITS ARE ON, YOU GET DISPLAY ONLY      *
***********************************************************************
         LA    R10,PSCBADDR
         EXTRACT (10),'S',FIELDS=PSB,MF=(E,EXTRACTD)
         L     R10,PSCBADDR
         USING PSCB,R10
         L     R2,0(R8)              GET FIRST PARM ADDR
         ST    R2,AAUTH               STORE IT AWAY
*
         CLC   PSCBUSER(7),=CL7'CONSINF'
         BE    NOAUTH
         TM    PSCBATR2,X'C0'        BOTH BITS ON ?
         BO    FULLAUTH              YUP, GOOD NEWS
         TM    PSCBATR2,X'80'        ONE BIT ON, AT LEAST ?
         BO    PARTAUTH              ALMOST GOOD NEWS.
         B     INFOAUTH              INFO ONLY - BAD NEWS
*
FULLAUTH DS    0H
         MVI   AUTHBYTE,B'11100001'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
PARTAUTH DS    0H
***********************************************************************
* IN OUR CASE, PART AUTHORITY MEANS I/O ONLY                          *
***********************************************************************
         MVI   AUTHBYTE,B'01000000'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
INFOAUTH DS    0H
         MVI   AUTHBYTE,B'00000000'  AS DEFINED BY TSSOPCMD
         XC    RETCODE(2),RETCODE
         B     PASTAUTH
PASTAUTH DS    0H
         L     R2,AAUTH              GET ADDRESS OF OSCMD AUTHBYTE
         MVC   0(1,R2),AUTHBYTE      AND PUT IT IN CALLER PARM LIST
         B     ENDPROG
NOAUTH   DS    0H
         MVC   RETCODE(2),=H'4'
         B     ENDPROG
ENDPROG  DS    0H
         LH    R8,RETCODE
         FREEMAIN R,LV=4096,A=(R11)   FREEMAIN THE PRIVATE STORAGE
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
         B     ENDPROG1
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8              PICK UP RETURN CODE.
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
***********************************************************************
* DATA AREA FOR OSCMDATH CONSTANTS.                                   *
*                                                                     *
***********************************************************************
EXTRACTC EXTRACT 0,'S',FIELDS=PSB
EXTRACTE EQU   *
*
*
***********************************************************************
* DSECT AREA FOR OSCMDATH                                             *
*                                                                     *
***********************************************************************
AUTHDATD DSECT
EXTRACTD EXTRACT 0,'S',FIELDS=PSB
AAUTH    DS    A                  ADDRESS OF AUTH BYTE PASSED TO US
PSCBADDR DS    A
RETCODE  DS    H                  RETURN CODE FROM OSCMDATH
AUTHBYTE DS    XL1                LOCAL COPY OF AUTHBYTE
         IKJPSCB
         END   OSCMDATH
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY OSCMDATH(''TSSO V 4.3.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    OSCMDATH'
 PUNCH ' NAME     OSCMDATH(R)'
         END
