TSSOINI3 CSECT
***********************************************************************
* THIS IS THE INITIALIZATION MODULE FOR THE TSSO RACF INTERFACE. THE  *
* PURPOSE OF THIS MODULE IS TO PERFORM A RACROUT CALL TO ALLOW TSSO   *
* TO ASSUME THE RACF ATTRIBUTES OF THE USERID SPECIFIED IN THE RACFID *
* PARAMETER.
*                                                                     *
* TSSO 4.3 SUPPORTS THE CONCEPT OF A GENERIC SECURITY DEFAULT USERID. *
* THIS SUPPORT WILL ALLOW A DEFAULT USERID IN RACF, ACF2 OR           *
* TOP SECRET, DEPENDING ON THE SECSYS PARAMETER IN THE TSSO INIT DECK *
*                                                                     *
*                                                                     *
***********************************************************************
         EQUATES
         STM   R14,R12,12(R13)    SAVE CALLERS REGSITERS
         LR    R12,R15            LOAD ENTRY POINT OF TSSO
         USING TSSOINI3,R12       GET ADDRESSIBILITY
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         LR    R8,R1              ADDRESS THE TSSO DATA AREA.
         GETMAIN R,LV=72           WE NEED STORAGE FOR THE SAVE AREA
         ST    R13,4(R1)          STORE ADDRESS OF CALLERS SAVE AREA
*                                 SAVE AREA
         ST    R1,8(R13)          REMEMBER WHERE THIS CORE IS
         LR    R13,R1             KEEP A COPY IN R13
*
         GETMAIN R,LV=1024
         LR    R11,R1
         USING INI3DATD,R11
*
         MVC   RACROUTD(RACROUTE-RACROUTC),RACROUTC  RACROUT MACRO
         LR    R9,R8
         USING DATD,R9
*
         L     R1,X'224'          GET ASCB ADDRESS
         USING ASCB,R1            ADDRESS THE ASCB
         L     R1,ASCBASXB        GET ASXB POINTER
         ST    R1,TSSOASXB        STORE FOR LATER
         CLC   SECSYS(4),=CL4'RACF'   ARE WE DEALING WITH RACF ?
         BE    SECRACF
         CLC   SECSYS(4),=CL4'ACF2'   HOW BOUT ACF2
         BE    SECACF2
         CLC   SECSYS(4),=CL4'TOPS'   TOP SECRET ?
         BE    SECTOPS                WE CAN USE RACF...
         B     ENDPROG                LOGIC ERROR - FORGET IT.
*
SECRACF  DS    0H
         L     R1,TSSOASXB
         USING ASXB,R1
         CLC   TSSORACF+1(8),=CL8' '  RACF ID WANTED ?
         BE    NORACF               NOPE...
         CLC   TSSORACF+1(8),=CL8'NONE' FOR THOSE WHO LIKE TO
*                                     SPECIFY PARMS THAT DO NOTHING
         BE    NORACF
         RACSTAT                  IS RACF ACTIVE ?
         LTR   R15,R15
         BNZ   RACFBAD
         L     R2,ASXBSENV
         ST    R2,TSSOSENV        REMEMBER ADDRESS OF ORIGINAL SEC.
*                                 ENVIRONMENT.
*
         ST    R2,ORIGACEE        REMEMBER ORIGINAL ACEE....
         LA    R2,NEWACEE
         DROP  R1                 (ASXB)
         RACROUTE  REQUEST=VERIFY,WORKA=(R11),                         X
               USERID=TSSORACF,MF=(E,RACROUTD),ACEE=(R2)
         LTR   R15,R15
         BZ    RACFOK
*
*
RACFBAD  DS    0H
         WTO   'TSSI014E ERROR ESTABLISING RACF ID- TSSO RUNNING WITH NX
               O RACF AUTHORITY'
         MODESET KEY=NZERO
         B     NORACF
RACFOK   DS    0H
         L     R2,TSSOASXB        GET ASXB AGAIN
         USING ASXB,R2
         MODESET KEY=ZERO
         MVC   ASXBUSER(7),TSSORACF+1
         MVC   ASXBSENV(4),NEWACEE       AND GET NEW ACEE...
         L     R2,ASXBSENV        GET ACEE ADDRESS
         ST    R2,TSSOACEE        AND REMEMBER IT (FOR DOCM).
         MODESET KEY=NZERO
         DROP  R2
         B     ENDPROG
NORACF   DS    0H
         B     ENDPROG
*
SECACF2  DS    0H
         WTO   'TSS3501S ACF2 SUPPORT IS NOT INCLUDED IN THIS VERSION OX
               F TSSO'
         B     ENDPROG
*
SECTOPS  DS    0H
         WTO   'TSS3501S TOP SECRET SUPPORT IS NOT INCLUDED IN THIS VERX
               SION OF TSSO'
         B     ENDPROG
*
ENDPROG  DS    0H
*
         FREEMAIN R,LV=1024,A=(R11)
*
         XR    R8,R8               PICK UP RETURN CODE
         LR    R1,R13              GET ADDRESS OF CORE IN R1
         L     R13,4(R13)          RESTORE R13
         FREEMAIN R,LV=72,A=(R1)    FREE UP THE CORE
ENDPROG1 DS    0H
         L     R14,12(R13)         RELOAD RETURN POINT
         LR    R15,R8              RELOAD RETURN CODE
         LM    R0,R12,20(R13)      RELOAD MORE REGISTERS
         BR    R14
***********************************************************************
* TSSOINI3 ONLY DATA CONSTANTS...                                     *
*                                                                     *
***********************************************************************
RACROUTC RACROUTE REQUEST=VERIFY,                                      X
               SMC=YES,PASSCHK=NO,ENVIR=CREATE,MF=L
RACROUTE DS    0H
*
INI3DATD DSECT
***********************************************************************
* TSSOINI3 ONLY DSECT.                                                *
*                                                                     *
***********************************************************************
INI3WORK DS    CL512
ORIGACEE DS    F                   ORIGINAL ACEE.
NEWACEE  DS    F                   ACEE RETURNED BY RACF.
RACROUTD RACROUTE REQUEST=VERIFY,                                      X
               SMC=YES,PASSCHK=NO,ENVIR=CREATE,MF=L
         COPY  TSSODATD
         IHAASCB
         IHAASXB
         IHAACEE
         END   TSSOINI3
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY TSSOINI3(''TSSO V 4.3.0  &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    TSSOINI3'
 PUNCH ' NAME     TSSOINI3(R)'
         END
