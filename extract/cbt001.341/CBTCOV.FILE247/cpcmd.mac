CPCMD    MENTER 12,EQU,CP=CP,COM=CPCMD
         B     EYE1
         COPY  EYECATCH
EYE1     DS    0H
         PRINT NOGEN
***********************************************************************
* THIS COMMAND IS USEFUL FOR ISSUING VM/CP COMMANDS FROM AN MVS       *
* MACHINE. THE COMMAND SYNTAX IS:
*
* CPCMD ANY-COMMAND
*
* WHEN RUN WITH THE TSSO SUBSYSTEM, THE OPERATOR HAS COMPLETE ACCESS
* TO CP FACILITIES. THIS COMMAND IS ALSO AVAILABLE TO TSO USERS.
***********************************************************************
* THIS COMMAND WAS WRITTEN BY CHERYL RAHN, BELLCORE AND ADAPTED TO TSO*
* AND THE TSSO SUBSYSTEM BY MARC SCHARE                               *
***********************************************************************
         TESTAUTH FCTN=1
         LTR   R15,R15
         BNZ   NOTAUTH1
         MODESET KEY=NZERO,MODE=SUP
         STIDP CPUID
         CLI   CPUID,X'FF'         ARE WE RUNNING UNDER VM?
         BE    GETSTOR             IF SO, PROGRAM CAN BE EXECUTED...
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE1H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
*
*
*
*        GET STORAGE FOR OUTPUT BUFFER
GETSTOR  DS    0H
         GETMAIN R,LV=4096,SP=228
         LR    R8,R1
         USING AREA,R8
**************************************************************
*
         MVC   REPLY(119),=CL119' '
         L     R2,CPPLCBUF         GET TSO COMMAND BUFFER
         LH    R3,2(R2)            GET OFFSET TO FIRST OPERAND
         LH    R4,0(R2)            GET TOTAL LENGTH OF BUFFER
         LA    R5,4(R3,R2)         R5 POINTS TO CP COMMAND
         LA    R3,0(R2,R4)         POINT TO END OF BUFFER.
         SR    R3,R5               R3 NOW HAS LENGTH OF OPERAND
         CH    R3,=H'1'
         BL    NOCOMM
         BCTR  R3,0
         EX    R3,MOVECMD
         OC    REPLY(119),=CL119' '
         TPUT  REPLY,119
*                                  R3 NOW HAS ADDR OF FIRST NONBLANK
*
*        ISSUE DIAGNOSE COMMAND TO TALK TO CP
*
*        R6   ADDR OF BEGINNING OF CP COMMAND   (RX)
*        R7   ADDR OF BUFFER FOR COMMAND RESPONSE
*        R10  LENGTH OF COMMAND FIELD   (RY)
*        R11  LENGTH OF RESPONSE BUFFER
*        R8   ADDRESS OF GETMAINED AREA (REPLY; RESPONSE)
*
*
         L     R11,LNGTHBUF
         L     R10,LNGTHREP
         O     R10,=XL4'40000000'
         LRA   R7,RESPONSE        ADDR OF RESPONSE BUFFER
         LRA   R6,REPLY           LOAD REAL ADDR (IN MVS'S VIEWPOINT)
*                                 OF BEGINNING OF CP COMMAND FIELD.
         SPACE 1
         DC    X'83',X'6A',XL2'0008'    ISSUE DIAGNOSE.
*
*        CHECK TO SEE IF CP COMMAND WAS SUCCESSFUL
         BNZ   TOOSMALL                 IS BUFFER TOO SMALL?
         LTR   R11,R11                  IS THERE A RESPONSE?
         BNZ   SUCCESS                  YES.  (MAY BE ERROR RESPONSE)
         LTR   R10,R10                  RETURN CODE IS IN REG 10
         BNZ   UNSUCC
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE4H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     FREESTOR
TOOSMALL DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE6H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         L     R11,LNGTHBUF             FULL SIZE OF RESPONSE BUFFER
*
*        R9    WILL CONTAIN START OF NEW LINE BEING EXAMINED
*        R4    INDEX REG--WILL CONTAIN ADDRESS IN LINE TO PRINT OUT
*        R2    INCREMENT REGISTER
*        R3    COMPAREND--FIRST BYTE PAST BUFFER DATA
*
SUCCESS  LA    R4,RESPONSE              ADDRESS OF RESPONSE BUFFER
         LA    R2,1                     INCREMENT
         LA    R3,0(R11,R4)             FIRST BYTE PAST DATA
         BCTR  R3,0                     LAST BYTE OF DATA (MINUS 1)
NEWLINE  LR    R9,R4                    SAVE START OF THIS LINE
SCANLOOP CLI   0(R4),X'15'              END-OF-LINE MARKER?
         BE    ENDLINE                  YES
         BXLE  R4,R2,SCANLOOP           EVERY LINE WILL END UNLESS
         B     FREESTOR                 RESPONSE WAS TOO LARGE FOR BUF
ENDLINE  LR    R5,R4                    COPY END
         SR    R5,R9                    COMPUTE LENGTH
         BCTR  R5,0                     SUBTRACT 1 FROM LENGTH (FOR EX)
         EX    R5,MOVEIN                MOVE DATA INTO PRINT
         LA    R5,5(R5)                 ADD 4+1 BYTES TO LENGTH OF LINE
         STH   R5,LINE5H                LENGTH OF MESSAGE
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE5H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         LA    R4,1(,R4)                POINT PAST END OF LINE
         CR    R4,R3                    ARE WE DONE WITH RESPONSE?
         BH    FREESTOR                 TERMINATE
         B     NEWLINE
MOVEIN   MVC   LINE5(0),0(R9)
MOVECMD  MVC   REPLY(0),0(R5)      ** EXECUTED TO MOVE COMMAND ***
FREESTOR DS    0H
         FREEMAIN R,LV=4096,A=(R8),SP=228
ENDPROG  DS    0H
         MLEAVE
*
*
*        ERROR CONDITIONS:
*
*        UNSUCCESSFUL--RETURN CODE FROM CP IS NOT ZERO
*
UNSUCC   CVD   R10,RETCP                CHANGE RETURN CODE TO EBCDIC
         UNPK  LINE3+30(4),RETCP         SO THAT IT CAN BE PRINTED IN
         OI    LINE3+33,X'F0'            MESSAGE TO CONSOLE.
WTOE     DS    0H                       CLEAR REGISTER 0
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE3H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     FREESTOR
NOTAUTH1 DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE2H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     ENDPROG
NOCOMM   DS    0H
         PUTLINE PARM=PUTBLOK,OUTPUT=(LINE7H,TERM,SINGLE,DATA),        X
               MF=(E,IOPLADS)
         B     FREESTOR
         SPACE 2
*        DATA AREA
CPUID    DS    1D
RETCP    DS    1D                       RETURN CODE FROM CP
LNGTHBUF DC    F'3976'                  SIZE OF OUTPUT BUFFER
LNGTHREP DC    F'119'                   SIZE OF OUTPUT BUFFER
CONS     DS    1C
TRTTABNB DC    64X'99'
         DC    X'00'                    TO BYPASS BLANK
         DC    191X'99'
LINE1H   DC    H'80',H'0'
LINE1    DC    CL80'CP COMMAND TERMINATED, YOU ARE NOT RUNNING UNDER VMX
               '
LINE2H   DC    H'80',H'0'
LINE2    DC    CL80'CP COMMAND TERMINATED, IT WAS NOT INVOKED AUTHORIZEX
               D'
LINE3H   DC    H'80',H'0'
LINE3    DC    CL80'CP COMMAND ERROR: RETURN CODE XXXX'
*
LINE4H   DC    H'80',H'0'
LINE4    DC    CL80'CP COMMAND COMPLETED WITH RETURN CODE 0'
*
LINE5H   DC    H'0',H'0'
LINE5    DC    CL120' '
*
LINE6H   DC    H'80',H'0'
LINE6    DC    CL80'CP COMMAND COMPLETED OK, BUT BUFFER TO SHORT'
*
LINE7H   DC    H'80',H'0'
LINE7    DC    CL80'INVALID COMMAND OR NO COMMAND SPECIFIED'
*
*
*
AREA     DSECT
REPLY    DS  CL119
         DS  0D
RESPONSE DS  CL3976
*
         IKJIOPL
         IEZJSCB
         END   CPCMD
 PUNCH ' SETCODE  AC(1)'
 PUNCH ' IDENTIFY CPCMD(''V 1.1 (TSSO V 4.3) &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    CPCMD'
 PUNCH ' NAME     CPCMD(R)'
         END
