TSSXWR00 TITLE 'CHRYSLER 328X WRITER'
***********************************************************************
*        THIS PROGRAM WAS DESIGNED AS PART OF A DSPRINT REPLACEMENT.
*        IT IS USED AS A STARTED TASK, AUTHORIZED,
*          AND HAS XWTR PROPERTIES.
*        MY IMPLEMENTATION WAS TO LKED IT TO AN AUTHORIZED LIBRARY,
*          AS IASXWR00(TO PICK UP XWTR PROPERTIES).
*        ATTRIBUTES: RENT,REUS,REFR.
*        RESTRICTION: ONLY LOCAL 328XS SUPPORTED.
*                     4 CHAR FORM NUMBERS
*                     ONLY SKIP TO CHAN. 1 SUPPORTED.
*                     SOME CHRYSLER DEPENDANT CODE (F /CHRYSLER/)
*                                       TSO FIND UNDER EDIT.
*        JCL:
*          //IEFPROC  EXEC  PGM=IASXWR00,REGION=64K,DPRTY=14
*          //L3284    DD    DDNAME=IEFRDER
*          //IEFRDER  DD    UNIT=FFF
*          //STEPLIB  DD    DSN=AUTHORIZED.LIBRARY.............
*          //* VOICE-OF-EXPERIENCE REAL XWTR WILL OPEN 1ST DD AS OUTPUT
*          //*   SO IF ABOVE STEPLIB DOESN'T HAVE IASXWR00 IN IT,
*          //*   AND STEPLIB IS FIRST,
*          //*   THE REAL XWTR WILL OPEN DIRECTORY AS OUTPUT.
*          //*   DOES AN OUTSTANDING JOB OF DESTROYING PDSS.
*          //*   DON'T APAR IT, DOC SAYS IEFRDER MUST BE FIRST.
*          //*   THATS WHAT YOU GET WITH A REL 17 WTR IN MVS!
*
*        STARTING:
*          S TSOWTR.P1,05F,(F=9999,C=123,D=DEST)
*             DEFAULT:      F=9DMY,C=Z,D=RMT1
*
*        MODIFYING:
*          F P1,F=9990             CHANGE FORM ONLY
*          F P1,C=1A               CHANGE CLASS(ES) ONLY
*          F P1,D=LOCAL            CHANGE DESTINATION ONLY
*          F P1,C=1A,F=9990        CHANGE BOTH
*          F P1,FLUSH              PURGE CURRENT JOB AND CONTINUE
*          F P1,FLUSH,PGMR00       SAME EXCEPT REQUEST FROM USER(PGMR00
*                                  MODIFY IS ISSUED FROM TSO CP.
*          F P1,STOP               TERM. AFTER FINISHING CURR. DATASET.
*        STOPPING:
*          P P1                    RE-QUEUE CURRENT JOB AND TERM. IMM.
*
*        WHY NOT DSPRINT?
*          NO ACCOUNTING (SMF CURRENTLY BEING ADD TO THIS PGM).
*          MOST TCAM FAILURES DUE TO DSPRINT CODE.
*          TSO USERS CAN BRING TCAM DOWN WITH TOO MUCH DATA.
*          DATA SENSITIVE CODE IN TCAM MSG HANDLER.
*          TCAM DISK REORG GETS OUTPUT OUT OF STEP.
*          TCAM WARM START LOOSES ALL OUTPUT.
*          SEPERATE QUEUE IN TCAM FOR OUTPUT VICE USING SPOOL.
*          WANT TO BE ABLE TO GET A LITTLE JCL ERROR PRINTED ON 3284.
*          I DONT LIKE TCAM - ONLY ONE IN SYSTEM, DIFFICULT TO TEST.
*          DSPRINT NOT SUPPORTED VERY WELL.
*          RINKY-DINKY METHOD OF PURGING PRINTER OUTPUT(OFF/ON)
*          IF A PRINTER GOES DOWN, NO ALTERNATE DEST, CANT USE 3211.
*          CAN'T GET A TSO SYSOUT D.S. PRINTED ON 3284.
*          IF YOU WANT MORE REASONS CALL ME:
*
*        CLARK HUNTER     PHONE (313-497-1342/0524/0533)
*        CHRYSLER SALES AND PARTS SERVICE DIVISION.
*        26311 LAWRENCE AVE.
*        CENTERLINE MICHIGAN 48015
*         CIMS 423-10-17
***********************************************************************
         TITLE 'START-UP'
TSSXWR00 CSECT
         PRIME ID=*,LV=TSDSASIZ
         USING DSA,R13
         L     R2,0(R1)            GET PARM
*
**       GET INTO SUPERVISOR STATE
*
         TESTAUTH FCTN=1           AM I AUTHORIZED
         LTR   R15,R15
         BNZ   TSWINIT             NO-ASSUME TEST MODE
         MODESET MODE=SUP
*
**       MOVE MODEL DATA TO GOTTEN AREA, ZERO REST.
*
TSWINIT  DS    0H
         LA    R0,TSDDECB          START OF DYNAMIC AREA
         LA    R1,(TSDCLRE-TSDDECB) LENGTH OF DYNAMIC AREA
         LA    R14,TSSWA           START OF STATIC DATA
         LA    R15,TSSMDL          LENGTH OF MODEL DATA
         MVCL  R0,R14              MOVE W/ZERO TO BE RENT,REUS
         XC    @TSCVDAT,@TSCVDAT   SHOW NO DATE ROUTINE
         LA    R1,TSDBEGJT         AREA TO PUT DATE/TIME
         ST    R1,@TSCVDPR         SET PARM POINTER
         MVI   @TSCVDPR,X'80'      ONLY ONE PARM TO TSCVDATE
         LOAD  EP=TSCVDATE,ERRET=TSCVNO
         ST    R0,@TSCVDAT         SAVE EP ADDR
TSCVNO   DS    0H                  ZEROS IN EP AREA WILL SKIP CALL
*
         LA    R1,TSDBUFR+1        GET START OF BUFFER
         ST    R1,TSDBFPTR         SAVE IT
         MVI   TSDBUFR,X'C8'       SET WCC = NL CHARS DETERMINE LINE L'
*
**       PROCESS PARM IF ANY
*
         CLC   =H'0',0(R2)         ANY PARM
         BZ    TSNOPARM            NO PARM
         LH    R15,0(R2)           MOVE PARM TO WORK AREA
         LA    R15,2(R15)          +2 TO INCLUDE LENGTH BYTES
         LA    R14,0(R2)           FROM ADDR
         LA    R0,TSDTXT           TO ADDR TEXT WORK AREA
         LA    R1,L'TSDTXT         MAX L'
         ICM   R15,8,=C' '         FILL CHAR IS SPACES
         MVCL  R0,R14              PUT TEXT IN WORK
         BAL   R14,TSCMFMSU        PARSE START PARMS
         B     TSNOPARM            B    0(,R14)  NO ERRORS
         WTO   'TSOWTR-BAD PARM',ROUTCDE=(8)
         ABEND 111
TSNOPARM DS    0H
*
**       PUT TASK ID IN MESSAGES
*
         L     R1,PSATNEW-PSA      NEW TCB
         L     R1,TCBTIO-TCB(R1)   TIOT
         MVC   TSDBFCL,8(R1)
         MVC   TSDWWC,8(R1)
*
**       LOCATE STOP/MODIFY ECB
*
         EXTRACT TSDCOMMP,MF=(E,TSDXTRC)
         L     R1,TSDCOMMP         GET PTR TO COMM/CIB ORG WORDS
         MVC   TSDCOMM,0(R1)       COPY ADDR OF ECB
         OI    TSDCOMM,X'80'       INDICATE COMM ECB IS LAST IN LIST
         LA    R2,4(R1)            POINT TO CIB ORIGIN
         QEDIT ORIGIN=(R2),CIBCTR=2 ALLOW 2 MODIFIES
*
**       OPEN OUT DCB - BTAM 328X
*
         MVI   TSDOPENX,X'80'      ONLY ONE IN LIST
         LA    R2,TSDDCB
         OPEN  ((R2),OUTPUT),MF=(E,TSDOPENX)
         L     R1,DCBDEBAD-IHADCB+TSDDCB DEB
         XR    R2,R2
         ICM   R2,7,DEBSUCBB-DEBBASIC(R1) UCB
         MVC   TSDBFUCB,UCBNAME-UCBOB(R2) EBCDIC ADDR
***                                                                 ***
*        INITIALIZE DYNAMIC ALLOCATION PARAMETER AREA                 *
***                                                                 ***
         LA    R8,TSDS99
         LR    R1,R8                 ADDRESS OF PARALLOC
         LA    R8,4(R8)            INCREMENT TO DAL REQUEST BLOCK
         ST    R8,0(R1)            SET UP RB PTR ADDR.
         OI    0(R1),S99RBPND
         USING S99RB,R8
         XC    S99RB(DYNRBSIZ),S99RB
         MVI   S99RBLN,DYNRBSIZ    SIZE OF RB
         LA    R3,DYNRBSIZ(R8)     INCREMENT TO FIRST TEXT POINTER
         USING DYNTXTPT,R3
         LA    R4,TXTPTSIZ(R3)     INCREMENT TO TEXT UNITS
         USING S99TUNIT,R4
*        DSNAME TEXT UNIT
         ST    R4,DSNTXTPT         SET UP ADDR OF FIRST TEXT UNIT
         XC    S99TUKEY(DYNEND-DYNTXTND),S99TUKEY ZERO TEXT UNITS
         MVI   S99TUKEY+1,DALDSNAM DSNAME IS SUPPLIED TO SVC99
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,44       SIZE OF DSNAME
         LA    R4,L'DSNTXTUT(R4)   INCREMENT TO SSREQ TEXT UNIT
*        SSREQ TEXT UNIT
         ST    R4,SSRTXTPT         ADDRESS OF SSREQ TEXT UNIT
         MVI   S99TUKEY+1,DALSSREQ THIS IS A REQUEST FOR SUBSYSTEM
         MVI   S99TULNG+1,4        LENGTH OF PARM FIELD
         L     R2,PSATNEW-PSA      TCB NEW
         L     R2,TCBJSCB-TCB(R2)  JSCB
         L     R2,JSCBSSIB-IEZJSCB(R2) SSIB
         USING SSIB,R2
         MVC   S99TUPAR(4),SSIBSSNM PRIMARY SUBSYSTEM NAME
         DROP  R2                  UNUSE SSIB
         MVI   S99TUNUM+1,X'01'    NUMBER OF REQUESTS
         LA    R4,L'SSRTXTUT(R4)   INCREMENT TO DDN TEXT UNIT
*        DDNAME RETURN TEXT UNIT
         ST    R4,DDNTXTPT         ADDR OF DDNAME RETURNED TEXT UNIT
         MVI   DDNTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DALRTDDN RETURN DDNAME TO ME
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         MVC   S99TUPAR(8),BLANKS  INITIALIZE TO BLANKS
         LA    R4,L'DDNTXTUT(R4)   INCREMENT TO DDNAME UNALLOC TEXT
*        DDNAME UNALLOCATE TEXT UNIT
         ST    R4,UDNTXTPT         SAVE UNALLOC TEXT ADDRESS
         MVI   S99TUKEY+1,DUNDDNAM USE DDNAME TO UNALLOCATE
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         MVI   S99TULNG+1,8        LENGTH OF PARM FIELD
         LA    R4,L'UDNTXTUT(R4)   INCREMENT TO OVERRIDE TEXT
*        DISPOSITION OVERRIDE TEXT UNIT
         ST    R4,UORTXTPT         SAVE ADDRESS OF OVERRIDE TEXT
         MVI   UORTXTPT,S99TUPLN   LAST TEXT UNIT PTR. IN LIST
         MVI   S99TUKEY+1,DUNOVDSP OVERRIDE DISPOSITION KEY
         MVI   S99TULNG+1,1        LENGTH OF PARM FIELD
         MVI   S99TUNUM+1,X'01'    NUMBER OF ENTRIES
         DROP  R4
***                                                                 ***
*   SET UP SSOB TO ISSUE IEFSSREQ TO ACCESS A DATA SET                *
***                                                                 ***
SD82SSOB EQU   *
         LA    R1,TSDSSOB
         LA    R7,4(R1)            INCREMENT TO SSOB ITSELF
         USING SSOB,R7
         ST    R7,0(R1)            SET UP ADDR OF ADDR OF SSOB IN R1
         OI    0(R1),X'80'
         XC    SSOBEGIN(SSOBHSIZ),SSOBEGIN   CLEAR SSOB HEADER
         LA    R2,SSOBGN           INITIALIZE SSOB
         XC    0(SSSOSIZE,R2),0(R2)     CLEAR SYSOUT EXTENSION
         MVC   SSOB(WTRSOSIZ),WTRSSOB   INITIALIZE HEADER
         ST    R2,SSOBINDV         ADDRESS OF SYSOUT EXTENSION
         LA    R4,SSSOSIZE         SYSOUT EXTENSION SIZE
         STH   R4,0(R2)            SET UP SIZE OF SYSOUT EXTENSION
         MVC   SSSOPGMN,BLANKS     INITIALIZE WRITER NAME TO BLANKS
         OI    SSSOFLG1,SSSODST    DESTINATION IS SELECT CRITERION
         OI    SSSOFLG1,SSSOSFRM   " FORMS
         OI    SSSOFLG1,SSSOSCLS   " CLASS LIST
*
**             SEND OUT SALUTATION  MESSAGE TO PRINTER (SEE IF UP)
*
         MVI   TSDINIT,C'Y'        SET INIT SWITCH
         LA    R1,TSDHELLO         START-UP MESSAGE
         LA    R0,L'TSDHELLO
         MVI   TSDTRUNC,C'Y'       FORCE WRITE (PARTIAL BUFFER)
         MVI   TSDCONCH,X'89'      SKIP TO CH1 AFTER
         BAL   R14,TSWLOCS         GOTO BTAM WRITER
         B     TSGEOD1             GO THRU PART OF EODAD RTN (ERRCK)
TSINITRT DS    0H                  RETURN FROM ERROR CHECK AT EODAD
         MVI   TSDINIT,C'N'        TURN OFF START-UP MSG
         TESTAUTH FCTN=1           AM I AUTHORIZED
         LTR   R15,R15
         BNZ   TERME               NO-GET OUT
         TITLE 'ASK JES FOR WORK'
SD82HASP EQU *
         MVI   TSDFLUSH,0          RESET FLUSH OPTION
         CLI   TSDSTOPA,C'Y'       CLOSE DOWN
         BE    SD82HALT            YES
         MVC   SSSOCLSL,TSDHCLAS   MOVE CLASS LIST
         MVC   SSSOFORM,TSDHFORM   MOVE FORM #
         MVC   SSSODEST(8),TSDHDEST MOVE DESTINATION
         LA    R1,TSDSSOB
         IEFSSREQ                  ASK HASP FOR WORK
         LTR   R15,R15             TEST RETURN CODE
         BZ    SD82RTRN            NORMAL RETURN
         MVC   TSDWERR1,=CL20'BAD  JES RETURN CODE'
         B     TSFATALF            GIVE R15
SD82RTRN EQU   *
         C     R15,SSOBRETN
         BE    SD82DAL             ZERO = WORK DELIVERED
         LA    R2,4                COMPARE RETURN TO 4
         C     R2,SSOBRETN         ARE THERE ANY MORE DATA SETS.
         BE    SD82WAIT            NO, WAIT FOR WORK
         MVC   TSDWERR1,=CL20'BAD  SSOB RETURN CODE'
         B     TSFATALF            GIVE R15
         TITLE 'DYNAMICALLY ALLOCATE DATA SET'
SD82DAL  EQU   *                   DYNAMICALLY ALLOCATE DATA SET
***                                                                 ***
*    SET UP TO DYNAMICALLY ALLOCATE DATA SET                          *
***                                                                 ***
         MVC   TSDJOBN,SSSOJOBN    SAVE JOBNAME
         MVC   TSDJOBI,SSSOJOBI    JOBNUMBER
         MVC   TSDHFORM,SSSOFORM   FORM #
         MVC   TSDCLAS,SSSOCLAS    CLASS
         MVC   TSDDSN,SSSODSN      DSN
         CLC   =C'PGM',TSDJOBN     TSO ID PREFEX ***CHRYSLER DEPENDANT*
         BNE   TS82NNEW            NO - NO NOTIFY
         CLI   TSDSEP,C'Y'         PAGE SEPERATORS?
         BNE   TS82NNEW            NO-MUST BE SPECIAL WRITER-NO NOTIFY
         LA    R0,L'TSDBFR         LENGTH
         LA    R1,TSDBFR           ADDR OF MSG
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=TSDJOBN
TS82NNEW DS    0H
         MVI   S99VERB,S99VRBAL    ALLOCATION VERB
         LA    R3,DISPDSN(R8)      ADDR. DSNAME TEXT UNIT
         ST    R3,S99TXTPP         STORE ADDR OF TEXT UNIT PTRS
         L     R4,0(,R3)           ADDR OF FIRST TEXT UNIT
         MVC   6(44,R4),SSSODSN    GET DATA SET NAME FROM SSOB
         BAL   R11,DYNAL           DYNAMICALLY ALLOCATE DATA SET
***                                                                 ***
*   INITIALIZE INPUT DCB                                              *
***                                                                 ***
*        REFRESH INPUT DCB
         MVC   TSDDCBI(TSSDCBIE-TSSDCBI),TSSDCBI
         L     R3,S99TXTPP         ADDRESS OF TEXT UNITS
         L     R4,DDNTXTPT-DYNTXTPT(R3) ADDRESS OF DDNAME TEXT UNIT
         MVC   DCBDDNAM-IHADCB+TSDDCBIS(8),6(R4) PUT DDNAME IN DCB
         LA    R15,TSDDCBI         GET INPUT DCB ADDR
         OPEN  ((R15),INPUT),MF=(E,TSDOPENX)
         MVI   TSDEOF,C'N'         RESET END-OF-FILE FLAG
         CLI   TSDSEP,C'Y'         PAGE SEPERATORS DESIRED?
         BNE   TSGNSEP             NO-SKIP IT
         MVC   TSDBEGJ,TSDDSN      PRINT DSN ON TOP
         MVC   TSDBEGJN,TSDJOBN    PRINT JOBNAME ALSO
         ICM   R15,15,@TSCVDAT     IS DATE/TIME ROUTINE THERE?
         BZ    TSGNDATE            NO-SKIP CALL
         LA    R1,@TSCVDPR         GET PARM AREA
         BALR  R14,R15             GET NICE PRINTABLE DATE/TIME
TSGNDATE DS    0H                  NO PRINT TODAY
         LA    R1,TSDBEG
         LA    R0,L'TSDBEG
         MVI   TSDCONCH,X'09'       1SP AFTER
         BAL   R14,TSWLOCS         PRINT HEADER
TSGNSEP  DS    0H                  SKIP DATASET HEADER
         MVI   TSDNEWDS,C'Y'       SET FLAG TO SUPPRESS 1ST SKIP
         B     TSGCKSM             CHECK FOR STOP/MODIFY
         TITLE  'LOOP GETTING/WRITING DATA SET UNTIL END'
TSGLOOP  DS    0H
         GET   TSDDCBI
*
**       GET CARRAGE, LINE LEN., AND LOCATION FOR WRITER.
*
         LH    R0,DCBLRECL-IHADCB+TSDDCBI GET DEF. LRECL
         TM    DCBRECFM-IHADCB+TSDDCBI,DCBRECU UNDEFINED
         BO    TSGNOTV             YES
         TM    DCBRECFM-IHADCB+TSDDCBI,DCBRECV VARIABLE
         BNO   TSGNOTV             NO
         LH    R0,0(R1)            GET REC LEN
         LA    R1,4(R1)            SKIP RDW
         SH    R0,=H'4'            SKIP RDW LEN.
TSGNOTV  DS    0H
         TM    DCBRECFM-IHADCB+TSDDCBI,X'02' MACH. CARRIAGE
         BNO   TSFRC1SP            NO-TRY ASCII
         MVC   TSDCONCH,0(R1)
TSADJREG DS    0H
         LA    R1,1(R1)            SKIP CC
         BCTR  R0,0                SKIP CC LEN.
         B     TSPUTIT             SETUP FOR WRITER
TSFRC1SP DS    0H
         MVI   TSDCONCH,X'09'      1SP AFTER
         TM    DCBRECFM-IHADCB+TSDDCBI,X'04' ASCII
         BNO   TSPUTIT             NO CARRIAGE
*
**       BREAK ASCII COMMAND DOWN INTO IMM. AND PRINT
*
         STM   R0,R1,TSDSAV01      SAVE STRT, LEN
         LA    R14,=X'F00B6013F18B'  ASCII XLATE TBL
         LA    R15,3          L' OF TABLE(3 PAIRS)
***********************************************************************
*        IDEA IS TO PRINT ALL W/ 1SP AFTER.
*        NOTE NO SUPPORT FOR '+' - OVERPRINT.
*        SO '0' = (SP 1 IMM)+(PR W/ 1 SP AFTER)
*           '-' = (SP 2 IMM)+(PR W/ 1 SP AFTER)
*           '1' = (SK CH 1 IMM)+(PR W/ 1 SP AFTER)
*           ' ' OR '+' = (PR W/ 1 SP AFTER)
***********************************************************************
TSFRLOC  DS    0H
         CLC   0(1,R14),0(R1)
         BE    TSFRFND             FOUND ASCII ARG. CHAR
         LA    R14,2(R14)          NEXT PAIR
         BCT   R15,TSFRLOC
         B     TSADJREG            UNKNOWN - FORCE 1 SP
TSFRFND  DS    0H
         MVC   TSDCONCH,1(R14)     MOVE IMMEDIATE PART OF ASCII
         XR    R0,R0               ZERO LENGTH
         BAL   R14,TSWLOCS         PUT IMMEDIATE PART
         LM    R0,R1,TSDSAV01      RESTORE REGS
         MVI   TSDCONCH,X'09'      1SP AFTER
         B     TSADJREG            ADJ REGS AROUND CARRIAGE
*
**       NORMAL PUT INTERFACE
*
TSPUTIT  DS    0H
         BAL   R14,TSWLOCS
         B     TSGCKSM             CK STOP/MODIFY
TSGCALLW DS    0H                  RE-CALL WTR - USE PREV. R0/R1
         BAL   R14,TSWLOC          GO TO BTAM WRITER
*
TSGCKSM  DS    0H
         BAL   R11,TSCMCK          CHECK FOR STOP/MODIFY
         B     TSGSTOP             STOP ISSUED
         B     TSGET               NORMAL RETURN
TSGSTOP  DS    0H
         MVI   TSDKEEP,C'Y'        KEEP CURRENT D.S.
         MVI   TSDSTOP,C'Y'        STOP AFTER UNALLOC/KEEP
         B     TSGEOD              SIM. EODAD
TSGET    DS    0H                  GET NEXT RECORD
         CLI   TSDSTOP,C'Y'        WAS STOP ISSUED DURING BTAM WRITE
         BE    TSGEOD              YES-SIM. EODAD
         CLI   TSDFLUSH,C'Y'       FLUSH AND PURGE CURR. D.S.
         BNE   TSCKIOER            NO FLUSH - CK FOR I/O ERR
TSSYNFL  DS    0H                                               9041
         MVI   TSDFLUSH,0          RESET
         LA    R1,TSDFLSMS         GET FLUSH MSG
         LA    R0,L'TSDFLSMS
         MVI   TSDCONCH,X'89'      CH1 AFTER
         MVI   TSDTRUNC,C'Y'       FORCE PHYSICAL WRITE(TRUNC)
         BAL   R14,TSWLOCS
         B     TSGECLS             CLOSE OUT
*
**       SYNAD  ROUTINE ON JES INPUT D.S.
*
TSSYN    DS    0H                                               9041
         MVC   TSDBYWHO,=CL8'IO ERROR'                          9041
         WTO   'TSOWTR - IO ERROR',ROUTCDE=(8)                  9041
         B     TSSYNFL             FLUSH DATASET                9041
*
**       CHECK FOR I/O ERROR
*
TSCKIOER DS    0H
         CLI   TSDIOERR,C'Y'             IO ERR
         BNE   TSGRSET             NO - GET NEXT
         LH    R1,TSDRETLM         GET CURRENT RETRY LIMIT
         LA    R1,1(R1)            BUMP
         STH   R1,TSDRETLM
         TM    TSDRETLM+1,X'07'    IS IT A MULTIPLE OF 8
         BNE   TSWAIT              NO - NO NOTIFY OF OPER
         MVC   TSDWWA,TSDBFUCB     MOVE UCB NAME
         LA    R1,TSDWWT1          WTO LIST
         SVC   35
TSWAIT   DS    0H
         STIMER WAIT,BINTVL==F'3000' WAIT A WHILE (SEC * 100)
         MVI   TSDIOERR,0
         B     TSGCALLW            RE-TRY WRITE AGAIN
TSGRSET  DS    0H
         CLI   TSDEOF,C'Y'         AT EOF?
         BE    TSGEOD1             YES-RETURN TO CLOSE CODE
         B     TSGLOOP
         TITLE  'END OF DATA SET'
TSGEOD   DS    0H
         MVI   TSDTRUNC,C'Y'       FORCE PHYSICAL WRITE
         MVI   TSDCONCH,X'8B'      SKIP CH1 IMM.
         LA    R1,TSDCONCH         DUMMY START ADDR
         XR    R0,R0               LEN. = ZERO
         BAL   R14,TSWLOCS         SKIP
TSGEOD1  DS    0H                  SECONDARY ENTRY POINT
         MVI   TSDEOF,C'Y'         SET END OF FILE
         CLI   TSDSTOP,C'Y'        STOPPING?
         BE    TSGEOD2             YES-DONT CHECK FOR I/O ERROR
         CLI   TSDIOERR,C'Y'       I/O ERROR ON FLUSH
         BE    TSCKIOER            YES-GOTO ERROR RECOVERY CODE
TSGEOD2  DS    0H                  SECONDARY ENTRY POINT
         CLI   TSDINIT,C'Y'        IN STARTUP PROCESSING
         BNE   TSGECLS             NO-CONTINUE
         CLI   TSDSTOP,C'Y'        WAS STOP ISSUED DURING START UP
         BNE   TSINITRT            NO-INIT RETURN
         B     TERME               YES-EXIT
TSGECLS  DS    0H
*
**       CLOSE INPUT DCB AND FREE BUFFER POOL
*
         LA    R15,TSDDCBI
         CLOSE ((R15),DISP),MF=(E,TSDOPENX)
         FREEPOOL TSDDCBI          FREE BUFFER POOL
*
*        UNALLOCATE INPUT D.S.
*
         MVI   S99VERB,S99VRBUN    UNALLOCATION VERB
         DROP  R3
         LA    R3,DISPUAL(R8)      INCREMENT TO UNAL
         ST    R3,S99TXTPP         STORE ADDR. OF TEXT UNIT PTRS.
         L     R3,0(R3)            ADDRESS OF DDNAME FOR UNALLOCATE
         L     R4,DISPDDN(R8)      ADDRESS OF DDNAME UNIT
         MVC   6(8,R3),6(R4)       PUT DDNAME INTO TEXT FOR UNALLOC
         L     R4,DISPOVR(R8)      ADDRESS OF OVERIDE UNIT
         CLI   TSDKEEP,C'Y'        IS INPUT TO BE KEPT
         BE    KEEPDS              YES
         MVI   6(R4),X'04'         DELETE DATA SET
         B     DYNUN               UNALLOCATE WITH DELETE DISP
KEEPDS   MVI   6(R4),X'08'         KEEP DATA SET
DYNUN    BAL   R11,DYNAL           DYNAMICALLY UNALLOCATE DATA SET
         MVI   TSDKEEP,0           RESET KEEP OPTION
         CLI   TSDSTOP,C'Y'        STOPPING
         BE    SD82HALT            YES
         B     SD82HASP            GO GET MORE WORK
         TITLE  'W A I T  F O R   W O R K  O R  S T O P  W R I T E R '
SD82WAIT EQU   *
         MVC   TSDWECB,SSSOWTRC    SAVE ADDR. OF HASP ECB
         LA    R1,TSDWECB          ADDR. OF WAIT LIST
WAIT     EQU   *
         WAIT  1,ECBLIST=(1)       WAIT FOR WORK OR COMMAND ECB
         MVI   SSSODSN,X'00'       INDICATE THIS IS AN INITIAL REQUEST
         BAL   R11,TSCMCK          SEE IF STOP/MODIFY ISSUED
         B     SD82HALT            HALT
         B     SD82HASP            GO TRY TO GET MORE WORK FROM HASP
***                                                                 ***
*        STOP THE WRITER                                              *
***                                                                 ***
SD82HALT OI    SSSOFLG2,SSSOCTRL   INDICATING STOPPING
         LA    R1,TSDSSOB          SSOB ADDR
         IEFSSREQ                  TELL HASP WTR IS QUITTING
         XR    R15,R15
*        RETURN TO CALLER
         MODESET MODE=PROB
TERME    DS   0H
         TERME
         TITLE  'B T A M  W R I T E R'
TSWLOCS  DS   0H
         ST    R0,TSDISIZ          L'
         ST    R1,TSDILOC          ADDR
TSWLOC   DS    0H
         ST    R14,TSDWRET         SAVE RETURN
*
**       SUPRESS SKIP IF FIRST FOR D.S.
*
         CLI   TSDNEWDS,C'Y'
         BNE   TSWNNEW             NOT A NEW D.S.
         MVI   TSDNEWDS,C'N'       RESET NEW FLAG
         CLI   TSDCONCH,X'8B'      IS 1ST CMD A SKIP CH1?
         BE    TSWRET              YES-SUPRESS IT-HEADER ALREADY AT CH1
         CLI   TSDCONCH,X'89'      CH 1 AFTER
         BNE   TSWNNEW             NO-NORMAL
         MVI   TSDCONCH,X'09'      MAKE 1 SP AFTER
TSWNNEW  DS    0H                  NO SUPRESSION
**       XLATE 328X CTRL/NON-PRINTS OUT
         LM    R0,R1,TSDILOC       GET ADDR,LENGTH OF LINE
         LM    R14,R15,TSDILOC     "
         SH    R15,=H'1'           -1 FOR EXECUTE INSTR
         BM    TSWNOTR             LEN = ZERO - NO XLATE
         EX    R15,TSSTR           XLATE 3270 CTRL CHARS OUT
TSWNOTR  DS    0H
*        MOVE PRINTLINE TO BUFFER
         C     R1,TSDWID           MAX WIDTH (LENGTH OF LINE)
         BL    *+8                 OK
         L     R1,TSDWID           FORCE DOWN TO MAX LENGTH
         L     R14,TSDBFPTR        GET OUTPUT BUFFER NXT AVAL.*
         OC    TSDRETLM,TSDRETLM   IO ERR RETRY?
         BNZ   TSWRETRY            YES-GO TO WRITE
         TM    TSDCONCH,X'02'      IMMEDIATE COMMAND (SPACE ONLY)
         BO    TSWIMM              YES - DONT MOVE DATA
         LR    R15,R1              NOT IMM. MOVE DATA TO BUFFER
         MVCL  R14,R0              R14 WILL POINT AFTER LAST BYTE
TSBKSCAN DS    0H
         BCTR  R14,0               BACK UP ONE BYTE
         CLI   0(R14),C' '         IS IT BLANK
         BE    TSBKSCAN            YES-BACK UP ANOTHER
*              CANT BACK UP OUT OF  BUFFER SINCE WCC IS FIRST BYTE
         LA    R14,1(R14)          R14 POINTS TO NEXT AVALIABLE BYTE
TSWIMM   DS    0H                  IMMEDIATE DATA
         TM    TSDCONCH,X'C0'      SKIP COMMAND
         BNZ   TSSKIP              YES-CALC # LINE FEEDS TO HEAD
TSWSPAC  DS    0H                  CHANGE SPACING TO NL(X'15')S
         IC    R1,TSDCONCH         GET CARRIAGE
         N     R1,=X'00000018'     MASK OFF JUNK
         BNZ   TSADDSRL            PUT OUT NLS
         LA    R1,1*8              FORCE AT LEAST 1 SP
TSADDSRL DS    0H
         SRL   R1,3                SHIFT TO GET SPACING
TSADDLN  DS    0H                  LOOP ADDING NLS TO BUFFER
         L     R0,TSDCURPO         GET CURRENT LINE #
         AR    R0,R1               ADD NEW SPACING
         ST    R0,TSDCURPO         STORE IT
TSADDNL  DS    0H
         MVI   0(R14),X'15'        NEW LINE CHAR
         LA    R14,1(R14)          NEW OUTPUT ADDR
         BCT   R1,TSADDNL          LOOP
TSADDEM  DS    0H
*
**       SEE IF BUFFER FULL YET
*
         ST    R14,TSDBFPTR        SAVE CURR POS.
         CLI   TSDTRUNC,C'Y'       FORCE WRITE
         BE    TSWFRC              YES
         LA    R0,TSDBUFR+1700
         CR    R14,R0              NEARLY FULL?
         BL    TSWRET              NO-GET NEXT
TSWFRC   DS    0H
TSWRETRY DS    0H
         MVI   0(R14),X'19'        PUT EOM IN
         LA    R14,1(R14)          NEXT BYTE
         LA    R4,TSDBUFR          GET START
         SR    R14,R4              LENGTH
         LA    R2,TSDDCB           OUTPUT DCB
         LA    R3,TSDDECB          DECB
         L     R1,DCBDEBAD-IHADCB+TSDDCB DEB
         ICM   R1,7,DEBSUCBB-DEBBASIC(R1) UCB
         MVI   TSDDECB,X'40'       SET UP A BAD BTAM COMP CODE
         CLI   UCBGRAF-UCBOB(R1),UCBBTAM+UCBDWNR NOT READY IN OPEN?
         BZ    TSBTAMXX            YES-HANDLE LIKE I/O ERROR
         WRITE (R3),TS,(R2),(R4),(R14),,1,MF=E
         LTR   R15,R15
         BNZ   TSBTAMER
TSBTAMXX DS    0H
         LA    R1,TSDDECB          GET DECB
         ST    R1,TSDWECB          STORE IN WAIT LIST
TSWWAIT  DS    0H                  WAIT FOR BTAM OR MODIFY
         LA    R1,TSDWECB          GET WAIT LIST
         WAIT  1,ECBLIST=(1)
         BAL   R11,TSCMCK          PROCESS MODIFY
         B     TSWHALT             STOP ISSUED
         TM    TSDDECB,X'40'       BTAM COMPLETE?
         BO    TSWCKCMP            YES-CHECK COMPLETION
         B     TSWWAIT             WAIT FOR BTAM
TSWHALT  DS    0H
         MVI   TSDKEEP,C'Y'        KEEP CURR. D.S.
         MVI   TSDSTOP,C'Y'        SHUT DOWN AFTER UNALLOCATE
         WAIT ECB=TSDDECB          WAIT FOR BTAM
TSWCKCMP DS    0H
         CLI   TSDDECB,X'7F'       GOOD IO
         BNE   TSRETRY             NO - SETUP FOR RETRY LATER
         MVI   TSDTRUNC,C'N'       RESET FORCED WRITE
         LA    R14,TSDBUFR+1
         ST    R14,TSDBFPTR        RESET PTR
         XC    TSDRETLM,TSDRETLM   YES - RESET RETRY COUNTER
TSWRET   DS    0H                  RETURN
         L     R14,TSDWRET         "
         BR    R14                 "
TSSKIP   DS    0H                  HANDLE SKIP COMMAND
         CLI   TSDCONCH,X'89'      SKIP CH 1 AFTER
         BE    TSSKIP1             YES
         CLI   TSDCONCH,X'8B'      SKIP CH 1 IMM.
         BE    TSSKIP1             YES
         B     TSWSPAC             FORCE TO SPACING COMMAND
TSSKIP1  DS    0H                  CALC # OF NLS TO TOP OF FORM
         L     R1,TSDCURPO         GET CURRENT POS.
         XR    R0,R0               CLEAR
         D     R0,TSDLEN           DIVIDE BY LINES/PAGE
         L     R1,TSDLEN           LINES/PAGE
         SR    R1,R0                MINUS REMAINDER = # OF NLS
         B     TSADDLN             INSERT NLS
TSRETRY  DS    0H                  IO ERROR
         MVI   TSDIOERR,C'Y'       LET CALL DECIDE WHAT TO DO
         B     TSWRET              RETURN(RE-CALL AT TWWLOC TO RETRY)
TSBTAMER DS    0H                  BAD BTAM WRITE
         MVC   TSDWERR1,=CL20'BAD  BTAM WRITE'
         B     TSFATALF            ERROR/DISPLAY R15
         TITLE 'CHECK FOR STOP/MODIFY'
TSCMCK   DS    0H
         L     R1,TSDCOMM          GET COMM PTRS
         TM    0(R1),X'40'         POSTED?
         BNO   4(R11)              NO - RETURN +4
TSCMLOOK DS    0H                  LOOK FOR CIB S
         L     R2,TSDCOMMP         GET COMM PTRS
         L     R2,4(R2)            CIB ORG
         LTR   R2,R2               END OF CHAIN
         BZ    TSCMFINI            END
         CLI   4(R2),X'40'         STOP
         BE    TSCMSTOP
         CLI   4(R2),X'44'         MODIFY
         BE    TSCMMOD
         CLI   4(R2),X'04'         START
         BE    TSCMMOD             HANDLE LIKE MOD
TSCMDEL  DS    0H                  DELETE A CIB
         L     R3,TSDCOMMP         ORG.
         LA    R3,4(R3)            POINT TO POINTER (TRUST ME)
         QEDIT ORIGIN=(R3),BLOCK=(2)  DELETE BLOCK
         B     TSCMLOOK            LOOK FOR MORE
*
****PROCESS MODIFY
*
TSCMMOD  DS    0H
         LA    R0,TSDTXT           HOLD AREA
         LA    R1,L'TSDTXT         L'
         LH    R15,14(R2)          L' OF MODIFY
         LA    R15,2(R15)          +2BYTE LEN
         ICM   R15,8,=C' '         PAD W/ BLANKS
         LA    R14,14(R2)          GET FROM ADDR
         MVCL  R0,R14              MOVE MODIFY TEXT
         MVC   TSDCONID,12(R2)     SAVE MCS ID. FOR RESPONSE
*
**             NULL INPUT
*
         CLI   TSDTXT+2,C' '       NULL INPUT
         BE    TSCMDEL             JUST DELETE IT
*
**             FLUSH (CANCEL PRINT )
*
         CLC   =C'FLUSH',TSDTXT+2  FLUSH CURRENT D.S.
         BNE   TSCMCAN             NO - TRY CANCEL
         MVI   TSDFLUSH,C'Y'       SET FLAG
         LA    R3,TSDTXT+2+5       POINT TO USERID (IF ANY)
         B     TSCMOK
*
**             STOP (AFTER CURRENT  OUTPUT QUIT)
*
TSCMCAN  DS    0H
         CLC   =C'STOP',TSDTXT+2   STOP AFTER CURR. D.S.
         BNE   TSCMPAUS            NO - TRY PAUSE
         MVI   TSDSTOPA,C'Y'       SET IT
         LA    R3,TSDTXT+2+4       POINT TO USERID (IF ANY)
         B     TSCMOK
*
**             PAUSE STOP FOR A WHILE
*
TSCMPAUS DS    0H
         CLC   =C'PAUSE',TSDTXT+2  STOP
         BNE   TSCMSTRT            NO-TRY START
         MVI   TSDPAUSE,C'Y'       PAUSE FOR A START CMD
         B     TSCMOK
*
**             START UP AFTER PAUSE
*
TSCMSTRT DS    0H
         CLC   =C'START',TSDTXT+2  START
         BNE   TSCMFORM            NO-LOOK FOR KEYWORDS
         MVI   TSDPAUSE,C'N'       TURN OFF PAUSE SWITCH
         B     TSCMOK
*
**             SCAN FOR KEYWORDS
*
TSCMFORM DS    0H
         LA    R14,TSCMFMXT        SET UP RETURN
**********************************************************************
*                                                                    *
**             PARSE DOWN STARTUP PARMS OR MODIFY KEYWORDS           *
*                                                                    *
**********************************************************************
TSCMFMSU DS    0H
         ST    R14,TSDFMRET        SAVE RETURN ADDR
         LA    R3,TSDTXT+2         GET 1ST BYTE OF PARM DATA AFTER L'
TSCMTYP  DS    0H
         CLI   0(R3),C','          LOOK FOR KEYWORDS
         BNE   *+8                 "
         LA    R3,1(R3)            BUMP PAST COMMA
         CLC   0(2,R3),=C'C='      CLASS
         BE    TSCMCLAS            HANDLE
         CLC   0(2,R3),=C'D='      DESTINATION
         BE    TSCMDEST            HANDLE
         CLC   0(2,R3),=C'F='      FORM
         BE    TSCMFRM             HANDLE
         CLC   0(2,R3),=C'S='      SEPERATOR
         BE    TSCMSEP             HANDLE
         CLC   0(2,R3),=C'L='      PAGE LENGTH
         BE    TSCMLEN             HANDLE
         CLC   0(2,R3),=C'W='      PAGE WIDTH
         BE    TSCMWID             HANDLE
         B     TSCMFMER            NOT FORM - ERROR
*
**       PROCESS FORMS CHANGE
*
TSCMFRM  DS    0H                  FORM
         LA    R0,4                ALL OUT FORMS ARE 4 CHAR ***CHRYSLER
         LA    R3,2(R3)            BUMP PAST F=
         LA    R15,TSDHFORM        GET HOLD AREA
         MVC   TSDHFORM,TSSFORM    INITIALIZE TO DEFAULT FORM
         BAL   R14,TSCMSCN         EDIT DATA
         LTR   R0,R0               4 CHARS
         BZ    TSCMCKMR            YES-GOOD
         MVC   TSDHFORM,TSSFORM    NO-RETURN TO DEFAULT
         B     TSCMFMER            GIVE INVALID MSG
*
**       PROCESS CLASS CHANGE
*
TSCMCLAS DS    0H                  CLASS CHANGE
         LA    R0,8                8 CHAR MAX
         LA    R3,2(R3)            BUMP PAST C=
         LA    R15,TSDHCLAS        GET HOLD AREA
         MVC   TSDHCLAS,TSSCLAS    MOVE DEFAULT
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
*
**       PROCESS DESTINATION
*
TSCMDEST DS    0H                  DEST CHANGE
         LA    R0,8                8 CHAR MAX
         LA    R3,2(R3)            BUMP PAST C=
         LA    R15,TSDHDEST        GET HOLD AREA
         MVC   TSDHDEST,=CL8' '    CLEAR AREA
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
*
**       PROCESS PAGE SEPERATOR OPTION
*
TSCMSEP  DS    0H                  SEP  CHANGE
         LA    R0,1                1 CHAR MAX
         LA    R3,2(R3)            BUMP PAST S=
         LA    R15,TSDSEP          GET SEP SWITCH
         MVI   TSDSEP,C'Y'         SET IT ON AS A DEFAULT
         BAL   R14,TSCMSCN         EDIT DATA
         B     TSCMCKMR            ASSUME DATA GOOD
*
**       PROCESS PAGE LENGTH CHANGE
*
TSCMLEN  DS    0H                  LEN  CHANGE
         LA    R1,TSDLEN           GET ANSWER LOC.
*
**       COMMON CODE FOR L=NNN,W=NNN
*
TSCMLENT DS    0H                  SCAN FOR NUMERIC KEYWORD
         LA    R0,3                3 DIG. MAX
         LA    R3,2(R3)            BUMP PAST L=
         LA    R15,TSDWORK         GET WORK AREA
         XC    TSDWORK,TSDWORK     CLEAR IT
         BAL   R14,TSCMSCN         EDIT DATA
         SH    R0,=H'3'            (RESIDUAL-MAX)=NEG. DIGITS
         BZ    TSCMCKMR            NONE-USE CURRENT
         LPR   R0,R0               DIGITS OF DATA
         XR    R15,R15             CLEAR WORK
TSCMLNXT DS    0H                  CONVERT VALUE TO BINARY
         IC    R14,TSDWORK         PICK A DIGIT
         N     R14,=F'15'          GET NUMERIC PART
         MH    R15,=H'10'          SHIFT CURRENT VALUE A DIGIT
         AR    R15,R14             ADD NEW DIGIT
         MVC   TSDWORK(7),TSDWORK+1 SHIFT DIGITS DOWN
         BCT   R0,TSCMLNXT         LOOP
         ST    R15,0(R1)           SAVE NEW VALUE IN ANSWER AREA
         B     TSCMCKMR            ASSUME DATA GOOD
*
**       PROCESS PAGE WIDTH CHANGE
*
TSCMWID  DS    0H                  WID  CHANGE
         LA    R1,TSDWID           GET ANSWER LOC.
         B     TSCMLENT            GO TO COMMON CODE
*
**       LOOK FOR ANOTHER KEYWORD
*
TSCMCKMR DS    0H                  SEE IF ANOTHER KEYWORD
         CLI   0(R3),C' '          END OF STRING
         BE    TSCMFMOK            YES-GIVE OK MSG
         B     TSCMTYP             NO-CLASSIFY
*
**       MOVE KEYWORD DATA
*
TSCMSCN  DS    0H                  EDIT
         CLI   0(R3),C' '          END OF STRING
         BE    TSCMSCND            YES-RETURN
         CLI   0(R3),C','          END OF STRING
         BE    TSCMSCND            YES-RETURN
         MVC   0(1,R15),0(R3)      MOVE DATA INTO WORK
         LA    R3,1(R3)            BUMP
         LA    R15,1(R15)          "
         BCT   R0,TSCMSCN          LOOP
TSCMSCND DS    0H                  RETURN
         BR    R14                 "
**********************************************************************
*
**             PARSE RETURN
*
TSCMFMOK DS    0H                  PARSE OK
         L     R14,TSDFMRET        GET RETURN ADDR
         B     0(R14)              +0 = OK
TSCMFMER DS    0H                  BAD PARSE
         L     R14,TSDFMRET        GET RETURN
         B     4(R14)              +4 = BAD
**********************************************************************
*
**              RETURN FROM PARSE  OF MODIFY TEXT
*
TSCMFMXT DS    0H                  BRANCH TABLE
         B     TSCMOK              +0=OK
         B     TSCMINV             +4=ERR
**********************************************************************
*
**             GIVE OPER STATUS TO MODIFY COMMAND
*
TSCMOK   DS    0H                  GIVE OPER OK MSG
         MVC   TSDCMW1,=CL7'OK'
         B     TSCMWTO             ISSUE WTO
TSCMINV  DS    0H                  GIVE INVALID MSG
         MVC   TSDCMW1,=CL7'INVALID'
TSCMWTO  DS    0H
         XR    R0,R0
         IC    R0,TSDCONID         GET CALLERS ID
         LA    R1,TSDCMWT1         WTO
         SVC   35                  WTO
*
**             GIVE OPERATOR COMPLETE STATUS
*
         L     R0,TSDLEN           GET BINARY PAGE LENGTH
         CVD   R0,TSDWORK          MAKE PRINTABLE
         UNPK  TSDLEN2,TSDWORK+6(2)
         OI    TSDLEN2+L'TSDLEN2-1,C'0'
         L     R0,TSDWID           GET BINARY PAGE LENGTH
         CVD   R0,TSDWORK          MAKE PRINTABLE
         UNPK  TSDWID2,TSDWORK+6(2)
         OI    TSDWID2+L'TSDWID2-1,C'0'
         XR    R0,R0
         IC    R0,TSDCONID         GET CALLERS ID
         LA    R1,TSDCMT2          WTO
         SVC   35                  WTO
*
**             LOOK FOR SPEC. INTRDR MODIFY
**              F PX,FLUSH,PGMXXX,PGMYYY (XXX=CALLER)
**                  (YYY=ID OF JOBNAME ON PRINTER(VERIFY))
**             GIVE TSO USER ACK. MSG.
*
         CLC   =C',PGM',0(R3)      TSO CALLER  ***CHRYSLER***
         BE    TSCMPGM             YES - FILL USERID IN
         MVC   TSDBYWHO,=CL8'OPER'
         B     TSCMEND             EXIT
TSCMPGM  DS    0H
*
**             VALIDITY CHECK TSOUSERS INFO
**             TSO CP: JESCNCL PGMYYY N (PGMYYY=JOBNAME, N=PRINTER#)
*
         MVC   TSDBYWHO(6),1(R3)   TELL WHO CANCELLED OUTPUT
         MVC   TSDBYWHO+6(2),=C'  '
         CLI   7(R3),C','          USER PROVIDES JOBNAME
         BNE   TSCMPINV            NO-PROBABLY OPER
         CLC   8(6,R3),TSDJOBN     MATCH?
         BE    TSCMPOK             YES-OK TO FLUSH
TSCMPINV DS    0H
         MVC   TSDCMW1,=CL7'INVALID'
         MVI   TSDFLUSH,C'N'       RESET FLUSH
TSCMPOK  DS    0H
         LA    R0,L'TSDCMW0
         LA    R1,TSDCMW0
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,       XXXXXXXXXX
               USERIDL=TSDBYWHO
TSCMEND  DS    0H
         B     TSCMDEL
TSCMSTOP DS    0H
         BR    R11                 RETURN +0 STOP ISSUED
TSCMFINI DS    0H
*
**             SEE IF PAUSE ISSUED
*
         CLI   TSDPAUSE,C'Y'       PAUSE SET
         BNE   4(R11)              NO-RETURN +4 NO STOP ISSUED
         L     R1,TSDCOMM          GET ADDR OF STOP/MOD ECB
         WAIT  1,ECB=(1)           ONLY WAIT FOR STOP/MODIFY
         B     TSCMCK              PROCESS NEW MODIFYS
         B     4(R11)              RETURN +4 NO STOP ISSUED
         TITLE 'CALL DYNAMIC ALLOCATE/UNALLOCATE SUBROUTINE'
DYNAL    LA    R1,TSDS99           LOAD ADDR. OF ALLOCATE PARM LIST
         DYNALLOC
         LTR   R15,R15             RETURN NORMAL?
         BZR   R11                 RETURN IF NORMAL RETURN
         MVC   TSDWERR1,=CL20'BAD DYNALLOC RETURN CODE'
         B     TSFATALF            ISSUE FATAL ERROR W/ R15
         TITLE 'F A T A L  E R R O R  M E S S A G E S'
TSFATALF DS    0H
         CVD   R15,TSDWORK         CONVERT CALLERS RETURN CODE IN R15
         STM   R14,R12,12(R13)     SAVE MY REGS
         UNPK  TSDWERR2,TSDWORK+6(2) 999 MAX RC SUPPORTED
         OI    TSDWERR2+L'TSDWERR2-1,C'0' MAKE PRINTABLE
         LA    R1,TSDWERR1         GET MF=L FORM OF WTO
         SVC   35                  ISSUE WTO
         ABEND  5,DUMP
         TITLE 'LITERAL POOL'
         LTORG
         TITLE 'STATIC STORAGE'
TSSWA    DS    0D
**********************************************************************
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
**********************************************************************
         PUSH  PRINT
         PRINT NOGEN
TSSDECB  WRITE TSSECB,TS,0,0,0,,1,MF=L
TSSDCB   DCB   DDNAME=L3284,DSORG=CX,MACRF=(R,W),READYQ=0
*DEFINE DCB AND OPEN AND CLOSE. (EODAD,SYNAD,OPEN AND CLOSE)
TSSDCBIS EQU   *
TSSDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,EODAD=TSGEOD,SYNAD=TSSYN
TSSDCBIE EQU   *
TSSBFR   DC    C'BEGINNING OUTPUT ON PRINTER PX(XXX)'
TSSXTRC  EXTRACT ,FIELDS=COMM,MF=L
TSSCMWT1 WTO   'TSOWTR-XXXXXXX',MCSFLAG=(REG0),MF=L
TSSWWT1  WTO   'I/O ERROR PX(XXX)',MF=L,ROUTCDE=(8)
TSSFLSMS DC    C'OUTPUT DELETED BY XXXXXXXX'
TSSHELLO DC    C'3284 WRITER STARTED'
TSSBEG   DC    CL91'****'
TSSCMT2  DS    0F                  CURRENT OPTIONS MESSAGE
         DC    AL2(TSSCMT2E-*)     L'TXT+L'ROUT+L'DESC
         DC    X'4000'             MCSFLAG=(REG0)
         DC    C'TSOWTR-F='
TSSFORM  DC    C'9DMY'             DEFAULT FORM
         DC    C',C='
TSSCLAS  DC    CL8'Z'              DEFAULT CLASSES
         DC    C',S='
TSSSEP   DC    CL1'Y'              DEFAULT PAGE SEPERATORS
         DC    C',L='
TSSLEN2  DC    C'999'              DEFAULT PAGE SIZE
         DC    C',W='
TSSWID2  DC    C'999'              DEFAULT PAGE WIDTH
         DC    C',PAUSE='
TSSPAUSE DC    C'N'                DEFAULT PAUSE IMMEDIATE OPTION
         DC    C',D='
TSSDEST  DC    CL8'RMT1'           DEFAULT DESTINATION
TSSCMT2E EQU   *                   NO ROUT/DESC W/ REG0
TSSLEN   DC    F'66'               DEFAULT PAGE SIZE
TSSWID   DC    F'125'              DEFAULT PAGE WIDTH
TSSWERR  WTO   'TSOWTR-X....X....X ...X....,R15=999',ROUTCDE=(8)
TSSMDL   EQU   *-TSSWA
         POP   PRINT
***********************************************************************
*  EVERYTHING FROM HERE ON IS NOT COPIED TO DYNAMIC AREA              *
***********************************************************************
TSSTR    TR    0(*-*,R14),TSSTBL   EXECUTED
BLANKS   DC    CL16' '
WTRSSOB  DS    0F
WTSSOBID DC    CL4'SSOB'
WTSSOBSZ DC    AL2(SSOBHSIZ)
WTSSOBFN DC    AL2(SSOBSOUT)
WTRSOSIZ EQU   *-WTRSSOB
         SPACE 2
TSSTBL   DC    256C'?'
UPCTBL   EQU   TSSTBL
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C'Ö'
         DC    C'Ö.<(+×&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);^-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    X'81,82,83,84,85,86,87,88,89'
         ORG   UPCTBL+X'91'
         DC    X'91,92,93,94,95,96,97,98,99'
         ORG   UPCTBL+X'A2'
         DC    X'A2,A3,A4,A5,A6,A7,A8,A9'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         TITLE 'DYNAMIC - GOTTEN AREA'
DSA      DSECT
         DS    9D                  O/S SAVE
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
TSDWA    DS    0D
TSDDECB  WRITE TSDECB,TS,0,0,0,,1,MF=L
TSDDCB   DCB   DDNAME=L3284,DSORG=CX,MACRF=(R,W),READYQ=0
TSDDCBIS EQU   *
TSDDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,SYNAD=TSSYN
TSDBFR   DC    C'BEGINNING OUTPUT ON PRINTER PX(XXX)' POSITION ONLY
TSDBFCL  EQU   TSDBFR+28,2
TSDBFUCB EQU   TSDBFR+31,3
TSDXTRC  EXTRACT ,FIELDS=COMM,MF=L
TSDCMWT1 WTO   'TSOWTR-XXXXXXX',MCSFLAG=(REG0),MF=L
TSDCMW0  EQU   TSDCMWT1+4,14
TSDCMW1  EQU   TSDCMWT1+11,7
TSDWWT1  WTO   'I/O ERROR PX(XXX)',MF=L,ROUTCDE=(8)
TSDWWC   EQU   TSDWWT1+4+10,2
TSDWWA   EQU   TSDWWT1+4+13,3
TSDFLSMS DC    C'OUTPUT DELETED BY XXXXXXXX'
TSDBYWHO EQU   TSDFLSMS+L'TSDFLSMS-8,8
TSDHELLO DC    C'328X WRITER STARTED'
TSDBEG   DC    0CL91'*',91C'*'
TSDBEGJ  EQU   TSDBEG+47,44
TSDBEGJN EQU   TSDBEG+38,8
TSDBEGJT EQU   TSDBEG+5,32
TSDCMT2  DS    0F                  CURRENT OPTIONS MESSAGE
         DC    AL2(TSDCMT2E-*)     L'TXT+L'ROUT+L'DESC
         DC    X'4000'             MCSFLAG=(REG0)
         DC    C'TSOWTR-F='
TSDHFORM DC    C'9DMY'             CURRENT FORM
         DC    C',C='
TSDHCLAS DC    CL8'Z'              CURRENT CLASS(ES)
         DC    C',S='
TSDSEP   DC    CL1'Y'              DEFAULT PAGE SEPERATORS
         DC    C',L='
TSDLEN2  DC    C'999'              DEFAULT PAGE SIZE
         DC    C',W='
TSDWID2  DC    C'999'              DEFAULT PAGE WIDTH
         DC    C',PAUSE='
TSDPAUSE DC    C'N'                DEFAULT PAUSE IMMEDIATE OPTION
         DC    C',D='
TSDHDEST DC    CL8'RMT1'           CURRENT DESTINATION
TSDCMT2E EQU   *                   NO ROUT/DESC W/ REG0
TSDLEN   DC    F'66'               PAGE SIZE
TSDWID   DC    F'125'              PAGE WIDTH
TSDWERR  WTO   'TSOWTR-X....X....X ...X....,R15=999',ROUTCDE=(8)
TSDWERR1 EQU   TSDWERR+4+7,20      DYNAMIC MESSAGE PORTION
TSDWERR2 EQU   TSDWERR+4+32,3      DECIMAL R15 RETURN CODE
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
TSDWORK  DS    D                   WORK AREA
TSDCURPO DS    F                   CURRENT LINE NUMBER
TSDWECB  DS    A                   HASP COMM ECB
TSDCOMM  DS    A                   O/S COMM ECB
TSDCOMMP DS    A                   PTR TO COMM PTRS
TSDILOC  DS    F                   OUTPUT RECORD TEXT LOC.
TSDISIZ  DS    F                   OUTPUT RECORD LENGTH  .
TSDIOERR DS    C                   C'Y' IF IO ERR TO PRT .
TSDCONCH DS    C                   CARRIAGE CONTROL
TSDSTOP  DS    C                   C'Y' TO STOP
TSDNEWDS DS    C                   C'Y' IF START OF DATA SET
TSDEOF   DS    C                   C'Y' IF POST EOF PROCESSING
TSDINIT  DS    C                   C'Y' IF IN START-UP PROCESSING
TSDCLAS  DS    C                   CURRENT JOB'S CLASS
TSDJOBN  DS    CL8                 CURR. JOBNAME
TSDJOBI  DS    CL8                 CURR. JOB ID.
TSDDSN   DS    CL44                CURR. DSN
TSDKEEP  DS    C                   KEEP SYSOUT
TSDRETLM DS    H                   RETRY COUNTER(IO ERR ON PRINTER)
TSDOPENX DS    F                   LIST FORM OF OPEN
TSDBFPTR DS    F                   CURR. AVAL. 328X BUFFER POS.
TSDTRUNC DS    C                   FORCE WRITE EVEN IF BFR NOT FULL
TSDCONID DS    X                   MCS ID OF MODIFY ISSUER
TSDFLUSH DS    C                   FLUSH CURRENT D.S. FLAG
TSDSTOPA DS    C                   SHUT DOWN AFTER CURR. WORK
TSDWRET  DS    F                   RETURN SAVE AREA
TSDFMRET DS    F                   RETURN SAVE AREA
TSDSAV01 DS    2F                  SAVE LINE/LEN DURING ASCII CONV.
@TSCVDAT DS    F                   ENTRY ADDR
@TSCVDPR DS    F                   PARM ADDR
         DS    0F                  SVC 99 BUILD AREA
TSDS99   DS    XL200               MUST BE LARGER THAN 'DYN' EQUATE
         DS    0F                  ALIGN
TSDTXT   DS    XL110               OPER MODIFY SAVEAREA
         DS    0F                  ALIGN
TSDSSOB  DS    (160)X              SSOB BUILD AREA
         DS    0F                  ALIGN
TSDBUFR  DS    XL2059              3284 BUILD AREA(BLOCK OUTPUT SOMEDAY
TSDCLRE  EQU   *                   END OF DYNAMIC AREA
TSDSASIZ EQU   *-TSDWA             SIZE OF DYNAMIC AREA
         TITLE 'DYNAMIC ALLOCATION PARAMETER LIST DSECT'
         IEFZB4D0
         TITLE 'DYNAMIC ALLOCATION TEXT UNIT KEYS'
         IEFZB4D2
         TITLE 'SUBSYSTEM OPTION BLOCK - ACCESS SYSOUT DATA SETS'
         IEFJSSOB (SO)
         TITLE 'JOB ENTRY SUBSYSTEM COMMUNICATIONS TABLE'
         IEFJESCT
         TITLE 'SUBSYSTEM IDENTIFICATION BLOCK'
         IEFJSSIB ,                SSIB MAPPING MACRO
         TITLE 'WRITER DYNAMIC ALLOCATION WORK AREA'
DYNTXTPT DSECT
DSNTXTPT DS    F                   POINTER TO DSNAME TEXT UNIT
SSRTXTPT DS    F                   PTR. TO SUBSYSTEM REQUEST TEXT UNIT
DDNTXTPT DS    F                   PTR TO DDNAME RETURNED TEXT UNIT
UDNTXTPT DS    F                   DDNAME TEXT PTR. FOR UNALLOCATE
UORTXTPT DS    F                   OVERRIDE TEXT PTR. FOR UNALLOCATE
DYNTXTND EQU   *                   END OF TEXT POINTERS
DSNTXTUT DS    CL50                DSNAME TEXT UNIT
SSRTXTUT DS    CL10                SUBSYSTEM REQUEST TEXT UNIT
DDNTXTUT DS    CL14                DDNAME RETURNED  TEXT UNIT
UDNTXTUT DS    CL14                UNALLOCATE DDNAME TEXT UNIT
UORTXTUT DS    CL7                 OVERRIDE DISPOSITION FOR UNALLOCATE
DYNEND   EQU   *
TXTPTSIZ EQU   DYNTXTND-DYNTXTPT   SIZE OF TEXT UNIT POINTERS
DYNRBSIZ EQU   S99RBEND-S99RB      SIZE OF DYN ALLOC REQUEST BLOCK
DYN      EQU   TXTPTSIZ+DYNRBSIZ+(DYNEND-DSNTXTUT)+L'S99RBPTR
DISPDDN  EQU   DYNRBSIZ+DDNTXTPT-DYNTXTPT    ADDR. OF DDNAME IN DYN LST
DISPOVR  EQU   DYNRBSIZ+UORTXTPT-DYNTXTPT    ADDR. OVRIDE IN DYN LST
DISPUAL  EQU   DYNRBSIZ+UDNTXTPT-DYNTXTPT    ADDR. UNAL IN DYN LST
DISPDSN  EQU   DYNRBSIZ+DSNTXTPT-DYNTXTPT    ADDR. DSNAME IN DYN LIST
         TITLE 'MISC EQUATES AND DSECTS'
         REGS
         TSCVDATE                  THIS DSECT NOT USED DOC. ONLY
         PUSH  PRINT
         PRINT NOGEN               SAVE A LITTLE PAPER
         DCBD  DSORG=PS
         IEZDEB
UCB      DSECT
         IEFUCBOB
         POP   PRINT
         CVT   DSECT=YES           CVT
         IHAPSA DSECT=YES          PSA
         IKJTCB DSECT=YES          TCB
         IEZJSCB                   JSCB
         END
