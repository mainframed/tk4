TRLO     TITLE 'CHARACTER TRANSLATION ROUTINE'
* * * * * * * * * * * * * * * * * * * * *
*                                       *
*              T R A N S L A T          *
*                                       *
* * * * * * * * * * * * * * * * * * * * *
         SPACE
*FUNCTION - OPERATION
*        THIS ROUTINE TRANSLATE THE BYTES FROM AN INPUT RECORD.
*        THE ROUTINE IS USED TO TRANSLATE TYPEWRITER RECORDS.
         SPACE
*INPUT
*        INPUT RECORDS FROM SYSUT1
*OUTPUT
*        REPRODUCED AND TRANSLATED RECORDS TO SYSUT2
*CONTROL
*        TRANSLATION TABLE FROM //SYSIN
*        COL.
*        01       10                           71
*        .        .                            .
*        XX       XX--                      --XX
*        DISPL                  DATA
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         PRINT ON,NOGEN
         DCBD  DSORG=PS
         USING IHADCB,R4
         SPACE 3
TRANSLAT CSECT
         SPACE
         USING TRANSLAT,R15
         B     START
         DC    AL1(8)
         DC    CL8'TRANSLAT'
SAVE     DC    18F'0'                   SAVE AREA
START    STM   14,12,12(13)
         ST    R13,SAVE+4
         LA    R13,SAVE
         BALR  R11,0                    LOAD BASE REGISTER
         DROP  R15
         USING *,R11
         OPEN  (SYSIN,,SYSUT1,,SYSPRINT,OUTPUT)
         SR    R3,R3
INPUT    GET   SYSIN
         TR    0(71,R1),XTRTAB
         PACK  DISPL(2),0(3,R1)
         IC    R3,DISPL
         LA    R4,TRANSTAB(R3)
         LA    R5,31                    NR OF ENTRIES
         LA    R1,9(0,R1)               FIRST DATA BYTE
NEXT     CLI   0(R1),0
         BE    INPUT
         CLI   1(R1),0
         BE    INPUT
         PACK  DATA(2),0(3,R1)
         MVC   0(1,R4),DATA
         LA    R1,2(0,R1)
         LA    R4,1(0,R4)
         BCT   R5,NEXT
         B     INPUT
         SPACE
CNTRLEND PUT   SYSPRINT,TITLE
         LA    R5,TRANSTAB
         LA    R7,8
TR002    LA    R4,PRNTDATA+1
         LA    R6,8
TR001    UNPK  0(9,R4),0(5,R5)
         TR    0(8,R4),XTRTAB
         MVI   8(R4),C' '
         LA    R4,9(0,R4)
         LA    R5,4(0,R5)
         BCT   R6,TR001
         PUT   SYSPRINT,PRNTDATA
         MVI   PRNTDATA,C' '
         BCT   R7,TR002
         LA    R4,SYSUT1
         LH    R2,DCBLRECL
         IC    R3,DCBRECFM
         LA    R4,SYSUT2
         STH   R2,DCBLRECL
         STC   R3,DCBRECFM
         SPACE 2
         OPEN  (SYSUT2,OUTPUT)
TR006    GET   SYSUT1
         LH    R2,DCBLRECL
         LR    R5,R1                    SAVE START
         TM    DCBRECFM,DCBRECU         UNDEFINED
         BO    ERROR1                   YES, ERROR
         TM    DCBRECFM,DCBRECV         VARAIBLE
         BZ    TR003                    NO, FIXED
         LH    R2,0(0,R1)               GET LRECL
         SH    R2,=H'4'                 DECR PREFIX
         LA    R1,4(0,R1)               INCR TO DATA
TR003    TM    DCBRECFM,DCBRECCA+DCBRECCM
         BZ    TR004
         LA    R1,1(0,R1)               SKIP CNTRL
         BCTR  R2,0                     DECR LRECL
TR004    LTR   R2,R2                    ANY BYTE
         BNP   TR006                    NO, NEXT INPUT
         BCTR  R2,0                     DECR FOR EX
         EX    R2,TRANSL                TRANSLATE
         PUT   SYSUT2,(R5)              WRITE TO OUTPUT
         B     TR006                    RETRY
         SPACE
TRANSL   TR    0(0,R1),TRANSTAB
         SPACE 3
ERROR1   WTO   'TRL001I RECFM=U IS NOT SUPPORTED',ROUTCDE=(11)
         ABEND 001,DUMP
         SPACE 2
DATAEND  CLOSE (SYSUT1,,SYSUT2,,SYSIN,,SYSPRINT)
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         EJECT
TRANSTAB DS    0D                       NORMAL TRANSL TABLE
         DC    256AL1(*-TRANSTAB)
         SPACE
XTRTAB   DS    0D                       HEXA TRANSL TABLE
         DC    256X'00'
         ORG   XTRTAB+C'A'
         DC    X'FAFBFCFDFEFF'
         ORG   XTRTAB+C'0'
         DC    C'0123456789ABCDEF'
         SPACE
TITLE    DC    CL81'1THE FOLLOWING TRANSLATION TABLE IS USED: '
PRNTDATA DC    CL81'0'
DISPL    DS    4C
DATA     DS    4C
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM),                    X
               RECFM=FBA,LRECL=81,BLKSIZE=810
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),EODAD=CNTRLEND,        X
               RECFM=FB,LRECL=80
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),EODAD=DATAEND
SYSUT2   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=(PM)
         LTORG
         END
