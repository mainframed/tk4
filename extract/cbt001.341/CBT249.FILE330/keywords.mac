         MACRO
&NAME KEYWORDS
         LCLA  &A1,&A2,&A3
         SPACE 3
***
*        KEYWORD INDEXES
***
         SPACE
COMMAND  EQU   128                      ENTRY IS A COMMAND
KEYWORD  EQU   64                       ENTRY IS A KEYWORD
         SPACE 3
***
*        KEYWORD SEARCH TABLE
***
         SPACE
&NAME    DC    A(BR&SYSNDX)             -> BRANCH TABLE
&A1      SETA  N'&SYSLIST
&A2      SETA  1
.KEY03   AIF   (&A1 EQ 0).KEYSTOP
         AIF   (N'&SYSLIST(&A2) LT 2).KEYERR1
         AIF   (T'&SYSLIST(&A2,2) EQ 'O').KEYERR1
&A3      SETA  K'&SYSLIST(&A2,1)
         AIF   (&A3 EQ 0 OR &A3 GT 32).KEYERR1
         DC    AL1(&A3)                 KEYWORD LENGTH
         AIF   (N'&SYSLIST(&A2) GT 2).KEY01
         DC    AL1(KEYWORD)             DEFAULT IDENTIFIER
         AGO   .KEY02
.KEY01   AIF   (T'&SYSLIST(&A2,3) EQ 'O').KEY06
         DC    AL1(&SYSLIST(&A2,3))     KEYWORD IDENTIFIER
.KEY02   ANOP
         DC    CL&A3'&SYSLIST(&A2,1)'
.KEY04   ANOP
&A2      SETA  &A2+1
&A1      SETA  &A1-1
         AGO   .KEY03
.KEYSTOP ANOP
         DC    AL1(0)
         SPACE 3
***
*        KEYWORD BRANCH TABLE
***
         SPACE
BR&SYSNDX   DS    0F
&A1      SETA  N'&SYSLIST
&A2      SETA  1
.KEY05   AIF   (&A1 EQ 0).KEYEND
         AIF   (N'&SYSLIST(&A2) LT 3).KEY08
         AIF   (T'&SYSLIST(&A2,3) EQ 'O').KEY06
.KEY08   AIF   (N'&SYSLIST(&A2) LT 2).KEY06
         AIF   (T'&SYSLIST(&A2,2) EQ 'O').KEY06
         AIF   (T'&SYSLIST(&A2,1) EQ 'O').KEY06
         B     &SYSLIST(&A2,2)
.KEY06   ANOP
&A1      SETA  &A1-1
&A2      SETA  &A2+1
         AGO   .KEY05
.KEYEND  ANOP
         MEXIT
.KEYERR1 MNOTE 8,'INVALID ENTRY SPECIFIED: &SYSLIST(&A2)'
         AGO   .KEY04
         MEND
