TIME     TITLE 'APEMAN - REPLACEMENT FOR TIME MACRO'
TIMER    CSECT
*
** BECAUSE OF THE EXCESSIVE USE OF THE TIME MACRO IN APEMAN, OPTIMUM
** PERFORMANCE OF THIS FUNCTION IS NECESSARY.  THE PURPOSE OF THIS
** ROUTINE IS TO AVOID SVC OVERHEAD AND SOME OF THE EXTRA PROCESSING
** IN THE TIME SVC ROUTINE (IEAVRT01) BY CODING A STRIPPED DOWN
** VERSION AS A SUBROUTINE.
** CALLING SEQUENCE IS TIMER (FLAG, DATE, TIME) WHERE FLAG VALUES ARE
** THE SAME AS IN THE TIME MACRO EXPANSION, NAMELY
**   0 = TU         1 = BIN        2 = DEC
**   3 = MIC        4 = STCK
** THE GMT OPTION IS NOT CURRENTLY SUPPORTED.
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2                  STANDARD APEMAN BASE REGISTER
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW15     EQU   15
         SAVE  (14,12),,*
         LR    RB2,RW15           SET UP BASE REGISTER
         USING TIMER,RB2          FOR ADDRESSABILITY
         LM    RW3,RW5,0(RW1)     GET ARGUMENT ADDRESSES
         STCK  STCKAREA           STORE CURRENT CLOCK VALUE
         BNZ   USESVC             LET SYSTEM WORRY ABOUT PROBLEMS
*
** NOW GET NEEDED VALUES FROM THE CVT AND THE SYSTEM'S TIMER WORKAREA
*
         L     RW8,CVTPTR         ADDRESS THE CVT
         MVC   0(4,RW4),CVTDATE(RW8)   GET CURRENT DATE
*                                 CVTTZ NEEDED ONLY FOR GMT
         L     RW4,CVTTPC(RW8)    POINT TO SYSTEM TIMER WORKAREA
*
         CLI   3(RW3),4           IS THIS A STCK REQUEST?
         BNE   ADJTIME            IF NOT, GET TIME SINCE LAST MIDNIGHT
         MVC   0(8,RW5),STCKAREA  JUST RETURN RESULT OF STCK
         B     NORMRET            THE REST IS NOT NEEDED
*
** THE FOLLOWING IS NEEDED BECAUSE THE SYSTEM MAINTAINS THE TOD VALUE
** FOR THE NEXT MIDNIGHT RATHER THAN THE PREVIOUS ONE.
*
ADJTIME  LM    RW6,RW7,DAYLEN     LOAD LENGTH OF DAY
         AL    RW7,STCKAREA+4     ADD RIGHT HALF OF CURRENT TOD
         BC    12,ADDLEFT         SKIP CARRY IF NO OVERFLOW
         AL    RW6,ONE            ALLOW FOR CARRY
ADDLEFT  AL    RW6,STCKAREA       ADD RIGHT HALF OF CURRENT TOD
         SL    RW7,MNIGHTRH(RW4)  SUBTRACT RIGHT HALF OF NEXT MIDNIGHT
         BC    11,SUBLEFT         SKIP CARRY IF NOT REQUIRED
         BCTR  RW6,0              ADJUST FOR RIGHT SIDE SHORTAGE
SUBLEFT  SL    RW6,MNIGHTLH(RW4)  SUBTRACT LEFT HALF OF NEXT MIDNIGHT
         L     RW1,0(,RW3)        LOAD OPTION CODE
         SLL   RW1,2              SHIFT TO INDEX THE TABLE
         B     TABLE(RW1)         BRANCH TO REQUESTED ROUTINE
TABLE    B     TU
         B     BIN
         B     DEC
MIC      STM   RW6,RW7,0(RW5)     MIC CALLS FOR DOUBLE WORD RESULT
         B     NORMRET            AND RETURN TO CALLER
TU       SLDL  RW6,13
         D     RW6,KTU
         SLL   RW7,1
         B     RESULT
BIN      D     RW6,KBIN
         B     RESULT
DEC      D     RW6,KBIN
         LA    RW4,32             SHIFT COUNT
         LA    RW3,12             SET DIVISOR MASK
         LA    RW15,10            INITIAL DIVISOR
DECLOOP  SR    RW6,RW6            CLEAR REMAINDER
         DR    RW6,RW15           REMAINDER = DEC DIGIT
         LR    RW0,RW6            SAVE THE DIGIT
         SRDL  RW0,4              IN REGISTER 1
         SH    RW4,FOUR           ADJUST SHIFT COUNT
         LTR   RW7,RW7            IF QUOTIENT = 0 THEN
         BZ    CONVEND              DONE, EXIT THIS CODE
         CL    RW4,TWENTY4        CHANGE DIVISOR IF COUNT < 24
         BNL   DECLOOP
         XR    RW15,RW3             SWITCH BETWEEN 10 AND 6
         B     DECLOOP            AND CONTINUE THE LOOP
CONVEND  SRL   RW1,0(RW4)         USE RESIDUAL SHIFT COUNT
         LR    RW7,RW1            GETTING HHMMSSTH IN RW7
RESULT   ST    RW7,0(,RW5)        STORE RESULT FOR CALLER
NORMRET  SR    RW15,RW15          INDICATE NORMAL COMPLETION
RET      RETURN (14,12),RC=(15)
*
** CONSTANTS FOR TIME CONVERSION
*
FOUR     DC    H'4'
TWENTY4  DC    F'24'
STCKAREA DS    D                  DOUBLE WORD FOR STCK INSTRUCTION
DAYLEN   DC    X'000141DD'        LENGTH OF DAY IN UNITS OF
         DC    X'76000000'         ????
KTU      DC    X'682AAAAB'
KBIN     DC    F'40960000'
ONE      DC    F'1'
*
CVTPTR   EQU   16
CVTDATE  EQU   56                 OFFSET OF CURRENT DATE IN CVT
CVTTPC   EQU   88                 CVT OFFSET OF TIMER WORK POINTER
MNIGHTLH EQU   56                 OFFSET IN TIMER WORK AREA
MNIGHTRH EQU   60                 OFFSET IN TIMER WORK AREA
*
** IF WE RAN INTO TROUBLE, JUST ISSUE NORMAL SVC
*
USESVC   L     RW1,0(RW3)         LOAD CODE TO INDICATE TIME UNITS
         LA    RW0,RW5            ADDRESS TIME TARGET FIELD
         SVC   11                 ISSUE THE TIME SVC
         LTR   RW15,RW15          TEST COMPLETION CODE
         BNZ   RET                IF BAD, PASS IT ON
         ST    RW1,0(,RW4)        STORE THE DATE
         CLI   3(RW3),3           CHECK FOR MIC OR STCK OPTION
         BNL   NORMRET            IF SO, TIME ALREADY STORED
         ST    RW0,0(RW5)         STORE TIME FOR CALLER
         B     NORMRET            AND RETURN
         END
