MONR     TITLE 'APEMAN SRM MONITOR - DSECTS FOR SRM AREAS'
* ORIGINAL DATA SET ANBMONR    AT LEVEL 001 AS OF 18/02/76
*APEMONR       MONR  TITLE 'MONR - APEMAN SRM MONITOR'
* MODIFICATION CW020 17/11/81 LEVEL UPDATE TO MVS/SP RELEASE 1
*                             (TOTAL REWRITE - NOT FLAGGED)
MONR     CSECT
         IEZBITS
         SPACE 3
         APECCT
         SPACE 3
         APEICT
         SPACE 3
         APEMCT
         SPACE 3
         APERCT
         SPACE 3
         APERMPT
         SPACE 3
         APERMCA
         SPACE 3
         APELSCT
         SPACE 3
         IHAPVT
         SPACE 3
*
*                             THE FOLLOWING LENGTH CONSTANTS ARE
*                             BASED ON THE DSECTS DEFINED ABOVE
*
*CCCLEN1 EQU   X'48'          LENGTH OF CONSTANTS IN CCT
*CCTLEN1 EQU   X'30'          LENGTH OF VARIABLES IN CCT
*CCTLEN2 EQU   X'A0'          LENGTH OF CCT CONST, VAR, & ASCBCHAP
*CCCLEN3 EQU   X'10'          LENGTH OF CONSTANTS AT END OF CCT
*ICCLEN1 EQU   X'40'          LENGTH OF CONSTANTS IN ICT
*ICTLEN1 EQU   X'20'          LENGTH OF VARIABLES IN ICT
*MCCLEN0 EQU   X'38'          LENGTH OF VCONS IN MCT
*MCCLEN  EQU   X'68'          LENGTH OF VCONS AND CONSTANTS IN MCT
*MCCLEN1 EQU   X'30'          LENGTH OF CONSTANTS IN MCT
*MCTLEN1 EQU   X'A8'          LENGTH OF CONSTANTS AND VARIABLES IN MCT
*MCTLEN2 EQU   X'40'          LENGTH OF VARIABLES IN MCT
*MCTLEN  EQU   X'F8'          TOTAL LENGTH OF MCT
*MCCLEN3 EQU   X'50'          LENGTH OF CONSTANTS AT END OF MCT
*RCCLEN1 EQU   X'3C'          LENGTH OF CONSTANTS IN RCT
*RCTLEN1 EQU   X'4C'          LENGTH OF VARIABLES IN RCT
*RMPTLEN EQU   X'68'          LENGTH OF RMPT (ALL CONSTANTS)
*RMCALEN1 EQU  X'7C'          LENGTH OF RMCA STUFF BEFORE SWAP COUNTS
*RMCALEN2 EQU  X'3C'          LENGTH OF SWAP COUNTS IN RMCA
*LSCCLEN1 EQU   X'24'         LENGTH OF CONSTANTS IN LSCT
*LSCTLEN1 EQU   X'0C'         LENGTH OF VARIABLES IN LSCT
         TITLE 'MONR - APEMAN SRM MONITOR'
MONR     CSECT
         B     ENTRY-MONR(15)
         DC    X'06',CL7'MONR'
ENTRY    EQU   *
*
**  LOGS SRM CONTROL BLOCKS - CCT,ICT,MCT,RCT,RMPT,RMCA,LSCT
**  LINKAGE IS MONR(INPUT,MONRT,TOD)
*
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW10     EQU   10
RW15     EQU   15
*
         STM   14,12,12(13)            SAVE REGISTERS
         BALR  RB2,0
         USING *,RB2
         ST    13,SAVE+4
         LR    RW3,13
         LA    13,SAVE
         ST    13,8(RW3)
*
         LM    RW3,RW4,4(1)            PICK UP PARAMS
         MVC   ADRT(4),0(RW3)          ADDRESS OF MONRT
         MVC   TOD(4),0(RW4)           MOVE TOD
*
**  FIND START OF RMCT
*
         L     RW3,16                  ADDRESS OF CVT
         L     RW3,604(RW3)            ADDRESS OF RMCT
         LA    RW5,NORMLEN             NORMAL LENGTH OF DATA
         TM    FIRST,X'FF'             FIRST TIME IN?
         BNZ   NOTFIRST
         MVI   FIRST,X'FF'             SET FLAG
*                                      GET FIELDS FROM PVT
         L     RW5,16                  CVT ADDRESS
         L     RW5,356(RW5)            PVT ADDRESS
         USING PVT,RW5                 TEMP PVT ADDRESSABILITY
         MVC   $PVTLPRV,PVTLPRIV       START PRIVATE AREA & CSA
         DROP  RW5                     DROP PVT ADDRESSABILITY
*
         LA    RW5,FIRSTLEN            FIRST TIME LENGTH
         L     RW4,4(RW3)              ADDRESS OF CCT
         MVC   $CCC1(CCCLEN1),0(RW4)        MOVE CCT CONSTANTS
         MVC   $CCC3(CCCLEN3),CCTLEN2(RW4)  MOVE CCT CONSTANTS
         L     RW4,8(RW3)              ADDRESS OF ICT
         MVC   $ICC(ICCLEN1),0(RW4)    MOVE IN ICT CONSTANTS
         L     RW4,12(RW3)             ADDRESS OF MCT
         MVC   $MCC1(MCCLEN1),MCCLEN0(RW4)  MOVE MCT CONSTANTS
         MVC   $MCC3(MCCLEN3),MCTLEN1(RW4)  MOVE MCT CONSTANTS
         L     RW4,228(RW3)            ADDRESS OF RCT
         MVC   $RCC(RCCLEN1),0(RW4)    MOVE IN RCT CONSTANTS
         L     RW4,16(RW3)             ADDRESS OF RMPT
         MVC   $RMPT(RMPTLEN),0(RW4)   MOVE RMPT
         L     RW4,60(RW3)             ADDRESS OF LSCT
         MVC   $LSCC(LSCCLEN1),0(RW4)  MOVE LSCT CONSTANTS
NOTFIRST EQU   *
         ST    RW5,LENGTH              STORE DATA LENGTH
         L     RW4,4(RW3)              ADDRESS OF CCT
         MVC   $CCT(CCTLEN1),CCCLEN1(RW4)  MOVE IN CCT VARIABLES
         L     RW4,8(RW3)                  ADDRESS OF ICT
         MVC   $ICT(ICTLEN1),ICCLEN1(RW4)  MOVE IN ICT VARIABLES
         L     RW4,12(RW3)                 ADDRESS OF MCT
         MVC   $MCT(MCTLEN2),MCCLEN(RW4)   MOVE IN MCT VARIABLES
         L     RW4,228(RW3)                ADDRESS OF RCT
         MVC   $RCT(RCTLEN1),RCCLEN1(RW4)  MOVE IN RCT VARIABLES
         L     RW4,20(RW3)                 ADDRESS OF RMCA
         MVC   $RMCA(RMCALEN2),RMCALEN1(RW4)  SWAP REASON COUNTS
         L     RW4,60(RW3)                 ADDRESS OF LSCT
         MVC   $LSCT(LSCTLEN1),LSCCLEN1(RW4)  MOVE LSCT VARIABLES
*
*        FIELDS DELETED FROM MONITORING INCLUDE THE FOLLOWING
*              RLCT                    DEFUNCT CONTROL BLOCK
*              BTOD                    MOVE IN SRM BASE TIME
*              BSRM                    MOVE CURRENT SRM TIME
*              NUMBER OF I/O STARTED ON EACH LCH
*
         L     RW3,16                  CVT ADDRESS
         L     RW3,356(RW3)            PVT ADDRESS
         USING PVT,RW3                 TEMP PVT ADDRESSABILITY
         MVC   $PVTAFC,PVTAFC          AFC, LOW AND OK THRESHOLDS
         MVC   $PVTDFRS,PVTDFRS        FRAME ALLOCATION DEFERRALS
         MVC   $PVTPAGE,PVTNPIN        PAGE COUNTS
         DROP  RW3                     DROP PVT ADDRESSABILITY
*
**  WRITE RECORD TO LOG
*
         LA    RW1,ADARGS              POINT AT PARAM LIST
         L     RW15,ADRT               ADDRESS OF MONRT
         BALR  14,15                   LINK TO MONRT
*
**  RETURN
*
         L     13,4(13)
         LM    14,12,12(13)            RESTORE REGISTERS
         BR    14
*
**  STORAGE AND CONSTANTS
*
FIRST    DC    X'00'
SAVE     DS    18F
ADRT     DS    F
ADARGS   DC    A(NAME)
         DC    A(LENGTH)
         DC    A(TOD)
         DC    A(FLAG)
NAME     DC    CL8'MONR'
LENGTH   DS    F
NULL     DC    CL8' '
FLAG     DC    F'0'
TOD      DS    F
$CCT     DS    0F            VARIABLES FROM CCT
         ORG   *+CCTLEN1
$ICT     DS    0F            VARIABLES FROM ICT
         ORG   *+ICTLEN1
$MCT     DS    0F            VARIABLES FROM MCT
         ORG   *+MCTLEN2
$RCT     DS    0F            VARIABLES FROM RCT
         ORG   *+RCTLEN1
$RMCA    DS    0F            SWAP REASON COUNTS FROM RMCA
         ORG   *+RMCALEN2
$LSCT    DS    0F            VARIABLES FROM LSCT
         ORG   *+LSCTLEN1
$PVTAFC  DS    CL6
$PVTDFRS DS    H
$PVTPAGE DS    CL56
NORMLEN  EQU   (*-TOD)/4     DATA RECORDED EVERY TIME
*
$CCC1    DS    0F            CONSTANTS FROM FRONT OF CCT
         ORG   *+CCCLEN1
$CCC3    DS    0F            CONSTANTS FROM END OF CCT
         ORG   *+CCCLEN3
$ICC     DS    0F            CONSTANTS FROM ICT
         ORG   *+ICCLEN1
$MCC1    DS    0F            CONSTANTS FROM FRONT OF MCT
         ORG   *+MCCLEN1
$MCC3    DS    0F            CONSTANTS FROM END OF MCT
         ORG   *+MCCLEN3
$RCC     DS    0F            CONSTANTS FROM RCT
         ORG   *+RCCLEN1
$RMPT    DS    0F            RMPT (ALL CONSTANTS)
         ORG   *+RMPTLEN
$LSCC    DS    0F            CONSTANTS FROM LSCT
         ORG   *+LSCCLEN1
$PVTLPRV DS    CL4           VBN ORIGIN OF PRIV AREA AND CSA
FIRSTLEN EQU   (*-TOD)/4     DATA RECORDED FIRST TIME ONLY
         END
