         TITLE 'MONS - SNAPSHOT MONITOR'
* MODIFICATION CW011 81/07/15 UPDATE CONTROL BLOCK REFERENCES     CW011
* MODIFICATION CW014 80/10/27 CHANGE REAL STORAGE COMPUTATION     CW014
         SPACE 3                                                  CW011
         MACRO                                                    CW011
&LABEL   IHAPFTE ,                                                CW011
.*                     THIS MACRO WAS CODED BECAUSE AN IBM        CW011
.*                     VERSION WAS NOT FOUND IN MACLIB OR MODGEN  CW011
&LABEL   DSECT                                                    CW011
PFTPGID  DS    0F      VIRTUAL PAGE IDENTIFIER                    CW011
PFTASID  DS    H       ASID OF PAGE CURRENTLY OWNING FRAME        CW011
PFTVBN   DS    H       VIRTUAL BLOCK NUMBER (HIGH 12 BITS + 0000) CW011
*                                                                 CW011
PFTQPTRS DS    0F      PAGE FRAME Q POINTERS, IN INDEX FORM       CW011
PFTFQPTR DS    H       INDEX OF NEXT ENTRY ON THIS PFQ            CW011
PFTBQPTR DS    H       INDEX OF PREVIOUS ENTRY ON THIS PFQ        CW011
*                                                                 CW011
PFTFXCT  DS    H       FIX COUNT OF THIS FRAME                    CW011
PFTRSV1  DS    H       *** RESERVED ***                           CW011
*                                                                 CW011
PFTFLAG1 DS    X       FIRST FLAG FIELD                           CW011
PFTONAVQ EQU   X'80'   THIS PFTE IS ON AVAILABLE QUEUE            CW011
PFTVRINT EQU   X'40'   PFTE INTERCEPTED FOR V=R                   CW011
PFTLSQA  EQU   X'20'   FRAME CONTAINS AN LSQA OR SQA PAGE.        CW011
*                        IF SQA, PFTASID = X'FFFF'                CW011
PFTLNGFX EQU   X'10'   PAGE IS LONG TERM FIXED                    CW011
PFTPCBSI EQU   X'08'   A PCB EXISTS FOR THIS PAGE                 CW011
PFTBADPG EQU   X'04'   FRAME IS BAD AND MAY NOT BE ALLOCATED      CW011
PFTVRALC EQU   X'02'   FRAME IS ALLOCATED TO A V=R REGION         CW011
PFTOFINT EQU   X'01'   FRAME IS PENDING OFFLINE                   CW011
*                                                                 CW011
PFTFLAG2 DS    X       SECOND FLAG BYTE                           CW011
PFTDFRLS EQU   X'80'   RELEASE DEFERRED UNTIL THIS PAGE IS FREED  CW011
PFTOFFLN EQU   X'40'   FRAME IS OFFLINE                           CW011
PFTVR    EQU   X'20'   PAGE MAY BE ALLOCATED TO A V=R REGION      CW011
PFTIRRG  EQU   X'10'   THIS IS A VIO FRAME                        CW011
PFTSTEAL EQU   X'08'   PFTE SELECTED FOR STEALING, NOT STOLEN YET CW011
PFTPREF  EQU   X'04'   PFTE IS IN THE PREFERRED AREA              CW011
*                                                                 CW011
PFTQNDX  DS    X       PFT QUEUE INDEX                            CW011
PFTAFQN  EQU   0       PFTE ON AVAILABLE Q                        CW011
PFTSRQN  EQU   4       PFTE ON SQA RESERVED Q                     CW011
PFTCFQN  EQU   8       PFTE ON COMMON FRAME Q                     CW011
PFTSQAN  EQU   12      PFTE ON SQA FRAME Q                        CW011
PFTLFQN  EQU   X'80'   PFTE ON LOCAL FRAME Q                      CW011
PFTLSQAN EQU   X'84'   PFTE ON LSQA FRAME Q                       CW011
PFTNQN   EQU   X'FF'   PFTE IS NOT QUEUED                         CW011
*                                                                 CW011
PFTUIC   DS    X       NUMBER OF STEAL INTERVALS FRAME NOT REF'D  CW011
         MEND                                                     CW011
         EJECT                                                    CW011
MONS     START
         B     ENTRY-MONS(15)
         DC    X'06',CL7'MONS'
ENTRY    EQU   *
*
*   ROUTINE SNAPS OUT FIELDS FROM ASCB,OUCB,PFTE
*   LINKAGE IS MONS(INPUT,MONRT,TIME)
*
*   OUTPUT IS OF THE FORM
*   MONS,LENGTH,TIME,JOBNAME,ASID,SNAPSHOT
*
RW0      EQU   0
RW1      EQU   1
RB2      EQU   2
RW3      EQU   3
RW4      EQU   4
RW5      EQU   5
RW6      EQU   6
RW7      EQU   7
RW8      EQU   8
RW9      EQU   9
RW10     EQU   10
RW11     EQU   11
RW12     EQU   12
RW14     EQU   14
RW15     EQU   15
*
         STM   14,12,12(13)
         BALR  RB2,0
         USING *,RB2
         LR    RW3,13
         ST    13,SAVE+4
         LA    13,SAVE
         ST    13,8(RW3)
*
*
         LM    RW3,RW5,0(1)            LOAD PARAMETERS
         L     RW4,0(RW4)              LOAD ADRESS OF MONRT
         ST    RW4,ADRT
         L     RW5,0(RW5)              LOAD TIME
         ST    RW5,TIME                STORE TIME
         SR    RW10,RW10               CONSTANT 0                 CW014
         LA    RW11,1                  CONSTANT 1                 CW014
         L     RW12,HWMASK             MASK FOR LOAD HALFWORD     CW014
*
*   CHAIN THROUGH CONTROL BLOCKS
*
         L     RW5,76                  CVT POINTER
         L     RW8,356(RW5)            PVT POINTER                CW014
         L     RW8,12(RW8)             APPARENT PFT ORIGIN        CW014
         L     RW5,556(RW5)            ASVT POINTER
         LA    RW5,528(RW5)            FIRST ASCB POINTER
AGAIN    EQU   *
         L     RW6,0(RW5)              ASCB ADDRESS
         LA    RW6,0(RW6)              REMOVE TOP BYTE
         LTR   RW6,RW6                 END OF ASVT Q
         BZ    RETURN
         TM    0(RW5),X'80'            ASCB IN USE?
         BO    INCR
         USING ASCB,RW6                ASCB ADDRESSABILITY        CW011
         CLC   ASCBID,ASCBASCB         ASCB?
         BNE   RETURN
*
*   SNAP OPTION  MOVE DATA FROM ASCB,OUCB
*
         L     RW7,ASCBJBNI            JOB NAME POINTER           CW011
         LTR   RW7,RW7                 INITIATED PROGRAM?
         BNZ   NAMOK
         L     RW7,ASCBJBNS            TASK NAME POINTER          CW011
         LTR   RW7,RW7                 START/MOUNT/LOGON
         BNZ   NAMOK
         LA    RW7,NULL                MOVE IN BLANKS
NAMOK    EQU   *
         MVC   JOBNAME(8),0(RW7)       MOVE JOBNAME
         LH    RW4,ASCBASID            ASID                       CW011
         ST    RW4,ASID
         SR    RW3,RW3
         STH   RW3,ASC$DP              ZERO STORES                CW011
         STH   RW3,OUC$NPG                                        CW011
         ST    RW3,OUC$WMS                                        CW011
         MVC   ASC$SEQN(2),ASCBSEQN    NO IN DISP Q               CW011
         MVC   ASC$DP+1(1),ASCBDP      DISP PRTY                  CW011
         MVC   ASC$EJST(8),ASCBEJST    ELAPSED JOB TIME           CW011
         MVC   ASC$JSTL(4),ASCBJSTL    TOTAL JOB TIME             CW011
         MVC   ASC$FLAG(2),ASCBRCTF    FLAGS                      CW011
         MVC   ASC$SRBT(8),ASCBSRBT    SRB TIME                   CW011
         MVC   ASC$IOSM(2),ASCBIOSM     I/O REQUEST COUNT         CW011
         MVC   ASC$VSC(2),ASCBVSC      VAM SLOT COUNT             CW011
         MVC   ASC$NVSC(2),ASCBNVSC    NON-VAM COUNT              CW011
*
         L     RW7,ASCBOUCB            LOAD OUCB ADDRESS          CW011
         USING OUCB,RW7                OUCB ADDRESSABILITY        CW011
         MVC   OUC$NPG(2),OUCBNPG      PERFORMANCE GROUP NUMBER   CW011
         MVC   OUC$SRC+1(1),OUCBSRC    SWAP REASON CODE           CW011
         MVC   OUC$NQH(1),OUCBNQC      NUMBER OF ENQHOLDS         CW011
         MVC   OUC$DMN(1),OUCBDMN      DOMAIN NUMBER              CW011
         MVC   OUC$WMR(2),OUCBWMR+2    WKLOAD MGR RECOMMEND VALUE CW011
         MVC   OUC$CMRV(4),OUCBCMRV    COMPOSITE RECOMMEND VALUE  CW011
         MVC   OUC$WSS(2),OUCBWSS      WORK SET AT LAST SWAP IN   CW011
         MVC   OUC$PSO(2),OUCBPSO      NUM PAGES LAST SWAPPED OUT CW011
         MVC   OUC$FLAG(1),OUCBYFL     TYPE OF JOB                CW011
         MVC   OUC$FLAG+1(1),OUCBEFL   TYPE OF WAIT               CW011
         MVC   OUC$SWC(2),OUCBSWC      TRANSACTION SWAP COUNT     CW011
         MVC   OUC$FLG2(2),OUCBQFL     SWAP FLAGS                 CW011
         MVC   OUC$FLG3(2),OUCBAFL     ALGORITHM/TRANSACTION FLGS CW011
*        MVC   OUC$CRV(2),OUCBCRV      CPU RECOMMENDATION VALUE   CW011
*        MVC   OUC$IRV(2),OUCBIRV      I/O RECOMMENDATION VALUE   CW011
         MVC   OUC$WMS(4),OUCBWMS      INTERVAL SERVICE           CW011
         L     RW0,ASCBJBNI            BATCH JOBNAME POINTER      CW011
         LTR   RW0,RW0                 ZERO?                      CW011
         BZ    NOTBATCH
         NI    OUC$FLAG,X'BF'          REMOVE START BIT           CW011
NOTBATCH EQU   *
         MVC   OUC$PS1,OUCBPS1         HIGH PART OF PAGE SECONDS  CW011
         TM    OUCBYFL,OUCBLOG         IS THIS A TSO USER?        CW011
         BZ    *+10                    KEEP PAGE SECONDS IF NOT   CW011
         MVC   OUC$TME,OUCBTME         GET LAST RESPONSE TIME     CW011
         STH   RW3,LSQA
         STH   RW3,LOCAL
         STH   RW3,VIO                                            CW014
         STH   RW3,UIC                                            CW014
         MVI   LSQABAD+1,X'F0'         SET TO ALLOW RECOVERY      CW014
         L     RW7,ASCBRSM             REAL STORAGE MGMT HEADER   CW014
         USING RSMHD,RW7               GET ADDRESSABILITY         CW014
         LH    RW0,RSMCNTFX            LOAD FIX COUNT             CW014
         STH   RW0,FIXED               STORE IN OUTPUT RECORD     CW014
         LH    RW0,ASCBFMCT                                       CW014
         STH   RW0,ALLOC                                          CW014
DOLSQA   SR    RW3,RW3                 CLEAR LSQA FRAME COUNTER   CW014
         LH    RW14,RSMLSQAF           INDEX TO FIRST LSQA FRAME  CW014
         NR    RW14,RW12               ARE ANY FRAMES ALLOCATED?  CW014
         BZ    RSMEND                  NO, LEAVE ALL VALUES ZERO  CW014
         L     RW9,ASCBOUXB            LOAD ADDRESS OF OUXB       CW014
         USING OUXB,RW9                                           CW014
         LH    RW0,OUXBUIC             OBTAIN HIGHEST UIC VALUE   CW014
         STH   RW0,UIC                   FOR THIS ADDRESS SPACE   CW014
         SR    RW0,RW0                 SHOW NO PREVIOUS FRAME     CW014
         B     LSQALOOP                ENTER LOOP FOR LSQA FRAMES CW014
         USING MYPFTE,RW9              PFTE DSECT ADDRESSABILITY  CW014
ASIDOK   CLM   RW0,3,PFTBQPTR          CHECK BACKWARD POINTER     CW014
         BNE   LSQABAD                 ABEND IF INVALID CHAIN     CW014
         TM    PFTFLAG1,PFTLSQA        MAKE SURE THIS IS LSQA     CW014
         BZ    LSQABAD                 ABEND IF FRAME ON WRONG Q  CW014
         AR    RW3,RW11                COUNT THIS LSQA FRAME      CW014
         LR    RW0,RW14                CURRENT INDEX BECOMES PREV CW014
         LH    RW14,PFTFQPTR           GET INDEX OF NEXT PFTE     CW014
         NR    RW14,RW12               CLEAR UPPER HALF WORD      CW014
         BZ    LSQAEND                 LSQA SCAN DONE IF ZERO     CW014
LSQALOOP LA    RW9,0(RW14,RW8)         LOAD ADDRESS OF LSQA PFTE  CW014
         CH    RW4,PFTASID             VERIFY ASID                CW014
         BE    ASIDOK                  CONTINUE IF THIS ADDRSPACE CW014
LSQABAD  B     *+6                     SWITCH TO ABEND OR NOT     CW014
         DC    H'0'                    ABEND IF INVALID PFTE      CW014
         MVI   LSQABAD+1,0               OCCURS TWICE IN A ROW    CW014
         B     DOLSQA                  RETRY, CHAINS MAY CHANGE   CW014
LSQAEND  STH   RW3,LSQA                SAVE COUNT OF LSQA FRAMES  CW014
         MVI   PFTEBAD+1,X'F0'         SET TO ALLOW RECOVERY      CW014
DOLOCAL  SR    RW3,RW3                 CLEAR LOCAL FRAME COUNTER  CW014
         LH    RW14,RSMLFQF            LOAD FIRST PFTE INDEX      CW014
         NR    RW14,RW12               ARE ANY FRAMES ALLOCATED?  CW014
         BZ    RSMEND                  NO, LOCAL FRAME COUNT = 0  CW014
         LH    RW15,VIO                LOAD CURRENT VIO COUNTER   CW014
         SR    RW0,RW0                 PREVIOUS PFTE INDEX        CW014
STLOCAL  LA    RW9,0(RW14,RW8)         LOAD ADDRESS OF NEW PFTE   CW014
         CLM   RW0,3,PFTBQPTR          CHECK BACKWARD POINTER     CW014
         BNE   PFTEBAD                 BRANCH IF BROKEN CHAIN     CW014
         CH    RW4,PFTASID             VALIDATE ASID              CW014
         BNE   PFTEBAD                 ABEND IF NOT THIS USER'S   CW014
         CLI   PFTQNDX,PFTLFQN         IS THIS A LOCAL PAGE?      CW014
         BE    GOTLOCAL                BRANCH IF SO               CW014
         TM    PFTFLAG2,PFTIRRG        IS THIS A VIO PAGE?        CW014
         BO    GOTVIO                  ERROR IF ANYTHING ELSE?    CW014
PFTEBAD  B     *+6                     SWITCH TO ABEND OR NOT     CW014
         DC    H'0'                    ABEND IF INVALID PFTE      CW014
         MVI   PFTEBAD+1,0               OCCURS TWICE IN A ROW    CW014
         B     DOLOCAL                 RETRY, CHAINS MAY CHANGE   CW014
GOTVIO   AR    RW15,RW11               INCREMENT VIO COUNT        CW014
         B     GOTLOCAL+2                                         CW014
GOTLOCAL AR    RW3,RW11                INCREMENT LOCAL COUNTER    CW014
         LR    RW0,RW14                CURRENT INDEX BECOMES PREV CW014
         LH    RW14,PFTFQPTR           GET INDEX OF NEXT PFTE     CW014
         NR    RW14,RW12               CLEAR UPPER HALF WORD      CW014
         BNZ   STLOCAL                 CONTINUE WITH NEXT PFTE    CW014
         STH   RW15,VIO                UPDATE VIO COUNTER         CW014
*                                                                 CW014
RSMEND   STH   RW3,LOCAL               DONE COUNTING LOCAL PAGES  CW014
*
**  WRITE OUT RECORD FOR ONE ASID
*
PFTEND   EQU   *
         LA    RW1,ADARGS
         L     RW15,ADRT
         BALR  14,15
*
INCR     EQU   *
         LA    RW5,4(RW5)              INCREMENT ASCB POINTER
         B     AGAIN
*
RETURN   EQU   *
         L     13,4(13)
         LM    14,12,12(13)
         BR    14
*
*   STORAGE AND CONSTANTS
*
LENGTH   DC    A(DATALEN)              RECORD LENGTH FLEXIBILITY  CW011
NAME     DC    CL8'MONS'
IFLAG    DC    F'0'
*
ADARGS   DC    A(NAME)
         DC    A(LENGTH)
         DC    A(TIME)
         DC    A(IFLAG)
*
*
         DS    0D
TIME     DS    F
JOBNAME  DC    C'        '
ASID     DS    F
ASC$EJST DS    D                                                  CW011
ASC$SEQN DS    H                                                  CW011
ASC$DP   DC    H'0'                                               CW011
ASC$JSTL DS    F                                                  CW011
ASC$SRBT DS    D                                                  CW011
ASC$FLAG DS    H                                                  CW011
ALLOC    DS    H
OUC$NPG  DC    H'0'                                               CW011
OUC$WMR  DS    H                                                  CW011
OUC$CMRV DS    F                                                  CW011
OUC$WMS  DS    F                                                  CW011
OUC$WSS  DS    H                                                  CW011
OUC$PSO  DS    H                                                  CW011
OUC$SWC  DS    H                                                  CW011
OUC$FLG2 DS    H                                                  CW011
OUC$FLAG DS    H                                                  CW011
ASC$IOSM DS    H                                                  CW011
ASC$VSC  DS    H                                                  CW011
ASC$NVSC DS    H                                                  CW011
OUC$FLG3 DS    H                                                  CW011
OUC$NQH  DS    HL1                     NUMBER OF ENQUEUE HOLDS    CW011
OUC$DMN  DS    HL1                     DOMAIN NUMBER              CW011
OUC$PS1  DS    0F                HIGH PART OF PAGE SECONDS        CW014
OUC$TME  DS    F                 LAST TSO RESPONSE TIME           CW014
OUC$SRC  DC    H'0'                    SWAP REASON CODE           CW011
LOCAL    DS    H
LSQA     DS    H
FIXED    DS    H
VIO      DS    H                                                  CW014
UIC      DS    H                                                  CW014
*OUC$CRV DS    H                       CPU RECOMMENDATION VALUE   CW011
*OUC$IRV DS    H                       I/O RECOMMENDATION VALUE   CW011
DATALEN  EQU   (*-TIME)/4                                         CW011
*
ADCVT    DS    F
ADRT     DS    F
HWMASK   DC    X'0000FFFF'  KILL SIGN BIT PROPAGATED BY LOAD HALF CW014
ASCBID   DC    C'ASCB'
NULL     DC    C'        '
SAVE     DS    18F
*
         LTORG
         TITLE 'APEMAN SNAPSHOT MONITOR - DSECTS'                 CW011
MYPFTE   IHAPFTE ,                                                CW011
         EJECT                                                    CW011
         IHAASCB ,                                                CW011
         SPACE 3                                                  CW011
         IRAOUCB ,                                                CW011
         SPACE 3                                                  CW011
         IHAOUXB ,                                                CW011
         EJECT                                                    CW011
         IHARSMHD                                                 CW011
         END
