MRD   TITLE 'APEMAN - ASSEMBLY LANGUAGE VERSION OF MRD ROUTINE'
*     SUBROUTINE MRD(ICT,IBUF,IFL)
MRD      PROGRAM  12
         LM    R2,R4,0(R1)    LOAD ADDRESSES OF ARGUMENTS
*     COMMON/OPTION/ISTRM
         L     R7,VCOMMON     LOAD ADDRESS OF OPTION COMMON BLOCK
         LA    R6,MYDCB       LOAD ADDRESS OF DCB
         USING IHADCB,R6      GET DCB ADDRESSABILITY
*     DIMENSION IBUF(1000)
*     IF (IFL .GE. 0) GOTO 5
         CLI   0(R4),0        CHECK FOR NORMAL POSITIVE OR ZERO
         BE    READIT         PROCEED WITH READ IF SO
*     REWIND ISTRM
         TM    MYFLAGS,#OPEN  HAS DATA SET BEEN OPENED YET?
         BZ    READIT         IF NOT, WILL START AT BEGINNING ANYWAY
         CLOSE (MYDCB,REREAD),TYPE=T  DO TCLOSE TO REPOSITION DATA SET
*   5 IFL=0
READIT   SR    R0,R0
         ST    R0,0(,R4)      SET INDICATOR FOR SUCCESSFUL READ
*     READ(ISTRM,1000,END=10) IBUF
         TM    MYFLAGS,#OPEN  HAS DATA SET BEEN OPENED YET?
         BZ    OPENIT         IF NOT, GO TO IT
**                            OPEN SHOULD NOT BE NEEDED AFTER TCLOSE
         CLC   MYDSRN,3(R7)   IS SAME UNIT BEING USED?
         BE    DOREAD         GO DO THE READ IF SO
*                             NOT SURE THIS IS SUPPOSED TO HAPPEN, SO
BOMB     ABEND 1010,DUMP
OPENIT   L     R0,0(,R7)      LOAD CALLER'S DSRN
         STC   R0,MYDSRN      SAVE IT FOR CONTINUITY
         CVD   R0,WORK        COPY IT INTO DDNAME
         UNPK  DCBDDNAM+2(2),WORK
         OI    DCBDDNAM+3,X'F0'    FIX UP LAST DIGIT OF DSRN
         OPEN  (MYDCB,(INPUT))
         TM    DCBOFLGS,X'10' IS THE DCB OPEN?
         BZ    BOMB           GO ABEND IF NOT
         OI    MYFLAGS,#OPEN  INDICATE DATA SET IS OPEN
*1000 FORMAT(4(250A4))
DOREAD   READ  MYDECB,SF,MYDCB,(R3),'S'
         CHECK MYDECB         USER EXPECTS READ TO COMPLETE
*     ICT=1
         LA    R0,1
         ST    R0,0(,R2)      INDICATE START OF BUFFER
*     RETURN
         B     EXIT
*  10 CONTINUE
MYEODAD  EQU   *
*     IFL=1
         LA    R0,1
         ST    R0,0(,R4)      INDICATE END OF FILE REACHED
*     RETURN
         B     EXIT
*     END
MYDSRN   DC    X'00'
MYFLAGS  DC    X'00'
#OPEN    EQU   1
VCOMMON  DC    V(OPTION)
MYDCB    DCB   DDNAME=FTXXF001,DSORG=PS,MACRF=(R),RECFM=F,LRECL=4000,  *
               BLKSIZE=4000,EODAD=MYEODAD
WORK     DS    D              USED FOR CONVERTING DSRN TO DDNAME
         DCBD  DSORG=BS,DEVD=(DA,TA)
         END
