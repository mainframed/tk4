DYNT     TITLE 'M V S   D Y N A M I C   T R A C E   F A C I L I T Y'
*********************************************************************
*                                                                   *
* ------------- G E N E R A L   I N F O R M A T I O N ------------- *
*       THE MVS DYNAMIC TRACE FACILITY IS AN AUTHORIZED PROGRAM     *
*   WHICH, WHEN INVOKED, PROVIDES THE CAPABILITY OF STARTING AND    *
*   STOPPING THE MVS INTERNAL TRACE FEATURE.                        *
*       THIS VERSION OF DYNAMIC TRACE DOES NOT ATTEMPT TO FREE      *
*   THE MAIN STORAGE REQUIRED FOR THE TRACE TABLE WHEN TURNING OFF  *
*   TRACE ACTIVITY, NOR TO REALLOCATE IT IF IT DOES NOT ALREADY     *
*   EXIST WHEN TURNING ON TRACE ACTIVITY.  IT THEREFORE REQUIRES    *
*   THAT THE TRACE WAS LEFT ON AT THE CONCLUSION OF THE IPL         *
*   PROCEDURE, ALTHOUGH IT MAY SINCE HAVE BEEN TURNED OFF.          *
*                                                                   *
*********************************************************************
         EJECT
*********************************************************************
*                                                                   *
* --------------------------- L O G I C --------------------------- *
*                                                                   *
*   1.  REGISTER EQUATES.                                           *
*                                                                   *
*   2.  ESTABLISH BASE REGISTER AND SAVE AREA PROTOCOL.             *
*                                                                   *
*   3.  CHECK THE PARM FIELD FOR 'TURNON' OR 'TURNOFF'.             *
*                                                                   *
*   4.  IF THE PARAMETER IS UNIDENTIFIABLE, WRITE A MESSAGE.        *
*                                                                   *
*   5.  RESTORE REGISTERS, SET RETURN CODE AND RETURN.              *
*                                                                   *
*   6.  IF PARM=TURNOFF   IS SPECIFIED ---                          *
*                                                                   *
*     A.  IF TRACE IS ALREADY TURNED OFF (CVTTRACE=X'07FB'), THEN   *
*         WRITE A MESSAGE TO THE OPERATOR AND RETURN.               *
*                                                                   *
*     B.  IF TRACE IS CURRENTLY TURNED ON (CVTTRACE=X'07FA'), THEN  *
*                                                                   *
*         1.  MODESET TO KEY 0 AND SUPERVISOR STATE.                *
*         2.  VERIFY THAT THE TRACE TABLE POINTERS IN THE PSA       *
*             (FLCTRACE), THE CVT (CVTTRCA), AND THE SECONDARY      *
*             CVT (SCVTRPTR) ARE IN AGREEMENT.                      *
*         3.  SET CVT TRACE FLAG(CVTTRACE) TO OFF STATUS(X'07FB').  *
*         4.  MODESET TO NON-ZERO KEY AND PROBLEM STATE.            *
*         5.  SEND MESSAGE TO THE OPERATOR AND RETURN.              *
*                                                                   *
*   7.  IF PARM=TURNON   IS SPECIFIED ---                           *
*                                                                   *
*     A.  IF TRACE IS ALREADY TURNED ON (CVTTRACE=X'07FA'), THEN    *
*         WRITE A MESSAGE TO THE OPERATOR AND RETURN.               *
*                                                                   *
*     B.  IF TRACE IS CURRENTLY TURNED OFF (CVTTRACE=X'07FB'), THEN *
*                                                                   *
*         1.  MODESET TO KEY 0 AND SUPERVISOR STATE.                *
*         2.  VERIFY THAT THE TRACE TABLE POINTERS IN THE PSA       *
*             (FLCTRACE), THE CVT (CVTTRCA), AND THE SECONDARY      *
*             CVT (SCVTRPTR) ARE IN AGREEMENT.                      *
*         3.  TURN ON THE TRACE FLAG(CVTTRACE=X'07FA') IN THE CVT.  *
*         4.  MODESET TO NON-ZERO KEY AND PROBLEM STATE.            *
*         5.  SEND MESSAGE TO THE OPERATOR AND RETURN.              *
*                                                                   *
*   8.  SAVE AREA                                                   *
*                                                                   *
*   9.  CONSTANTS                                                   *
*                                                                   *
*  10.  MACROS FOR CONTROL BLOCKS                                   *
*                                                                   *
*********************************************************************
         EJECT
*********************************************************************
*                                                                   *
* ------------------ I M P L E M E N T A T I O N ------------------ *
*                                                                   *
*       THE ASSEMBLER LANGUAGE PROGRAM FOLLOWING SHOULD BE LINK-    *
*   EDITED AS AN AUTHORIZED, NON RE-ENTRANT, MODULE INTO AN APF     *
*   AUTHORIZED LIBRARY.                                             *
*                                                                   *
*       THE PROGRAM IS THEN INVOKED AS A STARTED TASK BY ONE OF     *
*   THE FOLLOWING TWO JCL PROCEDURES WHICH SHOULD BE LOCATED IN     *
*   ONE OF THE PROCEDURE LIBRARIES DEFINED TO JES.  THE STEPLIB     *
*   WHICH IN THESE EXAMPLES POINTS TO AN APF AUTHORIZED LIBRARY     *
*   CALLED SYS1.APFLIB SHOULD DEFINE THAT APF AUTHORIZED LIBRARY    *
*   INTO WHICH THIS PROGRAM HAS BEEN LINK-EDITED.                   *
*                                                                   *
*       1.  TO TURN TRACE ON(PROC=TRACEON) -                        *
*                                                                   *
*           //TRACEON  EXEC PGM=DYNTRACE,PARM=TURNON                *
*           //STEPLIB  DD   DSN=SYS1.APFLIB,DISP=SHR                *
*           //SYSABEND DD   SYSOUT=A                                *
*           //SYSPRINT DD   SYSOUT=A                                *
*                                                                   *
*       2.  TO TURN TRACE OFF(PROC=TRACEOFF) -                      *
*                                                                   *
*           //TRACEOFF EXEC PGM=DYNTRACE,PARM=TURNOFF               *
*           //STEPLIB  DD   DSN=SYS1.APFLIB,DISP=SHR                *
*           //SYSABEND DD   SYSOUT=A                                *
*           //SYSPRINT DD   SYSOUT=A                                *
*                                                                   *
*   P L E A S E   N O T E   - - - - - - - - - -                     *
*                                                                   *
*       THE FOLLOWING MACROS ARE REQUIRED - - -                     *
*                                                                   *
*           CVT      IHAPSA   IHASCVT  WTO                          *
*                                                                   *
*********************************************************************
         EJECT
DYNTRACE START 0
*********************************************************************
*                                                                   *
* 1.     *  REGISTER EQUATES.                                       *
*                                                                   *
*********************************************************************
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*********************************************************************
*                                                                   *
* 2.     *  ESTABLISH BASE REGISTER AND SAVE AREA PROTOCOL.         *
*                                                                   *
*********************************************************************
*
         STM   R14,R12,12(R13) SAVE REGS IN PREVIOUS SAVE AREA
         BALR  R2,R0          LOAD R2 AS THE BASE REGISTER
         USING *,R2           ESTABLISH R2 AS THE BASE REGISTER
         ST    R13,SAVE+4     A(PREV. SAVE) INTO CURR. SAVE
         LA    R7,SAVE        R7=A(CURR. SAVE)
         ST    R7,8(R13)      A(CURR. SAVE) INTO PREV. SAVE
         LA    R13,SAVE       R13=A(CURR. SAVE)
*
*********************************************************************
*                                                                   *
* 3.      *  CHECK THE PARM FIELD FOR 'TURNON' OR 'TURNOFF'.        *
*                                                                   *
*********************************************************************
*
         L     R3,16          R3=A(CVT)
         USING XVT,R3         BASE THE CVT
         USING PSA,R0         BASE THE PSA                      CW81034
         CLI   CVTTRACE,X'07' MAKE SURE THERE'S A BR INST       CW81034
         BE    CVTOK          CONTINUE IF SO                    CW81034
         WTO   'TRACE099 - CVTTRACE FIELD UNRECOGNIZABLE',      CW81034X
               ROUTCDE=(2,10,11)    TELL OPERATOR               CW81034
         B     RETURN         AND QUIT                          CW81034
CVTOK    EQU   *              CVT FORMAT HASN'T CHANGED         CW81034
         L     R6,0(R1)       REG 6 IS ADDRESS OF PARM DATA AREA
         LH    R7,0(R6)       REG 7 IS PARM DATA AREA COUNT
         LA    R8,7           R8=7
         SR    R8,R7          MAKE SURE PARM LENGTH IS LESS THAN SEVEN
         BM    BADPARM        BR IF LENGTH .GT. 7
         BCTR  R7,R0          R7=R7-1
         EX    R7,MOVE        MOVE IN PARM FIELD
         CLC   PARM(7),OFF    TURN TRACE OFF ?
         BE    TURNOFF        YES, GO TURN TRACE OFF
         CLC   PARM(6),ON     TURN TRACE ON ?
         BE    TURNON         YES, GO TURN TRACE ON
*
*********************************************************************
*                                                                   *
* 4.     *  IF THE PARAMETER IS UNIDENTIFIABLE, WRITE A MESSAGE.    *
*                                                                   *
*********************************************************************
*
BADPARM  EQU   *
         WTO   'TRACE009 - INVALID PARM SPECIFIED ',ROUTCDE=(2,10,11)
*
*********************************************************************
*                                                                   *
* 5.     *  RESTORE REGISTERS, SET RETURN CODE AND RETURN.          *
*                                                                   *
*********************************************************************
*
RETURN   L     R13,SAVE+4     R13=RETURN ADDR
         LM    R14,R12,12(R13)     RESTORE REGS
         LA    R15,0          SET RETURN CODE TO ZERO
         BR    R14            RETURN
*
*********************************************************************
*                                                                   *
* 6.A    *  IF TRACE IS ALREADY TURNED OFF(CVTTRACE=X'07FB'),       *
*        *  THEN WRITE A MESSAGE TO THE OPERATOR AND RETURN.        *
*                                                                   *
*********************************************************************
*
TURNOFF  EQU   *
         CLI   CVTTRACE+1,ONBYTE   IS TRACE CURRENTLY ON ?      CW81034
         BE    OFF2           YES, GO TURN TRACE OFF            CW81034
         WTO   'TRACE004 - TRACE ALREADY INACTIVE ',ROUTCDE=(2,10,11)
         B     RETURN         RETURN
*
*********************************************************************
*                                                                   *
* 6.B.1     COMPARE THE TRACE TABLE POINTERS FROM THE PSA           *
*           (FLCTRACE), THE CVT (CVTTRCA), AND THE SECONDARY        *
*           CVT (SCVTRPTR).                                         *
*                                                                   *
* 6.B.2     MODESET TO KEY 0 AND SUPERVISOR STATE.                  *
*                                                                   *
*********************************************************************
*
OFF2     BAL   R7,SETUP       DO MODESET AND VERIFY             CW81034
*
*********************************************************************
*                                                                   *
* 6.B.3  *  SET CVT TRACE FLAG(CVTTRACE) TO OFF STATUS(X'07FB').    *
*                                                                   *
*********************************************************************
*
         MVI   CVTTRACE+1,OFFBYTE   CHANGE TRACE STATUS TO OFF  CW81034
*
*********************************************************************
*                                                                   *
* 6.B.4  *  MODESET TO NON-ZERO KEY AND PROBLEM STATE.              *
*                                                                   *
*********************************************************************
*
         MODESET KEY=NZERO,MODE=PROB
*
*********************************************************************
*                                                                   *
* 6.B.5  *  SEND MESSAGE TO OPERATOR AND RETURN.                    *
*                                                                   *
*********************************************************************
*
         WTO   'TRACE002 - TRACE NOW INACTIVE ',ROUTCDE=(2,10,11)
         B     RETURN
*
*********************************************************************
*                                                                   *
*        VERIFY THAT ALL TRACE TABLE POINTERS ARE IN AGREEMENT.     *
*                                                                   *
*********************************************************************
*
SETUP    L     R0,FLCTRACE    LOAD TRACE TABLE POINTER FROM PSA CW81034
SETUPON  C     R0,CVTTRCA     COMPARE WITH POINTER IN CVT       CW81034
         BNE   BADPTRS        POINTERS DISAGREE, CHICKEN OUT    CW81034
         USING SCVTSECT,R1    BASE THE SECONDARY CVT            CW81034
CHEKSCVT L     R1,CVTABEND    LOAD IN SCVT POINTER              CW81034
         C     R0,SCVTRPTR    COMPARE WITH SECONDARY CVT        CW81034
         BNE   BADPTRS        POINTERS DISAGREE, CHICKEN OUT    CW81034
         DROP  R1             RELEASE SCVT BASE                 CW81034
*                                                               CW81034
         MODESET KEY=ZERO,MODE=SUP   VERIFY OK, DO MODESET      CW81034
         BR    R7             RETURN TO CALLER                  CW81034
*                                                               CW81034
BADPTRS  WTO   'TRACE098 - INCONSISTENT TRACE TABLE POINTERS',  CW81034X
               ROUTCDE=(2,10,11)     TELL OPERATOR              CW81034
         B     RETURN         AND GIVE UP                       CW81034
*
*********************************************************************
*                                                                   *
* 7.A    *  IF TRACE IS ALREADY TURNED ON(CVTTRACE=X'07FA'), THEN   *
*        *  WRITE A MESSAGE TO THE OPERATOR AND RETURN.             *
*                                                                   *
*********************************************************************
*
TURNON   EQU   *
         CLI   CVTTRACE+1,ONBYTE      IS TRACE ALREADY ON ?     CW81034
         BNE   ON2            NO, GO TURN IT ON
         WTO   'TRACE003 - TRACE ALREADY ACTIVE ',ROUTCDE=(2,10,11)
         B     RETURN         RETURN
ON2      EQU   *
*
*********************************************************************
*                                                                   *
* 7.B.1     COMPARE THE TRACE TABLE POINTERS FROM THE PSA           *
*           (FLCTRACE), THE CVT (CVTTRCA), AND THE SECONDARY        *
*           CVT (SCVTRPTR).                                         *
*                                                                   *
* 7.B.2     MODESET TO KEY 0 AND SUPERVISOR STATE.                  *
*                                                                   *
*********************************************************************
*
         L     R0,FLCTRACE    LOAD TRACE TABLE POINTER FROM PSA CW81034
         LTR   R0,R0          IS THERE A TRACE TABLE?           CW81034
         BP    FOUNDTBL       CONTINUE IF SO                    CW81034
*                             WE'RE NOT PREPARED TO CREATE ONE  CW81034
         WTO   'TRACE097 - TRACE TABLE NOT FOUND',              CW81034X
               ROUTCDE=(2,10,11)       SO TELL OPERATOR         CW81034
         B     RETURN                  AND GO AWAY              CW81034
*                                                               CW81034
FOUNDTBL BAL   R7,SETUPON     PERFORM VERIFY AND MODESET        CW81034
*
*********************************************************************
*                                                                   *
* 7.B.3  *  TURN ON THE TRACE FLAG(CVTTRACE=X'07FA') IN THE CVT.    *
*                                                                   *
*********************************************************************
*
         MVI   CVTTRACE+1,ONBYTE       TURN TRACE ON            CW81034
*
*********************************************************************
*                                                                   *
* 7.B.4  *  MODESET TO NON-ZERO KEY AND PROBLEM STATE.              *
*                                                                   *
*********************************************************************
*
         MODESET KEY=NZERO,MODE=PROB
*
*********************************************************************
*                                                                   *
* 7.B.5  *  SEND MESSAGE TO THE OPERATOR AND RETURN.                *
*                                                                   *
*********************************************************************
*
         WTO   'TRACE001 - TRACE NOW ACTIVE ',ROUTCDE=(2,10,11)
         B     RETURN
*
*********************************************************************
*                                                                   *
* 8.     *  SAVE AREA                                               *
*                                                                   *
*********************************************************************
*
SAVE     DS    18F            SAVE AREA
*
*********************************************************************
*                                                                   *
* 9.       CONSTANTS AND WORK AREAS                                 *
*                                                                   *
*********************************************************************
*
PARM     DS    CL7            PARM AREA
OFF      DC    C'TURNOFF'
ON       DC    C'TURNON'
OFFBYTE  EQU   X'FB'          TRACE OFF
ONBYTE   EQU   X'FA'          TRACE ON
MOVE     MVC   PARM(0),2(R6)  MOVE FOR PARM FIELD
*
*********************************************************************
*                                                                   *
* 10.    *  MACROS.                                                 *
*                                                                   *
*********************************************************************
*
         EJECT
XVT      CVT   DSECT=YES,LIST=YES
         EJECT
PSA      IHAPSA
         EJECT
SCVTSECT IHASCVT  LIST=YES
         END
