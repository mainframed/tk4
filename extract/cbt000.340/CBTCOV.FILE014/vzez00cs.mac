//Z00302  JOB  (208000,00,5,000),KREBS,CLASS=S,MSGCLASS=X,
//        NOTIFY=Z00302
// EXEC JES3ASM,V=ST1RES
SPYJ     TITLE 'SPYJ - WITH JES3 SUPPORT'
*---------------------------------------------------------------------*
*                                                                     *
*        SPY WITH JES3-SUPPORT                                        *
*                                                                     *
*        ADDED ENHANCEMENT FOR 327X  2/3/4/5 AND EXTENDED COLOR-      *
*        SUPPORT.                                                     *
*                                                                     *
*-------------------------------------------------P.KREBS/11NOV86-----*
         SPACE
         PRINT NOGEN
         GBLB  &BVTAUTH
SPYJ     BVINIT
         L     R8,=V(BUFFER)
         USING BUFFER,R8
         GTTERM PRMSZE=PRIME,ALTSZE=ALT
         BVTAUTH PSCB2SPY
         MVC   USER+3(7),PSCBUSER-PSCB(R1)
         SPACE 2
*        SETUP STAX FOR ATTENTION INTERRUPTIONS
         SPACE
         LA    R3,STAXLIST         R3 = ADDRESS OF STAX LIST MACRO
         STAX  ATTNEXIT,MF=(E,(3)) ATTN EXIT TRAP
         SPACE 2
*        PROCESS ATTENTION
         SPACE
NEXTPAGE EQU   *
         CLI   ATTNFLG,X'FF'       WAS ATTN HIT?
         BNE   NOATTN              NO
         MVI   ATTNFLG,X'00'       YES, RESET FLAG
         MVC   TIME(3),BLANKS      BLANK OUT TIMER FIELD
         MVI   WAITFLG,X'00'       TURN OFF WAIT FLAG
         XC    TIMER,TIMER         SET TIMER TO 0
         TITLE 'COPY WRAP-AROUND-MESSAGE-TABLE OF IATUX31'
*---------------------------------------------------------------------*
*                                                                     *
*        FILL BUFFER AND TPUT                                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
NOATTN   EQU   *
         L     R15,16              CVT-POINTER.
         ICM   R5,15,CVTUSER-CVT(R15) ADDR. OF ZEDVCVT.
         BZ    DONE                NONE , FORGET IT.
         USING ZEDVCVT,R5
         ICM   R6,15,ZIAMBUF       SEE IF THERE IS A BUFFER.
         BZ    DONE                NONE , FORGET IT.
         L     R6,ZIACURR          CURRENT ENTRY
         LA    R7,ZIANBUF          N'ENTRIES IN BUFFER.
         LA    R14,BUF             LINE 1 OF OUTPUT-BUFFER.
BUFLOOP  EQU   *
         MVC   8(ZIAENTL-4,R14),4(R6) TRANSFER TO TPUT BUFFER.
         TR    8(ZIAENTL-4,R14),UPCTBL TRANSLATE UNPRINTABLES.
*        DC    X'2903C06042F44100',CL79' ' LINE 01
*                0 1 2 3 4 5 6 7
*                      H   C   E
         MVI   3(R14),X'60'        LOW    (DEFAULT).
         MVI   5(R14),X'F4'        GREEN  (DEFAULT).
         MVI   7(R14),X'00'        EXT.   (DEFAULT).
         LA    R15,HITAB-(7+3)     - 1 ENTRY.
HILOOP   EQU   *
         LR    R1,R14              SAVE TPUT BUFFER ADDRESS.
         LA    R15,7+3(,R15)       NEXT ENTRY.
         CLI   0(R15),X'FF'        END-OF-TABLE ?
         BE    SETOUT              USE DEFAULTS.
         LA    R2,40               SCAN RANGE.
HILOOPI  EQU   *
         CLC   8(7,R1),3(R15)      MSGID TO BE HIGHLIGTHED ?
         BE    SETHI               YES.
         LA    R1,1(,R1)           NEXT POS.
         BCT   R2,HILOOPI          TRY IF MESSAGE MATCHES.
         B     HILOOP
SETHI    EQU   *
         MVC   3(1,R14),0(R15)     HIGHLIGHTING.
         MVC   5(1,R14),2(R15)     COLOR.
         MVC   7(1,R14),1(R15)     EXTENDED HIGHLIGHTING.
SETOUT   EQU   *
         LA    R14,87(,R14)        NEXT LINE IN TPUT-BUFFER.
         L     R6,0(,R6)           NEXT LINE IN IATUX31-BUFFER.
         BCT   R7,BUFLOOP          PROCESS NEXT LINE.
         CLI   ALT,43
         BE    SET43
         CLI   ALT,24
         BE    SET24
         CLI   ALT,27
         BE    SET24A
         CLI   ALT,32
         BE    SET32
SET24A   EQU   *
         STSIZE SIZE=80,LINE=24
SET24    EQU   *
         MVC   BUFF24-L'WCC(L'WCC),WCC
         LA    R2,((BUFEND-BUFF24)+4)
         LA    R3,BUFF24-L'WCC
         B     TPUT
SET32    EQU   *
         MVC   BUFF32-L'WCC(L'WCC),WCC
         LA    R2,((BUFEND-BUFF32)+4)
         LA    R3,BUFF32-L'WCC
         B     TPUT
SET43    EQU   *
         LA    R2,BUFEND-BUFFER    L'BUFFER.
         LA    R3,BUFFER           A(BUFFER).
TPUT     EQU   *
         TPUT  (R3),(R2),NOEDIT,NOWAIT,NOHOLD
INPUT    EQU   *
         MVC   ERROR,BLANKS        BLANK OUT ERROR FIELD
         CLI   WAITFLG,X'FF'       IS THE WAIT FLAG ON?
         BNE   READCHAR            NO, SO GO GET A COMMAND
         STIMER WAIT,BINTVL=DELAY WAIT FOR DELAY*.01 SECONDS
         L     R2,TIMER            R2 = CURRENT VALUE OF TIMER
         BCTR  R2,0                TIMER = TIMER - 1
         ST    R2,TIMER            STORE NEW VALUE OF TIMER
         CVD   R2,WORK             CONVERT TO DECIMAL.
         MVC   SCRATCH(4),PATTERN  MOVE IN EDIT PATTERN
         ED    SCRATCH(4),WORK+6   EDIT IN CONSOLE NUMBER
         MVC   TIME(3),SCRATCH+1   MOVE TIME LEFT INTO PLACE
         LTR   R2,R2               HAS TIMER HIT ZERO?
         BNZ   NEXTPAGE            NO, CONTINUE TO COUNT
         MVC   TIME(3),BLANKS      CLEAR COUNTER FIELD
         XI    WAITFLG,X'FF'       TOGGLE WAIT FLAG OFF
         B     NEXTPAGE            AND GO ON AS IF NOTHING HAPPENED..
         TITLE 'ANALYZE INPUT'
*---------------------------------------------------------------------*
*                                                                     *
*        ANALYZE INPUT                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
READCHAR EQU   *
         MVC   REPLY,BLANKS        SHOW NO COMMAND
         TGET  REPLYBUF,85,ASIS    GET 79 CHARACTERS FROM TERMINAL
         CH    R15,=H'20'          SEE IF RETURN CODE VALID
         BH    INVALID             NO, BRANCH
         B     BRTAB(R15)          BRANCH TO BRANCH TABLE
BRTAB    B     OK                  RC=0
         B     NEXTPAGE            RC=4  (NO DATA AVAILABLE)
         B     ATTN                RC=8  (ATTENTION WAS HIT)
         B     NEXTPAGE            RC=12 (IGNORE DATA)
         B     INVALID             RC=16 (INVALID TPUT)
         B     IOERROR             RC=20 (TERMINAL DISCONNECTED)
INVALID  EQU   *
         TPUT  ERRMSG9,26          WRITE ERROR MSG
         ABEND 004                 ABEND THE COMMAND
IOERROR  EQU   *
         TPUT  ERRMSG10,26         WRITE ERROR MSG
         ABEND 005                 ABEND THE COMMAND
ATTN     EQU   *
         MVI   ATTNFLG,X'FF'       SET ATTENTION INDICATOR
         B     NEXTPAGE            AND CONTINUE AS IF ENTER PRESSED
OK       EQU   *
         CLI   REPLYBUF,X'7D'      ENTER KEY ?
         BE    NOPFK               YES, SKIP PFK LOGIC
         CLI   REPLYBUF,X'60'      ERROR (NO AID)?
         BE    NEXTPAGE            YES, CONTINUE AS IF ENTER PRESSED
         CLI   REPLYBUF,X'6B'      PA3 KEY ?
         BE    NEXTPAGE            YES, CONTINUE AS IF ENTER PRESSED
         CLI   REPLYBUF,X'6C'      PA1 KEY ?
         BE    ATTN                YES, PROCESS AS IF ATTN
         CLI   REPLYBUF,X'6D'      CLEAR KEY ?
         BE    NEXTPAGE            YES, CONTINUE AS IF ENTER PRESSED
         CLI   REPLYBUF,X'6E'      IS THIS A RESHOW? (VTAM ONLY)
         BE    NEXTPAGE            YES, CONTINUE AS IF ENTER PRESSED
         CLI   REPLYBUF,X'F0'      TEST REQ?
         BE    NEXTPAGE            YES, CONTINUE AS IF ENTER PRESSED
         CLI   REPLYBUF,X'7A'      PFK10 (RECALL) ?
         BE    RECALL              YES, GO DO RECALL COMMAND
         L     R15,=V(PFKTAB1)     ASSUME PFK 1-12.
         TM    REPLYBUF,B'11110000'  IS IT PFK 1-12 ?
         BO    PFLOW               YES
         L     R15,=V(PFKTAB2)     PFK 13-24.
PFLOW    EQU   *
         NI    REPLYBUF,X'0F'      TURN OFF HIGH BYTE
         SLR   R1,R1               CLEAR REGISTER
         IC    R1,REPLYBUF         GET PFK NUMBER
         LTR   R1,R1               TEST PFK NUMBER
         BNP   NEXTPAGE            ZERO OR NEGATIVE, CONTINUE
         BCTR  R1,0                DECREMENT BY 1
         MH    R1,=H'80'           GET OFFSET INTO TABLE
         LA    R1,0(R15,R1)        GET ADDRESS OF COMMAND
         MVC   REPLY,0(R1)         INSERT COMMAND
NOPFK    EQU   *
         MVC   USERLINE,ZEROES     BLANK OUT INPUT FIELD
         CLC   REPLY(2),=C'#T,ST'  SEND-COMMAND ?
         BE    NOOC                YES.
         OC    REPLY(79),BLANKS    CONVERT CHARS TO UPPER CASE
NOOC     EQU   *
         CLI   REPLY,C'$'          LINK REQUEST ?
         BE    LINK                YES
         CLI   REPLY,C' '          JUST A BLANK?
         BE    NEXTPAGE            YES, JUST GO REFRESH
         CLI   REPLY,C'Z'          END ?
         BE    DONE                YES.
         MVC   LASTRPLY,REPLY      SAVE THIS COMMAND FOR RECALL
         MVC   LASTUSER,REPLY      SHOW LAST ENTERED USER COMMAND
         CLI   REPLY,C'?'          IS HE ASKING FOR HELP?
         BE    GETHELP             NO,SO CONTINUE
         CLC   REPLY(2),=C'D='     ARE WE CHANGING THE TIME DELAY?
         BE    CDELAY              NO, SO CONTINUE
         CLC   REPLY(2),=C'#T,ST'  SEND-COMMAND ?
         BE    SENDIT              YES.
         CLC   REPLY(4),=C'SWAP'   XCTL TO SPY ?
         BE    SWAP                NO, SO CONTINUE
         CLC   REPLY(6),=C'RECALL' RECALL COMMAND ?
         BE    RECALL              YES, GO DO IT
         CLC   REPLY(3),=C'RCL'    RECALL COMMAND (SHORT FORM) ?
         BE    RECALL              NO, CHECK FOR WAIT COMMAND
         CLC   REPLY(2),=C'W0'     DO WE SHIFT TO WAIT MODE?
         BE    CHEKWAIT            NO, SO CONTINUE
         CLC   REPLY(2),=C'SH'     SHORT SCREEN ?
         BE    SHORT               NO, SO CONTINUE
         B     JES3                ASSUME JES3 COMMAND.
RECALL   EQU   *
         MVC   LASTUSER,LASTRPLY   RESTORE LAST ENTERED COMMAND
         MVC   USERLINE(L'LASTRPLY),LASTRPLY   RECALL LAST COMMAND
         B     NEXTPAGE            GIVE OPER THE CHANCE TO CHANGE
         SPACE
SENDIT   EQU   *
         MVC   USERLINE(L'LASTRPLY),LASTRPLY   RECALL LAST COMMAND
         MVI   USERLINE,C'*'       SET JES3-IDENTIFIER.
         B     NEXTPAGE            GIVE OPER THE CHANCE TO CHANGE
LINK     EQU   *
         LA    R1,REPLY+1
         LINK  EPLOC=(R1)
         B     NEXTPAGE
SHORT    EQU   *
         MVI   ALT,24
         B     SET24
         TITLE 'SWAP TO SPY'
*---------------------------------------------------------------------*
*                                                                     *
*        XCTL TO SPY                                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
SWAP     EQU   *
         LA    R12,SWAP                NEW BASE.
         L     R13,SAVEAREA+4          UNCHAIN.
         PUSH  USING
         DROP  R13
         USING SWAP,R12
         XCTL  (2,12),EP=SPY
         POP   USING
         TITLE 'SET UP WAIT MODE'
*---------------------------------------------------------------------*
*                                                                     *
*        WAIT  --  ENTER WAIT MODE                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CHEKWAIT EQU   *
         LA    R1,REPLY+2               LOAD START ADDRESS OF VALUE.
         XI    WAITFLG,X'FF'            TURN ON WAIT FLAG.
         L     R2,=A(9999)              SET  VALUE.
         ST    R2,TIMER                 STORE STARTING TIMER VALUE.
         B     NEXTPAGE                 GO DISPLAY NEXT PAGE.
         TITLE 'SET TIMER DELAY'
*---------------------------------------------------------------------*
*                                                                     *
*        DELAY  --  SET TIMER DELAY IN TENTHS OF A SECOND             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CDELAY   EQU   *
         LA    R1,REPLY+2               LOAD START ADDRESS OF VALUE.
         LA    R2,10                    DEFAULT VALUE = 10 TENTHS SEC.
         LA    R15,CONVBIN              BRANCH TO CONVERSION RTN
         BALR  R14,R15                  EBCDIC TO BINARY.
         MVC   SCRATCH(5),DPATTRN       MOVE IN EDIT PATTERN.
         ED    SCRATCH(5),WORK+6        EDIT IN DELAY TIME.
         MVC   PAUSE(3),SCRATCH+2       MOVE TIME LEFT INTO PLACE.
         MH    R2,=H'10'                CONVERT TO 100THS OF A SECOND.
         ST    R2,DELAY                 STORE WAIT DELAY VALUE.
         B     NEXTPAGE                 GO DISPLAY NEXT PAGE.
         TITLE 'DISPLAY HELP'
*---------------------------------------------------------------------*
*                                                                     *
*        HELP REQUESTED                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
GETHELP  EQU   *
         L     R1,=V(HELP)              R1 = ADDR OF HELP PAGE.
         LA    R0,HLENGTH               R0 = LENGTH OF HELP PAGE.
         TPUT  (1),(0),FULLSCR,NOWAIT   DISPLAY HELP.
         B     READCHAR
         TITLE 'ISSUE JES3 COMMANDS'
*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE JES3 COMMAND                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
JES3     EQU   *
         CLC   =C'*RETURN',REPLY        REJECT THIS ONE.
         BE    JES3NOK                  AND
         CLC   =C'RETURN',REPLY         THIS ONE.
         BE    JES3NOK
         B     JES3OK
JES3NOK  EQU   *
         MVC   ERROR,ERRMSG3            COMMAND WAS INVALID
         B     NEXTPAGE                 START AGAIN.
JES3OK   EQU   *
         MVC   JESCMD,REPLY
*-------------------------------------------------------------------*
*        ISSUE JES COMMAND THROUGH SUBSYSTEM INTERFACE              *
*-------------------------------------------------------------------*
         MODESET KEY=ZERO,MODE=SUP
         SETAMODE R15,AMODE=31
         USING SSVT,R15
         L     R15,16              POINT TO CVT
         L     R15,CVTJESCT-CVT(,R15)       JES CONTROL TABLE
         L     R15,JESSSCT-JESCT(,R15)      SUBSYSTEM CVT
         L     R15,SSCTSSVT-SSCT(,R15)      SUBSYS VECTOR TABLE
         SSISERV FUNC=SSOBCMND,PARM=SELAREA,MOD=MODINTCM
         SETAMODE R15,AMODE=24
         MODESET KEY=NZERO,MODE=PROB
         STIMER WAIT,DINTVL=WAIT05 WAIT FOR REFRESH DONE.
         B     NEXTPAGE                 REFRESH SCREEN.
         TITLE 'EXIT'
*---------------------------------------------------------------------*
*                                                                     *
*        EXIT                                                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
DONE     DS    0H
         LINK  EP=CLEAR
         B     RETURN              RETURN TO SYSTEM
         TITLE 'CONVERT EBCDIC TO BINARY'
*---------------------------------------------------------------------*
*                                                                     *
*        CONVERT EBCDIC TO BINARY                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CONVBIN  EQU   *
         CVD   R2,WORK             CONVERT TO DECIMAL.
         CLI   0(R1),C' '          DID HE ENTER A NUMBER?
         BER   R14                 NO, USE THE DEFAULT
         CLI   0(R1),C'0'          IS THE HEX CODE < 'F0' ?
         BL    BADCHAR             YES, ERROR
         CLI   0(R1),C'9'          IS THE HEX CODE > 'F9' ?
         BH    BADCHAR             YES, ERROR
         PACK  WORK(8),0(1,R1)     PACK EBCDIC (ASSUME 1 DIGIT)
         CLI   1(R1),C' '          DID HE ENTER 2 DIGITS?
         BE    CVB                 NO, DONT DO THE 2 DIGIT PACK
         CLI   1(R1),C'0'          IS THE HEX CODE < 'F0' ?
         BL    BADCHAR             YES, ERROR
         CLI   1(R1),C'9'          IS THE HEX CODE > 'F9' ?
         BH    BADCHAR             YES, ERROR
         PACK  WORK(8),0(2,R1)     PACK AGAIN, WITH 2 DIGITS THIS TIME
CVB      EQU   *
         CVB   R2,WORK             GET BINARY
         BR    R14                 RETURN TO MAINLINE
BADCHAR  EQU   *
         MVC   ERROR(L'ERROR),ERRMSG2   CONSOLE NUMBER ERROR
         BR    R14                 RETURN TO MAINLINE
         TITLE 'ATTENTION EXIT'
*---------------------------------------------------------------------*
*                                                                     *
*        ATTENTION EXIT                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ATTNEXIT EQU   *
         LR    R7,R15              ESTABLISH
         USING ATTNEXIT,R7         ADDRESSABILITY.
         MVI   ATTNFLG,X'FF'       SET ATTN FLAG
         BR    R14                 RETURN TO CALLER
         DROP  R7
         TITLE 'SSISERV WORK FIELDS'
*---------------------------------------------------------------------*
*                                                                     *
*        SSISERV WORK-FIELDS                                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
JESWORK  DS    0A
         DC    AL2(79)
JESCMD   DC    CL80' '
         DS    0A
SELAREA  DC    XL(L'SELSIZE)'00'
         ORG   (SELDATA-SELSTART)+SELAREA
         DC    A(JESWORK)
         ORG
         TITLE 'NUTS AND BOLTS'                                  ERZ-BV
*---------------------------------------------------------------------*
*                                                                     *
*        NUTS AND BOLTS'                                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
APARNUM  DC    CL8' '
WORK     DC    D'0'                WORK AREA FOR PACKS
SCRATCH  DC    D'0'                SCRATCH AREA FOR CHAR. MANIP
PRIME    DC    A(0)                PRIMARY SIZE.
ALT      DC    A(0)                ALTERN. SIZE.
WAIT05   DC    C'00000050'         0,5   SECOND.
TIMER    DC    F'30'               SECONDS LEFT ON TIMER
DELAY    DC    F'300'              DELAY FOR 100 HUNDREDTHS OF A SECOND
ATTNFLG  DC    X'00'               X'FF' INDICATES ATTN WAS TRAPPED
WAITFLG  DC    X'00'               X'00' INDICATES NOT IN WAIT MODE
PATTERN  DC    X'40202020'         EDIT PATTERN FIELD
DPATTRN  DC    X'4021204B20'       EDIT PATTERN FIELD
REPLYBUF DS    0CL85               TGET REPLY BUFFER
REPLYAID DS    CL6                 TGET ATTENTION IDENTIFICATION VALUES
REPLY    DC    CL79' '             USERS COMMAND INPUT FIELD
LASTRPLY DC    CL79' '             USERS LAST COMMAND INPUT
PAD      DC    C' '                PAD CHARACTER FOR MOVEBUFF MVC
STAXLIST STAX  ATTNEXIT,MF=L
BLANKS   DC    CL80' '                  FOR CLEAR.
ZEROES   DC    XL80'00'                 FOR CLEAR.
CNSLNUM  DC    C'M'
         SPACE
ERRMSG2  DC    CL26'ERROR - NON-NUMERIC VALUE '
ERRMSG3  DC    CL26'ERROR - INVALID COMMAND   '
ERRMSG9  DC    CL26'INVALID TGET COMMAND      '
ERRMSG10 DC    CL26'TERMINAL DISCONNECTED     '
         SPACE
HITAB    EQU   *
*                H E C                  H=HIGHLIGHTING , C=COLOR.
*                × × ×                  E=EXTENDED HIGHLIGHTING.
*                × × ×
USER     DC    X'E800F3',CL7'       '   CURRENT USERID.
         DC    X'E8F1F2',CL7'IAT1017'   SPOOL SPACE.
         DC    X'E800F7',CL7'IAT5210'   MOUNT.
         DC    X'E800F7',CL7'IAT5310'   RE-MOUNT.
         DC    X'E800F7',CL7'IAT5624'   MOUNT.
         DC    X'E8F1F1',CL7'IAT7847'   INTV. REQ.
         DC    X'E80000',CL7'IAT7851'   CANCEL.
         DC    X'E800F1',CL7'IAT7030'   PRINTER SETUP.
         DC    X'E8F1F2',CL7'IAT8572'   NO RESPONSE.
         DC    X'E800F7',CL7'IEC501A'   OS MOUNT.
         DC    X'E800F2',CL7'IEC509A'   FILE PROTECTED.
         DC    X'E800F7',CL7'IEC701D'   TAPE LABEL.
         DC    X'E800F7',CL7'IECTMS1'   TMS.
         DC    X'E800F7',CL7'IECTMS2'   TMS.
         DC    X'E800F7',CL7'IECTMS4'   TMS.
         DC    X'E800F2',CL7'IEF099I'   WAITING FOR DATASETS.
         DC    X'E800F7',CL7'IEF233A'   MOUNT.
         DC    X'E800F7',CL7'TMS002 '   MOUNT.
         DC    X'E800F7',CL7'TMS005 '   MOUNT.
         DC    X'E8F1F1',CL7'IOS003A'   INTERVENTION REQUIRED.
         DC    X'E8F1F1',CL7'UNITS: '   READY UNITS.
         DC    X'E8F2F2',CL7'ABEND C'   ABEND CODE.
         DC    X'E800F7',CL7'O VOLUM'   ALLOCATION FAILED.
         DC    X'E800F5',CL7'FASTRAC'   INSTORAGE PROFILE MANIPULATION.
         DC    7X'FF'                   EOT-INDICATOR.
         TITLE 'UNPRINTABLES GO TO ?'
*---------------------------------------------------------------------*
*                                                                     *
*        TRANSLATE TABLE                                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
UPCTBL   DC    256C'?'                  INVALIDS GO TO '??????'.
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C'Ö'
         DC    C'Ö.<(+×&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);^-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+X'A2'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         TITLE 'HELP FRAME'
*---------------------------------------------------------------------*
*                                                                     *
*        HELP FRAME                                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
HELP     CSECT
         DC    X'C1'               WCC
         DC    X'114040'           SBA TO ROW 1, COL 1
         DC    X'3C404000'         FILL SCREEN WITH NULLS
         DC    X'114040'
         DC    X'1DE8',CL79' HELP FRAME'
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79' D=XX       SET DELAY TO XX TENTHS SECONDS'
         DC    X'1D60',CL79' Z          EXIT                          '
         DC    X'1D60',CL79' RECALL     RECALL LAST COMMAND FOR UPDATE'
         DC    X'1D60',CL79' W0         START UNLIMITED TIMER MODE    '
         DC    X'1D60',CL79' ?          DISPLAY HELP                  '
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79'HITTING ATTENTION WILL STOP THE WAIT TIMER'
         DC    X'1D60' CL79' '
         DC    X'1D60',CL79' PFK 1=HELP    PFK 2=SWAP     PFK 3=Z     '
         DC    X'1D60',CL79' PFK 4=*I A    PFK 5=*I R,S   PFK 6=*I S,A'
         DC    X'1D60',CL79' PFK 7=*I R    PFK 8=#T,ST1,  PFK 9=SWAP  '
         DC    X'1D60',CL79' PFK10=RECALL  PFK11=*I,Q,S   PFK12=      '
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79' PFK13=$RMFMON PFK14=SWAP     PFK15=Z     '
         DC    X'1D60',CL79' PFK16=*I A    PFK17=*I R,S   PFK18=*I S,A'
         DC    X'1D60',CL79' PFK19=*I R    PFK20=#T,ST1,  PFK21=SWAP  '
         DC    X'1D60',CL79' PFK22=RECALL  PFK23=D=30     PFK24=W0    '
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79' NOTE : TO EXECUTE MVS-COMMANDS USE PFK8  '
         DC    X'1D60',CL79'        TO EXECUTE JES-COMMANDS DONT USE *'
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79' '
         DC    X'1D60',CL79'HIT ENTER TO CONTINUE'
         DC    X'1D40'
         DC    X'1340'
         DC    X'1DE8'
HLENGTH  EQU   *-HELP              LENGTH OF HELP TPUT
         TITLE 'OUTPUT-BUFFER'
*---------------------------------------------------------------------*
*                                                                     *
*        OUTPUT BUFFER                                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
BUFFER   CSECT
WCC      DC    X'F1C3114040'
BUF      EQU   *
         DC    X'2903C06042F44100',CL79' ' LINE 01
         DC    X'2903C06042F44100',CL79' ' LINE 02
         DC    X'2903C06042F44100',CL79' ' LINE 03
         DC    X'2903C06042F44100',CL79' ' LINE 04
         DC    X'2903C06042F44100',CL79' ' LINE 05
         DC    X'2903C06042F44100',CL79' ' LINE 06
         DC    X'2903C06042F44100',CL79' ' LINE 07
         DC    X'2903C06042F44100',CL79' ' LINE 08
         DC    X'2903C06042F44100',CL79' ' LINE 09
         DC    X'2903C06042F44100',CL79' ' LINE 10
         DC    X'2903C06042F44100',CL79' ' LINE 11
BUFF32   DC    X'2903C06042F44100',CL79' ' LINE 12
         DC    X'2903C06042F44100',CL79' ' LINE 13
         DC    X'2903C06042F44100',CL79' ' LINE 14
         DC    X'2903C06042F44100',CL79' ' LINE 15
         DC    X'2903C06042F44100',CL79' ' LINE 16
         DC    X'2903C06042F44100',CL79' ' LINE 17
         DC    X'2903C06042F44100',CL79' ' LINE 18
         DC    X'2903C06042F44100',CL79' ' LINE 19
BUFF24   DC    X'2903C06042F44100',CL79' ' LINE 20
         DC    X'2903C06042F44100',CL79' ' LINE 21
         DC    X'2903C06042F44100',CL79' ' LINE 22
         DC    X'2903C06042F44100',CL79' ' LINE 23
         DC    X'2903C06042F44100',CL79' ' LINE 24
         DC    X'2903C06042F44100',CL79' ' LINE 25
         DC    X'2903C06042F44100',CL79' ' LINE 26
         DC    X'2903C06042F44100',CL79' ' LINE 27
         DC    X'2903C06042F44100',CL79' ' LINE 28
         DC    X'2903C06042F44100',CL79' ' LINE 29
         DC    X'2903C06042F44100',CL79' ' LINE 30
         DC    X'2903C06042F44100',CL79' ' LINE 31
         DC    X'2903C06042F44100',CL79' ' LINE 32
         DC    X'2903C06042F44100',CL79' ' LINE 33
         DC    X'2903C06042F44100',CL79' ' LINE 34
         DC    X'2903C06042F44100',CL79' ' LINE 35
         DC    X'2903C06042F44100',CL79' ' LINE 36
         DC    X'2903C06042F44100',CL79' ' LINE 37
         DC    X'2903C06042F44100',CL79' ' LINE 38
         DC    X'2903C06042F44100',CL79' ' LINE 39
         DC    X'2903C06042F44100',CL79' ' LINE 40
         DC    X'2903C06042F44100',CL79' ' LINE 41
         SPACE
         DC    X'2903C0E842F641F4'      LINE 42.
L42      DC    CL79' '
         ORG   L42
         DC    CL24' '
LASTUSER DC    CL40' '
         ORG   LASTUSER
ERROR    DC    CL40' '                  ERROR MSG FIELD
         DC    CL8'TIMER:  '
TIME     DC    CL3' '                   SECONDS REMAINING ON TIMER
SLASH    DC    CL1'/'
PAUSE    DC    CL3'3.0'                 DELAY IN SECONDS
         ORG
         SPACE
         DC    X'2903C04042F74100'      LINE 43.
         DC    X'13'                    INSERT CURSOR
USERLINE DC    XL79'00'                 USERS COMMAND INPUT LINE
BUFEND   EQU   *
         TITLE 'PFK-TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*        PFK-TABLE                                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
PFKTAB1  CSECT
         MACRO
&N       PFK   &A,&B
&N       DC    XL80'00'
         ORG   &N
         DC    C&A
         AIF   (T'&B EQ 'O').A1
         DC    X&B
.A1      ANOP
         ORG
         MEND
PFK1     PFK   '?'
PFK2     PFK   'SWAP'
PFK3     PFK   'Z'
PFK4     PFK   '*I A'
PFK5     PFK   '*I R,S'
PFK6     PFK   '*I S,A'
PFK7     PFK   '*I,R'
         PRINT GEN
PFK8     PFK   '#T,ST1,','13'
PFK9     PFK   'SWAP'
PFK10    PFK   'RECALL'
PFK11    PFK   '*I,Q,S'
PFK12    PFK   ' '
*
PFKTAB2  CSECT
PFK13    PFK   '$RMFMON'
PFK14    PFK   'SWAP'
PFK15    PFK   '$CMDSBTSO'
PFK16    PFK   '*I A'
PFK17    PFK   '*I R,S'
PFK18    PFK   '*I S,A'
PFK19    PFK   '*I,R'
PFK20    PFK   '$LOCK'
PFK21    PFK   '$LOCK'
PFK22    PFK   'RECALL'
PFK23    PFK   'D=30'
PFK24    PFK   'W0'
SPYJ     CSECT
         LTORG
         TITLE 'DSECTS'
*---------------------------------------------------------------------*
*                                                                     *
*        DUMMY SECTIONS                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         PRINT NOGEN
         IATYDST
         IATYSEL
         IATYSVT
         IEFJESCT
         IEFJSCVT
         IEFJSSOB CM
         IKJCPPL
         CVT   DSECT=YES
         ZEDVCVT
         AIF   (&BVTAUTH).NOGEN
         IKJPSCB
.NOGEN   ANOP
         END
//L.SYSLMOD DD DSN=SYS1.CMDLIB1,DISP=SHR
  SETCODE AC(1)
  NAME SPYJ(R)
