* ./  NAME=KB90SCAN
         MACRO
&LBL     KB90SCAN  &FROMR=,&TO=,&MAXL=,&MIN=,&MAX=,                    *
               &NUM=N,&DELIM=',',                                      *
               &GLOB=Y,&LADR=,                                         *
               &ERR=,&NERR=,&LERR=,&SERR=
         AGO   .STRT
***********************************************************************
*/*KB90SCAN    EXTRACT AND/OR TEST DATA FROM PARAMETER STRINGS        *
*                                                                     *
*1 FUNCTION    EIN PARAMETER-STRING WIRD AUF VERSCHIEDENE KRITERIEN   *
*              GETESTET UND IN MEHRERE TEILE ZERLEGT/KONVERTIERT.     *
*                                                                     *
*2 SUMMARY                                                            *
*  TYPE        MACRO     BATCH          SYS2.COPYLIB                  *
*  ACCOUNT     90                       SYSTEMPROGRAMMIERUNG          *
*  HISTORY     1981.0924 HE   GRUNDVERSION                            *
*                                                                     *
*3 USAGE       - KB90SCAN FROMR=2,TO=FIELD,LERR=ERROR,DELIM=' '       *
**/                                                                   *
***********************************************************************
.*
.*       WHEN  'NUM=Y'  SPECIFIED THE DATA IS TESTED FOR  NUMERIC AND
.*       OPTIONALLY  CHECKED FOR VALID RANGE OF VALUES. THE VALUE MAY
.*       ALSO BE CONVERTED TO BINARY.
.*
.*       THE DELIMITER AND MAX/MIN VALUE  PARAMETERS MAY BE SPECIFIED
.*       AS LABELS OF DC INSTRUCTIONS THUS PERMITTING ONE 'KB90SCAN'-
.*       MACRO TO BE USED FOR VARYING REQUIREMENTS.
.*
.*       'KB90SCAN'  SAVES THE LAST ADDRESS  SCANNED  AND ON THE NEXT
.*       ENTRY AUTOMATICALLY  INCREASES THE  START-ADDRESS BY 1 IF IT
.*       IS THE SAME  AS THE LAST-SCAN  ADDRESS.  THE LAST-SCAN-SAVE-
.*       FIELD IS BY DEFAULT  DEFINED GLOBALLY  (ONCE ONLY),  SO THAT
.*       MULTIPLE 'KB90SCAN'- MACRO  DEFINITIONS MAY BE USED UPON THE
.*       SAME INPUT.  IF THIS IS NOT DESIRED BECAUSE  THERE MAY BE NO
.*       CONNECTION  BETWEEN ONE 'KB90SCAN'  USAGE AND OTHERS SPECIFY
.*       'GLOB=N' AND A 'LOCAL' SAVE FIELD WILL BE GENERATED.  A NAME
.*       MAY BE ASSIGNED TO THE LAST-SCAN SAVE FIELD.
.*
.*       OUTPUT -  REG. 1 CONTAINS THE  PARAMETER-LENGTH JUST SCANNED
.*                 'FROMR'  WILL POINT TO THE LAST  DELIMITER SCANNED
.*                 OR TO THE LAST CHAR.  CAUSING A LENGTH  OR NUMERIC
.*                 ERROR. IF  'TO'  SPECIFIED THE PARAMETER IS MOVED/
.*                 CONVERTED ACCORDING TO THE RULES UNDER 'TO'.
.*
.*       FROMR  -  MUST SPECIFY A GENERAL REG. (2-12)  CONTAINING THE
.*                 START-SCAN-ADDRESS.  MAY BE DEFINED  SYMBOLICALLY.
.*
.*       TO     -  IF OMITTED TESTS MADE AND LENGTH IS SET IN REG. 1.
.*                 IF LABEL OF A DC/DS STATEMENT-
.*                      NUM=N     DATA MOVED TO FIELD
.*                      NUM=Y     DATA PACKED INTO FIELD
.*                 OTHERWISE 'TO' ASSUMED TO BE A REGISTER-
.*                      NUM=N     DATA MOVED TO THIS ADDRESS
.*                      NUM=Y     VALUE  CONVERTED TO BINARY
.*                                (BEFORE RANGE TESTS)
.*
.*       NUM    -  IF  'Y'  DOES  NUMERIC TEST.  SEE RULES  FOR 'TO'.
.*
.*       MAXL   -  DIGITS SPECIFYING THE MAXIMUM  LENGTH OF PARAMETER
.*                 DEFAULTS- NUM=Y               16
.*                           TO=REGISTER        100
.*                           TO=LABEL       L'LABEL
.*                 MAY ALSO BE THE LABEL OF A HALFW. DS/DC STATEMENT.
.*
.*       DELIM  -  SPECIFY 1 OR MORE STOP-SCAN  CHARACTERS OR NAMES A
.*                 DS/DC STATEMENT  WHICH ALL OF WHOSE  VALUES APPLY.
.*                 EG. DELIM=('(',' ',')','=') OR  DEL  DC   C'(, )='
.*
.*       MIN/MAX-  DIGITS SPECIFYING THE  MIN AND/OR MAX  PERMISSABLE
.*                 VALUES OF A NUMERIC PARM.  MAY NAME A PACKED FIELD
.*                 CONTAINING THE RESPECTIVE VALUE. THIS FIELD MAY BE
.*                 SPECIFIED ALONE OR IN COMBINATION.
.*
.*       ERR    -  NAMES A  GENERAL ERROR ROUTINE  FOR ALL ERRORS NOT
.*                 HANDLED BY 'NERR', 'LERR', OR 'SERR'.
.*
.*       NERR      NON NUMERIC                 ERROR HANDLING ROUTINE
.*       LERR      EXCESSIVE PARM LENGTH       ERROR HANDLING ROUTINE
.*       SERR      MIN/MAX VIOLATION           ERROR HANDLING ROUTINE
.*
.*       *NB*   -  ALL POSSIBLE  ERRORS  (DEPENDING ON  NUM= MIN/MAX)
.*                 MUST BE COVERED BY A SPECIFIC ROUTINE OR A GENERAL
.*                 ERROR ROUTINE (ERR=).
.*
.*       GLOB      DEFAULT   'GLOB=Y'            SEE DISCUSSION ABOVE
.*
.*       LADR      NAME TO BE ASSIGNED TO THE  LAST-SCAN-ADDRESS SAVE
.*                 FIELD.  IGNORED IF A GLOBAL NAME  ALREADY ASSIGNED
.*                 AND 'GLOB=Y' IS DEFINED OR DEFAULTED.
.*
.*       ************************************************************
.*       *         E N D    O F    D E S C R I P T I O N            *
.*       ************************************************************
.*
.STRT    ANOP
         GBLC  &Z
         LCLA  &D,&L
         LCLB  &F
         LCLC  &NE,&LE,&SE
         LCLC  &X,&Y,&ML
&X       SETC  '&SYSNDX'                INDEX
&ML      SETC  'GPML&X'
         AIF   (T'&MAXL NE 'H').A001
&ML      SETC  '&MAXL'
.A001    AIF   (T'&LADR EQ 'O').A002
&Y       SETC  '&LADR'                  LAST ADDRESS SCANNED
         AGO   .A003
.A002    ANOP
&Y       SETC  'GPLS&X'
.A003    AIF   ('&Z' NE '').A004
&Z       SETC  '&Y'
&F       SETB  1
.A004    AIF   ('&GLOB' EQ 'N').A005
&Y       SETC  '&Z'
.A005    AIF   (T'&FROMR EQ 'O').E001
         AIF   (T'&ERR EQ 'O').A006
&NE      SETC  '&ERR'
&LE      SETC  '&ERR'
&SE      SETC  '&ERR'
.A006    AIF   (T'&NERR EQ 'O').A007
&NE      SETC  '&NERR'
.A007    AIF   (T'&LERR EQ 'O').A008
&LE      SETC  '&LERR'
.A008    AIF   (T'&SERR EQ 'O').A009
&SE      SETC  '&SERR'
.A009    AIF   (T'&ERR NE 'O').A101
         AIF   ('&NUM' EQ 'Y').A010
         AIF   (T'&LERR NE 'O').A101
         AGO   .E002
.A010    AIF   (T'&NERR EQ 'O').E003
         AIF   (T'&MIN EQ 'O' AND T'&MAX EQ 'O').A101
         AIF   (T'&SERR EQ 'O').E004
.A101    ANOP                           PROCESSING ROUTINE
&LBL     SR    1,1                      CLEAR OPERAND CHARACTER COUNT
         C     &FROMR,&Y                STILL POINTING LAST DELIMITER
         BNE   *+8                      NO
         LA    &FROMR,1(&FROMR)         PAST DELIMITER
         AIF   ('&NUM' EQ 'Y').A201
.*
.*       ****************************   NUM=NO    *****************
.*
         AIF   (T'&TO EQ 'O').A103
         AIF   (T'&TO NE 'U' AND T'&TO NE 'N').A102
         LR    15,&TO                   RECEIVING FIELD POINTER
         AGO   .A103
.A102    ANOP
         LA    15,&TO                   RECEIVING FIELD ADDRESS
.A103    ANOP
         STM   13,14,GPSV&X             SAVE R13 R14
         AIF   (T'&DELIM NE 'C' AND T'&DELIM NE 'X').A104
&D       SETA  L'&DELIM
IHE&X.A  LA    14,&D                    COUNT POSSIBLE DELIMITERS
         LA    13,&DELIM                POINT 1ST DELIMITER
IHE&X.B  CLC   0(1,&FROMR),0(13)        DELIMITER FOUND
         BE    IHE&X.X                  YES, DONE
         LA    13,1(13)                 NEXT DELIMITER
         BCT   14,IHE&X.B               CHECK NEXT DELIM IF ANY
         AGO   .A106
.A104    ANOP
&D       SETA  1
IHE&X.C  EQU   *
.A105    AIF   (&D GT N'&DELIM).A106
         CLI   0(&FROMR),C&DELIM(&D)    DELIMITER FOUND
         BE    IHE&X.X                  YES, DONE
&D       SETA  &D+1
         AGO   .A105
.A106    ANOP
         CH    1,&ML                    MAXIMUM LENGTH REACHED
         BL    IHE&X.D                  NO, OK
         ST    &FROMR,&Y                YES, SAVE SCANADDRESS
         LM    13,14,GPSV&X             RESTORE R13 R14
         B     &LE                      TO LENGTH-ERROR HANDLER
IHE&X.D  EQU   *
         AIF   (T'&TO EQ 'O').A107
         MVC   0(1,15),0(&FROMR)        COPY 1 CHARACTER
         LA    15,1(15)                 OUT ADDRESS +1
.A107    ANOP
         LA    &FROMR,1(&FROMR)         PARM ADDRESS +1
         LA    1,1(1)                   COUNT 1 BYTE
         B     IHE&X.C                  GO, DO NEXT CHARACTER
         AGO   .A901
.*
.*       **************************     NUM=YES    ****************
.*
.A201    ANOP
         LR    0,&FROMR                 SAVE STARTING LOCATION
         STM   13,14,GPSV&X             SAVE R13 R14
         AIF   (T'&DELIM NE 'C' AND T'&DELIM NE 'X').A202
&D       SETA  L'&DELIM
IHE&X.E  LA    14,&D                    COUNT POSSIBLE DELIMITERS
         LA    13,&DELIM                POINT 1ST DELIMITER
IHE&X.F  CLC   0(1,&FROMR),0(13)        DELIMITER FOUND
         BE    IHE&X.K                  YES
         LA    13,1(13)                 NEXT DELIMITER
         BCT   14,IHE&X.E               CHECK NEXT DELIM IF ANY
         AGO   .A204
.A202    ANOP
&D       SETA  1
IHE&X.G  EQU   *
.A203    AIF   (&D GT N'&DELIM).A204
         CLI   0(&FROMR),C&DELIM(&D)    DELIMITER FOUND
         BE    IHE&X.K                  YES
&D       SETA  &D+1
         AGO   .A203
.A204    ANOP
         CH    1,&ML                    MAXIMUM LENGTH REACHED
         BL    IHE&X.H                  NO, GO CHECK NUMERIC
         ST    &FROMR,&Y                YES, SAVE SCAN ADDRESS
         LM    13,14,GPSV&X             RESTORE R13 R14
         B     &LE                      TO LENGTH-ERROR HANDLER
IHE&X.H  CLI   0(&FROMR),C'0'           < 0
         BL    IHE&X.I
         CLI   0(&FROMR),C'9'           > 9
         BNH   IHE&X.J
IHE&X.I  ST    &FROMR,&Y                NOT NUMERIC, SAVE SCAN POINTER
         LM    13,14,GPSV&X             RESTORE R13 R14
         B     &NE                      TO NUMERIC ERROR HANDLER
IHE&X.J  LA    &FROMR,1(&FROMR)         PARM ADDRESS +1
         LA    1,1(1)                   COUNT 1 BYTE
         B     IHE&X.G                  GO, CHECK NEXT CHARACTER
IHE&X.K  ST    &FROMR,&Y                SAVE DELIM (SCAN) ADDRESS
         ZAP   GPDW&X,=P'0'             CLEAR WORK AREA
         LR    &FROMR,0                 RESTORE STARTING ADDRESS
         LTR   0,1                      SAVE & CHECK LENGTH SCANNED
         BZ    IHE&X.L                  ZERO, NOTHING SCANNED
         BCTR  1,0                      -1 FOR EX
         EX    1,GPEX&X                 PACK PARM INTO WORK
IHE&X.L  L     &FROMR,&Y                RESTORE SCAN POINTER
         AIF   (T'&TO EQ 'O').A206
         AIF   (T'&TO NE 'U' AND T'&TO NE 'N').A205
         CVB   &TO,GPDW&X               PROVIDE BINARY VALUE
         AGO   .A206
.A205    ANOP
         ZAP   &TO,GPDW&X               PROVIDE PACKED VALUE
.A206    ANOP
         LR    1,0                      RESTORE LENGTH
.A207    AIF   (T'&MIN EQ 'O').A212
         AIF   (T'&MIN EQ 'N').A208
         CP    GPDW&X,&MIN
         AGO   .A209
.A208    ANOP
         CP    GPDW&X,GPMN&X            VALUE < MINIMUM
.A209    AIF   (T'&MAX NE 'O').A210
         BNL   IHE&X.X                  EXIT
         AGO   .A211
.A210    ANOP
         BNL   *+12                     OK
.A211    ANOP
         LM    13,14,GPSV&X             RESTORE R13 R14
         B     &SE                      SIZE ERROR
.A212    AIF   (T'&MAX EQ  'O').A901
         AIF   (T'&MAX EQ 'N').A213
         CP    GPDW&X,&MAX
         AGO   .A214
.A213    ANOP
         CP    GPDW&X,GPMX&X            VALUE > MAXIMUM
.A214    ANOP
         BNH   IHE&X.X                  OK
         LM    13,14,GPSV&X             RESTORE R13 R14
         B     &SE                      SIZE ERROR
.A901    AIF   ('&NUM' NE 'Y').A903
         AIF   (T'&MIN NE 'O').A902
         B     IHE&X.X
.A902    ANOP
GPEX&X   PACK  GPDW&X,0(1,&FROMR)       EX'D - PARM SCANNED INTO WORK
GPDW&X   DC    D'0'
.A903    ANOP
GPSV&X   DC    D'0'                     SAVE REGS 13 & 14
         AIF   (T'&MIN EQ 'O' OR T'&MIN NE 'N').A904
GPMN&X   DC    P'&MIN'                  MINIMUM ALLOWABLE VALUE
.A904    AIF   (T'&MAX EQ 'O' OR T'&MAX NE 'N').A905
GPMX&X   DC    P'&MAX'                  MAXIMUM ALLOWABLE VALUE
.A905    AIF   ('&GLOB' EQ 'Y' AND NOT &F).A907
&Y       DC    A(0)                     LAST ADDRESS SCANNED
.A907    AIF   (T'&MAXL EQ 'O').A908
&L       SETA  &MAXL
         AGO   .A911
.A908    AIF   (T'&TO EQ 'O').A909
         AIF   (T'&TO EQ 'U' OR T'&TO EQ 'N').A909
&L       SETA  L'&TO
         AGO   .A911
.A909    AIF   ('&NUM' EQ 'Y').A910
&L       SETA  100
         AGO   .A911
.A910    ANOP
&L       SETA  16
.A911    ANOP
GPML&X   DC    H'&L'                    MAXIMUM PARAMETER LENGTH
.A912    ANOP
IHE&X.X  LM    13,14,GPSV&X             RESTORE R13 R14
         AIF   ('&NUM' EQ 'N').A913
         MEXIT
.A913    ANOP
         ST    &FROMR,&Y                SAVE LAST SCANNED LOCATION
         MEXIT
.E001    MNOTE 8,'FROMR=  PARAMETER MISSING'
         MEXIT
.E002    MNOTE 8,'ERR=  OR LERR=  MUST BE SPECIFIED'
         MEXIT
.E003    MNOTE 8,'ERR=  OR NERR=  MUST BE SPECIFIED'
         MEXIT
.E004    MNOTE 8,'ERR=  OR SERR=  MUST BE SPECIFIED '
         MEND
