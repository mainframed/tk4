         TITLE 'CDPFKLST      PFK LIST MODULE '
**********************************************************************
*                                                                    *
*                                                                    *
*   CDPFKLST                                                         *
*   ========                                                         *
*                                                                    *
*       FUNCTION:                                                    *
*       ---------                                                    *
*                                                                    *
*             1) LIST PROGRAM FUNCTION KEY'S (PFK) FOR ALL           *
*                GENERATED CONSOLES                                  *
*                                                                    *
*             2) GENERATE INPUT FOR CDPFKSET (PFK SET PGM)  OPTIONAL *
*                                                           ******** *
*                                                                    *
*        JCL                                                         *
*        ---                                                         *
*             //      EXEC  PGM=CDPFKLST                             *
*             //STEPLIB  DD DSN=C.TD.STEPLIB,DISP=SHR                *
*             //SYSPRINT DD SYSOUT=A                                 *
*          (  //GENINP   DD DSN=.... ,DCB=BLKSIZE=N X 80  )  OPT.    *
*                                                                    *
**********************************************************************
         EJECT
         PRINT NOGEN
CDPFKLST CDMSAVE SA=
         OPEN  (SYSPRINT,(OUTPUT),GENINP,(OUTPUT))
*
         USING CVT,R2
         L     R2,CVTPTR           LOAD CVT POINTER
         L     R3,CVTSMCA          ADDR SMCA
         USING SMCA,R3
         MVC   SYST(1),SMCASID     SYSTEM-ID
         L     R2,CVTCUCB          ADDR UCM-BASE
         LA    R2,72(R2)           UCM ENTRY ACCESSING DATA
         LM    R3,R5,0(R2)         R3 = ADDR OF FIRST UCM-ENTRY
*                                  R4 = LENGTH OF UCM-ENTRY
*                                  R5 = ADDR OF LAST UCM-ENTRY
         DROP  R2
         DROP  R3
         USING UCMLIST,R3
LOOP1    EQU   *
         LA    R6,UCMUCB           ADDR OF DEVICE-ADDR FIELD UCB NAME
         L     R7,UCMXB            ADDR OF DCM(GRAPHICS) OR ZERO
         LTR   R7,R7               TEST IF ZERO
         BZ    NEXTCONS            IF ZERO CONSOLE IS NOT A DISPLAY
         USING RDCM,R7
         L     R7,DCMADPFK         ADDR OF RESIDENT PFK AREA
         LTR   R7,R7               TEST IF ZERO
         BZ    NEXTCONS            NO PFK DEF
         DROP  R7
         USING UCB,R6
         USING PFKAR,R7
         SPACE
PFKLIST  EQU   *
         L     R6,0(R6)            UCB-ADDR
         MVC   CONS(3),UCBNAME     UCB NAME INTO TITLE
         LA    R11,TIT1
         BAL   R10,PRINT2          PUT FIRST TITLE LINE
         LA    R11,TIT2
         BAL   R10,PRINT2          PUT SECOND TITLE LINE
         MVI   ASA,C'0'
LOOP     EQU   *
         CLI   PFKNB,PFKLAST       TEST IF LAST ENTRY
         BE    NEXTCONS            YES GET NEXT UCM
         SR    R8,R8               CLEAR R8
         IC    R8,PFKNB            INSERT PFK#
         CVD   R8,DW               CONVERT TO DECIMAL
         UNPK  PFK#(3),DW+6(2)     UNPACK
         OI    PFK#+2,X'F0'
         MVI   PFK#,C' '           BLANK FIRST POS.
         MVC   CMD,PFKCMD          SET CMD(S) INTO LINE
         TM    PFKCON,X'A0'        TEST PFK MODE
         BO    CONY                CON=Y
         BM    CONN                CON=N
         MVC   CMD(11),=C'NOT DEFINED'
         B     DEF2
CONY     MVC   CON(3),=C'YES'      CON=YES INTO PRINT LINE
         B     DEF2
CONN     MVC   CON(2),=C'NO'       CON=NO INTO PRINT LINE
DEF2     EQU   *
         MVC   INPUT,BLANKS        CLEAR INPUT RECORD
         MVC   INCUU,CONS          SET CONSOLE ADDR.
         MVC   INPFK#,PFK#+1       SET PFK NUMBER
         CLC   CON(3),BLANKS       CON = BLANKS
         BE    INPOK               YES  PFK NOT DEFINED
         MVC   INCONV,CON
         MVC   INCMD,CMD           SET COMMAND
         TM    GENINP+48,X'10'     OPEN O.K. ? (DD-STMT PRESENT)
         BNO   PUTLST              NO
         CLC   CMD+65(43),BLANKS   TEST IF AN SECOND PART NECESSARY
         BE    INPOK               NO
         MVI   INCONT,C'*'         YES  SET CONTINUATION CHARAC.
INPOK    PUT   GENINP,INPUT        PUT A RECORD
         CLI   INCONT,C'*'         CONTINUATION REQ.
         BNE   PUTLST              NO  PUT LIST
         MVC   INPUT,BLANKS        CLEAR RECORD
         MVC   INCMD(43),CMD+65    SET SECOND PART OF CMD
         B     INPOK
*
PUTLST   EQU   *
         BAL   R12,PRINT           PUT LINE
         LA    R7,PFKARLG(R7)      GET NEXT PFK-ENTRY
         B     LOOP
         SPACE
NEXTCONS EQU   *
         BXLE  R3,R4,LOOP1         GET NEXT UCM ENTRY
END      EQU   *
         L     R13,4(R13)          RESTORE R13
         RETURN (14,12),RC=0       RETURN
         SPACE
PRINT    EQU   *
         LA    R11,LINE
         BAL   R10,PRINT2          PUT LINE
         MVI   0(R11),C' '
         MVC   1(120,R11),0(R11)   BLANK PRINT LINE
         BR    R12
PRINT2   PUT   SYSPRINT,(11)
         BR    R10
         TITLE 'CDPFKLST       DATA AREAS '
DW       DC    D'0'
         PRINT NOGEN
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS,RECFM=FA,LRECL=121,   *
               BLKSIZE=121
GENINP   DCB   DDNAME=GENINP,MACRF=PM,DSORG=PS,RECFM=FB,LRECL=80
*
LINE     DS    0CL121
ASA      DC    C' '
PFK#     DC    CL7' '
CON      DC    CL5' '
CMD      DC    CL108' '
*
TIT1     DS    0CL121
         DC    C'1'
         DC    CL35'  P F K - C O M M A N D S '
         DC    CL9'SYSTEM : '
SYST     DC    CL6' '
         DC    CL15'CONSOLE-ADDR = '
CONS     DC    CL56' '
*
TIT2     DS    0CL121
         DC    C'-'
         DC    CL6'PFK#'
         DC    CL7'CON='
         DC    CL107'CMD='
*
INPUT    DS    0CL80
INCUU    DS    CL3
INPFK#   DS    CL2
INCONV   DS    C
INCMD    DS    CL65
INCONT   DS    C
INNUM    DS    CL8
*
BLANKS   DC    CL80' '
         LTORG
         TITLE 'CDPFKLST        DUMMY SECTION''S '
*
PFKAR    DSECT
PFKNB    DS    X
PFKCON   DS    X
PFKCMD   DS    CL108
PFKARLG  EQU   *-PFKAR
PFKLAST  EQU   X'64'               LAST PFK ENTRY INDICATOR
         CVT   DSECT=YES
*
RDCM     DSECT
         IEECRDCM
*
         IEESMCA
SMCA     EQU   SMCABASE
*
TDCM     DSECT
         IEECDCM
*
UCB      DSECT
         IEFUCBOB
*
UCM      DSECT
         IEECUCM
         END
