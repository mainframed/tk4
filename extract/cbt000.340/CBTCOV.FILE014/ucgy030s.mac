CSASCAN  CDMSAVE SA=
         OPEN  MF=(E,OPENLI)       OPEN DATASET FOR INPUT
         GET   SYSUT1,TITLE        GET TITLE LINE
         CLOSE MF=(E,OPENLI)       CLOSE INPUT DATASET
         B     CONT1               AND CONTINUE BELOW
*
EODAD    CLOSE MF=(E,OPENLI)       CLOSE EMPTY DATASET
         OPEN  MF=(E,OPENLO)       OPEN IT FOR OUTPUT
         PUT   SYSUT1,TITLE
         CLOSE MF=(E,OPENLO)       CLOSE DATASET AGAIN
CONT1    TIME  DEC                 GET TIME AND DATE
         XR    R0,R1               MAKE
         XR    R1,R0                 R0=DATE
         XR    R0,R1                   R1=TIME
         SRL   R0,4                R0=000YYDDD
         SRDL  R0,12               R0-R1=000000YYDDDHHMMS
         STM   R0,R1,DW            SET THIS INTO DW
         OI    DW+7,X'0F'          SET CORRECT ZONE
         ED    LINE,DW+3           EDIT INTO LINE
         USING CVT,R2
         L     R2,CVTPTR           POINT TO CVT
         L     R3,CVTSMCA
         USING SMCA,R3
         MVC   TITLE(1),SMCASID
         DROP  R3
         L     R2,CVTGDA           AND TO GDA
         DROP  R2
         USING GDA,R2
         L     R4,CSAPQEP          POINT TO PQE OF CSA
         L     R7,20(,R4)          GET CSA SIZE
         SRL   R7,10               SWITCH TO UNITS OF K
         MH    R7,=H'10000'        SHIFT LEFT 4 UNITS
         SR    R0,R0               CLEAR FREE COUNTER
         LR    R5,R4
LOOPFBQE L     R5,0(,R5)           POINT TO NEXT FBQE
         CR    R5,R4               IS THIS END OF CHAIN
         BE    ENDFBQE             YES, CONTINUE BELOW
         A     R0,8(,R5)           NO, COUNT NUMBER OF BYTES
         B     LOOPFBQE            AND LOOP THROUGH FBQE'S
*
ENDFBQE  SRL   R0,10               SWITCH TO UNITS OF K
         AR    R7,R0
         CVD   R7,DW               AND PUT FREE COUNT
         ED    SIZE,DW+3               INTO LINE
         L     R5,CSASPQEP         POINT TO SPQE CHAIN
         USING SPQE,R5
SPQELOOP SR    R0,R0
         LA    R6,SPDQEPTR-(DQEPTR-DQE)
         USING DQE,R6
NEXTDQE  ICM   R6,15,DQEPTR
         BZ    ENDDQE
         A     R0,DQELNTH
         B     NEXTDQE
*
ENDDQE   LTR   R0,R0
         BZ    ENDSPQE
         MVC   CURRSPQE,MASKT
         SR    R9,R9
         IC    R9,SPQEID
         CVD   R9,DW
         UNPK  CURRSPQE+1(3),DW+6(2)
         OI    CURRSPQE+3,X'F0'
         UNPK  CURRSPQE+5(1),SPQEKEY
         OI    CURRSPQE+5,X'F0'
         LA    R3,TITENT
LOOPTIT  CLC   CURRSPQE,0(R3)
         BE    FOUND
         CLI   1(R3),C' '
         BE    ADD
         LA    R3,6(,R3)
         B     LOOPTIT
*
ADD      MVC   0(6,R3),CURRSPQE
FOUND    S     R3,=A(TITLE)
         LA    R3,LINE(R3)
         SRL   R0,10
         CVD   R0,DW
         MVC   0(6,R3),MASKD
         ED    0(6,R3),DW+5
ENDSPQE  TM    SPQEFLGS,LASTSPQE
         BO    RETURN
         L     R5,SPQEAD
         B     SPQELOOP
*
RETURN   OPEN  MF=(E,OPENLU)
         GET   SYSUT1U
         MVC   0(216,R1),TITLE
         PUTX  SYSUT1U
         CLOSE MF=(E,OPENLU)
         OPEN  MF=(E,OPENLO)
         PUT   SYSUT1,LINE
         CLOSE MF=(E,OPENLO)
         L     R13,4(,R13)
         RETURN (14,12),RC=0
*
TITLE    DC    C'  CSA LAYOUT  SIZE/FREE'
TITENT   DC    18CL6' '
LINE     DC    X'20204B2020204020204B2020'
SIZE     DC    X'4020202020206120202020'
         DC    18CL6' '
MASKT    DC    C' XXX/X'
MASKD    DC    X'402020202020'
CURRSPQE DC    C' XXX/X'
OPENLI   OPEN  SYSUT1,MF=L
OPENLO   OPEN  (SYSUT1,EXTEND),MF=L
OPENLU   OPEN  (SYSUT1U,UPDAT),MF=L
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GM,PM),BLKSIZE=131,       *
               EODAD=EODAD,LRECL=131,RECFM=F
SYSUT1U  DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL,PM),BLKSIZE=131,       *
               EODAD=EODAD,LRECL=131,RECFM=F
DW       DC    D'0'
         CDMDSECT (CVT,GDA,SPQE,DQE,SMCA)
         END
