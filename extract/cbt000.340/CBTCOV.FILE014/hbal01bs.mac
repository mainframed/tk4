HASPBAL0 TITLE 'BALOISE PRINT OUTPUT SEPARATOR PAGE FORMATTER'
HASPBAL0 START
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2
         SAVE  (14,12),,*
         USING HASPBAL0,R11,R12         ESTABLISH BASE REG
         LR    R11,R15
         LA    R12,2048(,R11)
         LA    R12,2048(,R12)
         SPACE
         LR    R14,R13                  CHAIN SAVE AREAS
         L     R5,0(R1)
         USING TABLE,R5
         LA    R13,BALSAVE
         ST    R14,4(R13)
*********ST****R13,8(R14)**********
         ST    R13,8(R14)
         SPACE
         EJECT
BAL000   CLC   BALFORM,=C' END'
*        BE    BAL006
         B     BAL006
         LA    R3,PRTTAB           GET ADDR. OF PRINTER TABLE
BAL001   CLI   0(R3),X'FF'         END OF TABLE?
         BE    BAL003
         CLC   0(8,R3),BALOUTDV
         BNE   BAL002              PRINTER ELEMENT NOT FOUND
         CLC   8(1,R3),BALOUTCL    SAME OUTPUT CALSS
         BNE   BAL004
         CLC   9(7,R3),BALJOBNU+1
         BE    BAL800              NO SEPARATOR
         B     BAL004
BAL002   LA    R3,16(R3)           NEXT ELEMENT IN TABLE
         B     BAL001
BAL003   MVC   0(8,R3),BALOUTDV
BAL004   MVC   8(8,R3),BALJOBNU
         MVC   8(1,R3),BALOUTCL
BAL006   LA    R3,NOSEPTAB         GET ADDR. OF SYSOUT CLASSES
BAL007   CLI   0(R3),C'*'          END OF TABLE?
         BE    BAL008
         CLC   0(1,R3),BALOUTCL
         BE    BAL800              NO SEPARATOR
         LA    R3,1(R3)            NEXT JOB CLASS IN TABLE
         B     BAL007
BAL008   LA    R3,BALZEIL          CLEAR PARM-AREA
         LA    R4,47               FOR
BAL010   MVI   0(R3),C' '          SEPARATOR PAGE
         MVC   1(119,R3),0(R3)
         LA    R3,120(R3)
         BCT   R4,BAL010
         CLI   BALJOBNU,C'J'       IF NOT A JOB
         BE    BAL012
         MVI   BALEDATE,C' '       SUPPRESS END AND ELAPSED TIME
         MVC   BALEDATE+1(29),BALEDATE
BAL012   ST    R3,BALFW
         MVC   0(240,R3),BALZ1     LINE 1 - 6
         LA    R3,240(R3)
         MVC   0(120,R3),BALZ7     LINE 7 - 9
         LA    R3,120(R3)
         MVC   0(40,R3),BALZ2      LINE 10
         MVC   40(40,R3),BALZ10    LINE 11
         MVC   50(4,R3),BALMODEL
         MVC   72(2,R3),BALSREL
         MVC   75(2,R3),BALSVERS
         LA    R3,80(R3)
         MVC   0(40,R3),BALZ2      LINE 12
         MVC   40(80,R3),BALZ1     LINE 13-14
         MVC   120(120,R3),BALZ11  LINE 15-17
         MVC   132(8,R3),BALJOBNA
         MVC   157(1,R3),BALJOBCL
         MVC   162(3,R3),BALJOBNU
         MVC   175(5,R3),BALJOBNU+3
         MVC   197(1,R3),BALOUTCL
         MVC   202(5,R3),=C'START'
         MVC   212(8,R3),BALSDATE
         MVC   222(6,R3),BALSJDAT
         MVC   230(8,R3),BALSTIME
         LA    R3,240(R3)
         MVC   0(40,R3),BALZ13     LINE 18
         MVC   2(4,R3),=C'STOP'
         CLC   BALSDATE,BALEDATE
         BNE   BAL015
         MVC   12(20,R3),BALZEIL
         B     BAL016
BAL015   MVC   12(8,R3),BALEDATE
         MVC   22(6,R3),BALEJDAT
BAL016   MVC   30(8,R3),BALETIME
         MVC   40(40,R3),BALZ13    LINE 19
         MVC   42(8,R3),BALINDEV
         CLC   BALSDATE,BALRDATE
         BNE   BAL020
         MVC   52(20,R3),BALZEIL
         B     BAL021
BAL020   MVC   52(8,R3),BALRDATE
         MVC   62(6,R3),BALRJDAT
BAL021   MVC   70(8,R3),BALRTIME
         MVC   80(40,R3),BALZ13    LINE 20
         MVC   82(8,R3),BALOUTDV
         CLC   BALSDATE,BALPDATE
         BNE   BAL025
         MVC   92(20,R3),BALZEIL
         B     BAL026
BAL025   MVC   92(8,R3),BALPDATE
         MVC   102(6,R3),BALPJDAT
BAL026   MVC   110(8,R3),BALPTIME
         LA    R3,120(R3)
         MVC   0(40,R3),BALZ14     LINE 21
         MVC   22(17,R3),BALZEIL
         MVC   12(8,R3),BALELTIM
         MVC   40(40,R3),BALZ15    LINE 22
         MVC   52(20,R3),BALPGMR
         MVC   80(40,R3),BALZ2     LINE 23
         MVC   120(80,R3),BALZ1    LINE 24-25
         LA    R3,200(R3)
         MVC   0(40,R3),BALZ2      LINE 26
         MVC   40(40,R3),BALZ2     LINE 27
         MVC   80(80,R3),0(R3)     LINE 28-29
         MVC   160(160,R3),0(R3)   LINE 30-33
         CLC   BALACCNT(80),BALZEIL
         BE    BAL030
         MVC   2(8,R3),=C'ACC-INF.'
         MVC   85(29,R3),BALACCNT
         MVC   125(29,R3),BALACCNT+29
         MVC   165(22,R3),BALACCNT+58
*        MVC   205(30,R3),BALACCNT+90
BAL030   LA    R3,320(R3)
         MVC   0(40,R3),BALZ1      LINE 34
BAL190   LA    R4,OUTCLTAB         TEST FOR SEPARATOR TYPE
BAL195   CLI   0(R4),C'*'          BAL200 IS BRANCH ADDRESS
         BE    BAL500              FOR NORMAL SEPARATOR
         CLC   0(1,R4),BALOUTCL    AND BAL500 IS THE ADDRESS
         BE    BAL200              OF MINI SEPARATOR ROUTINE
         LA    R4,1(R4)
         B     BAL195
BAL200   LA    R3,BALZEIL
         MVI   0(R3),C'*'          FILL FIRST LINE
         MVC   1(119,R3),0(R3)     WITH '*'
         LA    R3,120(R3)          SECOND LINE
         LA    R3,120(R3)          IS BLANK
         MVC   BALWORK(8),BALJOBNA
         MVI   BALWORK+8,C':'
         MVC   BALWORK+9(1),BALJOBCL
         MVI   BALWORK+10,C'/'
         MVC   BALWORK+11(1),BALOUTCL
         LA    R4,12
         ST    R4,BALFWWRK+4       SET CHARACTER COUNT
         LA    R4,0
BAL210   ST    R4,BALLCNT          SET LINE COUNT
         BAL   R10,BALBLPRT
         LA    R3,120(R3)          NEW LINE ADDRESS
         CLI   BALLCNT+3,X'09'
         BNL   BAL220
         L     R4,BALLCNT          INCREMENT LINE COUNT WITH 1
         LA    R4,1(R4)
         B     BAL210
BAL220   EQU   *
         LA    R3,120(R3)          LINE 13 IS BLANK
         MVC   BALWORK,0(R3)       CLEAR BALWORK
         CLC   BALFORM,=C' END'
         BE    BAL230
         MVC   BALWORK(8),BALSDATE STARTING EXECUTION
BAL230   LA    R4,8
         ST    R4,BALFWWRK+4       CHARACTER COUNT
         LA    R4,0                LINE COUNT
BAL240   ST    R4,BALLCNT          SET LINE COUNT
         BAL   R10,BALBLPRT
         L     R2,BALFW
         MVC   80(40,R3),0(R2)
         LA    R2,40(R2)
         ST    R2,BALFW
         LA    R3,120(R3)          NEW LINE ADDRESS
         CLI   BALLCNT+3,X'09'
         BNL   BAL250
         L     R4,BALLCNT          INCREMENT LINE COUNT WITH 1
         LA    R4,1(R4)
         B     BAL240
BAL250   MVC   80(40,R3),0(R2)     LINE 24
         LA    R2,40(R2)
         ST    R2,BALFW
         MVC   BALWORK,0(R3)       CLEAR BALWORK
         LA    R3,120(R3)          NEW LINE ADDRESS
         CLC   BALFORM,=C'STAR'
         BNE   BAL260
         MVC   BALWORK+2(5),BALSTIME STARTING EXECUTION
         B     BAL270
BAL260   CLC   BALFORM,=C' END'
         BNE   BAL265
         MVC   BALWORK+2(4),=C'ENDE'
         B     BAL270
BAL265   MVC   BALWORK+2(4),BALFORM
BAL270   LA    R4,8
         ST    R4,BALFWWRK+4       CHARACTER COUNT
         LA    R4,0                LINE COUNT
BAL280   ST    R4,BALLCNT          SET LINE COUNT
         BAL   R10,BALBLPRT
         CLC   BALFORM,=C'STAR'
         BNE   BAL285
         MVC   15(60,R3),20(R3)
BAL285   L     R2,BALFW
         MVC   80(40,R3),0(R2)
         LA    R2,40(R2)
         ST    R2,BALFW
         LA    R3,120(R3)          NEW LINE ADDRESS
         CLI   BALLCNT+3,X'09'
         BNL   BAL290
         L     R4,BALLCNT          INCREMENT LINE COUNT WITH 1
         LA    R4,1(R4)
         B     BAL280
BAL290   MVC   80(40,R3),0(R2)     LINE 35
         LA    R2,40(R2)
         ST    R2,BALFW
         LA    R3,120(R3)          NEW LINE ADDRESS
         MVC   BALWORK,0(R3)       CLEAR BALWORK
         CLI   BALPGMR,C','
         BE    BAL300
         MVC   BALWORK(8),BALPGMR  STARTING EXECUTION
         B     BAL310
BAL300   MVC   BALWORK(8),BALPGMR+1
BAL310   LA    R4,8
         ST    R4,BALFWWRK+4       CHARACTER COUNT
         LA    R4,0                LINE COUNT
BAL320   ST    R4,BALLCNT          SET LINE COUNT
         BAL   R10,BALBLPRT
         L     R2,BALFW
         MVC   80(40,R3),0(R2)
         LA    R2,40(R2)
         ST    R2,BALFW
         LA    R3,120(R3)          NEW LINE ADDRESS
         CLI   BALLCNT+3,X'09'
         BNL   BAL330
         L     R4,BALLCNT          INCREMENT LINE COUNT WITH 1
         LA    R4,1(R4)
         B     BAL320
BAL330   MVC   80(40,R3),0(R2)     LINE 46
         LA    R2,40(R2)
         ST    R2,BALFW
         LA    R3,120(R3)
         MVI   0(R3),C'*'
         MVC   1(119,R3),0(R3)     LAST LINE
         LA    R4,48
         ST    R4,BALLCNT
         B     BAL900
BAL500   LA    R3,BALZEIL
         L     R6,BALFW
         LA    R4,33
BAL550   MVC   0(40,R3),0(R6)
         LA    R3,120(R3)
         LA    R6,40(R6)
         BCT   R4,BAL550
         MVC   0(40,R3),0(R6)
         CLC   BALFORM,=C'STAR'
         BNE   BAL560
         MVC   BALZEIL+16(7),=C' START '
         B     BAL580
BAL560   CLC   BALFORM,=C' END'
         BNE   BAL570
         MVC   BALZEIL+16(6),=C' ENDE '
         B     BAL580
BAL570   MVC   BALZEIL+16(6),=C' CONT '
BAL580   MVC   16(7,R3),BALZEIL+16
         LA    R4,35
         ST    R4,BALLCNT
         B     BAL900
BAL800   LA    R4,1
         ST    R4,BALLCNT
BAL900   LR    R14,R13
         L     R13,4(R14)
         RETURN (14,12)
         EJECT
***********************************************************************
*                                                                     *
*        BALBLPRT   BLOCK CHARACTER ROUTINE                           *
*                                                                     *
***********************************************************************
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                   *
* THIS MODULE IS A REPLACEMENT OF THE IBM-SUPPLIED BLOCK CHARACTER  *
* ROUTINE. THIS MODULE PRODUCES CHARACTERS THAT ARE 9 WIDE  BY 10   *
* HIGH. THIS PERMITS UP TO 12 CHARACTERS PER LINE (INTER CHARACTER  *
* SPACING IS 1).                                                    *
* SPECIAL CHARACTERS ':','-','/','.','#','@' AND '$' ARE INCLUDED   *
*                                                                   *
*              INVALID CHARACTERS ARE TRANSLATED TO BLANK           *
*                                                                   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
BALBLPRT DS    0H
         ST    R10,BALFWWRK        SAVE RETURN REGISTER
         MVC   BALWORK1,BALWORK    SAVE TEXT
         L     R7,BALLCNT          GET LINE NUMBER
BALSK100 DS    0H
         L     R10,BALFWWRK+4      COUNT TO R10
         LR    R9,R10              AND SAVE IT INTO R9
         BCTR  R10,R0              SUBTRACT ONE.
         EX    R10,TRANSL          XLATE TO MY INTERNAL CODES.
         LA    R6,BALWORK          GET THE ADDRESS OF THE WA
         LA    R8,BALWORK1         GET ADDRESS OF SAVED TEXT
         ST    R3,BALLADDR         SAVE LINE ADDRESS
         SPACE 1
*      END OF INITIALIZATION.  THE CONVERT LOOP FOLLOWS.
*
CNVLOOP  XR    R4,R4               CLEAR WORKREG
         XR    R10,R10             CLEAR WORKREG.
         IC    R10,0(R6)           GET CHAR'S INTERNAL CODE.
         MH    R10,=H'10'          MPY BY 10, GIVES INDEX TO 'PXTAB'.
         AR    R10,R7              ADD CURRENT LINE # (0-9) TO INDEX.
         IC    R4,PXTAB(R10)       GET PATTERN # FROM 'PXTAB'.
         MH    R4,=H'9'            GENERATE INDEX TO 'PTAB'.
         LA    R10,PTAB            GET ADDRESS OF PATTERN TABLE
         AR    R10,R4              ADD BASE OF PTAB & INDEX.
         MVC   0(9,R3),0(R10)      MOVE PATTERN TO USER'S AREA.
         SPACE 2
*        THE STARS '*' OF THE OUTPUT PATTERN WILL BE REPLACED
*        BY THE CHARACTER THEY HAVE TO DISPLAY
         SPACE 1
         ST    R3,BALFWWRK+8       SAVE REG 3 IN WORK
         LA    R1,9                NUMBER OF PRINTPOSITIONS PER CHAR
CHLOOP   CLI   0(R3),X'40'         PRINT CHARACTER = BLANK?
         BE    STEP
         MVC   0(1,R3),0(R8)       CHARACTER IN OUTPUT
STEP     LA    R3,1(R3)            STEP OUTPUT POINTER
         BCT   R1,CHLOOP
         LA    R8,1(R8)            STEP THE CHARACTER POINTER
         L     R3,BALFWWRK+8       RESET REG 3
         SPACE 2
         LA    R3,10(R3)          STEP THE OUTPUT POINTER.
         LA    R6,1(R6)           STEP THE INPUT  POINTER.
         BCT   R9,CNVLOOP
RETU     EQU   *
         L     R3,BALLADDR         RESTORE LINE ADDRESS
         MVC   BALWORK,BALWORK1
         L     R10,BALFWWRK
         BR    R10
         SPACE 1
TRANSL   TR    BALWORK(1),TRTAB    EXECUTED TRANSLATE
         EJECT
*      TRTAB -- A TRANSLATE TABLE TO GET ALL CHARS INTO MY INTERNAL
*               CODE TO USE AS INDEX TO THE PATTERN INDEX TABLE.
         SPACE 1
TRTAB    DC    75X'00'                 ILLEGAL - CHARACTERS TO BLANKS
         DC    X'26'                   PERIOD
         DC    15X'00'             ILLEGAL
         DC    X'2A'               DOLLAR  SIGN
         DC    4X'00'              ILLEGAL
         DC    X'27'                    DASH
         DC    X'29'                   SLASH
         DC    9X'00'
         DC    X'25'                   COMMA
         DC    14X'00'
         DC    X'28'                   COLON
         DC    X'2B'               NUMBER  SIGN
         DC    X'2C'               AT SIGN
         DC    68X'00'             ILLEGAL
         DC    X'010203040506070809'   A - I
         DC    7X'00'                  ILLEGAL
         DC    X'0A0B0C0D0E0F101112'   J - R
         DC    8X'00'                  ILLEGAL
         DC    X'131415161718191A'     S - Z
         DC    6X'00'                  ILLEGAL
         DC    X'1B1C1D1E1F2021222324' 0 - 9
         DC    6X'00'                  ILLEGAL
         DC    AL2(*-TRTAB)  SHOULD BE = X'0100' (256)
         EJECT
*      PXTAB -- THE PATTERN INDEX TABLE.  THE TRANSLATED CHARACTERS
*      PLUS THE RELATIVE LINE NUMBER IS A INDEX TO THIS TABLE,
*      WHICH THEN PROVIDES THE INDEX TO THE PATTERN TABLE.
         SPACE 1
*                 1 2 3 4 5 6 7 8 910  CORRESPONDS TO LETTER-
PXTAB    DC    X'30303030303030303030' BLANK
         DC    X'0E0F1009090000090909' A
         DC    X'11000909111109090011' B
         DC    X'12000901010101090012' C
         DC    X'11000909090909090011' D
         DC    X'00000101131301010000' E
         DC    X'00000101131301010101' F
         DC    X'12000901011414090012' G
         DC    X'09090909000009090909' H
         DC    X'15150404040404041515' I
         DC    X'08080808080808090012' J
         DC    X'09161718191918171609' K
         DC    X'01010101010101010000' L
         DC    X'34093536222109090909' M
         DC    X'1A091C0D0A0B0C1D091B' N
         DC    X'12000909090909090012' O
         DC    X'11000909001101010101' P
         DC    X'120009090909270C1128' Q
         DC    X'11000909001118171609' R
         DC    X'12000901111E08090012' S
         DC    X'11110404040404040404' T
         DC    X'09090909090909090012' U
         DC    X'090909090909101F0E20' V
         DC    X'09090909090921222324' W
         DC    X'0909101F0E0E1F100909' X
         DC    X'16161625260404040404' Y
         DC    X'00000706050403020000' Z
         DC    X'0F12090909090909120F' 0
         DC    X'29050E05050505052A2A' 1
         DC    X'12000907060504031E00' 2
         DC    X'000016062E2F08090012' 3
         DC    X'2B062C2D1F2500000606' 4
         DC    X'000001111E0808090012' 5
         DC    X'12000901110009090012' 6
         DC    X'00001606050403030303' 7
         DC    X'12000909121209090012' 8
         DC    X'12000909001E08090012' 9
         DC    X'30303030303030310132' , COMMA
         DC    X'30303030303030313131' . PERIOD
         DC    X'30303030121230303030' - DASH
         DC    X'30300E0E30300E0E3030' : CPLON
         DC    X'33080706050403020132' / SLASH
         DC    X'201E0039111E3A001120' $ DOLLAR SIGN
         DC    X'10100000101000001010' # NUMBER SIGN
         DC    X'12001A0837380B0B0012' @ AT SIGN
         EJECT
PTAB     DC    C'*********'  THE PATTERN TABLE FROM WHICH EACH LINE
         DC    C'**       '  OF CHARACTERS IS BUILT.  (01)
         DC    C' **      '  02
         DC    C'  **     '  03
         DC    C'   **    '  04
         DC    C'    **   '  05
         DC    C'     **  '  06
         DC    C'      ** '  07
         DC    C'       **'  08
         DC    C'**     **'  09
         DC    C'** **  **'  0A
         DC    C'**  ** **'  0B
         DC    C'**   ****'  0C
         DC    C'****   **'  0D
         DC    C'   ***   '  0E
         DC    C'  *****  '  0F
         DC    C' **   ** '  10
         DC    C'******** '  11
         DC    C' ******* '  12
         DC    C'******   '  13
         DC    C'**   ****'  14
         DC    C' ******  '  15
         DC    C'**    ** '  16
         DC    C'**   **  '  17
         DC    C'**  **   '  18
         DC    C'****     '  19
         DC    C'*      **'  1A
         DC    C'**      *'  1B
         DC    C'***    **'  1C
         DC    C'**    ***'  1D
         DC    C' ********'  1E
         DC    C'  ** **  '  1F
         DC    C'    *    '  20
         DC    C'**  *  **'  21
         DC    C'** *** **'  22
         DC    C' *** *** '  23
         DC    C'  *   *  '  24
         DC    C' **  **  '  25
         DC    C'  ****   '  26
         DC    C'**   * **'  27
         DC    C' ****** *'  28
         DC    C'     *   '  29
         DC    C'  ****** '  2A
         DC    C'      *  '  2B
         DC    C'    ***  '  2C
         DC    C'   ****  '  2D
         DC    C'    **** '  2E
         DC    C'   ******'  2F
         DC    C'         '  30  THIS IS A BLANK
         DC    C'***      '  31
         DC    C'*        '  32
         DC    C'        *'  33
         DC    C'*       *' 34
         DC    C'***   ***' 35
         DC    C'**** ****' 36
         DC    C' ****  **' 37
         DC    C'****** **' 38
         DC    C'**  *    ' 39
         DC    C'    *  **' 3A
*      ANY USER PATTERNS FOLLOW HERE AND WOULD BE IDENTIFIED IN THE
*      PATTERN INDEX TABLE WITH NUMBERS STARTING WITH '3B'.
         EJECT
BALZ1    DC    CL40'****************************************'
BALZ2    DC    CL40'*                                      *'
BALZ3    DC    CL40'*                                      *'
BALZ4    DC    CL40'* BASLER,                              *'
BALZ5    DC    CL40'* VERSICHERUNGS-GESELLSCHAFT           *'
BALZ6    DC    CL40'* ZENTRALE DATENVERARBEITUNGSSTELLE    *'
BALZ7    DC    CL40'* AESCHENGRABEN 21                     *'
BALZ8    DC    CL40'* 4002  B A S E L                      *'
BALZ9    DC    CL40'*                                      *'
BALZ10   DC    CL40'* IBM/370-3032  MVS/SP  RELEASE XX.XX  *'
BALZ11   DC    CL40'* JOBNAME   XXXXXXXX       JOBCLASS  X *'
BALZ12   DC    CL40'* JOBNR        XXXXX       OUTCLASS  X *'
BALZ13   DC    CL40'*           TT.MM.JJ  JJ.TTT  HH:MM:SS *'
BALZ14   DC    CL40'* ELAPSED   HH:MM:SS  CPU     HH:MM:SS *'
BALZ15   DC    CL40'* PROG-NM                              *'
         EJECT
PRTTAB   DC    10X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' PRINTER TABLE
         SPACE 5
NOSEPTAB DC    C'8'                SYSOUT-CLASSES WITHOUT SEPARATOR
         DC    C'*'                END OF TABLE
         SPACE 5
OUTCLTAB DC    C'ACDLNPQRY7'       SYSOUT-CLASSES WITH
         DC    C'*'                  NORMAL SEPARATOR
         EJECT
         LTORG
         EJECT
TABLE    DSECT
BALFW    DS    F
BALJOBNA DS    CL8
BALJOBNU DS    CL8
BALJOBCL DS    CL1
BALOUTCL DS    CL1
BALMODEL DS    CL4
BALSREL  DS    CL2
BALSVERS DS    CL2
BALOUTDV DS    CL8
BALINDEV DS    CL8
BALPGMR  DS    CL20
         DS    0F
BALRDATI DS    XL8
BALRDATE DS    CL8
BALRJDAT DS    CL6
BALRTIME DS    CL8
         DS    0F
BALPDATI DS    XL8
BALPDATE DS    CL8
BALPJDAT DS    CL6
BALPTIME DS    CL8
         DS    0F
BALSDATI DS    XL8
BALSDATE DS    CL8
BALSJDAT DS    CL6
BALSTIME DS    CL8
         DS    0F
BALEDATI DS    XL8
BALEDATE DS    CL8
BALEJDAT DS    CL6
BALETIME DS    CL8
BALELTIM DS    CL8
BALCPU   DS    CL8
BALACCNT DS    CL142
BALFORM  DS    CL4
BALWORK  DS    CL12
BALWORK1 DS    CL12
BALFWWRK DS    3F
BALRES   DS    CL40
BALLADDR DS    F
BALLCNT  DS    F
BALSAVE  DS    18F
BALSAVE1 DS    18F
BALZEIL  DS    47CL120
BALBLOCK DS    34CL40
         END
