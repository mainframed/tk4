* ./  NAME=KB90LABL
         MACRO
&NAME    KB90LABL &LBL,&TEXT,           LABEL/HEAD-LINE(S)             X
               &S1=0,&S2=2,             SKP1 BEFORE/AFTER TITLE        X
               &S=YES,&L=RG12           REGISTER SAVE/NAME
         AGO  .STRT
*********************************************************************
*/*KB90LABL    GENERATE HEAD-LINES AND LABELS                       *
*                                                                   *
*1 FUNCTION    GENERIERUNG VON UEBERSCHRIFTEN UND LABEL MIT/OHNE    *
*              SAVE DES LINK-REGISTERS                              *
*                                                                   *
*2 SUMMARY                                                          *
*  TYPE        MACRO     BATCH          SYS2.COPYLIB                *
*  ACCOUNT     90                       SYSTEMPROGRAMMIERUNG        *
*  HISTORY     1979.0830 HE   GRUNDVERSION                          *
*              1981.0425 HE   STANDARD-DOKUMENTATION                *
*                                                                   *
*3 USAGE       - KB90LABL ,'TEXT',S1=E                              *
*                         GENERIERT UBERSCHRIFT, SKIP NEUE SEITE    *
*              - KB90LABL LABEL,'TEXT',S=N                          *
*                         ASM-STMT: LABEL   ST   RG12,LABELR        *
*                         NUR REENTRANT PROGRAMS, FW USER-SPECIFIED *
*              - KB90LABL LABEL,'TEXT',S=L                          *
*                         ASM-STMT: LABEL   DS   0H                 *
*              - KB90LABL LABEL,'TEXT'                              *
*                         ASM-STMT:         DC   F'-1'              *
*                                   LABELR  L    RG12,LABELR-4      *
*                                           BR   RG12               *
*                                   LABEL   ST   RG12,LABELR-4      *
*              - KB90LABL LABEL,'TEXT',L=RG5                        *
*                         WIE OBEN, JEDOCH LINK-REG. IST RG5        *
*              - KB90LABL LABEL,('ZEILE1','ZEILE2',ZEILE3')         *
*                         GENERIERUNG VON MAX. 3 UEBERSCHRIFTEN     *
**/                                                                 *
*********************************************************************
*
.STRT    ANOP
         LCLC  &T1,&T2,&T3
         LCLC  &L1,&L2,&L3
         LCLC  &C1
         AIF   (N'&TEXT EQ 0).NTXT       NO TEXT IN LINE
         AIF   ('&TEXT(1)' EQ '''').NTXT
&C1      SETC  '                                                     '
&T1      SETC  '&TEXT(1)'(2,K'&TEXT(1)-2)
&L1      SETC  '*&LBL*    &T1.&C1'(1,61)
         AIF   (N'&TEXT EQ 1).NTXT
         AIF   ('&TEXT(2)' EQ '''').NTXT
&T2      SETC  '&TEXT(2)'(2,K'&TEXT(2)-2)
&L2      SETC  '&T2.&C1'(1,55)
         AIF   (N'&TEXT EQ 2).NTXT
         AIF   ('&TEXT(3)' EQ '''').NTXT
&T3      SETC  '&TEXT(3)'(2,K'&TEXT(3)-2)
&L3      SETC  '&T3.&C1'(1,55)
.NTXT    AIF   ('&S' NE 'YES').BEG
         CNOP  0,4                      ALIGN FULLWORD BOUNDRY
.BEG     PUSH  PRINT
         PRINT GEN
         AIF   ('&S1' EQ '0').TXT
         AIF   ('&S1' EQ 'E').EJ1
         SPACE &S1
         AGO   .TXT
.EJ1     EJECT
.TXT     ANOP
*---------------------------------------------------------------------*
*                                                                     *
         AIF   (N'&TEXT EQ 0).OUT
         MNOTE '*        &L1*'          PRINT LABEL/1ST LINE
         AIF   (N'&TEXT EQ 1).OUT
*                                                                     *
         MNOTE '*              &L2*'          2ND LINE
         AIF   (N'&TEXT EQ 2).OUT
*                                                                     *
         MNOTE '*              &L3*'          3RD LINE
.OUT     ANOP
*                                                                     *
*---------------------------------------------------------------------*
         SPACE &S2
         AIF   (N'&LBL EQ 0).END         NO LABEL REQ.
         AIF   ('&S' EQ 'N').NSR
         AIF   ('&S' EQ 'L').LAB
         DC    F'-1'                    SAVE-AREA
&LBL.R   L     &L,&LBL.R-4              RESTORE LINK-REG.
         BR    &L
         SPACE 1
&LBL     ST    &L,&LBL.R-4              SAVE LINK-REG.
         AGO   .END
.NSR     ANOP
&LBL     ST    &L,&LBL.R                SAVE LINK-REG.
         AGO   .END
.LAB     ANOP
&LBL     DS    0H
.END     POP   PRINT
         MEND
