*
*        PDSDOC *SAMPL MPF EXIT DRIVER   FROM GG24-3142-01............*
*
* THIS IS A PROGRAM THAT ACTS AS A SIMPLE DRIVER PROGRAM FOR
* TESTING MPF EXITS.
* THE EXIT TO BE TESTED REQUIRES TO BE LINK EDITED WITH THIS
* MODULE, AND IT MUST HAVE A EXTERNAL ENTRY OF 'START' AS
* ITS FIRST EXECUTABLE STATEMENT
*
MPFTEST  CSECT
         PRINT ON,GEN
         ENTRY DRSTART
DRSTART  STM   14,12,12(13)
         BALR  BASER,0
         USING *,BASER
*
*
         GETMAIN RU,LV=DATAEND,SP=0,LOC=BELOW
         LR    DATAR,1             ADDRESS FOR DATA
         USING DATAAREA,DATAR
         LA    CTXTR,CTXTMOD       PICK UP ADDR OF CTXT
         USING CTXT,CTXTR
*
         ST    13,SAVEAREA+4       STANDARD SAVE AREA LINKAGE
         LA    WORKR,SAVEAREA
         ST    WORKR,8(13)
         LR    13,WORKR            R13 NOW POINTS TO SAVE AREA
*
         LA    TEXTR,TEXTBLOK      GET ADDRESS OF TEXT BLOCK
         USING CTXTATTR,TEXTR
         ST    TEXTR,CTXTTXPJ      STORE AS PRIMARY LINE
         MVC   CTXTTMSG,MESSAGE
         OI    CTXTTFB1,CTXTTFSL   INDICATE SINGLE LINE MSG
         OI    ROUTCODE,X'FF'      INDICATE ROUTED TO ALL CONSOLES
         OI    ROUTCODE+1,X'FF'
         LA    WORKR,ROUTCODE      STORE ROUTE CODE PTR TO CTXT
         ST    WORKR,CTXTRCP
         LA    WORKR,DESCCODE      STORE DESC CODE PTR TO CTXT
         ST    WORKR,CTXTDCP
         MVI   DESCCODE,X'C3'      SAMPLE
*
         ST    CTXTR,CTXTADDR      STORE FOR EXIT ROUTINE
         LA    1,CTXTADDR          POINT TO IT
         L     15,=V(START)        GO TO EXIT ROUTINE
GOEXIT   BALR  14,15
*
*
DRFINISH L     13,4(13)
         FREEMAIN  RU,LV=DATAEND,A=(DATAR)
         LM    14,12,12(13)
DREND    BR    14                  RETURN
         EJECT ,
MESSAGE  DC    CL132'IEF450I NV3V CANCELLED'
         SPACE 2
WORKR    EQU   15                  SHORT TERM WORK REGISTER
BASER    EQU   10                  ROUTINE BASE REGISTER
DATAR    EQU   11                  REGISTER FOR DATA AREA
CTXTR    EQU   9
TEXTR    EQU   8
         SPACE 2
DATAAREA DSECT
SAVEAREA DS    18F
CTXTADDR DS    F
CTXTMOD  DS    CL64                MODEL CTXT
TEXTBLOK DS    CL132               TEXT BLOCK
ROUTCODE DS    H                   ROUTING CODES FIELD
DESCCODE DS    H                   DESC CODES FIELD
CONSID   DS    CL9                 CONSOLE ID BLOCK
DATAEND  EQU   *-DATAAREA
         SPACE 2
         IEZVX100 ,
         END   MPFTEST
