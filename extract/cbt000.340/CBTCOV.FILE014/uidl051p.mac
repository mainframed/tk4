  /* SAMPLE PLI EXIT FOR MPF                          */
  /* CALLED FROM ASSEMBLER FRONT END                  */
  /*                SWL AUG 7 87                      */
 /*      PDSDOC *SAMPLE PL/1 MPF PROGRAM FROM GG24-3142-01...........*/
  /*                                                  */
  /* THIS ALLOWS THE PL/I PROGRAM TO MODIFY TEXT AND  */
  /* CTX FIELDS AS DESCRIBED IN THE SYSTEM EXITS SRL. */
  /* IT ALSO HAS TWO SERVICE FUNCTIONS:               */
  /* A. THE USER CAN CAUSE AN MVS COMMAND TO BE       */
  /* EXECUTED BY PUTTING THE COMMAND STRING IN THE    */
  /* PL/I FIELD 'COMMAND'. THIS CAN BE UP TO 40       */
  /* CHARACTERS LONG. IT CAN BE AM MVS COMMAND, A     */
  /* JES COMMAND, A NETVIEW COMMAND , ETC             */
  /* B. THE USER CAN CAUSE A WTO TO BE ISSUED. AGAIN, */
  /* PLACE THE TEXT IN THE FIELD 'MESSAGE' AND THE    */
  /* LENGTH OF THE MESSAGE+4 IN FIELD 'WTOLEN'.       */
  /* THE MESSAGE CAN BE UP TO 100 CHARACTERS.         */
  /* (ROUTE CODES AND DESC CODES ARE DEFAULT FOR      */
  /* WTO MACRO, YOU CAN MODIFY THESE WITH EXTRA       */
  /* CODE, BUT NOT TESTED)                            */
  /*                                                  */
  /* BOTH SERVICES MAY BE CALLED FROM SAME EXIT.      */
  /*                                                  */
  /* THIS EXAMPLE IS USED TO AUTOMATICALLY RESTART    */
  /* THE NETVIEW SSI ADDR SPACE 'NV3V" IF IT ABENDS   */
AOEXIT: PROC(CTXTPARM) OPTIONS(MAIN);
  P=ADDR(CTXTPARM);
  IF MINORLNE=0 THEN            /* IF MAJOR LINE                */
  P1 = CTXTTXPJ;                /* POINT TO MESSAGE TEXT        */
  ELSE P1 = CTXTTXPN;           /* IF MINOR LINE                */
  MSGID=SUBSTR(CTXTTMSG, 1,8);  /* ISOLATE THE MESSAGE ID       */
  IF MSGID = 'IEF450I ' THEN DO;
  IF INDEX(CTXTTMSG,' NV3V ') > 0
  THEN DO;                      /* IF YES THEN RESTART IT       */
  COMMAND = 'START NV3V';
  MESSAGE = ' *** NV3V RESTARTED BY USER EXIT***';
  WTOLEN = 40;                  /* LENGTH = CHARACTERS PLUS 4   */
  END;
  END;
  RETURN;                       /* BACK TO CALLER               */
  /*                                                  */
  DCL MSGID CHAR(8) AUTOMATIC;
  DCL CTXTPARM FIXED;           /* CTXT ADDR AS PASSED          */
  DCL 1 CTXT BASED(P),
    2 CTCTACRN CHAR(4),           /* ACRONYM - 'CTXT'             */
    2 CTXTVRSN CHAR(1),           /* VERSION                      */
    2 CTXTRSV1 CHAR(3),           /* RESERVED                     */
    2 CTXTTXPJ POINTER,           /* TO MAJOR LINE TEXT           */
    2 CTXTTXPN POINTER,           /* TO MINOR LINE TEXT           */
    2 CTXTSEQN FIXED BIN(31),     /* SEQ NUMBER                   */
    2 CTXTMLID FIXED BIN(31),     /* MULTILINE ID ********        */
    2 CTXTRPID FIXED BIN(15),     /* REPLY ID                     */
    2 CTXTRSV3 FIXED BIN(15),     /* RESERVED                     */
    2 CTXTRCLN FIXED BIN(15),     /* LENGTH OF ROUTECODE          */
    2 CTXTDCLN FIXED BIN(15),     /* LENGTH OF DESC CODE          */
    2 CTXTRCP POINTER,            /* TO ROUTE CODES               */
    2 CTXTDCP POINTER,            /* TO DESC CODES                */
    2 CTXTCIDP POINTER,           /* TO CONSOLE ID                */
    2 CTXTSFLG,                   /* STATUUS FLAGS                */
      3 CTXTSQPC BIT(1),            /* Q TO PARTICULAR ACT CONS     */
      3 CTXTSQUN BIT(1),            /* Q TO CONSOLE UNCOND          */
      3 CTXTSSUP BIT(1),            /* SUPPRESSED BY MPF            */
      3 CTXTSFHC BIT(1),            /* HARD COPY, NO DISPLAY        */
      3 CTXTSNHC BIT(1),            /* NO HARDCOPY                  */
      3 CTXTSHCO BIT(1),            /* HARDCOPY ONLY                */
      3 CTXTSRSP BIT(1),            /* COMMAND RESPONSE             */
      3 CTXTSBCA BIT(1),            /* BROADCAST TO ACTIVE CONS     */
      3 CTXTSRET BIT(1),            /* RETAIN BY AMRF               */
      3 CTXTSAUT BIT(1),            /* AUTOMATION SPECIFIED         */
      3 MOREFLGS BIT(22),
    2 CTXTRFLG,                   /* FLAGS - EXIT TO SYSTEM       */
      3 CTXTRCMT BIT(1),            /* CHAMGE THE MESSAGE TEXT      */
      3 CTXTRCRC BIT(1),            /* CHANGE ROUTING CODES         */
      3 CTXTRCDC BIT(1),            /* CHANGE DESC CODES            */
      3 CTXTRUPC BIT(1),            /* Q TO PARTICULAR CONSOLE      */
      3 CTXTRQUN BIT(1),            /* Q TO PART CONS UNCOND        */
      3 CTXTRQRC BIT(1),            /* Q BY ROUTE CODES ONLY        */
      3 CTXTRCCN BIT(1),            /* CHANGE CONSOLE ID            */
      3 CTXTRPML BIT(1),            /* PROCESS MINOR LINES          */
      3 CTXTRDTM BIT(1),            /* DELETE MSG, NO HCPY/DSPL     */
      3 CTXTROMS BIT(1),            /* OVERRIDE MPF SUPPRESSION     */
      3 CTXTRFHC BIT(1),            /* FORCE HARDCOPY               */
      3 CTXTRNHC BIT(1),            /* FORCE NO HARDCOPY            */
      3 CTXTRHCO BIT(1),            /* FORCE HARDCOPY ONLY          */
      3 CTXTRBCA BIT(1),            /* BROADCAST TO ALL ACTIVE      */
      3 CTXTRBCN BIT(1),            /* DO NOT BRCST TO ALL ACT      */
      3 CTXTRNRT BIT(1),            /* NO RETAIN BY AMRF            */
      3 CTXTRRET BIT(1),            /* RETAIN BY AMRF               */
      3 CTXTRCKY BIT(1),            /* CHANGE KEY                   */
      3 CTXTRCFC BIT(1),            /* CHANGE KEY                   */
      3 CTXTRCMF BIT(1),            /* CHANGE MSGTYP FLAGS          */
      3 CTXTRANO BIT(1),            /* AUTOMATION NOT REQD,         */
      3 CTXTRAYS BIT(1),            /* AUTOMATION REQUIRED,         */
      3 MOREFLG1 BIT(10),           /* REST OF FLAGS - UNUSED       */
    2 CTXTFCNP POINTER,
    2 CTXTPREQ POINTER,
    2 CTXTTOKN POINTER,           /* ADDR OF TOKEN                */
    2 CTXTKEY POINTER,            /* ADDR OF KEY                  */
    2 CTXTJBNM CHAR(8),           /* JOBNAME OF MSG ISSUER        */
    2 CTXTJBID CHAR(8),           /* JOBNUM OF MSG ISSUER         */
    2 CTXTSYSN CHAR(8),           /* SYSNAME OF MSG ISSUER        */
    2 CTXTAUTT CHAR(8),           /* AUTOMATION TOKEN VALUE       */
    2 CTXTRSV4 CHAR(4),           /* RESERVED                     */
    2 SWLPAD CHAR(4),             /* NOT USED AS YET              */
    2 COMMAND CHAR(40),           /* COMMAND THAT IS RESPONSE     */
    2 WTOLEN FIXED BIN(15),       /* LENGTH OF WTO                */
    2 MCSFLAGS FIXED BIN(15),     /* MCS FLAGS                    */
    2 MESSAGE CHAR(100);          /* AREA FOR WTO                 */
  DCL 1 CTXTTEXT BASED(P1),
    2 CTXTTLEN FIXED BIN(15),     /* TEXT LENGTH                  */
    2 CTXTTLMX FIXED BIN(15),     /* MAX LENGTH OF TEXT           */
    2 CTXTTFLG,                   /* MESSAGE TYPE FLAGS           */
      3 CTXTTFSL BIT(1),            /* SINGLE LINE MSG              */
      3 CTXTTFWR BIT(1),            /* WTOR                         */
      3 CTXTTFMJ BIT(1),            /* MULTI LINE MSG               */
      3 CTXTTFMC BIT(1),            /* CONTROL LINE                 */
      3 CTXTTFML BIT(1),            /* LABEL LINE                   */
      3 CTXTTFMD BIT(1),            /* DATA LINE                    */
      3 CTXTTFME BIT(1),            /* END LINE                     */
      3 CTXTTFB2 BIT(9),            /* UNUSED                       */
    2 CTXTTMSG CHAR(126);         /* TEXT OF MESSAGE              */
  DCL 1 CTXTDUP BASED(P),        /* DUPLICATE STRUCTURE FOR      */
    2 FIRST12 CHAR(12),           /* TO AVOID 'POINTER' PROBS     */
    2 MINORLNE FIXED BIN(31);
  END;
