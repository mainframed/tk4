         MACRO
&N       OSDATUM
*                          BEREITSTELLEN TAGESDATUM
&N       LA    1,2
         SVC   11                      TIME
         CNOP 6,8
         ST    0,TIME
         ZAP   *+X'54'(2),=P'01'
         ZAP   *+X'52'(2),=P'28'
         MVC   *+X'62'(8),=X'20204B20204B2020'
         ST    1,*+X'64'
         ED    *+X'5E'(2),*+X'61'
         TM    *+X'5B',1
         BNZ   *+18
         TM    *+X'53',X'12'
         BM    *+10
         AP    *+X'2C'(2),=P'1'
         LA    1,*+X'24'
         CP    *+X'42'(2),0(2,1)
         BNH   *+X'3E'
         SP    *+X'38'(2),0(2,1)
         AP    *+14(2),=P'1'
         LA    1,2(1)
         B     *-X'1A'
         DC    P'01,31,28,31,30,31,30,31,31,30,31,30,31'
         DC    X'20204B20204B2020'
         DC    F'0'
         OI    *-X'25',15
         UNPK  *-13(2),*-X'2A'(2)
         OI    *-11,15
         UNPK  *-X'1A'(2),*-X'10'(2)
         LA    1,*-X'20'        R1 = ADR. DES DATUMS XX.XX.XX
*
         MEND
         EJECT
         MACRO
&NAME    ENDE
&NAME    L     13,4(13)
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
*
         MEND
         EJECT
         MACRO
&N     BEGINN   &R1,&R2,&R3,&R4,&SA=SAVEAREA,&GEN=NOGEN
         GBLB  &L26REG
         LCLC  &TAG,&MON,&JAHR,&DATE
&TAG     SETC  '&SYSDATE'(4,2)
&MON     SETC  '&SYSDATE'(1,2)
&JAHR    SETC  '&SYSDATE'(7,2)
&DATE    SETC  '&TAG..'.'&MON..'.'&JAHR'
         MNOTE ' '
         CNOP  0,4
&N       STM   14,12,12(13)
         BALR  1,0
         L     1,98+16(1)
         SPM   1
         ST    1,8(13)
         ST    13,4(1)
         B     76(1)
         DC    CL24' &SYSTIME &DATE &SYSECT'
         AGO   .P08
.P05     AIF   ('&N' EQ '').P06
         DC    CL8'&N'
         AGO   .P08
.P06     DC    CL8'BEGINN'
.P08     AIF   ('&R1' NE '').P08C
         USING *,13
         AGO   .P09
.P08C    USING *,&R1
.P09     AIF   ('&R2' EQ '').P10
         USING *+4095,&R2
         AIF   ('&R3' EQ '').P10
         USING *+2*4095,&R3
         AIF   ('&R4' EQ '').P10
         USING *+3*4095,&R4
.P10     ANOP
&SA      DC    18F'0',B'1100',AL3(*-73)
         LR    13,1
         L     1,4(1)
         L     1,24(1)
         AIF   ('&R1' NE '').P20
         AIF   ('&R2' EQ '').P50
         LA    &R2,4095(13)
         AGO   .P40
.P20     AIF   ('&R1' EQ '13' OR '&R1' EQ 'R13').P30
         LR    &R1,13
.P30     AIF   ('&R2' EQ '').P50
         LA    &R2,4095(&R1)
.P40     AIF   ('&R3' EQ '').P50
         LA    &R3,4095(&R2)
         AIF   ('&R4' EQ '').P50
         LA    &R4,4095(&R3)
.P50     ANOP
         AIF   ('&R1' EQ '').MEND
         AIF   ('&R1'(1,1) NE 'R').MEND
&L26REG  SETB  1
         MNOTE ' '
R0       EQU   0                  RESERVED
R1       EQU   1                  RESERVED ODER ADR.DER PARMLISTE
R2       EQU   2                  RESERVED
R3       EQU   3                  WORKREGISTER
R4       EQU   4                  WORKREGISTER
R5       EQU   5                  WORKREGISTER
R6       EQU   6                  WORKREGISTER
R7       EQU   7                  WORKREGISTER
R8       EQU   8                  WORKREGISTER
R9       EQU   9                  WORKREGISTER
R10      EQU   10                 WORKREGISTER ODER 4.BASISREGISTER
R11      EQU   11                 WORKREGISTER ODER 3.BASISREGISTER
R12      EQU   12                 WORKREGISTER ODER 2.BASISREGISTER
R13      EQU   13                 1.BASISREGISTER
R14      EQU   14                 BAL-REGISTER
R15      EQU   15                 RETURNCODE-REGISTER
         MNOTE ' '
GR0      EQU   0                  GLEITKOMMAREG. 0
GR2      EQU   2                  GLEITKOMMAREG. 2
GR4      EQU   4                  GLEITKOMMAREG. 4
GR6      EQU   6                  GLEITKOMMAREG. 6
.MEND    MNOTE ' '
         PRINT &GEN
         MEND
         EJECT
         MACRO
         SMFREC &SUB=,&DSECT=YES
         AIF   ('&SUB' NE '').SOK
         MNOTE 12,'*** NO SUBTYPE-RECORD PARAMETER CODED ***'
         MEXIT
.SOK     ANOP
         SPACE 2
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
*     THIS DUMMY SECTION EXPLAINS THE SMF RECORD 150 ( EQUATE 'RECT') *
*     IF YOU CHANGE IT OR ADD AN ADDITIONAL SUBTYPE TO IT             *
*     RECOMPILE THE ENTIRE MODULS :                                   *
*                                                                     *
*                    SUBTYPE 1 : IEECVXIT,X002200,SMF-STATISTIC (FB)  *
*                                                                     *
*                    SUBTYPE 2 : X001905                              *
*                                                                     *
*                    SUBTYPE 3 : X001520,QX01521,QX01528,QX01529      *
*                                                                     *
*                    SUBTYPE 4 : IEFDB401                             *
*                                                                     *
*                    SUBTYPE 5 : SDTA --> PX001660,QX01660,...        *
*                                                                     *
*                    SUBTYPE 6 : X001295  (VIRTUAL STORAGE LAYOUT)    *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 3
         DS    0D
         AIF   ('&DSECT' NE 'YES').NODSECT
SMFREC   DSECT
.NODSECT ANOP
         AIF   ('&SUB' EQ '5').S5
REC54    DS    0H
RECVS2   EQU   2                       VS2
RECT     EQU   150                     RECORD TYPE
RECL     DC    AL2(REC)                 CONTAINS LENGTH
         DC    H'0'                    RESERVED
RECHEAD  DC    AL1(RECVS2)             VS2
RECTYPE  DC    AL1(RECT)               REC 150
RECTIMEB DS    BL4                     TIME BIN
RECDATE  DS    PL4                     DATE
RECSID   DS    CL4                     SYSTEM ID
         AIF   ('&SUB' EQ '1').S1
         AIF   ('&SUB' EQ '2').S2
         AIF   ('&SUB' EQ '3').S3
         AIF   ('&SUB' EQ '4').S4
         AIF   ('&SUB' EQ '6').S6
         MNOTE 12,'*** INVALID SUBTYPE CODED ***'
         MEXIT
.S1      ANOP
RECSTYPE DC    AL1(SUBTYPE)            SUBTYPE
SUBTYPE  EQU   1                       SUBTYPE ID IS 1
RECTIMED DC    PL4'0'                  TIME DEC
RECUNIT  DC    CL3' '                  UNIT
RECVOL   DC    CL6' '                  VOLUME
RECJOBN  DC    CL8' '                  JOBNAME
RECSTEP  DC    CL8' '                  STEP NAME
RECPGM   DC    CL8' '                  PROGRAMM NAME
RECDSN   DC    CL44' '                 DSNAME
         DC    BL1'0'                  RESERVED
REC      EQU   *-RECL                   LENGTH
         MEXIT
.S2      ANOP
RECSTYPE DC    AL1(SUBTYPE)            SUBTYPE
SUBTYPE  EQU   2
RECDBDNM DC    CL8' '                  DBD - NAME
RECDDNAM DC    CL8' '                  DD - NAME
RECDSNAM DC    CL44' '                 DS - NAME
RECSNBL  DC    CL7' '                  NUMBER OF BLOCKS IN DATASET
RECBLNK1 DC    C' '                    FILLER
RECACT   DC    CL5' '                  ACTUAL NBR OF FREE BLOCKS
RECBLNK2 DC    C' '                    FILLER
RECMIN   DC    CL5' '                  MIN NUMBER OF FREE BLOCKS
RECBLNK3 DC    C' '                    FILLER
RECMAX   DC    CL5' '                  MAX. NUMBER OF FREE BLOCKS
RECDUMMY DC    CL10' '                 RESERVED
REC      EQU   *-RECL                   LENGTH
         MEXIT
.S3      ANOP
RECSTYPE DC    AL1(SUBTYPE)            SUBTYPE
SUBTYPE  EQU   3
RECJOBNM DC    CL8' '                  JOBNAME
RECSTIME DC    BL4'0'                  START TIME
RECSDATE DC    PL4'0'                  START DATE
RECFVOL  DC    CL6' '                  FROM VOLUME
RECTVOL  DC    CL6' '                  TARGET VOL
RECNVOL  DC    CL6' '                  NEW VOLSER
RECNDSN  DC    AL2(0)                  NUMBER DATASETS COPIED
RECNDIO  DC    AL2(0)                  NUMBER DATASETS WITH I/O ERROR
RECNTRKS DC    AL4(0)                  NUMBER TRACKS COPIED
RECRECZ  DC    AL4(0)                  NUMBER RECORD ZERO
RECMODE  DC    X'0'                    COPY MODE
RECCOPY  EQU   128                     COPY WHOLE DISK
RECPRTY  EQU   64                      COPY WITH PRTY
RECMERG  EQU   32                      COPY WITH MERGE
RECCOMP  EQU   16                      COPY COMPACT
RECONLY  EQU   8                       COPY ONLY
RECEXCL  EQU   4                       COPY EXCL.
RECCSYS  EQU   2                       TEMP COPY
RECRET   DC    B'0'                    RETURN CODE
REC      EQU   *-RECL                   LENGTH
         MEXIT
.S5      ANOP
S5R54    DS    0H
S5RVS2   EQU   2                       VS2
S5RT     EQU   150                     RECORD TYPE
S5RCL    DC    AL2(S5R)                 CONTAINS LENGTH
         DC    H'0'                    RESERVED
S5RHEAD  DC    AL1(S5RVS2)             VS2
S5RTYPE  DC    AL1(S5RT)               REC 150
S5RTIMEB DS    BL4                     TIME BIN
S5RDATE  DS    PL4                     DATE
S5RSID   DS    CL4                     SYSTEM ID
S5RSTYPE DC    AL1(S5RTYPE5)            SUBTYPE
S5RTYPE5 EQU   5
S5RXRES  DS    X                       RESERVED
S5RETIME DS    XL4                     END TIME
S5REDATE DS    XL4                     END DATE
S5RXBYTE DS    XL4                     # BYTES TRANS.
S5RXCNT  DS    XL4                     # RECORDS TRANSFERED
S5RXDSN  DS    CL44                    DSNAME TRANSFERED
S5RORGN  DS    CL1                     ORIGN RZ
S5RDEST  DS    CL1                     DSTINATION RZ
S5RXLREC DS    XL2                     LRECL OF DSN
S5RXBLKS DS    XL2                     BLKSIZE OF DSN
S5RXRECF DS    X                       RECFM OF DSN
S5RXFLAG DS    X                       FLAGS
S5RXRST  EQU   128                     RESTARTED
S5RXINT  EQU   64                      INTERUPTED
S5RXSTP  EQU   32                      STOPPED
S5RXERR  EQU   16                      STOPPED
S5RXJOBN DS    CL8                     JOBNAME TO SUBMIT
S5RXCNTU DS    XL4                     # USER RECS
S5RXTRTB DS    CL7                     TRTAB FOR DATASET
S5RXAPPL DS    CL8                     APPLICATION NAME
         DS    5X                      RESERVED
S5R      EQU   *-S5RCL                 LENGTH
         MEXIT
.S4      ANOP
RECSTYPE DC    AL1(SUBTYPE)            SUBTYPE
SUBTYPE  EQU   4
RECJOBNM DC    CL8' '                  JOBNAME
RECDSN   DC    CL44' '                 DSNAME
RECSUBA  DC    CL2' '                  SUBAREA
REC      EQU   *-RECL                  LENGTH
         MEXIT
.S6      ANOP
RECSTYPE DC    AL1(SUBTYPE)            SUBTYPE
SUBTYPE  EQU   6                       SUBTYPE ID IS 6
RECTIMED DC    PL4'0'                  TIME DEC
RECJOBNM DC    CL8' '                  JOBNAME
RECNUCA  DC    PL4'0'                  NUC. CONTROL BLOCKS (DEC.)
RECHNUCA DC    BL4'0'                  NUC. CONTROL BLOCKS (HEX.)
RECFLPA  DC    PL4'0'                  FIXED LPA           (DEC.)
RECHFLPA DC    BL4'0'                  FIXED LPA           (HEX.)
RECBLDL  DC    PL4'0'                  FIXED BLDL          (DEC.)
RECHBLDL DC    BL4'0'                  FIXED BLDL          (HEX.)
RECNUCB  DC    PL4'0'                  CVTNUCB             (DEC.)
RECHNUCB DC    BL4'0'                  CVTNUCB             (HEX.)
RECSRSZ  DC    PL4'0'                  SYSTEM REGION SIZE
RECVR    DC    PL4'0'                  V=R REGION          (DEC.)
RECHVR   DC    BL4'0'                  V=R REGION          (HEX.)
RECVRSZ  DC    PL4'0'                  V=R REGION SIZE
RECREAL  DC    PL4'0'                  CVTREAL             (DEC.)
RECHREAL DC    BL4'0'                  CVTREAL             (HEX.)
RECUREG  DC    PL4'0'                  USER REGION SIZE
RECLSQA  DC    PL4'0'                  LSQA                (DEC.)
RECHLSQA DC    BL4'0'                  LSQA                (HEX.)
RECSHRV  DC    PL4'0'                  CVTSHRVM            (DEC.)
RECHSHRV DC    BL4'0'                  CVTSHRVM            (HEX.)
RECCSASZ DC    PL4'0'                  CSA SIZE
RECCSAU  DC    PL4'0'                  CSA USED
RECCSAT  DC    PL4'0'                  TOP OF CSA          (DEC.)
RECHCSAT DC    BL4'0'                  TOP OF CSA          (HEX.)
RECPBLD  DC    PL4'0'                  PAGEABLE BLDL       (DEC.)
RECHPBLD DC    BL4'0'                  PAGEABLE BLDL       (HEX.)
RECLPD   DC    PL4'0'                  CVTLPDIR            (DEC.)
RECHLPD  DC    BL4'0'                  CVTLPDIR            (HEX.)
RECPLPA  DC    PL4'0'                  PLPA SIZE
RECSQA   DC    PL4'0'                  SQA                 (DEC.)
RECHSQA  DC    BL4'0'                  SQA                 (HEX.)
RECSQASZ DC    PL4'0'                  SQA SIZE
RECSQAU  DC    PL4'0'                  SQA USED
RECMZ00  DC    PL4'0'                  CVTMZ00             (DEC.)
RECHMZ00 DC    BL4'0'                  CVTMZ00             (HEX.)
REC229   DC    PL4'0'                  SUBPOOL 229
REC230   DC    PL4'0'                  SUBPOOL 230
REC236   DC    PL4'0'                  SUBPOOL 236
REC237   DC    PL4'0'                  SUBPOOL 237
REC255   DC    PL4'0'                  SUBPOOL 255
RECSTOT  DC    PL4'0'                  SUBPOOL TOTAL
REC      EQU   *-RECL                  LENGTH
         MEND
         EJECT
*
*
*  THIS PROGRAM SHOWS A VIRTUAL STORAGE LAYOUT.
*
*  IT USES THE MACRO SMFWTM TO WRITE AN SMF RECORD
*  (TYPE 150 SUBTYPE 6) TO THE SMF DATASET. (ONLY IF
*  THIS TYPE IS RECORDED)
*
*  IF PARM='J=XXXXXXXX' OR PARM='A=XXX' IS SPECIFIED
*  AN SRB IS SCHEDULED INTO THE TARGET ADDRESS SPACE
*  TO COUNT THE SIZE OF SUBPOOLS 229,230,236,237 AND 255.
*
*  THE POINTER TO THE RESIDENT BLDL LIST ADDRESS IN IGC018
*  IS OBTAINED FROM THE SBV USER SVC 211.
*  (TYPE 2 USER SVC AS PART OF NUCLEUS)
*
*  AUTHORIZATION IS DONE BY THE SBV USER SVC 210.
*  (TYPE 4 USER SVC IN LPALIB. CONSISTS OF A BRANCH
*  REGISTER 15 INSTRUCTION)
*
*
         EJECT
X001295  CSECT
         BEGINN R13,R12
         SPACE 2
         USING SMFREC,R3
         USING SRBSECT,R7
         USING CVT,R11
         SPACE 2
         OPEN  (PRINT,OUTPUT)
         SPACE 2
         L     R1,SAVEAREA+4           GET HIGHER SAVEAREA
         L     R1,24(,R1)              GET REG1
         L     R2,0(,R1)               POINT TO PARM INFO
         LH    R3,0(,R2)               GET PARM LENGTH
         LTR   R3,R3                   Q. PARM SPECIFIED
         BZ    COMMON                  A. NO
         CLC   2(2,R2),=C'J='          Q. SELECT JOBNAME
         BE    SELJOB                  A. YES
         CLC   2(2,R2),=C'A='          Q. SELECT ASID
         BNE   COMMON                  A. NO
         SPACE 2
SELASID  DS    0H
         LA    R4,2                    A=
         SR    R3,R4                   SUBTRACT FROM PARM LENGTH
         LTR   R3,R3                   Q. ASID SPECIFIED
         BNZ   ASID1                   A. YES
         MVC   DRUBER,NOASID           MOVE MESSAGE TEXT
         B     ERROR
         SPACE 2
ASID1    DS    0H
         MVC   HEAD1+46(8),=CL8'ASID'  "ASID" IN HEAD LINE
         C     R3,=F'1'                Q. ONE CHARACTER
         BNE   ASID2                   A. NO
         MVC   DW+7(1),4(R2)           MOVE ASID (X)
         MVC   HEAD1+53(1),4(R2)       ASID IN HEAD LINE
         B     ASIDCONV
         SPACE 2
ASID2    DS    0H
         C     R3,=F'2'                Q. TWO CHARACTERS
         BNE   ASID3                   A. NO
         MVC   DW+6(2),4(R2)           MOVE ASID (XX)
         MVC   HEAD1+52(2),4(R2)       ASID IN HEAD LINE
         B     ASIDCONV
         SPACE 2
ASID3    DS    0H
         MVC   DW+5(3),4(R2)           MOVE ASID (XXX)
         MVC   HEAD1+51(3),4(R2)       ASID IN HEAD LINE
         SPACE 2
ASIDCONV DS    0H
         TR    DW+5(3),TRTAB           TRANSLATE
         MVO   DW+7(1),DW+6(1)              ASID
         MVN   DW+6(1),DW+5                   FOR COMPARE
         MVC   COMPASID,DW+6           SAVE ASID
         OI    PARMFLAG,X'80'          SET SELECTION FLAG
         B     COMMON
         SPACE 2
SELJOB   DS    0H
         LA    R4,2                    J=
         SR    R3,R4                   SUBTRACT FROM PARM LENGTH
         LTR   R3,R3                   Q. JOBNAME SPECIFIED
         BNZ   JOBNM                   A. YES
         MVC   DRUBER,NOJOB            MOVE MESSAGE TEXT
         B     ERROR
         SPACE 2
JOBNM    DS    0H
         BCTR  R3,0
         EX    R3,MOVEPARM             SAVE JOBNAME
         OI    PARMFLAG,X'08'          SET SELECTION FLAG
         SPACE 2
COMMON   DS    0H
         LA    R0,REC                  LENGTH OF SMF RECORD
         GETMAIN RC,LV=(0)             GET STORAGE
         LTR   R15,R15                 Q. STORAGE OBTAINED
         BZ    SMFTEST                 A. YES
         MVC   DRUBER,NOSTOR           MOVE MESSAGE TEXT
         B     ERROR
         SPACE 2
SMFTEST  DS    0H
         LR    R3,R1
         XC    0(REC,R3),0(R3)         CLEAR STORAGE
         SMFRTEST RECTYPE=RECT
         LTR   R15,R15                 Q. IS RECORD RECORDED
         BNZ   PROCESS                 A. NO
         CLI   PARMFLAG,X'00'          Q. PARM SPECIFIED
         BE    PROCESS                 A. NO
         MVI   SMFFLAG,X'FF'           SET SMF FLAG
         LA    R14,REC                 GET RECORD LENGTH
         STH   R14,RECL                STORE RECORD LENGTH
         MVI   RECHEAD,RECVS2          FILL SMF HEADER
         MVI   RECTYPE,RECT            FILL SMF HEADER
         MVI   RECSTYPE,SUBTYPE        FILL SMF HEADER
         TIME  BIN
         STCM  R0,B'1111',RECTIMEB     FILL SMF HEADER
         STCM  R1,B'1111',RECDATE      FILL SMF HEADER
         SPACE 2
PROCESS  DS    0H
         SPACE 2
         OSDATUM
         SPACE 1
         CLI   SMFFLAG,X'00'           Q. COLLECT SMF DATA
         BE    TITLINE                 A. NO
         MVC   RECTIMED,TIME           FILL SMF HEADER
         SPACE 2
TITLINE  DS    0H
         SPACE 2
         MVC   HEAD1+36(8),0(R1)       DATE
         MVC   HEAD1+28(6),=X'4021204B2020'
         ED    HEAD1+28(6),TIME        TIME
         L     R11,16                  POINT TO CVT
         L     R4,CVTSMCA              POINT TO SMCA
         MVC   HEAD1+56(4),16(R4)      SID
         CLI   SMFFLAG,X'00'           Q. COLLECT SMF DATA
         BE    NUC                     A. NO
         MVC   RECSID,16(R4)           FILL SMF HEADER
         SPACE 2
*        VIRTUAL STORAGE LAYOUT
         SPACE 2
NUC      DS    0H
         L     R8,372(0)               GET EP OF IEAVNIP0 FROM PSA
         LA    R1,RECNUCA-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   NUCADR(6),VIRT+1
         LA    R1,RECHNUCA-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   NUCADR+51(9),HEX
         MVC   NUCADR+53(6),DW+1
         L     R4,CVTMZ00              CVTMZ00
         ST    R4,FLPA                 INITIALIZE FLPA FIELD
         L     R4,CVTQLPAQ             CVTQLPAQ
         L     R5,0(,R4)               CDE
         SPACE 2
CDESCAN  DS    0H
         CLC   17(3,R5),CVTLPDIR       Q. EP BELOW PLPA
         BNL   CDENEXT                 A. NO
         TM    29(R5),X'20'            Q. HAS EXTENT LIST BEEN BUILT
         BNO   CDENEXT                 A. NO
         L     R4,FLPA                 GET FLPA FIELD
         L     R8,20(,R5)              EXTENT LIST
         L     R8,12(,R8)              ADDRESS OF MAIN STORAGE BLOCK
         CLR   R4,R8                   Q. ADDRESS LOWER
         BL    CDENEXT                 A. NO
         ST    R8,FLPA                 A. YES - STORE NEW ADDRESS
         SPACE 2
CDENEXT  DS    0H
         ICM   R6,B'1111',0(R5)        Q. IS THERE ANOTHER CDE
         BZ    NUCLEUS                 A. NO
         LR    R5,R6                   ADDRESS OF CDE IN REG5
         B     CDESCAN
         SPACE 2
NUCLEUS  DS    0H
         L     R8,FLPA                 GET FLPA FIELD
         LA    R1,RECFLPA-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   FLPAADR(6),VIRT+1
         LA    R1,RECHFLPA-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   FLPAADR+51(9),HEX
         MVC   FLPAADR+53(6),DW+1
         L     R8,CVTNUCB              CVTNUCB
         LA    R1,RECNUCB-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CVT80(6),VIRT+1
         LA    R1,RECHNUCB-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   CVT80+51(9),HEX
         MVC   CVT80+53(6),DW+1
         SPACE 2
VVVR     DS    0H
         L     R4,CVTREAL              CVTREAL
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECREAL-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CVT11C(6),VIRT+1
         LA    R1,RECHREAL-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   CVT11C+51(9),HEX
         MVC   CVT11C+53(6),DW+1
         L     R5,CVTGDA               GLOBAL DATA AREA
         L     R5,12(,R5)              V=R PQE PTR
         L     R5,20(,R5)              SIZE OF V=R REGION
         LR    R8,R5                   FOR CONVERT
         LA    R1,RECVRSZ-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   VRSIZE+37(6),VIRT+1
         SLR   R4,R5                   CALCULATE BEGIN OF V=R REGION
         LR    R8,R4                   FOR CONVERT
         ST    R8,VRSAVE               SAVE
         LA    R1,RECVR-SMFREC(,R3)    POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   VR(6),VIRT+1
         MVC   VR1+37(6),VIRT+1
         LA    R1,RECHVR-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   VR+51(9),HEX
         MVC   VR+53(6),DW+1
         L     R5,CVTNUCB              CVTNUCB
         SLR   R4,R5                   CALCULATE SYSTEM REGION SIZE
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECSRSZ-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   SRSIZE+37(6),VIRT+1
         SPACE 2
CSASZ    DS    0H
         L     R4,CVTSHRVM             CVTSHRVM
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECSHRV-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CVT1A0(6),VIRT+1
         LA    R1,RECHSHRV-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   CVT1A0+51(9),HEX
         MVC   CVT1A0+53(6),DW+1
         L     R5,CVTGDA               GLOBAL DATA AREA
         L     R6,8(,R5)               CSA PQE PTR
         L     R6,20(,R6)              SIZE OF CSA
         ST    R6,SIZECSA              SAVE
         ALR   R4,R6                   TOP OF CSA
         LR    R8,R6                   FOR CONVERT
         LA    R1,RECCSASZ-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CSASIZE+37(6),VIRT+1
         LR    R7,R4                   SAVE
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECCSAT-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CSATOP(6),VIRT+1
         LA    R1,RECHCSAT-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   CSATOP+51(9),HEX
         MVC   CSATOP+53(6),DW+1
         L     R5,52(,R5)              FIRST CSA SPQE PTR
         SR    R8,R8                   ZERO REG8
         SPACE 2
CSASPQE  DS    0H
         BAL   R10,CSADQE
         ICM   R6,B'1111',0(R5)        Q. IS THERE ANOTHER SPQE
         BZ    CSA                     A. NO
         LR    R5,R6                   ADDRESS OF SPQE IN REG5
         B     CSASPQE
         SPACE 2
CSADQE   DS    0H
         ICM   R6,B'1111',4(R5)        ADDRESS OF FIRST DQE
         BZR   R10                     ZERO - CHECK NEXT SPQE
         SPACE 2
CSASCAN  DS    0H
         L     R9,12(,R6)              GET LENGTH OF THIS QUEUE ELEMENT
         ALR   R8,R9                   ADD
         ICM   R2,B'1111',4(R6)        Q. IS THERE ANOTHER DQE
         BZR   R10                     A. NO
         LR    R6,R2                   ADDRESS OF DQE IN REG6
         B     CSASCAN                 TRY AGAIN
         SPACE 2
CSA      DS    0H
         ST    R8,USEDCSA              SAVE
         LA    R1,RECCSAU-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         L     R6,SIZECSA              CSA SIZE
         L     R8,USEDCSA              CSA USED
         BAL   R10,PERCENT             CALCULATE PERCENTAGE
         MVC   CSASIZE+27(3),VIRT+1
         SPACE 2
VIRTUAL  DS    0H
         L     R4,CVTMZ00              CVTMZ00
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECMZ00-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         XC    DW,DW
         LA    R6,1024
         ST    R8,FW
         SRDA  R8,32
         DR    R8,R6
         LA    R9,1(,R9)               ADJUST
         CVD   R9,DW
         CLI   SMFFLAG,X'00'           Q. SMF DATA COLLECTED
         BE    VIRTEDIT                A. NO
         MVC   0(4,R1),DW+4            FILL SMF RECORD
         SPACE 2
VIRTEDIT DS    0H
         SPACE 2
         MVC   VIRT(7),=X'4020204B202120'
         ED    VIRT(7),DW+5
         MVC   CVTA4(6),VIRT+1
         L     R5,CVTGDA               GLOBAL DATA AREA
         L     R5,24(,R5)              SQA SPQE PTR
         SR    R8,R8                   ZERO REG8
         SPACE 2
SQASZ    DS    0H
         L     R6,4(,R5)               ADDRESS OF FIRST DQE
         SPACE 2
SQADQE   DS    0H
         L     R9,12(,R6)              GET LENGTH OF THIS QUEUE ELEMENT
         ALR   R8,R9                   ADD
         ICM   R2,B'1111',4(R6)        Q. IS THERE ANOTHER DQE
         BZ    SQAFREE                 A. NO
         LR    R6,R2                   ADDRESS OF DQE IN REG6
         B     SQADQE                  TRY AGAIN
         SPACE 2
SQAFREE  DS    0H
         ST    R8,SIZESQA              SAVE
         L     R5,4(,R5)               ADDRESS OF FIRST DQE
         SR    R8,R8                   ZERO REG8
         SPACE 2
SQAFQEP  DS    0H
         ICM   R6,B'1111',0(R5)        ADDRESS OF FIRST FREE AREA
         BZ    SQANEXT
         SPACE 2
SQAFQE   DS    0H
         L     R9,4(,R6)               GET NUMBER OF BYTES IN FREE AREA
         ALR   R8,R9                   ADD
         ICM   R2,B'1111',0(R6)        Q. IS THERE ANOTHER FQE
         BZ    SQANEXT                 A. NO
         LR    R6,R2                   ADDRESS OF FQE IN REG6
         B     SQAFQE                  TRY AGAIN
         SPACE 2
SQANEXT  DS    0H
         ICM   R2,B'1111',4(R5)        Q. IS THERE ANOTHER DQE
         BZ    SQA                     A. NO
         LR    R5,R2                   ADDRESS OF DQE IN REG5
         B     SQAFQEP                 TRY AGAIN
         SPACE 2
SQA      DS    0H
         ST    R8,FREESQA              SAVE
         L     R5,SIZESQA              TOTAL SIZE
         LR    R8,R5                   FOR CONVERT
         LA    R1,RECSQASZ-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   SQASIZE+37(6),VIRT+1
         SLR   R4,R5                   CALCULATE BEGIN OF SQA
         LA    R4,1(,R4)               ADJUST
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECSQA-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   SQAADR(6),VIRT+1
         LA    R1,RECHSQA-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   SQAADR+51(9),HEX
         MVC   SQAADR+53(6),DW+1
         L     R9,FREESQA              TOTAL FREE
         SLR   R5,R9                   SUBTRACT FREE FROM TOTAL
         LA    R5,1024(,R5)            ROUND
         ST    R5,USEDSQA              SAVE TOTAL USED
         LR    R8,R5                   FOR CONVERT
         LA    R1,RECSQAU-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         L     R6,SIZESQA              SQA SIZE
         L     R8,USEDSQA              SQA USED
         BAL   R10,PERCENT             CALCULATE PERCENTAGE
         MVC   SQASIZE+27(3),VIRT+1
         SPACE 2
LOC211   DS    0H
         L     R5,CVTABEND             SCVT
         L     R5,132(,R5)             SVCTABLE ORIGIN
         L     R5,211*8(,R5)           EP OF IGC211
         CLC   STC,4(R5)               Q. CORRECT IGC211
         BE    BLDL                    A. YES
         MVC   DRUBER,NOSVC            WRONG IGC211
         MVI   LOGIC,X'FF'             SET ERROR FLAG
         B     ERROR
         SPACE 2
BLDL     DS    0H
         SR    R6,R6                   ZERO REG8
         ICM   R6,B'0111',9(R5)        POINTER TO RESIDENT BLDL LIST
         BZ    LPDIR                   ADDRESS IN IGC018
         ICM   R8,B'1111',0(R6)        Q. RES BLDL LIST
         BZ    LPDIR                   A. NO
         CLR   R8,R4                   Q. ADDRESS IN SQA
         BH    LPDIR                   A. YES
         CLR   R8,R7                   Q. PAGEABLE BLDL
         BH    PBLDL                   A. YES
         LA    R1,RECBLDL-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   BLDLADR+7(5),=C'K -->'
         MVC   BLDLADR(6),VIRT+1
         LA    R1,RECHBLDL-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   BLDLADR+51(9),HEX
         MVC   BLDLADR+53(6),DW+1
         B     LPDIR
         SPACE 2
PBLDL    DS    0H
         LA    R1,RECPBLD-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   PBLDLADR+7(5),=C'K -->'
         MVC   PBLDLADR(6),VIRT+1
         LA    R1,RECHPBLD-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   PBLDLADR+51(9),HEX
         MVC   PBLDLADR+53(6),DW+1
         SPACE 2
LPDIR    DS    0H
         SR    R5,R5
         ICM   R5,7,CVTLPDIR           CVTLPDIR
         LR    R8,R5                   FOR CONVERT
         LA    R1,RECLPD-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   CVT169(6),VIRT+1
         LA    R1,RECHLPD-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   CVT169+51(9),HEX
         MVC   CVT169+53(6),DW+1
         SLR   R4,R5
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECPLPA-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   PLPASIZE+37(6),VIRT+1
         CLI   PARMFLAG,X'80'          Q. SELECT ASID
         BE    ASIDSEL                 A. YES
         MVC   HEAD1+46(8),JOBNAME     JOBNAME IN HEAD LINE
         CLI   PARMFLAG,X'00'          Q. PARM SPECIFIED
         BE    USERREG                 A. NO
         SPACE 2
ASIDSEL  DS    0H
         L     R7,CVTASVT              ASVTASVT
         L     R0,516(,R7)             ASVTMAXU
         LA    R4,528(,R7)             ASVTENTY
         SPACE 2
ASCBLOOP DS    0H
         TM    0(R4),X'80'             Q. ASCB ASSIGNED
         BO    NEXTASCB                A. NO - GET NEXT ASCB
         L     R1,0(,R4)               ASCB
         ICM   R5,B'1111',172(R1)      JOBNAME FOR INITIATED TASKS
         BNZ   LOOK
         ICM   R5,B'1111',176(R1)      JOBNAME FOR START/MOUNT/LOGON
         BNZ   LOOK
         LA    R5,DEFJOBNM             DEFAULT JOBNAME "STARTING"
         SPACE 2
LOOK     DS    0H
         CLI   PARMFLAG,X'80'          Q. ASID SPECIFIED
         BE    LOOKASID                A. YES
         CLC   0(8,R5),JOBNAME         Q. SAME JOBNAME
         BE    ACTIVE                  A. YES
         B     NEXTASCB
         SPACE 2
LOOKASID DS    0H
         CLC   36(2,R1),COMPASID       Q. SAME ASID
         BE    ACTIVE                  A. YES
         SPACE 2
NEXTASCB DS    0H
         LA    R4,4(,R4)               NEXT ASCB
         BCT   R0,ASCBLOOP             LOOP THROUGH ALL ASCBS
         MVC   DRUBER,NOTACT           TASK NOT ACTIVE
         MVI   LOGIC,X'FF'             SET ERROR FLAG
         B     ERROR
         SPACE 2
ACTIVE   DS    0H
         CLI   SMFFLAG,X'00'           Q. SMF DATA COLLECTED
         BE    ACT                     A. NO
         MVC   RECJOBNM,0(R5)          FILL SMF RECORD
         SPACE 2
ACT      DS    0H
         SPACE 2
         ST    R1,ASCBSAVE             SAVE ASCB ADDRESS
         LA    R15,SVC210              POINT TO SVC210 CODE
         LR    R0,R12                  SAVE REG12
         LR    R1,R3                   SAVE REG3
         SVC   210
         CLI   LOGIC,X'00'             Q. ERROR
         BNE   ERROR                   A. YES
         CLI   PARMFLAG,X'00'          Q. PARM SPECIFIED
         BNE   DISPLAY                 A. YES
         SPACE 2
USERREG  DS    0H
         L     R4,CVTSHRVM             TOP
         LR    R8,R4                   FOR CONVERT
         BAL   R10,CONV
         MVC   LSQAADR1+37(6),VIRT+1
         L     R9,VRSAVE               BOTTOM
         SLR   R4,R9
         LR    R8,R4                   FOR CONVERT
         SPACE 2
UREGSIZE DS    0H
         BAL   R10,CONV
         MVC   UREG+37(6),VIRT+1       USER REGION
         SPACE 2
DISPLAY  DS    0H
         MVC   HEAD,HEAD1              MOVE HEAD LINE
         LA    R6,(TABEND-TABANF)/80
         LA    R7,TABANF
         SPACE 2
LAYOUT   DS    0H
         MVC   DRUBER(80),0(R7)
         PUT   PRINT,DRUBER
         LA    R7,80(R7)
         BCT   R6,LAYOUT
         SPACE 2
RELEASE  DS    0H
         SPACE 2
         LA    R0,REC                  LENGTH OF SMF RECORD
         FREEMAIN RC,LV=(0),A=(3)      FREE STORAGE
         SPACE 2
CLOSEDS  DS    0H
         SPACE 2
         CLOSE PRINT
         ENDE
         SPACE 2
ERROR    DS    0H
         SPACE 2
         PUT   PRINT,DRUBER            WRITE ERROR MESSAGE
         CLI   LOGIC,X'FF'             Q. ERROR
         BE    RELEASE                 A. YES
         B     CLOSEDS                 A. NO
         EJECT
         SPACE 2
CONV     DS    0H
         XC    DW,DW
         LA    R6,1024
         ST    R8,FW
         SRDA  R8,32
         DR    R8,R6
         CVD   R9,DW
         CLI   SMFFLAG,X'00'           Q. SMF DATA COLLECTED
         BE    CONVEDIT                A. NO
         MVC   0(4,R1),DW+4            FILL SMF RECORD
         SPACE 2
CONVEDIT DS    0H
         SPACE 2
         MVC   VIRT(7),=X'4020204B202120'
         ED    VIRT(7),DW+5
         BR    R10
         SPACE 2
PERCENT  DS    0H
         XC    DW,DW
         SRDA  R8,32
         M     R8,=F'100'
         DR    R8,R6
         CVD   R9,DW
         MVC   VIRT(4),=X'40202120'
         ED    VIRT(4),DW+6
         BR    R10
         SPACE 2
HEXCONV  DS    0H
         CLI   SMFFLAG,X'00'           Q. SMF DATA COLLECTED
         BE    HEXTRAN                 A. NO
         L     R8,FW
         ST    R8,0(R1)                FILL SMF RECORD
         SPACE 2
HEXTRAN  DS    0H
         SPACE 2
         XC    DW,DW
         UNPK  DW+1(7),FW+1(4)
         TR    DW+1(6),HEXTABLE-240
         BR    R10
         SPACE 2
SPSIZE   DS    0H
         ST    R5,REGSAVE
         SLL   R4,2                    MULTIPLY BY FOUR
         LA    R5,SRBPOOL              ADDRESS OF SUBPOOL TABLE
         LA    R5,0(R4,R5)             POINT TO SUBPOOL ELEMENT
         L     R8,0(,R5)               GET SUBPOOL ELEMENT
         L     R9,SPTOTAL
         ALR   R9,R8                   UPDATE
         ST    R9,SPTOTAL
         BAL   R10,CONV                CONVERT
         L     R5,REGSAVE
         BR    R5
         EJECT
         SPACE 2
SVC210   DS    0D
         USING *,R2
         LR    R2,R15                  ESTABLISH ADDRESSABILITY
         LR    R11,R14                 SAVE RETURN ADDRESS
         LR    R12,R0                  RESTORE REG12
         LR    R3,R1                   RESTORE REG3
         LA    R14,ESRB-SRBSECT        SRB LENGTH
         GETMAIN RC,LV=(14),SP=245     GETMAIN IN SQA
         LTR   R15,R15                 Q. SUCCESSFUL
         BZ    INITSRB                 A. YES
         MVC   DRUBER,NOSTOR           MOVE ERROR MESSAGE
         MVI   LOGIC,X'FF'             SET ERROR FLAG
         B     RET210                  RETURN
         SPACE 2
INITSRB  DS    0H
         ST    R1,SPADDR               SAVE STORAGE ADDRESS
         LR    R7,R1
         LR    R0,R1
         LR    R14,R1
         LA    R1,ESRB-SRBSECT
         SR    R15,R15
         MVCL  R0,R14                  CLEAR STORAGE
         MVC   SRBID,=C'SRB '          ACRONYM FOR SRB
         L     R1,ASCBSAVE
         ST    R1,SRBASCB              ADDRESS OF TARGET ASCB
         L     R1,PSAAOLD-PSA(0)
         ST    R1,SRBMYAS              ADDRESS OF MY ASCB
         MVC   SRBPASID,X'24'(R1)      IDENTIFIER OF ASCB
         MVC   SRBCPAFF,X'64'(R1)      CPU AFFINITY MASK
         L     R1,PSATOLD-PSA(0)
         ST    R1,SRBPTCB              IDENTIFIER OF TCB
         LA    R1,SRBCODE
         ST    R1,SRBEP                ENTRY POINT OF ROUTINE
         LA    R1,SRBECB
         ST    R1,SRBPARM              USER PARAMETER
         LA    R0,SAVESRB
         ST    R0,SRBSAVE              SAVE AREA POINTER
         OI    SRBPRIOR,SRBPNONQ       NON QUIESCEABLE SRB
         LA    R0,SRBCODE
         LA    R1,CODEE-CODEB          LENGTH OF SRB CODE
         LA    R14,CODEB               POINT TO SRB CODE
         LR    R15,R1
         MVCL  R0,R14                  MOVE SRB CODE
         SCHEDULE SRB=(R7)
         WAIT  1,ECB=SRBECB
         MVI   SRBECB,0
         SPACE 2
POOL229  DS    0H
         LA    R4,229                  SUBPOOL 229
         LA    R1,REC229-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R5,SPSIZE               CONVERT
         MVC   SP229+21(6),VIRT+1
         SPACE 2
POOL230  DS    0H
         LA    R4,230                  SUBPOOL 230
         LA    R1,REC230-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R5,SPSIZE               CONVERT
         MVC   SP229+37(6),VIRT+1
         SPACE 2
POOL236  DS    0H
         LA    R4,236                  SUBPOOL 236
         LA    R1,REC236-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R5,SPSIZE               CONVERT
         MVC   SP236+21(6),VIRT+1
         SPACE 2
POOL237  DS    0H
         LA    R4,237                  SUBPOOL 237
         LA    R1,REC237-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R5,SPSIZE               CONVERT
         MVC   SP236+37(6),VIRT+1
         SPACE 2
POOL255  DS    0H
         LA    R4,255                  SUBPOOL 255
         LA    R1,REC255-SMFREC(,R3)   POINT TO SMF-RECORD-FIELD
         BAL   R5,SPSIZE               CONVERT
         MVC   SP255+21(6),VIRT+1
         LA    R1,RECSTOT-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         L     R8,SPTOTAL
         BAL   R10,CONV
         MVC   SP255+37(6),VIRT+1
         SPACE 2
LSQASIZE DS    0H
         LA    R5,SRBPOOL              ADDRESS OF SUBPOOL TABLE
         L     R4,0(,R5)               GET FIRST SUBPOOL ELEMENT
         LR    R8,R4                   FOR CONVERT
         LA    R1,RECLSQA-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   LSQAADR(6),VIRT+1
         MVC   LSQAADR1+37(6),VIRT+1
         LA    R1,RECHLSQA-SMFREC(,R3) POINT TO SMF-RECORD-FIELD
         BAL   R10,HEXCONV
         MVC   LSQAADR+51(9),HEX
         MVC   LSQAADR+53(6),DW+1
         LR    R8,R4
         L     R4,VRSAVE
         SLR   R8,R4                   CALCULATE V=V REGION
         LA    R1,RECUREG-SMFREC(,R3)  POINT TO SMF-RECORD-FIELD
         BAL   R10,CONV
         MVC   UREG+37(6),VIRT+1
         CLI   SMFFLAG,X'00'           Q. SMF DATA COLLECTED
         BE    RET210                  A. NO
         SMFWTM (R3)                   WRITE SMF RECORD
         SPACE 2
RET210   DS    0H
         CLI   LOGIC,X'00'             Q. ERROR
         BNE   REST210                 A. YES
         LA    R0,ESRB-SRBSECT
         L     R1,SPADDR
         FREEMAIN RC,LV=(0),A=(1),SP=245
         SPACE 2
REST210  DS    0H
         LR    R14,R11
         BR    R14
         DROP  R2
         EJECT
         SPACE 2
DRUBER   DC    CL80' '
PRINT    DCB   DDNAME=PRINT,MACRF=(PM),RECFM=FB,BLKSIZE=800,LRECL=80,  *
               DSORG=PS
         SPACE 2
         DS    0H
MOVEPARM MVC   JOBNAME(*-*),4(R2)
         SPACE 2
STC      DC    C'STC'
VIRT     DC    CL7' '
HEX      DC    C'X''YYYYYY'''
DEFJOBNM DC    CL8'STARTING'
JOBNAME  DC    CL8' '
NOTACT   DC    CL80'TASK NOT ACTIVE'
NOSTOR   DC    CL80'STORAGE NOT AVAILABLE'
NOASID   DC    CL80'ASID NOT SPECIFIED'
NOJOB    DC    CL80'JOBNAME NOT SPECIFIED'
NOSVC    DC    CL80'WRONG IGC211 INSTALLED - CALL SE/OS'
HEAD1    DC    CL80'VIRTUAL STORAGE LAYOUT       HH.MM  DD.MM.JJ  JJJJJ*
               JJJ  SSSS'
         SPACE 2
PARMFLAG DC    X'00'
SMFFLAG  DC    X'00'
LOGIC    DC    X'00'
         SPACE 2
TRTAB    DC    256X'00'
         ORG   TRTAB+193
         DC    X'0A0B0C0D0E0F'
         ORG   TRTAB+240
         DC    X'00010203040506070809'
         ORG   TRTAB+256
         SPACE 2
HEXTABLE DC    C'0123456789ABCDEF'
         SPACE 2
         DS    0H
COMPASID DC    H'0'
         SPACE 2
         DS    0F
ASCBSAVE DC    A(0)
SPADDR   DC    A(0)
SPTOTAL  DC    F'0'
SIZESQA  DC    F'0'
FREESQA  DC    F'0'
USEDSQA  DC    F'0'
SIZECSA  DC    F'0'
USEDCSA  DC    F'0'
TIME     DC    F'0'
VRSAVE   DC    F'0'
REGSAVE  DC    F'0'
FLPA     DC    F'0'
FW       DC    F'0'
         SPACE 2
         DS    0D
DW       DC    D'0'
         EJECT
         SPACE 2
TABANF   EQU   *
HEAD     DC    CL80'                                                '
         DC    CL80'                                                '
         DC    CL80'                                                '
CVTA4    DC    CL80'     0 K --> ×---------------------------------×'
SQASIZE  DC    CL80'             ×  SQA     U:   0%   A:      0 K  ×'
SQAADR   DC    CL80'     0 K --> ×---------------------------------×'
PLPASIZE DC    CL80'             ×  PLPA                      0 K  ×'
CVT169   DC    CL80'     0 K --> ×---------------------------------×'
         DC    CL80'             ×  MODIFIED LPA                   ×'
         DC    CL80'             ×---------------------------------×'
         DC    CL80'             ×  PAGEABLE BLDL TABLE            ×'
PBLDLADR DC    CL80'             ×---------------------------------×'
         DC    CL80'             ×  PSA                            ×'
CSATOP   DC    CL80'     0 K --> ×---------------------------------×'
CSASIZE  DC    CL80'             ×  CSA     U:   0%   A:      0 K  ×'
CVT1A0   DC    CL80'     0 K --> ×=================================×'
         DC    CL80'             ×  L S Q A  /  S W A    SUBPOOLS  ×'
         DC    CL80'             ×                                 ×'
SP229    DC    CL80'             ×  229       0 K   230       0 K  ×'
SP236    DC    CL80'             ×  236       0 K   237       0 K  ×'
SP255    DC    CL80'             ×  255       0 K   TOT       0 K  ×'
LSQAADR  DC    CL80'     0 K --> ×---------------------------------×'
LSQAADR1 DC    CL80'             ×  V=V REGION                0 K  ×'
VR1      DC    CL80'             ×                    -       0 K  ×'
         DC    CL80'             ×                                 ×'
UREG     DC    CL80'             ×                    =       0 K  ×'
CVT11C   DC    CL80'     0 K --> ×---------------------------------×'
VRSIZE   DC    CL80'             ×  V=R REGION                0 K  ×'
VR       DC    CL80'     0 K --> ×---------------------------------×'
SRSIZE   DC    CL80'             ×  SYSTEM REGION             0 K  ×'
CVT80    DC    CL80'     0 K --> ×=================================×'
         DC    CL80'             ×  RMS NUC. EXTENSION             ×'
         DC    CL80'             ×---------------------------------×'
         DC    CL80'             ×  FIXED BLDL                     ×'
BLDLADR  DC    CL80'             ×---------------------------------×'
         DC    CL80'             ×  FIXED LPA                      ×'
FLPAADR  DC    CL80'     0 K --> ×---------------------------------×'
         DC    CL80'             ×  NUCLEUS CONTROL BLOCKS         ×'
NUCADR   DC    CL80'     0 K --> ×---------------------------------×'
         DC    CL80'             ×  NUCLEUS LOAD MODULE            ×'
         DC    CL80'     0 K --> ×---------------------------------×'
         DC    CL80'                                                '
TABEND   EQU   *
         SPACE 2
         LTORG
         TITLE 'S R B  ROUTINE'
         DROP  R3
         DROP  R7
         DROP  R11
         DS    0D
         SPACE 2
CODEB    EQU   *
         SPACE 2
         USING *,R15
         LR    R1,R0
         STM   R0,R15,SAVESRB-SRBSECT(R1)
         USING SRBSECT,R3
         DROP  R15
         USING CODEB,R12
         LR    R12,R15
         LR    R3,R1
         SPACE 2
CBSCAN   DS    0H
         SR    R4,R4               ZERO REG4
         SR    R5,R5               ZERO REG5
         SR    R10,R10             ZERO REG10
         L     R2,SRBASCB          ADDRESS OF TARGET ASCB
         L     R2,108(,R2)         ADDRESS OF ASXB
         L     R2,4(,R2)           ADDRESS OF FIRST TCB ON TCB QUEUE
         LA    R6,SRBPOOL          POINT TO SUBPOOL TABLE
         L     R7,16               POINT TO CVT
         L     R7,164(,R7)         CVTMZ00
         ST    R7,0(R6)            INITIALIZE FIRST ELEMENT
         SPACE 2
TCBSCANA DS    0H
         ICM   R4,B'1111',248(R2)  ADDRESS OF FIRST SWA SPQE
         BNZ   SWASPQE             CHECK SWA SPQE'S
         SPACE 2
TCBSCANB DS    0H
         ICM   R4,B'1111',268(R2)  ADDRESS OF SP 229/230 SPQE'S
         BNZ   UKEYSPQE            CHECK 229/230 SPQE'S
         SPACE 2
TCBNEXT  DS    0H
         L     R2,116(,R2)         ADDRESS OF NEXT TCB ON TCB QUEUE
         LTR   R2,R2               Q. IS THERE ONE
         BNZ   TCBSCANA            A. YES
         L     R2,SRBASCB          ADDRESS OF TARGET ASCB
         L     R2,48(,R2)          ADDRESS OF ASCBLDA
         SPACE 2
LDASCANA DS    0H
         ICM   R4,B'1111',1464(R2)      LSQA SPQE
         BNZ   LDASPQE                  CHECK LSQA SPQE
         B     POSTRTN                  RETURN
         SPACE 2
LDASPQE  DS    0H
         TM    8(R4),X'80'         Q. SPQE OWNED
         BO    POSTRTN             A. NO
         BAL   R11,DQESCAN
         ICM   R5,B'1111',0(R4)    Q. IS THERE ANOTHER SPQE
         BZ    POSTRTN             A. NO
         LR    R4,R5               ADDRESS OF SPQE IN REG4
         B     LDASPQE
         SPACE 2
SWASPQE  DS    0H
         TM    8(R4),X'80'         Q. SPQE OWNED
         BO    TCBSCANB            A. NO
         BAL   R11,DQESCAN
         ICM   R5,B'1111',0(R4)    Q. IS THERE ANOTHER SPQE
         BZ    TCBSCANB            A. NO
         LR    R4,R5               ADDRESS OF SPQE IN REG4
         B     SWASPQE             TRY AGAIN
         SPACE 2
UKEYSPQE DS    0H
         TM    8(R4),X'80'         Q. SPQE OWNED
         BO    TCBNEXT             A. NO
         BAL   R11,DQESCAN
         ICM   R5,B'1111',0(R4)    Q. IS THERE ANOTHER SPQE
         BZ    TCBNEXT             A. NO
         LR    R4,R5               ADDRESS OF SPQE IN REG4
         B     UKEYSPQE
         SPACE 2
DQESCAN  DS    0H
         ICM   R5,B'1111',4(R4)    ADDRESS OF FIRST DQE
         BZR   R11                 ZERO - CHECK NEXT SPQE
         SR    R6,R6               ZERO REG6
         IC    R6,10(,R4)          GET SUBPOOL IDENTIFIER
         SLL   R6,2                MULTIPLY BY FOUR
         LA    R7,SRBPOOL          ADDRESS OF SUBPOOL TABLE
         LA    R7,0(R6,R7)         POINT TO SUBPOOL ELEMENT
         SPACE 2
DQESP    DS    0H
         L     R8,0(,R7)           GET SUBPOOL ELEMENT
         L     R9,12(,R5)          GET LENGTH OF THIS QUEUE ELEMENT
         ALR   R8,R9               ADD
         ST    R8,0(R7)            STORE BACK
         LA    R6,SRBPOOL          POINT TO SUBPOOL TABLE
         L     R8,0(,R6)           GET FIRST ELEMENT
         L     R9,8(,R5)           GET ADDRESS OF FIRST 2K BLOCK
         CLR   R8,R9               Q. ADDRESS LOWER
         BL    DQENEXT             A. NO
         ST    R9,0(R6)            A. YES - STORE NEW ADDRESS
         SPACE 2
DQENEXT  DS    0H
         ICM   R10,B'1111',4(R5)   Q. IS THERE ANOTHER DQE
         BZR   R11                 A. NO
         LR    R5,R10              ADDRESS OF DQE IN REG5
         B     DQESP               TRY AGAIN
         SPACE 2
POSTRTN  DS    0H
         SR    R10,R10             ZERO REG10
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,RELATED=(SCAN)
         LA    R11,SRBECB          ADDRESS OF ECB
         LA    R0,128
         SLL   R0,24
         OR    R11,R0              INDICATE LAST ECB
         LR    R8,R3               SAVE REG3
         L     R13,SRBMYAS         ADDRESS OF ORIGIN ASCB
         L     R15,16              POINT TO CVT
         L     R15,152(,R15)       ADDRESS OF POST ROUTINE
         BALR  R14,R15             POST ECB
         LR    R3,R8               RESTORE REG3
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,RELATED=(SCAN)
         LM    R0,R15,SAVESRB
         BR    R14
         DROP  R3
         SPACE 2
CODEE    EQU   *
         SPACE 2
         PRINT NOGEN
         CVT   DSECT=YES
         IHAPSA
         IHASRB
         SPACE 2
SAVESRB  DS    16F
SRBECB   DC    F'0'
SRBMYAS  DS    F
SRBSIZ   EQU   *-SRBSECT
         SPACE 2
         DS    0D
SRBCODE  DS    CL(CODEE-CODEB)
         DS    0F
SRBPOOL  DS    256F
ESRB     EQU   *
         TITLE 'S M F  RECORD'
         SPACE 2
         PRINT GEN
         SMFREC SUB=6
         EJECT
         SPACE 2
         PRINT NOGEN
         IEESMCA
         END
