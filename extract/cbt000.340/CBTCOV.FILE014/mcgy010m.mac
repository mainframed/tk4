./       ADD   NAME=CDMDSECT
         MACRO
      CDMDSECT &LIST,&PRTGEN=,&EXPAND=YES
         GBLB  &CDMDSU(128),&CDMDSP(128),&CDMDSI(128)
         GBLC  &CDMSECT
         LCLA  &I,&J,&K,&N,&PHASE
         LCLB  &ANYEXP
         LCLC  &ITEM
.**********************************************************************
.*          PHASE 1 - PROCESS ITEM LIST IN THIS MACRO CALL            *
.**********************************************************************
&PHASE   SETA  1
&N       SETA  N'&LIST                    NO OF ITEMS IN LIST
&I       SETA  1
.NEXT1   AIF   (&I GT &N).END1
&ITEM    SETC  '&LIST(&I)'                CURRENT ITEM
         AGO   .TAB                       FIND IN TABLE, RETURN K
.SET1    ANOP
&CDMDSU(&K) SETB 1                        INDICATE DSECT(K) USED
.CONT1   ANOP
&I       SETA  &I+1
         AGO   .NEXT1
.END1    ANOP
.**********************************************************************
.*      PHASE 2 - PROCESS PRTGEN LIST FOR DSECTS TO BE PRINTED        *
.**********************************************************************
&PHASE   SETA  2
&N       SETA  N'&PRTGEN                  NO OF ITEMS IN LIST
&I       SETA  1
.NEXT2   AIF   (&I GT &N).END2
&ITEM    SETC  '&PRTGEN(&I)'              CURRENT ITEM
         AGO   .TAB                       FIND IN TABLE, RETURN K
.SET2    ANOP
&CDMDSU(&K) SETB 1                        INDICATE DSECT(K) USED
&CDMDSP(&K) SETB 1                        INDICATE DSECT(K) PRINTED
.CONT2   ANOP
&I       SETA  &I+1
         AGO   .NEXT2
.END2    ANOP
.**********************************************************************
.*      PHASE 3 - FIND NO OF DSECTS AND IF THERE ARE ANY TO BE        *
.*                ASSEMBLED (USING CDMDSU AND CDMDSI BIT MAPS)        *
.**********************************************************************
&PHASE   SETA  3
&ITEM    SETC  '    '                     SET DUMMY ITEM
         AIF   ('&EXPAND' EQ 'YES').TAB   FIND NO OF DSECTS, RETURN K
.EXIT3   MEXIT                            EXIT IF EXPAND=NO
.SET3    ANOP
&N       SETA  &K                         NO OF DSECTS SUPPORTED
&J       SETA  1
.NEXT3   AIF   (&J GT &N).END3
         AIF   (NOT &CDMDSU(&J) OR &CDMDSI(&J)).CONT3
&ANYEXP  SETB  1
.CONT3   ANOP
&J       SETA  &J+1
         AGO   .NEXT3
.END3    AIF   (NOT &ANYEXP).EXIT3
.**********************************************************************
.*      PHASE 4 - EXPAND DSECTS INDICATED IN CDMDSU BUT NOT IN        *
.*                CDMDSI (PREVIOUSLY ASSEMBLED)                       *
.**********************************************************************
&PHASE   SETA  4
         PUSH  PRINT
         PRINT NOGEN
&J       SETA  1
.NEXT4   AIF   (&J GT &N).END4
         AIF   (NOT &CDMDSU(&J) OR &CDMDSI(&J)).CONT4
         AIF   (NOT &CDMDSP(&J)).TAB
         PRINT GEN
         AGO   .TAB
.SET4    ANOP
&CDMDSI(&J) SETB 1                      INDICATE DSECT(J) INCLUDED
         AIF   (NOT &CDMDSP(&J)).CONT4
         PRINT NOGEN
.CONT4   ANOP
&J       SETA  &J+1
         AGO   .NEXT4
.END4    POP   PRINT
         AIF   ('&CDMSECT' EQ '').NRESUME
&CDMSECT CSECT ,                        RESUME CALLERS CSECT
&CDMSECT SETC  ''
.NRESUME MEXIT
.TAB     ANOP
&K       SETA  0
.**********************************************************************
.*                   ORIGIN OF DSECT BRANCH TABLE                     *
.**********************************************************************
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ '$JCT' ).$JCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ '$SJB' ).$SJB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ACB'  ).ACB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ASCB' ).ASCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ASVT' ).ASVT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ASXB' ).ASXB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CCT'  ).CCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CCVT' ).CCVT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CDE'  ).CDE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CIB'  ).CIB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CEPA' ).CEPA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CLST' ).CLST
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CPPL' ).CPPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CSCB' ).CSCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CSOA' ).CSOA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CSPL' ).CSPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'CVT'  ).CVT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DAPL' ).DAPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DCB'  ).DCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DEB'  ).DEB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DECB' ).DECB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DFP'  ).DFP
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DFPB' ).DFPB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DFPL' ).DFPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DPL'  ).DPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DQE'  ).DQE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DSAB' ).DSAB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DSCB1').DSCB1
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DSCB2').DSCB2
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DSCB3').DSCB3
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DSCB4').DSCB4
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'DVCT' ).DVCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ECA'  ).ECA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ECT'  ).ECT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'EUHD' ).EUHD
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'EUPL' ).EUPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'FQE'  ).FQE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'GDA'  ).GDA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'GFP'  ).GFP
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'GTPB' ).GTPB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'IOB'  ).IOB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'IOCM' ).IOCM
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'IOPL' ).IOPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'IOSB' ).IOSB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'JCT'  ).JCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'JESCT').JESCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'JFCB' ).JFCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'JMR'  ).JMR
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'JSCB' ).JSCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LCB'  ).LCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LCCA' ).LCCA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LCT'  ).LCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LDE'  ).LDE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LLE'  ).LLE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LPL'  ).LPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'LSD'  ).LSD
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'NIB'  ).NIB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'OUCB' ).OUCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PDS2' ).PDS2
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PGPB' ).PGPB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PPL'  ).PPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PSA'  ).PSA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PSCB' ).PSCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PTPB' ).PTPB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'PVT'  ).PVT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'QCB'  ).QCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'QDB'  ).QDB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'QEL'  ).QEL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RB'   ).RB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RDCM' ).RDCM
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RGSZ' ).RGSZ
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RMCT' ).RMCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RPL'  ).RPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'RRPA' ).RRPA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SCT'  ).SCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SCVT' ).SCVT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SDWA' ).SDWA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SIOT' ).SIOT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SMCA' ).SMCA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SPQE' ).SPQE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SRB'  ).SRB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SSIB' ).SSIB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'STPB' ).STPB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'STPL' ).STPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'SUBITS').SUBITS
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'S99'  ).S99
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TAIE' ).TAIE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TAXE' ).TAXE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TBLS' ).TBLS
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TCAST').TCAST
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TCB'  ).TCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TCT'  ).TCT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TDCM' ).TDCM
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TIOCB').TIOCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TIOCP').TIOCP
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TIOT' ).TIOT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TPL'  ).TPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TSB'  ).TSB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TSBX' ).TSBX
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'TVWA' ).TVWA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'UCB'  ).UCB
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'UCM'  ).UCM
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'UPT'  ).UPT
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'USFBC').USFBC
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'VCPL' ).VCPL
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'VCWA' ).VCWA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'WQE'  ).WQE
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'XADR' ).XADR
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'XSA'  ).XSA
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'XTLST').XTLST
&K       SETA  &K+1
         AIF   (&J EQ &K OR '&ITEM' EQ 'ZTBL' ).ZTBL
.**********************************************************************
.*                    END OF DSECT BRANCH TABLE                       *
.**********************************************************************
         AIF   (&PHASE EQ 3).SET3       RETURN TO PHASE 3
.*
         MNOTE 8,'CDM001I DSECT &ITEM NOT FOUND (LAST=&K)'
.*
         AIF   (&PHASE EQ 1).CONT1      RETURN TO PHASE 1
         AIF   (&PHASE EQ 2).CONT2      RETURN TO PHASE 2
.RET     AIF   (&PHASE EQ 1).SET1       RETURN TO PHASE 1
         AIF   (&PHASE EQ 2).SET2       RETURN TO PHASE 2
         AIF   (&PHASE EQ 4).SET4       RETURN TO PHASE 4
         MEXIT
.**********************************************************************
.*                      DSECT PROCESS SECTION                         *
.**********************************************************************
.$JCT    AIF   (&J EQ 0).RET
      CDM$JCT
         AGO   .RET
.**********************************************************************
.$SJB    AIF   (&J EQ 0).RET
      CDM$SJB
         AGO   .RET
.**********************************************************************
.ACB     AIF   (&J EQ 0).RET
ACB      DSECT
      IFGACB   DSECT=NO,AM=VTAM
         AGO   .RET
.**********************************************************************
.ASCB    AIF   (&J EQ 0).RET
      IHAASCB
         AGO   .RET
.**********************************************************************
.ASVT    AIF   (&J EQ 0).RET
      IHAASVT
         AGO   .RET
.**********************************************************************
.ASXB    AIF   (&J EQ 0).RET
      IHAASXB
         AGO   .RET
.**********************************************************************
.CCT     AIF   (&J EQ 0).RET
*     IRACCT
CCT      DSECT
         DS    0CL152
CCVUTILP EQU   CCT+102,2
         AGO   .RET
.**********************************************************************
.CCVT    AIF   (&J EQ 0).RET
      CDMCCVT  DSECT=YES
         AGO   .RET
.**********************************************************************
.CDE     AIF   (&J EQ 0).RET
      IHACDE
CDE      EQU   CDENTRY
         AGO   .RET
.**********************************************************************
.CIB     AIF   (&J EQ 0).RET
CIB      DSECT
      IEZCIB
         AGO   .RET
.**********************************************************************
.CEPA    AIF   (&J EQ 0).RET
      CDMCEPA
         AGO   .RET
.**********************************************************************
.CLST    AIF   (&J EQ 0).RET
      CDMCLST
         AGO   .RET
.**********************************************************************
.CPPL    AIF   (&J EQ 0).RET
      IKJCPPL
         AGO   .RET
.**********************************************************************
.CSCB    AIF   (&J EQ 0).RET
CSCB     DSECT
      IEECHAIN
         AGO   .RET
.**********************************************************************
.CSOA    AIF   (&J EQ 0).RET
      IKJCSOA
         AGO   .RET
.**********************************************************************
.CSPL    AIF   (&J EQ 0).RET
      IKJCSPL
         AGO   .RET
.**********************************************************************
.CVT     AIF   (&J EQ 0).RET
         CVT   DSECT=YES
         AGO   .RET
.**********************************************************************
.DAPL    AIF   (&J EQ 0).RET
      IKJDAPL
         AGO   .RET
.**********************************************************************
.DCB     AIF   (&J EQ 0).RET
         DCBD  DSORG=(PS,PO),DEVD=(DA,TA)
DCB      EQU   IHADCB
         AGO   .RET
.**********************************************************************
.DEB     AIF   (&J EQ 0).RET
      IEZDEB
         AGO   .RET
.**********************************************************************
.DECB    AIF   (&J EQ 0).RET
DECB     DSECT
DECSDECB DS    A                   ECB
DECTYPE  DS    0H                  TYPE OF I/O REQUEST
DECTYPE1 DS    X                   TYPE OF LENGTH OPERAND
DECTYPE2 DS    X                   TYPE OF OPERATION
DECLNGTH DS    H                   LENGTH OF KEY AND DATA
DECDCBAD DS    A                   ADDRESS OF DCB
DECAREA  DS    A                   ADDRESS OF KEY AND DATA
DECIOBPT DS    A                   ADDRESS OF IOB
         AGO   .RET
.**********************************************************************
.DFP     AIF   (&J EQ 0).RET
DFP      DSECT
      IKJEFFDF DFDSECT=NO
         AGO   .RET
.**********************************************************************
.DFPB    AIF   (&J EQ 0).RET
      IKJDFPB
         AGO   .RET
.**********************************************************************
.DFPL    AIF   (&J EQ 0).RET
      IKJDFPL
         AGO   .RET
.**********************************************************************
.DPL     AIF   (&J EQ 0).RET
      CGTDDPL
         AGO   .RET
.**********************************************************************
.DQE     AIF   (&J EQ 0).RET
DQE      DSECT
DQFQEPTR DS    F
DQEPTR   DS    F
DQEHRID  DS    0X
DQEBLKAD DS    F
DQELNTH  DS    F
         AGO   .RET
.**********************************************************************
.DSAB    AIF   (&J EQ 0).RET
      IHADSAB
         AGO   .RET
.**********************************************************************
.DSCB1   AIF   (&J EQ 0).RET
DSCB1    DSECT
      IECSDSL1 (1)
         AGO   .RET
.**********************************************************************
.DSCB2   AIF   (&J EQ 0).RET
DSCB2    DSECT
      IECSDSL1 (2)
         AGO   .RET
.**********************************************************************
.DSCB3   AIF   (&J EQ 0).RET
DSCB3    DSECT
      IECSDSL1 (3)
         AGO   .RET
.**********************************************************************
.DSCB4   AIF   (&J EQ 0).RET
DSCB4    DSECT
      IECSDSL1 (4)
         AGO   .RET
.**********************************************************************
.DVCT    AIF   (&J EQ 0).RET
      IHADVCT
         AGO   .RET
.**********************************************************************
.ECA     AIF   (&J EQ 0).RET
      IKJEBECA
ECA      EQU   IKJEBECA
         AGO   .RET
.**********************************************************************
.ECT     AIF   (&J EQ 0).RET
      IKJECT
         AGO   .RET
.**********************************************************************
.EUHD    AIF   (&J EQ 0).RET
      CGTDEUH
         AGO   .RET
.**********************************************************************
.EUPL    AIF   (&J EQ 0).RET
      CGTDEUP
         AGO   .RET
.**********************************************************************
.FQE     AIF   (&J EQ 0).RET
FQE      DSECT
FQEPTR   DS    0F
FQTYPE   DS    X
         DS    XL3
FQELNTH  DS    F
FQAREA   DS    F
FQERSVD  DS    F
         AGO   .RET
.**********************************************************************
.GDA     AIF   (&J EQ 0).RET
      IHAGDA
         AGO   .RET
.**********************************************************************
.GFP     AIF   (&J EQ 0).RET
GFP      DSECT
      IKJEFFGF GFDSECT=NO
         AGO   .RET
.**********************************************************************
.GTPB    AIF   (&J EQ 0).RET
      IKJGTPB
         AGO   .RET
.**********************************************************************
.IOB     AIF   (&J EQ 0).RET
      IEZIOB
         AGO   .RET
.**********************************************************************
.IOCM    AIF   (&J EQ 0).RET
      IECDIOCM
IOCM     EQU   IOCOM
         AGO   .RET
.**********************************************************************
.IOPL    AIF   (&J EQ 0).RET
      IKJIOPL
         AGO   .RET
.**********************************************************************
.IOSB    AIF   (&J EQ 0).RET
      IECDIOSB
         AGO   .RET
.**********************************************************************
.JCT     AIF   (&J EQ 0).RET
JCT      DSECT
      IEFAJCTB
JCTEQREG EQU   JCT+92,2
         AGO   .RET
.**********************************************************************
.JESCT   AIF   (&J EQ 0).RET
      IEFJESCT
         AGO   .RET
.**********************************************************************
.JFCB    AIF   (&J EQ 0).RET
JFCB     DSECT
      IEFJFCBN
         AGO   .RET
.**********************************************************************
.JMR     AIF   (&J EQ 0).RET
      IEFJMR
         AGO   .RET
.**********************************************************************
.JSCB    AIF   (&J EQ 0).RET
      IEZJSCB
JSCB     EQU   IEZJSCB
         AGO   .RET
.**********************************************************************
.LCB     AIF   (&J EQ 0).RET
      CGTDLCB
         AGO   .RET
.**********************************************************************
.LCCA    AIF   (&J EQ 0).RET
      IHALCCA
         AGO   .RET
.**********************************************************************
.LCT     AIF   (&J EQ 0).RET
LCT      DSECT
      IEFALLCT
         AGO   .RET
.**********************************************************************
.LDE     AIF   (&J EQ 0).RET
      CGTDLDE
         AGO   .RET
.**********************************************************************
.LLE     AIF   (&J EQ 0).RET
      IHALLE
         AGO   .RET
.**********************************************************************
.LPL     AIF   (&J EQ 0).RET
      CGTDLPL
         AGO   .RET
.**********************************************************************
.LSD     AIF   (&J EQ 0).RET
      IKJLSD
         AGO   .RET
.**********************************************************************
.NIB     AIF   (&J EQ 0).RET
      ISTDNIB
NIB      EQU   ISTDNIB
         AGO   .RET
.**********************************************************************
.OUCB    AIF   (&J EQ 0).RET
      IRAOUCB
         AGO   .RET
.**********************************************************************
.PDS2    AIF   (&J EQ 0).RET
      IHAPDS   PDSBLDL=NO
         AGO   .RET
.**********************************************************************
.PGPB    AIF   (&J EQ 0).RET
      IKJPGPB
         AGO   .RET
.**********************************************************************
.PPL     AIF   (&J EQ 0).RET
      IKJPPL
         AGO   .RET
.**********************************************************************
.PSA     AIF   (&J EQ 0).RET
      IHAPSA
         AGO   .RET
.**********************************************************************
.PSCB    AIF   (&J EQ 0).RET
      IKJPSCB
         AGO   .RET
.**********************************************************************
.PTPB    AIF   (&J EQ 0).RET
      IKJPTPB
         AGO   .RET
.**********************************************************************
.PVT     AIF   (&J EQ 0).RET
      IHAPVT
         AGO   .RET
.**********************************************************************
.QCB     AIF   (&J EQ 0).RET
      IHAQCB
         AGO   .RET
.**********************************************************************
.QDB     AIF   (&J EQ 0).RET
      IHAQDB
         AGO   .RET
.**********************************************************************
.QEL     AIF   (&J EQ 0).RET
      IHAQEL
         AGO   .RET
.**********************************************************************
.RB      AIF   (&J EQ 0).RET
      IHARB
RB       EQU   RBBASIC
         AGO   .RET
.**********************************************************************
.RDCM    AIF   (&J EQ 0).RET
RDCM     DSECT
      IEECRDCM
         AGO   .RET
.**********************************************************************
.RGSZ    AIF   (&J EQ 0).RET
      CDMRGSZ
         AGO   .RET
.**********************************************************************
.RMCT    AIF   (&J EQ 0).RET
*     IRARMCT
RMCT     DSECT
         DS    0CL224
RMCTCCT  EQU   RMCT+4,4
         AGO   .RET
.**********************************************************************
.RPL     AIF   (&J EQ 0).RET
      IFGRPL   AM=VTAM
RPL      EQU   IFGRPL
         AGO   .RET
.**********************************************************************
.RRPA    AIF   (&J EQ 0).RET
      IRARRPA
         AGO   .RET
.**********************************************************************
.SCT     AIF   (&J EQ 0).RET
SCT      DSECT
      IEFASCTB
         AGO   .RET
.**********************************************************************
.SCVT    AIF   (&J EQ 0).RET
      IHASCVT
         AGO   .RET
.**********************************************************************
.SDWA    AIF   (&J EQ 0).RET
      IHASDWA
         AGO   .RET
.**********************************************************************
.SIOT    AIF   (&J EQ 0).RET
SIOT     DSECT
      IEFASIOT
         AGO   .RET
.**********************************************************************
.SMCA    AIF   (&J EQ 0).RET
      IEESMCA
SMCA     EQU   SMCABASE
         AGO   .RET
.**********************************************************************
.SPQE    AIF   (&J EQ 0).RET
      IHASPQE
SPQE     EQU   SPQESECT
         AGO   .RET
.**********************************************************************
.SRB     AIF   (&J EQ 0).RET
      IHASRB
         AGO   .RET
.**********************************************************************
.SSIB    AIF   (&J EQ 0).RET
      IEFJSSIB
         AGO   .RET
.**********************************************************************
.STPB    AIF   (&J EQ 0).RET
      IKJSTPB
         AGO   .RET
.**********************************************************************
.STPL    AIF   (&J EQ 0).RET
      IKJSTPL
         AGO   .RET
.**********************************************************************
.SUBITS  AIF   (&J EQ 0).RET
      IHASUBIT
         AGO   .RET
.**********************************************************************
.S99     AIF   (&J EQ 0).RET
      IEFZB4D0
      IEFZB4D2
         AGO   .RET
.**********************************************************************
.TAIE    AIF   (&J EQ 0).RET
      IKJTAIE
         AGO   .RET
.**********************************************************************
.TAXE    AIF   (&J EQ 0).RET
      IKJTAXE
         AGO   .RET
.**********************************************************************
.TBLS    AIF   (&J EQ 0).RET
      CDMXTBL
         AGO   .RET
.**********************************************************************
.TCAST   AIF   (&J EQ 0).RET
      IKTTCAST
         AGO   .RET
.**********************************************************************
.TCB     AIF   (&J EQ 0).RET
      IKJTCB
         AGO   .RET
.**********************************************************************
.TCT     AIF   (&J EQ 0).RET
      IEFTCT
TCT      EQU   SMFTCT
         AGO   .RET
.**********************************************************************
.TDCM    AIF   (&J EQ 0).RET
TDCM     DSECT
      IEECDCM
         AGO   .RET
.**********************************************************************
.TIOCB   AIF   (&J EQ 0).RET
      IKJTIOCB
TIOCB    EQU   TIOCBUF
         AGO   .RET
.**********************************************************************
.TIOCP   AIF   (&J EQ 0).RET
      IKJTIOCP
TIOCP    EQU   TIOCRPT
         AGO   .RET
.**********************************************************************
.TLCB    AIF   (&J EQ 0).RET
      CDMTLCB
         AGO   .RET
.**********************************************************************
.TIOT    AIF   (&J EQ 0).RET
TIOT     DSECT
      IEFTIOT1
         AGO   .RET
.**********************************************************************
.TPL     AIF   (&J EQ 0).RET
      IKJTPL
         AGO   .RET
.**********************************************************************
.TSB     AIF   (&J EQ 0).RET
      IKJTSB   EXT=NO,LIST=YES
         AGO   .RET
.**********************************************************************
.TSBX    AIF   (&J EQ 0).RET
      IKTTSBX
         AGO   .RET
.**********************************************************************
.TVWA    AIF   (&J EQ 0).RET
      IKTTVWA
         AGO   .RET
.**********************************************************************
.UCB     AIF   (&J EQ 0).RET
UCB      DSECT
      IEFUCBOB
         AGO   .RET
.**********************************************************************
.UCM     AIF   (&J EQ 0).RET
UCM      DSECT
      IEECUCM
         AGO   .RET
.**********************************************************************
.UPT     AIF   (&J EQ 0).RET
      IKJUPT
         AGO   .RET
.**********************************************************************
.USFBC   AIF   (&J EQ 0).RET
      ISTUSFBC
         AGO   .RET
.**********************************************************************
.VCPL    AIF   (&J EQ 0).RET
      CGTDVCP
         AGO   .RET
.**********************************************************************
.VCWA    AIF   (&J EQ 0).RET
      CGTDVCW
         AGO   .RET
.**********************************************************************
.WQE     AIF   (&J EQ 0).RET
      IEEWQE
         AGO   .RET
.**********************************************************************
.XADR    AIF   (&J EQ 0).RET
      CDMXADR
         AGO   .RET
.**********************************************************************
.XSA     AIF   (&J EQ 0).RET
      CDMDSECT (RB),EXPAND=&EXPAND,PRTGEN=&PRTGEN
RBPREFIX DSECT
      IKJXSA
         AGO   .RET
.**********************************************************************
.XTLST   AIF   (&J EQ 0).RET
      IHAXTLST
         AGO   .RET
.**********************************************************************
.ZTBL    AIF   (&J EQ 0).RET
      CDMZTBL
         AGO   .RET
.**********************************************************************
         MEND
