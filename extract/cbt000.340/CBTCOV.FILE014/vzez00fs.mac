//Z00302  JOB  (208000,00,5,000),KREBS,CLASS=S,MSGCLASS=X,
//        NOTIFY=Z00302,REGION=4096K
// EXEC SERVASM,PARM.L='XREF,LIST,LET'
         TITLE 'ZDUMP00 - DUMP-EXIT , ZEDVCVT FORMATTER'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT ZEDVCVT AND ASSOCIATED DATA                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZDUMP0   BVINIT
         USING ABDPL,RABDPL
         USING ZEDVCVT,RZEDV
         LR    RABDPL,R1              PARAMETER-LIST.
         L     R2,X'220'             ASCB
         L     R2,ASCBASXB-ASCB(,R2) ASXB
         L     R2,ASXBSENV-ASXB(,R2) ACEE
         TM    ACEEFLG1-ACEE(R2),ACEEOPER+ACEESPEC
         BNO   RETURN
         TITLE 'FORMAT ZEDVCVT'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT ZEDVCVT                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         OI    ADPLFLAG,ADPLEJEC      FORCE EJECT.
         L     R15,ADPLBUF
         MVC   0(L'MSGUSER,R15),MSGUSER PRINT USER-HEADER.
         BAL   RL,PRINT
         SPACE
         BAL   RL1,GETZCVT            ACCESS ZEDVCVT.
         ST    RZEDV,ADPLPBAV         ADDRESS TO FORMAT.
         MVC   ADPLPPTR,=V(MZEDVCVT)  ADDR. OF MODEL.
         BAL   RL,FORMAT              FORMAT ZEDVCVT.
         TITLE 'CHECK ZEDVCVT FOR ABNORMAL SITUATIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        CHECK ZEDVCVT FOR ERROR STATUS                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ERRCHECK EQU   *
         BAL   RL,PRINT                 PRINT A BLANK-LINE.
         TM    ZCRFLAG1,ZCR1QIP         PROFILE MANIPULATION IN PROGR ?
         BNO   NO2QIP                   NO.
         MVC   0(L'MSG2QIP,R15),MSG2QIP
         BAL   RL,PRINT
NO2QIP   EQU   *
         TM    ZCRFLAG1,ZCR1CIP         CHECK IN PROGRESS ?
         BNO   NO2CIP                   NO.
         MVC   0(L'MSG2CIP,R15),MSG2CIP
         BAL   RL,PRINT
NO2CIP   EQU   *
         TM    ZCRCFLAG,ZCRCCPOK        CELL-POOL BUILT ?
         BNO   NOCCPOK                  NO
         MVC   0(L'MSGCCPOK,R15),MSGCCPOK
         BAL   RL,PRINT
NOCCPOK  EQU   *
         TM    ZCRCFLAG,ZCRCCBIP        CELL-POOL BUILD IN PROGRESS ?
         BNO   NOCCBIP                  NO
         MVC   0(L'MSGCCBIP,R15),MSGCCBIP
         BAL   RL,PRINT
         TITLE 'FORMAT MODREP''S LOADED-MODULE NAME-TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT MODREP'S LOADED-MODULE NAME-TABLE                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
NOCCBIP  EQU   *
         BAL   RL,PRINT               BLANK LINE.
         ICM   R14,15,ZFNAMTA         A(NAME-TABLE).
         BZ    FASTRAC                NONE , SKIP FORMAT.
         LA    R15,ZFNAMTN*ZFNAMTL+1  LENGTH OF STORAGE.
         BAL   RL,STORACC             ACCESS STORAGE.
         SPACE
         MVC   ADPLPBAV,ADPLPART      ADDRESS TO FORMAT.
         MVC   ADPLPPTR,=V(MZFNAMT)   ADDR. OF MODEL.
         BAL   RL,FORMAT
         TITLE 'FORMAT MODREP CDE''S AND XTLST''S'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT CDE'S AND XTLST'S                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         USING ZFNAMT,R2
         L     R2,ZFNAMTA             A(NAME-TABLE).
         LA    R15,ZFNAMTN*ZFNAMTL+1  LENGTH OF STORAGE.
         BAL   RL,STORACC             ACCESS STORAGE.
NAMTLOOP EQU   *
         CLI   TNAME,0                ENTRY USED ?
         BE    NEXTNAMT               NO.
         L     R14,TCDEA              GET CDE ADDRESS.
         LA    R15,CDEL+XTLSTL        LENGTH OF STORAGE.
         OI    FORMFLAG,128           RETURN ON ERROR.
         BAL   RL,STORACC             ACCESS STORAGE.
         LTR   R15,R15
         BZ    NAMTOK
         L     R15,ADPLBUF              RESTORE BUFFER-ADDRESS.
         MVC   0(L'MSGNOCDE,R15),MSGNOCDE
         BAL   RL,PRINT
         SPACE
NAMTOK   EQU   *
         MVC   ADPLPBAV,ADPLPART      ADDRESS TO FORMAT.
         MVC   ADPLPPTR,=V(MCDE)      ADDR. OF MODEL.
         BAL   RL,FORMAT
         L     R15,ADPLPART           ADDR. CDE.
         LA    R15,CDEL(,R15)         POINT TO XTLST.
         ST    R15,ADPLPBAV           ADDR. TO FORMAT.
         MVC   ADPLPPTR,=V(MXTLST)    ADDR. OF MODEL.
         BAL   RL,FORMAT
NEXTNAMT EQU   *
         LA    R2,ZFNAMTL(,R2)        NEXT ENTRY.
         CLI   TNAME,255              END-OF-TABLE ?
         BE    FASTRAC
         B     NAMTLOOP               PROCESS NEXT ENTRY.
         DROP  R2
         TITLE 'FORMAT INSTORAGE RACF-PROFILE CONTROL-BLOCKS'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT PROFILE-TABLE-ENTRIES                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
FASTRAC  EQU   *
         BAL   RL1,GETZCVT            ACCESS ZEDVCVT.
         ICM   R2,15,ZCRISPTB         ANY ENTRIES ?
         BZ    RETURN                 NO.
FASTNXT  EQU   *
         LR    R14,R2                 ADDR. OF DATA TO ACCESS.
         LA    R15,ZCRISL             LENGTH OF DATA.
         BAL   RL,STORACC             ACCESS STORAGE.
         L     R2,ADPLPART            ADDR. OF DATA IN BUFFER.
         ST    R2,ADPLPBAV            ADDRESS TO FORMAT.
         MVC   ADPLPPTR,=V(MRIS)      ADDR. OF MODEL.
         BAL   RL,FORMAT              FORMAT ZEDVCVT.
         ICM   R2,15,ZCRISNXT-ZCRIS(R2) NEXT IN CHAIN.
         BZ    RETURN                 LAST.
         B     FASTNXT                PROCESS NEXT ENTRY.
         TITLE 'FORMAT CONTROL-BLOCK'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT CONTROL-BLOCK                                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
FORMAT   EQU   *
         L     R15,ADPLSERV           EXIT SERVICE ROUTER.
         CALL  (15),((RABDPL),CODEFMT,ADPLPFMT),VL
         XC    ADPLPPTR,ADPLPPTR      *
         XC    ADPLPBAV,ADPLPBAV      *  CLEAN-UP
         XC    ADPLPBAS,ADPLPBAS      *  FOR REUSE.
         XC    ADPLPBLS,ADPLPBLS      *
         B     FTAB(R15)
FTAB     B     0(RL)                  0  = OK.
         B     TRUNCATE               4  = CONTROL BLOCK TRUNCATED.
         B     CBERR                  8  = MODEL ERROR.
         NOP   *                      12 = SHOULD NOT OCCUR.
         B     CATERR                 16 = CATASTROPHIC ERROR.
         SPACE
*        ERRORS FROM FORMAT SERVICE
         SPACE
TRUNCATE EQU   *
         L     R15,ADPLBUF
         MVC   0(L'MSGTRUNC,R15),MSGTRUNC
         BAL   RL,PRINT
         B     RETURN
CBERR    EQU   *
         L     R15,ADPLBUF
         MVC   0(L'MSGCBERR,R15),MSGCBERR
         BAL   RL,PRINT
         B     RETURN
CATERR   EQU   *
         L     R15,ADPLBUF
         MVC   0(L'MSGTERME,R15),MSGTERME
         BAL   RL,PRINT
         B     RETURN
         TITLE 'PRINT MESSAGES OR DUMP DATA'
*---------------------------------------------------------------------*
*                                                                     *
*        PRINT MESSAGES OR DUMP DATA                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
PRINT    EQU   *
         L     R15,ADPLSERV             EXIT SERVICE ROUTER.
         CALL  (15),((RABDPL),CODEPRT)
         LTR   R15,R15                  OK ?
         BNZ   PRINTERR                 NO.
         L     R15,ADPLBUF              RESTORE BUFFER-ADDRESS.
         MVC   0(L'BLANKS,R15),BLANKS   CLEAR FOR REUSE.
         BR    RL                       BACK TO CALLER.
PRINTERR EQU   *
         WTO   'ZEDVCVT - DUMP EXIT UNABLE TO CONTINUE',ROUTCDE=11
         B     RETURN                   TERMINATE BECAUSE OF ERROR.
         TITLE 'ACCESS STORAGE'
*---------------------------------------------------------------------*
*                                                                     *
*        ACCESS STORAGE                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
STORACC  EQU   *
         ST    R14,ADPLPAAD             ADDRESS TO RETRIEVE.
         STH   R15,ADPLDLEN             LENGTH OF STORAGE.
         L     R15,ADPLSERV             EXIT SERVICE ROUTER.
         CALL  (15),((RABDPL),CODEACC,ADPLPACC),VL
         TM    FORMFLAG,128
         BZ    STORCHK
         NI    FORMFLAG,255-128
         BR    RL
STORCHK  EQU   *
         LTR   R15,R15
         BZR   RL                       YES.
         WTO   'STORAGE ACCESS FAILURE',ROUTCDE=11
         B     RETURN
         TITLE 'ACCESS ZEDVCVT'
*---------------------------------------------------------------------*
*                                                                     *
*        ACCESS ZEDVCVT                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
GETZCVT  EQU   *
         L     R14,ADPLCVT            CVT-POINTER.
         LA    R15,CVTUSER-CVT        LENGTH OF STORAGE.
         BAL   RL,STORACC             ACCESS STORAGE.
         SPACE
         L     R14,ADPLPART           ADDR. OF CVT IN BUFFER.
         ST    R14,PART1 KRKR
         L     R14,CVTUSER-CVT(,R14)  CVTUSER.
         LA    R15,ZEDVCVTE-ZEDVCVT   LENGTH OF STORAGE.
         BAL   RL,STORACC             ACCESS STORAGE.
         SPACE
         L     RZEDV,ADPLPART         ADDR. OF DATA IN BUFFER.
         ST    RZEDV,PART3 KRKR
         CLC   ZCID,=CL8'ZEDVCVT'     HAVE WE REALLY THE RIGHT THING ?
         BER   RL1                    YES.
         L     R15,ADPLBUF            A(OUTPUT BUFFER).
         MVC   0(L'MSGZERR,R15),MSGZERR BAD CVT (MAYBE CLOBBERED).
         BAL   RL,PRINT
         B     RETURN
         TITLE 'MESSAGES'
*---------------------------------------------------------------------*
*                                                                     *
*        MESSAGES                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MSGUSER  DC    C'*****************   Z E D V C V T   *****************'
MSGZERR  DC    C'  *** ERROR *** / ZEDVCVT , BADD ADDR. OR CLOBBERED'
MSGTRUNC DC    C'  *** ERROR *** / CB-FORMAT TRUNCATED'
MSGCBERR DC    C'  *** ERROR *** / CB-FORMAT ERROR'
MSGTERME DC    C'  *** ERROR *** / CB-FORMAT UNABLE TO CONTINUE'
MSG2QIP  DC    C'  PROFILE MANIPULATION IN PROGRESS'
MSG2CIP  DC    C'  FASTRAC CHECK IN PROGRESS'
MSGCCPOK DC    C'  CELL POOL BUILT'
MSGCCBIP DC    C'  CELL POOL BUILD IN PROGRESS'
MSGNOCDE DC    C'  *** ERROR *** UNABLE TO ACCESS CDE'
         TITLE 'NUTS AND BOLTS'
*---------------------------------------------------------------------*
*                                                                     *
*        NUTS AND BOLTS                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         DS    0A
         DC    CL8'PART1'
PART1    DC    A(0)
         DC    CL8'PART2'
PART2    DC    A(0)
         DC    CL8'PART3'
PART3    DC    A(0)
RISP     DC    A(0)
CODEFMT  DC    A(ADPLSFMT)
CODEACC  DC    A(ADPLSACC)
CODEPRT  DC    A(ADPLSPRT)
BLANKS   DC    CL133' '
FORMFLAG DC    AL1(0)
         SPACE
         BLSABDPL AMDCPST=NO,                                          +
               AMDEXIT=NO,                                             +
               AMDOSEL=NO,                                             +
               AMDPACC=YES,                                            +
               AMDPECT=NO,                                             +
               AMDPFMT=YES,                                            +
               AMDPSEL=NO,                                             +
               DSECT=NO
         LTORG
         TITLE 'SPECIAL REGISTER EQUATES'
*---------------------------------------------------------------------*
*                                                                     *
*        SPECIAL REGISTER EQUATES                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RABDPL   EQU   5
RZEDV    EQU   6
RL       EQU   7
RL1      EQU   8
         TITLE 'ZEDVCVT - FORMAT MODEL'
*---------------------------------------------------------------------*
*                                                                     *
*        USERCVT - FORMAT MODEL                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MZEDVCVT CSECT
         PRINT NOGEN
         BLSQMDEF BASELBL=ZCID,                                        +
               CBLEN=ZEDVCVTE-ZCID,                                    +
               PREFIX=0,                                               +
               ACRONYM=ZEDVCVT,                                        +
               ACROLEN=8,                                              +
               ACROLBL=ZCID,                                           +
               OFFSETS=PRINT,                                          +
               LBLSPC=0
*
         BLSQMFLD NAME=ZCID,                                           +
               PREFIX=2,                                               +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCDATE,                                         +
               PREFIX=2,                                               +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCTIME,                                         +
               PREFIX=2,                                               +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISPTB,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NEWLINE,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISPTN,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRFLAG1,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=*,                                              +
               OFF=(ZCRFLAG1-ZCID)+L'ZCRFLAG1,                         +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=3,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRQLOCK,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=4,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRPLOCK,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=4,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=*,                                              +
               OFF=(ZCRPLOCK-ZCID)+4,                                  +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=16,                                                 +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZIAMBUF,                                        +
               PREFIX=1,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZIACURR,                                        +
               PREFIX=1,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=*,                                              +
               OFF=(ZIACURR-ZCID)+L'ZIACURR,                           +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=4,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZFETCHA,                                        +
               PREFIX=1,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZFNAMTA,                                        +
               PREFIX=1,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=*,                                              +
               OFF=(ZFNAMTA-ZCID)+L'ZFNAMTA,                           +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=4,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCFLAG,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCFLAG+1,                                     +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=3,                                                  +
               NOLABEL,                                                +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCPID,                                        +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCLOCK,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCLOCK+1,                                     +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=3,                                                  +
               NOLABEL,                                                +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCELLP,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCELLS,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRCSIZE,                                       +
               PREFIX=3,                                               +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMDEF END
         TITLE 'ZFNAMT - FORMAT FOR LOADED-MODULE NAME-TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT MODEL FOR LOADED-MODULE NAME-TABLE                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MZFNAMT  CSECT
         PRINT NOGEN
         BLSQMDEF BASELBL=ZFNAMT,                                      +
               PREFIX=0,                                               +
               CBLEN=ZFNAMTN*ZFNAMTL+1,                                +
               ACRONYM=ZFNAMT,                                         +
               ACROLEN=8,                                              +
               OFFSETS=PRINT,                                          +
               LBLSPC=0
*
         BLSQMFLD NAME=ZFNAMTE,                                        +
               VIEW=X'0300',                                           +
               ARRAY=ZFNAMTN,                                          +
               NOLABEL
*
         BLSQMFLD NAME=TNAME,                                          +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               NEWLINE,                                                +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TSVCO,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TSVCA,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TEPA,                                           +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TFLAG,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TSVCN,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=*,                                              +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               OFF=(TSVCN-ZFNAMT)+1,                                   +
               LEN=2,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=TCDEA,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               ARRAY=END,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=EOT,                                            +
               OFF=(ZFNAMTN*ZFNAMTL),                                  +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               LEN=1,                                                  +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMDEF END
         TITLE 'FORMAT CDE'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT CDE                                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MCDE     CSECT
         PRINT NOGEN
         BLSQMDEF BASELBL=CDENTRY,                                     +
               CBLEN=CDEEND-CDENTRY,                                   +
               PREFIX=2,                                               +
               ACRONYM=CDE,                                            +
               ACROLEN=3,                                              +
               ACROLBL=CDENTRY,                                        +
               OFFSETS=PRINT,                                          +
               LBLSPC=0
*
         BLSQMFLD NAME=CDCHAIN                                         +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDRRBP,                                         +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDNAME,                                         +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDENTPT,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDXLMJP,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDUSE,                                          +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDATTRB,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDSP,                                           +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDATTR,                                         +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDATTR2,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=CDATTR2,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMDEF END
         TITLE 'FORMAT XTLST'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT XTLST                                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MXTLST   CSECT
         PRINT NOGEN
         BLSQMDEF BASELBL=XTLST,                                       +
               CBLEN=XTLSTL,                                           +
               PREFIX=3,                                               +
               ACRONYM=XTLST,                                          +
               ACROLEN=5,                                              +
               ACROLBL=XTLST,                                          +
               OFFSETS=PRINT,                                          +
               LBLSPC=0
*
         BLSQMFLD NAME=XTLLNTH,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=XTLNRFAC,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=XTLMSBLA,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=XTLMSBAD,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMDEF END
         TITLE 'FORMAT CDE'
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT INSTORAGE-PROFILE CHAIN ENTRIES                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MRIS     CSECT
         PRINT NOGEN
         BLSQMDEF BASELBL=ZCRIS,                                       +
               CBLEN=ZCRISL,                                           +
               PREFIX=3,                                               +
               ACRONYM=RISP,                                           +
               ACROLEN=4,                                              +
               ACROLBL=ZCRIS,                                          +
               OFFSETS=PRINT,                                          +
               LBLSPC=0
*
         BLSQMFLD NAME=ZCRISCN,                                        +
               VIEW=X'0300',                                           +
               DTYPE=EBCDIC,                                           +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISNXT,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISPA,                                        +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISFLG,                                       +
               VIEW=X'0300',                                           +
               LEN=4,                                                  +
               OFF=(ZCRISPA-ZCRIS)+L'ZCRISPA,                          +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISSUC,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISREJ,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMFLD NAME=ZCRISUDF,                                       +
               VIEW=X'0300',                                           +
               DTYPE=HEX,                                              +
               CALLRTN,                                                +
               NOSPLIT
*
         BLSQMDEF END
         TITLE 'DUMMY SECTIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        DUMMY SECTIONS                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         BLSABDPL AMDCPST=NO,                                          +
               AMDEXIT=YES,                                            +
               AMDOSEL=NO,                                             +
               AMDPACC=NO,                                             +
               AMDPECT=NO,                                             +
               AMDPFMT=NO,                                             +
               AMDPSEL=NO,                                             +
               DSECT=YES
         PRINT NOGEN
         IHAACEE
         IHAASCB
         IHAASXB
         CVT   DSECT=YES
         ZEDVCVT
         IHAXTLST
XTLSTL   EQU   *-XTLST
         IHACDE
CDEEND   EQU   *
CDEL     EQU   *-CDENTRY
         END
//L.SYSLMOD DD DSN=SYS1.LINKLIB1,DISP=SHR
 SETCODE AC(1)
 NAME ZDUMP00(R)
