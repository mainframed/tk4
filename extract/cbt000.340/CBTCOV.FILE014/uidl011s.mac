GLUE     CSECT
*
*        PDSDOC *GLUE MODULE FOR PL/1    FROM GG24-3142-01............*
*
         PRINT ON,GEN
         ENTRY START
START    STM   14,12,12(13)
         BALR  BASER,0
         USING *,BASER
*
LOOP     L     CTXTR,0(1)         PICK UP ADDR OF CTXT
         USING CTXT,CTXTR
*
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW
         LR    DATAR,1             ADDRESS FOR DATA
         USING DATAAREA,DATAR
*
         ST    13,SAVEAREA+4       STANDARD SAVE AREA LINKAGE
         LA    WORKR,SAVEAREA
         ST    WORKR,8(13)
         LR    13,WORKR            R13 NOW POINTS TO SAVE AREA
*
* BLANK OUT THE FIELDS FOR WTO AND
* MVS COMMANDS
*
         MVC   WTOMODEL+4,=CL112' '
         MVC   COMMAND(L'COMMAND),BLANKS
*
         XR    WORKR,WORKR         ZERO REG AND STORE
         ST    WORKR,ALISTT        IN TASK ISA
         ST    WORKR,ALISTC        IN NUMBER OF CONC TASKS
         ST    WORKR,ALISTII       IN ISA INCREMENT
         ST    WORKR,ALISTHI       IN HEAP INCREMENT
         OI    ALISTII,X'80'       SHOW END OF LIST
         LA    WORKR,CTXTADDR      ARGUMENTS PASSED TO PL/I
         ST    WORKR,ALISTA
         LA    WORKR,ISALGTH       LENGTH OF ISA
         ST    WORKR,ALISTL
         LA    WORKR,ISA
         ST    WORKR,ALISTI
         LA    WORKR,OPTIONS       PL/I RUN TIME OPTIONS
         ST    WORKR,ALISTO
         LA    WORKR,HEAPSIZE
         ST    WORKR,ALISTHS
         LA    WORKR,HEAP
         ST    WORKR,ALISTHA
         MVC   CTXTCOPY,0(CTXTR)   CREATE COPY OF CTXT FOR PL/ I
         LA    WORKR,CTXTCOPY
         ST    WORKR,CTXTADDR      STORE FOR PL/I ROUTINE
         OI    CTXTADDR,X'80'      SHOW LAST PARAMETER PASSED
         LA    1,ALIST             POINT TO PARAMETER FOR PLICALLB
         L     15,=V(PLICALLB)     GO TO PL/I ROUTINE
GOPLI    BALR  14,15
*
         MVC   0(104,CTXTR),CTXTCOPY  RESTORE CTXT
         CLC   WTOMODEL+4(100),BLANKS  ANY MSG TO WTO?
         BE    CHKCOMMD            NO - CHECK IF COMMAND TO BE ISSUED
         WTO   MF=(E,WTOMODEL)     ISSUE THE WTO
*
CHKCOMMD DS    0H
         CLC   COMMAND(L'COMMAND),BLANKS
         BE    FINISHED            NO COMMAND ASKED FOR BY PL/I EXIT
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR MGCR MACRO AREA
         MVC   MGCRTEXT(L'COMMAND),COMMAND MOVE IN THE CMD TO MGCR
         LA    WORKR,40            STORE LENGTH OF COMMAND
         STC   WORKR,MGCRLGTH
         SR    0,0
         MGCR  MGCRPL              ISSUE THE SVC 34
*
*
FINISHED L     13,4(13)
         FREEMAIN  RU,LV=DATAEND,A=(DATAR),SP=SPINPRVT
         LM    14,12,12(13)
EXITEND  BR    14                  RETURN
         EJECT ,
ISALGTH  DC    A(ISAEND-ISA)       LENGTH OF ISA
HEAPSIZE DC    A(HEAPEND-HEAP)     HEAP SIZE
OPTIONS  DC    X'55'               NOREPORT,NOSPIE,NOSTAE,NOCOUNT
         DC    X'68'               NOFLOW,KEEPHEAP,HEAP ANYWHERE
         DC    X'0000'
BLANKS   DC    CL100' '
         SPACE 2
         IEZVX100 ,
         EJECT ,
SPINPRVT EQU   230                 99 FOR TESTING,230 FOR REAL
WORKR    EQU   15                  SHORT TERM WORK REGISTER
WORK2    EQU   7                   SHORT TERM WORK REGISTER
BASER    EQU   10                  ROUTINE BASE REGISTER
DATAR    EQU   11                  REGISTER FOR DATA AREA
CTXTR    EQU   9
         SPACE 2
DATAAREA DSECT
SAVEAREA DS    18F
CTXTADDR DS    F
ALIST    DS    0F
ALISTA   DS    A                   ADDR OF ARGLIST
ALISTL   DS    A                   LENGTH OF ISA FOR PL/I
ALISTI   DS    A                   ADDR OF ISA
ALISTT   DS    F                   TASK ISA
ALISTC   DS    F                   CONCURRENT TASKS
ALISTO   DS    A                   ADDR OF OPTIONS WORD
ALISTHS  DS    A                   ADDR OF HEAP SIZE
ALISTHA  DS    A                   ADDR OF HEAP
ALISTHI  DS    A                   ADDR OF HEAP INCREMENT
ALISTSHI DS    A                   ADDR OF SUBTASK HEAP INCREMENT
ALISTII  DS    A                   ADDR OF ISA INCREMENT
CTXTCOPY DS    CL108               COPY OF CTXT
         EJECT ,
* STATIC FORM OF WTO
COMMAND  DS    CL40                MVS COMMAND TO ISSUE
         PRINT ON
WTOMODEL WTO   '                                                       X
                                                                     ',X
               MF=L
MGCR     IEZMGCR DSECT=NO          MACRO FOR
HEAP     DS    256D                STORAGE HEAP FOR PL/I
HEAPEND  EQU   *
ISA      DS    1024D               ISA FOR PL/I
         DS    1024D
ISAEND   EQU   *
DATAEND  EQU   *-DATAAREA
         END   GLUE
