         TITLE '***  P1250400  ***   BUILD DIRECTORY LIST OF A PARTITIOX
               NED DATASET  ***'
*******************************************************************
*/*P1250400    BUILD DIRECTORY-LIST OF PARTITIONED DATASET        *
*1 FUNCTION    THIS PROGRAM BUILDS A DIRECTORY LIST OF A          *
*              PARTITIONED DATASET ON FORM OF AN ARRAY.           *
*2 SUMMARY                                                        *
*  TYPE        SYSTEM    BATCH                                    *
*  ACCOUNT     90                                                 *
*  ENTRY       P1250400                                           *
*                                                                 *
*  MACRO       BEGIN                    PROGRAM-INITIALISATION    *
*              ENDE                     RETURN TO USER            *
*  FILE        SYSLIB                   PARTITIONED DATASET OR    *
*                                       ALLOCATED VOLUME          *
*                                                                 *
*  HISTORY     1979.06   HE             GRUNDVERSION OS           *
*              1981.10   HE             OVERRIDE DDNAME BY USER   *
*              1982.04   HO             UEBERNAHME MVS 3.8.       *
*              1982.05   HE             CHANGE VOLUME LOCATION    *
**                                                                *
*3 USAGE       CALL P1250400,(PARM1,PARM2)                        *
*              PARM1 -  DSNAME OF THE LIBRARY (PADDED WITH BLANKS)*
*              PARM2 -  FULLWORD CONTAINING START-ADDRESS OF ARRAY*
*                                                                 *
*              THE CONTENT OF EACH ELEMENT IS AS FOLLOWS:         *
*              POS. 01-08   NAME OF PDS-MEMBER                    *
*              POS. 09-11   TTR                                   *
*              POS.    12   INDICATOR BYTE                        *
*              POS. 13-15   CORESIZE                              *
*              POS.    16   FLAGBYTE   BIT0    ALIAS-NAME         *
*                                      BIT1-7  NOT USED           *
*              POS. 17-20   SSI (SYSTEM-STATUS-INFORMATION)       *
*                                                                 *
*              TABLE PREFIX - 5 FULLWORDS AS FOLLOWS:             *
*              WORD1 -  TOTAL LENGTH OF ARRAY                     *
*              WORD2 -  START ADDRESS OF ARRAY                    *
*              WORD3 -  VOLUME SERIAL NUMBER                      *
*              WORD4 -  VOLUME SERIAL NUMBER (CONTD.)             *
*              WORD5 -  2 HALFWORDS: 1ST - NB. DIRECTORY-BLOCKS   *
*                                    2ND - NB. ALLOCATED BLOCKS   *
*                                                                 *
*        THE REQUIRED DATASET MUST NOT BE CATALOGED AS LONG AS    *
*        IS RESIDES ON A VOLUME ALLOCATED VIA 'SYSLIB' DD-STMT.   *
*                                                                 *
*        THE USER MAY CHANGE THE STANDARD DDNAME 'SYSLIB' BY      *
*        CODING PARM1 AS FOLLOWS: 'DDN=USERDD..44-CHAR-DSNAME..'  *
*                                                                 *
*        IF THE USER CODES A DSN= PARAMETER TO THE 'SYSLIB' DD    *
*        STMT., THE OBTAIN IS NOT USED TO FIND THE VOLUME CONT-   *
*        AINING 'PARM1' DSNAME. THE DSNAME WILL STILL BE ALTERED, *
*        BUT ALL FILES MUST RESIDE ON THE ALLOCATED VOLUME.       *
**/                                                               *
*******************************************************************
         PRINT NOGEN
         EJECT
P1250400 BEGIN PARAM=2,PARAMR=(RG2,RG3),SETEQ=YES             @@@@
         SPACE 2
         USING DJFCB,RG4                COMMUNICATION-AREA
         USING PDS2,RG5                 COMMUNICATION-AREA
         USING DSARRAY,RG3              COMMUNICATION-AREA
         SPACE 2
         KB90LABL ,'C O N T R O L  -  R O U T I N E'
         MVC   SYSLIB+40(8),STDNAM      STANDARD DDNAME              01
         CLC   0(4,RG2),=C'DDN='        USER'S OVERRIDE              01
         BNE   $CNTL00                                               01
         MVC   SYSLIB+40(8),4(RG2)      UPDT. WITH USER'S NAME       01
         LA    RG2,12(RG2)              POINT TO DSNAME              01
$CNTL00  DS    0H                                                    01
         LA    RG8,SYSLIB               ADDRESS DCB                  01
         RDJFCB ((8))
         LA    RG4,JFCBAR               JOB-FILE-CONTROL-BLOCK
         BAL   RG12,$LOCV01             LOCATE VOLUME
         MVC   JFCBDSNM,0(RG2)          DATASET NAME
         MVI   JFCBTSDM,X'C8'           DON'T RETURN TO JOB-QUEUE
         OPEN  ((8)),TYPE=J
         SR    RG7,RG7
         XC    DIRBLK(4),DIRBLK         INIT DIRECTORY BLK.COUNT
         BAL   RG12,$STOR01             CALCULATE STORAGE SIZE
         CLOSE (SYSLIB,REREAD),TYPE=T
         OPEN  ((8)),TYPE=J
         BAL   RG12,$BDIR01             BUILDING THE ARRAY
         XC    DARRAY,DARRAY            CLEAR LAST ENTRY
         MVC   DNAME,=8X'FF'            INDICATE END DIRECTORY
         MVI   $RDIR02+1,X'F0'          EOF ADDRESS
$CNTL01  DS    0H
         BAL   RG12,$RDIR01             READ DIRECTORY-BLOCK
         LH    RG1,DIRBLK
         AH    RG1,=H'1'                COUNT UNUSED BLOCKS
         STH   RG1,DIRBLK
         B     $CNTL01
         SPACE 1
PHEND    DS    0H
         L     RG1,APREFIX              ARRAY-ADDRESS
         MVC   16(4,RG1),DIRBLK         SAVE STATISTICS
         CLOSE (SYSLIB)
         ENDE  RC=0
         EJECT
         KB90LABL $LOCV01,'LOCATE VOLUME WITH SPECIFIED DSNAME'
         MVC   LDSNAM,0(RG2)            DATASET-NAME                 03
         LA    RG7,JFCBVOLS             VOL=SER LIST
         SR    RG6,RG6                  CLEAR WORK-REG
         IC    RG6,JFCBNVOL             NB. VOLUMES
         LTR   RG6,RG6                  TEST IF ON PRESENT           03
         BZ    $LOCV03                                               03
$LOCV02  DS    0H
         XC    LOCAR(148),LOCAR         CLEAR WORKAREA
         MVC   TVOLS,0(RG7)
         OBTAIN FORMAT1
         LTR   RG15,RG15                CKECK RETURN-CODE
         BZ    $LOCV04
         LA    RG7,6(RG7)               NOT FOUND,POINT NEXT SERIAL
         BCT   RG6,$LOCV02
         B     $LOCV05
         SPACE 1
$LOCV03  DS    0H
         LA    RG7,LOCAR+6              ADDRESS VOL-SER-LIST         03
         LOCATE CLIST                   SEARCH CATALOG               03
         LTR   RG15,RG15                                             03
         BNZ   $LOCV05                                               03
$LOCV04  DS    0H                                                    03
         MVC   JFCBVOLS(6),0(RG7)       SAVE SERIAL-NO.              03
         B     $LOCV01R                                              03
         SPACE 1
$LOCV05  DS    0H
         LR    RG11,RG15                SAVE RETURN-CODE
         MVC   EMSG+48(30),0(RG2)       DSNAME
EMSG     WTO   '*** P1250400 ***  OBTAIN FAILED FOR DSNAME -           X
                                   , RC=RG11',ROUTCDE=(11)
         ABEND 401
         EJECT
         KB90LABL $STOR01,'CALCULATE STORAGE-SIZE'
         BAL   RG12,$RDIR01             READ DIRECTORY-BLOCK
         LH    RG1,DIRBLK
         AH    RG1,=H'1'                COUNT 1 BLOCK
         STH   RG1,DIRBLK
         LH    RG6,DIRECT               USED BYTES OF BLOCK
         SH    RG6,=H'12'
         LA    RG5,DIRECT               ORIGIN
         AR    RG6,RG5                  ABS. ADDRESS LAST MEMBER
         LA    RG5,2(RG5)               1ST MEMBER
$STOR02 DS     0H
         LA    RG7,1(RG7)               COUNT 1 MEMBER
         CR    RG5,RG6                  LAST MEMBER IN BLOCK
         BH    $STOR01+4                GET NEXT
         CLC   PDS2NAME,=8X'FF'         LAST DIRECTORY ENTRY
         BE    $STOR03
         SR    RG2,RG2
         NI    11(RG5),X'1F'            RESET UNWANTED BITS
         IC    RG2,11(RG5)              NB. USER HALFWORDS
         SLL   RG2,1                    NB. USER BYTES
         LA    RG5,12(RG2,RG5)          POINT NEXT MEMBER
         B     $STOR02
         SPACE 1
$STOR03 DS     0H
         AH    RG7,=H'2'                TABLE PREFIX & LAST ENTRY
         MH    RG7,=H'20'               NB. MEMBERS * TAB WIDTH
         LR    RGNL,RG7
         GETMAIN R,LV=(0)               GET STORAGE
         ST    RG1,APREFIX
         STM   RGNL,RG1,0(RG1)          SAVE LENGTH/ADDRESS PREFIX
         MVC   8(6,RG1),TVOLS           SERIAL NUMBER
         MVC   DALBLK,DIRBLK            SAVE ACTIVE DIRECTORY-BLOCKS
         ST    RG1,0(RG3)               ADDRESS TO USER
         LR    RG3,RG1
         B     $STOR01R
         EJECT
         KB90LABL $BDIR01,'BUILD DIRECTORY ARRAY'
         BAL   RG12,$RDIR01             READ DIRECTORY-BLOCK
         LA    RG5,DIRECT
         SR    RG6,RG6
         LH    RG6,DIRECT               USED BYTES THIS BLOCK
         AR    RG6,RG5                  END ADDRESS
         LA    RG5,2(RG5)               PAST LENGTH 1ST MEMBER
$BDIR02  DS    0H
         LA    RG3,20(RG3)              NEXT ARRAY-ENTRY
         CLC   PDS2NAME,=8X'FF'         LAST MEMBER FOUND ?
         BE    $BDIR01R
         XC    DARRAY,DARRAY            CLEAR ARRAY ENTRY
         MVC   DNAME(11),PDS2NAME       MEMBER NAME & TTR
         MVC   DFLG,PDS2INDC-2          IS AN ALIAS
         NI    DFLG,S80                 RESET UNWANTED BITS
         NI    PDS2INDC-2,X'1F'         CLEAR UNUSED BYTES
         CLI   PDS2INDC-2,S02           L' USER DATA (HWD)
         BL    $BDIR04
         BH    $BDIR05
         MVC   DSTATUS,PDS2INDC-1       COPY SSI
         LA    RG7,PDS2NAME+16          NEXT MEMBER
         B     $BDIR05
         SPACE 1
$BDIR04  DS    0H
         LA    RG7,PDS2NAME+12          ANY USER DATA
         CLI   PDS2INDC-2,S01           ANY USER DATA
         BNE   $BDIR06
         LA    RG7,2(RG7)               NON LOAD-MOD, 2 BYTE USER
         B     $BDIR06
         SPACE 1
$BDIR05  DS    0H
         SR    RG2,RG2
         IC    RG2,PDS2INDC-2           USER DATA HWD
         AR    RG2,RG2                  *2 = NB. BYTES
         LA    RG7,PDS2USRD-2           POINT USER DATA
         AR    RG7,RG2                  PAST USER DATA
         CLI   PDS2INDC-2,X'0B'         MIN LENGTH LOAD-MOD
         BL    $BDIR06
         MVC   DSIZE,PDS2STOR-2         LOAD-MOD ASSUMED (CORE SIZE)
         TM    PDS2FTB1-2,S10           SSI-DATA PRESENT
         BZ    $BDIR06
         SH    RG7,=H'6'                POINT AUTH.CODE & SSI
         MVC   DSTATUS,0(RG7)           COPY SSI
         LA    RG7,6(RG7)               BACK AT NEXT MEMB
         EJECT
$BDIR06  DS    0H
         LR    RG5,RG7
         ST    RG7,SAVE1                CHECK ALIGNEMENT
         TM    SAVE1+3,S01              ODD
         BZ    *+8
         LA    RG5,1(RG5)
         CR    RG5,RG6                  END OF USED BYTES
         BL    $BDIR02
         B     $BDIR01+4                GO READ NEXT BLOCK
         SPACE 2
         KB90LABL $RDIR01,'READ A DIRECTORY-BLOCK'
         READ  DECBX,SF,SYSLIB,DIRECT,'S'
         CHECK DECBX
         B     $RDIR01R
         SPACE 1
$RDIR02  NOP   PHEND                    NORMAL END OF JOB
         WTO   '** P1250400 **  UNEXPECTED EOF REACHED IN DIRECTORY  ',X
               ROUTCDE=(11)
         ABEND 402
         SPACE 2
         LTORG
         EJECT
         KB90LABL ,'D A T A    A R E A S'
SYSLIB   DCB   DDNAME=SYSLIB,DSORG=PS,MACRF=R,EODAD=$RDIR02,           X
               RECFM=F,BLKSIZE=256,LRECL=256,                          X
               EXLST=AJFCB
FORMAT1  CAMLST SEARCH,LDSNAM,TVOLS,LOCAR
CLIST    CAMLST NAME,LDSNAM,,LOCAR                                   03
         SPACE 1
APREFIX  DC    F'0'
SAVE1    DC    F'0'
DIRBLK   DC    H'0'
DALBLK   DC    H'0'
AJFCB    DC    X'87'
         DC    AL3(JFCBAR)
         SPACE 1
JFCBAR   DS    CL176
DIRECT   DS    CL256
         DS    0D
LOCAR    DS    CL265
         SPACE 1
LDSNAM   DC    CL44' '
TVOLS    DC    CL6' '
STDNAM   DC    CL8'SYSLIB  '
         SPACE 2
         KB90LABL ,'COMMUNICATION - AREAS'
DJFCB    DSECT
         IEFJFCBN
DSARRAY  DSECT
DARRAY   DS    0CL20
DNAME    DS    CL8
DTRACK   DS    CL3
DINDK    DS    CL1
DSIZE    DS    CL3
DFLG     DS    CL1
DSTATUS  DS    CL4
         SPACE 1
         IHAPDS
         END   P1250400
