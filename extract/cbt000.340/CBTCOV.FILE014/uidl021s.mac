IDL433D  TITLE 'COMMUNICATIONS TASK USER EXIT FOR MESSAGE IEF433D'
*        PDSDOC *COMMS TASK EXIT TO REPLY 'NOHOLD'....................*
***********************************************************************
*                                                                     *
* MODULE NAME = IDL433D                                               *
*                                                                     *
* DESCRIPTIVE NAME = COMMUNICATIONS TASK USER EXIT FOR MESSAGE IEF433D*
*                                                                     *
* FUNCTION = ALWAYS REPLIES 'NOHOLD' TO IEF433D AND SUPPRESSES MESSAGE*
*                                                                     *
* THIS MODULE WRITTEN BY D.H.CARTWRIGHT, INTERDATA AG, LANGENTHAL     *
* BASED ON IBM SAMPLE EXIT 'CANCWAIT'.                                *
*                                                                     *
*                                                                     *
*                                                                     *
* INPUT DATA = R1 POINTS TO THE ADDRESS OF THE CTXT                   *
* R 13 ADDRESS OF STANDARD SAVE AREA                                  *
* R14 RETURN POINT                                                    *
* R15 ENTRY POINT                                                     *
*                                                                     *
* REGISTERS SAVED = R0 - R15                                          *
*                                                                     *
* REGISTER USAGE = R5 - POINTER TO THE ADDRESS OF THE CTXT            *
* R11 - MODULE DATA REGISTER                                          *
* R12 - MODULE BASE REGISTER                                          *
* R13 - POINTER TO A STANDARD SAVE AREA                               *
* R14      -     RETURN POINT                                         *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
IDL433D CSECT
IDL433D AMODE 31                   31-BIT ADDRESSING MODE
IDL433D RMODE ANY                  31-BIT RESIDENCE
***********************************************************************
*                                                                     *
* REGISTER ASSIGNMENTS                                                *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
CTXTPTR  EQU   5
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
SPINPRVT EQU   230
         EJECT ,
***********************************************************************
*                                                                     *
* STANDARD ENTRY LINKAGE                                              *
*                                                                     *
***********************************************************************
         STM   R14,R12,12(R13)        SAVE CALLER'S REGISTERS
         BALR  R12,R0              ESTABLISH MODULE BASE
         USING *,R12               REGISTER
         L     CTXTPTR,0(R1)       ESTABLISH ADDRESSABILITY
         USING CTXT,CTXTPTR        TO THE CTXT
***********************************************************************
*                                                                     *
* DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW             *
* THE 16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER          *
* LIST TO BE IN 24-BIT ADDRESSABLE STORAGE.                           *
*                                                                     *
***********************************************************************
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW  OBTAIN DYNAMIC   X
                                   STORAGE
         LR    R11,R1              ADDRESS RETURNED IN REG1
         USING DATAAREA,R11        ADDRESSABILITY TO DYNAMIC           X
                                   STORAGE
         ST    R13,SAVEAREA+4      SET BACKWARD PTR
         LA    R15,SAVEAREA        GET ADDRESS OF SAVE AREA
         ST    R15,8(R13)          SET FORWARD PTR
         LR    R13,R15             SET REG13 TO POINT TO               X
                                   STANDARD SAVE AREA
         EJECT ,
***********************************************************************
*                                                                     *
* WORKING WITH MESSAGE IEF433D THEREFORE REPLY TO THE MESSAGE         *
*                                                                     *
***********************************************************************
MSG235D  EQU   *
         OI    CTXTRFB2,CTXTRHCO   SUPPRESS THE MESSAGE
         BAL   R14,ISSUMGCR        REPLY TO IEF433D 'R XX,NOHOLD'
         EJECT ,
***********************************************************************
*                                                                     *
* STANDARD EXIT LINKAGE                                               *
*                                                                     *
***********************************************************************
FINISHED EQU   *
         L     R13,4(R13)          RESTORE REG 13
         FREEMAIN  RU,LV=DATAEND,A=(R11),SP=SPINPRVT  FREE THE STORAGE
         LM    R14,R12,12(R13)        RESTORE CALLER"S                 X
                                   REGISTERS
         BR    R14                 RETURN TO CALLER
         EJECT ,
***********************************************************************
*                                                                     *
* PROCEDURE - ISSUMGCR                                                *
*                                                                     *
* FUNCTION - REPLIES TO MESSAGE IEF433D WITH A NEGATIVE RESPONSE      *
*                                                                     *
* INPUT - NONE                                                        *
*                                                                     *
* OUTPUT - A REPLY COMMAND IS ISSUED VIA SVC 34                       *
*                                                                     *
* NOTES - THE MGCR MACRO MAY BE USED TO ISSUE A REPLY COMMAND         *
*                                                                     *
***********************************************************************
ISSUMGCR EQU   *
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR THE PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT MOVE IN THE REPLY BUFFER
         MVC   REPLY,CTXTRPID      INSERT THE REPLY ID
         LA    R1,(MGCRTEXT-MGCRPL)+L'TXTINSRT GET MGCRPL LENGTH
         STC   R1,MGCRLGTH         SAVE LENGTH IN THE MGCRPL
         SR    R0,R0               CLEAR REGISTER ZERO
         MGCR  MGCRPL              ISSUE THE COMMAND
         BR    R14                 RETURN TO CALLER
         EJECT ,
***********************************************************************
*                                                                     *
* CHARACTER CONSTANTS                                                 *
*                                                                     *
***********************************************************************
TXTINSRT DC    C'REPLY XX,NOHOLD'   WTOR REPLY
         EJECT ,
***********************************************************************
*                                                                     *
* STORAGE DEFINITIONS                                                 *
*                                                                     *
***********************************************************************
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F                 STANDARD SAVE AREA
         DS    0F
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL6                 THE REPLY COMMAND
REPLY    DS    CL2                 REPLY ID
REPLYMSG DS    CL3                 WTOR RESPONSE
         ORG
DATAEND  EQU   *-DATAAREA
         EJECT ,
***********************************************************************
*                                                                     *
* MAPPING OF THE MESSAGE TEXT                                         *
*                                                                     *
***********************************************************************
MSGTEXT  DSECT
MSGID    DS    CL8                 MESSAGE ID
JOBDATA  DS    CL8                 JOB DATA(ID AND NAME)
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         IEZVX100
         END   IDL433D
