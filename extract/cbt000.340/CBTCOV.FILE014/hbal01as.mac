         TITLE 'HASP CONTROL BLOCK GENERATION MACRO'
         SPACE 3
*
*****    $HASPCB    *****    GENERATE HASP CONTROL BLOCKS
         SPACE 1
         MACRO
         $HASPCB &DOC=NO,&LIST=NO
         GBLC  &PRINT,&GEN,&DATA
         PUSH  PRINT
         PRINT &PRINT
         $SVT  DOC=&DOC
         $PSA  LIST=&LIST
         $ASCB LIST=&LIST
         $CVT  LIST=&LIST
         $WPL  LIST=&LIST
         $JQE  DOC=&DOC            GENERATE HASP JQE DSECT
         $LRC  DOC=&DOC
         $PCE  DOC=&DOC            GENERATE HASP PCE DSECT
         $RDRWORK DOC=&DOC         GENERATE HASP RDR PCE EXTENSION
         $PPPWORK DOC=&DOC         GENERATE HASP PPPWORK
         $BUFFER DOC=&DOC          GENERATE HASP BUFFER DSECT
         $CMB  DOC=&DOC            GENERATE HASP CMB DSECT
         $ERA  DOC=&DOC
         $PDDB DOC=&DOC
         $TAB  DOC=&DOC
         $IOT  DOC=&DOC
         $JCT  DOC=&DOC            GENERATE HASP JCT DSECT
         $JOE  DOC=&DOC            GENERATE HASP JOE DSECT
         $CAT  DOC=&DOC            GENERATE HASP CAT DSECT
         $PRE  DOC=&DOC
         $PIT  DOC=&DOC            GENERATE HASP PIT DSECT
         $CSA  DOC=&DOC            GENERATE HASP CSA DSECT
         $DCT  DOC=&DOC            GENERATE HASP DCT DSECT
         $HCT  DOC=&DOC            GENERATE HASP HCT DSECT
         $KIT  DOC=&DOC            GENERATE HASP KIT DSECT
         $XIT  DOC=&DOC            GENERATE HASP XIT DSECT
         $MITETBL DOC=&DOC
         $MIT  DOC=&DOC            GENERATE HASP MIT DSECT
         $EXITPL DOC=&DOC          GENERATE HASP EXITPL DSECT
         $TEXT LIST=&LIST
         $KEYS LIST=&LIST
         $TGM  DOC=&DOC
         SPACE 1
         POP   PRINT
         PRINT &GEN,&DATA
         MEND
         TITLE 'HASP CONSOLE ROUTINES'
         SPACE 3
USER01   START 0
         SPACE 3
         COPY  $HASPGEN
         TITLE 'HASP MODULE INFORMATION TABLE'
USER01   $MODULE
         SPACE 3
         TITLE 'HASP CONSOLE ROUTINES'
HA$PUS01 CSECT ,
         SPACE 3
*
*                             EXTERNAL REFERENCES
*
         SPACE 5
         TITLE 'HASP CONTROL BLOCKS'
         SPACE 3
*
*                             DOCUMENTATION OPTIONS FOR THIS ASSEMBLY
*
         SPACE 3
         $SYSPARM (OFF,NOGEN,NODATA,NO,NO)
         SPACE 3
*
*                             GENERATE HASP CONTROL BLOCKS
*
         $HASPCB DOC=&DOC,LIST=&LIST
         TITLE 'PRINT/PUNCH SEPARATORS'
USERSEP1 $ENTRY CSECT=YES
         $SAVE                     SAVE CALLER'S REGISTERS
         SPACE 1
         LR    BASE3,R15           ESTABLISH BASE REGISTER
         SPACE 1
         LR    WE,R0               SAVE R0 FOR FUTURE USE
         SPACE 3
BAL001   L     WB,PCEDCT           PICK UP DCT ADDRESSABILITY
         USING DCTDSECT,WB
         CLC   DCTDEVN(2),=C'PU'   IS OUTPUT DEVICE A PUNCHER?
         BE    BAL700
         GETMAIN R,LV=8192         GET STORAGE FOR WORKFIELDS
         LR    WF,R1
         USING TABLE,WF            PROVIDE WORK AREA ADDRESSABILITY
         MVI   BALACCNT,C' '
         MVC   BALACCNT+1(141),BALACCNT CLEAR FIELD FOR ACCOUNT INFO
         SPACE 3
BAL002   CLI   $REGSAVC+19,X'04'   TEST FOR SEPARATOR TYPE
         BE    BAL003
         BL    BAL004
         MVC   BALFORM,=C' END'
         B     BAL005
BAL003   MVC   BALFORM,=C'CONT'
         B     BAL005
BAL004   MVC   BALFORM,=C'STAR'
BAL005   ST    WF,BALFW            SAVE GETMAIN ADDR
         SPACE 3
         MVC   BALJOBNA,JCTJNAME   JOBNAME
         MVC   BALJOBNU,JCTJOBID   JOBNUMBER
         MVC   BALJOBCL,JCTCLASS   JOBCLASS
         MVC   BALINDEV,JCTINDEV   INPUT DEVICE NAME
         MVC   BALPGMR,JCTPNAME    PROGRAMMER NAME
         SPACE 1
         MVC   BALRDATI,JCTRDRON   DATE/TIME READER START
         LM    R0,R1,JCTRDRON
         BAL   WE,BALDATE
         MVC   BALRDATE,BALSAVD    DATE (DD.MM.YY)
         MVC   BALRJDAT,BALSAVJ    DATE (YY.DDD)
         MVC   BALRTIME,BALSAVT    DATE (HH:MM:SS)
         MVC   BALSDATI,JCTXEQON   DATE/TIME EXECUTION START
         LM    R0,R1,JCTXEQON
         BAL   WE,BALDATE
         MVC   BALSDATE,BALSAVD    DATE (DD.MM.YY)
         MVC   BALSJDAT,BALSAVJ    DATE (YY.DDD)
         MVC   BALSTIME,BALSAVT    TIME (HH:MM:SS)
         SPACE 1
         MVC   BALEDATI,JCTXEQOF   DATE/TIME EXECUTION END
         LM    R0,R1,JCTXEQOF
         BAL   WE,BALDATE
         MVC   BALEDATE,BALSAVD    DATE (DD.MM.YY)
         MVC   BALEJDAT,BALSAVJ    DATE (YY.DDD)
         MVC   BALETIME,BALSAVT    TIME (HH:MM:SS)
         SPACE 1
         MVC   BALACCNT(80),JCTXWRK     USER ACCOUNT INFORMATION
         SPACE 3
         L     WE,16               GET CVT ADDRESS
         SH    WE,=H'6'            ADDRESS CVT - 6
         UNPK  BALMODEL(5),0(3,WE) MODEL
         MVC   BALSREL,2(WE)       GET RELEASE
         MVC   BALSVERS,4(WE)      AND VERSION
         SPACE 3
         MVC   BALOUTDV,DCTDEVN    OUTPUT DEVICE NAME
         SPACE 3
         TIME  BIN                 GET CURRENT DATE AND TIME
         STM   R0,R1,BALPDATI
         BAL   WE,BALDATE
         MVC   BALPDATE,BALSAVD    DATE (DD.MM.YY)
         MVC   BALPJDAT,BALSAVJ    DATE (YY.DDD)
         MVC   BALPTIME,BALSAVT    TIME (HH:MM:SS)
         SPACE 3
         L     R0,BALEDATI         GET END TIME
         L     R1,BALSDATI         GET START TIME
         CR    R0,R1               COMPARE FOR DATE CHANGE
         BNL   BAL010
         L     R15,=F'8640000'     ADD ONE DAY
         AR    R0,R15              TO END TIME
BAL010   SR    R0,R1               ELAPSED TIME
         ICM   R1,8,=X'01'         SET INDICATOR FOR TIME ONLY
         BAL   WE,BALDATE
         MVC   BALELTIM,BALSAVT    STORE ELAPSED TIME
         SPACE 3
         L     R1,PWKJOE           PICK UP WORK-JOE ADDRESS
         USING JOEDSECT,R1         ACTIVATE JOE ADDRESSABILITY
         MVC   BALOUTCL,JOECURCL   SYSOUT CLASS
         DROP  R1
         EJECT
BAL500   EQU   *
         ST    R13,BALSAVE1+4
         LA    R13,BALSAVE1
         MVI   BALFW,X'80'
         LA    R1,BALFW
         L     R15,=V(HASPBAL0)
         BALR  R14,R15
         L     R13,BALSAVE1+4
         MVI   BALTXT,C' '
         MVC   BALTXT+1(132),BALTXT
         LA    WC,BALZEIL
         L     WD,BALLCNT
         BCT   WD,BAL550
         B     BAL800
BAL550   MVI   BALTXT,X'8B'        SET EJECT CCW
         $PRPUT WAIT=YES,DATA=BALTXT,LEN=2,CC=M
BAL600   MVC   BALTXT(120),0(WC)
         LR    R1,WB
         $PRPUT WAIT=YES,DATA=BALTXT,LEN=120
         LA    WC,120(WC)
         BCT   WD,BAL600
         CLC   BALFORM,=C' END'
         BE    BAL800
         MVC   BALFORM,=C' END'
         MVI   BALTXT,X'8B'        SET EJECT CCW
         $PRPUT WAIT=YES,DATA=BALTXT,LEN=2,CC=M
         LA    WC,BALZEIL
         L     WD,BALLCNT
         BCT   WD,BAL600
BAL700   EQU   *                   PUNCH SEPARATOR
         CLI   $REGSAVC+19,X'08'
         BE    BAL900
         MVI   BALPCH,X'6A'
         MVC   BALPCH+1(9),BALPCH
         LA    R1,BALPCH+40
         MVC   BALWORK3(4),JCTJOBID+4
         SLR   WC,WC
BAL710   MVI   0(R1),X'6A'
         IC    R0,BALWORK3(WC)
         STC   R0,1(,R1)
         OI    1(R1),X'30'
         CLI   1(R1),X'F0'
         BH    BAL730
         BE    BAL720
         MVI   1(R1),X'EA'
BAL720   XI    1(R1),X'E0'
BAL730   XI    1(R1),X'60'
         MVC   2(7,R1),1(R1)
         MVI   9(R1),X'6A'
         LA    R1,10(R1)
         LA    WC,1(WC)
         CL    WC,=F'4'
         BL    BAL710
         MVI   BALPCH+10,C' '
         MVC   BALPCH+11(29),BALPCH+10
         MVC   BALPCH+11(8),JCTJNAME
         MVC   BALPCH+21(8),JCTPNAME
         $PRPUT WAIT=YES,DATA=BALPCH,LEN=80
         B     BAL900
BAL800   EQU   *
         LR    R1,WF
         FREEMAIN R,LV=8192,A=(1)
BAL900   EQU   *
         LA    R15,8
BALRET   EQU   *
         $STORE R15
         LA    R15,8
         $RETURN RC=(R15)
         EJECT
*
*        BALDATE   BALDATE - SUBROUTINE TO FORMAT DATE TO BALOISE-FORM
*
*        INPUT     R0    TIME BINARY
*                  R1    XXYYDDDC
*                        ×× =========> 00 TIME + DATE
*                                      01 TIME
*
*        OUTPUT     BALSAVD:       'DD.MM.YY' (DATE)
*                   BALSAVT:       'HH.MM.SS' (TIME)
*                   BALSAVJ:       'YY.DDD'   (DATE)
*
         SPACE 3
BALDATE  DS    0H
         ST    R1,BALWORK3+4        SAVE DATE
         XC    BALTIME,BALTIME     CLEAR TIME
         CLI   BALWORK3+4,X'01'     TIME ONLY?
         BE    *+10                YES SKIP DATE VALIDATION
         TM    BALWORK3+7,X'0C'     VALID DATE?
         BNOR  WE                  RETURN IF NOT
         SRDL  R0,32               DIVIDE TIME
         D     R0,=F'100'          AND GET SECONDS
         CVD   R1,BALWORK4          CONVERT TO DECIMAL
         DP    BALWORK4,=P'60'      DIVIDE SECONDS
         MVC   BALTIME,=X'4021202040212020'
         ED    BALTIME+4(4),BALWORK4+6 EDIT SECONDS
         MVC   BALSAVT+6(2),BALTIME+6 END SET INTO FIELD
         MVI   BALSAVT+5,C':'
         MVC   BALTIME+4(4),BALTIME SET MASK
         DP    BALWORK4(6),=P'60'   DIVIDE MINUTES
         ED    BALTIME+4(4),BALWORK4+4 EDIT MINUTES
         MVC   BALSAVT+3(2),BALTIME+6 END SET INTO FIELD
         MVI   BALSAVT+2,C':'
         MVC   BALTIME+4(4),BALTIME SET MASK
         ED    BALTIME+4(4),BALWORK4+2 EDIT HOURS
         MVC   BALSAVT(2),BALTIME+6 END SET INTO FIELD
         CLI   BALWORK3+4,X'01'     TIME ONLY?
         BER   WE                  RETURN IF YES
         SPACE 2
         L     R1,BALWORK3+4        LOAD DATE
         ICM   R1,8,=X'00'         CLEAR INDICATOR
         ST    R1,BALWORK3+4
         UNPK  BALSAVJ+1(5),BALWORK3+5(3)     EDIT
         MVC   BALSAVJ(2),BALSAVJ+1     JULIAN FORM
         MVI   BALSAVJ+2,C'.'      OF DATE
         XC    BALWORK2(9),BALWORK2 CLEAR DATE
         SPACE 2
         MVC   BALTIME(12),BALMONTH COPY DATE CONVERSION TABLE
         TM    BALWORK3+5,X'01'    ADJUST
         BO    BALD10               TABLE
         TM    BALWORK3+5,X'12'      ON
         BM    BALD10                 LEAP
         MVI   BALTIME+1,29            YEARS
         SPACE 1
BALD10   MVC   BALWORK4+5(3),=X'402120' GET PATTERN
         ED    BALWORK4+6(2),BALWORK3+5 AND EDIT THE YEAR (YY)
         XC    BALWORK3(6),BALWORK3     CLEAR ALL BUT JULIAN DAY
         SLR   R0,R0               CLEAR FOR IC
         CVB   R1,BALWORK3         CONVERT TO BINARY
         LA    WC,BALTIME-1
         LA    WD,0
         SPACE 1
BALD20   SLR   R1,R0               CONVERT
         LA    WC,1(,WC)            JULIAN DAY
         LA    WD,1(,WD)             TO
         IC    R0,0(,WC)              STANDARD DAY
         CLR   R0,R1                   *
         BL    BALD20                   (R1)
         CVD   R1,BALWORK3         CONVERT TO DECIMAL DAY
         UNPK  BALWORK4(2),BALWORK3+6(2)     PLACE DAY DD
         OI    BALWORK4+1,X'F0'
         CVD   WD,BALWORK3         CONVERT TO DECIMAL MONTH
         UNPK  BALWORK4+3(2),BALWORK3+6(2)   PLACE MONTH MM
         OI    BALWORK4+4,X'F0'
         MVI   BALWORK4+2,C'.'
         MVI   BALWORK4+5,C'.'
         MVC   BALSAVD,BALWORK4
         BR    WE
         SPACE 5
BALMONTH DC    AL1(31),AL1(28),AL1(31),AL1(30)
         DC    AL1(31),AL1(30),AL1(31),AL1(31)
         DC    AL1(30),AL1(31),AL1(30),AL1(31)
BALPCH   DC    CL133' '
         SPACE 2
         LTORG
         TITLE 'BALOISE COMMON HASP AREA'
TABLE    DSECT
BALFW    DS    F
BALJOBNA DS    CL8
BALJOBNU DS    CL8
BALJOBCL DS    CL1
BALOUTCL DS    CL1
BALMODEL DS    CL4
BALSREL  DS    CL2
BALSVERS DS    CL2
BALOUTDV DS    CL8
BALINDEV DS    CL8
BALPGMR  DS    CL20
         DS    0F
BALRDATI DS    XL8
BALRDATE DS    CL8
BALRJDAT DS    CL6
BALRTIME DS    CL8
         DS    0F
BALPDATI DS    XL8
BALPDATE DS    CL8
BALPJDAT DS    CL6
BALPTIME DS    CL8
         DS    0F
BALSDATI DS    XL8
BALSDATE DS    CL8
BALSJDAT DS    CL6
BALSTIME DS    CL8
         DS    0F
BALEDATI DS    XL8
BALEDATE DS    CL8
BALEJDAT DS    CL6
BALETIME DS    CL8
BALELTIM DS    CL8
BALCPU   DS    CL8
BALACCNT DS    CL142
BALFORM  DS    CL4
BALWORK  DS    CL12
BALWORK1 DS    CL12
BALFWWRK DS    3F
BALRES   DS    CL40
BALLADDR DS    F
BALLCNT  DS    F
BALSAVE  DS    18F
BALSAVE1 DS    18F
BALZEIL  DS    47CL120
BALBLOCK DS    34CL40
         SPACE 5
BALTXT   EQU   BALACCNT,133
BALTIME  EQU   BALACCNT+80,8       TIME FOR WORK
BALSAVD  EQU   BALACCNT+92,8       DATE (DD.MM.YY)
BALSAVT  EQU   BALACCNT+110,8      TIME (HH:MM:SS)
BALSAVJ  EQU   BALACCNT+118,6      DATE JULIAN FORM
BALWORK2 EQU   BALACCNT+80,9       WORK FIELD 2
BALWORK3 EQU   BALACCNT+126,8      WORK FIELD 3
BALWORK4 EQU   BALACCNT+134,8      WORK FIELD 4
USERSEP1 CSECT
         TITLE 'COMPLETE MODULE INFORMATION TABLE'
         $MODEND
         TITLE 'COMPUTE LENGTHS FOR PRIMARY CSECT'
$DLENGTH $DLENGTH ,
         END
