         MACRO
&NAME FULLSCRN &DEF=NO,&IO=FULLSC$,&CLEAR=NO,&BLINK=NO,&REG=NO,        *
               &INIT=NO,&READ=NO,&ASIS=NO
         GBLA  &A0FULL
         GBLA  &A1FULL
         GBLA  &A2FULL
         GBLC  &IOAREAF
         AIF   ('&DEF' EQ 'YES').DEF      . FOR DEFINITIONS?
         AIF   ('&READ' NE 'NO').READ     .
         AIF   ('&INIT' EQ 'NO').NOINIT   .
&IOAREAF SETC  '&IO'                      . SET I/O AREA
&A0FULL  SETA  &A0FULL+1                  .
&NAME    STFSMODE INITIAL=YES             .
         LTR   R15,R15                    . VTAM?
         BNZ   *+14                       .
         MVC   FULLSCR0(12),=X'03030303030303C11140401D'  . VTAM
         B     *+10                       .
         MVC   FULLSCR0(12),=X'C1115D7F1140403C4040001D'  . TCAM
         AGO   .MEND                      . INITIALISATION DONE
.NOINIT  ANOP                             .
         AIF   (&A0FULL EQ 0).MEND        . FIRST CALL ?
&A2FULL  SETA  &A2FULL+1                  .
         AIF   ('&BLINK' EQ 'YES').BLINK  . BLINKING?
&NAME    MVI   FULLSCR0+12,X'C9'          . NOBLINKING
         AGO   .CONTINU                   .
.BLINK   ANOP                             .
&NAME    MVI   FULLSCR0+12,X'05'          . BLINKING
.CONTINU ANOP                             .
         AIF   ('&CLEAR' NE 'YES').NOCLEAR  FOR CLEAR SCREEN?
         LA    R1,FULLSCRC                . BEGIN COMMAND STRING
         LA    R0,14                      . A(BUFFER LENGTH)
         MVC   FULLSCRC(14),=X'C1115D7E1140403C4040001DC913'
         AGO   .TPUT                      .
.NOCLEAR ANOP                             .
         AIF   ('&REG' NE 'NO').REGS      . REGISTER TPUT?
         LA    R1,FULLSCRB                . BEGIN COMMAND STRING
         MVC   FULLSCRB,FULLSCR0          .
.NOREGS  ANOP                             .
         LA    R0,FULLSCRL                . A(BUFFER LENGTH)
         LR    R15,R1                     .
         LA    R15,1932(R15)              .
         MVC   0(4,R15),=X'1140C113'      .
         AGO   .TPUT                      .
.REGS    ANOP                             .
         AIF   ('&REG' EQ '1').REGS1      .
         LR    R1,&REG                    .
.REGS1   ANOP                             .
         MVC   0(13,R1),FULLSCR0          .
         AGO   .NOREGS                    .
.TPUT    ANOP                             .
         CLI   0(R1),X'03'                . VTAM?
         BNE   *+12                       . NO
         SH    R0,=H'7'                   . TCAM LENGTH-7
         LA    R1,7(R1)                   . BUFFER ADDRESS+7
         TPUT  (1),(0),FULLSCR,WAIT,HOLD  . DO IT
         AGO   .MEND                      .
.READ    ANOP                             .
         AIF   ('&REG' EQ '1').REGS2      .
         LA    R1,&IOAREAF                .
         AIF   ('&REG' EQ 'NO').REGS2     . REGISTER TPUT?
         LR    R1,&REG                    .
.REGS2   ANOP                             .
         SR    0,0                        .
         SR    15,15                      .
         LA    15,128                     .
         AIF   ('&ASIS' EQ 'NO').REGS3    . ASISS REQUESTED?
         LA    15,129                     .
.REGS3   ANOP                             .
         SLL   15,24                      .
         OR    1,15                       .
         LA    0,&READ                    .
         TGET  (1),(0),R                  .
         TCLEARQ INPUT                    .
         AGO   .MEND                      .
.DEF     ANOP                             .
         AIF   (&A1FULL GT 0).MEND        . ERROR
&A1FULL  SETA  1                          . INDICATE DEFINITIONS DONE
FULLSCRC DS    XL14                       . CLEAR STRING VTAM/TCAM
FULLSCR0 DS    XL13                       . VTAM/TCAM COMMAND STRING
FULLSCRB DS    XL13                       . I/O AREA FOR COMMAND
         AIF   ('&REG' NE 'NO').OVREGS    .
&IOAREAF DS    XL1923                     . DATA
         DS    CL120
.OVREGS  ANOP                             .
FULLSCRL EQU   1936                       . COMMAND+BUFFER LENGTH
.MEND    MEND
