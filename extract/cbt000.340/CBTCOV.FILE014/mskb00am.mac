* ./  NAME=BEGIN
         MACRO
&NAME    BEGIN &AREA=,                                                 C
               &BASE1=RG9,                                             C
               &BASE2=,                                                C
               &BASE3=,                                                C
               &BASE4=,                                                C
               &BEGIN=,                                                C
               &CNAME=,                                                C
               &DATUM=NO,                                              C
               &END=,                                                  C
               &ENTRY=,                                                C
               &EP=,                                                   C
               &FLAG=NO,                                               C
               &LEVEL=,                                                C
               &MSG=NO,                                                C
               &PARAM=NO,                                              C
               &PARAMR=,                                               C
               &PARM=NO,                                               C
               &PCHECK=NO,                                             C
               &PRINT=NOGEN,                                           C
               &REGEQ=NO,                                       SGKB   C
               &SETEQ=NO,                                       SGKB   C
               &SAVE=(14,12),                                          C
               &VERSION=,                                              C
               &ZEIT=NO
*********************************************************************01
*/*BEGIN       PROGRAMM-/MODULE-BEGINN                               01
*                                                                    01
*1 FUNCTION    INITALISIERUNG DES PROGRAMMS + EINIGE SERVICE-        01
*              FUNKTIONEN                                            01
*                                                                    01
*2 SUMMARY                                                           01
*  TYPE        MACRO     BATCH                                       01
*  ACCOUNT     09                                                    01
*  EXTERNAL    P1301300                 SYSTEM-DATUM/-ZEIT           01
*              P1302600                 TEST-HILFE-ROUTINE ASS.-PROG.01
*  MACRO       EQURE                    SGKB-REGISTER-EQUATES        01
*  HISTORY     1977.01   HB   GRUNDVERSION                           01
*              1979.05   LA   EINBAU STANDARD-PROGRAMM-DOKUMENTATION;01
*                             PTR AUF MAIN-ENTRY BEI 2ND             01
*                             ENTRY-POINTS; FLAG-PARAM. INAKTIVIERT  01
*              1982.05   HE   ANPASSUNG MVS 3.8                      02
*                                                                    01
*3 USAGE       SIEHE PROGRAMMIER-RICHTLINIEN, HAUPTKAPITEL 08        01
**/                                                                  01
*********************************************************************01
         LCLA  &NMOD,&NMOD1,&NMOD2,&NP,&NP1,&NP2
         LCLC  &MODN,&P,&MODN1
         LCLC  &DAY,&MONTH,&YEAR,&HOURS,&MINUTES,&DATE,&TIME
         LCLC  &GNAME
&GNAME   SETC  'IHB'.'&SYSNDX'
         AIF   (T'&NAME NE 'O').A0
         MNOTE 2,'PROGRAMM-NAME FEHLT. GENERIERUNG ABGEBROCHEN'
         MEXIT
.A0      ANOP
         AIF   (T'&BASE3 EQ 'O').A1
         AIF   (T'&BASE2 NE 'O').A1
         MNOTE 2,'WENN 3.BASISREG VERLANGT,MUSS AUCH 2.BASISREG SPEZIFI-
               ZIERT WERDEN. GENERIERUNG ABGEBROCHEN'
         MEXIT
.A1      ANOP
         AIF   ('&PARAM' EQ 'NO').A2
         AIF   (T'&PARAMR NE 'O').A2
         MNOTE 2,'PARAM-REGISTER NICHT SPEZIFIZIERT. GENERIERUNG ABGEBR-
               OCHEN'
         MEXIT
.A2      ANOP
         AIF   ('&PARM' EQ 'NO').A5
         AIF   ('&PARAM' EQ 'NO').A5
         MNOTE 2,'WENN PARM=YES, MUSS PARAM = NO SEIN'
         MEXIT
.A5      ANOP
         AIF   (T'&ENTRY EQ 'O').A7
&NAME    CSECT
         USING *,RG15                   USE 15 AS INTERMEDIATE BASE
         SAVE  &SAVE,,*
.*                                                                   01
.*       LA    15,0(0,15)               CLEAR HIGH-ORDER-BYTE FOR    02
.*                                      INDICATING PRIMARY ENTRY-PNT 02
         XR    RG14,RG14                SET FLAG PRIMARY ENTRY-POINT
         B     &GNAME.1                 BRANCH AROUND 2ND ENTRY-PNTS 01
&NMOD    SETA  N'&ENTRY
&NMOD1   SETA  1
&NMOD2   SETA  0
.A6      ANOP
         ENTRY &ENTRY(&NMOD1)
&NMOD2   SETA  &NMOD1+1
         AIF   (&NMOD2 GT &NMOD).A61
&NMOD1   SETA  &NMOD2
         AGO   .A6
.A61     ANOP
&NMOD    SETA  N'&ENTRY
&NMOD1   SETA  1
&NMOD2   SETA  0
.A62     ANOP
         CNOP  0,4                                                   01
&ENTRY(&NMOD1) EQU *
         B     (&GNAME.0-&ENTRY(&NMOD1))(15) BRANCH TO REG.-SAVE     01
         DC    1A(&NAME)                PTR AUF PRIMARY ENTRY        01
.*                                                                   02
.*       B     10(15)                   BRANCH AROUND CONSTANTES     02
.*       DC    1A(&NAME)                PTR AUF PRIMARY ENTRY        02
.*       DC    1XL01'&NMOD1'            NB OF ENTRY                  02
.*       DC    1XL01'00'                FILLER                       02
.*       ICM   15,B'1000',8(15)         ENTRY-# IN HIGH-ORDER-BITS   02
.*       B     (&GNAME.0-&ENTRY(NMOD1))(15) BRANCH TO REG.-SAVE      02
.*                                                                   02
&NMOD2   SETA  &NMOD1+1
         AIF   (&NMOD2 GT &NMOD).A8
&NMOD1   SETA  &NMOD2
         AGO   .A62
.A7      ANOP
&NAME    CSECT
         SAVE  &SAVE,,*
.A8      ANOP
         SPACE 4
*
* REGISTER-VERWENDUNG
*
         SPACE 3
RGNL     EQU   0
RG1      EQU   1                        PARAMETER-ADDR AUS/FUER CALL
RG2      EQU   2
RG3      EQU   3
RG4      EQU   4
RG5      EQU   5
RG6      EQU   6
RG7      EQU   7
RG8      EQU   8
RG9      EQU   9                        DEFAULT BASIS 1
RG10     EQU   10                       DEFAULT BASIS 2
RG11     EQU   11                       DEFAULT BASIS 3
RG12     EQU   12
RG13     EQU   13                       SAVE-AREA-ADDRESS
RG14     EQU   14                       RUECKKEHR AUS SUBROUTINEN/CALL
RG15     EQU   15                       ENTRY-POINT-ADRESSE BEI CALL
         SPACE 3
* PRINT-CONTROL-CHARACTERS
         SPACE 2
WNSP     EQU   X'01'                    WRITE/NO SPACE
WSP1     EQU   X'09'                    WRITE/SPACE ONE LINE
WSP2     EQU   X'11'                    WRITE/SPACE TWO LINES
WSP3     EQU   X'19'                    WRITE/SPACE THREE LINES
SP1      EQU   X'0B'                    SPACE ONE LINE IMMEDIATELY
SP2      EQU   X'13'                    SPACE TWO LINES IMMEDIATELY
SP3      EQU   X'1B'                    SPACE THREE LINES IMMEDIATELY
WSK1     EQU   X'89'                    WRITE/SKIP TO CHANNEL ONE
SK1      EQU   X'8B'                    SKIP TO CHANNEL ONE IMMEDIATELY
         AIF   ('&SETEQ' EQ 'NO').ENDSET                             02
S00      EQU   X'00'
S01      EQU   X'01'
S02      EQU   X'02'
S04      EQU   X'04'
S08      EQU   X'08'
S10      EQU   X'10'
S20      EQU   X'20'
S40      EQU   X'40'
S80      EQU   X'80'
R01      EQU   X'FE'
R02      EQU   X'FD'
R04      EQU   X'FB'
R08      EQU   X'F7'
R20      EQU   X'DF'
R40      EQU   X'BF'
R80      EQU   X'7F'
RFF      EQU   X'FF'
.ENDSET  ANOP 02
         AIF   ('&REGEQ' EQ 'NO').A81
         SPACE 3
         EQURE
.A81     ANOP
         SPACE 4
         AIF   (T'&ENTRY EQ 'O').A89
.*                                                                   01
         DS    0H                                                    01
&GNAME.0 EQU   *                                                     01
         SAVE  &SAVE
.*                                                                   01
         DROP  RG15                     DROP INTERMEDIATE BASE REG.
.A89     ANOP
&GNAME.1 EQU   *
         AIF   (T'&BASE2 EQ 'O').F
         AIF   (T'&BASE3 EQ 'O').D
         AIF   (T'&BASE4 EQ 'O').E
         USING *,&BASE1,&BASE2,&BASE3,&BASE4
         BALR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         LR    &BASE2,&BASE1
         A     &BASE2,=F'4096'
         LR    &BASE3,&BASE2
         A     &BASE3,=F'4096'
         LR    &BASE4,&BASE3
         A     &BASE4,=F'4096'
         AGO   .I
.E       ANOP
         USING *,&BASE1,&BASE2,&BASE3
         BALR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         LR    &BASE2,&BASE1
         A     &BASE2,=F'4096'
         LR    &BASE3,&BASE2
         A     &BASE3,=F'4096'
         AGO   .I
.*
.D       ANOP
         USING *,&BASE1,&BASE2
         BALR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         LR    &BASE2,&BASE1
         A     &BASE2,=F'4096'
         AGO   .I
.*
.F       ANOP
         USING *,&BASE1
         BALR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
         BCTR  &BASE1,RGNL
.*
.I       ANOP
&DAY     SETC  '&SYSDATE'(4,2)
&MONTH   SETC  '&SYSDATE'(1,2)
&YEAR    SETC  '&SYSDATE'(7,2)
&HOURS   SETC  '&SYSTIME'(1,2)
&MINUTES SETC  '&SYSTIME'(4,2)
&DATE    SETC  '&DAY'.'.'.'&MONTH'.'.'.'&YEAR'
&TIME    SETC  '&HOURS'.'.'.'&MINUTES'
         B     &GNAME.2                                              01
         DC    C' ASSEMBLY DATE: ',CL8'&DATE',C'/TIME: ',CL8'&TIME'
         DC    C' '
         AIF   (T'&LEVEL EQ 'O').I5
         DC    C' LEVEL: ',CL3'&LEVEL',C' '
         AIF   (T'&VERSION EQ 'O').I5
         DC    C' VERSION: ',CL3'&VERSION',C' '
.I5      ANOP
         CNOP  0,4
&GNAME.2 EQU   *                                                     01
.*       AIF   (T'&ENTRY EQ 'O').A250                                02
.*       STCM  15,B'1000',PRIMFLAG      SAVE ENTRY-# FROM REG. 15    02
.* .A250 ANOP                                                        02
         ST    RG13,SAVE+4
         LR    RG15,RG13                SAVE-AREA-CHAINING
         LA    RG13,SAVE
         ST    RG13,8(RG15)
         AIF   ('&PARM' EQ 'NO').K1
         L     RG2,0(RG1)               GET ADDR OF PARM-FIELD
         LH    RG1,0(RG2)               GET LENGTH OF PARM-FIELD
         LTR   RG1,RG1                  DO NOT MOVE PARM IF
         BZ    &GNAME.A                 ZERO LENGTH
         BCTR  RG1,RGNL                 LAENGE MINUS EINS FUER MVC
         EX    RG1,MOVE                 PARM-FELD IN ZWISP
         AGO   .M
.K1      ANOP
         AIF   ('&PARAM' EQ 'NO').M
         AIF   ('&PARAM' NE '1').L
         L     &PARAMR,0(RG1)
         ST    &PARAMR,APARAM
         AGO   .M                       P
.L       ANOP
         LM    &PARAMR(1),&PARAMR(2),0(RG1)
         STM   &PARAMR(1),&PARAMR(2),APARAM
.M       ANOP
.*                                                                   02
.*       DIE NAECHSTEN 6 ZEILEN KOENNEN BEI LEVEL 02 ENTFERNT WERDEN 02
.*                                                                   02
         AIF   (T'&ENTRY EQ 'O').N                                   01
         NI    PRIMFLAG,X'00'           INDICATE "SECOND.ENTRY-POINT"
         LTR   RG14,RG14
         BNZ   &GNAME.A                 OK/SECONDARY ENTRY-POINT TAKEN
         OI    PRIMFLAG,X'FF'           INDICATE "PRIMARY ENTRY-POINT"
.N       ANOP
&GNAME.A NOP   &GNAME.B                 1ST-TIME-SWITCH
         OI    &GNAME.A+1,X'F0'         OFF
.*
.P       ANOP
         AIF   (T'&EP EQ 'O').P3
&NMOD    SETA  N'&EP
&NMOD1   SETA  1
&NMOD2   SETA  0
.P1      ANOP
         LOAD  EP=&EP(&NMOD1)
&MODN    SETC  '&EP(&NMOD1)'
&MODN1   SETC  '&EP(&NMOD1)'(2,7)
         ST    RGNL,A&MODN1
&NMOD2   SETA  &NMOD1+1
         AIF   (&NMOD2 GT &NMOD).P3
&NMOD1   SETA  &NMOD2
         AGO   .P1
.P3      ANOP
         AIF   ('&PCHECK' NE 'YES').P6
         AIF   (T'&BEGIN EQ 'O').P4
         AIF   (T'&END NE 'O').P5
.P4      ANOP
         MNOTE 2,'WENN "PCHECK=YES" MUESSEN "BEGIN=" UND "END=" DEFINIE+
               RT WERDEN / GENERATION TERMINATED.'
         MEXIT
.P5      ANOP
         LOAD  EP=P1302600
         LR    RG15,RGNL
         CALL  (15),(&BEGIN,&END)
.P6      ANOP
         AIF   ('&DATUM' EQ 'NO').P7
         LOAD  EP=P1301300
         ST    RGNL,A1301300
         LR    RG15,RGNL
         CALL  (15),(PDATE,DATUM)
.P7      ANOP
         AIF   ('&ZEIT' EQ 'NO').P74
         AIF   ('&DATUM' NE 'NO').P72
         LOAD  EP=P1301300              LOAD DATE-/TIME-MODULE
         ST    RGNL,A1301300
.P72     ANOP
         L     RG15,A1301300
         CALL  (15),(PZEIT,ZEIT)        GET SYSTEM TIME
.P74     ANOP
         B     &GNAME.B
         SPACE 4
SAVE     DC    18F'0'
         AIF   ('&PARM' EQ 'NO').Q
         AIF   ('&PARM' NE 'YES').P8
MOVE     MVC   &AREA.(1),2(RG2)         MOVE PARM-FIELD
         AGO   .Q
.P8      ANOP
MOVE     MVC   &PARM.(1),2(RG2)         MOVE PARM-FIELD
.Q       ANOP
.S       ANOP
         AIF   (T'&EP EQ 'O').V
&NMOD    SETA  N'&EP
&NMOD1   SETA  1
&NMOD2   SETA  0
.T       ANOP
&MODN    SETC  '&EP(&NMOD1)'
&MODN1   SETC  '&EP(&NMOD1)'(2,7)
A&MODN1  DC    F'0'
&NMOD2   SETA  &NMOD1+1
         AIF   (&NMOD2 GT &NMOD).V
&NMOD1   SETA  &NMOD2
         AGO   .T
.V       ANOP
         AIF   ('&PARAM' EQ 'NO').X
APARAM   DS    0F
&NP      SETA  &PARAM
&NP1     SETA  1
&NP2     SETA  0
.W       ANOP
&P       SETC  '&NP1'
APARAM&P DC    F'0'
&NP2     SETA  &NP1+1
         AIF   (&NP2 GT &NP).X
&NP1     SETA  &NP2
         AGO   .W
.X       ANOP
         AIF   ('&DATUM' EQ 'NO').X1
         CNOP  2,8
PDATE    DC    CL2'&DATUM'              STEUER-INFO FUER P1301300
DATUM    DC    CL8' '                   SYSTEM-DATUM
.X1      ANOP
         AIF   ('&ZEIT' EQ 'NO').X3
PZEIT    DC    CL2'&ZEIT'               PARAM 1
ZEIT     DC    CL11' '                  SYSTEM-ZEIT
         AGO   .X4
.X3      ANOP
         AIF   ('&DATUM' EQ 'NO').X5
.X4      ANOP
A1301300 DC    F'0'                     ZWISP EP-ADDR P1301300
.X5      ANOP
         AIF   (T'&ENTRY EQ 'O').X7                                  01
.*                                                                   01
PRIMFLAG DC    X'00'                    SWITCH "PRIM./SEC.ENTRY-POINT"
.X7      ANOP
         SPACE 4
         LTORG
         SPACE 3
         CNOP  0,4
&GNAME.B EQU   *
         SPACE 4
         PRINT &PRINT
         MEND
