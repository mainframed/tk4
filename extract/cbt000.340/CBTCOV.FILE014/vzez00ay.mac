//Z00302  JOB  (000000,00,0,000),KREBS,CLASS=S,MSGCLASS=X,
//        NOTIFY=Z00302
//*
//*
//*   -----> THIS JOB MUST NOT BE RENUMBERED    ××××××××××××××××××××
//*   -----> THIS JOB MUST NOT BE RENUMBERED    ××××××××××××××××××××
//*
//*
//*          WHEN ENTERING SPF-EDIT-MODE IMMEDIATLY ENTER  :
//*          ----------------------------------------------
//*
//*          ==========>    AUTONUM  OFFF
//*
//*
//*          CAUTION : BEFORE RUNNING THIS JOB UPDATE ALL STATEMENTS
//*                    MARKED WITH '/* <========'
//*                    (INSTALLATION DEPENDENT PARAMETERS LIKE TARGET-
//*                    DLIB- AND TARGET-NAME ZONES AS WELL AS  TARGET-
//*                    AND DLIB- LIBRAY-NAMES MUST BE ADAPTED TO YOUR
//*                    INSTALLATION).
//*
//*
//*                    IF CHANGE TO CVT IS NOT ACCEPTED (OR UPDATED
//*                    MANUALLY IN 'SYS1.AMODGEN') RERUN THIS JOB AFTER
//*                    ANY IOGEN (OR YOU WILL END UP WITHOUT VCON FOR
//*                    THE USER CVT).
//*                    ANOTHER WAY TO INCLUDE THE VCON FOR THE USERCVT
//*                    WOULD BE TO CONCATENATE 'SMPMTS' TO THE STAGE 2
//*                    JCL THAT ASSEMBLES 'IEAVBK00'.
//*
//*
//*
//*         THIS JOB WILL MODIFY IEANUC01 TO INCLUDE A USER CVT
//*         (ANCHORED ON CVTUSER).
//*
//*
//*         STEP SMP WILL  ADD     MACRO  ZEDVCVT
//*                        ADD     MACRO  FASTRAC
//*                        ADD     SOURCE ZEDVCVT
//*                        ADD     SOURCE IGC0025B
//*                        ADD     SOURCE FASTRAC0
//*                        UPDATE  MACRO  CVT.
//*
//*
//*         A NEW NUCLEUS WILL BE THE RESULT OF THIS JOB. THE NEW
//*         NUCLEUS WILL CONTAIN THE USER-CSECT 'ZEDVCVT'.
//*         THE ADDRESS OF 'ZEDVCVT' IS CONTAINED IN 'CVTUSER'.
//*
//*         MODULE IGC0025B (SVC 252) WILL BE LINKED INTO LPALIB1.
//*         MODULE FASTRAC0 (ISPF   ) WILL BE LINKED INTO CMDLIB1.
//*         MACRO  ZEDVCVT            WILL BE COPIED INTO MACLIB1.
//*         MACRO  FASTRAC            WILL BE COPIED INTO MACLIB1.
//*         SOURCE IGC0025B           WILL BE COPIED INTO SAMPLIB.
//*         SOURCE FASTRAC0           WILL BE COPIED INTO SAMPLIB.
//*         SOURCE ZEDVCVT            WILL BE COPIED.INTO SAMPLIB.
//*
//*
//*
//*
//SMP          EXEC    SMPMVS,COND=(0,NE)
//SMPPTFIN     DD      DATA,DLM='??'
++ USERMOD (IPOCVT0) .                            /* <=========== */
++ VER (Z038) FMID(JBB2110) PRE(UZ70706).         /* <=========== */
++MACUPD (CVT)     DISTLIB    (AMODGEN)
                   ASSEM      (IEAVBK00
                               ZEDVCVT           /* <============ */
                              )
                   DISTSRC    (SAMPLIB).         /* <============ */
./ CHANGE NAME=CVT
CVTUSER  DC    V(ZEDVCVT)    ZEDV USER CVT ADDRESS.
./ ENDUP
++MAC(FASTRAC)     SYSLIB     (MACLIB1)          /* <============= */
                   ASSEM      (FASTRAC0)         /* <============= */
                   DISTSRC    (SAMPLIB)          /* <============= */
                   DISTMOD    (SERVLINK)        /* <============= */
                   DISTLIB    (ASAMPLIB).        /* <============= */
         MACRO
&N       FASTRAC &O,&MF=I
*---------------------------------------------------------------------*
*                                                                     *
*        INVOKE ZEDV FRACHECK-PROCESSING                              *
*                                                                     *
*        INPUT  R1  :  POINTER TO A LIST OF 4 ADDRESS-WORDS .         *
*                                                                     *
*                      0   FUNCTION         ( AL4 )                   *
*                      4   RESOURCE-CLASS   ( CL8 )                   *
*                      8   RESOURCE         ( CL8 )                   *
*                     12   USERID           ( CL8 )                   *
*                                                                     *
*        RC     R15 :  0   ACCESS ALLOWED                             *
*                      4   RESOURCE UNDEFINED                         *
*                      8   ACCESS FAILED                              *
*                     12   RESOURCE-CLASS DISABLED                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         GBLB  &IHBSWA
         AIF   (T'&O EQ 'O').SVC
         AIF   ('&O' EQ '(1)').SVC
         AIF   ('&O' EQ '(R1)').SVC
         AIF   ('&MF' EQ 'L').A1
         AIF   ('&MF' EQ 'E').A0
         AIF   ('&O'(1,1)  EQ '(').A0
         AIF   (K'&O GT 5).A1
.A0      ANOP
         LR    R1,&O
         AGO   .SVC
.A1      ANOP
&IHBSWA  SETB  1
         AIF   ('&MF' NE 'L').NOLAB
&N       DS    0A
.NOLAB   ANOP
         IHBOPLST ,&O,MF=&MF
.SVC     ANOP
         AIF   ('&MF' EQ 'L').EXIT
         SVC   252
.EXIT    ANOP
         MEND
++MAC(ZEDVCVT)     SYSLIB     (MACLIB1)          /* <============= */
                   ASSEM      (ZEDVCVT
                               IGC0025B
                               FASTRAC0
                              )
                   DISTSRC    (SAMPLIB)          /* <============= */
                   DISTMOD    (SERVLINK)        /* <============= */
                   DISTLIB    (ASAMPLIB).        /* <============= */
         MACRO
         ZEDVCVT &DSECT=YES
         AIF     ('&DSECT' EQ 'YES').DSECT
         AIF     ('&DSECT' EQ 'NO').CSECT
         MNOTE   8,'DSECT= OPERAND HAS INVALID VALUE (YES/NO)'
         MEXIT
.CSECT   ANOP
ZEDVCVT  CSECT
         AGO     .OVER
.DSECT   ANOP
ZEDVCVT  DSECT
.OVER    ANOP
.*
.*
         TITLE 'ZEDVCVT  -  USER COMMUNICATIONS VECTOR-TABLE'
*---------------------------------------------------------------------*
*                                                                     *
*        ZEDVCVT , THE USER-CVT RESIDES IN FIXED STORAGE IN THE       *
*                  NUCLEUS-AREA OF MVS.                               *
*                  ZEDVCVT IS CHAINED OFF THE FIELD 'CVTUSER' IN      *
*                  THE CVT.                                           *
*                                                                     *
*        PURPOSE : PROVIDE AN AREA OF WRITE PROTECTED STORAGE THAT    *
*                  IS GLOBALLY ADDRESSABLE.                           *
*                  UPDATE-ACCESS TO ZEDVCVT MUST SERIALIZED USING     *
*                  CS/CDS OR OTHER LOCKING TECHNIQUES.                *
*                                                                     *
*        ACCESS  : NOT FETCH PROTECTED , KEY 0 FOR WRITE.             *
*                                                                     *
*        FUNCTIONS SUPPORTED :                                        *
*                                                                     *
*                  - FASTRAC - ZEDV FRACHECK SUPPORT USING INSTORAGE  *
*                    PROFILES CHAINED OFF THIS CVT.                   *
*                                                                     *
*                  - JES3 CONSOLE FOR TSO USERS                       *
*                                                                     *
*                  - JES3 XM-COMMAND SUPPORT (OBSOLETE)               *
*                    (FUNCTION REPLACED BY SSISERV)                   *
*                                                                     *
*                  - ZEDV CELL-POOL CONTROL                           *
*                                                                     *
*                  - SUPPORT FOR MODREP (REAL-TIME LOADER FOR SVC-    *
*                    AND LPA-MODULES.                                 *
*                                                                     *
*                                                                     *
*---------------------------------------ZEDV/P.KREBS/01FEB85----------*
         SPACE
ZCID     DC    CL8'ZEDVCVT'             IDENTIFIER.
ZCDATE   DC    C'**&SYSDATE**'          DATE OF ASSEMBLY.
ZCTIME   DC    C'**&SYSTIME**'          TIME OF ASSEMBLY.
         TITLE 'FASTRAC - ZEDV FRACHECK SUPPORT'
*---------------------------------------------------------------------*
*                                                                     *
*        FASTRAC - ZEDV FRACHECK PROCESSING                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZCRISPTB DS    A(0)                     INSTORAGE PROFILE TABLE.
         SPACE
ZCRISPTN DC    A(0)                     # OF TABLE ENTRIES.
         SPACE 2
*        REQUEST CODES FOR IGC0025B
         SPACE
ZCRCHECK EQU   0                        CHECK   (ISSUED BY REQUESTOR).
ZCRSTOP  EQU   4                        STOP    (ISSUED BY FASTRAC0).
ZCRREFR  EQU   8                        REFRESH  "
ZCRSTART EQU   12                       START. "
ZCRVER   EQU   16                       VERIFY USERS ACCESS-AUTHORITY
*                                       TO PERFORM STOP,REFRESH,START
*                                       (ISSUED BY FASTRAC0).
         SPACE 2
*        FLAGS
         SPACE
ZCRFLAGS DS    0A                       FASTRAC CHECKING FLAGS.
         SPACE
ZCRFLAG1 DC    AL1(0)                   FASTRAC STATUS FLAG 2.
ZCR1QIP  EQU   128                      CHAIN MANIPULATION IN PROGR.
ZCR1CIP  EQU   64                       CHECK IN PROGRESS.
*              32
*              16
*              8
*              4
*              2
*              1
         DC    AL3(0)
         SPACE
         DS    0A
ZCRQLOCK DC    AL1(ZCR1QIP),AL3(0)      LOCKWORD FOR INSTORAGE-PROFILE
*                                       CHAIN.
ZCRPLOCK DC    AL1(ZCRISPIP),AL3(0)     LOCKWORD FOR INSTORAGE-PROFILE
*                                       ENTRY.
         DC    A(0)                     RESERVED.
         DC    A(0)                     RESERVED.
         DC    A(0)                     RESERVED.
         DC    A(0)                     RESERVED.
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*        SUPER-SPY.ADDRESS OF JES3-MESSAGE-FLOW BUFFER                *
*        UPDATED IN WRAP-AROUND-MODE BY IATUX31.                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZIAMBUF  DC    A(0)                     ADDR. JES3 MASTER-CONSOLE BUFF.
ZIACURR  DC    A(0)                     ADDR. CURRENT ENTRY.
ZIAMBUFL EQU   ZIANBUF*ZIAENTL          SIZE OF BUFFER.
         SPACE 2
ZIANBUF  EQU   43-2                     NUMBER OF ENTRIES IN BUFFER
*                                       (-2 FOR COMMAND LINES).
ZIAENTL  EQU   79+4                     LENGTH OF ENTRY.
         DC    A(0)                     RESERVED.
         SPACE 3
*---------------------------------------------------------------------*
*                                                                     *
*        CROSS-MEMORY COMMAND SCHEDULING (JES3 ONLY)                  *
*                                                                     *
*                                                                     *
*        OBSOLETE ..... REPLACED BY SSISERV                           *
*        OBSOLETE ..... REPLACED BY SSISERV                           *
*        OBSOLETE ..... REPLACED BY SSISERV                           *
*        OBSOLETE ..... REPLACED BY SSISERV                           *
*        OBSOLETE ..... REPLACED BY SSISERV                           *
*                                                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZIATECB  DC    A(0)                     ECB FOR IATXCMD DSP.
ZIATFLAG DC    AL1(0)                   LOCKWORD.
         DC    AL3(0)
ZIATCSD  EQU   128                      COMMAND SCHEDULED FOR DSP.
ZIATLOCK DC    AL1(ZIATCSD),AL3(0)      LOCK-ARGUMENT FOR CROSS-MEMORY
*                                       SYNCHRONIZATION.
ZIATASCB DC    A(0)                     JES3 ASCB-ADDRESS.
ZIATCMD  DC    AL1(ZIATCMDL),CL79' '    COMMAND INPUT-AREA.
ZIATCMDL EQU   *-(ZIATCMD+1)            LENGTH OF COMMAND TEXT.
         DC    A(0)                     RESERVED.
         TITLE 'SUPPORT FOR MODREP'
*---------------------------------------------------------------------*
*                                                                     *
*        SUPPORT FOR MODREP PROGRAM (REAL-TIME LPA-/SVC-MODULE LOADER)*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZFETCHA  DC    A(0)                     ADDRESS OF CVTFETCH-TRAP.
ZFNAMTA  DC    A(0)                     ADDRESS OF NAME-TABLE.
         DC    A(0)                     RESERVED.
         TITLE 'CELL-POOL CONTROL'
*---------------------------------------------------------------------*
*                                                                     *
*        CELL-POOL CONTROL                                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZCRCFLAG DC    AL1(0)                   CELL POOL CONTROL-FLAG.
ZCRCCBIP EQU   128                      CELL-POOL BUILD IN PROGRESS.
ZCRCCPOK EQU   64                       CELL-POOL BUILT.
         DC    AL3(0)                   RESERVED FOR LOCK.
         SPACE
ZCRCPID  DS    A(0)                     CELL-POOL IDENTIFIER.
ZCRCLOCK DC    AL1(ZCRCCBIP),AL3(0)     LOCKWORD.
ZCRCELLP DC    AL2(0)                   PRIMARY   CELL-COUNT.
ZCRCELLS DC    AL2(0)                   SECONDARY CELL-COUNT.
ZCRCSIZE DC    AL2(0)                   CELL-SIZE.
         DC    AL2(0)                   RESERVED.
         TITLE 'RESERVED'
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
         DC    A(0)
ZEDVCVTE EQU   *
         TITLE 'INSTORAGE PROFILE TABLE ENTRY'
*---------------------------------------------------------------------*
*                                                                     *
*        FASTRAC INSTORAGE PROFILE TABLE ENTRY DSECT                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZCRIS    DSECT
         SPACE
ZCRISCN  DC    CL4' '                   RESOURCE CLASS-NAME.
ZCRISNXT DC    A(0)                     POINTER TO NEXT IN CHAIN OR 0.
ZCRISPA  DC    A(0)                     ADDR. INSTORAGE PROF.(ACEECLCP)
ZCRISFLG DC    AL1(0)                   FLAG.
ZCRISPIP EQU   128                      PROFILE MANIPULATION IN PROG.
ZCRISCIP EQU   64                       CHECK IN PROGRESS.
ZCRISCTD EQU   32                       RESOURCE-CLASS DEACTIVATED.
*              16
*              8
*              4
*              2
*              1
         DC    AL3(0)                   RESERVED.
         SPACE
*        STATISTICS COUNTERS
         SPACE
ZCRISSUC DC    A(0)                     TOTAL FRACHECKS SUCCESSFUL.
ZCRISREJ DC    A(0)                     TOTAL FRACHECKS FAILED.
ZCRISUDF DC    A(0)                     TOTAL FRACHECKS UNDEFINED.
         SPACE
ZCRISE   EQU   *
ZCRISL   EQU   ZCRISE-ZCRIS             ENTRY-LENGTH.
         TITLE 'NAME-TABLE DUMMY-SECTION'
*---------------------------------------------------------------------*
*                                                                     *
*        NAME-TABLE DUMMY SECTION                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ZFNAMT   DSECT
         SPACE
ZFNAMTN  EQU  10                        NUMBER OF ENTRIES.
         SPACE
TNAME    DC   CL8' '                    NAME OF SVC- OR LPA-MODULE.
TSVCO    DC   XL8'0'                    ORIGINAL SVC-TABLE ENTRY OR
*                                       ZERO IF TNAME IS NOT A SVC.
TSVCA    DC   A(0)                      ADDR. OF ABOVE SVC-TABLE-ENTRY.
TEPA     DC   A(0)                      SVC- OR LPA-MODULE EP.-ADDR.
*TMSBA   DC   A(0)                      XTLMSBAA FROM XTLST (SVC ONLY).
*TXLEN   DC   A(0)                      XTLMSBLA FROM XTLST (SVC ONLY).
         SPACE
TFLAG    DC   AL1(0)                    CONTENTS FLAG.
TSVC     EQU  128                       ENTRY MAPS A SVC.
TALIAS   EQU  64                        ENTRY MAPS AN ALIAS.
         SPACE
TSVCN    DC   AL1(0)                    NUMBER OF SVC OR ZERO.
         DC   AL2(0)                    RESERVED.
TCDEA    DC   A(0)                      CDE-ADDRESS.
ZFNAMTL  EQU  *-ZFNAMT                  LENGTH OF TABLE-ENTRY.
         MEND
++SRC(ZEDVCVT)     SYSLIB     (SAMPLIB)          /* <============= */
                   DISTLIB    (ASAMPLIB)        /* <============= */
                   DISTMOD    (SERVLINK).       /* <============= */
         ZEDVCVT DSECT=NO
         END
++SRC(FASTRAC0)    SYSLIB     (SAMPLIB)          /* <============= */
                   DISTLIB    (ASAMPLIB)         /* <============= */
                   DISTMOD    (SERVLINK).        /* <============= */
FASTRAC0 TITLE 'ZEDV INSTORAGE-PROFILE MANAGEMENT'
         PRINT ON,NOGEN
FASTRAC0 BVINIT RC=(15)
         SPACE
*        PERMANENT REGISTER ASSIGNMENTS
         SPACE
         USING ZEDVCVT,RZCVT            ZEDV-CVT.
         USING ZCRIS,RISP               INSTORAGE PROFILE TABLE-ENTRY.
         LOAD  EP=ISPLINK
         ST    R0,AISPL
         SPACE 2
         ISPLINK DEFSEL,RC12
         ISPLINK DEFCLS,RC12
         ISPLINK DEFSTA,RC12
         ISPLINK DEFCNT,RC12
         ISPLINK DEFMSG,RC12
         ISPLINK TABCRE,RC12
         SPACE 2
*        SCAN INCORE PROFILE TABLE
         SPACE 2
*        CHECK FOR RACF INSTALLED AND ACTIVE.
         SPACE
         L     R3,16                    CVT-ADDRESS.
         L     R15,CVTRAC-CVT(,R3)      RACF-CVT.
         LTR   R15,R15                  IS THERE A CVT ?
         BZ    RC08                     NO.
         TM    RCVTSTAT-RCVT(R15),RCVTRNA    RACF ACTIVE ?
         BO    RC08                     NO.
         TM    RCVTFLGS-RCVT(R15),RCVTROFF   DEACTIVATED BY RVARY ?
         BO    RC08                     YES.
         L     RZCVT,CVTUSER-CVT(,RZCVT) A(ZEDVCVT).
         LTR   RZCVT,RZCVT              IS THERE ANY USER-CVT ?
         BZ    RC08                     NO.
         TITLE 'SET UP INITIAL DISPLAY'
*---------------------------------------------------------------------*
*                                                                     *
*        INITIAL DISPLAY                                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
STATUS   EQU   *
         ICM   RISP,15,ZCRISPTB         PROFILE-TABLE ADDRESS.
         BZ    DISPL                    SKIP IF NONE.
STATGET1 EQU   *
         MVC   CLS,ZCRISCN              MOVE NAME.
         MVC   STA,STAACT               ASSUME ACTIVE.
         TM    ZCRISFLG,ZCRISCTD        ACTIVE ?
         BZ    STATGET2
         MVC   STA,STAINACT
STATGET2 EQU   *
         L     R15,ZCRISSUC             SUCCESS COUNT.
         CVD   R15,DWORD                TO DECIMAL.
         UNPK  CNT(8),DWORD+4(4)
         OI    CNT+7,X'F0'
         L     R15,ZCRISREJ             REJECT  COUNT.
         CVD   R15,DWORD                TO DECIMAL.
         UNPK  CNT+10(8),DWORD+4(4)
         OI    CNT+17,X'F0'
         L     R15,ZCRISUDF             UNDEFINED COUNT.
         CVD   R15,DWORD                TO DECIMAL.
         UNPK  CNT+20(8),DWORD+4(4)
         OI    CNT+27,X'F0'
         ISPLINK  TABADD,DISPL          TBADD.
STATGET3 EQU   *
         ICM   RISP,15,ZCRISNXT         NEXT ENTRY.
         BNZ   STATGET1                 TRY AGAIN.
DISPL    EQU   *
         ISPLINK TABTOP,RC04            TOP-OF TABLE.
         ISPLINK CNTRL,RC04             DISPLAY REFRESH.
         ISPLINK TABDISP                DISPLAY TABLE.
         B     GETNEXT1                 ANALYZE RETURN-CODE.
GETNEXT  EQU   *
         ISPLINK TABRETR                GET NEXT MODIFIED ENTRY.
GETNEXT1 EQU   *
         B     RCTAB(R15)               USE BRANCH-TABLE.
RCTAB    B     ANALYZE                  ANALYZE ENTERED CODE.
         B     ANALYZE                  SAME.
         B     TABEND1                  RC = 8 END.
ERROR    EQU   *
         ISPLINK TABMOD,RC04            INDICATE BAD CODE.
         ISPLINK TABMSG                 DISPLAY IT WITH A MESSAGE.
         MVC   MSG,BLANKS               CLEAR.
         MVI   SEL,C' '                 CLEAR.
         ISPLINK TABMOD,RC04            CLEAR IT IN TABLE.
COPY     EQU   *
         ISPLINK ZTDCOPY,RC04           HOW MANY SELECTED LINES LEFT ?
         CLC   ZTDSEL,ONE               = 1 MEANS ALL PROCESSED.
         BNE   GETNEXT                  GET NEXT ONE.
         B     DISPL                    FINAL DISPLAY.
TABEND   EQU   *
         ISPLINK CNTRL,RC04             DISPLAY REFRESH.
         ISPLINK TABTOP,RC04            TOP OF TABLE.
         ISPLINK TABDISP,RC04           DISPLAY IT.
TABEND1  EQU   *
         ISPLINK TABCLOSE,RC04          DELETE TABLE.
         ISPLINK  VARDEL,RC04           DELETE VARIABLES.
         DELETE EP=ISPLINK
         B     RETURN                   RETURN.
         TITLE 'ANALYZE ENTERED FUNCTION'
*---------------------------------------------------------------------*
*                                                                     *
*        ANALYZE FUNCTION                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
ANALYZE  EQU   *
         OI    SEL,C' '                 UPPERCASE.
         CLI   SEL,C'P'                 STOP.
         BE    STOP
         CLI   SEL,C'S'                 START.
         BE    START
         CLI   SEL,C'R'                 REFRESH.
         BE    REFR
         CLI   SEL,C' '                 COMMAND ENTERED.
         BE    CMDERR
         MVC   PANMSG,MSG001            FRAM001.
         MVC   MSG,MSGERR               FUNCTION ERROR.
         B     ERROR
CMDERR   EQU   *
         MVC   PANMSG,MSG002            FRAM001.
         B     ERROR
         SPACE
RACERR   EQU   *
         MVC   MSG,MSGRAC               RACF REJECT.
         MVC   PANMSG,MSG003
         B     ERROR
         TITLE 'REFRESH'
*---------------------------------------------------------------------*
*                                                                     *
*        REFRESH                                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
REFR     EQU   *
         BAL   RL,RACHECK
         FASTRAC (R,CLS)
         MVC   WTOREFR+12(L'CLS),CLS
         WTO   MF=(E,WTOREFR)
         MVC   STA,STAACT
         B     PUT
         TITLE 'START'
*---------------------------------------------------------------------*
*                                                                     *
*        START                                                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
START    EQU   *
         BAL   RL,RACHECK
         FASTRAC (S,CLS)
         MVC   STA,STAACT
         MVC   WTOSTRT+12(L'CLS),CLS
         WTO   MF=(E,WTOSTRT)
         B     PUT
         TITLE 'STOP'
*---------------------------------------------------------------------*
*                                                                     *
*        STOP                                                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
STOP     EQU   *
         BAL   RL,RACHECK
         FASTRAC (P,CLS)
         MVC   STA,STAINACT             ASSUME OK.
         B     STOPTAB(R15)
STOPTAB  B     STOPMSG
         MVC   STA,MSGERR               INDICATE ERROR.
         B     PUT
STOPMSG  EQU   *
         MVC   WTOSTOP+12(L'CLS),CLS
         WTO   MF=(E,WTOSTOP)
         B     PUT
         TITLE 'UPDATE TABLE WITH MESSAGE'
*---------------------------------------------------------------------*
*                                                                     *
*        UPDATE TABLE WITH MESSAGE FROM SVC 252                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
PUT      EQU   *
         MVI   SEL,C' '
         MVC   MSG,MSGPROC              PROCESSED
         ISPLINK TABMOD,RC04
         MVC   MSG,BLANKS
         B     COPY
         TITLE 'RETURN-CODES FOR INTERNAL/CONTROL FUNCTIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        RETURN-CODES FOR INTERNAL AND CONTROL FUNCTIONS              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RC04     EQU   *
         ISPLINK TABCLOSE,RC04
         LA    R15,4                    ISPLINK ERROR.
         B     RETURN
         SPACE
RC08     EQU   *
         LA    R15,8                    RACF/ZEDVCVT NOT INSTALLED.
         ABEND (15)
         SPACE
RC12     EQU   *
         LA    R15,12                   ISPLINK ERROR (BEFORE TABCRE).
         B     RETURN
         TITLE 'CHECK ACCESS AUTHORITY'
*---------------------------------------------------------------------*
*                                                                     *
*        CHECK ACCESS AUTHORITY                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RACHECK  EQU   *
         FASTRAC (V,CLS-1)
         B     RACTAB(R15)
RACTAB   B     0(,RL)                   OK.
         B     RACERR                   SHOULD NOT OCCUR.
         B     RACERR                   NO ACCESS.
         TITLE 'ISPLINK PARAMETER-LISTS'
*---------------------------------------------------------------------*
*                                                                     *
*        ISPLINK PARAMETER-LISTS                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         PRINT  NOGEN
TABCRE   ISPDEF (TBCREATE,TBNAME,FRACLS,VARS,NOWRITE)
TABMOD   ISPDEF (TBMOD,TBNAME)
TABADD   ISPDEF (TBADD,TBNAME)
TABTOP   ISPDEF (TBTOP,TBNAME)
CNTRL    ISPDEF (CONTROL,DISPLAY,REFRESH)
TABDISP  ISPDEF (TBDISPL,TBNAME,PNAME)
TABMSG   ISPDEF (TBDISPL,TBNAME,PNAME,PANMSG)
TABRETR  ISPDEF (TBDISPL,TBNAME)
TABCLOSE ISPDEF (TBCLOSE,TBNAME)
VARDEL   ISPDEF (VDELETE,VARS)
ZTDCOPY  ISPDEF (VCOPY,ZTDSELS,ZTDSELL,ZTDSEL,MOVE)
DEFSEL   ISPDEF (VDEFINE,FRASEL,SEL,CHAR,SELL)
DEFCLS   ISPDEF (VDEFINE,FRACLS,CLS,CHAR,CLSL)
DEFSTA   ISPDEF (VDEFINE,FRASTA,STA,CHAR,STAL)
DEFCNT   ISPDEF (VDEFINE,FRACNT,CNT,CHAR,CNTL)
DEFMSG   ISPDEF (VDEFINE,FRAMSG,MSG,CHAR,MSGL)
         SPACE 2
*        OTHER FIELDS
         SPACE
DISPLAY  DC    CL8'DISPLAY '
PANMSG   DC    CL8'        '
MSG001   DC    CL8'FRAM001 '
MSG002   DC    CL8'FRAM002 '
MSG003   DC    CL8'FRAM003 '
MOVE     DC    CL8'MOVE    '
TBTOP    DC    CL8'TBTOP   '
TBMOD    DC    CL8'TBMOD   '
TBCLOSE  DC    CL8'TBCLOSE '
REFRESH  DC    CL8'REFRESH '
CONTROL  DC    CL8'CONTROL '
TBDISPL  DC    CL8'TBDISPL '
NOWRITE  DC    CL8'NOWRITE '
TBCREATE DC    CL8'TBCREATE'
VDEFINE  DC    CL8'VDEFINE '
VGET     DC    CL8'VGET    '
VPUT     DC    CL8'VPUT    '
VCOPY    DC    CL8'VCOPY   '
VDELETE  DC    CL8'VDELETE '
TBADD    DC    CL8'TBADD   '
VARS     DC    C'(FRASEL FRACLS FRASTA FRACNT FRAMSG)'
ZTDSELS  DC    CL9'(ZTDSELS)'
FRASEL   DC    CL8'(FRASEL)'
FRACLS   DC    CL8'(FRACLS)'
FRASTA   DC    CL8'(FRASTA)'
FRACNT   DC    CL8'(FRACNT)'
FRAMSG   DC    CL8'(FRAMSG)'
CHAR     DC    CL8'CHAR    '
SHARED   DC    CL8'SHARED  '
TBNAME   DC    CL8'FRATAB  '
PNAME    DC    CL8'FRAPAN00'
         SPACE
MSGPROC  DC    CL13'* PROCESSED *'
MSGERR   DC    CL13'*   ERROR   *'
MSGRAC   DC    CL13'* NO ACCESS *'
STAACT   DC    CL13'*  ACTIVE   *'
STAINACT DC    CL13'* INACTIVE  *'
         SPACE
ZTDSEL   DC    CL4' '              FUNCTION-CODE.
ZTDSELL  DC    AL4(L'ZTDSEL)       FUNCTION-CODE LENGTH.
SEL      DC    CL1' '              FUNCTION-CODE.
SELL     DC    AL4(L'SEL)          FUNCTION-CODE LENGTH.
         DC    AL1(L'CLS)          NEEDED FOR RACHECK.
CLS      DC    CL4' '              RESOURCE-CLASS AREA.
CLSL     DC    AL4(L'CLS)          RESOURCE-CLASS AREA LENGTH.
STA      DC    CL13' '             STATUS-AREA.
STAL     DC    AL4(L'STA)          STATUS-AREA LENGTH.
CNT      DC    CL28' '             STATISTICS COUNTER.
CNTL     DC    AL4(L'CNT)          COUNTER LENGTH.
MSG      DC    CL13' '             MESSAGE-AREA.
MSGL     DC    AL4(L'MSG)          MESSAGE-AREA LENGTH.
         SPACE
WTOSTOP  WTO   'FASTRAC XXXX STOPPED',MF=L,ROUTCDE=9
WTOSTRT  WTO   'FASTRAC XXXX STARTED',MF=L,ROUTCDE=9
WTOREFR  WTO   'FASTRAC XXXX REFRESH',MF=L,ROUTCDE=9
         TITLE 'WORK-FIELDS AND CONSTANTS'
*---------------------------------------------------------------------*
*                                                                     *
*        WORK-FILELDS AND CONSTANTS                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
DWORD    DC    D'0'
R        DC    AL4(ZCRREFR)        REFRESH FUNCTION-CODE.
P        DC    AL4(ZCRSTOP)        STOP    FUNCTION-CODE.
S        DC    AL4(ZCRSTART)       START   FUNCTION-CODE.
V        DC    AL4(ZCRVER)         VERIFY  ACCESS AUTHORITY.
ONE      DC    C'0001'
AISPL    DC    A(0)
BLANKS   DC    CL17' '
         TITLE 'SPECIAL REGISTER EQUATES'
*---------------------------------------------------------------------*
*                                                                     *
*        SPECIAL REGISTER EQUATES                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RZCVT    EQU   3                        A(ZEDVCVT).
RISP     EQU   12                       INSTORAGE-PROFILE TABLE-ENTRY.
RL       EQU   11                       LINK-REGISTER.
         TITLE 'DUMMY-SECTIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         PRINT NOGEN
         ZEDVCVT
         ICHPRCVT
         CVT   DSECT=YES
         END
++SRC(IGC0025B)    SYSLIB     (SAMPLIB)          /* <============= */
                   DISTLIB    (ASAMPLIB)         /* <============= */
                   DISTMOD    (SERVLINK).        /* <============= */
IGC0025B TITLE 'FAST RACF-CHECKING'
*---------------------------------------------------------------------*
*                                                                     *
*        NAME         :      SVC 252  (IGC0025B).                     *
*                                                                     *
*        MODULE-TYPE  :      SVC TYPE 3.                              *
*                                                                     *
*        MODE         :      AMODE 24 / RMODE 24 (FOR RACF).          *
*                                                                     *
*        ATTRIBUTES   :      REENTRANT.                               *
*                                                                     *
*        EXECUTION    :      SUPERVISOR STATE , KEY 0.                *
*                                                                     *
*        LOCKS        :      NONE                                     *
*                                                                     *
*        RECOVERY     :      NONE                                     *
*                                                                     *
*        ABENDS       :      020    RACLIST/FRACHECK INTERNAL ERROR.  *
*                            024    ZEDVCVT NOT INSTALLED.            *
*                            028    NO RACF-CVT / RACF NOT INSTALLED. *
*                            032    PARAMETER-LIST ERROR.             *
*                            036    INSTORAGE-PROFILE-TABLE FULL.     *
*                            040    UNDEFINED RESOURCE-CLASS.         *
*                                                                     *
*                                                                     *
*        FUNCTION     :      IGC0025B IS USED FOR FAST-RACF CHECKING. *
*                                                                     *
*                            SVC252 IS CALLED WITH A STANDARD PARA-   *
*                            METER-LIST POINTED TO BY R1 :            *
*                                                                     *
*                            FUNCTION-CODE =   0                      *
*                                                                     *
*                            R1 -->   A( FUNCTION-CODE )              *
*                                     A( RESOURCE-CLASS )             *
*                                     A( RESOURCE-NAME )              *
*                                     AL1(80) AL3( USERID )           *
*                                                                     *
*                                                                     *
*                            FUNCTION-CODE =   4 , 8 OR C             *
*                                                                     *
*                            R1 -->   A( FUNCTION-CODE )              *
*                                     AL1(80) AL3( RESOURCE-CLASS )   *
*                                                                     *
*                                                                     *
*                            PARAMETER-FORMATS :                      *
*                                                                     *
*                            FUNCTION-CODE   =    XL4                 *
*                            RESOURCE-CLASS  =    CL8                 *
*                            RESOURCE-NAME   =    CL8                 *
*                            USERID          =    CL8                 *
*                                                                     *
*                                                                     *
*                                                                     *
*                            FUNCTION-CODES :                         *
*                                                                     *
*                            0   PERFORM RACF-CHECKING                *
*                            4   STOP   RACF-CHECKING                 *
*                            8   REFRESH INSTORAGE PROFILES           *
*                            C   START  RACF-CHECKING                 *
*                            10  VERIFY ACCESS AUTHORITY              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   PERFORM CHECKING         THE FIRST CALL FOR A SPECIFIC RESOURCE-  *
*   ----------------         CLASS WILL INITIALIZE THE ENVIRONMENT    *
*                            FOR THAT CLASS.                          *
*                                                                     *
*                            - FIRST CALL TO SVC WILL BUILD CELL-POOL *
*                              AND ITS ADDRESS STORED IN ZEDVCVT.     *
*                            - ISSUE GETMAIN FOR AN INSTORAGE-PROFILE.*
*                              CHAIN-ELEMENT AND ANCHOR IT OFF THE    *
*                              CVT ('ZCRISPTB') OR IF NOT THE FIRST   *
*                              ONE , CHAIN IT TO THE LAST CHAIN-      *
*                              ELEMENT.                               *
*                            - RACLIST IS ISSUED TO BUILD THE IN-     *
*                              STORAGE-PROFILE (USING A QUICK-CELL).  *
*                              RACLIST WILL STORE THE ADDRESS OF THE  *
*                              INSTORAGE-PROFILE INTO THE ACEE BUILT  *
*                              IN THE QUICK-CELL.                     *
*                            - PROPAGATE ACEECLCP TO INSTORAGE-       *
*                              PROFILE-CHAIN ELEMENT.                 *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                            IF THE INSTORAGE PROFILE FOR THE REQUES- *
*                            TED RESOURCE-CLASS IS ALREADY CREATED :  *
*                            - A QUICK-CELL IS AQUIRED AND INITIALIZED*
*                              WITH THE CONTENTS OF ASXBSENV-ACEE.    *
*                            - USERID IS MOVED TO THE WORK ACEE.      *
*                            - ACEESP SET TO INSTORAGE-PROFILE SUBPOOL*
*                              (IF THEY ARE NOT EQUAL NOTHING WILL    *
*                              HAPPEN ×××××××××××××××××××××××××××××). *
*                            - FRACHECK IS ISSUED.                    *
*                            - RELEASE QUICK-CELL.                    *
*                                                                     *
*                            THE FOLLOWING RETURN-CODES ARE RETURNED  *
*                            IN R15 TO THE CALLER :                   *
*                                                                     *
*                            0  =  ACCESS GRANTED                     *
*                            4     RESOURCE NOT DEFINED               *
*                            8     ACCESS FAILED                      *
*                            12    FUNCTION STOPPED.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*  STOP PROFILE              BIT 'ZCRISCTD' IS SET IN THE CORRES-     *
*  ------------              PONDING INSTORAGE-PROFILE TABLE-ENTRY.   *
*                                                                     *
*                            R1 -->   A( FUNCTION-CODE )              *
*                                     A( RESOURCE-CLASS )             *
*                                                                     *
*                                                                     *
*  REFRESH PROFILE           R1 -->   A( FUNCTION-CODE )              *
*  ---------------                    A( RESOURCE-CLASS )             *
*                                                                     *
*                            RACLIST 'DELETE' IS ISSUED FOR THE       *
*                            SELECTED CLASS.                          *
*                                                                     *
*                                                                     *
*                                                                     *
*  START PROFILE             BIT 'ZCRISCTD' IS SET OFF IN THE CORRES- *
*  -------------             PONDING INSTORAGE-PROFILE TABLE-ENTRY.   *
*                                                                     *
*                            R1 -->   A( FUNCTION-CODE )              *
*                                     A( RESOURCE-CLASS )             *
*                                                                     *
*                                                                     *
*                                                                     *
*  VERIFY ACCESS             TEST FOR AUTH=OPERATIONS.IF NOT ,ISSUE   *
*  -------------             RACHECK FOR CLASS WITH ENTITY 'BASE'.    *
*                                                                     *
*                            R1 -->   A( FUNCTION-CODE )              *
*                                     A( RESOURCE-CLASS )             *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*        REGISTERS           UPON ENTRY TO IGC0025B :                 *
*                            ---------------------                    *
*                                                                     *
*                            R0  :  UNCHANGED                         *
*                            R1  :  A(PARMLIST)                       *
*                            R2  :  UNPREDICTABLE                     *
*                            R3  :  CVT                               *
*                            R4  :  TCB OF CALLER                     *
*                            R5  :  SVRB                              *
*                            R6  :  EP-ADDRESS                        *
*                            R7  :  ASCB                              *
*                            R8-R12 UNPREDICTABLE                     *
*                            R13 :  UNCHANGED                         *
*                            R14 :  RETURN ADDRESS                    *
*                            R15 :  UNCHANGED                         *
*                                                                     *
*                                                                     *
*                            AT EXIT :                                *
*                            --------                                 *
*                                                                     *
*                            R0-R1  UNPREDICTABLE                     *
*                            R2-R14 SAME AS BEFORE CALL.              *
*                            R15 :  RETURN-CODE                       *
*                                                                     *
*                                                                     *
*                                                                     *
*        MACROS       :      ZEDVCVT.                                 *
*                            RACLIST.                                 *
*                            FRACHECK.                                *
*                            RACHECK.                                 *
*                            GETMAIN.                                 *
*                            CVT.                                     *
*                            CPOOL.                                   *
*                            ABEND.                                   *
*                            IHAASCB.                                 *
*                            IHAASXB.                                 *
*                            IHAACEE.                                 *
*                            ICHPRCVT.                                *
*                                                                     *
*                                                                     *
*---------------------------------------P.KREBS/07JAN85---------------*
         SPACE
         PRINT ON,GEN
         LCLA  &SP,&PCELL,&SCELL
&SP      SETA  245                      SUBPOOL FOR CONTROL-BLOCKS.
&PCELL   SETA  6                        PRIMARY CELL-COUNT.
&SCELL   SETA  4                        SECONDARY CELL-COUNT.
IGC0025B CSECT
         USING IGC0025B,RBASE           SET BY SVC-SLIH.
         B     AROUND                   SKIP EYE-CATCHER.
         DC    C'IGC0025BSP212'         XA212 LEVEL-ID.
         DC    C'&SYSDATE',C' ',C'&SYSTIME'
         SPACE 2
*        PERMANENT REGISTER ASSIGNMENTS
         SPACE
         USING ZEDVCVT,RZCVT            ZEDV-CVT.
         USING ZCRIS,RISP               INSTORAGE PROFILE TABLE-ENTRY.
         USING SVCWORK,RWK              REENTRANT WORK-AREA.
         SPACE 2
*        CHECK FOR RACF INSTALLED AND ACTIVE.
         SPACE
AROUND   DS    0H
         LR    RPARM,R1                 SAVE PARM. POINTER.
         SLR   RWK,RWK                  INDICATE NO CELL.
         ICM   R15,15,CVTRAC-CVT(R3)    RACF-CVT.
         BZ    RC28                     NO.
         TM    RCVTSTAT-RCVT(R15),RCVTRNA    RACF ACTIVE ?
         BO    RC28                     NO.
         TM    RCVTFLGS-RCVT(R15),RCVTROFF   DEACTIVATED BY RVARY ?
         BO    RC28                     YES.
         SPACE 2
*        CHECK IF CALLER IS A RACF USER.
         SPACE
         L     RACEE,ASCBASXB-ASCB(,R7) A(ASXB).
         ICM   RACEE,15,ASXBSENV-ASXB(RACEE) A(ACEE).
         BZ    RC0                      NO ACEE (RACF INACTIVE).
         TM    ACEEFLG1-ACEE(RACEE),ACEERACF ARE WE A RACF-USER ?
         BZ    RC28                     NO.
         SPACE 2
*        SETUP
         SPACE
         ICM   RZCVT,15,CVTUSER-CVT(RZCVT) A(ZEDVCVT).
         BZ    RC24                     NO USERCVT.
         TITLE 'VALIDATE PARMLIST'
*---------------------------------------------------------------------*
*                                                                     *
*        VALIDATE PARMLIST (PART 1)                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         L     RFC,0(,RPARM)            A(FUNCTION-CODE).
         L     RFC,0(,RFC)              FUNCTION-CODE.
         C     RFC,MAXFUNC              HIGHER THAN ALLOWED ?
         BH    RC32                     YES.
         L     RCL,4(,RPARM)            A(RESOURCE-CLASS-NAME).
         TITLE 'BUID CELL-POOL'
*---------------------------------------------------------------------*
*                                                                     *
*        BUILD CELL-POOL                                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CPOOLA   EQU   *
         TM    ZCRCFLAG,ZCRCCPOK        CELL-POOL EXISTS ?
         BO    CPOOLD                   YES.
         TM    ZCRCFLAG,ZCRCCBIP        BUILD-IN-PROGRESS ?
         BZ    CPOOLC                   NO , CONTINUE.
CPOOLB   EQU   *
         STIMER WAIT,DINTVL=WAIT02
         B     CPOOLA                   CHECK IF BUILD COMPLETE.
CPOOLC   EQU   *
         L     R1,ZCRCLOCK              = ZCRCCBIP.
         SLR   R0,R0                    ASSUME NO POOL YET.
         CS    R0,R1,ZCRCFLAG           ACQUIRE BUILD LOCK..
         BNE   CPOOLB                   DIDN'T GET IT.
         GETMAIN R,LV=72+CPOOLL         GET WORK-AREA.
         LR    R13,R1
         LA    R1,72(,R13)              A(LIST FORM).
         MVC   0(CPOOLL,R1),CPOOL       INITIALIZE WITH LIST FORM.
         CPOOL BUILD,                                                  +
               PCELLCT=&PCELL,                                         +
               SCELLCT=&SCELL,                                         +
               CSIZE=SVCWORKL,                                         +
               SP=&SP,                                                 +
               LOC=RES,                                                +
               CPID=ZCRCPID,                                           +
               HDR=CPOOLHDR,                                           +
               LINKAGE=SYSTEM,                                         +
               MF=(E,(1))
         FREEMAIN R,LV=72+CPOOLL,A=(R13)
         LA    R1,&PCELL                PRIMARY CELL-COUNT.
         STH   R1,ZCRCELLP              FOR DEBUGGING.
         LA    R1,&SCELL                SECONDARY CELL-COUNT.
         STH   R1,ZCRCELLS              FOR DEBUGGING.
         LA    R1,SVCWORKL              CELL-SIZE.
         STH   R1,ZCRCSIZE              FOR DEBUGGING.
         MVI   ZCRCFLAG,ZCRCCPOK        INDICATE CELL-POOL CREATED.
         TITLE 'GET-CELL AND BRANCH TO FUNCTION-PROCESSOR'
*---------------------------------------------------------------------*
*                                                                     *
*        BRANCH TO FUNCTION PROCESSOR                                 *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CPOOLD   EQU   *
         B     BTAB(RFC)                BRANCH-TABLE.
BTAB     B     CHECK                    0  = CHECK.
         B     STOP                     4  = STOP  INSTORAGE PROFILE.
         B     REFRESH                  8  = REFRESH INSTORAGE PROFILE.
         B     START                    C  = START INSTORAGE PROFILE.
         B     VERACC                   10 = VERIFY  ACCESS TO PROFILE.
         TITLE 'PROCESS CHECK REQUEST'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION = 0 ===> CHECK-REQUEST , VALIDATE PARM-LIST (PART 2)*
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
CHECK    EQU  *
         CLI   12(RPARM),128            ANY EOL ?
         BNE   RC32                     NO , FORMAT ERROR.
         LM    RRES,RUS,8(RPARM)        2 POINTERS MORE.
         CLC   0(4,RCL),BLANKS          VALID CLASS ?
         BE    RC32                     NO , FORMAT ERROR.
         CLC   0(4,RCL),NULLS           VALID CLASS ?
         BE    RC32                     NO , FORMAT ERROR.
         CLC   0(8,RRES),BLANKS         VALID RESOURCE ?
         BE    RC32                     NO , FORMAT ERROR.
         CLC   0(8,RRES),NULLS          VALID RESOURCE ?
         BE    RC32                     NO , FORMAT ERROR.
         CLC   0(8,RUS),BLANKS          VALID RESOURCE ?
         BE    RC32                     NO , FORMAT ERROR.
         CLC   0(8,RUS),NULLS           VALID RESOURCE ?
         BE    RC32                     NO , FORMAT ERROR.
         BAL   RL,GETCELL               GET A QUICK-CELL.
         BAL   RL,PTABGET               GET MATCHING INST.-PROFILE.
         B     CHECKTB1(R15)            USE BRANCH-TABLE.
CHECKTB1 B     CHECK1                   0 =   FOUND.
         B     CHECKA                   4 =   NOT FOUND (1ST CALL).
         B     CHECK0                   8 =   NAME FOUND.
         SPACE 2
*        RC = 4 , NO POINTER.BUILD PROFILE FIRST.
         SPACE
CHECKA   EQU   *
         BAL   RL,SETQIP                LOCK CHAIN.
         L     R0,GETMCHN               SP 245 , LENGTH.
         GETMAIN R,LV=(0)               GET CHAIN ELEMENT.
         XC    0(ZCRISL,R1),0(R1)       CLEAR ELEMENT.
         LTR   RISP,RISP
         BNZ   CHECKE
         ST    R1,ZCRISPTB              FIRST PROFILE.
         LR    RISP,R1                  MAKE IT CURRENT.
         B     CHECK0
CHECKE   EQU   *
         ST    R1,ZCRISNXT              CHAIN TO PREVIOUS.
         LR    RISP,R1                  MAKE IT CURRENT.
CHECK0   EQU   *
         BAL   RL,SETPIP                LOCK PROFILE.
         BAL   RL,INITP                 GO BUILD PROFILE.
         NI    ZCRISFLG,255-ZCRISPIP    UNLOCK PROFILE ENTRY.
         NI    ZCRFLAG1,255-ZCR1QIP     UNLOCK PROFILE CHAIN.
         SPACE
CHECK1   EQU   *
         OI    ZCRFLAG1,ZCR1CIP         INDICATE CHECK IN PROGRESS.
         OI    ZCRISFLG,ZCRISCIP        INDICATE CHECK-IN-PROGRESS.
         MVC   RCHECK(RCHECKL),CHECKL   MOVE LIST FORM.
         MVC   SVCACEE(ACEEL),0(RACEE)  COPY IT.
         MVC   SVCACEE+(ACEEUSRI-ACEE)(L'ACEEUSRI),0(RUS)
         XC    SVCACEE+(ACEEUNAM-ACEE)(4),RACEE+(ACEEUNAM-ACEE)
         MVI   SVCACEE+(ACEEUSRL-ACEE),6 L'USERID.
         MVC   SVCACEE+(ACEECLCP-ACEE)(L'ACEECLCP),ZCRISPA COPY ANCHOR.
         MVI   SVCACEE+(ACEESP-ACEE),&SP CHANGE SUBPOOL NUMBER.
         XC    SVCWKA(16*4),SVCWKA      WORK-AREA FOR FRACHECK.
         STM   R2,R5,SVCSAVE            WOULD BE CORRUPTED.
         LA    R1,RCHECK                POINT TO LIST-FORM.
         FRACHECK CLASS=(RCL),                                         *
               ENTITY=(RRES),                                          *
               ACEE=SVCACEE,                                           *
               WKAREA=SVCWKA,                                          *
               MF=(E,(1))
         LM    R2,R5,SVCSAVE            CORRUPTED BY FRACHECK.
         NI    ZCRFLAG1,255-ZCR1CIP     RESET CHECK-IN-PROGRESS.
         NI    ZCRISFLG,255-ZCRISCIP    RESET CHECK-IN-PROGRESS.
         B     CHECKTAB(R15)            BRANCH-TABLE.
CHECKTAB B     CHECKX                   0  =    ACCESS GRANTED.
         B     CHECKY                   4  =    RESOURCE NOT DEFINED.
         B     CHECKZ                   8  =    ACCESS DENIED.
         B     RC28                     C  =    RACF INACTIVE.
         B     RC20                     10 =    EXIT ERROR.
         B     RC28                     14 =    NO RACF CVT.
         SPACE 2
*        UPDATE STATISTICS COUNTER.
         SPACE
CHECKX   EQU   *
         LA    R0,1                     INCREMENT.
         L     R1,ZCRISSUC              SUCCESS-COUNTER.
CHECKXA  EQU   *
         LR    R15,R1                   SETUP COPY.
         AR    R15,R0                   INCREMENT COPY.
         CS    R1,R15,ZCRISSUC          UPDATE STATISTICS COUNTER.
         BNE   CHECKXA                  BUSY , TRY AGAIN.
         B     RC0
         SPACE
CHECKY   EQU   *
         LA    R0,1                     INCREMENT.
         L     R1,ZCRISUDF              SUCCESS-COUNTER.
CHECKYA  EQU   *
         LR    R15,R1                   SETUP COPY.
         AR    R15,R0                   INCREMENT COPY.
         CS    R1,R15,ZCRISUDF          UPDATE STATISTICS COUNTER.
         BNE   CHECKYA                  BUSY , TRY AGAIN.
         B     RC4
         SPACE
CHECKZ   EQU   *
         LA    R0,1                     INCREMENT.
         L     R1,ZCRISREJ              FAIL-COUNTER.
CHECKZA  EQU   *
         LR    R15,R1                   SETUP COPY.
         AR    R15,R0                   INCREMENT COPY.
         CS    R1,R15,ZCRISREJ          UPDATE STATISTICS COUNTER.
         BNE   CHECKZA                  BUSY , TRY AGAIN.
         B     RC8
         TITLE 'STOP   INSTORAGE PROFILE'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION = 4 ===> STOP                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
STOP     EQU   *
         ICM   RISP,15,ZCRISPTB         PROFILE-TABLE ADDRESS.
         BZ    RC4                      RESOURCE NOT DEFINED.
STOPGT1  EQU    *
         CLC   ZCRISCN,0(RCL)           SAME RESOURCE-CLASS ?
         BNE   STOPGT2                  NO.
         OI    ZCRISFLG,ZCRISCTD        INDICATE PROFILE STOPPED.
         B     RC0                      ALL OK.
STOPGT2  EQU    *
         ICM   RISP,15,ZCRISNXT         NEXT ENTRY IN CHAIN.
         BNZ   STOPGT1                  TRY AGAIN.
         B     RC4                      RESOURCE NOT DEFINED.
         TITLE 'REFRESH INSTORAGE PROFILES'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION = 8 ===> FORCE REFRESH                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
REFRESH  EQU   *
         BAL   RL,GETCELL               GET A QUICK-CELL.
         BAL   RL,PTABGET               GET PROFILE TABLE ENTRY.
         B     REFRTB1(R15)             USE BRANCH-TABLE.
REFRTB1  B     REFRESH1                 RC = 0.
         B     RC4                      RESOURCE NOT DEFINED.
         SPACE
REFRESH1 EQU  *
         CLC   ZCRISPA,NULLS            ANY PROFILE ?
         BE    RC0                      NO (REFRESH PENDING).
         BAL   RL,SETPIP                LOCK ENTRY.
         BAL   RL,DELP                  DELETE PROFILE.
         NI    ZCRISFLG,255-ZCRISPIP    UNLOCK PROFILE ENTRY.
         B     RC0                      EXIT.
         TITLE 'PROCESS START CHECKING'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION = C ===> START-REQUEST.                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
START    EQU  *
         ICM   RISP,15,ZCRISPTB         PROFILE-TABLE ADDRESS.
         BZ    RC4                      RESOURCE NOT DEFINED.
STARTGT1 EQU   *
         CLC   ZCRISCN,0(RCL)           SAME RESOURCE-CLASS ?
         BNE   STARTGT2                 NO.
         NI    ZCRISFLG-ZCRIS(RISP),255-ZCRISCTD     INDICATE STARTED.
         B     RC0                      ALL OK.
STARTGT2 EQU   *
         ICM   RISP,15,ZCRISNXT         NEXT ENTRY IN CHAIN.
         BNZ   STARTGT1                 TRY AGAIN.
         B     RC4                      RESOURCE NOT DEFINED.
         TITLE 'CHECK ACCESS AUTHORITY'
*---------------------------------------------------------------------*
*                                                                     *
*        CHECK ACCESS AUTHORITY OF REQUESTOR TO MANIPULATE PROFILE    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
VERACC   EQU   *
         L     R15,X'220'               ASCB.
         L     R15,ASCBASXB-ASCB(,R15)  ASXB.
         L     R15,ASXBSENV-ASXB(,R15)  ACEE.
         TM    ACEEFLG1-ACEE(R15),ACEESPEC  SPECIAL USER ?
         BO    RC0                      YES , ALL OK. SKIP RACHECK.
         SPACE 2
*        CHECK IF CALLER HAS ALTER ACCESS
         SPACE
         BAL   RL,GETCELL               GET A QUICK-CELL.
         MVC   RRCHK(RRCHKL),CHECKLR    INIT WITH LIST FORM.
         RACHECK CLASS=(RCL),MF=(E,RRCHK)
         B     VERACCT(R15)
VERACCT  B     RC0                      ACCESS ALLOWED.
         B     RC4                      UNDEFINED (SHOULD NOT OCCUR).
         B     RC8                      NO ACCESS.
         TITLE 'INIT INSTORAGE PROFILES'
*---------------------------------------------------------------------*
*                                                                     *
*        INITIALIZE AN INSTORAGE PROFILE                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
INITP    EQU   *
         MVC   ZCRISCN,0(RCL)           MOVE CLASS-NAME.
         MVC   RLIST(RLISTL),LISTL      MOVE LIST FORM.
         MVC   SVCACEE(ACEEL),0(RACEE)  COPY ACEE.
         MVI   SVCACEE+(ACEESP-ACEE),&SP CHANGE SUBPOOL NUMBER.
         LA    R1,RLIST                 POINT TO LIST-FORM.
         RACLIST CLASS=(RCL),ENVIR=CREATE,ACEE=SVCACEE,MF=(E,(1))
         LTR   R15,R15                  ZERO ?
         BNZ   INITERR                  NO ,SOMETHING WRONG.
         MVC   ZCRISPA,SVCACEE+(ACEECLCP-ACEE) COPY ANCHOR.
         L     R1,ZCRISPTN              NUMBER OF INSTORAGE PROFILES.
         LA    R1,1(,R1)                +1
         ST    R1,ZCRISPTN              FOR DEBUGGING.
INITERR  EQU   *
         B     INITETAB(R15)            BRANCH-TABLE.
INITETAB B     0(,RL)                   0  =  OK (EXIT OR CHECK).
         B     RC20                     4  =  ESTAE OR FUNCTION ERROR.
         B     RC40                     8  =  UNDEFINED RESOURCE CLASS
         B     RC20                     C  =  INTERNAL ERROR.
         B     RC28                     10 =  RACF OR CLASS INACTIVE.
         B     RC20                     14 =  EXIT ERROR.
         B     RC20                     18 =  PROGRAMMING ERROR.
         B     RC28                     28 =  NO RACF CVT.
         TITLE 'MISC. SUBROUTINES'
*---------------------------------------------------------------------*
*                                                                     *
*        GET PROFILE TABLE-ENTRY ADDRESS                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
PTABGET  EQU   *
         SLR   RISP,RISP                IF ZERO ON EXIT , CHAIN EMPTY.
         SLR   R15,R15                  ASSUME RC = 0.
         ICM   RISP,15,ZCRISPTB         FIRST PROFILE-TABLE ADDRESS.
         BZ    PTABGET4                 NONE YET.
PTABGET0 EQU   *
         CLC   ZCRISCN,0(RCL)           SAME RESOURCE-CLASS ?
         BNE   PTABGET3                 NO.
PTABGET1 EQU   *
         TM    ZCRISFLG,ZCRISPIP        ENTRY LOCKED ?
         BNO   PTABGET2                 NO.
         STIMER WAIT,DINTVL=WAIT02      WAIT 2/10 SECONDS.
         SLR   R15,R15                  ASSUME RC = 0.
         B     PTABGET1
PTABGET2 EQU   *
         TM    ZCRISFLG,ZCRISCTD        PROFILE STOPPED ?
         BO    RC12                     YES.
         CLC   ZCRISPA,NULLS            PROFILE EXISTS ?.
         BNER  RL                       YES.
         LA    R15,8                    NO , MUST BE RECREATED.
         BR    RL
PTABGET3 EQU   *
         ICM   R1,15,ZCRISNXT           POINTER TO NEXT IN CHAIN.
         BZ    PTABGET4                 LAST IN CHAIN.
         LR    RISP,R1                  MAKE IT CURRENT.
         B     PTABGET0                 TRY IT.
PTABGET4 EQU   *
         LA    R15,4                    NO PROFILE.
         BR    RL
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        LOCK PROFILE-CHAIN ENTRY (ISP-ENTRY)                         *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
SETPIP   EQU   *
         L     R1,ZCRPLOCK              = ZCRISPIP.
         L     R0,ZCRISFLG              CURRENT-VALUE.
         CS    R0,R1,ZCRISFLG           ACQUIRE TABLE-ENTRY.
         BER   RL                       OK , GOT IT.
SETPIPWT EQU   *
         STIMER WAIT,DINTVL=WAIT02      WAIT 2/10 SECONDS.
         B     SETPIP                   TRY AGAIN.
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        LOCK PROFILE-CHAIN                                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
SETQIP   EQU   *
         TM    ZCRFLAG1,ZCR1QIP         CHAIN LOCKED ?
         BO    SETQIPWT                 YES , WAIT A LITTLE.
         L     R1,ZCRQLOCK              = ZCR1QIP.
         L     R0,ZCRFLAG1              CURRENT-VALUE.
         CS    R0,R1,ZCRFLAG1           ACQUIRE CHAIN.
         BER   RL                       CHAIN LOCKED.
SETQIPWT EQU   *
         STIMER WAIT,DINTVL=WAIT02      WAIT 2/10 SECONDS.
         B     SETQIP                   TRY AGAIN.
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        GET A CELL                                                   *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
GETCELL  EQU   *
         CPOOL GET,                                                    +
               UNCOND,                                                 +
               CPID=ZCRCPID,                                           +
               LINKAGE=SYSTEM,                                         +
               REGS=USE
         LTR   RWK,R1
         BZ    RC36                     INDICATE GETCELL FAILED.
         L     RZCVT,CVTPTR             CVT-POINTER.
         L     RZCVT,CVTUSER-CVT(,RZCVT) RESTORE CVTUSER.
         BR    RL
         TITLE 'DELETE INSTORAGE PROFILE'
*---------------------------------------------------------------------*
*                                                                     *
*        FUNCTION = 14 ===> DELETE                                    *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
DELP     EQU  *
         MVC   RLIST(RLISTL),LISTL      MOVE LIST FORM.
         MVC   SVCACEE(ACEEL),0(RACEE)  COPY IT.
         MVC   SVCACEE+(ACEECLCP-ACEE)(L'ACEECLCP),ZCRISPA COPY ANCHOR.
*                                       TO BE DELETED.
         MVI   SVCACEE+(ACEESP-ACEE),&SP CHANGE SUBPOOL NUMBER.
         LA    R1,RLIST                 POINT TO LIST-FORM.
         RACLIST CLASS=(RCL),ENVIR=DELETE,ACEE=SVCACEE,MF=(E,(1))
         B     DELPETAB(R15)            BRANCH-TABLE.
DELPETAB B     DELEX                    0  =  CLEANUP.
         B     RC20                     4  =  ESTAE OR FUNCTION ERROR.
         B     RC40                     8  =  UNDEFINED RESOURCE CLASS
         B     RC20                     C  =  INTERNAL ERROR.
         B     RC28                     10 =  RACF OR CLASS INACTIVE.
         B     RC20                     14 =  EXIT ERROR.
         B     RC20                     18 =  PROGRAMMING ERROR.
         B     RC28                     28 =  NO RACF CVT.
         SPACE
DELEX    EQU   *
         L     R1,ZCRISPTN              NUMBER OF INSTORAGE PROFILES.
         BCTR  R1,0                     -1
         ST    R1,ZCRISPTN              FOR DEBUGGING.
         XC    ZCRISSUC,ZCRISSUC        CLEAR SUCCESS-COUNTER.
         XC    ZCRISREJ,ZCRISREJ        CLEAR FAIL   -COUNTER.
         XC    ZCRISUDF,ZCRISUDF        CLEAR UNDEF  -COUNTER.
         XC    ZCRISPA,ZCRISPA          ZERO ANCHOR.
         BR    RL                       EXIT.
         TITLE 'RETURN-CODES FOR CHECK FUNCTION'
*---------------------------------------------------------------------*
*                                                                     *
*        RETURN-CODES FOR CHECK FUNCTION                              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RC0      EQU   *
         SLR   R15,R15                  RETURN-CODE = 0 , ALL OK.
         B     SVC3
         SPACE
RC4      EQU   *
         LA    R15,4                    RESOURCE NOT DEFINED.
         B     SVC3
         SPACE
RC8      EQU   *
         LA    R15,8                    ACCESS DENIED.
         B     SVC3
         SPACE
RC12     EQU   *
         LA    R15,12                   FUNCTION STOPPED
         B     SVC3
         SPACE
RC16     EQU   *
         LA    R15,16                   *** AVAILABLE ***
         B     SVC3
SVC3     EQU   *
         LTR   RWK,RWK                  ANY CELL TO FREE ?
         BZ    SVC3A                    NO.
         LR    R5,R15                   SAVE RETURN-CODE.
         CPOOL FREE,                                                   +
               CPID=ZCRCPID,                                           +
               REGS=USE,                                               +
               CELL=(RWK)
         LR    R15,R5                   RESTORE RETURN-CODE.
SVC3A    EQU   *
         SVC   3
         TITLE 'RETURN-CODES FOR INTERNAL/CONTROL FUNCTIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        RETURN-CODES FOR INTERNAL AND CONTROL FUNCTIONS              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RC20     EQU   *
         LA    R1,20                    INTERNAL/PROGRAM/EXIT
         B     ABEND
         SPACE
RC24     EQU   *
         LA    R1,24                    ZEDVCVT NOT INSTALLED.
         B     ABEND
         SPACE
RC28     EQU   *
         LA    R1,28                    NO RACF-CVT OR RACF INACTIVE.
         B     ABEND
         SPACE
RC32     EQU   *
         LA    R1,32                    PARAMETER-LIST ERROR.
         B     ABEND
         SPACE
RC36     EQU   *
         LA    R1,36                    GETCELL FAILED.
         B     ABENDA
         SPACE
RC40     EQU   *
         LA    R1,40                    UNDEFINED RESOURCE-CLASS.
         B     ABEND
         SPACE
ABEND    EQU   *
         NI    ZCRFLAG1,255-ZCR1QIP     UNLOCK CHAIN.
         NI    ZCRISFLG,255-ZCRISPIP    UNLOCK ENTRY.
         LTR   RWK,RWK                  ANY CELL TO FREE ?
         BZ    ABENDA                   NO.
         LR    R5,R15                   SAVE RETURN-CODE.
         LR    R8,R1                    SAVE DUMP-CODE.
         CPOOL FREE,                                                   +
               CPID=ZCRCPID,                                           +
               REGS=USE,                                               +
               CELL=(RWK)
         LR    R15,R5                   RESTORE RETURN-CODE.
         LR    R1,R8                    RESTORE DUMP-CODE.
ABENDA   EQU   *
         ABEND (1),DUMP
         TITLE 'NUTS AND BOLTS'
*---------------------------------------------------------------------*
*                                                                     *
*        NUTS AND BOLTS                                               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
MAXFUNC  DC    A(16)                    HIGHEST POSSIBLE FUNCTION-CODE.
GETMCHN  DC    AL1(&SP),AL3(ZCRISL)     SUBPOOL , LENGTH.
BLANK    DC    CL8' '
BASE     DC    CL8'BASE'                DUMMY ENTITY FOR RACHECK.
NULLS    DC    XL8'00'
BLANKS   DC    CL8' '
WAIT02   DC    C'00000020'              2/10 SECOND.
CPOOLHDR DC    CL24'FASTRAC PROFILE CHAIN'
CHECKLR  RACHECK  ENTITY=BASE,ATTR=ALTER,MF=L
CHECKL   FRACHECK MF=L
LISTL    RACLIST  SUBPOOL=(&SP,&SP),MF=L
CPOOL    CPOOL BUILD,MF=L
CPOOLL   EQU   *-CPOOL
         TITLE 'SPECIAL REGISTER EQUATES'
*---------------------------------------------------------------------*
*                                                                     *
*        SPECIAL REGISTER EQUATES                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
RFC      EQU   2                        FUNCTION-CODE.
RZCVT    EQU   3                        A(ZEDVCVT).
RPARM    EQU   4                        SAVED PARAMETER POINTER.
RACEE    EQU   5                        ACEE OF CALLING ADDRESS-SPACE.
RBASE    EQU   6                        BASE-REGISTER.
RL       EQU   7                        INTERNAL LINK-REGISTER 1.
RCL      EQU   8                        RESOURCE-CLASS-NAME ADDRESS.
RRES     EQU   9                        RESOURCE-NAME ADDRESS.
RUS      EQU   10                       USERID ADDRESS.
RWK      EQU   11                       A(WORK-AREA).
RISP     EQU   12                       INSTORAGE-PROFILE TABLE-ENTRY.
         TITLE 'REGISTER EQUATES'
*---------------------------------------------------------------------*
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'DUMMY-SECTIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         PRINT GEN
         ZEDVCVT
         IHAACEE
ACEEL    EQU   *-ACEE                   L'WORK-AREA.
         PRINT NOGEN
         CVT   DSECT=YES
         IHAASCB
         IHAASXB
         ICHPRCVT
         TITLE 'SVC WORK-AREA'
*---------------------------------------------------------------------*
*                                                                     *
*        REENTRANT WORK-AREA FOR SVC (CELL-POOL SP 245)               *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         PRINT GEN
SVCWORK  DSECT
SVCSAVE  DC    18A(0)                   SAVE-AREA FOR FRACHECK.
SVCWKA   DC    16A(0)                   WORK-AREA FOR FRACHECK.
SVCACEE  DC    XL(ACEEL)'00'
RCHECK   FRACHECK MF=L
RCHECKL  EQU   *-RCHECK
         SPACE 2
*        WORK-AREA FOR RACLIST                                        *
         SPACE
RLIST    RACLIST SUBPOOL=(&SP,&SP),MF=L
RLISTL   EQU   *-RLIST
         SPACE 2
*        WORK-AREA FOR RACHECK                                        *
         SPACE
RRCHK    RACHECK ENTITY=BASE,ATTR=ALTER,MF=L
RRCHKL   EQU   *-RRCHK
SVCWORKL EQU   *-SVCWORK
         END
++JCLIN .
//*     ------------ JCLIN FOR FASTRAC0 -----------
//FASTRAC0 JOB MSGLEVEL=(1,1)
//FASTRAC0 EXEC LINKS,
// PARM='NCAL,LET,SIZE=(512K,100K)',
//    UNIT='',SER=,N=,NAME=CMDLIB1,P1=' ',
//    MOD=,P2=' ',OBJ=,CLASS=
//SERVLINK DD DSNAME=SERV.LINKLIB,DISP=SHR
//SYSIN DD *
 INCLUDE SERVLINK(FASTRAC0)
 NAME FASTRAC0(R)
//*     ------------ JCLIN FOR IGC0025B -----------
//IGC0025B JOB MSGLEVEL=(1,1)
//IGC0025B EXEC LINKS,
// PARM='NCAL,RENT,SIZE=(512K,100K),AC(1)',
//    UNIT='',SER=,N=,NAME=LPALIB1,P1=' ',
//    MOD=,P2=' ',OBJ=,CLASS=
//SERVLINK DD DSNAME=SERV.LINKLIB,DISP=SHR
//SYSIN DD *
 INCLUDE SERVLINK(IGC0025B)
 NAME IGC0025B(R)
//*     ------------ JCLIN FOR ZEDVCVT -----------
//ZEDVCVT  JOB MSGLEVEL=(1,1)
//ZEDVCVT  EXEC LINKS,
// PARM='LIST,XREF,NCAL,SCTR,LET,SIZE=(512K,64K)',
//    UNIT='',SER=,N=,NAME=NUCLEUS,P1=' ',
//    MOD=,P2=' ',OBJ=,CLASS=
//SERVLINK     DD      DSN=SERV.LINKLIB,DISP=SHR
//SYSLIN       DD      *
 INCLUDE SERVLINK(ZEDVCVT)
 NAME    IEANUC01(R)
??
//SMPEIN  DD *
 SET BDY(GLOBAL).
 REJECT S(IPOCVT0) BYPASS(APPLYCHECK).
 RECEIVE SYSMODS.
 SET BDY(ST1T).
 APPLY   S(IPOCVT0) ASSEM REDO NUCID(2).
