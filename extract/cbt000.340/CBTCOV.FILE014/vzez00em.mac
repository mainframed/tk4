./       ADD   NAME=BVTAUTH
         MACRO
&L       BVTAUTH &F,&ERR,&MAP=,&TYPE=TSO
.*--------------------------------------------------------------------*
.*                                                                    *
.*       BVTAUTH CAN BE USED TO GENERATE THE INSTALLATION DEFINED     *
.*       BIT-MAP FOR THE TWO-BYTE FLAG 'PSCBATR2' IN THE PSCB .       *
.*       TO GENERATE ONLY THESE EQUATES CODE 'MAP=ONLY'.              *
.*                                                                    *
.*       BVTAUTH ALSO GENERATES THE ROUTINE TO TEST IF A TSO-USER     *
.*       IS ALLOWED TO USE CERTAIN COMMAND PROCESSORS.                *
.*       TO GENERATE THIS ROUTINE DO NOT CODE 'MAP=ONLY'.             *
.*                                                                    *
.*       OPERANDS  :                                                  *
.*       --------                                                     *
.*                                                                    *
.*       &F        EQUATE DEFINED IN THE MAP (I.E. PSCB2ALL)          *
.*                 IF OMITTED ONLY A CHECK FOR  'PSCB2ALL' IS MADE.   *
.*                                                                    *
.*       &ERR      LABEL OF ROUTINE TO BE BRANCHED TO IF TSO-USER     *
.*                 IS NOT AUTHORIZED TO ISSUE ACTUAL COMMAND.         *
.*                                                                    *
.*       &MAP      IF 'ONLY' IS CODED ONLY FLAGS /DSECTS ARE          *
.*                 GENERATED.                                         *
.*                                                                    *
.*       &TYPE     IF 'BATCH' IS CODED CP MAY RUN TSO/BATCH           *
.*                 IF 'TSO'   IS CODED CP MAY RUN TSO ONLY            *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*-----------------------------------------P.KREBS/08JUN80------------*
.*
.*
         GBLB  &BVTAUTH                 MAP GENERATION INHIBIT SWITCH.
         LCLC  &A                       LABEL GENERATOR.
         LCLC  &O                       WORK FIELD FOR &F.
         LCLB  &P2                      IDENTIFIES PSCBATR2.
         LCLB  &P3                      IDENTIFIES PSCBATR3.
         LCLB  &FO                      SET IF &F IS OMITTED.
         LCLA  &I                       FLAG ID (2 OR 3).
.*
.*       WORK FLAG DEFINITIONS
.*
         LCLB  &ALL,&SPY,&JES,&SYS ...  ADD NEW DEFINITIONS HERE
.*
&I       SETA  0                        INITIAL.
.*
         AIF   ('&MAP' EQ 'ONLY').MAP   ONLY MAP GENERATION.
.*
.*
.*
.*       VALIDATE TYPE= OPERAND
.*
.*
         AIF   ('&TYPE' EQ 'BATCH').TOK
         AIF   ('&TYPE' EQ 'TSO').TOK
         AGO   .MNOTE2
.*
.*
.*
.TOK     ANOP
&FO      SETB  (T'&F EQ 'O')
         AIF   (&FO).NOCHECK
.*
.*       VALIDITY CHECK 1ST OPERAND  PSCB2...
.*
&ALL     SETB  ('&F' EQ 'PSCB2ALL')
&SPY     SETB  ('&F' EQ 'PSCB2SPY')
&JES     SETB  ('&F' EQ 'PSCB2JES')
&SYS     SETB  ('&F' EQ 'PSCB2SYS')
.* ...... ADD NEW FLAGS FOR PSCB2ATR HERE ..........
.*&XXX   SETB  ('&F' EQ 'PSCB3XXX')      .... CHANGE TO ACTIVATE ....
.* ...... ADD NEW FLAGS FOR PSCBATR3 HERE ..........
.*
&P2      SETB  (&ALL OR &SPY OR &JES OR &SYS)
.*&P3    SETB  (&XXX OR &... OR &... ...)
         AIF   (NOT &P2 AND NOT &P3).MNOTE1
         AIF   (&P2).NOCHECK
&I       SETA  1
.NOCHECK ANOP
.*
&A       SETC  'BV'.'&SYSNDX'
         AIF   (T'&L EQ 'O').NOLAB
&L       DS    0H
.NOLAB   ANOP
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        ACCESS PSCB                                                  *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         EXTRACT PSCBADDR,'S',FIELDS=PSB     GET PSCB
         L     R1,PSCBADDR             LOAD POINTER TO PSCB
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        AUTHORITY CHECK ROUTINE FOR TSO-USERS                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
         TM    PSCBATR2-PSCB(R1),PSCB2ALL ALL COMMANDS ALLOWED ?
         BO    &A.A                     YES.
         AIF   (&P2 OR &P3).N1
         AIF   (T'&ERR EQ 'O').ABEND
         B     &ERR                     NO.SCHEDULE ERROR PROCESSING.
.N1      ANOP
         AIF   (&FO).N2
         AIF   ('&F' EQ 'PSCB2ALL').NOPSC2
         TM    PSCBATR2+&I-PSCB(R1),&F  SPECIAL COMMAND (-GROUP)
*                                       ALLOWED ?
         BO    &A.A                     YES.
.*
.*
.*
.NOPSC2  ANOP
         AIF   (T'&ERR NE 'O').GOERR
&A.E     EQU   *
*        TPUT
         TPUT  &A.M,&A.T-&A.M
.ABEND   ANOP
         ABEND 000
         ABEND 000
EXTLIST  EXTRACT MF=L
PSCBADDR DC    A(0)
&A.M     DC    C'I AM THE WIZARD OF TSO. YOUR MASTER'
&A.T     EQU   *
         AGO   .N2
.*
.*
.*
.GOERR   ANOP
         B     &ERR                     NO.SCHEDULE ERROR PROCESSING.
.N2      ANOP
&A.A     DS    0H
.*
.*       GENERATE MAP
.*
.MAP     ANOP
         AIF  (NOT &BVTAUTH).GENIT
         MEXIT
.GENIT   ANOP
&BVTAUTH SETB  1
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        PSCBATR2/3 INSTALLATION DEFINED BIT MAP                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE
PSCB2SYS EQU    128                     CRITICAL SYSTEM FUNCTIONS
PSCB2JES EQU    64                      (QUEUE   ) JES2 QUEUE-READ.
*                                       ALLOWS TO READ ANY DATASET.
PSCB2SPY EQU    32                      (SPY     ) ALLOWS FOR SENDING
*                                       MVS OR JES COMMANDS FROM TSO.
PSCB2ALL EQU    16                      ========>  ALL COMMANDS
*                                       (I.E. SPY , # ETC).
*                8                      RESERVED.
*                4                      RESERVED.
*                2                      RESERVED.
*                1                      RESERVED.
         SPACE 2
*PSCBATR3 EQU  PSCBATR2+1               DEFINE SECOND BYTE.
         SPACE
*              128                      RESERVED.
*               64                      RESERVED.
*               32                      RESERVED.
*               16                      RESERVED.
*                8                      RESERVED.
*                4                      RESERVED.
*                2                      RESERVED.
*                1                      RESERVED.
.*
.*
.*
.*
         AIF   ('&MAP' EQ 'ONLY').EXIT
.*
.*
*        IKJPSCB
         IKJPSCB
&SYSECT  CSECT
.*
.*
.EXIT    ANOP
         MEXIT
.*
.*
.*
.MNOTE1  MNOTE 8,'&F INVALID FLAG. SEE BIT MAP'
         MEXIT
.*
.*
.MNOTE2  MNOTE 8,'TYPE=&TYPE INVALID. NOT BATCH OR TSO'
         MEXIT
.*
.*
.*
         MEND
./       ADD   NAME=FRAPAN00
)ATTR
$ TYPE(OUTPUT) INTENS(LOW)
# TYPE(OUTPUT) INTENS(HIGH)
)BODY
%-----------------------------------------------------------------------
%COMMAND===> _ZCMD                                         %SCROLL ===>
+- CLASS ---- STATUS --------- SUCC ---- FAIL --- NOTDEF ------- ACTION
)MODEL
_Z$Z      #Z                $Z                               #Z
)INIT
  .HELP = FRAPAN01
  &VARS = '(FRASEL FRACLS FRASTA FRACNT FRAMSG)'
  &SCR  = PAGE
)END
./       ADD   NAME=FRAPAN01
%---------------------- FASTRAC - INSTORAGE-PROFILE MANAGEMENT ---------
%
+ %SUPPORTED OPERATIONS ARE :
+
+
%        P+    STOP PROFILE
+              FOR EVERY REQUEST ISSUED FOR THE STOPPED PROFILE
+              RETURN-CODE 12 WILL BE RETURNED (TREATED AS RC=0).
+
%        S+    START PROFILE
+              ACCESS TO PROFILE WILL BE ENABLED AGAIN.
+
%        R+    REFRESH PROFILE
+              THE INSTORAGE PROFILE WILL BE REBUILT (MUST BE DONE AFTER
+              PERMITS , DELETES ETC.).
+
+
+              THE FOLLOWING MVS/JES3 EXITS USE FASTRAC-FUNCTIONS :
+
+              IEFUSO  (TUSO) , IEFUJV   (TUJV) , IKJEFF10 (TEXT)
+              IEFUTL  (TUTL) , IGGPRE00 (TALC).
+
+
+
+
)PROC
)END
