        TITLE 'CHECK WITHIN EXCP SECTION FOR COUNT=ZERO'
*---------------------------------------------------------------------*
*                                                                     *
*       ELEMINATE ALL SECTIONS WHICH HAVE AN EXCP-COUNT = ZERO        *
*       IF SUBTYPE=5 ELEMINATE THE WHOLE EXCP SECTION !!!             *
*       R3 CONTAINS RECORD ADDR. OF REC. 30                           *
*                                                                     *
*---------------------------------------------------------------------*
        SPACE
        ICM   R2,7,SMF30EOF+1         ANY EXCP SECTION ?
        BZ    NOEXCP
        ALR   R2,R3                   SET TO SECTION
        USING SMF30EXP,R2
        LH    R0,SMF30EON             NO. OF EXCP SECTIONS
        LTR   R0,R0                   NO SECTIONS
        BZ    NOEXCP
        CLC   SMF30TYP,TYPE5          IS IT TYPE 5 ?
        LR    R14,R0                  SET NO OF SECTIONS
        BE    SRR0R14                 IF TYPE 5 --> DELETE SECTION
        SR    R14,R14                 CLEAR WORK
        LR    R1,R2                   SET RECORD POINTER
        SPACE
LWORK   EQU   *
        L     R15,SMF30BLK-SMF30EXP(R1) GET EXCP COUNT
        LTR   R15,R15                 ANY ?
        BP    YESEXCP                 YES
        AH    R1,SMF30ELN             NEXT ELEMENT
        LA    R14,1(R14)              ONE TO COUNTER
        SPACE
SUBR0   EQU   *
        BCT   R0,LWORK                GET NEXT SECTION
        LH    R0,SMF30EON             NO. ELEMENTS
SRR0R14 EQU   *
        SR    R0,R14                  MOVED ELEMENTS
        STH   R0,SMF30EON             STORE ACTIVE ELEMENTS
        MH    R14,SMF30ELN            LENGTH OF NOT MOVED
        LH    R0,SMF30LEN             RECORD LENGTH
        SR    R0,R14                  REMAINING LENGTH
        STH   R0,SMF30LEN             INTO RECORD
        B     NOEXCP
        SPACE 2
YESEXCP EQU   *
        CR    R1,R2                   SAME AREA ADDR. ?
        BE    LEAVE                   YES  DONT MOVE IT
        LH    R15,SMF30ELN            ELEMENT LENGTH
        BCTR  R15,R0                  FOR EXECUTE
        EX    R15,MOVEEXCP            MOVE SECTION
   SPACE
LEAVE   EQU   *
        AH    R1,SMF30ELN              SET TO NEXT SECTION
        AH    R2,SMF30ELN              SET TO NEXT SECTION
        B     SUBR0                    AGAIN
  SPACE
MOVEEXCP MVC  SMF30EXP(0),0(R1)        MOVE SECTION
