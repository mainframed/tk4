         MACRO
         SGIEA2SU
         COPY  SGGBLPAK
   LCLC  &STORAGE,&TIMER,&FLOATPT,&IQE,&SERR,&EXT(8),&TRACE,&TRSVCTB
         LCLC  &JST                                                J/ST
         LCLC  &PROTECT,&SLICE                                     MTS0
         LCLA  &MOD,&MACH,&SW1,&SW2,&SIZER                         MO17
         LCLC  &HIARCHY                                            MO17
         LCLC  &SMF                                               19018
         LCLC  &ATTACH                                             I250
         LCLC  &EMU                                                EMUL
.*1306009720,009850-010800,017080,017240-018000                    ISER
.*1306001200-001300,009660,016600-016800,018400                    MO17
.*1306019000                                                       MP65
.*  007000-007400                                                 19018
.*007900                                                         A26704
.*011800-012400,012800-013000,013400-013800                   BD A56865
&STORAGE SETC  'PRIM'
&TIMER   SETC  'NOTIM'
&FLOATPT SETC  'N'
&IQE     SETC  'OTHR12'                                       BD A56865
&SERR    SETC  'SERP'
&EXT(1)  SETC  'N'
&EXT(2)  SETC  'N'
&EXT(3)  SETC  'N'
&EXT(4)  SETC  'N'
&EXT(5)  SETC  'N'
&EXT(6)  SETC  'N'
&EXT(7)  SETC  'N'
&EXT(8)  SETC  'N'
&TRACE   SETC  'N'
&TRSVCTB  SETC 'N'
&PROTECT  SETC  'N'
&SLICE   SETC  'NOSLICE'                                           MTS0
&JST     SETC  'N'                                                 J/ST
&SMF     SETC  'NSF'                                              19018
&ATTACH  SETC  'NOATH'                                             I250
&EMU     SETC  'EMUN'                                              EMUL
.****** SET MODEL NUMBER **********************************************
&MOD   SETA  40+10*&SGCPUB(3)+25*&SGCPUB(4)+35*&SGCPUB(5)+51*&SGCPUB(6)
&MOD     SETA  &MOD+45*&SGCPUB(7)                                  R85
&MOD     SETA  &MOD+155*&SGCPUB(9)                                M4600
&MOD     SETA  &MOD+755*&SGCPUB(13)                           BD  21108
&MOD     SETA  &MOD+115*&SGCPUB(8)                                19E07
&MOD     SETA  &MOD+125*&SGCPUB(11)                               19E08
&MOD     SETA  &MOD+118*&SGCPUB(14)                            BD 21122
&MOD     SETA  &MOD+128*&SGCPUB(15)                            BD 21122
.* MODEL NUMBER HAS BEEN CHANGED FROM 297 TO 145                  M0132
&MOD     SETA  &MOD+105*&SGCPUB(10)                               20301
&MOD     SETA  &MOD+95*&SGCPUB(12)                                21054
.****** TEST FOR STORAGE CONFIGURATION ********************************
         AIF   (&SGCPRGB(8) EQ 1).NEXT20
         AIF   (&SGCPRGB(1) EQ 1).NEXT3
         AIF   (&SGCPRGB(2) NE 1).NEXT1
&STORAGE SETC  'SING'
         AGO   .NEXT3
.NEXT1   AIF   (&SGCPRGB(3) NE 1).NEXT2
&STORAGE SETC  'SHAR'
         AGO   .NEXT3
.NEXT2   ANOP
&STORAGE SETC  'PART'
.****** TEST FOR TIMER OPTIONS ****************************************
.NEXT3   AIF   (&SGSUPRB(19) NE 1).NEXT3A    JOBSTEP TIMING       19018
         AIF   (&SGCPRGB(4) NE 1).PASS       MFT                  19018
&JST     SETC  'Y'                           SET JOBSTEP PARM     19018
         AIF   (&SGSCHDB(7) NE 1).PASS       SMF                  19018
&SMF     SETC  'SMF'                         SET SMF PARM         19018
         AGO   .PASS                                              M4959
.NEXT3A  AIF   (&SGSUPRB(14) NE 1).NEXT4                          19018
.PASS    ANOP                                                     19018
&TIMER   SETC  'TTIME'                                            19018
         AGO   .NEXT4A                                            19018
.NEXT4   AIF   (&SGSUPRB(13) NE 1).NEXT4B
&TIMER   SETC  'DTIME'
.NEXT4A  ANOP
&EXT(1)  SETC  'Y'
.****** TEST FOR TRANSIENT SVC TABLE OPTION ***************************
.NEXT4B  AIF   (&SGSUPRB(3) NE 1).NEXT5
&TRSVCTB  SETC 'Y'
.****** TEST FOR FLOATING POINT REGISTERS *****************************
.NEXT5   AIF   (NOT(&SGCPUB(32) OR &SGCPUB(33))).NEXT6
&FLOATPT SETC  'Y'
.****** TEST FOR MACHINE SIZE ****************************************
.NEXT6   ANOP                                                      MO17
&SW1     SETA  20                                                  MO17
&SW2     SETA  32768                                               MO17
.MACHSZ  AIF   (&SGCPUB(&SW1) NE 1).INCRMT                         MO17
&MACH    SETA  (&SW2)                                              MO17
         AGO   .HIARY1                                             MO17
.INCRMT  AIF   ((&SW1) EQ 29).HIARY1                               MO17
&SW1     SETA  ((&SW1)+1)                                          MO17
&SW2     SETA  ((&SW2)+(&SW2))                                     MO17
         AGO   .MACHSZ                                             MO17
.****** TEST FOR HIERARCHY *******************************************
.HIARY1  AIF   (&SGCPRGB(11) NE 1).HIARY2                          MO17
&HIARCHY SETC  'Y'                                                 MO17
         AGO   .NEXT6A                                             MO17
.HIARY2  ANOP                                                      MO17
&HIARCHY SETC  'N'                                                 MO17
.****** TEST FOR SER/R LEVEL ******************************************
.NEXT6A  ANOP                                                      MO17
&SIZER   SETA  &SGCPRGA(2)                                         MO17
         AIF   (&SGSUPRB(28) NE 1).NEXT6B                          ISER
&SERR    SETC  'MCH'
.NEXT6B  ANOP                                                      MTS0
.*                                                                 MTS0
.*         TIME SLICING IS DEPENDENT ON THE PRESENCE OF THE        MTS0
.*   INTERVAL TIMER.  'GENERATE' WILL VALIDITY CHECK THIS          MTS0
.*   CONDITION.  GLOBAL SWITCH SGCPRGB(12) INDICATES THE           MTS0
.*   TIME-SLICING OPTION.                                          MTS0
.*                                                                 MTS0
         AIF   (&SGCPRGB(12) EQ 0).GOBY                            MTS0
&SLICE   SETC  'TIMESL'                                            MTS0
.GOBY    ANOP                                                      MTS0
.****** TEST FOR CONSOLE SWITCHING ************************************
.NEXT7   AIF   ('&SGSCHDC(1)'(1,3) EQ '&SGSCHDC(2)'(1,3)).NEXT8
&EXT(2)  SETC  'Y'
.****** TEST FOR ASYNCHRONOUS EXIT INFO *******************************
.NEXT8   ANOP                                                 BD A56865
         AIF   (&SGSUPRB(15) EQ 1 OR &SGSUPRB(16) EQ 1).NEXT8B    65974
         AGO   .NEXT9                                         BD A56865
.NEXT8B  ANOP                                                 BD A56865
&IQE     SETC  'BOTH12'                                       BD A56865
.****** TEST FOR TRACE OPTION *****************************************
.NEXT9   AIF   (&SGSUPRA(1) EQ 0).NEXT10
&TRACE   SETC  'Y'
.******* TEST FOR PROTECTION FEATURE **********************************
.NEXT10  AIF  (&SGSUPRB(23) NE 1).NEXT11
&PROTECT  SETC  'Y'
.NEXT11  ANOP
.******************* TEST FOR ASYNCHRONOUS ATTACH *********************
         AIF   (NOT &SGSUPRB(37)).NEXT12B                          I250
&ATTACH  SETC  'ATTACH'                                            I250
.****** TEST FOR EMULATOR *********************************************
.NEXT12B AIF  (NOT &SGMENTB(58)).NEXT12                            EMUL
         AIF   (NOT &SGCPUB(7)).NEXT12                             EMUL
&EMU     SETC  'EMUY'                                              EMUL
.NEXT12  ANOP
&SGCTRLC(6) SETC 'IEAASU00'
         COPY  SGASMPAK
         PUNCH '     IEAAIH &STORAGE,&IEAHIA,&IEALOA,&IEALMTA,&SERR,(&E*
               XT(1),&EXT(2),&EXT(3),&EXT(4),&EXT(5),&EXT(6),&EXT(7),&E*
               XT(8)),                   XXXXXXXX'               A73495
         PUNCH '               &TRACE,&PROTECT,&FLOATPT,&TRSVCTB,&MOD,&*
               SIZER,&MACH,&HIARCHY,&TIMER,&SMF,&SGMENTB(68)'     19E01
         PUNCH '     IEAAPS &STORAGE,&TIMER,&FLOATPT,&IQE,&TRACE,&SLICEG
               ,&JST,&EMU,&ATTACH,&SMF,&SERR'                     19E02
         MEXIT
.NEXT20  AIF   (&SGSUPRB(28) NE 1).NEXT24                          ISER
&SERR    SETC  'MCH'
.NEXT24  ANOP
&SGCTRLC(6) SETC  'IEAASU00'
         COPY  SGASMPAK
 PUNCH ' IEAQFX &MOD,&SERR,&SGCPRGB(13),&SGMENTB(68),GENCODE=YES' 19E01
         MEND
