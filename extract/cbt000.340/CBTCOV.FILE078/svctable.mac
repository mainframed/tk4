         MACRO
         SVCTABLE
         COPY  SGGBLPAK
         LCLA  &I,&J,&K,&L,&M,&N,&P,&Q
         LCLC  &C,&D,&F
.*                                     LAST UPDATE 20 MARCH 1972
.L1      AIF   (N'&SYSLIST EQ 0).M1  ERROR EXIT FOR NO OPERANDS.
&I       SETA  &I+1  SET INDEX TO OPERAND.
         AIF  (K'&SYSLIST(&I) GT 13 OR K'&SYSLIST(&I) LT 11).M2
&J       SETA  0  SET INDEX TO CHARACTER OF OPERAND.
.L2      AIF   (&J+1 GT K'&SYSLIST(&I)).M2  ERROR END OF OPERAND.
&J       SETA  &J+1  BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) NE '-').L2  LOOK FOR MINUS.
&N       SETA  &J+1  SET POINTER TO FIRST SVC CHARACTER.
.L3      AIF   (&J+1 GT K'&SYSLIST(&I)).M2  ERROR END OF OPERAND.
&J       SETA  &J+1  BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) GE '0' AND '&SYSLIST(&I)'(&J,1) LE1
                '9').L3  GO BACK IF CHARACTER IN RANGE.
         AIF   ('&SYSLIST(&I)'(&J,1) NE '-').M2  ERROR INVALID CHAR.
         AIF  (&J-&N GT 3).M2             SVC NUMBER GT 3 CHARS.
&C       SETC  '&SYSLIST(&I)'(&N,&J-&N)   SET SVC NO. IN C BOX.
&K       SETA  &C  PUT SVC NUMBER IN A BOX.
         AIF  (&K LT 1 OR &K GT 255).M3   SVC OUT OF RANGE
.R4      ANOP
&P       SETA  &J+2  SET POINTER TO FIRST TYPE CHARACTER.
&J       SETA  &J+1  BUMP CHARACTER INDEX.
.L4      AIF   (&J+1 GT K'&SYSLIST(&I)).M2  ERROR END OF OPERAND.
&J       SETA  &J+1 BUMP CHARACTER INDEX.
         AIF   ('&SYSLIST(&I)'(&J,1) GE '0' AND '&SYSLIST(&I)'(&J,1) LE1
                '5').L4  GO BACK IF CHARACTER IN RANGE.
         AIF   (&J-&P EQ 0).L4A           ERROR INVALID CHAR.
         AIF   ('&SYSLIST(&I)'(&J,1) NE '-').M2  ERROR INVALID CHAR.
.L4A     ANOP
         AIF  (&J-&P GT 1).M4
&D       SETC  '&SYSLIST(&I)'(&P,&J-&P)   SET TYPE NO. IN C BOX.
         AIF   (&J-&P EQ 0).M4            ERROR INVALID CHAR.
&L       SETA  &D  PUT TYPE NUMBER IN A BOX.
.*                                      ONLY SGIEI2SV CAN SPECIFY TYPE5
         AIF   (&L EQ 5 AND NOT (&SGMENTB(88) OR &SGMENTB(69))).M4  SO
.*                                      TEST FOR GENERATE OR GENTSO ENY
         AIF  (&L LT 1 OR &L GT 5).M4   TYPE OUT OF RANGE
.R5      ANOP
         AIF   (&J+2 GT K'&SYSLIST(&I)).M2  ERROR NOT ENOUGH CHAR.
&Q       SETA  &J+2  SET POINTER TO FIRST SAVE CHARACTER.
&J       SETA  &J+1  BUMP CHARACTER INDEX.
.L5      ANOP       ENTRY FOR RETURN FROM SCAN.
&J       SETA  &J+1  BUMP CHARACTER INDEX.
         AIF   (&J GT K'&SYSLIST(&I)).L6  TRANSFER END OF SCAN.
         AIF   ('&SYSLIST(&I)'(&J,1) GE '0' AND '&SYSLIST(&I)'(&J,1) LE1
                '6').L5  GO BACK IF CHARACTER IN RANGE.
.L6      AIF  (&J-&Q GT 1).M5
&F  SETC  '&SYSLIST(&I)'(&Q,1)     SET SAVE INTO C BOX
         AIF  (&J-&Q EQ 0).M5      ERROR IF EQ TO ZERO
&M       SETA  &F  PUT SAVE NUMBER IN A BOX.
  AIF  (&M LT 0 OR &M GT 6).M5   SAVE OUT OF RANGE
         AIF   (&N-4 NE 1).M2  ERROR NOT ENOUGH CHAR. BEFORE SVC NO.
  AIF  ('&SYSLIST(&I)'(&N-4,3) NE 'SVC').M6  NO SVC
  AIF  ('&SYSLIST(&I)'(&P-1,1) NE 'T').M7    NO T
  AIF  ('&SYSLIST(&I)'(&Q-1,1) NE 'S').M8   NO S
  AIF (&L EQ 5 AND &M GE 1).M9
  AIF (&L EQ 1 AND &M GE 1).M9
         AIF   (&SGSVCAB(&K) OR &SGSVCBB(&K) OR &SGSVCCB(&K)).P2 ERR.
&SGSVCAB(&K) SETB (&L EQ 5 OR &L EQ 4)  SET TYPE FOUR BIT.
&SGSVCBB(&K) SETB (&L EQ 3 OR &L EQ 2)  SET TYPE TWO BIT.
&SGSVCCB(&K) SETB (&L EQ 5 OR &L EQ 3 OR &L EQ 1)  SET TYPE ONE BIT.
  AIF  (&L EQ 5 OR &L EQ 1).L8
&SGSVCDB(&K) SETB (&M EQ 6 OR &M EQ 5 OR &M EQ 4 OR &M EQ 3)  FOUR BIT.
&SGSVCEB(&K) SETB (&M EQ 6 OR &M EQ 5 OR &M EQ 2 OR &M EQ 1)  TWO BIT.
&SGSVCFB(&K) SETB (&M EQ 6 OR &M EQ 4 OR &M EQ 2 OR &M EQ 0)  ONE BIT.
.L8      AIF   (&I+1 LE N'&SYSLIST).L1  GO BACK FOR NEXT OPERAND.
         MEXIT
.M1 MNOTE 5,'* * * IEISVC101 NO SVC SPECIFIED'
&SGQUITB  SETB  1
         MEXIT
.M2 MNOTE 5,'* * * IEISVC102 OPERAND &I-VALUE &SYSLIST(&I) INVALID'
&SGQUITB  SETB  1
         AGO  .L8
.M3 MNOTE 5,'* * * IEISVC103 OPERAND &I-SVC VALUE &C INVALID'
&SGQUITB  SETB  1
         AGO  .R4
.M4 MNOTE 5,'* * * IEISVC104 OPERAND &I-TYPE VALUE  INVALID'
&SGQUITB  SETB  1
&J       SETA  &J+1
         AGO  .R5
.M5 MNOTE 5,'* * * IEISVC105 OPERAND &I-SAVE VALUE &F INVALID'
&SGQUITB  SETB  1
         AGO  .L8
.M6 MNOTE 5,'* * * IEISVC106 OPERAND &I-SVC NOT SPECIFIED'
&SGQUITB  SETB  1
         AGO  .L8
.M7 MNOTE 5,'* * * IEISVC107 OPERAND &I-TYPE NOT SPECIFIED'
&SGQUITB  SETB  1
         AGO  .L8
.M8 MNOTE 5,'* * * IEISVC108 OPERAND &I-SAVE NOT SPECIFIED'
&SGQUITB  SETB  1
         AGO  .L8
.M9 MNOTE 5,'* * * IEISVC109 OPERAND &I-TYPE AND SAVE INCOMPATIBLE'
&SGQUITB  SETB  1
         AGO  .L8
.P2 MNOTE 5,'* * * IEISVC110 OPERAND &I-SVC &K REPEATED'
&SGQUITB  SETB  1
         AGO  .L8
         MEND
