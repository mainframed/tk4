 MACRO
   GJOBCTL &UNITS=,&SYSOUT=,&PRI=,&CLASS=,&ALLOC=(20,5),&REGION=OMIT,  *
               &TYPE=GJP,&LINEREF=FT99F001,                            *
               &UNITREF=SYSDSPLY,&LINE=50,&DAPOOL=SYSDA
 COPY SGGBLPAK
 LCLA &B
 LCLA &A
 AIF (&SGMENTB(29)).E1                 HAS MACRO BEEN ENTERED BEFORE
&SGMENTB(29)  SETB  1
 AIF (T'&TYPE EQ '').A1A2                    2250'S ONLY
 AIF (N'&TYPE GT 1).A1B                         BOTH SPECIFIED
 AIF ('&TYPE' EQ 'GJP').A1A2                 2250'S ONLY
 AIF ('&TYPE' EQ 'SGJP').A1A                  SATELLITE SPECIFIED
 AGO .EF
.A1B AIF ('&TYPE(1)' EQ 'SGJP' OR '&TYPE(2)' EQ 'SGJP').A1A   SAT.
 AGO .EG
.A1B1 AIF ('&TYPE(1)' EQ 'GJP' OR '&TYPE(2)' EQ 'GJP').A1A2   2250'S
 AGO .EH
.A1A ANOP
    MNOTE *,'      THE SATELLITE GRAPHIC JOB PROCESSOR IS INCLUDED'
&SGGJCB(7) SETB  1
 AIF (T'&LINEREF EQ '').EI              SPECIFIED INCORRECTLY
 AIF ('&LINEREF'(1,1) LT 'A' OR '&LINEREF'(1,1) GT 'Z').EI
 AIF (K'&LINEREF GT 8).EI                     GREATER THAN 8 CHAR
&SGGJCC(11) SETC '&LINEREF'
    MNOTE *,'      &LINEREF IS DEFAULT OPTION FOR UNIT DDNAME'
 AIF (N'&TYPE GT 1).A1B1
 AGO .A1C
.A1A2 ANOP
    MNOTE *,'      THE GRAPHIC JOB CONTROL FUNCTION WILL BE INCLUDED'
    MNOTE *,'      2250 DISPLAY UNITS FOR GRAPHIC JOB CONTROL OPERATIONX
               S ARE '
&SGGJCB(6) SETB  1
.A1C  ANOP
 AIF (T'&UNITS EQ 'O').E2
 AIF (N'&UNITS GT 15).E3
&SGGJCA(1) SETA N'&UNITS
     MNOTE *,'       DEVICE ADDRESSES SPECIFIED'
.A1  ANOP
&B SETA  &B+1
  AIF (&B GT N'&UNITS OR &B GT 15).A2
 CONVERT TO=HEX,VALUE=&UNITS(&B),DIGITS=3
    MNOTE *,'                &UNITS(&B) '
&SGGJUA(&B) SETA &HEX
  AGO .A1
.A2 AIF (T'&SYSOUT EQ 'O').E4
 AIF (N'&SYSOUT NE 4 OR K'&SYSOUT(1) NE 1 OR K'&SYSOUT(2) NE 1 OR K'&SYX
               SOUT(3) NE 1 OR K'&SYSOUT(4) NE 1).E5
.A21 ANOP
&A  SETA &A+1
 AIF (NOT('&SYSOUT(&A)' LT 'A' OR '&SYSOUT(&A)' GT '9') AND NOT('&SYSOUX
               T(&A)' GT 'Z' AND '&SYSOUT(&A)' LT '0')).A22
  AGO .E4
.A22 AIF (&A NE 4).A21
&SGGJCC(1) SETC '&SYSOUT(1)&SYSOUT(2)&SYSOUT(3)&SYSOUT(4)'
    MNOTE *,'      SYSTEM OUTPUT CLASSES USED DURING GRAPHIC JOB CONTROX
               L OPERATIONS ARE &SYSOUT(1),&SYSOUT(2),&SYSOUT(3),&SYSOUX
               T(4)'
.A3 AIF (T'&PRI EQ 'O').A4
 AIF (N'&PRI NE 2).E6
 AIF (&PRI(1) LT 0 OR &PRI(1) GT 13 OR &PRI(2) LT 0 OR &PRI(2) GT 13).EX
               6
&SGGJCB(1)  SETB   1
&SGGJCA(2) SETA &PRI(1)
&SGGJCA(3) SETA &PRI(2)
    MNOTE *,'      PRIORITY ASSIGNED FOR GRAPHIC JOBS IS &PRI(1)'
    MNOTE *,'      PRIORITY ASSIGNED FOR BATCH JOBS IS &PRI(2)'
.A4 AIF ('&REGION' EQ 'OMIT').A5
 AIF (N'&REGION NE 3).E7
 AIF ('&REGION(1)' EQ '').A41
 AIF (T'&REGION(1) NE 'N').E7
 AIF (&REGION(1) LT 1).E7         INVALID REGION
&SGGJCA(4) SETA &REGION(1)
  MNOTE *,'      REGION SIZE FOR GRAPHIC JOB PROCESSOR IS &REGION(1) '
.A41     AIF   ('&REGION(2)' EQ '').A42
         AIF   (T'&REGION(2) NE 'N').E7
         AIF (&REGION(2) LT 1).E7  INVALID REGION
&SGGJCA(5) SETA &REGION(2)
   MNOTE *,'      REGION SIZE FOR GRAPHIC JOBS IS &REGION(2)'
.A42     AIF   ('&REGION(3)' EQ '').A5
         AIF   (T'&REGION(3) NE 'N').E7
         AIF   (&REGION(3) LT 1).E7 INVALID REGION
&SGGJCA(6) SETA &REGION(3)
&SGGJCB(2) SETB 1
    MNOTE *,'      REGION SIZE FOR BATCH JOBS IS &REGION(3)'
.A5 AIF (T'&CLASS EQ 'O').A6
    AIF (N'&CLASS LT 2 OR N'&CLASS GT 3).E8  ONLY TWO OR THREE ALLOWED
     AIF (K'&CLASS(1) NE 1 OR K'&CLASS(2) NE 1).E8
&SGGJCB(3) SETB 1
&SGGJCC(2) SETC '&CLASS(1)&CLASS(2)'
    MNOTE *,'      JOB CLASS FOR GRAPHIC JOBS IS &CLASS(1)'
    MNOTE *,'      JOB CLASS FOR BATCH JOBS IS &CLASS(2)'
    AIF (N'&CLASS NE 3).A6                   ONLY TWO PARAMETERS
    AIF (K'&CLASS(3) NE 1).E8
    AIF (NOT &SGGJCB(7)).EJ             SGJP NOT SPECIFIED
&SGGJCB(8) SETB 1
    MNOTE *,'      JOB CLASS FOR INITIAL PROCESSOR(SGJP) IS &CLASS(3)'
&SGGJCC(2) SETC '&CLASS(1)&CLASS(2)&CLASS(3)'
.A6 AIF (N'&ALLOC GT 2).E9
&SGGJCA(7)  SETA  20
&SGGJCA(8) SETA 5
 AIF ('&ALLOC(1)' EQ '').A61
 AIF (T'&ALLOC(1) NE 'N').E9
 AIF ((&ALLOC(1) LT 1) OR (&ALLOC(1) GT 9999)).E9
&SGGJCA(7)  SETA  &ALLOC(1)
.A61 AIF ('&ALLOC(2)' EQ '').A62
 AIF (T'&ALLOC(2) NE 'N').E9
 AIF ((&ALLOC(2) LT 1) OR (&ALLOC(2) GT 9999)).E9  INCORRECT ALLOC
&SGGJCA(8) SETA &ALLOC(2)
.A62 ANOP
    MNOTE *,'      &SGGJCA(7) IS PRIMARY ALLOCATION FOR NEW DATA SETS'
    MNOTE *,'      &SGGJCA(8) IS SECONDARY ALLOCATION FOR NEW DATA SETSX
               '
.A7 AIF (K'&UNITREF GT 8).EA
 AIF ('&UNITREF'(1,1) LT 'A' OR '&UNITREF'(1,1) GT 'Z').EA
&SGGJCC(3) SETC '&UNITREF'
    MNOTE *,'      &UNITREF IS DEFAULT OPTION FOR DISPLAY UNIT REFERENCX
               E'
.AC AIF (T'&LINE NE 'N').ED
 AIF (&LINE LT 10 OR &LINE GT 99).ED
&SGGJCA(11) SETA &LINE
    MNOTE *,'      MAXIMUM LINES PER PAGE OF PRINTED RECORD IS &LINE '
.AD AIF (K'&DAPOOL GT 8).EE
&SGGJCC(10) SETC '&DAPOOL'
    MNOTE *,'      DEFAULT OPTION FOR DIRECT ACCESS POOL IS &DAPOOL'
.AE MEXIT                    THIS IS END OF PROCESSING IN MACRO
.E1 MNOTE 5,'* * * IEIGJC100 MACRO HAS BEEN PREVIOUSLY SPECIFIED'
&SGQUITB SETB 1
 AGO .END
.E2 MNOTE 5,'* * * IEIGJC101 UNITS PARAMETER NOT SPECIFIED'
&SGQUITB SETB 1
 AGO .A2
.E3 MNOTE 5,'* * * IEIGJC102 MAXIMUM OF 15 UNITS EXCEEDED'
&SGQUITB SETB 1
 AGO .A1
.E4 MNOTE 5,'* * * IEIGJC103 SYSOUT PARAMETER NOT SPECIFIED'
&SGQUITB SETB 1
 AGO .A3
.E5 MNOTE 5,'* * * IEIGJC104 SYSOUT VALUE &SYSOUT INVALID'
&SGQUITB SETB 1
 AGO .A3
.E6 MNOTE 5,'* * * IEIGJC105 PRI VALUE &PRI INVALID'
&SGQUITB SETB 1
 AGO .A4
.E7 MNOTE 5,'* * * IEIGJC106 REGION VALUE &REGION INVALID'
&SGQUITB SETB 1
 AGO .A5
.E8 MNOTE 5,'* * * IEIGJC107 CLASS VALUE &CLASS INVALID'
&SGQUITB SETB 1
 AGO .A6
.E9 MNOTE 5,'* * * IEIGJC108 ALLOC VALUE &ALLOC INVALID'
&SGQUITB SETB 1
 AGO .A7
.EA MNOTE 5,'* * * IEIGJC109 UNITREF VALUE &UNITREF INVALID'
&SGQUITB SETB 1
 AGO .AC
.ED MNOTE 5,'* * * IEIGJC112 LINE VALUE &LINE INVALID'
&SGQUITB SETB 1
 AGO .AD
.EE MNOTE 5,'* * * IEIGJC113 DAPOOL VALUE &DAPOOL INVALID'
&SGQUITB SETB 1
 AGO .AE
.EF MNOTE 5,'* * * IEIGJC114 TYPE VALUE &TYPE INVALID'
&SGQUITB SETB 1
 AGO .A1A2
.EG MNOTE 5,'* * * IEIGJC115 TYPE SGJP &TYPE(1) OR &TYPE(2) INVALID'
&SGQUITB SETB 1
 AGO .A1B1
.EH MNOTE 5,'* * * IEIGJC116 TYPE GJP &TYPE(1) OR &TYPE(2) INVALID'
&SGQUITB SETB 1
 AGO .A1A2
.EI MNOTE 5,'* * * IEIGJC117 LINEREF VALUE &LINEREF INVALID'
&SGQUITB SETB 1
 AGO .A1C
.EJ MNOTE 5,'* * * IEIGJC118 THIRD CLASS PARAMETER SPECIFIED WITHOUT   X
               SGJP'
&SGQUITB SETB 1
 AGO .A6
.END MEND
