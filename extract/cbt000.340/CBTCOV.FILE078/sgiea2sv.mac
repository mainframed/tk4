         MACRO
         SGIEA2SV
         COPY  SGGBLPAK
         LCLA  &STORLO,&STORHI,&STORGAP,&CURRLO,&CURRHI,&CURRGAP
         LCLA  &NUMBER,&RESSUM,&TRSUM,&I,&TYPE,&REGSAVE
         LCLA  &RESADD,&N,&M,&NUM(13),&TRADD,&ESA
         LCLC  &BLK,&SVCNO,&TYPEC
         LCLC  &CC,&C(14),&CCC(20),&NAM(4),&EXT(4),&TR(10),&LENGTH
&SGCTRLC(6) SETC 'IEASVC00'
         COPY  SGASMPAK
.*
.* COMPUTE NUMBER OF RESIDENT SVCS AND NUMBER OF TRANSIENT SVCS
.*
&I       SETA  1
&RESSUM  SETA  2         ONE ENTRY  FOR NOP
&BLK  SETC  '        '
   MNOTE *,' * * * * * * * * SVC TABLE * * * * * * * *'
   MNOTE *,'      NUMBER       TYPE    EXT. SAVE AREA '
   MNOTE *,'                                          '
   MNOTE *,'        0            1             0'
.NEXT1   ANOP
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
&REGSAVE SETA  (4*&SGSVCDB(&I)+2*&SGSVCEB(&I)+1*&SGSVCFB(&I))
     AIF   (&TYPE EQ 0).CONT1
&SVCNO  SETC  '&I'.'&BLK'(1,2)
&TYPEC  SETC  'NOP'
&ESA   SETA 0
    AIF  (&TYPE EQ 5).X0
&TYPEC  SETC  '&TYPE'
    AIF  (&TYPE EQ 1).X0
&ESA    SETA   &REGSAVE-1
.X0      ANOP
&TYPEC SETC '&TYPEC'.'&BLK'(1,2)
&TYPEC SETC '&TYPEC'(1,3)
&SVCNO   SETC  '&SVCNO'(1,3)
    MNOTE *,'        &SVCNO          &TYPEC           &ESA'
    AIF (&TYPE EQ 3 OR &TYPE EQ 4 OR &TYPE EQ 5).CONT1
&RESSUM  SETA  &RESSUM+1
.CONT1   ANOP
&I       SETA  &I+1
         AIF   (&I LE 255).NEXT1
&TRADD   SETA  &RESSUM+1
&IEALMTA SETA  &RESSUM+1
.*
.* COMPUTE LARGEST GAP IN SVC NUMBERS
.*
&I       SETA  0
.NEXT3   ANOP
&I       SETA  &I+1
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
         AIF   (&TYPE NE 0).NOGAP
&STORGAP SETA  &STORGAP+1
&STORHI  SETA  0
         AIF   (&I LT 255).NEXT3
         AIF   (&STORGAP LT &CURRGAP).CONT3
&CURRLO  SETA  &STORLO
&CURRHI  SETA  256
         AGO   .CONT3
.NOGAP   ANOP
&STORHI  SETA  &I
         AIF   (&STORGAP LT &CURRGAP).CONT2
&CURRLO  SETA  &STORLO
&CURRHI  SETA  &STORHI
&CURRGAP SETA  &STORGAP
.CONT2   ANOP
&STORGAP SETA  0
&STORLO  SETA  &I
         AIF   (&I LT 255).NEXT3
.CONT3   ANOP
&IEAHIA  SETA  &CURRLO+1
&IEALOA  SETA  &CURRHI-1
.*
.* SET UP PREFIX TABLE FOR IBM SVC'S
.*
         PUNCH 'IHASVC00 CSECT'
         PUNCH '         ENTRY SVPRFX,SVCTBL,SVLMT'
         PUNCH 'NOPEXIT  SR    15,15   COMPLETION CODE = ZERO'
         PUNCH '         BR    14      RETURN'
         PUNCH 'SVPRFX   DS    0C'
&RESADD  SETA  3
&I       SETA  1
&NUM(1)  SETA  1
&N       SETA  2
.NEXT3A  ANOP
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
         AIF   (&TYPE EQ 1 OR &TYPE EQ 2).RESPFX1
         AIF   (&TYPE EQ 3 OR &TYPE EQ 4).TRPFX1
         AIF   (&TYPE EQ 5).NOP1
&NUM(&N) SETA  0
         AGO   .CONT3A
.RESPFX1 ANOP
&NUM(&N) SETA  &RESADD
&RESADD  SETA  &RESADD+1
         AGO   .CONT3A
.TRPFX1  ANOP
&NUM(&N) SETA  &TRADD
&TRADD   SETA  &TRADD+1
         AGO   .CONT3A
.NOP1    ANOP
&NUM(&N) SETA  2
.CONT3A  ANOP
&N       SETA  &N+1
         AIF   (&N LT 14).CONT3B
&N       SETA  1
         PUNCH '         DC    AL1(&NUM(1),&NUM(2),&NUM(3),&NUM(4),&NUM*
               (5),&NUM(6),&NUM(7),&NUM(8),&NUM(9),&NUM(10),&NUM(11),&N*
               UM(12),&NUM(13))'
.CONT3B  AIF   ((&I+1) GE &IEAHIA).CONT3B1
&I     SETA    &I+1
   AGO   .NEXT3A
.*
.* SET UP PREFIX TABLE FOR USER SVC'S
.*
.CONT3B1 AIF   ((&I+1) NE &IEAHIA).CONT7B
         AIF   (&IEALOA EQ 255).NEXT8
&I       SETA  255
         AGO   .NEXT3A
.CONT7B  ANOP
&I       SETA  &I-1
         AIF   ((&I-1) GE &IEALOA).NEXT3A
.NEXT8   ANOP
         AIF   (&N EQ 1).NEXT8A3
&NUMBER  SETA  &N-1
&N       SETA  1
.NEXT8A0 ANOP
         AIF   (&NUMBER EQ &N).NOCOMMA
&CC      SETC  ','
         AGO   .NEXT8A1
.NOCOMMA ANOP
&CC      SETC  ''
.NEXT8A1 ANOP
&C(&N)   SETC  '&NUM(&N)'.'&CC'
&N       SETA  &N+1
         AIF   (&N LE &NUMBER).NEXT8A0
.NEXT8A2 ANOP
&C(&N)   SETC  ''
&N       SETA  &N+1
         AIF   (&N LT 15).NEXT8A2
         PUNCH '         DC    AL1(&C(1)&C(2)&C(3)&C(4)&C(5)&C(6)&C(7)&*
               C(8)&C(9)&C(10)&C(11)&C(12)&C(13)&C(14))'
.NEXT8A3 ANOP
&N       SETA  2
&M       SETA  1
.*
.* GENERATE RESIDENT PORTION OF SVC TABLE
.*
         AIF   (&SGSUPRB(3) EQ 1).CONT27
         PUNCH 'SVCTBLX  DS    0C'
         PUNCH 'SVCTBL   EQU   SVCTBLX-3'
         AGO   .CONT28
.CONT27  ANOP
         PUNCH 'SVCTBLX  DS    0F'
         PUNCH 'SVCTBL   EQU   SVCTBLX-4'
.CONT28  ANOP
&NAM(1)  SETC  'IGC000+0'
&NAM(2)  SETC  'NOPEXIT'
&CCC(2)  SETC  ','
&EXT(1)  SETC  'IGC000'
&C(1)    SETC  ','
&CCC(1) SETC ','
&I       SETA  1
.NEXT9   ANOP
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
&REGSAVE SETA  (4*&SGSVCDB(&I)+2*&SGSVCEB(&I)+1*&SGSVCFB(&I))
         AIF   (&TYPE EQ 3 OR &TYPE EQ 4).NOADCON
         AIF   (&TYPE EQ 0 OR &TYPE EQ 5).NOADCON
&N       SETA  &N+1
&CCC(&N) SETC  ','
&M       SETA  &M+1
         AIF   (&I LT 10).CONT9A
         AIF   (&I LT 100).CONT9B
&NAM(&N) SETC  'IGC'.'&I'.'+'.'&REGSAVE'
&EXT(&M) SETC  'IGC'.'&I'
&C(&M)   SETC  ','
         AGO   .CONT9C
.CONT9A  ANOP
&NAM(&N) SETC  'IGC00'.'&I'.'+'.'&REGSAVE'
&EXT(&M) SETC  'IGC00'.'&I'
&C(&M)   SETC  ','
         AGO   .CONT9C
.CONT9B  ANOP
&NAM(&N) SETC  'IGC0'.'&I'.'+'.'&REGSAVE'
&EXT(&M) SETC  'IGC0'.'&I'
&C(&M)   SETC  ','
         AGO   .CONT9C
.CONT9C  ANOP
         AIF   (&N LT 4).NOADCON
&N       SETA  0
         AIF   (&SGSUPRB(3) EQ 1).CONT29
         PUNCH '         DC    AL3(&NAM(1),&NAM(2),&NAM(3),&NAM(4))'
         AGO  .CONT30
.CONT29  ANOP
         PUNCH '         DC    A(&NAM(1),&NAM(2),&NAM(3),&NAM(4))'
.CONT30  ANOP
         AIF   (&M EQ 0).NOADCON
.CONT9D  AIF   (&M LT 4).BLANK
         PUNCH '         EXTRN &EXT(1)&C(1)&EXT(2)&C(2)&EXT(3)&C(3)&EXT*
               (4)'
&M       SETA  0
         AGO   .NOADCON
.BLANK   ANOP
&C(&M)   SETC  ''
&M       SETA  &M+1
&EXT(&M) SETC  ''
&C(&M)   SETC  ''
         AGO   .CONT9D
.NOADCON ANOP
         AIF   ((&I+1) EQ &IEAHIA).CONT10
         AIF   ((&I+1) GT &IEAHIA).CONT12
&I       SETA  &I+1
         AGO   .NEXT9
.CONT10  ANOP
         AIF   (&IEALOA EQ 255).CONT14
&I       SETA  255
         AGO   .NEXT9
.CONT12  ANOP
&I       SETA  &I-1
         AIF   ((&I-1) GE &IEALOA).NEXT9
.CONT14  ANOP
         AIF   (&N EQ 0).CONT15
&CCC(&N) SETC  ''
.CONT14B ANOP
&N       SETA  &N+1
&CCC(&N) SETC  ''
&NAM(&N) SETC  ''
         AIF   (&N LT 4).CONT14B
         AIF   (&SGSUPRB(3) EQ 1).CONT31
         PUNCH '         DC    AL3(&NAM(1)&CCC(1)&NAM(2)&CCC(2)&NAM(3)&*
               CCC(3)&NAM(4))'
         AGO   .CONT32
.CONT31  ANOP
         PUNCH '         DC      A(&NAM(1)&CCC(1)&NAM(2)&CCC(2)&NAM(3)&*
               CCC(3)&NAM(4))'
.CONT32  ANOP
         AIF   (&M EQ 0).CONT15
&C(&M)   SETC  ''
.CONT14C ANOP
&M       SETA  &M+1
&EXT(&M) SETC  ''
&C(&M)   SETC  ''
         AIF   (&M LT 4).CONT14C
         PUNCH '         EXTRN &EXT(1)&C(1)&EXT(2)&C(2)&EXT(3)&C(3)&EXT*
               (4)'
.CONT15  ANOP
.*
.* GENERATE TABLE FOR TRANSIENT SVC'S
.*
&LENGTH  SETC  'L1'
         AIF   (&SGSUPRB(3) NE 1).CONT15A
&LENGTH  SETC  ''
         PUNCH 'SVLMT    DS    0F'
         AGO   .CONT15B
.CONT15A ANOP
         PUNCH 'SVLMT    DS    0C'
.CONT15B ANOP
&I       SETA  1
&N       SETA  0
.NEXT10  ANOP
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
&REGSAVE SETA  (4*&SGSVCDB(&I)+2*&SGSVCEB(&I)+1*&SGSVCFB(&I))
         AIF   (&TYPE EQ 1 OR &TYPE EQ 2 OR &TYPE EQ 5).CONT16
         AIF   (&TYPE EQ 0).CONT16
&N       SETA  &N+1
&CCC(&N) SETC  ','
&TR(&N)  SETC  '&REGSAVE'
         AIF   (&N LT 10).CONT16
         PUNCH '         DC    A&LENGTH.(&TR(1),&TR(2),&TR(3),&TR(4),&T*
               R(5),&TR(6),&TR(7),&TR(8),&TR(9),&TR(10))'
&N       SETA  0
.CONT16  ANOP
         AIF   ((&I+1) EQ &IEAHIA).CONT18
         AIF   ((&I+1) GT &IEAHIA).CONT20
&I       SETA  &I+1
         AGO   .NEXT10
.CONT18  ANOP
         AIF   (&IEALOA EQ 255).CONT22
&I       SETA  255
         AGO   .NEXT10
.CONT20  ANOP
&I       SETA  &I-1
         AIF   ((&I-1) GE &IEALOA).NEXT10
.CONT22  AIF   (&N EQ 0).CONT26
&CCC(&N) SETC  ''
.CONT25  ANOP
&N       SETA  &N+1
         AIF   (&N GT 10).CONT25A
&CCC(&N) SETC  ''
&TR(&N)  SETC  ''
         AGO   .CONT25
.CONT25A ANOP
         PUNCH '         DC    A&LENGTH.(&TR(1)&CCC(1)&TR(2)&CCC(2)&TR(*
               3)&CCC(3)&TR(4)&CCC(4)&TR(5)&CCC(5)&TR(6)&CCC(6)&TR(7)&C*
               CC(7)&TR(8)&CCC(8)&TR(9)&CCC(9)&TR(10))'
.CONT26  ANOP
         PUNCH '         END'
         PUNCH '/*'
         MEND
