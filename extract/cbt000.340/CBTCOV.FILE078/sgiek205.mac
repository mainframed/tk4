         MACRO
         SGIEK205
.*  SGIEH205 PUNCHES A CALL TO SGIEK005 WHICH BUILDS THE UNIT
.*   ASSIGNMENT TABLE FOR FORTRAN E/G/H AND OPTION TABLE FOR
.*   FORTRAN G AND H
         COPY  SGGBLPAK            DEFINE SYSGEN GLOBALS
         LCLA  &UTAB,&OBER#,&ONRD#,&ONPRT#,&ONNPH#,&OBER,&ONRD,&ONNPH
         LCLA  &C,&ERR,&ADJST
&UTAB    SETA  &SGFORLA(1)         NUMBER OF LOGICAL UNITS.
&OBER#   SETA  &SGFORLA(2)         OBJERR LOGICAL UNIT.
         AIF   (&SGFORLB(3) OR &SGFORLB(2)).HPRSNT TEST FOR G OR H.
&ONRD#   SETA  5                   READ DATA SET REFERENCE NUMBER
&ONPRT#  SETA  6                   PRINT DSRN.
&ONNPH#  SETA  7                   PUNCH DSRN.
&OBER    SETA  3                   OBJERR-PRINT FOR E.
&ONRD    SETA  1                   READ FOR E-SYSIN
&ONNPH   SETA  2                   PUNCH FOR E-SYSOUT
         AGO   .JCL
.HPRSNT  ANOP
&ONRD#   SETA  &SGFORLA(3)         ONLNRD FOR H.
&ONRD    SETA  5                  ONLNRD FOR H.
&ONPRT#  SETA  &SGFORLA(2)         IDENTICAL TO OBJERR FOR H.
&ONNPH#  SETA  &SGFORLA(4)         ONLNPCH FOR H.
&ONNPH   SETA  7                  ONLNPCH FOR H.
&OBER    SETA  6                  OBJERR FOR H.
.JCL     ANOP
&SGCTRLC(6)    SETC 'IEK205'       SET MODULE MEMBER NAME
         COPY  SGASMPAK            PROVIDE JCL FOR ASSEMBLY
         PUNCH '         SGIEK005   &UTAB,&OBER#,&ONRD#,&ONPRT#,&ONNPH#*
               ,&OBER,&ONRD,&ONNPH'
         PUNCH '         END'
         PUNCH '/*'
         AIF   (&SGFORLB(1)).END  IF ELIB DO NOT ASSEMBLE IHCUOPT
&C       SETA  &SGFORLA(5)
         AIF   (NOT &SGFORLB(5)).ADJ
&ERR     SETA   1
.ADJ     ANOP
         AIF   (NOT &SGFORLB(4)).ASM
&ADJST   SETA  1
.ASM     ANOP
&SGCTRLC(6) SETC  'IEK206'
         COPY  SGASMPAK
     PUNCH  '    SGIEK006  &C,&ERR,&ADJST                        '
     PUNCH  '    END                                             '
     PUNCH  '/*'
.END     ANOP
         MEND
