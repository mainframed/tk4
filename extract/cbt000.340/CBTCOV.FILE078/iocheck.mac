       MACRO
       IOCHECK &LOOKFOR=
       COPY    SGGBLPAK
 LCLA &LB,&HB,&I,&ADDR,&TB,&C,&FB,&TABDD,&COMEDD
         ACTR  50000
         AIF   (&SGCNTRA(3) EQ 0).BAD   NO IO DEVICES GIVEN
&C     SETA    &LOOKFOR
&LB    SETA    1             LOWER BOUNDARY INDEX
&HB    SETA    &SGCNTRA(3)   HIGHER BOUNDARY INDEX
 AIF (&SGCTRLB(1)).LOOPSER  DO SERIAL SEARCH FOR OPTCHAN ENTRY
.*   IF  C  OUTSIDE  BOUNDARIES  OF  TABLE  TRANSFERT  TO  .BAD
       AIF     (&C GT (&SGDADRA(&HB)-&SGDADRA(&HB)/4096*4096)).BAD
       AIF     (&C LT (&SGDADRA(1)-&SGDADRA(1)/4096*4096)).BAD
.LOOP  AIF     (&LB GE &HB).BAD  NO ENTRY FOR THIS ADDR IN TABLE
&TB    SETA    (&SGDADRA(&HB)-&SGDADRA(&HB)/4096*4096)   TOP BOUNDARY
&FB    SETA    (&SGDADRA(&LB)-&SGDADRA(&LB)/4096*4096)   LOWER BOUNDARY
&I     SETA    (&LB+&HB)/2             INDEX  TO  MIDDLE  TABLE
&ADDR  SETA    (&SGDADRA(&I)-&SGDADRA(&I)/4096*4096)     MIDDLE ADDRESS
       AIF     (&C EQ &TB).HBX
       AIF     (&C EQ &ADDR).IHX
 AGO .NOPTCH              SKIP OPTCHAN ROUTINE
.LOOPSER AIF (&LB GT &HB).BAD          ADDRESS NOT FOUND
&ADDR SETA (&SGDADRA(&LB)-&SGDADRA(&LB)/4096*4096)
 AIF (&C EQ &ADDR).IHX
 AIF (&LB EQ &SGCTRLA(2)).INDEX
&TABDD SETA &ADDR-&ADDR/X'100'*X'100'
&COMEDD SETA &C-&C/X'100'*X'100'
&I SETA &SGDADRA(&LB)/1048576   GET DEVICE POINTER
 AIF (NOT &SGDAP1B(&I)).OPT2            OPTCHAN 1
 AIF (&C/X'100' EQ 1).CUDEV             OPTCHAN AND DEV. CHAN 1
.OPT2 AIF (NOT &SGDAP2B(&I)).OPT3       OPTCHAN 2
 AIF (NOT &SGOPSUB(&I)).NOTSUB1        NOT SELECTOR SUB CHANNEL
&HEX SETA X'D'
 AIF (&COMEDD/X'10' EQ &HEX).CHAN      DUPLICATE C.U. POSITION
 AGO .OPT3
.NOTSUB1 ANOP
 AIF (&C/X'100' EQ 2).CUDEV             OPTCHAN AND DEV. CHAN 2
.OPT3 AIF (NOT &SGDAP3B(&I)).OPT4       OPTCHAN 3
 AIF (NOT &SGOPSUB(&I)).NOTSUB2        NOT SELECTOR SUB CHANNEL
&HEX SETA X'E'
 AIF (&COMEDD/X'10' EQ &HEX).CHAN      DUPLICATE C.U. POSITION
 AGO .OPT4
.NOTSUB2 ANOP
 AIF (&C/X'100' EQ 3).CUDEV             OPTCHAN AND DEV. CHAN 3
.OPT4 AIF (NOT &SGDAP4B(&I)).OPT5       OPTCHAN 4
 AIF (NOT &SGOPSUB(&I)).NOTSUB3        NOT SELECTOR SUB CHANNEL
&HEX SETA X'F'
 AIF (&COMEDD/X'10' EQ &HEX).CHAN      DUPLICATE C.U. POSITION
 AGO .INDEX
.NOTSUB3 ANOP
 AIF (&C/X'100' EQ 4).CUDEV             OPTCHAN AND DEV. CHAN 4
.OPT5 AIF (NOT &SGDAP5B(&I)).OPT6       OPTCHAN 5
 AIF (&C/X'100' EQ 5).CUDEV             OPTCHAN AND DEV. CHAN 5
.OPT6 AIF (NOT &SGDAP6B(&I)).OPT7       NOT OPTCHAN 6
 AIF (&C/X'100' EQ 6).CUDEV             OPTCHAN AND DEV. CHAN 6
.OPT7 AIF (NOT &SGDAP7B(&I)).OPT8       NOT OPTCHAN 7
 AIF (&C/X'100' EQ 7).CUDEV             OPTCHAN AND DEV. CHAN 7
.OPT8 AIF (NOT &SGDAP8B(&I)).OPT9       NOT OPTCHAN 8
 AIF (&C/X'100' EQ 8).CUDEV             OPTCHAN AND DEV. CHAN 8
.OPT9 AIF (NOT &SGDAP9B(&I)).OPTA       NOT OPTCHAN 9
 AIF (&C/X'100' EQ 9).CUDEV             OPTCHAN AND DEV. CHAN 9
.OPTA AIF (NOT &SGDAPAB(&I)).OPTB       NOT OPTCHAN 10
 AIF (&C/X'100' EQ 10).CUDEV            OPTCHAN AND DEV. CHAN 10
.OPTB AIF (NOT &SGDAPBB(&I)).OPTC       NOT OPTCHAN 11
 AIF (&C/X'100' EQ 11).CUDEV            OPTCHAN AND DEV. CHAN 11
.OPTC AIF (NOT &SGDAPCB(&I)).OPTD       NOT OPTCHAN 12
 AIF (&C/X'100' EQ 12).CUDEV            OPTCHAN AND DEV. CHAN 12
.OPTD AIF (NOT &SGDAPDB(&I)).OPTE       NOT OPTCHAN 13
 AIF (&C/X'100' EQ 13).CUDEV            OPTCHAN AND DEV. CHAN 13
.OPTE AIF (NOT &SGDAPEB(&I)).OPTF       NOT OPTCHAN 14
 AIF (&C/X'100' EQ 14).CUDEV            OPTCHAN AND DEV. CHAN 14
.OPTF AIF (NOT &SGDAPFB(&I)).INDEX      NOT OPTCHAN 15
 AIF (&C/X'100' EQ 15).CUDEV            OPTCHAN AND DEV. CHAN 15
 AGO .INDEX
.CHAN AIF (&ADDR/X'100' NE &C/X'100').INDEX  CHANNELS NOT THE SAME
&TABDD SETA &TABDD-&TABDD/X'10'*X'10'  DEVICE PORTION
&COMEDD SETA &COMEDD-&COMEDD/X'10'*X'10'    COMPARISON
.CUDEV AIF (&TABDD EQ &COMEDD).IHX
.INDEX  ANOP       MOVE TO NEXT ENTRY
&LB SETA &LB+1
 AGO .LOOPSER
.NOPTCH AIF (&C EQ &FB).LBX
       AIF     (&C  LT &ADDR).CHGBD         C  NOT  FOUND
.*              REDUCE  SIZE  OF  TABLE
&LB    SETA    &I+1                    LEAVE  LOWER  HALF  OF  TABLE
       AGO     .LOOP
.CHGBD ANOP
&HB    SETA    &I-1                    LEAVE  HIGHER  HALF  OF  TABLE
       AGO     .LOOP
.BAD   ANOP              EXIT  ADDR  NOT  FOUND  IN  TABLE
&HEX   SETA    0
       AGO     .MEND
.HBX   ANOP
&HEX   SETA    &HB      SET HEX TO VALUE OF HIGHER INDEX
       AGO     .MEND
.IHX   ANOP
 AIF (NOT &SGCTRLB(1)).SETIHX           ENTERED FOR OPTCHAN
&HEX SETA &ADDR     SET TO ADDRESS SHOWING CONFLICT
 AGO .MEND
.SETIHX ANOP
&HEX   SETA    &I     SET HEX TO VALUE OF MIDDLE INDEX
       AGO     .MEND
.LBX   ANOP
&HEX   SETA    &LB       SET HEX TO VALUE OF LOWER INDEX
.MEND  MEND
