         MACRO
         SGIFC600 &RESTYPE                                       S21021
         COPY  SGGBLPAK                                          S21021
         LCLA  &A,&B,&C,&D,&E,&F                                 S21021
.* THIS MACRO ALLOCATES SPACE FOR THE SYS1.LOGREC DEPENDENT UPON S21021
.* THE DEVICE WHERE IT IS TO RESIDE.                             S21021
.* THE SYS1.ASRLIB IS ALSO ALLOCATED.                            S21021
.*    -COMPLETE REPLACEMENT                                      S21021
&A       SETA  &SGCNTRA(3)+&SG2314A     .TOTAL STAT CTRS IN SYS  S21021
.START   AIF   ('&RESTYPE' EQ '2311').D2311    .SYSRES 2311      S21021
         AIF   ('&RESTYPE' EQ '2301').D2301    .SYSRES 2301      S21021
         AIF   ('&RESTYPE' EQ '2303').D2303    .SYSRES 2303      S21021
         AIF   ('&RESTYPE' EQ '2302').D2302    .SYSRES 2302      S21021
         AIF   ('&RESTYPE' EQ '2305-1').D23051 .SYSRES 2305 MOD 1S21021
         AIF   ('&RESTYPE' EQ '2305-2').D23052 .SYSRES 2305 MOD 2S21021
         AIF   ('&RESTYPE' EQ '2314').D2314    .SYSRES 2314      S21021
         AIF   ('&RESTYPE' EQ '3330').D3330    .SYSRES 3330      S21021
 MNOTE 5,'* * * IEIFFC600 DEVICE NOT FOUND'                      S21021
&SGQUITB SETB  1                        .ABEND SYSGEN            S21021
         MEXIT                                                   S21021
.D2311   ANOP                      .2311                         S21021
&B       SETA  25                       .DEVICES/TRACK           S21021
&C       SETA  35                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D2301   ANOP                      .2301                         S21021
&B       SETA  85                       .DEVICES/TRACK           S21021
&C       SETA  6                        .TRACK ALLOC 125K        S21021
&D       SETA  4                        .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D2303   ANOP                      .2303                         S21021
&B       SETA  25                       .DEVICES/TRACK           S21021
&C       SETA  26                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D2302   ANOP                      .2302                         S21021
&B       SETA  35                       .DEVICES/TRACK           S21021
&C       SETA  25                       .TRACK ALLOC 125K        S21021
&D       SETA  17                       .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D23051  ANOP                     .2305 MOD 1                    S21021
&B       SETA  20                       .DEVICE/TRACK
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D23052  ANOP                      .2305 MOD 2                   S21021
&B       SETA  40                       .DEVICES/TRACK           S21021
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D2314   ANOP                      .2314                         S21021
&B       SETA  30                       .DEVICES/TRACK           S21021
&C       SETA  17                       .TRACK ALLOC 125K        S21021
&D       SETA  11                       .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP                             .CONTINUE       S21021
.D3330   ANOP                      .3330                         S21021
&B       SETA  50                       .DEVICES/TRACK           S21021
&C       SETA  10                       .TRACK ALLOC 125K        S21021
&D       SETA  5                        .TRACK ALLOC-RMS/SER1    S21021
         AGO   .LOOP           .CONTINUE                         S21021
.LOOP    ANOP                                                    S21021
&A       SETA  (&A+&B-1)/&B             .NUMBER TRKS FOR OVERFLOWS21021
         AIF   (&SGCPUB(13) EQ 1).BYPASS  .MODEL 370/195         S99220
         AIF   (&SGCPUB(9) EQ 1).BYPASS .MODEL 195               S21021
         AIF   (&SGCPUB(6) EQ 1).BYPASS .MODEL 91/95             S21021
         AIF   (&SGSUPRB(28) EQ 1).MCH  .MCH SUPPORT             S21021
&D       SETA  0                        .TRACK ALLOC-NO RMS/SER1 S21021
.MCH     ANOP                                                    S21021
&F       SETA  &D                       .TRACK ALLOC SYS1.ASRLIB S21021
.BYPASS  ANOP                                                    S21021
         CONVERT TO=EBCDIC,DIGITS=3,VALUE=&SGBSCA .TERMS/2715    S21021
&E       SETA  &EBCDIC/100                   .ONE TRACK/100 TERMSS21021
&A       SETA  &A+&C+&D+&E                   .TOTAL TRACKS       S21021
&SGCTRLA(1) SETA &SGCTRLA(1)+1     .ADD ONE TO STEP NAME         S21021
         JOBSEP     TYPE=LK
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IFCDIP00,COND=(8,LT)'
 PUNCH '//STEPLIB DD   DSN=SYS1.CI505,DISP=SHR'                 SA58007
 PUNCH '//SERERDS DD   DSN=&SGCTRLC(40)..LOGREC,DISP=(NEW,KEEP),       X
                            XXXXXXXXX'                           S21021
 PUNCH '//             VOLUME=(,RETAIN,SER=&SGCTRLC(30)),              X
                           XXXXXXXXX'                            S21021
 PUNCH '//             SPACE=(TRK,(&A),,CONTIG),UNIT=&SGCTRLC(29)'
         AIF   (&SGSUPRB(28) EQ 0).MCHNO     .NOT MCH SUPPORT    S21021
 PUNCH '//ASRFRSH DD   DSNAME=&SGCTRLC(40)..ASRLIB,DISP=(NEW,KEEP),    X
                            XXXXXXXXX'                           S21021
 PUNCH '//             VOLUME=(,RETAIN,SER=&SGCTRLC(30)),              X
                           XXXXXXXXX'                            S21021
 PUNCH '//             SPACE=(TRK,(&F),,CONTIG),UNIT=&SGCTRLC(29)'
.MCHNO   ANOP                                                    S21021
         JOBSEP     TYPE=LK
&SGCTRLA(1) SETA &SGCTRLA(1)+1     .ADD ONE TO STEP NAME         S21021
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IEHPROGM,COND=(8,LT)'
 PUNCH '//TARG DD VOLUME=SER=&SGCTRLC(30),DISP=OLD,UNIT=&SGCTRLC(29)'
 PUNCH '//SYSPRINT DD SYSOUT=A&SGCTRLC(46)&SGCTRLC(47)&SGCTRLC(48)'
 PUNCH '//SYSIN DD *'                                            S21021
 PUNCH ' CATLG CVOL=&SGCTRLC(29)=RRNIPL,VOL=&SGCTRLC(29)=RRNIPL,DSNAME=X
               SYS1.LOGREC'                               S21021
         AIF   (&SGSUPRB(28) EQ 0).MCHNOT  .NOT MCH SUPPORTED    S21021
 PUNCH ' CATLG CVOL=&SGCTRLC(29)=&SGCTRLC(30),VOL=&SGCTRLC(29)=&SGCTRLCX
               (30),DSNAME=SYS1.ASRLIB'                          S21021
.MCHNOT  ANOP                                                    S21021
 PUNCH '/*'                                                      S21021
.END     MEND
