         MACRO
         UNITNAME   &UNIT=,&NAME=
         COPY  SGGBLPAK
         GBLA &B,&LIMITA
         LCLA  &A,&N,&LENGTH
         LCLA  &LIMITB
         LCLB  &RTNSW,&MULTAD
         LCLC  &C,&D,&E,&ADDR
         AIF   (T'&NAME EQ 'O' OR T'&UNIT EQ 'O').E8
  AIF  (&SGMENTB(36)).MAX4
&SGMENTB(36)  SETB   1      UNITNAME ENTRY SWITCH.
&B SETA 255   MAX. NO. OF NAME A-BOXES
  AIF  (&LIMIT(1)).MAX1
 AIF (&LIMIT(3)).MAX3
&SGMAXA(3)  SETA  50
&LIMITA  SETA  510
   AGO  .MX
.MAX1  ANOP
&SGMAXA(3)  SETA  100
&LIMITA  SETA  510
   AGO  .MX
.MAX3 ANOP
&SGMAXA(3) SETA 100
&LIMITA  SETA  1028
&B SETA 514   MAX. NO. OF NAME A-BOXES FOR 768 DEV. SUPPORT
.MX MNOTE *,'      MAXIMUM OF &SGMAXA(3) UNIT NAMES ALLOWED'
    MNOTE *,'      THE MAXIMUM NO. OF UNITS ALLOWED IS &B*2-THE NUMBER *
               OF NAMES'
.MAX4  ANOP
 AIF (K'&NAME LT 9 AND K'&NAME GT 0).TESTNM
 AGO .BAD
.TESTNM AIF (&A EQ K'&NAME).DONENM
&A SETA &A+1
&C SETC '&NAME'(&A,1)
 AIF (('&C' GE 'A' AND '&C' LE 'I') OR ('&C' GE 'J' AND '&C' LE 'R') OR*
               ('&C' GE 'S' AND '&C' LE 'Z') OR ('&C' GE '0' AND '&C' L*
               E '9')).TESTNM
 AIF ('&C' EQ '@' OR '&C' EQ '$' OR '&C' EQ '#' OR '&C' EQ '/' OR '&C' *
               EQ '-').TESTNM
.BAD MNOTE 5,'* * * IEIUNI104 NAME VALUE &NAME INVALID'
&SGQUITB SETB 1
 AGO .MEND
.DONENM  ANOP
&A SETA 0
.ON0     AIF   (&SGCNTRA(4) GE 1).ON1  HAS MACRO BEEN ENTERED BEFORE
         AGO   .INITIAL
.ON1  ANOP
         AIF   ('&SGUNNMC(&SGCNTRA(4))' EQ '&NAME').ON3
     AIF  (&SGCNTRA(4) GE &SGMAXA(3)).E1
&LIMITB  SETA  (&LIMITA-&SGCNTRA(4))          FIND MAX DEVICES AVAIBLE
         AIF   (&SGCNTRA(5) GT &LIMITB).E9    MAX EXCEDED
 AIF (&SGCNTRA(5) GE &B).E2            MAX. NO. OF UNITS EXCEEDED
&SGCNTRA(4)    SETA &SGCNTRA(4)+1  COUNT OF NAMES.
&SGUNNMC(&SGCNTRA(4))  SETC  '&NAME'
    MNOTE *,'     &NAME IS THE SYMBOLIC NAME ASSOCIATED WITH THE'
    MNOTE *,'        DEVICES HAVING THE FOLLOWING ADDRESSES'
         AIF   (&SGCNTRA(6) LT 3).FILLIN
&SGCNTRA(5)    SETA &SGCNTRA(5)+1  COUNT OF SETC'S IN USE FOR ADDRESSES
&SGCNTRA(6)    SETA 1              SET FLIP SWITCH TO ONE.
.FILLIN  ANOP              FFF MEANS END OF ONE NAME'S ADDRESSES.
&SGUNNMA(&SGCNTRA(5))  SETA 4096*&SGUNNMA(&SGCNTRA(5))+X'FFF'
&SGCNTRA(6)    SETA &SGCNTRA(6)+1
         AGO   .ON2
.INITIAL ANOP            INITIALIATION ROUTINE.
&SGCNTRA(4)    SETA 1         SET NAME COUNTER TO ONE.
&SGCNTRA(5)    SETA 1         SET ADDRESS COUNTER TO ONE.
&SGCNTRA(6)    SETA 1         SET FLIP MECHANISM TO ONE.
&SGUNNMC(1)  SETC  '&NAME'
    MNOTE *,'     &NAME IS THE SYMBOLIC NAME ASSOCIATED WITH THE'
    MNOTE *,'        DEVICES HAVING THE FOLLOWING ADDRESSES'
.ON3     AIF   (&RTNSW).LOOP           IF ON, GENERATE MULTIPLE ADDRESS
         AIF   (N'&UNIT EQ 0).E7
         AIF   (&A EQ N'&UNIT).MEND
&A       SETA  &A+1
         AIF   (K'&UNIT(&A) LT 3).E3
         AIF   (K'&UNIT(&A) GT 3).MULT  IF GT, CHECK FOR MULT ADDRESS
&C       SETC  '&UNIT(&A)'(1,1)
&D       SETC  '&UNIT(&A)'(2,1)
&E       SETC  '&UNIT(&A)'(3,1)
         AGO   .AIF
.MULT    ANOP
&MULTAD  SETB  1
&LENGTH  SETA  K'&UNIT(&A)             LENGTH OF MULT ADDRESS OPERAND
         AIF   (&LENGTH LE 6).E4
         AIF   ('&UNIT(&A)'(5,1) NE ',').E4
&LENGTH  SETA  &LENGTH-6
         AIF   (&LENGTH GT 3).E5
&N       SETA  &LENGTH
.TEST    AIF   (&N EQ 0).VALID
&C       SETC  '&UNIT(&A)'(&N+5,1)     CHECK VALIDITY OF INCREMENTAL
         AIF   (NOT('&C' GE '0' AND '&C' LE '9')).E5
&N       SETA  &N-1
         AGO   .TEST
.VALID   ANOP
&RTNSW   SETB  1
&C       SETC  '&UNIT(&A)'(6,&LENGTH)
&N       SETA  &C
&ADDR    SETC  '&UNIT(&A)'(2,3)        ADDRESS TO BE INCREMENTED
.LOOP    ANOP
&C       SETC  '&ADDR'(1,1)
&D       SETC  '&ADDR'(2,1)
&E       SETC  '&ADDR'(3,1)
.AIF     ANOP
 AIF (NOT(('&C' GE '0' AND '&C' LE '9')OR('&C' GE 'A' AND '&C' LE 'F'))*
               ).E3
 AIF (NOT(('&D' GE '0' AND '&D' LE '9')OR('&D' GE 'A' AND '&D' LE 'F'))*
               ).E3
 AIF (NOT(('&E' GE '0' AND '&E' LE '9')OR('&E' GE 'A' AND '&E' LE 'F'))*
               ).E3
         AIF   (&MULTAD).CONV
         CONVERT TO=HEX,DIGITS=3,VALUE=&UNIT(&A)  GET HEX. VALUE AND
 AIF     (&HEX EQ X'FFF').E3           NO VALID FFF ADDRESS
         AGO   .MAX
.CONV    CONVERT    TO=HEX,DIGITS=3,VALUE=&ADDR      GET HEX VALUE
         AIF   (&HEX EQ X'FFF').E4
.MAX     AIF   (&SGCNTRA(5) GE &B).E9        MAX. NO. OF UNITS EXCEEDED
&SGUNNMA(&SGCNTRA(5)) SETA 4096*&SGUNNMA(&SGCNTRA(5))+&HEX ADD IT IN.
         AIF   (&MULTAD).AD
    MNOTE *,'        &UNIT(&A)'
         AGO   .INC
.AD MNOTE *,'        &ADDR'
&N       SETA  &N-1
         AIF   (&N NE 0).SET
&RTNSW   SETB  0
&MULTAD  SETB  0
         AGO   .INC
.SET     ANOP
&HEX     SETA  &HEX+1                  INCREMENT ADDRESS
         CONVERT   TO=EBCDIC,VALUE=&HEX,DIGITS=3
&ADDR    SETC  '&EBCDIC'
.INC     ANOP
&SGCNTRA(6)    SETA &SGCNTRA(6)+1
.ON2     AIF   (&SGCNTRA(6) LT 3).ON3   IF THERE HAS BEEN TWO ADDRESSES
&SGCNTRA(5)    SETA &SGCNTRA(5)+1       STORED GO TO NEXT SETC SYMBOL.
&SGCNTRA(6)    SETA 1
         AGO   .ON3
.E1 MNOTE 5,'* * * IEIUNI101 &NAME-MAXIMUM NUMBER OF NAMES EXCEEDED'
&SGQUITB SETB  1
         AGO   .MEND
.E3      AIF   (&MULTAD).E6
    MNOTE 5,'* * * IEIUNI103 &NAME-UNIT VALUE &UNIT(&A) INVALID'
&SGQUITB SETB  1
         AGO   .ON3
.E6 MNOTE 5,'* * * IEIUNI106 &NAME-UNIT VALUE &ADDR INVALID'
&SGQUITB SETB  1
         AGO   .ON3
.E2 MNOTE 5,'* * * IEIUNI102 &NAME-EXCESSIVE ADDRESS VALUES'
&SGQUITB SETB  1
         AGO   .MEND
.E4 MNOTE 5,'* * * IEIUNI104 &NAME-MULTIPLE ADDRESS VALUE INVALID'
&SGQUITB SETB  1
         AGO   .MEND
.E5 MNOTE 5,'* * * IEIUNI105 &NAME-MULTIPLE ADDRESS INCREMENT INVALID'
&SGQUITB SETB  1
         AGO   .MEND
.E7 MNOTE 5,'* * * IEIUNI108 &NAME HAS NO ASSIGNED UNITS.'
&SGQUITB SETB  1
         AGO   .MEND
.E8 MNOTE 5,'* * *IEIUNI107 UNIT AND/OR NAME PARAMETERS ARE REQUIRED'
&SGQUITB SETB  1
         AGO   .MEND
.E9      MNOTE 5,'* * * IEIUNI109 MAXIMUM NUMBER OF UNITS SPECIFIED.  MC
               AXIMUM IS &LIMITA-NUMBER OF UNITNAMES.'
&SGQUITB SETB  1
.MEND  MEND
