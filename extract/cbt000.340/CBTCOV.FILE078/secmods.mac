         MACRO
         SECMODS
         COPY  SGGBLPAK
.*       SD CODE S99235 HAS BEEN USED TO REPRESENT BOTH 158 (SD S99233)
.*       AND 168  CPU CODE.
         LCLA  &F,&N
         LCLB  &B(3),&NS
         LCLC  &MOD,&SIZ,&SER
         AIF   (&SGMENTB(53)).V1
&SGMENTB(53)  SETB  1                   ENTRY SWITCH
         AIF   (N'&SYSLIST EQ 0).V2     NO PARAMETERS SPECIFIED
&N       SETA  &N+1
         AIF   ('&SYSLIST(&N)'(1,3) EQ 'ALL').DB
         AIF   ('&SYSLIST(&N)'(1,3) EQ '135').D0
         AIF   ('&SYSLIST(&N)'(1,3) EQ '145').D0
         AIF   ('&SYSLIST(&N)'(1,3) EQ '155').D0
         AIF   ('&SYSLIST(&N)'(1,3) EQ '158').D0                 S99235
         AIF   ('&SYSLIST(&N)'(1,3) EQ '165').D0                 S99235
         AIF   ('&SYSLIST(&N)'(1,3) EQ '168').D0                 S99235
&N       SETA  &N-1
    MNOTE *,'       SECONDARY CPU''S WITH SER/ASR SUPPORT'
     MNOTE *,'          MODEL          SER LEVEL'                S99247
.A0      AIF   (N'&SYSLIST EQ &N).C0    PARAMETERS EXHAUSTED
&N       SETA  &N+1
         AIF   (&N GT 5).VC             MAXIMUM OF 5 SECMODS     S99235
&B(1)    SETB  0
&B(2)    SETB  0
&B(3)    SETB  0
&MOD     SETC  '&SYSLIST(&N)'(1,2)
&SGMOD2B(&N)  SETB  (&MOD EQ 40)        * SET          * *  *
&SGMOD3B(&N)  SETB  (&MOD EQ 50)        *     MODEL         *
&SGMOD4B(&N)  SETB  (&MOD EQ 65)        *           GLOBALS *
&SGMOD5B(&N)  SETB  (&MOD EQ 75)        * * *           * * *
         AIF   (NOT(&SGMOD2B(&N) OR &SGMOD3B(&N) OR &SGMOD4B(&N) OR &SG*
               MOD5B(&N))).V4
         AIF   (&NS EQ 1).VB
          AIF  (K'&SYSLIST(&N) EQ 2).B4 ONLY MODEL WAS SPECIFIED S99247
         AIF   ('&SYSLIST(&N)'(3,1) NE '(').V5
&F       SETA  4                        PTR TO VALUE(S) INSIDE PARENS
.A4      AIF   ('&SYSLIST(&N)'(&F,5) NE 'SIZE-').A8
         AIF   (&B(1)).V5               SIZE SPECD TWICE IN A ROW
&B(1)    SETB  1                        *NO
&F        SETA      &F+6                INCREMENT CHARACTER PTR  S99247
          AIF       ('&SYSLIST(&N)'(&F,1) NE 'F' AND                   *
                    '&SYSLIST(&N)'(&F,1) NE 'G').B0              S99247
&F        SETA      &F+1                POINT TO COMMA           S99247
          AGO       .B0                                          S99247
.A8      AIF   ('&SYSLIST(&N)'(&F,4) NE 'SER-').V5
         AIF   (&B(2)).V5               SER SPECD CONTIGUOUSLY
&B(2)    SETB  1                        *NO
&F       SETA  &F+4                     INCREMENT POINTER
&SGSER2B(&N)  SETB  ('&SYSLIST(&N)'(&F,4) EQ 'SER0')
&SGSER3B(&N)  SETB  ('&SYSLIST(&N)'(&F,4) EQ 'SER1')
         AIF   (NOT(&SGSER2B(&N) OR &SGSER3B(&N))).V9 * SER0/SER1 *
&SER     SETC  '&SYSLIST(&N)'(&F,4).' '
&F       SETA  &F+4                     INCR. PTR
.B0      AIF   ('&SYSLIST(&N)'(&F,1) NE ',').B2   ANOTHER DETAL FOLLOWS
&F       SETA  &F+1
         AGO   .A4
.B2      AIF   ('&SYSLIST(&N)'(&F,1) NE ')').V5   END OF PARAM
.B4 MNOTE *,'          &MOD             &SER'                    S99247
&SER     SETC  '     '
.*                                     SEQ # 685000 REMOVED FOR S99247
         AGO   .A0
.C0      MEXIT
.D0      ANOP
    MNOTE *,'       SECONDARY CPU''S  '
    MNOTE *,'  MODEL    SER LEVEL   '
&N       SETA  &N-1
.D1      ANOP
         AIF   (N'&SYSLIST EQ &N).C0    PARAMETERS EXHAUSTED
&N       SETA  &N+1
         AIF   (&N EQ 1).D2             FIRST PARAMETER
         AIF   (&NS NE 1).VB
         AIF   (&N GT 5).VC         MORE THAN 5 SECONDARY MODELS S99235
.D2      ANOP
         AIF   ('&SYSLIST(&N)'(1,2) EQ '40').VB
         AIF   ('&SYSLIST(&N)'(1,2) EQ '50').VB
         AIF   ('&SYSLIST(&N)'(1,2) EQ '65').VB
         AIF   ('&SYSLIST(&N)'(1,2) EQ '75').VB
&NS      SETB  1
&MOD     SETC  '&SYSLIST(&N)'(1,3)
&SGSER4B(&N) SETB  1
&SER     SETC  'MCH'
&SGMOD6B(&N) SETB  (&MOD EQ 135)        SET
&SGMOD7B(&N) SETB  (&MOD EQ 145)          MODEL
&SGMOD8B(&N) SETB  (&MOD EQ 155)          GLOBELS
&SGMOD9B(&N) SETB  (&MOD EQ 165)                                 S99235
&SGMODAB(&N) SETB  (&MOD EQ 158)                                 S99235
&SGMODBB(&N) SETB  (&MOD EQ 168)                                 S99235
         AIF   (NOT(&SGMOD6B(&N) OR &SGMOD7B(&N) OR &SGMOD8B(&N) OR &SGX
               MOD9B(&N) OR &SGMODAB(&N) OR &SGMODBB(&N))).VE    S99235
         AIF   (K'&SYSLIST(&N) EQ 3).DA  ONLY MODEL WAS SPECIFIED
         AIF   ('&SYSLIST(&N)'(4,1) NE '(').VD
&F       SETA  5                        PTR TO VALUES INSIDE PARENS
.D4      AIF   ('&SYSLIST(&N)'(&F,5) NE 'SIZE-').D8
&F       SETA  &F+6                     INCREMENT CHARACTER PTR
         AIF   ('&SYSLIST(&N)'(&F,1) NE 'F' AND '&SYSLIST(&N)'(&F,1) NE*
               'G' AND '&SYSLIST(&N)'(&F,1) NE 'H').D5
&F       SETA  &F+1
.D5      ANOP
.D7      AIF   ('&SYSLIST(&N)'(&F,1) NE ',').D9   MORE PARAMETERS
&F       SETA  &F+1
         AGO   .D4
.D8      AIF   ('&SYSLIST(&N)'(&F,7) NE 'SER-MCH').VD
&F       SETA  &F+7
         AGO   .D7
.D9      AIF   ('&SYSLIST(&N)'(&F,1) NE ')').VD   END OF PARAMETERS
.DA MNOTE *,'  &MOD      &SER '
&SER     SETC  '   '
&MOD     SETC  '   '
         AGO   .D1
.DB      ANOP
&SGMOD6B(&N)   SETB 1
&SGMOD7B(&N)   SETB 1
&SGMOD8B(&N)   SETB 1
&SGMOD9B(&N)   SETB 1
&SGMODAB(&N)  SETB 1                                             S99235
&SGMODBB(&N)  SETB 1                                             S99235
&SGSER4B(&N)   SETB 1                                            S99235
    MNOTE *,' ALL MODELS, OTHER THAN THE PRIMARY MODEL, HAVE BEEN      *
               SPECIFIED AS SECONDARY MODELS'
         MEXIT
.V1 MNOTE 5,'* * * IEISEC001 SECMODS MACRO PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.V2 MNOTE 5,'* * * IEISEC002 NO MODELS SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.V4 MNOTE 5,'* * * IEISEC004 MODEL &MOD INVALID'
&SGQUITB SETB  1
         AGO   .A0
.V5 MNOTE 5,'* * * IEISEC005 &SYSLIST(&N) INCORRECTLY SPECIFIED'
&SGQUITB SETB  1
         AGO   .A0
.*                      SEQ # 870000-890000 REMOVED FOR S99247
.V9 MNOTE 5,'* * * IEISEC009 SER VALUE &SYSLIST(&N) INVALID'
&SGQUITB SETB  1
         AGO   .A0
.*                               SEQ # 957000-959000 REMOVED FOR S99247
.VB MNOTE 5,'* * * IEISEC011 BOTH 360 AND 370 MODELS SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.VC MNOTE 5,'* * * IEISEC012  NO MORE THAN FIVE SECONDARY MODELS MAY BE*
               SPECIFIED.'                                       S99235
&SGQUITB SETB  1
         MEXIT
.VD MNOTE 5,'* * * IEISEC005  &SYSLIST(&N) INCORRECTLY SPECIFIED'
&SGQUITB SETB  1
         AGO   .D1
.VE MNOTE 5,'* * * IEISEC013 MODEL &MOD INVALID'
&SGQUITB SETB 1
         AGO   .D1
.*                               SEQ # 959997-959999 REMOVED FOR S99247
         MEND
