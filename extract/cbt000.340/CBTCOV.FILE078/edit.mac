         MACRO
         EDIT  &DSTYPE=,&BLOCK=,&FORMAT=,&FIXED=,&VAR=,&CONVERT=,      X
               &CHECKER=,&USEREXT=,&PRMPTR=,&USERSRC=            A45714
         COPY  SGGBLPAK
         LCLA  &I,&J,&K,&L,&M
         LCLC  &C
         AIF   (NOT &SGMENTB(61)).E2    TEST FOR PREVIOUS ENTRY
    MNOTE 5,'* * * IEIEDI101 EDIT MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                        SET QUIT BIT
         MEXIT
.E2      ANOP
&SGMENTB(61)   SETB 1                   SET EDIT ENTRY SWITCH
         AIF   (N'&DSTYPE EQ 0).E44     TEST FOR NO OP5RAN4S     A45714
   MNOTE *,'      EDIT ATTRIBUTES NOT DEFAULTED ARE -'           A45714
&J       SETA  N'&DSTYPE                NUMBER OF OPERANDS       A45714
&I       SETA  1                       INIT USER TYPE INDEX      A45714
&K       SETA  1                       INIT INDEX FOR OPERANDS   A45714
.*                                                               A45714
.* * * THIS ROUTINE ISOLATES DATA SET TYPE ATTRIBUTES * * * *    A45714
.*                                                               A45714
.ED0     AIF   (&K GT &J OR &K EQ 19).EDEXT TEST FOR END OF OPER A45714
         AIF   ('&DSTYPE(&K)' EQ 'PLIF').ED5                     A45714
         AIF   ('&DSTYPE(&K)' EQ 'FORTE').ED6                    A45714
         AIF   ('&DSTYPE(&K)' EQ 'FORTG').ED7                    A45714
         AIF   ('&DSTYPE(&K)' EQ 'FORTH').ED8                    A45714
         AIF   ('&DSTYPE(&K)' EQ 'TEXT').ED9                     A45714
         AIF   ('&DSTYPE(&K)' EQ 'DATA').ED10                    A45714
         AIF   ('&DSTYPE(&K)' EQ 'CLIST').ED11                   A45714
         AIF   ('&DSTYPE(&K)' EQ 'CNTL').ED12                    A45714
         AIF   ('&DSTYPE(&K)' EQ 'ASM').ED13                     A45714
         AIF   ('&DSTYPE(&K)' EQ 'COBOL').ED14                   A45714
         AIF   ('&DSTYPE(&K)' EQ 'FORTGI').ED15                  A45714
         AIF   ('&DSTYPE(&K)' EQ 'GOFORT').ED16                  A45714
         AIF   ('&DSTYPE(&K)' EQ 'BASIC').ED17                   A45714
         AIF   ('&DSTYPE(&K)' EQ 'IPLI').ED17A                   A45714
         AIF   ('&DSTYPE(&K)' EQ 'PLI').ED18                     A45714
         AIF   ('&DSTYPE(&K)' NE '').ED3 CHECK IF DSTYPE IS NULL A45714
.ED2     ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI102 EDIT OPERAND &DSTYPE(&K) INVALID DATA SET X
               TYPE OR MISSING VALUE'                            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .EDNXT                  PROCESS NEXT DATA SET TYPEA45714
.ED3     AIF   (K'&DSTYPE(&K) LE 8).ED4 CHECK IF LENGTH VALID    A45714
    MNOTE 5,'* * * IEIED105 DATA SET NAME &DSTYPE(&K) LONGER THAN EIGHTX
                CHARACTERS'                                      A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .EDNXT                  PROCESS NEXT DATA SET TYPEA45714
.ED4     AIF   ('&DSTYPE(&K)'(1,1) EQ '@').ED4A CHECK IF FIRST   A45714
         AIF   ('&DSTYPE(&K)'(1,1) EQ '#').ED4A    IS VALID ALPHAA45714
         AIF   ('&DSTYPE(&K)'(1,1) EQ '$').ED4A                  A45714
         AIF   ('&DSTYPE(&K)'(1,1) LT 'A').ED2                   A45714
         AIF   ('&DSTYPE(&K)'(1,1) GT 'Z').ED2                   A45714
.ED4A    ANOP                                                    A45714
         AIF   (&I LT 11).ED4AA        TEST IF MORE THAN 10 SPEC A45714
    MNOTE 0,'* * * IEIEDIW31 MORE THAN 10 NON-STANDARD DATA SET TYPES  X
               WERE SPECIFIED - &DSTYPE(&K) IS IGNORED'          A45714
         AGO   .EDNXT                  PROCESS NEXT PARAMETER    A45714
.ED4AA   ANOP                                                    A45714
&M       SETA  1                                                 A45714
.ED4B    AIF   (&M GT &I).ED4C                                   A45714
         AIF   ('&SGEDITC(&M)' EQ '&DSTYPE(&K)').ED19            A45714
&M       SETA  &M+1                                              A45714
         AGO   .ED4B                                             A45714
.ED4C    ANOP                                                    A45714
&SGEDITC(&I) SETC '&DSTYPE(&K)'        SET DSTYPE GLOBAL VALUE   A45714
    MNOTE *,'           DATA SET TYPE &DSTYPE(&K):'              A45714
&L       SETA  &I                      SET INDEX FOR OTHER ATTR. A45714
&I       SETA  &I+1                    INCR. TO NEXT USER SLOT   A45714
         AGO   .ED20                   PROCESS ATTRIBUTES        A45714
.ED5     ANOP                                                    A45714
&L       SETA  11                      INDEX FOR PLIF DSTYPE     A45714
         AGO   .ED18A                                            A45714
.ED6     ANOP                                                    A45714
&L       SETA  12                      INDEX FOR FORTE DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED7     ANOP                                                    A45714
&L       SETA  13                      INDEX FOR FORTG DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED8     ANOP                                                    A45714
&L       SETA  14                      INDEX FOR FORTH DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED9     ANOP                                                    A45714
&L       SETA  15                      INDEX FOR TEXT DSTYPE     A45714
         AGO   .ED18A                                            A45714
.ED10    ANOP                                                    A45714
&L       SETA  16                      INDEX FOR DATA DSTYPE     A45714
         AGO   .ED18A                                            A45714
.ED11    ANOP                                                    A45714
&L       SETA  17                      INDEX FOR CLIST DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED12    ANOP                                                    A45714
&L       SETA  18                      INDEX FOR CNTL DSTYPE     A45714
         AGO   .ED18A                                            A45714
.ED13    ANOP                                                    A45714
&L       SETA  19                      INDEX FOR ASM DSTYPE      A45714
         AGO   .ED18A                                            A45714
.ED14    ANOP                                                    A45714
&L       SETA  20                      INDEX FOR COBOL DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED15    ANOP                                                    A45714
&L       SETA  21                      INDEX FOR FORTGI DSTYPE   A45714
         AGO   .ED18A                                            A45714
.ED16    ANOP                                                    A45714
&L       SETA  22                      INDEX FOR GOFORI DSTYPE   A45714
         AGO   .ED18A                                            A45714
.ED17    ANOP                                                    A45714
&L       SETA  23                      INDEX FOR BASIC DSTYPE    A45714
         AGO   .ED18A                                            A45714
.ED17A   ANOP                                                    A45714
&L       SETA  24                      INDEX FOR IPLI DSTYPE     A45714
         AGO   .ED18A                                            A45714
.ED18    ANOP                                                    A45714
&L       SETA  25                      INDEX FOR PLI DSTYPE      A45714
.ED18A   AIF   ('&SGEDITC(&L)' NE '').ED19                       A45714
&SGEDITC(&L) SETC '&DSTYPE(&K)'        SET DSTYPE GLOBAL VALUE   A45714
    MNOTE *,'           DATA SET TYPE &DSTYPE(&K):'              A45714
         AGO   .ED20                                             A45714
.ED19    ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI104 EDIT OPERAND &DSTYPE(&K) DATA SET TYPE    X
               PREVIOUSLY SPECIFIED'                             A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .EDNXT                                            A45714
.ED20    AIF   ('&BLOCK(&K)' EQ '').ED22 CHECK IF BLOCKSIZE SPEC A45714
         AIF   (&BLOCK(&K) GE 1 AND &BLOCK(&K) LE 32760).ED21    A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &BLOCK(&K) INVALID BLOCKSIZE X
               FOR DATA SET TYPE &DSTYPE(&K)'                    A45714
&SGQUITB SETB  1                                                 A45714
         AGO   .ED22                                             A45714
.ED21    ANOP                                                    A45714
&SGEDITA(&L) SETA &BLOCK(&K)           SET BLOCKSIZE GLOBAL VALUEA45714
    MNOTE *,'                    BLOCKSIZE = &BLOCK(&K)'         A45714
.ED22    AIF   ('&FORMAT(&K)' EQ '').ED24 CHECK IF RECFM SPEC.   A45714
         AIF   ('&FORMAT(&K)' EQ 'VAR').ED23                     A45714
         AIF   ('&FORMAT(&K)' EQ 'FIXED').ED23                   A45714
         AIF   ('&FORMAT(&K)' EQ 'FXDONLY').ED23                 A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &FORMAT(&K) INVALID RECFM    X
               FOR DATA SET TYPE &DSTYPE(&K)'                    A45714
&SGQUITB SETB  1                                                 A45714
         AGO   .ED24                                             A45714
.ED23    ANOP                                                    A45714
&SGEDT2C(&L) SETC '&FORMAT(&K)'        SET RECFM GLOBAL VALUE    A45714
    MNOTE *,'                    RECORD FORMAT = &FORMAT(&K)'    A45714
.ED24    AIF   ('&FIXED(&K)' EQ '' AND '&VAR(&K)' EQ '').ED29    A45714
         AIF   ('&FIXED(&K)' NE '' AND '&VAR(&K)' NE '').ED27    A45714
         AIF   ('&FIXED(&K)' EQ '').ED25                         A45714
&M       SETA  0                                                 A45714
.ED24A   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GE K'&FIXED(&K)).ED24C                        A45714
         AIF   ('&FIXED(&K)'(&M,1) NE '-').ED24A                 A45714
&C       SETC  '&FIXED(&K)'(&M+1,K'&FIXED(&K)-&M)                A45714
         AIF   (&C GE 1 AND &C LE 255).ED24B                     A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &FIXED(&K) INVALID FIXED     X
               MAXIMUM FOR DATA SET TYPE &DSTYPE(&K)'            A45714
         AGO   .ED24C                                            A45714
.ED24B   ANOP                                                    A45714
&SGEDT3A(&L) SETA &C                   SET FIXED MAXIMUM GLOBAL  A45714
    MNOTE *,'                    FIXED MAXIMUM LRECL = &SGEDT3A(&L)'
.ED24C   AIF   ('&FORMAT(&K)' EQ 'VAR' OR ('&FORMAT(&K)' EQ '' AND     X
               &L GT 10)).ED29                                   A45714
&M       SETA  0                                                 A45714
.ED24D   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GT K'&FIXED(&K)).ED24                         A45714
         AIF   ('&FIXED(&K)'(&M,1) NE '-').ED24D                 A45714
         AIF   (&M EQ 1).ED24X         CHECK IF MISSING          A45714
&C       SETC  '&FIXED(&K)'(1,&M-1)                              A45714
         AIF   (&SGEDT3A(&L) EQ 0).ED24Y MAX. LT DFLT. FIXED     A45714
         AIF   ('&SGEDT3A(&L)' LT '&C').ED24X                    A45714
.ED24Y   ANOP                                                    A45714
         AIF   (&C GE 1 AND &C LE 255).ED24E                     A45714
.ED24X   ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &FIXED(&K) INVALID FIXED     X
               DEFAULT FOR DATA SET TYPE &DSTYPE(&K)'            A45714
         AGO   .ED29                                             A45714
.ED24E   ANOP                                                    A45714
&SGEDT2A(&L) SETA &C                   SET FIXED DEFAULT GLOBAL  A45714
    MNOTE *,'                    FIXED DEFAULT LRECL = &SGEDT2A(&L)'
         AGO   .ED29                                             A45714
.ED25    AIF   ('&FORMAT(&K)' EQ 'FXDONLY').ED26                 A45714
&M       SETA  0                                                 A45714
.ED25A   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GE K'&VAR(&K)).ED25C                          A45714
         AIF   ('&VAR(&K)'(&M,1) NE '-').ED25A                   A45714
&C       SETC  '&VAR(&K)'(&M+1,K'&VAR(&K)-&M)                    A45714
         AIF   (&C GE 5 AND &C LE 255).ED25B                     A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &VAR(&K) INVALID VARIABLE    X
               MAXIMUM FOR DATA SET TYPE &DSTYPE(&K)'            A45714
         AGO   .ED25C                                            A45714
.ED25B   ANOP                                                    A45714
&SGEDT5A(&L) SETA &C                   SET VARIABLE MAX. GLOBAL  A45714
    MNOTE *,'                    VARIABLE MAXIMUM LRECL = &SGEDT5A(&L)'
.ED25C   AIF   ('&FORMAT(&K)' EQ 'FIXED').ED29                   A45714
         AIF   ('&FORMAT(&K)' EQ '' AND &L GT 10).ED29           A45714
&M       SETA  0                                                 A45714
.ED25D   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GT K'&VAR(&K)).ED26                           A45714
         AIF   ('&VAR(&K)'(&M,1) NE '-').ED25D                   A45714
         AIF   (&M EQ 1).ED25X         CHECK IF MISSING          A45714
&C       SETC  '&VAR(&K)'(1,&M-1)                                A45714
         AIF   (&SGEDT5A(&L) EQ 0).ED25Y  MAX. LT DFLT. VAR      A45714
         AIF   ('&SGEDT5A(&L)' LT '&C').ED25X                    A45714
.ED25Y   ANOP                                                    A45714
         AIF   (&C GE 5 AND &C LE 255).ED25E                     A45714
.ED25X   ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &VAR(&K) INVALID VARIABLE    X
               DEFAULT FOR DATA SET TYPE &DSTYPE(&K)'            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED29                                             A45714
.ED25E   ANOP                                                    A45714
&SGEDT4A(&L) SETA &C                   SET VARIABLE DEFLT. GLOBALA45714
    MNOTE *,'                    VARIABLE DEFAULT LRECL = &SGEDT4A(&L)'
         AGO   .ED29                                             A45714
.ED26    ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI106 EDIT OPERAND VAR INVALID WHEN FORMAT=     X
               &FORMAT(&K) SPECIFIED'                            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED29                                             A45714
.ED27    ANOP                                                    A45714
&M       SETA  0                                                 A45714
.ED27A   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GE K'&FIXED(&K)).ED27C                        A45714
         AIF   ('&FIXED(&K)'(&M,1) NE '-').ED27A                 A45714
&C       SETC  '&FIXED(&K)'(&M+1,K'&FIXED(&K)-&M)                A45714
         AIF   (&C GE 1 AND &C LE 255).ED27B                     A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &FIXED(&K) INVALID FIXED     X
               MAXIMUM FOR DATA SET TYPE &DSTYPE(&K)'            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED27C                                            A45714
.ED27B   ANOP                                                    A45714
&SGEDT3A(&L) SETA &C                   SET FIXED MAX. GLOBAL     A45714
    MNOTE *,'                    FIXED MAXIMUM LRECL = &SGEDT3A(&L)'
.ED27C   ANOP                                                    A45714
&M       SETA  0                                                 A45714
.ED27D   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GE K'&VAR(&K)).ED27E                          A45714
         AIF   ('&VAR(&K)'(&M,1) NE '-').ED27D                   A45714
&C       SETC  '&VAR(&K)'(&M+1,K'&VAR(&K)-&M)                    A45714
         AIF   (&C GE 5 AND &C LE 255).ED27E                     A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &VAR(&K) INVALID VARIABLE    X
               MAXIMUM FOR DATA SET TYPE &DSTYPE(&K)'            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED27F                                            A45714
.ED27E   ANOP                                                    A45714
&SGEDT5A(&L) SETA &C                   SET VARIABLE MAX. GLOBAL  A45714
    MNOTE *,'                    VARIABLE MAXIMUM LRECL = &SGEDT5A(&L)'
.ED27F   AIF   ('&FORMAT(&K)' EQ 'VAR').ED28                     A45714
         AIF   (&L GT 10 AND '&FORMAT(&K)' EQ '').ED29           A45714
&M       SETA  0                                                 A45714
.ED27G   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GT K'&FIXED(&K)).ED29                         A45714
         AIF   ('&FIXED(&K)'(&M,1) NE '-').ED27G                 A45714
         AIF   (&M EQ 1).ED27X         CHECK IF MISSING          A45714
&C       SETC  '&FIXED(&K)'(1,&M-1)                              A45714
         AIF   (&SGEDT3A(&L) EQ 0).ED27Y MAX. LT DFLT. FIXED     A45714
         AIF   ('&SGEDT3A(&L)' LT '&C').ED27X                    A45714
.ED27Y   ANOP                                                    A45714
         AIF   (&C GE 1 AND &C LE 255).ED27H                     A45714
.ED27X   ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &FIXED(&K) INVALID FIXED     X
               DEFAULT FOR DATA SET TYPE &DSTYPE(&K)'            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED29                                             A45714
.ED27H   ANOP                                                    A45714
&SGEDT2A(&L) SETA &C                   SET FIXED DEFAULT GLOBAL  A45714
    MNOTE *,'                    FIXED DEFAULT LRECL = &SGEDT2A(&L)'
         AGO   .ED29                                             A45714
.ED28    ANOP                                                    A45714
&M       SETA  0                                                 A45714
.ED28A   ANOP                                                    A45714
&M       SETA  &M+1                                              A45714
         AIF   (&M GT K'&VAR(&K)).ED29                           A45714
         AIF   ('&VAR(&K)'(&M,1) NE '-').ED28A                   A45714
         AIF   (&M EQ 1).ED28X         CHECK IF MISSING          A45714
&C       SETC  '&VAR(&K)'(1,&M-1)                                A45714
         AIF   (&SGEDT5A(&L) EQ 0).ED28Y MAX. LT DFLT. VAR       A45714
         AIF   ('&SGEDT5A(&L)' LT '&C').ED28X                    A45714
.ED28Y   ANOP                                                    A45714
         AIF   (&C GE 5 AND &C LE 255).ED28B                     A45714
.ED28X   ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &VAR(&K) INVALID VARIABLE    X
               DEFAULT FOR DATA SET TYPE &DSTYPE(&K)'            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED29                                             A45714
.ED28B   ANOP                                                    A45714
&SGEDT4A(&L) SETA &C                   SET VARIABLE DEFLT. GLOBALA45714
    MNOTE *,'                    VARIABLE DEFAULT LRECL = &SGEDT4A(&L)'
.ED29    AIF   ('&CONVERT(&K)' EQ '').ED32                       A45714
         AIF   ('&CONVERT(&K)' EQ 'CAPS').ED31                   A45714
         AIF   ('&CONVERT(&K)' EQ 'ASIS').ED31                   A45714
         AIF   ('&CONVERT(&K)' EQ 'CAPSONLY').ED31               A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &CONVERT(&K) INVALID FOR     X
               DATA SET TYPE &DSTYPE(&K)'                        A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED32                                             A45714
.ED31    ANOP                                                    A45714
&SGEDT3C(&L) SETC '&CONVERT(&K)'       SET CONVERSION GLOBAL     A45714
    MNOTE *,'                    CHARACTER CONVERSION = &CONVERT(&K)'
.ED32    AIF   ('&CHECKER(&K)' EQ '').ED36                       A45714
         AIF   (K'&CHECKER(&K) LE 8).ED33                        A45714
    MNOTE 5,'* * * IEIEDI105 CHECKER NAME &CHECKER(&K) LONGER THAN     X
               EIGHT CHARACTERS'                                 A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED36                                             A45714
.ED33    AIF   ('&CHECKER(&K)'(1,1) EQ '@').ED34                 A45714
         AIF   ('&CHECKER(&K)'(1,1) EQ '#').ED34                 A45714
         AIF   ('&CHECKER(&K)'(1,1) EQ '$').ED34                 A45714
         AIF   ('&CHECKER(&K)'(1,1) LT 'A').ED35                 A45714
         AIF   ('&CHECKER(&K)'(1,1) GT 'Z').ED35                 A45714
.ED34    ANOP                                                    A45714
&SGEDT4C(&L) SETC '&CHECKER(&K)'       SET CHECKER NAME GLOBAL   A45714
    MNOTE *,'                    SYNTAX CHECKER NAME = &CHECKER(&K)'
         AGO   .ED36                                             A45714
.ED35   ANOP                                                     A45714
    MNOTE 5,'* * * IEIED102 EDIT OPERAND &CHECKER(&K) INVALID CHECKER  X
               NAME OR MISSING VALUE'                            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
.ED36    AIF   ('&USEREXT(&K)' EQ '').ED40                       A45714
         AIF   (&L LE 10).ED36A                                  A45714
    MNOTE 5,'* * * IEIEDI107 USER EXIT CAN ONLY BE SPECIFIED FOR       X
               NON-STANDARD DATA SET TYPES - &USEREXT(&K) IS IGNORED'
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED40                                             A45714
.ED36A   AIF   (K'&USEREXT(&K) LE 8).ED37                        A45714
    MNOTE 5,'* * * IEIEDI105 USER EXIT NAME &USEREXT(&K) LONGER THAN   X
               EIGHT CHARACTERS'                                 A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED40                                             A45714
.ED37    AIF   ('&USEREXT(&K)'(1,1) EQ '@').ED38                 A45714
         AIF   ('&USEREXT(&K)'(1,1) EQ '#').ED38                 A45714
         AIF   ('&USEREXT(&K)'(1,1) EQ '$').ED38                 A45714
         AIF   ('&USEREXT(&K)'(1,1) LT 'A').ED39                 A45714
         AIF   ('&USEREXT(&K)'(1,1) GT 'Z').ED39                 A45714
.ED38    ANOP                                                    A45714
&SGEDT5C(&L) SETC '&USEREXT(&K)'       SET USER EXIT NAME GLB    A45714
    MNOTE *,'                    USER EXIT NAME = &USEREXT(&K)'  A45714
         AGO   .ED40                                             A45714
.ED39    ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI102 EDIT OPERAND &USEREXT(&K) INVALID USER    X
               EXIT NAME OR MISSING VALUE'                       A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
.ED40    AIF   ('&PRMPTR(&K)' EQ '').ED43 TEST IF SPECIFIED      A45714
         AIF   (K'&PRMPTR(&K) LE 8).ED40A TEST FOR VALID LENGTH  A45714
    MNOTE 5,'* * * IEIEDI105 PROMPTER NAME &PRMPTR(&K) LONGER THAN     X
               EIGHT CHARACTERS'                                 A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .ED43                                             A45714
.ED40A   AIF   ('&PRMPTR(&K)'(1,1) EQ '@').ED41                  A45714
         AIF   ('&PRMPTR(&K)'(1,1) EQ '#').ED41                  A45714
         AIF   ('&PRMPTR(&K)'(1,1) EQ '$').ED41                  A45714
         AIF   ('&PRMPTR(&K)'(1,1) LT 'A').ED42                  A45714
         AIF   ('&PRMPTR(&K)'(1,1) GT 'Z').ED42                  A45714
.ED41    ANOP                                                    A45714
&SGEDT6C(&L) SETC '&PRMPTR(&K)'        SET PROMPTER NAME GLOBAL  A45714
    MNOTE *,'                    PROMPTER NAME = &PRMPTR(&K)'    A45714
         AGO   .ED43                                             A45714
.ED42    ANOP                                                    A45714
    MNOTE 5,'* * * IEIEDI102 EDIT OPERAND &PRMPTR(&K) INVALID PROMPTER X
               NAME OR MISSING VALUE'                            A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
.ED43    AIF   ('&USERSRC(&K)' EQ '').EDNXT                      A45714
         AIF   ('&USERSRC(&K)' EQ 'DATASET').ED44                A45714
         AIF   ('&USERSRC(&K)' EQ 'INCORE').ED44                 A45714
    MNOTE 5,'* * * IEIEDI103 EDIT OPERAND &USERSRC(&K) INVALID FOR     X
               DATA SET TYPE &DSTYPE(&K)'                        A45714
&SGQUITB SETB  1                       SET QUIT BIT              A45714
         AGO   .EDNXT                                            A45714
.ED44    ANOP                                                    A45714
&SGEDT7C(&L) SETC '&USERSRC(&K)'       SET INPUT SOURCE GLB VALUEA45714
    MNOTE *,'                    PROMPTER INPUT SOURCE = &USERSRC(&K)'
.EDNXT   ANOP                                                    A45714
&K       SETA  &K+1                    INCREMENT INDEX BY ONE    A45714
         AGO   .ED0                                              A45714
.EDEXT   AIF   (&K LT 19).EMEND                                  A45714
    MNOTE 0,'* * * IEIEDIW02 MORE THAN 18 DATA SET TYPES WERE          X
               SPECIFIED - ONLY FIRST 18 UNIQUE DATA SET TYPES         X
               PROCESSED'                                        A45714
         AGO   .EMEND                                            A45714
.E44 MNOTE *,'      EDIT DATA SET TYPE ATTRIBUTE DEFAULTS TO BE TAKEN'
.EMEND   MEND
