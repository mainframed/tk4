         MACRO
         LOADER &PARM=,&SIZE=100K,&LIN=SYSLIN,&LIB=SYSLIB,             *
               &PRINT=SYSLOUT
         COPY  SGGBLPAK
         LCLA  &A
         LCLB  &B,&C
         LCLC  &VAL
         AIF (&SGMENTB(56)).ER1         LOADER ALREADY CALLED
&SGMENTB(56)   SETB 1
&A       SETA  1
         AIF  (T'&PARM NE 'O').BY1
&SGLDRB(5)     SETB 1              SET DEFAULT OF NORES
    AGO  .A2                       ALL DEFAULTS
.BY1     AIF  (N'&PARM GT 5).ER2
    MNOTE *,'      PARM FIELD PARAMETERS ARE'
.A AIF ('&PARM(&A)' EQ 'NCAL' OR '&PARM(&A)' EQ 'NOCALL' OR '&PARM(&A)'*
                EQ 'CALL').A1
 AIF ('&PARM(&A)' EQ 'LET' OR '&PARM(&A)' EQ 'NOLET' OR '&PARM(&A)' EQ *
               'PRINT').A1
 AIF ('&PARM(&A)' EQ 'NOPRINT' OR '&PARM(&A)' EQ 'MAP' OR '&PARM(&A)' E*
               Q 'RES').A1
 AIF ('&PARM(&A)' EQ 'NOMAP' OR '&PARM(&A)' EQ 'NORES').A1
         AGO   .ER3
.A1 MNOTE *,'           &PARM(&A)'
&SGLDRB(1)     SETB ('&PARM(&A)' EQ 'NCAL' OR '&PARM(&A)' EQ 'NOCALL' O*
               R &SGLDRB(1))
&SGLDRB(2)     SETB ('&PARM(&A)' EQ 'LET' OR &SGLDRB(2))
&SGLDRB(3)     SETB ('&PARM(&A)' EQ 'NOPRINT' OR &SGLDRB(3))
&SGLDRB(4)     SETB ('&PARM(&A)' EQ 'MAP' OR &SGLDRB(4))
&SGLDRB(5)     SETB ('&PARM(&A)' EQ 'NORES' OR &SGLDRB(5))
&C             SETB (&SGLDRB(5))     NO RES SPECIFIED IN PARM FIELD
.A1A     ANOP
&A       SETA  &A+1
         AIF (N'&PARM GE &A).A          RETURN FOR NEXT OPERAND
         AIF (N'&PARM EQ 5).A8          NO DEFAULTS
.A2      ANOP
    MNOTE *,'      PARM FIELD DEFAULTS ARE '
.A3      AIF (&SGLDRB(1)).A4            NCAL OR NOCALL SPECIFIED
    MNOTE *,'           CALL'
.A4      AIF (&SGLDRB(2)).A5            LET SPECIFIED
    MNOTE *,'           NOLET'
.A5      AIF (&SGLDRB(3)).A6            NOPRINT SPECIFIED
    MNOTE *,'           PRINT'
.A6      AIF (&SGLDRB(4)).A7            MAP SPECIFIED
    MNOTE *,'           NOMAP'
.A7      AIF (NOT(&SGLDRB(5)) OR &C).A8 RES SPECIFIED
    MNOTE *,'           NORES'
.A8      AIF   (&SGLDRB(1) AND NOT &SGLDRB(5)).ER8  RES AND NOT CALL
.B       AIF ('&SIZE'(K'&SIZE,1) NE 'K').B1  NO IN ''K'' VALUE
&B       SETB  1
&VAL     SETC  '&SIZE'(1,K'&SIZE-1)
&SGLDRA  SETA  &VAL
&SGLDRA  SETA  &SGLDRA*1024
         AGO   .B2
.B1      ANOP
&VAL     SETC  '&SIZE'
&SGLDRA  SETA  &VAL
.B2      AIF   (&SGLDRA GT 8388608).ER4
   MNOTE *,'      SIZE VALUE &SIZE INCLUDED'
.C       AIF (K'&LIN LT 1 OR K'&LIN GT 8).ER5
         AIF ('&LIN'(1,1) LT 'A' OR '&LIN'(1,1) GT 'Z').ER5
&SGLDRC(1)     SETC '&LIN'
    MNOTE *,'      LIN VALUE &LIN INCLUDED'
.D   AIF (K'&PRINT LT 1 OR K'&PRINT GT 8).ER6
     AIF ('&PRINT'(1,1) LT 'A' OR '&PRINT'(1,1) GT 'Z').ER6
&SGLDRC(2)     SETC '&PRINT'
    MNOTE *,'      PRINT VALUE &PRINT INCLUDED'
.E       AIF (K'&LIB LT 1 OR K'&LIB GT 8).ER7
         AIF ('&LIB'(1,1) LT 'A' OR '&LIB'(1,1) GT 'Z').ER7
&SGLDRC(3)     SETC '&LIB'
    MNOTE *,'      LIB VALUE &LIB INCLUDED'
         MEXIT
.ER1 MNOTE 5,'* * * IEILDR001 LOADER MACRO PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.ER2 MNOTE 5,'* * * IEILDR002 PARM PARAMETER HAS TOO MANY OPERANDS'
&SGQUITB SETB  1
         AGO   .A
.ER3 MNOTE 5,'* * * IEILDR003 PARM(&A) VALUE &PARM(&A) INVALID'
&SGQUITB SETB  1
         AGO   .A1A
.ER4 MNOTE 5,'* * * IEILDR004 SIZE VALUE &SIZE INVALID'
&SGQUITB SETB  1
         AGO   .C
.ER5 MNOTE 5,'* * * IEILDR005 LIN VALUE &LIN INVALID'
&SGQUITB SETB  1
         AGO   .D
.ER6 MNOTE 5,'* * * IEILDR006 PRINT VALUE &PRINT INVALID'
&SGQUITB SETB  1
         AGO   .E
.ER7 MNOTE 5,'* * * IEILDR007 LIB VALUE &LIB INVALID'
&SGQUITB SETB  1
         MEXIT
.ER8 MNOTE 5,'* * * IEILDR008 RES SPECIFIED AND CALL NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .B
         MEND
