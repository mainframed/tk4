         MACRO
         DATAMGT  &ACSMETH=
         COPY  SGGBLPAK
         LCLA  &A
         AIF   (&SGMENTB(34)).E1        WAS DATAMGT CALLED THIS SYSGEN
&SGMENTB(34) SETB 1                     SW ON MEANS DATAMGT USED ONCE
 AIF  (T'&ACSMETH EQ 'O').E2            IS REQ'D ACSMETH PARAM. OMITTED
    MNOTE *,'     OPTIONAL ACCESS METHOD '
.EXITEST AIF  (N'&ACSMETH EQ &A).FINIS  IF ACSMETH OMIT EXIT FROM MACRO
&A       SETA  &A+1                     INITIALIZE CTR FOR ACSMETH SCAN
     AIF ('&ACSMETH(&A)' EQ 'BDAM').S1  DID USER SPECIFY BDAM OPTION
     AIF ('&ACSMETH(&A)' EQ 'ISAM').S2  DID USER SPECIFY ISAM OPTION
    AIF  ('&ACSMETH(&A)' EQ 'QTAM').S3  DID USER SPECIFY QTAM OPTION
     AIF ('&ACSMETH(&A)' EQ 'BTAM').S4  DID USER SPECIFY BTAM OPTION
 AIF ('&ACSMETH(&A)' EQ 'TCAM').S5
&SGQUITB SETB  1                        INVALID VALUE SPECD FOR ACSMETH
    MNOTE 5,'* * * IEIDAT101 ACSMETH VALUE &ACSMETH(&A) INVALID'
         AGO   .EXITEST                 GO TEST FOR SCAN END
.S1 MNOTE *,'        BDAM  '
&SGDMGTB(1) SETB 1                      BDAM SPECIFIED IN ACSMETH OPND
&SGSUPRB(15) SETB 1
&SGSUPRB(17) SETB 1
         AGO   .EXITEST                 GO TEST FOR SCAN END
.S2 MNOTE *,'        ISAM '
&SGDMGTB(2) SETB 1                      ISAM SPECIFIED IN ACSMETH OPND.
&SGSUPRB(15) SETB 1
&SGSUPRB(17) SETB 1                                                7474
         AGO   .EXITEST                 GO TEST FOR SCAN END
.S3 MNOTE *,'        QTAM '
&SGDMGTB(3) SETB 1                      QTAM SPECIFIED IN ACSMETH OPND.
         AGO   .EXITEST                 GO TEST FOR END OF SCAN RTN
.S4 MNOTE *,'        BTAM '
&SGSUPRB(15) SETB 1
&SGSUPRB(17)   SETB 1
&SGDMGTB(4) SETB 1                      BTAM SPECIFIED IN ACSMETH OPND.
         AGO   .EXITEST                 GO TEST FOR END OF SCAN RTN
.S5 MNOTE *,'        TCAM'
&SGDMGTB(5) SETB 1
 AGO .EXITEST
.E2 MNOTE 5,'* * * IEIDAT102 ACSMETH VALUE NOT SPECIFIED'
&SGQUITB SETB  1                        IF CALL MACRO MUST HAVE ASCMETH
         AGO  .FINIS                    EXIT FROM DATAMGT MACRO
.E1 MNOTE 5,'* * * IEIDAT103 DATAMGT MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                        DATAMGT MACRO CALLED PREVIOUSLY
.FINIS   MEND                           EXIT FROM DATAGMT MACRO
