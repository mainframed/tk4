         MACRO
         SORTMERG &SIZE=,&SORTOPT=,&RECTYPE=,&CNTLFLD=,&SORTDEV=,&MERGEX
               =,&MESSAGE=
         COPY  SGGBLPAK
         LCLA  &A
         AIF   (&SGMENTB(35)).E9        WAS SORTMERG CALLED THIS SYSGEN
&SGMENTB(35) SETB 1                     SW ON MEANS SORTMERG USED ONCE
         AIF   (T'&SIZE EQ 'O').T1      IF SIZE OMIT,ASSUME 13312 BYTES
         AIF   (T'&SIZE NE 'N').T2      BR ERR RTN IF VALUE NOT SLFDFNG
         AIF   (&SIZE LT 12000).T2
&SGSTMGA SETA  &SIZE                    PUT SIZE VALUE IN GLOBAL A SYM
.T4 MNOTE *,'     SIZE &SGSTMGA BYTES '
         AGO   .NXTRTN
.T1      ANOP                           DEFAULT RTN FOR OMITTED SIZE
&SGSTMGA  SETA  12000
         AGO  .T4                       GO PRT SIZE PATEMETER
.T2 MNOTE 5,'* * * IEISOR101 SIZE VALUE &SIZE INVALID'
&SGQUITB SETB  1
         AGO  .NXTRTN
.NXTRTN AIF (T'&SORTOPT EQ 'O').H1      ASSUME MOD'FD LIB IF BR
        AIF ('&SORTOPT' EQ 'MODPRGM').H1 IS MOD'FTN PROG REQUESTED
        AIF ('&SORTOPT' EQ 'FULLIB').S1 IS FULL SORT/MERGE LIB REQUESTD
&SGQUITB SETB  1
    MNOTE 5,'* * * IEISOR102 SORTOPT VALUE &SORTOPT INVALID'
         AGO  .END                      EXIT FROM SORTMERG MACRO
.S1 MNOTE *,'     ENTIRE SORT/MERGE LIBRARY '
&SGSTMGB(1) SETB 1                      SW 1 ON EQ ENTIRE SORT/MERG LIB
         AGO  .END                      EXIT FROM SORTMERG MACRO
.H1 MNOTE *,'     SORT/MERGE OPTIONS INCLUDE- '
&SGSTMGB(16) SETB ('&SORTOPT' EQ 'MODPRGM') IF MODPRGM SPEC PUT SW16 ON
       AIF (NOT &SGSTMGB(16)).TESTYPE   IF MODPRGM SPECD PRT MESSAGE
    MNOTE *,'        MODIFICATION PROGRAMS '
.TESTYPE AIF   (N'&RECTYPE EQ 0).E2     WAS RECTYPE PARAMETER OMITTED
&A       SETA  0                        CLEAR CTR FOR RECTYPE PARA SCAN
.RETRY   AIF (&A EQ N'&RECTYPE).CK2CK3  IF SCAN DONE CHK FOR MIN 1 TYPE
&A       SETA  &A+1                     BUMP CTR POINT NXT RECTYPE OPND
  AIF ('&RECTYPE(&A)' EQ 'FIXED').S2    IF FIXED REC SPECD,PRT & SET SW
  AIF ('&RECTYPE(&A)' EQ 'VAR').S3      IF VARBL REC SPECD,PRT & SET SW
  AIF ('&RECTYPE(&A)' EQ 'LONG').S4     IF SPEC REC OVER 256,PRT/SET SW
&SGQUITB SETB  1                        'RECTYPE' SUBLISTD OPRN INVALID
    MNOTE 5,'* * * IEISOR103 RECTYPE VALUE &RECTYPE(&A) INVALID'
         AGO   .RETRY                   GO TEST NEXT RECTYPE VALUE
.S2 MNOTE *,'        FIXED LENGTH RECORDS '
&SGSTMGB(2) SETB 1                      SW 2 SET FOR FIXED LENGTH REC.S
         AGO   .RETRY                   GO TEST NEXT RECTYPE VALUE
.S3 MNOTE *,'        VARIABLE LENGTH RECORDS '
&SGSTMGB(3) SETB 1                      SW 3 SET FOR VARBL LENGTH REC.S
         AGO   .RETRY                   GO TEST NEXT RECTYPE VALUE
.S4 MNOTE *,'        RECORDS LONGER THAN 256 BYTES '
&SGSTMGB(4) SETB 1                      SW 4 SET FOR RECS OVER 256 BYTE
         AGO   .RETRY                   GO TEST NEXT RECTYPE VALUE
.CK2CK3 AIF (&SGSTMGB(2) OR &SGSTMGB(3)).CKCTFLD IF RECTYP OK CK CNTFLD
    MNOTE 5,'* * * IEISOR104 RECTYPE MUST INCLUDE VAR OR FIXED'
&SGQUITB SETB  1                        RECTYPE SPEC,BUT VAR/FIXED OMIT
         AGO   .CKCTFLD
.E2 MNOTE 5,'* * * IEISOR105 RECTYPE VALUE NOT SPECIFIED'
&SGQUITB SETB  1                        RECTYPE PARA. MUST NOT BE OMITD
.CKCTFLD AIF   (N'&CNTLFLD EQ 0).E6     WAS CNTLFLD PARAMETER OMITTED
&A       SETA  0                        CLEAR CTR FOR CNTLFLD PARA SCAN
.RESCAN AIF (&A EQ N'&CNTLFLD).CHKDEVS  IF SCAN DONE GO CHK SORTDEV OPT
&A       SETA  &A+1                     BUMP CTR POINT NXT CNTLFLD OPTN
    AIF ('&CNTLFLD(&A)' EQ 'SINGLE').S5 IF SINGLE SPECD,PRT AND SET SWT
  AIF ('&CNTLFLD(&A)' EQ 'MULTIPLE').S6 IF MLTPLE SPECD,PRT AND SET SWT
&SGQUITB SETB  1                        'CNTLFLD' SUBLISTD OPRN INVALID
    MNOTE 5,'* * * IEISOR106 CNTLFLD VALUE &CNTLFLD(&A) INVALID'
         AGO   .RESCAN                  GO TEST NEXT CNTLFLD VALUE
.S5 MNOTE *,'        SINGLE CONTROL FIELDS '
&SGSTMGB(5) SETB 1                      SW 5 SET FOR SINGLE CNTLFLD OPT
         AGO   .RESCAN                  GO TEST NEXT CNTLFLD VALUE
.S6 MNOTE *,'        MULTIPLE CONTROL FIELDS '
&SGSTMGB(6) SETB 1                      SW 6 SET FOR MLTPLE CNTLFLD OPT
         AGO   .RESCAN
.E6 MNOTE 5,'* * * IEISOR107 CNTLFLD VALUE NOT SPECIFIED'
&SGQUITB SETB  1                        MIN. OF SINGLE OR MULTIPLE REQD
.CHKDEVS AIF   (N'&SORTDEV EQ 0).E8     WAS SORTDEV PARAMETER OMITTED
&A       SETA  0                        CLEAR CTR FOR SORTDEV SCAN
.MOREDEV AIF (&A EQ N'&SORTDEV).CHKMERG IF SCAN DONE GO CHK MERGE OPT.S
&A       SETA  &A+1                     BUMP CTR POINT NXT SORTDEV OPTN
   AIF ('&SORTDEV(&A)' EQ '2311').S8    IF 2311=SORT/MERG DV,PRT/SET SW
   AIF ('&SORTDEV(&A)' EQ '2400').S9    IF 2400=SORT/MERG DV,PRT/SET SW
   AIF ('&SORTDEV(&A)' EQ '2301').S7    2301 SORT DEVICE
   AIF ('&SORTDEV(&A)' EQ '2314').SH
&SGQUITB SETB  1                        'SORTDEV' SUBLISTD OPRN INVALID
    MNOTE 5,'* * * IEISOR108 SORTDEV VALUE &SORTDEV(&A) INVALID'
         AGO   .MOREDEV                 GO TEST NEXT SORTDEV VALUE
.S8 MNOTE *,'        2311 DISK '
&SGSTMGB(8) SETB  1                     SW 8 SET FOR SORT/MERGE ON 2311
         AGO   .MOREDEV                 GO TEST NEXT SORTDEV VALUE
.S9 MNOTE *,'        2400 MAGNETIC TAPES '
&SGSTMGB(9) SETB  1                     SW 9 SET FOR SORT/MERGE ON 2400
         AGO   .MOREDEV                 GO TEST NEXT SORTDEV VALUE
.S7 MNOTE *,'      2301 DRUM '
&SGSTMGB(7) SETB 1
         AGO   .MOREDEV
.SH MNOTE *,'      2314 DIRECT ACCESS STORAGE'
&SGSTMGB(11)  SETB  1
         AGO   .MOREDEV
.E8 MNOTE 5,'* * * IEISOR109 SORTDEV VALUE NOT SPECIFIED'
&SGQUITB SETB  1                        'SORTDEV' OMITTED-MUST BE SPECD
.CHKMERG AIF (N'&MERGE EQ 0).CHKMESG    IF MERGE PARA. OMIT GO NEXT OPN
&A       SETA  0                        CLEAR CTR FOR MERGE SCAN
.RETEST AIF (&A EQ N'&MERGE).CHKMESG    IF MERGE SCAN THRU CHK MSG OPTS
&A       SETA  &A+1                     BUMP CTR POINT NXT MERGE VALUE
 AIF ('&MERGE(&A)' EQ 'MERGONLY').SA    IF MERGE-ONLY SPECD,PRT/SET SWT
&SGQUITB SETB  1                        'MERGE' SUBLISTD OPRN INVALID
    MNOTE 5,'* * * IEISOR110 MERGE VALUE &MERGE(&A) INVALID'
         AGO   .RETEST                  GO TEST NEXT MERGE OPTION SPECD
.SA MNOTE *,'        MERGE ONLY OPERATIONS '
&SGSTMGB(10) SETB 1                     SW 10 SET FOR MERGE-ONLY OPRATN
         AGO   .RETEST                  GO TEST NEXT MERGE OPTION SPECD
.CHKMESG AIF (N'&MESSAGE EQ 0).NO       IF MSG PARA NULL,NO-MSG-OP RQST
&A       SETA  0                        CLEAR COUNTER OF SUBLISTD OPRND
.TSTLOOP AIF (&A EQ N'&MESSAGE).CKMSGSW IF MSG SCAN DONE CK CMBNT/VALUE
&A       SETA  &A+1                     BUMP CTR POINT NXT MESAGE VALUE
  AIF ('&MESSAGE(&A)' EQ 'PRINTER').SC  IF SORT MSG ON PRTR,SET SW/PRT
  AIF ('&MESSAGE(&A)' EQ 'CONSOLE').SD  IF SORT MSG ON CNSL,SET SW/PRT
  AIF ('&MESSAGE(&A)' EQ 'ALL').SE      IF ALL SORT MSG O/P,SET SW/PRT
  AIF ('&MESSAGE(&A)' EQ 'CRITICAL').SF IF CRITICAL MSG O/P,SET SW/PRT
&SGQUITB SETB  1                        'MESSAGE' VALUE INVALID
    MNOTE 5,'* * * IEISOR111 MESSAGE VALUE &MESSAGE(&A) INVALID'
         AGO   .TSTLOOP                 GO TEST NEXT MESSAGE VALUE
.SC MNOTE *,'        SORT/MERGE MESSAGES ON PRINTER '
&SGSTMGB(12) SETB 1                     SW 12 SET FOR SORT MSG ON PRNTR
         AGO   .TSTLOOP                 GO TEST NEXT MESSAGE VALUE
.SD MNOTE *,'        SORT/MERGE MESSAGES ON CONSOLE TYPEWRITER '
&SGSTMGB(13) SETB 1                     SW 13 SET FOR SORT MSG ON CNSL.
         AGO   .TSTLOOP                 GO TEST NEXT MESSAGE VALUE
.SE MNOTE *,'        ALL SORT/MERGE MESSAGES GENERATED '
&SGSTMGB(14) SETB 1                     SW 14 SET FOR 'ALL-MSG' OPTN.
         AGO   .TSTLOOP                 GO TEST NEXT MESSAGE VALUE
.SF MNOTE *,'        ONLY CRITICAL SORT/MERGE MESSAGES GENERATED '
&SGSTMGB(15) SETB  1                    SW 15 SET FOR 'CRITICAL-MSG' OP
         AGO   .TSTLOOP                 GO TEST NEXT MESSAGE VALUE
.CKMSGSW AIF   (((&SGSTMGB(12) AND NOT &SGSTMGB(13))OR(&SGSTMGB(13) ANDX
                NOT &SGSTMGB(12)))AND((&SGSTMGB(14) AND NOT &SGSTMGB(15X
               ))OR(&SGSTMGB(15) AND NOT &SGSTMGB(14)))).END
&SGQUITB SETB  1                        MSG-DEV AND -TYPE BAD COMBINATN
    MNOTE 5,'* * * IEISOR111 MESSAGE VALUE &MESSAGE INVALID'
         MEXIT
.NO MNOTE *,'        NO SORT/MERGE MESSAGES GENERATED '
         AGO  .END                      EXIT FROM SORTMERG MACRO
.E9 MNOTE 5,'* * * IEISOR112 SORTMERG MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                        ONLY 1 SORTMERG MAC CALL/SYSGEN
.END     MEND                           EXIT FROM SORTMERG MACRO
