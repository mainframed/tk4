         MACRO
         CUPOINT
         COPY  SGGBLPAK
  LCLA &T
         LCLA  &M,&K,&J,&I,&A,&B
         ACTR  &SGACTR
.L1      AIF   (&SGCNTRA(3) LT 2 OR &SGCNTRA(3) GT &SGMAXA(2)).L8
&M       SETA  &SGCNTRA(3)  GET THE NUMBER OF IO DEVICE ADDRESSES.
.L2      AIF   (&M/2 LE 0).L6  TRANSFER OUT IO DEVICE ADDRESS SORTED.
&M       SETA  &M/2  SET INDEX TO OFF SET FOR SECOND IO DEVICE ADDRESS.
&K       SETA  &SGCNTRA(3)-&M  SET VALUE TO CHECK END OF PASS.
&J       SETA  1  SET OUTER INDEX VALUE.
.L3      ANOP    RETURN TO CONTINUE PASS AT SAME INCREMENT.
&I       SETA  &J  SET INDEX TO FIRST IO DEVICE ADDRESS.
.L4      AIF   (&SGDADRA(&I)-&SGDADRA(&I)/4096*4096 LT &SGDADRA(&I+&M)-1
               &SGDADRA(&I+&M)/4096*4096).L5  TRANSFER DEVICES ORDERED.
         AIF   (&SGDADRA(&I)-&SGDADRA(&I)/4096*4096 EQ &SGDADRA(&I+&M)-1
               &SGDADRA(&I+&M)/4096*4096).L9  ERROR DEVICES EQUAL.
.LA      ANOP
&T       SETA  &SGDADRA(&I)  IO ADDRESS OUT OF ORDER.
&SGDADRA(&I) SETA &SGDADRA(&I+&M)  SAVE FIRST ADDRESS AND PUT SECOND
&SGDADRA(&I+&M) SETA &T  ADDRESS IN FIRST. THEN PUT FIRST IN SECOND.
&I       SETA  &I-&M  RESET INNER INDEX TO MOVE ADDRESS FURTHER BACK.
         AIF   (&I GE 1).L4  TRANSFER TO MOVE ADDRESS FUTHER BACK.
.L5      AIF   (&J+1 GT &K).L2  TRANSFER FOR ANOTHER PASS.
&J       SETA  &J+1  STEP OUTER INDEX BY ONE.
         AGO   .L3  TRANSFER TO CONTINUE PASS AT SAME INCREMENT.
.L8 MNOTE 5,'* * * IEIGEN120 INVALID NUMBER OF IO DEVICES'
&SGQUITB SETB  1
         AIF  (&SGCNTRA(3)  LT  2).E2   NO. OF DEVICES LESS THAN TWO?
&SGCNTRA(3)  SETA  &SGMAXA(2)
         AGO   .L1
.L9      ANOP
&A       SETA  &SGDADRA(&I)-&SGDADRA(&I)/4096*4096
         CONVERT TO=EBCDIC,VALUE=&A,DIGITS=3
    MNOTE 5,'* * * IEIGEN121 TWO EQUAL IO ADDRESSES &EBCDIC'
.T1      ANOP
&SGQUITB SETB  1
         AGO   .LA
.L6 MNOTE *,'      THE SORTED DEVICE A BOXES CONTAIN.'
    MNOTE *,'         SEQ  ADDRESS  DEVICE POINTER C.U. POINTER'
&B       SETA  0
.E1      AIF   (&B EQ &SGCNTRA(3)).E2
&B       SETA  &B+1
&I SETA &SGDADRA(&B)/4096-&SGDADRA(&B)/1048576*256   GET CU POINTER
&J SETA &SGDADRA(&B)/1048576  GET DEVICE POINTER
&A SETA &SGDADRA(&B)-&SGDADRA(&B)/4096*4096
         CONVERT  TO=EBCDIC,DIGITS=3,VALUE=&A
    MNOTE *,'         &B   &EBCDIC        &J              &I'
         AGO   .E1
.E2      MEND
