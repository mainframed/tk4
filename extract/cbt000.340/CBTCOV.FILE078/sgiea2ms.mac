         MACRO
         SGIEA2MS
         COPY  SGGBLPAK
.* THIS IS THE MAIN STORAGE SUPERVISOR GROUP II SYSGEN MACRO WHICH
.* GENERATES THE ASSEMBLY JCL AND MACRO CALL(S) FOR SYSGEN GROUP 0
.* MSS MACROS FOR OPTIONS  1,2, AND 4.
.*
.*       OPTION 1   - ISSUES MACRO CALL FOR IEAAMS X,N,X TO GENERATE
.*                    THE FIXED TASK MSS MODULE IEAAMS00.
.*       OPTION 2   - ISSUES MACRO CALL FOR IEAAMS X,Y,X TO GENERATE
.*                    THE FIXED TASK MSS MODULE IEAAMS00.
.*       OPTION 4   - ISSUES MACRO CALLS FOR IEAQGM X AND IEAQPR X TO
.*                    GENERATE THE GETMAIN/FREEMAIN MODULE (IEAQGM01)
.*                    AND THE GETPART/FREEPART MODULE (IEAQPRT0) FOR
.*                    THE VARIABLE TASK MSS.
.*       GLOSSARY   - &VALIDCK - VALIDITY CHECK
.*                    &RORI    - ROLLOUT/ROLLIN
.*                    &MFT     - MULTI-FIXED TASKS
.*                    &LCS     - LARGE CORE STORAGE
.*                    &MPS     - MULTI PROSESSING
.*                    &SMF     - SYS MANAGEMENT FACILITY INDICATOR  SMF
.*                    &TSO     - TIME SHARING OPTION              20021
         LCLC  &VALIDCK,&RORI,&MFT,&LCS,&MPS,&ATTACH              M4952
         LCLB  &SMF                                                 SMF
         LCLB  &TSO                                               20021
&VALIDCK SETC  'N'
&RORI    SETC  'N'
&MFT     SETC  'N'
&LCS     SETC  'N'
&MPS     SETC  'N'
&ATTACH  SETC  'N'                                                 I250
         AIF   (&SGCPRGB(8) EQ 1).MVTMSS
         AIF   (NOT(&SGSUPRB(4))).NEXT2
&VALIDCK SETC  'Y'
.NEXT2   AIF   (NOT(&SGCPRGB(11))).NEXT3
&LCS     SETC  'Y'
.NEXT3   AIF   (NOT(&SGCPRGB(4))).NEXT4
&MFT     SETC  'Y'
&SMF     SETB  (&SGSCHDB(7))                                      M4952
.NEXT4A  ANOP                                                      I250
         AIF   (NOT &SGSUPRB(37)).NEXT4                            I250
&ATTACH  SETC  'Y'                                                 I250
.NEXT4   ANOP
&SGCTRLC(6) SETC 'IEAAMS00'
         COPY  SGASMPAK
         PUNCH '         IEAAMS &VALIDCK,&MFT,&LCS,&SMF,&ATTACH'  19018
         PUNCH '         END'
         PUNCH '/*'
         AGO   .NEXT99
.MVTMSS  ANOP
.NEXT5   AIF   (NOT(&SGCPRGB(11))).NEXT6
&LCS     SETC  'Y'
.NEXT6   AIF   (NOT(&SGCPRGB(6))).NEXT7
&RORI    SETC  'Y'
.NEXT7   AIF   (NOT(&SGCPRGB(13))).NEXT8
&MPS     SETC  'Y'
.NEXT8   ANOP
&SGCTRLC(6) SETC 'IEAQGM01'
&SMF     SETB  (&SGSCHDB(7))                                        SMF
&TSO     SETB  (&SGSCHDB(44))                                     20021
         COPY  SGASMPAK
         PUNCH '         IEAQGM &RORI,&LCS,&MPS,&SMF,&TSO'        20021
         PUNCH '         END'
         PUNCH '/*'
&SGCTRLC(6) SETC 'IEAQPRT0'
         COPY  SGASMPAK
         PUNCH '         IEAQPR &RORI,&LCS,&MPS'
         PUNCH '         END'
         PUNCH '/*'
.NEXT99  MEND
