         MACRO
         CHECKER &TYPE=,&DESIGN=,&SIZE=
         COPY  SGGBLPAK
         LCLA  &A,&B
         AIF   (NOT &SGMENTB(57)).A1  NOT ENTERED BEFORE
 AIF ('&TYPE' EQ 'PL1' AND &SGCKPLB(1) OR '&TYPE' EQ 'FORTRAN' AND &SGCX
               KFTB(1)).X2 SAME TYPE CHECKER SPECIFIED BEFORE
         AGO   .A2
.A1      ANOP
&SGMENTB(57) SETB 1
.A2      ANOP
         AIF   (T'&TYPE EQ 'O').X4
         AIF   ('&TYPE' NE 'FORTRAN' AND '&TYPE' NE 'PL1').X6
    MNOTE *,'      &TYPE SYNTAX CHECKER SPECIFIED'
      AIF  ('&TYPE' EQ 'FORTRAN').C0
&SGCKPLB(1) SETB 1            PL1 SYNTAX CHECKER SWITCH
    MNOTE *,'      PL1 SYNTAX CHECKER SIZE(S) ARE-'
         AIF   (T'&SIZE EQ 'O').B3  DEFAULT SIZE TO 20K
.B1      AIF   (&A GE N'&SIZE).END   DONE WITH PL1
&A SETA &A+1
         AIF ('&SIZE(&A)' EQ '16K').B2
         AIF ('&SIZE(&A)' EQ '20K').B3
         AIF ('&SIZE(&A)' EQ '27K').B4
         AGO   .X8    INVALID SIZE
.B2 MNOTE *,'      16K'
&SGCKPLB(2) SETB 1
         AGO   .B1       RETURN
.B3 MNOTE *,'      20K'
&SGCKPLB(3) SETB 1
         AGO   .B1       RETURN
.B4 MNOTE *,'      27K'
&SGCKPLB(4) SETB 1
         AGO   .B1       RETURN
.C0        ANOP
&SGCKFTB(1) SETB 1            FORTRAN SYNTAX CHECKER SWITCH
    MNOTE *,'      FORTRAN SYNTAX CHECKER DESIGN(S) ARE-'
         AIF   (T'&DESIGN EQ 'O').C3 DEFAULT DESIGN TO G
.C1      AIF   (&B GE N'&DESIGN).END DONE WITH FORTRAN
&B SETA &B+1
         AIF   ('&DESIGN(&B)' EQ 'E').C2
         AIF   ('&DESIGN(&B)' EQ 'G').C3
         AIF   ('&DESIGN(&B)' EQ 'H').C4
         AGO   .XA   INVALID DESIGN
.C2 MNOTE *,'      E'
&SGCKFTB(2) SETB 1
         AGO   .C1       RETURN
.C3 MNOTE *,'      G'
&SGCKFTB(3) SETB 1
         AGO   .C1       RETURN
.C4 MNOTE *,'      H'
&SGCKFTB(4) SETB 1
         AGO   .C1       RETURN
.* * *   ERROR MNOTES                                             * * *
.X2 MNOTE 5,'* * * IEICKR002 &TYPE SYNTAX CHECKER SPECIFIED PREVIOUSLY'
&SGQUITB SETB  1
         AGO   .END
.X4 MNOTE 5,'* * * IEICKR004 TYPE VALUE MUST BE SPECIFIED ON THE CHECKEX
               R MACRO'
&SGQUITB SETB  1
         AGO   .END
.X6 MNOTE 5,'* * * IEICKR006 TYPE VALUE &TYPE IS INVALID'
&SGQUITB SETB  1
         AGO   .END
.X8 MNOTE 5,'* * * IEICKR008 SIZE VALUE &SIZE(&A) IS INVALID'
&SGQUITB SETB  1
         AGO   .B1
.XA MNOTE 5,'* * * IEICKR010 DESIGN VALUE &DESIGN(&B) IS INVALID'
&SGQUITB SETB  1
         AGO   .C1
.END     MEND
