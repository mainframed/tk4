         MACRO
 CTRLPROG      &MAXIO=,&FETCH=,&OVERLAY=,&TYPE=,&ADDTRAN=,&QSPACE=,    *
               &OPTIONS=,&SYSQUE=,&HIARCHY=,&TMSLICE=,&WARN=0    S21167
         COPY  SGGBLPAK
         LCLA  &B,&C,&E,&F,&G,&H,&J,&K,&L,&N,&SYS
         LCLB  &W,&Y,&EO
         LCLB  &D(7),&EX,&NOEX
         LCLB  &ZER,&S
         LCLC  &UNIT,&TRACK,&HARCH,&I,&M
         AIF   (&SGMENTB(5)).MR              CTRLPROG ENTRY SWITCH
&SGMENTB(5) SETB 1
         AIF   (T'&MAXIO EQ 'O').M2          MAXIO - OMISSION CHECK
         AIF   (T'&MAXIO NE 'N').M1                  SELF DEFINING CHEK
         AIF   (&MAXIO LT 1).M1              IS MAXIO ZERO?
&SGCPRGA(1) SETA &MAXIO                              SET SWITCH
    MNOTE *,'      UP TO &MAXIO SIMULTANEOUS INPUT-OUTPUT REQUESTS'
.WARN    AIF   (T'&WARN NE 'N').WRN    WARN EQ NUMBER?           S21167
         AIF   (&WARN EQ 0).WARN1      WARN EQ ZERO?             S21167
         AIF   (&WARN LT 1 OR &WARN GT 16777215).WRN             S21167
&SGCPRGB(14)   SETB 1                  WARN IN SYSTEM            S21167
     MNOTE *,'     WARN FEATURE TO BE INCLUDED TN THE SYSTEM'    S21167
.WARN1   ANOP                                                    S21167
&SGCPRGA(23)   SETA &WARN              SET WARN VALUE            S21167
         AGO   .TYPE                                             S21167
.WRN MNOTE 5,'* * * IEICTR127 WARN VALUE &WARN INVALID'          S21167
&SGQUITB SETB  1                       SET QUIT BIT              S21167
.TYPE    AIF   (T'&TYPE EQ 'O').MF                DEFAULT MFT SYSTEM
         AIF ('&TYPE' EQ 'MFT').MF           *
         AIF   ('&TYPE' EQ 'MVT').MV         *
         AIF   ('&TYPE' EQ 'M65MP').M6
         AGO   .T1                           * ERROR IN SPECIFICATION
.MF MNOTE *,'      MULTIPROGRAMMING WITH A FIXED NUMBER OF TASKS'
    MNOTE *,'        WITH PARTITIONED DYNAMIC STORAGE'
&SGCPRGB(4) SETB 1                      SET MFT SWITCH
.FET     AIF   (T'&FETCH NE 'O').WF
 AIF ('&HIARCHY' EQ 'INCLUDE').FED
&SGLOADB(2) SETB 1                      SET STD FETCH SWITCH
    MNOTE *,'      STD FETCH IS PROVIDED'
         AGO   .OL
.FED ANOP
&SGLOADB(3) SETB 1                     SET PCI FETCH
    MNOTE *,'      PCI FETCH IS PROVIDED'
 AGO .OL
.WF      ANOP                           DETERMINE FETCH VALUE
&SGLOADB(2) SETB ('&FETCH' EQ 'STD')    *
&SGLOADB(3) SETB ('&FETCH' EQ 'PCI')    *
         AIF   ( NOT (&SGLOADB(2) OR &SGLOADB(3))).F1
&SGLOADB(1) SETB 1
    MNOTE *,'      &FETCH FETCH IS REQUESTED'
 AIF ('&HIARCHY' EQ 'INCLUDE' AND &SGLOADB(2)).F3
.OL      AIF   (T'&OVERLAY EQ 'O').LD   DETERMINE OVERLAY VALUE
&SGOVERB(2) SETB ('&OVERLAY' EQ 'BASIC')  *
&SGOVERB(3) SETB ('&OVERLAY' EQ 'ADVANCED')  *
         AIF   ( NOT (&SGOVERB(2) OR &SGOVERB(3))).O1
    MNOTE *,'      &OVERLAY OVERLAY IS REQUESTED'
&SGOVERB(1) SETB  1
         AGO   .ADD
.M6 MNOTE *,'      EXTENDED MVT FOR M65 MULTIPROCESSING'
&SGCPRGB(13) SETB 1
         AGO   .MV2                MVT ASSUMED WITH M65MP
.MV MNOTE *,'      MULTIPROGRAMMING WITH A VARIABLE NUMBER OF TASKS'
.MV2     ANOP
&SGCPRGB(8) SETB 1                      SET MVT SWITCH
         AIF   (T'&FETCH EQ 'O').FD     TEST FOR OMISSION
         AIF   ('&FETCH' EQ 'STD').F2   TEST FOR INVALID SPECIFICATIONS
         AIF   ('&FETCH' NE 'PCI').F2   *
&SGLOADB(3) SETB 1                      SET PCI SWITCH
    MNOTE *,'      &FETCH FETCH IS REQUESTED'
         AGO   .OV
.FD MNOTE *,'      PCI FETCH IS ASSUMED'
.OV      AIF   (T'&OVERLAY EQ 'O').OD
         AIF   ('&OVERLAY' NE 'ASYCHRON').O2
&SGOVERB(1) SETB 1                      SET REQUEST SWITCH
    MNOTE *,'      &OVERLAY OVERLAY IS REQUESTED'
&SGOVERB(4) SETB 1
         AGO   .ADD
.OD MNOTE *,'      ASYCHRON OVERLAY IS ASSUMED'
&SGOVERB(4) SETB 1                      SET ASYCHRON SWITCH
&SGOVERB(1) SETB  1
         AGO   .ADD
.LD MNOTE *,'      BASIC OVERLAY IS PROVIDED'
&SGOVERB(2) SETB 1                      SET BASIC SWITCH
&SGOVERB(1) SETB  1
.ADD     AIF   (T'&ADDTRAN EQ 'O').QSP            TEST FOR OMISSION
         AIF   (&SGCPRGB(4)).A1                   INVALID FOR MFT
         AIF   (T'&ADDTRAN NE 'N').A2             MUST BE SELF DEFINING
         AIF   (&ADDTRAN GT 15).A3        EXCEEDES LIMIT OF 15  SA65985
    MNOTE *,'      &ADDTRAN ADDITIONAL PAIRS OF TRANSIENT AREAS'
&SGCPRGA(5) SETA &ADDTRAN
.QSP     AIF   (T'&QSPACE EQ 'O').QD              TEST FOR OMISSION
         AIF   (&SGCPRGB(4)).Q2                   INVALID FOR MFT
         AIF   (T'&QSPACE NE 'N').Q1              MUST BE SELF DEFINING
&SGCPRGA(6) SETA &QSPACE                          SET QSPACE VALUE
    MNOTE *,'      &SGCPRGA(6) 2K BLOCKS REQUESTED FOR SYSTEM QUEUE SPAX
               CE'
         AGO   .DAS
.QD      AIF   (NOT &SGCPRGB(8)).DAS             IF NOT MVT CONTINUE
&SGCPRGA(6) SETA 10                               ELSE SET MVT DEFAULT
    MNOTE *,'      10 2K BLOCKS PROVIDED FOR SYSTEM QUEUE SPACE'
.DAS     ANOP
.OPT     AIF  (T'&OPTIONS EQ 'O').OPT4      TEST FOR OPTIONS
&B       SETA  0
.OPT1    AIF   (&B EQ N'&OPTIONS).OPT4
&B       SETA  &B+1
         AIF   ('&OPTIONS(&B)' EQ 'ROLLOUT').OPT2
         AIF   ('&OPTIONS(&B)' EQ 'SERVAID').OPT1
         AIF   ('&OPTIONS(&B)' EQ 'NOSERVAID').OPT3
         AGO   .OA          INVALID OPTION
.OPT2    AIF   (&SGCPRGB(4)).OB                   CHECK FOR MFT
&SGCPRGB(6) SETB  1
    MNOTE *,'      ROLLOUT ROLLIN OPTION HAS BEEN REQUESTED'
         AGO   .OPT1
.OPT3    ANOP
    MNOTE 0,'      NOSERVAID OPTION INVALID,'
         AGO   .OPT1
.OPT4    ANOP
&SGMENTB(13) SETB  1
    MNOTE *,'      SERVICE AID PROGRAMS WILL BE INCLUDED'
.SYSQ    AIF   (T'&SYSQUE EQ 'O').SD              TEST FOR OMISSION
         AIF   (&SGCPRGB(8) OR &SGCPRGB(13)).S2   INVALID FOR MVT/M65MP
         AIF   (T'&SYSQUE NE 'N').S1              MUST BE SELF DEFINING
&SYS     SETA  (&SYSQUE/X'08'*X'08')
         AIF   (&SYSQUE EQ &SYS).SYSQ1
&SYS     SETA  &SYS+8
.SYSQ1   MNOTE *,' &SYS BYTES TO BE ALLOCATED TO SYSTEM QUEUE AREA'
&SGCPRGA(6) SETA &SYS
.LCS   AIF  (T'&HIARCHY EQ 'O').NXT
       AIF   ('&HIARCHY' EQ 'EXCLUDE').PV
      AIF    ('&HIARCHY' NE 'INCLUDE').L1
         AIF   (&SGCPRGB(13)).L2
&SGCPRGB(11) SETB 1                              HIERARCHIES REQUESTED
    MNOTE *,'      HIERARCHIES HAVE BEEN REQUESTED'
         AGO   .TMSLC
.PV MNOTE *,'      NO HIERARCHY STRUCTURE HAS BEEN REQUESTED'
         AGO   .TMSLC
.NXT MNOTE *,'     NO HIERARCHY STRUCTURE PROVIDED'
.TMSLC   AIF   (T'&TMSLICE EQ 'O').MEND           TIME SLICE SPECIFIED?
&SGCPRGB(12) SETB 1                               TIMESLICE IDENT SWTCH
         AIF   (&SGCPRGB(8) OR &SGCPRGB(13)).TMVT
         AIF   (&SGCPRGB(4)).TMFT
.TE MNOTE 5,'* * * IEICTR000 TYPE VALUE &TYPE INVALID FOR TIME SLICING'
&SGQUITB SETB  1
         AGO   .MEND
.TMFT    ANOP                                     MFT TIME SLICING
         AIF   (N'&TMSLICE GT 2).TF
&J       SETA  &J+1                               CHAR POINTER
         AIF   ('&TMSLICE'(&J,1) EQ '(').TMFT     SCAN ROUTINE
         AIF   ('&TMSLICE'(&J,2) EQ '-P').TM1     *
         AIF   ('&TMSLICE'(&J,1) EQ 'P').TM2      *
         AIF   ('&TMSLICE'(&J,5) EQ ',SLC-').TM3  *
         AIF   ('&TMSLICE'(&J,1) EQ ')').TM4      *
         AGO   .TMFT
.TM1     ANOP
&L       SETA  &J-&K                              LENGTH OF FRST PART
         AIF   (&K LE 0).TA
         AIF   (&L LE 0).TA
&M       SETC  '&TMSLICE'(&K,&L)                  FIRST PARTITION
         AIF   ('&M' GE '52' OR '&M' LT '0').TA
&SGCPRGA(8) SETA &M                               *     NUMBER SET
&J       SETA  &J+2                               REINITIALIZE TO
&K       SETA  &J                                 *     CONTINUE
         AGO   .TMFT                              *     SCAN
.TM2     ANOP                                     FIRST P FOUND
&K       SETA  &J+1                               *
         AGO   .TMFT                              *
.TM3     ANOP
&S       SETB  1
&L       SETA  &J-&K                              LENGTH OF SECND PART
&M       SETC  '&TMSLICE'(&K,&L)                  SECND PARTITION
         AIF   ('&M' GE '52' OR '&M' LT '0').TA
&SGCPRGA(9) SETA &M                               *     NUMBER SET
         AIF   (&SGCPRGA(8) GE &SGCPRGA(9)).TA
    MNOTE *,'      PARTITIONS &SGCPRGA(8) THROUGH &SGCPRGA(9) ARE IN THX
               E TIME SLICE GROUP OF TASKS'
&J       SETA  &J+5                               REINITIALIZE  TO
&K       SETA  &J                                 *     CONTINUE
         AGO   .TMFT                              *     SCAN
.TM4     ANOP                                     TIME SLICE VALUE
         AIF   (NOT &S).TF
&L       SETA  &J-&K                              *
&M       SETC  '&TMSLICE'(&K,&L)                  *
         AIF   ('&M' GT '9999' OR '&M' LT '20').TB
&SGCPRGA(10) SETA &M                              *
    MNOTE *,'      &SGCPRGA(10) MILLISECONDS IS THE MAXIMUM LENGTH OF TX
               IME EACH TASK IN TIME-SLICE GROUP WILL HAVE CPU CONTROL'
         AGO   .MEND                              *
.TMVT    ANOP
&K       SETA  (N'&TMSLICE/X'02'*X'02')
         AIF   (&K NE N'&TMSLICE).TF  NOT EVEN NUMBER
         AIF   (N'&TMSLICE EQ &L).MEND            MVT TIME-SLICING
&L       SETA  &L+1
         AIF   (&L EQ &L/X'02'*X'02').SLC         IF EVEN PICK UP TIME
         AIF   (T'&TMSLICE(&L) NE 'N').TC         ERROR IF NOT ARITHMTC
&J       SETA  &TMSLICE(&L)+8
         AIF   ((&J-8 GE 15) OR (&J-8 LT 0)).TC
         AGO   .TMVT
.SLC     AIF   ('&TMSLICE(&L)'(1,4) NE 'SLC-').TD PICK UP TIME
&N       SETA  (K'&TMSLICE(&L))
&M       SETC  '&TMSLICE(&L)'(5,&N-4)
         AIF   ('&M' GT '9999' OR '&M' LT '20').TD
&SGCPRGA(&J) SETA &M
&N       SETA  &J-8
    MNOTE *,'      &SGCPRGA(&J) MILLISECONDS IS THE MAXIMUM LENGTH OF TX
               IME EACH TASK IN PRIORITY &N WILL HAVE CPU CONTROL'
         AGO   .TMVT
.TA MNOTE 5,'* * * IEICTR005 TMSLICE PARTITION VALUE  &M IS INVALID'
&SGQUITB SETB  1
         AGO   .MEND
.TB MNOTE 5,'* * * IEICTR010 &M INVALID AS TIME-SLICE VALUE'
&SGQUITB SETB  1
         AGO   .MEND
.TC MNOTE 5,'* * * IEICTR015 &TMSLICE(&L) INVALID AS A PRIORITY VALUE'
&SGQUITB SETB  1
         AIF   (N'&TMSLICE EQ &L).MEND
&L       SETA  &L+1
         AGO   .TMVT
.TD MNOTE 5,'* * * IEICTR020 &TMSLICE(&L) INVALID AS TIME-SLICE VALUE'
&SGQUITB SETB  1
         AGO   .TMVT
.TF MNOTE 5,'* * * IEICTR025 TMSLICE VALUE &TMSLICE INVALID'
&SGQUITB SETB 1
         AGO   .MEND
.SD      AIF   (NOT &SGCPRGB(4)).LCS
&SGCPRGA(6) SETA 1600
         AGO   .LCS
.S1 MNOTE 5,'* * * IEICTR030 SYSQUE VALUE &SYSQUE INVALID'
&SGQUITB SETB  1
         AGO   .LCS
.S2 MNOTE 5,'* * * IEICTR035 SYSQUE VALUE INVALID FOR THE &TYPE SYSTEM'
&SGQUITB SETB  1
         AGO   .LCS
.OA MNOTE 5,'* * * IEICTR040 OPTIONS VALUE &OPTIONS INVALID'
&SGQUITB SETB  1
         AGO   .OPT1
.OB MNOTE 5,'* * * IEICTR045 OPTIONS VALUE &OPTIONS INVALID FOR &TYPE S*
               YSTEM'
&SGQUITB SETB  1
         AGO   .OPT1
.Q1 MNOTE 5,'* * * IEICTR050 QSPACE VALUE &QSPACE INVALID'
&SGQUITB SETB  1
         AGO   .DAS
.Q2 MNOTE 5,'* * * IEICTR055 QSPACE IS INVALID IN THE MFT SYSTEM'
&SGQUITB SETB  1
         AGO   .DAS
.A1 MNOTE 5,'* * * IEICTR060 ADDTRAN IS INVALID IN THE MFT SYSTEM'
&SGQUITB SETB  1
         AGO   .QSP
.A2 MNOTE 5,'* * * IEICTR065 ADDTRAN VALUE &ADDTRAN INVALID'
&SGQUITB SETB  1
         AGO   .QSP
.A3 MNOTE 5,'* * * IEICTR070 ADDTRAN VALUE &ADDTRAN INVALID. ADDTRAN MU*
               ST NOT EXCEED 32.'
&SGQUITB  SETB 1
         AGO   .QSP
.T1 MNOTE 5,'* * * IEICTR075 TYPE VALUE &TYPE INVALID'
&SGQUITB SETB  1
         AGO   .MEND
.O1 MNOTE 5,'* * * IEICTR080 OVERLAY VALUE &OVERLAY INVALID'
&SGQUITB SETB  1
         AGO   .ADD
.O2 MNOTE 5,'* * * IEICTR085 OVERLAY VALUE &OVERLAY INVALID IN         *
               THE &TYPE SYSTEM'
&SGQUITB SETB  1
         AGO   .ADD
.F1 MNOTE 5,'* * * IEICTR090 FETCH VALUE &FETCH INVALID'
&SGQUITB SETB  1
         AGO   .OL
.F2 MNOTE 5,'* * * IEICTR095 FETCH VALUE &FETCH INVALID FOR &TYPE SYSTE*
               M'
&SGQUITB SETB  1
         AGO   .OV
.F3 MNOTE 5,'* * * IEICTR100 FETCH VALUE STD INVALID WITH'
    MNOTE *,'      HIARCHY=INCLUDE'
&SGQUITB SETB 1
 AGO .OL
.M1 MNOTE 5,'* * * IEICTR105 MAXIO VALUE &MAXIO INVALID'
&SGQUITB SETB  1
         AGO   .WARN                                             S21167
.M2 MNOTE 5,'* * * IEICTR110 MAXIO VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .WARN                                             S21167
.L1  MNOTE 5,'* * * IEICTRL115 HIARCHY VALUE &HIARCHY INVALID'
&SGQUITB  SETB   1
         AGO   .TMSLC
.L2 MNOTE 5,'* * * IEICTR120 HIARCHY INVALID FOR &TYPE SYSTEM'
&SGQUITB  SETB  1
         AGO   .TMSLC
.MR MNOTE 5,'* * * IEICTR125 CTRLPROG MACRO PREVIOUSLY USED'
&SGQUITB SETB  1
.MEND    MEND
