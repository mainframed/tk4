         MACRO
&NAME    CHANNEL &ADDRESS=,&TYPE=
         COPY  SGGBLPAK
         LCLA  &A,&B,&V
         LCLC  &ID
&SGMENTB(2)   SETB   1       CHANNEL ENTRY SWITCH.
&B       SETA  0
&SGCNTRA(1)    SETA &SGCNTRA(1)+1
.BACK1   AIF   ('&NAME' EQ '').NONE  BRANCH IF NAME NOT SPECIFIED.
&ID      SETC  '&NAME'
         AGO   .ON1
.NONE    ANOP
&ID      SETC  'CHAN. &SGCNTRA(1)'
.ON1     AIF   (&SGCNTRA(1) GT 14).E1
.BACK2   AIF   (T'&ADDRESS EQ 'O').E2      MUST BE SPECIFIED AND ONE
         AIF   (K'&ADDRESS NE 1).E3     ERROR ON NUMBER OF DIGITS
         AIF   (NOT(('&ADDRESS' GE '0' AND '&ADDRESS' LE '9')OR('&ADDRE*
               SS' GE 'A' AND '&ADDRESS' LE 'D'))).E3
         CONVERT TO=HEX,DIGITS=1,VALUE=&ADDRESS
&B       SETA  &HEX*15
         AIF   (&SGCHANB(&B+1)).E5      CHANNEL PREVIOUSLY SPECIFIED
    MNOTE *,'     CHANNEL &ADDRESS IS PRESENT'
&SGCHANB(&B+1) SETB 1
.TYPE    AIF   (T'&TYPE EQ 'O').E6
         AIF   (N'&TYPE EQ 1).ONE
         AIF   (N'&TYPE GT 2).E7   MAXIMUM OF TWO.
.TEST    AIF   ('&TYPE(1)'  EQ 'SELECTOR').S1
         AIF   ('&TYPE(2)'  EQ 'SELECTOR').S2
         AGO   .E8
.S1      ANOP
&A       SETA  2         SECOND VALUE MUST BE STORAGE.
         AGO   .ON2
.S2      ANOP
&A       SETA  1
.ON2  AIF  ('&TYPE(&A)' NE 'STORAGE').E8
    MNOTE *,'        IT IS A SELECTOR-STORAGE CHANNEL'
&SGCHANB(&B+3) SETB 1
&SGCHANB(&B+5) SETB 1
         AGO   .MEND
.ONE     AIF   ('&TYPE(1)' EQ 'SELECTOR').S3
         AIF   ('&TYPE(1)' EQ 'MULTIPLEXOR').M1
         AIF   ('&TYPE(1)' EQ 'HISPEEDMULTIPLEXOR').M2
         AIF   ('&TYPE(1)' EQ 'HISPEED').H1
         AIF   ('&TYPE(1)' EQ 'BLKMPXR').C1
    AGO  .E8
.C1 MNOTE *,'        IT IS A BLKMPXR CHANNEL'
&SGCHANB(&B+6) SETB 1
         AGO   .MEND
.S3 MNOTE *,'        IT IS A SELECTOR CHANNEL'
&SGCHANB(&B+3) SETB 1
         AGO   .MEND
.M1 MNOTE *,'        IT IS A MULTIPLEXOR CHANNEL'
&SGCHANB(&B+2) SETB 1
         AGO   .MEND
.M2 MNOTE *,'        IT IS A HIGH SPEED MULTIPLEXOR CHANNEL'
&SGCHANB(&B+4) SETB 1
         AGO   .MEND
.H1 MNOTE *,'        IT IS A HIGH SPEED CHANNEL'
&SGCHANB(&B+4) SETB 1
         AGO   .MEND
.E1 MNOTE 5,'* * * IEICHA101 &ID-MAXIMUM OF 14 CHANNELS EXCEEDED'
&SGQUITB SETB  1
         AGO   .BACK2    BACK TO CHECK AS MUCH AS POSSIBLE.
.E2 MNOTE 5,'* * * IEICHA102 &ID-ADDRESS VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .TYPE
.E3 MNOTE 5,'* * * IEICHA103 &ID-ADDRESS VALUE &ADDRESS INVALID'
&SGQUITB SETB  1
         AGO   .TYPE
.E5 MNOTE 5,'* * * IEICHA104 &ID-CHANNEL &ADDRESS PREVIOUSLY SPECIFIED'
&SGQUITB SETB  1
         AGO   .TYPE
.E6 MNOTE 5,'* * * IEICHA105 &ID-TYPE VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .MEND
.E7 MNOTE 5,'* * * IEICHA106 &ID-TOO MANY TYPE VALUES'
&SGQUITB SETB  1
         AGO   .TEST
.E8 MNOTE 5,'* * * IEICHA107 &ID-TYPE VALUE(S) &TYPE INVALID'
&SGQUITB SETB  1
.MEND  MEND
