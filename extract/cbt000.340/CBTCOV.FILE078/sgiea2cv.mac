         MACRO
         SGIEA2CV
         COPY  SGGBLPAK
         LCLC  &SYS,&OPTION(2)
         LCLA  &OPA                                                I250
         LCLA  &FLAG                                              19022
         LCLA  &FLAGB                                             20048
         LCLB  &TOD                                               20071
         LCLC  &PATCH                                             20075
         LCLC  &RPS                                               20201
.*0847000800,001200,002200-002400,002600-003000,003600-003800      UPDT
.*0847005700                                                       UPDT
&OPTION(1) SETC  'NOSLICE'                                         MTS0
&OPTION(2) SETC 'NOTIME'
&OPA     SETA  &SGCPRNA(1)                                         I250
.*
.*       &FLAG SETS THE FOLLOWING BITS IN THE CVT CVTOPTA BYTE    19022
.*             RMS BITS      182.0,182.1,182.2                    19022
.*             ATTACH BIT    182.5                                 I250
.*             ASCII BIT  182.6                                   20038
.*
.************************************************************     20075
.*                                                                20075
.*           &PATCH SET CVTPATCH WORD IN CVT TO ADDRESS OR ZERO   20075
.*                                                                20075
.*                                                                20075
.***************************************************************
.*                                                                20048
.*       &FLAGB SETS THE FOLLOWING BITS IN CVTOPTB (BYTE 183)     20048
.*                                                                20048
.*             183.0         PROTECTION BIT                       20048
.*             183.2         CVTTOD BIT
.*                                                                20048
.****** TEST FOR STORAGE CONFIGURATION ********************************
         AIF   (&SGCPRGB(1) NE 1).NEXT1
&SYS     SETC  'PCP'
         AGO   .NEXT4
.NEXT1   ANOP                                                     19022
.**********   CHECK FOR CPUS WITHOUT THE TOD CLOCK FEATURE  ****  20071
.*       MODELS  30,40,50,65,75,85,91 AND 195/360             CD 21108
&TOD     SETB  (NOT (&SGCPUB(1) OR &SGCPUB(2) OR &SGCPUB(3) OR &SGCPUB(*
               4) OR &SGCPUB(5) OR &SGCPUB(6) OR &SGCPUB(7) OR &SGCPUB(*
               9)))
&FLAGB   SETA  32*&TOD+128*&SGSUPRB(23)                          200071
&FLAG    SETA  2*&SGSUPRB(41)+32*&SGSUPRB(33)+64*&SGSUPRB(35)+128*&SGSU*
               PRB(29)                                            20038
         AIF   (&SGCPRGB(4) NE 1).NEXT2                           19022
&SYS     SETC  'MFT'
&FLAG    SETA  2*&SGSUPRB(41)+8*&SGSUPRB(37)+32*&SGSUPRB(33)+64*&SGSUPR*
               B(35)+128*&SGSUPRB(29)                             20038
         AGO   .NEXT4
.NEXT2   ANOP                                                      MFT2
&SYS     SETC  'MVT'
.****** TEST FOR INTERVAL TIMING **************************************
.NEXT4   AIF   (&SGSUPRB(14) NE 1 AND &SGSUPRB(13) NE 1 AND &SGSUPRB(19*
               ) NE 1).NEXT5                                      M4796
&OPTION(2) SETC 'INTERVAL'
         AIF   (NOT &SGCPRGB(12)).NEXT5                            MTS0
&OPTION(1) SETC 'TSLICE'                                           MTS0
.NEXT5   ANOP
&SGCTRLC(6) SETC 'IEACVT00'
&PATCH   SETC   'YES'                                             20075
.********   CHECK FOR RPS (ROTATIONAL POSITION SENSING) DEVICES   20201
&RPS     SETC  'NO'                                               20201
         AIF   (NOT (&SGDCLSB(69) OR &SGDCLSB(75) OR &SGDCLSB(76))).NOR*
               PS                                                 20201
&RPS     SETC  'YES'                                              20201
.NORPS   ANOP                                                     20201
         COPY  SGASMPAK
         PUNCH 'IEACVTRN CSECT '
         PUNCH '         ENTRY IEACVT '
         PUNCH '         DC    A(0)'                              19026
         PUNCH '         IHBRELNO     '
         PUNCH 'IEACVT   CVT SYS=&SYS,OPTIONS=(&OPTION(1),&OPTION(2),&O*
               PA),           XXXXXXXXXXXXXXXXX'
         PUNCH '               FLAG=&FLAG,FLAGB=&FLAGB,PATCH=&PATCH,RPS*
               =&RPS '                                            20201
         PUNCH '         END'
         PUNCH '/*'
         MEND
