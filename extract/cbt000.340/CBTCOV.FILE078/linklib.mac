         MACRO
         LINKLIB &PDS=,&MEMBERS=
         COPY  SGGBLPAK
         LCLA    &NOMEM,&CTR
.* UPDATED 29 DEC 65
         AIF   (NOT &SGMENTB(40)).LNK4       1ST TIME CALLED
    MNOTE 5,'* * * IEILNK101 LINKLIB MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                             QUIT SWITCH ON
         MEXIT
.LNK4    ANOP
&SGMENTB(40)  SETB  1                        ENTRY SWITCH ON
         AIF     ('&PDS' NE '').LNK1
    MNOTE 5,'* * * IEILNK102 PDS VALUE NOT SPECIFIED'
&SGQUITB SETB    1
    AGO  .LNK2
.LNK1 AIF ('&PDS'(1,5) EQ 'SYS1.' AND K'&PDS LE 13).LNK2
.L9 MNOTE 5,'* * * IEILNK103 PDS VALUE &PDS INVALID'
&SGQUITB SETB    1
         AGO  .L6
.LNK2   AIF  ('&PDS'(6,1) LT 'A' OR '&PDS'(6,1) GT 'Z').L9
.L6      ANOP
&CTR     SETA    K'&PDS
&SGLNKLC(1) SETC '&PDS'(6,&CTR-5)
         AIF     (N'&MEMBERS EQ 0).L3
         AIF   (NOT &LIMIT(4)).LNK22
         AIF   (N'&MEMBERS GT 5).L4
         AGO   .LNK24
.LNK22   AIF   (N'&MEMBERS GT 20).L4
.LNK24   ANOP
    MNOTE *,' FOLLOWING MEMBERS FROM &PDS MOVED TO LINKLIB '
&NOMEM   SETA    1+N'&MEMBERS
&CTR     SETA    1
.LNK3    ANOP
&CTR     SETA    &CTR+1
         AIF     (&CTR GT &NOMEM).LNKEND
         AIF     (K'&MEMBERS(&CTR-1) GT 8).L5
&SGLNKLC(&CTR)   SETC  '&MEMBERS(&CTR-1)'
    MNOTE *,'        &MEMBERS(&CTR-1) '
         AGO     .LNK3
.L3 MNOTE 5,'* * * IEILNK104 MEMBERS VALUE NOT SPECIFIED'
&SGQUITB  SETB  1
         MEXIT
.L4 MNOTE 5,'* * * IEILNK105 MAXIMUM NUMBER OF MEMBERS EXCEEDED'
&SGQUITB SETB    1
         MEXIT
.L5 MNOTE 5,'* * * IEILNK106 MEMBERS VALUE &MEMBERS(&CTR-1) INVALID'
&SGQUITB  SETB  1
         AGO     .LNK3
.LNKEND  MEND
