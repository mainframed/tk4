         MACRO
         SGIEA2ST
         COPY  SGGBLPAK
         LCLA  &STORLO,&STORGAP,&STORHI,&CURRGAP,&CURRLO,&CURRHI
         LCLA  &N,&I,&TYPE
         LCLC  &DESC(3),&NAME(3),&C(3),&BLK,&TYPEC,&SVCNO,&CON(3),&B(3)
.*0012015200                                                       2090
&SGCTRLC(6) SETC 'IEASVC00'
         COPY  SGASMPAK
.*
.* PRINT SVC TABLE
.*
&I       SETA  1
&BLK     SETC  '        '
   MNOTE *,' * * * * * * SVC TABLE * * * * * *'
   MNOTE *,'      NUMBER           TYPE       '
   MNOTE *,'                                  '
   MNOTE *,'        0                1        '
.NEXT0   ANOP
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
         AIF   (&TYPE EQ 0).NEXT2
&SVCNO   SETC  '&I'.'&BLK'(1,2)
&TYPEC   SETC  'NOP'
         AIF   (&TYPE EQ 5).NEXT1
&TYPEC   SETC  '&TYPE'
.NEXT1   ANOP
&TYPEC   SETC  '&TYPEC'.'&BLK'(1,2)
&TYPEC   SETC  '&TYPE'(1,3)
&SVCNO   SETC  '&SVCNO'(1,3)
   MNOTE *,'        &SVCNO              &TYPEC'
.NEXT2   ANOP
&I       SETA  &I+1
         AIF   (&I LE 255).NEXT0
   MNOTE *,' * * * * * * * * * * * * * * * * *'
.*
.* COMPUTE LARGEST GAP IN SVC NUMBERS
.*
&I       SETA  0
.NEXT3   ANOP
&I       SETA  &I+1
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
         AIF   (&TYPE NE 0).NOGAP
&STORGAP SETA  &STORGAP+1
&STORHI  SETA  0
         AIF   (&I LT 255).NEXT3
         AIF   (&STORGAP LT &CURRGAP).CONT3
&CURRLO  SETA  &STORLO
&CURRHI  SETA  256
         AGO   .CONT3
.NOGAP   ANOP
&STORHI  SETA  &I
         AIF   (&STORGAP LT &CURRGAP).CONT2
&CURRLO  SETA  &STORLO
&CURRHI  SETA  &STORHI
&CURRGAP SETA  &STORGAP
.CONT2   ANOP
&STORGAP SETA  0
&STORLO  SETA  &I
         AIF   (&I LT 255).NEXT3
.CONT3   ANOP
&IEAHIA  SETA  &CURRLO
&IEALOA  SETA  &CURRHI
.*
.* GENERATE SVC TABLE
.*
         PUNCH ' ENTRY USERORG'
         PUNCH 'IBMORG   CSECT'
&N       SETA  2
&I       SETA  0
&CON(1)  SETC  'VL3('
&DESC(1) SETC  'X''''00'''''
&NAME(1) SETC  'IGC000'
&B(1)    SETC  ')'
.NEXT4   AIF   (&I NE &IEAHIA).NEXT5
&I       SETA  &IEALOA-1
         PUNCH 'USERORG  DS    0F'
         AIF   (&IEALOA GT 255).NEXT30                             2090
.NEXT5   ANOP
&I       SETA  &I+1
&CON(&N) SETC  'VL3('
&DESC(&N) SETC 'X''''00'''''
&C(&N)   SETC  ','
&B(&N)   SETC  ')'
&TYPE    SETA  (4*&SGSVCAB(&I)+2*&SGSVCBB(&I)+1*&SGSVCCB(&I))
         AIF   (&TYPE NE 0).NEXT10
&NAME(&N) SETC 'IGCERROR'
         AGO   .NEXT25
.NEXT10  AIF   (NOT(&TYPE EQ 1 OR &TYPE EQ 2)).NEXT15
         AIF   (&TYPE EQ 1).NEXT101
&DESC(&N) SETC 'X''''80'''''
.NEXT101 AIF   (&I LT 10).NEXT10A
         AIF   (&I LT 100).NEXT10B
&NAME(&N) SETC 'IGC'.'&I'
         AGO   .NEXT25
.NEXT10A ANOP
&NAME(&N) SETC 'IGC00'.'&I'
         AGO   .NEXT25
.NEXT10B ANOP
&NAME(&N) SETC 'IGC0'.'&I'
         AGO   .NEXT25
.NEXT15  AIF   (NOT(&TYPE EQ 3 OR &TYPE EQ 4)).NEXT20
&DESC(&N) SETC 'X''''C0'''''
&NAME(&N) SETC '0'
&CON(&N) SETC  'AL3('
         AGO   .NEXT25
.NEXT20  ANOP
&NAME(&N) SETC 'IGCRETRN'
&CON(&N) SETC  'AL3('
.NEXT25  ANOP
&N       SETA  &N+1
         AIF   (&I EQ 255 OR &I EQ &IEAHIA).PAD
         AIF   (&N LE 3).NEXT4
         AGO   .PUNCH
.PAD     AIF   (&N EQ 4).PUNCH
&DESC(&N) SETC ''
&NAME(&N) SETC ''
&C(&N)   SETC  ''
&B(&N)   SETC  ''
&CON(&N) SETC  ''
&N       SETA  &N+1
         AGO   .PAD
.PUNCH   ANOP
         PUNCH ' DC &DESC(1),&CON(1)&NAME(1)&B(1)&C(2)&DESC(2)&C(2)&CON*
               (2)&NAME(2)&B(2)&C(3)&DESC(3)&C(3)&CON(3)&NAME(3)&B(3)'
&N       SETA  1
         AIF   ((&I LE &IEAHIA) OR (&I GE &IEALOA AND &I LT 255)).NEXT4
.NEXT30  ANOP
         PUNCH 'IGCRETRN SR    15,15 '
         PUNCH '         BR    14 '
         PUNCH '         END'
         PUNCH '/*'
         MEND
