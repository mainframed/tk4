         MACRO
&NAME IOCONTRL &UNIT=,&ADDRESS=,&MODEL=,&FEATURE=,&EXPBFR=
         COPY  SGGBLPAK
         LCLA  &A,&B,&C
         LCLA  &K,&L
         LCLC  &ID
         LCLC &CHANNEL
      AIF   (&SGMENTB(3)).MAX4
&SGMENTB(3)   SETB   1      IOCONTRL ENTRY SWITCH.
         AIF   (&LIMIT(1)).MAX1
         AIF   (&LIMIT(2)).MAX2
         AIF (&LIMIT(3)).MAX3
&SGMAXA(1)  SETA  40
         AGO   .MX
.MAX1    ANOP
&SGMAXA(1)  SETA  80
         AGO   .MX
.MAX2    ANOP
&SGMAXA(1)  SETA  16
 AGO .MX
.MAX3  ANOP
&SGMAXA(1) SETA 255
.MX MNOTE *,'      MAXIMUM OF &SGMAXA(1) I/O CONTROL UNITS ALLOWED'
.MAX4    ANOP
 AIF (NOT &SGNODEV).SETSW
   MNOTE 0,'* * * IEIIOC114 PREVIOUS CONTROL UNIT HAS NO DEVICES'
.SETSW ANOP
&SGNODEV SETB 1              INDICATE NO DEVICE ON C.U.
&SGCNTRA(2)    SETA &SGCNTRA(2)+1  TOTAL COUNT OF CONTROL UNITS.
&A       SETA  &SGCNTRA(2)
         AIF   ('&NAME' EQ '').NONAME   ANY NAME FIELD VALUE.
&ID      SETC  '&NAME'
         AGO   .CHKM
.NONAME  ANOP
&ID      SETC  'UNIT &A'
.CHKM    AIF  (&A GT &SGMAXA(1)).EA
         AIF   (T'&UNIT EQ 'O').E1
&SGUNT1B(&A)   SETB  1
         AIF   ('&UNIT' EQ '1051').U2
         AIF   ('&UNIT' EQ '2403').U3
         AIF   ('&UNIT' EQ '2404').U4
         AIF   ('&UNIT' EQ '2415').U5
         AIF   ('&UNIT' EQ '2803').U9
         AIF   ('&UNIT' EQ '2804').UA
&SGUNT1B(&A)   SETB  0
&SGUNT2B(&A)   SETB  1
         AIF   ('&UNIT' EQ '2701').U6
         AIF   ('&UNIT' EQ '2702').U7
         AIF   ('&UNIT' EQ '2820').UB
         AIF   ('&UNIT' EQ '2821').UC
         AIF   ('&UNIT' EQ '2822').UD
         AIF   ('&UNIT' EQ '2840').UE
         AIF   ('&UNIT' EQ '2841').UF
         AIF   ('&UNIT' EQ '2848').UG
&SGUNT2B(&A) SETB 0
&SGUNT3B(&A) SETB 1
         AIF   ('&UNIT' EQ '2703').UH
         AIF   ('&UNIT' EQ '3803').U9A
         AIF   ('&UNIT' EQ '3410').U9B
         AIF   ('&UNIT' EQ '3811').UJ
         AIF   ('&UNIT' EQ '3272').UK
    MNOTE 5,'* * * IEIIOC102 &ID-UNIT VALUE &UNIT INVALID'
&SGQUITB SETB  1
         AGO   .ON1
.U2 MNOTE *,'     1051 DATA COMMUNICATIONS CONTROL UNIT'
&SGUNU2B(&A)   SETB 1              SECOND GLOBAL UNIT SWITCH.
         AIF   (T'&MODEL EQ 'O').E2     ERROR IF NO MODEL.
         AIF   ('&MODEL' EQ '1').M1     MODELS 1   AND N1 ARE VALID.
         AIF   ('&MODEL' EQ 'N1').M2    ON 1051. IF NOT ONE OF THESE
    MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
&SGQUITB SETB  1
         AGO   .NEXT1    GO CHECK FOR FEATURES.
.E2 MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .NEXT1
.M1 MNOTE *,'        MODEL 1'
&SGUNM1B(&A)   SETB 1              FIRST GLOBAL MODEL SWITCH.
         AGO   .NEXT1              GO CHECK FOR FEATURES.
.M2 MNOTE *,'        MODEL N1'
&SGUNM3B(&A)   SETB 1              THIRD GLOBAL MODEL SWITCH.
.NEXT1   AIF   (T'&FEATURE EQ 'O').ON1
         AIF   ('&FEATURE(1)' EQ 'ALARM').F2      ONLY VALID FEATURE.
         AGO   .E4                 GO TO COMMON ERROR ROUTINE.
.F2 MNOTE *,'        AUDIBLE ALARM FEATURE'
&SGUNF1B(&A)   SETB 1         FIRST GLOBAL FEATURE SWITCH.
         AGO   .ERRMORE       GO TO COMMON CHECK FOR MORE FEATURES RTN.
.U3 MNOTE *,'     2403 MAGNETIC TAPE CONTROL UNIT'
&SGUNU3B(&A)   SETB 1         THIRD GLOBAL UNIT SWITCH.
         AGO   .UP2404   GO UP TO COMMON MODEL ROUTINE.
.U5 MNOTE *,'     2415 MAGNETIC TAPE CONTROL UNIT'
&SGUNU8B(&A)   SETB 1
         AGO   .UP2404
.U4 MNOTE *,'     2404 MAGNETIC TAPE CONTROL UNIT'
&SGUNU4B(&A)   SETB 1              FOURTH GLOBAL UNIT SWITCH.
.UP2404  AIF   (T'&MODEL EQ 'O').E3    ERROR IF NO MODEL.
         AIF   ('&MODEL' EQ '1' OR '&MODEL' EQ '2' OR '&MODEL' EQ      X
               '3').O2
         AIF  (('&MODEL' EQ '4' OR '&MODEL' EQ '5' OR '&MODEL' EQ '6') *
               AND (&SGUNU3B(&A) OR &SGUNU8B(&A))).O2 2403-2415 MOD456
.HS MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
&SGQUITB SETB  1
         AGO   .NEXT2         GO CHECK FOR FEATURES.
.E3 MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .NEXT2         GO CHECK FOR FEATURES.
.O2 MNOTE *,'        MODEL &MODEL'
&SGUNM1B(&A)   SETB (&MODEL EQ 1)
&SGUNM2B(&A)   SETB (&MODEL EQ 2)
&SGUNM3B(&A)   SETB (&MODEL EQ 3)
&SGUNM4B(&A)   SETB (&MODEL EQ 4)
&SGUNM5B(&A)   SETB (&MODEL EQ 5)
&SGUNM6B(&A)   SETB (&MODEL EQ 6)
         AGO   .NEXT2         GO CHECK FOR FEATURES.
.U9 MNOTE *,'     2803 MAGNETIC TAPE CONTROL UNIT'
&SGUNU6B(&A)   SETB 1              SIXTH GLOBAL UNIT SWITCH.
         AGO   .MOD12
.U9A     ANOP
   MNOTE *,'     3803 MAGNETIC TAPE CONTROL UNIT '
&SGUNU3B(&A)   SETB 1                   GLOBAL UNIT SWITCH
&SGUNF2B(&A)   SETB 1                   DATACONV SWITCH
         AGO   .MOD12
.U9B     ANOP
   MNOTE *,'     3410 MAGNETIC TAPE CONTROL UNIT '
&SGUNU4B(&A)   SETB 1                   GLOBAL UNIT SWITCH
         AGO   .MOD12
.UA MNOTE *,'     2804 MAGNETIC TAPE CONTROL UNIT'
&SGUNU7B(&A)   SETB 1              SEVENTH GLOBAL UNIT SWITCH.
.MOD12   AIF   (T'&MODEL EQ 'O').E3   ERROR IF NO MODEL
         AIF   ('&MODEL' EQ '1' OR '&MODEL' EQ '2').O2
         AIF   ('&UNIT' EQ '3803').HS   ONLY TWO MODELS VALID FOR 3803
         AIF   ('&MODEL' EQ '3').O2
         AGO   .HS       MODEL VAVUE INVALID
.NEXT2   AIF   (&B EQ N'&FEATURE).ON1   CHECK FOR FEATURES.
&B       SETA  &B+1      THIS FEATURE ROUTINE IS COMMON FOR 2403,2404,
         AIF   ('&FEATURE(&B)' EQ '800BPI').FX
         AIF   ('&FEATURE(&B)' EQ '7-TRACK').F3   2803 AND 2804
         AIF   ('&UNIT' EQ '3410').NO  NO OTHER FEATURES FOR 3410
         AIF   ('&FEATURE(&B)' EQ 'ALTCTRL').FG
         AIF   ('&FEATURE(&B)' EQ 'DATACONV').F4
         AIF   ('&UNIT' EQ '3803').NO  NO OTHER FEATURES FOR 3803
         AIF   ('&UNIT'(4,1) EQ '4' OR '&UNIT' EQ '2415').NO 2404,2804
  AIF ('&FEATURE(&B)' EQ '16-DRIVE').F5  AND 2415 CANNOT HAVE 16-DRIVE.
.NO MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB SETB  1
         AGO   .NEXT2         GO BACK FOR MORE FEATURES.
.F3 MNOTE *,'        7-TRACK COMPATIBILITY FEATURE'
&SGUNF1B(&A)   SETB 1              FIRST GLOBAL FEATURE SWITCH.
         AIF   (&SGUNF4B(&A)).FX22A  800BPI FEATURE?
         AIF   ('&UNIT' NE '3410').NEXT2  CHECK FOR 3410 7-TRK
&SGUNF2B(&A)   SETB 1              DEFAULT DATACONV FOR 3410 7-TRK
         AGO   .NEXT2         GO BACK FOR MORE FEATURES.
.F4 MNOTE *,'        DATA CONVERSION FEATURE'
&SGUNF2B(&A)   SETB 1              SECOND GLOBAL FEATURE SWITCH.
         AGO   .NEXT2              GO BACK FOR MORE FEATURES.
.F5 MNOTE *,'        16-DRIVE ADDRESSING FEATURE'
&SGUNF3B(&A)   SETB 1              THIRD  GLOBAL FEATURE SWITCH.
         AGO   .NEXT2              GO BACK FOR MORE FEATURES.
.FG MNOTE *,'        ALTERNATE CONTROL FEATURE'
&SGUNF6B(&A) SETB 1                    ALTERNATE CONTROL THIS C.U.
&SGMENTB(86) SETB 1                    ALTCTRL SPECIFIED DURING SYSGEN
 AGO .NEXT2
.*                                      800BPI FEATURE VALID ONLY ON
.*                                      MODEL 4,5,OR 6 OF 2403 OR 2415
.*                                      OR ON MODEL 2 OF 2803 OR 2804
.*                                      OR ON 3410
.*                                      OR ON 3803
.FX      AIF   ((&SGUNU3B(&A) OR &SGUNU8B(&A)) AND (&SGUNM4B(&A) OR &SG*
               UNM5B(&A) OR &SGUNM6B(&A))).FX1
         AIF   ((&SGUNU6B(&A) OR &SGUNU7B(&A)) AND &SGUNM2B(&A)).FX1
         AIF   (&SGUNU4B(&A) AND &SGUNT3B(&A)).FX1    VALID FOR 3410
         AIF   (NOT(&SGUNU3B(&A) AND &SGUNT3B(&A))).NO
.FX1     ANOP
    MNOTE *,'        800 BPI DENSITY FEATURE'
&SGUNF4B(&A)   SETB 1
         AIF   (&SGUNF1B(&A)).FX22A  7-TRACK FEATURE?
         AGO  .NEXT2
.FX2 MNOTE 5,'* * * IEIIOC115 800BPI AND 7-TRACK FEATURES ARE MUTUALLY *
               EXCLUSIVE FOR THE &UNIT CONTROL UNIT'
&SGQUITB SETB  1
         AGO   .NEXT2
.FX22A   AIF   ('&UNIT' EQ '3410').FX2                          SA69286
         AIF   ('&UNIT' EQ '3803' AND '&MODEL' NE '2').FX2      SA69286
         AGO   .NEXT2
.U6 MNOTE *,'     2701 DATA ADAPTER UNIT'
&SGUNU1B(&A)   SETB 1              FIRST GLOBAL UNIT SWITCH.
         AGO   .NOFEAT
.U7 MNOTE *,'     2702 TRANSMISSION CONTROL UNIT'
&SGUNU2B(&A)   SETB 1              SECOND GLOBAL UNIT SWITCH.
         AGO   .NOFEAT
.UB MNOTE *,'     2820 DRUM STORAGE CONTROL UNIT'
&SGUNU3B(&A)   SETB 1              THIRD GLOBAL UNIT SWITCH.
&SGDCLSB(40)  SETB  1
&SGUNF2B(&A)  SETB  1
         AIF   (T'&FEATURE EQ 'O').ON1
.RET2820 AIF (&B EQ N'&FEATURE).TSTFEAT
&B SETA &B+1
 AIF ('&FEATURE(&B)' EQ '2-CHANSW').F7
 AIF ('&FEATURE(&B)' EQ 'SHARED').F71
 MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB SETB 1
 AGO .RET2820
.F7 MNOTE *,'        TWO CHANNEL SWITCH'
&SGUNF3B(&A)   SETB 1              THIRD GLOBAL FEATURE SWITCH.
 AGO .RET2820
.F71 MNOTE *,'        SHARED DASD'
 MNOTE 0,'* * * IEIIOCW01 HARDWARE CHANNEL SWITCHING CAPABILITY MUST BE*
               AVAILABLE WITH SHARED'
&SGUNF5B(&A) SETB 1        FIFTH GLOBAL FEATURE SWITCH
&SGDCLSB(54) SETB 1       SHARED DASD CLASS SWITCH
 AGO .RET2820
.TSTFEAT AIF (&SGUNF5B(&A) AND NOT &SGUNF3B(&A)).INCFEAT
 AGO .ON1
.INCFEAT MNOTE 5,'* * * IEIIOC113 2-CHANSW REQUIRED WITH SHARED'
&SGQUITB SETB 1
 AGO .ON1
.UC MNOTE *,'     2821 READ,PUNCH AND PRINTER CONTROL UNIT'
&SGUNU4B(&A)   SETB 1              FOURTH GLOBAL UNIT SWITCH.
         AIF   (T'&MODEL EQ 'O').E5    NO MODEL ERROR.
         AIF  (('&MODEL' EQ '1') OR ('&MODEL' EQ '4') OR ('&MODEL' EQ 'X
               5') OR ('&MODEL' EQ '6')).OK                    @SA79798
.*           MODELS 1,4,5 OR 6 CAN HAVE COLUMN BINARY FEATURE. @SA79798
         AIF    ('&MODEL' EQ '2' OR '&MODEL' EQ '3').K2
    MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
&SGQUITB SETB  1
         AGO   .ON1                GO CHECK ADDRESS.
.E5 MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .ON1                GO CHECK ADDRESS.
.OK MNOTE *,'        MODEL &MODEL'
&SGUNM1B(&A)   SETB (&MODEL EQ 1)  GLOBAL MODEL SWITCH 1.
&SGUNM4B(&A)   SETB (&MODEL EQ 4)  GLOBAL MODEL SWITCH 4.
&SGUNM5B(&A)   SETB (&MODEL EQ 5)  GLOBAL MODEL SWITCH 5.
&SGUNM6B(&A)   SETB (&MODEL EQ 6)  GLOBAL MODEL SWITCH 6.      @SA79798
         AIF   (T'&FEATURE EQ 'O').ON1
         AIF   ('&FEATURE(1)' EQ 'COLBNRY').F8    ONLY VALID FEATURE.
         AGO   .E4       GO TO COMMON ERROR ROUTINE.
.F8 MNOTE *,'        COLUMN BINARY FEATURE'
&SGUNF1B(&A)   SETB 1                   FIRST FEATURE SWITCH.
         AGO   .ERRMORE
.K2 MNOTE *,'        MODEL &MODEL'
&SGUNM2B(&A)  SETB  (&MODEL EQ 2)
&SGUNM3B(&A)   SETB (&MODEL EQ 3)  GLOBAL MODEL SWITCH 3.
         AGO   .NOFEAT   COMMON ROUTINE FOR ANY FEATURES ARE INVALID.
.UD MNOTE *,'     2822 PAPER TAPE READER CONTROL'
&SGUNU5B(&A)   SETB 1              FIFTH GLOBAL UNIT SWITCH.
         AGO   .NOFEAT
.UE MNOTE *,'     2840 DISPLAY OR FILM CONTROL'
&SGUNU6B(&A)   SETB 1              SIXTH GLOBAL UNIT SWITCH.
 AIF (T'&MODEL EQ 'O').EC
 AIF (('&MODEL' NE '1') AND ('&MODEL' NE '2')).ED
&SGUNM1B(&A) SETB  ('&MODEL' EQ '1')
&SGUNM2B(&A) SETB ('&MODEL' EQ '2')
    MNOTE *,'      MODEL &MODEL'
.EXPBFR AIF (T'&EXPBFR NE 'O').R1
&SGCUN1A(&A) SETA 4096
   AGO .R2
.R1  ANOP
&SGCUN1A(&A)  SETA  &EXPBFR
.R2 MNOTE *,'      AMOUNT OF BUFFER SPACE IS &SGCUN1A(&A)'
 AIF (&SGUNM2B(&A)).NOFEAT
  AGO  .CHKF
.EC MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
&SGQUITB  SETB  1
 AGO .EXPBFR
.ED MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
&SGQUITB  SETB  1
 AGO .EXPBFR
.CHKF    AIF   (T'&FEATURE EQ 'O').ON1
.R3 AIF (&B EQ N'&FEATURE).ON1
&B  SETA  &B+1
 AIF ('&FEATURE(&B)' EQ 'BUFFER8K').F9
 AIF ('&FEATURE(&B)' EQ 'ABSLTVEC').R4
    MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID '
&SGQUITB  SETB  1
  AGO  .R3
.R4 MNOTE *,'      ABSOLUTE VECTOR CONTROL'
&SGUNF2B(&A) SETB  1
 AGO .R3
.F9 MNOTE *,'        8K BUFFER EXTENSION FEATURE'
&SGUNF1B(&A)   SETB 1              FIRST GLOBAL FEATURE SWITCH.
   AGO  .R3
.UF MNOTE *,'     2841 DIRECT ACCESS STORAGE CONTROL UNIT'
&SGUNU7B(&A)   SETB 1              SEVENTH GLOBAL UNIT SWITCH.
 AIF (T'&FEATURE EQ 'O').ON1   BRANCH IF NO FEATURES
.BACK4 AIF (&B EQ N'&FEATURE).FEATST   GO TEST SHARED IF &&B
&B       SETA  &B+1                     HAS REACHED THE NUMBER OF
         AIF   ('&FEATURE(&B)' EQ 'RECOFLO').FB   THROUGH THIS ROUTINE.
         AIF   ('&FEATURE(&B)' EQ '2-CHANSW').FC
 AIF ('&FEATURE(&B)' EQ 'ADSTORAG').FD
 AIF ('&FEATURE(&B)' EQ 'SHARED').FF
.FE  ANOP
    MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB SETB  1
         AGO   .BACK4
.FB MNOTE *,'        RECORD OVERFLOW FEATURE'
&SGUNF2B(&A)   SETB 1              SECOND GLOBAL FEATURE SWITCH.
&SGDCLSB(40)  SETB  1
         AGO   .BACK4              GO CHECK FOR MORE.
.FC AIF (&SGUNF4B(&A)).FE
    MNOTE *,'      TWO CHANNEL SWITCH FEATURE'
&SGUNF3B(&A)   SETB 1              THIRD FEATURE SWITCH.
         AGO   .BACK4              GO BACK FOR MORE.
.FD AIF (&SGUNF3B(&A)).FE
    MNOTE *,'      ADDITIONAL STORAGE FEATURE'
&SGUNF4B(&A)  SETB  1
  AGO .BACK4
.FF MNOTE *,'        SHARED DASD'
 MNOTE 0,'* * * IEIIOCW01 HARDWARE CHANNEL SWITCHING CAPABILITY MUST BE*
               AVAILABLE WITH SHARED'
&SGUNF5B(&A) SETB 1        FIFTH GLOBAL FEATURE SWITCH
&SGDCLSB(54) SETB 1      SHARED DASD CLASS SWITCH
 AGO .BACK4
.FEATST AIF (&SGUNF5B(&A) AND NOT &SGUNF3B(&A)).INCFEAT
 AGO .ON1
.UG MNOTE *,'     2848 DISPLAY CONTROL UNIT'
&SGUNU8B(&A)   SETB 1
 AIF (T'&MODEL EQ 'O').EB
 AIF (('&MODEL' EQ '1') OR ('&MODEL' EQ '2') OR ('&MODEL' EQ '3')).G1
 AIF (('&MODEL' EQ '21') OR ('&MODEL' EQ '22')).G1
    MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
&SGQUITB  SETB  1
   AGO  .R5
.G1 MNOTE *,'      MODEL &MODEL'
&SGUNM1B(&A)  SETB  ('&MODEL' EQ '1')
&SGUNM2B(&A)  SETB  ('&MODEL' EQ '2')
&SGUNM3B(&A)  SETB  ('&MODEL' EQ '3')
&SGUNM4B(&A) SETB ('&MODEL' EQ '21')
&SGUNM5B(&A) SETB ('&MODEL' EQ '22')
   AGO  .R5
.EB MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
&SGQUITB  SETB  1
.R5  AIF (&B EQ N'&FEATURE).ON1
&B  SETA  &B+1
  AIF  ('&FEATURE(&B)' EQ 'LINEADDR').R6
   AIF ('&FEATURE(&B)' EQ 'NODESCUR').R7
    MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB  SETB  1
  AGO  .R5
.R6 MNOTE *,'      LINE ADDRESSING FEATURE'
&SGUNF1B(&A)  SETB  1
  AGO  .R5
.R7 MNOTE *,'      NON-DESTRUCTIVE CURSOR'
&SGUNF2B(&A)  SETB  1
  AGO  .R5
.E4  AIF  (&B GT 0).E4X
&B   SETA 1
.E4X MNOTE 5,'* * * IEII0C105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB SETB  1
         AGO   .ON1
.UH MNOTE *,'     2703 TRANSMISSION CONTROL UNIT'
&SGUNU1B(&A) SETB 1
  AGO  .NOFEAT
.UJ MNOTE *,'     3811 CONTROL UNIT'
&SGUNU2B(&A) SETB 1
  AGO  .NOFEAT
.ERRMORE ANOP
&B       SETA  1
.NOFEAT  AIF   (&B EQ N'&FEATURE).ON1
&B       SETA  &B+1
    MNOTE 5,'* * * IEIIOC105 &ID-FEATURE VALUE &FEATURE(&B) INVALID'
&SGQUITB SETB  1
         AGO   .NOFEAT
.E1 MNOTE 5,'* * * IEIIOC101 &ID-UNIT VALUE NOT SPECIFIED'
&SGQUITB SETB   1
.ON1     AIF   (T'&ADDRESS EQ 'O').CHECKT ADDRESS NOT SPECIFIED
&CHANNEL SETC  '&ADDRESS'(1,1)     CHANNEL POSITION OF ADDRESS.
&SGCNTRU SETC '&ADDRESS'(2,1)  CONTROL UNIT POSITION OF ADDRESS
 AIF (NOT(('&CHANNEL' GE '0' AND '&CHANNEL' LE '9')OR('&CHANNEL' GE 'A'*
                AND '&CHANNEL' LE 'F'))).CBER
 AIF (('&SGCNTRU' GE '0' AND '&SGCNTRU' LE '9')OR('&SGCNTRU' GE 'A' AND*
                '&SGCNTRU' LE 'F')).K4
.CBER MNOTE 5,'* * * IEIIOC107 &ID-ADDRESS VALUE &ADDRESS INVALID'
&SGQUITB SETB  1
         AGO   .CHECKT
.K4 MNOTE *,'        ADDRESS=&CHANNEL&SGCNTRU'
.CHECKT AGO .MEND
.UK MNOTE *,'     3272 CONTROL UNIT'
&SGUNU5B(&A) SETB 1
         AIF (T'&MODEL EQ 'O').UKO      TEST FOR MODEL NUMBER
         AIF ('&MODEL' EQ '1').UKM      TEST FOR MODEL 1
         AIF ('&MODEL' EQ '2').UKM      TEST FOR MODEL 2
    MNOTE 5,'* * * IEIIOC103 &ID-MODEL VALUE &MODEL INVALID'
.UKQ     ANOP
&SGQUITB SETB  1
         AGO   .NOFEAT                  GO TEST FOR FEATURES
.UKO MNOTE 5,'* * * IEIIOC104 &ID-MODEL VALUE NOT SPECIFIED'
         AGO   .UKQ
.UKM MNOTE *,'        MODEL &MODEL'
&SGUNM1B(&A) SETB ('&MODEL' EQ '1')     SET MODEL 1 SW
&SGUNM2B(&A) SETB ('&MODEL' EQ '2')     SET MODEL 2 SW
         AGO   .NOFEAT
.EA MNOTE 5,'* * * IEIIOC112 &ID-MAXIMUM NUMBER OF UNITS EXCEEDED'
&SGQUITB SETB  1
.MEND  MEND
