               MACRO
         COBOL &DESIGN=,&MSGLEV=FLAGW,&LINECNT=60,&DATAMAP=,&PROCMAP=, X
               &DISPCHK=DISPCK,&BUFSIZE=180,&EDIT=REGED,               X
               &SIZE=81920,&BUF=,&SORLIST=SOURCE,&PUNCH=NODECK,        *
               &TYPERUN=LOAD,&SEQCHK=SEQ,&SPACE=SPACE1,                *
               &LITCHAR=APOST,&REFLIST=NOXREF,&CNDENSE=NOCLIST,        *
               &SUPPRES=NOSUPMAP,&TRACE=NOPDT,&TRNCATE=NOTRUNC,        *
               &VERB=VERB,&LIB=LIB                              SA61325
         COPY  SGGBLPAK
         LCLC  &C
         AIF   ('&DESIGN' EQ '').Q1     CHECK VALIDITY OF DESIGN VALUE
         AIF ('&DESIGN' EQ 'F').Q4
         AIF ('&DESIGN' NE 'E' AND '&DESIGN' NE 'U').Q2
         AIF (('&DESIGN' EQ 'E' AND &SGCBLEB(6)) OR ('&DESIGN' EQ 'U' A*
               ND &SGCBLUB(2))).Q3
.*  ERROR IF EITHER LEVEL WAS PREVIOUSLY CALLED
    MNOTE *,'      COBOL DESIGN LEVEL &DESIGN SPECIFIED'
&SGCBLEB(6) SETB (('&DESIGN' EQ 'E') OR &SGCBLEB(6))
&SGCBLUB(2) SETB (('&DESIGN' EQ 'U') OR &SGCBLUB(2))
&SGMENTB(24) SETB  1                    SET MACRO ENTRY SWITCH
         AIF   ('&MSGLEV' NE 'FLAGW').A1
    MNOTE *,'      ALL WARNING AND ERROR MESSAGES PRINTED'
&SGCBLUB(1) SETB ('&DESIGN' EQ 'U' OR &SGCBLUB(1)) SET U LEVEL MSG SW
         AGO   .A2
.A1      AIF   ('&MSGLEV' NE 'FLAGE').S4
&SGCBLEB(5) SETB ('&DESIGN' EQ 'E' OR &SGCBLEB(5))
    MNOTE *,'      ONLY ERROR MESSAGES PRINTED'
.A2      AIF   (T'&LINECNT NE 'N').S5
         AIF   (K'&LINECNT GT 2 OR &LINECNT LT 10).S5
    MNOTE *,'      MAXIMUM &LINECNT LINES PER PAGE OF COMPILER OUTPUT'
         AIF ('&DESIGN' EQ 'U').I3
&SGCBLEA(1)  SETA  &LINECNT             SET E LEVEL LINE COUNT
.*  COBOL E-LEVEL DEFAULT OPTION CHECKING
.A4      AIF (T'&DATAMAP EQ 'O' OR '&DATAMAP' EQ 'DMAP').A45
         AIF ('&DATAMAP' NE 'NODMAP').S6
&SGCBLEB(2) SETB 1
&C       SETC ' NOT '
         AGO  .A5
.A45     ANOP
&C       SETC ''
.A5 MNOTE *,'      RELATIVE LOCATION MAP WILL&C.BE PRINTED'
.A7      AIF (T'&PROCMAP EQ 'O' OR '&PROCMAP' EQ 'PMAP').A75
         AIF ('&PROCMAP' NE 'NOPMAP').S7
&SGCBLEB(3) SETB 1
&C       SETC ' NOT '
         AGO .A78
.A75     ANOP
&C       SETC ''
.A78 MNOTE *,'      PROCEDURE MAP WILL&C.BE PRODUCED'
.A8      AIF   ('&DISPCHK' NE 'DISPCK' AND '&DISPCHK' NE 'NODISPCK').S8
&C       SETC  ' NOT '(1,((K'&DISPCHK-6)*2+1))
    MNOTE *,'      LENGTH OF DISPLAYED FIELDS WILL&C.BE CHECKED'
&SGCBLEB(4) SETB ('&DISPCHK' EQ 'NODISPCK')
.A9      AIF   (T'&BUFSIZE NE 'N').S9
         AIF   (K'&BUFSIZE GT 5 OR &BUFSIZE LT 180).S9
    MNOTE *,'      &BUFSIZE BYTES OF STORAGE AVAILABLE PER WORK BUFFER'
&SGCBLEA(2) SETA   &BUFSIZE             SET BUFFER SIZE VALUE
.B1      AIF  ('&EDIT' NE 'REGED' AND '&EDIT' NE 'INVED').T0
    MNOTE *,'      EDITING FUNCTION OPTION IS &EDIT '
&SGCBLEB(1) SETB  ('&EDIT' EQ 'INVED')
         MEXIT      END OF E LEVEL DEFAULT OPTION CHECKING
.* COBOL U LEVEL DEFAULT OPTION CHECKING
.I3      ANOP
&SGCBLUA(1) SETA &LINECNT
.I4      AIF (T'&SIZE NE 'N').T1
         AIF (K'&SIZE GT 7 OR '&SIZE' LT '81920').T1
  MNOTE *,'      &SIZE BYTES OF STORAGE AVAILABLE FOR COMPILER'
&SGCBLUA(2) SETA &SIZE
         AIF ('&BUF' NE '').I5
&SGCBLUA(3) SETA (&SIZE-81920)/4+2768
         AGO .I55
.I5      AIF (T'&BUF NE 'N').T2
         AIF (K'&BUF GT 5 OR '&BUF' LT '2768').T2
&SGCBLUA(3) SETA &BUF
.I55 MNOTE *,'      &SGCBLUA(3) BYTES OF STORAGE AVAILABLE FOR BUFFERS'
.I6      AIF ('&SORLIST' NE 'SOURCE' AND '&SORLIST' NE 'NOSOURCE').T3
&C       SETC ' NOT '(1,((K'&SORLIST-6)*2+1))
  MNOTE *,'      SOURCE LISTING WILL&C.BE PRODUCED'
&SGCBLUB(3) SETB ('&SORLIST' EQ 'SOURCE')
.I7      AIF ('&PUNCH' NE 'DECK' AND '&PUNCH' NE 'NODECK').T5
&C       SETC ' NOT '(1,((K'&PUNCH-4)*2+1))
  MNOTE *,'      OBJECT DECK OF PROGRAM WILL&C.BE PUNCHED'
&SGCBLUB(5) SETB ('&PUNCH' EQ 'DECK')
.I8      AIF ('&TYPERUN' NE 'LOAD' AND '&TYPERUN' NE 'NOLOAD').T6
&C       SETC ' NOT '(1,((K'&TYPERUN-4)*2+1))
  MNOTE *,'      COMPILED PROGRAM WILL&C.BE LINKAGE EDITED'
&SGCBLUB(6) SETB ('&TYPERUN' EQ 'LOAD')
.I9      AIF ('&SEQCHK' NE 'SEQ' AND '&SEQCHK' NE 'NOSEQ').T7
&C       SETC ' NOT '(1,((K'&SEQCHK-3)*2+1))
  MNOTE *,'      CARD SEQUENCE OF PROGRAM WILL&C.BE CHECKED'
&SGCBLUB(7) SETB ('&SEQCHK' EQ 'SEQ')
.J0 AIF ('&SPACE' NE 'SPACE1' AND '&SPACE' NE 'SPACE2' AND '&SPACE' NE C
               'SPACE3').U0
&C       SETC '&SPACE'(6,1)
  MNOTE *,'      LISTING WILL HAVE &C SPACES BETWEEN LINES'
&SGCBLUB(10) SETB ('&SPACE' EQ 'SPACE2')
&SGCBLUB(11) SETB ('&SPACE' EQ 'SPACE3')
.J1      AIF ('&LITCHAR' NE 'QUOTE' AND '&LITCHAR' NE 'APOST').U01
         AIF ('&LITCHAR' EQ 'APOST').J15
 MNOTE *,'      QUOTES WILL BE USED TO DELINEATE LITERALS'
         AGO .J2
.J15  MNOTE *,'      APOSTROPHES WILL BE USED TO DELINEATE LITERALS'
&SGCBLUB(4) SETB 1
.J2      AIF ('&REFLIST' NE 'NOXREF' AND '&REFLIST' NE 'XREF').U02
&C       SETC ' NOT '(1,((K'&REFLIST-4)*2+1))
  MNOTE *,'      CROSS REFERENCE LISTING WILL&C.BE PRODUCED'
&SGCBLUB(8) SETB ('&REFLIST' EQ 'XREF')
.J3      AIF ('&CNDENSE' NE 'CLIST' AND '&CNDENSE' NE 'NOCLIST').U03
&C       SETC ' NOT '(1,((K'&CNDENSE-5)*2+1))
   MNOTE *,'      CONDENSED LISTING WILL&C.BE PRODUCED'
&SGCBLUB(9) SETB ('&CNDENSE' EQ 'CLIST')
.J4      AIF ('&SUPPRES' NE 'NOSUPMAP' AND '&SUPPRES' NE 'SUPMAP').U04
&C       SETC ' NOT '(1,((K'&SUPPRES-6)*2+1))
  MNOTE *,'      OBJECT LISTING WILL&C.BE SUPPRESSED IF E-LEVEL MSG IS X
               GENERATED'
&SGCBLUB(12) SETB ('&SUPPRES' EQ 'SUPMAP')
.J5      ANOP
.J6      AIF ('&TRACE' NE 'PDT' AND '&TRACE' NE 'NOPDT').U055
&C       SETC ' NOT '(1,((K'&TRACE-3)*2+1))
.J65     AIF ('&TRNCATE' NE 'TRUNC' AND '&TRNCATE' NE 'NOTRUNC').U057
         AIF ('&TRNCATE' EQ 'NOTRUNC').J66
 MNOTE *,'      BINARY ITEMS MOVED ACCORDING TO PICTURE CLAUSE'
         AGO .J67
.J66 MNOTE *,'      BINARY ITEMS MOVED ACCORDING TO SIZE OF FIELD IN '
 MNOTE *,'       STORAGE'
.J67     ANOP
&SGCBLUB(17) SETB ('&TRNCATE' EQ 'TRUNC')
.J68     ANOP
.J7      AIF (T'&DATAMAP EQ 'O' OR '&DATAMAP' EQ 'NODMAP').J75
         AIF ('&DATAMAP' NE 'DMAP').S6
&SGCBLUB(15) SETB 1
&C       SETC ''
         AGO .J78
.J75     ANOP
&C       SETC ' NOT '
.J78 MNOTE *,'      RELATIVE LOCATION MAP WILL&C.BE PRINTED'
.J8      AIF (T'&PROCMAP EQ 'O' OR '&PROCMAP' EQ 'NOPMAP').J85
         AIF ('&PROCMAP' NE 'PMAP').S7
&SGCBLUB(16) SETB 1
         AIF (&SGCBLUB(9)).U058
&C       SETC ''
         AGO .J88
.J85     ANOP
&C       SETC ' NOT '
.J88 MNOTE *,'      PROCEDURE MAP WILL&C.BE PRODUCED'
.J9      AIF   (T'&VERB EQ 'O' OR '&VERB' EQ 'VERB').J93        SA61325
         AIF   ('&VERB' NE 'NOVERB').U059                       SA61325
&C       SETC  ' NOT '                                          SA61325
         AGO   .J95                                             SA61325
.J93     ANOP                                                   SA61325
&C       SETC  ''                                               SA61325
&SGCBLUB(14) SETB 1                                             SA61325
.J95 MNOTE *,'      VERB OPTION WILL&C.BE INCLUDED'             SA61325
.J96     AIF   (T'&LIB EQ 'O' OR '&LIB' EQ 'LIB').J97           SA61325
         AIF   ('&LIB' NE 'NOLIB').U060                         SA61325
&C       SETC  ' NOT '                                          SA61325
         AGO   .J99                                             SA61325
.J97     ANOP                                                   SA61325
&C       SETC  ''                                               SA61325
&SGCBLUB(18) SETB 1                                             SA61325
.J99 MNOTE *,'      LIB OPTION WILL&C.BE INCLUDED'
         MEXIT
.Q1 MNOTE 5,'* * * IEICOB101 DESIGN LEVEL NOT SPECIFIED'
         AGO   .U1
.Q2 MNOTE 5,'* * * IEICOB102 DESIGN LEVEL &DESIGN INVALID'
         AGO   .U1
.Q3 MNOTE 5,'* * * IEICOB103 DESIGN LEVEL &DESIGN PREVIOUSLY SPECIFIED'
         AGO   .U1
.Q4 MNOTE 5,'* * * IEICOB130 DESIGN VALUE &DESIGN NOT SUPPORTED IN THIS*
                RELEASE'
         AGO   .U1
.S4 MNOTE 5,'* * * IEICOB104 MSGLEV VALUE &MSGLEV INVALID'
&SGQUITB SETB  1
         AGO   .A2
.S5 MNOTE 5,'* * * IEICOB105 LINECT VALUE &LINECNT INVALID'
&SGQUITB SETB  1
         AIF   ('&DESIGN' EQ 'E').A4
         AGO   .I4
.S6 MNOTE 5,'* * * IEICOB106 DATAMAP VALUE &DATAMAP INVALID'
&SGQUITB SETB  1
         AIF ('&DESIGN' EQ 'U').J8
         AGO   .A7
.S7 MNOTE 5,'* * * IEICOB107 PROCMAP VALUE &PROCMAP INVALID'
&SGQUITB SETB  1
         AIF  ('&DESIGN' EQ 'U').MEND
         AGO   .A8
.S8 MNOTE 5,'* * * IEICOB108 DISPCHK VALUE &DISPCHK INVALID'
&SGQUITB SETB  1
         AGO   .A9
.S9 MNOTE 5,'* * * IEICOB109 BUFSIZE VALUE &BUFSIZE INVALID'
&SGQUITB SETB  1
         AGO   .B1
.T0 MNOTE 5,'* * * IEICOB110 EDIT VALUE &EDIT INVALID'
         AGO   .U1
.T1 MNOTE 5,'* * * IEICOB111 SIZE VALUE &SIZE INVALID'
&SGQUITB SETB  1
         AGO   .I5
.T2 MNOTE 5,'* * * IEICOB112 BUF VALUE &BUF INVALID'
&SGQUITB SETB  1
         AGO   .I6
.T3 MNOTE 5,'* * * IEICOB113 SORLIST VALUE &SORLIST INVALID'
&SGQUITB SETB  1
         AGO   .I7
.T5 MNOTE 5,'* * * IEICOB115 PUNCH VALUE &PUNCH INVALID'
&SGQUITB SETB  1
         AGO   .I8
.T6 MNOTE 5,'* * * IEICOB116 TYPERUN VALUE &TYPERUN INVALID'
&SGQUITB SETB  1
         AGO   .I9
.T7 MNOTE 5,'* * * IEICOB117 SEQCHK VALUE &SEQCHK INVALID'
&SGQUITB SETB  1
         AGO   .J0
.U0 MNOTE 5,'* * * IEICOB120 SPACE VALUE &SPACE INVALID'
&SGQUITB SETB 1
         AIF ('&DESIGN' EQ 'U').J1
         MEXIT
.U01 MNOTE 5,'* * * IEICOB121 LITCHAR VALUE &LITCHAR INVALID'
&SGQUITB SETB 1
         AGO .J2
.U02  MNOTE 5,'* * * IEICOB122 REFLIST VALUE &REFLIST INVALID'
&SGQUITB SETB 1
         AGO .J3
.U03  MNOTE 5,'* * * IEICOB123 CNDENSE VALUE &CNDENSE INVALID'
&SGQUITB SETB 1
         AGO .J4
.U04  MNOTE 5,'* * * IEICOB124 SUPPRES VALUE &SUPPRES INVALID'
&SGQUITB SETB 1
         AGO .J5
.U055 MNOTE 5,'* * * IEICOB126 TRACE VALUE &TRACE INVALID'
&SGQUITB SETB 1
         AGO .J65
.U057 MNOTE 5,'* * * IEICOB127 TRNCATE VALUE &TRNCATE INVALID'
&SGQUITB SETB 1
         AGO .J68
.U058 MNOTE 5,'* * * IEICOB128 PMAP AND CLIST MUTUALLY EXCUSIVE'
&SGQUITB SETB 1
         AGO   .J9
.U059 MNOTE 5,'* * * IEICOB129 VERB VALUE &VERB INVALID'        SA61325
&SGQUITB SETB 1                                                 SA61325
         AGO   .J96                                             SA61325
.U060 MNOTE 5,'* * * IEICOB130 LIB VALUE &LIB INVALID'          SA61325
&SGQUITB SETB 1                                                 SA61325
         MEXIT
.U1      ANOP
&SGQUITB SETB 1
.MEND    MEND
