         MACRO
         SECONSLE &CONSOLE=,&ALTCONS=,&ROUTCDE=,&VALDCMD=,&USE=,       *
               &AREA=,&PFK=,&OPSTRAN=,&OPSTRNI=
         COPY  SGGBLPAK
         LCLA  &A1,&A2,&A3,&A4
    LCLB  &B1,&B2,&B3,&B4,&B5,&SW1,&SW2,&SW3
         LCLC  &C1,&C2,&C3,&C4,&C5
&SGSCNYA(2) SETA &SGSCNYA(2)+1              NO. OF MACRO CALLS
         AIF   (&SGSCNYA(2) GT 31).M0       MAX 31 SECONDARY CONSOLES
         AIF   (T'&CONSOLE EQ 'O').M1       CONSOLE VALUE OMITTED
         AIF   (N'&CONSOLE NE 1).A6
&A1      SETA  &SGSCNYA(1)
.A2      AIF   (&A1 EQ 0).AZ
         AIF   ('&CONSOLE&CONSOLE' EQ '&SGSCNCC(&A1)').M3
&A1      SETA  &A1-1
         AGO   .A2
.AZ      ANOP
&A4      SETA  1
&SW1     SETB  0
&SW2     SETB  0
&C5      SETC  '&CONSOLE(&A4)'(1,2)
         AIF   ('&C5' NE 'O-').A4
&C4      SETC  '&CONSOLE(&A4)'(3,3)
&C5      SETC   'FFF'
&SW1     SETB  1
         MNOTE *,'      OUTPUT ONLY CONSOLE ADDRESS &C4'
&SGSCNYA(1) SETA  &SGSCNYA(1)+1
&SGSCNCC(&SGSCNYA(1))   SETC   '&C5&C4'
         AGO   .B4
.A4      MNOTE *,'      CONSOLE ADDRESS &CONSOLE'
.AX      ANOP
&SGSCNYA(1) SETA &SGSCNYA(1)+1
&SGSCNCC(&SGSCNYA(1)) SETC '&CONSOLE&CONSOLE'
         AGO   .B4
.A6      AIF   (N'&CONSOLE NE 2).M2
         MNOTE *,'      COMPOSITE CONSOLE WITH FOLLOWING ADDRESSES--'
&SGSCNYA(1) SETA &SGSCNYA(1)+1
.A8      AIF   (&A2 EQ 2).B4
&A2      SETA  &A2+1
&C1      SETC  '&CONSOLE(&A2)'(3,3)
&C2      SETC  '&CONSOLE(&A2)'(1,2)
         AIF   ('&C2' NE 'I-').AE
         AIF   (&B1).M20
&B1      SETB  1
&A1      SETA  &SGSCNYA(1)-1
&C3      SETC  '&C1&C3'
.AA      AIF   (&A1 EQ 0).AC
         AIF   ('&C3' EQ '&SGSCNCC(&A1)').M30
&A1      SETA  &A1-1
         AGO   .AA
.AC      MNOTE *,'         &C1 - INPUT'
&SGSCNCC(&SGSCNYA(1)) SETC '&C1&SGSCNCC(&SGSCNYA(1))'  I-ADDR(O-ADDR)
         AGO   .A8
.AE      AIF   ('&C2' NE 'O-').M20
         AIF   (&B2).M20
&B2      SETB  1
&A1      SETA  &SGSCNYA(1)-1
&C3      SETC  '&C3&C1'
.B0      AIF   (&A1 EQ 0).B2
         AIF   ('&C3' EQ '&SGSCNCC(&A1)').M30
&A1      SETA  &A1-1
         AGO   .B0
.B2      MNOTE *,'         &C1 - OUTPUT'
&SGSCNCC(&SGSCNYA(1)) SETC '&SGSCNCC(&SGSCNYA(1))&C1'  (I-ADDR)O-ADDR
         AGO   .A8
.B4      AIF   (T'&ALTCONS EQ 'O').BC       OMITTED
&A4     SETA  1
         AIF   (N'&ALTCONS NE 1).B6
&C5      SETC  '&ALTCONS(&A4)'(1,2)
         AIF   ('&C5' NE 'O-').B4A
&C4      SETC  '&ALTCONS(&A4)'(3,3)
&SW2     SETB  1
&C5      SETC   'FFF'
         MNOTE *,'      OUTPUT ONLY ALTCONS ADDRESS &C4'
&SGSCNAC(&SGSCNYA(1))   SETC   '&C5&C4'
    AIF  (NOT &SW1).M9 OUTPUT ONLY ALTCONS VALID WITH O-ONLY CONSOLE
         AGO   .BC
.B4A     ANOP
         MNOTE *,'      ALTCONS ADDRESS &ALTCONS'
.B4B     ANOP
&SGSCNAC(&SGSCNYA(1)) SETC '&ALTCONS&ALTCONS'
         AGO   .BC
.B6      AIF   (N'&ALTCONS NE 2).M4
         MNOTE *,'      COMPOSITE ALTERNATE CONSOLE WITH FOLLOWING'
         MNOTE *,'       ADDRESSES--'
.B8      AIF   (&A3 EQ 2).BC
&A3      SETA  &A3+1
&C1      SETC  '&ALTCONS(&A3)'(3,3)
&C2      SETC  '&ALTCONS(&A3)'(1,2)
         AIF   ('&C2' NE 'I-').BA
         AIF   (&B3).M40
&B3      SETB  1
         MNOTE *,'         &C1 - INPUT'
&SGSCNAC(&SGSCNYA(1)) SETC '&C1&SGSCNAC(&SGSCNYA(1))'  I-ADDR(O-ADDR)
         AGO   .B8
.BA      AIF   ('&C2' NE 'O-').M40
         AIF   (&B4).M40
&B4      SETB  1
         MNOTE *,'         &C1 - OUTPUT'
&SGSCNAC(&SGSCNYA(1)) SETC '&SGSCNAC(&SGSCNYA(1))&C1'  (I-ADDR)O-ADDR
         AGO   .B8
.BC      AIF   (T'&ROUTCDE EQ 'O').C7          OMITTED
         AIF   ('&ROUTCDE' EQ 'ALL').C4
&A1      SETA  1
         MNOTE *,'      ROUTING CODES ASSIGNED TO THIS CONSOLE ARE--'
.BE      AIF   (N'&ROUTCDE LT &A1).C8       EXHAUSTED
         AIF   (T'&ROUTCDE(&A1) NE 'N').M5  NOT NUMERIC
         AIF   ('&ROUTCDE(&A1)' LT '1' OR '&ROUTCDE(&A1)' GT '16').M5
  AIF  (&SGRT01B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '1').M55 DUPLIC'D
  AIF  (&SGRT02B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '2').M55 DUPLIC'D
  AIF  (&SGRT03B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '3').M55 DUPLIC'D
  AIF  (&SGRT04B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '4').M55 DUPLIC'D
  AIF  (&SGRT05B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '5').M55 DUPLIC'D
  AIF  (&SGRT06B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '6').M55 DUPLIC'D
  AIF  (&SGRT07B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '7').M55 DUPLIC'D
  AIF  (&SGRT08B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '8').M55 DUPLIC'D
  AIF  (&SGRT09B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ  '9').M55 DUPLIC'D
  AIF  (&SGRT10B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '10').M55 DUPLIC'D
  AIF  (&SGRT11B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '11').M55 DUPLIC'D
  AIF  (&SGRT12B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '12').M55 DUPLIC'D
  AIF  (&SGRT13B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '13').M55 DUPLIC'D
  AIF  (&SGRT14B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '14').M55 DUPLIC'D
  AIF  (&SGRT15B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '15').M55 DUPLIC'D
  AIF  (&SGRT16B(&SGSCNYA(1)) AND '&ROUTCDE(&A1)' EQ '16').M55 DUPLIC'D
         MNOTE *,'         &ROUTCDE(&A1)'
&SGRT01B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '1' OR &SGRT01B(&SGSCNY*
               A(1)))
&SGRT02B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '2' OR &SGRT02B(&SGSCNY*
               A(1)))
&SGRT03B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '3' OR &SGRT03B(&SGSCNY*
               A(1)))
&SGRT04B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '4' OR &SGRT04B(&SGSCNY*
               A(1)))
&SGRT05B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '5' OR &SGRT05B(&SGSCNY*
               A(1)))
&SGRT06B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '6' OR &SGRT06B(&SGSCNY*
               A(1)))
&SGRT07B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '7' OR &SGRT07B(&SGSCNY*
               A(1)))
&SGRT08B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '8' OR &SGRT08B(&SGSCNY*
               A(1)))
&SGRT09B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ  '9' OR &SGRT09B(&SGSCNY*
               A(1)))
&SGRT10B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '10' OR &SGRT10B(&SGSCNY*
               A(1)))
&SGRT11B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '11' OR &SGRT11B(&SGSCNY*
               A(1)))
&SGRT12B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '12' OR &SGRT12B(&SGSCNY*
               A(1)))
&SGRT13B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '13' OR &SGRT13B(&SGSCNY*
               A(1)))
&SGRT14B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '14' OR &SGRT14B(&SGSCNY*
               A(1)))
&SGRT15B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '15' OR &SGRT15B(&SGSCNY*
               A(1)))
&SGRT16B(&SGSCNYA(1)) SETB ('&ROUTCDE(&A1)' EQ '16' OR &SGRT16B(&SGSCNY*
               A(1)))
.C0      ANOP
&A1      SETA  &A1+1
         AGO   .BE
.C4      MNOTE *,'      ALL ROUTING CODES ARE ASSIGNED TO THIS CONSOLE'
&SGRT01B(&SGSCNYA(1)) SETB 1
&SGRT02B(&SGSCNYA(1)) SETB 1
&SGRT03B(&SGSCNYA(1)) SETB 1
&SGRT04B(&SGSCNYA(1)) SETB 1
&SGRT05B(&SGSCNYA(1)) SETB 1
&SGRT06B(&SGSCNYA(1)) SETB 1
&SGRT07B(&SGSCNYA(1)) SETB 1
&SGRT08B(&SGSCNYA(1)) SETB 1
&SGRT09B(&SGSCNYA(1)) SETB 1
&SGRT10B(&SGSCNYA(1)) SETB 1
&SGRT12B(&SGSCNYA(1)) SETB 1
&SGRT11B(&SGSCNYA(1)) SETB 1
&SGRT13B(&SGSCNYA(1)) SETB 1
&SGRT14B(&SGSCNYA(1)) SETB 1
&SGRT15B(&SGSCNYA(1)) SETB 1
&SGRT16B(&SGSCNYA(1)) SETB 1
         AGO   .C8
.C7      MNOTE *,'      NO ROUTING CODES ARE ASSIGNED THIS CONSOLE'
.C8      AIF   (T'&VALDCMD EQ 'O').CE       OMITTED
 AIF (&SW1).M8               OUTPUT ONLY CONSOLE
.C9      ANOP
         MNOTE *,'      COMMAND CODES ASSIGNED TO THIS CONSOLE ARE--'
&A1      SETA  1
.CA      AIF  (N'&VALDCMD LT &A1).D1         EXHAUSTED
         AIF   (T'&VALDCMD(&A1) NE 'N').M6  NOT NUMERIC
         AIF   ('&VALDCMD(&A1)' LT '1' OR '&VALDCMD(&A1)' GT '3').M6
  AIF  (&SGCMD1B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '1').M7 DUPLIC'D
  AIF  (&SGCMD2B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '2').M7 DUPLIC'D
  AIF  (&SGCMD3B(&SGSCNYA(1)) AND '&VALDCMD(&A1)' EQ '3').M7 DUPLIC'D
         MNOTE *,'         &VALDCMD(&A1)'
&SGCMD1B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '1' OR &SGCMD1B(&SGSCNYA*
               (1)))
&SGCMD2B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '2' OR &SGCMD2B(&SGSCNYA*
               (1)))
&SGCMD3B(&SGSCNYA(1)) SETB ('&VALDCMD(&A1)' EQ '3' OR &SGCMD3B(&SGSCNYA*
               (1)))
.CC      ANOP
&A1      SETA  &A1+1
         AGO   .CA
.CE MNOTE *,'      COMMANDS ''DISPLAY,LOG'' ARE ASSIGNED THIS CONSOLE'
.D1      AIF   (T'&USE EQ 'O' OR '&USE' EQ 'FC').E1
         AIF (NOT('&USE' EQ 'MS')).D2        GO CHECK FOR SD
         MNOTE *,'      LIMITED CAPACITY CONSOLE-MESSAGE STREAM MODE'
&SGSCMSB(&SGSCNYA(1)) SETB 1
         AGO  .E1
.D2      AIF (NOT('&USE' EQ 'SD')).MA        ANY OTHER INVALID
         MNOTE *,'      LIMITED CAPACITY CONSOLE-STATUS DISPLAY MODE'
&SGSCSDB(&SGSCNYA(1)) SETB 1
.E1      AIF   (T'&AREA EQ 'O').G            OMITTED
         AIF   (&SGSCMSB(&SGSCNYA(1))).MB    ERROR IF MS CONSOLE
         AIF   (N'&AREA GT 11).MC            TOO MANY AREAS DEFINED
         MNOTE *,'      STATUS DISPLAY AREAS FOR THIS CONSOLE HAVE'
         MNOTE *,'       LENGTHS OF--'
&A1      SETA  1
.E2      AIF   (&A1 GT N'&AREA).E4           PARAMETERS EXHAUSTED
         AIF   (T'&AREA(&A1) NE 'N').MD      NOT NUMERIC
         AIF   ('&AREA(&A1)' LT '4' OR '&AREA(&A1)' GT '47').MD
         MNOTE *,'           &AREA(&A1)'
&SGAR12A(&SGSCNYA(1)) SETA &SGAR12A(&SGSCNYA(1))+&AREA(&A1)
.E3      ANOP
&A1      SETA  &A1+1
         AGO   .E2
.E4      AIF (&SW3).G                   ERROR IN PARAMETER
         AIF   (N'&AREA EQ 1).F1
         AIF   (N'&AREA EQ 2).F2
         AIF   (N'&AREA EQ 3).F3
         AIF   (N'&AREA EQ 4).F4
         AIF   (N'&AREA EQ 5).F5
         AIF   (N'&AREA EQ 6).F6
         AIF   (N'&AREA EQ 7).F7
         AIF   (N'&AREA EQ 8).F8
         AIF   (N'&AREA EQ 9).F9
         AIF   (N'&AREA EQ 10).F10
&SGAR11A(&SGSCNYA(1)) SETA &AREA(11)
.F10     ANOP
&SGAR10A(&SGSCNYA(1)) SETA &AREA(10)
.F9      ANOP
&SGAR09A(&SGSCNYA(1)) SETA &AREA(9)
.F8      ANOP
&SGAR08A(&SGSCNYA(1)) SETA &AREA(8)
.F7      ANOP
&SGAR07A(&SGSCNYA(1)) SETA &AREA(7)
.F6      ANOP
&SGAR06A(&SGSCNYA(1)) SETA &AREA(6)
.F5      ANOP
&SGAR05A(&SGSCNYA(1)) SETA &AREA(5)
.F4      ANOP
&SGAR04A(&SGSCNYA(1)) SETA &AREA(4)
.F3      ANOP
&SGAR03A(&SGSCNYA(1)) SETA &AREA(3)
.F2      ANOP
&SGAR02A(&SGSCNYA(1)) SETA &AREA(2)
.F1      ANOP
&SGAR01A(&SGSCNYA(1)) SETA &AREA(1)
.G       AIF (T'&PFK EQ 'O').H               OMITTED
         AIF (T'&PFK NE 'N').ME              NOT NUMERIC
         AIF ('&PFK' GT '12').ME             12 MAXIMUM
&SGSCNPA(&SGSCNYA(1)) SETA &PFK
&SGSCHDB(30) SETB 1
         MNOTE *,'      CONSOLE WILL HAVE &PFK PROGRAM FUNCTION KEYS'
.H       AIF   (T'&OPSTRAN EQ 'O').I         OMITTED
         AIF   (T'&OPSTRAN NE 'N').MF        NOT NUMERIC
         AIF   ('&OPSTRAN' GT '64').MF       INVALID
         AIF   (N'&CONSOLE GT 1).H1          COMPOSITE CONSOLE
         AIF   (&SW1).H1                     OUTPUT ONLY CONSOLE
    MNOTE *,'             CONSOLE ASSIGNED TO TRANSIENT GROUP &OPSTRAN'
&SGTGRIA(&SGSCNYA(1)) SETA &OPSTRAN        I/O CONSOLE - SAVED ONLY
         AGO   .I                             IN INPUT GLOBAL
.H1 MNOTE *,'             OUTPUT CONSOLE ASSIGNED TO TRANSIENT GROUP &O*
               PSTRAN'
&SGTGROA(&SGSCNYA(1)) SETA &OPSTRAN
.I       AIF   (T'&OPSTRNI EQ 'O').MEND      OMITTED
         AIF   (N'&CONSOLE NE 2).MG          NOT COMPOSITE
         AIF   (T'&OPSTRNI NE 'N').MH        NOT NUMERIC
         AIF   ('&OPSTRNI' GT '64').MH       INVALID
         AIF   ('&OPSTRAN' EQ '&OPSTRNI').MI
.I0 MNOTE *,'             INPUT CONSOLE ASSIGNED TO TRANSIENT GROUP &OP*
               STRNI'
&SGTGRIA(&SGSCNYA(1)) SETA &OPSTRNI
         MEXIT
.* * *    * * *       * * *        * * *        * * *       * * *
.M0 MNOTE 5,'* * * IEISCN001 MAXIMUM 31 SECONDARY CONSOLES ARE ALLOWED'
&SGQUITB SETB 1
         MEXIT
.M1 MNOTE 5,'* * * IEISCN002 CONSOLE VALUE NOT SPECIFIED'
&SGQUITB SETB 1
         MEXIT
.M20     ANOP
&SGSCNCC(&SGSCNYA(1)) SETC ''
&SGSCNYA(1) SETA &SGSCNYA(1)-1
.M2 MNOTE 5,'* * * IEISCN003 CONSOLE VALUE &CONSOLE INVALID'
&SGQUITB SETB 1
         MEXIT
.M30     ANOP
&SGSCNCC(&SGSCNYA(1)) SETC ''
&SGSCNYA(1) SETA &SGSCNYA(1)-1
.M3 MNOTE 5,'* * * IEISCN004 CONSOLE VALUE &CONSOLE PREVIOUSLY SPECIFIED
               D'
&SGQUITB SETB 1
         MEXIT
.M40     ANOP
&SGSCNAC(&SGSCNYA(1)) SETC ''
.M4 MNOTE 5,'* * * IEISCN005 ALTCONS VALUE &ALTCONS INVALID'
&SGQUITB SETB 1
         AGO   .BC
.M5 MNOTE 5,'* * * IEISCN006 ROUTCDE VALUE &ROUTCDE(&A1) INVALID'
&SGQUITB SETB 1
         AGO   .C0
.M55 MNOTE 5,'* * * IEISCN007 ROUTCDE VALUE &ROUTCDE(&A1) DUPLICATED'
&SGQUITB SETB 1
         AGO  .C0
.M6  MNOTE  5,'* * * IEISCNW01 WARNING- VALDCMD VALUE &VALDCMD(&A1)    C
               INVALID AND IGNORED'
         AGO   .CC
.M7 MNOTE 5,'* * * IEISCN009 VALDCMD VALUE &VALDCMD(&A1) DUPLICATED'
&SGQUITB SETB 1
         AGO  .CC
.M8 MNOTE 5,'* * * IEISCN010 VALDCMD SPECIFICATION FOR OUTPUT ONLY DEVIX
               CE INVALID'
&SGQUITB SETB  1
         AGO   .CC
.M9 MNOTE 5,'* * * IEISCN011 OUTPUT ONLY ALTCONS DEVICE INVALID WITH COC
               NSOLE DEVICE HAVING INPUT CAPABILITY'
&SGQUITB SETB  1
         AGO  .BC
.MA MNOTE 5,'* * * IEISCN012 USE VALUE &USE INVALID'
&SGQUITB SETB  1
         AGO   .E1
.MB MNOTE 5,'* * * IEISCN013 AREA SPECIFICATION INVALID WITH CONSOLE   C
               IN MESSAGE STREAM MODE'
&SGQUITB SETB  1
         AGO .G
.MC MNOTE 5,'* * * IEISCN014 MAXIMUM OF 11 AREAS MAY BE DEFINED'
&SGQUITB SETB  1
         AGO .G
.MD MNOTE 5,'* * * IEISCN015 AREA VALUE &AREA(&A1) INVALID'
&SGQUITB SETB  1
&SW3     SETB  1
         AGO   .E3
.ME MNOTE 5,'* * * IEISCN016 PFK VALUE &PFK INVALID'
&SGQUITB SETB  1
         AGO .H
.MF MNOTE 5,'* * * IEISCN017 OPSTRAN VALUE &OPSTRAN INVALID'
&SGQUITB SETB  1
         AGO .I
.MG MNOTE 5,'* * * IEISCN018 OPSTRNI SPECIFICATION REQUIRES COMPOSITE C*
               ONSOLE'
&SGQUITB SETB  1
         AGO .MEND
.MH MNOTE 5,'* * * IEISCN019 OPSTRNI VALUE &OPSTRNI INVALID'
&SGQUITB SETB  1
         AGO .MEND
.MI MNOTE 0,'* * * IEISCNW02 WARNING- BOTH HALVES OF COMPOSITE CONSOLE *
               SHARE SAME TRANSIENT AREA'
         AGO .I0
.MEND    MEND
