         MACRO
         TSOPTION  &LOGTIME=30,&LOGLINE=10,&BCLMT=100
         COPY  SGGBLPAK
         AIF   (NOT &SGMENTB(63)).T2    TEST FOR PREVIOUS ENTRY
    MNOTE 5,'* * * IEITSO101 TSOPTION MACRO PREVIOUSLY USED'
&SGQUITB SETB  1
         MEXIT
.T2      ANOP
&SGMENTB(63)   SETB 1                   SET TSOPTION ENTRY SWITCH
         AIF (T'&LOGTIME NE 'N').T4A      TEST FOR INVALID CHARS
         AIF (&LOGTIME GE 1 AND &LOGTIME LE 16777215).T4  VALID?
.T4A MNOTE 5,'* * * IEITSO102 LOGTIME VALUE &LOGTIME INVALID'
&SGQUITB SETB  1                          SET QUIT BIT
         AGO   .T14
.T4      ANOP
&SGTSOPA(2)    SETA  &LOGTIME
 MNOTE *,'     LOGON MESSAGE EVERY &SGTSOPA(2) SECONDS'
.T14     AIF   (T'&LOGLINE NE 'N').T14A TEST FOR INVALID CHARS.
         AIF   (&LOGLINE GE 1 AND &LOGLINE LE 32760).T16 VALID RANGE
.T14A MNOTE 5,'* * * IEITSO104 LOGLINE VALUE &LOGLINE INVALID'
&SGQUITB SETB  1                        SET QUIT BIT
         AGO   .T18                     GO CHECK NEXT PARAMETER
.T16 MNOTE *,'      LOGON AUTOMATICALLY CANCELED AFTER &LOGLINE LINES'
&SGTSOPA(3)    SETA &LOGLINE            SAVE VALUE
.T18     AIF   (T'&BCLMT NE 'N').T18A   TEST FOR INVALID CHARS.
         AIF   (&BCLMT GE 1 AND &BCLMT LE 1000).T20 VALID RANGE
.T18A MNOTE 5,'* * * IEITSO105 BCLMT VALUE &BCLMT INVALID'
&SGQUITB SETB  1                   SET QUIT BIT
         MEXIT
.T20 MNOTE *,'  SYS1.BRODCAST TO HAVE ROOM FOR &BCLMT NOTICE MESSAGES'
&SGTSOPA(4)    SETA &BCLMT              SAVE VALUE
         MEND
