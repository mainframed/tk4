         MACRO
  CENPROCS  &MODEL=40,&INSTSET=,&FEATURE=,&STORAGE=              S99247
         COPY  SGGBLPAK
.*
.*       SUPPROT CODE S99235 DESIGNATES CODE CHANGES FOR
.*       FOR BOTH 158/158 CONSOLE AND 168 CPU SUPPORT.
.*
         LCLA  &A
         AIF    (&SGMENTB(1)).EB        WAS CENPROCS CALLED THIS SYSGEN
&SGMENTB(1) SETB 1                      SW ON MEAN CENPROCS CALLED ONCE
&SGCPUB(2) SETB ('&MODEL' EQ '40')      IF MOD 40,PUT GLOBAL SWT 2 ON
&SGCPUB(3) SETB ('&MODEL' EQ '50')      IF MOD 50,PUT GLOBAL SWT 3 ON
&SGCPUB(4) SETB ('&MODEL' EQ '65')      IF MOD 65,PUT GLOBAL SWT 4 ON
&SGCPUB(5) SETB ('&MODEL' EQ '75')      IF MOD 75,PUT GLOBAL SWT 5 ON
&SGCPUB(6) SETB ('&MODEL' EQ '91')
&SGCPUB(7) SETB ('&MODEL' EQ '85')      IF MOD=85,PUT GLOBAL SWT 7 ON
&SGCPUB(8) SETB ('&MODEL' EQ '155')     IF MOD 155,PUT GLOBAL SWT 8 ON
&SGCPUB(9) SETB ('&MODEL' EQ '195')     IF MOD 195,PUT GBL SWT 9 ON
&SGCPUB(10) SETB ('&MODEL' EQ '145')    IF MOD 145,PUT GBL SWT 10 ON
&SGCPUB(11) SETB ('&MODEL' EQ '165')    IF MOD 165,PUT GLOBAL SWT 11 ON
&SGCPUB(12) SETB ('&MODEL' EQ '135')    IF MOD 135,PUT GLOBAL SWT 12 ON
.*                                                              S99235
&SGCPUB(14) SETB ('&MODEL' EQ '158')    IF MOD 158,PUT GLOBAL SWT 14 ON
&SGCPUB(15) SETB ('&MODEL' EQ '168')    IF MOD 168,PUT GLOBAL SWT 15 ON
.* &SGCPUB(13) WILL BE SET IF FEATURE=S370 IS SPECIFIED FOR MODEL 195
.A0      AIF   (&SGCPUB(2) OR &SGCPUB(3) OR &SGCPUB(4) OR              *
               &SGCPUB(5) OR &SGCPUB(6) OR &SGCPUB(7)).A1
         AIF   (&SGCPUB(8) OR &SGCPUB(9) OR &SGCPUB(10) OR             *
               &SGCPUB(11) OR &SGCPUB(12) OR &SGCPUB(14) OR            *
               &SGCPUB(15)).A1                                   S99235
&SGQUITB SETB  1                        MODEL NO. INVALID-PUT ENDGEN ON
    MNOTE 5,'* * * IEICEN101 MODEL VALUE &MODEL INVALID'
          AGO  .INSTEST           GO SCAN RTN FOR INST SET PARM  S99247
.A1 MNOTE *,'     MODEL &MODEL '
.CORTEST ANOP                           ENTER INSTSET RTN
          AIF  (T'&STORAGE EQ 'O').INSTEST IF STORAGE OMITTED, BRS99247
.*                                                               S99247
  MNOTE 0,'* * * STORAGE= &STORAGE IGNORED -- NO LONGER SUPPORTED'
.INSTEST AIF (('&INSTSET' EQ '')AND(('&MODEL' EQ '50')OR('&MODEL' EQ '6X
               5')OR('&MODEL' EQ '75')OR('&MODEL' EQ '91')OR('&MODEL' E*
               Q '85')OR('&MODEL' EQ '195'))).ANYFEAT
.*                                 THE FOLLOWING THREE LINES ARE S99235
 AIF (('&INSTSET' EQ '') AND(('&MODEL' EQ '145') OR ('&MODEL' EQ '155')*
               OR  ('&MODEL' EQ '158') OR ('&MODEL' EQ '165') OR       *
               ('&MODEL' EQ '168') OR ('&MODEL' EQ '135'))).ANYFEAT
 AIF (('&MODEL' EQ '40') AND ('&INSTSET' EQ '')).E4
&SGCPUB(30) SETB ('&INSTSET' EQ 'STD')  IF STD + MOD30/40,GLB SWT 30 ON
&SGCPUB(31) SETB ('&INSTSET' EQ 'COMM') IF COMMERCIAL SET,GLB SWT 31 ON
&SGCPUB(32) SETB ('&INSTSET' EQ 'SCNTF') IF SCIENTIFIC SET,GLB SWT32 ON
&SGCPUB(33) SETB ('&INSTSET' EQ 'UNIV') IF UNIVERSAL SET,GLB SWT 33 ON
     AIF (&SGCPUB(30) OR &SGCPUB(31) OR &SGCPUB(32) OR &SGCPUB(33)).A3
&SGQUITB SETB  1                        INSTSET INVALID-TURN ON SGQUITB
    MNOTE 5,'* * * IEICEN103 INSTSET VALUE &INSTSET INVALID'
         AGO   .ANYFEAT                 GO TEST IF OPT. FEATURES SPEC'D
.E4 MNOTE 5,'* * * IEICEN104 INSTSET VALUE NOT SPECIFIED'
&SGQUITB SETB  1                        MUST SPEC INSTSET FOR MOD 30/40
         AGO  .ANYFEAT                  GO TEST IF OPT. FEATURES SPEC'D
.A3 MNOTE *,'     &INSTSET INSTRUCTION SET '
 AIF (NOT((&SGCPUB(10) OR &SGCPUB(12)) AND &SGCPUB(30))).INSDEF
&SGCPUB(30) SETB 0
&SGCPUB(31) SETB 1
  MNOTE 0,'* * * IEICENW01  INSTSET &INSTSET CHANGED TO COMM FOR MODEL-*
               &MODEL .'
.INSDEF  ANOP
 AIF (NOT ((&SGCPUB(8) OR &SGCPUB(9) OR &SGCPUB(11) OR &SGCPUB(14) OR  *
               &SGCPUB(15)) AND (&SGCPUB(30) OR &SGCPUB(31) OR         *
               &SGCPUB(32)))).ANYFEAT                            S99235
&SGCPUB(30)    SETB 0
&SGCPUB(31)    SETB 0
&SGCPUB(32)    SETB 0
&SGCPUB(33)    SETB 1
   MNOTE 0,'* * * IEICENW01  INSTSET &INSTSET CHANGED TO UNIV FOR MODELX
               -&MODEL .'
.ANYFEAT AIF   (T'&FEATURE EQ 'O').PHASE2    GO TO STD. FEAT ROUTINE
    MNOTE *,'     FOLLOWING FEATURES SPECIFIED'
.TESTCTR AIF (&A EQ N'&FEATURE).PHASE2  IF ALL FEAT.S SCANNED,GO PHASE2
&A       SETA  &A+1                     INCREMENT TO NEXT FEAT SUB-OPRN
 AIF ('&FEATURE(&A)' EQ 'PROTECT').F2   IF STORAGE PROTECT,GO SET SW 41
 AIF ('&FEATURE(&A)' EQ 'S370' AND '&MODEL' EQ '195').F3
&SGQUITB SETB  1                        FEATURE INVALID-TURN ON SGQUITB
    MNOTE 5,'* * * IEICEN105 FEATURE VALUE &FEATURE(&A) INVALID'
         AGO   .TESTCTR                 GO SEE IF FEAT. SUBLST EXHAUSTD
.F2 MNOTE *,'        STORAGE PROTECTION'
&SGCPUB(41) SETB 1                      TURN ON PROTECTION SWT.
         AGO   .TESTCTR                 GO SEE IF ALL FEAT.S SCANNED
.F3 MNOTE *,'        SYSTEM 370 / MODEL 195'
&SGCPUB(9)  SETB 0       TURN OFF S360/M195 GLOBAL
&SGCPUB(13) SETB 1       TURN ON  S370/M195 GLOBAL
         AGO   .TESTCTR
.PHASE2  ANOP
    MNOTE *,'     FOLLOWING FEATURES STANDARD ON MODEL &MODEL '
 AIF (&SGCPUB(5) OR &SGCPUB(6) OR &SGCPUB(7) OR &SGCPUB(8) OR &SGCPUB(9*
               ) OR &SGCPUB(10) OR &SGCPUB(11) OR &SGCPUB(12) OR       *
                &SGCPUB(13) OR &SGCPUB(14) OR &SGCPUB(15)).A6    S99235
    AIF (&SGCPUB(4) OR &SGCPUB(3)).A7   IF MOD 65/50 SET 3 STD-FEAT SWS
         AGO   .A8                      GO SET 1 STD-FEAT FOR MODEL 40
.A6 MNOTE *,'        DIRECT CONTROL '
.*                                 THE FOLLOWING LINE IS FOR S99235
&SGCPUB(43) SETB 1   MOD. 75/85/91/135/145/155/158/165/168/195 DEFAULTS
 AIF ('&INSTSET' EQ 'UNIV' OR '&INSTSET' EQ 'SCNTF' OR '&INSTSET' NE ''*
               ).A71
   AIF (NOT (&SGCPUB(10) OR &SGCPUB(12))).A7      NOT 145,135
    MNOTE *,'        COMMERCIAL INSTRUCTION SET '
&SGCPUB(31) SETB 1            MOD. 135/145 DEFAULT
   AGO .A71
.A7 MNOTE *,'        UNIVERSAL INSTRUCTION SET '
.*                                 THE FOLLOWING LINE IS FOR S99235
&SGCPUB(33) SETB 1         MOD. 91/75/65/50/85/155/158/165/168 DEFAULT
.A71     ANOP
    MNOTE *,'        STORAGE PROTECTION  '
&SGCPUB(41) SETB 1
.A8 MNOTE *,'        INTERVAL TIMER '
&SGCPUB(40) SETB 1
.*                               SEQ # 019270-021190 REMOVED FOR S99247
.C1A     AIF (&SGCPUB(6) OR &SGCPUB(9) OR &SGCPUB(13)).EN  MOD 91/195
.C2 AIF (NOT((&SGCPUB(3) OR &SGCPUB(4) OR &SGCPUB(5)OR &SGCPUB(7)) AND *
               (&SGCPUB(30)OR &SGCPUB(31)OR &SGCPUB(32)))).EN
.T1      ANOP
&SGQUITB SETB  1
    MNOTE 5,'* * * IEICEN108 INSTSET VALUE &INSTSET INVALID ON MOD. &MOX
               DEL'
         MEXIT
.*                               SEQ # 023000-023600 REMOVED FOR S99247
.EB MNOTE 5,'* * * IEICEN109 CENPROCS MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                        ONLY SYSGEN ONE CPU AT A TIME
.EN      MEND                           EXIT FROM CENPROCS MACRO
