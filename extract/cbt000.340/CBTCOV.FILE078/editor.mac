         MACRO
         EDITOR  &DESIGN=,&SIZE=
         COPY  SGGBLPAK
         LCLA  &V1,&V2
         LCLC  &VAL1,&VAL2
         AIF   (T'&DESIGN EQ 'O').M0
         AIF   ('&DESIGN' EQ 'F44').F1
         AIF   ('&DESIGN' EQ 'F88').F3
         AIF   ('&DESIGN' EQ 'F128').F5
         AGO   .M1
.F1      AIF   (&SGLINKB(2)).M2                                 SA68152
&SGLINKB(2) SETB 1                                              SA68152
         AGO   .F2
.F3      AIF   (&SGLINKB(3)).M2                                 SA68152
&SGLINKB(3)    SETB  1                                          SA68152
         AGO   .F2
.F5      AIF   (&SGLINKB(4)).M2                                 SA68152
&SGLINKB(4)    SETB  1                                          SA68152
         AGO   .F2
.SIZE    AIF   ('&DESIGN' NE 'F128').NOT128
&V1      SETA  128
&V2      SETA  18                                               SA68152
         AGO   .DVAL1
.NOT128  AIF   ('&DESIGN' NE 'F88').NOT88
&V1      SETA  90
&V2      SETA  8
         AGO   .DVAL1
.NOT88   ANOP              F44
&V1      SETA  90
&V2      SETA  16
.DVAL1   AIF   (N'&SIZE EQ 0).DVAL
.VALUE   AIF   (N'&SIZE GT 2).M5       PICK UP VALUES
         AIF   (T'&SIZE(1) EQ 'O').VL2
         AIF   ('&SIZE(1)'(K'&SIZE(1),1) NE 'K').M5
&VAL1    SETC  '&SIZE(1)'(1,K'&SIZE(1)-1)
&V1      SETA  &VAL1
.VL2     AIF   (N'&SIZE EQ 1).DVAL
         AIF   ('&SIZE(2)'(K'&SIZE(2),1) NE 'K').M5
&VAL2    SETC  '&SIZE(2)'(1,K'&SIZE(2)-1)
&V2      SETA  &VAL2
.DVAL    AIF   (&V1 LT 44 OR &V1 GT 9999).M6   VALUE
         AIF   (&V2 LT 6 OR &V2 GT 100).M7     ERROR
         AIF   ('&DESIGN' EQ 'F44').S44     WHICH
         AIF   ('&DESIGN' EQ 'F88').S88          F
         AIF   ('&DESIGN' EQ 'F128').S128   EDITOR
.S44     AIF   (&V1-38 LT &V2).M8                               SA68152
&SGLINKA(1)  SETA  &V1*1024
&SGLINKA(2)  SETA  &V2*1024
         AGO   .MA
.S88     AIF  (&V1-52 LT &V2).MC                                SA68152
&SGLINKA(3)  SETA  &V1*1024
&SGLINKA(4)  SETA  &V2*1024
         AGO   .MA
.S128    AIF   (&V1-68 LT &V2).MB                               SA68152
&SGLINKA(5)  SETA  &V1*1024
&SGLINKA(6)  SETA  &V2*1024
         AGO   .MA
.F2 MNOTE *,'      LINKAGE EDITOR &DESIGN IS REQUESTED.'
&SGLINKB(1) SETB 1
         AGO   .SIZE
.M0 MNOTE 5,'* * * IEIEDT103 DESIGN VALUE NOT SPECIFIED'
&SGQUITB SETB  1
         MEXIT
.M1 MNOTE 5,'* * * IEIEDT101 DESIGN VALUE &DESIGN INVALID'
         AGO   .M3
.M2 MNOTE 5,'* * * IEIEDT102 DESIGN VALUE &DESIGN PREVIOUSLY REQUESTED'
         AGO   .M3
.M5 MNOTE 5,'* * * IEIEDT103 SIZE VALUE &SIZE INVALID'
         AGO   .M3
.M6 MNOTE 5,'* * * IEIEDT104 SIZE VALUE &V1 INVALID'
         AGO   .M3
.M7 MNOTE 5,'* * * IEIEDT105 SIZE VALUE &V2 INVALID'
         AGO   .M3
.M8 MNOTE 5,'* * * IEIEDT106 SIZE VALUE &V1 MUST BE 38K GREATER THAN &VV
               2'
         AGO   .M3
.MA MNOTE *,'      &V1.K IS THE MAXIMUM AMOUNT OF MAIN STORAGE THE LINKK
               AGE EDITOR WILL USE.  &V2.K  WILL BE ASSIGNED TO THE TEXX
               T BUFFER.'
         AGO   .MEND
.MB MNOTE 5,'* * * IEIEDT108 SIZE VALUE &V1 MUST BE 68K GREATER THAN &VV
               2'                                               SA68152
       AGO  .M3
.MC MNOTE 5,'* * * IEIEDT109 SIZE VALUE &V1 MUST BE 52K GREATER THAN &VV
               2'                                               SA68152
.M3      ANOP
&SGQUITB SETB  1
.MEND    MEND
