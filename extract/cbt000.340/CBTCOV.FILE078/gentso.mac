         MACRO
    GENTSO  &UT1SDS=,&UT2SDS=,&UT3SDS=,&OBJPDS=,&RESNAME=,&RESVOL=,    *
               &RESTYPE=2311,&LNKNAME=,&LNKVOL=,&LEPRT=,&ASMPRT=OFF,   *
               &DIRDATA=,&GENTYPE=ALL,                         SA68152 *
               &INDEX=SYS1,&UT4SDS=,&CMDVOL=,&CMDNAME=,&UTDISP=KEEP,   *
               &SYOUTDV=,&LOGREC=NOPRINT,&JOBSTMT=NOTSUPPLIED
         COPY  SGGBLPAK
         GBLB  &QUITONB
.**********************************************************************
.*** * *  SYNCHRONIZATION OF GENTSO AND GENERATE FOR TSO SYSGENS  * * *
.*** GENTSO   *   *   *   *   *   *   *   *   *   *   *   *   *   * * *
.*** 1) IF GENERATE ENTRY SW ON--ERROR 6) IF 2ND-CALL SW ON, GOTO 11)
.*** 2) SET GENTSO ENTRY SW            7) DO TSO GROUP 2,4,5 MACROS ETC
.*** 3) DO TSO ERROR CHECKING          8) SET GENERATE 2ND CALL SW
.*** 4) SET GENERATE FIRST-CALL SW     9) BRANCH TO RE-CALL GENR
.*** 5) CALL GENERATE                  10) SET GENERATE ENTRY SW, MEND
.*** GENERATE *   *   *   *   *   *   *   *   *   *   *   *   *   * * *
.*** 1) IF GENERATE ENTRY SW ON--ERROR
.*** 2) ERROR IF GENTSO ENTRY SW OFF AND OPTIONS=TSO
.*** 3) IF GENTSO 2ND-CALL SW ON DO GENR LE, COPIES,MEND
.*** 4) SET GENERATE ENTRY SW & CHECK PARMS ETC UP TO JOB CARD
.*** 5) DO GENERATE'S GROUP 2 MACROS
.*** 6) IF GENTSO 1ST CALL SW ON, MEXIT
.**********************************************************************
         AIF   (&SGMENTB(88) OR &SGMENTB(69)).DO TEST FOR PREVIOUS NTRY
&QUITONB SETB  (&SGQUITB)
         AIF (NOT &SGSCHDB(44)).DP  OPTIONS=TSO NOT CODED
.A00     ANOP
&SGMENTB(69)   SETB 1                   SET GENTSO ENTRY SWITCH
         AIF ('&GENTYPE(1)' EQ 'ALL').A0   ALL GEN?
         AIF ('&GENTYPE(1)' EQ 'PROCESSOR').A1 PROC GEN?
         AGO   .B22
.A0      AIF (NOT &SGMENTB(61) AND NOT &SGMENTB(62)).A3  NO EDIT OR OUT
         AIF (&SGMENTB(60)).A4             CMDLIB?
  MNOTE 5,'* * * IEITSO005 CMDLIB MUST BE SPECIFIED WITH OUTPUT OR     *
                           EDIT ON SYSGEN TYPE ALL'
&SGQUITB SETB  1                          SET QUIT SWITCH
         AGO   .A2                        SEE IF CMDVOL,NAME SPECIFIED
.A1      ANOP
&GETB(2) SETB  1
         AIF (&SGMENTB(60)).A4             CMDLIB?
         AIF   (NOT &SGMENTB(61) AND NOT &SGMENTB(62)).B222 NO EDIT,OUT
         AGO   .A4                        CHECK CMDVOL,NAME PARMS
.A2      AIF (T'&CMDVOL EQ 'O' AND T'&CMDNAME EQ 'O').B8 OMITTED PARMS?
  MNOTE 0,'* * * IEITSOW10 CMDVOL/CMDNAME PARAMETERS IGNORED-          *
                            CMDLIB MACRO WAS NOT SPECIFIED'
         AGO   .B8                        CALL TSGEN100
.A3      AIF (NOT &SGMENTB(60)).A2         NO CMDLIB?
.A4      AIF (N'&CMDVOL NE N'&CMDNAME).DM  NOT CORRECTLY SPECIFIED
         AIF ('&CMDNAME' EQ '').B6        NOT SPECIFIED-DEFAULT SYSRES
         AIF   ((K'&CMDNAME GT 8) OR (K'&CMDVOL GT 6)).DQ
&SGCTRLC(23)   SETC  '&CMDVOL'
&SGCTRLC(24)   SETC  '&CMDNAME'
   MNOTE *,'CMDLIB WILL RESIDE ON DEVICE &CMDNAME VOLUME &CMDVOL'
         AGO   .B8                        CALL TSGEN100
.B6      ANOP
&SGCTRLC(23)   SETC  '&RESVOL'
&SGCTRLC(24)   SETC  '&RESNAME'
   MNOTE *,'CMDLIB WILL RESIDE ON SYSRES'
         AGO   .B8                        CALL TSGEN100
.DM MNOTE 5,'* * * IEITSO015 CMDVOL/CMDNAME PARAMETERS CONTRADICTORY'
&SGQUITB SETB  1                          SET QUIT SWITCH
         AGO   .B8                        CALL TSGEN100
.DO MNOTE 5,'* * * IEITSO020 GENERATE OR GENTSO MACRO PREVIOUSLY USED'
         MEXIT
.DP MNOTE 5,'* * * IEITSO025 OPTIONS=TSO MUST BE CODED ON              *
                             SCHEDULR MACRO IF GENTSO IS CODED'
&SGQUITB SETB  1
         AGO   .A00
.DQ MNOTE 5,'* * * IEITSO030 CMDNAME VALUE &CMDNAME INVALID OR CMDVOL  *
               VALUE &CMDVOL INVALID'
&SGQUITB SETB  1
.*  *  *  *    TSO ERROR CHECKING    *  *  *  *
.B8      ANOP
*** TSGEN100 ***                                          *** C9505 ***
         TSGEN100
.B222    ANOP
&SGMENTB(70)   SETB 1                   SET GENERATE 1ST CALL SWITCH
.*  *  *  *  *  CALL GENERATE FOR GROUP 2 ASSEMBLY MACROS  *  *  *  *
.* * * * * * * * * * * *
.B22     ANOP
 GENERATE  UT1SDS=&UT1SDS,UT2SDS=&UT2SDS,UT3SDS=&UT3SDS,UT4SDS=&UT4SDS,*
               OBJPDS=&OBJPDS,RESNAME=&RESNAME,RESVOL=&RESVOL,RESTYPE=&*
               RESTYPE,LNKNAME=&LNKNAME,LNKVOL=&LNKVOL,GENTYPE=&GENTYPE*
               ,INDEX=&INDEX,UTDISP=&UTDISP,JOBSTMT=&JOBSTMT,SYOUTDV=&S*
               YOUTDV,LOGREC=&LOGREC,DIRDATA=&DIRDATA,         SA68152 *
               ASMPRT=&ASMPRT,LEPRT=&LEPRT
.* * * * * * * * * * * *
         AIF   (&SGMENTB(16)).OVRIDE
       AIF  (&SGQUITB).MEND
.OVRIDE  AIF   (&GETB(1) OR &GETB(3)).B52    TEST FOR I/O, NUC GENTYPES
         AIF   (&SGMENTB(71)).B52       TEST FOR 2ND RETURN - ALL DONE
         AIF (&GETB(2)).B10                PROC ONLY GEN?
*** SGIKJ2LP *** TSO SCHEDULER                            *** C5555 ***
         SGIKJ2LP
.B10     AIF (NOT &GETB(2) AND NOT &SGMENTB(60)).B20 NOT ALL,NO CMDLIB
         AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(61)).B12
*** SGIKJ2EB *** TSO EDIT                                 *** UL506 ***
         SGIKJ2EB
.B12     AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(62)).B20
*** SGIKJ2EF *** TSO TYPE 1 UTILITIES                     *** UN506 ***
         SGIKJ2EF
.B20     ANOP
.***** INSERT GROUP 4'S HERE
.*     INITIALIZE SWITCHES TO NULL
&SGCTRLC(6)    SETC ''
&SGCTRLC(10)   SETC ''
&SGCTRLC(11)   SETC ''
&SGCTRLC(12)   SETC ''
&SGCTRLC(13)   SETC ''
&SGCTRLC(14)   SETC ''
.*  *  *  SET LINKAGE EDIT CHARACTERISTICS  *  *  *  *  *
&SGCTRLC(4)    SETC  ''
.* REMOVAL OF LBMAINT PARAMETER 31600020, 32000020,32400020     PTM0003
&SGCTRLC(1)    SETC  ''
&SGCTRLC(2)    SETC  ''
         AIF   (('&LEPRT(1)' EQ '') AND ('&LEPRT(2)' EQ '')).B28
         AIF   ('&LEPRT(2)' EQ '').B26
&SGCTRLC(2)    SETC  ',&LEPRT(2)'
.B26     ANOP
&SGCTRLC(1)    SETC  ',&LEPRT(1)'
.B28     ANOP
         AIF   (&GETB(2)).B30           TEST FOR PROC (NOT ALL) GENTYPE
*** SGIKJ4EA *** TSO SUPERVISOR                           *** C2555 ***
         SGIKJ4EA
*** SGIKJ4EC *** TSO DATA MANAGEMENT                      *** CI555 ***
         SGIKJ4EC
.B30     ANOP                                                   SA69763
*** SGIKJ445 *** TSO SCHEDULER                            *** C5555 ***
         SGIKJ445
*** SGIKJ4EH *** TSO TYPE 1 UTILITIES                     *** UN506 ***
         SGIKJ4EH
*** SGIKJ4IE *** TSO LINK EDIT F PROMPTER                 *** ED521 ***
         SGIKJ4IE
.B31     AIF (NOT &GETB(2) AND NOT &SGMENTB(60)).B32
         AIF (NOT &SGMENTB(60)).B31B      CMDLIB?
*** SGIKJ4EG *** TSO TEST                                 *** UM506 ***
         SGIKJ4EG
.B31B    AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(61)).B31D
*** SGIKJ4EB *** TSO EDIT                                 *** UL506 ***
         SGIKJ4EB
.B31D    AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(62)).B32
*** SGIKJ4EF *** TSO TYPE 1 UTILITIES                     *** UN506 ***
         SGIKJ4EF
.B32     ANOP
.*  *  *  *  MOVE - COPY JCL FOR GROUP 5 MACRO CALLS  *  *  *  *
         AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(61)       X
               AND NOT &SGMENTB(62) AND NOT &SGMENTB(59) AND NOT       X
                &SGMENTB(65)).RXC         NO NEED FOR MOVE-COPY JCL
&SGCTRLA(1)    SETA &SGCTRLA(1)+1
&SGCTRLC(11)   SETC '&RESNAME'
&SGCTRLC(12)   SETC '&RESVOL'
&SGCTRLC(13)   SETC '&SGCTRLC(31)'
&SGCTRLC(14)   SETC '&SGCTRLC(32)'
*** GENTSO *** SYSGEN                                     *** C9555 ***
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IEBCOPY,COND=(8,LT)'
 PUNCH '//SYSUT3 DD DISP=SHR,DSNAME=SYS1.&SGCTRLC(3)'
 PUNCH '//SYSUT4 DD DISP=SHR,DSNAME=&UT4SDS'
 PUNCH '//SYSPRINT DD SPACE=(121,(750,100),RLSE),'
 PUNCH '//             DCB=(RECFM=FB,LRECL=121,BLKSIZE=121),           *
                       X'
 PUNCH '//             SYSOUT=A&SGCTRLC(46)&SGCTRLC(47)&SGCTRLC(48)'
 PUNCH '//CI535 DD DISP=SHR,VOL=(,RETAIN),DSN=SYS1.CI535'
 PUNCH '//CI555 DD DISP=SHR,VOL=(,RETAIN),DSN=SYS1.CI555'
         AIF (&GETB(2) AND NOT &SGMENTB(60)).B32B   PROC, NO CMDLIB
 PUNCH '//DM508 DD DISP=SHR,VOLUME=(,RETAIN),DSN=SYS1.DM508'
 PUNCH '//CI505 DD DISP=SHR,VOL=(,RETAIN),DSN=SYS1.CI505'
.B32B    AIF (NOT &SGMENTB(59)).B35    TEST FOR TSO HELP MACRO
.B33     AIF   ('&SGCTRLC(51)' NE '').B34    TEST FOR UNIT/VOLNO SPEC'D
&SGCTRLC(51)   SETC '&RESNAME'          SET DEFAULT VALUES
&SGCTRLC(52)   SETC '&RESVOL'
.B34     ANOP
 PUNCH '//DHELP DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.DHELP'
 PUNCH '//HELP DD DSNAME=&SGCTRLC(40)..HELP,VOLUME=(,RETAIN,SER=&SGCTRL*
               C(52)),            XXXXXXXX'
 PUNCH '//             UNIT=&SGCTRLC(51),DISP=OLD'
.B35     AIF   (NOT &SGMENTB(65)).B37    TEST FOR TSO UADS MACRO
         AIF   ('&SGCTRLC(49)' NE '').B36    TEST FOR UNIT/VOLNO SPEC'D
&SGCTRLC(49)   SETC '&RESNAME'          SET DEFAULT VALUES
&SGCTRLC(50)   SETC '&RESVOL'
.B36     ANOP
 PUNCH '//DUADS DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.DUADS'
 PUNCH '//UADS DD DSNAME=&SGCTRLC(40)..UADS,VOLUME=(,RETAIN,SER=&SGCTRL*
               C(50)),            XXXXXXXX'
 PUNCH '//             UNIT=&SGCTRLC(49),DISP=OLD'
.B37     AIF (NOT &GETB(2) AND NOT &SGMENTB(60)).B39  NO DD  CARDS
         AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(61) AND   X
                NOT &SGMENTB(62)).B39      NO DD  CARDS
 PUNCH '//DCMDLIB DD DISP=SHR,VOLUME=(,RETAIN),DSNAME=SYS1.DCMDLIB'
 PUNCH '//CMDLIB DD DSN=&SGCTRLC(40)..CMDLIB,VOL=(,RETAIN,SER=&SGCTRLC(*
               23)),'
 PUNCH '//             UNIT=&SGCTRLC(24),DISP=OLD'
.B39     ANOP
 PUNCH '//SVCLIB DD DSNAME=&SGCTRLC(40)..SVCLIB,VOLUME=(,RETAIN,SER=&SG*
               CTRLC(12)),        XXXXXXXX'
 PUNCH '//             UNIT=&SGCTRLC(11),DISP=OLD'
 PUNCH '//LINKLIB DD DSNAME=&SGCTRLC(40)..LINKLIB,VOLUME=(,RETAIN,SER=&*
               SGCTRLC(14)),      XXXXXXXX'
 PUNCH '//             UNIT=&SGCTRLC(13),DISP=OLD'
 PUNCH '//SYSIN DD *'
.***** INSERT GROUP 5'S HERE
         AIF   (NOT &SGMENTB(59)).B40A  TEST FOR HELP MACRO
 PUNCH ' COPY OUTDD=HELP,INDD=DHELP'
.B40A    AIF   (NOT &SGMENTB(65)).B40C  TEST FOR UADS MACRO
 PUNCH ' COPY OUTDD=UADS,INDD=DUADS'
.B40C    AIF   (&GETB(2)).B40E          TEST FOR PROCESSOR GENTYPE
*** SGIKJ5EA *** TSO SUPERVISOR                           *** C2555 ***
         SGIKJ5EA
*** SGIKJ5EC *** TSO DATA MANAGEMENT                      *** CI555 ***
         SGIKJ5EC
.B40E    AIF (NOT &GETB(2) AND NOT &SGMENTB(60)).B40F NO PROC,NO CMDLIB
         AIF (&GETB(2) AND NOT &SGMENTB(60)).B40F PROC, NO CMDLIB
*** SGIKJ5IE *** TSO LINK EDIT F PROMPTER                 *** ED521 ***
         SGIKJ5IE
.B40F    AIF (NOT &GETB(2) AND NOT &SGMENTB(60)).B44 NO PROC,NO CMDLIB
         AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(61)).B42
*** SGIKJ5EB *** TSO EDIT                                 *** UL506 ***
         SGIKJ5EB
.B42     AIF (&GETB(2) AND NOT &SGMENTB(60) AND NOT &SGMENTB(62)).B44
*** SGIKJ5EF *** TSO TYPE 1 UTILITIES                     *** UN506 ***
         SGIKJ5EF
.B44     AIF (&GETB(2) AND NOT &SGMENTB(60)).B46 PROC, NO CMDLIB
*** SGIKJ5EG *** TSO TEST                                 *** UM506 ***
         SGIKJ5EG
*** SGIKJ540 *** TSO SCHEDULER                            *** C5555 ***
         SGIKJ540
.B46     AIF (NOT &SGMENTB(60)).B50       CMDLIB?
*** SGIKJ5EH *** TSO TYPE 1 UTILITIES                     *** UN506 ***
         SGIKJ5EH
.B50     ANOP
 PUNCH '/*'
.*  *  *  *  *  RENAME STEP FOR TSO HELP AND UADS DATA SETS  *  *  *  *
         AIF (NOT &SGMENTB(65) AND NOT &SGMENTB(59)).RXC NO HELP,UADS
         AIF   ('&INDEX' EQ 'SYS1').RXC
&SGCTRLA(1)    SETA &SGCTRLA(1)+1
 PUNCH '//SG&SGCTRLA(1) EXEC PGM=IEHPROGM,COND=(8,LT)'
 PUNCH '//SYSPRINT DD SPACE=(121,(500,40),RLSE),'
 PUNCH '//             DCB=(RECFM=FB,LRECL=121,BLKSIZE=121),           *
                       X'
 PUNCH '//             SYSOUT=A&SGCTRLC(46)&SGCTRLC(47)&SGCTRLC(48)'
 PUNCH '//SYSRES DD DISP=OLD,VOLUME=(,RETAIN,SER=&RESVOL),UNIT=&RESNAME*
               '
.RNA     AIF   (NOT &SGMENTB(59)).RNB   TEST FOR TSO HELP DATA SET
 PUNCH '//HEL DD DISP=OLD,VOLUME=(,RETAIN,SER=&SGCTRLC(52)),UNIT=&SGCTR*
               LC(51) '
.RNB     AIF   (NOT &SGMENTB(65)).RNC   TEST FOR TSO UADS DATA SET
 PUNCH '//UAD DD DISP=OLD,VOLUME=(,RETAIN,SER=&SGCTRLC(50)),UNIT=&SGCTR*
               LC(49) '
.RNC     ANOP
 PUNCH '//SYSIN DD *'
.RXA     AIF   (NOT &SGMENTB(59)).RXB   TEST FOR TSO HELP DATA SET
 PUNCH ' RENAME DSNAME=&INDEX..HELP,NEWNAME=SYS1.HELP,                 *
                      XXXXXXXX'
 PUNCH '               VOL=&SGCTRLC(51)=&SGCTRLC(52)'
.RXB     AIF   (NOT &SGMENTB(65)).RXC   TEST FOR TSO UADS
 PUNCH ' RENAME DSNAME=&INDEX..UADS,NEWNAME=SYS1.UADS,                 *
                      XXXXXXXX'
 PUNCH '               VOL=&SGCTRLC(49)=&SGCTRLC(50)'
.RXC     ANOP
.*  *  *  *  TSO MACRO'S ARE THROUGH - CALL GENERATE FOR REMAINING *  *
&SGMENTB(71)   SETB 1                   SET GENERATE 2ND CALL SWITCH
         AGO   .B22                     GO TO CALL GENERATE
.B52     ANOP
&SGMENTB(88)   SETB 1                   SET GENERATE ENTRY SWITCH
.MEND    MEND
