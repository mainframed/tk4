         MACRO
         SGIEA2BK
         COPY SGGBLPAK
         LCLC  &ACSMETH
         LCLC  &ROLLOUT                                            MTS0
         LCLA  &FLAG                                              19095
         LCLA  &FLAGB                                             20071
         LCLB  &TOD                                               20071
         LCLC  &PATCH                                             20075
         LCLC  &RPS                                               20201
.*2501,002480-002530                                               MP65
.*2501,002587                                                      MP65
.* THIS MACRO PASSES THE NUMBER OF PAIRED TRANSIENT AREAS TO THE IEAQBK
.*       MACRO FOR THE ASSEMBLY OF THE BLOCK MODULE
.*
.*       &FLAG SETS THE FOLLOWING BITS IN THE CVT CVTOPTA BYTE    19022
.*             RMS BITS  182.0,182.1,182.2                        19022
.*             LCS BIT   182.5                                    19095
.*             ASCII BIT 182.6                                    M3190
.*
.***********************************************************      20071
.*                                                                20071
.*       &FLAGB SETS THE FOLLOWING BITS IN THE CVTOPTB-(BYTE 183) 20071
.*             183.2     CCVTTOD                                  20071
.*                                                                20071
.***********************************************************      20071
&FLAG    SETA  4*&SGCPRGB(11)+32*&SGSUPRB(33)+64*&SGSUPRB(35)+128*&SGSU*
               PRB(29)+2*&SGSUPRB(41)                             M3190
.********  CHECK FOR CPUS WITHOUT THE TOD CLOCK FEATURE   *****   20071
.*       MODELS  30,40,50,65,75,85,91 AND 195/360             CD 21108
&TOD     SETB  (NOT(&SGCPUB(1) OR &SGCPUB(2) OR &SGCPUB(3) OR &SGCPUB(4*
               ) OR &SGCPUB(5) OR &SGCPUB(6) OR &SGCPUB(7) OR &SGCPUB(9*
               )))                                                20071
&FLAGB   SETA  32*&TOD                                            20071
&SGCTRLC(6) SETC 'IEAQBK00'
         COPY SGASMPAK
         AIF   (NOT &SGDMGTB(3)).XX1
&ACSMETH SETC  'QTAM'
.XX1     ANOP
         AIF   (NOT &SGCPRGB(6)).XX2                               RORI
&ROLLOUT SETC  'ROLLOUT'
.XX2     ANOP                                                      RORI
&PATCH   SETC  'YES'                                              20075
&RPS     SETC  'NO'                                               20201
         AIF   (NOT(&SGDCLSB(69) OR &SGDCLSB(75) OR &SGDCLSB(76))).XX4
&RPS     SETC  'YES'                                              20201
.XX4     ANOP                                                     20201
         PUNCH '        IEAQBK &SGCPRGA(5),&ACSMETH,                   C
                                    XXXXXXXXXXXXXXX'
         PUNCH '               &ROLLOUT,                               C
                              XXXXXXXXXX'
         PUNCH '               (&SGCPRGA(8),&SGCPRGA(9),               C
                                            XXXXXXXX'
         PUNCH '               &SGCPRGA(10),&SGCPRGA(11),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(12),&SGCPRGA(13),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(14),&SGCPRGA(15),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(16),&SGCPRGA(17),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(18),&SGCPRGA(19),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(20),&SGCPRGA(21),              C
                                              XXXXXXXX'
         PUNCH '               &SGCPRGA(22)),&SGCPRGB(13),         MP65C
                                              XXXXXXXX'           19022
         PUNCH '               &SGSUPRB(33),                      19022C
                                   XXXXXXXXX'                     19022
         PUNCH '               FLAG=&FLAG,FLAGB=&FLAGB,           20071X
                                 XXXXXXXXXX'                     A38862
         PUNCH '               PATCH=&PATCH,RPS=&RPS,WARN=&SGCPRGA(23)'
.XX5     ANOP                                                      MTSO
         PUNCH '         END'
         PUNCH '/*'
         MEND
