         MACRO
         SCHEDULR &TYPE=MFT,&DESIGN=,&VLMOUNT=,&TAVR=800,&CONSOLE=,&ALT*
               CONS=,&JOBQRES=,&STARTR=,&STARTW=,&ACCTRTN=,&REPLY=,&WTO*
               BFRS=,&STARTI=,&PROCRES=,&WTLCLSS=,&WTLBFRS=,&JOBQFMT=,&*
               JOBQTMT=,&INITQBF=,&MINPART=,&OPTIONS=,&CONOPTS=,&ROUTCD*
               E=,&OLDWTOR=,&HARDCPY=,&ESV=,&EVA=NO,&JOBQWTP=,         *
               &JOBQLMT=,&SUBMITQ=,&AREA=,&PFK=,&OPSTRAN=,&OPSTRNI=
         COPY  SGGBLPAK
         LCLA  &A,&E,&F,&G,&H
         LCLB &B(10),&NOLOG
         LCLC  &C,&D
         AIF   (&SGMENTB(6)).V1         SCHEDULR ENTRY SWITCH
&SGMENTB(6)    SETB 1
.* * * TYPE * * *                       * * * TYPE * * *          * * *
         AIF   ('&TYPE' NE 'MFT').A1
&SGSCHDB(2)    SETB 1                   TYPE=MFT OR TYPE OMITTED
         AGO   .A4
.A1      AIF   ('&TYPE' NE 'M65MP').A2  TYPE=M65MP - MVT ASSUMED
&SGSCHDB(33)  SETB  1
         AGO   .A3
.A2      AIF   ('&TYPE' NE 'MVT').V2
.A3      ANOP
&SGSCHDB(3)    SETB 1
.A4 MNOTE *,'       THE &TYPE SCHEDULR IS INCLUDED'
.* * * DESIGN * * *                     * * * DESIGN * * *        * * *
.A6      AIF   (T'&DESIGN EQ 'O').B0    DESIGN OMITTED
         AIF   (&SGSCHDB(3)).V3         DESIGN INVALID FOR MVT
         AIF   ('&DESIGN' EQ '30K').AE
         AIF   ('&DESIGN' NE '44K').V4
&SGSCHDB(12)   SETB 1                   44K VALID FOR ALL TYPES
.AA MNOTE *,'       THE DESIGN LEVEL IS &DESIGN'
         AGO   .B2
.AE      ANOP
&SGSCHDB(9)    SETB 1
    MNOTE *,'       THE DESIGN LEVEL IS 30K'
         AGO   .B2
.B0      AIF   (&SGSCHDB(2)).AE         30K DEFAULT FOR MFT
.* * * VLMOUNT * * *                    * * * VLMOUNT * * *       * * *
.B2      AIF   (T'&VLMOUNT NE 'O').B2A  VLMOUNT SPECIFIED; PROCESS IT
&SGSCHDB(24)   SETB 1                    OMITTED SET TAVR TO DEFAULT
    MNOTE *,'      7 TRACK TAPE DENSITY IS 800 BPI'
         AIF   (&TAVR EQ 800).BA        TEST IF TAVR SPECD TO OTHER
    MNOTE 0,'* * * IEISCHW01 VLMOUNT WAS OMITTED SO TAVR VALUE &TAVR IS*
                             IGNORED'
         AGO   .BA
         AIF   ('&VLMOUNT' NE 'AVR').V7
.B2A     ANOP
&SGSCHDB(14)   SETB 1                   VLMOUNT=AVR
    MNOTE *,'       AUTOMATIC VOLUME RECOGNITION VOLUME MOUNTING'
    MNOTE *,'        PROCEDURE'
.* * * TAVR * * *                       * * * TAVR * * *          * * *
.B4      AIF   ('&TAVR' EQ '200').B8    TAVR=200
         AIF   ('&TAVR' NE '556').B6
&SGSCHDB(23)   SETB 1                   TAVR=556
         AGO   .B8
.B6      AIF   ('&TAVR' NE '800').V8    TAVR SPECIFICATION INVALID
&SGSCHDB(24)   SETB 1                   TAVR=800 OR TAVR= DEFAULTED
.B8 MNOTE *,'       TAPE DENSITY FOR SEVEN TRACK TAPES IS &TAVR'
.* * * CONOPTS * * *               * * * CONOPTS * * *           * * *
.BA  AIF  (T'&CONOPTS(1) EQ 'O' OR '&CONOPTS(1)' EQ 'NOMCS').BA0
       AIF  ('&CONOPTS(1)' NE 'MCS').YC
&C     SETC ' '
       AGO  .BA00
.BA0   ANOP
&C     SETC ' NOT '
.BA00  MNOTE *,'       MULTIPLE CONSOLE SUPPORT WILL&C.BE INCLUDED'
       MNOTE *,'        IN THE GENERATED SYSTEM'
&SGSCHDB(34) SETB ('&CONOPTS(1)' EQ 'MCS')
       AIF  (NOT &SGSCHDB(34)).BB2
.BA1   AIF  (T'&CONOPTS(2) EQ 'O' OR '&CONOPTS(2)' EQ 'NOEXIT').BA2
       AIF  ('&CONOPTS(2)' NE 'EXIT').YD
&C     SETC ' '
       AGO  .BA3
.BA2   ANOP
&C     SETC ' NOT '
.BA3   MNOTE *,'       CONTROL WILL&C.BE EXITED BEFORE THE ROUTING'
       MNOTE *,'        OF WTOR MESSAGES'
&SGSCHDB(35) SETB ('&CONOPTS(2)' EQ 'EXIT')
.*** HARDCPY ***             *** HARDCPY ***            *** HARDCPY ***
.BB1 AIF (T'&HARDCPY(1) EQ 'O').M01    OMITTED
 AIF (N'&HARDCPY GT 3).Y31    TWO MANY PARAMETERS
&C SETC ' '
 AGO .M02
.M01 ANOP
&C SETC ' NOT '
.M02 MNOTE *,'       HARD COPY LOG WILL&C.BE IN THE SYSTEM'
 AIF (T'&HARDCPY(1) EQ 'O').BAA        OMITTED
 AIF ('&HARDCPY(1)' NE 'SYSLOG' AND K'&HARDCPY(1) NE 3).Z4
 AIF ('&HARDCPY(1)' EQ 'SYSLOG').M03
&SGSCHDC(14) SETC '&HARDCPY(1)'
 MNOTE *,'       OUTPUT DEVICE &HARDCPY(1) TO BE HARD COPY LOG'
 AGO .M2
.M03 MNOTE *,'       SYSTEM LOG TO BE HARD COPY LOG'
&SGSCHDB(36) SETB 1
.M2 AIF (N'&HARDCPY LT 2).M32
    AIF (T'&HARDCPY(2) EQ 'O' OR '&HARDCPY(2)' EQ 'ALL').M32
 MNOTE *,'       WTOR MESSAGES WITH THE FOLLOWING ROUTING CODES'
 MNOTE *,'        WILL BE PRINTED ON THE HARD COPY LOG'
&F SETA 2          FIRST DIGIT OF NUMBER
&G SETA 0          COUNT OF DIGITS
&H SETA 2          SEARCH FOR , OR )
&C SETC '&HARDCPY(2)'(1,1)
 AIF ('&C' EQ '(').M21A
&G SETA 25         INDICATES ONE VALUE ONLY
&D SETC '&HARDCPY(2)'
 AIF ('&D' LT '1' OR '&D' GT '16').Z6
 AGO .M21D
.M21A ANOP
&C SETC '&HARDCPY(2)'(&H,1)       LOOK FOR ) OR ,
 AIF ('&C' EQ '(').Z66
 AIF ('&C' EQ ')' OR '&C' EQ ',').M21B
&H SETA &H+1
&G SETA &G+1
 AGO .M21A
.M21B AIF (&G EQ 0 AND &F EQ 2).M32
 AIF (&G EQ 0).M21E
&D SETC '&HARDCPY(2)'(&F,&G)
&F SETA &F+&G+1
&G SETA 0
&H SETA &H+1
 AIF ('&D' LT '1' OR '&D' GT '16').Z6
.M21D AIF (&SGSCHHB(&D)).Z65
&SGSCHHB(&D) SETB 1
 MNOTE *,'               &D'
.M31 AIF (&G EQ 25 OR '&C' EQ ')').M345
 AGO .M21A
.M21E ANOP
&F SETA &F+1
&H SETA &H+1
 AGO .M21A
.M32 MNOTE *,'       ALL WTOR MESSAGES WILL BE PRINTED ON THE'
 MNOTE *,'        HARD COPY LOG'
&E SETA 1
.M33 ANOP
&SGSCHHB(&E) SETB 1
 AIF (&E EQ 16).M345
&E SETA &E+1
 AGO .M33
.M345    AIF   (N'&HARDCPY NE 3).M39
         AIF ('&HARDCPY(3)' EQ 'NOCMDS').M39
      AIF ('&HARDCPY(3)' NE 'CMDS').M37
&SGSCHDB(37) SETB 1
      AGO .M40
.M37  AIF ('&HARDCPY(3)' NE 'INCMDS').M38
&SGSCHDB(42) SETB 1
    MNOTE *,'       STATUS DISPLAYS WILL NOT BE PRINTED ON'
    MNOTE *,'        THE HARD COPY LOG'
      AGO .M40
.M38  AIF ('&HARDCPY(3)' NE 'STCMDS').M42
&SGSCHDB(43) SETB 1
    MNOTE *,'       STATIC STATUS DISPLAYS WILL BE PRINTED ON'
    MNOTE *,'        THE HARD COPY LOG'
      AGO .M40
.M39 ANOP
&C SETC ' NOT '
.M40 MNOTE *,'       COMMANDS AND RESPONSES WILL&C.BE PRINTED ON'
     MNOTE *,'        THE HARD COPY LOG'
      AGO .BAA
.M42 MNOTE 5,'* * * IEISCH070 HARDCOPY VALUE &HARDCPY(3) INVALID'
&SGQUITB SETB 1
 AGO .BAA
.BB2 AIF (T'&HARDCPY NE 'O').Z3
.*** CONSOLE ***             *** CONSOLE ***           *** CONSOLE ***
.BAA     AIF   (T'&CONSOLE EQ 'O').V9   CONSOLE OMITTED
         AIF   (N'&CONSOLE GT 4).VA  MAXIMUM OF 4 ADDRESSES
&H SETA 0
&G       SETA  1                   C-BOX POINTER 1ST CONSOLE
.BC      ANOP
&A       SETA  &A+1                SUBLIST POINTER
         AIF   (K'&CONSOLE(&A) EQ 5).BD COMPOSITE CONSOLE
         AIF   (K'&CONSOLE(&A) NE 3).VA SINGLE CONSOLE
&SGSCHDC(&G) SETC '&CONSOLE(&A).&CONSOLE(&A)'    ADDR-ADDR
    MNOTE *,'       CONSOLE ADDRESS &CONSOLE(&A)'
         AGO   .C0
.BD      ANOP                      COMPOSITE CONSOLE CHECK
&C       SETC  '&CONSOLE(&A)'(3,3)
&D       SETC  '&CONSOLE(&A)'(1,2)
         AIF   ('&D' NE 'I-').BE
         AIF   (&B(1)).VA0         I-ADDR TWICE IN SUCCESSION
&B(1)    SETB  1
&SGSCHDC(&G) SETC '&C&SGSCHDC(&G)'  I-ADDR
    MNOTE *,'       COMPOSITE CONSOLE WITH FOLLOWING ADDRESSES--'
&H       SETA  &H+1                COMPOSITE CONSOLE COUNTER
    MNOTE *,'                &C-INPUT'
&A       SETA  &A+1                INCREMENT SUBLIST POINTER
&B(2)    SETB  0
         AGO   .BD
.BE      AIF   ('&D' NE 'O-').VA0
         AIF   (NOT &B(1) OR &B(2)).VA0 I-ADDR NOT PREVIOUSLY SPECD
&B(2)    SETB  1
&SGSCHDC(&G)  SETC  '&SGSCHDC(&G)&C'   O-ADDR
    MNOTE *,'                &C-OUTPUT'
&B(1)    SETB  0
.C0      AIF   (&A EQ N'&CONSOLE).C1
         AIF   (&G EQ 12).VA
         AIF   (NOT &SGSCHDB(33)).Z7    1 PRIMARY CONSOLE FOR MFT/MVT
         AIF  (&SGSCHDB(34)).Z7A
&G       SETA  12
         AGO   .BC
.C1   AIF  (&SGSCHDB(33) AND NOT &SGSCHDB(34) AND (&G NE 12)).Z8
.* * * ALTCONS * * *                    * * * ALTCONS * * *       * * *
.C2    AIF  (T'&ALTCONS EQ 'O' AND (&SGSCHDB(34))).YE  -- WITH MCS
         AIF   (T'&ALTCONS EQ 'O').C9   ALTCONS OMITTED
         AIF   (N'&ALTCONS GT 4).VB
&A       SETA  0                   SUBLIST POINTER
&G       SETA  2                   ALTCONS 1ST GLOBAL POINTER
.C4      ANOP
&A       SETA  &A+1
         AIF   (K'&ALTCONS(&A) EQ 5).C6 COMPOSITE ALTCONS
         AIF   (K'&ALTCONS(&A) NE 3).VB SINGLE ALTCONS
    AIF  ('&ALTCONS(&A)' EQ 'FFF').VB    INVALID ADDR
&SGSCHDC(&G) SETC '&ALTCONS(&A).&ALTCONS(&A)'    ADDR-ADDR
    MNOTE *,'       ALTCONS ADDRESS &ALTCONS(&A)'
         AGO   .C8
.C6      ANOP
&C       SETC  '&ALTCONS(&A)'(3,3)
&D       SETC  '&ALTCONS(&A)'(1,2)
         AIF   ('&D' NE 'I-').C7
     AIF  ('&C' EQ 'FFF').VB    INVALID ADDR
         AIF   (&B(3)).VB0         I-ADDR PREVIOUSLY - NO O-ADDR
&B(3)    SETB  1
&SGSCHDC(&G) SETC '&C&SGSCHDC(&G)'      I-ADDR
    MNOTE *,'       COMPOSITE ALTERNATE CONSOLE WITH FOLLOWING ADDRESSE*
               S--'
&H       SETA  &H+1                COMPOSITE COUNTER INCREMENTED
    MNOTE *,'                &C-INPUT'
&A       SETA  &A+1
&B(4)    SETB  0
         AGO   .C6
.C7      AIF   ('&D' NE 'O-').VB0
     AIF  ('&C' EQ 'FFF').VB0   INVALID ADDR
         AIF   ((NOT &B(3)) OR &B(4)).VB0 NO I-ADDR SPECIFIED
&B(4)    SETB  1
&SGSCHDC(&G) SETC '&SGSCHDC(&G).&C'     O-ADDR
    MNOTE *,'                &C-OUTPUT'
&B(3)    SETB  0
.C8      AIF   (&A EQ N'&ALTCONS).C85
         AIF   (NOT &SGSCHDB(33)).Z9
         AIF   (&G EQ 13).VB
&G       SETA  13
         AGO   .C4
.C85     AIF   (&G EQ 13).CD
         AGO   .CB
.C9      AIF   (&SGSCHDB(33)).CA
&SGSCHDC(2)  SETC '&SGSCHDC(1)'
         AGO   .CE
.CA      ANOP
&SGSCHDC(2)  SETC  'FFFFFF'
.CB      AIF   (NOT &SGSCHDB(33)).CD
&SGSCHDC(13)  SETC  'FFFFFF'
.CD      AIF   (&H GT 2).ZA        MORE THAN 2 COMPOSITE CONS. INVALID
.* * * JOBQRES * * *                    * * * JOBQRES * * *       * * *
.CE      AIF   (T'&JOBQRES EQ 'O').D0
         AIF   (K'&JOBQRES NE 3).VF
&SGSCHDC(9)    SETC '&JOBQRES'
    MNOTE *,'       JOB QUEUE RESIDENCE DEVICE ADDRESS IS &JOBQRES'
.* * * STARTR * * *                     * * * STARTR * * *        * * *
.D0      AIF   (T'&STARTR EQ 'O').D8    STARTR OMITTED
&SGSCHDB(16)   SETB 1                   STARTR WAS SPECIFIED
    MNOTE *,'       START READER COMMAND TO BE EXECUTED AUTOMATI-'
    MNOTE *,'        CALLY WHEN SYSTEM IS IPL''D--'
&A       SETA  0
.D2      AIF   (&A EQ N'&STARTR).DA     VALUES EXHAUSTED
&A       SETA  &A+1                     INCREMENT POINTER
&C       SETC  '&STARTR(&A)'(3,K'&STARTR(&A)-2)
&D       SETC  '&STARTR(&A)'(1,2)
         AIF   ('&D' NE 'A-').D4
         AIF   (&B(5)).W0               A-ADDR REDUNDANT
&B(5)    SETB  1                        A-ADDR SPECIFIED
         AIF   (K'&STARTR(&A) NE 5).W0
&SGSCHDC(3)    SETC '&C'                A-ADDR VALID
    MNOTE *,'            DEVICE ADDRESS - &C'
         AGO   .D2                      RETURN
.D4      AIF   ('&D' NE 'V-').D6
         AIF   (&B(6)).W0               V-SERIAL REDUNDANT
&B(6)    SETB  1                        V-SERIAL SPECIFIED
         AIF   (K'&STARTR(&A) GT 8).W0
&SGSCHDC(7)    SETC '&C'                V-SERIAL VALID
    MNOTE *,'            VOLUME SERIAL NUMBER - &C'
         AGO   .D2                      RETURN
.D6      AIF   ('&D' NE 'D-').W0
         AIF   (&B(7)).W0               D-NAME REDUNCANT
&B(7)    SETB  1                        D-SPECIFIED
         AIF   (K'&STARTR(&A) GT 10).W0
&SGSCHDC(4)    SETC '&C'                D-NAME VALID
    MNOTE *,'            READER DATA SET NAME IS &C'
         AGO   .D2                      RETURN
.D8      ANOP                           STARTR OMITTED
&SGSCHDC(3)    SETC '   '               SET BLANKS FOR ADDRESS
         AGO   .DC
.DA      AIF   (NOT &B(5)).W2
.* * * STARTW * * *                     * * * STARTW * * *        * * *
.DC      AIF   (T'&STARTW EQ 'O').E6    STARTW OMITTED
&SGSCHDB(17)   SETB 1                   STARTW SPECIFIED
    MNOTE *,'       START WRITER COMMAND TO BE EXECUTED AUTOMATI-'
    MNOTE *,'        CALLY WHEN SYSTEM IS IPL''D--'
&A       SETA  0
.DE      AIF   (&A EQ N'&STARTW).E8     VALUES EXHAUSTED
&A       SETA  &A+1                     INCREMENT POINTER
&C       SETC  '&STARTW(&A)'(3,K'&STARTW(&A)-2)
&D       SETC  '&STARTW(&A)'(1,2)
.E0      AIF   ('&D' NE 'A-').E2
         AIF   (&B(8)).W3               A-ADDR REDUNDANT
&B(8)    SETB  1
         AIF   (K'&STARTW(&A) NE 5).W3
&SGSCHDC(3)    SETC '&SGSCHDC(3)&C'     STARTR-ADDR - STARTW-ADDR
    MNOTE *,'            DEVICE ADDRESS - &C'
         AGO   .DE                      RETURN
.E2      AIF   ('&D' NE 'V-').E4
         AIF   (&B(9)).W3               V-SERIAL REDUNDANT
&B(9)    SETB  1                        V-SERIAL SPECIFIED
         AIF   (K'&STARTW(&A) GT 8).W3
&SGSCHDC(8)    SETC '&C'                V-SERIAL VALID
    MNOTE *,'            VOLUME SERIAL NUMBER - &C'
         AGO   .DE                      RETURN
.E4      AIF   ('&D' NE 'D-').W3
         AIF   (&B(10)).W3              D-NAME REDUNDANT
&B(10)   SETB  1                        D- SPECIFIED
         AIF   (K'&STARTW(&A) GT 10).W3
&SGSCHDC(5)    SETC '&C'                D-NAME VALID
    MNOTE *,'            WRITER DATA SET NAME IS &C'
         AGO   .DE                      RETURN
.E6      ANOP
&SGSCHDC(3)    SETC '&SGSCHDC(3)   '    SET BLANKS FOR ADDRESS
         AGO   .F0                     * ACCTRTN *
.E8      AIF   (NOT &B(8)).W5
.* * * ACCTRTN * * *                    * * * ACCTRTN * * *       * * *
.F0      AIF   (T'&ACCTRTN EQ 'O').F8   DEFAULT IS NOTSUPPLIED
         AIF   ('&ACCTRTN' EQ 'NOTSUPPLIED').F8
.F4 AIF (('&ACCTRTN' NE 'SUPPLIED')AND('&ACCTRTN' NE 'BASIC')).F6
&SGSCHDB(6)    SETB 1
    MNOTE *,'       A BASIC INSTALLATION ACCOUNT ROUTINE IS PROVIDED'
        AGO  .F8
.F6  AIF ('&ACCTRTN' NE 'SMF').W9
&SGSCHDB(7) SETB 1
    MNOTE *,'       SMF ACCOUNTING SUPPORT IS PROVIDED'
.* * * REPLY * * *                      * * * REPLY * * *         * * *
.F8      AIF   (T'&REPLY EQ 'O').FA
         AIF   (T'&REPLY NE 'N').WB
&SGSCHDA(1)    SETA &REPLY              REPLY VALID
    MNOTE *,'       MAXIMUM NUMBER OF OUTSTANDING REPLY REQUESTS'
    MNOTE *,'        IS &REPLY'
.* * * WTOBFRS * * *                    * * * WTOBFRS * * *       * * *
.FA      AIF   (T'&WTOBFRS EQ 'O').FC   WTOBFRS OMITTED
         AIF   (T'&WTOBFRS NE 'N').WD
&SGSCHDA(2)    SETA &WTOBFRS            WTOBFRS VALID
    MNOTE *,'       MAXIMUM NUMBER OF BUFFERS FOR WTO MESSAGES'
    MNOTE *,'        IS &WTOBFRS'
.* * * STARTI * * *                     * * * STARTI * * *        * * *
.FC      AIF   ('&STARTI' NE 'MANUAL' AND T'&STARTI NE 'O').G0
.FE MNOTE *,'       START INITIATOR COMMAND TO BE EXECUTED MANUALLY'
         AGO   .G2
.G0      AIF   ('&STARTI' NE 'AUTO').WF
&SGSCHDB(22)   SETB 1                   STARTI=AUTO
    MNOTE *,'       START INITIATOR COMMAND TO BE EXECUTED AUTOMATICALL*
               Y'
.* * * PROCRES * * *                    * * * PROCRES * * *       * * *
.G2      AIF   (T'&PROCRES EQ 'O').G4   PROCRES OMITTED
         AIF   (K'&PROCRES NE 3).X1
&SGSCHDC(10)   SETC '&PROCRES'          PROCRES VALID
    MNOTE *,'       PROCEDURE LIBRARY DEVICE ADDRESS IS &PROCRES'
.* * * WTLCLSS * * *                    * * * WTLCLSS * * *       * * *
.G4      AIF   (T'&WTLCLSS NE 'O').G8
&SGSCHDC(11)   SETC 'L'                 WTLCLSS OMITTED - DEFAULT IS L
         AGO   .GA
.G8      AIF   (K'&WTLCLSS NE 1).X3
&SGSCHDC(11)   SETC '&WTLCLSS'          WTLCLSS VALID
.GA MNOTE *,'       DEFAULT CLASSNAME FOR SYSTEM LOG SYSOUT IS &SGSCHDC*
               (11)'
.H4      AIF   (T'&JOBQFMT NE 'O').H6   OMITTED - CONTINUE
&SGSCHDA(4)    SETA 12                  DEFAULT IS 12
         AGO   .H8
.H6      AIF   ('&JOBQFMT' LT '10' OR '&JOBQFMT' GT '255').X6
&SGSCHDA(4)    SETA &JOBQFMT            JOBQFMT VALID
.H8 MNOTE *,'       NUMBER OF RECORDS PER LOGICAL TRACK FOR JOB'
    MNOTE *,'        QUEUE IS &SGSCHDA(4)'
.* * * JOBQWTP * * *                    * * * JOBQWTP * * *
.HB      AIF   (T'&JOBQWTP NE 'O').HB1
&SGSCHDA(12)   SETA 2                   JOBQWTP OMITTED--DEFAULT IS 2
         AGO   .HB2
.HB1     AIF   (T'&JOBQWTP NE 'N').X7A
         AIF   (&JOBQWTP GT 20).X7A
&SGSCHDA(12)   SETA &JOBQWTP            JOBQWTP VALID
.HB2 MNOTE *,'       MAXIMUM NUMBER OF RECORDS FOR WRITE TO'
     MNOTE *,'        PROGRAMMER IS &SGSCHDA(12)'
.* * * JOBQLMT * * *                    * * * JOBQLMT * * *       * * *
.HC      AIF   (T'&JOBQLMT NE 'O').HE
&SGSCHDA(5)    SETA 60                  DEFAULT IS 60
         AGO   .I0
.HE      AIF   (&JOBQLMT GT 9999).X8
&SGSCHDA(5)    SETA &JOBQLMT            JOBQLMT VALID
.I0 MNOTE *,'       THRESHOLD VALUE FOR JOBQUE OVERFLOW IS &SGSCHDA(5)'
.* * * JOBQTMT * * *                    * * * JOBQTMT * * *       * * *
.I4      AIF   (T'&JOBQTMT NE 'O').I6
&SGSCHDA(6)    SETA 60                  DEFAULT IS 60
         AGO   .I8
.I6      AIF   (&JOBQTMT GT 9999).XA
&SGSCHDA(6)    SETA &JOBQTMT            JOBQTMT VALID
.I8 MNOTE *,'       NUMBER OF RECORDS RESERVED FOR JOB TERMINATION'
    MNOTE *,'        IS &SGSCHDA(6)'
.* * * INITQBF * * *                    * * * INITQBF * * *       * * *
.J0      AIF   (T'&INITQBF EQ 'O').J4   OMITTED
         AIF   (&SGSCHDB(2)).XF
         AIF   (T'&INITQBF NE 'N').XE   INITQBF NOT A NUMBER
         AIF   (&INITQBF GT 255).XE
&SGSCHDA(8)    SETA &INITQBF            INITQBF VALID
    MNOTE *,'       NUMBER OF 1K BLOCKS TO BE USED AS BUFFERS FOR'
    MNOTE *,'        JOB QUEUE IS &SGSCHDA(8)'
.* * * MINPART * * *                    * * * MINPART             * * *
.J4      AIF   (T'&MINPART NE 'O').J6   MINPART OMITTED
 AIF ('&TYPE' EQ 'MFT' AND T'&DESIGN NE 'O').J5
&SGSCHDA(9) SETA 30
 AIF ('&TYPE' EQ 'MFT').J8
 AIF (T'&INITQBF EQ 'O').J5A
&SGSCHDA(9) SETA 52+&INITQBF
 AGO .J8
.J5A  ANOP
&SGSCHDA(9)    SETA 52                  DEFAULT IS 52
         AGO   .J8
.J5 ANOP
&C SETC '&DESIGN'(1,K'&DESIGN-1)
&SGSCHDA(9) SETA &C
 AGO .J8
.J6      AIF (T'&MINPART NE 'N').Y0     MINPART NOT A NUMBER
 AIF ('&TYPE' EQ 'MFT' AND T'&DESIGN NE 'O').J7
&SGSCHDA(9) SETA 30
 AIF ('&TYPE' EQ 'MFT').J71
         AIF   (&MINPART LT &SGSCHDA(8)+52).Y0
&SGSCHDA(9)    SETA &MINPART            MINPART VALID
 AGO .J8
.J7 ANOP
&C SETC '&DESIGN'(1,K'&DESIGN-1)
&SGSCHDA(9) SETA &C
.J71 AIF (&MINPART LT &SGSCHDA(9)).J7A
&SGSCHDA(9) SETA &MINPART
 AGO .J8
.J7A MNOTE 0,'* * * IEISCH200 MINPART VALUE DEFAULTED TO THE SIZE OF TH*
               E SCHEDULR'
.J8      ANOP
&SGSCHDA(9) SETA ((&SGSCHDA(9)+1)/2*2)
    MNOTE *,'     NUMBER OF 1K BLOCKS REQUIRED TO INITIATE A'
    MNOTE *,'        JOB IS &SGSCHDA(9)'
.* * * OPTIONS * * *                    * * * OPTIONS * * *       * * *
.JC      AIF   (T'&OPTIONS EQ 'O').K2A   OPTIONS OMITTED
    MNOTE *,'       SCHEDULR OPTIONS AVAILABLE--'
&A       SETA  0
.JE      AIF   (&A EQ N'&OPTIONS).K1AA    VALUES EXHAUSTED
&A       SETA  &A+1                     INCREMENT POINTER
         AIF   ('&OPTIONS(&A)' EQ 'NOLOG').K0B
         AIF   ('&OPTIONS(&A)' NE 'LOG').K0
     MNOTE *,'           LOG FUNCTION IN SYSTEM'
&SGSCHDB(38) SETB 1
     AGO  .JE
.K0B MNOTE *,'           NOLOG SPECIFIED IN SYSTEM'
&NOLOG  SETB  1
     AGO  .JE
.K0      AIF   ('&OPTIONS(&A)' NE 'RJE').K1
&SGSCHDB(26)   SETB 1
    MNOTE *,'            REMOTE JOB ENTRY'
         AGO   .JE
.K1      AIF   ('&OPTIONS(&A)' NE 'CRJE').KOC
&SGSCHDB(8)  SETB  1
   MNOTE *,'           CONVERSATIONAL REMOTE JOB ENTRY'
    AGO   .JE
.KOC     AIF   ('&OPTIONS(&A)' NE 'TSO').Y2 TEST IF THIS OPTION IS TSO
&SGSCHDB(44)  SETB   1       SET TSO SWITCH
   MNOTE *,'       TIME SHARING OPTION'
         AIF   (T'&SUBMITQ EQ 'O').SUBQD
         AIF   (T'&SUBMITQ NE 'N').SUBQE
&SGMODA(1) SETA &SUBMITQ
         AIF   (&SUBMITQ GE 0 AND &SUBMITQ LE 9999).SUBQM
.SUBQE MNOTE 7,'* * * IEISCH001 SUBMITQ VALUE &SUBMITQ INVALID'
&SGQUITB SETB  1
         AGO   .TSOMVT
.SUBQD MNOTE *,'  DEFAULT VALUE FOR'
&SGMODA(1) SETA 50
.SUBQM MNOTE *,'  MAXIMUM NO. OF TRACKS FOR ''FIB'' JOBS IS &SGMODA(1)'
.TSOMVT  AIF   ('&TYPE' EQ 'MFT').Y3E        MFT?
         AGO   .JE
.K1AA AIF (&NOLOG AND &SGSCHDB(38)).Y3C
.K2A     AIF   (&NOLOG OR &SGSCHDB(38)).GE
&SGSCHDB(38) SETB 1          LOG DEFAULT
 MNOTE *,'       LOG FUNCTION DEFAULTED'
.*** WTLBFRS ***             *** WTLBFRS ***            *** WTLBFRS ***
.GE AIF (T'&WTLBFRS NE 'O').H0
 AIF (&NOLOG).LOGTEST        NO WTLBFRS NEEDED WITH NOLOG
&SGSCHDA(3) SETA 4
 AGO .H2
.H0 AIF (T'&WTLBFRS NE 'N').X5
 AIF (&WTLBFRS EQ 0 AND &NOLOG).H1
 AIF (&WTLBFRS GE 4 AND NOT &NOLOG).H1
 AIF (&NOLOG).Y3D
&SGSCHDA(3) SETA 4           DEFAULT WHEN LOG AND WTLBFRS=0,1,2,3
 AGO .H2
.H1 ANOP
&SGSCHDA(3) SETA &WTLBFRS              VALID
.H2 MNOTE *,'       MAXIMUM NUMBER OF BUFFERS FOR WRITE TO LOG'
 MNOTE *,'        MESSAGES IS &SGSCHDA(3)'
.LOGTEST AIF (&SGSCHDB(36) AND NOT &SGSCHDB(38)).Y3B
.L6  ANOP
.*** IF MCS NOT SPECIFIED, IGNORE ROUTCDE AND OLDWTOR
.LA    AIF  (NOT &SGSCHDB(34)).M7
.* * * ROUTCDE * * *               * * * ROUTCDE * * *           * * *
       MNOTE *,'       ROUTING CODES ASSIGNED TO MASTER CONSOLE ARE--'
       MNOTE *,'             1'
       MNOTE *,'             2'
       AIF  (T'&ROUTCDE EQ 'O').LD          OMITTED
&E     SETA 1
.LB    AIF  (N'&ROUTCDE LT &E).LD           EXHAUSTED
       AIF  (T'&ROUTCDE(&E) NE 'N').Z0      NOT NUMERIC
       AIF  ('&ROUTCDE(&E)' LT '1' OR '&ROUTCDE(&E)' GT '16').Z0
       AIF  (&SGSCHRB(&ROUTCDE(&E))).Z05    DUPLICATED
       AIF   ('&ROUTCDE(&E)' LT '3').LC
       MNOTE *,'             &ROUTCDE(&E)'
&SGSCHRB(&ROUTCDE(&E)) SETB 1
.LC    ANOP
&E     SETA &E+1
       AGO  .LB
.* * * OLDWTOR * * *               * * * OLDWTOR * * *           * * *
.LD    AIF  (T'&OLDWTOR NE 'O').LD0         NOT OMITTED
       MNOTE *,'       MASTER CONSOLE WILL RECEIVE ALL WTOR MESSAGES'
       MNOTE *,'        NOT ASSIGNED ANY ROUTING CODE'
         AGO   .R1
.LD0   MNOTE *,'       WTOR MESSAGES NOT ASSIGNED ANY ROUTING CODE'
       MNOTE *,'        WILL HAVE THE FOLLOWING CODES--'
.LD1   ANOP
&E     SETA 1
.LE      AIF   (N'&OLDWTOR LT &E).R1
       AIF  (T'&OLDWTOR(&E) NE 'N').Z2      NOT NUMERIC
       AIF  ('&OLDWTOR(&E)' LT '1' OR '&OLDWTOR(&E)' GT '16').Z2
       AIF  (&SGSCHOB(&OLDWTOR(&E))).Z25    DUPLICATED
&SGSCHOB(&OLDWTOR(&E)) SETB 1
       MNOTE *,'             &OLDWTOR(&E)'
.LF    ANOP
&E     SETA &E+1
       AGO  .LE
.* * * NOMCS PARAMETER TESTS * * *         * * *           * * *
.M7 AIF (T'&ROUTCDE NE 'O').YF
.M8 AIF (T'&OLDWTOR NE 'O').Z1
.* * * ESV * * *               * * * ESV * * *          * * * ESV * * *
.R1 AIF (T'&ESV NE 'O').R1B
 MNOTE *,'       ERROR STATISTICS BY VOLUME IS DEFAULTED TO CON'
&SGSCHDB(15) SETB 1
.R1B ANOP
&SGSCHDB(31) SETB ('&ESV' EQ 'CON' OR T'&ESV EQ 'O')
&SGSCHDB(7) SETB (&SGSCHDB(7) OR '&ESV' EQ 'SMF')
.RY AIF ('&ESV' NE 'NO' AND NOT (&SGSCHDB(31) OR &SGSCHDB(7))).ZE
 AIF (T'&ESV EQ 'O').R2
 MNOTE *,'       ERROR STATISTICS BY VOLUME SPECIFIED IS &ESV'
.RZ AIF ('&ESV' EQ 'NO').R2
&SGSCHDB(15) SETB 1
 AIF (NOT &SGSCHDB(7)).R2
 AIF (NOT &SGSCHDB(6)).R2
&SGSCHDB(6) SETB 0
 MNOTE 0,'* * * IEISCH002 ACCTRTN=SMF DEFAULTED IN SUPPORT OF ESV=SMF'
.* * * EVA * * *                   * * * EVA * * *      * * * EVA * * *
.R2 AIF ('&EVA' EQ 'NO').S
 AIF (N'&EVA NE 2).ZF
 AIF ('&EVA(1)' LE '0' OR '&EVA(2)' LE '0').ZF
 AIF ('&EVA(1)' GT '255' OR '&EVA(2)' GT '255').ZF
&E SETA 1
.EVA ANOP
&A SETA 1
.EVA1 AIF (&A GT K'&EVA(&E)).EVA2
&C SETC '&EVA(&E)'(&A,1)
 AIF (NOT('&C' GE '0' AND '&C' LE '9')).ZF
&A SETA &A+1
 AGO .EVA1
.EVA2 AIF (&E EQ 2).EVA3
&E SETA &E+1
 AGO .EVA
.EVA3 ANOP
&A SETA K'&EVA(1)
 AIF (K'&EVA(1) EQ 3).EVA4
&C SETC '000'(1,3-&A).'&EVA(1)'
 AGO .EVA5
.EVA4 ANOP
&C SETC '&EVA(1)'
.EVA5 ANOP
&A SETA K'&EVA(2)
 AIF (K'&EVA(2) EQ 3).EVA6                                      SA65457
&D SETC '000'(1,3-&A).'&EVA(2)'
 AGO .EVA7
.EVA6 ANOP
&D SETC '&EVA(2)'
.EVA7 ANOP
&SGSCHDC(15) SETC '&C.&D'
 MNOTE *,'       ERROR VOLUME ANALYSIS SPECIFIED'
 MNOTE *,'        READ ERROR THRESHOLD IS &C'
 MNOTE *,'        WRITE ERROR THRESHOLD IS &D'
.* * * AREA * * *       * * * AREA * * *          * * * AREA * * *
.S       AIF   (T'&AREA EQ 'O').N            OMITTED
         AIF   (NOT &SGSCHDB(34)).ZG         MCS NOT SPECIFIED
         AIF   (N'&AREA GT 11).ZH            TOO MANY PARAMETERS
.S2      ANOP
         MNOTE *,'       STATUS DISPLAY AREAS FOR MASTER CONSOLE HAVE'
         MNOTE *,'        LENGTHS OF--'
&A       SETA  1
.S4      AIF   (&A GT N'&AREA).N             PARAMETERS EXHAUSTED
         AIF   (T'&AREA(&A) NE 'N').ZI       NOT NUMERIC
         AIF   ('&AREA(&A)' LT '4' OR '&AREA(&A)' GT '47').ZI
         AIF ('&A' GT '11').S5               BR IF TOO MANY PARAMETERS
&SGSCHAA(12) SETA &SGSCHAA(12)+&AREA(&A)
&SGSCHAA(&A) SETA &AREA(&A)
.S5      ANOP
         MNOTE *,'           &AREA(&A)'
.S6      ANOP
&A       SETA  &A+1
         AGO   .S4
.* * * PFK * * *             * * * PFK * * *            * * * PFK * * *
.N       AIF (T'&PFK EQ 'O').T               OMITTED
         AIF   (NOT &SGSCHDB(34)).ZK         MCS NOT SPECIFIED
         AIF (T'&PFK NE 'N').ZJ              NOT NUMERIC
         AIF ('&PFK' GT '12').ZJ             12 MAXIMUM
&SGSCHDA(16) SETA &PFK
&SGSCHDB(30) SETB 1
         MNOTE *,'       MASTER CONSOLE WILL HAVE &PFK PROGRAM'
         MNOTE *,'        FUNCTION KEYS'
.* * * OPSTRAN * * *      * * * OPSTRAN * * *       * * * OPSTRAN * * *
.T       AIF   (T'&OPSTRAN EQ 'O').U         OMITTED
         AIF   (NOT &SGSCHDB(34)).ZM         MCS NOT SPECIFIED
         AIF   (T'&OPSTRAN NE 'N').ZL        NOT NUMERIC
         AIF   ('&OPSTRAN' GT '64').ZL       INVALID
         AIF   (N'&CONSOLE GT 1).T1          COMPOSITE CONSOLE
    MNOTE *,'             MASTER CONSOLE ASSIGNED TO TRANSIENT GROUP &O*
               PSTRAN'
&SGTGRIA(32) SETA &OPSTRAN                   I/O CONSOLE - SAVED ONLY
         AGO   .U                             IN INPUT GLOBAL
.T1 MNOTE *,'             OUTPUT HALF OF MASTER CONSOLE ASSIGNED TO'
    MNOTE *,'              TRANSIENT GROUP &OPSTRAN'
&SGTGROA(32) SETA &OPSTRAN
.* * * OPSTRNI * * *      * * * OPSTRNI * * *       * * * OPSTRNI * * *
.U       AIF   (T'&OPSTRNI EQ 'O').V         OMITTED
         AIF   (NOT &SGSCHDB(34)).ZN         MCS NOT SPECIFIED
         AIF   (N'&CONSOLE NE 2).ZN          NOT COMPOSITE CONSOLE
         AIF   (T'&OPSTRNI NE 'N').ZO        NOT NUMERIC
         AIF   ('&OPSTRNI' GT '64').ZO       INVALID
         AIF   ('&OPSTRAN' EQ '&OPSTRNI').ZP
.U0 MNOTE *,'             INPUT HALF OF MASTER CONSOLE ASSIGNED TO'
    MNOTE *,'              TRANSIENT GROUP &OPSTRNI'
&SGTGRIA(32) SETA &OPSTRNI
.V       MEXIT
.**********************************************************************
.*                                                                    *
.*                               ERROR MESSAGES                       *
.*                                                                    *
.**********************************************************************
.V1 MNOTE 5,'* * * IEISCH003 SCHEDULR MACRO PREVIOUSLY USED'
&SGQUITB SETB  1
         AGO   .MEND          NO PROCESSING
.V2 MNOTE 5,'* * * JEISCH004 TYPE VALUE &TYPE INVALID'
&SGQUITB SETB  1
         AGO   .MEND          NO MORE PROCESSING
.V3 MNOTE 5,'* * * IEISCH005  DESIGN IS INVALID FOR &TYPE SCHEDULR'
&SGQUITB SETB  1
         AGO   .B2                 * VLMOUNT *
.V4 MNOTE 5,'* * * IEISCH006 DESIGN VALUE &DESIGN INVALID'
&SGQUITB SETB  1
         AGO   .B2                 * VLMOUNT *
.V6      ANOP            * * * * * FREE MESSAGE LABEL * * * * *
.V7 MNOTE 5,'* * * IEISCH007 VLMOUNT VALUE &VLMOUNT INVALID'
&SGQUITB SETB  1
         AGO   .BA                 * CONSOLE *
.V8 MNOTE 5,'* * * IEISCH008 TAVR VALUE &TAVR INVALID'
&SGQUITB SETB  1
         AGO   .BA                 * CONSOLE *
.V9 MNOTE 5,'* * * IEISCH009 CONSOLE VALUE NOT SPECIFIED'
&SGQUITB SETB 1
         AGO   .CE
.VA0  ANOP
&SGSCHDC(&G)  SETC  ''
.VA MNOTE 5,'* * * IEISCH010 CONSOLE VALUE &CONSOLE INVALID'
&SGQUITB SETB  1
         AGO   .CE
.VB0  ANOP
&SGSCHDC(&G)  SETC  ''
.VB MNOTE 5,'* * * IEISCH011 ALTCONS VALUE &ALTCONS INVALID'
&SGQUITB SETB  1
         AGO   .CE
.VF MNOTE 5,'* * * IEISCH015 JOBQRES VALUE &JOBQRES INVALID'
&SGQUITB SETB  1
         AGO   .D0                 * STARTR *
.W0 MNOTE 5,'* * * IEISCH016 STARTR VALUE &STARTR INVALID'
&SGQUITB SETB  1
         AGO   .DC                 * STARTW *
.W2 MNOTE 5,'* * * IEISCH018 STARTR ADDRESS NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .DC                 * STARTW *
.W3 MNOTE 5,'* * * IEISCH019 STARTW VALUE &STARTW INVALID'
&SGQUITB SETB  1
         AGO   .F0                      * ACCTRTN *
.W5 MNOTE 5,'* * * IEISCH021 STARTW ADDRESS NOT SPECIFIED'
&SGQUITB SETB  1
         AGO   .F0                      * ACCTRTN *
.W9 MNOTE 5,'* * * IEISCH025 ACCTRTN VALUE &ACCTRTN INVALID'
&SGQUITB SETB  1
         AGO   .F8                 * REPLY *
.WB MNOTE 5,'* * * IEISCH027 REPLY VALUE &REPLY INVALID'
&SGQUITB SETB  1
         AGO   .FA                 * WTOBFRS *
.WD MNOTE *,'* * * IEISCH029 WTOBFRS VALUE &WTOBFRS INVALID'
&SGQUITB SETB  1
         AGO   .FC                 * STARTI *
.WF MNOTE 5,'* * * IEISCH031 STARTI VALUE &STARTI INVALID'
&SGQUITB SETB  1
         AGO   .G2                 * PROCRES *
.X1 MNOTE 5,'* * * IEISCH033 PROCRES VALUE &PROCRES INVALID'
&SGQUITB SETB  1
         AGO   .G4                 * WTLCLSS *
.X3 MNOTE 5,'* * * IEISCH035 WTLCLSS VALUE &WTLCLSS INVALID'
&SGQUITB SETB  1
 AGO .H4                           * JOBQFMT *
.X5 MNOTE 5,'* * * IEIJSCH037 WTLBFRS VALUE &WTLBFRS INVALID'
&SGQUITB SETB  1
 AGO .LOGTEST                      * OPSEPAR *
.X6 MNOTE 5,'* * * IEISCH038 JOBQFMT VALUE &JOBQFMT INVALID'
&SGQUITB SETB  1
         AGO   .HB                 * JOBQWTP *
.X7A MNOTE 5,'* * * IEISCH039 JOBQWTP VALUE &JOBQWTP INVALID'
&SGQUITB SETB  1
         AGO   .HC                 * JOBQLMT *
.X8 MNOTE 5,'* * * IEISCH040 JOBQLMT VALUE &JOBQLMT INVALID'
&SGQUITB SETB  1
         AGO   .I4                 * JOBQTMT *
.XA MNOTE 5,'* * * IEISCH042 JOBQTMT VALUE &JOBQTMT INVALID'
&SGQUITB SETB  1
         AGO   .J0                      * INITQBF *
.XE MNOTE 5,'* * * IEISCH046 INITQBF VALUE &INITQBF INVALID'
&SGQUITB SETB  1
         AGO   .J4                 * MINPART *
.XF MNOTE 5,'* * * IEISCH047 INITQBF INVALID FOR &TYPE SCHEDULR'
&SGQUITB SETB  1
         AGO   .J4                 * MINPART *
.Y0 MNOTE 5,'* * * IEISCH048 MINPART VALUE &MINPART INVALID'
&SGQUITB SETB  1
         AGO   .JC                 * OPTIONS *
.Y2 MNOTE 5,'* * * IEISCH050 OPTIONS VALUE &OPTIONS(&A) INVALID'
&SGQUITB SETB  1
         AGO   .JE                 * RETURN *
.Y31 MNOTE 5,'* * * IEISCH051 INVALID NUMBER OF PARAMETERS FOR HARDCPY'
&SGQUITB SETB 1
 AGO .BAA                          * CONSOLE *
.Y3B MNOTE 5,'* * * IEISCH052 HARDCPY=SYSLOG INVALID WITH OPTIONS=NOLOG*
               '
&SGQUITB SETB 1
         AGO   .L6                      * ROUTCDE *
.Y3C MNOTE 5,'* * * IEISCH053 OPTIONS=LOG INVALID WITH OPTIONS=NOLOG'
&SGQUITB SETB 1
         AGO   .GE                      * WTLBFRS *
.Y3D MNOTE 0,'* * * IEISCH055 WTLBFRS INVALID WITH NOLOG AND HAS'
 MNOTE *,'                 BEEN SET TO 0'
&SGSCHDA(3) SETA 0
 AGO .LOGTEST                      * OPSEPAR *
.Y3E MNOTE 5,'* * *IEISCH057 TSO ONLY VALID WITH M65MP OR MVT'
&SGQUITB SETB  1
         AGO   .JE                 *  OPTIONS*
.YC    MNOTE 5,'* * * IEISCH058 CONOPTS VALUE &CONOPTS(1) INVALID'
&SGQUITB SETB 1
       AGO  .BA1
.YD    MNOTE 5,'* * * IEISCH060 CONOPTS VALUE &CONOPTS(2) INVALID'
&SGQUITB SETB 1
 AGO .BB1                          * HARDCPY *
.YE    MNOTE 5,'* * * IEISCH062 ALTCONS NOT SPECIFIED FOR MCS'
&SGQUITB SETB 1
       AGO  .C9
.YF    MNOTE 0,'* * * IEISCH063 ROUTCDE IGNORED -- MCS NOT SPECIFIED'
       AGO  .M8
.Z0    MNOTE 5,'* * * IEISCH064 ROUTCDE VALUE &ROUTCDE(&E) INVALID'
&SGQUITB SETB 1
       AGO .LC
.Z05   MNOTE 5,'* * * IEISCH065 ROUTCDE VALUE &ROUTCDE(&E) DUPLICATED'
&SGQUITB SETB 1
       AGO  .LC
.Z1    MNOTE 5,'* * * IEISCH066 OLDWTOR IGNORED -- MCS NOT SPECIFIED'
&SGQUITB SETB 1
         AGO   .R1                      *** ESV ***
.Z2    MNOTE 5,'* * * IEISCH067 OLDWTOR VALUE &OLDWTOR(&E) INVALID'
&SGQUITB SETB 1
      AGO   .LF
.Z25   MNOTE 5,'* * * IEISCH068 OLDWTOR VALUE &OLDWTOR(&E) DUPLICATED'
&SGQUITB SETB 1
       AGO  .LF
.Z3    MNOTE 0,'* * * IEISCH069 HARDCPY IGNORED -- MCS NOT SPECIFIED'
 AGO .BAA                          * CONSOLE *
.Z4    MNOTE 5,'* * * IEISCH070 HARDCPY VALUE &HARDCPY(1) INVALID'
&SGQUITB SETB 1
      AGO   .M2
.Z6 MNOTE 5,'* * * IEISCH071 HARDCPY VALUE &D INVALID AS 2ND PARAMETER'
&SGQUITB SETB 1
      AGO  .M31
.Z65   MNOTE 5,'* * * IEISCH075 HARDCPY VALUE &D DUPLICATED'
&SGQUITB SETB 1
       AGO  .M31
.Z66 MNOTE 5,'* * * IEISCH077 HARDCPY VALUE &HARDCPY(2) INVALID'
&SGQUITB SETB 1
 AGO .M345
.Z7 MNOTE 5,'* * * IEISCH079 NO MORE THAN ONE CONSOLE MAY BE SPECIFIED *
               FOR &TYPE SCHEDULR'
&SGQUITB SETB  1
         AGO   .C2
.Z7A MNOTE 5,'* * * IEISCH081 NO MORE THAN ONE MASTER CONSOLE MAY BE   *
               SPECIFIED WITH MCS IN THE SYSTEM'
&SGQUITB SETB 1
 AGO .C2
.Z8 MNOTE 5,'* * * IEISCH083 THE &TYPE SYSTEM REQUIRES TWO PRIMARY CONS*
               OLES'
&SGQUITB SETB  1
         AGO   .C2
.Z9 MNOTE 5,'* * * IEISCH085 NO MORE THAN ONE ALTERNATE CONSOLE MAY BE *
               SPECIFIED FOR &TYPE SCHEDULR'
&SGQUITB SETB  1
         AGO   .CE
.ZA MNOTE 5,'* * * IEISCH086 NO MORE THAN TWO COMPOSITE CONSOLES MAY BE*
                SPECIFIED'
&SGQUITB SETB  1
         AGO   .CE
.ZE MNOTE 5,'* * * IEISCH087 ESV VALUE &ESV INVALID'
&SGQUITB SETB 1
 AGO .R2
.ZF  MNOTE 5,'* * * IEISCH089 EVA VALUE &EVA INVALID'
&SGQUITB SETB 1
         AGO   .S
.ZG MNOTE 5,'* * * IEISCH090 AREA SPECIFICATION REQUIRES MCS'
&SGQUITB SETB  1
         AGO .N
.ZH MNOTE 5,'* * * IEISCH091 MAXIMUM OF 11 AREAS MAY BE DEFINED'
&SGQUITB SETB  1
         AGO   .S2
.ZI MNOTE 5,'* * * IEISCH092 AREA VALUE &AREA(&A) INVALID'
&SGQUITB SETB  1
         AGO   .S6
.ZJ MNOTE 5,'* * * IEISCH093 PFK VALUE &PFK INVALID'
&SGQUITB SETB  1
         AGO .T
.ZK MNOTE 5,'* * * IEISCH094 PFK SPECIFICATION REQUIRES MCS'
&SGQUITB SETB  1
         AGO .T
.ZL MNOTE 5,'* * * IEISCH095 OPSTRAN VALUE &OPSTRAN INVALID'
&SGQUITB SETB  1
         AGO .U
.ZM MNOTE 5,'* * * IEISCH096 OPSTRAN SPECIFICATION REQUIRES MCS'
&SGQUITB SETB  1
         AGO .U
.ZN MNOTE 5,'* * * IEISCH097 OPSTRNI SPECIFICATION REQUIRES MCS AND COM*
               POSITE MASTER CONSOLE'
&SGQUITB SETB  1
         AGO .V
.ZO MNOTE 5,'* * * IEISCH098 OPSTRNI VALUE &OPSTRNI INVALID'
&SGQUITB SETB  1
         AGO .V
.ZP MNOTE 0,'* * * IEISCH099 WARNING- BOTH HALVES OF COMPOSITE MASTER  *
               CONSOLE SHARE SAME TRANSIENT AREA'
         AGO .U0
.MEND  MEND
