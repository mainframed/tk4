         MACRO
         SGIEF212 &NAME=,&TYPLIST=,&HLFLIST=,&SGDT=,&SGDU=,&SGDF=,&SGUNX
               F=,&SGDM=,&SGUNM=,&SGDAD=
.* A006020                                                       S99249
.* A5800                                                          21088
.* A5420                                                         A43012
         COPY  SGGBLPAK
.*    GLOBAL SYMBOLS SET IN THIS MACRO
         LCLA  &A,&B,&X,&Y,&C,&E,&D
         LCLA  &DTSW,&DUSW                                        19033
.*             SGCNTRA(8) = COUNT OF UNIQUE DEVICE TYPES PRESENT
.*             SGCNTRA(9) = COUNT OF SUPPORTED DEVICES
.*             SGCNTRA(10)= COUNT OF POSSIBLE UNIQUE DEVICE TYPES
.*             SGMENTB(83)= SGIEF212 HAS BEEN ENTERED
.* SGDADRA(1-SGCNTRA(3)) BIT 31 SET ON IF DEVICE IS SUPPORTED
.*                 BITS 23-30= UNIQUE DEVICE TYPE
&SGCNTRA(10) SETA &SGCNTRA(10)+1   COUNT OF SUPPORTED DEVICE TYPES
         AIF   (&SGMENTB(83)).ON1  HAS MACRO BEEN ENTERED BEFORE
 PUNCH '         SGIEF010                                              X
                       X'
&SGMENTB(83)   SETB 1
.ON1     ANOP
         AIF   (&SGMENTB(84)).ON6  ARE WE PUNCHING CARDS FOR 2ND ASMBLY
.BACK1  AIF  (&A EQ &SGCNTRA(3) OR &SGCNTRA(9) EQ &SGCNTRA(3)).ON6
&A       SETA  &A+1
.*  BIT 31 ON WHEN DEVICE'S SWITCHES MATCH A SUPPORTED TYPE
         AIF   (&SGDADRA(&A) GT X'8000'*X'8000').BACK1
.* BITS 1-12=ADDR,13-20=PTR TO CU SWITCHES,21-30=PTR TO DEV SWITCHES
&X     SETA &SGDADRA(&A)/X'100000'
&B SETA &SGDT01B(&X)*1+&SGDT02B(&X)*2+&SGDT03B(&X)*3+&SGDT04B(&X)*4+&SGX
               DT05B(&X)*5+&SGDT06B(&X)*6+&SGDT07B(&X)*7+&SGDT08B(&X)*8X
               +&SGDT12B(&X)*12                                  A43012
&DTSW    SETA  &B                                                 19033
  AIF (&B NE &SGDT).BACK1  B TELLS WHICH SGDT(01-08)B SWITCH IS ON
&B SETA &SGDU01B(&X)+&SGDU02B(&X)*2+&SGDU03B(&X)*3+&SGDU04B(&X)*4+&SGDUX
               05B(&X)*5+&SGDU06B(&X)*6+&SGDU07B(&X)*7+&SGDU08B(&X)*8+&X
               SGDU09B(&X)*9+&SGDU10B(&X)*10                     S99249
&DUSW    SETA  &B                                                 19033
  AIF (&B NE &SGDU).BACK1  B TELLS WHICH SGDU(01-08)B SWITCH IS ON
         AIF   ('&SGDM' EQ '').ON2
&C       SETA  0
.BACK6   AIF   (&C EQ N'&SGDM).BACK1
&C       SETA  &C+1
&B SETA &SGDM01B(&X)+&SGDM02B(&X)*2+&SGDM03B(&X)*3+&SGDM04B(&X)*4+&SGDMX
               05B(&X)*5+&SGDM06B(&X)*6
&E       SETA  &SGDM(&C)
         AIF   (&E NE &B).BACK6
.ON2     AIF   ('&SGDF' EQ '').ON4
&C       SETA  0
.BACK2   AIF   (&C EQ N'&SGDF).ON4
&C  SETA &C+1       COUNT OF MEANINGFULL FEATURE SWITCHES FOR THIS TYPE
.* COMPARE THE MEANINGFULL FEATURE VALUE FOR THIS TYPE TO ITS
.*             SGDF(01-09)B  SWITCH. IF ON CONTINUE SEARCH.
&E       SETA  &SGDF(&C)
 AIF ((&E EQ 1 AND &SGDF01B(&X)) OR (&E EQ 2 AND &SGDF02B(&X)) OR (&E  X
               EQ 3 AND &SGDF03B(&X)) OR (&E EQ 4 AND &SGDF04B(&X)) OR X
               (&E EQ 5 AND &SGDF05B(&X))).BACK2
 AIF ((&E EQ 6 AND &SGDF06B(&X)) OR (&E EQ 7 AND &SGDF07B(&X)) OR (&E  X
               EQ 8 AND &SGDF08B(&X)) OR (&E EQ 9 AND &SGDF09B(&X))).BAX
               CK2
         AGO   .BACK1
.ON4     ANOP
.*  GET BITS 13-20 WHICH GIVE CONTROL UNIT POINTER VALUE
&Y     SETA &SGDADRA(&A)/X'1000'-&SGDADRA(&A)/X'100000'*X'100' GET13-20
         AIF   ('&SGUNF' EQ '').ON5
&C       SETA  0
.BACK4   AIF   (&C EQ N'&SGUNF).ON5
&C SETA &C+1 COUNT OF MEANINGFULL C.U.FEATURES FOR THIS TYPE
 AIF ((&SGUNF(&C) EQ 1 AND &SGUNF1B(&Y)) OR (&SGUNF(&C) EQ 2 AND &SGUNFX
               2B(&Y)) OR (&SGUNF(&C) EQ 3 AND &SGUNF3B(&Y))).BACK4
         AGO   .BACK1
.ON5     AIF   ('&SGUNM' EQ '').ON55
&B SETA &SGUNM1B(&Y)+2*&SGUNM2B(&Y)+3*&SGUNM3B(&Y)+4*&SGUNM4B(&Y)
 AIF (&B NE &SGUNM).BACK1  B TELLS WHICH SGUNM(1-4)B SWITCH IS ON
.ON55    ANOP
         AIF   ('&SGDAD' EQ '').ON56
&B SETA &SGDAD1B(&X)+&SGDAD2B(&X)*2+&SGDAD3B(&X)*3+&SGDAD4B(&X)*4+&SGDAX
               D5B(&X)*5+&SGDAD6B(&X)*6+&SGDAD7B(&X)*7+&SGDAD8B(&X)*8
         AIF   (&B NE &SGDAD).BACK1
.ON56    ANOP
.* PUT PTR TO DEV SW IN 13-22,DEVTYPE IN 23-30 AND BIT 31 ON
&B SETA &SGDADRA(&A)/X'100000'*4096-&SGDADRA(&A)/(X'40000'*4096)*X'4000X
               00'
&SGDADRA(&A) SETA &SGDADRA(&A)-&SGDADRA(&A)/4096*4096+&B+&SGCNTRA(10)*XX
               '400000'+X'40000'*4096
&SGCNTRA(9)    SETA &SGCNTRA(9)+1              COUNT SUPPORTED DEVICES
         AIF   (&DTSW NE 3).ON57                                 S20201
         AIF   (&DUSW NE 8).ON57                                 S20201
&A       SETA  &A+7     SKIP NEXT 7 SGDADRA BOXES FOR 2305       S20201
.ON57    ANOP                                                    S20201
         AIF   (&DTSW NE 6).ON58                                  19033
         AIF   (&DUSW NE 4 AND &DUSW NE 5).ON58                   19033
&A       SETA  &A+1     SKIP 2ND SGDADRA BOX FOR 1419 AND 1275    19033
.ON58    ANOP
         AIF   (&SGTYPEB(&SGCNTRA(10))).BACK1
&SGCNTRA(8)    SETA &SGCNTRA(8)+1  COUNT OF UNIQUE TYPES PRESENT
&SGTYPEB(&SGCNTRA(10)) SETB 1 SWITCH SHOWING DEV.THIS TYPE PRESENT
         AGO   .BACK1
.ON6 AIF (NOT &SGTYPEB(&SGCNTRA(10))).MEND BRANCH IF NONE OF THIS TYPE
 PUNCH '               &SGCNTRA(10),&NAME,X''&TYPLIST'',X''&HLFLIST'', X
                                           XXXXXXXX'
.MEND    MEND
