         MACRO
         UADS  &UNIT=,&VOLNO=
         COPY  SGGBLPAK
         LCLB  &Q
         AIF   (NOT &SGMENTB(65)).U1    TEST FOR PREVIOUS ENTRY
    MNOTE 5,'* * * IEIUAD101 UADS MACRO PREVIOUSLY USED'
         AGO   .UQUIT                   GO TO ERROR EXIT
.U1      ANOP
&SGMENTB(65)   SETB 1                   SET UADS ENTRY SWITCH
         AIF   (N'&UNIT EQ N'&VOLNO).U2 IS JUST 1 PARAMETER MISSING?
    MNOTE 5,'* * * IEIUAD102 UNIT VALUE OR VOLNO VALUE MISSING'
         AGO   .UQUIT                   GO TO ERROR EXIT
.U2      AIF   ('&UNIT' EQ '').U5       BRANCH FOR SYSRES DEFAULT
         AIF   (K'&UNIT LE 8).U3        TEST FOR VALID UNIT SPECIFICATN
    MNOTE 5,'* * * IEIUAD103 UNIT VALUE &UNIT INVALID'
&Q       SETB  1
.U3      AIF   (K'&VOLNO LE 6).U4       TEST VOLNO SPECIFICATION
    MNOTE 5,'* * * IEIUAD104 VOLNO VALUE &VOLNO INVALID'
         AGO   .UQUIT                   GO TO ERROR EXIT
.U4      AIF   (&Q).UQUIT               TEST FOR PREVIOUS ERROR
    MNOTE *,'      USER ATTRIBUTE DATA SET WILL RESIDE ON              *
                            UNIT &UNIT, VOLUME &VOLNO'
&SGCTRLC(50) SETC '&VOLNO'
&SGCTRLC(49) SETC '&UNIT'
         MEXIT
.U5 MNOTE *,'      USER ATTRIBUTE DATA SET WILL RESIDE ON SYSRES'
         MEXIT
.UQUIT   ANOP                           ERROR EXIT
&SGQUITB SETB  1                        SET QUIT BIT AND EXIT
         MEND
