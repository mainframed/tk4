         MACRO
         SVCLIB  &PDS=,&MEMBERS=
         COPY  SGGBLPAK
         LCLA  &CTR,&NOMEM
         AIF   (NOT &SGMENTB(41)).SVC4       1ST TIME CALLED
    MNOTE 5,'* * * IEISVL101 SVCLIB MACRO PREVIOUSLY USED'
&SGQUITB SETB  1                             QUIT SWITCH ON
         MEXIT
.SVC4    ANOP
&SGMENTB(41)  SETB  1                        ENTRY SWITCH ON
         AIF     ('&PDS' NE '').SVC1
    MNOTE 5,'* * * IEISVL102 PDS VALUE NOT SPECIFIED'
&SGQUITB SETB    1
         MEXIT
.SVC1    AIF  ('&PDS'(1,5) NE 'SYS1.' OR K'&PDS GT 13).SVC2
         AIF   (K'&PDS LT 6).SVC2
         AIF  ('&PDS'(6,1) LT 'A' OR '&PDS'(6,1) GT 'Z').SVC2
         AGO  .S1
.SVC2 MNOTE 5,'* * * IEISVL103 PDS VALUE &PDS INVALID'
&SGQUITB SETB    1
         AGO  .S12
.S1 MNOTE *,'                LISTED MEMBERS OF &PDS IN SVCLIB'
&CTR     SETA    K'&PDS
&SGSVCLC(1) SETC '&PDS'(6,&CTR-5)
.S12     AIF     (N'&MEMBERS EQ 0).L3
         AIF   (NOT &LIMIT(4)).SVC22
         AIF   (N'&MEMBERS GT 5).L4
         AGO   .SVC24
.SVC22   AIF   (N'&MEMBERS GT 50).L4
.SVC24   ANOP
&NOMEM   SETA  N'&MEMBERS
&CTR     SETA    1
.SVC3    ANOP
&CTR     SETA  &CTR+1
         AIF   (&CTR-1 GT &NOMEM).SVCEND
  AIF  (K'&MEMBERS(&CTR-1) LT 1 OR K'&MEMBERS(&CTR-1) GT 8).L5
  AIF  ('&MEMBERS(&CTR-1)'(1,1) EQ '@' OR '&MEMBERS(&CTR-1)'(1,1) EQ   *
               '#' OR '&MEMBERS(&CTR-1)'(1,1) EQ '$').Q
   AIF  ('&MEMBERS(&CTR-1)'(1,1) LT 'A' OR '&MEMBERS(&CTR-1)'(1,1) GT '*
               Z').L5
.Q       ANOP
&SGSVCLC(&CTR)  SETC  '&MEMBERS(&CTR-1)'
    MNOTE *,'        &MEMBERS(&CTR-1) '
         AGO   .SVC3
.L3 MNOTE 5,'* * * IEISVL104 MEMBERS VALUE NOT SPECIFIED'
&SGQUITB  SETB  1
         MEXIT
.L4 MNOTE 5,'* * * IEISVL105 MAXIMUM NUMBER OF MEMBERS EXCEEDED'
&SGQUITB SETB  1
         MEXIT
.L5 MNOTE 5,'* * * IEISVL106 MEMBERS VALUE &MEMBERS(&CTR-1) INVALID'
&SGQUITB  SETB  1
         AGO   .SVC3
.L6 MNOTE 5,'* * * IEISVL107 MEMBERS VALUE &MEMBERS(&CTR-1) NOT''IGC'''
&SGQUITB  SETB  1
         AGO   .SVC3
.SVCEND  MEND
