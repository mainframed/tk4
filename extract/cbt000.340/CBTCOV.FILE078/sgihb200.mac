         MACRO
         SGIHB200
         COPY  SGGBLPAK
         LCLA  &A,&I,&J,&K,&L,&M,&N
         LCLC  &DEVADDR(126)
         ACTR  12288
&L       SETA  &SGSCNYA(1)
&M       SETA  2+&L*4
&N       SETA  0
&I       SETA  1
&J       SETA  1
&K       SETA  1
&DEVADDR(1)    SETC  '&SGSCHDC(1)'(1,3)          MASTER CONSOLE INPUT
&DEVADDR(2)    SETC  '&SGSCHDC(1)'(4,3)          MASTER CONSOLE OUTPUT
.A       AIF   ('&SGSCNCC(&J)' EQ '').AA
&DEVADDR(&K+2)    SETC  '&SGSCNCC(&J)'(1,3)      SEC CONSOLE INPUT
&DEVADDR(&K+3)    SETC  '&SGSCNCC(&J)'(4,3)      SEC CONSOLE OUTPUT
.AA      AIF   ('&SGSCNAC(&J)' EQ '').AAA
&DEVADDR(&K+&L*2+2)    SETC  '&SGSCNAC(&J)'(1,3) ALT CONSOLE INPUT
&DEVADDR(&K+&L*2+3)    SETC  '&SGSCNAC(&J)'(4,3) ALT CONSOLE OUTPUT
.AAA     ANOP
&K       SETA  &K+2
&J       SETA  &J+1
         AIF   (&J LE &L).A
.* THIS PLACES ALL MCS CONSOLE ADDRESSES(INPUT AND OUTPUT) INTO TABLE
.* &DEVADDR WITH 2+&L*4 ACTIVE ENTRIES(MAXIMUM OF 126)
.B       AIF   ('&DEVADDR(&I)' EQ '').INCRI
         AIF   ('&DEVADDR(&I)'(1,3) EQ 'FFF').D                  A43870
         AIF   (&I GE &M).C
         AIF   ('&DEVADDR(&I)' EQ '&DEVADDR(&I+1)').D
.C       ANOP
         CONVERT  TO=HEX,DIGITS=3,VALUE=&DEVADDR(&I)
         IOCHECK  LOOKFOR=&HEX
&A       SETA  &SGDADRA(&HEX)/1048576                             P4790
         AIF   (&SGDT08B(&A) AND &SGDU08B(&A)).B2740
.D       ANOP
&DEVADDR(&I)    SETC  ''
.INCRI   ANOP
&I       SETA  &I+1
         AIF   (&I LE &M).B
         AGO   .DD
.B2740   ANOP
&N       SETA  &N+1
         AGO   .INCRI
.* THIS BLANKS ALL NON-2740 DEVICE ADDRESSES AND BLANKS THE INPUT
.* ADDRESS WHEN IT IS THE SAME AS THE OUTPUT ADDRESS FOR A CONSOLE
.* AND COUNTS THE NUMBER OF 2740'S REMAINING
.* WITH POSSIBLE DUPLICATION BETWEEN ALTERNATE AND SECONDARY OR
.* ALTERNATE AND MASTER 2740'S
.DD      ANOP
&J       SETA  1
.E       AIF   ('&DEVADDR(&J)' EQ '').INCRJ
&K       SETA  &J+1
.F       AIF   ('&DEVADDR(&K)' NE '&DEVADDR(&J)').INCK
&N       SETA  &N-1
&DEVADDR(&K)   SETC   ''
.INCK    ANOP
&K       SETA  &K+1
         AIF   (&K LE &M).F
.INCRJ   ANOP
&J       SETA  &J+1
         AIF   (&J LT &M).E
.* THIS COUNTS THE NUMBER OF UNIQUE 2740 DEVICES IN MCS
&SGCTRLC(6)    SETC  'IHB000'
         COPY  SGASMPAK
         PUNCH ' SGIHB000    &SGCPRGB(4),&SGSUPRB(21),&N '
         PUNCH ' END'
         PUNCH '/*  '
.* THIS ENABLES THE 2740 DSP GENERATING MACRO SGIHB000 TO BE EXPANDED
.* DURING STAGE 2
         MEND
