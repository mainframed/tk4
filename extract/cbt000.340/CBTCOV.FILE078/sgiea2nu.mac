         MACRO
         SGIEA2NU
         COPY  SGGBLPAK
.* THIS MACRO SUPPLIES THE FOLLOWING PARAMETERS TO THE IEAQNU MACRO -
.*       TIMER OPTION,MODEL,SER LEVEL,HIGH IBM SVC NO.,LOW USER   19017
.*       SVC NO.,TRACE ENTRIES - OPTIONS FOR DECIMAL SIMULATOR,   19017
.*       TESTRAN,TIME SLICING,MPS,SMF,EMULATOR,RESIDENT ERPS,
.*       IO RMS DDRSYS,TSO,2880 CHANNEL                           20021
         LCLA  &MOD
         LCLC  &TIMER,&SER
         LCLC  &TRACER
         LCLB  &DSIM
         LCLB  &TSTRN
         LCLB  &SMF
         LCLC  &EMU                                                EMUL
         LCLB  &DDRSYS                                            19022
         LCLB  &ERP                                               19017
         LCLB  &TSO                                               20021
         LCLB  &TIMESL,&M65MP
         LCLB  &CHANLOG                                           19E03
.*2509,009900                                                      SMF
.*0811,006800                                                      85S
.*
.*        DETERMINE TRACE OPTION-
.*
&TRACER  SETC  ''
         AIF   (&SGSUPRA(1) EQ 0).NEXT0
&TRACER  SETC  'TRACE'
.NEXT0   ANOP
.*
.* DETERMINE TIMER OPTION
.*
&TIMER   SETC  'TIME6B'                 INTERVAL TIMING
         AIF   (NOT(&SGSUPRB(19))).NEXT2
&TIMER   SETC  'JSTIME'                 JOB STEP TIMING
.*
.* DETERMINE MODEL
.*
.NEXT2   ANOP
.*
.*       TEST FOR TIME SLICING AND MULTIPROCESSING
.*
&TIMESL  SETB  (&SGCPRGB(12))
&M65MP   SETB  (&SGCPRGB(13))
&MOD     SETA  40
         AIF   (NOT(&SGCPUB(3))).NEXT3
&MOD     SETA  50
         AGO   .NEXT9
.NEXT3   AIF   (NOT(&SGCPUB(4))).NEXT4
&MOD     SETA  65
         AGO   .NEXT9
.NEXT4   AIF   (NOT(&SGCPUB(5))).NEXT8                             85S
&MOD     SETA  75
.NEXT8   AIF   (NOT(&SGCPUB(7))).NEXT9                             85S
&MOD     SETA  85                                                  85S
.NEXT9   AIF   (NOT(&SGCPUB(6))).NEXT9A
&MOD     SETA  91
.NEXT9A  AIF   (NOT(&SGCPUB(9))).NEXT9B                       BD  21108
&MOD     SETA  195                                                19035
.NEXT9B  AIF   (NOT(&SGCPUB(13))).NEXT9C                      BD  21108
&MOD     SETA  795                                            BD  21108
.NEXT9C  AIF   (NOT(&SGCPUB(8))).NEXT9D                           19E07
&MOD     SETA  155                                                19E07
.NEXT9D  AIF   (NOT(&SGCPUB(11))).NEXT9E                          19E08
&MOD     SETA  165                                                19E08
.NEXT9E  AIF   (NOT(&SGCPUB(10))).NEXT9F
.* THE MODEL NUMBER HAS BEEN CHANGED FROM 297 TO 145              M0132
&MOD     SETA  145                                                20301
.NEXT9F  AIF   (NOT(&SGCPUB(14))).NEXT9G                       BD M5501
&MOD     SETA  158                                             BD 21122
.NEXT9G  AIF   (NOT(&SGCPUB(15))).NEXT9H                       BD 21122
&MOD     SETA  168                                             BD 21122
.NEXT9H AIF (NOT(&SGCPUB(12))).NEXT9I                          @SA80054
&MOD    SETA    135                                            @SA80054
.NEXT9I ANOP                                                   @SA80054
.*
.* DETERMINE LEVEL OF SER
.*
&SER     SETC  'SERP'
         AIF   (NOT(&SGSUPRB(28))).NEXT12                          I112
&SER     SETC  'MCH'
.NEXT12  AIF (NOT(&SGSUPRB(24))).NEXT13
&DSIM    SETB  (1)
.NEXT13  AIF (NOT(&SGMENTB(17))).NEXT14
&TSTRN   SETB  (1)
.NEXT14  ANOP
.*                                                                 EMUL
.* DETERMINE EMULATOR OPTION                                       EMUL
.*                                                                 EMUL
&EMU     SETC  'EMUN'                                              EMUL
         AIF   (NOT &SGMENTB(58)).NEXT15                           EMUL
         AIF   (NOT &SGCPUB(7)).NEXT15                             EMUL
&EMU     SETC  'EMUY'                                              EMUL
.NEXT15  ANOP
.*       SET INDICATOR FOR I/ORMS                                 19022
&DDRSYS  SETB  (&SGSUPRB(34))                                     19022
.*       SET INDICATOR FOR ERP                                    19017
&ERP     SETB  (&SGSUPRB(40))                                     19017
.*       SET INDICATOR FOR SMF                                     SMF
&SMF     SETB  (&SGSCHDB(7))                                       SMF
.*       SET INDICATOR FOR 2880 CHANNEL                           19E03
&CHANLOG SETB  (&SGMENTB(68))                                     19E03
.*       SET INDICATOR FOR TIME SHARING OPTION                    20021
&TSO     SETB  (&SGSCHDB(44))                                     20021
&SGCTRLC(6) SETC 'IEAQNU00'
         AIF   (NOT &SGSCHDB(44)).NEXT16                          20021
         COPY  TSASMPAK                                           20021
         AGO   .NEXT17                                            20021
.NEXT16  ANOP                                                     20021
         COPY  SGASMPAK
.NEXT17  ANOP                                                     20021
 PUNCH ' IEAQNU  &TIMER,&MOD,&SER,&IEAHIA,&IEALOA,&TRACER,&DSIM,       C
                                      XXXXXXXXX'
 PUNCH '               &TSTRN,&SGCPRGB(12),&SGCPRGB(13),&SMF,&EMU,&ERP,C
               &DDRSYS,&TSO,&CHANLOG'                             20021
         PUNCH '         END'
         PUNCH '/*'
         MEND
