             MACRO
             SGIEW201
.*
.* C032000-040000,D048000,D096000-144000,C152000-168000         SA68152
.* D176000,C184000,D192000,C208000-216000,D224000,C232000       SA68152
.* D240000,D264000-280000,C296000,D336000-352000,C392000        SA68152
.* D432000-448000,C488000,D528000-544000,D560000-712000         SA68152
.* C720000,D728000-824000                                       SA68152
.*
             COPY  SGGBLPAK
             LCLA  &DPVAL,&DFVAL1,&DFVAL2                       SA68152
             LCLB  &LNKSW(3)                                    SA68152
             LCLC  &EDNAME
.*
             AIF   (&SGLINKB(1)).FTEST   PROCEED IF ANY EDITOR
             MEXIT                       NONE -- EXIT
.*
.FTEST       ANOP                                               SA68152
&LNKSW(1)    SETB  (&SGLINKB(2))       SET FLAG FOR EDITOR F44  SA68152
&LNKSW(2)    SETB  (&SGLINKB(3))       SET FLAG FOR EDITOR F88  SA68152
&LNKSW(3)    SETB  (&SGLINKB(4))       SET FLAG FOR EDITOR E128 SA68152
.*
.EDTEST      AIF   (&LNKSW(1)).GOF44                            SA68152
             AIF   (&LNKSW(2)).GOF88                            SA68152
             AIF   (&LNKSW(3)).GOF128                           SA68152
             MEXIT                       NO MORE EDITORS -- EXIT
.*
.GOF44       ANOP
&LNKSW(1)    SETB  0                     CLEAR F44 FLAG         SA68152
&DPVAL       SETA  (44*1024)             SET F44 DESIGN POINT VALUE
&DFVAL1      SETA  (&SGLINKA(1))         SET F44 VALUE 1
&DFVAL2      SETA  (&SGLINKA(2))         SET F44 VALUE 2
&EDNAME      SETC  ' F44'                SET EDITOR IDENTITY
&SGCTRLC(6)  SETC  'IEWLM044'            SET F44 MEMBER NAME
             AGO   .GOCOM
.*
.GOF88       ANOP                        F88 SET-UP
&LNKSW(2)    SETB  0                     CLEAR F88 FLAG         SA68152
&DPVAL       SETA  (88*1024)             SET F88 DESIGN POINT VALUE
&DFVAL1      SETA  (&SGLINKA(3))         SET F88 VALUE 1
&DFVAL2      SETA  (&SGLINKA(4))         SET F88 VALUE 2
&EDNAME      SETC  ' F88'                SET EDITOR IDENTITY
&SGCTRLC(6)  SETC  'IEWLM088'            SET F88 MEMBER NAME
             AGO   .GOCOM
.*
.GOF128      ANOP
&LNKSW(3)    SETB  0                     CLEAR F128 FLAG        SA68152
&DPVAL       SETA  (128*1024)            SET F128 DESIGN POINT VALUE
&DFVAL1      SETA  (&SGLINKA(5))         SET F128 VALUE 1
&DFVAL2      SETA  (&SGLINKA(6))         SET F128 VALUE 2
&EDNAME      SETC  'F128'                SET EDITOR IDENTITY
&SGCTRLC(6)  SETC  'IEWLM128'            SET F128 MEMBER NAME
.GOCOM       ANOP                                               SA68152
             COPY  SGASMPAK              PROVIDE ASSEMBLER JCL
 PUNCH ' TITLE ''IEWLMDEF   &EDNAME  EDITOR DEFAULT SIZE PARAMETERS '''
     PUNCH '         SGIEW001  VAL1=&DFVAL1,VAL2=&DFVAL2,DPVAL=&DPVAL'
     PUNCH '         IHBRELNO  '                                 S21016
     PUNCH '         END'
     PUNCH '/*'
             AGO   .EDTEST
.*
             MEND
