         MACRO
         SGIEE301
         COPY  SGGBLPAK
.*A000848-000857,003409-003539,148890-149890,197890,             S21003
.*A211890-212890,260890,274890-275890,316890-322890,             S21003
.*A323890-324390,594890-604390                                   S21003
.*C323390                                                        S21003
.*D094390-141390,155390-183390,204390-211390,218390-253390       S21003
.*D267390,274390,281390-309390,554390-594390                     S21003
.*                                                                19029
.*000700,000800,012660,012730,014250-014290,014371-024385,054390  19029
.*074390,114390,154390-314390,494390                              19029
.*DIDOCS                                                          19029
.*                                                                 I274
.*011200,013200,014200,014361,014365,013950                 19085  I274
.*CONSOLIDATED SYSGEN                                       19085  I274
.*                                                                 I254
.*WRITE TO PROGRAMMER -WTP-                                 19058  I254
.*                                                                19872
.*014330                                                          19872
.*OWNERSHIP                                                       19872
.*02677019,02717019-02777019                                     A34917
         GBLA  &GRCON,&TRCON                                     S21003
         GBLC  &BRODA(64),&BRODD(64)                             S21003
         LCLC  &A(64),&B(64),&D(64)                              S21003
         LCLA  &TO                                               S21003
         LCLA  &N,&M,&O                                           19029
         AIF   (&SGCPRGB(8)).MS1
         AIF   (&SGCPRGB(4)).MFT                                   MFT2
.*
.*       THIS IS A MASTER SCHEDULER GROUP III SYSGEN MACRO WHICH
.*       GENERATES INCLUDE CARDS FOR THE FOLLOWING NUCELUS MODS;
.*       1.  MASTER SCHEDULER EXTERNAL INTERRUPT HANDLER ROUTINE
.*       (IF ALTERNATE CONSOLE SPEDIFIED)
.*       2.  ONE OF TWO MASTER SCHEDULER CONSOLE ATTENTION
.*       INTERRUPT HANDLER ROUTINES(ONE FOR SYSTEMS 1 THRU 3;
.*       OR ONE FOR SYSTEMS 4 AND 5)
.*       3.  ONE OF TWO MASTER SCHEDULER RESIDENT CORE
.*       DEFINITION MODULES (ONE FOR 1052 SUPPORT; OR ONE
.*       FOR COMPOSITE CONSOLE SUPPORT)
.*       4.  INITIATOR UNSOLICITED INTERRUPT ROUTINE
.*       5. MASTER SCHEDULER RESIDENT CORE                       OPTN 4
.*       6. ATTENTION ROUTINE FOR ALLOCATE                       OPTN 4
.*       7. QUEUE MANAGER RESIDENT CORE                          OPTN 4
.*       8. VARIOUS CONTROL BLOCKS INCLUDING ATTENTION ECB       OPTN 4
.*       9. WRITE TO PROGRAMMER MODULE IEFWTP0A IN PCP             I254
.*
.*       THE FOLLOWING SYSGEN SYMBOLS WILL BE EQUATED TO THE
.*       CORRESPONDING VALUES:
.*       1.  &SGSCHDC(1)(1,3) = PRIN CONSOLE INPUT UCB
.*                      (4,3) = PRIN CONSOLE OUTPUT UCB
.*       2.  &SGSCHDC(2)(1,3) = ALT CONSOLE INPUT UCB
.*                      (4,3) = ALT CONSOLE OUTPUT UCB
.*       3.  &SGSCHDB(1) = SYSTEMS 1-5
.*           &SGCPRGB(3) = SYSTEM 4
.*           &SGCPRGB(4) = SYSTEM 5 OR 7
.*       4.  &SGCPRGB(8) = OPTION 4
.*       5.  &SGSCHDB(34) = MCS OPTION                            19872
.*       6.  &GETB(3) = I/O DEVICE SYSGEN                         19872
.*       7.  &SGSCHDB(7) = SMF OPTION                             19872
.*
         AIF   ('&SGSCHDC(1)'(1,3) EQ '&SGSCHDC(1)'(4,3)).A1
         PUNCH ' INCLUDE CI505(IEERSR01)'  M.S. RES CORE(COMPOSITE
         PUNCH ' INCLUDE CI505(IEFWTP0A)'    WTP MODULE            I254
         AIF   (&GETB(3)).A2B               IODEVICE SYSGEN        I274
         AGO   .A2
.A1      PUNCH ' INCLUDE CI505(IEERSC01)'  M.S. RES CORE(1052)
         PUNCH ' INCLUDE CI505(IEFWTP0A)'    WTP MODULE            I254
.A1A     AIF   (&GETB(3)).A2B               IODEVICE SYSGEN        I274
.A2      PUNCH ' INCLUDE CI505(IEFDPOST)'
         PUNCH ' INCLUDE SYSPUNCH(IEACVTPC)'
         AIF   (&SGCPRGB(4)).A2A
         AIF   ('&SGSCHDA(7)' GE '1').A20                          4605
         PUNCH ' INCLUDE CI505(IEFKRESA)'
         AGO   .A2B
.A20     PUNCH ' INCLUDE CI505(IEFKRESB)'                          4605
         AGO   .A2B                                                4605
.A2A     AIF   ('&SGSCHDA(7)' GE '1').A2A1                         4605
         PUNCH ' INCLUDE CI505(MCONRESA)'                          4605
         AGO   .A2A2                                               4605
.A2A1    PUNCH ' INCLUDE CI505(MCONRESB)'                          4605
.A2A2    PUNCH ' INCLUDE SYSPUNCH(IEFSD032)' MST BE CHGD FOR N PRT 4605
.A2B     ANOP
         AIF   ('&SGSCHDC(1)'(1,3) EQ '&SGSCHDC(2)'(1,3)).A3
         AIF   (&SGCPRGB(3) OR &SGCPRGB(4)).A2C
         PUNCH ' INCLUDE CI505(IEEBC1PE)'  EXTERNAL INTER RT(SYS1-3)
         AGO   .A3
.A2C     PUNCH ' INCLUDE CI505(IEECVCRX)'  EXTERNAL INTER RT(SYS4-5)
.A3      AIF   (&GETB(3)).END               IODEVICE SYSGEN        I274
         AIF   (&SGSCHDB(1)).A4                                    I274
         AGO   .END
.A4      AIF   (&SGCPRGB(3) OR &SGCPRGB(4)).A5
         PUNCH ' INCLUDE CI505(IEECIR01)'  ATTN INTER RT(SYS1-3)
         AGO   .END
.A5      PUNCH ' INCLUDE CI505(IEECVCRA,IEECVCTW)'  ATTN INT RT(SYS4-5)
         PUNCH ' INCLUDE CI505(IEECVPRG)'  REPLY PURGE ROUTINE(SYS4-5)
         PUNCH ' INCLUDE SYSPUNCH(IEECVUCM)'
.END     ANOP
         MEXIT
.MS1     AIF   (&GETB(3) AND &SGSCHDB(34)).MCS1                    I274
         AIF   (&GETB(3)).IO1                                      I274
         PUNCH ' INCLUDE CI535(IEEBASEC,IEFVPOST)'
         PUNCH ' INCLUDE CI505(IEECVCRA,IEFQRESD,IEFATECB)'
         AIF   (&SGSCHDB(34)).MCS1                                  MCS
         PUNCH ' INCLUDE CI535(IEECVCTB,IEECVED2)'
         AGO   .RADICE8                                             MCS
.MCS1    PUNCH ' INCLUDE CI535(IEECMED2,IEECMQWR)'                  MCS
.RADICE8 AIF   (&GETB(3) AND &SGSCHDB(34)).IO1                     I274
         PUNCH ' INCLUDE CI505(IEECVCRX)'
.IO1     PUNCH ' INCLUDE SYSPUNCH(IEECVUCM) '                      I274
         AIF   (&GETB(3) AND NOT &SGSCHDB(34)).OUT                 I274
         AIF   (&GETB(3) AND &SGSCHDB(34)).IO2                     I274
 PUNCH ' INCLUDE SYSPUNCH(IEACVTPC)'                              REFR
         PUNCH ' INCLUDE CI535(IEAQCB02)'                          REFR
.IO2     ANOP                                                      I274
         AGO   .TESTMCS                                             MCS
.MFT     ANOP                                                      MFT2
         AIF   (&GETB(3) AND &SGSCHDB(34)).MCS2                    I274
         AIF   (&GETB(3)).IEFSD                                  A30068
         PUNCH ' INCLUDE CI505(IEFSD567,IEFATECB)'                 MFT2
         PUNCH ' CHANGE IEECVPRB(IEEMSER)'
         PUNCH ' INCLUDE CI505(IEESD568,IEFQRESD)'                 MFT2
.*      FIX FOR A47740 TO ONLY INCLUDE IEECVCRX WITH ALT CONSOLE A47740
         PUNCH ' INCLUDE CI505(IEECVCRA)'
         AIF   ('&SGSCHDC(1)'(1,3) EQ '&SGSCHDC(2)'(1,3)).NOXTRN A47740
         PUNCH ' INCLUDE CI505(IEECVCRX)'
.NOXTRN  AIF   (&SGSCHDB(34)).MCS2                            BE A47740
         PUNCH ' INCLUDE CI505(IEECVCTW)'                          MFT2
         AGO   .RADICE2                                             MCS
.MCS2    PUNCH ' INCLUDE SYSPUNCH(IEECMAWR)'                     A27750
.RADICE2 ANOP                                                       MCS
          AIF  (NOT &SGSCHDB(38) OR NOT &SGCPRGB(4)).RADICE3        MCS
         PUNCH ' INCLUDE CI505(IEELOGWR)'                           MCS
.RADICE3 AIF   (&GETB(3) AND &SGSCHDB(34)).IEFSD                 A39537
         PUNCH ' INCLUDE CI505(IEECIR50)'                          MFT2
         PUNCH ' INCLUDE CI505(IEAQCB01)'                          REFR
         PUNCH ' INCLUDE SYSPUNCH(IEACVTPC)'                       REFR
         PUNCH ' INCLUDE SYSPUNCH(IEFSD032)'
.IEFSD   ANOP                                                     M4993
         PUNCH ' CHANGE IEECVTCB(IEACMTCB)'                       M4993
         PUNCH ' INCLUDE SYSPUNCH(IEECUCB)'                       M4993
.G2250   ANOP                                                      I274
.TESTMCS AIF   (NOT &SGSCHDB(34)).OUT                               MCS
.*  THIS CODE INCLUDES RESIDENT PORTION OF DCM INTO SYSTEM       S21003
&N       SETA  &SG5450A                                          S21003
.KCHECK  ANOP                                                     19029
         AIF    (&N EQ 0).TST2250                                S21003
         PUNCH ' INCLUDE SYSPUNCH(IEECVD&N) '                    S21003
&N       SETA &N-1                                               S21003
         AGO   .KCHECK                                            19029
.TST2250 ANOP                                                     19029
&N       SETA  &SGSCHDA(10)                                      S21003
.RCHECK  ANOP                                                     19029
         AIF   (&N EQ 0).TST2260                                 S21003
         PUNCH ' INCLUDE SYSPUNCH(IEECVE&N)  '                   S21003
&N       SETA  &N-1                                              S21003
         AGO   .RCHECK                                            19029
.TST2260 ANOP                                                     19029
&N       SETA   &SGSCHDA(11)                                     S21003
.CCHECK  ANOP                                                     19029
         AIF  (&N EQ 0).TSTAN48                               MD  M0434
         PUNCH ' INCLUDE SYSPUNCH(IEECVF&N) '                    S21003
&N       SETA  &N-1                                              S21003
         AGO   .CCHECK                                            19029
.TSTAN48 ANOP                                                 MD  M0434
&N       SETA  &SGSCHDA(17)            COUNT OF 3277-1        MD  M0434
.AN4CHK  ANOP                                                 MD  M0434
         AIF   (&N EQ 0).TSTAN19                              MD  M0434
         PUNCH ' INCLUDE SYSPUNCH(IEECVG&N) ' 3277-1 DCM      MD  M0434
&N       SETA  &N-1                    DECRE COUNT            MD  M0434
         AGO   .AN4CHK                 GET NEXT               MD  M0434
.TSTAN19 ANOP                                                 MD  M0434
&N       SETA  &SGSCHDA(18)            COUNT OF 3277-2        MD  M0434
.AN9CHK  ANOP                                                 MD  M0434
         AIF   (&N EQ 0).RESTRAN       ANY 3277-2 CONSOLES    MD  M0434
         PUNCH ' INCLUDE SYSPUNCH(IEECVH&N) ' 3277-2 DCM      MD  M0434
&N       SETA &N-1                                            MD  M0434
         AGO   .AN9CHK                                        MD  M0434
.RESTRAN ANOP                                                    S21003
&M       SETA 1                                                  S21003
.PASSER  ANOP                                                    S21003
&A(&M)   SETC  '&BRODA(&M)'                                      S21003
&D(&M)   SETC  '&BRODD(&M)'                                      S21003
&M       SETA  &M+1                                              S21003
         AIF  (&M LE 64).PASSER                                  S21003
.* THIS CODE INCLUDES TRANSIENT AREAS INTO THE SYSTEM            S21003
&N       SETA  1                                                 S21003
.CHECNAM ANOP                                                    S21003
         AIF  ('&D(&N)' EQ '').DOAGAIN                           S21003
         PUNCH ' INCLUDE SYSPUNCH(IEECV&D(&N)) '                 S21003
.DOAGAIN ANOP                                                    S21003
&N       SETA &N+1                                               S21003
         AIF  (&N LE 64).CHECNAM                                 S21003
.* THIS CODE INCLUDES PFK AREAS INTO THE SYSTEM                  S21003
         AIF   (&SGSCHDA(16) EQ 0).SECPROC                       S21003
         AGO   .KEYPCH                                           S21003
.SECPROC ANOP                                                    S21003
&N       SETA  1                                                 S21003
.TRYAGIN ANOP                                                    S21003
         AIF   (&SGSCNPA(&N) EQ 0).TRYMORE                       S21003
.KEYPCH  PUNCH '   INCLUDE SYSPUNCH(IEEPFKEY)  '                 S21003
         AGO  .ONE                                               S21003
.TRYMORE ANOP                                                    S21003
&N       SETA  &N+1                                              S21003
         AIF   (&N LE 31).TRYAGIN                                S21003
.*  THIS INCLUDES RESIDENT AREAS INTO SYSTEM                     S21003
.ONE     ANOP                                                    S21003
&TO       SETA  &GRCON-&TRCON                                    S21003
.PRNTNAM ANOP                                                    S21003
         AIF   (&TO EQ 0).STOPGPH                                S21003
         PUNCH '  INCLUDE SYSPUNCH(IEECVR&TO) '                  S21003
&TO      SETA  &TO-1                                             S21003
         AGO   .PRNTNAM                                          S21003
.STOPGPH ANOP                                                    S21003
         AIF   (NOT &SGSCHDB(35)).RADICE4  TEST DUMMY EXIT       S21003
         PUNCH ' INCLUDE CI505(IEECVCTE) '                       S21003
.RADICE4 PUNCH ' INCLUDE CI505(IEECMDSV,IEECMWSV) '              S21003
         AIF   (&SG5450A GT 0 OR &SGSCHDA(10) GT 0 OR &SGSCHDA(11) GT 0X
                OR &SGSCHDA(17) GT 0 OR &SGSCHDA(18) GT 0 OR &SGDCLSB(8X
               0)).DOMIN                                         M5508
.NODOM   PUNCH '  INCLUDE CI505(IEECVDOM)  '                     S21003
         AGO   .RADICE1                                          S21003
.DOMIN   PUNCH '  INCLUDE CI505(IEECMDOM) '                      S21003
         PUNCH ' INCLUDE SYSPUNCH(IEECVSUB) '                    S21003
.RADICE1 ANOP                                                       MCS
         AIF   (NOT &SGSCHDB(13)).OUT                               MCS
         PUNCH ' INCLUDE SYSPUNCH(IHB000) '                         MCS
.*       THIS INCLUDES THE 2740 DSP INTO THE NUCLEUS DURING STAGE 2 MCS
         PUNCH ' INCLUDE CQ513(IECTLOPN)'                           MCS
.*       THIS INCLUDES THE LINE OPEN ROUTINE INTO THE NUCLEUS IN STAGE2
         AIF   ( NOT &SGCPRGB(4) OR &SGSUPRB(21)).OUT               MCS
.*       IF MFT IS NOT PRESENT OR A RESIDENT ACCESS METHOD IS,EXIT  MCS
         PUNCH ' INCLUDE CQ513(IGG019MA,IGG019MB,IGG019M0)'         MCS
.*       THIS INCLUDES THE BTAM MODULES INTO THE NUCLEUS DURING STAGE 2
.OUT     MEND                                                       MCS
