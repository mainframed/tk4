CPFK     TITLE 'QUEUE COMMAND - LIST / CHANGE PFK DEFINITIONS'    ONL01
CPFK     QSTART ,                  START CPFK CSECT ASSEMBLY      ONL01
         USING QDISPLAY,R9
         L     R9,QVDSPL           LOAD BASE REG FOR DISPLAY AREA
         USING WORK,R13            BASE FOR SAVE / WORK AREA
         LH    R1,QLNG1            WAS A PARM PASSED?
         LTR   R1,R1
         BZ    PFKDISP             NO, GO DISPLAY CURRENT DEFNS
         SPACE 1
*        CHANGE THE CURRENT SETTING FOR THE PFK DEFINED BY QPARM1
         SPACE 1
         LH    R2,QLNG1            LENGTH OF DATASET ID FIELD
         SH    R2,=H'1'            IS THE DATASET ID FIELD ZERO LENGTH?
         BM    TILT                YES. QUIT.
         MVC   QFZONES,QFZONE      INITIALIZE NUMERIC TEST
         EX    R2,MVZ              MOVE THE ZONES FOR VALIDITY CHECK
         CLC   QFZONES,QFZONE      IS THE FIELD NUMERIC?
         BNE   TILT                NO. QUIT.
         EX    R2,PACK             PACK THE FIELD
         CVB   R2,DWORD            CONVERT TO BINARY
         LA    R15,QDREPLY         POINT TO REPLY AREA
         AH    R15,QOFF2           ADD OFFSET PAST PFK NUMBER
         BCTR  R2,0                RELATIVE PFK NUMBER
         SLL   R2,3                TIME LENGTH OF AN ENTRY
         LA    R1,QDPFKTAB(R2)     POINT TO TABLE ENTRY
         MVC   0(8,R1),0(R15)      SET NEW DEFINITION
         SPACE 1
*        DISPLAY THE CURRENT PFK DEFINITIONS
PFKDISP  MVC   QDHLINE,HEADING     SET HEADING
         LA    R2,QDPFKTAB         POINT TO TABLE
         LA    R3,1                FIRST ENTRY
         LA    R4,12               NUMBER OF ENTRIES
DISPLOOP MVC   QDMSG,QBLANK        BLANK THE OUTPUT LINE
         MVC   QDMSG+1(4),=X'40202120' SET MASK
         CVD   R3,DWORD            PFK NUMBER TO DECIMAL
         ED    QDMSG+1(4),DWORD+6  SET IN MESSAGE
         LR    R15,R3              GET PFK NUMBER
         BCTR  R15,0               DROP BY ONE
         SLL   R15,3               TIMES 8
         AR    R15,R2              POINT TO PFK TEXT
         MVC   QDMSG+8(8),0(R15)   COPY TEXT
         LA    R0,QDMSG            POINT TO MESSAGE
         ST    R0,QDMSGA           SAVE MSG POINTER
         MVC   QDMLNG,=H'80'       SET MESSAGE LENGTH
         L     R15,=V(DISPLAY)     WRITE THE
         BALR  R14,R15               MESSAGE
         LA    R3,1(,R3)           BUMP TO NEXT VALUE
         BCT   R4,DISPLOOP         LOOP FOR ALL PFK'S
         SPACE 1
PUTHELP  MVC   QDMLNG,=H'80'   SET MSG LENGTH
         LA    R2,PFKHMN       NUMBER OF LINES
         LA    R3,PFKHM        START OF TEXT AREA
PUTHELP1 ST    R3,QDMSGA       SET MESSAGE ADDRESS
         L     R15,=V(DISPLAY) ADDRESS OF DISPLAY ROUTINE
         BALR  R14,R15         LINK TO ROUTINE
         LA    R3,80(,R3)      POINT TO NEXT LINE
         BCT   R2,PUTHELP1     LOOP TILL DONE
         SPACE 1
         QSTOP
TILT     QTILT '*** PFK NUMBER IS INVALID ***'
MVZ      MVZ   QFZONES(*-*),QPARM1 CHECK FOR NUMERIC
PACK     PACK  DWORD,QPARM1(*-*)   CONVERT TO BINARY
HEADING  DC    CL80' PFK CURRENT DEFINITIONS'
PFKHM    EQU   *
 DC CL84' '
 DC CL80'TO SPECIFY OPERANDS (FOR PF5, AND OPTIONALLY FOR PF6, 9, 12),'
 DC CL80'OR TO TEMPORARILY OVERRIDE THE DEFAULTS (FOR PF7, 8, 10, 11),'
 DC CL80'KEY IN THE VALUE AND PRESS THE APPROPRIATE PF KEY.'
 DC CL80' '
 DC CL80'TO REDEFINE A PFK, ENTER "PFK # TEXT"'
 DC CL80'  WHERE # IS THE PFK NUMBER AND TEXT IS THE DESIRED STRING'
 DC CL4' '
PFKHMN   EQU   (*-PFKHM)/80    NUMBER OF MESSAGES
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
WORK     DSECT
         DS    18F
DWORD    DS    D                   SCRATCH DOUBLE WORD
         END
