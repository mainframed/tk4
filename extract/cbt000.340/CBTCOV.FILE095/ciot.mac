CIOT     TITLE 'QUEUE COMMAND - DUMP AN IOT IN HEX'                  CL
CIOT     QSTART ,                  START CIOT CSECT ASSEMBLY         CL
***********************************************************************
* IOT JOBNAME                                                         *
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JCT                                    *
*                                                                     *
***********************************************************************
         L     R15,=V(FINDJOB) ADDR OF MODULE TO FIND JOB
         BALR  R14,R15        GO TO IT
***********************************************************************
*                                                                     *
*   CALL HEXDUMP TO DUMP THE IOT                                      *
*                                                                     *
***********************************************************************
         L     R10,QVCKPT     BASE FOR CKPT WORK AREA
         SPACE 1                                                     CL
         USING QCKPT,R10      ADDRESSING FOR IT
         USING JCT,R6              JCT ADDRESSABILITY             ONL16
         USING WORK,R13            WORK AREA ADDRESSABILITY          CL
         USING IOT,R3              IOT ADDRESSABILITY                CL
         USING QDISPLAY,R9         DISPLAY WORK ADDRESABILITY        CL
         L     R9,QVDSPL           ADDR OF DISPLAY WORK AREA         CL
         SPACE 1                                                     CL
         MVI   SWITCH,0            CLEAR FLAG BYTE                   CL
         L     R6,QCJCTA      ADDRESS OF JCT
         LH    R5,HASPACE+62 MAX LEN INCLUDING NETWORK HEADERS
         LA    R5,IOTSTART-IOT(R5)    + LEN OF PREFIX
         LA    R4,IOTSTART-IOT      OFFSET TO START OF IOT           CL
         SR    R5,R4          TOTAL LENGTH - OFFSET = LENGTH TO DUMP
         SLL   R4,16          MOVE OFFSET TO PROPER POSITION
         OR    R4,R5          INSERT INTO LENGTH REG
         L     R1,QCJQEA           ADDR OF JQE                       CL
         USING JQE,R1              JQE ADDRESSABILITY                CL
         TM    JQEFLAG3,JQE3SYSD   SKIP IF THIS IS NOT               CL
         BNO   *+8                  SYSTEM JQE                       CL
         OI    SWITCH,NOSPIN       DO NOT READ SPIN IOTS             CL
         L     R3,QCIOTA           IOT BASE                          CL
         LR    R7,R3               IOAREA FOR READING IOTS           CL
         SH    R3,=Y(IOTSTART-IOT) ADJUST FOR HEADER                 CL
         B     IOTREAD             FIRST IOT HAS BEEN READ           CL
         SPACE 1                                                     CL
READIOT  ICM   R14,15,IOTIOTTR     MTTR OF NEXT IOT                  CL
         BNZ   READISK             GO READ IT IF AVAILABLE           CL
         TM    SWITCH,NOSPIN       ALL THRU IF SPIN IOTS             CL
         BO    STOP                 HAVE BEEN READ                   CL
         OI    SWITCH,NOSPIN       REMEMBER SPIN IOT READ            CL
         ICM   R14,15,JCTSPIOT     MTTR OF FIRST SPIN IOT            CL
         BZ    STOP                EXIT IF NONE                      CL
         SPACE 1                                                     CL
READISK  ST    R14,QCTRAK          SET MTTR                          CL
         MVC   QDMSG,QBLANK        BLANK LINE                        CL
         MVC   QDMLNG,=H'80'       LENGTH OF LINE                    CL
         LA    R0,QDMSG            SET ADDRESS                       CL
         ST    R0,QDMSGA            OF MESSAGE LINE                  CL
         L     R15,=V(DISPLAY)     ADDR OF SCREEN DISPLAY RTN        CL
         BALR  R14,R15             GO OUTPUT ONE BLANK LINE          CL
         BALR  R14,R15             OUTPUT SECOND BLANK LINE          CL
         LR    R1,R7               ADDR OF IO AREA                   CL
         L     R15,=V(READSPC)     ADDR OF RTN TO READ HASPACE       CL
         BALR  R14,R15             GO TO IT                          CL
         SPACE 1                                                     CL
IOTREAD  CLC   IOTID,=CL4'IOT'     BR IF NOT                         CL
         BNE   BADIOT               GOOD IOT                         CL
         CLC   QPJOBID,IOTJBKEY    BR IF JOB                         CL
         BNE   BADIOT               KEYS DO NOT MATCH                CL
         LR    R1,R7               ADDR OF IO AREA                   CL
         LR    R0,R4               OFFSET AND LENGTH OF DATA         CL
         L     R15,=V(HEXDUMP) ADDRESS OF DUMP ROUTINE
         BALR  R14,R15        LINK TO IT
         B     READIOT             GO READ NEXT IOT                  CL
STOP     QSTOP
***********************************************************************
*                                                                     *
*   EXCEPTIONS AND RETURN                                             *
*                                                                     *
***********************************************************************
BADIOT   QTILT '*** INVALID IOT READ ***'
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
CIOT     CSECT ,                                                  UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN      CL
        $BUFFER
         IFGRPL
        $JQE
        $TAB                                                         CL
        $JCT
        $IOT
        $HASPEQU
         QCOMMON
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS       CL
WORK     DSECT                                                       CL
         DS    18A                 REG SAVE AREA                     CL
SWITCH   DS    X                   SWITCHES HERE                     CL
NOSPIN   EQU   X'80'               READ NO SPIN IOTS                 CL
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
