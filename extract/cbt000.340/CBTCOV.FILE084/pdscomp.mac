         PROC 2 DSNAME1 DSNAME2 SYSIN() TSO
           /*                                                        */
           /*  NOTE:                                                 */
           /*    THIS IS THE "COMPARX" CLIST; IT WAS PROVIDED BY     */
           /*    SAM LEPORE FROM WELLS FARGO BANK IN SAN FRANCISCO   */
           /*    AS AN EXAMPLE CLIST WHICH CAN BE INVOKED BY PDS     */
           /*    OR FROM NATIVE TSO.                                 */
           /*                                                        */
           /*    IF INVOKED BY PDS..., THE CLIST PREFIXES EACH       */
           /*    TSO COMMAND WITH "TSO" SO THAT PDS WILL PASS IT     */
           /*    TO TSO INSTEAD OF USING IT AS A PDS SUBCOMMAND.     */
           /*                                                        */
           /*    IN THE WELLS FARGO PDS GENERATION OF THE PDS        */
           /*    PROGRAM, &CCAR IS SET TO '%COMPARX' TO GIVE         */
           /*    THIS CLIST CONTROL FOR PDS COMPARE SUBCOMMANDS.     */
           /*                                                        */
           /*    SAMPLE PDS SESSION:                                 */
           /*      PDS LIB.CNTL                                      */
           /*      COMPARE MEMBERA MEMBERB SYSIN(INPUTX)             */
           /*                                                        */
           /*    EQUIVALENT TSO SESSION:                             */
           /*      COMPAREX LIB.CNTL(MEMBERA) LIB.CNTL(MEMBERB) +    */
           /*               SYSIN(INPUTX)                            */
           /*                                                        */
         IF &SUBSTR(1:3,&SYSPCMD   ) = PDS THEN +
            SET &TSO = TSO
         IF &STR(&SYSIN) = &STR() THEN +
            SET &SYSIN = &STR('NULLFILE')
         CONTROL NOMSG
         &TSO FREE F(SYSUT1 SYSUT2 SYSIN)
         CONTROL MSG
         &TSO ALLOC F(SYSUT1) DA(&DSNAME1) SHR
         &TSO ALLOC F(SYSUT2) DA(&DSNAME2) SHR
         &TSO ALLOC F(SYSIN) DA(&SYSIN) SHR
         IF &STR(&SYSIN) = &STR(*) THEN +
            WRITE ENTER COMPAREX CONTROL STATEMENTS:
         &TSO CALL 'SYS4.LINKLIB(COMPAREX)'
         &TSO FREE F(SYSUT1 SYSUT2 SYSIN)
         &TSO ALLOC F(SYSIN) DA(*) SHR
