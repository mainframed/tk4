         TITLE 'P D S  --  PDS DISPLAY, PATTERN              09/28/88'
***********************************************************************
***      DISPLAY SUBCOMMAND                                         ***
***                                                                 ***
***      PATTERN SUBCOMMAND    ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
DISPLAY  CSECT
         USING *,R8
         B     PATTERNZ                  **  MEMBER RANGE
         B     DISPLAY2                  **  ADDITIONAL GROUP MEMBERS
PATTERN  L     R8,=A(DISPLAY)            **  MEMBER PATTERN
         B     DISPLAYA
         SPACE 1
PATTERNZ OI    FLAGSAA,FMEMRANG          **  MEMBER RANGE SELECTION
         TM    FLAGSAA,FMEMBER1+FMEMBER2 DISPLAY RANGE?
         BNO   DISPLAYA                  NO, NO RANGE CHECK
         LH    R14,LMEMBER1
         CH    R14,LMEMBER2              DETERMINE MIN NAME LENGTH
         BNH   *+8
         LH    R14,LMEMBER2
         CLC   MEMBER1(*-*),MEMBER2      <<EXECUTED>>
         EX    R14,*-6                   VALID DISPLAY RANGE?
         LA    R1,L700                   INVALID RANGE MESSAGE
         BH    MSGNEWXX                  YES, BRANCH
         SPACE 1
DISPLAYA LA    R1,80                     ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON            ISPMODE ACTIVE?
         BO    DISPLAY0                  YES, BRANCH
         TM    CONTOPTN,1                ANY LOG RECORDING?
         BO    DISPLAY0                  YES, BRANCH
*        GTSIZE ,                        TERMINAL SIZE         SS MAY89
         GTSIZE
         CH    R1,=H'120'                120 OR LESS BYTES?
         BL    *+8                       YES, BRANCH
         LH    R1,=H'120'                NO, USE 120 BYTES
DISPLAY0 ST    R1,LINESIZE               CHARACTERS/LINE
         NI    FLAGSBB,FF-FEXIST-FLINESET-FRANPAT
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 1
DISPLAY1 L     R4,LINESIZE               GET TERMINAL LINE SIZE
         LA    R4,MSGTEXT1+3-11(R4)      END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4             START OF LINE
         MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         SPACE 1
DISPLAY2 BAL   R14,READDIR               GET NEXT DIRECTORY ENTRY
         B     DISPLAY6                  LAST MEMBER IN DIRECTORY
         SPACE 1
         OI    FLAGSBB,FEXIST            MEMBER EXISTS FLAG
         BAL   R2,MFILTER            CHECK MEMBER NAMES
         B     DISPLAY6                00 - SIMULATE DIRECTORY END
         B     DISPLAY2                04 - MEMBER FAILED FILTERING
         B     DISPLAY4                08 - MEMBER PASSED FILTERING
         SPACE 1
DISPLAYT TRT   MEMNAME(*-*),TRTMEM       <<EXECUTED>>
DISPLAY4 TM    FLAGSAA,FMEM#MEM          MEMBER NAME GROUP DESIRED?
         BO    MEMSNEXT                  YES, FOUND A MEMBER
         LA    R1,7                      MEMBER NAME MACHINE LENGTH
         LA    R14,MEMNAME+8             END OF MEMBER NAME +1
DISPLAYB BCTR  R14,0                     SCAN
         CLI   0(R14),X'40'                  BACKWARDS
         BNE   *+8                                    FOR A
         BCT   R1,DISPLAYB                                 NON-BLANK
         CLI   MEMNAME,C'0'              FIRST DIGIT NUMERIC?
         BNL   *+12                      YES, FORCE A HEX DISPLAY
         EX    R1,DISPLAYT               ANY INVALID OR UNPRINTABLE?
         BZ    DISPLAY8                  NO, BRANCH
         TM    FLAGSBB,FLINESET          LINE IN PROGRESS?
         BZ    DISPLAY9                  NO, SKIP THE PUTLINE
         M$ERRST MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         SPACE 2
DISPLAY9 MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         UNPK  MSGTEXT1+4(9),MEMNAME(5)  FIRST HALF OF NAME
         UNPK  MSGTEXT1+12(9),MEMNAME+4(5) SECOND HALF OF NAME
         TR    MSGTEXT1+4(16),TRTABLE    FINISH TRANSLATION
         MVC   MSGTEXT1+20(8),BLANKS     CLEAN UP GARBAGE
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    *+10                      NO, SKIP -A
         MVC   MSGTEXT1+20(2),=C'-A'     YES, SET -A
         MVI   MSGTEXT1+24,C'*'
         MVC   MSGTEXT1+25(8),MEMNAME    MEMBER NAME FROM DIRECTORY
         TR    MSGTEXT1+25(8),TRLINE     TRANSLATE TO PRINTABLE
         MVI   MSGTEXT1+33,C'*'
         OI    FLAGSBB,FLINESET+FRANPAT  OUTPUT AND MEMBERS EXIST NOW
         B     DISPLAY5
         SPACE 1
DISPLAY8 MVC   0(8,R5),MEMNAME
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    DISPLAY7                  NO, BRANCH
         MVC   8(2,R5),=C'-A'
DISPLAY7 OI    FLAGSBB,FLINESET+FRANPAT
         LA    R5,10+2(R5)
         CR    R5,R4                     LINE FULL?
         BL    DISPLAY2                  NO, CONTINUE
         SPACE 1
DISPLAY5 M$ERRST MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         B     DISPLAY1                  RETURN
         SPACE 2
DISPLAY6 TM    FLAGSBB,FLINESET          OUTPUT LINE IN PROGRESS?
         BNO   DISPLAYX                  NO, BRANCH
         LA    R1,MSGTEXT1
         TM    FLAGSAA,FMEM#MEM          MEMBER GROUP IN PROGRESS?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DISPLAYX TM    FLAGSBB,FRANPAT           MEMBER IN RANGE?
         BO    DISPLAYZ                  YES, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE ANY MEMBER GROUP
         TM    FLAGSBB,FEXIST            NO, BUT ANY IN DIRECTORY?
         LA    R1,L400
         BZ    MSGNEW                    NO, ** EMPTY DIRECTORY **
         LA    R1,L711                   ASSUME NONE IN RANGE
         TM    FLAGSAA,FMEMRANG          CORRECT?
         BO    MSGNEW                    YES, BRANCH
         LA    R1,L713                   ASSUME NO MATCH PARTIAL NAME
         TM    FLAGSAA,FMEMASTR          CORRECT?
         BO    MSGNEW                    YES, BRANCH
         LA    R1,L712                   NO, NONE MATCHING PATTERN
         B     MSGNEW
         SPACE 1
DISPLAYZ TM    FLAGSAA,FMEM#MEM          MEMBER GROUP?
         BNO   NEWCMD                    NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE THE MEMBER GROUP
         B     NEWCMD
