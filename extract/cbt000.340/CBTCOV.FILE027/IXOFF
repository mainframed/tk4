00010000PROC 1 VOLSER DEBUG
00020000IF &DEBUG = DEBUG THEN CONTROL LIST CONLIST SYMLIST NOFLUSH NOPROMPT MSG
00030000                  ELSE CONTROL NOLIST NOCONLIST NOSYMLIST FLUSH NOPROMPT NOMSG
00040000 
00050000 /***********************************************************/
00060000 /*  NAME: IXOFF                                            */
00070000 /*  FUNCTION: EXECUTED BY TSSO WHENEVER MESSAGE IEC606I    */
00080000 /*            IS INTERCEPTED INDICATING THAT AN INDEXED    */
00090000 /*            VTOC HAS BEEN DISABLED.                      */
00100000 /*            A JOB STEP WILL BE BUILT AND ADDED TO        */
00110000 /*            DATASET A9.SM.IXOFF.DATA TO CAUSE THE        */
00120000 /*            INDEX TO BE REBUILT.                         */
00130000 /*                                                         */
00140000 /* CREATED 08/27/85  (LIONEL DYCK)                         */
00150000 /*                                                         */
00160000 /* CHANGE HISTORY:                                         */
00170000 /*    9/04/85  -  ADD SEND TO $A1238 AND $A2412            */
00180000 /*    9/27/85  -  REMOVED SEND TO $A2412                   */
00181001 /*   12/02/87  -  ADD $A1298 (G. BELL) TO SEND             */
00190000 /***********************************************************/
00200000 
00210000 OSWTO *** THE INDEXED VTOC ON VOLUME &VOLSER HAS BEEN DISABLED
00220000 OSWTO *** THIS IS BEING LOGGED FOR STORAGE MANAGMENET ACTION.
00230000 
00240002SHOWTSO SMFID=SYSID
00250000 
00260000SET &CPU = &SUBSTR(2:2,&SMFID)   /* EXTRACT 1 CHARACTER CPU ID */
00270000 
00280000EDIT 'A9.SM.IXOFF.DATA(&CPU)' DA NONUM
00290000BOTTOM
00300000INSERT //     EXEC PGM=ICKDSF
00310000INSERT //*** CPU=SMFID  DATE=SYSDATE  TIME=SYSTIME
00320000INSERT //STEPLIB  DD DISP=SHR,DSN=SYS1.APFLIB
00330000INSERT //SYSPRINT DD SYSOUT=A
00340000INSERT //DDA      DD UNIT=SYSALLDA,VOL=SER=&VOLSER,DISP=OLD
00350000INSERT //SYSIN    DD *
00360000INSERT   BUILDIX DDNAME(DDA) IX
00370000TOP
00380000FIND 'SMFID'
00390000CHANGE  * {SMFID{&SMFID{
00400000CHANGE  * {SYSDATE{&SYSDATE{
00410000CHANGE  * {SYSTIME{&SYSTIME{
00420000END SAVE
00430000 
00440000 
00450000EXITA: +
00460000 SEND '&VOLSER LOST ITS INDEX ON CPU &SMFID &SYSDATE &SYSTIME' USER($A1238 ) LOGON
00470001 SEND '&VOLSER LOST ITS INDEX ON CPU &SMFID &SYSDATE &SYSTIME' USER($A1298 ) LOGON
