00010012PROC 1 USERID TSO
00020015 
00030015SET USERID = &USERID  /* STRIP OFF TRAILING BLANKS*/
00040015 
00050001SET SYSOUTTRAP = 100
00060001  ACF
00070001  L &USERID
00080001  END
00090001SET SYSOUTTRAP = 0
00100001SET TRAPKNT = &SYSOUTLINE
00110001 
00120001SET LINE = &&SYSOUTLINE1
00130001SET L = &LENGTH(&STR(&LINE))
00140001IF &SUBSTR(2:8,&STR(&LINE)) ^= &STR(&USERID) THEN DO
00150013  IF &TSO = TSO +
00160016    THEN WRITE USERID  &USERID  NOT FOUND!
00170016    ELSE OSWTO  USERID  &USERID  NOT FOUND!
00180001  EXIT
00190001END
00200015SET N = &SYSINDEX(&STR(&USERID),&STR(&LINE),9)
00210015SET NF = &N + &LENGTH(&USERID)
00220015IF &NF < &L THEN SET NAME = &SUBSTR(&NF:&L,&STR(&LINE))
00230003 
00240002SET LP  = &STR((
00250014SET RP  = )
00260002SET MSC = &STR(MAILCODE&LP)
00270002SET PHC = &STR(PHONE&LP)
00280001 
00290001SET KNT = 2
00300001DO WHILE(&KNT <= 5)
00310003  SET LINE = &&SYSOUTLINE&KNT
00320001  SET PL = &SYSINDEX(&STR(&PHC),&STR(&LINE),1)
00330001  IF &PL > 0 THEN DO
00340014    SET PL = &SYSINDEX(&STR(&LP),&STR(&LINE),&PL)
00350005    SET PR = &SYSINDEX(&STR(&RP),&STR(&LINE),&PL)
00360014    IF &PL+1 <= &PR-1 THEN +
00370014      SET PHONE = &SUBSTR(&PL+1:&PR-1,&STR(&LINE))
00380001  END
00390001  SET ML = &SYSINDEX(&STR(&MSC),&STR(&LINE),1)
00400001  IF &ML > 0 THEN DO
00410014    SET ML = &SYSINDEX(&STR(&LP),&STR(&LINE),&ML)
00420005    SET MR = &SYSINDEX(&STR(&RP),&STR(&LINE),&ML)
00430014    IF &ML+1 <= &MR-1 THEN +
00440014      SET MAILCODE = &SUBSTR(&ML+1:&MR-1,&STR(&LINE))
00450001  END
00460001  SET KNT = &KNT + 1
00470001END
00480001 
00490013IF &TSO = TSO +
00500017  THEN WRITE USER &USERID IS &NAME. &PHONE &MAILCODE
00510017  ELSE OSWTO  USER &USERID IS &NAME. &PHONE &MAILCODE
