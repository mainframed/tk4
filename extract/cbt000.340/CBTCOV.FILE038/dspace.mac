*)F FUNCTION -
*  THE DSPACE COMMAND  DISPLAYS THE AMOUNT OF FREE SPACE ON A VOLUME.
*)X SYNTAX -
*         DSPACE 'VOLLIST'
*  REQUIRED - 'VOLLIST'
*  DEFAULTS - NONE
*  ALIAS    - DS
*)O OPERANDS -
*  'VOLLIST' - A SINGLE VOLUME OR A LIST OF VOLUMES TO BE LISTED.
         EJECT
DSPACE   CSECT
         SAVE  (14,7),,*
         LR    R7,R15
         USING DSPACE,R7
*
**  FIND START AND CORRECT LENGTH OF PARAMETER LIST
*
         L     R2,0(R1)   LOAD A(PARAMETER LIST)
         LH    R3,0(R2)   LOAD L'(PARM LIST)
         LH    R1,2(R2)   LOAD L'OFFSET
         LA    R1,4(R1)   LOAD L'(OFFSET+HEADER)
         SR    R3,R1   CORRECT L'PARMS
         AR    R2,R1   A(START OF PARMS)
         LTR   R4,R3
         BZ    END   NONE PRESENT
         BCTR  R4,R0   DECREMENT
         EX    R4,ORCHAR   CONVERT TO UPPER CASE
*
**  FIND A VOLUME SERIAL NUMBER
*
LOOP1    MVC   VOLID(7),BLANKS   BLANK OUT AREA
         LR    R4,R2   INITILIZE COUNTER
LOOP2    CLI   0(R4),C'A'   ASSUME NO SPECIAL CHARACTERS
         BL    FNDVOL
         LA    R4,1(R4)   INCREMENT
         BCT   R3,LOOP2   TRY AGAIN
*
**  VOLUME FOUND - VERIFY AND MOVE TO OUTPUT AREA
*
FNDVOL   SR    R4,R2   FIND L'NAME
         BZ    INCR2   CHECK FOR ZERO LENGTH
         BCTR  R4,R0
         CH    R4,=H'5'   TEST MAXIMUM LENGTH
         BH    LENERR   TOO LONG - WRITE OUT NOT MOUNTED MESSAGE
         EX    R4,MVCVOL
         B     FNDUCBAD
LENERR   MVC   NMNT(7),0(R2)   MOVE FIRST 7 CHARACTERS TO MESSAGE AREA
         B     PUTNMSG   WRITE IT OUT
*
**  FIND THE A(UCB)
*
FNDUCBAD L     R5,16   A(CVT)
         L     R5,40(R5)   A(UCB ADDRESSES)
NEXTUCB  LH    R6,0(R5)   A(A UCB)
         LTR   R6,R6   CHECK FOR VALID ENTRIES
         BZ    INCR1   UCB HOLE
         BM    NOTMNT   END OF UCB LIST - VOLUME NOT FOUND
         CLC   28(6,R6),VOLID   CHECK FOR MY VOLUME
         BE    FNDUCB
INCR1    LA    R5,2(R5)
         B     NEXTUCB   TRY NEXT UCB
*
**  UCB FOUND - NOW GET VOLUME SPACE
*
FNDUCB   LR    R0,R6   A(UCB)
         LA    R1,WORK   A(WORK SPACE)
         SVC   78   ISSUE LSPACE SVC
*
**  REFORMAT SPACE IF RETURN CODE IS ZERO
*
**  NOTE - THE LSPACE SVC FORMATS THE AVAILABLE SPACE IN EBCDIC
*          BUT THE FIELDS ARE NOT LABELED.
*
         MVC   ADDR(3),13(R6)   MOVE UNIT ADDRESS
         LTR   R15,R15   TEST RETURN CODE FROM LSPACE
         BNZ   LSPCERR   ERROR
         XR    R5,R5
         IC    R5,19(R6)   LOAD UNIT TYPE CODE
         SLL   R5,2   *4
         LA    R5,UNITABLE(R5)   A(UNITYPE)
         MVC   UNIT(4),0(R5)   MOVE UNIT TYPE
         MVC   SPCYL(4),WORK+6   MOVE SPACE - CYL
         MVC   SPTRK(4),WORK+11   MOVE SPACE - TRK
         MVC   EXTS(4),WORK+16   MOVE NUMBER OF EXTENTS
         MVC   EXCYL(4),WORK+21   MOVE EXTENT - CYL
         MVC   EXTRK(4),WORK+26   MOVE EXTENT - TRK
*
**  REREAD FORMAT4 DSCB TO CHECK FOR VTOC ERRORS
*
         OBTAIN FORMAT4
         TM    DSCB+58,X'8C'   TEST FOR VTOC ERRORS
         BZ    PUTSPACE   NO ERRORS
         MVI   VOLID+6,C'*'   INDICATE PRESENCE OF ERRORS
*
**  WRITE IT OUT
*
PUTSPACE TPUT  LINE,78
         B     NEXTVOL   FIND NEXT VOLUME
*
**  NOT MOUNTED ERROR
*
NOTMNT   MVC   NMNT(7),VOLID
PUTNMSG  TPUT  NMNT,L'NMNT
         B     NEXTVOL
*
**  LSPACE ERROR - WRITE ALTERNATE ERROR MESSAGE
*
LSPCERR  MVC   LERRMSG(11),LINE   MOVE VOL AND ADDR ONLY
         TPUT  LERRMSG,L'LERRMSG
*
**  CHECK FOR END OR NEXT VOLUME
*
NEXTVOL  LTR   R3,R3
         BNP   END
         LA    R2,2(R4,R2)   INCREMENT R2 PAST LAST VOLUME
         BCT   R3,LOOP1
         B     END   FINISHED
INCR2    LA    R2,1(R2)
         BCT   R3,LOOP1
*
**  END OF REQUEST
*
END      RETURN (14,7),RC=0
*
**  DATA
*
ORCHAR   OC    0(0,R2),BLANKS
MVCVOL   MVC   VOLID(0),0(R2)
BLANKS   DC    CL130' '
UNITABLE DC    C'    '    TYPE 00
         DC    C'2311'    TYPE 01
         DC    C'2301'    TYPE 02
         DC    C'2303'    TYPE 03
         DC    C'2302'    TYPE 04
         DC    C'2321'    TYPE 05
         DC    C'2305'    TYPE 06
         DC    C'2305'    TYPE 07
         DC    C'2314'    TYPE 08
         DC    C'3330'    TYPE 09
         DC    C'    '    TYPE 0A
         DC    C'3350'    TYPE 0B
         DC    C'    '    TYPE 0C
         DC    C'3311'    TYPE 0D
         DC    C'    '    TYPE 0E
         DC    C'    '    TYPE 0F
*
**  MESSAGES
*
*  SPACE MESSAGE
*
LINE     EQU   *
VOLID    DC    CL7' '
ADDR     DC    CL4' '
UNIT     DC    CL5' '
         DC    C'SPACE/CYL='
SPCYL    DC    CL4' '
         DC    C',TRK='
SPTRK    DC    CL4' '
         DC    C',EXT='
EXTS     DC    CL4' '
         DC    C' LARGEST EXT/CYL='
EXCYL    DC    CL4' '
         DC    C',TRK='
EXTRK    DC    CL4' '
         DC    C'/'
*
*  NOT MOUNTED MESSAGE
*
NMNT     DC    CL18'       NOT MOUNTED'
*
*  LSPACE ERROR MESSAGE
*
LERRMSG  DC    0CL34' '
         DC    CL4' '   OVERLAP PART OF RETURNED MESSAGE
WORK     DC    CL30' '
*
**  CAMLST TO REREAD FORMATJ DSCB
*
FORMAT4  CAMLST SEARCH,DSCB,VOLID,DATA
DSCB     DC    44X'04'   NAME OF VTOC
DATA     DC    CL148' '
*
**  EQUATES
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   DSPACE
