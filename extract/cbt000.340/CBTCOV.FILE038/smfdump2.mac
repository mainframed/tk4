SMFDUMP2 TITLE 'SMF DUMP ROUTINE TO DUMP MAN_'
*/* REFER:  IPO1.SAMPLIB(SMFDUMP)
*/* COMPID: INST
*/* DOCL    THIS PROGRAM IS USED TO DUMP NON-EMPTY SMF DATASETS.
***********************************************************************
*                                                                     *
*             MODULE NAME = SMFDUMP2                                  *
*                                                                     *
*             DESCRIPTIVE NAME =  IPO SUPPLIED ROUTINE TO DUMP THE    *
*                SMF DATASETS                                         *
*                                                                     *
*             COPYRIGHT = NONE                                        *
*                                                                     *
*             FUNCTION =                                              *
*                DUMP SYS1.MAN_ DATASETS                              *
*                ACTIVE AND HAS DATA TO DUMP.                         *
*                                                                     *
*                OPERATION =                                          *
*                   CHECK THAT DUMPOUT DD IS PRESENT.                 *
*                   CHECK THAT DUMPOUT DEVICE TYPE IS DISK OR TAPE    *
*                   IF SMF RECORDING NOT ACTIVE DUMP SYS1.MANX THEN   *
*                     DUMP SYS1.MANY                                  *
*                   IF ALTERNATE SMF DATASET IS ELIGIBLE TO BE DUMPED *
*                     THEN DUMP IT                                    *
*                   IF PRIMARY SMF DATASET IF ELIGBLE TO BE DUMPED    *
*                     THEN SWITCH SMF AND DUMP WHAT WAS THE PRIMARY   *
*                     SMF DATASET                                     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE *
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          *
*                                                                     *
*                 RESTRICTIONS =                                      *
*                    BOTH SYS1.MANX AND SYS1.MANY MUST BE CATALOGED   *
*                    DATASETS ON DIRECT ACCESS DEVICES.               *
*                    DUMPOUT DEVICE MUST BE DISK OR TAPE.             *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 0 TO 3  = WORK REGISTERS               *
*                    REGISTERS 4 TO 6  = UNUSED                       *
*                    REGISTER   7      = LINKAGE TO INTERNAL ROUTINES *
*                    REGISTERS 8 TO 10 = UNUSED                       *
*                    REGISTER  11      = ADDRESSABILITY TO SMCA DSECT *
*                    REGISTER  12      = ADDRESSABILITY TO SMFDUMP2   *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER           *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        *
*                   BINARY ZEROES)                                    *
*                                                                     *
*             MODULE TYPE = PROCEDURE                                 *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = 1200 BYTES                             *
*                                                                     *
*                ATTRIBUTES = KEY 8, NON-REENTRANT,                   *
*                   PROBLEM PROGRAM STATE,AUTHORIZED                  *
*                                                                     *
*             ENTRY POINTS = SMFDUMP2 (ONLY ENTRY POINT)              *
*                                                                     *
*                LINKAGE =                                            *
*                                                                     *
*             INPUT = NONE                                            *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODES =                                       *
*                   00 = NORMAL RETURN                                *
*                   04 = NO SMF DATASETS AVAILABLE TO DUMP            *
*                   08 = IFASMFDP RETURNED NON-ZERO RETURN CODE       *
*                   12 = UNABLE TO SWITCH SMF RECORDING               *
*                   16 = DUMPOUT DD MISSING                           *
*                                                                     *
*             EXIT - ERROR = NONE - PROGRAM WILL RETURN WITH          *
*                CODE IN REGISTER 15                                  *
*                                                                     *
*                OUTPUT = NONE                                        *
*                                                                     *
*                RETURN CODE = ZERO                                   *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IFASMFDP, IKJEFF18                        *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = SMCA, CVT.                          *
*                                                                     *
*             TABLES = NONE                                           *
*                                                                     *
*             MACROS = SAVE, DEVTYPE, WTO, MODESET, RETURN, DYNALLOC, *
*                   STIMER, LINK, IEESMCA, IKJEFFDF.                  *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                DAIRFAIL SERVICE ROUTINE IS USED TO ISSUE MESSAGES.  *
*                THOSE ISSUED BY THE COMMAND ARE FOUND FOLLOWING THE  *
*                LABEL ERRMSGS.                                       *
*                                                                     *
*             ABEND CODES = NONE                                      *
*                                                                     *
***********************************************************************
         EJECT
SMFDUMP2 CSECT ,                       CSECT NAME DECLARED
*
*  CONSTANT EQUATES
*
CMDSVC   EQU   34                      SVC TO ISSUE COMMAND
CVTPTR   EQU   X'10'                   OFFSET OF CVT ADDRESS
CVTSMCA  EQU   196                     OFFSET OF SMCA PTR IN CVT
DASD     EQU   X'20'                   UCB DEVICE TYPE FOR DASD DEVICES
TAPE     EQU   X'80'                   UCB DEVICE TYPE FOR TAPE DEVICES
HALFMIN  EQU   30                      30 SECONDS (LOOP CNT)
ONEMIN   EQU   60                      60 SECONDS PER MINUTE (LOOP CNT)
*
K0       EQU   0                       CONSTANT 0
K1       EQU   1                       CONSTANT 1
K2       EQU   2                       CONSTANT 2
K4       EQU   4                       CONSTANT 4
K8       EQU   8                       CONSTANT 8
K12      EQU   12                      CONSTANT 12
K16      EQU   16                      CONSTANT 16
*
* REGISTER EQUATES
*
R00      EQU   00
R01      EQU   01
R02      EQU   02
R03      EQU   03
R04      EQU   04
R05      EQU   05
R06      EQU   06
R07      EQU   07
R08      EQU   08
R09      EQU   09
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,SMFDUMP2_&SYSDATE SAVE REGISTERS
         USING SMFDUMP2,R12            SET UP BASE ADDRESSABILITY
         LR    R12,R15                 LOAD BASE REG WITH ENTRY POINT
         LA    R14,SAVE                GET ADDRESS OF REGISTER SAVE
         ST    R13,K4(R14)             SAVE CALLER'S SAVE AREA ADDR
         ST    R14,K8(R13)             SAVE MY SAVE AREA ADDRESS
         LR    R13,R14                 LOAD SAVE AREA ADDRESS
*
* CHECK FOR DUMPOUT DD PRESENT
*
         DEVTYPE DDOUT,DEVAREA         GET DEVICE TYPE OF DUMPOUT DD
         LTR   R15,R15                 IS DUMPOUT DD PRESENT?
         BNZ   NODDOUT                 DD MISSING BR TO ERROR RTN
*
* CHECK DEVICE TYPE OF DUMPOUT DEVICE(TAPE OR DASD)
*
         CLI   DEVAREA+K2,DASD         IS DUMPOUT A DASD DEVICE
         BE    OKTODUMP                YES, BR TO CONTINUE PROCESSING
         CLI   DEVAREA+K2,TAPE         IS DUMPOUT A TAPE DEVICE
         BNE   NOCANDMP                NO, INVLAID DEVICE TYPE BR
*
* CHECK IF ALTERNATE SMF DATASET IS ELIGIBLE TO BE DUMPED
*
OKTODUMP L     R11,CVTPTR(K0,K0)       LOAD ADDRESS OF CVT
         L     R11,CVTSMCA(,R11)       GET ADDRESS OF SMCA
         USING SMCABASE,R11            GET SMCA BASE ADDRESS
         EJECT
         LA    R09,2                   NUMBER OF TIMES TO DO THIS
*        CLC   SMCAFRDS(4),SMCASRBR    IS THE FIRST DATASET ACTIVE?
*        BNE   GETRDS                  NO
*        LA    R09,3                   UP NUMBER OF TIMES BY ONE
         B     GETRDS                  SKIP SMF SWITCH
*
* ISSUE OPERATOR 'SWITCH SMF' COMMAND AND WAIT FOR COMPLETION
*
SMFSWTCH ENQ   (SMFQNAME,SMFRNAME,E,,SYSTEM) ENQ ON SMF SMFWRITER-Q  #
         MODESET MF=(E,SUPRMOD)        GET IN SUPR STATE FOR SVC 34  #
         SLR   R00,R00                 CLEAR REG 0
         LA    R01,CMDISMF             GET ADDRESS OF COMMAND I SMF
         SVC   CMDSVC                  ISSUE COMMAND VIA SVC 34
         MODESET MF=(E,PROBMOD)        GET BACK IN PROB STATE
         STIMER WAIT,BINTVL=TENSEC     WAIT FOR TEN SECONDS
         LA    R03,ONEMIN              SET LOOP COUNTER TO LOOK FOR 1 M
CHKSWTCH CLC   SAVEARDS(4),SMCASRBR    HAS SWITCH OCCURED ?
         BNE   DEQ                     IF EQ SW HASN'T OCCURED - BR
SWWAIT   STIMER WAIT,BINTVL=ONESEC     WAIT FOR ONE SECOND
         BCT   R03,CHKSWTCH            LOOP BACK TO TRY AGAIN
         DEQ   MF=(E,DEQLIST)          DEQ ON SMF SMFWRITER-Q        #
*
         WTO   MF=(E,MSG002)           UNABLE TO SWITCH ERROR MESSAGE
         LA    R15,K12                 BAD RETURN CAN GO NO FURTHER
         B     EXITR                   RETURN TO CALLER'S CALLER
DEQ      DEQ   MF=(E,DEQLIST)          DEQ ON SMF SMFWRITER-Q        #
         SPACE
GETRDS   L     R10,SMCAFRDS            FIRST RDS
         USING RDSDSECT,R10
TESTRDS  TM    RDSFLAG1,RDSDUMP        DOES IT NEED TO BE DUMP?
         BZ    NEXTRDS
         MVC   MANSUFX(1),RDSDSN+8     MOVE IN SUFFIX FOR SYS1.MAN_
         BAL   R07,DALOC               CALL DUMP SUBROUTINE
NEXTRDS  L     R10,RDSNEXT             CHECK NEXT RDS ENTRY
         TM    RDSFLAG2,RDSLAST           TO SEE IF LAST
         BZ    TESTRDS                       IF NOT, KEEP LOOKING
         SPACE
         MVC   SAVEARDS(4),SMCASRBR    SAVE ACTIVE RDS
         TM    SMCAMISC,SMCAUSER+SMCAMAN IS SMF RECORDING
         BZ    EXIT                    NO, EXIT
         BCT   R09,SMFSWTCH
         EJECT
*
* RETURN BACK TO CALLER
*
EXIT     SLR   R15,R15                 SET NORMAL RETURN CODE
EXITR    L     R13,K4(R13)             LOAD CALLERS SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)        RETURN TO CALLER
         EJECT
*
NOCANDMP WTO   MF=(E,MSG008)           ELSE ISSUE ERROR MESSAGE
         LA    R15,K16                 LOAD RETURN CODE
         B     EXITR                   BR TO EXIT WITH RET. CODE
*
NODDOUT  WTO   MF=(E,MSG001)           ELSE ISSUE ERROR MESSAGE
         LA    R15,K16                 LOAD RETURN CODE
         B     EXITR                   BR TO EXIT WITH RET. CODE
*
*  ROUTINE TO ALLOCATE MAN_ DATASET AS DDNAME DUMPIN
*
DALOC    DS    0H                      ALLOC SUBROUTINE
         XC    FREEER(K4),FREEER       CLEAR DYNALLOC ERROR AREAS
         LA    R01,FREE                LOAD DYNALLOC PARM LIST
         DYNALLOC                      FREE DDNAME DUMPIN
         XC    ALOCER(K4),ALOCER       CLEAR DYNALLOC ERROR AREAS
         LA    R01,ALOC                LOAD PARM LIST ADDRESS
         DYNALLOC                      ALLOC MAN_ DATASET DD=DUMPIN
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   DYNERR                  IF ZERO THEN ALLOC OK
         LINK  SF=(E,LINKDUMP)         LINK TO SMF UTILITY
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   DUMPERR                 IF NOT ZERO THEN BR TO EXIT
         MVC   MSG007SX,MANSUFX        MOVE DSN SUFFIX TO MESSAGE
         WTO   MF=(E,MSG007)           ISSUE MESSAGE OF DATASET DUMPED
         XC    FREEER(K4),FREEER       CLEAR DYNALLOC ERROR AREAS
         LA    R01,FREE                LOAD DYNALLOC PARM LIST
         DYNALLOC                      FREE DDNAME DUMPIN
         BR    R07                     RETURN TO CALLER
*
DUMPERR  WTO   MF=(E,MSG006)           ISSUE DUMP FAILED MESSAGE
         LA    R15,K8                  SET RETURN CODE
         B     EXITR                   BR TO EXIT FROM SMFDUMP2
*
DYNERR   ST    R15,DRFLR15             SAVE RETCODE FOR DAIRFAIL
         LA    R01,DRFLPARM            LOAD DAIRFAIL PARM LIST ADDRESS
         XC    DRFLPARM(DFLEN),DRFLPARM CLEAR AREA FIRST
         USING DFDSECTD,R01            SET ADDRESSIBLITY TO PARM LIST
         LA    R15,ALOCRB              GET ADDRESS OF ALLOCATION RB
         ST    R15,DFS99RBP            SAVE IN DAIRFAIL PARM LIST
         LA    R15,DRFLR15             GET ADDRESS OF SAVED REG 15
         ST    R15,DFRCP               SAVE IN DAIRFAIL PARM LIST
         LA    R15,FULL0               GET ADDRESS OF OF DUMMY F02
         ST    R15,DFJEFF02            SAVE IN DAIRFAIL PARM LIST
         LA    R15,DRFLID              GET ADDRESS OF CALLER'S FLAGS
         ST    R15,DFIDP               SAVE IN DAIRFAIL PARM LIST
         DROP  R01
         LINK  SF=(E,LINKDRFL)         LINK TO DAIRFAIL SERVICE ROUTINE
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   DRFLERR                 IF NOT ZERO THEN GOTO DRFL ERR
         WTO   MF=(E,MSG005)           ELSE ISSUE DYNALLOC FAILED MSG
         LA    R15,K12                 SET RETURN CODE
         B     EXITR                   BR TO EXIT FROM SMFDUMP2
*
DRFLERR  WTO   MF=(E,MSG004)           ELSE ISSUE DAIRFAIL ERROR MSG
         LA    R15,K16                 SET RETURN CODE
         B     EXITR                   BR TO EXIT FROM SMFDUMP2
         EJECT
*
*  DATA AREA
*
         DS    0D                                                    #
SAVE     DC    18F'0'                  REGISTER SAVE AREA
SAVEARDS DC    F'0'                    SAVE ACTIVE RDS POINTER
SMFQNAME DC    CL8'IPOSMF01'           QNAME FOR IFASMFDP      SIPO50#
SMFRNAME DC    CL7'DATASET'            RNAME FOR IFASMFDP            #
DEQLIST  DEQ   (SMFQNAME,SMFRNAME,,SYSTEM),MF=L     SMF SMFWRITER-Q  #
CMDISMF  DC    Y(12),Y(00),CL08'I SMF' COMMAND (I SMF) SVC 34 INPUT
*
*  CONTROL FOR SVC 99 TO FREE DDNAME DUMPIN
*
FREE     DC    0F'0',X'80',AL3(FREERB)  RB POINTER
FREERB   DS    0F                      REQUEST BLOCK FO DYNALLOC
         DC    AL1(20),AL1(02),AL2(0)  LENGTH,UNALLOC_VERB,FLAGS
FREEER   DC    AL2(0)                  ERROR CODE
FREEIN   DC    AL2(0)                  INFO CODE
         DC    A(FREETP)               TXT UNIT LIST POINTER
         DC    A(0)                    RESEVRD
         DC    A(0)                    FLAGS
FREETP   CALL  ,(FREEDDN),VL,MF=L      TEXT UNIT POINTERS
FREEDDN  DC    XL2'1',XL2'1',XL2'8',CL8'DUMPIN' FREE DDNAME=DUMPIN
*
*  CONTROL FOR SVC 99 TO ALLOCATE DDNAME DUMPIN TO MAN_
*
ALOC     DC    0F'0',X'80',AL3(ALOCRB)  RB POINTER
ALOCRB   DS    0F
         DC    AL1(20),AL1(01),AL2(0)  LENGTH,ALLOC_VERB,FLAGS
ALOCER   DC    AL2(0)                  ERROR CODE
ALOCIN   DC    AL2(0)                  INFO CODE
         DC    A(ALOCTP)               TEXT UNIT LIST POINTER
         DC    A(0)                    RESERVED
         DC    A(0)                    FLAGS
ALOCTP   CALL  ,(ALOCDDN,ALOCDSN,ALOCDSP,ALOCVBS),VL,MF=L TU POINTERS
ALOCDDN  DC    XL2'1',XL2'1',XL2'8',CL8'DUMPIN' DDNAME=DUMPIN
ALOCDSN  DC    XL2'2',XL2'1',XL2'9',CL10'SYS1.MAN'  DSN=SYS1.MAN_
MANSUFX  EQU   ALOCDSN+14              SUFFIX FOR SYS1.MAN_ IN TEXT U.
ALOCDSP  DC    XL2'4',XL2'1',XL2'1',XL1'08'      DISP=SHR
ALOCVBS  DC    XL2'49',XL2'1',XL2'1',XL1'58'     RECFM=VBS
*
* PROGRAM CONSTANTS
*
ONESEC   DC    F'100'                  100 HUNDREDTHS SEC. FOR STIMER
TENSEC   DC    F'1000'                 1000 HUNDREDTHS SEC. FOR STIMER
DDOUT    DC    CL8'DUMPOUT'            OUTPUT DDNAME FOR TIOT SCAN
DEVAREA  DC    2F'0'                   OUTPUT FOR DEVTYPE MACRO
         IKJEFFDF DFDSECT=YES      DAIRFAIL PARM LIST MAP
SMFDUMP2 CSECT
FULL0    DC    F'0'                    ADDRESS OF NULL IKJEFF02 ROUTINE
DRFLR15  DC    A(0)
DRFLPARM DC    XL(DFLEN)'00'
DRFLID   DC    AL1(DFWTP),AL1(DFSVC99)
SAVEYORX DC    C' '                    SAVE AREA FOR ALT. MAN_ DATASET
*
* MODESETS ISSUED
*
SUPRMOD  MODESET KEY=ZERO,MODE=SUP,MF=L GET IN SUPR STATE FOR SVC 34
PROBMOD  MODESET KEY=NZERO,MODE=PROB,MF=L GET BACK IN PROB STATE
*
* LINKS ISSUED
*
LINKDRFL LINK  EP=IKJEFF18,SF=L        LINK TO DAIRFAIL SERVICE ROUTINE
LINKDUMP LINK  EP=IFASMFDP,SF=L        LINK TO IFASMFDP SMF DUMP RTN
*
* MESSAGES ISSUED
*
ERRMSGS  DS    0F
MSG001   WTO   'SMF001I SMFDUMP2 - DUMPOUT DD MISSING',                X
               ROUTCDE=11,MF=L
MSG002   WTO   'SMF002I SMFDUMP2 - SWITCH OF SMF FAILED TO OCCUR',     X
               ROUTCDE=11,MF=L
MSG003   WTO   'SMF003I SMFDUMP2 - NO SMF DATASETS ELIGIBLE FOR DUMP', X
               ROUTCDE=11,MF=L
MSG004   WTO   'SMF004I SMFDUMP2 - DYNAMIC ALLOCATION FAILED, AND DAIRFX
               AIL',ROUTCDE=11,MF=L
MSG005   WTO   'SMF005I SMFDUMP2 - DYNAMIC ALLOCATION FAILED',         X
               ROUTCDE=11,MF=L
MSG006   WTO   'SMF006I SMFDUMP2 - IFASMFDP PROGRAM FAILED TO DUMP SMF'X
               ,ROUTCDE=11,MF=L
MSG007   WTO   'SMF007I SMFDUMP2 - COMPLETED FOR DATASET SYS1.MAN_',   X
               ROUTCDE=11,MF=L
MSG007SX EQU   MSG007+53              SUFFIX FOR MAN_ DSN IN MSG
MSG008   WTO   'SMF008I SMFDUMP2 - DUMPOUT DD INVALID DEVICE TYPE ',   X
               ROUTCDE=11,MF=L
*
* PATCH AREA
*
         DS    0D
MODLEN   EQU   *-SMFDUMP2          EQUATE FOR MODULE LENGTH(- PATCH)
PTCHLEN  EQU   ((MODLEN+7)/8+7)/8*8 PATCH AREA LENGTH EQUATE
PTCHLNTH DC    Y(PTCHLEN)          LENGTH OF PATCH AREA
PTCHBASE DC    S(PATCH)            BASE DISPLACED ADDRESS OF PATCH
PATCH    DC    XL(PTCHLEN)'00'     PATCH AREA
         EJECT
         DS    0D
RDSDSECT DSECT
RDS      DS    CL224
         ORG   RDS+4
RDSNEXT  DS    A
         ORG   RDS+12
RDSFLAG1 DS    C
RDSFLAG2 DS    C
         ORG   RDS+16
RDSDSN   DS    CL9
RDSDUMP  EQU   X'08'               DUMP INDICATOR
RDSLAST  EQU   X'80'               LAST RDS INDICATOR
         EJECT
         IEESMCA ,                 SMCA DSECT IN SYS1.AMODGEN
         END   SMFDUMP2
