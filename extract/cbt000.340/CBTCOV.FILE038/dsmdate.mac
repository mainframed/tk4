* SYSTEM NAME:        DSM - DASD SPACE MANAGEMENT SYSTEM
*
* PROGRAM NAME:       DSMDATE
*
* PROGRAM TITLE:      DSM REFORMAT AND DATE HISTORY FILE
*
* AUTHOR:             G. W. JACKSON - MEMOREX CORPORATION
*
* ABSTRACT:           THIS PROGRAM BUILDS A DSM HISTORY FILE.
*
* METHOD:             THIS PROGRAM USES THE RECORDS PRODUCED BY
*                     THE C.S.G. $MONVOL PROGRAM, SORTED BY
*                     VOLSER AND DSNAME. IT APPENDS A DATE RECORD
*                     TO THE FILE, THEN REFORMATS THE $MONVOL
*                     RECORDS FROM 133 TO 80 BYTES.  THE OUTPUT
*                     OF THIS PROGRAM IS A GENERATION DATASET.
*
* PARMS:              NONE
*
* JCL REQUIRED:       //STEP  EXEC  PGM=DSMDATE
*                     //SYSPRINT DD  SYSOUT=A
*                     //DSMOLD   DD  << OUTPUT FROM $MONVOL >>
*                     //DSMNEW   DD  << NEW HISTORY FILE >>
*                     //DSMOUT   DD  << ERROR REPORT >>
*
* HISTORY:   1 AUG 78 - FIRST VERSION.
*
*            6 SEP 78 - MODIFICATIONS: THIS COMMENT PAGE ADDED;
*                       LISTING CONTROL ADDED.
*
DSMDATE  START 0
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT NOGEN
BEGIN    SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         ST    R13,SAVE+4
         LA    R13,SAVE
         EJECT
***********************************************************************
*                                                                     *
*                  INITIALIZATION                                     *
*                                                                     *
***********************************************************************
         OPEN  (OLDFILE,INPUT,NEWFILE,OUTPUT,REPTFILE,OUTPUT)
         DAY   DATE8=TODAY
         MVC   NEWREC+6(8),TODAY
         PUT   NEWFILE,NEWREC
         EJECT
***********************************************************************
*                                                                     *
*                  MAIN FLOW                                          *
*                                                                     *
***********************************************************************
READFILE GET   OLDFILE,OLDREC          READ INPUT FILE
         MVC   NEWREC(6),OLDREC+109    MOVE PACK TO NEWREC
         MVC   NEWREC+6(44),OLDREC+1   MOVE DSNAME TO NEWREC
         MVC   NEWREC+50(4),OLDREC+46  MOVE DSORG TO NEWREC
         MVC   NEWREC+54(8),OLDREC+53  MOVE CREATE DATE TO NEWREC
         MVC   NEWREC+62(4),OLDREC+116 MOVE TRKS ALLC TO NEWREC
         MVC   NEWREC+66(4),OLDREC+121 MOVE TRKS USED TO NEWREC
         PUT   NEWFILE,NEWREC          WRITE OUTPUT FILE
         B     READFILE
         EJECT
***********************************************************************
*                                                                     *
*                  END OF JOB PROCESSING                              *
*                                                                     *
***********************************************************************
EOJRTN   CLOSE (OLDFILE,,NEWFILE,,REPTFILE)
         L     R13,SAVE+4
         RETURN (14,12)
         EJECT
***********************************************************************
*                                                                     *
*                  DCB'S                                              *
*                                                                     *
***********************************************************************
OLDFILE  DCB   DDNAME=DSMOLD,DSORG=PS,MACRF=GM,EODAD=EOJRTN
NEWFILE  DCB   DDNAME=DSMNEW,DSORG=PS,MACRF=PM
REPTFILE DCB   DDNAME=DSMOUT,DSORG=PS,MACRF=PM
         EJECT
***********************************************************************
*                                                                     *
*                  DATA DEFINITIONS                                   *
*                                                                     *
***********************************************************************
SAVE     DS    18F                     SAVE REGISTERS
TIMEWORK DS    F
RUNTIME  DS    CL5
RUNDATE  DS    CL5
OLDREC   DS    CL133
NEWREC   DC    80C' '
TODAY    DS    CL8
         END   BEGIN
