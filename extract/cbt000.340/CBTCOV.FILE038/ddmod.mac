         TITLE 'D D M O D  --  DDNAME MODIFICATION SUBROUTINE'
         ENTRY DDMODIN
         ENTRY DDMODOUT
         SPACE 5
DDMOD    CSECT
         SPACE 2
DDMODOUT EQU   *
         USING *,15                    REG 15 WILL POINT TO THIS PGM
         B     10(0,15)                BRANCH AROUND ID
         DC    AL1(5)                  LENGTH OF ID
         DC    CL5'DDMOD'              PGM IDENTIFIER
         STM   14,12,12(13)            SAVE REGISTERS
         ST    13,SAVEAREA+4           KEEP REG 13 FOR CALLING PROGRAM
         LR    12,13                   KEEP CALLING PGM SAVEAREA ADDR
         LA    13,SAVEAREA             PRIME PGM BASE REG WITH SA ADDR
         ST    13,8(12)                SET UP SAVEAREA BACK CHAIN
         B     GOTOIT                  BEGIN PROCESSING
         SPACE 5
DDMODIN  EQU   *                       ENTRY POINT FOR INPUT DATA SETS
         USING *,15                    REG 15 WILL POINT TO THIS PGM
         STM   14,12,12(13)            SAVE REGISTERS
         ST    13,SAVEAREA+4           KEEP REG 13 FOR CALLING PROGRAM
         LR    12,13                   KEEP CALLING PGM SAVEAREA ADDR
         LA    13,SAVEAREA             PRIME PGM BASE REG WITH SA ADDR
         ST    13,8(12)                SET UP SAVEAREA BACK CHAIN
         MVI   OPEN+4,X'80'            INDICATE INPUT DATA SET IN OPEN
         B     GOTOIT                  BEGIN PROCESSING
         SPACE 5
SAVEAREA DS    18F                     REG SAVEAREA AND PGM ORIGIN
         SPACE 5
GOTOIT   EQU   *
         DROP  15                      RELEASE TEMP BASE REGISTER
         USING SAVEAREA,13             SET UP REG 13 / SAVEAREA AS BASE
         EJECT
         XC    FLAGS,FLAGS             INITIALIZE ALL FLAGS TO ZERO
         L     DDREG,0(1)              GET ADDR OF DDNAME FROM CALLER
         L     WKREG,16                LOAD ADDRESS OF CVT
         L     WKREG,0(WKREG)          LOAD POINTER TO TCB ADDR
         L     TCBREG,4(WKREG)         LOAD ADDR OF CURRENT TCB
         L     TIOTREG,12(TCBREG)      LOAD ADDR OF TIOT
         L     DEBREG,8(TCBREG)        LOAD ADDR OF FIRST DEB IN CHAIN
LOOP     L     DCBREG,24(DEBREG)       LOAD ADDR OF DCB FROM DEB
         LH    WKREG,40(DCBREG)        LOAD TIOT OFFSET FOR THIS DDNAME
         AR    WKREG,TIOTREG           ADD TIOT OFFSET TO TIOT ADDR
         CLC   0(6,DDREG),4(WKREG)     IS THIS DEB ASSOC WITH MY DDNAME
         BE    DDMATCH                 IF SO, BRANCH TO PROCESS THE DCB
         L     DEBREG,4(DEBREG)        OTHERWISE, LOAD ADDR OF NEXT DEB
         LA    DEBREG,0(DEBREG)        ZERO OUT REMAINDER OF REGISTER
         LTR   DEBREG,DEBREG           IS THIS THE END OF THE DEB CHAIN
         BNZ   LOOP                    NO, CONTINUE LOOKING FOR DDNAME
         BAL   BRREG,NODDCARD          END OF DEB CHAIN, INDICATE ERROR
         B     RETURN                  GO TO PGM EXIT ROUTINE
         EJECT
DDMATCH  ST    DCBREG,DCBADDR          MOVE DCB ADDR TO STORAGE
         MVC   CLOSE+5(3),DCBADDR+1    MOVE DCB ADDR TO CLOSE ROUTINE
         MVC   OPEN+5(3),DCBADDR+1     MOVE DCB ADDR TO OPEN ROUTINE
CLOSE    CLOSE (DUMMY)                 CLOSE DCB ASSOC WITH THE DDNAME
         PACK  PACK,46(2,DCBREG)       PACK LAST TWO CHAR OF DDNAME
         AP    PACK,ONE                ADD 1 TO THIS FIELD
         UNPK  46(2,DCBREG),PACK       AND STORE NEW DDNAME IN THE DCB
         OI    47(DCBREG),X'F0'        FORCE GOOD ZONE OVER NUMBER
         LA    WKREG,24(TIOTREG)       GET FIRST ENTRY IN TIOT
NEXTDD   L     WKREG2,0(WKREG)         GET LENGTH OF
         SRL   WKREG2,24                             THIS DD ENTRY
         LTR   WKREG2,WKREG2           IS THIS THE LAST TIOT ENTRY
         BZ    NOTFOUND                BRANCH IF LAST ONE
         CLC   40(8,DCBREG),4(WKREG)   OTHERWISE, CHECK DDNAME
         BE    OPEN                    AND CONTINUE IF DDNAME FOUND
         AR    WKREG,WKREG2            OR GO TO NEXT TIOT ENTRY
         B     NEXTDD                  AND CONTINUE LOOKING
NOTFOUND BAL   BRREG,NODDCARD          GO TELL OPERATOR OF ERROR
         PACK  PACK,46(2,DCBREG)       PACK LAST TWO CHAR OF DDNAME
         SP    PACK,ONE                SUBTRACT 1 FROM THIS FIELD
         UNPK  46(2,DCBREG),PACK       RESTORE OLD DDNAME IN THE DCB
         OI    47(DCBREG),X'F0'        FORCE GOOD ZONE OVER NUMBER
OPEN     OPEN  (DUMMY,(OUTPUT))        OPEN DCB ASSOC WITH THIS DDNAME
         B     RETURN                  GO TO PGM EXIT ROUTINE
         EJECT
NODDCARD OI    FLAGS,DDERROR           SET DDNAME ERROR FLAG ON
         CLI   OPEN+4,X'80'            IS THIS AN INPUT DATA SET
         BE    RETURN                  IF SO, DON'T ISSUE AN ERROR MSG
         MVC   WTO+16(8),0(TIOTREG)    MOVE JOBNAME TO MESSAGE AREA
         MVC   WTO+25(8),8(TIOTREG)    MOVE STEPNAME TO MESSAGE AREA
         MVC   WTO+37(8),40(DCBREG)    MOVE DDNAME TO MESSAGE AREA
WTO      WTO   'MRX001I JJJJJJJJ.SSSSSSSS - ''DDDDDDDD'' DD CARD MISSING
               G - DDNAME SWITCH IGNORED - '  TELL OPERATOR OF ERROR
         BR    BRREG                   RETURN TO CALLING ROUTINE
         SPACE 5
RETURN   TM    FLAGS,DDERROR           TEST FOR PREVIOUS DDNAME ERROR
         BO    RC16                    IF SO, INDICATE ERROR IN RETURN
         L     13,SAVEAREA+4           ADDR OF CALLING PGM'S SAVEAREA
         RETURN  (14,12),T,RC=0        NORMAL RETURN TO CALLING PROGRAM
RC16     L     13,SAVEAREA+4           ADDR OF CALLING PGM'S SAVEAREA
         RETURN  (14,12),T,RC=16       RETURN AND INDICATE DDNAME ERROR
         EJECT
FLAGS    DS    C                       FLAG BYTE
ONE      DC    PL2'1'                  USED TO INCREMENT DDNAME
PACK     DS    CL2                     WORK AREA USED TO CONVERT DDNAME
DCBADDR  DS    F                       TEMP STORAGE FOR DCB ADDRESS
         SPACE 2
DDERROR  EQU   X'01'                   DDNAME ERROR FLAG BIT
DUMMY    EQU   *                       TEMP DUMMY NAME FOR OPEN MACRO
         SPACE 2
WKREG2   EQU   2                       GENERAL PURPOSE WORK REGISTER
WKREG    EQU   3                       GENERAL PURPOSE WORK REGISTER
DDREG    EQU   4                       ADDR OF DDNAME FROM CALLER
TIOTREG  EQU   5                       ADDR OF TIOT FOR THIS TCB
TCBREG   EQU   6                       ADDR OF TCB FOR THIS TASK
DEBREG   EQU   7                       ADDR OF EACH DEB IN CHAIN
DCBREG   EQU   8                       ADDR OF DCB ASSOC WITH EACH DEB
BRREG    EQU   12                      USED FOR 'BAL' WITHIN THIS PGM
         SPACE 2
         END
