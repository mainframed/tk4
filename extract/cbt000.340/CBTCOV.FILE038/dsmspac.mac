*-SYSTEM NAME :      UTILITY
*-
*-PROGRAM NAME :     SPACERPT
*-
*-AUTHOR :           N . RUINARD
*-                   MODIFIED BY G. W. JACKSON
*-
*-ABSTRACT :
*-                   THIS PROGRAM IS A REPORT PROGRAM TO LIST
*-                   DISK SPACE ALLOCATION BY HIGHEST LEVEL BREAKDO
*-                   AND PROVIDES A DEATILED OR A SUMMARY REPORT ,
*-                   AND A SUMMARY PLOT
*-
*-
*-METHOD :           INPUT TO THIS PROGRAM IS THE OUTPUT OF THE
*-                   C.S.G. $MONVOLS PROCEDURE , AND SORTED BY NAME .
*-                   THE VOLSERS OF THE DISKPACKS ARE CONTROLLED
*-                   BY SYSIN OF THE MONVOL (FIRST) STEP .
*-
*-LIMITATION :       MAX NUMBER OF LEVELS IS SET AS AN ASSEMBLER
*-                   VALUE "NUMLEV" (5000) ;
*-
*-PARMFIELD :        PARM='A-'  FOR DETAILED REPORT .
*-                   PARM='S-'  FOR SUMMARY  REPORT .
*-                   PARM='-R'  FOR RUN-ON   REPORT .
*-                   PARM='-B'  FOR BREAK ON INDEX LEVEL.
*-                   DEFAULT :  PARM='AR' .
*-
*-JCL REQUIRED :     //STEP  EXEC PGM=SPACERPT,REGION=512K,PARM=
*-                   //SYSUT1  DD  <<INPUT DATASET ; OUTPUT OF SORT>>
*-                   //PRINT   DD  SYSOUT=A
SPACERPT CSECT
         PRINT  NOGEN
         BEGINN  BASE=(R12,R11),SIZE=4000
         USING  WORK,R13
         SPACE 3
**
*        INIT VARIBALES
*
         MVC   PLINE,BL
         MVC   FLDCNT,=H'1'
         LA    R2,PLINE+1
         ST    R2,CPTR       INIT CURRENT FLD PTR FOR "S"
         MVC   LCNT(2),=H'100'  HIGH LINECOUNT
         XC    SUM,SUM
         XC    TSUM,TSUM
         XC    USUM,USUM
         XC    TUSUM,TUSUM
         XC    DSCNT,DSCNT
         MVC   OL,BL
         MVC   NL,BL
         XC    NRVOLS,NRVOLS   CLR # VOLUMES
*
*
*
         MVC   PARM,=C'AR'    DEFAULT TO "AR" , PRINT ALL
         GSTRING R=GRES,ERR=SERR01
         LR    R2,R1
         OC    0(4,R2),0(R2)
         BZ    S0100        MISSING  PARMFLD : "A"
         L     R3,0(R2)     PTR TO STRING
         CLI   0(R3),C'A'
         BE    S0080
         CLI   0(R3),C'S'
         BNE   SERR01
S0080    CLI   1(R3),C'R'
         BE    S0090
         CLI   1(R3),C'B'
         BNE   SERR01
S0090    MVC   PARM(2),0(R3)
         SPACE 5
S0100    DAY   DATE8=TODAY
         SPACE 10
         L     R0,=A(NUMLEV*LSIZE)     SPACE REQ FOR LEVEL TABLE
         GETMAIN R,LV=(0)
         ST    R1,LTABS             START OF TABLE
         ST    R1,LTABC             CURRENT (NEXT TO USE) PTR IN LTAB
         L     R2,=A(NUMLEV*LSIZE)
         AR    R2,R1
         LA    R15,LSIZE
         SR    R2,R15
         ST    R2,LTABH             HIGH END OF LTAB
*
*        LTAB HAS THE FOLLOWING FORMAT :
*        DS    CL16     LEVEL NAME
*        DS    4X       BIN VALUE OF NUMBER OF TRAKSALLOCATED
*        DS    4X       BIN VALUE OF NUMBER OF TRAKS  USED
*
         MVC   HDR1+12(8),TODAY     SET UP HEADER FOR RPT
         EJECT
         OPEN  (PRINT,(OUTPUT),UT1,(INPUT))
         SPACE 3
S0120    GET   UT1
         LA    R2,1(R1)    POINT TO FIRST CHAR
         BAL   R10,SS01000   FIND OUT IF THIS IS A NEW LEVEL
*              R15=0 FOR SAME LEVEL
         LTR   R15,R15
         BZ    S0200       B. IF SAME LEVEL
         SPACE 3
         BAL   R10,SS02000   HANDLE LEVEL CHANGE
         SPACE 3
S0200    BAL   R10,SS03000   ADD TRACKS TO SUM
         CLI   PARM,C'A'
         BNE   S0250
         BAL   R10,SS04000   PRINT DETAIL LINE FOR "A"
S0250    B     S0120         AND FOR THE NEXT ONE
         SPACE 5
ENDUT1   LA    R2,1(R1)
         MVC   FLDCNT,=H'99'  FORCE SUMMARY ANYWAY
         BAL   R10,SS02000    PRINT SUMMARY ANYWAY
         SPACE 3
         MVC   PLINE,BL
         MVI   PLINE,C'-'
         LH    R4,LCNT
         LA    R4,10(R4)   PREPARE FOR TRAILER BOX
         CH    R4,MCNT     WILL BOX FITT ON THIS PAGE
         BNH   *+18        B. IF SO
         MVC   LCNT,=H'0' OTHERWISE SET UP FOR NEW PAGE
         MVI   PLINE,C'1'  SKIP  FOR NEW PAGE
         B     S0300
         LH    R4,LCNT
         LA    R4,2(R4)
         STH   R4,LCNT
S0300    BAL   R10,SPRINT   PRINT BL LINE
         LH    R4,LCNT
         LA    R4,2(R4)
         STH   R4,LCNT
         MVC   PLINE+1(132),=132C'+'
         BAL   R10,SPRINT
         MVC   PLINE,BL
         MVI   PLINE+1,C'+'
         BAL   R10,SPRINT
*        MVC   PLINE+50(40),GRTOTAL
*        CONVD  PLINE+90,TSUM,L=10
*        MVC   PLINE+104(6),=C'TRACKS'
*        BAL   R10,SPRINT
         MVC   PLINE+20(40),GRTOTAL
         CONVD  PLINE+56,TSUM,L=10
         MVC   PLINE+68(7),=C'TRACKS;'
         CONVD  PLINE+75,TUSUM,L=10
         MVC   PLINE+87(5),=C'USED;'
         L     R5,TUSUM
         LTR   R5,R5
         BZ    S0350
         SR    R4,R4
         MH    R5,=H'100'
         D     R4,TSUM                 CALCULATE PERCENTAGE.
S0350    CONVD  PLINE+94,(R5),L=3
         MVC   PLINE+99(9),=C'PER CENT;'
         CONVD  PLINE+109,DSCNT,L=5
         MVC   PLINE+117(9),=C'DATASETS.'
         BAL   R10,SPRINT
         MVC   PLINE,BL
         MVI   PLINE+1,C'+'
         BAL   R10,SPRINT
         MVC   PLINE+1(132),=132C'+'
         BAL   R10,SPRINT
         MVC   LCNT(2),=H'0'   IGNORE FROM HERE PAGEHDRS
         MVC   PLINE,BL
         MVI   PLINE+1,C'+'
         BAL   R10,SPRINT
         MVC   PLINE+50(10),=CL10'VOLUMES :'
         BAL   R10,SPRINT
         LH    R4,NRVOLS
         LTR   R4,R4
         BZ    S0400
         LA    R5,VOLUMES    PRINT THE VOLSER'S
         MVC   PLINE+50(10),BL
S0380    MVC   PLINE+52(6),0(R5)
         BAL   R10,SPRINT
         LA    R5,6(R5)
         BCT   R4,S0380
         MVC   PLINE+1(132),=132C'+'
         BAL   R10,SPRINT
S0400    EQU   *
        SPACE 5
         CONVD  TOTVAL,TSUM,L=10
        MVC   LCNT,=H'100'     SET UP FOR PLOT SUMMARY
        BAL   R10,SS05000      PLOT SUMMARY
         CLOSE (PRINT,,UT1)
         SR    R15,R15
         SPACE 3
RETURN   STOP  RC=(R15)
         EJECT
SERR01   OPEN  (PRINT,(OUTPUT))
         LA    R2,MSG01
         B     SERTN
SERR02   LA    R2,MSG02
         B     SERTN
SERR03   LA    R2,MSG03
         B     SERTN
SERTN    MVC   PLINE,BL
         MVC   PLINE,C'1'
         MVC   PLINE+1(30),0(R2)
         PUT   PRINT,PLINE
         CLOSE PRINT
         LA    R15,4
         B     RETURN
         EJECT
******************************
*
*        FIND OUT IF WE HAVE A NEW LEVEL BREAKDOWN
*
*        R2 POINTS TO UT1 RECORD
*
******************************
SS01000  STM   R2,R10,SSAVE
         LR    R3,R2      COMPOSE NEW LEVEL
         LA    R4,8        FIRST LEVEL; COU T
SS01010  CLI   0(R3),C'.'
         BE    SS01012
         CLI   0(R3),C' '
         BE    SS01012
         LA    R3,1(R3)
         BCT   R4,SS01010
SS01012  SR    R3,R2     LENGTH
         BNP   SERR02
         CLC   0(3,R2),=C'MEG'   MEGX LEVEL ?
         BNE   SS01030
         LA    R4,4
         LA    R3,1(R3,R2)       IF SO BREAK DOWN ON 2 LEVELS
SS01020  CLI   0(R3),C'.'
         BE    SS01022
         CLI   0(R3),C' '
         BE    SS01022
         LA    R3,1(R3)
         BCT   R4,SS01020
SS01022  SR    R3,R2
SS01030  MVC   NL,BL         INIT TO BL
         BCTR  R3,0          L-1
         EX    R3,SMVC01     MVC   NL(1),0(R2)
         SR    R15,R15
         CLC   NL,OL         LEVELS SAME ?
         BE    SS01090
         CLI   OL,C' '       IF NOT EQ , IS IT FIRST TIME ?
         BNE   SS01040        B. IF NOT
         MVC   OL,NL         SET OL ON FIRST TIME
          B     SS01090   EXIT RC=0
SS01040   LA    R15,4
SS01090   LM    R2,R10,SSAVE
          BR    R10
         EJECT
*******************************
*
*        NEW LEVEL HERE .
*        - PRINT SUMMARY
*        - SET SUM=0
*        - SET OL=NL
*
*        R2 POINTS TO RECORD
********************************
SS02000  STM   R2,R10,SSAVE
         L     R10,TSUM
         A     R10,SUM
         ST    R10,TSUM
         L     R10,TUSUM
         A     R10,USUM
         ST    R10,TUSUM
         L     R10,LTABC    MOVE LEVEL AND SUM TO LTAB
         C     R10,LTABH    WILL IT FIT
         BNL   SERR03       B. FOR GET A FIT
         MVC   0(16,R10),OL
         MVC   16(4,R10),SUM
         MVC   20(4,R10),USUM    USED
         LA    R10,LSIZE(R10)
        ST    R10,LTABC   NEW NEXT PTR
         CLI   PARM,C'S'
         BE    SS02500    FOR "S" BUILD FIELDS
         MVC   PLINE,BL
         MVI   PLINE,C'-'
*        MVC   PLINE+74(20),=CL20'TOTAL FOR LEVEL --'
*        MVC   PLINE+94(16),OL
*        LA    R10,PLINE+94  FIND FIRST BL
*        LA    R9,16
*        CLI   0(R10),C' '
*        BE    *+12
*        LA    R10,1(R10)
*        BCT   R9,*-12
*        MVC   0(2,R10),=2C'-'
         CONVD  PLINE+114,SUM,L=6
         CONVD  PLINE+121,USUM,L=4
         MVI   PLINE+120,C'-'
         L     R5,USUM
         LTR   R5,R5
         BZ    SS02220
         SR    R4,R4
         MH    R5,=H'100'
         D     R4,SUM    %
SS02220  CONVD PLINE+126,(R5),L=3
         MVI   PLINE+125,C'-'
         LH    R4,LCNT
         LA    R4,2(R4)
         STH   R4,LCNT
         BAL   R10,SPRINT
         XC    SUM,SUM
         XC    USUM,USUM
         MVC   OL,NL
*
* THE FOLLOWING CODE IS FOR RUN-ON REPORTS.
*
         CLI   PARM+1,C'R'
         BNE   SS02490
         LH    R4,LCNT
         CH    R4,MCNT
         BNL   SS02900
         MVC   PLINE,BL
         BAL   R10,SPRINT   PRINT EXTRA BL LINE
         B     SS02900
*
* END OF RUN-ON REPORTS CODE.
*
*
* THE FOLLOWING CODE IS FOR PAGE BREAK AT LEVEL CHANGE.
*
SS02490  MVC   LCNT,=H'100'     FORCE NEW PAGE FOR NEXT LEVEL.
*
* END OF PAGE BREAK CODE.
*
SS02900  LM    R2,R10,SSAVE
         BR    R10
        SPACE 5
SS02500 L     R3,CPTR   CURRENT FLD PTR
        MVC   0(FLDSIZ,R3),BL    INIT TO BL
        MVC   0(12,R3),OL
        LA    R4,12(R3)
        CONVD (R4),SUM,L=6
        LA    R3,FLDSIZ(R3)
        LH    R5,FLDCNT     # OF THE FLD
        LA    R5,1(R5)
        LA    R6,NUMFLDS    MAX # FLD FOR PLINE
        CR    R5,R6
        BNH   SS02510
        BAL   R10,SPRINT    PRINT IF FILLED
        LA    R5,1       NEW CURRENT FLD NUM
        LA    R3,PLINE+1
        MVC   PLINE,BL
SS02510 ST    R3,CPTR
        STH   R5,FLDCNT
        XC    SUM,SUM
        XC    USUM,USUM
        MVC   OL,NL
        B     SS02900
        EJECT
**********************************
*
*        ADD TRACK VALUE TO "SUM"
*
**********************************
SS03000  STM   R2,R10,SSAVE
         SPACE 5
*
*        SET UP VOLUME LIST
*
         LA    R3,108(R2)   VOLSER
         LH    R4,NRVOLS
         LA    R5,VOLUMES
         LTR   R4,R4
         BNP   SS03002
         CLC   0(6,R5),0(R3)   VOLSER ALREADY THERE ?
         BE    SS03009         QUIT IF SO
         LA    R5,6(R5)
         BCT   R4,*-14
SS03002  MVC   0(6,R5),0(R3)   MOVE IN THE VOLSER
         LH    R4,NRVOLS
         LA    R4,1(R4)
         STH   R4,NRVOLS
SS03009  EQU   *
         SPACE 5
         XC    DBL,DBL
         L     R5,DSCNT                ADD 1 TO DATASET COUNT.
         LA    R5,1(R5)
         ST    R5,DSCNT
         LA    R3,115(R2)
         CONVB (R4),(R3),L=4
         LTR   R15,R15
         BNZ   SS03010
         L     R5,SUM
         AR    R5,R4
         ST    R5,SUM
SS03010  LA    R3,120(R2)
         CONVB  (R4),(R3),L=4
         LTR   R15,R15
         BNZ   SS03020
         L     R5,USUM
         AR    R5,R4
         ST    R5,USUM
SS03020  LM    R2,R10,SSAVE
         BR    R10
         EJECT
****************************
*        PRINT DETAIL FOR "A" ONLY
****************************
SS04000   STM   R2,R10,SSAVE
         MVC   PLINE,BL
         MVC   PLINE+1(132),0(R2)
         BAL   R10,SPRINT
         LM    R2,R10,SSAVE
         BR    R10
         EJECT
*****************************
*       SPRINT
*        -SET UP FOR PRINT
****************************
SPRINT  STM   R2,R10,PSAVE
         LH    R3,LCNT
         LA    R3,1(R3)
         STH   R3,LCNT
         CH    R3,MCNT    MAX LINE COUNT ?
         BNH   SP0200
         CLI   PARM,C'S'   FOR "S" DO SOMETHING ELSE
         BE    SP0500
         MVC   HLINE,BL   SET HEADER
         SR    R15,R15
         IC    R15,LHDR1  L-1 OF FIRST PART
         EX    R15,SMVC02  MVC   HLINE(1),HDR1
         PUT   PRINT,HLINE
         MVC   HLINE,BL
         SR    R15,R15
         IC    R15,LHDR2
         EX    R15,SMVC10    MVC   HLINE(1),HDR2
         PUT   PRINT,HLINE
         MVC   HLINE,BL
         SR    R15,R15
         IC    R15,LHDR3
         EX    R15,SMVC11   MVC   HLINE(1),HDR3
         PUT   PRINT,HLINE
         MVC   LCNT,=H'6'
SP0200   PUT   PRINT,PLINE
         LM    R2,R10,PSAVE
         BR    R10
        SPACE 5
SP0500   MVC   HLINE,BL
         SR    R15,R15
         IC    R15,LHDRS1
         EX    R15,SMVC03   MVC   HLINE(1),HDRS1
         LA    R15,HLINE+1(R15)
         MVC   0(8,R15),TODAY
         PUT   PRINT,HLINE
         LA    R10,NUMFLDS
         LA    R9,HLINE+1
         MVC   HLINE,BL
SP0510   MVC   0(FLDSIZ,R9),HDRS2
         LA    R9,FLDSIZ(R9)
         BCT   R10,SP0510
         MVI   HLINE,C'-'
         PUT   PRINT,HLINE
         MVI   PLINE,C'-'
         MVC   LCNT,=H'7'
         B     SP0200
        EJECT
*************************************
*       PLOT THE ALLOCATION PERCENTAGES
*       THESE ARE IN LTAB
*************************************
SS05000 STM   R2,R10,SSAVE
         L     R2,LTABS    START OF TAB
         C     R2,LTABC    ANYTHING AT ALL ?
         BE    SS05900     B. IF NOT
         SPACE 3
*        SET UP PLOT TRAILER LINE
         SPACE 3
         MVI   PLFLG,X'00'  SET INIT PASS IND
         MVC   PLTRL1,BL
         LA    R6,5     SET COUNT
         SR    R7,R7    ACCU TSUM*N
         LA    R8,PLTRL1+22
         LA    R9,PLTRL1+75
SS05010  A     R7,TSUM
         SR    R4,R4
         LR    R5,R7
         D     R4,=F'500'
         CONVD (R8),(R5),L=6
         SR    R4,R4
         LR    R5,R7
         D     R4,=F'5'
         CONVD (R9),(R5),L=6
         LA    R8,10(R8)
         LA    R9,10(R9)
         BCT   R6,SS05010
         MVC   0(6,R9),=C'TRACKS'
         SPACE 3
SS05020  LA    R8,2    SET COUNT OF 2
         LA    R7,STAR  PRINT CHAR
         LA    R3,16(R2)  ALLOCATED
         MVC   PLINE,GRID
         MVC   PLINE+1(12),0(R2)
SS05030  L     R5,0(R3)     SUM
         SR    R4,R4
         MH    R5,=H'100'  FIND %
         D     R4,TSUM
         LA    R6,PLINE+18
         LTR   R5,R5
         BZ    SS05050
         MVC   0(50,R6),0(R7)  MOVE STARS OR +
         LA    R6,PLINE+71
         L     R5,0(R3)   VALUE OF SUM
         SR    R4,R4
         MH    R5,=H'50'  IN UNITS OF 2%
SS05040  D     R4,TSUM
         LA    R5,1(R5)   ADD 1
         CH    R5,=H'50'  MAX 50 UNITS
         BNH   *+8
         LA    R5,50
         MVC   0(1,R6),0(R7)  MOVE STAR OR PLUS
         LA    R6,1(R6)
         BCT   R5,*-10
         LA    R7,50(R7)  NEXT PLOT CHAR
         LA    R3,4(R3)   NEXT SUM
         BCT   R8,SS05030
SS05047  BAL   R10,SPLOT
         B     SS05100   GO TO NEXT SUM
         SPACE 3
SS05050  L     R5,0(R3)
         SR    R4,R4
         MH    R5,=H'5000' IN .02%
         LA    R6,PLINE+18
         B     SS05040
         SPACE  3
SS05100  LA    R2,LSIZE(R2)
         C     R2,LTABC
         BL    SS05020
         MVC   HLINE,BL
         IC    R15,LPLH3
         EX    R15,SMVC06   MVC   HLINE(1),PLHDR3
         PUT   PRINT,HLINE
         MVC   HLINE,PLTRL1
         PUT   PRINT,HLINE
SS05900  LM    R2,R10,SSAVE
         BR    R10
         EJECT
****************************
*        ACTUAL PUT TO PRINT
****************************
SPLOT    STM   R2,R10,PSAVE
         LH    R3,LCNT
         LA    R3,1(R3)
         STH   R3,LCNT
        CH     R3,MCNT
         BNH   SPLOT10
         CLI   PLFLG,X'00'   FIRST TIME ?
         BNE   SPLOT01
         MVI   PLFLG,X'FF'
         B     SPLOT02
SPLOT01  MVC   HLINE,BL
         IC    R15,LPLH3
         EX    R15,SMVC06   MVC   HLINE(1),PLHDR3
         PUT   PRINT,HLINE
         MVC   HLINE,PLTRL1
         PUT   PRINT,HLINE
SPLOT02  EQU   *
         MVC   HLINE,BL    SET UP PAGE HDR
         IC    R15,LPLH1
         EX    R15,SMVC04   MVC   HLINE,PLHDR1
         MVC   HLINE+12(8),TODAY
         PUT   PRINT,HLINE
         MVC   HLINE,BL
         IC    R15,LPLH2
         EX    R15,SMVC05   MVC   HLINE(1),PLHDR2
         PUT   PRINT,HLINE
         MVC   HLINE,BL
         IC    R15,LPLH3
         EX    R15,SMVC06   MVC   HLINE(1),PLHDR3
         PUT   PRINT,HLINE
         MVC   LCNT,=H'6'
SPLOT10  PUT   PRINT,PLINE
         LM    R2,R10,PSAVE
         BR    R10
          EJECT
PRINT    DCB   DSORG=PS,DDNAME=PRINT,MACRF=(PM),                       *
               RECFM=FBA,LRECL=133,BLKSIZE=1330
UT1      DCB   DSORG=PS,DDNAME=SYSUT1,MACRF=(GL),EODAD=ENDUT1
BL       DC    CL133' '
HDR1     DC    CL35'1RUN DATE ='
         DC    CL98'** MEMOREX **  D A T A S E T  U T I L I Z A T I O N*
                 R E P O R T'
LHDR1    DC    AL1(*-HDR1-1)
HDR2     DC    C'-   ',9C'-',C' D A T A S E T  N A M E '
       DC    7C'-',CL2' ',CL7'DSORG',CL10'CREATION',CL11'EXPIRATION'
        DC    CL10'LAST USE',CL12'LAST MOD',CL5'USE'
        DC    CL8'AVG USE',CL7'VOLUME'
        DC    2CL5'TRKS',CL6' %   #'
LHDR2   DC    AL1(*-HDR2-1)
HDR3    DC    CL55' ',CL11'DATE',2CL10'DATE',CL9'DATE'
        DC    CL20'COUNT PER DAY',C'ALLC USED ALLC XTN'
LHDR3   DC    AL1(*-HDR3-1)
GRID    DC    CL17' ',5CL10'+',C'+  ',6CL10'+',CL3' '
PLHDR1  DC    CL46'1RUN DATE ='
         DC    C'SPACE ALLOCATION IN PERCENTAGE OF TOTAL'
TOTVAL   DC    CL10' ',C' TRACKS'
LPLH1    DC    AL1(*-PLHDR1-1)
PLHDR2   DC    C'-',CL25' ',CL10'.2',CL10'.4',CL10'.6',CL9'.8'
         DC    CL14'1.0  0',CL10'20',CL10'40',CL10'60',CL9'80',C'100'
LPLH2    DC    AL1(*-PLHDR2-1)
PLHDR3   DC    CL17' ',C'+',5C'---------+',C'  +',5C'---------+'
LPLH3    DC    AL1(*-PLHDR3-1)
HDRS1    DC    C'1',CL45' ',C'SPACE ALLOCATION    '
LHDRS1   DC    AL1(*-HDRS1-1)
HDRS2    DC    CL14'LEVEL',CL10'TRKS'
EHDRS2   DS     0C
FLDSIZ   EQU   EHDRS2-HDRS2
GRTOTAL  DC    CL40'GRAND TOTAL DISK SPACE ALLOCATION  :'
NUMFLDS  EQU   5  MAX FLIEDS PER PINE
NUMLEV   EQU   5000    ***** PARAMETER : MAX NUMBER OF LEVELS ***
MCNT     DC    H'56'
MSG01    DC    CL30'PARM FIELD NOT CORRECT'
MSG02    DC    CL30'UNABLE TO ANALYZE SYSUT1'
MSG03    DC    CL30'NUMBER OF LEVELS EXCEEDS MAX'
SMVC01   MVC   NL(1),0(R2)
SMVC02   MVC   HLINE(1),HDR1
SMVC03   MVC   HLINE(1),HDRS1
SMVC04   MVC   HLINE(1),PLHDR1
SMVC05   MVC   HLINE(1),PLHDR2
SMVC06   MVC   HLINE(1),PLHDR3
SMVC10   MVC   HLINE(1),HDR2
SMVC11   MVC   HLINE(1),HDR3
STAR     DC    50C'.',50C'*'
         LTORG
         EJECT
WORK     DSECT
         DS    18F
SSAVE    DS    9F
PSAVE    DS    9F
GRES     DS    30F
OL       DS    CL16
NL       DS    CL16
PARM     DS    CL2
PLINE    DS    CL133
HLINE    DS    CL133
PLTRL1   DS    CL133
PLFLG    DS    X
TODAY    DS    CL8
SUM      DS    F
TSUM     DS    F
USUM     DS    F
TUSUM    DS    F
DSCNT    DS    F
CPTR     DS    F
LTABS    DS    F     START OF LEVEL TABLE
LTABC    DS    F     CURRENT  LEVEL TABLE
LTABH    DS    F     HIGHEST  LEVEL TABLE
LENTRY   DS    CL16   LTAB LAYOUT : LEVEL
         DS    4X     BIN VAL OF ALLOCATED SUM
         DS    4X     BIN VAL OF USED SUM
LEND     DS    0C
LSIZE    EQU   LEND-LENTRY
DBL      DS    D
LCNT     DS    H
FLDCNT   DS    H
NRVOLS   DS    H
VOLUMES  DS    50CL6
         END
