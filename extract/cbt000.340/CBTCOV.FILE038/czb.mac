         MACRO
&NAME    CZB   &A,&B,&C,&ID=,&MF=I
         LCLC  &I,&R,&E,&G
         AIF   ('&MF' NE 'L').MFE
         AIF   ('&ID' NE '').E1
         AIF   ('&NAME' EQ '').E2
         CNOP  0,4
&NAME    LTR   15,15
         BZ    CZBF&SYSNDX
         STM   0,1,CZBB&SYSNDX
         BCTR  1,0
CZBA&SYSNDX LA 1,1(1)
         TM    0(1),X'F0'
         BCR   14,15
         CLI   0(1),X'F9'
         BCR   2,15
         BCT   0,CZBA&SYSNDX
         LM    15,1,CZBB&SYSNDX
CZBF&SYSNDX BCTR 15,0
         EX    15,CZBD&SYSNDX
         CVB   1,CZBE&SYSNDX
         BR    14
CZBE&SYSNDX DS D
CZBB&SYSNDX DS F
         DS    F
CZBD&SYSNDX PACK CZBE&SYSNDX,0(0,1)
         MEXIT
.MFE     AIF   ('&MF' NE 'E').R
         AIF   ('&A' EQ '' OR '&B' EQ '' OR '&ID' EQ '').E3
         AIF   ('&ID' EQ '').E3
         AIF   ('&A' EQ '').E4
         AIF   ('&B' EQ '').E5
         AIF   ('&B'(1,1) NE '(').BNO1
&NAME    LR    0,&B(1)
         AGO   .A1
.BNO1    ANOP
&NAME    LA    0,&B
.A1      AIF   ('&A'(1,1) NE '(').ANO1
         LR    1,&A(1)
         AGO   .C1
.ANO1    LA    1,&A
.C1      AIF   ('&C' NE '').C2
         SR    15,15
         AGO   .BRANCH
.C2      AIF   ('&C'(1,1) NE '(').CNO1
         LR    15,&C(1)
         AGO   .BRANCH
.CNO1    LA    15,&C
.BRANCH  BAL   14,&ID
         MEXIT
.R       AIF   ('&MF' NE 'I').E1
         AIF   ('&A' EQ '' OR '&B' EQ '').E3
         AIF   ('&ID' NE '').E1
&I       SETC  '&NAME'
         AIF   ('&C' EQ '').PACK
         AIF   ('&C'(1,1) NE '(').BTEST
&G       SETC  'R'
.BTEST   AIF   ('&B'(1,1) NE '(').BNO2
&NAME    LR    0,&B(1)
         AGO   .A2
.BNO2    ANOP
&NAME    LA    0,&B
.A2      AIF   ('&A'(1,1) NE '(').ANO2
         LR    1,&A(1)
         AGO   .BCTR1
.ANO2    LA    1,&A
.BCTR1   BCTR  1,0
CZBA&SYSNDX LA 1,1(1)
         TM    0(1),X'F0'
         BC&G  14,&C(1)
         CLI   0(1),X'F9'
         BC&G  2,&C(1)
         BCT   0,CZBA&SYSNDX
&I       SETC  ' '
.PACK    AIF   ('&B'(1,1) EQ '(').VSEX
         AIF   ('&A'(1,1) EQ '(').BASE
&I       PACK  CZBC&SYSNDX,&A.(&B)
.REPRO   CVB   1,CZBC&SYSNDX
         B     CZBD&SYSNDX
         AGO   .NOEX
.BASE    ANOP
&I       PACK  CZBC&SYSNDX,0(&B,&A(1))
         AGO   .REPRO
.VSEX    AIF   ('&A'(1,1) EQ '(').EX
&I       LA    1,&A
&I       SETC  ' '
&E       SETC  '1'
         AGO   .YES
.EX      ANOP
&E       SETC  '&A(1)'
.YES     ANOP
&I       LR    15,&B(1)
         BCTR  15,0
         EX    15,CZBB&SYSNDX
         CVB   1,CZBC&SYSNDX
         B     CZBD&SYSNDX
CZBB&SYSNDX PACK CZBC&SYSNDX,0(0,&E)
.NOEX    ANOP
CZBC&SYSNDX DS D
CZBD&SYSNDX EQU *
         MEXIT
.E1      MNOTE 12,'ILLEGAL OPERAND(S) SPECIFIED'
         MEXIT
.E2      MNOTE 12,'LIST FORM REQUIRES TAG'
         MEXIT
.E3      MNOTE 12,'REQIRED OPERAND(S) OMITTED'
         MEND
