*****
*   UPDATED WILLIAM SMITH, MEMOREX CORPORATION
*   01AUG80 SSG TECHNICAL SUPPORT
*           SAN TOMAS AT CENTRAL EXPRESSWAY
*           DEPT. 8205, MS 10-35
*           SANTA CLARA, CALIFORNIA  94025
*           (408)-987-3919
*   . AUTHOR LARRY RUSH, HEWLETT PACKARD, PALO ALTO, CALIFORNIA
*   . DOCUMENTED WITH ASSEMBLY ERRORS CORRECTED
*   . MVS 3.8/JES2 4.1 - SE 1.1
*   . PANVALET/V10 - TSO OPTION
*   . ADDED A COMPANY TITLE ON TABLE OF CONTENTS
*
* FUNCTION:
* ---------
*  PANLIBPT IS USED TO PRODUCE A HARDCOPY LISTING OF A PANVALET DATA
*  SET OR A DISCRETE RANGE OF MEMBERS WITHIN THAT DATA SET.  AN ACCOMP-
*  ANYING TABLE OF CONTENTS IS ALSO PRINTED.
*
* DESCRIPTION:
* ------------
*  PANLIBPT INPUTS A PANVALET LIBRARY (DDNAME = SYSUT1) AND OUTPUTS A
*  LISTING OF THE MEMBERS IN THAT LIBRARY (DDNAME = SYSUT3) AND A
*  CORRESPONDING TABLE OF CONTENTS (DDNAME = SYSUT2) WHICH CONTAINS THE
*  PAGE NUMBER OF THE MEMBER IN THE LISTING.  IF A LISTING OF A RANGE
*  OF MEMBERS IS DESIRED THEN A PARM MUST BE INCLUDED ON THE EXEC
*  STATEMENT AS FOLLOWS:
*
*              PARM='<FIRST MEMBERNAME>,<LAST MEMBERNAME>'
*
*  THE LAST MEMBERNAME MUST BE LOGICALLY GREATER THAN OR EQUAL TO THE
*  FIRST MEMBERNAME.  IF A LISTING OF ONLY MEMBERS BEGINNING WITH A
*  CERTAIN SET OF CHARACTERS IS DESIRED, YOU MUST INCLUDE THE FOLLOWING
*  PARM ON THE EXEC STATEMENT:
*
*              PARM=<CHARACTER STRING>
*
* SAMPLE USE:
* ----------
*  THE FOLLOWING JCL WILL PRODUCE A LISTING OF THE MEMBERS "EPA30614S1"
*  TO "EPB30011S1" INCLUSIVE FROM "TEST.SRCELIB":
*
* //PANPRINT EXEC PGM=PANLIBPT,PARM='EPA30614S1,EPB30011S1'
* //SYSUT1    DD  DSN=TEST.SRCELIB,DISP=SHR
* //*         **  THE ABOVE DDCARD READS THE DIRECTORY AND THE MEMBERS
* //SYSUT2    DD  SYSOUT=*
* //*         **  THE ABOVE DDCARD PRINTS THE TABLE OF CONTENTS
* //SYSUT3    DD  SYSOUT=*
* //*         **  THE ABOVE DDCARD PRINTS THE MEMBERS
* //SYSUDUMP  DD  SYSOUT=*
*
*  THE FOLLOWING JCL WILL PRODUCE A LISTING OF THE ALL MEMBERS BEGIN-
*  ING WITH "EP" IN "TEST.SRCELIB".
*
* //PANPRINT EXEC PGM=PANLIBPT,PARM=EP
* //SYSUT1    DD  DISP=SHR,DSN=TEST.SRCELIB
* //*         **  THE ABOVE DDCARD READS THE DIRECTORY AND THE MEMBERS
* //SYSUT2    DD  SYSOUT=*
* //*         **  THE ABOVE DDCARD PRINTS THE TABLE OF CONTENTS
* //SYSUT3    DD  SYSOUT=*
* //*         **  THE ABOVE DDCARD PRINTS THE MEMBERS
* //SYSUDUMP  DD  SYSOUT=*
*
* JCL CONSIDERATIONS:
* -------------------
*  1.  SYSUT1, SYSUT2, AND SYSUT3 ARE REQUIRED.
*  2.  IF NO "PARM=" IS FURNISHED, THE ENTIRE LIBRARY IS LISTED.
*
* MESSAGES:
* ---------
*  PANVALET OPEN FAILED
*      U016 ABEND; CHECK DD CARD SPECIFICATION FOR PANVALET LIBRARY
*
*  PANVALET SEARCH FAILED
*      U032 ABEND; CORRECT PARM AND RERUN JOB
*
*  PANVALET READ FAILED
*      U064 ABEND; POSSIBLE I/O ERROR
*
*  INVALID PARM ON EXEC STATEMENT
*      U128 ABEND; CORRECT PARM
*
*  BEGINNING MEMBER NAME IS HIGHER IN COLLATING SEQUENCE
*      U256 ABEND; CORRECT ORDER OF MEMBER NAMES ON PARM
*
*  READ JFCB FAILED
*      U512 ABEND; POSSIBLE I/O ERROR
*
* RETURN CODES:
* ------------
*  0000 INDICATES NORMAL COMPLETION
*
*  ANY OTHERS, CHECK THE ABOVE MESSAGES
*****
*
* THIS PROCEDURE CALLS THE PANVALET ACCESS METHOD (PAM) SUBROUTINES.
* AT LINK-EDIT TIME DON'T FORGET TO INCLUDE THEM.
*
*****
         MACRO
&LABEL   BEGIN ,,&SAVAREA,&NAME,                                       *
               &BASE=,                                                 *
               &SAVE=,                                                 *
               &TITLBL=*DFLT,&TITLE=,&TITSTMT=YES,                     *
               &CSECT=YES,                                             *
               &TYPE=CSECT,                                            *
               &STMLBL=,                                               *
               &ID=,&TMFLAG=A0,                                        *
               &R=R,&GEN=NOGEN,                                        *
               &NOP=NO,                                                *
               &REQUS=NO,                                              *
               &USING13=YES,                                           *
               &VER=,                                                  *
               &PAHDR=NO,                                              *
               &SETSSI=NO
.*
.*
.*
         GBLB  &RFLAG
         GBLC  &RLEN,&RSP
         LCLC  &CLABEL,&SCODE,&SREG1,&SREG2,&SLBL,&SAVNAME,&BREG1
         LCLC  &TITLBLC,&TITL,&TITLVER,&VERTEMP
.*
         LCLC  &C,&SPOT,&HDR,&VERSION,&CVAL
.*
.*
.*
         MNOTE *,'*** PLAIN BEGIN, WITHOUT TIMESTMP SUPPORT. ***'
.*
.*
.*
         AIF   ('&SYSLIST(1)' EQ '').ERR10
         MNOTE 8,'*** REGISTER RANGE NO LONGER SUPPORTED IN BEGIN OR FI*
               NISH ***'
.ERR10   ANOP
         AIF   ('&SYSLIST(2)' EQ '').ERR99
         MNOTE 8,'*** SAVE CODE PARAMETER NO LONGER SUPPORTED IN BEGIN *
               ***'
.ERR99   ANOP
.*
.*
&CLABEL  SETC  '&SYSECT'
         AIF   ('&CSECT' EQ 'NO').CLBL99
&CLABEL SETC   '&LABEL'
         AIF   ('&CLABEL' NE '').CLBL99
&CLABEL  SETC  'PRIVATE'
         MNOTE *,'*** NO LABEL ON BEGIN, &TYPE CALLED ''PRIVATE'' ***'
.CLBL99  ANOP
.*
.*
         AIF    ('&TYPE' NE 'ENTRY').ENT99
&CLABEL  DS    0H
         ENTRY &CLABEL
         AGO   .CST99
.ENT99   ANOP
.*
         AIF   ('&CSECT' EQ 'NO').CST99
&CLABEL  CSECT
.CST99   ANOP
.*
.*
.*
&SLBL    SETC  '&STMLBL'
         AIF   ('&SLBL' NE '').S1
&SLBL    SETC  '&NAME'
.S1      ANOP
&SLBL    STM   &R.14,&R.12,12(&R.13)
.*
.*
         USING &CLABEL,&R.15
&C       SETC  'C'
&SPOT    SETC  '&SYSECT'
         AIF   ('&CLABEL' EQ '').LBL99
&SPOT    SETC  '&CLABEL'
         AIF   ('&CLABEL' EQ '&SYSECT').LBL99
&C       SETC  'E'
.LBL99   ANOP
.*
.*
.*   PAD THE VERSION WITH X'S
&VERTEMP SETC  '&VERSION'
         AIF   ('&VERSION' EQ '').VERNULL
         AIF   ('&VERSION'(1,1) NE '''').NOQUOTE
&VERTEMP SETC  '&VERSION'(2,K'&VERSION-2)
.NOQUOTE PAD   &VERTEMP,4,PAD=X
&VERTEMP SETC  '&CVAL'
.VERNULL ANOP
.*
.*
&HDR     SETC  'CL8''XXX&C.XXX'''
         AIF   ('&VERTEMP' EQ '').HDR99
&HDR     SETC  'CL3''X&C.X'',CL4''&VERTEMP.'',C''X'''
.HDR99   ANOP
.*
         B     *+28
         DC    &HDR
         DC    CL8'&SPOT'
         DC    C'X',AL3(&SPOT),C'XXXX'
.*
.*
.*
         AIF ('&NOP' EQ 'NO').NONOP
         CNOP  0,4
         B     *+20
         DC    CL16'ZAP TO 17FF07FE:'
         NOPR  R15         CAN ZAP TO 17FF ( XR  R15,R15 )
         NOPR  R14         CAN ZAP TO 07FE ( BR  R14     )
.NONOP   ANOP
         DROP  &R.15
.*
.*
&BREG1   SETC  '&BASE(1)'
         AIF   ('&BREG1' NE '').B10
&BREG1   SETC '&R.12'
         MNOTE *,'** NO BASE REGISTER SPECIFIED ASSUME REGISTER 12 **'
.B10     ANOP
         LR    &BREG1,&R.15
         USING &CLABEL,&BREG1
         AIF   ('&BASE(2)' EQ '').B999
         LA    &BASE(2),4095(&BREG1)
         LA    &BASE(2),1(&BASE(2).)
         USING &CLABEL+4096,&BASE(2)
         AIF   ('&BASE(3)' EQ '').B999
         LA    &BASE(3),4095(&BASE(2).)
         LA    &BASE(3),1(&BASE(3).)
         USING &CLABEL+8192,&BASE(3)
.B999    ANOP
.*
         AIF   ('&SAVE' EQ 'NONE').SV99
&SAVNAME SETC  '&SAVE(1)'
&RLEN    SETC  '&SAVE(2)'
&RSP     SETC  '&SAVE(3)'
         AIF   ('&SAVNAME' NE '').SV10
&SAVNAME SETC  '&SAVAREA'
.SV10    ANOP
         AIF   ('&SAVE(1)' NE '' OR '&SAVE(2)'  NE '' OR '&SAVE(3)' NE *
               '').SV20
         AIF   ('&SAVAREA' NE '').SVNRENT
         AGO   .SVRENT
.SV20    ANOP
         AIF   ('&SAVE(2)' EQ '' AND '&SAVE(3)' EQ '').SVNRENT
         AGO   .SVRENT
.*
.*
.SVNRENT ANOP
&RFLAG   SETB  0
         LR    &R.15,&R.13
         LA    &R.13,&SAVNAME
         ST    &R.15,4(&R.13)
         ST    &R.13,8(&R.15)
         AGO   .SV99
.*
.*
.SVRENT  ANOP
&RFLAG   SETB  1
         AIF   ('&RLEN' NE '').SVRSET
&RLEN    SETC  '72'
         MNOTE *,'*** REENTRANT SAVE AREA FORMAT USED ***'
.SVRSET  ANOP
.*
.********* FOLLOWING FIX APPLIED 15 JAN 79 - DP *********************
.* IF &RLEN IS AN 'L'' TYPE CONSTANT, DOUBLE-UP THE QUOTES FOR MNOTE
         GBLC  &RLENA,&RLENB
         LCLA  &LRLEN
&RLENA   SETC  ''
&RLENB   SETC  '&RLEN'
         AIF   ('&RLEN'(1,2) NE 'L''').NORLQ
&RLENA   SETC  'L'''''
&LRLEN   SETA  K'&RLEN-2
&RLENB   SETC  '&RLEN'(3,&LRLEN)
.NORLQ   ANOP
         MNOTE *,'*      GETMAIN R,LV=&RLENA.&RLENB,SP=&RSP'
         PUSH  PRINT
         PRINT &GEN
         GETMAIN R,LV=&RLEN,SP=&RSP
         POP   PRINT
         LR    &R.15,&R.13
         LR    &R.13,&R.1
         ST    &R.15,4(&R.13)
         ST    &R.13,8(&R.15)
         LM    &R.15,&R.1,16(&R.15)    RESET 0, 1, AND 15
         AGO   .SV99
.*
.*
.SV99    ANOP
.*
.*
         AIF   ('&SAVNAME' EQ '').UTH99
         AIF   ('&USING13' EQ 'NO').UTH99
         USING &SAVNAME,&R.13
.UTH99   ANOP
.*
         AIF   ('&REQUS' NE 'YES').REQNO
         MNOTE *,'REGISTER NAMES R0 THROUGH R15 ARE ASSIGNED'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.REQNO   ANOP
.*
         AIF   ('&TITSTMT' EQ 'NO').TIT99
&TITLBLC SETC  '&CLABEL'
         AIF   ('&TITLBL' EQ '*DFLT').TIT50
&TITLBLC SETC  '&TITLBL'
.TIT50   ANOP
&TITL    SETC  '&TITLE'
         PUSH  PRINT
         PRINT OFF       TITLE STATEMENT FOLLOWS
         AIF   ('&TITLE' NE '').TIT79
&TITL    SETC  ''' '''
         AIF   ('&VERSION' EQ '').TIT79
&TITLVER SETC  '&VERSION'
&TITL    SETC  '''VERSION '.'&TITLVER'.''''
.TIT79   ANOP
         AIF   ('&TITL'(1,1) EQ '''').TIT89
&TITL    SETC  ''''.'&TITL'.''''
.TIT89   ANOP
&TITLBLC TITLE &TITL
         POP   PRINT
.TIT99   ANOP
         MEND
         MACRO
&LABEL   FINISH  &RC=0,&R=R,&GEN=NOGEN,&RC1=,&RTRNOFF=
.*
.*
.*
         GBLB  &RFLAG
         GBLC  &RLEN,&RLENA,&RLENB,&RSP,&CT
         LCLC  &OPCD,&OPC1,&RETCODE,&RETCD1
.*
.*
          AIF   ('&SYSLIST(1)' EQ '').ERR99
          MNOTE 8,'*** PARTIAL REGISTER LIST NO LONGER SUPPORTED IN BEG*
                IN OR FINISH ***'
.ERR99   ANOP
.*
.*
&RETCODE SETC  '&RC(1)'
         AIF   ('&RETCODE' NE '').RC99
&RETCODE SETC  '0'
         MNOTE *,'*** NO RETURN CODE SPECIFIED ** ZERO ASSUMED ***'
.RC99    ANOP
.*
.*
&OPCD    SETC  'LR'
         AIF   ('&RC'(1,1) EQ '(').OP99
&OPCD    SETC  'LA'
.OP99    ANOP
.*
.*
.*
&RETCD1  SETC  '&RC1(1)'
         AIF   ('&RETCD1' EQ '').OP199
.*
.*
&OPC1    SETC  'LR'
         AIF   ('&RC1'(1,1) EQ '(').OP199
&OPC1    SETC  'LA'
.OP199   ANOP
         CNOP  0,4
&LABEL   OI    4(&R.13),X'FF'
         AIF   (NOT &RFLAG).GOHOME
.*
.*
.RENT   ANOP
         AIF   ('&OPCD' NE 'LR').RENT10
         LR    &R.2,&RETCODE
&RETCODE SETC  '&R.2'
.RENT10  ANOP
         AIF   ('&OPC1' NE 'LR').RENT20
         LR    &R.3,&RETCD1
&RETCD1  SETC  '&R.3'
.RENT20  ANOP
         LR    &R.1,&R.13
         L     &R.13,4(&R.13)
         MNOTE *,'*      FREEMAIN R,LV=&RLENA.&RLENB,A=(1),SP=&RSP'
         PUSH  PRINT
         PRINT &GEN
         FREEMAIN R,LV=&RLEN,A=(1),SP=&RSP
         POP   PRINT
         AGO   .COMEND
.*
.*
.*
.GOHOME ANOP
         L     &R.13,4(&R.13)
.COMEND  ANOP
         &OPCD &R.15,&RETCODE
         L     &R.14,12(&R.13)
         AIF   ('&OPC1' EQ '').GH90
         L     &R.0,20(&R.13)
         &OPC1 &R.1,&RETCD1
         LM    &R.2,&R.12,28(&R.13)
         AGO   .GH99
.GH90    ANOP
         LM    &R.0,&R.12,20(&R.13)
.GH99    ANOP
         MVI   12(&R.13),X'FF'
.*
         AIF   ('&RTRNOFF' EQ '').BR14
         B     &RTRNOFF.(&R.14)      TAKE SPECIAL RETURN WITH OFFSET
         AGO   .RET99
.BR14    ANOP
         BR    &R.14
.RET99   ANOP
         MEND
         PRINT ON,NOGEN
PANPRINT BEGIN SAVE=SAVEAREA,ID=PANPRINT,REQUS=YES
         EJECT
*****
*  CHECK AND SEE IF I GOT A PARM LIST
*****
CHCKPARM EQU   *
         L     R2,0(R1)                SAVE ADDRESS POINTER TO PARM
         SR    R3,R3                   CLEAR RISTER 3
         ICM   R3,3,0(R2)              PICKUP PARM LENGTH
         LTR   R3,R3                   0 MEANS NO PARM
         BZ    PANOPEN
         C     R3,=F'21'               21 IS THE MAX LENGTH OF PARM
         BH    BADNEWS
         MVC   STARTAT(11),BLANKLNE    BLANK FIELDS STARTAT
         LR    R6,R3                   LOAD PARM LENGTH
         BCTR  R6,0                    DECREMENT BY 1 FOR EX
         ST    R6,PARMLEN              SAVE FOR LATER MVC
         EX    R6,MVC1                 MOVE IN PARM TO DISPLAY AREA
         LR    R6,R3                   USE PARM LENGTH IN BCT
         LA    R3,2(R2)                POINT TO DATA IN PARM
         CLI   0(R3),C' '              1ST BYTE BLANK?
         BE    BADNEWS                 YES, ERROR
         LA    R4,STARTAT              POINT TO TARGET STRING
         XR    R5,R5                   CLEAR COUNTER
CKLOOP1  EQU   *
         CLI   0(R3),C','              STRING DELIMETER FOUND?
         BE    CK1DONE                 YES, STARTAT STRING DONE
         MVC   0(1,R4),0(R3)           MOVE A BYTE
         LA    R3,1(R3)                BUMP A BYTE WITHIN THE PARM
         LA    R4,1(R4)                BUMP A BYTE IN THE TARGET STRING
         LA    R5,1(R5)                COUNT A BYTE
         C     R5,=F'10'               COUNT > 10?
         BH    BADNEWS                 YES, ERROR
         BCT   R6,CKLOOP1              LOOP FOR LEN OF STR OR DELIMETER
         B     ONEPARM                 MUST ONLY BE ONE PARM
CK1DONE  EQU   *
         BCTR  R6,0                    SUBTRACT 1 FROM PARM LENGTH
         LTR   R6,R6                   CHECK CURRENT STRING LENGTH
         BNP   BADNEWS                 R6 <= 0, THAT IS BAD
         LA    R3,1(R3)                BUMP OVER THE COMMA
         CLI   0(R3),C' '              NEXT BYTE A BLANK?
         BE    BADNEWS                 YES, ERROR
         LA    R4,ENDAT                POINT TO TARGET STRING
         XR    R5,R5                   CLEAR COUNTER
CKLOOP2  EQU   *
         MVC   0(1,R4),0(R3)           MOVE A BYTE
         LA    R3,1(R3)                BUMP A BYTE WITHIN THE PARM
         LA    R4,1(R4)                BUMP A BYTE IN TARGET STRING
         LA    R5,1(R5)                COUNT A BYTE
         C     R5,=F'10'               COUNT > 10?
         BH    BADNEWS                 YES, ERROR
         BCT   R6,CKLOOP2              LOOP FOR LENGTH OF PARM
         B     PARMDONE                WE ARE FINALLY FINISHED
ONEPARM  EQU   *
         L     R6,PARMLEN              PICKUP PARM LEN
         BCTR  R6,0
         EX    R6,MVC3                 SETUP ENDAT ADDR
PARMDONE EQU   *
         CLC   STARTAT(10),ENDAT       STARTING STRING > ENDING STRING?
         BH    DUMMY                   YES, ERROR
         OI    SWITCH,ON               INDICATE PARM WAS SUPPLIED
         EJECT
*****
*  PERFORM HOUSEKEEPING
*****
PANOPEN  EQU   *
         TIME  DEC
         ST    R1,DATE
         MVC   TITLELNE+26(5),=CL5'DATE='
         MVC   TITLELNE+31(6),=XL6'21204B202020'
         ED    TITLELNE+31(6),DATE+1
         RDJFCB MF=(E,RDJFCB#1)
         LTR   R15,R15                 READ JFCB FAILED?
         BNZ   RDJBAD                  YES
         OPEN  (INDEXDCB,(OUTPUT),OUTPTDCB,(OUTPUT))
         MVC   TITLELNE+1(7),=CL7'MEMOREX'
         MVC   TITLELNE+11(7),=CL7'VOLSER='
         MVC   TITLELNE+18(6),JFCBAREA+118 MOVE VOL SER #
         MVC   TITLELNE+39(14),=CL14'DATA SET NAME='
         MVC   TITLELNE+53(26),JFCBAREA
         PUT   INDEXDCB,TITLELNE
         PUT   INDEXDCB,IHLINE
         PUT   INDEXDCB,UNDRSCRE
         TM    SWITCH,ON
         BNO   AROUND1
         L     R6,PARMLEN
         EX    R6,MVC2                 MOVE IN PARM TO DISPLAY AREA
         MVC   DBLESPCE+23(16),=CL16'PARM VALUE IS:  '
AROUND1  EQU   *
         MVC   NAME1(10),STARTAT
         MVC   NAME2(11),ENDAT
         MVC   DNAME(10),STARTAT
         PUT   INDEXDCB,DBLESPCE
         MVI   DBLESPCE+1,X'40'
         MVC   DBLESPCE+2(L'DBLESPCE-2),DBLESPCE+1
         XC    RTRNCODE,RTRNCODE       CLEAR PAN RETURN CODE LOCATION
         CALL  POPEN,(RTRNCODE,DDNAME,BACKUP),VL OPEN PANVALET LIBRARY
         CLC   RTRNCODE(4),=F'0'       GOOD RETURN FROM POPEN?
         BNE   ERROR#1                 NO
         EJECT
*****
*  PERFORM PAN SEARCH
*****
PANSRCH  EQU   *
         MVC   NAME1(10),DNAME         SETUP NEW RANGE
         XC    RTRNCODE,RTRNCODE       CLEARPAN RETURN CODE LOCATION
         CALL  PSRCH,(RTRNCODE,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL
         CLC   RTRNCODE(4),=F'0'       GOOD RETURN FROM PSRCH?
         BNE   ERROR#2                 NO
         CLC   DIRENTRY(2),=C'$*'      MORE ENTRIES?
         BE    CLOSEPAN                NO, CLOSE PAN FILE
         TM    FIRSTSW,ON              FIRST TIME SEARCH?
         BO    NXTPSRCH                NO
         OI    FIRSTSW,ON              INDICATE FIRST SEARCH DONE
         B     PANREAD                 PROCESS MEMBER
NXTPSRCH EQU   *
         XC    RTRNCODE,RTRNCODE       CLEARPAN RETURN CODE LOCATION
         CALL  PSRCH,(RTRNCODE,DIRENTRY,NAME1,NAME2,COMMENT,SUBSET),VL
         CLC   RTRNCODE(4),=F'0'       GOOD RETURN FROM PSRCH?
         BNE   ERROR#2                 NO
         CLC   DIRENTRY(2),=C'$*'      MORE ENTRIES?
         BE    CLOSEPAN                NO, CLOSE PAN FILE
         SP    LINECNT,LINECNT         RESET LINE COUNTER
         B     PANREAD                 YES, READ DATA
         EJECT
*****
*  THIS ROUTINE PRINTS THE MEMBER JUST FOUND
*****
PANREAD  EQU   *
         AP    PAGECNT,ONE             ADD 1 TO PAGE COUNTER
         BAL   R10,INDEX
         PUT   OUTPTDCB,TLINE1         PRINT OUT TITLE LINE 1
         MVC   TLINE2+16(10),DNAME     MOVE MEMBER NAME TO TITLE PAGE
         MVC   TLINE2+70(4),=CL4'PAGE'  MOVE CONSTANT 'PAGE' TO PRINT
         MVC   TLINE2+74(6),=XL6'402020202120' MOVE EDIT MASK TO PRINT
         ED    TLINE2+74(6),PAGECNT+1  PERFORM EDITTING
         PUT   OUTPTDCB,TLINE2         PRINT OUT TITLE LINE 2
         PUT   OUTPTDCB,TLINE3         PRINT OUT TITLE LINE 3
         PUT   OUTPTDCB,DBLESPCE       DOUBLE SPACE PRINTOUT
READPAN  EQU   *
         XC    RTRNCODE,RTRNCODE       CLEAR RETURN CODE
         MVC   NAME(10),DNAME          SETUP CALL FOR PANREAD
         MVC   INCLUDES,=C'NO-ENTRY'   SETUP CALL FOR PANREAD
         MVC   COMMENT(8),=C'NO-ENTRY' SETUP CALL FOR PANREAD
         CALL  PREAD,(RTRNCODE,CARDAREA+1,NAME,INCLUDES,COMMENT),VL
         CLC   RTRNCODE(4),=F'0'       GOOD RETURN FROM PREAD?
         BNE   ERROR#3                 NO
         CLC   CARDAREA+1(2),=C'$*'    MORE DATA?
         BE    PANSRCH                 NO, CHECK FOR MORE MEMBERS
         CP    LINECNT,MAXPLNES        COMPARE AGAINST 52 LINES
         BE    NEWPAGE
PUTLINE  EQU   *
         PUT   OUTPTDCB,CARDAREA       PRINT OUT LINE
         AP    LINECNT,ONE             ADD 1 TO LINE COUNTER
         B     READPAN                 READ NEXT BLOCK OF DATA
         EJECT
NEWPAGE  EQU   *
         PUT   OUTPTDCB,DBLESPCE
         MVC   DBLESPCE+26(28),=CL28'*  CONTINUED ON NEXT PAGE  *'
         MVI   CARDAREA,C' '           SET UP CARRIAGE CONTROL BYTE
         PUT   OUTPTDCB,DBLESPCE
         MVI   DBLESPCE,C'1'
         MVC   DBLESPCE+1(L'DBLESPCE-1),TLINE2+1
         MVC   DBLESPCE+28(12),=CL12'CONTINUED  *'
         MVC   DBLESPCE+70(4),=CL4'PAGE' MOVE CONSTANT 'PAGE' TO PRINT
         AP    PAGECNT,ONE             ADD 1 TO PAGE COUNTER
         MVC   DBLESPCE+74(6),=XL6'402020202120' MOVE IN EDIT MASK
         ED    DBLESPCE+74(6),PAGECNT+1 PERFORM EDIT
         PUT   OUTPTDCB,DBLESPCE
         MVI   DBLESPCE,C' '
         MVC   DBLESPCE+1(L'DBLESPCE-1),DBLESPCE
         MVI   DBLESPCE,C'0'
         PUT   OUTPTDCB,DBLESPCE       DOUBLE SPACE OUTPUT LINE
         SP    LINECNT,LINECNT         RESET LINECNT TO 0
         B     PUTLINE                 CONTINUE PRINTING THE BLOCK
INDEX    EQU   *
* TEST TO SEE IF NECESSARY FOR ANOTHER TABLE OF CONTENTS PAGE
         AP    ILINECT,TWO
         CP    ILINECT,MAXLTOC
         BE    HEADING
ENTRY    EQU   *
* WRITE OUT MEMBER NAME AND PAGE NUMBER IN THE TABLE OF CONTENTS
         MVC   ILINE+1(10),DNAME
         MVC   ILINE+11(63),DOTS
         MVC   ILINE+74(6),=XL6'402020202120'
         ED    ILINE+74(6),PAGECNT+1
         PUT   INDEXDCB,ILINE
         BR    R10
HEADING  EQU   *
* WRITE OUT HEADING PAGES FOR TABLE OF CONTENTS
         AP    IPAGECT,ONE
         MVI   ILINE+1,X'40'
         MVC   ILINE+2(L'ILINE-2),ILINE+1
         MVC   ILINE+30(11),=CL11'PAGE NUMBER'
         MVC   ILINE+42(6),=XL6'402020202120'
         ED    ILINE+42(6),IPAGECT+1
         PUT   INDEXDCB,ILINE
         PUT   INDEXDCB,TITLELNE
         PUT   INDEXDCB,IHLINE
         PUT   INDEXDCB,UNDRSCRE
         SP    ILINECT,ILINECT
         B     ENTRY
         EJECT
*****
*  CLOSE PAN FILE
*****
CLOSEPAN EQU   *
         XC    RTRNCODE,RTRNCODE       CLEARPAN RETURN CODE LOCATION
         CALL  PCLOSE,(RTRNCODE),VL    CLOSE PAN FILE
         AP    IPAGECT,ONE
         MVI   ILINE+1,X'40'
         MVC   ILINE+2(L'ILINE-2),ILINE+1
         MVC   ILINE+30(11),=CL11'PAGE NUMBER'
         MVC   ILINE+42(6),=XL6'402020202120'
         ED    ILINE+42(6),IPAGECT+1
         PUT   INDEXDCB,ILINE
         CLOSE (INDEXDCB,DISP,OUTPTDCB,DISP)
         FINISH RC=0
         EJECT
*****
*  ERROR CODES DEFINED HERE
*****
ERROR#1  EQU   *
         WTO   ' '
         WTO   'PANVALET OPEN FAILED'
         ABEND 0016,DUMP
ERROR#2  EQU   *
         WTO   ' '
         WTO   'PANVALET SEARCH FAILED'
         ABEND 0032,DUMP
ERROR#3  EQU   *
         WTO   ' '
         WTO   'PANVALET READ FAILED'
         ABEND 0064,DUMP
BADNEWS  EQU   *
         WTO   ' '
         WTO   'INVALID PARM ON EXEC STATEMENT'
         ABEND 0128,DUMP
DUMMY    EQU   *
         WTO   ' '
         WTO   'BEGINNING MEMBER NAME IS HIGHER IN COLLATING SEQUENCE'
         ABEND 0256,DUMP
RDJBAD   EQU   *
         WTO   ' '
         WTO   'READ JFCB FAILED'
         ABEND 0512,DUMP
         EJECT
*****
*  DATA AREAS, CONSTANTS, AND DCB'S
*****
INPUTDCB DCB   DDNAME=SYSUT1,MACRF=PM,DSORG=PS, (JUST A DUMMY DCB)     C
               EXLST=EXITLIST
INDEXDCB DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,RECFM=FBA,LRECL=133
OUTPTDCB DCB   DDNAME=SYSUT3,MACRF=PM,DSORG=PS,RECFM=FBA,LRECL=133
RDJFCB#1 EQU   *
         RDJFCB INPUTDCB,MF=L
EXITLIST EQU   *
         DC    XL1'87'
         DC    AL3(JFCBAREA)
JFCBAREA DS    44F
         SPACE 3
         EJECT
         DS    0D
SAVEAREA DS    9D
ILINE    DS    0CL133
         DC    CL1'0'
         DC    80CL1' '
         DC    52CL1' '
IHLINE   DS    0CL133
         DC    CL1'0'
         DC    CL22'MEMBER                '
         DC    CL33'T A B L E   O F   C O N T E N T S'
         DC    16CL1' '
         DC    CL9'PAGE NO. '
         DC    52CL1' '
UNDRSCRE DS    0CL133
         DC    CL1'+'
         DC    CL22'______                '
         DC    33CL1'_'
         DC    16CL1' '
         DC    CL9'________ '
         DC    52CL1' '
CARDAREA DS    0CL133
         DC    CL1' '
         DC    80CL1' '
         DC    CL52' '
TLINE1   DS    0CL133
         DC    CL1'1'
         DC    CL40'****************************            '
         DC    40CL1' '
         DC    52CL1' '
TLINE2   DS    0CL133
         DC    CL1' '
         DC    CL40'*  MEMBER NAME=            *            '
         DC    40CL1' '
         DC    52CL1' '
TLINE3   DS    0CL133
         DC    CL1' '
         DC    CL40'****************************            '
         DC    40CL1' '
         DC    52CL1' '
DBLESPCE DS    0CL133
         DC    CL1'0'
BLANKLNE DC    132CL1' '
TITLELNE DS    0CL133
         DC    CL1'1'
         DC    80CL1' '
         DC    52CL1' '
         EJECT
DATE     DS    F
PARMLEN  DS    F
DOTS     DC    63CL1'.'
         DS    0D
         DS    0D
PAGECNT  DC    PL4'+0'
LINECNT  DC    PL2'+0'
MAXLTOC  DC    PL2'+54'
MAXPLNES DC    PL2'+52'
ILINECT  DC    PL2'+0'
IPAGECT  DC    PL4'+0'
ONE      DC    PL1'+1'
TWO      DC    PL1'+2'
SWITCH   DC    XL1'00'
FIRSTSW  DC    XL1'00'
PARMLIST DC    CL21' '
         DS    0D
STARTAT  DC    CL10'A'
ENDAT    DC    CL11'Z999999999 '
ON       EQU   X'80'
*****
*  PANVALET PARAMETERS FOR POPEN, PSRCH, PREAD, AND PCLOSE
*****
         DS    0D
RTRNCODE DC    F'0'
DDNAME   DC    CL8'SYSUT1'
BACKUP   DC    CL8'NO-ENTRY'
*  PSRCH PARAMETERS
DIRENTRY DS    0CL80
DNAME    DS    CL10
DLEVEL   DS    CL3
DUSER    DS    CL4
DSECURE  DS    CL1
DTYPE    DS    CL5
DSTATUS  DS    0CL3
DPRODT   DS    CL1
DEORDD   DS    CL1
DAORDI   DS    CL1
DDATEM   DS    CL8
DDATEA   DS    CL8
DBLOCKS  DS    CL5
DSTMTS   DS    CL8
DLASTACT DS    CL4
DBYTES   DS    CL2
DSUBSET  DS    CL4
DNAME2   DS    CL10
         DS    CL1
         DS    CL1
DINC     DS    CL1
DVERSION DS    XL2
NAME1    DC    CL22' '
NAME2    DC    CL11'NO-ENTRY'
COMMENT  DC    CL52'NO-ENTRY'
SUBSET   DC    CL27'NO-ENTRY'
INCLUDES DS    CL8
*
NAME     DC    CL22' '
MVC1     MVC   PARMLIST(0),2(R2)
MVC2     MVC   DBLESPCE+39(0),PARMLIST
MVC3     MVC   ENDAT(0),2(R2)
         LTORG
         END   PANPRINT
