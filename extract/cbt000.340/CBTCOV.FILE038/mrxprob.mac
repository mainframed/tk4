*)F  FUNCTION -
*  THE MRXPROB COMMAND PROVIDES THE TSO USER WITH A MEANS TO DOCUMENT
*  PROBLEMS EXPERIENCED WHILE USING THE SERVICES PROVIDED AT THE
*  MEMOREX CORPORATE COMPUTER CENTER.  THESE PROBLEMS WILL BE REVIEWED
*  ON A DAILY BASIS BY THE MEMOREX/SSG USER SUPPORT SERVICES PROBLEM
*  COORDINATOR.  COMMENTS, RECOMMENDATIONS, AND SUGGESTIONS FOR AREAS
*  OF IMPROVEMENT ARE ALSO WELCOME AND ENCOURAGED.
*
*  THE MRXPROB COMMAND WILL PROMPT THE USER FOR DOCUMENTATION.
*  THE USER SHOULD ENTER AS MANY 80 CHARACTER LINES AS NECESSARY TO
*  THOROUGHLY EXPLAIN HIS/HER PROBLEM; THEN ENTER A CARRIAGE RETURN
*  TO SIGNAL THAT THE INPUT IS COMPLETE.
*)X  SYNTAX   -
*         MRXPROB
*)O  OPERANDS -
*         THERE ARE NO OPERANDS FOR THIS COMMAND.
         EJECT
MRXPROB  TITLE 'MEMOREX/ISD TSO USER PROBLEM DOCUMENTATION FACILITY'
         SPACE 2
*****
**  UPDATED 04APR79, WILLIAM SMITH, MEMOREX CORPORATION,
**    SANTA CLARA, CALIFORNIA, 95052
**    .  SOURCE - DEVELOPED BY THE UNITED STATES POSTAL SERVICE,
**       SAN BRUNO, CALIFORNIA -- FORMERLY KNOWN AS 'PDCPROB' --
**       AUTHOR:  ROBERT WENZEL, SYSTEMS PROGRAMMER
**    .  CHANGED ALL REFERENCES IN THE SOURCE CODE FROM 'PDCPROB'
**       (POSTAL DATA CENTER) TO 'MRXPROB' (MEMOREX CORPORATE
**       COMPUTER CENTER)
**    .  CHANGED THE NAME OF THE PROBLEM DATASET ('DSNAME' IN THE
**       SOURCE CODE) FROM 'PROD.PDCPROB' TO 'SYS.MRXPROB.DATA'
**       TO CONFORM TO MEMOREX NAMING CONVENTIONS
**    .  CHANGED THE DSNAME LENGTH FROM '12' TO '16'; NOTE THAT
**       ANY INSTALLATION CHANGING THE DATA SET NAME MUST CHANGE
**       THE 'DSNLNGTH' CONSTANT ACCORDINGLY - OTHERWISE DAIR
**       ERROR CODE 320 (INVALID DATA SET NAME) WILL BE ISSUED
**       WITH TERMINATION OF THE COMMAND PROCESSOR (S0C4)
**    .  REMOVED THE 'ENTER' MACRO INSTRUCTION (HOUSEKEEPING) AND
**       SUPPLIED THE OS REGISTER LINKAGE AND HOUSEKEEPING IN-LINE
**    .  GENERAL TIDYING OF THE CODE AND DOCUMENTATION
**
**  NOTE:
**       THE PROBLEM DOCUMENTATION DATA SET MUST BE PRE-ALLOCATED
**       PRIOR TO EXECUTION OF THE COMMAND PROCESSOR; OTHERWISE,
**       DAIR ERROR CODE 6708 (DSCB NOT FOUND IN VTOC) WILL BE
**       ISSUED ALONG WITH TERMINATION OF THE COMMAND PROCESSOR
**       (FB/80/3120 IS SATISFACTORY FOR A 3330-1)
*****
MRXPROB  START 0                                                 WJS
         SAVE  (14,12)        STANDARD OS/REGISTER HOUSKEEPING   WJS
         BALR  12,0           BASE REG = 12                      WJS
         USING *,12                                              WJS
         ST    13,4(13)       ESTABLISH POINTER TO SAVE AREA     WJS
         MVC   DAPLUPT,4(1)
         MVC   DAPLECT,12(1)
         MVC   DAPLPSCB,8(1)
*
*              GET USERID,TIME,DATE AND TERMINAL ID
*
         LR    R11,R1         POINTER TO CPPL
         USING CPPL,R11
         L     R9,CPPLPSCB
         USING PSCB,R9
         MVI   OUT,X'40'      BLANK OUT
         MVC   OUT+1(79),OUT
         MVC   OUT(7),PSCBUSER GET USERID
         CALL  DATEV,(MDY,HMS) GET TIME AND DATE
         MVI   HMS+2,C':'
         MVI   HMS+5,C':'
         DROP  R9
         DROP  R11
*
*
*
SETUP    LA    R1,DAPL       ADDRESS OF DAIR PARAMETER LIST
         LINK  EP=IKJEFD00   LINK TO DAIR
         LTR   R15,R15      TEST FOR ERROR
         BNZ   DAIRERR      JUMP TO TEST DAIR RETURN CODE
*
*              MOVE DDNAME TO DCB
*
         LA    R10,IN
         USING IHADCB,R10
         MVC   DCBDDNAM(8),DA08DDN
         MVC   DA18DDN(8),DA08DDN
*
*
*
         OPEN  (IN,(OUTPUT))   OPEN DSN
         TM    DCBOFLGS,X'10' TEST FOR ERROR
         BZ    OPENERR        JUMP ON ERROR
*
*
*
         PUT   IN,OUT         PUT USERID, TIME AND DATE
         TPUT  INMSG1,80
         TPUT  INMSG2,80
         TPUT  INMSG3,80
         TPUT  INMSG4,80
         TPUT  INMSG5,80
         TPUT  INMSG6,80
         TPUT  INMSG7,80
*
*              GET A LINE FROM THE TERMINAL
*
TGET     TGET  OUT,80
         C     R15,=F'0'      RETURN CODE OK?
         BE    CHKLNGTH
         C     R15,=F'12'     RETURN CODE = 12?
         BNE   ERRTGET
         TR    OUT(80),TABLE1
         PUT   IN,OUT
         B     TGET
CHKLNGTH C     R1,=F'0'       IS IT A NULL LINE?
         BE    WRAPUP
         TR    OUT(80),TABLE1
         PUT   IN,OUT
         B     TGET
ERRTGET  TPUT  INERR,80
         B     TGET
*
*              OK TO FREE DATASET - FIRST CLOSE
*
WRAPUP   MVI   OUT,X'40'
         MVC   OUT+1(79),OUT
         PUT   IN,OUT
         PUT   IN,OUT
         TPUT  OUTMSG1,80
         CLOSE (IN)
         LA    R1,DAPL        ADDRESS OF DAIR PARAMETER LIST
         LA    R9,FREE
         ST    R9,DAPLDAPB
         LINK  EP=IKJEFD00    LINK TO DAIR
         LTR   R15,R15        TEST FOR ERROR RETURN
         BNZ   FREEERR        JUMP ON ERROR
*
*
*
RETURN   L     R13,4(R13)
         RETURN (14,12),T,RC=0
*
*              DAIR ERROR DETERMINATION SECTION
*
DAIRERR  EQU   *       DAIR ERROR RETURN
         CH    R15,=H'12'             IS ERROR FOUND IN DARC?
         BNE   DAIRRC                 NO, SHOW REGISTER 15
*
         L     R2,16(R1)              DAPL
         LH    R4,4(R2)               DAPBDARC
         CH    R4,=X'0210'            IN USE ERROR
         BNE   DARCERR                NO, SHOW DARC ERROR CODE
*
         LA    R2,INUSEMSG            IN USE MESSAGE
         LA    R3,L'INUSEMSG          ITS LENGTH
         B     PUTERRM               GO TO PUT ERROR MESSEAGE
*
DAIRRC   LA    R2,ERRMG1
         LA    R3,L'ERRMG1
COVERR   CVD   R15,DBLWRD     CONVERT ERROR CODE TO CHAR
         MVC   31(6,R2),DMASK
         ED    31(6,R2),DBLWRD+5
         B     PUTERRM               GO TO PUT ERROR MESSEAGE
*
DARCERR  LA    R2,ERRMG1
         LA    R3,L'ERRMG1
         LR    R7,R4               CHANGE
         LA    R8,16               FROM
         LA    R9,ERRMG1+31        HEX CHAR.
         LA    R10,4               TO
HEXCHAR  SR    R6,R6               PRINTABLE CHAR.
         DR    R6,R8               EXAMPLE:
         C     R6,=F'9'               FROM 320
         BH    ALPHA                    TO F3F2F0
         STC   R6,0(R9)             OR
         OI    0(R9),X'F0'            FROM 32A
         B     DECRER9                  TO F3F2C1
ALPHA    S     R6,=F'9'
         STC   R6,0(R9)
         OI    0(R9),X'C0'
DECRER9  BCTR  R9,0
         BCT   R10,HEXCHAR
PUTERRM  TPUT  (R2),(R3)
         B     RETURN
OPENERR  EQU   *              ERROR ON OPEN OF DATASET
         LA    R2,ERRMG2
         LA    R3,L'ERRMG2
         B     COVERR
FREEERR  EQU   *              ERROR FOR DAIR CALL TO FREE DSN
         LA    R2,ERRMG3
         LA    R3,L'ERRMG3
         B     COVERR
*****
**  THE FOLLOWING CONSTANT (DSNLNGTH) MUST SPECIFY THE NUMBER OF
**  CHARACTERS (PERIODS INCLUDED) IN THE CONSTANT 'DSNAME' THAT
**  ACTUALLY COMPOSE THE DATA SET NAME - OTHERWISE, YOU WILL
**  GET A DAIR RETURN CODE OF 320 (INVALID DATA SET NAME) AND
**  ABEND 0C4.
**     WILLIAM SMITH, MEMOREX CORPORATION, 04APR79
*****
DSNLNGTH DC    H'16'
DSNAME   DC    CL44'SYS.MRXPROB.DATA'
INUSEMSG DC    CL80'SOMEONE ELSE IS USING MRXPROB - PLEASE TRY LATER'
DBLWRD   DS    D
DMASK    DC    X'402020202020'
ERRMG1   DC    CL80'DAIR08 ERROR RETURN CODE ='
ERRMG2   DC    CL80'OPEN ERROR RETURN CODE   ='
ERRMG3   DC    CL80'DAIR18 ERROR RETURN CODE ='
INERR    DC    CL80'TERMINAL ERROR REENTER LAST LINE'
INMSG1   DC    CL80'ON THE FIRST LINE'
INMSG2   DC    CL80'            ENTER: YOUR NAME AND TELEPHONE NUMBER'
INMSG3   DC    CL80'                   WHERE YOU CAN BE REACHED'
INMSG4   DC    CL80'ON SUBSEQUENT LINES'
INMSG5   DC    CL80'            ENTER: YOUR PROBLEM'
INMSG6   DC    CL80'ON THE LAST LINE'
INMSG7   DC    CL80'            ENTER: A CARRIAGE RETURN ONLY'
OUTMSG1  DC    CL80'THANK YOU FOR YOUR ASSISTANCE - USER SUPPORT SERVICX
               ES'
OUT      DS    0CL80
USERID   DS    CL8
MDY      DS    CL8
         DS    CL2
HMS      DS    CL8
         DS    CL1
ADDR     DS    CL4
         DS    CL49
ECB      DC    F'0'
TJID     DC    H'0'
         EJECT
*
*
*
         DS    0D
ALLOCATE DS    0CL84
DA08CD   DC    X'0008'
         DC    H'0'
         DC    H'0'
         DC    H'0'
DA08DSN  DC    A(DSNLNGTH)
DA08DDN  DC    CL8' '
         DC    CL8' '
DA08SER  DC    CL8' '
         DC    F'0'
         DC    F'0'
         DC    F'0'
         DC    F'0'
DA08MNM  DC    CL8' '
         DC    CL8' '
DA08DPS1 DC    X'02'
DA08DPS2 DC    X'08'
DA08DPS3 DC    X'08'
         DC    X'00'
         DC    F'0'
         DC    2F'0'
*
*
*
FREE     DS    0CL40
DA18CD   DC    X'0018'
         DC    H'0'
         DC    H'0'
         DC    H'0'
DA18DSN  DC    A(DSNLNGTH)
DA18DDN  DC    2F'0'
DA18MNM  DC    CL8' '
         DC    H'0'
         DC    H'0'
         DC    2F'0'
*
*
*
DAPL     DS    0CL20
DAPLUPT  DS    F
DAPLECT  DS    F
DAPLECB  DC    A(ECB)
DAPLPSCB DS    F
DAPLDAPB DC    A(ALLOCATE)
         EJECT
TABLE1   DC    X'000102030405060708090A0B0C0D0E0F'       00 - 0F
         DC    X'101112131415161718191A1B1C1D1E1F'       10 - 1F
         DC    X'202122232425262728292A2B2C2D2E2F'       20 - 2F
         DC    X'303132333435363738393A3B3C3D3E3F'       30 - 3F
         DC    X'404142434445464748494A4B4C4D4E4F'       40 - 4F
         DC    X'505152535455565758595A5B5C5D5E5F'       50 - 5F
         DC    X'606162636465666768696A6B6C6D6E6F'       60 - 6F
         DC    X'707172737475767778797A7B7C7D7E7F'       70 - 7F
         DC    X'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'       80 - 8F
         DC    X'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'       90 - 9F
         DC    X'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'       A0 - AF
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'       B0 - BF
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'       C0 - CF
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'       D0 - DF
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'       E0 - EF
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'       F0 - FF
         EJECT
         LTORG
*
*
*
IN       DCB   DSORG=PS,MACRF=PM,DDNAME=DUMMY
         EJECT
         IKJCPPL
         EJECT
         IKJPSCB
         EJECT
         REGS
         EJECT
         DCBD  DSORG=PS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*       THIS SUBROUTINE OUTPUTS DATE AND TIME OF DAY WHEN             *
*       CALLED BY THE STATEMENT                                       *
*                                                                     *
*            DATEV (MDY,HMS)                                          *
*                                                                     *
*            MDY - THE FIRST OUTPUT ARGUMENT CONTAINING DATE IN       *
*                   MM/DD/YY FORMAT, WHERE                            *
*                        MM - MONTH OF THE YEAR (2 DECIMAL DIGITS)    *
*                        DD - DAY OF THE MONTH                        *
*                        YY - LAST TWO DIGITS OF THE YEAR             *
*                                                                     *
*            HMS - THE SECOND OUTPUT ARGUMENT CONTAINING TIME IN      *
*                   HH.MM.SS FORMAT, WHERE                            *
*                        HH - HOUR OF THE DAY (2 DECIMAL DIGITS)      *
*                        MM - MINUTES                                 *
*                        SS - SECONDS                                 *
*                                                                     *
*       NOTE - ARGUMENTS MUST EACH BE ABLE TO CONTAIN 8 CHARACTERS    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE  5
DATEV    CSECT
         USING  *,15
         SAVE   (14,12)
         LR     4,1                     ARGLIST ADDRS NOW IN R4
         LA     12,SAVE
         ST     13,4(12)
         ST     12,8(13)
         LR     13,12
         LR     12,15
         DROP   15
         USING  DATEV,12
         TIME  DEC                      MACRO-INST GIVES DATE IN REG1,
*                                       TIME IN REGO FORMATTED 00YYDDDC
*                                       AND HHMMSSTH RESPECTIVELY
         LM     2,3,0(4)                SAVE ARGS
         ST    0,TIMET
         OI    TIMET+3,X'0F'            INSERT DECIMAL SIGN
         MVC   WKAREA(9),PTTRN          SET UP FOR EDITING
         ED    WKAREA(9),TIMET
         MVC   0(8,3),WKAREA+1          SEND TIME TO CALLER
         ST    1,TIMET
         MVO   TEMP,TIMET+1(1)          PREPARE FOR CONVERSION
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK  DATEN+6(2),TEMP+6(2)     FORMAT YY IN DATEN
         CVB   9,TEMP                   CONVERT YY TO BINARY
         STC   9,YEAR
         TM    YEAR,X'03'               LEAP YEAR
         BC    5,*+10                   NO
         MVC   TABLE+4,=H'29'           MODIFY TABLE
         MVC   TEMP,=D'0'               ZERO TEMP
         MVC   TEMP+6(2),TIMET+2        PREPARE TO CONVERT DDD
         CVB   11,TEMP                  CONVERT DDD TO BINARY
         LA    10,2                     INITIALIZE INDEX REG OF TABLE
LOOPDATE SH    11,TABLE(10)             SUBTRACT DAYS OF MONTH(INDEXED)
         BC    12,CHECK                 RESULT ZRO OR NEGATIVE
         LA    10,2(0,10)               INCREMENT INDEX BY 2
         CH    10,=H'26'                ERROR
         BC    12,LOOPDATE              NO
         B     DATERROR
CHECK    AH    11,TABLE(10)
         CVD   11,TEMP                  DD NOW IN PACKED DECIMAL
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK  DATEN+3(2),TEMP+6(2)     DD IN BCD - PLACE IN DATEN
         SRA   10,1                     MONTH NOW IN REG 10
         CVD   10,TEMP                  CONVERT MONTH TO PACKED DECIMAL
         OI    TEMP+7,X'0F'             INSERT SIGN
         UNPK  DATEN(2),TEMP+6(2)       MM IN BCD - PLACE IN DATEN
         MVC   0(8,2),DATEN             SEND DATE TO CALLER
         B     RESTORE
DATERROR MVC   DATEN(8),=C'ER/R0/R '
RESTORE  L     13,4(13)
         MVI   12(13),X'FF'
         RETURN (14,12)
*
TEMP     DS    D
TIMET    DS    F
TABLE    DS    H
         DC    H'31'
         DC    H'28'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'31'
         DC    H'30'
         DC    H'31'
         DC    H'30'
         DC    H'31'
SAVE     DS    18F
WKAREA   DS    CL9
PTTRN    DC    X'4021214B21214B2121'
DATEN    DC    C'  /  /  '
         DS    C
YEAR     DS    C
         LTORG
         END   MRXPROB
