         TITLE '*----VSAMLST8----* VSAM CATALOG LIST UTILITY --  HISTOR*
               Y FILE WRITER'
         PRINT OFF
         MACRO
&NAME    INIT &BASE=3,&REGS=Y,&PATCH=3,&RENT=N,&SAVE=Y
         AIF   ((&BASE LT 13) AND (&BASE GT 1)).N020
         MNOTE 12,'INVALID BASE REGISTER'
         MEXIT
.N020    ANOP
         PUSH  PRINT
         PRINT ON,GEN
         EJECT
&SYSECT  CSECT
         USING *,15
         B     BEGIN
         DC    AL1(24)
         DC    CL8'&SYSECT'
         DC    CL16'-&SYSDATE-&SYSTIME'
         AIF   ('&RENT' EQ 'Y').N004
MYSAVE   DC    18F'-1'
.N004    ANOP
         AIF   ('&PATCH' EQ '0').N005
PATCH    DC    &PATCH.CL8'*PATCH*'
.N005    ANOP
         AIF   ('&REGS' EQ 'N').N030
         AIF   ('&REGS' EQ 'Y').N010
         MNOTE 4,'REGS OPERAND INVALID. Y SUBSTITUTED'
.N010    ANOP
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.N030    ANOP
BEGIN    DS   0H
         STM   14,12,12(13)
         LR    &BASE,15
         DROP  15
         USING &SYSECT,&BASE
         AIF   ('&SAVE' EQ 'N').N003
         AIF   ('&RENT' EQ 'Y').N002
         AIF   ('&RENT' EQ 'N').N001
         MNOTE 4,'RENT OPERAND INVALID. N SUBSTITUTED'
.N001    ANOP
         ST    13,MYSAVE+4
         LR    15,13
         LA    13,MYSAVE
         ST    13,8(15)
         AGO   .N003
.N002    ANOP
         GETMAIN R,LV=72
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
.N003    ANOP
         POP   PRINT
         EJECT
         MEND
         PRINT ON
*  MODULE NAME:         VSAMLST8     (REL. 1.1  08/10/79)
*
*  MODULE DESCRIPTION:  VSAM CATALOG LIST UTILITY - HISTORY FILE WRITER
*
*  RETURN LINKAGE:      RETURN (14,12),RC=(15)
*
*  LINKAGE TABLE:       NONE - SINGLE ENTRY
*
*  PARAMETERS:          R1 POINTS TO THE ADDRESS OF A 92-BYTES RECORD
*                       FROM SORT/ESPXXX
*
*  EXIT:                RC=00 - SUCCESSFUL
*                       RC=04 - UNABLE TO OPEN DCB FOR HISTORY FILE
*
*  MODULE FUNCTION:     FOR EACH RECORD FROM SORT/ESPXXX,   THIS MODULE
*                       FORMATS AND WRITES IT OUT TO THE VSAM HISTORY
*                       FILE.
*
*  CALLER:              ESPXXX
*
*  CALLS:               NONE
*
*  SYSTEMS SERVICES:    CLOSE, OPEN, PUT (QSAM)
*
*  MODULE ENVIRONMENT:  OS/VS1
         TITLE 'VSAMLST8 --- VSAM CATALOG LIST UTILITY HISTORY FILE WRIX
               TER (3.3) - VSAM VOLUME CONTENTS'
*        *-----------------------------------*
*        *   R E G I S T E R   U S A G E S   *
*        *-----------------------------------*
*
*  R0  -  STANDARD LINKAGE
*  R1  -  STANDARD LINKAGE (ADDR OF PARM LIST) & TEMP WORK SPACE
*  R2  -  TEMP WORK SPACE
*  R3  -  BASE REG FOR DSECT INREC
*  R4  -  NOT USED
*  R5  -  NOT USED
*  R6  -  NOT USED
*  R7  -  NOT USED
*  R8  -  NOT USED
*  R9  -  NOT USED
*  R10 -  NOT USED
*  R11 -  RETURN ADDR FROM SUBROUTINE
*  R12 -  BASE REG FOR CSECT VSAMLST8
*  R13 -  STANDARD LINKAGE (ADDR OF SAVE AREA)
*  R14 -  STANDARD LINKAGE (ADDR TO RETURN)
*  R15 -  STANDARD LINKAGE (ADDR OF ENTRY POINT & RETURN CODE)
         SPACE 3
VSAMLST8 CSECT
         INIT  BASE=12
***********************************************************************
*                                                                     *
*  3.3    VSAM HISTORY FILE WRITER                                    *
*                                                                     *
*     FUNCTION:  FOR EACH RECORD PASSED TO THIS MODULE, IT FORMATS    *
*                AND WRITES IT FOR THE VSAM HISTORY FILE.             *
*                                                                     *
*     ERRORS:  NONE                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
VCL33    EQU   *
         USING INREC,R3
         LM    R2,R3,0(R1)
         B     HERE(R2)
HERE     EQU   *
         B     INIT                     FUNC CODE 0
         B     PROCESS                  FUNC CODE 4
         B     FINAL                    FUNC CODE 8
         EJECT
*        *-------------------*
*        *   P R O C E S S   *
*        *-------------------*
         SPACE
PROCESS  EQU   *
         MVC   DSCB7VOL,INVOLNO
         MVC   DS7DSNAM,INENTNM
         MVC   DS7DSSN,INVOLNO
         LA    R1,INCREDT
         LA    R2,DS7CREDT
         BAL   R11,BINDATE              XL3'YYDDDF' TO XL3'YYDDDD'
         LA    R1,INEXPDT
         LA    R2,DS7EXPDT
         BAL   R11,BINDATE              XL3'YYDDDF' TO XL3'YYDDDD'
         SPACE
         MVC   DS7SCTYP,INSCTYP
         MVC   DS7SCQTY,INSCALC
         MVC   DS7NOEXT,INNOEXT
         MVC   DS7TRKAL(L'INTRKAL+L'INTRKUS),INTRKAL
         MVC   DS7ENTYP,INENTYP
         MVC   DS7DATTR,INDSORG
         MVC   DS7CISIZ(L'INCISIZ+L'INMAXRC+L'INAVGRC),INCISIZ
         SPACE
         PUT   VSAMHIST,DSCB7DTE
         SR    R15,R15                  CLEAR RETURN CODE
         SPACE 2
*        *-------------*
*        *   E X I T   *
*        *-------------*
         SPACE
GOHOME   EQU   *
         L     R13,MYSAVE+4
         RETURN (14,12),RC=(15)
         EJECT
*        *---------------------*
*        *   B I N   D A T E   *
*        *---------------------*
         SPACE
BINDATE  EQU   *
         MVC   0(3,R2),ZERO             CLEAR TARGET AREA
         CLC   ZERO(3),0(R1)            IS IT A GOOD DATE?
         BER   R11                      NO - RETURN NOW
         CLC   FOXES(3),0(R1)           IS IT A GOOD DATE?
         BER   R11                      NO - RETURN NOW
         SPACE
         MVO   YEAR+6(2),0(1,R1)        'YY' OF X'YYDDDF'
         OI    YEAR+7,X'0F'
         CVB   R0,YEAR
         STC   R0,0(,R2)
         SPACE
         MVC   DAYS+6(2),1(R1)          'DDDF' OF X'YYDDDF'
         CVB   R0,DAYS
         STCM  R0,3,1(R2)
         SPACE
         BR    R11                       SPACE
         SPACE 2
ZERO     DC    F'0'
FOXES    DC    F'-1'
YEAR     DC    D'0'
DAYS     DC    D'0'
         EJECT
*        *-------------*
*        *   I N I T   *
*        *-------------*
         SPACE
INIT     EQU   *
         OPEN  (VSAMHIST,(OUTPUT))
         SPACE
         TM    VSAMHIST+48,X'10'        IS OPEN SUCCESSFUL?
         BO    DCBOK                    YES - INIT'S DONE
         LA    R15,4                    NO - SET RETURN CODE
         B     GOHOME                   AND QUIT
         SPACE
DCBOK    EQU   *
         TIME  DEC
         SPACE
         STCM  R1,7,DSCB7DTE
         STCM  R0,15,DSCB7TME
         OI    DSCB7TME+3,X'0F'
         SR    R15,R15                  CLEAR RETURN CODE
         B     GOHOME
         SPACE 2
*        *---------------*
*        *   F I N A L   *
*        *---------------*
         SPACE
FINAL    EQU   *
         CLOSE VSAMHIST
         SPACE
         SR    R15,R15                  CLEAR RETURN CODE
         B     GOHOME                   AND QUIT
         EJECT
VSAMHIST DCB   DDNAME=VSAMHIST,DSORG=PS,MACRF=(PM)
         EJECT
*        *-------------------------*
*        *   F M T   7   D S C B   *
*        *-------------------------*
         SPACE
         DS    0D
DSCB7DTE DS    PL3
DSCB7TME DS    PL4
DSCB7VOL DS    CL6
DSCB7CHR DC    XL5'00'
DSCB7    DS    140X'00'                 ALIGNED AS 0D
         ORG   DSCB7
DS7DSNAM DS    CL44                     ENTRY NAME
DS7FMTID DC    CL1'7'
DS7DSSN  DS    CL6                      VOLUME SERIAL NUMBER
DS7VOLSQ DC    XL2'0001'
DS7CREDT DS    XL3                      CREATION DATE
DS7EXPDT DS    XL3                      EXPIRATION DATE
DS7NOEPV DS    XL1
DS7NOBDB DS    XL1
         DS    CL1
DS7SYSCD DC    CL13'IBMOS/360'
         DS    CL7
DS7DSORG DC    XL2'0008'
DS7RECFM DC    XL1'C0'
DS7OPTCD DS    XL1
DS7BLKL  DS    H
DS7LRECL DS    H
DS7KEYL  DS    XL1
DS7RKP   DS    XL2
DS7DSIND DS    XL1
DS7SCALO DS    0XL4
DS7SCTYP DS    XL1                      SECONDARY ALLOCATION TYPE
DS7SCQTY DS    XL3                      SECONDARY ALLOCATION QUANTITY
DS7LSTAR DS    XL3
DS7TRBAL DS    XL2
         DS    CL2
DS7EXT1  DS    XL10
DS7EXT2  DS    XL10
DS7EXT3  DS    XL10
DS7PTRDS DS    XL5
         ORG   DS7EXT1                  VSAM INFO'S STARTS HERE
DS7NOEXT DS    XL1                      NO. OF EXTENTS FOR DATA SET
DS7TRKAL DS    H                        TRK'S ALLOC'D FOR DATA SET
DS7TRKUS DS    H                        TRK'S USED FOR DATA SET
DS7ENTYP DS    CL1                      ENTRY TYPE
DS7DATTR DS    XL1                      DATA SET ORGANIZATION (AMDATTR)
DS7CISIZ DS    XL4                      CONTROL INTERVAL SIZE
DS7MAXRC DS    XL4                      MAXIMUM RECORD SIZE
DS7AVGRC DS    XL4                      AVERAGE RECORD SIZE
         ORG
         EJECT
INREC    DSECT
INVOLNO  DS    CL6                      VOLUME SERIAL NUMBER
INRPTNO  DS    0CL1                     REPORT NO. B'0... ....' DETAILS
INRCKEY  DS    CL4                      RECORD KEY - GENERATED SORT KEY
INRCSEQ  DS    CL2                      RECORD SEQ - SEQ NO IN 'NAMEDS'
INENTNM  DS    CL44                     ENTRY NAME
INENTYP  DS    CL1                      ENTRY TYPE
INCREDT  DS    CL3                      CREATION DATE
INEXPDT  DS    CL3                      EXPIRATION DATE
INCRAVL  DS    CL6                      CRA VOLUME SERIAL NUMBER
INSCALC  DS    CL3                      SECONDARY SPACE ALLOCATION
INSCTYP  DS    CL1                      SPACE ALLOCATION TYPE
INDSORG  DS    CL1                      DATA SET ORGANIZATION (AMDATTR)
INCISIZ  DS    CL4                      CONTROL INTERVAL SIZE
INMAXRC  DS    CL4                      MAXIMUM RECORD SIZE
INAVGRC  DS    CL4                      AVERAGE RECORD SIZE
INTRKAL  DS    H                        TRK'S ALLOC'D FOR DATA SET
INTRKUS  DS    H                        TRK'S USED FOR DATA SET
INPCTUS  DS    CL1                      PERCENT OF USAGE
INNOEXT  DS    CL1                      NO. OF EXTENTS FOR DATA SET
         END
