000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.
000030     PRG-3-FLOBOL.
000040 AUTHOR.
000050     NAVY PROGRAMMING LANGUAGES DIVISION.
000060 INSTALLATION.
000070     NPLD OFFICE OF INFORMATION SYSTEMS PLANNING AND DEVELOPMENT.
000080 DATE-WRITTEN.
000090     VERSION 1.0 01-01-69 VERSION 2.0 09-01-69.
000100     UPDATED AS OF  1 APRIL 1970.
000110 DATE-COMPILED.
000120     COMMENT-ENTRY.
000130 SECURITY.
000140     FREEDOM OF INFORMATION ACT.
000150 REMARKS.
000160     *        ANY COMMENTS ABOUT THIS PROGRAM SHOULD BE SENT TO  *
000170     *                                                           *
000180     *        NAVY PROGRAMMING LANGUAGES DIVISION                *
000190     *        OFFICE OF INFORMATION SYSTEMS PLANNING AND         *
000200     *             DEVELOPMENT                                   *
000210     *        ROOM 5D840 PENTAGON                                *
000220     *        WASHINGTON D. C.  20350 USA                        *
000230     *                                                           *
000240     *     ATTN  FLOBOL.                                         *
000250     *                                                           *
000260     *                                   (202) OX5-4750          *
000270     *                                                           *
000280     *        *         *         *         *         *          *
000290          PARFILEIN AND INFILE ARE USED TO CREATE FORWARD AND
000300     BACKWARD REFERENCES FOR EACH 'GO TO', 'ALTER', AND 'PERFORM'.
000310     ON SUPPLEMENTAL-PART3-OUT WHICH IS SORTED AND PASSED TO
000320     PRG-4.
000330     INPUT FILES.   PRG-3.
000340        PARFILEIN.
000350           1. CREATED IN PRG-2 AND ALSO USED IN PRG-4.
000360           2. RECORD LENGTH IS 60 CHARACTERS.
000370           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
000380           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
000390              IF NECESSARY.
000400           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
000410              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
000420              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
000430              FILE IN THE FLOBOL SYSTEM.
000440           6. A DESCRIPTION OF THE RECORDS IN THIS FILE
000450              FOLLOWS.
000460          PARAGRAPH / SECTION RECORD
000470              A PROGRAM NUMBER
000480              B PROCEDURE NAME
000490              C COBOL REFERENCE NUMBER OF PROCEDURE NAME
000500              D COBOL STATEMENT NUMBER OF PROCEDURE NAME
000510              E RECORD OCCURENCE NUMBER
000520        INFILE
000530           1. CREATED IN PRG-2 FROM  'C' TYPE RECORDS FROM
000540              SUPPLEMENTAL-PART2-IN.
000550           2. RECORD LENGTH IS 120 CHARACTERS.
000560           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
000570           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
000580              IF NECESSARY.
000590           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
000600              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
000610              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
000620              FILE IN THE FLOBOL SYSTEM.
000630           6. A DESCRIPTION OF THE RECORDS IN THIS FILE
000640              FOLLOWS.
000650          PERFORM ENTRANCE RECORDS
000660              A C (TYPE C RECORD)
000670              B PROGRAM NUMBER
000680              C PROGRAM ID
000690              D PERFORM NAME FIRST ONE
000700              E 0000
000710              F COBOL REFERENCE NUMBER OF PERFORM
000720              G COBOL STATEMENT NUMBER OF PERFORM
000730              H PER ENT
000740              I PARAGRAPH THAT PERFORM WAS WITHIN
000750              J            NOT USED
000760              K            NOT USED
000770              L RECORD OCCURRENCE NUMBER
000780          PERFORM EXIT RECORD
000790              A C (TYPE C RECORDS)
000800              B PROGRAM NUMBER
000810              C PROGRAM ID
000820              D PERFORM NAME SECOND IF PRESENT
000830              E ****
000840              F COBOL REFERENCE NUMBER OF PERFORM
000850              G COBOL STATEMENT NUMBER OF PERFORM
000860              H PER EXT
000870              I            NOT USED
000880              J            NOT USED
000890              K            NOT USED
000900              L RECORD OCCURRENCE NUMBER
000910          PARAGRAPH AND SECTION RECORDS
000920              A C (TYPE C RECORD)
000930              B PROGRAM NUMBER
000940              C PROGRAM ID
000950              D PARAGRAPH OR SECTION NAME
000960              E COBOL REFERENCE NUMBER
000970              F 0000
000980              G            NOT USED
000990              H PARAGRAPH OR SECTION
001000              I            NOT USED
001010              J            NOT USED
001020              K            NOT USED
001030              L RECORD OCCURRENCE NUMBER
001040          CONNECTOR RECORDS
001050              A C (TYPE C RECORDS)
001060              B PROGRAM NUMBER
001070              C PROGRAM ID
001080              D PARAGRAPH THAT GO TO ADDRESSED
001090              E 0000
001100              F COBOL REFERENCE OF GO TO
001110              G COBOL STATEMENT OF GO TO
001120              H CONNECTOR
001130              I PARAGRAPH THAT GO TO WAS WITHIN
001140              J            NOT USED
001150              K            NOT USED
001160              L RECORD OCCURRENCE NUMBER
001170          SWITCH / ALTER RECORD
001180              A C (TYPE C RECORD)
001190              B PROGRAM NUMBER
001200              C PROGRAM ID
001210              D ALTER NAME FIRST ONE
001220              E ----
001230              F COBOL REFERENCE NUMBER OF ALTER
001240              G COBOL STATEMENT NUMBER OF ALTER
001250              H SWITCH
001260              I ALTER NAME SECOND ONE
001270              J            NOT USED
001280              K            NOT USED
001290              L RECORD OCCURRENCE NUMBER
001300          CONNECTOR FROM SWITCH / ALTER SETTING RECORD
001310              A C (TYPE C RECORD)
001320              B PROGRAM NUMBER
001330              C PROGRAM ID
001340              D ALTER NAME SECOND ONE
001350              E ----
001360              F 0000
001370              G 0000
001380              H SWITCHENT
001390              I ALTER NAME FIRST ONE
001400              J            NOT USED
001410              L RECORD OCCURRENCE NUMBER
001420     OUTPUT FILES.  PRG-3.
001430        SUPPLEMENTAL-PART3-OUT.
001440           1.  SORTED AND PASSED TO PRG-4.
001450                  MAJOR FIELD  01  -  01 ASCENDING
001460                  MINOR FIELD  05  -  64 ASCENDING.
001470           2. RECORD LENGTH IS 120 CHARACTERS.
001480           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
001490           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
001500              IF NECESSARY.
001510           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
001520              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
001530              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
001540              FILE IN THE FLOBOL SYSTEM.
001550           6. A DESCRIPTION OF THE RECORDS FOUND IN THIS FILE
001560              FOLLOW.
001570           NOTE  **  THIS FILE CONTAINS ALL OF THE RECORDS FOUND
001580           IN INFILE  INCLUDING THE FOLLOWING NEW RECORDS.
001590          CREATED FROM CONNECTOR VALUES FOR GO TO STATEMENT.
001600              A C (TYPE C RECORD)
001610              B PROGRAM NUMBER
001620              C PROGRAM ID
001630              D PARAGRAPH THAT GO TO WAS WITHIN
001640              E COBOL REFERENCE OF GO TO
001650              F COBOL STATEMENT NUMBER OF GO TO
001660              G 0000
001670              H GOTOLINE
001680              I            NOT USED
001690              J COBOL REFERENCE NUMBER OF PARAGRAPH + 01
001700              K 0001
001710              L 00000000
001720          CREATED FROM PERFORM ENTRANCE VALUES
001730              A C (TYPE C RECORD)
001740              B PROGRAM NUMBER
001750              C PROGRAM ID
001760              D PARAGRAPH THAT PERFORM WAS WITHIN
001770              E COBOL REFERENCE NUMBER OF PERFORM
001780              F COBOL STATEMENT NUMBER OF PERFORM
001790              G 0000
001800              H PRFMLINE
001810              I            NOT USED
001820              J COBOL REFERENCE NUMBER OF PARAGRAPH + 01
001830              K 0001
001840              L 00000000
001850 ENVIRONMENT DIVISION.
001860 CONFIGURATION SECTION.
001870 INPUT-OUTPUT SECTION.
001880 FILE-CONTROL.
001890     SELECT INFILE ASSIGN TO
001900     UT-S-XXXXX04.
001910     SELECT PARFILEIN ASSIGN TO
001920     UT-S-XXXXX03.
001930     SELECT SUPPLEMENTAL-PART3-OUT ASSIGN TO
001940     UT-S-XXXXX02.
001950 DATA DIVISION.
001960 FILE SECTION.
001970 FD  INFILE
001980     LABEL RECORDS ARE STANDARD
001990     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002000     DATA RECORDS ARE INRECORD INWHOLE.
002010 01  INRECORD.
002020     02 SKEY                            PICTURE IS X.
002030     02 FILLER                          PICTURE IS XXX.
002040     02 SPROGNO                         PICTURE IS 9999.
002050     02 SPROGRAMID                      PICTURE IS X(12).
002060     02 SPARNAME                        PICTURE IS X(32).
002070     02 SHOLD1                          PICTURE IS 9999.
002080     02 SHOLD2                          PICTURE IS 9999.
002090     02 SHOLD3                          PICTURE IS 9999.
002100     02 SWHAT                           PICTURE IS X(9).
002110     02 SFROMPAR                        PICTURE IS X(32).
002120     02 SDIFFERENT REDEFINES SFROMPAR.
002130     03 SHOLD4                          PICTURE IS XXXX.
002140     03 SHOLD5                          PICTURE IS XXXX.
002150     03 FILLER                          PICTURE IS X(24).
002160     02 NEXT-SENTENCE-LINENO            PICTURE IS 9999.
002170     02 FILLER                          PICTURE IS X(3).
002180     02 SLOCATION                       PICTURE IS 9(8).
002190 01  INWHOLE                            PICTURE IS X(120).
002200 FD  PARFILEIN
002210     LABEL RECORDS ARE STANDARD
002220     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002230     DATA RECORDS ARE PARRECIN.
002240 01  PARRECIN.
002250     02 PARPROGNO                       PICTURE IS 9999.
002260     02 PARPARNAME                      PICTURE IS X(32).
002270     02 PARLINE                         PICTURE IS 9999.
002280     02 PARSTATE                        PICTURE IS 9999.
002290     02 PARLOCATION                     PICTURE IS 9(8).
002300     02 FILLER                          PICTURE X(8).
002310 FD  SUPPLEMENTAL-PART3-OUT
002320     LABEL RECORDS ARE STANDARD
002330     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002340     DATA RECORDS ARE SORTWHOLE SORTRECORD.
002350 01  SORTWHOLE                          PICTURE IS X(120).
002360 01  SORTRECORD.
002370     02 SORTKEY                         PICTURE IS X(64).
002380     02 SSORTKEY REDEFINES SORTKEY.
002390     03 SSKEY                           PICTURE IS X.
002400     03 SSKEYFILL                       PICTURE IS XXX.
002410     03 SSPROGNO                        PICTURE IS 9999.
002420     03 SSPROGRAMID                     PICTURE IS X(12).
002430     03 SSPARNAME                       PICTURE IS X(32).
002440     03 SSHOLD1                         PICTURE IS 9999.
002450     03 SSHOLD2                         PICTURE IS 9999.
002460     03 SSHOLD3                         PICTURE IS 9999.
002470     02 SSWHAT                          PICTURE IS X(9).
002480     02 SSFROMPAR                       PICTURE IS X(32).
002490     02 SSDIFFERENT REDEFINES SSFROMPAR.
002500     03 SSHOLD4                         PICTURE IS 9999.
002510     03 SSHOLD5                         PICTURE IS 9999.
002520     03 SSFILLER1                       PICTURE IS X(24).
002530     02 SSFILLER2                       PICTURE IS X(7).
002540     02 SSLOCATION                      PICTURE IS 9(8).
002550 WORKING-STORAGE SECTION.
002560 77  CNTER  PICTURE S9  VALUE ZERO.
002570 77  PARHLD  PICTURE X(32) VALUE SPACE.
002580 77  NEXT-SENTENCE-LINENO-X             PICTURE IS XXXX.
002590 PROCEDURE DIVISION.
002600 FPOC         SECTION.
002610 BEGINPHASE1.
002620     OPEN     OUTPUT SUPPLEMENTAL-PART3-OUT.
002630     OPEN     INPUT INFILE.
002640     OPEN     INPUT PARFILEIN.
002650     READ     INFILE AT END
002660     DISPLAY ' FLOBOL03, NO INFILE RECORDS PRESENT, EOJ '
002670     GO TO FPOCEND.
002680     READ     PARFILEIN AT END
002690     DISPLAY ' FLOBOL03, NO PARFILEIN RECORDS PRESENT, EOJ '
002700     GO TO FPOCEND.
002710     GO       TO MAINTESTS.
002720 READINFILE.
002730     READ     INFILE AT END
002740              GO TO FPOCEND.
002750     IF       SPARNAME IS EQUAL TO PARPARNAME
002760              GO TO MAINTESTS.
002770     MOVE PARPARNAME TO PARHLD.
002780 R1F2.
002790     READ     PARFILEIN AT END
002800     DISPLAY ' FLOBOL03, PARFILEIN ENDS BEFORE INFILE, EOJ '
002810     GO TO FPOCEND.
002820     IF PARPARNAME EQUAL TO PARHLD
002830     DISPLAY ' DUPLICATE PARAGRAPH NAMES, NAME SKIPPED '
002840     DISPLAY PARPARNAME
002850     GO TO R1F2.
002860     MOVE 0 TO CNTER.
002870 RECHECK.
002880     IF       SPARNAME IS EQUAL TO PARPARNAME
002890              GO TO MAINTESTS.
002900     DISPLAY  'SPARNAME=' SPARNAME.
002910     DISPLAY 'PARPARNAME=' PARPARNAME.
002920     DISPLAY 'NO MATCH, BAD PROCEDURE REFERENCE'.
002930     ADD 1 TO CNTER.
002940     IF CNTER GREATER THAN 5
002950     DISPLAY ' ABORT FLOBOL-3'
002960     GO TO FPOCEND.
002970     READ     INFILE AT END
002980              GO TO FPOCEND.
002990     GO       TO RECHECK.
003000 MAINTESTS.
003010     IF       SWHAT IS EQUAL TO 'CONNECTOR' OR
003020              SWHAT IS EQUAL TO 'PER ENT  '
003030              GO TO CREATION.
003040     IF       SWHAT IS EQUAL TO 'SWITCH   ' OR
003050              SWHAT IS EQUAL TO 'SWITCHENT'
003060              GO TO ISSWITCH.
003070     MOVE     INWHOLE TO SORTWHOLE.
003080     MOVE     PARLOCATION TO SSLOCATION.
003090     WRITE    SORTRECORD.
003100     GO       TO READINFILE.
003110 CREATION.
003120     MOVE     INWHOLE TO SORTWHOLE.
003130     MOVE     SPACES TO SSFROMPAR.
003140     MOVE     PARLOCATION TO SSLOCATION.
003150     MOVE     NEXT-SENTENCE-LINENO TO NEXT-SENTENCE-LINENO-X.
003160     IF       SPARNAME IS EQUAL TO SFROMPAR AND
003170              NEXT-SENTENCE-LINENO-X NOT EQUAL TO SPACES
003180              MOVE 'XXX' TO SSKEYFILL.
003190     WRITE    SORTRECORD.
003200     MOVE 'C' TO SSKEY.
003210     MOVE     SPACES TO SSKEYFILL.
003220     MOVE     SPROGNO TO SSPROGNO.
003230     MOVE     SPROGRAMID TO SSPROGRAMID.
003240     MOVE     SFROMPAR TO SSPARNAME.
003250     MOVE     SHOLD2 TO SSHOLD1.
003260     MOVE     SHOLD3 TO SSHOLD2.
003270     MOVE     ZEROES TO SSHOLD3.
003280     IF       SWHAT IS EQUAL TO 'CONNECTOR'
003290              MOVE 'GOTO LINE' TO SSWHAT
003300              ELSE
003310              MOVE 'PRFMLINE ' TO SSWHAT.
003320     MOVE     NEXT-SENTENCE-LINENO TO NEXT-SENTENCE-LINENO-X.
003330     IF       SPARNAME IS EQUAL TO SFROMPAR AND
003340              NEXT-SENTENCE-LINENO-X NOT EQUAL TO SPACES
003350              MOVE NEXT-SENTENCE-LINENO TO SSHOLD4
003360              ELSE
003370              MOVE PARLINE TO SSHOLD4.
003380     MOVE     0001 TO SSHOLD5.
003390     MOVE     SPACES TO SSFILLER1 SSFILLER2.
003400     MOVE     ZEROES TO SSLOCATION.
003410     WRITE    SORTRECORD.
003420     GO       TO READINFILE.
003430 ISSWITCH.
003440     MOVE     INWHOLE TO SORTWHOLE.
003450     MOVE 'Z' TO SSKEY.
003460     MOVE     SPARNAME TO SSFROMPAR.
003470     MOVE     SFROMPAR TO SSPARNAME.
003480     MOVE     PARLOCATION TO SSLOCATION
003490              WRITE SORTRECORD.
003500     GO       TO READINFILE.
003510 FPOCEND.
003520     CLOSE    INFILE.
003530     CLOSE    SUPPLEMENTAL-PART3-OUT.
003540     CLOSE    PARFILEIN.
003550     STOP     RUN.
