  RCT: PROC OPTIONS(MAIN);
  /*     THE IDEA FOR THIS COMMAND CAME FROM
         HOWARD GILBERT
         YALE UNIVERSITY COMPUTER CENTER
         175 WHITNEY AVE
         NEW HAVEN, CT.
         203 432-4080

         IT WAS REWRITTEN BY
         ROBERT P WENZEL
         MEMOREX CORPORATION
         MS 10-02
         SAN TOMAS AT CENTRAL EXPRESSWAY
         SANTA CLARA, CA 95052

         UPDATED 08SEP80, JACK LEMA, MEMOREX CORPORATION, MS 10-35,
         SAN TOMAS AT CENTRAL EXPRESSWAY, SANTA CLARA, CA., 95052
         .  UPDATED FOR SE 1.1 WITH SU64

         UPDATED 22OCT80, JACK LEMA, MEMOREX CORPORATION, MS 10-35,
         SAN TOMAS AT CENTRAL EXPRESSWAY, SANTA CLARA, CA., 95052
         .  UPDATED FOR SE 2.0 WITH SU64

         USE PLI OPTIMIZING COMPILER            */
  DCL 1 OLDPVTV (12) FIXED BIN(31) INIT(0);
  DCL 1 SUM (12)     FIXED BIN(31) INIT(0);
  DCL SIXTEEN PTR;
  UNSPEC(SIXTEEN)=(24)'0'B××'00010000'B;
  DCL ISASIZE FIXED BIN(31) EXT INIT(4096);
  DCL CVTPTR PTR BASED(SIXTEEN);
  DCL 1 CVT BASED(CVTPTR),
          2 F1       CHAR(356),
          2 CVTPVTP  PTR,
          2 F2       CHAR(244),
          2 CVTOPCTP PTR;
  DCL 1 PVT BASED(CVTPVTP),
          2 F1       CHAR(248),
          2 PVTV (12) FIXED BIN(31);
  DCL 1 RMCT BASED(CVTOPCTP),
          2 RMCTID   CHAR(4),
          2 F1       CHAR(16),
          2 RMCTRMCA PTR,
          2 F2       CHAR(204),
          2 RMCTRCT  PTR;
  DCL 1 RCT BASED(RMCTRCT),
          2 RCTID    CHAR(4),
          2 F4       CHAR(58),
          2 (UICA,CPUA,AVQC) FIXED BIN,
          2 F5       FIXED BIN,
          2 (ASMQA,PTR) FIXED BIN;
  DCL 1 RMCA BASED(RMCTRMCA),
          2 RMCAID   CHAR(4),
          2 F1       CHAR(2),
          2 RMCAINUS FIXED BIN;
 DO I=1 TO 12;
    OLDPVTV(I)=PVTV(I);
    END;
 PUT EDIT('CPU STATISTICS')
         (COL(1),A);
 IF RMCTID^='RMCT' THEN PUT EDIT(' RMCT VALIDATION FAILED')
         (COL(1),A);
 IF RMCAID^='RMCA' THEN PUT EDIT(' RMCA VALIDATION FAILED')
         (COL(1),A);
 PUT EDIT('IN CORE USERS',RMCAINUS)
         (COL(1),A(20),F(5));
 IF RCTID^='RCT ' THEN PUT EDIT(' RCT VALIDATION FAILED')
         (COL(1),A);
 PUT EDIT('RCT AVERAGES',
          'UIC',UICA,'CPU',CPUA/16,'ASMQ',ASMQA,'PAGING',PTR)
         (COL(1),A(15),2(A(5),F(5),X(2)),2(A(10),F(5),X(2)));
 DELAY(3000);   /* WAIT 3 SECONDS */
 DO I=1 TO 12;
    IF (PVTV(I) >= OLDPVTV(I))
       THEN SUM(I) = PVTV(I) - OLDPVTV(I);
            ELSE SUM(I) = 2147483647 - OLDPVTV(I) + PVTV(I);
    END;
 PUT EDIT('SWAP','IN',(SUM(6)+1)/3,'OUT',(SUM(7)+1)/3,'NUMBER',
          (SUM(9)+1)/3,'TOTAL',(SUM(6)+SUM(7)+1)/3)
         (COL(1),A(15),2(A(5),F(5),X(2)),2(A(10),F(5),X(2)));
 PUT EDIT('PAGE','IN',(SUM(1)+1)/3,'OUT',(SUM(2)+1)/3,'RECLAIMS',
          (SUM(8)+1)/3,'TOTAL',(SUM(1)+SUM(2)+SUM(8)+1)/3)
         (COL(1),A(15),2(A(5),F(5),X(2)),2(A(10),F(5),X(2)));
 PUT EDIT('COMMON','IN',(SUM(10)+1)/3,'OUT',(SUM(11)+1)/3,'RECLAIMS',
          (SUM(12)+1)/3,'TOTAL',(SUM(10)+SUM(11)+SUM(12)+1)/3)
         (COL(1),A(15),2(A(5),F(5),X(2)),2(A(10),F(5),X(2)));
 PUT EDIT('VIO','IN',(SUM(3)+1)/3,'OUT',(SUM(4)+1)/3,'RECLAIMS',
          (SUM(5)+1)/3,'TOTAL',(SUM(3)+SUM(4)+SUM(5)+1)/3)
         (COL(1),A(15),2(A(5),F(5),X(2)),2(A(10),F(5),X(2)));
 END;
