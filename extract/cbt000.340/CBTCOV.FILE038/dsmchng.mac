* SYSTEM NAME:        DSM- DASD SPACE MANAGEMENT SYSTEM
*
* PROGRAM NAME:       DSMCHNG
*
* PROGRAM TITLE:      DSM DASD CHANGE HISTORY REPORTS
*
* AUTHOR:             G. W. JACKSON - MEMOREX CORPORATION
*
* ABSTRACT:           THIS PROGRAM COMPARES DASD SPACE ALLOCATION
*                     DATA FROM TWO DIFFERENT POINTS IN TIME AND
*                     REPORTS THE DIFFERENCES BETWEEN THE DATA.
*
* METHOD:             THE INPUT TO THIS PROGRAM IS TWO GENERATIONS
*                     OF DASD SPACE ALLOCATION DATA.  THE DATA IS
*                     ORIGINALLY PRODUCED BY THE C.S.G. $MONVOL
*                     PROGRAM, SORTED BY VOLSER AND DSNAME, AND
*                     PROCESSED BY AN EXTRACT PROGRAM WHICH BUILDS
*                     A CONDENSED RECORD AND WRITES IT TO A
*                     GENERATION DATASET.  ANY TWO GENERATIONS OF
*                     DATA CAN BE COMPARED ALTHOUGH IT WOULD BE
*                     MOST COMMON TO COMPARE THE CURRENT TO THE
*                     PRECEEDING GENERATION.
*
* PARMS:              NONE
*
* JCL REQUIRED:       //STEP  EXEC  PGM=DSMCHNG
*                     //SYSPRINT DD  SYSOUT=A
*                     //DSMOLD   DD  << PREV. HISTORY DATA >>
*                     //DSMNEW   DD  << CURR. HISTORY DATA >>
*                     //DSMREPT1 DD  SYSOUT=A
*                     //DSMREPT2 DD  SYSOUT=A
*                     //DSMREPT3 DD  SYSOUT=A
*
* HISTORY:  15 AUG 78 - FIRST VERSION.
*
*           30 AUG 78 - MODIFICATIONS: CHANGES TO TRACKS USED NOT
*                       REPORTED IN CHANGE SUMMARY; LEADING ZEROES
*                       SUPPRESSED IN DETAIL REPORT; THIS COMMENT
*                       PAGE ADDED; LISTING CONTROL ADDED.
*
DSMCHNG  START 0
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         PRINT NOGEN
BEGIN    SAVE  (14,12)
         BALR  R11,0
         USING *,R11,R12
         LA    R12,2048(R11)
         LA    R12,2048(R12)
         ST    R13,SAVE+4
         LA    R13,SAVE
         OPEN  (OLDFILE,INPUT,NEWFILE,INPUT,RPTFILE1,OUTPUT,           C
               RPTFILE2,OUTPUT,RPTFILE3,OUTPUT)
         BAL   R10,READOLD             READ OLD FILE
         CLI   OLDREC+1,X'FF'          FILE MISSING?
         BNE   OLDDATE1                NO - CHECK FOR DATE
OLDDATE1 CLI   OLDREC+1,C' '           IS THIS A DATE RECORD?
         BE    NEWDATE1                YES - TRY NEW DATE
NEWDATE1 CLI   NEWREC+1,X'FF'          FILE MISSING?
         BNE   NEWDATE2                NO - CHECK FOR DATE
NEWDATE2 BAL   R10,READNEW             READ NEW FILE
         CLI   NEWREC+1,C' '           IS THIS A DATE RECORD?
         BE    HOUSEKPG                YES - SET UP HEADINGS ETC.
HOUSEKPG DAY   DATE8=TODAY
         MVC   TITLE1A+10(8),TODAY     MOVE RUN DATE TO TITLE
         MVC   TITLE1C+67(8),OLDREC+6  MOVE OLD DATE TO TITLE
         MVC   TITLE1C+78(8),NEWREC+6  MOVE NEW DATE TO TITLE
         BAL   R10,READOLD             READ OLD FILE
         BAL   R10,READNEW             READ NEW FILE
         EJECT
MAINLOOP CLC   OPACKDSN,NPACKDSN       IS OLD PACK/DSN = NEW?
         BH    NOOLDREC                NO - OLD RECORD MISSING
MOVEOLD  L     R5,POLDDSCT
         LA    R5,1(R5)                ADD ONE TO OLD DS COUNT
         ST    R5,POLDDSCT
         MVC   SPACKDSN,OPACKDSN       MOVE OLD PACK/DSN TO SAVE
         MVC   OUTLINE1+1(44),ODSNAME  MOVE OLD DSNAME TO LINE
         MVC   OUTLINE1+47(4),ODSORG   MOVE OLD DSORG TO LINE
         MVC   OUTLINE1+54(4),OTRKSALL MOVE OLD TRKS TO LINE
         CONVB WOLDALLC,OTRKSALL,L=4
         LTR   R15,R15                 ERROR?
         BZ    MOVEOLDU                NO - GO ON
         XC    WOLDALLC,WOLDALLC       YES - SET TO ZERO
MOVEOLDU MVC   OUTLINE1+74(4),OTRKSUSD MOVE OLD TRKS USED TO LINE
         CONVB WOLDUSED,OTRKSUSD,L=4
         LTR   R15,R15                 ERROR?
         BZ    CMPPKDSN                NO - GO ON
         XC    WOLDUSED,WOLDUSED       YES - SET TO ZERO
CMPPKDSN CLC   OPACKDSN,NPACKDSN       IS OLD PACK/DSN = NEW?
         BE    MOVENEW                 YES - MOVE NEW DATA
         MVC   OUTLINE1+61(3),=C'N/A'  NO - MOVE 'N/A' TO LINE
         XC    WNEWALLC,WNEWALLC
         MVC   OUTLINE1+81(3),=C'N/A'  MOVE 'N/A' TO LINE
         XC    WNEWUSED,WNEWUSED
         MVC   OUTLINE1+94(25),OLDDELMS MOVE OLD DELETED MSG TO LINE
         B     CALCCHNG                CALCULATE CHANGES
NOOLDREC MVC   SPACKDSN,NPACKDSN       MOVE NEW PACK/DSN TO SAVE
         MVC   OUTLINE1+1(44),NDSNAME  MOVE NEW DSNAME TO LINE
         MVC   OUTLINE1+47(4),NDSORG   MOVE NEW DSORG TO LINE
         MVC   OUTLINE1+55(3),=C'N/A'  MOVE 'N/A' TO LINE
         XC    WOLDALLC,WOLDALLC
         MVC   OUTLINE1+75(3),=C'N/A'  MOVE 'N/A' TO LINE
         XC    WOLDUSED,WOLDUSED
         MVC   OUTLINE1+94(25),NEWADDMS MOVE NEW ADDED MSG TO LINE
         MVC   OUTLINE1+114(8),NCREATED MOVE CREATE DATE TO LINE
MOVENEW  L     R5,PNEWDSCT
         LA    R5,1(R5)                ADD ONE TO NEW COUNT
         ST    R5,PNEWDSCT
         MVC   OUTLINE1+60(4),NTRKSALL MOVE NEW TRKS ALLC TO LINE
         CONVB WNEWALLC,NTRKSALL,L=4
         LTR   R15,R15                 ERROR?
         BZ    MOVENEWU                NO - GO ON
         XC    WNEWALLC,WNEWALLC       YES - SET TO ZERO
MOVENEWU MVC   OUTLINE1+80(4),NTRKSUSD MOVE NEW TRKS USED TO LINE
         CONVB WNEWUSED,NTRKSUSD,L=4
         LTR   R15,R15                 ERROR?
         BZ    CALCCHNG                NO - GO ON
         XC    WNEWUSED,WNEWUSED       YES - SET TO ZERO
CALCCHNG L     R5,WOLDALLC
         L     R6,WNEWALLC
         LA    R4,OUTLINE1+65
         LA    R3,OUTLINE1+94          ADDRESS OF MESSAGE AREA
         BAL   R10,CALCULAT
CALCUSED L     R5,WOLDUSED
         L     R6,WNEWUSED
         LA    R4,OUTLINE1+85
         BAL   R10,CALCULAT
         CLI   OUTLINE1+94,C' '        ANY PREVIOUS MESSAGE?
         BNE   CHKFIRST                YES - NO CHANGE MESSAGE
         CLC   OUTLINE1+65(5),=C' SAME' DID ALLOCATION CHANGE?
         BE    CHKUSAGE                NO - CHECK USAGE CHANGE
         MVC   0(10,R3),=C'ALLOCATION' MOVE FIRST PART OF MESSAGE
         LA    R3,10(R3)               MOVE R3 TO OPEN POSITION
CHKUSAGE CLC   OUTLINE1+85(5),=C' SAME' DID USAGE CHANGE?
         BE    FINISHMS                NO - FINISH MESSAGES
         CLI   OUTLINE1+94,C' '        ANY MESSAGE YET?
         BE    NOCOMMA                 NO - NO COMMA
         MVC   0(2,R3),=C', '          MOVE COMMA TO MESSAGE
         LA    R3,2(R3)                MOVE R3 TO OPEN POSITION
NOCOMMA  MVC   0(5,R3),=C'USAGE'       MOVE 'USAGE' TO MESSAGE
         LA    R3,5(R3)                MOVE R3 TO OPEN POSITION
FINISHMS CLI   OUTLINE1+94,C' '        ANY MESSAGE FINALLY?
         BE    CHKFIRST                NO -
         MVC   0(8,R3),=C' CHANGED'    FINISH MESSAGE
CHKFIRST CLC   PREVPACK,=C'      '     FIRST TIME?
         BNE   COMPPAKS                NO - COMPARE THIS TO PREVIOUS
         MVC   PREVPACK,SPAKNAME       SET PACK NAME
         B     ADDALLOC                ADD TO TOTALS
COMPPAKS CLC   SPAKNAME,PREVPACK       IS THIS PACK SAME AS PREV?
         BE    ADDALLOC                YES - ADD TO TOTALS
         BAL   R10,SUMPACK             SUMMARIZE PACK INFORMATION
ADDALLOC L     R5,WOLDALLC
         L     R6,POLDALLC
         AR    R6,R5
         ST    R6,POLDALLC
         L     R5,WNEWALLC
         L     R6,PNEWALLC
         AR    R6,R5
         ST    R6,PNEWALLC
         L     R5,WOLDUSED
         L     R6,POLDUSED
         AR    R6,R5
         ST    R6,POLDUSED
         L     R5,WNEWUSED
         L     R6,PNEWUSED
         AR    R6,R5
         ST    R6,PNEWUSED
         CLI   OUTLINE1+94,C' '
         BE    CLEARLIN                NO CHANGE
         CLI   OUTLINE1+94,C'U'
         BE    WRITERPT
         L     R5,PCHGDSCT
         LA    R5,1(R5)
         ST    R5,PCHGDSCT
         CLI   OUTLINE1+94,C'N'        IS THIS AN ADD?
         BNE   CHKDELET                NO - CHECK OTHER ACTIVITY
         L     R5,PADDDSCT
         LA    R5,1(R5)                ADD ONE TO ADDED COUNT
         ST    R5,PADDDSCT
         L     R5,WNEWALLC
         L     R6,PADDALLC
         AR    R6,R5
         ST    R6,PADDALLC
         B     WRITERPT                WRITE LINE
CHKDELET CLI   OUTLINE1+94,C'O'        IS THIS A DELETE?
         BNE   CHKCHANG                NO - CHECK OTHER ACTIVITY
         L     R5,PDELDSCT
         LA    R5,1(R5)                ADD ONE TO DELETE COUNT
         ST    R5,PDELDSCT
         L     R5,WOLDALLC
         L     R6,PDELALLC
         AR    R6,R5
         ST    R6,PDELALLC
         B     WRITERPT                WRITE LINE
CHKCHANG L     R5,WOLDALLC
         L     R6,WNEWALLC
         CR    R5,R6                   IS OLD GREATER THAN NEW?
         BH    ADDMINUS                YES - DO DECREASE
         SR    R6,R5
         L     R7,PPLSALLC
         AR    R7,R6
         ST    R7,PPLSALLC
         L     R5,PPLSDSCT
         LA    R5,1(R5)
         ST    R5,PPLSDSCT
         B     WRITERPT
ADDMINUS SR    R5,R6
         L     R7,PMINALLC
         AR    R7,R5
         ST    R7,PMINALLC
         L     R5,PMINDSCT
         LA    R5,1(R5)
         ST    R5,PMINDSCT
         B     WRITERPT                WRITE LINE
WRITERPT LA    R3,OUTLINE1+54
         BAL   R10,SUPPZERO            SUPPRESS ZEROS
         LA    R3,OUTLINE1+60
         BAL   R10,SUPPZERO            SUPPRESS ZEROS
         LA    R3,OUTLINE1+74
         BAL   R10,SUPPZERO            SUPPRESS ZEROS
         LA    R3,OUTLINE1+80
         BAL   R10,SUPPZERO            SUPPRESS ZEROS
         L     R5,LINECTR1
         LA    R5,1(R5)
         ST    R5,LINECTR1
         C     R5,PAGELIM              LINES OVER PAGE LIMIT?
         BNH   PUTLINE1                NO - WRITE LINE
         BAL   R10,TITLES1             YES - PUT OUT TITLES
PUTLINE1 PUT   RPTFILE1,OUTLINE1
CLEARLIN MVI   OUTLINE1,C' '
         MVC   OUTLINE1+1(132),OUTLINE1
CHKOLD   CLC   OPACKDSN,SPACKDSN       IS OLD PACK/DSN = SAVE?
         BNE   CHKNEW                  NO - CHECK NEW RECORD
         CLC   OPACKDSN,HEXFF          IS OLDFILE AT END?
         BE    CHKNEW                  YES - DON'T READ
         BAL   R10,READOLD             READ OLD FILE
CHKNEW   CLC   NPACKDSN,SPACKDSN       IS NEW PACK/DSN = SAVE?
         BNE   ENDMAIN                 NO -
         CLC   NPACKDSN,HEXFF          IS NEWFILE AT END?
         BE    ENDMAIN                 YES - DON'T READ
         BAL   R10,READNEW             READ NEW FILE
ENDMAIN  B     MAINLOOP                BR TO TOP OF LOOP
         EJECT
SUMPACK  STM   R2,R10,SUBSAVE          SAVE REGISTERS
         MVI   SUMLINE1+1,C' '
         MVC   SUMLINE1+2(131),SUMLINE1+1
         MVC   SUMLINE1+5(6),PREVPACK  SET UP PACK TOTAL LINE
         MVC   SUMLINE1+12(6),=C'TOTALS'
         L     R5,POLDALLC
         L     R7,TOLDALLC
         AR    R7,R5
         ST    R7,TOLDALLC
         LA    R4,SUMLINE1+52
         BAL   R10,LOADLINE
         L     R5,PNEWALLC
         L     R7,TNEWALLC
         AR    R7,R5
         ST    R7,TNEWALLC
         LA    R4,SUMLINE1+58
         BAL   R10,LOADLINE
         L     R5,POLDALLC
         L     R6,PNEWALLC
         LA    R4,SUMLINE1+65
         BAL   R10,CALCULAT
         ST    R6,PCHGALLC
         L     R5,POLDUSED
         L     R7,TOLDUSED
         AR    R7,R5
         ST    R7,TOLDUSED
         LA    R4,SUMLINE1+72
         BAL   R10,LOADLINE
         L     R5,PNEWUSED
         L     R7,TNEWUSED
         AR    R7,R5
         ST    R7,TNEWUSED
         LA    R4,SUMLINE1+78
         BAL   R10,LOADLINE
         L     R5,POLDUSED
         L     R6,PNEWUSED
         LA    R4,SUMLINE1+85
         BAL   R10,CALCULAT
         PUT   RPTFILE1,SUMLINE1
         BAL   R10,TITLES1
         MVI   OUTLINE2+1,C' '
         MVC   OUTLINE2+2(131),OUTLINE2+1
         MVC   OUTLINE2+1(6),PREVPACK
         L     R5,POLDDSCT
         L     R7,TOLDDSCT
         AR    R7,R5
         ST    R7,TOLDDSCT
         L     R5,PNEWDSCT
         L     R7,TNEWDSCT
         AR    R7,R5
         ST    R7,TNEWDSCT
         L     R5,PADDDSCT
         L     R7,TADDDSCT
         AR    R7,R5
         ST    R7,TADDDSCT
         LA    R4,OUTLINE2+7
         BAL   R10,LOADLINE
         L     R5,PADDALLC
         L     R7,TADDALLC
         AR    R7,R5
         ST    R7,TADDALLC
         LA    R4,OUTLINE2+13
         BAL   R10,LOADLINE
         L     R5,PDELDSCT
         L     R7,TDELDSCT
         AR    R7,R5
         ST    R7,TDELDSCT
         LA    R4,OUTLINE2+19
         BAL   R10,LOADLINE
         L     R5,PDELALLC
         L     R7,TDELALLC
         AR    R7,R5
         ST    R7,TDELALLC
         LA    R4,OUTLINE2+25
         BAL   R10,LOADLINE
         L     R5,PPLSDSCT
         L     R7,TPLSDSCT
         AR    R7,R5
         ST    R7,TPLSDSCT
         LA    R4,OUTLINE2+31
         BAL   R10,LOADLINE
         L     R5,PPLSALLC
         L     R7,TPLSALLC
         AR    R7,R5
         ST    R7,TPLSALLC
         LA    R4,OUTLINE2+37
         BAL   R10,LOADLINE
         L     R5,PMINDSCT
         L     R7,TMINDSCT
         AR    R7,R5
         ST    R7,TMINDSCT
         LA    R4,OUTLINE2+43
         BAL   R10,LOADLINE
         L     R5,PMINALLC
         L     R7,TMINALLC
         AR    R7,R5
         ST    R7,TMINALLC
         LA    R4,OUTLINE2+49
         BAL   R10,LOADLINE
         L     R5,PCHGDSCT
         L     R7,TCHGDSCT
         AR    R7,R5
         ST    R7,TCHGDSCT
         LA    R4,OUTLINE2+55
         BAL   R10,LOADLINE
         L     R5,PCHGALLC
         L     R7,TCHGALLC
         AR    R7,R5
         MVC   OUTLINE2+62(6),SUMLINE1+66
         L     R5,TOTTRKS
         L     R6,PNEWALLC
         SR    R5,R6
         CONVD OUTLINE2+71,(R5),L=6
         L     R5,LINECTR2
         LA    R5,1(R5)
         ST    R5,LINECTR2
         C     R5,PAGELIM              LINES OVER PAGE LIMIT?
         BNH   PUTLINE2                NO - WRITE LINE
         BAL   R10,TITLES2             YES - PUT OUT TITLES
PUTLINE2 PUT   RPTFILE2,OUTLINE2
         L     R5,LINECTR3
         LA    R5,3(R5)
         ST    R5,LINECTR3
         C     R5,PAGELIM              LINES OVER PAGE LIMIT?
         BNH   BLDLINE3                NO - WRITE LINE
         BAL   R10,TITLES3             YES - PUT OUT TITLES
BLDLINE3 PUT   RPTFILE3,SKELETON
         MVC   TRKSALLC,POLDALLC
         MVC   TRKSUSED,POLDUSED
         MVC   DSCOUNT,POLDDSCT
         MVC   VOLNAME,PREVPACK
         BAL   R10,GRAPHRTN
         MVC   TRKSALLC,PNEWALLC
         MVC   TRKSUSED,PNEWUSED
         MVC   DSCOUNT,PNEWDSCT
         MVC   VOLNAME,=C' (NEW)'
         BAL   R10,GRAPHRTN
         PUT   RPTFILE3,SKELETON
         L     R5,TOTTRKS
         L     R6,GRANDTOT
         AR    R6,R5
         ST    R6,GRANDTOT
         XC    POLDALLC,POLDALLC
         XC    PNEWALLC,PNEWALLC
         XC    POLDUSED,POLDUSED
         XC    PNEWUSED,PNEWUSED
         XC    POLDDSCT,POLDDSCT
         XC    PNEWDSCT,PNEWDSCT
         XC    PADDDSCT,PADDDSCT
         XC    PADDALLC,PADDALLC
         XC    PDELDSCT,PDELDSCT
         XC    PDELALLC,PDELALLC
         XC    PPLSDSCT,PPLSDSCT
         XC    PPLSALLC,PPLSALLC
         XC    PMINDSCT,PMINDSCT
         XC    PMINALLC,PMINALLC
         XC    PCHGDSCT,PCHGDSCT
         XC    PCHGALLC,PCHGALLC
         MVC   PREVPACK,SPAKNAME       SET PACK NAME
         LM    R2,R10,SUBSAVE          RESTORE REGISTERS
         BR    R10
         EJECT
CALCULAT SR    R6,R5
         CONVD (R4),(R6),L=5
         LTR   R6,R6
         BZ    NOCHANGE
         BNM   SIGNPLUS
         MVI   5(R4),C'-'              SIGN IT MINUS
         B     ENDCALC
SIGNPLUS MVI   5(R4),C'+'              SIGN IT PLUS
         B     ENDCALC
NOCHANGE MVC   1(4,R4),=C'SAME'        MOVE 'SAME' TO DIFFERENCE
ENDCALC  BR    R10                     RETURN TO INVOCATION
         EJECT
SUPPZERO LA    R4,4                    SET R4 TO 4
ZEROLOOP CLI   0(R3),C'0'              IS THIS A ZERO?
         BNE   ENDSUPP                 NO - STOP SUPPRESSING
         MVI   0(R3),C' '              YES - MOVE BLANK TO POSITION
         LA    R3,1(R3)                BUMP UP R6
         BCT   R4,ZEROLOOP             TRY AGAIN
ENDSUPP  BR    R10                     RETURN TO INVOCATION
         EJECT
GRAPHRTN STM   R2,R10,SUBSAVE2        SAVE REGISTERS
         L     R5,TRKSALLC
         SR    R4,R4
         MH    R5,=H'100'              ADJUST FOR PERCENT CALULATION
         D     R4,TOTTRKS              CALCULATE PERCENT ALLOCATED
         ST    R5,PCTALLC
         L     R5,TRKSUSED
         SR    R4,R4
         MH    R5,=H'100'              ADJUST FOR PERCENT CALCULATION
         D     R4,TOTTRKS              CALCULATE PERCENT ALLOCATED
         ST    R5,PCTUSED
         MVC   OUTLINE3,SKELETON
         MVC   OUTLINE3+1(6),VOLNAME
         CONVD OUTLINE3+9,TRKSALLC,L=6
         CONVD OUTLINE3+16,TRKSUSED,L=6
         CONVD OUTLINE3+23,DSCOUNT,L=6
         LA    R9,OUTLINE3+32          POINT TO START OF GRAPH
         L     R8,PCTUSED              SET R8 TO PCT USED
         LTR   R8,R8
         BZ    SETLOOP2                BYPASS IF ZERO
LOOP1    LA    R9,1(R9)                POINT R9 TO NEXT POS IN GRAPH
         MVI   0(R9),C'*'              MOVE '*' TO GRAPH
         BCT   R8,LOOP1                BR ON COUNT TO TOP OF LOOP
SETLOOP2 L     R8,PCTALLC              SET R8 TO PCT ALLOCATED
         L     R7,PCTUSED
         SR    R8,R7                   SUBTRACT USED FROM ALLOCATED
         BNP   PUTLINE3                BYPASS IF NOT POSITIVE
LOOP2    LA    R9,1(R9)                POINT R9 TO NEXT POS IN GRAPH
         MVI   0(R9),C'.'              MOVE '.' TO GRAPH
         BCT   R8,LOOP2                BR ON COUNT TO TOP OF LOOP
PUTLINE3 PUT   RPTFILE3,OUTLINE3       PRINT DETAIL LINE
         LM    R2,R10,SUBSAVE2        RESTORE REGISTERS
         BR    R10                     RETURN
         EJECT
LOADLINE CONVD (R4),(R5),L=6
         BR    R10
         EJECT
TITLES1  SR    R5,R5
         ST    R5,LINECTR1             ZERO LINECTR1
         L     R5,PAGECTR1
         LA    R5,1(R5)
         ST    R5,PAGECTR1             ADD 1 TO PAGECTR1
         CONVD TITLE1A+130,PAGECTR1,L=3
         PUT   RPTFILE1,TITLE1A
         PUT   RPTFILE1,TITLE1B
         PUT   RPTFILE1,TITLE1C
         PUT   RPTFILE1,HEADER1A
         PUT   RPTFILE1,HEADER1B
         PUT   RPTFILE1,HEADER1C
         BR    R10                     RETURN TO INVOCATION
         EJECT
TITLES2  SR    R5,R5
         ST    R5,LINECTR2             ZERO LINECTR2
         L     R5,PAGECTR2
         LA    R5,1(R5)
         ST    R5,PAGECTR2             ADD 2 TO PAGECTR2
         CONVD TITLE1A+130,PAGECTR2,L=3
         PUT   RPTFILE2,TITLE1A
         PUT   RPTFILE2,TITLE1B
         PUT   RPTFILE2,TITLE1C
         PUT   RPTFILE2,HEADER2A
         PUT   RPTFILE2,HEADER2B
         PUT   RPTFILE2,HEADER2C
         BR    R10                     RETURN TO INVOCATION
         EJECT
TITLES3  SR    R5,R5
         ST    R5,LINECTR3             ZERO LINECTR3
         L     R5,PAGECTR3
         LTR   R5,R5                   IS THIS FIRST PAGE?
         BZ    ADDPAGE3                YES - BYPASS FOOTINGS
         PUT   RPTFILE3,HEADER3B        PAGE FOOTINGS
         MVI   HEADER3A,C' '
         PUT   RPTFILE3,HEADER3A
         MVI   HEADER3A,C'0'
ADDPAGE3 LA    R5,1(R5)
         ST    R5,PAGECTR3             ADD 1 TO PAGECTR3
         CONVD TITLE1A+130,PAGECTR3,L=3
         PUT   RPTFILE3,TITLE1A
         PUT   RPTFILE3,TITLE1B
         PUT   RPTFILE3,TITLE1C
         PUT   RPTFILE3,HEADER3A
         PUT   RPTFILE3,HEADER3B
         BR    R10                     RETURN TO INVOCATION
         EJECT
READOLD  GET   OLDFILE,OLDREC
         BR    R10                     NORMAL RETURN
EOFOLD   CLC   NPACKDSN,HEXFF          IS NEW DSNAME HEX FF'S?
         BE    EOJRTN                  YES - BR TO EOJ.
         MVC   OPACKDSN,HEXFF          MOVE HEX FF'S TO OLD.
         BR    R10                     RETURN TO INVOCATION.
         EJECT
READNEW  GET   NEWFILE,NEWREC
         BR    R10                     NORMAL RETURN
EOFNEW   CLC   OPACKDSN,HEXFF          IS OLD DSNAME HEX FF'S?
         BE    EOJRTN                  YES - BR TO EOJ.
         MVC   NPACKDSN,HEXFF          MOVE HEX FF'S TO NEW.
         BR    R10                     RETURN TO INVOCATION.
         EJECT
EOJRTN   BAL   R10,SUMPACK              SUMMARIZE LAST PACK
         MVI   SUMLINE1+1,C' '
         MVC   SUMLINE1+2(131),SUMLINE1+1
         MVC   SUMLINE1+5(15),=C'TOTAL ALL PACKS' MARK LINE
         L     R5,TOLDALLC
         LA    R4,SUMLINE1+52
         BAL   R10,LOADLINE
         L     R5,TNEWALLC
         LA    R4,SUMLINE1+59
         BAL   R10,LOADLINE
         L     R5,TOLDALLC
         L     R6,TNEWALLC
         LA    R4,SUMLINE1+66
         LA    R3,SUMLINE1+95          ADDRESS OF MESSAGE AREA
         BAL   R10,CALCULAT
         L     R5,TOLDUSED
         LA    R4,SUMLINE1+73
         BAL   R10,LOADLINE
         L     R5,TNEWUSED
         LA    R4,SUMLINE1+80
         BAL   R10,LOADLINE
         L     R5,TOLDUSED
         L     R6,TNEWUSED
         LA    R4,SUMLINE1+88
         LA    R3,SUMLINE1+95          ADDRESS OF MESSAGE AREA
         BAL   R10,CALCULAT
         PUT   RPTFILE1,SUMLINE1
         MVI   OUTLINE2+1,C' '
         MVC   OUTLINE2+2(131),OUTLINE2+1
         MVC   OUTLINE2+1(6),=C'TOTAL '
         L     R5,TADDDSCT
         LA    R4,OUTLINE2+7
         BAL   R10,LOADLINE
         L     R5,TADDALLC
         LA    R4,OUTLINE2+13
         BAL   R10,LOADLINE
         L     R5,TDELDSCT
         LA    R4,OUTLINE2+19
         BAL   R10,LOADLINE
         L     R5,TDELALLC
         LA    R4,OUTLINE2+25
         BAL   R10,LOADLINE
         L     R5,TPLSDSCT
         LA    R4,OUTLINE2+31
         BAL   R10,LOADLINE
         L     R5,TPLSALLC
         LA    R4,OUTLINE2+37
         BAL   R10,LOADLINE
         L     R5,TMINDSCT
         LA    R4,OUTLINE2+43
         BAL   R10,LOADLINE
         L     R5,TMINALLC
         LA    R4,OUTLINE2+49
         BAL   R10,LOADLINE
         L     R5,TCHGDSCT
         LA    R4,OUTLINE2+55
         BAL   R10,LOADLINE
         MVC   OUTLINE2+62(6),SUMLINE1+66
         L     R5,GRANDTOT
         L     R6,TNEWALLC
         SR    R5,R6
         CONVD OUTLINE2+71,(R5),L=6
         PUT   RPTFILE2,OUTLINE2
         PUT   RPTFILE3,SKELETON
         MVC   TOTTRKS,GRANDTOT
         MVC   TRKSALLC,TOLDALLC
         MVC   TRKSUSED,TOLDUSED
         MVC   DSCOUNT,TOLDDSCT
         MVC   VOLNAME,=C'TOTAL'
         BAL   R10,GRAPHRTN
         MVC   TRKSALLC,TNEWALLC
         MVC   TRKSUSED,TNEWUSED
         MVC   DSCOUNT,TNEWDSCT
         MVC   VOLNAME,=C' (NEW)'
         BAL   R10,GRAPHRTN
         PUT   RPTFILE3,SKELETON
         PUT   RPTFILE3,HEADER3B        PAGE FOOTINGS
         CLOSE (OLDFILE,,NEWFILE,,RPTFILE1,,RPTFILE2,,RPTFILE3)
         L     R13,SAVE+4
         RETURN (14,12)
         EJECT
OLDFILE  DCB   DDNAME=DSMOLD,DSORG=PS,MACRF=GM,EODAD=EOFOLD
NEWFILE  DCB   DDNAME=DSMNEW,DSORG=PS,MACRF=GM,EODAD=EOFNEW
RPTFILE1 DCB   DDNAME=DSMREPT1,DSORG=PS,MACRF=PM
RPTFILE2 DCB   DDNAME=DSMREPT2,DSORG=PS,MACRF=PM
RPTFILE3 DCB   DDNAME=DSMREPT3,DSORG=PS,MACRF=PM
         EJECT
OLDREC   DS    0CL80
OPACKDSN DS    0CL50
OPAKNAME DS    CL6
ODSNAME  DS    CL44
ODSORG   DS    CL4
OCREATED DS    CL8
OTRKSALL DS    CL4
OTRKSUSD DS    CL4
         DS    CL10
NEWREC   DS    0CL80
NPACKDSN DS    0CL50
NPAKNAME DS    CL6
NDSNAME  DS    CL44
NDSORG   DS    CL4
NCREATED DS    CL8
NTRKSALL DS    CL4
NTRKSUSD DS    CL4
         DS    CL10
         EJECT
SAVE     DS    18F
SUBSAVE  DS    9F
SUBSAVE2 DS    9F
HEXFF    DC    50X'FF'
PAGECTR1 DC    F'0'
PAGECTR2 DC    F'0'
PAGECTR3 DC    F'0'
PAGELIM  DC    F'45'
LINECTR1 DC    F'51'
LINECTR2 DC    F'51'
LINECTR3 DC    F'51'
WOLDALLC DC    F'0'
WOLDUSED DC    F'0'
WNEWALLC DC    F'0'
WNEWUSED DC    F'0'
POLDALLC DC    F'0'
POLDUSED DC    F'0'
POLDDSCT DC    F'0'
PNEWALLC DC    F'0'
PNEWUSED DC    F'0'
PNEWDSCT DC    F'0'
PADDDSCT DC    F'0'
PADDALLC DC    F'0'
PDELDSCT DC    F'0'
PDELALLC DC    F'0'
PPLSDSCT DC    F'0'
PPLSALLC DC    F'0'
PMINDSCT DC    F'0'
PMINALLC DC    F'0'
PCHGDSCT DC    F'0'
PCHGALLC DC    F'0'
TOLDALLC DC    F'0'
TOLDUSED DC    F'0'
TOLDDSCT DC    F'0'
TNEWALLC DC    F'0'
TNEWUSED DC    F'0'
TNEWDSCT DC    F'0'
TADDDSCT DC    F'0'
TADDALLC DC    F'0'
TDELDSCT DC    F'0'
TDELALLC DC    F'0'
TPLSDSCT DC    F'0'
TPLSALLC DC    F'0'
TMINDSCT DC    F'0'
TMINALLC DC    F'0'
TCHGDSCT DC    F'0'
TCHGALLC DC    F'0'
TRKSALLC DC    F'0'
TRKSUSED DC    F'0'
DSCOUNT  DC    F'0'
TOTTRKS  DC    F'15351'
TOTALLC  DC    F'0'
TOTUSED  DC    F'0'
TOTDSCNT DC    F'0'
GRANDTOT DC    F'0'
PCTALLC  DC    F'0'
PCTUSED  DC    F'0'
TITLE1A  DS    0CL133
         DC    CL48'1RUN DATE'
         DC    CL77'M E M O R E X   C O R P O R A T I O N'
         DC    CL8'PAGE'
TITLE1B  DS    0CL133
         DC    56C' '
         DC    CL21'DASD SPACE MANAGEMENT'
         DC    56C' '
TITLE1C  DS    0CL133
         DC    47C' '
         DC    CL86'SPACE CHANGE REPORT          -'
HEADER1A DS    0CL133
         DC    CL47'0'
         DC    CL86' DS       TRACKS ALLOC        TRACKS USED'
HEADER1B DS    0CL133
         DC    CL47' DATASET NAME'
         DC    CL47' ORG    OLD   NEW   CHG     OLD   NEW   CHG '
         DC    CL39'REMARKS'
HEADER1C DS    0CL133
         DC    CL47' --------------------------------------------'
         DC    CL47'----   ----  ----  ----    ----  ----  ---- '
         DC    CL39'---------------------------------------'
HEADER2A DS    0CL133
         DC    CL45'0         DS ADDED    DS DELETED  DS + ALLC  '
         DC    CL88' DS - ALLC   TOTAL CHG   FREE SPACE'
HEADER2B DS    0CL133
         DC    CL45' VOLUME   CNT TRACK   CNT  TRKS   CNT  TRKS  '
         DC    CL88' CNT  TRKS   CNT  TRKS   IN TRACKS'
HEADER2C DS    0CL133
         DC    CL45' ------   --- -----   --- -----   --- -----  '
         DC    CL88' --- -----   --- -----     -----'
HEADER3A DS    0CL133
         DC    CL41' VOLUME   ALLC   USED   DSCNT   0'
         DC    CL10'10'
         DC    CL10'20'
         DC    CL10'30'
         DC    CL10'40'
         DC    CL10'50'
         DC    CL10'60'
         DC    CL10'70'
         DC    CL10'80'
         DC    CL9'90'
         DC    CL3'100'
HEADER3B DS    0CL133
         DC    CL32' '
         DC    10C'+---------'
         DC    CL1'+'
OUTLINE1 DC    CL133' '
OUTLINE2 DC    CL133'0'
OUTLINE3 DS    CL133
SKELETON DS    0CL133
         DC    CL32' '
         DC    10CL10'+'
         DC    CL1'+'
SUMLINE1 DC    CL133'0'
OLDDELMS DC    CL25'OLD DATASET DELETED'
NEWADDMS DC    CL25'NEW DATASET CREATED'
CHANGEMS DC    CL25'CHANGE'
TODAY    DC    CL8'      '
SPACKDSN DS    0CL50
SPAKNAME DC    CL6' '
         DC    CL44' '
PREVPACK DC    CL6' '
VOLNAME  DS    CL6
         END   BEGIN
