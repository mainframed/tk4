COPYTAPE   CSECT
         BEGIN
         GETPARM PARMS=2
         MVC   PRTPAT(133),OKMSG2     SAVE PRINT PATTERNS
         OPEN   (SYSPRINT,OUTPUT)
*  READ THE JFCB'S
         RDJFCB MF=(E,RDJFCB1)   READ THE SYSUT1 JFCB
         RDJFCB MF=(E,RDJFCB2)   READ THE SYSUT2 JFCB
  SPACE 1
         LA    R7,0           SET FILE NO TO 0
NEWFILE  LA    R7,1(R7)       BUMP FILE NO
         STH   R7,FLSQ1       STOR FILE NO IN JFCB1
         STH   R7,FLSQ2       STOR FILE NO IN JFCB2
*
         OPEN  (SYSUT1,INPUT),TYPE=J
         OPEN  (SYSUT2,OUTPUT),TYPE=J
  EJECT
*  SET UP R6 AS RECORD COUNTER
         SR    R6,R6
  SPACE 1
*  SET UP DATE AND TIME
         TIME
         ST    R1,CONVERT
         ED    OKDATE(7),CONVERT+1
         ST    R0,CONVERT
         ED    OKTIME(9),CONVERT
  SPACE 1
*  COPY SYSUT1 TO SYSUT2 AND COUNT THE RECORDS
*
COPY     EQU   *
         GET   SYSUT1
         LA    R6,1(R6)   INCREMENT RECORD COUNT
         MVC   UT2LRECL(2),UT1LRECL  PUT SYSUT2 LRECL IN DCB
         CLC   0(3,R1),=C'VOL'      IS THIS VOL RECORD
         BNE   PUTX                 NO, GO WRITE IT
         CLC   4(6,1),PARM1         YES, IS THIS SER NO OK
         BNE   PUTX                 NO, GO WRITE IT
         MVC   4(6,1),PARM2         CHANGE SER NO TO OP SER NO
PUTX     PUTX  SYSUT2,SYSUT1
         B     COPY
  EJECT
UT1EOD   EQU   *
  SPACE 1
         CH    R6,=H'0'        IS RECORD COUNT ZERO?
         BE    STOP0           YES, END OF TAPE, GO STOP
         MVC   OKMSG2(132),PRTPAT           MOVE PATTERN TO OP RECORD
*  SET UP THE RECORD COUNT
         CVD   R6,CONVERT
         ED    RECCNT(10),CONVERT+4
   SPACE  1
*  PUT OUTPUT BLKSIZE INTO MESSAGE
         LH    R6,UT2LRECL
         CVD   R6,CONVERT
         ED    OPBLKSZ(8),CONVERT+4
  EJECT
*  PRINT THE SYSPRINT MESSAGES
*
PRINT    EQU   *
         PUT   SYSPRINT,OKMSG2
         CLOSE (SYSUT1,,SYSUT2)
      SPACE    1
         B     NEWFILE            GO COPY NEXT FILE
  EJECT
STOP0    CLOSE (SYSUT1,,SYSUT2)
         PUT   SYSPRINT,OKMSG
       STOP
  SPACE 1
STOP8    STOP  RC=8
  SPACE 2
ANALYS   SYNADAF ACSMETH=QSAM
         TM    SYSPRINT+30,X'10'  IS SYSPRINT DD CARD PRESENT
         BZ    ABEND
         PUT   SYSPRINT,R1
         SYNADRLS
ABEND    EQU   *
         ABEND 16,DUMP
   EJECT
CONVERT  DS    1D
ZERO     DC    X'00000000'
  SPACE 3
*  MESSAGES
*
*
OKMSG    DC    CL133' COPYTAPE - SUCCESSFUL END - RETURN CODE = 00 .  '
*
OKMSG2   EQU   *
         DC    C' DATE = '
OKDATE   DC    X'4020204B212020'
         DC    C', TIME = '
OKTIME   DC    X'4020204B21204B2020'
         DC    C','
         DC    C' RECORDS PROCESSED ='
RECCNT   DC    X'40206B2020206B202120'
         DC    C', '
         DC    C'BLKSIZE ='
OPBLKSZ  DC    X'4020202020202120'
         DC    CL55' '
PRTPAT   DC    CL133' '
  EJECT
SYSUT1   DCB   DEVD=DA,DSORG=PS,RECFM=U,EODAD=UT1EOD,MACRF=GL,         X
               EXLST=EXLIST1,BLKSIZE=16383,                            X
               SYNAD=ANALYS,DDNAME=SYSUT1,BUFNO=3
UT1LRECL EQU   SYSUT1+82
 SPACE 2
SYSUT2   DCB   DEVD=DA,DSORG=PS,RECFM=U,MACRF=PM,EXLST=EXLIST2,        X
               SYNAD=ANALYS,DDNAME=SYSUT2,BUFNO=3,BLKSIZE=16383
UT2LRECL EQU   SYSUT2+82
 SPACE 2
SYSPRINT DCB   DEVD=DA,DSORG=PS,RECFM=FBA,LRECL=133,BLKSIZE=133,       X
               MACRF=PT,DDNAME=SYSPRINT,SYNAD=ABEND
  EJECT
RDJFCB1  RDJFCB SYSUT1,MF=L
  SPACE 1
EXLIST1  DC    X'07'
         DC    AL3(JFCB1)
         DC    X'80'
JFCB1    DS    44F
LRECL1   EQU   JFCB1+104
BLKSI1   EQU   JFCB1+102
FLSQ1    EQU   JFCB1+68
  SPACE 3
RDJFCB2  RDJFCB SYSUT2,MF=L
  SPACE 1
         DS    0F
EXLIST2  DC    X'07'
         DC    AL3(JFCB2)
         DC    X'80'
JFCB2    DS    44F
JFCRECFM EQU   JFCB2+100
JFCBLKSI EQU   JFCB2+102
JFCLRECL EQU   JFCB2+104
FLSQ2    EQU   JFCB2+68
  SPACE 1
         END   COPYTAPE
