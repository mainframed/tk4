DSHEX    CSECT
         PRINT NOGEN
         BEGIN  BASE=(R12,R11),SIZE=4000
         USING  WORK,R13
         EJECT
************************************
*
*        PURPOSE :
*         INSPECT THE KEYWORDS :
*          STARTKEY , ENDKEY , SKIPKEY , ALLKEY ;
*         IF THE KEYWORD VALUE IS HEX NOTATION THEN
*          REPLACE <X'.....'>  WITH TRUE HEX STRING ;
*
*        ON ANY ERROR :
*         - PAD THE KEYWORD ON THE RIGHT WITH AT LEAST
*           ONE '@' ;
*         - DO NOT MOD KEYWORD VALUE ;
*         - RC=4
*           (NOTE : THE MAINLINE DSUTIL WILL HANDLE THIS AS ERROR )
*
*         INPUT :
*          - R1 POINTS TO PARM1 ;
******************************************************
         EJECT
         MVI   KWFLAG,X'00'   PRESET OK RETURN
         SR    R15,R15
         CLC   0(8,R1),=CL8' '  ANYTHING AT ALL ?
         BE    RETURN         B. IF NOTHING
         SPACE 3
         LR   R3,R1           SAVE INPUT PTR
         LR   R4,R3           FLOATING PARMX PTR
D0090    LA    R5,KWTAB       SELECT KEYWORD
D0100    CLC   0(8,R4),0(R5)
         BE    D0200          B. TO PROCESS HEX STRING
         LA    R5,8(R5)  NEXT IN KEYWORD TABLE
         CLI   0(R5),C' '
         BNE   D0100
         SPACE 3
D0150    LA    R4,12(R4)    NEXT IN INPUT PARMX
         CLI   0(R4),C' '
         BNE   D0090
         SPACE 5
         SR    R15,R15
         CLI   KWFLAG,X'00'
         BE    RETURN
         LA    R15,4      SET BAD RC
RETURN   STOP  RC=(R15)
         EJECT
D0200    EQU   *          LOOK FOR ANY HEX STRING
*        DO NOT TOUCH R3,R4,R5
*        R4   POINTS TO KEYWORD ENTRY
*        RETURN TO D0150  FOR NEXT PARMX
*
         L     R6,8(R4)   PTR TO STRING
         SR    R7,R7
         IC    R7,8(R4)   L-1 OF STRING
         CH    R7,=H'3'
         BL    D0150     B. IF NO CANDIDATE
         LA    R15,0(R6,R7)   PTS TO END OF STRING
         CLC   0(2,R6),=C'X'''
         BNE   D0150     B. IF NO HEX STRING
         CLI   0(R15),C''''   ELSE END MUST BE A QOUTE
         BE    D0210     B. IF OK
         BAL   R10,DS100    ELSE  FLAG KEYWORD AS BAD
         B     D0150     AND GO TO NEXT PARMX
D0210    SH    R7,=H'3'  ADJUST L-1 OF INNER STRING
         LA    R6,2(R6)  AND POITER TO IT
         EX    R7,DMVC01   MVC   VALUE(1),0(R6)  MOVE TO OWN AREA
         SR    R9,R9     ADJUST FOR REAL PAIR IN VALUE
         LR    R8,R7
         SRDL  R8,1      IF ODD THE REAL LEWNGTH IS EVEN AND OK
         LTR   R9,R9
         BNZ   D0220     B. IF OK
         MVC   HVALUE(256),VALUE
         EX    R7,DMVC02   MVC   VALUE+1(1),HVALUE
         MVI   VALUE,C'0'  AND ADD A ZERO IN FRONT
         LA    R7,1(R7)
D0220    EX    R7,DTRT01   TRT  VALUE(1),TRTTAB   CHECK FOR VALID
*              CHAR IN HEX STRING
         BZ    D0230     B. IF OK
         BAL   R10,DS100   ELSE FLAG AS BAD STRING
         B     D0150      AND GO FOR THE NEXT ONE
D0230    EX    R7,DTR01    TR   VALUE(1),TRTAB
*         REPLACE  WITH BIN VALUES
         LR    R8,R7      WORKING REG LENGTH VOUNT
         L     R6,8(R4)   PTR TO TARGET
         SR    R10,R10    NEW LENGTH COUNTER , AFTER PACKING
         LA    R9,VALUE   SOURCE TO PACK FROM
         MVI   PACKI+2,X'00'   DUMMY TRAILING BYTE
D0240    MVC   PACKI(2),0(R9)
         PACK  PACKO(2),PACKI(3)
         MVC   0(1,R6),PACKO    RESUTING PACKED BYTE
         LA    R6,1(R6)
         LA    R10,1(R10)
         LA    R9,2(R9)
         SH    R8,=H'2'
         BP    D0240
         BCTR  R10,0    L-1 OF RESULT STRING
         STC   R10,8(R4)
         B     D0150    RETURN TO MAIN LINE
         EJECT
***********************************
*
*        ERROR IN HEX CHAR FLD
*
*        PAD KEYWORD WITH C'@'
*
*        SET KWFLAG
*
*
*        R4  POINTS TO KEYWORD NAME
*********************************
DS100    STM   R2,R10,DSAVE
         LA    R10,7(R4)    END OF NAME
         MVI   0(R10),C'@'  FORCE
         BCTR  R10,0
DS105    CLI   0(R10),C' '
         BNE   DS110
         MVI   0(R10),C'@'
         BCTR   R10,0
         B     DS105
DS110    MVI   KWFLAG,X'FF'
         LM    R2,R10,DSAVE
         BR    R10
         EJECT
KWTAB    DC    CL8'STARTKEY'
         DC    CL8'STKY'
         DC    CL8'H'
         DC    CL8'ENDKEY'
         DC    CL8'NDKY'
         DC    CL8'I'
        DC    CL8'SKIPKEY'
        DC    CL8'SKKY'
        DC    CL8'J'
        DC    CL8'ALLKEY'
        DC    CL8'ALKY'
        DC    CL8'K'
        DC    CL8' '     END OF TAB
TRTTAB  DC    256X'FF'
        ORG   TRTTAB+C'A'
        DC    6X'00'     ALLOW A-F AS VALID
        ORG   TRTTAB+C'0'
        DC    10X'00'     ALLOW  0-9  AS VALID
        ORG   TRTTAB+256
TRTAB   DC    256X'00'
        ORG   TRTAB+C'A'
        DC    X'0A0B0C0D0E0F'
        ORG   TRTAB+C'0'
        DC    X'00010203040506070809'
        ORG   TRTAB+256
        SPACE 5
DMVC01  MVC   VALUE(1),0(R6)
DMVC02  MVC   VALUE+1(1),HVALUE
DTRT01   TRT   VALUE(1),TRTTAB
DTR01    TR    VALUE(1),TRTAB
         LTORG
         EJECT
WORK     DSECT
         DS    18F
DSAVE    DS    9F
KWFLAG   DS    X
VALUE    DS    CL300
HVALUE   DS    CL300
PACKI    DS    CL3
PACKO    DS    CL2
         END
