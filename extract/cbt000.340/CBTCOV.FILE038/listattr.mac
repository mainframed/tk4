LISTATTR CSECT
**
*   LISTATTR  -  DAVID G. SAGER  -  FEB, 1976
*
*   ATTRIBUTES - REENTRANT, REFRESHABLE, REUSEABLE
*
**
*   UPDATED 07JUN79, WILLIAM SMITH, MEMOREX CORPORATION
*
*        THIS COMMAND HAS BEEN EXAMINED FOR COMPATABILITY WITH IBM
*   PROGRAM PRODUCT 5740-XT6/TSO COMMAND PACKAGE (SU-11) FOR MVS 3.8
*   AND HAS BEEN FOUND TO NATIVELY SUPPORT THE ADDED FEATURE OF
*   'OPTCD(R)' - RELATIVE BLOCK ADDRESSING.  CONSEQUENTLY, NO CHANGES
*   TO THE SOURCE CODE WILL BE NECESSARY IF THE PRODUCT IS INSTALLED.
**
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
         EJECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING LISTATTR,R12
         LR    R2,R1              A(CPPL)
         GETMAIN R,LV=WKAL
         LR    R11,R1
         USING WKA,R11
         LA    R10,SAVE
         ST    R13,SAVE+4
         ST    R10,8(R13)
         LR    R13,R10
         MVI   SW,X'00'
         L     R3,16                                  CVT
         L     R3,0(R3)                               NEW/CURRENT TCB
         L     R3,4(R3)                               CURRENT TCB
         L     R3,12(R3)                              TIOT
         LA    R3,24(R3)          FIRST TIOT ENTRY
         USING TIOELNGH,R3
TIOTENT  XR    R4,R4
         IC    R4,TIOELNGH        LENGTH OF THIS ENTRY
         LTR   R4,R4              END OF TIOT?
         BZ    EXIT               YES, EXIT
         CLI   TIOEDDNM,X'00'     VALID DDNAME?
         BE    NEXTTIO            NO, CONTINUE
         CLI   TIOEDDNM,X'40'     VALID DDNAME?
         BE    NEXTTIO            NO, CONTINUE
         XR    R5,R5
         ICM   R5,7,TIOEJFCB      A(JFCB)-16
         BZ    NEXTTIO            IF ZERO, SKIP
         LA    R5,16(R5)          A(JFCB)
         USING INFMJFCB,R5
         CLC   JFCBDSNM(44),NULLFILE  DSNAME = NULLFILE?
         BE    FORMAT             YES, REPRESENTS ATTRIBUTE
NEXTTIO  DS    0H
         AR    R3,R4              POINT TO NEXT TIOT ENTRY
         B     TIOTENT
FORMAT   DS    0H
         TM    TIOELINK,TIOTTERM  IS THIS A TERMINAL FILE?
         BO    NEXTTIO            YES, SKIP
         MVI   SW,X'FF'
         MVI   OUT,X'40'                              BLANK OUT
         MVC   OUT+1(249),OUT
         LA    R10,OUT
         MVC   0(8,R10),TIOEDDNM                      MOVE IN NAME
         LA    R10,9(R10)
TTINPUT  TM    JFCBFLG2,X'80'                           INPUT?
         BNO   TTOUTPUT                                 NO
         MVC   0(5,R10),=CL5'INPUT'
         LA    R10,6(R10)
         B     TEXPDT
TTOUTPUT TM    JFCBFLG2,X'40'                            OUTPUT?
         BNO   TDIAGNS                                   NO
         MVC   0(6,R10),=CL6'OUTPUT'
         LA    R10,7(R10)
TDIAGNS  TM    JFCBFLG2,X'04'                            GTF TRACE?
         BNO   TEXPDT                                    NO
         MVC   0(13,R10),=CL13'DIAGNS(TRACE)'
         LA    R10,14(R10)
TEXPDT   CLC   JFCBXPDT(3),ZERO                       ANY EXPDT?
         BE    TBUFNO                                 NO
         SR    R6,R6                                      PICK
         MVC   0(12,R10),=CL12'EXPDT(00000)'              UP
         IC    R6,JFCBXPDT                                  YEAR
         CVD   R6,DBLEWORD
         UNPK  ANSWER(8),DBLEWORD+4(4)
         OI    ANSWER+7,X'F0'
         MVC   6(2,R10),ANSWER+6
         SR    R6,R6                                      PICK
         LH    R6,JFCBXPDT+1                               UP
         CVD   R6,DBLEWORD                                  DAY
         UNPK  ANSWER(8),DBLEWORD+4(4)                       OF
         OI    ANSWER+7,X'F0'                                 YEAR
         MVC   8(3,R10),ANSWER+5
         LA    R10,13(R10)
TBUFNO   CLI   JFCBUFNO,X'00'                         ANY BUFNO?
         BE    TBFTEK                                 NO
         SR    R6,R6                                      PICK
         IC    R6,JFCBUFNO                                 UP
         CVD   R6,DBLEWORD                                  BUFNO
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(10,R10),=CL10'BUFNO(000)'
         OI    ANSWER+7,X'F0'
         MVC   6(3,R10),ANSWER+5
         LA    R10,11(R10)
TBFTEK   CLI   JFCBFTEK,X'00'                         ANY BFTEK?
         BE    TBUFL                                  NO
         MVC   0(8,R10),=CL8'BFTEK(D)'
         TM    JFCBFTEK,X'60'                             AUTO?
         BNO   TTBFTS                                     NO
         MVI   6(R10),C'A'
         B     TBFTEKE
TTBFTS   TM    JFCBFTEK,X'40'                             SIMPLE?
         BNO   TTBFTR                                     NO
         MVI   6(R10),C'S'
         B     TBFTEKE
TTBFTR   TM    JFCBFTEK,X'20'                             RECORD?
         BNO   TTBFTE                                     NO
         MVI   6(R10),C'R'
         B     TBFTEKE
TTBFTE   TM    JFCBFTEK,X'10'                             EXCHANGE
         BNO   TBFALND                                    NO
         MVI   6(R10),C'E'
TBFTEKE  LA    R10,9(R10)
TBFALND  TM    JFCBFTEK,X'02'                         ANY BFALN(DBLWRD)
         BNO   TBFALNF                                NO
         MVC   0(8,R10),=CL8'BFALN(D)'
         LA    R10,9(R10)
         B     TBUFL
TBFALNF  TM    JFCBFTEK,X'01'                         ANY BFALN(FLLWRD)
         BNO   TBUFL                                  NO
         MVC   0(8,R10),=CL8'BFALN(F)'
         LA    R10,9(R10)
TBUFL    CLC   JFCBUFL(2),ZERO                        ANY BUFL
         BE    TRKP                                   NO
         SR    R6,R6                                      PICK
         LH    R6,JFCBUFL                                  UP
         CVD   R6,DBLEWORD                                  BUFL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(11,R10),=CL11'BUFL(00000)'
         OI    ANSWER+7,X'F0'
         MVC   5(5,R10),ANSWER+3
         LA    R10,12(R10)
TRKP     CLC   JFCRKP(2),ZERO                         ANY BUFL
         BE    TEROPT                                 NO
         SR    R6,R6                                      PICK
         LH    R6,JFCRKP                                   UP
         CVD   R6,DBLEWORD                                  BUFL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(10,R10),=CL10'RKP(00000)'
         OI    ANSWER+7,X'F0'
         MVC   4(5,R10),ANSWER+3
         LA    R10,11(R10)
TEROPT   CLI   JFCEROPT,X'00'                         ANY EROPT?
         BE    TBUFOF                                 NO
         MVC   0(10,R10),=CL10'EROPT(ABE)'
TTACCEPT TM    JFCEROPT,X'80'                             ACCEPT?
         BNO   TTSKIP                                     NO
         MVC   6(3,R10),=CL3'ACC'
         B     TEROPTE
TTSKIP   TM    JFCEROPT,X'40'                             SKIP?
         BNO   TEROPTE                                    NO
         MVC   6(3,R10),=CL3'SKP'
TEROPTE  LA    R10,11(R10)
TBUFOF   CLI   JFCBUFOF,X'00'                         ANY BUFOF?
         BE    TRECFM                                 NO
         MVC   0(6,R10),=CL6'BUFOF('
         LA    R10,6(R10)
         TM    JFCBUFOF,X'80'                         L?
         BNO   TBUFOF1                                NO
         MVI   0(R10),C'L'
         LA    R10,1(R10)
         B     TBUFOFE
TBUFOF1  SR    R6,R6                                      PICK
         IC    R6,JFCBUFOF                                 UP
         CVD   R6,DBLEWORD                                  BUFOF
         UNPK  ANSWER(8),DBLEWORD+4(4)
         OI    ANSWER+7,X'F0'
         MVC   0(3,R10),ANSWER+5
         LA    R10,3(R10)
TBUFOFE  MVI   0(R10),C')'
         LA    R10,2(R10)
TRECFM   CLI   JFCRECFM,X'00'                         ANY RECFM?
         BE    TOPTCD                                 NO
         MVC   0(6,R10),=CL6'RECFM('
         LA    R10,6(R10)
TTUNDEF  TM    JFCRECFM,X'C0'                             UNDEFINED?
         BNO   TTFIXED                                    NO
         MVI   0(R10),C'U'
         LA    R10,1(R10)
         B     TRECFME
TTFIXED  TM    JFCRECFM,X'80'                             FIXED?
         BNO   TTVARIBL                                   NO
         MVI   0(R10),C'F'
         LA    R10,1(R10)
TTVARIBL TM    JFCRECFM,X'40'                             VARIBLE
         BNO   TTTRKOV                                    NO
         MVI   0(R10),C'V'
         LA    R10,1(R10)
TTTRKOV  TM    JFCRECFM,X'20'                             TRKOVERFLW?
         BNO   TTBLKED                                    NO
         MVI   0(R10),C'T'
         LA    R10,1(R10)
TTBLKED  TM    JFCRECFM,X'10'                             BLOCKED?
         BNO   TTSTDBL                                    NO
         MVI   0(R10),C'B'
         LA    R10,1(R10)
TTSTDBL  TM    JFCRECFM,X'08'                             STANDARD?
         BNO   TTASAPC                                    NO
         MVI   0(R10),C'S'
         LA    R10,1(R10)
TTASAPC  TM    JFCRECFM,X'04'                             ASCI CAR CNT?
         BNO   TTMACHCC                                   NO
         MVI   0(R10),C'A'
         LA    R10,1(R10)
TTMACHCC TM    JFCRECFM,X'02'                             MCHN CAR CNT?
         BNO   TRECFME                                     NO
         MVI   0(R10),C'M'
         LA    R10,1(R10)
TRECFME  MVI   0(R10),C')'
         LA    R10,2(R10)
TOPTCD   CLI   JFCOPTCD,X'00'                         ANY OPTCD?
         BE    TDSORG                                 NO
         MVC   0(6,R10),=CL6'OPTCD('
         LA    R10,6(R10)
         TM    JFCDSORG,X'20'                             DIRECT ACCES?
         BO    TOPTDA                                     YES
TOPTPS   DS    0H
TTWTVCH  TM    JFCOPTCD,X'80'                             VALD CHK?
         BNO   TTBEOF                                     NO
         MVI   0(R10),C'W'
         LA    R10,1(R10)
TTBEOF   TM    JFCOPTCD,X'40'                             BYPASS EOF?
         BNO   TTCHNSCH                                   NO
         MVI   0(R10),C'B'
         LA    R10,1(R10)
TTCHNSCH TM    JFCOPTCD,X'20'                             CHAIN SCHD?
         BNO   TTANSITR                                   NO
         MVI   0(R10),C'C'
         LA    R10,1(R10)
TTANSITR TM    JFCOPTCD,X'08'                             TRANLATE?
         BNO   TTREDERR                                   NO
         MVI   0(R10),C'Q'
         LA    R10,1(R10)
TTREDERR TM    JFCOPTCD,X'04'                             REDUCED ERR?
         BNO   TTUSERT                                    NO
         MVI   0(R10),C'Z'
         LA    R10,1(R10)
TTUSERT  TM    JFCOPTCD,X'02'                             USER TOTAL?
         BNO   TOPTCDE                                    NO
         MVI   0(R10),C'T'
         LA    R10,1(R10)
         B     TOPTCDE
TOPTDA   DS    0H
TTWTVCH1 TM    JFCOPTCD,X'80'                             VALD CHK?
         BNO   TTEXTS                                     NO
         MVI   0(R10),C'W'
         LA    R10,1(R10)
TTEXTS   TM    JFCOPTCD,X'20'                             EXT SEARCH?
         BNO   TTFDBK                                     NO
         MVI   0(R10),C'E'
         LA    R10,1(R10)
TTFDBK   TM    JFCOPTCD,X'10'                             FEEDBACK?
         BNO   TTACTADR                                   NO
         MVI   0(R10),C'F'
         LA    R10,1(R10)
TTACTADR TM    JFCOPTCD,X'08'                             ACTUAL ADDR?
         BNO   TTRELBLK                                   NO
         MVI   0(R10),C'A'
         LA    R10,1(R10)
TTRELBLK TM    JFCOPTCD,X'01'                             RELATIVE BLK?
         BNO   TOPTCDE                                    NO
         MVI   0(R10),C'R'
         LA    R10,1(R10)
         B     TOPTCDE
TOPTCDE  MVI   0(R10),C')'
         LA    R10,2(R10)
TDSORG   CLI   JFCDSORG,X'00'                         ANY DSORG?
         BE    TBLKSI                                 NO
         MVC   0(10,R10),=CL10'DSORG(** )'
TTIS     TM    JFCDSORG,X'80'                             IS?
         BNO   TTPS                                       NO
         MVC   6(2,R10),=CL2'IS'
TTPS     TM    JFCDSORG,X'40'                             PS?
         BNO   TTDA                                       NO
         MVC   6(2,R10),=CL2'PS'
TTDA     TM    JFCDSORG,X'20'                             DA?
         BNO   TTPO                                       NO
         MVC   6(2,R10),=CL2'DA'
TTPO     TM    JFCDSORG,X'02'                             PO?
         BNO   TTU                                        NO
         MVC   6(2,R10),=CL2'PO'
TTU      TM    JFCDSORG,X'01'                             UNMOVABLE?
         BNO   TDSORGE                                    NO
         MVI   8(R10),C'U'
TDSORGE  LA    R10,11(R10)
TLIMCT   TM    JFCDSORG,X'20'                         DA?
         BNO   TBLKSI                                 NO, NO LIMCT
         CLC   JFCLIMCT(3),ZERO                       ANY LIMCT?
         BE    TBLKSI                                 NO
         SR    R6,R6                                      PICK
         ICM   R6,7,JFCLIMCT                               UP
         CVD   R6,DBLEWORD                                  LIMCT
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(9,R10),=CL9'LIMCT(00)'
         OI    ANSWER+7,X'F0'
         MVC   6(2,R10),ANSWER+6
         LA    R10,10(R10)
TBLKSI   CLC   JFCBLKSI(2),ZERO                       ANY BLKSIZE?
         BE    TLRECL                                 NO
         SR    R6,R6                                      PICK
         LH    R6,JFCBLKSI                                 UP
         CVD   R6,DBLEWORD                                  BLKSIZE
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(14,R10),=CL14'BLKSIZE(00000)'
         OI    ANSWER+7,X'F0'
         MVC   8(5,R10),ANSWER+3
         LA    R10,15(R10)
TLRECL   CLC   JFCLRECL(2),ZERO                       ANY LRECL?
         BE    TNCP                                   NO
         CLC   JFCLRECL(2),=XL2'8000'  LRECL=X?
         BNE   TLRECL1            NO
         MVC   0(8,R10),=CL8'LRECL(X)'
         LA    R10,9(R10)
         B     TNCP
TLRECL1  SR    R6,R6                                      PICK
         LH    R6,JFCLRECL                                 UP
         CVD   R6,DBLEWORD                                  LRECL
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(12,R10),=CL12'LRECL(00000)'
         OI    ANSWER+7,X'F0'
         MVC   6(5,R10),ANSWER+3
         LA    R10,13(R10)
TNCP     CLI   JFCNCP,X'00'                           ANY NCP?
         BE    TDEN                                   NO
         SR    R6,R6                                      PICK
         IC    R6,JFCNCP                                   UP
         CVD   R6,DBLEWORD                                  NCP
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(7,R10),=CL7'NCP(00)'
         OI    ANSWER+7,X'F0'
         MVC   4(2,R10),ANSWER+6
         LA    R10,8(R10)
TDEN     CLI   JFCDEN,X'00'                           ANY DEN?
         BE    TTRTCH                                 NO
         MVC   0(6,R10),=CL6'DEN(*)'
TTDEN0   CLI   JFCDEN,X'03'                               200 BPI?
         BNE   TTDEN1                                     NO
         MVI   4(R10),C'0'
TTDEN1   CLI   JFCDEN,X'43'                               556 BPI?
         BNE   TTDEN2                                     NO
         MVI   4(R10),C'1'
TTDEN2   CLI   JFCDEN,X'83'                               800 BPI?
         BNE   TTDEN3                                     NO
         MVI   4(R10),C'2'
TTDEN3   CLI   JFCDEN,X'C3'                               1600 BPI?
         BNE   TTDEN4                                     NO
         MVI   4(R10),C'3'
TTDEN4   CLI   JFCDEN,X'D3'                               6250 BPI?
         BNE   TDENE                                      NO
         MVI   4(R10),C'4'
TDENE    LA    R10,7(R10)
TTRTCH   CLI   JFCTRTCH,X'00'                         ANY DEN?
         BE    WRITE                                  NO
         MVC   0(9,R10),=CL9'TRTCH(* )'
TTTRE    CLI   JFCTRTCH,X'23'                             E?
         BNE   TTTRT                                      NO
         MVI   6(R10),C'E'
         B     TTRTCHE
TTTRT    CLI   JFCTRTCH,X'3B'                             T?
         BNE   TTTRC                                      NO
         MVI   6(R10),C'T'
         B     TTRTCHE
TTTRC    CLI   JFCTRTCH,X'13'                             C?
         BNE   TTTRET                                     NO
         MVI   6(R10),C'C'
         B     TTRTCHE
TTTRET   CLI   JFCTRTCH,X'2B'                             ET?
         BNE   TKEYLEN                                    NO
         MVC   6(2,R10),=CL2'ET'
         B     TTRTCHE
TKEYLEN  SR    R6,R6                                      PICK
         IC    R6,JFCKEYLE                                 UP
         CVD   R6,DBLEWORD                                  KEYLEN
         UNPK  ANSWER(8),DBLEWORD+4(4)
         MVC   0(11,R10),=CL11'KEYLEN(000)'
         OI    ANSWER+7,X'F0'
         MVC   7(3,R10),ANSWER+5
TTRTCHE  LA    R10,12(R10)
         SPACE  2
WRITE    LA    R6,OUT
         SR    R10,R6
*
*              PRINT ONLY 79 CHARACTERS PER LINES
*              MAKE SURE LAST CHARACTER OF A LINE IS A BLANK
*
GTRTHN79 LA    R7,79
         CR    R10,R7
         BNH   TPUTFNL
         CLI   79(R6),C' '
         BNE   ADD
         LA    R7,1(R7)
         B     TPUT
ADD      AR    R7,R6
DECRMNT  S     R7,=F'1'
         CLI   0(R7),C' '
         BNE   DECRMNT
         SR    R7,R6
TPUT     TPUT  (R6),(R7)
         AR    R6,R7
         SR    R10,R7
         B     GTRTHN79
TPUTFNL  TPUT  (R6),(R10)
         B     NEXTTIO
EXIT     DS    0H
         CLI   SW,X'FF'           ANY ATTR?
         BE    RETURN             YES
         TPUT  NOATTR,80
RETURN   L     R13,SAVE+4
         LR    R1,R11
         FREEMAIN R,LV=WKAL,A=(1)
         RETURN (14,12)
         EJECT
ZERO     DC    F'0'
BLANK    DC    CL4' '
NULLFILE DC    CL44'NULLFILE'
NOATTR   DC    CL80'NO ATTRIBUTE LISTS WERE FOUND'
WKA      DSECT
SAVE     DS    18F
DBLEWORD DS    D
ANSWER   DS    D
OUT      DS    CL250
SW       DS    X
WKAE     EQU   *
WKAL     EQU   WKAE-WKA
         IEFTIOT1
         IEFJFCBN
         END
