         PRINT OFF
         MACRO
&NAME    INIT &BASE=3,&REGS=Y,&PATCH=3,&RENT=N,&SAVE=Y
         AIF   ((&BASE LT 13) AND (&BASE GT 1)).N020
         MNOTE 12,'INVALID BASE REGISTER'
         MEXIT
.N020    ANOP
         PUSH  PRINT
         PRINT ON,GEN
         EJECT
&SYSECT  CSECT
         USING *,15
         B     BEGIN
         DC    AL1(24)
         DC    CL8'&SYSECT'
         DC    CL16'-&SYSDATE-&SYSTIME'
         AIF   ('&RENT' EQ 'Y').N004
MYSAVE   DC    18F'-1'
.N004    ANOP
         AIF   ('&PATCH' EQ '0').N005
PATCH    DC    &PATCH.CL8'*PATCH*'
.N005    ANOP
         AIF   ('&REGS' EQ 'N').N030
         AIF   ('&REGS' EQ 'Y').N010
         MNOTE 4,'REGS OPERAND INVALID. Y SUBSTITUTED'
.N010    ANOP
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.N030    ANOP
BEGIN    DS   0H
         STM   14,12,12(13)
         LR    &BASE,15
         DROP  15
         USING &SYSECT,&BASE
         AIF   ('&SAVE' EQ 'N').N003
         AIF   ('&RENT' EQ 'Y').N002
         AIF   ('&RENT' EQ 'N').N001
         MNOTE 4,'RENT OPERAND INVALID. N SUBSTITUTED'
.N001    ANOP
         ST    13,MYSAVE+4
         LR    15,13
         LA    13,MYSAVE
         ST    13,8(15)
         AGO   .N003
.N002    ANOP
         GETMAIN R,LV=72
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
.N003    ANOP
         POP   PRINT
         EJECT
         MEND
         PRINT ON
*  MODULE NAME:         VSAMLST6     (REL. 1.1  08/10/79)
*
*  MODULE DESCRIPTION:  VSAM CATALOG LIST UTILITY - REPORT 1 FORMATTER
*
*  RETURN LINKAGE:      RETURN (14,12),RC=(15)
*
*  LINKAGE TABLE:       NONE - SINGLE ENTRY
*
*  PARAMETERS:          R1 POINTS TO THE ADDRESS OF A 92-BYTES RECORD
*                       FROM SORT/VSAMLST5
*
*  EXIT:                RC=00 - SUCCESSFUL
*
*  MODULE FUNCTION:     FOR EACH RECORD FROM SORT/VSAMLST5, THIS MODULE
*                       FORMATS IT FOR THE VSAM VOLUME CONTENTS REPORT.
*
*  CALLER:              VSAMLST5
*
*  CALLS:               NONE
*
*  SYSTEMS SERVICES:    NONE
*
*  MODULE ENVIRONMENT:  OS/VS1
         TITLE 'VSAMLST6 --- VSAM CATALOG LIST UTILITY REPORT 1 FORMATTX
                ER(3.1) - VSAM VOLUME CONTENTS'
*        *-----------------------------------*
*        *   R E G I S T E R   U S A G E S   *
*        *-----------------------------------*
*
*  R0  -  STANDARD LINKAGE
*  R1  -  STANDARD LINKAGE (ADDR OF PARM LIST) & TEMP WORK SPACE
*  R2  -  TEMP WORK SPACE
*  R3  -  TEMP WORK SPACE
*  R4  -  TEMP WORK SPACE
*  R5  -  TEMP WORK SPACE
*  R6  -  NOT USED
*  R7  -  NOT USED
*  R8  -  NOT USED
*  R9  -  BASE REG FOR DSECT INDTAIL
*  R10 -  BASE REG FOR DSECT TXTVCTNT
*  R11 -  RETURN ADDR FOR SUBROUTINES
*  R12 -  BASE REG FOR CSECT VSAMLST6
*  R13 -  STANDARD LINKAGE (ADDR OF SAVE AREA)
*  R14 -  STANDARD LINKAGE (ADDR TO RETURN)
*  R15 -  STANDARD LINKAGE (ADDR OF ENTRY POINT & RETURN CODE)
         SPACE 3
VSAMLST6 CSECT
         INIT  BASE=12
***********************************************************************
*                                                                     *
*  3.1    VSAM VOLUME CONTENTS REPORT FORMATTER                       *
*                                                                     *
*     FUNCTION:  FOR EACH RECORD PASSED TO THIS MODULE, IT FORMATS    *
*                IT FOR THE VSAM VOLUME CONTENTS REPORT.  THIS        *
*                REPORT CONTAINS THE DEVICE DESCRIPTION, THE VOLUME   *
*                INFORMATION, THE AVAILABLE SPACE (OS AND VSAM),      *
*                ENTRY NAME, ENTRY TYPE, CREATION DATE, EXPIRATION    *
*                DATE, ENTRY ORGANIZATION, CONTROL INTERVAL SIZE,     *
*                AVERAGE RECORD SIZE, MAXIMUM RECORD SIZE, TRACKS     *
*                ALLOCATED, TRACKS USED, PERCENTS OF USAGE, NUMBER    *
*                OF EXTENTS, SECONDARY ALLOCATION AND ITS TYPE,       *
*                RECOVERY VOLUME AND AN ENTRY FLAG WHICH IS SET TO    *
*                AN '*' IF SPACE USED IS MORE THAN 85 PERCENTS.       *
*                                                                     *
*     ERRORS:  NONE                                                   *
*                                                                     *
***********************************************************************
         SPACE 3
VCL31    EQU   *
         USING INDTAIL,R9
         USING TXTVCTNT,R10
         LM    R9,R10,0(R1)
         SPACE
         MVC   TXTENTNM,INDENTNM        ENTRY NAME
         SR    R2,R2                    CLEAR INDEX
         TRT   INDENTYP,RECTBL          IS RECORD TYPE DEFINED?
         BZ    SETCREDT                 NO - SKIP THIS FIELD
         BCTR  R2,0                     YES - PROCESS IT
         MH    R2,FIVE                  DISPL = ( INDEX - 1 ) * 5
         LA    R1,ENTYPE(R2)            A(ENTYP) = A(ENTYPTAB) + DISPL
         MVC   TXTENTYP,0(R1)           ENTRY TYPE
         SPACE
SETCREDT EQU   *
         CLC   INDCREDT(2),ZERO         IS IT A ZERO DATE?
         BE    SETEXPDT                 YES - SKIP IT
         LA    R1,INDCREDT              NO - CONVERT IT
         BAL   R11,CNVDATE
         MVC   TXTCREDT,0(R1)           CREATION DATE
         SPACE
SETEXPDT EQU   *
         CLC   INDEXPDT(2),ZERO         IS IT A ZERO DATE?
         BE    SETDSORG                 YES - SKIP IT
         LA    R1,INDEXPDT              NO - CONVERT IT
         BAL   R11,CNVDATE
         MVC   TXTEXPDT,0(R1)           EXPIRATION DATE
         EJECT
SETDSORG EQU   *
         CLI   INDDSORG,X'FF'           IS THIS AN 'ASSOC' RECORD?
         BNE   NOTASSOC                 NO - NEXT TEST
         MVC   TXTDSORG(5),=C'ASSOC'    YES - SET IT
         B     CHKTYPE                  AND SEE IF THERE ARE MORE
         SPACE
NOTASSOC EQU   *
         CLI   INDENTYP,C'I'            IS THIS AN 'INDEX' RECORD?
         BNE   NOTINDEX                 NO - NEXT TEST
         MVC   TXTDSORG(3),=C'IXD'      YES - SET IT
         B     CHKTYPE                  AND SEE IF THERE ARE MORE
         SPACE
NOTINDEX EQU   *
         TM    INDDSORG,X'80'           IS THIS A 'KSDS' ENTRY?
         BZ    NOTAKSDS                 NO - NEXT TEST
         MVC   TXTDSORG(3),=C'IXD'      YES - SET IT
         B     CHKTYPE                  AND SEE IF THERE ARE MORE
         SPACE
NOTAKSDS EQU   *
         TM    INDDSORG,X'02'           IS THIS AN 'ESDS' ENTRY?
         BO    NOTAESDS                 NO - IT'S AN 'RRDS' ENTRY
         MVC   TXTDSORG(4),=C'NIXD'     YES - SET IT
         B     CHKTYPE                  AND SEE IF THERE ARE MORE
         SPACE
NOTAESDS EQU   *
         MVC   TXTDSORG(4),=C'NUMD'     ENTRY ORGANIZATION
         SPACE 2
CHKTYPE  EQU   *
         CLI   INDENTYP,C'D'            IF IT IS A TYPE 'D'
         BE    RPT1PAR2                 OR
         CLI   INDENTYP,C'I'            TYPE 'I'
         BE    RPT1PAR2                 THEN MORE PROCESSING
         B     GOHOME                   ELSE GO HOME
         EJECT
*        *------------------------------------------------------------*
*        *  REPORT 1 PART II - MORE INFO'S ON TYPE 'D' & 'I' RECORDS  *
*        *------------------------------------------------------------*
         SPACE
RPT1PAR2 EQU   *
         ICM   R1,15,INDCISIZ           CONTROL INTERVAL SIZE
         CVD   R1,WORK
         MVC   TXTCISIZ,PAT5
         ED    TXTCISIZ,WORK+5
         SPACE
         ICM   R1,15,INDAVGRC           AVERAGE RECORD SIZE
         BP    SETAVGRC                 ONLY IF IT'S POSITIVE
         MVI   TXTAVGRC+5,C'0'          EITHER ALL X'00' OR ALL X'FF'
         B     SETMAXRC                 NEXT FIELD
         SPACE
SETAVGRC EQU   *
         CVD   R1,WORK
         MVC   TXTAVGRC,PAT5
         ED    TXTAVGRC,WORK+5
         SPACE
SETMAXRC EQU   *
         ICM   R1,15,INDMAXRC           MAXIMUM RECORD SIZE
         CVD   R1,WORK
         MVC   TXTMAXRC,PAT5
         ED    TXTMAXRC,WORK+5
         SPACE
         LH    R1,INDTRKAL              TRACKS ALLOCATED
         CVD   R1,WORK
         MVC   TXTTRKAL,PAT5
         ED    TXTTRKAL,WORK+5
         SPACE
         LH    R1,INDTRKUS              TRACKS USED
         CVD   R1,WORK
         MVC   TXTTRKUS,PAT5
         ED    TXTTRKUS,WORK+5
         SPACE
         SR    R1,R1                    PERCENTS OF USAGE
         IC    R1,INDPCTUS
         CVD   R1,WORK
         MVC   TXTPCTUS,PAT3
         ED    TXTPCTUS,WORK+6
         SPACE
         IC    R1,INDNOEXT              NUMBER OF EXTENTS
         CVD   R1,WORK
         MVC   TXTNOEXT,PAT3
         ED    TXTNOEXT,WORK+6
         EJECT
*        *----------------------------*
*        *  REPORT 1 PART II - CONT.  *
*        *----------------------------*
         SPACE
         ICM   R1,7,INDSCALC            SECONDARY SPACE ALLOCATION
         CVD   R1,WORK
         MVC   TXTSCALC,PAT5
         ED    TXTSCALC,WORK+5
         SPACE
         TM    INDSCTYP,X'C0'           SPACE ALLOCATION TYPE
         BNO   NOTCYL
         MVI   TXTSCTYP,C'C'            B'11.. ....' - CYLINDERS
         B     SETCRAVL
         SPACE
NOTCYL   EQU   *
         TM    INDSCTYP,X'80'
         BZ    SETCRAVL
         MVI   TXTSCTYP,C'T'            B'10.. ....' - TRACKS
         SPACE
SETCRAVL EQU   *
         CLI   INDCRAVL,X'00'           IS THERE A RECOVERY VOLUME?
         BE    SETENTFL                 NO - SKIP THIS FIELD
         MVC   TXTCRAVL,INDCRAVL        YES - MOVE IT
         SPACE
SETENTFL EQU   *
         CLI   INDPCTUS,85              ARE 85 PERCENTS OR MORE USED?
         BL    GOHOME                   NO - ALL DONE
         MVI   TXTENTFL,C'*'            YES - INDICATE SO
         SPACE 2
*        *-------------*
*        *   E X I T   *
*        *-------------*
         SPACE
GOHOME   EQU   *
         L     R13,MYSAVE+4
         RETURN (14,12),RC=(15)
         SPACE 2
         LTORG
         EJECT
*        *-----------------------*
*        *   C O N S T A N T S   *
*        *-----------------------*
         SPACE
ZERO     DC    H'0'
FIVE     DC    H'5'
PAT3     DC    XL4'40202120'
PAT5     DC    XL6'402020202120'
         SPACE 2
RECTBL   DC    256X'00'                 TYPE  0 - UNDEFINED
         ORG   RECTBL+C'A'
         DC    X'01'                    TYPE  1 - NON VSAM
         ORG   RECTBL+C'B'
         DC    X'02'                    TYPE  2 - GENERATION DATA GROUP
         ORG   RECTBL+C'C'
         DC    X'03'                    TYPE  3 - CLUSTER
         ORG   RECTBL+C'D'
         DC    X'04'                    TYPE  4 - DATA
         ORG   RECTBL+C'G'
         DC    X'05'                    TYPE  5 - ALTERNATE INDEX
         ORG   RECTBL+C'I'
         DC    X'06'                    TYPE  6 - INDEX
         ORG   RECTBL+C'R'
         DC    X'07'                    TYPE  7 - PATH
         ORG   RECTBL+C'U'
         DC    X'08'                    TYPE  8 - USER CATALOG
         ORG   RECTBL+C'V'
         DC    X'09'                    TYPE  9 - SPACE (VOLUME)
         ORG   RECTBL+C'X'
         DC    X'0A'                    TYPE 10 - ALIAS
         ORG   RECTBL+C'Y'
         DC    X'0B'                    TYPE 11 - UPGRADE
         ORG
         SPACE 2
ENTYPE   DS    0CL40
         DC    CL5'NVSAM'               A
         DC    CL5'GDG'                 B
         DC    CL5'CL'                  C
         DC    CL5'DATA'                D
         DC    CL5'AIX'                 G
         DC    CL5'INDEX'               I
         DC    CL5'PATH'                R
         DC    CL5'U-CAT'               U
         DC    CL5'SPACE'               V
         DC    CL5'ALIAS'               X
         DC    CL5'UPGRD'               Y
         SPACE 2
         DS    0H
         EJECT
*        *-----------------------------------------------------------*
*        *  DATE CONVERSION ROUTINE FOR CREATION & EXPIRATION DATES  *
*        *  IN:  R1 = ADDR OF PACKED DATE (XL3'YYDDDC')              *
*        *  OUT: R1 = ADDR OF CHAR DATE (C'MM/DD/YY' OR C'YY.DDD  ') *
*        *-----------------------------------------------------------*
         SPACE
CNVDATE  EQU   *
         MVC   YEAR(3),0(R1)            SAVE PARM XL3'YYDDDC'
         OI    DAYS+1,X'0F'
         UNPK  YYDDD,WORD
         MVC   MMDDYY+6(2),YYDDD        SET YEAR
         SPACE
         XC    WORK,WORK                WORK = X'00000000 00000000'
         MVO   WORK+6(2),YEAR           WORK = X'00000000 00000YY0'
         OI    WORK+7,X'0F'             WORK = X'00000000 00000YYF'
         CVB   R4,WORK
         STC   R4,YEAR                  YEAR IN BINARY
         MVC   WORK+6(2),DAYS           WORK = X'00000000 0000DDDF'
         CVB   R4,WORK
         LTR   R4,R4                    IS IT ZERO DAYS?
         BZ    ZERODATE                 YES - SET 00/00/YY
         STH   R4,DAYS                  DAYS IN BINARY
         SPACE
         LA    R3,2                     ASSUME LEAP YEAR
         TM    YEAR,X'03'               IS YEAR A MULTIPLE OF 4?
         BZ    LEAPYEAR                 YES - THIS IS A LEAP YEAR
         LA    R3,2(,R3)                NO - THIS IS NOT A LEAP YEAR
LEAPYEAR EQU   *
         LA    R2,DTCVNTAB              ADDR OF DATE CONVERSION TABLE
         LA    R5,12                    NUMBER OF MONTHS
CMPDAYS  EQU   *
         CH    R4,6(R3,R2)              NUMBER OF DAYS UP TO NEXT MONTH
         BNH   WHICHDAY                 THIS IS THE MONTH
         LA    R2,6(,R2)                BUMP TO NEXT ENTRY
         BCT   R5,CMPDAYS               NEXT MONTH, PLEASE
         EJECT
*        *-----------------------*
*        *  SET DATE AND RETURN  *
*        *-----------------------*
         SPACE
BADYYDDD EQU   *
         MVC   BADDATE(2),YYDDD         BADDATE = C'YY.***  '
         MVC   BADDATE+3(3),YYDDD+2     BADDATE = C'YY.DDD  '
         LA    R1,BADDATE
         BR    R11
         SPACE
ZERODATE EQU   *
         MVC   MMDDYY(5),MMDD0          MMDDYY = C'00/00/YY'
         B     DATESET                  THOUGH INPUT'S ALL ZERO
         SPACE
WHICHDAY EQU   *
         SH    R4,0(R3,R2)
         CVD   R4,WORK
         OI    WORK+7,X'0F'
         UNPK  MMDDYY+3(2),WORK+6(2)    SET DAY
         MVC   MMDDYY(2),0(R2)          SET MONTH
         SPACE
DATESET  EQU   *
         LA    R1,MMDDYY
         BR    R11                      RETURN
         SPACE 2
WORK     DC    D'0'
WORD     DC    F'0'
         ORG   WORD+1
YEAR     DS    XL1
DAYS     DS    H
MMDDYY   DC    C'MM/DD/YY'
BADDATE  DC    C'YY.DDD  '
YYDDD    DC    C'YYDDD'
MMDD0    DC    C'00/00'
         SPACE
DTCVNTAB DS    0H
         DC    C'01',H'0',H'0'
         DC    C'02',H'31',H'31'
         DC    C'03',H'60',H'59'
         DC    C'04',H'91',H'90'
         DC    C'05',H'121',H'120'
         DC    C'06',H'152',H'151'
         DC    C'07',H'182',H'181'
         DC    C'08',H'213',H'212'
         DC    C'09',H'244',H'243'
         DC    C'10',H'274',H'273'
         DC    C'11',H'305',H'304'
         DC    C'12',H'335',H'334'
TOTDAYS  DC    C'13',H'366',H'365'
         EJECT
INDTAIL  DSECT
INDVOLNO DS    CL6                      VOLUME SERIAL NUMBER
INDRPTNO DS    0CL1                     REPORT NO. B'0... ....' DETAILS
INDRCKEY DS    CL4                      RECORD KEY - GENERATED SORT KEY
INDRCSEQ DS    CL2                      RECORD SEQ - SEQ NO IN 'NAMEDS'
INDENTNM DS    CL44                     ENTRY NAME
INDENTYP DS    CL1                      ENTRY TYPE
INDCREDT DS    CL3                      CREATION DATE
INDEXPDT DS    CL3                      EXPIRATION DATE
INDCRAVL DS    CL6                      CRA VOLUME SERIAL NUMBER
INDSCALC DS    CL3                      SECONDARY SPACE ALLOCATION
INDSCTYP DS    CL1                      SPACE ALLOCATION TYPE
INDDSORG DS    CL1                      DATA SET ORGANIZATION (AMDATTR)
INDCISIZ DS    CL4                      CONTROL INTERVAL SIZE
INDMAXRC DS    CL4                      MAXIMUM RECORD SIZE
INDAVGRC DS    CL4                      AVERAGE RECORD SIZE
INDTRKAL DS    H                        TRK'S ALLOC'D FOR DATA SET
INDTRKUS DS    H                        TRK'S USED FOR DATA SET
INDPCTUS DS    CL1                      PERCENT OF USAGE
INDNOEXT DS    CL1                      NO. OF EXTENTS FOR DATA SET
         EJECT
TXTVCTNT DSECT
         DS    CL1
TXTENTNM DS    CL44
         DS    CL1
TXTENTYP DS    CL5
         DS    CL1
TXTCREDT DS    CL8
         DS    CL1
TXTEXPDT DS    CL8
         DS    CL1
TXTDSORG DS    CL5
TXTCISIZ DS    CL6
TXTAVGRC DS    CL6
TXTMAXRC DS    CL6
TXTTRKAL DS    CL6
TXTTRKUS DS    CL6
TXTPCTUS DS    CL4
TXTNOEXT DS    CL4
TXTSCALC DS    CL6
         DS    CL1
TXTSCTYP DS    CL1
         DS    CL1
TXTCRAVL DS    CL6
         DS    CL1
TXTENTFL DS    CL1
         END
