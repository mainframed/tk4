000100 ID DIVISION.
000200 PROGRAM-ID.    VBLKFCTR.
000300 AUTHOR.        C.VILLA
000400 DATE-WRITTEN.  4/06/79.
000500 DATE-COMPILED.
000600     SKIP3
000700 REMARKS.
000800           ****************************************************
000900           *                                                  *
001000           *     WILL PRODUCE FOUR LISTINGS OF THE MOST       *
001100           *     EFFICIENT BLOCKING FACTORS TO USE.           *
001200           *                                                  *
001300           *     USE PARM=NNNNNNNNNNXXXX (FOURTEEN NUMERICS)  *
001400           *     WHERE THE FIRST FIVE  = BEGINNING LRECL      *
001500           *           THE SECOND FIVE = ENDING LRECL         *
001600           *           THE ELEVENTH    = REPORT-1             *
001700           *           THE TWELVETH    = REPORT-2             *
001800           *           THE THIRTEENTH  = REPORT-3             *
001900           *           THE FOURTEENTH  = REPORT-4             *
002000           *                                                  *
002100           *     REPORT-1 = 3330 WITHOUT KEYS                 *
002200           *     REPORT-2 = 3350 WITHOUT KEYS                 *
002300           *     REPORT-3 = 3330 WITH KEYS                    *
002400           *     REPORT-4 = 3350 WITH KEYS                    *
002500           *                                                  *
002600           *     IF PARM NOT USED THE DEFAULT VALUES ARE      *
002700           *     LRECL 10 TO 1000, ALL FOUR REPORTS WILL      *
002800           *     PROCESS.                                     *
002900           *                                                  *
003000           *     IF  START LRECL IS GREATER THAN END LRECL    *
003100           *     OR EITHER ARE NOT NUMERIC THE PROGRAM        *
003200           *     WILL END.                                    *
003300           *                                                  *
003400           *     ANY COMBINATION OF PARMS MAY BE USED AS      *
003500           *     LONG AS THEY DO NOT CONFLICT WITH THE        *
003600           *     DEFAULT VALUES.                              *
003700           *                                                  *
003800           *     REPORT SELECT CAN BE ANY CHARACTER.          *
003900           *                                                  *
004000           ****************************************************
004100     EJECT
004200 ENVIRONMENT DIVISION.
004300     SKIP1
004400 CONFIGURATION SECTION.
004500 SOURCE-COMPUTER.    IBM-370.
004600 OBJECT-COMPUTER.    IBM-370.
004700     SKIP1
004800 INPUT-OUTPUT SECTION.
004900 FILE-CONTROL.
005000     SELECT PRINTER            ASSIGN TO UT-S-BLKFCTOR.
005100     SKIP1
005200 DATA DIVISION.
005300 FILE SECTION.
005400 FD  PRINTER
005500     LABEL         RECORDS     ARE   STANDARD
005600     RECORDING     MODE        IS    F
005700     BLOCK         CONTAINS    0     RECORDS
005800     RECORD        CONTAINS    133   CHARACTERS
005900     DATA          RECORD      IS    PRINTER-REC.
006000 01  PRINTER-REC.
006100     05  PR-CC           PIC X(01).
006200     05  PR-BODY         PIC X(132).
006300     EJECT
006400 WORKING-STORAGE SECTION.
006500 01  MISC-STUFF.
006600     05  LINE-COUNT      PIC S9(04)  VALUE +70     COMP  SYNC.
006700     05  PLUS-SEVENTY    PIC S9(04)  VALUE +70     COMP  SYNC.
006800     05  PAGE-COUNT      PIC S9(04)  VALUE +0      COMP  SYNC.
006900     05  LINE-MAX        PIC S9(04)  VALUE +59     COMP  SYNC.
007000     05  RPT-SWITCH      PIC S9(04)  VALUE +0      COMP  SYNC.
007100     05  PARM-ERROR      PIC S9(04)  VALUE +0      COMP  SYNC.
007200     05  B-ZERO          PIC S9(04)  VALUE +0      COMP  SYNC.
007300     05  B-ONE           PIC S9(04)  VALUE +1      COMP  SYNC.
007400     05  B-TWO           PIC S9(04)  VALUE +2      COMP  SYNC.
007500     05  B-SIX           PIC S9(04)  VALUE +6      COMP  SYNC.
007600     05  B-EIGHT         PIC S9(04)  VALUE +8      COMP  SYNC.
007700     05  SUB             PIC S9(04)  VALUE +0      COMP  SYNC.
007800     05  A-SPACE         PIC X(01)   VALUE ' '.
007900     05  FIVE-SPACES     PIC X(05)   VALUE '     '.
008000     05  SIX-ZEROS       PIC 9(06)   VALUE 000000.
008100     05  PACKED-ZEROS    PIC S9(07)  VALUE +0      COMP-3.
008200     05  A-ONE           PIC X(01)   VALUE '1'.
008300     05  A-BAR           PIC X(01)   VALUE '×'.
008400     SKIP3
008500 01  WORK-CONSTANTS.
008600     05  TRKSIZE30       PIC S9(07)  VALUE +13165  COMP-3.
008700     05  TRKSIZE50       PIC S9(07)  VALUE +19254  COMP-3.
008800     05  OVERHEAD30      PIC S9(07)  VALUE +135    COMP-3.
008900     05  OVERHEAD50      PIC S9(07)  VALUE +185    COMP-3.
009000     05  OVHDKEY30       PIC S9(07)  VALUE +191    COMP-3.
009100     05  OVHDKEY50       PIC S9(07)  VALUE +267    COMP-3.
009200     05  BEGIN-LRECL     PIC S9(07)  VALUE +10     COMP-3.
009300     05  LRECL-MAX       PIC S9(07)  VALUE +1000   COMP-3.
009400     SKIP1
009500     05  BLK30-LESS-OVHD.
009600         10  FILLER      PIC S9(07)  VALUE +13030  COMP-3.
009700         10  FILLER      PIC S9(07)  VALUE +6447   COMP-3.
009800         10  FILLER      PIC S9(07)  VALUE +4253   COMP-3.
009900         10  FILLER      PIC S9(07)  VALUE +3156   COMP-3.
010000         10  FILLER      PIC S9(07)  VALUE +2498   COMP-3.
010100         10  FILLER      PIC S9(07)  VALUE +2059   COMP-3.
010200     05  BLK30 REDEFINES BLK30-LESS-OVHD     OCCURS 6 TIMES
010300                         PIC S9(07)                COMP-3.
010400     SKIP1
010500     05  BLK50-LESS-OVHD.
010600         10  FILLER      PIC S9(07)  VALUE +19069  COMP-3.
010700         10  FILLER      PIC S9(07)  VALUE +9442   COMP-3.
010800         10  FILLER      PIC S9(07)  VALUE +6233   COMP-3.
010900         10  FILLER      PIC S9(07)  VALUE +4628   COMP-3.
011000         10  FILLER      PIC S9(07)  VALUE +3665   COMP-3.
011100         10  FILLER      PIC S9(07)  VALUE +3024   COMP-3.
011200     05  BLK50 REDEFINES BLK50-LESS-OVHD     OCCURS 6 TIMES
011300                         PIC S9(07)                COMP-3.
011400     SKIP1
011500     05  BLK30-KEY-LESS-OVHD.
011600         10  FILLER      PIC S9(07)  VALUE +12974  COMP-3.
011700         10  FILLER      PIC S9(07)  VALUE +6391   COMP-3.
011800         10  FILLER      PIC S9(07)  VALUE +4197   COMP-3.
011900         10  FILLER      PIC S9(07)  VALUE +3100   COMP-3.
012000         10  FILLER      PIC S9(07)  VALUE +2442   COMP-3.
012100         10  FILLER      PIC S9(07)  VALUE +2003   COMP-3.
012200     05  BLK30-KEY REDEFINES BLK30-KEY-LESS-OVHD
012300                                             OCCURS 6 TIMES
012400                         PIC S9(07)                COMP-3.
012500     SKIP1
012600     05  BLK50-KEY-LESS-OVHD.
012700         10  FILLER      PIC S9(07)  VALUE +18987  COMP-3.
012800         10  FILLER      PIC S9(07)  VALUE +9360   COMP-3.
012900         10  FILLER      PIC S9(07)  VALUE +6151   COMP-3.
013000         10  FILLER      PIC S9(07)  VALUE +4546   COMP-3.
013100         10  FILLER      PIC S9(07)  VALUE +3583   COMP-3.
013200         10  FILLER      PIC S9(07)  VALUE +2942   COMP-3.
013300     05  BLK50-KEY REDEFINES BLK50-KEY-LESS-OVHD
013400                                             OCCURS 6 TIMES
013500                         PIC S9(07)             COMP-3.
013600     SKIP3
013700 01  WORK-VALUES.
013800     05  W-LRECL         PIC S9(07)  VALUE +0      COMP-3.
013900     05  W-UNBLK-RECS    PIC S9(07)  VALUE +0      COMP-3.
014000     05  W-BLKSIZE       PIC S9(07)  VALUE +0      COMP-3.
014100     05  W-RECS-PER-BLK  PIC S9(07)  VALUE +0      COMP-3.
014200     05  W-RECS-PER-TRK  PIC S9(07)  VALUE +0      COMP-3.
014300     05  W-OVHDLRECL     PIC S9(07)  VALUE +0      COMP-3.
014400     05  W-TRK-HOLD      PIC S9(07)  VALUE +0      COMP-3.
014500     SKIP3
014600 01  HDR-1.
014700     05  FILLER          PIC X(01)   VALUE '1'.
014800     05  FILLER          PIC X(08)   VALUE 'BLKFCTOR'.
014900     05  FILLER          PIC X(44)   VALUE SPACES.
015000     05  RPT-TITLE       PIC X(27)   VALUE SPACES.
015100     05  FILLER          PIC X(44)   VALUE SPACES.
015200     05  FILLER          PIC X(05)   VALUE 'PAGE '.
015300     05  H-PAGE          PIC ZZZZ.
015400     SKIP3
015500 01  HDR-2.
015600     05  FILLER          PIC X(01)   VALUE '0'.
015700     05  FILLER          PIC X(10)   VALUE '          '.
015800     05  FILLER          PIC X(10)   VALUE '×  1 BLOCK'.
015900     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
016000     05  FILLER          PIC X(10)   VALUE '× 2 BLOCKS'.
016100     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
016200     05  FILLER          PIC X(10)   VALUE '× 3 BLOCKS'.
016300     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
016400     05  FILLER          PIC X(10)   VALUE '× 4 BLOCKS'.
016500     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
016600     05  FILLER          PIC X(10)   VALUE '× 5 BLOCKS'.
016700     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
016800     05  FILLER          PIC X(10)   VALUE '× 6 BLOCKS'.
016900     05  FILLER          PIC X(10)   VALUE ' PER TRACK'.
017000     05  FILLER          PIC X(02)   VALUE '× '.
017100     SKIP3
017200 01  HDR-3.
017300     05  FILLER  PIC X(01)   VALUE SPACES.
017400     05  FILLER  PIC X(25)   VALUE ' LRECL UNB×  BLOCK       '.
017500     05  FILLER  PIC X(25)   VALUE 'RECS ×  BLOCK RECS  RECS '.
017600     05  FILLER  PIC X(25)   VALUE '×  BLOCK RECS  RECS ×  BL'.
017700     05  FILLER  PIC X(25)   VALUE 'OCK RECS  RECS ×  BLOCK R'.
017800     05  FILLER  PIC X(25)   VALUE 'ECS  RECS ×  BLOCK RECS  '.
017900     05  FILLER  PIC X(07)   VALUE 'RECS × '.
018000     SKIP3
018100 01  HDR-4.
018200     05  FILLER  PIC X(01)   VALUE SPACES.
018300     05  FILLER  PIC X(25)   VALUE '       REC×  SIZE        '.
018400     05  FILLER  PIC X(25)   VALUE 'PER  ×  SIZE  PER   PER  '.
018500     05  FILLER  PIC X(25)   VALUE '×  SIZE  PER   PER  ×  SI'.
018600     05  FILLER  PIC X(25)   VALUE 'ZE  PER   PER  ×  SIZE  P'.
018700     05  FILLER  PIC X(25)   VALUE 'ER   PER  ×  SIZE  PER   '.
018800     05  FILLER  PIC X(07)   VALUE 'PER  × '.
018900     SKIP3
019000 01  HDR-5.
019100     05  FILLER  PIC X(01)   VALUE SPACES.
019200     05  FILLER  PIC X(25)   VALUE '          ×              '.
019300     05  FILLER  PIC X(25)   VALUE 'TRACK×        BLOCK TRACK'.
019400     05  FILLER  PIC X(25)   VALUE '×        BLOCK TRACK×    '.
019500     05  FILLER  PIC X(25)   VALUE '    BLOCK TRACK×        B'.
019600     05  FILLER  PIC X(25)   VALUE 'LOCK TRACK×        BLOCK '.
019700     05  FILLER  PIC X(07)   VALUE 'TRACK× '.
019800     SKIP3
019900 01  UNDERSCORE-LINE.
020000     05  FILLER  PIC X(01)   VALUE SPACES.
020100     05  FILLER  PIC X(25)   VALUE '_________________________'.
020200     05  FILLER  PIC X(25)   VALUE '_________________________'.
020300     05  FILLER  PIC X(25)   VALUE '_________________________'.
020400     05  FILLER  PIC X(25)   VALUE '_________________________'.
020500     05  FILLER  PIC X(25)   VALUE '_________________________'.
020600     05  FILLER  PIC X(07)   VALUE '_____  '.
020700     SKIP3
020800 01  BAR-LINE.
020900     05  FILLER  PIC X(11)   VALUE SPACES.
021000     05  FILLER  PIC X(01)   VALUE '×'.
021100     05  FILLER  PIC X(19)   VALUE SPACES.
021200     05  FILLER  PIC X(01)   VALUE '×'.
021300     05  FILLER  PIC X(19)   VALUE SPACES.
021400     05  FILLER  PIC X(01)   VALUE '×'.
021500     05  FILLER  PIC X(19)   VALUE SPACES.
021600     05  FILLER  PIC X(01)   VALUE '×'.
021700     05  FILLER  PIC X(19)   VALUE SPACES.
021800     05  FILLER  PIC X(01)   VALUE '×'.
021900     05  FILLER  PIC X(19)   VALUE SPACES.
022000     05  FILLER  PIC X(01)   VALUE '×'.
022100     05  FILLER  PIC X(19)   VALUE SPACES.
022200     05  FILLER  PIC X(01)   VALUE '×'.
022300     05  FILLER  PIC X(01)   VALUE SPACES.
022400     SKIP3
022500 01  RPT-LITERALS.
022600     05  RPT1    PIC X(27)   VALUE '     3330 WITHOUT KEYS     '.
022700     05  RPT2    PIC X(27)   VALUE '     3350 WITHOUT KEYS     '.
022800     05  RPT3    PIC X(27)   VALUE '       3330 WITH KEYS      '.
022900     05  RPT4    PIC X(27)   VALUE '       3350 WITH KEYS      '.
023000     SKIP3
023100 01  P-LINE                      PIC X(133)  VALUE SPACES.
023200     SKIP3
023300 01  PRINT-LINE REDEFINES P-LINE.
023400     05  P-CC                    PIC X(01).
023500     05  P-LRECL                 PIC ZZZZZZ.
023600     05  FILLER                  PIC X(01).
023700     05  P-UNBLK-RECS            PIC ZZZ.
023800     05  P-BLOCKING-VALUES OCCURS 6 TIMES.
023900         10  P-BAR               PIC X(01).
024000         10  P-BLKSIZE           PIC ZZZZZZ.
024100         10  FILLER              PIC X(01).
024200         10  P-RECS-PER-BLK      PIC ZZZZZZ.
024300         10  P-RECS-PER-TRK      PIC ZZZZZZ.
024400     05  P-BAR-END               PIC X(01).
024500     05  FILLER                  PIC X(01).
024600     SKIP3
024700 LINKAGE SECTION.
024800 01  PARMS.
024900     05  PARM-LENGTH     PIC S9(04)  COMP.
025000     05  START-LRECL     PIC X(05).
025100     05  END-LRECL       PIC X(05).
025200     05  PARM-REPORTS.
025300         10  P-RPT1      PIC X(01).
025400         10  P-RPT2      PIC X(01).
025500         10  P-RPT3      PIC X(01).
025600         10  P-RPT4      PIC X(01).
025700     EJECT
025800 PROCEDURE DIVISION USING PARMS.
025900     OPEN  OUTPUT  PRINTER.
026000     IF  PARM-LENGTH > B-ZERO
026100         PERFORM 010-CHECK-PARMS THRU 015-EXIT
026200       ELSE
026300         MOVE A-ONE TO   P-RPT1
026400                         P-RPT2
026500                         P-RPT3
026600                         P-RPT4.
026700     IF  PARM-ERROR < B-ONE
026800         PERFORM 020-MAINLINE THRU 025-EXIT.
026900     CLOSE PRINTER
027000     GOBACK.
027100     EJECT
027200 010-CHECK-PARMS.
027300     IF  START-LRECL NOT = FIVE-SPACES
027400       IF  START-LRECL NOT NUMERIC
027500           DISPLAY '***** INVALID START LRECL PARM *****'
027600           MOVE B-ONE TO PARM-ERROR.
027700     IF  END-LRECL NOT = FIVE-SPACES
027800       IF  END-LRECL NOT NUMERIC
027900           DISPLAY '***** INVALID END LRECL PARM *****'
028000           MOVE B-ONE TO PARM-ERROR.
028100     IF  START-LRECL NUMERIC
028200       IF  START-LRECL > '00000'
028300           MOVE START-LRECL TO BEGIN-LRECL.
028400     IF  END-LRECL NUMERIC
028500       IF  END-LRECL IS > '00000'
028600           MOVE END-LRECL TO LRECL-MAX.
028700     IF  BEGIN-LRECL > LRECL-MAX
028800         DISPLAY '***** BEGIN LRECL > END LRECL *****'
028900         MOVE B-ONE TO PARM-ERROR.
029000     IF  P-RPT1 > A-SPACE
029100         MOVE B-ONE TO RPT-SWITCH.
029200     IF  P-RPT2 > A-SPACE
029300         MOVE B-ONE TO RPT-SWITCH.
029400     IF  P-RPT3 > A-SPACE
029500         MOVE B-ONE TO RPT-SWITCH.
029600     IF  P-RPT4 > A-SPACE
029700         MOVE B-ONE TO RPT-SWITCH.
029800     IF  RPT-SWITCH < B-ONE
029900         MOVE A-ONE TO   P-RPT1
030000                         P-RPT2
030100                         P-RPT3
030200                         P-RPT4.
030300 015-EXIT.
030400     EXIT.
030500     EJECT
030600 020-MAINLINE.
030700     IF  P-RPT1 > A-SPACE
030800         MOVE BEGIN-LRECL TO W-LRECL
030900         MOVE RPT1 TO RPT-TITLE
031000         PERFORM 100-BUILD-A-3330-LINE THRU 105-EXIT.
031100         SKIP1
031200     IF  P-RPT2 > A-SPACE
031300         MOVE BEGIN-LRECL TO W-LRECL
031400         MOVE +0 TO PAGE-COUNT
031500         MOVE RPT2 TO RPT-TITLE
031600         PERFORM 600-HDR THRU 605-EXIT
031700         PERFORM 110-BUILD-A-3350-LINE THRU 115-EXIT.
031800         SKIP1
031900     IF  P-RPT3 > A-SPACE
032000         MOVE BEGIN-LRECL TO W-LRECL
032100         MOVE +0 TO PAGE-COUNT
032200         MOVE RPT3 TO RPT-TITLE
032300         PERFORM 600-HDR THRU 605-EXIT
032400         PERFORM 120-BUILD-A-KEY-3330-LINE THRU 125-EXIT.
032500         SKIP1
032600     IF P-RPT4 > A-SPACE
032700         MOVE BEGIN-LRECL TO W-LRECL
032800         MOVE +0 TO PAGE-COUNT
032900         MOVE RPT4 TO RPT-TITLE
033000         PERFORM 600-HDR THRU 605-EXIT
033100         PERFORM 130-BUILD-A-KEY-3350-LINE THRU 135-EXIT.
033200 025-EXIT.
033300     EXIT.
033400     EJECT
033500 100-BUILD-A-3330-LINE.
033600     IF  W-LRECL > LRECL-MAX
033700         GO TO 105-EXIT.
033800     ADD B-TWO TO LINE-COUNT.
033900     IF LINE-COUNT > LINE-MAX
034000         PERFORM 600-HDR THRU 605-EXIT.
034100     ADD OVERHEAD30 W-LRECL GIVING W-OVHDLRECL.
034200     DIVIDE TRKSIZE30 BY W-OVHDLRECL GIVING W-UNBLK-RECS.
034300     MOVE W-LRECL TO P-LRECL.
034400     MOVE W-UNBLK-RECS TO P-UNBLK-RECS.
034500     MOVE B-SIX TO SUB.
034600     MOVE PACKED-ZEROS TO W-TRK-HOLD.
034700     PERFORM 200-FINISH-LINE THRU 205-EXIT
034800     MOVE A-BAR TO P-BAR-END.
034900     MOVE A-SPACE TO P-CC.
035000     MOVE PRINT-LINE TO PRINTER-REC
035100     PERFORM 500-PRINT-LINE THRU 505-EXIT
035200     MOVE BAR-LINE TO PRINTER-REC
035300     PERFORM 500-PRINT-LINE THRU 505-EXIT
035400     ADD B-ONE TO W-LRECL
035500     GO TO 100-BUILD-A-3330-LINE.
035600 105-EXIT.
035700     EXIT.
035800     EJECT
035900 110-BUILD-A-3350-LINE.
036000     IF  W-LRECL > LRECL-MAX
036100         GO TO 115-EXIT.
036200     ADD B-TWO TO LINE-COUNT.
036300     IF LINE-COUNT > LINE-MAX
036400         PERFORM 600-HDR THRU 605-EXIT.
036500     ADD OVERHEAD50  W-LRECL GIVING W-OVHDLRECL.
036600     DIVIDE TRKSIZE50 BY W-OVHDLRECL GIVING W-UNBLK-RECS.
036700     MOVE W-LRECL TO P-LRECL.
036800     MOVE W-UNBLK-RECS TO P-UNBLK-RECS.
036900     MOVE B-SIX TO SUB.
037000     MOVE PACKED-ZEROS TO W-TRK-HOLD.
037100     PERFORM 210-FINISH-LINE THRU 215-EXIT
037200     MOVE A-BAR TO P-BAR-END.
037300     MOVE A-SPACE TO P-CC.
037400     MOVE PRINT-LINE TO PRINTER-REC
037500     PERFORM 500-PRINT-LINE THRU 505-EXIT
037600     MOVE BAR-LINE TO PRINTER-REC
037700     PERFORM 500-PRINT-LINE THRU 505-EXIT
037800     ADD B-ONE TO W-LRECL
037900     GO TO 110-BUILD-A-3350-LINE.
038000 115-EXIT.
038100     EXIT.
038200     EJECT
038300 120-BUILD-A-KEY-3330-LINE.
038400     IF  W-LRECL > LRECL-MAX
038500         GO TO 125-EXIT.
038600     ADD B-TWO TO LINE-COUNT.
038700     IF LINE-COUNT > LINE-MAX
038800         PERFORM 600-HDR THRU 605-EXIT.
038900     ADD OVHDKEY30  W-LRECL GIVING W-OVHDLRECL.
039000     DIVIDE TRKSIZE30 BY W-OVHDLRECL GIVING W-UNBLK-RECS.
039100     MOVE W-LRECL TO P-LRECL.
039200     MOVE W-UNBLK-RECS TO P-UNBLK-RECS.
039300     MOVE B-SIX TO SUB.
039400     MOVE PACKED-ZEROS TO W-TRK-HOLD.
039500     PERFORM 220-FINISH-LINE THRU 225-EXIT
039600     MOVE A-BAR TO P-BAR-END.
039700     MOVE A-SPACE TO P-CC.
039800     MOVE PRINT-LINE TO PRINTER-REC
039900     PERFORM 500-PRINT-LINE THRU 505-EXIT
040000     MOVE BAR-LINE TO PRINTER-REC
040100     PERFORM 500-PRINT-LINE THRU 505-EXIT
040200     ADD B-ONE TO W-LRECL
040300     GO TO 120-BUILD-A-KEY-3330-LINE.
040400 125-EXIT.
040500     EXIT.
040600     EJECT
040700 130-BUILD-A-KEY-3350-LINE.
040800     IF  W-LRECL > LRECL-MAX
040900         GO TO 135-EXIT.
041000     ADD B-TWO TO LINE-COUNT.
041100     IF LINE-COUNT > LINE-MAX
041200         PERFORM 600-HDR THRU 605-EXIT.
041300     ADD OVHDKEY50  W-LRECL GIVING W-OVHDLRECL.
041400     DIVIDE TRKSIZE50 BY W-OVHDLRECL GIVING W-UNBLK-RECS.
041500     MOVE W-LRECL TO P-LRECL.
041600     MOVE W-UNBLK-RECS TO P-UNBLK-RECS.
041700     MOVE B-SIX TO SUB.
041800     MOVE PACKED-ZEROS TO W-TRK-HOLD.
041900     PERFORM 230-FINISH-LINE THRU 235-EXIT
042000     MOVE A-BAR TO P-BAR-END.
042100     MOVE A-SPACE TO P-CC.
042200     MOVE PRINT-LINE TO PRINTER-REC
042300     PERFORM 500-PRINT-LINE THRU 505-EXIT
042400     MOVE BAR-LINE TO PRINTER-REC
042500     PERFORM 500-PRINT-LINE THRU 505-EXIT
042600     ADD B-ONE TO W-LRECL
042700     GO TO 130-BUILD-A-KEY-3350-LINE.
042800 135-EXIT.
042900     EXIT.
043000     EJECT
043100 200-FINISH-LINE.
043200     IF  SUB < B-ONE
043300         GO TO 205-EXIT.
043400     DIVIDE BLK30 (SUB) BY W-LRECL GIVING W-RECS-PER-BLK.
043500     MULTIPLY W-RECS-PER-BLK BY W-LRECL GIVING W-BLKSIZE.
043600     MULTIPLY W-RECS-PER-BLK BY SUB GIVING W-RECS-PER-TRK.
043700     MOVE A-BAR TO P-BAR (SUB).
043800     MOVE W-BLKSIZE TO P-BLKSIZE (SUB).
043900     IF SUB = B-ONE
044000         MOVE SIX-ZEROS TO P-RECS-PER-BLK (SUB)
044100       ELSE
044200         MOVE W-RECS-PER-BLK TO P-RECS-PER-BLK (SUB).
044300     MOVE W-RECS-PER-TRK TO P-RECS-PER-TRK (SUB).
044400     PERFORM 400-INEFFICIENT-BLOCK THRU 405-EXIT.
044500     SUBTRACT B-ONE FROM SUB
044600     GO TO 200-FINISH-LINE.
044700 205-EXIT.
044800     EXIT.
044900     EJECT
045000 210-FINISH-LINE.
045100     IF  SUB < B-ONE
045200         GO TO 215-EXIT.
045300     DIVIDE BLK50 (SUB) BY W-LRECL GIVING W-RECS-PER-BLK.
045400     MULTIPLY W-RECS-PER-BLK BY W-LRECL GIVING W-BLKSIZE.
045500     MULTIPLY W-RECS-PER-BLK BY SUB GIVING W-RECS-PER-TRK.
045600     MOVE A-BAR TO P-BAR (SUB).
045700     MOVE W-BLKSIZE TO P-BLKSIZE (SUB).
045800     IF SUB = B-ONE
045900         MOVE SIX-ZEROS TO P-RECS-PER-BLK (SUB)
046000       ELSE
046100         MOVE W-RECS-PER-BLK TO P-RECS-PER-BLK (SUB).
046200     MOVE W-RECS-PER-TRK TO P-RECS-PER-TRK (SUB).
046300     PERFORM 400-INEFFICIENT-BLOCK THRU 405-EXIT.
046400     SUBTRACT B-ONE FROM SUB
046500     GO TO 210-FINISH-LINE.
046600 215-EXIT.
046700     EXIT.
046800     EJECT
046900 220-FINISH-LINE.
047000     IF  SUB < B-ONE
047100         GO TO 225-EXIT.
047200     DIVIDE BLK30-KEY (SUB) BY W-LRECL GIVING W-RECS-PER-BLK.
047300     MULTIPLY W-RECS-PER-BLK BY W-LRECL GIVING W-BLKSIZE.
047400     MULTIPLY W-RECS-PER-BLK BY SUB GIVING W-RECS-PER-TRK.
047500     MOVE A-BAR TO P-BAR (SUB).
047600     MOVE W-BLKSIZE TO P-BLKSIZE (SUB).
047700     IF SUB = B-ONE
047800         MOVE SIX-ZEROS TO P-RECS-PER-BLK (SUB)
047900       ELSE
048000         MOVE W-RECS-PER-BLK TO P-RECS-PER-BLK (SUB).
048100     MOVE W-RECS-PER-TRK TO P-RECS-PER-TRK (SUB).
048200     PERFORM 400-INEFFICIENT-BLOCK THRU 405-EXIT.
048300     SUBTRACT B-ONE FROM SUB
048400     GO TO 220-FINISH-LINE.
048500 225-EXIT.
048600     EXIT.
048700     EJECT
048800 230-FINISH-LINE.
048900     IF  SUB < B-ONE
049000         GO TO 235-EXIT.
049100     DIVIDE BLK50-KEY (SUB) BY W-LRECL GIVING W-RECS-PER-BLK.
049200     MULTIPLY W-RECS-PER-BLK BY W-LRECL GIVING W-BLKSIZE.
049300     MULTIPLY W-RECS-PER-BLK BY SUB GIVING W-RECS-PER-TRK.
049400     MOVE A-BAR TO P-BAR (SUB).
049500     MOVE W-BLKSIZE TO P-BLKSIZE (SUB).
049600     IF SUB = B-ONE
049700         MOVE SIX-ZEROS TO P-RECS-PER-BLK (SUB)
049800       ELSE
049900         MOVE W-RECS-PER-BLK TO P-RECS-PER-BLK (SUB).
050000     MOVE W-RECS-PER-TRK TO P-RECS-PER-TRK (SUB).
050100     PERFORM 400-INEFFICIENT-BLOCK THRU 405-EXIT.
050200     SUBTRACT B-ONE FROM SUB
050300     GO TO 230-FINISH-LINE.
050400 235-EXIT.
050500     EXIT.
050600     EJECT
050700 400-INEFFICIENT-BLOCK.
050800     IF  W-RECS-PER-TRK > W-TRK-HOLD     OR
050900         W-RECS-PER-TRK = W-TRK-HOLD
051000         MOVE W-RECS-PER-TRK TO W-TRK-HOLD
051100       ELSE
051200         MOVE SIX-ZEROS TO P-BLKSIZE (SUB)
051300                           P-RECS-PER-BLK (SUB)
051400                           P-RECS-PER-TRK (SUB).
051500 405-EXIT.
051600     EXIT.
051700     SKIP3
051800 500-PRINT-LINE.
051900     WRITE PRINTER-REC AFTER POSITIONING PR-CC LINES.
052000 505-EXIT.
052100     EXIT.
052200     EJECT
052300 600-HDR.
052400     IF  LINE-COUNT < PLUS-SEVENTY
052500         MOVE UNDERSCORE-LINE TO PRINTER-REC
052600         PERFORM 500-PRINT-LINE THRU 505-EXIT.
052700     ADD B-ONE TO PAGE-COUNT
052800     MOVE PAGE-COUNT TO H-PAGE
052900     MOVE HDR-1 TO PRINTER-REC
053000     PERFORM 500-PRINT-LINE THRU 505-EXIT
053100     MOVE HDR-2 TO PRINTER-REC
053200     PERFORM 500-PRINT-LINE THRU 505-EXIT
053300     MOVE BAR-LINE TO PRINTER-REC
053400     PERFORM 500-PRINT-LINE THRU 505-EXIT
053500     MOVE HDR-3 TO PRINTER-REC
053600     PERFORM 500-PRINT-LINE THRU 505-EXIT
053700     MOVE HDR-4 TO PRINTER-REC
053800     PERFORM 500-PRINT-LINE THRU 505-EXIT
053900     MOVE HDR-5 TO PRINTER-REC
054000     PERFORM 500-PRINT-LINE THRU 505-EXIT
054100     MOVE UNDERSCORE-LINE TO PRINTER-REC
054200     PERFORM 500-PRINT-LINE THRU 505-EXIT
054300     MOVE B-EIGHT TO LINE-COUNT.
054400 605-EXIT.
054500     EXIT.
