000010 IDENTIFICATION DIVISION.
000020 PROGRAM-ID.
000030     PRG-4-FLOBOL.
000040 AUTHOR.
000050     NAVY PROGRAMMING LANGUAGES DIVISION.
000060 INSTALLATION.
000070     NPLD OFFICE OF INFORMATION SYSTEMS PLANNING AND DEVELOPMENT.
000080 DATE-WRITTEN.
000090     VERSION 1.0 01-01-69 VERSION 2.0 09-01-69.
000100     NO CHANGES AS OF 1 APRIL 1970.
000101     CHANGED 3/16/78 - WJS/PRC - THE PARAGRAPH NAME 'MESSAGE'
000102     WAS CHANGED TO 'MESAGE' DUE TO IBM EXTENSION IN COBOLV4
000103     RELEASE 1.4 (PROGRAM PRODUCT) WHICH MAKES IT A RESERVED
000104     WORD. IN ADDITON, ALL FLOBOL MODULES WERE OPTIMIZED WITH
000105     WITH OPTIMIZER II RELEASE 6.3 (CAPEX CORPORATION).
000110 DATE-COMPILED.
000120     COMMENT-ENTRY.
000130 SECURITY.
000140     FREEDOM OF INFORMATION ACT.
000150 REMARKS.
000160     *        ANY COMMENTS ABOUT THIS PROGRAM SHOULD BE SENT TO  *
000170     *                                                           *
000180     *        NAVY PROGRAMMING LANGUAGES DIVISION                *
000190     *        OFFICE OF INFORMATION SYSTEMS PLANNING AND         *
000200     *             DEVELOPMENT                                   *
000210     *        ROOM 5D840 PENTAGON                                *
000220     *        WASHINGTON D. C.  20350 USA                        *
000230     *                                                           *
000240     *     ATTN  FLOBOL.                                         *
000250     *                                                           *
000260     *                                   (202) OX5-4750          *
000270     *                                                           *
000280     *        *         *         *         *         *          *
000290          EACH RECORD OF SUPPLEMENTAL-PART4-IN IS REFORMATTED AND
000300     PASSED TO A SORT WHICH PLACES THE FILE IN THE ORIGINAL
000310     SEQUENCE IN WHICH THEY WERE CREATED.
000320     INPUT FILES.  PRG-4.
000330        PARFILEIN.
000340           1. CREATED IN PRG-2 USED IN PRG-3 AND PASSED TO PRG-4.
000350           2. RECORD LENGTH IS 60 CHARACTERS.
000360           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
000370           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
000380              IF NECESSARY.
000390           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
000400              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
000410              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
000420              FILE IN THE FLOBOL SYSTEM.
000430           6. A DESCRIPTION OF THE RECORDS IN THIS FILE
000440              FOLLOWS.
000450          PARAGRAPH / SECTION RECORD
000460              A PROGRAM NUMBER
000470              B PROCEDURE NAME
000480              C COBOL REFERENCE NUMBER OF PROCEDURE NAME
000490              D COBOL STATEMENT NUMBER OF PROCEDURE NAME
000500              E RECORD OCCURENCE NUMBER
000510        SUPPLEMENTAL-PART4-IN.
000520           1. CREATED FROM SUPPLEMENTAL-PART3-OUT IN PRG-3.
000530           2. RECORD LENGTH IS 120 CHARACTERS.
000540           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
000550           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
000560              IF NECESSARY.
000570           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
000580              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
000590              CONSISTENT WITH THE OTHER DESCRIPTIONS FOR THIS
000600              FILE IN THE FLOBOL SYSTEM.
000610           6 A DESCRIPTION OF THE RECORDS FOUND IN THIS FILE
000620             FOLLOW.
000630          PERFORM ENTRANCE RECORDS
000640              A C (TYPE C RECORD)
000650              B PROGRAM NUMBER
000660              C PROGRAM ID
000670              D PERFORM NAME FIRST ONE
000680              E 0000
000690              F COBOL REFERENCE NUMBER OF PERFORM
000700              G COBOL STATEMENT NUMBER OF PERFORM
000710              H PER ENT
000720              I PARAGRAPH THAT PERFORM WAS WITHIN
000730              J            NOT USED
000740              K            NOT USED
000750              L RECORD OCCURRENCE NUMBER
000760          PERFORM EXIT RECORD
000770              A C (TYPE C RECORDS)
000780              B PROGRAM NUMBER
000790              C PROGRAM ID
000800              D PERFORM NAME SECOND IF PRESENT
000810              E ****
000820          F COBOL REFERENCE NUMBER OF PERFORM
000830          G COBOL STATEMENT NUMBER OF PERFORM
000840          H PER EXT
000850              I            NOT USED
000860              J            NOT USED
000870              K            NOT USED
000880              L RECORD OCCURRENCE NUMBER
000890          PARAGRAPH AND SECTION RECORDS
000900              A C (TYPE C RECORD)
000910              B PROGRAM NUMBER
000920              C PROGRAM ID
000930          D PARAGRAPH OR SECTION NAME
000940              E COBOL REFERENCE NUMBER
000950              F 0000
000960              G            NOT USED
000970              H PARAGRAPH OR SECTION
000980              I            NOT USED
000990              J            NOT USED
001000              K            NOT USED
001010              L RECORD OCCURRENCE NUMBER
001020          CONNECTOR RECORDS
001030              A C (TYPE C RECORDS)
001040              B PROGRAM NUMBER
001050              C PROGRAM ID
001060              D PARAGRAPH THAT GO TO ADDRESSED
001070              E 0000
001080              F COBOL REFERENCE OF GO TO
001090              G COBOL STATEMENT OF GO TO
001100              H CONNECTOR
001110              I PARAGRAPH THAT GO TO WAS WITHIN
001120              J            NOT USED
001130              K            NOT USED
001140              L RECORD OCCURRENCE NUMBER
001150          SWITCH / ALTER RECORD
001160              A Z (TYPE Z RECORD)
001170              B PROGRAM NUMBER
001180              C PROGRAM ID
001190              D ALTER NAME FIRST ONE
001200              E ----
001210              F COBOL REFERENCE NUMBER OF ALTER
001220              G COBOL STATEMENT NUMBER OF ALTER
001230              H SWITCH
001240              I ALTER NAME SECOND ONE
001250              J            NOT USED
001260              K            NOT USED
001270              L RECORD OCCURRENCE NUMBER
001280          CONNECTOR FROM SWITCH / ALTER SETTING RECORD
001290              A Z (TYPE Z RECORD)
001300              B PROGRAM NUMBER
001310              C PROGRAM ID
001320              D ALTER NAME SECOND ONE
001330              E ----
001340              F 0000
001350              G 0000
001360              H SWITCHENT
001370              I ALTER NAME FIRST ONE
001380              J            NOT USED
001390              L RECORD OCCURRENCE NUMBER
001400        OUTFILE
001410           1 PASSED TO SORT 3 THEN TO FLOBOL 05
001420                  MAJOR  1  -  8 ASCENDING
001430                  MINOR  13  -  24 ASCENDING
001440           2. RECORD LENGTH IS 80 CHARACTERS.
001450           3. BLOCKING FACTOR IS 1 RECORD PER BLOCK.
001460           4. LABEL RECORDS HAVE BEEN OMITTED BUT SHOULD BE USED
001470              IF NECESSARY.
001480           5. RECORD LENGTH AND/OR BLOCKING FACTOR MAY BE
001490              INCREASED FOR EFFICIENCY REASONS BUT SHOULD BE
001500              CONSISTENT WITH THE OTHER DESCRIPTION FOR THIS
001510              FILE IN THE FLOBOL SYSTEM.
001520          SWITCH OR SWITCH END RECORD.
001530              A RECORD OCCURANCE NUMBER
001540              B PROGRAM NUMBER
001550              C A AT  (TYPE A RECORD)
001560              D COBOL REFERENCE NUMBER OF PROCEDURE NAME
001570              E 0001
001580              F ----
001590              G SWITCH OR SWITCH CONNECTOR
001600              H COBOL REFERENCE NUMBER OF PROCEDURE NAME
001610              I 00001
001620          PERFORM ENTRANCE RECORD
001630              A PROGRAM OCCURANCE NUMBER
001640              B PROGRAM NUMBER
001650              C B AT  (TYPE B RECORD)
001660              D COBOL REFERENCE NUMBER OF GO TO
001670              E 0000
001680              F PERFORM NAME FIRST ONE
001690              G PERFORM ENTRANCE
001700              H COBOL REFERENCE NUMBER OF GO TO
001710              I 0000
001720          CONNECTOR RECORD
001730              A PROGRAM OCCURANCE NUMBER
001740              B PROGRAM NUMBER
001750              C C AT  (TYPE C RECORD)
001760              D COBOL REFERENCE NUMBER OF GO TO
001770              E COBOL STATEMENT NUMBER OF GO TO
001780              F PARAGRAPH THAT GO TO ADDRESSED
001790              G FROM CONNECTOR
001800              H COBOL REFERENCE NUMBER OF GO TO
001810              I COBOL STATEMENT NUMBER OF GO TO
001820          PARAGRAPH OR SECTION RECORD
001830              A RECORD OCCURANCE NUMBER
001840              B PROGRAM NUMBER
001850              C D AT  (TYPE D RECORD)
001860              D COBOL REFERENCE NUMBER
001870              E 0000
001880              F PARAGRAPH OR SECTION NAME
001890              G PARAGRAPH OR SECTION
001900              H COBOL REFERENCE NUMBER
001910              I 0000
001920          SECTION RECORD
001930              A PROGRAM OCCURANCE NUMBER
001940              B PROGRAM NUMBER
001950              C  SAT  (TYPE OF RECORD IS SPACE)
001960              D COBOL REFERENCE NUMBER
001970              E 0000
001980              F SECTION NAME
001990              G SECTION
002000              H COBOL REFERENCE NUMBER
002010              I 0000
002020          CREATED FROM PERFORM ENTRANCE VALUES FOR PERFORM
002030          STATEMENT
002040              A RECORD OCCURANCE NUMBER
002050              B PROGRAM NUMBER
002060              C E AT  (TYPE E RECORD)
002070              D COBOL REFERENCE NUMBER OF PERFORM
002080              E COBOL STATEMENT NUMBER OF PERFORM
002090               F PARAGRAPH THAT PERFORM WAS WITHIN
002100              G PERFORM LINE AT
002110              H COBOL REFERENCE NUMBER OF PARAGRAPH  +0001
002120              I 0001
002130          CREATED FROM CONNECTOR VALUES FOR GO TO STATEMENT
002140              A RECORD OCCURANCE NUMBER
002150              B PROGRAM NUMBER
002160              C E AT  (TYPE E RECORD)
002170              D COBOL REFERENCE NUMBER OF GO TO
002180              E COBOL STATEMENT NUMBER OF GO TO
002190              F PARAGRAPH THAT GO TO WAS WITHIN
002200              G GO TO LOCATION--
002210              H COBOL REFERENCE NUMBER OF PARAGRAPH  +0001
002220              I 0001
002230          PERFORM EXIT RECORD
002240              A RECORD OCCURANCE NUMBER
002250              B PROGRAM NUMBER
002260              C G AT  (TYPE G RECORD)
002270              D COBOL REFERENCE NUMBER OF PERFORM
002280              E COBOL STATEMENT NUMBER OF PERFORM
002290              F PERFORM NAME SECOND IF  PRESENT ELSE FIRST
002300              G PERFORM EXIT
002310              H COBOL REFERENCE NUMBER OF PERFORM
002320              I COBOL STATEMENT NUMBER OF PERFORM
002330 ENVIRONMENT DIVISION.
002340 CONFIGURATION SECTION.
002350 INPUT-OUTPUT SECTION.
002360 FILE-CONTROL.
002370     SELECT OUTFILE ASSIGN TO
002380     UT-S-XXXXX04.
002390     SELECT SUPPLEMENTAL-PART4-IN ASSIGN TO
002400     UT-S-XXXXX02.
002410     SELECT PARFILEIN ASSIGN TO
002420     UT-S-XXXXX03.
002430 DATA DIVISION.
002440 FILE SECTION.
002450 FD  PARFILEIN
002460     LABEL RECORDS ARE STANDARD
002470     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002480     DATA RECORDS ARE PARRECIN PARRECOUT.
002490 01  PARRECIN.
002500     02 PARPROGNO                       PICTURE IS X(4).
002510     02 PARPARNAME                      PICTURE IS X(32).
002520     02 PARLINE                         PICTURE IS 9999.
002530     02 PARSTATE                        PICTURE IS 9999.
002540     02 PARLOCATION                     PICTURE IS 9(8).
002550     02 FILLER                   PICTURE X(8).
002560 01  PARRECOUT.
002570     02 PROGNOPAR                       PICTURE IS 9999.
002580     02 PARNAMEPAR                      PICTURE IS X(32).
002590     02 LINEPAR                         PICTURE IS 9999.
002600     02 STATEPAR                        PICTURE IS 9999.
002610     02 LOCATIONPAR                     PICTURE IS 9(8).
002620     02 FILLER                   PICTURE X(8).
002630 FD  SUPPLEMENTAL-PART4-IN
002640     LABEL RECORDS ARE STANDARD
002650     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002660     DATA RECORDS ARE SORTWHOLE SORTRECORD.
002670 01  SORTWHOLE                          PICTURE IS X(120).
002680 01  SORTRECORD.
002690     02 SORTKEY                         PICTURE IS X(64).
002700     02 SSORTKEY REDEFINES SORTKEY.
002710     03 SSKEY                           PICTURE IS X.
002720     03 SSKEYFILL                       PICTURE IS XXX.
002730     03 SSPROGNO                        PICTURE IS X(4).
002740     03 SSPROGRAMID                     PICTURE IS X(12).
002750     03 SSPARNAME                       PICTURE IS X(32).
002760     03 SSHOLD1                         PICTURE IS 9999.
002770     03 SSHOLD2                         PICTURE IS 9999.
002780     03 SSHOLD3                         PICTURE IS 9999.
002790     02 SSWHAT                          PICTURE IS X(9).
002800     02 SSFROMPAR                       PICTURE IS X(32).
002810     02 SSDIFFERENT REDEFINES SSFROMPAR.
002820     03 SSHOLD4                         PICTURE IS 9999.
002830     03 SSHOLD5                         PICTURE IS 9999.
002840     03 SSFILLER1                       PICTURE IS X(24).
002850     02 SSFILLER2                       PICTURE IS X(7).
002860     02 SSLOCATION                      PICTURE IS 9(8).
002870 FD  OUTFILE
002880     LABEL RECORDS ARE STANDARD
002890     BLOCK CONTAINS 0 RECORDS RECORDING MODE IS F
002900     DATA RECORDS ARE NEWRECORD.
002910 01  NEWRECORD.
002920     02 NLOCATION                       PICTURE IS 9(8).
002930     02 NPROGNO                         PICTURE IS X(4).
002940     02 NKEY                            PICTURE IS XXXX.
002950     02 NLINE                           PICTURE IS 9999.
002960     02 NSTATE                          PICTURE IS 9999.
002970     02 NPAR                            PICTURE IS X(32).
002980     02 NWHAT                           PICTURE IS X(16).
002990     02 NLLLL                           PICTURE IS XXXX.
003000     02 NSSSS                           PICTURE IS XXXX.
003010 PROCEDURE DIVISION.
003020 LPOC         SECTION.
003030 LPOCBEGIN.
003040     OPEN     INPUT SUPPLEMENTAL-PART4-IN.
003050     OPEN     INPUT PARFILEIN.
003060     OPEN     OUTPUT OUTFILE.
003070     READ     PARFILEIN AT END
003080     DISPLAY ' FLOBOL04, NO PARFILEIN RECORDS PRESENT, EOJ '
003090     GO TO WRAPUP.
003100 READSORTER.
003110     READ     SUPPLEMENTAL-PART4-IN AT END
003120              GO TO WRAPUP.
003130     IF       SSWHAT IS EQUAL TO 'SWITCH   ' OR
003140              SSWHAT IS EQUAL TO 'SWITCHENT'
003150              GO TO HAVESWITCH.
003160 CHECKPROGNO.
003170     IF       SSPROGNO IS EQUAL TO PARPROGNO
003180              GO TO CHECKPARNAME.
003190     READ     PARFILEIN AT END
003200              GO TO MESAGE.
003210     GO       TO CHECKPROGNO.
003220 CHECKPARNAME.
003230     IF       SSPARNAME IS EQUAL TO PARPARNAME
003240              GO TO FORMNEWRECORD.
003250     READ     PARFILEIN AT END
003260              GO TO MESAGE.
003270     GO       TO CHECKPARNAME.
003280 MESAGE.
003290     DISPLAY ' FLOBOL04, PARFILEIN OUT OF SEQUENCE, EOJ '
003300     GO TO WRAPUP.
003310 FORMNEWRECORD.
003320     MOVE     PARLOCATION TO NLOCATION.
003330     MOVE     PARPROGNO TO NPROGNO.
003340     IF       SSWHAT IS EQUAL TO 'PER ENT  '
003350              GO TO FORMB.
003360     IF       SSWHAT IS EQUAL TO 'CONNECTOR'
003370              GO TO FORMC.
003380     IF       SSWHAT IS EQUAL TO 'PARAGRAPH' OR
003390              SSWHAT IS EQUAL TO 'SECTION  '
003400              GO TO FORMD.
003410     IF       SSWHAT IS EQUAL TO 'PRFMLINE '
003420              GO TO FORME.
003430     IF       SSWHAT IS EQUAL TO 'GOTO LINE'
003440              GO TO FORMF.
003450     IF       SSWHAT IS EQUAL TO 'PER EXT  '
003460              GO TO FORMG.
003470     DISPLAY ' FLOBOL04, SSWHAT OF SUPPLEMENTAL-PART4-IN CONTAINS
003480-    ' INVALID DATA, NEXT RECORD '.
003490     GO TO READSORTER.
003500 FORMB.
003510     MOVE 'B AT' TO NKEY.
003520     MOVE     SSHOLD2 TO NLINE NLLLL.
003530     MOVE     SSHOLD3 TO NSTATE NSSSS.
003540     MOVE     SSPARNAME TO NPAR.
003550     MOVE 'PERFORM ENTRANCE' TO NWHAT.
003560     WRITE    NEWRECORD.
003570     GO       TO READSORTER.
003580 FORMC.
003590     IF       SSKEYFILL IS EQUAL TO 'XXX'
003600              GO TO READSORTER.
003610     MOVE 'C AT' TO NKEY.
003620     MOVE     SSHOLD2 TO NLINE NLLLL.
003630     MOVE     SSHOLD3 TO NSTATE NSSSS.
003640     MOVE     SSPARNAME TO NPAR.
003650     MOVE 'FROM CONNECTOR  ' TO NWHAT.
003660     WRITE    NEWRECORD.
003670     GO       TO READSORTER.
003680 FORMD.
003690     MOVE 'D AT' TO NKEY.
003700     MOVE     SSHOLD1 TO NLINE NLLLL.
003710     MOVE     SSHOLD2 TO NSTATE NSSSS.
003720     MOVE     SSPARNAME TO NPAR.
003730     MOVE     SSWHAT TO NWHAT.
003740     WRITE    NEWRECORD.
003750     IF       SSWHAT IS EQUAL TO 'PARAGRAPH'
003760              GO TO READSORTER.
003770     MOVE     PARLOCATION TO NLOCATION.
003780     MOVE     PARPROGNO TO NPROGNO.
003790     MOVE ' SAT' TO NKEY.
003800     MOVE     SSHOLD1 TO NLINE NLLLL.
003810     MOVE     SSHOLD2 TO NSTATE NSSSS.
003820     MOVE     SSPARNAME TO NPAR.
003830     MOVE     SSWHAT TO NWHAT.
003840     WRITE    NEWRECORD.
003850     GO       TO READSORTER.
003860 FORME.
003870     MOVE 'E AT' TO NKEY.
003880     MOVE     SSHOLD1 TO NLINE.
003890     MOVE     SSHOLD2 TO NSTATE.
003900     MOVE     SSPARNAME TO NPAR.
003910     MOVE 'PERFORM LINE AT ' TO NWHAT.
003920     MOVE     SSHOLD4 TO NLLLL.
003930     MOVE     SSHOLD5 TO NSSSS.
003940     WRITE    NEWRECORD.
003950     GO       TO READSORTER.
003960 FORMF.
003970     MOVE 'E AT' TO NKEY.
003980     MOVE     SSHOLD1 TO NLINE.
003990     MOVE     SSHOLD2 TO NSTATE.
004000     MOVE     SSPARNAME TO NPAR.
004010     MOVE 'GO TO LOCATION--' TO NWHAT.
004020     MOVE     SSHOLD4 TO NLLLL.
004030     MOVE     SSHOLD5 TO NSSSS.
004040     WRITE    NEWRECORD.
004050     GO       TO READSORTER.
004060 FORMG.
004070     MOVE 'G AT' TO NKEY.
004080     MOVE     SSHOLD2 TO NLINE NLLLL.
004090     MOVE     SSHOLD3 TO NSTATE NSSSS.
004100     MOVE     SSPARNAME TO NPAR.
004110     MOVE 'PERFORM EXIT    ' TO NWHAT.
004120     WRITE    NEWRECORD.
004130     GO       TO READSORTER.
004140 HAVESWITCH.
004150     GO       TO FIRSTONE.
004160 FIRSTONE.
004170     CLOSE    PARFILEIN.
004180     OPEN     INPUT PARFILEIN.
004190     READ     PARFILEIN AT END
004200     DISPLAY ' FLOBOL04, PARFILEIN PREMATURE EOF, EOJ '
004210     GO TO WRAPUP.
004220     ALTER    HAVESWITCH TO PROCEED TO FINDPROGNO.
004230 FINDPROGNO.
004240     IF       SSPROGNO IS EQUAL TO PARPROGNO
004250              GO TO FINDPARNAME.
004260     READ     PARFILEIN AT END
004270     DISPLAY ' FLOBOL04, PARFILEIN PREMATURE EOF, EOJ '
004280     GO TO WRAPUP.
004290     GO       TO FINDPROGNO.
004300 FINDPARNAME.
004310     IF       SSPARNAME IS EQUAL TO PARPARNAME
004320              GO TO HAVEFIND.
004330     READ     PARFILEIN AT END
004340              GO TO MESAGE.
004350     GO       TO FINDPARNAME.
004360 HAVEFIND.
004370     MOVE     SSLOCATION TO NLOCATION.
004380     MOVE     PARPROGNO TO NPROGNO.
004390     MOVE 'A AT' TO NKEY.
004400     MOVE     PARLINE TO NLINE NLLLL.
004410     MOVE     0001 TO NSTATE NSSSS.
004420     MOVE     SSFROMPAR TO NPAR.
004430     IF       SSWHAT IS EQUAL TO 'SWITCH   '
004440              MOVE SSWHAT TO NWHAT
004450              ELSE
004460              MOVE 'SWITCH CONNECTOR' TO NWHAT.
004470     WRITE    NEWRECORD.
004480     GO       TO READSORTER.
004490 WRAPUP.
004500     CLOSE    SUPPLEMENTAL-PART4-IN.
004510     CLOSE    PARFILEIN.
004520     CLOSE    OUTFILE.
004530     STOP     RUN.
