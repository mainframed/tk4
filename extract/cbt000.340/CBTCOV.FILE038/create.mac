         MACRO
         PMVC  &TO,&FROM
         XC    &TO-2(2),&TO-2 CLEAR LENGTH FIELD
         TM    &FROM+6,X'80'  PARM?
         BZ    PM&SYSNDX      NO
         LH    PMTOL,&FROM+4  LENGTH
         STH   PMTOL,&TO-2    SAVE LENGTH
         LR    PMFML,PMTOL
         LA    PMTO,&TO
         L     PMFM,&FROM
         MVCL  PMTO,PMFM      MOVE STRING
PM&SYSNDX      EQU            *
         MEND
         MACRO
         CNV   &TO,&FROM
         XC    &TO.(2),&TO    CLEAR
         CLI   &FROM-1,X'00'  PARM?
         BE    CN&SYSNDX      NO
         LH    6,&FROM-2      LENGTH
         XC    DBWK,DBWK
         LA    7,&FROM        FROM
         LA    8,DBWK+8
         SR    8,6            TO
         BCTR  6,0            -1
         EX    6,MVIT         MOVE FIELD
         PACK  DBWK,DBWK
         CVB   6,DBWK
         STH   6,&TO          STORE
CN&SYSNDX      EQU            *
         MEND
PMTO     EQU   6
PMTOL    EQU   7
PMFM     EQU   8
PMFML    EQU   9
CREATE   CSECT
         SAVE  (14,12)
         LR    2,1
         BALR  10,0
         USING *,10
         GETMAIN              R,LV=ENDAREA-WKAREA
         ST    13,4(1)
         ST    1,8(13)
         LR    13,1
         USING WKAREA,13
         USING CPPL,2
         LA    3,DAIRLIST     SET UP DAIR LIST PARMS
         USING DAPL,3         MOVE CPPL INFO
         MVC   DAPLUPT,CPPLUPT
         MVC   DAPLECT,CPPLECT
         MVC   DAPLPSCB,CPPLPSCB
         LA    1,CPECB        MY ECB
         ST    1,DAPLECB      STORE IN PARM LIST
         XC    CPECB,CPECB    CLEAR
         LA    1,DAIRBLK
         ST    1,DAPLDAPB
         XC    MYFLAG(5),MYFLAG CLEAR AREA
         XC    SAVDIR(2),SAVDIR
         XC    TRKALLOC(8),TRKALLOC
         MVC   VOLNUM(6),=C'      '
         MVI   BUF,C' '
         MVC   BUF+1(132),BUF CLEAR MSG AREA
         DROP  3
* NOW PARSE THE COMMAND STRING
*
         LA    3,PPLSPACE
         USING PPL,3
         MVC   PPLUPT,CPPLUPT SET UP PARSE INFO
         MVC   PPLECT,CPPLECT
         LA    4,CPECB
         ST    4,PPLECB
         L     4,=V(TABLE)
         ST    4,PPLPCL
         LA    4,ANSPL
         ST    4,PPLANS       STORE BACK
         MVC   PPLCBUF,CPPLCBUF
         LR    1,3
         LINK  EP=IKJPARS
         L     5,ANSPL        ->PDL RETURNED BY PARSE
         LTR   15,15
         BNZ   DONE
*SET ATTENTION HANDLING(MOVE LIST FORM TO DSECT WORKAREA)
         MVC   STAX(LENSTAX),LSTAX
         STAX  MF=(E,STAX)
         LA    11,PRMS
         USING IKJPARMD,5
         USING PARMS,11
         MVI   PARMS,C' '
         MVC   PARMS+1(245),PARMS
*MOVE SUPPLIED PARMS
         XC    MYFLAG(5),MYFLAG
         PMVC BLK,BLKNUM
         PMVC  LREC,LNUM
         PMVC  FIL,FLE
         PMVC  RECF,FORM
         PMVC  PVOL,VOL
         PMVC  PBUFL,BFL
         PMVC  PBUFNO,BUF#
         PMVC  PKEYLEN,KEY
         PMVC  PNCP,NC#
         PMVC  PBFAL,BFA
         PMVC  PEROPT,ERO
         PMVC  POPTCD,OPT
         PMVC  PBFTEK,BFT
         PMVC  PEXPDT,EXPD
         PMVC  PRETPD,RTPD
         PMVC  DI,DREC
         PMVC  QUAN,SP1
         PMVC  INCR,SP2
         CLI   TYPE+1,1       TRK?
         BNE   S1             NO
         OI    TRK,X'80'
         B     S3
S1       CLI   TYPE+1,2       CYL?
         BNE   S2             NO
         OI    CYL,X'C0'
         B     S3
S2       OI    ABLK,X'40'     DEFAULT OF BLOCK
S3       EQU   *
         SR    7,7
         LA    4,DSNAM
         LH    7,DSNAM+4
         STH   7,SAVDSLN      SAVE FOR LATER
         LR    9,7            LENGTH
         LA    6,DSN          TO
         L     8,0(4)         FROM
         MVCL  6,8            MOVE IT
         TM    6(4),X'40'     QUOTED?
         BO    QUO            YES
         OI    UID,X'20'      NO
QUO      EQU   *
         TM    14(4),X'80'    MEMBER SPECIFIED?
         BZ    NOMEM
         LH    7,DSNAM+12     LENGTH
         LR    9,7
         LA    6,MEM          TO
         L     8,8(4)         FROM
         MVCL  6,8            MOVE  IT
*
* PARMS COMPLETED-NOW CREATE A DAIR34 & ATTRIB LIST
*
NOMEM    EQU   *
         LA    4,LIKEDSN      SAVE ADDRESS FOR LATER
         ST    4,ADLIKEDS     MUST DROP PARMD REG.5
         DROP 5
         DROP 3
         LA    3,DAIRBLK
         USING DAPB34,3
         MVC   DA34CD,=X'0034'
         XC    DA34FLG(6),DA34FLG
         OI    DA34CTRL,DA34CHN
         MVC   DA34NAME(8),=C'DSCREATE'
         LA    5,DAIRACB
         ST    5,DA34ADDR
         USING ATRCB,5
         XC    DAIRACB(80),DAIRACB
         OI    DAILABEL,DAIOUTIN
*SELECT RECFM OPTIONS
         LA    6,8            OUTER LOOP
         LA    8,LIST-2
LP1      LA    9,RECF-1
         LA    7,5            INNER LOOP
         LA    8,2(8)
LP2      LA    9,1(9)
         CLC   0(1,8),0(9)
         BE    HIT
         BCT   7,LP2
         B     ENDLP1
HIT      OC    DAIRECFM,1(8) MASK IN BITS
ENDLP1   BCT   6,LP1
*SELECT OPTCD OPTIONS
         LA    6,4            OUTER LOOP
         LA    8,LIST2-2
LP3      LA    9,POPTCD-1
         LA    7,4            INNER LOOP
         LA    8,2(8)
LP4      LA    9,1(9)
         CLC   0(1,8),0(9)
         BE    HIT2
         BCT   7,LP4
         B     ENDLP3
HIT2     OC    DAIOPTCD,1(8)  MASK IN BITS
ENDLP3   BCT   6,LP3
*FILL IN EXPDT FIELD
         CLI   PEXPDT,C' '    BLANK?
         BE    E1             YES
*CONVERT YEAR
         XC    DBWK,DBWK
         MVC   DBWK+6(2),PEXPDT   MOVE YEAR ONLY
         PACK  DBWK,DBWK
         CVB   6,DBWK
         STC   6,DAIYEAR      STORE
*FILL IN THE DAY
         XC    DBWK,DBWK
         MVC   DBWK+5(3),PEXPDT+2  MOVE DAY
         PACK  DBWK,DBWK
         CVB   6,DBWK
         SLL   6,4            LEFT JUSTIFY
         ST    6,FTMP
         MVC   DAIDAY(2),FTMP+2
*FILL IN EROPT BITS
E1       CLC   PEROPT,=C'SKP'
         BNE   E2
         OI    DAIEROPT,X'40'
         B     E4
E2       CLC   PEROPT,=C'ABE'
         BNE   E3
         OI    DAIEROPT,X'20'
         B     E4
E3       CLC   PEROPT,=C'ACC'
         BNE   E4
         OI    DAIEROPT,X'80'
E4       EQU   *
*COMBINE BFTEK AND BFAL OPTIONS
         CLI   PBFAL,C'F'     FULLWORD?
         BNE   DBND           NO
         OI    DAIBFTEK,X'01'
         B     *+8
DBND     OI    DAIBFTEK,X'02' DOUBLEWORD
         CLI   PBFTEK,C' '    BLANK?
         BE    B4             YES
         CLI   PBFTEK,C'S'    SIMPLE?
         BNE   B1             NO
         OI    DAIBFTEK,X'40'
         B     B4
B1       CLI   PBFTEK,C'E'    EXCHANGE?
         BNE   B2             NO
         OI    DAIBFTEK,X'10'
         B     B4
B2       CLI   PBFTEK,C'A'    AUTO?
         BNE   B3             NO
         OI    DAIBFTEK,X'60'
         B     B4
B3       CLI   PBFTEK,C'R'    RECORD?
         BNE   B4             NO
         OI    DAIBFTEK,X'20'
B4       EQU   *
*CONVERT CHARACTER FIELDS TO BINARY & STORE
         CNV   DAIBLKSI,BLK
         CNV   DAILRECL,LREC
         CNV   HTMP,PNCP
         MVC   DAINCP(1),HTMP+1
         CLI   DAINCP,X'00'   ANY CP'S?
         BNE   *+8            YES
         MVI   DAINCP,X'01'   DEFAULT OF 1
         CNV   HTMP,PKEYLEN
         MVC   DAIKEYLE(1),HTMP+1
         CNV   DAIBUFL,PBUFL
         CNV   HTMP,PBUFNO
         MVC   DAIBUFNO(1),HTMP+1
         CNV   HTMP,DI        SAVE HTMP FOR DAIR08
*
*THIS SECTION IS FOR LIKE(DSN) PROCESSING. LOCATE
*THEN OBTAIN THE DATA SET SPECIFIED. ANY PARMS IN
*THE COMMAND STRING OVERRIDE THE DSCB INFORMATION
*OBTAINED FROM THE LIKE DATA SET.
         L     4,ADLIKEDS
         TM    6(4),X'80'     LIKE(DSN) EXIST?
         BZ    DAIRCALL       NO SKIP THIS CODE
         MVI   AB,C' '        CLEAR AREA
         MVC   AB+1(43),AB    FOR OCATE MACRO
         LA    1,AB
         SR    15,15
         TM    6(4),X'40'     QUOTED?
         BO    YESQUO         YES
*PICK UP PREFIX FROM UPT
         L     12,CPPLUPT
         SR    7,7
         IC    7,23(12)       LENGTH OF PREFIX
         LR    15,7           SAVE
         LA    15,1(15)       ACCOUNT FOR PERIOD
         LR    9,7
         LR    6,1            TO AREA(AB)
         LR    8,12           ADDRESS OF UPT
         LA    8,16(12)       FROM AREA(UPTPREFX)
MOVEUID  EQU   *
         MVCL  6,8            MOVE PREFIX
         MVI   0(6),X'4B'     INSERT PERIOD
         LA    6,1(6)         BUMP ONCE MORE
         LR    1,6            CORRECT R1 PTR
YESQUO   LH    7,4(4)         LENGTH OF DSN PARM
         AR    15,7           IN CASE UID PREFIX
         STH   15,LIKEDSLN    SAVE FOR DIR ROUTINE ALLOC
         LR    9,7
         LR    6,1            LOCATE MACRO DSN AREA(AB)
         L     8,0(4)         FROM AREA(PARSE)
         MVCL  6,8            MOVE NAME
*FIND WHERE DATASET RESIDES. SET UP LOCATE
*CAMLST ADDRESSES
         DROP  2
         MVC   LCAM(LOLEN),LOCAM
         LA    1,AB
         ST    1,LCAM+4
         LA    1,LOCAREA
         ST    1,LCAM+12
*EXECUTE LOCATE
         LOCATE LCAM
         B     *+4(15)
         B     LOCOK          0
         B     LOERR1         4
         B     LOERR2         8
         B     LOERR2         12
         B     LOERR2         16
         B     LOERR2         20
         B     LOERR1         24
         B     LOERR1         28
         B     LOERR1         32
LOERR1   MVC   BUF+1(26),=C'LOCATE ERROR ON VOLUME FOR'
         B     *+10
LOERR2   MVC   BUF+1(40),=C'FOLLOWING DATA SET NOT FOUND IN CATALOG:'
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         LA    0,44
         LA    1,AB
         TPUT  (1),(0),R
         B     DONE
LOCOK    EQU   *
*GET DSCB INFORMATION. SET UP OBTAIN
*CAMLST ADDRESSES
         MVC   OCAM(OBLEN),OBCAM
         LA    1,DSAB
         ST    1,OCAM+4
         LA    1,VOLNUM
         ST    1,OCAM+8
         LA    1,WRKSET
         ST    1,OCAM+12
*MOVE DSN AND VOL INTO OBTAIN CAMLST
         MVC   DSAB(44),AB
         MVC   VOLNUM(6),LOCAREA+6
*EXECUTE OBTAIN
         OBTAIN OCAM
         B     *+4(15)        0
         B     OBOK           0
         B     OBERR1         4
         B     OBERR2         8
         B     OBERR1         12
         B     OBERR1         16
         B     OBERR1         20
OBERR1   MVC   BUF+1(26),=C'OBTAIN ERROR ON VOLUME FOR'
         B     *+10
OBERR2   MVC   BUF+1(39),=C'FOLLOWING DATA SET NOT FOUND ON VOLUME:'
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         LA    0,44
         LA    1,AB
         TPUT  (1),(0),R
         B     DONE
OBOK     EQU   *
*CHECK IF PARMS FROM CMD STRING EXIST
         CLI   PEXPDT,C' '    PARM?
         BNE   NOEXPDT        YES-DO NOT USE DSCB INFO
         MVC   DAIEXPDT(3),DS1EXPDT
NOEXPDT  CLI   PKEYLEN-1,X'00'
         BNE   NOKEYLEN       BRANCH-DON'T USE DSCB
         MVC   DAIKEYLE(1),DS1KEYL
NOKEYLEN CLI   RECF-1,X'00'
         BNE   NORECF         DON'T USE DSCB
         MVC   DAIRECFM(1),DS1RECFM
NORECF   CLI   PEROPT-1,X'00'
         BNE   NOEROPT        DON'T USE DSCB
         MVC   DAIOPTCD(1),DS1OPTCD
NOEROPT  CLI   BLK-1,X'00'
         BNE   NOBLK          DON'T USE DSCB
         MVC   DAIBLKSI(2),DS1BLKL
NOBLK    CLI   LREC-1,X'00'
         BNE   NOLREC
         CLC   DS1LRECL(2),=X'8000'  >32,756 IS A SPANNED RECORD?
         BE    NOLREC         IGNORE
         MVC   DAILRECL(2),DS1LRECL
NOLREC   CLI   DI-1,X'00'
         BNE   NODIR
         TM    DS1DSORG,X'02' PO?
         BZ    NODIR          NO
         BAL    9,GETDIR      YES
*R2 CONTAINS #DIRECTORY BLOCKS
         STH   2,SAVDIR       SAVE FOR LATER
NODIR    CLI   QUAN-1,X'00'
         BNE   DAIRCALL
         BAL   2,GETSPACE     GOTO DS SPACE ROUTINE
DAIRCALL LA    1,DAIRLIST
LIKECALL EQU   *
         LINK  EP=IKJDAIR
TESTRC   B     TESTALOC(15)
TESTALOC B     OK             0
         B     QUIT           4
         B     QUIT           8
         B     DAIRERR        12
         B     DAIRERR        16
         B     DAIRERR        20
         B     QUIT           24
         B     QUIT           28
         B     QUIT           32
         B     QUIT           36
         B     QUIT           40
         B     DAIRERR        44
         B     QUIT           48
OK       TM    MYFLAG,X'40'  FROM DIRECTORY ROUTINE?
         BO    DIRRTN         YES-GO BACK
         TM    MYFLAG,X'80'   ALL DONE?
         BO    QUIT           YES-DONE
         OI    MYFLAG,X'80'  COMPLETE DAIR34 CALL
         DROP  3
*
*NOW ALLOCATE THE DATASET WITH DAIR08
*
         LA    3,DAIRBLK
         USING DAPB08,3
         MVI   DAPB08,C' '
         MVC   DAPB08+1(83),DAPB08
         MVC   DA08CD,=X'0008'
         XC    DA08FLG(6),DA08FLG
         LA    6,DSNBUF
         ST    6,DA08PDSN
         MVC   DSNLEN(2),SAVDSLN
         MVC   NAME(44),DSN
         MVC   DA08DDN(8),FIL
         CLI   PVOL-1,X'00'   VOL SUPPLIED?
         BE    *+10           NO
         MVC   DA08SER(6),PVOL
         XC    DA08BLK(16),DA08BLK
*RETRIEVE DIRECTORY INFO IF ANY
         MVC   DA08DQTY+2(2),HTMP
         MVC   DA08MNM,MEM
         XC    DA08DSP1(8),DA08DSP1
         OI    DA08DSP1,X'04' NEW
         OI    DA08DPS2,X'02' CATALOG
         OI    DA08DPS3,X'08' KEEP
         TM    UID,X'20'      SUPPLY THE USERID?
         BZ    NOID           NO
         OC    DA08CTL,UID
*CHECK IF VOLUME AND DIRECTORY SUPPLIED.
*CHECK IF SPACE FROM 'LIKE' DSCB WAS CALCULATED.
*IF NOT-BRANCH TO BYPASS LABEL AND CONTINUE
NOID     EQU   *
         OI    DA08CTL,X'02'  ATTRIB LIST
         MVC   DA08ALN(8),=C'DSCREATE'  NAME
         CLI   DA08SER,C' '   VOL SUPPLIED?
         BNE   *+10           YES
         MVC   DA08SER(6),VOLNUM  USE OBTAIN DATA IF ANY
         CLI   DA08DQTY+3,X'00' DIR SUPPLIED?
         BNE   *+10           YES
         MVC   DA08DQTY+2(2),SAVDIR   USE DIR ROUTINE DATA IF ANY
         CLI   TRKALLOC+3,X'00'  ANY TRKS CALCULATED?
         BE    BYPASS           NO
         OI    DA08CTL,X'80'  SPECIFY UNITS IN TRKS
         MVC   DA08PQTY+2(2),TRKALLOC+2
         MVC   DA08SQTY+2(2),SECALLO+2
         B     DAIRCALL
BYPASS   TM    TRK,X'80'
         BZ    NOTRK
         OC    DA08CTL,TRK
         B     SP
NOTRK    TM    CYL,X'C0'
         BZ    NOCYL
         OC    DA08CTL,CYL
         B     SP
NOCYL    TM    ABLK,X'40'
         BZ    SP
         OC    DA08CTL,ABLK
         CNV   HTMP,BLK
         MVC   DA08BLK+2(2),HTMP
SP       EQU   *
         CNV   HTMP,QUAN
         MVC   DA08PQTY+2(2),HTMP  MOVE PRIMARY SPACE
         CNV   HTMP,INCR
         MVC   DA08SQTY+2(2),HTMP  MOVE SECONDARY SPACE
         B     DAIRCALL
DAIRERR  MVC   BUF+1(15),=C'DAIR ERROR CODE'
         UNPK  BUF+17(5),DA08DARC(3)
         TR    BUF+17(4),HEX-240
         MVI   BUF+21,C' '    BLANK LAST BYTE
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         B     QUIT
*ATTENTION EXIT ROUTINE
         USING *,15
ATTEXIT  BR    14             MUST COMPLETE
         DROP  15
QUIT     EQU   *
*DELETE ATTRIB ACB FROM CHAIN
         DROP  3
         LA    3,DAIRBLK
         USING DAPB34,3
         MVC   DA34CD,=X'0034'
         XC    DA34FLG(6),DA34FLG
         OI    DA34CTRL,DA34UNCH
         MVC   DA34NAME,=C'DSCREATE'
         LA    1,DAIRLIST
         LINK  EP=IKJDAIR
DONE     L     6,4(13)
         FREEMAIN R,LV=ENDAREA-WKAREA,A=(13)
         LR    13,6
         RETURN (14,12)
*
*GETSPACE ROUTINE FOR LIKE(DSN) OPTION
*
GETSPACE EQU   *
         MVC   SECALLO(4),DS1SCALO  SAVE SECONDARY SPACE FIELD
         LA    8,DS1EXT1
         LA    9,3            3 EXTENTS IN FMT1 DSCB
GETEXT   CLI   0(8),X'00' EXTENT DESCRIPTION?
         BE    GOTSPACE       NO
         MVC   EXTENT(8),2(8)
         BAL   14,GETTRKS     GET #TRKS
         LA    8,10(8)        NEXT EXTENT
         BCT   9,GETEXT
         CLC   DS1PTRDS(5),=X'0000000000' PTR TO FMT3?
         BE    GOTSPACE       NO
*
*OBTAIN THE FMT3 AND GET ITS SPACE
         MVC   O3CAM(OB3LEN),OB3CAM
         MVC   CCHHR(5),DS1PTRDS
         LA    1,CCHHR
         ST    1,O3CAM+4
         LA    1,VOLNUM
         ST    1,O3CAM+8
         LA    1,WRKSET
         ST    1,O3CAM+12
         OBTAIN O3CAM
         B     *+4(15)
         B     O3OK           0
         B     OBERR1         4
         B     OBERR1         8
         B     OBERR1         12
         B     OBERR1         16
         B     OBERR1         20
O3OK     EQU   *
         LA    9,4            4 EXTENTS IN DS3EXTNT
         LA    8,DS3EXTNT
GETEXT2  CLI   0(8),X'00' EXTENT?
         BE    GOTSPACE       NO
         MVC   EXTENT(8),2(8)
         BAL   14,GETTRKS
         LA    8,10(8)        NEXT EXTENT
         BCT   9,GETEXT2
         LA    8,DS3ADEXT
         LA    9,9            9 EXTENTS IN DS3ADEXT
GETADEXT CLI   0(8),X'00'
         BE    GOTSPACE
         MVC   EXTENT(8),2(8) DESCRIPTOR
         BAL   14,GETTRKS
         LA    8,10(8)        NEXT EXTENT
         BCT   9,GETADEXT
*
*THE SPACE IS NOW CALCULATED
*
GOTSPACE BR    2 GO BACK
*GET TRACKS ROUTINE
GETTRKS  LH    1,LOWCC BEGIN CYL
         MH    1,=H'19'       #TRKS ON 3330
         AH    1,LOWHH        ADD HEAD#
         LTR   1,1
         BNP   ERR            NOT POSITIVE-THEN ERROR
         LH    4,HICC         HIGH CYL
         MH    4,=H'19'       IN TRACKS
         AH    4,HIHH         ADD HEAD#
         CR    1,4            LOW>HI?
         BH    ERR            ERROR IF SO
         SR    4,1
         LA    4,1(4)         #TRKS THIS EXTENT
         A     4,TRKALLOC     ADD TO TOTAL
         ST    4,TRKALLOC     STORE
         BR    14             RETURN
*UNABLE TO COMPUTE SPACE
ERR      MVC   BUF+1(26),=C'ERROR IN CALCULATING SPACE'
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         B     DONE
*
*DIRECTORY ROUTINE TO ALLOCATE LIKE(DSN), OPEN AND READ
*THE NUMBER OF BLOCKS FOR THE NEW DSN.
*
GETDIR   EQU   *
*ALLOCATE THE DATA SET FOR OPEN
         MVC   DAIRLST2(20),DAIRLIST  COPY DAPL
         LA    2,DAIRBLK2
         ST    2,DAIRLST2+16
         USING DAPB08,2
         MVI   DAPB08,C' '    CLEAR AREA
         MVC   DAPB08+1(83),DAPB08
         MVC   DA08CD,=X'0008'
         XC    DA08FLG(6),DA08FLG
         LA    6,DSNBUF
         ST    6,DA08PDSN
         MVC   DSNLEN(2),LIKEDSLN
         MVC   NAME(44),DSAB
         MVC   DA08SER(6),VOLNUM
         XC    DA08BLK(16),DA08BLK
         XC    DA08DSP1(8),DA08DSP1
         OI    DA08DSP1,X'08' SHR
         OI    DA08DPS2,X'08' KEEP
         OI    DA08DPS3,X'08' KEEP
         OI    DA08CTL,X'08'  MARK IN-USE
         OI    MYFLAG,X'40'   WENT THROUGH CODE
         LA    1,DAIRLST2
         B     LIKECALL
DIRRTN   EQU   *
         XC    MYFLAG(1),MYFLAG
         MVC   X(YLEN),Y      MOVE DCB
         LA    6,X
         USING IHADCB,6
         MVC   DCBDDNAM(8),DA08DDN
         DROP  2              DAIRBLK FINISHED
         MVI   OPLIST,X'80'                                    KMH
         OPEN  (X,INPUT),MF=(E,OPLIST)
         TM    DCBOFLGS,X'10'
         BO    GOODOPEN       YES
         MVC   BUF+1(10),=C'OPEN ERROR'
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         B     DONE
GOODOPEN SR    2,2  COUNTER FOR #BLKS READ
         SR    12,12          CLEAR R12
         MVC   RDMAC(LLREAD),LREAD  MOVE READ MACRO
DIREAD   READ  RWDECB1,SF,X,READBUFF,,MF=E
         CHECK RWDECB1
         LA    2,1(2)         ADD TO BLK COUNTER
         L     7,DCBIOBA      A(IOB PREFIX)
         LA    7,8(7)         A(IOB)
         LA    8,256          #BYTES IN BLOCK
         LH    12,=H'13030'   DCB BLKSIZE
         SH    12,14(7)       SUB RESIDUAL COUNT
         CR    12,8
         BE    DIREAD         BRANCH-EQUAL 256 BLK
*NOT A DIRECTORY BLOCK
         CLOSE (X),MF=(E,OPLIST)
         MVC   BUF+1(16),=C'ERROR: NOT A PDS'
         LA    0,132
         LA    1,BUF+1
         TPUT  (1),(0),R
         B     DONE
ENDDIR   EQU   *
         DROP  6
         CLOSE (X),MF=(E,OPLIST)
         BR    9              RETURN
         DS    0F
LSTAX    STAX  ATTEXIT,REPLACE=YES,MF=L
LENSTAX  EQU   *-LSTAX
OBCAM    DS    0F
         CAMLST               SEARCH,0,0,0
OBLEN    EQU   *-OBCAM
LOCAM    DS    0F
         CAMLST               NAME,0,,0
LOLEN    EQU   *-LOCAM
OB3CAM   DS    0F
         CAMLST               SEEK,0,0,0
OB3LEN   EQU   *-OB3CAM
Y        DS    0F
         DCB   DSORG=PS,MACRF=R,DDNAME=YY,EODAD=ENDDIR,                X
               RECFM=U,BLKSIZE=13030
YLEN     EQU   *-Y
LREAD    DS    0F
         READ  XXX,SF,0,0,'S',MF=L
LLREAD   EQU   *-LREAD
MVIT     MVC   0(0,8),0(7)
LIST     DC    C'F'
         DC    X'80'
         DC    C'V'
         DC    X'40'
         DC    C'U'
         DC    X'C0'
         DC    C'T'
         DC    X'20'
         DC    C'B'
         DC    X'10'
         DC    C'S'
         DC    X'08'
         DC    C'A'
         DC    X'04'
         DC    C'M'
         DC    X'02'
LIST2    DC    C'W'
         DC    X'80'
         DC    C'C'
         DC    X'20'
         DC    C'Q'
         DC    X'08'
         DC    C'T'
         DC    X'02'
HEX      DC    CL16'0123456789ABCDEF'
         LTORG
         IKJCPPL
         IKJPPL
         IKJDAP08
         IKJDAPL
         IKJDAP34
         DCBD  DSORG=(PS),DEVD=(DA)
WKAREA   DSECT
SAVE     DS    18F
CPPLADR  DS    F
CPECB    DS    F
DAIRLIST DS    5F
DAIRBLK  DS    24F
DAIRACB  DS    20F
DAIRLST2 DS    5F
DAIRBLK2 DS    24F
PRMS     DS    70F
DBWK     DS    D
PPLSPACE DS    7A
ANSPL    DS    A
FTMP     DS    F
HTMP     DS    H
SAVDSLN  DS    H
DSNBUF   DS    0CL46
DSNLEN   DS    CL2
NAME     DS    CL44
BUF      DS    CL133
SAVDIR   DS    H
LIKEDSLN DS    H
ADLIKEDS DS    F
MYFLAG   DS    X
UID      DS    X
TRK      DS    X
CYL      DS    X
ABLK     DS    X
         DS    0H
EXTENT   DS    CL8
         ORG   EXTENT
LOWCC    DS   CL2
LOWHH    DS    CL2
HICC     DS    CL2
HIHH     DS    CL2
TRKALLOC DS    F
SECALLO  DS    F
STAX     STAX  0,REPLACE=YES,MF=L
OCAM     DS    0F
         CAMLST               SEARCH,0,0,0
DSAB     DS    CL44
VOLNUM   DS    CL6
WRKSET   DS    0D
         DS    148C
O3CAM    DS    0F
         CAMLST               SEEK,0,0,0
CCHHR    DS    CL5
LCAM     DS    0F
         CAMLST               NAME,0,,0
AB       DS    CL44
LOCAREA  DS    0D
         DS    265C
OPLIST   OPEN  (,),MF=L
RDMAC    DS    0F
         READ  RWDECB1,SF,0,0,0,MF=L
X        DS    0F
         DCB   DSORG=PS,MACRF=R,DDNAME=XX,EODAD=ENDDIR,                X
               RECFM=U,BLKSIZE=13030
READBUFF DS    CL13030
DSCB     EQU   WRKSET-44
DS1EXPDT EQU   DSCB+56
DS1DSORG EQU   DSCB+82
DS1RECFM EQU   DSCB+84
DS1OPTCD EQU   DSCB+85
DS1BLKL  EQU   DSCB+86
DS1LRECL EQU   DSCB+88
DS1KEYL  EQU   DSCB+90
DS1SCALO EQU   DSCB+94
DS1EXT1  EQU   DSCB+105
DS1PTRDS EQU   DSCB+135
DS3EXTNT EQU   WRKSET+4
DS3ADEXT EQU   WRKSET+45
ENDAREA  EQU   *
PARMS    DSECT
         DS    H
BLK      DS    CL5
         DS    H
LREC     DS    CL5
         DS    H
FIL      DS    CL8
         DS    H
RECF     DS    CL5
         DS    H
PVOL     DS    CL6
         DS    H
PBUFL    DS    CL5
         DS    H
PBUFNO   DS    CL3
         DS    H
PKEYLEN  DS    CL3
         DS    H
PNCP     DS    CL2
         DS    H
PBFAL    DS    CL1
         DS    H
PEROPT   DS    CL3
         DS    H
POPTCD   DS    CL4
         DS    H
PBFTEK   DS    CL1
         DS    H
PEXPDT   DS    CL5
         DS    H
PRETPD   DS    CL4
         DS    H
DSO      DS    CL2
         DS    H
DI       DS    CL3
         DS    H
QUAN     DS    CL4
         DS    H
INCR     DS    CL4
         DS    H
KIND     DS    CL5
DSN      DS    CL46
MEM      DS    CL8
LIKENAME DS    CL46
ATRCB    DSECT
RESERVE  DS    2A
DAIMASK  DS    CL8
         ORG   DAIMASK+6
DAILABEL DS    X
DAIINOUT EQU   X'80'
DAIOUTIN EQU   X'40'
         ORG   DAIMASK+8
         DS    CL3
DAIEXPDT DS    0CL3
DAIYEAR  DS    X
DAIDAY   DS    CL2
         DS    CL2
DAIBUFNO DS    X
DAIBFTEK DS    X
DAIBUFL  DS    CL2
DAIEROPT DS    X
DAIKEYLE DS    X
         DS    CL6
DAIRECFM DS    X
DAIOPTCD DS    X
DAIBLKSI DS    CL2
DAILRECL DS    CL2
DAINCP   DS    X
         EJECT
CREATE   CSECT
TABLE    IKJPARM
DSNAM    IKJPOSIT DSNAME,PROMPT='DATA SET NAME'
LIKE     IKJKEYWD
         IKJNAME 'LIKE',SUBFLD=LK
BLKSIZE  IKJKEYWD
         IKJNAME 'BLKSIZE',SUBFLD=BLOCK
LRECL    IKJKEYWD
         IKJNAME 'LRECL',SUBFLD=RECORD
FILE     IKJKEYWD
         IKJNAME 'FILE',SUBFLD=DDN
RECFM    IKJKEYWD
         IKJNAME 'RECFM',SUBFLD=FORMAT
VOLUME   IKJKEYWD
         IKJNAME 'VOLUME',SUBFLD=SERIAL
BUFL     IKJKEYWD
         IKJNAME 'BUFL',SUBFLD=BUFLNTH
BUFNO    IKJKEYWD
         IKJNAME 'BUFNO',SUBFLD=BUFN
KEYLEN   IKJKEYWD
         IKJNAME 'KEYLEN',SUBFLD=KEYL
NCP      IKJKEYWD
         IKJNAME 'NCP',SUBFLD=CHAN
BFALN    IKJKEYWD
         IKJNAME 'BFALN',SUBFLD=BFAL
EROPT    IKJKEYWD
         IKJNAME 'EROPT',SUBFLD=EROP
OPTCD    IKJKEYWD
         IKJNAME 'OPTCD',SUBFLD=OPTC
BFTEK    IKJKEYWD
         IKJNAME 'BFTEK',SUBFLD=BFTE
EXPDT    IKJKEYWD
         IKJNAME 'EXPDT',SUBFLD=DATE
RETPD    IKJKEYWD
         IKJNAME 'RETPD',SUBFLD=RETP
DIR      IKJKEYWD
         IKJNAME 'DIR',SUBFLD=DIREC
SPACE    IKJKEYWD
         IKJNAME 'SPACE',SUBFLD=AMT
LK       IKJSUBF
LIKEDSN  IKJPOSIT DSNAME,PROMPT='LIKE DATA SET NAME'
BLOCK    IKJSUBF
BLKNUM   IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='BLKSIZE',MAXLNTH=5
RECORD   IKJSUBF
LNUM     IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='LRECL',MAXLNTH=5
DDN      IKJSUBF
FLE      IKJIDENT 'ALPHA',FIRST=ALPHA,OTHER=ALPHANUM,                  X
               PROMPT='FILENAME'
FORMAT   IKJSUBF
FORM     IKJIDENT 'ALPHA',FIRST=ALPHA,OTHER=ALPHA,                     X
               PROMPT='RECFM',MAXLNTH=5
SERIAL   IKJSUBF
VOL      IKJIDENT 'ALPHA',FIRST=ALPHANUM,OTHER=ALPHANUM,               X
               PROMPT='VOLUME',MAXLNTH=6
BUFLNTH  IKJSUBF
BFL      IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='BUFL',MAXLNTH=5
BUFN     IKJSUBF
BUF#     IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='BUFNO',MAXLNTH=3
KEYL     IKJSUBF
KEY      IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='KEYLEN',MAXLNTH=3
CHAN     IKJSUBF
NC#      IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='NCP',MAXLNTH=2
BFAL     IKJSUBF
BFA      IKJIDENT 'ALPHA',FIRST=ALPHA,PROMPT='BFALN',MAXLNTH=1
EROP     IKJSUBF
ERO      IKJIDENT 'ALPHA',FIRST=ALPHA,OTHER=ALPHA,                     X
               PROMPT='EROPT',MAXLNTH=3
OPTC     IKJSUBF
OPT      IKJIDENT 'ALPHA',FIRST=ALPHA,OTHER=ALPHA,                     X
               PROMPT='OPTCD',MAXLNTH=4
BFTE     IKJSUBF
BFT      IKJIDENT 'ALPHA',FIRST=ALPHA,                                 X
               PROMPT='BFTEK',MAXLNTH=1
DATE     IKJSUBF
EXPD     IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='EXPDT',MAXLNTH=5
RETP     IKJSUBF
RTPD     IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='RETPD',MAXLNTH=4
DIREC    IKJSUBF
DREC     IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='DIR BLKS',MAXLNTH=3
AMT      IKJSUBF
SP1      IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               PROMPT='PRIMARY QUANTITY',MAXLNTH=4
SP2      IKJIDENT 'NUMBER',FIRST=NUMERIC,OTHER=NUMERIC,                X
               DEFAULT='0',MAXLNTH=4
TYPE     IKJKEYWD DEFAULT='BLOCK'
         IKJNAME 'TRK'
         IKJNAME 'CYL'
         IKJNAME 'BLOCK'
         IKJENDP
         END
