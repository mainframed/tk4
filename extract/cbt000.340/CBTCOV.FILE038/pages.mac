 PAGREPT: PROC OPTIONS(MAIN,REENTRANT) REORDER;
  /*     VERSION DATE 10/4/76
         MVS R3.7 + SU 7
         YALE UNIVERSITY COMPUTER CENTER
         175 WHITNEY AVE
         NEW HAVEN, CT.
         203 432-4080
         HOWARD GILBERT
         MODIFIED BY BOB JUCH, SAN BRUNO POSTAL DATA CENTER
         RECOMMENDED AS A TSO COMMAND,
         USE PLI OPTIMIZING COMPILER

         UPDATED 16SEP80, WILLIAM SMITH, MEMOREX CORPORATION
                          SAN TOMAS AT CENTRAL EXPRESSWAY,
                          DEPT. 8205, MS 10-35
                          SANTA CLARA, CALIFORNIA  95052
         . SCREEN FORMATTING OVERLAP PROBLEM
         . MVS 3.8 SE 1.1                                            */
 DCL PLIXOPT CHAR(32) VAR STATIC EXT INIT('NOSTAE,NOSPIE,ISA(3K)');
  DCL SIXTEEN PTR;
  UNSPEC(SIXTEEN)=(24)'0'B××'00010000'B;
  DCL CVTPTR PTR BASED(SIXTEEN);
  DCL 1 CVT BASED(CVTPTR),
          2 F1 CHAR(704),
          2 CVTASMVT PTR;
  DCL 1 ASMVT BASED(CVTASMVT),
          2 F1 CHAR(4),
          2 ASMSART PTR,
          2 ASMPART PTR,
          2 F2 CHAR(96),
          2 (UNRESERVED,POOL,VIO,ADSPC) FIXED BIN(31);
  DCL 1 PART BASED(ASMPART),
          2 ID CHAR(4),
          2 PARTSIZE FIXED BIN(31),
          2 F2 CHAR(16),
          2 PARTDSNL PTR,
          2 F3 CHAR(52),
          2 PARTE (N REFER(PARTSIZE)),
                3 F4 CHAR(8),
                3 TYPE BIT(4) UNAL,
                3 PAD1 BIT(4) UNAL,
                3 NOTINUSE BIT UNALIGNED,
                3 DSBAD BIT UNALIGNED,
                3 INCP BIT UNALIGNED,
                3 F5 CHAR(6),
                3 (SLOTS,AVAIL,ERRS) FIXED BIN(31),
                3 (IORB,PAT,PCT,EDP) PTR,
                3 UCBP PTR,
                3 F6 CHAR(8),
                3 COR FIXED BIN(31),
                3 F7 CHAR(4);
  DCL 1 SART BASED(ASMSART),
          2 ID CHAR(4),
          2 SARTSIZE FIXED BIN(31),
          2 F2 CHAR(16),
          2 SARTDSNL PTR,
          2 F3 CHAR(52),
          2 SARTE (N REFER(SARTSIZE)),
                3 F4 CHAR(9),
                3 NOTINUSE BIT UNALIGNED,
                3 DSBAD BIT UNALIGNED,
                3 F5 CHAR(6),
                3 (SETS,AVAILABLE,ERRORS) FIXED BIN(31),
          3 (IORB,SAT,SDCT,EDB) PTR,
                3 UCBPTR PTR;
  DCL DSN(1) CHAR(44) BASED(P); /* DUMMY DIMENSION OF DSN */
  DCL N FIXED BIN;
  DCL USABLE CHAR(6);
  DCL P PTR;
  DCL TYPES(4) CHAR(6) INIT('PLPA','COMMON','DUPLEX','LOCAL');
  DCL 1 UCB BASED,
       2 F1 CHAR(13),
       2 UCBNAME CHAR(3),
       2 F2 CHAR(12),
       2 UCBSER CHAR(6);
1PUT EDIT('PAGE DATASETS:',
          'TYPE   SER   UNIT SLOTS  USED  ERRS USABLE PPT DSNAME')
         (A(14),SKIP,A(53));
0DO I=1 TO PARTSIZE;
    USABLE='      ';
    IF ^PARTE.NOTINUSE(I) & SLOTS(I)^=0 THEN DO;
       IF PARTE.DSBAD(I) THEN USABLE='    NO';
       IF PARTE.INCP(I) THEN USABLE='  INCP';
       PUT EDIT(TYPES(INDEX(TYPE(I),'1'B)),
                  UCBP(I)->UCBSER, UCBP(I)->UCBNAME,
                  SLOTS(I),SLOTS(I)-AVAIL(I),ERRS(I),
                  USABLE,
                  (50000/COR(I)+2),
                  PARTDSNL->DSN(I))
                 (SKIP,2 A(7),A(3),3 F(6),X(1),A(6),X(1),F(3),X(1),
                 (1A(29));
       END;
    END;
0PUT EDIT('PAGE SLOTS ARE',(POOL-UNRESERVED)*100/POOL,' % RESERVED')
         (SKIP,A(14),F(4),A(11));
0PUT EDIT('SWAP DATASETS:',
          '       SER   UNIT  SETS  USED  ERRS USABLE DSNAME')
         (SKIP,A(14),SKIP,A(42));
0DO I=1 TO SARTSIZE;
    USABLE='      ';
    IF ^SARTE.NOTINUSE(I) &SETS(I)^=0 THEN DO;
       IF SARTE.DSBAD(I) THEN USABLE='    NO';
       PUT EDIT(UCBPTR(I)->UCBSER, UCBPTR(I)->UCBNAME,
                  SETS(I),SETS(I)-AVAILABLE(I),ERRORS(I),
                  USABLE,
                  SARTDSNL->DSN(I))
              (SKIP,X(7),A(7),A(3),3 F(6),X(1),A(6),X(1),A(30));
       END;
    END;
0END;
