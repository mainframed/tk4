*                              AT LEVEL 003 AS OF 05/21/79
*                                MODIFIED BY BETTY MCCARDLE TO
*                                CATALOG DS TO DISK AND TAPE.
*          DATA SET VOLSER     AT LEVEL 002 AS OF 04/09/73
*          DATA SET VOLSER     AT LEVEL 001 AS OF 04/09/73
         START
BEGIN    SAVE  (14,12),,*
         BALR  11,0
         USING *,11,12
HERE     L     12,BASE12
         B     INIT
BASE12   DC    A(HERE+4096)
*
INIT     DS    0H
         LA    0,(L'MSG14+15)
         MVC   TEXTOUT(L'MSG14),MSG14
         BAL   9,PRTIT
*
START    DS    0H
         MVC   GDSNBR,PTWO
         MVI   CAMDSN,C' '
         MVC   CAMDSN+1(43),CAMDSN
         MVC   TEXTOUT(L'INITMSG),INITMSG
         LA    0,L'INITMSG
         LA    1,TEXTOUT
         BAL   9,PRTIT2
         TGET  TEXTIN,44
         CLC   TEXTIN(3),=C'   '
         BE    RETRN
         TR    TEXTIN,UPPER        TRANSLATE TO UPPER CASE
         LA    1,TEXTIN
         TRT   0(45,1),COMMA       SEARCH FOR COMMA
         LR    4,1                 R1 AND R4 = COMMA ADDR
         LA    5,TEXTIN
         SR    4,5
         S     4,ONE                R4 = LENGTH OF DSNAME
         A     1,ONE               BUMP R1
         LR    6,1
         EX    4,MOVE
         CLC   ACTN1,0(6)          CATALOG ?
         BE    CATCHK                YES
         CLC   ACTN2,0(6)          UNCATLG ?
         BE    UNCATCHK              YES
         CLC   ACTN3,0(6)          LIST GENERATIONS?
         BE    ALLCHK                YES
CAT3     DS    0H
         MVI   CATSW,C'3'
LOOKUP   DS    0H
         LOCATE INDGX
         CH    15,=H'0'
         BE    CODEOK              DSNAME FOUND ON CATALOG
         CLI   CATSW,C'4'
         BE    UNCATMSG
         CLI   CATSW,C'2'
         BL    CATLOK              NOT FOUND ON CATALOG
         B     LOOKERR
CODEOK   DS    0H
         CLI   CATSW,C'2'
         BE    UNCATOK             OK TO UNCATALOG
         BL    CATERR              ALREADY ON CATALOG
PUTNAME  DS    0H
         MVC   TEXTOUT(L'MSG2),MSG2
LAINST   LA    0,(L'MSG2+15)
         BAL   9,PRTIT
         LH    7,BLOCK             BINARY VOL CNT TO R7
         L     10,=A(BLOCK+6)
LOOP     DS    0H
         CH    7,=H'0'
         BE    CHKOTHR
         MVC   VOLSER,0(10)
         MVC   TEXTOUT(L'MSG3),MSG3
         LA    0,(L'MSG3+15)
         BAL   9,PRTIT
         SH    7,=H'1'
         A     10,TWELVE
         B     LOOP
CATCHK   DS    0H
         MVI   CATSW,C'1'
         B     LOOKUP
UNCATCHK DS    0H
         MVI   CATSW,C'2'
         B     LOOKUP
*
ALLCHK   MVI   CATSW,C'5'
         ZAP   GDSNBR,=P'0'            INITIALIZE FOR GENERATION 0
         S     6,ONE
         A     4,FIVE
CHKOTHR  CLI   CATSW,C'5'
         BNE   START
         UNPK  GDSREL,GDSNBR
         OC    GDSREL+1,=X'F0'
         MVC   0(5,6),GDSMASK
         AP    GDSNBR,=P'1'
         EX    4,MOVE
         B     LOOKUP
CATERR   MVC   TEXTOUT(L'MSG4),MSG4
         LA    0,(L'MSG4+15)
         BAL   9,PRTIT
         MVI   CATSW,C'3'
         B     PUTNAME
LOOKERR  CP    GDSNBR,PTWO
         BE    LOOKER2
         MVI   CATSW,C'0'
         B     START
LOOKER2  MVC   TEXTOUT(L'MSG1),MSG1
         LA    0,(L'MSG1+15)
         BAL   9,PRTIT
         B     START
*
UNCATOK  DS    0H
         BAL   8,ILLEGAL
         CATALOG REMOVE
         CH    15,=H'0'
         BNE   UNCATERR
         MVI   CATSW,C'4'
         B     LOOKUP
UNCATERR MVC   TEXTOUT(L'MSG5),MSG5
         LA    0,(L'MSG5+15)
         BAL   9,PRTIT
         B     START
UNCATMSG MVC   TEXTOUT(L'MSG10),MSG10
         LA    0,(L'MSG10+15)
         BAL   9,PRTIT
         B     START
*
ILLEGAL  DS    0H
         LA    1,TEXTIN
         CLC   TEXTIN(5),=C'SYS1.'
         BE    TCHTCH
         CLC   TEXTIN(6),=C'PUBLIC'
         BE    TCHTCH
         BR    8
TCHTCH   MVC   TEXTOUT(L'MSG13),MSG13
         LA    0,(L'MSG13+15)
         BAL   9,PRTIT
         B     START
*
CATLOK   DS    0H                   BUMP TO UNIT
         A     6,FOUR
         CLC   CTAPE,0(6)          **  BLM 05/21/79  **
         BE    TAPE                **  BLM 05/21/79  **
         CLC   C2314,0(6)
         BE    DSK14
         CLC   CDISK,0(6)          **  BLM 05/21/79  **
         BE    DSK30
         MVC   TEXTOUT(L'MSG6),MSG6
         LA    0,(L'MSG6+15)
         BAL   9,PRTIT
         B     START
TAPE     DS    0H                  **  BLM 05/21/79  **
         MVC   UNIT,UTAPE          **  BLM 05/21/79  **
         B     SERIALCK
DSK14    DS    0H
         MVC   UNIT,U2314
         B     SERIALCK
DSK30    DS    0H
         MVC   UNIT,UDISK           **  BLM 05/21/79 **
SERIALCK DS    0H
         A     6,FIVE
         CLC   BLANKS,0(6)
         BE    NOSER
         MVC   SERIAL,0(6)
         CATALOG ADD
         CH    15,=H'0'
         BE    CAT3                VERIFY
         MVC   TEXTOUT(L'MSG7),MSG7
         LA    0,(L'MSG7+15)
         BAL   9,PRTIT
RETRN    RETURN (14,12),RC=0
MOVE     MVC   CAMDSN(0),0(5)
NOSER    DS    0H
         MVC   TEXTOUT(L'MSG9),MSG9
         LA    0,(L'MSG9+15)
         BAL   9,PRTIT
         B     START
PRTIT    DS    0H                  TPUT ROUTINE
         LA    1,MSGOUT
PRTIT2   TPUT  (1),(0)
         LTR   15,15
         BNZ   TPUTERR
         BR    9
TPUTERR  MVC   TEXTOUT,MSG15
         TPUT  MSGOUT,33
         B     START
*
MSGOUT   DS    0CL65
         DC    CL15'               '
TEXTOUT  DS    CL51
TEXTIN   DS    CL44
         DC    X'6B'
MSG1     DC    CL20'DSNAME NOT CATALOGED'
MSG2     DS    0CL51
INITMSG  DC    CL7'DSNAME='
CAMDSN   DC    CL44' '
MSG3     DS    0CL14
         DC    CL8'VOL=SER='
VOLSER   DS    CL6
MSG4     DC    CL24'DSNAME ALREADY CATALOGED'
MSG5     DC    CL15'UNCATALOG ERROR'
MSG6     DC    CL38'INVALID UNIT, ENTER TAPE OR DISK' ** BLM 05/21/79
MSG7     DC    CL13'CATALOG ERROR'
MSG8     DC    CL15'XXXXXXXXXXX     '
MSG9     DC    CL21'INVALID SERIAL NUMBER'
MSG10    DC    CL19'DATASET UNCATALOGED'
MSG11    DC    CL17'DATASET SCRATCHED'
MSG12    DC    CL13'SCRATCH ERROR'
MSG13    DC    CL14'ILLEGAL DSNAME'
MSG14    DC    CL27'HIT RETURN KEY TO TERMINATE'
MSG15    DC    CL18'TPUT ERROR - RETRY'
*
         DS    0D
UPPER    DS    0CL256
         DC    64X'00'
         DC    X'40000000000000000000004B004D4E00'     40 - 4F
         DC    X'00000000000000000000005B005D0000'     50 - 5F
         DC    X'60000000000000000000006B00000000'     60 - 6F
         DC    X'00000000000000000000007B00000000'     70 - 7F
         DC    X'00C1C2C3C4C5C6C7C8C9000000000000'     80 - 8F
         DC    X'00D1D2D3D4D5D6D7D8D9000000000000'     90 - 9F
         DC    X'00E1E2E3E4E5E6E7E8E9000000000000'     A0 - AF
         DC    16X'00'                                 B0 - BF
         DC    X'00C1C2C3C4C5C6C7C8C9000000000000'     C0 - CF
         DC    X'00D1D2D3D4D5D6D7D8D9000000000000'     D0 - DF
         DC    X'00E1E2E3E4E5E6E7E8E9000000000000'     E0 - EF
         DC    X'F0F1F2F3F4F5F6F7F8F9000000000000'     F0 - FF
COMMA    DS    0CL256
         DC    107X'00'
         DC    X'6B'
         DC    148X'00'
*
ONE      DC    F'1'
FOUR     DC    F'4'
FIVE     DC    F'5'
TWELVE   DC    F'12'
*
BLOCK    DS    0D
         DS    265C
INDGX    CAMLST NAME,CAMDSN,,BLOCK
REMOVE   CAMLST UNCAT,CAMDSN
ADD      CAMLST CAT,CAMDSN,,VOLUME
SCRATCAM CAMLST SCRATCH,CAMDSN,,VOLUME,,OVRD
         DS    0H
VOLUME   DC    H'1'                ONE VOLUME
UNIT     DS    CL4                 UNIT
SERIAL   DS    CL6                 SERIAL NBR
         DC    H'0'
U2314    DC    X'30C02008'
UTAPE    DC    X'32008003'            **  BLM  05/21/79 **
UDISK    DC    X'3050200D'            **  BLM  05/21/79 **
CTAPE    DC    CL4'TAPE'              **  BLM  05/21/79 **
C2314    DC    CL4'2314'
CDISK    DC    CL4'DISK'              **  BLM  05/21/79 **
CATSW    DS    C
ACTN1    DC    CL3'CAT'
ACTN2    DC    CL5'UNCAT'
ACTN3    DC    CL3'ALL'
BLANKS   DC    CL6'      '
GDSMASK  DS    0CL5
         DC    CL2'(-'
GDSREL   DS    CL2
         DC    C')'
GDSNBR   DS    PL2
PTWO     DC    PL2'01'
         END
