WTOEXIT2 CSECT
*
*   TEST CV ---
*
***      THIS MODULE IS DYNAMICALLY LOADED BY (WTOEXIT) TO
***      EXAMINE IDMS-CV'S MESSAGE TO THE CONSOLE OPERATOR
***      FOR A 'DISK JOURNAL FULL' CONDITION.  IF THE MESSAGE
***      IS A 'DISK JOURNAL FULL' CONDITION, THIS PROGRAM WILL
***      DYNAMICALLY ASSIGN AN INTERNAL READER TO AUTOMATICALLY
***      START THE IDMSAJNL JOB TO DUMP THE FULL DISK JOURNAL
***
***   FJP/03DEC80 ---
***      .  CHANGED JOBNAME TO $OPS1IDM & DEPT# TO 8202
***
*
R0       EQU        0
R1       EQU        1
R2       EQU        2
R3       EQU        3
R4       EQU        4
R5       EQU        5
R6       EQU        6
R7       EQU        7
R8       EQU        8
R9       EQU        9
R10      EQU        10
R11      EQU        11
R12      EQU        12
R13      EQU        13
R14      EQU        14
R15      EQU        15
         USING      *,R12              ASSIGN REG 12 AS BASE
         LR         R12,R15            INIT BASE REG
         ST         R1,WTOADDRS        SAVE WTO ADDRESS
         CLC        MSG2012(6),9(R1)   IS IT MESSAGE 202012?
         BE         ASGRDR
         CLC        MSG5002(6),9(R1)   IS IT MESSAGE 205002?
         BE         ASGRDR
         CLC        MSG5003(6),9(R1)   IS IT MESSAGE 205003?
         BE         ASGRDR
         B          GOBACK             NOT RIGHT MESSAGE, RETURN
*
**  DYNAMICALLY ALLOCATE INTERNAL READER
*
ASGRDR   LA    R1,RBPTR                 ALLOCATE PARM LIST
         DYNALLOC                       ALLOCATE READER
         LTR   R15,R15                  ALLOCATE SUCCESSFUL
         BNZ   ERROR                    ALLOCATE FAILED
         MVC   INTRDR+40(8),ALTXU4+6    MOVE DDNAME TO ACB
*
         OPEN  (INTRDR,(OUTPUT))        OPEN INTRDR TO SUBMIT JOB
         TM    INTRDR+48,X'10'          OPEN SUCCESSFUL?
         BZ    ERROR
         MVC   OUTBUF(80),JOBCRD        MOVE JOB CARD
         PUT   RPL=JCLFILE
         MVC   OUTBUF(80),EXECRD        MOVE EXEC CARD
         PUT   RPL=JCLFILE
         MVC   OUTBUF(80),STRTJOB       MOVE START JOB CARD
         PUT   RPL=JCLFILE
         MVC   OUTBUF(80),SLASHES       MOVE SLASHES
         ENDREQ RPL=JCLFILE
         CLOSE INTRDR                   CLOSE & FREE INTERNAL READER
         L     R1,WTOADDRS              LOAD WTO MSG ADDRESS
         CLC   MSG2012(6),9(R1)
         BE    MOVMSG
         MVC   44(23,R1),BLNKS
MOVMSG   MVC   66(44,R1),SUBMSG         MOVE SUBMITTED MESSAGE
GOBACK   L     R13,4(R13)
         LM    R14,R12,12(R13)          RESTORE REGISTERS
         XR    R15,R15
         BR    14
*
ERROR    L     R1,WTOADDRS
         MVC   80(50,R1),ERRMSG
         MVC   136(2,R1),HLDMSG         SET DESC-CODE SO MESSAGE DOES
*                                       NOT ROLL OFF OPERATOR CONSOLE
         B     GOBACK
*
**  ACB FOR INTERNAL READER
*
INTRDR   ACB   MACRF=(ADR,SEQ,OUT)
*
**  RPL FOR INTERNAL READER
*
JCLFILE  RPL ACB=INTRDR,AREA=OUTBUF,RECLEN=80,OPTCD=(ADR,SEQ,SYN,NUP)
*
**  STORAGE AND MESSAGES
*
WTOADDRS DS    1F
OUTBUF   DS    CL80
HLDMSG   DC    XL2'2000'
BLNKS    DC    CL23' '
MSG2012  DC    CL6'202012'
MSG5002  DC    CL6'205002'
MSG5003  DC    CL6'205003'
JOBCRD   DC    CL80'//$OPS1IDM JOB (8202,0000),''OPERATIONS'',CLASS=Z,MX
               SGCLASS=S'
EXECRD   DC    CL80'//STEP01  EXEC   PGM=IEFBR14'
STRTJOB  DC    CL80'// S IDMSAJNL '
SLASHES  DC    CL80'//  '
*
**  MESSAGE TEXTS
*
ERRMSG   DC    CL50'**  INTRDR ALLOC FAILED - DO A ''S IDMSAJNL''  ** '
SUBMSG   DC    CL44'****  IDMSAJNL SUBMITTED AUTOMATICALLY  ****'
*
**  DYNAMIC ALLOCATION PARM LIST
*
RBPTR    DS    0F
         DC    AL1(S99RBPND)            REQUEST BLOCK POINTERS
         DC    AL3(ALRB)                REQUEST BLOCK ADDRESS
*
ALRB     DS    0F
         DC    AL1(S99RBEND-S99RB)      LENGTH
         DC    AL1(S99VRBAL)            ALLOCATION
         DC    AL2(0)                   FLAGS
         DC    AL2(0)                   RESERVED
         DC    AL2(0)                   REASON CODE
         DC    A(ALTXPTRS)              TEXT POINTER ADDR
         DC    A(0)                     RESERVED
         DC    A(0)                     FLAGS
*
ALTXPTRS DS    0F
         DC    A(ALTXU1)                TEXT UNIT
         DC    A(ALTXU2)                TEXT UNIT
         DC    A(ALTXU3)                TEXT UNIT
         DC    AL1(S99TUPLN)            LAST POINTER
         DC    AL3(ALTXU4)              TEXT UNIT
*
ALTXU1   DS    0F
         DC    AL2(DALSYSOU)            SYSOUT SPEC
         DC    AL2(1)                   NO. OF PARMS
         DC    AL2(1)                   LENGTH
         DC    CL1'A'                   SYSOUT CLASS
*
ALTXU2   DS    0F
         DC    AL2(DALSPGNM)            SYSOUT PGM NAME
         DC    AL2(1)                   NO. OF PARMS
         DC    AL2(6)                   LENGTH
         DC    CL6'INTRDR'              INTERNAL READER
*
ALTXU3   DS    0F
         DC    AL2(DALCLOSE)            FREE=CLOSE
         DC    AL2(0)                   NO. OF PARMS
*
ALTXU4   DS    0F
         DC    AL2(DALRTDDN)            ALLOCATED DDNAME
         DC    AL2(1)                   NO. OF PARMS
         DC    AL2(8)                   LENGTH
         DC    CL8'        '            DDNAME
         IEFZB4D0
         IEFZB4D2
         END
