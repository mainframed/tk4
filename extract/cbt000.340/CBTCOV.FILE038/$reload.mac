         TITLE 'ARCHIVAL REQUEST FOR RELOAD'
$RELOAD  START 0
***********************************************************************
*   WRITTEN BY G. ZOLLWEG 6/10/76
*
*    THIS CODE IS A MODEL ONLY!!  IT IS NOT SUPPORTED BY CSG.
*    HOWEVER, WE AT CSG USE THIS COMMAND SUCCESSFULLY ON AN MVS
*    SYSTEM.
*    I WILL ANSWER ANY QUESTIONS YOU MAY HAVE CONCERNING ITS USE
*    AND WOULD BE INTERESTED IN ANY BUGS OR PROBLEMS THAT MAY POP
*    UP SO THAT I MAY PASS THEM ALONG TO OTHER USERS.
*
*   FJP/22JUL80 ---
*      . CHANGED DEPARTMENT NUMBER IN JOB CARD FROM 8220 TO 8202
*      . CHANGED CLASS IN JOB CARD FROM V TO Z
*   FJP/03DEC80 ---
*      . CHANGED JOBNAME FROM $RELOAD TO $OPS1RLD
*
***********************************************************************
* FUNCTION
*    THIS MODULE SUBMITS A BACKGROUND JOB TO CLASS U FOR THE
*    PURPOSE OF DOING AN ASM2 $RL OR $RC.  IT DYNAMICALLY ALLOCATES
*    AN INTERNAL READER FOR THIS.  A MESSAGE IS SENT TO THE USER
*    TELLING HIM IF THE RESTORE WAS SUCCESSFUL OR NOT.
*
*        THE DYNAMIC ALLOCATION MAY FAIL IF ALL THE INTERNAL READERS
*    IN THE SYSTEM ARE BEING USED. DARC CODE IS 214
* ENTRY POINTS -
*         $RELOAD
* INPUT
*    REGISTER 1 POINTS TO THE CPPL WHICH HAS A POINTER TO THE COMMAND
*    BUFFER.
* OUTPUT
*    BATCH JOB SUBMITTED TO THE SYSTEM.
*    MESSAGE TO THE USER SAYING THAT THE SUBMIT WAS OK.
* EXTERNAL REFERENCES - NONE
* EXITS,NORMAL - RETURN TO CALLER
* EXITS,ERROR - ABEND U100
* TABLES/WORK AREAS -
*   DSECTS NEEDED - IKJPSCB,IKJPPL
* ATTRIBUTES - NONE
* CHARACTER CODE DEPENDENCY - NONE
* NOTES - $RECOVER IS AN ALIAS FOR $RELOAD.
***********************************************************************
         EJECT
         SAVE  (14,12)
         BALR  R12,0
         USING *,R12
         LA    R3,SAVE
         ST    R3,8(R13)
         ST    R13,SAVE+4
         LA    R13,SAVE
         SPACE 2
*******************************************************
*        SAVE ADDRESS OF THE CPPL                     *
*              COMMAND PROCESSOR PARMATER LIST        *
*******************************************************
         SPACE 2
         LR    R11,R1                  SAVE ADDRESS OF CPPL
         USING CPPL,R11
         SPACE 2
*******************************************************
*        PARSE THE COMMAND                            *
*******************************************************
         SPACE 2
         LA    R1,PARMLIST         ESTABLISH ADDRESSABILITY
         USING PPL,R1                   FOR PARSE PARAMETER LIST
         MVC   PPLUPT,CPPLUPT      BUILD PPL BY FILLING IN ADDRESSES
         MVC   PPLECT,CPPLECT           OF THE UPT, ECT,
         LA    R2,ECB                          ECB,
         ST    R2,PPLECB
         MVC   PPLPCL,APCL                     PCL,
         LA    R2,ADDRPDL                      ANSWER AREA,
         ST    R2,PPLANS
         MVC   PPLCBUF,CPPLCBUF                AND COMMAND BUFFER
*                                  NOTHING IS PUT IN PPLUWA
*
         MVC   DAPLUPT,PPLUPT        SET UP DAIR PARAM LIST
         MVC   DAPLECT,PPLECT
         MVC   DAPLPSCB,CPPLPSCB
*
*******************************************************
*        PARSE THE COMMAND                            *
*******************************************************
*
         LINK EP=IKJPARS
         DROP  R1
         LTR   R15,R15             TEST RETURN CODE FROM PARSE
         BZ    PARSGOOD            IF ZERO, CONTINUE
         CH    R15,C12             IF 4 OR 8, TERMINATE
         BL    EXIT01
         LA    R1,100(R15)         IF 12, 16, OR 20, ABEND WITH
         ABEND (R1),DUMP                CODE 112, 116, OR 120
         SPACE 5
PARSGOOD DS    0H
         L     R9,ADDRPDL
         USING IKJPARMD,R9
         SPACE 2
*******************************************************
*        OBTAIN USER - ID                             *
*******************************************************
         SPACE 2
         L     R2,CPPLPSCB             GET ADDRESS OF THE PSCB
         USING PSCB,R2
         MVC   USER(7),PSCBUSER        SAVE USER-ID
         SR    R10,R10
         IC    R10,PSCBUSER+7          LENGHT OF USERID
         BCTR  R10,0
         LA    R1,DAPL                @ OF DAIR PARAM LIST
*******************************************************
*        ALLOCATE AN INTERNAL READER                  *
*******************************************************
CALLDAIR EQU   *
         LINK EP=IKJDAIR
         LTR   R15,R15
         BZ    OPENIT
         CH    R15,C12
         BNE   ABEND
         LH    R15,DA30DARC          DAIR ERROR CODE
         CVD   R15,DOUB
         UNPK  DAIRMSG+38(4),DOUB    CONVERT TO CHAR
         OI    DAIRMSG+41,X'F0'      SET ZONE
         TPUT  DAIRMSG,L'DAIRMSG
         B     EXIT01
ABEND    EQU   *
         ABEND 100,DUMP
         DROP  R2
OPENIT   OPEN     (BQUECD,OUTPUT)
X        DS      0H
         LA    0,L1
         PUT   BQUECD,(0)              WRITE LINE 1
         LA    0,L2
         PUT   BQUECD,(0)              WRITE LINE 2
         LA    0,L5
         PUT   BQUECD,(0)              WRITE LINE 5
         LA    0,L6
         PUT   BQUECD,(0)              WRITE LINE 6
         LA    0,L7
         PUT   BQUECD,(0)              WRITE LINE 7
         LA    R7,PNAME
USERREQ  EQU   *
         MVC   L8+4(76),BLANKS        BLANK OUT LINE
         SR    R5,R5
         LA    R5,74
         LA    R8,L8
         L     R4,CPPLCBUF            @ COMMAND BUFFER
         CLC   4(8,R4),=C'$RECOVER'   RECOVER COMMAND?
         BNE   BUMP
         MVC   ACPTMSG(7),=C'RECOVER'
         MVI   2(R8),C'C'              MAKE IT THE $RC COMMAND
BUMP     LA    R8,4(R8)                BUMP POINTER
         MVI   SAMEDSN,X'00'        RESET FLAG
RELOAD   EQU   *
         L     R14,0(R7)            PTR TO DSNAME
         CLI   0(R14),X'7D'        DSN QUOTED?
         BE    QUOTED                 YES
         MVI   0(R8),X'7D'             MOVE IN STARTING QUOTE
         LA    R8,1(R8)
         SR    R5,R10
         BNP   NEWLINE
         EX    R10,USERID              MOVE IN USERID
         LA    R8,1(R10,R8)             INCR INTO LINE
         S     R5,=F'2'
         BNP   NEWLINE
         MVC   0(1,R8),=C'.'            MOVE IN PERIOD
         LA    R8,1(R8)
QUOTED   LH    R3,4(0,R7)                 LOAD LENGTH
         L     R2,0(0,R7)
         CH    R3,=H'44'
         BH    LENERROR
         SR    R5,R3                     MINUS LENGHT OF DSN
         BNP   NEWLINE
         MVI   SAMEDSN,X'00'          RESET FLAG
         BCTR  R3,0                      SUBTRACT 1
         EX    R3,MOVENME
         LA    R8,0(R3,R8)               ADDR OF FINAL '
         L     R14,0(R7)            PTR TO DSNAME
         CLI   0(R14),X'7D'        DSN QUOTED?
         BE    QUOTE2                 YES
         LA    R8,1(R8)
         MVI   0(R8),C''''            MOVE IN ENDING '''
QUOTE2   EQU   *
         TM    8(R7),X'FF'           MORE NAMES IN LIST?
         BO    PUTCARD                NO
         OI    SAMEDSN,X'01'         SET FLAG
         LA    R8,1(R8)               ADDR OF COMMA
         MVI   0(R8),C','              MOVE IN COMMA
         LR    R1,R8                   SAVE PTR TO COMMA
         L     R7,8(R7)               PTR TO NEXT DSNAME
         LA    R8,1(R8)                BUMP PTR PASSED COMMA
         S     R5,=F'3'
         BNP   NEWLINE
         B     RELOAD
NEWLINE  LA    R8,L8+79
         SR    R8,R1                   LENGHT OF FIELD TO BE BLANKED
         EX    R8,MOVBLNKS             BLANK OUT FIELD
PUTCARD  LA    0,L8
         PUT   BQUECD,(0)              WRITE LINE 8
         TM    SAMEDSN,X'01'           ALREADY HAVE A NAME?
         BO    USERREQ                 YES
         TM    8(R7),X'FF'
         BNO   USERREQ                 MORE TO GO
         LA    R8,L9+29
         EX    R10,USERID              MOVE IN USERID
         LA    R8,1(R10,R8)
         MVC   0(2,R8),=X'7D6B'         MOVE IN ENDING ',
         LA    0,L9
         PUT   BQUECD,(0)              WRITE LINE 9
         LA    0,L10
         PUT   BQUECD,(0)              WRITE LINE 10
         LA    R8,L11+29
         EX    R10,USERID
         LA    R8,1(R10,R8)
         MVC   0(2,R8),=X'7D6B'         MOVE IN ENDING ',
         LA    0,L11
         PUT   BQUECD,(0)              WRITE LINE 11
         LA    0,L12
         PUT   BQUECD,(0)              WRITE LINE 12
         LA    0,L13
         PUT   BQUECD,(0)              WRITE LINE 13
         LA    0,L14
         PUT   BQUECD,(0)              WRITE LINE 14
EF       LA    0,L15
         PUT   BQUECD,(0)              WRITE LINE 15
         CLOSE (BQUECD)
         LA    R1,DAPB18
         ST    R1,DAPLDAPB
         XC    DAPLECB,DAPLECB
         LA    R1,DAPL
*******************************************************
*        FREE THE INTERNAL READER                     *
*******************************************************
         LINK EP=IKJDAIR
ACCEPTIT DS    0H
         LA    1,ACPTMSG
         LA    0,ACPTMSGL
         TPUT  (1),(0),R
         EJECT
**********************************************
*     END OF JOB                             *
**********************************************
*
*
EXIT01   DS    0H
         L     R13,SAVE+4
         LM    R14,R12,12(R13)               RESTORE TMP'S REGS
         SR    R15,R15                       SET RETCD TO ZERO
         BR    R14                           RETURN TO TMP
LENERROR DS    0H
         LA    1,LENTHER
         LA    0,LENTHERL
         TPUT  (1),(0),R
         B     EXIT01
*
MOVENME  MVC   0(0,R8),0(R2)             MOVE IN DATASET NAME
MOVBLNKS MVC   0(0,R1),BLANKS            MOVE IN DATASET NAME
USERID   MVC   0(0,R8),USER        MOVE IN USERID
*
         TITLE 'CONSTANTS'
C12      DC    H'12'
*
DOUB     DS    D
APCL     DC    A(PCL)
         DS    0F
SAMEDSN  DC    X'00'
BLANKS   DC    CL76' '
LENTHER  DC    C'DATASET LENGTH EXCEEDS 44 CHARACTERS'
LENTHERL EQU   L'LENTHER
ACPTMSG  DC    C'RELOAD  REQUEST ACCEPTED'
ACPTMSGL EQU   L'ACPTMSG
         DS    0F
         EJECT
L1    DC   CL80'//$OPS1RLD JOB (8202,0000),''BACKGROUND RELOAD'','
L2       DC   CL80'//    CLASS=Z,MSGCLASS=S'
L5       DC   CL80'//RETRIEVE EXEC ASM2RETU'
L6       DC   CL80'//*     RELOAD  DATASET FROM ARCHIVE  OR     '
L7       DC   CL80'//*     BACKUP  TAPES             '
L8       DC   CL80'$RL'
L9       DC   CL80'//AOK     EXEC PGM=AOK,PARM='''
L10      DC   CL80'//        COND=(4,LT,RETRIEVE.ASM2RETU)'
L11      DC   CL80'//BAD     EXEC PGM=BAD,PARM='''
L12      DC   CL80'//        COND=(3,GT,RETRIEVE.ASM2RETU)'
L13      DC   CL80'/*'
L14      DC   CL80'//'
L15      DC   CL80'/*EOF'
DAIRMSG  DC    C'ERROR IN DAIR ROUTINE - DARC CODE IS -     '
         EJECT
         DS   0F
DAPL     DS   0CL20
DAPLUPT  DC   F'0'
DAPLECT  DC   F'0'
DAPLECB  DC   F'0'
DAPLPSCB DC   F'0'
DAPLDAPB DC   A(DAPB30)
         EJECT
DAPB30   DS    0CL84
***********************************************************************
* THIS OPERATION CODE CAUSES ALLOCATION OF A SYSOUT DATASET.          *
***********************************************************************
DA30CD   DC    X'0030'  DAIR ENTRY CODE
DA30FLG  DC    X'0000'  FUNCTIONS TO BE PERFORMED WHEN RET CODE IS 0
DA30DARC DS    H        DYN ALLOC RETURN CODE
DA30CTRC DS    H        CATALOG RETURN CODE
DA30PDSN DC    F'0'      POINTER TO DSNAME TO BE SEARCHED IN DSE
DA30DDN  DC    CL8'JES2RDR' DDNAME TO BE SEARCHED IN DSE
DA30UNIT DC    CL8' '   UNITNAME REQUESTED
DA30SER  DC    CL8' '   VOLUME SERIAL NUMBER-PADDED W/BLANKS
DA30BLK  DC    F'0'     DATA SET  AVERAGE RECORD LENGTH
DA30PQTY DC    F'0'     PRIMARY SPACE QUANTITY
DA30SQTY DC    F'0'     SECONDARY SPACE QUANTITY
DA30PGNM DC    CL8'INTRDR'  INTERAL READER
DA30FORM DC    CL4' '   FORMS
DA300CLS DC    CL2' A'
RESERVED DC    X'00'
DA30CTL  DC    X'08'    PERMANENTLY ALLOCATE DATASET
DA30ALN  DS    CL8      ATTR-LIST-NAME                           C99236
*
         EJECT
***********************************************************************
* THIS OPERATION CODE CAUSES FREEING OF A DATASET.                    *
***********************************************************************
DAPB18   DS    0CL40
DA18CD   DC    X'0018'  DAIR ENTRY CODE
DA18FLG  DS    X        FUNCTIONS TO BE PERFORMED WHEN RET CODE IS 0
         DS    X
DA18DARC DS    H        DYNAMIC ALLOCATION RETURN CODE
DA18CTRC DS    H        CATALOG RETURN CODE AREA
DA18PDSN DC    F'0'     POINTER TO DSNAME TO BE SEARCHED IN DSE
DA18DDN  DC    CL8'JES2RDR'   DDNAME TO BE SEARCHED IN DSE
DA18MNM  DC    CL8' '   MEMBER NAME
DA18SCLS DC    CL2'A '  SYSOUT CLASS DESIRED WHEN UNALLOCATING  A
DA18DPS2 DC    X'00'    DATA SET DISPOSITION
DA18CTL  DC    X'10'    FLAGS FOR SPECIAL DAIR PROCESSING
DA18JBNM DC    CL8' '   IGNORED AS OF OS VS/2 RELEASE 2       Y02670
*
         EJECT
BQUECD   DCB   DDNAME=JES2RDR,DSORG=PS,MACRF=(PM),RECFM=F,LRECL=80,    X
               BLKSIZE=80,BUFNO=1,OPTCD=C,DEVD=DA
*
SAVE     DS    18F       SAVE AREA
*
PARMLIST DS    7F        PPL OR DAPL
*
ECB      DS    F'0'      COMMAND PROCESSOR ECB
*
ADDRPDL  DS    A         ANSWER AREA FOR PARSE - ADDRESS OF PDL
*
USER     DS    CL8                     USER REQUESTING LISTC
         TITLE 'TSO MACROS'
PCL      IKJPARM
PNAME    IKJIDENT 'DSNAME',PROMPT='DATASET TO BE RESTORED',LIST,       X
               HELP='ENTER DSLIST',FIRST=ANY,OTHER=ANY
         IKJENDP
         EJECT
         IKJPSCB
         IKJCPPL
         EJECT
         IKJPPL
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
