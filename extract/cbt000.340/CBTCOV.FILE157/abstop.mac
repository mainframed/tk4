ABSTOP   CSECT
         EXTRN IBMBOCLA
         SPACE 3
***********************************************************************
*                                                                     *
*        THIS ROUTINE IS CALLED FROM A PLI PROGRAM TO ABEND (WITHOUT  *
*        A DUMP) THE CURRENT STEP (NOT JUST TASK) WITH A USER         *
*        COMPLETION CODE OF 0747.  BEFORE ISSUING THE ABEND, IT       *
*        GOES TO IBMBOCLD TO CLOSE ALL OPEN FILES.                    *
*        THIS IS THE ENTRY POINT TO THE PL/I IMPLICIT CLOSE MODULE    *
*                                                                     *
***********************************************************************
         SPACE 4
***********************************************************************
*        EQUATES.                                                     *
***********************************************************************
R1       EQU   1               REGISTER 1.  PARAMETER REGISTER.
RA       EQU   10              REGISTER 10.  BASE REGISTER.
RC       EQU   12              REGISTER 12.  PSEUDO REGISTER.
RE       EQU   14              REGISTER 14.  RETURN REGISTER.
RF       EQU   15              REGISTER 15.  GENERAL FLUNKIE.
MSGCODE  EQU   69                  OFFSET TO ABEND CODE IN MSG.
***********************************************************************
         SPACE 4
***********************************************************************
*        SHORT HOUSE-KEEPING.                                         *
***********************************************************************
START    EQU   *
         USING *,RF            TEMPORARY ADDRESSABILITY.
         B     BEGIN           BRANCH AROUND ID.
         DC    AL1(6)
         DC    CL6'ABSTOP'
BEGIN    DS    0H
         LR    RA,RF           PRIME BASE REGISTER.
         DROP  RF
         USING START,RA        TELL ASSEMBLER OF CHOICE.
*        USE CALLING PROGRAMS SAVE AREA FOR CALL TO IBMBOCLD.
***********************************************************************
         EJECT
***********************************************************************
*        PROCESS PARM INFORMATION, IF PRESENT
***********************************************************************
         L     RF,0(,R1)           GET PARM OPTION
         LTR   R1,R1               IS THERE ANY PARM?
         BZ    U747                NO PARM PRESENT, ISSUE U747
         L     RF,0(,RF)           GET STRING ADDR.
         MVC   WTO1+MSGCODE(4),0(RF) GET NEW ABEND CODE
         OI    3(RF),X'F0'         SET TO ALL NUMERIC
         PACK  DBLWORD+5(3),0(4,RF)     SET USER ABEND
         CVB   RF,DBLWORD
***********************************************************************
         EJECT
***********************************************************************
*        CLOSE ALL OPEN FILES.                                        *
***********************************************************************
         ST    RF,SAVE         SAVE REG. 15
U747     EQU   *
         LR    R1,RC           POINT TO PSEUDO REGISTER VECTOR.
         L     RF,=V(IBMBOCLD) POINT TO CLOSE ROUTINE.
         BALR  RE,RF           GO CLOSE THEM FILES.
***********************************************************************
         SPACE 4
***********************************************************************
*        TELL PROGRAMMER WHO ABENDED HIM AND THEN ABEND HIM (OR HER). *
***********************************************************************
         STAE  0               DISABLE PLI'S STAE.
         WTO   MF=(E,WTO1)     TELL PROGRAMMER.
         L     R1,SAVE         LOAD PARAMETER INTO REG. 1
         ABEND (R1)            CRASH THIS TASK, NOT WHOLE STEP (IMS)
***********************************************************************
         SPACE 4
***********************************************************************
DBLWORD  DC    D'0'
         LTORG
WTO1     WTO   'IZD001I PROGRAM ABNORMALLY TERMINATED BY CALL TO ABSTOP.
                (USER CC=0747).',MF=L,ROUTCDE=11
SAVE     DC    F'0747'   SAVE AREA FOR REG. 15
         DC    C'THIS VERSION OF ABSTOP FOR IMS/NONIMS PROGRAMS '
         DC    CL9'&SYSDATE'
         DC    CL8'&SYSTIME'
         END
