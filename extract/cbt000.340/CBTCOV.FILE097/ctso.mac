CTSO     TITLE 'ISSUE TSO COMMANDS WHILE IN QUEUE COMMAND'        ONL01
CTSO     QSTART ,                  START CTSO CSECT ASSEMBLY      ONL01
         USING WORK,R13       BASE FOR WORK AREA
         L     R10,QVCKPT     BASE FOR CKTP AREA
         USING QCKPT,R10      ADDRESSING FOR AREA
         L     R9,QVDSPL      BASE FOR DISPLAY AREA
         USING QDISPLAY,R9    ADDRESSING FOR AREA
         LA    R8,WCPPL       BASE FOR CPPL AREA
         USING CPPL,R8        ADDRESSING FOR AREA
         OC    QLNG1,QLNG1    TEST LENGTH OF COMMAND
         BZ    NOCMD          NONE, ABORT
         MVC   QDHLINE,QBLANK BLANK HEADING LINE
         TPUT  CLEAR,L'CLEAR,FULLSCR,MF=(E,QTPUT)  CLEAR THE SCREEN
*        STLINENO LINE=1,MODE=OFF  TURN OFF FULL SCREEN MODE
         MVC   WMODEL(WMODELEN),MODEL  COPY MODEL AREA TO DSECT AREA
         MVC   WBLDLFF,=AL2(1)  1 ENTRY IN LIST
         MVC   WBLDLLL,=AL2(12) 12 BYTES TO BE RETURNED
         L     R1,DAPLECT     POINT TO ECT
         USING ECT,R1         SET TEMP ADDRESSING
         MVC   QDWORD,ECTPCMD SAVE PRIMARY COMMAND NAME
         MVC   QDWORK,ECTSCMD  AND SECONDARY COMMAND NAME
         DROP  R1             DROP TEMP ADDRESSING
         SPACE 1
         MVC   CPPLUPT,DAPLUPT   COPY UPT POINTER
         MVC   CPPLPSCB,DAPLPSCB COPY PSCB POINTER
         MVC   CPPLECT,DAPLECT   COPY ECT POINTER
         SPACE 1
         LA    R15,QDREPLY    POINT TO COMMAND LINE
         AH    R15,QOFF1      POINT TO 1ST PARM (AFTER "TSO")
         SH    R15,=H'4'      BACK UP FOR BUFFER HEADER
         USING CMDBUF,R15     TEMP ADDRESSING FOR BUFFER
         LH    R14,QDRLNG     LENGTH OF REPLY
         SH    R14,QOFF1      - OFFSET TO OPERAND = TEXT LENGTH
         LA    R14,4(,R14)    + HEADER
         STH   R14,CMDLEN     SAVE AS LENGTH IN BUFFER
SCAN     ST    R15,CPPLCBUF   SET BUFFER CB POINTER
         XC    CMDOFF,CMDOFF  CLEAR OFFSET TO SECOND OPERAND
         DROP  R15            DROP TEMP ADDRESSING
         LA    R1,WCSPL       POINT TO IKJSCAN PARM LIST
         USING CSPL,R1        SET TEMP ADDRESSING
         L     R2,DAPLUPT     POINT TO UPT
         ST    R2,CSPLUPT
         L     R2,DAPLECT     POINT TO ECT
         ST    R2,CSPLECT
         LA    R2,WTCBECB     POINT TO ECB
         MVI   0(R2),0          CLEAR ECB
         ST    R2,CSPLECB
         LA    R2,WTCBADDR    WORD FOR FLAGS
         MVI   0(R2),0          CLEAR FLAGS
         ST    R2,CSPLFLG
         LA    R2,WCSOA       POINT TO OUTPUT AREA
         ST    R2,CSPLOA
         ST    R15,CSPLCBUF   COMMAND BUFFER ADDRESS TO CSPL
         DROP  R1             DROP TEMP ADDRESSING
         CALLTSSR EP=IKJSCAN  SCAN INPUT BUFFER
         LA    R1,WCSOA       POINT TO OUTPUT AREA
         USING CSOA,R1        SET TEMP ADDRESSING
         L     R14,CSOACNM    POINTER TO COMMAND NAME
         ICM   R15,3,CSOALNM  LENGTH OF NAME
         BZ    NOCMD2         NONE, SKIP REST
         BCTR  R15,0          DROP FOR EXECUTE
         DROP  R1             DROP TEMP ADDRESSING
         MVC   WBLDLNAM,QBLANK FILL WITH BLANKS
         EX    R15,MVCCMD      MOVE COMMAND TO WBLDLNAM
         CLC   =C'EX',QSUBNAME   IMPLICIT EXEC OF CLIST?
         BNE   NOTEXEC           NO, SKIP THIS
SETEXEC  NI    QSUBNAME,255-X'40'  DROP TO LOWER CASE
         MVC   WBLDLNAM,=CL8'EXEC' SET MODULE NAME TO ATTACH
         L     R1,CPPLCBUF       POINT TO BUFFER
         USING CMDBUF,R1         TEMP ADDRESSING
         XC    CMDOFF,CMDOFF     CLEAR OFFSET FOR EXEC
         B     OKATTACH       AND GO DO IT
         DROP  R1                DROP TEMP ADDRESSING
NOTEXEC  DS    0H
         BLDL  0,WBLDLPRM     CHECK FOR MODULE PRESENT
         LTR   R15,R15        CHECK RETURN CODE
         BNZ   SETEXEC        NONE, MUST BE CLIST
         SPACE 1
OKATTACH L     R1,DAPLECT     POINT TO ECT
         USING ECT,R1         SET TEMP ADDRESSING
         MVC   ECTPCMD,WBLDLNAM FAKE PRIMARY COMMAND NAME
         MVC   ECTSCMD,QBLANK AND SECONDARY COMMAND NAME
         DROP  R1             DROP TEMP ADDRESSING
         SPACE 1
         MVI   WTCBECB,0      CLEAR ECB
         LA    R1,WCPPL       CPPL PTR FOR COMMAND
ATTACH   DS    0H
         ATTACH EPLOC=WBLDLNAM,ECB=WTCBECB,                            +
               MF=(1,(1)),SF=(E,WATTL)
         ST    R1,WTCBADDR    SAVE TCB ADDR
WAIT     DS    0H
         WAIT  ECB=WTCBECB    WAIT
DETACH   DS    0H
         DETACH WTCBADDR
         SPACE 1
NOCMD2   DS    0H
         ICM   R1,15,WGTPB+4  ADDRESS OF GETLINE BUFFER
         BZ    NOFREE         NONO, SKIP FREEMAIN
         LH    R0,0(R1)       LENGTH OF BUFFER
         ICM   R0,B'1000',=X'01'  SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)  FREE THE BUFFER
         SPACE 1
NOFREE   L     R1,DAPLECT     POINT TO ECT
         USING ECT,R1         TEMP ADDRESSING
         L     R1,ECTIOWA     --> I/O WORK AREA
         DROP  R1             DROP TEMP ADDRESSING
         L     R1,0(R1)       --> TO ELEMENT ON STACK
         TM    0(R1),X'40'    FROM STORAGE (CLIST)?
         BZ    DONE           NO, DONE WITH PROCESSING
         LA    R1,WIOPL       ADDRESS OF IO PARM LIST
         L     R2,CPPLUPT     ADDRESS OF UPT
         L     R3,CPPLECT     ADDRESS OF ECT
         LA    R4,WTCBECB     ADDRESS OF ECB
         MVI   WTCBECB,0      CLEAR ECB
         GETLINE PARM=WGTPB,UPT=(R2),ECT=(R3),ECB=(R4),MF=(E,(1))
         CH    R15,=H'16'     END OF INPUT?
         BE    DONE           YES, CLEAN UP AND RETURN
         LA    R1,WGTPB       POINT TO GETLINE PARM LIST
         USING GTPB,R1        TEMP ADDRESSING
         L     R15,GTPBIBUF   POINT TO INPUT BUFFER
         DROP  R1             DROP TEMP ADDRESSING
         B     SCAN           AND GO TO SCAN PROCESSING
         SPACE 1
DONE     L     R1,DAPLECT     POINT TO ECT
         USING ECT,R1         SET TEMP ADDRESSING
         MVC   ECTPCMD,QDWORD RESTORE PRIMARY COMMAND NAME
         MVC   ECTSCMD,QDWORK  AND SECONDARY COMMAND NAME
         DROP  R1             DROP TEMP ADDRESSING
TPUT     DS    0H
*        TPUT  DONEMSG,L'DONEMSG  ASK FOR ENTER WHEN DONE
TGET     DS    0H
*        TGET  QDWORD,L'QDWORD,EDIT,MF=(E,QTGET)  READ RESPONCE
*        STFSMODE ON          RESTORE FULL SCREEN MODE
QSTOP    DS    0H
         QSTOP
         SPACE 1
NOCMD    QTILT '*** NO COMMAND SPECIFIED ***'
         SPACE 1
MVCCMD   MVC   WBLDLNAM(*-*),0(R14) MOVE COMMAND TO WBLDLNAM
         SPACE 1
DONEMSG  DC    C'*** PRESS ENTER TO RETURN TO QUEUE COMMAND ***'
CLEAR1   EQU   *
*        DC    X'27F5C1
         DC    X'C1'               FOR SPF/TCAM                   UF041
         DC    X'115D7E'
         DC    X'114040'
         DC    X'3C404000'
         DC    X'1DC8'
         DC    X'13'
CLEAR    EQU   CLEAR1,*-CLEAR1
         SPACE 1
MODEL    DS    0D
MGTPB    GETLINE MF=L
MATTL    ATTACH SHSPV=78,     NEEDED TO PREVENT S305 ABENDS            +
               SF=L
MODELEN  EQU   *-MODEL        LENGTH OF MODEL AREA
         SPACE 1
         LTORG
         SPACE  1
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
WORK     DSECT ,              .
         DS    18F            SAVE AREA
         DS    0D
WMODEL   DS    0D             START OF MODEL AREA
WGTPB    GETLINE MF=L
WATTL    ATTACH  SF=L
WMODELEN EQU   *-WMODEL       CHECK FOR SAME LENGTH.
         SPACE 1
WTCBADDR DC    A(0)           ADDRESS OF CREATED TASK
WTCBECB  DC    A(0)           COMPLETION CONTROL BLOCK.
WCPPL    DS    4F             SPACE FOR CPPL TO BE PASSED TO CMD
WPARML   DS    12F            SPACE FOR PARM LISTS
WIOPL    EQU   WPARML,16      IO PARM LIST FOR GETLINE
WCSPL    EQU   WPARML,24      PARM LIST FOR IKJSCAN
WCSOA    EQU   WPARML+24,8    OUTPUT AREA FROM IKJSCAN
         SPACE 1
         DS    0F
WBLDLPRM DS    XL16           WORK AREA FOR BLDL
WBLDLFF  EQU   WBLDLPRM,2     NUMBER OF ENTRIES IN LIST
WBLDLLL  EQU   WBLDLPRM+2,2   LENGTH OF EACH ENTRY
WBLDLNAM EQU   WBLDLPRM+4,8   MEMBER NAME
WBLDLTTR EQU   WBLDLPRM+12,3  TTR OF START
WBLDLK   EQU   WBLDLPRM+15,1  CONCATENATION NUMBER
         SPACE 1
WORKLEN  EQU   *-WORK         LENGTH OF WORK AREA
         SPACE 1
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         IKJCPPL  ,
         IKJCSOA  ,
         IKJCSPL  ,
         IKJECT   ,
         IKJGTPB  ,
         CVT   DSECT=YES
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
         SPACE 1
CMDBUF   DSECT
CMDLEN   DC    H'0'           LENGTH, INCLUDES HEADER (+4)
CMDOFF   DC    H'0'           OFFSET TO NONBLANK PAST COMMAND.
CMDTEXT  DC    C' '           FIRST TEXT BYTE.
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
