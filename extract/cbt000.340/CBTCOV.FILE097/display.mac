DISPLAY  TITLE 'QUEUE COMMAND - 3270 DISPLAY ROUTINES'            ONL01
DISPLAY  QSTART ,                  START DISPLAY CSECT ASSEMBLY   ONL01
******************************************************************BFG05
* BFG CHANGES:                                                    BFG05
*     (1) BFG05 - ADD SUPPORT TO ALLOW BOTH JES2 COMMANDS AND     BFG05
*                 OS COMMANDS TO BE ISSUED FOR AUTHORIZED USERS   BFG05
*                 JES2 COMMANDS START WITH A '$' AND OS COMMANDS  BFG05
*                 MUST BE PRECEEDED WITH A '.'                    BFG05
*     (2) BFG06 - ADD SUPPORT TO DISPLAY THE CURRENT CPU PERCENT  BFG06
*                 BUSY AND THE DEMAND PAGE RATE TO UPPER RIGHT    BFG06
*                 CORNER OF THE DISPLAY                           BFG06
*     (3) BFG07 - HANDLE ANOTHER COMMAND DURING CONSOLE PROCESSINGBFG07
*     (4) *ISPF*- SUPPORT ISPF ENVIRONMENT VIA THE TABLE SERVICES*ISPF*
*     (5) BFG09 - HANDLE ENTRY AND EXIT FROM KEY=0                BFG09
*     (6) BFG16 - CHANGE SUPPORT FOR JES2 COMMANDS SO THAT ONLY   BFG16
*                 COMMANDS THAT START WITH THE CURRENT COMCHR     BFG16
*                 ARE ALLOWED. STILL ACCEPT ANY COMMAND WITH      BFG16
*                 A '.'.                                          BFG16
*     (7) BFG18 - CHANGE CPU AND DPR FOR XA                       BFG18
*     (8) BFG20 - ACCEPT A 'Y' AS ACKNOWLEDGEMENT FOR RESPONSE TO BFG20
*                 ANY DISPLAY FROM THE "SYSOUT" MODULE            BFG20
******************************************************************BFG05
***********************************************************************
* RNB CHANGES:                                                        *
*     (1) RNB13 - MODIFICATIONS TO FIX PROBLEMS WITH TCAM FULL-SCREEN *
*                 PROCESSING OF TEST-REQUEST, SYSTEM REQUEST, AND THE *
*                 PA2/PA3 KEYS.                                       *
*     (2) RNB14 - MODIFICATIONS TO FIX PROBLEMS FULL-SCREEN           *
*                 PROCESSING. THIS ONE ALLOWS THE USER TO TYPE IN THE *
*                 TOP COMMAND LINE WITHOUT QUEUE MISINTERPRETING WHAT *
*                 WAS ENTERED. (TRY THE DO COMMAND FROM BOTH PLACES   *
*                 WITHOUT THE MOD TO SEE THE EFFECT.)                 *
***********************************************************************
         GBLB  &QPFK          PF-KEY OPTION. DEFINED BY QSTART     ICBC
         GBLB  &QNERDC                                            UF040
         USING QDISPLAY,R10   BASE REG FOR DISPLAY WORK AREA
         L     R10,QVDSPL     ADDR OF DISPLAY WORK AREA
         USING QCPRINT,R9     BASE REG FOR PRINT   WORK AREA       FCI*
         L     R9,QVPRINT     ADDR OF PRINT   WORK AREA            FCI*
***********************************************************************
*                                                                     *
*   CHECK FOR ROOM ON SCREEN                                          *
*                                                                     *
***********************************************************************
         MVI   QDOVER,0       ZERO OUT THE PAGE OVERFLOW INDICATOR
         MVI   QDPLUS,C' '    BLANK THE OVERFLOW INDICATOR
         LH    R4,QDMLNG      LOAD MSG LENGTH
         CH    R4,QDSCRLEN    IS THE MSG LENGTH > SCRSIZE?        UF003
         BH    RETURN         YES. GO AWAY.
         MVC   QPRSAVE,QDTLINE        SAVE SUBTITLE LINE ON ENTRY  FCI*
         LTR   R4,R4          IS MSG LENGTH ZERO?
         BZ    WRTSCR         YES. WRITE SCREEN.
         TM    QFLAG1,QFLG1ISP ARE WE IN ISPF??                  *ISPF*
         BO    Q5ISPF1       YES SO BUILD SCREEN IN ISPF         *ISPF*
         MVI   QDPLUS,C'+'    INDICATE SCREEN OVERFLOW
         AH    R4,QDNEXT      ADD CURRENT LOCATION ON SCREEN
         CH    R4,QDSCRLEN    IS THERE ROOM ON THE SCREEN?        UF003
         BH    WRTSCR         NO. WRITE SCREEN.
***********************************************************************
*                                                                     *
*   MOVE THE MESSAGE TO THE SCREEN                                    *
*                                                                     *
***********************************************************************
DSP2     LH    R7,QDMLNG      LOAD MESSAGE LENGTH
         LTR   R7,R7          IS MESSAGE LENGTH ZERO?
         BZ    RETURN         YES. RETURN.
         LH    R4,QDNEXT      LOAD CURRENT SCREEN LINE NUMBER
         LR    R1,R4          SAVE LINE NUMBER
         LA    R4,QDLINE1(R4) LOAD ADDRESS OF NEXT LINE
         L     R6,QDMSGA      LOAD ADDR OF MESSAGE
         LH    R5,QDLNELEN    LENGTH MUST BE MULTIPLE OF LINESIZE UF003
DSP3     CR    R5,R7          IS 5 NOT LESS THAN 7?
         BNL   DSP4           YES. GO DO IT.
         AH    R5,QDLNELEN    INCR BY LINE LENGTH                 UF003
         B     DSP3           TRY AGAIN
DSP4     AR    R1,R5          UPDATE LINE NUMBER
         STH   R1,QDNEXT      STORE LINE NUMBER
         MVCL  R4,R6          MOVE THE MESSAGE TO THE SCREEN
***********************************************************************
*                                                                     *
*   RETURN TO CALLER                                                  *
*                                                                     *
***********************************************************************
RETURN   QSTOP
******************************************************************ISPF*
*                                                                *ISPF*
*   MOVE THE MESSAGE TO THE SCREEN       ISPF MODE               *ISPF*
*                                                                *ISPF*
******************************************************************ISPF*
Q5ISPF1  DS    0H                                                *ISPF*
         L     R6,QDMSGA           POINT AT LINES                *ISPF*
         CH    R4,=H'80'           IS ONLY LINE < 80             *ISPF*
         BNL   DSP3A               NOPE                          *ISPF*
         BCTR  R4,0                GET MACHINE LENGTH            *ISPF*
         MVC   QDLINE1,QBLANK      BLANK OUT LINE                *ISPF*
         EX    R4,DSP5             MOVE IN LINE                  *ISPF*
         L     R15,ISPLNK          GET ADDRESS OF 'ISPLINK'      *ISPF*
         CALL  (15),(TBADD,TBNAM),VL, PUT IN TABLE               *ISPF*X
               MF=(E,TBLST)                                      *ISPF*
         B     RETURN              EXIT                          *ISPF*
DSP5     MVC   QDLINE1(0),0(R6)    **EXECUTED ONLY**             *ISPF*
DSP3A    MVC   QDLINE1,0(R6)  MOVE IN LINE OF DISPLAY            *ISPF*
         L     R15,ISPLNK          GET ADDRESS OF 'ISPLINK'      *ISPF*
         CALL  (15),(TBADD,TBNAM),VL, PUT IN TABLE               *ISPF*X
               MF=(E,TBLST)                                      *ISPF*
         LA    R6,80(R6)      POINT TO NEXT ONE                  *ISPF*
         SH    R4,=H'80'      COUNT DOWN LENGTH                  *ISPF*
         CH    R4,=H'80'      SEE IF LINE LEFT                   *ISPF*
         BNL   DSP3A          IF SO THEN LOOP                    *ISPF*
         B     RETURN         ALL DONE SO EXIT                   *ISPF*
***********************************************************************
*                                                                     *
*   WRITE A FULL SCREEN, WAIT FOR REPLY      NORMAL MODE         *ISPF*
*                                                                     *
***********************************************************************
WRTSCR   TM    QFLAG1,QFLG1ISP  ARE WE IN ISPF???                *ISFP*
         BO    Q5ISPF2       YES SO PROCESS PROPERLY             *ISPF*
         LA    R1,QDLINE1     ENSURE PRINTABILITY                 UF003
         LH    R15,QDSCRLEN   LENGTH TO XLATE                     UF003
         LA    R14,255        SET FOR EXECUTE                     UF003
WRTSCR1  CR    R15,R14        SEE IF ONLY ONE NEEDED              UF003
         BNH   WRTSCRN        YES, DO IT                          UF003
         EX    R14,WRTSCRTR   TRANSLATE PART OF BUFFER            UF003
         LA    R1,1(R1,R14)   POINT TO NEXT SLOT                  UF003
         SR    R15,R14        DROP FOR LENGTH DONE                UF003
         BCTR  R15,0          END FOR EXECUTE FIX                 UF003
         B     WRTSCR1        LOOP TILL DONE                      UF003
         SPACE 1                                                  UF003
WRTSCRTR TR    0(*-*,R1),TABLE  FIX UNPRINTABLES                  UF003
         SPACE 1                                                  UF003
WRTSCRN  BCTR  R15,0          DROP FOR EXECUTE                    UF003
         LTR   R15,R15        TEST FOR NULL                       UF003
         BM    *+8            SKIP IF NULL                        UF003
         EX    R15,WRTSCRTR   TRANSLATE END OF BUFFER             UF003
         SPACE 1                                                  UF003
TPUTSCRN DS    0H                                                 UF003
         TM    QFLAG1,QFLG1ISP  ARE WE IN ISPF?????              *ISPF*
         BO    Q5ISPF2          YES GO REPOST ISPF SCREEN        *ISPF*
         MVC   QATTRIB(2),=X'1DF0'    MOVE IN HEADER STUFF        BFG06
         MVC   QCONST(2),=C'C='                                   BFG06
         MVC   QDONST(3),=C' D='                                  BFG06
         L     R1,16          GET CVT ADDRESS                     BFG18
         TM    QFLAG1,QFLG1XA         IS THIS MVS-XA              BFG18
         BNO   NOXA1                  IF NOT THEN SKIP            BFG18
         L     R15,=A(TOXA1+X'80000000') GET MODE                 BFG18
         DC    X'0B0F'                BSM  0,15                   BFG18
TOXA1    DS    0H                                                 BFG18
         L     R1,604(R1)             LOAD ADDR OF RMCT           BFG18
         L     R1,228(R1)             LOAD ADDR OF RCT            BFG18
         A     R1,=A(56-64)   OFFSET FOR XA                       BFG18
         B     YXA1                                               BFG18
NOXA1    DS    0H                                                 BFG18
         L     R1,604(R1)             LOAD ADDR OF RMCT           BFG18
         L     R1,228(R1)             LOAD ADDR OF RCT            BFG18
YXA1     DS    0H                                                 BFG18
         LH    R15,64(R1)             LOAD RCTCPUA FIELD          BFG06
         SRL   R15,4                  GET VALUE                   BFG06
         CVD   R15,QDOUBLE                                        BFG06
         UNPK  QCPU(3),QDOUBLE(8)     FORMAT CPU BUSY PERCENT     BFG06
         OI    QCPU+2,X'F0'                                       BFG06
         LH    R15,74(R1)                                         BFG06
         CVD   R15,QDOUBLE                                        BFG06
         UNPK  QDPR(3),QDOUBLE(8)     FORMAT DEMAND PAGE RATE     BFG06
         OI    QDPR+2,X'F0'                                       BFG06
         TM    QFLAG1,QFLG1XA         IS THIS MVS-XA              BFG18
         BNO   NOXA2                  IF NOT THEN SKIP            BFG18
         LA    R15,NOXA2              GET MODE                    BFG18
         DC    X'0B0F'                BSM  0,15                   BFG18
NOXA2    DS    0H                                                 BFG18
         LH    R0,QDSCRPLN    LOAD LENGTH FOR TPUT                UF003
         TPUT  QDSCREEN,(0),FULLSCR,MF=(E,QTPUT)                  UF003
         MVC   QDTLINE,QPRSAVE        RESTORE SUBTITLE INFO        FCI*
         AIF  (&QPFK).PFK1    SKIP NON-PFK CODE                    ICBC
         TGET  QDREPLY,63,EDIT,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?
         BNE   NOCLEAR        NO. CONTINUE.
         TCLEARQ INPUT        CLEAR THE QUEUE
NOCLEAR  STH   R1,QDRLNG      STORE LENGTH OF REPLY
         AGO   .PFK2                                               ICBC
.PFK1    ANOP                                                      ICBC
         LA    R6,QDREPLY                                          ICBC
         XC    PFREPLY,PFREPLY                                     ICBC
         XC    QDREPLY,QDREPLY                                     ICBC
         TGET  PFREPLY,69,ASIS,MF=(E,QTGET)
         CH    R15,=H'12'     IS INPUT LONGER THAN BUFFER?         ICBC
         BNE   NOCLEAR        NO. CONTINUE.                        ICBC
         TCLEARQ INPUT        CLEAR THE QUEUE                      ICBC
NOCLEAR  IC    R4,PFCODE                                           ICBC
         XR    R5,R5                                               ICBC
         CLI   PFCODE,X'F0'      TEST-REQ/SYS-REQ?                RNB13
         BE    ENTKEY            /YES - TREAT AS ENTER            RNB13
         CLI   PFCODE,X'01'      OTHER KIND OF SYS-REQ?           RNB13
         BE    ENTKEY            /YES - TREAT AS ENTER            RNB13
         CLI   PFCODE,X'6E'      PA2?                             RNB13
         BE    TPUTSCRN          /YES - GO RESHOW SCREEN          RNB13
         CLI   PFCODE,X'6B'      PA3?  (TCAM GENERATED)           RNB13
         BE    TPUTSCRN          /YES - GO RESHOW SCREEN          RNB13
         N     R4,=X'0000000F'   EXTRACT PF-KEY NUMBER             ICBC
         CH    R4,=H'12'                                           ICBC
         BH    ENTKEY           "ENTER" KEY                        ICBC
         BCTR  R4,0                                                ICBC
         SLL   R4,3              TIMES SIZE OF AN ENTRY           UF043
         LA    R5,QDPFKTAB                                        UF043
         LA    R5,0(R4,R5)                                         ICBC
         MVC   QDREPLY(8),0(R5)  MOVE PF-KEY VALUE                UF043
         LA    R5,8                                               UF043
         LA    R6,6(,R6)                                          UF043
         SH    R1,=H'3'          ALLOW USER TO INPUT ON EITHER    UF043
         BZ    NOTXT             THE TOP OR BOTTOM COMMAND LINE   RNB14
         MVC   0(57,R6),PFTXT                                     UF043
         LA    R5,6                                               UF043
         SH    R1,=H'3'                                           UF043
         B     NOTXT                                              RNB14
ENTKEY   EQU   *                                                   ICBC
         SH    R1,=H'3'                                            ICBC
         BZ    NOTXT                                               ICBC
         MVC   0(63,R6),PFTXT                                     RNB14
         SH    R1,=H'3'                                            ICBC
NOTXT    LA    R1,0(R5,R1)                                         ICBC
         STH   R1,QDRLNG      STORE LENGTH OF REPLY                ICBC
.PFK2    ANOP                                                      ICBC
PFK2     OC    QDREPLY,QBLANK     UPPERCASE THE COMMAND     FCI* *ISPF*
         CLI   QCOMCHR,C' '        ANY COMCHR?(SUBSYS VALID?)     BFG16
         BE    NOJES               NO - SO SKIP NEXT TEST         BFG16
         CLC   QCOMCHR,QDREPLY     DOES COMCHR MATCH              BFG16
         BE    JES2CMD             IF SO THEN ISSUE               BFG16
NOJES    DS    0H                                                 BFG16
         CLI   QDREPLY,C'.'       IS IT AN OS COMMAND             BFG05
         BE    JES2CMD            YES, PROCESS IT                 BFG05
         CLC   QDREPLY(2),=CL2'PR' POSSIBLE PRINT COMMAND      PWF FCI*
         BNE   NOTPRT             NOPE..TRY ACK OF SYSOUT REQ     BFG20
*                                                                  FCI*
         L     R15,=V(PRINT)  FETCH PRINT ENTRY ADDRESS            FCI*
         BALR  R14,R15        AND CALL HIM                         FCI*
         LTR   R15,R15        HOW IS HIS RETURN CODE               FCI*
         BNZ   CLSCRN         NOTHING TO REPORT                    FCI*
*                                                                  FCI*
         B     TPUTSCRN       GO REPOST SCREEN                     FCI*
NOTPRT   DS    0H                                                 BFG20
         CLC   QSUBCMD,=V(SYSOUT) ARE WE IN SYSOUT                BFG20
         BNE   NOTACK         IF NOT THEN SKIP                    BFG20
         CLC   QDRLNG,=H'1'   WAS RESPONSE A 1                    BFG20
         BNE   NOTACK         NOPE.. SO SKIP                      BFG20
         CLI   QDREPLY,C'Y'   POSITIVE ACK                        BFG20
         BNE   NOTACK         NOPE..SO SKIP                       BFG20
         XC    QDRLNG,QDRLNG  ZERO REPLY LEN AS POS ACK           BFG20
         B     CLSCRN         EXIT                                BFG20
NOTACK   DS    0H                                                 BFG20
*                                                                 BFG20
**       CLEAR SCREEN IMAGE AND EITHER DISPLAY MORE               BFG20
**       OR GO TO THE PARSER WITH A NEW COMMAND                   BFG20
*                                                                 BFG20
CLSCRN   TM    QFLAG1,QFLG1ISP  ARE WE IN ISPF?????              *ISPF*
         BZ    CLSCRNN        NO GO DO NORMAL PROCESSING         *ISPF*
         LH    R1,QDRLNG      STORE LENGTH OF REPLY              *ISPF*
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?    *ISPF*
         BNZ   INTER          YES. INTERRUPT PROCESSING.         *ISPF*
         B     RETURN              ALL DONE                      *ISPF*
*                                                                *ISPF*
CLSCRNN  LA    R4,QDLINE1     LOAD ADDRESS OF FIRST LINE         *ISPF*
         LH    R5,QDSCRLEN    LOAD LENGTH OF SCREEN               UF003
         SR    R6,R6          NO SENDING FIELD NEEDED              FCI*
         STH   R6,QDNEXT      STORE ZERO IN LINE NUMBER            FCI*
         SR    R7,R7          FILL SCREEN WITH NULLS               FCI*
         MVCL  R4,R6          CLEAR THE SCREEN                     FCI*
*
         LH    R1,QDRLNG      STORE LENGTH OF REPLY
         LTR   R1,R1          WAS THERE A RESPONSE FROM USER?
         BNZ   INTER          YES. INTERRUPT PROCESSING.
         MVI   QDOVER,1       INDICATE PAGE OVERFLOW
         B     DSP2           CONTINUE PROCESSING
INTER    DS    0H                                                 BFG07
*                                                                 BFG07
**       DO AXSET AND ESAR IF SP3 DESIRED                         BFG07
*                                                                 BFG07
         TM    QFLAG2,QFLG2AXS     WAS  AXSET DONE                BFG07
         BZ    NSP33               IF NOT SKIP                    BFG07
         QAUTHCK QAKEY0            GET INTO KEY 0  (IF AXSET WAS  BFG09
*                                  DONE THEN WE ARE AUTHORIZED)   BFG09
         AXSET AX=AXV              RESET AUTH                     BFG07
         NI    QFLAG2,255-QFLG2AXS TURN OFF THE FLAG              BFG07
*                                                                 BFG07
**       GET OUT OF KEY ZERO                                      BFG07
*                                                                 BFG07
         QAUTHCK QAKEYP       GET BACK INTO PROBLEM STATE         BFG09
NSP33    DS    0H                                                 BFG07
         L     R13,QFRSTSA    GO BACK TO MAIN MODULE
         LM    R14,R12,12(R13) RESTORE REGISTERS FROM FIRST SAVEAREA
         BR    R10            ADDRESS OF INTERRUPT HANDLER IN QUEUE
         EJECT
******************************************************************BFG05
*                                                                 BFG05
*   JES2CMDS THRU THE INTERNAL READER                             BFG05
*                                                                 BFG05
******************************************************************BFG05
JES2CMD  EQU   *                                                  BFG05
         TM    QAFLAGS,QAFXOPSP ARE WE OPER OR SYSPGM             BFG05
         BNO   TPUTSCRN       NO, DON'T PROCESS THE COMMAND       BFG05
         MVC   QINTCMD(80),QBLANK  BLANK OUT THE JES COMMAND AREA BFG05
         CLI   QDREPLY,C'.'   IS IT OS COMMAND                    BFG16
         BE    JES2CMD1       YES SO MOVE CORRECTLY               BFG16
         MVC   QINTCMD(63),QDREPLY PUT IN COMMAND FROM USER       BFG05
         B     JES2CMD2                                           BFG05
JES2CMD1 MVC   QINTCMD(62),QDREPLY+1 MOVE IN ALL BUT REST         BFG05
JES2CMD2 DS    0H                                                 BFG05
         QAUTHCK QAKEY0       GET INTO KEY=0                      BFG09
******************************************************************BFG05
*                                                                 BFG05
*   ISSUE COMMAND WITH SVC 34                                     BFG05
*                                                                 BFG05
******************************************************************BFG05
         LA    R1,QCMDR                                           BFG05
         SR    R0,R0               MARK FROM NO CONSOLE           BFG05
         SVC   34                  ISSUE COMMAND                  BFG05
******************************************************************BFG05
*                                                                 BFG05
*   RESET EXECUTION KEY                                           BFG05
*                                                                 BFG05
******************************************************************BFG05
         QAUTHCK QAKEYP       GET BACK INTO PROBLEM STATE         BFG09
         XC    QDRLNG,QDRLNG  ZERO REPLY LENGTH                   BFG05
         MVC   QDREPLY,QBLANK BLANK OUT THE FIELD                 BFG05
         B     TPUTSCRN       AND RE-DISPLAY THE SCREEN           BFG05
         EJECT                                                   *ISPF*
******************************************************************ISPF*
*                                                                *ISPF*
*   WRITE A FULL SCREEN, WAIT FOR REPLY      ISPF MODE           *ISPF*
*                                                                *ISPF*
******************************************************************ISPF*
Q5ISPF2  EQU   *                                                 *ISPF*
         L     R1,16          GET CVT ADDRESS                     BFG18
         TM    QFLAG1,QFLG1XA         IS THIS MVS-XA              BFG18
         BNO   NOXA3                  IF NOT THEN SKIP            BFG18
         L     R15,=A(TOXA3+X'80000000') GET MODE                 BFG18
         DC    X'0B0F'                BSM  0,15                   BFG18
TOXA3    DS    0H                                                 BFG18
         L     R1,604(R1)     LOAD ADDR OF RMCT (31 BIT)          BFG18
         L     R1,228(R1)     LOAD ADDR OF RCT (31 BIT)           BFG18
         A     R1,=A(56-64)   OFFSET FOR XA                       BFG18
         B     YXA3                                               BFG18
NOXA3    DS    0H                                                 BFG18
         L     R1,604(R1)             LOAD ADDR OF RMCT           BFG18
         L     R1,228(R1)             LOAD ADDR OF RCT            BFG18
YXA3     DS    0H                                                 BFG18
         LH    R15,64(R1)             LOAD RCTCPUA FIELD   BFG06 *ISPF*
         SRL   R15,4                  GET VALUE            BFG06 *ISPF*
         CVD   R15,QDOUBLE                                 BFG06 *ISPF*
         UNPK  QCPU(3),QDOUBLE(8)                          BFG06 *ISPF*
         OI    QCPU+2,X'F0'                                BFG06 *ISPF*
         LH    R15,74(R1)                                  BFG06 *ISPF*
         CVD   R15,QDOUBLE                                 BFG06 *ISPF*
         UNPK  QDPR(3),QDOUBLE(8)                          BFG06 *ISPF*
         OI    QDPR+2,X'F0'                                BFG06 *ISPF*
         TM    QFLAG1,QFLG1XA         IS THIS MVS-XA              BFG18
         BNO   NOXA4                  IF NOT THEN SKIP            BFG18
         LA    R15,NOXA4              GET MODE                    BFG18
         DC    X'0B0F'                BSM  0,15                   BFG18
NOXA4    DS    0H                                                 BFG18
         L     R4,ISPLNK     GET ADDRESS OF 'ISPLINK'            *ISPF*
         LR    R15,R4                                            *ISPF*
         CALL  (15),(TBTOP,TBNAM),VL,                            *ISPF*X
               MF=(E,TBLST)                                      *ISPF*
         LR    R15,R4                                            *ISPF*
         CALL  (15),(TBDISPL,TBNAM,TBPNL),VL,                    *ISPF*X
               MF=(E,TBLST)                                      *ISPF*
         CH    R15,=H'8'       WAS END OR RETURN COMMAND ENTERED *ISPF*
         BNE   ISPF1           NOPE                              *ISPF*
         MVC   QDREPLY,=C'END' YES SO FAKE UP END                *ISPF*
         MVC   QDRLNG,=H'3'    SET LENGTH                        *ISPF*
         B     ISPF2           SKIP TO PROCESS                   *ISPF*
******************************************************************ISPF*
*                                                                *ISPF*
*        COPY IN THE REPLY VARIABLE                              *ISPF*
*                                                                *ISPF*
******************************************************************ISPF*
ISPF1    DS    0H                                                *ISPF*
         MVC   VLEN3,=F'32'   SET MAXIMUM LENGTH                 *ISPF*
         LA    R3,QDREPLY                                        *ISPF*
         LR    R15,R4                                            *ISPF*
         CALL  (15),(VCOPY,V3,VLEN3,(3),VMOVE),VL,               *ISPF*X
               MF=(E,VCALLST)                                    *ISPF*
         LA    R6,QDREPLY          POINT AT REPLY                *ISPF*
         L     R5,VLEN3            GET LENGTH OF REPLY           *ISPF*
         STH   R5,QDRLNG           STORE LENGTH OF REPLY         *ISPF*
ISPF2    DS    0H                                                *ISPF*
         MVC   QDTLINE,QBLANK BLANK OUT ERROR LINE               *ISPF*
         B     PFK2            CONTINUE PROCESSING               *ISPF*
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
* TABLE OF PRINTABLE CHARACTERS
TABLE    DC    CL64' '
         DC    191AL1(*-TABLE)  REST OF TABLE IS OK               UF003
         DC    C' '              EXCEPT FOR X'FF'                 UF031
SYMDEL   DSECT ,                   KILL SYM CARD GENERATION       UF023
         QPRBGEN BEGIN             SET PRINT FOR CNTL BLOCK GEN   ONL01
         QCOMMON
         $HASPEQU
         QPRBGEN DONE              RESTORE NORMAL PRINT STATUS    ONL01
SYMNODEL DSECT ,                   RESTORE SYM CARD GENERATION    UF023
         END
