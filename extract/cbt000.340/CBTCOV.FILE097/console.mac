CONSOLE  TITLE 'QUEUE COMMAND - ECHO MVS CONSOLE ON USER SCREEN'  BFG07
CONSOLE  QSTART ,                   START CONSOEL CSECT ASSEMBLY  BFG07
******************************************************************BFG07
*                                                                 BFG07
*  THIS MODULE INCORPORATES BOTH THE ORIGINAL BFG CONSOLE AND     BFG07
*  THE UNIVERSITY OF AKRON ENHANCEMENTS                           BFG07
*                                                                 BFG07
* BFG CHANGES:                                                    BFG07
*    (1) ISPF  - SUPPORT ISPF INTERFACE                           BFG07
*    (2) BFG09 - USE AUTH MODULE TO GET INTO KEY 0 SUPERVISOR     BFG09
*                STATE AND GET BACK OUT                           BFG09
*    (3) BFG31 - SUPPORT XA2.2 AND HIGHER MOVEMENT OF RDCM        BFG31
******************************************************************BFG07
         USING QDISPLAY,R9,R10                                    BFG31
         L     R9,QVDSPL                                          BFG07
         LA    R10,2048(,R9)                                      BFG31
         LA    R10,2048(,R10)                                     BFG31
         USING QCPRINT,R8                                         BFG07
         L     R8,QVPRINT                                         BFG07
         MVC   QDHLINE,QBLANK        BLANK OUT THE HEADER LINE    BFG07
         TM    QFLAG1,QFLG1ISP ARE WE IN ISPF????                *ISPF*
         BO    QCISPF1        IF SO SKIP THE TOP MESSAGE         *ISPF*
         MVC   QDMSG(80),TOP         SET TOPLINE                  BFG07
         MVC   QDMLNG,=H'80'         SET THE LENGTH               BFG07
         LA    R0,QDMSG              GET THE ADDR                 BFG07
         ST    R0,QDMSGA             SET THE ADDR                 BFG07
         L     R15,=V(DISPLAY)                                    BFG07
         BALR  R14,R15               DISPLAY THE LINE             BFG07
QCISPF1  DS    0H                                                 BFG07
         QAUTHCK QAKEY0       TRY TO GET INTO KEY 0               BFG09
AUTHCODE EQU  *                                                   BFG07
*                                                                 BFG07
**       DO AXSET AND ESAR IF SP3 DESIRED                         BFG07
*                                                                 BFG07
         L     R7,CVTPTR             GET CVT ADDRESS              BFG07
         ICM   R1,15,X'0140'(R7)   SEE IF SP3                     BFG07
         BZ    NONSP3              IF NOT SKIP                    BFG07
         OI    QFLAG2,QFLG2AXS     MARK AXSET DONE                BFG07
         AXSET AX==H'1'            SET TO ALL AUTH                BFG07
         STH   R0,AXV              SAVE OLD AX VALUE              BFG07
         ESAR  R1                  GET OLD SASID                  BFG07
         STH   R1,SSAN             SAVE SECONDARY ASID            BFG07
NONSP3   DS    0H                                                 BFG07
         L     R7,CVTPTR             GET CVT ADDRESS              BFG07
         L     R5,CVTCUCB-CVT(R7)    GET CVTCUCB A(IEECUCB) CONSO BFG07
         USING UCM,R5              SETUP ADDR OF UCM              BFG07
***************************************************************** BFG07
*                                                                 BFG07
**       FOR SP3 DISPLAY THE MASTER CONSOLE SINCE                 BFG07
**       SINCE NO WQES EXIST FOR LINES ON IT                      BFG07
*                                                                 BFG07
***************************************************************** BFG07
         TM    QFLAG2,QFLG2AXS     IS THIS SP3 SYS (AXSET DONE?)  BFG07
         BZ    NSP34               IF NOT SKIP                    BFG07
         LR    R1,R5               GET UCM BASE                   BFG07
         SL    R1,=F'4'            GET PREFIX WORD                BFG07
         L     R1,0(R1)            GET MCS PREFIX ADDR            BFG07
         L     R7,UCMMCENT-UCMPRFX(R1) GET MASTER CONSOLE ENTRY   BFG07
         USING UCMLIST,R7          SETUP ADDR FOR UCME ENTRY      BFG07
         L     R3,UCMUCB           GET UCB PTR  FOR MSG           BFG31
         L     R2,UCMXB            POINT AT RDCM                  BFG07
         LTR   R2,R2               IS THIS A GRAPHICS DEVICE      BFG07
         BZ    NSP34               NOPE SO CANNOT DISPLAY         BFG07
         DROP  R7                                                 BFG07
         TM    QFLAG2,QFLG2X22 Q. ARE WE IN XA2.2                 BFG31
         BZ    NXA22          A. NOPE                             BFG31
         LH    R1,UCMCTID     SET TO CONSOLE ADDRESS SPACE        BFG31
         SSAR  R1             SET SECONDARY TO IT                 BFG31
         LA    R15,0          SET KEY OF 0                        BFG31
         LA    R1,4           GET LENGTH                          BFG31
         MVCP  MYWQE(R1),0(R2),R15 GET ADDR OF TDCM               BFG31
         L     R7,MYWQE                                           BFG31
         LH    R1,SSAN        GET NATIVE ASID                     BFG31
         SSAR  R1             SET SECONDARY TO IT                 BFG31
         B     YXA22                                              BFG31
NXA22    L     R7,0(R2)            POINT AT TDCM                  BFG07
YXA22    LTR   R7,R7               IS TDCM OPEN                   BFG07
         BZ    NSP34               NOPE SO CANNOT DISPLAY         BFG07
*                                                                 BFG07
**       SINCE WE NOW HAVE AN OPEN GRAPHICS CONSOLE PUT OUT MSG   BFG07
*                                                                 BFG07
         MVC   QDMSG(80),TPMSTR    SET TOP MESSAGE                BFG07
         MVC   QDMLNG,=H'80'       SET LENGTH                     BFG07
         MVC   QDMSG+47(3),13(R3)  MOVE IN DEV ADDR               BFG31
         BAL   R6,DISPLY           GO AND DISPLAY IT              BFG07
*                                                                 BFG07
**       NOW GO CROSS MEMORY AND STEAL SOME DATA                  BFG07
*                                                                 BFG07
         LH    R1,UCMCTID          SET TO CONSOLE ADDR SPACE ID   BFG07
         SSAR  R1                  SET SECONDARY TO CONSOLE       BFG07
         LA    R15,0               SET KEY TO 0                   BFG07
         LA    R1,4                SET LENGTH TO MOVE             BFG07
         MVCP  MYWQE(R1),DCMASCRN(R7),R15   GET ADDR OF SCREEN    BFG07
         L     R4,MYWQE            SET POINTER TO SCREEN          BFG07
         MVCP  MYWQE(R1),DCMMSGAL(R7),R15   GET NUMBER OF LINES   BFG07
         LH    R3,MYWQE            SET NUMBER OF LINES            BFG07
         MVCP  MYWQE(R1),DCMCORLN(R7),R15   GET NUMBER OF LINES   BFG07
         LH    R2,MYWQE            SET LENGTH OF EACH LINE        BFG07
MSTCNLP  DS    0H                                                 BFG07
         LH    R1,UCMCTID          SET TO CONSOLE ADDR SPACE ID   BFG07
         SSAR  R1                  SET SECONDARY TO CONSOLE       BFG07
         LA    R1,78               LET LENGTH                     BFG07
         SR    R15,R15             SETUP KEY                      BFG07
         MVCP  QDMSG(R1),0(R4),R15 MOVE IN LINE                   BFG07
         LH    R1,SSAN                                            BFG07
         SSAR  R1                  RESET SECONDARY ASID           BFG07
         CLC   QDMSG(20),QBLANK    IS THIS LINE BLANK             BFG07
         BE    NDMSTLP             IF SO THEN END OF LOOP         BFG07
         MVC   QDMLNG,=H'78'       SET LENGTH                     BFG07
         MVI   QDMSG,C'-'          SET SO IT DOES NOT SHIFT       BFG07
         BAL   R6,DISPLY           OUTPUT LINE                    BFG07
         AR    R4,R2               POINT TO NEXT LINE             BFG07
         BCT   R3,MSTCNLP          DO NEXT LINE                   BFG07
NDMSTLP  DS    0H                                                 BFG07
*                                                                 BFG07
**       OUTPUT LINE FOR END OF CONSOLE                           BFG07
*                                                                 BFG07
         MVC   QDMSG(80),BTMSTR    SET TOP MESSAGE                BFG07
         MVC   QDMLNG,=H'80'       SET LENGTH                     BFG07
         BAL   R6,DISPLY           GO AND DISPLAY IT              BFG07
NSP34    DS    0H                                                 BFG07
***************************************************************** BFG07
*                                                                 BFG07
**       GET ALL OUTSTANDING WQES AND DISPLAY THEM                BFG07
*                                                                 BFG07
***************************************************************** BFG07
         USING WQE,R7                ADDRESS WQE                  BFG07
         L     R7,UCMWTOQ            POINT AT FIRST WQE           BFG07
         MVC   QDMSG,QBLANK          BLANK OUT THE MESSAGE        BFG07
LOOP     EQU   *                                                  BFG07
         LA    R7,WQE                CLEAR TOP BYTE OF REGISTER   BFG07
         LTR   R7,R7                 DOES WQE EXIST               BFG07
         BZ    END                   NO, QUIT                     BFG07
         TM    QFLAG2,QFLG2AXS     WAS AXSET DONE??               BFG07
         BZ    NSP32               IF NOT THEN SKIP CODE          BFG07
         LH    R1,UCMCTID          SET TO CONSOLE ADDR SPACE ID   BFG07
         SSAR  R1                  SET SECONDARY TO CONSOLE       BFG07
         LA    R15,0               SET KEY TO 0                   BFG07
         LA    R1,WQESIZE          SET LENGTH TO MOVE             BFG31
         MVCP  MYWQE(R1),WQE,R15   MOVE IN WQE                    BFG07
         LH    R1,SSAN                                            BFG07
         SSAR  R1                  RESET SECONDARY ASID           BFG07
         LA    R7,MYWQE            POINT AT MY COPY               BFG07
         B     SP32                CONTINUE FOR SP3.X             BFG07
NSP32    LRA   R0,WQE                SEE IF STARING ADDR EXISTS   BFG07
         BNE   END                                                BFG07
         LRA   R0,WQE+WQESIZE-1      SEE IF ENDING ADDR EXISTS    BFG31
         BNE   END                                                BFG07
SP32     TM    WQEXA,WQEDOM          SEE IF MESSAGE IS VALID      BFG07
         BO    LOOP1                 NO, SKIP IT                  BFG07
         MVC   QDMSG(80),WQETS       MESSAGE TO SCREEN            BFG07
         MVC   QDMLNG,=H'80'         SET THE LENGTH               BFG07
         BAL   R6,DISPLY             GO DISPLAY IT                BFG07
         LRA   R0,WQE                SEE IF ADDR EXISTS           BFG07
         BNE   END                                                BFG07
         TM    WQEMCSF2,WQEMCSJ      MULTI-LINE WTO               BFG07
         BO    MLWTO                 YES, DO MLWTO PROCESSING     BFG07
         CLC   WQENBR,=F'62'         MESSAGE LONGER THAN 61?      BFG07
         BNH   LOOP1                 NO, KEEP ON TRUCKING         BFG07
         MVC   QDMSG(14),QBLANK      BLANK OUT THE FIRST PART     BFG07
         MVC   QDMSG+14(66),WQETS+80 MOVE REST OF MESSAGE TO SCRE BFG07
         MVC   QDMLNG,=H'80'         SET THE LENGTH               BFG07
         BAL   R6,DISPLY             GO DISPLAY IT                BFG07
LOOP1    EQU   *                                                  BFG07
         LRA   R0,WQE                SEE IF ADDR EXISTS           BFG07
         BNE   END                                                BFG07
         L     R7,WQELKP            GET NEXT MESSAGE FROM OS      BFG07
         B     LOOP                 AND GET THE NEXT LINE         BFG07
         USING WQEMAJ,R7            ADDRESS MAJOR WQE             BFG07
MLWTO    CLC   WMJMTXTL,=H'62'      MESSAGE LONGER THAN 61?       BFG07
         BNH   GETMINOR             NO, KEEP ON TRUCKING(AS THEY  BFG07
         MVC   QDMSG(80),QBLANK     BLANK OUT THE LINE            BFG07
         MVC   QDMSG+14(10),WMJMTS+80 MOVE REST OF MESSAGE TO SCR BFG07
         MVC   QDMLNG,=H'80'        SET THE LENGTH                BFG07
         BAL   R6,DISPLY            GO DISPLAY IT                 BFG07
GETMINOR LRA   R0,WQEMAJ            SEE IF ADDR EXISTS            BFG07
         BNE   END                                                BFG07
         USING WQEMIN,R3            ADDRESS MINOR WQE             BFG07
         L     R3,WMJMMIN           GET ADDR OF FIRST MINOR       BFG07
MINORLP  LA    R3,WQEMIN            ZERO UPPER BYTE               BFG07
         LTR   R3,R3                IS THERE ANOTHER MINOR        BFG07
         BZ    LOOP1                NO, GET NEXT MAJOR            BFG07
         TM    QFLAG2,QFLG2AXS      WAS AXSET DONE????            BFG07
         BZ    NSP35                NO, SKIP CODE                 BFG07
         LH    R1,UCMCTID           GET CONSOLE ASID              BFG07
         SSAR  R1                   SET SECONDARY TO CONSOLE      BFG07
         LA    R15,0                SET KEY TO ZERO               BFG07
         LA    R1,WMNMSIZE          SET LENGTH TO MOVE            BFG31
         MVCP  MYMINWQE(R1),WQEMIN,R15 MOVE IN MINOR WQE          BFG07
         LH    R1,SSAN              GET OLD SECONDARY ASID        BFG07
         SSAR  R1                   AND RESET IT BACK             BFG07
         LA    R3,MYMINWQE          POINT TO MY COPY              BFG07
         B     SP35                 CONTINUE FOR SP3.X            BFG07
NSP35    LRA   R0,WQEMIN            SEE IF STARTING ADDR EXISTS   BFG07
         BNE   LOOP1                NO, GO TO NEXT MAJOR          BFG07
         LRA   R0,WQEMIN+WMNMSIZE-1 SEE IF ENDING ADDR EXISTS     BFG31
         BNE   LOOP1                NO, GO TO NEXT MAJOR          BFG07
SP35     MVC   QDMSG(4),QBLANK      BLANK OUT FIRST PART OF LINE  BFG07
         MVC   QDMSG+4(76),WMNMHCT1 GET THE TEXT                  BFG07
         MVC   QDMLNG,=H'80'        SET THE LENGTH                BFG07
         BAL   R6,DISPLY            GO DISPLAY IT                 BFG07
         LRA   R0,WQEMIN            SEE IF ADDR STILL EXISTS      BFG07
         BNE   LOOP1                NO, GO TO NEXT MAJOR WQE      BFG07
         L     R3,WMNM              GET NEXT MINOR (OR SECOND HAL BFG31
         B     MINORLP              AND GO TRY TO DISPLAY IT      BFG07
END      EQU   *                                                  BFG07
*                                                                 BFG07
**       DO AXSET AND ESAR IF SP3 DESIRED                         BFG07
*                                                                 BFG07
         TM    QFLAG2,QFLG2AXS     WAS AXSET DONE???              BFG07
         BZ    NSP33               IF NOT SKIP                    BFG07
         AXSET AX=AXV              RESET AUTH                     BFG07
         NI    QFLAG2,255-QFLG2AXS TURN OFF THE FLAG              BFG07
NSP33    DS    0H                                                 BFG07
         QAUTHCK QAKEYP       GET BACK TO PROBLEM STATE           BFG09
         TM    QFLAG1,QFLG1ISP ARE WE IN ISPF????                *ISPF*
         BO    QCISPF2        YES SO SKIP LAST LINE              *ISPF*
         MVC   QDMSG(80),BOTTOM      SET BOTTOM LINE              BFG07
         MVC   QDMLNG,=H'80'         SET THE LENGTH               BFG07
         LA    R0,QDMSG              GET THE ADDR                 BFG07
         ST    R0,QDMSGA             SET THE ADDR                 BFG07
         L     R15,=V(DISPLAY)                                    BFG07
         BALR  R14,R15               DISPLAY THE LINE             BFG07
QCISPF2  DS    0H                                                *ISPF*
         QSTOP                                                    BFG07
         EJECT                                                    BFG07
******************************************************************BFG07
*                                                                 BFG07
*        SUBROUTINE TO SEND A LINE TO DISPLAY                     BFG07
*                                                                 BFG07
******************************************************************BFG07
DISPLY   DS    0H                    DISPLAY THE LINE             BFG07
         QAUTHCK QAKEYP       GET BACK TO PROBLEM STATE           BFG09
         LA    R0,QDMSG              GET THE ADDR                 BFG07
         ST    R0,QDMSGA             SET THE ADDR                 BFG07
         L     R15,=V(DISPLAY)                                    BFG07
         BALR  R14,R15               DISPLAY THE LINE             BFG07
         QAUTHCK QAKEY0       GET INTO SUPERVISOR STATE AGAIN     BFG09
         BR    R6                  EXIT                           BFG07
         EJECT                                                    BFG07
******************************************************************BFG07
*                                                                 BFG07
*        MISCELLANY                                               BFG07
*                                                                 BFG07
******************************************************************BFG07
         LTORG ,                                                  BFG07
TOP      DC   CL80'********************************* TOP OF DATA ******X
               ****************************'                      BFG07
BOTTOM   DC   CL80'******************************** BOTTOM OF DATA ****X
               ****************************'                      BFG07
TPMSTR   DC   CL80'**************************** MASTER CONSOLE AT XXX *X
               ****************************'                      BFG07
BTMSTR   DC   CL80'**************************** END OF MASTER CONSOLE *X
               ****************************'                      BFG07
         SPACE 2                                                  BFG07
DCMASCRN EQU   48                  TDCM OFFSET FOR SCREEN BUF ADDRBFG07
DCMMSGAL EQU   254                 TDCM MSG LINE COUNT            BFG07
DCMCORLN EQU   260                 TDCM CORE LINE LENGTH          BFG07
SYMDEL   DSECT ,                                                  BFG07
CONSOLE  CSECT ,                                                  BFG07
         QPRBGEN BEGIN                                            BFG07
         $HASPEQU ,
         IHAPSA  ,                                                BFG07
         QCOMMON  ,                                               BFG07
         IEECUCM  FORMAT=NEW,LIST=YES                             BFG07
         CVT   DSECT=YES                                          BFG07
         QPRBGEN DONE                                             BFG07
SYMNODEL DSECT ,                                                  BFG07
         END
