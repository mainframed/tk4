SUBS     TITLE 'P D S  --  PDS SUBROUTINES                    5/12/86'
         SPACE 2
         PRINT OFF
         COPY  PDSGEN73
         PRINT ON
         SPACE 2
*
*  THIS MEMBER CONTAINS PDS SUBCOMMANDS CSECTS IN ALPHABETICAL ORDER
*
         SPACE 2
ALIAS    CSECT
         ENTRY LIST170,DIRENTRY,REPLACE,FIND,PATTERN,INVABBR
         WXTRN VTSOCMD
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING WORKAREA,R7
         USING PDSCOMM,R12
         TITLE 'P D S  --  PDS ALIAS                          5/12/86'
***********************************************************************
***      ALIAS SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
         USING *,R8
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BNO   ALIAS0                     NO, BRANCH
         MVC   SPFSAVM2,MEMBER2           SAVE MEMBER NAME     SS AUG85
         B     ALIAS1                     NO, BRANCH
         SPACE 2
ALIAS0   MVC   MEMBERD+1(2),LMEMBER2      CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2     CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1           ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'           NO MEMBER LIST NOW
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
ALIAS1   MVC   DIRNAME,MEMBER2            SET ALIAS NAME
         MVI   ENTRYPT,C'?'               ENTRY POINT NOT KNOWN YET
         TM    DIRFLAG,X'80'              MODULE A CURRENT ALIAS?
         BZ    ALIAS2                     NO, BRANCH
         TM    FLAGSCC,RECFMU             LOAD MODULE?
         BO    ALIAS3                     YES, BRANCH
         B     ALIAS4                     NO, BRANCH
         SPACE 1
ALIAS2   TM    FLAGSCC,RECFMU             LOAD MODULE?
         BZ    ALIAS4                     NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG VS LKED & APF DATA PRESENT?
         BZ    ALIAS2Y                    NO, BRANCH
         SPACE 1
         LA    R2,DIRAPF                  POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BZ    *+8                        NO, BRANCH
         LA    R2,DIRAPF3                 YES, POINT TO APF DATA
         LA    R4,11(,R2)                 POINT TO ALIAS APF DATA
         TM    DIRATTR2,DIR2SSI           SSI PRESENT?
         BZ    ALIAS2B                    NO, BRANCH
         SPACE 1
         LA    R2,1(,R2)                  ROUND TO HALFWORD
         N     R2,=F'-2'                  FFFFFFFE MASK
         LA    R4,1(,R4)                  ROUND TO HALFWORD
         N     R4,=F'-2'                  FFFFFFFE MASK
         MVC   0(4,R4),0(R2)              MOVE SSI INFORMATION
         LA    R4,4(,R4)                  MOVE PAST SSI INFORMATION
         LA    R2,4(,R2)                  POINT TO APF DATA
         SPACE 1
ALIAS2B  CLI   0(R2),1                    IS APF LENGTH OK?
         BE    ALIAS2D                    YES, BRANCH
         $TMSG L710                       BAD APF INFORMATION FORMAT
         LA    R2,ZERO                    SET APF=0
         SPACE 1
ALIAS2D  MVI   0(R4),1                    SET PROPER LENGTH
         MVC   1(1,R4),1(R2)              MOVE IN APF DATA
         SPACE 3
ALIAS2Y  LA    R4,DIREP                   POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND2-DIRUSER+1)/2   NUMBER OF HALFWORDS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   ALIAS2Z                    NO, BRANCH
         LA    R4,DIREPSC                 POINT TO START OF ALIAS INFO.
         LA    R5,(DIREND3-DIRUSER+1)/2   NUMBER OF HALFWORDS
         SPACE 2
ALIAS2Z  MVC   0(3,R4),DIREPA             MOVE IN MAIN MODULE ENTRY
         MVC   3(8,R4),MEMBER1            ADD MAIN MODULE NAME
         TM    DIRATTR2,DIR2SSI           SSI INFORMATION?
         BNO   *+8                        NO, BRANCH
         LA    R5,2(,R5)                  YES, TWO MORE HALFWORDS
         NI    DIRFLAG,X'E0'              TURN OFF HALFWORD COUNT
         OI    DIRFLAG,*-*                <<EXECUTED>>
         EX    R5,*-4                     MOVE IN NUMBER OF HALFWORDS
         SPACE 1
ALIAS3   TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ALIAS3S                    YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    DIRATTR3,DIRAM24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    DIRATTR3,DIRAM31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS3S  L     R15,=A(READESD)            SCAN ESD FOR MEMBER NAME
         BALR  R14,R15                    MEMBER NAME IN ESD ENTRIES?
         B     ALIAS4                     NO, PSEUDO ENTRY
         SPACE 1
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ALIAS4                     YES, BRANCH
         NI    DIRATTR3,FF-DIRAA24-DIRAA31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 1
ALIAS4   OI    DIRFLAG,X'80'              SET ALIAS FLAG
         BAL   R2,OPENSTOW                OPEN STOW DCB; ENQUEUES
         B     NEWCMD                     COULD NOT OPEN -- ERROR
         STOW  STOWDCB,DIRNAME,A          ADD ALIAS TO DIRECTORY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     ALIAS5                     00 - SUCCESSFUL
         B     MEMEXIST                   04 - MEMBER ALREADY EXISTS
         EX    0,*                        08 - SHOULD NOT HAPPEN - ADD
         B     FULLDIR                    12 - DIRECTORY IS FULL
         B     IOERROR                    16 - I/O ERROR IN DIRECTORY
         SPACE 3
ALIAS5   $TMSG L010                       MSG - ALIAS ASSIGNED
         TM    FLAGSCC,RECFMU             MUST WE GIVE ENTRY POINT?
         BZ    NEWCMD                     NO, BRANCH
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         $TMSG (R1)
         B     NEWCMD
         TITLE 'P D S  --  PDS ATTRIB                         5/12/86'
***********************************************************************
***      ATTRIB SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
ATTRIB   CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    ATTR020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTAT1
         MVI   MTHIGHL,72
         TM    ##ADRCM#+#LSUB,FATTRHDR  HEADER WRITTEN YET?
         BO    ATTR000                  YES, BRANCH
         $TMSG L230$1
         OI    ##ADRCM#+#LSUB,FATTRHDR  MARK FOR NEXT CALL
         SPACE 1
ATTR000  MVC   INSERT#1(72),BLANK128
         CLI   #SSIOPT,0               ANY SSI CHANGE?
         BE    ATTR003                 NO, BRANCH
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR002                 YES, BRANCH
         TM    DIRFLAG,X'0F'           ISPF STATISTICS?
         BO    ATTR003                 YES, BRANCH
         NI    DIRFLAG,FF-X'1F'        TURN OFF SSI DATA
         XC    DIRSTART(4),DIRSTART    CLEAR THE SSI FIELD
         B     ATTR003
         SPACE 1
ATTR002  MVC   DIRSTART(4),#SSITEXT    ADD THE CURRENT SSI DATA
         NI    DIRFLAG,FF-X'1F'        TURNS OFF ANY HALFWORDS
         OI    DIRFLAG,X'02'           ADDS IN TWO HALFWORDS
         SPACE 1
ATTR003  CLI   #ADDSTAT,0              ANY STATS CHANGE?
         BE    ATTR006                 NO, BRANCH
         CLI   #ADDSTAT,2              ADDSTATS?
         BE    ATTR004                 YES, BRANCH
         NI    DIRFLAG,FF-X'1F'        TURN OFF SSI DATA OR ISPF STATS
         XC    DIRSTART(4),DIRSTART    CLEAR THE SSI FIELD
         B     ATTR006
         SPACE 1
ATTR004  TM    DIRFLAG,X'0F'           ISPF STATISTICS?
         BO    ATTR006                 YES, BRANCH
         XC    DIRSTART(30),DIRSTART   CLEAR THE DIRECTORY AREA
         OI    DIRFLAG,X'0F'           ADDS 15 HALFWORDS
         TIME  DEC                     GOT THE TIME?
         STCM  R1,B'1111',DIRSPFCR     CREATION DATE
         STCM  R1,B'1111',DIRSPFCD     CHANGED DATE
         STCM  R0,B'1100',DIRSPFCT     TIME OF LAST CHANGE
         MVI   DIRSPFSI+1,1            SIZE=1
         MVI   DIRSPFIN+1,1            INIT=1
         MVC   DIRSPFID+7(3),BLANKS    BLANK TRAILING ID BYTES
         L     R1,ADDRPSCB             ADDRESS OF THE PSCB
         MVC   DIRSPFID(7),PSCBUSER-PSCB(R1)  LOGON USERID
         SPACE 1
ATTR006  MVC   INSERT#1+1(8),DIRNAME
         CLI   #UNALIAS,1              TURN OFF ALIAS BIT?
         BNE   *+8                     NO, BRANCH
         NI    DIRFLAG,FF-X'80'        YES, RESET ANY ALIAS BIT
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   ATTR010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   ATTR010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   ATTR010             NO, BRANCH
         SPACE 1
         TM    #ATTROPT,X'80'        USERID CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFID(7),#USERTXT  NEW USERID
         SPACE 1
         TM    #ATTROPT,X'40'        VERSION CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFV(1),#ATTRVER   NEW VERSION NUMBER
         SPACE 1
         TM    #ATTROPT,X'20'        MOD CHANGE?
         BNO   *+10                  NO, BRANCH
         MVC   DIRSPFR(1),#ATTRMOD   NEW MODIFICATION NUMBER
         SPACE 1
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         B     ATTR012                 NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
ATTR010  CLC   ZERO,0(R2)              ZERO?
         BE    ATTR012                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR012                 YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         SPACE 1
ATTR012  $TMSG L230$1                  OUTPUT STATISTICS
         MVI   MTHIGHL,8               RESET THE INSERT LENGTH
         CLI   #OPTOPT,FF              ANY ILLEGAL CHANGES?
         BNE   ATTR014                 NO, BRANCH
         $TMSG L701                    NOT A LOAD MODULE
ATTR014  CLI   #OPTOPT,X'F0'           ANY SSI CHANGE?
         BE    ATTR240                 YES, BRANCH
         TM    #ATTROPT,X'FF'          ANY ATTRIBUTES CHANGED?
         BNZ   ATTR240                 YES, BRANCH
         B     ATTR600                 CHECK FOR ALIAS
         SPACE 3
ATTR020  CLC   ##SUBCOM+#LSUB(8),$ATTL IF OR THEN FORM?
         BE    *+8                     NO, BRANCH
         MVI   #ALIAOPT,X'03'          YES, SET FOR SHORT FORMAT
         TM    FLAGSAA,FHEAD           BLANK SEPARATOR WRITTEN YET?
         BNO   ATTR022                 NO, BRANCH
         CLI   #ALIAOPT,X'03'          SHORT FORMAT DESIRED?
         BE    ATTR022                 YES, BRANCH
         $MESAGE MSGBLANK              NOW SEPARATORS CAN BE WRITTEN
         SPACE 1
ATTR022  OI    FLAGSAA,FHEAD
         LA    R1,L702                 ASSUME SOURCE CHANGES WERE MADE
         TM    #ATTROPT,X'FF'          CORRECT?
         BNZ   MSGNEWXX                YES, BRANCH
         SPACE 2
         MVI   RLDCOUNT,X'FF'          ASSUME NO RLD/CONTROL DATA
         MVI   LKEDDATE,X'FF'          ASSUME NO LKED DATE
         CLI   #RLDERR,X'01'           "RLDFIX" OPERAND?
         BE    ATTR024                 YES, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE"?
         BE    ATTR030                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    ATTR024                 YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR030                 YES, BRANCH
ATTR024  L     R15,=A(READIDR)         FIRST RLD COUNT, LKED DATA
         BALR  R14,R15                 IDR DATA AVAILABLE?
         MVI   LKEDDATE,X'FF'          NO, ERROR
         SPACE 1
ATTR030  TM    #OPTOPT,X'F0'           ANY CHANGED ATTRIBUTES?
         BNO   ATTR250                 NO, BRANCH
         SPACE 1
         CLI   #RLDERR,X'01'           "RLDFIX"?
         BNE   ATTR038                 NO, BRANCH
         TM    DIRATTR2,DIRAOSLE       OS/VS LINKAGE-EDITOR?
         BNO   ATTR034                 NO, BRANCH
         CLI   RLDCOUNT,X'FF'          ANY RLD/CONTROL DATA FOUND?
         BE    ATTR034                 NO, BRANCH
         MVC   DIRATTR4(1),RLDCOUNT    MODIFY THE DIRECTORY ENTRY
         B     ATTR038
ATTR034  $TMSG L723                    NO CHANGE IS POSSIBLE
         SPACE 2
ATTR038  CLI   #AUTHOPT,X'01'          CHANGE APF PROCESSING?
         BL    ATTR060                 NO, BRANCH
         BE    ATTR040                 YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE APF INFORMATION?
         BNO   ATTR060                     NO, BRANCH
         SPACE 1
ATTR040  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE APF INFORMATION?
         BNO   ATTR050                     NO, BRANCH
         LA    R2,DIRAPF                   YES, POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               SCATTER SIZE
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR044                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR044  LA    R2,11(,R2)              ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI        SSI?
         BNO   *+12                    NO, BRANCH
         LA    R2,5(,R2)               ADD 4 AND ROUND TO HALFWORD
         N     R2,=F'-2'
         CLI   0(R2),1                 LENGTH RIGHT?
         BNE   ATTR050                 NO, BRANCH
         MVI   1(R2),1                 ASSUME TURN AUTHORIZATION ON
         CLI   #AUTHOPT,X'01'          CORRECT?
         BE    ATTR060                 YES, BRANCH
         MVI   1(R2),0                 NO, TURN OFF
         B     ATTR060
         SPACE 3
ATTR050  $TMSG L722                        INVALID APF DATA LENGTH
         SPACE 2
ATTR060  CLI   #PAGEOPT,X'01'              CHANGE PAGE ALIGNMENT?
         BL    ATTR100                     NO, BRANCH
         BE    ATTR070                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  NEED CHANGE PAGE INFO?
         BNO   ATTR100                     NO, BRANCH
         SPACE 1
ATTR070  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  CAN CHANGE PAGE INFORMATION?
         BNO   ATTR080                     NO, BRANCH
         OI    DIRATTR2,DIR2PAGA           ASSUME TURN ON PAGE BOUNDARY
         CLI   #PAGEOPT,X'01'              CORRECT?
         BE    ATTR100                     YES, BRANCH
         NI    DIRATTR2,FF-DIR2PAGA        NO, TURN OFF PAGE BOUNDARY
         B     ATTR100
         SPACE 1
ATTR080  $TMSG L726                    PAGE ALIGNMENT CANNOT BE CHANGED
         SPACE 2
ATTR100  CLI   #RMODE,1                    ANY RMODE CHANGE?
         BL    ATTR120                     NO, BRANCH
         BH    ATTR110                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR120                     NO, BRANCH
         SPACE 1
ATTR110  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE RMODE?
         BNO   ATTR150                     NO, BRANCH
         NI    DIRATTR3,FF-DIRRMANY        SET RMODE=24
         CLI   #RMODE,1                    CORRECT?
         BE    ATTR120                     YES, BRANCH
         OI    DIRATTR3,DIRRMANY           NO, TURN ON RMODE ANY
         SPACE 2
ATTR120  CLI   #AMODE,1                    ANY AMODE CHANGE?
         BL    ATTR160                     NO, BRANCH
         BH    ATTR130                     YES, BRANCH
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR160                     NO, BRANCH
         SPACE 1
ATTR130  LA    R1,L724                     NOT MVS LKED MESSAGE
         TM    DIRATTR2,DIRAOSLE           CAN CHANGE AMODE?
         BNO   ATTR150                     NO, BRANCH
         TM    DIRFLAG,X'80'               ALIAS ENTRY?
         BO    ATTR140                     YES, BRANCH
         OI    DIRATTR3,DIRAM24+DIRAM31    MAIN ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAM31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAM31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAM24            SET AMODE=31
         B     ATTR160
         SPACE 1
ATTR140  OI    DIRATTR3,DIRAA24+DIRAA31    ALIAS ENTRY -- AMODE=24+31
         XI    DIRATTR3,DIRAA31            SET AMODE=24
         CLI   #AMODE,1                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         OI    DIRATTR3,DIRAA31            NO, SET AMODE=ANY
         CLI   #AMODE,3                    CORRECT?
         BE    ATTR160                     YES, BRANCH
         XI    DIRATTR3,DIRAA24            SET AMODE=31
         B     ATTR160
         SPACE 1
ATTR150  $TMSG (R1)                        AMODE/RMODE MESSAGE
         SPACE 2
ATTR160  OC    DIRATTR,#ATTRYES        ADD POSITIVE ATTRIBUTES
         NC    DIRATTR,#ATTRNO         REMOVE NEGATIVE ATTRIBUTES
         SPACE 1
*
*   RESTRICTION: BOTH RENT AND REUS MUST BE SPECIFIED FOR REENTRANT
         TM    DIRATTR,ATTRRENT        WAS REENTRABLE SPECIFIED?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR,ATTRREUS        YES, ALSO FORCE REUSABLE
         SPACE 1
ATTR180  CLI   #SSIOPT,0               SSI(HEXDATA) OR NOSSI?
         BE    ATTR216                 NO, BRANCH
         SR    R1,R1                   SSI INDICATOR
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR182                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR182  LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         LR    R3,R2                   START OF DATA BEFORE ANY SSI
         LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR184                 YES, BRANCH
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR188                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR188                 YES, NO SSI
         B     ATTR186                 NO, POINT TO SSI
         SPACE 1
ATTR184  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR188                 NO, NO SSI
         SPACE 1
ATTR186  LR    R1,R2                   POINT TO SSI
ATTR188  LR    R15,R2                  SSI POSITION
         SR    R15,R3                  OFFSET FROM LAST DATA TO SSI
         LA    R15,2(,R15)             ADJUSTMENT AMOUNT
         CLI   #SSIOPT,1               SSI(HEXDATA)?
         BE    ATTR200                 YES, BRANCH
         SPACE 1
*** NOSSI
         LTR   R1,R1                   ANY SSI?
         BZ    ATTR216                 NO, DONE
         ICM   R0,B'0011',4(R2)        APF BYTES
         XC    0(8,R3),0(R3)           CLEAR FOLLOWING BYTES
         STCM  R0,B'0011',0(R3)        MOVE APF BEFORE THE SSI
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         NI    DIRATTR2,FF-DIR2SSI     NO SSI PRESENT
         LA    R3,2+1(,R3)             2 FOR APF AND 1 FOR ROUNDING
         B     ATTR214
         SPACE 2
ATTR200  LTR   R1,R1                   SSI ALREADY?
         BZ    ATTR210                 NO, BRANCH
*** SSI(HEXTEXT) WITH EXISTING SSI INFORMATION
         MVC   0(4,R2),#SSITEXT        UPDATE THE SSI DATA
         B     ATTR216
         SPACE 2
*** SSI(HEXTEXT) WITH NO PREVIOUS SSI INFORMATION
ATTR210  ICM   R0,B'0011',0(R3)        APF BYTES
         MVI   0(R3),0                 CLEAR THE FIRST APF BYTE
         MVC   0(4,R2),#SSITEXT        MOVE IN ADDED SSI DATA
         STCM  R0,B'0011',4(R2)        RESET THE APF INFORMATION
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   *+8                     NO, BRANCH
         OI    DIRATTR2,DIR2SSI        SSI PRESENT
         LA    R3,4+2(,R2)             4 FOR SSI AND 2 FOR APF
         SPACE 1
ATTR214  LA    R0,DIRSTART             FIRST BYTE AFTER DIRFLAG
         SR    R3,R0                   LENGTH OF ADDED DATA
         SRL   R3,1                    IN HALFWORDS
         NI    DIRFLAG,FF-X'1F'        REMOVE LENGTH BITS
         IC    R0,DIRFLAG              PICK UP ALIAS FLAG, TTR COUNT
         OR    R0,R3                   ADD IN NEW LENGTH IN HALFWORDS
         STC   R0,DIRFLAG              UPDATE DIRECTORY FLAG ENTRY
         SPACE 2
ATTR216  CLI   #UNALIAS,1              UNALIAS?
         BNE   ATTR220                 NO, BRANCH
         TM    DIRFLAG,X'80'           ALIAS?
         BNO   ATTR220                 NO, BRANCH
         XI    DIRFLAG,X'80'           TURN OFF THE ALIAS FLAG
         SR    R1,R1
         LA    R3,DIRAPF               NORMAL APF LOCATION
         TM    DIRATTR,ATTRSCTR        SCATTER LOAD?
         BNO   *+8                     NO, BRANCH
         LA    R3,8(,R3)               YES, ADD 8
         LA    R1,11(,R3)              POINT AFTER THE ALIAS INFO.
         TM    DIRATTR2,DIRAOSLE       VS LKED?
         BNO   ATTR218                 NO, BRANCH
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR217                 NO, BRANCH
         MVI   0(R3),0                 CLEAR THE FIRST BYTE
         LA    R3,1(,R3)               SET FOR ROUNDING
         N     R3,=F'-2'               ROUND TO HALFWORD
         LA    R1,1(,R1)               POINT TO SSI
         N     R1,=F'-2'               ROUND TO HALFWORD
ATTR217  MVC   0(8,R3),0(R1)           SLIDE DOWN DATA
         LA    R3,2+1(,R3)             ACCOUNT FOR APF
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR214                 NO, BRANCH
         LA    R3,4(,R3)               ACCOUNT FOR APF
         B     ATTR214                 UPDATE DIRECTORY ENTRY LENGTH
         SPACE 1
ATTR218  LA    R3,1(,R3)
         LA    R14,1(,R1)              ROUND TO
         N     R14,=F'-2'                      NEXT HALFWORD
         CLC   ZERO,0(R14)             ZERO?
         BE    ATTR214                 YES, NO SSI
         CLC   =F'-1',0(R14)           FFFFFFFF?
         BE    ATTR214                 YES, NO SSI
         N     R3,=F'-2'               ROUND TO HALFWORD
         MVC   0(8,R3),0(R14)          SLIDE DOWN DATA
         LA    R3,4(,R3)               ADD FOR SSI
         B     ATTR214                 UPDATE DIRECTORY ENTRY LENGTH
         SPACE 2
ATTR220  CLI   #MODTXT,X'41'              ANY ENTRY(EXTNAME)?
         BL    ATTR240                    NO, BRANCH
         MVC   #USERTXT(8),DIRNAME        SAVE THE CURRENT MEMBER NAME
         MVC   DIRNAME(8),#MODTXT         NAME TO SEARCH FOR IN ESD
         SPACE 1
         L     R15,=A(READESD)            SCAN ESD
         BALR  R14,R15                    EXTERNAL NAME IN ESD ENTRIES?
         NOP   0                          NO, IGNORE
         CLC   ENTRYPT(8),#MODTXT         NAME MATCH?
         BE    ATTR222                    YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         MVC   INSERT#1(8),#MODTXT        EXTERNAL NAME
         $TMSG L705$1
         B     ATTR240
         SPACE 1
ATTR222  MVC   DIRNAME(8),#USERTXT        RESTORE THE MEMBER NAME
         OI    DIRATTR+1,ATTREP0          ASSUME ENTRY POINT ZERO
         LTR   R1,R1                      CORRECT?
         BZ    *+8                        YES, BRANCH
         XI    DIRATTR+1,ATTREP0          NO, INSURE ATTR FLAG OFF
         STCM  R1,B'0111',DIREPA          SAVE ENTRY ADDRESS
         TM    DIRATTR,ATTRSCTR           SCATTER LOADED?
         BNO   *+8                        NO, BRANCH
         STCM  R15,B'0011',DIRSCEP        YES, SAVE ESDID OF ENTRY PT.
         STC   R0,#ALIAESD                SAVE MODE RMODE/AMODE INFO
         TM    DIRATTR,ATTROVLY           OVERLAY ATTRIBUTES
         BO    ATTR240                    YES, BRANCH
         OC    #RMODE(2),#RMODE           RMODE/AMODE CHANGES ALREADY?
         BNZ   ATTR240                    YES, BRANCH
         TM    DIRATTR2,DIRAOSLE          CAN CHANGE AMODE?
         BNO   ATTR240                    NO, BRANCH
         OI    #RMODE,1                   MARK RMODE/AMODE CHANGE
         SPACE 1
         TM    DIRFLAG,X'80'              ALIAS ENTRY?
         BO    ATTR224                    YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY       SET RMODE=24
         TM    #ALIAESD,RMODEANY          CORRECT?
         BZ    *+8                        YES, BRANCH
         OI    DIRATTR3,DIRRMANY          NO, TURN ON RMODE ANY
         SPACE 2
         NI    DIRATTR3,FF-DIRAM24-DIRAM31  MAIN ENTRY -- AMODE=24+31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAM31           YES, SET THE DIRECTORY
         B     ATTR240
         SPACE 1
ATTR224  NI    DIRATTR3,FF-DIRAA24-DIRAA31  ALIAS ENTRY -- AMODE=24+31
         TM    #ALIAESD,AMODE24           AMODE 24?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA24           YES, SET THE DIRECTORY
         TM    #ALIAESD,AMODE31           AMODE 31?
         BNO   *+8                        NO, BRANCH
         OI    DIRATTR3,DIRAA31           YES, SET THE DIRECTORY
         SPACE 2
ATTR240  BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR
         STOW  STOWDCB,DIRNAME,R       REPLACE OPTION
         LR    R5,R15                  SAVE RETURN CODE
***      BAL   R2,SHUTSTOW             *** CLOSE STOW AFTER NEWCMD
         SPACE 1
         B     *+4(R5)                 PROCESS RETURN CODE
         B     ATTR250                   00 - SUCCESSFUL
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
ATTR250  TM    FLAGSCC,RECFMU          LOAD LIBRARY?
         BNO   ATTR600                 NO, CHECK FOR ALIASES
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(128),BLANK128
         LA    R1,INSERT#2-1           FIRST COMMA ADDRESS (HIDDEN)
         MVC   INSERT#2+1(4),=C'NONE'  IF NO SPECIAL ATTRIBUTES
         LA    R14,TBLATTR-12          ATTRIBUTES TABLE
         LH    R2,DIRATTR              GET MODULE ATTRIBUTES
         SPACE 2
ATTR300  LA    R14,12(R14)
         CLI   0(R14),X'FF'            END OF TABLE?
         BE    ATTR310                 YES, BRANCH
         SPACE 1
         LH    R15,0(R14)              GET ATTRIBUTE FLAGS
         TM    2(R14),X'80'            POSITIVE OR NEGATIVE ATTRIBUTE?
         LA    R3,X'80'                MASK FOR POS (FOR BZ INST)
         BZ    *+8
         LA    R3,X'70'                MASK FOR NEG (FOR BNZ) INST
         NR    R15,R2                  CHECK FOR ATTRIBUTE PRESENT
         NOP   ATTR300                 <<BZ OR BNZ>>
         EX    R3,*-4                  BRANCH IF NO DISPLAY ATTRIBUTE
         SPACE 1
         MVC   0(6,R1),ATTRCOMM        COMMA AND FOLLOWING BLANKS
         IC    R15,2(R14)              LENGTH-1 OF ATTRIBUTE
         LA    R3,X'7F'                MASK FOR 7 BITS
         NR    R15,R3
         MVC   2(*-*,R1),3(R14)        <<EXECUTED>>
         EX    R15,*-6
         LA    R1,3(R15,R1)            JUMP POINTER
         B     ATTR300
         SPACE 2
ATTR310  DS    0H
         CLI   #ALIAOPT,X'03'          SHORT FORMAT DESIRED?
         BE    ATTS310                 YES, BRANCH
         MVI   MTHIGHL+4,127
         $TMSG L020$2                  ATTRIBUTES MSG
         MVI   MTHIGHL+4,8
         SPACE 2
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR314                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTR314  LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTR330                 YES, BRANCH
         $TMSG L022                    NON-VS LINKAGE EDITOR MESSAGE
         B     ATTR340
         SPACE 1
ATTR330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTR360                 NO, SKIP SSI PROCESSING
         SPACE 1
ATTR340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTR360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTR360                 YES, NO SSI
         SPACE 1
         UNPK  INSERT#1(9),0(5,R2)
         TR    INSERT#1(8),TRTABLE
         $TMSG L025$1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTR360  TM    FLAGSCC,RECFMU          LOAD MODULE LIB?
         BZ    ATTR600                 NO, BRANCH
         SPACE 1
         MVI   MODESAVE,0
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTR540                     NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    ATTR400                     NO, BRANCH
         MVC   INSERT#1(8),BLANKS          RMODE FILLER
         MVC   INSERT#2(8),BLANKS          AMODE FILLER
         MVC   INSERT#1(3),=C'24 '         MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#1(3),=C'ANY'         MOVE IN THE RMODE TEXT
         MVC   INSERT#2(3),=C'24 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   INSERT#2(3),=C'ANY'         MOVE IN THE AMODE TEXT
         $TMSG L120$2
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTR400                     NO, BRANCH
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    ATTR370                     YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    ATTR400                     YES, BRANCH
         LA    R1,=C'24 '                  AMODE IS 24
ATTR370  MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         $TMSG L880$1                      RMODE ANY/AMODE CONFLICT
         SPACE 2
ATTR400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTR500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTR500                 YES, BRANCH
         $TMSG L826
         SPACE 2
ATTR500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTR510                 NO, BRANCH
         $TMSG L024                    PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTR510  LA    R1,L720                 ASSUME NO APF DATA
         TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   ATTR530                 NO, BRANCH
         SPACE 1
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTR530                 NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTR530                 YES, BRANCH
         BL    ATTR540                 NO, APF=0 (NO MESSAGE NEEDED)
         LA    R1,L023                 APF DATA IS TOO LARGE
         SPACE 1
ATTR530  $TMSG (R1)
         SPACE 2
ATTR540  TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANK FILL
         MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REENTRANT
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   ATTR542                         NO, BRANCH
         $TMSG L884$1                          YES, ERROR
ATTR542  MVC   INSERT#1(4),REUSTXT+3           OVERLAY AND REUSABLE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   ATTR543                         NO, BRANCH
         $TMSG L884$1                          YES, ERROR
ATTR543  MVC   INSERT#1(4),REFRTXT+3           OVERLAY AND REFRESHABLE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   ATTR544                         NO, BRANCH
         $TMSG L884$1                          YES, ERROR
ATTR544  MVC   INSERT#1(4),SCTRTXT+3           OVERLAY AND SCATTER
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   ATTR545                         NO, BRANCH
         $TMSG L884$1                          YES, ERROR
ATTR545  TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   ATTR550                         NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         TM    MODESAVE,DIRRMANY               CORRECT?
         BNO   ATTR546                         NO, BRANCH
         $TMSG L884$1                          YES, ERROR
ATTR546  MVI   INSERT#1,C'A'                   AMODE ANY
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    ATTR550                         YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    ATTR550                         YES, BRANCH
         MVC   INSERT#1+6(3),=C'31 '           NO, AMODE 31
         $TMSG L884$1                          YES, ERROR
         SPACE 1
ATTR550  TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   ATTR560                         NO, BRANCH
         $TMSG L882                            YES, ERROR
         SPACE 1
ATTR560  TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   ATTR570                         NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    ATTR570                         YES, BRANCH
         $TMSG L881                            ERROR MESSAGE
         SPACE 1
ATTR570  TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   ATTR590                         NO, BRANCH
         $TMSG L883                            YES, ERROR
         SPACE 2
ATTR590  UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         $TMSG L102$1                 NO ENTRY POINT MESSAGE
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         $TMSG L104$2
         SPACE 2
         CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTR600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTR600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTR600                 YES, BRANCH
         SPACE 1
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTHIGHL+4,18                   LENGTH OF THIS INSERT
         MVC   INSERT#2(10),LKEDNAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),LKEDVVMM(2)
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),LKEDVVMM+1(2)
         SPACE 1
         $TMSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTHIGHL+4,8                    LENGTH OF STANDARD INSERT
         SPACE 3
ATTR600  TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTR630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTR630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTR630                 NO, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM24
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTR610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTR630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTR610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTR610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM24
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTR610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM24
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         $TMSG L092$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTR610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTR630  NI    FLAGSBB,FF-FLINESET
         CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
         BE    ATTR640                 YES, BRANCH
         BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD                  YES, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BO    ATTR640                 YES, BRANCH
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTR640  MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTR900                 NO, FIND ALL ALIASES
         XC    INSERT#1(8),INSERT#1    NO MAIN(S) FOUND
         SPACE 2
*  LIST ANY CORRESPONDING MAIN MODULE NAMES
         SPACE 1
ATTR800  BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTR810            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTR800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTR800            NO, BRANCH
         MVC   INSERT#1(8),MEMNAME
         $TMSG L066$1
         B     ATTR800
         SPACE 1
ATTR810  OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, BRANCH
         $TMSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 2
*  LIST ANY ALIASES FOR THE MODULE
         SPACE 1
ATTR900  BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     ATTR940              LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   ATTR900              NO, BRANCH
         TM    MEMFLAG,X'80'        AN ALIAS?
         BO    ATTR910              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME   DID I FIND MYSELF?
         BE    ATTR900              YES, BRANCH
         MVC   INSERT#1(8),MEMNAME
         $TMSG L864$1               THIS IS AN APPARENT ALIAS MEMBER
         B     ATTR900
         SPACE 1
ATTR910  LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BO    ATTR920              YES, BRANCH
         OI    FLAGSBB,FLINESET     LINE NOW IN PROGRESS
         LA    R1,80                ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON       ISPMODE ACTIVE?
         BO    ATTR912              YES, BRANCH
         TM    CONTOPTN,1           ANY LOG RECORDING?
         BO    ATTR912              YES, BRANCH
         GTSIZE ,                   TERMINAL SIZE
         CH    R1,=H'120'           120 OR LESS BYTES?
         BL    *+8                  YES, BRANCH
         LH    R1,=H'120'           NO, USE 120 BYTES
ATTR912  SH    R1,=H'7'             LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE          CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGALIAS),MSGALIAS
         LA    R5,MSGLINE+4+L'MSGALIAS
         MVC   FULLWORD(3),L160$1   MESSAGE IDENTIFIER
         SPACE 1
ATTR920  CLI   0(R2),X'40'          SCAN FOR
         BNE   *+8                          LAST
         BCT   R2,ATTR920                       NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R5,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   ATTR930
         LA    R1,MSGLINE+4
         SR    R5,R1
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3         INDENT FOR BLANKS
         SPACE 1
ATTR930  MVI   0(R5),X'40'
         MVC   1(8,R5),MEMNAME
         TR    1(8,R5),TRLINE
         LA    R1,2(R2,R5)
         MVI   0(R1),C','
         LA    R5,3(R2,R5)
         B     ATTR900
         SPACE 2
ATTR940  TM    FLAGSBB,FLINESET     LINE IN PROGRESS?
         BZ    NEWCMD               NO, EXIT
         LA    R1,MSGLINE+5
         SR    R5,R1
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         B     NEWCMD
ATTRCOMM DC    C',     '         COMMA AND FOLLOWING BLANKS
MSGALIAS DC    C'ALIASES FOR THIS MEMBER ARE:'
TBLATTR  DS    0H                                    PDS PROCESSING
*                                                    ==============
         DC    AL1(ATTROVLY,0,00006),CL9'OVERLAY'       LIST ONLY
         DC    AL1(ATTRTEST,0,00003),CL9'TEST'          LIST ONLY
         DC    AL1(0,ATTRFLEV,128+6),CL9'E-LEVEL'       LIST ONLY
SCTRTXT  DC    AL1(ATTRSCTR,0,00003),CL9'SCTR'          LIST ONLY
RENTTXT  DC    AL1(ATTRRENT,0,00003),CL9'RENT'       CHANGE AND LIST
REUSTXT  DC    AL1(ATTRREUS,0,00003),CL9'REUS'       CHANGE AND LIST
REFRTXT  DC    AL1(0,ATTRREFR,00003),CL9'REFR'       CHANGE AND LIST
         DC    AL1(ATTRLOAD,0,00008),CL9'LOAD ONLY'  CHANGE AND LIST
         DC    AL1(0,ATTRNE+0,00007),CL9'NOT EDIT'   CHANGE AND LIST
         DC    AL1(ATTREXEC,0,128+7),CL9'NOT EXEC'   CHANGE AND LIST
         DC    AL1(0,ATTRNODC,128+1),CL9'DC'         CHANGE AND LIST
         DC    X'FFFF'
         SPACE 2
SPFSTAT1 DC    CL72'SPF STATS:  VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'
         SPACE 3
ATTS310  BALR  R8,0                    SHORT FORM ATTRIB SUBCOMMAND
         USING *,R8                    4K OF ADDRESSABILITY
         MVC   MSGTEXT2(86),INSERT#2   SAVE THE VARIABLE PART
         TM    ##ADRCM#+#LSUB,FATTRHDR HEADER WRITTEN YET?
         BO    ATTS312                 YES, BRANCH
         MVC   INSERT#1(72),ATTSTAT    HEADER INFORMATION
         MVI   MTHIGHL,72
         $TMSG L232$1
         OI    ##ADRCM#+#LSUB,FATTRHDR MARK FOR NEXT CALL
         SPACE 1
ATTS312  MVC   INSERT#1(127),BLANK128
         MVI   MTHIGHL,112             MAXIMUM LENGTH INSERT
         MVC   INSERT#1(8),DIRNAME
         SPACE 2
         LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTS314                 YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
ATTS314  LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BO    ATTS330                 YES, BRANCH
***      $TMSG L022                    NON-VS LINKAGE EDITOR MESSAGE
         B     ATTS340
         SPACE 1
ATTS330  TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   ATTS360                 NO, SKIP SSI PROCESSING
         SPACE 1
ATTS340  LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    ATTS360                 YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    ATTS360                 YES, NO SSI
         SPACE 1
         UNPK  INSERT#1+33(9),0(5,R2)  CONVERT TO HEXADECIMAL
         TR    INSERT#1+33(8),TRTABLE  CONVERT TO PRINTABLE
         MVI   INSERT#1+33+8,X'40'     FIX UNPRINTABLE CHARACTER
***      $TMSG L025$1
         LA    R2,4(,R2)               ADD SSI SIZE
         SPACE 2
ATTS360  DS    0H
         MVI   MODESAVE,0
         LA    R3,INSERT#1+42              WHERE VARIABLE OUTPUT STARTS
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    ATTS540                     NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    ATTS400                     NO, BRANCH
         MVI   0(R3),C'R'                  R FOR RMODE
         MVC   1(3,R3),ATTS24              MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    ATTS362                     NO, BRANCH
         MVC   1(4,R3),ATTSANY             MOVE IN THE RMODE TEXT
         LA    R3,1(,R3)                   ADD AN EXTRA ONE
ATTS362  LA    R3,5(,R3)                   ALLOW FOR "R24, "
         SPACE 1
         MVI   0(R3),C'A'                  A FOR AMODE
         MVC   1(3,R3),ATTS24              MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   1(3,R3),ATTS31              MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   ATTS364                     NO, BRANCH
         MVC   1(4,R3),ATTSANY             MOVE IN THE AMODE TEXT
         LA    R3,1(,R3)                   ADD AN EXTRA ONE
ATTS364  LA    R3,5(,R3)                   ALLOW FOR "A24, "
         SPACE 1
***      $TMSG L120$2
***      TM    MODESAVE,DIRRMANY           RMODE ANY?
***      BZ    ATTS400                     NO, BRANCH
***      LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
***      TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
***      BO    ATTS370                     YES, INVALID
***      TM    MODESAVE,DIRAM31            AMODE 31?
***      BO    ATTS400                     YES, BRANCH
***      LA    R1,=C'24 '                  AMODE IS 24
***S370  MVC   INSERT#1(8),BLANKS
***      MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
***      $TMSG L880$1                      RMODE ANY/AMODE CONFLICT
         SPACE 2
ATTS400  CLI   RLDCOUNT,X'FF'          ANY DATA TO CHECK?
         BE    ATTS500                 NO, BRANCH
         CLC   RLDCOUNT(1),DIRATTR4    RLD/CONTROL AMOUNTS AGREE?
         BE    ATTS500                 YES, BRANCH
***      $TMSG L826
         SPACE 2
ATTS500  TM    DIRATTR2,DIR2PAGA       PAGE ALIGNMENT REQUIRED?
         BNO   ATTS510                 NO, BRANCH
         MVC   0(5,R3),ATTSPAGE        PAGE, TEXT
         LA    R3,6(,R3)               NEXT PARAMETER
***      $TMSG L024                    PAGE ALIGNMENT REQUIRED
         SPACE 2
ATTS510  TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   ATTS540                 NO, BRANCH
         SPACE 1
         LA    R1,ATTSAPFX             ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   ATTS530                 NO, BRANCH
         SPACE 3
         LA    R1,ATTSAPF1             ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BE    ATTS530                 YES, BRANCH
         BL    ATTS540                 NO, APF=0 (NO MESSAGE NEEDED)
         SPACE 1
***S530  $TMSG (R1)
ATTS530  MVC   0(5,R3),0(R1)           MOVE IN TEXT
         MVI   5(R3),C','              ADD A COMMA
         LA    R3,7(,R3)               NEXT PARAMETER
         SPACE 2
ATTS540  DS    0H
         CLC   MSGTEXT2+1(4),ATTSNONE  NONE PARAMETER?
         BNE   ATTS542                 NO, BRANCH
         LR    R1,R3                   NEXT POSITION FOR TEXT
         SH    R1,=H'2'                POINT TO LAST COMMA IF A LIST
         CLI   0(R1),C','              COMMA?
         BNE   ATTS542                 NO, BRANCH
         MVI   0(R1),X'40'             BLANK THE LAST COMMA
         MVC   MSGTEXT2+1(4),BLANK128  BLANK "NONE"
         SPACE 1
ATTS542  MVC   0(85,R3),MSGTEXT2+1     ADD THE VARIABLE PART
         SPACE 2
ATTS590  CLI   LKEDDATE,X'FF'          ANY DATA TO OUTPUT?
         BE    ATTS600                 NO, BRANCH
         CLI   #LKEDOPT,X'02'          "NOLKEDDATE" DESIRED?
         BE    ATTS600                 YES, BRANCH
         CLI   #LKEDOPT,X'01'          "LKEDDATE" DESIRED?
         BE    *+12                    YES, BRANCH
         TM    FLAGSGG,FLKEDCON        NO LKEDDATE DEFAULT?
         BO    ATTS600                 YES, BRANCH
         SPACE 1
         MVC   MSGTEXT2(9),DATEMASK
         ED    MSGTEXT2(9),LKEDDATE
         MVC   INSERT#1+18(2),MSGTEXT2+7 YY OF MM/DD/YY
         MVI   INSERT#1+20,C'/'          YY/
         MVC   INSERT#1+21(5),MSGTEXT2+1 MM/DD OF MM/DD/YY
         OI    INSERT#1+21,X'F0'         REMOVE ANY LEADIN BLANK IN MM
***      $TMSG L064$2                         LINKAGE EDIT MESSAGE
         SPACE 3
ATTS600  ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   MSGTEXT2(7),=X'402020202120D2404040'
         ED    MSGTEXT2(6),DOUBLE+5
         MVC   INSERT#1+26(6),MSGTEXT2+1
***      $TMSG L104$2
         SPACE 3
         TM    DIRFLAG,X'80'           ALIAS?
         BO    ATTS630                 YES, BRANCH
         OC    #RMODE(2),#RMODE        ANY RMODE/AMODE CHANGE?
         BZ    ATTS630                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   ATTS630                 NO, BRANCH
         NI    MODESAVE,DIRRMANY+DIRAM31+DIRAM24
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE MAIN ENTRY
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
ATTS610  MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  RESTORE MAIN ENTRY
         BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     ATTS630                 LAST MEMBER PROCESSED
         SPACE 2
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   ATTS610                 NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   ATTS610                 NO, CONTINUE SEARCHING
         SPACE 2
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         MVC   DOUBLE(1),DIRATTR3
         NI    DOUBLE,DIRRMANY+DIRAM31+DIRAM24
         CLC   MODESAVE(1),DOUBLE      RMODE/AMODE ENTRIES EQUAL?
         BE    ATTS610                 YES, BRANCH
         NI    DIRATTR3,FF-DIRRMANY-DIRAM31-DIRAM24
         OC    DIRATTR3(1),MODESAVE    CHANGE THE RMODE/AMODE ENTRY
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
***      MVC   INSERT#1(8),DIRNAME     MEMBER NAME
***      $TMSG L092$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     ATTS610                 CONTINUE FOR ALL ALIASES
         SPACE 2
ATTS630  NI    FLAGSBB,FF-FLINESET
***      CLI   #ALIAOPT,X'01'          "ALIASINFO" DESIRED?
***      BE    ATTS640                 YES, BRANCH
***      BH    NEWCMD                  "NOALIASINFO", DONE
         TM    FLAGSGG,FALINCON        NO ALIAS INFORMATION DEFAULT?
         BO    ATTS900                 YES, BRANCH
***      TM    FLAGSCC,RECFMU          RECFM=U?
***      BO    ATTS640                 YES, BRANCH
***      TM    DIRFLAG,X'80'           MODULE AN ALIAS?
***      BNO   NEWCMD                  NO, DONE
         SPACE 1
ATTS640  TM    DIRFLAG,X'80'           MODULE AN ALIAS?
         BNO   ATTS900                 NO, FIND ALL ALIASES
         MVC   INSERT#1+9(8),ATTSUNK   NO MAIN FOUND
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 2
*  FIND ANY CORRESPONDING MAIN MODULE NAME
         SPACE 1
ATTS800  BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     ATTS900            LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    ATTS800            YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   ATTS800            NO, BRANCH
         MVC   INSERT#1+9(8),MEMNAME
***      $TMSG L066$1
***      B     ATTS800
         SPACE 1
ATTS900  DS    0H                      OUTPUT FINAL RESULT
         $TMSG L232$1
         B     NEWCMD
*
         SPACE 2
ATTSTAT  DC    CL72'NAME     ALIASOF   CREATED  SIZE SSI      ATTRIBUTEX
               S'
ATTS24   DC    C'24,'
ATTS31   DC    C'31,'
ATTSANY  DC    C'ANY,'
ATTSPAGE DC    C'PAGE,'
ATTSNONE DC    C'NONE'
ATTSAPFX DC    C'APF=?'
ATTSAPF1 DC    C'APF=1'
ATTSUNK  DC    C'?UNKNOWN'
*PDS232I NAME     ALIASOF   CREATED  SIZE SSI      ATTRIBUTES
*PDS232I 12345678 A2345678 86/01/16 9999K 12345678
*PDS232I                                           AC=1,
*PDS232I                                           A24,
*PDS232I                                           A31,
*PDS232I                                           AANY,
*PDS232I                                           R24,
*PDS232I                                           RANY,
*PDS232I                                           NONE **
*PDS232I                                           LOAD,
*PDS232I                                           NOT EDIT,
*PDS232I                                           NOT EXEC,
*PDS232I                                           NOTF,
*PDS232I                                           NOVS,
*PDS232I                                           RENT,
*PDS232I                                           REUS,
*PDS232I                                           REFR,
*PDS232I                                           DC,
*PDS232I                                           TEST,
*PDS232I                                           OVLY,
*PDS232I                                           SCTR,
         TITLE 'P D S -- PDS BROWSE/EDIT/ISPF/ISPMODE/MEMLIST 5/12/86'
***********************************************************************
***     BROWSE SUBCOMMAND      ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***     EDIT SUBCOMMAND        ADDED BY BRUCE LELAND -- JULY, 1982  ***
***                                                                 ***
***     ISPF SUBCOMMAND        ADDED BY STEVE SMITH  -- MAY,  1984  ***
***                                                                 ***
***     ISPMODE SUBCOMMAND     ADDED BY STEVE SMITH  -- AUG., 1984  ***
***                                                                 ***
***     MEMLIST SUBCOMMAND     ADDED BY STEVE SMITH  -- SEPT, 1984  ***
***********************************************************************
*
         SPACE 1
BROWSE   CSECT
         USING *,R8
         AIF ('&CISP' EQ 'ISPFV2').NSPF150
         LA    R1,L730                  ASSUME ALREADY UNDER SPF
         TM    FLAGSFF,FSPFOPT6         CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
.NSPF150 ANOP
         OI    ##ADRPA#,$A+$D           SET TO CANCEL STAX EXIT
         LA    R1,L731                  ASSUME SPF DID NOT INITIALIZE
         TM    FLAGSFF,FSPFERR          CORRECT?
         BO    MSGNEWXX                 YES, BRANCH
         MVC   MSGTEXT1(8),##SUBCAL     SPF SERVICE REQUESTED
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    SPFINIT                  YES, BRANCH
         CLI   #SPFOLD,X'01'            "NEW" SPECIFIED?
         BNE   SPFMEMOK                 NO, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     SPFMEMOK                   04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
SPFMEMOK TM    ##ADRPC#,@I             BROWSE OR EDIT?
         BNO   SPFINIT                 NO, ALLOW IT
         LA    R1,DIRNAME-1            START OF MEMBER NAME
         LA    R0,8                    MAXIMUM MEMBER NAME
         SPACE 1
SPFMEM2  LA    R1,1(,R1)               SCAN
         CLI   0(R1),C'.'                  FOR
         BE    BADMEMB                        FIRST
         BCT   R0,SPFMEM2                          PERIOD
         CLI   DIRNAME,X'EF'           NUMERIC FIRST CHARACTER?
         BH    BADMEMB                 YES, INVALID
         SPACE 1
SPFMEM4  CLC   ##SUBCOM(8),$EDI        EDIT SUBCOMMAND?
         BE    SPFMEM5                 YES, BRANCH
         CLC   ##SUBCOM(8),$SPF        SPFEDIT SUBCOMMAND?
         BNE   SPFINIT                 NO, ALLOW IT
SPFMEM5  MVC   DSNMEMQ(8),DIRNAME      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         LA    R1,L702                 ASSUME THIS IS A LOAD LIBRARY
         TM    FLAGSCC,RECFMU          CORRECT?
         BO    MSGNEWXX                YES, BRANCH
         SPACE 2
SPFINIT  TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF ENVIRONMENT INITIALIZED?
         BM    SPFLINK                    YES, CALL SPF EDIT/BROWSE
         BAL   R2,SPFRECUR                NO, INVOKE PDS AS A DIALOG
         SPACE 3
SPFLINK  ICM   R15,B'1111',ISPLINK      ISPLINK ADDRESS KNOWN?
         BNZ   SPFINTRF                 YES, BRANCH
         OI    FLAGSFF,FSPFERR          SPF ERROR
         MVI   ##ADRCM#,CONTINUE+EDITOR+FABEND
         LOAD  EP=ISPLINK               ADDRESS OF SPF INTERFACE MODULE
         ST    R0,ISPLINK               SAVE ADDRESS FOR LATER
         XI    FLAGSFF,FSPFERR          NOT AN SPF ERROR NOW
         MVI   ##ADRCM#,0
SPFINTRF DS    0H                                              SS JUL84
         STFSMODE ON,INITIAL=YES       ENTER FULLSCREEN MODE
         CLC   ##SUBCOM(8),$ISM        ISPF DISPLAY MODE SUBCD SS JUL84
         BE    SPFSETM                 YES, ALLOW IT           SS JUL84
         CLC   ##SUBCOM(8),$MML        MEMLIST SUBCOMMAND?    ABL OCT84
         BE    MEMLIST                 YES, ALLOW IT          ABL OCT84
***TEST  BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS JUL84
         SPACE 2
         XC    RECOVER,RECOVER            NO ESTAE RECOVER ATTEMPTED
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'REFRESH'           THIRD PARAMETER
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'           LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BO    *+6                        YES, BRANCH
         BALR  R14,R15                    NO, LET SPF KNOW
         SPACE 1
         TM    ##ADRPA#,$P                SELECT PANEL SUPPORT SS NOV84
         BO    SPFP1                      GO PROCESS           SS NOV84
***TEST  BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS NOV84
         CLC   ##SUBCOM(8),$ISP           ISPF SUBCOMMAND?
         BNE   SPFNOMEN                   NO, BRANCH
         LA    R0,SPFP0SEL                FIRST PARAMETER
         LA    R1,29                      16 CHARACTERS
         ST    R1,FULLWORD
         LA    R1,FULLWORD                SECOND PARAMETER
         LA    R2,MSGTEXT1                START OF STRING BYTES
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+8,X'80'           MARK END OF LIST (THIRD ITEM)
         MVC   0(33,R2),ISPPANEL          ADD PANEL(ISR@PRIM) ...
         OC    #COMMDSZ(2),#COMMDSZ       ANY PARM DATA?
         BZ    SPFCALL                    NO, BRANCH
         MVC   33(80,R2),MSGLINE          ADD THE PARM DATA
         LA    R1,34                      LENGTH OF PARAMETER STRING
         AH    R1,#COMMDSZ                LENGTH OF PARAMETER STRING
         ST    R1,FULLWORD                UPDATE THE PARAMETER LENGTH
         AR    R2,R1                      POINT TO LAST BYTE +1
         BCTR  R2,0                       POINT TO LAST BYTE
         MVI   0(R2),C')'                 LAST PAREN
         B     SPFCALL
         SPACE 2
SPFNOMEN MVI   MSGTEXT1+8,C''''           ADD A QUOTE
         MVC   MSGTEXT1+8+1(44),DSNAME    ADD THE DATA SET NAME
         LH    R15,DSNLEN                 DSNAME ACTUAL LENGTH
         LA    R2,MSGTEXT1+8(R15)         POINT TO CURRENT BYTE
         TM    DSORG,DS1DSGPO             NON-PARTITIONED DATA SET?
         BZ    SPFALLM                    YES, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS DESIRED?
         BNZ   SPFMEMBR                   NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP IN PROGRESS?
         BNO   SPFALLM                    NO, BRANCH
         SPACE 1
SPFMEMBR MVI   1(R2),C'('               MEMBER NAME PARENTHESIS
         MVC   2(8,R2),DIRNAME          ADD MEMBER NAME
         MVI   10(R2),X'40'             INSURE SCAN STOPS
         LA    R2,1(,R2)                SCAN
         CLI   0(R2),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R2),C')'               ADD FINAL PARENTHESIS
         SPACE 2
SPFALLM  MVI   1(R2),C''''              ADD FINAL QUOTE
         MVI   2(R2),X'40'              ADD FINAL BLANK
         AIF ('&CISP' NE 'ISPFV2').NSPF160
         CLC   ##SUBCOM(8),$BRO         BROWSE?
         BE    SPFRECV9                 YES, BRANCH
         TM    FLAGSEE,FEDREC           EDIT RECOVERY ATTEMPTED?
         BO    SPFRECV9                 YES, BRANCH
         OI    FLAGSEE,FEDREC           EDIT RECOVERY ATTEMPTED NOW
         SPACE 1
SPFRECV2 LA    R0,SPFEDREC              EDIT RECOVERY
         LA    R1,SPFQUERY              QUERY
         STM   R0,R1,MSGTEXT2           ARGUMENT LIST
         OI    MSGTEXT2+4,X'80'         END OF LIST
         LA    R1,MSGTEXT2
         L     R15,ISPLINK
         BALR  R14,R15
         SPACE 1
         CH    R15,=H'4'                ANY RECOVERY TO PERFORM?
         BNE   SPFRECV9                 NO, BRANCH
         LA    R0,=CL8'DISPLAY'         DISPLAY
         LA    R1,=CL8'P73RECOV'        EDIT RECOVERY PANEL
         STM   R0,R1,MSGTEXT2           ARGUMENT LIST
         OI    MSGTEXT2+4,X'80'         END OF LIST
         LA    R1,MSGTEXT2
         L     R15,ISPLINK
         BALR  R14,R15
         MVI   MSGTEXT2+50,C'E'         ASSUME DEFER FOR END COMMAND
         LTR   R15,R15                  CORRECT?
         BNZ   SPFRECV8                 YES, BRANCH
         MVI   MSGTEXT2+50,X'40'        ASSUME NORMAL ACTION
         LA    R1,=CL8'VDEFINE'         VDEF
         LA    R2,=CL8'PDSEDOP'         PDSEDOP
         LA    R3,MSGTEXT2+50           WHERE TO PLACE OUTPUT
         LA    R4,=CL8'CHAR'            CHARACTER
         LA    R15,8                    LENGTH 8
         ST    R15,FULLWORD
         LA    R5,FULLWORD              USE AS PARM
         STM   R1,R5,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+16,X'80'     END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R15,R15                  DOES PDSEDOP EXIST?
         BNZ   SPFRECV7                 NO, BRANCH
         SPACE 1
         LA    R1,=CL8'VGET'            VGET
         LA    R2,=CL8'PDSEDOP'         PDSEDOP
         LA    R3,=CL8'SHARED'          SHARED
         STM   R1,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+8,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         SPACE 1
SPFRECV7 LA    R2,=CL8'VDELETE'         VDELETE
         LA    R3,=CL8'PDSEDOP'         PDSEDOP
         STM   R2,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+4,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         SPACE 1
SPFRECV8 LA    R0,SPFEDREC              EDIT RECOVERY
         LA    R1,SPFPROCE              PROCESS
         CLI   MSGTEXT2+50,C'D'         DEFER?
         BNE   *+8                      NO, BRANCH
         LA    R1,SPFDEFER              YES, USE DEFER
         CLI   MSGTEXT2+50,C'E'         DEFER -- END?
         BNE   *+8                      NO, BRANCH
         LA    R1,SPFDEFER              YES, USE DEFER
         CLI   MSGTEXT2+50,C'C'         CANCEL?
         BNE   *+8                      NO, BRANCH
         LA    R1,SPFPCANC              YES, USE CANCEL
         STM   R0,R1,MSGTEXT2           ARGUMENT LIST
         OI    MSGTEXT2+4,X'80'         END OF LIST
         LA    R1,MSGTEXT2
         L     R15,ISPLINK
         BALR  R14,R15
         CH    R15,=H'20'               ERROR?
         BE    SPFRECV9                 YES, BRANCH
         CLI   MSGTEXT2+50,C'E'         DEFER -- END?
         BNE   SPFRECV2                 NO, BRANCH
SPFRECV9 DS    0H
.NSPF160 ANOP
         SPACE 1
         LA    R0,MSGTEXT1              FIRST PARAMETER
         LA    R1,MSGTEXT1+8            SECOND PARAMETER
         LA    R2,VOLUME                THIRD PARAMETER
         LA    R3,PASSWORD              FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2           SAVE PARAMETER ADDRESSES
         MVI   MSGTEXT2+12,X'80'        MARK END OF LIST (FOUR ITEMS)
         CLC   ##SUBCOM(8),$BRO         BROWSE SUBCOMMAND?
         BNE   SPFALLZ                  NO, BRANCH
         SPACE 1
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,SPFPERRO              SECOND PARAMETER
         LA    R2,SPFPRETU              THIRD PARAMETER
         STM   R0,R2,MSGTEXT2+24        SAVE ADDRESSES
         OI    MSGTEXT2+24+08,X'80'     LAST
         LA    R1,MSGTEXT2+24               PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15
         SPACE 1
         LA    R1,MSGTEXT2              PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15                  SPF INTERFACE ROUTINE
         LR    R3,R15                   SAVE ANY ERROR INDICATION
         SPACE 1
         LA    R0,=CL8'CONTROL'         FIRST PARAMETER
         LA    R1,SPFPERRO              SECOND PARAMETER
         LA    R2,SPFPCANC              THIRD PARAMETER
         STM   R0,R2,MSGTEXT2+24        SAVE ADDRESSES
         OI    MSGTEXT2+24+08,X'80'     LAST
         LA    R1,MSGTEXT2+24               PARAMETER
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R3,R3                    ANY ERROR?
         BZ    SPFRET00                 NO, BRANCH
         $TMSG L733
         B     SPFRET00
         SPACE 1
SPFALLZ  CLI   #SPFPROF,0               ANY PROFILE NAME?
         BNE   SPFMORE2                 YES, BRANCH
         MVC   #SPFPROF,SETPROF         INITIALIZE WITH DEFAULT
SPFMORE  CLC   #SPFMACR(1),#SPFPROF     ANY MACRO OR PROFILE?
         BNE   SPFMORE2                 YES,  BRANCH
         CLI   #SPFPROF,0               ANY PROFILE NAME?
         BE    SPFCALL                  NO, BRANCH
         SPACE 1
SPFMORE2 SR    R4,R4                    FIFTH PARAMETER
         SR    R5,R5                    ASSUME NO MACRO NAME
         CLI   #SPFMACR,0               CORRECT?
         BE    *+8                      NO, BRANCH
         LA    R5,#SPFMACR              SIXTH PARAMETER
         SR    R6,R6                    ASSUME NO PROFILE NAME?
         CLI   #SPFPROF,0               CORRECT?
         BE    *+8                      NO, BRANCH
         LA    R6,#SPFPROF              SEVENTH PARAMETER
         STM   R0,R6,MSGTEXT2           SAVE PARAMETER ADDRESSES
         MVI   MSGTEXT2+24,X'80'        MARK END OF LIST (SEVEN ITEMS)
         SPACE 1
SPFCALL  LA    R1,MSGTEXT2              PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15                  SPF INTERFACE ROUTINE
         ST    R15,SPFZDATA             SAVE RC FOR ISPDSPY    SS JAN86
         SPACE 1
SPFRET00 DS    0H
         AIF ('&RETURNX' EQ 'NO').RETURNX
         CLI   FLAGSSS,0                ANY RETURN TRAP SET?
         BE    SPFRET30                 NO, BRANCH
         CLC   ##SUBCOM(8),$ISP         ISPF SUBCOMMAND?
         BNE   SPFRET20                 NO, BRANCH
         MVI   FLAGSSS,0                YES, TRAP IS RESET
         B     SPFRET30
         SPACE 1
SPFRET20 CLC   ##SUBCOM(1),FLAGSSS      THIS SERVICE AGAIN?
         MVI   FLAGSSS,0
         BNE   SPFCALL                  NO, CALL IT AGAIN
         SPACE 1
SPFRET30 TM    SPFLAG0,SPFDON           ISPF DIALOG TABLES?
         BNO   SPFRET35                 NO, BRANCH
         CLI   PDSENTRY+3,0             ENTRY TO PDS AS A DIALOG?
         BE    SPFRET60                 YES, DO NOT RE-INVOKE
SPFRET35 CLC   ##SUBCOM(8),$ISP         ISPF SUBCOMMAND?
         BNE   SPFRET40                 NO, BRANCH
         CH    R15,=H'4'                YES, RETURN OR EXIT OPTION?
         BNE   SPFRET60                      NO, BRANCH
         MVI   FLAGSSS,C'Z'                  YES, SET RE-INVOKE TRAP
         B     SPFRET80
         SPACE 1
SPFRET40 LA    R1,=CL8'VDEFINE'         VDEF
         LA    R2,=CL8'ZVERB'           ZVERB
         LA    R3,MSGTEXT2+50           WHERE TO PLACE OUTPUT
         LA    R4,=CL8'CHAR'            CHARACTER
         LA    R15,8                    LENGTH 8
         ST    R15,FULLWORD
         LA    R5,FULLWORD              USE AS PARM
         STM   R1,R5,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+16,X'80'     END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R15,R15                  DOES ZVERB EXIST?
         BNZ   SPFRET50                 NO, BRANCH
         SPACE 1
         LA    R1,=CL8'VGET'            VGET
         LA    R2,=CL8'ZVERB'           ZVERB
         LA    R3,=CL8'SHARED'          SHARED
         STM   R1,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+8,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         LTR   R15,R15                  DOES ZVERB EXIST?
         BNZ   SPFRET50                 NO, BRANCH
         SPACE 1
         CLC   MSGTEXT2+50(3),SPFPRETU  RETURN KEY?
         BNE   SPFRET50                 NO, BRANCH
         MVC   FLAGSSS(1),##SUBCOM      SET THE TRAP CHARACTER
SPFRET50 LA    R2,=CL8'VDELETE'         VDELETE
         LA    R3,=CL8'ZVERB'           ZVERB
         STM   R2,R3,MSGTEXT2+20        PARAMETER LIST
         OI    MSGTEXT2+20+4,X'80'      END OF LIST
         LA    R1,MSGTEXT2+20           START OF PARAMETER LIST
         L     R15,ISPLINK
         BALR  R14,R15
         B     SPFRET80
         SPACE 1
SPFRET60 MVI   FLAGSSS,0                RESET THE TRAP CHARACTER
         SPACE 1
SPFRET80 DS    0H
.RETURNX ANOP
***TEST  OI    SPFLAG2,SPFNPOS            NO POSITIONING       SS AUG85
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'SM'                THIRD PARAMETER
         LA    R3,=F'1'                   FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+12,X'80'          LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BO    *+6                        YES, BRANCH
         BALR  R14,R15                    NO, LET SPF KNOW
         NI    ##ADRPA#,FF-$A-$D        DO NOT IGNORE ATTENTION NOW
         SPACE 1
         ESTAE 0                        CANCEL THE ESTAE ADDRESS
         MVC   STAEPARM(4),STAELIST     RESET THE ESTAE ADDRESS
         L     R2,=V(STAEEXIT)          ESTAE EXIT ADDRESS
         ESTAE (R2),CT,PARAM=(R7),MF=(E,STAEPARM)
         B     NEWCMD                   ***TEST
         MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
         SPACE 1
         MVI   ##ADRCM#,EDITOR          NO REPEATED WARNING MESSAGE
         L     R1,X'21C'                --> CURRENT TCB
         L     R1,X'00C'(,R1)           --> TIOT
         LA    R1,24(,R1)               TIOENTRY
SPFNTIOT CLI   0(R1),0                  END OF TIOT?
         BE    RESTART0                 YES, REALLOCATE AND OPEN AGAIN
         CLC   4(8,R1),DDNAME           THIS DDNAME?
         BE    RESTART2                 YES, JUST OPEN AGAIN
         SR    R15,R15
         IC    R15,0(,R1)
         LA    R1,0(R15,R1)
         B     SPFNTIOT
SPFSETM  DS    0H                       DO FUNCTION            SS JUL84
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?       ABL OCT84
         BO    SPFSETMM                 YES, BRANCH           ABL OCT84
         OI    SPFLAG0,SPFDON           ISPMODE ACTIVE NOW     SS JUL84
         NI    FLAGSBB,FF-FONESHOT      ALWAYS CONTINUE       ABL OCT84
         TM    SPFLAG0,SPFDSUSP         ISPMODE SUSPENDED?    ABL OCT84
         BO    SPFSETMM                 YES, BRANCH           ABL OCT84
         NI    SPFLAG0,255-SPFDSUSP     NO LONGER SUSPENDED   ABL OCT84
         OI    SPFLAG3,SPFCHN           FORCE MSG TO BE LOGGED SS SEP84
         MVC   INSERT#1(8),PDSNAME                            ABL OCT84
         $TMSG L100$1                                         ABL OCT84
         $MESAGE MSGBLANK               ONE BLANK LINE        ABL OCT84
         L     R15,=A(DSNAMES)          DO A "DSN" SUBCOMMAND ABL SEP85
         BALR  R2,R15                                         ABL OCT85
         SPACE 3
SPFSETMM NI    SPFLAG0,255-SPFDSUSP     SET SUSPEND MODE OFF   SS JUL84
         NI    SPFLAG3,255-SPFCLIST     END CLIST INTERCEPT    SS NOV84
         TM    #ISPLIST,1               LIST OPTION?          ABL OCT85
         BNO   NEWCMD                   NO, BRANCH            ABL OCT85
         MVC   INSERT#1(L'ISPLISTK),ISPLISTK  ADD PROTOTYPE   ABL OCT85
         MVC   INSERT#1(8),##SUBCOM     SUBCOMMAND NAME       ABL OCT85
         SR    R0,R0                                          ABL OCT85
         ICM   R0,B'0011',SPFCKSZ       CHECKPOINT SIZE       ABL OCT85
         CVD   R0,DOUBLE                                      ABL OCT85
         MVC   INSERT#2(11),ISPEMASK                          ABL OCT85
         ED    INSERT#2(6),DOUBLE+5                           ABL OCT85
         LA    R1,INSERT#2                                    ABL OCT85
         LA    R1,1(,R1)                                      ABL OCT85
         CLI   0(R1),X'40'              SCAN FOR FIRST        ABL OCT85
         BE    *-8                        NON-BLANK           ABL OCT85
         MVC   INSERT#1+ISPLIST2(6),0(R1)                     ABL OCT85
         SPACE 1
         SR    R0,R0                                          ABL OCT85
         ICM   R0,B'0011',SPFMAX        MAXIMUM COMMANDS      ABL OCT85
         CVD   R0,DOUBLE                                      ABL OCT85
         MVC   INSERT#2(11),ISPEMASK                          ABL OCT85
         ED    INSERT#2(6),DOUBLE+5                           ABL OCT85
         LA    R1,INSERT#2                                    ABL OCT85
         LA    R1,1(,R1)                                      ABL OCT85
         CLI   0(R1),X'40'              SCAN FOR FIRST        ABL OCT85
         BE    *-8                        NON-BLANK           ABL OCT85
         MVC   INSERT#1+ISPLIST3(6),0(R1)                     ABL OCT85
         SPACE 1
         SR    R0,R0                                          ABL OCT85
         ICM   R0,B'0011',SPFSIZE       TABLE SIZE            ABL OCT85
         CVD   R0,DOUBLE                                      ABL OCT85
         MVC   INSERT#2(11),ISPEMASK                          ABL OCT85
         ED    INSERT#2(6),DOUBLE+5                           ABL OCT85
         LA    R1,INSERT#2                                    ABL OCT85
         LA    R1,1(,R1)                                      ABL OCT85
         CLI   0(R1),X'40'              SCAN FOR FIRST        ABL OCT85
         BE    *-8                        NON-BLANK           ABL OCT85
         MVC   INSERT#1+ISPLIST4(6),0(R1)                     ABL OCT85
         SPACE 1
         MVI   MTHIGHL,L'ISPLISTK       MESSAGE LENGTH        ABL OCT85
         $TMSG L192$1                   ISSUE MESSAGE         ABL OCT85
         B     NEWCMD                                         ABL OCT84
         SPACE 3
*                                                              SS NOV84
*    SELECT PANEL SUPPORT INTERFACE                            SS NOV84
*                                                              SS NOV84
SPFP1    DS    0H                                              SS NOV84
         TM    ##ADRPA#,$J                MEMBERS PRESENT      SS NOV84
         BO    SPFP10                     PROCESS WITHOUT      SS NOV84
         TM    DSORG,DS1DSGPO             NON-PARTITIONED DATA SET?
         BZ    SPFP10                     YES, BRANCH
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS DESIRED?
         BNZ   SPFP5                      NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP IN PROGRESS?
         BNO   SPFP10                     NO, BRANCH
SPFP5    MVC   SPFSAVM2,DIRNAME           SET CURRENT MEMBER   SS AUG85
         B     SPFP20                                          SS NOV84
SPFP10   MVI   SPFSAVM2,C' '                                   SS AUG85
         MVC   SPFSAVM2+1(7),SPFSAVM2     BLANK                SS AUG85
SPFP20   DS    0H                                              SS NOV84
         LH    R1,#COMMDSZ                GET ADDED DATA LEN   SS NOV84
         STH   R1,SPFZLEN                 SAVE LEN (EVEN ZERO) SS NOV84
         LTR   R1,R1                      IS IT ZERO           SS NOV84
         BZ    SPFP22                     NO DATA              SS NOV84
         LA    R1,MSGLINE                 GET ADDR             SS NOV84
         ST    R1,SPFZDATA                SAVE ADDR ZCMD DATA  SS NOV84
SPFP22   DS    0H                                              SS NOV84
         MVC   SPFSAVP2,##SUBCAL          COPY PANEL ID        SS AUG85
         MVI   SPFLAG1,SPFPAND            CODE                 SS NOV84
         L     R15,=V(ISPDSPY)                                 SS NOV84
         BALR  R14,R15                                         SS NOV84
         NOP   0                                               SS NOV84
         CLC   0(4,R1),SPFP0END           END KEY              SS NOV84
         BE    SPFP30                     YES, BRANCH         ABL DEC84
         LR    R2,R1                      3RD PARAMETER        SS NOV84
         LA    R0,SPFP0SEL                FIRST PARAMETER      SS NOV84
         LA    R1,250                     BUFFER 250 CHARS     SS NOV84
         ST    R1,FULLWORD                                     SS NOV84
         LA    R1,FULLWORD                SECOND PARAMETER     SS NOV84
         STM   R0,R2,MSGTEXT2             SAVE ADDRESSES       SS NOV84
         OI    MSGTEXT2+8,X'80'           MARK END OF LIST     SS NOV84
***TEST  BAL   R2,CLOSEIT                 CLOSE THE INPUT DATA SS NOV84
SPFP30   B     SPFCALL                                         SS NOV84
SPFP0END DC    C'END '                                         SS NOV84
SPFP0SEL DC    CL8'SELECT'
SPFPERRO DC    C'ERRORS '
SPFPRETU DC    C'RETURN '
SPFPCANC DC    C'CANCEL '
SPFEDREC DC    CL8'EDREC'
SPFQUERY DC    CL8'QUERY'
SPFDEFER DC    CL8'DEFER'
SPFPROCE DC    CL8'PROCESS'
ISPLISTK DC    C'ISPMODE  CHKMAX(N)     CMDMAX(N)     TBLMAX(N)       '
*SPLIST2 EQU     456789012345678901234567890123456789012345678901234
ISPLIST2 EQU    16
ISPLIST3 EQU    30
ISPLIST4 EQU    44
ISPEMASK DC    X'4020202021205D40404040'   EDIT MASK
         SPACE 5                                               SS NOV84
*************************************************************  SS SEP84
*                                                              SS SEP84
*        ADDED SUPPORT OF DIALOG MEMLIST MODE USING            SS SEP84
*        A DIALOG TABLE TO DISPLAY PDS MEMBER LIST             SS SEP84
*                                                              SS SEP84
*************************************************************  SS SEP84
         SPACE 2
MEMLIST  DS    0H                                              SS SEP84
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         TM    SPFLAG0,SPFDON      ISPMODE ALREADY ACTIVE?    ABL OCT84
         BO    MEMLIST2            YES, BRANCH                ABL OCT84
         OI    SPFLAG0,SPFDON      ISPMODE IS ACTIVE NOW      ABL OCT84
         NI    FLAGSBB,FF-FONESHOT ALWAYS CONTINUE            ABL OCT84
         TM    SPFLAG0,SPFDSUSP    ISPMODE SUSPENDED?         ABL OCT84
         BO    MEMLIST2            YES, BRANCH                ABL OCT84
         NI    SPFLAG0,FF-SPFDSUSP NO LONGER SUSPENDED        ABL OCT84
         OI    SPFLAG3,SPFCHN      FORCE MSG TO BE LOGGED     ABL OCT84
         MVC   INSERT#1(8),PDSNAME                            ABL OCT84
         $TMSG L100$1                                         ABL OCT84
         $MESAGE MSGBLANK               ONE BLANK LINE        ABL OCT84
         L     R15,=A(DSNAMES)          DO A "DSN" SUBCOMMAND ABL OCT84
         BALR  R2,R15                                         ABL OCT84
MEMLIST2 DS    0H                                             ABL OCT84
         MVI   SPFLAG1,SPFADD      REQUEST ADD TO MEMLIST      SS SEP84
         L     R15,=V(ISPDSPY)                                 SS SEP84
         BALR  R14,R15                                         SS SEP84
         NOP   NEWCMD              IGNORE INTERRUPT RETURN     SS SEP84
         B     NEWCMD                                          SS SEP84
         TITLE 'P D S  --  PDS CLEAR                          5/12/86'
***********************************************************************
***      CLEAR   SUBCOMMAND  (SCREEN CLEAR FOR 3270 SCREENS)        ***
***********************************************************************
*
         SPACE 1
CLEAR    CSECT
         USING *,R8
         B     CLEAR0              NORMAL CLEAR
         B     CLEAR1              WAKE UP SESSION MANAGER
         SPACE 1
CLEAR0   ICM   R0,B'1111',PAGESIZE 3270 DISPLAY SCREEN?
         BZ    CLEAR2              NO, JUST WRITE A BLANK LINE
         SPACE 2
         TM    SPFLAG0,SPFDON      ISPMODE ON?
         BO    CLEAR2              YES, JUST WRITE A BLANK LINE
         L     R1,16               --> CVT
         L     R1,0(,R1)           --> TCB WORDS
         L     R1,8(,R1)           --> CURRENT USER ASCB
         L     R1,X'6C'(,R1)       --> ASXB
         L     R1,X'14'(,R1)       --> LWA
         CLC   X'94'+1(3,R1),=F'0' SESSION MANAGER TMP ACTIVE?
         BNE   CLEAR2              YES, JUST WRITE A BLANK LINE
         AIF ('&CONVTAM' EQ 'TCAMONLY').NCONV10
         STFSMODE ON               TURN ON FULLSCREEN MODE
.NCONV10 ANOP
         SPACE 1
         AIF ('&CONVTAM' EQ 'VTAMONLY').NCONV20
         LA    R0,12               SET SIZE FOR TPUT
         LA    R1,=X'F1115D7E114040133C404000'
         ICM   R1,B'1000',=X'03'   FULLSCR OPTION BITS
         TPUT  (1),(0),R           FULL SCREEN TPUT TO CLEAR THE SCREEN
.NCONV20 ANOP
         SPACE 1
CLEAR1   TCLEARQ INPUT             CLEAR INPUT QUEUE FOR SYNCRONIZATION
         AIF ('&CONVTAM' EQ 'TCAMONLY').NCONV30
         STLINENO LINE=1,MODE=OFF  TURN OFF FULL SCREEN MODE
.NCONV30 ANOP
         SPACE 1
         STFSMODE OFF              TURN OFF FULL SCREEN MODE
         STTMPMD  OFF              WAKE UP SESSION MANAGER
         BR    R2
         SPACE 1
CLEAR2   $MESAGE MSGBLANK          WRITE A BLANK LINE ONLY
         BR    R2
         TITLE 'P D S  --  PDS CHANGE                         5/12/86'
***********************************************************************
***      CHANGE SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
CHANGE   CSECT
         USING *,R8
         L     R1,=A(DSNAMES)           ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD              EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE        FLAG TO CONTINUE
         SPACE 2
CHANGE2  L     R8,=A(CHANGE)            BASE REGISTER
         OI    FLAGSJJ,FCHANGEB         CHANGE DATA SET BUFFERING
         NI    FLAGSFF,FF-FCHANGE       DATA SET WAS CHANGED
         LA    R0,RESTART0              ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         OI    SPFLAG3,SPFCHN           SIGNAL ISPMODE         SS SEP84
         BAL   R2,SHUTSTW2              DELETE ANY DATA SET ENQUEUES
         BAL   R2,CLOSEIT               CLOSE THE DATA SET
         BAL   R2,DEALLDCB              FREE THE DATA SET
         TM    FLAGSAA,FOPTIONS         ANY OPERANDS?
         BO    RESTART0                 YES, BRANCH
         SPACE 2
         OI    FLAGSBB,FNOTFILE         NO FILE NAME NOW
         CLI   SAVDSN+1+2,0             ANY OTHER DSNAME?
         BE    RESTART0                 NO, DONE
         SPACE 2
         XC    SAVDSN+1(SAVLEN),DSNLEN  EXCHANGE
         XC    DSNLEN(SAVLEN),SAVDSN+1          OLD AND NEW
         XC    SAVDSN+1(SAVLEN),DSNLEN                     DSNAMES
         MVC   DSPALLOC,SAVDSN          GET PREVIOUS DISPOSITION
         MVC   SAVDSN(1),DSPREQST       SAVE REQUESTED DISPOSITION
         MVC   DSPREQST,DSPALLOC        GET PREVIOUS DISPOSITION
         B     RESTART0                 BRANCH TO REALLOCATE
         TITLE 'P D S  --  PDS COMPARE                        5/12/86'
***********************************************************************
***      COMPARE SUBCOMMAND    ADDED BY BRUCE LELAND --  MAY, 1984  ***
***********************************************************************
*
         SPACE 1
COMPARE  CSECT
         USING *,R8
         SPACE 1
*
*  MSGLINE
*      OFFSET    FIELD      DESCRIPTION
*     --------  ---------  -------------
*     0-99      OPERANDS   ANY OPERANDS AFTER THE NAMES
*MSGDS1  EQU   MSGLINE+100,64
*     +0,56     DSNAME1    THE FIRST DATA SET NAME STRING
*     +61,1     FLAG       X'80' IF MEMBER ONLY
*     +62,2     LSTR1      LENGTH OF STRING
*                         
*MSGDS2  EQU   MSGLINE+164,64
*     +0,56     DSNAME2    THE SECOND DATA SET NAME STRING
*     +61,1     FLAG       X'80' IF MEMBER ONLY
*     +62,2     LSTR2      LENGTH OF STRING
*
*
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MSGDS1          FIRST PART OF DSNAME1
         CLI   MSGDS1+63,8             MORE THAN 8 DIGITS?
         BH    COMPA10                 YES, BRANCH
         CLI   MSGDS1,C''''            QUOTED?
         BE    COMPA10                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA10                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA02  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA10                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA10                 YES, BRANCH
         BCT   R0,COMPA02              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFLNCD         LINE COMMAND?           SS AUG85
         BO    COMPA07                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS1+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA07  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA10                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
COMPA10  MVC   DIRNAME,MSGDS2          FIRST PART OF DSNAME2
         CLI   MSGDS2+63,8             MORE THAN 8 DIGITS?
         BH    COMPA20                 YES, BRANCH
         CLI   MSGDS2,C''''            QUOTED?
         BE    COMPA20                 YES, BRANCH
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   COMPA20                 NO, BRANCH
         LA    R1,DIRNAME-1            START OF SEARCH -1
         LA    R0,8                    CHARACTERS TO SEARCH
         SPACE 1
COMPA12  LA    R1,1(,R1)               NEXT CHARACTER
         CLI   0(R1),C'.'              PERIOD?
         BE    COMPA20                 YES, BRANCH
         CLI   0(R1),C'('              PARENTHESIS?
         BE    COMPA20                 YES, BRANCH
         BCT   R0,COMPA12              CHECK ALL 8 CHARACTERS
         SPACE 1
         OI    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME
         TM    SPFLAG2,SPFLNCD         LINE COMMAND?           SS AUG85
         BO    COMPA17                 YES, BRANCH
         SPACE 1
         LH    R0,MSGDS2+62            MEMBER NAME LENGTH
         BCTR  R0,0                    MACHINE LENGTH
         STCM  R0,B'0011',MEMBERD+1    CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 2
COMPA17  BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COMPA20                   00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
COMPA20  MVC   DIRNAME(8),MSGDS1       FIRST MEMBER NAME
         MVC   MSGTEXT1+4(8),##SUBCAL  PROCESSOR TO ATTACH
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         SPACE 2
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,MSGTEXT1+13(R15)     POINT TO CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
         TM    MSGDS1+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA22                 YES, BRANCH
         LA    R3,MSGTEXT1+13          POINT TO CURRENT BYTE
         MVC   0(57,R3),MSGDS1         ADD THE FIRST DATA SET NAME
         AH    R3,MSGDS1+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA27
         SPACE 1
COMPA22  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA22                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 2
COMPA27  MVC   DIRNAME(8),MSGDS2       SECOND MEMBER NAME
         TM    MSGDS2+61,X'80'         USE THIS AS A MEMBER NAME?
         BO    COMPA29                 YES, BRANCH
         MVC   0(57,R3),MSGDS2         ADD THE SECOND DATA SET NAME
         AH    R3,MSGDS2+62            ADD ITS LENGTH
         MVI   0(R3),X'40'             ADD A BLANK
         LA    R3,1(,R3)
         B     COMPA37
         SPACE 2
COMPA29  MVI   0(R3),C''''             ADD A QUOTE
         MVC   1(44,R3),DSNAME         ADD THE DATA SET NAME
         LH    R15,DSNLEN              DSNAME LENGTH
         LA    R3,0(R3,R15)            CURRENT BYTE -1
         SPACE 1
         MVI   1(R3),C'('              MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME         MEMBER NAME
         LA    R3,2+8(,R3)             END OF MEMBER +1
         LA    R2,8                    MAXIMUM MACHINE LENGTH+1
         SPACE 1
COMPA30  BCTR  R3,0                    SCAN
         CLI   0(R3),X'40'                  FOR
         BNE   *+8                              FIRST
         BCT   R2,COMPA30                             NON-BLANK
         SPACE 1
         BCTR  R2,0                    MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM     <<EXECUTED>>
         EX    R2,*-6                  VALID MEMBER NAME?
         BNZ   BADMEMB                 NO, ERROR
         CLI   DIRNAME,C'0'            VALID FIRST CHARACTER?
         BNL   BADMEMB                 NO, BRANCH
         LA    R3,1(,R3)               POINT TO TERMINATOR
         MVI   0(R3),C')'              ADD A CLOSING PARENTHESIS
         SPACE 1
         MVI   1(R3),C''''             ADD A QUOTE
         MVI   2(R3),X'40'             ADD A BLANK
         LA    R3,3(,R3)               ACCOUNT FOR ")' "
         SPACE 1
COMPA37  MVC   0(100,R3),MSGLINE       ADD ANY OTHER DATA
         AH    R3,#COMMDSZ             ADD LENGTH OF KEYWORD DATA
         SPACE 2
         LA    R2,MSGTEXT1             START OF COMMAND TEXT
         SR    R3,R2                   LENGTH OF COMMAND
         SLL   R3,16                   CLEAR BOTTOM TWO BYTES
         STCM  R3,B'1111',0(R2)        SAVE STRING TOTAL LENGTH
*        $MESAGE (R2)                  DELETE * TO OUTPUT COMMAND
         ST    R2,ADDRTEXT             COMMAND ADDRESS
         ST    R2,ADDRCBUF             COMMAND ADDRESS
         LA    R1,ADDRTEXT             COMMAND ADDRESS
         CLI   ##SUBCAL,C'%'           CLIST CALL?
         BNE   COMPA38                 NO, BRANCH
         L     R8,=A(EXEC)             YES, IMPLIED CLIST
         BR    R8
         SPACE 1
COMPA38  MVI   3(R2),9                 OPERAND OFFSET
         SPACE 1
         LA    R3,##SUBCAL             PROCESSOR TO ATTACH
         AIF ('&CC296' EQ 'YES').YESC296
COMPAZAP NOP   COMPA39                 DO NOT LINK TO COMPARE
         AGO   .NOTC296
.YESC296 ANOP
COMPAZAP B     COMPA39                 LINK TO COMPARE
.NOTC296 ANOP
***  THE FOLLOWING INTERFACE IS FOR COMPARE FROM THE CBT TAPE, FILE 300
         BAL   R2,ATTACH               ATTACH THIS COMMAND PROCESSOR
         B     NEWCMD
         SPACE 1
***  EITHER THE ABOVE OR THE FOLLOWING INTERFACE MAY BE
***  USED BY THE COMPARE COMMAND FROM THE CBT TAPE, FILE 296
COMPA39  LA    R15,WORKTBL           ***  START OF PARAMETER LIST
         MVC   0(12,R15),COMPA40     ***  SAVE RE-ENTRY CODE
         LA    R0,COMPA50            ***  START OF WRITE SUBROUTINE
         ST    R0,12(,R15)           ***  OPERAND OFFSET
         ST    R7,16(,R15)           ***  SAVE GETMAIN ADDRESS
         MVC   4(4,R2),COMPRAST      ***  SET COMPARE FLAGS
         ST    R15,8(,R2)            ***  SAVE REENTRY LIST ADDRESS
         LA    R0,NEWSTAX            ***  ABEND RECOVERY
         ST    R0,RECOVER            ***
         SPACE 2
         XC    PARMLIST(8),PARMLIST       *** INITIALIZE LINK LIST
         LINK  EPLOC=((3)),SF=(E,PARMLIST)
         B     NEWCMD
         SPACE 1
COMPA40  ST    R1,20(,R15)           ***  SAVE PARAMETER ADDRESS
         L     R15,12(,R15)          ***  RE-ENTRY POINT
         BAL   R1,0(,R15)            ***  LINK TO SUBROUTINE
         SPACE 3
COMPA50  SAVE  (14,12)               ***
         LR    R8,R15                ***  BASE REGISTER ADDRESS
         USING COMPA50,R8            ***  BASE REGISTER NOTIFICATION
         L     R7,4(,R1)             ***  WORK AREA ADDRESS
         L     R1,8(,R1)             ***  OUTPUT MESSAGE START
         LM    R9,R12,BASES          ***  RESTORE BASE REGISTERS
         LA    R15,VALSAVE           ***  SAVE AREA FOR THIS ROUTINE
         ST    R13,4(,R15)           ***
         ST    R15,8(,R13)           ***  CHAIN SAVE AREAS
         LR    R13,R15               ***
         MVC   MSGTEXT2+4(80),1(R1)  ***
         LA    R1,79+4               ***  LENGTH
         SLL   R1,16                 ***  CLEAR OUT OTHER BYTES
         ST    R1,MSGTEXT2           ***  SET IN THE MESSAGE
         LA    R1,MSGTEXT2           ***  MESSAGE ADDRESS
        $PUTLINE (R1),ATTN=COMPA58   ***  OUTPUT THIS MESSAGE
         SR    R15,R15               ***  SUCCESSFUL OUTPUT
         B     COMPA59               ***  RETURN
COMPA58  LA    R15,4                 ***  SET REGISTER R15
COMPA59  L     R13,4(,R13)           ***  UNCHAIN SAVE AREAS
         ST    R15,16(R13)           ***  SAVE RETURN CODE
         RETURN (14,12)              ***  RETURN
COMPRAST DC    C'****'
         TITLE 'P D S  --  PDS COMPRESS                       5/12/86'
***********************************************************************
***      COMPRESS SUBCOMMAND   ADDED BY BRUCE LELAND -- APR., 1984  ***
***********************************************************************
*
         SPACE 1
COMPRESS CSECT
         USING *,R8
         SPACE 1
         CLI   #CMXFLAG,0                 LIST OPTION SET?
         BNE   *+8                        YES, BRANCH
         MVI   #CMXFLAG,&COPYLST          NO, USE THE DEFAULT
         MVI   VOLALLOC,X'40'             READ THE DSCB AGAIN
         CLI   DSPALLOC,ALLOOLD           EXCLUSIVE USE ALREADY?
         BE    CMXSYSDD                   YES, BRANCH
         CLI   #CMXSHR,1                  SHR COMPRESS DESIRED?
         BE    CMXSYSDD                   YES, BRANCH
*  ALLOCATE THE DATA SET TO COMPRESS AS "OLD"
         BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         MVI   DSPALLOC,ALLOOLD           WANT EXCLUSIVE USE NOW
         L     R15,=V(ALLOCATE)           ALLOCATION ROUTINE ADDRESS
         BALR  R14,R15                    ALLOCATE THE DATA SET?
         B     RESTART0                   NO, ERROR
         SPACE 1
         L     R8,##ADRCMD                REESTABLISH THE BASE REGISTER
         CLI   DSPALLOC,ALLOOLD           OLD ALLOCATION NOW?
         BE    CMXSYSDD                   YES, BRANCH
         CLI   #CMXSHR,1                  SHR COMPRESS DESIRED?
         BNE   RESTART2                   NO, QUIT
         SPACE 2
*  ALLOCATE COMPRESS SYSIN FILE
CMXSYSDD XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(20),CMXSYSIN    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         MVC   #CMXDDIN(8),#CMXSYS3+6     MOVE IN THE DSNAME
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   CMXERROR                   NO, BRANCH
         SPACE 3
*  ALLOCATE COMPRESS SYSPRINT DATA SET
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(4),CMXSYSIN     ASSUME DUMMY TEXT IS DESIRED
         CLI   #CMXFLAG,3                 NOLIST?
         BNE   *+8                        NO, BRANCH
         LA    R5,4(,R5)                  YES, USE THE DUMMY TEXT
         MVC   S99TUPTR+4(40),CMXSYSPR    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),CMXPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         AR    R15,R15                    MAXIMUM MEMBERS*2
         SR    R14,R14
         D     R14,=F'50'                 50 MEMBERS/PAGE
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDPR(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   CMXERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COMPRESS SYSUT3 DATA SET
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),CMXDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(24),CMXSYSUT    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),CMXPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         MH    R15,=H'105'
         SR    R14,R14
         D     R14,=F'100'                TOTAL SPACE IN BLOCKS
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDT3(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BZ    CMXSTAX                    YES, BRANCH
         SPACE 2
CMXERROR ST    R15,DAIRRC                 SAVE RETURN CODE
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         LA    R14,M99RB                  POINTER TO SVC 99 BLOCK
         LA    R15,DAIRRC                 POINTER TO RETURN CODE
         LA    R0,ADDRFF02                POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)           POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL                POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP             INITIALIZE DFDAPLP, DFDRCP,
*                                           DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                    PROBLEM WITH IKJEFF18?
         BZ    CMXERR2                    NO, BRANCH
         SPACE 1
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         $TMSG L835$1
         SPACE 1
CMXERR2  BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         B     CMXRETRN
         SPACE 3
CMXSTAX  $TMSG L480                       COMPRESS IN PROGRESS MESSAGE
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         SPACE 1
         XC    WORKTBL,WORKTBL            CLEAR THE TEMPORARY TABLE
         STAX  COMPNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(PDS480W,PDS480WL),MF=(E,WORKTBL)
         LA    R1,ZERO                    NO FIRST PARAMETER
         ST    R1,#CMXLIST
         LA    R1,#CMXDDNA                SECOND PARAMETER
         ST    R1,#CMXLIST+4
         OI    #CMXLIST+4,X'80'           MARK END OF LIST
         XC    #CMXDDNA(34),#CMXDDNA      CLEAR FIRST PORTION OF LIST
         MVI   #CMXDDNA+1,80              LENGTH OF DDNAME LIST
         MVC   #CMXDDT1,DDNAME            SYSUT1 DDNAME
         MVC   #CMXDDT2,DDNAME            SYSUT2 DDNAME
         BAL   R6,RESERVE                 RESERVE DEVICE IF REQUIRED
         XC    #SUBHOLD(28+20),#SUBHOLD
         LA    R14,#SUBHOLD+28            POINT TO FLAGS
         LA    R15,CMXCOPY                POINT TO CALLED PROGRAM
         LA    R0,#SUBHOLD+28+4           POINT TO PROGRAM NAME LENGTH
         LA    R1,#SUBHOLD+28+8           POINT TO RETURN CODE
         LA    R2,#SUBHOLD+28+12          POINT TO REASON CODE
         LA    R3,#SUBHOLD+28+16          POINT TO ABEND CODE
         LA    R4,#CMXLIST                POINT TO PARAMETER LIST
         STM   R14,R4,#SUBHOLD            UPDATE IKJEFTSR PARM LIST
         OI    #SUBHOLD+24,X'80'          INDICATE THE END OF THE LIST
         MVI   #SUBHOLD+28+3,X'02'        A PROGRAM IS BEING INVOKED
         MVI   #SUBHOLD+28+4+3,8          PROGRAM NAME LENGTH IS 8
         SPACE 1
COMPRZAP LA    R0,3239  **TO BE ZAPPED    PASSWORD
         SVC   247      **TO BE ZAPPED    GET AUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),CMXEFTSR       INVOKE IKJEFTSR?
         BNE   COMPNLNK                   NO, BRANCH
         LA    R1,#SUBHOLD                POINT TO THE PARAMETER LIST
         SPACE 2
COMPNLNK BAL   R2,ATTACH                  SPFCOPY/IEBCOPY/$PDSFAST
COMPLINK SR    R0,R0                      UNAUTHORIZATION CODE
         SVC   247      **TO BE ZAPPED    GET UNAUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         SPACE 3
         BAL   R2,DEQ                     RELEASE ANY RESERVES
         BAL   R2,CLOSEIT                 CLOSE THE DATA SET
         BAL   R2,DEALLDCB                UNALLOCATE THE DATA SET
         LA    R0,RESTART0                ABEND RECOVERY
         ST    R0,RECOVER                               RESTART ADDRESS
         STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         CLC   ##SUBCAL(8),CMXEFTSR       INVOKE IKJEFTSR?
         BNE   CMXTSROK                   NO, BRANCH
         MVC   SUBSECB+3(1),#SUBHOLD+28+11  COPY FINAL RETURN CODE
         CLI   SUBSECB+3,8                RC:8
         BL    CMXTSROK                   LOW, VALID COMPRESS
         BE    *+12                       EQUAL, ATTENTION
         CLI   SUBSECB+3,X'FF'            RETURN CODE = FF?
         BNE   CMXTSROK                   NO, BRANCH
         $TMSG L980                       ATTENTION WITH IKJEFTSR
         B     CMXRETRN
         SPACE 1
CMXTSROK CLI   SUBSECB+3,12               RETURN CODE > 12?
         BH    CMXNOPEN                   YES, BRANCH
         CLI   #CMXFLAG,3                 NOLIST REQUESTED?
         BNE   CMXOPEN                    NO, BRANCH
CMXNOPEN MVI   MTHIGHL+4,3
         SR    R15,R15
         ICM   R15,B'0001',SUBSECB+3
         CVD   R15,DOUBLE
         MVC   INSERT#1(8),##SUBCOM       SUBCOMMAND NAME
         MVC   INSERT#2-1(4),=X'40202120'
         ED    INSERT#2-1(4),DOUBLE+6
         MVI   INSERT#2,C'='
         $TMSG L171$2
         B     CMXRETRN
         SPACE 3
CMXOPEN  MVC   STOWDCB(CMXDL),CMXDCB      GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+STOWDCB(8),#CMXDDPR
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB),MF=(E,OPENLIST)      OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  SUCCESSFUL OPEN?
         BO    CMXMSGIN                       YES, BRANCH
         $TMSG L865                           NO STATUS AVAILABLE
         B     CMXRETRN                       EXIT
         SPACE 3
*  PROCESS SYSPRINT RECORDS
         SPACE 1
CMXMSGIN GET   STOWDCB                    GET LIST RECORD
         CLI   1(R1),X'40'                HEADER LINE?
         BE    CMXMSGIN                   YES, IGNORE
         CLI   #CMXFLAG,2                 FULL LIST REQUESTED?
         BE    CMXMSGON                   YES, OUTPUT
         LA    R3,CMXEXCL-6               START OF EXCLUSION TABLE -6
CMXMSGCK LA    R3,6(,R3)                  NEXT MESSAGE ID
         CLC   1(6,R1),0(R3)              MESSAGE ID TO BE EXCLUDED?
         BE    CMXMSGIN                   YES, IGNORE
         CLI   0(R3),FF                   END OF TABLE?
         BNE   CMXMSGCK                   NO, LOOP
         SPACE 3
CMXMSGON MVC   MSGTEXT1(136),MSGBL132     BLANK THE MESSAGE LINE
         CLC   1(6,R1),CMXIEB14           UNUSED TRACKS MESSAGE?
         BNE   *+8                        NO, BRANCH
         MVI   MSGTEXT1+1,64              YES, TRUNCATE THE MESSAGE
         MVC   MSGTEXT1+4(120),1(R1)      ADD THE IEBCOPY MESSAGE
         $MESAGE MSGTEXT1                 OUTPUT THE MESSAGE
         B     CMXMSGIN
         SPACE 3
CMXEXCL  DC    C'IEB152'           IEB152I MEMBERNAME COMPRESSED-WAS AL
         DC    C'IEB154'           IEB154I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB167'           IEB167I FOLLOWING MEMBER(S) COPIED F
         DC    C'IEB161'           IEB161I COMPRESS TO BE DONE USING IN
* FOLLOWING ARE FOR PDSFAST, VERSION 3.2 OR LATER:
         DC    C'COPYRI'           COPYRIGHT  ... PDSFAST HEADER LINE
         DC    C'PDF101'           PDF101I START PDSFAST EXECUTION
         DC    C'PDF104'           PDF104I START COPY STEP 1
         DC    C'PDF110'           PDF110I OPERATION IS COMPRESS - INDD
         DC    C'PDF121'           PDF121I NO CONTROL CARDS FOUND ON DD
         DC    C'PDF156'           PDF156M MEMBER ........ MOVED
         DC    C'PDF189'           PDF189I END OF COPY STEP 1, CONDITIO
         DC    C'PDF195'           PDF195I END OF CONTROL CARD FILE
* FOLLOWING ARE FOR PDSFAST, VERSION 3.1 OR EARLIER:
         DC    C'PDSFAS'           PDSFAST   ... HEADER LINE
         DC    C'PDF00 '           PDF00  START EXECUTION
         DC    C'PDF06 '           PDF06   INPUT DATASET:  ...
         DC    C'PDF09 '           PDF09  MEMBER ........ - MOVED ...
         DC    X'FF'
         SPACE 2
*** THE FOLLOWING MESSAGES ARE ALSO CANDIDATES FOR SUPPRESSION:
CMXIEB14 DC    C'IEB144'           IEB144I THERE ARE XXXXXXX UNUSED TRA
***      DC    C'IEB147'           IEB147I END OF JOB X WAS HIGHEST RET
***      DC    C'IEB149'           IEB149I THERE ARE XXXXXXX UNUSED DIR
***      DC    C'IEB153'           IEB153I ALL MEMBERS COMPRESSED-ALL W
         DC    X'FF'
         SPACE 2
CMXMDONE DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (STOWDCB),MF=(E,OPENLIST)  CLOSE THE SYSPRINT FILE.
         SPACE 2
CMXRETRN $MESAGE MSGBLANK
         LA    R1,#CMXDDIN                SYSIN DDNAME
         BAL   R2,CMXFREDD                FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDPR                SYSPRINT DDNAME
         BAL   R2,CMXFREDD                FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDT3                SYSUT3 DDNAME
         BAL   R2,CMXFREDD                FREE THIS DATA SET
         B     RESTART0
         SPACE 1
CMXFREDD CLI   0(R1),X'41'                ANY DDNAME ALLOCATED?
         BLR   R2                         NO, RETURN
         SPACE 1
         MVC   #CMXSYS3+6(8),0(R1)        DDNAME TO FREE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYS3                TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),CMXFDDNA       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         LA    R0,CMXUNALC                UNALLOC EVEN IF "PERM"
         ST    R0,S99TUPTR+4              ADD THIS ELEMENT TOO
         OI    S99TUPTR+4,X'80'           MARK END OF THE TEXT LIST
         SPACE 1
         DYNALLOC
         BR    R2
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
COMPNULL BR    14                      JUST RETURN TO THE RETURN
         SPACE 2
*  COMPRESS CONSTANTS
         PRINT NOGEN
CMXDCB   DCB   DSORG=PS,DDNAME=CMX,MACRF=GL,EODAD=CMXMDONE
CMXDL    EQU   *-CMXDCB
         PRINT GEN
         SPACE 2
PDS480W  DC    C'PDS480W COMPRESS MAY NOT BE INTERRUPTED'
PDS480WL EQU   *-PDS480W
         SPACE 2
CMXEFTSR DC    CL8'IKJEFTSR'
CMXCOPY  DC    CL8'IEBCOPY'
CMXDDNAM DC    X'0055000100084040404040404040'
CMXFDDNA DC    X'000100010008'
CMXDUMMY DC    X'00240000'                       DUMMY DATA SET
CMXBLKSI DC    X'0030000100020050'               DCB=BLKSIZE=80
CMXNEW   DC    X'00040001000104'                 DISP=(NEW)
CMXDEL   DC    X'00050001000104'                 DISP=(,DELETE)
CMXCDEL  DC    X'00060001000104'                 DISP=(,,DELETE)
CMXPERM  DC    X'00520000'                       PERMANENT ALLOCATION
CMXUNALC DC    X'00070000'                       UNALLOCATE PERMANENT
         SPACE 2
         SPACE 2
CMXPRTFI DC    X'000A00010003',AL3(*-*)          SPACE=(,NNN)
CMXPRTSE DC    X'000B00010003',AL3(*-*)          SPACE=(,(,NNN))
CMXPRTIN DC    X'00090001000300181B'             SPACE=(6171)
CMXUT3IN DC    X'000900010003000050'             SPACE=(80)
         SPACE 1
CMXPRTRE DC    X'004900010001',XL1'94'           DCB=RECFM=FBA
CMXPRTLR DC    X'004200010002',AL2(121)          DCB=LRECL=121
CMXPRTBL DC    X'003000010002',AL2(6171)         DCB=BLKSIZE=6171
         SPACE 2
CMXSYSIN DC    A(CMXDUMMY,CMXBLKSI,CMXNEW,CMXDEL),X'80',AL3(CMXCDEL)
CMXSYSUT DC    A(CMXPRTFI,CMXPRTSE,CMXUT3IN)
         DC    A(CMXNEW,CMXDEL),X'80',AL3(CMXCDEL)
CMXSYSPR DC    A(CMXPRTFI,CMXPRTSE,CMXPRTIN)
         DC    A(CMXPRTRE,CMXPRTLR,CMXPRTBL)
         DC    A(CMXNEW,CMXDEL,CMXCDEL),X'80',AL3(CMXPERM)
         TITLE 'P D S  --  PDS CONTROL                        5/12/86'
***********************************************************************
***      CONTROL SUBCOMMAND    ADDED BY BRUCE LELAND -- OCT., 1983  ***
***********************************************************************
*
         SPACE 1
CONTROL  CSECT
         USING *,R8
         AIF ('&CISP' EQ 'NO SPF').NSPF200
         TM    FLAGSEE,FBKGRND               BACKGROUND MODE?
         BO    CONT004                       YES, BRANCH
         TM    FLAGSFF,FSPFOPT6+FSPFERR+FSPFCALL+FSPFDIAL  CHANGE SPF?
         BNZ   CONT004                                     NO, BRANCH
         TM    #OUT+10,X'01'                 SYSOUT/DSNAME?
         BNO   CONT004                       NO, BRANCH
         BAL   R2,SPFRECUR                   INVOKE PDS AS A DIALOG
         SPACE 1
.NSPF200 ANOP
         SPACE 2
CONT004  CLI   #OUT+10,0                     SYSOUT/NOSYSOUT/DSN/NODSN?
         BE    CONT010                       NO, BRANCH
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN?
         BZ    CONT010                       NO, BRANCH
*** CLOSE THE SYSOUT/DSNAME DATA SET
         NI    FLAGSFF,FF-FLOGWRT         TURN OFF RECORD FLAG
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         $TMSG L081
         SPACE 2
         CLI   CONTOPTN,X'03'             DISK LOG OPEN?
         BNE   CONT010                    NO, BRANCH
         LH    R1,DSNLEN                  DSNAME LENGTH
         BCTR  R1,0                       MACHINE LENGTH
         LA    R15,FIRST4K
         CLC   DSNAME(*-*),CONTDSN-FIRST4K(R15)
         EX    R1,*-6                     THIS DATA SET?
         BNE   CONT010                    NO, BRANCH
         SPACE 1
         MVI   ##ADRCM#,CONTINUE          FLAG TO CONTINUE
         MVI   VOLALLOC,X'40'             READ THE DSCB AGAIN
         BAL   R2,CLOSEIT                 CLOSE THE DCB
         B     RESTART2                   OPEN THE DATA SET AGAIN
         SPACE 2
CONT010  $MESAGE MSGBLANK          ONE BLANK LINE
         MVC   INSERT#1(8),PDSNAME
         $TMSG L100$1
         $MESAGE MSGBLANK          ONE BLANK LINE
         SPACE 1
         CLI   #OUT+0,1            ANY CONTROL OPERAND?
         BL    CONT020             NO, BRANCH
         BH    CONT012             YES, THE NO FORM
         NI    FLAGSGG,FF-FALINCON YES FORM
         B     CONT020
CONT012  OI    FLAGSGG,FALINCON    NO FORM
         SPACE 1
CONT020  CLI   #OUT+1,1            ANY CONTROL OPERAND?
         BL    CONT030             NO, BRANCH
         BH    CONT022             YES, THE NO FORM
         NI    FLAGSGG,FF-FTRANCON YES FORM
         B     CONT030
CONT022  OI    FLAGSGG,FTRANCON    NO FORM
         SPACE 1
CONT030  CLI   #OUT+2,1            ANY CONTROL OPERAND?
         BL    CONT040             NO, BRANCH
         BH    CONT032             YES, THE NO FORM
         NI    FLAGSGG,FF-FLKEDCON YES FORM
         B     CONT040
CONT032  OI    FLAGSGG,FLKEDCON    NO FORM
         SPACE 1
CONT040  CLI   #OUT+3,1            ANY CONTROL OPERAND?
         BL    CONT050             NO, BRANCH
         BH    CONT042             YES, THE NO FORM
         NI    FLAGSGG,FF-FPROMCON YES FORM
         B     CONT050
CONT042  OI    FLAGSGG,FPROMCON    NO FORM
         SPACE 1
CONT050  CLI   #OUT+4,1            ANY CONTROL OPERAND?
         BL    CONT060             NO, BRANCH
         BH    CONT052             YES, THE NO FORM
         NI    FLAGSGG,FF-FRECVCON YES FORM
         B     CONT060
CONT052  OI    FLAGSGG,FRECVCON    NO FORM
         SPACE 1
CONT060  CLI   #OUT+5,1            ANY CONTROL OPERAND?
         BL    CONT100             NO, BRANCH
         CLI   #OUT+5,2            ANY CONTROL OPERAND?
         BE    CONT062             YES, THE NO FORM
         NI    FLAGSEE,FF-FDOUBCON-FFULLTRK  YES FORM
         XI    FLAGSEE,FDOUBCON    DOUBLE BUFFER FORM
         CLI   #OUT+5,1            CONTROL DOUBLE?
         BE    CONT100             YES, BRANCH
         XI    FLAGSEE,FDOUBCON    NO, FULLTRACK REQUEST
         B     CONT100
CONT062  OI    FLAGSEE,FDOUBCON+FFULLTRK     NO FORM
         SPACE 2
CONT100  MVI   MTHIGHL,100
         MVC   INSERT#1(128),BLANK128
         LA    R1,INSERT#1-2       START OF GLOBAL TEXT
         SPACE 1
         TM    FLAGSGG,FALINCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CALINCON,R1),CALINCON
         LA    R1,L'CALINCON(R1)
         SPACE 1
         TM    FLAGSGG,FLKEDCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON,R1),CLKEDCON
         LA    R1,L'CLKEDCON(R1)
         SPACE 1
         TM    FLAGSGG,FPROMCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CPROMCON,R1),CPROMCON
         LA    R1,L'CPROMCON(R1)
         SPACE 1
         TM    FLAGSGG,FRECVCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CRECVCON,R1),CRECVCON
         LA    R1,L'CRECVCON(R1)
         SPACE 1
         TM    FLAGSGG,FTRANCON    CONTROL NO FORM?
         BNO   *+14                NO, BRANCH
         MVC   2(L'CTRANCON,R1),CTRANCON
         LA    R1,L'CTRANCON(R1)
         SPACE 2
         TM    FLAGSGG,FALINCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CALINCON-2,R1),CALINCON+2
         LA    R1,L'CALINCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FLKEDCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CLKEDCON-2,R1),CLKEDCON+2
         LA    R1,L'CLKEDCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FPROMCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CPROMCON-2,R1),CPROMCON+2
         LA    R1,L'CPROMCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FRECVCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CRECVCON-2,R1),CRECVCON+2
         LA    R1,L'CRECVCON-2(R1)
         SPACE 1
         TM    FLAGSGG,FTRANCON    CONTROL YES FORM?
         BNZ   *+14                NO, BRANCH
         MVC   2(L'CTRANCON-2,R1),CTRANCON+2
         LA    R1,L'CTRANCON-2(R1)
         SPACE 1
         MVC   0(8,R1),BLANKS      DELETE THE LAST COMMA
         $TMSG L030$1
         SPACE 2
         MVC   INSERT#1(120),BLANK128
         CLI   #OUT+13,X'02'              NODEST?
         BE    *+12                       YES, BRANCH
         CLI   #OUT+15,0                  ANY DEST?
         BE    CONT112                    NO, BRANCH
         MVC   CONTDEST(2),#OUT+14        DEST LENGTH
         MVC   CONTDEST+2(8),#OUT+30      DEST
         SPACE 1
CONT112  CLI   #OUT+12,X'02'              NOFORM?
         BE    *+12                       YES, BRANCH
         CLI   #OUT+17,0                  ANY FORM?
         BE    CONT114                    NO, BRANCH
         MVC   CONTFORM(2),#OUT+16        DEST LENGTH
         MVC   CONTFORM+2(4),#OUT+38      DEST
         SPACE 1
CONT114  CLI   #OUT+10,0                  ANY SWITCHED LOG?
         BE    CONT116                    NO, BRANCH
         MVC   CONTOPTN(1),#OUT+10        CURRENT OPTION
         CLI   CONTOPTN,X'02'             NOSYSOUT?
         BNE   *+8                        NO, BRANCH
         MVI   CONTOPTN,0                 YES, CONVERT TO NULL
         CLI   CONTOPTN,X'04'             NODSNAME?
         BNE   *+8                        NO, BRANCH
         MVI   CONTOPTN,0                 YES, CONVERT TO NULL
         SPACE 2
CONT116  CLI   CONTOPTN,X'03'             DSNAME?
         BNE   CONT124                    NO, BRANCH
         LA    R14,FIRST4K                BASE REGISTER
         LA    R14,CONTDSN-FIRST4K(,R14)  POINT TO THE CONTDSN
         CLI   #OUT+19,0                  ANY DSNAME?
         BE    CONT120                    NO, USE PREVIOUS
         MVC   0(60,R14),BLANK128         BLANK CONTDSN
         MVC   0(44,R14),#OUT+44          DSNAME
         LH    R1,#OUT+18                 DSNAME LENGTH
         LA    R15,0(R1,R14)              POINT TO START OF MEMBER
         CLI   #OUT+21,0                  ANY MEMBER?
         BE    CONT118                    NO, BRANCH
         MVI   0(R15),C'('                BEGINNING (
         MVC   1(8,R15),#OUT+88           MEMBER NAME
         AH    R15,#OUT+20                MEMBER LENGTH
         MVI   1(R15),C')'                ENDING )
         LA    R15,2(,R15)                CURRENT LENGTH OF NAME
         SPACE 1
CONT118  MVI   0(R15),C')'                TO MATCH DSN( ...
         MVC   2(3,R15),CLOGSHR           SHR
         CLI   #OUT+11,X'02'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGMOD           MOD
         CLI   #OUT+11,X'03'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGNEW           NEW
         CLI   #OUT+11,X'04'              CORRECT?
         BE    CONT120                    YES, BRANCH
         MVC   2(3,R15),CLOGOLD           OLD
         SPACE 1
CONT120  MVC   INSERT#1(7),CLOGDSN        DSNAME KEYWORD
         MVC   INSERT#1+7(60),0(R14)
         B     CONT129
         SPACE 2
CONT124  LA    R15,INSERT#1
         CLI   CONTOPTN,0                 ANY CURRENT OPTIONS?
         BH    CONT126                    YES, BRANCH
         MVC   0(9,R15),CLOGNDSN          ADD NODSNAME,
         LA    R15,10(,R15)
         MVC   0(9,R15),CLOGNSYS          ADD NOSYSOUT,
         LA    R15,10(,R15)
         B     CONT127
         SPACE 1
CONT126  MVC   0(7,R15),CLOGSYS           ADD SYSOUT(
         MVC   7(1,R15),CONTSYS           ADD SYSOUT CLASS
         MVC   8(2,R15),CLOGCOMM          ADD ),
         LA    R15,11(,R15)
         SPACE 1
CONT127  MVC   0(7,R15),CLOGNFOR          ADD NOFORM,
         CLI   CONTFORM+1,0               ANY FORM?
         BNE   *+12                       YES, BRANCH
         LA    R15,8(,R15)
         B     CONT128
         MVC   0(5,R15),CLOGFOR           ADD FORM(
         MVC   5(4,R15),CONTFORM+2        ADD FORM NAME
         LH    R1,CONTFORM                LENGTH
         LA    R15,5(R1,R15)
         MVC   0(2,R15),CLOGCOMM          ADD ),
         LA    R15,3(R15)
         SPACE 1
CONT128  MVC   0(6,R15),CLOGNDES          ADD NODEST,
         CLI   CONTDEST+1,0               ANY DEST?
         BE    CONT129                    NO, BRANCH
         MVC   0(5,R15),CLOGDES           ADD DEST(
         MVC   5(8,R15),CONTDEST+2        ADD DEST NAME
         LH    R1,CONTDEST                LENGTH
         LA    R15,5(R1,R15)
         MVI   0(R15),C')'                ADD )
         SPACE 2
CONT129  DS    0H
         $TMSG L030$1
         MVC   INSERT#1(120),BLANK128
         MVC   INSERT#1(8),CSINGCON       ASSUME SINGLE BUFFERING
         TM    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL SINGLE?
         BO    CONT130                    YES, BRANCH
         MVC   INSERT#1(8),CDOUBCON       ASSUME DOUBLE BUFFERING
         TM    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL DOUBLE?
         BM    CONT130                    YES, BRANCH
         MVC   INSERT#1(8),CMULTCON       READ MULTIPLE
         SPACE 2
CONT130  MVI   MTHIGHL,8
         $TMSG L031$1
         $MESAGE MSGBLANK                 ONE BLANK LINE
         SPACE 2
CONT134  TM    CONTOPTN,1                    ANY LOG RECORDING?
         BNO   CONT200                       NO, BRANCH
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN ALREADY
         BO    CONT200                       YES, BRANCH
***  ALLOCATE THE SYSOUT/DATA SET
         NI    FLAGSFF,FF-FLOGWRT            TURN OFF RECORD FLAG
         SPACE 1
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'CONDDNAM,R3),CONDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDSORG,R3),CONDSORG         SPECIFY DSORG TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDSORG(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFREE,R3),CONFREE           SPECIFY FREE TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFREE(,R3)                 START OF NEXT TEXT
         SPACE 1
CONT140  CLI   CONTOPTN,X'03'                    DSNAME ALLOCATION?
         BNE   CONT150                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDSNAM,R3),CONDSNAM         SPECIFY DSNAME TEXT
         MVC   6(44,R3),#OUT+44                  SPECIFY DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+88,X'41'                     ANY MEMBER?
         BL    CONT141                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONMEMBR,R3),CONMEMBR         SPECIFY MEMBER TEXT
         MVC   6(8,R3),#OUT+88                   SPECIFY MEMBER
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONMEMBR(,R3)                START OF NEXT TEXT
         SPACE 1
CONT141  CLI   #OUT+96,X'41'                     ANY PASSWORD?
         BL    CONT142                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONPASSW,R3),CONPASSW         SPECIFY PASSWORD TEXT
         MVC   6(8,R3),#OUT+96                   SPECIFY PASSWORD
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONPASSW(,R3)                START OF NEXT TEXT
         SPACE 1
CONT142  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONALLOC,R3),CONALLOC         SPECIFY DISP TEXT
         CLI   #OUT+11,2                         SHR?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONOSHR                     SPECIFY DISPOSITION
         CLI   #OUT+11,3                         MOD?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONOMOD                     SPECIFY DISPOSITION
         CLI   #OUT+11,4                         NEW?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),CONONEW                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+11,3                         MOD OR NEW?
         BL    CONT149                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFIRST,R3),CONFIRST         SPECIFY PRIMARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFIRST(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONSECON,R3),CONSECON         SPECIFY SECONDARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONSECON(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONTRK,R3),CONTRK             SPECIFY TRACK ALLOC
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONTRK(,R3)                  START OF NEXT TEXT
         SPACE 1
         CLI   #OUT+88,X'41'                     ANY MEMBER NAME?
         BL    CONT149                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDIR,R3),CONDIR             SPECIFY DIRECTORY BLKS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDIR(,R3)                  START OF NEXT TEXT
         SPACE 1
CONT149  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONNORM,R3),CONNORM           SPECIFY DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONANORM,R3),CONANORM         SPECIFY DISP=(,,KEEP)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         B     CONT159
         SPACE 1
CONT150  CLI   CONTFORM+1,0                      ANY FORM?
         BZ    CONT152                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONFORMS,R3),CONFORMS         SPECIFY FORMS TEXT
         MVC   6(4,R3),CONTFORM+2                SPECIFY FORM NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONFORMS(,R3)                START OF NEXT TEXT
         SPACE 1
CONT152  CLI   CONTDEST+1,0                      ANY DEST?
         BZ    CONT154                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONDESTS,R3),CONDESTS         SPECIFY DEST TEXT
         MVC   6(8,R3),CONTDEST+2                SPECIFY DESTINATION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'CONDESTS(,R3)                START OF NEXT TEXT
         SPACE 1
CONT154  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'CONSYSOU,R3),CONSYSOU         SPECIFY SYSOUT TEXT
         MVC   6(1,R3),CONTSYS                   SPECIFY SYSOUT CLASS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SPACE 1
CONT159  OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   DDNAMEH,WORKTBL+6        SAVE DDNAME
         LTR   R15,R15                  SUCCESSFUL?
         BZ    CONT164                  YES, BRANCH
         SPACE 2
         LA    R14,M99RB                POINTER TO SVC 99 BLOCK
         ST    R15,DAIRRC               RETURN CODE
         LA    R15,DAIRRC               POINTER TO RETURN CODE
         LA    R0,ADDRFF02              POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)         POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL              POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP           INITIALIZE DFDAPLP, DFDRCP,
*                                         DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                  PROBLEM WITH IKJEFF18?
         BZ    CONT162                  NO, BRANCH
         SPACE 2
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         $TMSG L835$1
         SPACE 1
CONT162  MVI   CONTOPTN,0               TURN OFF LOGGING
         NI    FLAGSFF,FF-FLOGWRT       TURN OFF LOGGING
         B     CONT200
         SPACE 2
CONT164  CLI   CONTOPTN,X'03'           DSNAME ALLOCATION?
         BNE   CONT180                  NO, BRANCH
         LA    R1,L781$1                ASSUME A LOGICAL ERROR
         CLI   WORKTBL+14+6,DS1DSGPO    PARTITIONED?
         BNE   CONT166                  NO, BRANCH
         CLI   #OUT+88,X'41'            ANY MEMBER NAME?
         BL    CONT190                  NO, ERROR
         B     CONT180
         SPACE 1
CONT166  CLI   WORKTBL+14+6,DS1DSGPS    SEQUENTIAL?
         BNE   CONT190                  NO, ERROR
         CLI   #OUT+88,X'40'            ANY MEMBER NAME?
         BH    CONT190                  YES, ERROR
         SPACE 2
*** OPEN THE SYSOUT/DATA SET
CONT180  MVC   LOGDCB(LLOGDCB),LOGSDCB       CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+LOGDCB(8),DDNAMEH
         MVI   OPENLIST,X'80'
         OPEN  (LOGDCB,(OUTPUT)),MF=(E,OPENLIST)
         LA    R1,L780$1
         TM    DCBOFLGS-IHADCB+LOGDCB,X'10'  OPEN ALREADY
         BNO   CONT190                       NO, ERROR
         TM    FLAGSFF,FLOGWRT               OPEN WITH GOOD VALUES?
         BO    CONT200                       YES, BRANCH
         MVI   OPENLIST,X'80'
         CLOSE (LOGDCB),MF=(E,OPENLIST)
         LA    R1,L781$1                     ERROR MESSAGE
CONT190  MVI   CONTOPTN,0                    TURN OFF ANY LOGGING
         MVC   INSERT#1(8),CTEXTLOG          LOGCOPY
         B     MSGNEW
         SPACE 2
CONT200  CLI   #OUT+7,1            CPULOOP?
         BNE   CONT300             NO, BRANCH
         $TMSG L032                YES, LOOP MESSAGE
         B     *                   LOOP
         SPACE 1
CONT300  CLI   #OUT+7,2            ABEND?
         BNE   CONT400             NO, BRANCH
         $TMSG L033                YES, ABEND MESSAGE
         B     ABEND0C1            ABEND 0C1 AT +2C
         SPACE 1
CONT400  CLI   #OUT+7,3            OUTLOOP?
         BNE   CONT500             NO, BRANCH
         $TMSG L034                YES, LOOP MESSAGE
         B     CONT400             LOOP
         SPACE 1
CONT500  CLI   #OUT+7,4            MESSAGES?
         BNE   CONT600             NO, BRANCH
         MVC   INSERT#1(8),CTEXTP1
         MVC   INSERT#2(8),CTEXTP2
         LA    R4,L000             FIRST INFORMATIONAL MESSAGE
CONT510  LR    R1,R4               POINT TO IT
         $TMSG (R1)                OUTPUT IT
         LA    R4,3(,R4)           NEXT MESSAGE
         CLI   0(R4),C'3'          DONE?
         BNH   CONT510             NO, BRANCH
         LA    R5,CONLIST          FIRST ACTION MESSAGE
CONT520  L     R1,0(,R5)           POINT TO IT
         $MESAGE (R1)              OUTPUT IT
         LA    R5,4(,R5)           NEXT MESSAGE
         CLI   0(R5),X'FF'         DONE?
         BNE   CONT520             NO, BRANCH
CONT530  LR    R1,R4               NEXT WARNING/ERROR MESSAGE
         $TMSG (R1)                OUTPUT IT
         LA    R4,3(,R4)           NEXT MESSAGE
         CLI   0(R4),C'9'          DONE?
         BNH   CONT530             NO, BRANCH
         SPACE 1
CONT600  CLI   #OUT+7,5            IOSTATS DESIRED?
         BNE   CONT700             NO, BRANCH
         LH    R1,IODONE           ** INPUT ROUTINE ENTRIES
         LA    R14,MSGCOA1
         BAL   R2,CONT900
         LH    R1,IODONEW          ** NUMBER OF TTR CHANGES
         LA    R14,MSGCOA2
         BAL   R2,CONT900
         LH    R1,IOLOGUSE         ** LOGICAL INPUTS PERFORMED
         LA    R14,MSGCOA3
         BAL   R2,CONT900
         LH    R1,IOPHYTRK         ** PHYSICAL TRACKS READ
         LA    R14,MSGCOA4
         BAL   R2,CONT900
         XC    IODONE(8),IODONE
         SPACE 1
CONT700  CLI   #OUT+7,6            TESTREAD?
         BNE   NEWCMD              NO, BRANCH
         MVC   #OUT+20(1),FLAGSEE         SAVE THE INPUT FLAGS
         $MESAGE MSGBLANK                 ONE BLANK LINE
         MVC   INSERT#1(8),CSINGCON       SINGLE BUFFERING
         $TMSG L031$1
         OI    FLAGSEE,FDOUBCON+FFULLTRK  CONTROL SINGLE
         BAL   R2,CONT710                 READ THE FIRST RECORD
         SPACE 1
         MVC   INSERT#1(8),CDOUBCON       DOUBLE BUFFERING
         $TMSG L031$1
         XI    FLAGSEE,FDOUBCON           CONTROL DOUBLE
         BAL   R2,CONT710                 READ THE FIRST RECORD
         SPACE 1
         MVC   INSERT#1(8),CMULTCON       DOUBLE BUFFERING
         $TMSG L031$1
         NI    FLAGSEE,FF-FFULLTRK-FDOUBCON
         BAL   R2,CONT710                 READ THE FIRST RECORD
         MVC   FLAGSEE(1),#OUT+20         RESET THE INPUT FLAGS
         B     NEWCMD
         SPACE 2
CONT710  MVI   STARTTR+2,1                SET TTR TO 1
         L     R15,=V(EXCP)               INPUT IT
         BALR  R14,R15
         CVD   R15,DOUBLE                 CONVERT RETURN CODE
         MVC   INSERT#1(8),BLANKS
         MVC   INSERT#1-2(4),=X'40212020' EDIT CHARACTERS
         ED    INSERT#1-2(4),DOUBLE+6     CONVERT TO DISPLAY
         STM   R2,R12,28(R13)             CONVERT CCHHR TO TTR
         LA    R2,IOBSEEK                 NEXT MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,FULLWORD                TTR OF CURRENT DIRECTORY EOF
         UNPK  INSERT#2(7),FULLWORD(4)
         TR    INSERT#2(6),TRTABLE
         MVC   INSERT#2+6(2),BLANKS
         $TMSG L035$2
         $MESAGE MSGBLANK                 ONE BLANK LINE
         BR    R2
         SPACE 2
CONT900  MVC   MSGTEXT1,0(R14)
         MVC   MSGTEXT1+4(7),MSGCOAM
         LTR   R1,R1
         BZR   R2
         CVD   R1,DOUBLE
         ED    MSGTEXT1+4(7),DOUBLE+5
         $MESAGE MSGTEXT1
         BR    R2
         SPACE 4
         USING IHADCB,R1
*** DCB OPEN EXIT
CONT980  CLC   DCBDSORG(2),ZERO              ANY DSORG?
         BNE   *+8                           YES, BRANCH
         OI    DCBDSORG,DS1DSGPS             NO, USE DSORG=PS
         TM    DCBDSORG,DS1DSGPS             DSORG=PS?
         BNOR  R14                           NO, ERROR
         CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+8                           YES, BRANCH
         MVI   DCBRECFM,DCBRECF+DCBRECBR     NO, USE RECFM=FB
         TM    DCBRECFM,DCBRECF+DCBRECV      RECFM=F. OR RECFM=V.?
         BNMR  R14                           NO, ERROR
         LA    R0,80                         DEFAULT LRECL
         TM    DCBRECFM,DCBRECV              RECFM=V.?
         BNO   *+8                           NO, BRANCH
         LA    R0,255                        DEFAULT LRECL FOR VARIABLE
         CLC   DCBLRECL(2),ZERO              ANY LRECL?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBLRECL                   NO, USE LRECL=80
         TM    DCBRECFM,DCBRECV              RECFM=V.?
         BO    CONT988                       YES, BRANCH
         CH    R0,DCBLRECL                   LRECL=80?
         BNER  R14                           NO, ERROR
CONT988  LA    R0,3120                       DEFAULT BLKSIZE
         CLC   DCBBLKSI(2),ZERO              ANY BLKSIZE?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBBLKSI                   NO, USE BLKSIZE=3120
         OI    FLAGSFF,FLOGWRT               ALL ATTRIBUTES ARE CORRECT
         BR    R14
         DROP  R1
         SPACE 2
         PRINT NOGEN
LOGSDCB  DCB   DSORG=PS,DDNAME=PDSLOG,MACRF=(PM),EXLST=CONT990
LLOGDCB  EQU   *-LOGSDCB
         PRINT GEN
         SPACE 2
CONT990  DC    0F'0',X'85',AL3(CONT980)      OPEN EXIT ONLY
         PRINT NOGEN
MSGCOA1  $MSG  ' 12,345 INPUT ROUTINE ENTRIES'
         ORG   MSGCOA1+4
MSGCOAM  DC    X'4020206B202120'
         ORG   ,
MSGCOA2  $MSG  ' 12,345 TTR CHANGES'
MSGCOA3  $MSG  ' 12,345 LOGICAL INPUTS PERFORMED'
MSGCOA4  $MSG  ' 12,345 PHYSICAL TRACKS READ'
         PRINT GEN
CALINCON DC    C'NOALIASINFO, '
CLKEDCON DC    C'NOLKEDDATE, '
CPROMCON DC    C'NOPROMPT, '
CRECVCON DC    C'NORECOVER, '
CTRANCON DC    C'NOTRANSLATOR, '
CSINGCON DC    CL8'SINGLE'
CDOUBCON DC    CL8'DOUBLE'
CMULTCON DC    CL8'MULTIPLE'
CTEXTP1  DC    CL8'<PARM#1>'
CTEXTP2  DC    CL8'<PARM#2>'
CTEXTLOG DC    CL8'LOGCOPY'
CLOGOLD  DC    CL3'OLD'
CLOGMOD  DC    CL3'MOD'
CLOGSHR  DC    CL3'SHR'
CLOGNEW  DC    CL3'NEW'
CLOGDSN  DC    CL7'DSNAME('
CLOGNDSN DC    CL9'NODSNAME,'
CLOGSYS  DC    CL7'SYSOUT('
CLOGNSYS DC    CL9'NOSYSOUT,'
CLOGFOR  DC    CL5'FORM('
CLOGNFOR DC    CL7'NOFORM,'
CLOGDES  DC    CL5'DEST('
CLOGNDES DC    CL6'NODEST'
CLOGCOMM DC    CL2'),'
CONLIST  DC    A(PDS300A)
         DC    A(PDS380A,PDS381A,PDS382A,PDS383A,PDS384A,PDS385A)
         DC    A(PDS386A,PDS390A,PDS391A,PDS392A,PDS393A,PDS394A)
         DC    A(PDS395A,PDS396A),X'FF'
         SPACE 2
CONDDNAM DC    X'0055000100084040404040404040'     RETURN DDNAME
CONDSORG DC    X'0057000100024040'                 RETURN DSORG
CONFREE  DC    X'001C0000'                         FREE=CLOSE
CONMEMBR DC    X'0003000100084040404040404040'     DSNAME
CONPASSW DC    X'0050000100084040404040404040'     PASSWORD
CONDSNAM DC    X'00020001002C'                     DSNAME
CONALLOC DC    X'00040001000101'                   DISP=(_)
CONNORM  DC    X'00050001000102'                   DISP=(,CATLG)
CONANORM DC    X'00060001000108'                   DISP=(,,KEEP)
CONTRK   DC    X'00070000'                         SPACE=(TRK,
CONFIRST DC    X'000A00010003000001'               SPACE=(,1
CONSECON DC    X'000B00010003000004'               SPACE=(,,4
CONDIR   DC    X'000C00010003000005'               SPACE=(,,,5)
CONOOLD  EQU   X'01'
CONOMOD  EQU   X'02'
CONONEW  EQU   X'04'
CONOSHR  EQU   X'08'
CONSYSOU DC    X'001800010001C1'                   SYSOUT=A
CONFORMS DC    X'001A00010004C4F1F0F1'             SYSOUT=(,,D101)
CONDESTS DC    X'005800010008D5F1F3F040404040'     DEST=R130
         PRINT NOGEN
PDS380A  $MSG  'PDS380A REENTER THE SEARCH STRING WITH DELIMITERS:'
PDS382A  $MSG  'PDS382A REENTER THE FIRST TTR:'
PDS383A  $MSG  'PDS383A REENTER THE SECOND TTR:'
PDS384A  $MSG  'PDS384A REENTER THE HEX OFFSET:'
PDS385A  $MSG  'PDS385A REENTER THE SSI DATA:'
PDS386A  $MSG 'PDS386A REENTER THE REPLACEMENT STRING WITH DELIMITERS:'
         PRINT GEN
         TITLE 'P D S  --  PDS COPY                           5/12/86'
***********************************************************************
***      COPY SUBCOMMAND       ADDED BY BRUCE LELAND -- AUG., 1985  ***
***********************************************************************
*
         SPACE 1
COPY     CSECT
         USING *,R8,R6
         LA    R6,2048
         LA    R6,2048(R6,R8)        SECOND BASE
         SPACE 1
         CLI   #CMXFLAG,0            LIST OPTION SET?
         BNE   *+8                   YES, BRANCH
         MVI   #CMXFLAG,&COPYLST     NO, USE THE DEFAULT
         TM    DSORG,DS1DSGPO        PARTITIONED INPUT?
         BO    COP2100               YES, BRANCH
         OI    #COPALI,#COPSEQ       NO, MUST USE IEBGENER
         B     COP2120               YES, BRANCH
         SPACE 2
**
**2.DELETE, SUBMIT, COPY AND REPRO USED AS LINE COMMANDS IN MEMLIST
**  IF A SUBLIST IS ACTIVE, ALL SUBLIST ENTRIES ARE SELECTED
**
**  IN   SUBS  -- FROM LIONEL DYCK
**
COP2100  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    COP2200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2110               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    COP2200               YES, BRANCH
COP2110  TM    #COPALI,#COPASS2      ASSOCIATES TOO?
         BO    COP2200               YES, BRANCH
         SPACE 1
COP2120  OI    #COPALI,#COPONE       ONLY A SINGLE MEMBER/DATA SET
         LA    R4,DDNAMEH            HOLD FOR THIS SUBCOMMAND
         MVC   0(8,R4),DIRNAME
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         B     COP2900
         SPACE 3
COP2200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         SPACE 1
COP2210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   COP2220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     COP2500
         SPACE 1
COP2220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
COP2300  TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    COP2320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    COP2320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   COP2500               NO, BRANCH
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BNO   COP2605               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   COP2605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    COP2319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     COP2315                   00 - SUCCESSFUL
         B     COP2310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
COP2310  MVC   INSERT#1(8),DIRNAME
         $TMSG L853$1                MSG - (MEMBER) NOT FOUND
         B     COP2300
         SPACE 1
COP2315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     COP2500
         SPACE 1
COP2319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
COP2320  BAL   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     COP2600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    COP2400               YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMRANG      MEMBER NAME RANGE?
         BNO   COP2340               NO, BRANCH
         TM    FLAGSAA,FMEMBER1      START ENTRY SPECIFIED?
         BNO   COP2330               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER1          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               ABOVE THIS VALUE?
         BH    COP2300               YES, BRANCH
         XI    FLAGSAA,FMEMBER1      FIRST NAME IS SATISFIED
         SPACE 2
COP2330  TM    FLAGSAA,FMEMBER2      END ENTRY SPECIFIED?
         BNO   COP2500               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER2(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               BELOW THIS VALUE?
         BL    COP2600               YES, BRANCH
         B     COP2500
         SPACE 3
COP2340  LH    R15,LMEMBER1          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER1    <<EXECUTED>>
COP2350  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    COP2360               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,COP2350            MAYBE, CHECK ALL OTHER POSITIONS
         B     COP2300               NO, IGNORE THIS ENTRY
         SPACE 2
COP2360  TM    FLAGSAA,FMEMBER2      A SECOND PATTERN TOO?
         BNO   COP2500               NO, USE THIS ENTRY
         LH    R15,LMEMBER2          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER2    <<EXECUTED>>
COP2370  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    COP2500               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,COP2370            MAYBE, CHECK ALL OTHER POSITIONS
         B     COP2300               NO, IGNORE THIS ENTRY
         SPACE 3
COP2400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     COP2410+4             SKIP FIRST ADD
COP2410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   COP2300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   COP2410               NO, BRANCH
         SPACE 3
COP2500  L     R1,#MEMPTR            START OF MEMBER LIST
         B     COP2510+4             SKIP FIRST ADD
COP2510  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   COP2580               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    COP2300               YES, IGNORE THIS ENTRY
         B     COP2510               NO, CONTINUE SEARCHING
         SPACE 2
COP2580  MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   COP2210               NO, BRANCH
         B     COP2300               YES, GET NEXT MEMBER
         SPACE 3
COP2600  TM    #COPALI,#COPASSP      ASSOCIATED MEMBERS?
         BO    COP2605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    COP2605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    COP2300               YES, BRANCH
         SPACE 1
COP2605  TM    #COPALI,#COPASSP      ASSOCIATES PHASE STARTED?
         BO    COP2700               YES, BRANCH
         TM    #COPALI,#COPASS2      ASSOCIATE MEMBERS TOO?
         BNO   COP2700               NO, BRANCH
         OI    #COPALI,#COPASSP      YES, ASSOCIATES PHASE STARTED
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     COP2300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
COP2700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         SPACE 3
COP2900  DS    0H
COP3000  ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
         MVC   #CMXDDT1,DDNAME       SYSUT1 DDNAME
*  ALLOCATE COPY SYSIN FILE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(40),COPSYSIN    ADD OTHER LIST ELEMENTS
         SPACE 1
         DYNALLOC
         MVC   #CMXDDIN(8),#CMXSYS3+6     MOVE IN THE DDNAME
         SPACE 2
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 3
*  ALLOCATE COPY SYSPRINT DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(4),COPDUMMA     ASSUME DUMMY TEXT IS DESIRED
         CLI   #CMXFLAG,3                 NOLIST?
         BNE   *+8                        NO, BRANCH
         LA    R5,4(,R5)                  YES, USE THE DUMMY TEXT
         MVC   S99TUPTR+4(40),COPSYSPR    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         AR    R15,R15                    MAXIMUM MEMBERS*2
         SR    R14,R14
         D     R14,=F'50'                 50 MEMBERS/PAGE
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDPR(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY SYSUT3 DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(24),COPSYSUT    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4              CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LH    R15,TOTUSEDX               NUMBER OF USED MEMBER BLOCKS
         MH    R15,=H'21'                 MAXIMUM MEMBERS
         MH    R15,=H'105'
         SR    R14,R14
         D     R14,=F'100'                TOTAL SPACE IN BLOCKS
         SRL   R15,4                      DIVIDED BY 16
         LA    R15,2(,R15)                ADD 2
         STCM  R15,B'0111',MSGTEXT2+6     UPDATE IN PRIMARY SPACE
         STCM  R15,B'0111',MSGTEXT2+9+6   UPDATE IN SECONDARY SPACE
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDT3(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY SYSUT4 DATA SET
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBAL           ALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTERS
         SPACE 1
         MVC   #CMXSYS3(14),COPDDNAM      REQUEST DDNAME FEEDBACK
         LA    R3,#CMXSYS3                FIRST TEXT POINTER
         USING S99TUNIT,R3
         ST    R3,S99TUPTR                POINT TO FIRST TEXT UNITS
         MVC   S99TUPTR+4(28),COPSYSU4    ADD OTHER LIST ELEMENTS
         MVC   MSGTEXT2(9+9),COPPRTFI     ADD PRIMARY, SECONDARY
         LA    R0,MSGTEXT2                START OF PRIMARY AMOUNT
         ST    R0,S99TUPTR+4+4            CHANGE ADDRESS POINTER
         LA    R0,MSGTEXT2+9              START OF SECONDARY AMOUNT
         ST    R0,S99TUPTR+4+4+4          CHANGE ADDRESS POINTER
         MVI   MSGTEXT2+9+6+2,30          SECONDARY IS 30 TRACKS
         LA    R0,COPTRK
         ST    R0,S99TUPTR+4+12           CHANGE BLK(8) TO TRACK ALLOC.
         SPACE 1
         DYNALLOC
         SPACE 2
         MVC   #CMXDDT4(8),#CMXSYS3+6     MOVE IN THE DDNAME
         DROP  R1,R3,R4,R5
         LTR   R15,R15                    SUCCESSFUL?
         BNZ   COPERROR                   NO, BRANCH
         SPACE 2
*  ALLOCATE COPY SYSUT2 DATA SET
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'COPDDNAM,R3),COPDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSORG,R3),COPDSORG         SPECIFY DSORG TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSORG(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSNAM,R3),COPDSNAM         SPECIFY DSNAME TEXT
         LA    R2,5(,R3)                         WHERE TO STORE OUTPUT
         MVC   6(44,R3),BLANK128                 CLEAR DSNAME AREA
         LA    R1,MSGDS2
         LA    R0,44
         CLI   0(R1),C''''                       QUOTED?
         BE    COP4510                           YES, BRANCH
         SPACE 1
         SR    R14,R14
         L     R15,ADDRUPT                       START OF THE UPT
         ICM   R14,B'0001',UPTPREFL-UPT(R15)     USERID LENGTH ZERO?
         BZ    COP4510+4                         YES, BRANCH
         MVC   6(7,R3),UPTPREFX-UPT(R15)         USERID
         AR    R2,R14                            POSITION FOR PERIOD
         SR    R0,R14
         MVI   1(R2),C'.'
         BCTR  R0,0
         BCTR  R1,0
         LA    R2,1(,R2)
         SPACE 1
COP4510  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    COP5000                           YES, BRANCH
         CLI   0(R1),C'('                        PARENTHESIS?
         BE    COP4520                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    COP5000                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,COP4510
         B     COP5000
         SPACE 1
COP4520  MVC   DOUBLE(8),BLANK128                (MEMBER OR DDNAME)
         LA    R0,8
         LA    R2,DOUBLE-1
         SPACE 1
COP4530  LA    R1,1(,R1)                         NEXT INPUT BYTE
         LA    R2,1(,R2)                         NEXT OUTPUT BYTE
         CLI   0(R1),C''''                       END OF DSNAME?
         BE    COP4540                           YES, BRANCH
         CLI   0(R1),C')'                        PARENTHESIS?
         BE    COP4540                           YES, BRANCH
         CLI   0(R1),X'40'                       BLANK?
         BE    COP4540                           YES, BRANCH
         MVC   0(1,R2),0(R1)                     MOVE IN ONE MORE BYTE
         BCT   R0,COP4530
         SPACE 1
COP4540  CLC   MSGDS2(4),COPLFILE                FILE?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(3),COPLFILE                FIL?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(2),COPLFILE                FI?
         BE    COP4580                           YES, BRANCH
         CLC   MSGDS2(1),COPLFILE                F?
         BE    COP4580                           YES, BRANCH
         SPACE 1
         CLI   DOUBLE,X'40'                      MEMBER NAME?
         BE    COP4560                           NO, BRANCH
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY CURRENT RENAME?
         BNZ   COP4560                           YES, IGNORE THIS
         MVC   #COPTON(8),DOUBLE                 RENAME STRING
         SR    R15,R15
         LA    R1,DOUBLE
         MVI   DOUBLE+8,X'40'                    STOP THE SCAN
         SPACE 1
COP4550  LA    R15,1(,R15)                       LENGTH COUNT
         LA    R1,1(,R1)                         CHARACTER POINTER
         CLI   0(R1),X'40'                       BLANK?
         BNE   COP4550                           NO, BRANCH
         STC   R15,#COPTO#                       SAVE LENGTH COUNT
         SPACE 1
COP4560  TM    #COPALI,#COPSEQ                   SEQUENTIAL INPUT?
         BNO   COP5000                           NO, BRANCH
         OI    #COPALI,#COPSEQ+#COPMEMB          MUST USE IEBGENER
         LA    R1,M99RBPTR                       RESET R1
         MVC   MSGDS2(44),6(R3)                  RESET THE DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPMEMBE,R3),COPMEMBE         SPECIFY MEMBER TEXT
         MVC   6(8,R3),#COPTON                   GET THE MEMBER NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPMEMBE(,R3)                START OF NEXT TEXT
         B     COP5005
         SPACE 2
COP4580  MVC   #CMXDDT2(8),DOUBLE                DDNAME ALLOCATION
         L     R1,X'21C'                         --> CURRENT TCB
         L     R1,X'00C'(,R1)                    --> TIOT
         LA    R15,24                            OFFSET TO TIOENTRY
         SPACE 1
COP4582  LA    R1,0(R15,R1)                      NEXT ENTRY
         CLI   0(R1),0                           END OF TIOT?
         BE    COP5090                           YES, BRANCH
         SR    R15,R15
         IC    R15,0(,R1)
         CLC   4(8,R1),#CMXDDT2                  THIS DDNAME?
         BNE   COP4582                           NO, BRANCH
         SPACE 1
         LA    R14,0(R15,R1)                     NEXT ENTRY
         CLC   4(8,R14),BLANKS                   CONCATENATED?
         BNE   COP5090                           NO, BRANCH
         SPACE 1
         SR    R0,R0                             NUMBER COUNT
COP4590  A     R0,=F'1'                          NEXT CONCAT
         C     R0,#COPCONC                       DDCARD TO USE?
         BNL   COP4594                           YES, BRANCH
         LR    R14,R1                            LAST CONCATENATED DD
         LA    R1,0(R15,R1)                      NEXT CONCATENATED DD
         IC    R15,0(,R1)                        NEXT LENGTH
         CLC   4(8,R1),BLANKS                    CONCATENATED?
         BE    COP4590                           YES, BRANCH
         LR    R1,R14                            NO, USE THE LAST ONE
COP4594  L     R14,16(,R1)                       UCB ADDRESS
         MVC   #COPVOL(6),28(R14)                VOL=SER=...
         ICM   R14,B'0111',12(R1)                ---> JFCB
         MVC   6(44,R3),16(R14)                  ADD DSNAME
         SPACE 2
COP5000  LA    R1,M99RBPTR                       RESET R1
         MVC   MSGDS2(44),6(R3)                  RESET THE DSNAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
COP5005  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPALLOC,R3),COPALLOC         SPECIFY DISP TEXT
         CLI   #CMXSHR,2                         SHR?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),COPOSHR                     SPECIFY DISPOSITION
         CLI   #CMXSHR,3                         NEW?
         BNE   *+8                               NO, BRANCH
         MVI   6(R3),COPONEW                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   #COPVOL,0                         ANY VOLUME SERIAL?
         BE    COP5010                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPVOLSR,R3),COPVOLSR         VOLUME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(6,R3),#COPVOL                   VOLUME SERIAL
         LA    R3,L'COPVOLSR(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPUNIT+8,R3),COPUNIT         ADD UNIT=SYSALLDA
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPUNIT+8(,R3)               START OF NEXT TEXT
         SPACE 1
COP5010  CLI   #CMXSHR,3                         NEW?
         BL    COP5050                           NO, BRANCH
         SPACE 1
         TM    #COPALI,#COPMEMB                  MEMBER OUTPUT?
         BO    COP5011                           YES, BRANCH
         TM    #COPALI,#COPSEQ                   SEQUENTIAL INPUT?
         BO    COP5014                           YES, BRANCH
COP5011  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDIR,R3),COPDIR             SPECIFY DIRECTORY BLKS
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         ICM   R0,B'1111',#COPDIR#               ANY DIR(NUM) SPECIFED?
         BNZ   COP5012                           YES, BRANCH
         ICM   R0,B'0011',TOTALLOX               NO, ANY CURRENT COUNT?
         BNZ   COP5012                           YES, BRANCH
         LA    R0,20                             NO, JUST USE 20
COP5012  STCM  R0,B'0111',6(R3)                  UPDATE DIR COUNT
         LA    R3,L'COPDIR(,R3)                  START OF NEXT TEXT
         SPACE 1
COP5014  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPTRK,R3),COPTRK             SPECIFY TRACK ALLOC
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         TM    DS1SCALO,X'C0'                    CYLINDER ALLOCATION?
         BNO   *+8                               NO, BRANCH
         MVI   1(R3),X'08'                       YES, CONVERT TO CYL
         SR    R0,R0
         ICM   R0,B'0011',#COPSPAC               ANY PRIMARY SPACE?
         BZ    COP5018                           NO, BRANCH
         CLI   #COPTRCY,0                        TRK/CYL REQUESTED?
         BH    COP5018                           YES, BRANCH
         MVI   #COPTRCY,1                        NO, DEFAULT TO TRK
COP5018  CLI   #COPTRCY,1                        TRACK?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'07'                       YES, CONVERT TO TRK
         CLI   #COPTRCY,2                        CYLINDER?
         BNE   *+8                               NO, BRANCH
         MVI   1(R3),X'08'                       YES, CONVERT TO CYL
         LA    R3,L'COPTRK(,R3)                  START OF NEXT TEXT
         SPACE 2
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPFIRST,R3),COPFIRST         SPECIFY PRIMARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#COPSPAC              ANY PRIMARY SPACE?
         BNZ   COP5020                           YES, BRANCH
         LH    R14,DSNTOTAL                      TOTAL TRACKS ALLOC
         TM    DS1SCALO,X'C0'                    CYLINDER ALLOCATION?
         BNO   COP5020                           NO, BRANCH
         LH    R0,DEVTTRKS                       TRACKS PER CYLINDER?
         LR    R15,R0
         BCTR  R15,0
         AR    R15,R14
         SR    R14,R14
         DR    R14,R0
         LR    R14,R15                           CONVERTED TO CEIL(CYL)
COP5020  STCM  R14,B'0111',6(R3)                 UPDATE FIRST QUANTITY
         LA    R3,L'COPFIRST(,R3)                START OF NEXT TEXT
         SPACE 2
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPSECON,R3),COPSECON         SPECIFY SECONDARY
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         SR    R14,R14
         ICM   R14,B'0011',#COPSPAC              ANY PRIMARY SPACE?
         BZ    COP5026                           NO, BRANCH
         ICM   R14,B'0011',#COPSPAC+2            SECONDARY SPACE?
         B     COP5030
         SPACE 1
COP5026  ICM   R14,B'0111',DS1SCALO+1            ANY SECONDARY?
         BZ    COP5030                           NO, BRANCH
         TM    DS1SCALO,X'80'                    TRK OR CYL ALLOC?
         BO    COP5030                           YES, BRANCH
         LH    R14,BLKSI                         BLOCKSIZE
         ICM   R14,B'1000',=X'01'                R=1, K=0, D=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         SR    R14,R14
         ICM   R14,B'0111',DS1SCALO+1            SECONDARY SIZE
         LR    R15,R0
         AR    R15,R14
         SR    R14,R14
         DR    R14,R0
         LR    R14,R15                           CONVERTED TO CEIL(TRK)
COP5030  STCM  R14,B'0111',6(R3)                 UPDATE OTHER QUANTITY
         LA    R3,L'COPSECON(,R3)                START OF NEXT TEXT
         SPACE 2
COP5050  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPNORM,R3),COPNORM           DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #CMXSHR,3                         NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,KEEP)
         LA    R3,L'COPNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPANORM,R3),COPANORM         DISP=(,,DELETE)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         CLI   #CMXSHR,3                         NEW?
         BE    *+8                               YES, BRANCH
         MVI   6(R3),X'08'                       NO, USE DISP=(,,KEEP)
         SPACE 1
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   #CMXDDT2,WORKTBL+6            SAVE DDNAME
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   COPERROR                      NO, BRANCH
         SPACE 2
         CLI   WORKTBL+14+6,DS1DSGPO         PARTITIONED?
         BE    COP5079                       YES, BRANCH
         TM    #COPALI,#COPSEQ               SEQUENTIAL INPUT?
         BO    COP5080                       YES, BRANCH
         TM    #COPALI,#COPONE               SINGLE MEMBER?
         BNO   COP5078                       NO, BRANCH
         OI    #COPALI,#COPSEQ               SEQUENTIAL INPUT NOW
         SPACE 2
*  ALLOCATE COPY SYSUT1 DATA SET TO A SPECIFIC MEMBER
         XC    M99RBPTR(40),M99RBPTR    CLEAR THE WORK AREA
         LA    R1,M99RBPTR              DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND        MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20               LENGTH 20
         MVI   S99VERB,S99VRBAL         ALLOCATE
         ST    R5,S99TXTPP              POINT TO TEXT POINTERS
         LA    R3,WORKTBL                        START OF TEXT
         SPACE 1
         MVC   0(L'COPDDNAM,R3),COPDDNAM         SPECIFY DDNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDDNAM(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPDSNAM,R3),COPDSNAM         SPECIFY DSNAME TEXT
         LA    R2,5(,R3)                         WHERE TO STORE OUTPUT
         MVC   6(44,R3),DSNAME                   MOVE IN DSNAME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPDSNAM+44(,R3)             START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPMEMBE,R3),COPMEMBE         SPECIFY MEMBER TEXT
         MVC   6(8,R3),MEMNAME                   GET THE MEMBER NAME
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPMEMBE(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPALLOC,R3),COPALLOC         SPECIFY DISP TEXT
         MVI   6(R3),COPOSHR                     SPECIFY DISPOSITION
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPALLOC(,R3)                START OF NEXT TEXT
         SPACE 1
         CLI   VOLUME,X'41'                      ANY VOLUME SERIAL?
         BL    COP5060                           NO, BRANCH
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPVOLSR,R3),COPVOLSR         VOLUME TEXT
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVC   6(6,R3),VOLUME                    VOLUME SERIAL
         LA    R3,L'COPVOLSR(,R3)                START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPUNIT+8,R3),COPUNIT         ADD UNIT=SYSALLDA
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         LA    R3,L'COPUNIT+8(,R3)               START OF NEXT TEXT
         SPACE 1
COP5060  LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPNORM,R3),COPNORM           DISP=(,CATLG)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVI   6(R3),X'08'                       USE DISP=(,KEEP)
         LA    R3,L'COPNORM(,R3)                 START OF NEXT TEXT
         SPACE 1
         LA    R5,4(,R5)                         NEXT POINTER ELEMENT
         MVC   0(L'COPANORM,R3),COPANORM         DISP=(,,DELETE)
         ST    R3,S99TUPTR                       POINT TO TEXT UNIT
         MVI   6(R3),X'08'                       USE DISP=(,,KEEP)
         SPACE 1
         OI    S99TUPTR,X'80'                    MARK END OF LIST
         DYNALLOC
         SPACE 2
         DROP  R1,R4,R5
         MVC   #CMXDDT1,WORKTBL+6            SAVE DDNAME
         LTR   R15,R15                       SUCCESSFUL?
         BNZ   COPERROR                      NO, BRANCH
         B     COP5080
         SPACE 1
COP5078  $TMSG L532                          NO, MULTIPLE MEMBER UNLOAD
         B     COP5080                       DO NOT OPEN FOR BLDL
         SPACE 2
COP5079  TM    DSORG,DS1DSGPO                PARTITIONED INPUT?
         BO    COP5090                       YES, BRANCH
         TM    #COPALI,#COPMEMB              SINGLE MEMBER OUTPUT?
         BO    COP5080                       YES, BRANCH
         $TMSG L982                          NO, PARTITIONED BUT NO MEM
         B     COP7525
         SPACE 2
*** OPEN THE OUTPUT DATA SET TO GET THE UCB ADDRESS IF DISP=SHR
COP5080  OI    #COPALI,#COPSOME              SOME OUTPUT MEMBERS
         CLI   #CMXSHR,2                     DISP=SHR?
         BNE   COP6000                       NO, BRANCH
         MVC   BAMDCB(LCOPDCB),COPDCBPO      CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDT2  ADD DDNAME
         MVI   DCBDSORG-IHADCB+BAMDCB,DS1DSGPS     DSORG=PS
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  OPEN OK?
         BO    COP5084                       YES, BRANCH
         MVC   INSERT#1(8),COPYDCB           NO, ERROR
         $TMSG L780$1
         B     COP7525
         SPACE 2
COP5084  L     R1,DCBDEBAD-IHADCB+BAMDCB     DEB ADDRESS
         L     R1,DEBUCBAD(,R1)              UCB ADDRESS
         ST    R1,#SUBHOLD+48                UPDATE FOR THIS SUBCOMMAND
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         B     COP6000
         SPACE 2
*** OPEN THE OUTPUT DATA SET TO CHECK FOR MEMBER PRESENCE
COP5090  MVC   BAMDCB(LCOPDCB),COPDCBPO      CONSTRUCT A DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDT2  ADD DDNAME
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  OPEN OK?
         BO    COP5500                       YES, BRANCH
         MVC   INSERT#1(8),COPYDCB           NO, ERROR
         $TMSG L780$1
         B     COP7525
         SPACE 2
COP5500  L     R1,DCBDEBAD-IHADCB+BAMDCB     OUTPUT DEB ADDRESS
         L     R1,DEBUCBAD(,R1)              UCB ADDRESS
         ST    R1,#SUBHOLD+48                UPDATE FOR THIS SUBCOMMAND
         LH    R1,DCBBLKSI-IHADCB+BAMDCB     OUTPUT BLKSIZE
         STH   R1,#REPRBLK                   SAVE FOR LATER
         L     R3,#MEMPTR
         L     R4,#MEMLAST                   LAST VALUE
         SH    R3,=H'16'
         CLI   #COPTO#,0                     ANY REPLACE VALUE?
         BE    COP5510                       NO, BRANCH
         TM    #COPALI,#COPONE               SINGLE MEMBER - NO ALIAS?
         BNO   COP5510                       NO, BRANCH
         MVI   #COPTO#,8                     YES, SET LENGTH TO 8
         SPACE 1
COP5510  LA    R3,16(,R3)                    NEXT ELEMENT
         CR    R3,R4                         DONE?
         BNL   COP5560                       YES, BRANCH
         CLI   #COPEXST,0                    ANY EXIST/NOEXIST?
         BH    COP5511                       YES, BRANCH
         CLI   #COPREP,1                     REPLACE?
         BNE   COP5511                       NO, BRANCH
         OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 1
COP5511  MVC   DIRNAME(8),0(R3)
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#            NUMBER OF BYTES
         BZ    COP5512                       NONE, BRANCH
         BCTR  R1,0
         MVC   DIRNAME(*-*),#COPTON          <<EXECUTED>>
         EX    R1,*-6                        UPDATE NAME
         SPACE 1
COP5512  BLDL  BAMDCB,BLDLLIST
         B     *+4(R15)                      PROCESS RETURN CODE
         B     COP5520                        00 - ALREADY THERE
         B     COP5540                        04 - NOT FOUND
         B     IOERROR                        08 - I/O ERROR
         SPACE 1
COP5520  CLI   #COPEXST,1                    ANY EXIST/NOEXIST?
         BL    COP5524                       NONE, ---BRANCH
         BE    COP5522                       EXIST, --BRANCH
         MVI   0(R3),0                       NOEXIST, NULLIFY ENTRY
         B     COP5510
         SPACE 1
COP5522  OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 1
COP5524  OI    #COPALI,#COPSOME              SOME OUTPUT
         MVC   INSERT#1(8),DIRNAME
         $TMSG L852$1
         B     COP5510
         SPACE 2
COP5540  CLI   #COPEXST,1                    ANY EXIST/NOEXIST?
         BL    COP5544                       NONE, ---BRANCH
         BE    COP5542                       EXIST, --BRANCH
         OI    #COPALI,#COPSOME              NOEXIST, SOME OUTPUT
         B     COP5510
         SPACE 1
COP5542  MVI   0(R3),0                       NULLIFY ENTRY
         B     COP5510
         SPACE 1
COP5544  OI    #COPALI,#COPSOME              SOME OUTPUT
         B     COP5510
         SPACE 3
COP5560  MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         LA    R1,L245                       NO MATCHING MEMBERS FOUND
         TM    #COPALI,#COPSOME              ANY OUTPUT?
         BNO   MSGNEW                        NO, BRANCH
         B     COP6000
         SPACE 2
COPERROR ST    R15,DAIRRC                 SAVE RETURN CODE
         LA    R14,M99RB                  POINTER TO SVC 99 BLOCK
         LA    R15,DAIRRC                 POINTER TO RETURN CODE
         LA    R0,ADDRFF02                POINTER TO A(IKJEFF02)
         LA    R1,=AL2(DFSVC99)           POINTER TO INVOCATION TYPE
         L     R2,ADDRCPPL                POINTER TO THE CPPL
         STM   R14,R2,DFDAPLP             INITIALIZE DFDAPLP, DFDRCP,
*                                           DFJEFF02, DFIDP AND DFCPPLP
         SPACE 1
         LINK  EP=IKJEFF18,MF=(E,DFPARMS)
         LTR   R15,R15                    PROBLEM WITH IKJEFF18?
         BZ    COP7525                    NO, BRANCH
         SPACE 1
         CVD   R15,DOUBLE
         MVI   MTHIGHL,4
         MVC   INSERT#1(4),=X'40202120'
         ED    INSERT#1(4),DOUBLE+6
         MVI   INSERT#1,C'='
         $TMSG L835$1
         B     COP7525
         SPACE 3
COP6000  DS    0H
* OPEN SYSIN DATA SET
         MVC   BAMDCB(COPDL),COPDCB           GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDIN
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB,OUTPUT),MF=(E,OPENLIST) OPEN SYSIN
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'    SUCCESSFUL OPEN?
         BO    COP6010                         YES, BRANCH
         $TMSG L865                            NO STATUS AVAILABLE
         B     COP7525                         EXIT
         SPACE 3
*  OUTPUT SYSIN RECORDS
         SPACE 1
COP6010  TM    #COPALI,#COPSEQ             SEQUENTIAL PROCESSING?
         BO    COP6590                     YES, BRANCH
         SPACE 1
         L     R3,#MEMPTR            FIRST MEMBER POINTER
         L     R4,#MEMLAST           LAST MEMBER POINTER
         B     COP6040+4             SKIP FIRST ADD
COP6040  LA    R3,16(,R3)            NEXT MEMBER POSITION
         CR    R3,R4                 END OF MEMBER TABLE?
         BNL   COP6070               YES, NOT FOUND
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#   ANY RENAME?
         BZ    COP6070               NO, BRANCH
         BCTR  R15,0                 MACHINE LENGTH OF COMPARE
         LR    R1,R3                 NEXT MEMBER POSITION
COP6050  LA    R1,16(,R1)            NEXT MEMBER POSITION
         CR    R1,R4                 END OF MEMBER TABLE?
         BNL   COP6040               YES, NO MATCH
         MVC   DOUBLE(8),0(R3)       FIRST MEMBER NAME
         XC    DOUBLE(8),0(R1)       VERSUS SECOND MEMBER NAME
         XC    DOUBLE(*-*),DOUBLE    <<EXECUTED>>
         EX    R15,*-6               CLEAR THE FRONT FEW CHARACTERS
         OC    DOUBLE(8),DOUBLE      EXACT MATCH AFTER FIRST PART?
         BNZ   COP6050               NO, LOOP
         SPACE 1
         MVC   INSERT#1(8),0(R3)     MEMBER NAME
         MVC   INSERT#1(*-*),#COPTON <<EXECUTED>>
         EX    R15,*-6               SET THE FRONT FEW CHARACTERS
         $TMSG L885$1                ERROR MESSAGE
         OI    #COPALI,#COPDUP       MARK FOR LATER
         B     COP6040               NO, LOOP
         SPACE 1
COP6070  MVC   MSGTEXT1(136),MSGBL132      CLEAR THE MESSAGE AREA
         MVC   MSGTEXT1+4(11),COPDSN1M     " COPY    O="
         MVC   MSGTEXT1+4+11(8),#CMXDDT2
         LA    R1,MSGTEXT1+4+11
         SPACE 1
         LA    R1,1(,R1) NEXT CHARACTER
         CLI   0(R1),X'40'                 BLANK?
         BNE   *-8                         NO, LOOP
         SPACE 1
         CLI   #CMXFLAG,1                  SUMMARY LIST REQUESTED?
         BH    COP6100                     NO, BRANCH
         MVC   0(8,R1),COPLISTN            ADD ",LIST=NO"
         LA    R1,8(,R1)                   EIGHT CHARACTERS
         SPACE 1
COP6100  EQU   *
         AIF ('&COPYMOD' EQ 'NO').NOCOPYM
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP6300                     NO, BRANCH
         OC    #REPRBLK(2),#REPRBLK        ANY BLKSIZE?
         BZ    COP6300                     NO, MUST BE SEQUENTIAL
         OC    #REPRBLK+2(2),#REPRBLK+2    ANY MAXBLK PARAMETER?
         BNZ   COP6150                     YES, BRANCH
         CLC   BLKSI(2),#REPRBLK           INPUT BLKSIZE=OUTPUT?
         BE    COP6300                     YES, BRANCH
         LH    R15,#REPRBLK                COPY OUTPUT BLKSIZE
         STH   R15,#REPRBLK+2              TO MAXBLK FIELD
         SPACE 1
COP6150  MVC   MSGTEXT1+4(8),COPCOPMO      CHANGE COPY TO COPYMOD
         MVC   0(8,R1),COPMAXBL            ",MAXBLK="
         LH    R15,#REPRBLK+2              OUTPUT MAXBLK OR BLKSIZE
         CVD   R15,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)       CONVERT TO DISPLAY
         OI    DOUBLE+4,X'F0'              MAKE LAST DIGIT PRINTABLE
         MVC   0+8(5,R1),DOUBLE            MOVE TO MESSAGE
         LA    R1,8+5(,R1)                 UPDATE LENGTH
.NOCOPYM ANOP
         SPACE 1
COP6300  MVC   0(3,R1),COPDSN2M            ",I="
         MVC   3(8,R1),#CMXDDT1
         CLI   #COPREP,1                   REPLACE?
         BNE   COP6310                     NO, BRANCH
         MVC   3(2,R1),COPDSNLP            "(("
         MVC   3+2(8,R1),#CMXDDT1
         SPACE 1
         LA    R1,1(,R1) NEXT CHARACTER
         CLI   0(R1),X'40'                 BLANK?
         BNE   *-8                         NO, LOOP
         SPACE 1
         MVC   0(4,R1),COPDSNRP            ",R))"
COP6310  PUT   BAMDCB,MSGTEXT1+4           OUTPUT COPY STATEMENT
         SPACE 1
         L     R3,#MEMPTR            FIRST MEMBER POINTER
         L     R4,#MEMLAST           LAST MEMBER POINTER
         CLI   #COPNCAL,2            NOCALL?
         BNE   COP6520               NO, BRANCH
         $MESAGE MSGTEXT1            YES, OUTPUT FIRST LINE
         SPACE 1
COP6520  LA    R1,MSGTEXT1+4+5-1                 START OF MEMBER NAMES
         LA    R0,MSGTEXT1+4+60                  END OF MEMBER NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(5),COPMEM1M            " S M="
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    COP6530                           NO, BRANCH
         LA    R1,MSGTEXT1+4+5                   START OF MEMBER NAMES
         LA    R0,MSGTEXT1+4+47                  47 INSTEAD OF 60
         SPACE 1
COP6530  CR    R3,R4
         BNL   COP6580
         CLI   0(R3),0                           NULLIFIED ENTRY?
         BNE   COP6532                           NO, BRANCH
         LA    R3,16(,R3)                        YES, GET NEXT ENTRY
         B     COP6530
         SPACE 1
COP6532  SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    COP6540                           NO, BRANCH
         SPACE 1
         BCTR  R15,0                             MACHINE LENGTH
         MVI   MSGTEXT1+4+5,C'('                 BEGINNING (
         CLC   0(*-*,R3),#COPTON                 <<EXECUTED>>
         EX    R15,*-6                           SAME PREFIX?
         BE    COP6540                           YES, BRANCH
         SPACE 1
COP6535  MVI   1(R1),C'('                        BEGINNING ((
         MVC   2(8,R1),0(R3)                     INPUT MEMBER NAME
         LA    R14,3+8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R1,2(,R14)
         MVC   0(8,R1),0(R3)                     INPUT MEMBER NAME
         MVC   0(*-*,R1),#COPTON                 <<EXECUTED>>
         EX    R15,*-6                           OUTPUT MEMBER NAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C')'                       FINAL )
         LA    R14,1(,R14)
         B     COP6560
         SPACE 2
COP6540  MVC   1(8,R1),0(R3)                     ADD THE MEMBER NAME
         LA    R14,1+8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
COP6560  MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         LA    R3,16(,R3)                        NEXT MEMBER ENTRY
         CR    R1,R0                             END OF CARD?
         BNH   COP6530                           NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    *+8                               NO, BRANCH
         MVI   0(R1),C')'                        YES, FINAL ))
         PUT   BAMDCB,MSGTEXT1+4                 OUTPUT SELECT STMT
         CLI   #COPNCAL,2                        NOCALL?
         BNE   COP6570                           NO, BRANCH
         SPACE 1
         $MESAGE MSGTEXT1                        YES, OUTPUT FIRST LINE
COP6570  MVC   MSGTEXT1(136),MSGBL132
         B     COP6520
         SPACE 2
COP6580  CLI   MSGTEXT1+4+5,X'40'                ANY TEXT TO OUTPUT?
         BE    COP6590                           NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         SR    R15,R15
         ICM   R15,B'0001',#COPTO#               ANY REP NAME?
         BZ    *+8                               NO, BRANCH
         MVI   0(R1),C')'                        YES, FINAL ))
         SPACE 1
         PUT   BAMDCB,MSGTEXT1+4                 OUTPUT SELECT STMT
         CLI   #COPNCAL,2                        NOCALL?
         BNE   COP6590                           NO, BRANCH
         $MESAGE MSGTEXT1                        YES, OUTPUT FIRST LINE
         SPACE 2
COP6590  MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE SYSIN
         TM    #COPALI,#COPDUP            ANY COLLISIONS?
         BO    COP7525                    YES, BRANCH
         SPACE 2
         CLC   DSNAME(44),MSGDS2     SAME DSNAME?
         BNE   COP6620               NO, BRANCH
         CLI   #COPVOL,0             ANY VOLUME
         BE    COP6610               NO, BRANCH
         CLC   #COPVOL(6),VOLALLOC   SAME VOLUME?
         BNE   COP6620               NO, BRANCH
COP6610  $TMSG L900                  SAME DATA SET MESSAGE
         B     COP7525
         SPACE 2
COP6620  CLI   #COPNCAL,2                 NOCALL?
         BE    COP7530                    YES, BRANCH
         $TMSG L484                       COPY IN PROGRESS MESSAGE
         OI    ##ADRPA#,$A                DEFER ALL ATTENTIONS
         SPACE 1
         XC    WORKTBL,WORKTBL            CLEAR THE TEMPORARY TABLE
         STAX  COPYNULL,USADDR=(R7),REPLACE=NO,IBUF=0,                 X
               OBUF=(PDS484W,PDS484WL),MF=(E,WORKTBL)
         TM    #COPALI,#COPSEQ             SEQUENTIAL COPY?
         BNO   *+10                        NO, BRANCH
         MVC   ##SUBCAL(8),COPGENER        YES, USE IEBGENER
         LA    R1,=F'0'                   NO FIRST PARAMETER
         ST    R1,#CMXLIST
         LA    R1,#CMXDDNA                SECOND PARAMETER
         ST    R1,#CMXLIST+4
         OI    #CMXLIST+4,X'80'           MARK END OF LIST
         XC    #CMXDDNA(34),#CMXDDNA      CLEAR FIRST PORTION OF LIST
         MVI   #CMXDDNA+1,80              LENGTH OF DDNAME LIST
         XC    #SUBHOLD(28+20),#SUBHOLD
         LA    R14,#SUBHOLD+28            POINT TO FLAGS
         LA    R15,COPCOPY                POINT TO CALLED PROGRAM
         LA    R0,#SUBHOLD+28+4           POINT TO PROGRAM NAME LENGTH
         LA    R1,#SUBHOLD+28+8           POINT TO RETURN CODE
         LA    R2,#SUBHOLD+28+12          POINT TO REASON CODE
         LA    R3,#SUBHOLD+28+16          POINT TO ABEND CODE
         LA    R4,#CMXLIST                POINT TO PARAMETER LIST
         STM   R14,R4,#SUBHOLD            UPDATE IKJEFTSR PARM LIST
         OI    #SUBHOLD+24,X'80'          INDICATE THE END OF THE LIST
         MVI   #SUBHOLD+28+3,X'02'        A PROGRAM IS BEING INVOKED
         MVI   #SUBHOLD+28+4+3,8          PROGRAM NAME LENGTH IS 8
         SPACE 2
         L     R2,RECOVER
         LA    R1,COP7080                  RECOVERY ADDRESS
         ST    R1,RECOVER
         STM   R2,R8,#SUBHOLD+88           SAVE REGISTERS
         SPACE 1
         CLI   #CMXSHR,2                   SHR?
         BNE   COP7020                     NO, BRANCH
         SPACE 1
         CLI   RESERVET,C'R'               ALREADY RESERVED?
         BH    COP7020                     YES, BRANCH
         MVI   RESERVET,C'S'               MARK AS OUTPUT RESERVED
         SPACE 2
         MVC   PARMLIST(4),COPRESV         LIST FORM OF RESERVE
         RESERVE (COPEDIT,MSGDS2,E,44,SYSTEMS),RET=HAVE,              XX
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         SPACE 1
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP7020                     NO, RETURN
         SPACE 1
         L     R1,#SUBHOLD+48              UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   COP7000                     NO, BRANCH
         MVC   PARMLIST(4),COPRESV         LIST FORM OF RESERVE
         RESERVE (COPIEWLP,MSGDS2,E,44,SYSTEMS),RET=HAVE,             XX
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         B     COP7020
         SPACE 1
COP7000  MVC   PARMLIST(4),COPHAVE         LIST FORM OF RESERVE
         ENQ   (COPIEWLP,MSGDS2,E,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
COP7020  DS    0H
         SPACE 3
COPYRZAP LA    R0,3239  **TO BE ZAPPED    PASSWORD
         SVC   247      **TO BE ZAPPED    GET AUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         LA    R3,##SUBCAL                WHO TO ATTACH
         LA    R1,#CMXLIST                PARAMETERS TO PASS
         CLC   ##SUBCAL(8),COPEFTSR       INVOKE IKJEFTSR?
         BNE   COPYNLNK                   NO, BRANCH
         LA    R1,#SUBHOLD                POINT TO THE PARAMETER LIST
         SPACE 2
COPYNLNK BAL   R2,ATTACH                  SPFCOPY/IEBCOPY/$PDSFAST
COPYLINK SR    R0,R0                      UNAUTHORIZATION CODE
         SVC   247      **TO BE ZAPPED    GET UNAUTHORIZED
         ORG   *-2      -- NOTE --        OVERLAY THE SVC CALL
         NOPR  0        **TO BE ZAPPED    OVERLAY THE SVC CALL
         SPACE 3
COP7080  LM    R2,R8,#SUBHOLD+88
         ST    R2,RECOVER                  RESET RECOVERY ADDRESS
         CLI   RESERVET,C'S'               RESERVED?
         BNE   COP7520                     NO, BRANCH
         MVI   RESERVET,0                  NO LONGER RESERVED
         SPACE 2
         MVC   PARMLIST(4),COPSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (COPEDIT,MSGDS2,44,SYSTEMS),RET=HAVE,                   X
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         SPACE 2
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BNO   COP7520                     NO, RETURN
         SPACE 1
         L     R1,#SUBHOLD+48              UCB ADDRESS
         TM    UCBTYP+1(R1),UCBRR          SHARED DEVICE?
         BNO   COP7500                     NO, BRANCH
         MVC   PARMLIST(4),COPSYSS         LIST FORM, DEQ "SYSTEMS"
         DEQ   (COPIEWLP,MSGDS2,44,SYSTEMS),RET=HAVE,                  X
               UCB=#SUBHOLD+48,MF=(E,PARMLIST)
         B     COP7520
         SPACE 1
COP7500  MVC   PARMLIST(4),COPSYS          LIST FORM, DEQ "SYSTEM"
         DEQ   (COPIEWLP,MSGDS2,44,SYSTEM),RET=HAVE,MF=(E,PARMLIST)
COP7520  B     COP7530
         SPACE 1
COP7525  MVI   #COPNCAL,2                 NO CALL
COP7530  STAX  ,
         NI    ##ADRPA#,FF-$A             ALLOW ATTENTIONS AGAIN
         MVI   ATTNECB,0                  CLEAR THE ATTENTION ECB
         SPACE 1
         LA    R1,#CMXDDIN                SYSIN DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDT3                SYSUT3 DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         LA    R1,#CMXDDT4                SYSUT4 DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
         MVC   DOUBLE(8),#CMXDDT2         COPY DDNAME
         CLC   DOUBLE(3),=CL8'SYS00000'   TEMPORARY TYPE DDNAME?
         BNE   COP7540                    NO, BRANCH
         NC    DOUBLE(8),=CL8'SYS00000'   MASK WITH "SYS00000"
         CLC   DOUBLE(8),=CL8'SYS00000'   TEMPORARY TYPE DDNAME?
         BNE   COP7540                    NO, BRANCH
         LA    R1,#CMXDDT2                SYSUT2 DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
COP7540  CLC   DDNAME(8),#CMXDDT1         EXPLICIT FREE NEEDED?
         BE    COP7550                    NO, BRANCH
         LA    R1,#CMXDDT1                SYSUT1 DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         SPACE 1
**
**1.COPY COMMAND ERRORS DETECTED BY IEBCOPY GET PDS980E INTERRUPTED
**  MESSAGE INSTED OF THE IEBCOPY OUTPUT DISPLAY. IKJETSR
**  INTERFACE ONLY
**
**  IN   SUBS  -- FROM LIONEL DYCK
**
COP7550  CLI   #COPNCAL,2                 NOCALL?
         BE    COP8550                    YES, BRANCH
         CLC   ##SUBCAL(8),COPEFTSR       INVOKE IKJEFTSR?
         BNE   COP7570                    NO, BRANCH
         MVC   SUBSECB+3(1),#SUBHOLD+28+11  COPY FINAL RETURN CODE
         CLI   SUBSECB+3,12               RC:12
         BL    COP7570                    LOW, VALID COPY
         BE    *+12                       EQUAL, ATTENTION
         CLI   SUBSECB+3,X'FF'            RETURN CODE = FF?
         BNE   COP7570                    NO, BRANCH
         $TMSG L980                       ATTENTION WITH IKJEFTSR
         B     COP8550
         SPACE 1
COP7570  CLI   SUBSECB+3,12               RETURN CODE > 12?
         BH    COP7580                    YES, BRANCH
         CLI   #CMXFLAG,3                 NOLIST REQUESTED?
         BNE   COP8000                    NO, BRANCH
COP7580  MVI   MTHIGHL+4,3
         SR    R15,R15
         ICM   R15,B'0001',SUBSECB+3
         CVD   R15,DOUBLE
         MVC   INSERT#1(8),##SUBCOM
         MVC   INSERT#2-1(4),=X'40202120'
         ED    INSERT#2-1(4),DOUBLE+6
         MVI   INSERT#2,C'='
         $TMSG L171$2
         B     COP8550
         SPACE 3
COP8000  MVC   BAMDCB(COPDL),COPDCB         GET MODEL DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB(8),#CMXDDPR
         MVI   OPENLIST,X'80'
         OPEN  (BAMDCB),MF=(E,OPENLIST)     OPEN SYSPRINT
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10' SUCCESSFUL OPEN?
         BO    COP8010                      YES, BRANCH
         $TMSG L865                         NO STATUS AVAILABLE
         B     COP8550                      EXIT
         SPACE 3
*  PROCESS SYSPRINT RECORDS
         SPACE 1
COP8010  GET   BAMDCB                     GET LIST RECORD
         CLI   1(R1),X'40'                HEADER LINE?
         BNE   COP8040                    NO, BRANCH
         CLI   22(R1),X'40'               HEADER LINE?
         BE    COP8010                    YES, IGNORE
COP8040  CLI   #CMXFLAG,2                 FULL LIST REQUESTED?
         BE    COP8080                    YES, OUTPUT
         LA    R3,COPEXCL-6               START OF EXCLUSION TABLE -6
COP8060  LA    R3,6(,R3)                  NEXT MESSAGE ID
         CLC   1(6,R1),0(R3)              MESSAGE ID TO BE EXCLUDED?
         BE    COP8010                    YES, IGNORE
         CLI   0(R3),FF                   END OF TABLE?
         BNE   COP8060                    NO, LOOP
         SPACE 3
COP8080  MVC   MSGTEXT1(136),MSGBL132     BLANK THE MESSAGE LINE
         CLC   1(6,R1),COPIEB14           UNUSED TRACKS MESSAGE?
         BNE   *+8                        NO, BRANCH
         MVI   MSGTEXT1+1,64              YES, TRUNCATE THE MESSAGE
         MVC   MSGTEXT1+4(120),1(R1)      ADD THE IEBCOPY MESSAGE
         CLC   MSGTEXT1+4+1(10),BLANK128  FIRST 10 BLANKS?
         BNE   *+10                       NO, BRANCH
         MVC   MSGTEXT1+4(110),11(R1)     SHIFT THE IEBCOPY MESSAGE
         $MESAGE MSGTEXT1                 OUTPUT THE MESSAGE
         B     COP8010
         SPACE 3
COPEXCL  DC    C'IEB154'           IEB154I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB155'           IEB155I MEMBERNAME HAS BEEN SUCCESSF
         DC    C'IEB167'           IEB167I FOLLOWING MEMBER(S) COPIED F
         DC    C'IEB161'           IEB161I COMPRESS TO BE DONE USING IN
         DC    C'PDSFAS'           PDSFAST   ... HEADER LINE
         DC    C'PDF00 '           PDF00  START EXECUTION
         DC    C'PDF06 '           PDF06   INPUT DATASET:  ...
         DC    C'PDF09 '           PDF09  MEMBER ........ - MOVED ...
         DC    X'FF'
         SPACE 2
*** THE FOLLOWING MESSAGES ARE ALSO CANDIDATES FOR SUPPRESSION:
COPIEB14 DC    C'IEB144'           IEB144I THERE ARE XXXXXXX UNUSED TRA
***      DC    C'IEB147'           IEB147I END OF JOB X WAS HIGHEST RET
***      DC    C'IEB149'           IEB149I THERE ARE XXXXXXX UNUSED DIR
         DC    X'FF'
         SPACE 2
COP8500  DS    0H
         MVI   OPENLIST,X'80'
         CLOSE (BAMDCB),MF=(E,OPENLIST)   CLOSE THE SYSPRINT FILE.
         SPACE 2
COP8550  $MESAGE MSGBLANK
         LA    R1,#CMXDDPR                SYSPRINT DDNAME
         BAL   R2,COP8580                 FREE THIS DATA SET
         B     NEWSTAX
         SPACE 1
COP8580  CLI   0(R1),X'41'                ANY DDNAME ALLOCATED?
         BLR   R2                         NO, RETURN
         MVC   #CMXSYS3+6(8),0(R1)        DDNAME TO FREE
         XC    M99RBPTR(40),M99RBPTR      CLEAR THE WORK AREA
         LA    R1,M99RBPTR                DYNAMIC ALLOCATION
         USING S99RBP,R1
         SPACE 1
         LA    R4,M99RB
         USING S99RB,R4
         ST    R4,S99RBPTR
         OI    S99RBPTR,S99RBPND          MARK END OF LIST
         SPACE 1
         LA    R5,M99TUPL
         USING S99TUPL,R5
         MVI   S99RBLN,20                 LENGTH 20
         MVI   S99VERB,S99VRBUN           UNALLOCATE
         ST    R5,S99TXTPP                POINT TO TEXT POINTER
         SPACE 1
         LA    R3,#CMXSYS3                TEXT POINTER
         USING S99TUNIT,R3
         MVC   S99TUKEY(6),COPFDDNA       ADD TEXT KEYS
         ST    R3,S99TUPTR                POINT TO TEXT UNIT
         LA    R0,COPUNALC                UNALLOC EVEN IF "PERM"
         ST    R0,S99TUPTR+4              ADD THIS ELEMENT TOO
         OI    S99TUPTR+4,X'80'           MARK END OF THE TEXT LIST
         SPACE 1
         DYNALLOC
         BR    R2
         SPACE 1
         DROP  R1,R3,R4,R5
         SPACE 2
         B     RESTART0
         SPACE 2
         USING IHADCB,R1
*** DCB OPEN EXIT
***8980  CLC   DCBDSORG(2),=F'0'             ANY DSORG?
***      BNE   *+8                           YES, BRANCH
***      OI    DCBDSORG,DS1DSGPO             NO, USE DSORG=PO
COP8980  CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+10                          YES, BRANCH
         MVC   DCBRECFM(1),DCBRECFM-IHADCB+INDCB
         CLC   DCBLRECL(2),=F'0'             ANY LRECL?
         BNE   *+10                          YES, BRANCH
         MVC   DCBLRECL(2),LRECL             NO, USE INPUT DEFAULT
         CLC   DCBBLKSI(2),=F'0'             ANY BLKSIZE?
         BNE   *+10                          YES, BRANCH
         MVC   DCBBLKSI(2),BLKSI             NO, USE INPUT DEFAULT
         BR    R14
         DROP  R1
         SPACE 2
COPYNULL BR    R14
         SPACE 2
*  COPY CONSTANTS
         PRINT NOGEN
COPDCB   DCB   DSORG=PS,DDNAME=COP,MACRF=(GL,PM),EODAD=COP8500
COPDL    EQU   *-COPDCB
         SPACE 2
COPDCBPO DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=COP8990
LCOPDCB  EQU   *-COPDCBPO
COP8990  DC    0F'0',X'85',AL3(COP8980)      OPEN EXIT ONLY
         PRINT GEN
         SPACE 2
COPLFILE DC    C'FILE'
COPYDCB  DC    CL8'COPYDCB'
COPMEMBR DC    X'0003000100084040404040404040'     DSNAME
COPDSNAM DC    X'00020001002C'                     DSNAME
COPALLOC DC    X'00040001000101'                   DISP=(_)
COPNORM  DC    X'00050001000102'                   DISP=(,CATLG)
COPANORM DC    X'00060001000104'                   DISP=(,,DELETE)
COPTRK   DC    X'00070000'                         SPACE=(TRK,
COPFIRST DC    X'000A00010003000001'               SPACE=(,1
COPSECON DC    X'000B00010003000004'               SPACE=(,,4
COPDIR   DC    X'000C00010003000005'               SPACE=(,,,5)
COPOOLD  EQU   X'01'
COPONEW  EQU   X'04'
COPOSHR  EQU   X'08'
         SPACE 1
COPEFTSR DC    CL8'IKJEFTSR'
COPCOPY  DC    CL8'IEBCOPY'
COPDDNAM DC    X'0055000100084040404040404040'   RETURN DDNAME
COPMEMBE DC    X'0003000100084040404040404040'   MEMBER NAME
COPFDDNA DC    X'000100010008'                   FOR FREE DDNAME
COPDSORG DC    X'0057000100024040'               RETURN DSORG
COPDUMMY DC    X'00240000'                       DUMMY DATA SET
COPNEW   DC    X'00040001000104'                 DISP=(NEW)
COPDEL   DC    X'00050001000104'                 DISP=(,DELETE)
COPCDEL  DC    X'00060001000104'                 DISP=(,,DELETE)
COPPERM  DC    X'00520000'                       PERMANENT ALLOCATION
COPUNALC DC    X'00070000'                       UNALLOCATE PERMANENT
COPVOLSR DC    X'001000010006404040404040'       VOL=SER=
         SPACE 2
         SPACE 2
COPPRTFI DC    X'000A00010003',AL3(005)          SPACE=(,NNN)
COPPRTSE DC    X'000B00010003',AL3(005)          SPACE=(,(,NNN))
COPPRTIN DC    X'000900010003',AL3(6171)         SPACE=(6171)
COPSINSP DC    X'000900010003',AL3(3120)         SPACE=(3120)
COPUT3IN DC    X'000900010003',AL3(80)           SPACE=(80)
COPUNIT  DC    X'001500010008',CL8'SYSALLDA'     UNIT=SYSALLDA
         SPACE 1
COPPRTRE DC    X'004900010001',XL1'94'           DCB=RECFM=FBA
COPPRTLR DC    X'004200010002',AL2(121)          DCB=LRECL=121
COPPRTBL DC    X'003000010002',AL2(6171)         DCB=BLKSIZE=6171
         SPACE 2
COPSINRE DC    X'004900010001',XL1'90'           DCB=RECFM=FB
COPSINLR DC    X'004200010002',AL2(80)           DCB=LRECL=80
COPSINBL DC    X'003000010002',AL2(3120)         DCB=BLKSIZE=3120
         SPACE 2
COPDUMMA DC    A(COPDUMMY)
COPSYSIN DC    A(COPPRTFI,COPPRTSE,COPSINSP)
         DC    A(COPSINRE,COPSINLR,COPSINBL)
         DC    A(COPNEW,COPDEL,COPCDEL),X'80',AL3(COPPERM)
COPSYSU4 DC    A(COPUNIT)
COPSYSUT DC    A(COPPRTFI,COPPRTSE,COPUT3IN)
         DC    A(COPNEW,COPDEL),X'80',AL3(COPCDEL)
COPSYSPR DC    A(COPPRTFI,COPPRTSE,COPPRTIN)
         DC    A(COPPRTRE,COPPRTLR,COPPRTBL)
         DC    A(COPNEW,COPDEL,COPCDEL),X'80',AL3(COPPERM)
COPGENER DC    CL8'IEBGENER'
COPDSN1M DC    C' COPY    O='
COPCOPMO DC    C' COPYMOD'
COPDSN2M DC    C',I='
COPLISTN DC    C',LIST=NO'
COPMAXBL DC    C',MAXBLK='
COPMEM1M DC    C' S M='
COPDSNLP DC    C'(('
COPDSNRP DC    C',R))'
         SPACE 2
PDS484W  DC    C'PDS484W COPY SHOULD NOT BE INTERRUPTED'
PDS484WL EQU   *-PDS484W
         SPACE 2
COPHAVE  ENQ   (*-*,*-*,E,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   COPHAVE+4
         SPACE 7
COPRESV  RESERVE (*-*,*-*,E,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   COPRESV+4
         SPACE 3
COPSYSS  DEQ   (*-*,*-*,*-*,SYSTEMS),RET=HAVE,UCB=0,MF=L
         ORG   COPSYSS+4
         SPACE 3
COPEDIT  DC    CL8'SPFEDIT'               FOR SPF MEMBER ENQ TEST
COPIEWLP DC    CL8'SYSIEWLP'              FOR LINKAGE-EDITOR RESERVE
COPSYS   DEQ   (*-*,*-*,*-*,SYSTEM),RET=HAVE,MF=L
         ORG   COPSYS+4
         SPACE 2
#COPASSP EQU   X'80'            COPY: ASSOCIATES PHASE
#COPSEQ  EQU   X'40'            COPY: USE IEBGENER FOR THE COPY
#COPMEMB EQU   X'20'            COPY: DSNAME(MEMBER) FORM WAS ENTERED
#COPONE  EQU   X'10'            COPY: A SINGLE MEMBER
#COPSOME EQU   X'08'            COPY: SOME MEMBERS EXIST/NOEXIST
#COPDUP  EQU   X'04'            COPY: A COLLISION WAS FOUND
#COPASS2 EQU   X'01'            COPY: COPY ASSOCIATES TOO
DEBNMEXT EQU   16           OFFSET IN DEB FOR NUMBER OF EXTENTS
DEBUCBAD EQU   32           OFFSET IN DEB FOR UCB ADDRESS
UCBTYP   EQU   16           OFFSET IN UCB FOR UNIT TYPE WORD
UCBRR    EQU   X'20'        UCB IS ON A SHARED DASD DEVICE
UCBVOLI  EQU   28           OFFSET IN UCB FOR DISK VOLUME NAME
         TITLE 'P D S  --  PDS DELETE, MEMBERS                5/12/86'
***********************************************************************
***    DELETE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- OCT., 1983  ***
***                                                                 ***
***    MEMBERS SUBCOMMAND   MODIFIED BY BRUCE LELAND -- JUNE, 1985  ***
***********************************************************************
*
         SPACE 1
DELETE   CSECT
         USING *,R8
         CLC   ##SUBCOM(8),$MEM          MEMBERS SUBCOMMAND?
         BNE   *+8                       NO, BRANCH
         OI    #MEMFLAG,X'80'            YES, SET A FLAG
         CLC   ##SUBCOM(8),$SUB          SUBMIT SUBCOMMAND?
         BNE   DELE001                   NO, BRANCH
         MVC   WORKTBL(40),MSGLINE       SAVE THE OPERAND
         CLI   ##SUBCAL,C'%'             IMPLIED CLIST?
         BE    DELE998                   YES, BRANCH
         TM    DSORG,DS1DSGPO            PARTITIONED?
         BNO   DELE998                   NO, BRANCH
         OI    #MEMFLAG,X'40'            YES, SET A SUBMIT FLAG
         SPACE 1
DELE001  LA    R1,L530                   ASSUME PARTITIONED
         TM    DSORG,DS1DSGPO            CORRECT?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DELE002  TM    FLAGSII,FSINGLE+FMEMBERS  IF OR FIND THEN(DEL/MEM/SUB)?
         BZ    DELE010                   NO, BRANCH
         TM    #MEMFLAG,X'80'            MEMBERS?
         BO    DELE800                   YES, DONE
         TM    #MEMFLAG,X'40'            SUBMIT?
         BO    DELE998                   YES, BRANCH
         MVC   IODIR1+4+12+00(8),CURDIR  RESET READ ADDRESS
         MVI   IODIR1+4+12+08,X'08'      RESET KEY LENGTH
         MVC   IODIR1+4+12+09(2),=H'256' RESET DATA LENGTH
         MVI   IODIR1+4+12+11,0          RESET SECTOR ADDRESS TO ZERO
         MVI   IOECB,X'7F'               LAST READ WAS OK
         LA    R4,DIRNAME                DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)                POSITION FOR ONE ENTRY
         B     DELE100                   PROMPT BY DEFAULT
         SPACE 1
DELE010  MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    DELE200               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    DELE020               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE200               YES, BRANCH
         SPACE 1
DELE020  OI    #DELFLAG,#DELONE      ONLY A SINGLE MEMBER
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATES TOO?
         BO    DELE200               YES, BRANCH
         SPACE 1
         LA    R4,DIRNAME            DUMMY ARRAY ENTRY
         ST    R4,#MEMPTR
         LA    R4,16(,R4)            POSITION FOR ONE ENTRY
         SPACE 1
         TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BZ    DELE090               NO, BRANCH
         TM    FLAGSAA,FMEMSTAR      ASTERISK NOTATION?
         BNO   DELE080               NO, BRANCH
         MVC   INSERT#1(8),MEMBER1
         $TMSG L002$1                WHO IS BEING PROCESSED
DELE080  TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    DELE998               YES, BRANCH
         B     DELE800               NO, MUST BE MEMBERS
         SPACE 2
DELE090  TM    FLAGSAA,FMEMSTAR      ASTERISK NOTATION?
         BO    DELE110               YES, NO PROMPTING
         TM    FLAGSAA,FOPTIONS      ANY MEMBER NAME?
         BO    DELE110               YES, NO PROMPTING
         SPACE 2
DELE100  LA    R1,PDS393A            DELETE SINGLE MEMBER
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO -- DONE
         SPACE 1
DELE110  MVC   DSNMEMQ(8),DIRNAME    MEMBER NAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         B     NEWCMD                YES, IGNORE THE COMMAND
         B     DELE900               NO, DELETE IT
         SPACE 3
DELE200  OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
         LA    R1,80                 ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON        ISPMODE ACTIVE?
         BO    DELE202               YES, BRANCH
         TM    CONTOPTN,1            ANY LOG RECORDING?
         BO    DELE202               YES, BRANCH
         GTSIZE ,                    TERMINAL SIZE
         CH    R1,=H'120'            120 OR LESS BYTES?
         BL    *+8                   YES, BRANCH
         LH    R1,=H'120'            NO, USE 120 BYTES
DELE202  SH    R1,=H'7'              LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE           CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGDELET),MSGDELET
         LA    R6,MSGLINE+4+L'MSGDELET
         MVC   FULLWORD(3),L162$1    MEMBERS TO BE DELETED
         NI    FLAGSBB,FF-FLINESET   NO LINE IN PROGRESS
         MVI   SUBPOOLT,21           SUBPOOL 21 DATA
         LA    R5,256/2              256 MEMBERS INITIALLY
         TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BZ    DELE210               NO, BRANCH
         MVC   MSGLINE+4(L'MSGMEMBS),MSGMEMBS
         LA    R6,MSGLINE+4+L'MSGMEMBS
         MVC   FULLWORD(3),L165$1    MEMBERS ARE:
         SPACE 1
DELE210  SLL   R5,1                  MEMBERS*2 FOR EACH LOOP
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                  TABLE SIZE=MEMBERS*16
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         LR    R4,R1                 NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR    ANY PREVIOUS MEMBERS?
         BNZ   DELE220               YES, BRANCH
         ST    R4,#MEMPTR            INITIALIZE MEMBER BASE
         B     DELE500
         SPACE 1
DELE220  SRL   R3,1                  SECOND HALF OF TABLE (# MEMBERS)
         LR    R14,R4                NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                 OLD TABLE SIZE=MEMBERS*8
         LR    R2,R15                START OF NEW PART
         LR    R1,R15
         MVCL  R14,R0                PRESERVE THE OLD TABLE
         L     R1,#MEMPTR
         LR    R0,R2                 LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         ST    R4,#MEMPTR            START OF NEW TABLE
         AR    R4,R2                 WHERE TO ADD MEMBERS
         SPACE 3
DELE300  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE320               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   DELE500               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   DELE605               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    DELE319               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     DELE315                   00 - SUCCESSFUL
         B     DELE310                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
DELE310  MVC   INSERT#1(8),DIRNAME
         $TMSG L853$1                MSG - (MEMBER) NOT FOUND
         B     DELE300
         SPACE 1
DELE315  MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     DELE500
         SPACE 1
DELE319  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
DELE320  BAL   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     DELE600               END OF MEMBERS -- BRANCH
         SPACE 1
         TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE?
         BO    DELE400               YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMRANG      MEMBER NAME RANGE?
         BNO   DELE340               NO, BRANCH
         TM    FLAGSAA,FMEMBER1      START ENTRY SPECIFIED?
         BNO   DELE330               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER1          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               ABOVE THIS VALUE?
         BH    DELE300               YES, BRANCH
         XI    FLAGSAA,FMEMBER1      FIRST NAME IS SATISFIED
         SPACE 2
DELE330  TM    FLAGSAA,FMEMBER2      END ENTRY SPECIFIED?
         BNO   DELE500               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER2(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               BELOW THIS VALUE?
         BL    DELE600               YES, BRANCH
         B     DELE500
         SPACE 3
DELE340  LH    R15,LMEMBER1          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER1    <<EXECUTED>>
DELE350  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DELE360               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DELE350            MAYBE, CHECK ALL OTHER POSITIONS
         B     DELE300               NO, IGNORE THIS ENTRY
         SPACE 2
DELE360  TM    FLAGSAA,FMEMBER2      A SECOND PATTERN TOO?
         BNO   DELE500               NO, USE THIS ENTRY
         LH    R15,LMEMBER2          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER2    <<EXECUTED>>
DELE370  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    DELE500               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,DELE370            MAYBE, CHECK ALL OTHER POSITIONS
         B     DELE300               NO, IGNORE THIS ENTRY
         SPACE 3
DELE400  L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE410+4             SKIP FIRST ADD
DELE410  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE300               YES, IGNORE THIS ENTRY
         CLC   8(3,R1),MEMTTR        MATCHING TTR?
         BNE   DELE410               NO, BRANCH
         SPACE 3
         L     R1,#MEMPTR            START OF MEMBER LIST
         B     DELE430+4             SKIP FIRST ADD
DELE430  LA    R1,16(,R1)            NEXT TABLE ENTRY
         CR    R1,R4                 END OF TABLE?
         BNL   DELE500               YES, ADD THIS ENTRY
         CLC   0(8,R1),MEMNAME       FIND MYSELF?
         BE    DELE300               YES, IGNORE THIS ENTRY
         B     DELE430               NO, CONTINUE SEARCHING
         SPACE 3
DELE500  TM    #MEMFLAG,X'80'+X'40'  MEMBER/SUBMIT SUBCOMMAND?
         BNZ   DELE505               YES, BRANCH
         MVC   DSNMEMQ(8),MEMNAME
         BAL   R2,ENQMTEST           MEMBER IN USE?
         OI    #DELFLAG,#DELENQ      YES, MARK TO EXIT LATER
         SPACE 1
DELE505  L     R1,#MEMCNTM
         LA    R1,1(,R1)
         ST    R1,#MEMCNTM           ONE MORE MEMBER IN THIS GROUP
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET      LINE IN PROGRESS?
         BO    DELE510               YES, BRANCH
         OI    FLAGSBB,FLINESET      LINE NOW IN PROGRESS
DELE510  CLI   0(R2),X'40'           SCAN
         BNE   *+8                       FOR LAST
         BCT   R2,DELE510                        NON-BLANK
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE          FIT ON THIS OUTPUT LINE?
         BNH   DELE520               YES, BRANCH
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R6,MSGLINE+3          INDENT FOR BLANKS
         SPACE 1
DELE520  MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE        MAKE PRINTABLE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','            ADD A COMMA
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME      ADD TO THE TABLE
         LA    R4,16(,R4)            NEXT MEMBER POSITION
         S     R3,=F'1'              ANY MORE FIT?
         BNP   DELE210               NO, BRANCH
         TM    #DELFLAG,#DELONE      A SINGLE MEMBER?
         BNO   DELE300               NO, BRANCH
         SPACE 3
DELE600  TM    #DELFLAG,#DELASSP     ASSOCIATED MEMBERS?
         BO    DELE605               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    DELE605               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    DELE300               YES, BRANCH
         SPACE 1
DELE605  TM    FLAGSBB,FLINESET      ANY MEMBERS FOUND?
         BNO   DELE610               NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         $MESAGE MSGBLANK
         SPACE 1
DELE610  TM    #DELFLAG,#DELASSP     ASSOCIATES PHASE STARTED?
         BO    DELE700               YES, BRANCH
         TM    #DELFLAG,#DELASS2     DELETE ASSOCIATE MEMBERS TOO?
         BNO   DELE700               NO, BRANCH
         OI    #DELFLAG,#DELASSP     YES, ASSOCIATES PHASE STARTED
         NI    #DELFLAG,FF-#DELONE   WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         NI    FLAGSBB,FF-FLINESET   NEW OUTPUT LINE
         MVC   MSGLINE+4(L'MSGDELAS),MSGDELAS
         LA    R6,MSGLINE+4+L'MSGDELAS
         MVC   FULLWORD(3),L163$1    MEMBERS TO BE DELETED
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     DELE300               DO ANY ASSOCIATED MEMBERS
         SPACE 3
DELE700  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         TM    #DELFLAG,#DELENQ      ANY MEMBERS IN USE?
         BO    NEWCMD                YES, BRANCH
         SPACE 1
         TM    #MEMFLAG,X'80'        MEMBERS SUBCOMMAND?
         BO    DELE810               YES, BRANCH
         SPACE 1
         LA    R1,PDS395A            SUBMIT PROMPT MESSAGE
         TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    *+8                   YES, BRANCH
         LA    R1,PDS394A            DELETE PROMPT MESSAGE
         BAL   R2,YESNO              PROMPT FOR YES OR NO
         B     NEWCMD                NO, QUIT
         B     DELE900               YES, BRANCH
         SPACE 2
DELE800  MVI   #MEMCNTM+3,1          SET COUNT TO 1
         CLI   #MEMCNTK,1            COUNT KEYWORD?
         BNE   NEWCMD                NO, BRANCH
         $MESAGE MSGBLANK
         SPACE 1
DELE810  CLI   #MEMCNTK,1            COUNT KEYWORD?
         BNE   NEWCMD                NO, BRANCH
         L     R1,#MEMCNTM           COUNT OF MEMBERS
         CVD   R1,DOUBLE
         MVC   INSERT#2(16),MEMEDIT  EDIT PICTURE
         ED    INSERT#2(9),DOUBLE+4
         LA    R1,INSERT#2
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1(8),0(R1)     COUNT OF MEMBERS
         $TMSG L193$1                MEMBER COUNT MESSAGE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 2
DELE900  TM    #MEMFLAG,X'40'        SUBMIT SUBCOMMAND?
         BO    DELE998               YES, BRANCH
         BAL   R2,OPENSTOW           OPEN STOW DCB; ENQUEUE
         B     NEWCMD                NO OPEN -- QUIT
         SPACE 1
         AGO   .TESTORD              ***TEST MODIFY DELETE ORDER
         L     R3,#MEMPTR            START OF MEMBER LIST
         B     DELE910+4             SKIP FIRST ADD
DELE910  LA    R3,16(,R3)
         CR    R3,R4
         BNL   DELE980
.TESTORD ANOP
         LR    R3,R4                 END OF MEMBER LIST
         LA    R6,16                 DECREMENT AMOUNT
DELE910  SR    R3,R6
         CLM   R3,B'0111',#MEMPTR+1
         BL    DELE980
         STOW  STOWDCB,0(R3),D       ISSUE STOW WITH DELETE OPTION
         SPACE 1
         B     *+4(R15)              PROCESS RETURN CODE
         B     DELE940                 00 - SUCCESSFUL
         EX    0,*                     04 - SHOULD NOT OCCUR
         B     DELE960                 08 - MEMBER NOT FOUND
         B     IOERROR                 12 - I/O ERROR IN DIRECTORY
         SPACE 1
DELE940  MVC   INSERT#1(8),0(R3)
         $TMSG L040$1
         B     DELE910
         SPACE 1
DELE960  MVC   INSERT#1(8),0(R3)
         $TMSG L853$1
         B     DELE910
         SPACE 1
DELE980  BAL   R2,SHUTSTOW            CLOSE FOR: IF * THEN(DELETE)
         B     NEWCMD
         SPACE 1
DELE998  L     R1,=V($PRI)            PRINTOFF
         MVC   ##ADRCMD(12),8(R1)     CONTINUE WITH PRINTOFF
         L     R8,##ADRCMD            SUBCOMMAND ROUTINE START
         LA    R2,NEWCMD              SUBCOMMAND TERMINATION ADDRESS
         BR    R8                     LINK TO THE SUBCOMMAND
         SPACE 2
#DELASSP EQU     X'80'            DELETE: ASSOCIATES PHASE OF DELETE
#DELONE  EQU     X'10'            DELETE: DELETE A SINGLE MEMBER
#DELENQ  EQU     X'08'            DELETE: A DELETE MEMBER IS "IN USE"
#DELASS2 EQU     X'01'            DELETE: DELETE ASSOCIATES TOO
#MEMBLKE EQU     X'08'            BLKSIZE TOO LARGE
#MEMDIVE EQU     X'04'            BLKSIZE/LRECL NOT INTEGRAL
#MEMLRLE EQU     X'02'            LRECL TOO LARGE
#MEMLRL0 EQU     X'01'            LRECL NEGATIVE OR ZERO
MSGDELET DC    C'MEMBERS TO BE DELETED ARE:'
MSGMEMBS DC    C'MEMBERS ARE:'
MSGDELAS DC    C'ASSOCIATED MEMBERS TO BE DELETED ARE:'
MEMEDIT  DC    X'40202020206B20212040404040404040'
         TITLE 'P D S  --  PDS DISPLAY, PATTERN               5/12/86'
***********************************************************************
***      DISPLAY SUBCOMMAND                                         ***
***                                                                 ***
***      PATTERN SUBCOMMAND    ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
DISPLAY  CSECT
         USING *,R8
         B     PATTERNZ                  **  MEMBER RANGE
         B     DISPLAY2                  **  ADDITIONAL GROUP MEMBERS
PATTERN  L     R8,=A(DISPLAY)            **  MEMBER PATTERN
         B     DISPLAYA
         SPACE 1
PATTERNZ OI    FLAGSAA,FMEMRANG          **  MEMBER RANGE SELECTION
         TM    FLAGSAA,FMEMBER1+FMEMBER2 DISPLAY RANGE?
         BNO   DISPLAYA                  NO, NO RANGE CHECK
         LH    R14,LMEMBER1
         CH    R14,LMEMBER2              DETERMINE MIN NAME LENGTH
         BNH   *+8
         LH    R14,LMEMBER2
         CLC   MEMBER1(*-*),MEMBER2      <<EXECUTED>>
         EX    R14,*-6                   VALID DISPLAY RANGE?
         LA    R1,L700                   INVALID RANGE MESSAGE
         BH    MSGNEWXX                  YES, BRANCH
         SPACE 1
DISPLAYA LA    R1,80                     ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON            ISPMODE ACTIVE?
         BO    DISPLAY0                  YES, BRANCH
         TM    CONTOPTN,1                ANY LOG RECORDING?
         BO    DISPLAY0                  YES, BRANCH
         GTSIZE ,                        TERMINAL SIZE
         CH    R1,=H'120'                120 OR LESS BYTES?
         BL    *+8                       YES, BRANCH
         LH    R1,=H'120'                NO, USE 120 BYTES
DISPLAY0 ST    R1,LINESIZE               CHARACTERS/LINE
         NI    FLAGSBB,FF-FEXIST-FLINESET-FRANPAT
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 1
DISPLAY1 L     R4,LINESIZE               GET TERMINAL LINE SIZE
         LA    R4,MSGTEXT1+3-11(R4)      END OF LINE ADDRESS
         LA    R5,MSGTEXT1+4             START OF LINE
         MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         SPACE 1
DISPLAY2 BAL   R14,READDIR               GET NEXT DIRECTORY ENTRY
         B     DISPLAY6                  LAST MEMBER IN DIRECTORY
         SPACE 1
         OI    FLAGSBB,FEXIST            MEMBER EXISTS FLAG
         TM    FLAGSAA,FMEMRANG          MEMBER NAME RANGE?
         BNO   PATTERN1                  NO, BRANCH
         TM    FLAGSAA,FMEMBER1          START ENTRY SPECIFIED?
         BZ    DISPLAY3                  NO, BRANCH
         LH    R15,LMEMBER1              LENGTH-1 OF MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6
         BH    DISPLAY2                  NOT WANTED, GET NEXT
         XI    FLAGSAA,FMEMBER1
         SPACE 2
         AIF   ('&CONDRNG' EQ 'N').NRNG
         TM    FLAGSAA,FMEMBER2          LAST ENTRY SPECIFIED?
         BO    DISPLAY3                  YES, BRANCH
         CLC   $DIS(8),##SUBCOM          DISPLAY SUBCOMMAND? ***TEST
         BNE   DISPLAY3                  NO, BRANCH          ***TEST
         STH   R15,LMEMBER2              LENGTH-1 OF SECOND NAME
         MVC   MEMBER2(*-*),MEMBER1      <<EXECUTED>>
         EX    R15,*-6                   DUPLICATE FIRST MEMBER NAME
         OI    FLAGSAA,FMEMBER2          SET SECOND ENTRY HERE
         SPACE 2
.NRNG    ANOP
DISPLAY3 TM    FLAGSAA,FMEMBER2          LAST ENTRY SPECIFIED?
         BZ    DISPLAY4                  NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2
         CLC   MEMBER2(*-*),MEMNAME      <<EXECUTED>>
         EX    R15,*-6                   PAST END?
         BL    DISPLAY6                  YES, END OF DISPLAY
         B     DISPLAY4                  NO, DISPLAY THIS MEMBER NAME
         SPACE 1
PATTERN1 LH    R15,LMEMBER1              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER1        <<EXECUTED>>
PATTERN2 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    PATTERN3                  YES, CHECK FOR ANOTHER PATTERN
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN2               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
PATTERN3 TM    FLAGSAA,FMEMBER2          A SECOND PATTERN SPECIFIED?
         BZ    DISPLAY4                  NO, DISPLAY THE MEMBER NAME
         LH    R15,LMEMBER2              LENGTH-1 OF THE PATTERN
         LA    R1,8                      MAXIMUM PATTERN LENGTH
         SR    R1,R15                    NUMBER OF COMPARE LOOPS
         LA    R14,MEMNAME               START SCAN POSITION
         CLC   0(*-*,R14),MEMBER2        <<EXECUTED>>
PATTERN4 EX    R15,*-6                   PATTERN IN THIS MEMBER NAME?
         BE    DISPLAY4                  YES, DISPLAY THE MEMBER NAME
         LA    R14,1(,R14)               MAYBE, CHECK AT THE NEXT BYTE
         BCT   R1,PATTERN4               CHECK ALL POSSIBLE POSITIONS
         B     DISPLAY2                  NO, IGNORE THIS ENTRY
         SPACE 1
DISPLAYT TRT   MEMNAME(*-*),TRTMEM       <<EXECUTED>>
DISPLAY4 TM    FLAGSAA,FMEM#MEM          MEMBER NAME GROUP DESIRED?
         BO    MEMSNEXT                  YES, FOUND A MEMBER
         LA    R1,7                      MEMBER NAME MACHINE LENGTH
         LA    R14,MEMNAME+8             END OF MEMBER NAME +1
DISPLAYB BCTR  R14,0                     SCAN
         CLI   0(R14),X'40'                  BACKWARDS
         BNE   *+8                                    FOR A
         BCT   R1,DISPLAYB                                 NON-BLANK
         CLI   MEMNAME,C'0'              FIRST DIGIT NUMERIC?
         BNL   *+12                      YES, FORCE A HEX DISPLAY
         EX    R1,DISPLAYT               ANY INVALID OR UNPRINTABLE?
         BZ    DISPLAY8                  NO, BRANCH
         TM    FLAGSBB,FLINESET          LINE IN PROGRESS?
         BZ    DISPLAY9                  NO, SKIP THE PUTLINE
         $MESAGE MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         SPACE 2
DISPLAY9 MVC   MSGTEXT1(136),MSGBL132    CLEAR THE DATA LINE
         UNPK  MSGTEXT1+4(9),MEMNAME(5)  FIRST HALF OF NAME
         UNPK  MSGTEXT1+12(9),MEMNAME+4(5) SECOND HALF OF NAME
         TR    MSGTEXT1+4(16),TRTABLE    FINISH TRANSLATION
         MVC   MSGTEXT1+20(8),BLANKS     CLEAN UP GARBAGE
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    *+10                      NO, SKIP -A
         MVC   MSGTEXT1+20(2),=C'-A'     YES, SET -A
         MVI   MSGTEXT1+24,C'*'
         MVC   MSGTEXT1+25(8),MEMNAME    MEMBER NAME FROM DIRECTORY
         TR    MSGTEXT1+25(8),TRLINE     TRANSLATE TO PRINTABLE
         MVI   MSGTEXT1+33,C'*'
         OI    FLAGSBB,FLINESET+FRANPAT  OUTPUT AND MEMBERS EXIST NOW
         B     DISPLAY5
         SPACE 1
DISPLAY8 MVC   0(8,R5),MEMNAME
         TM    MEMFLAG,X'80'             ALIAS?
         BZ    DISPLAY7                  NO, BRANCH
         MVC   8(2,R5),=C'-A'
DISPLAY7 OI    FLAGSBB,FLINESET+FRANPAT
         LA    R5,10+2(R5)
         CR    R5,R4                     LINE FULL?
         BL    DISPLAY2                  NO, CONTINUE
         SPACE 1
DISPLAY5 $MESAGE MSGTEXT1
         NI    FLAGSBB,FF-FLINESET
         B     DISPLAY1                  RETURN
         SPACE 2
DISPLAY6 TM    FLAGSBB,FLINESET          OUTPUT LINE IN PROGRESS?
         BNO   DISPLAYX                  NO, BRANCH
         LA    R1,MSGTEXT1
         TM    FLAGSAA,FMEM#MEM          MEMBER GROUP IN PROGRESS?
         BNO   MSGNEW                    NO, BRANCH
         SPACE 2
DISPLAYX TM    FLAGSBB,FRANPAT           MEMBER IN RANGE?
         BO    DISPLAYZ                  YES, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE ANY MEMBER GROUP
         TM    FLAGSBB,FEXIST            NO, BUT ANY IN DIRECTORY?
         LA    R1,L400
         BZ    MSGNEW                    NO, ** EMPTY DIRECTORY **
         LA    R1,L401                   ASSUME NONE IN RANGE
         TM    FLAGSAA,FMEMRANG MEMBER   CORRECT?
         BO    MSGNEW                    YES, BRANCH
         LA    R1,L402                   NO, NONE MATCHING PATTERN
         B     MSGNEW
         SPACE 1
DISPLAYZ TM    FLAGSAA,FMEM#MEM          MEMBER GROUP?
         BNO   NEWCMD                    NO, BRANCH
         NI    FLAGSAA,FF-FMEM#MEM       TERMINATE THE MEMBER GROUP
         B     NEWCMD
         TITLE 'P D S  --  PDS DSNAME                         5/12/86'
***********************************************************************
***      DSNAME  SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
DSNAMES  CSECT
         BALR  R6,0
         USING *,R6
         MVC   INSERT#1(80),BLANK128    BLANK THE FIRST LINE
         MVI   MTHIGHL,80               80 CHARACTER MESSAGE
         TM    FLAGSJJ,FDSNMSG          MSG ALLOCATION?
         BO    DSNMSG                   YES, BRANCH
         TM    FLAGSJJ,FDSNTSO          TSO ALLOCATION?
         BO    DSNTSO                   YES, BRANCH
         SPACE 1
DSNJCL   MVC   INSERT#1(20),DSNJDSN     ADD "//12345678  DD  DSN="
         MVC   INSERT#1+2(8),DDNAME     MOVE IN THE DDNAME
         LA    R1,INSERT#1+20           DSNAME START POSITION
         MVC   0(44,R1),DSNAME          MOVE IN THE DSNAME
         AH    R1,DSNLEN                LENGTH OF THE DSNAME
         MVC   0(15,R1),DSNJDSND        ADD ",DISP=SHR,UNIT="
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   6(3,R1),=C'OLD'          NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         LA    R1,15(,R1)               START OF UNIT DATA
         MVC   0(8,R1),1(R15)           ADD TO THE MESSAGE
         MVI   8(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R1),C','               ADD A TERMINATOR AND CONTINUE
         $TMSG L220$1
         MVC   INSERT#1(80),BLANK128    BLANK THE SECOND LINE
         MVC   INSERT#1(20),DSNJDCT     "// DCB=(RECFM=, ..."
         IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+15           MOVE INTO MESSAGE
         LA    R1,INSERT#1+16
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    DSN010                   YES, NO LRECL
         MVC   0(7,R1),DSNJDCTL         MOVE IN LRECL TEXT
         LA    R1,7(R1)
         LH    R4,LABLRECL
         BAL   R14,DSNCONV
         SPACE 1
DSN010   MVC   0(9,R1),DSNJDCTB
         LA    R1,9(R1)
         LH    R4,BLKSI
         BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0001',DS1KEYL       ANY KEY LENGTH?
         BZ    DSN020                   NO, BRANCH
         MVC   0(8,R1),DSNJDCTK         ADD ,KEYLEN=
         LA    R1,8(R1)
         BAL   R14,DSNCONV
         SPACE 1
         MVC   0(5,R1),DSNJDCTR         ADD ,RKP=
         LA    R1,5(R1)
         SR    R4,R4
         ICM   R4,B'0011',DS1RKP        RELATIVE KEY POSITION
         BAL   R14,DSNCONV
         SPACE 1
DSN020   TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN024                   NO, BRANCH
         MVC   0(7,R1),DSNJDCTO         ADD ,OPTCD=
         LA    R1,7(R1)
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
         SPACE 1
DSN024   TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN030                   NO, BRANCH
         MVC   0(7,R1),DSNJDCTD         ADD ,DSORG=
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   7(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   7(2,R1),DSNLVS
         LA    R1,9(,R1)                SKIP OVER DSORG TEXT
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN030                   NO, BRANCH
         MVI   0(R1),C'U'               ADD U
         LA    R1,1(,R1)                SKIP OVER U
         SPACE 1
DSN030   MVC   0(10,R1),DSNJDCTV        ADD ),VOL=SER=
         MVC   10(6,R1),VOLALLOC        ADD THE SERIAL NUMBER
         MVI   10+6(R1),C','
         $TMSG L220$1
         SPACE 1
         MVC   INSERT#1(80),DSNJSPA     //  SPACE= ...
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         LA    R1,INSERT#1+DSNJSPAP     PRIMARY SPACE
         LH    R4,DSNTOTAL              TOTAL FREE SPACE
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   DSN050                   NO, BRANCH
         MVC   INSERT#1+11(3),=C'CYL'   YES, MARK AS CYLINDER ALLOC.
         LH    R0,DEVTTRKS              TRACKS/CYLINDER
         LR    R5,R0
         BCTR  R5,0                     TRACKS/CYLINDER -1
         AR    R5,R4                    TOTAL+TRK/CYL -1
         SR    R4,R4
         DR    R4,R0                    CEIL(TOTAL+TRK/CYL-1) / TRK/CYL
         LR    R4,R5                    RESULTING QUOTIENT
DSN050   BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN070                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN060                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN060                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN060   MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         BAL   R14,DSNCONV
         SPACE 1
DSN070   TM    DSORG,DS1DSGPO           PARTITIONED DATA SET?
         BNO   DSN080                   NO, BRANCH
         MVI   0(R1),C','               SEPARATOR
         CLC   DS1SCALO+1(3),ZERO       SECONDARY ALLOCATION?
         BNE   *+8                      YES, BRANCH
         LA    R1,1(,R1)                NO, ADD ANOTHER COMMA
         MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         BAL   R14,DSNCONV
         SPACE 2
DSN080   MVI   0(R1),C')'               PAIRED PARENTHESIS 1
         MVI   1(R1),C')'               PAIRED PARENTHESIS 2
         TM    DS1SCALO,X'01'           CYLINDER ROUNDING?
         BNO   *+10                     NO, BRANCH
         MVC   1(9,R1),DSNLROUN         YES, MARK IT
         SPACE 1
         LA    R1,INSERT#1+DSNJSPAT     WHERE TO STORE NUMBER FREE
         LH    R4,DSNEMPTY              TOTAL FREE TRACKS
         BAL   R14,DSNCONV
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   DSN090                   NO, BRANCH
         MVC   0(10,R1),DSNLFREE        FREE DIRECTORY BLOCKS
         LA    R1,10(,R1)               WHERE TO STORE NUMBER FREE
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         BAL   R14,DSNCONV
DSN090   MVI   0(R1),C'*'               TERMINATOR
         MVI   1(R1),C'/'                         CHARACTERS
         $TMSG L220$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         $MESAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNTSO   MVC   INSERT#1(22),DSNTDSN     ADD "ALLOC F(1234567)  DA('"
         MVC   INSERT#1+8(8),DDNAME     MOVE IN THE DDNAME
         LA    R1,INSERT#1+8            DDNAME START POSITION
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVI   0(R1),C')'               ADD TERMINATOR
         LA    R1,INSERT#1+22           DSNAME START POSITION
         MVC   0(44,R1),DSNAME          MOVE IN THE DSNAME
         AH    R1,DSNLEN                LENGTH OF THE DSNAME
         MVC   0(12,R1),DSNTDSND        ADD "') SHR UNIT("
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   3(3,R1),=C'OLD'          NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         LA    R1,12(,R1)               START OF UNIT DATA
         MVC   0(8,R1),1(R15)           ADD TO THE MESSAGE
         MVI   8(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVC   0(3,R1),DSNLPAR          ADD A TERMINATOR AND CONTINUE
         $TMSG L210$1
         MVC   INSERT#1(80),BLANK128    BLANK THE SECOND LINE
         MVC   INSERT#1(08),DSNTDCT     "  RECFM("
         IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+8            MOVE INTO MESSAGE
         LA    R1,INSERT#1+9
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'B'
         LA    R1,2(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'S'
         LA    R1,2(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'T'
         LA    R1,2(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'A'
         LA    R1,2(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   1(R1),C'M'
         LA    R1,2(R1)
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    DSN310                   YES, NO LRECL
         MVC   0(8,R1),DSNTDCTL         MOVE IN LRECL TEXT
         LA    R1,8(R1)
         LH    R4,LABLRECL
         BAL   R14,DSNCONV
         SPACE 1
DSN310   MVC   0(10,R1),DSNTDCTB
         LA    R1,10(R1)
         LH    R4,BLKSI
         BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0001',DS1KEYL       ANY KEY LENGTH?
         BZ    DSN320                   NO, BRANCH
         MVC   0(9,R1),DSNTDCTK         ADD ,KEYLEN=
         LA    R1,9(R1)
         BAL   R14,DSNCONV
         SPACE 1
         MVC   0(6,R1),DSNTDCTR         ADD ,RKP=
         LA    R1,6(R1)
         SR    R4,R4
         ICM   R4,B'0011',DS1RKP        RELATIVE KEY POSITION
         BAL   R14,DSNCONV
         SPACE 1
DSN320   TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN324                   NO, BRANCH
         MVC   0(8,R1),DSNTDCTO         ADD OPTCD(
         LA    R1,8(R1)
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
         SPACE 1
DSN324   TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN330                   NO, BRANCH
         MVC   0(8,R1),DSNTDCTD         ADD ) DSORG(
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   8(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   8(2,R1),DSNLVS
         LA    R1,10(,R1)               SKIP OVER DSORG TEXT
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN330                   NO, BRANCH
         MVI   0(R1),C'U'               ADD U
         LA    R1,1(,R1)                SKIP OVER U
         SPACE 1
DSN330   MVC   0(9,R1),DSNTDCTV         ADD ) VOLUME(
         LA    R1,9(,R1)                START OF UNIT DATA
         MVC   0(6,R1),VOLALLOC         ADD THE SERIAL NUMBER
         MVI   6(R1),X'40'              ADD A TERMINATOR
         LA    R1,1(,R1)                SCAN
         CLI   0(R1),X'40'                  FOR FIRST
         BNE   *-8                                   BLANK
         MVC   0(3,R1),DSNLPAR          ADD A TERMINATOR AND CONTINUE
         $TMSG L210$1
         SPACE 1
         MVC   INSERT#1(80),DSNTSPA     "  TRK SPACE(N ..."
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         LA    R1,INSERT#1+DSNTSPAP     PRIMARY SPACE
         LH    R4,DSNTOTAL              TOTAL FREE SPACE
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   DSN350                   NO, BRANCH
         MVC   INSERT#1+2(3),=C'CYL'    YES, MARK AS CYLINDER ALLOC.
         LH    R0,DEVTTRKS              TRACKS/CYLINDER
         LR    R5,R0
         BCTR  R5,0                     TRACKS/CYLINDER -1
         AR    R5,R4                    TOTAL+TRK/CYL -1
         SR    R4,R4
         DR    R4,R0                    CEIL(TOTAL+TRK/CYL-1) / TRK/CYL
         LR    R4,R5                    RESULTING QUOTIENT
DSN350   BAL   R14,DSNCONV
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN370                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN360                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN360                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN360   MVI   0(R1),C','               SEPARATOR
         LA    R1,1(,R1)
         BAL   R14,DSNCONV
         SPACE 1
DSN370   MVI   0(R1),C')'               PAIRED PARENTHESIS 1
         LA    R1,1(,R1)
         TM    DSORG,DS1DSGPO           PARTITIONED DATA SET?
         BNO   DSN380                   NO, BRANCH
         MVC   1(4,R1),DSNLDIR          SEPARATOR
         LA    R1,5(,R1)
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         BAL   R14,DSNCONV
         MVI   0(R1),C')'               FINAL PARENTHESIS
         SPACE 2
DSN380   LA    R1,INSERT#1+DSNTSPAT     WHERE TO STORE NUMBER FREE
         LH    R4,DSNEMPTY              TOTAL FREE TRACKS
         BAL   R14,DSNCONV
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   DSN390                   NO, BRANCH
         MVC   0(10,R1),DSNLFREE        FREE DIRECTORY BLOCKS
         LA    R1,10(,R1)               WHERE TO STORE NUMBER FREE
         LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         BAL   R14,DSNCONV
DSN390   MVI   0(R1),C'*'               TERMINATOR
         MVI   1(R1),C'/'                         CHARACTERS
         $TMSG L210$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         $MESAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNMSG   MVC   INSERT#1(80),DSNM200H       ADD HEADER
         TM    OPTCD,X'A4'                 ANY OPTCD CHARACTERS?
         BZ    *+10                        NO, BRANCH
         MVC   INSERT#1+10(3),DSNLOPT      ADD OPT
         TM    DSORG,DS1DSGPO              PARTITIONED?
         BO    *+10                        YES, BRANCH
         MVC   INSERT#1+63(7),DSNLDSO      NO, CHANGE THE HEADER
         $TMSG L200$1
         MVC   INSERT#1(80),DSNM200E    ADD DATA LINE
         CLI   DSPALLOC,ALLOSHR         DISP=SHR?
         BE    *+10                     YES, BRANCH
         MVC   INSERT#1+0(3),=C'OLD'    NO, DISP=OLD
         LH    R15,BYTEUCB              UCBBYTE
         MH    R15,=H'9'                INDEX INTO THE UNIT TABLE
         LA    R15,UNITTBL(R15)         DEVICE UNIT TYPE
         MVC   INSERT#1+05(8),1(R15)    DEVICE TYPE TEXT
         TM    OPTCD,X'A4'              ANY OPTCD CHARACTERS?
         BZ    DSN650                   NO, BRANCH
         LA    R1,INSERT#1+10
         TM    OPTCD,DCBOPTW            OPTCD=W?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'W'
         LA    R1,1(R1)
         TM    OPTCD,DCBOPTC            OPTCD=C?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'C'
         LA    R1,1(R1)
         TM    OPTCD,DCBSRCHD           OPTCD=Z?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'Z'
         LA    R1,1(R1)
DSN650   IC    R5,FLAGSCC               RECFM BITS
         SLL   R5,29                    DROP TOP BITS
         SRL   R5,29                    REPOSITION
         IC    R5,RECFMTYP(R5)          RECORD FORMAT
         STC   R5,INSERT#1+14           MOVE INTO MESSAGE
         LA    R1,INSERT#1+15
         TM    RECFM,DCBRECBR           RECFM=.B?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'B'
         LA    R1,1(R1)
         TM    RECFM,DCBRECSB           RECFM=.S?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'S'
         LA    R1,1(R1)
         TM    RECFM,DCBRECTO           RECFM=.T?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'T'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCA           RECFM=.A?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'A'
         LA    R1,1(R1)
         TM    RECFM,DCBRECCM           RECFM=.M?
         BNO   *+12                     NO, BRANCH
         MVI   0(R1),C'M'
         LA    R1,1(R1)
         LH    R4,LABLRECL
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   *+6                      NO, BRANCH
         SR    R4,R4                    YES, LRECL IS ZERO
         CVD   R4,DOUBLE                CONVERT TO DECIMAL
         ED    INSERT#1+19(6),DOUBLE+5  CONVERT TO DISPLAY
         LH    R4,BLKSI
         CVD   R4,DOUBLE                CONVERT TO DECIMAL
         ED    INSERT#1+27(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         CVD   R14,DOUBLE
         ED    INSERT#1+33(4),DOUBLE+6  CONVERT TO DISPLAY
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         SR    R0,R0
         AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,*-8                  BRANCH FOR ALL EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         CVD   R0,DOUBLE
         ED    INSERT#1+38(6),DOUBLE+5  CONVERT TO DISPLAY
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 1
         CVD   R0,DOUBLE
         ED    INSERT#1+46(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         SR    R4,R4
         ICM   R4,B'0111',DS1SCALO+1    SECONDARY ALLOCATION?
         BZ    DSN660                   NO, BRANCH
         TM    DS1SCALO,X'80'           TRACK OR CYLINDER ALLOCATION?
         BO    DSN660                   YES, BRANCH
         TM    DS1SCALO,X'40'           AVERAGE BLOCK ALLOCATION?
         BNO   DSN660                   NO, BRANCH
         LH    R14,BLKSI                BLOCKSIZE
         ICM   R14,B'1000',=X'01'       R=1, K=0, DD=BLKSIZE
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),               X
               REGSAVE=YES,MF=(E,PARMLIST)
         LR    R5,R0
         BCTR  R5,0
         AR    R5,R4
         SR    R4,R4
         DR    R4,R0                    CEIL((BUFFERS+#/TRK-1)/(#/TRK))
         LR    R4,R5                    RESULTING QUOTIENT
DSN660   CVD   R4,DOUBLE
         ED    INSERT#1+52(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
         TM    DS1SCALO,X'C0'           CYLINDER ALLOCATION?
         BNO   *+10                     NO, BRANCH
         MVC   INSERT#1+59(3),=C'CYL'   YES, MARK AS CYLINDER ALLOC.
         SPACE 1
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BO    DSN680                   NO, BRANCH
         LA    R1,INSERT#1+63           WHERE TO ADD DSORG DATA
         MVC   0(8,R1),BLANKS           BLANK THE EDIT DATA
         IC    R5,DSORG                 DSORG BITS
         SLL   R5,24                    DROP TOP BITS
         SRL   R5,28                    REPOSITION BITS *2
         LA    R5,DSORGTYP(R5)          DSORG
         MVC   0(2,R1),0(R5)            MOVE INTO MESSAGE
         TM    DSORG+1,DS1ACBM          VSAM DATA SET?
         BNO   *+10                     YES, BRANCH
         MVC   0(2,R1),DSNLVS
         TM    DS1DSORG,DS1DSGU         UNMOVEABLE?
         BNO   DSN690                   NO, BRANCH
         MVI   2(R1),C'U'               ADD U
         B     DSN690
         SPACE 1
DSN680   LH    R4,TOTALLOX              TOTAL DIRECTORY BLOCKS
         SH    R4,TOTUSEDX              FREE DIRECTORY BLOCKS
         CVD   R4,DOUBLE
         ED    INSERT#1+64(6),DOUBLE+5  CONVERT TO DISPLAY
         SPACE 1
DSN690   $TMSG L200$1
         MVI   MTHIGHL,8                RESET THE INSERT LENGTH
         $MESAGE MSGBLANK               ONE BLANK LINE
         BR    R2
         SPACE 3
DSNCONV  CVD   R4,DOUBLE
         UNPK  DOUBLE(5),DOUBLE+5(3)
         LA    R4,DOUBLE-1
         LA    R4,1(R4)
         CLI   0(R4),C'0'
         BE    *-8
         OI    DOUBLE+4,X'F0'
DSNMOVE  MVC   0(1,R1),0(R4)
         LA    R1,1(R1)
         TM    1(R4),X'F0'
         LA    R4,1(R4)
         BO    DSNMOVE
         BR    R14
         SPACE 1
DSNTDSN  DC    C'ALLOC F(1234567)  DA('''') SHR UNIT('
DSNTDSND EQU   DSNTDSN+22,12
         SPACE 1
DSNTDCT  DC    C'  RECFM() LRECL() BLKSIZE() DSORG(**) KEYLEN() RKP() OX
               PTCD() VOLUME('
DSNTDCTL EQU   DSNTDCT+8,8
DSNTDCTB EQU   DSNTDCT+8+8,10
DSNTDCTD EQU   DSNTDCT+8+8+10,10
DSNTDCTK EQU   DSNTDCT+8+8+10+10,9
DSNTDCTR EQU   DSNTDCT+8+8+10+10+9,6
DSNTDCTO EQU   DSNTDCT+8+8+10+10+9+6,8
DSNTDCTV EQU   DSNTDCT+8+8+10+10+9+6+8,9
         SPACE 1
DSNTSPA  DC    CL80'  TRK SPACE(N                            /*FREE TRKX
               =N'
DSNTSPAP EQU   12,4
DSNTSPAT EQU   52,7
         SPACE 2
DSNJDSN  DC    C'//12345678  DD  DSN=,DISP=SHR,UNIT='
DSNJDSND EQU   DSNJDSN+20,15
         SPACE 1
DSNJDCT  DC    C'//  DCB=(RECFM=,LRECL=,BLKSIZE=,DSORG=**,KEYLEN=,RKP=,X
               OPTCD=),VOL=SER='
DSNJDCTL EQU   DSNJDCT+15,7
DSNJDCTB EQU   DSNJDCT+15+7,9
DSNJDCTD EQU   DSNJDCT+15+7+9,9
DSNJDCTK EQU   DSNJDCT+15+7+9+9,8
DSNJDCTR EQU   DSNJDCT+15+7+9+9+8,5
DSNJDCTO EQU   DSNJDCT+15+7+9+9+8+5,7
DSNJDCTV EQU   DSNJDCT+15+7+9+9+8+5+7,10
         SPACE 1
DSNJSPA  DC    CL80'//  SPACE=(TRK,(N                        /*FREE TRKX
               =N'
DSNJSPAP EQU   16,4
DSNJSPAT EQU   52,7
         SPACE 1
DSNM200H DC    CL80'DISP UNIT     RECFM LRECL BLKSIZE   ALLOCTRK FREETRX
               K SECONDARY FREEDIR'                   123X 12345
DSNM200E DC    CL80'SHR                                  X             X
                       TRK        '
         ORG   DSNM200E+19
         DC    X'402020202120'
         ORG   DSNM200E+27
         DC    X'402020202120'
         ORG   DSNM200E+33
         DC    X'40202120'
         ORG   DSNM200E+38
         DC    X'402020202120'
         ORG   DSNM200E+46
         DC    X'402020202120'
         ORG   DSNM200E+52
         DC    X'402020202120'
         ORG   DSNM200E+64
         DC    X'402020202120'
         ORG   ,
DSNLFREE DC    C',FREE DIR='
DSNLROUN DC    C',,,ROUND)'
DSNLDSO  DC    CL7'DSORG'
DSNLDIR  DC    C'DIR('
DSNLOPT  DC    C'OPT'
DSNLPAR  DC    C') -'
DSNLVS   DC    C'VS'
DSORGTYP DC    C'**DAPS??IS'              DSORG=.. TEXT
RECFMTYP DC    C'*FV?U'                   RECFM=. TEXT
ALLOOLD  EQU   X'01'
ALLOMOD  EQU   X'02'
ALLOSHR  EQU   X'08'
         DROP  R6
         TITLE 'P D S  --  PDS EXEC                           5/12/86'
***********************************************************************
***      EXEC SUBCOMMAND       ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 1
EXEC     CSECT
         USING *,R8
         B     EXECNORM             NORMAL EXEC
         SPACE 1
EXECIMPL L     R2,ADDRCBUF          EXEC (%NAME -- IMPLIED FORM)
         XC    2(2,R2),2(R2)        CLEAR OFFSET
         L     R1,ADDRECT                  ECT ADDRESS
         OI    ECTSWS-ECT(R1),ECTNOPD      ASSUME NO OPERAND
         TM    CSOAFLG-CSOA(R15),CSOAVWP   ANY OPERAND?
         BNO   *+8                         NO, BRANCH
         NI    ECTSWS-ECT(R1),FF-ECTNOPD   YES, ZAP THE NO OPERAND FLAG
         SPACE 1
EXECNORM L     R2,ADDRCBUF          EXEC (EXEC NAME -- EXPLICIT FORM)
         NI    FLAGSBB,FF-FONESHOT  ALLOW OTHER SUBCOMMANDS
         LA    R1,L732              ASSUME ISPMODE IS ACTIVE
         TM    SPFLAG0,SPFDON       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         L     R1,ADDRCPPL
         ST    R2,0(,R1)
         SPACE 1
         LA    R3,#EXE              TSO EXEC CLIST PROCESSOR
         BAL   R2,ATTACH            GO ATTACH IT
         B     NEWSTAX
         TITLE 'P D S  --  PDS FIXPDS                         5/12/86'
***********************************************************************
***      FIXPDS SUBCOMMAND     ADDED BY BRUCE LELAND -- NOV., 1982  ***
***********************************************************************
*
         SPACE 1
FIXPDS   CSECT
         USING *,R8
         SPACE 1
         ICM   R1,B'1111',#RECFM       NEW DCB=RECFM SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #RECFM+3(1),RECFM       NO, USE PREVIOUS RECFM
         ICM   R2,B'1111',#LRECL       NEW DCB=LRECL SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #LRECL+2(2),LABLRECL    NO, USE THE LRECL FROM THE DCB
         TM    #RECFM+3,X'C0'          RECFM=U..?
         BNO   *+10                    NO, BRANCH
         XC    #LRECL,#LRECL           YES, USE LRECL=0
         ICM   R3,B'1111',#BLKSI       NEW DCB=BLKSIZE SPECIFIED?
         BP    *+10                    YES, BRANCH
         MVC   #BLKSI+2(2),BLKSI       NO, USE PREVIOUS BLOCKSIZE
         SPACE 2
         TM    #RECFM+3,DCBRECF        RECFM=F OR RECFM=U?
         BNO   FIXPDS10                NO, BRANCH
         TM    #RECFM+3,DCBRECV        RECFM=U?
         BO    FIXPDS10                YES, BRANCH
         SPACE 1
         L     R15,#BLKSI              REQUESTED BLOCKSIZE
         SR    R14,R14
         ICM   R4,B'1111',#LRECL       ANY LRECL?
         BZ    FIXPDS10                NO, AVOID DIVIDE ERROR
         DR    R14,R4                  CHECK LRECL'S PER BLOCK
         LTR   R14,R14                 INTEGRAL NUMBER?
         BZ    FIXPDS10                YES, BRANCH
         $TMSG L482                    NO, DIVIDE ERROR MESSAGE
         SPACE 2
FIXPDS10 TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXPDS50                NO, BRANCH
         CLI   #FIXOPT,X'02'           DCB?
         BL    FIXPDS50                YES, BRANCH
         SPACE 1
         MVC   ##SUBCOM(8),$USA        TEMPORARY SUBCOMMAND IDENTIFIER
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         BAL   R14,READDIR             NUMBER OF DIRECTORY BLOCKS
         MVC   ##SUBCOM(8),$FIX        RESET THE SUBCOMMAND NAME
         SPACE 1
         ICM   R3,B'1111',TOTMEMR      REAL (2 BYTES) AND ALIASES (2)
         LH    R6,TOTBLOCK             NUMBER OF DIRECTORY BLOCKS
         LTR   R6,R6                   ANY CURRENT DIRECTORY BLOCKS?
         BP    FIXPDS20                YES, BRANCH
         LA    R1,L752                 ASSUME RESET OR EXPAND & NOCOUNT
         CLC   #NEWDIR,ZERO            CORRECT?
         BE    MSGNEW                  YES, ERROR EXIT
         SPACE 1
FIXPDS20 CLI   #FIXOPT,X'02'           EXPANDDIR?
         BE    FIXPDS30                YES, BRANCH
         ICM   R1,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         ST    R6,#NEWDIR              NO, USE CURRENT NUMBER
         SR    R6,R6                   CLEAR CURRENT DIRECTORY NUMBER
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BZ    FIXPDS30                NO, A WARNING IS NOT NEEDED
         $TMSG L451                    YES, ALL MEMBERS WILL BE LOST
         SPACE 1
FIXPDS30 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#EOF1TTR             TTR OF CURRENT DIRECTORY EOF
         SPACE 1
         ICM   R4,B'1111',#NEWDIR      ANY DIRECTORY BLOCKS SPECIFIED?
         BP    *+8                     YES, BRANCH
         LA    R4,50                   NO, ADD 50 TO THE CURRENT NUMBER
         AR    R4,R6                   DESIRED + CURRENT DIR. BLOCKS
         ST    R4,#NEWDIR              UPDATE (NOW TOTAL # OF BLOCKS)
         LTR   R3,R3                   ANY MAIN OR ALIAS MEMBERS?
         BNZ   *+6                     YES, BRANCH
         SR    R6,R6                   NO, CLEAR DIRECTORY COUNT
         SPACE 1
         LR    R1,R4
         SR    R1,R6                   NUMBER OF BLOCKS TO ADD
         ST    R1,#NEWDIR              UPDATE (NOW NUMBER TO ADD)
         SPACE 1
         L     R14,=X'01080100'        R=1, K=08, DD=256 BYTES
         TRKCALC FUNCTN=TRKCAP,TYPE=BYTEUCB+1,RKDD=(14),              XX
               REGSAVE=YES,MF=(E,PARMLIST)
         ST    R0,#DIRBTRK             SAVE DIRECTORY BLOCKS PER TRACK
         SRDA  R4,32                   TOTAL BLOCKS
         DR    R4,R0                   TOTAL / ENTRIES PER TRACK
         STH   R5,#EOF2TT              TRACK OF NEW DIRECTORY EOF
         L     R2,DCBDEBAD-IHADCB+INDCB  DEB ADDRESS
         LA    R1,L870                 ASSUME MULTI-EXTENT DIRECTORY
         CLC   32+14(2,R2),ZERO        NULL DATA SET?
         BE    FIXPDS31                YES, ALLOW THE ALLOCATION
         CH    R5,32+14(,R2)           CORRECT?
         BNL   MSGNEW                  YES, ERROR
         SPACE 1
FIXPDS31 LTR   R4,R4                   PARTIAL TRACK USED?
         BP    FIXPDS32                YES, BRANCH
         OI    #NEWDIR,X'80'           MARK SPECIAL HANDLING
         SPACE 2
FIXPDS32 LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         USING DIRLINK,R3              NO, BRANCH
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
FIXPDS40 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     FIXPDS50                LAST MEMBER, BRANCH
         SPACE 1
         CLI   #FIXOPT,X'03'           RESET DIRECTORY?
         BE    FIXPDS42                YES, BRANCH
         CLC   #EOF2TT(2),MEMTTR       IN THE NEW DIRECTORY AREA?
         BL    FIXPDS40                NO, BRANCH
         MVI   SUBPOOLT,21             MARK TEMPORARY SUBPOOL IN USE
         LA    R0,DIREND-DIRLINK       GET ANOTHER DIRECTORY ENTRY
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,DIRLINK              CHAIN TO THE
         LR    R3,R1                               NEXT ENTRY
         L     R14,DIRPTRS             ACTUAL DIRECTORY ENTRY
         MVC   DIRNAME(74),0(R14)      ADD THE DIRECTORY INFORMATION
         MVC   INSERT#1(8),DIRNAME
         MVC   INSERT#2(8),=CL8'MOVED'
         $TMSG L050$2
         DROP  R3
         SPACE 2
FIXPDS42 MVC   DSNMEMQ(8),MEMNAME      MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     FIXPDS40                YES, OUTPUT WARNING MESSAGE ONLY
         B     FIXPDS40                REPEAT FOR ALL MEMBERS
         SPACE 2
FIXPDS50 $MESAGE MSGBLANK              ONE SPACING LINE
         L     R15,=A(DSNAMES)
         BALR  R2,R15                  DISPLAY ALLOCATION STATUS
         LA    R1,PDS392A              CORRECT DATA SET
         BAL   R2,YESNO                RESPONSE "YES" OR "NO"?
         B     NEWCMD                  "NO", QUIT
         SPACE 1
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   FIXDCB                  NO, BRANCH
*** REOPEN THE DATA SET TO ENSURE DS1LSTAR IS CORRECT
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     SET BEYOND END OF DATA SET
         L     R15,=V(EXCP)
         BALR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      CALL IS COMPLETED
         SPACE 2
         MVI   #DIRFIX,X'F9'           HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),=C'FIXPDS' TEMPORARY STOW AND DELETE NAME
         BAL   R6,RESERVE              RESERVE/ENQUE AS REQUIRED
         LH    R4,#NEWDIR+2            DIRECTORY BLOCKS DESIRED
         CLI   #FIXOPT,X'02'           WHICH PROCESSING MODE?
         BL    FIXDCB                    DCB       -- CHANGE DCB ONLY
***      BE    EXPAND                    EXPANDDIR -- ADD TO DIRECTORY
         BH    FIXRES                    RESETDIR  -- REWRITE DIRECTORY
         SPACE 3
EXPAND   ICM   R1,B'1111',TOTMEMR      ANY MAIN OR ALIAS MEMBERS?
         BZ    FIXRES                  NO, USE RESETDIR INSTEAD
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAL   R2,SHUTSTW2             CLOSE AND FREE THE STOW DCB
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT
         SPACE 1
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         LA    R1,L831                           NO OPEN ERROR MSG
         BNO   MSGNEW                            NO, QUIT
         CLC   #MEMPTR(4),ZERO         ANY MEMBERS TO MOVE?
         BE    EXPAND80                NO, BRANCH
         LA    R0,80                   FIRST RECORD IS 80 BYTES LONG
         SPACE 2
*  ONE OR MORE MEMBERS MUST BE MOVED
*    WRITE GARBAGE RECORDS TO OCCUPY SPACE UP TO THE NEW DIRECTORY EOF
EXPAND02 STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,IOBUFF1              START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         LH    R0,BLKSI                MAXIMUM OUTPUT RECORD LENGTH
         CLM   R1,B'1100',#EOF2TT      PAST THE NEW DIRECTORY EOF?
         BNH   EXPAND02                NO, BRANCH
         ST    R1,#FIXTTRO             HIGHEST OUTPUT TTR
         LA    R3,#MEMPTR
         USING DIRLINK,R3              NO, BRANCH
         SPACE 1
EXPAND12 ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    EXPAND20                END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   EXPAND18                NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
EXPAND14 ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    EXPAND18                      NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    EXPAND14                      YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   EXPAND14                      NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    EXPAND16                      YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    EXPAND14                      YES, BRANCH
         SPACE 1
EXPAND16 MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
EXPAND18 TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    EXPAND12                      NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     EXPAND12
         SPACE 3
EXPAND20 LA    R3,#MEMPTR
EXPAND22 ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    EXPAND80                NO, BRANCH
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(8),=CL8'MOVED'
         $TMSG L051$2
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   EXPAND22                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND24 EX    R5,*-4                  ANOTHER TTR?
         BZ    EXPAND30                NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    EXPAND26                NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         L     R15,=V(EXCP)            INPUT IT
         BALR  R14,R15
         LA    R1,L751                 ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
EXPAND26 LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,EXPAND24
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
EXPAND30 MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         SPACE 2
EXPAND32 L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND34                  00 - GOOD READ
         B     EXPAND60                  04 - END OF MEMBER
         B     EXPAND60                  08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
EXPAND34 STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND50                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND36 EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    EXPAND38                NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    EXPAND40                YES, BRANCH
EXPAND38 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,EXPAND36
         B     EXPAND50                NOT THIS RECORD, BRANCH
         SPACE 2
EXPAND40 SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,L750                 ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
EXPAND42 TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,EXPAND42             MOVE AND CHECK ALL ENTRIES
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
EXPAND50 L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    EXPAND32                NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND52 EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   EXPAND54                NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   EXPAND54                NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
EXPAND54 EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   EXPAND58                NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
EXPAND56 CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,EXPAND56
EXPAND58 LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,EXPAND52
         B     EXPAND32               MOVE ALL MEMBERS
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
EXPAND60 LA    R1,L871                 ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   EXPAND62                NO, BRANCH
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         LA    R1,L833
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,L832
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
EXPAND62 TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO MAIN)?
         BNO   EXPAND70                NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND70                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
EXPAND70 LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
EXPAND72 MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     EXPAND74                  00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 1
EXPAND74 ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    EXPAND22                       END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   EXPAND74                       NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    EXPAND74                       YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
EXPAND76 EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   EXPAND72                       NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,EXPAND76
         B     EXPAND72
         DROP  R3
         SPACE 3
*  WRITE THE ADDED DIRECTORY RECORDS
EXPAND80 LH    R6,#NEWDIR+2                   DIRECTORY BLOCKS TO ADD
         LA    R0,CCW1                        FIRST CCW
         ST    R0,IOBCCW                               TO USE
         MVI   CCW3,X'1D'                     COMMAND FOR WRITE C,K,D
         LA    R0,MSGTEXT1+3                  STORAGE FOR
         STCM  R0,B'0111',CCW3+1                         WRITE ADDRESS
         NI    CCW3+4,FF-X'40'                LAST CCW IN CHAIN
         MVC   CCW3+6(2),=H'8'                DATA LENGTH IS EIGHT
         XC    MSGTEXT1(20),MSGTEXT1          CLEAR THE WRITE ADDRESS
         MVI   MSGTEXT1+8,8                   KEY IS EIGHT BYTES
         MVI   MSGTEXT1+9,1                   DATA IS 1*256 BYTES
         MVI   OPENLIST,X'80'
         CLOSE (INDCB),MF=(E,OPENLIST)
         OPEN  (INDCB,UPDAT),MF=(E,OPENLIST)
         SPACE 2
EXPAND82 L     R0,#EOF1TTR                    POINT TO LAST RECORD
         STM   R15,R12,12+4(R13)              CONVERT TTR TO MBBCCHHR
         L     R1,INDCB+(DCBDEBAD-IHADCB)     DEB ADDRESS
         LA    R2,MSGTEXT1                    RETURN CCHHR
         L     R15,ADDRCNVT                   TTR TO MBBCCHHR CONVERT
         LR    R3,R13                         SAVE AREA ADDRESS
         BALR  R14,R15
         LR    R13,R3                         SAVE AREA ADDRESS
         LM    R15,R12,12+4(R13)              RESTORE REGISTERS
         MVC   IOBSEEK(8),MSGTEXT1
         SR    R1,R1
         IC    R1,IOBSEEK+7
         S     R1,=F'1'                       REDUCE INITIAL TTR
         STC   R1,IOBSEEK+7
         MVI   IOECB,0
         EXCP  IOB                            WRITE THIS RECORD
         WAIT  ECB=IOECB                      WAIT FOR I/O COMPLETION
         LA    R1,L836                        I/O ERROR IN DIRECTORY
         CLI   IOECB,X'7F'                    SUCCESSFUL WRITE?
         BNE   EXIT8M                         NO, ERROR
         SR    R1,R1
         IC    R1,#EOF1TTR+2                  INCREMENT
         A     R1,=F'1'                                R OF
         STC   R1,#EOF1TTR+2                               TTR
         C     R1,#DIRBTRK                    FIT ON THIS TRACK?
         BNH   EXPAND84                       YES, BRANCH
         LH    R1,#EOF1TTR                    INCREMENT
         A     R1,=F'1'                                TT OF
         STH   R1,#EOF1TTR                                  TTR
         MVI   #EOF1TTR+2,1                   RESET R OF TTR
         SPACE 1
EXPAND84 S     R6,=F'1'                       MORE BLOCKS?
         BP    EXPAND82                       YES, BRANCH
         SPACE 2
*  WRITE THE DATA SET END-OF-FILE MARKER
         OC    MSGTEXT1+8(3),MSGTEXT1+8          END-OF-FILE WRITTEN?
         BZ    FIXDCB                            YES, BRANCH
         XC    MSGTEXT1+8(3),MSGTEXT1+8          SET KEY, DL=0
         TM    #NEWDIR,X'80'                     END OF TRACK?
         BNO   *+10                              NO, BRANCH
         MVC   INDCB+DCBTRBAL-IHADCB(2),ZERO     FORCE TO NEXT TRACK
         B     EXPAND82
         SPACE 3
FIXRES   BAL   R2,CLOSEIT                        CLOSE INPUT DATA SET
         LA    R0,=A(CHANGE2)                    ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVC   BAMDCB(LDIRDCB),DIRDCB            COPY PROTTYPE DCB
         MVC   DCBDDNAM-IHADCB+BAMDCB,DDNAME     CURRENT DDNAME
         MVI   OPENLIST,X'80'                    END OF LIST MARKER
         OPEN  (BAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+BAMDCB,X'10'  DIRECTORY OPEN?
         BNO   EXIT12O                       NO, QUIT
         MVC   MSGTEXT1(18),KEYDATA          MOVE IN THE KEY AND DATA
         XC    MSGTEXT1+18(256),MSGTEXT1+18  FOLLOWED BY ZEROES
         SPACE 1
FIXRES30 WRITE FIXPDSD,SF,BAMDCB,MSGTEXT1,'S',MF=E
         CHECK FIXPDSD                 CHECK THIS OUTPUT OPERATION
         XC    MSGTEXT1(18),MSGTEXT1   ZERO THE KEY AND DATA PORTION
         S     R4,=F'1'                SUFFICIENT BLOCKS WRITTEN?
         BP    FIXRES30                NO, BRANCH
         TM    #NEWDIR,X'80'                     END OF TRACK?
         BNO   *+10                              NO, BRANCH
         MVC   BAMDCB+DCBTRBAL-IHADCB(2),ZERO    FORCE TO NEXT TRACK
         CLOSE (BAMDCB),MF=(E,OPENLIST)
         SPACE 3
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN        MARK FOR DCB OPEN EXIT USE
         SPACE 2
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 1
         STOW  STOWDCB,#DIRFIX,A       ADD A NULL MEMBER
         LA    R1,L832                 NOT ADDED MESSAGE
         LTR   R15,R15                 ADDED?
         BP    EXIT8M                  NO, QUIT
         STOW  STOWDCB,#DIRFIX,D       DELETE THE DUMMY MEMBER
         LA    R1,L833                 NOT DELETED MESSAGE
         LTR   R15,R15                 DELETED?
         BP    EXIT8M                  NO, QUIT
         B     FIXDCB20                YES, CLOSE AND END
         SPACE 3
FIXDCB   BAL   R2,CLOSEIT              CLOSE THE INPUT DCB
         TM    DSORG,DS1DSGPO          PARTITIONED DATA SET?
         BO    FIXDCB10                YES, BRANCH
         LA    R0,RESTART0             ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAL   R2,DEALLDCB             FREE THE DATA SET
         MVI   DSPALLOC,CONOMOD        ASSUME MOD -- NO CHANGE
         CLI   #FIXOPT,X'03'           CORRECT?
         BNE   *+8                     NO, BRANCH
         MVI   DSPALLOC,CONOOLD        YES, OPEN FOR RESET PROCESSING
         MVI   ##ADRCM#,EDITOR         NO REPEATED WARNING MESSAGE
         L     R15,=V(ALLOCATE)        ALLOCATION ROUTINE
         BALR  R14,R15                 GOOD ALLOCATION?
         B     RESTART0                NO, ERROR
         SPACE 1
         L     R8,##ADRCMD             REESTABLISH THE BASE REGISTER
         CLI   DSPALLOC,CONOSHR        REQUESTED ALLOCATION?
         BE    RESTART2                NO, OLD ALLOCATION FAILED
         SPACE 1
         LA    R0,=A(CHANGE2)          ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVI   DCBDSORG-IHADCB+STOWDCB,DS1DSGPS  SET FOR SEQUENTIAL
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         B     FIXDCB20
         SPACE 1
FIXDCB10 LA    R0,=A(CHANGE2)          ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         MVI   OPENLIST,X'80'          END OF LIST MARKER
         MVC   STOWDCB(LSAMDCB),SAMDCB
         MVC   DCBDDNAM-IHADCB+STOWDCB,DDNAME
         MVI   ##ADRCM#,FIXOPEN                  MARK FOR DCB OPEN EXIT
         SPACE 1
         OPEN  (STOWDCB,(OUTPUT)),MF=(E,OPENLIST)
         BAL   R6,RESERVE
         TM    DCBOFLGS-IHADCB+STOWDCB,X'10'  OUT OPEN?
         BNO   EXIT12O                        NO, QUIT
         SPACE 2
FIXDCB20 CLI   #FIXOPT2,4                      MAXSPACE?
         BNE   FIXDCB30                        NO, BRANCH
         L     R1,DCBDEBAD-IHADCB+STOWDCB      GET DEB ADDRESS
         LH    R14,NUMEXT                      NUMBER OF EXTENTS
         SR    R0,R0
         AH    R0,32+14(,R1)                   ADD TRACKS IN EXTENT
         LA    R1,16(,R1)                      NEXT EXTENT
         BCT   R14,*-8                         BRANCH FOR ALL EXTENTS
         S     R0,=F'1'                        MAX 00TT OF TTR VALID?
         BM    FIXDCB30                        NO, BRANCH
         SLL   R0,8                            0TTX
         A     R0,=F'1'                        0TT1 (R OF TTR IS 1)
         SLL   R0,8                            TT10 -- NEW DS1LSTAR
         STM   R14,R12,12(R13)                 SAVE REGISTERS
         L     R1,DCBDEBAD-IHADCB+STOWDCB      DEB ADDRESS
         LA    R2,DCBFDAD-IHADCB+STOWDCB       RETURN ADDRESS
         L     R15,ADDRCNVT                    CONVERSION ROUTINE
         LR    R3,R13                          SAVE R13
         BALR  R14,R15
         LR    R13,R3                          RESTORE R13
         LM    R14,R12,12(R13)                 RESTORE REGISTERS
         XC    DCBTRBAL-IHADCB+STOWDCB(2),DCBTRBAL-IHADCB+STOWDCB
         SPACE 2
FIXDCB30 CLOSE (STOWDCB),MF=(E,OPENLIST)
         $MESAGE MSGBLANK              ONE SPACING LINE
         L     R1,=A(DSNAMES)          ALLOCATION STATUS ROUTINE
         ST    R1,##ADRCMD             EXECUTED AFTER OPEN OF DATA SET
         MVI   ##ADRCM#,CONTINUE+EDITOR  CONTINUE AND NO WARNING
         CLI   #FIXOPT2,0              RELEASE/RELSAVE/RELEXT/MAXSPACE?
         BE    FIXDCB90                NO, BRANCH
         CLI   #FIXOPT2,4              MAXSPACE?
         BE    FIXDCB90                YES, BRANCH
         MVC   MSGTEXT1+4(8),##SUBCAL  RELEASE COMMAND NAME
         MVI   MSGTEXT1+12,X'40'       ADD A BLANK
         MVI   MSGTEXT1+13,C''''       ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME  ADD THE DSNAME
         LH    R15,DSNLEN              LENGTH OF DSNAME
         LA    R3,MSGTEXT1+14(R15)     WHERE ADDITIONAL TEXT STARTS
         MVC   0(22,R3),BLANK128       BLANK IT FIRST
         MVI   0(R3),C''''             ADD A FINAL QUOTE
         LA    R4,14+22(,R15)          LENGTH OF COMMAND BUFFER
         SLL   R4,16
         ST    R4,MSGTEXT1             UPDATE LENGTH FIELD
         SPACE 1
         CLI   #FIXOPT2,1              RELEASE ALL?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(3,R3),FIXLEXT         ADD EXT FOR EXTENTS
         CLI   #FIXOPT2,3              RELEASE EXTENTS ONLY?
         BE    FIXDCB70                YES, BRANCH
         MVC   2(4,R3),FIXLSPA         ADD SPA( FOR SPACE(
         L     R1,#FIXRES              TRACKS TO RESERVE
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'          CONVERT SIGN
         UNPK  2+4(5,R3),DOUBLE+5(3)   CONVERT TO PRINTABLE
         MVI   2+4+5(R3),C')'          ENDING )
         SPACE 1
         AIF ('&CF296' EQ 'YES').YESF296
FIXDCB70 B     FIXDCB80                ***ZAP (NOP ) TO ADD V(SERIAL)
         AGO   .NOTF296
.YESF296 ANOP
FIXDCB70 NOP   FIXDCB80                ***ZAP (B ) TO BYPASS V(SERIAL)
.NOTF296 ANOP
         MVI   2+4+7(R3),C'V'          V OF VOLUME
         MVI   2+4+8(R3),C'('          BEGINNING (
         MVC   2+4+8+1(6,R3),VOLALLOC  VOLUME ALLOCATED
         MVI   2+4+8+1+6(R3),C')'      ENDING )
         SPACE 1
FIXDCB80 LA    R1,MSGTEXT1             START OF COMMAND BUFFER
         ST    R1,ADDRTEXT             COMMAND ADDRESS
         MVI   3(R1),9                 OFFSET TO OPERANDS
         LA    R1,ADDRTEXT             COMMAND ADDRESS
         LA    R3,##SUBCAL             RELEASE COMMAND PROCESSOR
         BAL   R2,ATTACH               CALL THE COMMAND
         SPACE 2
FIXDCB90 BAL   R2,DEALLDCB             FREE THE DATA SET
         B     RESTART0                REALLOCATE AND START OVER
FIXLEXT  DC    C'EXT'
FIXLSPA  DC    C'SPA('
         TITLE 'P D S  --  PDS HELP                           5/12/86'
***********************************************************************
***      HELP  SUBCOMMAND      ADDED BY BRUCE LELAND -- JAN, 1980   ***
***********************************************************************
*
         SPACE 2
HELP     CSECT
         USING *,R8
         MVC   DDNAMEH,DDNAME        SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP        DDNAME TO FREE
         BAL   R2,DEALLOCZ           FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH        RESTORE ACTUAL DDNAME
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),PDSNAME   SET PDS COMMAND NAME
         MVC   ECTSCMD-ECT(8,R14),HELPLHEL  SET PDS SUBCOMMAND NAME
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?
         BNO   *+8                      NO, BRANCH
         MVI   ECTSCMD-ECT+4(R14),C'X'  YES, HELPX (FOR NO LINE MODE)
         L     R2,ADDRCBUF           INPUT BUFFER ADDRESS
         L     R1,ADDRCPPL
         USING CPPL,R1               ACTUAL ADDRESS
         ST    R2,CPPLCBUF           SET COMMAND ADDRESS
         DROP  R1
         LA    R3,##SUBCAL           TSO HELP COMMAND
         BAL   R2,ATTACH             GO ATTACH IT
         B     NEWSTAX               NEXT COMMAND
HELPLHEL DC    CL8'HELP'
         SPACE 2
*  NOTE: THE SYSHELP FILE IS FREED BEFORE THE HELP
*        CALL IN CASE A PCF EXIT ALREADY REQUESTED HELP.
         TITLE 'P D S  --  PDS HISTORY                        5/12/86'
***********************************************************************
***      HISTORY SUBCOMMAND                                         ***
***********************************************************************
*
         SPACE 1
HISTORY  CSECT
         USING *,R8
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, BRANCH
         SPACE 1
         OI    FLAGSGG,FOUTSUB     SUBCOMMAND GAINED CONTROL
         TM    FLAGSCC,RECFMU      LOAD MODULE LIBRARY?
         BO    HIST020             YES, BRANCH
         SPACE 1
         MVC   INSERT#1(72),SPFSTAT2
         MVI   MTHIGHL,72
         TM    FLAGSAA,FHEAD       HEADER WRITTEN YET?
         BO    HIST000             YES, BRANCH
         $TMSG L230$1
         OI    FLAGSAA,FHEAD       NOW IT HAS BEEN WRITTEN
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         SPACE 1
HIST000  MVC   INSERT#1(72),BLANK128
         MVC   INSERT#1+1(8),DIRNAME
         TM    DIRFLAG,X'80'           ALIAS MEMBER?
         BNO   *+10                    NO, BRANCH
         MVC   INSERT#1+1+8(2),=C'-A'  YES, ADD A FLAG
         LA    R2,DIRUSER          LOAD START OF USER AREA (FOR SSI)
         TM    DIRFLAG,X'0F'       SPF STATISTICS PRESENT?
         BNO   HIST010             NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ  RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   HIST010             NO, BRANCH
         CLI   DIRSPFCD,0          00 OF OTHER 00YYDDDF ZERO?
         BNZ   HIST010             NO, BRANCH
         SPACE 1
         LA    R2,INSERT#1-7
         SR    R1,R1
         IC    R1,DIRSPFR          REVISION NUMBER FIRST
         CVD   R1,DOUBLE
         MVC   21(4,R2),=X'40212020'
         ED    21(4,R2),DOUBLE+6
         MVI   22(R2),C'.'
         IC    R1,DIRSPFV          VERSION NUMBER
         CVD   R1,DOUBLE
         MVC   18(4,R2),=X'40212020'
         ED    18(4,R2),DOUBLE+6
         LA    R1,DIRSPFCR+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   26(L'DATEMASK,R2),DATEMASK
         ED    26(L'DATEMASK,R2),FULLWORD
         LA    R1,DIRSPFCD+1           POINT TO YYDDDF
         LA    R15,FULLWORD+1
         BAL   R14,CONVDATE            CONVERT TO MMDDYY FORMAT
         MVC   FULLWORD(1),FULLWORD+3  CONVERT TO YYMMDD
         MVC   36(L'DATEMASK,R2),DATEMASK
         ED    36(L'DATEMASK,R2),FULLWORD
         MVC   45(6,R2),=X'4021207A2020'
         ED    45(6,R2),DIRSPFCT       TIME OF LAST CHANGE
         LH    R1,DIRSPFSI
         CVD   R1,DOUBLE
         MVC   51(6,R2),=X'402020202120'
         ED    51(6,R2),DOUBLE+5
         SPACE 1
         LH    R1,DIRSPFIN
         CVD   R1,DOUBLE
         MVC   57(6,R2),=X'402020202120'
         ED    57(6,R2),DOUBLE+5
         LH    R1,DIRSPFMD
         CVD   R1,DOUBLE
         MVC   63(6,R2),=X'402020202120'
         ED    63(6,R2),DOUBLE+5
         MVC   71(8,R2),DIRSPFID
         LA    R1,L230$1
         B     MSGNEW                  NO SSI FOR SPF-SAVED MEMBERS
         SPACE 1
HIST010  LA    R1,L230$1
         CLC   ZERO,0(R2)              ZERO?
         BE    MSGNEW                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MSGNEW                  YES, NO SSI
         SPACE 1
         MVC   INSERT#1+2+8+3(4),=C'SSI:'
         UNPK  INSERT#1+2+8+3+5(9),0(5,R2)
         TR    INSERT#1+2+8+3+5(8),TRTABLE
         MVI   INSERT#1+2+8+3+5+8,X'40'
         B     MSGNEW
         SPACE 3
HIST020  OC    #ZAPOPT(4),#ZAPOPT        ANY ZAP, USER, TRAN OR LKED?
         BNZ   HIST021                   YES, BRANCH
         OC    #ZAPOPT(4),=X'01010101'   NO, DEFAULT TO DO THEM ALL
         TM    FLAGSGG,FTRANCON          TRANSLATOR DATA OFF?
         BZ    HIST021                   NO, BRANCH
         MVI   #TRANOPT,X'02'            YES, NO TRANSLATOR DATA
         SPACE 1
HIST021  L     R15,=A(READESD)           FORMAT THE ESD DATA
         BALR  R14,R15                   ANY ESD DATA?
         B     *+8                       NO, BRANCH
         B     HIST024                   YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FDELAYM     DELAYED MESSAGE AVAILABLE?
         BNO   HIST022             NO, BRANCH
         TM    PMEMMIN,X'80'       MEMBER LIST?
         BO    *+12                YES, BRANCH
         TM    FLAGSAA,FMEM#MEM    MEMBER GROUP?
         BNO   HIST022             NO, BRANCH
         CLI   #MODLEN,0           ANY FILTERING?
         BH    NEWCMD              NO, BRANCH
         $MESAGE MSGBLANK
         $MESAGE DELAYMSG
         XI    FLAGSAA,FDELAYM     MESSAGE WAS WRITTEN
         SPACE 1
HIST022  LA    R1,MSGTEXT1         MESSAGE IDENTIFIER
         B     MSGNEW
         SPACE 2
HIST024  L     R15,=A(READIDR)     FORMAT THE IDR DATA
         BALR  R14,R15             IS IDR DATA AVAILABLE?
         B     NOHIST              NO, MESSAGE AND QUIT
         OI    FLAGSGG,FOUTSOME    SOME OUTPUT WAS GENERATED
         B     HISTTRN$            YES, CONTINUE
         SPACE 1
NOHIST   SR    R6,R6
         BAL   R4,HISTHDR
         LA    R1,L460             NO HISTORY AVAILABLE
         B     MSGNEW
         SPACE 3
HISTHDR  TM    FLAGSAA,FDELAYM     ANY DELAYED HEADER?
         BNO   HISTHDR2            NO, BRANCH
         XI    FLAGSAA,FDELAYM     MESSAGE WAS WRITTEN
         $MESAGE MSGBLANK
         $MESAGE DELAYMSG
HISTHDR2 TM    FLAGSCC,F1IDR       FIRST IDR RECORD?
         BZR   R4                  NO, RETURN
         NI    FLAGSCC,FF-F1IDR
         LTR   R1,R6               ANY SECOND HEADER?
         BZR   R4                  NO, RETURN
         $TMSG (R1)                INDIVIDUAL HISTORY TITLE
         BR    R4
         EJECT
*
*        FORMAT TRANSLATOR IDR DATA ENTRIES
*
HISTTRN$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         CLI   #TRANOPT,X'02'      NO TRANSLATOR DATA?
         BE    HISTCOB$            YES, BRANCH
         SPACE 2
HISTTRN  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOB$            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTTRN             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTTRN1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTTRN             YES, BRANCH
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTTRNP            YES, ALLOW THIS
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTTRN1            NO, SKIP THIS
HISTTRNP CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTTRN1            NO, BRANCH
         SPACE 1
         LA    R6,L060
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(8),ESDNAME
         MVC   MSGTEXT1+12(9),DATEMASK
         ED    MSGTEXT1+12(9),IDRDATE
         MVC   MSGTEXT1+24(10),IDRTDATA   TRANSLATOR 1
         UNPK  DOUBLE(5),IDRTDATA+10(3)   VVMM PACKED
         MVI   MSGTEXT1+36,C'V'
         MVC   MSGTEXT1+36+1(2),DOUBLE    VV
         MVI   MSGTEXT1+40,C'M'
         MVC   MSGTEXT1+40+1(2),DOUBLE+2  MM
         CLI   IDRLDATA,15                TWO TRANSLATORS?
         BL    HISTTRN3                   NO, BRANCH
         SPACE 1
         OI    IDRTDATA+27+2,X'0F'        FIX THE SIGN FIELD
         LA    R15,FULLWORD               OUTPUT LOCATION
         LA    R1,IDRTDATA+27             SECOND YYDDD
         BAL   R14,CONVDATE               CONVERT TO MMDDYY FORMAT
         MVC   MSGTEXT1+48(9),DATEMASK
         ED    MSGTEXT1+48(9),FULLWORD
         MVC   MSGTEXT1+60(10),IDRTDATA+15  TRANSLATOR 2
         UNPK  DOUBLE(5),IDRTDATA+25(3)     VVMM PACKED
         MVI   MSGTEXT1+72,C'V'
         MVC   MSGTEXT1+72+1(2),DOUBLE      VV
         MVI   MSGTEXT1+76,C'M'
         MVC   MSGTEXT1+76+1(2),DOUBLE+2    MM
         SPACE 1
HISTTRN3 $MESAGE MSGTEXT1
         B     HISTTRN1            NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 3
*
*        FORMAT SPECIFIC ATTRIBUTES FOR COBOL CSECTS
*
HISTCOB$ LA    R3,#IDRPTR            SCAN IDR CHAIN
         USING IDRENTRY,R3
         LA    R2,#ESDPTR            START OF ESD CHAIN
         SPACE 2
HISTCOBA ICM   R3,B'1111',IDRLINK    END OF IDR CHAIN?
         BZ    HISTCOBB              YES, BRANCH
         CLI   IDRTYPE,IDRTRAN       TRANSLATOR ENTRY?
         BNE   HISTCOBA              NO, BRANCH
         CLC   IDRTDATA(6),HISTCTR1  COBOL TRANSLATOR ID?
         BNE   HISTCOBA              NO, BRANCH
         B     HISTCOBC              YES, COBOL -- BRANCH
         SPACE 2
         USING ESDENTRY,R2
HISTCOBB ICM   R2,B'1111',ESDLINK    END OF ESD CHAIN?
         BZ    HISTZAP$              YES, BRANCH
         CLI   ESDTYPE,CODESD        CSECT ENTRY?
         BNE   HISTCOBB              NO, SKIP THIS
         CLC   ESDNAME(3),HISTLILB   COBOL ESD ENTRY NAME?
         BNE   HISTCOBB              NO, BRANCH
         SPACE 2
HISTCOBC LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         TM    DIRATTR,ATTREXEC    EXECUTABLE?
         BNO   HISTZAP$            NO, IGNORE COBOL ATTRIBUTES
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,HISTCABE         RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD  EPLOC=DIRNAME,DCB=INDCB
         LR    R4,R0               ENTRY POINT ADDRESS OF MODULE
         ICM   R1,B'0111',DIREPA   ENTRY POINT OFFSET OF MODULE
         LA    R1,0(,R1)           CLEAR THE TOP BYTE
         SR    R4,R1               START OF MODULE
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         B     HISTCOB
         SPACE 1
HISTCABE LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE LOAD
         B     HISTZAP$            LOAD DID NOT WORK
         SPACE 2
HISTCOB  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTCOBZ            YES, BRANCH
         CLI   IDRTYPE,IDRTRAN     TRANSLATOR ENTRY?
         BNE   HISTCOB             NO, BRANCH
         SPACE 2
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
HISTCOB1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTCOBZ            YES, DONE
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTCOB1            NO, SKIP THIS
         SPACE 1
         SR    R5,R5
         ICM   R5,B'0111',ESDADDR         OFFSET TO THIS CSECT
         SR    R15,R15
         ICM   R15,B'0111',ESDLEN         LENGTH OF CSECT
         CH    R15,HISTL450               POSSIBLY COBOL?
         BL    HISTCOB1                   NO, BRANCH
         AR    R5,R4                      ADDRESS OF CSECT START
         LR    R1,R5
         AR    R15,R5                     ADDRESS OF CSECT END
         CLC   0(2,R5),HISTLX07           PADDED NOP ENTRY POINT?
         BNE   HISTCEP                    NO, BRANCH
         CLC   14(8,R5),BLANK128          PADDED ENTRY POINT?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,22(,R5)                 GET THE REAL POINTER
         B     HISTCGOT
HISTCEP  CLC   12(8,R5),BLANK128          ENTRY POINT CODING?
         BNE   HISTCGOT                   NO, BRANCH
         L     R5,20(,R5)                 GET THE REAL POINTER
HISTCGOT LA    R5,0(,R5)
         CR    R5,R1                      IN THIS CSECT?
         BL    HISTCOB1                   NO, BRANCH
         CR    R5,R15                     IN THIS CSECT?
         BH    HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCGT2 LA    R1,HISTCVS                 ASSUME VS COMPILER
         CLC   20(4,R5),HISTCVSR          CORRECT?
         BE    HISTCMSG                   YES, BRANCH
         LA    R1,HISTCV4                 ASSUME V4 COMPILER
         CLC   20(4,R5),HISTCANS          CORRECT?
         BNE   HISTCOB1                   NO, BRANCH
         SPACE 1
HISTCMSG MVC   INSERT#1(70),HISTCHDR      INITIALIZE THE HEADER LINE
         MVI   MTHIGHL,70                 70 CHARACTER MESSAGE
         TM    FLAGSCC,F1IDR              A COBOL HEADER WRITTEN?
         BNO   HISTCCOM                   NO, BRANCH
         XI    FLAGSCC,F1IDR              HEADER HAS BEEN WRITTEN
         TM    FLAGSAA,FDELAYM            ANY DELAYED HEADER?
         BNO   HISTCMS2                   NO, BRANCH
         XI    FLAGSAA,FDELAYM            MESSAGE WAS WRITTEN
         $MESAGE MSGBLANK
         $MESAGE DELAYMSG
HISTCMS2 $TMSG L250$1
         MVC   INSERT#1(70),HISTCUND      INITIALIZE THE HEADER LINE
         $TMSG L250$1
         B     HISTCGT2                   FIND THE COMPILER TYPE AGAIN
         SPACE 1
HISTCCOM MVC   INSERT#1+00(8),ESDNAME     SET THE CSECT NAME
         MVC   INSERT#1+09(3),0(R1)       SET THE COMPILER NAME
         L     R6,60(,R5)                 TGT POINTER
         CLC   X'1BC'(8,R6),HISTCOUT      SYSOUT?
         BE    HISTCBIT                   YES, BRANCH
         $TMSG L500
         B     HISTCOB1
         SPACE 1
HISTCBIT TM    X'48'(R6),X'08'            SYMDUMP?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+52(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'04'            FLOW?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+19(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'48'(R6),X'02'            STATE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+24(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'49'(R6),X'10'            TEST?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+30(04),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'80'            RESIDENT?
         BZ    HISTCRS2                   YES, BRANCH (INVERSE TEST)
         MVC   INSERT#1+41(03),BLANK128   NO, CLEAR THE DOC
         OI    #TRANOPT,X'80'             MARK NORES FOR LATER
         B     HISTCEN0
HISTCRS2 OI    #TRANOPT,X'40'             MARK RES FOR LATER
         SPACE 1
HISTCEN0 TM    X'4A'(R6),X'40'            ENDJOB?
         BO    HISTCEN2                   YES, BRANCH
         MVC   INSERT#1+45(06),BLANK128   NO, CLEAR THE DOC
         B     HISTCOBJ
HISTCEN2 OI    #TRANOPT,X'20'             MARK ENDJOB FOR LATER
         SPACE 1
HISTCOBJ MVC   INSERT#1+57(03),BLANK128   CLEAR "OBJ"
         TM    X'4A'(R6),X'20'            OBJ370?
         BNO   *+10                       NO, BRANCH
         MVC   INSERT#1+57(03),HISTC370   YES, ADD "370"
         SPACE 1
         MVC   INSERT#1+61(10),BLANK128   CLEAR "OPTIMIZE "
         TM    X'48'(R6),X'01'            COBOL OPTIMIZED?
         BNO   HISTCCPX                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCOPT   YES, ADD "COBOL"
         B     HISTCTWO
         SPACE 1
HISTCCPX CLC   X'48'(5,R5),HISTCCAP       CAPEX?
         BNE   HISTCTWO                   NO, BRANCH
         MVC   INSERT#1+61(05),HISTCCAP   YES, ADD "CAPEX"
         L     R5,X'38'(,R5)              PGT POINTER
         SH    R5,=H'8'
         ICM   R5,B'1111',0(R5)           DTECT POSSIBLE?
         BZ    HISTCTWO                   NO, BRANCH
         AR    R5,R6                      POINT TO OPTION BYTE
         TM    0(R5),X'60'                XCOUNT OR PFLOW?
         BZ    HISTCTWO                   YES, BRANCH
         MVC   INSERT#1+61(09),HISTCC$D   NO, ADD "CAP/DTECT"
         SPACE 1
HISTCTWO CLC   INSERT#1(3),HISTCVS        VS COMPILER?
         BE    HISTCFIN                   NO, BRANCH
         TM    X'4A'(R6),X'08'            COUNT?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+13(05),BLANK128   NO, CLEAR THE DOC
         SPACE 1
         TM    X'4A'(R6),X'10'            TRACE?
         BO    *+10                       YES, BRANCH
         MVC   INSERT#1+35(05),BLANK128   NO, CLEAR THE DOC
HISTCFIN $TMSG L250$1
         B     HISTCOB1
         SPACE 1
HISTCOBZ DELETE EPLOC=DIRNAME
         B     HISTZAP$
         SPACE 2
HISTCTR1 DC    C'5740CB'
HISTLILB DC    C'ILB'                     COBOL SUBROUTINE NAME
HISTCHDR DC    C'CSECT    VER COUNT FLOW STATE TEST TRACE '
         DC    C'RES ENDJOB SYMD OBJ OPTIMIZE  '
HISTCUND DC    C'-----    --- ----- ---- ----- ---- ----- '
         DC    C'--- ------ ---- --- --------  '
HISTCVS  DC    C'VS '
HISTCV4  DC    C'V4 '
HISTCANS DC    C'ANS4'
HISTCVSR DC    C'VSR1'
HISTCOUT DC    CL8'SYSOUT'
HISTC370 DC    C'370'
HISTCOPT DC    C'COBOL'
HISTCCAP DC    C'CAPEX'
HISTCC$D DC    C'CAP/DTECT'
SPFSTAT2 DC    CL72'SPF STATS:  VER.MOD  CREATED   LAST MODIFIED  SIZE X
                INIT   MOD   ID'
         EJECT
*
*        FORMAT AMASPZAP IDR DATA ENTRIES
*
HISTZAP$ LA    R3,#IDRPTR          SCAN IDR CHAIN
         USING IDRENTRY,R3
         OI    FLAGSCC,F1IDR
         MVI   MTHIGHL,8
         TM    #TRANOPT,X'80'+X'40'+X'20'  ENDJOB WITH RES AND NORES?
         BNO   HISTZAP                     NO, BRANCH
         $TMSG L502
         SPACE 1
HISTZAP  ICM   R3,B'1111',IDRLINK  END OF ESD CHAIN?
         BZ    HISTUSR$            YES, BRANCH
         CLI   IDRTYPE,IDRZAP      AMASPZAP ENTRY?
         BNE   HISTZAP             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD CHAIN
         USING ESDENTRY,R2
         SPACE 1
HISTZAP1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTZAP3            YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTZAPP            YES, BRANCH
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTZAP1            NO, SKIP THIS
HISTZAPP CLC   ESDID,IDRESDID      REQUESTED IDR RECORD?
         BNE   HISTZAP1            NO, BRANCH
         SPACE 1
HISTZAP2 LA    R6,L061
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(L'IDRZDATA),IDRZDATA
         $MESAGE MSGTEXT1
         B     HISTZAP             NEXT IDR DATA RECORD THIS ESD ENTRY
         SPACE 1
HISTZAP3 CLI   #MODLEN,0           ANY MODULE NAME SELECTION?
         BH    HISTZAP             YES, CANNOT DETERMINE MISSING ESD'S
         LA    R2,HISTZAP4-4       MESSAGE IF CSECT NAME UNKNOWN
         B     HISTZAP2            NEXT IDR DATA RECORD THIS ESD ENTRY
HISTZAP4 DC    C'?UNKNOWN'         UNKNOWN CSECT NAME
         EJECT
*
*        FORMAT THE USER-SUPPLIED IDR DATA RECORDS
*
         SPACE 1
HISTUSR$ LA    R3,#IDRPTR
         OI    FLAGSCC,F1IDR
         SPACE 1
HISTUSR  ICM   R3,B'1111',IDRLINK  END OF IDR CHAIN?
         BZ    HISTLKD$            YES, BRANCH
         CLI   IDRTYPE,IDRUSER     USER IDR DATA RECORD?
         BNE   HISTUSR             NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          ADDRESS OF ESD CHAIN
         SPACE 1
HISTUSR1 ICM   R2,B'1111',ESDLINK  END OF ESD CHAIN?
         BZ    HISTUSR             YES, BRANCH
         SPACE 1
         CLI   ESDTYPE,CODEPC      $PRIVATE ENTRY?
         BE    HISTUSRP            YES, BRANCH
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   HISTUSR1            NO, BRANCH
HISTUSRP CLC   ESDID,IDRESDID      WANTED ESD RECORD?
         BNE   HISTUSR1            NO, BRANCH
         SPACE 1
         LA    R6,L062
         BAL   R4,HISTHDR
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4(L'ESDNAME),ESDNAME
         MVC   MSGTEXT1+8+L'ESDNAME(L'DATEMASK),DATEMASK
         ED    MSGTEXT1+8+L'ESDNAME(L'DATEMASK),IDRDATE
         SR    R1,R1
         ICM   R1,B'0001',IDRLDATA ZERO BYTES?
         BZ    HISTUSRS            YES, SKIP THE MOVE (LENGTH ZERO)
         SPACE 1
         MVC   MSGTEXT1+12+L'ESDNAME+L'DATEMASK(*-*),IDRDATA  <<EXEC>>
         EX    R1,*-6
         SPACE 1
HISTUSRS $MESAGE MSGTEXT1
         B     HISTUSR1
         DROP  R2,R3
         EJECT
*
*        FORMAT THE LINKAGE-EDITOR IDR DATA RECORD
*
HISTLKD$ CLI   #LKEDOPT,X'01'           "LKED" REQUESTED?
         BNE   NEWCMD                   NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN      ANY LKED NAME CHECK?
         BZ    HISTLKDG                 NO, BRANCH
         BCTR  R1,0                     MACHINE LENGTH
         CLC   #LKEDTXT(*-*),LKEDNAME   <<EXECUTED>>
         EX    R1,*-6                   THIS LINKAGE EDITOR?
         BNE   NEWCMD                   NO, BRANCH
HISTLKDG SR    R6,R6
         BAL   R4,HISTHDR
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTHIGHL+4,18                   LENGTH OF THIS INSERT
         MVC   INSERT#2(10),LKEDNAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),LKEDVVMM(2)
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),LKEDVVMM+1(2)
         SPACE 1
         $TMSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTHIGHL+4,8                    LENGTH OF STANDARD INSERT
         B     NEWCMD
         TITLE 'P D S  --  PDS HISTORY IDR SCAN SUBROUTINE    5/12/86'
*
*        IDR SCAN SUBROUTINE
*
         SPACE 3
READIDR  STM   R14,R12,ESDIDRSV    SAVE REGISTERS
         LR    R8,R15              BASE FOR THIS SUBROUTINE
         USING READIDR,R8
         NI    FLAGSCC,255-FIDR    IDR DATA DOES NOT EXIST YET
         LA    R6,IDRNORML         BRANCH ADDRESS IF NOT CONTINUED
         LA    R2,#IDRPTR          BASE FOR IDR LIST
         USING IDRENTRY,R2
         MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
IDREXCP  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     IDREXCP1              00 - GOOD READ
         B     LASTIDR               04 - END OF MEMBER
         B     LASTIDR               08 - END OF DATA SET
         B     NEWCMD                12 - I/O ERROR
         SPACE 1
IDREXCP1 LR    R15,R0              START OF RECORD
         TM    0(R15),X'50'        TEST SYM OR SCATTER RECORD?
         BM    IDREXCP             YES, SKIP IT
         SPACE 1
         CLI   0(R15),X'20'        CESD RECORD?
         BE    IDREXCP             YES, SKIP
         SPACE 1
         CLI   0(R15),X'80'        IDR RECORD?
         BNE   LASTIDR             NO, BRANCH
         SPACE 1
         LA    R3,3(,R15)          START OF IDR DATA
         SR    R5,R5
         IC    R5,1(,R15)          BYTE COUNT THIS RECORD
         LA    R5,0(R5,R15)        END OF BUFFER ADDRESS
         BR    R6                  BRANCH TO IDRNORML OR AS CONTINUED
         SPACE 2
IDRNORML TM    2(R15),IDRLKED      LINKAGE EDITOR IDR RECORD?
         BO    LKEDIDR             YES, BRANCH
         TM    2(R15),IDRTRAN      TRANSLATOR IDR RECORD?
         BO    TRANIDR             YES, BRANCH
         TM    2(R15),IDRZAP       AMASPZAP IDR RECORD?
         BO    ZAPIDR              YES, BRANCH
         TM    2(R15),IDRUSER      USER-SUPPLIED IDR RECORD?
         BNO   NEXTIDR             NO, NEXT RECORD
         EJECT
*  USER-SUPPLIED IDR RECORDS:
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
*   0 - 1    2 - 4            5               6 - 6+COUNT          *
*   ESDID    IDENTIFY DATE    COUNT (0-40)    IDRDATA              *
* --  -- --+ --  --  --  -- -+ --  --  --  -- + --  --  --  --  -- ---*
         SPACE 1
USERIDR  CLI   #USEROPT,0          "USER"?
         BE    NEXTIDR             NO, BRANCH
         BAL   R14,GETIDR          GET A NEW IDR RECORD
         ST    R1,IDRLINK
         LR    R2,R1               ADDRESS OF NEW RECORD
         SPACE 2
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH MOVED
         CH    R4,=H'5'            CONTINUED USER HEADER?
         BNL   USER20              NO, BRANCH
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R4,*-6              MOVE IN DATA
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         LA    R14,IDRUPREF+1(R4)  TARGET OF MOVE
         LA    R15,5-1(,R3)        LENGTH BYTE ADDRESS-1 IF NOT SPLIT
         SR    R15,R4              AMOUNT WE ALREADY HAVE
         SR    R1,R1
         IC    R1,0(,R15)          LENGTH BYTE
         LA    R15,6-2(,R1)        MACHINE LENGTH OF RECORD -1
         SR    R15,R4              MACHINE LENGTH TO MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         B     USER30
         SPACE 2
USER20   SR    R15,R15
         IC    R15,5(,R3)          LENGTH OF USER DATA AREA
         LA    R15,5(,R15)         MACHINE LENGTH FOR MOVE
         MVC   IDRUPREF(*-*),0(R3) <<EXECUTED>>
         EX    R15,*-6             MOVE IT INTO THE RECORD
         SPACE 1
USER30   LA    R4,1(,R15)          ACTUAL RECORD LENGTH
         MVI   IDRTYPE,IDRUSER     USER IDR DATA IDENTIFIER
         MVC   IDRESDID,IDRUPREF   ADD THE CSECT ID
         LA    R1,IDRUPREF+2       YYDDD IDR RECORD CREATED
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SR    R15,R15
         IC    R15,IDRUPREF+5      LENGTH OF USER DATA AREA
         BCTR  R15,0
         STC   R15,IDRLDATA
         SPACE 2
USER40   BXLE  R3,R4,USERIDR
         EJECT
         SR    R3,R5
         BCTR  R3,0                ACTUAL REMAINING LENGTH
         LTR   R4,R3               CONTINUED DATA PORTION?
         BNP   NEXTIDR             NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRDATA+1(R14)  END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R4,1(,R4)           ACTUAL LENGTH OF LAST PORTION
         B     USER40
         SPACE 4
*  AMASPZAP IDR RECORDS:
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
*  0 - 1    2 - 4       5 - 12                                      *
*  ESDID    ZAP DATE    IDRDATA OR USER IDENTIFICATION              *
* --  -- -+ --  --  -- + --  --  --  --  --  --  --  --  --  --  -- --*
         SPACE 1
ZAPIDR   CLI   #ZAPOPT,X'01'       "ZAP"?
         BNE   NEXTIDR             NO, BRANCH
         SR    R4,R4
         NI    0(R3),X'3F'
         IC    R4,0(R3)            GET COUNT OF AMASPZAP ENTRIES
         LA    R3,1(R3)
         LA    R4,1(R4)            JUMP COUNT FOR LOOP
         B     ZAPIDR1
         SPACE 2
ZAPIDR2  BAL   R14,GETIDR
         ST    R1,IDRLINK
         LR    R2,R1
         MVC   IDRESDID,0(R3)      MOVE ESDID TO IDR RECORD
         LA    R1,2(R3)            ADDRESS OF DATE OF RECORD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         SPACE 1
         MVC   IDRDATA,5(R3)       MOVE DATA TO IDR RECORD
         MVI   IDRLDATA,8          SET LENGTH FOR COMPATIBILITY
         MVI   IDRTYPE,IDRZAP      INDICATE AMASPZAP ENTRY
         LA    R3,13(R3)           JUMP DATA ADDRESS
         SPACE 1
ZAPIDR1  BCT   R4,ZAPIDR2          IF ANOTHER ENTRY THIS RECORD
         B     NEXTIDR             READ NEXT IDR RECORD
         EJECT
*  TRANSLATOR IDR RECORDS:
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*  0 - 1   2 - 3   N - N+1            0    (0 IS 1 TRANSLATOR)  
*  ESDID   ...     X'80'+LAST ESDID   FLAG (1 IS 2 TRANSLATORS) 
* --  -- + --  -- + --  --  --  -- ---+ --  --  --  --  --  --  -- +---
*
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
*    1 - 10        11 - 12    13 -15       16 - 30                 *
*    TRANSLATOR    VVMM       TRANS DATE   OPTIONAL, SAME AS 1-15  *
*   --  --  -- --+ --  -- ---+ --  --  -- -+ --  --  --  --  --  -- --*
         SPACE 1
TRANIDR  CLI   #TRANOPT,0          NOTRAN?
         BE    NEXTIDR             YES, BRANCH
         LR    R4,R5
         SR    R4,R3               MACHINE LENGTH OK?
         BM    NEXTIDR             NO, DONE WITH THIS RECORD
         BAL   R14,GETIDR
         ST    R1,IDRLINK          CHAIN ON A
         LR    R2,R1                         NEW IDR RECORD
         SPACE 1
         MVI   IDRTYPE,IDRTRAN     TRANSLATOR TYPE RECORD
         LTR   R4,R4               MACHINE LENGTH OK?
         BP    TRAN20              YES, NOT SPLIT
         MVC   IDRESDID(1),0(R3)   NO, ONLY ONE ESDID BYTE
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         MVC   IDRESDID+1(1),0(R3) SECOND BYTE OF ESDID
         BCTR  R3,0                BACK UP THE INPUT POINTER BY ONE
         B     TRAN30
         SPACE 2
TRAN20   MVC   IDRESDID(2),0(R3)   ESDID FIELD
         SPACE 1
TRAN30   MVC   IDRUPREF(2),IDRESDID  ORIGINAL ESDID
         NI    IDRESDID,FF-X'80'   TURN OFF THE HIGH-ORDER BIT
         TM    IDRUPREF,X'80'      LAST ESDID?
         BO    TRAN50              YES, BRANCH
         LA    R4,2                ESDID WIDTH
         BXLE  R3,R4,TRANIDR       CONTINUE FOR ALL ESDID'S
         B     NEXTIDR             GET THE NEXT RECORD
         SPACE 1
TRAN50   LA    R3,2(,R3)
         LR    R4,R5
         SR    R4,R3               FLAG BYTE INPUT?
         BNM   TRAN60              YES, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
TRAN60   LA    R1,14               ASSUME ONE TRANSLATOR
         CLI   0(R3),0             CORRECT?
         BE    *+8                 YES, BRANCH
         LA    R1,29               NO, TWO TRANSLATORS
         MVC   IDRTDATA(30),1(R3)  MOVE IN THE MAXIMUM LENGTH
         STC   R1,IDRLDATA         MACHINE LENGTH
         LA    R4,2(,R1)           ACTUAL SEGMENT LENGTH
TRAN70   BXLE  R3,R4,TRAN80
         EJECT
         LR    R4,R3
         SR    R4,R5               BYTES LEFT +1
         S     R4,=F'1'            ANY ACTUAL REMAINING BYTES?
         BNP   TRAN80              NO, BRANCH
         BAL   R6,IDRCONT          GET THE CONTINUED RECORD AND RETURN
         SPACE 1
         SR    R14,R14
         IC    R14,IDRLDATA        ORIGINAL LENGTH -1
         LA    R14,IDRTDATA+1(R14) END OF DATA
         SR    R14,R4              START POSTION FOR MOVE
         BCTR  R4,0                MACHINE LENGTH FOR MOVE
         MVC   0(*-*,R14),0(R3)    <<EXECUTED>>
         EX    R4,*-6              MOVE IN THE REMAINDER
         LA    R3,1(R3,R4)         POSITION FOR THE NEXT SEGMENT
         SPACE 2
TRAN80   LA    R1,IDRTDATA+12      ADDRESS OF CREATION YYDDD
         LA    R15,IDRDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         LA    R1,#IDRPTR          SCAN IDR CHAIN
         SPACE 1
TRAN90   ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)
         BZ    TRANIDR                        DONE, BRANCH
         CLI   IDRTYPE-IDRENTRY(R1),IDRTRAN   TRANSLATOR TYPE?
         BNE   TRAN90                         NO, BRANCH
         CLI   IDRDATE-IDRENTRY(R1),0         ANY DATE YET?
         BNE   TRAN90                         YES, BRANCH
         MVC   IDRDATE-IDRENTRY(3+6+1+30,R1),IDRDATE
         B     TRAN90
         DROP  R2
         SPACE 3
*  LINKAGE EDITOR IDR RECORDS:
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
*  0 - 9                  10 - 11    12 - 14                        *
*  LINKAGE EDITOR NAME    VVMM       LINKAGE EDIT DATE              *
* --  --  --  --  -- ---+ --  -- ---+ --  --  --  --  --  --  --  -- -*
         SPACE 1
LKEDIDR  OI    FLAGSCC,FIDR
         LA    R1,12(R3)           ADDRESS OF DATE
         LA    R15,LKEDDATE
         BAL   R14,CONVDATE        CONVERT TO MMDDYY FORMAT
         MVC   LKEDNAME(10),0(R3)  LINKAGE EDITOR NAME
         MVC   LKEDVVMM(2),10(R3)  VV.MM IN HEX
         SPACE 2
NEXTIDR  LA    R6,IDRNORML         NOT A CONTINUED RECORD
         SPACE 1
IDRCONT  B     IDREXCP             CONTINUED RECORD
         EJECT
LASTIDR  MVC   RLDCOUNT(1),3(R15)    SAVE THE FIRST RLD/CONTROL AMOUNT
         OC    #ZAPLEN(3),#ZAPLEN    ANY IDR FILTERING?
         BZ    LASTIDRZ              NO, BRANCH
         SPACE 2
         USING IDRENTRY,R3
         SR    R4,R4
         LA    R3,#IDRPTR            SCAN IDR CHAIN
IDRFIL10 LR    R2,R3                 PREVIOUS ENTRY
         ICM   R3,B'0111',IDRLINK+1  CURRENT ENTRY?
         BZ    LASTIDRZ              NO, BRANCH
         ICM   R4,B'0111',IDRLINK+1  NEXT ENTRY
         SPACE 2
         SR    R15,R15               COMPARE STRING LENGTH
         CLI   IDRTYPE,IDRTRAN       IDR TYPE:
         BH    IDRFIL40                USER, BRANCH
         BL    IDRFIL30                ZAP, BRANCH
         LA    R1,#TRANTXT             TRANSLATOR
         SPACE 1
         ICM   R15,B'0001',#TRANLEN  ANY TRANSLATOR FILTERING?
         BZ    IDRFIL10              NO, BRANCH
         CLI   IDRLDATA,15           ONE TRANSLATOR ONLY?
         BL    IDRFIL60              YES, BRANCH
         BCTR  R15,0                 COMPARE MACHINE LENGTH
         CLC   IDRTDATA+15(*-*),#TRANTXT  <<EXECUTED>>
         EX    R15,*-6                    THIS PL/S TRANSLATOR?
         BE    IDRFIL10                   YES, BRANCH
         IC    R15,#TRANLEN          COMPARE STRING LENGTH (AGAIN)
         B     IDRFIL60              CHECK THE FIRST TRANSLATOR TOO
         SPACE 2
IDRFIL30 IC    R15,#ZAPLEN           ZAP IDR RECORD
         LA    R1,#ZAPTXT
         B     IDRFIL60
         SPACE 2
IDRFIL40 IC    R15,#USERLEN          USER IDR RECORD
         LA    R1,#USERTXT
         SPACE 2
IDRFIL60 S     R15,=F'1'             VALID MACHINE LENGTH?
         BM    IDRFIL10              NO, BRANCH
         SPACE 1
         CLC   IDRDATA(*-*),0(R1)    <<EXECUTED>>
         EX    R15,*-6               PARTIAL NAME MATCH?
         BE    IDRFIL10              YES, BRANCH
         SPACE 1
         LR    R3,R2                 NO, UNCHAIN THE
         ST    R4,IDRLINK                           CURRENT ENTRY
         B     IDRFIL10
         EJECT
LASTIDRZ LA    R3,#IDRPTR          PERFORM AN IDR RECORD SORT
         SPACE 2
SORTIDR  ICM   R3,B'1111',IDRLINK
         BZ    SORTIDRZ
         LR    R1,R3
         SPACE 1
SORTIDR2 ICM   R1,B'1111',IDRLINK-IDRENTRY(R1)    GET THE NEXT ENTRY
         BZ    SORTIDR
         SPACE 1
         ICM   R0,B'1110',IDRDATE   ==> SORT BY DATE (DESCENDING ORDER)
         CLM   R0,B'0010',IDRDATE-IDRENTRY+2(R1)  THIRD BYTE:THIRD BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'1000',IDRDATE-IDRENTRY+0(R1)  FIRST BYTE:FIRST BYTE
         BH    SORTIDR2                           HIGH - NO SWITCH
         BL    SORTIDR3                           LOW  - NO SWITCH
         CLM   R0,B'0100',IDRDATE-IDRENTRY+1(R1)  SECONDBYTE:SECONDBYTE
         BNL   SORTIDR2                           HIGH/EQUAL - BRANCH
         SPACE 1
SORTIDR3 XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         XC    IDRSTART-IDRENTRY(LENIDR1,R1),IDRSTART
         XC    IDRSTART(LENIDR1),IDRSTART-IDRENTRY(R1)
         B     SORTIDR2
         SPACE 1
SORTIDRZ LM    R14,R12,ESDIDRSV    RESTORE REGISTERS
         TM    FLAGSCC,FIDR        ANY IDR RECORDS?
         BZR   R14                 NO, RETURN AT +0
         B     4(,R14)             YES, RETURN AT +4
         DROP  R3
         SPACE 2
GETIDR   CLC   ##SUBCOM(8),$ATTL     ATTRIB SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         CLC   ##SUBCOM(8),$MML      MEMLIST SUBCOMMAND?
         BE    NEXTIDR               YES, IGNORE THE RECORD
         MVI   SUBPOOLT,21
         LA    R0,LENIDR
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENIDR,R1),0(R1)
         OI    FLAGSCC,FIDR          IDR DATA EXISTS
         BR    R14
HISTL450 DC    H'450'
HISTLX07 DC    X'0700'
         TITLE 'P D S  --  PDS IF                       5/12/86'
***********************************************************************
***      IF SUBCOMMAND         ADDED BY BRUCE LELAND -- JAN., 1983  ***
***********************************************************************
*
         SPACE 1
IF       CSECT
         USING *,R8
         OI    FLAGSGG,FOUTSUB        SUBCOMMAND GAINED CONTROL
         OC    #ACTIONT(8),#ACTIONT   ANY THEN OR ELSE?
         BNZ   *+10                   YES, BRANCH
         MVC   #ACTIONT(4),IFLA$ATT   DEFAULT THEN ACTION IS ATTRIB
         SPACE 1
         TM    #OPTOPT,X'80'        PARSE INVOKED?
         BNO   IF996                NO, DONE
         CLC   #ATTRLO(3),DIRTTR    IN TTR LOWER BOUND?
         BH    IF997                NO, DO ANY ELSE
         CLC   #ATTRHI(3),DIRTTR    IN TTR UPPER BOUND?
         BL    IF997                NO, DO ANY ELSE
         SPACE 1
         TM    FLAGSCC,RECFMU       A LOAD LIBRARY?
         BO    IF006                YES, BRANCH
         LA    R1,L701              ASSUME LINKEDIT ATTRIBUTES
         TM    #OPTOPT,X'01'        CORRECT?
         BO    MSGNEWXX             YES, ERROR
         B     IF016
         SPACE 2
IF006    MVC   MODESAVE(1),DIRATTR3 RMODE/AMODE BYTE
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   MODESAVE,0           NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,MODESAVE
         SRA   R15,2                AMODE FOR ALIAS
         TM    DIRFLAG,X'80'        ALIAS?
         BNO   *+8                  NO, BRANCH
         STC   R15,MODESAVE         YES, USE ALIAS AMODE BITS
         NI    MODESAVE,DIRAM24+DIRAM31 TURN OFF OTHER BITS
         SPACE 3
IF016    CLI   #RMODE,X'01'         ANY RMODE SELECTION?
         BL    IF036                NO, BRANCH
         MVC   DOUBLE(1),DIRATTR3   RMODE/AMODE BYTE
         NI    DOUBLE,DIRRMANY      TURN OFF OTHER BITS
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BO    *+8                  YES, BRANCH
         MVI   DOUBLE,0             NO, NO MVS/XA BITS
         SR    R15,R15
         IC    R15,#RMODE
         SLA   R15,2                #RMODE*4
         B     *(R15)               CHECK FOR:
         B     IF020                  RMODE=24
         B     IF030                  RMODE=ANY
         B     IF030                  NO RMODE=24
         B     IF020                  NO RMODE=ANY
IF020    CLI   DOUBLE,0             RMODE=24?
         BNE   IF997                NO, DO ANY ELSE
         B     IF036                YES, BRANCH
IF030    CLI   DOUBLE,0             RMODE=24?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF036    CLI   #AMODE,X'01'         ANY AMODE SELECTION?
         BL    IF102                NO, BRANCH
         SR    R15,R15
         IC    R15,#AMODE
         SLA   R15,2                #AMODE*4
         B     *(R15)               CHECK FOR:
         B     IF040                  AMODE=24
         B     IF050                  AMODE=31
         B     IF060                  AMODE=ANY
         B     IF070                  NO AMODE=24
         B     IF080                  NO AMODE=31
         B     IF090                  NO AMODE=ANY
IF040    CLI   MODESAVE,DIRAM24     AMODE=24?
         BH    IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF050    CLI   MODESAVE,DIRAM31     AMODE=31?
         BNE   IF997                NO, DO ANY ELSE
         B     IF102                YES, BRANCH
IF060    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BNE   IF997                     NO, DO ANY ELSE
         B     IF102                     YES, BRANCH
IF070    CLI   MODESAVE,DIRAM24     AMODE=24?
         BNH   IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF080    CLI   MODESAVE,DIRAM31     AMODE=31?
         BE    IF997                YES, DO ANY ELSE
         B     IF102                NO, BRANCH
IF090    CLI   MODESAVE,DIRAM24+DIRAM31  AMODE=ANY?
         BE    IF997                     YES, DO ANY ELSE
         SPACE 3
IF102    CLI   #ALIAOPT,X'01'       "ALIAS" OR "NOALIAS"?
         BL    IF106                NEITHER, BRANCH
         BH    IF104                "NOALIAS", BRANCH
         TM    DIRFLAG,X'80'        "ALIAS" -- CORRECT?
         BO    IF106                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF104    TM    DIRFLAG,X'80'        ALIAS?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF106    CLI   #VSLKED,X'01'        "VSLKED" OR "NOVSLKED"?
         BL    IF110                NEITHER, BRANCH
         BH    IF108                "NOVSLKED", BRANCH
         TM    DIRATTR2,DIRAOSLE    "VSLKED" -- CORRECT?
         BO    IF110                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF108    TM    DIRATTR2,DIRAOSLE    VSLKED?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF110    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"?
         BL    IF113                NEITHER, BRANCH
         LA    R2,ZERO              ASSUME NOT AUTHORIZED
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF111                       NO, NOT AUTHORIZED
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF111                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF111    CLI   #AUTHOPT,X'01'       "AUTH" OR "NOAUTH"
         BH    IF112                "NOAUTH", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              "AUTH" -- CORRECT?
         BH    IF113                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF112    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF113                NO, CANNOT BE AUTHORIZED
         CLI   1(R2),0              AUTHORIZED?
         BH    IF997                YES, DO ANY ELSE
         SPACE 3
IF113    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"?
         BL    IF116                NEITHER, BRANCH
         LA    R2,=X'01010101'      ASSUME NO APF ERROR
         TM    DIRATTR2,DIRAOSLE+DIRAPFLG  APF INFORMATION AVAILABLE?
         BNO   IF114                       NO, NO APF ERROR
         LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF114                NO, BRANCH
         LA    R2,5(,R2)            YES, FOUR MORE BYTES
         N     R2,=F'-2'                                ROUNDED
IF114    CLI   #APFERR,X'01'        "APFERR" OR "NOAPFERR"
         BH    IF115                "NOAPFERR", BRANCH
         CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BE    IF997                YES, DO ANY ELSE
         B     IF116                NO, BRANCH
IF115    CLI   0(R2),X'01'          CORRECT APF LENGTH?
         BNE   IF997                NO, DO ANY ELSE
         SPACE 3
IF116    CLI   #PAGEOPT,X'01'       "PAGE" OR "NOPAGE"?
         BL    IF120                NEITHER, BRANCH
         BH    IF118                "NOPAGE", BRANCH
         TM    DIRATTR2,DIR2PAGA    "PAGE" -- CORRECT?
         BO    IF120                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF118    TM    DIRATTR2,DIR2PAGA    PAGE?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF120    CLI   #SSIOPT,X'01'        "SSI" OR "NOSSI"?
         BL    IF130                NEITHER, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF122                YES, BRANCH
         LA    R2,ZERO              ASSUME NO SSI
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BO    IF124                YES, NO SSI INFORMATION
         LA    R2,DIRUSER           POINT TO SSI INFORMATION
         B     IF124                CHECK FOR ANY SSI
IF122    LA    R2,DIRAPF            BASIC APF DATA START
         TM    DIRATTR,ATTRSCTR     SCATTER LOADED?
         BNO   *+8                  NO, BRANCH
         LA    R2,8(,R2)            YES, EIGHT MORE BYTES
         TM    DIRFLAG,X'80'        ALIAS?
         BO    *+12                 YES, BRANCH
         CLI   8(R2),0              CONVERTED ALIAS ENTRY?
         BE    *+8                  NO, BRANCH
         LA    R2,11(,R2)           ADD ALIAS LENGTH
         TM    DIRATTR2,DIRAOSLE    VS LINKAGE EDITOR?
         BNO   IF124                NO, BRANCH
         TM    DIRATTR2,DIR2SSI     SSI INFORMATION?
         BNO   IF128                NO, BRANCH
IF124    LA    R2,1(,R2)            ROUND TO START
         N     R2,=F'-2'                          OF NEXT HALFWORD
         CLC   ZERO,0(R2)           ZERO?
         BE    IF128                YES, NO SSI
         CLC   =F'-1',0(R2)         ALL ONES?
         BE    IF128                YES, NO SSI
IF126    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BNE   IF997                NO, BRANCH
         OC    #SSITEXT(4),#SSITEXT ANY SSI SEARCH?
         BZ    IF130                NO, BRANCH
         CLC   0(4,R2),#SSITEXT     THIS SSI?
         BE    IF130                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF128    CLI   #SSIOPT,X'01'        SEARCH FOR "SSI"?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF130    TM    FLAGSCC,RECFMU       RECFM=U?
         BNO   IF140                NO, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#ATTRYES  ANY YES+0 ATTRIBUTES?
         BZ    IF132                NO, BRANCH
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF132    ICM   R1,B'0001',#ATTRYES+1 ANY YES+1 ATTRIBUTES?
         BZ    IF134                NO, BRANCH
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ALL ATTRIBUTES MATCH?
         BNO   IF997                NO, DO ANY ELSE
         SPACE 1
IF134    ICM   R1,B'0001',#ATTRNO   NO+0 ATTRIBUTES
         TM    DIRATTR,*-*          <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 1
         ICM   R1,B'0001',#ATTRNO+1 NO+1 ATTRIBUTES
         TM    DIRATTR+1,*-*        <<EXECUTED>>
         EX    R1,*-4               ANY ATTRIBUTES MATCH?
         BNZ   IF997                YES, DO ANY ELSE
         SPACE 3
IF140    CLI   #USERIDO,X'01'       USERID, NOUSERID OR NOTUSERID?
         BL    IF144                NO, BRANCH
         LA    R1,L702              ASSUME DATA SET IS A LOAD MODULE
         TM    FLAGSCC,RECFMU       CORRECT?
         BO    MSGNEWXX             YES, ERROR
         TM    DIRFLAG,X'0F'        SPF STATISTICS?
         BNO   IF143                NO, BRANCH
         CLI   #USERIDO,X'02'       "NOUSERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SR    R1,R1
         ICM   R1,B'0001',#USERIDL  ANY USERID FILTERING?
         BP    IF141                YES, BRANCH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF144                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
         SPACE 1
IF141    BCTR  R1,0                 MACHINE LENGTH
         CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BNE   IF142                NO, BRANCH
         EX    R1,IF142             PARTIAL USERID MATCH?
         BNE   IF997                NO, DO ANY ELSE
         B     IF144                YES, BRANCH
         SPACE 1
IF142    CLC   #USERTXT(*-*),DIRSPFID <<EXECUTED>>
         EX    R1,*-6               PARTIAL USERID MATCH?
         BE    IF997                YES, DO ANY ELSE
         B     IF144                NO, BRANCH
         SPACE 1
IF143    CLI   #USERIDO,X'01'       "USERID" DESIRED?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
IF144    TM    #OPTOPT,X'02'        NEED ANY MAP OR HISTORY DATA?
         BNO   IF156                NO, BRANCH
         MVI   RLDCOUNT,X'FF'       ASSUME NO RLD/CONTROL DATA FOUND
         SPACE 1
         L     R15,=A(READESD)
         BALR  R14,R15              ANY ESD DATA?
         NOP   0                    NO, IGNORE
         SPACE 1
         ICM   R2,B'1110',#ZAPOPT       SAVE ZAP/USER/TRAN
         MVC   #ZAPOPT(3),=X'01010101'  CHECK ZAP/USER/TRAN
         MVC   LKEDDATE+2,X'FF'         NO LINKAGE-EDIT DATE FOUND
         L     R15,=A(READIDR)
         BALR  R14,R15              ANY IDR DATA?
         B     IF145                NO, BRANCH
         SPACE 2
         MVI   #LKEDOPT,0           ASSUME LINKAGE-EDITOR FOUND
         SR    R1,R1
         ICM   R1,B'0001',#LKEDLEN  ANY LKED NAME CHECK?
         BZ    IF145                NO, BRANCH
         BCTR  R1,0                 MACHINE COMPARE LENGTH
         CLC   LKEDNAME(*-*),#LKEDTXT  <<EXECUTED>>
         EX    R1,*-6               THIS PARTIAL LKED NAME?
         BE    IF145                YES, BRANCH
         MVI   #LKEDOPT,1           NO, LINKAGE-EDITOR NOT FOUND
         SPACE 1
IF145    OC    #IFCREIF(6),#IFCREIF        ANY CREATION DATE RANGE?
         BZ    IF146                       NO, CONTINUE
         ICM   R1,B'0100',LKEDDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',LKEDDATE         MMDD OF MMDDYY
         CLM   R1,B'0111',#IFCREIF         DATA VALUE < FIRST YYMMDD?
         BL    IF997                       YES, DO ANY ELSE
         CLM   R1,B'0111',#IFCREIF+3       DATA VALUE > SECOND YYMMDD?
         BH    IF997                       YES, DO ANY ELSE
         SPACE 2
IF146    STCM  R2,B'1110',#ZAPOPT   RESTORE ZAP/USER/TRAN
         USING ESDENTRY,R2
         LA    R2,#ESDPTR           SCAN ESD CHAIN
         TM    #MODOPT,X'01'+X'02'  WKEXTERN OR EXTERN?
         BZ    IF148                NO, BRANCH
         OC    0(4,R2),0(R2)        ANY FOUND?
         BZ    IF148                NO, BRANCH
         MVI   #MODOPT,0            YES, DROP MODULE/WKEXTERN/EXTERN
         SPACE 1
IF148    ICM   R2,B'1111',ESDLINK   ANY MORE ESD ENTRIES?
         BZ    IF154                NO, BRANCH
         CLI   ESDTYPE,CODESD       CSECT ESD ENTRY?
         BNE   IF148                NO, BRANCH
         SPACE 1
         USING IDRENTRY,R3
         LA    R3,#IDRPTR           SCAN IDR CHAIN
         SPACE 1
IF150    ICM   R3,B'1111',IDRLINK   ANY MORE IDR ENTRIES?
         BZ    IF148                NO, BRANCH
         CLC   ESDID,IDRESDID       THIS CSECT IDENTIFIER?
         BNE   IF150                NO, BRANCH
         SPACE 1
         MVI   #MODOPT,0            YES, MODULE FOUND
         CLI   IDRTYPE,IDRTRAN      ZAP RECORD?
         BNL   IF152                NO, BRANCH
         MVI   #ZAPOPT,0            YES, ZAP RECORDS MATCH
         OC    #IFMODIF(6),#IFMODIF       ANY MODIFICATION CHECK?
         BE    IF152                      NO, BRANCH
         ICM   R1,B'0100',IDRDATE+2       YY OF MMDDYY
         ICM   R1,B'0011',IDRDATE         MMDD OF MMDDYY
         CLM   R1,B'0111',#IFMODIF        DATA VALUE < FIRST YYMMDD?
         BL    IF152                      YES, NO MATCH
         CLM   R1,B'0111',#IFMODIF+3      DATA VALUE > SECOND YYMMDD?
         BH    IF152                      YES, NO MATCH
         XC    #IFMODIF(6),#IFMODIF       A MODIFICATION RECORD MATCHES
         SPACE 1
IF152    CLI   IDRTYPE,IDRTRAN      IDR RECORD TYPE:
         BNH   *+8
         MVI   #USEROPT,0              **USER
         BNE   *+8
         MVI   #TRANOPT,0              **TRANSLATOR
         B     IF150
         DROP  R2,R3
IF154    OC    #IFMODIF(6),#IFMODIF ANY CHANGES MATCH?
         BNZ   IF997                NO, DO ANY ELSE CONDITIONS
         SPACE 1
IF156    OC    #ZAPOPT(5),#ZAPOPT   ZAP/USER/TRAN/LKED/MODULE FOUND?
         BNZ   IF997                NO, DO ANY ELSE
         SPACE 3
         TM    DIRATTR2,DIRAOSLE    OS/VS LINKAGE EDITOR?
         BO    *+10                 YES, BRANCH
         MVC   RLDCOUNT(1),DIRATTR4 NO, WANT MATCHING RLD/CONTROL
         CLI   #RLDERR,X'01'        "RLDERR" OR "NORLDERR"
         BL    IF160                NEITHER, BRANCH
         BH    IF158                "NORLDERR", BRANCH
         CLC   DIRATTR4(1),RLDCOUNT "RLDERR" -- CORRECT?
         BNE   IF160                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF158    CLC   DIRATTR4(1),RLDCOUNT "NORLDERR" -- CORRECT?
         BNE   IF997                YES, DO ANY ELSE
         SPACE 3
IF160    NI    #IFABOVE,FF-X'01'    ASSUME SIZE IS NOT AVAILABLE
         TM    #IFABOVE,X'80'       ANY ABOVE CODED?
         BO    IF164                YES, BRANCH
         TM    #IFBELOW,X'80'       ANY BELOW CODED?
         BZ    IF170                NO, BRANCH
         SPACE 1
IF164    SR    R1,R1
         ICM   R1,B'0111',DIRSTORE  MODULE SIZE
         TM    FLAGSCC,RECFMU       RECFM=U?
         BO    IF168                YES, BRANCH
         TM    DIRFLAG,X'0F'        SPF STATISTICS PRESENT?
         BNO   IF180                NO, BRANCH
         OC    DIRSPFZ(3),DIRSPFZ   RESERVED AND 00 OF 00YYDDDF ZEROS?
         BNZ   IF180                NO, BRANCH
         CLI   DIRSPFCD,0           00 OF OTHER 00YYDDDF ZERO?
         BNZ   IF180                NO, BRANCH
         LH    R1,DIRSPFSI
         SPACE 1
IF168    OI    #IFABOVE,X'01'       SIZE IS AVAILABLE
         ST    R1,#IFLEN            LENGTH OF THE MEMBER
         SPACE 1
IF170    TM    #OPTOPT,X'04'        NEED TO READ THE MEMBER?
         BNO   IF300                NO, BRANCH
         SPACE 1
IF180    SR    R6,R6                COUNT OF INPUT LOGICAL RECORDS
         OI    ##ADRPA#,$Q          QUIET MODE READ IS REQUIRED
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BL    IF204                NO, BRANCH
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD      CALL EXCP FOR OPEN ONLY
         L     R15,=V(EXCP)
         BALR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD   EXCP CALL IS COMPLETE
         SPACE 1
         CLC   DIRTTR(3),DS1LSTAR   BEYOND END OF DATA MARKER?
         BNL   IF242                YES, REALLY PAST DS1LSTAR
         MVC   STARTTR(3),DIRTTR    FIRST DISK ADDRESS TO READ
         SPACE 3
IF204    L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)             PROCESS RETURN CODE
         B     IF208                  00 - SUCCESSFUL READ
         B     IF240                  04 - END OF MEMBER
         B     IF240                  08 - END OF DATA SET
         B     IF242                  12 - I/O ERROR
         SPACE 2
IF208    LR    R3,R0                BUFFER START
         L     R5,LS                BLOCK LENGTH
         CH    R5,#MAXBLKI          VALID BLKSIZE?
         BNH   IF220                YES, BRANCH
         OC    #MAXBLKI(2),#MAXBLKI ANY MAXBLK()?
         BNZ   IF210                YES, BRANCH
         CH    R5,BLKSI             BLKSIZE VALID?
         BNH   IF220                YES, BRANCH
IF210    OI    #OPTOPT,X'20'        NO, INDICATE A BLOCKSIZE ERROR
         SPACE 1
IF220    TM    FLAGSCC,RECFMF       FIXED FORMAT?
         BNO   IF222                NO, BRANCH
         LH    R14,LRECL            CURRENT LRECL
         LR    R0,R5                BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14               BLKSIZE/LRECL
         LTR   R0,R0                ANY REMAINDER?
         BZ    IF222                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (BLKSIZE/LRECL)
         SPACE 1
IF222    LR    R1,R5
         AR    R5,R3                END OF BUFFER
         BCTR  R5,0                 END OF BUFFER -1
         TM    FLAGSCC,RECFMF       RECFM=F?
         LH    R4,LRECL
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMU       RECFM=U?
         LR    R4,R1
         BO    IF228                YES, BRANCH
         TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, ASSUME RECFM=U
         AH    R3,=H'4'             SKIP BLOCK LENGTH WORD
         B     IF226
         SPACE 3
IF224    BXLE  R3,R4,IF226          GET THE NEXT LOGICAL RECORD
         B     IF204                END OF BLOCK, BRANCH
         SPACE 2
IF226    TM    FLAGSCC,RECFMV       RECFM=V?
         BNO   IF228                NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)     RECORD LENGTH +4
         LR    R14,R4               RECORD LENGTH +4
         AH    R3,=H'4'             SKIP RECORD LENGTH WORD
         SH    R4,=H'4'             RECORD LENGTH VALID?
         BNM   IF228                YES, BRANCH
         SR    R4,R4                NO, USE LENGTH 0
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (SHORT RECORD)
         B     IF224                SKIP THE RECORD
         SPACE 2
IF228    LA    R6,1(,R6)            ANOTHER OUTPUT RECORD
         CH    R14,LRECL            VALID LRECL VALUE?
         BNH   IF224                YES, BRANCH
         OI    #OPTOPT,X'10'        NOTE: LRECL ERROR (LONG RECORD)
         B     IF224
         SPACE 2
IF240    TM    DIRFLAG,DIR2TTR+DIR1TTR    ANY USER TTR?
         BZ    IF244                      NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',DIRSTART        TTR ACTUALLY INPUT?
         BNH   IF242                      NO, I/O ERROR
         TM    DIRFLAG,DIR2TTR            A SECOND TTR TOO?
         BZ    IF244                      NO, BRANCH
         CLM   R0,B'1110',DIRNOTE         TTR ACTUALLY INPUT?
         BH    IF244                      YES, BRANCH
         SPACE 1
IF242    OI    #OPTOPT,X'40'        PERMANENT I/O ERROR
         SPACE 2
IF244    LTR   R6,R6                ANY RECORDS?
         BNZ   IF246                YES, BRANCH
         OI    #OPTOPT,X'08'        NO, MARK AS NULL
         SPACE 3
IF246    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE READ
         CLI   #IOERROR,X'01'       "IOERROR" OR "NOIOERROR"?
         BL    IF260                NEITHER, BRANCH
         BH    IF248                "NOIOERROR", BRANCH
         TM    #OPTOPT,X'40'        "IOERROR" -- CORRECT?
         BO    IF260                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF248    TM    #OPTOPT,X'40'        IOERROR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF260    CLI   #BLOCKER,X'01'       BLOCKERR/MAXBLK(SIZE)/NOBLOCKERR?
         BL    IF266                NONE, BRANCH
         CLI   #BLOCKER,X'02'       "BLOCKERR" OR "MAXBLK(SIZE)?
         BH    IF262                NO, BRANCH
         TM    #OPTOPT,X'20'        "BLOCKERR" -- CORRECT?
         BO    IF266                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF262    TM    #OPTOPT,X'20'        BLOCKERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF266    CLI   #LRECLER,X'01'       "LRECLERR" OR "NOLRECLERR"?
         BL    IF280                NEITHER, BRANCH
         BH    IF268                "NOLRECLERR", BRANCH
         TM    #OPTOPT,X'10'        "LRECLERR" -- CORRECT?
         BO    IF280                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF268    TM    #OPTOPT,X'10'        LRECLERR?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF280    CLI   #NULL,X'01'          "NULL" OR "NONULL"?
         BL    IF300                NEITHER, BRANCH
         BH    IF282                "NONULL", BRANCH
         TM    #OPTOPT,X'08'        "NULL" -- CORRECT?
         BO    IF300                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF282    TM    #OPTOPT,X'08'        NULL?
         BO    IF997                YES, DO ANY ELSE
         SPACE 3
IF300    TM    #IFABOVE,X'01'         SIZE AVAILABLE?
         BNO   *+8                    NO, BRANCH
         L     R6,#IFLEN              YES, USE THAT LENGTH
         SPACE 1
         TM    #IFABOVE,X'80'         ABOVE CHECK?
         BNO   IF320                  NO, BRANCH
         CLM   R6,B'0111',#IFABOVE+1  ABOVE THE MINIMUM VALUE?
         BNH   IF997                  NO, DO ANY ELSE
         SPACE 1
IF320    TM    #IFBELOW,X'80'         BELOW CHECK?
         BNO   IF360                  NO, BRANCH
         CLM   R6,B'0111',#IFBELOW+1  BELOW THE MAXIMUM VALUE?
         BNL   IF997                  NO, DO ANY ELSE
         SPACE 3
IF360    CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NO, BRANCH
         SPACE 1
         SR    R3,R3                RESULT REGISTER
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),DIRNAME
         SPACE 1
IF382    BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,IF382                                  NON-BLANK
         SPACE 1
         CLI   DIRNAME,C'0'        NUMERIC FIRST CHARACTER?
         BL    *+8                 NO, BRANCH
         LA    R3,1                YES, NON-STANDARD NAME
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         BZ    *+8                 YES, BRANCH
         LA    R3,1                NO, NON-STANDARD NAME
         SPACE 1
         CLI   #NAMEERR,X'01'       "NAMEERR" OR "NONAMEERR"?
         BL    IF390                NEITHER, BRANCH
         BH    IF384                "NONAMEERR", BRANCH
         LTR   R3,R3                "NAMEERR" -- CORRECT?
         BP    IF390                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF384    LTR   R3,R3                NAMEERR?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK EACH MODULE FOR LINKAGE EDIT ATTRIBUTE CONFLICTS IF REQUIRED
IF390    CLI   #LKEDERR,X'01'              CONFLICT CHECK REQUESTED?
         BL    IF400                       NO, BRANCH
         SPACE 1
         TM    DIRATTR,ATTRRENT            REENTRANT?
         BNO   IF392                       NO, BRANCH
         TM    DIRATTR,ATTRREUS            REUSABLE?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF392    TM    DIRATTR+1,ATTRSYMS+ATTRNE   TEST AND NOT EDIT
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTRREUS+ATTRSCTR   REUSABLE AND SCATTER
         BO    IF399                       YES, CONFLICT FOUND
         SPACE 1
         TM    DIRATTR,ATTROVLY            OVERLAY ATTRIBUTE?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR,ATTRRENT++ATTRREUS+ATTRSCTR  RENT/REUS/SCTR?
         BNZ   IF399                                YES, CONFLICT
         TM    DIRATTR+1,ATTRREFR          REFRESHABLE?
         BO    IF399                       YES, CONFLICT FOUND
         TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF394                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 24?
         BNZ   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF394    TM    DIRATTR2,DIRAOSLE           MVS LINKAGE-EDITOR?
         BNO   IF397                       NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   IF397                       NO, BRANCH
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BO    IF399                       YES, CONFLICT FOUND
         TM    MODESAVE,DIRAM31            AMODE 31?
         BNO   IF399                       NO, CONFLICT FOUND
         SPACE 1
IF397    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BE    IF997                       YES, DO ANY ELSE
         B     IF400                       NO, NONE FOUND
         SPACE 1
IF399    CLI   #LKEDERR,1                  CONFLICT DESIRED?
         BNE   IF997                       NO, DO ANY ELSE
         SPACE 3
* LOAD EACH MODULE IF REQUIRED
IF400    CLI   #LOADERR,X'01'      LOAD CHECK REQUESTED?
         BL    IF420               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE LOAD IS REQUIRED
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,IF402            RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,MSGTEXT2      SAVE REGISTERS
         MVI   RECOVER,C'L'        LOAD RECOVERY FLAG
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         MVI   RECOVER,0           LOAD WORKED
         B     IF404
         SPACE 1
IF402    LM    R2,R8,MSGTEXT2      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   RECOVER,1           LOAD DID NOT WORK
         SPACE 1
IF404    NI    ##ADRPA#,FF-$Q       END OF QUIET MODE LOAD
         CLI   #LOADERR,X'01'       "LOADERR" OR "NOLOADERR"?
         BL    IF420                NEITHER, BRANCH
         BH    IF406                "NOLOADERR", BRANCH
         CLI   RECOVER,1            "LOADERR" -- CORRECT?
         BE    IF420                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF406    CLI   RECOVER,1            LOAD ERROR?
         BE    IF997                YES, DO ANY ELSE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
IF420    CLI   #SPFEDIT,X'01'      INUSE CHECK REQUESTED?
         BL    IF440               NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         QUIET MODE SPF USE CHECK
         SR    R3,R3               ASSUME NOT IN USE
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         LA    R3,1                YES, MARK AS SUCH
         NI    ##ADRPA#,FF-$Q      END OF QUIET MODE
         SPACE 1
         CLI   #SPFEDIT,X'01'       "SPFEDIT" OR "NOSPFEDIT"?
         BL    IF440                NEITHER, BRANCH
         BH    IF422                "NOSPFEDIT", BRANCH
         LTR   R3,R3                "SPFEDIT" -- CORRECT?
         BP    IF440                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF422    LTR   R3,R3                SPFEDIT?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF440    CLI   #AALIAS,X'01'        APPARENT ALIAS MEMBER CHECK?
         BL    IF450                NO, BRANCH
         SR    R3,R3                ASSUME NOT AN APPARENT ALIAS
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF444                YES, CANNOT BE AN APPARENT ALIAS
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF442    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF444                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF442                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF442                NO, BRANCH
         CLC   DIRNAME,MEMNAME      SAME MEMBER?
         BE    IF442                YES, BRANCH
         LA    R3,1                 AN APPARENT ALIAS MEMBER
         SPACE 1
IF444    CLI   #AALIAS,X'01'        "APPARENTALIAS" OR "NOAPPAREN.."?
         BL    IF450                NEITHER, BRANCH
         BH    IF446                "NOAPPARENTALIAS", BRANCH
         LTR   R3,R3                "APPARENTALIAS" -- CORRECT?
         BP    IF450                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF446    LTR   R3,R3                APPARENT ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF450    CLI   #HASALIA,X'01'       HAS ALIAS MEMBER CHECK?
         BL    IF460                NO, BRANCH
         SR    R3,R3                ASSUME IT HAS NO ALIASES
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BO    IF454                YES, CANNOT HAVE ALIASES
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         SPACE 1
IF452    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF454                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BNO   IF452                NO, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF452                NO, BRANCH
         LA    R3,1                 YES, HAS AN ALIAS
         SPACE 1
IF454    CLI   #HASALIA,X'01'       "HASALIAS" OR "NOHASALIAS"?
         BL    IF460                NEITHER, BRANCH
         BH    IF456                "NOHASALIAS", BRANCH
         LTR   R3,R3                "HASALIAS" -- CORRECT?
         BP    IF460                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF456    LTR   R3,R3                HAS ALIAS?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF460    CLI   #ORPHAN,X'01'        ORPHAN MEMBER CHECK?
         BL    IF470                NO, BRANCH
         TM    DIRFLAG,X'80'        MEMBER AN ALIAS?
         BNO   IF463                NO, CANNOT BE AN ORPHAN
         MVI   STARTTR+2,X'01'      TTR=000001 (START OF DIRECTORY)
         LA    R3,1                 ASSUME IT IS AN ORPHAN
         SPACE 1
IF462    BAL   R14,READDIR          GET NEXT DIRECTORY MEMBER
         B     IF464                LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'        IS THIS AN ALIAS?
         BO    IF462                YES, IGNORE
         CLC   DIRTTR,MEMTTR        TTR MATCH?
         BNE   IF462                NO, BRANCH
IF463    SR    R3,R3                NOT AN ORPHAN
         SPACE 1
IF464    CLI   #ORPHAN,X'01'        "ORPHAN" OR "NOORPHAN"?
         BL    IF470                NEITHER, BRANCH
         BH    IF466                "NOORPHAN", BRANCH
         LTR   R3,R3                "ORPHAN" -- CORRECT?
         BP    IF470                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF466    LTR   R3,R3                ORPHAN?
         BP    IF997                YES, DO ANY ELSE
         SPACE 3
IF470    CLI   #RLD0,X'01'          "RLDZERO" OR "NORLDZERO"
         BL    IF476                NEITHER, BRANCH
         BH    IF472                "NORLDZERO", BRANCH
         CLI   DIRATTR4,0           "RLDZERO" -- CORRECT?
         BE    IF476                YES, BRANCH
         B     IF997                NO, DO ANY ELSE
IF472    CLI   DIRATTR4,0           "NORLDZERO" -- CORRECT?
         BE    IF997                YES, DO ANY ELSE
         SPACE 1
IF476    TM    FLAGSCC,RECFMU            LOAD MODULE?
         BO    IF490                     YES, BRANCH
         CLI   #IFCREIF+2,0              ANY CREATION DATE?
         BE    IF480                     NO, CHECK FOR MODIFICATION
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         CLC   #IFCREIF(3),DIRSPFCR+1    HIGHER THAN DIRECTORY?
         BH    IF997                     YES, DO ANY ELSE
         CLC   #IFCREIF+3(3),DIRSPFCR+1  LOWER THAN DIRECTORY?
         BL    IF997                     YES, DO ANY ELSE
         SPACE 1
IF480    CLI   #IFMODIF+2,0              ANY MODIFICATION DATE?
         BE    IF490                     NO, CONTINUE
         TM    DIRFLAG,X'0F'             SPF STATISTICS?
         BNO   IF997                     NO, DO ANY ELSE
         CLC   #IFMODIF(3),DIRSPFCD+1    HIGHER THAN DIRECTORY?
         BH    IF997                     YES, DO ANY ELSE
         CLC   #IFMODIF+3(3),DIRSPFCD+1  LOWER THAN DIRECTORY?
         BL    IF997                     YES, DO ANY ELSE
         SPACE 3
IF490    DS    0H
         SPACE 1
IF996    MVC   DELAYMSG+25(4),=C'THEN'
         L     R2,#ACTIONT          THEN ACTION
         B     IF998
         SPACE 1
IF997    MVC   DELAYMSG+25(4),=C'ELSE'
         L     R2,#ACTIONE          ELSE ACTION
         SPACE 1
IF998    LTR   R2,R2                ANY ASSOCIATED ACTION?
         BNP   NEWCMD               NO, BRANCH
         TM    ##ADRPA#-##SUBCOM(R2),$D  SUPRESS DEFAULT MESSAGE?
         BO    IF999                     YES, BRANCH
         SPACE 1
         $MESAGE MSGBLANK
         TM    FLAGSAA,FDELAYM      ANY HEADER MESSAGE?
         BNO   IF999                NO, BRANCH
         XI    FLAGSAA,FDELAYM      NO MORE MESSAGE
         MVC   DELAYMSG+30(8),0(R2) SUBCOMMAND NAME
         $MESAGE DELAYMSG           OUTPUT THE HEADER MESSAGE
         SPACE 1
IF999    MVC   ##ANSWER(LISUBS),ISUBS INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(PTW),0(R2)    CHANGE THE SUBCOMMAND
         OI    FLAGSGG,FOUTSOME       A MATCH WAS MADE
         B     CALLCMDZ               CALL THE SECONDARY SUBCOMMAND
IFLA$ATT DC    V($ATT)                DEFAULT THEN ACTION
         TITLE 'P D S  --  PDS LIST, DIRENTRY, FIND, REPLACE  5/12/86'
***********************************************************************
***      LIST SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      DIRENTRY SUBCOMMAND   ADDED BY BRUCE LELAND -- JUNE, 1982  ***
***                                                                 ***
***      FIND SUBCOMMAND    MODIFIED BY BRUCE LELAND -- APR,  1985  ***
***                                                                 ***
***      REPLACE SUBCOMMAND    ADDED BY BRUCE LELAND -- APR,  1985  ***
***********************************************************************
*
LIST     CSECT
         USING *,R8
         MVI   ##HELOFF,C'L'       LIST SUBCOMMAND
         B     LIST010
*
         SPACE 1
DIRENTRY L     R8,=A(LIST)         BASE REGISTER FOR LIST
         LA    R1,L530             ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO      CORRECT?
         BZ    MSGNEW              YES, ERROR
         SPACE 1
         MVI   ##HELOFF,C'D'       DIRENTRY SUBCOMMAND
         IC    R1,DIRFLAG
         N     R1,=XL4'0000001F'   NUMBER OF HALFWORDS
         LA    R5,12(R1,R1)        LENGTH IN BYTES
         LA    R6,DIRNAME          START OF DIRECTORY ENTRY
         LA    R4,16               DUMP FORMAT, WIDTH=16
         MVI   MTHIGHL+4,2
         MVC   MSGTEXT1(3),L143$2  DIRECTORY DUMP HEADER
         MVC   INSERT#1(8),DIRNAME
         CVD   R5,DOUBLE
         MVC   INSERT#2-2(4),=X'40202120'
         ED    INSERT#2-2(4),DOUBLE+6
         AR    R5,R6
         BCTR  R5,0
         LA    R3,LIST600          PROCESSING ROUTINE
         BR    R3
         SPACE 2
*
FIND     L     R8,=A(LIST)         BASE REGISTER FOR LIST
         MVI   ##HELOFF,C'F'       FIND SUBCOMMAND
         MVI   ##HELOFF+3,C'D'     NO OUTPUT YET
         B     LIST008
         SPACE 2
*
REPLACE  L     R8,=A(LIST)         BASE REGISTER FOR LIST
         MVI   ##HELOFF,C'R'       REPLACE SUBCOMMAND
         MVI   ##HELOFF+3,C'D'     NO OUTPUT YET
         MVC   DSNMEMQ(8),DIRNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         B     NEWCMD              YES, OUTPUT AN IN-USE MESSAGE
         SPACE 1
         CLI   REPLTH+1,X'FF'      REPLACEMENT STRING EVER CODED?
         BNE   LIST002             YES, BRANCH
         LA    R1,L772             NO, CHECK WHICH MESSAGE TO OUTPUT
         CLI   FINDLTH+1,0         SEARCH STRING EVER CODED?
         BNE   MSGNEWXX            YES, TERMINATE THIS GROUP
         SPACE 1
LIST002  MVC   FLAGSDD(1),HFLAGSDD INITIALIZE FLAGSDD
         TM    DSORG,DS1DSGPO        DSORG=PO?
         BNO   LIST004               NO, BRANCH
         TM    FLAGSCC,RECFMU        LOAD MODULE?
         BNO   LIST004               NO, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BM    LIST004                          YES, BRANCH
         MVI   FLAGSDD,LDUMP                    NO, USE LDUMP
LIST004  TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BNM   LIST006                          NO, BRANCH
         LA    R1,L773
         CLC   FINDLTH(2),REPLTH   EQUAL LENGTH STRINGS?
         BNE   MSGNEWXX            NO, TERMINATE THIS GROUP
         SPACE 1
LIST006  TM    #REPFLAG,LIST#REP   REPLACE SPECIFIED?
         BNO   LIST008             NO, BRANCH
         BAL   R2,SHUTSTW2         CLOSE AND FREE THE STOW DCB
         SPACE 1
LIST008  LA    R1,L770
         CLI   FINDLTH+1,0         STRING EVER CODED?
         BZ    MSGNEWXX            NO, TERMINATE THIS GROUP
         OI    FLAGSGG,FOUTSUB     SUBCOMMAND GAINED CONTROL
         XC    WORKTBL,WORKTBL     RESET THE TRT TABLE
         SR    R15,R15
         IC    R15,FINDSTR         FIRST CHARACTER OF NEW STRING
         LA    R1,WORKTBL(R15)     ADDRESS OF NEW TRT BYTE
         MVI   0(R1),X'77'         SET TRT BYTE TO NON-ZERO
         SPACE 2
LIST010  MVC   FLAGSDD(1),HFLAGSDD INITIALIZE FLAGSDD
         OC    #MAXIN,#MAXIN       ANY INPUT DESIRED?
         BZ    LIST910             NO, BRANCH
         SPACE 2
         TM    DSORG,DS1DSGPO        DSORG=PO?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSCC,RECFMU        LOAD MODULE?
         BNO   LIST090               NO, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP  ANY LOAD FORMAT?
         BM    LIST020                          YES, BRANCH
         MVI   FLAGSDD,LDUMP                    NO, USE LDUMP
LIST020  TM    FLAGSDD,LBLOCK+LDUMP  LBLOCK OR LDUMP FORMAT?
         BZ    LIST090               NO, BRANCH
         SPACE 1
         LA    R2,#ESDPTR          START OF ESD LIST
         L     R15,=A(READESD)     ESD SYMBOL ROUTINE
         BALR  R14,R15             ANY SYMBOLS?
         B     *+8                 NO, "NO EXTERNAL SYMBOLS"
         B     LIST030             YES, BRANCH
         SPACE 2
         MVC   INSERT#1(8),DIRNAME MEMBER NAME IN ERROR
         $TMSG L704$1              OUTPUT AN ERROR MESSAGE
         B     LIST090             CONTINUE, BUT NO CSECT IDENTIFIERS
         SPACE 2
         USING ESDENTRY,R2
*  LOOPS TO SET MAXIMUM LENGTH FIELDS FOR LABEL REFERENCE ENTRIES
*  AND TO ASSOCIATE ENTRY RECORDS WITH THE APPROPRIATE CSECT NAMES
LIST030  OI    #LISTFLG,X'01'      CSECT OPERATION FLAG
         ICM   R2,B'1111',ESDLINK  ANY MORE ENTRIES?
         BZ    LIST060             NO, BRANCH
         MVI   ESDSEG#,0           CLEAR THE SEGMENT NUMBER
         CLI   ESDTYPE,CODESD      CSECT ENTRY?
         BNE   LIST030             NO, BRANCH
         LR    R1,R2
         SPACE 1
LIST040  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BZ    LIST060
         CLI   ESDTYPE-ESDENTRY(R1),CODELR  ASSOCIATED EXTERNAL LABEL?
         BNE   LIST030                      NO, BRANCH
         MVI   ESDSEG#-ESDENTRY(R1),0       CLEAR THE SEGMENT NUMBER
         ST    R2,ESDCSECT-ESDENTRY(R1)     SAVE POINTER TO NAME
         SR    R14,R14
         LH    R0,ESDID                     RELATIVE ESDID OF CSECT
         STH   R0,ESDID-ESDENTRY(R1)        SAVE FOR ENTRY
         L     R0,ESDADDR-1                 BASE START ADDRESS
         ST    R0,ESDMAIN-ESDENTRY(R1)      SAVE MAIN OFFSET
         A     R0,ESDLEN-1                  BASE MAXIMUM REFERENCE
         ICM   R14,B'0111',ESDADDR-ESDENTRY(R1)
         SR    R0,R14
         ST    R0,ESDLEN-ESDENTRY-1(R1)     MAXIMUM SYMBOL LENGTH
         B     LIST040
         SPACE 2
*  LOOP TO FILTER THE SYMBOL NAMES
LIST060  LA    R2,#ESDPTR          START OF ESD LIST
         SR    R3,R3
         ICM   R3,B'0001',#MODLEN  ANY FILTERING?
         BZ    LIST090             NO, BRANCH
         BCTR  R3,0                MACHINE LENGTH
         SPACE 1
LIST070  LR    R1,R2
         ICM   R2,B'1111',ESDLINK  END OF LIST?
         BZ    LIST090             YES, BRANCH
         CLC   ESDNAME(*-*),#MODTXT  <<EXECUTED>>
         EX    R3,*-6              MATCH THE PARTIAL NAME?
         BE    LIST070             YES, BRANCH
         MVC   ESDLINK-ESDENTRY(4,R1),ESDLINK
         LR    R2,R1                    DROP THIS SYMBOL
         B     LIST070
         SPACE 1
LIST090  MVC   STARTTR(3),DIRTTR   FIRST TTR
         SPACE 2
LIST100  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     LIST120                    00 - GOOD READ
         B     LIST110                    04 - END OF MEMBER
         B     LIST900                    08 - END OF DATA SET
         B     LIST910                    12 - I/O ERROR
         SPACE 1
LIST110  TM    DSORG,DS1DSGIS           ISAM DATA SET?
         BNO   LIST900                  NO, BRANCH
         $TMSG L005                     YES, OUTPUT A MESSAGE
         B     LIST100                  CONTINUE
         SPACE 1
LIST120  NI    #LISTFLG,FF-X'20'        TURN OFF THE CSECT DATA FLAG
         L     R5,LS                    CURRENT LENGTH
         LR    R6,R0                    START OF BUFFER
         LR    R1,R5                    SAVE BLOCKSIZE FOR LATER
         LM    R14,R15,#LISTN0          SAVE IF NOT PHYSICAL BLOCKS
         L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R5                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         TM    FLAGSDD,BLOCK            BLOCK OUTPUT DESIRED?
         BO    LIST400                  YES, BRANCH
         TM    FLAGSDD,DUMP             DUMP OUTPUT DESIRED?
         BO    LIST420                  YES, BRANCH
         STM   R14,R15,#LISTN0          NOT PHYSICAL BLOCKS
         AR    R5,R6                    ADDRESS OF END OF BUFFER
         BCTR  R5,0                     END OF BUFFER ADDRESS
         LA    R3,LIST200               ASSUME FIXED OR UNDEFINED
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL                 GET RECORD LENGTH
         BOR   R3                       YES, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1                    BLOCK LENGTH
         BOR   R3                       YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNOR  R3                       NO, PROCESS LIKE RECFM=U
         SPACE 1                        SET END OF BUFFER ADDRESS
         AH    R6,=H'4'                 RECFM=V; START OF LRECL'S
         LA    R3,LIST150               VARIABLE LEN RECORD PROCESSOR
         SPACE 2
LIST150  SR    R4,R4
         ICM   R4,B'0011',0(R6)         RECORD LENGTH +4
         AH    R6,=H'4'                 SKIP OVER HEADER
         SH    R4,=H'4'                 SUBTRACT HEADER LENGTH
         BP    LIST200                  BRANCH IF RECORD EXISTS
         SR    R4,R4                    NO, RESET LENGTH TO ZERO
         SPACE 1
LIST160  LR    R0,R4                    PREVIOUS RECORD LENGTH
         A     R0,#LISTN1               OFFSET IN CURRENT RECORD
         ST    R0,#LISTN1               UPDATE
         ICM   R0,B'1111',#MAXFIND      DONE WITH FIND/REPLACE?
         BP    LIST166                  NO, BRANCH
         TM    #REPFLAG,LIST#REP        WRITE SPECIFIED?
         BNO   NEWCMD                   NO, BRANCH
         TM    #REPFLAG,LISTUPDE        UPDATE ERROR?
         BO    NEWCMD                   YES, BRANCH
         TM    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK?
         BO    LIST170                  YES, BRANCH
         B     NEWCMD
         SPACE 1
LIST166  BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         CLI   ##HELOFF,C'D'            DIRENTRY SUBCOMMAND?
         BE    DIR000                   YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BZ    LIST170                  NO, GET A NEW BLOCK
         LM    R3,R6,#LFSAVE4           RESTORE R3-R6
         BXLE  R6,R4,0(R3)              SKIP TO NEXT RECORD
         SPACE 3
*  REWRITE ANY UPDATED RECORDS FOR REPLACE
LIST170  CLI   ##HELOFF,C'R'            REPLACE SUBCOMMAND?
         BNE   LIST100                  NO, BRANCH
         TM    #REPFLAG,LIST#REP        WRITE SPECIFIED?
         BNO   LIST100                  NO, BRANCH
         TM    #REPFLAG,LISTUPDE        UPDATE ERROR?
         BO    LIST198                  YES, BRANCH
         TM    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK?
         BNO   LIST198                  YES, BRANCH
         XI    #REPFLAG,LISTUPDR        NO DATA UPDATED NOW
         SPACE 1
         TM    #REPFLAG,LISTUPDO        OPEN FOR UPDATE?
         BO    LIST190                  YES, BRANCH
         OI    #REPFLAG,LISTUPDO        OPEN FOR UPDATE NOW
         SPACE 1
         OI    FLAGSCC,FUPDATE          MARK FOR STOW OPEN ROUTINE
         BAL   R2,OPENSTOW              OPEN OUTPUT DCB AND RESERVE
         B     NEWCMD                   COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE OBTAINED
         LH    R0,=H'32760'             MAXIMUM BUFFER SIZE
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#LISTBUF              POINTER TO BUFFER AREA
         SPACE 1
* UPDATE IDR RECORDS FOR LOAD MODULES IF REQUIRED
*  IDR FOR SUPERZAP TAKES THE FOLLOWING FORM:
*        BYTE  0.....'80' FOR ID
*        BYTE  1.....LENGTH OF IDR (USUALLY X'FA' = 250.)
*        BYTE  2.....WHO'S IDR IT IS (X'01' FOR SUPERZAP AND PDS)
*        BYTE  3.....NUMBER OF 13-BYTE IDR DATA ENTRIES (BITS 2-7)
*        BYTES 4-16, 17-29,... IDR DATA ENTRY AS FOLLOWS:
*              BYTES 0-1 --- ESD ID IN BINARY (FIRST IF NOT KNOWN)
*              BYTES 2-4 --- DATE ('YYDDDF')
*              BYTES 5-12 -- INFO ('USERID' WHERE USERID=TSO USERID)
         SPACE 2
         TM    DSORG,DS1DSGPO          PARTITIONED?
         BNO   LIST190                 NO, BRANCH
         TM    FLAGSCC,RECFMU          RECFM=U?
         BNO   LIST190                 NO, BRANCH
         ICM   R0,B'0111',DIRTTR       TTR TO START WITH
         SLL   R0,8                    Z OF TTRZ IS ZERO
         ST    R0,DOUBLE               SAVE TTRZ TEMPORARILY
         SPACE 1
         LA    R0,DOUBLE               POINT TO TTRZ VALUE
         POINT STOWDCB,(0)             POINT TO BLOCK TO BE UPDATED
         SPACE 1
LIST174  LH    R0,=H'32760'            MAXIMUM BUFFER SIZE
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,#LISTBUF             START OF BUFFER
         SPACE 1
         READ  FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
         LR    R1,R2
         TM    0(R1),X'01'              CONTROL RECORD OF SOME KIND?
         BO    LIST190                  YES, CANNOT FIND THE ZAP IDR
         SPACE 1
         CLI   0(R1),X'20'              CESD?
         BNE   LIST180                  NO, BRANCH
         OC    #LISTIDU(2),#LISTIDU     ANY ESDID?
         BNZ   LIST174                  YES, BRANCH
         LH    R3,4(R2)                 FIRST ESDID
LIST178  LA    R2,16(,R2)               NEXT ESD ENTRY
         LA    R3,1(,R3)                NEXT ESDID
         CLI   0(R2),0                  CSECT ENTRY?
         BNE   LIST178                  NO, BRANCH
         OC    5(3,R2),5(R2)            VALID LENGTH?
         BZ    LIST178                  NO, BRANCH
         BCTR  R3,0                     LESS ONE
         STH   R3,#LISTIDU              UPDATE THE ESDID
         B     LIST174
         SPACE 1
LIST180  CLI   0(R1),X'40'              TEST SYMBOLS?
         BE    LIST174                  YES, READ ANOTHER
         CLI   0(R1),X'80'              IDR?
         BNE   LIST190                  NO, BYPASS IDR MARKING
         TM    2(R1),X'01'              YES - BUT DONE BY SUPERZAP?
         BNO   LIST174                  NO - FORGET THIS ONE
         SR    R2,R2                    CLEAR WORK REG
         IC    R2,3(R1)                 PICK UP # OF IDR DATA ENTRIES
         LA    R2,1(R2)                 INCREMENT # OF IDR ENTRIES...
         LR    R4,R2                    SAVE IT FOR LATER
         N     R2,=XL4'0000003F'        WE JUST WANT BITS 2-7
         MH    R2,=H'13'                EACH ENTRY IS 13 BYTES LONG
         LA    R2,3(R2)                 IDR HDR IS 3 BYTES LONG
         MVC   FULLWORD(2),0(R1)        CURRENT COUNTS
         MVI   FULLWORD,0               CLEAR FIRST BYTE
         NI    FULLWORD,X'3F'           MASK SECOND BYTE
*  SO NOW R2 HAS AN OFFSET TO THE NEXT AVAILABLE POSITION IN THE IDR
         CLM   R2,B'0011',FULLWORD      AT END OF THE IDR RECORD?
         BH    LIST174                  YES, BRANCH
         SH    R2,=H'13'                BACK UP OFFSET
         STC   R4,3(R1)                 UPDATE COUNT
         AR    R2,R1                    NEXT IDR POSITION
         MVC   1(2,R2),#LISTIDU         MOVE IN ESDID
         TIME  ,                        GET THE DATE
         STCM  R1,B'0111',3(R2)         SAVE YYDDDF IN IDR DATA FIELD
         L     R1,ADDRPSCB              ADDRESS OF THE PSCB
         MVC   6(7,R2),PSCBUSER-PSCB(R1)  LOGON USERID
         MVI   6+7(R2),X'40'              FOLLOWING BLANK
         SPACE 1
         L     R2,#LISTBUF             START OF BUFFER
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
LIST190  LH    R1,#REPCNT
         LA    R1,1(,R1)
         STH   R1,#REPCNT
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         ST    R0,DOUBLE                  SAVE TTRZ TEMPORARILY
         SPACE 1
         POINT STOWDCB,DOUBLE             POINT TO BLOCK TO BE UPDATED
         SPACE 1
         L     R0,LS                      RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB UPDATE IN THE DCB
         L     R2,#LISTBUF                START OF BUFFER
         READ  FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
         L     R2,EXCPBUFF                EXCHANGE FOR THE UPDATED DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 1
LIST198  CLI   OPENLIST,X'7C'                    REPLACE TERMINATION?
         BNE   LIST100                           NO, BRANCH
         MVI   MTHIGHL,L'MSGBLKU-1
         MVC   INSERT#1-1(L'MSGBLKU),MSGBLKU
         LH    R0,#REPCNT
         CVD   R0,DOUBLE
         ED    INSERT#1-1(10),DOUBLE+4
         $TMSG L145$1
         BAL   R2,SHUTSTW2                       FORCE STOW DCB CLOSE
         B     NEWCMD
         SPACE 3
LIST200  L     R0,#LISTN0               PREVIOUS MEMBER DISPLACEMENT
         ST    R0,#LISTN1               UPDATE MEMBER DISPLACEMENT
         AR    R0,R4                    ADD CURRENT LENGTH FOR NEXT
         ST    R0,#LISTN0               UPDATE FOR NEXT ENTRY
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         STM   R3,R6,#LFSAVE4             SAVE FOR LBLOCK/LDUMP
         L     R0,#SKIPREC
         S     R0,=F'1'                   SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST160                    YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                   SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                    YES, BRANCH
LIST210  LR    R1,R4                      LENGTH OF DATA AREA
         TM    FLAGSDD,LBLOCK             LBLOCK?
         BO    LIST400                    YES, BRANCH
         TM    FLAGSDD,LDUMP              LDUMP?
         BO    LIST420                    YES, BRANCH
         LA    R0,8                       WIDTH OF LINE NUMBER FIELD
         LR    R15,R6                     MOVE START POSITION
         LR    R14,R15                    LINE NUMBER FOR RECFM=V OR U
         A     R15,#SKIPCOL               ADJUST START FOR START COLUMN
         S     R1,#SKIPCOL                ADJUST LEN FOR START COLUMN
         TM    FLAGSDD,NONUM+DONONUM      NONUM FORMAT?
         BNZ   LIST330                    YES, BRANCH
         AR    R15,R0                     ADJUST START FOR RECFM=V OR U
         SR    R1,R0                      ADJUST LENGTH, RECFM=V U OR F
         TM    FLAGSCC,RECFMF             RECFM=F?
         BNO   LIST230                    NO, BRANCH
         SR    R15,R0                     CORRECT DATA START POINTER
         LA    R14,0(R4,R6)               END OF LINE
         SR    R14,R0                     START OF LINE NUMBER
LIST230  TM    FLAGSDD,SNUM               SNUM FORMAT?
         BO    LIST330                    YES, BRANCH
         MVC   MSGLINE+4(8),0(R14)        NUM FORMAT -- GET LINE NUMBER
         NC    MSGLINE+4(8),LISTL000      DROP NUMERIC PART
         CLC   MSGLINE+4(8),LISTL000      ZONE DIGITS FOR ALL BYTES?
         BE    LIST240                    YES, BRANCH
         OI    FLAGSDD,DONONUM            CONTINUE WITH NONUM FORMAT
         B     LIST210
         SPACE 1
LIST240  MVC   MSGLINE+4(6),0(R14)        SPF LINE NUMBER FIELD
         TM    DIRFLAG,X'0F'              SPF ATTRIBUTES STORED?
         BO    LIST320                    YES, BRANCH
         LA    R0,5                       MAXIMUM OF 5 DIGITS TO BLANK
         LA    R2,MSGLINE+3               START OF FIELD TO BLANK
         MVC   MSGLINE+4(6),2(R14)        LINE NUMBER FIELD
LIST310  LA    R2,1(,R2)
         CLI   0(R2),C'0'                 BLANK LEADING ZEROES IN THE
         BNE   LIST320                       LINE NUMBER FIELD
         MVI   0(R2),X'40'
         BCT   R0,LIST310                 DO UP TO 5 DIGITS
LIST320  LA    R0,249                     MAXIMUM ALLOWED DATA LENGTH
         MVI   MSGLINE+10,X'40'           BLANK AFTER LINE NUMBER
         LA    R14,MSGLINE+11             WHERE TO MOVE DATA
         B     LIST340
         SPACE 2
LIST330  LA    R0,256                   MAXIMUM LRECL TO DISPLAY
         LA    R14,MSGLINE+4            WHERE TO MOVE OUTPUT DATA
         SPACE 1
LIST340  CR    R1,R0                    LENGTH>MAXIMUM ALLOWED?
         BL    *+6                      NO, BRANCH
         LR    R1,R0                    YES, USE MAXIMUM
         S     R1,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST360                  NO, BRANCH
         STM   R14,R1,#LUSAVE4          SAVE R14, R15, R0, R1
***      MVC   0(*-*,R14),0(R15)        <<EXECUTED>>
***      EX    R1,*-6                   MOVE DATA INTO LINE
         SPACE 1
LIST360  LR    R2,R1                    MACHINE LENGTH
         LA    R1,MSGLINE+4             START OF DATA
         CR    R14,R1                   NUM MODE?
         BE    *+8                      NO, BRANCH
         AH    R2,=H'7'                 YES, ACCOUNT FOR LINE NO, BLANK
         A     R2,=F'1'                 ACTUAL DATA LENGTH
         C     R2,#MAXLEN               LENGTH>#MAXLEN?
         BL    *+8                      NO, BRANCH
         L     R2,#MAXLEN               YES, USE #MAXLEN INSTEAD
         S     R2,=F'1'                 MACHINE LENGTH VALID?
         BM    LIST160                  LENGTH=0, BRANCH
         LA    R0,5(,R2)                ACTUAL LINE LENGTH + HALFWORDS
         SLL   R0,16
         ST    R0,MSGLINE               CREATE OUTPUT LINE HEADER
         L     R1,#LUSAVE4+4            START OF DATA
         L     R2,#LUSAVE4+12           MACHINE LENGTH
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         LA    R14,1(R1,R2)             END OF DATA
         ST    R14,#LUSAVE4+8           END OF DATA
         MVI   ##HELOFF+1,C'N'          N OF FN.D OR RN.D
         CLI   ##HELOFF,C'F'            FIND OPERATION?
         BE    LIST370                  YES, BRANCH
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BNE   LIST399                  NO, LIST -- BRANCH
         SPACE 1
LIST370  L     R14,#LUSAVE4+8           END OF DATA
         LH    R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         LH    R15,FINDLTH              STRING LENGTH
         ICM   R0,B'1111',#MAXFIND      DONE WITH FIND/REPLACE?
         BNP   LIST398                  YES, BRANCH
         BCTR  R15,0                    MACHINE LENGTH
         EX    R2,LISTTRT               FIRST CHARACTER FOUND?
         BZ    LIST398                  NO, NOT IN STRING, QUIT
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BNL   LIST398                  NO, QUIT
         CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         EX    R15,*-6                  ENTIRE STRING FOUND?
         BNE   LIST397                  NO, TRY THE DATA AGAIN
         SPACE 2
**       IF RECFM=F, LRECL=80 AND NUM OR SNUM FORMAT,
**       THEN DO NOT USE THE CONTINUATION COLUMN (COLUMN 72)
         LR    R0,R2
         TM    FLAGSCC,RECFMF           RECFM=F?
         BNO   LIST374                  NO, BRANCH
         CLI   LRECL+1,80               LRECL=80?
         BNE   LIST374                  NO, BRANCH
         TM    FLAGSDD,NONUM            NONUM FORMAT SPECIFIED?
         BO    LIST374                  YES, BRANCH
         S     R0,=F'1'                 DROP THE CONTINUATION COLUMN
         CR    R15,R0                   POSSIBLY IN STRING?
         BNL   LIST397                  NO, BRANCH
LIST374  L     R15,#MAXFIND             FOUND
         S     R15,=F'1'                     ANOTHER
         ST    R15,#MAXFIND                         ONE
         SPACE 1
*** ADDED FOR  REPLACE MEMBER /BEFORE/AFTER/ . . . .
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BE    LIST376                  YES, BRANCH
         MVI   ##HELOFF+1,C'F'          NO, FIND
         AH    R1,FINDLTH               POINT AFTER THE STRING
         BCTR  R1,0                     POINT AT END OF STRING
         B     LIST397
         SPACE 1
LIST376  AR    R0,R1                    LAST MOVABLE BYTE
         LR    R15,R1                   REPLACEMENT LENGTH
         AH    R15,REPLTH               TARGET OF MOVE
         LR    R2,R1                    REPLACEMENT LENGTH
         AH    R2,FINDLTH               SOURCE OF MOVE
         CR    R15,R2                   REPLACE LENGTH:FIND LENGTH
         BL    LIST390                    LOW: REDUCE THE STRING
         BE    LIST394                    EQUAL: REPLACE THE STRING
*                                         HIGH:  EXPAND THE STRING
LIST380  CR    R2,R15                   COMPLETED?
         BE    LIST394                  YES, BRANCH
         LR    R14,R2                   START A NEW SCAN
         BCTR  R14,0                    LESS ONE
LIST382  LA    R14,1(,R14)              NEXT BYTE
         CR    R14,R0                   POSSIBLY ANY BLANKS?
         BNL   LIST388                  NO, BRANCH
         CLI   0(R14),X'40'             BLANK?
         BNE   LIST382                  NO, BRANCH
         CLI   1(R14),X'40'             FOLLOWED BY ANOTHER BLANK?
         BNE   LIST382                  NO, BRANCH
         SPACE 1
LIST384  BCTR  R14,0
         MVC   1(1,R14),0(R14)          MOVE THE PREVIOUS CHARACTER
         CR    R14,R2                   DONE WITH THE MOVE?
         BH    LIST384                  NO, BRANCH
         LA    R2,1(,R2)                NEXT TARGET BYTE
         B     LIST380                  REPLACE THE STRING
         SPACE 1
LIST388  BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         $TMSG L774                     ERROR MESSAGE
         OI    #REPFLAG,LISTUPDE        MARK AN UPDATE ERROR
         B     LIST399
         SPACE 2
LIST390  CR    R15,R2                   COMPLETED?
         BE    LIST394                  YES, BRANCH
         MVI   0(R15),X'40'             MOVE IN A BLANK
         CR    R2,R0                    CAN IT BE MOVED?
         BNL   *+10                     NO, BRANCH
         MVC   0(1,R15),0(R2)           YES, MOVE IT DOWN
         LA    R15,1(,R15)              NEXT TARGET BYTE
         CLI   0(R2),X'40'              FOUND FIRST BLANK?
         BE    LIST390                  YES, BRANCH
         CR    R2,R0                    NEXT SOURCE BYTE VALID?
         BNL   LIST390                  NO, LOOP
         LA    R2,1(,R2)                YES, INCREMENT
         B     LIST390
         SPACE 1
LIST394  LH    R15,REPLTH               ACTUAL LENGTH OF STRING
         S     R15,=F'1'                MACHINE LENGTH OF STRING VALID?
         BM    LIST395                  NO, BRANCH
         MVC   0(*-*,R1),REPLSTR        <<EXECUTED>> -- MOVE IN STRING
         EX    R15,*-6                  MOVE IN THE REPLACEMENT
LIST395  AR    R1,R15                   POSITION AFTER REPLACED STRING
         MVI   ##HELOFF+1,C'R'          REPLACED DATA
         OI    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK
         L     R2,#LUSAVE4+8            END OF DATA
         LA    R0,1(,R1)                TRY NEXT CHARACTER
         SR    R2,R0                    MACHINE LENGTH LEFT
         STH   R2,#LUSAVE4+12           MACHINE LENGTH LEFT
         SPACE 2
LIST397  LA    R1,1(,R1)                TRY NEXT CHARACTER
         L     R2,#LUSAVE4+8            END OF DATA
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   LIST370                  NO, KEEP SEARCHING
         SPACE 2
LIST398  CLI   ##HELOFF+1,C'N'          UNSUCCESSFUL FIND OR REPLACE?
         BE    LIST160                  YES, BRANCH
         SPACE 1
LIST399  L     R14,#LUSAVE4+0           WHERE OUTPUT GOES
         L     R15,#LUSAVE4+4           DATA START
         LH    R1,#LUSAVE4+14           ORIGINAL MACHINE LENGTH
         MVC   0(*-*,R14),0(R15)        <<EXECUTED>>
         EX    R1,*-6                   MOVE DATA INTO LINE
         B     LIST730                  OUTPUT THIS LINE
         SPACE 3
LIST400  LA    R4,64                    BLOCK FORMAT, WIDTH=64
         B     LIST430                  FORMAT HEADER
         SPACE 1
LIST420  LA    R4,16                    DUMP FORMAT, WIDTH=16
         SPACE 1
LIST430  LA    R3,LIST600               PROCESSING ROUTINE
         L     R0,#SKIPCOL              CHARACTERS TO SKIP
         ST    R0,#LISTN2               UPDATE BLOCK OFFSET
         A     R0,#LISTN1               CURRENT DATA
         ST    R0,#LISTN1                           DISPLACEMENT
         MVI   MTHIGHL,5
         MVI   MTHIGHL+4,L'MSGDUMP-1
         MVC   MSGTEXT1(3),L140$2
         MVC   INSERT#1(5),LISTLBLO
         MVC   INSERT#2-1(40),MSGDUMP   HEADER FOR BLOCK OR DUMP
         NI    FLAGSDD,FF-OVERLAP       TURN OFF THE OVERLAP INDICATOR
         LR    R5,R1                    DATA LENGTH
         C     R5,#MAXLEN               RESTRICT DATA LENGTH?
         BL    *+8                      NO, BRANCH
         L     R5,#MAXLEN               YES, USE MAX LENGTH
         AR    R5,R6                    COMPUTE NEW END-OF-BLOCK
         BCTR  R5,0                     COMPUTE NEW END-OF-BLOCK -1
         A     R6,#SKIPCOL              ADJUST FOR START COLUMN
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST450                  YES, BRANCH
         L     R0,#BLKCNT
         A     R0,=F'1'
         ST    R0,#BLKCNT
         CVD   R0,DOUBLE
         ED    INSERT#2-1(10),DOUBLE+4
         L     R0,#SKIPREC
         S     R0,=F'1'                 SKIP THIS RECORD?
         ST    R0,#SKIPREC
         BNM   LIST170                  YES, BRANCH
         L     R0,#MAXIN
         S     R0,=F'1'                 SUFFICIENT INPUT RECORDS?
         ST    R0,#MAXIN
         BM    LIST910                  YES, BRANCH
         CVD   R1,DOUBLE                  RECORD LENGTH
         ED    INSERT#2+MSGDUMPL-MSGDUMP-1(7),DOUBLE+5
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         ST    R0,DOUBLE                SAVE RESULTING TTR
         UNPK  INSERT#2+MSGDUMPT-MSGDUMP-1(7),DOUBLE(4)
         TR    INSERT#2+MSGDUMPT-MSGDUMP-1(6),TRTABLE
         MVI   INSERT#2+MSGDUMPT-MSGDUMP+6-1,X'40'
         TM    FLAGSDD,DUMP
         BNOR  R3
         MVC   INSERT#1(5),LISTLDUM
         BR    R3
         SPACE 2
LIST450  TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BNOR  R3                       NO, BRANCH
         OI    #LISTFLG,X'08'           NEED A HEADER CHANGE
         STH   R4,#LISTWD               WIDTH OF EACH LINE
         L     R6,EXCPBUFF              BUFFER START
         L     R5,LS                    BUFFER LENGTH
         AR    R5,R6                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    #LISTFLG,X'40'           ANY CSECT ADDRESS YET?
         BO    LIST460                  YES, BRANCH
         TM    0(R6),X'F0'              TEST, SCATTER, CESD OR IDR?
         BM    LIST170                  YES, BRANCH
LIST460  TM    #LISTFLG,X'C0'           ANY CURRENT ESD RECORD?
         BO    LIST470                  YES, THIS IS A DATA RECORD
         CLI   8(R6),X'06'              AN ESD RECORD?
         BNE   LIST170                  NO, BRANCH
         OI    #LISTFLG,X'C0'           FLAGS FOR ESD RECORD INPUT
         MVI   #LISTVIR,00              CLEAR THE HIGH-ORDER DIGIT
         MVC   #LISTVIR+1(3),9(R6)      SAVE THE OFFSET ADDRESS
         LH    R15,6(R6)                OFFSET TO ESDID
         LA    R5,16(R15,R6)            FIRST ESDID FOR THIS BLOCK
         LH    R6,4(R6)                 ESDID ENTRIES *4
         SRL   R6,2                     ESDID ENTRIES
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         SPACE 2
*  LOOP TO MARK ENTRIES FOR THE NEXT PHYSICAL BLOCK
LIST464  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST170                  NO, DONE
         MVI   ESDIDIN,0                TURN OFF FIRST
         LR    R14,R5                   START OF ESDID LIST
         LR    R15,R6                   NUMBER OF ESDID ENTRIES
         SPACE 1
LIST468  CLC   0(2,R14),ESDID           THIS ENTRY?
         BNE   *+8                      NO, BRANCH
         MVI   ESDIDIN,1                YES, MARK IT
         LA    R14,4(,R14)              NEXT ESDID ENTRY
         BCT   R15,LIST468              MARK ALL ENTRIES THAT MATCH
         B     LIST464                  CHECK ALL ESD ENTRIES
         SPACE 3
LIST470  XI    #LISTFLG,X'A0'           OFF CURRENT ESD; ON CSECT DATA
         L     R0,#LISTVIR              BLOCK OFFSET
         L     R14,#LISTOFF             LIST OFFSET QUANTITY
         ST    R14,#LISTN2              BLOCK OFFSET
         A     R14,#LISTN1              DATA
         ST    R14,#LISTN1                  DISPLACEMENT
         ICM   R14,B'1111',#LISTOFF     LIST OFFSET?
         BP    *+6                      YES, BRANCH
         LR    R14,R0                   NO, USE NORMAL BLOCK OFFSET
         SR    R6,R0
         ST    R6,#LISTVIR              VIRTUAL BUFFER START ADDRESS
         LA    R2,#ESDPTR               START OF ESD ADDRESSES
         AR    R6,R14                   OFFSET --> ACTUAL ADDRESS
         SPACE 2
*  LOOP TO FIND A START ENTRY
LIST480  ICM   R2,B'1111',ESDLINK       NEXT ENTRY?
         BZ    LIST170                  NO, DONE
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR      START ADDRESS OF SYMBOL
         LR    R15,R14                  CSECT START OFFSET
         A     R14,#LISTVIR             OFFSET --> ADDR OF CSECT START
         A     R15,ESDLEN-1             CSECT END OFFSET
         A     R15,#LISTVIR             OFFSET --> ADDRESS OF CSECT END
         CR    R6,R15                   CURRENT:CSECT END
         BNL   LIST480                    HIGH OR EQUAL, LOOP FOR NEXT
         CR    R5,R14                   END:CSECT START
         BL    LIST480                    LOW, LOOP FOR NEXT
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST480                  NO, LOOP FOR NEXT
         MVC   #LISTID(2),ESDID         YES, MARK THE CURRENT ESDID
         ST    R2,#LISTENT              SAVE THIS ENTRY START
         LR    R1,R2
         SPACE 1
LIST490  ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)  NEXT ENTRY?
         BZR   R3                               NO, DONE
         CLC   #LISTID(2),ESDID-ESDENTRY(R1)    SAME ESDID ON NEXT ONE?
         BNER  R3                               NO, DONE
         ICM   R15,B'0111',ESDADDR-ESDENTRY(R1) NEXT ENTRY START
         A     R15,#LISTVIR                     OFFSET --> NEXT START
         CR    R14,R15                          CSECT START:NEXT ENTRY
         BE    LIST490                            SAME, IGNORE ENTRY
         CR    R15,R6                           NEXT START:CURRENT
         BNH   LIST480                            NOT ABOVE, USE NEXT
         BR    R3
         SPACE 3
LIST600  BAL   R14,LIST800              PERFORM ANY CSECT POSITIONING
         LR    R2,R4                    DATA LENGTH
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    LIST160                  NO, BRANCH
         CR    R2,R14
         BNH   *+8
         LA    R2,1(,R14)               DATA LENGTH FOR THIS SEGMENT
         BCTR  R2,0                     MACHINE LENGTH FOR THE SEGMENT
         ST    R2,#LUSAVE4+8            SAVE FOR REPLACE
         CLI   ##HELOFF,C'R'            REPLACE OPERATION?
         BE    LIST644                  YES, BRANCH
         CLI   ##HELOFF,C'F'            FIND OPERATION?
         BNE   LIST690                  NO, LIST -- BRANCH
LIST644  XC    #LUSAVE4(8),#LUSAVE4
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LH    R15,FINDLTH              STRING LENGTH
         ST    R14,#LUSAVE4             END OF DISPLAYED PART
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    MACHINE LENGTH OF STRING
         CR    R14,R5                   BEYOND RECORD END?
         BL    LIST660                  NO, BRANCH
         LR    R14,R5                   YES, USE END OF RECORD -1
         SPACE 1
LIST660  ICM   R0,B'1111',#MAXFIND      ANY MORE TO FIND?
         BNP   LIST670                  NO, BRANCH
         EX    R2,LISTTRT               FIRST CHARACTER FOUND?
         BZ    LIST670                  NO, NOT IN STRING
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   POSSIBLY IN STRING?
         BH    LIST670                  NOT IN STRING
         CLC   0(*-*,R1),FINDSTR        <<EXECUTED>> -- COMPLETE STRING
         EX    R15,*-6                  ENTIRE STRING FOUND?
         BNE   LIST666                  NO, BRANCH
         ST    R1,#LUSAVE4+4            YES, SAVE THE LAST ADDRESS
         L     R0,#MAXFIND              FOUND
         S     R0,=F'1'                      ONE
         ST    R0,#MAXFIND                      MORE
         CLI   ##HELOFF,C'R'            REPLACE SUBCOMMAND?
         BNE   LIST664                  NO, BRANCH
         MVC   0(*-*,R1),REPLSTR        <<EXECUTED>> -- REPLACE STRING
         EX    R15,*-6                  MOVE IN THE REPLACEMENT
         OI    #REPFLAG,LISTUPDR        REPLACED DATA IN THIS BLOCK
         SPACE 1
LIST664  AR    R1,R15                   SKIP OVER THE REPLACED DATA
         OC    #LISTIDU(2),#LISTIDU     ANY ESDID YET?
         BNZ   LIST666                  YES, BRANCH
         MVC   #LISTIDU(2),#LISTID      NO, SAVE THIS ONE
LIST666  LA    R1,1(,R1)                NO, TRY NEXT CHARACTER
         L     R2,#LUSAVE4              LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   LIST660                  NO, CONTINUE SEARCHING
         SPACE 2
LIST670  ICM   R1,B'1111',#LUSAVE4+4    ANY STRING FOUND?
         BZ    LIST680                  NO, BRANCH
         SR    R1,R6                    DISPLACEMENT INTO DISPLAY
         AH    R1,FINDLTH               LENGTH AT END OF COMPARE
         CR    R1,R4                    BEYOND DISPLAYED LENGTH?
         BNH   LIST684                  NO, BRANCH
         OI    FLAGSDD,OVERLAP          YES, MARK AS AN OVERLAP
         B     LIST690
         SPACE 1
LIST680  TM    FLAGSDD,OVERLAP          NO STRING FOUND -- OVERLAP SET?
         BO    LIST684                  YES, BRANCH
         L     R2,#LUSAVE4+8            MACHINE LENGTH OF THIS SEGMENT
         L     R1,#LISTN2               CSECT
         LA    R1,1(R1,R2)                   BYTE
         ST    R1,#LISTN2                        DISPLACEMENT
         B     LIST160
         SPACE 1
LIST684  NI    FLAGSDD,FF-OVERLAP       CLEAR THE OVERLAP FLAG
         B     LIST690
         SPACE 1
LISTTRT  TRT   0(*-*,R1),WORKTBL        <<EXECUTED>> -- FIND FIRST BYTE
         SPACE 2
LIST690  L     R2,#LUSAVE4+8            MACHINE LENGTH
         LA    R0,20(,R2)               LINE OUTPUT LENGTH
         LA    R1,MSGLINE+17            OUTPUT DATA START ADDRESS
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BE    LIST691                  YES, BRANCH
         LA    R15,MSGLINE              OUTPUT START FOR OFFSETS
         TM    FLAGSDD,LBLOCK+BLOCK     ANY BLOCK FORMAT?
         BM    LIST692                  YES, BRANCH
LIST691  LA    R0,62(,R2)               LINE OUTPUT LENGTH
         LA    R1,MSGLINE+57            OUTPUT CHARACTER DUMP ADDRESS
         LA    R15,MSGLINE+2            OUTPUT START FOR OFFSETS
         MVC   MSGLINE(80),MSGBL132     CLEAR THE OUTPUT LINE
         SPACE 2
LIST692  SLL   R0,16
         ST    R0,MSGLINE               OUTPUT LENGTH
         MVI   0(R1),C'*'               FIRST *
         MVC   1(*-*,R1),0(R6)          <<EXECUTED>>
         EX    R2,*-6                   MOVE IN THE DATA
         LA    R1,2(R1,R2)              POSITION OF FINAL ASTERISK
         MVI   0(R1),C'*'               FINAL *
         SPACE 1
         UNPK  4(7,R15),#LISTN1+1(4)    UNPACK ADDRESS
         TR    4(6,R15),TRTABLE         CONVERT TO HEXADECIMAL
         MVI   10(R15),X'40'            BLANK THE FOLLOWING BYTE
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BNE   *+10                     NO, BRANCH
         MVC   4(6,R15),BLANKS          YES, BLANK THIS FIELD
         UNPK  DOUBLE(7),#LISTN2+1(4)   UNPACK ADDRESS
         TR    DOUBLE(7),TRTABLE        CONVERT TO HEXADECIMAL
         MVC   11(4,R15),DOUBLE+2       MOVE IN ADDRESS
         MVI   15(R15),X'40'            BLANK THE FOLLOWING BYTE
         CLI   DOUBLE+1,C'0'            LESS THAN 64K?
         BE    *+10                     YES, BRANCH
         MVC   11(5,R15),DOUBLE+1       MOVE IN ADDRESS
         MVI   16(R15),X'40'            BLANK ANOTHER BYTE
         CLI   ##HELOFF,C'D'            DIRENTRY?
         BE    LIST694                  YES, BRANCH
         TM    FLAGSDD,LBLOCK+BLOCK     ANY BLOCK FORMAT?
         BM    LIST700                  YES, BRANCH
LIST694  LA    R15,MSGLINE+11           WHERE TO PUT OUTPUT
         LR    R1,R6                    CURRENT DATA POINTER
         LA    R14,1(,R2)               NUMBER OF BYTES TO FORMAT
         SR    R0,R0                    CURRENT FORMATTED CHARACTER
         SPACE 2
LIST696  MVC   DOUBLE(1),0(R1)          AVOID 0C5 FOR LAST BUFFER BYTE
         UNPK  8(3,R15),DOUBLE(2)       UNPACK DATA
         TR    8(2,R15),TRTABLE         CONVERT TO HEXADECIMAL
         MVI   10(R15),X'40'            CLEAR FOLLOWING BYTE
         LA    R15,2(,R15)              INCR INTO PRINT AREA
         A     R1,=F'1'                 INCREMENT INTO DATA
         A     R0,=F'1'                 NEXT FORMATTED CHARACTER
         ST    R0,DOUBLE
         TM    DOUBLE+3,X'03'           4, 8, 12, 16, ...?
         BNZ   LIST698                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
         TM    DOUBLE+3,X'07'           8, 16, 24, 32, ...?
         BNZ   LIST698                  NO, BRANCH
         LA    R15,1(,R15)              YES, INCREMENT ONE
LIST698  BCT   R14,LIST696              GO FORMAT
         SPACE 3
LIST700  L     R2,#LUSAVE4+8            MACHINE LENGTH OF THIS SEGMENT
         L     R1,#LISTN2               CSECT
         LA    R1,1(R1,R2)                   BYTE
         ST    R1,#LISTN2                        DISPLACEMENT
         CLI   INSERT#1,X'FF'           ANY HEADER?
         BE    LIST760                  NO, BRANCH
         CLI   ##HELOFF,C'D'            DIRENTRY SUBCOMMAND?
         BE    LIST720                  YES, BRANCH
         ICM   R0,B'1111',#MAXOUT       ANY MORE OUTPUT?
         BNP   LIST910                  NO, ALL DONE
         CLI   ##HELOFF+3,C'Y'          ANY OUTPUT YET?
         BNE   LIST710                  NO, BRANCH
         LA    R15,MSGBLANK
         BAL   R14,LIST770              OUTPUT A BLANK LINE
LIST710  TM    #LISTFLG,X'01'           CSECT OUTPUT?
         BO    LIST720                  YES, BRANCH
         TM    FLAGSDD,LBLOCK+LDUMP     LBLOCK OR LDUMP?
         BM    LIST730                  YES, BRANCH
LIST720  LA    R15,MSGTEXT1             OUTPUT THE HEADER LINE
         BAL   R14,LIST770
LIST730  MVI   INSERT#1,X'FF'           TURN OFF THE HEADER
         OI    FLAGSGG,FOUTSOME         SOME OUTPUT WAS GENERATED
         L     R0,#MAXOUT               DECREMENT
         S     R0,=F'1'                          THE #MAXOUT
         ST    R0,#MAXOUT                                   COUNTER
         BM    LIST910                  NEGATIVE, ALL DONE
LIST760  TR    MSGLINE+4(256),TRLINE    UNPRINTABLES --> PERIODS
         LA    R15,MSGLINE              OUTPUT THE NEXT LINE
         BAL   R14,LIST770              OUTPUT THE NEXT LINE
         TM    ##ADRPA#,$F              FINDLIST MODIFICATION?
         BNO   *+8                      NO, BRANCH
         MVI   ##HELOFF,C'L'            YES, NOW LIST THE REST
         B     LIST160                  YES, CONTINUE
         SPACE 1
LIST770  STM   R14,R15,#LFSAVE2         SAVE REGISTERS 14 AND 15
         CLI   ##HELOFF+3,C'D'          FIND AND NO OUTPUT YET?
         BNE   LIST780                  NO, BRANCH
         SPACE 1
         LA    R1,=C'THEN'
         ICM   R2,B'1111',#ACTIONT      ANY THEN ACTION?
         BP    LIST980                  YES, BRANCH
         SPACE 1
         ICM   R0,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    NEWCMD                   YES, IGNORE THIS MEMBER
         SPACE 1
         BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         SPACE 1
LIST780  L     R1,#LFSAVE2+4            POINT TO MESSAGE
         MVI   ##HELOFF+3,C'Y'          OUTPUT WAS GENERATED
         $TMSG (R1)                     OUTPUT THE MESSAGE
         L     R14,#LFSAVE2             RESTORE REGISTER 14
         BR    R14
         SPACE 3
LIST800  TM    #LISTFLG,X'60'           CSECT DATA?
         BNOR  R14                      NO, RETURN
         TM    #LISTFLG,X'08'           NEED ADJUSTMENT?
         BNO   LIST830                  NO, BRANCH
         XI    #LISTFLG,X'08'           RESET THE ADJUSTMENT FLAG
LIST804  LH    R4,#LISTWD               DISPLAY CHARACTER WIDTH
         ICM   R2,B'1111',#LISTENT      END OF CSECT ENTRIES?
         BZ    LIST170                  YES, BRANCH
         CLI   ESDIDIN,1                THIS CSECT ACTIVE?
         BNE   LIST170                  NO, GET THE NEXT INPUT BLOCK
         SPACE 1
*  ADJUST TO BEGINNING BOUNDARY
         SR    R0,R0
         ICM   R0,B'0111',ESDADDR       CURRENT OFFSET
         A     R0,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         A     R0,#LISTMFF              ADD THE MODULE OFFSET
         CR    R0,R6                    VALID ADJUSTMENT?
         BNH   *+6                      NO, BRANCH
         LR    R6,R0                    YES, POSITION TO SYMBOL START
         LR    R1,R6
         BCTR  R1,0
         CR    R1,R5                    IN THIS DATA RECORD?
         BNL   LIST170                  NO, IGNORE THIS RECORD
         XC    #LISTOFF(4),#LISTOFF     LIST OFFSET IS SATISFIED
         MVC   #LISTID(2),ESDID         SAVE THE CURRENT ESD ID
         MVI   MTHIGHL,39
         MVC   MSGTEXT1(3),L141$1
         MVC   INSERT#1(39),MSGLDUMP    "AT 123456  CSECT 12345678 .."
         UNPK  INSERT#1(7),ESDADDR(4)
         TR    INSERT#1(6),TRTABLE      OFFSET OF CSECT OR ENTRY
         MVI   INSERT#1+6,X'40'
         MVC   INSERT#1+14(8),ESDNAME   PRIVATE, COMMON OR CSECT NAME
         UNPK  INSERT#1+31(7),ESDLEN(4)
         TR    INSERT#1+31(6),TRTABLE   LENGTH OF CSECT
         MVI   INSERT#1+31+6,X'40'
         LR    R0,R6
         S     R0,#LISTVIR              ACTUAL ADDRESS --> OFFSET
         ST    R0,#LISTN1               OFFSET IN LOAD MODULE
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR       ENTRY ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         CLI   ESDTYPE,CODESD           CSECT ENTRY?
         BE    LIST810                  YES, BRANCH
         TM    ESDTYPE,CODEPC           PRIVATE CODE OR OVERLAY?
         BO    LIST810                  YES, BRANCH
         L     R15,ESDCSECT             ADDRESS OF MAIN ENTRY
         MVC   INSERT#1+14(8),4(R15)    CSECT NAME
         MVC   INSERT#1+24(6),LISTLENT   ENTRY WITHIN A CSECT
         MVC   INSERT#1+31(8),ESDNAME
         AR    R0,R1
         ICM   R1,B'0111',ESDMAIN+1     CSECT START ADDRESS
         SR    R0,R1
         ST    R0,#LISTN2               OFFSET IN CSECT
         SPACE 2
LIST810  ICM   R0,B'0111',ESDADDR       ENTRY ADDRESS
         LR    R15,R0                   SAVE THE ENTRY ADDRESS
         A     R0,ESDLEN-1              LAST ADDRESS TO BE ACCESSED
LIST814  LTR   R2,R2                    AT END OF LIST?
         BZ    LIST820                  YES, BRANCH
         ICM   R2,B'1111',ESDLINK       ANOTHER ESD ENTRY?
         ST    R2,#LISTENT
         BZ    LIST820                  NO, BRANCH
         CLC   #LISTID(2),ESDID         ENTRY FOR THIS CSECT?
         BNE   LIST820                  NO, USE THE CURRENT ENTRY
         CLM   R15,B'0111',ESDADDR      START:NEXT START
         BNL   LIST814                    HIGH OR EQUAL, DISCARD IT
         CLM   R0,B'0111',ESDADDR       END:NEXT START
         BNH   LIST820                    LOW OR EQUAL, USE THIS LIMIT
         ICM   R0,B'0111',ESDADDR         HIGH, USE THE LOWER LIMIT
         SPACE 1
LIST820  LR    R1,R0                    CSECT OR ENTRY END
         SR    R1,R15                   CSECT OR ENTRY LENGTH
         C     R1,#MAXLEN               ABOVE MAXIMUM CSECT LENGTH?
         BL    *+8                      NO, BRANCH
         L     R1,#MAXLEN               YES, USE THE REQUESTED MAXIMUM
         AR    R1,R15                   END OF CSECT OR ENTRY
         A     R1,#LISTVIR              OFFSET --> ACTUAL ADDRESS
         ST    R1,#LISTE1               SAVE END ADDRESS
         SPACE 2
*  CHECK FOR END OF CSECT OR EXTERNAL LABEL
LIST830  LR    R1,R4                    DATA LENGTH
         LR    R15,R5
         SR    R15,R6                   MACHINE LENGTH VALID?
         BM    LIST170                  NO, IGNORE THIS RECORD
         CR    R1,R15
         BNH   *+8
         LA    R1,1(,R15)               R1=MIN(16 OR 64,LENGTH LEFT)
         AR    R1,R6                    END OF THIS LINE
         C     R1,#LISTE1               END ON THIS LINE?
         BLR   R14                      NO, RETURN
         OI    #LISTFLG,X'08'           NEW HEADER IS NEEDED
         L     R4,#LISTE1               MAXIMUM ADDRESS OF CSECT
         SR    R4,R6                    DISPLAY LENGTH VALID?
         BPR   R14                      YES, BRANCH
         LTR   R2,R2                    AT END OF LIST?
         BZ    LIST170                  YES, IGNORE THIS MEMBER
         B     LIST804                  NO, TRY THE NEXT CSECT
         SPACE 3
LIST900  MVI   ##HELOFF+2,X'EF'         END OF FILE ENCOUNTERED
LIST910  CLI   ##HELOFF+3,C'D'          FIND AND NO OUTPUT?
         BNE   LIST920                  NO, BRANCH
         LA    R1,=C'ELSE'
         ICM   R2,B'1111',#ACTIONE      ANY ELSE ACTION?
         BP    LIST980                  YES, BRANCH
         TM    FLAGSAA,FMEM#MEM         MEMBER GROUP IN PROGRESS?
         BO    NEWCMD                   YES, DONE
         TM    DSORG,DS1DSGPO           PARTITIONED?
         BNO   LIST940                  NO, BRANCH
         TM    PMEMMIN,X'80'            MEMBER LIST AND NO OUTPUT?
         BO    NEWCMD                   YES, BRANCH
         BAL   R2,LIST996               OUTPUT ANY MEMBER HEADER
         B     LIST940
         SPACE 1
LIST920  TM    FLAGSDD,LBLOCK+LDUMP+BLOCK+DUMP SEGMENT FORMAT?
         BZ    LIST940                         NO, BRANCH
         $MESAGE MSGBLANK               OUTPUT A BLANK LINE
         SPACE 1
LIST940  CLI   ##HELOFF+2,X'EF'         END OF FILE ENCOUNTERED?
         BNE   NEWCMD                   NO, DONE
         MVI   MTHIGHL,L'MSGBLK-1
         LA    R1,L142$1
         MVC   INSERT#1-1(34),MSGBLK
         L     R0,#BLKCNT
         CVD   R0,DOUBLE
         ED    INSERT#1-1(10),DOUBLE+4
         TM    #LISTFLG,X'01'           CSECT FORMATTING?
         BO    LIST960                  YES, BRANCH
         TM    FLAGSDD,DUMP+BLOCK
         BNZ   LIST960
         MVC   INSERT#1+MSGBLKL-MSGBLK-1(6),LISTLLIN
LIST960  TM    DSORG,DS1DSGPO
         BO    MSGNEW
         MVC   INSERT#1+MSGBLKS-MSGBLK-1(8),LISTLDAT
         B     MSGNEW
         SPACE 2
LIST980  MVC   DELAYMSG+25(4),0(R1)     ADD "THEN" OR "ELSE"
         TM    ##ADRPA#-##SUBCOM(R2),$D SUPPRESS DEFAULT MESSAGE?
         BO    LIST990                  YES, BRANCH
         SPACE 1
         $MESAGE MSGBLANK               OUTPUT A BLANK LINE
         TM    FLAGSAA,FDELAYM          ANY MESSAGE TO OUTPUT?
         BNO   LIST990                  NO, BRANCH
         XI    FLAGSAA,FDELAYM          NO MORE MESSAGE
         MVC   DELAYMSG+30(8),0(R2)     SUBCOMMAND TO EXECUTE
         $MESAGE DELAYMSG               IDENTIFICATION LINE
         SPACE 1
LIST990  MVC   ##ANSWER(LISUBS),ISUBS   INITIALIZE THE PDL SAVE AREA
         MVC   ##SUBCOM(PTW),0(R2)      CHANGE THE SUBCOMMAND
         MVI   MTHIGHL,8
         MVI   MTHIGHL+4,8
         OI    FLAGSGG,FOUTSOME         SOME OUTPUT WAS GENERATED
         B     CALLCMDZ                 CALL THE SECONDARY SUBCOMMAND
         SPACE 3
* OUTPUT A FIND OR REPLACE HEADER MESSAGE IF REQUIRED
LIST996  TM    DSORG,DS1DSGPO           PARTITIONED?
         BNOR  R2                       NO, NO MESSAGE
         TM    FLAGSAA,FDELAYM          ANY HEADER MESSAGE?
         BNOR  R2                       NO, NO MESSAGE
         $MESAGE MSGBLANK               OUTPUT A BLANK LINE
         $MESAGE DELAYMSG               OUTPUT THE HEADER LINE
         NI    FLAGSAA,FF-FDELAYM       TURN OFF THE HEADER LINE
         BR    R2
LISTUPDE EQU   X'80'                    #REPFLAG -- UPDATE ERROR
LISTUPDO EQU   X'40'                    #REPFLAG -- UPDATE OPEN
LISTUPDR EQU   X'20'                    #REPFLAG -- UPDATE THIS BLOCK
LIST#REP EQU   X'01'                    #REPFLAG -- WRITE FLAG
MSGBLKU  DC    C' 1,234,567 BLOCKS UPDATED'
         ORG   MSGBLKU
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
         SPACE 1
MSGDUMP  DC    C' 1,234,567   LENGTH 12,345   TTR 123456 '
         ORG   MSGDUMP
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   MSGDUMP+19
MSGDUMPL DC    X'4020206B202120'         EDIT PATTERN
MSGDUMPT EQU   MSGDUMP+33,6              TTR OUTPUT
         ORG   ,
MSGLDUMP DC    C'123456  CSECT 12345678  LENGTH 123456  '
*              C'1234567890123456789012345678901234567890
MSGBLK   DC    C' 1,234,567 BLOCKS IN THIS MEMBER  '
         ORG   MSGBLK
         DC    X'40206B2020206B202120'   EDIT PATTERN
         ORG   ,
MSGBLKL  EQU   MSGBLK+11,6               " LINES"
MSGBLKS  EQU   MSGBLK+26,7               "DATA SET"
LISTL000 DC    C'00000000'
LISTLBLO DC    C'BLOCK'
LISTLDUM DC    C'DUMP '
LISTLENT DC    C' ENTRY'
LISTLLIN DC    C' LINES'
LISTLDAT DC    C'DATA SET'
         DROP  R2
         SPACE 5
DIR000   BALR  R8,0                     OUTPUT ADDITIONAL DIR DATA
         USING *,R8
         $MESAGE MSGBLANK               OUTPUT ADDITIONAL DIR DATA
         CLC   ##SUBCOM+#LSUB(8),$DIR   IF OR THEN FORM?
         BNE   NEWCMD                   YES, NO ADDITIONAL OUTPUT
         CLI   #DIRSHOR,1               SHORT FORM?
         BE    NEWCMD                   YES, BRANCH
         MVI   MTHIGHL,70               LENGTH OF MESSAGE INSERT
         MVC   INSERT#1(100),BLANK128   CLEAR THE HEADER LINE
         MVC   INSERT#1(40),MSG262H     HEADER INFORMATION
         $TMSG L262$1
         MVC   INSERT#1(40),MSG262U     UNDERLINE INFORMATION
         SPACE 2
         SR    R2,R2                    OFFSET IN THE DIRECTORY
         LA    R3,DIRHDR                START OF HEADER NAMES
         BAL   R4,DIRFMT10              OUTPUT FIRST MESSAGE
         SPACE 1
         MVC   INSERT#1+13(8),DIRNAME   ADD THE MEMBER NAME
         BAL   R4,DIRFMT                (00)
         LA    R2,8(,R2)                POSITION PAST MEMBER NAME
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (08)
         LA    R2,3(,R2)                POSITION PAST TTR
         SPACE 1
         LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         IC    R14,DIRFLAG
         SLL   R14,25                   DROP ALIAS BIT
         SRDL  R14,30                   SAVE HALFWORD COUNT
         LA    R14,X'F0'(,R14)          CONVERT TO DISPLAY
         STC   R14,INSERT#1+30          SAVE TTR COUNT IN MESSAGE
         SPACE 1
         SRL   R15,32-5                 HALFWORD COUNT
         CVD   R15,DOUBLE
         MVC   FULLWORD(4),DIR4020      EDIT PATTERN
         ED    FULLWORD(4),DOUBLE+6
         MVC   INSERT#1+45(2),FULLWORD+2  ADD COUNT
         SPACE 1
         TM    DIRFLAG,X'80'            ALIAS?
         BO    DIR040                   YES, BRANCH
         MVC   INSERT#1+23+44(12),BLANK128
         MVC   INSERT#1+23(44),INSERT#1+30  DROP "ALIAS;"
         SPACE 1
DIR040   BAL   R4,DIRFMT                (0B)
         LA    R2,1(,R2)                POSITION PAST INDC
         SPACE 2
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   DIR800                   YES, BRANCH
         LA    R0,4                     TTR +1 BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         MVC   INSERT#1+21(1),INSERT#1+20
         MVC   INSERT#1+20(1),INSERT#1+19
         MVI   INSERT#1+19,C','
         BAL   R4,DIRFMT                (0C)
         LA    R2,4(,R2)                POSITION PAST TTRT
         SPACE 1
         TM    DIRATTR,X'24'            OVERLAY OR SCATTER?
         BZ    *+8                      NO, BRANCH
         BAL   R4,DIRFMT20              YES, GET NEXT DOC.
         TM    DIRATTR,X'20'            OVERLAY?
         BO    DIR110                   YES, BRANCH
         LA    R0,4                     TTR +1 BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         MVC   INSERT#1+21(1),INSERT#1+20
         MVC   INSERT#1+20(1),INSERT#1+19
         MVI   INSERT#1+19,C','
         BAL   R4,DIRFMT                (10)
         LA    R2,4(,R2)                POSITION PAST TTRN
         B     DIR120
         SPACE 1
DIR110   BAL   R4,DIRFMT20              GET NEXT DOC.
         LA    R0,3                     TTR
         BAL   R4,DIRFMT                (10)
         LA    R2,3(,R2)                POSITION PAST TTRN
         SPACE 1
         SR    R0,R0
         IC    R0,DIRNOTE#              NUMBER OF NOTELIST ELEMENTS
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (13)
         LA    R2,1(,R2)                POSITION PAST NL
         SPACE 1
DIR120   LA    R3,DIRHDR14              REPOSITION THE HEADER INFO.
         BAL   R4,DIRFMT20
         LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         LA    R14,X'80'*2              TEST MASK
         LA    R5,DIRBITP1-2            TEXT OFFSETS
         BAL   R4,DIRBITS
         BAL   R4,DIRFMT                (14)
         SPACE 1
         LA    R14,X'08'*2              TEST MASK
         LA    R5,DIRBITP2-2            TEXT OFFSETS
         BAL   R4,DIRBITS
         BAL   R4,DIRFMT10              (14)
         LA    R2,1(,R2)                SKIP ATR1
         SPACE 1
         LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         LA    R14,X'80'*2              TEST MASK
         LA    R5,DIRBITP3-2            TEXT OFFSETS
         BAL   R4,DIRBITS
         BAL   R4,DIRFMT                (15)
         SPACE 1
         LA    R14,X'08'*2              TEST MASK
         LA    R5,DIRBITP4-2            TEXT OFFSETS
         BAL   R4,DIRBITS
         BAL   R4,DIRFMT10              (15)
         LA    R2,1(,R2)                SKIP ATR2
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0111',DIRSTORE      3 INPUT BYTES
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (16)
         LA    R2,3(,R2)                SKIP STOR
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0011',DIRTEXTL      2 INPUT BYTES
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (19)
         LA    R2,2(,R2)                SKIP FTBL
         SPACE 1
         LA    R0,3                     3 INPUT BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (1B)
         LA    R2,3(,R2)                SKIP EPA
         SPACE 1
         TM    DIRATTR2,X'80'           VS LINKAGE EDITOR?
         BO    DIR210                   YES, BRANCH
         MVC   INSERT#1+04(08),DIRHDROS NEW OPERAND NAME
         MVC   INSERT#1+23(44),DIRHDROS+8  NEW DESCRIPTION
         LA    R0,3                     3 INPUT BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (1E)
         LA    R2,3(,R2)                SKIP FTB0, FTB1, FTB2
         B     DIR300
         SPACE 1
DIR210   LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (1E)
         LA    R2,1(,R2)                SKIP FTB0
         SPACE 1
         LA    R4,DIR220                NEXT RETURN POINT
         TM    DIRATTR2,X'20'           PAGE ALIGNED?
         BO    DIRFMT10                 YES, BRANCH
         BAL   R4,DIRFMT20              NO, GET NEXT DOC.
         SPACE 1
DIR220   LA    R4,DIR230                NEXT RETURN POINT
         TM    DIRATTR2,X'10'           SSI PRESENT?
         BO    DIRFMT10                 YES, BRANCH
         BAL   R4,DIRFMT20              NO, GET NEXT DOC.
         SPACE 1
DIR230   LA    R4,DIR240                NEXT RETURN POINT
         TM    DIRATTR2,X'08'           APF INFORMATION VALID?
         BO    DIRFMT10                 YES, BRANCH
         BAL   R4,DIRFMT20              NO, GET NEXT DOC.
         SPACE 1
DIR240   LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         TM    DIRATTR3,DIRRMANY        RMODE=ANY?
         BO    *+10                     YES, BRANCH
         MVC   INSERT#1+29(4),DIR24     NO, RMODE 24
         SPACE 1
         TM    DIRATTR3,DIRAA31+DIRAA24 ALIAS AMODE=ANY?
         BO    DIR250                   YES, BRANCH
         MVC   INSERT#1+46(4),DIR24     NO, "24; "
         TM    DIRATTR3,DIRAA31         ALIAS AMODE=31?
         BNO   DIR250                   YES, BRANCH
         MVC   INSERT#1+46(2),DIR31     NO, "31"
         SPACE 1
DIR250   TM    DIRATTR3,DIRAM31+DIRAM24 MAIN AMODE=ANY?
         BO    DIR260                   YES, BRANCH
         MVC   INSERT#1+62(4),DIR24     NO, "24; "
         MVI   INSERT#1+62+2,C' '       BLANK OUT THE ;
         TM    DIRATTR3,DIRAM31         MAIN AMODE=31?
         BNO   DIR260                   YES, BRANCH
         MVC   INSERT#1+62(2),DIR31     NO, "31"
         SPACE 1
DIR260   BAL   R4,DIRFMT                (1F)
         LA    R2,1(,R2)                SKIP FTB1
         SPACE 2
         LA    R0,1                     1 INPUT BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (20)
         LA    R2,1(,R2)                SKIP FTB2
         SPACE 1
DIR300   LA    R3,DIRHDR21              REPOSITION HEADER
         BAL   R4,DIRFMT20
         TM    DIRATTR,X'04'            SCATTER LOAD?
         BNO   DIR400                   NO, BRANCH
         SR    R0,R0
         ICM   R0,B'0011',DIRSCLL       COUNT
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (21)
         LA    R2,2(,R2)                SKIP SLSZ
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0011',DIRSCTL       COUNT
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (23)
         LA    R2,2(,R2)                SKIP TTSZ
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0011',DIRSCET       COUNT
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (25)
         LA    R2,2(,R2)                SKIP ESDT
         SPACE 1
         SR    R0,R0
         ICM   R0,B'0011',DIRSCEP       COUNT
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (27)
         LA    R2,2(,R2)                SKIP ESDC
         SPACE 1
DIR400   LA    R3,DIRHDR29              REPOSITION HEADER
         BAL   R4,DIRFMT20
         TM    DIRFLAG,DIRALIAS         ALIAS?
         BNO   DIR500                   NO, BRANCH
         LA    R0,3                     3 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (29)
         LA    R2,3(,R2)                SKIP EPM
         SPACE 1
         LA    R1,DIRNAME(R2)           START OF MAIN MEMBER NAME
         MVC   INSERT#1+13(8),0(R1)     MOVE INTO MESSAGE AREA
         BAL   R4,DIRFMT                (2C)
         LA    R2,8(,R2)                SKIP MNM
         SPACE 1
DIR500   LA    R3,DIRHDR34              REPOSITION HEADER
         BAL   R4,DIRFMT20
         TM    DIRATTR2,X'80'           VS LINKAGE EDITOR?
         BO    DIR550                   YES, BRANCH
         LA    R2,1(,R2)
         N     R2,=F'-2'                ROUND TO HALFWORD BOUNDARY
         CLC   ZERO,0(R2)               ZERO?
         BE    DIR990                   YES, NO SSI
         CLC   =F'-1',0(R2)             FFFFFFFF?
         BE    DIR990                   YES, NO SSI
         LA    R0,4                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (34)
         LA    R2,4(,R2)                SKIP SSI
         B     DIR990
         SPACE 1
DIR550   TM    DIRATTR2,DIR2SSI         ANY SSI INFORMATION?
         BNO   DIR600                   NO, BRANCH
         LA    R2,1(,R2)
         N     R2,=F'-2'                ROUND TO HALFWORD BOUNDARY
         LA    R0,4                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (34)
         LA    R2,4(,R2)                SKIP SSI
         SPACE 1
DIR600   LA    R3,DIRHDR38              REPOSITION HEADER
         BAL   R4,DIRFMT20
         TM    DIRATTR2,DIRAPFLG        VALID APF INFORMATION?
         BNO   DIR990                   NO, BRANCH
         LA    R0,1                     1 HEX BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (38)
         LA    R2,1(,R2)                SKIP APFCT
         SPACE 1                        O
         LA    R0,1                     1 HEX BYTE
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (39)
         LA    R2,1(,R2)                SKIP APFAC
         B     DIR990
         SPACE 3
DIR800   TM    DIRFLAG,X'0F'            SPF STATISTICS?
         BO    DIR900                   YES, BRANCH
         MVC   DOUBLE(1),DIRFLAG        COPY THE ATTRIBUTE BYTE
         NI    DOUBLE,X'FF'-X'80'       RESET THE ALIAS FLAG
         CLI   DOUBLE,X'02'             SSI INFORMATION?
         BL    DIR990                   NO, BRANCH
         LA    R3,DIRHDR34              REPOSITION HEADER
         BAL   R4,DIRFMT20
         LA    R0,4                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (34)
         LA    R2,4(,R2)                SKIP SSI
         B     DIR990
         SPACE 1
DIR900   LA    R3,DIRHDRSP              REPOSITION HEADER
         BAL   R4,DIRFMT20
         SPACE 1
         SR    R0,R0
         IC    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-0C)
         LA    R2,1(,R2)                SKIP SPFV
         SPACE 1
         SR    R0,R0
         IC    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-0D)
         LA    R2,1(,R2)                SKIP SPFR
         SPACE 1
         LH    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-0E)
         LA    R2,2(,R2)                SKIP SPFZ
         SPACE 1                        O
         LA    R0,4                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (SPF-10)
         LA    R2,4(,R2)                SKIP SPFCR
         SPACE 1
         LA    R0,4                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (SPF-14)
         LA    R2,4(,R2)                SKIP SPFCD
         SPACE 1
         LA    R0,2                     4 HEX BYTES
         BAL   R4,DIRHEX                CONVERT TO HEX
         BAL   R4,DIRFMT                (SPF-18)
         LA    R2,2(,R2)                SKIP SPFCT
         SPACE 1
         LH    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-1A)
         LA    R2,2(,R2)                SKIP SPFSI
         SPACE 1
         LH    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-1C)
         LA    R2,2(,R2)                SKIP SPFIN
         SPACE 1
         LH    R0,DIRNAME(R2)
         BAL   R4,DIRDEC                CONVERT TO DECIMAL
         BAL   R4,DIRFMT                (SPF-1E)
         LA    R2,2(,R2)                SKIP SPFMD
         SPACE 1
         LA    R1,DIRNAME(R2)           START OF MAIN MEMBER NAME
         MVC   INSERT#1+13(8),0(R1)     MOVE INTO MESSAGE AREA
         BAL   R4,DIRFMT                (SPF-20)
         LA    R2,8(,R2)                SKIP SPFID
         SPACE 1
DIR990   $MESAGE MSGBLANK
         B     NEWCMD
DIRDEC   C     R0,DIRDEC99
         BH    DIRDEC10
         CVD   R0,DOUBLE
         MVC   INSERT#1+100+8(8),BLANK128
         MVC   INSERT#1+100(8),DIR2020
         ED    INSERT#1+100(7),DOUBLE+5
         LA    R1,INSERT#1+100-1
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1+13(8),0(R1)
         BR    R4
DIRDEC10 LR    R1,R0
         LA    R1,1023(,R1)
         SRL   R1,10                    THE NEXT HIGHER K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#1+100+8(8),BLANK128
         MVC   INSERT#1+100(7),DIR2020
         MVI   INSERT#1+100+7,C'K'
         ED    INSERT#1+100(7),DOUBLE+5
         LA    R1,INSERT#1+100-1
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1+13(8),0(R1)
         BR    R4
DIRDEC99 DC    F'99999'
         SPACE 3
DIRHEX   LA    R1,DIRNAME(R2)           POINT TO DATA
         LA    R15,INSERT#1+13          START OF OUTPUT DATA
         SPACE 1
DIRHEX10 UNPK  0(3,R15),0(2,R1)         UNPACK TO HEXADECIMAL
         TR    0(2,R15),TRTABLE         CONVERT TO PRINTABLE
         MVI   2(R15),X'40'             BLANK GARBAGE BYTE
         LA    R15,2(,R15)              NEXT OUTPUT DIGIT
         LA    R1,1(,R1)                NEXT INPUT DIGIT
         BCT   R0,DIRHEX10              FORMAT ALL INPUT DIGITS
         BR    R4
         SPACE 3
DIRFMT   STC   R2,DOUBLE                SAVE OFFSET IN STORAGE
         UNPK  INSERT#1(3),DOUBLE(2)    UNPACK TO HEXADECIMAL
         TR    INSERT#1(2),TRTABLE      CONVERT TO PRINTABLE OFFSET
         MVI   INSERT#1+2,X'40'         CLEAR GARBAGE BYTE
         SPACE 1
DIRFMT10 TR    INSERT#1(70),TRLINE      CONVERT TO PRINTABLE CHARACTERS
         $TMSG L262$1                   OUTPUT
         SPACE 1
DIRFMT20 MVC   INSERT#1(100),BLANK128   BLANK THE OUTPUT AREA
         MVC   INSERT#1+04(08),0(R3)    ADD THE PDS... NAME
         MVC   INSERT#1+23(44),8(R3)    ADD THE DESCRIPTION
         LR    R6,R3                    THIS TEXT
         LA    R3,52(,R3)               NEXT TEXT
         BR    R4
         SPACE 3
DIRBITS  ST    R14,DOUBLE+4             SAVE TEST MASK
         LA    R15,INSERT#1+23          OUTPUT START
         MVC   0(44,R15),BLANK128
         LA    R0,4                     BITS PER LINE
         LR    R1,R6                    INPUT TEXT
         SPACE 1
DIRBIT10 LA    R5,2(,R5)                NEXT OFFSET
         LH    R6,0(,R5)
         LPR   R6,R6
         ST    R6,FULLWORD              OFFSET OF TEXT
         AR    R6,R1
         ST    R6,DOUBLE                START OF TEXT
         LH    R6,2(,R5)
         LPR   R6,R6
         S     R6,FULLWORD
         ST    R6,FULLWORD              LENGTH OF TEXT
         L     R14,DOUBLE+4             SHIFT
         SRL   R14,1                         FOR NEXT
         ST    R14,DOUBLE+4                          BIT TEST
         LA    R6,DIRNAME(R2)           POINT TO TARGET DATA
         TM    0(R5),X'80'              NEGATIVE FORM?
         BO    DIRBIT20                 YES, BRANCH
         TM    0(R6),*-*                <<EXECUTED>>
         EX    R14,*-4                  ADD "NOT "?
         BO    DIRBIT40                 NO, BRANCH
         B     DIRBIT30                 YES, BRANCH
         SPACE 1
DIRBIT20 TM    0(R6),*-*                <<EXECUTED>>
         EX    R14,*-4                  ADD "NOT "?
         BNO   DIRBIT40                 NO, BRANCH
         SPACE 1
DIRBIT30 MVC   0(4,R15),DIRNOT          ADD "NOT "
         LA    R15,4(,R15)              UPDATE LENGTH
         SPACE 1
DIRBIT40 L     R14,FULLWORD             ACTUAL LENGTH
         BCTR  R14,0                    MACHINE LENGTH
         L     R6,DOUBLE                START OF STRING
         MVC   0(*-*,R15),0(R6)         <<EXECUTED>>
         EX    R14,*-6                  MOVE IN STRING
         LA    R15,1(R14,R15)           NEXT OUTPUT LOCATION
         BCT   R0,DIRBIT10              DO ALL 4 BITS
         BR    R4
         SPACE 3
DIRBITP1 DC    0H'0',AL2(+08,+19,+25,+34,+38)
DIRBITP2 DC    0H'0',AL2(+08,+19,+28,+34,+40)
DIRBITP3 DC    0H'0',AL2(-08,+12,+24,-30,+38)
DIRBITP4 DC    0H'0',AL2(-08,+14,+20,+28,+39)
         SPACE 1
DIRNOT   DC    C'NOT '
DIR24    DC    C'24; '
DIR31    DC    C'31'
DIR2020  DC    X'4020206B2021204B'      EDIT MASK
DIR4020  DC    X'40202120'              EDIT MASK
MSG262H  DC    CL40'LOC NAME     VALUE     DESCRIPTION'
MSG262U  DC    CL40'--- ----     -----     -----------'
*IRHDR    DC CL52'1234567890123456789012345678901234567890123456789012'
DIRHDR    DC CL52'PDS2NAMEMEMBER NAME                                 '
          DC CL52'PDS2TTRPTTR OF FIRST BLOCK OF DATA                  '
          DC CL52'PDS2INDCALIAS; N TTRS FOLLOW; NN HALFWORDS OF DATA  '
          DC CL52'PDS2TTRTTTR OF FIRST TEXT BLOCK                     '
          DC CL52'PDS2TTRN(NOT USED FOR THIS MEMBER)                  '
          DC CL52'PDS2TTRNTTR OF SCATTER TRANSLATION TABLE            '
          DC CL52'PDS2TTRNTTR OF OVERLAY NOTE LIST                    '
          DC CL52'PDS2NL  NUMBER OF ENTRIES IN NOTE LIST              '
*                           1    1    2    2    3    3    4    4
*                           0    5    0    5    0    5    0    5
DIRHDR14  DC CL52'PDS2ATR1REENTRANT; REUS; OVERLAY; TEST          '
          DC CL52'        ONLY LOAD; SCATTER; EXEC; 1 TEXT            '
          DC CL52'PDS2ATR2DC; TEXT ORG=0; EP=0; HAS RLDS              '
          DC CL52'        EDIT; TEST; LKED F; REFRESHABLE             '
          DC CL52'PDS2STORTOTAL CONTIGUOUS MAIN STORAGE REQUIRED      '
          DC CL52'PDS2FTBLLENGTH OF FIRST BLOCK OF TEXT               '
          DC CL52'PDS2EPA ENTRY POINT ADDRESS                         '
          DC CL52'PDS2FTB1PROCESSED BY OS/VS LINKAGE EDITOR           '
          DC CL52'        PAGE ALIGNMENT REQUIRED                     '
          DC CL52'        SSI INFORMATION IS PRESENT                  '
          DC CL52'        APF INFORMATION IS VALID                    '
          DC CL52'PDS2FTB2RMODE ANY; ALIAS AMODE ANY; MAIN AMODE ANY  '
          DC CL52'PDS2FTB3RLD/CONTROL RECORDS AFTER FIRST TEXT BLOCK  '
DIRHDROS  DC CL52'PDS2FTB0ORIGIN OF FIRST BLOCK OF TEXT (FOR O.S. USE)'
DIRHDR21  DC CL52'PDS2SLSZNUMBER OF BYTES IN SCATTER LIST             '
          DC CL52'PDS2TTSZNUMBER OF BYTES IN TRANSLATION TABLE        '
          DC CL52'PDS2ESDTESD CSECT IDENTIFIER OF FIRST TEXT BLOCK    '
          DC CL52'PDS2ESDCESD CSECT IDENTIFIER OF ENTRY POINT BLOCK   '
DIRHDR29  DC CL52'PDS2EPM ENTRY POINT OF MAIN MEMBER                  '
          DC CL52'PDS2MNM MEMBER NAME OF MAIN MEMBER                  '
DIRHDR34  DC CL52'PDSSSIWDSSI INFORMATION                             '
DIRHDR38  DC CL52'PDSAPFCTLENGTH OF PROGRAM AUTHORIZATION CODE        '
          DC CL52'PDSAPFACPROGRAM AUTHORIZATION CODE                  '
DIRHDRSP  DC CL52'DIRSPFV MEMBER VERSION NUMBER                       '
          DC CL52'DIRSPFR MEMBER REVISION NUMBER                      '
          DC CL52'DIRSPFZ (CURRENTLY NOT USED)                        '
          DC CL52'DIRSPFCRCREATION DATE    -- FORMAT: 00YYDDDF        '
          DC CL52'DIRSPFCDLAST CHANGE DATE -- FORMAT: 00YYDDDF        '
          DC CL52'DIRSPFCTLAST CHANGE TIME -- FORMAT: HHMM            '
          DC CL52'DIRSPFSINUMBER OF LINES CURRENTLY                   '
          DC CL52'DIRSPFINNUMBER OF LINES INITIALLY                   '
          DC CL52'DIRSPFMDNUMBER OF MODIFIED LINES                    '
          DC CL52'DIRSPFIDUSERID OF LAST PERSON TO UPDATE             '
         TITLE 'P D S  --  PDS MAP                            5/12/86'
***********************************************************************
***      MAP SUBCOMMAND                                             ***
***********************************************************************
*
         SPACE 1
MAP      CSECT
         USING *,R8
         LA    R1,L530          ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         LA    R1,L701          ASSUME NOT A LOAD LIBRARY
         TM    FLAGSCC,RECFMU   CORRECT?
         BZ    MSGNEW           YES, BRANCH
         OI    FLAGSGG,FOUTSUB  SUBCOMMAND GAINED CONTROL
         MVI   ENTRYPT,C'?'     NO ENTRY POINT YET
         SPACE 1
         L     R15,=A(READESD)  FORMAT THE ESD DATA
         BALR  R14,R15          ANY ESD DATA?
         B     *+8              NO, BRANCH
         B     MAP04            YES, BRANCH
         SPACE 1
MAPNOESD TM    FLAGSAA,FDELAYM  DELAYED MESSAGE AVAILABLE?
         BNO   MAP02            NO, BRANCH
         TM    PMEMMIN,X'80'    MEMBER LIST?
         BO    *+12             YES, BRANCH
         TM    FLAGSAA,FMEM#MEM MEMBER GROUP?
         BNO   MAP02            NO, BRANCH
         CLI   #MODLEN,0        ANY FILTERING?
         BH    NEWCMD           NO, BRANCH
         $MESAGE MSGBLANK
         $MESAGE DELAYMSG
         XI    FLAGSAA,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 1
MAP02    LA    R1,MSGTEXT1               MESSAGE IDENTIFIER
         $TMSG (R1)
         B     MAPNOMSG
         SPACE 1
MAP04    LA    R4,#ESDPTR       ESD TABLE CHAIN POINTER
         OI    FLAGSGG,FOUTSOME SOME OUTPUT WAS GENERATED
         USING ESDENTRY,R4      BASE FOR TABLE
         TM    FLAGSAA,FDELAYM  ANY DELAYED MESSAGE AVAILABLE?
         BNO   MAPESD           NO, BRANCH
         $MESAGE MSGBLANK
         $MESAGE DELAYMSG
         XI    FLAGSAA,FDELAYM  MESSAGE WAS WRITTEN
         SPACE 2
MAPESD   MVC   MSGTEXT1(136),MSGBL132
         ICM   R4,B'1111',ESDLINK    END OF CHAIN?
         BZ    MAPLAST               YES, BRANCH
         SPACE 1
         CLI   #MAPOPT,X'04'         RELINK FORMAT?
         BE    MAP600                YES, BRANCH
         CLI   ESDTYPE,CODELR        EXTERNAL REFERENCE?
         BE    MAPLR                 YES, BRANCH
         CLI   ESDTYPE,CODEER        $UNRESOLVED EXTERNAL REFERENCE?
         BE    MAPER                 YES, BRANCH
         CLI   ESDTYPE,CODEWK        $UNRESOLVED WEAK EXTERNAL REF?
         BE    MAPWK                 YES, BRANCH
         SPACE 1
         CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+4(8),ESDNAME
         UNPK  MSGTEXT1+14(7),ESDADDR(4)
         TR    MSGTEXT1+14(6),TRTABLE
         MVI   MSGTEXT1+20,X'40'     CLEAR GARBAGE BYTE
         UNPK  MSGTEXT1+22(7),ESDLEN(4)
         TR    MSGTEXT1+22(6),TRTABLE
         MVI   MSGTEXT1+28,X'40'     CLEAR GARBAGE BYTE
         SPACE 1
         TM    DIRATTR,ATTROVLY      OVERLAY PROGRAM?
         BZ    MAPNOVLY              NO, BRANCH
         SR    R0,R0
         IC    R0,ESDSEG#
         CVD   R0,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+30(2),DOUBLE+6(2)
         B     MAPPRINT
         SPACE 1
MAPNOVLY CLI   ESDXAFLG,AMODE24          RMODE 24 AND AMODE 24?
         BNH   MAPPRINT                  NO, BRANCH
         MVC   MSGTEXT1+30(5),=C'RMODE'
         MVC   MSGTEXT1+36(3),=C'ANY'
         TM    ESDXAFLG,RMODEANY         RMODE ANY?
         BO    *+10                      YES, BRANCH
         MVC   MSGTEXT1+36(3),=C'24 '    RMODE 24
         MVC   MSGTEXT1+40(5),MAPLAMOD
         MVC   MSGTEXT1+46(3),=C'24 '
         TM    ESDXAFLG,AMODE31          AMODE 31?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'31 '    AMODE 31
         TM    ESDXAFLG,AMODE24+AMODE31  AMODE ANY?
         BNO   *+10                      NO, BRANCH
         MVC   MSGTEXT1+46(3),=C'ANY'    AMODE ANY
         B     MAPPRINT
         SPACE 2
MAPWK    MVC   INSERT#1(8),ESDNAME
         $TMSG L441$1
         B     MAPESD
         SPACE 2
MAPER    MVC   INSERT#1(8),ESDNAME
         $TMSG L442$1
         B     MAPESD
         SPACE 1
MAPLR    CLC   ESDADDR(3),DIREPA     ENTRY POINT?
         BNE   *+10                  NO, BRANCH
         MVC   ENTRYPT,ESDNAME       SAVE THE ENTRY POINT NAME
         MVC   MSGTEXT1+56(8),ESDNAME
         UNPK  MSGTEXT1+66(7),ESDADDR(4)
         TR    MSGTEXT1+66(6),TRTABLE
         MVI   MSGTEXT1+66+6,X'40'   CLEAR GARBAGE BYTE
         SPACE 1
MAPPRINT $MESAGE MSGTEXT1
         B     MAPESD
         SPACE 1
MAPLAST  $MESAGE MSGBLANK
MAPNOMSG UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         $TMSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         $TMSG L104$2
         SPACE 1
         TM    DIRFLAG,X'80'      IS MODULE AN ALIAS?
         BZ    NEWCMD             NO, NO ALIAS INFORMATION
         CLI   #MAPOPT,X'01'      SHORT OR ENTRY POINT MAP?
         BH    NEWCMD             YES, DONE
         TM    FLAGSGG,FALINCON   NO ALIAS INFORMATION DEFAULT?
         BO    NEWCMD             YES, DONE
         EJECT
*        FIND THE CORRESPONDING MAIN MODULE
         MVI   STARTTR+2,X'01'    TTR=000001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1  NO MAIN(S) FOUND
         SPACE 1
MAPAREAL BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAPNREAL           LAST MEMBER PROCESSED
         SPACE 1
         TM    MEMFLAG,X'80'      THIS MODULE AN ALIAS?
         BO    MAPAREAL           YES, IGNORE
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAPAREAL           NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         $TMSG L066$1
         B     MAPAREAL
         SPACE 2
MAPNREAL OC    INSERT#1(8),INSERT#1  ANY MAIN(S) FOUND?
         BNZ   NEWCMD                YES, QUIT
         $TMSG L860
         TM    FLAGSCC,RECFMU     LOAD MODULE?
         BNO   NEWCMD             NO, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         LA    R1,L861$1
         B     MSGNEW
         SPACE 1
* GENERATE JCL AND CONTROL CARDS TO LINKAGE EDIT A MODULE
MAP600   TM    DIRFLAG,X'80'                     ALIAS?
         BNO   MAP604                            NO, BRANCH
         MVI   STARTTR+2,X'01'                   READ THE DIRECTORY
         SPACE 1
MAP602   BAL   R14,READDIR                       GET NEXT MEMBER ENTRY
         B     MAP604                            END OF MEMBERS, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR                  THIS TTR?
         BNE   MAP602                            NO, LOOP
         TM    MEMTTR+3,X'80'                    ALIAS?
         BO    MAP602                            YES, LOOP
         L     R14,DIRPTRS                       START OF ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)    REPLACE THE ENTRY
         SPACE 2
MAP604   MVC   MSGTEXT1+4(L'MAPLLKED),MAPLLKED   //LKED  EXEC PGM=IEWL,
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   MSGTEXT1+4(L'MAPLPARM),MAPLPARM   //      PARM='NCAL, ..
         LA    R1,MSGTEXT1+4+L'MAPLPARM
         SPACE 1
         TM    DIRATTR,ATTRRENT                  REENTRANT?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+00               YES, ADD ,RENT
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRREUS                  REUSABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+05               YES, ADD ,REUS
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRREFR                REFRESHABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+10               YES, ADD ,REFR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+15               YES, ADD ,OVLY
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRTEST                  TEST SYMBOLS?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+20               YES, ADD ,TEST
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRSCTR                  SCATTER LOADED?
         BNO   *+14                              NO, BRANCH
         MVC   0(5,R1),MAPLATTR+25               YES, ADD ,SCTR
         LA    R1,5(,R1)
         SPACE 1
         TM    DIRATTR,ATTRLOAD                  ONLY LOADABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+30               YES, ADD ,OL
         LA    R1,3(,R1)
         SPACE 1
         TM    DIRATTR+1,ATTRNE                  NOT EDITABLE?
         BNO   *+14                              NO, BRANCH
         MVC   0(3,R1),MAPLATTR+33               YES, ADD ,NE
         LA    R1,3(,R1)
         SPACE 1
         MVI   0(R1),C''''                       ADD FINAL QUOTE
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSUT1),MAPLSUT1   //SYSUT1
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSPRT),MAPLSPRT   //SYSPRINT
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSLIB),MAPLSLIB   //SYSLIB
         MVC   MSGTEXT1+4+L'MAPLSLIB(44),DSNAME  ADD DSNAME
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSMOD),MAPLSMOD   //SYSLMOD
         MVC   MSGTEXT1+4+L'MAPLSMOD(44),DSNAME  ADD DSNAME
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLSLIN),MAPLSLIN   //SYSLIN
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         MVC   MSGTEXT1+4(L'MAPLINCL),MAPLINCL   ADD "INCLUDE SYSLIB("
         MVC   MSGTEXT1+4+L'MAPLINCL(8),DIRNAME  ADD MEMBER NAME
         LA    R1,MSGTEXT1+4+L'MAPLINCL+8
         CLI   0(R1),X'40'
         BNE   *+8
         BCT   R1,*-8
         MVI   1(R1),C')'
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
         TM    DIRATTR,ATTROVLY                  OVERLAY MODULE?
         BO    MAP700                            YES, BRANCH
         SPACE 3
         LA    R4,#ESDPTR
         SPACE 1
MAP610   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+60                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLORD           ADD " ORDER"
         SPACE 1
MAP620   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP670                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP630                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP630                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP620                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP620                            NO, BRANCH
         B     MAP640                            NO, BRANCH
         SPACE 1
MAP630   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP620                            YES, BRANCH
         SPACE 1
MAP640   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         TM    ESDADDR+1,X'0F'                   POSSIBLE 4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         TM    ESDADDR+2,X'FF'                   4K MULTIPLE?
         BNZ   MAP660                            NO, BRANCH
         MVC   1(3,R14),MAPLPAG                  YES, ADD "(P)"
         LA    R14,3(,R14)
         SPACE 1
MAP660   MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP620                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP610
         SPACE 2
MAP670   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP800                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP800
         SPACE 2
MAP700   MVC   STARTTR(3),DIRSTART               ADDRESS OF NOTELIST
         L     R15,=V(EXCP)
         BALR  R14,R15
         LA    R1,L751                           NOTELIST NOT FOUND
         LTR   R15,R15                           ANY ERROR?
         BP    MSGNEW                            YES, BRANCH
         SPACE 1
         LR    R2,R0                             START OF RECORD
         LR    R5,R0                             START OF RECORD
         LA    R14,24(,R2)                       FIRST SEGMENT
         LA    R0,4                              INCREMENT
         LR    R1,R2
         A     R1,LS                             LAST SEGMENT
         SR    R1,R0                             FOR BXLE
         SR    R15,R15
         SPACE 1
MAP710   LA    R15,1(,R15)                       NEXT SEGMENT #
         STC   R15,3(,R14)                       STORE IN SEGTAB
         BXLE  R14,R0,MAP710                     DO ALL ENTRIES
         SPACE 2
         MVI   0(R14),X'FF'                      MARK END OF TABLE
         MVC   FULLWORD,=F'1'                    REGION = 1
         LA    R2,24(,R5)                        BEGIN SEGMENT 1
         LA    R6,8(,R5)                         BEGIN REGION 1
         SPACE 1
MAP720   SR    R1,R1
         IC    R1,0(,R6)                         # OF LAST SEGMENT
         LTR   R1,R1                             END OF STRUCTURE?
         BZ    MAP800                            YES, BRANCH
         SLL   R1,2                              SEGMENT * 4
         LA    R3,20(R1,R5)                      LAST SEGMENT
         SPACE 2
MAP724   LA    R4,#ESDPTR                        START OF ESD ELEMENTS
         SPACE 1
MAP730   LA    R1,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R0,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(6),MAPLINS           ADD " INSERT"
         SPACE 1
MAP732   ICM   R4,B'1111',ESDLINK                NEXT ESD ELEMENT?
         BZ    MAP742                            NO, BRANCH
         CLC   ESDSEG#(1),3(R2)                  THIS SEGMENT?
         BNE   MAP732                            NO, BRANCH
         CLI   ESDTYPE,CODESD                    CSECT?
         BE    MAP734                            YES, BRANCH
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP734                            YES, BRANCH
         CLC   ESDNAME(8),MAPL$BLK               BLANK COMMON?
         BE    MAP732                            YES, BRANCH
         CLI   ESDTYPE,CODECM                    COMMON?
         BNE   MAP732                            NO, BRANCH
         B     MAP740
         SPACE 1
MAP734   CLC   ESDADDR(3),DIREPA                 ENTRY POINT?
         BNE   *+10                              NO, BRANCH
         MVC   ENTRYPT,ESDNAME                   YES, SAVE THE NAME
         CLI   ESDTYPE,CODELR                    EXTERNAL REFERENCE?
         BE    MAP732                            YES, BRANCH
         SPACE 1
MAP740   MVC   1(8,R1),ESDNAME                   ADD THE ESDNAME
         LA    R14,8(,R1)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R1,1(,R14)
         CR    R1,R0                             END OF CARD?
         BNH   MAP732                            NO, BRANCH
         SPACE 2
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP730
         SPACE 1
MAP742   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP750                            NO, BRANCH
         MVI   0(R1),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP750   CLI   4(R2),X'FF'                       END OF SEGTAB?
         BE    MAP800                            YES, BRANCH
         CR    R2,R3                             END OF REGION?
         BE    MAP764                            YES, BRANCH
         SPACE 1
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD " REGION"
         MVC   MSGTEXT1+4+1+7+1+6(1),FULLWORD+3  ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         SPACE 1
         SR    R1,R1
         ICM   R1,B'0001',4(R2)                  ANY PREVIOUS SEGMENT?
         BZ    MAP760                            NO, BRANCH
         MVC   MSGTEXT1+4+1+7+1(6),MAPLOVLY      ADD " OVLY"
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'
         UNPK  MSGTEXT1+4+1+7+1+4(3),DOUBLE+6(2) ADD SEGMENT NUMBER
MAP760   $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 1
MAP764   LA    R0,4
         LR    R1,R3
         BXLE  R2,R0,MAP724                      DO EACH SEGMENT
         SPACE 2
         LA    R6,2(,R6)
         CLI   0(R6),X'00'                       ANY SEGMENTS IN RGN?
         BE    MAP800                            NO, BRANCH
         L     R1,FULLWORD
         LA    R0,4
         CR    R1,R0                             FOURTH REGION DONE?
         BNL   MAP800                            YES, BRANCH
         LA    R1,1(,R1)                         NEXT REGION
         ST    R1,FULLWORD
         MVC   MSGTEXT1+4+1(7),MAPLOVER          ADD " OVERLAY"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         STC   R1,MSGTEXT1+4+1+7+1+6             ADD REGION NUMBER
         OI    MSGTEXT1+4+1+7+1+6,X'F0'          MAKE PRINTABLE
         MVC   MSGTEXT1+4+1+7+1+6+1(8),MAPLREG   ADD "(REGION)"
         MVC   MSGTEXT1+4+1+7+1(6),MAPLREG+1     ADD "REGION"
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP720
         SPACE 2
MAP800   LA    R2,DIRAPF               POINT TO APF INFORMATION
         SPACE 1
         TM    DIRATTR,ATTRSCTR        SCATTER FORMAT?
         BNO   *+8                     NO, BRANCH
         LA    R2,8(,R2)               YES, ADD SCATTER SIZE BYTES
         SPACE 1
         TM    DIRFLAG,X'80'           ALIAS?
         BO    MAP802                  YES, BRANCH
         CLI   8(R2),0                 CONVERTED ALIAS ENTRY?
         BE    *+8                     NO, BRANCH
MAP802   LA    R2,11(,R2)              ADD ALIAS LENGTH
         SPACE 1
         TM    DIRATTR2,DIRAOSLE       VS LINKAGE EDITOR?
         BNO   MAP804                  NO, BRANCH
         SPACE 1
         TM    DIRATTR2,DIR2SSI        SSI PRESENT?
         BNO   MAP820                  NO, SKIP SSI PROCESSING
         SPACE 1
MAP804   LA    R2,1(,R2)               ROUND UP TO HALFWORD
         N     R2,=F'-2'
         SPACE 1
         CLC   ZERO,0(R2)              ZERO?
         BE    MAP820                  YES, NO SSI
         CLC   =F'-1',0(R2)            FFFFFFFF?
         BE    MAP820                  YES, NO SSI
         SPACE 1
         MVC   MSGTEXT1+5(6),MAPLSET   ADD " SETSSI"
         UNPK  MSGTEXT1+5+6+2(9),0(5,R2)
         TR    MSGTEXT1+5+6+2(8),TRTABLE
         MVI   MSGTEXT1+5+6+2+8,X'40'
         LA    R2,4(,R2)               ADD SSI SIZE
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP820   TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BZ    MAP860                      NO, CANNOT BE AUTHORIZED
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BNO   *+8                         NO, BRANCH
         OI    MODESAVE,DIRRMANY           YES, SET RMODE AGAIN
         SPACE 1
         TM    MODESAVE,DIRRMANY+DIRAM31   UNUSUAL RMODE OR AMODE?
         BZ    MAP840                      NO, BRANCH
         MVC   DOUBLE(8),BLANKS            RMODE/AMODE FILLER
         MVC   DOUBLE(3),=C'24 '           MOVE IN THE RMODE TEXT
         TM    MODESAVE,DIRRMANY           RMODE ANY?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE(3),=C'ANY'           MOVE IN THE RMODE TEXT
         MVC   DOUBLE+4(3),=C'24 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM31            AMODE 31?
         BZ    *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'31 '         MOVE IN THE AMODE TEXT
         TM    MODESAVE,DIRAM24+DIRAM31    AMODE ANY?
         BNO   *+10                        NO, BRANCH
         MVC   DOUBLE+4(3),=C'ANY'         MOVE IN THE AMODE TEXT
         MVC   MSGTEXT1+4(L'MAPLMODE),MAPLMODE
         LA    R1,MSGTEXT1+4+L'MAPLMODE
         MVC   0(3,R1),DOUBLE              ADD 24/ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVC   2(L'MAPLAMO,R1),MAPLAMO     ADD "),AMODE("
         LA    R1,2+L'MAPLAMO(,R1)
         MVC   0(3,R1),DOUBLE+4            ADD 24/31 OR ANY
         CLI   2(R1),X'40'                 ANY DATA?
         BE    *+8                         NO, BRANCH
         LA    R1,1(,R1)                   YES, ADD 1
         MVI   2(R1),C')'                  ADD FINAL PARENTHESIS
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP840   TM    DIRATTR2,DIRAPFLG       APF DATA PRESENT AND VALID?
         BNO   MAP860                  NO, BRANCH
         SPACE 1
         LA    R1,L721                 ASSUME APF LENGTH INCORRECT
         CLI   0(R2),1                 APF DATA LENGTH OK?
         BNE   MAP860                  NO, BRANCH
         SPACE 3
         LA    R1,L021                 ASSUME AUTHORIZED
         CLI   1(R2),1                 AUTHORIZED?
         BL    MAP860                  NO, BRANCH
         MVC   MSGTEXT1+4(L'MAPLAUTH),MAPLAUTH
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
MAP860   MVC   MSGTEXT1+4(L'MAPLENTR),MAPLENTR
         CLI   ENTRYPT,C'?'           ANY ENTRY POINT?
         BE    *+10                   NO, BRANCH
         MVC   MSGTEXT1+4+L'MAPLENTR-8(8),ENTRYPT
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         SPACE 2
         MVC   ENTRYPT(8),DIRNAME      DEFAULT MODULE NAME
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 2
MAP880   LA    R2,MSGTEXT1+4+8                   START OF CSECT NAMES
         LA    R3,MSGTEXT1+4+62                  END OF CSECT NAMES
         MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+4+1(5),MAPLALIA          ADD " ALIAS"
         SPACE 1
MAP890   BAL   R14,READDIR        GET NEXT DIRECTORY MEMBER
         B     MAP910             LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR      TTR MATCH?
         BNE   MAP890             NO, BRANCH
         TM    MEMFLAG,X'80'      ALIAS ENTRY FOUND?
         BO    MAP900             YES, BRANCH
         TM    DIRFLAG,X'80'      ALIAS ORIGINALLY?
         BNO   MAP890             NO, BRANCH
         MVC   ENTRYPT(8),MEMNAME SAVE REAL MEMBER NAME
         B     MAP890
         SPACE 1
MAP900   MVC   1(8,R2),MEMNAME                   ADD THE ALIAS NAME
         LA    R14,8(,R2)
         SPACE 1
         CLI   0(R14),X'40'                      BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R14,*-8
         SPACE 1
         MVI   1(R14),C','                       ADD A COMMA
         LA    R2,1(,R14)
         CR    R2,R3                             END OF CARD?
         BNH   MAP890                            NO, BRANCH
         SPACE 2
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         MVC   MSGTEXT1(136),MSGBL132
         B     MAP880
         SPACE 2
MAP910   CLI   MSGTEXT1+4+9,X'40'                ANY TEXT TO OUTPUT?
         BE    MAP920                            NO, BRANCH
         MVI   0(R2),X'40'                       CLEAR THE LAST COMMA
         $MESAGE MSGTEXT1
         SPACE 1
MAP920   MVC   MSGTEXT1(136),MSGBL132
         MVC   MSGTEXT1+5(L'MAPLNAME),MAPLNAME
         MVC   MSGTEXT1+5+8(8),ENTRYPT
         LA    R1,MSGTEXT1+5+8+8
         SPACE 1
         CLI   0(R1),X'40'                       BLANK?
         BNE   *+8                               NO, BRANCH
         BCT   R1,*-8
         SPACE 1
         MVC   1(3,R1),MAPLREP
         SPACE 1
         $MESAGE MSGTEXT1
         B     NEWCMD
MAPLLKED DC    C'//LKED   EXEC  PGM=IEWL,'
MAPLPARM DC    C'//       PARM=''NCAL,MAP,LIST,LET'
MAPLATTR DC    C',RENT,REUS,REFR,OVLY,TEST,SCTR,OL,NE'
MAPLSUT1 DC    C'//SYSUT1   DD  UNIT=SYSDA,SPACE=(1024,(200,20))'
MAPLSPRT DC    C'//SYSPRINT DD  SYSOUT=*'
MAPLSLIB DC    C'//SYSLIB   DD  DISP=SHR,DSN='
MAPLSMOD DC    C'//SYSLMOD  DD  DISP=SHR,DSN='
MAPLSLIN DC    C'//SYSLIN   DD  *'
MAPLINCL DC    C' INCLUDE SYSLIB('
MAPLAUTH DC    C' SETCODE AC(1)'
MAPLENTR DC    C' ENTRY   ????????'
MAPLMODE DC    C' MODE    RMODE('
MAPLAMO  DC    C'),AMODE('
MAPLNAME DC    C'NAME'
MAPLSET  DC    C'SETSSI'
MAPLALIA DC    C'ALIAS'
MAPLORD  DC    C'ORDER'
MAPLPAG  DC    C'(P)'
MAPLREP  DC    C'(R)'
MAPLREG  DC    C'(REGION)'
MAPLOVER DC    C'OVERLAY'
MAPLOVLY DC    C'OVLY'
MAPLINS  DC    C'INSERT'
         DROP  R4
         SPACE 3
*
*        ESD SCAN SUBROUTINE
*
         SPACE 2
READESD  STM   R14,R12,ESDIDRSV   SAVE REGISTERS FOR RETURN
         LR    R8,R15             ENTRY POINT ADDRESS
         USING READESD,R8         SUBROUTINE BASE REGISTER
         LA    R2,#ESDPTR         ROOT OF ESD CHAIN
         MVC   STARTTR(3),DIRTTR  FIRST TTR
         SPACE 1
ESDEXCP  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)           PROCESS RETURN CODE
         B     ESDEXCP1             00 - GOOD READ
         B     ESDLAST              04 - END OF MEMBER
         B     ESDLAST              08 - END OF DATA SET
         B     NEWCMD               12 - I/O ERROR
         SPACE 1
ESDEXCP1 LR    R3,R0              START OF RECORD
         CLI   0(R3),X'40'        TEST SYM RECORD?
         BE    ESDEXCP            YES, SKIP RECORD
         CLI   0(R3),X'20'        CESD RECORD?
         BNE   ESDLAST            NO, BRANCH
         SPACE 1
         LH    R6,4(,R3)          RELATIVE # OF 1ST ESD ID
         LH    R5,6(,R3)          LENGTH OF DATA IN BUFFER
         LA    R3,8(,R3)          START OF ESD DATA
         AR    R5,R3
         LA    R4,16              LENGTH OF ONE ENTRY
         SR    R5,R4
         SPACE 2
         USING ESDNAME,R3
ESDSCAN  IC    R0,ESDTYPE
         LA    R1,CODESEG         CHECK FOR SEGTAB/ENTAB
         TM    DIRATTR,ATTROVLY   OVERLAY MODULE?
         BO    *+8                YES, BRANCH
         NI    ESDXAFLG,RMODEANY+AMODE24+AMODE31
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   #MAPOPT,X'03'      ENTRY POINT ONLY?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ATTL  ATTRIBUTE ENTRY(NAME) OPERAND?
         BE    ESDSCAN2           YES, BRANCH
         CLC   ##SUBCOM(8),$ALI   ALIAS SEARCH REQUEST?
         BNE   ESDMAP             NO, BRANCH
ESDSCAN2 CLI   ESDTYPE,CODESD     VALID EXTERNAL SYMBOL?
         BE    ESDCHK             YES, BRANCH
         CLI   ESDTYPE,CODELR     ANOTHER VALID ENTRY
         BNE   NEXTESD            NO, GET THE NEXT ESD
         SPACE 1
ESDCHK   TM    DIRATTR,ATTROVLY   OVERLAY ATTRIBUTE?
         BNO   ESDCHK1            NO, BRANCH
         CLI   ESDSEG#,1          SYMBOL IN ROOT SEGMENT?
         BNE   NEXTESD            NO, IGNORE
ESDCHK1  CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT ADDRESS?
         BNE   ESDCHK2            NO, BRANCH
         MVC   ENTRYPT,ESDNAME    YES, ENTRY POINT NAME IF NO ESD MATCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    ESDUSE3            YES, BRANCH
ESDCHK2  CLC   ESDNAME,DIRNAME    THIS NAME?
         BNE   NEXTESD            NO, BRANCH
         CLI   #MAPOPT,X'03'      ENTRY POINT MAP ONLY?
         BE    NEXTESD            YES, BRANCH
         IC    R0,ESDXAFLG        GET MVS/XA ESD FLAGS
         SR    R1,R1
         ICM   R1,B'0111',ESDADDR GET SYMBOL OFFSET
         ST    R6,ESDIDRSV+4      RETURN ESDID IN REG 15
         STM   R0,R1,ESDIDRSV+4+4 RETURN ESD FLAGS, OFFSET IN REGS 0,1
         MVC   ENTRYPT,ESDNAME    USE THIS ESDNAME FOR THE ENTRY POINT
         B     ESDOUT             FOUND THE SYMBOL
         SPACE 1
ESDMAP   CLC   ##SUBCOM(8),$IFX   IF SUBCOMMAND?
         BNE   ESDMAP2            NO, BRANCH
         TM    #MODOPT,X'01'      WKEXTERN?
         BNO   ESDMAP0            NO, BRANCH
         CLI   ESDTYPE,CODEWK     FOUND?
         BE    ESDUSE2            YES, BRANCH
         B     NEXTESD            NO, BRANCH
         SPACE 1
ESDMAP0  TM    #MODOPT,X'02'      EXTERN?
         BNO   ESDMAP2            NO, BRANCH
         CLI   ESDTYPE,CODEER     FOUND?
         BE    ESDUSE2            YES, BRANCH
         B     NEXTESD            NO, BRANCH
         SPACE 1
ESDMAP2  CR    R0,R1              THIS SEGTAB/ENTAB ENTRY?
         BE    ESDUSE1            YES, BRANCH
         CLI   ESDTYPE,CODESD     SD ENTRY?
         BE    ESDUSE2            YES, BRANCH
         CLI   ESDTYPE,CODECM     COMMON BLOCK CODE?
         BE    ESDUSE0            YES, BRANCH
         CLC   ##SUBCOM(8),$MAP   MAP SUBCOMMAND?
         BNE   ESDMAP4            NO, BRANCH
         CLI   ESDTYPE,CODEER     $UNRESOLVED EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BE    ESDMAP4            YES, BRANCH
         CLI   ESDTYPE,CODEWK     $UNRESOLVED WEAK EXTERNAL REFERENCE?
         BE    ESDUSE2            YES, BRANCH
         SPACE 1
ESDMAP4  CLI   ESDTYPE,CODELR     LR ENTRY?
         BNE   ESDMAPPC           NO, BRANCH
         CLI   #MAPOPT,X'02'      SHORT MAP?
         BL    ESDUSE2            NO, BRANCH
         CLC   ESDADDR(3),DIREPA  THIS ENTRY POINT?
         BE    ESDUSE2            NO, BRANCH
ESDMAPPC CLI   ESDTYPE,CODEPC     PC ENTRY?
         BNE   NEXTESD            NO, BRANCH
         MVC   ESDNAME,MAPL$PRI        PRIVATE CODE
         B     ESDUSE2
         SPACE 1
ESDUSE0  CLC   ESDNAME(8),BLANKS                   BLANK COMMON?
         BNE   ESDUSE2                             NO, BRANCH
         MVC   ESDNAME(8),MAPL$BLK                 YES, USE $BLKCOM
         B     ESDUSE2
         SPACE 1
ESDUSE1  STC   R0,ESDTYPE                          RESTORE OVERLAY TYPE
         MVC   ESDNAME(8),MAPL$SEG                 ASSUME SEGMENT TABLE
         OC    ESDADDR(3),ESDADDR                  CORRECT?
         BZ    ESDUSE2                             YES, BRANCH
         MVC   ESDNAME(8),MAPL$ENT                 NO, ENTRY TABLE
ESDUSE2  SR    R1,R1
         CLC   ##SUBCOM(8),$LIS                    LIST SUBCOMMAND?
         BE    ESDUSE3                             YES, BRANCH
         ICM   R1,B'0001',#MODLEN                  ANY MODULE FILTER?
         BZ    ESDUSE3                             NO, BRANCH
         BCTR  R1,0                                MACHINE LENGTH
         CLC   ESDNAME(*-*),#MODTXT                <<EXECUTED>>
         EX    R1,*-6                              THIS PARTIAL NAME?
         BNE   NEXTESD                             NO, BRANCH
ESDUSE3  BAL   R14,GETESD
         ST    R1,ESDLINK-ESDENTRY(R2)             SAVE LINK POINTER
         STH   R6,ESDID-ESDENTRY(R1)               SAVE ESD ID
         MVC   ESDNAME-ESDENTRY(LENESD1,R1),0(R3)  SAVE ESD ENTRY
         LR    R2,R1                               CHAIN TO NEXT ONE
NEXTESD  LA    R6,1(R6)
         BXLE  R3,R4,ESDSCAN
         B     ESDEXCP            GET NEXT ESD RECORD
         DROP  R3
         SPACE 2
         USING ESDENTRY,R2
ESDLAST  MVC   MSGTEXT1(3),L740
         L     R14,ESDIDRSV
         LA    R2,#ESDPTR
         OC    #ESDPTR,#ESDPTR    ANY ESD DATA AVAILABLE?
         BNZ   ESDSORT            YES, BRANCH
         CLI   #MODLEN,0          ANY ESD FILTERING?
         BH    ESDEXIT            YES, BRANCH
         MVC   MSGTEXT1(3),L703
         B     ESDEXIT
         SPACE 1
ESDSORT  L     R2,ESDLINK
         ICM   R1,B'1111',ESDLINK END OF ESD ENTRIES?
         BZ    ESDOUT             YES, BRANCH
         SPACE 1
ESDSORT2 TM    DIRATTR,ATTROVLY             OVERLAY ATTRIBUTE?
         BNO   ESDNOSEG
         CLC   ESDSEG#,ESDSEG#-ESDENTRY(R1) CHECK ENTRY SEGMENT
         BL    ESDSORT3
         BH    ESDSWAP
         SPACE 1
ESDNOSEG CLC   ESDADDR,ESDADDR-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDTYPE,ESDTYPE-ESDENTRY(R1)
         BL    ESDSORT3
         BH    ESDSWAP
         CLC   ESDNAME,ESDNAME-ESDENTRY(R1)
         BNH   ESDSORT3
         SPACE 1
ESDSWAP  XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         XC    ESDNAME-ESDENTRY(LENESD2,R1),ESDNAME
         XC    ESDNAME(LENESD2),ESDNAME-ESDENTRY(R1)
         SPACE 1
ESDSORT3 ICM   R1,B'1111',ESDLINK-ESDENTRY(R1)
         BNZ   ESDSORT2
         B     ESDSORT
         DROP  R2
         SPACE 2
GETESD   MVI   SUBPOOLT,21
         LA    R0,LENESD
         ICM   R0,B'1000',SUBPOOLT   SUBPOOL 21
         GETMAIN R,LV=(0)
         XC    0(LENESD,R1),0(R1)
         BR    R14
         SPACE 1
ESDOUT   L     R14,ESDIDRSV
         LA    R14,4(,R14)         ADJUST EXIT ADDRESS
ESDEXIT  LM    R15,R12,ESDIDRSV+4
         BR    R14                 EXIT
MAPLAMOD DC    C'AMODE'
MAPL$BLK DC    CL8'$BLKCOM'
MAPL$PRI DC    CL8'$PRIVATE'
MAPL$SEG DC    CL8'$SEGTAB'
MAPL$ENT DC    CL8'$ENTAB'
         TITLE 'P D S  --  PDS OUTCOPY                        5/12/86'
***********************************************************************
***      OUT SUBCOMMAND        ADDED BY BRUCE LELAND -- JULY, 1983  ***
***********************************************************************
*
         SPACE 1
OUTCOPY  CSECT
         USING *,R8
         LA    R1,L530                       ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO                CORRECT?
         BZ    MSGNEW                        YES, BRANCH
         TM    #OUT,1                        CLOSE FORCED?
         BO    OUT84                         YES, BRANCH
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT ALREADY OPEN?
         BO    OUT10                         YES, BRANCH
         AIF ('&CISP' EQ 'NO SPF').NSPF250
         TM    FLAGSEE,FBKGRND               BACKGROUND MODE?
         BO    OUT04                         YES, BRANCH
         TM    FLAGSFF,FSPFOPT6+FSPFERR+FSPFCALL+FSPFDIAL  CHANGE SPF?
         BNZ   OUT04                                       NO, BRANCH
         BAL   R2,SPFRECUR                   INVOKE PDS AS A DIALOG
         SPACE 1
.NSPF250 ANOP
OUT04    MVC   QAMDCB(LQSAMDCB),QSAMDCB      CONSTRUCT A DCB
         MVI   OPENLIST,X'80'                END OF LIST
         OPEN  (QAMDCB,(OUTPUT)),MF=(E,OPENLIST)
         MVC   INSERT#1(8),OTEXTOUT          OUTCOPY
         LA    R1,L780$1                     ASSUME OPEN ERROR
         TM    DCBOFLGS-IHADCB+QAMDCB,X'10'  OUTPUT OPEN?
         BNO   MSGNEWXX                      NO, ERROR
         CLI   #OUT,1                        CORRECT DCB ATTRIBUTES?
         BNE   OUT84                         NO, BRANCH
         SPACE 2
OUT10    MVI   #OUT,X'40'
         LA    R15,FIRST4K
         LA    R15,SAVEOUT-FIRST4K(,R15)     SAVE TEXT AREA
         CLI   #OUTOPTN,1                    ANY TEXT?
         BNE   *+10                          NO, BRANCH
         MVC   0(40,R15),MSGLINE             YES, SAVE THE DEFAULT TEXT
         CLI   #OUTOPTN,2                    NOTEXT?
         BNE   *+8                           NO, BRANCH
         MVI   0(R15),0                      YES, RESET DEFAULT TEXT
         MVC   MSGLINE(40),0(R15)            SET THE TEXT OPERAND
         B     OUT14
         SPACE 1
OUT12    CLI   #OUTOPTN,1                    ANY TEXT?
         BE    OUT14                         YES, BRANCH
         MVI   MSGLINE,0                     NO, RESET THE TEXT
         SPACE 1
OUT14    TM    FLAGSEE,FOUTASS               ALIASES ALSO DESIRED?
         BNO   *+8                           NO, BRANCH
         MVI   STARTTR+2,X'01'               YES, SET THE START TTR
         L     R2,RECOVER                    PREVIOUS RECOVERY ADDRESS
         LA    R1,OUT80                      RESUME ADDRESS
         ST    R1,RECOVER                    IN-LINE RECOVERY
         STM   R2,R8,#OUTREGS                SAVE REGISTERS
         TM    FLAGSFF,FCHANGE               CHANGED DATA SET?
         BNZ   OUT50                         NO, BRANCH
         OI    FLAGSFF,FCHANGE               YES, GOT A COPY STATEMENT
         TM    FLAGSFF,FIEBUPDT              IEBUPDTE FORMAT?
         BO    OUT50                         YES, BRANCH
         MVC   #OUT,OUTCOPYS                 " COPY OUTPUT=OUTDD,IN..."
         SR    R1,R1                         MACHINE LENGTH
         LA    R3,DSNAME-2
         AH    R3,DSNLEN
         LA    R0,8                          MAX LOOPS +1
         CH    R0,DSNLEN                     MIN (8, NAME LENGTH)
         BH    *+8
         LH    R0,DSNLEN
OUT30    S     R0,=F'1'                      ONE CHARACTER?
         BZ    OUT40                         YES, BRANCH
         CLI   0(R3),C'.'                    PERIOD?
         BE    OUT40                         YES, BRANCH
         LA    R1,1(,R1)                     MACHINE LENGTH
         BCT   R3,OUT30                      BACK UP ONE BYTE
         SPACE 1
OUT40    MVC   #OUT+33(*-*),1(R3)            <<EXECUTED>>
         EX    R1,*-6                        MOVE LOW-LEVEL QUALIFIER
         PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         LA    R3,OUTMEMBS                   START OF DUPLICATE LIST
         TM    FLAGSFF,FNOECHO               OUTPUT TO THE TERMINAL?
         BNZ   OUT42                         NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         $MESAGE MSGTEXT1
         SPACE 1
OUT42    ICM   R3,B'0111',1(R3)              NEXT DUPLICATE STRUCTURE
         BZ    OUT50                         END, BRANCH
         MVI   6(R3),0                       CLEAR THE
         MVI   7(R3),1                                CURRENT COUNT
         B     OUT42                         LOOP FOR ALL STRUCTURES
         SPACE 1
OUT50    LA    R4,OUTMEMBS                   ROOT OF DUP STRUCTURES
OUT54    LR    R3,R4
         ICM   R4,B'0111',1(R4)              NEXT LIST ENTRY?
         BNZ   OUT60                         YES, BRANCH
         LA    R5,256                        256 -1 MEMBERS
         LR    R0,R5
         SLL   R0,3                          ELEMENT WIDTH IS 8
         MVI   OUTMEMBS,22                   SUBPOOL IS 22
         ICM   R0,B'1000',OUTMEMBS           GET THE SUBPOOL
         GETMAIN R,LV=(0)
         LR    R4,R1
         STCM  R4,B'0111',1(R3)              CHAIN TO LAST STUCTURE
         XC    0(8,R4),0(R4)                 NULL LINK POINTERS
         STH   R5,4(,R4)                     MAXIMUM ELEMENTS/STRUCTURE
         MVI   7(R4),1                       ONE ITEM CURRENTLY
         B     OUT70
         SPACE 2
OUT60    LH    R5,6(R4)                      NUMBER OF ELEMENTS
         LR    R1,R4
OUT64    S     R5,=F'1'                      ANY MORE ITEMS?
         BZ    OUT70                         NO, BRANCH
         LA    R1,8(,R1)                     NEXT ELEMENT
         CLC   0(8,R1),DIRNAME               THIS ELEMENT?
         BNE   OUT64                         NO, BRANCH
         B     OUT74                         YES, IGNORE DUPLICATE
         SPACE 1
OUT70    CLC   6(2,R4),4(R4)                 CURRENT:MAXIMUM
         BE    OUT54                         EQUAL - GET NEXT STRUCTURE
         MVC   8(8,R1),DIRNAME               ADD THIS ITEM
         LH    R5,6(R4)                      ADD
         LA    R5,1(,R5)                        1 TO
         STH   R5,6(R4)                             NUMBER OF ELEMENTS
         MVC   #OUT,OUTSEL                   "          S M=12345678  "
         MVC   #OUT+14(8),DIRNAME            MEMBER NAME
         LA    R1,#OUT+14                    POINT TO EQUALS
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         CLI   MSGLINE,0                     ANY TEXT?
         BE    OUT71                         NO, BRANCH
         MVC   #OUT+24(40),MSGLINE           YES, ADD THE TEXT
         B     OUT73
         SPACE 1
OUT71    TM    FLAGSFF,FIEBUPDT              IEBUPDTE FORMAT?
         BNO   OUT72                         NO, BRANCH
         MVC   #OUT(13),OUTSELUP             ADD "./  ADD  NAME"
         TM    DIRFLAG,X'0F'                 SPF STATISTICS PRESENT?
         BO    OUT73                         YES, BRANCH
         TM    FLAGSCC,RECFMU                LOAD MODULE?
         BO    OUT73                         YES, BRANCH
         SPACE 1
         CLC   ZERO,DIRUSER                  ZERO?
         BE    OUT73                         YES, NO SSI
         CLC   =F'-1',DIRUSER                FFFFFFFF?
         BE    OUT73                         YES, NO SSI
         SPACE 1
         MVC   0(5,R1),OUTSSI                ADD ,SSI=
         UNPK  5(9,R1),DIRUSER(5)
         TR    5(8,R1),TRTABLE
         MVI   5+8(R1),X'40'
         B     OUT73
         SPACE 1
OUT72    MVI   1(R1),C','                    ADD BLANK AND COMMA
         MVC   2(8,R1),DIRNAME               MEMBER NAME AGAIN
         SPACE 1
         LA    R1,1(,R1)                     FIND
         CLI   0(R1),X'40'                       NEXT
         BNE   *-8                                   BLANK
         SPACE 1
         MVI   0(R1),C')'                    ADD ONE PARENTHESIS
         MVI   1(R1),C')'                    ADD SECOND PARENTHESIS
OUT73    PUT   QAMDCB,#OUT                   OUTPUT THIS LINE
         TM    FLAGSFF,FNOECHO               OUTPUT TO THE TERMINAL?
         BNZ   OUT74                         NO, BRANCH
         MVC   MSGTEXT1+4(80),#OUT           OUTPUT LINE
         LA    R0,84
         SLL   R0,16
         ST    R0,MSGTEXT1
         $MESAGE MSGTEXT1
         SPACE 1
OUT74    TM    FLAGSEE,FOUTASS               ASSOCIATE MEMBERS DESIRED?
         BNO   OUT76                         NO, BRANCH
         BAL   R14,READDIR                   GET NEXT DIRECTORY MEMBER
         B     OUT76                         LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR                 TTR MATCH?
         BNE   OUT74                         NO, BRANCH
         MVC   DIRNAME(8),MEMNAME            SUBSTITUTE THE MEMBER NAME
         B     OUT50                         ADD ALIAS TO THE OUTPUT
         SPACE 2
OUT76    ST    R2,RECOVER                    RESET THE RECOVERY ADDRESS
         B     NEWCMD                        TERMINATE
         SPACE 3
OUT80    LM    R2,R8,#OUTREGS                RESET REGISTERS
         ST    R2,RECOVER                    RESTORE RECOVERY ADDRESS
         SPACE 1
OUT84    MVI   OPENLIST,X'80'                END OF LIST
         CLOSE (QAMDCB),MF=(E,OPENLIST)      CLOSE OUT DATA SET
         NI    FLAGSFF,FF-FCHANGE            NEED A COPY STATEMENT
         LA    R1,L781$1                     OUT DCB IS NOT FB, 80
         MVC   INSERT#1(8),OTEXTOUT          OUTCOPY
         CLI   #OUT,0                        ERROR CLOSE?
         BE    MSGNEWXX                      YES, OUTPUT A MESSAGE
         LA    R1,L080                       DATA SET IS NOW CLOSED
         B     MSGNEWXX                      NOW CLOSED
         SPACE 4
         USING IHADCB,R1
OUT90    CLC   DCBDSORG(2),ZERO              ANY DSORG?
         BNE   *+8                           YES, BRANCH
         OI    DCBDSORG,DS1DSGPS             NO, USE DSORG=PS
         TM    DCBDSORG,DS1DSGPS             DSORG=PS?
         BNOR  R14                           NO, ERROR
         CLI   DCBRECFM,0                    ANY RECFM?
         BNE   *+8                           YES, BRANCH
         MVI   DCBRECFM,DCBRECF+DCBRECBR     NO, USE RECFM=FB
         TM    DCBRECFM,DCBRECF              RECFM=F.?
         BNOR  R14                           NO, ERROR
         LA    R0,80                         DEFAULT LRECL
         CLC   DCBLRECL(2),ZERO              ANY LRECL?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBLRECL                   NO, USE LRECL=80
         CH    R0,DCBLRECL                   LRECL=80?
         BNER  R14                           NO, ERROR
         LA    R0,3120                       DEFAULT BLKSIZE
         CLC   DCBBLKSI(2),ZERO              ANY BLKSIZE?
         BNE   *+8                           YES, BRANCH
         STH   R0,DCBBLKSI                   NO, USE BLKSIZE=3120
         MVI   #OUT,1                        ALL ATTRIBUTES ARE CORRECT
         BR    R14
         DROP  R1
         SPACE 2
         PRINT NOGEN
QSAMDCB  DCB   DSORG=PS,DDNAME=PDSOUT,MACRF=(PM),EXLST=OUT99
LQSAMDCB EQU   *-QSAMDCB
         PRINT GEN
         SPACE 2
OUT99    DC    0F'0',X'85',AL3(OUT90)        OPEN EXIT ONLY
OUTCOPYS DC    CL80'          COPY OUTDD=OUTPUT,INDD=1'
OUTSEL   DC    CL80'          S M=12345678            '
OUTSELUP DC    CL13'./  ADD  NAME'
OUTSSI   DC    C',SSI='
OTEXTOUT DC    CL8'OUTCOPY'
         TITLE 'P D S  --  PDS PRINTOFF AND ITS ALIASES       5/12/86'
***********************************************************************
***    PRINTOFF SUBCOMMAND (AND ITS ALIASES)                        ***
***********************************************************************
*
         SPACE 1
*   SUPPORTED SUBCOMMANDS: DSPRINT, PRINTOFF, REVIEW, SUBMIT,
*                          TSOLIST, VPSPRINT, &XS1, &XS2, &XS3
PRINTOFF CSECT
         USING *,R8
         TM    #MEMFLAG,X'40'           SUBMIT?
         BNO   PRINT2                   NO, BRANCH
         MVC   MSGLINE(40),WORKTBL      RESET THE OPERAND
         LR    R6,R4                    SAVE R4
         L     R5,#MEMPTR               START OF MEMBER LIST
         SH    R5,=H'16'                FOR FIRST ADD
         STM   R5,R6,#OUTREGS
         SPACE 1
PRINT1   LM    R5,R6,#OUTREGS
         LA    R5,16(,R5)               NEXT MEMBER
         CR    R5,R6                    ANY MORE?
         BNL   NEWSTAX                  NO, BRANCH
         STM   R5,R6,#OUTREGS
         MVC   DIRNAME(12),0(R5)        MEMBER NAME
         SPACE 1
PRINT2   MVC   MSGTEXT1+4(8),##SUBCAL   PROCESSOR TO ATTACH
         MVI   MSGTEXT1+12,X'40'        ADD A BLANK
         MVI   MSGTEXT1+13,C''''        ADD A QUOTE
         MVC   MSGTEXT1+14(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R4,14(,R15)              LENGTH OF "PRINTOFF'"
         LA    R3,MSGTEXT1+13(R15)      POINT TO CURRENT BYTE -1
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    PRINT6                   YES, BRANCH
         SPACE 1
         MVI   1(R3),C'('               MEMBER NAME PARENTHESIS
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R4,2+8(,R4)              LENGTH OF MEMBER +1
         LA    R2,8                     MAXIMUM MACHINE LENGTH+1
PRINT3   BCTR  R3,0                     SCAN
         BCTR  R4,0                         BACKWARDS
         CLI   0(R3),X'40'                           FOR
         BNE   *+8                                       FIRST
         BCT   R2,PRINT3                                      NON-BLANK
         SPACE 1
         BCTR  R2,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R2,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
PRINT6   MVI   1(R3),C''''              ADD A QUOTE
         MVI   2(R3),X'40'              ADD A BLANK
         LA    R4,3(,R4)                ACCOUNT FOR ")' "
         LA    R2,MSGLINE               START OF ANY ADDED TEXT
         SR    R2,R4                    WHERE TO START MESSAGE
         LR    R14,R4                   CURRENT MESSAGE LENGTH
         BCTR  R14,0                    MESSAGE MACHINE LENGTH
         MVC   0(*-*,R2),MSGTEXT1       <<EXECUTED>>
         EX    R14,*-6                  MOVE IN MESSAGE TEXT
         SPACE 1
PRINT9   AH    R4,#COMMDSZ              ADD REMAINING LENGTH
         SLL   R4,16                    CLEAR BOTTOM TWO BYTES
         STCM  R4,B'1111',0(R2)         SAVE STRING TOTAL LENGTH
         SPACE 1
         LA    R3,##SUBCAL              PROCESSOR TO ATTACH
*        $MESAGE (R2)                   DELETE * TO OUTPUT COMMAND
         ST    R2,ADDRTEXT              COMMAND ADDRESS
         ST    R2,ADDRCBUF              COMMAND ADDRESS
         CLI   ##SUBCAL,C'%'            IMPLIED CLIST?
         BNE   PRINT10                  NO, BRANCH
         L     R8,=A(EXEC)              YES, SET UP ENTRY ADDRESS
         BR    R8
         SPACE 1
PRINT10  MVI   3(R2),9                  POINT TO OPERAND OFFSET
         SPACE 1
         LA    R0,=CL8'CONTROL'           FIRST PARAMETER
         LA    R1,=CL8'DISPLAY'           SECOND PARAMETER
         LA    R2,=CL8'SM'                THIRD PARAMETER
         LA    R3,=F'0'                   FOURTH PARAMETER
         STM   R0,R3,MSGTEXT2             SAVE ADDRESSES
         OI    MSGTEXT2+12,X'80'          LAST
         LA    R1,MSGTEXT2                    PARAMETER
         L     R15,ISPLINK
         TM    SPFLAG0,SPFDON             ISPMODE ACTIVE?
         BNO   *+6                        NO, BRANCH
         BALR  R14,R15                    YES, LET SPF KNOW
         SPACE 1
         L     R14,ADDRECT
         MVC   ECTPCMD-ECT(8,R14),##SUBCAL  PROCESSOR COMMAND NAME
         LA    R1,ADDRTEXT              ADDRESS OF THE CPPL
         LA    R3,##SUBCAL              PROCESSOR TO ATTACH
         BAL   R2,ATTACH                CLEAR THE ATTENTION ECB
         TM    #MEMFLAG,X'40'           SUBMIT?
         BO    PRINT1                   YES, BRANCH
         B     NEWSTAX                  DO THE NEXT COMMAND
         TITLE 'P D S  --  PDS RENAME                         5/12/86'
***********************************************************************
***      RENAME SUBCOMMAND                                          ***
***********************************************************************
*
         SPACE 1
RENAME   CSECT
         USING *,R8
         CLI   #RENRANG,1              RENAME GROUP?
         BE    RENAME30                YES, BRANCH
         SPACE 1
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BNO   RENAME3                    NO, BRANCH
         MVC   SPFSAVM2,MEMBER2           GET MEMBER NAME      SS AUG85
         B     RENAME6                    NO, BRANCH
         SPACE 1
RENAME3  MVC   MEMBERD+1(2),LMEMBER2   CHANGE DEFAULT MEMBER LENGTH
         MVC   MEMBERD+1+2(8),MEMBER2  CHANGE DEFAULT MEMBER NAME
         MVI   MEMBERD,FMEMBER1        ONLY ONE MEMBER NAME NOW
         NI    PMEMMIN,FF-X'80'        NO MEMBER LIST NOW
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         CLI   #RENREP,0               ANY REPBY NAME?
         BE    RENAME6                 NO, BRANCH
         MVC   MEMBERD+1(2),LMEMBER1   YES, SAVE NEW DEFAULT NAME
         MVC   MEMBERD+1+2(8),MEMBER1  YES, SAVE NEW DEFAULT NAME
         BAL   R14,DEFGROUP            ADD DEFAULT GROUP
         SPACE 1
RENAME6  MVC   DSNMEMQ(8),MEMBER1      MEMBER NAME TO TEST
         BAL   R2,ENQMTEST             MEMBER IN USE?
         B     NEWCMD                  YES, ERROR
         SPACE 1
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUES
         B     NEWCMD                  COULD NOT OPEN -- ERROR
         LA    R6,NEWCMD               ASSUME NOT A SWAP REQUEST
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BNO   RENAME20                NO, BRANCH
         SPACE 1
*** RENAME WITH SWAP OPERAND *** *** *** *** *** *** *** *** *** *** **
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME10                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 2
RENAME10 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     NEWCMD
         SPACE 3
*** RENAME USING THE REPBY PARAMETER *** *** *** *** *** *** *** *** **
RENAME20 CLI   #RENREP,0               ANY REPBY OPERAND?
         BE    RENAME26                NO, BRANCH
         SPACE 1
         MVC   DIRNAME(8),#RENREP      MEMBER NAME FOR AN ERROR MESSAGE
         BLDL  INDCB,BLDLLIST          CHECK FOR MEMBER PRESENCE
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME22                  00 - FOUND THE MEMBER
         B     NOMEMBER                  04 - MEMBER WAS NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
RENAME22 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME24                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 1
RENAME24 BAL   R6,RENAME90             UPDATE ANY ALIAS ENTRIES
         MVC   MEMBER2(8),MEMBER1      SWAP MEMBER NAMES
         MVC   MEMBER1(8),#RENREP      SWAP MEMBER NAMES
         LA    R6,NEWCMD               EXIT ADDRESS
         SPACE 3
*** RENAME WITH NO OPERANDS  *** *** *** *** *** *** *** *** *** *** **
RENAME26 MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         B     NOMEMBER                  08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP OPERAND ** *** *** *** *** *** *** *** *** *** **
RENAME30 NI    FLAGSBB,FF-FLINESET     NO OUTPUT IN PROGRESS YET
         MVI   SUBPOOLT,21             TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'         READ THE DIRECTORY
         LA    R5,256/2                256 MEMBERS INITIALLY
         SPACE 1
RENAME40 SLL   R5,1                    MEMBERS*2 FOR EACH LOOP
         SR    R2,R2
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                    MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)              ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1                   NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR      ANY PREVIOUS MEMBERS?
         BZ    RENAME42                NO, BRANCH
         SRL   R3,1                    YES, USE SECOND HALF OF TABLE
         LR    R14,R4                  NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3                   MEMBERS*8 IS OLD TABLE SIZE
         LR    R2,R15                  DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0                  PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R2                   LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
RENAME42 ST    R4,#MEMPTR              MEMBER TABLE BASE
         AR    R4,R2                   WHERE TO ADD MEMBERS
         SPACE 3
RENAME44 BAL   R14,READDIR             GET THE NEXT MEMBER
         B     RENAME52                END OF MEMBERS, BRANCH
         SPACE 1
         LH    R1,LMEMBER1             MEMBER NAME LENGTH
         CLC   MEMNAME(*-*),MEMBER1    <<EXECUTED>>
         EX    R1,*-6                  MEMBER:GROUP
         BL    RENAME44                  LOW, LOOP
         BH    RENAME52                  HIGH, DONE WITH FIRST PHASE
         MVC   DSNMEMQ(8),MEMNAME        EQUAL, CHECK FOR ENQUEUED
         BAL   R2,ENQMTEST
         NOP   0                       OUTPUT AN ERROR MESSAGE ONLY
         SPACE 2
         LA    R2,MEMNAME+7
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BO    RENAME46                YES, BRANCH
         OI    FLAGSBB,FLINESET        LINE NOW IN PROGRESS
         LA    R1,80                   ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON          ISPMODE ACTIVE?
         BO    RENAME45                YES, BRANCH
         TM    CONTOPTN,1              ANY LOG RECORDING?
         BO    RENAME45                YES, BRANCH
         GTSIZE ,                      TERMINAL SIZE
         CH    R1,=H'120'              120 OR LESS BYTES?
         BL    *+8                     YES, BRANCH
         LH    R1,=H'120'              NO, USE 120 BYTES
RENAME45 SH    R1,=H'7'                LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE             CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRENAM),MSGRENAM
         LA    R6,MSGLINE+4+L'MSGRENAM
         MVC   FULLWORD(3),L161$1
         SPACE 1
RENAME46 CLI   0(R2),X'40'             SCAN FOR
         BNE   *+8                             LAST
         BCT   R2,RENAME46                         NON-BLANK
         SPACE 1
         LA    R0,MEMNAME
         SR    R2,R0
         BNM   *+6
         SR    R2,R2
         LA    R15,2(R6,R2)
         LA    R1,MSGLINE
         SR    R15,R1
         C     R15,LINESIZE
         BNH   RENAME50
         LA    R1,MSGLINE+4
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R6,MSGLINE+3            INDENT FOR BLANKS
         SPACE 1
RENAME50 MVI   0(R6),X'40'
         MVC   1(8,R6),MEMNAME
         TR    1(8,R6),TRLINE
         LA    R1,2(R2,R6)
         MVI   0(R1),C','
         LA    R6,3(R2,R6)
         MVC   0(16,R4),MEMNAME        CURRENT MEMBER NAME
         LA    R4,16(,R4)              NEXT MEMBER NAME
         S     R3,=F'1'                ANY HOLD POSITIONS LEFT?
         BP    RENAME44                YES, BRANCH
         B     RENAME40                NO, REALLOCATE
         SPACE 2
RENAME52 LA    R1,L401                 ASSUME NO MEMBERS FOUND
         TM    FLAGSBB,FLINESET        LINE IN PROGRESS?
         BZ    MSGNEW                  NO, EXIT
         LA    R1,MSGLINE+5
         SR    R6,R1
         STC   R6,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         $MESAGE MSGBLANK
         SPACE 1
         LA    R1,PDS391A              VERIFY THAT YOU WISH TO RENAME..
         BAL   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, BRANCH
         SPACE 2
         BAL   R2,OPENSTOW             OPEN STOW DCB; ENQUEUE
         B     NEWCMD                  OPEN FAILED -- ERROR
         SPACE 3
         L     R3,#MEMPTR              FIRST MEMBER POINTER
         LA    R6,RENAME60             WHERE TO GO
         MVI   #RENDEFT,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
         MVC   #RENDEFT+1(2+8+8+2),LMEMBER1  LENGTH1, MEMBER1, M2, L2
         SPACE 1
RENAME60 MVC   MEMBERD(1+2+8+8+2),#RENDEFT    FLAG, LEN1, MEM1, M2, L2
         MVC   MEMBERD+1(2),#RENDEFT+1+2+8+8  REPEAT LEN2 FOR LEN1
         MVC   MEMBERD+1+2(8),#RENDEFT+1+2+8  REPEAT MEM2 FOR MEM1
         NI    PMEMMIN,FF-X'80'               NO MEMBER LIST NOW
         CR    R3,R4                   DONE?
         BE    NEWCMD                  YES, BRANCH
         MVC   DIRNAME(16),0(R3)       CURRENT MEMBER NAME
         LA    R3,16(,R3)              NEXT MEMBER NAME
         MVC   MEMBER1(8),DIRNAME
         MVC   MEMBER2(8),DIRNAME
         LH    R1,LMEMBER2
         MVC   MEMBER2(*-*),#RENDEFT+1+2+8  <<EXECUTED>>
         EX    R1,*-6                       MOVE IN FIRST FEW BYTES
         SPACE 1
         CLC   MEMBER1(8),MEMBER2      UPDATE ALIASES ONLY?
         BE    RENAME90                YES, BRANCH
         SPACE 2
         TM    #RENSWAP,X'01'          SWAP MEMBER NAMES?
         BO    RENAME70                YES, BRANCH
         SPACE 1
         MVC   DIRNAME(8),MEMBER2      MEMBER NAME FOR AN ERROR MESSAGE
         STOW  STOWDCB,MEMBER1,C       ISSUE STOW WITH CHANGE OPTION
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME90                  00 - SUCCESSFUL
         B     MEMEXIST                  04 - THIS NAME ALREADY EXISTS
         EX    0,*                       08 - MEMBER NAME NOT FOUND
         EX    0,*                       12 - INVALID RETURN CODE
         B     IOERROR                   16 - DIRECTORY I/O ERROR
         SPACE 3
*** RENAME WITH GROUP AND SWAP OPERAND   *** *** *** *** *** *** *** **
RENAME70 XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         SPACE 1
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         LTR   R15,R15                 ANY PROBLEM?
         BNZ   IOERROR                 YES, ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         MVC   MSGTEXT1(DIREND-DIRNAME),DIRNAME  SAVE THE FIRST MEMBER
         XC    DIRUSER,DIRUSER         CLEAR THE USER FIELDS
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     RENAME80                  00 - SUCCESSFUL
         B     NOMEMBER                  04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 3
RENAME80 MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2 BLDL ADDS THINGS
         SPACE 1
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   DIRNAME,MEMBER1         FIRST MEMBER NAME
         STOW  STOWDCB,DIRNAME,D       ISSUE STOW WITH DELETE OPTION
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         SPACE 1
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         MVC   MSGTEXT2(DIREND-DIRNAME),DIRNAME  SAVE THE SECOND MEMBER
         SPACE 2
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT1  FIRST MEMBER
         MVC   DIRNAME,MEMBER2         SECOND MEMBER NAME
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A       ADD THIS MEMBER ENTRY
         SPACE 1
         LTR   R15,R15                 SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         MVC   DIRNAME(DIREND-DIRNAME),MSGTEXT2  SECOND MEMBER
         MVC   MEMBER1(8),MEMBER2      SWAP THE
         MVC   MEMBER2(8),DIRNAME              MEMBER NAMES
         BAL   R6,RENAME90             UPDATE ANY ALIAS MEMBERS
         B     RENAME60                PROCESS OTHER MEMBERS IN GROUP
         SPACE 2
** UPDATE ANY ALIAS ENTRIES FOR LOAD MODULES
RENAME90 MVC   INSERT#1(8),MEMBER1
         $TMSG L090$1
         SPACE 1
         TM    FLAGSCC,RECFMU          LOAD MODULE?
         BNOR  R6                      NO, BRANCH
         TM    DIRFLAG,DIRALIAS        AN ALIAS?
         BOR   R6                      YES, BRANCH
         SPACE 1
         MVI   STARTTR+2,X'01'         TTR=000001 (START OF DIRECTORY)
         SPACE 1
RENAME95 BAL   R14,READDIR             GET NEXT DIRECTORY MEMBER
         B     0(R6)                   LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR,MEMTTR           TTR'S MATCH?
         BNE   RENAME95                NO, BRANCH
         TM    MEMFLAG,X'80'           ALIAS ENTRY?
         BNO   RENAME95                NO, CONTINUE SEARCHING
         SPACE 1
         L     R14,DIRPTRS             CURRENT DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         SPACE 1
         LA    R14,DIRREAL             CORRESPONDING MAIN ENTRY NAME
         TM    DIRATTR,ATTRSCTR        SCATTER LOADED?
         BNO   *+8                     NO, BRANCH
         LA    R14,DIRREALS            YES, USE THIS NAME
         CLC   0(8,R14),MEMBER2        ALIAS NAME POINTER=MEMBER NAME?
         BE    RENAME95                YES, BRANCH
         SPACE 1
         MVC   0(8,R14),MEMBER2        CHANGE THE MEMBER POINTER
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         SPACE 1
         LR    R5,R15                  SAVE RETURN CODE
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         $TMSG L065$1
         LTR   R15,R5                  SUCCESSFUL?
         BNZ   IOERROR                 NO, I/O ERROR
         B     RENAME95                CONTINUE FOR ALL ALIASES
MSGRENAM DC    C'MEMBERS TO BE RENAMED ARE:'
         TITLE 'P D S  --  PDS REPRO                          5/12/86'
***********************************************************************
***      REPRO SUBCOMMAND      ADDED BY BRUCE LELAND -- SEPT, 1985  ***
***********************************************************************
*
         SPACE 1
REPRO    CSECT
         USING *,R8
         SPACE 1
         LA    R1,L530               ASSUME A NON-PARTITIONED DATA SET
         TM    DSORG,DS1DSGPO        CORRECT?
         BZ    MSGNEW                YES, BRANCH
         SPACE 2
         MVC   #REPRMAX+2(2),BLKSI   BUFFER SIZE
         CLC   #REPRBLK(4),ZERO      ANY MAXBLK(SIZE)?
         BE    REPR040               NO, BRANCH
         LH    R1,LRECL              MINIMUM RECORD LENGTH
         TM    FLAGSCC,RECFMV        RECFM=V?
         BNO   *+8                   NO, BRANCH
         LA    R1,4(,R1)             YES, ADD 4 FOR COUNT
         C     R1,#REPRBLK           LRECL:MAXBLK
         BNH   REPR020                 LOW OR EQUAL, BRANCH
         ST    R1,#REPRBLK             USE THE LRECL VALUE
         $TMSG L443                    MAXBLK HAS BEEN RAISED TO LRECL
         SPACE 1
REPR020  CLC   #REPRMAX(4),#REPRBLK  BLKSIZE:MAXBLK
         BNL   REPR030                 LOW OR EQUAL, BRANCH
         $TMSG L444                    MAXBLK EXEEDS BLKSIZE
REPR030  MVC   #REPRMAX(4),#REPRBLK  USE MAXBLK(SIZE)
         SPACE 1
REPR040  MVI   SUBPOOLT,21           SUBPOOL NUMBER
         L     R0,#REPRMAX           BUFFER SIZE
         ICM   R0,B'1000',SUBPOOLT   ADD SUBPOOL NUMBER
         GETMAIN R,LV=(0)            GET TEMPORARY STORAGE
         ST    R1,#REPRBUF           POINT TO BUFFER WORK AREA
         SPACE 1
         OI    FLAGSII,FDIRGRP       SIMULATE MEMBER GROUP INPUT
         LA    R3,#MEMPTR            CURRENT MEMBER POINTER
         MVC   MEMNAME(12),DIRNAME   ENSURE MEMBER NAME IS PRESENT
         MVC   #DELSAVE(1),FLAGSAA   SAVE FLAGSAA
         TM    FLAGSAA,FMEM#MEM      MEMBER GROUP?
         BO    REPR090               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    REPR050               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    REPR090               YES, BRANCH
REPR050  OI    #COPALI,#COPONE       ONLY A SINGLE MEMBER
         SPACE 1
REPR090  NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUPS NOW
         B     REPR300               PROCESS
         SPACE 3
REPR100  TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    REPR120               YES, BRANCH
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    REPR120               YES, BRANCH
         TM    #DELSAVE,FF-FMEM#MEM  MEMBER ALREADY ADDED?
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         BNO   REPR300               NO, BRANCH
         L     R1,PMEMCURR           CURRENT LIST ELEMENT
         TM    0(R1),X'80'           ANOTHER ELEMENT?
         BNO   REPR405               NO, END OF LIST
         MVC   FLAGSAA(1),4(R1)      NEXT DESCRIPTOR FLAGS
         MVC   LMEMBER1+1(19),5(R1)  LEN1, MEMBER1, LEN2, MEMBER2
         L     R1,0(,R1)             NEXT ELEMENT POINTER
         ST    R1,PMEMCURR
         MVC   #DELSAVE(1),FLAGSAA   SAVE THE MEMBER DESCRIPTOR
         MVC   MEMNAME(8),MEMBER1    FIRST MEMBER NAME
         NI    FLAGSAA,FF-FMEM#MEM   TURN OFF MEMBER GROUP
         TM    #DELSAVE,FMEM#MEM     MEMBER GROUP?
         BO    REPR119               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  MARK MEMBER AS ADDED
         MVC   DIRNAME,MEMBER1         FIRST PART OF DSNAME2
         SPACE 2
         BLDL  INDCB,BLDLLIST          LOCATE DIRECTORY ENTRY
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR115                   00 - SUCCESSFUL
         B     REPR110                   04 - MEMBER NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
REPR110  MVC   INSERT#1(8),DIRNAME
         $TMSG L853$1                MSG - (MEMBER) NOT FOUND
         B     REPR100
         SPACE 1
REPR115  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  DELETE ADDED BYTES
         MVC   MEMNAME(12),DIRNAME   FIRST MEMBER NAME
         B     REPR300
         SPACE 1
REPR119  MVI   STARTTR+2,X'01'       REREAD THE DIRECTORY
         SPACE 1
REPR120  BAL   R14,READDIR           GET NEXT DIRECTORY BLOCK
         B     REPR400               END OF MEMBERS -- BRANCH
         SPACE 1
         L     R14,DIRPTRS           START OF DIRECTORY ENTRY
         MVC   DIRNAME(DIREND-DIRNAME),0(R14)
         TM    #COPALI,#COPASSP      ASSOCIATES PHASE?
         BO    REPR200               YES, BRANCH
         SPACE 1
         TM    FLAGSAA,FMEMRANG      MEMBER NAME RANGE?
         BNO   REPR140               NO, BRANCH
         TM    FLAGSAA,FMEMBER1      START ENTRY SPECIFIED?
         BNO   REPR130               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER1          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER1(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               ABOVE THIS VALUE?
         BH    REPR100               YES, BRANCH
         XI    FLAGSAA,FMEMBER1      FIRST NAME IS SATISFIED
         SPACE 2
REPR130  TM    FLAGSAA,FMEMBER2      END ENTRY SPECIFIED?
         BNO   REPR300               NO, BRANCH
         SPACE 1
         LH    R15,LMEMBER2          MACHINE LENGTH OF THE MEMBER NAME
         CLC   MEMBER2(*-*),MEMNAME  <<EXECUTED>>
         EX    R15,*-6               BELOW THIS VALUE?
         BL    REPR400               YES, BRANCH
         B     REPR300
         SPACE 3
REPR140  LH    R15,LMEMBER1          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER1    <<EXECUTED>>
REPR150  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    REPR160               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,REPR150            MAYBE, CHECK ALL OTHER POSITIONS
         B     REPR100               NO, IGNORE THIS ENTRY
         SPACE 2
REPR160  TM    FLAGSAA,FMEMBER2      A SECOND PATTERN TOO?
         BNO   REPR300               NO, USE THIS ENTRY
         LH    R15,LMEMBER2          MACHINE LENGTH OF THIS PATTERN
         LA    R1,8                  MAXIMUM PATTERN LENGTH
         SR    R1,R15                COMPARE LENGTH
         LA    R14,MEMNAME           POSITION IN MEMBER NAME
         CLC   0(*-*,R14),MEMBER2    <<EXECUTED>>
REPR170  EX    R15,*-6               IN THIS MEMBER NAME?
         BE    REPR300               YES, BRANCH
         LA    R14,1(,R14)
         BCT   R1,REPR170            MAYBE, CHECK ALL OTHER POSITIONS
         B     REPR100               NO, IGNORE THIS ENTRY
         SPACE 3
REPR200  LA    R1,#MEMPTR            START OF MEMBER LIST
REPR210  ICM   R1,B'0111',1(R1)      LAST TABLE ENTRY?
         BZ    REPR100               YES, IGNORE THIS ENTRY
         CLC   12(3,R1),DIRTTR       MATCHING TTR?
         BNE   REPR210               NO, BRANCH
         SPACE 3
REPR300  LA    R1,#MEMPTR            START OF MEMBER LIST
REPR310  ICM   R1,B'0111',1(R1)      LAST TABLE ENTRY?
         BZ    REPR340               YES, ADD THIS ENTRY
         CLC   4(8,R1),DIRNAME       FIND MYSELF?
         BE    REPR100               YES, IGNORE THIS ENTRY
         B     REPR310               NO, CONTINUE SEARCHING
         SPACE 3
REPR340  SR    R15,R15
         ICM   R15,B'0001',#COPTO#   ANY RENAME?
         BZ    REPR380               NO, BRANCH
         BCTR  R15,0                 MACHINE LENGTH OF COMPARE
         LA    R1,#MEMPTR            START OF MEMBER LIST
         TM    #COPALI,#COPONE       SINGLE MEMBER?
         BNO   REPR350               NO, BRANCH
         TM    #COPALI,#COPASS2      ALIAS KEYWORD?
         BO    REPR350               YES, BRANCH
         MVI   #COPTO#,8             SET LENGTH TO 8
         LA    R15,7                 SET LENGTH TO 8
         SPACE 1
REPR350  ICM   R1,B'0111',1(R1)      ANY MORE MEMBERS?
         BZ    REPR380               NO, BRANCH
         MVC   DOUBLE(8),DIRNAME     FIRST MEMBER NAME
         XC    DOUBLE(8),4(R1)       VERSUS SECOND MEMBER NAME
         XC    DOUBLE(*-*),DOUBLE    <<EXECUTED>>
         EX    R15,*-6               CLEAR THE FRONT FEW CHARACTERS
         OC    DOUBLE(8),DOUBLE      EXACT MATCH AFTER FIRST PART?
         BNZ   REPR350               NO, LOOP
         SPACE 1
         MVC   INSERT#1(8),4(R1)     MEMBER NAME
         MVC   INSERT#1(*-*),#COPTON <<EXECUTED>>
         EX    R15,*-6               MOVE THE FIRST FEW CHARACTERS
         $TMSG L885$1                ERROR MESSAGE
         OI    #COPALI,#COPDUP       MARK FOR LATER
         SPACE 2
REPR380  MVI   SUBPOOLT,21           TEMPORARY SUBPOOL
         LA    R0,DIREND-DIRLINK     POINTER AND DIRECTORY ENTRY
         ICM   R0,B'1000',SUBPOOLT   ADD SUBPOOL NUMBER
         GETMAIN R,LV=(0)
         XC    0(DIREND-DIRLINK,R1),0(R1)
         ST    R1,0(,R3)             CHAIN TO PREVIOUS ENTRY
         LR    R3,R1                 SAVE CURRENT POINTER
         MVC   4(DIREND-DIRNAME,R3),DIRNAME
         MVC   INSERT#1(8),DIRNAME   WHO TO MOVE
         MVC   INSERT#2(8),=CL8'MOVED'
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#         ANY RENAME OPERAND?
         BZ    REPR390                    NO, BRANCH
         BCTR  R1,0                       MACHINE LENGTH
         MVC   INSERT#2(8),=CL8'COPIED'   "COPIED  "
         MVC   INSERT#2+7(3),=C'TO '      "TO "
         MVC   INSERT#2+7+3(8),DIRNAME    MEMBER NAME
         MVC   INSERT#2+7+3(*-*),#COPTON  <<EXECUTED>>
         EX    R1,*-6                     MOVE IN PARTIAL NAME
         TR    INSERT#2+7+3(8),TRLINE     MAKE PRINTABLE
         MVI   MTHIGHL+4,18
REPR390  $TMSG L050$2
         MVI   MTHIGHL+4,8
         TM    #COPALI,#COPONE       A SINGLE MEMBER?
         BNO   REPR100               NO, BRANCH
         SPACE 3
REPR400  TM    #COPALI,#COPASSP      ASSOCIATED MEMBERS?
         BO    REPR405               YES, BRANCH
         MVI   #DELSAVE,FF-FMEM#MEM  ELEMENT HAS NOW BEEN ADDED
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    REPR405               YES, BRANCH
         TM    PMEMMIN,X'80'         MEMBER LIST?
         BO    REPR100               YES, BRANCH
         SPACE 1
REPR405  TM    #COPALI,#COPASSP      ASSOCIATES PHASE STARTED?
         BO    REPR500               YES, BRANCH
         TM    #COPALI,#COPASS2      ASSOCIATE MEMBERS TOO?
         BNO   REPR500               NO, BRANCH
         OI    #COPALI,#COPASSP      YES, ASSOCIATES PHASE STARTED
         NI    #COPALI,FF-#COPONE    WANT ALL ASSOCIATED MEMBERS
         NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
         MVI   STARTTR+2,X'01'       TTR=000001 (START OF DIRECTORY)
         B     REPR100               DO ANY ASSOCIATED MEMBERS
         SPACE 3
REPR500  NI    FLAGSII,FF-FDIRGRP    NORMAL INPUT NOW
REPR600  ST    R4,#MEMLAST           SAVE LAST MEMBER POINTER
         TM    #COPALI,#COPDUP       ANY COLLISIONS?
         BO    NEWCMD                YES, BRANCH
         SPACE 4
         LA    R3,#MEMPTR              BASE FOR THE MEMBER LIST
         USING DIRLINK,R3
REPR740  LR    R4,R3                   SAVE LAST MEMBER ENTRY
         ICM   R3,B'0111',DIRLINK+1    LAST TABLE ENTRY?
         BZ    REPR770                 YES, BRANCH
         SR    R1,R1
         ICM   R1,B'0001',#COPTO#      ANY RENAME OPERAND?
         BZ    REPR760                 NO, BRANCH
         BCTR  R1,0                    ACTUAL STRING COUNT
         MVC   DIRNAME(*-*),#COPTON    <<EXECUTED>>
         EX    R1,*-6                  MOVE IN PARTIAL NAME
         SPACE 1
         CLI   #COPREP,1               REPLACE?
         BE    REPR760                 YES, BRANCH
         SPACE 1
         LA    R2,BLDLLIST-WORKAREA(R7)  IN WORKING STORAGE ADDRESS
         MVC   4(8,R2),DIRNAME         MOVE FOR BLDL
         BLDL  INDCB,0(R2)
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR750                   00 - ALREADY THERE
         B     REPR760                   04 - NOT FOUND
         B     IOERROR                   08 - I/O ERROR
         SPACE 1
REPR750  MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         $TMSG L852$1                  MEMBER IS ALREADY PRESENT
         OI    #MEMPTR,X'80'           MARK FOR LATER
         SPACE 1
REPR760  MVC   DSNMEMQ(8),DIRNAME      MEMBER NAME
         BAL   R2,ENQMTEST             MEMBER IN USE?
         OI    #MEMPTR,X'40'           YES, MARK FOR LATER
         B     REPR740                 REPEAT FOR ALL MEMBERS
         SPACE 2
REPR770  $MESAGE MSGBLANK              ONE SPACING LINE
         TM    #MEMPTR,X'80'           ANY FOUND ALREADY?
         BO    NEWCMD                  YES, BRANCH
         LA    R1,PDS396A              REPRO PROMPT MESSAGE
         BAL   R2,YESNO                PROMPT FOR YES OR NO
         B     NEWCMD                  NO, QUIT
         SPACE 2
         USING DIRLINK,R3
         LA    R3,#MEMPTR
         XC    #FIXTTRO,#FIXTTRO       CLEAR HIGHEST TTR
         SPACE 1
REPR780  ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    REPR782                 END OF LIST, BRANCH
         CLC   #FIXTTRO(3),DIRTTR      SAVE:CURRENT
         BH    REPR780                   HIGHER, BRANCH
         MVC   #FIXTTRO(3),DIRTTR      SAVE NEW HIGHEST TTR
         B     REPR780                 LOOP FOR ALL ENTRIES
         SPACE 1
         DROP  R3
REPR782  CLC   #FIXTTRO(3),DS1LSTAR    BEYOND END OF DATA MARKER?
         BL    REPR790                 NO, BRANCH
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     FIRST DISK ADDRESS TO READ
         L     R15,=V(EXCP)
         BALR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      EXCP CALL IS COMPLETED
         SPACE 2
REPR790  SR    R1,R1
         ICM   R1,B'0001',#COPTO#      ANY RENAME OPERAND?
         BZ    REPR794                 NO, BRANCH
         TM    SPFLAG2,SPFLNCD       LINE COMMAND?
         BO    REPR794               YES, BRANCH
         BCTR  R1,0                    MACHINE LENGTH
         USING DIRLINK,R3
         LR    R3,R4                   LAST MEMBER NAME
         SPACE 1
         STCM  R1,B'0011',MEMBERD+1    LENGTH
         MVC   MEMBERD+1+2(8),DIRNAME  MEMBER NAME
         MVI   MEMBERD,FMEMBER1        SINGLE MEMBER
         TM    #COPALI,#COPONE         SINGLE MEMBER?
         BO    REPR792                 YES, BRANCH
         STCM  R1,B'0011',MEMBERD+1+2+8+8
         MVC   MEMBERD+1+2+8(8),DIRNAME  MEMBER NAME
         MVI   MEMBERD,FMEMBER1+FMEMBER2+FMEMRANG+FMEM#MEM
REPR792  NI    PMEMMIN,FF-X'80'        TURN OFF MEMBER LIST
         BAL   R14,DEFGROUP            DEFINE NEW MEMBER GROUP
REPR794  BAL   R6,RESERVE              RESERVE/ENQUE AS REQUIRED
         SPACE 1
         MVI   SUBPOOLT,21             MARK TEMPORARY STORAGE OBTAINED
         LA    R0,1024*3               STORAGE FOR THREE NOTELISTS
         ICM   R0,B'1000',SUBPOOLT     SUBPOOL
         GETMAIN R,LV=(0)
         ST    R1,#NOTEPTR             POINTER TO NOTELIST WORK AREA
         BAL   R2,SHUTSTW2             CLOSE AND FREE THE STOW DCB
         SPACE 1
         MVI   OPENLIST,X'80'
         OPEN  (STOWDCB,OUTPUT),MF=(E,OPENLIST)  OPEN IT FOR OUTPUT
         SPACE 1
         TM    DCBOFLGS-IHADCB+STOWDCB,DCBOFOPN  STOW DCB OPEN?
         LA    R1,L831                           NO OPEN ERROR MSG
         BNO   MSGNEW                            NO, QUIT
         LA    R3,#MEMPTR
         SPACE 1
REPR812  ICM   R3,B'0111',DIRLINK+1    NEXT MEMBER ENTRY
         BZ    REPR820                 END OF LIST, BRANCH
         MVI   DIRLINK,X'80'           ASSUME THE MEMBER IS TO BE MOVED
         TM    DIRFLAG,DIRALIAS        ALIAS ENTRY?
         BNO   REPR818                 NO, BRANCH
         LA    R2,#MEMPTR
         SPACE 1
REPR814  ICM   R2,B'0111',1(R2)              MORE IN THE LIST?
         BZ    REPR818                       NO, THIS ITEM WILL MOVE
         CR    R2,R3                         SAME ITEM?
         BE    REPR814                       YES, TRY AGAIN
         CLC   DIRTTR(3),DIRTTR-DIRLINK(R2)  SAME TTR?
         BNE   REPR814                       NO, BRANCH
         TM    DIRLINK-DIRLINK(R2),X'80'     OTHER ITEM TO BE MOVED?
         BO    REPR816                       YES, TRUE ALIAS
         TM    DIRFLAG-DIRLINK(R2),DIRALIAS  OTHER ITEM AN ALIAS?
         BO    REPR814                       YES, BRANCH
         SPACE 1
REPR816  MVI   DIRLINK,0                     DO NOT MOVE THIS ENTRY
REPR818  TM    DIRFLAG,DIR1TTR+DIR2TTR       ANY USER TTR'S?
         BZ    REPR812                       NO, BRANCH
         SPACE 1
         OI    DIRLINK,X'01'                 AT LEAST ONE TTR
         TM    DIRFLAG,DIR2TTR               TWO OR MORE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'02'                 AT LEAST TWO TTR'S
         TM    DIRFLAG,DIR1TTR+DIR2TTR       THREE TTR'S?
         BNO   *+8                           NO, BRANCH
         OI    DIRLINK,X'04'                 THREE TTR'S
         B     REPR812
         SPACE 3
REPR820  LA    R3,#MEMPTR
REPR822  ICM   R3,B'0111',DIRLINK+1    ANY MORE MEMBERS IN THE LIST?
         BZ    REPR980                 NO, BRANCH
         MVC   INSERT#1(8),DIRNAME     MEMBER NAME
         MVC   INSERT#2(8),=CL8'MOVED'
         CLI   #COPTO#,0                 ANY RENAME OPERAND?
         BE    *+10                      NO, BRANCH
         MVC   INSERT#2(8),=CL8'CREATED' YES, CREATED INSTEAD
         $TMSG L051$2
         TM    DIRLINK,X'80'           MEMBER TO BE MOVED?
         BNO   REPR822                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'+X'01'          MASK BITS FOR TESTING
         LA    R6,3                    LOOP CONTROL
         SPACE 1
*  INPUT THE OVERLAY NOTELIST TABLE
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR824  EX    R5,*-4                  ANOTHER TTR?
         BZ    REPR830                 NO, BRANCH
         CLI   3(R2),0                 NOTELIST COUNT?
         BZ    REPR826                 NO, BRANCH
         OI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  MARK FOR NOTELIST PROCESSING
         MVC   STARTTR(3),0(R2)        NOTELIST TTR ADDRESS
         L     R15,=V(EXCP)            INPUT IT
         BALR  R14,R15
         LA    R1,L751                 ASSUME AN INPUT PROBLEM
         LTR   R15,R15                 CORRECT?
         BP    MSGNEW                  YES, BRANCH
         LR    R14,R0                  INPUT ADDRESS
         SR    R15,R15
         IC    R15,3(,R2)              NUMBER OF NOTELIST ENTRIES
         SLL   R15,2                   SIZE OF NOTELIST AREA
         LR    R0,R4                   START OF THIS NOTELIST AREA
         LR    R1,R15                  SAVE AREA MOVE LENGTH
         MVCL  R0,R14                  SAVE THE NOTELIST DATA
         SPACE 1
REPR826  LA    R2,4(,R2)               NEXT POTENTIAL USER TTR
         LA    R4,1024(,R4)            NEXT POTENTIAL NOTELIST AREA
         SLL   R5,1                    NEXT MASK BITS
         BCT   R6,REPR824
         SPACE 3
*  READ EACH RECORD OF THE MEMBER
REPR830  MVC   STARTTR(3),DIRTTR       FIRST TTR OF THE MEMBER
         MVC   #FINDTTR(3),DIRTTR      SAVE THE FIRST TTR FOR LATER
         XC    #REPRLEN(4),#REPRLEN    RESET THE COUNT FIELD
         TM    FLAGSCC,RECFMF          RECFM=F?
         BO    REPR870                 YES, BRANCH
         MVI   #REPRLEN+3,4            SET LENGTH FOR RECFM=V
         TM    FLAGSCC,RECFMV          RECFM=V?
         BO    REPR870                 YES, BRANCH
         SPACE 2
REPR832  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR834                   00 - GOOD READ
         B     REPR960                   04 - END OF MEMBER
         B     REPR960                   08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
REPR834  STM   R2,R12,28(R13)          CONVERT CCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         ST    R0,#FIXTTRI             SAVE CURRENT INPUT TTR
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    REPR844                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST STORAGE
         LA    R5,X'10'                MASK
         LA    R6,3                    LOOP CONTROL
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR836  EX    R5,*-4                  ANY NOTELIST FOR THIS ENTRY?
         BZ    REPR838                 NO, BRANCH
         CLM   R0,B'1110',0(R2)        IS THIS THE NOTELIST ENTRY?
         BE    REPR840                 YES, BRANCH
REPR838  LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST ENTRY
         SLL   R5,1                    NEXT MASK
         BCT   R6,REPR836
         B     REPR844                 NOT THIS RECORD, BRANCH
         SPACE 2
REPR840  SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
         LA    R1,L750                 ERROR MESSAGE FOR A PROBLEM
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R5,*-4                  TURN OFF THIS NOTELIST ENTRY
         L     R5,EXCPBUFF             OUTPUT START ADDRESS
REPR842  TM    3(R4),X'80'             NOTELIST TTR PREVIOUSLY UPDATED?
         BNO   MSGNEW                  NO, ERROR
         MVC   0(3,R5),0(R4)           REPLACE THE BUFFER TTR
         LA    R4,4(,R4)               NEXT STORAGE ADDRESS
         LA    R5,4(,R5)               NEXT BUFFER ADDRESS
         BCT   R0,REPR842              MOVE AND CHECK ALL ENTRIES
         SPACE 3
REPR844  L     R2,EXCPBUFF             START OF OUTPUT
         CLI   0(R2),X'80'             IDR RECORD?
         BNE   REPR846                 NO, BRANCH
         TM    2(R2),X'01'             ZAP IDR?
         BNO   REPR846                 NO, BRANCH
         OI    #REPRLEN,X'80'          MARK ZAP IDR INPUT
         SPACE 1
REPR846  TM    #REPRLEN,X'80'          ANY ZAP IDR?
         BNO   REPR850                 NO, BRANCH
         TM    #REPRLEN,X'40'          ZAP IDR ALREADY ADDED?
         BO    REPR850                 YES, BRANCH
         CLI   0(R2),X'80'             IDR RECORD?
         BNE   REPR848                 NO, BRANCH
         TM    2(R2),X'01'             ZAP IDR?
         BO    REPR850                 YES, BRANCH
REPR848  CLI   #REPADD,1               ADD AN IDR RECORD?
         BNE   REPR850                 NO, BRANCH
         OI    #REPRLEN,X'40'          IDR RECORD ADDED
         XC    MSGTEXT1(256),MSGTEXT1  CLEAR THE RECORD TO ADD
         MVI   MSGTEXT1+0,X'80'        IDR RECORD
         MVI   MSGTEXT1+1,X'FA'        IDR RECORD LENGTH
         MVI   MSGTEXT1+2,X'01'        IDR RECORD FOR ZAP INFORMATION
         LA    R0,251                  RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         LA    R2,MSGTEXT1             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         SPACE 3
*  OUTPUT EACH RECORD AND CHANGE TTR POINTERS AS NECESSARY
REPR850  L     R0,LS                   RECORD LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R2,EXCPBUFF             START OF DATA
         WRITE FIXPDSD,SF,STOWDCB,(2),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         TM    DIRLINK,X'77'           ANY PENDING TTR UPDATES?
         BZ    REPR832                 NO, BRANCH
         LA    R2,DIRSTART             FIRST USER TTR
         L     R4,#NOTEPTR             START OF NOTELIST
         LA    R5,X'10'                NOTELIST MASK
         LA    R6,3                    LOOP CONTROL
         LA    R14,X'01'               TTR MASK
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR852  EX    R14,*-4                 TTR IN THIS POSITION?
         BNO   REPR854                 NO, BRANCH
         CLC   #FIXTTRI(3),0(R2)       THIS INPUT TTR?
         BNE   REPR854                 NO, BRANCH
         STCM  R1,B'1110',0(R2)        YES, UPDATE THE TTR
         XI    DIRLINK,*-*             <<EXECUTED>>
         EX    R14,*-4                 TURN OFF THE TTR INDICATOR
         SPACE 1
         TM    DIRLINK,*-*             <<EXECUTED>>
REPR854  EX    R5,*-4                  NOTELIST FOR THIS ENTRY?
         BNO   REPR858                 NO, BRANCH
         LR    R15,R4                  START OF NOTELIST
         SR    R0,R0
         IC    R0,3(R2)                NUMBER OF NOTELIST ENTRIES
REPR856  CLC   0(3,R15),#FIXTTRI       THIS TTR?
         BNE   *+12                    NO, BRANCH
         STCM  R1,B'1110',0(R15)       YES, UPDATE THE TTR
         OI    3(R15),X'80'                 AND MARK AS UPDATED
         LA    R15,4(,R15)             NEXT NOTELIST ENTRY
         BCT   R0,REPR856
REPR858  LA    R2,4(,R2)               NEXT USER TTR
         LA    R4,1024(,R4)            NEXT NOTELIST STORAGE
         SLL   R5,1                    NEXT NOTELIST BIT MASK
         SLL   R14,1                   NEXT TTR BIT MASK
         BCT   R6,REPR852
         B     REPR832                MOVE ALL MEMBERS
         SPACE 3
*  REBLOCK FOR REPRO
REPR870  L     R15,=V(EXCP)            NEXT USER TTR
         BALR  R14,R15
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR872                   00 - GOOD READ
         B     REPR918                   04 - END OF MEMBER
         B     REPR918                   08 - END OF DATA SET
         B     NEWCMD                    12 - I/O ERROR
         SPACE 1
REPR872  L     R5,LS                   CURRENT LENGTH
         LR    R6,R0                   START OF RECORD
         AR    R5,R6                   END OF RECORD
         BCTR  R5,0                    END OF RECORD -1
         LA    R2,REPR890              WHERE-TO-GO FOR FB
         TM    FLAGSCC,RECFMF          RECFM=F?
         LH    R4,LRECL
         BOR   R2                      YES, BRANCH
         AH    R6,=H'4'                SKIP FIRST VB COUNT
         LA    R2,REPR874              WHERE-TO-GO FOR VB
         SPACE 1
REPR874  SR    R4,R4
         ICM   R4,B'0011',0(R6)        LRECL FOR VB
         B     REPR890                 PROCESS THIS RECORD
         SPACE 1
REPR880  BXLE  R6,R4,0(R2)             GET NEXT LRECL
         B     REPR870                 GET NEXT RECORD
         SPACE 1
REPR890  STM   R3,R6,#LFSAVE4
         L     R6,#REPRBUF             START OF BUFFER
         L     R5,#REPRLEN             LENGTH OF BUFFER
         AR    R5,R6                   END OF BUFFER
         SPACE 1
         LR    R1,R5
         AR    R1,R4                   TEST ADDITION
         SPACE 1
         L     R0,#REPRMAX             MAXIMUM BLKSIZE
         A     R0,#REPRBUF             MAXIMUM ADDRESS
         CR    R1,R0                   FIT IN THIS BLOCK?
         BH    REPR920                 NO, BRANCH
         SPACE 2
REPR910  L     R0,#LFSAVE4+12          CURRENT RECORD
         LR    R1,R4                   CURRENT LRECL
         LR    R4,R5                   CURRENT OUTPUT START
         LR    R5,R1                   CURRENT LRECL
         MVCL  R4,R0                   MOVE THE CURRENT RECORD
         LM    R3,R6,#LFSAVE4          RESET REGISTERS
         L     R0,#REPRLEN
         AR    R0,R4                   ADD CURRENT LENGTH
         ST    R0,#REPRLEN             NEW LENGTH
         B     REPR880
         SPACE 2
REPR918  OI    #REPRLEN,X'80'          MARK END OF MEMBER
         LH    R0,#REPRLEN+2           LENGTH
         LTR   R0,R0                   ANY DATA?
         BZ    REPR960                 NO, BRANCH
         CH    R0,=H'4'                FOUR?
         BH    REPR920                 NO, BRANCH
         TM    FLAGSCC,RECFMV          RECFM=V?
         BO    REPR960                 YES, BRANCH
         SPACE 1
REPR920  LH    R0,#REPRLEN+2           LENGTH
         STH   R0,STOWDCB+DCBBLKSI-IHADCB  UPDATE IN THE DCB
         L     R4,#REPRBUF             START OF DATA
         TM    FLAGSCC,RECFMV          RECFM=V?
         BNO   *+8                     NO, BRANCH
         STH   R0,0(,R4)               UPDATE THE LENGTH FIELD
         WRITE FIXPDSD,SF,STOWDCB,(4),'S',MF=E
         CHECK FIXPDSD
         NOTE  STOWDCB                 OUTPUT TTR
         ST    R1,#FIXTTRO
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      FIRST MEMBER RECORD?
         BNE   *+10                    NO, BRANCH
         MVC   DIRTTR(3),#FIXTTRO      YES, UPDATE THE TTR
         SPACE 1
         LM    R3,R6,#LFSAVE4          RESET REGISTERS
         TM    #REPRLEN,X'80'          END OF MEMBER?
         BO    REPR960                 YES, BRANCH
         MVC   #REPRLEN(4),ZERO        CLEAR THE LENGTH FIELD
         TM    FLAGSCC,RECFMF          RECFM=F?
         BO    REPR890                 YES, BRANCH
         MVI   #REPRLEN+3,4            SET INPUT FOR COUNT FIELD
         B     REPR890
         SPACE 3
*  VERIFY THAT THE MEMBER WAS CORRECTLY PROCESSED
REPR960  LA    R1,L871                 ASSUME A TTR WAS NOT UPDATED
         TM    DIRLINK,X'77'           CORRECT?
         BNZ   MSGNEW                  YES, BRANCH
         SPACE 1
         CLC   DIRTTR(3),#FINDTTR      A NULL MEMBER?
         BNE   REPR962                 NO, BRANCH
         SPACE 1
* UPDATE TTR BY TWO AND STOW AGAIN FOR NULL MEMBERS
         STOW  STOWDCB,DIRNAME,D
         SPACE 1
         ICM   R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         LA    R1,2(,R1)
         STCM  R1,B'0111',DCBRELAD-IHADCB+STOWDCB  HIGHEST OUTPUT TTR+2
         STOW  STOWDCB,DIRNAME,A
         LA    R1,L832
         LTR   R15,R15
         BP    EXIT8M
         SPACE 1
         SR    R1,R1                               YES, CHANGE THE TTR
         ICM   R1,B'0111',DCBRELAD-IHADCB+STOWDCB  TTR OF LAST EOF
         S     R1,=F'1'                            TTR OF NULL DATA
         STCM  R1,B'0111',DIRTTR                   TTR OF NULL DATA
         STCM  R1,B'0111',#FIXTTRO                 HIGHEST OUTPUT TTR
         SPACE 2
REPR962  TM    DIRFLAG,DIRALIAS        ALIAS (WITH NO MAIN)?
         BNO   REPR970                 NO, BRANCH
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         XI    DIRFLAG,DIRALIAS        TEMPORARILY TURN OFF ALIAS BIT
         STOW  STOWDCB,DIRNAME,R       REPLACE THIS MEMBER ENTRY
         OI    DIRFLAG,DIRALIAS        RESET THE ALIAS BIT
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR970                   00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         B     REPR970                   08 - NOT PRESENT
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 3
*  UPDATE THIS MEMBER AND ALL OF ITS ALIASES
REPR970  LA    R2,#MEMPTR
         LR    R4,R3
         SPACE 1
REPR972  MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR-DIRLINK(R4)  TTR
         STOW  STOWDCB,4(R4),R         REPLACE THIS MEMBER ENTRY
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     REPR974                   00 - REPLACED
         EX    0,*                       04 - SHOULD NOT OCCUR
         B     REPR974                   08 - NOT PRESENT
         B     FULLDIR                   12 - DIRECTORY FULL
         B     IOERROR                   16 - I/O ERROR
         SPACE 1
REPR974  ICM   R2,B'0111',1(R2)               NEXT MEMBER ENTRY
         BZ    REPR822                        END OF LIST, BRANCH
         CLC   #FINDTTR(3),DIRTTR-DIRLINK(R2) THIS TTR TOO?
         BNE   REPR974                        NO, BRANCH
         CR    R2,R3                          SAME ENTRY?
         BE    REPR974                        YES, BRANCH
         LR    R4,R2
         NI    DIRLINK-DIRLINK(R2),FF-X'80'   DO NOT MOVE THIS MEMBER
         MVC   DIRTTR-DIRLINK(3,R2),DIRTTR    NEW PRIMARY TTR
         LA    R14,DIRSTART-DIRLINK(,R2)      FIRST ALIAS USER TTR
         LA    R15,DIRSTART                   FIRST MOVED USER TTR
         LA    R5,X'01'                       MASK BIT
         LA    R6,3                           LOOP CONTROL
         SPACE 1
         TM    DIRLINK-DIRLINK(R2),*-*        <<EXECUTED>>
REPR976  EX    R5,*-4                         A TTR AT THIS ENTRY?
         BNO   REPR972                        NO, BRANCH
         MVC   0(3,R14),0(R15)                YES, UPDATE THE TTR ENTRY
         LA    R14,4(,R14)                    NEXT ALIAS TTR
         LA    R15,4(,R15)                    NEXT MOVED TTR
         SLL   R5,1                           NEXT MASK BIT
         BCT   R6,REPR976
         B     REPR972
         DROP  R3
         SPACE 3
REPR980  BAL   R2,CLOSEIT                        CLOSE INPUT DATA SET
         LA    R0,RESTART0                       ABEND RECOVERY ADDRESS
         ST    R0,RECOVER
         BAL   R2,DEALLDCB             FREE THE DATA SET
         B     RESTART0                REALLOCATE AND START OVER
         TITLE 'P D S  --  PDS RESTORE                        5/12/86'
***********************************************************************
***   RESTORE SUBCOMMAND    MODIFIED BY BRUCE LELAND -- JUNE, 1982  ***
***********************************************************************
*
         SPACE 1
RESTORE  CSECT
         USING *,R8
         ICM   R1,B'1111',MEMBER2       TTR TO BE USED
         CLI   LMEMBER2+1,1             COMPARE LENGTH:1
         BH    REST010                    HIGH (DO NOT ADJUST)
         SRL   R1,8
         BE    REST010                    EQUAL (ADJUSTED ONE TIME)
         SRL   R1,8                       LOW (ADJUSTED TWICE)
         SPACE 1
REST010  STCM  R1,B'1111',MEMBER2       SAVE THE UPDATED ADDRESS
         STCM  R1,B'1111',DIRTTR        SAVE THE UPDATED ADDRESS
         SPACE 1
*** REOPEN THE DATA SET TO ENSURE THAT DS1LSTAR IS CORRECT
         OI    FLAGSJJ,FNOREAD         CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DS1LSTAR     FIRST DISK ADDRESS TO READ
         L     R15,=V(EXCP)
         BALR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD      EXCP OPEN IS COMPLETE
         SPACE 1
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST020                  NO, BRANCH
         MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         MVI   #RESTDIR+9,ATTREXEC      EXECUTABLE
         MVI   #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         MVI   #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST020  CLI   #RESTLIK,0               ANY LIKE OPERAND?
         BE    REST080                  NO, BRANCH
         XC    DIRNAME(74),DIRNAME
         MVC   DIRNAME,#RESTLIK         LIKE NAME
         BLDL  INDCB,BLDLLIST           GET MEMBER DIRECTORY
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST030                    00 - FOUND
         B     NOMEMBER                   04 - NOT FOUND
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
REST030  MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2  BLDL ADDS THINGS
         MVC   #RESTDIR,DIRFLAG         SAVE USER DATA
         NI    #RESTDIR,FF-X'80'        TURN OFF ANY ALIAS BIT
         TM    FLAGSCC,RECFMU           LOAD MODULE?
         BNO   REST080                  NO, BRANCH
         XC    #RESTDIR,#RESTDIR        CLEAR THE USER DATA
         LA    R2,DIRAPF                POINT TO APF DATA
         TM    DIRATTR,ATTRSCTR         SCATTER LOAD?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, POSITION OVER FIELDS
         TM    DIRFLAG,X'80'            ALIAS?
         BO    *+12                     YES, BRANCH
         CLI   8(R2),0                  CONVERTED ALIAS ENTRY?
         BE    *+8                      NO, BRANCH
         LA    R2,11(,R2)               ADD ALIAS LENGTH
         TM    DIRATTR2,DIRAOSLE        O/S MVS LINKAGE EDITOR?
         BNO   REST040                  NO, BRANCH
         TM    DIRATTR2,DIR2SSI         ANY SSI FIELD?
         BNO   REST050                  NO, BRANCH
REST040  LA    R1,1(,R2)                ADD FOR ROUNDING TO HALFWORD
         N     R1,=F'-2'                ROUND TO HALFWORD
         CLC   ZERO,0(R1)               ZERO?
         BE    REST050                  YES, BRANCH
         CLC   =F'-1',0(R1)             HIGH VALUES?
         BE    REST050                  YES, BRANCH
         MVC   #RESTSSI,0(R1)           SAVE THE SSI VALUE
         LA    R2,4(,R1)                POSITION AFTER SSI
         SPACE 1
REST050  TM    DIRATTR2,DIRAOSLE+DIRAPFLG  VS LKED AND APF DATA GOOD?
         BO    REST060                     YES, BRANCH
         XC    DIRATTR2(3),DIRATTR2        NO, CLEAR NEW FLAGS
         B     REST070
         SPACE 1
REST060  CLI   0(R2),X'01'              VALID APF LENGTH?
         BNE   REST070                  NO, BRANCH
         MVC   #RESTAPF,1(R2)           YES, SAVE APF DATA
         SPACE 1
REST070  MVI   #RESTDIR,X'20'+12        1 TEXT TTR, 12 HALFWORDS
         NI    DIRATTR,X'CA'            RENT, REUS, ONLY LOAD, EXEC.
         NI    DIRATTR+1,X'09'          NOT EDIT, REFR
         MVC   #RESTDIR+9(2),DIRATTR    COPY INTO THE HOLD AREA
         OI    #RESTDIR+10,ATTRNODC+ATTRZORG+ATTREP0+ATTRFLEV
*                                       NOT DC, ZERO ORG, EP=0, F-LEVEL
         NI    DIRATTR2,DIR2PAGA        PAGE BOUNDARY FLAG
         NI    DIRATTR2+1,X'1F'         RMODE, AMODE FLAGS
         MVC   #RESTDIR+19(2),DIRATTR2  COPY INTO THE HOLD AREA
         OI    #RESTDIR+19,DIRAOSLE+DIRAPFLG  VS EDITOR, APF DATA IS OK
         SPACE 1
REST080  MVC   DIRNAME,MEMBER1          MEMBER NAME TO BE RESTORED
         MVC   DIRTTR(3),MEMBER2        TTR OF MEMBER TO BE RESTORED
         SPACE 1
         LA    R1,L873
         CLC   DIRTTR(3),DS1LSTAR       IN DATA SET BOUNDS?
         BNL   MSGNEW                   NO, ERROR
         SPACE 1
         TM    #RESFLAG,#RESFREP+#RESFDIS  REPEAT, DISPLAY OR PROMPT?
         BZ    REST803                     NO, BRANCH
         SPACE 1
         OI    FLAGSII,FNOTDOUB         FORCE SINGLE OR MULTIPLE BUFFER
         ICM   R1,B'1110',DIRTTR        T T R X  ZERO?
         BZ    REST210                  YES, BRANCH
         SRL   R1,8                     0 T T R
         S     R1,=F'1'                 0 T T R - 1 = 0?
         BZ    REST210                  YES, BRANCH
         SLL   R1,8                     T T R 0 (WHERE R IS REDUCED)
         STCM  R1,B'1110',DIRTTR        T T R
         CLI   MEMBER2+2,X'01'          R OF TTR 1 OR LESS?
         BH    REST210                  YES, BRANCH
         SRL   R1,16                    0 0 T T
         BCTR  R1,0                     0 0 T T - 1
         SLL   R1,16                    T T 0 0 (WHERE TT IS REDUCED)
         STCM  R1,B'1100',DIRTTR        T T  UPDATED
         MVI   DIRTTR+2,X'01'           RESET R TO 1 OF PREVIOUS TRACK
         SPACE 1
REST210  TM    #RESFLAG,#RESFREP        REPEAT?
         BZ    REST220                  NO, BRANCH
         SPACE 2
         LA    R1,7                     MACHINE LENGTH
         LA    R2,DIRNAME+7             LAST CHARACTER
         SPACE 1
         CLI   0(R2),X'40'              BLANK?
         BNE   *+10                     NO, BRANCH
         BCTR  R2,0                     PREVIOUS CHARACTER
         BCT   R1,*-10                  TRY ALL BYTES
         SPACE 1
         ST    R1,#RESTLEN                          NAME LENGTH
         SPACE 3
REST220  MVI   SUBPOOLT,21              MARK TEMPORARY STORAGE IN USE
         LA    R5,1024                  1024 MEMBERS INITIALLY
         SPACE 1
REST240  LR    R0,R5
         SLL   R0,2                     MEMBERS*4 FOR TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)
         ST    R1,#MEMPTR               START OF TTR LIST
         LR    R4,R1
         XC    0(4,R4),0(R4)            DUMMY FIRST ELEMENT
         LR    R3,R5                    NUMBER OF ELEMENTS
         MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST242  BAL   R14,READDIR              GET THE NEXT MEMBER
         B     REST246                  END OF DIRECTORY, BRANCH
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR START<TTR OF THIS MEMBER?
         BNL   REST242                  NO, IGNORE
         S     R3,=F'1'                 WILL THIS FIT?
         BNP   REST244                  NO, BRANCH
         LA    R4,4(,R4)                NEXT TTR TABLE POSITION
         MVC   0(3,R4),MEMTTR           SAVE THE MEMBER START TTR
         MVI   3(R4),0                  CLEAR THE END FLAG
         B     REST242                  GET THE NEXT ENTRY
         SPACE 1
REST244  SR    R0,R0
         ICM   R0,B'1000',SUBPOOLT      SUBPOOL TO FREE
         FREEMAIN R,SP=(0)
         SLL   R5,1                     DOUBLE THE NUMBER OF ENTRIES
         B     REST240
         SPACE 2
REST246  MVI   3(R4),X'FF'              MARK THE END OF THE TTR LIST
         L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BALR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         SPACE 2
REST247  TM    #RESFLAG,#RESFREP        REPEAT RESTORE?
         BNO   REST248                  NO, BRANCH
         L     R1,#RESTSEQ              THIS MEMBER NUMBER
         LA    R1,1(,R1)
         ST    R1,#RESTSEQ              NEW MEMBER NUMBER
         CVD   R1,DOUBLE
         OI    DOUBLE+7,X'0F'           CORRECT SIGN
         L     R1,#RESTLEN              MACHINE LENGTH
         UNPK  DIRNAME(8),DOUBLE(8)     CONVERT THE SEQUENTIAL NAME
         MVC   DIRNAME(*-*),MEMBER1     <<EXECUTED>>
         EX    R1,*-6                   ADD THE CODED NAME PORTION
         B     REST260                  SKIP THIS CLEAR
         SPACE 1
REST248  L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BALR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         SPACE 2
REST260  $MESAGE MSGBLANK               ONE BLANK LINE
         MVC   STARTTR(3),DIRTTR        FIRST DISK ADDRESS TO READ
         MVI   #RESTERR+1,3             ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 2
REST262  L     R15,=V(EXCP)
         BALR  R14,R15
         LA    R1,L006                  ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST262                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
*  PERMANENT I/O ERROR
*    A.  IF TOO MANY ERRORS FOR THIS MEMBER, TERMINATE
*    B.  SIMULATE AN END OF MEMBER CONDITION
*    C.  CONTINUE ON THE NEXT TRACK
REST263  LA    R1,L804                  ASSUME TOO MANY ERRORS
         LH    R0,#RESTERR
         S     R0,=F'1'                 CORRECT?
         BM    MSGNEW                   YES, BRANCH
         STH   R0,#RESTERR
         $TMSG L801
         STM   R2,R12,28(R13)
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         SRL   R0,16                    TT OF LAST INPUT TTRN
         A     R0,=F'1'
         STH   R0,STARTTR               TT OF NEXT TRACK
         MVI   STARTTR+2,X'01'          R OF TTR
         SPACE 2
*  END OF MEMBER, GET THE NEXT DATA BLOCK
REST264  L     R15,=V(EXCP)
         BALR  R14,R15
         LA    R1,L006                  ASSUME END OF DATA SET
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST266                    00 - SUCCESSFUL READ
         B     REST264                    04 - END OF MEMBER
         B     MSGNEW                     08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST266  STM   R2,R12,28(R13)           CONVERT MBBCCHHR TO TTR
         LA    R2,CURMBB
         L     R1,INDCB+(DCBDEBAD-IHADCB)
         L     R15,ADDRRLTV
         LR    R3,R13
         BALR  R14,R15
         LR    R13,R3
         LM    R2,R12,28(R13)
         STCM  R0,B'1110',DIRTTR        TTR OF THIS MEMBER
         CLM   R0,B'1110',MEMBER2       AT OR AFTER THE START POINT?
         BL    REST262                  NO, BRANCH
         SPACE 1
         L     R4,#MEMPTR               START OF TTR TABLE
REST268  CLC   DIRTTR(3),0(R4)          IN THE TABLE?
         BE    REST262                  YES, IGNORE THIS MEMBER
         CLI   3(R4),X'FF'              END OF TTR TABLE?
         LA    R4,4(,R4)
         BNE   REST268                  NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU           RECFM=U?
         BNO   REST410                  NO, BRANCH
         CLI   #RESTNAM,X'40'           ANY MODULE NAME?
         BH    REST430                  YES, BRANCH
         SPACE 1
REST410  CLI   #RESTSTR+1,0             ANY STRING?
         BNE   REST490                  NO, BRANCH
         B     REST710
         SPACE 2
REST420  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST430                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST430  L     R3,EXCPBUFF              START OF RECORD
         CLI   0(R3),X'80'              END OF ESD, FIRST IDR?
         BE    REST262                  YES, BRANCH
         CLI   0(R3),X'20'              ESD RECORD?
         BNE   REST420                  NO, BRANCH
         SPACE 1
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 2
         USING ESDNAME,R3
REST440  CLI   ESDTYPE,X'03'            ENTRY SYMBOL?
         BE    REST444                  NO, BRANCH
         TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST450                  NO, BRANCH
REST444  SR    R1,R1
         ICM   R1,B'0001',#RESTNA#      LENGTH TO TEST
         BCTR  R1,0                     MACHINE LENGTH TO TEST
         CLC   ESDNAME(*-*),#RESTNAM    <<EXECUTED>>
         EX    R1,*-6                   THIS MODULE NAME?
         BE    REST470                  YES, BRANCH
REST450  LA    R3,16(,R3)               NEXT ESD
         BCT   R4,REST440               DO ALL ESD SUBENTRIES
         B     REST420                  DO ALL ESD RECORDS
         DROP  R3
         SPACE 2
REST470  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         CLI   #RESTSTR+1,0             ANY STRING TOO?
         BE    REST590                  NO, BRANCH
         L     R15,=V(EXCP)
         BALR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST490  XC    WORKTBL,WORKTBL
         SR    R15,R15
         IC    R15,#RESTSTR+2           FIRST SEARCH CHARACTER
         LA    R1,WORKTBL(R15)
         MVI   0(R1),X'77'              SET SEARCH CHARACTER OF STRING
         B     REST510                  SKIP FIRST READ
         SPACE 2
REST500  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST510                    RC=00 -- GOOD READ
         B     REST264                    RC=04 -- END OF MEMBER
         B     REST264                    RC=08 -- END OF DATA SET
         B     REST263                    RC=12 -- I/O ERROR
         SPACE 2
REST510  L     R6,EXCPBUFF              START OF RECORD
         L     R1,LS                    LENGTH OF RECORD
         LA    R4,256                   MAXIMUM LENGTH FOR TRT
         LR    R5,R1
         AR    R5,R6                    END OF RECORD
         BCTR  R5,0                     END OF RECORD -1
         B     REST540
         SPACE 1
REST520  BXLE  R6,R4,REST540            GET NEXT DATA SEGMENT
         B     REST500                  GET NEXT RECORD
         SPACE 1
REST540  LR    R2,R4                    EXECUTE LENGTH +1
         LR    R14,R5
         SR    R14,R6                   MACHINE LENGTH VALID?
         BM    REST520                  NO, BRANCH
         SPACE 1
         CR    R2,R14                   LONGER THAN RECORD LEFT?
         BNH   *+8                      NO, BRANCH
         LA    R2,1(,R14)               YES, USE REMAINING LENGTH
         BCTR  R2,0                     EXECUTE LENGTH
         SPACE 1
         LR    R1,R6                    START OF DATA
         LA    R14,0(R6,R2)             END OF DATA -1
         LR    R3,R14
         SPACE 1
         LH    R15,#RESTSTR             STRING LENGTH
         AR    R14,R15                  MAXIMUM ACCESS
         BCTR  R15,0                    EXECUTE LENGTH
         CR    R14,R5                   BEYOND RECORD LENGTH?
         BL    *+6                      NO, BRANCH
         LR    R14,R5                   YES, USE RECORD END -1
         SPACE 2
REST550  EX    R2,REST560               FIRST BYTE OF STRING FOUND?
         BZ    REST520                  NO, BRANCH
         LR    R2,R14
         SR    R2,R1                    MACHINE LENGTH LEFT
         CR    R15,R2                   IN THIS SEGMENT?
         BH    REST520                  NO, BRANCH
         CLC   0(*-*,R1),#RESTSTR+2     <<EXECUTED>>
         EX    R15,*-6                  ENTIRE STRING PRESENT?
         BE    REST590                  YES, BRANCH
         LA    R1,1(,R1)                NO, TRY NEXT BYTE
         LR    R2,R3                    LAST ACCESS BYTE
         SR    R2,R1                    MACHINE LENGTH LEFT NEGATIVE?
         BNM   REST550                  NO, KEEP SEARCHING
         B     REST520                  YES, TRY NEXT SEGMENT
         SPACE 1
REST560  TRT   0(*-*,R1),WORKTBL        <<EXECUTED>>
         SPACE 2
REST590  MVC   STARTTR(3),DIRTTR        REREAD THIS MEMBER
         L     R15,=V(EXCP)
         BALR  R14,R15
         LTR   R15,R15                  GOOD READ?
         BNZ   REST263                  NO, MUST BE I/O ERROR
         SPACE 3
REST710  UNPK  INSERT#1(7),DIRTTR(4)    TTR OF THIS DELETED MEMBER
         TR    INSERT#1(6),TRTABLE      MAKE PRINTABLE
         MVC   INSERT#1+6(2),BLANKS
         $TMSG L101$1
         TM    #RESFLAG,#RESFDIS        DISPLAY OPTION?
         BNO   REST790                  NO, BRANCH
         $MESAGE MSGBLANK
         SR    R5,R5                    LINE IN PROGRESS POINTER
         SR    R6,R6                    OUTPUT COUNTER
         TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST736                  YES, BRANCH
         B     REST762                  NO, MUST BE V OR F
         SPACE 3
*  RECORD FORMAT U (LOAD MODULE)
REST730  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST736                    00 - SUCCESSFUL READ
         B     REST732                    04 - END OF MEMBER
         B     REST732                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST732  LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST734                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         B     REST790
         SPACE 1
REST734  $TMSG L805                     NO CESD OR LKED IDR RECORDS
         MVC   STARTTR(3),DIRTTR        RE-READ THE MEMBER
         B     REST760                  OUTPUT WITH CHARACTER OUTPUT
         SPACE 2
REST736  L     R3,EXCPBUFF              BUFFER ADDRESS
         CLI   0(R3),X'20'              CESD RECORD?
         BNE   REST750                  NO, CHECK FOR LINKAGE-EDIT IDR
         C     R6,#RESTNUM              NEED TO BUILD MORE CSECT LINES?
         BNL   REST730                  NO, BRANCH
         LH    R4,6(,R3)                LENGTH OF ESD DATA
         SRL   R4,4                     LENGTH/16=NUMBER OF ENTRIES
         LA    R3,8(,R3)                START OF ESD DATA
         SPACE 1
         USING ESDNAME,R3
REST738  TM    ESDTYPE,X'0F'            CSECT ENTRY?
         BNZ   REST746                  NO, BRANCH
         CLI   ESDNAME,X'40'            BLANK CSECT NAME?
         BE    REST746                  YES, IGNORE
         LA    R2,ESDNAME+7
         LTR   R5,R5                    LINE IN PROGRESS?
         BP    REST740                  YES, BRANCH
         LA    R1,80                    ASSUME AN ACTIVE MODE
         TM    SPFLAG0,SPFDON           ISPMODE ACTIVE?
         BO    REST739                  YES, BRANCH
         TM    CONTOPTN,1               ANY LOG RECORDING?
         BO    REST739                  YES, BRANCH
         GTSIZE ,                       TERMINAL SIZE
         CH    R1,=H'120'               120 OR LESS BYTES?
         BL    *+8                      YES, BRANCH
         LH    R1,=H'120'               NO, USE 120 BYTES
REST739  TM    FLAGSCC,RECFMU           LOAD LIBRARY?
         BNO   *+8                      NO, BRANCH
         SH    R1,=H'7'                 YES, LESS SEVEN FOR PREFIX
         ST    R1,LINESIZE              CHARACTERS/LINE
         MVC   MSGLINE+4(L'MSGRESTO),MSGRESTO
         LA    R5,MSGLINE+4+L'MSGRESTO
         MVC   FULLWORD(3),L164$1
         SPACE 2
REST740  CLI   0(R2),X'40'              SCAN BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R2,REST740                                    NON-BLANK
         SPACE 1
         LA    R0,ESDNAME
         SR    R2,R0                    MACHINE LENGTH OF NAME
         LA    R15,2(R2,R5)             CURRENT + NAME-1 + BLANK + 1
         LA    R1,MSGLINE
         SR    R15,R1                   DISPLACEMENT INTO THE LINE
         C     R15,LINESIZE             PAST END OF LINE?
         BNH   REST744                  NO, BRANCH
         SPACE 1
         LA    R6,1(,R6)                ONE MORE OUTPUT LINE
         C     R6,#RESTNUM              ENOUGH LINES?
         BL    REST742                  NO, BRANCH
         MVC   0(4,R5),RESTLDOT         INDICATE MORE CSECTS EXIST
         LA    R5,5(,R5)                INCREMENT TO END +1
         B     REST730
         SPACE 2
REST742  LA    R1,MSGLINE+4
         SR    R5,R1                    MESSAGE LENGTH
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         MVI   FULLWORD+2,C'9'
         LA    R5,MSGLINE+3             INCREMENT TO END
         SPACE 1
REST744  MVI   0(R5),X'40'              BLANK BEFORE CSECT NAME
         MVC   1(8,R5),ESDNAME          CSECT NAME
         TR    1(8,R5),TRLINE           MAKE PRINTABLE
         LA    R5,2(R2,R5)              POSITION TO FOLLOWING COMMA
         MVI   0(R5),C','               ADD THE COMMA
         LA    R5,1(,R5)                POSITION AFTER THE COMMA
         SPACE 1
REST746  LA    R3,16(,R3)               NEXT ESD ENTRY
         BCT   R4,REST738               LOOP FOR ALL ENTRIES
         B     REST730                  GET THE NEXT RECORD
         DROP  R3
         SPACE 3
REST750  CLI   0(R3),X'80'              IDR RECORD?
         BNE   REST730                  NO, CONTINUE INPUT
         TM    2(R3),IDRLKED            LINKAGE EDITOR IDR RECORD?
         BNO   REST730                  NO, CONTINUE INPUT
         LTR   R5,R5                    LINE IN PROGRESS?
         BNP   REST752                  NO, BRANCH
         LA    R1,MSGLINE+5
         SR    R5,R1                    LENGTH OF MESSAGE SO FAR
         STC   R5,MTHIGHL
         MVC   INSERT#1(127),MSGLINE+4
         LA    R1,FULLWORD
         $TMSG (R1)
         MVI   MTHIGHL,8
         SPACE 1
REST752  $MESAGE MSGBLANK
         LA    R1,12+3(,R3)             ADDRESS OF YYDDD FORMAT DATE
         LA    R15,LKEDDATE             ADDRESS OF YYMMDD FORMAT OUTPUT
         BAL   R14,CONVDATE             CONVERT TO MMDDYY FORMAT
         MVC   INSERT#1-1(9),DATEMASK
         ED    INSERT#1-1(9),LKEDDATE
         MVI   MTHIGHL+4,18                   LENGTH OF THIS INSERT
         MVC   INSERT#2(10),3(R3)             LINKAGE EDITOR NAME
         MVC   INSERT#2+10(2),=CL2' V'
         UNPK  INSERT#2+10+2(3),10+3(2,R3)        VV IN HEX
         MVC   INSERT#2+10+2+2(2),=CL2' M'
         UNPK  INSERT#2+10+2+2+2(3),10+3+1(2,R3)  MM IN HEX
         SPACE 1
         $TMSG L064$2                         LINKAGE EDIT MESSAGE
         MVI   MTHIGHL+4,8                    LENGTH OF STANDARD INSERT
         B     REST790                  PROMPT FOR RESTORE
         SPACE 2
*  RECORD FORMAT V, F OR U
REST760  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST762                    00 - SUCCESSFUL READ
         B     REST778                    04 - END OF MEMBER
         B     REST778                    08 - END OF DATA SET
         B     REST263                    12 - I/O ERROR
         SPACE 1
REST762  L     R5,LS                    BLOCK LENGTH
         L     R2,EXCPBUFF              BUFFER START
         LR    R1,R5
         AR    R5,R2                    END OF BUFFER
         BCTR  R5,0                     END OF BUFFER -1
         TM    FLAGSCC,RECFMF           RECFM=F?
         LH    R4,LRECL
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMU           RECFM=U?
         LR    R4,R1
         BO    REST768                  YES, BRANCH
         TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, ASSUME RECFM=U
         AH    R2,=H'4'                 SKIP BLOCK LENGTH WORD
         B     REST766
         SPACE 2
REST764  BXLE  R2,R4,REST766            GET THE NEXT LOGICAL RECORD
         B     REST760                  END OF BLOCK, BRANCH
         SPACE 2
REST766  TM    FLAGSCC,RECFMV           RECFM=V?
         BNO   REST768                  NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R2)         RECORD LENGTH +4
         AH    R2,=H'4'                 SKIP RECORD LENGTH WORD
         SH    R4,=H'4'                 RECORD LENGTH VALID?
         BP    REST768                  YES, BRANCH
         SR    R4,R4                    NO, USE LENGTH ZERO
         B     REST764                  IGNORE THE RECORD
         SPACE 1
REST768  LA    R6,1(,R6)                ANOTHER OUTPUT RECORD
         C     R6,#RESTNUM              ENOUGH OUTPUT?
         BH    REST778                  YES, BRANCH
         MVC   INSERT#1(8),BLANKS       BLANK
         MVC   FULLWORD(8),BLANKS       BLANK
         CVD   R6,DOUBLE
         MVC   FULLWORD(4),=X'40202120'
         ED    FULLWORD(4),DOUBLE+6
         LA    R1,FULLWORD
         LA    R1,1(,R1)
         CLI   0(R1),X'40'
         BE    *-8
         MVC   INSERT#1(3),0(R1)
         $TMSG L144$1
         LR    R1,R4                    CURRENT RECORD LENGTH
         CH    R1,=H'256'
         BNH   *+8
         LH    R1,=H'256'               MAXIMUM DISPLAY LENGTH
         BCTR  R1,0                     MACHINE LENGTH
         MVC   MSGTEXT1+4(*-*),0(R2)    <<EXECUTED>>
         EX    R1,*-6                   MOVE IN THE DISPLAY TEXT
         TR    MSGTEXT1+4(256),TRLINE   CONVERT TO PRINTABLES
         LA    R1,4+1(,R1)              MESSAGE LENGTH
         CH    R4,=H'256'               TRUNCATED RECORD MESSAGE?
         BNH   *+14                     NO, BRANCH
         MVC   MSGTEXT1+4+256(4),RESTLDOT  INDICATE THAT MORE EXISTS
         LA    R1,4(,R1)                ADD TO THE MESSAGE LENGTH
         STH   R1,MSGTEXT1
         $MESAGE MSGTEXT1
         C     R4,LINESIZE              LRECL EXACTLY MATCH LINESIZE?
         BE    REST764                  YES, A BLANK LINE ALREADY (+1)
         $MESAGE MSGBLANK
         B     REST764
         SPACE 1
REST778  TM    FLAGSCC,RECFMU           RECFM=U?
         BO    REST260                  YES, INVALID LOAD MODULE
REST790  $MESAGE MSGBLANK
         TM    #RESFLAG,#RESFPRO        PROMPT OPTION?
         BO    REST794                  YES, BRANCH
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BNZ   REST805                         YES, BRANCH
REST794  TM    FLAGSEE,FBKGRND+FNOTTERM INPUT FROM THE TERMINAL?
         BNZ   REST805                  NO, RESTORE THIS MEMBER
         LA    R1,PDS390A               CORRECT MEMBER MESSAGE
         BAL   R2,YESNO1                RESPONSE "YES" OR "NO"?
         B     REST248                  NO, CONTINUE SEARCHING
         L     R8,=A(CLEAR)             ENTRY POINT TO CALL
         BALR  R2,R8                    INVOKE CLEAR
         L     R8,=A(RESTORE)           RESET BASE REGISTER
         B     REST805                  NO DIRECTORY CHECK NEEDED
         SPACE 3
REST803  MVI   STARTTR+2,X'01'          TTR=000001 (START OF DIRECTORY)
         SPACE 2
REST804  BAL   R14,READDIR              GET NEXT DIRECTORY MEMBER
         B     REST805                  LAST MEMBER PROCESSED
         SPACE 1
         CLC   DIRTTR(3),MEMTTR         TTR'S MATCH?
         BNE   REST804                  NO, BRANCH
         SPACE 1
         MVC   INSERT#1(8),MEMNAME
         LA    R1,L802$1                MEMBER ALREADY EXISTS AT TTR
         TM    MEMFLAG,X'80'            AN ALIAS?
         BNO   *+8                      NO, BRANCH
         LA    R1,L190$1                AN ALIAS ALREADY EXISTS AT TTR
         $TMSG (R1)
         SPACE 1
         TM    MEMFLAG,X'80'            ALIAS ENTRY?
         BO    REST804                  YES, CONTINUE SEARCHING
         SPACE 1
         CLC   RESTLTMP,DIRNAME         ALLOW IT ANYWAY?
         BNE   NEWCMD                   NO, BRANCH
         SPACE 2
REST805  TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BNO   REST896             NO, STOW AND QUIT
         XC    DIRUSER,DIRUSER     CLEAR THE ADDITIONAL FIELDS
         MVC   DIRFLAG(33),#RESTDIR  INITIALIZE THE USER AREA
         SPACE 1
         MVI   #RESTESD,AMODE24    AMODE 24 IS THE DEFAULT
         MVI   #RESTMOD,RMODEANY   SET RMODE ANY SUMMARY START
         XC    #BLKSI(4),#BLKSI    CLEAR THE MAXIMUM MEMBER BLOCKSIZE
         LA    R2,REST810          WHERE-TO-GO
         MVC   STARTTR(3),DIRTTR   START ADDRESS
         SPACE 3
REST806  L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)                 PROCESS RETURN CODE
         B     REST808                    00 - GOOD READ
         B     REST890                    04 - END OF MEMBER
         B     REST890                    08 - END OF DATA SET
         B     NEWCMD                     12 - I/O ERROR
         SPACE 2
REST808  L     R5,LS                    CURRENT LENGTH
         LR    R4,R0                    START OF BUFFER
         C     R5,#BLKSI                BLKSIZE>MAX?
         BNHR  R2                       NO, BRANCH
         ST    R5,#BLKSI                YES, SAVE NEW MAXIMUM
         BR    R2                       DECODE THE RECORD
         SPACE 3
REST810  MVI   ENTRYPT,C'?'             NO ENTRY POINT NAME YET
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BNE   REST812                  NO, BRANCH
         OI    DIRATTR,ATTRTEST         TEST SYM FLAG
         OI    DIRATTR+1,ATTRSYMS       SYMBOLS ARE AVAILABLE
         SPACE 1
REST812  LA    R2,REST812               WHERE-TO-GO NEXT
         CLI   0(R4),X'40'              TEST SYM RECORD?
         BE    REST806                  YES, GET NEXT RECORD
         SPACE 1
         CLI   0(R4),X'20'              CESD RECORD?
         BNE   REST820                  NO, BRANCH
         SPACE 1
         LH    R6,4(,R4)                CURRENT ESDID
         LA    R3,8(,R4)                START OF ESD DATA
         USING ESDNAME,R3
         LH    R5,6(,R4)                LENGTH OF DATA IN BUFFER
         AR    R5,R3                    END OF DATA
         LA    R4,16                    LENGTH OF ONE ENTRY
         SR    R5,R4                    END OF DATA -(ONE ENTRY LENGTH)
         SPACE 3
REST813  IC    R0,ESDTYPE
         LA    R1,CODESEG          CHECK FOR SEGTAB/ENTAB
         NI    ESDTYPE,X'0F'
         NR    R0,R1
         CLI   ESDTYPE,CODESD      VALID EXTERNAL SYMBOL?
         BE    REST814             YES, BRANCH
         CLI   ESDTYPE,CODELR      EXTERNAL REFERENCE?
         BNE   REST817             NO, BRANCH
         SPACE 1
REST814  CLC   ESDNAME,DIRNAME     THIS MEMBER NAME?
         BNE   REST815             NO, BRANCH
         SR    R14,R14
         ICM   R14,B'0111',ESDADDR  ENTRY ADDRESS ZERO?
         BZ    *+8                  YES, BRANCH
         NI    DIRATTR+1,FF-ATTREP0 NO, INSURE ATTR FLAG OFF
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
         SPACE 1
REST815  OC    ESDADDR(3),ESDADDR  ZERO OFFSET?
         BNZ   REST817             NO, BRANCH
         CLI   ENTRYPT,C'?'        ANY SYMBOL NAME YET?
         BNE   REST817             YES, BRANCH
         MVC   ENTRYPT,ESDNAME     SAVE THE NAME FOR LATER
         STCM  R6,B'0011',DIRSCEP  SAVE THE ESDID OF THE ENTRY POINT
         MVC   #RESTESD,ESDXAFLG   SAVE THE MVS/XA ESD FLAG ENTRY
REST817  CR    R0,R1               THIS SEGTAB/ENTAB ENTRY?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODESD      EXTERNAL DEFINITION?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODEPC      PRIVATE CODE?
         BE    REST818             YES, BRANCH
         CLI   ESDTYPE,CODECM      COMMON STORAGE?
         BNE   REST819             NO, BRANCH
         SPACE 1
REST818  L     R1,ESDADDR-1        RELATIVE OFFSET
         L     R14,ESDLEN-1        LENGTH OF ESD ENTRY
         LA    R14,7(R1,R14)       MAXIMUM DISPLACEMENT +7
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         CLM   R14,B'0111',DIRSTORE SMALLER THAN PREVIOUS HIGH?
         BL    *+8                 YES, BRANCH
         STCM  R14,B'0111',DIRSTORE NO, UPDATE MODULE LENGTH
         NC    #RESTMOD,ESDXAFLG   SUMMARIZE ALL RMODE ENTRIES
         OC    ESDADDR,ESDADDR     ORIGIN AT LOCATION ZERO?
         BNZ   REST819             NO, BRANCH
         OC    ESDLEN,ESDLEN       NULL LENGTH BLOCK?
         BZ    REST819             YES, BRANCH
         STCM  R6,B'0011',DIRSCET  SAVE THE FIRST TEXT ESDID
         SPACE 1
REST819  A     R6,=F'1'            NEXT ESDID
         BXLE  R3,R4,REST813
         B     REST806             GET NEXT RECORD
         SPACE 3
         DROP  R3
REST820  LA    R2,REST820          WHERE-TO-GO NEXT
         CLI   0(R4),X'80'         IDR RECORD?
         BE    REST806             YES, GET NEXT RECORD
         SPACE 1
         LA    R2,REST830          WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'         FILLER RECORD?
         BNE   REST830             NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD  NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT    ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST830  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST831             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST831  TM    DIRATTR+1,ATTRNRLD  NO RLD MARKED?
         BNO   REST833             NO, BRANCH
         CLM   R5,B'0111',DIRSTORE FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST833             YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 1
REST833  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLI   0(R4),X'10'         SCATTER LOADED?
         BE    REST860             YES, BRANCH
         STCM  R0,B'1110',DIRSTART RESULT TTR
         STCM  R5,B'0011',DIRTEXTL SAVE LENGTH OF FIRST TEXT RECORD
         LA    R2,REST806          WHERE-TO-GO NEXT TIME
         CLI   0(R4),X'00'         OVERLAY DEFINITION RECORD?
         BNER  R2                  NO, LOOP UNTIL END OF MEMBER
         LA    R2,REST840          WHERE-TO-GO NEXT
         OI    DIRATTR,ATTROVLY    OVERLAY MODULE
         XI    DIRFLAG,X'40'+X'20' TWO USER TTR'S NOW
         LA    R14,7(,R5)
         N     R14,RESTLFF8        ROUND TO NEXT DOUBLE WORD
         OC    DIREPA(3),DIREPA    NON-ZERO ENTRY POINT?
         BNZ   REST806             YES, BRANCH
         STCM  R14,B'0111',DIREPA  MOVE ENTRY ADDRESS TO DIRECTORY
         NI    DIRATTR+1,FF-ATTREP0  INSURE ATTR FLAG OFF
         MVI   ENTRYPT,C'?'        ENTRY POINT ADDRESS IS NOT KNOWN
         MVI   #RESTESD,AMODE24    AMODE 24 AGAIN
         MVI   #RESTMOD,0          RMODE 24 FOR SUMMARY
         B     REST806
         SPACE 2
REST840  SRL   R5,2                LENGTH/4
         STC   R5,DIRNOTE#         NUMBER OF ENTRIES
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRNOTE  SAVE OVERLAY TABLE TTR
         B     REST806             CONTINUE UNTIL END-OF-MEMBER
         SPACE 1
REST860  STCM  R0,B'1110',DIRNOTE  START OF SCATTER LIST TABLE
         LA    R2,REST863          WHERE-TO-GO NEXT
         MVI   DIRFLAG,X'40'+16    2 TEXT TTRS, 16 HALFWORDS
         OI    DIRATTR,ATTRSCTR    SCATTER LOADED MODULE
         SR    R3,R3               ACCUMULATE LENGTH OF OVERLAY RECORDS
         SPACE 1
REST863  CLI   0(R4),X'10'           SCATTER DEFINITION RECORD?
         BNE   REST865               NO, BRANCH
         AR    R3,R5                 ACCUMULATE SCATTER RECORD LENGTH
         SH    R3,=H'4'              LESS THE HEADER AMOUNT
         B     REST806               CONTINUE FOR ALL SCATTER RECORDS
         SPACE 1
REST865  LA    R2,REST867            WHERE-TO-GO NEXT
         CLI   0(R4),X'0D'           FILLER RECORD?
         BNE   REST867               NO, BRANCH
         OI    DIRATTR+1,ATTRNRLD    NO RLD ENTRIES
         OI    DIRATTR,ATTR1TXT      ASSUME ONLY ONE TEXT RECORD TOO
         SPACE 1
REST867  CLI   0(R4),X'0D'         FILLER RECORD?
         BE    REST806             YES, IGNORE
         CLI   0(R4),X'01'         RLD ENTRY?
         BNE   REST872             NO, BRANCH
         MVC   DIRATTR4,3(R4)      GET THE NUMBER OF LATER ESD ENTRIES
         B     REST806
         SPACE 1
REST872  TM    DIRATTR+1,ATTRNRLD    NO RLD MARKED?
         BNO   REST876               NO, BRANCH
         CLM   R5,B'0111',DIRSTORE   FIRST TEXT LENGTH=TOTAL LENGTH?
         BE    REST876               YES, ONLY ONE TEXT BLOCK
         NI    DIRATTR,FF-ATTR1TXT   NO, MULTI-BLOCK TEXT AND NO RLD
         SPACE 2
REST876  STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         STCM  R0,B'1110',DIRSTART   START OF TEXT
         STCM  R5,B'0011',DIRTEXTL   LENGTH OF FIRST TEXT RECORD
         LA    R6,2(R6,R6)           (#CSECTS+1)*2+2
         SRL   R6,2                  ((#CSECTS+1)*2+2)/4
         SLL   R6,2                  ROUNDED TO NEXT FULLWORD*2
         STCM  R6,B'0011',DIRSCTL    SAVE LENGTH OF TRANSLATE TABLE
         SR    R3,R6
         STCM  R3,B'0011',DIRSCLL    SAVE LENGTH OF SCATTER LIST
         LA    R2,REST806            WHERE-TO-GO NEXT TIME
         BR    R2                    SCAN UNTIL END-OF-MEMBER
         SPACE 1
REST890  LA    R1,L800
         OC    DIRSTART(3),DIRSTART     ANY TEXT TTR?
         BZ    REST898                  NO, ERROR
         SPACE 1
         LA    R1,L703
         OC    DIRSTORE(3),DIRSTORE     STORAGE SIZE AVAILABLE?
         BZ    REST898                  NO, ERROR
         SPACE 1
         CLC   #BLKSI+2(2),=H'1024'     LARGEST BLOCK=1024?
         BNE   *+8                      NO, PROBABLY NOT DC
         NI    DIRATTR+1,FF-ATTRNODC    YES, TURN OFF NO DC
         SPACE 1
         LA    R2,DIRAPF                APF FLAG
         TM    DIRATTR,ATTRSCTR         SCATTER LOADED?
         BNO   *+8                      NO, BRANCH
         LA    R2,8(,R2)                YES, SKIP OVER SCATTER DATA
         OC    #RESTSSI,#RESTSSI        ANY SSI DATA?
         BZ    REST893                  NO, BRANCH
         LA    R2,1(,R2)                PREPARE FOR HALFWORD ROUNDING
         N     R2,=F'-2'                ROUND TO HALFWORD
         OI    DIRATTR2,DIR2SSI         SSI DATA IS PRESENT
         OI    DIRFLAG,X'02'            ADD TWO MORE HALFWORDS
         MVC   0(4,R2),#RESTSSI         ADD THE SSI DATA
         LA    R2,4(,R2)                POSITION OVER SSI DATA
         SPACE 1
REST893  MVI   0(R2),X'01'              APF LENGTH IS 1
         MVC   1(1,R2),#RESTAPF         ADD APF DATA
         CLI   #RESTLIK,0               ANY LIKE MEMBER?
         BH    REST896                  YES, BRANCH (RMODE, AMODE SET)
         SPACE 1
         TM    #RESTMOD,RMODEANY        ALL CSECTS RMODE ANY?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRRMANY        YES, SET RMODE ANY
         TM    #RESTESD,AMODE24         LOW-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM24         YES, SET AMODE 24 ON
         TM    #RESTESD,AMODE31         HIGH-ORDER BIT ON?
         BNO   *+8                      NO, BRANCH
         OI    DIRATTR3,DIRAM31         YES, SET AMODE 31 ON
         SPACE 1
REST896  BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     NEWCMD              COULD NOT OPEN -- ERROR
         MVC   DCBRELAD-IHADCB+STOWDCB(3),DIRTTR  TTR
         STOW  STOWDCB,DIRNAME,A   ADD AT THE GIVEN TTR
         LR    R5,R15              SAVE RETURN CODE
         BAL   R2,SHUTSTOW         CLOSE STOW, RELEASE ENQUEUES
         SPACE 1
         B     *+4(R5)             PROCESS RETURN CODE
         B     REST897               00 - ADDED CORRECTLY
         B     MEMEXIST              04 - NAME ALREADY IN THE DIRECTORY
         EX    0,*                   08 - SHOULD NOT HAPPEN WITH ADD
         B     FULLDIR               12 - DIRECTORY IS ALREADY FULL
         B     IOERROR               16 - I/O ERROR IN THE DIRECTORY
         SPACE 1
REST897  MVC   INSERT#1(8),DIRNAME   RESTORED MEMBER
         $TMSG L091$1
         SPACE 1
         TM    SPFLAG2,SPFLNCD            LINE COMMAND?        SS AUG85
         BO    REST89D                    YES, BRANCH
         SPACE 1
         MVC   MEMBERD+1(2+8+8+2),LMEMBER1          DEFAULT MEMBER1
         MVC   MEMBERD+1+2+8(8),LMEMBER1+2          DEFAULT MEMBER2
         MVC   MEMBERD+1+2+8+8(2),LMEMBER1          DEFAULT LENGTH
         MVI   MEMBERD,FMEMBER1                     ASSUME ONE MEMBER
         NI    PMEMMIN,FF-X'80'                     NO MEMBER LIST NOW
         TM    #RESFLAG,#RESFREP                    REPEAT?
         BZ    *+8                                  NO, BRANCH
         OI    MEMBERD,FMEMBER2+FMEMRANG+FMEM#MEM   MEMBER RANGE
         BAL   R14,DEFGROUP                    ADD DEFAULT GROUP
         SPACE 2
REST89D  TM    FLAGSCC,RECFMU         LOAD LIBRARY?
         BNO   REST899                NO, DONE WITH THIS MEMBER
         SPACE 1
         UNPK  INSERT#1(7),DIREPA(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         MVC   INSERT#2(8),ENTRYPT    NAME OF THE ENTRY POINT
         TR    INSERT#2(8),TRLINE     MAKE PRINTABLE
         LA    R1,L102$1              ASSUME NONE FOUND
         CLI   ENTRYPT,C'?'           ANY FOUND?
         BE    *+8                    NO, BRANCH
         LA    R1,L103$2              YES, SHOW ENTRY SYMBOL
         $TMSG (R1)
         SPACE 1
         UNPK  INSERT#1(7),DIRSTORE(4)
         TR    INSERT#1(6),TRTABLE
         MVC   INSERT#1+6(2),BLANKS
         ICM   R1,B'0111',DIRSTORE    ACTUAL MODULE LENGTH
         LA    R1,1023(,R1)           NEXT HIGHER
         SRL   R1,10                             1K VALUE
         CVD   R1,DOUBLE
         MVC   INSERT#2(8),=X'402020202120D2404040'
         ED    INSERT#2(6),DOUBLE+5
         $TMSG L104$2
         B     REST899
         SPACE 1
REST898  $TMSG (R1)
         SPACE 2
REST899  TM    #RESFLAG,#RESFREP   REPEAT OPTION?
         BNO   NEWCMD              NO, DONE
         TM    #RESFLAG,#RESFPRO*2+#RESFPRO*4  NOPROMPT OR NO.. GLOBAL?
         BZ    REST247                         NO, BRANCH
         TM    #RESFLAG,#RESFPRO   PROMPT OPTION?
         BO    REST247             YES, PROCESS THE NEXT DELETED MEMBER
         $MESAGE MSGBLANK          ONE BLANK LINE
         $MESAGE MSGBLANK          ONE BLANK LINE
         B     REST247             PROCESS THE NEXT DELETED MEMBER
         SPACE 1
RESTLFF8 DC    0F'0',X'FFFFFFF8'
RESTLDOT DC    C' ...'
RESTLTMP DC    C'TEMP'
MSGRESTO DC    C'CSECTS ARE:'
         TITLE 'P D S  --  PDS SUBLIST                        5/12/86'
***********************************************************************
***      SUBLIST SUBCOMMAND - ADDED BY BRUCE LELAND 4/14/85         ***
***********************************************************************
*
         SPACE 1
SUBLIST  CSECT
         USING *,R8
         LA    R1,L530                    ASSUME NON-PARTITIONED
         TM    DSORG,DS1DSGPO             CORRECT?
         BZ    MSGNEW                     YES, BRANCH
         SPACE 1
         TM    FLAGSEE,FMEMGRP            MEMBER GROUP DEFINE START?
         BO    SUBL080                    YES, BRANCH
         SPACE 1
         OI    FLAGSEE,FMEMGRP            FIRST SUBLIST ELEMENT
         MVC   MMEMMAX(4),MMEMMIN         POINT TO THE FIRST ELEMENT
         XC    MMEMBERD(1+2+8+8+2),MMEMBERD  CLEAR THE MEMBER NAME
         MVC   MMEMBERD+1+2(8),DIRNAME    MEMBER NAME
         MVC   MMEMBERD+1+2+8+8+2(3),DIRTTR TTR OF FIRST MEMBER
         LA    R1,DIRNAME+7               END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
SUBL050  CLI   0(R1),X'40'                BLANK?
         BNE   SUBL060                    NO, BRANCH
         BCTR  R1,0                       PREVIOUS CHARACTER
         BCT   R0,SUBL050                 CHECK ALL BYTES
SUBL060  STCM  R0,B'1100',MMEMBERD+1      SAVE THE MACHINE LENGTH
         MVI   MMEMBERD,FMEMBER1          A SINGLE MEMBER NAME
         NI    MMEMMIN,FF-X'80'           MEMBER LIST IS INACTIVE
         SPACE 1
         ICM   R1,B'0111',MMEMMIN+1       FIRST ELEMENT POINTER?
         BZ    NEWCMD                     NO, DONE
         MVI   0(R1),0                    YES, INACTIVE ELEMENT
         B     NEWCMD
         SPACE 2
SUBL080  CLI   #ALIAOPT,X'80'+1           ALIAS PHASE?
         BNE   SUBL100                    NO, BRANCH
         MVI   STARTTR+2,X'01'            START OF DIRECTORY
         SPACE 1
SUBL090  CLI   #ALIAOPT,X'80'+1           ALIAS PHASE?
         BNE   NEWCMD                     NO, BRANCH
         BAL   R14,READDIR                GET THE NEXT MEMBER
         B     NEWCMD                     LAST MEMBER
         SPACE 1
         MVC   DIRNAME(12),MEMNAME        THIS MEMBER NAME
         LA    R1,MMEMMIN                 ANCHOR FOR MEMBER LIST
         CLC   DIRTTR(3),MMEMBERD+1+2+8+8+2  SAME TTR?
         BNE   SUBL092                       NO, BRANCH
         CLC   DIRNAME(8),MMEMBERD+1+2       FIND MYSELF?
         BE    SUBL090                       YES, BRANCH
         B     SUBL100                       YES, BRANCH
         SPACE 2
SUBL092  L     R1,0(,R1)                  NEXT ELEMENT
         TM    0(R1),X'80'                ACTIVE ELEMENT?
         BNO   SUBL090                    NO, NOT FOUND
         CLC   DIRTTR(3),4+2+8+8+2(R1)    SAME TTR?
         BNE   SUBL092                    NO, BRANCH
         CLC   DIRNAME(8),4+2(R1)         FIND MYSELF?
         BE    SUBL092                    YES, BRANCH
         SPACE 2
SUBL100  L     R1,MMEMMAX                 CURRENT END POINTER
         CLC   1(3,R1),ZERO               ANY SLOT ENTRIES LEFT?
         BNE   SUBL180                    YES, BRANCH
         LA    R0,36*28                   36 MEMBERS, 28 WIDE EACH
         ICM   R0,B'1000',=AL1(24)        ASSUME SPF IS INITIALIZED
         STCM  R0,B'1000',SUBLIST2        MARK FOR LATER RELEASE
         TM    FLAGSFF,FSPFCALL+FSPFDIAL  SPF INITIALIZED?
         BM    SUBL110                    YES, BRANCH
         AIF   ('&CISP' NE 'ISPFV2').NSPFMEM
         TM    FLAGSFF,FSPFOPT6           CALLED FROM OPTION 6?
         BO    SUBL110                    YES, BRANCH
.NSPFMEM ANOP
         ICM   R0,B'1000',=AL1(23)        SPF IS NOT INITIALIZED
         STCM  R0,B'1000',SUBLIST1        MARK FOR LATER RELEASE
         MVI   SUBLIST2,0                 CLEAR THE EARLIER MARK
SUBL110  GETMAIN R,LV=(0)                 GET MORE ELEMENT STORAGE
         LR    R15,R1                     START OF TABLE
         L     R1,MMEMMAX                 CURRENT ELEMENT
         LA    R0,36-1                    NUMBER OF ELEMENTS -1
SUBL136  LA    R15,28(,R15)               NEXT ELEMENT POINTER
         ST    R15,0(,R1)                 POINT TO THE NEXT ELEMENT
         LR    R1,R15                     NEXT ELEMENT
         BCT   R0,SUBL136                 DO 36 ELEMENTS
         XC    0(4,R1),0(R1)              CLEAR THE LAST POINTER
         SPACE 2
SUBL180  LA    R1,MMEMMIN                 ANCHOR FOR MEMBER LIST
         CLC   DIRNAME(8),MMEMBERD+1+2    ALREADY IN LIST?
         BE    SUBL090                    YES, DO NOT ADD AGAIN
         SPACE 2
SUBL190  L     R1,0(,R1)                  NEXT ELEMENT
         TM    0(R1),X'80'                ACTIVE ELEMENT?
         BNO   SUBL200                    NO, NOT FOUND
         CLC   DIRNAME(8),4+2(R1)         ALREADY IN LIST?
         BE    SUBL090                    YES, DO NOT ADD AGAIN
         B     SUBL190                    SEARCH THE ENTIRE LIST
         SPACE 2
SUBL200  L     R1,MMEMMAX                 CURRENT ADDED ELEMENT
         L     R15,0(,R1)                 NEXT POINTER
         MVI   0(R15),0                   INACTIVE THE NEXT ELEMENT
         ST    R15,MMEMMAX                NEXT ELEMENT TO ADD
         OI    0(R1),X'80'                ACTIVE ELEMENT
         OI    MMEMMIN,X'80'              MEMBER LIST IS ACTIVE
         XC    4(2+8+8+2,R1),4(R1)        CLEAR THE MEMBER NAME
         MVC   4+2(8,R1),DIRNAME          MEMBER NAME
         MVC   4+2+8+8+2(3,R1),DIRTTR     ADD THIS TTR
         LA    R15,DIRNAME+7              END OF MEMBER NAME
         LA    R0,7                       MACHINE LENGTH
SUBL250  CLI   0(R15),X'40'               BLANK?
         BNE   SUBL260                    NO, BRANCH
         BCTR  R15,0                      PREVIOUS CHARACTER
         BCT   R0,SUBL250                 CHECK ALL BYTES
SUBL260  STC   R0,5(R1)                   SAVE THE MACHINE LENGTH
         MVI   4(R1),FMEMBER1             A SINGLE MEMBER NAME
         B     SUBL090
         TITLE 'P D S  --  PDS TIME                           5/12/86'
***********************************************************************
***      TIME  SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
TIME     CSECT
         USING *,R8
         MVC   MSGTEXT2(132),MSGBL132    BLANK LINE
         MVC   MSGTEXT2+4(14),TIMETBL    EDIT MASK
         TIME  DEC
         CL    R0,TIMEL12A                 MORNING?
         BL    *+12                      YES, BRANCH
         MVI   MSGTEXT2+14,C'P'          NO, SET TO PM
         SL    R0,TIMEL12A               NO, SUBTRACT 12 HOURS
         ST    R0,TIMEWRKW               ADJUSTED TIME
         TM    TIMEWRKW,X'08'            ADJUSTMENT ERROR (20 OR 21)?
         BZ    *+8                       NO, BRANCH
         NI    TIMEWRKW,X'09'            YES, CONVERT TO 8 OR 9 (PM)
         CLI   TIMEWRKW,X'00'            ZERO HOURS?
         BNE   *+8                       NO, BRANCH
         MVI   TIMEWRKW,X'12'            YES, CONVERT ZERO TO 12
         ED    MSGTEXT2+4(9),TIMEWRKW    FINISH TIME PROCESSING
         MVC   MSGTEXT2+34(25),DATEPC    TIME CONSTANT
         ST    R1,TIMEWRKW               SAVE 00YYDDDF DATE WORD
         UNPK  MSGTEXT2+44(5),TIMEWRKW+1(3) MOVE IN "YY"
         MVC   MSGTEXT2+51(2),MSGTEXT2+44 (YY.
         MVC   MSGTEXT2+54(3),MSGTEXT2+46 DDD)
         OI    MSGTEXT2+56,X'F0'         FIX THE ZONE
         MVC   MSGTEXT2+46(3),BLANKS     BLANK THE NEXT THREE BYTES
         LH    R1,TIMEWRKW+2
         SRL   R1,12                     D..  (HUNDREDS)
         M     R0,TIMEL10
         IC    R15,TIMEWRKW+2
         N     R15,=F'15'
         AR    R1,R15                    .D.  (TENS)
         M     R0,TIMEL10
         IC    R15,TIMEWRKW+3
         SRL   R15,4
         AR    R1,R15                    ..D  (UNITS)
         PACK  TIMEDOUB(8),MSGTEXT2+44(2)
         CVB   R15,TIMEDOUB              CONVERT YEAR TO BINARY
         LR    R0,R15
         N     R15,TIMELFFC              YEAR/4*4  (INTEGER DIVISION)
         CR    R0,R15                    LEAP YEAR?
         BE    NOADJUST                  YES, NO ADJUSTMENT NEEDED
         CH    R1,CUMDAYS+4              BEFORE FEB 30?
         BL    NOADJUST                  YES, NO ADJUSTMENT NEEDED
         AH    R1,=H'1'                  ADD 1 TO THE JULIAN DATE
NOADJUST SR    R15,R15
         AH    R15,=H'2'
         CH    R1,CUMDAYS(R15)
         BH    *-8
         SH    R1,CUMDAYS-2(R15)
         LR    R14,R15
         SRL   R15,1                     MONTH IN BINARY
         LA    R14,0(R14,R15)
         LA    R14,MONTHS-3(R14)
         MVC   MSGTEXT2+34(3),0(R14)     MONTH TEXT
         AR    R15,R15
         LA    R15,CONVTBL(R15)
         MVC   TIMEDOUB+4(2),0(R15)      MOVE IN MONTH
         LR    R15,R1                    DAY OF MONTH IN BINARY
         AR    R15,R15
         LA    R15,CONVTBL(R15)
         MVC   MSGTEXT2+38(2),0(R15)     MOVE IN DAY OF MONTH
         MVC   TIMEDOUB(4),MSGTEXT2+42   MOVE IN '19XX'
         MVC   TIMEDOUB+6(2),MSGTEXT2+38 MOVE IN DAY OF MONTH
         BAL   R14,WEEKDAY               FIND THE DAY OF WEEK
         L     R15,TIMEWRKW
         SLL   R15,4
         SRL   R15,28
         MH    R15,=H'10'
         LA    R15,DAY@WEEK(R15)
         MVC   MSGTEXT2+22(10),0(R15)
         MVI   MSGTEXT2+1,79
         $MESAGE MSGTEXT2
         B     NEWCMD
         SPACE 4
DAY@WEEK DC    CL10'SUNDAY',CL10'MONDAY',CL10'TUESDAY',CL10'WEDNESDAY'
         DC    CL10'THURSDAY',CL10'FRIDAY',CL10'SATURDAY'
         SPACE 2
CONVTBL  DC    CL32' 0 1 2 3 4 5 6 7 8 9101112131415'
         DC    CL32'16171819202122232425262728293031'
         SPACE 2
TIMETBL  DC    XL14'4021204B20204B202040C1D44040'
         SPACE 2
DATEPC   DC    CL26'ABC EF, 19GH    (12.456)'
         SPACE 2
CUMDAYS  DC    H'0,31,60,91,121,152,182,213,244,274,305,335,32000'
         SPACE 2
MONTHS   DC    CL36'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*     THIS REENTRANT SUBROUTINE DETERMINES THE DAY OF THE             *
*   WEEK FOR ANY GREGORIAN DATE FROM OCTOBER 15,1582, THROUGH         *
*   FEBRUARY 28, 4000.                                                *
*                                                                     *
*         NOTE:  DATE IS THE ARGUMENT AND DAY IS THE ANSWER.          *
*           THEY HAVE THE FOLLOWING FORMATS, RESPECTIVELY -           *
*                                                                     *
*TIMEDOUB DS   0ZL8     ARGUMENT                                      *
*TIMEINYR DS   ZL4      YEAR - ANY VALUE 1582-4000                    *
*TIMEINMT DS   ZL2      MONTH - ANY VALUE 01-12                       *
*TIMEINDY DS   ZL2      DAY OF MONTH - ANY VALUE 01-31                *
*TIMEWRKW DS   ZL1      DAY OF WEEK -  0 (SUNDAY) - 6 (SATURDAY)      *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
WEEKDAY  ST    R14,R14SAVE
         XC    TIMEDDAT(8),TIMEDDAT
         PACK  TIMEDECC,TIMEINDY
         CVB   R1,TIMEDDAT
         PACK  TIMEDECM,TIMEINMT
         CVB   R15,TIMEDDAT
         IC    R15,FFTBL-1(R15)      FIND FUDGE FACTOR
         AR    R1,R15                   AND APPLY IT TO CORRECTION
         PACK  TIMEDECC,TIMEINYR
         SP    TIMEDECC,TIMELP3      ADJUST YEAR IF JANUARY OR FEBRUARY
         OI    TIMEYRSG,15           LEGITIMIZE SIGN FOR ZAP
         ZAP   TIMEDECC,TIMEDECY
         CVB   R15,TIMEDDAT
         SR    R14,R14
         LA    R0,3                  USED TO FIND CENTURY MOD 4
         D     R14,=F'100'           FIND CENTURY
         NR    R15,R0                CENTURIES MOD 4
         NR    R0,R14                THIS MANY COMMON YEARS
         SRA   R14,2                   AND
         AR    R15,R14                   THIS MANY LEAP YEARS
         MH    R15,=H'5'
         AR    R1,R15
         AR    R1,R0
         SR    R1-1,R1-1             PREPARE TO DIVIDE
         D     R1-1,TIMEL7
         STC   R1-1,TIMEWRKW
         OI    TIMEWRKW,X'F0'        ADD A ZONE FIELD
         L     R14,R14SAVE
         BR    R14                   AND RETURN
         SPACE 1
FFTBL    DC    FL1'0,3,2,5,0,3,5,1,4,6,2,4'  FUDGE FACTORS
TIMEL10  DC    F'10'
TIMEL7   DC    F'7'
TIMEL12A DC    X'12000000'
TIMELFFC DC    X'FFFFFFFC'
TIMELP3  DC    P'3'
         TITLE 'P D S  --  PDS TSO                            5/12/86'
***********************************************************************
***      TSO   SUBCOMMAND      ADDED BY BRUCE LELAND -- AUG., 1982  ***
***********************************************************************
*
         SPACE 1
TSO      CSECT
         USING *,R8
         XC    MSGTEXT2(32),MSGTEXT2
TSO000   L     R1,ADDRCBUF                 POINT TO THE COMMAND BUFFER
         ST    R1,ADDRTEXT
         MVC   MSGTEXT2(4),0(R1)           COMMAND BUFFER LENGTHS
         LA    R1,PARMLIST                 USE THE SAME PARAMETER LIST
         L     R15,ADDRSCAN                SCAN START ADDRESS
         BALR  R14,R15                     INVOKE COMMAND SCAN
         SPACE 1
         LA    R5,SCANANSR                       ADDR OF ANSWER AREA
         TM    CSOAFLG-CSOA(R5),CSOANOC          EMPTY BUFFER?
         BO    TSO090                            YES, NO TSO COMMAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP+CSOAVNP  VALID COMMAND?
         LA    R1,L874                           ASSUME NOT
         BZ    MSGNEW                            NO, BRANCH
         L     R2,CSOACNM-CSOA(,R5)              ADDR OF COMMAND NAME
         LH    R3,CSOALNM-CSOA(,R5)              LENGTH OF NAME
         BCTR  R3,0                              MACHINE LENGTH
         SPACE 1
         MVC   DIRNAME,BLANKS              INITIALIZE WITH BLANKS
         MVC   DIRNAME(*-*),0(R2)          <<EXECUTED>>
         EX    R3,*-6                      MOVE IN THE COMMAND NAME
         SPACE 1
         L     R1,ADDRCBUF                 COMMAND BUFFER ADDRESS
         ST    R1,ADDRTEXT                 COMMAND ADDRESS
         SPACE 1
TSO005   CLC   DIRNAME(5),TSOLTIME         TIME REQUEST?
         BNE   *+10                        NO, BRANCH
         MVC   DIRNAME(8),TSOLIKJ          YES, USE IKJEFT25 INSTEAD
         SPACE 1
         CLC   DIRNAME(2),TSOLH            HELP REQUEST?
         BE    TSO010                      YES, BRANCH
         CLC   DIRNAME(5),TSOLHEL          HELP REQUEST?
         BNE   TSO020                      NO, BRANCH
TSO010   MVC   DDNAMEH,DDNAME              SAVE ACTUAL DDNAME
         MVC   DDNAME,SYSHELP              DDNAME TO FREE
         BAL   R2,DEALLOCZ                 FORCE A FREE OF SYSHELP
         MVC   DDNAME,DDNAMEH              RESTORE ACTUAL DDNAME
         SPACE 1
TSO020   ICM   R15,B'1111',=V(VTSOCMD)     VERIFY THE TSO COMMAND?
         BZ    TSO024                      NO, BRANCH
         OI    FLAGSJJ,FTSOCMD             YES, SET A ESTAE FLAG
         MVC   MSGTEXT1(16),ADDRTEXT       MOVE ARGUMENT LIST
         LA    R2,DIRNAME                  COMMAND NAME
         ST    R2,MSGTEXT1+16              FOR VERIFICATION ROUTINE
         OI    MSGTEXT1+16,X'80'           MARK END OF LIST
         LA    R1,MSGTEXT1                 START OF ARGUMENT LIST
         BALR  R14,R15                     CALL VERIFICATION ROUTINE
         NI    FLAGSJJ,FF-FTSOCMD          RESET THE ESTAE FLAG
         LTR   R15,R15                     AUTHORIZED?
         BZ    TSO024                      YES, BRANCH
         CH    R15,=H'8'                   RETURN CODE 8?
         BNE   TSO022                      NO, BRANCH
         OI    CSOAFLG-CSOA(R5),CSOAEXEC   IMPLIED CLIST
         B     TSO024
TSO022   MVC   INSERT#1(8),DIRNAME         COMMAND NAME
         $TMSG L910$1                      NOT AUTHORIZED
         B     TSO090                      BYPASS THIS COMMAND
         SPACE 1
TSO024   L     R4,ADDRECT                  ECT ADDRESS
         OI    ECTSWS-ECT(R4),ECTNOPD      ASSUME NO OPERAND
         TM    CSOAFLG-CSOA(R5),CSOAVWP    ANY OPERAND?
         BNO   *+8                         NO, BRANCH
         NI    ECTSWS-ECT(R4),FF-ECTNOPD   YES, ZAP THE NO OPERAND FLAG
         TM    CSOAFLG-CSOA(R5),CSOAEXEC   IMPLIED CLIST?
         BO    TSO030                      YES, BRANCH
         SPACE 2
*** MODULE NOT AN IMPLIED CLIST, TRY FOR A LINKLIST LIBRARY
         BLDL  0,BLDLLIST                  CHECK FOR A LOAD MODULE
         LTR   R15,R15                     IN THE LINKLIST?
         BZ    TSO040                      YES, ATTACH IT
         L     R15,CVTPTR                  NEED TO ALSO CHECK LPALIB
         L     R15,CVTLPDIA-CVTMAP(R15)    POINT TO LINK PACK DIRECTORY
         SPACE 2
*** MODULE NOT IN LINKLIST, TRY THE LPALIB DIRECTORY (IN STORAGE)
TSO026   CLC   8(8,R15),DIRNAME            THIS MODULE NAME?
         BE    TSO040                      YES, FOUND
         CLI   8(R15),X'FF'                END OF DIRECTORY?
         LA    R15,40(,R15)
         BNE   TSO026                      NO, BRANCH
         SPACE 2
*** MODULE NOT FOUND ANYWHERE, TRY IT AS AN IMPLIED CLIST
TSO030   MVC   DIRNAME(8),#EXE             INVOKE CLIST PROCESSOR
         L     R15,ADDRCBUF                COMMAND LINE
         MVC   0(4,R15),MSGTEXT2           RESET THE OPERAND OFFSET
*        NI    ECTSWS-ECT(R4),FF-ECTNOPD   ZAP THE NO OPERAND FLAG
         SR    R1,R1
         ICM   R1,B'0011',2(R15)           OFFSET TO OPERAND
         BZ    TSO040                      NULL RETURN
         STCM  R1,B'1100',2(R15)           CLEAR THE OFFSET
TSO034   LA    R15,1(,R15)                 NEXT BYTE
         MVI   3(R15),X'40'                CLEAR THE BYTE
         BCT   R1,TSO034                   REPEAT FOR ALL CHARACTERS
         SPACE 1
*** MODULE WAS FOUND, WE CAN ATTACH IT DIRECTLY
TSO040   MVC   ECTPCMD-ECT(8,R4),DIRNAME   COMMAND NAME
         MVC   ECTSCMD-ECT(8,R4),BLANKS    NO SUBCOMMAND
         SPACE 1
         CLI   ##SUBCAL,X'40'              TSOEXEC-TYPE PROCESSOR?
         BE    TSO050                      NO, BRANCH
         CLC   ECTPCMD-ECT(8,R4),#EXE      CLIST PROCESSOR?
         BE    TSO050                      YES, BRANCH
         LA    R3,##SUBCAL                 PDS EXTERNAL SUBCOMMAND
         NI    ECTSWS-ECT(R4),FF-ECTNOPD   ZAP THE NO OPERAND FLAG
         MVC   ECTPCMD-ECT(8,R4),##SUBCAL  COMMAND NAME IS TSOEXEC
         L     R15,ADDRCBUF                COMMAND BUFFER ADDRESS
         MVC   0(4,R15),MSGTEXT2           RESTORE THE COMMAND OFFSETS
         MVC   DIRNAME(8),##SUBCAL         INVOKE TSOEXEC
         SPACE 1
TSO050   L     R1,ADDRTEXT                 CPPL START
*        $MESAGE (R1)                      DELETE * FOR OUTPUT
         LA    R1,ADDRTEXT                 CPPL START
         MVI   ATTNECB,0                   CLEAR ATTENTION ECB
         LA    R3,DIRNAME                  PDS EXTERNAL SUBCOMMAND
         BAL   R2,ATTACH                   GO ATTACH IT
         SPACE 1
TSO090   ICM   R1,B'1111',MSGTEXT2+4+4     ANY GETLINE BUFFER?
         BZ    TSO092                      NO, BRANCH
         LH    R0,0(,R1)                   LENGTH
         ICM   R0,B'1000',=X'01'           SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
TSO092   L     R4,ADDRECT
         L     R1,ECTIOWA-ECT(R4)          --> I/O WORK AREA
         L     R1,0(,R1)                   --> ELEMENT ON STACK
         TM    0(R1),X'40'                 FROM CLIST?
         BZ    NEWSTAX                     NO, BRANCH
         SPACE 1
         LA    R1,MSGTEXT2+12              I/O PARM LIST OFR GETLINE
         L     R2,ADDRUPT                  UPT FOR GETLINE
         L     R3,ADDRECT                  ECT FOR GETLINE
         L     R4,MSGTEXT2+12+16           ECB FOR GETLINE
         L     R15,ADDRGETL                ADDRESS OF GETLINE
         MVC   MSGTEXT2+4(TSO099),TSO098   INITIALIZE GETLINE PARMS
         GETLINE PARM=MSGTEXT2+4,UPT=(R2),ECT=(R3),ECB=(R4),          XX
               ENTRY=(15),MF=(E,(1))
         CH    R15,=H'16'                  END OF INPUT?
         BE    NEWSTAX                     YES, BRANCH
         L     R1,MSGTEXT2+4+4             NEXT COMMAND
         ST    R1,ADDRCBUF
         SPACE 2
         LA    R15,PARMLIST
         USING CSPL,R15
         SPACE 1
         L     R1,ADDRUPT              ADDRESS OF THE UPT
         L     R2,ADDRECT              ADDRESS OF THE ECT
         LA    R3,ATTNECB              ADDRESS OF THE ATTENTION ECB
         LA    R4,ZERO                 ADDRESS OF A ZERO FLAG AREA
         LA    R5,SCANANSR             ADDRESS OF THE RESULT AREA
         L     R6,ADDRCBUF             ADDRESS OF THE SUBCOMMAND
         STM   R1,R6,CSPLUPT           SETUP CSPLUPT, CSPLECT, CSPLECB,
         DROP  R15                           CSPLFLG, CSPLOA & CSPLCBUF
         B     TSO000
         SPACE 1
TSO098   GETLINE MF=L
TSO099   EQU   *-TSO098
* MSGTEXT2+00,4   COMMAND BUFFER START HALFWORDS
* MSGTEXT2+04,8   GETLINE I/O AREA
* MSGTEXT2+12,16  GETLINE I/O PARM LIST
* MSGTEXT2+28,4   GETLINE ECB
TSOLTIME DC    C'TIME '
TSOLIKJ  DC    C'IKJEFT25'
TSOLH    DC    C'H '
TSOLHEL  DC    C'HELP '
         TITLE 'P D S  --  PDS TSOEDIT AND FSE                5/12/86'
***********************************************************************
***      TSOEDIT AND FSE SUBCOMMANDS                                ***
***********************************************************************
*
         SPACE 1
TSOEDIT  CSECT
         USING *,R8
         MVC   DSNMEMQ(8),DIRNAME       MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST              MEMBER IN USE?
         NOP   0                        IF YES, OUTPUT A WARNING ONLY
         CLI   #EDITOLD,X'01'           "NEW" SPECIFIED?
         BNE   TSOED5                   NO, BRANCH
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    TSOED5                   YES, BRANCH
         BLDL  INDCB,BLDLLIST           INSURE IT DOES NOT EXIST
         SPACE 1
         B     *+4(R15)                 PROCESS RETURN CODE
         B     MEMEXIST                   00 - MEMBER IS NOT NEW
         B     TSOED5                     04 - MEMBER DOES NOT EXIST
         B     IOERROR                    08 - I/O ERROR IN DIRECTORY
         SPACE 1
TSOED5   MVC   MSGTEXT2+4(8),##SUBCOM   MOVE IN "EDIT" OR "FSE"
         MVI   MSGTEXT2+12,C''''        ADD A QUOTE
         MVC   MSGTEXT2+13(44),DSNAME   ADD THE DATA SET NAME
         LH    R15,DSNLEN               DSNAME ACTUAL LENGTH
         LA    R3,MSGTEXT2+12(R15)      POINTER TO CURRENT BYTE
         MVI   1(R3),C'('               MEMBER NAME PAREN
         SR    R5,R5                    LENGTH OF LAST QUALIFIER
         LR    R14,R3                   BACKWARD SCAN POINTER
         SPACE 1
TSOED6   BCTR  R14,0                    SCAN
         LA    R5,1(,R5)                    TO
         CLI   0(R14),C'.'                    PREVIOUS
         BNE   TSOED6                                 PERIOD
         MVC   MSGTEXT1+4(9),1(R14)
         TM    DSORG,DS1DSGPO           NON-PARTITIONED DATA SET?
         BZ    TSOED8                   YES, BRANCH
         MVC   2(8,R3),DIRNAME          MEMBER NAME
         LA    R3,2+8(,R3)              END OF MEMBER +1
         LA    R1,8                     MAXIMUM MACHINE LENGTH+1
         SPACE 1
TSOED7   BCTR  R3,0                     SCAN
         CLI   0(R3),X'40'                  BACKWARDS
         BNE   *+8                                   FOR FIRST
         BCT   R1,TSOED7                                      NON-BLANK
         SPACE 1
         BCTR  R1,0                     MACHINE LENGTH
         TRT   DIRNAME(*-*),TRTMEM      <<EXECUTED>>
         EX    R1,*-6                   VALID MEMBER NAME?
         BNZ   BADMEMB                  NO, ERROR
         CLI   DIRNAME,C'0'             VALID FIRST CHARACTER?
         BNL   BADMEMB                  NO, BRANCH
         LA    R3,1(,R3)                POINT TO TERMINATOR
         MVI   0(R3),C')'               ADD A CLOSING PARENTHESIS
         SPACE 1
TSOED8   MVI   1(R3),C''''              ADD A QUOTE
         MVI   2(R3),X'40'              ADD A BLANK
         LA    R3,3(,R3)
         CLI   #EDITNON,1               NONUM CODED?
         BNE   TSOED9                   NO, BRANCH
         SPACE 2
         MVC   0(6,R3),TSOELNON         YES
         LA    R3,6(,R3)
TSOED9   CLI   #EDITOLD,1               "NEW" SPECIFIED?
         BNE   TSOED10                  NO, BRANCH
         MVC   0(4,R3),TSOELNEW         YES
         LA    R3,4(,R3)
TSOED10  CLI   #EDITASI,1               ASIS CODED?
         BNE   TSOED11                  NO, BRANCH
         MVC   0(5,R3),TSOELASI         YES
         LA    R3,5(,R3)
TSOED11  ICM   R15,B'1111',#EDITTYP     ANY DATA TYPE CODED?
         BZ    TSOED13                  NO, BRANCH
         MH    R15,=H'7'                INDEX  =  OPTIONUM * 7
         LA    R15,EDITTYPE-7(R15)      POINT TO THE PROPER OPTION
         B     TSOED20                  DONE WITH FORMATTING
         SPACE 1
TSOED13  BCTR  R5,0                     MACHINE QUALIFIER LENGTH
         LA    R15,EDITTYPE-7
TSOED15  LA    R15,7(,R15)              TABLE WIDTH IS 7
         CLC   PLIF,0(R15)              END OF DESCRIPTIVE QUALIFIERS?
         BE    TSOED17                  YES, USE "DATA" OR "GOFORT"
         CLC   0(*-*,R15),MSGTEXT1+4    <<EXECUTED>>
         EX    R5,*-6                   THIS QUALIFIER?
         BNE   TSOED15                  NO, TRY THE NEXT ONE
         B     TSOED20                  YES, USE THIS QUALIFIER
         SPACE 1
TSOED17  LA    R15,EDITTYPE             NOT FOUND, ASSUME "DATA"
         CLC   TSOELFOR,MSGTEXT1+4      FORT QUALIFIER?
         BNE   TSOED20
         LA    R15,GOFORT               YES, USE "GOFORT" (EDIT RULES)
         SPACE 1
TSOED20  MVC   0(7,R3),0(R15)           MOVE IN DATA TYPE
         LA    R1,MSGTEXT2
         ST    R1,ADDRTEXT              COMMAND ADDRESS
         LA    R3,7(,R3)
         SR    R3,R1                    COMMAND LENGTH
         SLL   R3,16
         ST    R3,MSGTEXT2              SAVE LENGTH OF STRING
*        $MESAGE MSGTEXT2               DELETE * TO OUTPUT THE COMMAND
         MVI   MSGTEXT2+3,8             OPERAND OFFSET
***TEST  BAL   R2,CLOSEIT               NEED TO CLOSE BEFORE THE EDIT
***TEST  MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
***TEST  LA    R0,RESTART2              ABEND RECOVERY ADDRESS
***TEST  ST    R0,RECOVER
         SPACE 1
         L     R4,ADDRECT
         MVC   ECTPCMD-ECT(8,R4),##SUBCAL      COMMAND IS EDIT OR FSE
         NI    ECTSWS-ECT(R4),FF-ECTNOPD       WE ALWAYS HAVE OPERANDS
         LA    R1,ADDRTEXT                     THE CPPL FOR EDIT/FSE
         LA    R3,##SUBCAL                     TSO EDIT/FSE COMMAND
         BAL   R2,ATTACH2                      GO ATTACH IT
         B     NEWCMD ***TEST
         MVI   VOLALLOC,X'40'           READ THE DSCB AGAIN
         SPACE 1
         MVI   ##ADRCM#,EDITOR          NO REPEATED WARNING MESSAGE
         L     R1,X'21C'                --> CURRENT TCB
         L     R1,X'00C'(,R1)           --> TIOT
         LA    R1,24(,R1)               TIOENTRY
TSOED97  CLI   0(R1),0                  END OF TIOT?
         BE    RESTART0                 YES, REALLOCATE AND OPEN AGAIN
         CLC   4(8,R1),DDNAME           THIS DDNAME?
         BE    RESTART2                 YES, JUST OPEN AGAIN
         SR    R15,R15
         IC    R15,0(,R1)
         LA    R1,0(R15,R1)
         B     TSOED97
TSOELNON DC    C'NONUM '
TSOELNEW DC    C'NEW '
TSOELASI DC    C'ASIS '
TSOELFOR DC    C'FORT('
         SPACE 2
**  THE FOLLOWING ORDERED TABLE MUST CORRESPOND WITH "PCLTYPE"
**
**     DATA SET TYPE VS. EDIT DATA TYPE RULES ARE FROM "HELP EDIT S"
**
EDITTYPE DC    CL7'DATA'
** INSERT INSTALLATION-ADDED EDIT DATA TYPES HERE AND IN "PCLTYPE"
         DC    CL7'LIST'      ADDED LOCALLY AS A VALID DATA SET TYPE
         DC    CL7'VSBASIC'   NEW VS BASIC
         DC    CL7'BASIC'
         DC    CL7'IPLI'
         DC    CL7'TEXT'
         DC    CL7'COBOL'
         DC    CL7'ASM'
         DC    CL7'CLIST'
         DC    CL7'CNTL'
         DC    CL7'PLI'
* END OF VALID DATA SET TYPES
PLIF     DC    CL7'PLIF'
         DC    CL7'FORTE'    *
         DC    CL7'FORTG'    *
         DC    CL7'FORTGI'   *  -- DATA TYPE .FORT
         DC    CL7'FORTH'    *        (DEFAULT EDIT TYPE IS GOFORT)
GOFORT   DC    CL7'GOFORT'   *
         TITLE 'P D S  --  PDS USAGE                          5/12/86'
***********************************************************************
***      USAGE SUBCOMMAND                                           ***
***********************************************************************
*
         SPACE 1
USAGE    CSECT
         USING *,R8
         L     R15,=A(DSNAMES)
         BALR  R2,R15                   FORMAT THE ALLOCATION STATUS
         SPACE 1
         MVC   INSERT#1(66),MSG180H
         MVI   MTHIGHL,56
         CLI   DS1SYSCD+8,X'FF'        ASM2 IDENTIFIER?
         BNE   *+8                     NO, BRANCH
         MVI   MTHIGHL,66              YES, USE 66 CHARACTERS
         $TMSG L180$1
         MVC   INSERT#1(66),BLANK128
         SR    R15,R15
         MVC   INSERT#1+10(7),USAL000
         ICM   R15,B'0111',DS1CREDT    DATA SET CREATION DATE NULL?
         BZ    US070                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+08(9),USAL4020
         ED    INSERT#1+08(9),MSGTEXT1
         SPACE 1
US070    SR    R15,R15
         MVC   INSERT#1+19(7),USAL000
         ICM   R15,B'0111',DS1EXPDT    DATA SET EXPIRATION DATE NULL?
         BZ    US080                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+17(9),USAL4020
         ED    INSERT#1+17(9),MSGTEXT1
         SPACE 1
US080    SR    R15,R15
         MVC   INSERT#1+29(7),USAL000
         ICM   R15,B'0111',DS1REFD     LAST OPEN DATE NULL?
         BZ    US090                   YES, BRANCH
         ST    R15,MSGTEXT1            SAVE FOR LATER
         SRL   R15,16                  YEAR ONLY
         MH    R15,=H'1000'            SHIFT FOR THE ADDITION
         AH    R15,MSGTEXT1+2          RESET DAYS
         CVD   R15,MSGTEXT1            STORE AS 00000000 00YYDDDF
         LA    R1,MSGTEXT1+5           POINT TO YYDDDF
         LA    R15,MSGTEXT1            WHERE RESULT GOES
         BAL   R14,CONVDATE            CONVERT TO MM/DD/YY
         MVC   INSERT#1+27(9),USAL4020
         ED    INSERT#1+27(9),MSGTEXT1
         SPACE 1
US090    MVC   INSERT#1+38(3),USALYES  ASSUME UPDATED
         TM    DS1DSIND,DS1DSCHA       CORRECT?
         BO    *+10                    YES, BRANCH
         MVC   INSERT#1+38(3),USALNO   NO, RESET THE DATA
         SPACE 1
         MVC   INSERT#1+47(4),=C'NONE' ASSUME NO SECURITY
         TM    DS1DSIND,X'40'+X'10'+X'04'  CORRECT?
         BZ    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALRACF ASSUME RACF DEFINED
         TM    DS1DSIND,X'40'          CORRECT?
         BO    US100                   YES, BRANCH
         MVC   INSERT#1+47(4),USALREAD ASSUME READ PROTECTED
         TM    DS1DSIND,X'10'+X'04'    CORRECT?
         BM    US100                   YES, BRANCH
         MVC   INSERT#1+47(5),USALWRIT   MUST BE WRITE PROTECTED
         SPACE 1
US100    MVC   INSERT#1+57(8),DS1SYSCD USERID IF ANY
         SPACE 1
         $TMSG L180$1
         $MESAGE MSGBLANK
         SPACE 2
         L     R1,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R14,NUMEXT               NUMBER OF EXTENTS
         SR    R0,R0
         SPACE 1
USEXTNT  AH    R0,32+14(,R1)            ADD NUMBER OF TRACKS IN EXTENT
         LA    R1,16(,R1)               NEXT EXTENT
         BCT   R14,USEXTNT              IF MORE EXTENTS
         STH   R0,DSNTOTAL              SAVE TOTAL D.S. SIZE IN TRACKS
         LH    R1,DS1LSTAR              GET TT OF LAST TRACK
         CLC   DS1LSTAR(3),ZERO         ANY ALLOCATION?
         BE    *+8                      NO, BRANCH
         LA    R1,1(,R1)                JUMP FOR COMPUTATION
         SR    R0,R1
         STH   R0,DSNEMPTY              TOTAL FREE SPACE
         SPACE 2
         MVC   INSERT#1(120),BLANK128
         MVI   MTHIGHL,120
         L     R4,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT               NUMBER OF EXTENTS
         LA    R2,INSERT#1-2           POSITION FOR FIRST DATA
         SPACE 1
US120    LH    R1,32+14(,R4)           TRACKS IN THIS EXTENT
         CVD   R1,DOUBLE
         MVC   MSGTEXT1(6),=X'402020202120'
         ED    MSGTEXT1(6),DOUBLE+5
         LA    R1,MSGTEXT1-1
         LA    R5,6
         SPACE 1
US130    LA    R1,1(,R1)               SCAN
         BCTR  R5,0                        FOR
         CLI   0(R1),X'40'                    FIRST
         BE    US130                               NON-BLANK
         SPACE 1
         MVI   0(R2),C','
         MVC   2(*-*,R2),0(R1)        <<EXECUTED>>
         EX    R5,*-6                 MOVE IN THE LENGTH TEXT
         LA    R2,2+1(R2,R5)          POSITION FOR NEXT COMMA
         LA    R4,16(,R4)             NEXT EXTENT START
         BCT   R3,US120               DO ALL EXTENTS
         SPACE 1
         $TMSG L181$1
         $MESAGE MSGBLANK
         SPACE 2
US200    MVI   MTHIGHL,38
         MVC   INSERT#1(38),MSG182H
         $TMSG L182$1
         MVC   INSERT#1(38),MSG182E
         SPACE 2
         LH    R1,DSNTOTAL            TOTAL DATA SET TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+11(6),DOUBLE+5
         SPACE 1
         LH    R1,DSNTOTAL            TOTAL DATA SET TRACKS
         SH    R1,DSNEMPTY            TOTAL DATA SET USED TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(6),DOUBLE+5
         SPACE 1
         LH    R1,DSNEMPTY            TOTAL DATA SET FREE TRACKS
         CVD   R1,DOUBLE
         ED    INSERT#1+23(6),DOUBLE+5
         SPACE 1
         LH    R1,NUMEXT              TOTAL DATA SET EXTENTS
         CVD   R1,DOUBLE
         ED    INSERT#1+34(4),DOUBLE+6
         SPACE 1
         $TMSG L182$1
         $MESAGE MSGBLANK
         SPACE 2
         TM    DSORG,DS1DSGPO         DSORG=PO?
         BNO   US300                  NO, BRANCH
         MVI   STARTTR+2,X'01'        TTR=000001 (START OF DIRECTORY)
         SPACE 1
         BAL   R14,READDIR            SCAN THROUGH THE DIRECTORY
         SPACE 1
         MVI   MTHIGHL,60
         MVC   INSERT#1(60),MSG183H
         $TMSG L183$1
         MVC   INSERT#1(60),MSG183E
         SPACE 1
         LH    R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+11(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTUSED             TOTAL USED DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+17(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTBLOCK            TOTAL DIRECTORY BLOCKS
         SH    R1,TOTUSED             TOTAL FREE DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+23(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTTRKS             TOTAL TRACKS FOR DIRECTORY BLOCKS
         CVD   R1,DOUBLE
         ED    INSERT#1+31(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTMEMR             TOTAL REAL ENTRIES
         AH    R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+40(6),DOUBLE+5
         SPACE 1
         LH    R1,TOTMEMA             TOTAL ALIAS ENTRIES
         CVD   R1,DOUBLE
         ED    INSERT#1+49(6),DOUBLE+5
         SPACE 1
         $TMSG L183$1
         $MESAGE MSGBLANK
         SPACE 2
US300    TM    FLAGSAA,FOPTIONS       ANY OPTION?
         BNO   NEWCMD                 NO, BRANCH
         MVI   MTHIGHL,70
         MVC   INSERT#1(70),MSG184H
         $TMSG L184$1
         MVC   INSERT#1(70),MSG184U
         $TMSG L184$1
         MVC   INSERT#1(70),BLANK128
         L     R2,DCBDEBAD-IHADCB+INDCB GET DEB ADDRESS
         LH    R3,NUMEXT
         SR    R4,R4                  EXTENT NUMBER
         SR    R5,R5                  BEGINNING TT OF TTR
         SPACE 1
US310    CVD   R4,DOUBLE              EXTENT
         A     R4,=F'1'
         MVC   INSERT#1+2(4),=X'40202120'
         ED    INSERT#1+2(4),DOUBLE+6
         L     R1,32(,R2)             UCB ADDRESS
         MVC   INSERT#1+8(3),13(R1)
         LH    R6,32+14(,R2)          TRACKS IN THIS EXTENT
         LR    R0,R5
         AR    R5,R6                  START OF NEXT EXTENT
         LA    R15,2                  LOOPS
         LA    R14,INSERT#1+16
         SPACE 1
US320    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US320
         SPACE 1
         CLI   INSERT#1+24,C'.'       SECOND TTR VALUE?
         BE    US330                  YES, BRANCH
         LA    R14,INSERT#1+22
         LA    R15,2
         LTR   R0,R5                  ANY DATA?
         BZ    US320                  NO, BRANCH
         BCTR  R0,0                   YES, REDUCE BY ONE
         B     US320
         SPACE 1
US330    MVI   INSERT#1+18,X'40'      CLEAR FIRST EXTRA PERIOD
         MVI   INSERT#1+24,X'40'      CLEAR SECOND EXTRA PERIOD
         CVD   R6,DOUBLE              TRACKS
         MVC   INSERT#1+26(6),=X'402020202120'
         ED    INSERT#1+26(6),DOUBLE+5
         SPACE 2
         ICM   R0,B'1111',38(R2)      FIRST DEVICE CC.CC.HH.HH
         LA    R15,4                  LOOPS
         LA    R14,INSERT#1+43
         SPACE 1
US420    SRDL  R0,4                   TT OF TTR
         SRL   R1,28                  LOW REGISTER POSITION
         MVI   2(R14),C'.'
         IC    R1,HEXTBL(R1)
         STC   R1,1(,R14)
         SRDL  R0,4
         SRL   R1,28
         IC    R1,HEXTBL(R1)
         STC   R1,0(,R14)
         SH    R14,=H'3'
         BCT   R15,US420
         SPACE 1
         CLI   INSERT#1+57,C'.'       SECOND TTR VALUE?
         BE    US430                  YES, BRANCH
         LA    R14,INSERT#1+55
         ICM   R0,B'1111',42(R2)      SECOND CC.CC.HH.HH FIGURE
         LA    R15,4
         B     US420
         SPACE 1
US430    MVI   INSERT#1+45,X'40'
         MVI   INSERT#1+57,X'40'
         MVC   INSERT#1+59(3),=C'CYL' ASSUME CYLINDER BOUNDARY
         CLC   40(2,R2),ZERO          FIRST HH.HH=0?
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         LH    R1,44(,R2)
         LA    R1,1(,R1)
         CH    R1,DEVTTRKS            END HH.HH=MAX/DEVICE
         BE    *+10                   YES, BRANCH
         MVC   INSERT#1+59(3),USALTRK NO, TRACK BOUNDARY
         SPACE 1
         $TMSG L184$1
         LA    R2,16(,R2)             NEXT EXTENT
         BCT   R3,US310               DO ALL EXTENTS
         SPACE 2
         $MESAGE MSGBLANK
         B     NEWCMD
         SPACE 3
MSG180H  DC    CL66'DATA SET: CREATED  EXPIRES  LAST USE  UPDATED  SECUX
               RITY  ASM2ID'
MSG182H  DC    CL38'TRACKS: ALLOCATED  USED  FREE  EXTENTS'
MSG182E  DC    CL38'                                      '
         ORG   MSG182E+11
         DC    X'402020202120'
         ORG   MSG182E+17
         DC    X'402020202120'
         ORG   MSG182E+23
         DC    X'402020202120'
         ORG   MSG182E+34
         DC    X'40202120'
         ORG   ,
MSG183H  DC    CL60'DIRECTORY: BLOCKS  USED  FREE  TRACKS  MEMBERS  ALIX
               ASES'
MSG183E  DC    CL60'                                            '
         ORG   MSG183E+11
         DC    X'402020202120'
         ORG   MSG183E+17
         DC    X'402020202120'
         ORG   MSG183E+23
         DC    X'402020202120'
         ORG   MSG183E+31
         DC    X'402020202120'
         ORG   MSG183E+40
         DC    X'402020202120'
         ORG   MSG183E+49
         DC    X'402020202120'
         ORG   ,
MSG184H  DC    CL70'EXTENT  UCB  LO TT-HI TT  TRACKS     LOW CCHH-HIGH X
               CCHH    BOUNDARY '
MSG184U  DC    CL70'------  ---  ----- -----  ------  ----------- -----X
               ------  -------- '
USAL000  DC    C'0/00/00'
USAL4020 DC    X'402021612020612020'
USALYES  DC    C'YES'
USALNO   DC    C'NO '
USALTRK  DC    C'TRK'
USALRACF DC    C'RACF'
USALREAD DC    C'READ'
USALWRIT DC    C'WRITE'
         PRINT GEN
         TITLE 'P D S  --  PDS VERIFY                   5/12/86'
***********************************************************************
***      VERIFY SUBCOMMAND     ADDED BY BRUCE LELAND --  MAY, 1983  ***
***********************************************************************
*
         SPACE 1
VERIFY   CSECT
         USING *,R8
         TM    FLAGSCC,RECFMF      FIXED FORMAT RECORDS?
         BNO   VERP00              NO, BRANCH
         LH    R15,BLKSI           BLKSIZE
         SR    R14,R14
         LH    R1,LRECL            LRECL
         LTR   R1,R1               ANY LRECL?
         BZ    VERP00              NO, AVOID DIVIDE ERROR
         DR    R14,R1              BLKSIZE/LRECL
         LTR   R14,R14             INTEGRAL NUMBER OF LRECL RECORDS?
         BZ    VERP00              YES, BRANCH
         $TMSG L482                NO, ERROR
         SPACE 2
VERP00   TM    DSORG,DS1DSGPO      PARTITIONED DATA SET?
         BNO   VERS00              NO, PERFORM OTHER GROUP OF CHECKS
         SPACE 1
         TM    FLAGSAA,FMEMBER1+FMEMBER2  ALL MEMBERS?
         BNZ   VERP02                     NO, BRANCH
         TM    FLAGSAA,FMEM#MEM           MEMBER GROUP?
         BNO   VERP03                     NO, BRANCH
         SPACE 2
* SINGLE MEMBER CHECK IS DESIRED
VERP02   OI    #VERUPDT,4+2               SET FLAG FOR LATER
         SR    R1,R1                      NO MESSAGE YET
         BAL   R2,VERMBR                  OUTPUT MEMBER IDENTIFIER
         LA    R4,#VERR4                  POINT TO A WORK AREA
         ST    R4,#MEMPTR                 ADD AT END OF THE TABLE
         SR    R3,R3                      NO TABLE ENTRIES
         B     VERP18                     SKIP INITIALIZATION
         SPACE 2
VERP03   LA    R1,L875             ASSUME NO DIRECTORY BLOCKS
         CLC   DS1LSTAR(3),ZERO    CORRECT?
         BE    MSGNEW              YES, BRANCH
         TM    #VERUPDT,2          NOUPDATE SPECIFIED?
         BO    VERP09              YES, DO NOT OPEN STOW
         BAL   R2,OPENSTOW         OPEN THE STOW DCB; ENQUEUE
         B     VERP08              COULD NOT OPEN -- ERROR
         SPACE 1
         MVI   MSGTEXT2,X'F9'          HIGH IN COLLATING SEQUENCE
         MVC   #DIRFIX+1(7),=C'FIXPDS' TEMPORARY STOW AND DELETE NAME
         MVI   MSGTEXT2+11,B'1001111'  ALIAS/MAX LENGTH ENTRY
         STOW  STOWDCB,MSGTEXT2,A      ADD THIS MEMBER
         SPACE 1
         B     *+4(R15)                PROCESS RETURN CODE
         B     VERP06                    00 - SUCCESSFUL
         B     VERP06                    04 - MEMBER ALREADY EXISTS
         EX    0,*                       08 - SHOULD NOT OCCUR
         B     VERP04                    12 - FULL DIRECTORY
         $TMSG L836                      16 - I/O ERROR IN DIRECTORY
         B     VERP08
         SPACE 1
VERP04   $TMSG L855                    DIRECTORY IS FULL
         B     VERP08                    12 - FULL DIRECTORY
         SPACE 2
VERP06   STOW  STOWDCB,MSGTEXT2,D      DELETE THIS MEMBER
         SPACE 1
         LTR   R15,R15                 ANY ERROR?
         BNP   VERP08                  NO, BRANCH
         $TMSG L836                    YES, I/O ERROR IN DIRECTORY
VERP08   BAL   R2,SHUTSTOW         CLOSE THE STOW DCB; DEQUEUE
         SPACE 2
VERP09   MVI   SUBPOOLT,21         TEMPORARY STORAGE IN USE
         MVI   STARTTR+2,X'01'     READ THE DIRECTORY
         LA    R5,1024/2           1024 MEMBERS INITIALLY
         SPACE 1
VERP10   SLL   R5,1                MEMBERS*2 FOR EACH LOOP
         SR    R6,R6
         LR    R3,R5
         LR    R0,R5
         SLL   R0,4                MEMBERS*16 IS TABLE SIZE
         ICM   R0,B'1000',SUBPOOLT
         GETMAIN R,LV=(0)          ALLOCATE A NEW MEMBER TABLE
         LR    R4,R1               NEW TABLE ADDRESS
         ICM   R0,B'1111',#MEMPTR  ANY PREVIOUS MEMBERS?
         BZ    VERP12              NO, BRANCH
         SRL   R3,1                YES, USE SECOND HALF OF TABLE
         LR    R14,R4              NEW TABLE START ADDRESS
         LR    R15,R5
         SLL   R15,3               MEMBERS*8 IS OLD TABLE SIZE
         LR    R6,R15              DISPLACEMENT TO NEW PART
         LR    R1,R15
         MVCL  R14,R0              PRESERVE THE PREVIOUS TABLE
         L     R1,#MEMPTR
         LR    R0,R6               LENGTH TO FREE
         ICM   R0,B'1000',SUBPOOLT SUBPOOL TO FREE
         FREEMAIN R,LV=(0),A=(1)
         SPACE 1
VERP12   ST    R4,#MEMPTR          MEMBER TABLE BASE
         AR    R4,R6               WHERE TO ADD MEMBERS
         SPACE 2
VERP14   BAL   R14,READDIR         GET THE NEXT MEMBER
         B     VERP50              END OF MEMBERS, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         SPACE 3
* CHECK FOR STANDARD MEMBER NAMES IF REQUIRED
         TM    #VERSTND,X'02'      STANDARD NAME CHECK?
         BNZ   VERP18              NO, BRANCH
         SPACE 1
         LA    R1,7
         LA    R14,DOUBLE+8
         MVC   DOUBLE(8),MEMNAME
         SPACE 1
VERP16   BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERP16                                 NON-BLANK
         SPACE 1
         SR    R1,R1               NO MESSAGE YET
         LA    R2,VERP18           EXIT ADDRESS
         CLI   MEMNAME,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR              YES, NON-STANDARD
         SPACE 1
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              STANDARD MEMBER NAME?
         LA    R1,0
         LA    R2,VERP18
         BNZ   VERMBR              NO, BRANCH
         SPACE 3
* CHECK FOR CONFLICTING LOAD MODULE ATTRIBUTES
VERP18   L     R14,DIRPTRS                 CURRENT MEMBER
         MVC   DIRNAME(12),0(R14)          MOVE IN NAME, FIRST TTR
         TM    FLAGSCC,RECFMU              LOAD LIBRARY?
         BZ    VERP28                      NO, BRANCH
         TM    #VERNLKD,X'02'              NOLKED?
         BO    VERP24                      YES, BRANCH
         MVC   DIRNAME(40),0(R14)          MOVE IN NAME, LKED FLAGS
         TM    DIRATTR2,DIRAOSLE           VS LINKAGE EDITOR?
         BNO   VERP20                      NO, BRANCH
         TM    DIRATTR3,DIRRMANY           RMODE ANY?
         BZ    VERP20                      NO, BRANCH
         SR    R1,R1
         IC    R1,DIRATTR3                 AMODE BITS
         TM    DIRFLAG,X'80'               ALIAS?
         BNO   *+8                         NO, BRANCH
         SRL   R1,2                        YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                 SAVE CURRENT AMODE
         LA    R1,=C'ANY'                  ASSUME AMODE IS ANY
         TM    MODESAVE,DIRAM24+DIRAM31    CORRECT?
         BO    VERP19                      YES, INVALID
         TM    MODESAVE,DIRAM31            AMODE 31?
         BO    VERP20                      YES, BRANCH
         LA    R1,=C'24 '                  AMODE MUST BE 24
VERP19   MVI   MTHIGHL,3                   MESSAGE LENGTH
         MVC   INSERT#1(3),0(R1)           MOVE IN THE AMODE TEXT
         LA    R1,L880$1                   RMODE ANY/AMODE CONFLICT
         BAL   R2,VERMBR                   OUTPUT AN ERROR MESSAGE
         SPACE 3
VERP20   TM    DIRATTR,ATTRRENT                REENTRANT?
         BNO   VERP22                          NO, BRANCH
         TM    DIRATTR,ATTRREUS                REUSABLE?
         BO    VERP22                          YES, BRANCH
         LA    R1,L881                         RENT AND NOT REUS
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
VERP22   LA    R1,L882                         TEST AND NOT EDIT
         TM    DIRATTR+1,ATTRSYMS+ATTRNE       TEST AND NOT EDIT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         LA    R1,L883                         REUSABLE AND SCATTER
         TM    DIRATTR,ATTRREUS+ATTRSCTR       REUSABLE AND SCATTER?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR,ATTROVLY                OVERLAY DEFINED?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(8),BLANKS              BLANKS
         MVC   INSERT#1(4),VERRENT             OVERLAY AND REENTRANT
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRRENT                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERREUS             OVERLAY AND REUSABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRREUS                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERREFR             OVERLAY AND REFRESHABLE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR+1,ATTRREFR              CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVC   INSERT#1(4),VERSCTR             OVERLAY AND SCATTER
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR,ATTRSCTR                CORRECT?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         TM    DIRATTR2,DIRAOSLE               MVS LINKAGE-EDITOR?
         BNO   VERP24                          NO, BRANCH
         MVC   INSERT#1(5),=C'RMODE'
         MVC   INSERT#1+6(3),=C'ANY'           RMODE ANY
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    DIRATTR3,DIRRMANY               RMODE ANY?
         BNO   *+8                             NO, BRANCH
         BAL   R2,VERMBR                       YES, ERROR MESSAGE
         MVI   INSERT#1,C'A'                   AMODE ANY
         SR    R1,R1
         IC    R1,DIRATTR3                     AMODE BITS
         TM    DIRFLAG,X'80'                   ALIAS?
         BNO   *+8                             NO, BRANCH
         SRL   R1,2                            YES, SHIFT AMODE BITS
         STC   R1,MODESAVE                     SAVE CURRENT AMODE
         LA    R1,L884$1                       OVERLAY CONFLICT MESSAGE
         TM    MODESAVE,DIRAM31                AMODE 24?
         BZ    VERP24                          YES, BRANCH
         TM    MODESAVE,DIRAM24                AMODE ANY?
         BO    *+10                            YES, BRANCH
         MVC   INSERT#1+6(3),=C'31 '           NO, AMODE 31
         BAL   R2,VERMBR                       ERROR MESSAGE
         SPACE 3
* LOAD EACH MEMBER IF REQUIRED AND THIS IS A LOAD LIBRARY
VERP24   TM    #VERLOAD,X'02'      LOAD CHECK REQUESTED?
         BNZ   VERP28              NO, BRANCH
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD LIBRARY?
         BZ    VERP28              NO, BRANCH
         SPACE 1
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         L     R2,RECOVER          PREVIOUS RECOVERY ADDRESS
         LA    R1,VERP26           RESUME ADDRESS
         ST    R1,RECOVER          IN-LINE RECOVERY
         STM   R2,R8,#SAVEREG      SAVE REGISTERS
         LOAD   EPLOC=DIRNAME,DCB=INDCB
         DELETE EPLOC=DIRNAME
         ST    R2,RECOVER          RESET RECOVERY ADDRESS
         B     VERP28
         SPACE 1
VERP26   LM    R2,R8,#SAVEREG      SAVE REGISTERS
         ST    R2,RECOVER          RESTORE RECOVERY ADDRESS
         MVI   MTHIGHL,4           FOUR CHARACTERS FROM ABEND MESSAGE
*        MVC   INSERT#1(4),INSERT#1
         LA    R1,L998$1           "ABEND SNNN LOADING THIS MODULE"
         BAL   R2,VERMBR           OUTPUT AN ABENDED MESSAGE
         SPACE 3
* CHECK IF THIS MEMBER IS IN USE BY AN SPF EDIT SESSION
VERP28   OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   DSNMEMQ(8),DIRNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         BAL   R2,VERMBR           YES, OUTPUT AN IN-USE MESSAGE
         SPACE 3
* CHECK THAT THE PDS DIRECTORY MEMBERS ARE IN CORRECT COLLATING ORDER
VERP30   NI    ##ADRPA#,FF-$Q      LEAVE QUIET MODE PROCESSING
         CLC   #VERMBR(8),DIRNAME  PREVIOUS:CURRENT
         BH    VERP32                HIGH  -- ERROR, OUT OF ORDER
         MVC   #VERMBR(8),DIRNAME
         BL    VERP34                LOW   -- VALID
         LA    R1,L824               EQUAL -- ERROR, DUPLICATE
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 1
VERP32   LA    R1,L825             MEMBER OUT OF SEQUENCE
         BAL   R2,VERMBR
         B     VERP14              IGNORE THIS MEMBER
         SPACE 3
* SELECT THE HIGHEST TTR POINTERS FOR THIS MEMBER
VERP34   MVC   0(8+4,R4),DIRNAME   MOVE IN THE NAME, FIRST TTR
         XC    12(4,R4),12(R4)     CLEAR THE SECOND TTR ADDRESS
         TM    MEMTTR+3,DIR1TTR+DIR2TTR  ANY OTHER TTR'S?
         BZ    VERP35                    NO, BRANCH
         SPACE 1
         MVC   12(3,R4),DIRNAME+12 ADD FIRST USER TTR
         TM    MEMTTR+3,DIR2TTR    SECOND TTR?
         BZ    VERP35              NO, BRANCH
         SPACE 1
         CLC   12(3,R4),DIRNAME+16 FIRST TTR > SECOND TTR?
         BH    *+10                YES, BRANCH
         MVC   12(3,R4),DIRNAME+16 NO, USE SECOND TTR
         SPACE 1
VERP35   NI    11(R4),X'80'        SAVE ONLY THE ALIAS BIT
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         SPACE 1
*** PAST DS1LSTAR -- PERHAPS WE NEED TO REOPEN THE DATA SET
         OI    FLAGSJJ,FNOREAD     CALL EXCP FOR OPEN ONLY
         MVC   STARTTR(3),DIRTTR   READ ADDRESS
         L     R15,=V(EXCP)        READ THE FIRST RECORD OF THIS MEMBER
         BALR  R14,R15
         NI    FLAGSJJ,FF-FNOREAD  EXCP OPEN IS COMPLETE
         SPACE 2
         CLC   8(3,R4),DS1LSTAR    IN DATA SET BOUNDS?
         BNH   VERP36              YES, BRANCH
         LA    R1,L872
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         B     VERP38
         SPACE 1
VERP36   CLC   12(3,R4),DS1LSTAR   IN DATA SET BOUNDS?
         BNH   VERP37              YES, BRANCH
         LA    R1,L873
         BAL   R2,VERMBR           NO, OUTPUT AN ERROR MESSAGE
         SPACE 1
VERP37   MVI   15(R4),X'FF'        ASSUME NOT AN OS/VS LINKAGE-EDITOR
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP38              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP38              YES, BRANCH
         TM    DIRATTR2,DIRAOSLE   OS/VS LINKAGE-EDITOR?
         BNO   VERP38              NO, BRANCH
         NI    DIRATTR3,DIRRMANY+DIRAM31+DIRAM24
         OC    11(1,R4),DIRATTR3   SAVE THE RMODE/AMODE CHARACTER
         MVC   15(1,R4),DIRATTR4   SAVE THE RLD/CONTROL COUNT
         SPACE 3
* PERFORM AN INSERTION SORT OF THIS ENTRY
VERP38   MVC   DIRNAME(16),0(R4)   SAVE ENTRY FOR INSERTION SORT
         LR    R14,R4              LAST SORT POSITION
         L     R1,#MEMPTR          FIRST SORT POSTION
         SH    R1,=H'16'           ADJUST FOR NEXT LA
         SPACE 1
VERP40   LA    R1,16(,R1)          NEXT TABLE ENTRY
         CLC   8(4,R1),DIRTTR      TABLE TTR:NEW TTR
         BL    VERP40                LOW   --  ITERATE
         BH    VERP41                HIGH  --  INSERT
         CLC   0(8,R1),DIRNAME       EQUAL --  TABLE NAME:NEW NAME
         BL    VERP40                            LOW   -- ITERATE
         BE    VERP42                            EQUAL -- IN PLACE
         SPACE 1
VERP41   SH    R14,=H'16'          PREVIOUS TABLE ENTRY
         MVC   16(16,R14),0(R14)   SLIDE DOWN TO MAKE ROOM
         CR    R14,R1              DONE WITH SHIFTING?
         BH    VERP41              NO, BRANCH
         MVC   0(16,R1),DIRNAME    INSERT NEW ENTRY TO ITS POSITION
         SPACE 2
VERP42   LA    R4,16(,R4)          NEXT TABLE ENTRY
         S     R3,=F'1'            ANY MORE ENTRIES FIT?
         BP    VERP14              YES, BRANCH
         BZ    VERP10              NO, REALLOCATE THE MEMBER TABLE
         SPACE 3
* SINGLE MEMBER -- CHECK DIRECTORY ATTRIBUTES
         ST    R4,#MEMLAST         UPDATE LAST MEMBER POINTER
         MVI   STARTTR+2,X'01'     TTR=00001 (START OF DIRECTORY)
         XC    INSERT#1(8),INSERT#1
         SPACE 1
VERP44   BAL   R14,READDIR         GET NEXT MEMBER
         B     VERP48              LAST MEMBER, BRANCH
         SPACE 1
         CLC   DIRTTR,MEMTTR       TTR MATCH?
         BNE   VERP44              NO, BRANCH
         TM    DIRFLAG,X'80'       FIRST THE MAIN ENTRY?
         BZ    VERP45              YES, BRANCH
         TM    MEMFLAG,X'80'       SECOND AN ALIAS?
         BO    VERP44              YES, IGNORE
         SPACE 1
* FOUND A REAL CORRESPONDING ENTRY
         MVC   INSERT#1(8),MEMNAME REAL ENTRY FOR THE ALIAS
         $TMSG L066$1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, DONE
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP44              YES, BRANCH
         LA    R14,DIRREAL
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R14,DIRREALS
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         CLC   0(8,R14),MEMNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, MESSAGE
         B     VERP47
         SPACE 3
* FOUND AN ALIAS CORRESPONDING ENTRY
VERP45   MVC   INSERT#1(8),MEMNAME
         TM    MEMFLAG,X'80'       AN ALIAS?
         BO    VERP46              YES, BRANCH
         CLC   DIRNAME(8),MEMNAME  FIND MYSELF?
         BE    VERP44              YES, BRANCH
         $TMSG L864$1              FOUND AN APPARENT ALIAS
         B     VERP73
         SPACE 1
VERP46   TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP44              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP44              YES, BRANCH
         L     R3,DIRPTRS
         LA    R14,DIRREAL-DIRNAME(R3)
         TM    DIRATTR-DIRNAME(R3),ATTRSCTR
         BNO   *+8
         LA    R14,DIRREALS-DIRNAME(R3)
         MVC   INSERT#2(8),0(R14)
         TR    INSERT#2(8),TRLINE
         LA    R1,L827$2
         CLC   0(8,R14),DIRNAME    CORRECT MEMBER NAME?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, MESSAGE
         SPACE 1
VERP47   L     R3,DIRPTRS
         LA    R1,L823$1           RLD/CONTROL MISMATCH
         CLC   DIRATTR4(1),DIRATTR4-DIRNAME(R3)
         BE    *+8
         BAL   R2,VERMBR
         SPACE 1
         MVI   DOUBLE+0,DIRRMANY
         MVI   DOUBLE+1,DIRRMANY
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L821$1           RMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, ERROR
         SPACE 1
         MVI   DOUBLE+0,DIRAM31+DIRAM24
         MVI   DOUBLE+1,DIRAM31+DIRAM24
         NC    DOUBLE+0(1),DIRATTR3
         NC    DOUBLE+1(1),DIRATTR3-DIRNAME(R3)
         LA    R1,L822$1           AMODE BITS DO NOT CORRESPOND
         CLC   DOUBLE(1),DOUBLE+1  EQUAL?
         BE    *+8                 YES, BRANCH
         BAL   R2,VERMBR           NO, ERROR
         B     VERP44
         SPACE 1
* END OF MEMBERS
VERP48   TM    DIRNAME,X'80'       ALIAS FIRST MEMBER?
         BNO   VERP73              NO, BRANCH
         CLI   INSERT#1,0          ANY MAIN ENTRY FOUND?
         BNE   VERP73              YES, BRANCH
         $TMSG L860                ORPHAN MEMBER
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP73              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP73              YES, BRANCH
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         $TMSG L861$1
         B     VERP73
         SPACE 3
* DIRECTORY TTR SORT COMPLETE -- CHECK FOR ORPHAN MEMBERS, APPARENT
* ALIAS MEMBERS AND ALIAS MEMBERS WITH INCORRECT MAIN MEMBER POINTERS
VERP50   LA    R1,L400
         ST    R4,#MEMLAST         SAVE THE END POINTER
         ICM   R3,B'1111',#MEMPTR  FIRST MEMBER
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         OI    #VERSTND,X'02'      NO MORE MEMBER NAME CHECKING
         LR    R5,R3
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BO    VERP54              YES, BRANCH
         B     VERP58+4
         SPACE 1
VERP52   LA    R3,16(,R5)          NEXT TABLE ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R3,R4               DONE?
         BE    VERP66              YES, BRANCH
         LR    R5,R3
         SPACE 1
         TM    8+3(R3),X'80'       ALIAS ENTRY?
         BNO   VERP58+4            NO, BRANCH
VERP54   LA    R1,L860             YES, THIS IS AN ORPHAN
         MVC   MEMNAME(8),0(R3)
         BAL   R2,VERMBR
         NI    8+3(R3),X'FF'-X'80' TURN OFF THE ALIAS BIT (ORPHAN)
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP52              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP52              YES, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,0(R3)       MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         MVC   INSERT#1(8),DIRREAL
         SPACE 1
         TM    DIRATTR,ATTRSCTR   SCATTER LOADED?
         BNO   *+10               NO, BRANCH
         MVC   INSERT#1(8),DIRREALS
         $TMSG L861$1
         B     VERP52
         SPACE 2
VERP58   LA    R5,16(,R5)          NEXT POTENTIAL ALIAS ENTRY
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         CR    R5,R4               DONE?
         BE    VERP66              YES, BRANCH
         CLC   8(3,R3),16+8(R5)    TTR FOR AN ALIAS?
         BNE   VERP52              NO, BRANCH
         TM    16+8+3(R5),X'80'    ALIAS FLAG SET?
         BNO   VERP64              NO, APPARENT ALIAS
         SPACE 1
         TM    FLAGSCC,RECFMU      LOAD MODULE?
         BNO   VERP58              NO, BRANCH
         TM    #VERNLKD,X'02'      NOLKED?
         BO    VERP58              YES, BRANCH
         XC    DIRUSER,DIRUSER
         MVC   DIRNAME,16(R5)      MEMBER NAME
         BLDL  INDCB,BLDLLIST
         LTR   R15,R15             ANY BLDL ERROR?
         BP    IOERROR             YES, I/O ERROR
         SPACE 1
         MVC   DIRFLAG(DIREND-DIRFLAG),DIRFLAG+2
         LA    R1,DIRREAL          NAME OF CORRESPONDING MAIN
         TM    DIRATTR,ATTRSCTR    SCATTER LOADED?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRREALS         YES, USE THIS NAME INSTEAD
         SPACE 1
         CLC   0(8,R1),0(R3)       CORRECT NAME RECORDED?
         BE    VERP60              YES, BRANCH
         MVI   MTHIGHL,8           MEMBER NAME 1
         MVI   MTHIGHL+4,8         MEMBER NAME 2
         MVC   INSERT#1(8),0(R3)
         MVC   INSERT#2(8),0(R1)
         TR    INSERT#2(8),TRLINE
         LA    R1,L820$2
         MVC   MEMNAME(8),16(R5)
         BAL   R2,VERMBR
         SPACE 1
VERP60   CLC   15(1,R3),16+15(R5)  MATCHING RLD/CONTROL COUNTS?
         BE    VERP62              YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L823$1           NO, RLD/CONTROL MISMATCH
         BAL   R2,VERMBR
         SPACE 1
VERP62   MVI   DOUBLE,DIRRMANY       BIT TO TEST
         MVI   DOUBLE+1,DIRRMANY     BIT TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS RMODE BIT
         NC    DOUBLE+1(1),11(R3)    MAIN RMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    RMODE BITS EQUAL?
         BE    VERP63                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L821$1             NO, RMODE DOES NOT MATCH
         BAL   R2,VERMBR
         SPACE 1
VERP63   MVI   DOUBLE,DIRAM31+DIRAM24    BITS TO TEST
         MVI   DOUBLE+1,DIRAM31+DIRAM24  BITS TO TEST
         NC    DOUBLE(1),16+11(R5)   ALIAS AMODE BITS
         NC    DOUBLE+1(1),11(R3)    MAIN AMODE BIT
         CLC   DOUBLE(1),DOUBLE+1    AMODE BITS EQUAL?
         BE    VERP58                YES, BRANCH
         MVC   INSERT#1(8),16(R5)
         MVC   MEMNAME(8),0(R3)
         LA    R1,L822$1             NO, AMODE DOES NOT MATCH
         BAL   R2,VERMBR
         B     VERP58
         SPACE 1
VERP64   MVC   INSERT#1(8),16(R5)  MEMBER NAME INSERTED
         MVC   MEMNAME(8),0(R3)
         LA    R1,L864$1
         BAL   R2,VERMBR
         OI    16+8+3(R5),X'80'    TURN ON ALIAS BIT (APPARENT ALIAS)
         B     VERP58
         SPACE 3
* END OF DIRECTORY CHECK, NOW PERFORM ANY INPUT CHECK
VERP66   MVI   STARTTR+2,X'01'     TTR=000001 (START OF DIRECTORY)
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    VERP73              YES, BRANCH
         SPACE 1
VERP68   L     R15,=V(EXCP)        READ THE DIRECTORY RECORDS
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP70                00 - SUCCESSFUL READ
         B     VERP72                04 - END OF MEMBER
         B     VERP72                08 - END OF DATA SET
         B     IOERROR               12 - I/O ERROR
         SPACE 2
VERP70   L     R0,LS               LENGTH OF RECORD (DD OF RKDD)
         ICM   R0,B'0100',=X'08'   KEY LENGTH OF RECORD (K OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         B     VERP68              READ ALL DIRECTORY BLOCKS
         SPACE 1
VERP72   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         CLI   CURMBB,0            MULTI-EXTENT DIRECTORY?
         BE    VERP73              NO, BRANCH
         $MESAGE MSGBLANK          YES, ERROR MESSAGE
         $MESAGE MSGBLANK
         $TMSG L870
         SPACE 1
VERP73   ICM   R6,B'1111',#MEMPTR  FIRST MEMBER
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BNO   *+8                 NO, BRANCH
         NI    8+3(R6),FF-X'80'    YES, READ THIS MEMBER
         CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERP99              YES, BRANCH
         B     VERP74+4
         SPACE 2
VERP74   LA    R6,16(,R6)          NEXT TABLE ENTRY
         MVI   RLDCOUNT,X'FF'      ASSUME NO RLD/CONTROL IS FOUND
         C     R6,#MEMLAST         DONE?
         BE    VERP97              YES, BRANCH
         TM    8+3(R6),X'80'       ALIAS ENTRY?
         BO    VERP74              YES, BRANCH
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         MVC   MEMNAME(8),0(R6)    INITIALIZE THE MEMBER NAME
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         LA    R5,3                ALLOW 3 PERMANENT ERRORS/MEMBER
         SPACE 1
         CLC   8(3,R6),DS1LSTAR    BEYOND END OF DATA MARKER?
         BNL   VERP90              YES, DONE
         MVC   STARTTR(3),8(R6)    NO, FIRST DISK ADDRESS TO READ
         SPACE 3
VERP76   L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERP80                00 - SUCCESSFUL READ
         B     VERP92                04 - END OF MEMBER
         B     VERP90                08 - END OF DATA SET
         B     VERP78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF MEMBER
VERP78   LA    R1,L991$1           OUTPUT THE I/O ERROR MESSAGE
         MVI   MTHIGHL,6           LENGTH OF THE TTR INSERT
         BAL   R2,VERMBR
         MVI   MTHIGHL,8           RESET TO EIGHT
         B     VERP92
         SPACE 2
VERP80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         LR    R0,R5               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         SPACE 1
         CH    R5,#MAXBLKV         EXCEED MAXBLK?
         BNH   VERP81              YES, BRANCH
         OC    #MAXBLKV(2),#MAXBLKV  ANY MAXBLK()?
         BZ    VERP80A               NO, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L806$1           BLKSIZE ERROR
         BAL   R2,VERMBR
         SPACE 1
VERP80A  CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERP81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERP81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L812$1           BLKSIZE ERROR
         BAL   R2,VERMBR
         SPACE 1
VERP81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERP82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERP82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERP82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L814$1           BLKSIZE/LRECL ERROR
         BAL   R2,VERMBR
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERP82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERP88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERP87              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERP86
         SPACE 3
VERP84   BXLE  R3,R4,VERP86        GET THE NEXT LOGICAL RECORD
         B     VERP76              END OF BLOCK, BRANCH
         SPACE 2
VERP86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERP88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BNM   VERP88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERP76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTHIGHL,1           LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         LA    R1,L811$1           LRECL TOO SMALL
         BAL   R2,VERMBR
         B     VERP76
         SPACE 2
VERP87   CLI   RLDCOUNT,X'FF'      ANY RLD/CONTROL RECORD YET?
         BNE   VERP88              YES, BRANCH
         TM    0(R3),X'F0'         TEST, ESD, IDR OR SCATTER RECORD?
         BM    VERP88              YES, BRANCH
         MVC   RLDCOUNT(1),3(R3)   NO, FIRST RLD/CONTROL ENTRY
         SPACE 2
VERP88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERP84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERP84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERP2020
         ED    INSERT#1(7),DOUBLE+5
         LA    R1,L813$1           LRECL TOO LARGE
         BAL   R2,VERMBR
         B     VERP84
         SPACE 2
VERP90   MVI   #MEMCNT,X'FF'              END OF DATA SET
         SPACE 2
VERP92   SR    R0,R0               LENGTH OF RECORD & KEY (KDD OF RKDD)
         BAL   R2,VERCOMP          DATA IS RETAINED IN THE OUTPUT
         OC    12(3,R6),12(R6)            ANY USER TTR?
         BZ    VERP93                     NO, BRANCH
         STM   R14,R12,12(R13)            CONVERT CCHHR TO TTR
         LA    R2,CURMBB                  CCHHR TO CONVERT
         L     R1,INDCB+(DCBDEBAD-IHADCB) DEB ADDRESS
         L     R15,ADDRRLTV               ADDRESS OF CCHHR->TTR CONVERT
         LR    R3,R13                     ADDRESS OF SAVE AREA
         BALR  R14,R15                    CALL CONVERT ROUTINE
         LR    R13,R3                     RESTORE SAVE AREA ADDRESS
         ST    R0,12+4+4(R13)             RESULT TTR RETURNED IN R0
         LM    R14,R12,12(R13)            RESTORE REGISTERS
         CLM   R0,B'1110',12(R6)          TTR ACTUALLY INPUT?
         BH    VERP93                     YES, BRANCH
         LA    R1,L871                    NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 1
VERP93   OC    #MEMCNT+1(3),#MEMCNT+1  ANY RECORDS?
         BNZ   VERP94                  YES, BRANCH
         LA    R1,L510                 NO, NULL MEMBER WARNING
         BAL   R2,VERMBR
         SPACE 2
VERP94   CLI   15(R6),X'FF'        OS/VS LINKAGE EDITOR?
         BE    VERP96              NO, BRANCH
         CLC   15(1,R6),RLDCOUNT   RLD/CONTROL COUNTS MATCH?
         BE    VERP96              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BNO   VERP96              NO, BRANCH
         LA    R1,L826             NO, ERROR MESSAGE
         BAL   R2,VERMBR
         SPACE 2
VERP96   CLI   #MEMCNT,X'FF'       END OF DATA SET?
         BNE   VERP74              NO, BRANCH
         $MESAGE MSGBLANK
         $TMSG L006                YES, STATUS MESSAGE
         SPACE 2
VERP97   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         $MESAGE MSGBLANK
         MVI   MTHIGHL,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERP98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L110$1
         SPACE 1
VERP98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERP98B             NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L113$1
         SPACE 1
VERP98B  TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         LH    R0,DS1LSTAR         GET TT OF LAST TRACK USED
         S     R0,#VERCTRK         LESS THE AMOUNT AFTER THE COMPRESS
         BNM   *+6                 ZERO OR MORE, BRANCH
         SR    R0,R0               NEGATIVE, ERROR (USE ZERO)
         CVD   R0,DOUBLE           NUMBER OF WHOLE COMPRESSED TRACKS
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L114$1
         B     VERP99B
         SPACE 2
VERP99   TM    #VERSTAT,2          STATISTICS DESIRED?
         BO    NEWCMD              NO, BRANCH
         TM    #VERUPDT,4+2        SINGLE MEMBER CHECK?
         BO    NEWCMD              YES, DONE
         $MESAGE MSGBLANK
         SPACE 1
VERP99B  MVI   MTHIGHL,10          TEN BYTES OF INSERTED MESSAGE
         L     R1,#MEMLAST         LAST MEMBER POINTER
         L     R2,#MEMPTR          START OF MEMBER LIST
         LA    R2,0(,R2)           CLEAR HIGH-ORDER DIGIT
         SR    R1,R2               (END+1 - START) * 16
         SRL   R1,4                DIVIDE BY 16
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERP206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L115$1
         $MESAGE MSGBLANK
         B     NEWCMD
         SPACE 2
VERMBR   ST    R2,#SAVEREG         SAVE REGISTER
         ST    R1,MSGTEXT1         SAVE FOR LATER OUTPUT
         LA    R1,MEMNAME          ASSUME DATA SET CHECK
         TM    #VERUPDT,4+2        MEMBER NAME CHECK?
         BNO   *+8                 NO, BRANCH
         LA    R1,DIRNAME          YES, USE DIRECTORY NAME
         CLC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED?
         BE    VERMBR20            YES, BRANCH
         MVC   #VERMEM(8),0(R1)    MEMBER NAME DISPLAYED
         SPACE 1
         MVC   MSGTEXT2(136),MSGBL132
         MVC   MSGTEXT2+4(2),VERLSTAR
         MVC   MSGTEXT2+4+3(8),##SUBCOM
         TM    DSORG,DS1DSGPO      PARTITIONED?
         BNO   VERMBR10            NO, BRANCH
         $MESAGE MSGBLANK          ONE BLANK LINE
         MVC   MSGTEXT2+4+3+8+1(8),#VERMEM
         TR    MSGTEXT2+4+3+8+1(8),TRLINE
         MVC   DOUBLE(8),#VERMEM
         LA    R1,7
         LA    R14,DOUBLE+8
         SPACE 1
VERMBR02 BCTR  R14,0               SCAN
         CLI   0(R14),X'40'            BACKWARDS
         BNE   *+8                              FOR FIRST
         BCT   R1,VERMBR02                               NON-BLANK
         SPACE 1
         CLI   #VERMEM,C'0'        NUMERIC FIRST CHARACTER?
         BNL   VERMBR04            YES, NON-STANDARD
         TRT   DOUBLE(*-*),TRTMEM  <<EXECUTED>>
         EX    R1,*-6              VALID CHARACTERS?
         BZ    VERMBR10            YES, BRANCH
         SPACE 1
VERMBR04 MVC   MSGTEXT2+4+3+8+1+8+2(2),=C'X'''
         UNPK  MSGTEXT2+4+3+8+1+8+2+2(9),#VERMEM(5)
         UNPK  MSGTEXT2+4+3+8+1+8+2+2+8(9),#VERMEM+4(5)
         TR    MSGTEXT2+4+3+8+1+8+2+2(8+8),TRTABLE
         MVI   MSGTEXT2+4+3+8+1+8+2+2+8+8,C''''
         TM    #VERSTND,X'02'          STANDARD NAME CHECKING?
         BNZ   VERMBR10                NO, BRANCH
         MVC   MSGTEXT2+48(24),VERLNONS
         $MESAGE MSGTEXT2          MEMBER HEADER MESSAGE
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 1
VERMBR10 $MESAGE MSGTEXT2          MEMBER HEADER MESSAGE
         SPACE 1
VERMBR20 L     R1,MSGTEXT1         MESSAGE IDENTIFIER OR MESSAGE
         LTR   R1,R1               ANY MESSAGE?
         BZ    VERMBR30            NO, BRANCH
         $TMSG (R1)
VERMBR30 MVI   MTHIGHL,8           RESET THE MESSAGE LENGTH
         L     R2,#SAVEREG         RESTORE REGISTER
         BR    R2
         SPACE 2
VERCOMP  ST    R0,#VERCREG
         SPACE 1
VERCOMP2 L     R0,#VERCREG
         L     R1,#VERCREC
         A     R1,=F'1'            CURRENT COMPRESS RECORD
         ST    R1,#VERCREC
         ICM   R0,B'1000',#VERCREC+3  RECORD NUMBER IN RKDD (R OF RKDD)
         SPACE 1
         TRKCALC FUNCTN=TRKBAL,TYPE=BYTEUCB+1,REGSAVE=YES,             X
               BALANCE=#VERCBAL+2,RKDD=(0),MF=(E,PARMLIST)
         SPACE 1
         LTR   R15,R15             FIT ON CURRENT TRACK?
         BZ    VERCOMPX            YES, BRANCH
         SPACE 1
         L     R1,#VERCTRK
         A     R1,=F'1'            START A NEW TRACK IN OUTPUT
         ST    R1,#VERCTRK
         MVI   #VERCREC+3,0        RECORD ZERO
         B     VERCOMP2            TRY AGAIN
         SPACE 1
VERCOMPX ST    R0,#VERCBAL         SAVE THE TRACK BALANCE
         BR    R2
VERLSTAR DC    C'**'
VERLNONS DC    CL24'NON-STANDARD MEMBER NAME'
VERP2020 DC    X'4020206B20212040'
VERP206B DC    X'40206B2020206B202120'
VERRENT  DC    C'RENT'
VERREUS  DC    C'REUS'
VERREFR  DC    C'REFR'
VERSCTR  DC    C'SCTR'
         SPACE 3
VERS00   BALR  R8,0
         USING *,R8
         OI    ##ADRPA#,$Q         ENTER QUIET MODE PROCESSING
         SPACE 1
         MVC   MEMNAME(8),BLANKS   INITIALIZE THE MEMBER NAME
         MVC   DSNMEMQ(8),MEMNAME  MEMBER NAME TO CHECK
         BAL   R2,ENQMTEST         MEMBER IN USE?
         B     *+8                 YES, BRANCH
         B     VERS40              NO, BRANCH
         MVC   INSERT#1(8),INSERT#2  JOBNAME
         $TMSG L851$1
         SPACE 2
VERS40   CLI   #VERREAD,X'02'      NOINPUT REQUESTED?
         BE    VERS99              YES, BRANCH
         SPACE 2
         MVI   #MEMPTR,0           CLEAR THE MEMBER FLAG
         XC    #MEMCNT(4),#MEMCNT  COUNT OF INPUT LOGICAL RECORDS
         MVI   STARTTR+2,X'01'     TTR=000001 (START OF DATA SET)
         SPACE 2
VERS76   L     R15,=V(EXCP)
         BALR  R14,R15
         B     *+4(R15)            PROCESS RETURN CODE
         B     VERS80                00 - SUCCESSFUL READ
         B     VERS90                04 - END OF FILE
         B     VERS92                08 - END OF DATA SET
         B     VERS78                12 - I/O ERROR
         SPACE 2
*  PERMANENT I/O ERROR  -- SIMULATE END OF DATA SET
VERS78   MVI   MTHIGHL,6           LENGTH OF THE TTR INSERT
*        MVC   INSERT#1(6),INSERT#1        FIRST PARAMETER
         $TMSG L991$1                      OUTPUT AN ERROR MESSAGE
         MVI   MTHIGHL,8                   RESET TO EIGHT
         B     VERS97
         SPACE 2
VERS80   LR    R3,R0               BUFFER START
         L     R5,LS               BLOCK LENGTH
         C     R5,#VERMAX          GREATER THAN MAXIMUM?
         BNH   *+8                 NO, BRANCH
         ST    R5,#VERMAX          YES, SAVE NEW MAXIMUM
         L     R0,#VERCBLK         NUMBER
         A     R0,=F'1'                  OF PHYSICAL
         ST    R0,#VERCBLK                          BLOCKS
         L     R0,#VERCBLC         NUMBER
         AR    R0,R5                     OF PHYSICAL
         ST    R0,#VERCBLC                          CHARACTERS
         SPACE 1
         CH    R5,#MAXBLKV         EXCEED MAXBLK?
         BNH   VERS81              YES, BRANCH
         OC    #MAXBLKV(2),#MAXBLKV  ANY MAXBLK()?
         BZ    VERS80A               NO, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         $TMSG L806$1              BLKSIZE ERROR
         SPACE 1
VERS80A  CH    R5,BLKSI            BLKSIZE VALID?
         BNH   VERS81              YES, BRANCH
         TM    #MEMPTR,#MEMBLKE    BLKSIZE SIZE ERROR NOTED?
         BO    VERS81              YES, BRANCH
         OI    #MEMPTR,#MEMBLKE    BLKSIZE ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         $TMSG L812$1
         SPACE 1
VERS81   TM    FLAGSCC,RECFMF      FIXED FORMAT?
         BNO   VERS82              NO, BRANCH
         LH    R14,LRECL           CURRENT LRECL
         TM    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED?
         BO    VERS82              YES, BRANCH
         LR    R0,R5               BLOCK LENGTH
         SRDA  R0,32
         DR    R0,R14              BLKSIZE/LRECL
         LTR   R0,R0               ANY REMAINDER?
         BZ    VERS82              YES, BRANCH
         OI    #MEMPTR,#MEMDIVE    BLKSIZE/LRECL ERROR NOTED NOW
         CVD   R5,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         $TMSG L814$1
         LH    R14,LRECL           CURRENT LRECL
         SPACE 1
VERS82   LR    R1,R5
         AR    R5,R3               END OF BUFFER
         BCTR  R5,0                END OF BUFFER -1
         TM    FLAGSCC,RECFMF      RECFM=F?
         LH    R4,LRECL
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         LR    R4,R1
         BO    VERS88              YES, BRANCH
         TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, ASSUME RECFM=U
         AH    R3,=H'4'            SKIP BLOCK LENGTH WORD
         B     VERS86
         SPACE 3
VERS84   BXLE  R3,R4,VERS86        GET THE NEXT LOGICAL RECORD
         B     VERS76              END OF BLOCK, BRANCH
         SPACE 2
VERS86   TM    FLAGSCC,RECFMV      RECFM=V?
         BNO   VERS88              NO, BRANCH
         SR    R4,R4
         ICM   R4,B'0011',0(R3)    RECORD LENGTH +4
         LR    R14,R4              RECORD LENGTH +4
         AH    R3,=H'4'            SKIP RECORD LENGTH WORD
         SH    R4,=H'4'            RECORD LENGTH VALID?
         BP    VERS88              YES, BRANCH
         TM    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED?
         BO    VERS76              YES, BRANCH
         OI    #MEMPTR,#MEMLRL0    LRECL ERROR NOTED
         MVI   MTHIGHL,1           LENGTH OF MESSAGE
         LA    R14,240(,R14)       RECORD LENGTH IN DISPLAY
         STC   R14,INSERT#1        PLACE IN THE MESSAGE
         $TMSG L811$1
         MVI   MTHIGHL,8                   RESET TO EIGHT
         B     VERS76              YES, BRANCH
         SPACE 2
VERS88   L     R1,#MEMCNT          ANOTHER
         LA    R1,1(,R1)                  OUTPUT
         ST    R1,#MEMCNT                       RECORD
         L     R0,#VERCLOG         NUMBER
         A     R0,=F'1'                  OF LOGICAL
         ST    R0,#VERCLOG                         RECORDS
         CH    R14,LRECL           VALID LRECL VALUE?
         BNH   VERS84              YES, BRANCH
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS84              YES, BRANCH
         TM    #MEMPTR,#MEMLRLE    LRECL ERROR NOTED?
         BO    VERS84              YES, BRANCH
         OI    #MEMPTR,#MEMLRLE    LRECL ERROR HAS BEEN NOTED
         CVD   R14,DOUBLE
         MVC   INSERT#1(8),VERS2020
         ED    INSERT#1(7),DOUBLE+5
         $TMSG L813$1
         B     VERS84
         SPACE 2
VERS90   $MESAGE MSGBLANK
         $TMSG L005                       END OF FILE
         $MESAGE MSGBLANK
         B     VERS76                     LOOP
         SPACE 2
VERS92   $MESAGE MSGBLANK
         $TMSG L006                       END OF DATA SET
         SPACE 2
VERS97   MVI   MTHIGHL,10
         TM    FLAGSCC,RECFMU      RECFM=U?
         BO    VERS98              YES, BRANCH
         L     R0,#VERCLOG         NUMBER OF LOGICAL RECORDS INPUT
         CVD   R0,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L110$1
         SPACE 1
VERS98   L     R2,#VERCBLK         NUMBER OF PHYSICAL BLOCKS INPUT
         CVD   R2,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L111$1
         SPACE 1
         LTR   R2,R2               ANY RECORDS?
         BNP   VERS99              NO, BRANCH
         L     R1,#VERMAX          MAXIMUM CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L112$1
         SR    R0,R0
         L     R1,#VERCBLC         NUMBER OF CHARACTERS READ
         SRL   R2,1                AMOUNT FOR ROUNDING
         AR    R1,R2
         D     R0,#VERCBLK         AVERAGE CHARACTERS/BLOCK
         CVD   R1,DOUBLE
         MVC   INSERT#1(10),VERS206B
         ED    INSERT#1(10),DOUBLE+4
         $TMSG L113$1
         B     NEWCMD
         SPACE 2
VERS99   $MESAGE MSGBLANK
         LA    R1,L116
         B     MSGNEW
VERS2020 DC    X'4020206B20212040'
VERS206B DC    X'40206B2020206B202120'
PDSCOMM  CSECT
         TITLE 'P D S  --  PDS STANDARD MESSAGES              5/12/86'
*
*        SUBCOMMAND MESSAGE SUBROUTINES
*
         SPACE 3
NOMEMBER LA    R1,L853$1              MSG - (MEMBER) NOT FOUND
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
BADMEMB  LA    R1,L854$1              MSG - (MEMBER) IS AN INVALID NAME
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
MEMEXIST LA    R1,L852$1              MSG - (MEMBER) EXISTS
         MVC   INSERT#1(8),DIRNAME
         B     MSGNEW
         SPACE 3
INVABBR  LA    R1,L930$1              MSG - (SUBCOM.) INVALID ABBREV.
         MVC   INSERT#1(8),##SUBCOM
         LA    R3,1(,R3)              ACTUAL SUBCOMMAND NAME LENGTH
         STC   R3,MTHIGHL
         B     MSGNEW
         SPACE 3
IOERROR  LA    R1,L836                MSG - I/O ERROR IN DIRECTORY
         B     MSGNEWXX
         SPACE 3
FULLDIR  LA    R1,L855                MSG - DIRECTORY IS FULL
         SPACE 2
MSGNEWXX NI    FLAGSAA,FF-FMEM#MEM    TURN OFF ANY MEMBER GROUP
         MVC   PMEMCURR(4),PMEMMAX    TERMINATE ANY MEMBER LIST
         SPACE 2
MSGNEW   $TMSG (R1)                   OUTPUT THE MESSAGE REFERENCED
         B     NEWCMD                 GET A NEW SUBCOMMAND
         TITLE 'P D S  --  PDS CONSTANTS                      5/12/86'
BLANKS   DC    0F'0',8X'40'
         SPACE 2
FF       EQU   X'FF'        IMMEDIATE FLAG MASK (FOR NI INSTRUCTIONS)
         SPACE 3
         PRINT NOGEN
SAMDCB   DCB   DSORG=PO,DEVD=DA,DDNAME=X,MACRF=(R,W),EXLST=EXLST
LSAMDCB  EQU   *-SAMDCB
DIRDCB   DCB   DSORG=PS,RECFM=FB,LRECL=256,BLKSIZE=256,DDNAME=X,       X
               MACRF=(W),KEYLEN=8,DEVD=DA
LDIRDCB  EQU   *-DIRDCB
EXLST    DC    X'85',VL3(OPENEXIT)
         PRINT GEN
         SPACE 2
#RESFREP EQU     X'01'            RESTORE: REPEAT
#RESFDIS EQU     X'02'            RESTORE: DISPLAY
#RESFPRO EQU     X'04'            RESTORE: PROMPT
DATEMASK DC    X'402120612020612020'
         SPACE 2
SYSHELP  DC    CL8'SYSHELP'               FOR DDNAME FREE IN HELP
         AIF ('&CISP' EQ 'ISPF' OR '&CISP' EQ 'ISPFV2').FORISPF
ISPPANEL DC    CL33'PANEL(ISP@PRIM)              OPT('
         AGO   .FORBOTH
.FORISPF ANOP
ISPPANEL DC    CL33'PANEL(ISR@PRIM) NEWAPPL(ISR) OPT('
.FORBOTH ANOP
ISPFPGM  EQU   12,8                       DISPLACEMENT TO PROGRAM NAME
ISPFPARM EQU   32,8                       DISPLACEMENT TO PARM PASSED
         SPACE 3
ISUBS    DC    33F'0'           INITIAL PDL AREA
         ORG   *-20
ZERO     DC    F'0'
         DC    XL4'FFFFFF'      INITIALIZE #MAXLEN
         DC    3F'9999999'      INITIALIZE #MAXFIND, #MAXIN, #MAXOUT
LISUBS   EQU   *-ISUBS          INITIALIZATION LENGTH
         SPACE 2
KEYDATA  DC    XL8'FFFFFFFFFFFFFFFF',XL2'000E',XL8'FFFFFFFFFFFFFFFF'
         EJECT
UNITTBL  DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL9'D????    '
         DC    CL1'&DB23051',CL8'&UN23051 '
         DC    CL1'&DB23052',CL8'&UN23052 '
         DC    CL1'&DB2314 ',CL8'&UN2314  '
         DC    CL1'&DB3330 ',CL8'&UN3330  '
         DC    CL1'&DB3340 ',CL8'&UN3340  '
         DC    CL1'&DB3350 ',CL8'&UN3350  '
         DC    CL1'&DB3375 ',CL8'&UN3375  '
         DC    CL1'&DB33301',CL8'&UN33301 '
         DC    CL1'&DB3380 ',CL8'&UN3380  '
         DC    CL9'M????    '
         SPACE 3
STAELIST ESTAE *-*,PARAM=*-*,MF=L
         ORG   STAELIST+4
         SPACE 3
***BITS FOR DIRATTR+0:
ATTRRENT EQU   X'80'          REENTRANT
ATTRREUS EQU   X'40'          REUSABLE
ATTROVLY EQU   X'20'          OVERLAY STRUCTURE
ATTRTEST EQU   X'10'          TEST SYMBOLS ARE AVAILABLE
ATTRLOAD EQU   X'08'          ONLY LOADABLE
ATTRSCTR EQU   X'04'          SCATTER LOADED
ATTREXEC EQU   X'02'          EXECUTABLE
ATTR1TXT EQU   X'01'          ONE TEXT RECORD ONLY AND NO RLD ITEMS
         SPACE 1
***BITS FOR DIRATTR+1:
ATTRNODC EQU   X'80'          NOT DOWNWARD-COMPATABLE
ATTRZORG EQU   X'40'          FIRST TEXT BLOCK HAS A ZERO ORIGIN
ATTREP0  EQU   X'20'          ENTRY POINT IS AT OFFSET ZERO
ATTRNRLD EQU   X'10'          NO RLD ITEMS
ATTRNE   EQU   X'08'          CANNOT BE REPROCESSED BY LINKAGE-EDITOR
ATTRSYMS EQU   X'04'          CONTAINS TEST SYMBOLS
ATTRFLEV EQU   X'02'          CREATED BY F LEVEL LINKAGE-EDITOR
ATTRREFR EQU   X'01'          REFRESHABLE
         SPACE 3
$ATTL    DC    CL8'&NATT '
$ALI     DC    CL8'&NALI '
$BRO     DC    CL8'&NBRO '
$DIR     DC    CL8'&NDIR '
$DIS     DC    CL8'&NDIS '
$EDI     DC    CL8'&NEDI '
$FIX     DC    CL8'&NFIX '
$IFX     DC    CL8'&NIFX '
$ISM     DC    CL8'&NISM '
$ISP     DC    CL8'&NISP '
$LIS     DC    CL8'&NLIS '
$MAP     DC    CL8'&NMAP '
$MEM     DC    CL8'&NMEM '
$MML     DC    CL8'&NMML '
$SPF     DC    CL8'&NSPF '
$SUB     DC    CL8'&NSUB '
$USA     DC    CL8'&NUSA '
#EXE     DC    CL8'&CEXE '
PTW      EQU   32        SUBCOMMAND TABLE WIDTH
         SPACE 3
         PRINT NOGEN
MSGBLANK $MSG  '  '
MSGBL132 $MSG  '                                                       X
                                                                       X
                                    '  -- 132 BYTES
BLANK128 EQU   MSGBL132+4,128
         SPACE 2
** PROMPTING MESSAGES
PDS300A  $MSG  'PDS300A ENTER OPTION -- DSN=...                        X
                                                                      '
*BYTE COUNT:   ' 234567 10 234567 20 234567 30 234567 40 234567 50 2345
*DS300A  $MSG  'PDS300A ENTER OPTION -- DSN=123456789012345678901234567
*              89012345678901234,VOL=SER=123456  MEM=12345678:12345678'
*BYTE COUNT:   67 60 234567 70 234567 80 234567 90 23456 100 23456 110'
*
*FOR DIALOG:   '- DSN=12345678901234567890123456789012345678901234,VOL=
*              SER=123456  MEM=12345678'
*
PDS381A  $MSG  'PDS381A REENTER THE DATA SET NAME, VOLUME AND DISPOSITIX
               ON'
         SPACE 2
** YES/NO PROMPTS
PDS390A  $MSG  'PDS390A SHOULD THIS MEMBER BE RESTORED (Y/N) ?'
PDS391A  $MSG  'PDS391A SHOULD THESE MEMBERS BE RENAMED (Y/N) ?'
PDS392A  $MSG  'PDS392A SHOULD THIS DATA SET BE MODIFIED (Y/N) ?'
PDS393A  $MSG  'PDS393A SHOULD THIS MEMBER BE DELETED (Y/N) ?'
PDS394A  $MSG  'PDS394A SHOULD ALL OF THESE MEMBERS BE DELETED (Y/N) ?'
PDS395A  $MSG  'PDS395A SHOULD THESE MEMBERS BE SUBMITTED (Y/N) ?'
PDS396A  $MSG  'PDS396A SHOULD REPRO CONTINUE (Y/N) ?'
         TITLE 'P D S  --  PDS LITERALS                       5/12/86'
         LTORG
         TITLE 'P D S  --  TRANSLATE TABLES                   5/12/86'
TRTABLE  EQU   *-X'F0'
HEXTBL   DC    C'0123456789ABCDEF'
         SPACE 2
***  TRANSLATE AND TEST TABLE TO FIND INVALID MEMBER NAMES
TRTMEM   DC    256X'FF'           ASSUME ALL INVALID FIRST
         ORG   TRTMEM+C'#'
         DC    1X'00'             # IS OK
         ORG   TRTMEM+C'$'
         DC    1X'00'             $ IS OK
         ORG   TRTMEM+C'@'
         DC    1X'00'             @ IS OK
         ORG   TRTMEM+C'A'
         DC    9X'00'             ABCDEFGHI  ARE OK
         ORG   TRTMEM+C'J'
         DC    9X'00'             JKLMNOPQR  ARE OK
         ORG   TRTMEM+C'S'
         DC    8X'00'             STUVWXYZ  ARE OK
         ORG   TRTMEM+C'0'
         DC    10X'00'            0123456789  ARE OK
         ORG   ,
         SPACE 3
***  TRANSLATE TABLE TO CONVERT UNPRINTABLES TO PERIODS
TRLINE   DC    256C'.'            ASSUME ALL UNPRINTABLE FIRST
         ORG   TRLINE+C' '
         DC    1X'40'             BLANK IS OK
         ORG   TRLINE+C''
         DC    C'.<(+&&'        .<(+&  ARE OK
         ORG   TRLINE+C'!'
         DC    C'!$*);^-/'        !$*);^-/  ARE OK
         ORG   TRLINE+C','
         DC    C',%_>?'           ,%_>?  ARE OK
         ORG   TRLINE+C':'
         DC    C':#@''="'         :#@'="  ARE OK
         ORG   TRLINE+X'81'       LOWER-CASE ABCDEFGHI
         DC    X'818283848586878889'
         ORG   TRLINE+X'91'       LOWER-CASE JKLMONPQR
         DC    X'919293949596979899'
         ORG   TRLINE+X'A2'       LOWER-CASE STUVWXYZ
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   TRLINE+C'A'
         DC    C'ABCDEFGHI'       ABCDEFGHI  ARE OK
         ORG   TRLINE+C'J'
         DC    C'JKLMNOPQR'       JKLMNOPQR  ARE OK
         ORG   TRLINE+C'S'
         DC    C'STUVWXYZ'        STUVWXYZ  ARE OK
         ORG   TRLINE+C'0'
         DC    C'0123456789'      0123456789  ARE OK
         ORG   ,
         TITLE 'P D S  --  PDS MESSAGE LABELS/MESSAGE CSECT   5/12/86'
L000     DS    0H
L001     DC    C'001'
L002$1   DC    C'002'
L005     DC    C'005'
L006     DC    C'006'
L010     DC    C'010'
L020$2   DC    C'020'
L021     DC    C'021'
L022     DC    C'022'
L023     DC    C'023'
L024     DC    C'024'
L025$1   DC    C'025'
L030$1   DC    C'030'
L031$1   DC    C'031'
L032     DC    C'032'
L033     DC    C'033'
L034     DC    C'034'
L035$2   DC    C'035'
L040$1   DC    C'040'
L050$2   DC    C'050'
L051$2   DC    C'051'
L060     DC    C'060'
L061     DC    C'061'
L062     DC    C'062'
L064$2   DC    C'064'
L065$1   DC    C'065'
L066$1   DC    C'066'
L071     DC    C'071'
L080     DC    C'080'
L081     DC    C'081'
L090$1   DC    C'090'
L091$1   DC    C'091'
L092$1   DC    C'092'
L100$1   DC    C'100'
L101$1   DC    C'101'
L102$1   DC    C'102'
L103$2   DC    C'103'
L104$2   DC    C'104'
L110$1   DC    C'110'
L111$1   DC    C'111'
L112$1   DC    C'112'
L113$1   DC    C'113'
L114$1   DC    C'114'
L115$1   DC    C'115'
L116     DC    C'116'
L120$2   DC    C'120'
L140$2   DC    C'140'
L141$1   DC    C'141'
L142$1   DC    C'142'
L143$2   DC    C'143'
L144$1   DC    C'144'
L145$1   DC    C'145'
L160$1   DC    C'160'
L161$1   DC    C'161'
L162$1   DC    C'162'
L163$1   DC    C'163'
L164$1   DC    C'164'
L165$1   DC    C'165'
L171$2   DC    C'171'
L180$1   DC    C'180'
L181$1   DC    C'181'
L182$1   DC    C'182'
L183$1   DC    C'183'
L184$1   DC    C'184'
L190$1   DC    C'190'
L192$1   DC    C'192'
L193$1   DC    C'193'
L200$1   DC    C'200'
L210$1   DC    C'210'
L220$1   DC    C'220'
L230$1   DC    C'230'
L232$1   DC    C'232'
L241     DC    C'241'
L242     DC    C'242'
L243     DC    C'243'
L244     DC    C'244'
L245     DC    C'245'
L250$1   DC    C'250'
L262$1   DC    C'262'
L400     DC    C'400'
L401     DC    C'401'
L402     DC    C'402'
L410$1   DC    C'410'
L412     DC    C'412'
L441$1   DC    C'441'
L442$1   DC    C'442'
L443     DC    C'443'
L444     DC    C'444'
L451     DC    C'451'
L460     DC    C'460'
L470     DC    C'470'
L480     DC    C'480'
L481     DC    C'481'
L482     DC    C'482'
L484     DC    C'484'
L485     DC    C'485'
L500     DC    C'500'
L502     DC    C'502'
L510     DC    C'510'
L520     DC    C'520'
L530     DC    C'530'
L532     DC    C'532'
L700     DC    C'700'
L701     DC    C'701'
L702     DC    C'702'
L703     DC    C'703'
L704$1   DC    C'704'
L705$1   DC    C'705'
L710     DC    C'710'
L720     DC    C'720'
L721     DC    C'721'
L722     DC    C'722'
L723     DC    C'723'
L724     DC    C'724'
L726     DC    C'726'
L730     DC    C'730'
L731     DC    C'731'
L732     DC    C'732'
L733     DC    C'733'
L740     DC    C'740'
L750     DC    C'750'
L751     DC    C'751'
L752     DC    C'752'
L770     DC    C'770'
L771$1   DC    C'771'
L772     DC    C'772'
L773     DC    C'773'
L774     DC    C'774'
L780$1   DC    C'780'
L781$1   DC    C'781'
L800     DC    C'800'
L801     DC    C'801'
L802$1   DC    C'802'
L804     DC    C'804'
L805     DC    C'805'
L806$1   DC    C'806'
L811$1   DC    C'811'
L812$1   DC    C'812'
L813$1   DC    C'813'
L814$1   DC    C'814'
L820$2   DC    C'820'
L821$1   DC    C'821'
L822$1   DC    C'822'
L823$1   DC    C'823'
L824     DC    C'824'
L825     DC    C'825'
L826     DC    C'826'
L827$2   DC    C'827'
L830     DC    C'830'
L831     DC    C'831'
L832     DC    C'832'
L833     DC    C'833'
L834$1   DC    C'834'
L835$1   DC    C'835'
L836     DC    C'836'
L840     DC    C'840'
L841     DC    C'841'
L842     DC    C'842'
L843     DC    C'843'
L850$2   DC    C'850'
L851$1   DC    C'851'
L852$1   DC    C'852'
L853$1   DC    C'853'
L854$1   DC    C'854'
L855     DC    C'855'
L860     DC    C'860'
L861$1   DC    C'861'
L862     DC    C'862'
L864$1   DC    C'864'
L865     DC    C'865'
L870     DC    C'870'
L871     DC    C'871'
L872     DC    C'872'
L873     DC    C'873'
L874     DC    C'874'
L875     DC    C'875'
L876     DC    C'876'
L877     DC    C'877'
L880$1   DC    C'880'
L881     DC    C'881'
L882     DC    C'882'
L883     DC    C'883'
L884$1   DC    C'884'
L885$1   DC    C'885'
L892     DC    C'892'
L893     DC    C'893'
L900     DC    C'900'
L910$1   DC    C'910'
L920$1   DC    C'920'
L930$1   DC    C'930'
L980     DC    C'980'
L982     DC    C'982'
L990     DC    C'990'
L991$1   DC    C'991'
L998$1   DC    C'998'
L999$1   DC    C'999'
         DC    X'FF'             LIST TERMINATOR
         SPACE 3
&PDSPRT  SETC  'ON'
         COPY  PDSDSECT
         TITLE 'P D S  --  PDS ESD, IDR DSECTS                5/12/86'
*
*        ESD TABLE FORMAT
*
ESDENTRY DSECT
ESDLINK  DS    F
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL
ESDTYPE  DS    X              ESD TYPE
CODESD   EQU   X'00'          ENTERNAL DEFINITION
CODEER   EQU   X'02'          ENTERNAL REFERENCE ($UNRESOLVED)
CODELR   EQU   X'03'          LABEL REFERENCE
CODEPC   EQU   X'04'          PRIVATE CODE DEFINITION
CODECM   EQU   X'05'          COMMON BLOCK
CODEPR   EQU   X'06'          PSEUDO REGISTER
CODENL   EQU   X'07'          NULL
CODEWK   EQU   X'0A'          WEAK EXTERNAL ($UNRESOLVED WEAK)
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE
ESDADDR  DS    XL3            RELATIVE OFFSET
ESDSEG#  DS    X              SEGMENT NUMBER
ESDXAFLG EQU   ESDSEG#        FLAGS FOR MVS/XA
RMODEANY EQU   X'04'          RMODE=ANY
AMODE24  EQU   X'01'          AMODE=24
AMODE31  EQU   X'02'          AMODE=31
ESDLEN   DS    XL3            LENGTH OF SD ENTRY
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA
ESDID    DS    XL2            RELATIVE ESD ID #
ESDIDIN  DS    XL2            ESD IS IN THIS PHYSICAL RECORD
ESDCSECT DS    XL4            LIST ONLY -- POINTER TO CSECT ENTRY
ESDMAIN  DS    XL4            LIST ONLY -- ENTRY POINT OF CSECT ENTRY
LENESD2  EQU   *-ESDNAME      LENGTH OF ESD DATA
LENESD   EQU   *-ESDENTRY     LENGTH OF ENTRY
         SPACE 3
*
*        IDR TABLE FORMAT
*
IDRENTRY DSECT
IDRLINK  DS    A
IDRSTART EQU   *
IDRESDID DS    XL2
IDRTYPE  DS    X
IDRZAP   EQU   X'01'
IDRLKED  EQU   X'02'
IDRTRAN  EQU   X'04'
IDRUSER  EQU   X'08'
IDRDATE  DS    XL3
IDRLDATA DS    X
IDRUPREF DS    XL6            USER RECORD PREFIX
IDRZDATA DS    0CL8
IDRTDATA DS    0CL30
IDRDATA  DS    CL40
LENIDR1  EQU   *-IDRSTART
LENIDR   EQU   *-IDRENTRY
         TITLE 'P D S  --  PDS SVC 99 PARAMETER LIST          5/12/86'
         IEFZB4D0
         SPACE 3
         IKJCSPL
         SPACE 2
         IKJCSOA
         SPACE 5
         IKJIOPL
**       TITLE 'P D S  --  PDS GTPB DSECT                     5/12/86'
**       IKJGTPB
         TITLE 'P D S  --  PDS UPT / PSCB / ECT DSECTS        5/12/86'
         IKJUPT
         SPACE 3
         IKJPSCB
         SPACE 3
         IKJECT
         TITLE 'P D S  --  PDS (OTHER DSECTS)                 5/12/86'
         IKJCPPL
         SPACE 3
         PRINT NOGEN
         DCBD  DSORG=PO
         SPACE 5
         CVT   DSECT=YES
         SPACE 5
         END
