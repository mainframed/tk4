         MACRO
&NAME    READY &GMMSG=0,&RSMSG=0
         GBLB  &IEDQZA(64)
         AIF   (&IEDQZA(7) EQ 0).ERR3
         AIF   (&IEDQZA(50) EQ 1).ERR4
         AIF   ('&GMMSG' EQ '').ERR1
         AIF   ('&RSMSG' EQ '').ERR2
&IEDQZA(50) SETB 1
&NAME    B     IEDQRDY .                BRANCH AROUND ADCONS
IEDQRDYA DC    A(&GMMSG) .              GOOD MORNING MESSAGE ROUTINE
         DC    A(&RSMSG) .              RESTART-IN-PROGRESS MSG RTN
IEDQRDY  EQU   *
         AIF   (NOT &IEDQZA(51)).NOTSO                              TSO
         L     2,IEDTSOPT .             ADDRESS OF TSO WORK AREA
         L     2,12(2) .                GET ADDRESS OF STAE EXIT
*        STAE  (2),PURGE=HALT
         STAE  (2),PURGE=HALT
.NOTSO   ANOP                                                       TSO
*        LINK  EP=IEDQND                INVOKE CKPT ATTACHER
         LINK  EP=IEDQND .              INVOKE CKPT ATTACHER
         LA    1,IEDSAVE2 .             ADDRESS OF DISP SAVEAREA
         L     15,IEDEA .               TAM DISPATCHER ENTRY POINT
         OI    IEDBIT1,IEDREADN .       SET 'READY DONE' FLAG IN AVT
         BALR  14,15 .                  CALL TAM DISPATCHER
IEDREADN EQU   X'08' .                  READY MACRO HAS BEEN EXECUTED
         MEXIT
.ERR1    MNOTE 12,'***IHB002 INVALID GMMSG OPERAND SPECIFIED-   '
         MEXIT
.ERR2    MNOTE 12,'***IHB002 INVALID RSMSG OPERAND SPECIFIED-   '
         MEXIT
.ERR3    MNOTE 12,'***IHB305 INTRO MACRO NOT SPECIFIED'
         MEXIT
.ERR4    MNOTE 12,'***IHB067 READY MACRO PREVIOUSLY USED '
         MEND
