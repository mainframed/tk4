         MACRO
&NAME    MSGGEN &MASK,&MSG,&CODE=,&IN=(1030,1050,105F,1060,2260,2265,27*
               40,274F,ITA2,ZSC3,TTYA,TTYB,TTYC,6BIT,ASCI,EBCD,BC41,EB4*
               1,CR41),&CONNECT=OR
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &A,&IX,&LEN,&T
         LCLB  &NOGO,&ALG,&BIT
         LCLC  &C
.*             VERSION DATED DECEMBER 18,1974                  @SA72488
.*C540000                                                      @SA72488
         SPACE
&C       SETC  '&SYSECT'
&IX      SETA  1
&T       SETA  10
         AIF   (&IEDQZA(5)).OK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.OK      AIF   ('&MSG' NE '').CHK
         MNOTE 12,'***  IHB004  REQUIRED OPERANDS NOT SPECIFIED'
&NOGO    SETB  1
.CHK     AIF   ('&MASK' EQ '').NXT
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR
         AIF   ('&MASK'(1,1) NE 'X').NXT
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
         AGO   .NXT
.ERR     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NXT     AIF   ('&MSG' EQ '').MSGERR
         AIF   ('&MSG'(K'&MSG,1) EQ '''' AND K'&MSG LT 4).MSGERR
         AIF   ('&MSG'(K'&MSG,1) NE '''' AND K'&MSG GT 8).MSGERR
         AIF   ('&MSG'(K'&MSG,1) NE '''').CODECHK
         AIF   (NOT &IEDQZA(9)).TCAMMSG                             TSO
         IEDQCHAR 52,&MSG                                           TSO
         AGO   .ERRCHK                                              TSO
.TCAMMSG IEDQCHAR 24,&MSG
.ERRCHK  AIF   (&IEDQZA(24)).MSGERR
         AIF   (&IEDQZD LE 24).CODECHK
         AIF   (NOT &IEDQZA(9)).MSGERR
         AIF   (&IEDQZD LE 52).CODECHK                              TSO
.MSGERR  MNOTE 12,'***  IHB300  MESSAGE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CODECHK AIF   ('&CODE' EQ 'NO' OR '&CODE' EQ 'NONE' OR '&CODE' EQ '').*
               AND
&LEN     SETA  4
         AIF   (K'&CODE GT 8).CMN
.USER    AIF   (&IX GT 19).USERTAB
         AIF   ('&CODE' EQ '&IN(&IX)').AND
&IX      SETA  &IX+1
&T       SETA  &T+1
         AGO   .USER
.USERTAB AIF   (('&CODE'(1,1) GE 'A' AND '&CODE'(1,1) LE 'Z') OR '&CODE*
               '(1,1) EQ '$' OR '&CODE'(1,1) EQ '#' OR '&CODE'(1,1) EQ *
               '@').AND
.CMN     MNOTE 12,'***  IHB300  CODE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.AND     AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.OP      AIF   (&NOGO).MEND
         AIF   ('&CONNECT' EQ 'OR').ORG
&ALG     SETB  1
.ORG     AIF   (&IEDQZA(9)).TSO                                     TSO
         IEDQVCON 6,IEDQBL
&A       SETA  6                                                    TSO
         AGO   .NOTSO                                               TSO
.TSO     IEDQVCON 44,IEDAYM                                         TSO
&A       SETA  44                                                   TSO
.NOTSO   AIF   (NOT &IEDQZA(1)).PARM
&IEDQZA(18) SETB 1
.PARM    AIF   ('&MASK' NE '0' AND '&MASK' NE 'X''0000000000''' AND '&M*
               ASK' NE 'XL5''0000000000''' AND '&MASK' NE 'XL5''0''' AN*
               D '&MASK' NE 'XL5''00''' AND '&MASK' NE '').SKP @SA72488
&BIT     SETB  1
.SKP     AIF   (NOT &BIT).M
&NAME    DC    AL1(&IEDQZE(&A)+&BIT),AL1(8+&LEN+&ALG)
         AGO   .CODE
.M       ANOP
&NAME    DC    AL1(&IEDQZE(&A)+&BIT),AL1(12+&LEN+&ALG)
.CODE    AIF   ('&CODE' NE '').X00
         DC    X'80'
         AGO   .SETK
.X00     AIF   ('&CODE' EQ 'NO' OR '&CODE' EQ 'NONE').X10
         DC    AL1(0)
         AGO   .SETK
.X10     DC    X'10'
.SETK    AIF   (NOT &BIT).S
         DC    AL1(0)
         AGO   .MSG
.S       AIF   ('&MASK'(1,1) EQ 'X').DC
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .MSG
.DC      DC    &MASK .                  ERROR MASK
.MSG     AIF   ('&MSG'(K'&MSG,1) EQ '''').SPLIT
         DC    AL4(&MSG) .              ADDRESS OF MESSAGE
         AGO   .CODE1
.SPLIT   ANOP
IEDQMISC CSECT
         DC    AL1(&IEDQZD),&MSG
&C       CSECT
         DC    AL4(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+1+&IEDQZD
.CODE1   AIF   ('&CODE' EQ 'NO' OR '&CODE' EQ 'NONE' OR '&CODE' EQ '').*
               MEND
         AIF   (&IX GT 19).USERTBL
&C       SETC  'IEDQ'.'&T'
         DC    V(&C) .                  STANDARD TABLE
         AGO   .MEND
.USERTBL DC    A(&CODE) .               ADDRESS OF SPECIAL TABLE
.MEND    SPACE 2
         MEND
