         MACRO
&NAME    TCHNG &TERMNME,&WRKAREA,&PASSWRD=
.*C030000,610000-620000                                         SA51790
.*D070000-120000                                                SA51790
.*             VERSION DATED MARCH 23,1972                      SA51790
         AIF   ('&TERMNME' EQ '').ERR0
         AIF   ('&WRKAREA' EQ '').ERR0
&NAME    DS    0H
.NOPLIST ANOP
         AIF   ('&TERMNME'(1,1) EQ '(' OR K'&TERMNME LT 9).OK
         MNOTE 12,'*** IHB002 INVALID TERMNAME OPERAND SPECIFIED'
         MEXIT
.ERR0    ANOP
         MNOTE 12,'*** IHB004 REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.OK      ANOP
         AIF   ('&WRKAREA'(1,1) EQ '(' OR K'&WRKAREA LT 9).OK1
         MNOTE 12,'*** IHB002 INVALID WORKAREA OPERAND SPECIFIED'
         MEXIT
.OK1     ANOP
         AIF   ('&TERMNME'(1,1) NE '(').TAD
         ST    &TERMNME(1),IEDQ&SYSNDX  ADDRESS OF TERMINAL NAME
*                                       INTO PARAMETER LIST
         AGO   .WRK
.TAD     ANOP
         BAL   1,*+12 .                 ADDR OF TERMINAL NAME
         DC    CL8'&TERMNME' .          TERMINAL NAME
         ST    1,IEDQ&SYSNDX .          ADDRESS OF TERMINAL NAME
*                                       INTO PARAMETER LIST
.WRK     ANOP
         AIF   ('&WRKAREA'(1,1) NE '(').PASS
         ST    &WRKAREA(1),IEDQ&SYSNDX+4 ADDRESS OF WORKAREA
*                                       INTO PARAMETER LIST
.PASS    ANOP
         AIF   ('&PASSWRD' EQ '').LIST
         AIF   ('&PASSWRD' NE '' AND K'&PASSWRD LT 9).OK2
         MNOTE 12,'*** IHB002 INVALID PASSWORD OPERAND SPECIFIED'
         MEXIT
.OK2     ANOP
         AIF   ('&PASSWRD' EQ '').LIST
         BAL   1,*+12 .                 ADDRESS OF PASSWORD
         DC    CL8'&PASSWRD' .          UNSCRAMBLED PASSWORD
         ST    1,IEDQ&SYSNDX+8 .        ADDRESS OF PASSWORD
*                                       INTO PARAMETER LIST
.LIST    ANOP
         CNOP  0,4
         BAL   1,*+16 .                 ADDRESS OF PARAMETER LIST
IEDQ&SYSNDX DS 0F .
         DC    A(0) .                   ADDRESS OF TERMINAL NAME
         AIF   ('&WRKAREA'(1,1) NE '(').OK3
         DC    A(0) .                   ADDRESS OF WORKAREA
         AGO   .OK4
.OK3     ANOP
         DC    A(&WRKAREA) .            ADDRESS OF WORKAREA
.OK4     ANOP
         DC    A(0) .                   ADDRESS OF PASSWORD
         LINK  EP=IEDQE3                                        SA51790
         MEND
