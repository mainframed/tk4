         MACRO
&NAME    FULLSCR &TRIGGER=X'6D',&LCOUNT=X'00',&CARRIER=X'00'
         AIF   ('&TRIGGER'(1,1) NE 'X').MNOTE2
         AIF   (&LCOUNT GT 24).MNOTE3
         AIF   (&CARRIER GT 80).MNOTE4
         AIF   (K'&TRIGGER NE 5).MNOTE2
         AIF   (K'&NAME GT 7).MNOTE5
.**********************************************************************
.*                                                                    *
.*  MACRO NAME= FULLSCR                                               *
.*                                                                    *
.*  DESCRIPTIVE NAME = WRITE FULL SCREEN OF DATA TO 3270              *
.*                                                                    *
.*  COPYRIGHT = 'NONE'                                                *
.*                                                                    *
.*  STATUS:  CHANGE LEVEL 5                                           *
.*                                                                    *
.* CHANGE ACTIVITY AS FOLLOWS                                         *
.* C720000                                                     @YA06331
.*A264000,384000,400000,416000                                 @OZ18764
*    REGISTER    USAGE
*    REG 1    ADDRESS OF TERM TABLE ENTRY
*    REG 1    ADDRESS OF BUFFER
*    REG 14   ADDRESS QCB
*    REG 15   ADDRESS OF TSO OPTION FIELD
*    REG 15   RETURN CODES
*             X'00'    GOOD RETURN
*             X'04'    TRIGGER NOT FOUND
*             X'08'    OPTION FIELD NOT FOUND
*             X'0C'    NOT A TSO MESSAGE
*    OPTION FIELD-IEDQFSCR - X'01' INDICATES FULL SCREEN MODE
*                            X'10' INDICATES 3705 CTL UNIT
&NAME    L     1,IEDADBUF         *    ADDRESS OF BUFFER
         TM    20(1),X'10'        *    IS THIS A TS MESSAGE
         BNO   &NAME.E            *    NO,DO NOTHING
         L     1,12(1)            *    ADDRESS OF LCB
         MVC   &NAME.P+3(1),28(1)      STORE RESERVE(PADS) LENGTH
         LH    1,90(1)            *    GET INDEX TO TERM ENTRY
         LTR   1,1                     IS TTCIN ZERO
         BZ    &NAME.E                 YES GIVE 0C RETURN
         L     15,IEDRNMPT        *    TERM NAME TABLE ROUTINE
         BALR  14,15              *    EXECUTE IT
         ST    1,&NAME.K               STORE TERM TABLE ENTRY ADDR
         B     &NAME.J                 BRANCH AROUND STORAGE
&NAME.K  DS    F                       STORE REG1 DURING LOCOPT
&NAME.P  DC    XL4'00'                 RESERVE(PAD) LENGTH
&NAME.L  DC    X'000000FF'              MASK FOR 'AND'         @OZ18764
*        LOCOPT IEDQFSCR     *    LOCOPT IS USED TO FIND OPT FIELD
*                            *    IF MNOTES OCCUR ON NEXT STATEMENT
*                            *    CHECK TCAM PROG GUIDE UNDER LOCOPT
&NAME.J  EQU   *
         LOCOPT IEDQFSCR
         LTR   15,15                   GOOD RETURN
         BZ    &NAME.F                 OPTION FIELD NOT FOUND
         L     1,&NAME.K               LOAD TERM TABLE ADDR
         L     14,0(,1)                GET QCB ADDR
         AIF   (&TRIGGER EQ X'00').OFF
         AIF   (&TRIGGER EQ X'FF').ON
         TM    11(1),X'10'             IS THIS A 3705 TERMINAL
         BZ    &NAME.I                 NO,BRANCH
         OI    0(15),X'10'             TURN ON OPTION FLD BIT
&NAME.I  EQU   *
         IC    1,X'10'(,1)              GET INDEX TO DEV CHAR  @OZ18764
         N     1,&NAME.L                CLEAR ALL EXCEPT INDEX @OZ18764
         BCTR  1,0                      DECREMENT BY ONE       @OZ18764
         SLL   1,2                      MULTIPLY BY FOUR       @OZ18764
         A     1,X'100'(,13)            ADD START OF CHAR TABLE@OZ18764
         TM    2(1),X'02'               IS THIS A LOCAL?       @OZ18764
         BZ    &NAME.M                  NO                     @OZ18764
         L     1,IEDADBUF              ADDRESS OF BUFFER
         TM    20(1),X'01'              IS IT A HDR?           @OZ18764
         BZ    &NAME.Q                  YES                    @OZ18764
         LA    1,4                      NOT FOUND              @OZ18764
         B     &NAME.D                                         @OZ18764
&NAME.Q  EQU   *                                               @OZ18764
         A     1,&NAME.P                ADD LENGTH OF PADS     @OZ18764
         CLI   42(1),&TRIGGER          IS THIS LOCAL FULLSCR MODE
         BE    &NAME.C                 YES, FLIP SWITCH
         B     &NAME.G                                         @OZ18764
&NAME.M  EQU   *                                               @OZ18764
         L     1,IEDADBUF               GET BUFFER ADDRESS     @OZ18764
         TM    20(1),X'01'              IS IT A HDR?           @OZ18764
         BZ    &NAME.R                  YES                    @OZ18764
         LA    1,4                      NOT FOUND              @OZ18764
         B     &NAME.D                                         @OZ18764
&NAME.R  EQU   *                                               @OZ18764
         A     1,&NAME.P                ADD LENGTH OF PADS     @OZ18764
         TM    0(15),X'10'             IS THIS A 3705 TERMINAL
         BO    &NAME.A                 YES BRANCH
         CLI   45(1),&TRIGGER          IS THIS REMOTE FULLSCR MODE
         B     &NAME.G                 BRANCH AROUND 3705 COMPARE
&NAME.A  EQU   *
         CLI   44(1),&TRIGGER          IS THIS FULLSCREEN MODE ON 3705
*                                      SUPPORTED DISPLAY
&NAME.G  EQU   *
         LA    1,4                     SET TRIGGER NOT FOUND TEMP SWT
         BNE   &NAME.D                 NO, DO NOT FLIP
&NAME.C  EQU   *
         XI    0(15),X'01'             FLIP MODE SWITCH
         LA    1,0                     GOOD RETURN
&NAME.D  EQU   *
         TM    0(15),X'01'             IS ENTRY FOR 'FULLSCR'
         BZ    &NAME.B                 NO, DO NOT RESET
         MVI   21(14),&LCOUNT        *   ZERO LINE COUNT
         MVI   29(14),&CARRIER      *    ZERO CARRIAGE COUNT
&NAME.B  EQU   *
         LR    15,1                    PICK UP RETURN CODE
         B     &NAME.H                 EXIT
&NAME.E  EQU   *
         LA    15,12                   NOT A TSO MESSAGE
         B     &NAME.H                 EXIT
         AGO   .COND
.ON      OI    0(15),X'01'             TURN MODE SWT ON
         MVI   21(14),&LCOUNT        *   ZERO LINE COUNT
         MVI   29(14),&CARRIER      *    ZERO CARRIAGE COUNT
         SR    15,15                   GOOD RETURN
         B     &NAME.H                 EXIT
&NAME.F  EQU   *
         LA    15,8                    OPTION FIELD NOT FOUND
         B     &NAME.H
&NAME.E  EQU   *
         LA    15,12                   NOT A TSO MESSAGE
&NAME.H  EQU   *                       CONTINUE
         MEXIT
.OFF      NI   0(15),X'FE'              TURN MODE SWITCH OFF   @YA06331
         SR    15,15                   GOOD RETURN
         B     &NAME.H                 EXIT
&NAME.F  EQU   *
         LA    15,8                    OPTION FIELD NOT FOUND
         B     &NAME.H
&NAME.E  EQU   *
         LA    15,12                   NOT A TSO MESSAGE
&NAME.H  EQU   *                       CONTINUE
         MEXIT
.COND    ANOP
&NAME.F  EQU   *
         LA    15,8                    OPTION NOT FOUND
&NAME.H  EQU   *
         MEXIT
.MNOTE2  MNOTE 8,' IHB289  TRIGGER VALUE MUST BE A HEX DIGIT'
         MEXIT
.MNOTE3  MNOTE 8,' IHB290  LCOUNT VALUE MUST BE DECIMAL LESS THAN OR EQU
               AL TO 24'
         MEXIT
.MNOTE4  MNOTE 8,' IHB291  CARRIAGE VALUE MUST BE DECIMAL LESS THAN OR E
               QUAL TO 80'
         MEXIT
.MNOTE5  MNOTE 8,' IHB292  LABEL MUST BE 7 CHARS OR LESS'
         MEND
