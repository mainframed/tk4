         MACRO
&N       INTRO &PROGID=,&CROSSRF=0,&DISK=YES,                          *
               &MSMIN=50,&MSMAX=70,&DLQ=,&CPRCDS=2,&TOPMSG=YES,        *
               &CONTROL=,&PASSWRD=,&CPB=0,                             *
               &PRIMARY=SYSCON,&INTVAL=0,&CKREQS=0,                    *
               &STARTUP=,&COMWRTE=NO,&WTTONE=0,&APDUMP=YES,    @ZA07083*
               &TRACE=0,&CIB=2,&ENVIRON=TCAM,                   SA65392*
               &USEREG=0,&OLTEST=18,&DTRACE=0,                 @SA74046*
               &RESTART=0,&LINETYP=BOTH,&THRESH=(255,1,1,1),    SA65392*
               &FEATURE=(DIAL,2741,TIMER,NOCONC,NO3705),               *
               &BTRACE=0,&PLCBNO=0,&COMMBUF=,                          *
               &LNUNITS=0,&MSUNITS=0,&CPINTVL=1800,&KEYLEN=,&UNITSZ=
.* CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C032000,595000,601000-602000,665000                           SA51078
.*A043900,579500,596200-596600,665300-665600                    SA51078
.*C142000                                                        S21903
.*D286000-294000                                                 S21903
.*A286000                                                        S21903
.*C010000,032000,057000,354550,416000                            S22027
.*A014100,030500,043800,045300-045600,327000-343960              S22027
.*A354554-354590,416090-416900                                   S22027
.*D327000-327600                                                 S22027
.*C008000,311000,315000,317000                                   S99258
.*C057000,684000                                                 S22029
.*D685000-687000                                                 S22029
.*D327000,354500                                                 S22026
.*C010000,032000                                                 S22026
.*A014500,043300-043600,066050-066850,326500-327500              S22026
.*A343090-343900,347300-347600,354460-354520                     S22026
.*                               CLEAN-UP                        S21903
.*A014000,074000                                                SA50203
.*C085000                                                       SA62343
.*A396000                                                       SA63008
.*A030700,285000                                                SA65392
.*C007000,009000                                                SA65392
.*D286000                                                       SA65392
.*A143000                                                       OX02212
.*D690000                                                       OX02212
.*A311000                                                       SA68227
.*C315000,317000                                                OY04908
.*C670000                                                      @SA71946
.*A600000                                                      @XA05310
.*C601000-602000,604000                                        @XA05310
.*D571000                                                      @XA05310
.*C008000                                                      @SA74046
.*A032000,043900                                               @ZA07083
.*C006000,327780-327810                                        @ZA07083
.*A043950,579500                                               @OS77945
.*C032500,382000,573000,595000,596400                          @OS77945
         GBLA  &IEDQZF
.* &IEDQZF NUMBER WORDS TO BE ADDED TO SCB FOR SAVEAREA
         GBLB  &IEDQRN(8)                                        S22024
         GBLB  &IEDQZA(64)
.* &IEDQZA(44) SET BY COMMBUF=(N,N,N) OPERAND                    S22027
.* &IEDQZA(60) SET BY &FEATURE=NODIAL                           SA50203
.* &IEDQZA(13) SET BY FEATURE=(,,,CONCO) OR FEATURE=(,,,CONC)    S22026
.* &IEDQZA(42) SET BY NON-ZERO RESPONSE TO MSUNITS=INTEGER
.* &IEDQZA(51) SET BY &ENVIRON=TSO OR MIXED
.* &IEDQZA(52) SET BY &ENVIRON=TCAM OR MIXED
.* &IEDQZA(41) SET BY DISK=YES
.* &IEDQZA(7)  SET BY INTRO, DISTINGUISHES TCAM FROM QTAM.
.*             CHECKED BY INTRO TO AVOID DUPLICATE INTROS.
         LCLA  &IEDLKEY
         LCLA  &IEDLSA
.* &IEDLSA IS INDEX THRU STARTUP OPERANDS
         LCLA  &IEDLSCB
         LCLA  &IEDLTON
         LCLA  &IEDLMIN,&IEDLMAX
         LCLA  &IEDLMS
         LCLA  &IEDLTRA
.* &IEDLTRA IS TRACE OPERAND SAVED FOR TREXIT
         LCLA  &A                                                S22027
         LCLA  &B                                                S22027
         LCLA  &C,&D,&E,&F              THRESHHOLD VALUES       SA65392
         LCLB  &IEDLDSK
         LCLB  &IEDLSB(16)                                     @OS77945
.* &IEDLSB(1)  STARTUP=C OR W SPECIFIED
.* &IEDLSB(2)  STARTUP=C SPECIFIED
.* &IEDLSB(3)  STARTUP=I SPECIFIED
.* &IEDLSB(4)  STARTUP=Y SPECIFIED
.* &IEDLSB(5)  LINETYP=STSP OR LINETYP=BOTH SPECIFIED - ZEROED
.* &IEDLSB(6)  LINETYP=BISC OR LINETYP=BOTH SPECIFIED - ZEROED
.* &IEDLSB(5) AND (6) BOTH SET IF LINETYP=MINI SPECIFIED
.* &IEDLSB(7)  TOPMSG=NO SPECIFIED
.* &IEDLSB(8)  FEATURE=NODIAL
.* &IEDLSB(9)  FEATURE=(,NO2741)
.* &IEDLSB(10) FEATURE=(,,NOTIMER)
.* &IEDLSB(11) FEATURE=(,,,CONC)                                 S22026
.* &IEDLSB(12) FEATURE=(,,,CONCO)                                S22026
.* &IEDLSB(13) COMMBUF=(N,N,N) SPECIFIED                         S22027
.* &IEDLSB(14) STARTUP=E SPECIFIED                              SA51078
.* &IEDLSB(15) APDUMP=YES SPECIFIED                            @ZA07083
.* &IEDLSB(16) STARTUP=N SPECIFIED                             @OS77945
         LCLC  &IEDLSEC
         LCLC  &IEDLSKP
         LCLC  &IEDCBUF(3)                                       S22027
.* &IEDCBUF SET TO COMBUF VALUES                                 S22027
&IEDLSB(5) SETB 1                       SET TO DEFAULT OF
&IEDLSB(6) SETB 1                         LINETYP=BOTH
&IEDLSKP SETC  'IEDQ&SYSNDX'
         AIF   (NOT &IEDQZA(7)).GOODINT
         SPACE
         MNOTE 12,'IHB067 INTRO MACRO PREVIOUSLY SPECIFIED'
         SPACE 2
         MEXIT
.GOODINT ANOP
&IEDQZA(7) SETB 1
         SPACE
****      INTRO UPDATED LEVEL  ' 05E '    ************
         SPACE 2
&IEDQZA(52) SETB ('&ENVIRON' EQ 'TCAM' OR '&ENVIRON' EQ 'MIXED')
&IEDQZA(51) SETB ('&ENVIRON' EQ 'TSO' OR '&ENVIRON' EQ 'MIXED')
         AIF   (&IEDQZA(51) OR &IEDQZA(52)).SKIPINV
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND ENVIRON, TCAM ASSUMED'
         SPACE 2
&IEDQZA(52) SETB 1                      SET DEFAULT TO 'TCAM'
.SKIPINV ANOP
         AIF   ('&FEATURE(4)' NE 'CONCO').CTEST2                 S22026
&IEDLSB(12) SETB 1                      FEATURE=(,,,CONCO)       S22026
&IEDQZA(13) SETB 1                      FEATURE=(,,,CONCO)       S22026
&IEDLSB(8)  SETB 1                      FORCE FEATURE=NODIAL     S22026
&IEDLSB(9)  SETB 1                      FORCE FEATURE=(,NO2741)  S22026
         AGO   .GOODFEA                                          S22026
.CTEST2  AIF   ('&FEATURE(4)' NE 'CONC').CTEST3                  S22026
&IEDLSB(11) SETB 1                      FEATURE=(,,,CONC)        S22026
&IEDQZA(13) SETB 1                      FEATURE=(,,,CONC)        S22026
         AGO   .DTST                                             S22026
.CTEST3  AIF   ('&FEATURE(4)' EQ 'NOCONC' OR '&FEATURE(4)' EQ '').DTST
.*                                                               S22026
         SPACE 1                                                 S22026
         MNOTE 12,'*** IHB303 INVALID OPERAND FEATURE, (,,,NOCONC) ASSUX
               MED' .                                            S22026
         SPACE 2                                                 S22026
.DTST    ANOP                                                    S22026
         AIF   ('&FEATURE(1)' EQ 'DIAL' OR '&FEATURE(1)' EQ '').DIAL
         AIF   ('&FEATURE(1)' EQ 'NODIAL').NODIAL
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND FEATURE, DIAL ASSUMED'
         SPACE 2
         AGO   .DIAL
.NODIAL  ANOP
&IEDLSB(8) SETB 1                       FEATURE=NODIAL
&IEDQZA(60) SETB 1                                              SA50203
.DIAL    ANOP
         AIF   ('&FEATURE(2)' EQ '2741' OR '&FEATURE(2)' EQ '').T2741
         AIF   ('&FEATURE(2)' EQ 'NO2741').NO2741
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND FEATURE, (,2741) ASSUMED'
         SPACE 2
         AGO   .T2741
.NO2741  ANOP
&IEDLSB(9) SETB 1                       FEATURE=(,NO2741)
.T2741   ANOP
         AIF   (&IEDQZA(51) AND &IEDLSB(9)).BADFEA              SA62343
         AGO   .GOODFEA
.BADFEA  ANOP
&IEDLSB(8) SETB 0
&IEDLSB(9) SETB 0
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND FEATURE, (DIAL,2741) ASSUMED'
         SPACE 2
.GOODFEA AIF   ('&FEATURE(3)' EQ 'TIMER' OR '&FEATURE(3)' EQ '').TIMER
         AIF   ('&FEATURE(3)' EQ 'NOTIMER').NOTIME
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND FEATURE, (,,TIMER) ASSUMED'
         SPACE 2
         AGO   .TIMER
.NOTIME  ANOP
&IEDLSB(10) SETB 1                      FEATURE=(,,NOTIMER)
.TIMER   ANOP
&IEDQRN(1)  SETB  1                                              S22024
         AIF   ('&FEATURE(5)' EQ 'NO3705' OR '&FEATURE(5)' EQ          *
               '').NO3705                                        S22024
         AIF   ('&FEATURE(5)' EQ 'MIXD3705').T3705               S22024
         AIF   ('&FEATURE(5)' NE 'ONLY3705').BAD3705             S22024
&IEDQRN(2)  SETB  1                                              S22024
         AGO   .T3705                                            S22024
.BAD3705 ANOP                                                    S22024
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND FEATURE, (,,,,NO3705) ASSUMED*
               '                                                 S22024
         SPACE 2
.NO3705  ANOP                                                    S22024
&IEDQRN(1)  SETB  0                                              S22024
.T3705   ANOP                                                    S22024
         AIF   (&IEDQZA(52)).SETCOR
         AGO   .NODISK                  SKIP IF TSO ONLY
.SETCOR  ANOP
         AIF   ('&MSUNITS' EQ 'YES').COREYES
         AIF   ('&MSUNITS' EQ 'NO').CORENO
         IEDQMASK &MSUNITS,2
         AIF   (&IEDQZA(24)).CORERR
         AIF   ('&MSUNITS'(1,1) EQ 'X' OR &MSUNITS LT 0).CORERR
         AIF   (&MSUNITS EQ 0).CORENO
&IEDLMS  SETA  &MSUNITS
.SETGBL  ANOP
&IEDQZA(42) SETB 1                      MSUNITS REQUESTED
         AGO   .COROUT
.COREYES ANOP
         MNOTE 12,'***IHB303 INVALID OPERAND MSUNITS, 1 ASSUMED'
&IEDLMS  SETA  1                        MSUNITS=1 DEFAULT VALUE
         AGO   .SETGBL
.CORENO  ANOP
&IEDLMS  SETA  0                        MSUNITS=0 OR NO
         AIF   ('&DISK' NE 'NO').COROUT
         SPACE
         MNOTE 12,'IHB300 MSUNITS/DISK OPERAND INVALID AS SPECIFIED'
         SPACE 2
         AGO   .COROUT
.CORERR  ANOP
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND MSUNITS, 1 ASSUMED'
         SPACE 2
         AGO   .COREYES
.COROUT  ANOP
         AIF   ('&DISK' EQ 'NO').NODISK
         AIF   ('&DISK' EQ 'YES').DODSK
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND DISK, YES ASSUMED'
         SPACE 2
.DODSK   ANOP
&IEDQZA(41) SETB 1                      DISK=YES
.NODISK  ANOP
         CNOP  0,8 .                    FORCE DOUBLEWORD BOUNDARY
         USING *,15 .                   TEMPORARY BASE
&N       B     &IEDLSKP .               SKIP CONSTANTS
          DC   AL1(3),C'05E'            SYSTEM TCAM LEVEL  05E
&IEDLDSK SETB  ('&DISK' NE 'NO' AND &IEDQZA(52))
&IEDLSEC SETC  '&SYSECT'                                        OX02212
&IEDLSEC CSECT                                                  OX02212
IEDQAVT  DS    0D
         ENTRY IEDQAVT
IEDSAVE1 DC    18F'0' .                 MESSAGE CONTROL PROG SAVEAREA
IEDSAVE2 DC    14F'0',A(IEDQAVT),3F'0' .DISPATCHER SAVEAREA
IEDSAVE3 DC    18F'0' .                 SUBTASK'S SAVEAREA
IEDSAVE4 DC    18F'0' .                 1ST LEVEL SUBROUTINE SAVEAREA
         DC    8F'0' .                  DISABLED SAVEAREA
         AIF   ('&DLQ' EQ '').NODLQ
         AIF   ('&DLQ'(1,1) EQ '0' AND K'&DLQ EQ 1).NODLQ
         AIF   (K'&DLQ GT 8).BADDLQ
         DC    CL8'&DLQ' .              DLQ=TERMNAME
         AGO   .SKIPDLQ
.BADDLQ  SPACE
         MNOTE 12,'IHB304 DLQ SPECIFIED TOO MANY CHARACTERS'
         SPACE 2
.NODLQ   DC    2F'0' .                  DLQ=TERMNAME NOT SPECIFIED
.SKIPDLQ ANOP
         DC    A(IEDQSTCS,0) .
         AIF   ('&PRIMARY' EQ 'SYSCON' OR '&PRIMARY' EQ '').NOPRI
         AIF   ('&PRIMARY'(1,1) EQ '0' AND K'&PRIMARY EQ 1).NOPRI
         AIF   (K'&PRIMARY GT 8).BADMARY
         DC    CL8'&PRIMARY' .          PRIMARY=TERMNAME
         AGO   .SKIPRI
.BADMARY SPACE
         MNOTE 12,'IHB304 PRIMARY SPECIFIED TOO MANY CHARACTERS'
         SPACE 2
.NOPRI   DC    8X'00' .                 PRIMARY=SYSCON
.SKIPRI  ANOP
IEDDOUBL DC    2F'0' .
         AIF   ('&CONTROL' EQ '').NOTROL
         AIF   ('&CONTROL'(1,1) EQ '0' AND K'&CONTROL EQ 1).NOTROL
         AIF   (K'&CONTROL GT 8).BADTROL
         DC    CL8'&CONTROL' .          CONTROL=CHARS SPECIFIED
         AGO   .SKIPTRL
.BADTROL SPACE
         MNOTE 12,'IHB304 CONTROL SPECIFIED TOO MANY CHARACTERS'
         SPACE 2
.NOTROL  DC    8X'FF' .                 CONTROL=CHARS OMITTED
.SKIPTRL AIF   ('&PASSWRD' EQ '').NOPASS
         AIF   ('&PASSWRD'(1,1) EQ '0' AND K'&PASSWRD EQ 1).NOPASS
         AIF   (K'&PASSWRD GT 8).BADPAS
         DC    CL8'&PASSWRD' .          PASSWRD=CHARS SPECIFIED
         AGO   .SKIPASS
.BADPAS  SPACE
         MNOTE 12,'IHB304 PASSWRD SPECIFIED TOO MANY CHARACTERS'
         SPACE 2
.NOPASS  DC    CL8' ' .                 PASSWRD=CHARS OMITTED
.SKIPASS ANOP
         DC    A(0) .                   ADDR OF MESSAGE CONTROL TCB
         IEDQMASK &TRACE,2
         AIF   (&IEDQZA(24)).BADTRA
         AIF   ('&TRACE'(1,1) EQ 'X' OR &TRACE LT 0).BADTRA
IEDRACE  DC    F'&TRACE' .                   TRACE=INTEGER
&IEDLTRA SETA  &TRACE                   SAVE TRACE OPERAND FOR TREXIT
         AGO   .SKPTRA
.BADTRA  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND TRACE, 0 ASSUMED'
         SPACE 2
IEDRACE  DC    F'0' .                   TRACE=0 DEFAULT VALUE
.SKPTRA  ANOP
IEDREADY DC    A(IEDDELEM) .            ADDRESS OF FIRST OF CHAIN
*                                         OF ELEMENTS,PRIORITY ORDER
IEDREADD DC    AL1(4),AL3(0),A(0) .     FIFO DISABLED READY QUEUE
IEDCKGET DC    A(0) .                   CHECKPOINT WORKAREA-GETMAIN
IEDOCGET DC    A(0) .                   OPERATOR CONTROL WORKAREA
&IEDLSCB SETA  84 .                     SIZE OF SCB WITHOUT SAVEAREA
         IEDQMASK &USEREG,1
         AIF   (&IEDQZA(24)).ERREG
         AIF   (&USEREG EQ 0).ZEREG
         AIF   ('&USEREG'(1,1) EQ 'X' OR &USEREG GT 10).ERREG
&IEDQZF  SETA  &USEREG
         MVC   &IEDLSCB.(4*&IEDQZF,3),28(13) USEREG=INTEGER
         MVC   28(4*&IEDQZF,13),&IEDLSCB.(3)
.* &IEDQZF IS GLOBAL, NUMBER WORDS TO BE ADDED TO SCB FOR SAVEAREA
         AGO   .SKIPREG
.ZEREG   ANOP
&IEDQZF  SETA  0                        ASSUME 0 DEFAULT VALUE
         NOPR  0 .                      USEREG=0
         DC    4X'00' .
         NOPR  0 .
         DC    4X'00' .
         AGO   .SKIPREG
.ERREG   SPACE
         MNOTE 12,'IHB303 INVALID OPERAND USEREG, 0 ASSUMED'
         SPACE 2
         AGO   .ZEREG
.SKIPREG ANOP
&IEDLSCB SETA  &IEDLSCB+4*&IEDQZF
IEDPARM  DC    A(0,IEDQAVT,0) .         PARAMETER LIST
         IEDQMASK &DTRACE,2
         AIF   (&IEDQZA(24)).DTRERR
         AIF   ('&DTRACE'(1,1) EQ 'X' OR &DTRACE LT 0).DTRERR
IEDDISTR DC    F'&DTRACE' .                   DTRACE=INTEGER
         AGO   .DTROUT
.DTRERR  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND DTRACE, 0 ASSUMED'
         SPACE 2
IEDDISTR DC    F'0' .                   DTRACE=0 DEFAULT VALUE
.DTROUT  ANOP
IEDRNMPT DC    A(IEDQTNT) .             TERMNAME TABLE POINTER
         DC    A(IEDQRDYA) .            USER EXIT ADDRESS IN READY
         DC    A(0) .                   LINE END BSC MSG SCAN
         DC    A(IEDDUMBR) .            LINE TRACE TABLE UPDATE
         DC    A(IEDCLOSB,IEDCLOSB)     QPOST PARAMS
IEDSPLPT DC    A(0) .                   START PARAMETER LIST POINTER
         IEDQMASK &CIB,1
         AIF   (&IEDQZA(24)).CIBERR
         AIF   ('&CIB'(1,1) EQ 'X').CIBERR
         AIF   ('&CIB' EQ '0').CIBERR                            S21903
         DC    AL1(&CIB) .              CIB=INTEGER
         AGO   .CIBOUT
.CIBERR  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CIB, 2 ASSUMED'
         SPACE 2
         DC    AL1(2) .                 CIB=2 DEFAULT VALUE
.CIBOUT  ANOP
         IEDQMASK &CKREQS,1
         AIF   (&IEDQZA(24)).BADREQ
         AIF   ('&CKREQS'(1,1) EQ 'X' OR &CKREQS LT 0).BADREQ
         DC    AL1(&CKREQS) .           CKREQS=INTEGER
         AGO   .SKPREQ
.BADREQ  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CKREQS, 0 ASSUMED'
         SPACE 2
         DC    AL1(0) .                 CKREQS=0 DEFAULT VALUE
.SKPREQ  ANOP
         IEDQMASK &LNUNITS,2
         AIF   (&IEDQZA(24)).LINERR
         AIF   ('&LNUNITS'(1,1) EQ 'X' OR &LNUNITS LT 0).LINERR
         DC    AL2(&LNUNITS) .          LNUNITS=INTEGER
         AGO   .LINOUT
.LINERR  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND LNUNITS, 0 ASSUMED'
         SPACE 2
         DC    AL2(0) .                 LNUNITS=0 DEFAULT VALUE
.LINOUT  ANOP
IEDAS    DC    A(0) .                   RELEASE
         DC    4F'0' .                  TCB ADDRS OF ATTACHED TASKS
         DC    X'40',AL3(0) .           COMMON WRITE FE TASK ECB
         DC    3F'0' .                  EVENT CONTROL BLOCKS
IEDOSECB DC    F'0' .                   DISPATCHER'S ECB
IEDPCBPT DC    A(0) .                   FIRST PROCESS CONTROL BLOCK
IEDOPTPT DC    F'0' .                   ORGED BY OPTION,OPTION TAB ADDR
         AIF   (NOT &IEDQRN(2)).IOGEN                            S22024
IEDKA02  DC    A(0)                                              S22024
         AGO   .SKIOGN                                           S22024
.IOGEN   ANOP                                                    S22024
IEDKA02  DC    V(IEDQKA02) .            I/O GENERATOR
.SKIOGN  ANOP                                                    S22024
&C       SETA  0                                                SA65392
.THRESH1 ANOP                                                   SA65392
&C       SETA  &C+1                                             SA65392
         AIF   (T'&THRESH(&C) NE 'N').THRESH2                   SA65392
         AIF   (&THRESH(&C) GT 0 AND &THRESH(&C) LT 256).THRESH3
.THRESH2 MNOTE 12,'*** IHB002 INVALID THRESH OPERAND SPECIFIED - &THRESC
               H(&C)'                                           SA65392
         AGO   .THRESH6                                         SA65392
.THRESH3 AIF   (&C NE 4).THRESH1                                SA65392
&C       SETA  &THRESH(1)                                       SA65392
&D       SETA  &THRESH(2)                                       SA65392
&E       SETA  &THRESH(3)                                       SA65392
&F       SETA  &THRESH(4)                                       SA65392
         AIF   (&D LE &C).THRESH4                               SA65392
&D       SETA  &C                                               SA65392
.THRESH4 AIF   (&E LE &C).THRESH5                               SA65392
&E       SETA  &C                                               SA65392
.THRESH5 AIF   (&F LE &C).THRESH6                               SA65392
&F       SETA  &C                                               SA65392
.THRESH6 ANOP                                                   SA65392
         DC    AL1(&C),AL1(&D),AL1(&E),AL1(&F)                  SA65392
.SKIPTR  ANOP
         IEDQMASK &CROSSRF,2
         AIF   (&IEDQZA(24)).CROSSER
         AIF   ('&CROSSRF'(1,1) EQ 'X').CROSSER
IEDCRSRF DC    F'&CROSSRF' .            CROSSRF=INTEGER
         AGO   .CROSSOT
.CROSSER SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CROSSRF, 0 ASSUMED'
         SPACE 2
IEDCRSRF DC    F'0' .                   CROSSRF=0 DEFAULT VALUE
.CROSSOT ANOP
         DS    A(0) .                   ADDR OF COMMUNICATIONS PARM
IEDUI    DC    V(IEDQUI) .              USER INTERFACE ROUTINE   S22025
IEDE8    DC    A(0) .                   BIN SEARCH APPLICATION PGS22025
         IEDQMASK &OLTEST,1
         AIF   (&IEDQZA(24)).EROLT
         AIF   ('&OLTEST'(1,1) EQ 'X' OR &OLTEST LT 0).EROLT
         AIF   (&OLTEST LT 18 AND &OLTEST GT 0).EROLT           SA68227
         DC    AL1(&OLTEST) .           OLTEST=INTEGER
         AGO   .SKPOLT
.EROLT   SPACE
         MNOTE 12,'IHB303 INVALID OPERAND OLTEST, 18 ASSUMED'   OY04908
         SPACE 2
         DC    AL1(18)                  OLTEST=18 DEFAULT       OY04908
.SKPOLT  ANOP
         DC    VL3(IEDQHG02) .          REMOVE TIME DELAY ELEMENT
IEDAL    DC    V(IEDQAL) .              ADDRESS COMPUTATION
IEDGD    DC    V(IEDQGD) .              BUFFER ASSOCIATION
IEDA3    DC    A(0) .                   TRANSPARENCY CCW BUILDER (GT)
IEDAX    DC    V(IEDQAX) .              SCAN
IEDEA    DC    A(0) .                   DISPATCHER
IEDDISP  EQU   *
         DC    A(0) IGG019R3            RECEIVE SCHEDULER
         AIF   ('&COMMBUF' EQ '').NCMBUF                         S22027
         AIF   (N'&COMMBUF EQ 3).CMBFOK                          S22027
.CMBERR  MNOTE 12,'IHB303 INVALID OPERAND COMMBUF'               S22027
&IEDQZA(44) SETB 0                                               S22027
         AGO   .NCMBUF                                           S22027
.CMBFOK  ANOP                                                    S22027
&IEDQZA(44) SETB 1                                               S22027
         AIF   (K'&COMMBUF(1) GT 8 OR K'&COMMBUF(2) GT 8 OR K'&COMMBUF(X
               3) GT 8).CMBERR                                   S22027
.LOOP    ANOP                                                    S22027
         AIF   (&A EQ 3).CKCMBUF                                 S22027
&A       SETA  &A+1                                              S22027
&IEDCBUF(&A) SETC '&COMMBUF(&A)'                                 S22027
         IEDQMASK &IEDCBUF(&A),2                                 S22027
         AIF   (NOT &IEDQZA(24)).LOOP                            S22027
         MNOTE 12,'IHB303 INVALID OPERAND &COMMBUF(&A)'          S22027
&IEDQZA(44) SETB 0                                               S22027
         AIF   (&A LT 3).LOOP                                    S22027
.CKCMBUF AIF   (NOT &IEDQZA(44)).NCMBUF                          S22027
         AIF   (&COMMBUF(1) EQ 0 OR &COMMBUF(2) EQ 0 OR &COMMBUF(3) EQ X
               0).NCMBUF1                                        S22027
&A       SETA  &IEDCBUF(3)                                       S22027
&B       SETA  &A/4                                              S22027
&B       SETA  &B*4                                              S22027
         AIF   (&B NE &A).NCMBUF1                                S22027
&IEDLSB(13) SETB 1                                               S22027
         AGO   .NCMBUF                                           S22027
.NCMBUF1 ANOP                                                    S22027
&IEDQZA(44) SETB 0                                               S22027
         MNOTE 12,'IHB303 INVALID OPERAND COMMBUF'               S22027
.NCMBUF  ANOP                                                    S22027
         AIF   ('&APDUMP' EQ 'YES').APDMP                      @ZA07083
         AIF   ('&APDUMP' EQ 'NO').NODMP                       @ZA07083
         MNOTE 4,'IHB303 INVALID OPERAND APDUMP,YES ASSUMED'   @ZA07083
.APDMP   ANOP                                                  @ZA07083
&IEDLSB(15) SETB 1                                             @ZA07083
.NODMP   ANOP                                                  @ZA07083
         DC    B'00&IEDLSB(15)&IEDLSB(13)&IEDLSB(11)&IEDLSB(12)&IEDLSB(X
               9)&IEDLSB(8)' .                                 @ZA07083
         AIF   (&IEDLSB(8)).XXDIAL
*                                         DIAL
         AGO   .XXSKDI
.XXDIAL  ANOP
*                                         NODIAL
.XXSKDI  AIF   (&IEDLSB(9)).XX27
*                                         2741
         AGO   .XXSK27
.XX27    ANOP
*                                         NO2741
.XXSK27  AIF   (&IEDLSB(10)).XXTI
*                                         TIMER
         AGO   .XXSKTI
.XXTI    ANOP
*                                         NOTIMER
.XXSKTI  ANOP
         AIF   (&IEDLSB(11) OR &IEDLSB(12)).XXCONC               S22026
*                                         NOCONC                 S22026
         AGO   .XXCONCX                                          S22026
.XXCONC  AIF   (&IEDLSB(11)).XXCONCM                             S22026
*                                         CONCO (OVERIDES DIAL   S22026
*                                         AND 2741 IF SPECIFIED) S22026
         AGO   .XXCONCX                                          S22026
.XXCONCM ANOP                                                    S22026
*                                         CONC                   S22026
.XXCONCX ANOP                                                    S22026
         AIF   (NOT &IEDQZA(44)).NOCBCMT                         S22027
*                                  COMMBUF KEYWORD SPECIFIED     S22027
.NOCBCMT ANOP                                                    S22027
         DC    AL3(0) .                 SEND SCHEDULER
*                     IGG019R4          HAS TSO,DIAL,2741 SUPPORT
*                     IGG019Q6          NO TSO, NO DIAL, NO 2741 S21903
*                     IGG019Q7          NO TSO, NO 2741, BUT HAS DIAL
*                                       NONE OF ABOVE IF CONC    S22026
*                                         ONLY (CONCO) SPECIFIED S22026
         DC    A(0) .                   GET SCHEDULER
         DC    A(0) .                   PUT SCHEDULER
         DC    A(0) .                   GET FIFO
IEDBZ    DC    A(0) .                   LOG SCHEDULER
IEDR1    DC    A(0) .                   DIAL SCHEDULER
IEDHB    DC    A(0) .                   BUFFERED SCHEDULER
         DC    A(0) .                   RETRIEVE SCHEDULER
         DC    A(0) .                   LOCAL SCHEDULER
IEDCSCH  DC    A(0) .                   CONC SEND SCHEDULER      S22026
         DC    A(0) .                   3705 SEND SCHEDULER      S22024
         DC    A(0) .                                            S22024
         AIF   (NOT &IEDQZA(44)).XXCMBF                          S22027
IEDCMBSS DC    H'0' .                   COMMBUF SEND SCHEDULER   S22027
*                                       AFTER OPEN               S22027
&A       SETA  &IEDCBUF(3)                                       S22027
         DC    AL2(&A) .                SIZE OF CB DATA AREA     S22027
         AGO   .XXCMBF1                                          S22027
.XXCMBF  ANOP                                                    S22027
IEDCMBSS DC    A(0) .                   COMMBUF SEND SCHEDULER   S22027
.XXCMBF1 ANOP                                                    S22027
         DC    A(0) .                                            S22024
         AIF   (NOT &IEDQRN(1)).NORN                             S22024
         DC    A(IEDNSAVT) .            SECONDARY AVT POINTER    S22024
&IEDLSEC SETC  '&SYSECT'                                         S22024
IEDNSAVT CSECT .                                                S22024
         DC    A(0) .                   RESERVED                 S22024
         DC    A(0) .                   PLCB RETURN QCB ADDRESS  S22024
         DC    F'&BTRACE' .             BTU TRACE TABLE ADDRESS  S22024
         DC    A(0) .                   3705 DEBLOCK QCB ADDRESS S22024
         DC    A(0) .                   3705 OUTDH QCB           S22024
         DC    A(0) .                   3705 FLDH QCB ADDRESS    S22024
         DC    A(0) .                   APPLICATION REQ. QUEUE   S22024
         DC    H'0' .                   BTU REQUEST TAG          S22024
         DC    H'0' .                   RESERVED                 S22024
         DC    A(0) .                   PSEUDO-LCB POOL ADDRESS  S22024
IEDID    DC    A(0) .                   ID VERIFICATION ROUTINE  S22024
&IEDLSEC CSECT .                                                S22024
         AIF   ( T'&PLCBNO NE 'N' ).BDPLCB                       S22024
         DC    H'&PLCBNO' .             NUMBER OF PLCBS          S22024
         AGO   .GABND                                            S22024
.BDPLCB  SPACE 1 .                                               S22024
         MNOTE 12,'IHB303 INVALID OPERAND PLCBNO, 0 ASSUMED'     S22024
         SPACE 2
         AGO   .NOPLCB                                           S22024
.NORN    DC    A(0) .                   SECONDARY AVT POINTER    S22024
.NOPLCB  DC    H'0' .                   RESERVED                 S22024
.GABND   ANOP                                                    S22024
IEDABEND BALR  1,0 .                    ESTABLISH BASE REG
         USING *,1 .                      FOR ABEND S045
         B     IEDSVC13 .               GO DO ABEND, SYSTEM 045
         DC    F'0' .                   DUMMY LINE I/O ECB
IEDA3TL  DC    A(0) .                   IEDQA3, TRANSLATE LIST
         IEDQMASK &WTTONE,2
         AIF   (&IEDQZA(24)).BADTONE
         AIF   ('&WTTONE'(1,1) EQ 'X').BADTONE
         AIF   (&WTTONE GT 450).BADTONE
         AIF   (&WTTONE EQ 0).ZEROTON
&IEDLTON SETA  &WTTONE
         DC    A(IEDTON) .              ADDR OF WORLD TRADE TONE CHARS
         AGO   .GOODTON
.BADTONE SPACE
         MNOTE 12,'IHB303 INVALID OPERAND WTTONE, 0 ASSUMED'
         SPACE 2
.ZEROTON ANOP
&IEDLTON SETA  0
         DC    A(0) .                   WTTONE=0
.GOODTON ANOP
         DC    A(0) IEDQNX              OPERATOR AWARENESS MSG ROUTER
         DC    A(0) IGG019Q0            I/O TRACE TABLE HANDLER
         DC    A(0) IEDQHI              SYSTEM DELAY SUBTASK QCB ADDR
         AIF   ( &IEDQRN(2) ).NOHK .                             S22024
         DC    V(IEDQHK) .              STOPLINE SUBTASK QCB ADDR
         AGO   .NOHK1 .                                          S22024
.NOHK    ANOP                                                    S22024
         DC    A(0) .                   HK UNUSED IN 3705 ONLY   S22024
.NOHK1   ANOP .                                                  S22024
         DC    X'20',AL3(IEDCPRMB),X'F0',AL3(0),A(IEDCKELE,IEDCKPTB)
*                                         CKPT ELE FROM TIME Q
IEDCKELE DC    X'70',AL3(IEDCKPTB),X'EC',AL3(0) CKPT TIME DELAY ELE
         DC    AL1(&IEDLSCB) .          SCB SIZE INCLUDING SAVEAREA
         DC    AL3(IEDCKPTB) .          QCB TO RECEIVE ELE WHEN
*                                         INTERVAL IS UP
         AIF   ('&STARTUP' EQ '').NOSN                         @OS77945
&IEDLSA  SETA  1                                               @OS77945
.SNLOOP  AIF   (K'&STARTUP LT &IEDLSA).NOSN                    @OS77945
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'N').GOSN             @OS77945
&IEDLSA  SETA  &IEDLSA+1                                       @OS77945
         AGO   .SNLOOP                                         @OS77945
.GOSN    ANOP                                                  @OS77945
&IEDLSB(16) SETB 1                                             @OS77945
.NOSN    DC    B'&IEDLSB(16).0000000'                          @OS77945
         IEDQMASK &CPRCDS,1
         AIF   (&IEDQZA(24)).BADCPR
         AIF   ('&CPRCDS'(1,1) EQ 'X').BADCPR
         AIF   (&CPRCDS LT 2 OR &CPRCDS GT 75).BADCPR
         DC    AL1(&CPRCDS) .           CPRCDS=INTEGER
         AGO   .SKIPCPR
.BADCPR  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CPRCDS, 2 ASSUMED'
         SPACE 2
         DC    AL1(2) .                 CPRCDS=2 DEFAULT VALUE
.SKIPCPR ANOP
         IEDQMASK &CPINTVL,2
         AIF   (&IEDQZA(24)).BADCPVL
         AIF   ('&CPINTVL'(1,1) EQ 'X' OR &CPINTVL LT 30).BADCPVL
         AIF   (&CPINTVL GE 43200).BADCPVL                      SA63008
         DC    AL2(&CPINTVL) .          CPINTVL=INTEGER
         AGO   .SKPCPVL
.BADCPVL SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CPINTVL, 1800 ASSUMED'
         SPACE 2
         DC    AL2(1800) .              CPINTVL=1800 DEFAULT VALUE
.SKPCPVL ANOP
         DC    H'0' .                   TIME OF DAY OF INTERRUPT
         DC    X'08' .                  OFFSET TO CKPT QCB ADDR
         DC    4X'00' .                 OPEN CONSTANTS
         DC    X'00' .                  CKPT ELE STATUS
IEDOPETR DS    0X .                     INTRO RETURN CODE
         DC    V(IEDQHG01) .            TIME DELAY SUBROUTINE
IEDBSX   DC    AL1(8) .                 OFFSET TO BINARY SEARCH ADDR
         DC    AL3(0) .                 LINK ON TIME QUEUE
IEDDELEM DC    A(IEDREADY-8,IEDDELEM) . DUMMY LAST ELEMENT
         DC    X'20',AL3(IEDCKPTB),A(0) CLOSE CHECKPOINT ELEMENT
IEDCLRHI DC    X'0000FFFF' .            CLEARS HIGH HALF OF REG
IEDADBUF DC    A(0) .                   BUFFER ADDRESS FOR MH
         DC    A(0) .                   2260 LOCAL APPENDAGE
         DC    AL1(0) .                 RESERVED
IEDMSGS  DC    AL3(IEDQMSGS) .          LIST OF OPTIONAL VCONS
         AIF   (NOT &IEDQZA(44)).XXCMBF2                         S22027
&A       SETA  &IEDCBUF(1)                                       S22027
IEDCBSTB DC    AL2(&A) .                NUMBER OF COMMBUF STCBS  S22027
&A       SETA  &IEDCBUF(2)                                       S22027
IEDCBNDA DC    AL2(&A) .                NUMBER OF CB DATA AREAS  S22027
*                                       AFTER INTRO THE ABOVE    S22027
*                                       TWO HALF WORDS CONTAIN   S22027
*                                       THE ADDR OF CB MASTER QCBS22027
         AGO   .XXCMBF3                                          S22027
.XXCMBF2 DC    A(0) .                   RESERVED FOR CB          S22027
.XXCMBF3 ANOP                                                    S22027
IEDSUPPT DC    A(0) .                   STARTUP MESSAGE
         AIF   (&IEDQZA(51)).DOQCB
         DC    A(0) .                   TIME SHARING INPUT QCB ADDR
*                                        (UNUSED WHEN ENVIRON=TCAM)
         AGO   .SKDOQCB
.DOQCB   ANOP
IEDTSOPT DC    A(IEDAYTSQ) .            TS INPUT QCB ADDR (QCB IS
*                                         GENERATED BY TSINPUT MACRO)
.SKDOQCB ANOP
IEDOCQPT DC    A(0) IEDQEU              OPEN/CLOSE APPLICATION PROG
IEDDELYB DC    A(*,0),V(IEDQHG) .       TIME DELAY QCB
         DC    A(0) .                   STIMER INTERVAL
         DC    H'0' .                   REFERENCE TIME
IEDINOUT DC    X'0100' .                DUMMY INEND/OUTEND PARM LIST
         DC    X'0C',AL3(IEDDELYB),A(IEDDELYB) PARM TO SVC 102 TO POST
*                                       TIME DELAY QCB TO ITSELF WHEN
*                                       TIME INTERRUPT IS RECEIVED
         DC    A(IEDDELYB) .            TIME DELAY Q
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQGA) AVAILABLE BUFFER
IEDBFRTB DC    X'02',AL3(IEDDELEM),A(0),V(IEDQGB) BUFFER RETURN QCB
IEDCKPTB DC    X'02',AL3(IEDDELEM),A(0,IEDCKPTB) CHECKPOINT QCB
IEDOPCOB DC    X'02',AL3(IEDDELEM),A(0,IEDOPCOB) OPERATOR CONTROL
IEDOLTQB DC    X'02',AL3(IEDDELEM),A(0,IEDOLTQB) ON LINE TEST
         AIF   (NOT &IEDQRN(2)).ACTQCB                           S22024
         DC    X'02',AL3(IEDDELEM),A(0),A(0) ACTIVATE QCB        S22024
&IEDLSB(5) SETB 0                                                S22024
&IEDLSB(6) SETB 0                                                S22024
         AGO   .LNTOUT                                           S22024
.ACTQCB  ANOP                                                    S22024
         AIF   ('&LINETYP' EQ 'STSP').LNTSTSP
         AIF   ('&LINETYP' EQ 'BISC').LNTBISC
         AIF   ('&LINETYP' EQ 'MINI').LNTMINI
         AIF   ('&LINETYP' EQ 'BOTH').LNTBOTH
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND LINETYP, BOTH ASSUMED'
         SPACE 2
.LNTBOTH ANOP
*                                       LINETYP=BOTH SPECIFIED
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQKA) ACTIVATE QCB
&IEDLSB(5) SETB 0
&IEDLSB(6) SETB 0
         AGO   .LNTOUT
.LNTMINI ANOP
*                                       LINETYP=MINI SPECIFIED
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQKD) ACTIVATE QCB
         AGO   .LNTOUT
.LNTBISC ANOP
*                                       LINETYP=BISC SPECIFIED
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQKB) ACTIVATE QCB
&IEDLSB(6) SETB 0                       BISC ONLY
         AGO   .LNTOUT
.LNTSTSP ANOP
*                                       LINETYP=STSP
         AIF   (&IEDQZA(51)).STSPTSO
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQKE) ACTIVATE QCB
         AGO   .SETSTSP
.STSPTSO ANOP
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQKC) ACTIVATE QCB
.SETSTSP ANOP
&IEDLSB(5) SETB 0                       STSP ONLY
.LNTOUT  ANOP
IEDCLOSB DC    X'02',AL3(*-1),A(0),V(IEDQNA) CLOSEDOWN COMPLETION
IEDCPRMB DC    X'02',AL3(*-1),A(0),V(IEDQHG03) REMOVE TIME DELAY
*                                         REQUEST ELE FROM TIME Q
         AIF   (&IEDQZA(51)).DOYO
         AIF   (&IEDQZA(41) AND &IEDQZA(42)).FA IF CORE AND DISK
         AIF   (&IEDQZA(41)).FA2        IF DISK ONLY
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQFA1) CORE ONLY Q QCB
         AGO   .SKFA
.FA2     ANOP
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQFA2) DISK ONLY Q QCB
         AGO   .SKFA
.FA      ANOP
         DC    X'02',AL3(IEDDELEM),A(0),V(IEDQFA) QUEUE QCB
.SKFA    ANOP
IEDCPBCB DC    X'02',AL3(*-1),A(0),V(IEDQFQ) CPB CLEANUP QCB
         AGO   .SKIPYO
.DOYO    DC    X'02',AL3(IEDDELEM),A(0),V(IEDAYO) QUEUE QCB
IEDCPBCB DC    X'02',AL3(*-1),A(0),V(IEDAYO02) CPB CLEANUP QCB
.SKIPYO  ANOP
         DC    2F'0' .                  COREC AND CADDR
         DC    F'0' .                   FULLWORD OF ZEROES
         DC    V(IEDQFE) .              FE PATCH MODULE
         DC    F'0',X'40',AL3(0) .
         AIF   ('&COMWRTE' EQ 'NO').NOCMWR
         AIF   ('&COMWRTE' EQ 'YES').YESCMWR
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND COMWRTE, NO ASSUMED'
         SPACE 2
.NOCMWR  DC    X'00' .                  COMWRTE=NO SPECIFIED
         AGO   .SKCMWR
.YESCMWR DC    X'80' .                  COMWRTE=YES SPECIFIED
.SKCMWR  ANOP
         DC    X'00FFFF0000000040000000',19F'0' FE PATCH AREA
*        GETMAIN EC,MF=L                GETMAIN PARAMETER LIST
         GETMAIN EC,MF=L
         DC    H'2,3,4,7,16' .          HALFWORD CONSTANTS
&IEDLKEY SETA  0                        DEFAULT FOR KEYLEN (UNITSZ)
         AIF   ('&KEYLEN' EQ '').TRYUNSZ
         AIF   ('&UNITSZ' NE '').BOTHBAD
         AGO   .TRYKEY
.TRYUNSZ AIF   ('&UNITSZ' EQ '').SKPKYX
         IEDQMASK &UNITSZ,1
         AIF   (&IEDQZA(24)).BADUNSZ
         AIF   (&UNITSZ EQ 0).GOODUNS
         AIF   ('&UNITSZ'(1,1) EQ 'X' OR &UNITSZ LT 33).BADUNSZ
.GOODUNS ANOP
&IEDLKEY SETA  &UNITSZ
*                                       UNITSZ=INTEGER
         AGO   .SKPKY
.BOTHBAD SPACE
         MNOTE 12,'IHB303 INVALID OPERAND KEYLEN/UNITSZ, 0 ASSUMED'
         SPACE 2
.SKPKYX  ANOP
*                                       KEYLEN=0 (UNITSZ=0) DEFAULT
         AGO   .SKPKY
.BADUNSZ SPACE
         MNOTE 12,'IHB303 INVALID OPERAND UNITSZ, 0 ASSUMED'
         SPACE 2
*                                       UNITSZ=0 DEFAULT VALUE
         AGO   .SKPKY
.TRYKEY  ANOP
         IEDQMASK &KEYLEN,1
         AIF   (&IEDQZA(24)).BADKY
         AIF   (&KEYLEN EQ 0).GOODKEY
         AIF   ('&KEYLEN'(1,1) EQ 'X' OR &KEYLEN LT 33).BADKY
.GOODKEY ANOP
&IEDLKEY SETA  &KEYLEN
*                                       KEYLEN=INTEGER
         AGO   .SKPKY
.BADKY   SPACE
         MNOTE 12,'IHB303 INVALID OPERAND KEYLEN, 0 ASSUMED'
         SPACE 2
*                                       KEYLEN=0 DEFAULT VALUE
.SKPKY   ANOP
         DC    H'&IEDLKEY'
         DC    5H'0' .                  LNCNT,OPCNT,OPCON,AVFCT,SMCNT
         IEDQMASK &INTVAL,2
         AIF   (&IEDQZA(24)).BADINT
         AIF   ('&INTVAL'(1,1) EQ 'X' OR &INTVAL LT 0).BADINT
         DC    AL2(&INTVAL) .               INTVAL=INTEGER
         AGO   .SKPINT
.BADINT  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND INTVAL, 0 ASSUMED'
         SPACE 2
         DC    H'0' .                   INTVAL=0 DEFAULT VALUE
.SKPINT  ANOP
IEDDLQX  DC    H'0' .                   DEAD LETTER Q OFFSET
IEDDUMBR BR    11 .                     DUMMY TRACE TABLE UPDATE
IEDBIT1  DC    B'0&IEDQZA(51).&IEDQZA(52).0000&IEDLDSK'
         AIF   ('&TOPMSG' EQ 'NO').NOTOP
         AIF   ('&TOPMSG' EQ 'YES').YESTOP
         SPACE
         MNOTE 12,'IHB303 INVALID OPERAND TOPMSG, YES ASSUMED'
         SPACE 2
.YESTOP  ANOP
&IEDLSB(7) SETB 0
*                                       TOPMSG=YES, DISPLAY T.O. MSGS
         AGO   .ENDTOP
.NOTOP   ANOP
&IEDLSB(7) SETB 1
*                                       TOPMSG=NO, SUPPRESS T.O. MSGS
.ENDTOP  ANOP
         AIF   ('&STARTUP' EQ '').ENDSTRT
         AIF   (K'&STARTUP GT 3).ERRSTRT                       @OS77945
&IEDLSA  SETA  1                        PRESET INDEX
.STALOOP AIF   (K'&STARTUP LT &IEDLSA).ENDSTR
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'C').GOTC
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'W').GOTW
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'I').GOTI
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'Y').GOTY
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'E').GOTE              SA51078
         AIF   ('&STARTUP'(&IEDLSA,1) EQ 'N').STABUMP          @OS77945
.ERRSTRT SPACE
         MNOTE 12,'IHB300 STARTUP OPERAND INVALID AS SPECIFIED'
         SPACE 2
         AGO   .ENDSTRT
.GOTC    AIF   (&IEDLSB(1)).ERRSTRT
&IEDLSB(1) SETB 1                       STARTUP SPECIFIED
&IEDLSB(2) SETB 1                       'C' SPECIFIED
         AGO   .STABUMP
.GOTW    AIF   (&IEDLSB(1)).ERRSTRT
&IEDLSB(1) SETB 1                       STARTUP SPECIFIED
&IEDLSB(2) SETB 0                       'W' SPECIFIED
         AGO   .STABUMP
.GOTI    AIF   (&IEDLSB(3)).ERRSTRT
&IEDLSB(3) SETB 1                       'I' SPECIFIED
         AGO   .STABUMP
.GOTY    AIF   (&IEDLSB(4) OR &IEDLSB(14) OR                           X
               &IEDLSB(16)).ERRSTRT                            @OS77945
&IEDLSB(4) SETB 1                       'Y' SPECIFIED
         AGO   .STABUMP                                         SA51078
.GOTE    AIF   (&IEDLSB(4) OR &IEDLSB(14) OR                           X
               &IEDLSB(16)).ERRSTRT                            @OS77945
.*                                                             @OS77945
&IEDLSB(14) SETB 1                                              SA51078
.STABUMP ANOP
&IEDLSA  SETA  &IEDLSA+1                BUMP INDEX
         AGO   .STALOOP                 LOOK AT NEXT CHARACTER
.ENDSTR  AIF   (NOT &IEDLSB(1)).ERRSTRT
         DC    B'&IEDLSB(14).00&IEDLSB(7).00&IEDLSB(6).&IEDLSB(5)'
*                                       STARTUP CODE SPECIFIED @XA05310
         DC    B'0000&IEDLSB(4).&IEDLSB(3).&IEDLSB(2).&IEDLSB(1)'
         AGO   .MOREBIT
.ENDSTRT ANOP                                                  @XA05310
         DC    B'&IEDLSB(14).00&IEDLSB(7).00&IEDLSB(6).&IEDLSB(5)'
         DC    B'00000000'              STARTUP CODE NOT SPEC  @XA05310
.MOREBIT ANOP
         IEDQMASK &RESTART,1
         AIF   (&IEDQZA(24)).BADREST
         AIF   ('&RESTART'(1,1) EQ 'X').BADREST
         DC    AL1(&RESTART) .          RESTART=INTEGER
         AGO   .SKPREST
.BADREST SPACE
         MNOTE 12,'IHB303 INVALID OPERAND RESTART, 0 ASSUMED'
         SPACE 2
         DC    AL1(0) .                 RESTART=0 DEFAULT VALUE
.SKPREST ANOP
         DC    2AL1(0) .
.* &IEDQZA(52) SET BY ENVIRON=TCAM OR MIXED
         AIF   (NOT &IEDQZA(52)).TSONLY
         DC    V(IEDQHM02) .            DESTINATION ASSIGNEMNT
         IEDQMASK &MSMIN,1
         AIF   (&IEDQZA(24)).BADMIN
         AIF   ('&MSMIN'(1,1) EQ 'X' OR &MSMIN GE 100).BADMIN
&IEDLMIN SETA  &MSMIN
         AGO   .SKIPMIN
.BADMIN  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND MSMIN, 50 ASSUMED'
         SPACE 2
*                                       DEFAULT VALUE FOR MSMIN USED
&IEDLMIN SETA  50
.SKIPMIN ANOP
         IEDQMASK &MSMAX,1
         AIF   (&IEDQZA(24)).BADMAX
         AIF   ('&MSMAX'(1,1) EQ 'X' OR &MSMAX GT 100 OR &MSMAX LT 1).BA
               ADMAX
&IEDLMAX SETA  &MSMAX
         AGO   .SKIPMAX
.BADMAX  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND MSMAX, 70 ASSUMED'
         SPACE 2
*                                       DEFAULT VALUE FOR MSMAX USED
&IEDLMAX SETA  70
.SKIPMAX ANOP
         AIF   (&IEDLMAX GT &IEDLMIN).OKINAX
         SPACE
         MNOTE 12,'IHB300 MSMIN/MSMAX OPERAND INVALID AS SPECIFIED'
         SPACE 2
         DC    F'50' .                  MSMIN=50 DEFAULT VALUE
         DC    F'70' .                  MSMAX=70 DEFAULT VALUE
         AGO   .MINMAX
.OKINAX  ANOP
         DC    F'&IEDLMIN' .                  MSMIN=INTEGER
         DC    F'&IEDLMAX' .                  MSMAX=INTEGER
.MINMAX  ANOP
         DC    F'&IEDLMS' .             MSUNITS=INTEGER
         DC    AL1(4),AL3(0),A(0) .     ELEMENTS WAITING FOR CPBS
         AIF   (&IEDQZA(41)).GOTDISK
         AGO   .END
.GOTDISK ANOP
IEDFL    DC    A(0) .                   DISK EXCP DRIVER
IEDIA    DC    A(0) .                   REUS
         DC    A(0) .                   ADDR OF COPY SUBTASK QCB
         DC    AL1(40),AL3(0),A(0) .    CPBS FOR CPB CLEANUP-DISABLE
         DC    AL1(40),AL3(0),A(0) .    SAME, BUT ENABLED
         DC    AL1(40),AL3(0),A(0) .    CPBS WITHOUT BUFFERS
IEDBIT4  DC    XL1'00' .                FLAGBITS                SA51078
         DC    AL3(0) .                 RESERVED                SA51078
         DC    A(0) .                   RESERVED                SA51078
         DC    AL1(40),AL3(0),A(0) .    CPBS WANTING I/O
IEDFCPB  DC    A(0) .                   CPB FREEPOOL
         DC    17F'0' .                 DISK OPEN CONSTANTS
IEDNADDR DC    2F'0' .                  NON-REUS AND REUS DISK ADDRS
         DC    A(IEDOPCOB),X'EA',AL3(0),X'310C00FF'            @SA71946
*                                       NON-REUSABLE DISK
*                                         THRESHOLD CLOSEDOWN ELE
         IEDQMASK &CPB,2
         AIF   (&IEDQZA(24)).BADCPB
         AIF   ('&CPB'(1,1) EQ 'X' OR &CPB LT 0).BADCPB
         DC    AL2(&CPB) .                 CPB=INTEGER
         AGO   .SKPCPB
.BADCPB  SPACE
         MNOTE 12,'IHB303 INVALID OPERAND CPB, 0 ASSUMED'
         SPACE 2
         DC    AL2(0) .                 CPB=0 DEFAULT VALUE
.SKPCPB  ANOP
         AGO   .END
.TSONLY  ANOP                                                    S22029
.END     SPACE 2
IEDOA    DC    CL8'IEDQOA  ' .          INITIALIZATION LINKOR SUBRT
IEDSVC13 DS    0H .
         L     1,IED045 .               GET ABEND CODE
         DROP  1 .                      FORGET BASE
         STM   0,15,0(13) .             SAVE REGS IN CURRENT SAVEAREA
*        ABEND (1) .                    SYSTEM 045,SUBCODE IN R15
         ABEND (1) .                    SYSTEM 045,SUBCODE IN R15
         DS    0F .                     WORD BOUNDARY
IED045   DC    X'80045000' .            ABEND SYSTEM CODE
         AIF   (&IEDLTON EQ 0).NOTONE
IEDTON   CSECT
         DC    AL2(&IEDLTON),&IEDLTON.X'FF' .     TONE CHAR WORLD TRADE
.NOTONE  AIF   (&IEDLSB(10) EQ 0).HGOK
         SPACE 3
IEDQHG   CSECT .                        DUMMY TIME DELAY SUBTASK
*                                       FEATURE=(,,NOTIMER)
IEDQHG03 DS    0H
         ENTRY IEDQHG01,IEDQHG02,IEDQHG03
         DC    X'0400' .                STCB FOR TIME DELAY SUBTASK
IEDQHG01 DS    0H .                     SUBROUTINE TYPE ENTRY
IEDQHG02 DS    0H .                       POINTS TO TIME DELAY
         LA    15,6 .                   ABEND SUBCODE
         BAL   14,IEDABEND-IEDSAVE2(13) ILLEGAL CALL TO TIME
*                                         DELAY, SO ABEND
.HGOK    ANOP
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                      TCAM MESSAGE CONTROL PROGRAM
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
&IEDLSEC CSECT
         AIF   ('&PROGID' EQ '').SKIPSEC
         DC    C'&PROGID'
.SKIPSEC ANOP
*
&IEDLSKP DS    0H
         STM   14,12,12(13) .           SAVE CALLERS REGISTERS
         ST    1,IEDSPLPT .             SAVE ADDRESS OF START
*                                         PARAMETER LIST POINTER
         LA    1,IEDSAVE1 .             GET ADDRESS OF SAVEAREA
         ST    13,IEDSAVE1+4 .          EXCHANGE
         ST    1,8(13) .                  SAVEAREA ADDRESSES
         USING IEDSAVE1,13 .            IDENTIFY PROGRAM BASE
         LR    13,1 .                   SET PROGRAM BASE
         DROP  15 .                     FORGET TEMPORARY
*                                       PASS ADDR OF AVT AS PARM IN R1
*        LINK  EPLOC=IEDOA,MF=(E,(1))   GET OPERATOR REPLY ROUTINE
         LINK  EPLOC=IEDOA,MF=(E,(1))
         SPACE 2
         MEND
