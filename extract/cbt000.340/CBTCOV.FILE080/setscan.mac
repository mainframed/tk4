         MACRO
&NAME    SETSCAN &CHARS,&MOVE=KEEP,&POINT=FORWARD,&RESULT=(15),&BLANK=Y*
               ES,&LAST=NO                                     @OY15007
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB,&IEDQZC
         LCLA  &LEN,&BIT,&LNG,&I,&O                              A44874
         LCLB  &NOGO,&NOREG
         LCLC  &WORK,&C
.*A084000                                                        A44874
.*C049000,070000,511000,553000-581000                            A44874
.*D630000-658000                                                 A44874
.*A119000,343000,371000,679000,707000                          @OY15007
.*          VERSION DATED NOVEMBER 11,1976                     @OY15007
         SPACE
&IEDQZE(40) SETA 0
&I       SETA  30                                                A44874
&O       SETA  1                                                 A44874
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.TSOCHK  ANOP                                                       TSO
         AIF   ('&LAST' EQ 'NO').NEXT                          @OY15007
         AIF   ('&LAST' EQ 'YES').DFLT                         @OY15007
         MNOTE 12,'***  IHB300  LAST OPERAND INVALID AS SPECIFIED'
         AGO   .MEND                                           @OY15007
.DFLT    ANOP                                                  @OY15007
         AIF   (&IEDQZA(14)).GOOD                              @OY15007
         AIF   (&IEDQZA(6) AND NOT &IEDQZA(3) AND NOT &IEDQZA(4)).GOOD
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INBLK,INBU*
               F OR OUTBUF MACRO'                              @OY15007
         AGO   .MEND                                           @OY15007
.GOOD    ANOP                                                  @OY15007
         AIF   ('&MOVE' NE 'KEEP' AND '&MOVE' NE 'RETURN').BADMVE
         AGO   .RESULT                                         @OY15007
.NEXT    ANOP                                                  @OY15007
         AIF   (&IEDQZA(3) OR &IEDQZA(4)).TRY
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INHDR OR O*
               UTHDR MACRO'
&NOGO    SETB  1
.TRY     AIF   ('&CHARS' EQ '').MNOTE
         AIF   ('&MOVE' EQ 'KEEP' OR '&MOVE' EQ 'RETURN').POINT
.BADMVE  ANOP                                                  @OY15007
         MNOTE 12,'***  IHB300  MOVE OPERAND INVALID AS SPECIFIED'
         AIF   ('&LAST' EQ 'YES').MEND                         @OY15007
&NOGO    SETB  1
.POINT   AIF   ('&POINT' EQ 'FORWARD' OR '&POINT' EQ 'BACK').RESULT
         MNOTE 12,'***  IHB300  POINT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.RESULT  AIF   ('&RESULT'(1,1) NE '(' OR '&RESULT'(K'&RESULT,1) NE ')').
               .ERR
         AIF   (K'&RESULT GT 4).ERR
         AIF   (K'&RESULT EQ 3 AND '&RESULT'(2,1) LT '2').ERR
         AIF   (K'&RESULT EQ 4 AND ('&RESULT'(2,2) GT '12' AND '&RESULT*
               '(2,2) NE '15')).ERR
         AIF   ('&LAST' EQ 'YES').MOVE                         @OY15007
         AGO   .NEW
.ERR     MNOTE 12,'***  IHB300  RESULT OPERAND INVALID AS SPECIFIED'
         AIF   ('&LAST' EQ 'YES').MEND                         @OY15007
&NOGO    SETB  1
.NEW     AIF   ((('&CHARS' GE '0' AND '&CHARS' LE '8') OR '&CHARS' EQ '*
               255') AND '&MOVE' EQ 'RETURN').SETLEN
         AGO   .CONS
.SETLEN  ANOP
&LEN     SETA  1
         AGO   .GETCNT
.CONS    AIF   ('&MOVE' EQ 'RETURN' AND '&POINT' NE 'FORWARD').INCONS
         AIF   ('&CHARS' EQ '255' AND '&BLANK' EQ 'NO').INCONS
         AIF   ('&CHARS'(1,1) GE '0' AND '&CHARS'(1,1) LE '9').NXT
         AIF   ('&POINT' EQ 'BACK').INCONS
.NXT     AIF   ('&CHARS'(1,1) GE '0' AND '&CHARS'(1,1) LE '9' AND '&MOV*
               E' EQ 'RETURN').INCONS
.MOVE    ANOP                                                  @OY15007
         AIF   ('&MOVE' EQ 'RETURN' AND '&RESULT' EQ '').INCONS
         AIF   ('&MOVE' EQ 'KEEP' AND '&RESULT' NE '' AND '&RESULT' NE *
               '(15)').INCONS
         AIF   ('&LAST' NE 'YES').BLANK                        @OY15007
         AIF   ('&MOVE' NE 'KEEP').GETSET                      @OY15007
&BIT     SETA  255                                             @OY15007
         AGO   .GETSET                                         @OY15007
.INCONS  MNOTE 12,'***  IHB066  INCONSISTENT OPERANDS'
         AIF   ('&LAST' EQ 'YES').MEND                         @OY15007
&NOGO    SETB  1
.BLANK   AIF   ('&BLANK' NE 'NO').BL1
&IEDQZE(40) SETA 2
         AGO   .CHARS
.BL1     AIF   ('&BLANK' EQ '' OR '&BLANK' EQ 'YES').CHARS
         IEDQCHAR 1,&BLANK
         AIF   (NOT &IEDQZA(24)).CHARS
         MNOTE 12,'***  IHB300  BLANK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CHARS   AIF   ('&CHARS'(1,1) GE '0' AND '&CHARS'(1,1) LE '9').GETCNT
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).MNOTE
         AIF   (&IEDQZD LE 8).GETSET
         AGO   .MNOTE
.GETCNT  IEDQMASK &CHARS,2
         AIF   (NOT &IEDQZA(24)).ADD
.MNOTE   MNOTE 12,'***  IHB300  FIRST OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ADD     AIF   ('&POINT' EQ 'FORWARD').ORGA                      A44874
         AIF   (&NOGO).MEND                                      A44874
         IEDQVCON 28,IEDQA0                                      A44874
&I       SETA  28                                                A44874
&O       SETA  0                                                 A44874
.ORGA    ANOP                                                    A44874
         AIF   ('&BLANK' EQ '' OR '&BLANK' EQ 'YES' OR '&BLANK' EQ 'NO'*
               ).BAL
&LNG     SETA  2
.BAL     AIF   (&NOGO).MEND
&NAME    BAL   1,*+8+&LNG
&NOREG   SETB  1
         DC    AL1(&IEDQZE(&I)+&IEDQZE(40)+&O),AL1(4+&LNG) .     A44874
         AIF   (&LEN NE 0).SPL                                   A44874
         DC    AL2(&CHARS) .                                     A44874
         AGO   .SETB                                             A44874
.SPL     DC    X'00',AL1(&CHARS) .                               A44874
.SETB    AIF   (&LNG NE 2).LOAD
         AIF   ('&BLANK'(K'&BLANK,1) NE '''').FRB
         DC    &BLANK,AL1(0)
         AGO   .LOAD
.FRB     DC    C'&BLANK',AL1(0)
         AGO   .LOAD
.GETSET  IEDQVCON 29,IEDQAJ
.BLDPARM AIF   (&NOGO).MEND
         CNOP  0,4
         AIF   ('&LAST' NE 'YES').NOTLST                       @OY15007
&NAME    BAL   1,*+8                                           @OY15007
         DC    AL1(&IEDQZE(29)+1),AL1(4),AL1(&BIT)             @OY15007
         AGO   .RSLT                                           @OY15007
.NOTLST  ANOP                                                  @OY15007
&NAME    BAL   1,*+12
         AIF   ('&MOVE' NE 'KEEP').K
&BIT     SETA  255
.K       DC    AL1(&IEDQZE(29)+1+&IEDQZE(40)),AL1(8),AL1(&BIT)
.RSLT    ANOP                                                  @OY15007
&WORK    SETC  '&RESULT'(2,K'&RESULT-2)
&LEN     SETA  &WORK
         AIF   (&LEN EQ 15).SETREG
&LEN     SETA  &LEN*4+20
         DC    AL1(&LEN) .              REGISTER OFFSET
         AGO   .CSECT
.SETREG  DC    AL1(16)
.CSECT   ANOP
         AIF   ('&LAST' EQ 'YES').LOAD                         @OY15007
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         AIF   ('&CHARS'(K'&CHARS,1) EQ '''').NOFR
         DC    AL1(&IEDQZD),C'&CHARS'
         AGO   .EXIT
.NOFR    DC    AL1(&IEDQZD),&CHARS
.EXIT    ANOP
&C       CSECT
         AIF   ('&BLANK' EQ 'NO').DC1
         AIF   ('&BLANK' EQ '' OR '&BLANK' EQ 'YES').DC2
         AIF   ('&BLANK'(K'&BLANK,1) NE '''').FRAME
         DC    &BLANK,AL3(IEDQMISC+&IEDQZK)
         AGO   .ENDSET
.FRAME   DC    C'&BLANK',AL3(IEDQMISC+&IEDQZK)
         AGO   .ENDSET
.DC2     DC    X'40',AL3(IEDQMISC+&IEDQZK)
         AGO   .ENDSET
.DC1     DC    AL1(0),AL3(IEDQMISC+&IEDQZK)
.ENDSET  ANOP
&IEDQZK  SETA  &IEDQZK+1+&IEDQZD
.LOAD    L     15,IEDUI
         BALR  14,15
         AIF   (&NOREG).MNOTEW
         AGO   .MEND
.MNOTEW  AIF   ('&RESULT'(2,2) EQ '15').MEND
         MNOTE 12,'*** IHB347 WARNING - RESULT IS RETURNED IN REGISTER *
               15'
.MEND    SPACE 2
         MEND
