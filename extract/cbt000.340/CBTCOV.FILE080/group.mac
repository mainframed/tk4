         MACRO
&NAME    GROUP &BUFOUT=2,&BUFMAX=2,&MH=,&DCB=,&BUFSIZE=,&INVLIST=,     *
               &TRANS=EBCD
         GBLA  &IEDQAR(64)
         GBLA  &IEDQRL
         GBLB  &IEDQNO,&IEDQZA(64),&IEDQRN(8)
         GBLB  &IEDQGP
         GBLC  &IEDQSV(8)
         GBLC  &IEDQGN
         LCLA  &B,&C,&D,&S,&L,&TRNDX
         LCLC  &NMPR,&TRNM
         SPACE 1
&IEDQSV(4) SETC '&SYSECT'
         AIF   (&IEDQRN(1)).A00
         MNOTE 12,'***  IED914  GROUP MUST FOLLOW INTRO MACRO WITH FEAT*
               URE=(,,,,MIXD3705/ONLY3705) OPERAND'
&IEDQNO  SETB  1
.A00     ANOP
         AIF   (&IEDQZA(55)).A01   CHECK TO SEE IF TTABLE CODED
         AIF   ('&IEDQZA(54)' EQ '&IEDQZA(55)' AND '&IEDQZA(53)'       *
               EQ '&IEDQZA(55)').A02
         MNOTE 12,'***  IHB315  SEQUENCE ERROR-MUST PRECEDE LAST ENTRY *
               SPEC. BY TTABLE MACRO'
         AGO   .A03
.A02     MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW TTABLE MACRO*
               '
.A03     ANOP
&IEDQNO  SETB  1
.A01     AIF   ('&NAME' NE '').A04
         MNOTE 12,'***  IHB076  MACRO NAME FIELD BLANK-NAME REQUIRED'
&IEDQNO  SETB  1
.A04     AIF   (K'&NAME LE &IEDQAR(1)-3).A05
&S       SETA  &IEDQAR(1)-3
         MNOTE 4,'***  IHB302  NAME FIELD LONG-TRUNCATED TO &S CHARACTE*
               RS'
.A05     AIF   ('&MH' NE '' AND '&DCB' NE '').A06 MH AND DCB REQUIRED
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
&IEDQNO  SETB  1
.A06     AIF   (&BUFOUT LT 1 OR &BUFOUT GT 15).A07
         AIF   (&BUFMAX LT 2 OR &BUFMAX GT 15).A07
         AIF   (&BUFMAX LT &BUFOUT).A07
         AGO   .A07A
.A07     MNOTE 12,'***  IHB002  BUFOUT/BUFMAX OPERAND INCORRECTLY SPECI*
               FIED'
&IEDQNO  SETB  1
.A07A    AIF   ('&BUFSIZE' EQ '').A08
         AIF   (&BUFSIZE GT 35 AND &BUFSIZE LT 65536).A09
         MNOTE 12,'***  IHB002  BUFSIZE OPERAND INCORRECTLY SPECIFIED'
&IEDQNO  SETB  1
.A08     ANOP
&B       SETA  0
         AGO   .A10
.A09     ANOP
&B       SETA  &BUFSIZE
.A10     ANOP
&NMPR    SETC  'IEDQ'
&TRNDX   SETA  25
         AIF   ('&TRANS' EQ 'EBCD').SETTR
         AIF   ('&TRANS' NE 'EBCF').A15
&NMPR    SETC  'IEDN'
         AGO   .SETTR
.A15     ANOP
&TRNDX   SETA  0
.SETTR   ANOP
&TRNM    SETC  '&TRNDX'
         AIF   (&IEDQNO).EXIT
&IEDQRL  SETA  1
&IEDQGP  SETB  1
&IEDQGN  SETC  '&NAME'
&IEDQAR(4) SETA &IEDQAR(4)+1
         AIF   (&IEDQAR(4) LE 9999).SP
&IEDQAR(4) SETA 0
&IEDQAR(7) SETA &IEDQAR(7)+1
.SP      SPACE 1
IEDQTNT  CSECT
&L       SETA  &IEDQAR(1)-3
         DC    CL&L'&NAME'  .   ENTRY NAME
         DC    AL3(&NAME.+20)
         SPACE 1
&IEDQSV(4)  CSECT
&IEDQSV(1) SETC '&NAME'
&C       SETA  N'&INVLIST
         ORG   *-16 TO ELIMINATE UNUSED SPACE
&NAME    DS    0F ORIGIN ON WORD BOUNDARY
         ORG   *+16 TO ORIGIN GENERATION
         DC    AL2(0) RESERVED
         DC    AL2(0) RSID
         DC    AL1(&BUFOUT) BUFOUT
         DC    AL3(&MH) ADDRESS OF MH
         DC    AL1(0) NUMBER OF LINES IN GROUP
         DC    XL1'10' PCI=ADD
         DC    BL2'1000000000000000' DSORG
         DC    AL1(&BUFMAX) BUFMAX
         DC    AL3(&DCB) ADDRESS OF DCB
         DC    BL1'0' FLAGS
         AIF   (&TRNDX EQ 0).A10B
         DC    VL3(&NMPR&TRNM) ADDRESS OF TRANS TABLE
         AGO   .A10A
.A10B    DC    AL3(&TRANS) ADDRESS OF TRANS TABLE
.A10A    ANOP
         DC    A(0) RESERVED
         DC    CL8'&NAME' GROUP NAME
         DC    BL1'0' OFLGS
         DC    AL3(0) ADDRESS OF SCT
         DC    AL1(&C) ILCT
         DC    AL1(0) RESERVED
         DC    AL2(&B) BUFSIZE
         DC    A(0) RESERVED
&D       SETA  1
.A12     AIF   ('&INVLIST(&D)' NE '').A13
         DC    A(0) ADDRESS OF INVLIST
         AIF   (&C EQ 0).A11
         AGO   .A14
.A13     DC    A(&INVLIST(&D)) ADDRESS OF INVLIST
.A14     AIF   (&D EQ &C).A11
&D       SETA  &D+1
         AGO   .A12
.A11     SPACE 2
         AIF   (&C EQ 0 OR &IEDQRN(3)).EXIT
&IEDQRN(3) SETB 1
IEDNSAVT CSECT
         ORG   IEDID
         DC    V(IEDNID) .              ID VERIFICATION ROUTINE
         ORG
&IEDQSV(4)  CSECT
         SPACE 2
.EXIT    ANOP
         MEND
