         MACRO
&NAME    COMMBUF  &LIST=,&MAXDEEP=
         GBLA  &IEDQZE(64),&IEDQZD,&IEDQZK
         GBLB  &IEDQZA(64)
         LCLB  &NOGO
         LCLC  &C
.*       VERSION DATED APRIL 18,1972
         SPACE 2
         AIF   (&IEDQZA(3) OR (&IEDQZA(1) AND (NOT &IEDQZA(5))) AND (NOX
               T &IEDQZA(14))).SEQOK
         MNOTE 12,'*** IHB070 SEQUENCE ERROR - MUST FOLLOW INHDR OR INB*
               UF MACRO'
&NOGO    SETB  1
.SEQOK   ANOP
         AIF   (&IEDQZA(44)).EXP
         MNOTE 8,'*** IHB233 INCONSISTENT WITH COMMBUF OPERAND OF THE IX
               NTRO MACRO'
.EXP     ANOP
         AIF   ('&LIST' NE '').OP1
         MNOTE 12,'*** IHB001 LIST OPERAND REQ''D-NOT SPECIFIED'
&NOGO    SETB  1
         AGO   .LISTOK
.OP1     ANOP
         IEDQCHAR  8,&LIST
         AIF   (&IEDQZA(24)).ERR1
         AIF   ('&LIST'(1,1) GE 'A' AND '&LIST'(1,1) LE 'Z').LISTOK
.ERR1    MNOTE 12,'*** IHB300 LIST OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.LISTOK  ANOP
         AIF   ('&MAXDEEP' NE '').CHK
         MNOTE 12,'*** IHB001 MAXDEEP OPERAND REQ''D-NOT SPECIFIED'
&NOGO    SETB  1
         AGO   .MAXOK
.CHK     ANOP
         AIF   ('&MAXDEEP'(1,1) LT '1' OR '&MAXDEEP'(1,1) GT '9').MAXER
         AIF   ('&MAXDEEP' LT '1' OR '&MAXDEEP' GT '65535').MAXER
         IEDQMASK  &MAXDEEP,2
         AIF   (NOT &IEDQZA(24)).MAXOK
.MAXER   MNOTE 12,'*** IHB300 MAXDEEP OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.MAXOK   ANOP
         AIF   (&NOGO).MEND
         IEDQVCON  58,IEDQBV
         CNOP  0,4
         BAL   1,*+12
&NAME    DC    AL1(&IEDQZE(58)),AL1(8) .
         DC    AL2(&MAXDEEP) .          MAXDEEP VALUE
&C       SETC  '&SYSECT'
IEDQMISC CSECT
         DC    C'&LIST'
&C       CSECT
         DC    AL1(&IEDQZD),AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&IEDQZD
         L     15,IEDUI
         BALR  14,15
.MEND    SPACE 2
         MEND
