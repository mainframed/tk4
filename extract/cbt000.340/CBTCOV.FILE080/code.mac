         MACRO
&NAME    CODE  &TBL,&IN=(1030,1050,105F,1060,2260,2265,2740,274F,ITA2,Z*
               SC3,TTYA,TTYB,TTYC,6BIT,ASCI,EBCD,BC41,EB41,CR41),&BLANK*
               =YES
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB
         LCLA  &IX,&FOUR,&T,&BIT
         LCLB  &NOGO
         LCLC  &C,&PARM
.*             VERSION DATED AUGUST 28, 1970
         SPACE
&IX      SETA  1
&T       SETA  10
         AIF   ((&IEDQZA(1) OR &IEDQZA(2)) AND NOT &IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INHDR, INB*
               UF, OUTHDR OR OUTBUF MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&TBL' EQ '').ORG
         AIF   ('&TBL'(1,1) EQ '(' AND '&TBL'(K'&TBL,1) EQ ')').ORG
         AIF   (K'&TBL LE 8).ORG
         MNOTE 12,'***  IHB300  TABLE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ORG     AIF   (&NOGO).MEND
         AIF   (NOT &IEDQZA(3)).SMV
         IEDQVCON (9,12),(IEDQAW,IEDQAQ)
         AGO   .PARM
.SMV     IEDQVCON 9,IEDQAW
.PARM    AIF   ((&IEDQZA(1) AND NOT &IEDQZA(3)) OR (&IEDQZA(2) AND NOT *
               &IEDQZA(4))).TEMP
         AGO   .BITS
.TEMP    ANOP
&BIT     SETA  32
.BITS    CNOP  0,4
         AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     AIF   ('&TBL' EQ '').SET
         AIF   ('&TBL' EQ 'NONE').OPCTL
&FOUR    SETA  4
         AIF   ('&TBL'(1,1) NE '(').SET
&C       SETC  '&TBL'(2,K'&TBL-2)
         ST    &C,*+12 .                STORE TBL ADDR IN LIST
.SET     BAL   1,*+(8+&FOUR) .          SKIP AROUND PARMLIST
         DC    AL1(&IEDQZE(9)),AL1(4+&FOUR),AL1(0)
         AIF   ('&TBL' NE '').REG
         DC    AL1(128+&BIT) .          USE DCB TABLE
         AGO   .OUT
.REG     AIF   ('&TBL'(1,1) NE '(').USER
         DC    AL1(64+&BIT) .           NON-STANDARD TBL
         DC    A(0) .                   ADDRESS OF SPECIAL TABLE
         AGO   .OUT
.USER    AIF   (&IX GT 19).USETBL
         AIF   ('&TBL' EQ '&IN(&IX)').SETT
&IX      SETA  &IX+1
&T       SETA  &T+1
         AGO   .USER
.SETT    DC    AL1(&BIT) .              STANDARD TBL
&C       SETC  'IEDQ'.'&T'
         DC    V(&C) .                  STANDARD TABLE
         AGO   .OUT
.USETBL  DC    AL1(64+&BIT) .           NON-STANDARD TBL
         DC    A(&TBL) .                ADDRESS OF SPECIAL TABLE
.OUT     L     15,IEDUI .               CODE ROUTINE
         BALR  14,15
.OPCTL   AIF   (NOT &IEDQZA(3)).MEND
         CNOP  0,4
         IEDQSCAN 255,,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BM    *+16
         LR    1,15
         L     15,IEDMSGS-1 .           OPERATOR CONTROL TEST
         L     15,&IEDQZE(12)(,15)
         BALR  14,15
.MEND    SPACE 2
         MEND
