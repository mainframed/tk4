         MACRO
&NAME    INHDR &PATH=
         GBLA  &IEDQZE(64)                                       S22025
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB,&IEDQZJ
         LCLB  &NOGO
.*             VERSION DATED NOVEMBER 15, 1971                   S22025
         SPACE
&IEDQZA(6) SETB 1
         AIF   (&IEDQZA(5) OR &IEDQZA(10)).MN
         AIF   (&IEDQZA(46)).PTEST
.MN      MNOTE 12,'***  IHB070  SEQUENCE ERROR - MUST FOLLOW INBLOCK SU*
               BGROUP OR STARTMH MACRO'                          S22025
&NOGO    SETB  1
.PTEST   AIF   ('&PATH' EQ '').GLOB
         AIF   ('&PATH(1)' NE '' AND '&PATH(2)' NE '').PATH
         AGO   .PERR
.PATH    AIF   ('&PATH(1)'(K'&PATH(1),1) EQ ')' OR '&PATH(1)'(K'&PATH(1*
               ),1) EQ '''').PERR
         IEDQMASK &PATH(2),1
         AIF   (NOT &IEDQZA(24)).GLOB
.PERR    MNOTE 12,'***  IHB300  PATH OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.GLOB    AIF   (&NOGO).MEND
         AIF   ('&PATH' EQ '').NAM
         AIF   ('&PATH(2)' EQ '0' OR '&PATH(2)' EQ 'X''00''' OR '&PATH(*
               2)' EQ 'X''0''' OR '&PATH(2)'(1,2) EQ 'XL').PERR
.NAM     AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     ANOP
&IEDQZA(1) SETB 1
&IEDQZA(2) SETB 0
&IEDQZA(4) SETB 0
&IEDQZA(5) SETB 0
&IEDQZA(14) SETB 0                                               S22025
&IEDQZA(23)    SETB 0                                            S22025
         AIF   (NOT &IEDQZA(20)).NXT
         AIF   ('&IEDQZJ' EQ '').NXT
&IEDQZJ  DS    0H
.NXT     ANOP
&IEDQZB  DS    0H .                                              S22025
         AIF   (&IEDQZA(3)).NOAB                                 S22025
         IEDQVCON 56,IEDQAB                                      S22025
         BAL   1,*+6                                             S22025
         DC    AL1(&IEDQZE(56)),AL1(2)                           S22025
         L     15,IEDUI                                          S22025
         BALR  14,15                                             S22025
.NOAB    ANOP                                                    S22025
&IEDQZA(3)  SETB 1
         L     1,IEDADBUF .             GET BUFFER ADDR          S22025
&IEDQZB  SETC  'IEDR'.'&SYSNDX'
         TM    20(1),X'81' .            TEXT OR RECALLED HDR
         BNZ   &IEDQZB                  BRANCH IF EITHER
         AIF   ('&PATH' EQ '').MEND
         LOCOPT &PATH(1)
         AIF   (&IEDQZA(61)).MEND
         LTR   15,15 .                  OPTION FIELD FOUND
         BZ    &IEDQZB .                NO - GO TO NEXT DELIM
         TM    0(15),&PATH(2) .         PATH SWITCHES ON
         BZ    &IEDQZB .                NO - GO TO NEXT DELIM
.MEND    SPACE 2
&IEDQZJ  SETC  ''
&IEDQZA(20) SETB 0
         MEND
