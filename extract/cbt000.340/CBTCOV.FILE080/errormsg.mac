         MACRO
&NAME    ERRORMSG &MASK,&DEST=,&DATA=,&EXIT=,&CONNECT=OR,      @OY12678*
               &HEADER=YES                                     @OY12678
         GBLA  &IEDQZD,&IEDQZE(64),&IEDQZK
         GBLB  &IEDQZA(64)
         LCLA  &LEN,&BIT,&D,&ALG,&U
         LCLB  &NOGO
         LCLC  &C
.*C552000                                                       SA58454
.*A440000,480000                                                SA59170
.*C472000                                                       SA59170
.*             VERSION DATED NOVEMBER 1,1972                    SA59170
         SPACE
&C       SETC  '&SYSECT'
&D       SETA  0
         AIF   ('&HEADER' EQ 'NO').TSOCHK                      @OY12678
         AIF   (('&HEADER' EQ 'YES') AND (NOT &IEDQZA(9))).TSOCHK
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
         AGO   .MEND                                                TSO
.TSOCHK  ANOP                                                       TSO
         AIF   (&IEDQZA(5)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG OR OUT*
               MSG MACRO'
&NOGO    SETB  1
.CHECK   ANOP                                                  @OY12678
         AIF   ('&HEADER' EQ 'YES').ANYDEST                    @OY12678
         AIF   ((&IEDQZA(2) AND &IEDQZA(5) AND '&DEST' NE 'ORIGIN') OR *
               (&IEDQZA(1) AND &IEDQZA(5))).GOODOUT            @OY12678
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INMSG MACRO'
&NOGO    SETB  1                                               @OY12678
.GOODOUT ANOP                                                  @OY12678
         AIF   ((&IEDQZA(1) AND &IEDQZA(5) AND '&DEST' NE 'DESTIN') OR *
               (&IEDQZA(2) AND &IEDQZA(5))).ANYDEST            @OY12678
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW OUTMSG MACRO*
               '                                               @OY12678
&NOGO    SETB  1                                               @OY12678
.ANYDEST ANOP                                                  @OY12678
         AIF   ('&DATA' NE '').MASKCHK                         @OY12678
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
&NOGO    SETB  1
.MASKCHK AIF   ('&MASK' EQ '').NXT
         IEDQMASK &MASK,5
         AIF   (&IEDQZA(24)).ERR
         AIF   ('&MASK'(1,1) NE 'X').NXT
         AIF   ('&MASK'(2,1) EQ 'L' AND '&MASK'(3,1) NE '5').ERR
         AIF   ('&MASK'(2,1) NE 'L' AND K'&MASK NE 13).ERR
         AGO   .NXT
.ERR     MNOTE 12,'***  IHB300  MASK OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.NXT     AIF   ('&DEST' EQ '').DATACHK
         AIF   ('&DEST' EQ 'ORIGIN' OR '&DEST' EQ 'DESTIN').DATACHK
         IEDQCHAR 8,&DEST
&D       SETA  &IEDQZD
         AIF   (&IEDQZA(24)).DESTERR
         AIF   (&IEDQZD LE 8).DATACHK
.DESTERR MNOTE 12,'***  IHB300  DESTINATION OPERAND INVALID AS SPECIFIE*
               D'
&NOGO    SETB  1
.DATACHK AIF   ('&DATA'(K'&DATA,1) EQ ')').DATAERR
         AIF   ('&DATA'(K'&DATA,1) NE '''' AND K'&DATA GT 8).DATAERR
         AIF   ('&DATA'(K'&DATA,1) NE '''').EXITCHK
         IEDQCHAR 255,&DATA
         AIF   (&IEDQZA(24)).DATAERR
         AIF   (&IEDQZD LE 255).EXITCHK
.DATAERR MNOTE 12,'***  IHB300  DATA OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.EXITCHK AIF   ('&EXIT' EQ '').RTNCHK
&U       SETA  4
         AIF   (K'&EXIT LE 8).RTNCHK
         MNOTE 12,'***  IHB300  EXIT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.RTNCHK  AIF   ('&CONNECT' EQ 'AND' OR '&CONNECT' EQ 'OR').OP
         AIF   ('&CONNECT' EQ 'NAND').OP                        SA59170
         MNOTE 12,'***  IHB300  CONNECT OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.OP      AIF   (&NOGO).MEND
         AIF   ('&CONNECT' NE 'AND').CKNAND                     SA59170
&ALG     SETA  1
         AGO   .ORG                                             SA59170
.CKNAND  AIF   ('&CONNECT' NE 'NAND').ORG                       SA59170
&ALG     SETA  2                                                SA59170
.ORG     AIF   ('&DEST' EQ '').SMV
         AIF   ('&DEST' EQ 'ORIGIN' OR '&DEST' EQ 'DESTIN' OR '&DEST'(K*
               '&DEST,1) EQ '''').SMV
         IEDQVCON (7,3,31,38),(IEDQAZ,IEDQAT,IEDQAE,IEDQAF)
         AGO   .TEST
.SMV     IEDQVCON (7,3,38),(IEDQAZ,IEDQAT,IEDQAF)
.TEST    AIF   (NOT &IEDQZA(1)).AT
&IEDQZA(18) SETB 1
.AT      AIF   ('&MASK' EQ '0' OR '&MASK' EQ 'XL5''0''').SET    SA58454
         AIF   ('&MASK' EQ 'X''0000000000''').SET               SA58454
         AIF   ('&MASK' EQ 'XL5''0000000000''').SET             SA58454
         AIF   ('&MASK' EQ 'XL5''00''' OR '&MASK' EQ '').SET    SA58454
         AGO   .SKP                                             SA58454
.SET     ANOP                                                   SA58454
&BIT     SETA  1
.SKP     ANOP
         AIF   ('&HEADER' EQ 'NO').SKPRCL                      @OY12678
&BIT     SETA  &BIT+2
.SKPRCL  ANOP                                                  @OY12678
         AIF   (&BIT NE 1 AND &BIT NE 3).M
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(16+&ALG+&U),AL1(1),AL1(0)
         AGO   .DEST
.M       ANOP
&NAME    DC    AL1(&IEDQZE(7)+&BIT),AL1(20+&ALG+&U),AL1(1)
&LEN     SETA  0
         AIF   ('&MASK'(1,1) EQ 'X').XM
         DC    FL5'&MASK' .             ERROR MASK
         AGO   .DEST
.XM      DC    &MASK .                  ERROR MASK
.DEST    AIF   ('&DEST' EQ '').ORGDST
         AIF   ('&DEST' EQ 'ORIGIN').S
         AIF   ('&DEST' EQ 'DESTIN').D
         AIF   ('&DEST'(K'&DEST,1) EQ '''').DST
         DC    C'O',AL1(&IEDQZE(31)),AL1(&DEST),AL1(16)
         AGO   .DATA
.DST     ANOP
&LEN     SETA  1
IEDQMISC CSECT
         DC    AL1(&D),&DEST .          DESTINATION
         AIF   ('&DATA'(K'&DATA,1) NE '''').OUT
         DC    &DATA .            ERROR MESSAGE
.OUT     ANOP
&C       CSECT
         DC    C'N',AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+1+&D
         AGO   .DATA
.ORGDST  AIF   (&IEDQZA(2)).D
.S       DC    C'S',AL3(0) .            SEND TO SOURCE
         AGO   .DATA
.D       DC    C'D',AL3(0) .            SEND TO DESTINATION
.DATA    DC    AL1(&IEDQZE(3)),AL1(8+&U),AL1(&IEDQZE(38)),AL1(0)
         AIF   ('&DATA'(K'&DATA,1) EQ '''').CD
         DC    AL1(0),AL3(&DATA) .      ERROR MESSAGE
         AGO   .GO
.CD      AIF   (&LEN EQ 1).DCV
IEDQMISC CSECT
         DC    &DATA .                  ERROR MESSAGE
&C       CSECT
.DCV     DC    AL1(&IEDQZD),AL3(IEDQMISC+&IEDQZK)
&IEDQZK  SETA  &IEDQZK+&IEDQZD
.GO      AIF   ('&EXIT' EQ '').MEND
         DC    AL4(&EXIT) .             ADDRESS OF EXIT ROUTINE
.MEND    SPACE 2
         MEND
