         MACRO
&A       TPDATE &DCB=,&DTSAREA=,&RECDLM=NO,&DELETE=NO
         GBLB  &IEDQB2
         LCLA  &SWITCH
.* CHANGE ACTIVITY AS FOLLOWS
.******************* MICROFICHE FLAGS *********************** SUPT CODE
.*C 570000                                                       S21903
.*D 000500                                                       S21903
.*C080000                                                       SA61065
         AIF   ('&DCB' EQ '').ERR1
         AIF   (N'&SYSLIST GT 4).ERR2
         CNOP  0,4
&A       DS    0H                                               SA61065
         AIF   (&IEDQB2).NOVCON                                 SA61065
         B     *+8
IEDQDTS  DC    V(IEDQB2) .              ADDRESS OF TPDATE ROUTINE
&IEDQB2  SETB  1
.NOVCON  ANOP
         AIF   ('&RECDLM' EQ 'NO').NOREC
         AIF   ('&RECDLM' EQ 'YES').REC
         MNOTE 12,'*** IHB002 INVALID RECDLM OPERAND SPECIFIED'
         AGO   .NOREC
.REC     ANOP
&SWITCH  SETA  128
.NOREC   ANOP
         AIF   ('&DELETE' EQ 'NO').NODEL
         AIF   ('&DELETE' EQ 'YES').DEL
         MNOTE 12,'*** IHB002 INVALID DELETE OPERAND SPECIFIED'
         AGO   .NODEL
.DEL     ANOP
&SWITCH  SETA  &SWITCH+64
.NODEL   ANOP
         AIF   ('&DCB'(1,1) EQ '(').DCBREG
         LA    1,&DCB
         AGO   .GOTDCB
.DCBREG  ANOP
         LR    1,&DCB(1)
         LA    1,0(1)
.GOTDCB  ANOP
         AIF   ('&DTSAREA' EQ '').TAG
&SWITCH  SETA  &SWITCH+32
.TAG     ANOP
         LA    0,&SWITCH
         SLL   0,24
         AR    0,1
         AIF   ('&DTSAREA' EQ '').NODTS
         AIF   ('&DTSAREA'(1,1) EQ '(').DREG
         LA    1,&DTSAREA
.GOTDTS  ANOP
         L     15,IEDQDTS
         BALR  14,15
         MEXIT
.DREG    ANOP
         LR    1,&DTSAREA(1)
         LA    1,0(1)
         AGO   .GOTDTS
.NODTS   ANOP
         SR    1,1
         AGO   .GOTDTS
.ERR1    MNOTE 12,'*** IHB004 REQUIRED OPERAND NOT SPECIFIED'
         MEXIT
.ERR2    MNOTE 12,'*** IHB004  EXCESSIVE OPERANDS SPECIFIED '    S21903
         MEND
