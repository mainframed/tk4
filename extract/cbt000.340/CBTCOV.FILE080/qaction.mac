         MACRO
&NAME    QACTION   &EXIT=,&TYPE=V
         GBLB  &IEDQZA(64)
         GBLA  &IEDQZE(64)
         LCLB  &NOGO
         LCLC  &C,&T
.*             VERSION DATED AUGUST 19, 1971
         SPACE
         AIF   (NOT &IEDQZA(9)).CONCHK
         MNOTE 12,'*** IHB312 MUST FOLLOW STARTMH MACRO WITH TSOMH=NO'
&NOGO    SETB  1
.CONCHK  ANOP
         AIF   (&IEDQZA(13)).CSPEC
         MNOTE 8,'*** IHB233 INCONSISTENT WITH FEATURE OPERAND OF THE IX
               NTRO MACRO' .
&NOGO    SETB  1
.CSPEC   ANOP
         AIF   (&IEDQZA(3)).QA1
         MNOTE 12,'*** IHB070 SEQUENCE ERROR- MUST FOLLOW INHDR MACRO'
&NOGO    SETB  1
.QA1     AIF   ('&EXIT' NE '').QA2
         MNOTE 12,'*** IHB001 EXIT OPERAND REQ''D- NOT SPECIFIED'
&NOGO    SETB  1
.QA2     ANOP
&T       SETC  '&TYPE'
         AIF   ('&TYPE' EQ 'V' OR '&TYPE' EQ 'A').QA3
&T       SETC  'V'
         MNOTE 4,'*** IHB086 TYPE INVALID KEYWORD, V ASSUMED'
.QA3     AIF   (&NOGO).MEND
&C       SETC  'IEDQBQ'
         IEDQVCON 49,&C
         CNOP  0,4
&NAME    BAL   1,*+12 .                 SKIP PARAMETER LIST
         DC    AL1(&IEDQZE(49)),AL1(8),AL2(0)
         DC    &T.(&EXIT)
         L     15,IEDUI .               EXIT TO QACTION MODULE
         BALR  14,15
.MEND    SPACE 2
         MEND
