         MACRO
&NAME   TRANLIST &LIST=,&CHARS=,&MODEL=(1030,1050,105F,1060,2260,2265,2*
               740,274F,ITA2,ZSC3,TTYA,TTYB,TTYC,6BIT,ASCI,EBCD,BC41,EB*
               41,CR41),&OPFLD=,&L2741=,&L1050=
         GBLA  &IEDQZE(64)
         GBLB  &IEDQZA(64)
         LCLA  &N,&X,&CT,&A
         LCLB  &NOGO
         LCLC  &BUCKET
.*A161000                                                       SA70210
         AIF   ('&NAME' NE '').OK
         MNOTE 12,'XXX IHB     -NAME OPERAND REQUIRED.'
&NOGO    SETB  1
.OK      AIF   ('&LIST' NE '' OR '&L2741' NE '' OR '&L1050' NE '').OK1A
         MNOTE 12,'XXX IHB    TRANSLATE TABLES REQUIRED.'
&NOGO    SETB  1
.OK1A    AIF   ('&LIST' NE '' AND '&L1050' NE '').ER1
         AIF   ('&LIST' NE '' AND '&L2741' NE '').ER1
         AGO   .OK1
.ER1     MNOTE 12,'XXX IHB    - LIST AND L2741 OR L1050 ARE MUTUALLY EXC
               CLUSIVE'
&NOGO    SETB  1
.OK1     AIF   ('&CHARS' NE '').OK2
         MNOTE 'XXX IHB    - CHARACTER STRINGS MUST BE SPECIFIED.'
&NOGO    SETB  1
         AGO   .OPCHK                                           SA70210
.OK2     ANOP
         AIF   (N'&CHARS LE 5).OK3
         MNOTE 12,'XXX IHB    -NUMBER OF STRINGS MUST BE 5 OR LESS.'
&NOGO    SETB  1
.OK3     ANOP
&N       SETA  N'&CHARS
.LPP     AIF   (K'&CHARS(&N) LT 9).OK4
         MNOTE 12,'XXX IHB    -MAXIMUM CHARACTER STRING IS 8.'
&NOGO    SETB  1
.OK4     ANOP
&N       SETA  &N-1
         AIF   (&N NE 0).LPP
&N       SETA  N'&CHARS
.NEXT    AIF   (K'&CHARS(&N) LT 9 AND '&CHARS(&N)'(1,1) LT '0').OK5
         MNOTE 12,'XXX IHB    -ILLEGAL CHARACTER STRING.'
&NOGO    SETB  1
.OK5     ANOP
&N       SETA  &N-1
         AIF   (&N NE 0).NEXT
.OPCHK   AIF   ('&OPFLD' NE '').OK6
.EOP     MNOTE 12,'XXX IHB    - OPTION FIELD MUST BE SPECIFIED.'
&NOGO    SETB  1
.OK6     AIF   (K'&OPFLD LE 8).CONTX
.EOO     MNOTE 12,'XXX IHB    - OPFLD INVALID AS SPECIFIED.'
&NOGO    SETB  1
         AIF   ('&OPFLD(1)' GE 'A' AND '&OPFLD(1)' LE 'Z').CONTX
         MNOTE 12,'XXX IHB    - OPTION FIELD INVALID AS SPECIFIED.'
.CONTX   AIF   (NOT &NOGO).CONT
         MEXIT
.CONT    ANOP
         AIF   (&IEDQZA(62)).GENA3
&IEDQZA(62) SETB 1
         ORG   IEDA3TL
         DC    V(IEDQA3)
         ORG
.GENA3   ANOP
         DS    0F
&NAME    DC    XL1'80',AL1(&OPFLD)
&N       SETA  N'&CHARS
&X       SETA  N'&LIST+N'&L2741+N'&L1050
         DC    AL1(&N) .                NUMBER OF STRINGS
         DC    AL1(&X) .                NUMBER OF TRANSLATE TABLES
         AIF   ('&LIST' EQ '').C2741
&X       SETA  1
&N       SETA  1
.LSTLP   ANOP
&A       SETA  0
&CT      SETA  K'&LIST(&X)
&BUCKET  SETC  '&LIST(&X)'(1,&CT)
         AIF   (&CT NE 4).LSTUSR
.LSTLP1  ANOP
&A       SETA  &A+1
         AIF   (&A GT N'&MODEL).LSTUSR
         AIF   ('&LIST(&X)'(1,4) NE '&MODEL(&A)'(1,4)).LSTLP1
&A       SETA  &A+9
.LSTCON  DC    XL1'00',VL3(IEDQ&A)
.LSTLPQ  ANOP
&X       SETA  &X+1
         AIF   (&X LE N'&LIST).LSTLP
         AGO   .C2741
.LSTUSR  DC    XL1'00',VL3(&BUCKET)
         AGO   .LSTLPQ
.C2741   AIF   ('&L2741' EQ '').C1050
&X       SETA  1
&N       SETA  1
.L41LP   ANOP
&A       SETA  0
&CT      SETA  K'&L2741(&X)
&BUCKET  SETC  '&L2741(&X)'(1,&CT)
         AIF   (&CT NE 4).L41USR
.L41LP1  ANOP
&A       SETA  &A+1
         AIF   (&A GT N'&MODEL).L41USR
         AIF   ('&L2741(&X)'(1,4) NE '&MODEL(&A)'(1,4)).L41LP1
&A       SETA  &A+9
.L41CON  DC    XL1'01',VL3(IEDQ&A)
.L41LPQ  ANOP
&X       SETA  &X+1
         AIF   (&X LE N'&L2741).L41LP
         AGO   .C1050
.L41USR  DC    XL1'01',VL3(&BUCKET)
         AGO   .L41LPQ
.C1050   AIF   ('&L1050' EQ '').NXT
&X       SETA  1
&N       SETA  1
.L50LP   ANOP
&A       SETA  0
&CT      SETA  K'&L1050(&X)
&BUCKET  SETC  '&L1050(&X)'(1,&CT)
         AIF   (&CT NE 4).L50USR
.L50LP1  ANOP
&A       SETA  &A+1
         AIF   (&A GT N'&MODEL).L50USR
         AIF   ('&L1050(&X)'(1,4) NE '&MODEL(&A)'(1,4)).L50LP1
&A       SETA  &A+9
.L50CON  DC    XL1'02',VL3(IEDQ&A)
.L50LPQ  ANOP
&X       SETA  &X+1
         AIF   (&X LE N'&L1050).L50LP
         AGO   .NXT
.L50USR  DC    XL1'02',VL3(&BUCKET)
         AGO   .L50LPQ
.NXT     ANOP
&CT      SETA  K'&CHARS(&N)
&BUCKET  SETC  '&CHARS(&N)'(1,&CT)
&X       SETA  &CT
.LOOPR AIF ('&CHARS(&N)'(&CT,1) EQ '''').REDU
&X       SETA  &X-1
         AIF   (&X NE 0).LOOPR
         AGO   .OKQU
.REDU    ANOP
&CT      SETA  &CT-1
.OKQU    ANOP
         DC    AL1(&CT)
         DC    C'&BUCKET'
&N       SETA  &N+1
         AIF   (&N LE N'&CHARS).NXT
         MEND
