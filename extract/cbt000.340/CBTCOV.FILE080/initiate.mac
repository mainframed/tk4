         MACRO
&NAME    INITIATE &CHARS,&BLANK=YES
         GBLA  &IEDQZD
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB
         LCLB  &NOGO
.*             VERSION DATED DECEMBER 23, 1970
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
&NOGO    SETB  1
.TSOCHK  AIF   (&IEDQZA(3)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&CHARS' EQ '').DOIT
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).ERR
         AIF   (&IEDQZD LE 8).ORG
.ERR     MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.ORG     AIF   (&NOGO).MEND
         AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   *+28
.DOIT    AIF   (&NOGO).MEND
         AIF   ('&CHARS' NE '').GO
         AIF   ('&NAME' EQ '').GO
&NAME    DS    0H
.GO      L     1,IEDADBUF .             GET BUFFER ADDRESS
         TM    20(1),X'01' .            IS THIS A TEXT BUFFER
         AIF   ('&CHARS' NE '').HI
         BO    *+12 .                   YES, DON'T SET BIT
         AGO   .LOAD
.HI      BO    *+16 .                   YES, DON'T SET BIT
         STH   15,24(,1) .              UPDATE SCAN POINTER
.LOAD    L     1,12(,1) .               GET LCB ADDRESS
         OI    70(1),X'10' .            SET ON INITIATE BIT
.MEND    SPACE 2
         MEND
