         MACRO
&NAME    LOCK  &TYPE,&CHARS,&BLANK=YES
         GBLA  &IEDQZD,&IEDQZE(64)
         GBLB  &IEDQZA(64)
         GBLC  &IEDQZB
         LCLB  &LEN,&NOGO
.*             VERSION DATED OCTOBER 16, 1970
         SPACE
         AIF   (NOT &IEDQZA(9)).TSOCHK                              TSO
         MNOTE 12,'***  IHB312  MUST FOLLOW STARTMH MACRO WITH TSOMH=NO*
               '
&NOGO    SETB  1
.TSOCHK  AIF   (&IEDQZA(3)).CHECK
         MNOTE 12,'***  IHB070  SEQUENCE ERROR-MUST FOLLOW INHDR MACRO'
&NOGO    SETB  1
.CHECK   AIF   ('&TYPE' EQ '').CHAR
         AIF ('&TYPE' EQ 'MESSAGE' OR '&TYPE' EQ 'EXTEND').CHAR
         MNOTE 12,'***  IHB300  TYPE OPERAND INVALID AS SPECIFIED'
&NOGO    SETB  1
.CHAR    AIF   (&NOGO).MEND
         AIF   ('&TYPE' EQ 'EXTEND').SKP
&LEN     SETB  1
.SKP     AIF   ('&NAME' EQ '').NON
&NAME    DS    0H
.NON     AIF   ('&CHARS' EQ '').DOIT
         IEDQCHAR 8,&CHARS
         AIF   (&IEDQZA(24)).CHARERR
         AIF    (&IEDQZD LE 8).ORG
.CHARERR MNOTE 12,'***  IHB300  CHARACTER OPERAND INVALID AS SPECIFIED'
         AGO   .MEND
.ORG     IEDQSCAN &IEDQZD,&CHARS,&BLANK
         AIF   (&IEDQZA(24)).MEND
         LTR   15,15
         BNP   *+24
         L     1,IEDADBUF .             GET BUFFER ADDRESS
         STH   15,24(,1) .              STORE SCAN POINTER IN BFR PFX
.DOIT    IEDQVCON 23,IEDQBE
         BAL   1,*+6
         DC    AL1(&IEDQZE(23)),AL1(&LEN)
         L     15,IEDUI
         BALR  14,15
.MEND    SPACE 2
         MEND
