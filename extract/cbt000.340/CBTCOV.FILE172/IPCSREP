00005030PROC 0 DEBUG TERM() PRNT() LIST() ABST() STAT() DESC() OPEN() +
00005040       CLOS() ZERO() ONE() TWO() THRE() FOUR() +
00005050       DSN() GRP() COMP() USER() OWNER() SYS() PARM(00) NOSEV(0) +
00005060       SWTCH(N)
00005070/*********************************************************************/
00005080/*  REFERENCE: SYS.MIPCS.CLIST(IPCSREP)                              */
00005090/*                                                                   */
00005100/*  CLIST TO GENERATE A REPORT BASED ON SEARCH ARGUMENTS FROM INPUT. */
00005110/*  PARMS:                                                           */
00005120/*                                                                   */
00005130/*  TERM      - OUTPUT TO TERMINAL?                                  */
00005140/*  PRNT      - OUTPUT TO PRINT?                                     */
00005150/*  LIST      - GENERATE SIMPLE LIST OF PROBLEMS                     */
00005160/*  ABST      - GENERATE LIST WITH ABSTRACT                          */
00005170/*  STAT      - GENERATE LIST WITH STATUS                            */
00005180/*  DESC      - GENERATE LIST WITH PROBLEM DESCRIPTION               */
00005190/*  OPEN      - LIST OPEN PROBLEMS                                   */
00005200/*  CLOS      - LIST CLOSED PROBLEMS                                 */
00005210/*  ZERO      - LIST SEVERITY 0 PROBLEMS                             */
00005220/*  ONE       - LIST SEVERITY 1 PROBLEMS                             */
00005230/*  TWO       - LIST SEVERITY 2 PROBLEMS                             */
00005240/*  THRE      - LIST SEVERITY 3 PROBLEMS                             */
00005250/*  FOUR      - LIST SEVERITY 4 PROBLEMS                             */
00005260/*  DSN       - GENERATE LIST WITH ASSOCIATED DATA SET NAMES         */
00005270/*  GRP       - SEARCH ON GROUP                                      */
00005280/*  COMP      - SEARCH ON COMPONENT ID                               */
00005290/*  USER      - SEARCH ON USER GROUP                                 */
00005300/*  OWNER     - SEARCH ON OWNER                                      */
00005310/*  SYS       - SEARCH ON SYSTEM (CPU) NUMBER                        */
00005320/*  PARM(00)  - SYS1.PARMLIB(IPCSPRXX)                               */
00005330/*  NOSEV(0)  - SWITCH IF NO SEVERITY WAS USED AS SEARCH ARGUMENT    */
00005340/*  SWTCH     - SWITCH                                               */
00005350/*                                                                   */
00005360/*********************************************************************/
00005370CONTROL NOMSG NOFLUSH
00005380ALLOC DDN(IPCSPRNT) SYSOUT(A)
00005390CONTROL MSG FLUSH
00005400CONTROL PROMPT END(ENDO)
00005410IF &DEBUG EQ DEBUG THEN CONTROL SYMLIST CONLIST LIST
00005420/*                                                                   */
00005430IF &TERM = Y THEN SET &TERM = TERMINAL
00005440             ELSE SET &TERM = NOTERMINAL
00005450IF &PRNT = Y THEN SET &PRNT = PRINT
00005460             ELSE SET &PRNT = NOPRINT
00005470/*                                                                   */
00005480IF &DESC = Y THEN SET &REPTYP = DESCRIPTION
00005490   ELSE IF &STAT = Y THEN SET &REPTYP = STATUS
00005500      ELSE IF &ABST = Y THEN SET &REPTYP = ABSTRACT
00005510         ELSE IF &LIST = Y THEN DO
00005520                                   SET &SWTCH = Y
00005530                                   SET &REPTYP = LIST
00005540                                ENDO
00005550            ELSE SET &REPTYP =
00005560IF &DSN = Y THEN DO
00005570                    SET &DSN = DSN
00005580                    SET &REPTYP = DESCRIPTION
00005590                 ENDO
00005600            ELSE SET &DSN =
00005610/*                                                                   */
00005620IF &OPEN = Y AND &CLOS = Y THEN SET &PSTAT = &STR(PSTAT(OPEN,CLOSED))
00005630  ELSE IF &OPEN = Y AND &CLOS NE Y THEN SET &PSTAT = &STR(PSTAT(OPEN))
00005640  ELSE IF &OPEN NE Y AND &CLOS = Y THEN SET &PSTAT = &STR(PSTAT(CLOSED))
00005650  ELSE SET &PSTAT =
00005660/*                                                                   */
00005670IF &GRP   NE  THEN SET &GRP = &STR(GROUP(&GRP.))
00005680IF &COMP  NE  THEN SET &COMP = &STR(COMPID(&COMP.))
00005690IF &USER  NE  THEN SET &USER = &STR(USER(&USER.))
00005700IF &OWNER NE  THEN SET &OWNER = &STR(OWNER(&OWNER.))
00005710IF &SYS   NE  THEN SET &SYS = &STR(SYS(&SYS.))
00005720/*                                                                   */
00005730IF &ZERO = Y THEN SET &S0 = 0
00005740             ELSE SET &S0 =
00005750IF &ONE = Y THEN SET &S1 = 1
00005760            ELSE SET &S1 =
00005770IF &TWO = Y THEN SET &S2 = 2
00005780            ELSE SET &S2 =
00005790IF &THRE = Y THEN SET &S3 = 3
00005800             ELSE SET &S3 =
00005810IF &FOUR = Y THEN SET &S4 = 4
00005820             ELSE SET &S4 =
00005830IF &S0 =  AND &S1 =  AND &S2 =  AND &S3 =  AND &S4 =  THEN SET &SEV =
00005840   ELSE SET &SEV = &STR(SEV(&S0 &S1 &S2 &S3 &S4.))
00005850IF &TERM = TERMINAL THEN DO
00005860K
00005870WRITE &STR(-------------------------  IPCS - INQUIRY AND REPORT  -------------------------)
00005880WRITE
00005890ENDO
00005900/*                                                                   */
00005910IPCS PARM(00)
00005920SETDEF CONFIRM NODATASET FLAG(SEVERE) NOLIST NOPRINT NOPROBLEM -
00005930       DISPLAY(MACHINE REMARK REQUEST NOSTORAGE SYMBOL) -
00005940       TERMINAL NOTEST VERIFY
00005950/*                                                                   */
00005960IF &SWTCH = N THEN DO
00005970   LP LIST OWNER(ALL) &PSTAT &SEV &PRNT &TERM +
00005980      &GRP &COMP &USER &OWNER &SYS
00005990   NOTE SPACE(4)
00006000ENDO                                                                 */
00006010IF &STR(&ZERO) = Y THEN DO
00006020      NOTE 'SEVERITY ZERO PROBLEM REPORTS FOLLOW:' PAGE &PRNT &TERM
00006030      LP &REPTYP OWNER(ALL) &PSTAT SEVERITY(0) &PRNT &TERM &DSN +
00006040         &GRP &COMP &USER &OWNER &SYS
00006050      NOTE SPACE(4)
00006060   ENDO
00006070                   ELSE SET &NOSEV = &NOSEV +1
00006080IF &STR(&ONE) = Y THEN DO
00006090      NOTE 'SEVERITY ONE PROBLEM REPORTS FOLLOW:' PAGE &PRNT &TERM
00006100      LP &REPTYP OWNER(ALL) &PSTAT SEVERITY(1) &PRNT &TERM &DSN +
00006110         &GRP &COMP &USER &OWNER &SYS
00006120      NOTE SPACE(4)
00006130   ENDO
00006140                   ELSE SET &NOSEV = &NOSEV +1
00006150IF &STR(&TWO) = Y THEN DO
00006160      NOTE 'SEVERITY TWO PROBLEM REPORTS FOLLOW:' PAGE &PRNT &TERM
00006170      LP &REPTYP OWNER(ALL) &PSTAT SEVERITY(2) &PRNT &TERM &DSN +
00006180         &GRP &COMP &USER &OWNER &SYS
00006190      NOTE SPACE(4)
00006200   ENDO
00006210                   ELSE SET &NOSEV = &NOSEV +1
00006220IF &STR(&THRE) = Y THEN DO
00006230      NOTE 'SEVERITY THREE PROBLEM REPORTS FOLLOW:' PAGE &PRNT &TERM
00006240      LP &REPTYP OWNER(ALL) &PSTAT SEVERITY(3) &PRNT &TERM &DSN +
00006250         &GRP &COMP &USER &OWNER &SYS
00006260      NOTE SPACE(4)
00006270   ENDO
00006280                   ELSE SET &NOSEV = &NOSEV +1
00006290IF &STR(&FOUR) = Y THEN DO
00006300      NOTE 'SEVERITY FOUR PROBLEM REPORTS FOLLOW:' PAGE &PRNT &TERM
00006310      LP &REPTYP OWNER(ALL) &PSTAT SEVERITY(4) &PRNT &TERM &DSN +
00006320         &GRP &COMP &USER &OWNER &SYS
00006330      NOTE SPACE(4)
00006340   ENDO
00006350                   ELSE SET &NOSEV = &NOSEV +1
00006360IF &NOSEV = 5 THEN DO
00006370      NOTE 'ALL SEVERITY PROBLEM REPORT FOLLOWS:' PAGE &PRNT &TERM
00006380      LP &REPTYP OWNER(ALL) &PSTAT &PRNT &TERM &DSN +
00006390         &GRP &COMP &USER &OWNER &SYS
00006400      NOTE SPACE(4)
00006410   ENDO
00006420END /* IPCS */
