00000180PROC 0 DEBUG REP() USER() OWNER() S() STAT() DATE() TIME() A() D()+
00000190       T() SYS() GRP() COMP() LOC() DEV() UA() PARM(00)
00000200/*********************************************************************/
00000210/*  REFERENCE: SYS.MIPCS.CLIST(IPCSADD)                              */
00000220/*                                                                   */
00000230/*  CLIST FOR TO ADD A PROBLEM TO THE IPCS DATA BASE.                */
00000240/*  PARMS:                                                           */
00000250/*                                                                   */
00000260/*  REP       - REPORT NUMBER                                        */
00000270/*  USER      - USER GROUP ASSIGNED                                  */
00000280/*  OWNER     - LOGON ID OF OWNER                                    */
00000290/*  S         - PROBLEM SEVERITY                                     */
00000300/*  STAT      - STATUS OF PROBLEM                                    */
00000310/*  DATE      - DATE MM/DD/YY                                        */
00000320/*  TIME      - TIME HH:MM                                           */
00000330/*  A         - ABSTRACT Y/N                                         */
00000340/*  D         - DESCRIPTION Y/N                                      */
00000350/*  T         - ASSOCIATED DATA Y/N                                  */
00000360/*  SYS       - SYSTEM (CPU) ID                                      */
00000370/*  GRP       - GROUP ID                                             */
00000380/*  COMP      - COMPONENT ID                                         */
00000390/*  LOC       - LOCATION                                             */
00000400/*  DEV       - DEVICE TYPE                                          */
00000410/*  UA        - UNIT ADDRESS OR SERIAL NUMBER OF FAILING DEVICE      */
00000420/*  PARM(00)  - SYS1.PARMLIB(IPCSPRXX)                               */
00000430/*********************************************************************/
00000440/*                                                                   */
00000450/* ALLOCATE IPCSPRNT - NO MSG IF ALREADY ALLOCATED                   */
00000460CONTROL NOMSG NOFLUSH
00000470ALLOC DDN(IPCSPRNT) SYSOUT(A)
00000480CONTROL MSG FLUSH
00000490CONTROL PROMPT END(ENDO)
00000500IF &DEBUG EQ DEBUG THEN CONTROL SYMLIST CONLIST LIST
00000510/*                                                                   */
00000520SET &CLOSFLAG = 0  /* ASSUME PROBLEM WILL NOT BE CLOSED */
00000530/*                                                                   */
00000540IF &DATATYPE(&STR(&REP)) NE NUM THEN DO  /* REPORT # MUST BE A # */
00000550   WRITE INVALID REPORT NUMBER. TRY AGAIN.
00000560   EXIT QUIT
00000570ENDO
00000580/*                                                                   */
00000590/* UPDATE PARMS FOR CALL TO IPCS                                     */
00000600/*                                                                   */
00000610IF &STR(&DATE)  NE  THEN SET &DATE = &STR(DATE(&DATE.))
00000620IF &STR(&TIME)  NE  THEN SET &TIME = &STR(TIME(&TIME.:00))
00000630IF &STR(&SYS)   NE  THEN SET &SYS = &STR(SYSTEM(&SYS.))
00000640   ELSE IF &STR(&LOC) NE  THEN SET &SYS = &STR(SYSTEM(&LOC.))
00000650        ELSE DO
00000660             WRITE EITHER SYSTEM OR LOCATION MUST BE ENTERED!
00000670             WRITE CONTACT COORDINATOR FOR PROPER KEY-WORD OR
00000680             WRITE USE HELP PF KEY FOR DESCRIPTION OF KEY-WORDS.
00000690             EXIT QUIT
00000700        ENDO
00000710IF &STR(&GRP)   NE  THEN SET &GRP = &STR(GROUP(&GRP.))
00000720   ELSE IF &STR(&DEV) NE  THEN SET &GRP = &STR(GROUP(&DEV.))
00000730        ELSE DO
00000740             WRITE EITHER GROUP OR DEVICE TYPE MUST BE ENTERED!
00000750             WRITE CONTACT COORDINATOR FOR PROPER KEY-WORD OR
00000760             WRITE USE HELP PF KEY FOR DESCRIPTION OF KEY-WORDS.
00000770             EXIT QUIT
00000780        ENDO
00000790IF &STR(&COMP)  NE  THEN SET &COMP = &STR(COMPID(&COMP.))
00000800   ELSE IF &STR(&UA) NE  THEN SET &COMP = &STR(COMPID(&UA.))
00000810        ELSE DO
00000820             WRITE EITHER COMPONENT OR UNIT ADDRESS MUST BE ENTERED!
00000830             WRITE CONTACT COORDINATOR FOR PROPER KEY-WORD OR
00000840             WRITE USE HELP PF KEY FOR DESCRIPTION OF KEY-WORDS.
00000850             EXIT QUIT
00000860        ENDO
00000870IF &STR(&USER)  NE  THEN SET &USER = &STR(USER(&USER.))
00000880IF &STR(&OWNER) NE  THEN DO
00000890      SEND 'NEW PROBLEM ASSIGNED TO &OWNER &SYSDATE &SYSTIME BY' +
00000900           USER(&OWNER) LOGON NOWAIT
00000910      SET &OWNER = &STR(OWNER(&OWNER))
00000920   ENDO
00000930IF &STR(&S)     NE  THEN SET &SEV = &STR(SEVERITY(&S.))
00000940                    ELSE SET &SEV =
00000950IF &STR(&STAT)  NE  THEN DO
00000960      IF &SUBSTR(1:2,&STAT) = &STR(CL) THEN SET &CLOSFLAG = 1
00000970      SET &STAT = &STR(PSTAT(&STAT.))
00000980   ENDO
00000990IF &STR(&D) = Y THEN SET &DESC = &STR(DESC())
00001000                ELSE SET &DESC =
00001010IF &STR(&A) = Y THEN SET &ABST = &STR(ABST())
00001020                ELSE SET &ABST =
00001030/*                                                                    */
00001040WRITENR PROBLEM REPORTED BY (NAME IN QUOTES) ==>
00001050READ &NAME
00001060/*                                                                    */
00001070IPCS PARM(&PARM)
00001080SETDEF CONFIRM NODATASET FLAG(SEVERE) NOLIST NOPRINT NOPROBLEM +
00001090       DISPLAY(MACHINE REMARK REQUEST NOSTORAGE SYMBOL) +
00001100       TERMINAL NOTEST VERIFY
00001110/*                                                                    */
00001120AP &ABST DESC('REPORT NUMBER ==> &REP.') &DATE &TIME &STAT +
00001130   &SYS &GRP &COMP &USER &OWNER &SEV DEFAULT
00001140/*                                                                    */
00001150IF &STR(&NAME) NE  THEN MP DESC ('------REPORTED BY &NAME------')
00001160                   ELSE MP DESC ('------REPORTED BY &SYSUID------')
00001170/*                                                                    */
00001180MOREDESC:+
00001190   DO WHILE &DESC NE
00001200      MP &DESC
00001210      WRITENR IS MORE DESCRIPTION SPACE REQUIRED? (Y/N) ==>
00001220      READ &ANS
00001230      IF &ANS = Y THEN SET &DESC = &STR(DESC())
00001240                  ELSE SET &DESC =
00001250   ENDO
00001260/*                                                                    */
00001270ADDDSN: +
00001280   DO WHILE &DSN = Y
00001290      WRITE ENTER DATA SET TYPE (DUMP, PRINT, OR UDEF)
00001300      WRITENR DEFAULT IS DUMP.     ENTER TYPE ==>
00001310      READ &TYP
00001320      AD DESC() DSN() TYPE(&TYP) UNMANAGED DEFAULT NOCONFIRM
00001330      WRITENR ANY MORE RELATED DATA SETS? (Y/N) ==>
00001340      READ &DSN
00001350   ENDO
00001360/*                                                                    */
00001370LIST: +
00001380   IF &CLOSFLAG = 1 THEN +
00001390/*    PROBLEM WAS CLOSED WHEN ADDED */  +
00001400      DO
00001410         MP DESC ('------CLOSED &SYSDATE &SYSTIME BY &SYSUID------')
00001420         LP DESC DSN PRINT TERM
00001430         NOTE '******** THIS PROBLEM WAS CLOSED WHEN ENTERED ********' +
00001440            SPACE(2) PRINT NOTERMINAL
00001450      ENDO
00001460   ELSE LP DESC DSN PRINT TERM
00001470END  /* IPCS */
