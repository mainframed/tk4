00003660PROC 0 DEBUG PRB() USER() OWNER() SEV() PSTAT() ISTAT() FSTAT() +
00003670       PTFSTAT() APID() PTID() ABS() DESC() DSN() FID() +
00003680       SYS() GROUP() COMP() LOC() DEV() UA() PARM(00)
00003690/*********************************************************************/
00003700/*  REFERENCE: SYS.MIPCS.CLIST(IPCSMOD)                              */
00003710/*                                                                   */
00003720/*  CLIST TO MODIFY PROBLEMS ASSIGNED TO LOGON ID                    */
00003730/*  PARMS:                                                           */
00003740/*                                                                   */
00003750/*  PRB       - PROBLEM NUMBER                                       */
00003760/*  USER      - USER GROUP                                           */
00003770/*  OWNER     - TSO LOGON ID OF OWNER                                */
00003780/*  SEV       - SEVERITY                                             */
00003790/*  PSTAT     - PROBLEM STATUS                                       */
00003800/*  ISTAT     - IBM STATUS                                           */
00003810/*  FSTAT     - FIX STATUS                                           */
00003820/*  PTFSTAT   - PTF STATUS                                           */
00003830/*  APID      - APAR ID                                              */
00003840/*  PTID      - PTF ID                                               */
00003850/*  ABS       - ABSTRACT                                             */
00003860/*  DESC      - DESCRIPTION                                          */
00003870/*  DSN       - ASSOCIATED DATA SET NAME                             */
00003880/*  FID       - FIX ID                                               */
00003890/*  SYS       - SYSTEM (CPU) NUMBER                                  */
00003900/*  GROUP     - GROUP WITHIN USER GROUP                              */
00003910/*  COMP      - COMPONENT ID                                         */
00003920/*  LOC       - LOCATION                                             */
00003930/*  DEV       - DEVICE TYPE                                          */
00003940/*  UA        - UNIT ADDRESS                                         */
00003950/*  PARM      - SYS1.PARMLIB(IPCSPRXX)                               */
00003960/*                                                                   */
00003970/*  CALLS TO: SYS.MIPCS.CLIST(IPCSLOG) TO INSERT LOGGING             */
00003980/*            INFORMATION (USER, TIME, AND DATE STAMP)               */
00003990/*                                                                   */
00004000/*********************************************************************/
00004010CONTROL NOMSG NOFLUSH
00004020ALLOC DDN(IPCSPRNT) SYSOUT(A)
00004030CONTROL MSG FLUSH
00004040CONTROL PROMPT END(ENDO)
00004050IF &DEBUG EQ DEBUG THEN CONTROL SYMLIST CONLIST LIST
00004060                    /************************************************/
00004070                    /* INITIALIZE VARRIABLES                        */
00004080SET &CLOSFLAG = 0   /* ASSUME MODIFY IS NOT FOR CLOSE               */
00004090SET &OWNFLAG = 0    /* ASSUME MODIFY IS NOT FOR OWNER               */
00004100SET &OWN =          /*                                              */
00004110SET &ADMIN = IPCSAD /* SET ADMINISTRATOR LOGON ID FOR LOGGING CLOSE */
00004120                    /************************************************/
00004130IF &DATATYPE(&STR(&PRB)) NE NUM THEN DO
00004140      WRITE INVALID PROBLEM NUMBER. TRY AGAIN.
00004150      EXIT QUIT
00004160   ENDO
00004170/*                                                                    */
00004180IF &STR(&USER)  NE  THEN SET &USER = &STR(USER(&USER.))
00004190IF &STR(&OWNER) NE  THEN DO
00004200      SET &OWN = &STR(OWNER(&OWNER.))
00004210      SET &OWNFLAG = 1
00004220   ENDO
00004230IF &STR(&SEV) NE  THEN SET &SEV = &STR(SEVERITY(&SEV.))
00004240IF &STR(&PSTAT) NE  THEN DO
00004250      IF &SUBSTR(1,&PSTAT) = &STR(C) THEN SET &CLOSFLAG = 1
00004260      SET &PSTAT = &STR(PSTATUS(&PSTAT.))
00004270   ENDO
00004280IF &STR(&SYS) NE  THEN SET &SYS = &STR(SYSTEM(&SYS.))
00004290   ELSE IF &STR(&LOC) NE  THEN SET &SYS = &STR(SYSTEM(&LOC.))
00004300IF &STR(&GROUP) NE  THEN SET &GROUP = &STR(GROUP(&GROUP.))
00004310   ELSE IF &STR(&DEV) NE  THEN SET &GROUP = &STR(GROUP(&DEV.))
00004320IF &STR(&COMP)  NE  THEN SET &COMP = &STR(COMPID(&COMP.))
00004330   ELSE IF &STR(&UA) NE  THEN SET &COMP = &STR(COMPID(&UA.))
00004340IF &STR(&ISTAT) NE  THEN SET &ISTAT = &STR(IBMSTATUS(&ISTAT.))
00004350IF &STR(&FSTAT) NE  THEN SET &FSTAT = &STR(FIXSTATUS(&FSTAT.))
00004360IF &STR(&PTFSTAT) NE  THEN SET &PTFSTAT = &STR(PTFSTATUS(&PTFSTAT.))
00004370IF &STR(&FID) = Y THEN SET &FID = &STR(FIXID())
00004380IF &STR(&PTID) NE  THEN SET &PTID = &STR(PTFID(&PTID.))
00004390IF &STR(&APID) NE  THEN SET &APID = &STR(APARID(&APID.))
00004400IF &STR(&ABS) = Y THEN SET &ABS = &STR(ABS())
00004410                  ELSE SET &ABS =
00004420IF &STR(&DESC) = Y THEN DO
00004430      SET &DESC = &STR(DESC())
00004440      SET &D1= +
00004450        &STR(DESC('------UPDATE &SYSDATE &SYSTIME BY &SYSUID------'))
00004460   ENDO
00004470   ELSE DO
00004480      SET &DESC =
00004490      SET &D1 =
00004500   ENDO
00004510/*                                                                    */
00004520IPCS PARM(&PARM)
00004530SETDEF CONFIRM NODATASET FLAG(SEVERE) NOLIST NOPRINT +
00004540       DISPLAY(MACHINE REMARK REQUEST NOSTORAGE SYMBOL) +
00004550       TERMINAL NOTEST VERIFY PROBLEM(&PRB)
00004560/*                                                                    */
00004570MP PROB(&PRB) &GROUP &COMP &USER &OWN &SYS &SEV +
00004580   &PSTAT &ISTAT &FSTAT &PTFSTAT &FID &PTID &APID +
00004590   &ABS
00004600/*                                                                    */
00004610IF &LASTCC NE 0 THEN DO
00004620                        LP DESC DSN TERM PROB(&PRB) OWNER(ALL)
00004630                        END /*IPCS*/
00004640                        EXIT QUIT
00004650                     ENDO
00004660/*  TIMESTAMP AND UPDATE DESCRIPTION                                  */
00004670MP &D1
00004680DO WHILE &DESC NE
00004690   MP &DESC
00004700   WRITENR IS MORE DESCRIPTION SPACE REQUIRED? (Y/N) ==>
00004710   READ &ANS
00004720   IF &ANS = Y THEN SET &DESC = &STR(DESC())
00004730               ELSE SET &DESC =
00004740ENDO
00004750/*  ASSOCIATE A DATASET TO THIS PROBLEM                               */
00004760DO WHILE &DSN = Y
00004770   WRITE ENTER DATA SET TYPE (DUMP, PRINT, OR UDEF)
00004780   WRITENR DEFAULT IS DUMP.     ENTER TYPE ==>
00004790   READ &TYP
00004800   AD DESC() DSN() TYPE(&TYP) UNMANAGED DEFAULT NOCONFIRM
00004810   WRITENR ANY MORE RELATED DATA SETS? (Y/N) ==>
00004820   READ &DSN
00004830ENDO
00004840/*                                                                    */
00004850LIST: +
00004860   IF &OWNFLAG = 1 THEN DO
00004870/*    PROBLEM WAS REASSIGNED                                          */
00004880      MP DESC('------REASSIGNED &SYSDATE &SYSTIME BY &SYSUID TO &OWNER+
00004890                ------')
00004900      SEND 'PRB&PRB REASSIGNED &SYSDATE &SYSTIME TO &OWNER BY' +
00004910            USER(&OWNER) LOGON NOWAIT
00004920   ENDO
00004930   IF &CLOSFLAG = 1 THEN DO
00004940/*    MODIFY WAS TO CLOSE PROBLEM                                     */
00004950      MP DESC('------CLOSED &SYSDATE &SYSTIME BY &SYSUID------')
00004960      SEND 'PRB&PRB CLOSED - &SYSDATE &SYSTIME BY' +
00004970            USER(&ADMIN) LOGON NOWAIT
00004980      EX 'SYS.MIPCS.CLIST(IPCSLOG)' 'PRB(&PRB)'
00004990   ENDO
00005000   LP DESC DSN PRINT TERM PROB(&PRB) OWNER(ALL)
00005010END  /* IPCS */
