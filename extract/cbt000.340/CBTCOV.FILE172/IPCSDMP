00001800PROC 0 PRB() DSN() TIC() DPW() ADD() NEW() DIR() PSW() CAT() +
00001810    PREFIX(MIPCS) CATNAME(CATALOG.VLIB101) VOL(LIB100) PARM(00) +
00001820    DEBUG
00001830/**********************************************************************/
00001840/*  REFERENCE: SYS.MIPCS.CLIST(IPCSDMP)                               */
00001850/*                                                                    */
00001860/*  CLIST FOR IPCS DUMP ANALYSIS W/SPF MENU SUPPORT                   */
00001870/*  PARMS:                                                            */
00001880/*                                                                    */
00001890/*  PRB       - PROBLEM NUMBER                                        */
00001900/*  DSN       - DUMP DATA SET NAME (WITHOUT SURROUNDING QUOTES)       */
00001910/*  TIC       - FLAG TO INDICATE DUMP DSNAME IS FULLY QUALIFIED       */
00001920/*  DPW       - DUMP DATA SET PASSWORD                                */
00001930/*  ADD       - FLAG TO REQUEST A "ADDDSN" UNCONDITIONALLY            */
00001940/*  NEW       - FLAG TO REQUEST A "DROPDUMP" FIRST                    */
00001950/*  DIR       - DUMP DIRECTORY ID - MIDDLE LEVEL DSNAME QUALIFIER     */
00001960/*  PSW       - DUMP DIRECTORY PASSWORD                               */
00001970/*  CAT       - CATALOG PASSWORD (TO DEFINE A DUMP DIRECTORY)         */
00001980/*  DEBUG     - CAUSES PRINTING OF EACH CLIST COMMAND AS IT IS        */
00001990/*              EXECUTED AND AFTER SYMBOLIC SUBSTITUTION.             */
00002000/*  PREFIX    - (INTERNAL) HIGH LEVEL DUMP DIRECTORY INDEX            */
00002010/*  CATNAME   - (INTERNAL) VSAM CATALOG IN WHICH TO DEFINE DIRECTORY  */
00002020/*  VOL       - VOLUME WHERE DUMP DIRECTORY TO BE DEFINED             */
00002030/*                                                                    */
00002040/*  NOTE: THIS CLIST ASSUMES THE TERMINAL USED IS A 3278-4 WITH 43    */
00002050/*        LINES.  IF THE TERMINAL USED IS NOT OF THIS TYPE THE        */
00002060/*        SECOND TERM COMMAND.                                        */
00002070/*                                                                    */
00002080/**********************************************************************/
00002090CONTROL NOFLUSH NOMSG NOPROMPT END(ENDO)
00002100ALLOC DDN(IPCSPRNT) SYSOUT(A)
00002110IF &DEBUG EQ DEBUG THEN CONTROL CONLIST SYMLIST LIST MSG
00002120/*                                                                    */
00002130IF &TIC EQ Y THEN SET &TIC = &STR(')
00002140             ELSE SET &TIC =
00002150IF &DPW NE THEN SET &DPW = &STR(/&DPW)
00002160/* WRITE * TEST STUB FOR DUMP ANALYSIS CLIST - DISPLAY PARMS:         */
00002170/* WRITE PRB ==> &PRB                                                 */
00002180/* WRITE DSN ==> &TIC&DSN&TIC                                         */
00002190/* WRITE DPW ==> &DPW                                                 */
00002200/* WRITE ADD ==> &ADD                                                 */
00002210/* WRITE NEW ==> &NEW                                                 */
00002220/* WRITE DIR ==> &DIR                                                 */
00002230/* WRITE PSW ==> &PSW                                                 */
00002240/* WRITE CAT ==> &CAT                                                 */
00002250/*                                                                    */
00002260FREE F(IPCSDDIR,TAPE /* AMDPRDMP FILE */)
00002270ALLOC F(TAPE) DS(&TIC&DSN&TIC&DPW)      /* TEST CATLG'D AND AVAILABLE */
00002280IF &LASTCC NE 0 THEN         /* SORRY CAN'T ALLOC SO CAN'T EXAMINE */ +
00002290 DO /* IN CASE NOT IN DEBUG MODE, ISSUE MESSAGE */
00002300 WRITE &TIC&DSN&TIC NOT ALLOCATED, RC = &LASTCC
00002310 EXIT QUIT
00002320 ENDO
00002330/*  NOPROMPT MUST BE IN EFFECT FOR NEXT ALLOC TO WORK AS EXPECTED */
00002340IF &DIR EQ THEN /* ATTEMPT TO ALLOCATE AN OLD DUMP DIRECTORY */ +
00002350 ALLOC F(IPCSDDIR) DA('&PREFIX..&SYSUID..DUMPDIR') OLD
00002360  ELSE IF &DIR EQ SAVEDMP THEN /* DIFFERENT LOW LEVEL QUAL */ +
00002370        ALLOC F(IPCSDDIR) DS('&PREFIX..&DIR..DEBUG') OLD
00002380       ELSE ALLOC F(IPCSDDIR) DS('&PREFIX..&DIR..DUMPDIR') OLD
00002390IF &LASTCC NE 0 THEN /* ALLOC FAILED SO ATTEMPT DEFINE */ +
00002400DO
00002410 IF &PSW NE THEN SET &PSW = &STR(MRPW(&PSW) CTLPW(&PSW) UPDPW(&PSW) +
00002420                                 RDPW(&PSW))
00002430 IF &CAT NE THEN SET &CAT = &STR(/&CAT)
00002440 IF &DIR EQ THEN SET &DIR = &SYSUID /*  DEFAULT TO USERID FOR NAME */
00002450 ERROR DO
00002460       WRITE *** DEFINE FAILED FOR DUMP DIRECTORY:
00002470       WRITE *** '&PREFIX..&DIR..DUMPDIR'
00002480       WRITE *** POSSIBLY INVALID PASSWORD SUPPLIED
00002490       EXIT QUIT
00002500       ENDO
00002510 DEFINE CLUSTER (NAME('&PREFIX..&DIR..DUMPDIR') -
00002520                 UNIQUE &PSW -
00002530                 VOLUMES(&VOL) -
00002540                ) -
00002550          INDEX (NAME('&PREFIX..&DIR..DUMPDIRI') -
00002560                 TRACKS(1 1) &PSW -
00002570                ) -
00002580           DATA (NAME('&PREFIX..&DIR..DUMPDIRD') -
00002590                 CYLINDERS(1 1) &PSW -
00002600                 KEYS(128 0) -
00002610                 BUFFERSPACE(X'10000') -
00002620                 CONTROLINTERVALSIZE(X'1000') -
00002630                 ) -
00002640        CATALOG('&CATNAME.'&CAT)
00002650 ERROR
00002660 CONTROL MSG
00002670 ALLOC F(IPCSDDIR) DA('&PREFIX..&DIR..DUMPDIR') OLD
00002680 IF &LASTCC NE 0 THEN EXIT QUIT
00002690 IPCSDDIR '&PREFIX..&DIR..DUMPDIR'
00002700 IF &LASTCC NE 0 THEN EXIT QUIT
00002710ENDO /* END DEFINE ATTEMPT */
00002720CONTROL MSG
00002730TERM SCR(24,80)            /* CIRCUMVENT DSPL3270/3278-4 PROBLEM     */
00002740IPCS PARM(&PARM)           /* PRINT FILE SHOULD ALREADY BE ALLOCATED */
00002750TERM SCR(43,80)            /* RESET IN CASE ON A 3278-4              */
00002760SETDEF NOCONFIRM  FLAG(INFORMATIONAL) PRINT TERMINAL -
00002770       DISPLAY(MACHINE REMARK REQUEST STORAGE SYMBOL) -
00002780       PROBLEM(&PRB) -
00002790       DATASET(&TIC&DSN&TIC)
00002800IF &LASTCC NE 0 THEN EXIT        /* COULDN'T SET DEFAULTS */
00002810IF &ADD EQ Y THEN ADDDSN        /* UNCONDITIONALLY ADD THE DATA SET*/
00002820IF &NEW EQ Y OR &ADD EQ Y THEN /* RESET THE DUMP DIRECTORY */ +
00002830 DO
00002840 DROPDUMP
00002850 INITIALIZING THE DUMP
00002860 ENDO
00002870LIST TITLE
00002880WRITE ENTERING FULL-SCREEN MODE: DSPL3270
00002890CONTROL PROMPT MSG
00002900DSPL3270 PAUSE
00002910/* END IPCS - LEAVE IPCSDDIR ALLOCATED TO HAVE ONE FOR OTHER MENUS */
