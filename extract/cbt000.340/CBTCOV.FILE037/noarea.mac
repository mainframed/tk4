NO       TITLE 'NOAREA - PROGRAM TO TURN OFF DISPLAY AREA FOR ALL ACTIVX
               E MCS CONSOLES'
NOAREA   CSECT
***********************************************************************
* PROGRAMMER  -   HOWARD M. DEAN                                     **
*                 GTE DATA SERVICES                                  **
*                 4750 LINCOLN BLVD.                                 **
*                 MARINA DEL REY, CA 90291                           **
*                 PHONE -- 821-0511 X330                             **
*                                                                    **
* STATUS      -   MVS 03.8 SYSTEM PRODUCT REL 1.3.0 (JBB1326)        **
*                                                                    **
* FUNCTION    -   THIS PROGRAM WILL SCAN THE UCM CHAIN AND           **
*                 ISSUE A 'K A,NONE' FOR EVERY ACTIVE GRAPHIC        **
*                 CONSOLE IN THE SYSTEM.                             **
*                 'K S,DEL=R,RTME=001,CON=N' IS ALSO ISSUED.*LBD 11/8**
*                                                                    **
* OPERATION   -   S SETCONS (EXECUTES THE 'NOAREA' PROGRAM)          **
*                 OR EXECUTE AS A COMMAND UNDER TSSO.       *LBD 11/85*
*                                                                    **
* ATTRIBUTES  -   APF AUTHORIZED                                     **
*                 (ISSUES MODESET TO USE SVC 34)                     **
*                                                                    **
* CSECT NAME  -   NOAREA                                             **
*                                                                    **
* MACROS USED -   SETREG, WTO, IEECUCM, CVT                          **
*                 EOJ, LUSE, LDROP, MODESET                          **
*                                                                    **
* JCL REQUIRED TO EXECUTE THIS PROGRAM -- (SETCONS PROCEDURE)        **
*                                                                    **
*    //JOBNAME    JOB  (ACCTG),MSGLEVEL=1                            **
*    //STEP1      EXEC PGM=NOAREA                                    **
*    //STEPLIB    DD  DSN=AUTH.LIB.NAME (AUTHORIZED LIBRARY)         **
*                                                                    **
***********************************************************************
*                                                                    **
* CHANGE HISTORY:                                           *LBD 11/85*
*        03DEC85  ADDED K S,DEL=R SUPPORT                   *LBD 11/85*
*                 SET SO ALL COMMANDS APPEAR TO COME FROM   *LBD 11/85*
*                 THE MASTER CONSOLE.                       *LBD 11/85*
*              LIONEL DYCK, ROCKWELL INTERNATIONAL          *LBD 11/85*
*                                                                    **
***********************************************************************
         EJECT
         SETREG 12                SET REGISTERS
         EJECT
***********************************************************************
*                                                                     *
*          BUILD A TABLE OF UCM ADDRESSES (ONE PER CONSOLE)           *
*                                                                     *
***********************************************************************
BLDUCMS  DS    0H
         L     R4,CVTPTR          R4 = ADDR OF CVT
         LUSE  CVT,R4
         L     R4,CVTCUCB         R4 = ADDR OF 'CUCB' (UCM BASE)
         LDROP R4
         LUSE  UCM,R4
         LUSE  UCMLIST,R5
         LM    R5,R7,UCMVEA       R5 = ADDR OF FIRST UCM ENTRY
*                                 R6 = LENGTH OF EACH UCM ENTRY
*                                 R7 = ADDR OF LAST UCM ENTRY
*
         LA    R8,UCMTAB+4        R8 = ADDR OF UCMTAB
         LA    R9,UCMTABE         R9 = ADDR OF END OF UCMTAB
         XR    R10,R10            R10 = 0 (NUMBER OF VALID UCMS)
UCMLOOP  ST    R5,0(R8)           SAVE UCM ADDRESS IN UCMTAB
         TM    UCMDISP1,UCMDISPA   IS THIS THE MASTER       *LBD 11/85*
         BZ    NOTMSTR             NO                       *LBD 11/85*
         MVC   MSTUCMID,UCMID      YES - SAVE THE ID        *LBD 11/85*
NOTMSTR  DS    0H                                           *LBD 11/85*
         LA    R10,1(R10)         R10 = R10 + 1  (ONE MORE UCM)
         LA    R8,4(R8)           R8 = ADDR OF NEXT UCMTAB ENTRY
         CR    R8,R9              DOES R8 POINT PAST END OF UCMTAB?
         BNL   UCMDONE            YES; LEAVE LOOP
         AR    R5,R6              R5 = ADDR OF NEXT UCM ENTRY
         CR    R5,R7              DOES R5 POINT PAST UCM ENTRIES?
         BL    UCMLOOP            NOPE; KEEP GOING
UCMDONE  STH   R10,NUMUCMS        SAVE NUMBER OF UCMS FOUND
         LDROP R4
         INUSE ,
         EJECT
***********************************************************************
*                                                                     *
*          LOOP THROUGH UCM TABLE AND ISSUE K A,NONE FOR EACH         *
*          ACTIVE CRT CONSOLE                                         *
*                                                                     *
***********************************************************************
UCMSTRT  XR    R4,R4              CLEAR CONSOLE NUMBER (INIT)
KALOOP   LA    R4,1(,R4)          INCREMENT CONSOLE NUMBER
         LA    R5,UCMTAB          R5 = ADDR OF UCMTAB
         CH    R4,NUMUCMS         IS NUMBER TOO HIGH?
         BH    ENDJOB             YES, END OF JOB
         ST    R4,CONSOLE         SAVE CONSOLE ID
         SLL   R4,2               MULTIPLY BY 4
         LA    R5,0(R5,R4)        R5 = ADDR OF ADDR OF UCM
         L     R5,0(R5)           R5 = ADDR OF UCM
         L     R4,CONSOLE         R4 = ACTIVE CONSOLE NUMBER
         INUSE ,
         L     R6,UCMXB           R6 = ADDR OF RDCM
         LTR   R6,R6              IS THIS A GRAPHICS CONSOLE?
         BNP   KALOOP             NO, KEEP SEARCHING
         TM    UCMATR,UCMUF       DEVICE ACTIVE?
         BZ    KALOOP             NO, DO NOT SET SCREEN
         TM    UCMATR,UCMAT04     CHANGING STATUS?
         BO    KALOOP             YES, NO SCREEN CHANGE NOW
         EJECT
***********************************************************************
*                                                                     *
*          ISSUE A   'K A,NONE'  COMMAND HERE                         *
*                                                                     *
***********************************************************************
COMMAND  DS    0H
         CVD   R4,DWORD          CONVERT CONSOLE ID
         UNPK  UNPKR(3),DWORD+6(2)       UNPACK VALUE
         OI    UNPKR+2,X'F0'     FIX SIGN
         MVC   CONID,UNPKR+1     CONSOLE ID TO SVC 34 PARMS
         MVC   CONID1,CONID      INSERT IN MGCR MESSAGE
         MVC   WCONID,CONID      INSERT IN WTO MESSAGE
         MVC   WCONID1,CONID     INSERT IN WTO MESSAGE
*
         MODESET KEY=ZERO        GET SET TO ISSUE COMMAND
*
         WTO   MF=(E,WTOLIST)    ISSUE WTO
         WTO   MF=(E,WTOLIST1)   ISSUE WTO                  *LBD 11/85*
*
         SR    R0,R0               CLEAR UCMID              *LBD 11/85*
         IC    R0,MSTUCMID         SET MASTER UCMID         *LBD 11/85*
         LA    R1,CMDLIST          -> K A,NONE COMMAND      *LBD 11/85*
         SVC   34                  ISSUE SVC 34             *LBD 11/85*
         SR    R0,R0               CLEAR UCMID              *LBD 11/85*
         IC    R0,MSTUCMID         SET MASTER UCMID         *LBD 11/85*
         LA    R1,CMDLIST1         -> K S,DEL=R COMMAND     *LBD 11/85*
         SVC   34                                           *LBD 11/85*
*
         MODESET KEY=NZERO       NO NEED FOR THIS ANYMORE
*
         B     KALOOP            GET NEXT CONSOLE
ENDJOB   EOJ   RC=0              RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*          PROGRAM CONSTANTS                                          *
*                                                                     *
***********************************************************************
UCMTAB   DS    99F                    HOLDS 99 CONSOLES
UCMTABE  EQU   *
NUMUCMS  DS    F                      NUMBER OF CONSOLES
CONSOLE  DS    F                      CURRENT CONSOLE NUMBER
TIMER    DC    F'200'                 TIME FOR 2 SECONDS
MSTUCMID DC    X'00'                  MASTER CONSOLE UCMID  *LBD 11/85*
DWORD    DS    D                      TO UNPACK CONSOLE NUMBER
UNPKR    DS    CL3                    TARGET OF UNPK INSTRUCTION
*
CMDLIST  DS    0F                     MGCR PARAMETER LIST
TEXTL    DC    AL2(MSGTXTL)
MCSFLGS  DC    X'0000'
MSGTXT   DC    C'K A,NONE,L='
CONID    DC    C'XX'
MSGTXTL  EQU   *-TEXTL
*
*                                                           *LBD 11/85*
CMDLIST1 DS    0F                     MGCR PARAMETER LIST   *LBD 11/85*
TEXTL1   DC    AL2(MSGTXTL1)                                *LBD 11/85*
MCSFLGS1 DC    X'0000'                                      *LBD 11/85*
MSGTXT1  DC    C'K S,DEL=R,L='                              *LBD 11/85*
CONID1   DC    C'XX'                                        *LBD 11/85*
MSGTXTL1 EQU   *-TEXTL1                                     *LBD 11/85*
*                                                           *LBD 11/85*
WTOLIST  DS    0F                     WTO PARAMETER LIST
MSGLEN   DC    AL2(WTOLEN)
         DC    X'8000'
MSGID    DC    C'SETCONKA '                                 *LBD 11/85*
MSGWTO   DC    C'K A,NONE,L=XX'
WCONID   EQU   *-2,2,X'C3'            MESSAGE ID EQUATE     *HMD 04/81*
WTOLEN   EQU   *-MSGLEN
DESCCODE DC    B'0001010000000000'
ROUTCODE DC    B'1000000000100000'
*                                                           *LBD 11/85*
WTOLIST1 DS    0F                     WTO PARAMETER LIST    *LBD 11/85*
MSGLEN1  DC    AL2(WTOLEN1)                                 *LBD 11/85*
         DC    X'8000'                                      *LBD 11/85*
MSGID1   DC    C'SETCONKS '                                 *LBD 11/85*
MSGWTO1  DC    C'K S,DEL=R,RTME=001,CON=N,L=XX'             *LBD 11/85*
WCONID1  EQU   *-2,2,X'C3'            MESSAGE ID EQUATE     *HMD 04/81*
WTOLEN1  EQU   *-MSGLEN1                                    *LBD 11/85*
DESCCOD1 DC    B'0001010000000000'                          *LBD 11/85*
ROUTCOD1 DC    B'1000000000100000'                          *LBD 11/85*
         EJECT
***********************************************************************
*                                                                     *
*        MAPPING DSECTS                                               *
*                                                                     *
***********************************************************************
         IEECUCM FORMAT=NEW,LIST=YES
         EJECT
         CVT    LIST=YES,DSECT=YES
         END   NOAREA
