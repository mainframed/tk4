         TITLE 'NODENAME CLIST VARIABLE SET PROGRAM'
*---------------------------------------------------------------------*
*        TSO PROGRAM/COMMAND TO SET CLIST VARIABLE NODENAME TO THE    *
*        NAME OF THE CURRENT NJE NODE NAME BASE UPON THE VALUE FOUND  *
*        IN THE CVTSNAME FIELD.                                       *
*                                                                     *
*        THE CVTSNAME FIELD IS ASSUMED TO BE OF THE FORMAT            *
*        X#NODE                                                       *
*        WHERE X = THE CPU ID (SINGLE CHARACTER)                      *
*        # IS A DELIMETER IN CASE  THE CPU ID EXCEEDS A SINGLE CHAR   *
*             IN THE FUTURE. (THIS  CODE DOES NOT SUPPORT THIS YET)   *
*        NODE = THE NODE NAME (4 CHARACTERS)                          *
*---------------------------------------------------------------------*
         EJECT
TSONODE  $PROLOG
         L     R1,CVTPTR        -> CVT
         LA    R1,CVTSNAME-CVTMAP(,R1)
         MVC   VALUE(4),2(R1)   MOVE NODE NAME TO VALUE
         SPACE 1
         L     R15,CVTPTR       -> CVT
         L     R15,CVTTVT-CVTMAP(,R15) ACCESS THE TSVT
         L     R15,TSVTVACC-TSVT(,R15)
*
         CALL  (15),(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,TOKEN),VL
         LTR   R15,R15
         BNZ   EXIT8
         $EPILOG 0
EXIT8    $EPILOG 8
         LTORG
ECODE    DC    A(TSVEUPDT)      ENTRY CODE
VALUE    DC    CL4' '
VALUEPTR DC    A(VALUE)
VALUELEN DC    F'4'
NAME     DC    CL8'NODENAME'
NAMEPTR  DC    A(NAME)
NAMELEN  DC    F'8'
TOKEN    DC    F'0'
         EJECT
         IKJTSVT
         EJECT
         CVT   DSECT=YES,LIST=YES
         END
