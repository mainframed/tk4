TAPEMARK TITLE 'TAPEMARK - TAPEMARK COMMAND ROUTINE'
*        FUNCTION
*
*              THIS ROUTINE WILL WRITE 2 TAPEMARKS ON ANY TAPE
*              DRIVE THAT IS NOT ALLOCATED WHEN SPECIFIED BY
*              OPERATOR COMMAND.  IF THE DEVICE IS ALLOCATED FORCE
*              MAY BE SPECIFIED TO IGNORE THE DEVICE BEING ALLOCATED.
*              IF THE TAPE IS AT LOAD POINT AND THE DEVICE IS DUAL
*              DENSITY LOW OR HIGH MAY BE SPECIFIED TO GET THE
*              DESIRED DENSITY (IF MISSING HIGH IS ASSUMED FOR 9TR).
*              THE DENSITY MUST BE SPECIFIED FOR 7 TRACK TAPES.
*              ANY CURRENT STIMER WILL BE SAVED THEN RESTORED.
*              A 1 SECOND STIMER WILL BE USED DURING WRITING TAPEMARKS.
*              IF TIME EXPIRES THE TAPEMARK I/O WILL BE PURGED.
*              ALL I/O BLOCKS WILL BE BUILT IN LSQA.  EXCP IS USED
*              WITH ERROR RECOVERY DISABLED.  THE SENSE DATA WILL BE
*              DISPLAYED IF ERROR IS OTHER THAN NOT READY OR FILE
*              PROTECT.  THE DENSITY WILL BE DISPLAYED IF A 9 TRACK
*              TAPE IS AT LOAD POINT OR THE TAPE IS 7 TRACK.
*
*
*        INPUT - OPERATOR COMMAND 'TAPEMARK'
*
*        COMMAND PARMS - (ANY ORDER AFTER 1ST PARM)
*
*              CUU    - 3 BYTE EBCDIC DEVICE ADDRESS.   (REQUIRED 1ST)
*              200    - TAPE DENSITY.                   (REQD 7 TRACK)
*              556    - TAPE DENSITY.                   (REQD 7 TRACK)
*              800    - TAPE DENSITY.                   (REQD 7 TRACK)
*              LOW    - TAPE DENSITY.  (200 IF 7TR)     (OPTIONAL 9TR)
*              HIGH   - TAPE DENSITY.  (800 IF 7TR)     (DEFAULT) (OPT)
*              FORCE  - IGNORE ALLOCATION CHECK.        (OPTIONAL)
*              REWIND - REWIND AFTER TAPEMARKS.         (OPTIONAL)
*              UNLOAD - REWIND/UNLOAD AFTER TAPEMARKS.  (OPTIONAL)
*
*
*        OUTPUT -
*
*              TAPE - 2 TAPEMARKS WILL BE WRITTEN AND TAPE WILL
*                     BE POSITIIONED BETWEEN THEM UNLESS REWIND OR
*                     UNLOAD IS SPECIFIED.
*
*              CONSOLE - OPERATOR WILL BE INFORMED OF ACTION.
*                        DENSITY WILL BE DIAPLAYED FOR ALL 7 TRACK
*                        TAPES AND 9 TRACK TAPES AT LOAD POINT.
*                        SENSE DATA IS DISPLAYED FOR I/O ERRORS.
*
*
*        DEVICE CONDITION PREVENTING ACTION
*
*              1.  THE DEVICE SPECIFIED IS NOT A TAPE.
*              2.  TAPE ALLOCATED (UNLESS FORCE SPECIFIED).
*              3.  CURRENT I/O REQUEST NOT POSTED.
*              4.  SENSE COMMAND PENDING.
*              5.  SENSE COMMAND ACTIVE.
*              6.  DENSITY NOT SPECIFIED FOR A 7 TRACK TAPE.
*
*
*        ATTRIBUTES
*              APF AUTHORIZED
*
*        ENVIRONMENT
*
*              TSO CP
*              MVS/370 OR MVS/XA
*              I/O ERROR RECOVERY DISABLED
*
         EJECT
TAPEMARK MENTER 12,EQU,COM=TAPEMARK,CP=YES
*
*        INITIALIZE WORK CORE
*
         MVI   CNTLFLAG,0          ZERO CONTROL FLAG
         MVI   DENFLAG,0           ZERO DENSITY CONTROL FLAG
         L     R3,CPPLCBUF         COMMAND ENTRY BUFFER
         LH    R8,2(R3)            OFFSET TO PARMS
         LH    R1,0(R3)            LOAD LEN                      12/85*
         LA    R3,4(R3)            PAST HEADER
         AR    R3,R8               ADD OFFSET
         CLI   0(R3),X'40'         ANY PARM ?
         BNH   NOPARM              NOPE, --->
         SR    R1,R8               LESS OFFSET                   12/85*
         SH    R1,=H'03'           LESS HEADER                   12/85*
         CH    R1,=H'80'                                         12/85*
         BNH   MOVEIT                                            12/85*
         LA    R1,80               SET TO 80 MAX                 12/85*
MOVEIT   DS    0H                                                12/85*
         EX    R1,MVCPARM          AND MOVE IT                   12/85*
         OC    OURPARM,BLANKS      UPPER CASE IT                 12/85*
         LA    R3,OURPARM          SET ADDR OF OUR PARMS         12/85*
         TIME  DEC                 GET TIME OF DAY
         ST    R0,DWK              SAVE TIME OF DAY FOR EDIT
         ED    TOD,DWK             EDIT TIME OF DAY INTO MSG
*
*        VERIFY INPUT PARMS
*
SAVEDEV  MVC   TAPEADDR,0(R3)      SAVE TAPE DEVICE ADDR
         LA    R3,3(R3)            BUMP OVER DEV ADDR
         LA    R4,77               SET LOOP COUNTER           12/85
PARMLOOP CLI   0(R3),BLANK         IS THIS END OF PARMS
         BNH   PARMCMPL            IF GET OUT OF LOOP
         CLI   0(R3),COMMA         IS THIS A COMMA
         BE    NEXTPARM            IF YES SKIP IT
         CLC   FORCE,0(R3)         IS FORCE SPECIFIED
         BE    SETFORCE            IF YES GO SET FLAG
         CLC   REWIND,0(R3)        IS REWIND SPECIFIED
         BE    SETREW              IF YES GO SET FLAG
         CLC   UNLOAD,0(R3)        IS UNLOAD SPECIFIED
         BE    SETRUN              IF YES GO SET FLAG
         CLC   LOW,0(R3)           IS LOW DENSITY SPECIFIED
         BE    SETLOW              IF YES GO SET FLAG
         CLC   HIGH,0(R3)          IS HIGH DENSITY SPECIFIED
         BE    SETHIGH             IF YES GO SET FLAG
         CLC   DEBUG,0(R3)         IS DEBUG SPECIFIED
         BE    SETDUMP             IF YES GO SET FLAG
         CLC   D200,0(R3)          IS 7TR DENSITY SPECIFIED
         BE    SET200              IF YES GO SET FLAG
         CLC   D556,0(R3)          IS 7TR DENSITY SPECIFIED
         BE    SET556              IF YES GO SET FLAG
         CLC   D800,0(R3)          IS 7TR DENSITY SPECIFIED
         BE    SET800              IF YES GO SET FLAG
         B     NEXTPARM            TERMINATE IF INVALID PARM
SET200   MVI   DENFLAG,DEN200      SET 7TR DENSITY FLAG
         B     NEXTPARM            GO GET NEXT PARM
SET556   MVI   DENFLAG,DEN556      SET 7TR DENSITY FLAG
         B     NEXTPARM            GO GET NEXT PARM
SET800   MVI   DENFLAG,DEN800      SET 7TR DENSITY FLAG
         B     NEXTPARM            GO GET NEXT PARM
SETLOW   MVI   DENFLAG,DENLOW+DEN200  SET LOW DENSITY FLAGS
         B     NEXTPARM            GO GET NEXT PARM
SETHIGH  MVI   DENFLAG,DEN800      CLEAR 9TR LOW DENSITY & SET 7TR DEN
         B     NEXTPARM            GO GET NEXT PARM
SETFORCE OI    CNTLFLAG,FORCEFL    SET FORCE FLAG
         B     NEXTPARM            GO GET NEXT PARM
SETREW   OI    CNTLFLAG,REWFLAG    SET REWIND TAPE FLAG
         B     NEXTPARM            GO GET NEXT PARM
SETRUN   OI    CNTLFLAG,RUNFLAG    SET REWIND/UNLOAD TAPE FLAG
         B     NEXTPARM            GO GET NEXT PARM
SETDUMP  OI    CNTLFLAG,DUMPFLAG   SET DUMP FLAG
NEXTPARM DS    0H
         LA    R3,1(R3)            BUMP THE INDEX             01/86
         BCT   R4,PARMLOOP         AND LOOP THRU ALL PARMS    12/85
PARMCMPL EQU   *
*
*        LOOP THRU UCB ADDRESS LIST UNTIL UCB IS FOUND
*
         USING CVT,R3              TELL ASSEMBLER
         USING IHADCB,R4           TELL ASSEMBLER
         USING DEBBASIC,R5         TELL ASSEMBLER
         L     R3,CVTPTR           LOAD CVT BASE REG
UCBLOOP  DS    0H
         L     R15,CVTUCBSC                                      12/85*
         CALL  (15),(SCANAREA,DEVCLASS,UCBADDR)
         LTR   R15,R15             END OF LIST?
         BNZ   DEVERR              YEP, ---->
         L     R7,UCBADDR          GET UCB ADDR
         USING UCBOB,R7            TELL THE ASSEMBLER
         CLC   TAPEADDR,UCBNAME    COMPARE TO THE INPUT
         BNE   UCBLOOP             NOT THE SAME, ----->
*
*        CHECK DEVICE TYPE & AVAILABILITY
*
GOTUCB   DS    0H
         CLI   UCBTBYT3,UCB3TAPE   IS DEVICE A TAPE
         BNE   DEVERR              IF NOT PARM IS INVALID
*        TM    UCBFLA,UCBPST       IS AN IOQ/IOSB ACTIVE    (MVS/370)
*        BO    DEVBSY              IF YES ISSUE ERROR MSG   (MVS/370)
         TM    UCBFLA,X'20'        IS THE SUBCHANNEL USABLE (MVS/XA)
         BO    DEVBSY              IF NO  ISSUE ERROR MSG   (MVS/XA)
         TM    UCBFLA,UCBPSNS      IS SENSE REQUEST PENDING
         BO    DEVSNS              IF YES ISSUE ERROR MSG
*        TM    UCBFLB,UCBASNS      IS SENSE REQUEST ACTIVE  (MVS/370)
*        BO    DEVSNS              IF YES ISSUE ERROR MSG   (MVS/370)
         TM    UCBFLB,X'40'        IS A PATH AVAILABLE      (MVS/XA)
         BO    DEVSNS              IF NO  ISSUE ERROR MSG   (MVS/XA)
         TM    CNTLFLAG,FORCEFL    IS FORCE SPECIFIED
         BO    INITIO              IF YES SKIP ALLOCATION CHECK
         TM    UCBSTAT,UCBALOC     IS UCB ALLOCATED
         BO    ALOCERR             IF YES TAPE IS NOT USABLE
*
*        INITIALIZE I/O CONTROL BLOCKS
*
INITIO   DS    0H
         L     R3,CVTPTR           GET CVT ADDRESS            12/85
         LA    R4,DCB              GET DCB ADDR               12/85
         LA    R5,DEBBLOCK         GET DEB ADDR               12/85
         OI    IOBFLAG1,IOBCMDCH   SET COMMAND CHAIN
         OI    IOBFLAG1,IOBUNREL   SET UNRELATED FLAG         12/85
         LA    R1,ECB              LOAD ECB ADDRESS
         ST    R1,IOBECBPT         INITIALIZE ECB ADDR
         LA    R1,CCW1             LOAD SENSE CCW ADDRESS
         ST    R1,IOBSTART         INITIALIZE STARTING CCW ADDRESS
         LA    R1,DCB              LOAD DCB ADDRESS
         ST    R1,IOBDCBPT         INITIALIZE DCB ADDRESS
         ST    R1,DEBDCBAD         INITIALIZE DCB ADDRESS
         L     R1,CVTTCBP          LOAD TCB POINTERS ADDR
         L     R1,4(R1)            LOAD CURRENT TCB ADDR
         ST    R1,DEBTCBAD         INITIALIZE TCB ADDRESS
         LA    R1,DEBBLOCK         LOAD DEB ADDRESS
         ST    R1,DCBDEBAD         INITIALIZE DEB ADDRESS
         ST    R1,PRGDEBAD         INITIALIZE DEB ADDRESS
         ST    R7,DEBSUCBA         INITIALIZE UCB ADDRESS
         LA    R1,IOBSTDRD         LOAD IOB ADDRESS
         ST    R1,PRGIOBAD         INITIALIZE IOB ADDRESS
         LA    R1,APPENDVT         LOAD APPENDAGE VECTOR TABLE
         ST    R1,DEBAPPAD         SAVE IT IN THE DEB
         MVI   DCBIFLGS,DCBIFEC    DISABLE IOS ERROR RECOVERY
         MVI   DEBDEBID,X'0F'      INITIALIZE DEB ID
         MVI   DEBNMEXT,1          SET NUMBER OF EXTENTS FOR PURGE
         MVI   DEBEXSCL,2          INITIALIZE EXTENT SCALE SIZE
         MVI   PURGELST,PURGEOPN   INITIALIZE PURGE OPTIONS BYTE
         LA    R1,SENSE            LOAD SENSE BYTES ADDRESS
         STCM  R1,7,CCW1+1         INITIALIZE SENSE CCW ADDRESS
         LA    R1,SENSE            LOAD SENSE BYTES ADDRESS
         STCM  R1,7,CCW1+1         INITIALIZE SENSE CCW ADDRESS
CHKREW   TM    CNTLFLAG,REWFLAG    IS REWIND SPECIFIED
         BZ    CHKRUN              IF NOT DON'T CHANGE CCW
         MVI   CCW4,REW            CHANGE CCW FROM BSF TO REWIND
CHKRUN   TM    CNTLFLAG,RUNFLAG    IS REWIND SPECIFIED
         BZ    INITMODE            IF NOT DON'T CHANGE CCW
         MVI   CCW4,RUN            CHANGE CCW TO REWIND/UNLOAD
         MVI   CCW4+4,SLI          KILL COMMAND CHAIN
*
*        INITIALIZE PROPER MODE SET VALUE
*
INITMODE TM    UCBTBYT2,UCB2OPT0   IS TAPE 7 TRACK
         BO    INIT7TR             IF YES GO SET 7TR MODE
INIT9TR  TM    UCBTBYT2,UCBDUDN1+UCBDUDN2  IS DEVICE DUAL DEN
         BZ    SINGDEN             IF NOT SKIP OPTION CHECK
         TM    UCBTBYT2,UCBDUDN1   IS DEVICE 800/1600
         BO    CHKDENOP            GO CHECK DENSITY REQUEST
         TM    DENFLAG,DENLOW      IS LOW DEVICE SPECIFIED
         BO    SETM1600            IF YES GO SET LOW DENSITY
         B     SETM6250            IF NOT GO SET HIGH DENSITY
CHKDENOP TM    DENFLAG,DENLOW      IS LOW DEVICE SPECIFIED
         BO    SETM800             IF YES GO SET LOW DENSITY
         B     SETM1600            IF NOT GO SET HIGH DENSITY
SINGDEN  TM    UCBTBYT1,UCBD1600   IS DEVICE DENSITY 1600
         BO    SETM1600            IF YES GO SET DENSITY
         TM    UCBTBYT1,UCBD6250   IS DEVICE DENSITY 6250
         BO    SETM6250            IF YES GO SET DENSITY
SETM800  MVI   DEBSDVM,X'CB'       INITIALIZE MODE SET FOR 800 BPI
         B     SETTIMER            GO SAVE PREVIOUS STIMER
SETM1600 MVI   DEBSDVM,X'C3'       INITIALIZE MODE SET FOR 1600 BPI
         B     SETTIMER            GO SAVE PREVIOUS STIMER
SETM6250 MVI   DEBSDVM,X'D3'       INITIALIZE MODE SET FOR 6250 BPI
         B     SETTIMER            GO SAVE PREVIOUS STIMER
INIT7TR  TM    DENFLAG,DEN200+DEN556+DEN800   IS 7TR DEN SPECIFIED
         BZ    NODENERR            IF NONE ISSUE ERROR MSG
         CLI   DENFLAG,DEN556      IS DEN 556 SPECIFIED
         BE    SETM7556            GO SET 556 DEN MODE
         BL    SETM7800            GO SET 800 DEN MODE
SETM7200 MVI   DEBSDVM,X'33'       SET 7TR 200 DEN MODE
         B     SETTIMER            GO SAVE PREVIOUS STIMER
SETM7556 MVI   DEBSDVM,X'73'       SET 7TR 556 DEN MODE
         B     SETTIMER            GO SAVE PREVIOUS STIMER
SETM7800 MVI   DEBSDVM,X'B3'       SET 7TR 800 DEN MODE
         EJECT
*
*        WRITE TAPE MARK
*
SETTIMER EQU   *
         STIMER  REAL,EXITCONS,BINTVL=IOTIME   SET TIMER
         MODESET KEY=ZERO,MODE=SUP                               12/85*
         MVI   UCBFL1,0            CLEAR ANY ERROR
         EXCP  IOBSTDRD            READ SENSE BYTES
         MODESET KEY=NZERO,MODE=PROB                             12/85*
         WAIT  ECB=ECB             WAIT FOR I/O COMPLETION
         TTIMER  CANCEL            KILL OUR TIMER
*
*        CHECK I/O COMPLETION
*
CHKIO    CLI   ECB,X'7F'           WAS I/O SUCCESSFUL
         BE    IOCOMP              IF YES GO ISSUE MSG
         CLI   ECB,X'48'           DID A TIMEOUT OCCUR
         BE    NOTREADY            IF YES INFORM OPERATOR
*
*        INFORM OPERATOR STATUS & TERMINATE
*
IOERR    TM    IOBSIOCC,X'30'      WAS SIO CC=3
         BO    NOPATHAV            IF YES GO ISSUE MSG
         TM    IOBSENS0,IOBS0B1    IS ERROR NOT READY
         BO    NOTREADY            IF YES GO ISSUE MSG
         TM    IOBSENS0,IOBS0B6    IS ERROR FILE PROTECT
         BO    NORING              IF YES GO ISSUE MSG
         MVC   MSGTXT,IOERTXT      MOVE TEXT TO MESSAGE
         OI    CNTLFLAG,SNSFLAG    SET FLAG TO DISPLAY SENSE DATA
         B     WTO                 GO ISSUE OPER MSG
NORING   MVC   MSGTXT,PROTTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
ADDRERR  MVC   MSGTXT,BADATXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
DEVERR   MVC   MSGTXT,BADDTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
DEVBSY   MVC   MSGTXT,BUSYTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
DEVSNS   MVC   MSGTXT,SENSTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
ALOCERR  MVC   MSGTXT,ALOCTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
NOPATHAV MVC   MSGTXT,NOPHTXT      MOVE TEXT TO MESSAGE
         B     WTO                 GO ISSUE OPER MSG
NOTREADY MVC   MSGTXT,NOTRTXT      MOVE TEXT TO MESSAGE
         TM    CNTLFLAG,DUMPFLAG   IS DEBUG SPECIFIED
         BZ    WTO                 IF NOT DON'T DISPLAY SENSE BYTES
         OI    CNTLFLAG,SNSFLAG    SET FLAG TO DISPLAY SENSE DATA
         B     WTO                 GO ISSUE OPER MSG
IOCOMP   MVC   MSGTXT,COMPTXT      MOVE TEXT TO MESSAGE
         OI    CNTLFLAG,IOOKFLAG   SET GOOD IO FLAG
*
*        DISPLAY ERROR MSG OR TAPEMARK SUCCESSFUL MSG
*
NOPARM   EQU   *
WTO      DS    0H
         PUTLINE PARM=PUTBLOK,                                         +
               OUTPUT=(MSGHDR,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         TM    CNTLFLAG,SNSFLAG    IS SENSE MSG REQUIRED
         BO    FMTSNS              IF YES GO FORMAT SENSE DATA
         TM    CNTLFLAG,IOOKFLAG   WERE TAPEMARKS WRITTEN OK
         BNO   CHKDUMP             IF NOT SKIP DENSITY MSG
*
*        DISPLAY DENSITY IF TAPE IS AT LOAD POINT
*
FMTDEN   MVC   MSGTXT,DENTXT       LOAD DENSITY MSG TEXT
FMT800   MVC   TOD,DTXT800         LOAD DENSITY CONSTANT
         TM    UCBTBYT2,UCB2OPT0   IS TAPE 7 TRACK ?
         BO    FMT7DEN             IF YES GO BUILD MSG
         TM    SENSE+1,LOADPT      WAS TAPE AT LOAD POINT ?
         BNO   CHKDUMP             IF NOT SKIP IT
FMT9DEN  CLI   DEBSDVM,X'CB'       CHECK DENSITY
         BL    FMT1600             GO SET DENSITY
         BE    DENWTO              GO DISPLAY DENSITY MSG
FMT6250 MVC    TOD(4),DTXT6250     CHANGE DENSITY TO 6250
         B     DENWTO              GO DISPLAY DENSITY MSG
FMT1600 MVC    TOD(4),DTXT1600     CHANGE DENSITY TO 6250
         B     DENWTO              GO DISPLAY DENSITY MSG
FMT7DEN  CLI   DEBSDVM,X'73'       CHECK DENSITY
         BE    FMT556              GO SET DENSITY
         BH    DENWTO              GO DISPLAY 800 DENSITY
FMT200   MVI   TOD,C'2'            CHANGE DENSITY TO 200
         B     DENWTO              GO DISPLAY DENSITY MSG
FMT556   MVC   TOD(3),DTXT556      CHANGE DENSITY TO 556
DENWTO   DS    0H
         PUTLINE PARM=PUTBLOK,                                         +
               OUTPUT=(MSGHDR,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     CHKDUMP             GO CHECK DEBUG DUMP
*
*        DISPLAY I/O ERROR SENSE DATA
*
FMTSNS   MVC   SNSDHEAD,SNSDCONS   INITIALIZE MSG CONSTANT
         UNPK  SNSWORD1(9),SENSE+00(5) CONVERT 4 SENSE BYTES
         UNPK  SNSWORD2(9),SENSE+04(5) CONVERT 4 SENSE BYTES
         UNPK  SNSWORD3(9),SENSE+08(5) CONVERT 4 SENSE BYTES
         UNPK  SNSWORD4(9),SENSE+12(5) CONVERT 4 SENSE BYTES
         UNPK  SNSWORD5(9),SENSE+16(5) CONVERT 4 SENSE BYTES
         UNPK  SNSWORD6(9),SENSE+20(5) CONVERT 4 SENSE BYTES
         TR    SNSWORD1(50),HEXTABLE   CONVERT ALPHA
         L     R15,UCBEXTPT        LOAD UCB EXTENSION BASE
         USING UCBCMEXT,R15
         SR    R1,R1               ZERO SENSE LENGTH REG
         IC    R1,UCBSNSCT         LOAD NUMBER OF SENSE BYTES
         AR    R1,R1               DOUBLE VALUE
         LA    R1,SNSWORD1-MSGHDR(R1)  GET TOTAL MSG LENGTH
         CLI   UCBSNSCT,8          IS A DELIMITER PRESENT
         BNH   SNSWTO              IF NOT MSG IS COMPLETE
         LA    R1,1(R1)            INCLUDE DELIMITER
         MVI   SNSBL1,COMMA        INSERT DELIMITER
         CLI   UCBSNSCT,16         IS A DELIMITER PRESENT
         BNH   SNSWTO              IF NOT MSG IS COMPLETE
         LA    R1,1(R1)            INCLUDE DELIMITER
         MVI   SNSBL2,COMMA        INSERT DELIMITER
SNSWTO   DS    0H
         STH   R1,MSGHDR           SET SENSE DATA MSG LENGTH
         PUTLINE PARM=PUTBLOK,                                         +
               OUTPUT=(MSGHDR,TERM,SINGLE,DATA),MF=(E,IOPLADS)
CHKDUMP  B     FREEWORK            * IGNORE THIS CODE FOR NOW     *
         EJECT
FREEWORK DS    0H                  EXIT
         MLEAVE                    RETURN TO SYSTEM
MVCPARM  MVC   OURPARM(0),0(R3)    *** EXECUTED                  12/85*
*
*        DISPLAY INVALID PARM MSG
*
BADPARM  DS    0H
         PUTLINE PARM=PUTBLOK,                                         +
               OUTPUT=(MSG1,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     FREEWORK            GO FREE WORKAREA & RETURN
*
*        DISPLAY MISSING DENSITY PARM MSG
*
NODENERR DS    0H
         PUTLINE PARM=PUTBLOK,                                         +
               OUTPUT=(MSG2,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     FREEWORK            GO FREE WORKAREA & RETURN
*
*        CONSTANTS FOR WORKAREA (THEY MUST REMAIN IN SAME SEQUENCE)
*        STANDARD MSG CONSTANT
*
BLANKS   DC    CL80' '                                           12/85*
         EJECT
*
*        STIMER EXIT ROUTINE
*
         USING EXITCONS,R15
EXITCONS SAVE  (14,1)              SAVE REGS
         LA    R1,PURGELST         LOAD PURGE PARM ADDRESS
         SVC   PURGE               PURGE I/O
         BR    14                  RESTORE REGS
         DROP  R15
         EJECT
*
*        DUMMY I/O APPENAGE
*
DUMYAPPD BR    14
*
*        PURGE PARM LIST ( NOTE PARM LIST MUST FOLLOW EXIT ROUTINE )
*
PURGELST DS    0F
PRGDEBAD DC    A(0)                ADDRESS OF DEB PURGED
PRGTCBAD DC    A(0)                TCB ADDRESS
PRGIOBAD DC    A(0)                IOB ADDRESS
*
*        COM TASK STIMER SAVE AREA
*
EXITSAVE DS    A                   TIMER EXIT ROUTINE ADDRESS
TIMESAVE DS    F                   REMAINING TIME INTERVAL
*
*        I/O CONTROL BLOCKS
*
         PRINT   NOGEN
ECB      DC    F'0'                EVENT CONTROL BLOCK
DEBBLOCK DC    XL(40)'00'
         IEZIOB DSECT=NO                                      12/85
DCB      DCB   DSORG=PS,MACRF=E                               12/85
*
*        I/O CONTROL BLOCKS
*
CCW1     CCW   SEN,SENSE,CC+SLI,24 READ SENSE BYTES
CCW2     CCW   WTM,0,CC+SLI,1      WRITE TAPE MARK
CCW3     CCW   WTM,0,CC+SLI,1      WRITE TAPE MARK
CCW4     CCW   BSF,0,CC+SLI,1      POSITION IN FRONT OF 2ND TAPE MARK
CCW5     CCW   NOP,0,SLI,1         NOP TO FORCE DE WITH CE
SENSE    DC    XL24'00'            SENSE BYTES
*
*
APPENDVT DS    0F
         DC    A(DUMYAPPD)         DUMMY I/O APPENAGE ADDR
         DC    A(DUMYAPPD)         DUMMY I/O APPENAGE ADDR
         DC    A(DUMYAPPD)         DUMMY I/O APPENAGE ADDR
         DC    A(DUMYAPPD)         DUMMY I/O APPENAGE ADDR
         DC    A(DUMYAPPD)         DUMMY I/O APPENAGE ADDR
*
*
FF       EQU   X'FF'               ALL BITS ON MASK
SUBPOOL  EQU   0                   I/O CONTROL BLOCK SUBPOOL
EXIT     EQU   3                   EXIT SVC
PURGE    EQU   16                  PURGE SVC
STIMER   EQU   47                  STIMER SVC
LOADPT   EQU   X'08'               SENSE BYTE 1 LOAD POINT BIT
PURGEOPN EQU   X'E0'               PURGE OPTIONS
CC       EQU   X'40'               COMMAND CHAIN
SLI      EQU   X'20'               SUPPRESS LENGTH INDICATION
NOP      EQU   X'03'               NO OPERATION OP CODE
SEN      EQU   X'04'               SENSE COMMAND OP CODE
WTM      EQU   X'1F'               WRITE TAPE MARK OP CODE
BSF      EQU   X'2F'               BACKSPACE 1 FILE OP CODE
REW      EQU   X'07'               REWIND
RUN      EQU   X'0F'               REWIND AND UNLOAD
BLANK    EQU   C' '                BLANK DELIMITER
COMMA    EQU   C','                COMMA DELIMITER
IOTIME   DC    F'100'              1 SECOND TIMER VALUE
LISTEND  DC    H'-1'               END OF UCB ADDRESS LIST INDICATOR
D200     DC    C'200'              7TR DENSITY TAPEMARK PARM
D556     DC    C'556'              7TR DENSITY TAPEMARK PARM
D800     DC    C'800'              7TR DENSITY TAPEMARK PARM
LOW      DC    C'LOW'              9TR LOW DENSITY TAPEMARK PARM
HIGH     DC    C'HIGH'             9TR HIGH DENSITY TAPEMARK PARM
FORCE    DC    C'FORCE'            IGNORE ALLOCATION PARM
REWIND   DC    C'REWIND'           REWIND TAPE AFTER TAPEMARKS
UNLOAD   DC    C'UNLOAD'           REWIND/UNLOAD AFTER TAPEMARKS
DEBUG    DC    C'DEBUG'            DEBUG PARM TO ISSUE SDUMP SVC
SNSDCONS DC    C'SENSE='           SENSE DATA MSG CONSTANT
HEXTABLE EQU   *-240               HEX TO ALPHA CONVERSION TABLE
         DC    C'0123456789ABCDEF'
BADATXT  DC    CL10'BAD ADDR'
BADDTXT  DC    CL10'BAD DEVICE'
BUSYTXT  DC    CL10'DEV BUSY'
SENSTXT  DC    CL10'SENSE PEND'
NOTRTXT  DC    CL10'NOT READY'
NOPHTXT  DC    CL10'NO PATH AV'
COMPTXT  DC    CL10'TAPEMARKED'
ALOCTXT  DC    CL10'ALLOCATED'
IOERTXT  DC    CL10'I/O ERROR'
PROTTXT  DC    CL10'FILE PROT'
DENTXT   DC    CL10'DENSITY ='
DTXT800  DC    CL9'800'            DENSITY MSG CONSTANT
DTXT556  DC    C'556'              DENSITY MSG CONSTANT
DTXT1600 DC    C'1600'             DENSITY MSG CONSTANT
DTXT6250 DC    C'6250'             DENSITY MSG CONSTANT
MSG1     DC    AL2(M1-*),X'0000'
         DC    C'INVALID TAPEMARK PARM'
M1       EQU   *
MSG2     DC    AL2(M2-*),X'0000'
         DC    C'TAPEMARK DENSITY NOT SPECIFIED'
M2       EQU   *
UCBADDR  DC    F'0'
SCANAREA DC    XL100'00'
DEVCLASS DC    XL1'80'             TAPE DEVICE CLASS
MSGHDR   DC    Y(MSGLN),X'0000',C'TAPE '
TAPEADDR DS    CL3                 EBCDIC TAPE DEVICE ADDR
         DS    C
MSGTXT   DC    CL10' '                                           12/85*
TOD      DC    X'4021204B20204B2020'   TIME OF DAY EDIT MASK
MSGLN    EQU   *-MSGHDR                                          12/85*
         ORG   MSGHDR+4
SNSDHEAD DS    C'SENSE='           SENSE DATA IDENTIFIER
SNSWORD1 DS    CL8                 4 SENSE BYTES
SNSWORD2 DS    CL8                 4 SENSE BYTES
SNSBL1   DS    C                   DELIMITER
SNSWORD3 DS    CL8                 4 SENSE BYTES
SNSWORD4 DS    CL8                 4 SENSE BYTES
SNSBL2   DS    C                   DELIMITER
SNSWORD5 DS    CL8                 4 SENSE BYTES
SNSWORD6 DS    CL8                 4 SENSE BYTES
SNSBL3   DS    C                   END OF MSG GARBAGE BYTE
         ORG   ,                                                 12/85*
         DS    0D
DWK      DS    D                                                 12/85*
OURPARM  DS    CL80                                              12/85*
CNTLFLAG DS    X                   CONTROL FLAG
FORCEFL  EQU   X'80'               WRITE TAPEMARK EVEN IF ALLOCATED
REWFLAG  EQU   X'20'               REWIND TAPE AFTER TAPEMARKS
RUNFLAG  EQU   X'10'               REWIND/UNLOAD TAPE AFTER TAPEMARKS
IOOKFLAG EQU   X'04'               TAPEMARK SUCCESSFUL
SNSFLAG  EQU   X'02'               SENSE DATA MSG REQUIRED
DUMPFLAG EQU   X'01'               ISSUE SDUMP FOR DEBUG
DENFLAG  DS    X                   DENSITY CONTROL FLAG
DEN200   EQU   X'80'               7TR 200 DENSITY
DEN556   EQU   X'40'               7TR 556 DENSITY
DEN800   EQU   X'20'               7TR 800 DENSITY
DENLOW   EQU   X'08'               LOW DENSITY (9TR DUAL DENSITY ONLY)
         IEZDEB                                               12/85
         IHADCB  DSORG=XE                                     12/85
         IKJTCB  LIST=YES
         IKJIOPL
CVT      DSECT
         CVT   LIST=YES
UCB      DSECT
         IEFUCBOB  PREFIX=YES,LIST=YES
         END
