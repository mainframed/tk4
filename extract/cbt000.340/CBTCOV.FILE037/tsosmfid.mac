         TITLE 'SMFID CLIST VARIABLE SET PROGRAM'
*---------------------------------------------------------------------*
*        TSO PROGRAM/COMMAND TO SET CLIST VARIABLE SMFID TO THE       *
*        NAME OF THE CURRENT CPU ID NAME BASE UPON THE VALUE FOUND    *
*        IN THE SMCASID FIELD.                                        *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
TSOSMFID $PROLOG
         L     R1,CVTPTR        -> CVT
         L     R1,CVTSMCA-CVTMAP(,R1)
         MVC   VALUE(4),SMCASID-SMCABASE(R1) MOVE SMFID TO VALUE
         SPACE 1
         L     R15,CVTPTR       -> CVT
         L     R15,CVTTVT-CVTMAP(,R15) ACCESS THE TSVT
         L     R15,TSVTVACC-TSVT(,R15)
*
         CALL  (15),(ECODE,NAMEPTR,NAMELEN,VALUEPTR,VALUELEN,TOKEN),VL
         LTR   R15,R15
         BNZ   EXIT8
         $EPILOG 0
EXIT8    $EPILOG 8
         LTORG
ECODE    DC    A(TSVEUPDT)      ENTRY CODE
VALUE    DC    CL4' '
VALUEPTR DC    A(VALUE)
VALUELEN DC    F'4'
NAME     DC    CL5'SMFID'
NAMEPTR  DC    A(NAME)
NAMELEN  DC    F'5'
TOKEN    DC    F'0'
         EJECT
         IKJTSVT
         EJECT
         CVT   DSECT=YES,LIST=YES
         EJECT
         IEESMCA
         END
