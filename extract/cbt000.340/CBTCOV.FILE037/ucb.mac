UCB      MENTER  12,EQU,COM=UCB,CP=CP
***********************************************************************
* THIS PROGRAM WILL PERFORM THE IOS LOOKUP TABLE FUNCTION.            *
* IT IS MEANT TO BE EXECUTED UNDER THE TSSO SUBSYSTEM.
* COMMAND SYNTAX-     #UCB UCB/VOLUME
* IF NO UCB IS SPECIFIED, THE SYSTEM RESIDENCE UCB WILL BE
* DISPLAYED.
***********************************************************************
         SETPARSE  PCL=PARSEPCL
         CALLTSSR  EP=IKJPARS
         CH    R15,=H'3'
         BH    TRUB1               RC = 4 OR ABOVE
         L     R4,MYANS
         USING IKJPARMD,R4
         TM    UCBP+6,X'80'        IS UCB PRESENT?
         BNO   USERES
         LH    R3,UCBP+4           GET LENGTH
         CH    R3,=H'3'            UCB IS 3 BYTES
         BE    SETUCB
         CH    R3,=H'6'            VOLSER IS 6 BYTES !
         BNE   INVALID             GET ADDR OF PARM
         B     SKIPSET
SETUCB   SETON DOUCB
SKIPSET  DS    0H
         L     R3,UCBP             POINT TO THE UCB
         MVC   VOLDES(72),0(R3)
         OC    VOLDES(72),BLANKS   MAKE UPPER CASE
         L     R3,16
         USING CVTMAP,R3
         XC    WORKAREA(100),WORKAREA  ZERO OUT WORKAREA
         LA    R1,WORKAREA
         ST    R1,PARM1
         LA    R1,DEVCLASS
         ST    R1,PARM2
         LA    R1,UCBADDR
         ST    R1,PARM3
         OI    PARM3,X'80'
SRCHIT   DS    0H
         LA    R1,PARMLIST
         L     R15,CVTUCBSC
         BALR  R14,R15
         LTR   R15,R15
         BNZ   ENDLIST
         B     FORMUCB
USERES   DS    0H
***********************************************************************
* IF NO UCB WAS SPECIFIED IN THE COMMAND, THE PROGRAM WILL JUMP    HERE
* POINTING TO THE UCB FOR THE SYSTEM RESIDENCE VOLUME.
***********************************************************************
         L     R5,16
         USING CVT,R5
         L     R5,CVTSYSAD
         DROP  R5
         USING UCBD,R5
         ST    R5,UCBADDR
         MVC   VOLDES(6),UCBVOLI
         B     FORMIT
FORMUCB  DS    0H
         L     R5,UCBADDR
         IFON  DOUCB,CKUCB
         CLC   UCBVOLI,VOLDES      THE VOLSER WE WANT ?
         BNE   SRCHIT
         B     FORMIT
CKUCB    DS    0H
         CLI   VOLDES+3,C','       IS A LIST SPECIFIED?       02/86
         BNE   CKUCB02             NOPE, -->                  02/86
         LA    R2,VOLDES           GET ADDRESS OF UCB LIST    02/86
         SETON UCBX                SET FLAG                   02/86   *
CKUCB00  DS    0H                                             02/86
         CLC   0(3,R2),UCBNAME     IS THIS UCB IN THE LIST?   02/86
         BNE   CKUCB01             NOPE, -->                  02/86
         MVC   0(3,R2),=X'000000'  CLEAR OUT FOUND UCB ENTRY  02/86
         B     FORMIT              AND PROCESS IT             02/86
CKUCB01  DS    0H                                             02/86
         CLI   3(R2),C')'          END OF LIST?               02/86
         BE    SRCHIT              YEP, -->                   02/86
         CLI   3(R2),C' '          END OF LIST?               02/86
         BE    SRCHIT              YEP, -->                   02/86
         LA    R2,4(R2)            POINT TO NEXT UCB IN LIST  02/86
         B     CKUCB00             LOOP --->                  02/86
CKUCB02  DS    0H                                             02/86
         CLI   VOLDES+2,C'X'       DISPLAY GROUP OF UCB'S   *LBD 11/85*
         BNE   CKUCB03             NO                       *LBD 11/85*
         SETON UCBX                SET FLAG                 *LBD 11/85*
         CLC   VOLDES(2),UCBNAME   YES - CHECK FOR THEM     *LBD 11/85*
         BE    FORMIT              GOT ONE                  *LBD 11/85*
CKUCB03  CLC   UCBNAME,VOLDES      DESIRED UCB
         BNE   SRCHIT              NO
FORMIT   DS    0H
         FORMAT  (UCBADDR,4,XXXXXXXX),TO=UCBECB,WKA=WKA
         L     R5,UCBADDR
         MVC   MSGDEV,UCBNAME
         MVC   MSGUCBAD,UCBECB+2
         TM    UCBSTAT,UCBONLI     IS DEVICE ONLINE
         BZ    SETOFF              GO SET OFFLINE STATUS
         MVC   MSGSTAT,ON          SET ONLINE
         B     PVOLCHK             GO CHECK FOR VOLUME SERIAL
SETOFF   MVC   MSGSTAT,OFF         SET OFFLINE
         SPACE
PVOLCHK  EQU   *
         TM    UCBTBYT3,UCB3TAPE+UCB3DACC  IS DEV TAPE OR DA
         BZ    PNONDACK            IF NOT GO CHECK NON DA ALLOCATION
         OC    MSGVOL,UCBVOLI      MOVE VOLUME SERIAL TO BUFFER
         SPACE
PMNTCHK  EQU   *
         TM    UCBSTAT,UCBRESV     CHECK VOLUME MOUNTING
         BO    SETRSV              GO SET TYPE
         TM    UCBSTAT,UCBPRES     CHECK VOLUME MOUNTING
         BO    SETPR               GO SET TYPE
         MVC   MSGMOUNT,REM        REMOVABLE VOLUME
         B     PALLCHK             GO SET ALLOCATION TYPE
SETPR    MVC   MSGMOUNT,PR         PERM RESIDENT
         B     PALLCHK             GO SET ALLOCATION TYPE
SETRSV   MVC   MSGMOUNT,RSV        RESERVED
         SPACE
PALLCHK  EQU   *
         TM    UCBSTAB,UCBBPRV     CHECK TYPE OF ALLOCATION
         BO    SETPRI              GO SET TYPE
         TM    UCBSTAB,UCBBPUB     CHECK TYPE OF ALLOCATION
         BO    SETPUB              GO SET TYPE
         TM    UCBSTAB,UCBBSTR     CHECK TYPE OF ALLOCATION
         BZ    POCNTCHK            GO CHECK ALLOCATION COUNT
SETSTO   MVC   MSGALLOC,STO        STORAGE
         B     POCNTCHK            GO CHECK ALLOCATION COUNT
SETPRI   MVC   MSGALLOC,PRI        PRIVATE
         B     POCNTCHK            GO CHECK ALLOCATION COUNT
SETPUB   MVC   MSGALLOC,PUB        PUBLIC
         SPACE
POCNTCHK EQU   *
         IC    R0,UCBDMCT          LOAD OPEN DCB COUNT
         LA    R15,127             LOAD MAXIMUM OPEN DCB COUNT
         NR    R0,R15              ZERO REG EXCEPT FOR COUNT
         CVD   R0,DWD              CONVERT VALUE TO DECIMAL
         MVC   MSGOCNT,EDMASK      LOAD EDIT MASK
         ED    MSGOCNT,DWD+6       EDIT INTO MESSAGE
         SPACE
PACNTCHK EQU   *
         TM    UCBTBYT3,UCB3DACC   IS DEVICE DIRECT ACCESS
         BZ    PNONDACK            IF NOT GO CHECK NON DA ALLOCATION
         TM    CVTDCB,CVTMVSE      XA ?                     *LBD 11/85*
         BZ    UCBU370             NO - 370                 *LBD 11/85*
         LH    R0,UCBUSER          XA UCBUSER HALFWORD      *LBD 11/85*
         B     CVDUSER                                      *LBD 11/85*
UCBU370  IC    R0,UCBUSER          LOAD ALLOCATION COUNT
CVDUSER  CVD   R0,DWD              CONVERT VALUE TO DECIMAL
         MVC   MSGACNT,EDMASK      LOAD EDIT MASK
         ED    MSGACNT,DWD+6       EDIT INTO MESSAGE
         SPACE
PRCNTCHK EQU   *
         IC    R0,UCBSQC           LOAD RESERVE MACRO COUNT
         CVD   R0,DWD              CONVERT VALUE TO DECIMAL
         MVC   MSGRCNT,EDMASK      LOAD EDIT MASK
         ED    MSGRCNT,DWD+6       EDIT INTO MESSAGE
         B     PERRCHK             GO CHECK ERRORS
         SPACE
*        SET ALLOCATION COUNT FOR NON DA DEVICES
         SPACE
PNONDACK EQU   *
         TM    UCBSTAT,UCBALOC     IS DEVICE ALLOCATED
         BZ    PERRCHK             IF NOT GO CHECK FOR ERRORS
         MVI   MSGACNT+3,C'1'      SET ALLOCATED COUNT TO 1
         SPACE
*        FORMAT DEVICE STATUS
         SPACE
PERRCHK  EQU   *
         EJECT
*        GET DEVICE TYPE FOR MESSAGE IF XA ONLY             *LBD 11/85*
         TM    CVTDCB,CVTMVSE      ARE WE XA                *LBD 11/85*
         BZ    SKIPIT              NO - IGNORE THIS CODE    *LBD 11/85*
         MVC   UNITTYPE,UCBTYP     MOVE DEVICE TYPE         *LBD 11/85*
         LA    R1,B4UVPARM         -> PARM                  *LBD 11/85*
         LINK  EP=IEFEB4UV         GO TO LOOK UP ROUTINE    *LBD 11/85*
         MVC   MSGUNAME,UNITNAME   MOVE TO MESSAGE          *LBD 11/85*
         MVC   HEADUN(8),=CL8'TYPE'    SET UP HEADING       *LBD 11/85*
SKIPIT   DS    0H                                           *LBD 11/85*
         EJECT                                              *LBD 11/85*
*        PRINT COMPLETED BUFFER MESSAGE
         SPACE
PRNTLINE EQU   *
         IFON  HEADX,SKIPHEAD                               *LBD 11/85*
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(HEAD,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         SETON HEADX                                        *LBD 11/85*
SKIPHEAD DS    0H                                           *LBD 11/85*
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(MSG,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         MVC   MSGUCBAD(MSGUNAME-MSGUCBAD),BLANKS          *LBD 04/86*
         IFON  UCBX,SRCHIT                                  *LBD 11/85*
         B     ENDPROG
TRUB1    DS    0H
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(MSG3,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     ENDPROG
INVALID  DS    0H
         PUTLINE PARM=PUTBLOK,                                         X
               OUTPUT=(MSG1,TERM,SINGLE,DATA),MF=(E,IOPLADS)
         B     ENDPROG
ENDLIST  DS    0H
         LA    R2,VOLDES           GET UCB INPUT PARM         02/86
         IFOFF HEADX,ENDLIST2      --> IF HEADING WAS USED  *LBD 11/85*
         CLI   VOLDES+3,C','       WAS A INPUT LIST USED?     02/86
         BNE   ENDPROG             NOPE, -->                  02/86
ENDLIST1 DS    0H                                             02/86
         CLC   0(3,R2),=C'   '    'IS THERE AN ENTRY NOT FOUND02/86
         BH    ENDLIST2            YEP, -->                   02/86
         CLI   4(R2),C')'          END OF LIST?               02/86
         BE    ENDPROG             YEP, -->                   02/86
         CLI   4(R2),C' '          END OF LIST?               02/86
         BE    ENDPROG             YEP, -->                   02/86
         LA    R2,4(R2)            GET NEXT ENTRY IN LIST     02/86
         B     ENDLIST1            AND TEST AGAIN, -->        02/86
ENDLIST2 DS    0H                                             02/86
         MVC   MSGEUCB,0(R2)       MOVE IN UCB NOT FOUND      02/86
         IFON  DOUCB,ENDLIST3      --> IF WORKING W/UCB'S     02/86
         MVC   MSGEUCB-3(6),VOLDES MOVE IN VOLSER             02/86
ENDLIST3 DS    0H                                             02/86
         PUTLINE PARM=PUTBLOK,OUTPUT=(MSGE,TERM,SINGLE,DATA),          X
               MF=(E,IOPLADS)
         IFOFF HEADX,ENDPROG       WAS A HEADING USED?, --> *LBD 11/85*
         LA    R2,4(R2)            GET NEXT ENTRY             02/86
         B     ENDLIST1            AND TEST AGAIN, -->        02/86
ENDPROG  DS    0H
         MLEAVE
PARSEPCL IKJPARM
UCBP     IKJIDENT  'UCB',LIST,UPPERCASE,MAXLNTH=72,FIRST=ALPHANUM,     X
               OTHER=ALPHANUM,CHAR,                                    X
               HELP=('UCB IN THE FORM XXX OR VVVVVV')
         IKJENDP
         BITSW DOUCB,UCBX,HEADX
PUTECB   DS    F
BLANKS   DC    CL80' '
MSGE     DC    H'42',H'0',C'UCB2        '
MSGEUCB  DC    CL3' '
         DC    CL30' NOT FOUND DURING SCAN'
MSG1     DC    H'50',H'0'
         DC    CL50'INVALID UCB SPECIFIED - REENTER COMMAND'
MSG3     DC    H'50',H'0'
         DC    CL50'IKJPARSE FAILURE, CONTACT SYSTEMS'
         DS    0F                                           *LBD 11/85*
UCBECB   DC    CL8' '
WKA      DC    CL12' '
UCBADDR  DS    F
VOLDES   DC    CL80' '             VOLUME DESIRED
DEVHEX   DS    F
ADDRECB  DC    CL8' '
CURRADDR DS    F                   STATUS OF SEARCH IN UCBS
PARMLIST DS    0D
PARM1    DS    F
PARM2    DS    F
PARM3    DS    F
WORKAREA DS    XL100
DEVCLASS DC    XL1'00'
NUM      DS    F
COMLIST  DS    0H
DWD      DS    D
HEAD     DC    AL2(HE-*),X'0000'
         DC    C'UCB0 UCBA   DEV STA VOLSER MOUNT   ALL OPN RSV'
         DC    CL2' '                                       *LBD 11/85*
HEADUN   DC    CL8' '                                       *LBD 11/85*
HE       EQU   *
         SPACE
B4UVPARM DS    0F                                           *LBD 11/85*
         DC    A(UNITNAME)                                  *LBD 11/85*
         DC    A(FLAG4UV)                                   *LBD 11/85*
UNITNAME DC    CL8' '                                       *LBD 11/85*
UNITTYPE DC    CL4' '              DEVICE TYPE              *LBD 11/85*
         DC    A(0)                                         *LBD 11/85*
FLAG4UV  DC    X'2100'             IEFEB4UV FLAG            *LBD 11/85*
         SPACE
MSG      EQU   *
MSGHDR   DC    AL2(ME-*),X'0000'
MSGTEXT  DC    C'UCB1 '
MSGUCBAD DC    CL6' '              UCB ADDRESS
MSGBL1   DC    C' '                BLANK FIELD
MSGDEV   DC    CL3' '              UCB NAME
MSGBL2   DC    C' '                BLANK FIELD
MSGSTAT  DC    CL3' '              ONLINE / OFFLINE
MSGBL3   DC    C' '                BLANK FIELD
MSGVOL   DC    CL6' '              VOLUME SERIAL
MSGBL4   DC    C' '                BLANK FIELD
MSGMOUNT DC    CL3' '              TYPE OF VOLUME MOUNTING
MSGBL5   DC    C' '                BLANK FIELD
MSGALLOC DC    CL3' '              TYPE OF ALLOCATION
MSGACNT  DC    CL4' '              NUMBER OF USERS ALLOCATED
MSGOCNT  DC    CL4' '              NUMBER OF OPEN DCBS
MSGRCNT  DC    CL4' '              RESERVE MACRO COUNT
MSGBL6   DC    CL2' '              BLANK FIELD
MSGUNAME DC    CL8' '              UNIT NAME IF AVAIL (XA)  *LBD 11/85*
MSGEREND EQU   *-1                 END OF MSG ERROR BUFFER
ME       EQU   *
         ORG   MSGUCBAD
MSGERDEV DS    CL3                 DEVICE OR CU NOT FOUND
         ORG   MSGUCBAD
MSGERVOL DS    CL6                 VOLUME SERIAL NOT FOUND
         ORG   ,                                            *LBD 11/85*
ON       DC    C' ON'              ONLINE
OFF      DC    C'OFF'              OFFLINE
PR       DC    C'P/R'              PERM RESIDENT
RSV      DC    C'RSV'              RESERVED
REM      DC    C'REM'              REMOVABLE
PRI      DC    C'PRI'              PRIVATE
PUB      DC    C'PUB'              PUBLIC
STO      DC    C'STO'              STORAGE
EDMASK   DC    X'40202120'         EDIT MASK
ACT      DC    C'ACT'              ACTIVE I/O
NOTRDY   DC    C'NOTRDY'           DEVICE NOT REDY
CUBUSY   DC    C'CUBUSY'           CONTROL UNIT BUSY
BUSY     DC    C'BUSY'             DEVICE BUSY
CONSOLE  DC    C'CONSOLE'          CONSOLE DEVICE
SENSE    DC    C'SENSE'            SENSE PENDING
QUIESCED DC    C'QUIESCED'         DEVICE QUIESCED
OLTEP    DC    C'OLTEP'            OLTEP USING DEVICE
HEXTABLE EQU   *-240
         DC    C'0123456789ABCDEF'  HEX TRANSLATE TABLE
         CVT   DSECT=YES
         IKJIOPL
         EJECT
UCBD     DSECT
         IEFUCBOB  LIST=YES
         END
