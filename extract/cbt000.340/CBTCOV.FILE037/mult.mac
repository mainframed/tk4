         TITLE '     S T A C K                '
************************************************************
*                                                          *
*           'STACK' TSO COMMAND                            *
*                                                          *
************************************************************
         SPACE
*        WRITTEN BY. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. PRC COMPUTER CENTER INC, MCLEAN VA.
*        DATE WRITTEN. APRIL 27 1977.
*        DATE UPDATED. SEPTEMBER 10 1980.
*        DESCRIPTION.
*            THIS COMMAND STACKS A LIST OF COMMANDS.
*            THE LIST OF COMMANDS IS WITHIN PARENTHESES.
*            THE FIRST CHARACTER AFTER THE OPEN PARENTHESES
*            WILL BE USED AS THE DELIMITER TO SEPARATE THE
*            COMMANDS IN THE LIST.  FOR EXAMPLE, THE FOLLOWING
*            TWO COMMANDS WILL GIVE THE SAME RESULTS.
*               STACK (;EDIT X DATA;V;FIND /ABC/;FIND /WXY/;END)
*               STACK (ZEDIT X DATAZVZFIND /ABC/ZFIND /WXY/ZEND)
*            NOTE: INSTALLATIONS THAT HAVE THE PCF PRODUCT CANNOT
*            USE THE SEMICOLON AS THE DELIMITER IN THIS COMMAND,
*            BECAUSE PCF SEES IT AND STRIPS IT OUT BEFORE THIS COMMAND
*            GETS CONTROL.  BUT THOSE WHO HAVE PCF HAVE NO NEED FOR
*            THIS COMMAND ANYWAY, BECAUSE PCF INCLUDES THIS FUNCTION.
         SPACE
         GBLB  &MVS
&MVS     SETB  1                   1 - MVS   0 - SVS,MVT
         SPACE
MULT     START
         USING *,R12
         B     @PROLOG-*(,15)
         DC    AL1(11),CL11'MULT   '
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(1),AL3(@DATAL)
@PROLOG  STM   14,12,12(13)
         LR    R12,R15
         LR    R2,R1
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         ST    13,4(,1)
         ST    1,8(,13)
         LR    13,1
         LR    R1,R2               RESTORE CPPL PTR
         USING @DATA,R13
         SPACE 1
*              IOPL --> STPB --> LSD --> LIST
         SPACE
$CPPL    EQU   1
$IOPL    EQU   7
$LSD     EQU   9
         SPACE
         XC    LINKAREA(8),LINKAREA
         SLR   R15,R15
         ST    R15,PTRSTCK         ZERO IKJSTCK ADDRESS
         LA    $IOPL,MYIOPL
         LA    R8,MYSTPB
         SPACE
         USING CPPL,$CPPL
         USING IOPL,$IOPL
         USING LSD,$LSD
         SPACE
*                             INITIALIZE THE IOPL
         L     R2,CPPLUPT
         ST    R2,IOPLUPT
         L     R2,CPPLECT
         ST    R2,IOPLECT
         LA    R2,MYECB
         ST    R2,IOPLECB
*              IOPLIOPB WILL BE INITIALIZED BY THE STACK MACRO
         SPACE
*        STPB WILL BE INITIALIZED BY THE STACK MACRO
         SPACE
************************************************************
*                                                          *
*        SET UP PPL FOR PARSE                              *
*                                                          *
************************************************************
         SPACE
         LA    R15,MYPPL
         USING PPL,R15
         MVC   PPLUPT(4),CPPLUPT
         MVC   PPLECT(4),CPPLECT
         LA    R0,MYECB
         ST    R0,PPLECB
         XC    MYECB,MYECB
*        L     R0,=A(STACKPCL)
         LA    R0,PCLADDR
         ST    R0,PPLPCL
         LA    R0,MYANS
         ST    R0,PPLANS
         MVC   PPLCBUF(4),CPPLCBUF
         ST    R13,PPLUWA
         DROP  R15                 PPL
         SPACE 1
************************************************************
*                                                          *
*        CALL THE PARSE SERVICE ROUTINE                    *
*                                                          *
************************************************************
         SPACE 1
         LR    R1,R15              POINT TO PPL
         AIF   (NOT &MVS).SKIP1
         L     R15,16              CVTPTR
         TM    524(R15),X'80'      IF HI ORDER BIT NOT ON
         BNO   PARSELNK               THEN DO LINK, NOT CALL
         L     R15,524(,R15)       CVTPARS
         BALR  R14,R15             CALL IKJPARS
         B     PARSEEXT            SKIP AROUND LINK
PARSELNK EQU   *
.SKIP1   ANOP
         LINK  EP=IKJPARS,SF=(E,LINKAREA)
PARSEEXT EQU   *
         SPACE 1
         LTR   R15,R15
         BZ    OKPARSE
         LA    R15,12
         B     EXIT
OKPARSE  EQU   *
         L     R3,MYANS
         USING IKJPARMD,R3
         SPACE
         LA    R1,LIST             POINT TO IN-STORAGE LIST
         ST    R1,NEXTPTR          SAVE NEXT RECORD ADDRESS
         SPACE
*
*              ISOLATE SUBCOMMANDS
*
         LA    R2,SUBCHAIN
         TM    6(R2),X'80'         PRESENT?
         BZ    SCX                 NO - BRANCH
         LH    R5,4(,R2)           GET LENGTH
         CH    R5,=H'2'            LENGTH AT LEAST 2
         BL    SCX                 NO - BRANCH
         L     R4,0(,R2)           POINT TO STRING
         MVC   DELIMITR(1),0(R4)   USE FIRST BYTE AS DELIMITER
         LA    R4,1(,R4)           POINT PAST DELIMITER
         BCTR  R5,0                REDUCE LENGTH BY 1
SCNEXT   L     R6,NEXTPTR
         LA    R6,4(,R6)
         SLR   R1,R1
SCLOOP   CLC   0(1,R4),DELIMITR    DELIMITER?
         BE    SCSEMI              YES - BRANCH
         IC    R0,0(,R4)
         STC   R0,0(,R6)
         LA    R4,1(,R4)
         LA    R6,1(,R6)
         LA    R1,1(,R1)
         BCT   R5,SCLOOP
SCSEMI   LTR   R1,R1
         BZ    SCNULL
         LA    R1,4(,R1)
         L     R15,NEXTPTR
         STH   R1,HALF
         MVC   0(2,R15),HALF
         XC    2(2,R15),2(R15)
         LA    R15,0(R1,R15)
         ST    R15,NEXTPTR
SCNULL   LA    R4,1(,R4)
         LTR   R5,R5
         BZ    SCX
         BCT   R5,SCNEXT
SCX      LA    R14,LIST
         C     R14,NEXTPTR         ANYTHING TO STACK?
         BNE   OKSTRING            YES - BRANCH
         LA    R15,12              NO - EXIT
         B     EXIT
OKSTRING EQU   *
         L     R15,NEXTPTR
         SLR   R15,R14             GET LENGTH
         STH   R15,LISTOTLN
         SPACE
*                             GET AND INITIALIZE THE LSD
         LH    0,LISTOTLN          GETMAIN FOR LIST
         A     0,SP78                          AND LSD (16 BYTES)
         GETMAIN R,LV=(0)
         LR    $LSD,1
         LA    R14,16(,1)          REG 14 --> LIST
         ST    R14,LSDADATA
         ST    R14,LSDANEXT
         SR    R14,R14
         STH   R14,LSDRCLEN
         LH    R14,LISTOTLN
         STH   R14,LSDTOTLN
         SPACE
         BCTR  R14,0               LENGTH FOR EX
         EX    R14,MVLIST          MOVE LIST INTO SUBPOOL 78
         SPACE
         AIF   (NOT &MVS).SKIP2
         L     R15,16              CVTPTR
         TM    472(R15),X'80'      IS IKJSTCK IN LPA
         BNO   *+12                NO, BRANCH TO LOAD
         L     R15,472(,R15)       YES, LOAD CVTSTCK
         B     SETSTCK             BRANCH AROUND LOAD
.SKIP2   ANOP
         LA    R0,=CL8'IKJSTCK'
         LOAD  EPLOC=(0)
         LR    R15,R0              GET ENTRY POINT ADDRESS
         LA    R15,0(,R15)         ZERO HIGH ORDER BYTE
SETSTCK  ST    R15,PTRSTCK         SAVE ADDRESS OF IKJSTCK
         SPACE
         LR    R1,$IOPL
         CLI   LISTKW+1,1          'LIST' SPECIFIED?
         BE    LISTEM              YES - BRANCH
         SPACE
         STACK PARM=(R8),STORAGE=(($LSD),PROCN),ENTRY=(15),MF=(E,(1))
         SPACE
         B     STACKED
LISTEM   EQU   *
         SPACE
         STACK PARM=(R8),STORAGE=(($LSD),PROCL),ENTRY=(15),MF=(E,(1))
         SPACE
STACKED  LTR   R15,R15
         BZ    EXIT0
         L     R15,PTRSTCK
         LR    R1,$IOPL
         STACK PARM=(R8),DELETE=ALL,ENTRY=(15),MF=(E,(1))
         SPACE
         LA    0,L'STACKMSG
         LA    1,STACKMSG
         TPUT (1),(0),R
         LA    R15,16
         B     EXIT
EXIT0    LA    15,0
EXIT     LR    R2,R15              SAVE RETURN CODE
         L     R15,PTRSTCK
         LTR   R15,R15             WAS IKJSTCK LOADED
         BNP   EXITX               NO, BRANCH
         LA    R0,=CL8'IKJSTCK'
         DELETE EPLOC=(0)
EXITX    LR    R15,R2              RESTORE RETURN CODE
         LR    1,13
         L     R0,@SIZE
         L     13,4(,13)
         LR    R2,R15
         FREEMAIN R,A=(1),LV=(0)
         LR    R15,R2
         LM    0,12,20(13)
         L     R14,12(,13)
         BR    R14
         SPACE 1
************************************************************
*                                                          *
*        CONSTANTS                                         *
*                                                          *
************************************************************
         SPACE
MVLIST   MVC   16(0,1),LIST        MOVE LIST TO SUBPOOL 78 AFTER LSD
STACKMSG DC    CL28'STACK ERROR'
SP78     DC    0F'0',AL1(78),AL3(16) SUBPOOL 78
         LTORG
PATCH    DC    4D'0'               PATCH AREA
         SPACE
DDNAM    EQU   40                  DCB OFFSET
EXLST    EQU   36                  DCB OFFSET
PCLADDR  DC    0D'0'               END MAIN CSECT, BEGIN PCL CSECT
         SPACE
************************************************************
*                                                          *
*        PARSE PARAMETERS                                  *
*                                                          *
************************************************************
         SPACE
         PRINT NOGEN
STACKPCL IKJPARM
SUBCHAIN IKJPOSIT PSTRING
LISTKW   IKJKEYWD
         IKJNAME 'LIST'
         IKJNAME 'NOLIST'
         IKJENDP
         PRINT GEN
         SPACE
************************************************************
*                                                          *
*        DSECTS                                            *
*                                                          *
************************************************************
         SPACE
@DATA    DSECT
         DS    18F
DOUBLE   DS    D
HALF     EQU   DOUBLE,2
LINKAREA DS    2F
PTRSTCK  DS    F
MYPPL    DS    7F
MYANS    DS    F
MYECB    DS    F
MYIOPL   DS    4F
MYSTPB   DS    8F
DELIMITR DS    C
LISTOTLN DS    H
NEXTPTR  DS    F
LIST     DS    0D,F
         DS    256X
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE
         IKJCPPL
         SPACE 2
         IKJIOPL
         SPACE 2
         IKJPPL
         SPACE 2
         IKJLSD
         SPACE 2
         IKJSTPB
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
 PUNCH ' SETCODE  AC(0)'
 PUNCH ' IDENTIFY MULT(''V 2.0 &SYSDATE &SYSTIME'') '
 PUNCH ' ENTRY    MULT'
 PUNCH ' NAME     MULT(R)'
         END
