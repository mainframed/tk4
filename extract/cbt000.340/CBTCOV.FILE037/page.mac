PAGE     TITLE '- PAGE DATA SET DISPLAY COMMAND'
*        THIS COMMAND WILL DISPLAY ALL ACTIVE PAGE DATA SETS
*        WITH WITH SELECTED INFORMATION.  IF ANY SWAP DATA
*        SETS ARE IN USE THEY WILL ALSO BE DISPLAYED.
*        ALL MESSAGES WILL BE DISPLAYED ON ISSUING CONSOLE
*        AND HARDCOPY.  IF THE DATA SET IS NOT IN USE BECAUSE
*        OF ERRORS 'ALL' WILL APPEAR IN BADCT COLUMN.
*
*
*
*        CAN BE USED ON MVS/XA OR  MVS/370
*
*
*        COMMAND FORMAT:
*
*        PAGE  (NO OPERAND)
*
*
*        OUTPUT MSG FORMAT
*
*        DEV VOLSER DSNAME TYPE RATE  SIZE USED% BADCT
*
*
*
PAGE     AMODE 31                                             02/86
PAGE     RMODE ANY                                            02/86
PAGE     MENTER 12,COM=PAGE
         L     R3,CVTPTR           LOAD CVT BASE REG
         USING CVT,R3
         L     R4,CVTASMVT         LOAD ASMVT BASE
         USING ASMVT,R4
         L     R5,ASMPART          LOAD PAGING ACT REF TABLE BASE
         USING PART,R5
         L     R6,PARTDSNL         LOAD DATA SET NAME LIST BASE
         USING DSNLIST,R6
         LA    R7,PARTENTS         LOAD ENTRY BASE REG
         L     R9,PARTEUSE         LOAD NUMBER OF PART ENTRIES IN USE
         USING PARTENT,R7
         USING UCB,R8
         SPACE
*        DISPLAY HEADING
         LA    R2,HEADMSG
         BAL   R11,PUTLINE         DISPLAY HEADING
         SPACE
*        INITIALIZE BUFFER MSG
         SPACE
         ICM   R1,15,HEADMSG       LOAD HEADING MSG LENGTH & FLAGS
         STCM  R1,15,MSGLN         SET MSG LENGTH & FLAGS
         MVI   MSGBL1,BLANK        INITIALIZE BLANK FIELD
         MVI   MSGBL2,BLANK        INITIALIZE BLANK FIELD
         MVI   MSGBL3,BLANK        INITIALIZE BLANK FIELD
         MVI   MSGBL4,BLANK        INITIALIZE BLANK FIELD
         MVI   MSGBL5,BLANK        INITIALIZE BLANK FIELD
         MVI   MSGBL6,BLANK        INITIALIZE BLANK FIELD
         SPACE
*        BUILD & DISPLAY MESSAGE FOR EACH PAGE DATA SET
         SPACE
PAGELOOP EQU   *
         TM    PAREFLG1,PARENUSE   IS THIS PART USED
         BO    NEXTPART            IF NOT SKIP IT
         L     R8,PAREUCBP         LOAD UCB BASE REG
         MVC   MSGDEV,UCBNAME      MOVE DEVICE ADDR TO MSG
         MVC   MSGVOL,UCBVOLI      MOVE VOLUME SERIAL TO MSG
         MVC   MSGDSN,DSNAME       MOVE DSNAME TO MSG
         SPACE
         LA    R14,3               LOAD LOOP LIMIT
         IC    R0,PARETYPE         LOAD PAGE TYPE FLAG
         LA    R15,PAGETYPE        LOAD START OF TYPE CONSTANTS
         SRL   R0,4                SHIFT FLAG BITS
TYPELOOP SRDL  R0,1                SHIFT RIGHT MOST TO R1
         LTR   R1,R1               DID FLAG BIT APPEAR IN R1
         BM    SETTYPE             IF YES GET OUT OF LOOP
         LA    R15,4(R15)          BUMP TYPE CONSTANT ADDRESS REG
         BCT   R14,TYPELOOP        LOOP THRU ALL BITS
SETTYPE  MVC   MSGTYPE,0(R15)      MOVE PAGE DATA SET TYPE TO MSG
         SPACE
         L     R1,PARERQTM         LOAD BURST RATE
         SRL   R1,8                DEVIDE BY 256
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGRATE,MASK4       LOAD EDIT MASK
         ED    MSGRATE,XAS+6       EDIT VALUE INTO MSG
         SPACE
         L     R1,PARESZSL         LOAD SIZE IN SLOTS
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGSIZE,MASK6       LOAD EDIT MASK
         ED    MSGSIZE,XAS+5       EDIT VALUE INTO MSG
         SPACE
         TM    PAREFLG1,PAREINCP   IS DATA SET INTERCEPTED
         BO    INTRPART            IF YES USE 'TEMP' FOR USED %
         LR    R15,R1              COPY SIZE
         S     R1,PARESLTA         FIND USED SLOT COUNT
         M     R0,F100             MULTIPLY FOR % CALC
         DR    R0,R15              FIND % OF DATA SET USED
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGUSED,MASK4       LOAD EDIT MASK
         ED    MSGUSED,XAS+6       EDIT VALUE INTO MSG
         B     BADPCHK             SKIP NEXT ISNE
INTRPART MVC   MSGUSED,TEMP        MOVE 'TEMP' TO USED %
         SPACE
BADPCHK  TM    PAREFLG1,PAREDSBD+PAREINCP  IS DATA SET BAD OR INTERCPT
         BNZ   BADPART             IF YES USE 'ALL' NOT COUNT
         L     R1,PARERRCT         LOAD BAD SLOT COUNT
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGBAD,MASK6        LOAD EDIT MASK
         ED    MSGBAD,XAS+5        EDIT VALUE INTO MSG
         B     DISPPART            GO DISPLAY COMPLETED MSG
BADPART  MVC   MSGBAD,ALLBAD       MOVE 'ALL'  TO MSG
         SPACE
DISPPART LA    R2,WTOMSG           LOAD MESSAGE
         BAL   R11,PUTLINE         DISPLAY COMPLETED MSG
         SPACE
NEXTPART LA    R6,44(R6)           BUMP DSNAME LIST BASE
         LA    R7,64(R7)           BUMP PART ENTRY BASE REG
         BCT   R9,PAGELOOP         LOOP THRU ALL PART ENTRIES
         SPACE 3
*        DIAPLAY SWAP DATA SETS IF ANY IN USE
         SPACE
         L     R5,ASMSART          LOAD SWAP ACT REF TABLE BASE
         USING SART,R5
         ICM   R9,15,SARTEUSE      ANY SWAP DATA SETS IN USE
         BZ    RETURN
         L     R6,SARTDSNL         LOAD DATA SET NAME LIST BASE
         USING DSNLIST,R6
         LA    R7,SARTENTS         LOAD ENTRY BASE REG
         USING SARTE,R7
         MVC   MSGTYPE,SWAP        MOVE SWAP DATA SET TYPE TO MSG
         MVC   MSGRATE,BLANKS      BLANK BURST RATE
         SPACE
*        BUILD & DISPLAY MESSAGE FOR EACH SWAP DATA SET
         SPACE
SWAPLOOP EQU   *
         TM    SREFLG,SRENUSE      IS THIS PART USED
         BO    NEXTSART            IF NOT SKIP IT
         L     R8,SREUCB           LOAD UCB BASE REG
         MVC   MSGDEV,UCBNAME      MOVE DEVICE ADDR TO MSG
         MVC   MSGVOL,UCBVOLI      MOVE VOLUME SERIAL TO MSG
         MVC   MSGDSN,DSNAME       MOVE DSNAME TO MSG
         SPACE
         L     R1,SRETOTSL         LOAD SIZE IN SWAP SETS
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGSIZE,MASK6       LOAD EDIT MASK
         ED    MSGSIZE,XAS+5       EDIT VALUE INTO MSG
         SPACE
         LR    R15,R1              COPY SIZE
         S     R1,SREAVLSL         FIND USED SWAP COUNT
         M     R0,F100             MULTIPLY FOR % CALC
         DR    R0,R15              FIND % OF DATA SET USED
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGUSED,MASK4       LOAD EDIT MASK
         ED    MSGUSED,XAS+6       EDIT VALUE INTO MSG
         SPACE
         TM    SREFLG,SREDSBD      IS DATA SET BAD
         BO    BADSWAP             IF YES USE 'ALL' NOT COUNT
         L     R1,SRERRCNT         LOAD BAD SWAP SET COUNT
         CVD   R1,XAS              CONVERT VALUE
         MVC   MSGBAD,MASK6        LOAD EDIT MASK
         ED    MSGBAD,XAS+5        EDIT VALUE INTO MSG
         B     DISPSART            GO DISPLAY COMPLETED MSG
BADSWAP  MVC   MSGBAD,ALLBAD       MOVE ALLBAD TO MSG
         SPACE
DISPSART LA    R2,WTOMSG
         BAL   R11,PUTLINE         DISPLAY COMPLETED MSG
         SPACE
NEXTSART LA    R6,44(R6)           BUMP DSNAME LIST BASE
         LA    R7,48(R7)           BUMP SART ENTRY BASE REG
         BCT   R9,SWAPLOOP         LOOP THRU ALL SART ENTRIES
         SPACE 3
RETURN   DS    0H                                             02/86
         MLEAVE                    RETURN TO SYSTEM
*                                                             02/86
PUTLINE  DS    0H                                             02/86
         PUTLINE PARM=PUTBLOK,                                02/86    +
               OUTPUT=((R2),TERM,SINGLE,DATA),                02/86    +
               MF=(E,IOPLADS)                                 02/86
         BR    R11                                            02/86
         SPACE 3
         EJECT
BLANK    EQU   C' '                BLANK CONSTANT
EXIT     EQU   3                   EXIT SVC NUMBER
XAS      DC    D'0'                DATA AREA FOR CALCULATIONS
F100     DC    F'100'              CONSTANT FOR % CALCULATION
PAGETYPE DC    C'LOCLDPLXCOMMPLPA' TYPE OF PAGE DATA SETS
SWAP     DC    C'SWAP'             SWAP DATA SET TYPE
TEMP     DC    C'TEMP'             TEMP NON USE OF DUPLEX DATA SET
BLANKS   DC    CL8' '              BLANK FIELD
ALLBAD   DC    C'  ALL '           BAD DATA SET INDICATOR
MASK4    DC    X'40202120'         4 BYTE EDIT MASK
MASK6    DC    X'402020202120'     6 BYTE EDIT MASK
         SPACE 3
HEADMSG  DC    Y(HM-*),X'0000'
         DC    C'DEV VOLUME     DSNAME           TYPE  RATE SIZE USED  +
               BADCT'
         EJECT
HM       EQU   *
WTOMSG   DS    0F                  OPERATOR MSG BUFFER
MSGLN    DS    H                   MSG LENGTH
MSGFLAGS DS    XL2                 MSG FLAGS
MSGDEV   DS    CL3                 DEVICE ADDRESS
MSGBL1   DS    C                   BLANK DELIMITER
MSGVOL   DS    CL6                 DEVICE VOLUME SERIAL
MSGBL2   DS    C                   BLANK DELIMITER
MSGDSN   DS    CL20                DATA SET NAME
MSGBL3   DS    C                   BLANK DELIMITER
MSGTYPE  DS    CL4                 TYPE OF PAGE DATA SET
MSGBL4   DS    C                   BLANK DELIMITER
MSGRATE  DS    CL4                 BURST RATE
MSGSIZE  DS    CL6                 SIZE IN SLOTS
MSGBL5   DS    C                   BLANK DELIMITER
MSGUSED  DS    CL4                 USED %
MSGBL6   DS    C                   BLANK DELIMITER
MSGBAD   DS    CL6                 BAD SLOT COUNT
MSGPGLN  EQU   *-MSGLN             TOTAL MSG LENGTH
         EJECT
DSNLIST  DSECT
DSNAME   DS    CL44                DSNAME OF PAGING DATA SET
DSNNEXT  EQU   *
         SPACE 3
*        ILRASMVT
ASMVT    DSECT
         ORG   ASMVT+4
ASMSART  DS    A                   ADDRESS OF SWAP ACTIVITY REF TABLE
ASMPART  DS    A                   ADDRESS OF PAGING ACTIVITY REF TBL
         ORG   ASMVT+20
ASMEREC  DS    A                   ADDRESS OF BAD SLOT ERROR RECORD
         SPACE 3
*        ILRPART
PART     DSECT
PARTIDEN DS    C'PART'             EBCDIC IDENTIFIER
PARTSIZE DS    F                   NUMBER OF ENTRIES IN THIS PART
PARTEUSE DS    F                   NUMBER OF PARTE'S IN USE
         ORG   PART+24
PARTDSNL DS    A                   ADDRESS OF DSNAME LIST
         ORG   PART+80
PARTENTS DS    XL64                1ST PART ENTRY
         SPACE 3
*        ILRSART
SART     DSECT
SARTIDEN DS    C'SART'             EBCDIC IDENTIFIER
SARTSIZE DS    F                   NUMBER OF ENTRIES IN THIS SART
SARTEUSE DS    F                   NUMBER OF SARTE'S IN USE
         ORG   SART+24
SARTDSNL DS    A                   ADDRESS OF DSNAME LIST
         ORG   SART+80
SARTENTS DS    XL48                1ST SART ENTRY
         EJECT
PARTENT  DSECT                     PART ENTRY
PAREPARE DS    A                   POINTER TO NEXT PARTE IN USE
PARELKUP DS    0F                  PARTE LOCK WORD
PARELKFL DS    X                   LOCK FLAG BYTE
PAREFSIP EQU   X'80'               SLOT SORT IN PROGRESS FLAG
PAREIORN DS    FL1                 NO OF IORB'S BUILT FOR THIS PARTE
PARECPCT DS    H                   LOCK COUNT USED FOR RECOVERY
PARETYPE DS    X                   PAGE DATA SET TYPE FLAGS
PAREPLPA EQU   X'80'               PLPA DATA SET
PARECOMM EQU   X'40'               COMMON DATA SET
PAREDPLX EQU   X'20'               DUPLEX DATA SET
PARELOCL EQU   X'10'               LOCAL DATA SET
PAREFLG1 DS    X                   PARTE FLAG
PARENUSE EQU   X'80'               PARTE NOT IN USE
PAREDSBD EQU   X'40'               DATA SET BAD FLAG
PAREINCP EQU   X'20'               DUPLEX INTERCEPTED FLAG
PAREIOEQ DS    A                   ADDR OF 1ST IOE ON UNSORTED READ Q
PARESZSL DS    F                   SIZE OF DATA SET IN SLOTS
PARESLTA DS    F                   SLOTS AVAILABLE FOR ALLOCATION
PARERRCT DS    F                   NUMBER OF PERM I/O ERRORS
PAREIOBR DS    A                   ADDR OF 1ST IORB FOR THIS DATA SET
PAREPATP DS    A                   ADDR OF PAT FOR THIS DATA SET
PAREPCTP DS    A                   ADDR OF PCT FOR THIS DATA SET TYPE
PAREEDBP DS    A                   ADDR OF EDB FOR THIS DATA SET
PAREUCBP DS    A                   ADDR OF UCB FOR THIS DATA SET
PARENODE DS    A                   SORTED READ Q ANCHOR
PAREWTQE DS    A                   ADDR OF WRITE Q
PARERQTM DS    F                   CORR VALUE FOR SERV BURST CALC
PARELCYL DS    XL2                 LAST REL CYL PROC BY SLOT SORT
         DS    XL2                 RESERVED
         EJECT
SARTE    DSECT                     SART ENTRY
SRENEXT  DS    A                   POINTER TO NEXT SARTE IN USE
SRELOCK  DS    F                   LOCK WORD
         DS    X                   RESERVED
SREFLG   DS    X                   SARTE FLAG
SRENUSE  EQU   X'80'               SARTE NOT IN USE
SREDSBD  EQU   X'40'               DATA SET BAD FLAG
SREDRIVE EQU   X'20'               SWAP DRIVER REDRIVE FLAG
SREFIXED EQU   X'10'               FIXED HEAD FILE FLAG
SRENN    DS    H                   SARTE NUMBER FOR THIS SARTE
SRECCW   DS    A                   ADDR OF 1ST SCCWS IN CHAIN
SRETOTSL DS    F                   TOT SWAP SETS ON THIS DATA SET
SREAVLSL DS    F                   AVAILABLE SWAP SETS ON THIS DS
SRERRCNT DS    F                   NUMBER OF ERROR SWAP SETS
SREIOBR  DS    A                   ADDR OF 1ST IORB FOR THIS DATA SET
SREPAT   DS    A                   ADDR OF SAT FOR THIS DATA SET
SRESDCTE DS    A                   ADDR OF SDCTE FOR THIS DATA SET
SREEDB   DS    A                   ADDR OF EDB FOR THIS DATA SET
SREUCB   DS    A                   ADDR OF UCB FOR THIS DATA SET
         EJECT
         IKJIOPL
         EJECT
CVT      DSECT
         CVT   DSECT=YES
         EJECT
UCB      DSECT
         IEFUCBOB
         END
