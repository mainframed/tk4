//* THIS MOD INSTALLS SPECIAL RMF TRACE FEATURE TO MEASURE EFFECT OF
//* OTHER MODS ON CICS PAGING AND PERFORMANCE.
//* IT IS USEFUL TO MEASURE PAGING INDICATORS FOR A SPECIFIC ADDRESS
//* SPACE, FOR EXAMPLE, ITS HIGH UIC, PAGE-FAULT RATE, OR FRAME COUNT.
//* CAUTION, THE ADDRESS SPACE YOU WANT TO MEASURE MUST BE SWAPPED
//* IN, OR RESULT WILL BE UNPREDICTABLE. MAKE IT NON-SWAPPABLE VIA
//* PPT OR IPS.
//* PART 1 IS SOURCE FOR RMF TRACE EXIT THAT CHECKS SPECIAL FIELD NAMES
//* AND LOCATES THOSE FIELDS FOR RMF.  THESE FIELDS ARE MOSTLY IN
//* CICS'S OUCB, OUXB, OR ASCB, BUT SOME ARE IN THE PVT.
//* PART 2 INSTALLS THE SPECIAL NAMES IN RMF'S VALIDATION TABLE,
//* AND CHANGES SAMPLES-PER-SET VALUE FOR RMF. STOCK VALUE OF
//* 60 GENERATES TRACE READOUTS 2 MINS APART WHEN USED WITH MAXIMUM
//* PRACTICAL CYCLE TIME OF 2000 MS. BY CHANGING VALUE TO 1, WE WILL
//* GET A READOUT OF ONE PER 2 SECS.
//*
//SYSIN DD *
       TITLE 'ERBTRACE - REGISTER USAGE AND EQUATES'
ERBTRACE CSECT
R0       EQU   0 ADDRESS OF FIELD LOCATED BY EXIT
R1       EQU   1 PARAM LIST PTR
R2       EQU   2 WORK
R3       EQU   3 WORK
R4       EQU   4 WORK
R5       EQU   5 WORK
R6       EQU   6 OUCB PTR
R7       EQU   7 ASCB PTR
R8       EQU   8 OUXB PTR
R9       EQU   9 PVT PTR
RA       EQU   10 CVT PTR
RB       EQU   11 BASE REG
RC       EQU   12 INTERNAL BAL
RD       EQU   13 S-A POINTER
RE       EQU   14 RETURN ADDR
RF       EQU   15 RETURN CODE
NOTOREND EQU   B'1010'  TRT NOT FOUND OR FOUND AT END
FOUNDMID EQU   B'0100'  TRT FOUND BEFORE END
FOUND    EQU   B'0110'  TRTFOUND BEFORE OR AT END
FOUNDEND EQU   B'0010'  TRT FOUND ONLY AT END
NOTFOUND EQU   B'1000'  TRT NOT FOUND IN STRING
         TITLE 'ERBTRACE - INITIALIZATION'
         USING *,RF
RENTLINK EQU   *
         B     LOADBASE
         CNOP  0,8
         DC    C'ERBTRACE07/17/79'
LOADBASE EQU   *
         STM   RE,RC,12(RD)
         LR    RB,RF        SETUP 11 AS BASE REG ALIGNED WITH RENTLINK
         USING RENTLINK,RB
         DROP  RF
         B     START
RETURN0  EQU   *
         SLR   RF,RF SET OK RETCODE - NOTE - REG 13 MUST BE POINTING TO
*  CALLER'S SAVE AREA TO USE EITHER OF THESE RETURN POINTS
RETURN   EQU   *
         L     RE,12(,RD)  SET RETURN ADDRESS
         LM    R0,RC,20(RD)  RESTORE CALLER'S REGS
         BR    RE
         EJECT
BINZEROS DC    14F'0'           FOR INITIALIZING COUNTERS
START    EQU   *
         LM    R2,RA,BINZEROS   CLEAN ALL BITS OUT
         LM    RE,R0,BINZEROS
*
********************
* DETERMINE WHETHER NAME PASSED FROM RMF IS KNOWN TO THIS EXIT.
* FOR PROCESSING PURPOSES THESE USER FIELDS ARE GROUPED BY THE
* CONTROL BLOCKS THEY APPEAR IN. FOLLOWING CODE DETERMINES BLOCKS
* NEEDED AND BRANCHES TO APPROPRIATE CODE.
********************
*
         LM    R3,R5,NAMETABL  INIT LOOP CTL
         L     R2,0(,R1)      PTR TO FIELD NAME PASSED FROM RMF
CHKNAME  EQU   *
         CLC   0(8,R2),0(R3)  CHK AGAINST KNOWN NAMES
         BE    NAMFOUND
         BXLE  R3,R4,CHKNAME
NONAME   EQU   *
NOOUCB   EQU   *
         LA    RF,4           INDICATE USER NAME UNKNOWN TO EXIT
         B     RETURN
         CNOP  0,4
FIELDNMS DC    CL8'PVTCHUIC',A(PVT1)  SYSTEM COMMON AREA HI UIC
         DC    CL8'PVTAFCLO',A(PVT2)  SYSTEM AFC LOW-THRESHOLD
         DC    CL8'PVTAFCOK',A(PVT3)  SYSTEM AFC HI-THRESHOLD
         DC    CL8'CICSIOSM',A(OUCB1) EXCP COUNT FOR ADDR SPACE
         DC    CL8'CICSIOC ',A(OUCB2) INTERVAL I/O SERVICE UNITS
         DC    CL8'CICSCPU ',A(OUCB3) INTERVAL CPU SERVICE UNITS
         DC    CL8'CICSMSO ',A(OUCB4) INTERVAL MSO SERVICE UNITS
         DC    CL8'CICSFMCT',A(ASCB1) CURRENT FRAMES OWNED BY ADDR SP
         DC    CL8'CICSUIC ',A(OUXB0) CURRENT HI-UIC FOR ADDRESS SPACE
         DC    CL8'CICSPIN ',A(OUXB1) PRIVATE PAGE-IN COUNT FOR ADDR SP
         DC    CL8'CICSPOUT',A(OUXB2) PRIVATE PAGE-OUT COUNT FOR AD SP
         DC    CL8'CICSPREC',A(OUXB3) PRIVATE PAGE-RECLAIM COUNT AD SP
         DC    CL8'CICSCAPI',A(OUXB4) COMMON-AREA PAGE-IN COUNT AD SP
         DC    CL8'CICSCAPR',A(OUXB5) COMMON-AREA PAGE-OUT COUNT AD SP
         DC    CL8'CICSSTCT',A(OUXB6) PAGES-STOLEN COUNT FOR ADDR SP
         DC    CL8'CICSSTC ',A(OUXB7) INTERVAL STEAL COUNT FOR AD SP
         DC    CL8'CICSJBS ',A(OUXB8) SESSION SERVICE UNIT COUNT FOR AS
         DC    CL8'CICSTRS ',A(OUXB9) TRANSACTION S-U COUNT FOR AD SP
NAMESEND EQU   *
NAMETABL DC    A(FIELDNMS),F'12',A(NAMESEND)  LOOP CNTL
NAMFOUND EQU   *
         L     RC,8(,R3)      POINT LINK REG TO CORRECT ROUTINE
         BR    RC             AND BRANCH TO IT
         TITLE 'ERBTRACE - PROCESS PVT FIELDS'
PVT1     EQU   *
         L     RA,16(,R0)     CVT PTR
         USING CVT,RA
         L     R9,CVTPVTP     PVT PTR
         USING PVT,R9
         LA    R0,PVTCHUIC
         B     RETANSWR
PVT2     EQU   *
         L     RA,16(,R0)     CVT PTR
         USING CVT,RA
         L     R9,CVTPVTP     PVT PTR
         USING PVT,R9
         LA    R0,PVTAFCLO
         B     RETANSWR
PVT3     EQU   *
         L     RA,16(,R0)     CVT PTR
         USING CVT,RA
         L     R9,CVTPVTP     PVT PTR
         USING PVT,R9
         LA    R0,PVTAFCOK
         B     RETANSWR
         TITLE 'ERBTRACE - PROCESS OUXB FIELDS'
OUXB0    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBUIC
         B     RETANSWR
OUXB1    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBPIN
         B     RETANSWR
OUXB2    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBPOUT
         B     RETANSWR
OUXB3    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBPREC
         B     RETANSWR
OUXB4    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBCAPI
         B     RETANSWR
OUXB5    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBCAPR
         B     RETANSWR
OUXB6    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBSTCT
         B     RETANSWR
OUXB7    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBSTC
         B     RETANSWR
OUXB8    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBJBS
         B     RETANSWR
OUXB9    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUXB,R8
         LA    R0,OUXBTRS
         B     RETANSWR
         TITLE 'ERBTRACE - PROCESS ASCB FIELDS'
ASCB1    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING ASCB,R7
         LA    R0,ASCBFMCT
         B     RETANSWR
         TITLE 'ERBTRACE - PROCESS OUCB FIELDS'
OUCB1    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUCB,R6
         LA    R0,OUCBIOSM
         B     RETANSWR
OUCB2    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUCB,R6
         LA    R0,OUCBIOC
         B     RETANSWR
OUCB3    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUCB,R6
         LA    R0,OUCBCPU
         B     RETANSWR
OUCB4    EQU   *
         BAL   RC,FINDBLKS    FIND MAJOR CTL BLOCKS
         USING OUCB,R6
         LA    R0,OUCBMSO
         B     RETANSWR
         TITLE 'ERBTRACE - LOCATE MAIN CTL BLOCKS NEEDED FOR CICS'
FINDBLKS EQU   *
         L     RA,16(,R0)     CVT PTR
         USING CVT,RA
         L     R6,CVTOPCTP    RMCT PTR
         USING RMCT,R6
         L     R6,RMCTINQE    OUCB IN Q HDR
         LR    R5,R6          SAVE TO CHECK FOR END OF OUCB INQ CHAIN
         USING OUCB,R6
STEPOUCB EQU   *
         L     R6,OUCBFWD     PT TO NEXT OUCB SWAPPED IN
         CR    R5,R6          ARE WE BACK AT HEADER?
         BE    NOOUCB         YES, CANT FIND OUCB FOR JOB WANTED
         L     R7,OUCBASCB    FIND INITIATED JOB NAME
         USING ASCB,R7
         L     R2,ASCBJBNI
         CLC   =C'CICS0000',0(R2) IS IT CICS?
         BNE   STEPOUCB
         L     R8,ASCBOUXB    YES, SET OUXB PTR
         BR    RC
         TITLE 'ERBTRACE - SETUP POINTERS FOR RETURN TO RMF'
RETANSWR EQU   *
         L     R1,4(,R1)      POINT TO RMF'S ANSWER FIELD
         ST    R0,0(,R1)      AND STORE ITS ADDRESS IN ANSW FIELD
         B     RETURN0        RETURN AND INDICATE GOOD USER FIELD
         TITLE 'ERBTRACE - CONSTANTS AND LITERALS'
         DS    0D
TEMPPRMS DC    A(TEMPNAME)    TESTING ONLY
         DC    A(TEMPANS)     TESTING ONLY
TEMPNAME DC    C'SYSTFLTS'
TEMPANS  DC    F'0'
         LTORG
         TITLE 'ERBTRACE - ASCB MAP'
         IHAASCB DSECT=YES
         TITLE 'ERBTRACE - CSD MAP'
CSD      DSECT
CSDCSD   DS    CL4
CSDCPUJS DS    XL2
CSDCHAD  DS    XL2
CSDCPUAL DS    XL2
         TITLE 'ERBTRACE - CVT MAP'
         CVT   DSECT=YES
         TITLE 'ERBTRACE - OUCB MAP'
         IRAOUCB DSECT=YES
         TITLE 'ERBTRACE - OUXB MAP'
         IHAOUXB DSECT=YES
         TITLE 'ERBTRACE - PCCA MAP'
PCCA     DSECT
PCCAPCCA DS    CL4
PCCACPID DS    CL12
PCCACPUA DS    XL2
PCCACAFM DS    XL2
PCCATQEP DS    F
PCCAPSAV DS    F
         TITLE 'ERBTRACE - PSA MAP'
PSA      DSECT
PSASYSTM DS    XL1536         LEFT TO SYSTEM
PSATRACE DS    D              FOR FOOTPRINT INDICATING OK TO USE
PSASAVE  DS    9D             FOR SAVE AREA
PSAPARMS DS    0D             PARAM LIST PASSED BY RMF
PSAFIELD DS    F              PTR TO NAME OF FIELD TO BE TRACED
PSAANSWR DS    F              PTR TO RMF'S ANSWER AREA
PSAPVT1T DS    F              TIME OF DAY PREV INVOCATION OF PVT1 RTNE
PSASTAMP DS    0D             TIME STAMP FOR CURRENT CALL
PSATIME  DS    F              TIME OF DAY
PSADATE  DS    F              YEAR AND DAY
PSAOLTIM DS    F              BEGINNING OF INTERVAL FOR CURR FIELD
PSAINTVL DS    F              INTERVAL LENGTH IN .01 SECONDS
PSAFRATE DS    F              PAGEFAULT RATE
PSANPIN  DS    F              PRIVATE PAGEINS
         DS    2F             AND ITS TIMESTAMP
PSANPREC DS    F              PRIVATE RECLAIMS
         DS    2F             AND ITS TIMESTAMP
PSACAIN  DS    F              COMMON PAGEINS
         DS    2F             AND ITS TIMESTAMP
PSACAREC DS    F              COMMON RECLAIMS
         DS    2F             AND ITS TIMESTAMP
PSAZEROS DS    F              FIELD FOR REPORTING NULL VALUE
         TITLE 'ERBTRACE - PVT MAP'
         IHAPVT DSECT=YES
         TITLE 'ERBTRACE - RMCT MAP'
RMCT     DSECT
RMCTSYS1 DS    3F
RMCTMCT  DS    F
RMCTSYS2 DS    24F
RMCTINQE DS    F
         END
//SMPPTFIN DD *
++USERMOD (SM00011).
++VER (Z038) FMID(HRM2204)  /* SPECIAL RMF TRACING NAMES */.
++MOD (ERBTRACE) DISTLIB(RMFMOD01) LKLIB(LOADLIB).
++ZAP (ERBMFTTB) DISTLIB(RMFMOD01).
NAME ERBMFTTB
VER 0000 003C               SAMPLES PER SET = 60
VER 02A0 020040D7,C1E3C3C8  CHECK PATCH AREA
REP 0000 0001               SAMPLES PER SET = 1
REP 02A0 02D7E5E3,C3C8E4C9,C308DC02,D7E5E3C1  PVTCHUIC,PVTAFCLO
REP 02B0 C6C3D3D6,08DC04D7,E5E3C1C6,C3D6D208  PVTAFCOK
REP 02C0 DC04C3C9,C3E2C9D6,E2D408DC,04C3C9C3  CICSIOSM,CICSIOC
REP 02D0 E2C9D6C3,4007DC04,C3C9C3E2,C3D7E440  CICSCPU
REP 02E0 07DC04C3,C9C3E2D4,E2D64007,DC04C3C9  CICSMSO,CICSFMCT
REP 02F0 C3E2C6D4,C3E308DC,02C3C9C3,E2D7C9D5  CICSPIN
REP 0300 4007DC04,C3C9C3E2,D7D6E4E3,08DC04C3  CICSPOUT,CICSPREC
REP 0310 C9C3E2D7,D9C5C308,DC04C3C9,C3E2C3C1  CICSCAPI
REP 0320 D7C908DC,04C3C9C3,E2C3C1D7,D908DC04  CICSCAPR
REP 0330 C3C9C3E2,E2E3C3E3,08DC04C3,C9C3E2E2  CICSSTCT,CICSSTC
REP 0340 E3C34007,DC02C3C9,C3E2D1C2,E24007DC  CICSJBS,
REP 0350 04C3C9C3,E2E3D9E2,4007DC04,C3C9C3E2  CICSTRS,CICSUIC
REP 0360 E4C9C340,07DC0200
