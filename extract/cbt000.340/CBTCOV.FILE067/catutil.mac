 PROC 0 DEBUG
 /* THIS CLIST IS USED BY THE 'CATUTIL' (H.1) OPTION OF SPF */

 /* COPYRIGHT HUGHES AIRCRAFT COMPANY 1983                  */
 /* ALL RIGHTS RESERVED.  THIS CLIST MAY NOT BE USED OR     */
 /* REPRODUCED WITHOUT THE EXPRESS PERMISSION OF THE AUTHOR */
 /*                              LEONARD D. WOREN           */
 /*                              BLDG S50  MS X303          */
 /*                              PO BOX 92919               */
 /*                              LOS ANGELES, CA  90009     */

 IF &DEBUG = DEBUG THEN CONTROL LIST SYMLIST CONLIST
 CONTROL NOMSG NOFLUSH
 ISPEXEC CONTROL ERRORS RETURN
 ISPEXEC TBEND CATUTIL
 ISPEXEC CONTROL ERRORS CANCEL
 CONTROL MSG FLUSH
 ISPEXEC VGET (CATUTLV) PROFILE
 SET &CC = &LASTCC
 IF &CC = 8 THEN DO
      SET &CATUTLV = &SYSPREF
      SET &CC = 0
      END
 IF &CC ^= 0 THEN DO
      WRITE *** VGET #1 FAILED IN CATUTIL CLIST (RC = &CC) ***
      EXIT CODE(24)
      END
 SET &LEVEL = &CATUTLV
 SET &MSG =
 TOP:  +
 SET &OPT =
 SET &CURPOS = LEVEL
 TOP2:  +
 ISPEXEC DISPLAY PANEL(CATUTIL1) &MSG CURSOR(&CURPOS)
 IF &LASTCC = 8 THEN EXIT
 SET &CURPOS = LEVEL
 IF &STR(&OPT) =   THEN GOTO OPTOK1
 IF &STR(&OPT) = END THEN EXIT
 SET &RETURN1 = OPTOK1   /* SET NORMAL RETURN ADDRESS */
 SET &RETERR1 = TOP2     /* SET ERROR  RETURN ADDRESS */
 SET &CURPOS = OPT       /* FIELD TO PUT CURSOR IN IF ERROR */
 GOTO EVALOPT            /* EVALUATE OPT       */
 OPTOK1:  SET &OPT =
 SET &MSG =
 CONTROL NOFLUSH NOMSG
 FREE F(CATLIST #CATLIST)
 CONTROL MSG
 ATTR #CATLIST RECFM(V B A) LRECL(137) BLKSIZE(141)
 ALLOC F(CATLIST) NEW UNIT(VIO) USING(#CATLIST) +
       AVBLOCK(137) SP(3000 1000) RELEASE
 CONTROL NOMSG   /* TRY TO SUPPRESS EMPTY INDEX LEVEL ERROR MSG       --
                    THIS IS SOMEWHAT RISKY... (KILLS ALL MESSAGES) */
 IF &DEBUG = TIME THEN DO
      TIME
      CONTROL LIST
      END
 ISPEXEC CONTROL DISPLAY LOCK
 ISPEXEC DISPLAY PANEL(CATUTIL1) MSG(CATUT010)
 LISTCAT NONVSAM LEVEL(&LEVEL) ALL OUTFILE(CATLIST)
 IF &DEBUG = TIME THEN DO
      CONTROL NOLIST
      TIME
      END
 CONTROL MSG
 SET &CATUTLV = &LEVEL
 ISPEXEC VPUT (CATUTLV) PROFILE
 ISPEXEC TBCREATE CATUTIL NAMES(UNIT VOL DSN FSEQN) NOWRITE
 ISPEXEC CONTROL DISPLAY LOCK
 ISPEXEC DISPLAY PANEL(CATUTIL1) MSG(CATUT011)
 OPENFILE CATLIST INPUT
 ATTN GOTO ATTN
 ERROR DO
      SET &CC = &LASTCC
      CONTROL MSG   /* RESTORE AFTER CONTROL NOMSG PRECEDING GETFILE */
      GOTO ERROR
      END
 SET &DSNUM = 0
 LOOP1:  +
 CONTROL NOMSG   /* SUPPRESS EOF MSG */
 GETFILE CATLIST
 CONTROL MSG
 SET &CATLIST = &STR(&CATLIST                                          -
)
 IF &DEBUG = LIST THEN +
   WRITE LOOP1          ========> &SUBSTR(2:30,&CATLIST) <========
 SET &DSN = ?
 SET &VOL = ?
 SET &UNIT = ?
 CHECK1:  +
 IF &SUBSTR(1:24,&CATLIST) = 1IDCAMS  SYSTEM SERVICES THEN DO
      CONTROL NOMSG   /* SUPPRESS EOF MSG */
      GETFILE CATLIST
      CONTROL MSG
      SET &CATLIST = &STR(&CATLIST                                     -
)
      IF &DEBUG = LIST THEN +
        WRITE   CHECK1       ========> &SUBSTR(2:30,&CATLIST) <========
      END
 /* &SUBSTR(2:9,&CATLIST) = IDC0002I THEN GOTO EOF
 IF &SUBSTR(2:10,&CATLIST) = &STR(NONVSAM -) THEN DO
      SET &DSN = &SUBSTR(18:61,&CATLIST)
      FINDVOL:  +
      CONTROL NOMSG   /* SUPPRESS EOF MSG */
      GETFILE CATLIST
      CONTROL MSG
      SET &CATLIST = &STR(&CATLIST                                     -
)
      IF &SUBSTR(1:24,&CATLIST) = 1IDCAMS  SYSTEM SERVICES THEN DO
           IF &DEBUG = LIST THEN +
             WRITE     FINDVOL:  ** IDCAMS PAGE HEADER IGNORED **
           GOTO FINDVOL
           END
      IF &DEBUG = LIST THEN +
        WRITE     FINDVOL    ========> &SUBSTR(2:30,&CATLIST) <========
      IF &SUBSTR(2:2,&CATLIST) ^=   THEN GOTO CHECK1
      IF &SUBSTR(9:15,&CATLIST) = &STR(VOLSER-) THEN DO
           SET &VOL = &SUBSTR(27:32,&CATLIST)
           SET &UNIT = &SUBSTR(53:60,&CATLIST)
           IF &UNIT = 3050200D THEN SET &UNIT = &STR(3330-1)
                               ELSE +
           IF &UNIT = 3010200E THEN SET &UNIT = 3380
                               ELSE +
           IF &UNIT = 30502009 THEN SET &UNIT = 3350
                               ELSE +
           IF &UNIT = 32008003 THEN SET &UNIT = &STR(3400-6)
                               ELSE +
           IF &UNIT = 00000000 THEN SET &UNIT =
           IF &SUBSTR(67:72,&CATLIST) = &STR(FSEQN-) THEN DO
                SET &FSEQN = &SUBSTR(87:90,&CATLIST)
                IF &STR(&FSEQN) = &STR(---0) THEN SET &FSEQN =
                                             ELSE DO
                     SET &I = 1
                     DASHLP:  IF &SUBSTR(&I,&FSEQN) = &STR(-) THEN DO
                          SET &FSEQN = &SUBSTR(1:&I,0000)+
                                       &SUBSTR(&I+1:4,&FSEQN)
                          SET &I = &I + 1
                          GOTO DASHLP
                          END
                     END
                END
                                                     ELSE SET &FSEQN =
           IF &DEBUG = LIST THEN WRITE DSN=&DSN VOL=&VOL UNIT=&UNIT....
           ISPEXEC TBADD CATUTIL
           SET &DSNUM = &DSNUM + 1
           GOTO LOOP1   /* FIND NEXT NONVSAM */
           END
      GOTO FINDVOL /* KEEP LOOKING FOR VOLUME LINE */
      END
 GOTO LOOP1 /* FIND NEXT NONVSAM ENTRY */
 ERROR:  +
 IF &CC = 400 THEN GOTO EOF    /* EOF ON GETFILE */
 WRITE *** ERROR OCCURRED IN CLIST -- CODE = &CC ***
 EXIT CODE(999)
 EOF:  +
 CLOSFILE CATLIST
 ATTN OFF
 ERROR OFF
 FREE F(CATLIST #CATLIST)
 /***ISPEXEC TBTOP CATUTIL
 SET &TOPROW = 0
 LOOP2:  +
 ISPEXEC TBQUERY CATUTIL ROWNUM(ROWNUM)
 IF &ROWNUM = 0 THEN DO
      SET &MSG = MSG(CATUT004)
      GOTO TOP
      END
 SET &SELECT =
 ISPEXEC TBTOP CATUTIL
 ISPEXEC TBSKIP CATUTIL NUMBER(&TOPROW)
 ISPEXEC TBDISPL CATUTIL PANEL(CATUTIL2) &MSG
 SET &DISPLCC = &LASTCC
 SET &MSG =
 LOOP2X: +
 IF &DISPLCC = 8 THEN DO
      LEAVE:  +
      ISPEXEC TBEND CATUTIL
      SET &CC = &LASTCC
      IF &CC ^= 0 THEN WRITE TBEND RC = &CC
      GOTO TOP
      END
 SET &TOPROW = &ZTDTOP    /* SAVE NUMBER OF TOP ROW ON SCREEN */
 IF &DISPLCC > 4 THEN WRITE TBDISPL RC = &DISPLCC
 IF &STR(&OPT) =   THEN GOTO OPTOK2
 IF &STR(&OPT) = END THEN GOTO LEAVE
 SET &RETURN1 = OPTOK2   /* SET NORMAL RETURN ADDRESS */
 SET &RETERR1 = LOOP2    /* SET ERROR  RETURN ADDRESS */
 GOTO EVALOPT            /* EVALUATE OPT       */
 OPTOK2:  SET &OPT =
 IF &SELECT =   THEN GOTO LOOP2        /* PRIMARY LINE CMD ENTERED */
 SET &DSN = &EVAL(&DSN)   /* STRIP OFF TRAILING BLANKS */
 IF &STR(&VOL) ^= MIGRAT THEN DO
      #DSORG '&DSN'
      SET &DSORGCC = &LASTCC
      IF &DSORGCC = 0 THEN DO
           SET &MSG = MSG(CATUT001)
           SET &OOPSDSN = &DSN
           GOTO LOOP2
           END
      END
 IF &SELECT = A THEN SET &WTG = FUNCA  /* ATTR   */
                ELSE +
 IF &SELECT = B THEN SET &WTG = FUNCB  /* BROWSE */
                ELSE +
 IF &SELECT = C THEN SET &WTG = FUNCC  /* COMPRESS */
                ELSE +
 IF &SELECT = D THEN SET &WTG = FUNCD  /* DELETE */
                ELSE +
 IF &SELECT = E THEN SET &WTG = FUNCE  /* EDIT   */
                ELSE +
 IF &SELECT = K THEN SET &WTG = FUNCC  /* COMPRESS (SHR) */
                ELSE +
 IF &SELECT = R THEN SET &WTG = FUNCR  /* RENAME */
                ELSE DO
      SET &MSG = MSG(CATUT000)
      SET &FUNC = &SELECT
      GOTO LOOP2
      END
 ISPEXEC CONTROL ERRORS RETURN
 GOTO &WTG
 FUNCA:  ISPEXEC DISPLAY LINE START(5)
 DSAT '&DSN' FULL EXTENTS
 GOTO COMMON
 FUNCB:  ISPEXEC BROWSE DATASET('&DSN')
 SET &CC = &LASTCC
 GOTO COMMON
 FUNCC:  SET &OOPSDSN = &DSN
 IF &DSORGCC ^= 2 THEN DO
      ISPEXEC CONTROL ERRORS CANCEL
      SET &MSG = MSG(CATUT008)
      GOTO LOOP2
      END
 CONTROL NOMSG
 FREE F(SYSIN SYSPRINT C SYSUT1 SYSUT2 SYSUT3 SYSUT4)
 CONTROL MSG
 IF &SELECT = C THEN ALLOC F(C) OLD DA('&DSN')
                ELSE ALLOC F(C) SHR DA('&DSN')
 ALLOC F(SYSPRINT) DUMMY
 ALLOC F(SYSIN) NEW UNIT(VIO)
 OPENFILE SYSIN OUTPUT
 SET &SYSIN = &STR( COPY INDD=C,OUTDD=C,LIST=NO)
 PUTFILE SYSIN
 CLOSFILE SYSIN
 SET &DSATUSED = XXXXXX          /* CREATE CLIST VARIABLE */
 SET &DSATDEVTYPE = XXXXXX       /* CREATE CLIST VARIABLE */
 DSAT '&DSN' SAVE NOLIST
 /*CALL 'SYS1.LINKLIB(IEBCOPY)' 'SIZE=350K'
 /*$ SPFCOPY 'SIZE=350K'
 $ PDSFAST 'SIZE=350K'
 SET &CC = &LASTCC
 FREE F(SYSIN SYSPRINT)
 ALLOC F(SYSIN) DA(*)
 ALLOC F(SYSPRINT) DA(*)
 CONTROL NOMSG
 IF &SELECT = C THEN FREE DA('&DSN')
                ELSE FREE F(C)
 CONTROL MSG
 IF &CC ^= 0 THEN DO
      SET &MSG = MSG(CATUT005)
      GOTO COMMON
      END
 IF &STR(&DSATDEVTYPE) = 3380         THEN SET &TRKCYL = 15
                                      ELSE +
 IF &STR(&DSATDEVTYPE) = &STR(3330-1) THEN SET &TRKCYL = 19
                                      ELSE +
 IF &STR(&DSATDEVTYPE) = 3350         THEN SET &TRKCYL = 30
                                      ELSE DO
      SET &MSG = MSG(CATUT006)
      GOTO COMMON
      END
 SET &OLDCYL = &SUBSTR(1:3,&DSATUSED)
 IF &OLDCYL =    THEN SET &OLDCYL = 0
 SET &OLDTRK = &SUBSTR(5:6,&DSATUSED)
 SET &OLDTRK = &OLDCYL * &TRKCYL + &OLDTRK
 DSAT '&DSN' SAVE NOLIST
 SET &NEWCYL = &SUBSTR(1:3,&DSATUSED)
 IF &NEWCYL =    THEN SET &NEWCYL = 0
 SET &NEWTRK = &SUBSTR(5:6,&DSATUSED)
 SET &NEWTRK = &NEWCYL * &TRKCYL + &NEWTRK
 SET &OLDTRK = &OLDTRK - &NEWTRK    /* TOTAL TRACKS AMOUNT FREED */
 /*T &NEWCYL = &OLDTRK / &TRKCYL    /* CYLINDERS FREED */
 /*T &NEWTRK = &OLDTRK // &TRKCYL   /* LEFTOVER TRACKS FREED */
 SET &MSG = MSG(CATUT007)
 GOTO COMMON
 FUNCD:  ISPEXEC DISPLAY PANEL(CATUTILD)
 SET &CC = &LASTCC
 IF &CC = 8 THEN GOTO LOOP2
 ISPEXEC CONTROL DISPLAY LINE START(6)
 DELETE '&DSN'
 ISPEXEC TBDELETE CATUTIL
 SET &DSNUM = &DSNUM - 1
 GOTO COMMON
 FUNCE:  ISPEXEC EDIT   DATASET('&DSN')
 SET &CC = &LASTCC
 GOTO COMMON
 FUNCR:  SET &OLDDSN = &DSN
 SET &DSN = '&DSN'
 ISPEXEC DISPLAY PANEL(CATUTILR) CURSOR(DSN)
 SET &CC = &LASTCC
 IF &CC = 8 THEN GOTO LOOP2
 /*ISPEXEC CONTROL DISPLAY LINE START(5)
 RENAME '&OLDDSN' &DSN
 IF &SUBSTR(1:1,&DSN) = ' THEN +
   SET &DSN = &SUBSTR(2:&LENGTH(&DSN)-1,&DSN)
                          ELSE SET &DSN = &SYSPREF..&DSN
 ISPEXEC TBPUT CATUTIL
 COMMON:  +
 ISPEXEC CONTROL ERRORS CANCEL
 IF &CC <= 4 THEN DO
      IF &DISPLCC = 4 THEN DO
           ISPEXEC TBDISPL CATUTIL
           SET &DISPLCC = &LASTCC
           GOTO LOOP2X
           END
      GOTO LOOP2
      END
 SET &OOPSDSN = &DSN
 SET &MSG = MSG(CATUT009)
 GOTO LOOP2
 ATTN:  +
 CLOSFILE CATLIST
 ATTN OFF
 FREE F(CATLIST #CATLIST)
 EXIT

 /********************************************************************/
 /*** SUBROUTINE TO EVALUATE &OPT AND PERFORM ANY REQUESTED ACTION ***/
 /********************************************************************/
 EVALOPT:  +
 IF &LENGTH(&STR(&OPT)) < 7 THEN GOTO BADOPT
 IF &SUBSTR(1:6,&STR(&OPT)) ^= &STR(DEBUG ) THEN GOTO BADOPT
 IF &STR(&OPT) = &STR(DEBUG OFF) THEN DO
      CONTROL NOLIST NOSYMLIST NOCONLIST
      SET &DEBUG =
      GOTO &RETURN1
      END
 IF &STR(&OPT) = &STR(DEBUG ON) THEN DO
      CONTROL LIST SYMLIST CONLIST
      SET &DEBUG = LIST
      GOTO &RETURN1
      END
 IF &STR(&OPT) = &STR(DEBUG TIME) THEN DO
      CONTROL NOLIST NOSYMLIST NOCONLIST
      SET &DEBUG = TIME
      GOTO &RETURN1
      END
 IF &STR(&OPT) = &STR(DEBUG LIST) THEN DO
      CONTROL LIST
      SET &DEBUG = LIST
      GOTO &RETURN1
      END
 IF &STR(&OPT) = &STR(DEBUG SYMLIST) THEN DO
      CONTROL SYMLIST
      GOTO &RETURN1
      END
 IF &STR(&OPT) = &STR(DEBUG CONLIST) THEN DO
      CONTROL CONLIST
      GOTO &RETURN1
      END
 BADOPT:  +
 SET &MSG = MSG(CATUT003)
 GOTO &RETERR1
