PROC 0 T P DEBUG LOG
/********************************************************************/
/* LAST UPDATED 06/04/82 - HOWARD M. DEAN                           */
/* LAST UPDATED 05/09/83 - HOWARD M. DEAN                           */
/********************************************************************/
IF &DEBUG EQ DEBUG THEN CONTROL LIST MSG CONLIST SYMLIST FLUSH PROMPT
          ELSE CONTROL NOPROMPT NOMSG NOFLUSH
IF &T NE AND &P NE THEN                                                -
   DO
     WRITE
     WRITE ** SPECIFY TARGET (T) OR PRODUCTION (P) SMP DATASETS - NOT  -
BOTH **
     WRITE
     CONTROL FLUSH
     EXIT QUIT
   END
SET &CC EQ TARGET
IF  &P  EQ P THEN SET &CC EQ PRODUCTION
WRITE ENTER SMP UPDATE/INQUIRY CONTROL STATEMENTS FOR THE &CC. SYSTEM
WRITE AFTER THE KEYBOARD UNLOCKS:
WRITE
IF &CC EQ PRODUCTION THEN                                              -
   DO
       SET &CC EQ P
       SET &VC EQ PRD
       SET &DL EQ &STR(02)
   END
ELSE                                                                   -
   DO
       SET &CC EQ T
       SET &VC EQ TRG
       SET &DL EQ &STR(01)
   END
FREE  F(SYSIN SYSPRINT SMPCDS SMPSCDS SMPCRQ SMPPTS SMPLOG SMPACDS)
FREE  F(SMPACRQ SMPMTS SMPSTS SMPOUT SMPRPT SMPLIST SMPCNTL)
IF &LOG EQ LOG THEN                                                    -
      ALLOC F(SMPLOG)  DA('SYS&CC..SMPLOG') MOD
ELSE  ALLOC FI(SMPLOG) DUMMY
ALLOC F(SMPACDS)  DA('SYS1.SMPACDS')    UNIT(DISK)    VOL(DLIB&DL) SHR
ALLOC F(SMPACRQ)  DA('SYS1.SMPACRQ')    UNIT(DISK)    VOL(DLIB&DL) SHR
ALLOC F(SMPCDS)   DA('SYS&CC..SMPCDS')  UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPSCDS)  DA('SYS&CC..SMPSCDS') UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPCRQ)   DA('SYS&CC..SMPCRQ')  UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPMTS)   DA('SYS&CC..SMPMTS')  UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPSTS)   DA('SYS&CC..SMPSTS')  UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPPTS)   DA('SYS&CC..SMPPTS')  UNIT(DISK)    VOL(SMP&VC)  SHR
ALLOC F(SMPOUT)   DA(*)
ALLOC F(SYSIN)    DA(*)
ALLOC F(SMPRPT)   DA(*)
ALLOC F(SMPLIST)  DA(*)
ALLOC F(SYSPRINT) DA(*)
ALLOC F(SMPCNTL)  DA(*)
XEQ   HMASMP      /* CALL THE SMP PROCESSOR */
FREE  F(SMPCDS  SMPSCDS SMPCRQ SMPPTS SMPLOG SMPACDS)
FREE  F(SMPACRQ SMPMTS  SMPSTS SMPOUT SMPRPT SMPLIST SMPCNTL)
