         /*  CLIST WRITTEN BY: CAROL WINICUR        */
         /*                    GREAT WESTERN BANK   */
         /*                    9401 CORBIN AVE.     */
         /*                    NORTHRIDGE, CA 91328 */
         /*                    (818) 701-8309       */
         /*                                         */
         PROC 1 DDNAME DEBUG
         CONTROL NOMSG NOLIST
         IF &DEBUG = DEBUG   THEN  +
            CONTROL LIST CONLIST SYMLIST MSG
         SET &DDLN = &LENGTH(&DDNAME) + 2
         SET &SW=OFF
         SET &ANY=NONE
         SET SYSOUTTRAP=500
         LISTA ST
         SET &END=&SYSOUTLINE
         SET &BEGIN=1
         DO WHILE &BEGIN LE &END
            SET CMDLIN = &&SYSOUTLINE&BEGIN
            SET INDENT = &STR(&SUBSTR(1:2,&CMDLIN))
            IF &STR(&INDENT) NE &STR(  ) THEN DO
               SET SAVDSN = &STR(&CMDLIN)
               GOTO UPIT
               END
            IF &SUBSTR(3:&DDLN,&CMDLIN) = &DDNAME  THEN DO
                SET SW=ON
                WRITE &DDNAME
                SET ANY=ANY
                GOTO CONCATL
                END
            IF &SW = OFF THEN GOTO UPIT
            IF &SUBSTR(3:3,&CMDLIN) NE &STR( ) THEN +
                SET SW=OFF
            CONCATL: +
            IF &SW = ON THEN +
               WRITE    &SAVDSN
            UPIT: SET &BEGIN=&BEGIN+1
            END
         IF &ANY = NONE   +
            THEN WRITE &DDNAME NOT FOUND
