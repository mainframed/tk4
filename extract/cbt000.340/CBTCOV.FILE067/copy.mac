PROC 0 DEBUG
IF &DEBUG = DEBUG THEN CONTROL MSG LIST CONLIST SYMLIST PROMPT NOFLUSH
                  ELSE CONTROL NOMSG
ISPEXEC VGET (IDSN IDISP ODSN ODISP POPT PLIST)
     ISPEXEC SELECT PGM(DSNINFO) PARM(IDSN)
         IF &POPT = SUBMIT THEN GOTO BATCH
                     ISPEXEC VGET (DSORG ATRECFM BLKSIZE LRECL RECFM   -
MEMBER DIRALLOC)
FREE ATTR(CAT) FILE(SYSUT1 SYSUT2  SYSPRINT)
IF &ODISP = NEW THEN DO
                     DEL &ODSN
                     CONTROL MSG
                     ISPEXEC DISPLAY PANEL(COPYNEW)
                     IF &LASTCC >0 THEN EXIT
                     ATTR CAT RECFM(&ATRECFM) BLKSIZE(&BLKSIZE)        -
LRECL(&LRECL)
                     IF &ALLUNIT = TRK THEN SET &ALLUNIT = T
                     IF &DEVTYPE = THEN SET &DEVTYPE = SYSALLDA
                     IF &VOLSER = THEN DO
                                       ALLOC F(SYSUT2) DS(&ODSN) NEW   -
SPA(&PRIM,&SEC) &ALLUNIT USI(CAT) DIR(&DIRALLOC)
                                       END
                     IF &VOLSER ^= THEN DO
                                        ALLOC F(SYSUT2) DS(&ODSN) NEW  -
SPA(&PRIM,&SEC) &ALLUNIT USI(CAT) VOL(&VOLSER)                         -
                                              UNIT(&DEVTYPE)           -
DIR(&DIRALLOC)
                                        END
                     END
IF &ODISP ^= NEW THEN DO
                      ALLOC F(SYSUT2) DS(&ODSN) SHR
                      END
ALLOC F(SYSUT1) DS(&IDSN) SHR
IF &DIRALLOC = THEN SET &DIRALLOC = 0
IF &DIRALLOC = 0 THEN DO
                      ALLOC F(SYSPRINT) DS(*)
                      $P SYSREPRO
                      GOTO EXIT
                      END
               ELSE DO
                    FREE F(SYSUT3 SYSUT4 SYSIN)
                    ALLOC F(SYSIN) UNIT(SYSVIO) SPA(1) BLOCK(80)
                    OPENFILE SYSIN OUTPUT
                    IF &PLIST = YES THEN SET &SYSIN = &STR( COPY       -
INDD=SYSUT1,OUTDD=SYSUT2 )
                                    ELSE SET &SYSIN = &STR( COPY       -
INDD=SYSUT1,OUTDD=SYSUT2,LIST=NO )
                    PUTFILE SYSIN
                    IF &MEMBER ^= THEN DO
                                       SET &SYSIN = &STR( SELECT       -
MEMBER=&MEMBER)
                                       PUTFILE SYSIN
                                       END
                    CLOSFILE SYSIN
                    ALLOC F(SYSPRINT) DS(*)
                    ALLOC F(SYSUT3) SPA(10) UNIT(SYSDA) CYL
                    ALLOC F(SYSUT4) SPA(10) UNIT(SYSDA) CYL
                    @P IEBCOPY
                    FREE F(SYSUT3 SYSUT4 SYSIN)
                    END
EXIT: +
      FREE F(SYSUT1 SYSUT2 SYSPRINT)
      EXIT
BATCH: +
      IF &ODISP = NEW THEN ISPEXEC DISPLAY PANEL(COPYNEW)
      IF &LASTCC >0 THEN EXIT
      ISPEXEC SELECT PGM(RJOBCARD)
      SET &CIDSN = &STR(&IDSN)
      SET &CODSN = &STR(&ODSN)
      ISPEXEC VPUT (CODSN CIDSN)
      ISPEXEC SELECT PGM(FIXUPDSN) PARM(CIDSN)
      ISPEXEC SELECT PGM(FIXUPDSN) PARM(CODSN)
      ISPEXEC VGET (CIDSN CODSN)
      ISPEXEC FTOPEN TEMP
      ISPEXEC FTINCL COPYJCL
      ISPEXEC FTCLOSE
      ISPEXEC VGET   ZTEMPF
      %GENSUB '&ZTEMPF'
