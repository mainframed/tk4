 PROC 0
 /* THIS CLIST USED BY THE NEW SPF OPTION 3.7                      */

 ISPEXEC VGET (VTOCFL1 VTOCFL2 VTOCOPT) PROFILE
 SET &VTOCLVL = &SYSPREF
 ISPEXEC VPUT (VTOCLVL)

 /* FIRST, DETERMINE WHICH LOGICAL SCREEN WE ARE ON BY LOOKING AT  */
 /* THE NAME OF THE TEMPORARY CNTL DATASET FOR THIS SCREEN         */
 ISPEXEC VGET (ZTEMPF)
 /* CHECK FOR PERMANENT DSNAME OF FORM UID.SPFTEMPN.CNTL           */
 SET &L = &LENGTH(&ZTEMPF)
 IF &SUBSTR(&L-5:&L,&ZTEMPF) = 1.CNTL THEN SET &X = A
                                      ELSE +
 IF &SUBSTR(&L-5:&L,&ZTEMPF) = 2.CNTL THEN SET &X = B
                                      ELSE +
 IF &SUBSTR(34,&ZTEMPF) = 1 THEN SET &X = A  /* CHECK FOR TEMP DSN */
                            ELSE SET &X = B  /* OF FORM &ISPCTLN   */
 CONTROL NOMSG NOFLUSH
 FREE F(VTOCOUT #VTOCOUT)
 IF &SYSPREF = &SYSUID THEN SET &SPFVTOC = SPFTEMP&X..LIST
                       ELSE SET &SPFVTOC = &SYSUID..SPFTEMP&X..LIST
 #DSORG &SPFVTOC
 IF &LASTCC ^= 0 THEN DELETE &SPFVTOC
 CONTROL MSG
 ATTR #VTOCOUT BL(6150)
 ALLOC DA(&SPFVTOC) NEW TR SP(5 5) RELEASE USING(#VTOCOUT) UNIT(SYSDB)
 FREE A(#VTOCOUT)
 /*ATTN GOTO LEAVE
 ERROR GOTO LEAVE
 TOP: +
 ISPEXEC DISPLAY PANEL(VTOCFAST)
 /* IF END OR RETURN KEY PRESSED, RC=8 WILL PASS CONTROL TO ERROR UNIT -
*/
 ISPEXEC VPUT (VTOCFL1 VTOCFL2 VTOCOPT) PROFILE
 IF &VTOCLVL =   THEN SET &L =
                 ELSE SET &L = LEVEL(&VTOCLVL)
 ALLOC DA(&SPFVTOC) F(VTOCOUT) OLD
 VTOC (&VOLUME) &L HEADING(' ') &VTOCOPT &VTOCOPT2 +
      PRINT(NEW (&VTOCFL1 &VTOCFL2))
 FREE F(VTOCOUT)             /* IN CASE OF SPLIT SCREEN */
 ISPEXEC BROWSE DATASET(&SPFVTOC)
 GOTO TOP
 LEAVE: +
 ERROR OFF
 /*ATTN OFF
 CONTROL NOMSG
 FREE F(VTOCOUT)
 /* WRITE OUT VARIABLES AGAIN IN CASE HE CHANGED ONE AND HIT "END" */
 ISPEXEC VPUT (VTOCFL1 VTOCFL2 VTOCOPT) PROFILE
 DELETE &SPFVTOC
