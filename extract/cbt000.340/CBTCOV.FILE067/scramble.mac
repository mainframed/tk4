 PROC 2 DSN KEY  VOLUME() UNIT(SYSDA) ENCIPHER DECIPHER TIME SKIPREC(0)
 CONTROL NOMSG MAIN END(ENDD)
 IF (&SYSPCMD = EDIT × &SYSPCMD = E ) THEN /* OOPS..NOT UNDER EDIT */+
   DO
     WRITE ****RETURN TO TSO COMMAND LEVEL TO EXECUTE SCRAMBLE ***
     EXIT QUIT
   ENDD
 IF (&ENCIPHER =  && &DECIPHER =   ) THEN +
   DO
     WRITE ..YOU MUST SPECIFY 'ENCIPHER' OR 'DECIPHER' AS PART OF
     WRITE   SCRAMBLE COMMAND (E.G. 'SCRAMBLE DSNAME MYKEY ENCI')
     EXIT QUIT
   ENDD
 IF (&ENCIPHER ^=  && &DECIPHER ^=   ) THEN +
   DO
     WRITE ..YOU MUST SPECIFY EITHER 'ENCIPHER' OR 'DECIPHER' (BUT
     WRITE   NOT BOTH) AS PART OF THE SCRAMBLE COMMAND.
     EXIT QUIT
   ENDD
 IF &ENCIPHER ^=   THEN  SET &OP = &STR(E)
                   ELSE  SET &OP = &STR(D)
 IF (&LENGTH(&KEY) > 8) THEN +
   DO
     WRITE ..INVALID KEY '&KEY'. MUST BE 1 TO 8 CHARACTERS IN LENGTH
     WRITE   FIRST CHARACTER IS ALPHEBETIC.
     EXIT QUIT
   ENDD
 IF (&DATATYPE(&SKIPREC) ^= NUM) THEN +
   DO
     WRITE ..INVALID NUMBER IN SKIPREC(&SKIPREC)..
     WRITE   MUST BE VALID NUMBER OF RECORDS TO SKIP BEFORE OPERATION
     EXIT QUIT
   ENDD
 FREE F(SYSPRINT,DATASET)
 ALLOC F(SYSPRINT) DA(*)
 IF &VOLUME ^=   THEN +
   DO
     SET &V = &STR(VOLUME(&VOLUME) UNIT(&UNIT) )
     WRITE &DSN ON &VOLUME WILL BE USED
  ENDD
 ELSE +
     SET &V = &STR()
 CONTROL MSG
 ALLOC F(DATASET) DA(&DSN.) SHR &V
 SET &RC = &LASTCC
 CONTROL NOMSG
 IF &RC ^= 0 THEN +
   DO
     WRITE ..UNABLE TO RUN SCRAMBLE..I QUIT
     EXIT QUIT
   ENDD
 ATTN +
   DO
     ATTN OFF
     WRITE **ATTENTION ACCEPTED**
     WRITENR IF YOU WANT TO QUIT AND SCREW UP THE DATASET, REPLY 'YES'
     READ
     IF &STR(&SYSDVAL) = &STR(YES) THEN +
       DO
         WRITE ..OK, ITS YOUR FUNERAL...
         FREE F(DATASET)
         EXIT QUIT
       ENDD
     WRITE ..RETURNING TO SCRAMBLE
     RETURN
   ENDD
 IF &TIME ^=  THEN DTIME
 $P SCRAMBLE    '/&OP,&KEY,&SKIPREC'
 IF &TIME ^=  THEN DTIME
 FREE F(DATASET)
 END
