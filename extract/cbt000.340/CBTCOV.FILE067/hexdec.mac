PROC 1 HEX
/*
/* HEXADECIMAL TO DECIMAL CONVERTER. BIG DEAL.
/*
/* )STARTHELP
/* )F FUNCTION                                                         -
/*     HEXDEC WILL CONVERT ANY REASONABLE HEXADECIMAL NUMBER TO        -
DECIMAL.
/* )X SYNTAX                                                           -
/*     HEXDEC NUMBER
/* )O OPERANDS                                                         -
/*     NUMBER: ANY HEXADECIMAL NUMBER FROM 0 THROUGH 2**31-1
/* )ENDHELP
ERROR  DO
       WRITE THE MAXIMUM ALLOWABLE HEXADECMAL VALUE IS FFFFFFF.
       EXIT
       END
SET L EQ 0    /* RANGES OVER &HEX FROM RIGHT TO LEFT
SET DEC EQ 0  /* DECIMAL VALUE ACCUMULATOR
SET P EQ 1    /* TAKES VALUES 1, 16, 256, ETC
SET D EQ '00010203040506070809101112131415'
SET X EQ 0123456789ABCDEF
DO WHILE &LENGTH(&HEX)-&L GT 0
  SET M EQ 1  /* RANGES OVER &D
  SET N EQ 1  /* RANGES OVER &X
  DO WHILE &SUBSTR(&N,&X) NE &SUBSTR(&EVAL((&LENGTH(&HEX)-&L)),&HEX)
  SET M EQ &M+2
  SET N EQ &N+1
  IF &N GT 16 THEN DO
    WRITE INVALID HEXADECMAL VALUE &HEX
    EXIT
    END
  END
  SET W EQ &SUBSTR(&M+1:&M+2,&D)
  SET W EQ &W*&P
  SET DEC EQ &DEC+&W
  SET L EQ &L+1
  SET P EQ &P*16
END
WRITE DECIMAL EQUIVALENT OF HEXADECIMAL &HEX IS &DEC
