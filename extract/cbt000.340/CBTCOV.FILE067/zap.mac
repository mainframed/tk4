PROC 1 DSNAME DEBUG VTOC PRINT V()
IF &DEBUG EQ DEBUG THEN CONTROL LIST CONLIST PROMPT MSG SYMLIST
   ELSE CONTROL NOMSG NOLIST NOPROMPT
FREE F(SYSIN SYSPRINT SYSLIB) ATTR(F6050 KEYED)
ATTR F6050 BLKSIZE(6050) LRECL(121) RECFM(F B A)
ATTR KEYED KEYLEN(44)
ALLOC F(SYSIN) DA(*)
IF &VTOC EQ VTOC THEN                                                  -
  DO
    CATALOG SCR FORMAT4.DSCB (&DSNAME)
    CONTROL NOFLUSH
    ALLOC FI(SYSLIB) DA('FORMAT4.DSCB') KEEP SP(0) TRA                 -
          UNIT(DISK) VOL(&DSNAME) US(KEYED)
    IF &LASTCC NE 0 THEN                                               -
       DO
         WRITE VOLUME &DSNAME IS NOT MOUNTED ON THIS SYSTEM
         WRITE SUPERZAP PROCESSING ENDED
         FREE FI(SYSLIB SYSIN SYSPRINT) ATTR(KEYED F6050)
         ALLOC FI(SYSPRINT) DA(*)
         ALLOC FI(SYSIN)    DA(*)
         CONTROL FLUSH
         EXIT CODE(16)
       END
    /* FOR SOME CRAZY REASON, THE VTOC MUST BE ALLOCATED SHR?? */
    FREE  FI(SYSLIB)
    ALLOC FI(SYSLIB) DA('FORMAT4.DSCB') UNIT(DISK) VOL(&DSNAME) SHR    -
          US(KEYED)
  END
ELSE                                                                   -
  DO
    IF &V EQ THEN ALLOC FI(SYSLIB) DA(&DSNAME) SHR
             ELSE ALLOC FI(SYSLIB) DA(&DSNAME) VOL(&V) SHR
  END
IF &PRINT EQ &STR() THEN                                               -
   DO
      ALLOC F(SYSPRINT) DA(*)
      AUTH AMASPZAP
      SET &LCC EQ &LASTCC
      WRITE SUPERZAP PROCESSING ENDED LASTCC = &LCC
   END
ELSE                                                                   -
   DO
      DEL '&SYSPREF..PRINTZAP.DATA' PURGE
      ALLOC FI(SYSPRINT) DA('&SYSPREF..PRINTZAP.DATA')                 -
            SP(5,0) CYL UNIT(SYSDA) US(F6050)
      AUTH AMASPZAP
      SET &LCC EQ &LASTCC
      WRITE SUPERZAP PROCESSING ENDED LASTCC = &LCC
      WRITE SYSPRINT OUTPUT IS IN DATASET '&SYSPREF..PRINTZAP.DATA'
   END
FREE FI(SYSLIB SYSIN SYSPRINT) ATTR(KEYED F6050)
ALLOC FI(SYSPRINT) DA(*)
ALLOC FI(SYSIN)    DA(*)
IF VTOC EQ VTOC THEN CATALOG SCR FORMAT4.DSCB (&DSNAME)
EXIT CODE(&LCC)
