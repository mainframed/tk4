         /*  CLIST WRITTEN BY: CAROL WINICUR        */
         /*                    GREAT WESTERN BANK   */
         /*                    9401 CORBIN AVE.     */
         /*                    NORTHRIDGE, CA 91328 */
         /*                    (818) 701-8309       */
         /*                                         */
         PROC 3 DDN DSN POSITION DSNCOUNT(0) DEBUG
         CONTROL MSG NOLIST END(BYE)
         SET &DASH = &STR(-)
         IF &DEBUG = DEBUG  THEN  +
            CONTROL MSG LIST CONLIST SYMLIST END(BYE)
         IF &SYSENV NE FORE THEN DO
            WRITE ALMRC01 &DASH ALLOCMOR MUST BE EXECUTED IN FOREGROUND
            EXIT
            BYE
         IF &SYSISPF = ACTIVE THEN DO
            WRITE ALMRC02 &DASH ALLOCMOR MAY NOT EXECUTE IN THE SPF ENVI
            EXIT
            BYE
         IF &SYSDSN(&DSN)=OK THEN GOTO CHKQUOT
         SET &ERR = &SYSDSN(&DSN)
         WRITE &ALMRC03 &DASH &DSN.&DASH.&ERR
         EXIT
         CHKQUOT: +
         IF &SUBSTR(1:1,&DSN) = &STR(') THEN DO
            SET LN = &LENGTH(&STR(&DSN)) - 1
            SET &DSN = &SUBSTR(2:&LN,&DSN)
            GOTO TSTIT
            BYE
         SET &DSN = &SYSPREF..&DSN
         TSTIT:  +
         IF &POSITION = F THEN SET &POSITION = FIRST
         IF &POSITION = L THEN SET &POSITION = LAST
         IF &POSITION = FIRST  +
            OR &POSITION = LAST +
            THEN GOTO BEGIN
         WRITENR ENTER  "FIRST", "F", "LAST", OR "L" FOR POSITION ===>
         READ &POSITION
         GOTO TSTIT
         BEGIN:  +
         %ALLOCTRP &DDN &DSN &DEBUG
         SET &SPC = &STR( )
         ALLOCMR &DDN.,&DSN
         IF &DSNCOUNT = 0 THEN DO
            WRITE ALMRC04 &DASH &DSN NOT CONCATENATED TO &DDN
            GOTO CLEANUP
            BYE
         SET &CNT=1
         DO WHILE &DSNCOUNT GE &CNT
            IF &CNT LT 10 THEN +
               SET &DSNT=&STR('&&PSSDSN0&CNT')
               ELSE SET &DSNT=&STR('&&PSSDSN&CNT')
            SET &DSNP=&DSNP.&SPC.&STR(&DSNT)
            SET &CNT=&CNT+1
            BYE
         IF &POSITION = FIRST  THEN +
            ALLOC FI(&DDN) DA('&DSN' &DSNP) SHR REUSE
            ELSE  ALLOC FI(&DDN) DA(&DSNP '&DSN') SHR REUSE
         CLEANUP:  +
         IF &DEBUG NE DEBUG  THEN  +
            CONTROL NOMSG
         FREE FI(TLISTA)
         FREE ATTR(VBAT)
         DEL @TRAP.&DDN
