PROC 0 DEBUG
   IF &DEBUG^=DEBUG THEN CONTROL MSG NOSYM NOCON NOLIST NOPROMPT
                    ELSE CONTROL MSG   SYM   CON   LIST   PROMPT
      WRITENR ADDRESS  ADDRESS/(LIST) ===>
      READ &ADDRESS
      IF &ADDRESS=  THEN SET &ADDRESS=LIST
      WRITENR DO YOU WANT PRINT THE OUTPUT?       Y/(N) ===>
      READ &ANS
      IF &ANS EQ Y  THEN SETDEF PRINT
                    ELSE SETDEF NOPRINT
      WRITENR DO YOU WANT OUTPUT TO THE TERMINAL? (Y)/N ===>
      READ &ANS
      IF &ANS EQ N  THEN SETDEF NOTERMINAL
                    ELSE SETDEF TERMINAL
    SETDEF DISPLAY(SYMBOL,NOMACHINE)
    GLOBAL &DECVALUE
    SET &FOUND = 0
    NOTE ' ******************************************************** '
    NOTE '                  LPAMAP DISPLAY                          '
    NOTE ' ******************************************************** '
    EQ CVTPTR 10. LEN(4) PTR
    EQ CVTLPAQ CVTPTR%+BC LEN(4) PTR
    EQ CDCHAIN CVTLPAQ% LEN(4)
    IF &ADDRESS NE LIST THEN  +
        %DECIMAL &ADDRESS &DEBUG
NEXTCDE: +
    EQ CDENTRY CDCHAIN% LEN(32)
    COMPARE ADDR(CDCHAIN) WITH (VALUE(X'00000000'))
    IF &LASTCC EQ 0 THEN GOTO EXIT
    EQ CDCHAIN CDCHAIN% LEN(4) PTR
    EQ CDNAME CDCHAIN+8 LEN(8) CHAR
    EQ CDXLMJP CDCHAIN+14% LEN(4) PTR
    EVALUATE  CDXLMJP+8
    SET &XTLMSBLN = &LASTCC
    EVALUATE    CDXLMJP+C
    SET &XTLMSBAA = &LASTCC
    SET &ENDLOC = &XTLMSBAA+&XTLMSBLN
    IF &ADDRESS EQ LIST THEN GOTO LISTCDE
    COMPARE ADDR(CDENTRY+1C) WITH (VALUE(X'04')) LEN(1) MASK(X'04')
    IF &LASTCC = 0 THEN GOTO NEXTCDE /* SKIP MINOR CDE */
    IF &DECVALUE LT &XTLMSBAA   THEN GOTO NEXTCDE
    IF &DECVALUE GE &ENDLOC     THEN GOTO NEXTCDE
    SET &FOUND = 1
LISTCDE:                                                              +
    NOTE ' '
    LIST CDNAME
    LIST CDXLMJP+8 LEN(8)
    IF &ADDRESS EQ LIST THEN GOTO NEXTCDE
EXIT:                                                                 +
    IF &FOUND = 0 THEN DO
        NOTE ' ADDRESS &ADDRESS NOT FOUND IN MASTER CDE LIST '
        END
    NOTE ' ********************************************************* '
    NOTE ' *             END OF STORAGE LISTER                     * '
    NOTE ' ********************************************************* '
