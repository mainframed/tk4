PROC 3 JOBNAME NAME ROOM DEST(U4) PROCLIB('LA.TEST.PROCLIB')
CONTROL NOMSG
SET &HR = &SUBSTR(1:2,&SYSTIME)
SET &MIN = &SUBSTR(4:5,&SYSTIME)
SET &SEC = &SUBSTR(7:8,&SYSTIME)
SET &TIME = T&HR.&MIN.&SEC
FREE F(IEBJOB) ATTRLIST(SYNL)
ATTR SYNL RECFM(F B) LRECL(80) BLKSIZE(2480)
ALLOC F(IEBJOB) DA('&SYSPREF..&JOBNAME..&TIME..CNTL') NEW SPACE(1)     -
TRACKS RELEASE USING(SYNL)
OPENFILE IEBJOB OUTPUT
SET &IEBJOB = &STR(//&SYSUID.JOB  JOB                             9ZZZ,-
MSGLEVEL=1,CLASS=B,)
PUTFILE IEBJOB
SET &IEBJOB = &STR(//          NOTIFY=&SYSUID.,MSGCLASS=Z)
PUTFILE IEBJOB
SET &IEBJOB = &STR(//STEP0  EXEC  &JOBNAME  )
PUTFILE IEBJOB
CLOSFILE IEBJOB
FREE F(IEBJOB)
ALLOC F(SYNJOB) DA('&SYSPREF..&TIME..CNTL') NEW SPACE(1)               -
TRACKS RELEASE USING(SYNL)
OPENFILE SYNJOB OUTPUT
SET &SYNJOB = &STR(//&SYSUID.JOB  JOB                             9ZZZ,-
MSGLEVEL=1,CLASS=B,)
PUTFILE SYNJOB
SET &SYNJOB = &STR(//          NOTIFY=&SYSUID.,MSGCLASS=A)
PUTFILE SYNJOB
SET &ACCT = &STR(//*ACCT)
SET &SYNJOB = &STR(&ACCT        &NAME.,&ROOM)
PUTFILE SYNJOB
SET &ROUTE = &STR(/*ROUTE)
SET &SYNJOB = &STR(&ROUTE   PRINT &DEST.)
PUTFILE SYNJOB
SET &SYNJOB = &STR(//STEP0    EXEC PGM=IKJEFT01,DYNAMNBR=24,PERFORM=29)
PUTFILE SYNJOB
SET &SYNJOB = &STR(//SYSPRINT DD SYSOUT=*   )
PUTFILE SYNJOB
SET &SYNJOB = &STR(//SYSTSPRT DD                                       -
SYSOUT=*,DCB=(RECFM=VBA,LRECL=137,BLKSIZE=141)
PUTFILE SYNJOB
SET &SYNJOB = &STR(//SYSTSIN  DD * )
PUTFILE SYNJOB
SET &SYNJOB = &STR(L  '&PROCLIB(&JOBNAME)'   )
PUTFILE SYNJOB
SET &SYNJOB = &STR(SYNTAX '&SYSPREF..&JOBNAME..&TIME..CNTL'            -
PROCLIB('&PROCLIB') )
PUTFILE SYNJOB
SET &SYNJOB = &STR(/*   )
PUTFILE SYNJOB
CLOSFILE SYNJOB
CONTROL MSG
SUB '&SYSPREF..&TIME..CNTL'
CONTROL NOMSG
DEL '&SYSPREF..&TIME..CNTL'
FREE F(SYNJOB) ATTRLIST(SYNL)
EXIT
