PROC 0 RESTART()
WRITE DO YOU WISH A TUTORIAL? Y/N
READ &TUT
IF &SUBSTR(1:1,&TUT) EQ N THEN GOTO NOTUT
WRITE SCRAM IS A DATASET DELETION AID.
WRITE IT WILL PROMPT YOU WITH: "PLEASE ENTER THE DATASET PREFIX YOU    -
WISH TO SCAN."
WRITE ENTER THE DATSET PREFIX YOU ARE INTERESTED IN SCANNING FOR.
WRITE ENTERING A NULL LINE WILL CAUSE YOUR PREFIX TO BE SCANNED.
WRITE THE CLIST WILL THEN WRITE THE NAMES OF ALL CATALOGED DATASETS
WRITE HAVING THAT PREFIX FOLLOWED BY THE MESSAGE: DELETE? Y/N/M
WRITE
WRITE IF YOU RESPOND "Y", THE DATASET WILL BE DELETED. IF IT IS ON A
WRITE VOLUME THAT IS NOT CURRENTLY MOUNTED, ITS NAME WILL BE SCRATCHED
WRITE FROM THE CATALOG. (SYS1 AND SYS2 PREFIXES WILL NOT BE DELETED.)
WRITE
WRITE IF YOU RESPOND "N", THE DATASET WILL NOT BE DELETED.
WRITE
WRITE IF YOU RESPOND "M", THE CLIST WILL DO A "LISTDS" ON THE DATASET
WRITE SO THAT YOU CAN FIND OUT ITS' CREATION DATE, THE VOLUME
WRITE THAT IT RESIDES ON, AND ITS' MEMBERS (IF IT IS A PDS).
WRITE IF IT IS A SEQUENTIAL DATASET, SCRAM WILL DO A "LIST" ON THE
WRITE DATASET. (IF YOU WANT TO INTERRUPT THE "LIST" FUNCTION, HIT PA1.)
WRITE
WRITE IF YOU WANT TO INTERRUPT "SCRAM" (IE, RETURN TO "READY"),
WRITE KEY IN "ENDEND" AT THE TIME YOU GET THE "DELETE? Y/N/M" MESSAGE.
WRITE THE MESSAGE: "CURRENT DATASET *** DATASET NAME ***" WILL APPEAR.
WRITE FOLLOWED BY "READY".
WRITE
WRITE TO RESTART, KEY: SCRAM RESTART(FULLY.QUALIFIED.DATASET.NAME)
WRITE
NOTUT: WRITE PLEASE ENTER THE DATASET PREFIX YOU WISH TO SCAN.
CONTROL NOMSG MAIN NOFLUSH
READ &LEVEL
IF &LEVEL = THEN SET &LEVEL=&SYSPREF
FREE F(IN)
FREE ATTR(A)
ATTR A RECFM(V B A) LRECL(255) BL(3120)
ALLOC FI(IN) SHR NEW SP(1 1) TRACKS BL(255) USING(A) UNIT(VIODA)
LC: LISTCAT OUTFILE(IN) L(&LEVEL)
IF &LASTCC = 4 THEN +
  DO
   WRITE INVALID LEVEL &LEVEL - REENTER
   READ &LEVEL
   IF &LEVEL = THEN SET &LEVEL=&SYSPREF
   GOTO LC
  END
ATTN +
GOTO RETRY
ERROR +
DO
IF &LASTCC EQ 400 THEN +
  DO
    CLOSFILE IN
    EXIT
  END
ELSE +
IF &LASTCC EQ 8 THEN +
  DO
    CONTROL NOMSG
    DEL '&IN' NOSCRATCH
    WRITE &IN REMOVED FROM CATALOG. PACK NOT MOUNTED.
    GOTO GET
    CONTROL MSG
  END
ELSE +
  DO
    WRITE ** ERROR RETURN CODE = &LASTCC **
    CLOSFILE IN
    FREE FI(IN)
    EXIT
  END
END
SET &RSW EQ NO
IF &RESTART EQ &STR() THEN SET &RSW EQ YES
CONTROL NOFLUSH NOMSG
OPENFILE IN
GET: +
GETFILE IN
IF &SUBSTR(18:&EVAL(&LENGTH(&LEVEL)+17),&IN) NE &STR(&LEVEL) THEN GOTO -
GET
SET IN EQ &SUBSTR(18:&LENGTH(&IN),&IN)
DO WHILE &RSW NE YES
IF &IN NE &RESTART THEN GOTO GET
SET &RSW EQ YES
END
RETRY: +
WRITE *** &IN ***
WRITE DELETE? Y/N/M. ENTER "ENDEND" TO TERMINATE PROCESSING.
READ &ANS
IF &ANS EQ ENDEND THEN GOTO LAST
IF &ANS EQ N THEN GOTO GET
IF &ANS EQ M THEN +
DO
CONTROL MSG
LISTDS '&IN' HISTORY MEMBERS
CONTROL NOMSG
L '&IN'
CONTROL MSG
GOTO RETRY
END
IF &ANS NE Y THEN +
DO
WRITE INVALID REPLY: &ANS
GOTO RETRY
END
IF &SUBSTR(1:3,&LEVEL) EQ HMD AND &LENGTH(&LEVEL) = 3 AND &SYSUID NE   -
SSUD THEN +
DO
WRITE ONLY 'THE WIZARD' MAY DELETE DATASETS UNDER INDEX &LEVEL
GOTO GET
END
IF &SUBSTR(1:&LENGTH(&LEVEL),&LEVEL) EQ SYS1 OR                        -
&SUBSTR(1:&LENGTH(&LEVEL),&LEVEL) EQ SYS2 THEN +
DO
WRITE DELETION OF DATASETS WITH PREFIX OF                              -
&SUBSTR(1:&LENGTH(&LEVEL),&LEVEL) NOT SUPPORTED
GOTO GET
END
WRITE DATASET &IN ABOUT TO BE DELETED. ENTER "D" TO DELETE.
READ &DEL
IF &DEL NE D THEN GOTO GET
DEL '&IN'
GOTO GET
LAST: WRITE CURRENT DATASET *** &IN ***
CLOSFILE IN
EXIT
