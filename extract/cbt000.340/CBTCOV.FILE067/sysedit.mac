 PROC 0 DSNUM() DEBUG TRACE
 /*SET &TRACE = TRACE
 /*SET &DEBUG = DEBUG

 /* THIS CLIST IS USED BY THE SYSEDIT (S.2) FUNCTION OF SPF */

 IF &DEBUG = DEBUG THEN CONTROL LIST SYMLIST CONLIST
 CONTROL NOMSG NOFLUSH
 ISPEXEC CONTROL ERRORS RETURN
 ISPEXEC TBEND SYSEDIT
 ISPEXEC CONTROL ERRORS CANCEL
 CONTROL MSG FLUSH
 ISPEXEC VGET (ZTRAIL)
 IF &STR(&ZTRAIL&DSNUM) =   THEN SET &ONESHOT =
                            ELSE SET &ONESHOT = YES


 ISPEXEC TBCREATE SYSEDIT KEYS(NUM) NAMES(DSN MEMBER) NOWRITE


 SET &NUM = 1
 SET &DSN = ACF2.R314.ACFJOBS
 SET &MEMBER = POSTEFJB
 ISPEXEC TBADD SYSEDIT

 IF &DEBUG = DEBUG THEN CONTROL NOLIST NOSYMLIST NOCONLIST

 SET &NUM = &NUM + 1
 SET &DSN = HAC1.HARDCPY.DATA
 SET &MEMBER =
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS1.PROCLIB
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS2.PARMLIB
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS3.STC.JOBCARDS.CNTL
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS3.STC.PROCLIB
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS3.TSO.PROCLIB
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS8.MANL.HISTORY.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS8.MANL.ORDERS.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS8.MANL.SLSS.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS8.PROD.CNTL
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.ACF2.DSN.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.ACF2.VOL.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.GEN.ASM
 SET &MEMBER = STAGE1
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.GEN.CNTL
 SET &MEMBER =
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.STAGE2.CNTL
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = SYS9.STAGE3.CNTL
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO.CLIST
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO1.CLIST
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO.NEWS.TEXT
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO1.TEXT
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO1.HISTORY.TEXT
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO.SUGGEST.TEXT
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TSO.SUGGEST.HISTORY.TEXT
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TT26865.MVS3MOD8.CNTL
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TT74481.MANUAL.DATA
 ISPEXEC TBADD SYSEDIT

 SET &NUM = &NUM + 1
 SET &DSN = TT66234.LINES.DATA
 ISPEXEC TBADD SYSEDIT

 IF &DEBUG = DEBUG THEN CONTROL LIST SYMLIST CONLIST

 SET &MAXNUM = &NUM     /* SAVE THE HIGHEST NUMBER */
 ISPEXEC TBTOP SYSEDIT
 SET &MSG =
 SET &TOPROW = 0


 LOOP:  IF &TRACE = TRACE THEN WRITE TRACE: LOOP
 SET &ZCMD = STUPID
 SET &MEMBER =
 SET &CMDMBR =
 SET &SELECT =
 ISPEXEC TBTOP SYSEDIT
 ISPEXEC TBSKIP SYSEDIT NUMBER(&TOPROW)
 IF &ZTRAIL = 2 THEN SET &ZTRAIL =
 IF &ZTRAIL ^=   THEN DO
      IF &SUBSTR(1:2,&ZTRAIL) ^= &STR(2.) THEN DO
           WRITE *** LOGIC ERROR IN SYSEDIT CLIST -- NOTIFY LDW ***
           EXIT CODE(20)
           END
      SET &DSNUM = &SUBSTR(3:&LENGTH(&ZTRAIL),&ZTRAIL)
      SET &ZTRAIL =               /* PREVENT LOOPS */
      SET &ZTDTOP = 0    /* ANY OLD VALUE TO PREVENT ERROR BELOW */
      END
 IF &DSNUM =   THEN ISPEXEC TBDISPL SYSEDIT PANEL(SYSEDIT) &MSG
               ELSE DO
      SET &ZCMD = &DSNUM
      SET &DSNUM  =               /* PREVENT LOOPS */
      SET &ZTDTOP = 0    /* ANY OLD VALUE TO PREVENT ERROR BELOW */
      END
 SET &DISPLCC = &LASTCC

 LOOPX:  IF &TRACE = TRACE THEN WRITE TRACE: LOOPX
 IF &DISPLCC = 8 THEN GOTO EXIT
 SET &TOPROW = &ZTDTOP     /* SAVE NUMBER OF TOP ROW ON SCREEN */
 IF &ZCMD = DEBUG ON THEN DO
      CONTROL   LIST   SYMLIST   CONLIST
      SET &ZCMD =
      GOTO LOOP
      END
 IF &ZCMD = DEBUG OFF THEN DO
      CONTROL NOLIST NOSYMLIST NOCONLIST
      SET &ZCMD =
      GOTO LOOP
      END
 IF &ZCMD = TRACE ON THEN DO
      SET &TRACE = TRACE
      SET &ZCMD =
      GOTO LOOP
      END
 IF &ZCMD = TRACE OFF THEN DO
      SET &TRACE =
      SET &ZCMD =
      GOTO LOOP
      END
 /* SEE IF THERE IS A '.MEMBER' IN THE INPUT */
 SET &I = 1
 SET &L = &LENGTH(&ZCMD)
 IF &L > 2 THEN DO WHILE(&I < &L)
      SET &I = &I + 1   /* NEXT POSITION */
      IF &SUBSTR(&I,&ZCMD) = . THEN DO
           SET &CMDMBR = &SUBSTR(&I+1:&L,&ZCMD)
           SET &ZCMD   = &SUBSTR(1:&I-1,&ZCMD)
           GOTO GOTMBR
           END
      END

 GOTMBR:  IF &TRACE = TRACE THEN WRITE TRACE: GOTMBR
 IF &DATATYPE(&ZCMD) = NUM THEN DO
      SET &TEMPSEL = B
      SET &NUM = &ZCMD
      END
                          ELSE DO
      IF &ZCMD =  THEN GOTO BLANK
      IF &LENGTH(&ZCMD) < 2 THEN GOTO ERR3
      SET &TEMPSEL = &SUBSTR(1,&ZCMD)
      IF &TEMPSEL = S THEN SET &TEMPSEL = B
      IF &TEMPSEL ^= E AND &TEMPSEL ^= B THEN GOTO ERR3
      SET &NUM = &SUBSTR(2:&LENGTH(&ZCMD),&ZCMD)
      END
 SET &NUM = &EVAL(&NUM)
 IF &DATATYPE(&NUM) ^= NUM THEN DO
      ERR3: SET &MSG = MSG(SYSED003)
      GOTO LOOP
      END
 IF &NUM > &MAXNUM THEN GOTO ERR3
 ISPEXEC TBSCAN SYSEDIT ARGLIST(NUM)
 SET &ZCMD =
 SET &SELECT = &TEMPSEL
 SET &MEMBER = &CMDMBR

 BLANK:  IF &TRACE = TRACE THEN WRITE TRACE: BLANK
 SET &MSG =
 SET &XDSN = &DSN
 SET &XMBR = &MEMBER
 IF &DISPLCC > 4 THEN WRITE TBDISPL RC = &DISPLCC
 #DSORG '&DSN'
 SET &DSORG = &LASTCC
 IF &DSORG = 0 THEN DO
      SET &MSG = MSG(SYSED001)
      GOTO LOOP
      END
 IF &DSORG = 1 /* PS */ AND &MEMBER ^=   THEN DO
      SET &MSG = MSG(SYSED006)    /* MEMBER NAME NOT ALLOWED FOR PS */
      GOTO LOOP
      END
 /* IF MEMBER IS BLANK, SET IT TO NULL!!! */
 IF &MEMBER =   THEN SET &MEMBER =
                ELSE SET &MEMBER = &STR((&MEMBER))
 IF &SELECT = S THEN SET &SELECT = B
 IF &SELECT ^= E AND &SELECT ^= B THEN DO
      SET &MSG = MSG(SYSED000)
      GOTO LOOP
      END
 ISPEXEC CONTROL ERRORS RETURN

 IF &SELECT = E THEN ISPEXEC EDIT   DATASET('&DSN&MEMBER')
                ELSE ISPEXEC BROWSE DATASET('&DSN&MEMBER')
 SET &CC = &LASTCC
 IF &ONESHOT = YES THEN GOTO EXIT
 ISPEXEC CONTROL ERRORS CANCEL
 IF &CC < 5 THEN DO
      IF &DISPLCC = 4 THEN DO
           ISPEXEC TBDISPL SYSEDIT
           SET &DISPLCC = &LASTCC
           GOTO LOOPX
           END
      IF &SELECT = E AND &CC = 0 THEN DO
           IF &DSORG = 2 /* PO */ AND &MEMBER ^=   THEN +
              SET &MSG = MSG(SYSED004)   /* SO WE WILL SAY 'SAVED' */
           IF &DSORG = 1 /* PS */                  THEN +
              SET &MSG = MSG(SYSED005)   /* SO WE WILL SAY 'SAVED' */
           END
      GOTO LOOP
      END
 SET &MSG = MSG(SYSED002)
 GOTO LOOP

 EXIT:  +
 ISPEXEC TBEND SYSEDIT
 SET &CC = &LASTCC
 IF &CC ^= 0 THEN WRITE TBEND RC = &CC
