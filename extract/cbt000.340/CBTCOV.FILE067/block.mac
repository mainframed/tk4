PROC 1 RECL MIN(0) MAX(19069) PCC(90) B50 B3350 B30 B3330 B5 B3
/*  CONTROL SYMLIST ---- FOR DEBUGGING  */
SET &NORECS = NORECS                                                   -
         /* SET FLAG FOR NO GOOD BLOCKSIZES FOUND */
IF &DATATYPE(&RECL) = CHAR THEN GOTO CHARRCL                           -
                          /* LRECL IS NOT NUMERIC */
SET &PC = &PCC      /* COMMENTS ARE OFF TO THE RIGHT                   -
                   EVALUATE PERCENT OF TRACK USED */
SET &LRECL = &RECL                                                     -
                           /* RESOLVE EXPRESSIONS */
IF &LRECL LT 0 THEN GOTO NEGRCL                                        -
                       /* LRECL IS LESS THEN ZERO */
IF &DATATYPE(&PC) = CHAR THEN GOTO PCBADD                              -
                        /* PERCENT IS NOT NUMERIC */
IF &PC GT 100 THEN GOTO PCBADD                                         -
                    /* PERCENT WANTED IS OVER 100 */
IF &PC LT 1 THEN GOTO PCBADD                                           -
                 /* PERCENT WANTED IS LESS THAN 1 */
IF &LRECL = 0 THEN GOTO ZEROBLK                                        -
                     /* LRECL OF 0 IS NOT ALLOWED */
SET &LOW30 = (&PC * 13030) / 100
SET &LOW50 = (&PC * 19069) / 100
SET &PC30 = 0                                                          -
         /* INITIALIZE PERCENT OF 3330 TRACK USED */
SET &PC50 = 0                                                          -
         /* INITIALIZE PERCENT OF 3350 TRACK USED */
SET &SK50 = DUMMM                                                      -
                                   /* INITIALIZE  */
SET &DASDTYP = BOTH 3330 AND 3350                                      -
                            /* INITIALIZE HEADING */
IF &B3350 = B3350 THEN SET &SK50 = SK30                                -
                               /* SKIP 3330 TEST  */
IF &B50 = B50 THEN SET &SK50 = SK30                                    -
                               /* SKIP 3330 TEST  */
IF &B5 = B5 THEN SET &SK50 = SK30                                      -
                               /* SKIP 3330 TEST  */
IF &B3330 = B3330 THEN SET &SK50 = SK50                                -
                               /* SKIP 3350 TEST  */
IF &B30 = B30 THEN SET &SK50 = SK50                                    -
                               /* SKIP 3350 TEST  */
IF &B3 = B3 THEN SET &SK50 = SK50                                      -
                               /* SKIP 3350 TEST  */
IF &SK50 = SK30 THEN SET &DASDTYP = 3350                               -
                               /* FOR THE HEADING */
IF &SK50 = SK50 THEN SET &DASDTYP = 3330                               -
                               /* FOR THE HEADING */
WRITE THE LRECL WHICH YOU SPECIFIED IS &LRECL..
WRITE
WRITE THE LIST OF BLOCKSIZES WHICH USE MORE THAN
WRITE &PC% OF &DASDTYP TRACKS FOLLOWS:
WRITE
SET &MULT = 0                                                          -
                                  /*  INITIALIZE  */
LOOP: SET &MULT = &MULT + 1                                            -
                                          /* BUMP */
SET &BLOCK = &MULT * &LRECL
IF &BLOCK GT &MAX THEN GOTO FINISH
IF &BLOCK LT &MIN THEN GOTO LOOP
SET &SUMM30 = 0
SET &SUMM50 = 0
LOOP2:                                                                 -
              /* TEST BLOCKSIZE FOR % ON 3330'S */ +
IF &SK50 = SK30 THEN GOTO LOOP3                                        -
                                /* SKIP 3330 TEST */
SET &SUMM30 = &SUMM30 + &BLOCK + 135
SET &SUMM50 = &SUMM50 + &BLOCK + 185
IF ((&SUMM30 / (&BLOCK + 135)) * &BLOCK) LT &LOW30 THEN GOTO LOOP2     -
                  /*  NOT YET DESIRED PERCENTAGE  */
IF (&BLOCK + 135) LE (13030 - &SUMM30) THEN GOTO LOOP2                 -
                          /* FIT IN ANOTHER BLOCK */
IF (&SUMM30 - 135) GT 13030 THEN GOTO LOOP                             -
                      /* BAD BLOCKSIZE THIS TIME  */
SET &RT30 = (&SUMM30 / (&BLOCK + 135)) * &MULT                         -
                            /* RECORDS / 3330 TRK */
SET &PC30 = (100 * &LRECL * &RT30) / 13030                             -
                    /* PERCENT OF 3330 TRACK USED */
SET &SUMM50 = &SUMM50 - (&BLOCK + 185)                                 -
                   /* ACCOUNT FOR NEXT LOOP ENTRY */
LOOP3:                                                                 -
              /* TEST BLOCKSIZE FOR % ON 3350'S */ +
IF &SK50 = SK50 THEN GOTO WRITIT                                       -
                            /* SKIP THE 3350 TEST */
SET &SUMM50 = &SUMM50 + &BLOCK + 185
IF ((&SUMM50 / (&BLOCK + 185)) * &BLOCK) LT &LOW50 THEN GOTO LOOP3     -
                  /*  NOT YET DESIRED PERCENTAGE  */
IF (&BLOCK + 185) LE (19069 - &SUMM50) THEN GOTO LOOP3                 -
                          /* FIT IN ANOTHER BLOCK */
IF (&SUMM50 - 185) GT 19069 THEN GOTO LOOP                             -
                      /* BAD BLOCKSIZE THIS TIME  */
WRITIT:                                                                -
                     /* WRITE THE DETAIL LINE  */  +
SET &NORECS = RECORDS                                                  -
        /* SET FLAG THAT THERE WERE RECORDS FOUND */
IF &SK50 = SK50 THEN WRITE &MULT. TO 1  BLKSIZE = &BLOCK.   &RT30.     -
RCDS/3330 TRACK (&PC30%)
IF &SK50 = SK50 THEN GOTO LOOP
SET &RT50 = (&SUMM50 / (&BLOCK + 185)) * &MULT                         -
                             /* RCDS / 3350 TRACK */
SET &PC50 = (100 * &LRECL * &RT50) / 19069                             -
                    /* PERCENT OF 3350 TRACK USED */
IF &SK50 = SK30 THEN WRITE &MULT. TO 1  BLKSIZE = &BLOCK.   &RT50.     -
RCDS/3350 TRACK (&PC50%)
IF &SK50 = SK30 THEN GOTO LOOP
WRITE &MULT. TO 1  BLKSIZE = &BLOCK.   &RT30. RCDS/3330 TRK (&PC30%)   -
&RT50. RCDS/3350 TRK (&PC50%)
GOTO LOOP
FINISH:                                                                -
                       /* WRITE FINAL MESSAGE  */  +
WRITE
IF &NORECS = NORECS THEN GOTO NORCRDS                                  -
                /* NO GOOD BLOCKSIZES WERE FOUND  */
WRITE  THIS COMPLETES THE LIST OF BLOCKSIZES.
EXIT QUIT
NORCRDS:                                                               -
              /* NO GOOD BLOCKSIZES WERE FOUND */  +
WRITE THERE ARE NO BLOCKSIZES WHICH ACHIEVE &PC% BLOCKING FOR
WRITE THE RECORD LENGTH OF &LRECL ON &DASDTYP DISKS.
EXIT QUIT
WRITE  THIS COMPLETES THE LIST OF BLOCKSIZES.
EXIT QUIT
ZEROBLK: WRITE
WRITE ZERO ???  DROP DEAD &SYSPREF. !!!
EXIT QUIT
PCBADD: WRITE
WRITE PC (PERCENT OF TRACK UTILIZATION) MUST BE
WRITE AN INTEGER BETWEEN 1 AND 100, INCLUSIVE.
EXIT QUIT
CHARRCL: WRITE
WRITE RECORD LENGTH MUST BE NUMERIC
EXIT QUIT
NEGRCL: WRITE
WRITE RECORD LENGTH MUST BE POSITIVE
