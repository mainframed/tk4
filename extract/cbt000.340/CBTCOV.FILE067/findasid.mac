PROC     1     JOBNAME DEBUG
/**********************************************************************/
DEBUG
/*       IPCS CLIST TO FIND THE ASID OF A SPECIFIED JOB NAME.  THE     -
*/
/*       PARAMETER SPECIFIES THE NAME OF A BATCH OR DEMAND SELECT      -
JOB.*/
/*       IF THE JOB IS FOUND, THE ASID IS DISPLAYED, AND IS SET AS     -
*/
/*       THE DEFAULT.                                                  -
*/
/**********************************************************************/

IF       &DEBUG = DEBUG +
THEN     CONTROL PROMPT LIST CONLIST SYMLIST
SET      &NAME = &SUBSTR(1:8,&JOBNAME       )
SET      &ASCBJBNI = 172N
SET      &ASCBJBNS = 176N
SET      &ASVTAVAL = X'80'
SET      &ASVTFRST = 524N
SET      &ASVTMAXU = 516N
EVALUATE ASVT STRUCTURE(ASVT)
SET      &ASID = 1
EVALUATE ASVT+&ASVTMAXU LENGTH(4)
SET      &LASTASID = &LASTCC
SET      &JOBASID = 0
DO       WHILE &ASID <= &LASTASID && &JOBASID = 0 /* ASID LOOP */
         SET   &ASVTENTY = &STR(ASVT+&ASVTFRST+&EVAL(&ASID*4)N)
         COMPARE ADDRESS(&ASVTENTY) +
                 WITH(VALUE(&ASVTAVAL)) LENGTH(1)
         IF    &LASTCC ^= 0 +
         THEN  DO    /* CHECK JOB NAME */
               EQUATE XASCB &ASVTENTY% STRUCTURE(ASCB)
               SET   &JOBPTR = &STR(XASCB+&ASCBJBNI)
               COMPARE ADDRESS(&JOBPTR) +
                       WITH(VALUE(X'00000000')) LENGTH(4)
               IF    &LASTCC = 0 +
               THEN  SET   &JOBPTR = &STR(XASCB+&ASCBJBNS)
               COMPARE ADDRESS(&JOBPTR%) +
                       WITH(VALUE(C'&NAME')) LENGTH(8)
               IF &LASTCC = 0 +
               THEN  SET   &JOBASID = &ASID
               END   /* CHECK JOB NAME */
         SET   &ASID = &ASID + 1
         END   /* ASID LOOP */
IF       &JOBASID ^= 0 +
THEN     DO    /* DEFINE ASID */
         NOTE  'JOB &JOBNAME IS ASID &JOBASID'
         SETDEF ASID(&JOBASID)
         END   /* DEFINE ASID */
ELSE     NOTE  'JOB &JOBNAME NOT FOUND'
