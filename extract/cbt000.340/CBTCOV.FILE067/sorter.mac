PROC 0  SORTPARM('CORE=MAX')
CONTROL  NOMSG,NOPROMPT,NOFLUSH
ATTN DO
     WRITE  (((((  SORT ABORTED. ALL DATASETS AND DCB'S FREED ))))
     CONTROL NOMSG
     FREE FI(SORTIN,SORTOUT,SYSIN,SYSPRINT,SORTWK01,SORTWK02)
     FREE FI(SORTWK03,SORTMSG) ATTR(IND,SRTDD)
     ALLOC FI(SYSIN) DA(*)
     ALLOC FI(SYSPRINT) DA(*)
     EXIT CODE(16)
     END
FREE FI(SORTIN SORTOUT SYSIN SYSPRINT SORTWK01 SORTWK02)
FREE FI(SORTWK03 SORTMSG) ATTR(IND,SRTDD)
ALLOC FI(SYSPRINT) DA(*)
DEL  SORTCARD.&SYSUID..DATA
K
WRITE ENTER THE INPUT DATASET NAME, FULLY QUALIFIED, NO QUOTES..
READ  &SORTIN
ALLOC F(SORTIN) DA('&SORTIN') SHR
WRITE ENTER THE OUTPUT DATASET NAME, FULLY QUALIFIED, NO QUOTES...
READ  &SORTOUT
DEL '&SORTOUT'
WRITE ENTER THE DCB PARAMETERS OF THE SORTED OUTPUT AS FOLLOWS:
WRITENR LOGICAL RECORD LENGTH:
READ &LRECL
WRITENR BLOCK SIZE:
READ &BLKSIZE
WRITENR RECORD FORMAT (F OR V ONLY):
READ &RECFM
ATTR IND RECFM(&RECFM,B) LRECL(&LRECL) BLKSIZE(&BLKSIZE)
K
AGAIN: WRITE FOR THE SORT CONTROL STATEMENT, ENTER:
WRITE     "1"      TO REFER ME TO A DATASET OR LIBRARY
WRITE OR  "2"      TO ENTER IT VIA THE TUBE
READ &CHOICE
IF &CHOICE EQ 1 THEN DO
   WRITE ENTER THE DATASET NAME, FULLY QUALIFIED, NO QUOTES
   READ  &SORTCARDLIB
   ALLOC F(SYSIN) DA('&SORTCARDLIB') SHR
   GOTO ENDIF
   END
IF &CHOICE = 2 THEN DO
      K
      WRITE OVERLAY THE SORT CONTROL CARD SHOWN WITH THE PARAMETERS    -
YOU NEED,
      WRITE THEN HIT ENTER:
      WRITE         PP = THE POSITION OF THE SORT FIELD
      WRITE         LL = THE LENGTH   OF THE SORT FIELD
      WRITE         A  = ASCENDING (MAY ALSO BE D FOR DESCENDING)
      WRITE
      WRITE ' SORT FIELDS=(PP,LL,A,PP,LL,A),FORMAT=CH                  -
           '
      READ  &SORTCARD
      ATTR SRTDD RECFM(F,B) LRECL(80) BLKSIZE(80)
      ALLOC F(SRTSYSIN)   DA(SORTCARD.&SYSUID..DATA) NEW CATALOG       -
SPACE(1)                                                               -
            TRACKS USING(SRTDD)
      OPENFILE SRTSYSIN OUTPUT
      SET &SRTSYSIN = &STR(&SORTCARD)
      PUTFILE SRTSYSIN
      CLOSFILE  SRTSYSIN
      FREE  DA(SORTCARD.&SYSUID..DATA)
      ALLOC F(SYSIN) DA(SORTCARD.&SYSUID..DATA) SHR
      GOTO ENDIF
      END
K
WRITE YOUR RESPONSE WAS NOT A 1 OR A 2, CLOWN
WRITE
GOTO AGAIN
ENDIF:                                                                 -
ALLOC F(SORTOUT) DA('&SORTOUT') NEW CAT USING(IND) SPACE(99) CYL       -
RELEASE
ALLOC F(SORTWK01) SPACE(10) CYL RELEASE
ALLOC F(SORTWK02) SPACE(10) CYL RELEASE
ALLOC F(SORTWK03) SPACE(10) CYL RELEASE
ALLOC F(SORTMSG) DA(*)
K
WRITE CONFIRM THE FOLLOWING SORT INFORMATION:  (PA1 TO CANCEL)
WRITE                                        (ENTER TO CONTINUE)
WRITE SORT  INPUT: &SORTIN
WRITE SORT OUTPUT: &SORTOUT
WRITE             DCB: LRECL(&LRECL) BLKSIZE(&BLKSIZE) RECFM(&RECFM.,B)
IF &CHOICE = 2 THEN WRITE SORTCARD: &SORTCARD
ELSE DO
     WRITE SORTCARD: FROM &SORTCARDLIB
     L '&SORTCARDLIB'
     END
READ &GO
K
WRITE SORT NOW IN PROGRESS
XEQ ICEMAN '&SORTPARM'
IF &LASTCC = 0 THEN DO
   WRITE
   WRITE ((( SORT WAS SUCESSFUL )))
   END
FREE FI(SORTIN SORTOUT SYSIN SYSPRINT SORTWK01 SORTWK02)
FREE FI(SORTWK03 SORTMSG) ATTR(IND,SRTDD)
ALLOC FI(SYSPRINT) DA(*)
ALLOC FI(SYSIN) DA(*)
DEL SORTCARD.&SYSUID..DATA
THEEND: END
