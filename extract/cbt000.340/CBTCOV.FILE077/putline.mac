         MACRO
&NAME    PUTLINE &PARM=,&UPT=,&ECT=,&ECB=,&OUTPUT=,&TERMPUT=,&ENTRY=,  *
               &MF=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*STATUS:                                                             *
.*   CHANGE LEVEL 000.                                                *
.*                                                                    *
.*FUNCTION:                                                           *
.*   THE PUTLINE MACRO EXAMINES PARAMETERS SPECIFIED IN THE PUTLINE   *
.*   MACRO INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE *
.*   PUTLINE SERVICE ROUTINE (REFER TO SECTION 3.48.10.1 FOR THE      *
.*   PARAMETER BLOCK FORMAT).                                         *
.*   IN THE LIST FORM, THE PUTLINE MACRO GENERATES THE PUTLINE        *
.*   PARAMETER BLOCK FORMAT CONSISTING OF ONE WORD OF CONTROL         *
.*   (CONTROL AND TOUTPUT OPTIONS), THE ADDRESS OF THE OUTPUT LINE,   *
.*   AND THE ADDRESS OF THE FORMATTED LINE.                           *
.*   IN THE EXECUTE FORM OF THE PUTLINE MACRO, CODE IS GENERATED      *
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *
.*   BE GENERATED TO INVOKE THE PUTLINE SERVICE ROUTINE UNLESS AN     *
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    *
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              *
.*                                                                    *
.*ENTRY POINTS:                                                       *
.*   PUTLINE MACRO CALL - ONLY ENTRY POINT.                           *
.*                                                                    *
.*INPUT:                                                              *
.*   PARAMETERS SPECIFIED IN THE PUTLINE MACRO FORMAT (FOR FORMAT     *
.*   SPECIFICATIONS REFER TO SECTION 3.48.10.1).                      *
.*                                                                    *
.*OUTPUT:                                                             *
.*   IN THE L-FORM, A 3-WORD PARAMETER BLOCK (REFER TO SECTION        *
.*   3.48.10.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO, THE  *
.*   ADDRESS OF THE OUTPUT LINE, AND THE ADDRESS OF THE FORMATTED     *
.*   LINE.                                                            *
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *
.*                                                                    *
.*EXTERNAL REFERENCES:                                                *
.*   LINK MACRO.                                                      *
.*                                                                    *
.*EXITS,NORMAL:                                                       *
.*   IN THE L-FORM, RETURN TO CALLER.                                 *
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE PUTLINE SERVICE   *
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  *
.*   SECTION 3.48.1.5).                                               *
.*                                                                    *
.*EXITS,ERROR:                                                        *
.*   INVALID INPUT.                                                   *
.*                                                                    *
.*TABLES/WORK AREAS:                                                  *
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *
.*   TO THE SPECIFIED MACRO PARAMETERS, THE ADDRESS OF THE OUTPUT     *
.*   LINE, AND THE ADDRESS OF THE FORMATTED LINE.                     *
.*                                                                    *
.*ATTRIBUTES:                                                         *
.*   SERIALLY REUSABLE.                                               *
.*                                                                    *
.*NOTES:                                                              *
.*   DATE OF CURRENT RELEASE :                                        *
.*      01/22/70                                                      *
.*   PREVIOUS RELEASE DATES :                                         *
.*      1/12/70                                                       *
.*      12/12/69                                                      *
.*      10/22/69                                                      *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.**********************************************************************
         LCLA  &A
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B7,&B8,&B9
         LCLB  &BS1,&BS2,&BS3,&BS4
         LCLC  &C
.E1      AIF   (T'&MF EQ 'O').ERR1     NO MF PARAMETER
         AIF   (N'&SYSLIST EQ 0).MF1   POSITIONAL PARAMETERS INVALID
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR4
.MF1     AIF   ('&MF' EQ 'L').L1       LIST FORM
.**********************************************************************
.*                                                                    *
.*                               EXECUTE FORM                         *
.*                                                                    *
.**********************************************************************
         AIF   ('&MF(1)' NE 'E').ERR1
         AIF   (T'&NAME EQ 'O').E2
&NAME    DS    0H
.E2      AIF   (T'&MF(2) EQ 'O').ERR1
         AIF   ('&MF(2)' EQ '(1)').E3  PARM LIST ADDR IN REG 1
         AIF   ('&MF(2)'(1,1) EQ '(').WARN3
.E2A     LA    1,&MF(2)                LOAD PARM LIST ADDR
.E3      AIF   (T'&UPT EQ 'O').E4      UPT OMITTED
         AIF   ('&UPT'(1,1) EQ '(').E3A
         LA    14,&UPT                 LOAD UPT ADDR
         ST    14,0(0,1)               STORE UPT ADDR IN PARM LIST
         AGO   .E4
.E3A     ANOP
&A       SETA  K'&UPT-2
&C       SETC  '&UPT'(2,&A)
         ST    &C,0(0,1)     STORE UPT ADDR IN PARM LIST
.E4      AIF   (T'&ECT EQ 'O').E5      ECT OMITTED
         AIF   ('&ECT'(1,1) EQ '(').E4A
         LA    14,&ECT                 LOAD ECT ADDR
         ST    14,4(0,1)               STORE ECT ADDR IN PARM LIST
         AGO   .E5
.E4A     ANOP
&A       SETA  K'&ECT-2
&C       SETC  '&ECT'(2,&A)
         ST    &C,4(0,1)               STORE ECT ADDR IN PARM LIST
.E5      AIF   (T'&ECB EQ 'O').E6      ECB OMITTED
         AIF   ('&ECB'(1,1) EQ '(').E5A
         LA    14,&ECB                 LOAD ECB ADDR
         ST    14,8(0,1)               STORE ECB ADDR IN PARM LIST
         AGO   .E6
.E5A     ANOP
&A       SETA  K'&ECB-2
&C       SETC  '&ECB'(2,&A)
         ST    &C,8(0,1)               STORE ECB ADDR IN PARM LIST
.E6      AIF   (T'&PARM EQ 'O').E6B    PARM OMITTED
         AIF   ('&PARM'(1,1) EQ '(').E6A
         LA    14,&PARM                LOAD PARM ADDR
         ST    14,12(0,1)              STORE PARM ADDR IN PARM LIST
         AGO   .E7
.E6A     ANOP
&A       SETA  K'&PARM-2
&C       SETC  '&PARM'(2,&A)
         ST    &C,12(0,1)              STORE PARM ADDR IN PARM LIST
         AIF   (N'&OUTPUT EQ 0 AND N'&TERMPUT EQ 0).E11
         LR    14,&C                   LOAD PARM ADDR
         AGO   .E7
.E6B     AIF   (N'&OUTPUT EQ 0 AND N'&TERMPUT EQ 0).E11
         L     14,12(0,1)              LOAD PARM BLOCK ADDR
.E7      AIF   (N'&OUTPUT EQ 0   AND N'&TERMPUT EQ 0).E11
&A       SETA  N'&OUTPUT
         AIF   (&A EQ 0).E9            OUTPUT OMITTED
         AIF   ('&OUTPUT' EQ '()').E7L ALL OPERANDS OMITTED
         AIF   (&A GT 4).ERR6          TOO MANY OPERANDS
         AIF   (&A EQ 1).E7L           OUTPUT LINE ADDR ONLY
.E7A     AIF   (T'&OUTPUT(&A) EQ 'O').E7K OMITTED OPERAND
         AIF   ('&OUTPUT(&A)' NE 'DATA').E7B
&B1      SETB  1
         AGO   .E7C
.E7B     AIF   ('&OUTPUT(&A)' NE 'INFOR').E7D
&B5      SETB  1
.E7C     AIF   (&BS1).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .E7K
.E7D     AIF   ('&OUTPUT(&A)' NE 'MULTLIN').E7E
         AIF   ('&OUTPUT(2)' NE 'DATA' AND '&OUTPUT(3)' NE 'DATA' AND  *
               '&OUTPUT(4)' NE 'DATA').ERR2 MULTLIN MUST BE DATA
&B3      SETB  1
         AGO   .E7G
.E7E     AIF   ('&OUTPUT(&A)' NE 'MULTLVL').E7F
         AIF   ('&OUTPUT(2)' EQ 'DATA' OR '&OUTPUT(3)' EQ 'DATA' OR    *
               '&OUTPUT(4)' EQ 'DATA').ERR5 MULTLVL MUST BE INFOR
&B4      SETB  1
         AGO   .E7G
.E7F     AIF   ('&OUTPUT(&A)' NE 'SINGLE').E7H
&B2      SETB  1
.E7G     AIF   (&BS2).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
         AGO   .E7K
.E7H     AIF   ('&OUTPUT(&A)' NE 'FORMAT').E7I
&B9      SETB  1
         AGO   .E7J
.E7I     AIF   ('&OUTPUT(&A)' NE 'TERM').ERR6
&B9      SETB  0
.E7J     AIF   (&BS3).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS3     SETB  1
.E7K     ANOP
&A       SETA  &A-1
         AIF   (&A NE 1).E7A
         AIF   (&B9 AND (&B1 OR &B3 OR &B4)).ERR3 MUTUALLY EXCLUSIVE
.E7L     AIF   (&BS1).E7M              SET DEFAULT VALUES
&B5      SETB  1
.E7M     AIF   (&BS2).E7N              SET DEFAULT VALUES
&B2      SETB  1
.E7N     AIF   (&BS3).E7O              SET DEFAULT VALUES
&B9      SETB  0
.E7O     MVI   0(14),B'00&B1.&B2.&B3.&B4.&B5.0' SET CONTROL FIELD
         MVI   1(14),B'00&B9.00000'    SET CONTROL FIELD
.E8      AIF   (T'&OUTPUT(1) EQ 'O' OR '&OUTPUT' EQ '()').WARN1
         AIF   ('&OUTPUT(1)'(1,1) EQ '(').E8A
         LA    0,&OUTPUT(1)            LOAD ADDR OF OUTPUT
         ST    0,4(0,14)               STORE ADDR OF OUTPUT
         AGO   .E8B
.E8A     ANOP
&A       SETA  K'&OUTPUT(1)-2
&C       SETC  '&OUTPUT(1)'(2,&A)
         ST    &C,4(0,14)              STORE ADDR OF OUTPUT
.E8B     ANOP
&B3      SETB  0
&B4      SETB  0
&B5      SETB  0
&B6      SETB  0
&B7      SETB  0
&BS1     SETB  0
&BS2     SETB  0
&BS3     SETB  0
.E9      ANOP
&A       SETA  N'&TERMPUT
         AIF   (&A EQ 0).E11           NO TREMPUT OPERANDS
         AIF   (&A GT 4).ERR7          TOO MANY OPERANDS
.E9A     AIF   ('&TERMPUT' EQ '()').E9O    ALL OPERANDS OMITTED
         AIF   (T'&TERMPUT(&A) EQ 'O').E9N OMITTED OPERAND
         AIF   ('&TERMPUT(&A)' NE 'BREAKIN').E9B
&B5      SETB  1
         AGO   .E9C
.E9B     AIF   ('&TERMPUT(&A)' NE 'NOBREAK').E9D
.E9C     AIF   (&BS1).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .E9N
.E9D     AIF   ('&TERMPUT(&A)' NE 'HOLD').E9E
&B4      SETB  1
         AGO   .E9F
.E9E     AIF   ('&TERMPUT(&A)' NE 'NOHOLD').E9G
.E9F     AIF   (&BS2).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
         AGO   .E9N
.E9G     AIF   ('&TERMPUT(&A)' NE 'NOWAIT').E9H
&B3      SETB  1
         AGO   .E9I
.E9H     AIF   ('&TERMPUT(&A)' NE 'WAIT').E9J
.E9I     AIF   (&BS3).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS3     SETB  1
         AGO   .E9N
.E9J     AIF   ('&TERMPUT(&A)' NE 'CONTROL').E9K
&B6      SETB  1
         AGO   .E9M
.E9K     AIF   ('&TERMPUT(&A)' NE 'ASIS').E9L
&B7      SETB  1
         AGO   .E9M
.E9L     AIF   ('&TERMPUT(&A)' NE 'EDIT').ERR7
.E9M     AIF   (&BS4).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS4     SETB  1
.E9N     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).E9A
.E9O     ANOP
         MVI   2(14),B'000&B3.&B4.&B5.&B6.&B7' SET TOUTPUT FIELD
.E11     AIF   (T'&ENTRY EQ 'O').E12   ENTRY POINT OMITTED
         AIF   ('&ENTRY' EQ '(15)').E11A
         AIF   ('&ENTRY'(1,1) EQ '(').WARN4
.E11A1   LA    15,&ENTRY               LOAD EP ADDR
.E11A    BALR  14,15                   BRANCH TO SERVICE ROUTINE
         MEXIT
.E12     LINK  EP=IKJPUTL              LINK TO SERVICE ROUTINE
         MEXIT
.**********************************************************************
.*                                                                    *
.*                                LIST FORM                           *
.*                                                                    *
.**********************************************************************
.L1      DS    0F
         AIF   (N'&OUTPUT EQ 0   AND N'&TERMPUT EQ 0).L2O
&A       SETA  N'&OUTPUT
         AIF   (&A EQ 0).L1P           NO OUTPUT OPERANDS
         AIF   ('&OUTPUT' EQ '()').L1O ALL OPERANDS OMITTED
         AIF   (&A GT 4).ERR6          TOO MANY OPERANDS
         AIF   (&A EQ 1).L1O           OUTPUT ADDR ONLY
.L1A     AIF   (T'&OUTPUT(&A) EQ 'O').L1K OMITTED OPERAND
         AIF   ('&OUTPUT(&A)' NE 'DATA').L1B
&B1      SETB  1
         AGO   .L1C
.L1B     AIF   ('&OUTPUT(&A)' NE 'INFOR').L1D
&B5      SETB  1
.L1C     AIF   (&BS1).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .L1K
.L1D     AIF   ('&OUTPUT(&A)' NE 'MULTLIN').L1E
         AIF   ('&OUTPUT(2)' NE 'DATA' AND '&OUTPUT(3)' NE 'DATA' AND  *
               '&OUTPUT(4)' NE 'DATA').ERR2 MULTLIN MUST BE DATA
&B3      SETB  1
         AGO   .L1G
.L1E     AIF   ('&OUTPUT(&A)' NE 'MULTLVL').L1F
         AIF   ('&OUTPUT(2)' EQ 'DATA' OR '&OUTPUT(3)' EQ 'DATA' OR    *
               '&OUTPUT(4)' EQ 'DATA').ERR5 MULTLVL MUST BE INFOR
&B4      SETB  1
         AGO   .L1G
.L1F     AIF   ('&OUTPUT(&A)' NE 'SINGLE').L1H
&B2      SETB  1
.L1G     AIF   (&BS2).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
         AGO   .L1K
.L1H     AIF   ('&OUTPUT(&A)' NE 'FORMAT').L1I
&B9      SETB  1
         AGO   .L1J
.L1I     AIF   ('&OUTPUT(&A)' NE 'TERM').ERR6
&B9      SETB  0
.L1J     AIF   (&BS3).ERR6             MUTUALLY EXCLUSIVE OPERANDS
&BS3     SETB  1
.L1K     ANOP
&A       SETA  &A-1
         AIF   (&A NE 1).L1A
         AIF   (&B9 AND (&B1 OR &B3 OR &B4)).ERR3 MUTUALLY EXCLUSIVE
.L1L     AIF   (&BS1).L1M              SET DEFAULT VALUES
&B5      SETB  1
.L1M     AIF   (&BS2).L1N              SET DEFAULT VALUES
&B2      SETB  1
.L1N     AIF   (&BS3).L1N1             SET DEFAULT VALUES
&B9      SETB  0
.L1N1    ANOP
&NAME    DC    B'00&B1.&B2.&B3.&B4.&B5.0' SET CONTROL FIELD
         DC    B'00&B9.00000'          SET CONTROL FIELD
         AGO   .L2
.L1O     ANOP
&NAME    DC    X'1240'                 SET CONTROL FIELD
         AGO   .L2AA
.L1P     ANOP
&NAME    DC    X'0000'                 SET CONTROL FIELD
         AGO   .L2AA
.L2      ANOP
&B4      SETB  0
&B5      SETB  0
&B8      SETB  0
&BS1     SETB  0
&BS2     SETB  0
&BS3     SETB  0
.L2AA    ANOP
&A       SETA  N'&TERMPUT
         AIF   (&A EQ 0).L2P           NO TERMPUT OPERANDS
         AIF   ('&TERMPUT' EQ '()').L2NN ALL OPERANDS OMITTED
         AIF   (&A GT 4).ERR7          TOO MANY OPERANDS
.L2A     AIF   (T'&TERMPUT(&A) EQ 'O').L2N OMITTED OPERAND
         AIF   ('&TERMPUT(&A)' NE 'BREAKIN').L2B
&B6      SETB  1
         AGO   .L2C
.L2B     AIF   ('&TERMPUT(&A)' NE 'NOBREAK').L2D
.L2C     AIF   (&BS1).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS1     SETB  1
         AGO   .L2N
.L2D     AIF   ('&TERMPUT(&A)' NE 'HOLD').L2E
&B5      SETB  1
         AGO   .L2F
.L2E     AIF   ('&TERMPUT(&A)' NE 'NOHOLD').L2G
.L2F     AIF   (&BS2).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS2     SETB  1
         AGO   .L2N
.L2G     AIF   ('&TERMPUT(&A)' NE 'NOWAIT').L2H
&B4      SETB  1
         AGO   .L2I
.L2H     AIF   ('&TERMPUT(&A)' NE 'WAIT').L2J
.L2I     AIF   (&BS3).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS3     SETB  1
         AGO   .L2N
.L2J     AIF   ('&TERMPUT(&A)' NE 'CONTROL').L2K
&B7      SETB  1
         AGO   .L2M
.L2K     AIF   ('&TERMPUT(&A)' NE 'ASIS').L2L
&B8      SETB  1
         AGO   .L2M
.L2L     AIF   ('&TERMPUT(&A)' NE 'EDIT').ERR7
.L2M     AIF   (&BS4).ERR7             MUTUALLY EXCLUSIVE OPERANDS
&BS4     SETB  1
.L2N     ANOP
&A       SETA  &A-1
         AIF   (&A NE 0).L2A
.L2NN    ANOP
         DC    B'000&B4.&B5.&B6.&B7.&B8' SET TOUTPUT OPTIONS FIELD
         DC    X'00'                   SET TOUTPUT OPTIONS FIELD
         AGO   .L3
.L2O     ANOP
&NAME    DC    X'00000000'       SET CONTROL AND TOUTPUT OPTIONS FIELDS
         DC    A(0)                    ADDR OF OUTPUT LINE
         DC    A(0)                    ADDRESS OF FORMATTED LINE
         MEXIT
.L2P     DC    X'0000'                 SET TOUTPUT OPTIONS FIELD
.L3      AIF   (N'&OUTPUT EQ 0).L4
         AIF   (T'&OUTPUT(1) EQ 'O' OR '&OUTPUT' EQ '()').WARN2
         DC    A(&OUTPUT(1))           ADDR OF OUTPUT LINE
         DC    A(0)                    ADDRESS OF FORMATTED LINE
         MEXIT
.L4      DC    A(0)                    ADDR OF OUTPUT LINE
         DC    A(0)                    ADDRESS OF FORMATTED LINE
.L5      MEXIT
.WARN1   MNOTE 0,'OUTPUT ADDR IS ASSUMED TO BE IN PARM BLOCK'
         AGO   .E8B
.WARN2   MNOTE 0,'FIRST OUTPUT OPERAND HAS BEEN OMITTED'
         AGO   .L4
.WARN3   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'
         AGO   .E2A
.WARN4   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'
         AGO   .E11A1
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'
         MEXIT
.ERR2    MNOTE 8,'MULTLIN MESSAGES MUST BE DATA MESSAGES'
         MEXIT
.ERR3    MNOTE 8,'FORMAT MESSAGES MUST BE SINGLE LINE INFOR MESSAGES'
         MEXIT
.ERR4    MNOTE 8,'POSITIONAL PARAMETERS ARE INVALID'
         MEXIT
.ERR5    MNOTE 8,'MULTLVL MESSAGES MUST BE INFOR MESSAGES'
         MEXIT
.ERR6    MNOTE 8,'INVALID OPERAND IN OUTPUT PARAMETER SUBLIST'
         MEXIT
.ERR7    MNOTE 8,'INVALID OPERAND IN TERMPUT PARAMETER SUBLIST'
         MEXIT
         MEND
