         MACRO
&LABEL   IKJEBESH &COMAREA,&NAME,&PARAM=,&MF=
         LCLA  &A,&B,&C,&D,&E,&I
         LCLC  &OPERN,&OPERN1,&OPERN2,&REG,&REGNO,&RTN
         AIF   ('&MF(1)' NE 'E').ERROR1
         AIF   (N'&PARAM EQ 0 OR N'&PARAM GT 2).ERROR2
         AIF   ('&NAME' EQ '').ERROR3
         AIF   ('&NAME'(1,1) EQ '(').TWOP
         AIF   ('&NAME'(1,6) NE 'IKJEBE').ERROR4
         AIF   ('&NAME'(7,8) EQ 'MS').MSRTN
         AIF   ('&NAME'(7,8) EQ 'LE').LERTN
         AIF   ('&NAME'(7,8) EQ 'UT').UTRTN
.*
.* SET PARMREG0 AND PARMREG1 FOR MACRO EXPANSION.
.*
.TWOP    ANOP
&C       SETA  0
         AIF   ('&LABEL' EQ '').TWOP0
&LABEL   EQU   *
.TWOP0   ANOP
         AIF   (N'&PARAM EQ 1).ONEPARM
&A       SETA  1
&B       SETA  N'&PARAM
&I       SETA  1
&REGNO   SETC  '0'
.TWOP1   ANOP
         AIF   ('&PARAM(&I)' EQ '').TWOP3
         AIF   ('&PARAM(&I)'(1,1) EQ '(').TWOP2
         LA    PARMREG&REGNO,&PARAM(&I) -   LOAD PARAMETER REGISTER
         AGO   .TWOP3
.TWOP2   ANOP
         AIF   (K'&PARAM(&I) LE 8).TWOP4
&D       SETA  K'&PARAM(&I)
&E       SETA  &D-2
&REG     SETC  '&PARAM(&I)'(2,&E)
         LR    PARMREG&REGNO,&REG
         AGO   .TWOP3
.TWOP4   ANOP
         AIF   (&B EQ 1 AND '&PARAM(&I)' EQ '(1)').TWOP3
         AIF   (&B EQ 2 AND &I EQ 1 AND '&PARAM(&I)' EQ '(0)').TWOP3
         AIF   (&B EQ 2 AND &I EQ 2 AND '&PARAM(&I)' EQ '(1)').TWOP3
         LR    PARMREG&REGNO,&PARAM(&I)
.TWOP3   ANOP
&REGNO   SETC  '1'
&A       SETA  &A+1
&I       SETA  &I+1
         AIF   (&A GT &B).TWOOUT
         AGO   .TWOP1
.ONEPARM ANOP
&A       SETA  0
&B       SETA  1
&REGNO   SETC  '1'
&I       SETA  1
         AGO   .TWOP1
.*
.* GENERATE LINK MACRO EXPANSION.
.*
.TWOOUT  ANOP
         AIF   (&C NE 0).LOAD
         AIF   ('&NAME'(1,1) EQ '(').EPLOC
         LINK  EP=&NAME
         MEXIT
.EPLOC   ANOP
         LINK  EPLOC=&NAME,SF=&MF
         MEXIT
.*
.* GENERATE LABELS FOR LOAD INSTRUCTIONS FOR PERMANENTLY
.* RESIDENT SERVICE ROUTINES.
.*
.MSRTN   ANOP
&LABEL   L     RETCDREG,(CAPTMS-IKJEBECA)(,COMMREG) - ENTRY ADDRESS
         AGO   .LOADOUT
.LERTN   ANOP
&LABEL   L     RETCDREG,(CAPTLE-IKJEBECA)(,COMMREG) - ENTRY ADDRESS
         AGO   .LOADOUT
.UTRTN   ANOP
&LABEL   L     RETCDREG,(CAPTUT-IKJEBECA)(,COMMREG) - ENTRY ADDRESS
.LOADOUT ANOP
&C       SETA  1
         AGO   .TWOP0
.*
.* GENERATE BRANCH LINKAGE TO PERMANENTLY RESIDENT
.* SERVICE ROUTINE.
.*
.LOAD    ANOP
         BALR  RETREG,RETCDREG
         MEXIT
.ERROR1  MNOTE 12,'ONLY EXECUTE FORM SUPPORTED'
         MEXIT
.ERROR2  MNOTE 12,'INVALID NUMBER OF PARAMETERS'
         MEXIT
.ERROR3  MNOTE 12,'MODULE NAME MUST BE SUPPLIED'
         MEXIT
.ERROR4  MNOTE 12,'A NON-EDIT MODULE NAME SUPPLIED'
         MEND
