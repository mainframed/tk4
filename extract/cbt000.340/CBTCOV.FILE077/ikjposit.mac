         MACRO
&LABEL   IKJPOSIT &TYPNAM,&OPTION,&OPTION2,&OPTION3,&OPTION4,          *
               &PROMPT=,&DEFAULT=,&HELP=,&VALIDCK=
.*                                                               S21105
.* RELEASE 20 SUPPORT CODE -- 20035
         GBLC  &IKJCSNM,&IKJDSNM
         LCLA  &TYPENO,&PCLSIZ,&T,&END
         LCLB  &FLIST,&FASIS,&FPRPT,&FDFLT,&FHELP,&FRANGE,&FVALID,&FEND*
               BIT,&FQSTR
.*                                                               S21105
&FENDBIT SETB  0
         AIF   (N'&SYSLIST LT 6).GOOD
.*                                                               S21105
         MNOTE 8,'NO MORE THAN 5 POSITIONAL PARAMETERS ALLOWED'
.*                                                               S21105
&FENDBIT SETB  1
.GOOD    ANOP
         AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&FENDBIT SETB  1
.GOOD2   ANOP
&FLIST   SETB  ('&OPTION' EQ 'LIST' OR '&OPTION2' EQ 'LIST' OR         *
               '&OPTION3' EQ 'LIST' OR '&OPTION4' EQ 'LIST')
.*                                                               S21105
&FASIS   SETB  ('&OPTION' EQ 'ASIS' OR '&OPTION2' EQ 'ASIS' OR         *
               '&OPTION3' EQ 'ASIS' OR '&OPTION4' EQ 'ASIS')
.*                                                               S21105
&FRANGE  SETB  ('&OPTION' EQ 'RANGE' OR '&OPTION2' EQ 'RANGE' OR       *
               '&OPTION3' EQ 'RANGE' OR '&OPTION4' EQ 'RANGE')
.*                                                               S21105
&FQSTR   SETB  ('&OPTION' EQ 'SQSTRING' OR '&OPTION2' EQ 'SQSTRING' OR *
               '&OPTION3' EQ 'SQSTRING' OR '&OPTION4' EQ 'SQSTRING')
.*                                                               S21105
         AIF   ('&OPTION' EQ '' OR '&OPTION' EQ 'LIST' OR '&OPTION'    *
               EQ 'RANGE' OR '&OPTION' EQ 'ASIS' OR '&OPTION'          *
               EQ 'UPPERCASE' OR '&OPTION' EQ 'SQSTRING').OPT1
.*                                                               S21105
         MNOTE 8,'''&OPTION'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT1    AIF   ('&OPTION2' EQ '' OR '&OPTION2' EQ 'LIST' OR '&OPTION2' *
               EQ 'RANGE' OR '&OPTION2' EQ 'ASIS' OR '&OPTION2' EQ     *
               'UPPERCASE' OR '&OPTION2' EQ 'SQSTRING').OPT2
.*                                                               S21105
         MNOTE 8,'''&OPTION2'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT2    AIF   ('&OPTION3' EQ '' OR '&OPTION3' EQ 'LIST' OR '&OPTION3' *
               EQ 'RANGE' OR '&OPTION3' EQ 'ASIS' OR '&OPTION3' EQ     *
               'UPPERCASE' OR '&OPTION3' EQ 'SQSTRING').OPT4
.*                                                               S21105
         MNOTE 8,'''&OPTION3'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT4    AIF   ('&OPTION4' EQ '' OR '&OPTION4' EQ 'LIST' OR '&OPTION4' *
               EQ 'RANGE' OR '&OPTION4' EQ 'ASIS' OR '&OPTION4' EQ     *
               'UPPERCASE' OR '&OPTION4' EQ 'SQSTRING').OPT3
.*                                                               S21105
         MNOTE 8,'''&OPTION4'' IS AN INVALID POSITIONAL PARAMETER'
&FENDBIT SETB  1
.OPT3    AIF   (('&OPTION' EQ 'UPPERCASE' OR '&OPTION2' EQ             *
               'UPPERCASE' OR '&OPTION3' EQ 'UPPERCASE' OR '&OPTION4'  *
               EQ 'UPPERCASE') AND &FASIS).ERROR0
         AGO   .A0
.ERROR0  MNOTE 8,'BOTH ''UPPERCASE'' AND ''ASIS'' CANNOT BE SPECIFIED'
&FENDBIT SETB  1
.A0      ANOP
         AIF   (('&TYPNAM' EQ 'DELIMITER' OR '&TYPNAM' EQ 'SPACE')     *
               AND ('&PROMPT' NE '' OR '&DEFAULT' NE '' OR '&HELP' NE  *
               '' OR '&VALIDCK' NE '')).ERROR3
&FPRPT   SETB  ('&PROMPT' NE '')
&FDFLT   SETB  ('&DEFAULT' NE '')
&FHELP   SETB  ('&HELP' NE '')
&FVALID  SETB  ('&VALIDCK' NE '')
         AGO   .A1
.ERROR3  MNOTE  4,'PROMPT, DEFAULT, HELP, VALIDCK IGNORED WITH &TYPNAM'
.A1      AIF   (('&TYPNAM' NE 'ADDRESS' AND '&TYPNAM' NE 'VALUE')      *
               AND &FRANGE).RANGE
         AGO   .A15
.RANGE   MNOTE 4,'''RANGE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FRANGE  SETB  0
.A15     AIF   (&FDFLT AND &FPRPT).ERROR1
         AGO   .A2
.ERROR1  MNOTE 8,'PARAMETER CANNOT BOTH DEFAULT AND REQUIRE PROMPTING'
&FENDBIT SETB  1
.A2      AIF   ('&LABEL' EQ '').ERROR2
         AGO   .A3
.ERROR2  MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
&FENDBIT SETB  1
.A3      AIF   (('&TYPNAM' NE 'STRING') AND &FQSTR).ERROR4
.*                                                               S21105
         AGO   .A4
.ERROR4  MNOTE 8,'''SQSTRING'' OPTION ILLEGAL WITH &TYPNAM'
.*                                                               S21105
&FENDBIT  SETB  1
.A4      AIF   (NOT &FLIST OR ( '&TYPNAM' NE 'DELIMITER' AND '&TYPNAM' *
               NE 'STRING' AND '&TYPNAM' NE 'PSTRING' AND '&TYPNAM' NE *
               'QSTRING' AND '&TYPNAM' NE 'SPACE')).ACCEPT1
         MNOTE 4,'''LIST'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FLIST   SETB  0
.ACCEPT1 AIF   (NOT &FASIS OR ( '&TYPNAM' NE  'DELIMITER' AND          *
               '&TYPNAM' NE 'SPACE')).ACCEPT2
         MNOTE 4,'''ASIS'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FASIS   SETB  0
.ACCEPT2 ANOP
         AIF   ('&OPTION' NE 'UPPERCASE' AND '&OPTION2' NE 'UPPERCASE' *
               AND '&OPTION3' NE 'UPPERCASE' OR ('&TYPNAM' NE          *
               'DELIMITER' AND '&TYPNAM' NE 'SPACE')).ACCEPT4
         MNOTE 4,'''UPPERCASE'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
.ACCEPT4 ANOP
         AIF   (NOT &FVALID OR ('&TYPNAM' NE 'STRING')).ACCEPT3
         MNOTE 4,'''VALIDCK'' OPTION ILLEGAL WITH &TYPNAM - IGNORED'
&FVALID  SETB  0
.ACCEPT3 ANOP
&TYPENO  SETA  1
         AIF   ('&TYPNAM' EQ 'DELIMITER').TYPED
&TYPENO  SETA  10
         AIF   ('&TYPNAM' EQ 'SPACE').TYPED
&TYPENO  SETA  3
&PCLSIZ  SETA  2
         AIF   ('&TYPNAM' EQ 'VALUE').TYPED
&TYPENO  SETA  2
         AIF   ('&TYPNAM' EQ 'STRING').TYPED
&TYPENO  SETA  5
         AIF   ('&TYPNAM' EQ 'PSTRING').TYPED
&TYPENO  SETA  9
         AIF   ('&TYPNAM' EQ 'QSTRING').TYPED
&PCLSIZ  SETA  4
&TYPENO  SETA  6
         AIF   ('&TYPNAM' EQ 'USERID').TYPED
&TYPENO  SETA  4
&PCLSIZ  SETA  9
         AIF   ('&TYPNAM' EQ 'ADDRESS').TYPED
&PCLSIZ  SETA  6
&TYPENO  SETA  7
         AIF   ('&TYPNAM' EQ 'DSNAME').TYPED
&TYPENO  SETA  8
         AIF   ('&TYPNAM' EQ 'DSTHING').TYPED
         AIF   ('&TYPNAM' EQ '').NULTYPE
         AGO   .ILLTYPE
.NULTYPE MNOTE 8,'POSITIONAL PARAMETER TYPE FIELD MISSING BUT REQUIRED'
&FENDBIT SETB  1
         AGO   .TYPED
.ILLTYPE ANOP
         MNOTE 8,'ILLEGAL POSITIONAL TYPE - ''&TYPNAM'''
.END     MEXIT
.TYPED   AIF   (&FENDBIT).END
&IKJCSNM CSECT ,                       RESUME PCL CSECT
POST&SYSNDX EQU *
         DC    BL2'001&FPRPT&FDFLT.0&FHELP&FVALID&FLIST&FASIS&FRANGE.0&*
               FQSTR.000'                                        S21105
         DC    AL2(PEND&SYSNDX-POST&SYSNDX) LENGTH OF PCE
         DC    AL2(&LABEL-&IKJDSNM)    OFFSET TO PDE
         DC    AL1(&TYPENO)            TYPE OF POSITIONAL
         AIF   (&FPRPT).PRPT           PROMPT
         AIF   (NOT &FDFLT).NPRDF      NEITHER
         DC    AL1(IKJ@&SYSNDX-*-2),C&DEFAULT DEFAULT DATA
         AGO   .EQUGEN
.PRPT    DC    AL1(IKJ@&SYSNDX-*-2),C&PROMPT PROMPT DATA
.EQUGEN  ANOP
IKJ@&SYSNDX EQU *
.NPRDF   ANOP
         AIF   (NOT &FHELP).NOHELP     IS HELP SPECIFIED
         DC    AL2(IKJ$&SYSNDX-*)      LENGTH OF HELP DATA
&T       SETA  1
.HELPLP  ANOP
&T       SETA  &T+1                    NEXT HELP
         AIF   (&T EQ 256).HELPER
         AIF   (&T LE N'&HELP).HELPLP
         AGO   .HELPCT
.HELPER  MNOTE 4,'ONLY 255 LEVELS OF HELP AVAILABLE. OTHERS IGNORED'
&T       SETA  &T+1                    PROVIDE FOR DECREMENT
.HELPCT  ANOP
&T       SETA  &T-1
         DC    AL1(&T)                 NUMBER OF HELP MESSAGES
&END     SETA  &T
&T       SETA  1
.HELPLPA ANOP
         DC    AL2(IKJ&SYSNDX.&T-*),AL2(00),C&HELP(&T) HELP MSG SEGMENT
IKJ&SYSNDX.&T EQU *
&T       SETA  &T+1
         AIF   (&T LE &END).HELPLPA
IKJ$&SYSNDX EQU *
.NOHELP  AIF   (NOT &FVALID).NOVALID   IS VALIDCK SPECIFIED
         DC    AL3(&VALIDCK)           VALIDCK ROUTINE ADDRESS
.NOVALID ANOP
PEND&SYSNDX EQU *
&IKJDSNM DSECT
&LABEL   DS    &PCLSIZ.A               ALLOCATE PDE SPACE
         AIF   (NOT &FRANGE).NRNGGEN
         DS    &PCLSIZ.A               ALLOCATE ADDITIONAL PDE SPACE
.NRNGGEN ANOP
         AIF   (NOT &FLIST).NLSTGEN
         DS    A                       FOR LIST CHAIN POINTER
.NLSTGEN ANOP
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
