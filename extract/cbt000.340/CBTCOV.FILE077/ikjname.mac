         MACRO
         IKJNAME &NAME,&SUBFLD=,&INSERT=
.* RELEASE 20 SUPPORT CODE -- 20035
         GBLC  &IKJCSNM
         LCLB  &FSUBF,&FINSERT,&EXITBIT
         LCLC  &QUOTE
&FSUBF   SETB  ('&SUBFLD' NE '')       SUBFIELD FLAG
&FINSERT SETB  ('&INSERT' NE '')
         AIF   (N'&SYSLIST LT 2).A0
         MNOTE 8,'NO MORE THAN 1 POSITIONAL PARAMETER ALLOWED'
&EXITBIT SETB  1
.A0      AIF   ('&NAME' NE '').A1
         MNOTE 8,'NAME OF KEYWORD IS MISSING'
&EXITBIT SETB  1
.A1      AIF   ('&IKJCSNM' NE '').GOOD2
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
.END     MEXIT
.GOOD2   AIF   (&EXITBIT).END
&IKJCSNM CSECT ,                       RESUME PCL CSECT
NAME&SYSNDX EQU *
         DC    BL2'01100&FSUBF.00000&FINSERT.0000' FLAGS
         DC    AL2(NEND&SYSNDX-NAME&SYSNDX) LENGTH OF PCE
.*
.* THE FOLLOWING CODE SUPPLIES QUOTES TO &NAME IF THEY ARE MISSING.
.*
&QUOTE   SETC  '&NAME'(1,1)
         AIF   ('&QUOTE' EQ '''').QUOT
&QUOTE   SETC  ''''
         DC    AL1(IKJ@&SYSNDX-*-2),C&QUOTE&NAME&QUOTE NAME VALID FOR K*
               EYWORD
         AGO   .QUOTED
.QUOT    ANOP
         DC    AL1(IKJ@&SYSNDX-*-2),C&NAME NAME VALID FOR KEYWORD
.QUOTED  ANOP
IKJ@&SYSNDX EQU *
         AIF   (NOT &FSUBF).NOSUBF
         DC    AL2(&SUBFLD-&IKJCSNM)   OFFSET TO SUBFIELD
.NOSUBF  AIF   (NOT &FINSERT).NOINSRT
         DC    AL1(IKJ$&SYSNDX-*-2),C&INSERT INSERT DATA
IKJ$&SYSNDX EQU *
.NOINSRT ANOP
NEND&SYSNDX EQU *
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
