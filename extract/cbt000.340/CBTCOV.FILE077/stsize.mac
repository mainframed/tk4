         MACRO
&NAME    STSIZE &SIZE=,&SIZELOC=,&LINE=,&LINELOC=
.**********************************************************************
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*NAME - STSIZE                                                       *
.*                                                                    *
.*FUNCTION - THE STSIZE MACRO IS USED TO SPECIFY THE LINE SIZE OF THE *
.*   TERMINAL. THE MAXIMUM LINE SIZE THAT CAN BE SPECIFIED IS 255.    *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF EXPANSION IS BELOW                      *
.*                                                                    *
.*&NAME  L     0,10                     LOAD ENTRY CODE               *
.*       SLL   0,24                     PUT ENTRY CODE IN LEFT MOST   *
.*                                      BYTE OF REGISTER              *
.*       L     1,15                     LOAD LINE NUMBER              *
.*       OR    0,1                      PUT LINE NUMBER IN PARM REG   *
.*       L     1,120                    LOAD LINE SIZE                *
.*       SVC   94                       ISSUE SVC                     *
.*                                                                    *
.**********************************************************************
&NAME    LA    0,10                     LOAD ENTRY CODE
         SLL   0,24                     PUT CODE IN HIGH ORDER BYTE
         AIF   ('&LINE' NE '' AND '&LINELOC' EQ '').LNHERE
         AIF ('&LINE' NE '' AND '&LINELOC' NE '').ERROR
         AIF   ('&LINE' EQ '' AND '&LINELOC' NE '').LNADDR
         AGO  .SZLC
.LNHERE  AIF   ('&LINE'(1,1) EQ '(').LNREG
         LA    1,&LINE                  PUT LINE NUMBER IN REG
         AGO   .OREG
.LNREG   LR    1,&LINE                  PUT LINE NUMBER IN REG
         AGO   .OREG
.LNADDR  AIF   ('&LINELOC'(1,1) EQ '(').LNADDRG
         L     1,&LINELOC               GET LINE NUMBER
         AGO   .OREG
.LNADDRG L     1,0(&LINELOC)            GET LINE NUMBER
.OREG    OR    0,1                      PUT LINE NUMBER IN PARM REG
.SZLC    AIF ('&SIZE' NE '' AND '&SIZELOC' NE '').ERROR
         AIF ('&SIZE' EQ '' AND '&SIZELOC' EQ '').ERROR1
         AIF   ('&SIZE' NE '' AND '&SIZELOC' EQ '').SZHERE
         AIF   ('&SIZE' EQ '' AND '&SIZELOC' NE '').SZADDR
         AGO   .ERROR
.SZHERE  AIF   ('&SIZE'(1,1) EQ '(').SZREG
         LA    1,&SIZE
         AGO   .EXIT
.SZREG   ANOP
         LR    1,&SIZE                  PREPARE PARAMETER
         AGO   .EXIT
.SZADDR  AIF   ('&SIZELOC'(1,1) EQ '(').ADDREG
         L     1,&SIZELOC               PREPARE PARAMETER
         AGO   .EXIT
.ADDREG  ANOP
         L     1,0(&SIZELOC)            LOAD LINE SIZE
.EXIT    SVC   94                       ISSUE SVC
         MEXIT
.ERROR1  IHBERMAC 8,,,
         MEXIT
.ERROR   IHBERMAC 54,,,
         MEXIT
         MEND
