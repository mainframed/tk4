         MACRO
&NAME    STACK &PARM=,&UPT=,&ECT=,&ECB=,&TERM=,&STORAGE=,&DELETE=,     *
               &ENTRY=,&MF=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*STATUS:                                                             *
.*   CHANGE LEVEL 000.                                                *
.*                                                                    *
.*FUNCTION:                                                           *
.*   THE STACK MACRO EXAMINES PARAMETERS SPECIFIED IN THE STACK MACRO *
.*   INSTRUCTION AND BUILDS A PARAMETER BLOCK TO BE USED BY THE STACK *
.*   SERVICE ROUTINE (REFER TO SECTION 3.48.8.1 FOR THE PARAMETER     *
.*   BLOCK FORMAT).                                                   *
.*   IN THE LIST FORM, THE STACK MACRO GENERATES THE STACK            *
.*   PARAMETER BLOCK CONSISTING OF ONE WORD OF CONTROL (OPERATION AND *
.*   CODE) AND THE ADDRESS OF THE STORAGE LIST SOURCE DESCRIPTOR.     *
.*   IN THE EXECUTE FORM OF THE STACK MACRO, CODE IS GENERATED        *
.*   WHICH WILL RESET THE PARAMETER BLOCK IF REQUIRED AND LOAD THE    *
.*   ADDRESSES OF THE UPT, ECT, ECB, AND PARAMETER BLOCK INTO THE     *
.*   SERVICE ROUTINE PARAMETER LIST IF SPECIFIED. A LINK MACRO WILL   *
.*   BE GENERATED TO INVOKE THE STACK SERVICE ROUTINE UNLESS AN       *
.*   ENTRY POINT IS SPECIFIED. IF AN ENTRY POINT IS SPECIFIED, THE    *
.*   CODE TO ENABLE A BRANCH AND LINK WILL BE GENERATED.              *
.*                                                                    *
.*ENTRY POINTS:                                                       *
.*   STACK MACRO CALL - ONLY ENTRY POINT.                             *
.*                                                                    *
.*INPUT:                                                              *
.*   PARAMETERS SPECIFIED IN THE STACK MACRO FORMAT (FOR FORMAT       *
.*   SPECIFICATIONS REFER TO SECTION 3.48.8.1).                       *
.*                                                                    *
.*OUTPUT:                                                             *
.*   IN THE L-FORM, A 2-WORD PARAMETER BLOCK (REFER TO SECTION        *
.*   3.48.8.1) CONSISTING OF CONTROL WHICH IS SET BY THE MACRO AND    *
.*   THE ADDRESS OF THE STORAGE LIST SOURCE DESCRIPTOR.               *
.*   IN THE E-FORM, CODE WHICH RESETS CONTROL IN THE PARAMETER BLOCK, *
.*   PLACES THE ADDRESSES OF THE SERVICE ROUTINE PARAMETER BLOCK,     *
.*   UPT, ECT, AND ECB IN THE SERVICE ROUTINE PARAMETER LIST AS       *
.*   SPECIFIED, AND INVOKES THE SERVICE ROUTINE.                      *
.*                                                                    *
.*EXTERNAL REFERENCES:                                                *
.*   LINK MACRO.                                                      *
.*                                                                    *
.*EXITS,NORMAL:                                                       *
.*   IN THE L-FORM, RETURN TO CALLER.                                 *
.*   IN THE E-FORM, CODE IS GENERATED TO INVOKE THE STACK SERVICE     *
.*   ROUTINE ACCORDING TO THE STANDARD LINKAGE CONVENTIONS (REFER TO  *
.*   SECTION 3.48.1.5).                                               *
.*                                                                    *
.*EXITS,ERROR:                                                        *
.*   INVALID INPUT.                                                   *
.*                                                                    *
.*TABLES/WORK AREAS:                                                  *
.*   SERVICE ROUTINE PARAMETER LIST - A TABLE OF POINTERS TO THE UPT, *
.*   ECT, ECB, AND SERVICE ROUTINE PARAMETER BLOCK.                   *
.*   SERVICE ROUTINE PARAMETER BLOCK - A CONTROL TABLE SET ACCORDING  *
.*   TO THE SPECIFIED MACRO PARAMETERS AND THE ADDRESS OF THE STORAGE *
.*   LIST SOURCE DESCRIPTOR.                                          *
.*                                                                    *
.*ATTRIBUTES:                                                         *
.*   SERIALLY REUSABLE.                                               *
.*                                                                    *
.*NOTES:                                                              *
.*   DATE OF CURRENT RELEASE :                                        *
.*      01/22/70                                                      *
.*   PREVIOUS RELEASE DATES :                                         *
.*      12/12/69                                                      *
.*      10/22/69                                                      *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.*                                                                    *
.**********************************************************************
         LCLA  &A
         LCLC  &C
         AIF   (T'&MF EQ 'O').ERR1           NO MF PARAMETER
         AIF   (N'&SYSLIST EQ 0).MF1   POSITIONAL PARAMETERS INVALID
         AIF   (T'&SYSLIST(1) NE 'O' OR N'&SYSLIST NE 1).ERR6
.MF1     AIF   ('&MF' EQ 'L').L1             BRANCH ON LIST FORM
.**********************************************************************
.*                                                                    *
.*                               EXECUTE FORM                         *
.*                                                                    *
.**********************************************************************
         AIF   ('&MF(1)' NE 'E').ERR1
         AIF   (T'&NAME EQ 'O').E1
&NAME    DS    0H
.E1      AIF   (T'&MF(2) EQ 'O').ERR1
         AIF   ('&MF(2)' EQ '(1)').E2
         AIF   ('&MF(2)'(1,1) EQ '(').WARN3
.E1A     LA    1,&MF(2)       LOAD PARM LIST ADDR
.E2      AIF   (T'&UPT EQ 'O').E3       UPT OMITTED
         AIF   ('&UPT'(1,1) EQ '(').E2A
         LA    14,&UPT        LOAD UPT ADDR
         ST    14,0(0,1)     STORE UPT ADDR
         AGO   .E3
.E2A     ANOP
&A       SETA  K'&UPT-2
&C       SETC  '&UPT'(2,&A)
         ST    &C,0(0,1)     STORE UPT ADDR IN PARM LIST
.E3      AIF   (T'&ECT EQ 'O').E4     ECT OMITTED
         AIF   ('&ECT'(1,1) EQ '(').E3A
         LA    14,&ECT                  LOAD ECT ADDR
         ST    14,4(0,1)               STORE ECT ADDR IN PARM LIST
         AGO   .E4
.E3A     ANOP
&A       SETA  K'&ECT-2
&C       SETC  '&ECT'(2,&A)
         ST    &C,4(0,1)               STORE ECT ADDR IN PARM LIST
.E4      AIF   (T'&ECB EQ 'O').E5       ECB OMITTED
         AIF   ('&ECB'(1,1) EQ '(').E4A
         LA    14,&ECB        LOAD ECB ADDR
         ST    14,8(0,1)     STORE ECB ADDR IN PARM LIST
         AGO   .E5
.E4A     ANOP
&A       SETA  K'&ECB-2
&C       SETC  '&ECB'(2,&A)
         ST    &C,8(0,1)     STORE ECB ADDR IN PARM LIST
.E5      AIF   (T'&PARM EQ 'O').E5B     PARM OMITTED
         AIF   ('&PARM'(1,1)  EQ '(').E5A
         LA    14,&PARM       LOAD PARM ADDR
         ST    14,12(0,1)    STORE PARM ADDR IN PARM LIST
         AGO   .E6
.E5A     ANOP
&A       SETA  K'&PARM-2
&C       SETC  '&PARM'(2,&A)
         ST    &C,12(0,1)    STORE PARM ADDR IN PARM LIST
         AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0 AND                 *
               T'&DELETE EQ 'O').E10
         LR    14,&C          LOAD PARM ADDR
         AGO   .E6
.E5B     AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0   AND T'&DELETE     *
               EQ 'O').E10
         L     14,12(0,1)     LOAD PARM BLOCK ADDR INTO REGISTER
.E6      AIF   (T'&TERM EQ 'O' AND N'&STORAGE EQ 0   AND T'&DELETE     *
               EQ 'O').E10
.E7      AIF   (T'&TERM EQ 'O').E8      TERM OMITTED
         AIF   (N'&STORAGE NE 0   OR T'&DELETE NE 'O').ERR3
         AIF   ('&TERM' NE '*').ERR4
         MVI   0(14),X'80'         SET OPERATION FIELD
         MVI   1(14),X'80'         SET CODE FIELD
         AGO   .E10
.E8      AIF   (N'&STORAGE EQ 0).E9          STORAGE OMITTED
         AIF   (T'&TERM NE 'O' OR T'&DELETE NE 'O').ERR3
         MVI   0(14),X'80'         SET OPERATION FIELD
         AIF   ('&STORAGE' EQ '()').E8E      ALL OPERANDS OMITTED
&A       SETA  N'&STORAGE
         AIF   (&A GT 2).ERR2           TOO MANY OPERANDS
         AIF   (&A EQ 1 OR T'&STORAGE(2) EQ 'O').E8E   LSD ADDR ONLY
.E8B     AIF   ('&STORAGE(2)' NE 'PROCN').E8C
         MVI   1(14),X'42'         SET CODE FIELD
         AGO   .E8F
.E8C     AIF   ('&STORAGE(2)' NE 'PROCL').E8D
         MVI   1(14),X'43'         SET CODE FIELD
         AGO   .E8F
.E8D     AIF   ('&STORAGE(2)' NE 'SOURCE').ERR2
.E8E     MVI   1(14),X'40'         SET CODE FIELD
.E8F     AIF   (T'&STORAGE(1) EQ 'O' OR '&STORAGE' EQ '()').WARN1
         AIF   ('&STORAGE(1)'(1,1) EQ '(').E8G
         LA    0,&STORAGE(1)       LOAD STORAGE SOURCE DESCRIPTOR ADDR
         ST    0,4(0,14)          STORE STORAGE SOURCE DESCRIPTOR ADDR
         AGO   .E10
.E8G     ANOP
&A       SETA  K'&STORAGE(1)-2
&C       SETC  '&STORAGE(1)'(2,&A)
         ST    &C,4(0,14)         STORE STORAGE SOURCE DESCRIPTOR ADDR
         AGO   .E10
.E9      AIF   (T'&DELETE EQ 'O').E10        DELETE OMITTED
         AIF   (T'&TERM NE 'O' OR N'&STORAGE NE 0).ERR3
.E9A     AIF   ('&DELETE' NE 'TOP').E9B
         MVI   0(14),X'40'    SET OPERATION FIELD
         AGO   .E10
.E9B     AIF   ('&DELETE' NE 'PROC').E9C
         MVI   0(14),X'20'    SET OPERATION FIELD
         AGO   .E10
.E9C     AIF   ('&DELETE' NE 'ALL').ERR5
         MVI   0(14),X'10'    SET OPERATION FIELD
.E10     AIF   (T'&ENTRY EQ 'O').E11         ENTRY POINT OMITTED
         AIF   ('&ENTRY' EQ '(15)').E10B
         AIF   ('&ENTRY'(1,1) EQ '(').WARN4
.E10A    LA    15,&ENTRY      LOAD EP ADDR
.E10B    BALR  14,15     BRANCH TO SERVICE ROUTINE
         MEXIT
.E11     LINK  EP=IKJSTCK          LINK TO SERVICE ROUTINE
         MEXIT
.**********************************************************************
.*                                                                    *
.*                                 LIST FORM                          *
.*                                                                    *
.**********************************************************************
.L1      DS    0F
.L2      AIF   (T'&TERM EQ 'O').L3      TERM OMITTED
         AIF   ('&TERM' NE '*').ERR4
         AIF   (N'&STORAGE NE 0   OR T'&DELETE NE 'O').ERR3
&NAME    DC    X'80800000'         SET OPERATION AND CODE FIELDS
         DC    A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.L3      AIF   (T'&DELETE EQ 'O').L4    DELETE OMITTED
         AIF   (T'&TERM NE 'O' OR N'&STORAGE NE 0).ERR3
.L3A     AIF   ('&DELETE' NE 'TOP').L3B
&NAME    DC    X'40000000'         SET OPERATION AND CODE FIELDS
         AGO   .L3D
.L3B     AIF   ('&DELETE' NE 'PROC').L3C
&NAME    DC    X'20000000'         SET OPERATION AND CODE FIELDS
         AGO   .L3D
.L3C     AIF   ('&DELETE' NE 'ALL').ERR5
&NAME    DC    X'10000000'         SET OPERATION AND CODE FIELDS
.L3D     DC    A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.L4      AIF   (N'&STORAGE EQ 0).L5     NO STORAGE OPERANDS
         AIF   (T'&TERM NE 'O' OR T'&DELETE NE 'O').ERR3
         AIF   ('&STORAGE' EQ '()').L4E      ALL OPERANDS OMITTED
&A       SETA  N'&STORAGE
         AIF   (&A EQ 1 OR T'&STORAGE(2) EQ 'O').L4E   LSD ADDR ONLY
.L4B     AIF   ('&STORAGE(2)' NE 'PROCN').L4C
&NAME    DC    X'80420000'         SET OPERATION AND CODE FIELDS
         AGO   .L4F
.L4C     AIF   ('&STORAGE(2)' NE 'PROCL').L4D
&NAME    DC    X'80430000'         SET OPERATION AND CODE FIELDS
         AGO   .L4F
.L4D     AIF   ('&STORAGE(2)' NE 'SOURCE').ERR2
.L4E     ANOP
&NAME    DC    X'80400000'         SET OPERATION AND CODE FIELDS
.L4F     AIF   (T'&STORAGE(1) EQ 'O' OR '&STORAGE' EQ '()').WARN2
         DC    A(&STORAGE(1))      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.L5      ANOP
&NAME    DC    X'00000000'         SET OPERATION AND CODE FIELDS
         DC    A(0)      STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.WARN1   MNOTE 0,'FIRST STORAGE OPERAND IS ASSUMED TO BE IN PARM BLOCK'
         AGO   .E10
.WARN2   MNOTE 0,'FIRST STORAGE OPERAND IS OMITTED'
         DC    A(0)           STORAGE SOURCE DESCRIPTOR ADDR
         MEXIT
.WARN3   MNOTE 0,'PARAMETER LIST ADDR IN MF PARAMETER IS NOT IN REG 1'
         AGO   .E1A
.WARN4   MNOTE 0,'ENTRY POINT ADDR IN ENTRY PARAMETER IS NOT IN REG 15'
         AGO   .E10A
.ERR1    MNOTE 8,'INVALID OR OMITTED MF PARAMETER'
         MEXIT
.ERR2    MNOTE 8,'INVALID STORAGE PARAMETER OPERAND'
         MEXIT
.ERR3    MNOTE 8,'TERM, STORAGE, AND DELETE ARE MUTUALLY EXCLUSIVE'
         MEXIT
.ERR4    MNOTE 8,'INVALID TERM OPERAND'
         MEXIT
.ERR5    MNOTE 8,'INVALID DELETE OPERAND'
         MEXIT
.ERR6    MNOTE 8,'POSITIONAL PARAMETERS ARE INVALID'
         MEXIT
         MEND
