         MACRO
&LABEL   IKJKEYWD &DUMMY,&DEFAULT=
.* RELEASE 20 SUPPORT CODE -- 20035
         GBLC  &IKJCSNM,&IKJKWST,&IKJDSNM
         LCLB  &FDFLT,&EXITBIT
&FDFLT   SETB  ('&DEFAULT' NE '')      SET THE DEFAULT FLAG
         AIF   (N'&SYSLIST EQ 0).GOOD
         MNOTE 4,'IKJKEYWD SHOULD HAVE NO POSITIONAL PARAMETERS'
.GOOD    ANOP
         AIF   ('&IKJCSNM' NE '').A1
         MNOTE 8,'CSECT NAME (FROM IKJPARM MACRO LABEL) IS MISSING'
&EXITBIT SETB  1
.A1      AIF   ('&LABEL' EQ '').ERROR2
         AGO   .A2
.ERROR2  MNOTE 8,'STATEMENT LABEL MISSING BUT REQUIRED'
.END     MEXIT
.A2      AIF   (&EXITBIT).END
&IKJCSNM CSECT ,                       RESUME PCL CSECT
         AIF   ('&IKJKWST' EQ '').NOSET IF NULL, NO SET NECESSARY
&IKJKWST EQU   *-&IKJCSNM              OFFSET TO FIRST KEYWORD
&IKJKWST SETC  ''
.NOSET   ANOP
KEYW&SYSNDX EQU *
         DC    BL2'0100&FDFLT.00000000000' FLAGS
         DC    AL2(KEND&SYSNDX-KEYW&SYSNDX) LENGTH OF PCE
         DC    AL2(&LABEL-&IKJDSNM)    OFFSET TO LABEL
         AIF   (NOT &FDFLT).NPRDF      NEITHER NEEDED
         DC    AL1(IKJ@&SYSNDX-*-2),C&DEFAULT DEFAULT
IKJ@&SYSNDX EQU *
.NPRDF   ANOP
KEND&SYSNDX EQU *
&IKJDSNM DSECT
&LABEL   DS    AL2                     ALLOCATE PDE SPACE
&SYSECT  CSECT ,                       RESUME USERS CSECT
         MEXIT
         MEND
