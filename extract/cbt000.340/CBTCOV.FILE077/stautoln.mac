         MACRO
&NAME    STAUTOLN &S=,&I=
.**********************************************************************
.*                                                                    *
.*                                                                    *
.*RELEASE 23 DELETIONS                                                *
.*                                                                    *
.*RELEASE 22 DELETIONS                                                *
.*                                                                    *
.*RELEASE 21 DELETIONS                                                *
.*                                                                    *
.*STATUS - CHANGE LEVEL 000                                           *
.*                                                                    *
.*NAME - STAUTOLN                                                     *
.*                                                                    *
.*FUNCTION - THIS MACRO IS USED TO ALLOW THE USER TO REQUEST AUTO-    *
.*   MATIC LINE NUMBERING.                                            *
.*                                                                    *
.*       THE MACRO PROTOTYPE IS ABOVE                                 *
.*                                                                    *
.*       THE STANDARD FORM OF THE EXPANSION IS BELOW                  *
.*                                                                    *
.*&NAME  LA    1,ADDR1                  PUT ADDR OF S VALUE IN REG 1  *
.*       LA    0,9                      LOAD ENTRY CODE IN REG 0      *
.*       SLL   0,24                     SHIFT ENTRY CODE TO HIGH ORDER*
.*       OR    0,1                      PUT ADDR OF S VALUE IN REG 0  *
.*       LA    1,ADDR2                  PUT ADDR OF 2 VALUE IN REG 1  *
.*       SVC   94                       ISSUE STATUS SVC              *
.*                                                                    *
.**********************************************************************
         AIF   ('&S' EQ '' OR '&I' EQ '').ERROR
         AIF   ('&S'(1,1) EQ '(').SREG
&NAME    LA    1,&S                     PUT ADDR OF S VALUE IN REG 1
         AGO   .ENTRYCD
.SREG    ANOP
&NAME    LR    1,&S                     PUT ADDR OF S VALUE IN REG 1
         LA    1,0(1)                  CLEAR HIGH ORDER BYTE
.ENTRYCD LA    0,9                      LOAD ENTRY CODE IN REG 0
         SLL   0,24                     PUT ENTRY CODE IN HIGH ORDER
         OR    0,1                     PUT ADDR. OF S VALUE IN REG. 0
         AIF   ('&I'(1,1) EQ '(').IREG
         LA    1,&I                    PUT ADDR. OF I VALUE IN REG. 1
         AGO   .SVC
.IREG    LR    1,&I                    PUT ADDR. OF I VALUE IN REG. 1
         LA    1,0(1)                  CLEAR HIGH ORDER BYTE
.SVC     SVC   94                      ISSUE SVC 94
         MEXIT
.ERROR  IHBERMAC 24
         MEND
