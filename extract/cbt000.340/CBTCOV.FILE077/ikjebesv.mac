         MACRO
&NAME    IKJEBESV &REG,&CODE,&ID,&CODEREG=YES,&COMMREG=(1)
.*
.*  THIS MACRO IS USED BY ALL EDIT SUBCOMMANDS AND SERVICE ROUTINES
.*  TO CONTROL PROLOG CODE GENERATED.  THE BASIC SECTION OF THIS
.*  MACRO IS THE OS/360 SAVE MACRO INSTRUCTION.  IN ADDITION, THIS
.*  MACRO WILL GENERATE A BALR AND USING ON REGISTER 11 AND WILL
.*  CROSS CHAIN SAVE AREAS.
.*  MACRO LAST UPDATED 04-07-70
.*
.* C 104000                                                     SA69114
         LCLA  &A,&B,&C
         LCLC  &E,&F,&G,&H
         AIF   ('&REG' EQ '').E1                                SA69114
         AIF   ('&ID' EQ '').NULLID
         AIF   ('&ID' EQ '*').SPECID
&A       SETA  ((K'&ID+2)/2)*2+4
&NAME    B     &A.(0,15)      BRANCH AROUND IDENTIFIER.
&A       SETA  K'&ID
         DC    AL1(&A)        LENGTH OF IDENTIFIER.
.CONTB   AIF   (&A GT 32).SPLITUP
.CONTAA  AIF   (&A GT 8).BRAKDWN
&E       SETC  '&ID'(&B+1,&A)
         DC    CL&A'&E'       IDENTIFIER.
         AGO   .CONTA
.BRAKDWN ANOP
&E       SETC  '&ID'(&B+1,8)
         DC    CL8'&E'        IDENTIFIER.
&B       SETA  &B+8
&A       SETA  &A-8
         AGO   .CONTAA
.SPLITUP ANOP
&E       SETC  '&ID'(&B+1,8)
&F       SETC  '&ID'(&B+9,8)
&G       SETC  '&ID'(&B+17,8)
&H       SETC  '&ID'(&B+25,8)
         DC    CL32'&E.&F.&G.&H'   IDENTIFIER.
&B       SETA  &B+32
&A       SETA  &A-32
         AGO   .CONTB
.NULLID  ANOP
&NAME    DS    0H
         AGO   .CONTA
.SPECID  AIF   ('&NAME' EQ '').CSECTN
&E       SETC  '&NAME'
&A       SETA  1
.CONTQ   AIF   ('&E'(1,&A) EQ '&E').LEAVE
&A       SETA  &A+1
         AGO   .CONTQ
.LEAVE   ANOP
&B       SETA  ((&A+2)/2)*2+4
&NAME    B     &B.(0,15)      BRANCH AROUND ID.
         DC    AL1(&A)
         DC    CL&A'&E'       IDENTIFIER.
         AGO   .CONTA
.CSECTN  AIF   ('&SYSECT' EQ '').E4
&E       SETC  '&SYSECT'
&A       SETA  1
         AGO   .CONTQ
.E4      MNOTE 4,'CSECT NAME NOT SPECIFIED'
.CONTA   AIF   (T'&REG(1) NE 'N').E3
         AIF   ('&CODE' EQ 'T').CONTC
         AIF   ('&CODE' NE '').E2
&A       SETA  &REG(1)*4+20
         AIF   (&A LE 75).CONTD
&A       SETA  &A-64
.CONTD   AIF   (N'&REG NE 2).CONTE
         STM   &REG(1),&REG(2),&A.(13) SAVE REGISTERS.
         AGO   .BASE
.CONTE   AIF   (N'&REG NE 1).E3
         ST    &REG(1),&A.(0,13) SAVE REGISTER.
         AGO   .BASE
.CONTC   AIF   (&REG(1) GE 14 OR &REG(1) LE 2).CONTF
         STM   14,15,12(13)   SAVE REGISTERS.
&A       SETA  &REG(1)*4+20
         AIF   (N'&REG NE 2).CONTG
         STM   &REG(1),&REG(2),&A.(13) SAVE REGISTERS.
         AGO   .BASE
.CONTG   AIF   (N'&REG NE 1).E3
         ST    &REG(1),&A.(0,13) SAVE REGISTER.
         AGO   .BASE
.CONTF   AIF   (N'&REG NE 2).CONTH
         STM   14,&REG(2),12(13) SAVE REGISTERS.
         AGO   .BASE
.CONTH   AIF   (N'&REG NE 1).E3
         STM   14,&REG(1),12(13) SAVE REGISTERS.
.BASE    ANOP
         AIF   ('&CODEREG' EQ 'NO').SKPBASE
         BALR  BASEREG,0 -    ESTABLISH ADDRESSABILITY FOR THIS
         USING *,BASEREG -    ROUTINE.
.SKPBASE ANOP
         AIF   ('&COMMREG' NE '(1)').COMM1
         LR    COMMREG,1 -    INITIALIZE COMMUNICATION AREA
*                             POINTER.
         AGO   .COMM3
.COMM1   AIF   ('&COMMREG'(1,1) NE '(').COMM2
         LR    COMMREG,&COMMREG INITIALIZE COMMUNICATION AREA
*                             POINTER.
         AGO   .COMM3
.COMM2   ANOP
         AIF   ('&COMMREG' EQ 'NO').COMM3
         L     COMMREG,&COMMREG INITIALIZE COMMUNICATION AREA
*                             POINTER.
.COMM3   ANOP
         L     15,(CANXTSVA-IKJEBECA)(COMMREG) ADDRESS OF NEXT
*                             AVAILABLE SAVE AREA.
         ST    13,4(,15) -    CROSS CHAIN PREVIOUS AND CURRENT
         ST    15,8(,13) -    SAVE AREAS.
         LR    13,15 -        INITIALIZE SAVE AREA POINTER.
         LA    15,72(,13) -   OBTAIN ADDRESS OF NEXT SAVE AREA
         ST    15,(CANXTSVA-IKJEBECA)(COMMREG) AND SAVE.
         MEXIT
.E1      MNOTE 12,'REGISTER PARAMETER NOT SPECIFIED.'
         MEXIT
.E2      MNOTE 12,'INVALID CODE SPECIFIED.'
         MEXIT
.E3      MNOTE 12,'INVALID REGISTER SPECIFICATION.'
         MEXIT
         MEND
