         MACRO
&NAME    IKJEBERT &REG,&PARA,&RC=O
.*
.*  THIS MACRO IS USED BY ALL EDIT SUBCOMMANDS AND SERVICE ROUTINES
.*  TO CONTROL EPILOG CODE GENERATED.  THE BASIC SECTION OF THIS
.*  MACRO IS THE OS/360 RETURN MACRO INSTRUCTION.  IN ADDITION, THIS
.*  MACRO WILL DEQUEUE AND ZERO THE CURRENT SAVE AREA AND UPDATE THE
.*  POINTER TO THE CURRENT SAVE AREA IN THE COMMUNICATION AREA.
.*
.*  MACRO LAST UPDATED 02-04-70
.*
         LCLA  &A
         AIF   ('&NAME' EQ '').GO
&NAME    DS    0H
.GO      ST    13,(CANXTSVA-IKJEBECA)(COMMREG) UPDATE NEXT
*                                  AVAILABLE SAVE AREA POINTER.
         LR    12,13          COPY OVER SAVE AREA ADDRESS.
         L     13,4(,13)      ADDRESS OF HIGHER SAVE AREA.
         XC    0(72,12),0(12) ZERO SAVE AREA.
         AIF   ('&RC' EQ '(15)').SPECRT
         AIF   ('&REG' EQ '').CONTA
.COMBACK ANOP
&A       SETA  &REG(1)*4+20
         AIF   (&A LE 75).CONTB
&A       SETA  &A-64
.CONTB   AIF   (N'&REG NE 2).CONTC
         LM    &REG(1),&REG(2),&A.(13) RESTORE THE REGISTERS.
         AGO   .CONTA
.SPECRT  AIF   ('&REG(1)' NE '14' AND '&REG(1)' NE '15').COMBACK
         AIF   ('&REG(1)' EQ '14' AND N'&REG EQ 1).COMBACK
         AIF   ('&REG(1)' EQ '15' AND N'&REG EQ 1).CONTA
         AIF   ('&REG(1)' EQ '14').SKIP
         AIF   ('&REG(2)' EQ '0').ZTWO
.LM      LM    0,&REG(2),20(13) RESTORE THE REGISTERS.
         AGO   .CONTA
.ZTWO    L     0,20(0,13)     RESTORE REGISTER ZERO.
         AGO   .CONTA
.SKIP    L     14,12(0,13)    RESTORE REGISTER 14.
         AIF   ('&REG(2)' EQ '15').CONTA
         AIF   ('&REG(2)' EQ '0').ZTWO
         AIF   ('&REG(2)' EQ '0').ZTWO
         AGO   .LM
.CONTC   AIF   (N'&REG NE 1).ERROR1
         L     &REG(1),&A.(0,13) RESTORE REGISTER.
.CONTA   AIF   ('&PARA' EQ '').CONTD
         AIF   ('&PARA' NE 'T').ERROR2
         MVI   12(13),X'FF'   SET RETURN INDICATION.
.CONTD   AIF   ('&RC' EQ 'O').CONTE
         AIF   ('&RC'(1,1) EQ '(').ISAREG
         LA    15,&RC.(0,0)   LOAD RETURN CODE.
         AGO   .CONTE
.ISAREG  AIF   ('&RC(1)' EQ '15').CONTE
         MNOTE 12,'INVALID REGISTER NOTATION IN RC OPERAND.'
         MEXIT
.CONTE   BR    14             RETURN.
         AGO   .END
.ERROR1  MNOTE 12,'INVALID FIRST OPERAND SPECIFIED.'
         MEXIT
.ERROR2  MNOTE 12,'INVALID SECOND OPERAND SPECIFIED.'
.END     MEND
